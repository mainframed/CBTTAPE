SCRATCHS TITLE 'S C R A T C H S  ***  OPERATOR CONTROLLED SCRATCH'
         MACRO ,
&NM      DEFVOL &A                                              GP02289
         LCLA  &K
         AIF   ('&A' EQ '' OR K'&A GT 6).BAD
&K       SETA  K'&A-1                                           GP02289
&NM      DC    AL1(&K),C'&A'                                    GP02289
         MEXIT ,
.BAD     MNOTE 8,'VOL-SER MISSING OR TOO LONG.'
         MEND  ,
         SPACE 1                                                 81340
         MACRO ,
&NM      DEFDSN &A                                              GP02289
         LCLA  &K
         AIF   ('&A' EQ '' OR K'&A GT 8).BAD
&K       SETA  K'&A-1
&NM      DC    AL1(&K),C'&A'
         MEXIT ,
.BAD     MNOTE 8,'DSNAME MISSING OR TOO LONG.'
         MEND  ,
         SPACE 1                                                 81340
         MACRO ,
&NM      DEFINX &A                                              GP02289
         LCLA  &K
         AIF   ('&A' EQ '' OR K'&A GT 43).BAD                   GP02299
&K       SETA  K'&A          (L-1 DUE TO ADDED PERIOD)
&NM      DC    AL1(&K),C'&A..'
         MEXIT ,
.BAD     MNOTE 8,'INDEX LEVEL MISSING OR TOO LONG'
         MEND  ,
         SPACE 1
         PUNCH '   ORDER SCRATCHS(P) '  PRETTIER DUMPS          GP05033
         PUNCH '   SETCODE AC(1) '      FASTER DUMPS <G>        GP05033
         SPACE 2
         COPY  OPTIONGB      DEFINE GLOBALS                      81340
         SPACE 1                                                 81340
         SYSPARM LIST=YES    SET AND LIST OPTIONS                81340
         SPACE 3                                                GP02298
***********************************************************************
**                                                                   **
**  1) THIS PROGRAM ACTS ON SELECTED VOLUMES.                        **
**     A) SPECIFIC VOLUME SERIAL                                     **
**     B) GROUP OF VOLUMES (VOLUME SERIAL MASK  XYZ*  MVS??1)        **
**     C) SPECIFIC (OR GROUP) OF TYPE (E.G., 3380,VOLSER)            **
**     D) ALL PUBLIC AND STORAGE VOLUMES (SYSDA)                     **
**     E) ALL PUBLIC AND STORAGE OF ONE TYPE (E.G., 3380,SYSDA)      **
**     F) ALL VOLUMES OF ONE TYPE (E.G., 3390,ALL)                   **
**     G) ALL VOLUMES (INPUT OF ALL)                                 **
**                                                                   **
**  2) SELECTED DATA SETS ARE SCRATCHED:                             **
**     A) ALL TEMPORARY DATA SETS OF THE FORM SYSNNNNN.TNNNNNN       **
**        IF OLDER THAN TWO DAYS                                     **
**     B) ALL UTILITY TEMPORARY DATA SETS OF THE FORM *.             **
**        IF OLDER THAN TWO DAYS                                     **
**     C) FOR VOLUMES IN THE WORKTBL LIST, ALL DATA SETS ARE         **
**        SCRATCHED UNLESS THEY ARE IN THE EXCEPTION INDEX/DSN       **
**        LIST (SYS1/SYSCTLG/PASSWORD)                               **
**                                                                   **
**  3) WHEN THE OVERRIDE SWITCH IS TURNED ON                         **
**     A) 'FORGOTTEN' DATA SETS CREATED BY ISPF/PDF, OF THE FORM     **
**        *.SPFLOG AND *.SPFTEMP ARE DELETED                         **
**     B) TEMPORARY DATA SETS (SYSNNNNN AND *.) ARE DELETED REGARD-  **
**        LESS OF AGE.                                               **
**                                                                   **
**  4) INPUT IS READ FROM THE SYSIN DD, OR FROM THE OPERATOR CONSOLE **
**     WHEN NO DD IS PRESENT, OR WHEN IT IS UNREADABLE (E.G., DUMMY) **
**       VOLSER          SPECIFIC SERIAL                             **
**       VOLMSK          MASKED SERIAL                               **
**       SYSDA           PUBLIC/STORAGE ONLY                         **
**       ALL             ALL DASD                                    **
**       TYPE,VOL        DASD TYPE ONLY/SERIAL OR MASK               **
**       TYPE,ALL        DASD TYPE ONLY/ALL VOLUMES                  **
**       TYPE,SYSDA      DASD TYPE ONLY/PUBLIC & STORAGE VOLUMES     **
**       DASD,XXX        ALL DASD                                    **
**                                                                   **
**       OVERRIDE        OVERRIDES EXPIRATION DATE CHECKING          **
**       NONORIDE        RESTORES EXPIRATION CHECKING                **
**                                                                   **
**       TERSE           OMIT INFORMATIVE MESSAGES                   **
**       VERBOSE         PRINT ALL MESSAGES                          **
**                                                                   **
***********************************************************************
**                                                                   **
** ---===>>> PROGRAM RUNS IN TEST MODE UNLESS PARM=DELETE <<<===---  **
**                                                                   **
***********************************************************************
**                                                               81340*
**  DEPENDENCIES : LOCAL AND IBM MACRO LIBRARIES; EXTERNAL       81340*
**   MODULES @SERVICE, @PRINTER AND @OBTAINS                     81340*
**                                                               81340*
**  THE PROGRAM WILL RUN CORRECTLY WHEN UNAUTHORIZED, BUT WILL       **
**  RUN SUBSTANTIALLY FASTER WHEN AUTHORIZED AND A SPECIAL           **
**  //VOLMOUNT DD DUMMY DD CARD IS PROVIDED.                     81340*
**                                                                   **
***********************************************************************
**                                                                   **
**  COPYRIGHT 1981 EXPERT SYSTEM PROGRAMMING, INC.                   **
**  COPYRIGHT 2002 GERHARD POSTPISCHIL, EXPERT SYSTEM PROGRAMMING    **
**                                                                   **
***********************************************************************
         MTITL 'OPERATOR CONTROLLED SCRATCH FUNCTION'            81352
         EJECT ,
         PRINT &PRTSOR                                           81340
SCRATCHS PGMHEAD ZERO15,BASE=(R11,R12),PARM=R9,AM=31,RM=24 (S.A)GP02233
         SPACE 1                                                 81340
         LA    R1,SAVE                                          GP02238
         LR    R4,R1                                            GP02238
         AH    R1,=Y(INXTABLE-SAVE)                             GP02238
         AH    R4,=Y(INXTABLN-8-8-SAVE)  STOP TWO BEFORE        GP02238
         LA    R2,L'INXTABLE                                    GP02238
         LR    R3,R1                                            GP02238
         SR    R3,R2         SET FIRST AVAILABLE                GP02238
         STM   R1,R4,INXBXLE                                    GP02238
         LA    R1,SAVE                                          GP02238
         LR    R4,R1                                            GP02238
         AH    R1,=Y(VSXTABLE-SAVE)                             GP02238
         AH    R4,=Y(VSXTABLN-8-8-SAVE)  STOP TWO BEFORE        GP02238
         LA    R2,L'VSXTABLE                                    GP02238
         LR    R3,R1                                            GP02238
         SR    R3,R2         SET FIRST AVAILABLE                GP02238
         STM   R1,R4,VSXBXLE                                    GP02238
         SPACE 1                                                 81340
         MVI   FLAGS,FGTEST  FORCE TEST MODE                     81340
         SERVINIT ,          LOAD @SERVICE FUNCTIONS             81340
         SERVLOAD @PRINTER,(@OBTAINS,@OBTAIN),@INPREAD  LOAD    GP05120
         L     R0,=A(16*1024)  SET SIZE TO LEAVE FREE            81357
         @OBTAIN (R0),OPT=RESERVE   AND RESERVE IT               81357
         MVICC 2             SET RETURN CODE - NOTHING SCRATCHED 81340
         PRTOPEN SYSPRINT,OPT=ABEND    OPEN THE PRINT FILE OR BOMB
         TESTAUTH FCTN=1                                        GP02346
         BXH   R15,R15,UNAUTH                                   GP02346
         LTCB  R5,USE=R5     GET CURRENT TCB                    GP02346
         ICM   R5,15,TCBJSCB  GET JSCB                          GP02346
         BZ    UNAUTH        HUH?                               GP02346
         USING IEZJSCB,R5    DECLARE IT                         GP02346
         TM    JSCBOPTS,JSCBAUTH       APF AUTHORIZED ?         GP02346
         BZ    UNAUTH        NO; SKIP ALL THIS                  GP02346
         MODESET KEY=ZERO    GET KEY ZERO                       GP02346
         OI    JSCBSWT1,JSCBPASS   ALLOW DATA SET ACCESS        GP02346
         MODESET KEY=NZERO   RESTORE USER KEY                   GP02346
         DROP  R5                                               GP02346
         SPACE 1                                                 81340
*---------------------------------------------------------------------*
*                                                                     *
*  LOOK FOR PARM=DELETE CANCELLING TEST MODE                          *
*                                                                     *
*---------------------------------------------------------------------*
UNAUTH   LA    R9,0(,R9)     ANY PARM POINTER ?                  81340
         LTR   R9,R9                                             81340
         BZ    NOPARM                                            81340
         L     R9,0(,R9)     GET PARM ADDRESS                   GP02288
         LA    R9,0(,R9)     CLEAN IT                           GP02288
         LTR   R9,R9         ADDRESS OF PARM                     81340
         BZ    NOPARM        NONE; SKIP                          81340
         ICM   R14,3,0(R9)   GET PARM LENGTH                     81340
         BZ    NOPARM                                            81340
         NI    FLAGS,255-FGTEST  RESET TEST MODE                 81340
         CLI   1(R9),6       CORRECT PARM LENGTH ?               81340
         BNE   BADPARM                                           81340
         CLC   =C'DELETE',2(R9)  DELETE REQUEST ?                81340
         BE    NOPARM                                            81340
BADPARM  WTO   'INVALID PARM FIELD'                              81340
         ABEND 666                                               81340
         SPACE 1                                                GP02288
*---------------------------------------------------------------------*
*                                                                     *
*  PREPARE LOCATE AND OBTAIN PARM LISTS                               *
*                                                                     *
*---------------------------------------------------------------------*
NOPARM   PRTLIST TITLE,TITLE=1                                   81340
         PRTV  SUBHEAD,TITLE=4,CC=NO                             81340
         MVI   VOLIST+1,1    SET VOLUME LIST FOR SINGLE VOLUME   81340
         L     R14,PATFMT1   BUILD CAMLISTS                      81340
         LA    R15,CCHHR                                         81340
         LA    R0,DSPVOL                                         81340
         LA    R1,DS1DSNAM   OUTPUT AND WORK AREA                81340
         STM   R14,R1,DSCB1  SEEK LIST                           81340
         L     R14,PATFMT4                                       81340
         LA    R15,DS1DSNAM  SET TO 44X'04'                      81340
*        LA    R0,DSPVOL                                         81340
         LA    R1,DS4IDFMT                                       81340
         STM   R14,R1,DSCB4  SEARCH FOR FORMAT 4                 81340
         L     R14,PATSCR                                        81340
*        LA    R15,DS1DSNAM                                      81340
         SR    R0,R0         NO VOLSER                           81340
         LA    R1,VOLIST     VOLUME LIST                         81340
         STM   R14,R1,SCRDSN  SCRATCH LIST                       81340
         L     R14,PATCAT                                        81340
*        LA    R15,DS1DSNAM                                      81340
*        SR    R0,R0         NO CVOL                             81340
         LA    R1,CATWORK                                        81340
         STM   R14,R1,CATFND  CATALOG LOOKUP                     81340
         L     R14,PATUCAT                                       81340
*        LA    R15,DS1DSNAM                                      81340
*        SR    R0,R0                                             81340
         SR    R1,R1                                             81340
         STM   R14,R1,UNCAT  UNCATALOG REQUEST                   81340
         MVC   WTOR(PATWTORL),PATWTOR                            81340
*---------------------------------------------------------------------*
*                                                                     *
*  RETRIEVE AND REFORMAT THE IPL DATE                                 *
*    PRE-XA USE 5 DAYS BEFORE TODAY                                   *
*---------------------------------------------------------------------*
         PUSH  USING                                            GP02239
         L     R4,CVTPTR                                        GP02239
         L     R4,CVTSMCA-CVTMAP(,R4)  SMCA ADDRESS             GP02239
         USING SMCABASE,R4             SMCA ADDRESSIBILITY      GP02239
         MVC   IPLSYS,SMCASID          MOVE SMFID TO TPUT       GP02239
         AIF   (&MVSXA).XAIPL                                   GP05031
         TIME  BIN           GET TODAY'S DATE AND TIME          GP05031
         ST    R1,DB+4       STASH DATE                         GP05031
         LR    R15,R0        COPY TIME                          GP05031
         CP    DB+4+2(2),=P'5'  SUBTRACT OK IN THIS YEAR ?      GP05031
         BH    FUDGEIPL      YES                                GP05031
         SP    DB+4(4),=P'635' - 1000 + 365 (YEAR ADJUST)       GP05031
FUDGEIPL SP    DB+4(4),=P'5' FAKE IPL DATE                      GP05031
         AGO   .COMIPL                                          GP05031
.XAIPL   L     R15,SMCAITME            LOAD IPL TIME (BIN) TO WORK
         MVC   DB+4(4),SMCAIDTE        MOVE DATE TO WORK AREA   GP02239
.COMIPL  CLI   DB+4,X'02'              OLD(ER) FORMAT?          GP02239
         BH    THISCENT                YES                      GP02239
         AP    DB+4(4),=P'1900000'     MAKE CORRECT YEAR        GP02239
THISCENT UNPK  IPLCENT(7),DB+4(4)   CONVERT TO PRINTABLE        GP02239
         OI    IPLDATE+L'IPLDATE-1,C'0'  ZONING RESTRICTION     GP02239
         SR    R14,R14                 CLEAR HIGH ORDER         GP02239
         LA    R4,100        CONSTANT                           GP02239
         DR    R14,R4                  R15 NOW IN SECONDS       GP02239
         SR    R14,R14                 CLEAR                    GP02239
         LA    R0,60                   60 SECS/MIN              GP02239
         DR    R14,R0                  R14 HAS SECS, R15 HAS BALANCE
         LR    R1,R0                   SAVE SECS                GP02239
         SR    R14,R14                 CLEAR                    GP02239
         DR    R14,R0                  R14 HAS MINUTES,R15 HAS HOURS
         LR    R3,R15                  COPY HOURS               GP02239
         MR    R2,R4         HOURS*100                          GP02239
         AR    R3,R14        ADD MINUTES                        GP02239
         MR    R2,R4         HOURS/MINUTES/00                   GP02239
         AR    R3,R1                                            GP02239
         CVD   R3,DB         PACK                               GP02239
         OI    DB+7,X'0F'    DEC NUMBER NOW PACKED WITH SIGN    GP02239
         UNPK  IPLTIME,DB    MAKE IT PRINTABLE                  GP02239
         POP   USING                                            GP02239
         SERVCALL RJFCB,SYSPRINT  GET SYSPRINT DSN              GP02239
         BXH   R15,R15,NOPRTDSN                                 GP02239
         MVC   PRTDSN,0(R1)  SAVE DSN ONLY                      GP02239
         SPACE 1
*---------------------------------------------------------------------*
*                                                                     *
*  SPECIAL CONSIDERATIONS:                                            *
*                                                                     *
*  1) WHEN THE JOB NAME IS KILLJOY, SET OVERRIDE MODE                 *
*     WHEN A DD DUMMY WITH NAME OVERRIDE IS PRESENT, SET OVERRIDE     *
*                                                                     *
*  2) WHEN THE STEP NAME IS SYSTEM, SET SYSDA-ONLY MODE               *
*                                                                     *
*  3) WHEN SYSIN IS PRESENT, INHIBIT CONVERSATIONAL MODE (WTOR)       *
*    A) SYSIN DD DUMMY - TREATED AS ALL (UNLESS SYSDA SET)            *
*    B) OTHER SYSIN - READ AND EXECUTE COMMANDS                       *
*                                                                     *
*---------------------------------------------------------------------*
NOPRTDSN LTCB  R3,USE=R3     GET CURRENT TCB                    GP02239
         L     R3,TCBTIO     GET TIOT                           GP02346
         USING TIOT1,R3      DECLARE IT                          81340
         DEVTYPE =CL8'OVERRIDE',DB  IS THERE AN OVERRIDE DD ?   GP03109
         BXLE  R15,R15,SETNOVER                                 GP03109
         CLC   =C'KILLJOY ',TIOCNJOB   SPECIAL JOB NAME ?        81340
         BNE   SYSSYST       NO; CHECK FOR SYSTEM PACKS
SETNOVER OI    FLAGS,FGOVER  YES, SET OVERRIDE                   81340
SYSSYST  CLC   =C'SYSTEM ',TIOCSTEP   REQUEST FOR SYSTEM PACKS ? 81340
         BNE   *+8           NO
         OI    FLAGS,FGSYS   DO SYSTEM PACKS ONLY                81340
SYSINLP  DEVTYPE =CL8'SYSIN',DB   LOOK FOR SYSIN DATA            81340
         BXH   R15,R15,NOSYSIN   NONE                            81340
         OI    FLAGS,FGAUTO  SET AUTOMATIC REPLY MODE            81340
         CLI   DB+2,0        DD DUMMY ?                          81340
         BE    SYSINNED      YES; ALL SET                        81340
         XI    FLAGS,FGSYN+FGAUTO   SET INPUT MODE
         INPOPEN SYSIN       OPEN THE INPUT FILE                 81340
         BXLE  R15,R15,SYSINNED  OK                              81340
ABENDRDR ABEND 2540          ELSE INDICATE ERROR                 81340
         SPACE 1                                                 81340
NOSYSIN  PRTOPEN CONSOLE,DEV=2  OPEN THE CONSOLE FILE            81340
         PRTV  MGINIT1,CC=NO,DEV=2  WRITE INITIAL MESSAGES       81340
         PRTV  MGINIT2,CC=NO,DEV=2                               81340
         SPACE 2                                                 81340
*---------------------------------------------------------------------*
*                                                                     *
*  GET CURRENT DATE AND TWO DAYS AGO; CHANGE TO DSCB DATE FORMAT      *
*                                                                     *
*---------------------------------------------------------------------*
SYSINNED TIME  TU            GET THE TIME QUICKLY                81340
         SR    R0,R0                                             81340
         STM   R0,R1,DB      SAVE THE PACKED DATE                81340
         CVB   R5,DB         GET BINARY EQUIVALENT               81340
         SR    R4,R4                                             81340
         D     R4,=F'1000'   SEPARATE YEAR FROM DAYS             81340
         STCM  R4,3,CUDATE+1   SET DAYS                         GP02239
         STC   R5,CUDATE     SET YEAR IN DSCB FORMAT             81340
         SH    R4,=H'1'      SET DAY FOR 2-DAY LIMIT CHECK       81340
         BP    DATEOK        OK                                  81340
         BCTR  R5,0          GET PRIOR YEAR                      81340
         LA    R4,365(,R4)   ADD A YEAR'S WORTH OF DAYS          81340
         TM    DB+5,X'01'    ODD YEAR ?                          81340
         BNZ   DATEOK        YES; NOT LEAP                       81340
         TM    DB+5,X'12'    MULTIPLE OF FOUR ?                  81340
         BM    DATEOK        NO                                  81340
         LA    R4,1(,R4)     SET FOR LEAP YEAR                   81340
DATEOK   STCM  R4,3,PDATE+1   SET DAYS                          GP02239
         STC   R5,PDATE      AND YEAR IN DSCB FORMAT             81340
         MH    R5,=H'1000'                                       81340
         AR    R5,R4         MAKE FUNNY DATE AGAIN               81340
         CVD   R5,DB         CONVERT TO PACKED                   81340
         AP    DB(8),=P'1900000'  MAKE CORRECT CENTURY          GP02233
         OI    DB+7,X'0F'    MAKE PROPER ZONE                   GP02233
         ZAP   JULPACK,DB    SAVE IT FOR FORMATTING             GP02233
         UNPK  JULCENT,DB+4(4) MAKE EBCDIC DATE                 GP02233
         SPACE 1                                                 81340
*---------------------------------------------------------------------*
*                                                                     *
*   SHOW IPL DATE, OPTIONS, ETC.                                      *
*                                                                     *
*---------------------------------------------------------------------*
         PRTLIST OPTFD       SHOW DATE AND OPTIONS               81340
         EJECT ,
***********************************************************************
**                                                                   **
**  MAIN LOOP -- ENTERED TO GET OPERATOR REPLY, OR AUTOMATICALLY     **
**    WHEN IN UCB LOOKUP LOOP                                        **
**                                                                   **
***********************************************************************
GETCMND  TM    FLAGS,FGUCB   IN UCB LOOKUP MODE ?                81340
         BNZ   NEXTUCB       YES; GET NEXT UCB                   81340
         SPACE 1                                                 81340
*---------------------------------------------------------------------*
*  CHECK FOR AUTO REPLY                                               *
*---------------------------------------------------------------------*
GETCKAUT TM    FLAGS,FGAUTO  AUTO REPLY MODE ?                   81340
         BZ    GETCSYN                  NO, SKIP                 81340
         TM    FLAGS,FGAUTOD  SECOND TIME ?                      81340
         BNZ   ALLDONE       YES, EXIT                           81340
         OI    FLAGS,FGAUTOD  SECOND TIME                        81340
         MVC   REPLY,BLANKS
         MVC   REPLY(3),=C'ALL '
         TM    FLAGS,FGSYS   SYSDA ONLY ?
         BZ    GOTREPLY      NO; DO ALL
         MVC   REPLY(5),=C'SYSDA '  SET TO PROCESS SYSDA ONLY
         B     GOTREPLY                                          81340
         SPACE 1                                                 81340
*---------------------------------------------------------------------*
*  READ NEXT CARD IF IN SYSIN INPUT MODE                              *
*---------------------------------------------------------------------*
GETCSYN  TM    FLAGS,FGSYN   SYSIN INPUT PROCESSING ?            81340
         BZ    GETCWTO          NO, GET OPERATOR REPLY           81340
         INPGET ,            GET A CARD                          81340
         BXH   R15,R15,PREEMY    QUIT THE HARD WAY               81340
         MVC   REPLY,0(R1)   COPY INPUT TEXT                     81340
         B     GOTREPLY         CHECK THE INPUT                  81340
         SPACE 1                                                GP02288
***********************************************************************
**                                                                   **
**       MAX LENGTH IS 13, BUT ALLOW 16 SO OPERATOR WILL GET MORE    **
**       MEANINGFUL MESSAGE TO A TOO LONG REPLY                      **
**                                                                   **
**       CHECK FOR FOLLOWING CONDITIONS:                             **
**       LENGTH 3+    'END ', 'EXIT ', ETC. - QUIT WITH SUMMARY      **
**       LENGTH 8-10  'TYPE,ALL'  LOOP THROUGH UCBS                  **
**       LENGTH 11-13 'TYPE,VOLSER'                                  **
**       LENGTH 1-6  VOLSER                                          **
**       ALL ELSE IN ERROR                                           **
**                                                                   **
***********************************************************************
GETCWTO  MVI   ECB,0         RESET ECB                           81340
         MVC   REPLY,BLANKS      BLANK OUT REPLY AREA            81340
         WTOR  ,REPLY,16,ECB,MF=(E,WTOR)  REQUEST OPERATOR INPUT 81340
         WAIT  ,ECB=ECB      WAIT FOR REPLY
         SPACE 1                                                 81352
GOTREPLY OC    REPLY,BLANKS      CONVERT REPLY TO UPPER CASE     81340
         PRTLIST MGREPLY,DEV=1    WRITE REPLY TO PRINTER         81340
         MVC   VOLMASK,=CL6'*'  ALL SERIALS FOR UCB LOOP (DFLT) GP02289
         LA    R5,REPLY      START SCAN                         GP02288
         BLOOK T=LOOKTABL,R=REPLY  EXAMINE INPUT                GP02287
         CLI   0(R5),C' '                                       GP02287
         BNH   SYNTERR       NOTHING INPUT ?                    GP02287
         B     CHKTYPVS      CHECK FOR TYPE,VOLSER INPUT        GP02299
         SPACE 1                                                 81352
*---------------------------------------------------------------------*
*                                                                     *
*  SET / RESET OPTIONS FLAGS                                          *
*                                                                     *
*---------------------------------------------------------------------*
OVERRIDE OI    FLAGS,FGOVER                                      81352
         B     GETCMND                                           81352
         SPACE 1                                                 81352
NONORIDE NI    FLAGS,255-FGOVER                                  81352
         B     GETCMND                                           81352
         SPACE 1                                                 81352
SETTEST  OI    FLAGS,FGTEST                                     GP02238
         B     GETCMND                                          GP02238
         SPACE 1                                                 81352
SETPROD  NI    FLAGS,255-FGTEST  RESET TEST MODE                GP02287
         B     GETCMND                                          GP02287
         SPACE 1                                                GP02287
SETVERB  OI    FLAGS,FGVERB  VERBOSE MODE                       GP02287
         B     GETCMND                                          GP02287
         SPACE 1                                                GP02287
SETTERSE NI    FLAGS,255-FGVERB  TERSE MODE                     GP02287
         B     GETCMND                                          GP02287
         SPACE 1                                                GP02287
TOOLONG  LA    R3,MGDTLONG              - YES - KVETCH           81352
         B     SETCODE4                                          81352
         SPACE 1                                                GP02287
*---------------------------------------------------------------------*
*                                                                     *
*  TABLE OF KEYWORD RESPONSES                                         *
*                                                                     *
*---------------------------------------------------------------------*
LOOKTABL VERBTAB 'END ',ALLDONE,BASE=*  END THIS PROGRAM        GP02287
         VERBTAB 'EOJ ',ALLDONE         END THIS PROGRAM        GP02287
         VERBTAB 'EXIT ',ALLDONE        END THIS PROGRAM        GP02287
         VERBTAB 'CANCEL ',ALLDONE      END THIS PROGRAM        GP02287
         VERBTAB 'TEST ',SETTEST        TEST MODE - NO SCRATCH  GP02287
         VERBTAB 'OVERRIDE ',OVERRIDE   ALLOW UTIL/EXPDT SCRATCH
         VERBTAB 'NONORIDE ',NONORIDE   NO UTIL/EXPDT SCRATCH   GP02287
         VERBTAB 'TERSE ',SETTERSE      SKIP DETAIL OUTPUT      GP02287
         VERBTAB 'VERBOSE ',SETVERB     PRODUCE DETAIL OUTPUT   GP02287
         VERBTAB *END                                           GP02287
         SPACE 1                                                 81352
*---------------------------------------------------------------------*
*                                                                     *
*   SCAN TYPE AND VOLSER                                          81352
*                                                                     *
*---------------------------------------------------------------------*
CHKTYPVS NI    FLAGS,255-FGSYS  EXPLICIT VOLSER - RESET SYSDA   GP02287
         SR    R2,R2                                            GP02287
         LA    R1,1(,R6)     DEFAULT STOPPER - END OF FIELD (TOO LONG)
         TRT   0(L'REPLY,R5),TRTSEP  GET SEPARATION TABLE       GP02287
         BZ    TOOLONG       NO GOOD                            GP02287
         LA    R4,1(,R1)     POINT PAST SEPARATOR               GP02287
         SR    R1,R5         GET LENGTH OF OPERAND              GP02287
         BP    MOVEUNIT                                         GP02287
SYNTERR  LA    R3,MGSYNT     SET FOR SYNTAX ERROR               GP02287
         B     SETCODE4      AND SET ERROR                      GP02287
MOVEUNIT MVC   REQUNIT,BLANKS                                    81352
         CLM   R2,1,TRTSEP+C' ' TERMINATED BY BLANK ?           GP02287
         BE    MOVEVOL       YES; TREAT OPERAND AS SERIAL       GP02287
         CH    R1,=Y(L'REQUNIT) NOT TOO LONG ?                  GP02287
         BH    TOOLONG                                          GP02287
         BCTR  R1,0          SET EXECUTE LENGTH                 GP02287
         LA    R15,REQUNIT   MOVE TEXT TO UNIT                  GP02287
         EX    R1,EXMVCR5    MOVE TEXT                          GP02287
         LR    R5,R4         SET FOR SECOND OPERAND             GP02287
         SR    R2,R2                                            GP02287
         LA    R1,1(,R6)     DEFAULT STOPPER - END OF FIELD (TOO LONG)
         TRT   0(L'REPLY,R5),TRTSEP  GET SEPARATION TABLE       GP02287
         BZ    TOOLONG       NO GOOD                            GP02287
         LA    R4,1(,R1)     POINT PAST SEPARATOR               GP02287
         SR    R1,R5         GET LENGTH OF OPERAND              GP02287
         BNP   SYNTERR       OOPS                               GP02287
         CLM   R2,1,TRTSEP+C' ' TERMINATED BY BLANK ?           GP02287
         BNE   SYNTERR       NO; MALFORMED INPUT                GP02287
MOVEVOL  CH    R1,=Y(L'VOLMASK) TOO LONG ?                      GP02287
         BH    TOOLONG                                          GP02287
         BCTR  R1,0          SET EXECUTE LENGTH                 GP02287
         LA    R15,VOLMASK   MOVE TEXT TO UNIT                  GP02287
         EX    R1,EXMVCR5    MOVE TEXT                          GP02287
         SPACE 1                                                 81352
         CLC   =C'ALL ',VOLMASK    ALL OF ONE TYPE ?             81352
         BNE   TESTSYSD      NO; CHECK FOR SYSDA REQUEST         81352
         NI    FLAGS,255-FGSYS  EXPLICIT VOLSER - RESET SYSDA   GP02287
         B     SETVSALL      YES; SET GENERAL LOOP              GP02299
         SPACE 1                                                 81352
TESTSYSD CLC   =C'SYSDA ',VOLMASK  PUBLIC/STORAGE ?             GP02288
         BNE   VMASKCHK             NO                           81352
         OI    FLAGS,FGUCB+FGSYS    YES - SET FLAG FOR AUTO SCAN 81352
SETVSALL MVC   VOLMASK,=CL6'*'  RESTORE GENERAL MASK            GP02289
         SPACE 1                                                 81352
VMASKCHK TRT   VOLMASK,TRTMSK   ANY MASK CHARACTERS IN SERIAL?  GP02289
         BZ    LODTYPE       NO                                 GP02289
         OI    FLAGS,FGUCB   YES - SET FLAG FOR UCB LOOP        GP02289
         SPACE 1                                                 81352
LODTYPE  LM    R15,R1,=A(TYPETABL,TYPETLEN,TYPETEND)            GP02239
         USING TYPETMAP,R15                                      81352
LODTLOOP CLC   REQUNIT,TYPETCOD    MATCHING TYPE ?               81352
         BE    GOTDEVT              YES                          81352
         BXLE  R15,R0,LODTLOOP      NO  - BUMP                  GP02239
         LA    R3,MGINDEV          DEVICE TYPE INVALID           81352
SETCODE4 OICC  4                   SET MINOR ERROR               81352
         B     PRTMSG              AND PRINT MESSAGE             81352
EXMVCR5  MVC   0(0,R15),0(R5)  MOVE TEXT                        GP02287
         SPACE 1                                                 81352
*---------------------------------------------------------------------*
*                                                                     *
*  LOOK UP USER'S DEVICE TYPE (USE DASD IF NONE) AND SET MASK LENGTH  *
*                                                                     *
*---------------------------------------------------------------------*
GOTDEVT  MVC   VOLTYP,TYPETYPE                                   81352
         MVC   DSPDEVTP,BLANKS  (LONGER THAN TNAM)              GP02287
         MVC   DSPDEVTP(L'TYPETNAM),TYPETNAM  MOVE NAME TO LISTING
         DROP  R15                                              GP02239
         LA    R15,1         SET LEN-1 OF UNIT TYPE COMPARE     GP02239
         CLI   VOLTYP+3,X'FF'  ACCEPT ALL ?                     GP02239
         BL    ACCADASD      NO; SPECIFIC                       GP02239
         SR    R15,R15       ACCEPT ANY DASD                    GP02239
ACCADASD STH   R15,CMPMSKLN  SAVE LENGTH FOR EXECUTE            GP02239
         TM    FLAGS,FGUCB   UCB LOOKUP MODE ?                   81340
         BNZ   INITUCB       YES; NO NEED TO FINAGLE             81352
         MVC   DSPVOL,VOLMASK      STASH VOLUME SERIAL          GP02288
      SERVCALL UCBDK,VOLMASK       SEE IF VOLSER AVAILABLE       81352
         BXH   R15,R15,OBT4NMNT  TREAT AS NOT MOUNTED ON ERROR  GP02287
         LTR   R7,R0         LOAD PRESUMED UCB ADDRESS           81340
         BNZ   PROCUCB       GO TO PROCESS THE UCB              GP02239
         B     OBT4NMNT      NOT MOUNTED                         81340
         EJECT ,
***********************************************************************
**                                                                   **
**   UCB LOOKUP REQUEST: SCAN ALL UCB ENTRIES, RETAIN ONLY THOSE     **
**   THAT ARE OF THE DESIRED DEVICE TYPE, ONLINE, AND READY          **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP02239
INITUCB  XC    @UCB,@UCB                                        GP02298
NEXTUCB  SERVCALL UCBUM,*@UCB  GET NEXT UCB                     GP02298
         BXH   R15,R15,DONEUCBS  TREAT ERROR AS END OF CHAIN    GP02298
         LTR   R7,R0         ELSE COPY RETURN                   GP02298
         BZ    DONEUCBS      NORMAL END OF CHAIN                GP02298
         USING UCBOB,R7      DECLARE THE UCB MAPPING             81340
         ST    R7,@UCB       UPDATE ADDRESS                     GP02298
         SPACE 1                                                GP02298
         TM    UCBSTAT,UCBONLI  ON-LINE ?                        81340
         BZ    NEXTUCB       NO; SKIP                            81340
         TM    UCBSTAT,UCBCHGS+UCBUNLD  CHANGING STATUS ?        81340
         BNZ   NEXTUCB       YES; IGNORE                         81340
         SPACE 1                                                GP02298
         LH    R1,CMPMSKLN   0 OR 1 FOR TYPE COMPARE            GP02239
         EX    R1,EXCMPMSK   COMPARE UNIT TYPE                  GP02239
         BNE   NEXTUCB       NO, SKIP                            81340
         CLI   UCBVOLI,C' '   HAS VOL-SER BEEN STORED FOR IT ?   81340
         BNH   NEXTUCB       NO, GET ANOTHER ONE                 81340
         ICM   R0,3,UCBVTOC  VTOC ON IPL TRACK ?                GP13227
         BZ    NEXTUCB         YES; IGNORE IT                   GP13227
*---------------------------------------------------------------------*
*                                                                     *
*  FOR A SYSDA REQUEST, CHECK FOR PUBLIC OR STORAGE ATTRIBUTE         *
*                                                                     *
*---------------------------------------------------------------------*
         TM    FLAGS,FGSYS   SYSDA PACKS ONLY ?                 GP02288
         BZ    MASKUCB       NO, NO VOL-SER CHECK                81340
         TM    UCBSTAB,UCBBPUB+UCBBSTR  PUBLIC/STORAGE ?        GP02288
         BZ    NEXTUCB       SKIP THIS ENTRY                     81340
         B     MASKUCB                                          GP02288
EXCMPMSK CLC   UCBTBYT3(0),VOLTYP+2  REQUEST TYPE ?             GP02239
         SPACE 1
*---------------------------------------------------------------------*
*                                                                     *
*  END OF UCB CHAIN - RESET AND CLEAR, THEN CHECK FOR MORE INPUT      *
*                                                                     *
*---------------------------------------------------------------------*
DONEUWRG PRTV  ERRMSGUS,DEV=(1,2),CC=NO                         GP02239
DONEUCBS NI    FLAGS,255-FGUCB  IF WE FELL THROUGH, WE'RE DONE   81340
         XC    @UCB,@UCB                                        GP02298
         B     GETCKAUT      AND GET OPERATOR REPLY             GP02238
         EJECT ,                                                GP02288
*---------------------------------------------------------------------*
*                                                                     *
* FOR UCB LOOP MODE, VERIFY THE VOLUME SERIAL AGAINST THE USER'S MASK *
*                                                                     *
*---------------------------------------------------------------------*
MASKUCB  MVC   DSPVOL,UCBVOLI      STASH VOLUME SERIAL          GP02288
         CALL  SUBCOMP,(CMPVOL,DSPVOL,VOLMASK,CMP@WORK),MF=(E,CMPPLIST)
         BXH   R15,R15,NEXTUCB  NOT A MATCH                     GP02289
         PRTLIST MGREPUC,DEV=(1,2)  PRINT UCB VOLUME MESSAGE     81340
         SPACE 2
***********************************************************************
**                                                                   **
**   THIS SECTION IS COMMON TO UCB LOOKUP AND REPLY ANALYSIS         **
**   PREPARE TO PROCESS THE CURRENT VOLUME                           **
**                                                                   **
***********************************************************************
PROCUCB  MVC   VOLTYP(4),UCBTBYT1  COPY TRUE DEVICE TYPE         81340
         NI    VOLTYP+1,255-UCBRR-UCBVLPWR-UCBDVPWR  MAKE CATLG TYPE
         CLI   UCBTBYT4,X'0A'    3340 ?                          81357
         BNE   *+8           NO                                  81357
         NI    VOLTYP+1,255-UCBRPS CONSISTENCY THICKENS THE PLOT 81357
         SPACE 1                                                GP02239
*---------------------------------------------------------------------*
*                                                                     *
*  ADD VOLUME TO TABLE OF PROCESSED VOLUMES                           *
*                                                                     *
*---------------------------------------------------------------------*
         LM    R1,R3,VSXBXLE   LOAD TABLE POINTERS              GP02238
         CR    R1,R3         FIRST TIME ?                       GP02238
         BH    VSXADD        YES; JUST ADD IT                   GP02238
VSXLOOP  CLC   DSPVOL,0(R1)  MATCHING SERIAL?                   GP02238
         BE    VSXHAVE       YES                                GP02238
         BXLE  R1,R2,VSXLOOP TRY AGAIN                          GP02238
VSXADD   MVC   0(6,R1),DSPVOL SAVE THIS VOLUME                  GP02238
         C     R1,VSXBXLE+12  AT END OF TABLE ?                 GP02238
         BH    VSXHAVE       YES; IGNORE                        GP02238
         ST    R1,VSXBXLE+8  UPDATE TABLE END ADDRESS           GP02238
VSXHAVE  SERVCALL UCBGN,(R7)  GET SYSTEM NAME FOR DEVICE        GP02239
         BXH   R15,R15,*+4+6                                    GP02239
         MVC   DSPDEVTP,0(R1)  COPY RETURN                      GP02239
         PRTSPACE 1,DEV=1    SKIP A LINE                         81340
         SPACE 1                                                GP02289
*---------------------------------------------------------------------*
*                                                                     *
*  TEST WHETHER THIS VOLUME IS A SPECIAL WORK PACK (ALWAYS CLEARED)   *
*                                                                     *
*---------------------------------------------------------------------*
         MVC   CURFLAGS,UCBSTAB  COPY FLAGS                     GP02289
         NI    CURFLAGS,UCBBPRV+UCBBPUB+UCBBSTR  KEEP ATTR ONLY GP02289
         LM    R1,R3,BXLEWORK  GET TABLE POINTER TO SPECIAL VOLUME
WORKLOOP IC    R2,0(,R1)     GET LENGTH - 1                     GP02288
         EX    R3,EXCLCVOL   IS IT OURS ?                       GP02288
         BE    WORKVOL       YES, CONTINUE                      GP02288
         LA    R2,2(,R2)     MAKE TRUE LENGTH OF ENTRY          GP02288
         BXLE  R1,R2,WORKLOOP  TRY AGAIN                        GP02288
         B     GETDSCB4      NOT FOUND - USER OR SYSTEM VOLUME  GP02289
WORKVOL  OI    CURFLAGS,FGWORK  SHOW WORK VOLUME                GP02289
         EJECT ,
***********************************************************************
**                                                                   **
**  NOW FIND THE FIRST ( DSCB4 ) VTOC ENTRY                          **
**                                                                   **
***********************************************************************
GETDSCB4 MVI   DS1DSNAM,X'04'    MAKE FORMAT 4 DSCB DSNAME       81340
         MVC   DS1DSNAM+1(L'DS1DSNAM-1),DS1DSNAM                 81340
         @OBTAIN DSCB4,OPT=LOADED   LOOK FOR FMT4 / SET LOAD FG  81357
         BIX   VAL=(R15),SRL=2,ERR=OBT4IOER,                           *
               LOC=(OBT4HAVE,     CC 0 - RETURNING DSCB                *
               OBT4NMNT,          CC 4 - VOLUME NOT MOUNTED (?)        *
               OBT4CHK4)          CC 8 - ERROR - NORMAL FOR DSCB 4
         SPACE 1
OBT4IOER LA    R3,MGERR4     SET FOR FORMAT 4 ERROR              81340
         B     ERRMSG                                            81340
         SPACE 1
OBT4NMNT LA    R3,MGNMT      SET FOR VOLUME NOT MOUNTED          81340
ERRMSG   OICC  8             SET MAJOR ERROR                     81340
PRTMSG   PRTV  (R3),CC=NO,DEV=(1,2)    WRITE ERROR MESSAGE       81340
         B     GETCMND       GET ANOTHER COMMAND                 81340
         SPACE 1
*---------------------------------------------------------------------*
*                                                                     *
*   RETURN CODE 8 IS NORMAL FOR A DSCB4 REQUEST (DSCB GOTTEN, NOT 1)  *
*                                                                     *
*---------------------------------------------------------------------*
OBT4CHK4 CLI   DS4IDFMT,C'4'    IS IT TYPE 4 DSCB ANYWAY ?       81340
         BNE   OBT4IOER      IF NOT TYPE 4 IT'S A VALID 'NOT FOUND'
         SPACE 1
*---------------------------------------------------------------------*
*                                                                     *
*   RETURN CODE 0 - DSCB4 GOTTEN FROM @OBTAINS                        *
*                                                                     *
*---------------------------------------------------------------------*
OBT4HAVE MVC   CCHHR(4),DS4VTOCE+2  GET VTOC LOW EXTENT ( CCHH ) 81340
         MVI   R,1           DSCB 4 IS AT RECORD 1
*NEXT*   B     NXTDSCB1
         EJECT ,
***********************************************************************
**                                                                   **
**  RETURN TO THIS POINT TO GET ANOTHER DS NAME                      **
**                                                                   **
***********************************************************************
NXTDSCB1 SR    R2,R2
         IC    R2,R          GET PREVIOUS RECORD NUMBER
         LA    R2,1(,R2)     BUMP BY ONE
         STC   R2,R
         CLM   R2,1,DS4DEVDT  IS IT HIGHER THAN FITS ON TRACK ?  81340
         BNH   FIND1         NO, CHECK AGAINST LAST DSCB 1
         MVI   R,1           YES, RESET RECORD NO. TO 1
         LH    R2,HH         GET TRACK NUMBER
         LA    R2,1(,R2)     AND BUMP BY ONE
         STH   R2,HH
         CH    R2,DS4DEVSZ+2  STILL IN SAME CYLINDER ?           81340
         BL    FIND1         YES, CHECK FOR VALID ADDRESS
         STCM  R2,12,HH      NO, RESET TRACK TO 0                81340
         LH    R2,CC         AND BUMP CYLINDER
         LA    R2,1(,R2)     NUMBER BY 1
         STH   R2,CC
FIND1    CLC   CCHHR,DS4HPCHR   BEYOND LAST FORMAT 1 ?           81340
*        CAUTION - IN NON-PCP ENVIRONMENT OR WITH SHARED DASD THIS
*        COULD HAVE CHANGED SINCE WE READ THE DSCB 4.
         BNH   GETDSCB1      NO, GET THE DS NAME
         B     DONEDSCB      SHOW END OF VOLUME                  81340
*
DONEDSER OICC  8             SET ERROR                           81340
DONEDSCB LA    R3,MGEOV      POINT TO END VOLUME MESSAGE         81340
         B     PRTMSG        AND WRITE IT OUT                    81340
         SPACE 2
GETDSCB1 @OBTAIN DSCB1       SEEK THE DSCB AT THIS ADDRESS
         BIX   VAL=(R15),SRL=2,ERR=OBT1IOER,                           *
               LOC=(OBT1HAVE,     CC 0 - RETURNING DSCB                *
               OBT4NMNT,          CC 4 - VOLUME NOT MOUNTED (?)        *
               ,                  CC 8 - ERROR                         *
               ,                  CC12 - ERROR                         *
               ,                  CC16 - ERROR                         *
               DONEDSER)          CC20 - BEYOND VTOC - END OF VOLUME
         SPACE 1
OBT1IOER PRTLIST MGERR1,DEV=(1,2)  SHOW ERROR WITH ADDRESS       81340
         B     GETCMND       GET ANOTHER                         81340
         EJECT ,
OBT1HAVE CLI   DS1FMTID,C'1'   FOUND A TYPE 1 DSCB ?             81340
         BNE   NXTDSCB1      NO, IGNORE
         MVI   BLKSTART,C' '   BLANK ALL STATUS MESSAGES         81340
         MVC   BLKSTART+1(BLLEN-1),BLKSTART                      81340
         SPACE 1                                                GP02238
*---------------------------------------------------------------------*
*   IF THE DATA SET HAS A NON-ZERO EXPIRATION DATE, FORMAT IT         *
*---------------------------------------------------------------------*
         SR    R0,R0                                            GP02287
         ICM   R0,3,DS1EXPDT+1 ANY EXPIRATION DATE ?            GP02287
         BZ    SKIPEXP         NO; LEAVE BLANK                  GP02287
         SR    R0,R0                                            GP02287
         IC    R0,DS1EXPDT   LOAD EXPIRATION DATE               GP02287
         A     R0,=F'1900'   ADD CENTURY                        GP02287
         MH    R0,=H'1000'   MAKE ROOM FOR DAY                  GP02287
         AH    R0,DS1EXPDT+1  ADD JULIAN DAY                    GP02287
         CVD   R0,DB         CONVERT TO PACKED FORM             GP02287
         MVC   EXPDATE-1(9),=X'40202020204B202020'              GP02287
         ED    EXPDATE-1(9),DB+4                                GP02287
         SPACE 1                                                GP02238
*---------------------------------------------------------------------*
*  AS AN ADDED INCENTIVE TO RUN THIS PROGRAM, WE BUILD A TABLE OF     *
*    HIGH-LEVEL INDICES ENCOUNTERED.                                  *
*---------------------------------------------------------------------*
SKIPEXP  LA    R1,DS1DSNAM+8  SET END POINT                     GP02238
         TRT   DS1DSNAM(8),TRTDSX  LOOK FOR A PERIOD OR BLANK   GP02238
         LA    R0,DS1DSNAM+1                                    GP02238
         SR    R1,R0         GET EXECUTE LENGTH                 GP02238
         BM    NXTDSCB1      SKIP BAD NAME                      GP02289
         MVC   DB,BLANKS                                        GP02238
         EX    R1,EXMVCDSX   MOVE INDEX TO WORK SPACE           GP02238
         LM    R1,R3,INXBXLE   LOAD TABLE POINTERS              GP02238
         CR    R1,R3         FIRST TIME ?                       GP02238
         BH    INXADD        YES; JUST ADD IT                   GP02238
INXLOOP  CLC   DB,0(R1)      MATCHING INDEX ?                   GP02238
         BE    INXHAVE       YES                                GP02238
         BXLE  R1,R2,INXLOOP TRY AGAIN                          GP02238
INXADD   MVC   0(8,R1),DB    SAVE THIS INDEX                    GP02238
         C     R1,INXBXLE+12  AT END OF TABLE ?                 GP02238
         BH    INXHAVE       YES; IGNORE                        GP02238
         ST    R1,INXBXLE+8  UPDATE TABLE END ADDRESS           GP02238
         SPACE 1                                                GP02238
*---------------------------------------------------------------------*
*  SKIP ACCESS METHODS I CAN'T (OR DON'T WANT TO) HANDLE              *
*---------------------------------------------------------------------*
INXHAVE  CLI   DS1DSORG+1,0  ANYTHING IN SECOND BYTE ?
         BNE   NXTDSCB1      YES; DON'T TOUCH
         TM    DS1DSORG,DS1DSGIS+DS1DSGCX+X'08'+X'04'  FUNNIES?
         BNZ   NXTDSCB1      YES; DON'T TOUCH
         TM    DS1SMSFG,DS1PDSE+DS1PDSEX  PDS/E OR HFS ?
         BNZ   NXTDSCB1      YES; DON'T TOUCH
*---------------------------------------------------------------------*
*  AN EXPIRATION DATE OF 99.365 MAKES DATA SET UNTOUCHABLE            *
*---------------------------------------------------------------------*
         CLC   DS1EXPDT,NOEXDATE  PERM. SYSTEM DATA SET ?       GP02288
         BE    SHOEXPDT      YES; DON'T TOUCH                   GP02288
*---------------------------------------------------------------------*
*  WHEN THE PASSWORD FLAG IS ON, LEAVE IT ALONE (AVOID S913)          *
*---------------------------------------------------------------------*
         TM    DS1DSIND,FGPASSWD  PASSWORD OR RACF PROTECTED ?   81357
         BNZ   SHOPASS       YES; DON'T TOUCH                   GP02298
*---------------------------------------------------------------------*
*  A ZERO CREATION DATE MAKES IT UNUSED SINCE ALLOCATION              *
*---------------------------------------------------------------------*
         ICM   R0,7,DS1CREDT      EVER USED ?                   GP09212
         BZ    OTHERDSN             NO - TRY FURTHER PROCESSING GP09212
         SPACE 1                                                GP02288
*---------------------------------------------------------------------*
*                                                                     *
*  TEST FOR IEHMOVE UTILITY DATA SET - NAME IS *.  *.*., ETC.         *
*                                                                     *
*---------------------------------------------------------------------*
         CLI   DS1DSNAM,C'*'  IS IT A UTILITY DATASET ?          81340
         BNE   NOTIEHMV      NO, DON'T CHECK FOR UTILITY SCRATCH
         TM    FLAGS,FGOVER  OVERRIDE MODE ?                     81340
         BNZ   GOSCRTCH      YES, SCRATCH IT                     81340
         CLC   DS1CREDT,PDATE  WHEN WAS THIS DATA SET CREATED ?  81340
         BNH   GOSCRTCH      TOO LONG AGO, SCRATCH              GP02288
         B     SHOEXPDT      CURRENT ? LEAVE                    GP02298
EXMVCDSX MVC   DB(0),DS1DSNAM                                   GP02238
         SPACE 1                                                GP02288
*---------------------------------------------------------------------*
*                                                                     *
*   CHECK FOR SYSTEM TEMPORARY DATA SET SYSNNNNN.TNNNNNN.XXX          *
*                                                                     *
*---------------------------------------------------------------------*
NOTIEHMV CLC   =C'SYS',DS1DSNAM    IS DS NAME SYS... ?           81352
         BNE   OTHERDSN             NO  - TRY FURTHER PROCESSING 81352
         CLI   DS1DSNAM+4,C'.'     XXXN. PREFIX ?               GP09212
         BE    NXTDSCB1             YES - SKIP DS                81352
         CLC   DS1DSNAM+8(2),=C'.T'                              81340
         BNE   OTHERDSN      NOT TEMP DS                        GP02239
         CLI   DS1DSNAM+16,C'.'                                  81340
         BNE   OTHERDSN      NOT TEMP D.S.                      GP02239
         TRT   DS1DSNAM+3(5),TRTDIG   5 DIGITS AFTER 'SYS'      GP02239
         BNZ   OTHERDSN      NOT NUMERIC                        GP02239
         TRT   DS1DSNAM+10(6),TRTDIG  6 DIGITS IN TIME          GP02239
         BNZ   OTHERDSN      NOT NUMERIC                        GP02239
         SPACE 1                                                GP02288
*---------------------------------------------------------------------*
*                                                                     *
*   WE HAVE A DATA SET NAME OF: 'SYSNNNNN.TNNNNNN.'                   *
*                                                                     *
*---------------------------------------------------------------------*
         CLC   DS1CREDT,PDATE  WHEN WAS THIS DATA SET CREATED ?  81340
         BNH   GOSCRTCH      TOO LONG AGO, SCRATCH              GP02288
         TM    FLAGS,FGOVER  IS OVERRIDE ON ?                    81340
         BZ    SETCURR       NOT ALLOWED TO SCRATCH              81340
         CLC   DS1DSNAM+3(5),IPLDATE  ALLOCATED PRIOR TO IPL ?   81340
         BH    SETCURR       AFTER - SHOW CURRENT IN MSG
         BL    CHKPRNT       BEFORE - SEE IF ONE OF OURS
         CLC   DS1DSNAM+10(6),IPLTIME  SEE IF PRIOR TO IPL       81340
         BNL   SETCURR       NO; SET CURRENT
         SPACE 1                                                GP02288
*---------------------------------------------------------------------*
*                                                                     *
*   DON'T SCRATCH SYSPRINT DATA SET FOR THIS JOB (NON-JES)            *
*                                                                     *
*---------------------------------------------------------------------*
CHKPRNT  CLC   PRTDSN,DS1DSNAM  IS IT OUR SYSPRINT DATASET ?    GP02239
         BNE   CHKCURR                  CHECK IF CURRENT DATA SET
         MVC   DSPEXPL(8),=C'SYSPRINT' EXPLAIN WHY NOT SCRATCHED
         B     SHONOTSC      AND PRINT MESSAGE                  GP02298
         SPACE 1
CHKCURR  CLC   PRTDSN(17),DS1DSNAM  DATA SET GEN'D BY SAME READER ?
         BH    GOSCRTCH                 NO, SCRATCH             GP02288
SETCURR  MVC   DSPEXPL(7),=C'CURRENT' EXPLAIN WHY SKIPPED
         B     SHONOTSC                                         GP02298
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*   ON A WORK VOLUME (SORT OR RESTORE), DELETE EVERYTHING EXCEPT      *
*   VTOC, VVDS, AND CATALOG (AS SPECIFIED IN DSNTAB)                  *
*                                                                     *
*   NOTE THAT THIS CODE, FOR PERIODIC CLEAN-UP, IS NOW PERFORMED      *
*   BY THE CLEARVOL FUNCTION OF COPYVOL.                              *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1                                                GP02288
OTHERDSN TM    CURFLAGS,FGWORK  IS THIS A WORK VOLUME?          GP02289
         BZ    OTHERVOL      NO; IF OVERRIDE, CHECK OTHER NAMES GP02289
         LM    R1,R3,BXLEDSNX  CHECK SPECIAL DSNAMES            GP02288
WORKDSN  IC    R2,0(,R1)                                         81340
         EX    R2,EXCLCDSN   IS IT SPECIAL ?                     81340
         BE    SHOSPEC       YES, NO SCRATCH                    GP02298
         LA    R2,2(0,R2)    SIZE OF THIS ENTRY                 GP02288
         BXLE  R1,R2,WORKDSN   TRY AGAIN                        GP02288
         CLI   DS1NOEPV,0    PATTERN DSCB ?                      81340
         BE    NXTDSCB1      YES; IGNORE (WEEKLY CLEANUP WILL CATCH)
         CLC   DS1EXPDT,CUDATE                                   81340
         BNL   SHOEXPDT      DON'T SCRATCH - EXPIRATION DATE STILL GOOD
         TM    FLAGS,FGOVER  OVERRIDE ON ?                       81340
         BNZ   GOSCRTCH      YES, SCRATCH                        81340
         CLC   DS1CREDT,PDATE  TOO OLD ?                         81340
         BH    SHOEXPDT      NO, KEEP                           GP02298
         B     GOSCRTCH      GO SCRATCH IT                      GP02288
         SPACE 2                                                GP02289
*---------------------------------------------------------------------*
*                                                                     *
*   FOR ANY OTHER VOLUME, CHECK FOR DELETEABLE DATA SET BY MASK       *
*                                                                     *
*---------------------------------------------------------------------*
OTHERVOL ICM   R0,7,DS1CREDT      EVER USED ?                   GP09212
         BZ    OTHUNKBX             NO - TRY FURTHER PROCESSING GP09212
         CLI   DS1NOEPV,0    PATTERN DSCB ?                      81340
         BE    NXTDSCB1      YES; IGNORE (WEEKLY CLEANUP WILL CATCH)
OTHUNKBX LM    R3,R5,BXLEUNK      CHECK UNCONDITIONAL ENTRIES   GP09212
OTHUNKLP IC    R4,0(,R3)     GET LENGTH - 1 OF MASK             GP09212
         MVC   MASKL44,BLANKS  CLEAR                            GP09212
         BCTR  R4,0          SET LENGTH FOR EXECUTE             GP09212
         EX    R4,EXMVCMSK   MOVE NAME TO MASK                  GP09212
         LA    R4,2(,R4)     SET LENGTH FOR BXLE                GP09212
      CALL  SUBCOMP,(CMPDSN,DS1DSNAM,MASKL44,CMP@WORK),MF=(E,CMPPLIST)
         BXLE  R15,R15,OTHUNKFD  PROCESS ON MATCH               GP09212
         BXLE  R3,R4,OTHUNKLP  ELSE TRY ANOTHER                 GP09212
         SPACE 1
         LM    R3,R5,BXLEMASK     CHEC OVERRIDE ENTRIES         GP02289
OTHVOLLP IC    R4,0(,R3)     GET LENGTH - 1 OF MASK             GP02289
         MVC   MASKL44,BLANKS  CLEAR                            GP02298
         BCTR  R4,0          SET LENGTH FOR EXECUTE             GP02299
         EX    R4,EXMVCMSK   MOVE NAME TO MASK                  GP02289
         LA    R4,2(,R4)     SET LENGTH FOR BXLE                GP02289
      CALL  SUBCOMP,(CMPDSN,DS1DSNAM,MASKL44,CMP@WORK),MF=(E,CMPPLIST)
         BXLE  R15,R15,OTHVOLFD  PROCESS ON MATCH               GP02289
         BXLE  R3,R4,OTHVOLLP  ELSE TRY ANOTHER                 GP02289
         B     NULDSCB1      ELSE IGNORE                        GP02289
EXMVCMSK MVC   MASKL44(0),1(R3)  MOVE MASK                      GP02298
         SPACE 2                                                GP02289
*---------------------------------------------------------------------*
*                                                                     *
*   FOUND DELETABLE DATA SET - CHECK EXPIRATION                       *
*                                                                     *
*---------------------------------------------------------------------*
OTHUNKFD CLC   DS1EXPDT,CUDATE                                  GP09212
         BNL   SHOEXPDT      DON'T SCRATCH - EXPIRATION DATE STILL GOOD
         CLC   DS1CREDT,PDATE  TOO OLD ?                        GP09212
         BH    SHOEXPDT      NO, KEEP                           GP09212
         B     GOSCRTCH      KILL IT                            GP09212
         SPACE 2                                                GP09212
*---------------------------------------------------------------------*
*                                                                     *
*   FOUND DELETABLE DATA SET - CHECK EXPIRATION                       *
*                                                                     *
*---------------------------------------------------------------------*
OTHVOLFD CLC   DS1EXPDT,CUDATE                                  GP02289
         BNL   SHOEXPDT      DON'T SCRATCH - EXPIRATION DATE STILL GOOD
         CLC   DS1CREDT,PDATE  TOO OLD ?                        GP02289
         BH    SHOEXPDT      NO, KEEP                           GP02289
         TM    FLAGS,FGOVER  OVERRIDE ON ?                      GP02289
         BNZ   GOSCRTCH      GO SCRATCH IT                      GP02289
         MVC   DSPEXPL(9),=C'OVERRIDE'                         GP02289
         B     SHONOTSC      SHOW WHY NOT                       GP02289
         SPACE 2                                                GP02289
*---------------------------------------------------------------------*
*                                                                     *
*   NOT SPECIAL - CHECK FOR NULL DATA SET                             *
*                                                                     *
*---------------------------------------------------------------------*
***** DISABLED - TOO MANY SYSTEM DATA SETS (HASPACE, DUMP, ETC.) ******
*---------------------------------------------------------------------*
NULDSCB1 TM    DS1DSORG,DS1DSGPS+DS1DSGPO  SEQUENTIAL/PDS ?     GP02299
         B     NXTDSCB1      NEITHER; IGNORE***WAS BZ***        GP02299
         ICM   R0,7,DS1LSTAR  EVER USED ?                       GP02299
         BNZ   NXTDSCB1      YES; LEAVE IT ALONE                GP02299
         TM    DS1SMSFG,DS1PDSE  IS IT A PDS/E ?                GP02299
         BNZ   NXTDSCB1      YES; CAN'T TELL USAGE              GP02299
         CLC   DS1EXPDT,CUDATE                                  GP02299
         BNL   SHOEXPDT      DON'T SCRATCH - EXPIRATION DATE STILL GOOD
         CLC   DS1CREDT,PDATE  TOO OLD ?                        GP02299
         BH    SHOEXPDT      NO, KEEP                           GP02299
         TM    FLAGS,FGOVER  OVERRIDE ON ?                      GP02299
         BNZ   GOSCRTCH      GO SCRATCH IT                      GP02299
         MVC   DSPEXPL(9),=C'OVERRIDE'                         GP02299
         B     SHONOTSC      SHOW WHY NOT                       GP02299
         SPACE 2
***********************************************************************
**                                                                   **
**  SCRATCH PROLOGUE - TEST DATA SET'S ENQUEUE STATUS; SKIP IN USE   **
**                                                                   **
***********************************************************************
GOSCRTCH TESTAUTH FCTN=1     AUTHORIZED ?                       GP03011
         BXH   R15,R15,GOSCRTNQ NO; SKIP TEST                   GP03011
         LA    R1,DS1DSNAM+L'DS1DSNAM  IN CASE 44               GP03011
         LA    R3,DS1DSNAM                                      GP03011
         TRT   0(L'DS1DSNAM,R3),TRTBLANK  FIND FIRST BLANK      GP03011
         SR    R1,R3         GET NAME LENGTH                    GP03011
         LR    R3,R1         COPY FOR ENQ                       GP03011
         ENQ   (,DS1DSNAM,,(R3),),RET=TEST,MF=(E,ENQLIST)       GP03011
         LTR   R15,R15       AVAILABLE ?                        GP03011
         BNZ   SHOOPEN       NO; SHOW OPEN/UNAVAILABLE          GP03011
         SPACE 2
***********************************************************************
**                                                                   **
**  SCRATCH THE DATA SET                                             **
**                                                                   **
***********************************************************************
GOSCRTNQ XC    VOLSEQ(2),VOLSEQ      ZERO RETURN INDICATOR      GP02288
         TM    FLAGS,FGTEST   TEST MODE ?                        81340
         BNZ   CATCHK        YES, SKIP SCRATCH ATTEMPT           81340
         SR    R0,R0         ZERO UCB ADDRESS TO PREVENT MOUNTING
         SCRATCH SCRDSN      TAKE OUT HE TRASH                   81340
         B     *+4(R15)
         B     CATCHK        SCRATCHED, NOW CHECK IF UNCATLG NEEDED
         B     OBT4NMNT      NOT MOUNTED ?
         B     SCRCODE8      PROBLEM - NOT SCRATCHED
         B     PROGERR       NOT MOUNTED ?
PROGERR  MVC   DSPEXPL(9),=C'PGM ERROR'                         GP02315
         MVC   DSPYESNO,=C'NOT'                                 GP02315
         PRTLIST MGDSN                                          GP02315
         SERVTERM ,          CLOSE EVERYTHING                   GP02315
         ABEND 666           OOPS                               GP02315
         SPACE 1                                                GP02315
SCRCODE8 CLI   VOLSEQ+1,2    SCRATCHED OR NOT FOUND ?           GP02315
         BL    CATCHK        SCRATCHED ALREADY; NEED TO UNCATLG ?
         CLI   VOLSEQ+1,7    DATA SET OPEN ?                    GP02315
         BE    SHOOPEN       YES; JUST ISSUE MESSAGE            GP02315
         ST    R15,DB        SAVE RETURN CODE                   GP02315
         MVC   DSPEXPL(8),=C'- FAILED'                          GP02315
         MVC   DSPYESNO,=C'NOT'                                 GP02315
         PRTLIST MGERRSCR    ERROR ON SCRATCH                   GP02315
         B     NXTDSCB1                                         GP02315
         SPACE 1
SHOPASS  MVC   DSPEXPL(8),=C'PASSWORD'                          GP02298
         B     SHONOTRS                                         GP02298
SHOOPEN  MVC   DSPEXPL(8),=C'*IN USE*'                          GP02315
         B     SHONOTRS                                         GP02315
SHOINDX  MVC   DSPEXPL(8),=C'SPC.INDX'                          GP02288
         B     SHONOTRS                                         GP02298
SHOSPEC  MVC   DSPEXPL(7),=C'SPECIAL'                           GP02298
         B     SHONOTRS                                         GP02298
SHOEXPDT MVC   DSPEXPL(9),=C'EXP. DATE' HIGHER EXP. DATE THAN YESTERDAY
         SPACE 1
SHONOTRS TM    FLAGS,FGVERB  TERSE MODE ?                       GP02288
         BZ    NXTDSCB1      YES; SKIP PRINTING                 GP02288
SHONOTSC MVC   DSPYESNO,=C'NOT'                                  81340
         SPACE 1                                                 81340
SHOLINE  TM    FLAGS,FGTEST  TEST MODE ?                         81340
         BZ    PRTSCRM       NO                                  81340
         CLC   DSPYESNO,BLANKS PRETENDED SCRATCH ?               81340
         BNE   PRTSCRM       NO                                  81340
         CLC   DSPEXPL,BLANKS ANY EXCEPTION ?                    81340
         BNE   PRTSCRM       YES                                 81340
         MVC   DSPYESNO,=C'NOT'                                  81340
         MVC   DSPEXPL+1(6),=C'*TEST*'                           81340
PRTSCRM  PRTLIST MGDSN       PRINT DSN AND STATUS                81340
         B     NXTDSCB1
         SPACE 2
***********************************************************************
**                                                                   **
**   TEST WHETHER THIS DATA SET IS CATALOGED AS RESIDING ON THE      **
**     CURRENT VOLUME.  IGNORE MULTI-VOLUME ENTRIES                  **
**   MATCHED VOLUME CAUSES THE ENTRY TO BE UNCATALOGED               **
**                                                                   **
***********************************************************************
CATCHK   NI    RETCC,255-2   RESET 'NOTHING SCRATCHED' BIT       81340
         SR    R0,R0                                             81340
         LOCATE CATFND       IS IT CATALOGED ?                   81340
         MVC   DSPCATST,=CL9'NOT CATLG' PROVISIONAL EXPLANATION  81340
         BXH   R15,R15,SHOLINE NO; JUST PRINT                    81340
         MVC   DSPCATST,=CL9'NOT UNCAT' PROVISIONALLY INDICATE NOT
         CLC   =H'1',CATVOL#  ONE VOLUME ENTRY ?
         BNE   SHOLINE       NO, FORGET IT                       81340
         CLC   CATDEV+2(2+6),VOLTYP+2  SAME MAJOR TYPE/VOLSER  ? 81357
         BNE   SHOLINE       NO, SKIP IT                         81340
         TM    FLAGS,FGTEST  TEST MODE ?                         81340
         BNZ   SHONOTSC      UNCONDITIONAL BYPASS IN TEST MODE   81340
         CATALOG UNCAT       REMOVE IT FROM CATLG
         BXH   R15,R15,SHOLINE LEAVE NOT MESSAGE                 81340
         MVC   DSPCATST,=CL9'UNCATLG''D'
         B     SHOLINE       PRINT MESSAGE                       81340
         SPACE 3
*---------------------------------------------------------------------*
*                                                                     *
*   END OF SYSIN PRIOR TO END CARD, OR I/O ERROR. QUIT WITH MESSAGE   *
*                                                                     *
*---------------------------------------------------------------------*
PREEMY   PRTV  MSGNOEND,CC=NO  END CARD MISSING OR I/O ERROR    GP02288
         OICC  16            I'M UNHAPPY                        GP02288
         SPACE 1                                                GP02238
***********************************************************************
**                                                                   **
**  PROCESSING COMPLETE - FREE RESOURCES AND RETURN                  **
**                                                                   **
***********************************************************************
ALLDONE  @OBTAIN OPT=CLOSE   CLOSE VTOC                          81340
         SPACE 1                                                GP02238
         LM    R3,R5,INXBXLE  GET TABLE OF INDICES              GP02238
         CR    R3,R5         TEST FIRST                         GP02238
         BH    DONEIXL       NONE ?                             GP02238
         ST    R3,SOPLOW     START SORT ADDRESS                 GP02239
         ST    R5,SOPHIGH    LAST ITEM ADDRESS                  GP02239
         OI    SOPHIGH,SOPFAD  SHOW ADDRESS PASSED              GP02239
         MVC   SOPSEQ(4),=AL1(C'A',0,8,8) SEQ/KEYOFF/KEYLN/ENTLN
         SERVCALL SORTH,SORTPARM  SORT IT                       GP02239
         PRTROOM 10          MAKE SURE ENOUGH ROOM              GP02238
         PRTSPACE 5          AND MAKE SOME SPACE                GP02238
         PRTV  MSGINDEX,CC=NO                                   GP02239
PRNTIXL  PRTLIST SHOWINX     SHOW THIS NAME                     GP02238
         BXLE  R3,R4,PRNTIXL  UNTIL DONE                        GP02238
         SPACE 1                                                GP02238
DONEIXL  LM    R3,R5,VSXBXLE  GET TABLE OF VOLUME SERIALS       GP02238
         CR    R3,R5         TEST FIRST                         GP02238
         BH    DONEMSG       NONE ?                             GP02238
         ST    R3,SOPLOW     START SORT ADDRESS                 GP02239
         ST    R5,SOPHIGH    LAST ITEM ADDRESS                  GP02239
         OI    SOPHIGH,SOPFAD  SHOW ADDRESS PASSED              GP02239
         MVC   SOPSEQ(4),=AL1(C'A',0,6,6) SEQ/KEYOFF/KEYLN/ENTLN
         SERVCALL SORTH,SORTPARM  SORT IT                       GP02239
         PRTROOM 10          MAKE SURE ENOUGH ROOM              GP02238
         PRTSPACE 4          SPACE A BIT                        GP02238
         PRTV  MSGVOLSR,CC=NO                                   GP02239
PRNTVSX  PRTLIST SHOWVSX     SHOW THIS NAME                     GP02238
         BXLE  R3,R4,PRNTVSX  UNTIL DONE                        GP02238
         SPACE 1                                                GP02238
DONEMSG  PRTLIST MGEOJ,DEV=(1,2)  WRITE END OF JOB MESSAGE       81340
         ICM   R0,15,CMP@WORK  COMPARE WORK AREA?               GP02289
         BZ    DONEFREE                                         GP02289
 CALL  SUBCOMP,(=C'END',DB,DB,CMP@WORK),MF=(E,CMPPLIST)  FREE WORK AREA
         XC    CMP@WORK,CMP@WORK  JUST IN CASE                  GP02289
DONEFREE SERVTERM ,          CLOSE EVERYTHING                    81340
         L     R9,RETCODE    GET RETURN CODE                     81340
         PGMEXIT RC=(R9)     EXIT WITH CONDITION CODE           GP02233
         SPACE 2
EXCLCVOL CLC   DSPVOL(0),1(R1)                                  GP02288
EXCLCDSN CLC   DS1DSNAM(0),1(R1)                                 81340
         EJECT ,
BXLEWORK DC    A(WORKTAB,1,WORKTABN)                            GP02288
BXLEDSNX DC    A(DSNXTAB,1,DSNXTABN)                            GP02288
BXLEMASK DC    A(MASKTAB,1,MASKTABN)                            GP02289
BXLEUNK  DC    A(MASKUNK,1,MASKUNKN)                            GP09212
SYSPRINT PRTWORK SYSPRINT,TITLE=5                                81340
CONSOLE  PRTWORK *CONSOLE                                        81340
SYSIN    INPWORK SYSIN,EODAD=ALLDONE                             81340
TYPETABL DC    XL4'30002001',CL14'2311  2311'                    81352
         DC    XL4'30C02008',CL14'2314  2314'                    81352
         DC    XL4'30502009',CL14'3330  3330 M1'                 81352
         DC    XL4'3050200A',CL14'3340  3340'                    81352
         DC    XL4'3050200B',CL14'3350  3350'                    81352
         DC    XL4'3050200C',CL14'3375  3375'                    82220
         DC    XL4'3050200D',CL14'3330-13330 M11'                81352
         DC    XL4'3050200D',CL14'333D  3330 M11'                81352
         DC    XL4'3030200E',CL14'3380  3380'                    82220
         DC    XL4'3030200F',CL14'3390  3390'                   GP02233
         DC    XL4'000020FF',CL14'DISK  DASD'                    81352
         DC    XL4'000020FF',CL14'      DASD'   ALL BLANK IS GEN. MATCH
TYPETEND DC    XL4'000020FF',CL14'DASD  DASD'                    81352
         SPACE 1                                                 81340
*        LISTS FOR OBTAIN AND SCRATCH
*
PATFMT4  CAMLST SEARCH,1,2,3   DSN4,DSPVOL,DS4IDFMT              81340
         ORG   PATFMT4+4                                         81340
PATFMT1  CAMLST SEEK,1,2,3  CCHHR,DSPVOL,DS1DSNAM                81340
         ORG   PATFMT1+4                                         81340
PATSCR   CAMLST SCRATCH,1,,3,,OVRD  DS1DSNAM,,VOLIST,,OVRD       81340
         ORG   PATSCR+4                                          81340
PATCAT   CAMLST NAME,1,,3  DS1DSNAM,,CATWORK                     81340
         ORG   PATCAT+4                                          81340
PATUCAT  CAMLST UCATDX,1  DS1DSNAM                               81340
         ORG   PATUCAT+4                                         81340
         SPACE 2                                                 81340
         STITL 'MESSAGES'                                        81352
BLANKS   DC    CL44' '       LOTS OF BLANKS                      81340
PATWTOR  WTOR  'NEXT VOLUME TO BE SCRATCHED:',1,16,2,MF=L        81340
PATENQ   ENQ   (QSYSDSN,4-4,E,44,SYSTEM),RET=TEST,MF=L   3/2    GP03011
PATWTORL EQU   *-PATWTOR                                         81340
QSYSDSN  DC    CL8'SYSDSN '                                     GP03011
MGINIT1  VCON  'REPLY ''END'' TO TERMINATE SCRATCH'              81340
         SPACE 1                                                 81352
MGINIT2  VCON  'OR SPECIFY DISK TYPE,VOLSER TO BE SCRATCHED, E.G. 3330-*
               1,WORK30'                                         81352
MGDTLONG VCON  'TEXT TOO LONG; TYPE && VOLSER 6 CHARACTERS EACH'  02287
MGSYNT   VCON  'TEXT INVALID (NEED TYPE,VOLSER)'                GP02287
MGINDEV  VCON  'INVALID OR UNSUPPORTED DEVICE TYPE'              81340
MGINVOLS VCON  'INVALID VOLUME SERIAL'                           81340
MGERR4   VCON  'I/O OR PROCESSING ERROR IN VTOC'                 81340
MGNMT    VCON  'VOLUME COULD NOT BE MOUNTED'                     81340
MGEOV    VCON  '  END OF VOLUME'                                 81340
ERRMSGUS VCON  'ERROR IN UCBSCAN SERVICE'                       GP02239
MGEOJ    FDOPT NL,CC=C'-'                                        81340
         FDCLI RETCC,0,BE=MGEOJ0                                 81340
         FDPRT 'END OF JOB - COMPLETION CODE',PADR               81340
         FDPRT RETCODE,I                                         81340
         FDPRT *END                                              81340
MGEOJ0   FDPRT 'THANKS - THE ITCH IS GONE'                       81340
         FDPRT *END                                              81340
MGREPLY  FDOPT NL,CC=C'-'                                        81340
         FDPRT 'REPLY :',PAD                                     81340
         FDPRT REPLY                                             81340
         FDPRT *END                                              81340
MGREPUC  FDOPT NL,CC=C'-'                                        81340
         FDPRT 'NEXT UCB VOLUME :'                               81340
         FDPRT DSPVOL,PADL                                       813520
         FDPRT *END                                              81340
MGERR1   FDOPT NL,CC=C'0'                                        81340
         FDPRT 'I/O ERROR READING DSCB AT'                       81340
         FDPRT CC,HEX,PADL                                       81340
         FDPRT '.'                                               81340
         FDPRT HH,HEX                                            81340
         FDPRT '.'                                               81340
         FDPRT R                                                 81340
         FDPRT *END                                              81340
MGERRSCR FDEXEC MGDSN,MGDSNX  STANDARD DSN MESSAGE              GP02315
         FDPRT '***** RETURN CODE',NL                           GP02315
         FDPRT DB,4,I,PADL                                      GP02315
         FDPRT ', STATUS CODE X'''                              GP02315
         FDPRT VOLSEQ+1,1,HEX                                   GP02315
         FDPRT ''', SEC. STATUS X'''                            GP02315
         FDPRT VOLSEQ,1,HEX                                     GP02315
         FDPRT ''''                                             GP02315
         FDPRT *END                                             GP02315
MGDSN    FDPRT DSPDEVTP,NL,LEN=9                                GP02287
         FDPRT DSPVOL,LEN=8                                      81352
         FDPRT DS1DSNAM,LEN=46                                   81352
         FDPRT EXPDATE,LEN=12                                   GP02287
         FDPRT ' ',LEN=5                                         81340
         FDPRT DSPYESNO      BLANK OR NOT                        81340
         FDPRT 'SCRATCHED   ',PAD                                81340
         FDPRT DSPEXPL       EXPLANATION FOR UNSCRATCHED         81340
         FDPRT ' ',LEN=2                                         81340
MGDSNX   FDPRT DSPCATST      UNCATLGD/NOT UNCAT MSG              81340
         FDPRT *END                                              81340
TITLE    FDOPT NL,CC=C'#'                                        81340
         FDPRT 'TEMPORARY DATASET SCRATCH FUNCTION'              81340
         FDTM  FLAGS,FGTEST,BZ=TITLEX                            81340
         FDPRT '***** DEBUG MODE *****',RADJ,LEN=30              81340
TITLEX   FDPRT *END                                              81340
         SPACE 1                                                GP02238
SHOWINX  FDPRT 0(R3),8,PAD   SHOW INDEX NAME, NOT DEBLANKED     GP02238
         FDPRT *END                                             GP02238
         SPACE 1                                                GP02238
SHOWVSX  FDPRT 0(R3),6,PAD   SHOW VOLUME SERIAL                 GP02239
         FDPRT *END                                             GP02239
SUBHEAD  VCON  ' ',END=SUBHEADE                                  81340
         DC    CL9'DEV-TYPE'                                    GP02287
         DC    CL8'VOLSER'                                      GP02287
         DC    CL46'DATA SET NAME'                              GP02287
         DC    CL12'EXPIRES '                                   GP02287
         DC    CL9' '                                           GP02287
         DC    CL13'ACTION'                                     GP02287
         DC    C'REASON'                                        GP02287
         VCON  ,                                                 81340
MSGINDEX VCON  ' THE FOLLOWING INDEX LEVELS WERE FOUND:'        GP02239
MSGVOLSR VCON  ' THE FOLLOWING VOLUMES WERE PROCESSED:'         GP02239
MSGNOEND VCON  ' PREMATURE SYSIN END - NO END CARD READ'        GP02288
OPTFD    FDPRT 'DATASETS ALLOCATED BEFORE',NL                    81340
         FDPRT JULPACK,DATE,PAD                                 GP02233
         FDPRT '('                                              GP02233
         FDPRT JULCENT,4                                        GP02233
         FDPRT '.'                                              GP02233
         FDPRT JULCENT+4,3                                      GP02233
         FDPRT ')',PADR                                         GP02233
         FDPRT 'WILL BE SCRATCHED.'                              81340
         FDPRT 'LAST IPL OF',NL                                 GP02239
         FDPRT IPLSYS,PAD                                       GP02239
         FDPRT 'WAS ON',PAD                                     GP02239
         FDPRT IPLCENT,4                                         81340
         FDPRT '.'                                              GP02239
         FDPRT IPLCENT+4,3                                       81340
         FDPRT 'AT',PAD                                          81340
         FDPRT IPLTIME,2                                        GP02239
         FDPRT ':'                                              GP02239
         FDPRT IPLTIME+2,2                                      GP02239
         FDPRT ':'                                              GP02239
         FDPRT IPLTIME+4,2                                      GP02239
.NOUCVO  FDPRT *END                                              81340
CMPDSN   DC    C'DSN'        COMPARE DSN TO MASK                GP02289
CMPVOL   DC    C'VOL'        COMPARE VOLSER MASK                GP02289
         STITL 'LITERAL POOL'                                    81352
         PRINT NOGEN                                            GP02288
         LTORG ,                                                 81352
         STITL 'READ-ONLY DATA AREA'                            GP02239
NOEXDATE DC    0XL3'0',AL1(99),AL2(365)  NO EXPIRATION EVER     GP02288
TRTDSX   DC    256AL1(0)     ALL VALID                          GP02238
         TRENT TRTDSX,4,0,C' ',C'.'  STOP ON HEX ZERO, BLANK, PERIOD
         ORG   ,                                                GP02238
         SPACE 1                                                GP02238
TRTDIG   DC    256AL1(4)     EVERYTHING INVALID                 GP02239
         TRENT TRTDIG,0,(C'0',10)  EXCEPT DIGITS                GP02239
         ORG   ,                                                GP02239
         SPACE 1                                                GP02238
TRTSEP   DC    256AL1(0)     EVERYTHING VALID                   GP02287
         TRENT TRTSEP,16,C' '  END OF TEXT                      GP02287
         TRENT TRTSEP,4,C',',C';'  UNIT/VOL SEPARATOR           GP02287
         TRENT TRTSEP,4,C'/',C'-',C'=' UNIT/VOL SEPARATOR       GP02287
         ORG   ,                                                GP02287
         SPACE 1                                                GP02238
TRTMSK   DC    256AL1(0)     EVERYTHING VALID                   GP02289
         TRENT TRTMSK,4,C'?',C'*'  EXCEPT MASK CHARACTERS       GP02289
         ORG   ,                                                GP02289
TRTBLANK DC    256AL1(0)     DON'T STOP                         GP03011
         TRENT TRTBLANK,4,C' '  EXCEPT ON BLANK                 GP03011
         ORG   ,             JUST IN CASE                       GP03011
         STITL 'VOLSER; DS AND INDEX; AND GENERAL DELETES'       81352
*---------------------------------------------------------------------*
*                                                                     *
*  ALL DEFINITIONS FOLLOWING ARE REFERENCED VIA BXLE TABLES, AND DO   *
*    NOT NEED A BASE REGISTER FOR ADRESSABILITY                       *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*  TABLE OF VOLUME SERIAL (PREFIXES) FOR WHICH ALL DATA SETS          *
*    EXCEPT VTOC, CATALOG, AND VVDS ARE DELETED                       *
*                                                                     *
*---------------------------------------------------------------------*
WORKTAB  DEFVOL SORT          SORT AND USER WORK PACKS          GP02287
WORKTABN DEFVOL WORK          SYSTEM SCRATCH PACKS               81352
         SPACE 2                                                 81352
*---------------------------------------------------------------------*
*                                                                     *
*  DATA SET NAMES AND INDICES RETAINED ON WORK PACKS                  *
*                                                                     *
*---------------------------------------------------------------------*
DSNXTAB  DEFDSN PASSWORD                                         81340
         DEFDSN SYSCTLG                                          81340
         DEFINX CATALOG                                         GP02288
         DEFINX UCAT                                            GP02287
         DEFINX USERCAT                                         GP02287
DSNXTABN DEFINX SYS1                                            GP02287
         SPACE 1                                                GP02289
*---------------------------------------------------------------------*
*                                                                     *
*  DATA SET MASKS ELIGIBLE FOR DELETION WHEN OVERRIDE IS USED         *
*                                                                     *
*---------------------------------------------------------------------*
MASKTAB  BCON  '*.SPFLOG*.**'     DELETE OLD ISPF/PDF DATA      GP02289
         BCON  '*.SPF*.I*'        COMPRESS OUTPUT               GP02346
         BCON  'SYS2.EXHIBIT.DU*'      EXORCIST WORK DATA       GP09210
MASKTABN BCON  '*.SPFTEMP*.**'    DELETE OLD ISPF/PDF DATA      GP02289
         SPACE 1                                                GP02289
*---------------------------------------------------------------------*
*                                                                     *
*  DATA SET MASKS ELIGIBLE FOR DELETION UNLESS UNEPIRED               *
*                                                                     *
*---------------------------------------------------------------------*
MASKUNK  BCON  'SYS2.EXHIBIT.DU*'      EXORCIST WORK DATA       GP09212
MASKUNKN EQU   MASKUNK       -ONLY ONE AT PRESENT-              GP09212
         STITL 'WORK AREA'                                       81352
*        THE FOLLOWING CONSTANTS AND AREAS ARE IN A SEQUENCE TO FORCE
*       BOUNDARIES AND TO SHARE WORK AREAS - DO NOT ALTER THIS SEQUENCE
*
SAVE     DSECT ,             SAVE AND WORK AREA                  81340
DB       DS    D             WORK WORD                           81340
         SERVDEFS ,          SERVICE ADDRESSES                  GP05120
RETCC    EQU   RETCODE+3,1,C'F'  SMALL PORTION OF RETURN CODE   GP05120
@UCB     DS    A             ADDRESS OF CURRENT UCB             GP02298
DSCB4    CAMLST SEARCH,DS1DSNAM,DSPVOL,DS4IDFMT                  81340
DSCB1    CAMLST SEEK,CCHHR,DSPVOL,DS1DSNAM                       81340
SCRDSN   CAMLST SCRATCH,DS1DSNAM,,VOLIST,,OVRD                   81340
CATFND   CAMLST NAME,DS1DSNAM,,CATWORK  SEE IF IN CATALOG        81340
UNCAT    CAMLST UCATDX,DS1DSNAM    UNCATALOG                     81340
JULPACK  DC    PL4'0'        PACKED DECIMAL JULIAN DATE         GP02233
JULCENT  DC    0CL7' ',CL2' '  JULIAN DATE PREFIX - CENTURY     GP02233
JULDAT   DC    CL5' '        JULIAN DATE                         81340
PDATE    DS    XL3           DATE IN DSCB FORMAT                 81340
CUDATE   DS    XL3           DATE IN DSCB FORMAT                 81340
IPLCENT  DC    0CL4'CENT',CL2'99',0CL5'99999'  DATE OF LAST IPL GP02239
IPLDATE  DC    CL5'99999'    DATE OF LAST IPL                    81340
IPLTIME  DC    CL6'999999'   TIME OF LAST IPL                    81340
IPLSYS   DC    CL4' '        ID OF SYSTEM IPL                   GP02239
WTOR     WTOR  'NEXT VOLUME TO BE SCRATCHED:',REPLY,16,ECB,MF=L 1/2
ENQLIST  ENQ   (QSYSDSN,DS1DSNAM,E,44,SYSTEM),RET=TEST,MF=L     2/2
ECB      DC    F'0'
REPLY    DC    CL16' '
MASKL44  DC    CL44' '       MASK FOR SUBCOMP CALLS             GP02289
VOLMASK  DC    CL6' '        REFERENCED VOLUME (INPUT MASK)     GP02289
CMPPLIST DC    A(CMPDSN,CMPDSN,MASKL44,CMP@WORK)                GP02289
CMP@WORK DC    A(0)          ADDRESS OF GETMAINED WORK AREA     GP02289
BLKSTART DS    0C            START OF AREA BLANKED FOR EACH DSCB 81340
         DS    C       1/2   (PAD FOR EXPDATE)                  GP02299
EXPDATE  DS    CL10'YYYY/MM/DD'  EXPIRATION DATE OR BLANKS      GP02287
DSPYESNO DS    CL3' '        BLANK OR 'NOT'                      81340
DSPEXPL  DS    CL9           BLANK OR EXCEPTION MESSAGE          81340
DSPCATST DS    CL9           BLANK OR SCR/CAT/UNCAT MESSAGE      81340
BLLEN    EQU   *-BLKSTART                                        81340
CMPMSKLN DC    H'0'          0 OR 1 (LEN-1) OF UNIT TYPE CLC    GP02239
SORTPARM SERVSORT DSECT=NO   PARAMETER LIST FOR INDEX SORT      GP02239
         SPACE 1                                                GP02298
CCHHR    DS    CL5' '        CCHHR OF THIS/NEXT DSCB             81340
CC       EQU   CCHHR,2         CYLINDER
HH       EQU   CCHHR+2,2         TRACK
R        EQU   CCHHR+4,1            RECORD                       81340
         SPACE 1                                                 81352
FLAGS    DC    X'00'         PROCESSING FLAGS                    81340
FGAUTO   EQU   X'80'           USE CANNED REQUESTS (DUMMY SYSIN) 81340
FGAUTOD  EQU   X'40'           AUTO REPLY ISSUED                 81340
FGSYN    EQU   X'20'           USE SYSIN, NOT WTOR               81340
FGUCB    EQU   X'10'           LOOP THROUGH UCBS BY TYPE         81340
FGTEST   EQU   X'08'           TEST MODE (NO SCRATCHING)         81340
FGSYS    EQU   X'04'           PROCESS SYSTEM PACKS ONLY         81340
FGOVER   EQU   X'02'           FORCE EXPIRATION DATE OVERRIDE    81340
FGVERB   EQU   X'01'           PRINT LOTS OF OUTPUT             GP02287
         SPACE 1                                                 81352
CURFLAGS DC    X'00'         CURRENT VOLUME FLAGS               GP02289
FGWORK   EQU   X'80'           WORK/SORT UNIT - CLEAN ALL       GP02289
FGBPRV   EQU   X'10'           PRIVATE PACK (MATCHES UCBBPRV)   GP02289
FGBSTR   EQU   X'08'           STORAGE PACK (MATCHES UCBBSTR)   GP02289
FGBPUB   EQU   X'04'           PUBLIC PACK (MATCHES UCBBPUB)    GP02289
         SPACE 1                                                 81352
PRTDSN   DS    CL44          DATA SET NAME OF SYSPRINT          GP02239
REQUNIT  DC    CL6' '              EBCDIC UNIT TYPE              81352
DSPDEVTP DC    CL8' '              EBCDIC UNIT TYPE FOR LISTING  81352
VOLIST   DC    H'1'    1/4   NUMBER OF VOLUMES DATA SET RESIDES ON
VOLTYP   DC    XL4'0'  2/4   TYPE OF RESIDENCE VOLUME
DSPVOL   DC    CL6' '  3/4   VOLUME SERIAL NUMBER + COMPARE PAD GP02288
VOLSEQ   DC    H'0'    4/4   SEQUENCE OF VOL. FOR DS / RETURN CODE
WORK4    IECSDSL1 4          EXPAND FORMAT 4 DSCB                81340
FMT4CCHH DS    XL5           @OBTAIN RETURNED DSCB ADDRESS       81340
         IECSDSL1 1          EXPAND FORMAT 1 DSCB                81340
FMT1CCHH DS    XL5           @OBTAIN RETURNED DSCB ADDRESS       81340
CATWORK  DS    35D                 WORK AREA
FGPASSWD EQU   DS1IND40+DS1IND10+DS1IND04                        80023
         AIF   (&MVSXA).SYSDEF                                  GP04234
DS1FLAG1 EQU   DS1NOBDB+1,1,C'X'  MORE FLAGS
DS1COMPR EQU   X'80'           COMPRESSABLE EXTENDED IF DS1STRP
DS1CPOIT EQU   X'40'           CHECKPOINTED D S
DS1SMSFG EQU   DS1FLAG1+17,1,C'X'  SMS FLAG
DS1SMSDS EQU   X'80'           SMS D S
DS1SMSUC EQU   X'40'           NO BCS ENTRY
DS1REBLK EQU   X'20'           MAY BE REBLOCKED
DS1CRSDB EQU   X'10'           BLKSZ BY DADSM
DS1PDSE  EQU   X'08'           PDS/E
DS1STRP  EQU   X'04'           EXTENDED FORMAT D S
DS1PDSEX EQU   X'02'           HFS D S
DS1DSAE  EQU   X'01'           EXTENDED ATTRIBUTES EXISY
DS1SCEXT EQU   DS1SMSFG+1,3,C'X'  SECONDARY SPACE EXTENSION
DS1SCXTF EQU   DS1SCEXT,1,C'X'  -"- FLAG
DS1SCAVB EQU   X'80'           SCXTV IS AVG BLOCK LEN
DS1SCMB  EQU   X'40'                 IS IN MEGBYTES
DS1SCKB  EQU   X'20'                 IS IN KILOBYTES
DS1SCUB  EQU   X'10'                 IS IN BYTES
DS1SCCP1 EQU   X'08'           SCXTV COMPACTED BY 256
DS1SCCP2 EQU   X'04'                 COMPACTED BY 65536
DS1SCXTV EQU   DS1SCXTF+1,2,C'X'  SEC SPACE EXTNSION VALUE
DS1ORGAM EQU   DS1ACBM         CONSISTENT NAMING - VSAM D S
DS1RECFF EQU   X'80'           RECFM F
DS1RECFV EQU   X'40'           RECFM V
DS1RECFU EQU   X'C0'           RECFM U
DS1RECFT EQU   X'20'           RECFM T   001X XXXX IS D
DS1RECFB EQU   X'10'           RECFM B
DS1RECFS EQU   X'08'           RECFM S
DS1RECFA EQU   X'04'           RECFM A
DS1RECMC EQU   X'02'           RECFM M
*   OPTCD DEFINITIONS   BDAM    W.EFA..R
*                       ISAM    WUMIY.LR
*             BPAM/BSAM/QSAM    WUCHBZTJ
DS1OPTIC EQU   X'80'  FOR DS1ORGAM - CATLG IN ICF CAT
DS1OPTBC EQU   X'40'           ICF CATALOG
DS1RACDF EQU   DS1IND40
DS1SECTY EQU   DS1IND10
DS1WRSEC EQU   DS1IND04
DS1SCAL1 EQU   DS1SCALO,1,C'X'    SEC. ALLOC FLAGS
DS1DSPAC EQU   X'C0'         SPACE REQUEST MASK
DS1CYL   EQU   X'C0'           CYLINDER BOUND
DS1TRK   EQU   X'80'           TRACK
DS1AVRND EQU   X'41'           AVG BLOCK + ROUND
DS1AVR   EQU   X'40'           AVG BLOCK LEN
DS1MSGP  EQU   X'20'
DS1EXT   EQU   X'10'           SEC. EXTENSION EXISTS
DS1CONTG EQU   X'08'           REQ. CONTIGUOUS
DS1MXIG  EQU   X'04'           MAX
DS1ALX   EQU   X'02'           ALX
DS1DSABS EQU   X'00'           ABSOLUTE TRACK
DS1SCAL3 EQU   DS1SCAL1+1,3,C'X'  SEC ALLOC QUANTITY
.SYSDEF  SPACE 1
*              RACF     PASSWORD R/O                             80023
         ORG   CATWORK
CATVOL#  DS    H             NO. OF VOLUMES
CATDEV   DS    CL4           DEVICE TYPE
CATVOL   DS    CL6           FIRST VOLUME
         ORG   ,
         SPACE 1                                                GP02238
*  TABLE TO ACCUMULATE HIGH-INDICES                             GP02238
*                                                               GP02238
VSXBXLE  DC    A(VSXTABLE-SAVE,6,VSXTABLN-6-SAVE,0) BXLE ACCESS GP02239
INXBXLE  DC    A(INXTABLE-SAVE,8,INXTABLN-8-SAVE,0) BXLE ACCESS GP02238
VSXTABLE DS    (1000)CL6     VOLUME SERIALS                     GP02239
VSXTABLN EQU   *             END OF TABLE                       GP02239
INXTABLE DS    (1000)CL8     INDICES FOUND                      GP02238
INXTABLN EQU   *             END OF TABLE                       GP02238
SAVEEND  EQU   *             END OF WORK AREA                    81340
         STITL 'TYPE TABLE MAPPING'                              81352
TYPETMAP DSECT ,                                                 81352
TYPETYPE DS    XL4                 UCBTYPE                       81352
TYPETCOD DS    CL6                 TYPE CODE IN REPLY            81352
TYPETNAM DS    CL8                 TYPE NAME IN LISTING          81352
TYPETNXT EQU   *                   NEXT ENTRY                    81352
TYPETLEN EQU   TYPETNXT-TYPETMAP   LENGTH OF ENTRY               81352
         STITL 'MISCELLANEOUS MAPPINGS'                          81352
         DCBD  DEVD=DA,DSORG=XE                                  81352
         SPACE 1                                                 81352
         PRINT &PRTSYS                                           81340
CVT      DSECT ,                                                 81340
         CVT   DSECT=YES                                         81340
         SPACE 1                                                 81340
         IHACDE ,                                                81340
         SPACE 1                                                 81340
         IEFUCBOB ,                                              81340
         SPACE 1                                                 81340
         IKJTCB ,                                                81340
         SPACE 1                                                 81340
         IEZJSCB ,                                              GP02346
         SPACE 1                                                 81340
         IEFTIOT1 ,                                              81340
         SPACE 1                                                 81340
         IEESMCA ,                                              GP02239
         SPACE 1                                                 81340
         IHAPSA ,                                               GP02239
         END   ,
