MSGREFOS TITLE 'M S G R E F O S  ***  SYSTEM MESSAGE REFERENCE'
         PUNCH '  ORDER MSGREFOS(P) '   MAKE DUMPS EASIER       GP05314
         SPACE 1
         COPY  OPTIONGB      DEFINE GLOBALS                     GP05314
         SPACE 1
         SYSPARM LIST=YES    SET AND SHOW GLOBAL OPTIONS        GP05314
         SPACE 1
***********************************************************************
**                                                                   **
**   PROGRAM MSGREFOS COLLECTS CESD (SD, LR, ER) INFORMATION AND     **
**   MESSAGE IDS FROM OBJECT AND LOAD-MODULE LIBRARIES.              **
**     IT WILL PROCESS ANY PDS DD OTHER THAN THOSE STARTING WITH     **
**     SYS, JOB, STEP, OR DEBT                                       **
**   A SINGLE MODULE MAY BE PROCESSED BY SPECIFYING IT AS DSN(MEM)   **
**   ON THE DD. MULTIPLE MODULES MAY BE PROCESSED USING A MINUS SIGN **
**   (MVT, SVS) OR A POUND SIGN AS A WILD-CARD (E.G. IE##CT##).      **
**   ALIAS ENTRIES ARE SKIPPED EXCEPT WHEN EXPLICITLY SPECIFIED      **
**   WITHOUT A MASK.                                                 **
**                                                                   **
**   OUTPUT IS CARD FORMAT, AND IS FED INTO A SORT STEP. SORTED      **
**   OUTPUT IS PROCESSED BY PROGRAM MSGREFLS.                        **
**                                                                   **
**   THIS CODE (AND THE MATCHING MSGREFLS) WERE DISTRIBUTED AS IBM   **
**   TYPE 3 PROGRAMS UNDER A DIFFERENT NAME. I HAD IT AS SAMSGREF.   **
**                                                                   **
***********************************************************************
**                                                                   **
**   THE PROGRAM RUNS THROUGH ALL INPUT LIBRARIES AND COLLECTS BASIC **
**   LOAD MODULE INFORMATION, AND EXTRACTS (POSSIBLE) MESSAGE IDS    **
**   FOR REFERENCING. MESSAGE HEADERS TYPICALLY HAVE THREE LETTERS,  **
**   THREE OR FOUR DIGITS, AND ONE MORE LETTER (A, D, I, W, E)       **
**                                                                   **
**   THE DISTRIBUTED OBJECT DECKS WERE DISASSEMBLED AND MODIFIED TO  **
**   SUPPORT A DASD BLOCK SIZE OF 32K            G. POSTPISCHIL      **
**                                                                   **
**   THE DISASSEMBLY AND MINOR MODS WERE DESIRABLE BECAUSE ONE OF MY **
**   LOAD LIBRARIES HAS A NON-LOAD MODULE MEMBER, AND THIS PROGRAM   **
**   NEEDS ERROR RECOVERY TO AVOID AN S001-1 ABEND.                  **
**                                                                   **
***********************************************************************
         SPACE 1
*DEFER*  PRINT &PRTSRC
MSGREFOS PGMHEAD ZERO31,BASE=(R12),AM=24,RM=ANY                 GP05314
         MVI   PRINTCC,C' '  INIT PRINT BUFFER                  GP05314
         MVC   PRINTLIN,PRINTCC  CLEAR ALL OF IT                GP05314
         MVC   SORTREC,PRINTLIN  CLEAR SORT OUTPUT, ALSO        GP05314
         #OPMSG 'MSGREFOS',TYPE=INIT                            GP05314
         OPEN  MF=(E,OCPRINT)  OPEN PRINT AND FICHE OUTPUT      GP05314
         TM    DCBOFLGS-IHADCB+SYSPRINT,DCBOFOPN  SUCCESSFUL?   GP05314
         BZ    ERRNTPRT
         TM    DCBOFLGS-IHADCB+SYSFICHE,DCBOFOPN  SUCCESSFUL?   GP05314
         BZ    ERRNTFCH
         LTCB  R1            GET MY TCB                         GP05314
         L     R4,TCBTIO-TCB(,R1)   GET TIOT                    GP05314
         USING MYTIOT,R4
         LA    R4,TIOENTRY   SKIP PRELUDE                       GP05314
         USING TIOELNGH,R4
LOOPTIOT SR    R11,R11
         ICM   R11,1,TIOELNGH                                   GP05314
         BZ    DONETIOT
         CLC   =C'JOB',TIOEDDNM                                 GP05314
         BE    BUMPTIOT
         CLC   =C'STEP',TIOEDDNM                                GP05314
         BE    BUMPTIOT
         CLC   =C'SYS',TIOEDDNM                                 GP05314
         BE    BUMPTIOT
         CLC   =C'PGM=',TIOEDDNM                                GP05314
         BE    BUMPTIOT
         CLC   =C'DEBT',TIOEDDNM  LOCAL DEBTRACE                GP05314
         BE    BUMPTIOT                                         GP05314
         CLI   TIOEDDNM,X'40'  CONCATENATED ?                   GP05314
         BNE   HAVETIOT
BUMPTIOT AR    R4,R11        NEXT ENTRY
         B     LOOPTIOT
         SPACE 1
ABENDU08 #OPMSG 'ERROR PROCESSING DD |',LIST=(TIOEDDNM,8)       GP05314
         ABEND 8,DUMP                                           GP05314
         SPACE 1
HAVETIOT MVC   DCBDDNAM+PDSDCB-IHADCB(8),TIOEDDNM               GP05314
         MVC   DCBDDNAM+MEMDCB-IHADCB(8),TIOEDDNM               GP05314
         DROP  R4
         NI    PROFLAGS,RESPDSF   RESET DATA SET PROCESSING     GP05314
         RDJFCB (PDSDCB,INPUT)                                  GP05314
         MVC   SORTSRC(5),JFCBDSNM+5                            GP05314
         DEVTYPE PDSDCB+DCBDDNAM-IHADCB,DB                      GP05314
         CLI   DB+2,X'20'    UCB3DACC                           GP05314
         BNE   ERRNTDA
         L     R14,CAMPAT    GET CAMLIST FLAGS                  GP05314
         LA    R15,JFCBDSNM  GET DS NAME                        GP05314
         LA    R0,JFCBVOLS   GET VOLUME SERIAL                  GP05314
         LA    R1,DS1FMTID   GET RESULT ADDRESS                 GP05314
         STM   R14,R1,DB     BUILD CAMLIST THE HARD WAY         GP05314
         OBTAIN DB           GET FORMAT 1 DSCB FOR DATA SET     GP05314
         LTR   R15,R15
         BNZ   ABENDU08
         TM    DS1DSORG,DS1DSGPO  PARTITIONED ?
         BNO   ERRNTPDS
         TM    DS1RECFM,X'C0'     RECFM=U ?
         BO    PROCTIOT
         CLC   =H'80',DS1LRECL  CARD FORMAT ?                   GP05314
         BNE   ERRNTCRD
         OI    PROFLAGS,FGCARD    CARD FORMAT INPUT             GP05314
*HUH?*   MVC   SORTSRC(5),DCCSYSGN                              GP05314
PROCTIOT AR    R4,R11
         ST    R4,@NEXTDD
         MVI   JCLMEM,C'*'   UNIVERSAL MATCH                    GP05314
         TM    JFCBIND1,JFCPDS   DDNAME WITH MEMBER ?           GP05314
         BZ    MASKTEST      NO; PROCESS ALL MEMBERS (NO ALII)  GP05314
         SPACE 1
PDSNJMEM MVC   JCLMEM,JFCBELNM
         NI    JFCBIND1,255-JFCPDS  RESET PDS BIT               GP05314
         MVC   JFCBELNM,BLANKS
         MVC   SORTSRC(5),DCBDDNAM-IHADCB+PDSDCB                GP05314
MASKTEST L     R14,=A(TRTWILD)   POINT TO WILDCARD TABLE        GP05314
         TRT   JCLMEM,0(R14) ANY WILD CARDS ?                   GP05314
         BNZ   PREPPDS       YES; SKIP ALII                     GP05314
         OI    PROFLAGS,FGDOAL    SPECIFIC MEMBER - ALIAS OK    GP05314
         SPACE 1
PREPPDS  OPEN  MF=(E,OCPDS),TYPE=J  OPEN MEMBER AND DIRECTORY   GP05314
         SPACE 1
READDIRE LA    R2,PDEKEY     POINT TO DIRECTORY KEY             GP05314
         READ  PDSDECB,SF,PDSDCB,(R2),256+8  READ DIRECTORY     GP05314
         CHECK PDSDECB                                          GP05314
         TM    PROFLAGS,FGPDSIO  READ ERROR?                    GP05314
         BO    WTOIOERR
         ICM   R0,3,PDEBLOCK  PAST LAST DIRECTORY ENTRY ?       GP05314
         BZ    PDSEODAD      YES; MISSED X'FF' ENTRY ?
         LA    R11,PDETEXT
         USING PDS2,R11
LOOPDIRE CLI   PDS2NAME,X'FF'   LOGICAL END OF DIRECTORY?
         BE    PDSEODAD
         NI    PROFLAGS,RESMEMF   RESET MEMBER FLAGS            GP05314
         MVC   SORTMEM,PDS2NAME
         LA    R15,JCLMEM    POINT TO MASK                      GP05314
         LA    R1,PDS2NAME   POINT TO MEMBER NAME               GP05314
         LA    R0,L'PDS2NAME  LENGTH OF MASK AND NAME           GP05314
         BAL   R14,CLCMASK   COMPARE                            GP05314
         BNE   BUMPDIRE      IGNORE MEMBER                      GP05314
         TM    PROFLAGS,FGDOAL  PROCESS ALIAS ENTRIES?          GP05314
         BO    ACCALIAS      YES
         TM    PDS2INDC,PDS2ALIS   ALIAS?
         BO    BUMPDIRE      YES; SKIP
ACCALIAS TM    PROFLAGS,FGCARD  CARD FORMAT INPUT?              GP05314
         BO    PROCMEM       YES                                GP05314
         TM    PDS2INDC,PDS2NTTR  ANY NOTE LISTS?               GP05314
         BZ    BUMPDIRE     NO; NOT A LOAD MODULE               GP05314
         TM    PDS2ATR2,PDS2NREP   SKIP IF NO CESD INFO
         BO    NEDITMEM
         B     PROCMEM
         SPACE 1
BUMPDIRE CLC   PDEKEY,PDS2NAME   DID LAST IN BLOCK ?
         BNH   READDIRE
         IC    R15,PDS2INDC                                     GP05314
         SLL   R15,27                                           GP05314
         SRL   R15,26                                           GP05314
         LA    R11,PDS2USRD(R15)                                GP05314
         B     LOOPDIRE
         SPACE 1
PDSEODAD CLOSE MF=(E,OCPDS)  CLOSE MEMBER AND DIRECTORY DCB     GP05314
         L     R4,@NEXTDD
         B     LOOPTIOT
         SPACE 1
NEDITMEM MVC   SORTKEY,DCCNE
         UNPK  SORTOFF(5),PDS2STOR(4)
         MVI   SORTRSV2,C' '
         TR    SORTOFF,HEXTRTAB
         MVC   SORTSECT,SORTMEM
         PUT   SYSFICHE,SORTREC                                 GP05314
         LA    R3,TEXTDONE
         MVC   HWORD,PDS2FTBL                                   GP05314
         L     R7,FWORD                                         GP05314
         MVC   HWORD,PDS2FTB2                                   GP05314
         L     R6,FWORD                                         GP05314
         MVC   CURRTTR(3),PDS2USRD
         POINT MEMDCB,CURRTTR                                   GP05314
         TM    PDS2ATR,PDS21BLK  SINGLE BLOCK ?                 GP05314
         BO    TEXTLEOF
         B     TEXTTSEC
         SPACE 1
PROCMEM  MVC   CURRTTR(3),PDS2TTRP
         POINT MEMDCB,CURRTTR                                   GP05314
CNTLREAD BAL   R10,SUBREAD                   PERFORM
         TM    PROFLAGS,FGCARD  CARD FORMAT ?                   GP05314
         BO    CARDBLOK      YES
         LA    R3,CNTLREAD   NORMAL CONTINUATION                GP05314
         NI    CESDTYPE,X'F7'                                   GP05314
         CLI   CESDTYPE,X'20'                                   GP05314
         BE    PROCCESD      PROCESS CESD DATA
         TM    CESDTYPE,X'01'                                   GP05314
         BO    TEXTNEXT      NEXT RECORD IS A TEXT RECORD
         TM    CESDTYPE,X'04'                                   GP05314
         BO    BUMPDIRE      LAST RECORD IN LOAD MODULE
         B     CNTLREAD
TEXTNEXT LM    R6,R7,CESDADDR
         LA    R6,0(R6)
         LA    R7,0(R7)
         TM    CESDTYPE,X'04'                                   GP05314
         BNO   TEXTTSEC
TEXTLEOF OI    PROFLAGS,FGMEMEOF                                GP05314
TEXTTSEC ICM   R0,3,PDS2FTBL                                    GP05314
         BZ    BUMPDIRE                                         GP05314
         BAL    R10,SUBREAD                   PERFORM
         BAL    R3,TEXTSECT                   PERFORM
TEXTDONE TM    PROFLAGS,FGMEMEOF  LOGICAL END ?                 GP05314
         BO    BUMPDIRE
         B     CNTLREAD
         DROP   R11
TEXTSECT LA     R8,SECTAB
         SR     R2,R2
         SR     R1,R1
         LA     R4,SECTAB(R7)
         ST     R4,@SECT
         C      R6,@NSECT
         LA     R4,0(R6,R7)
         ST     R4,@NSECT
         BE     FINDLOOP
         XC     SECTAB(6),SECTAB
FINDLOOP SH    R7,=H'256'                                       GP05314
         BL    FINDPART                                         GP05314
         LA    R4,X'FF'                                         GP05314
FINDLEX  EX    R4,EXTRTCH1
         BZ    FINDBUMP
         AR     R7,R8
         LA     R7,1(R4,R7)
         SR     R7,R1
         LR     R8,R1
         TRT    1(3,R8),TRTMSGID                                GP05314
         BM     FINDSKIP
         BZ     FINDTIEG
         CLI    1(R8),X'C7'
         BE     FINDTIGF
FINDCONT TRT    3(4,R8),TRTMSGNO                                GP05314
         BM     FINDSKIP
         BZ     FINDTIEW
         BCT    R2,FINDTIET                   LOOP
FINDSORT L     R4,@NSECT
         SR    R4,R7
         SH    R4,=H'6'                                         GP05314
         ST    R4,DB                                            GP05314
         UNPK  SORTOFF(5),DB(5)                                 GP05314
         MVI   SORTRSV1,C' '                                    GP05314
         MVI   SORTRSV2,C' '
         TR    SORTOFF,HEXTRTAB
         MVC   SORTMSG,0(R8)
         MVI   SORTREC,C' '
         MVC   SORTKEY,DCCZBB
         PUT   SYSFICHE,SORTREC                                 GP05314
FINDSKIP LA     R8,1(R8)
         BCT    R7,FINDLOOP                   LOOP
FINDNMAT L      R4,@SECT
         MVC    SECTAB(6),0(R4)
         BR     R3
FINDBUMP LA     R8,1(R4,R8)
         B      FINDLOOP
FINDTIGF CLI    2(R8),X'C6'
         BE     FINDCONT
         B      FINDSKIP
EXTRTCH1 TRT   0(0,R8),TRTMSCH1
FINDPART AH    R7,=H'255'    FIX LENGTH                         GP05314
         BM    FINDNMAT      DONE
         LR    R4,R7
         SR    R7,R7
         B     FINDLEX
FINDTIET CLC    DCCET(2),1(R8)
         BNE    FINDTIEU
FINDLEN6 MVI    6(R8),X'40'
         B      FINDSORT
FINDTIEU CLC    DCCEU(2),1(R8)
         BE     FINDLEN6
         B      FINDSKIP
FINDTIEW CLC    DCCEW(2),1(R8)
         BE     FINDSORT
         CLC    DCCEQ(2),1(R8)
         BE     FINDSORT
         CLC    DCCEM(2),1(R8)
         BE     FINDSORT
         B      FINDSKIP
FINDTIEG CLC    1(3,R8),DCCEGE
         BE     FINDT2NO
         CLC    1(3,R8),DCCEGI
         BE     FINDT2NO
         CLC    1(4,R8),DCCFBF0
         BE     FINDSORT
         B      FINDSKIP
FINDT2NO TRT    4(2,R8),TRTMSGNO                                GP05314
         BNZ    FINDSKIP
         B      FINDLEN6
         SPACE 2
***********************************************************************
*   PROCESS A CESD BLOCK                                              *
*   WRITE A RECORD FOR EACH SD, LR, AND ER ENTRY                      *
***********************************************************************
         PUSH  USING
PROCCESD LH    R5,CESDLEN    GET DATA LENGTH                    GP05314
         SRA   R5,4          CONVERT TO ENTRY COUNT             GP05314
         BNPR  R3            QUIT IF EMPTY                      GP05314
         LA    R8,CESDNAME
LOOPCESD NI    CESDENTY-CESDNAME(R8),X'0F'
         CLI   CESDENTY-CESDNAME(R8),X'00'
         BE    FOUNDSD
         CLI   CESDENTY-CESDNAME(R8),X'03'
         BE    FOUNDLR
         CLI   CESDENTY-CESDNAME(R8),X'01'
         BE    FOUNDLR
         CLI   CESDENTY-CESDNAME(R8),X'02'
         BNE   BUMPCESD
         SPACE 1
FOUNDER  MVC   SORTSECT,CESDNAME-CESDNAME(R8)
         MVC   SORTKEY,ESDTYER
         MVC   SORTOFF,BLANKS
         B     FOUNDPUT
         SPACE 1
FOUNDSD  MVC   SORTKEY,ESDTYSD
         MVC   SORTSECT,CESDNAME-CESDNAME(R8)
         UNPK  SORTOFF(5),CESDCSIZ-CESDNAME(4,R8)
         MVI   SORTRSV2,C' '
         TR    SORTOFF,HEXTRTAB
         B     FOUNDPUT
         SPACE 1
FOUNDLR  MVC   SORTKEY,ESDTYLR
         MVC   SORTSECT,CESDNAME-CESDNAME(R8)
         MVC   SORTOFF,SORTRSV1
         B     FOUNDPUT
         SPACE 1
FOUNDPUT PUT   SYSFICHE,SORTREC                                 GP05314
         SPACE 1
BUMPCESD LA    R8,CESDELEN(,R8)  NEXT ENTRY                     GP05314
         BCT   R5,LOOPCESD   DO ALL IN THIS BLOCK               GP05314
         BR    R3
         POP   USING
         SPACE 2
***********************************************************************
*   INPUT IS CARD FORMAT - LOOK FOR OBJECT DECK ESD AND TXT CARDS     *
***********************************************************************
CARDBLOK L     R1,MEMDECB+DECIOBPT-DECB  GET IOB ADDRESS        GP05314
         LH    R2,MEMDCB+DCBBLKSI-IHADCB  BLOCK SIZE            GP05314
         SH    R2,IOBCSW+5-IOBSTDRD(,R1)  LESS RESIDUAL LENGTH  GP05314
         LA    R2,DATBLOCK(R2)
         LA    R1,DATBLOCK
         STM   R1,R2,PTRDATA
         CLI   OBJID,OBJIDOBJ   OBJECT DECK ?                   GP05314
         BE    TYPEOBJ
LOOKOBJ  LM    R1,R2,PTRDATA
LOOKINC  LA    R1,80(,R1)
         CR    R1,R2
         BNL   CNTLREAD
         CLI   OBJID-OBJID(R1),OBJIDOBJ   OBJECT DECK ?         GP05314
         BNE   LOOKINC                                          GP05314
         MVC   DATBLOCK(80),OBJID-OBJID(R1)                     GP05314
         ST    R1,PTRDATA
TYPEOBJ  CLC   OBJTYPE,ESDTYTXT                                 GP05314
         BE    OBJTXT
         CLC   OBJTYPE,ESDTYESD                                 GP05314
         BE    OBJESD
         CLC   OBJTYPE,ESDTYEND                                 GP05314
         BNE   LOOKOBJ                                          GP05314
         B     BUMPDIRE                                         GP05314
OBJESD   LH    R5,OBJLEN                                        GP05314
         SRA   R5,4          CONVERT LENGTH TO ENTRY COUNT      GP05314
         BNP   LOOKOBJ       IGNORE IF INVALID                  GP05314
         LA    R8,OBJDATA                                       GP05314
         BAL   R3,LOOPCESD                   PERFORM
         B     LOOKOBJ
         SPACE 1
OBJTXT   L     R6,OBJADDR-1                                     GP05314
         LA    R6,0(,R6)
         LH    R7,OBJLEN                                        GP05314
         MVC   DATBLOCK(56),OBJDATA                             GP05314
         BAL   R3,TEXTSECT                   PERFORM
         B     LOOKOBJ
         SPACE 1
SUBREAD  LA    R2,DATBLOCK   POINT TO DATA BUFFER               GP05314
         READ  MEMDECB,SF,MEMDCB,(R2),'S'   READ A BLOCK        GP05314
         CHECK MEMDECB       WAIT FOR IT                        GP05314
         TM    PROFLAGS,FGMEMIO  I/O ERROR DURING READ?         GP05314
         BNZ   MEMEODAD      YES; STOP PROCESSING THIS MEMBER   GP05314
         BR    R10
         SPACE 1
PDSSYNAD OI    PROFLAGS,FGPDSIO   SIGNAL I/O ERROR              GP05314
         OICC  8                                                GP05314
         BR    R14
MEMSYNAD OI    PROFLAGS,FGMEMIO   SIGNAL I/O ERROR              GP05314
         OICC  8                                                GP05314
         BR    R14                                              GP05314
         SPACE 1
WTOIOERR CLOSE MF=(E,OCPDS)  CLOSE DIRECTORY (& MEMBER) DCB     GP05314
         #OPMSG 'DIRECTORY ERROR READING DD |',LIST=(DCBDDNAM-IHADCB+PD*
               SDCB,8)       ISSUE MESSAGE TO CONSOLE           GP05314
         #OPMSG 'DIRECTORY ERROR READING DD |',LIST=(DCBDDNAM-IHADCB+PD*
               SDCB,8),TYPE=TEXT   FORMAT ERROR MESSAGE         GP05314
         LH    R15,0(,R1)    GET MESSAGE LENGTH                 GP05314
         BCTR  R15,0         ADJUST FOR EX                      GP05314
         EX    R15,EXMVIOER  MOVE TO PRINT LINE                 GP05314
EXMVIOER MVC   MSGERDDN(0),2(R1)  MOVE ERROR MESSAGE            GP05314
         BAL   R3,PRTLINE    PRINT IT
         B     PDSEODAD
         SPACE 1
ERRNTDA  MVC   MSGERMSG(23),MSGNTDA
         B     ERRCOMM
         SPACE 1
ERRNTPDS MVC   MSGERMSG(16),MSGNTPDS
ERRCOMM  MVC   MSGERDDN(8),DCBDDNAM-IHADCB+PDSDCB               GP05314
         MVC   PRINTLIN(6),DCCDDN
         BAL   R3,PRTLINE    PRINT
         B     BUMPTIOT
         SPACE 1
PRTLINE  PUT   SYSPRINT,PRINTCC                                 GP05314
         MVC   PRINTLIN,PRINTCC  CLEAR PRINT LINE               GP05314
         BR    R3
         SPACE 1
ERRNTCRD MVC   MSGERDDN(8),DCBDDNAM-IHADCB+PDSDCB               GP05314
         MVC   MSGERMSG(12),MSGNTCRD
         BAL   R3,PRTLINE    PRINT
         B     BUMPTIOT
         SPACE 1
ERRNTPRT #OPMSG 'SYSPRINT DD MISSING'                           GP05314
         B      DONETIOT
         SPACE 1
ERRNTFCH MVC    MSGERDDN(24),MSGNTFCH
         BAL    R3,PRTLINE                    PERFORM
         SPACE 1
DONETIOT CLOSE  MF=(E,OCPRINT)    CLOSE OUTPUT FILES            GP05314
         CLOSE  MF=(E,OCPDS)      CLOSE DCBS                    GP05314
         PGMEXIT COPYRET=(RETCODE,4)                            GP05314
         SPACE 1
MEMEODAD CLOSE MF=(E,OCMEM)  CLOSE MEMBER DCB                   GP05314
         OPEN  MF=(E,OCMEM),TYPE=J                              GP05314
         B      BUMPDIRE
         SPACE 1
***********************************************************************
*   MASKED COMPARE - MASK ADDRESS IN R15, COMPARE ADDRESS IN R1       *
*     MAX LENGTH IN R0                                                *
*   RETURN TO R14 WITH CONDITION CODE SET = OR Â¬=                     *
***********************************************************************
CLCMASK  CMASK MASKEND='*',MASKEQU='#-',RET=R14,MA=R15,ML=R0,A=R1,L=R0
         SPACE 1
         LTORG ,
         SPACE 2
OCPDS    DC    A(PDSDCB)     OPEN/CLOSE LIST FORM - INPUT       GP05314
OCMEM    OPEN  (MEMDCB),MF=L OPEN/CLOSE LIST FORM - INPUT       GP05314
OCPRINT  OPEN  (SYSPRINT,OUTPUT,SYSFICHE,OUTPUT),MF=L           GP05314
         SPACE 1
MEMDCB   DCB   DDNAME=X,DSORG=PS,MACRF=RP,                             *
               EODAD=MEMEODAD,SYNAD=MEMSYNAD,EXLST=PDSEXLST     GP05314
         SPACE 1
PDSEXLST DC    0A(0),X'87',AL3(JFCBDSNM)                        GP05314
PDSDCB   DCB   DDNAME=X,DSORG=PS,MACRF=R,                              *
               RECFM=U,LRECL=256,BLKSIZE=256,KEYLEN=8,                 *
               EODAD=PDSEODAD,EXLST=PDSEXLST,SYNAD=PDSSYNAD     GP05314
         SPACE 1
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,                      *
               RECFM=FBA,LRECL=121                              GP05314
         SPACE 1
SYSFICHE DCB   DDNAME=SYSFICHE,DSORG=PS,MACRF=PM,EROPT=ACC,            *
               RECFM=FB,LRECL=80,BLKSIZE=3120                   GP05314
         SPACE 1
CAMPAT   CAMLST SEARCH,1,2,3                                    GP05314
         ORG   CAMPAT+4      ONLY NEED FLAGS                    GP05314
         SPACE 1
         IEFJFCBN ,          EXPAND JFCB IN-LINE                GP05314
         SPACE 1
HEXTAB   DC    C'0123456789ABCDEF'                              GP05314
HEXTRTAB EQU   HEXTAB-C'0'                                      GP05314
         SPACE 1
BLANKS   DC     CL8' '
MSGNTDA  DC     C'ALLOCATED DEVICE NOT DA'
MSGNTPDS DC     C'DATA SET NOT PDS'
MSGNTFCH DC     C'SYSFICHE DD CARD MISSING'
DCCFBF0  DC     C'FBF0'
MSGNTCRD DC     C'LRECL NOT 80'
DCCET    DC     C'ET'
DCCEU    DC     C'EU'
DCCEW    DC     C'EW'
DCCEQ    DC     C'EQ'
DCCEM    DC     C'EM'
DCCDDN   DC     C'DDNAME'
DCDDSYS  DC     C'SYS'
DCCSYSGN DC     C'SYSGN'
DCCNE    DC     C' NE'
DCCZBB   DC     X'004040'
DCCEGE   DC     C'EGE'
DCCEGI   DC     C'EGI'
ESDTYSD  DC     C' SD'
ESDTYER  DC     X'00C5D9'
ESDTYLR  DC     C' LR'
ESDTYTXT DC     C'TXT'
ESDTYESD DC     C'ESD'
ESDTYEND DC     C'END'
         SPACE 2
SAVE     DSECT ,             SAVE AND WORK AREA                 GP05314
DB       DS    3D            WORK AREA                          GP05314
         SERVDEFS ,          COMMON VARIABLES                   GP05314
         SPACE 1
PTRDATA  DC    2A(0)         OBJECT BLOCK START/END             GP05314
JCLMEM   DC    CL8' '        DD MEMBER NAME                     GP05314
CURRTTR  DC    F'0'          MEMBER TTR(N)                      GP05314
@NEXTDD  DC    A(0)                                             GP05314
@NSECT   DC    A(0)                                             GP05314
@SECT    DC    A(0)                                             GP05314
FWORD    DC    0F'0',H'0'    WORK SPACE                         GP05314
HWORD    DC    H'0'          WORK SPACE                         GP05314
SECTAB   DC    C'      '                                        GP05314
         SPACE 1
PROFLAGS DC    X'00'         PROCESSING FLAGS                   GP05314
FGCARD   EQU   X'80'           INPUT IS CARD FORMAT SEQUENTIAL  GP05314
FGDOAL   EQU   X'40'           PROCESS ALIAS ENTRIES            GP05314
FGMEMEOF EQU   X'04'           LOGICAL EOF ON MEMBER            GP05314
FGPDSIO  EQU   X'02'           PDS DIRECTORY I/O ERROR          GP05314
FGMEMIO  EQU   X'01'           MEMBER DATA I/O ERROR            GP05314
RESMEMF  EQU   255-FGMEMIO-FGMEMEOF   RESET MEMBER ONLY         GP05314
RESPDSF  EQU   255-FGCARD-FGDOAL-FGPDSIO-FGMEMIO-FGMEMEOF RESET GP05314
         SPACE 1
PRINTCC  DC    0CL121' ',C' '  CARRIAGE CONTROL AND BLANKER     GP05314
PRINTLIN DC    CL120' '                                         GP05314
MSGERDDN EQU   PRINTLIN+4,8,C'C'                                GP05314
MSGERMSG EQU   MSGERDDN+10,80,C'C'                              GP05314
         SPACE 1
SORTREC  DC    CL80' '                                          GP05314
         ORG   SORTREC                                          GP05314
SORTSECT DC    0CL8' ',C' '  RE-USE MSG FOR CESD/MOD NAME       GP05314
SORTMSG  DC    CL7' '                                           GP05314
SORTKEY  DC    CL3' ',C' '                                      GP05314
SORTMEM  DC    CL8' ',C' '                                      GP05314
SORTSRC  DC    CL5' '                                           GP05314
SORTRSV1 DC    C' '                                             GP05314
SORTOFF  DC    CL4' '                                           GP05314
SORTRSV2 DC    C' '                                             GP05314
         ORG   SORTREC+80                                       GP05314
         SPACE 2
         IECSDSL1 1          EXPAND FORMAT 1 DSCB IN-LINE       GP05314
         SPACE 2
PDEKEY   DC    CL8' '   1/3  DIRECTORY BLOCK KEY                GP05314
PDEBLOCK DC    H'0'     2/3  DIRECTORY BLOCK LOGICAL LENGTH     GP05314
PDETEXT  DC    XL254'0' 3/3  DIRECTORY BLOCK DATA               GP05314
         SPACE 1
         DS    0D            ALIGN                              GP05314
DATBLOCK DC    128XL256'0'   32K DASD BUFFER                    GP05314
         SPACE 1
         ORG   DATBLOCK      REDEFINE FOR LOAD MODULE CESD REC  GP05314
CESDTYPE DS    X             RECORD TYPE                        GP05314
         DS    XL3'00'                                          GP05314
CESDID   DS    XL2'0000'                                        GP05314
CESDLEN  DS    XL2'0000'                                        GP05314
CESDADDR DS    XL4'00'                                          GP05314
CESDFLAG EQU   CESDADDR,1                                       GP05314
CESDSIZE DS    XL4'00'                                          GP05314
CESDRLDF EQU   CESDSIZE,1                                       GP05314
         SPACE 1
         ORG   CESDADDR      ESD ENTRY                          GP05314
CESDNAME DS    CL8           ESD NAME                           GP05314
CESDENTY DS    X             TYPE (SD, LR, ER, ...)             GP05314
CESDOFFS DS    XL3           OFFSET FROM START OF MODULE        GP05314
CESDREGN DS    X             OVERLAY REGION/SEGMENT             GP05314
CESDCSIZ DS    XL3           SECT SIZE                          GP05314
CESDELEN EQU   *-CESDNAME    LENGTH OF ONE ENTRY                GP05314
         SPACE 1
         ORG   DATBLOCK      REDEFINE FOR OBJECT DECK           GP05314
OBJID    DS    X             OBJECT CARD IDENTIFIER             GP05314
OBJIDOBJ EQU   X'02'         IDENTIFY OBJECT DECK               GP05314
OBJTYPE  DS    CL3           ESD/SYM/TXT/END                    GP05314
         DS    X             SPACE OR HEX ZERO                  GP05314
OBJADDR  DS    XL3           REL. ADDRESS                       GP05314
         DS    XL2           SPACE OR HEX ZERO                  GP05314
OBJLEN   DS    XL2           DATA LENGTH                        GP05314
         DS    XL2           SPACE OR HEX ZERO                  GP05314
OBJESDID DS    XL2           ESDID OF TEXT                      GP05314
OBJDATA  DS    XL56          0-56 BYTES OF DATA                 GP05314
         DS    CL8           (OPT) DECK SEQUENCE NUMBER         GP05314
         ORG   ,             REDEFINE FOR OBJECT DECK           GP05314
SAVEEND  EQU   *                                                GP05314
         SPACE 2
MSGREFOS CSECT ,             UNADDRESSABLE STORAGE              GP05314
TRTMSGID DC    256X'FF'      STOP ON ALL, EXCEPT                GP05314
         TRENT TRTMSGID,0,(C'A',9),(C'J',9),(C'S',8)  ALPHAS    GP05314
         SPACE 1
TRTMSGNO DC    256X'FF'      STOP ON ALL, EXCEPT                GP05314
         TRENT TRTMSGNO,0,(C'0',10)    DIGITS                   GP05314
         TRENT TRTMSGNO,1,C'A',C'D',C'E',C'I',C'W'  FLAGS       GP05314
         SPACE 1
TRTMSCH1 DC    256X'00'      SKIP ON ALL, EXCEPT                GP05314
         TRENT TRTMSCH1,*-TRTMSCH1,(C'A',9),(C'J',9),(C'S',8)   GP05314
         SPACE 1
TRTWILD  DC    256X'00'      NO CHARS ARE WILD CARDS, EXCEPT    GP05314
         TRENT TRTWILD,8,C'*'  UNIVERSAL MATCH                  GP05314
         TRENT TRTWILD,4,C'#',C'-'  SINGLE CHARACTER MATCH      GP05314
         SPACE 1
         SPACE 1
         PRINT ON,NOGEN
         IHAPSA ,                                               GP05314
         CVT   DSECT=YES,LIST=YES                               GP05314
         IKJTCB ,                                               GP05314
MYTIOT   DSECT ,                                                GP05314
         IEFTIOT1 ,                                             GP05314
         IHADCB DEVD=DA,DSORG=PO                                GP05314
         IHADECB ,                                              GP05314
         IEZIOB ,                                               GP05314
         IHAPDS PDSBLDL=NO                                      GP05314
         END   MSGREFOS
