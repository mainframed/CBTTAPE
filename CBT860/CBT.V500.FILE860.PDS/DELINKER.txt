DELINKER TITLE 'D E L I N K E R  ***  DELINK LOAD MODULES TO OBJECT'
*$DOC@*****************************************************************
*                                                                     *
*  DELINK0 - SYSTEM/360 OS FE SERVICE AID PROGRAM.                    *
*            PRODUCES AN OBJECT MODULE FROM A LOAD MODULE.            *
*            CREATES ESD FOR NON-EDITABLE MODULES.                    *
*            EXPANDS MODULE SIZE FOR PATCH AREA.                      *
*                                                                     *
*  SOURCE CREATED BY BILL GODFREY, PLANNING RESEARCH CORPORATION,     *
*  BY DISASSEMBLING THE DELINK0 OBJECT DECK AND FIXING UP THE         *
*  DISASSEMBLED CODE, WHICH HAD NO COMMENTS OR MEANINGFUL NAMES.      *
*  MANY AREAS STILL NEED FIXING UP, BUT THE CODE MATCHES THE          *
*  OS DELINK0 OBJECT DECK.                                            *
*                                                                     *
*  DELINK IS DESCRIBED IN THE OS/360 FE HANDBOOK S229-3169.           *
*                                                                     *
*  DELINKER IS A MODIFICATION WITH ADDITIONAL CAPABILITIES.2010/05/07 *
*                                                                     *
*  //DELINK  EXEC PGM=DELINKER                                        *
*  //SYSPRINT DD  SYSOUT=*                       OUTPUT MESSAGES      *
*  //SYSLIB   DD  DSN=ANY.LOAD.LIBRARY,DISP=SHR  INPUT LIBRARY        *
*  //SYSPUNCH DD  SYSOUT=B                       OUTPUT OBJECT DECK   *
*  //SYSIN    DD  *                              INPUT CONTROL CARD   *
*   MEMBER CSECT                                                      *
*  /*                                                                 *
*                                                                     *
*  ONLY ONE CSECT NAME MAY BE USED ON A CONTROL CARD, BUT A NAME OF   *
*    * OR *ALL* WILL PROCESS ALL SECTIONS.                            *
*  A NAME OF ! WILL PROCESS THE FIRST CSECT, A BLANK NAME USES THE    *
*  MEMBER NAME AS THE CSECT NAME (NOT VALID WITH EXPAND OPTIONS).     *
*                                                                     *
*  IF COLUMN 1 OF THE CONTROL CARD IS A '>' (GREATER THAN SIGN)       *
*  THEN THE CSECT SIZE IN THE PUNCHED DECK WILL BE EXPANDED.          *
*  THE NEW SIZE MUST BE SPECIFIED FOLLOWING THE CSECT NAME, AS        *
*  8 DECIMAL DIGITS OR X'NNNNNN' IN HEX, AND MUST BE GREATER          *
*  THAN THE CURRENT LENGTH OF THE CSECT.                              *
*  FOR EXAMPLE:    > MEMBER CSECT X'0A10'                             *
*  COLUMN 1 MAY ALSO BE <, WITH THE SAME MEANING.                     *
*  WHEN COLUMN 1 IS +, THE VALUE IS USED TO INCREMENT THE CSECT SIZE: *
*    + MEMBER CSECT 32                                                *
*    MEMBER CSECT + 32       BOTH FORMS EXPAND BY X'20' BYTES.        *
*  AN ASTERISK IN COLUMN 1 DENOTES A COMMENT CARD.                    *
*                                                                     *
*  IF THE CSECT NAME IS OMITTED, IT IS ASSUMED TO BE THE SAME         *
*  AS THE MEMBER NAME. CSECT NAME IS REQUIRED FOR EXPAND REQUESTS.    *
*                                                                     *
*                                                                     *
*  OTHER CHANGES - ADDED BACK MORE SYSTEM MACROS; REMOVED FESNAP;     *
*    FIXED 0C4; CONSOLIDATED GETMAINS.             G. POSTPISCHIL     *
*                                                                     *
*$DOC$*****************************************************************
         PUNCH '  ORDER DELINKER(P) '  EASIER DEBUGGING         GP10127
         SPACE 1
         PRINT NOGEN         SAVE A TREE                        GP91129
DELINKER PGMHEAD ZERO31,BASE=(R11,R12),PARM=R9,BNDRY=PAGE       GP10127
         SERVINIT ,          INITIALIZE MY SUPPORT              GP10127
         SERVLOAD @INPREAD,@PRINTER    LOAD SUBROUTINES         GP10127
         SPACE 1
         PRTOPEN SYSPRINT,OPT=(WTO,ABEND)   NEED PRINTOUT       GP10127
         PRTF  HEADER,133,TITLE=1                               GP10127
         PRTOPEN SYSPUNCH,DEV=3             NEED PUNCH ?        GP10127
         INPOPEN SYSIN,OPT=(ABEND)          NEED INPUT          GP10127
         SPACE 1
         OPEN  (SYSLIB,INPUT)                                   GP10127
         TM    DCBOFLGS-IHADCB+SYSLIB,DCBOFOPN  SYSLIB OPEN SUCCESSFUL?
         BZ    MSGNOLIB            NO - UNABLE TO OPEN SYSLIB   GP10127
         SPACE 2
*---------------------------------------------------------------------*
*   PROCESS NEXT LOAD MODULE/CSECT REQUEST                            *
*---------------------------------------------------------------------*
NEXTCARD XC    BLDLL+4(72),BLDLL+4     CLEAR BLDL               GP10127
         MVC   BLDLL(4),=Y(1,72)       PREPARE LIST             GP10127
         MVC   PDS2NAME,BLANKS         CLEAR NAME               GP10127
         MVI   LINE-1,C' '                                      GP10127
         MVC   LINE,LINE-1        CLEAR PRINT LINE              GP10127
         MVC   PUNCHCRD-1(L'PUNCHCRD+1),LINE   CLEAR PUNCH CARD GP10127
         MVI   OBJID,X'02'   OBJECT IDENTIFIER                  GP10127
         XC    MODWORK(MODCLEN),MODWORK  CLEAR FOR NEXT REQUEST GP10127
         NI    SWX,FGHAVIN   RESET ALL NON-INPUT FLAGS          GP10127
         MVI   RECID,X'00'
         LA    R1,OBJTEXT    START FOR TEXT FILL
         ST    R1,PTRTEX                                        GP10127
         ST    R1,PTRTESD    AND ESD FILL                       GP10127
         LA    R1,CESDBUF                                       GP10127
         ST    R1,@CESDS
         ST    R1,@CESDN
         A     R1,=A(WORKBUF-CESDBUF)  SPACE TO END OF BUFFER   GP10127
         ST    R1,@TXTND               DO FIXED AREAS FIRST     GP10127
         A     R1,=A(128*256)            PAST LAST BUFFER       GP10127
         ST    R1,@RLDEND              SAVE END                 GP10127
         LA    R1,1                                             GP10127
         STH   R1,CNTESX                                        GP10127
         STH   R1,CURRID                                        GP10127
         SPACE 1
REGET    INPGET PUNCHCRD           GET A CARD FROM SYSIN        GP10127
         BXH   R15,R15,EOJ           QUIT ON EOJ OR ERROR       GP10127
         LA    R3,PUNCHCRD         SAVE BUFFER ADDRESS          GP10127
         MVC   LINE+21(80),PUNCHCRD   ECHO TO PRINTER           GP10127
         BAL   R14,PRINTOUT        PRINT CARD IMAGE             GP10127
         CLI   0(R3),C'*'          COMMENTS CARD ?              GP10127
         BE    REGET                 YES; GET NEXT              GP10127
         CLC   1(71,R3),0(R3)      ALL BLANK OR SOMETHING ?     GP10127
         BE    REGET                 YES; GET NEXT              GP10127
         OI    SWX,FGHAVIN         SYSIN CONTAINS DATA          GP10127
         CLI   0(R3),C'+'          ADD TO SIZE ?                GP10127
         BNE   REQNTADD              NO                         GP10127
         OI    SWX,FGEXP2          SET ADD OPTION               GP10127
         B     REQEXPND              OF EXPAND
REQNTADD CLI   0(R3),C'<'          IS USER CONFUSED?            GP10127
         BE    REQEXPND              YES; SET TO EXPAND         GP10127
         CLI   0(R3),C'>'          DOES COL 1 = '>' FOR EXPAND?
         BNE   REQASIS             NO, SKIP SWITCH
REQEXPND OI    SWX,FGEXP1          YES - SET SWITCH
         LA    R3,1(,R3)           AND POINT TO COLUMN 2
REQASIS  BAL   R14,PARSER          EXTRACT THE FIELDS           GP10127
         MVC   PUNCHCRD,PUNCHCRD-1     CLEAR INPUT FOR REUSE    GP10127
         MVI   OBJID,X'02'   OBJECT IDENTIFIER                  GP10127
         SPACE 1
*---------------------------------------------------------------------*
*   PROCESS LOAD MODULE; RESTART POINT FOR *ALL* CSECT OPTION         *
*---------------------------------------------------------------------*
RESTART  BLDL  SYSLIB,BLDLL                                     GP10127
         LTR   R15,R15             BLDL SUCCESSFUL?             GP10127
         BNZ   MSGNOBLD            NO - MEMBER NOT FOUND        GP10127
         TM    OPTFLAGS,FGMSG      DATA ALREADY SHOWN?          GP10127
         BNZ   REPOINT             YES                          GP10127
         OI    OPTFLAGS,FGMSG      DATA ALREADY SHOWN           GP10127
 PRTDATA ' Module',(PDS2NAME,PAD),' Flags',(PDS2ATR,HEX,PAD),' Size',(P*
               DS2STOR,HEX,PAD),' TTR',(PDS2TTRP,HEX,PAD)       GP10127
         CLI   RETCODE+3,8         FATAL ERRORS?                GP10127
         BNL   NEXTCARD              YES; SCAN FOR ERRORS ONLY  GP10127
         MVC   @ENTRY+1(3),PDS2EPA     SAVE ENTRY ADDRESS       GP10127
REPOINT  BAL   R14,POINT           YES, POINT TO MEMBER
         SPACE 1
         LA    R5,NXTESD           R5 WILL POINT HERE OR AT EDITNOTR
         L     R10,@CESDS
         USING ESDDSECT,R10                                     GP10127
         SPACE 1
         TM    PDS2ATR2,PDS2NREP   MODULE MARKED NOT-EDITABLE?  GP10127
         BZ    READCESD            NO - BRANCH
*---------------------------------------------------------------------*
*   PROCESS MODULE WITH NOT-EDITABLE ATTRIBUTE                        *
*---------------------------------------------------------------------*
         XC    ESDDSECT(ESDSIZE),ESDDSECT   CLEAR ENTRY         GP10127
         MVC   ESDNAME,PDS2NAME    MOVE MEMBER NAME             GP10127
         MVC   ESDLEN,PDS2STOR     GET SIZE OF MODULE           GP10127
         NI    OPTFLAGS,255-FGALL    ONLY ONE SECTION TO DO     GP10127
         BAL   R5,CSEFOUND
EDITNOTR MVC   ESDSIZE(240-ESDSIZE,R10),0(R10)  PROPAGATE???    GP10127
         TM    PDS2ATR1,PDS21BLK   NO RLD ITEMS? ONE BLOCK OF TEXT?
         BZ    *+8
         MVI   RECID,X'0C'
         LA    R10,260(,R10)
         ST    R10,@TEXTN
         MVC   TEXLEN,PDS2FTBL      MOVE LEN OF FIRST BLK TXT   GP10127
         OI    SWX,FGISNE         CALLED FROM NOT EDITABLE MOD  GP10127
         MVC   PDS2TTRP,PDS2TTRT  MOVE TTR OF FIRST TXT         GP10127
         BAL   R14,POINT
         LA    R4,ZEROES+2        POINT TO ZERO ORIGIN          GP10127
         SR    R7,R7
         B     READTXT
         SPACE 1
*---------------------------------------------------------------------*
*   READ CESD INFORMATION AND BUILD ESD TABLE BEFORE TEXT             *
*---------------------------------------------------------------------*
READCESD L     R10,@CESDN          GET HWM                      GP10127
READCES2 BAL   R14,READ            READ A BLOCK                 GP10127
         CLI   0(R10),X'20'        IS IT A CESD RECORD?
         BE    YESCESD             YES - BRANCH
         TM    0(R10),X'03'        IS IT CONTROL (01) OR RLD (02 06 0E)
         BM    CCWRED              YES - BRANCH
         B     READCES2            NEITHER - GO READ ANOTHER    GP10127
         SPACE 1
YESCESD  LH    R9,4(,R10)          R9  =  ESDID OF 1ST ITEM     GP10127
         LH    R7,6(,R10)          R7 = NO OF BYTES OF ESD DATA
         LR    R15,R7              COPY                         GP10127
         BCTR  R15,0               LENGTH MINUS 1 FOR EX
         EX    R15,EXMVCESD        MOVE ESD DATA TO WHERE R10 POINTS
         LA    R1,1(R15,R10)       NEW END                      GP10127
         ST    R1,@CESDN           SAVE ADDRESS OF END OF ESD TABLE
         C     R10,@TXTND          HAS TABLE OVERLAYED BLK AREA?
         BH    ESDOFLOW            YES - TOO BIG TO HANDLE - BRANCH
         SPACE 1
*---------------------------------------------------------------------*
*   CHECK FOR DESIRED CSECT NAME, AND FIND RELATED ENTRIES            *
*---------------------------------------------------------------------*
ESDCLC   CLC   SECTNAME,ESDNAME    DOES CSECT = SYMBOL IN ESD?
         BE    CSEFOUND            YES - BRANCH
         TM    SWX,FGHAVSD         ESD ENTRY ALREADY FOUND?     GP10127
         BO    ESDARF              YES - BRANCH
         TM    OPTFLAGS,FG1ST+FGALL  WANT FIRST OR ALL ?        GP10127
         BZ    NXTESD              NO                           GP10127
         TM    ESDTYPE,X'90'       UNRESOLVED OR SEGTAB?        GP10127
         BNZ   NXTESD              YES; IGNORE                  GP10127
         TM    ESDTYPE,X'07'       NULL ENTRY?                  GP10127
         BO    NXTESD              YES; IGNORE                  GP10127
         TM    ESDTYPE,X'03'       LR ?                         GP10127
         BO    NXTESD              YES; IGNORE                  GP10127
         TM    OPTFLAGS,FG1ST      WANT FIRST ?                 GP10127
         BNZ   USETHIS             YES; USE IT                  GP10127
         CH    R9,WANTID           FOUND RESTART POINT?         GP10127
         BNL   USETHIS             YES; USE IT                  GP10127
         SPACE 1
NXTESD   LA    R9,1(,R9)           ADD 1 TO ESDID
         LA    R10,ESDSIZE(,R10)   POINT TO NEXT ESD ENTRY IN RECORD
         SH    R7,=AL2(ESDSIZE)    GET REMAINING LENGTH
         BP    ESDCLC                HAVE MORE
         B     READCESD            GO READ ANOTHER RECORD
EXMVCESD MVC   ESDNAME(0),8(R10)   MOVE ESD ENTRIES TO R10
         SPACE 1
ESDARF   CH    R9,WANTID           ESD EQUAL?
         BNE   ESDNEQ              NO - BRANCH
         MVI   RECID,X'00'                                      GP10127
         B     ESDEQL
         SPACE 1
ESDNEQ   TM    RECID,X'FF'
         BOR   R5                                               GP10127
         TM    ESDTYPE,X'03'
         BNOR  R5     Z OR MIXED   BR IF ANYTHING BUT LR OR NULL
         TM    ESDTYPE,X'07'       NULL?
         BOR   R5                  YES - BRANCH
         CLC   WANTID,ESDID        IS ESDID SAME AS LR ID?
         BNER  R5                    NO - BRANCH
         MVC   WDWORK+1(3),ESDADDR   YES - SAVE LR ADDRESS
         MVI   WDWORK,X'00'                                     GP10127
         L     R6,WDWORK
         S     R6,ORIGIN
         ST    R6,WDWORK           SAVE ABSOLUTE LR ADDRESS
         MVI   WDWORK,X'01'        LR IS 01 IN OBJ, 03 IN LOAD MODULE
         MVC   ESDID,CURRID
         B     BUILDESD
         SPACE 1
A001D6   CH    R9,ESDID
         BL    A00208
         LH    R6,ESDID
         STH   R6,WANTID
         LR    R9,R6
         BCTR  R6,0
         SLL   R6,4
         A     R6,@CESDS
         SR    R10,R6
         AR    R7,R10
         LR    R10,R6
ESDEQL   MVC   SECTNAME,ESDNAME
         B     MATCHID
A00208   MVC   WANTID,ESDID
         MVI   RECID,X'FF'
         BR    R5
         SPACE 1
*---------------------------------------------------------------------*
*   CURRENT ENTRY (R10) HAS DESIRED NAME; TEST IF USABLE              *
*---------------------------------------------------------------------*
USETHIS  MVC   SECTNAME,ESDNAME    ADOPT THIS SECTION           GP10127
         NI    OPTFLAGS,255-FG1ST  RESET FIRST SECTION REQUEST  GP10127
CSEFOUND TM    ESDTYPE,X'07'       NULL ENTRY?
         BOR   R5                  YES - IGNORE
         TM    SWX,FGHAVSD         BEEN HERE ALREADY?           GP10127
         BO    ESDPASS2            YES - BRANCH
         MVI   CSESIZ,X'00'
         MVC   CSESIZ+1(3),ESDLEN  SAVE CSECT SIZE
         TM    SWX,FGEXP1  +EXP2   TO BE EXPANDED?
         BZ    NOEXPN              NO - BRANCH
         L     R6,EXPVAL           GET SIZE OR INCREMENT        GP10127
         TM    SWX,FGEXP2          INCREMENT ?                  GP10127
         BZ    DOEXPSIZ              NO; NEW SIZE               GP10127
         A     R6,CSESIZ             YES; ADD TO SIZE           GP10127
DOEXPSIZ C     R6,CSESIZ           LARGER THAN BEFORE ?         GP10127
         BNH   MSGBDEXP              NO; INVALID REQUEST        GP10127
         C     R6,=X'00FFFFFF'     VALID LENGTH ?               GP10127
         BH    MSGBDEXP              NO; INVALID REQUEST        GP10127
         STCM  R6,7,ESDLEN         UPDATE LENGTH                GP10127
         S     R6,CSESIZ           R6 = LENGTH OF EXTENSION     GP10127
         BNP   MSGBDEXP            BAD NUMBER ON EXTEND CARD    GP10127
         STH   R6,EXPDIF
 PRTDATA '    expanding',(ESDNAME,PAD),'from',(CSESIZ+2,3,HEX,PAD),'to'*
               ,(ESDLEN,HEX,PADL)                               GP10127
         SPACE 1
NOEXPN   STH   R9,WANTID
 PRTDATA '  CSect',(ESDNAME,PAD),' Type',(ESDTYPE,HEX,PAD),' Addr',(ESD*
               ADDR,HEX,PAD),' Seg',(ESDFLAG,HEX,PAD),' Len',(ESDLEN,HE*
               X,PADL)                                          GP10127
         MVC   SAVTYP,ESDTYPE      SAVE ESD TYPE (SD,LR,PC,ER...)
         OI    SWX,FGHAVSD                                      GP10127
MATCHID  MVC   ORIGIN+1(3),ESDADDR   SAVE START ADDRESS
         MVC   WDWORK,ZEROES
         MVN   WDWORK(1),ESDTYPE
         MVC   NUMSEG,ESDFLAG      SAVE SEGMENT NUMBER
         TM    ESDTYPE,X'0F'       SD IS 00, ALL OTHERS MIXED
         BM    MSGNOESD      NAME NOT SECTION DEFINITION        GP10127
         SPACE 1
BUILDESD OI    SWX,FGHVESD                                      GP10127
         TM    PDS2ATR1,PDS2OVLY  OVERLAY?                      GP91137
         BNZ   NOSEGMNT      YES; KILL SEGMENT                  GP91137
         CLI   ESDTYPE,X'00'  SD ?                              GP91137
         BE    CKSEGMNT      YES; CHECK MODE                    GP91137
         CLI   ESDTYPE,X'05'  COMMON ?                          GP91137
         BE    CKSEGMNT      YES; CHECK MODE                    GP91137
         CLI   ESDTYPE,X'04'  PRIVATE SECTION ?                 GP91137
         BNE   NOSEGMNT      NO; NO SEGMENT OR MODE             GP91137
CKSEGMNT TM    ESDFLAG,X'06'  AMODE/RMODE OTHER THAN 24/24 ?    GP91137
         BNZ   DOSEGMNT      YES; LEAVE MODES                   GP91137
NOSEGMNT MVI   ESDFLAG,X'40'       BLANK SEGMENT NUMBER         GP91137
DOSEGMNT L     R6,PTRTESD                                       GP10127
         TM    ESDTYPE,X'03'        IS TYPE LR?
         BO    *+8                 YES - BRANCH
         OI    SWX,FGHVEID         EID FOR FIRST ENTRY          GP10127
         CLI   ESDTYPE,X'02' EXTERNAL REFERENCE ?               GP10127
         BNE   *+10                NO - BRANCH                  GP10127
         MVC   ESDADDR,ZEROES   YES; RESET OFFSET               GP10127
         MVC   0(ESDSIZE,R6),ESDNAME   MOVE ESD ENTRY TO ESD CARD
         MVC   8(4,R6),WDWORK      MOVE ESD TYPE AND ADDRESS
         MVC   ESDID,CURRID
         OI    ESDFLAG,X'FF'
         LA    R6,ESDSIZE(,R6)     POINT TO NEXT AVAIL ON ESD CARD
         LH    R4,CNTESD
         LA    R4,1(,R4)            ADD 1 TO ESD'S THIS CARD
         STH   R4,CNTESD
         LA    R0,OBJSEQ-16        LAST POSSIBLE ESD            GP10127
         CR    R6,R0               ESD CARD NOW FULL?           GP10127
         BH    PUNESD              YES - BRANCH                 GP10127
         ST    R6,PTRTESD          SAVE PARTIAL CARD POSITION   GP10127
         TM    SAVTYP,X'05'        COMMON
         BO    PUNEND              YES, BRANCH
         BR    R5
         EJECT
*---------------------------------------------------------------------*
*              * * *   PUNCH AN ESD CARD   * * *                      *
*---------------------------------------------------------------------*
PUNESD   MVC   OBJTYPE,=C'ESD'                                  GP10127
         TM    SWX,FGHVEID         EID SET ?                    GP10127
         BZ    *+10
         MVC   OBJTEID,CNTESX                                   GP10127
         NI    SWX,255-FGHVEID-FGHVESD      EID AND ESD DONE    GP10127
         LH    R2,CNTESD           GET NO. OF ESD'S THIS CARD
         SLL   R2,4                MULTIPLY BY 16 TO GET LENGTH
         STCM  R2,3,OBJTLEN        STORE IN COLUMNS 11-12       GP10127
         SRL   R2,4                GET NO. OF ESD'S THIS CARD
         LH    R3,CNTESX
         LA    R6,PUNCHCRD                                      GP10127
PUNES1   CLI   24(R6),X'01'        LD TYPE?
         BE    *+8                 YES - DON'T COUNT - BRANCH
         LA    R3,1(,R3)           COUNT NO OF ESD'S EXCEPT LD'S
         LA    R6,16(,R6)
         BCT   R2,PUNES1
         STH   R3,CNTESX           SAVE NO. OF ESD'S EXCEPT LD'S
         STH   R2,CNTESD           ZERO OUT NO. OF ESD'S THIS CARD
         LA    R0,OBJTEXT          RESTORE TO CARD START        GP10127
         ST    R0,PTRTESD          SAVE PARTIAL CARD POSITION   GP10127
         BAL   R14,PUNCHOUT                                     GP10127
         BR    R5
         EJECT
*---------------------------------------------------------------------*
*   PARSE CONTROL CARD       R3 START (PUNCHCRD [+1])                 *
*---------------------------------------------------------------------*
PARSER   STM   R0,R15,SUBSAVER                                  GP10127
         LA    R4,OBJSEQ-1     END OF TEXT (NO CC COLUMN)       GP10127
         BAL   R14,GETFIELD  GET MODULE NAME                    GP10127
           B   MSGNOMEM        NO MEMBER NAME                   GP10127
         CH    R15,=AL2(L'PDS2NAME)    VALID LENGTH ?           GP10127
         BH    MSGTOBIG        NAME TOO LONG                    GP10127
         MVC   PDS2NAME,BLANKS     CLEAR                        GP10127
         BCTR  R15,0                                            GP10127
         EX    R15,EXMVCNAM  MOVE MEMBER NAME                   GP10127
         MVC   SECTNAME,PDS2NAME  CSECT DEFAULTS TO MEMBER      GP10127
         LA    R3,1(,R1)     SET NEXT SCAN START                GP10127
         SPACE 1
         BAL   R14,GETFIELD  GET CSECT NAME                     GP10127
           B   PARSNOCS        NO CSECT NAME                    GP10127
         CH    R15,=AL2(L'SECTNAME)    VALID LENGTH ?           GP10127
         BH    MSGTOBIG        NAME TOO LONG                    GP10127
         MVC   SECTNAME,BLANKS     CLEAR                        GP10127
         BCTR  R15,0                                            GP10127
         EX    R15,EXMVCSEC  MOVE MEMBER NAME                   GP10127
         CLC   =C'! ',SECTNAME    PROCESS FIRST SECTION?        GP10127
         BNE   *+8                  NO                          GP10127
         OI    OPTFLAGS,FG1ST       YES; PROCESS FIRST ONE      GP10127
         CLC   =C'* ',SECTNAME    PROCESS ALL SECTIONS ?        GP10127
         BE    PARSETAL             NO                          GP10127
         CLC   =C'*ALL* ',SECTNAME  PROCESS ALL SECTIONS ?      GP10127
         BNE   *+8                  NO                          GP10127
PARSETAL OI    OPTFLAGS,FGALL       YES; PROCESS ALL            GP10127
         LA    R3,1(,R1)     SET NEXT SCAN START                GP10127
         SPACE 1
         TM    SWX,FGEXP1    NEED ANOTHER FIELD ?               GP10127
         BZ    PARSEXIT      NO; RETURN                         GP10127
         TM    OPTFLAGS,FGALL     REQUESTED ALL CSECTS?         GP10127
         BNZ   MSGEXCON           YES; UNSUPPORTED              GP10127
PARSENUM BAL   R14,GETFIELD  GET EXPAND SIZE                    GP10127
           B   MSGBDNUM        NO SIZE                          GP10127
         MVI   TRTADHOC,4                                       GP10127
         MVC   TRTADHOC+1(255),TRTADHOC                         GP10127
         XC    TRTADHOC+C'0'(10),TRTADHOC+C'0'   DIGITS         GP10127
         CLI   0(R3),C'+'    INCREMENT ?                        GP10127
         BNE   PARSESIZ                                         GP10127
         OI    SWX,FGEXP2    SET INCREMENT                      GP10127
         LA    R3,1(,R3)     SKIP OVER                          GP10127
         SH    R15,=H'1'     ADJUST LENGTH                      GP10127
         BNP   PARSENUM        SPACE AFTER + ?                  GP10127
PARSESIZ CLI   0(R3),C'X'    HEX FORMAT?                        GP10127
         BE    PARSEHEX        YES                              GP10127
         CH    R15,=AL2(L'DB)  VALID LENGTH ?                   GP10127
         BH    MSGBDNUM      NO                                 GP10127
         BCTR  R15,0                                            GP10127
         EX    R15,EXVALNUM  VALIDATE DIGITS ONLY               GP10127
         BNZ   MSGBDNUM        NOT VALID                        GP10127
         EX    R15,EXPAKINT  PACK INPUT                         GP10127
         CVB   R0,DB         MAKE BINARY                        GP10127
         ST    R0,EXPVAL     SAVE IT                            GP10127
PARSEVAL L     R0,EXPVAL     GET NEW SIZE OR INCREMENT          GP10127
         LTR   R0,R0                                            GP10127
         BNP   MSGBDNUM      NEGATIVE IS INVALID                GP10127
         C     R0,=X'00FFFFFF'    VALID SIZE?                   GP10127
         BH    MSGBDNUM      NO                                 GP10127
         B     PARSEXIT      AND RETURN                         GP10127
         SPACE 1
PARSEHEX BCTR  R1,0          LAST BYTE OF TEXT                  GP10127
         SH    R15,=H'4'     LENGTH OF HEX - 1                  GP10127
         BM    MSGBDNUM        INVALID                          GP10127
         CLC   1(1,R3),0(R1) DOES FIRST EQUAL LAST ('  ") ?     GP10127
         BNE   MSGBDNUM      NO                                 GP10127
         XC    TRTADHOC+C'A'(6),TRTADHOC+C'A'  ALLOW A-F        GP10127
         XC    TRTADHOC+X'81'(6),TRTADHOC+X'81'  ALLOW a-f      GP10127
         LA    R3,2(,R3)     SPACE TO HEX PROPER                GP10127
         EX    R15,EXVALNUM  VALID HEX ?                        GP10127
         BNZ   MSGBDNUM        NO                               GP10127
         EX    R15,EXHEXMSK  MASK WITH 1F                       GP10127
         EX    R15,EXHEXTRN  CONVERT TO HEX BYTES               GP10127
         LA    R15,1(,R15)   SPARE BYTE                         GP10127
         EX    R15,EXHEXPAK  MAKE HEX VALUE                     GP10127
         MVC   EXPVAL,DB     MOVE FROM SAFE PLACE               GP10127
         B     PARSEVAL      AND VALIDATE                       GP10127
         SPACE 1
PARSNOCS TM    SWX,FGEXP1    NEED ANOTHER FIELD ?               GP10127
         BNZ   MSGNOSEC      YES; CSECT MISSING                 GP10127
PARSEXIT LM    R0,R15,SUBSAVER    RESTORE                       GP10127
         BR    R14           RETURN                             GP10127
         SPACE 1
*   GET NEXT FIELD; R3 CURRENT; R4 CARD END                     GP10127
*     RETURN R15 LEN  R3 START FIELD                            GP10127
GETFIELD LR    R15,R4        END                                GP10127
         SR    R15,R3        LENGTH                             GP10127
         SH    R15,=H'1'     EXECUTE LENGTH                     GP10127
         BMR   R14           NO ADDITIONAL FIELD                GP10127
*WHY     XC    TRTADHOC,TRTADHOC   CLEAR WORK FIELD             GP10127
*NOT?    XC    TRTADHOC,TRTBLANK   STOP ON ALL BUT BLANK        GP10127
         MVI   TRTADHOC,4    STOP                               GP10127
         MVC   TRTADHOC+1(255),TRTADHOC   PROPAGATE             GP10127
         MVI   TRTADHOC+C' ',0     STOP ON BLANK                GP10127
         LA    R1,1(,R4)     END IF NO MATCH                    GP10127
         EX    R15,EXTRTNBL  FIND START OF OPERAND              GP10127
         BZR   R14           NO MORE                            GP10127
         LR    R3,R1         NEW FIELD START                    GP10127
         LR    R15,R4        END                                GP10127
         SR    R15,R3        LENGTH                             GP10127
         SH    R15,=H'1'     EXECUTE LENGTH                     GP10127
         BMR   R14           NO ADDITIONAL FIELD                GP10127
         LA    R1,1(,R4)     END IF NO MATCH                    GP10127
         EX    R15,EXTRTBLK  FIND START OF OPERAND              GP10127
         LR    R15,R1        COPY END                           GP10127
         SR    R15,R3        LESS START                         GP10127
         BNPR  R14           NO OPERAND                         GP10127
         B     4(,R14)       RETURN LEN IN R15; START IN R3     GP10127
EXTRTNBL TRT   0(0,R3),TRTADHOC                                 GP10127
EXTRTBLK TRT   0(0,R3),TRTBLANK                                 GP10127
EXMVCNAM MVC   PDS2NAME(0),0(R3)  MOVE NAME                     GP10127
EXMVCSEC MVC   SECTNAME(0),0(R3)  MOVE NAME                     GP10127
EXVALNUM TRT   0(0,R3),TRTADHOC   DIGITS ONLY ?                 GP10127
EXPAKINT PACK  DB,0(0,R3)    PACK INPUT                         GP10127
EXHEXMSK NC    0(0,R3),=8X'1F'    FIX ZONES                     GP10127
EXHEXTRN TR    0(0,R3),=X'001011121314150000000000000000000001020304050*
               6070809'                                         GP10127
EXHEXPAK PACK  DB(5),0(0,R3)      HEX NYBBLES                   GP10127
         EJECT
*---------------------------------------------------------------------*
*   READ AND CHECK A BUFFER - NO OVERLAPPED I/O                       *
*                10      RECORD AREA ADDRESS                          *
*                14      RETURN ADDRESS                         GP10127
*        RETURN:  8      BLOCK SIZE                             GP10127
*---------------------------------------------------------------------*
READ     ST    R14,SAVER14                                      GP10127
         XC    0(256,R10),0(R10)       CLEAR FOR DEBUGGING      GP10127
         READ  READECB,SF,SYSLIB,(R10),'S'   READ FULL BLOCK    GP10127
         SPACE 1
         CHECK READECB
         LH    R8,DCBLRECL-IHADCB+SYSLIB                        GP10127
         TM    RUNFLAGS,FGDEBUG                                 GP10127
         BZ    READEXIT                                         GP10127
READEXIT L     R14,SAVER14                                      GP10127
         BR    R14                 RETURN                       GP10127
         SPACE 2
*---------------------------------------------------------------------*
*   POINT TO REQUESTED TTR.                                           *
*     NOTE THAT ORIGINAL POINT MARO WAS REPLACED BY FIND IN ORDER     *
*     TO SUPPORT CONCATENATED PDS INPUT.                              *
*---------------------------------------------------------------------*
POINT    ST    R14,SAVER14                                      GP10127
         FIND  SYSLIB,PDS2TTRP,C  POINT TO TTR; SUPPORT CONCAT  GP10127
POINTEX  L     R14,SAVER14                                      GP10127
         BR    R14                                              GP10127
         SPACE 2
*---------------------------------------------------------------------*
*   POSITION TO TTR FOR OVERLAY SEGMENT                               *
*---------------------------------------------------------------------*
POINTOVL MVC   PDS2TTRP,PDS2TTRN      MOVE SCATTER/OVLY TTR     GP10127
         BAL   R14,POINT
         BAL   R14,READ
         SR    R1,R1
         IC    R1,NUMSEG
         BCTR  R1,0
         SLL   R1,2
         AR    R1,R10
         MVC   PDS2TTRP,0(R1)                                   GP10127
         BAL   R14,POINT
         BAL   R14,READ
         BR    R4
         EJECT
*---------------------------------------------------------------------*
*    BUFFER CONTAINS CONTROL OR RLD BLOCK (ESD AND IDR DONE EARLIER)  *
*---------------------------------------------------------------------*
         SPACE 1
CCWRED   TM    SWX,FGHAVSD         CSECT FOUND YET?             GP10127
         BZ    MSGNOTXT            NO - TEXT FOR CSECT NOT FOUND
         ST    R10,@TEXTN          SAVE RECORD ADDRESS
         MVC   RECID,0(R10)        SAVE RECORD TYPE
         S     R10,@CESDS           GET LENGTH OF ESD TABLE
         LR    R7,R10              INTO R7
         LA    R9,1                SET ESDID = 1
         L     R10,@CESDS
         B     ESDCLC
         SPACE 1
ESDPASS2 L     R10,@TEXTN
         TM    PDS2ATR1,PDS2OVLY   OVERLAY?                     GP10127
         BZ    A00582              NO - BRANCH
         CLI   NUMSEG,X'01'        FIRST SEGMENT?
         BE    A00582              YES - BRANCH
         BAL   R4,POINTOVL
A00582   LH    R4,6(,R10)          GET LEN OF POSSIBLE RLD DATA
         LH    R6,4(,R10)          GET LEN OF CONTROL INFO
         SRL   R6,2                DIVIDE BY 4 = NO. OF ENTRIES
         LA    R4,16(R4,R10)       POINT TO CONTROL DATA
         SR    R7,R7
A00594   TM    SWX,FGISNE                                       GP10127
         BZ    A005A6
         NI    SWX,255-FGISNE                                   GP10127
         MVC   WANTID,0(R4)
A005A6   CLC   WANTID,0(R4)
         BE    A005F0
         TM    SWY40,X'40'
         BO    PUNEND
         AH    R7,2(,R4)
         LA    R4,4(,R4)
         BCT   R6,A00594
         TM    0(R10),X'0C'        END OF SEGMENT OR MODULE?
         BC    5,PUNEND            YES - BRANCH IF ONES OR MIXED
         BAL   R14,READ            SKIP TEXT RECORD
READCNTL BAL   R14,READ            GET NEXT CONTROL RECORD
         TM    0(R10),X'01'        CCW RECORD?
         BO    A00582              YES - BRANCH
         TM    0(R10),X'0C'        END OF SEGMENT OR MODULE?
         BC    5,PUNEND            YES - BRANCH IF ONES OR MIXED
         B     READCNTL            NO - GO READ ANOTHER
A005F0   OI    SWY40,X'40'
         MVC   RECID,0(R10)
*OLD*    LH    R8,14(,R10)        GET LEN OF NEXT RECORD FROM CCW
         MVC   TEXLEN,2(R4)
         CLC   14(2,R10),2(R4)
         BE    READTXT
         MVC   TEXLEN,CSESIZ+2
         OI    SWY20,X'20'
READTXT  LA    R10,260(,R10)       POINT TO AREA FOR NEXT READ
A0061C   BAL   R14,READ
         TM    SWY20,X'20'
         BO    A00630
         ST    R10,PTRTEX
         B     A00638
A00630   AR    R7,R10
         ST    R7,PTRTEX
         SR    R7,R7
A00638   TM    RECID,X'0C'         EOS? EOM?
         BZ    A00652
         NI    RECID,X'FE'
         B     PUNTX1
A00652   AR    R10,R8                                           GP10127
         LA    R10,4(,R10)
         N     R10,=A(0-4)   TRUNCATE TO WORD BOUNDARY          GP10127
         ST    R10,RLDPOS
         LA    R1,260(,R10)
         C     R1,@RLDEND                                       GP10127
         BH    RLDOFLOW
         BAL   R14,READ
         TM    0(R10),X'02'        RLD?
         BZ    PUNTX0              NO - BRANCH
         L     R2,ORIGIN
         AH    R2,2(,R4)
         ST    R2,PTR480
A00690   LH    R3,6(,R10)          GET LEN OF RLD DATA
         LA    R2,16(,R10)         POINT TO RLD DATA
A00698   TM    SWY20,X'20'
         BZ    A006B4
         CLC   5(3,R2),ORIGIN+1
         BL    A007A6
         CLC   5(3,R2),PTR480+1
         BH    A007A6
A006B4   MVC   DBWORK+1(3),5(R2)
         OI    SWY08,X'08'
         NI    DBWORK,X'00'
         L     R1,DBWORK
         S     R1,ORIGIN
         ST    R1,DBWORK
         MVC   5(3,R2),DBWORK+1
         AR    R1,R7
         A     R1,PTRTEX
         TM    4(R2),X'0C'         4-BYTE?
         BO    A00716              YES - BRANCH
         TM    4(R2),X'08'         3-BYTE?
         BZ    MSGADCON            NO - ERROR - ADCON NOT 3 OR 4
         TM    SWY80,X'80'
         BO    A006F6
         BAL   R9,A007BE
A006F6   MVC   DBWORK+1(3),0(R1)
         NI    DBWORK,X'00'
         L     R5,DBWORK
         S     R5,PTR476
         ST    R5,DBWORK
         MVC   0(3,R1),DBWORK+1
         B     A00752
A00716   MVC   DBWORK,0(R1)
         TM    SWY80,X'80'
         BO    A00728
         BAL   R9,A007BE
A00728   TM    4(R2),X'10'         V-TYPE ADDRESS?
         BO    A0074C              YES - BRANCH
         MVC   DBWORK,0(R1)
         L     R5,DBWORK
         S     R5,PTR476
         ST    R5,DBWORK
         MVC   0(4,R1),DBWORK
         B     A00752
A0074C   MVC   0(4,R1),ZEROES
A00752   TM    4(R2),X'01'         DOES NEXT RLD HAVE SAME REL, POS?
         BZ    A00762              NO - BRANCH
         OI    SWY80,X'80'
         B     A0076E
A00762   NI    SWY80,X'7F'
         LA    R2,4(,R2)           POINT PAST REL, POS
         SH    R3,H4               DECREMENT LENGTH
A0076E   LA    R2,4(,R2)           POINT TO NEXT FLAG, ADDRESS
         SH    R3,H4               DECREMENT LENGTH
         LTR   R3,R3               END OF RLD DATA?
         BP    A00698              NO - BRANCH
         TM    0(R10),X'01'        CCW DATA ALSO PRESENT?
         BO    A0079E              YES - BRANCH
         TM    0(R10),X'0C'        EOS? EOM?
         BC    5,A0079E            YES - BRANCH IF ONES OR MIXED
         AR    R10,R8                                           GP10127
         OI    SWY10,X'10'
         BAL   R14,READ
         TM    0(R10),X'02'        RLD?
         BO    A00690              YES - BRANCH
A0079E   ST    R10,@TEXTN
         B     PUNTX0
A007A6   OI    0(R2),X'FF'
A007AA   TM    4(R2),X'01'
         BZ    A00752
         LA    R2,4(,R2)
         SH    R3,H4
         B     A007AA
A007BE   STM   R7,R10,STM710
         LH    R4,0(,R2)
         BCTR  R4,0
         SLL   R4,4
         A     R4,@CESDS
         MVC   PTR476+1(3),9(R4)
         TM    12(R4),X'FF'
         BO    A00836
         OI    SWX,FGHVEID                                      GP10127
         LR    R10,R4
         MVC   WDWORK,ZEROES                                    GP10127
         TM    8(R4),X'05'
         BNO   A00804               BRANCH IF ZEROS OR MIXED
         CLC   0(8,R4),BLANKS        SPACES?                    GP10127
         BNE   A00804
         MVI   WDWORK,X'05'
         B     A00816
A00804   MVI   WDWORK,2                                         GP10127
         XC    ESDLEN,ESDLEN                                    GP10127
A00816   LH    R7,CURRID
         LA    R7,1(,R7)
         STH   R7,CURRID
         STH   R7,0(,R2)
         STM   R1,R4,STM14
         BAL   R5,BUILDESD
         LM    R1,R4,STM14
         B     A0083C
A00836   MVC   0(2,R2),14(R4)
A0083C   MVC   2(2,R2),=H'1'                                    GP10127
         LM    R7,R10,STM710
         BR    R9
         SPACE 1
*---------------------------------------------------------------------*
*              * * *   PUNCH TXT CARDS   * * *                        *
*---------------------------------------------------------------------*
PUNTX0   MVC   RECID,0(R10)
PUNTX1   TM    SWX,FGHVESD                                      GP10127
         BZ    PUNTX2
         BAL   R5,PUNESD
PUNTX2   L     R7,PTRTEX
         LH    R5,TEXLEN           GET TEXT LENGTH
         LTR   R5,R5
         BM    MSGBDNUM
COPYTEXT MVC   OBJTYPE,=C'TXT'                                  GP10127
         MVC   OBJTADD,ADDRES+1     MOVE ADDRESS TO COL 6-8     GP10127
         MVC   OBJTEID,=H'1'        MOVE ESDID TO COL 15-16     GP10127
         CH    R5,=AL2(L'OBJTEXT)                               GP10127
         BNH   COPYSHRT
         MVC   OBJTLEN,=AL2(L'OBJTEXT)   LENGTH 56 TO COL 11-12 GP10127
         MVC   OBJTEXT,0(R7)        MOVE TEXT TO COL 17-72      GP10127
         L     R6,ADDRES
         LA    R6,L'OBJTEXT(,R6)                                GP10127
         ST    R6,ADDRES
         BAL   R14,PUNCHOUT                                     GP10127
         OI    OPTFLAGS,FGSOME    SHOW SOME OUTPUT PRODUCED     GP10127
         TM    SWY,FGDOFILL     DOING FILL ?                    GP10127
         BNZ   MODDECR            YES; KEEP SOURCE=OBJTEXT      GP10127
         LA    R7,L'OBJTEXT(,R7)                                GP10127
MODDECR  SH    R5,=AL2(L'OBJTEXT)                               GP10127
         B     COPYTEXT
EXMVCTXT MVC   OBJTEXT(0),0(R7)                                 GP10127
         SPACE 1
COPYSHRT LTR   R5,R5
         BNP   COPYDONE         BRANCH IF ZERO OR MINUS
         L     R6,ADDRES
         AR    R6,R5
         ST    R6,ADDRES
         STCM  R5,3,OBJTLEN                                     GP10127
         BCTR  R5,0
         EX    R5,EXMVCTXT                                      GP10127
         BAL   R14,PUNCHOUT                                     GP10127
COPYDONE L     R10,RLDPOS
         TM    SWY08,X'08'
         BZ    A00A52
         NI    SWY80,X'F7'
         SR    R7,R7
         LA    R6,OBJSEQ-4                                      GP10127
         MVC   OBJTYPE,=C'RLD'                                  GP10127
         LA    R4,OBJTEXT                                       GP10127
A00924   LR    R3,R10
         LH    R5,6(,R3)
         LA    R5,16(R5,R3)
         LA    R3,16(,R3)
A00932   TM    0(R3),X'FF'
         BO    A00A0C
         OI    SWY01,X'01'
         MVC   RLDADR,0(R3)
         MVC   0(8,R4),0(R3)
         LA    R3,4(,R3)
         LA    R4,8(,R4)
         LA    R7,8(,R7)
         TM    0(R3),X'01'
         BO    A00972
A0095E   LA    R3,4(,R3)
         CR    R4,R6
         BNL   PUNCHRL
         CR    R3,R5
         BE    A00A2A
         B     A00932
A00972   CR    R4,R6
         BH    PUNCHRLD
         MVC   0(4,R4),4(R3)
         LA    R3,4(,R3)
         LA    R4,4(,R4)
         LA    R7,4(,R7)
         TM    0(R3),X'01'
         BO    A00972
         B     A0095E
PUNCHRLD TM    0(R3),X'01'
         BZ    PUNCHRL
         OI    SWY80,X'80'
PUNCHRL  NI    SWY01,X'FE'
         STCM  R7,3,OBJTLEN                                     GP10127
         SH    R4,H4
         NI    0(R4),X'FE'
         BAL   R14,PUNCHOUT                                     GP10127
         MVC   OBJTYPE,=C'RLD'                                  GP10127
         LA    R4,OBJTEXT                                       GP10127
         LA    R6,OBJTEXT+52                                    GP10127
         SR    R7,R7
         TM    SWY80,X'80'
         BZ    A00A02
         NI    SWY80,X'7F'
         OI    SWY01,X'01'
         MVC   0(4,R4),RLDADR
         LA    R4,4(,R4)
         LA    R7,4(,R7)
         B     A00972
A00A02   CR    R3,R5
         BE    A00A2A
         B     A00932
A00A0C   TM    4(R3),X'01'
         BO    A00A22
         LA    R3,8(,R3)
         CR    R3,R5
         BE    A00A2A
         B     A00932
A00A22   LA    R3,4(,R3)
         B     A00A0C
A00A2A   L     R10,RLDPOS
         TM    SWY10,X'10'
         BZ    A00A52
         TM    0(R10),X'0D'
         BC    5,A00A52            BRANCH IF ONES OR MIXED
         LA    R10,260(,R10)
         TM    0(R10),X'02'
         BZ    A00A52
         ST    R10,RLDPOS
         B     A00924
A00A52   TM    SWY01,X'01'
         BO    PUNCHRL
         TM    SWY20,X'20'
         BO    PUNEND
         TM    RECID,X'01'
         BZ    PUNEND
         NI    SWY10,X'EF'
         SR    R7,R7
         L     R4,8(,R10)
         LA    R4,0(,R4)
         S     R4,ORIGIN
         ST    R4,ADDRES
         SR    R7,R4
         LH    R4,6(,R10)
         LA    R4,16(R4,R10)
         TM    SWX,FGISNE                                       GP10127
         BZ    A00AA0
         NI    SWX,255-FGISNE                                   GP10127
         MVC   WANTID,0(R4)
A00AA0   CLC   0(2,R4),WANTID
         BNE   PUNEND
         LH    R8,2(,R4)                                        GP10127
         STH   R8,TEXLEN                                        GP10127
         L     R10,PTRTEX
         LA    R1,OBJTEXT          RESTORE TO CARD START        GP10127
         ST    R1,PTRTESD          SAVE PARTIAL CARD POSITION   GP10127
         B     A0061C
PUNEND   TM    SWX,FGHVESD                                      GP10127
         BZ    A00ADA
         BAL   R5,PUNESD
A00ADA   TM    SWX,FGEXP1  +EXP2   EXPAND SPECIFIED?
         BZ    PUNCHEND            NO - BRANCH
         NI    SWX,255-(FGEXP1+FGEXP2) YES, SET SWITCHES OFF    GP10127
         OI    SWY,FGDOFILL       SET FOR FILL OPERATION        GP10127
         MVC   TEXLEN,EXPDIF
         LA    R1,OBJTEXT         SET TO BLANK FILL             GP10127
         ST    R1,PTRTEX
         B     PUNTX2
         SPACE 1
*---------------------------------------------------------------------*
*              * * *  PUNCH THE 'END' CARD  * * *                     *
*---------------------------------------------------------------------*
PUNCHEND MVC   OBJTYPE,=C'END'                                  GP10127
         L     R1,@ENTRY          GET MODULE ENTRY POINT        GP10127
         S     R1,ORIGIN          RELOCATE                      GP10127
         BM    PUNCHENT           IN EARLIER SECTION            GP10127
         C     R1,CSESIZ          IN THIS SECTION ?             GP10127
         BNL   PUNCHENT           NO; LATER                     GP10127
         STCM  R1,7,OBJTADD       SET ENTRY OFFSET              GP10127
         MVC   OBJTEID,=H'1'      AND CESD ID                   GP10127
 PRTDATA  '        ',SECTNAME,' entry point at offset',                *
               (OBJTADD,HEX,PADL)                               GP10127
PUNCHENT BAL   R14,PUNCHOUT                                     GP10127
         TM    OPTFLAGS,FGALL     PROCESS ALL CSECTS?           GP10127
         BNZ   NEXTSECT             YES; GET NEXT SECTION       GP10127
         PRTSPACE 1               SEPARATE INPUT REQUEST        GP10127
         B     NEXTCARD             NO; DO ANOTHER REQUEST      GP10127
NEXTSECT INCH  WANTID             SET TO PROCESS NEXT SECTION   GP10127
         MVI   LINE-1,C' '                                      GP10127
         MVC   LINE,LINE-1        CLEAR PRINT LINE              GP10127
         MVC   PUNCHCRD-1(L'PUNCHCRD+1),LINE   CLEAR PUNCH CARD GP10127
         MVI   OBJID,X'02'   OBJECT IDENTIFIER                  GP10127
         XC    MODWORK(ESDCLEN),MODWORK  CLEAR FOR NEXT REQUEST GP10127
         NI    SWX,FGHAVIN                                      GP10127
         MVI   RECID,X'00'
         LA    R1,OBJTEXT    START FOR TEXT FILL
         ST    R1,PTRTEX                                        GP10127
         ST    R1,PTRTESD    AND ESD FILL                       GP10127
         LA    R1,CESDBUF                                       GP10127
         ST    R1,@CESDS
         ST    R1,@CESDN
         A     R1,=A(WORKBUF-CESDBUF)  SPACE TO END OF BUFFER   GP10127
         ST    R1,@TXTND               DO FIXED AREAS FIRST     GP10127
         A     R1,=A(128*256)            PAST LAST BUFFER       GP10127
         ST    R1,@RLDEND              SAVE END                 GP10127
         LA    R1,1                                             GP10127
         STH   R1,CNTESX                                        GP10127
         STH   R1,CURRID                                        GP10127
         B     RESTART             GO PROCESS NEXT SECTION      GP10127
         SPACE 1
*---------------------------------------------------------------------*
*   SUBROUTINE TO PUNCH SEQUENCED CARDS   R14-R1 VOLATILE             *
*---------------------------------------------------------------------*
PUNCHOUT ST    R14,SAVER14                                      GP10127
         LH    R15,SEQNUM                                       GP10127
         LA    R15,1(,R15)           ADD 1 TO SEQNUM            GP10127
         STH   R15,SEQNUM                                       GP10127
         CVD   R15,DB                                           GP10127
         OI    DB+7,X'0F'            EVENTUAL PLUS ZONE         GP10127
         UNPK  OBJSEQ,DB+4(4)                                   GP10127
         MVC   OBJSEQ(4),SUFFIX      CSECT FOR DECK ID          GP10127
         PRTF  PUNCHCRD,DEV=3,CC=NO    PUNCH THE CARD           GP10127
         MVC   PUNCHCRD,PUNCHCRD-1     CLEAR IT                 GP10127
         MVI   OBJID,X'02'   OBJECT IDENTIFIER                  GP10127
         L     R14,SAVER14                                      GP10127
         BR    R14                                              GP10127
         EJECT
*---------------------------------------------------------------------*
*     ERROR MESSAGE PRINTING                                          *
*---------------------------------------------------------------------*
MSGNOLIB LA    R10,1
         B     MESSGX
MSGIOLIB LA    R10,2
         SYNADAF ACSMETH=BPAM                                   GP10127
         LH    R2,0(,R1)     MESSAGE IN WTO FORMAT              GP10127
         SH    R2,=H'5'      MAKE EXECUTE LENGTH                GP10127
         BM    MESSGX          NONE?                            GP10127
         EX    R2,EXSYNMSG   MOVE TO PRINT LINE                 GP10127
         BAL   R14,PRINTOUT  DISPLAY IT                         GP10127
         SYNADRLS ,          RELEASE BUFFER                     GP10127
         B     MESSGX
EXSYNMSG MVC   LINE+15(0),4(R1)   DISPLAY SYNAD MESAGE          GP10127
         SPACE 1
LIBEOF   LA    R10,3                                            GP10127
         B     MESSGX
         SPACE 1
MSGNOMEM LA    R10,4
         B     MESSGX
         SPACE 1
MSGTOBIG LA    R10,5
         B     MESSGX
         SPACE 1
MSGNOSEC LA    R10,6
         B     MESSGX
         SPACE 1
MSGBDNUM LA    R10,7
         B     MESSGX                                           GP10127
         SPACE 1
MSGEXCON LA    R10,8                                            GP10127
         B     MESSGX                                           GP10127
         SPACE 1
MSGNOBLD LA    R10,9
         B     MESSGX
         SPACE 1
RLDOFLOW MVC   MESSAG12(3),=C'RLD'                              GP10127
ESDOFLOW LA    R10,10
         B     MESSGX
         SPACE 1
MSGNOESD TM    8(R10),X'05'        COMMON?
         BO    BUILDESD
         TM    8(R10),X'03'        LR?
         BO    A001D6
         LA    R10,11
         B     MESTALL
         SPACE 1
MSGNOTXT LA    R10,12
MESTALL  TM    OPTFLAGS,FGSOME    END CARD PROCESSED?           GP10127
         BZ    MESSGX             NO; REAL ERROR                GP10127
         TM    OPTFLAGS,FGALL     DOING *ALL* CSECTS?           GP10127
         BZ    MESSGX                                           GP10127
         PRTSPACE 1               NEATEN                        GP10127
         B     NEXTCARD             YES; OVERSTAYED OUR WELCOME GP10127
         SPACE 1
MSGBDEXP LA    R10,13
         B     MESSGX                                           GP10127
         SPACE 1
MSGADCON LA    R10,14
*NEXT*   B     MESSGX
         SPACE 1
MESSGX   LR    R2,R10        PRESERVE                           GP10127
         BCTR  R10,0                                            GP10127
         SLL   R10,5
         LA    R10,MESSAGES(R10)                                GP10127
         MVC   LINE+1(32),0(R10)      MOVE MESSAGE TO LINE
         BAL   R14,PRINTOUT
         CLI   RETCODE+3,8   BEEN HERE BEFORE ?                 GP10127
         BNL   NEXTCARD        YES; CONTINUE IN INTERPRETIVE MODE
         PRTL  ' Execution terminated due to errors; scanning remaining*
                input for errors only'
         SETCC 8,(R2)        SET ERROR AND REASON CODE          GP10127
         B     NEXTCARD            ABORT RUN                    GP10127
         EJECT
*---------------------------------------------------------------------*
*    PROGRAM TERMINATION                                              *
*---------------------------------------------------------------------*
EOJ      TM    SWX,FGHAVIN         WERE THERE ANY CARDS?        GP10127
         BO    EOJHC               YES - BRANCH                 GP10127
EOJQUICK SETCC 16,88               NO - SET FOR MISSING INPUT   GP10127
         SPACE 1
EOJHC    CLOSE (SYSLIB)                                         GP10127
         SERVTERM ,                                             GP10127
         SPACE 1
         PGMEXIT COPYRET=(RETCODE,8)   SET RETURN & REASON CODE GP10127
         SPACE 2
*---------------------------------------------------------------------*
*   PRINT ROUTINE WITH PAGINATION                                     *
*---------------------------------------------------------------------*
PRINTOUT ST    R14,SAVER14                                      GP10127
         PRTF  LINE,CC=ASA        PRINT WITH CARRIAGE CONTROL   GP10127
         MVC   LINE,LINE-1        CLEAR PRINT LINE              GP10127
         L     R14,SAVER14                                      GP10127
         BR    R14                                              GP10127
         SPACE 2
         LTORG ,                                                GP10127
         EJECT
***************************************************************
*                                                             *
*        LOAD MODULE RECORD TYPES                             *
*                                                             *
*        0100 0000   X'40'   SYM RECORD                       *
*        0010 0000   X'20'   CESD RECORD                      *
*        0001 0000   X'10'   SCATTER/TRANSLATION RECORD       *
*        0000 0001   X'01'   CONTROL (CCW) RECORD             *
*        0000 0101   X'05'   CONTROL (CCW) - END OF SEGMENT   *
*        0000 1101   X'0D'   CONTROL (CCW) - END OF MODULE    *
*        0000 0010   X'02'   RLD                              *
*        0000 0110   X'06'   RLD - LAST RECORD OF SEGMENT     *
*        0000 1110   X'0E'   RLD - LAST RECORD OF MODULE      *
*        0000 0011   X'03'   CONTROL (CCW) AND RLD            *
*        0000 0111   X'07'   CONTROL (CCW) AND RLD - (EOS)    *
*        0000 1111   X'0F'   CONTROL (CCW) AND RLD - (EOM)    *
*        1000 0000   X'80'   CSECT IDENTIFICATION RECORD - IDR*
*                                                             *
***************************************************************
*                                                             *
*        ESD TYPES                                            *
*                                                             *
*        XXXX 0000   X'00'   SD   SECTION DEFINITION          *
*        XXXX 0001   X'01'   LR   LABEL REFERENCE (OBJ DECK)  *
*        XXXX 0010   X'02'   ER   EXTERNAL REFERENCE          *
*        XXXX 0011   X'03'   LR   LABEL REFERENCE (LOAD MOD)  *
*        XXXX 0100   X'04'   PC   PRIVATE CODE                *
*        XXXX 0101   X'05'   CM   COMMON                      *
*        XXXX 0110   X'06'   PR   PSEUDO REGISTER, XD         *
*        0000 0111   X'07'   --   NULL                        *
*        XXXX 1010   X'0A'   WX   WEAK EXTERNAL REF           *
*        XXX1 X100   X'14'        PRIVATE CODE MARKED DELETE  *
*                                  (ENTAB AND SEGTAB CSECTS)  *
*****                                                         *
*                                                             *
*        XXX1 XXXX   X'10'   DELETE/REPLACE                   *
*        XX1X XXXX   X'20'   INSERT                           *
*        X1XX XXXX   X'40'   CHAIN                            *
*        1XXX XXXX   X'80'   MAP                              *
*                                                             *
***************************************************************
         SPACE 1
SYSPRINT PRTWORK SYSPRINT,TITLE=3
         SPACE 1
SYSPUNCH PUNWORK SYSPUNCH
         SPACE 1
SYSIN    INPWORK SYSIN,WIDTH=80,EODAD=EOJ
         SPACE 1
SYSLIB   DCB   DDNAME=SYSLIB,DSORG=PO,MACRF=R,SYNAD=MSGIOLIB,          *
               RECFM=U,EODAD=LIBEOF                             GP10127
         SPACE 1
H4       DC    H'4'
BLANKS   DC    CL8' '                                           GP10127
         SPACE 1
MESSAGES DC    CL32'UNABLE TO OPEN SYSLIB.'           M1
         DC    CL32'I/O ERROR ON SYSLIB.'             M2
         DC    CL32'UNEXPECTED END-FILE ON SYSLIB  '  M3        GP10127
         SPACE 1
         DC    CL32'NO MEMBER NAME RECEIVED.'         M4
         DC    CL32'NAME MORE THAN EIGHT CHARACTERS.' M5
         DC    CL32'NO CSECT NAME RECEIVED.'          M6
         DC    CL32'NUMBER OR EXTEND CARD INCORRECT'  M7
         DC    CL32'EXPAND NOT SUPPORTED WITH *ALL*'  M8        GP10127
         SPACE 1
         DC    CL32'MEMBER NAME NOT FOUND IN PDS.'    M9
MESSAG12 DC    CL32'ESD BUFFER OVERFLOW.'             M10
MESSAGE6 DC    CL32'ESD NAME NOT SECTION DEFINITION.' M11
         DC    CL32'TEXT FOR CSECT NOT FOUND.'        M12
         DC    CL32'EXPAND SIZE NOT VALID.   '        M13
         DC    CL32'ADCON NOT 3 OR 4 BYTES**ERROR**.' M14
         SPACE 1
HEADER   DC    CL35'#          DELINKER       V1  L1   '        GP10127
         DC    CL34'EXPAND AND NOT EDITABLE FUNCTION  '
         DC    CL64'&SYSDATE       '                            GP10127
         SPACE 1
TRTBLANK DC    256AL1(0)     ALL VALID                          GP10127
         ORG   TRTBLANK+C' '                                    GP10127
         DC    AL1(8)        STOP ON BLANK                      GP10127
         ORG   ,                                                GP10127
         TITLE 'D E L I N K E R  ***  DYNAMIC WORK AREA AND DSECTS'
ESDDSECT DSECT ,             DECRIBE CESD ENTRIES               GP10127
ESDNAME  DS    CL8           ITEM NAME                          GP10127
ESDTYPE  DS    X             ITEM TYPE                          GP10127
ESDADDR  DS    XL3           ITEM ADDRESS IN LOAD MODULE        GP10127
ESDFLAG  DS    X             OVLY REGION, ETC.                  GP10127
ESDLEN   DS    0XL3,X        ITEM LENGTH                        GP10127
ESDID    DS    XL2           CESD ID                            GP10127
ESDSIZE  EQU   *-ESDDSECT      ENTRY LENGTH                     GP10127
         SPACE 2
SAVE     DSECT ,                                                GP10127
         SERVDEFS ,                                             GP10127
DB       DS    D             WORK AREA                          GP10127
EXPVAL   DC    D'0'                                             GP91137
DBWORK   DC    0D'0',F'0'    1/2                                GP91137
WDWORK   DC    F'0'          2/2                                GP10127
SUBSAVER DS    16A           SUBROUTINE SAVER                   GP10127
SAVER14  DS    A             SUBROUTINE RETURN SAVER            GP10127
STM710   DC    4F'0'
STM14    DC    4F'0'
@CESDS   DC    A(0)
@CESDN   DC    A(0)
@TXTND   DC    A(0)
@RLDEND  DC    A(0)                                             GP10127
CSESIZ   DC    F'0'  416
ZEROES   DC    F'0'  432
SEQNUM   DC    H'1'                                             GP10127
         SPACE 1
RUNFLAGS DS    X             NEW(ER) FLAGS                      GP10127
FGDEBUG  EQU   X'80'           LIST DEBUGGING  INFORMATION      GP10127
FGLIST   EQU   X'40'           LIST ADDITIONAL INFORMATION      GP10127
         SPACE 1
FGHAVIN  EQU   X'80'               SYSIN NOT EMPTY
FGHAVSD  EQU   X'40'               CSECT FOUND                  GP10127
FGISNE   EQU   X'20'
FGEXP1   EQU   X'10'               EXPAND REQUESTED             GP10127
FGEXP2   EQU   X'08'               EXPAND INCREMENT (ELSE SIZE) GP10127
FGHVEID  EQU   X'02'         HAVE ESD ID FOR THIS ESD CARD      GP10127
FGHVESD  EQU   X'01'         FINISHED ESD CARDS                 GP10127
SWX      DC    X'00'
         SPACE 1
MODWORK  DS    0D            AREA CLEARED FOR EACH CSECT        GP10127
ADDRES   DC    A(0)
ORIGIN   DC    A(0)          CSECT OFFSET
PTR476   DC    F'2004318071' 476
PTR480   DC    F'2004318071' 480
@TEXTN   DC    A(0)
PTRTEX   DC    A(0)          RESUME TEXT FILL
PTRTESD  DC    A(0)          RESUME ESD FILL                    GP10127
RLDPOS   DC    A(0)
RLDADR   DC    A(0)
SECTNAME DC    CL8' '        REQUESTED CSECT                    GP10127
SUFFIX   EQU   SECTNAME,4,C'C'   MOVED TO COL 73-80 OF OBJ      GP10127
EXPDIF   DC    H'0'          AMOUNT TO EXPAND CSECT BY          GP10127
TEXLEN   DC    H'0'
CURRID   DC    H'1'                                             GP10127
CNTESX   DC    H'1'                                             GP10127
CNTESD   DC    H'0'
RECID    DC    X'77'
NUMSEG   DC    X'77' 440
SAVTYP   DC    X'77' 441
         SPACE 1
SWY80    EQU   *
SWY40    EQU   *
SWY20    EQU   *
SWY10    EQU   *
SWY08    EQU   *
FGDOFILL EQU   X'04'         PROCESSING EXPAND FILL BYTES       GP10127
SWY01    EQU   *
SWY      DC    X'00'
ESDCLEN  EQU   *-MODWORK     LENGTH TO CLEAR FOR *ALL* OPTION   GP10127
         SPACE 1
OPTFLAGS DC    X'00'         OPTION FLAGS PER LOAD MODULE       GP10127
FGALL    EQU   X'80'           PROCESS ALL POSSIBLE             GP10127
FGMSG    EQU   X'40'           DON'T REPEAT BLDL DATA MESSAGE   GP10127
FGSOME   EQU   X'02'           SHOW END CARD PROCESSED          GP10127
FG1ST    EQU   X'01'           SELECT FIRST PROCESSABLE ID      GP10127
WANTID   DC    H'0'          ESDID OF USER'S CSECT
@ENTRY   DC    A(0)          ENTRY POINT ADDRESS FROM PDS2      GP10127
         SPACE 1
MODCLEN  EQU   *-MODWORK     LENGTH TO CLEAR                    GP10127
         SPACE 1
BLDLL    DC    0A(0),H'1,72'                                    GP10127
BLDLMEM  DC    CL72' '                                          GP10127
         ORG   BLDLMEM                                          GP10127
         IHAPDS DSECT=NO                                        GP10127
         ORG   BLDLMEM+L'BLDLMEM                                GP10127
         SPACE 1
         DS    C' '            FOR FAST CLEARING                GP10127
LINE     DC    CL133'0  '
         SPACE 1
         DS    C' '            FOR FAST CLEARING                GP10127
PUNCHCRD DC    0CL80' ' 01 - 80      CARD IMAGE
OBJID    DC    CL01' '  01 - 01      X'02' FOR OBJECT TEXT
OBJTYPE  DC    CL03' '  02 - 04      ESD/TXT/RLD/END
         DC    CL01' '  05 - 05
OBJTADD  DC    CL03' '  06 - 08      TEXT ADDRESS
         DC    CL02' '  09 - 10
OBJTLEN  DC    CL02' '  11 - 12      TEXT LENGTH
         DC    CL02' '  13 - 14
OBJTEID  DC    CL02' '  15 - 16      TEXT ESDID
OBJTEXT  DC    CL56' '  17 - 72      MNEMONIC
OBJSEQ   DC    CL08' '  73 - 80      STATEMENT SEQUENCE NUMBER
         SPACE 1
TRTADHOC DC    XL256'0'      TRT TABLE BUILT ON THE FLY         GP10127
         SPACE 1
         DS    0D            MAKE DUMPS PRETTIER                GP10127
CESDBUF  DS    128XL256      32K WORK BUFFER                    GP10127
         DS    128XL256      32K WORK BUFFER                    GP10127
         DS    128XL256      32K WORK BUFFER                    GP10127
         DS    128XL256      32K WORK BUFFER                    GP10127
         DS    128XL256      32K WORK BUFFER                    GP10127
         DS    128XL256      32K WORK BUFFER                    GP10127
         DS    128XL256      32K WORK BUFFER                    GP10127
         DS    128XL256      32K WORK BUFFER                    GP10127
         DS    128XL256      32K WORK BUFFER                    GP10127
         DS    128XL256      32K WORK BUFFER                    GP10127
         DS    128XL256      32K WORK BUFFER                    GP10127
         DS    128XL256      32K WORK BUFFER                    GP10127
         DS    128XL256      32K WORK BUFFER                    GP10127
         DS    128XL256      32K WORK BUFFER                    GP10127
         DS    128XL256      32K WORK BUFFER                    GP10127
         DS    128XL256      32K WORK BUFFER                    GP10127
         DS    128XL256      32K WORK BUFFER                    GP10127
         DS    8XL256          SPILL AREA                       GP10127
WORKBUF  DS    64XL256       16K RLD  BUFFER                    GP10127
SAVEEND  EQU   *             END OF GOTTEN AREA
         SPACE 1
         PRINT NOGEN                                            GP10127
         DCBD  DSORG=PS,DEVD=DA                                 GP10127
         END
