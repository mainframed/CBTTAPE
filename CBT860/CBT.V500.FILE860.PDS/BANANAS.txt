LBLS     TITLE 'B A N A N A S  ***  QUICK AND DIRTY LABEL PRINTER'
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
***********************************************************************
*                                                                     *
*        THIS PROGRAM PRODUCES CURVED (TAPE) LABELS, KNOWN HERE AS    *
*        'BANANA' LABELS, EVEN THOUGH THEY ARE WHITE.                 *
*                                                                     *
*        INPUT:  ONE TO EIGHT CONTROL CARDS, MAXIMUM ONE '>' LINE     *
*        '1' IN COLUMN 1 - FOLLOWED BY 39 TEXT CHARACTERS             *
*              BEGINS A NEW LABEL, PRINTED AS READ                    *
*        BLANK IN COLUMN 1 - FOLLOWED BY 39 TEXT CHARACTERS           *
*              PRINTED AS READ                                        *
*        > IN COLUMN 1 - FOLLOWED BY 6 OR 13 CHARACTERS               *
*              PRINTED AS FIVE LINES IN 5 BY 5 MATRIX                 *
*              MAY BE SPECIFIED AS RANGE:  XXXNNN/XXXNNN              *
*                ONLY NUMERICS ARE INCREMENTED.                       *
*        N IN COLUMN 1 - FOLLOWED BY 0 TO 8 DIGITS                    *
*              REPEAT COUNT FOR FOLLOWING LABELS                      *
*              (CURRENT LABEL DONE WITH PRIOR COUNT)                  *
*                                                                     *
*        DEPENDENCIES - LOCAL SERVICE ROUTINES AND MACROS             *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT ,
         PRINT &PRTSOR
BANANAS  PGMHEAD ZERO12,BASE=R12,PARM=R9,BNDRY=PAGE              91055
         SPACE 1
         SERVINIT ,
         SERVLOAD @PRINTER,@INPREAD,IEFSD095                    GP03146
         PRTOPEN SYSPRINT,OPT=ABEND
         PRTOPEN SUMPRINT,OPT=NOWTO,DEV=2  ERROR MESSAGE FILE
         INPOPEN SYSIN,OPT=ABEND  GET INPUT FILE
         LA    R9,0(,R9)     CLEAN PARM POINTER
         LTR   R9,R9         ANY POINTER ?
         BZ    NEWLABEL      NO
         L     R9,0(,R9)     LOAD PARM ADDRESS
         LA    R9,0(,R9)     CLEAN IT
         LTR   R9,R9         ANY AT ALL ?
         BZ    NEWLABEL      NO
         SLR   R8,R8
         ICM   R8,3,0(R9)    ANY TEXT ?
         BNP   NEWLABEL      NO
         CLC   =C'NO',2(R9)        'NOALIGN' ?
         BNE   NEWLABEL      NO
         OI    FLAGS,FGALIGN   FAKE ALIGN DONE
         SPACE 1
NEWLABEL TM    FLAGS,FGEOF   ALREADY HIT EOF ?
         BNZ   EXIT          YES; ALL DONE
         CLRL  LABWORK,LABWORKL   CLEAR THE AREA
         MVI   OUTLINE,C' '
         MVC   OUTLINE+1(L'OUTLINE-1),OUTLINE  BLANK
         MVC   NUMTODO,REPCOUNT  SET REPEAT COUNT
GETACARD INPGET ,            GET A LINE
         LR    R5,R1         COPY THE ADDRESS
         USING CARDSECT,R5   DECLARE IT
         TM    FLAGS,FGALIGN DID WE FINISH ALIGNMENT ?
         BZ    DOALIGN       NO; MAKE ALIGNMENT PATTERN FIRST
         SPACE 1
         CLI   CARDCON,C'N'  NUMBER OF COPIES ?
         BNE   GETACOP       NO
         ICM   R0,15,LINECNT  ANY LABEL STACKED ?
         BNZ   GETAHEAD      YES; PRINT CURRENT LABEL WITH OLD REP
         SLR   R14,R14
         LA    R15,C'0'
         LA    R0,8          MAXIMUM INPUT
         LA    R1,CARDTEXT
GETANUM  CLI   0(R1),C'0'    ANOTHER DIGIT ?
         BL    GETANUMD      NO; DONE
         CLI   0(R1),C'9'    DITTO ?
         BH    GETANUMD      DITTO
         SLDL  R14,8
         IC    R15,0(,R1)    ADD NEXT ONE IN
         LA    R1,1(,R1)
         BCT   R0,GETANUM
GETANUMD STM   R14,R15,DB2   SAVE
         PACK  DB,DB2
         CVB   R0,DB         MAKE BINARY
         ST    R0,REPCOUNT   UPDATE REPETITION COUNT
         ST    R0,NUMTODO    AND NEXT LABEL COUNT
         B     GETACARD
         SPACE 1
GETACOP  CLI   CARDCON,C'1'  START A NEW LABEL ?
         BE    GETAHEAD      YES
         CLI   CARDCON,C'L'  TOP AND LARGE ?
         BNE   LABSTACK      NO
GETAHEAD ICM   R0,15,LINECNT   HOW MANY LINES SO FAR ?
         BZ    LABSTACK      NONE; JUST STACK THIS ONE
         INPKEEP ,           PRESENT THIS CARD ON NEXT REQUEST
         B     LABMAKE       MAKE A LABEL
         SPACE 1
DOALIGN  INPKEEP ,           STACK FIRST INPUT CARD 'TIL LATER
         MVI   NUMTODO+L'NUMTODO-1,6  ONE ALIGNMENT PAGE
         MVI   LINECNT+L'LINECNT-1,8  FAKE 8 LINES
         LA    R0,8
         LA    R3,LINEBASE
         USING LINESECT,R3
ALLEIN   MVI   LINECON,C' '  PLAIN LINE
         MVI   LINETEXT,C'X'
         MVC   LINETEXT+1(L'LINETEXT-1),LINETEXT  PRPAGATE
         LA    R3,LINESIZE(,R3)
         BCT   R0,ALLEIN     FILL ALL
         DROP  R3
         MVC   LINETEXT-LINESECT+LINEBASE(14),LINECON-LINESECT+LINEBASE
  MVC  LINETEXT+14+11-LINESECT+LINEBASE(14),LINETEXT-LINESECT+LINEBASE
         OI    FLAGS,FGALIGN SHOW ALIGN DONE
         B     LABMAKE       PRINT ALIGNMENT
         SPACE 1
         SPACE 1
LABSTACK L     R4,LINECNT
         LA    R3,LINESIZE   GET SIZE OF ONE ARRAY ENTRY
         MR    R2,R4         GET OFFSET TO CURRENT ONE
         LA    R3,LINEBASE(R3)  GET CURRENT ENTRY
         USING LINESECT,R3   DECLARE IT
         MVC   LINECON(CARDLEN),CARDCON  MOVE ENTIRE CARD
         INC   LINECNT,WORK=R15
         CH    R15,=H'8'     ARE WE OVER LABEL LIMIT ?
         BH    EXCESS        YES; TOO BAD
         CLI   LINECON,C'>'  LARGE LETTER REQUEST ?
         BE    SAVEHUGE      YES
         CLI   LINECON,C'L'  ALTERNATE ?
         BNE   SAVEDONE      NO; GET NEXT CARD
SAVEHUGE LA    R15,4(,R15)   ADD FOUR MORE
         ST    R15,LINECNT   STASH BACK
         CH    R15,=H'8'     ARE WE OVER THE LIMIT ?
         BH    EXCESS        YES
         MVI   LINECON,C'>'  MAKE SINGLE CHOICE
*        PARSE RANGE
         LA    R5,LINETEXT   GET TEXT ADDRESS
         SLR   R2,R2         CLEAR FOR IC
         LR    R15,R5        SAVE START
SKIPLBLK TRT   0(L'TEXTLO+1,R5),ENDTABLE  LOOK FOR END OF PARM
         BZ    SAVEDONE      LOGIC ERROR IF NO STOP HIT
         LA    R5,1(,R1)     SET NEXT SCAN ADDRESS
         LR    R6,R1         SAVE HIT COLUMN
         SR    R6,R15        GET HIT LENGTH
         BNP   SKIPLBLK      IGNORE EXTRANEOUS SEPARATOR
         BNP   SAVEDONE      LEADING SEPARATOR - NOT RANGE
         CH    R6,=H'6'      LENGTH=6 ?
         BH    SAVEDONE      NO; BOOBOO
         CLM   R2,1,ENDTABLE+C' '  LEADING SEPARATOR ?
         CLM   R2,1,ENDTABLE+C'-'  RANGE ENTRY ?
         BNE   SKIPLBLK      NO; KEEP LOOKING
         MVI   TEXTLO,C' '
         MVC   TEXTLO+1(L'TEXTLO+L'TEXTHI-1),TEXTLO  BLANK
         LA    R14,TEXTLO    POINT TO HOLD FIELD
         BCTR  R6,0          ADJUST FOR EXECUTION
         EX    R6,SAVETEXT   MOVE TEXT
         TRT   0(7,R5),ENDTABLE  CHECK FOR END OF RANGE
         CLM   R2,1,ENDTABLE+C' '  NORMAL END ?
         BNE   SAVEDONE      NO; FAIL
         LR    R15,R5        SAVE START
         LA    R5,1(,R1)     SET NEXT SCAN ADDRESS
         LR    R6,R1         SAVE HIT COLUMN
         SR    R6,R15        GET HIT LENGTH
         CH    R6,=H'6'      LENGTH=6 ?
         BH    SAVEDONE      NO; BOOBOO
         LA    R14,TEXTHI    POINT TO HOLD FIELD
         BCTR  R6,0          ADJUST FOR EXECUTION
         EX    R6,SAVETEXT   MOVE TEXT
         CLC   TEXTLO,TEXTHI VALID RANGE ?
         BH    SAVEDONE      NO
         ST    R3,RANGER     KEEP THIS ENTRY
SAVEDONE L     R15,LINECNT   RELOAD
         CH    R15,=H'8'     EXACTLY ONE LABEL ?
         BNE   GETACARD      NO; GET ANOTHER CARD
         SPACE 2
*   MAKE A LABEL - ALL DATA STACKED
LABMAKE  LA    R3,LINEBASE   GET FIRST CARD
         ICM   R4,15,LINECNT  GET NUMBER OF LINES DEFINED
         BNP   EXIT16        NUTS - NO INPUT
LABMAKER CLI   LINECON,C'>'  BIG LETTERS ?
         BE    LABLOOP       YES
         PRTF  LINETEXT,CC=NO
         B     LABMAKEN      GO TO END LABEL
         SPACE 1
LABLOOP  LA    R2,5          SET COUNT
         LA    R14,LINETEXT  POINT TO INPUT TEXT
         ICM   R0,15,RANGER  DOING A RANGE ?
         BZ    *+8           NO
         LA    R14,TEXTLO    YES; POINT TO LOW
         LA    R15,LINECUR   POINT TO CURRENT LINE NUMBER
         LA    R0,OUTLINE+2  POINT TO OUTPUT TEXT HOLDER
         LA    R1,BIGFLAGS   POINT TO FLAGS
         STM   R14,R1,CALLPARM  MAKE PARM LIST                  GP03146
         L     R5,IEFSD095   GET BIG LETTER ROUTINE
LABLOOPS LA    R0,6          GET MAX LINE+1
         SR    R0,R2         GET CURRENT LINE
         ST    R0,LINECUR    STASH IT
         LA    R1,CALLPARM   GET PARAMETER LIST                 GP03146
         LR    R15,R5        GET ADDRESS
         BASR  R14,R15       MAKE ONE LINE PATTERN              GP03146
         PRTF  OUTLINE,CC=NO  BUILD ONE LINE
         BCT   R2,LABLOOPS   REPEAT
         LA    R3,4*LINESIZE(,R3)  SKIP NEXT FOUR
         SH    R4,=H'4'      SET INCREMENT FOR ALL BUT ONE
         BNP   LABMAKEX
LABMAKEN LA    R3,LINESIZE(,R3)
         BCT   R4,LABMAKER   DO NEXT LINE
LABMAKEX INC   NUMLABS,WORK=R15  INCREMENT LABEL COUNT
         LA    R1,12-8       SET SPACE TO NEXT LABEL
         SLR   R14,R14
         D     R14,=F'6'     DIVIDE BY LABELS PER PAGE
         LTR   R14,R14       ANY REMAINDER ?
         BNZ   LABMAKEY      YES; SKIP TO NEXT LABEL
         LA    R1,255        SKIP TO NEXT PAGE
LABMAKEY PRTSPACE (R1)       SPACE OR EJECT
         L     R15,NUMTODO   GET NUMBER OF COPIES TO DO
         SH    R15,=H'1'     ADJUST
         ST    R15,NUMTODO   UPDATE
         BP    LABMAKE       DO NEXT COPY
         MVC   NUMTODO,REPCOUNT  REFRESH COUNT FOR NEXT LABEL
         ICM   R6,15,RANGER  ARE WE DOING A RANGE ?
         BZ    NEWLABEL      NO; DO ANOTHER
         LA    R15,TEXTLO+L'TEXTLO  POINT PAST LAST BYTE
         LA    R0,L'TEXTLO   NUMBER TO DO
LOCLOOP  BCTR  R15,0         GET PRIOR BYTE
         CLI   0(R15),C'0'   IS IT NUMERIC ?
         BL    LOCBUMP       NO; TRY ANOTHER
         CLI   0(R15),C'9'   IS IT NUMERIC ?
         BL    LOCINC        YES; INCREMENT AND PROCESS
         MVI   0(R15),C'0'   REPLACE BY ZERO AND DO PRIOR
LOCBUMP  BCT   R0,LOCLOOP    LOOK AGAIN
         B     NEWLABEL      NO MORE POSSIBLE
LOCINC   SLR   R14,R14
         IC    R14,0(,R15)   GET CURRENT DIGIT
         LA    R14,1(,R14)   INCREASE
         STC   R14,0(,R15)   STASH IT BACK
         CLC   TEXTLO,TEXTHI STILL IN RANGE ?
         BNH   LABMAKE       YES; DO IT AGAIN
         B     NEWLABEL      ELSE LOOK FOR ANOTHER
         SPACE 1
EXIT     SERVTERM ,
         L     R9,RETCODE                                       GP03146
         PGMEXIT RC=(R9)     RETURN WITH CONDITION              GP03146
SAVETEXT MVC   0(0,R14),0(R15)  MOVE TEXT FOR INCREMENTATION
         SPACE 1
SYNEOD   OI    FLAGS,FGEOF   RECURSION ?
         ICM   R0,15,LINECNT   ANY LABEL PENDING ?
         BNZ   LABMAKE       YES; PROCESS
         ICM   R0,15,NUMLABS  DID AT LEAST ONE ?
         BNZ   EXIT          YES; QUIT NORMALLY
         SPACE 1
EXIT16   MVICC 16            SET MAJOR ERROR                    GP03146
         B     EXIT          AND QUIT
         SPACE 1
EXCESS   PRTF  '*** MORE THAN 8 LINES REQUESTED IN LABEL ***',CC=NO,   *
               DEV=2         WRITE WARNING
         OICC  8             SET ERROR                          GP03146
         B     EXIT
         SPACE 2
SYSPRINT PRTWORK SYSPRINT,TITLE=0,LPP=72  6 LABELS @ 12 LINES
SUMPRINT PRTWORK SUMPRINT,ERRPRINT,TITLE=3
SYSIN    INPWORK SYSIN,EDIT=128,EODAD=SYNEOD
BIGFLAGS DC    AL1(2,X'80',0,6)  5*5, MORE OPTS, 6 PER LINE
         DC    AL1(0,0)      BACKGROUND/FOREGROUND
         DC    AL1(0,0,0,0,0,0)   CHAR.SPACE/...
         SPACE 1
ENDTABLE DC    256AL1(255)   MAKE ALL CHARACTERS INVALID
*   ACCEPT THESE AS VALID DATA :
         TRENT ENDTABLE,0,(X'81',9)    LOWER CASE A-I
         TRENT ENDTABLE,0,(X'91',9)    LOWER CASE J-R
         TRENT ENDTABLE,0,(X'A2',8)    LOWER CASE S-Z
         TRENT ENDTABLE,0,(C'A',9)     A-I
         TRENT ENDTABLE,0,(C'J',9)     J-R
         TRENT ENDTABLE,0,(C'S',8)     S-Z
         TRENT ENDTABLE,0,(C'0',10)    0-9
         TRENT ENDTABLE,0,C'#',C'@',C'$'    NATIONAL CHARS
         TRENT ENDTABLE,0,C'?',C'*',C'+'   OTHERS
         TRENT ENDTABLE,0,C'%',C'&&',C'_'
*   ACCEPT THESE AS SEPARATING CHARACTERS
         TRENT ,4,X'00',C' '
*   ACCEPT THESE AS RANGE INDICATORS
         TRENT ,8,C'-',C':',C'/',C'|'
         ORG   ,
         SPACE 1
CARDSECT DSECT ,             MAPPING OF INPUT CARD
CARDCON  DS    C             CONTROL BYTE (BLANK, 1, >, OR L)
CARDTEXT DS    CL39          WIDTH OF LABEL
CARDLEN  EQU   *-CARDSECT      SIZE OF ONE ENTRY
         DS    CL40          FILLER
         SPACE 1
LINESECT DSECT ,             MAPPING OF ONE LINE ENTRY
LINECON  DS    C             CONTROL BYTE
LINETEXT DS    CL39          TEXT
LINESIZE EQU   *-LINESECT    ONE ENTRY
         SPACE 1
SAVE     DSECT ,
DB       DS    D
DB2      DS    D
         SERVDEFS ,          DEFINE @SERVICE, @PRINTER, ETC.    GP03146
NUMLABS  DS    F             COUNT OF LABELS PRINTED
REPCOUNT DS    F             COPIES OF EACH LABEL TO DO
LINECUR  DS    F             PATTERN LINE NUMBER
FLAGS    DS    X             PROCESSING FLAGS
FGEOF    EQU   X'80'           END-FILE ON INPUT
FGALIGN  EQU   X'01'           PRINTED ALIGNMENT PAGES
OUTLINE  DS    CL39          BIG LETTER ROUTINE
         SPACE 1
LABWORK  DS    0F            START OF LABEL WORK AREA
LINECNT  DS    F             COUNT OF LINES IN THIS LABEL
RANGER   DS    F             POINTER TO 'HUGE' LINE IN ARRAY
NUMTODO  DS    F             NUMBER OF COPIES OF THIS LABEL LEFT
TEXTLO   DS    CL6           CURRENT (ORIGINAL LOW) TEXT
TEXTHI   DS    CL6           END OF RANGE TEXT
LINEBASE DS    9CL(LINESIZE)  STACK
LABWORKL EQU   *-LABWORK     SIZE TO CLEAR FOR EACH LABEL
SAVEEND  EQU   *
         SPACE 1
         CVT   DSECT=YES
         IHACDE ,
         END   ,
