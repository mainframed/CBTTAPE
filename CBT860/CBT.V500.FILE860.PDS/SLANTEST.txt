SLAN     TITLE 'STANDARD LABEL TAPE ANALYSIS AND COPY PROGRAM'
         MACRO ,                                                 82002
&NM      INHEX &OUT,&OL,&IN,&IL                                  82002
&NM      UNPK  &OUT.(1+&OL),&IN.(1+&IL)                          82002
         TR    &OUT.(&OL),HEXTRTAB-C'0'                          82002
         MVI   &OUT+&OL,C' '                                     82002
         MEND  ,                                                 82002
         SPACE 1                                                 82002
         COPY  OPTIONGB      DEFINE LOCAL OPTIONS                82002
         SPACE 1                                                 82002
         SYSPARM LIST=YES    SET LOCAL OPTIONS                   82002
         SPACE 1                                                 82002
***********************************************************************
*                                                                     *
*        COPYRIGHT 1982  EXPERT SYSTEM PROGRAMMING, INC.              *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         SPACE 1                                                 82002
*        THIS UTILITY IS BASED ON THE IDEAS EMBODIED IN PROGRAM  82002
*        SLANALYZ, DISTRIBUTED BY SHARE, INC.  AND CONTAINS FEATURES
*        AND CODE SUGGESTED BY BRUCE UTTLEY (U. OF WATERLOO) AND 82002
*        SEYMOUR J. METZ.  WRITTEN BY G. POSTPISCHIL.            82002
*                                                                90245
*        FULL RE-ENTRANCY, 3480 SUPPORT (PRELIMINARY) ADDED      90245
*                                                                90245
*   SAMPLE TAPE ANALYZE PROCEDURE :                              90245
*                                                                90245
*/SLANAL  PROC TAPE='USER01',LF=,N=1,U=1,UNI='3400-5',DN=3,LBL=LTM,
*/             START=1,END=1,INC=1,SKIP=SL,FULL=1024,ERRSIZE=1024,
*/             MAXPRT=5,LABPRT=15,LABERR=10,MAXERR=100,CONSERR=50,
*/             OTHER=TRUNC,PRG=SLANALYZ,EROPT=ABE,CODE=,BLK=32760
*/*                                                              90245
*/SLANAL  EXEC PGM=&PRG,REGION=128K,                             90245
*/             PARM=('ST=&START','EN=&END','IN=&INC','SK=&SKIP', 90245
*/             'SZ=&FULL','ES=&ERRSIZE','MP=&MAXPRT',            90245
*/             'LP=&LABPRT','LR=&LABERR','MR=&MAXERR',           90245
*/             'CR=&CONSERR','&OTHER')                           90245
*/*                                                              90245
*/SUMPRINT DD  SYSOUT=*                                          90245
*/SYSPRINT DD  SYSOUT=*                                          90245
*/SYSUDUMP DD  SYSOUT=*                                          90245
*/SYSUT1   DD  UNIT=(&UNI,&U,DEFER),DISP=OLD,DSN=&&ANY,          90245
*/             LABEL=(&LF,&LBL,EXPDT=98000),VOL=(,,,&N,SER=(&TAPE)),
*/             DCB=(DEN=&DN,EROPT=&EROPT,OPTCD=&CODE,BLKSIZE=&BLK)
*/*                                                              90245
*/*      THIS PROCEDURE FORMATS STANDARD LABEL TAPE HEADER AND TRAILER
*/*      FILES, AND DUMPS SELECTED BLOCKS OF EACH DATA FILE ON THE TAPE
*/*                                                              90245
*/*      TAPE=    IS A LIST OF THE VOLUME SERIALS TO BE ANALYZED 90245
*/*               FORMAT IS :  TAPE='VOLONE,VOLTWO,...'          90245
*/*               FIVE VOLUME SERIALS ARE THE MAXIMUM SUPPORTED  90245
*/*      LF=      LIMIT FILE SCAN - LAST FILE # TO BE DONE       90245
*/*      N=       IS NOT YET DEFINED - RESERVED FOR FUTURE USE   90245
*/*      U=       IS NOT YET DEFINED - RESERVED FOR FUTURE USE   90245
*/*      UNI=     TAPE DRIVE DEVICE TYPE ('3400-5', '3400-3', '3400-2')
*/*      DN=      TAPE DENSITY (4 - 6250 BPI, 3 - 1600, 2 - 800) 90245
*/*      CODE=    USE CODE=Q FOR ASCII TAPES                     90245
*/*      LBL=     BLP; OR LTM FOR DOS TAPES WITH LEADING TAPEMARK
*/*      START=   GIVES THE NUMBER OF THE FIRST BLOCK IN EACH FILE
*/*      END=     THE LAST BLOCK IN EACH FILE TO BE DUMPED       90245
*/*      INC=     INTERVAL FOR PRINTING BLOCKS (START+INC, ...)  90245
*/*      FULL=    NUMBER OF BYTES TO PRINT PER BLOCK (DFLT 1024) 90245
*/*      SKIP=YES   SKIP OVER DATA RECORDS (SKIPS ERROR CHECKING)
*/*      SKIP=NO    READ ALL DATA RECORDS                        90245
*/*      SKIP=SL    USE SKIP MODE FOR LABELLED TAPES; NO SKIP FOR NL
*/*      MAXPRT=    PRINT LIMIT COUNT FOR ERROR BLOCKS           90245
*/*      LABPRT=    PRINT LIMIT FOR BAD LABELS                   90245
*/*      LABERR=    TERMINATION LIMIT FOR LABEL ERRORS           90245
*/*      MAXERR=    TERMINATION LIMIT FOR DATA ERRORS            90245
*/*      CONSERR=   TERMINATION LIMIT FOR CONSECUTIVE ERRORS     90245
*/*      OTHER      ...ONE OR MORE OPTIONS IN AOPSTROPHES :      90245
*/*        TRUNCATE  TRUNCATE BLOCKS WITH BAD BLOCKSIZES         90245
*/*        FORCE     FORCES NL MODE; RUNS UNTIL LIMIT FILE (LF=) 90245
*/*        DEBUG     FORCES A DUMP FOR PROBLEM DIAGNOSIS         90245
*/*                                                              90245
*/*      BLK=       MAXIMUM BLOCKSIZE EXPECTED ON THE TAPE.      90245
*/*      BLK=0      SETS BLOCK SIZE TO 64K (FOR FDR DUMP TAPES); 90245
*/*                 REQUIRES MINIMUM 192K REGION                 90245
*/*                                                              90245
*/*                                                              90245
*                                                                90245
*        SAMPLE TAPE COPY PROCEDURE                              90245
*                                                                90245
*/SLCOPY  PROC IUN='3400-5',OUN='3400-5',IDE=3,ODE=4,ITR=ET,OTR=ET,
*/             LF=,N=1,U=1,LBL=LTM,IQ=,OQ=,                      90245
*/             START=1,END=1,INC=1,SKIP=SL,FULL=1024,ERRSIZE=1024,
*/             MAXPRT=5,LABPRT=15,LABERR=10,MAXERR=100,CONSERR=50,
*/             OTHER=,PRG=SLANCOPY,EROPT=ABE,BLK=32760,          90245
*/             ITAPE=USER01,OTAPE=COPY,IDSN='&&ANY',ODSN=,       90245
*/             TMSIN=',EXPDT=98000',TMSOUT=',EXPDT=98000'        90245
*/*                                                              90245
*/SLCOPY  EXEC PGM=&PRG,REGION=128K,                             90245
*/             PARM=('ST=&START','EN=&END','IN=&INC','SK=&SKIP', 90245
*/             'SZ=&FULL','ES=&ERRSIZE','MP=&MAXPRT',            90245
*/             'LP=&LABPRT','LR=&LABERR','MR=&MAXERR',           90245
*/             'CR=&CONSERR','&OTHER')                           90245
*/*                                                              90245
*/SUMPRINT DD  SYSOUT=*                                          90245
*/SYSPRINT DD  SYSOUT=*                                          90245
*/SYSUDUMP DD  SYSOUT=*                                          90245
*/SYSUT1   DD  UNIT=(&IUN,&U,DEFER),LABEL=(&LF,&LBL,&TMSIN),     90245
*/             DISP=OLD,DSN=&IDSN,                               90245
*/      DCB=(DEN=&IDE,TRTCH=&ITR,OPTCD=&IQ,EROPT=&EROPT,BLKSIZE=&BLK),
*/             VOL=(,,,&N,SER=(&ITAPE))                          90245
*/SYSUT2   DD  UNIT=(&OUN,&U,DEFER),LABEL=(,&LBL,&TMSOUT),       90245
*/             DISP=(OLD,KEEP),DSN=&ODSN,                        90245
*/             DCB=(DEN=&ODE,TRTCH=&OTR,OPTCD=&OQ,BLKSIZE=&BLK), 90245
*/             VOL=SER=(&OTAPE)                                  90245
*/*                                                              90245
*/*      THIS PROCEDURE COPIES INDIVIDUAL TAPES; MULTI-VOLUME FILES
*/*      ARE NOT SUPPORTED UNLESS EACH INDIVIDUAL TAPE CAN BE COPIED
*/*      SEPARATELY.  CHANGE OF FORMAT (EBCDIC TO USASCII AND V.V.)
*/*      AND DEVICE TYPE/DENSITY IS SUPPORTED, PROVIDING THE OUTPUT
*/*      FROM ANY TAPE WILL FIT ON A SINGLE OUTPUT REEL.         90245
*/*                                                              90245
*/*      PARAMETERS :                                            90245
*/*                                                              90245
*/*      IUN / OUN   UNIT TYPES VALID FOR REQUESTED DENSITY AND TRACK
*/*      IDE / ODE   DENSITY                                     90245
*/*      ITR / OTR   TRTCH VALUE FOR 7-TRACK REQUESTS            90245
*/*      IQ/OQ    SET =Q FOR USASCII TAPES                       90245
*/*      ITAPE       1 TO 5 VOLUME SERIALS ENCLOSED IN QUOTES    90245
*/*      OTAPE       IF OMITTED (=COPY), VOL HEADERS COPIED AS IS
*/*                  ELSE  1 TO 5 VOLUME SERIALS ENCLOSED IN QUOTES
*/*      ODSN=    DEFAULT DSN TO BE USED BY TMS FOR UNLABELLED TAPES
*/*      SKIP=     NOT MEANINGFUL (IGNORED)                      90245
*/*      LF=      LIMIT FILE SCAN - LAST FILE # TO BE DONE       90245
*/*      N=       IS NOT YET DEFINED - RESERVED FOR FUTURE USE   90245
*/*      U=       IS NOT YET DEFINED - RESERVED FOR FUTURE USE   90245
*/*      LBL=     BLP; OR LTM FOR DOS TAPES WITH LEADING TAPEMARK
*/*      START=   GIVES THE NUMBER OF THE FIRST BLOCK IN EACH FILE
*/*      END=     THE LAST BLOCK IN EACH FILE TO BE PRINTED      90245
*/*      INC=     INTERVAL FOR PRINTING BLOCKS (START+INC, ...)  90245
*/*      FULL=    NUMBER OF BYTES TO PRINT PER BLOCK (DFLT 1024) 90245
*/*      MAXPRT=    PRINT LIMIT COUNT FOR ERROR BLOCKS           90245
*/*      LABPRT=    PRINT LIMIT FOR BAD LABELS                   90245
*/*      LABERR=    TERMINATION LIMIT FOR LABEL ERRORS           90245
*/*      MAXERR=    TERMINATION LIMIT FOR DATA ERRORS            90245
*/*      CONSERR=   TERMINATION LIMIT FOR CONSECUTIVE ERRORS     90245
*/*      OTHER      ...ONE OR MORE OPTIONS IN AOPSTROPHES :      90245
*/*        TRUNCATE  TRUNCATE BLOCKS WITH BAD BLOCKSIZES         90245
*/*        FORCE     FORCES NL MODE; RUNS UNTIL LIMIT FILE (LF=) 90245
*/*        DEBUG     FORCES A DUMP FOR PROBLEM DIAGNOSIS         90245
*/*        SAME     IF INPUT AL OR AL3, COPY ANSI OUTPUT (ELSE SL)
*/*        ANSI1    USE 7-BIT TRANSLATE TABLE (NOT REVERSIBLE)   90245
*/*        ANSI3    USE 8-BIT TRANSLATION (ISCII)                90245
*/*                                                              90245
*/*      BLK=       MAXIMUM BLOCKSIZE EXPECTED ON THE TAPE.      90245
*/*      BLK=0      SETS BLOCK SIZE TO 64K (FOR FDR DUMP TAPES); 90245
*/*                 REQUIRES MINIMUM 192K REGION                 90245
*/*                                                              90245
*/*      EROPT=  ABE-ERROR COUNT/ABEND; SKP-SKIP ERRORS; ACC-ACCEPT
         EJECT ,                                                 82002
         PRINT &PRTSOR                                           82002
SLANALYZ SAVEM ZERO12,BASE=R10,BASE2=R11,BASE3=R12,PARM=R7,            *
               BNDRY=PAGE,ENTRY=SLANCOPY                         82002
         LA    R15,SLANCOPY  SET FOR COPY ENTRY                  82002
         L     R1,4(,R13)                                        82002
         CLM   R15,7,SAVE15+1-SAVE(R1)  COPY OR ANALYZE ENTRY ?  82002
         BNE   SLANCOMM      ANALYSIS REQUEST                    82002
         MVI   FLAG,FGCOPY   SET COPY MODE                       82002
SLANCOMM SERVINIT ,          LOAD THE SERVICE ROUTINE            82002
         SERVCALL LPALD,=CL8'@PRINTER'  LOAD THE PRINTER ROUTINE 82002
         ST    R0,@PRINTER                                       82002
         MVI   MSGUNIT,2     SET MESSAGES FOR SUMPRINT           82002
         PRTOPEN SUMPRINT,OPT=(NOWTO,DUMMY),DEV=2                82082
         CH    R15,=H'4'     SUMPRINT OR SYSTERM SUPPLIED ?      82002
         BH    NEEDSPRT      NO; MUST HAVE SYSPRINT              82002
         PRTOPEN SYSPRINT,OPT=(NOWTO,DUMMY)  OPTIONAL WITH SUM   82082
         B     SLANPCOM      GO TO COMMON                        82002
NEEDSPRT PRTOPEN SYSPRINT,OPT=(ABEND,NOWTO)  REQUIRED IF NO SUM  82002
         MVI   MSGUNIT,1     SET MESSAGES TO SYSPRINT            82002
SLANPCOM PRTOPEN CONSOLE,DEV=3  OPEN CONSOLE FOR OUTPUT          82002
         TESTAUTH FCTN=1     AUTHORIZED ?                        82002
         BXLE  R15,R15,SLANAUTH  YES; OK                         82002
         PRTV  MGBADAUT,DEV==MSGUNIT  AUTHORIZATION REQUIRED     82002
         SERVTERM ,          CLOSE FILES AGAIN                   82002
         ABEND 047           AUTHORIZATION REQUIRED              82002
         SPACE 1                                                 82002
SLANAUTH SERVCALL ACGET,DB   GET CURRENT ACCOUNT PRIVILEGES      82002
         STC   R0,AUTHFLAG   SAVE PRIVILEGE FLAG                 82002
         LA    R1,ACB                                            82002
         ST    R1,ECBADD+4                                       82002
         MVI   ECBADD+4,X'80'                                    82002
         LA    R1,1                                              82002
         STH   R1,MAXVOL                                         82002
         STH   R1,MAXNAM                                         82002
         MVC   MAXFIL,DC7FFF                                     82002
         MVI   DMSTART+3,1   SET TO DEFAULT OF 1                 82002
         MVI   DMEND+3,1                                         82002
         MVI   DMINC+3,1     SET DUMP INCREMENT                  82002
         MVC   MAXERR,=F'1000000'   NO ERROR TERMINATION         82002
         MVI   PRTERR+3,5    MAX BAD BLOCKS TO PRINT             82002
         MVC   MAXCONS,=F'1000000'  NO CONSECUTIVE ERROR TERMINATION
         MVI   MAXLBERR+3,15  MAX LABEL ERRORS TO QUIT ON        82002
         MVI   PRTLABEL+3,5    MAX BAD LABELS TO PRINT           82002
         OI    FLAG,FGSKIP   SET DEFAULT - SKIP MODE             82002
         MVC   ANSIVOL1(L'ANSIVOL1+L'ANSIHDR),DCVOL1  INIT VOL1/HDR
         L     R1,=A(TREBTOAS)                                   82002
         TR    ANSIVOL1(L'ANSIVOL1+L'ANSIHDR),0(R1)  MAKE ANSI HEADER
         PRTLIST VTITLE,TITLE=1,DEV=(1,2)  PRINT TITLE           82002
         SPACE 1
         LA    R7,0(,R7)     CLEAN PARM ADDRESS                  82002
         LTR   R7,R7         ANY PARM ADDRESS ?                  82002
         BZ    PARMDFLT      NO; USE DEFAULTS                    82002
         ICM   R7,7,1(R7)    LOAD AND TEST PARM ADDRESS          82002
         BZ    PARMDFLT      NONE                                82002
         SLR   R9,R9                                             82002
         ICM   R9,3,0(R7)    LOAD AND TEST PARM LENGTH           82002
         BZ    PARMDFLT      NONE                                82002
         LA    R8,100        OS MAXIMUM                          82002
         CR    R9,R8                                             82002
         BNH   *+6           OK                                  82002
         LR    R9,R8         TRUNCATE                            82002
         MVC   WORKLINE,BLANKS  CLEAR WORK AREA                  82002
         BCTR  R9,0          SET FOR EXECUTE                     82002
         EX    R9,PARMMOVE   MOVE PARM TO WORK AREA              82002
         LA    R7,WORKLINE+1  POINT TO START OF PARM             82002
         SLR   R8,R8         CLEAR BXLE INCREMENT REGISTER       82002
         AR    R9,R7         SET LAST PARM ADDRESS               82002
         SPACE 1                                                 82002
PARMLOOP LA    R4,PARMTAB    GET VERB TABLE FOR PARMS            82002
PARMVERB ICM   R8,1,0(R4)    INSERT AND TEST LENGTH BYTE         82002
         BM    PARMERR       NO MATCH - DISPLAY BAD DATA         82002
         EX    R8,PARMCLC    MATCH ?                             82002
         BE    PARMGO        YES; GO TO APPROPRIATE ENTRY        82002
         LA    R4,4(R8,R4)   POSITION TO NEXT PARM               82002
         B     PARMVERB      TRY NEXT ONE                        82002
PARMGO   SLR   R15,R15                                           82002
         ICM   R15,3,1(R4)   GET BRANCH OFFSET                   82002
         B     SLANALYZ(R15)  ENTER SUBROUTINE                   82002
PARMMOVE MVC   WORKLINE+1(0),2(R7)  MOVE PARM FIELD              82002
PARMCLC  CLC   0(0,R7),3(R4)        COMPARE VERB                 82002
PARMBADM VCON  ' ***** ERROR IN PARM FIELD *****'                82002
         SPACE 1                                                 82002
PARMTAB  VERBTAB ' ',PARMBUMP,BASE=*,LEN=2    SKIP BLANKS        82002
         VERBTAB ',',PARMBUMP                 SKIP COMMAS        82002
         VERBTAB 'SKIP=',PRMSKIP       YES/NO SKIP OPTION        82002
         VERBTAB 'SK=',PRMSKIP             ADDED FOR PARM < 100  82002
         VERBTAB 'START=',PRMSTART     START BLOCK NUMBER        82002
         VERBTAB 'ST=',PRMSTART            ADDED FOR PARM < 100  82002
         VERBTAB 'END=',PRMEND         END BLOCK NUMBER          82002
         VERBTAB 'EN=',PRMEND              ADDED FOR PARM < 100  82002
         VERBTAB 'INC=',PRMINC         INCREMENT                 82002
         VERBTAB 'IN=',PRMINC              ADDED FOR PARM < 100  82002
         VERBTAB 'ERRSIZE=',PRMFERR    SET ERROR PRINT SIZE      82002
         VERBTAB 'ES=',PRMFERR             ADDED FOR PARM < 100  82002
         VERBTAB 'SIZE=',PRMFULLI      DUMP SIZE OPTION          82002
         VERBTAB 'SZ=',PRMFULLI            ADDED FOR PARM < 100  82002
         VERBTAB 'FULL=',PRMFULLI      DUMP SIZE OPTION          82002
DCFULL   VERBTAB 'FULL',PRMFULL        DUMP FULL BLOCKS          82011
         VERBTAB 'MAXERR=',PRMMAXER    TERMINATION MAX ERRORS/FILE
         VERBTAB 'MR=',PRMMAXER            ADDED FOR PARM < 100  82002
         VERBTAB 'MAXPRT=',PRMPRTER    MAX BAD BLOCKS/FILE TO PRINT
         VERBTAB 'MP=',PRMPRTER            ADDED FOR PARM < 100  82002
         VERBTAB 'CONSERR=',PRMMAXCS   TERMINATION MAX CONSECUTIVE
         VERBTAB 'CR=',PRMMAXCS            ADDED FOR PARM < 100  82002
         VERBTAB 'LABERR=',PRMMAXLB    TERMINATION MAX LABEL ERRORS
         VERBTAB 'LR=',PRMMAXLB            ADDED FOR PARM < 100  82002
         VERBTAB 'LABPRT=',PRMPRTLB    MAX BAD LABELS TO PRINT   82002
         VERBTAB 'LP=',PRMPRTLB            ADDED FOR PARM < 100  82002
         VERBTAB 'TRUNCATE',PRMTRUNC   TRUNCATE BIG BLOCKS       82002
         VERBTAB 'TRUNC',PRMTRUNC                                82002
         VERBTAB 'FORCE',PRMFORCE      FORCED NL MODE            82002
         VERBTAB 'DEBUG',PRMDEBUG      SET DEBUG OPTION          82002
         VERBTAB 'CUCC',PRMCUCC        COLUMBIA U. KERMIT TAPE   84253
         VERBTAB 'UNSEC',PRMUNSEC                                89055
         VERBTAB 'ANSI1',PRMANSI1                                90243
         VERBTAB 'ANSI3',PRMANSI3                                90243
         VERBTAB 'SAME',PRMANSIS                                 90243
         DC    X'FF'         END OF TABLE                        82002
         SPACE 1                                                 82002
PARMERR  PRTV  PARMBADM,DEV==MSGUNIT    PRINT ERROR IN PARM      82002
         PRTF  WORKLINE,L'WORKLINE,DEV==MSGUNIT  SHOW FULL PARM  82002
         MVC   WORKLINE,BLANKS         CLEAR PARM FIELD          82002
         MVI   0(R7),C'|'    POINT TO BAD PARAMETER              82002
         PRTF  WORKLINE,L'WORKLINE,DEV==MSGUNIT  POINT TO ERROR  82002
         SERVTERM ,          CLOSE AND FREE                      82002
         TM    FLAG,FGDEBUG  DEBUG MODE ?                        82002
         BNZ   ABEND100      YES; TAKE A DUMP                    82002
         ABEND 100           TERMINATE                           82002
ABEND100 ABEND 100,DUMP                                          82002
         SPACE 1                                                 82002
PRMDEBUG OI    FLAG,FGDEBUG  SET DEBUG MODE                      82002
         B     PARMBUMP      DO ANOTHER                          82002
         SPACE 1                                                 84253
PRMCUCC  TM    FLAG,FGCOPY   SLCOPY ?                            84253
         BZ    PARMBUMP      NO; IGNORE FIX-UP REQUEST           84253
         OI    CLEANFG,FGCUCC  FIX COLUMBIA U. KERMIT TAPES      84253
         B     PARMBUMP      FOR RECFM=VB, LRECL IS 4 TOO SMALL  84253
         SPACE 1                                                 82002
PRMFORCE OI    FLAG,FGFORCE  SET FORCED NL MODE                  82002
         B     PARMBUMP      DO ANOTHER                          82002
         SPACE 1                                                 89055
PRMUNSEC TM    AUTHFLAG,VAASYS+VAASUP  AUTHORIZED USER ?         89055
         BZ    PARMERR       NO; FAIL                            89055
         OI    CLEANFG,FGUNSEC  PERMIT OVERRIDE                  89055
         B     PARMBUMP      MORE                                89055
         SPACE 1                                                 82002
PRMTRUNC OI    CLEANFG,FGTRUNC   TRUNCATE BIG BLOCKS             82002
         B     PARMBUMP                                          82002
         SPACE 1                                                 90243
PRMANSIS OI    FLAG2,FGSAME  RETAIN SAME CODE ON OUTPUT          90243
         B     PARMBUMP                                          90243
PRMANSI1 OI    FLAG2,FGANSI1  REGRESS OUTPUT TO ANSI 1           90243
         NI    FLAG2,255-FGANSI3  USER WOULDN'T DO THIS ?        90243
         B     PARMBUMP                                          90243
PRMANSI3 OI    FLAG2,FGANSI3  FORCE OUTPUT TO ANSI 3             90243
         NI    FLAG2,255-FGANSI1  USER WOULDN'T DO THIS ?        90243
         B     PARMBUMP                                          90243
         SPACE 1                                                 82002
PRMSKIP  OI    FLAG,FGSKIP   PROVISIONALLY SET SKIP MODE         82002
         LA    R7,1(R7,R8)   POINT TO NEXT PARM BYTE             82002
         BAL   R14,PARMNBLK  GET NEXT NON-BLANK                  82002
         SLR   R8,R8         CLEAR BUMP INCREMENT                82002
         CLI   0(R7),C','    NULL PARM ?                         82002
         BE    PARMBUMP      YES; SKIP OVER THE COMMA            82002
         CLI   0(R7),C' '    END OF PARM FIELD ?                 82002
         BE    PARMBUMP                                          82002
         LA    R8,2          L'YES-1                             82002
         CLC   =C'YES',0(R7) SKIP MODE ?                         82002
         BE    PARMBUMP      YES; ALL SET                        82002
         BCTR  R8,0          L'NO                                82002
         CLC   =C'SL',0(R7)  CONDITIONAL SKIP MODE ?             82002
         BE    PRMSKIPS      YES                                 82002
         CLC   =C'AL',0(R7)    DITTO                             82002
         BE    PRMSKIPS        DITTO                             82002
         CLC   =C'NO',0(R7)  NO ?                                82002
         BNE   PARMERR       NO; ERROR IN PARM FIELD             82002
         NI    FLAG,255-FGSKIP  RESET SKIP MODE                  82002
         B     PARMBUMP      SKIP TO NEXT PARM                   82002
PRMSKIPS OI    FLAG,FGSKIPSL   SET CONDITIONAL SKIP MODE         82002
         B     PARMBUMP      DO NEXT PARM                        82002
         SPACE 1                                                 82002
PRMSTART LA    R5,DMSTART    SET ADDRESS FOR START BLOCK         82002
         B     PARMINTG      GO TO COMMON INTEGER PROCESSING     82002
PRMEND   LA    R5,DMEND      SET ADDRESS FOR END PRINT BLOCK     82002
         B     PARMINTG                                          82002
PRMINC   LA    R5,DMINC      SET PRINT INCREMENT                 82002
         B     PARMINTG                                          82002
PRMFERR  LA    R5,ERRSIZE    SET FOR ERROR PRINT SIZE            82002
         B     PARMINTG                                          82002
PRMFULLI LA    R5,DMSIZE     SET FOR DUMP SIZE                   82002
         LA    R14,1(R7,R8)  POINT TO INPUT AFTER =              82011
         CLC   0(4,R14),DCFULL+3  IS IT SIZE=FULL ?              82011
         BNE   PARMINTG      NO; MUST BE NUMERIC                 82011
         LA    R8,4(,R8)     ADJUST LENGTH TO SKIP               82011
PRMFULL  OI    SLFG,SLFULL   SET FOR FULL BLOCK DUMPS            82002
         B     PARMBUMP                                          82002
PRMMAXER LA    R5,MAXERR     SET FOR MAX ERROR VALUE             82002
         B     PARMINTG                                          82002
PRMPRTER LA    R5,PRTERR     MAX ERROR BLOCKS PRINTED            82002
         B     PARMINTG                                          82002
PRMMAXCS LA    R5,MAXCONS    MAX CONSECUTIVE ERRORS TO QUIT      82002
         B     PARMINTG                                          82002
PRMMAXLB LA    R5,MAXLBERR   LABELS ERRORS TO QUIT ON            82002
         B     PARMINTG                                          82002
PRMPRTLB LA    R5,PRTLABEL   MAX LABELS TO DUMP ON ERROR         82002
*        B     PARMINTG                                          82002
         SPACE 1                                                 82002
PARMINTG LA    R7,1(R7,R8)   SPACE TO FIRST BYTE                 82002
         BAL   R14,PARMNBLK  GET FIRST NON-BLANK                 82002
         SLR   R0,R0                                             82002
         LA    R1,C'0'       PRESET FOR NULL INPUT               82011
         CLI   0(R7),C','    NULL ?                              82002
         BE    PARMINUL      YES                                 82002
         CLI   0(R7),C' '    END OF PARM ?                       82002
         BE    PARMINUL      YES; STOP                           82002
         LA    R8,1          SET INCREMENT FOR LOCAL BXLE        82002
         BCTR  R7,0          BACKSPACE                           82002
         LA    R14,8         MAX INTEGER LENGTH PERMITTED        82002
PARMILOP BXH   R7,R8,PARMIDON  SKIP IF DONE                      82002
         CLI   0(R7),C'0'    NUMERIC ?                           82002
         BL    PARMIDON      NO                                  82002
         CLI   0(R7),C'9'                                        82002
         BH    PARMIDON                                          82002
         SLDL  R0,8                                              82002
         IC    R1,0(,R7)     LOAD NEXT BYTE                      82002
         BCT   R14,PARMILOP  TRY AGAIN                           82002
         B     PARMINUL                                          82002
PARMIDON BCTR  R7,0          BACKSPACE                           82002
PARMINUL SLR   R8,R8         SET LENGTH-1 TO SKIP AFTER          82002
         STM   R0,R1,UPD     STASH VALUE                         82002
         PACK  DB,UPD                                            82002
         CVB   R0,DB                                             82002
         ST    R0,0(,R5)     STASH VALUE                         82002
         B     PARMBUMP      CONTINUE SCAN                       82002
         SPACE 1                                                 82002
PARMNBLK LA    R8,1          SET SKIP LENGTH                     82002
         BCTR  R7,0          BACKUP ONE                          82002
PARMNBLL BXH   R7,R8,0(R14)  RETURN IF END OF FIELD              82002
         CLI   0(R7),C' '    EMBEDDED BLANK ?                    82002
         BNER  R14           NO; RETURN                          82002
         B     PARMNBLL      ELSE TRY AGAIN                      82002
         SPACE 1                                                 82002
PARMBUMP LA    R8,1(,R8)     GET TRUE LENGTH                     82002
         BXLE  R7,R8,PARMLOOP  EXAMINE NEXT PARM                 82002
         SPACE 2                                                 82002
PARMDONE ICM   R0,15,DMSTART  DID USER CLOBBER START RECORD NUMBER ?
         BP    PARMDSTP      NO; USE IT                          82002
         LA    R0,1                                              82002
         ST    R0,DMSTART    FORCE RECORD 1 ANYWAY               82002
PARMDSTP CLC   DMSTART,DMEND   DID USER OVERRIDE START WITHOUT END ?
         BNH   PARMDFLT      NO; OK                              82002
         ICM   R0,15,DMEND   SET TO ZERO ?                       82002
         BZ    PARMDFLT      YES; REQUEST TO BYPASS DUMPING      82002
         MVC   DMEND,DMSTART SET FOR ONE PRINT BLOCK ANYWAY      82002
         SPACE 1                                                 *UOW*
PARMDFLT MVC   WORKLINE,BLANKS   BLANK WORK LINE AGAIN           82002
         MVI   VLDMSEP1,C'*'   PREPARE FOR DUMP                  82002
         MVI   VLDMSEP2,C'*'                                     82002
         LA    R0,1024       SET DEFAULT                         82002
         ICM   R1,15,DMSIZE  GET DUMP SIZE                       82002
         BNZ   PARMDSIZ      USER SUPPLIED                       82002
         LR    R1,R0         SET 1024 BYTE DEFAULT               82002
         TM    SLFG,SLFULL   DUMP FULL BLOCKS ?                  82002
         BZ    PARMDSIS      NO; STASH                           82002
         SLL   R1,6          SET TO 64K                          82002
PARMDSIS ST    R1,DMSIZE     SET DUMP SIZE                       82002
PARMDSIZ ICM   R1,15,ERRSIZE ERROR DUMP SIZE SUPPLIED ?          82002
         BNZ   PARMDERR      YES                                 82002
         ST    R0,ERRSIZE    SET 1024 BYTE DEFAULT               82002
PARMDERR ICM   R0,15,DMINC   GET PRINT INCREMENT                 82002
         BP    PARMDINC      OK ?                                82002
         LA    R0,1                                              82002
         ST    R0,DMINC      FORCE INCREMENT TO 1                82002
PARMDINC MVC   SYSUT1(DCBLEN),DCBPAT  SET DCBS                   82002
         MVI   DCBOPTCD-IHADCB+SYSUT1,0  CLEAR OPTCD FOR MERGE   82002
         MVC   SYSUT2(DCBLEN),DCBPAT  SET DCBS                   82002
         MVI   DCBOPTCD-IHADCB+SYSUT2,0                          85046
         MVC   SYSUT1DD,SYSUT1+DCBDDNAM-IHADCB                   82002
         MVC   SYSUT2+DCBDDNAM-IHADCB(8),SYSUT2DD                82002
         L     R1,=A(READEOF)                                    82002
         STCM  R1,7,SYSUT1+DCBEODA-IHADCB  EODAD                 82002
         LA    R1,INPEXLST                                       82002
         STCM  R1,7,SYSUT1+DCBEXLSA-IHADCB  EXLST                82002
         LA    R1,OUTEXLST                                       82002
         STCM  R1,7,SYSUT2+DCBEXLSA-IHADCB  EXLST - OUTPUT       82002
         LA    R14,OPENEXIT                                      82002
         LA    R15,INPJFCB                                       82002
         LA    R0,OPENXOUT                                       82002
         LA    R1,OUTJFCB                                        82002
         STM   R14,R1,INPEXLST   BUILD EXIT LISTS                82002
         MVI   INPEXLST,5     DCB EXIT IN                        82002
         MVI   OUTEXLST,5     DCB EXIT OUT                       82002
         MVI   INPEXLST+4,X'87'  JFCB IN                         82002
         MVI   OUTEXLST+4,X'87'  JFCB OUT                        82002
         LA    R0,SYSUT1                                         82002
         LA    R1,SYSUT2                                         82002
         STM   R0,R1,OPLSTIN   COMPLETE OPEN LISTS               82002
         MVI   OPLSTIN,X'80'   INPUT                             82002
         MVI   OPLSTOUT,X'8F'  OUTPUT                            82002
         SPACE 1
         EXTRACT DB,'S',FIELDS=COMM,MF=(E,UPD)                   82002
         L     R1,DB         GET ADDRESS OF COMM DATA LIST
         MVC   ECBADD+1(3),1(R1)   COPY COMM ECB ADDRESS
         MVI   DB+2,0        CLEAR                               82002
         DEVTYPE SYSUT1DD,DB   GET TIOT INFORMATION              82002
         CLI   DB+2,UCB3TAPE   TAPE ?                            82002
         BNE   INITNO1                                           82002
         RDJFCB MF=(E,OPLSTIN)                                   82002
         BXH   R15,R15,INITNO1  TOO BAD                          82002
         SPACE 1
         LA    R3,2          DO JFCB PROCESSING TWICE            82002
         TM    FLAG,FGCOPY   IS THIS A COPY OPERATION ?          82002
         BZ    INITBUMP      NO                                  82002
         MVI   DB+2,0        NOW REPEAT FOR OUTPUT TAPE          82002
         DEVTYPE SYSUT2DD,DB   GET OUTPUT TYPE                   82002
         CLI   DB+2,UCB3TAPE  TAPE ?                             82002
         BNE   INITNO2                                           82002
         RDJFCB MF=(E,OPLSTOUT)                                  82002
         BXH   R15,R15,INITNO2   TOO BAD                         82002
         NI    FLAG,255-FGSKIP  KILL SKIP FLAG                   82002
         LA    R9,OUTJFCB                                        82002
         USING INFMJFCB,R9   DECLARE THE JFCB MAPPING            82002
         LA    R1,1                                              82002
         STH   R1,JFCBFLSQ   SET FILE SEQUENCE TO 1              82002
         STC   R1,JFCBVLCT   FIRST OUTPUT VOLUME                 82002
         STC   R1,JFCBNVOL   1 OUTPUT VOLUME                     82002
         CLC   =C'ASIS ',JFCBVOLS  COPY ALL VOLUMES AS IS ?      82045
         BNE   TMSCOPYT      NO                                  82045
         MVC   JFCBVOLS,JFCBVOLS-INFMJFCB+INPJFCB  COPY VOLSERS  82045
         TM    AUTHFLAG,VAASYS+VAASUP  SPECIAL USER ?            82045
         BZ    TMSCOPYT      NO                                  82045
         OI    CLEANFG,FGASIS  PERMIT ASIS COPY OF TMS TAPES     82045
TMSCOPYT CLC   =C'COPY ',JFCBVOLS  COPY VOLUME SERIAL ?          82045
         BNE   *+10          NO; USE USER SERIAL                 82002
         MVC   JFCBVOLS(6),BLANKS   ELSE SET TO BLANKS           82002
         TM    JFCBLTYP,JFCBAL  ANSI LABEL ?                     86204
         BZ    *+8           NO                                  86204
         OI    JFCOPTCD,JFCOPTQ  REMEMBER IT                     86204
         MVI   JFCBLTYP,JFCBLP   PREVENT ANY LABEL PROCESSING    82002
         CLC   =X'620000',JFCBXPDT  DEFAULT EXPDT ?              80048
         BE    TMSCRUD       YES; OVERRIDE FOR SCRATCH           80048
         CLC   JFCBXPDT,ZEROES   USER SPECIFIED EXPIRATION DATE ?
         BNE   INITJLOP      YES; LEAVE AS IS                    80048
TMSCRUD  MVC   JFCBXPDT,=X'620000'  DEFAULT TO 98000 (NON-TMS)   80048
         LA    R1,JFCBVOLS   POINT TO VOLSER                     82045
         LR    R5,R13        SAVE SAVE AREA                      90245
         LA    R13,SUBSAVE   USE SUB-TASKS ADDRESSING            90245
         BAL   R14,CHECKTMS  SEE IF TMS TAPE                     90243
         B     INITJDAY      SET DEFAULT RETENTION FOR SCRATCH   82045
         B     INITJDAY      DITTO FOR IN-HOUSE (TMS) TAPE       82045
         B     INITJLOD      ELSE LEAVE AS IS                    90245
INITJDAY MVC   JFCBXPDT+1(2),=H'365'  ONE-YEAR RETENTION         82002
INITJLOD LR    R13,R5        RESTORE OUR SAVE/WORK AREA          90245
         SPACE 1                                                 82002
INITJLOP TM    JFCBIND2,JFCTEMP  DID USER SUPPLY DEFAULT NAME ?  81030
         BZ    INITJPRM      YES; USE IT                         81030
         MVC   JFCBDSNM,BLANKS   BLANK THE DSN FIELD             82002
         MVC   JFCBDSNM(8),=CL8'SLANAL.T'                        *TSM*
         MVC   JFCBDSNM+8(6),JFCBVOLS                            *TSM*
INITJPRM NI    JFCBIND2,255-JFCNEW-JFCSHARE-JFCTEMP              81030
         OI    JFCBIND2,JFCNEW         FORCE NEW DISP            82002
         OI    JFCRECFM,JFCUND   FORCE UNDEFINED
         OC    ERROPT,JFCEROPT   SAVE DCB EROPT                  83353
         MVC   MAXNAM+1(1),JFCBNVOL   MOVE NO. OF VOLUME NAMES
         MVI   JFCBNVOL,1    CURRENTLY FORCE ONE
         MVI   JFCBVLCT,1
INITBUMP LA    R9,INPJFCB    GET INPUT JFCB                      82002
         BCT   R3,INITJLOP   AND INITIALIZE IT                   82002
         TM    JFCBLTYP,JFCBAL  ANSI LABEL ?                     86204
         BZ    *+8           NO                                  86204
         OI    JFCOPTCD,JFCOPTQ  REMEMBER IT                     86204
         MVC   USERJFCB(JFCBLGTH),OUTJFCB   SAVE USER JFCB       82002
         TM    JFCBLTYP,JFCBLTM   LEADING TAPEMARKS EXPECTED ?   82002
         BZ    INITBLPS      NO; JUST SET BLP                    82002
         OI    FLAG,FGLTM    YES; DON'T TERMINATE ON LEADING EOF 82002
INITBLPS MVI   JFCBLTYP,JFCBLP   USE BLP MODE IN EITHER CASE     82002
         ICM   R3,3,JFCBFLSQ   GET FILE SEQ (LIMIT FILE #)       82002
         BNP   INITNSEQ      NOT SPECIFIED - DO ALL FILES        82002
         STH   R3,MAXFIL     SAVE LIMIT NUMBER                   82002
         SPACE 1
INITNSEQ L     R5,=F'65535'  LOAD MAXIMUM DESIRED SIZE           82002
         LA    R6,256        LOAD DESIRED MINIMUM                82002
         LH    R4,JFCBLKSI   GET USER SPECIFIED INPUT BLKSIZE    82002
         LTR   R4,R4         ANY ?                               82002
         BP    INITMBLK      SPECIFIED                           82002
         LR    R4,R5         USE MAX AS DEFAULT                  82002
INITMBLK ST    R4,MAXBLKUS   SAVE USER'S MAXIMUM BLOCKSIZE       82002
         LA    R4,8(,R4)     UP BY 8 ANYWAY                      82002
         CR    R4,R6         AT LEAST MINIMUM ?                  82002
         BNL   INITNBLK      YES                                 82002
         LR    R4,R6         SET MINIMUM                         82002
INITNBLK CR    R4,R5         GREATER THAN MAX ?                  82002
         BNH   INITLBLK      NO                                  82002
         LR    R4,R5         ELSE USE MAXIMUM                    82002
INITLBLK ST    R4,MAXBLKSI   SAVE SIZE                           82002
         LA    R4,BUFDATA-IOBUF+7(,R4)  MAKE WORK BLOCK SIZE     82002
         SRL   R4,3          ROUND TO MULTIPLE                   82002
         SLL   R4,3          OF EIGHT                            82002
         ST    R4,BUFINC     SAVE INCREMENT                      82002
         LR    R6,R4         COPY                                82002
         LR    R7,R4         COPY                                82002
         MH    R7,=H'20'     SET FOR REASONABLE MAXIMUM          82002
         LH    R5,=H'8192'   SET FOR SYSTEM OVERHEAD             82002
         AR    R6,R5         MAKE MINIMUM SIZE                   82002
         AR    R7,R5         MAKE MAXIMUM USEFUL SIZE            82002
         L     R1,=A(256*1024)  SET MAXIMUM TO FIX               82002
         CR    R6,R1         HIGHER ?                            82002
         BNH   *+6           NO                                  82002
         LR    R6,R1         REDUCE                              82002
         CR    R7,R1         HIGHER ?                            82002
         BNH   *+6           NO                                  82002
         LR    R7,R1         SET MAXIMUM                         82002
         STM   R6,R7,DB      SET MIN AND MAX REQUEST             82002
         LA    R6,DB                                             82002
         LA    R7,MAINLY                                         82002
         MVC   MAINLINE(PATMAINL),PATMAIN  MOVE GETMAIN PATTERN  90243
         GETMAIN VC,LA=(R6),A=(R7),MF=(E,MAINLINE)  GET STORAGE  90243
         BXH   R15,R15,FUNNY804  GET OUT IF NOT ENOUGH           82002
         L     R7,MAINLY+4   GET STORAGE SIZE GOTTEN             82002
         SR    R7,R5         ALLOW FOR SYSTEM OVERHEAD           82002
         SLR   R6,R6                                             82002
         DR    R6,R4         GET NUMBER OF BUFFERS               82002
         LTR   R7,R7         SEE IF AT LEAST MINIMUM             82025
         BNP   FUNNY804      FAIL ?                              82025
         TM    FLAG,FGCOPY   COPY MODE ?                         86096
         BZ    CRYPTIC       NO                                  86096
         LA    R0,2          SET FOR MAXIMUM SAFE BUFFERS        86096
         CR    R7,R0         DUE TO ERROR IN RESTARTING AN       86096
         BNH   CRYPTIC       OUTPUT RETRY                        86096
         LR    R7,R0         TRUNCATE                            86096
CRYPTIC  ST    R7,MAXEXCP    SET NUMBER OF BUFFERS               82002
         AR    R5,R6         GET SIZE TO RELEASE                 82002
         LM    R6,R7,MAINLY  GET ORIGINAL VALUES BACK            82002
         SR    R7,R5         GET NEW LENGTH                      82002
         ST    R7,MAINLY+4   STASH IT                            82002
         AR    R6,R7         SET STARTING ADDRESS TO FREE        82002
         FREEMAIN R,LV=(R5),A=(R6)  FREE SYSTEM AND UNUSED SPACE 82002
         LM    R14,R15,MAINLY   GET ADDRESS AND SIZE BACK        82002
         CLRL  (R14),(R15)   CLEAR STORAGE FOR SHORTER DUMPS     82002
         LH    R1,MAXNAM     GET NUMBER OF VOLUMES
         LA    R0,5          SET MAX IN ONE JFCB                 82002
         CR    R1,R0         MORE THAN PRIMARY JFCB ?            82002
         BNH   *+6           NO, OK                              82002
         LR    R1,R0         ONLY FIRST FIVE VOL-SERS ACCESSIBLE 82002
         LTR   R1,R1         AT LEAST ONE ?                      82002
         BP    STASHMAX      YES; SET VOLUMES TO PROCESS         82002
         LA    R1,1          USE ONE
         MVC   JFCBVOLS(6),=CL6'TAPE-1'   USE FAKE VOL-SER
STASHMAX STH   R1,MAXNAM     SET NUMBER                          82002
         STH   R1,MAXVOL
         SPACE 1                                                 82002
         IDENTIFY EP=SLCOPY,ENTRY=SLCOPY                         82002
         PRTLIST OPTFD,DEV==MSGUNIT  DISPLAY THE RUN OPTIONS     82002
         SPACE 2
NEXTTAPE LA    R1,1          GET VOLUME COUNT                    82002
         AH    R1,CURVOL     SET FOR NEXT ONE                    82002
         STH   R1,CURVOL     SAVE
         STC   R1,JFCBVLCT-INFMJFCB+INPJFCB                      82002
         CH    R1,MAXVOL     DONE ?
         BH    EXITS                                             82002
         MVC   OUTJFCB(JFCBLGTH),USERJFCB  REFRESH OUTPUT JFCB   82002
         MVC   JFCBVOLS-INFMJFCB+OUTJFCB(30),BLANKS              82002
         LA    R9,INPJFCB    GET JFCB STORAGE ADDRESS            82002
         USING INFMJFCB,R9   DECLARE IT
         LA    R3,6
         MR    R2,R1
         LA    R2,JFCBVOLS-INFMJFCB+USERJFCB-6(R3)               82002
         CLC   =C'COPY ',0(R2)    SCRATCH ?                      82002
         BE    *+10          YES; NO CHANGE                      82002
         MVC   JFCBVOLS-INFMJFCB+OUTJFCB(6),0(R2)  SET OUTPUT VOLUME
         OI    JFCBIND2-INFMJFCB+OUTJFCB,JFCNEW  SET NEW FOR SCRATCH
         CLI   JFCBVOLS-INFMJFCB+OUTJFCB,C' '   SCRATCH ?        82002
         BNH   *+12          YES                                 82002
         NI    JFCBIND2-INFMJFCB+OUTJFCB,255-JFCNEW              82002
         OI    JFCBIND2-INFMJFCB+OUTJFCB,JFCOLD  OLD FOR TMS REWRITE
         LA    R3,JFCBVOLS-6(R3)   GET NEXT ONE
         CH    R1,MAXNAM
         BNH   VOLSINIT
         SPACE 1
FAKESER  MVC   0(6,R3),=C'SL 000'  GARBAGE VOLSER                82002
         MVN   5(1,R3),CURVOL+1  MAKE RELATIVE VOLUME COUNT      82002
VOLSINIT LA    R0,1          SET CURRENT FILE NUMBER             82002
         STH   R0,JFCBFLSQ   INPUT JFCB                          82002
         STH   R0,JFCBFLSQ-INFMJFCB+OUTJFCB                      82002
         OI    JFCBTSDM,JFCVSL
         MVC   JFCBVOLS(6),0(R3)   MOVE VOL-SER
         MVC   VSSER,0(R3)
         PRTLIST VSUB,TITLE=3,DEV=1   PRINT PROVISIONAL SUBTITLE 82002
         XC    CLRVOL(CLRVOLL),CLRVOL  CLEAR VOLUME DATA         82002
         LA    R0,1          SET CURRENT FILE NUMBER             82002
         STH   R0,LOGFIL     LOGICAL FILE NUMBER                 82002
         STH   R0,PHYSFIL    PHYSICAL FILE NUMBER                82002
         MVC   RECFM,BLANKS  CLEAR RECFM F FLAG                  82002
         MVI   SAVEVOL1,C' '                                     82002
         MVC   SAVEVOL1+1(3*L'SAVEVOL1-1),SAVEVOL1  CLEAR LABELS 82002
         L     R2,MAXEXCP    GET NUMBER OF BUFFERS               82002
         L     R8,MAINLY     GET FIRST ONE                       82002
         USING IOBUF,R8                                          82002
         BAL   R9,INITBUFF   INITIALIZE BUFFER CHAIN             82002
         MVC   BUFLINK,MAINLY  TO POINT TO FIRST                 82002
         MVC   CURBLOCK,MAINLY  SET TO FIRST BLOCK START         82002
         LA    R2,1          SET FOR ONE SPECIAL IOB             82002
         LA    R8,SPECIOB    POINT TO IT                         82002
         BAL   R9,INITBUFF   INITIALIZE IT                       82002
         ST    R8,BUFLINK    CHAIN IT TO ITSELF                  82002
         MVC   SYSUT1(DCBLEN),DCBPAT  SET DCBS                   82002
         MVI   DCBOPTCD-IHADCB+SYSUT1,0  CLEAR OPTCD FOR MERGE   82002
         MVC   SYSUT2(DCBLEN),DCBPAT  SET DCBS                   82002
         MVI   DCBOPTCD-IHADCB+SYSUT2,0                          85046
         MVC   SYSUT1DD,SYSUT1+DCBDDNAM-IHADCB                   82002
         MVC   SYSUT2+DCBDDNAM-IHADCB(8),SYSUT2DD                82002
         L     R1,=A(READEOF)                                    82002
         STCM  R1,7,SYSUT1+DCBEODA-IHADCB  EODAD                 82002
         LA    R1,INPEXLST                                       82002
         STCM  R1,7,SYSUT1+DCBEXLSA-IHADCB  EXLST                82002
         LA    R1,OUTEXLST                                       82002
         STCM  R1,7,SYSUT2+DCBEXLSA-IHADCB  EXLST - OUTPUT       82002
         NI    FLAG,255-FGEOV-NEWVOL  RESET VOLUME CONTROL FLAGS 82002
         DROP  R9
         SPACE 1
*        ATTACH ROUTINE
         SPACE 1
         MVC   LISTATT(PATATTL),PATATT  MOVE ATTACH LIST         90245
         XC    ACB,ACB       CLEAR SUB-TASK COMPLETION ECB       82002
         MVI   IOFG,0      CLEAR I/O FLAG                        82002
         LA    R1,SUBSAVE    PASS WORK AREA ADDRESS TO SUBTASK   82002
         LA    R2,ACB                                            82002
         ATTACH EP=SLCOPY,ECB=(R2),SF=(E,LISTATT)                90245
         ST    R1,TCBSAVE              SAVE FOR DETACH
ATTWAIT  WAIT  ECBLIST=ECBADD
         TM    ACB,X'40'     SUB-TASK COMPLETION ?
         BO    ATTDET        YES - TERMINATE NORMALLY
         MVC   ACB(4),=X'40222000'   ELSE 'STOP' ISSUED BY OPSIE
         OI    CC,CCWARN     SET WARNING CODE                    82002
         SPACE 1
ATTDET  DETACH TCBSAVE
         L     R1,ECBADD     GET COMM ECB
         TM    0(R1),X'40'   WAS IT POSTED ?
         BZ    ATTMSGT       NO
         PRTV  OPSTOP,DEV=(1,2)  SHOW WHY                        82002
ATTMSGT  ICM   R2,7,ACB+1    LOAD AND TEST COMPLETION CODE       82004
         BZ    NEXTTAPE                                          82002
         SLL   R2,4          SHIFT SYSTEM CODE                   82004
         STCM  R2,12,DB+2    PUT PART OF SYSTEM CODE IN DB+2     82004
         OI    CC,CCFATAL    SET MAJOR ERROR CODE                82002
         MVC   DB(2),ACB+2   COPY USER COMPLETION CODE FIELD     82002
         NI    DB,X'0F'      LEAVE ONLY USER CODE
         MVC   DB+4(2),ACB+1  COPY LEADING PORTION OF SYS CODE   82002
         NI    DB+5,X'F0'    REMOVE USER PORTION                 82002
         PRTLIST VFRABE,DEV=(1,2,3)                              82002
         TM    FLAG,FGDEBUG  DEBUG MODE ?                        82002
         BZ    ATTNDUMP      NO; DON'T TAKE DUMP                 82002
         PRTCLOSE DEV=ALL    CLOSE PRINT FILES                   82002
         L     R1,ACB        GET SUBTASK RETURN CODE             82004
         ICM   R1,8,=X'90'   SET STEP TERMINATION                82004
         SVC   13            AND QUIT THE NASTY WAY              82004
ATTNBOMB PRTCLOSE DEV=ALL    CLOSE PRINT FILE                    82004
         L     R1,ACB        ECHO SUBTASK ERROR CODE             82004
         SVC   13            AND QUIT                            82004
ATTNDUMP CLI   ACB+1,X'0C'   PROGRAM INTERRUPT ?                 82002
         BE    ATTNBOMB      YES, GET OUT                        82002
         CLI   DB+3,X'13'    X13 ABEND ?                         82004
         BE    ATTNBOMB      YES; DUMP                           82004
         CLI   DB+3,X'14'    X14 ABEND ?                         82004
         BE    ATTNBOMB      YES; BOMB                           82004
         CLI   DB+3,X'37'    X37 ABEND ?                         82004
         BE    ATTNBOMB      YES; BOMB                           82004
         CLI   DB+3,X'22'    X22 ABEND ?                         82004
         BE    EXITS         YES, EXIT                           82002
         PRTV  ASSEND,DEV=(1,2)   PRINT END-TAPE ASSUMED         82004
         B     NEXTTAPE                                          82002
         SPACE 1                                                 82002
INITBUFF L     R4,BUFINC     GET WORK BLOCK SIZE                 82002
INITBUFL LA    R5,0(R8,R4)   LOAD NEXT BUFFER ADDRESS            82002
         ST    R5,BUFLINK    SET LINK FIELD AND ZERO FLAG        82002
         LA    R1,BIOECB                                         82002
         ST    R1,BIOECBPT   INIT IOB                            82002
         LA    R1,1                                              82002
         STH   R1,BIOINCAM                                       82002
         LM    R14,R15,=X'020000002000FFFF'                      82002
         ICM   R15,3,MAXBLKSI+2  GET MAXIMUM BUFFER SIZE         82002
         STM   R14,R15,READCCW                                   82002
         STM   R14,R15,RITECCW                                   82002
         MVI   RITECCW,1     MAKE WRITE OP                       82002
         MVC   WTMCCW(16),READCCW                                82002
         MVI   WTMCCW,X'1F'  SET WTM                             82002
         MVI   WTMCCW+4,X'60'   SLI/CMD CHAIN                    82002
         MVI   WTMCCW+8,X'03'   NOP                              82002
         LA    R1,BUFDATA    SET INPUT AREA                      82002
         STCM  R1,7,READCCW+1  INTO DECB                         82002
         STCM  R1,7,RITECCW+1  INTO CCW                          82002
         LR    R8,R5         SET NEXT BUFFER ADDRESS             82002
         BCT   R2,INITBUFL   DO ALL                              82002
         SR    R8,R4         GET LAST WORK BLOCK                 82002
         BR    R9            RETURN TO CALLER                    82002
         SPACE 2
INITNO2  LA    R3,SYSUT2DD   POINT TO SYSUT2                     82002
         B     INITNODD      GO TO MESSAGE FORMATTING            82002
INITNO1  LA    R3,SYSUT1DD   SET FOR 'SYSUT1'                    82002
INITNODD PRTLIST UT1ERR,DEV==MSGUNIT   PROBLEM WITH SYSUT1/SYSUT2
         SPACE 1                                                 82002
EXIT16   MVI   CC,CCFATAL    SET MAJOR ERROR                     82002
EXITS    TM    FLAG,FGCOPY   COPY MODE ?                         89055
         BNZ   EXITSC        YES; KILL TYPE BITS                 89055
         CLI   CC,4          WARNING, ERROR, ETC. ?              89055
         BNH   EXITS3        NO                                  89055
EXITSC   NI    CC,255-3      RESET NL,ASCII BITS                 89055
EXITS3   ICM   R9,15,RETCODE    GET RETURN CODE                  82002
         CH    R9,=H'4'      FLAGS ONLY ?                        89051
         BL    EXITS0        YES; DON'T PRINT ERROR MESSAGE      89051
         PRTLIST COMPCODE,DEV=(1,2)                              82002
EXITS0   SERVTERM ,          CLOSE AND FREE EVERYTHING           82002
         ENDM  RC=(R9)       RETURN WITH CODE                    82002
         SPACE 1                                                 82002
FUNNY804 PRTLIST NOSTORGE,DEV==MSGUNIT  INDICATE REGION TOO SMALL
         SERVTERM ,                                              82002
         ABEND 804                                               82002
         SPACE 1                                                 82002
SYSPRINT PRTWORK SYSPRINT,TITLE=5                                82002
SUMPRINT PRTWORK SUMPRINT,TITLE=3                                82002
CONSOLE  PRTWORK *CONSOLE                                        82002
PATMAIN  GETMAIN VC,LA=1234,A=1234,MF=L                          90243
PATMAINL EQU   *-PATMAIN                                         90243
PATATT   ATTACH EP=SLCOPY,ECB=1234,SZERO=NO,SHSPV=0,DPMOD=+4,SF=L
PATATTL  EQU   *-PATATT                                          90245
         SPACE 1                                                 82002
DCBPAT   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=(E),REPOS=N,OPTCD=Z,       *
               EODAD=READEOF,DEVD=TA                             82002
PATDDNM2 DC    CL8'SYSUT2'                                       82002
DCBLEN   EQU   *-DCBPAT                                          82002
         SPACE 2                                                 82002
         USING IHADCB,R1                                         82002
OPENEXIT DS    0H                                                82002
OPENXOUT NI    DCBOPTCD,DCBOPTQ+DCBOPTZ  LEAVE ASCII AND RER     82002
         OI    DCBOPTCD,DCBOPTZ        SET REDUCED ERROR RECOVERY
         BR    R14           RETURN                              82002
         DROP  R1                                                82002
         SPACE 1                                                 82002
MGBADAUT VCON  '0PROGRAM NOT AUTHORIZED'                         82002
VTITLE   FDOPT NL,CC=C'#'    SET AUTOMATIC PAGE/DATE/TIME OPTION 82002
         FDPRT 'STANDARD LABEL TAPE ANALYSIS',RADJ,LEN=43        82002
         FDTM  FLAG,FGCOPY,BZ=VTITLEO   SKIP IF NOT COPY         82002
         FDPRT 'AND COPY',PADL                                   82002
VTITLEO  FDPRT *END                                              82002
VSUB     FDPRT ' VOLUME',PADR,NL      PROVISIONAL SUBTITLE       82002
         FDPRT JFCBVOLS-INFMJFCB+INPJFCB,6                       82002
         FDPRT *END                                              82002
NOSTORGE FDOPT NL,CC=C'0'    TRIPLE-SPACE                        82002
         FDPRT '***** REGION TOO SMALL FOR BUFFER SIZE ('        82002
         FDPRT MAXBLKSI,I                                        82002
         FDPRT ')'                                               82002
         FDPRT *END                                              82002
OPSTOP   VCON  '0***** PROGRAM STOPPED BY OPERATOR'              82002
ASSEND   VCON  ' ***** END OF VOLUME ASSUMED'                    82002
UT1ERR   FDOPT NL,CC=C'0'                                        82002
         FDPRT '*****',PADR                                      82002
         FDPRT 0(R3),8,DEB,PAD   DD NAME                         82002
         FDPRT 'DD CARD MISSING OR BAD',PADL                     82002
         FDPRT *END                                              82002
         SPACE 1
VFRABE   FDOPT NL,CC=C'0'    DOUBLE-SPACE                        82002
         FDPRT 'SUBTASK ABEND',PADR                              82002
         FDCLC DB+4,ZEROES,2,BE=VFRABEU  SKIP IF ZERO SYSTEMS CODE
         FDPRT 'SYSTEM',PAD                                      82002
         FDPRT DB+4,2,HEX,LEN=3   SYSTEM CODE                    82002
VFRABEU  FDCLC DB,ZEROES,2,BE=VFRABEZ  SKIP IF ZERO USER CODE    82002
         FDPRT 'USER',PAD                                        82002
         FDPRT DB,2,I,PAD                                        82002
VFRABEZ  FDPRT 'DURING',PAD                                      82002
         FDTM  IOFG,IOOPEN,BZ=VFRABNOP   OPEN FLAG ON ?          82002
         FDPRT 'OPEN',PAD                                        82002
         FDPRT *END                                              82002
VFRABNOP FDTM  IOFG,IOCLOSE,BZ=VFRABNCL  CLOSE ON ?              82002
         FDPRT 'CLOSE',PAD                                       82002
         FDPRT *END                                              82002
VFRABNCL FDTM  IOFG,IOREAD,BZ=VFRABNRD  DURING READ ?            82002
         FDPRT 'READ',PAD                                        82002
         FDPRT *END                                              82002
VFRABNRD FDPRT 'PROCESSING',PADL                                 82002
         FDPRT *END                                              82002
         SPACE 1                                                 82002
OPTFD    FDPRT 'OPTIONS TO BE USED ON THIS RUN :',NL             82002
         FDPRT 'BLOCKS',PAD                                      82002
         FDPRT DMSTART,I                                         82002
         FDPRT 'THROUGH',PAD                                     82002
         FDPRT DMEND,I                                           82002
         FDPRT 'WILL BE PRINTED',PAD                             82002
         FDCLC DMINC,DCF1,4,BL=OFDINC,BE=OFDINC                  82002
         FDPRT 'IN INCREMENTS OF',PAD                            82002
         FDPRT DMINC,I                                           82002
OFDINC   FDOPT IND=10        SET AUTOMATIC INDENT                82002
         FDPRT 'UP TO',NL,PAD                                    82002
         FDPRT DMSIZE,I                                          82002
         FDPRT 'BYTES OF EACH RECORD WILL BE PRINTED;  ',PADL    82002
         FDPRT ERRSIZE,I,PAD                                     82002
         FDPRT 'BYTES OF BAD RECORDS WILL BE DUMPED'             82002
         FDTM  ERROPT,JFCABN,BO=OFDERABE   EROPT=ABE (DEFAULT)   83353
         FDTM  ERROPT,JFCACC+JFCSKP,BZ=OFDERABE                  83353
         FDPRT 'RECORDS WITH I/O ERRORS WILL BE',PADR,NL         83353
         FDTM  ERROPT,JFCACC,BO=OFDERACC   EROPT=ACCEPT          83353
         FDPRT 'COUNTED AND SKIPPED'                             83353
         FDGOTO OFDERABE                                         83353
OFDERACC FDPRT 'ACCEPTED AS READ INTO STORAGE'                   83353
OFDERABE FDPRT 'TAPE PROCESSING WILL STOP AFTER',PADR,NL         83353
         FDTM  ERROPT,JFCABN,BO=OFDERABF                         83353
         FDTM  ERROPT,JFCACC+JFCSKP,BM=OFDERABG,BO=OFDERABG      83353
OFDERABF FDPRT MAXERR,I,PAD                                      83353
         FDPRT 'DATA OR',PAD                                     82002
OFDERABG FDPRT MAXLBERR,I,PAD                                    83353
         FDPRT 'LABEL ERRORS OR'                                 82002
         FDPRT MAXCONS,I,PAD                                     82002
         FDPRT 'CONSECUTIVE ERRORS'                              82002
         FDPRT 'PRINTING OF BAD RECORDS WILL STOP AFTER',PADR,NL 82002
         FDPRT PRTERR,I,PAD                                      82002
         FDPRT 'DATA OR',PAD                                     82002
         FDPRT PRTLABEL,I,PAD                                    82002
         FDPRT 'LABEL ERRORS'                                    82002
         FDPRT MAXEXCP,I,NL                                      82002
         FDPRT 'BUFFERS OF SIZE',PAD                             82002
         FDPRT MAXBLKSI,I                                        82002
         FDPRT 'HAVE BEEN ALLOCATED;    RECORDS LONGER THAN',PAD 82002
         FDPRT MAXBLKUS,I,PAD                                    82002
         FDPRT 'BYTES WILL',PAD                                  82002
         FDTM  CLEANFG,FGTRUNC,BZ=OFDOTRNC                       82002
         FDPRT 'BE TRUNCATED'                                    82002
         FDGOTO OFDONTRC                                         82002
OFDOTRNC FDPRT 'NOT BE PROCESSED'                                82002
OFDONTRC FDTM  CLEANFG,FGCUCC,BM=OFDOSOPT,BO=OFDOSOPT            84253
         FDTM  FLAG,255-FGCOPY,BZ=OFDNOPT                        82002
OFDOSOPT FDOPT IND=0         RESET INDENT                        84253
         FDPRT 'SPECIAL PROCESSING OPTIONS :',PADR,NL            82002
         FDOPT IND=10        SET INDENT                          82002
         FDTM  FLAG,FGLTM,BZ=OFDOLTM                             82002
         FDPRT 'LEADING TAPEMARKS PERMITTED'                     82002
         FDOPT NL                                                82002
OFDOLTM  FDTM  FLAG,FGSKIP,BZ=OFDOSKP                            82002
         FDPRT 'RECORD AND FILE SPACING WILL BE USED'            82002
         FDTM  FLAG,FGSKIPSL,BZ=OFDONSKP                         82002
         FDPRT 'FOR LABELLED TAPES',PADL                         82002
OFDONSKP FDOPT NL                                                82002
OFDOSKP  FDTM  FLAG,FGFORCE,BZ=OFDOFOR                           82002
         FDPRT 'FULL VOLUME PROCESSING FORCED'                   82002
         FDOPT NL                                                82002
OFDOFOR  FDTM  FLAG,FGDEBUG,BZ=OFDOANS                           85046
         FDPRT 'DEBUG MODE REQUESTED'                            82002
         FDOPT NL                                                82002
OFDOANS  FDTM  JFCOPTCD+OUTJFCB-INFMJFCB,DCBOPTQ,BZ=OFDONAM      90243
         FDPRT 'OUTPUT WILL BE TRANSLATED TO ASCII/ISCII'        90243
         FDOPT NL                                                85046
         FDGOTO OFDONA1                                          90243
OFDONAM  FDTM  FLAG2,FGSAME,BZ=OFDONA1                           90243
         FDPRT 'OUTPUT MODE (IBM/ANSI) WILL BE SAME AS INPUT'    90243
         FDOPT NL                                                90243
OFDONA1  FDTM  FLAG2,FGANSI1,BZ=OFDONA3                          90243
         FDPRT 'ANSI OUTPUT WILL BE REGRESSED TO ANSI 1 MODE (7-BIT)'
         FDOPT NL                                                90243
OFDONA3  FDTM  FLAG2,FGANSI3,BZ=OFDOBUG                          90243
         FDPRT 'ANSI OUTPUT WILL BE CHANGED TO ANSI 3 MODE (8-BIT)'
         FDOPT NL                                                90243
OFDOBUG  FDTM  CLEANFG,FGCUCC,BZ=OFDCOPT                         84253
         FDPRT 'RECORD LENGTH WILL BE UPPED BY 4 IN VARIABLE LENGTH FIL*
               ES'                                               84253
         FDOPT NL                                                84253
         FDGOTO OFDCOPT                                          82002
OFDNOPT  FDPRT 'NO SPECIAL PROCESSING OPTIONS REQUESTED',NL      82002
OFDCOPT  FDCLC MAXFIL,DC7FFF,2,BE=OFDNMAX                        82002
         FDPRT 'ONLY',NL                                         82002
         FDPRT MAXFIL,I,PAD                                      82002
         FDPRT 'FILES ON EACH TAPE WILL BE PROCESSED',PAD        82002
OFDNMAX  FDOPT IND=0         RESET INDENT                        82002
         FDPRT MAXVOL,I,NL                                       82002
         FDPRT 'VOLUME',PADL                                     83353
         FDCLC DCH1,MAXVOL,2,BE=OFDVONE                          83353
         FDPRT 'S'                                               83353
OFDVONE  FDPRT 'WILL BE READ :',PAD                              83353
         FDPRT JFCBVOLS-INFMJFCB+INPJFCB,6,PAD                   82002
         FDPRT JFCBVOLS+6-INFMJFCB+INPJFCB,6,PAD                 82002
         FDPRT JFCBVOLS+12-INFMJFCB+INPJFCB,6,PAD                82002
         FDPRT JFCBVOLS+18-INFMJFCB+INPJFCB,6,PAD                82002
         FDPRT JFCBVOLS+24-INFMJFCB+INPJFCB,6,PAD                82002
         FDPRT *END                                              82002
         SPACE 1                                                 82002
COMPCODE FDOPT NL,CC=C'-'                                        82002
         FDPRT 'LEVEL'                                           82002
         FDPRT RETCODE,I,PAD                                     82002
         FDPRT 'ERRORS ENCOUNTERED'                              82002
         FDPRT *END                                              82002
         SPACE 1                                                 82002
         LTORG ,                                                 82002
         EJECT ,
         DROP  ,                                                 82002
SLCOPY   STM   R14,R12,12(R13)                                   82002
         USING SLCOPY,R10,R11,R12                                82002
         LA    R10,0(,R15)   GET CLEANED BASE ADDRESS            82002
         LA    R11,2048(,R10)                                    82002
         LA    R11,2048(,R11)                                    82002
         LA    R12,2048(,R11)                                    82002
         LA    R12,2048(,R12)                                    82002
         ST    R1,8(,R13)    LINK SAVE AREAS                     82002
         ST    R13,4(,R1)    CHAIN                               82002
         LR    R13,R1                                            82002
         USING SUBSAVE,R13                                       82002
         MVI   IOFG,IOOPEN  SIGNAL OPEN IN PROGRESS              82002
         OPEN  (,(INPUT,REREAD)),TYPE=J,MF=(E,OPLSTIN)  INPUT OPEN
         TM    DCBOFLGS-IHADCB+SYSUT1,DCBOFOPN  IS IT OPEN ?     80048
         BZ    SUBEX16       TERMINATE                           82002
         MVI   IOFG,0      RESET I/O FLAG - OPEN OK              82002
         SPACE 2
FILELOOK XC    DCBBLKCT-IHADCB+SYSUT1(4),DCBBLKCT-IHADCB+SYSUT1  82002
         XC    DCBBLKCT-IHADCB+SYSUT2(4),DCBBLKCT-IHADCB+SYSUT2  82002
         NI    LBLFG,LVOL1+LPROT+LPROF   TURN OFF FLAGS          82002
         MVC   BLKFILE,BLKALL   SAVE BLOCK COUNT OF DATA FILE    82002
         MVC   OUTALL,OUTCUR   SAVE O/P COUNT OF PRIOR FILE      82002
         XC    CLRFILE(CLRFILEL),CLRFILE  CLEAR FILE COUNTERS    82002
         NI    SLFG,SLNL+SLANSI3     RESET SPACING FLAGS         90243
         MVI   WHICH,1       PROVISIONALLY SET FOR DATAFILE
         LH    R1,PHYSFIL    GET PHYSICAL FILE NUMBER            82002
         TM    SLFG,SLNL     NL ?                                82002
         BNZ   FILELOOV      YES                                 82002
         LA    R1,2(,R1)     UP
         SLR   R0,R0
         D     R0,=F'3'      GET DATA FILE NUMBER
         STC   R0,WHICH
         LTR   R3,R0         HEADER FILE ?                       82002
         BNZ   FILEHEAD      NO; PRINT APPROPRIATE LINE          82002
FILELOOV STH   R1,LOGFIL     SET LOGICAL FILE NUMBER             82002
         B     FILESIZE      GO TO BRANCH BY TYPE                82004
         SPACE 1                                                 82002
*********************************************************************
*                                                                82045
*        CONSTANTS AND ROUTINES WITH SHARED ADDRESSABILITY       82045
DCF1     DC    0F'1',H'0'                                        82004
DCH1     DC    H'1'                                              82004
DC7FFF   DC    H'32767'                                          82002
BLANKS   DC    CL133' '      BLANKS
DCVOL    DC    0C'VOL'
DCVOL1   DC    C'VOL1'
DCHDR    DC    0C'HDR'                                           82002
DCHDR1   DC    C'HDR1'                                           82002
         SPACE 1                                                 82045
*        LOCAL CODE TO CHECK FOR TMS VS. NON-TMS TAPE            82045
*        R15 - POINTER TO VOLSER                                 82045
*        RETURN R14 - SCRATCH TAPE                               82045
*               R14+4 - TMS TAPE                                 82045
*               R14+8 - NON-TMS TAPE                             82045
CHECKTMS CLI   0(R1),X'FF'   SCRATCH ?                           82045
         BER   R14           YES; RETURN SCRATCH                 82045
         CLI   0(R1),C' '    OTHER SCRATCH ?                     82045
         BNHR  R14           YES; RETURN IT                      82045
         BALR  R15,0         MAKE LOCAL BASE                     82045
         USING *,R15                                             82045
         CLC   DCSCRTCH,0(R1)  LONG FORM ?                       82045
         BER   R14           YES; SET SCRATCH                    82045
         STCM  R14,15,0(R13)    SAVE RETURN ADDRESS              90243
         SERVCALL VSTMS,(R1)  SEE IF IN TMC                      82102
         DROP  R15           CLOBBERED BY CALL                   82102
         USING *,R14         USE VALUE SET BY CALL               82102
         ICM   R14,15,0(R13)    RESTORE RETURN ADDRESS           90243
         BXLE  R15,R15,4(R14)  RETURN R14+4 IF TAPE IN TMC       82102
         B     8(,R14)       ELSE SET NON-TMS TAPE               82102
         DROP  R14                                               82102
DCSCRTCH DC    C'SCRTCH'     CONSTANT FOR SCRATCH                82045
*                                                                82045
*********************************************************************
         SPACE 1
FILEHEAD SLL   R3,2                                              82002
         L     R1,FDHEAD-4(R3)                                   82002
         PRTV  (R1)          PRINT DATA FILE/ TRAILER LINE       82002
FILESIZE L     R5,MAXBLKSI   GET MAXIMUM BUFFER SIZE             82004
         TM    LBLFG,LVOL1   HEADER FOUND ?                      82004
         BZ    FILESIZD      NO; LEAVE MAX FOR NL DATA FILE      82004
         TM    SLFG,SLNL     NL TAPE ?                           82004
         BNZ   FILESIZD      DITTO; USE MAX                      82004
         LA    R4,256        SET MINIMUM SIZE                    82004
         CLI   WHICH,1       IS IT A DATA FILE ?                 82004
         BNE   FILESIZL      NO; SET LABEL SIZE                  82004
         ICM   R4,15,FBLOCK  VALID BLOCKSIZE IN HDR2 ?           82004
         BZ    FILESIZD      NO; USE MAX                         82004
         AH    R4,=H'7'      ADD A LITTLE FOR EXCESS LENGTH CHECK
         CR    R4,R5         IS BLOCK LESS THAN MAX ?            82004
*DEL*    BL    FILESIZL      YES; USE IT TO REDUCE PAGING        82062
         BH    FILESIZD      GREATER - WILL BOMB ?               82004
         CH    R4,DC7FFF     POSSIBLE FDR DUMP FILE ?            82004
         BNL   FILESIZD      YES; LEAVE MAX                      82004
FILESIZL LR    R5,R4         USE SHORTER LENGTH                  82004
FILESIZD ST    R5,MAXBLKRD   SET MAXIMUM READ BLOCK SIZE         82004
         CLI   WHICH,1       BRANCH BY FILE TYPE                 82004
         BH    TRAILERS      TRAILER RECORDS
         BE    DATAFILE      DATA
         SPACE 2
         MVC   RECFM,BLANKS                                      82004
         XC    FBLOCK,FBLOCK  CLEAR BLOCKSIZE                    82002
         MVI   SAVEHDR1,C' '                                     82002
         MVC   SAVEHDR1+1(2*L'SAVEHDR1-1),SAVEHDR1  CLEAR LABELS 82002
         NI    LBLFG,LVOL1+LPROT  RESET 'PROTECTED FILE' BIT     82002
         MVI   STACKFG,0     RESET STACKED HEADERS               82002
         BAL   R9,READBLKS   SCHEDULE THE BUFFERS FOR READ       82002
         USING IOBUF,R8      DECLARE BUFFER MAPPING              82002
         TM    LBLFG,LVOL1   WAS VOLUME LABEL FOUND PREVIOUSLY ? 82002
         BO    VH1READP      YES, GO TO DO HEADER LABEL CHECK    82002
         TM    IOFG,IOEOF    IMMEDIATE END-FILE ?                82002
         BNZ   VL1LTMCK      YES; CHECK FOR LEADING TAPE-MARK OPTION
         TM    IOFG,IOERR    ERROR FLAG ON ?                     82025
         BZ    VL1CAPAB      NO                                  82025
         TM    BIOSENS1,1    NOT CAPABLE ?                       82025
         BZ    VL1CAPAB      NO                                  82025
         OI    SLFG,SLNL     FORCE NL MODE                       82025
         OI    CC,CCNL       SET 'NL' RETURN CODE                89050
         BAL   R9,SUBHEAD    WRITE HEADER AND SUBHEAD            82025
         PRTV  MGBADDEN,DEV=(1,2)  WRITE DENSITY ERROR MSG       82025
         BAL   R9,TESTCLOS   FORCE MODE ?                        82025
         B     VL1NLTAP      YES; KEEP PROCESSING                82025
         BAL   R9,ERRMSG     WRITE ERROR MESSAGE                 82025
         BAL   R9,LABDUMP    DUMP THE DATA                       82025
         B     FORCLOSE      ELSE QUIT THIS TAPE                 82025
VL1CAPAB TM    FLAG,FGFORCE   FORCED NL MODE ?                   82025
         BNZ   VL1NLTAP      YES; FORCE NL                       82002
         CLC   DCVOL1,VOLLABI  VOL1 HEADER LABEL ?               82002
         BE    VL1VOL1       YES                                 82002
         CLC   DCHDR1,FL1LABI  MISSING VOL1 RECORD ?             86096
         BNE   VL1NLTAP      NO; SOMETHING ELSE FUNNY            86096
         TM    FLAG,FGCOPY   COPY MODE ?                         86096
         BZ    VL1NLTAP      NO; DON'T BOTHER                    86096
         L     R1,=A(FAKEBUFF)  POINT TO FAKE BUFFER             86096
         ST    R8,BUFLINK-IOBUF(,R1)  SAVE TRUE BUFFER ADDRESS   86096
         LR    R8,R1         USE FAKE BUFFER FOR A WHILE         86096
         B     VL1VOL1       AND TREAT AS PROPER TAPE            86096
VL1NLTAP OI    SLFG,SLNL     SHOW UNLABELLED                     82002
         OI    CC,CCNL       SET 'NL' RETURN CODE                89050
         OI    LBLFG,LVOL1   SHOW VOLUME PROCESSING DONE         82002
         BAL   R9,SUBHEAD    UPDATE SUB-HEADER                   82002
         BAL   R9,OPENOUT    OPEN THE OUTPUT TAPE                82025
         B     DATAFILE      PROCESS THE FILE AS DATA            82002
VL1LTMCK TM    FLAG,FGLTM    LEADING TAPE MARK EXPECTED ?        82002
         BZ    VL1EMPTY      NO; ASSUME EMPTY VOLUME             82002
VL1LTMDO OI    SLFG,SLNL     SET UNLABELLED                      82002
         OI    CC,CCNL       SET 'NL' RETURN CODE                89050
         OI    LBLFG,LVOL1   JUST IN CASE - FAKE IT OUT          82002
         BAL   R9,SUBHEAD    REPEAT SUB-HEADER WITH CHANGE       82002
         PRTV  LTMTAPE,DEV=(1,2)  SHOW LEADING TAPE MARK         82002
         BAL   R9,OPENOUT    PROPAGATE END-FILE ONLY TO OUTPUT   82002
         BAL   R9,PUTOUTOP   WRITE THE END-FILE                  82002
         B     CLOSEFIL      GO TO PROCESS FILE 2                82002
VL1EMPTY OI    SLFG,SLNL     SHOW NOT SL                         82002
         OI    CC,CCNL       SET 'NL' RETURN CODE                89050
         OI    LBLFG,LVOL1   JUST IN CASE                        82002
         BAL   R9,TESTCLOS   CHECK FOR CLOSE BYPASS              82002
         B     VL1NLTAP      YES; TREAT AS UNLABELLED TAPE       82002
         BAL   R9,SUBHEAD    PRINT SUBHEADERS                    82002
         PRTV  VOL1EOF,DEV=(1,2)                                 82002
         B     FORCLOSE      FORCE CLOSE AND VOLUME SWITCH       82002
         SPACE 1                                                 82002
VL1VOL1  MVC   SAVEVOL1,DXLBL  SAVE VOLUME 1 LABEL               82002
         OI    STACKFG,STKVOL1  SHOW VOL1 STACKED                82002
         OI    LBLFG,LVOL1   SET FOR VOL1 FOUND                  82002
         TM    DCBOPTCD-IHADCB+SYSUT1,DCBOPTQ  ANSI INPUT ?      90243
         BZ    VL1VOL1E      NO                                  90243
         TM    FLAG2,FGSAME  MAKE OUTPUT MATCH INPUT ?           90243
         BZ    VL1NSAM       NO                                  90243
         OI    JFCOPTCD-INFMJFCB+OUTJFCB,JFCOPTQ  STAY ANSI      90243
VL1NSAM  TM    FLAG2,FGANSI3  FORCE ANSI3 ?                      90243
         BNZ   VL1MAKA3      YES                                 90243
         TM    FLAG2,FGANSI1  FORCE ANSI1 ?                      90243
         BZ    *+8           NO                                  90243
         MVI   LABSTAND,C'1'  OVERRIDE USER                      90243
         CLI   LABSTAND,C'1'  OLD ANSI TYPE 1 ?                  90243
         BE    VL1VOL1E      YES                                 90243
VL1MAKA3 MVI   LABSTAND,C'3'   FORCE TYPE 3                      90243
         OI    SLFG,SLANSI3  AND SET MATCHING TRANSLATE MASK     90243
VL1VOL1E MVC   VPHSER,VSSER  SAVE EXPECTED VOL-SER               82002
         MVC   VSSER,VOLSERNO  COPY ACTUAL FOUND                 82002
         MVC   VLVOL,VOLSERNO  ALSO SET FOR 'COPY' OUTPUT SERIAL 82002
         BAL   R9,SUBHEAD    MAKE SUB-HEADERS                    82002
         PRTLIST VLVOL1,DEV=(1,2)  PRINT LABEL AS READ           82002
         CLC   VPHSER,VOLSERNO  EXPECTED VOL-SER ?               82002
         BE    VL1SEROK      YES                                 82002
         CLC   =C'USER0',VPHSER   CANNED VOL-SER ?               82002
         BE    VL1SEROK      YES - SKIP ERROR MESSAGE            82002
         PRTLIST VPHEADB,DEV=(1,2)   NO, SHOW POSS. ERROR        82002
         OI    CC,CCWARN     SET MINOR ERROR                     82002
VL1SEROK CLI   VOLSEC,C' '   SECURITY BYTE ON ?                  82002
         BNH   VL1NOSEB      NO (OR INVALID - RESET)             83051
         CLI   VOLSEC,C'0'   DITTO ?                             82002
         BE    VL1NOSEC                                          82002
         OI    LBLFG,LPROT   YES; SHOW TAPE PROTECTED            82002
         PRTLIST MSGPROTP    PRINT PROTECTION MESSAGE            82002
         BAL   R9,SECORIDE   SEE IF OVERRIDE PERMITTED           82002
         TM    LBLFG,LPROT   WAS IT TURNED OFF ?                 82002
         BNZ   VL1NOSEC      NO                                  82002
VL1NOSEB MVI   VOLSEC,C' '   RESET VOLUME SECURITY               83051
         MVI   VOLSEC-DXLBL+SAVEVOL1,C' '  ALSO RESET COPY       83051
VL1NOSEC TM    IOFG,IOFBAD   ANY I/O ERROR ?                     82002
         BZ    VL1VL1NR      NO; CHECK LABEL LENGTH              82002
         BAL   R9,ERRMSG     PRINT ERROR MESSAGE, IF ANY         82002
         BAL   R9,LABDUMP    DUMP LABEL                          82002
         OI    STACKFG,STKVOL1R  SET FOR ERROR IN VOL1           82002
         LA    R0,80         SET PREFERRED LABEL LENGTH          82002
         ST    R0,DALEN      CRAM IT IN                          82002
         STH   R0,RITECCW+6  SET LABEL WRITE LENGTH              82004
VL1VL1NR L     R9,=A(FAKEBUFF)  POINT TO FAKE VOL1 BUFFER        86096
         CR    R8,R9         ARE WE USING FAKE BUFFER ?          86096
         BNE   VL1VL1NT      NO                                  86096
         L     R8,BUFLINK    GET HDR1 BUFFER BACK                86096
         B     VH1READR      AND PROCESS IT                      86096
VL1VL1NT BAL   R9,LABLEN     CHECK CORRECT LABEL LENGTH          82002
         B     VH1READ       NOW PROCESS HDR1/HDR2               82002
         SPACE 1                                                 82002
*        COMMON ENTRY FOR HEADER FILES                           82002
VH1READP TM    IOFG,IOEOF    IMMEDIATE END-FILE ?                82002
         BNZ   VH1READF      YES; CHECK FURTHER                  82002
         CLC   DMEND,ZEROES  ANY DATA TO BE DUMPED ?             82002
         BNE   VH1READS      YES; DO NORMAL SUB-TITLE            82002
         PRTROOM 10          LEAVE ROOM FOR HEADERS              82002
         PRTSPACE 2          ELSE TRIPLE-SPACE                   82002
         PRTLIST VSUBHEAD    USE AS FILE HEADER ONLY             82002
         B     VH1READR      GO TO CHECK                         82002
VH1READF BAL   R9,TESTCLOS   CLOSE BYPASS ?                      82002
         B     VH1READS      YES; USE NORMAL SUB-TITLE           82002
         B     VH1NHDR       ELSE GO TO END-REEL PROCESSING      82002
         SPACE 1                                                 82002
VH1READS PRTLIST VSUBHEAD,TITLE=3  MAKE SUB-TITLE                82002
         B     VH1READR      SKIP READ                           82002
VH1READ  BAL   R9,READBLKS   LOOK FOR A HEADER1 LABEL            82002
VH1READR TM    IOFG,IOEOF+IOEQC  END-FILE OR END OF REEL ?       82002
         BNZ   VH1NHDR1      YES; BAD VOLUME                     82002
         TM    IOFG,IOFBAD   ANY ERRORS ?                        82002
         BZ    VH1HDR1       NO; SEE IF EXPECTED HEADER 1        82002
         BAL   R9,ERRMSG     DISPLAY I/O ERROR MESSAGE           82002
         BAL   R9,LABDUMP    DISPLAY LABEL                       82002
         OI    STACKFG,STKHDR1R  SHOW ERROR IN HDR1 READ         82002
VH1HDR1  CLC   DCHDR1,FL1LABI  HEADER 1 ?                        82002
         BNE   VH1QHDR1      NO; CHECK FOR POSSIBLE HDR2         82002
         MVC   SAVEHDR1,DXLBL   SAVE THE LABEL                   82002
         OI    STACKFG,STKHDR1  SHOW HEADER 1 FOUND              82002
         OI    LBLFG,LHDR1   DITTO                               82002
         BAL   R9,LABLEN     CHECK AND FIX LENGTH                82002
         MVC   JFCBDSNM-INFMJFCB+OUTJFCB(44),BLANKS  CLEAR DSN   82002
         MVC   JFCBDSNM-INFMJFCB+OUTJFCB(L'FL1ID),FL1ID          82002
         MVI   BUFLAG,BUFEMPTY   RELEASE THIS BUFFER             82002
         PRTLIST VLHDR1      PRINT                               82002
         CLI   FL1FSEC,C' '   SECURITY BYTE ON ?                 83051
         BNH   VL1HOSEB      NO (OR INVALID - RESET)             83051
         CLI   FL1FSEC,C'0'   DITTO ?                            83051
         BE    VL1HOSEC                                          83051
         CLI   FL1FSEC,C'3'  WRITE/DELETE PROTECT ONLY ?         83051
         BE    VL1HOSEA      NO; READ AND WRITE PROTECTED        88118
         PRTLIST MSGPROTW    SHOW WRITE PROTECTED                83051
         B     VL1HOSEC                                          83051
VL1HOSEA OI    LBLFG,LPROF   YES; SHOW FILE PROTECTED            83051
         PRTLIST MSGPROTF    PRINT PROTECTION MESSAGE            83051
         BAL   R9,SECORIDE   SEE IF OVERRIDE PERMITTED           83051
         B     VL1HOSEC      BYPASS ERROR RESETS                 83051
VL1HOSEB MVI   FL1FSEC,C' '   RESET VOLUME SECURITY              83051
         MVI   FL1FSEC-DXLBL+SAVEHDR1,C' '  ALSO RESET COPY      83051
VL1HOSEC PRTLIST VLHDR1F     PRINT HEADER 1 DATA                 83051
         CLC   FL1GNO(L'FL1GNO+L'FL1VNG),BLANKS   GDG DATA ?     82002
         BE    VH2READ       NO                                  82002
         PRTLIST VLHDR1S                                         82002
         B     VH2READ       NOW READ HEADER2                    82002
VH1QHDR1 CLC   DCHDR2,FL1LABI  POSSIBLE HDR2 OUT OF SEQUENCE ?   82002
         BE    VH2HDR2       YES; CHECK IT                       82002
         TM    IOFG,IOFBAD   ALREADY DUMPED ?                    82002
         BNZ   VH1QH1ER      YES; JUST SET ERROR                 82002
         BAL   R9,LABDUMP    DUMP THE LABEL                      82002
         PRTV  HDINVAL       SHOW INVALID LABEL RECORD           82018
VH1QH1ER OI    STACKFG,STKHDR1R  SHOW HEADER 1 ERROR             82002
         B     VH1READ       TRY ANOTHER READ                    82002
VH1NHDR1 TM    IOFG,IOEQC    EQUIPMENT CHECK ?                   82002
         BZ    VH1NHDR       NO; END-FILE                        82002
         BAL   R9,ERRMSG     PRINT ERROR MESSAGE                 82002
         B     FORCLOSE      FORCE VOLUME SWITCH                 82002
         SPACE 1                                                 82002
VH1NHDR  TM    LBLFG,LHDR1   WAS A HEADER 1 READ ?               82002
         BNZ   VH1N2HDR      YES                                 82002
         CLC   DCH1,PHYSFIL  FIRST FILE ?                        82004
         BE    VH1EOF1       YES                                 82002
         PRTSPACE 1          DOUBLE-SPACE ON SYSPRINT            82002
         PRTV  /VOLENDOK,DEV=(1,2),CC=NO  PRINT NORMAL END OF VOLUME
         BAL   R9,TESTCLOS   TEST FOR FORCED CONTINUATION        82002
         B     VH1EOFCM      YES; CONTINUE PROCESSING            82002
         B     CLOSENEW      ELSE TERMINATE THE TAPE             82002
VH1EOF1  PRTLIST /SKPEOF,DEV=(1,2)                               90243
         PRTV  /VOLNUSE,DEV=(1,2)                                90243
VH1EOFCM BAL   R9,TESTCLOS   CLOSE BYPASS ?                      82002
         B     *+8           YES                                 82002
         B     FORCLOSE      ELSE FORCE CLOSE                    82002
         OI    SLFG,SLNL     FORCE UNLABELLED                    82002
         OI    CC,CCNL       SET 'NL' RETURN CODE                89050
         BAL   R9,OPENOUT    OPEN                                82002
         BAL   R9,PUTOUTST   WRITE A TAPEMARK                    82002
         B     CLOSEFIL      AND DO NEXT FILE                    82002
VH1N2HDR TM    LBLFG,LHDR2   HEADER TWO ALSO PRESENT ?           82002
         BO    VH1N2END      YES; PROCESS NORMALLY               82002
         CLI   FL1ID-DXLBL+SAVEHDR1,C'0'  NULL DSN ?             82002
         BE    VH1NOHD2      MAYBE                               82002
         CLI   FL1ID-DXLBL+SAVEHDR1,C' '  ALTERNATE FORM ?       82002
         BNE   VH1N1SCR                                          82002
VH1NOHD2 CLC   FL1ID+1-DXLBL+SAVEHDR1(16),FL1ID-DXLBL+SAVEHDR1   82002
         BNE   VH1N1SCR      NOT SCRATCH                         82002
         PRTV  /VOLINIT,DEV=(1,2)                                90243
         BAL   R9,TESTCLOS   CHECK FOR BYPASS                    82002
         B     VH1N2END      ON FORCE PROCEED NORMALLY           82025
         B     FORCLOSE      ELSE FORCE VOLUME SWAP              82025
VH1N1SCR PRTV  VDOSDSN       SHOW MISSING HDR2                   82002
         SPACE 1                                                 82002
VH1N2END BAL   R9,OPENOUT    OPEN THE OUTPUT FILE                82002
         TM    DCBOFLGS-IHADCB+SYSUT2,DCBOFOPN  OPEN ?           82002
         BZ    VH1N2NH2      NO; SKIP PUTS                       82002
         TM    STACKFG,STKVOL1   VOL1 TO DO ?                    82002
         BZ    VH1N2NV1      NO                                  82002
         AIF   ('&LOCAL' NE 'PID').NOVPKL                        92279
         SERVCALL VSTMS,VOLSERNO-DXLBL+SAVEVOL1                  83051
         BXH   R15,R15,VH1N2SY1  LEAVE IF NOT TMS TAPE           83051
         MVI   VOLSEC-DXLBL+SAVEVOL1,C'0'  UNPROTECT OUR TAPES   83051
.NOVPKL  ANOP  ,                                                 83051
VH1N2SY1 CLI   VOLSEC-DXLBL+SAVEVOL1,C' '  BLANK SECURITY BYTE ? 83051
         BNE   VH1N2SY2      NO                                  83051
         MVI   VOLSEC-DXLBL+SAVEVOL1,C'0'  REPLACE BY IBM STANDARD
VH1N2SY2 CLI   VOLSEC-DXLBL+SAVEVOL1,C'0'  IBM UNPROTECTED ?     83051
         BNE   VH1N2SY3      NO; LEAVE IT ALONE                  83051
         TM    DCBOPTCD-IHADCB+SYSUT2,DCBOPTQ  ANSI OUTPUT ?     83051
         BZ    VH1N2SY3      NO; LEAVE IT                        83051
         MVI   VOLSEC-DXLBL+SAVEVOL1,C' '  REPLACE BY BLANK      83051
         CLI   LABSTAND-DXLBL+SAVEVOL1,C'1'  ANSI TYPE 1 ?       90243
         BE    VH1N2SY3      YES; RETAIN                         90243
         MVI   LABSTAND-DXLBL+SAVEVOL1,C'3'  MAKE TYPE 3 (DFLT)  90243
VH1N2SY3 LA    R1,SAVEVOL1   POINT TO VOLUME LABEL               83051
         BAL   R9,PUTHDR     WRITE A STACKED HEADER              82002
VH1N2NV1 TM    STACKFG,STKHDR1   HDR1 STACKED ?                  82002
         BZ    VH1N2NH1      NO                                  82002
         BAL   R9,UPDHDR1    UPDATE HEADER 1 VOLSER              82002
         LA    R1,SAVEHDR1   POINT TO HDR1                       82002
         BAL   R9,PUTHDR     WRITE STACKED HDR1                  82002
VH1N2NH1 TM    STACKFG,STKHDR2   HDR2 STACKED ?                  82002
         BZ    VH1N2NH2      NO                                  82002
         BAL   R9,UPDHDR2    UPDATE HEADER 2 DATA                82002
         LA    R1,SAVEHDR2   GET STACKED HDR2                    82002
         BAL   R9,PUTHDR     WRITE HDR2                          82002
VH1N2NH2 L     R8,CURBLOCK   GET CURRENT BUFFER BACK             82002
         TM    IOFG,IOEOF    END-FILE ?                          82002
         BNZ   CLOSEWTM      YES                                 82002
         TM    BUFLAG,BUFREAD  UNPROCESSED BUFFER ?              82002
         BZ    UHDREAD       READ ANOTHER                        82002
         B     UHDCHECK      ELSE PROCESS USER LABELS            82002
         SPACE 1                                                 82002
UHDPRINT CLC   DCUHD,DXLBL   CORRECT HEADER ?                    82002
         BE    UHDOK         ACCEPT                              85118
         CLC   =C'HDR3',DXLBL   DEC/VAX THIRD HEADER ?           85118
         BNE   UHDERR        NO; TREAT AS ERROR                  82002
UHDOK    BAL   R9,LABLEN     TEST AND FIX LENGTH                 85118
         BAL   R9,LABPRINT   PRINT THE LABEL                     82002
UHDPUT   BAL   R9,PUTOUTOP   COPY TO OUTPUT TAPE                 82002
UHDREAD  BAL   R9,READBLKS   GET ANOTHER BLOCK                   82002
UHDCHECK TM    IOFG,IOEOF    END-FILE ?                          82002
         BNZ   CLOSEWTM      YES; COPY TAPEMARK AND PROCESS NEXT FILE
         TM    IOFG,IOFBAD   ANY ERRORS ?                        82002
         BZ    UHDPRINT      NO; PROCESS THIS ONE                82002
UHDERR   BAL   R9,ERRMSG     WRITE ERROR MESSAGE                 82002
         TM    IOFG,IOEQC    END OF REEL ?                       82002
         BNZ   FORCLOSE      YES; FORCE VOLUME SWITCH            82002
         BAL   R9,LABDUMP    DUMP THE ERROR BLOCK                82002
         TM    IOFG,IOFBAD   REAL ERROR ?                        82018
         BNZ   UHDERRNP      YES                                 82018
         PRTV  HDINVAL       SHOW BAD LABEL RECORD               82018
UHDERRNP CLC   BLKERR,MAXLBERR  TOO MANY ERRORS ?                82018
         BL    UHDREAD       NO; READ ANOTHER                    82002
         B     FORCLOSE      FORCE END OF VOLUME                 82002
         SPACE 1                                                 82002
VH2READ  BAL   R9,READBLKS   READ A RECORD                       82002
         TM    IOFG,IOEOF+IOEQC  END-REEL OR END-FILE ?          82002
         BNZ   VH1NHDR1      YES; TOO BAD                        82002
         TM    IOFG,IOFBAD   ANY ERRORS ?                        82002
         BZ    VH2HDR2       NO; SEE IF EXPECTED HEADER 2        82002
         BAL   R9,ERRMSG     DISPLAY I/O ERROR MESSAGE           82002
         BAL   R9,LABDUMP    DISPLAY LABEL                       82002
         OI    STACKFG,STKHDR2R  SHOW ERROR IN HDR2 READ         82002
VH2HDR2  CLC   DCHDR2,FL1LABI  HEADER 2 ?                        82002
         BNE   VH1QHDR2      NO; CHECK FOR POSSIBLE HDR1         82002
         MVC   CUCCLREC,FL2LRECL  SAVE LRECL                     84253
         MVC   SAVEHDR2,DXLBL   SAVE THE LABEL                   82002
         OI    STACKFG,STKHDR2  SHOW HEADER 2 FOUND              82002
         OI    LBLFG,LHDR2   DITTO                               82002
         BAL   R9,LABLEN     CHECK AND FIX LENGTH                82002
         MVI   BUFLAG,BUFEMPTY   RELEASE THIS BUFFER             82002
         BAL   R9,UPDHDR2    SET RECFM, DENSITY, TRTCH           82002
         MVC   DXLBL(80),SAVEHDR2  MOVE BACK FOR FORMATTING      82002
         PRTLIST VLHDR2                                          82002
         LA    R1,1          MAKE CONSTANT ONE                   82327
         ST    R1,FRECLN      TO PREVENT 0C9 IF LRECL=0          82327
         LA    R1,FL2BLKL    GET BLOCK SIZE FIELD                82002
         LA    R2,L'FL2BLKL  AND LENGTH                          82002
VH2FLOOP TM    0(R1),C'0'    NUMERIC ?                           82002
         BNO   VH2RECNF      NO, CANCEL FIXED PROCESSING         82002
         LA    R1,1(,R1)     NEXT                                82002
         BCT   R2,VH2FLOOP   TRY ALL                             82002
         PACK  DB,FL2BLKL    PACK                                82002
         CVB   R1,DB         MAKE BINARY                         82002
         LTR   R1,R1         TEST                                82002
         BNP   VH2RECNF      NO GOOD                             82002
         ST    R1,FBLOCK     SAVE SIZE                           82002
*        CLC   DCH1,PHYSFIL  FIRST FILE ?                        82004
*        BE    VH2FLDCB      YES; GET OTHER DCB PARAMETERS       82002
*        CLI   RECFM,C'F'    FIXED LENGTH RECORDS ?              82002
*        BNE   VH1QHDR2      NO                                  82002
VH2FLDCB LA    R1,FL2LRECL   GET RECORD LENGTH FIELD             82002
         LA    R2,L'FL2LRECL  AND LENGTH                         82002
VH2FLOOR TM    0(R1),C'0'    NUMERIC ?                           82002
         BNO   VH2RECNF      NO, CANCEL FIXED PROCESSING         82002
         LA    R1,1(,R1)     NEXT                                82002
         BCT   R2,VH2FLOOR   TRY ALL                             82002
         PACK  DB,FL2LRECL   PACK                                82002
         CVB   R1,DB         MAKE BINARY                         82002
         LTR   R1,R1         TEST                                82002
         BNP   VH2RECNF      NO GOOD                             82002
         C     R1,FBLOCK     GREATER THAN BLOCKSIZE ?            82328
         BH    VH2RECNF      YES; SHOULD NEVER HAPPEN ?          82328
         TM    CLEANFG,FGCUCC  KERMIT TAPE CLEAN-UP ?            84253
         BZ    VH2CUCCX      NO                                  84253
         L     R0,FBLOCK     GET BLOCKSIZE                       84253
         SH    R0,=H'8'      SET FOR LARGEST ADJUSTABLE SIZE     84253
         BNP   VH2CUCCX      SKIP IF FUNNY                       84253
         CLI   RECFM,C'V'    RECFM=V ?                           84253
         BE    VH2CUCCA      YES; CHECK ADJUSTMENT               84253
         CLI   RECFM,C'R'    RECFM=VB ?                          84253
         BNE   VH2CUCCX      NO, SKIP                            84253
VH2CUCCA CR    R1,R0         IS LRECL GREATER ?                  84253
         BH    VH2CUCCX      YES; DO NOT ADJUST                  84253
         LA    R1,4(,R1)     ADJUST COLUMBIA'S ERRONEOUS SIZE    84253
         CVD   R1,DB         MAKE IT PACKED                      84253
         UNPK  FL2LRECL,DB   CONVERT TO ZONED                    84253
         OI    FL2LRECL+L'FL2LRECL-1,C'0'  MAKE IT PRINTABLE     84253
         MVC   SAVEHDR2+FL2LRECL-DXLBL(L'FL2LRECL),FL2LRECL      84253
*        LR    R2,R1                                             84253
*        PRTLIST VLHDR2      PRINT ADJUSTED HEADER               84253
*        LR    R1,R2         RESTORE LRECL                       84253
VH2CUCCX ST    R1,FRECLN     SAVE SIZE                           84253
         MVC   CUCCLREC,FL2LRECL  SAVE ADJUSTED LRECL            84253
         CLC   DCH1,PHYSFIL  DOING FIRST FILE ?                  82002
         BNE   VH2RECFT      NO; SEE IF FIXED                    82002
         MVC   JFCBLKSI-INFMJFCB+OUTJFCB(2),FBLOCK+2  SET TMS BLOCKSIZE
         CH    R1,DC7FFF      LRECL=X ?                          82004
         BNH   *+8           NO                                  82002
         L     R1,=X'00008000'  SET LRECL=X                      82002
         STH   R1,JFCLRECL-INFMJFCB+OUTJFCB  SET TMS RECORD LENGTH
         LA    R2,RECBITS    POINT TO RECORD FORM TABLE          82002
         LA    R3,(RECBITND-RECBITS)/(RECBIT2-RECBITS)           82002
         MVI   JFCRECFM-INFMJFCB+OUTJFCB,0  CLEAR RECFM          82002
VH2RCFLP LA    R14,RECFM     POINT TO RECFM                      82002
         LA    R15,L'RECFM   AND NUMBER OF ENTRIES               82002
VH2RCFLI CLC   0(1,R2),0(R14)  MATCHING OPTION ?                 82002
         BE    VH2RCFLM      YES; SET IT                         82002
         LA    R14,1(,R14)                                       82002
         BCT   R15,VH2RCFLI  ELSE TRY NEXT                       82002
         B     VH2RCFLN      SKIP UNMATCHED                      82002
VH2RCFLM OC    JFCRECFM-INFMJFCB+OUTJFCB(1),1(R2)  OR RECFM OPTION
VH2RCFLN LA    R2,RECBIT2-RECBITS(,R2)                           82002
         BCT   R3,VH2RCFLP   TRY NEXT RECFM BYTE                 82002
VH2RECFT CLI   RECFM,C'F'    FIXED LENGTH RECORDS ?              82002
         BE    VH1QHDR2      YES; CONTINUE                       82002
         SPACE 1                                                 82002
VH2RECNF CLI   RECFM,C'F'    FIXED RECORD FORMAT ?               82002
         BNE   VH1QHDR2      NO; LEAVE AS IS                     82002
         MVI   RECFM,X'86'   MAKE LOWER CASE - INVALID F FORMAT  82002
VH1QHDR2 TM    LBLFG,LHDR1   HEADER 1 ALREADY READ ?             82002
         BZ    VH1READ       NO; TRY FOR IT                      82002
         TM    LBLFG,LHDR2   HEADER 2 FOUND ?                    82002
         BNZ   VH1N2END      YES; UNSTACK                        82002
         BAL   R9,READBLKS   PRE-READ                            82002
         B     VH1N2END      GO TO POST-PROCESSING               82002
         SPACE 1                                                 82002
SUBHEAD  TM    STACKFG,STKSUB    SUB-HEADER PRINTED ALREADY ?    82002
         BNZR  R9            YES; RETURN                         82002
         OI    STACKFG,STKSUB    SET SUB-HEADER PROCESSED        82002
         PRTLIST VSUBHEAD,TITLE=3  TITLE TO SYSPRINT             82002
         PRTROOM 10,DEV=2    LEAVE ROOM FOR A FEW FILES          82002
         PRTSPACE 2,DEV=2    TRIPLE-SPACE SUMMARY LISTING        82002
         PRTLIST VSUBSUM,DEV=2  DO SUB-TITLE AS PRINT LINE       82002
         BR    R9            RETURN TO CALLER                    82002
         SPACE 2
LABPRINT CLC   BLKCNT,PRTLABEL  ENOUGH IS ENOUGH ?               82002
         BHR   R9            YES; DON'T WASTE PAPER
         PRTLIST VLAB                                            82002
         BR    R9            RETURN
         SPACE 1                                                 82002
LABLEN   LA    R2,80         SET PREFERRED LABEL LENGTH          82002
         C     R2,DALEN      CORRECT OS LABEL LENGTH ?           82002
         BER   R9            YES; RETURN                         82002
         TM    DCBOPTCD-IHADCB+SYSUT1,DCBOPTQ  DOING ANSI LABELS ?
         BNZ   LABLENFX      YES; JUST FIX LENGTH                82002
         OI    CC,CCWARN     SET WARNING CODE                    82002
         PRTLIST MSGLABLN    SHOW BAD LABEL LENGTH               82002
LABLENFX ST    R2,DALEN      SET CORRECT LENGTH                  82002
         STH   R2,RITECCW+6  SET LABEL WRITE LENGTH              82004
         BR    R9            RETURN OK                           82002
         SPACE 3
DATAFILE XC    AVGBLK,AVGBLK   CLEAR AVERAGE ACCUMULATOR
         XC    BLKMIN(L'BLKMIN+L'BLKMAX),BLKMIN                  82002
         MVI   BLKMIN,X'7F'  SET VERY LARGE                      82002
         TM    SLFG,SLNL     NL MODE ?                           82002
         BNZ   DATAFBK0      YES; CLEAR LOGICAL BLOCKSIZE        82062
         L     R14,FBLOCK    GET LOGICAL BLOCKSIZE               82062
         CH    R14,=H'32760'   FDR SIZE ?                        82062
         BNE   DATAFBKN      NO; LEAVE IT                        82062
         C     R14,MAXBLKUS  DID USER SPECIFY BLKSIZE=0 ?        82062
         BNL   DATAFBKN      NO; LEAVE IT ALONE                  82062
DATAFBK0 XC    FBLOCK,FBLOCK  YES; CLEAR HDR2 BLOCKSIZE          82062
DATAFBKN XC    CLRUTIL(CLRUTILL),CLRUTIL  CLEAR UTILITY FIELDS   82062
         CLC   PHYSFIL,DCH1  ENTERED FROM VOL1 CODE ?            82011
         BE    DATAREED      YES; READ ALREADY DONE              82002
         BAL   R9,READBLKS   START THE READS                     82002
         TM    SLFG,SLNL     UNLABELLED ?                        82002
         BZ    DATAREED      NO; ALL SET                         82002
         TM    IOFG,IOEQC    EQUIPMENT CHECK ?                   82002
         BNZ   DATANHED      YES; NO SUB-HEADER                  82002
         TM    IOFG,IOEOF    END-FILE ?                          82002
         BNZ   DATANHED      YES; SKIP SUB-HEADER                82002
         CLC   DMEND,ZEROES  ANY BLOCKS TO BE PRINTED ?          82002
         BE    DATANHED      NO; NO PAGE EJECT                   82002
         PRTLIST VSUBHEAD,TITLE=3                                82002
         B     DATAREED      PROCEED                             82002
DATANHED PRTSPACE 1          DOUBLE-SPACE                        82002
         PRTLIST VSUBHEAD    DON'T EJECT FOR BAD/END FILE        82002
         B     DATAREED      THEN CONTINUE NORMALLY              82002
         SPACE 1
DATAREAD BAL   R9,PUTOUTOP   COPY THE RECORD                     82002
         BAL   R9,READBLKS   START READING                       82002
DATAREED TM    IOFG,IOEQC                                        82002
         BZ    DATATEST
         BAL   R9,ERRMSGEQ  PRINT EQUIPMENT CHECK MSG AND SET EOV
         PRTV  /VOLNUSE,DEV=(1,2)                                90243
         OI    BUFLAG,IOEOF  SET BUFFER TO WTM                   82002
         OI    IOFG,IOEOF    SET EOF FLAG                        82002
*        B     DATATEST      FALL THROUGH TO EOF PROCESSING      82002
         SPACE 1
DATATEST TM    IOFG,IOEOF                                        82002
         BZ    DATALOOK
         BAL   R9,PUTOUTOP   WRITE TAPEMARK                      82002
         CLC   BLKALL,ZEROES   ANY BLOCKS READ ?                 82002
         BNE   DATATEOF      YES                                 82002
         PRTV  NORECS                                            82002
         TM    SLFG,SLNL     NL PROCESSING ?                     82002
         BZ    DATATEOF      NO; CLOSE NORMALLY                  84152
         CLC   MAXFIL,DC7FFF     USER SPECIFIED END ?            82004
         BL    DATATEOF      YES; CLOSE FILE; CONTINUE VOLUME    82002
         L     R1,=A(ASSEND)   ADDRESS OF 'END TAPE ASSUMED' MSG 82002
         PRTV  (R1),DEV=(1,2)   ELSE SHOW EOV ASSUMED            82002
         OI    FLAG,NEWVOL   FORCE EOV                           82002
         B     CLOSEFIL                                          82002
         SPACE 2                                                 82002
DATATEOF L     R5,MAXEXCP    GET NUMBER OF BLOCKS                82002
         LA    R6,PUTGOOD3   FORCE COMPLETE WRITES               82004
         BAL   R9,PUTWAIT    WRITE ALL                           82002
         CLC   LOGFIL,DCH1   FIRST DATA FILE ?                   82004
         BNE   *+10          NO                                  82002
         MVC   BLKFILE1,OUTCUR  SAVE OUTPUT COUNT                82002
         LM    R0,R1,AVGBLK
         ICM   R2,15,BLKCNT  GET BLOCK COUNT                     82002
         BP    DATATMD       GO TO DIVIDE IF POSITIVE            82002
         LA    R2,1          ELSE USE 1 TO AVOID ZERO DIVIDE     82002
DATATMD  DR    R0,R2         AVERAGE BLOCK SIZE                  82002
         ST    R1,AVGBLKSZ                                       82002
         PRTROOM 5,DEV=2     MAKE LINES CONTIGUOUS               82004
         CLC   ANSIHDR,SAVEHDR1  ASCII HEADER LABELS ?           89318
         BNE   DATATNAS      NO                                  89318
         L     R1,=A(TRASTOEB)                                   89318
         TR    SAVEHDR1(L'SAVEHDR1+L'SAVEHDR2),0(R1)  MAKE EBCDIC
DATATNAS PRTLIST SUMFDFIL,DEV=2   SHOW FILE ID                   82002
         TM    SLFG,SLSKIPED+SLSPACED  ANY BLOCKS SKIPPED ?      82002
         BZ    DATATMV       NO; PRINT STATS                     82002
         PRTV  /SKPTERM      ELSE SHOW SKIP TERMINATION          90207
         B     DATATMUT      CHECK FOR UTILITY CODE              82004
DATATMV  L     R0,BLKZER     GET ZERO LENGTH BLOCK COUNT         83353
         A     R0,BLKERR     ADD ERROR COUNT                     83353
         BZ    DATATMX       NO PROBLEMS                         82002
         OI    CC,CCWARN     SET WARNING FLAG                    82002
DATATMX  CLC   BLKALL,ZEROES  NULL FILE ?                        84152
         BE    DATATMFY      YES; SKIP BLOCK STATISTICS          84152
         PRTLIST VLDAT                                           84152
         CLC   BLKCNT,ZEROES   ANY BLOCKS ?                      82002
         BE    DATATMFY                                          82002
         TM    SLFG,SLNL     NL TAPE ?                           82002
         BNZ   DATATMNL      YES; NO FINAGLING                   82002
         PRTLIST SPACE5,DEV=2   MAKE LISTING PRETTIER            82002
DATATMNL CLI   RECFM,C'F'    FIXED PROCESSING ?                  82002
         BE    DATATMF       YES, USE SPECIAL MESSAGE            82002
         PRTLIST VLDATF                                          82002
         PRTLIST VLDADF,DEV=2  WRITE TO SUMMARY ALSO             82002
         B     DATATMFY      DO OUTPUT COUNT CHECK               82002
         SPACE 1
DATATMF  PRTLIST VLDATFF     PRINT SPECIAL MESSAGE               82002
         PRTLIST VLDADFF,DEV=2  WRITE TO SUMMARY ALSO            82002
DATATMFY CLC   BLKMAX,FBLOCK  ANY EXCESSIVE BLOCKS ?             82002
         BNH   DATATMCY      NO, OK                              82002
         CLC   FBLOCK,ZEROES   ANY SIZE STASHED ?                82002
         BE    DATATMCY      NO; NOT AN ERROR                    82002
         PRTLIST VLDATFH,DEV=(1,2)   PRINT WARNING               82002
         OI    CC,CCWARN     SET MINOR ERROR                     82002
DATATMCY ICM   R0,15,BLKFIX  ANY BLOCKS ADJUSTED ?               82002
         BZ    DATATMNB      NO                                  82002
         PRTLIST VLFIXED,DEV=(1,2)  PRINT WARNING                82002
         OI    CC,CCWARN     SET MINOR ERROR                     82002
DATATMNB ICM   R0,15,BLKACC  ANY BLOCKS ACCEPTED ?               83353
         BZ    DATATMAC      NO                                  83353
         PRTLIST VLACCED,DEV=(1,2)  PRINT WARNING                83353
         OI    CC,CCWARN     SET MINOR ERROR                     83353
DATATMAC TM    FLAG,FGCOPY   COPY OP ?                           83353
         BZ    DATATMUT      NO; CHECK FOR UTILITY CODE          82004
         CLC   BLKMAX,MAXBLKUS  ANY EXCESSIVE BLOCKS ?           82002
         BNH   DATATMMX      NO                                  82002
         PRTV  MGMAXBLK,DEV=(1,2),CC=NO                          82002
         L     R1,=X'00002000'  FAKE SYSTEM 002 ABEND            82004
         SVC   13                                                82004
DATATMMX L     R0,BLKCNT     GET INPUT COUNT                     82002
*        A     R0,BLKERR     ALLOW FOR BAD BLOCKS                82002
         C     R0,OUTCUR     MATCH ?                             82002
         BE    DATATMUT      YES; NOW CHECK UTILITY STUFF        82004
         PRTLIST VLOFF       PRINT NASTY                         82002
         PRTLIST SUMFDOFF,DEV=2                                  82002
         OI    CC,CCERROR    SET ERROR                           82002
DATATMUT CLI   UTYFLAG,UTFCOP2  ANY UTILITY DATA PRESENT ?       82004
         BL    CLOSEFIL      NO; JUST TERMINATE THE FILE         82004
         XC    WORKLINE(99),WORKLINE                             82004
         MVC   WORKLINE+UCBTBYT1-UCBOB(4),UTYTYPE MAKE FAKE UCB  82004
         SERVCALL UCBGN,WORKLINE  GET DEVICE NAME FROM FAKE UCB  82004
         MVC   UTYGEN,0(R1)   MOVE GENERIC                       82004
         SERVCALL DSFMT,UTYDSORG  FORMAT FAKE DSCB1 DCB PARMS    82004
         MVC   WORKLINE(27),0(R1)  MOVE RESULT                   82004
         TM    SLFG,SLNL     UNLABELLED ?                        82017
         BNZ   DATATMU3      YES; PRINT SUMMARY AND DETAIL       82017
         PRTLIST UTILFD      ELSE PRINT DETAIL ONLY              82017
         B     DATATMU2         (SUMMARY DONE IN TRAIL...)       82017
DATATMU3 PRTLIST UTILFD,DEV=(1,2)  FORMAT                        82017
         PRTSPACE 1,DEV=2    MAKE LISTING PRETTY                 82017
DATATMU2 MVC   WORKLINE(99),BLANKS   CLEAR WORK SPACE AGAIN      82017
         B     CLOSEFIL                                          82002
         SPACE 2
DATALOOK NI    ERROPT,255-ERRNOPRT-ERRNOACC  RESET CONTROL BITS  83353
         TM    IOFG,IOFBAD-IOLEN                                 83353
         BZ    DATASTAT
DATAERR  L     R1,BLKERR     GET ERRONEOUS BLOCK COUNT           83353
         A     R1,BLKACC     PLUS ACCEPTED ERRORS                83353
         C     R1,PRTERR     TO BE PRINTED ?                     83353
         BH    DATAERRX                                          82002
         LAT   R1,IOMSGPTR,DATAERRX                              82002
         BAL   R9,ERRMSG1    PUT OUT ERROR MESSAGE               82002
         BAL   R9,DATADUMP   DUMP THE BAD BLOCK                  82002
         OI    ERROPT,ERRNOPRT  SET PRINT SUPPRESS BIT           83353
DATAERRX TM    ERROPT,JFCABN+ERRNOACC  ABEND/LOGICAL ERROR ?     83353
         BNZ   DATAEABE      YES                                 83353
         CLI   READCCW,X'02'   WAS THIS A READ ?                 83353
         BNE   DATAEABE      NO                                  83353
         TM    IOFG,IOEQC+IOLEN    UNABLE TO PROCESS ?           83353
         BNZ   DATAEABE      YES; COUNT                          83353
         TM    ERROPT,JFCSKP   SKIP ERROR BLOCKS ?               83353
         BNZ   DATAREAD      YES; READ ANOTHER                   83353
         TM    ERROPT,JFCACC   ACCEPT BAD BLOCKS ?               83353
         BZ    DATAEABE      NO; DEFAULT TO ABEND                83353
         NI    IOFG,255-IOERR   RESET THE ERROR FLAG             83353
         NI    BUFLAG,255-IOERR  RESET ERROR IN BUFFER FLAG      83353
         INC   BLKERR,INC=-1  CHEAT - DON'T COUNT AS ERROR       83353
         INC   BLKCNT        COUNT 'GOOD' BLOCKS                 83353
         INC   BLKACC        COUNT ACCEPTED BLOCKS               83353
         B     DATASTAT      PROCESS DESPITE ERROR               83353
DATAEABE CLC   BLKERR,MAXERR   MAXIMUM ERROR COUNT REACHED ?     83353
         BH    QUITERR       YES; TERMINATE                      82002
         LA    R1,1                                              82002
         A     R1,CONSOLD    GET ERROR COUNT BEFORE THIS RECORD + 1
         ST    R1,CONSCNT    SET NEW COUNT                       82002
         C     R1,MAXCONS    CHECK FOR MAXIMUM CONSECUTIVE ERRORS
         BNH   DATAREAD      NOT YET                             82002
QUITCONS PRTSPACE 1          SPACE ON PRINT FILE                 82002
         PRTV  MGMAXCNS,DEV=(1,2),CC=NO  PRINT ERROR MESSAGE     82002
         B     FORCLOSE      AND FORCE VOLUME TERMINATION        82002
         SPACE 1                                                 82002
DATABBLK OI    ERROPT,ERRNOACC  SUPPRESS EROPT=ACC PROCESSING    83353
         LAT   R1,IOMSGPTR,,DATAERR  PRINT MESSAGE IF SET        83353
         OI    BUFLAG,IOERR  PREVENT WRITE OF THIS BLOCK         82004
         INC   BLKERR        TREAT AS I/O ERROR                  82002
         INC   BLKCNT,INC=-1  AND DECREASE GOOD BLOCK COUNT      82002
         INC   BLKFIX        COUNT BAD BLOCKS                    82002
         LR    R1,R5         SET FOR REAL ERROR MESSAGE          82002
         ST    R1,IOMSGPTR                                       82002
         B     DATAERR       PRINT IT AS HEADER                  82002
         SPACE 1
DATASTAT TM    IOFG,IOLEN  ZERO LENGTH ?                         82002
         BNZ   DATAPRT       YES; GO TO DATA PRINTING            82002
         CLI   READCCW,X'02'  WAS THIS A READ ?                  82002
         BNE   DATAREAD      NO; RUNNING IN SKIP MODE            82002
         L     R1,BLKALL     GET CURRENT BLOCK NUMBER            82004
         CH    R1,=H'2'      CHECK IT                            82004
         BL    BCOPY1        CHECK FOR IEHMOVE/IEBCOPY           82011
         BE    BCOPY2        CHECK SECOND IEBCOPY RECORD         82004
         TM    UTYFLAG,UTFCOP2+UTFCOP3  IEBCOPY D.E. COUNTS ?    82011
         BNM   DATANOTL      NO                                  82011
         B     BCOPY3        YES                                 82011
BCOPY1   L     R2,DALEN      GET BLOCK LENGTH                    82011
         CH    R2,=H'288'    LONG ENOUGH FOR IEHMOVE BASIC STUFF ?
         BL    BCOPY1B       NO; CHECK IEBCOPY                   82011
         CLC   =C'IBM UTILITY, SYSMOVE',BUFDATA+50               82004
         BNE   BCOPY1B                                           82011
         MVI   UTYFLAG,UTFMOVE   SHOW IEHMOVE RECORD             82004
         MVC   UTYDSN,DS1DSNAM  MOVE DATASET NAME                82004
         MVC   UTYTYPE,MV1TYPE  UCB TYPE                         82004
         MVC   UTYDSORG(UTYKEYLN-UTYDSORG+1),DS1DSORG+2 (FINAGLE)
*        NOW SEE IF THE IEHMOVE DEVICE IS GENERATED IN THIS SYSTEM
         CLI   DS1NOEPV,0    ANY EXTENTS IN DATASET ?            82017
         BE    DATANUTL      NO; DON'T CARE ABOUT SPACE          82017
         CLI   MV1TYPE+2,UCB3DACC  DASD TYPE ?                   82017
         BNE   DATANUTL      NO; SKIP SPACE                      82017
         LA    R2,DVCTYPMK   GET SUB-TYPE MASK                   82017
         IC    R1,MV1TYPE+3  GET SUB-TYPE                        82115
         NR    R1,R2         MASK IT                             82115
         SERVCALL DVTBL,(R1)  FIND THE DEVICE TABLE ENTRY        82115
         LTR   R2,R0         IS ENTRY AVAILABLE ?                82115
         BZ    DATANUTL      NO; SKIP OUT                        82115
         USING DVCCYL,R2                                         82017
         LH    R2,DVCTRK     SAVE TRACKS PER CYLINDER            82017
         DROP  R2                                                82017
         SLR   R3,R3         ZERO TRACK COUNTER                  82017
         LA    R4,DS1EXT1+2  POINT TO FIRST EXTENT (+FINAGLE)    82017
         SLR   R5,R5                                             82017
         IC    R5,DS1NOEPV   GET NUMBER OF EXTENTS               82017
         LA    R1,3          SET MAXIMUM IN IEHMOVE BUFFER       82017
         CR    R5,R1         MORE THAN PRESENT ?                 82017
         BNH   *+6           NO                                  82017
         LR    R5,R1         SET TO MAXIMUM AVAILABLE            82017
BMOVE1C  SLR   R15,R15       CLEAR 'LAST' EXTENT SIZE            82017
         OC    0(10,R4),0(R4)   ANYTHING IN IT ?                 82017
         BZ    BMOVE1D       NO, ASSUME DONE                     82017
         MVC   DB(8),2(R4)   MOVE LOW CCHH, HI CCHH              82017
         LH    R1,DB+6       LAST TRACK                          82017
         SH    R1,DB+2         MINUS FIRST TRACK                 82017
         AH    R1,=H'1'          PLUS 1                          82017
         LH    R15,DB+4      LAST CYLINDER                       82017
         LR    R0,R2         SET TRACKS PER CYLINDER             82017
         CLI   0(R9),X'80'   SPLIT CYL. ?                        82017
         BNE   *+6           NO                                  82017
         LR    R0,R1         YES; TRKS/CYL IS HI-LO+1 TRACK      82017
         SH    R15,DB          MINUS START CYLINDER              82017
         MR    R14,R0        CYLS TO TRACKS                      82017
         AR    R15,R1        ADD IN TRACKS                       82017
         AR    R3,R15        PLUS ONES PREVIOUSLY DONE           82017
         LA    R4,DS1EXT2-DS1EXT1(,R4)  POINT TO NEXT EXTENT     82017
         BCT   R5,BMOVE1C    TRY NEXT EXTENT                     82017
BMOVE1D  LA    R1,3          SET MAXIMUM EXTENTS IN IEHMOVE BLOCK
         IC    R5,DS1NOEPV   GET EXTENTS PRESENT                 82017
         SR    R5,R1         ANY ADDITIONAL ONES ?               82017
         BNP   BMOVE1E       NO                                  82017
         MR    R14,R5        ASSUME 4-16 SAME SIZE AS EXTENT 3   82017
         AR    R3,R15        ADD TO TOTAL                        82017
BMOVE1E  ST    R3,UTYTRACK   SAVE TRACK TOTAL                    82017
         B     DATANUTL      DONE WITH MOVE DATA                 82004
         SPACE 1                                                 82004
BCOPY1B  CH    R2,=H'60'     IEBCOPY SIZE ?                      82011
         BE    BCOPY1C       YES                                 91072
         CH    R2,=H'64'     DIFFERENT IEBCOPY SIZE ?            91072
         BNE   DATANUTL                                          82004
BCOPY1C  CH    R2,CP1RDW     SAME SIZE IN RDW ?                  82004
         BNE   DATANUTL      NO                                  82004
         CLC   =X'CA6D0F',CP1ID  IEBCOPY IDENTIFIER ?            82004
         BNE   DATANUTL      NO                                  82004
         CLI   CP1TYPE+2,UCB3DACC  DASD DEVICE TYPE ?            82004
         BNE   DATANUTL      NO                                  82004
         MVC   UTYTYPE,CP1TYPE  COPY FIELDS                      82004
         MVC   UTYDSORG,CP1DSORG                                 82004
         MVC   UTYRECFM,CP1RECFM                                 82004
         MVC   UTYOPTCD,CP1OPTCD                                 82004
         MVC   UTYBLKSI,CP1BLKSI                                 82004
         MVC   UTYLRECL,CP1LRECL                                 82004
         MVC   UTYKEYLN,CP1KEYLN                                 82004
         MVC   UTYTPCYL,CP1DZTRK  TRACKS/CYLINDER                82004
         MVI   UTYFLAG,UTFCOP1   SHOW FIRST IEBCOPY RECORD COMPLETE
         B     DATANOTL      READ NEXT BLOCK                     82017
         SPACE 1                                                 82018
BCOPY2   CLI   UTYFLAG,UTFCOP1   FOLLOWS GOOD IEBCOPY FIRST BLOCK ?
         BNE   DATANUTL      NO; TOO BAD                         82004
         L     R2,DALEN      GET BLOCK LENGTH                    82004
         CH    R2,=H'284'    CORRECT LENGTH ?                    82004
         BNE   DATANUTL      NO                                  82004
         CH    R2,CP2RDW     CORRECT LENGTH IN RDW ?             82004
         BNE   DATANUTL      NO                                  82004
         SLR   R2,R2                                             82011
         ICM   R2,1,DEBNMEXT-DEBNMEXT+CP2DEB  TEST NUMBER OF EXTENTS
         BZ    BCOPY2D       NONE                                82011
         LA    R1,CP2DASD    POINT TO FIRST EXTENT               82011
         SLR   R0,R0         ZERO TRACK ACCUMULATOR              82011
BCOPY2A  AH    R0,DEBNMTRK-DEBDASD(,R1)  ADD TRACKS IN THIS EXTENT
         LA    R1,DEBNMTRK-DEBDASD+L'DEBNMTRK(,R1)               82011
         BCT   R2,BCOPY2A    GET NEXT EXTENT                     82011
         ST    R0,UTYTRACK   SAVE NUMBER OF TRACKS               82011
BCOPY2D  MVI   UTYFLAG,UTFCOP2  SHOW IEBCOPY COMPLETE (BEFORE DE CODE)
         B     DATANOTL      PROCESS THIS BLOCK                  82011
         SPACE 1                                                 82011
BCOPY3   L     R0,DALEN                                          82011
         LA    R1,CP3BDW     POINT TO BDW                        82011
         CH    R0,CP3BDW     CORRECT BDW ?                       82011
         BNE   BCOPY3Y       NO; FAIL IT                         82011
         LA    R1,CP3RDW     SKIP BDW                            82011
         SH    R0,=H'4'      DECREMENT RESIDUAL LENGTH           82011
         BNP   DATANOTL      SKIP NULL RECORD                    82011
         USING CP3RDW,R1     DECLARE RDW MAPPING                 82011
BCOPY3A  LH    R2,CP3RDW     GET SEGMENT LENGTH                  82011
         LA    R3,CP3BLKID   POINT PAST RDW                      82011
         SH    R2,=H'4'      ANYTHING IN IT ?                    82011
         BNP   BCOPY3N       NO; GET ANOTHER                     82011
         DROP  R1                                                82011
         USING CP3BLKID,R3                                       82011
BCOPY3B  CLC   CP3BLKID,ZEROES   PROPER ID ?                     82011
         BNE   BCOPY3Y       NO; DON'T KNOW WHAT IT IS ?         82011
         CLC   ZEROES(2),CP3KLDL+1  ZERO DL ?                    82011
         BE    BCOPY3X       YES; QUIT ON END-FILE               82011
         CLC   =X'080100',CP3KLDL   PDS KEY/LENGTH ?             82011
         BNE   BCOPY3Y       NO; QUIT COUNTING                   82011
         INC   UTYDE,WORK=R15  COUNT THE DIRECTORY BLOCK         82011
         CLC   CP3KEY,=8X'FF'   LAST BLOCK ?                     82011
         BE    BCOPY3X       YES; QUIT                           82011
BCOPY3L  LA    R3,CP3KEY-CP3BLKID+8+256(,R3)  GET NEXT BLOCK     82011
         SH    R2,=Y(CP3KEY-CP3BLKID+8+256)  RESIDUAL LENGTH     82011
         CH    R2,=Y(CP3KEY-CP3BLKID)  ROOM FOR ANOTHER BLOCK ?  82011
         BNL   BCOPY3B       YES; DO IT                          82011
         DROP  R3                                                82011
BCOPY3N  LH    R2,0(,R1)     GET RDW LENGTH                      82011
         AR    R1,R2                                             82011
         SR    R0,R2         GET REMAINING LENGTH                82011
         BP    BCOPY3A       TRY AGAIN                           82011
         B     DATANOTL      GET ANOTHER BLOCK                   82011
BCOPY3Y  XC    UTYDE,UTYDE   CLEAR COUNTS ON ERROR               82011
BCOPY3X  OI    UTYFLAG,UTFCOP3  SHOW DE COUNTS COMPLETE          82011
*        B     DATANUTL      ENABLE SKIP MODE                    82011
         SPACE 1                                                 82004
DATANUTL TM    FLAG,FGSKIP   SKIP MODE REQUESTED ?               82011
         BZ    DATANOTL      NO                                  82011
         TM    FLAG,FGSKIPSL   CONDITIONAL SKIP ?                82002
         BZ    DATANUTS      NO; SET SKIP                        82011
         TM    SLFG,SLNL     NL TAPE ?                           82002
         BNZ   DATANOTL      YES; NO SKIP                        82011
DATANUTS OI    SLFG,SLSPACE  ENABLE SPACING                      82011
         SPACE 1                                                 82011
DATANOTL L     R1,DALEN                                          82011
         LR    R0,R1         SAVE LENGTH FOR CCW REPLACEMENT     82160
         C     R1,BLKMIN                                         82002
         BNL   *+8
         ST    R1,BLKMIN                                         82002
         C     R1,BLKMAX                                         82002
         BNH   *+8
         ST    R1,BLKMAX                                         82002
         SLR   R4,R4         SET FIX COUNTER                     82002
         L     R5,=A(MGBIGBLK)   FOR BLOCK TOO BIG MESSAGE       89318
         C     R1,MAXBLKUS   EXCEEDS USER SPECIFIED BLOCKSIZE ?  82002
         BNH   DATASTAF      NO                                  82002
         TM    CLEANFG,FGTRUNC  RUNNING WITH TRUNCATE ?          82002
         BZ    DATABBLK      NO; ERROR                           82002
         L     R1,MAXBLKUS   TRUNCATE TO MAXIMUM                 82002
         LR    R0,R1         UPDATE FOR CCW LENGTH               82160
         LA    R4,1          SET FIX COUNTER                     82002
DATASTAF ICM   R2,15,FBLOCK  WAS A BLOCKSIZE STASHED ?           82002
         BZ    DATASTAX      NO; SKIP TEST                       82002
         CR    R1,R2         LONGER ?                            82002
         BNH   DATASTAX      NO; MAY BE OK ?                     82002
         TM    CLEANFG,FGTRUNC  RUNNING WITH TRUNCATE ?          82002
         BZ    DATABBLK      NO; TOO BAD                         82002
         LR    R1,R2         TRUNCATE                            82002
         LR    R0,R1         UPDATE FOR CCW LENGTH               82160
         LA    R4,1          SET FIX FLAG                        82002
DATASTAX CLI   RECFM,C'F'    FIXED PROCESSING ?                  82002
         BE    DATAFBLK      YES; CHECK SIZES AND MULTIPLES      90207
         CLI   RECFM,C'V'    VARIABLE LENGTH ?                   90207
         BNE   DATAVG        NO, DO NORMALLY
         CLM   R0,3,CP3BDW   DOES SIZE MATCH BDW SIZE ?          90207
         BE    DATAVG        YES                                 90207
         TM    CLEANFG,FGTRUNC  FIX UP ?                         90207
         BZ    DATAVG        NO; IGNORE ERROR                    90207
         SLR   R14,R14                                           90207
         ICM   R14,3,CP3BDW  LOAD BDW                            90207
         MIN   (R0),(R14)    COPY SHORTER LENGTH                 90207
         STCM  R0,3,CP3BDW   AND MAKE IT LOGICAL LENGTH          90207
         LA    R4,1          SET FIX-UP                          90207
         B     DATAVG        CONTINUE                            90207
DATAFBLK CR    R1,R2         TEST BLOCK SIZE                     90207
         BE    DATAFDIV      NORMAL, OK                          82002
         INC   BLKSHORT,WORK=R15  UP COUNT OF SHORT BLOCKS       82160
         CH    R15,DCH1      FIRST SHORT BLOCK ?                 82160
         BH    DATAFDIV      NO; USE TRUE LENGTH IN AVERAGE      82160
         LR    R1,R2         FOR AVERAGE - USE FULL LENGTH       82002
DATAFDIV LR    R15,R1        COPY THE LENGTH                     82002
         SLR   R14,R14                                           82002
         D     R14,FRECLN    DIVIDE BY RECORD LENGTH             82002
         LTR   R14,R14       ANY REMAINDER ?                     82002
         BZ    DATAVG        NO                                  82002
         L     R5,=A(MGMODBLK)   SET FOR MOD(LRECL,BLKSIZE)=0   82002
         TM    CLEANFG,FGTRUNC  TRUNCATION ?                     82002
         BZ    DATABBLK      NO; ERROR                           82002
         LA    R4,1          SET FIX COUNTER                     82002
         M     R14,FRECLN    MAKE BLOCKSIZE A MULTIPLE           82002
         LR    R1,R15        SET GOOD SIZE                       82002
         LR    R0,R1         UPDATE FOR CCW SIZE                 82160
DATAVG   A     R4,BLKFIX     UP FIX COUNT                        82002
         ST    R4,BLKFIX                                         82002
         STH   R0,RITECCW+6  UPDATE POSSIBLY TRUNCATED LENGTH    82160
         LM    R2,R3,AVGBLK                                      82002
         ALR   R3,R1
         BC    12,*+8        BRANCH IF NO CARRY
         AH    R2,DCH1       GERY-CARRY                          82004
         STM   R2,R3,AVGBLK
         SPACE 1
DATAPRT  L     R15,BLKALL    GET CURRENT BLOCK NUMBER            82002
         C     R15,DMEND     PAST END OF PRINT ?                 82002
         BH    DATAREAD      YES; GO TO READ OR FSF              82002
         L     R0,DMSTART    GET FIRST RECORD TO BE DUMPED       82002
         SR    R15,R0        GET OFFSET FROM FIRST PRINT RECORD  82002
         BM    DATAREAD      NOT QUITE THERE YET; READ AGAIN     82002
         TM    ERROPT,ERRNOPRT  ALREADY PRINTED DUE TO I/O ERR ? 83353
         BNZ   DATAREAD      YES; READ NEXT                      83353
         SLR   R14,R14                                           82002
         D     R14,DMINC     SEE IF MULTIPLE OF PRINT INCREMENT  82002
         LTR   R14,R14                                           82002
         BNZ   DATAREAD      NO; GET ANOTHER RECORD              82002
         TM    LBLFG,LPROT+LPROF    BLOCK TO BE SKIPPED ?        82002
         BZ    DATAPRT1        NO - OK TO DISPLAY                *TSM*
         TM    LBLFG,LPROM   MESSAGE ALREADY ISSUED ?            82002
         BNZ   DATAREAD      YES; JUST SKIP                      82002
         OI    LBLFG,LPROM   INDICATE MESSAGE ISSUED             82002
         PRTV  =A(VLSUPPR)   'BLOCK SUPPRESSED'                  82002
         B     DATAREAD      SKIP BLOCK                          *TSM*
DATAPRT1 PRTSPACE 1          DOUBLE-SPACE                        82002
         LA    R9,DATAREAD   SET RETURN POINT
         L     R1,DALEN      GET DATA LENGTH                     82002
         L     R0,DMSIZE     GET USER'S DUMP SIZE                82002
         CR    R1,R0         DUMP MAX(USER,ACTUAL) SIZE          82002
         BNH   DADST                                             82002
         LR    R1,R0         SET FOR USER SIZE                   82002
DADST    ST    R1,DMLEN      SET DUMP LENGTH                     82002
         L     R5,DMLEN      GET LENGTH                          82002
         LA    R6,BUFDATA    GET START DUMP ADDRESS              82002
         PRTLIST VLBHD       PRINT BLOCK HEADER                  82002
         AGO   .OLDDUMP                                          90243
         SPACE 1
         XC    DMDISP,DMDISP   CLEAR DISPOSITION FIELD
         SPACE 1
DATALINE LTR   R5,R5         ANY BYTES LEFT TO BE DUMPED ?
         BNPR  R9            NO, RETURN TO CALLER
         MVC   WORKLINE(99),BLANKS  CLEAR PRINT LINE AGAIN       90243
         UNPK  VLDMDISP(5),DMDISP+2(3)                           82002
         MVI   VLDMDISP+L'VLDMDISP,C' '                          82002
         TR    VLDMDISP,HEXTRTAB-C'0'                            82002
         L     R1,DMDISP     GET CURRENT DISPLACEMENT            82002
         LA    R1,32(,R1)    UP IT FOR NEXT TIME
         ST    R1,DMDISP     AND SAVE IT                         82002
         LA    R2,VLDMHEX    GET FIRST HEX OUTPUT FIELD
         LA    R3,8          SET FOR EIGHT WORDS PER LINE
         LA    R14,VLDMEBC   SET FIRST OUTPUT BYTE
         MVC   VLDMEBC,BLANKS  CLEAR EBCDIC OUTPUT FIELD
         MVC   VLDMHEX(80),BLANKS   CLEAR HEX OUTPUT FIELD
         SPACE 1
DATAWORD MVC   DB(4),0(R6)   MOVE DATA WORD (PREVENT 0C4 ON LAST BYTE)
         UNPK  UPD(9),DB(5)  UNPACK CURRENT WORD                 82002
         TR    UPD(8),HEXTRTAB-C'0'   PRINTABLE HEX              82002
         LA    R15,4         SET FOR MAXIMUM CHUNK               90243
         MIN   (R15),(R5)    SET FOR SMALLER OF AVAILABLE/DOABLE 90243
         BCTR  R15,0         LENGTH FOR MVC
         EX    R15,EXMVC2    MOVE TEXT TO LINE                   90243
         LA    R15,1(R15,R15)   GET DOUBLE LENGTH - 1 FOR HEX MOVE
         EX    R15,EXMVC1    MOVE HEX DATA                       90243
         LA    R14,4(,R14)   UP TO NEXT EBCDIC O/P
         LA    R2,10(,R2)    SET NEXT HEX O/P FIELD
         CH    R3,=H'5'      DONE EXACTLY FOUR WORDS ?           82002
         BNE   *+8           NO
         LA    R2,2(,R2)     YES - SKIP 2 MORE BYTES
         LA    R6,4(,R6)     SKIP TO NEXT INPUT WORD
         SH    R5,=H'4'      GET REMAINING LENGTH                82002
         BNP   DATAPUT       NO MORE - PRINT AND EXIT
         BCT   R3,DATAWORD   DO REST OF LINE, OR FALL THROUGH TO PRINT
         SPACE 1
DATAPUT  PRTF  WORKLINE,L'WORKLINE  PRINT HEX/EBCDIC DATA LINE   82002
         B     DATALINE      DO NEXT DATA LINE
EXMVC1   MVC   0(8-8,R2),UPD   MOVE HEX DATA                     90243
EXMVC2   MVC   0(4-4,R14),0(R6)  MOVE TEXT                       90243
.OLDDUMP BR    R9            RETURN TO CALLER                    90243
         SPACE 2
DATADUMP L     R1,DALEN      GET BLOCK LENGTH                    82002
         C     R1,ERRSIZE                                        82002
         BNH   DADST         SIZE SMALLER THAN LIMIT             82002
         L     R1,ERRSIZE    PRINT LIMIT SIZE ONLY               82002
         B     DADST         START DUMPING                       82002
         SPACE 2
LABDUMP  TM    IOFG,IOEQC+IOERR+IOLEN  PHYSICAL ERROR ?          82002
         BNZ   LABDUMPN      YES                                 82002
         INC   BLKCNT,INC=-1                                     82002
         INC   BLKERR        FIX COUNTS                          82002
LABDUMPN L     R1,DALEN      GET LENGTH                          82002
         LR    R7,R9         SAVE RETURN ADDRESS                 82002
         LA    R9,LABDUMPX   SET RETURN TO EXCESSION TEST        82002
         CLC   BLKERR,PRTLABEL  ENOUGH IS ENOUGH ?               82002
         BHR   R9            YES; DON'T WASTE PAPER
         LA    R0,80         SET STANDARD LABEL LENGTH           82002
         CR    R1,R0         LIMIT TO 80 BYTES                   82002
         BNH   DADST         START DUMPING                       82002
         LR    R1,R0         SET FOR EIGHTY BYTE DUMP            82002
         B     DADST         GO TO SET DUMP LENGTH AND PROCESS   82002
LABDUMPX LR    R9,R7         RESTORE RETURN REGISTER             82002
         LA    R1,1                                              82002
         A     R1,CONSOLD    MAKE CONSECUTIVE ERROR COUNT        82002
         ST    R1,CONSCNT    SET IT BACK                         82002
         CLC   BLKERR,MAXLBERR  MAXIMUM ERRORS EXCEEDED ?        82002
         BH    QUITERR       YES; QUIT                           82002
         C     R1,MAXCONS    CONSECUTIVE ERROR LIMIT REACHED ?   82002
         BH    QUITCONS      YES; QUIT                           82002
         BR    R9            RETURN TO CALLER                    82002
QUITERR  PRTSPACE 1          DOUBLE-SPACE ON PRINT FILE          82002
         PRTV  MGMAXERR,DEV=(1,2),CC=NO                          82002
         B     FORCLOSE      FORCE VOLUME TERMINATION            82002
         SPACE 2                                                 *UOW*
TRALALA  BAL   R9,LABLEN     TRUNCATE LENGTH TO 80               85119
         BAL   R9,LABPRINT   PRINT THE LABEL                     85119
TRAILERP BAL   R9,TRASPILL   WRITE STACKED EOF1/EOF2 RECORDS     82002
         BAL   R9,PUTOUTOP   WRITE PREVIOUS RECORD               82002
TRAILERS BAL   R9,OPENOUT    OPEN THE OUTPUT FILE                82002
         BAL   R9,READBLKS                                       82002
         BAL   R9,TRASPILL   WRITE STACKED RECORDS, IF NECESSARY 82002
         TM    IOFG,IOEOF                                        82002
         BZ    TRAILA
         BAL   R9,PUTOUTOP   WRITE THE TAPEMARK                  82002
         TM    LBLFG,LHDR1                                       82002
         BZ    TRAILNUS      BAD TRAILER INFO
         MVC   SAVEHDR1,DXLBL                                    82002
         BAL   R9,UPDTLR1    FIX OUTPUT VOL-SER                  88181
         MVC   DXLBL(80),SAVEHDR1                                82002
         TM    FLAG,FGEOV    CONTINUED ?
         BZ    TRAILUTM      NO; CHECK FOR UTILITY SUMMARY       82017
         PRTV  VOLCONT,DEV=(1,2)                                 93098
TRAILUTM CLI   UTYFLAG,UTFCOP2  ANY UTILITY DATA PRESENT ?       82017
         BL    CLOSEFIL      NO; JUST TERMINATE THE FILE         82017
         XC    WORKLINE(99),WORKLINE                             82017
         MVC   WORKLINE+UCBTBYT1-UCBOB(4),UTYTYPE MAKE FAKE UCB  82017
         SERVCALL UCBGN,WORKLINE  GET DEVICE NAME FROM FAKE UCB  82017
         MVC   UTYGEN,0(R1)   MOVE GENERIC                       82017
         SERVCALL DSFMT,UTYDSORG  FORMAT FAKE DSCB1 DCB PARMS    82017
         MVC   WORKLINE(27),0(R1)  MOVE RESULT                   82017
         PRTLIST UTILFD,DEV=(2)  FORMAT ON SUMMARY ONLY          82017
         PRTSPACE 1,DEV=2    MAKE LISTING PRETTY                 82017
         MVC   WORKLINE(99),BLANKS   CLEAR WORK SPACE AGAIN      82017
         B     CLOSEFIL                                          82002
         SPACE 1
TRAILNUS PRTV  /VOLNUSE,DEV=(1,2)                                90243
TRAILNEW OI    FLAG,NEWVOL                                       82017
         B     TRAILUTM                                          82017
         SPACE 1
TRAILA   TM    IOFG,IOFBAD                                       82002
         BNZ   TRAILER
         TM    FL1NO,C'0'    NUMERIC ?
         BNO   TRAILER
         BAL   R9,LABLEN     TEST AND CORRECT LABEL LENGTH       82002
         CLC   FL1LABI,DCTRAIL
         BE    TRAILN
         CLC   FL1LABI,DCTRALE  END-VOL ?
         BE    TRAILM        YES, SIGNAL CONTINUATION
         TM    LBLFG,LHDR1+LHDR2                                 82002
         BNO   TRAILER
         LA    R9,TRAILERP
         LA    R1,TRAILABE
         LA    R2,(TRAILABT-TRAILABE)/3
TRAILE   CLC   FL1LABI,0(R1)   VALID ?
         BE    TRALALA       YES, PRINT AND CONTINUE             82002
         LA    R1,3(,R1)     TRY AGAIN
         BCT   R2,TRAILE
         SPACE 2
TRAILER  BAL   R9,ERRMSG     CHECK FOR SYNAD MSG                 82002
         TM    IOFG,IOLEN   ZERO LENGTH ?                        82002
         BNZ   TRAILER5                                          82002
*        TM    IOFG,IOERR                                        82002
*        BO    TRAILER4
*        BAL   R9,LABDUMP                                        82002
*        B     TRAILER5
TRAILER4 BAL   R9,LABDUMP
TRAILER5 PRTV  HDINVAL                                           82002
         OI    CC,CCERROR    SET PROB. ERROR                     82002
         CLC   BLKERR,MAXLBERR                                   82002
         BL    TRAILERP
         B     TRAILNEW                                          82017
         SPACE 2
TRAILM   OI    FLAG,NEWVOL+FGEOV   NORMAL END OF VOLUME; FILE CONTINUED
         SPACE 1
TRAILN   LA    R9,TRAILERP
         CLI   FL1NO,C'1'    VALID ?
         BL    TRAILER
         CLI   FL1NO,C'2'    VALID ?
         BH    TRALALA       PRINT AND COPY DEC'S EOF3           85119
         BE    TRAILV2
         TM    LBLFG,LHDR1   TRAILER 1 READ BEFORE ?             82002
         BNZ   TRAILPSQ      YES; SHOW SEQUENCE ERROR            82002
         MVC   SAVEHDR1,DXLBL                                    82002
         BAL   R9,UPDTLR1    FIX OUTPUT VOL-SER                  88181
         MVC   DXLBL(80),SAVEHDR1                                82002
         OI    LBLFG,LHDR1                                       82002
         PRTLIST VLHDR1                                          82002
         PRTLIST VLHDR1F                                         82002
         PRTLIST VLHDR1G     ADD BLOCKS EXPECTED                 82002
         TM    FLAG,FGSKIP   RUNNING IN SKIP MODE ?              82004
         BZ    TRAILB        NO                                  82004
         PRTLIST SPACE5,DEV=2  FINAGLE                           82004
         PRTLIST VLTLR1G,DEV=2  ADD EXPECTED NUMBER OF BLOCKS    82004
TRAILB   L     R0,BLKFILE    BLOCK COUNT OF DATA FILE            82004
         CVD   R0,DB         PACK                                82002
         UNPK  CLCNT,DB      UNPACK BLOCK COUNT FOR DATAFILE     82002
         OC    CLCNT,DC3C0   OR IN ZEROES
         OC    FL1BLKCT,DC3C0  OR IN ZEROES
         CLC   CLCNT,FL1BLKCT    COUNTS EQUAL ?
         BE    TRAILG        YES
         TM    FLAG,FGSKIP   SKIP MODE ?                         82002
         BNZ   TRAILG        YES - COUNTS INVALID - DATA NOT READ
         PRTV  CNTBAD,DEV=(1,2)   SHOW BAD BLOCK COUNT           82002
         OI    CC,CCERROR    SET PROB. ERROR                     82002
TRAILG   CLC   FL1GNO(L'FL1GNO+L'FL1VNG),BLANKS   GDG DATA ?
         BE    TRAILV1       NO                                  82002
         PRTLIST VLHDR1S                                         82002
TRAILV1  TM    LBLFG,LHDR2   TRAILER 2 READ PREVIOUSLY ?         82002
         BZ    TRAIL1V2      NO; GO TO COMMON                    82002
         B     TRAILSEQ                                          82002
TRAILPSQ BAL   R9,LABDUMP                                        82002
TRAILSEQ PRTV  MSGOUTSQ,DEV=(1,2)   SHOW OUT-OF-SEQUENCE         93098
         OI    CC,CCWARN     PROBABLY 137 ABEND IN SL            93098
TRAIL1V2 MVI   BUFLAG,BUFEMPTY  KILL IT                          82002
         B     TRAILERP
         SPACE 1
TRAILV2  TM    LBLFG,LHDR2   TRAILER 2 READ PREVIOUSLY ?         82002
         BNZ   TRAILPSQ      YES; SIGNAL ERROR                   82002
         OI    LBLFG,LHDR2                                       82002
         TM    CLEANFG,FGCUCC  COLUMBIA KERMIT ADJUSTMENT ?      84253
         BZ    *+10          NO                                  84253
         MVC   FL2LRECL,CUCCLREC  FORCE ADJUSTED LRECL           84253
         MVC   SAVEHDR2,DXLBL  MOVE IT                           82002
         BAL   R9,UPDHDR2    SET RECFM; ADJUST DENSITY/TRTCH     82002
         MVC   DXLBL(80),SAVEHDR2  COPY BACK FOR FORMATTING      82002
         PRTLIST VLHDR2                                          82002
         TM    LBLFG,LHDR1+LHDR2                                 82002
         BNO   TRAILSEQ      ERROR IF TRAILER 1 NOT YET FOUND    82002
         B     TRAIL1V2      STACK THIS RECORD                   82002
         SPACE 1                                                 82002
TRASPILL TM    STACKFG,STKHDR1+STKHDR2  TRAILERS YET TO BE DONE ?
         BZR   R9            NO                                  82002
         TM    LBLFG,LHDR1+LHDR2  TRAILERS READ YET ?            82002
         BO    TRASPILD      YES                                 82002
         TM    IOFG,IOERR+IOEQC+IOEOF  ERROR CONDITION ?         82002
         BNZ   TRASPILD      YES; PROCESS                        82002
         CLC   DXLBL(2),DCTRAIL  LABEL RECORD ?                  82002
         BER   R9            YES; PROCESS                        82002
TRASPILD STM   R8,R9,UPD     SAVE BUFFER AND RETURN              82002
         TM    LBLFG,LHDR1   WAS TRAILER 1 FOUND ?               82002
         BNZ   TRASPILE      YES                                 82002
         TM    STACKFG,STKHDR1  WAS HEADER 1 READ ?              82002
         BZ    TRASPILX      NO; ERROR                           82002
         TM    FLAG,FGCOPY   COPY OP ?                           93098
         BZ    TRASPILE      NO; SKIP OUTPUT                     93098
         MVC   SAVEHDR1(3),DCTRAIL  MAKE EOF1 DEFAULT            82002
         BAL   R9,UPDTLR1    UPDATE VOL-SER, ETC.                88181
TRASPILE TM    FLAG,FGCOPY   COPY OP ?                           93098
         BZ    TRASPILG      NO; SKIP OUTPUT                     93098
         LA    R1,SAVEHDR1                                       82002
         BAL   R9,PUTHDR     WRITE STACKED TRAILER               82002
TRASPILG TM    LBLFG,LHDR2   WAS TRAILER 2 READ ?                82002
         BNZ   TRASPILF      YES                                 82002
         TM    STACKFG,STKHDR2  WAS HEADER 2 READ ?              82002
         BZ    TRASPILY      NO; JUST GET OUT                    82002
         TM    FLAG,FGCOPY   COPY OP ?                           93098
         BZ    TRASPILF      NO; SKIP OUTPUT                     93098
         BAL   R9,UPDHDR2    UPDATE HEADER 2                     82002
         MVC   SAVEHDR2(3),SAVEHDR1  MAKE EOF2 OR EOV2           93098
         PRTV  MSGNOEF2,DEV=(1,2)   ISSUE WARNING                93098
         OI    CC,CCWARN     PROBABLY 137 ABEND IN SL            93098
TRASPILF TM    FLAG,FGCOPY   COPY OP ?                           93098
         BNZ   TRASPILY      NO; GET OUT                         82002
         LA    R1,SAVEHDR2                                       82002
         BAL   R9,PUTHDR     WRITE EOF/EOV2                      82002
         B     TRASPILY      AND EXIT                            82002
TRASPILX PRTV  MSGNOEF1,DEV=(1,2)   WRITE DIAGNOSTIC             93098
         OI    CC,CCWARN     PROBABLY 137 ABEND IN SL            93098
TRASPILY LM    R8,R9,UPD     RESTORE                             82002
TRASPILZ NI    STACKFG,255-STKHDR1-STKHDR2  SHOW NO HEADERS      82002
         BR    R9            RETURN TO CALLER                    82002
         SPACE 2
TESTCLOS TM    FLAG,FGFORCE  TAPE PROCESSING FORCED ?            82002
         BNZR  R9            YES; RETURN TO CONTINUE             82002
         CLC   MAXFIL,DC7FFF     USER END-FILE NUMBER SET ?      82004
         BE    4(,R9)        NO; STOP NOW                        82002
         BR    R9            LET CLOSEFIL CHECK IF REACHED       82002
         SPACE 1                                                 82002
FORCLOSE OI    CC,CCMAJOR    FORCE ERROR                         82002
         OI    FLAG,NEWVOL   REQUEST ANOTHER VOLUME              82002
         B     CLOSEWTM      AND QUIT                            82025
         SPACE 1                                                 82002
CLOSENEW OI    FLAG,NEWVOL   SET FOR SWITCH AND WRITE TAPE-MARK  82002
CLOSEWTM MVI   BUFLAG,BUFREAD+IOEOF  FORCE END-FILE              82025
         BAL   R9,PUTOUTST   WRITE TAPEMARK                      82025
CLOSEFIL L     R5,MAXEXCP    GET BLOCKS TO CHECK                 82002
         LA    R6,PUTGOOD3   FORCE COMPLETE WRITES               82004
         BAL   R9,PUTWAIT    DO FINAL RITES                      82002
         L     R5,MAXEXCP                                        82004
         L     R8,CURBLOCK                                       82004
CLOSECLR ICM   R8,7,BUFLINKA  GET NEXT BLOCK                     82004
         TM    BUFLAG,BUFREAD  READ ISSUED ?                     82004
         BZ    CLOSECLN      NO                                  82004
         TM    BIOECB,X'40'  POSTED ?                            82004
         BNZ   CLOSECLN      YES; JUST RESET IT                  82004
         WAIT  ECB=BIOECB                                        82004
CLOSECLN MVI   BUFLAG,BUFEMPTY  RESET THE BUFFER                 82004
         BCT   R5,CLOSECLR   CLEAN ALL                           82004
         TM    FLAG,NEWVOL   REAL CLOSE REQUIRED ?               82002
         BNZ   CLOSEVOL      YES; CLOSE THE VOLUME               82002
         XC    BLKEXCP,BLKEXCP  CLEAR STACKED I/O COUNT          82004
         NI    DCBIFLGS-IHADCB+SYSUT1,255-DCBIFEC  RESET ERROR BITS
         NI    DCBOFLGS-IHADCB+SYSUT1,255-DCBOFTM  RESET 'AT TM' 82004
         LA    R0,1                                              82002
         AH    R0,PHYSFIL    GET NEW FILE NUMBER                 82002
         STH   R0,PHYSFIL    AND SET IT                          82002
         LH    R1,MAXFIL     GET LIMIT FILE NUMBER               82002
         TM    SLFG,SLNL     SL ?                                82002
         BNZ   CLOSEVNL      NO; LEAVE COUNT                     82002
         MH    R1,=H'3'      MAKE TRUE FILE NUMBER               82002
CLOSEVNL CR    R0,R1         REACHED LIMIT FILE ?                82002
         BNH   FILELOOK      NO; DO THE NEXT FILE                82002
         PRTSPACE 1          DOUBLE-SPACE ON SYSPRINT            82002
         PRTV  FILELIM,DEV=(1,2)                                 82002
CLOSEVOL MVI   IOFG,IOCLOSE  SIGNAL CLOSE IN PROCESS             82002
         TM    SYSUT2-IHADCB+DCBOFLGS,DCBOFOPN  OUTPUT OPEN ?    82002
         BNZ   CLOSEVOT      YES                                 82002
         CLOSE (,REWIND),MF=(E,OPLSTIN)  CLOSE WITH REWIND OPTION
         TM    FLAG,FGCOPY   COPY OP ?                           82002
         BZ    CLOSEVOX      NO                                  82002
         PRTV  /MSGNOCPY,DEV=(1,2)  SHOW NOT COPIED              90243
         OI    CC,CCMAJOR    SET MAJOR ERROR                     82002
CLOSEVOT MVC   DCBBLKCT-IHADCB+SYSUT2(4),BLKFILE1 SET TMS BLOCK COUNT
         NI    OPLSTIN,255-X'80'  MAKE TWO DCB LIST              90243
         CLOSE (,REWIND,,REWIND),MF=(E,OPLSTIN) PARALLEL CLOSE   90243
         OI    OPLSTIN,X'80' RESTORE TWO SINGLE DCB LISTS        90243
CLOSEVOX NI    IOFG,255-IOCLOSE  SIGNAL CLOSE OK                 82002
         PRTROOM 0,DEV=(1,2)  FORCE PRINT PUT (IN CASE OF X22)   82017
*        B     SUBEXIT                                           82002
         SPACE 1                                                 82002
SUBEXIT  SLR   R15,R15                                           82002
         B     SUBEXCM                                           82002
SUBEX16  LA    R15,16        SET ERROR                           82002
SUBEXCM  L     R13,4(,R13)                                       82002
         L     R14,12(,R13)                                      82002
         LM    R0,R12,20(R13)                                    82002
         BR    R14           RETURN TO MAIN TASK                 82002
         SPACE 2                                                 82002
UPDTLR1  TM    FLAG,FGCOPY   COPY FUNCTION ?                     88181
         BZR   R9            NO; RETURN WITH NO CHANGES          88181
         L     R0,OUTALL     GET OUTPUT COUNT                    88181
         CVD   R0,DB         PACK IT                             88181
         UNPK  FL1BLKCT-DXLBL+SAVEHDR1,DB FIX OUTPUT BLOCK COUNT 88206
         OC    FL1BLKCT-DXLBL+SAVEHDR1,DC3C0                     88206
UPDHDR1  TM    FLAG,FGCOPY   COPY FUNCTION ?                     82002
         BZR   R9            NO; RETURN WITH NO CHANGES          82002
         TM    CLEANFG,FGUNSEC  CLEAR ?                          89055
         BNZ   UPDHDR1F      YES                                 89055
         CLI   FL1FSEC-DXLBL+SAVEHDR1,C' '  BLANK SECURITY BYTE ?
         BNE   UPDHDR1B      NO                                  83051
UPDHDR1F MVI   FL1FSEC-DXLBL+SAVEHDR1,C'0'  REPLACE BY IBM STANDARD
UPDHDR1B CLI   FL1FSEC-DXLBL+SAVEHDR1,C'0'  IBM UNPROTECTED ?    83051
         BNE   UPDHDR1S      NO; LEAVE IT ALONE                  83051
         TM    DCBOPTCD-IHADCB+SYSUT2,DCBOPTQ  ANSI OUTPUT ?     83051
         BZ    UPDHDR1S      NO; LEAVE IT                        83051
         MVI   FL1FSEC-DXLBL+SAVEHDR1,C' '  REPLACE BY BLANK     83051
UPDHDR1S CLC   VSSER,FL1FILSR-DXLBL+SAVEHDR1   SAME VOLSER ?     83051
         BNER  R9            NO; RETURN WITHOUT CHANGE           82002
         MVC   FL1FILSR-DXLBL+SAVEHDR1,JFCBVOLS-INFMJFCB+OUTJFCB 82002
         BR    R9            RETURN                              82002
         SPACE 1                                                 82002
UPDHDR2  MVC   RECFM,BLANKS  CLEAR RECFM FIELD                   82002
         MVC   RECFM(1),FL2RECFM-DXLBL+SAVEHDR2  PRIMARY RECORD FORMAT
         LA    R1,RECFM+1    SPACE OVER IT                       82002
         CLI   FL2BLKA-DXLBL+SAVEHDR2,C' '  ANY BLOCKING ?       82002
         BE    RECFMNBS      NO                                  82002
         MVC   0(1,R1),FL2BLKA-DXLBL+SAVEHDR2  COPY IT           82002
         CLI   FL2BLKA-DXLBL+SAVEHDR2,C'R'  VBS ?                82002
         BNE   RECFMNB1      NO                                  82002
         MVC   0(2,1),=C'BS'  SET BOTH                           82002
         LA    R1,1(,R1)     SKIP EXTRA                          82002
RECFMNB1 LA    R1,1(,R1)     SKIP OVER                           82002
RECFMNBS MVC   0(1,R1),FL2CNTRL-DXLBL+SAVEHDR2  CARRIAGE CONTROL BYTE
         TM    FLAG,FGCOPY   IS THIS A COPY OP ?                 82002
         BZR   R9            NO; DON'T BOTHER WITH DEN/TRTCH     82002
         TM    DCBOFLGS-IHADCB+SYSUT2,DCBOFOPN  OPEN YET ?       82002
         BZR   R9            NO; JUST RETURN                     82002
         LA    R1,PATDEN     POINT TO DENSITY TABLE              82002
         LA    R2,PATLNDEN   GET NUMBER OF DEFINED ENTRIES       82002
UPDDEN1  CLC   DCBDEN-IHADCB+SYSUT2(1),0(R1)  MATCHING DENSITY ? 82002
         BE    UPDDEN2       OK                                  82002
         LA    R1,PATDEN2-PATDEN(,R1)                            82002
         BCT   R2,UPDDEN1    ELSE TRY AGAIN                      82002
UPDDEN2  MVC   FL2DEN-DXLBL+SAVEHDR2(1),1(R1)  SET CORRECT DENSITY
         L     R1,DCBDEBAD-IHADCB+SYSUT2  GET THE DEB            82002
         L     R3,DXDEBUCB-DXDEB(,R1)    GET THE OUTPUT UCB      82002
         USING UCBOB,R3                                          90243
         CLI   UCBTBYT4,UCB3480  CARTRIDGE DRIVE ?               90243
         BE    UPD2SKD       YES; SKIP DENSITY, ETC.             90243
         LA    R15,PATTRTCX  SET TO 9-TRACK DEFAULT              82002
         TM    UCBTBYT2,UCB2OPT0  7-TRACK ?                      82002
         BZ    UPDTCH2       NO; SET 9-TRACK                     82002
         LA    R15,PATTRTCH   POINT TO TRTCH DEFINITIONS         82002
         LA    R14,PATLNTCH  NUMBER OF SPECIAL ENTRIES           82002
UPDTCH1  CLC   DCBTRTCH-IHADCB+SYSUT2(1),0(R15)  MATCH ?         82002
         BE    UPDTCH2       YES                                 82002
         LA    R15,PATTRTC2-PATTRTCH(,R15)                       82002
         BCT   R14,UPDTCH1                                       82002
UPDTCH2  MVC   FL2TRTCH-DXLBL+SAVEHDR2(2),1(R15)                 82002
UPD2SKD  MVC   FL2ID-DXLBL+SAVEHDR2(5),BLANKS  PROVISIONALLY CLEAR
         AIF   (&MVSXA).SKP2400                                  90199
         CLI   UCBTBYT4,UCB2400  2400 SERIES ?                   82002
         BER   R9            YES; THAT'S NON-SENSE               82002
.SKP2400 TM    DCBOPTCD-IHADCB+SYSUT2,DCBOPTQ  ANSI LABELS ?     82002
         BNZR  R9            YES; NO ID FIELD DEFINED            82002
         AIF   (&MVS).MVSSENS                                    82195
         TM    UCBFL5,UCBEXTSN  EXTENDED SENSE ?                 80048
         BZR   R9            NO                                  80048
         L     R1,UCBSENSE   GET SENSE BYTES                     80048
         DROP  R1                                                80048
         AGO   .COMSENS                                          82002
.MVSSENS STM   R8,R9,DB      SAVE CURRENT BUFFER ADDRESS         82002
         LA    R8,SPECIOB    GET SPECIAL IOB                     82002
         LA    R14,SENSEDAT  GET SENSE DATA ADDRESS              82002
         LA    R15,L'SENSEDAT    AND LENGTH                      82002
         CLI   UCBTBYT4,UCB3480  CARTRIDGE ?                     90243
         BNE   UPD2NCRT      NO                                  90243
         MVI   FL2DEN,C'0'   SET ONE AND ONLY DENSITY            90243
         MVC   FL2TRTCH,BLANKS  AND RECORDING TECHNIQUE          90243
         LA    R15,L'SENS3480  SET LONGER SENSE                  90243
UPD2NCRT STM   R14,R15,WTMCCW  CLOBBER WTM CCW                   90243
         MVI   WTMCCW,X'04'  SET SENSE OP                        82002
         MVI   WTMCCW+4,X'20'  SET SLI                           82002
         XC    SENS3480,SENS3480  PRE-CLEAR                      90243
         BAL   R9,PUTSPECL   DO SPECIAL I/O                      82002
         MVI   WTMCCW,X'1F'  RESTORE WTM CCW                     82002
         MVI   WTMCCW+4,X'60'                                    82002
         LM    R8,R9,DB      RESTORE BUFFER AND RETURN ADDRESS   82002
         LA    R1,SENSEDAT   GET SENSE DATA                      82002
.COMSENS CLI   UCBTBYT4,UCB3480  CARTRIDGE ?                     90243
         BNE   UPD2SN40      NO                                  90243
         UNPK  FL2ID-DXLBL+SAVEHDR2(5),28(3,R1) SENSE/LOGICAL/PHYSICAL
         MVC   FL2ID+4-DXLBL+SAVEHDR2(1),30(R1)  RESTORE PHYSICAL
         OI    FL2ID+4-DXLBL+SAVEHDR2,X'F0'  MAKE ZONE FOR TRANSLATE
         TR    FL2ID-DXLBL+SAVEHDR2(5),HEXTRTAB-C'0'             90316
         BR    R9            RETURN                              90243
UPD2SN40 SLR   R0,R0         CLEAR FOR ICM                       90243
         ICM   R0,3,15(R1)  GET SENSE BYTES 15-16; DRIVE SERIAL  82002
         LA    R15,15        MASK FOR MODEL                      82002
         IC    R14,6(,R1)    MODEL NUMBER++                      82002
         NR    R14,R15       MASK MODEL NUMBER                   82002
         LA    R14,TAPEMODL(R14)  POINT TO MODEL NUMBER          82002
         CVD   R0,DB        PACK SERIAL                          82002
         UNPK  FL2ID-DXLBL+SAVEHDR2(5),DB+5(3)    UNPACK         82002
         OI    FL2ID+L'FL2ID-1-DXLBL+SAVEHDR2,C'0'  SURE SIGN    82002
         MVN   FL2ID-DXLBL+SAVEHDR2(1),0(R14)  ADD MODEL NUMBER  82002
         BR    R9            RETURN TO CALLER                    82002
TAPEMODL DC    AL1(0,0,0,3,5,7,0,0,0,0,0,4,6,8,0,0)              80353
PATDEN   DC    X'03',C'0'    200 BPI                             82002
PATDEN2  DC    X'43',C'1'    556 BPI                             82002
         DC    X'83',C'2'    800 BPI                             82002
         DC    X'C3',C'3'    1600 BPI                            82002
         DC    X'D3',C'4'    6250 BPI                            82002
PATDENDF DC    X'00',C' '      UNKNOWN                           82002
PATLNDEN EQU   (PATDENDF-PATDEN)/(PATDEN2-PATDEN)  NUMBER OF ENTRIES
PATTRTCH DC    X'23',C'E '   EVEN PARITY                         82002
PATTRTC2 DC    X'3B',C'T '   BCD TRANSLATION                     82002
         DC    X'13',C'C '   DATA CONVERSION                     82002
PATTRTC9 DC    X'2B',C'ET'   EVEN, TRANSLATE                     82002
PATTRTCX DC    X'00',C'  '     ODD                               82002
PATLNTCH EQU   (PATTRTCX-PATTRTCH)/(PATTRTC2-PATTRTCH)  NUMBER   82002
SECORIDE TM    AUTHFLAG,VAASYS+VAASUP  SPECIAL USER ?            82002
         BNZ   SECORITE      YES; PERMIT ACCESS                  82002
         L     R1,TCBSAVE    GET CURRENT TCB                     82002
         USING TCB,R1        DECLARE IT                          82002
         AIF   ('&LOCAL' NE 'CCSI' AND '&LOCAL' NE 'IIH').NOLAUTH
         TM    TCBUSER,X'B0'  PASSWORD OVERRIDE ?  LOCAL MOD ?   82002
         BNZ   SECORITE      YES; PERMIT PRIVILEGED ACCESS       82002
.NOLAUTH L     R1,TCBJSCB    GET JSCB                            82002
         USING IEZJSCB,R1    DECLARE IT                          82002
         TM    JSCBSWT1,JSCBPASS  INVOKED AS AUTHORIZED SUBTASK ?
         DROP  R1                                                82002
         BNZ   SECORITE      YES; PERMIT                         82002
         PRTLIST MSGPRONO    INDICATE OVERRIDE NOT PERMITTED     82002
         BR    R9            RETURN TO CALLER                    82002
SECORITE PRTLIST MSGPROYE    INDICATE OVERRIDE PERMITTED         82002
         NI    LBLFG,255-LPROT-LPROF  RESET PROTECTION BITS      82002
         BR    R9            RETURN TO CALLER                    82002
         EJECT ,                                                 82002
READPURG L     R2,MAXEXCP    GET NUMBER TO CHECK                 82002
         XC    BLKEXCP,BLKEXCP  CLEAR UNCHECKED COUNT            82002
         NI    DCBIFLGS-IHADCB+SYSUT1,255-DCBIFEC  RESET ERROR BITS
         NI    SLFG,255-SLDIDSF  RESET FSF ISSUED                82004
READPURL CLI   BIOECB,X'4B'  PURGED ?                            82025
         BH    READBLKT      NO                                  82025
         CLI   BIOECB,X'44'  INTERCEPTED ?                       82002
         BL    READBLKT      NO                                  82025
READPURR MVI   BUFLAG,BUFEMPTY  SET BUFFER IDLE                  82002
         STCM  R8,7,CURBLOCK+1  RESTART I/O AFTER THIS ONE       82002
         ICM   R8,7,BUFLINKA   GET THE NEXT BUFFER               82002
         TM    BUFLAG,BUFRITE  WRITE BUFFER ?                    82004
         BNZ   READBLKT      YES; DON'T CLEAR                    82004
         BCT   R2,READPURL   CLEAR ALL                           82002
         B     READBLKT                                          82004
         SPACE 1                                                 82002
READBLKS MVC   CONSOLD(8),CONSCNT  SAVE OLD ERROR COUNT/ZERO NEW 82004
         MVI   IOFG,IOREAD  SHOW READ IN PROGRESS                82002
         TM    FLAG,NEWVOL   FORCED SWITCH ?                     82004
         BO    CLOSEFIL      YES, EXIT FROM READ                 82002
         L     R8,CURBLOCK                                       82004
         ICM   R8,7,BUFLINKA  GET NEXT BLOCK                     82004
         TM    BUFLAG,BUFRITE  SCHEDULED FOR WRITE ?             82004
         BZ    READBLKT      NO; USE IT                          82004
         LR    R7,R9         SAVE RETURN ADDRESS                 82004
         L     R8,CURBLOCK   POINT TO CURRENT BLOCK AGAIN        82004
         BAL   R9,PUTWAIT1   WAIT FOR BLOCK TO FINISH            82004
         LR    R9,R7         RESTORE RETURN ADDRESS              82004
READBLKT L     R2,MAXEXCP    GET NUMBER OF BLOCKS                82004
         L     R8,CURBLOCK   GET CURRENT BLOCK POINTER           82002
         TM    BUFLAG,BUFRITE  WAS BLOCK SCHED. FOR WRITE ?      82002
         BNZ   READLOOP      YES; OK                             82002
         MVI   BUFLAG,BUFEMPTY   ELSE FREE IT UP                 82002
READLOOP ICM   R8,7,BUFLINKA  GET NEXT BLOCK                     82002
         TM    BUFLAG,BUFREAD  READ SCHEDULED ?                  82002
         BNZ   READNEXB      YES; CHECK NEXT BLOCK               82002
         TM    BUFLAG,BUFRITE  SCHEDULED FOR WRITE ?             82004
         BZ    READLOOT      NO; USE IT                          82004
         TM    BIOECB,X'40'  POSTED ?                            82004
         BZ    READLOOT      NO; NO WAIT                         82004
         LA    R5,1          SET FOR ONE CHECK                   82004
         LR    R7,R9         SAVE RETURN ADDRESS                 82004
         LA    R6,READLOOK   SET RETURN ADDRESS                  82004
         BAL   R9,PUTPOST    DO WRITE POST-PROCESSING            82004
READLOOK LR    R9,R7         RESTORE RETURN ADDRESS              82004
READLOOT TM    BUFLAG,BUFREAD+BUFRITE  BUSY BLOCK ?              83353
         BNZ   READNEXB      NO                                  83353
         LA    R3,1          SET READ LENGTH FOR CONTROL CCWS    82004
         MVI   BIOECB,X'48'  SET FOR PURGED I/O                  82002
         MVI   BUFLAG,BUFREAD  SHOW READ SCHEDULED               82002
         INC   BLKEXCP,WORK=R15   UPDATE COUNT FOR THIS BLOCK    82004
         TM    SLFG,SLSPACE  RECORD SPACING REQUIRED ?           82002
         BZ    READNSKP      NO; SKIP THIS                       82002
         A     R15,BLKALL    GET PRESUMED COUNT AFTER I/O        82004
         TM    SLFG,SLDIDSF  WAS SPACE FILE ISSUED ?             82002
         BNZ   READNEXB      YES; DO NOT RE-ISSUE                82002
         C     R15,DMEND     PAST LAST RECORD ?                  82004
         BH    READDFSF      YES; ISSUE FORWARD SPACE FILE       82004
         S     R15,DMSTART   BLOCKS FROM FIRST ONE               82004
         BM    READDSKP      NOT READY TO START READING YET      82004
         SLR   R14,R14       CLEAR FOR DIVIDE                    82004
         D     R14,DMINC     SEE IF MULTIPLE                     82002
         LTR   R14,R14       REMAINDER ?                         82002
         BZ    READNSKP      NO; READ THIS BLOCK                 82004
READDSKP MVI   READCCW,X'37'  SET FOR FORWARD SPACE BLOCK        82002
         B     READNRID      GO TO EXCP                          82004
READDFSF MVI   READCCW,X'3F'   SET FORWARD SPACE FILE            82004
         OI    SLFG,SLDIDSF   SHOW FSF ISSUED                    82004
         B     READNRID      GO TO EXCP                          82004
READNSKP MVI   READCCW,X'02'   MAKE READ CCW                     82002
         L     R3,MAXBLKRD   SET FULL READ SIZE                  82004
READNRID STH   R3,READCCW+6  SET READ LENGTH FOR THIS COMMAND    82004
         LA    R1,SYSUT1     SET FOR INPUT DCB                   82004
         STCM  R1,7,BIODCBPT+1  SET INTO IOB                     82002
         LA    R1,READCCW                                        82002
         ST    R1,BIOSTART                                       82002
         ST    R1,BIORESTR   JUST IN CASE                        82002
         MVI   BIOFLAG1,X'40'  RESET RESTART BIT; SET CMD CHAIN  82002
         LA    R0,BUFDATA    GET BUFFER ADDRESS                  82002
         SLR   R14,R14       CLEAR FOR MVCL OF ZEROES            82002
         SLR   R15,R15                                           82002
         LA    R1,80         SET LABEL LENGTH                    82002
         TM    FLAG,FGDEBUG  DEBUG MODE ?                        82002
         BZ    READNBUG      NO; CLEAR SHORT SECTION ONLY        82002
         ICM   R1,3,READCCW+6  GET MAX LENGTH                    82002
READNBUG MVCL  R0,R14        CLEAR THE BUFFER                    82002
         LA    R1,BIO        GET IOB ADDRESS                     82002
         STCM  R1,7,DCBIOBAA-IHADCB+SYSUT1  (POSSIBLE) DUMP FORMATTING
         MVI   BIOECB,0      JUST IN CASE                        82002
         EXCP  (1)           SCHEDULE THE READ                   82002
         OI    BUFLAG,BUFXLATE  SHOW TRANSLATION REQUIRED        82002
READNEXB BCT   R2,READLOOP   SCHEDULE ANOTHER                    82002
READNEXT L     R8,CURBLOCK   GET FIRST ONE                       82002
READNEXX ICM   R8,7,BUFLINKA  GET NEXT AVAILABLE                 82002
         TM    BUFLAG,BUFREAD  READ SCHEDULED ?                  82002
         BZ    READNEXX      NO; TRY AGAIN                       82002
         STCM  R8,7,CURBLOCK+1  SAVE CURRENT BLOCK ADDRESS       82002
         TM    BIOECB,X'40'  POSTED ?                            82004
         BNZ   READPOST      YES; SKIP SVC OVERHEAD              82004
         WAIT  ECB=BIOECB    WAIT FOR COMPLETION                 82002
READPOST NI    DCBOFLGS-IHADCB+SYSUT1,255-DCBOFTM  RESET 'AT TM' 82004
         INC   BLKEXCP,INC=-1  RESET UNCHECKED EXCP COUNT        82002
         CLI   BIOECB,X'44'  INTERCEPTED ?                       82025
         BL    READPOSX      NO                                  82025
         CLI   BIOECB,X'4B'  PURGED ?                            82025
         BNH   READPURG      YES; RESCHEDULE                     82025
READPOSX XC    IOMSGPTR,IOMSGPTR  CLEAR MESSAGE POINTER          82025
         INC   BLKALL        UP BLOCK COUNTER                    82002
         CLI   READCCW,X'3F'   SPACE FILE COMMAND ?              82002
         BE    READEOFS      YES; ASSUME IT WORKED               82002
         NI    IOFG,255-IOREAD   SHOW READ COMPLETED             82002
         SLR   R3,R3                                             82002
         SLR   R15,R15                                           82002
         ICM   R3,3,READCCW+6   GET REQUESTED LENGTH             82002
         ICM   R15,3,BIOCSW+5  GET LENGTH NOT PROCESSED          82002
         SR    R3,R15        GET LENGTH PROCESSED                82002
         ST    R3,DALEN      SAVE                                82002
         STH   R3,RITECCW+6  SAVE LENGTH FOR WRITE               82002
         LA    R0,READCCW+8  CHECK CORRECT CSW ADDRESS           82002
         CLM   R0,7,BIOCSW   EXPECTED COMPLETION ?               82002
         BNE   READLPER      NO; ERROR                           82002
         CLI   BIOECB,X'7F'  GOOD COMPLETION ?                   82002
         BE    READXLEN      YES; CHECK LENGTH                   82002
         TM    BIOCSW+3,X'01'  UNIT EXCEPTION ?                  82002
         BNZ   READEOF       YES; SET END-FILE                   82002
READLPER BAL   R14,SYNAD     GET SYNAD INFORMATION               82002
         TM    IOFG,IOERR  ERROR SIGNALLED ?                     82002
         BO    READXLAT      YES - DON'T ALTER BLOCK COUNTS      82002
READXLEN LTR   R3,R3         TEST LENGTH                         82002
         BZ    READLZER      COUNT ZERO LENGTH BLOCKS SEPARATELY 82002
         INC   BLKCNT        UP BLOCK COUNT                      82002
READXLAT LTR   R3,R3         CHECK LENGTH                        82002
         BNP   READLZER      SET ZERO LENGTH IF NONE             82002
         TM    CLEANFG,FGANTEST  TEST ASCII VS. EBCDIC ?         89050
         BNZ   *+12          NO; ALREADY DECIDED                 89050
         LA    R2,BUFDATA    POINT TO THE DATA                   89050
         BAL   R14,TRTASCII  YES; CHECK                          89050
         TM    BUFLAG,BUFXLATE  TRANSLATION REQUIRED ?           82002
         BZ    READOUT       NO; RETURN                          82002
         NI    BUFLAG,255-BUFXLATE  RESET                        82002
         TM    LBLFG,LVOL1   VOL1 HEADER FOUND ?                 82002
         BNZ   READPVOL      YES                                 82002
         TM    FLAG,FGFORCE  FORCED NL PROCESSING ?              89055
         BNZ   READPVOL      YES; BYPASS VOL1 TEST               89055
         CLC   ANSIVOL1,VOLLABI  IS IT AN ANSI VOL1 LABEL ?      82002
         BNE   READPVEB      NO; CHECK EBCDIC                    82002
         OI    DCBOPTCD-IHADCB+SYSUT1,DCBOPTQ  FORCE TRANSLATION 82002
         B     READXLAV      GO TO TRANSLATE                     82002
READPVEB CLC   DCVOL1,VOLLABI  IS IT AN EBCDIC VOL1 HEADER ?     82002
         BNE   READPVOL      NO; CHECK USER'S REQUEST            82002
         NI    DCBOPTCD-IHADCB+SYSUT1,255-DCBOPTQ  RESET TRANSLATE
         B     READOUT       AND FINISH                          82002
READPVOL TM    DCBOPTCD-IHADCB+SYSUT1,DCBOPTQ  TRANSLATION REQUIRED ?
         BZ    READOUT       NO                                  82002
READXLAV LA    R2,BUFDATA    POINT TO START OF TEXT              82002
         BAL   R14,TRTOEBCD  TRANSLATE TO EBCDIC                 82002
         B     READOUT       RETURN TO CALLER                    82002
READLZER CLI   READCCW,X'02'  SPACE COMMAND ?                    82002
         BE    READZERL      NO; SET COUNT                       82002
         INC   BLKCNT        COUNT THE BLOCK AS VALID            82002
         OI    SLFG,SLSPACED  SHOW FSR ISSUED                    82002
         B     READOUT       AND RETURN                          82002
READZERL INC   BLKZER        UP COUNT                            82002
         OI    IOFG,IOLEN  SHOW BAD LENGTH                       82002
READOUT  MVN   BUFLAG,IOFG  SAVE ERROR BITS                      82002
         BR    R9
         SPACE 1
READEOFS OI    SLFG,SLSKIPED  SHOW FSF ISSUED                    82002
READEOF  NI    IOFG,255-IOREAD                                   82002
         OI    IOFG,IOEOF  SIGNAL END-FILE                       82002
         INC   BLKALL,INC=-1  DON'T COUNT EOF                    82002
         OI    DCBOFLGS-IHADCB+SYSUT1,DCBOFTM  POSITIONED AT TAPEMARK
         B     READOUT       SET END-FILE BIT IN BUFFER          82002
         SPACE 2
SYNAD    TM    BIOSENS0,X'10'  EQUIPMENT CHECK ?                 82002
         BNZ   SYNADEQ       YES; TREAT AS END OF TAPE           82002
         CLC   =X'0060',BIOSENS0  MEMOREX SPECIAL - 150 FOOT GAP ?
         BNE   SYNADCOM      NO, DO MORE                         82004
         MVC   IOMSGPTR,MSGIMRX  SET FOR BLANK TAPE MESSAGE      82002
         B     SYNADEQC                                          82002
SYNADEQ  MVC   IOMSGPTR,MSGIEQC  SET MSG ADDRESS                 82002
SYNADEQC OI    IOFG,IOEQC  SHOW WHAT, AND MSG                    82002
         OI    FLAG,NEWVOL   RAN OFF REEL - FORCE VOLUME SWITCH  82002
         BR    R14           RETURN TO CALLER                    82002
         SPACE 1
SYNADO   TM    BIOCSW+3,X'01'  UNIT EXCEPTION ?                  82004
         BZ    SYNADO2       NO                                  82004
         MVC   IOMSGPTR,MSGOEOT  SET END OF TAPE                 82004
         B     SYNADEQC                                          82004
SYNADO2  TM    BIOSENS0,X'10'  EQUIPMENT CHECK ?                 82004
         BNZ   SYNADEQ       YES; SET IT                         82004
SYNADCOM LA    R1,BIO        SET IOB ADDRESS                     82004
         LR    R2,R13        COPY WORK AREA                      82002
         DROP  R13                                               82002
         USING SUBSAVE,R2                                        82002
       SYNADAF ACSMETH=EXCP  GET ERROR FORMATTED; IOB IN R1      82002
SYNADNLN MVI   MIOSYN+5,C' '  PREPARE TO BLANK OUT               84253
         MVC   MIOSYN+6(MIOSYNE-MIOSYN-6),MIOSYN+5  BLANKS       84253
         MVI   MIOSYN+4,C'0'  DOUBLE-SPACE C.C.                  84253
         LA    R0,MIOSYNE-MIOSYN  MESSAGE LENGTH                 84253
         STH   R0,MIOSYN     SET LENGTH                          84253
         STCM  R0,12,MIOSYN+2  SET FLAGS OFF                     84253
         LA    R0,MIOSYN                                         84253
         ST    R0,IOMSGPTR   SET MESSAGE ADDRESS                 84253
         MVC   SYNADECB-4(4),=C'ECB='                            84253
         MVC   SYNADCSW-4(4),=C'CSW='                            84253
         MVC   SYNADCCW-4(4),=C'CCW='                            84253
         MVC   SYNADSEN-6(6),=C'SENSE='                          84253
         MVC   SYNADTXT,68(R1)   MOVE REMAINING TEXT             84253
         OI    IOFG,IOERR   SHOW ERROR + MESS                    82002
         INC   BLKERR        UP ERROR COUNT                      82002
         INHEX SYNADECB,2,BIOECB,1                               82002
         INHEX SYNADSEN,4,BIOSENS0,2                             82002
         INHEX SYNADCSW,2,BIOFL3,1                               82002
         INHEX SYNADCSW+3,6,BIOCSW,3                             82002
         INHEX SYNADCSW+10,4,BIOCSW+3,2                          82002
         INHEX SYNADCSW+15,4,BIOCSW+5,2                          82002
         L     R15,BIOSTART  GET START CCW                       82002
         USING NOBYTE,R15    FAKE USING                          84253
         INHEX SYNADCCW,2,NOBYTE,1                               84253
         INHEX SYNADCCW+3,6,NOBYTE+1,3                           84253
         INHEX SYNADCCW+10,4,NOBYTE+4,2                          84253
         INHEX SYNADCCW+15,4,NOBYTE+6,2                          84253
         DROP  R15                                               82002
       SYNADRLS
         DROP  R2                                                82002
         USING SUBSAVE,R13   RESTORE WORK AREA                   82002
         BR    R14           RETURN TO CALLER                    82002
         SPACE 2                                                 82002
OPENOUT  TM    DCBOFLGS-IHADCB+SYSUT2,DCBOFOPN  ALREADY OPEN ?   82002
         BNZR  R9           ALREADY OPEN - RETURN                82002
         TM    FLAG,FGCOPY   COPY OP ?                           82002
         BZR   R9            NO                                  86204
         MVI   DB+4,0        CLEAR DOM FLAG                      82115
         LA    R1,JFCBVOLS-INFMJFCB+OUTJFCB  POINT TO O/P VOLSER 82045
         CLC   VSSER,JFCBVOLS-INFMJFCB+OUTJFCB  INPUT AS OUTPUT ?
         BE    OPENOUTC      YES; CHECK FURTHER                  82045
         BAL   R14,CHECKTMS  CHECK VOLSER TYPE                   82045
         B     OPENOUT1      SCRATCH                             82045
         B     OPENOUTA      TMS                                 82062
         B     OPENOUT2      NON-TMS                             82045
OPENOUTC BAL   R14,CHECKTMS  CHECK VOLSER TYPE FOR DUPLICATE     82045
         B     OPENOUT1      SCRATCH ?                           82045
         B     *+8           TMS                                 82062
         B     OPENOUTD      DUPLICATE - WRITE WARNING           82045
         TM    CLEANFG,FGASIS   I/P=O/P PERMITTED ?              82045
         BNZ   OPENOUTD      YES; PROCEED                        82045
OPENOUT1 MVC   JFCBVOLS-INFMJFCB+OUTJFCB(6),DCSCRTCH  FAKE IT    82045
OPENOUTA CLC   =X'620000',JFCBXPDT-INFMJFCB+OUTJFCB  98000 ?     82062
         BNE   OPENOUT2      NO                                  82025
         MVC   JFCBXPDT+1-INFMJFCB+OUTJFCB(2),=H'365'  MAKE 98365
         B     OPENOUT2                                          82045
OPENOUTD L     R1,=A(PATMSGD1)  POINT TO MESSAGE 1/2             90243
         MVC   OPMSGD1(PATMSGL1),PATMSGD1-PATMSGD1(R1)  MOVE PATTERN
         MVC   OPMSGD1V,VSSER   SHOW VOLSER                      82045
         L     R15,DCBDEBAD-IHADCB+SYSUT1  GET INPUT DEB         82045
         L     R15,DXDEBUCB-DXDEB(,R15)  GET INPUT UCB           82045
         MVC   OPMSGD1U,UCBNAME-UCBOB(R15)  MOVE INPUT UNIT NAME 82045
         WTO   MF=(E,OPMSGD1) WRITE DUPLICATE WARNING            82045
         ST    R1,DB         SAVE MESSAGE ID FOR DOM             82115
         L     R1,=A(OPMSGD2)  POINT TO SECOND PORTION OF MESSAGE
         WTO   MF=(E,(1))      MESSAGES                          90243
         ST    R1,DB+4       SAVE MESSAGE ID FOR DOM             82115
         MVI   DB+4,X'80'    SET END OF LIST BIT                 82115
         SPACE 1                                                 90243
OPENOUT2 MVI   OPLSTOUT,X'8F'  OUTPUT                            82002
         CLC   DCSCRTCH,JFCBVOLS-INFMJFCB+OUTJFCB  SCRATCH ?     88031
         BNE   *+10          NO                                  88031
         MVC   JFCBVOLS-INFMJFCB+OUTJFCB,=CL6' '  PREVENT S413   88031
         OI    JFCBFLG3-INFMJFCB+OUTJFCB,JFCDQDSP  DEQUEUE ON DEMOUNT
         OPEN  TYPE=J,MF=(E,OPLSTOUT)  OPEN OUTPUT DCB           82002
         TM    DB+4,X'80'    WTO ISSUED ?                        82115
         BZ    NEMTODOM      NO                                  82115
         DOM   MSGLIST=DB    DELETE FROM SCREEN                  82115
         MVI   DB+4,0        RESET FLAG BIT JUST IN CASE         82115
NEMTODOM TM    DCBOFLGS-IHADCB+SYSUT2,DCBOFOPN  OPEN NOW ?       82115
         BZ    OPENFAIL                                          82002
         L     R15,DCBDEBAD-IHADCB+SYSUT2  GET DEB ADDRESS       80048
         L     R15,DXDEBUCB-DXDEB(,R15)    GET UCB ADDRESS       80048
         CLI   UCBVOLI-UCBOB(R15),C'9'  VALID VOLSER ?           80048
         BH    OPENOUTV      NO                                  82002
         CLI   UCBVOLI-UCBOB(R15),C'$'                           80048
         BL    OPENOUTV                                          82002
         CLC   DCSCRTCH,UCBVOLI-UCBOB(R15)  SCRATCH ?            82045
         BNE   OPENOUTU      NO; USE IT                          82002
OPENOUTV LA    R15,VSSER-(UCBVOLI-UCBOB)  COPY - USE INPUT VS    82002
OPENOUTU MVC   JFCBVOLS-INFMJFCB+OUTJFCB(6),UCBVOLI-UCBOB(R15)  TMS ?
         MVC   VOLSERNO-DXLBL+SAVEVOL1(6),UCBVOLI-UCBOB(R15)     82002
         MVC   MOPVCUU,UCBNAME-UCBOB(R15)  SET UNIT              82004
         SLR   R0,R0                                             82004
         IC    R0,JFCBXPDT-INFMJFCB+OUTJFCB  GET EXPIRATION YEAR 82004
         MH    R0,=H'1000'   FINAGLE                             82004
         AH    R0,JFCBXPDT+1-INFMJFCB+OUTJFCB                    82004
         CVD   R0,DB         PACK                                82004
         MVC   MOPVDAT,=X'21204B202020'  MAKE EDIT MASK          82004
         ED    MOPVDAT-1(7),DB+5   MAKE NICE LOOKING DATE        82004
         LA    R14,PATDEN                                        82004
         LA    R15,PATLNDEN  NUMBER OF DENSITY ENTRIES           82004
OPENOUTX CLC   0(1,R14),DCBDEN-IHADCB+SYSUT2  SAME DENSITY ?     82004
         BE    OPENOUTY      YES                                 82004
         LA    R14,2(,R14)                                       82004
         BCT   R15,OPENOUTX                                      82004
OPENOUTY MVC   MOPVDEN,1(R14)  SET DENSITY                       82004
         PRTLIST MOPVOL,DEV=(1,2)  SHOW OUTPUT VOLUME            90243
         BR    R9            RETURNED WITH CORRECT VOL-SER       80048
OPENFAIL OI    FLAG,NEWVOL   TERMINATE VOLUME                    82002
         OI    CC,CCFATAL    SET MAJOR ERROR                     82002
         PRTV  /ABOUT213,DEV=(1,2,3)                             90243
         L     R1,=X'80213000'   FAKE 213 COMPLETION CODE        82004
         SVC   13            QUIT WITH DUMP                      82004
         SPACE 2
PUTOUTST TM    SYSUT2+DCBOFLGS-IHADCB,DCBOFOPN  OUTPUT OPEN ?    82002
         BZ    PUTNULL       NO; JUST RELEASE BUFFER             82002
PUTOUTOP TM    FLAG,FGCOPY   RUNNING REAL COPY ?                 82002
         BNZ   PUTOUTRT      YES; TEST IF READ BUFFER            82002
PUTNULL  MVI   BUFLAG,BUFEMPTY  ELSE MAKE BUFFER AVAILABLE FOR READ
         BR    R9            RETURN TO CALLER                    82002
PUTOUTRT TM    BUFLAG,BUFREAD  READ BUFFER ?                     82002
         BZR   R9            NO; IGNORE THIS BLOCK               82002
         TM    BUFLAG,IOFBAD    ANYTHING FUNNY IN INPUT ?        82002
         BNZ   PUTNULL       YES; JUST RELEASE IT                82002
         SLR   R2,R2                                             83353
         ICM   R2,3,RITECCW+6   GET REQUESTED OUTPUT LENGTH      82004
         B     PUTHDCOM                                          82002
         SPACE 1                                                 82002
*        ENTRY TO PUT STACKED HEADER RECORD                      82002
PUTHDR   LA    R8,SPECIOB    GET SPECIAL IOB                     82002
         STCM  R1,7,RITECCW+1  STASH RECORD ADDRESS              82002
         LA    R2,80         SET LABEL LENGTH                    82002
         MVI   BUFLAG,BUFREAD   JUST IN CASE                     82002
         B     PUTHDCOM      GO TO COMMON                        82002
PUTSPECL LA    R8,SPECIOB                                        82002
         MVI   BUFLAG,BUFREAD+IOEOF  SET FOR TAPE-MARK           82002
         SPACE 1                                                 82002
PUTHDCOM STH   R2,RITECCW+6  SET WRITE LENGTH                    82002
         LA    R1,WTMCCW     SET FOR EOF WRITE                   82002
         NI    BUFLAG,BUFRITE+IOEOF  LEAVE ONLY EOF BIT          82004
         TM    BUFLAG,IOEOF  END-FILE ?                          82002
         BNZ   PUTCCWAD      YES; STASH WTM CCW                  82002
         LTR   R3,R2         ANY LENGTH ?                        82002
         BNPR  R9            NO; SKIP ZERO LENGTH BLOCK          82004
         TM    DCBOPTCD-IHADCB+SYSUT2,DCBOPTQ  TRANSLATION REQUIRED ?
         BZ    PUTCCWWR      NO                                  82002
         ICM   R2,7,RITECCW+1  GET START OF TEXT                 82002
         BAL   R14,TRTOANSI  TRANSLATE TO ASCII                  82002
PUTCCWWR LA    R1,RITECCW                                        82002
PUTCCWAD ST    R1,BIOSTART                                       82002
         ST    R1,BIORESTR   JUST IN CASE                        82002
         LA    R1,SYSUT2     SET FOR OUTPUT DCB                  82002
         STCM  R1,7,BIODCBPT+1  SET INTO IOB                     82002
         NI    DCBIFLGS-IHADCB+SYSUT2,255-DCBIFEC  RESET ERROR BITS
         MVI   BIOFLAG1,X'40'  RESET RESTART BIT                 82002
         LA    R1,BIO        GET IOB ADDRESS                     82002
         STCM  R1,7,DCBIOBAA-IHADCB+SYSUT2  (POSSIBLE) DUMP FORMATTING
         EXCP  (1)           WRITE A BLOCK                       82002
         OI    BUFLAG,BUFRITE  SET WRITE OP                      82002
PUTWAIT1 LA    R5,1          SCHEDULE ONE CHECK                  82004
         LR    R6,R9         IF NEXT NOT WRITTEN; RETURN         82004
PUTWAIT  ICM   R8,7,BUFLINKA  GET NEXT BLOCK                     82002
         TM    BUFLAG,BUFRITE  WAS IT WRITTEN ?                  82002
         BZR   R6    RETURNS TO CALLER (NORMAL) OR PUTGOOD3 (QUIESCE)
         TM    BIOECB,X'40'   POSTED ALREADY ?                   82004
         BNZ   PUTPOST       YES; AVOID SVC OVERHEAD             82004
         WAIT  ECB=BIOECB    CHECK IT                            82002
PUTPOST  NI    BUFLAG,IOEOF  SET BUFFER (ALMOST) IDLE            82004
         CLI   BIOECB,X'7F'  GOOD COMPLETION ?                   82002
         BE    PUTGOOD       YES ?                               82002
         MVI   BUFLAG,BUFEMPTY  MAKE COMPLETELY IDLE             82002
         BAL   R14,SYNADO    FORMAT AN ERROR MESSAGE             82002
         TM    IOFG,IOFBAD   ANY ERROR ?                         82004
         BZ    PUTGOOD
         L     R4,=X'80001000'  FAKE 001 SYSTEM ABEND            82004
         TM    BIOCSW+3,1    UNIT EXCEPTION ?                    82004
         BZ    *+8           NO                                  82004
         L     R4,=X'00D37000'  SET FOR D37 ABEND; NO DUMP       82004
         L     R3,IOMSGPTR   SAVE MESSAGE                        82004
         BAL   R9,ERRMSGEQ   PRINT MESSAGE                       82004
         LTR   R3,R3         ANY MESSAGE ?                       82004
         BZ    PUTFERR       NO                                  82004
         PRTV  (R3),DEV=2                                        82004
PUTFERR  L     R1,=A(MOPERR)  GET ERROR MESSAGE ADDRESS          86096
         PRTV  (R1),DEV=(1,2)      AND PRINT IT                  86096
         LR    R1,R4         SET ABEND CODE                      82004
         SVC   13            AND QUIT UNGRACEFULLY               82004
         SPACE 1                                                 82004
ERRMSGEQ OI    CC,CCNASTY    SET NASTY ERROR                     82002
         OI    FLAG,NEWVOL   FORCE NEXT VOLUME                   82002
ERRMSG   LAT   R1,IOMSGPTR,(R9)   MESSAGE TEST/WRITE SUBROUTINE  82002
ERRMSG1  PRTV  (R1)                                              82002
         XC    IOMSGPTR,IOMSGPTR  CLEAR MSG POINTER              82002
         BR    R9
PUTGOOD  TM    BUFLAG,IOEOF  EOF WRITE ?                         82002
         BNZ   PUTGOOD2      YES; DON'T BUMP COUNT               82002
PUTGOOD1 INC   OUTCUR        UP WRITE COUNT                      82002
PUTGOOD2 MVI   BUFLAG,BUFEMPTY  RELEASE BUFFER                   82002
         BCTR  R5,R6         REPEAT AS NECESSARY TO CLEAR OUT    82004
         BR    R9
PUTGOOD3 C     R8,CURBLOCK   TRIED ALL ?                         82002
         BNE   PUTWAIT       NO; TRY AGAIN                       82002
         BR    R9            ELSE RETURN                         82002
         SPACE 2                                                 82002
*        TRANSLATION ROUTINES                                    82002
*        ENTRY R2 - ADDRESS  R3 - LENGTH                         82002
*        DESTROYS R15-R3;  RETURNS ON R14                        82002
TRTOANSI L     R15,=A(TREBTOAS)  GET EBCDIC TO ASCII TRANSLATE   82328
         TM    SLFG,SLANSI3  DOING ANSI TYPE 3 ?                 90243
         BZ    TRTOFRO       NO                                  90243
         LA    R15,TREBTOIS-TREBTOAS(,R15)  USE ISCII            90243
         B     TRTOFRO       GO TO COMMON                        82002
TRTOEBCD L     R15,=A(TRASTOEB)  SET ASCII TO EBCDIC TRANSLATE   82328
         TM    SLFG,SLANSI3  DOING ANSI TYPE 3 ?                 90243
         BZ    TRTOFRO       NO                                  90243
         LA    R15,TRISTOEB-TRASTOEB(,R15)  USE ISCII            90243
TRTOFRO  LTR   R3,R3         CHECK LENGTH                        82002
         BNPR  R14           NOTHING TO DO; RETURN               82002
         LA    R0,256        MAXIMUM TRANSLATABLE                82002
TRTOLOOP LR    R1,R0         SET LENGTH                          82002
         CR    R1,R3         COMPARE TO RESIDUAL LENGTH          82002
         BNH   *+6           MORE THAN ONE SEGMENT               82002
         LR    R1,R3         PARTIAL SEGMENT LENGTH              82002
         BCTR  R1,0          SET LENGTH FOR EXECUTE              82002
         EX    R1,TRTOTR     TRANSLATE                           82002
         AR    R2,R0         NEXT SEGMENT ADDRESS                82002
         SR    R3,R0         REMAINING LENGTH                    82002
         BP    TRTOLOOP                                          82002
         BR    R14           RETURN TO CALLER                    82002
TRTOTR   TR    0(0,R2),0(R15)  TRANSLATE                         82002
         SPACE 1                                                 89050
*        CHARACTER CODE DETECTION                                89050
*        ENTRY R2 - ADDRESS  R3 - LENGTH                         89050
*        DESTROYS R15-R1;  RETURNS ON R14                        89050
TRTASCII STM   R2,R5,DB      PRESERVE START ADDRESS AND LENGTH   89050
         LTR   R3,R3         CHECK LENGTH                        89050
         BNPR  R14           NOTHING TO DO; RETURN               89050
         L     R15,=A(TRTANSI)  SET FOR ASCII/EBCDIC TRT         89050
         LR    R4,R2         COPY START ADDRESS                  89050
         LA    R0,256        MAXIMUM TRANSLATABLE                89050
         SLR   R2,R2         CLEAR FOR TRT                       89050
TRTALOOP LR    R1,R0         SET LENGTH                          89050
         CR    R1,R3         COMPARE TO RESIDUAL LENGTH          89050
         BNH   *+6           MORE THAN ONE SEGMENT               89050
         LR    R1,R3         PARTIAL SEGMENT LENGTH              89050
         BCTR  R1,0          SET LENGTH FOR EXECUTE              89050
         EX    R1,TRTATR     TRANSLATE                           89050
         LTR   R2,R2         EBCDIC CODE FOUND ?                 89050
         BNZ   TRTADONE      YES; SET FLAGS                      89050
         AR    R4,R0         NEXT SEGMENT ADDRESS                89050
         SR    R3,R0         REMAINING LENGTH                    89050
         BP    TRTALOOP                                          89050
         OI    CC,CCANSI     SET PRESUMED ASCII CODE             89050
TRTADONE OI    CLEANFG,FGANTEST  SHOW CODE TESTED                89050
         LM    R2,R5,DB      RESTORE REGISTERS                   89050
         BR    R14           RETURN TO CALLER                    89050
TRTATR   TRT   0(0,R4),0(R15)  TRANSLATE                         89050
         SPACE 2
         LTORG ,
         EJECT ,
FDHEAD   DC    A(VPDATA,VPTRAIL)                                 82002
MSGIEQC  DC    A(MIOEQC)
MSGIMRX  DC    A(MIOMRX)                                         82002
MSGOEOT  DC    A(MOPEOT)                                         82004
DCHDR2   DC    C'HDR2'                                           82002
DCUHD    DC    C'UHL'        USER HEADER LABEL                   82002
DCTRAIL  DC    C'EOF'        END-FILE TRAILER LABEL
DCTRALE  DC    C'EOV'        END-VOLUME, FILE CONT'D TRAILER
HEADLABE DC    C'UHL'        OTHER ALLOWED HEADER LABELS
HEADLABT EQU   *
TRAILABE DC    C'UTL'        USER TRAILER LABELS
TRAILABT EQU   *
HEXTRTAB DC    C'0123456789ABCDEF'                               82002
RECBITS  DC    C'D',AL1(JFCVARD)                                 82002
RECBIT2  DC    AL1(C'U',JFCUND,C'F',JFCFIX,C'V',JFCVAR)          82002
         DC    AL1(C'B',JFCRFB,C'S',JFCRFS,C'A',JFCASA,C'M',JFCMAC)
RECBITND EQU   *                                                 82002
         SPACE 1
DC3C0    DC    3C'000'
         SPACE 1                                                 82002
SPACE5   FDPRT ' ',LEN=5     STUPID SPACER                       82002
         FDPRT *END                                              82002
         SPACE 1                                                 82002
VSUBSUM  FDEXEC VSUBHEAD,VSUBHED2                                82002
         FDGOTO VSUBHED3     SUMMARY - OMIT FILE NUMBER          82002
         SPACE 1                                                 82002
VSUBHEAD FDPRT '  VOLUME',NL                                     82002
VSUBHED2 FDPRT JFCBVOLS-INFMJFCB+INPJFCB,6,DEB,PADL              82002
         FDPRT ',    DATA FILE',PADR                             82002
         FDPRT LOGFIL,I                                          82002
VSUBHED3 FDPRT ',   ',PADR                                       82002
         FDTM  DCBOPTCD-IHADCB+SYSUT1,DCBOPTQ,BZ=VSUBHANS  NOT ANSI
         FDPRT 'ANSI',PAD                                        82002
VSUBHANS FDTM  SLFG,SLNL,BZ=VSUBHSL                              82002
         FDPRT 'UN',PADL                                         82002
VSUBHSL  FDPRT 'LABELLED TAPE'                                   82002
         FDPRT *END                                              82002
         SPACE 1
VPHEADB  FDPRT '***** VOLUME SERIAL READ (',NL                   82002
         FDPRT VSSER,DEB                                         82002
         FDPRT ') DOES NOT MATCH DD CARD ('                      82002
         FDPRT VPHSER,DEB                                        82002
         FDPRT ') ; TAPE IS A COPY OR OPERATOR MOUNTED WRONG VOLUME'
         FDPRT *END                                              82002
         SPACE 1                                                 82002
VLOFF    FDPRT OUTCUR,I,NL,RADJ,LEN=15                           82002
         FDPRT 'BLOCKS WRITTEN',PADL                             82002
         FDPRT '; ***** COUNT DIFFERS FROM INPUT *****'          82002
         FDPRT *END                                              82002
         SPACE 1
VLAB     FDPRT DXLBL,80,NL   PRINT LABEL RECORD                  82002
         FDPRT *END                                              82002
         SPACE 1
VLBHD    FDPRT '    BLOCK #',PADR,NL   BLOCK HEADER              82002
         FDPRT BLKALL,I,PADL                                     82002
         FDPRT ',     LENGTH',PADR                               82002
         FDPRT DALEN,I                                           82002
         FDSNAP (R6),DUAL,NOABS,OFFSET,LEN=(R5),BASE=(R6) DUMP DATA
         FDPRT *END                                              82002
         SPACE 1
VLVOL1   FDPRT VOLLABI,4,NL,LEN=8                                82002
         FDPRT 'VOLSER='                                         82002
         FDPRT VLVOL         PRINT ACTUAL VOL-SER                82002
         FDPRT ', SECURITY='                                     82002
         FDPRT VOLSEC                                            82002
         FDPRT ', OWNER='                                        82002
         FDPRT AVOLOWNR                                          82002
         FDTM  DCBOPTCD-IHADCB+SYSUT1,DCBOPTQ,BZ=VLVOL1FX        90243
         FDPRT ' TAPE IS',PAD                                    90243
         FDCLI LABSTAND-DXLBL+SAVEVOL1,C'1',BE=VLVOL1F1  OLD     90243
         FDCLI LABSTAND-DXLBL+SAVEVOL1,C'3',BE=VLVOL1F3  NEW     90243
         FDPRT 'ASCII, BUT NOT A VALID TYPE 1 OR 3:',PAD         90243
         FDPRT LABSTAND-DXLBL+SAVEVOL1,1                         90243
         FDGOTO VLVOL1FX                                         90243
VLVOL1F1 FDPRT 'ASCII (7-BIT) MODE'                              90243
         FDGOTO VLVOL1FX                                         90243
VLVOL1F3 FDPRT 'ISCII (8-BIT) MODE'                              90243
VLVOL1FX FDPRT *END                                              82002
         SPACE 1                                                 82002
MSGPROTP FDPRT '     TAPE IS READ AND WRITE PROTECTED',NL        82002
         FDPRT *END                                              82002
MSGPROTF FDPRT '     FILE IS READ AND WRITE PROTECTED',NL        82002
         FDPRT *END                                              82002
MSGPROTW FDPRT '     FILE IS WRITE PROTECTED',NL                 82002
         FDPRT *END                                              82002
MSGPRONO FDPRT '; NO DATA WILL BE PRINTED'                       82002
         FDPRT *END                                              82002
MSGPROYE FDPRT '; ACCESS PERMITTED'                              82002
         FDPRT *END                                              82002
         SPACE 1                                                 82002
MSGLABLN FDPRT '***** INVALID',NL                                82002
         FDPRT FL1LABI,4,PAD                                     82002
         FDPRT 'LABEL LENGTH OF'                                 82002
         FDPRT DALEN,I,PAD                                       82002
         FDPRT 'CHANGED TO 80'                                   82002
         FDPRT *END                                              82002
         SPACE 1
VLHDR1   FDPRT FL1LABI,4,NL,LEN=8                                82002
         FDPRT 'DSN='                                            82002
         FDPRT FL1ID                                             82002
         FDPRT ', DATASET STARTS ON VOLUME',PADR                 82002
         FDPRT FL1FILSR                                          82002
         FDPRT ', FILE',PADR                                     82002
         FDPRT FL1FILSQ,PADR                                     82002
         FDPRT ';  THIS IS REEL NUMBER',PAD                      82002
         FDPRT FL1VOLSQ                                          82002
         FDPRT *END                                              82002
         SPACE 1                                                 82002
VLHDR1S  FDPRT ' ',NL,LEN=8                                      82002
         FDPRT 'MEMBER OF A GENERATION DATA GROUP; GENERATION NUMBER'
         FDPRT FL1GNO,PADL                                       82002
         FDPRT ', VERSION',PADR                                  82002
         FDPRT FL1VNG                                            82002
         FDPRT *END                                              82002
         SPACE 1
VLHDR1F  FDPRT ' ',NL,LEN=8                                      82002
         FDPRT 'CREATED ON',PADR                                 82002
         FDPRT FL1CREDT,3    1/1 IS BLANK 19XX, 0-20XX 1-21XX    90243
         FDPRT '.'                                               82002
         FDPRT FL1CREDT+3,3                                      82002
         FDPRT ', EXPIRES',PADR                                  82002
         FDPRT FL1EXPDT,3                                        90243
         FDPRT '.'                                               82002
         FDPRT FL1EXPDT+3,3                                      82002
         FDPRT ', SECURITY='                                     82002
         FDPRT FL1FSEC                                           82002
         FDPRT '; SYSTEM='                                       82002
         FDPRT FL1SYSCD                                          82002
         FDPRT *END                                              82002
VLHDR1G  FDPRT ' ',LEN=15                                        82002
VLTLR1G  FDOPT IND=25        SET PROVISIONAL INDENT              82115
         FDROOM 21           SPACE FOR 21 TEXT BYTES             82115
         FDPRT FL1BLKCT,DEBZ,RADJ,LEN=5                          82115
         FDPRT 'BLOCKS EXPECTED',PADL                            82002
         FDPRT *END                                              82002
         SPACE 1                                                 82002
VLHDR2   FDPRT FL1LABI,4,NL,LEN=8                                82002
         FDPRT 'RECFM='                                          82002
         FDPRT RECFM                                             82002
         FDPRT ', LRECL='                                        82002
         FDPRT FL2LRECL                                          82002
         FDPRT ', BLKSIZE='                                      82002
         FDPRT FL2BLKL                                           82002
         FDPRT ', DEN='                                          82002
         FDPRT FL2DEN                                            82002
         FDCLI FL2TRTCH,C' ',BE=VLHDR2TR  SKIP IF NO TRTCH       82002
         FDPRT ', TRTCH='                                        82002
         FDPRT FL2TRTCH                                          82002
VLHDR2TR FDCLI RECFM,C'D',BNE=VLHDR2OF  SKIP BUFFER OFFSET       82002
         FDPRT ',BUFOFF='                                        82002
         FDPRT FL2BUFOF                                          82002
VLHDR2OF FDPRT ', FILE POS='                                     82002
         FDPRT FL2FILP                                           82002
         FDPRT '; CREATED BY',PADR                               82002
         FDPRT FL2JOBD                                           82002
         FDPRT ', STEP',PADR                                     82002
         FDPRT FL2STEPD                                          82002
         FDCLI FL2ID,C' ',BE=VLHDR2X  SKIP MISSING DRIVE ID      82002
         FDPRT 'TU#',PAD                                         82002
         FDPRT FL2ID                                             82002
VLHDR2X  FDPRT *END                                              82002
         SPACE 1                                                 82002
SUMFDFIL FDOPT NL,CC=C' '    FORCE A NEW LINE                    82004
         FDTM  FLAG,FGCOPY,BNZ=SUMFDID  NEED PRINT SPACE; SKIP 'FILE'
         FDPRT 'FILE',NL,PADR                                    82004
SUMFDID  FDPRT LOGFIL,I,RADJ,LEN=5                               82004
         FDTM  SLFG,SLSKIPED,BZ=SUMFDBLK  SKIP MODE ?            82002
         FDPRT '?',RADJ,LEN=7                                    82002
         FDGOTO SUMFDCNT                                         82002
SUMFDBLK FDPRT BLKCNT,I,RADJ,LEN=7                               82002
SUMFDCNT FDPRT 'BLOCKS',PADL                                     82002
         FDTM  FLAG,FGSKIP,BZ=SUMFDNSK   SKIP IF NOT SKIP        82002
         FDTM  FLAG,FGSKIPSL,BZ=SUMFDSKP                         82002
         FDTM  SLFG,SLNL,BNZ=SUMFDNSK                            82002
SUMFDSKP FDPRT '(SKIP MODE)',PADL                                82002
SUMFDNSK FDTM  FLAG,FGCOPY,BZ=SUMFDCIN   SKIP IF NOT COPY        82002
         FDPRT 'READ',PAD                                        82002
         FDPRT OUTCUR,I,RADJ,LEN=7                               82002
         FDPRT 'WRITTEN',PAD                                     82002
SUMFDCIN FDCLC BLKERR,ZEROES,4,BE=SUMFDCOK                       82002
         FDPRT BLKERR,I,RADJ,LEN=7                               82002
         FDPRT 'ERRORS',PAD                                      82004
SUMFDCOK FDPRT FL1ID-DXLBL+SAVEHDR1,L'FL1ID,PAD                  82002
         FDPRT RECFM,PAD,DEB,LEN=4                               82002
       FDPRT FL2LRECL-DXLBL+SAVEHDR2,L'FL2LRECL,PAD,DEBZ,DEB,RADJ,LEN=5
         FDPRT FL2BLKL-DXLBL+SAVEHDR2,L'FL2BLKL,PAD,DEBZ,DEB,RADJ,LEN=5
         FDPRT *END                                              82002
SUMFDOFF FDPRT '  BLOCK COUNT DISCREPANCY',PAD                   82002
         FDPRT *END                                              82002
         SPACE 1
VLDAT    FDOPT NL,CC=C'0'    DOUBLE-SPACE                        82002
         FDPRT '**** END FILE.',PADR                             82002
         FDPRT BLKALL,I,PAD,RADJ,LEN=7                           82002
         FDPRT 'BLOCKS PROCESSED;',PAD                           82002
         FDPRT BLKCNT,I,RADJ,LEN=7                               82002
         FDPRT 'GOOD BLOCKS READ,',PAD                           82002
         FDPRT BLKERR,I,RADJ,LEN=7                               82002
         FDPRT 'READ ERRORS',PADL                                82002
         FDCLC BLKZER,ZEROES,4,BE=VLDATX  SKIP IF NO ZERO LENGTH 82002
         FDPRT ',',PADR                                          82002
         FDPRT BLKZER,I,RADJ,LEN=7                               82002
         FDPRT 'ZERO LENGTH BLOCKS',PADL                         82002
VLDATX   FDPRT *END                                              82002
         SPACE 1
VLDATF   FDPRT ' ',NL,LEN=25                                     82002
         FDPRT BLKMIN,I                                          82002
         FDPRT 'MINIMUM,',PAD                                    82002
         FDPRT BLKMAX,I                                          82002
         FDPRT 'MAXIMUM,',PAD                                    82002
         FDPRT AVGBLKSZ,I                                        82002
         FDPRT 'AVERAGE BLOCK SIZE',PADL                         82002
         FDPRT *END                                              82002
         SPACE 1                                                 82004
VLDADF   FDOPT IND=25        SET CONDITIONAL INDENTATION         82115
         FDROOM 10           NEW LINE UNLESS 25 BYTES FIT        82115
         FDPRT BLKMIN,I,RADJ,LEN=5                               82115
         FDPRT 'MIN,',PAD                                        82004
         FDROOM 10           SPACE FOR 10 MORE BYTES             82115
         FDPRT BLKMAX,I,RADJ,LEN=5                               82004
         FDPRT 'MAX,',PAD                                        82004
         FDROOM 14           ROOM FOR REST OF TEXT               82115
         FDPRT AVGBLKSZ,I,RADJ,LEN=5                             82004
         FDPRT 'AVG SIZE',PADL                                   82004
         FDPRT *END                                              82004
         SPACE 1
VLDATFF  FDPRT ' ',NL,LEN=25                                     82002
         FDPRT AVGBLKSZ,I                                        82002
         FDPRT 'NORMAL SIZE;',PAD                                82002
         FDPRT BLKSHORT,I                                        82002
         FDPRT 'SHORT; SHORTEST HAS',PAD                         82002
         FDPRT BLKMIN,I                                          82002
         FDPRT 'BYTES',PADL                                      82002
         FDPRT *END                                              82002
         SPACE 1                                                 82004
VLDADFF  FDPRT BLKSHORT,I,RADJ,LEN=5                             82004
         FDPRT 'SHORT;',PAD                                      82004
         FDPRT BLKMIN,I,RADJ,LEN=5                               82004
         FDPRT 'MIN,',PAD                                        82004
         FDPRT AVGBLKSZ,I,RADJ,LEN=5                             82004
         FDPRT 'AVG SIZE',PADL                                   82004
         FDPRT *END                                              82004
         SPACE 1
VLDATFH  FDPRT '****',NL,PADR                                    82002
         FDPRT 'AT LEAST ONE BLOCK EXCEEDS THE BLOCKSIZE;'       82002
         FDPRT 'MAX LENGTH IS',PAD                               82002
         FDPRT BLKMAX,I,PADL                                     82002
         FDPRT *END                                              82002
         SPACE 1                                                 82002
VLFIXED  FDPRT '****',NL                                         82002
         FDPRT BLKFIX,I,PAD                                      82002
         FDPRT 'BLOCKS HAD ERRONEOUS BLOCKSIZE'                  82002
         FDTM  CLEANFG,FGTRUNC,BZ=VLFIXEDX                       82002
         FDPRT 'ADJUSTED',PADL                                   82002
VLFIXEDX FDPRT *END                                              82002
         SPACE 1                                                 83353
VLACCED  FDPRT '****',NL                                         83353
         FDPRT BLKACC,I,PAD                                      83353
         FDPRT 'BLOCKS WERE ACCEPTED DESPITE I/O ERRORS'         83353
         FDPRT *END                                              83353
         SPACE 1                                                 82004
UTILFD   FDTM  UTYFLAG,X'FF',BO=UTILFDMO                         82004
         FDPRT 'IEBCOPY FILE',NL,RADJ,PADR,LEN=37                82017
         FDGOTO UTILFDCM                                         82004
UTILFDMO FDPRT 'IEHMOVE FILE',NL,RADJ,PADR,LEN=17                82004
UTILFDCM FDPRT 'FROM',PAD                                        82004
         FDPRT UTYGEN,DEB,PAD                                    82004
         FDTM  UTYDSN,X'FF',BZ=UTILNDSN                          82004
         FDPRT UTYDSN,DEB,PAD                                    82004
UTILNDSN FDPRT WORKLINE,3,PAD          DSORG                     82004
         FDPRT WORKLINE+3,6,PAD        RECFM                     82004
         FDPRT WORKLINE+3+6+8+5,5,PAD  LRECL                     82004
         FDPRT WORKLINE+3+6+8,5,PAD    BLKSIZE                   82004
         FDCLC UTYTRACK,ZEROES,4,BE=UTILFDEX                     82004
         FDPRT UTYTRACK,I,PAD,RADJ,LEN=7                         82011
         FDPRT 'TRACKS'                                          82004
         FDCLC UTYDE,ZEROES,4,BE=UTILFDEX                        82004
         FDPRT UTYDE,I,PAD,RADJ,LEN=7                            82011
         FDPRT 'DIR BLKS USED'                                   82017
UTILFDEX FDOPT NL                                                82004
         FDPRT *END                                              82004
         SPACE 1                                                 90243
MOPVOL   FDPRT '---- OUTPUT TAPE IS',NL                          90243
         FD    VOLSERNO-DXLBL+SAVEVOL1,L'VOLSERNO,PAD            90243
         FD    'ON'                                              90243
         FD    MOPVCUU,PADL                                      90243
         FD    ', DEN='                                          90243
         FD    MOPVDEN                                           90243
         FD    ', EXPIRES ON'                                    90243
         FD    MOPVDAT,PAD                                       90243
         FDTM  DCBOPTCD-IHADCB+SYSUT2,DCBOPTQ,BZ=MOPVOLFX        90243
         FDPRT ' TAPE IS',PAD                                    90243
         FDCLI LABSTAND-DXLBL+SAVEVOL1,C'1',BE=MOPVOLF1  OLD     90243
         FDCLI LABSTAND-DXLBL+SAVEVOL1,C'3',BE=MOPVOLF3  NEW     90243
         FDPRT 'ASCII, BUT NOT A VALID TYPE 1 OR 3:',PAD         90243
         FDPRT LABSTAND-DXLBL+SAVEVOL1,1                         90243
         FDGOTO MOPVOLFX                                         90243
MOPVOLF1 FDPRT 'ASCII (7-BIT) MODE'                              90243
         FDGOTO MOPVOLFX                                         90243
MOPVOLF3 FDPRT 'ISCII (8-BIT) MODE'                              90243
MOPVOLFX FDPRT *END                                              90243
         SPACE 1
VPDATA   VCON  '0***** DATA FILE'                                82002
NORECS   VCON  ' ***** NO DATA RECORD FOUND *****'               82002
CNTBAD   VCON  ' *********** EXPECTED BLOCK COUNT DOES NOT MATCH ACTUAL*
                NUMBER READ ***********'                         82002
VPTRAIL  VCON  '0***** TRAILER LABELS'                           82002
HDINVAL  VCON  ' ***** INVALID LABEL RECORD'                     82002
MSGOUTSQ VCON  ' ***** RECORD OUT OF SEQUENCE'                   82002
MSGNOEF1 VCON  ' ***** NO EOF1/EOV1 TRAILER LABEL FOUND'         82002
MSGNOEF2 VCON  ' ***** NO EOF2/EOV2 TRAILER LABEL FOUND'         93098
VOL1EOF  VCON  '0***** END-FILE IN PLACE OF VOL1 LABEL'          82002
LTMTAPE  VCON  '0LEADING TAPE MARK FOUND - EMPTY OR NON-OS VOLUME'
MGBADDEN VCON  ' ***** TAPE HAS DENSITY NOT PROCESSABLE ON THIS UNIT'
MGMAXERR VCON  '**** LIMIT ERROR COUNT REACHED - PROCESSING TERMINATED'
MGMAXCNS VCON  '**** CONSECUTIVE ERROR LIMIT REACHED - PROCESSING TERMI*
               NATED'                                            82002
MGMAXBLK VCON  '***** RERUN THIS JOB WITH A LARGER DCB BLKSIZE *****'
FILELIM  VCON ' ***** LIMIT FILE COUNT REACHED - PROCESSING TERMINATED'
VOLCONT  VCON  ' ***** NORMAL END OF VOLUME  --  FILE CONTINUED ON ANOT*
               HER REEL'                                         82002
VDOSDSN  VCON  ' DOS DATASET; DD CARD OR PROGRAM WILL NEED TO SUPPLY DC*
               B PARAMETERS'                                     82002
VOLNVOL1 VCON  '0***** VOL1 LABEL REQUIRED, BUT NOT FOUND'       82002
ERRTERM  VCON  ' ***** FILE PROCESSING TERMINATED'               82002
SKPTERM  VCON  ' *** SKIP MODE - FILE PROCESSING SUSPENDED'      82002
MGBIGBLK VCON  '0BLOCK EXCEEDS SPECIFIED BLOCKSIZE'              82002
MGMODBLK VCON  '0BLOCK IS NOT A MULTIPLE OF THE RECORD LENGTH'   82002
VLSUPPR  VCON  '0DATA SUPPRESSED DUE TO PASSWORD PROTECT'        82002
PATMSGD1 DC    Y(PATMSGX1-*),X'8000',C'CAUTION - NEXT MOUNT FOR '
         DC    CL6' ',C' IS NOT TAPE ON '                        90243
         DC    CL3' ',C','                                       90243
PATMSGX1 DC    X'40008000'   ROUTCDE=1,DESC=2                    90243
PATMSGL1 EQU   *-PATMSGD1    LENGTH TO MOVE                      90243
OPMSGD2  DC Y(OPMSGD2X-*),X'8000',C'CHECK SETUP FOR PROPER OUTPUT TAPE'
OPMSGD2X DC    X'40008000'                                       90243
MOPERR VCON  '**** PROCESSING TERMINATED DUE TO UNRECOVERABLE OUTPUT ER*
               ROR'                                              82004
MIOEQC   VCON  '0TAPE RAN OFF THE REEL - FILE MARK MISSING'      82002
MIOMRX  VCON '0MORE THAN 100 FEET OF BLANK TAPE PASSED; I/O TERMINATED'
MOPEOT   VCON  '0END OF OUTPUT TAPE REACHED'                     82004
VOLNUSE  VCON  ' TAPE NOT USABLE AS A STANDARD LABELLED VOLUME'  90243
MSGNOCPY VCON  ' ****** TAPE NOT COPIED ******'                  90243
ABOUT213 VCON  ' ****** OUTPUT OPEN FAILED *****'                90243
SKPEOF   FDOPT NL,CC=C'0'    DOUBLE-SPACE                        90243
         FDPRT '***** END FILE ENCOUNTERED AFTER BLOCK',PADR     82002
         FDPRT BLKALL,I                                          82002
         FDPRT *END                                              82002
VOLINIT  VCON  '0***** TAPE HAS BEEN INITIALIZED, BUT NOT USED. FIRST R*
               EFERENCE MUST SPECIFY DISP=NEW'                   90243
VOLENDOK VCON  '***** NORMAL END OF VOLUME'                      90243
         SPACE 1                                                 82002
         PRINT &PRTMAC                                           90243
TREBTOAS TRTAB TYPE=ETOA                                         90243
TRASTOEB TRTAB TYPE=ATOE                                         90243
         SPACE 1                                                 89050
TRTANSI  DC    128X'00',128X'01'  ASCII CODE DETECTION (>7F)     89050
         SPACE 1                                                 90243
TREBTOIS TRTAB TYPE=ETOI     ISCII SUPPORT                       90243
TRISTOEB TRTAB TYPE=ITOE     ISCII SUPPORT                       90243
         PRINT &PRTSYS                                           90243
         SPACE 3
         PRINT &PRTSYS                                           82002
IOBUF    DSECT                                                   82002
BUFLINK  DC    0A(0)         ADDRESS OF NEXT BUFFER              82002
BUFLAG   DC    X'0'          I/O BUFFER CONTROL FLAG             82002
BUFEMPTY EQU   0             BUFFER UNUSED                       82002
BUFREAD  EQU   128           READ STARTED                        82002
BUFRITE  EQU   64            WRITE STARTED                       82002
BUFXLATE EQU   16            BUFFER TRANSLATION REQUIRED         82002
*        LOW BITS X'08'-X'01' DEFINED AS IOFG LOW BYTE           82002
BUFLINKA DC    AL3(0)        ADDRESS OF NEXT BUFFER              82002
BIOECB   DC    F'0'          I/O ECB                             82002
BIO      DS    0D                                                82002
BIOFLAG1 DC    X'40'         1 RELATED IOB (HAH)                 82002
BIOFLAG2 DC    X'00'                                             82002
BIOSENS0 DC    X'00'                                             82002
BIOSENS1 DC    X'00'                                             82002
BIOECBPT DC    A(BIOECB)                                         82002
BIOFL3   DC    X'00'                                             82002
BIOCSW   DC    XL7'00'                                           82002
BIOSTART DC    A(READCCW)                                        82002
BIODCBPT DC    A(0)          SYSUT1 OR SYSUT2 DCB                82002
BIORESTR DC    A(READCCW)                                        82002
BIOINCAM DC    H'1'                                              82002
BIOERRCT DC    H'0'                                              82002
READCCW  CCW   X'02',0,X'20',X'FFFF'   READ BIG BLOCKS           82002
RITECCW  CCW   X'01',0,X'20',X'FFFF'   WRITE BIG BUFFER          82002
WTMCCW   CCW   X'1F',0,X'60',1         WRITE TAPE MARK           82002
         CCW   3,0,X'20',1               NOP                     82002
BUFDATA  DS    0D            START OF DATA BLOCK                 82002
         SPACE 1                                                 82004
         IECDSECT  ,
         SPACE 1                                                 82004
         ORG   BUFDATA       DEFINE FOR IEBCOPY BLOCKS           82004
CP1RDW   DS    Y(60,0,56,0)  FIXED HEADER                        82004
CP1INDC  DS    X             ERROR FLAG - X'80' BAD DUMP         82004
CP1ID    DS    XL3           IEBCOPY IDENTIFIER                  82004
CP1DSORG DS    XL2                                               82004
CP1BLKSI DS    XL2           BLOCKSIZE                           82004
CP1LRECL DS    XL2           LRECL                               82004
CP1RECFM DS    X             RECFM                               82004
CP1KEYLN DS    X                                                 82004
CP1OPTCD DS    X                                                 82004
         DS    X               RESERVED ?                        82004
         DS    XL2             TAPE BLOCKSIZE                    82004
CP1TYPE  DS    XL4           UCB DEVICE TYPE                     82004
CP1DZBLK DS    XL4           MAX DEVICE BLOCKSIZE                82004
CP1DZCYL DS    XL2           CYL/VOLUME                          82004
CP1DZTRK DS    XL2           TRK/CYL                             82004
         SPACE 1                                                 82004
         ORG   BUFDATA       DEFINE FOR SECOND IEBCOPY RECORD    82004
CP2RDW   DS    Y(284,0,280,0)                                    82004
CP2DEB   DS    XL16          LAST 16 BYTES OF DEBBASIC           82004
CP2DASD  DS    16XL16        DEB EXTENTS                         82004
         SPACE 1                                                 82004
         ORG   BUFDATA+85    IEHMOVE DSCB1 COPY                  82004
         IECSDSL1 1          EXPAND UNALIGNED FORMAT1 DSCB       82004
         ORG   BUFDATA+282                                       82004
MV1TYPE  DS    XL4           DEVICE TYPE                         82004
         SPACE 1                                                 82011
         ORG   BUFDATA       DEFINE IEBCOPY DIRECTORY BLOCK      82011
CP3BDW   DS    Y(0,0)        BLOCK DESCRIPTOR                    82011
CP3RDW   DS    Y(0,0)        RECORD DESCRIPTOR                   82011
CP3BLKID DS    XL4'0'        CONTROL WORD                        82011
CP3CCHHR DS    XL5           DISK ADDRESS                        82011
CP3KLDL  DS    XL3           KEY/DATA LENGTH                     82011
CP3KEY   DS    XL8           KEY                                 82011
         ORG   ,                                                 82004
         SPACE 1                                                 86096
SLANALYZ CSECT ,                                                 86096
FAKEBUFF DC    0D'0',XL(BUFDATA-IOBUF)'0'                        86096
FAKEVOL1 DC    CL80'VOL1*NONE*   VOL1 LABEL NOT ON INPUT TAPE??????????*
               '                                                 86096
         SPACE 2                                                 82002
SAVE     DSECT ,             CONTINUATION OF WORK/SAVE AREA      82002
SUBSAVE  DS    18F'0'        SUBTASK SAVE AREA                   82002
DB       DC    2D'0'         DOUBLE WORD WORK AREA               89050
ZEROES   DC    D'0'          CONSTANT                            82002
UPD      DC    2D'0'         EXTENDED WORK AREA                  82002
@SERVICE DS    A             ADDRESS OF @SERVICE ROUTINE         82002
@PRINTER DS    A             ADDRESS OF @PRINTER ROUTINE         82002
RETCODE  DS    0F,XL3        RETURN CODE                         82002
CC       DS    X               RETURN CODE                       82002
CCANSI   EQU   1               NO CODE OVER X'7F' READ           89050
CCNL     EQU   2               UNLABELLED TAPE                   89050
CCWARN   EQU   4               MINOR PROBLEM                     89050
CCERROR  EQU   8               ERROR                             89050
CCMAJOR  EQU   12              MAJOR ERROR                       89050
CCNASTY  EQU   16              MAJOR MAJOR ERROR                 89050
CCFATAL  EQU   20              FATAL ERROR                       89050
AUTHFLAG DS    X             AUTHORIZATION FLAGS                 82002
MSGUNIT  DS    X             DEV 1 OR 2 FOR MESSAGES             82002
ERROPT   DC    X'00'         'OR'ED COPY OF JFCEROPT             83353
ERRNOACC EQU   2             SUPPRESS ERROR ACCEPTANCE (LOG.ERR) 83353
ERRNOPRT EQU   1             SUPPRESS PRINTING (SECOND TIME)     83353
         SPACE 2                                                 82002
SYSUT1   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=(E),REPOS=N,OPTCD=Z,       *
               EODAD=READEOF,EXLST=INPEXLST,DEVD=TA              82002
SYSUT1DD DC    CL8'SYSUT1'                                       82002
SYSUT2   DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=(E),REPOS=N,OPTCD=Z,       *
               EXLST=OUTEXLST,DEVD=TA                            82002
SYSUT2DD DC    CL8'SYSUT2'                                       82002
OPLSTIN  DC    0A(0),X'80',AL3(SYSUT1)  OPEN LIST                82002
OPLSTOUT DC    0A(0),X'8F',AL3(SYSUT2)  OPEN LIST - OUTPUT       82002
         SPACE 1                                                 82002
INPEXLST DC    0F'0',X'05',AL3(OPENEXIT),X'87',AL3(INPJFCB)      82002
OUTEXLST DC    0F'0',X'05',AL3(OPENXOUT),X'87',AL3(OUTJFCB)      82002
         SPACE 1                                                 82002
*        FIELDS SET ONCE PER RUN                                 82002
ACB      DC    A(0)
TCBSAVE  DC    A(0)
LISTATT  ATTACH EP=SLCOPY,ECB=1234,SZERO=NO,SHSPV=0,DPMOD=+4,SF=L
MAINLINE GETMAIN VC,LA=1234,A=1234,MF=L                          90243
MAINLY   DC    2A(0)         ADDRESS/SIZE OF GETMAIN             82002
CURBLOCK DC    A(0)          CURRENT I/O WORK BLOCK              82002
BUFINC   DC    F'0'          SIZE OF WORK BLOCK                  82002
ECBADD   DC    A(0),X'80',AL3(ACB)
MAXBLKUS DC    F'0'          MAXIMUM USER SPECIFIED BLKSIZE      82002
MAXBLKSI DC    F'0'          MAXIMUM BUFFER READ LENGTH          82002
MAXBLKRD DC    F'0'          CURRENT FILE'S BUFFER LENGTH        82004
MAXEXCP  DC    F'0'          NUMBER OF BUFFERS                   82002
DMSTART  DC    F'1'          FIRST BLOCK # DUMPED IN EACH FILE   82002
DMEND    DC    F'1'          LAST BLOCK IN EACH FILE TO BE DUMPED
DMSIZE   DC    F'1024'       MAX BLOCK LENGTH TO BE DUMPED       82002
ERRSIZE  DC    F'1024'       MAX LENGTH DUMPED FOR ERROR BLOCKS  82002
MAXERR   DC    F'9999999'    ERRORS RESULTING IN VOLUME TERMINATION
PRTERR   DC    F'15'         MAXIMUM BAD BLOCKS PRINTED PER FILE 82002
MAXCONS  DC    F'9999999'    CONSECUTIVE ERRORS CAUSING TERMINATION
MAXLBERR DC    F'9999999'    MAXIMUM LABEL ERRORS TO QUIT ON     82002
PRTLABEL DC    F'10'         BAD LABELS TO DUMP/GOOD LABELS TO PRINT
DMINC    DC    F'1'          DUMP INCREMENT                      82002
MAXVOL   DC    H'1'          MAX VOLUMES TO DO
MAXNAM   DC    H'1'
MAXFIL   DC    H'32767'      MAXIMUM FILE TO BE PROCESSED        82002
CURVOL   DC    H'0'          CURRENT VOLUME NUMBER
ANSIVOL1 DC    C'VOL1'       TRANSLATED TO ASCII                 82002
ANSIHDR  DC    C'HDR'        TRANSLATED TO ASCII                 89318
         SPACE 1
FLAG     DC    X'0'
FGCOPY   EQU   X'80'           THIS IS A COPY (RATHER THAN ANALYZE)
FGDEBUG  EQU   X'40'           DEBUG MODE                        82002
FGSKIP   EQU   X'20'           USE FSR/FSF MODE                  82002
FGSKIPSL EQU   X'10'           USE FSR/FSF MODE ON SL TAPES ONLY 82002
FGLTM    EQU   X'08'           LEADING TAPE MARK PERMITTED       82002
FGFORCE  EQU   X'04'           FORCE NL MODE                     82002
FGEOV    EQU   X'02'           END-VOLUME FOUND                  82002
NEWVOL   EQU   X'01'           FORCE CLOSE AND DO NEXT VOLUME    82002
         SPACE 1                                                 90243
FLAG2    DC    X'00'         MORE FLAGS                          90243
FGANSI3  EQU   X'80'           CONVERT OUTPUT TO ANSI 3          90243
FGANSI1  EQU   X'40'           CONVERT OUTPUT TO ANSI 1          90243
FGSAME   EQU   X'20'           COPY ANSI INPUT AS ANSI OUTPUT    90243
         SPACE 1                                                 82002
CLEANFG  DC    X'00'         SPECIAL CLEANUPS                    82002
FGTRUNC  EQU   X'80'           TRUNCATE LONG BLOCKS AND FB LRECL|BLKSI
FGANTEST EQU   X'40'           TESTED EBCDIC VS. ASCII CODE      89050
FGCUCC   EQU   X'20'           KERMIT TAPE FIX REQUEST           84253
FGUNSEC  EQU   X'02'           UNSET FL1FSEC (PRIVY USER)        89055
FGASIS   EQU   X'01'           I/P VOLSER MAY BE EQUAL TO O/P    82045
         SPACE 1                                                 82002
*        FIELDS CLEARED FOR EACH VOLUME                          82002
CLRVOL   DS    0F            START OF VOLUME CLEAR               82002
BLKEXCP  DC    F'0'          UNCHECKED EXCPS                     82002
BLKFILE  DC    F'0'          BLOCK COUNT OF DATA FILE            82002
BLKFILE1 DC    F'0'          BLOCK COUNT OF FIRST DATA FILE      82002
OUTALL   DC    F'0'          PREVIOUS FILE'S OUTPUT COUNT        82002
LOGFIL   DC    H'0'          CURRENT LOGICAL FILE NUMBER         82002
PHYSFIL  DC    H'0'          CURRENT PHYSICAL FILE NUMBER        82002
FBLOCK   DC    F'0'          FIXED RECFM - EXPECTED BLOCK SIZE
FRECLN   DC    F'0'          FIXED RECFM - RECORD LENGTH         82002
WHICH    DC    X'0'          FILE TYPE (0-HDR, 1-DATA, 2-TRLR)   82002
         SPACE 1
IOFG     DC    X'0'          I/O FLAG                            82002
IOOPEN   EQU   X'80'         OPEN IN PROGRESS
IOCLOSE  EQU   X'40'         CLOSE IN PROGRESS
IOREAD   EQU   X'20'         READ IN PROGRESS
IOLEN    EQU   X'08'         ZERO BLOCK SIZE ???????             82002
IOERR    EQU   X'04'         I/O ERROR OTHER THAN EQUIPMENT CHECK
IOEQC    EQU   X'02'         I/O ERROR - EQUIP CHK - ASSUME END-REEL
IOEOF    EQU   X'01'         END-FILE HAS BEEN READ
IOFBAD   EQU   IOEQC+IOERR+IOLEN   ERROR CONDITIONS
         SPACE 1
LBLFG    DC    X'0'                                              82002
LVOL1    EQU   X'80'         VOL1 LABEL READ
LHDR1    EQU   X'40'         HDR1 OR TRL1 READ
LHDR2    EQU   X'20'         HDR2 OR TRL2 READ
LPROM    EQU   X'04'           PROTECT MSG ISSUED                82002
LPROF    EQU   X'02'         PROTECTED FILE (FL1FSEC='1')        82002
LPROT    EQU   X'01'         PROTECTED TAPE (VOLSEC='1')         *TSM*
         SPACE 1
SLFG     DC    X'00'         LABEL TYPE/DATAFILE PROCESSING FLAG 82002
SLNL     EQU   X'80'           NL TAPE OR BAD SL                 82002
SLFULL   EQU   X'80'           DUMP DATA BLOCKS IN FULL (PARM SCAN)
SLSKIPED EQU   X'40'           FSF USED FOR THIS FILE            82002
SLSPACED EQU   X'20'           FSR USED FOR THIS FILE            82002
SLSPACE  EQU   X'10'           USE SPACING CCWS FOR THIS FILE    82002
SLDIDSF  EQU   X'08'           FSF ISSUED                        82002
SLANSI3  EQU   X'04'           VOL1 IN ANSI TYPE 3               90243
         SPACE 1                                                 82002
STACKFG  DC    X'00'         STACKED HEADER/TRAILER RECORDS      82002
STKVOL1  EQU   X'80'           VOL1 LABEL STACKED                82002
STKHDR1  EQU   X'40'           HDR1/EOF1/EOV1 STACKED            82002
STKHDR2  EQU   X'20'           HDR2/EOF2/EOV2 STACKED            82002
STKSUB   EQU   X'10'           FILE SUB-HEADER PRINTED           82002
STKVOL1R EQU   X'08'           I/O ERROR IN VOL1 LABEL           82002
STKHDR1R EQU   X'04'           I/O ERROR IN HDR1 LABEL           82002
STKHDR2R EQU   X'02'           I/O ERROR IN HDR2 LABEL           82002
         SPACE 1                                                 82017
*        FIELDS CLEARED EXPLICITLY                               82017
CLRUTIL  DS    0F            START OF UTILITY WORK AREA          82017
*        SAVE AREA FOR IEHMOVE/IEBCOPY FILE DATA                 82004
UTYTRACK DS    F             NUMBER OF TRACKS                    82004
UTYDE    DS    F             NUMBER OF DIRECTORY BLOCKS          82004
UTYTPCYL DS    H             TRACKS PER CYLINDER                 82004
UTYDSN   DS    CL44          IEHMOVE ONLY - DATASET NAME         82004
UTYTYPE  DS    XL4           UCB DEVICE TYPE                     82004
UTYGEN   DS    CL6           EQUIVALENT GENERIC NAME             82004
UTYDSORG DS    XL2           DCB PARAMETERS IN DSCB1 SEQUENCE    82004
UTYRECFM DS    X                                                 82004
UTYOPTCD DS    X                                                 82004
UTYBLKSI DS    XL2                                               82004
UTYLRECL DS    XL2                                               82004
UTYKEYLN DS    X                                                 82004
UTYFLAG  DS    X             ABOVE DATA PRESENT                  82004
UTFMOVE  EQU   X'FF'           IEHMOVE FILE                      82004
UTFCOP1  EQU   1               IEBCOPY - FIRST BLOCK READ        82004
UTFCOP2  EQU   2               IEBCOPY - SECOND BLOCK READ       82004
UTFCOP3  EQU   4               IEBCOPY - D.E. COUNT COMPLETE     82011
CLRUTILL EQU   *-CLRUTIL     LENGTH TO CLEAR                     82017
         SPACE 1                                                 82002
*        FIELDS CLEARED FOR EACH FILE                            82002
CLRFILE  DS    0D            START OF FILE CLEAR                 82002
AVGBLK   DC    0D'0',F'0'    AVERAGE BLOCK SIZE - ADDER          82002
AVGBLKSZ DC    F'0'          AVERAGE BLOCK SIZE                  82002
IOMSGPTR DC    A(0)          ZERO OR POINTER TO V-FORMAT ERROR MSG
BLKFIX   DC    F'0'          NUMBER OF BLOCKS WITH SIZE FIXES    82002
BLKACC   DC    F'0'          ACCEPTED DESPITE I/O ERROR          83353
BLKMIN   DC    F'0'                                              82002
BLKMAX   DC    F'0'                                              82002
BLKCNT   DC    F'0'          BLOCK COUNTER                       82002
BLKERR   DC    F'0'          ERROR COUNTER                       82002
BLKZER   DC    F'0'          ZERO LENGTH BLOCKS                  82002
BLKSHORT DC    F'0'          NUMBER OF SHORT BLOCKS IN A FIXED FILE
BLKALL   DC    F'0'          TOTAL BLOCKS READ                   82002
OUTCUR   DC    F'0'          CURRENT OUTPUT RECORD COUNT         82002
DALEN    DC    F'0'          BLOCK LENGTH READ                   82002
DMLEN    DC    F'0'          LENGTH TO BE DUMPED                 82002
DMDISP   DC    F'0'          DISPLACEMENT FROM START OF RECORD   82002
CONSOLD  DC    F'0'   1/3    CONSECUTIVE ERRORS FOR PRIOR RECORDS
CONSCNT  DC    F'0',F'0'     CONSECUTIVE ERROR COUNTER / ZEROES  82002
CLRFILEL EQU   *-CLRFILE     LENGTH TO CLEAR                     82002
CLRVOLL  EQU   *-CLRVOL      LENGTH TO CLEAR FOR NEW VOLUME      82002
         SPACE 1
         DC    0A(0)         FORCE ALIGNMENT
INPJFCB  DC    XL176'0'                                          82002
OUTJFCB  DC    176X'0'       OUTPUT JFCB                         82002
USERJFCB DC    176X'0'       ORIGINAL OUTPUT JFCB                82002
VSSER    DC    CL6' '        VOLSER IN INPUT JFCB                82002
VLVOL    DC    CL6' '        INTERIM VOLSER HOLDER               82002
VPHSER   DC    CL6' '        VOLSER FOR MISMATCH BETWEEN LABEL AND DD
CLCNT    DC    CL6' '
RECFM    DC    CL4' '        RECORD FORMAT OF DATA FILE          82002
         SPACE 1                                                 82002
SAVEVOL1 DC    CL80' '       VOL1 OF CURRENT TAPE                82002
SAVEHDR1 DC    CL80' '       HDR1 OF CURRENT TAPE                82002
SAVEHDR2 DC    CL80' '       HDR2 OF CURRENT TAPE                82002
         SPACE 1
WORKLINE DC    0CL133' ',CL7' '                                  82002
VLDMDISP DC    CL4' '        DISPLACEMENT FROM START OF BLOCK    82002
         DC    CL4' '        SPACER                              82002
VLDMHEX  DC    4CL10' '      4 HEX WORDS                         82002
         DC    CL2' '        EXTRA GAP                           82002
         DC    4CL10' '      FOUR MORE HEX WORDS                 82002
         DC    CL2' '        SPACER                              82002
VLDMSEP1 DC    C'*'          SEPARATOR                           82002
VLDMEBC  DC    CL32' '       EBCDIC TEXT                         82002
VLDMSEP2 DC    C'*'          SEPARATOR                           82002
         SPACE 1                                                 90243
         ORG   WORKLINE      RE-USE SOME SPACE                   90243
OPMSGD1  DC    Y(OPMSGD1X-*),X'8000',C'CAUTION - NEXT MOUNT FOR '
OPMSGD1V DC    CL6' ',C' IS NOT TAPE ON '                        90243
OPMSGD1U DC    CL3' ',C','                                       90243
OPMSGD1X DC    X'40008000'   ROUTCDE=1,DESC=2                    90243
         SPACE 1                                                 90243
         ORG   WORKLINE      RE-USE SOME MORE                    90243
MOPVCUU  DC    CL3' '                                            90243
MOPVDAT  DC    CL6' '                                            90243
MOPVDEN  DC    C' '          DENSITY                             90243
         ORG   WORKLINE+133  RESTORE CTR                         90243
         SPACE 1                                                 82002
SPECIOB  DS    0D            IOB FOR UNCHAINED I/O REQUESTS      82002
         DS    XL(BUFDATA-IOBUF)  BASIC IOB AND CCWS             82002
SENS3480 DC    0XL32'0'      EXTRA EXTENDED SENSE ON 3480        90243
SENSEDAT DC    XL24'0'       EXTENDED SENSE                      82002
         DS    XL(80-L'SENSEDAT)  IN CASE IOB USED FOR INPUT     82004
         SPACE 1                                                 84253
CUCCLREC DC    CL5' '        HDR2 LRECL (SAVED FOR EOF2/EOV2)    84253
MIOSYN   VCON  '0     ',END=MIOSYNE                              84253
SYNADTXT DC    CL46' '                                           84253
         DC    C' ECB='                                          84253
SYNADECB DC    C'XX',C' CSW='                                    84253
SYNADCSW DC    C'XX XXXXXX XXXX XXXX',C' CCW='                   84253
SYNADCCW DC    C'XX XXXXXX XXXX XXXX',C' SENSE='                 84253
SYNADSEN DC    C'XXXX'                                           84253
         VCON  ,             END OF SYNAD MESAGE                 84253
SAVEND   EQU   *             END OF DYNAMIC WORK AREA            82002
         SPACE 1                                                 84253
NOSECT   DSECT ,             GARBAGE SECTION                     84253
NOBYTE   DS    0X                                                84253
NOHALF   DS    0H                                                84253
NOWORD   DS    0F                                                84253
NODOUB   DS    D                                                 84253
         SPACE 1
         PRINT &PRTSYS                                           82002
         DCBD  DEVD=TA,DSORG=PS
         SPACE 1
         CVT   DSECT=YES                                         90199
         SPACE 1                                                 82002
         IHACDE ,                                                82002
         SPACE 1                                                 82002
         IKJTCB ,                                                82002
         SPACE 1                                                 82002
         IEZJSCB ,                                               82002
         SPACE 1                                                 82002
         IEFUCBOB ,                                              82002
         AIF   (&MVSSP).NOTBOBO                                  90243
UCB3480  EQU   X'80'         UCBTBYT4 FOR 3480 TAPE DRIVE        90243
.NOTBOBO SPACE 1                                                 82011
         IEZDEB ,                                                82011
         SPACE 1                                                 82017
         IHADVCT ,     DASD DEVICE CONSTANT TABLE (IN SVC 18)    82017
         SPACE 1                                                 82017
         END
