XERFONTS TITLE 'X E R F O N T S  ***  DISPLAY XEROX FONT'
         PUNCH ' ORDER XERFONTS(P) '  MAKE DUMPS EASIER
         PUNCH ' ALIAS LASFONTS,HPFONTS ' OTHER CODES
         PUNCH ' SETCODE AC(1)     '  LET @PRINTER ACCESS SYSOUT
         COPY  OPTIONGB
         GBLC  &DJDE
         SPACE 1
         SYSPARM LIST=YES
         SPACE 1
***********************************************************************
*                                                                     *
*        COPYRIGHT 1990-1993  EXPERT SYSTEM PROGRAMMING, INC.         *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         SPACE 1
***********************************************************************
*                                                                     *
*        THIS PROGRAM PRINTS A CHARACTER MATRIX (VALUES X'40' TO      *
*        X'FF' FOR CHARACTERS SETS 00 THROUGH 09 (ANALOGOUS TO IBM'S  *
*        OPTCD=J). THE FONTS ARE DEFINED INDIRECTLY BY FORM NUMBER.   *
*        USE PARM=CONTROL TO CHANGE RANGE TO X'00' - X'FF'            *
*        USE PARM=NN PARM=N PARM='TRC=N' TO SPECIFY FONT N (0 - 15)   *
*                                                                     *
*        THE OUTPUT IS DEFINED BY CLASS AND FORM ON A SYSPRINT DD     *
*        CARD ÝE.G., SYSOUT=(X,,1928)¨                                *
*                                                                     *
*        OPTIONAL CJDE AND DJDE CARDS MAY BE SUPPLIED WITH ALTERNATE  *
*        BEGIN AND FONT VALUES                                        *
*                                                                     *
*    ENTRY (MAIN) XERFONTS USED FOR XEROX 4050 PRINTER                *
*                                                                     *
*    ENTRY LASFONTS USED FOR STC 6100 (MAY WORK FOR IBM 38XX SERIES)  *
*                                                                     *
*    ENTRY HPFONTS FOR USE WITH A P/390 ATTACHED HP LASERJET          *
*                                                                     *
***********************************************************************
         EJECT ,
*DEBUG*  PRINT &PRTSOR
XERFONTS PGMHEAD ZERO12,BASE=R12,PARM=R9,BNDRY=PAGE,                   *
               ENTRY=(LASFONTS,HPFONTS),ENTNO=FLAGS
         SPACE 1
&DJDE    SETC  ' $DJDE$     '   XEROX IDEN PREFIX DEFINITION
         SPACE 1
         MVC   SCALE,PATSCALE
         MVI   LINE,C' '
         MVC   LINE+1(L'LINE-1),LINE
         SPACE 1
         SERVINIT ,
         SERVLOAD @INPREAD,@PRINTER,@PARSER                     GP03142
         SLR   R5,R5                                            GP03142
         IC    R5,FLAGS      GET OUTPUT TYPE FLAG               GP03142
         IC    R5,NUMFONTS(R5)  APPROPRIATE TEST PAGE COUNT     GP97338
         LA    R9,0(,R9)     CLEAN PARM POINTER                  93046
         ICM   R9,15,0(R9)                                       93046
         LA    R9,0(,R9)     CLEAR PARM ADDRESS                  93046
         LTR   R9,R9         ANY ?                               93046
         BZ    SETMAX        NO                                  93046
         SLR   R8,R8                                             93046
         ICM   R8,3,0(R9)    GET PARM LENGTH                     93046
         BNP   SETMAX                                            93046
         PARSE 2(R9),(R8),OPT=(KEYWORD,UNQUOTE,COLKEY)          GP03142
         BXH   R15,R15,BADPARM
         LA    R3,PAR@TABL   POINT TO START OF LIST             GP03142
         USING PRSDSECT,R3   DECLARE RETURN ENTRIES             GP03142
PARMLOOP ICM   R3,15,PRSLINK   GET NEXT PARSED ENTRY            GP03142
         BZ    SETMAX        NONE                               GP03142
         TM    PRSFLAGS,PRSFGINT  VALID INTEGER ?               GP03142
         BZ    PARMKEY       NO                                 GP03142
         ICM   R5,15,PRS#TINT+L'PRS#TINT-4                      GP03142
         BNP   BADPARM       NO; FAIL                           GP03142
         CH    R5,=H'16'     NOT TOO LARGE ?                    GP03142
         BNL   BADPARM                                           93046
         TM    PRSTYPE,PRSPFKEY  KEYWORD ?                      GP03142
         BZ    PARMLOOP      NO; LOOK FOR MORE                  GP03142
         CLC   =C'TR',PRSKEYWD  VALID REQUEST ?                 GP03142
         BNE   BADPARM       NO                                  93046
         B     PARMLOOP      NO; LOOK FOR MORE                  GP03142
         SPACE 1                                                GP03142
PARMKEY  OC    PRS$TEXT(8),BLANKS  UPPER CASE                   GP03142
         CLC   =C'CNT',PRS$TEXT  INCLUDE CONTROL CHARACTERS ?   GP03142
         BE    PARMKEYD      NO                                 GP03142
         CLC   =C'CON',PRS$TEXT  INCLUDE CONTROL CHARACTERS ?   GP03142
         BNE   BADPARM       NO                                  93046
PARMKEYD OI    FLAGS,FGCNTRL  INCLUDE CONTROL CHARACTERS        GP03142
         B     PARMLOOP      NO; LOOK FOR MORE                  GP03142
         SPACE 1                                                GP03142
BADPARM  WTO   'INVALID PARM FIELD'                              93046
         ABEND 16                                                93046
SETMAX   STH   R5,MAXFONT                                        93043
         SPACE 1
         PRTOPEN SYSPRINT,OPT=ABEND                             GP03142
         LTR   R3,R0         TRANSFER PRINTER ADDRESS            91312
         USING PWWORK,R3                                         91312
         CLI   PWSOUTFM,C' '  ANY FORM NUMBER ?                  91312
         BNH   NOFORM#                                           91312
         MVC   FORM,PWSOUTFM  COPY                               91312
         DROP  R3                                                91312
NOFORM#  PRTLIST HEADER1,TITLE=1
         SPACE 1
         INPOPEN SYSIN,OPT=(DUMMY,NOWTO) LOOK FOR OVERRIDING INPUT
         CH    R15,=H'4'     ANY ?
         BH    DONESYN       NO
MSGULOOP INPGET DEV=1        GET A MESSAGE
         BXH   R15,R15,DONESYN END OF INPUT PROCESSING (ERR OR EOF)
         LR    R2,R1         SAVE CARD ADDRESS
         CLI   0(R2),C'*'    COMMENTS CARD ?                    GP03142
         BE    MSGPCARD      YES; PRINT                         GP03142
         CLI   4(R2),C' '    FOUR BYTES ?
         BNE   MSGUMSG       NO; NOT CONTROL CARD
         ICM   R0,15,0(R2)   GET FIRST FOUR BYTES
         O     R0,BLANKS     MAKE UPPER CASE
         CL    R0,=C'TEXT'   LIST ?                             GP03142
         BE    MSGUMSG                                          GP03142
         CL    R0,=C'LIST'   LIST ?                             GP03142
         BE    MSGUMSG                                          GP03142
         CLM   R0,7,=C'JDE'  DJDE OVERRIDE ?
         BNE   MSGUMSG       NO
         STCM  R0,8,0(R1)    MAKE SURE UPPER CASE BYTE SAVED
*        BUILD A SUBPOOL 0 GETMAIN CHAIN OF DJDE REQUESTS
         GETMAIN R,LV=72     GET A CARD'S WORTH (LESS SEQUENCE)
         MVC   0(72,R1),0(R2)  COPY INPUT LESS SEQUENCE
         XC    0(4,R1),0(R1)   CLEAR CHAIN POINTER
         ICM   R14,15,UDJDEND GET LAST DJDE
         BNZ   MSGUDJNF
         ST    R1,UDJDE      SET HEAD OF QUEUE
         B     MSGUDJYF      SAVE TAIL
MSGUDJNF ST    R1,0(,R14)    SAVE TAIL POINTER
MSGUDJYF ST    R1,UDJDEND    SET LAST ENTRY ADDRESS
         CLI   0(R2),C'D'    'DJDE' ?
         BE    MSGPCARD      YES; NORMAL SUPPLEMENTAL TEXT
         OI    UDJDE,X'80'   INDICATE REPLACEMENT TEXT
         B     MSGPCARD      GET ANOTHER CARD
         SPACE 1                                                GP03142
MSGUMSG  PRTLIST LISTASIS    TEXT CARD                          GP03142
         B     MSGULOOP                                         GP03142
         SPACE 1
MSGPCARD PRTLIST LISTCARD
         B     MSGULOOP
         SPACE 1
BADSYN   PRTLIST LISTCBAD
         OI    CCODE+L'CCODE-1,4
         B     MSGULOOP
LISTASIS FD    ' ',NL,LEN=20                                    GP03142
         FD    0(R2),80,ASIS,PAD                                GP03142
         FD    *END                                             GP03142
LISTCARD FD    ' ',NL,LEN=20
         FD    0(R2),80,PAD
         FD    *END
LISTCBAD FD    'INVALID CARD',NL,RADJ,LEN=20
         FD    0(R2),80,PAD
         FD    *END
         SPACE 1
DONESYN  INPCLOSE DEV=1      CLOSE THE INPUT FILE
XERIDEND ICM   R4,15,UDJDE   USER DJDE OVERRIDES ?
         BZ    XERIUEND      NO; WRITE LAST
         PRTF  '1'           FORCE A PAGE EJECT
         BAL   R2,PUTDJDE0   RE-INIT FOR DJDE
XERIUJDE MVC   0(68,R1),4(R4)  MOVE THE CARD IMAGE
         BAL   R2,PUTDJDE    WRITE IT
         ICM   R4,15,0(R4)   ANY MORE TO DO ?
         BNZ   XERIUJDE
         CLI   FLAGS,FGXER   XEROX MODE?                        GP97338
         BH    XERIUEND      NO DJDE, NO END                    GP97338
         MVC   0(4,R1),=C'END;'  TERMINATE THE DJDE SEQUENCE
         BAL   R2,PUTDJDE    WRITE FINAL RECORD
         B     XERIUEND      SKIP AROUND
         SPACE 2
PUTDJDE  PRTV  RDW           WRITE A DJDE RECORD
PUTDJDE0 CLI   FLAGS,FGXER   XEROX MODE?                        GP97338
         BH    PUTNOJDE                                         GP97338
         MVC   RDW(2),=Y(4+1+L'DJDE+68)  INITIALIZE LENGTH      GP97338
         MVI   AREA,C' '     PLAIN CARRIAGE CONTROL
         MVC   AREA+1(L'DJDE),DJDE  MAKE PREFIX
         MVC   AREA+L'DJDE+1(L'AREA-L'DJDE-1),AREA+L'DJDE  BLANK
         LA    R1,AREA+1+L'DJDE  FIRST INSERTION ADDRESS
         BR    R2            RETURN TO CALLER
         SPACE 1                                                GP97338
PUTNOJDE MVC   RDW(2),=Y(4+1+68)  INITIALIZE LENGTH             GP97338
         MVI   AREA,C' '     PLAIN CARRIAGE CONTROL             GP97338
         MVC   AREA+1(L'AREA-1),AREA  BLANK                     GP97338
         LA    R1,AREA+1     FIRST INSERTION ADDRESS            GP97338
         BR    R2            RETURN TO CALLER                   GP97338
         SPACE 2
XERIUEND MVI   LINE,C' '
         MVC   LINE+1(L'LINE-1),LINE
         MVI   LINE,C'0'
         SLR   R8,R8         FONT SELECTOR
         CLI   MAXFONT+L'MAXFONT-1,1  SINGLE FONT ONLY ?
         BNE   FONTLOOP      NO; KEEP
         LA    R8,C' '       USE BLANK INSTEAD
FONTLOOP STC   R8,FONT
         PRTLIST SUBFONT,TITLE=2
         PRTF  '1       .0  .1  .2  .3  .4  .5  .6  .7  .8  .9  .A  .B *
                .C  .D  .E  .F '
         LA    R2,X'40'      SKIP LOWLIFE                       GP97338
         LR    R3,R2         STOPPER FOR LOW LIFE               GP03142
         TM    FLAGS,FGCNTRL  INCLUDE CONTROL CHARACTER?        GP03142
         BZ    HIGHLIFE      NO; JUST DO (MOSTLY) PRINTABLES)   GP03142
         SLR   R2,R2                                            GP03142
LOWLINE  STC   R2,DB                                            GP03142
         PRTLIST CONCHAR                                        GP03142
         LA    R2,1(,R2)                                        GP03142
         CR    R2,R3                                            GP03142
         BL    LOWLINE                                          GP03142
HIGHLIFE LA    R2,X'40'      RESTORE BLANK                      GP08086
LINELOOP LA    R3,16
         LR    R1,R2         COPY CURRENT VALUE
         SRL   R1,4          ISOLATE HIGH PART
         LA    R1,HEXCHAR(R1)   GET ZONE CHARACTER
         MVC   ZONE,0(R1)
         LA    R4,CHAR
BYTELOOP STC   R2,CHAR-CHAR(,R4)
         LA    R2,1(,R2)
         LA    R4,CHAR2-CHAR(,R4)
         BCT   R3,BYTELOOP
         PRTF  LINE
         CH    R2,=H'256'
         BL    LINELOOP
         STC   R8,SCALFONT
         PRTF  SCALE
         LA    R8,1(,R8)     NEXT FONT
         CH    R8,MAXFONT    LAST FONT ?                         93043
         BL    FONTLOOP      NO                                  93043
EXIT     SERVTERM ,
         LH    R9,CCODE
         PGMEXIT RC=(R9)     RETURN WITH CONDITION              GP03142
         SPACE 2
SYSIN    INPWORK SYSIN,EDIT=128,WIDTH=80,EODAD=DONESYN
SYSPRINT PRTWORK SYSPRINT,TITLE=5,FILL=255  (INHIBIT TRANSLATE)  93043
HEXCHAR  DC    C'0123456789ABCDEF'
DJDE     DC    C'&DJDE'
NUMFONTS DC    AL1(10,4,1)   FONTS PER XEROX; STC 6100; HP LJ   GP97338
PATSCALE DC    C'-0----+---10----+---20----+---30----+---40----+---50'
         DC    C'----+---60----+---70----+---80----+---90----+--100'
         DC    C'----+--110----+--120----+--130----+--140----+--150'
         DC    C'----+--160----+--170----+--180----+--190----+--200'
         DC    C'----'       MAX WIDTH = 204 (IBM 3800, XRX 9700, ETC)
PATSCALL EQU   *-PATSCALE
         SPACE 1
SUBFONT  FD    FORM,NL,PAD,DEB,NULL,RADJ,LEN=32                  91312
         FD    'FONT SELECTOR',PAD                               91312
         FD    FONT,I
         FD    *END
         SPACE 1
HEADER1  FDTM  FLAGS,FG6100+FGHPLJ,BZ=HEADER1X                  GP97338
         FD    ' L A S F O N T S  **  FONT DEFINITION',NL,LEN=41 93043
         FDGOTO HEADER1P                                         93043
HEADER1X FD    ' X E R F O N T S  **  FONT DEFINITION',NL,LEN=41 93043
HEADER1P FD    'RUN ON ##/##/##  AT ##:##:##  PAGE #P##'
         FD    *END
         SPACE 1                                                GP03142
CONCHAR  FD    '   ',NL
         FD    'CHAR'                                           GP03142
         FD    DB,1,HEX,PAD                                     GP03142
         FD    '->'                                             GP03142
         FD    DB,1,ASIS                                        GP03142
         FD    '<-'                                             GP03142
         FD    *END                                             GP03142
         SPACE 1
SAVE     DSECT ,
DB       DS    D
DB2      DS    D
         SERVDEFS ,                                             GP03142
FORM     DS    CL8                                               91312
UDJDE    DS    A             CHAIN OF DJDE REQUESTS
UDJDEND  DS    A               PREVIOUS DJDE
CCODE    DS    H             RETURN CODE
MAXFONT  DS    H             NUMBER OF FONTS TO DO               93043
         SPACE 1
         MAPPARSE DSECT=NO                                      GP03142
         SPACE 1                                                GP03142
         CNOP  2,4           ALIGN 'BLANKS' ON WORD BOUNDARY     93043
RDW      DC    Y(0,0)        RECORD LENGTH, ZERO FLAGS
AREA     DS    0CL133        ASA CARRIAGE CONTROL, TEXT
LINE     DC    0CL133' ',C'0'
FONT     DC    X'00'
BLANKS   DC    CL4' '
ZONE     DC    C'0'
         DC    CL3' '
CHAR     DC    C' '
         DC    CL3' '
CHAR2    DC    C' '
         ORG   LINE+133
SCALE    DC    CL(PATSCALL)' '                                  GP05013
SCALFONT EQU   SCALE+1,1,C'C'
         SPACE 1                                                 93043
FLAGS    DC    X'00'         OPTIONS                             93043
FGXER    EQU   X'00'          XEROX (4050)                      GP97338
FG6100   EQU   X'01'           3800/6100 MODE (NOT XEROX)        93043
FGHPLJ   EQU   X'02'           HEWLETT-PACKARD LASERJET (P/390)
FGCNTRL  EQU   X'80'           INCLUDE CONTROL CHARACTERS       GP03142
SAVEEND  EQU   *                                                GP03142
         SPACE 1
         MAPPARST ,                                             GP03142
         SPACE 1                                                GP03142
         CVT   DSECT=YES
         IHACDE ,
         MAPPRTWK ,          MAP PRINTER WORK AREA               91312
         END   ,
