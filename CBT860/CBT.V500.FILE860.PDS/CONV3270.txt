CNV70    TITLE 'C O N V 3 2 7 0  ***  3270 BUFFER => ASYNC CRT'  93326
         MACRO ,                                                 93326
         TTYFLAGS ,          SEE IGPVOLT VTTTY--- CODE           84092
         GBLB  &SQTTYFG                                          93326
         AIF   (&SQTTYFG).NODUPES                                93326
&SQTTYFG SETB  1             PREVENT DUPLICATES                  93326
TDOCLT   EQU   0             OFFSET TO CLEAR TABS                93326
TDOCLD   EQU   1             OFFSET TO CLEAR DEVICE SEQUENCE     93326
TDOCLS   EQU   2             OFFSET TO CLEAR SCREEN SEQUENCE     93326
TDOCBRK  EQU   3             OFFSET TO PLOT BREAK RESET FUNCTION 93326
TDOTEXT  EQU   4             OFFSET TO NORMAL TEXT ATTRIBUTES    93326
TDOERR   EQU   5             OFFSET TO ERROR SEQUENCE            93326
TDOHIDE  EQU   6             OFFSET TO HIDDEN INPUT SEQUENCE     93326
TDOWARN  EQU   7             OFFSET TO WARNING MESSAGE           93326
TDOW80   EQU   8             SET NARROW SCREEN                   93326
TDOW132  EQU   9             SET WIDE SCREEN                     93326
TDOTAB1  EQU   10            TABS SEQUENCE PART 1                93326
TDOTAB2  EQU   11            TABS SEQUENCE PART 2                93326
TDOPR1N  EQU   12            PRINTER ON/CRT OFF                  93326
TDOPR1F  EQU   13            PRINTER OFF/CRT ON                  93326
TDOPR2N  EQU   14            PRINTER+CRT ON                      93326
TDOPR2F  EQU   15            CRT ONLY                            93326
TDOFSCMD EQU   16            SET FULL-SCREEN MODE                93326
TDOLINMD EQU   17            SET LINE MODE                       93326
TDOKBDUN EQU   18            KEYBOARD UNLOCK SEQUENCE            93326
TDOKBDLK EQU   19            KEYBOARD LOCK                       93326
TDOADR1  EQU   20            SCREEN ADDRESS - SEQ 1              93326
TDOADR2  EQU   21            SCREEN ADDRESS - SEQ 2              93326
TDOADR3  EQU   22            SCREEN ADDRESS - SEQ 3              93326
TDOLAD1  EQU   23            LINE ADDRESS - SEQ 1                93326
TDOLAD2  EQU   24            LINE ADDRESS - SEQ 2                93326
TDOLAD3  EQU   25            LINE ADDRESS - SEQ 3                93326
.*                                                               93326
TDOPTOFF EQU   26            MAX OFFSET / OPTION OFFSET          93326
TDOPTION EQU   255           CODED REQUEST FOR OPTION STRING     84106
.*                                                               93326
TRMTNULL EQU   0             UNDEFINED TTY TERMINAL              93326
TRMTFOX  EQU   1             PERKIN-ELMER FOX, OWL               93326
TRMTDEC  EQU   2             DEC-WRITER                          93326
TRMTDAM  EQU   3             DATAMEDIA COLORSCAN X0              93326
TRMTESP  EQU   4             HAZELTINE ESPRIT III (COLOR)+PRT    93326
TRMTIMS  EQU   5             IMS ULTIMA II; TELEVIDEO 950        93326
TRMTIBM  EQU   6             IBM P/C - PRELIMINARY ONLY          93326
TRMTTEK  EQU   7             TEKTRONIX 40XX                      93326
TRMTHP   EQU   8             HEWLETT-PACKARD 2621/7221 PLTR      93326
TRMTWYS  EQU   9             WYSE 50                             93326
TRMT3101 EQU   10            IBM 3101                            93326
TRMTV100 EQU   11            VT-100 AND WYSE-75                  84183
TRMTWY30 EQU   12            WYSE-300 (COLOR)                    84183
TRMTV200 EQU   13            VT-200, 220, 240 (241?)             84267
TRMTTV92 EQU   14            TELE-VIDEO 925 (AND 920)            85020
TRMTTVPT EQU   15            TELE-VIDEO PERSONAL TERMINAL        85020
TRMTWY60 EQU   16            WYSE-60 (IN NATIVE OR WY-50+ MODE)  87054
.NODUPES MEND  ,                                                 93326
         SPACE 1                                                 93326
         MACRO ,                                                 93326
&NM      TTYSEQ &FUN,&TYPE,&HAVE=,&ERR=                 ADDED ON 84106
         GBLC  &MACPLAB                                          93326
&MACPLAB SETC  '&NM'                                             93326
         MACPARM R0,&FUN     LOAD FUNCTION REQUEST               93326
         TTYFLAGS ,          EXPAND OPTION FLAG MAPPING          93326
         AIF   (T'&TYPE EQ 'O').HATYP                            93326
         AIF   ('&TYPE'(1,1) NE '(').HATYP                       93326
         AIF   ('&TYPE'(2,1) EQ '(').HATYP                       93326
         MACPARM R15,&TYPE                                       93326
         MACPARM R15,24,OP=SLL                                   93326
         MACPARM R0,(R15),OPR=OR                                 93326
         AGO   .PATYP                                            93326
.HATYP   ANOP  ,                                                 93326
         MACPARM R0,8,&TYPE,MODE=THREE,NULL=SKIP,OP=ICM         GP02241
.PATYP   AIF   ('&HAVE' EQ 'CVT' OR '&HAVE' EQ 'UCV' OR                *
                '&HAVE' EQ 'VOLT').HAVOLT                        93326
         AIF   (T'&HAVE EQ 'O').NOHA                             93326
         MNOTE 4,'INVALID HAVE= PARAMETER IGNORED'               93326
.NOHA    ANOP  ,                                                 93326
.HAVOLT  ANOP  ,                                                 93326
         MACPARM R14,TTYSEQ,OP=BAL,OPR=BALR                      93326
         MACPARM &ERR,NULL=SKIP,MODE=ONE,OP=BZ,OPR=BZR          GP02241
         MEND  ,                                                 93326
         SPACE 1                                                 93326
         MACRO ,                                                 93326
&NM      TTYDEF &NAME=,&LENSF=0,&TABTYP=0,&CLD=,&CLR=,&CLT=,           *
               &FSCMD=,&LINMD=,&KBDUN=,&KBDLK=,&ADRTYP=,               *
               &ADR1=,&ADR2=,&ADR3=,&LAD1=,&LAD2=,&LAD3=,              *
               &BRK=,&TEXT=,&ERR=,&HIDE=,&WARN=,&W80=,&W132=,          *
               &TAB1=,&TAB2=,&P1N=,&P1F=,&P2N=,&P2F=             93326
.*                                                      ADDED ON 84092
.*       THIS MEMBER EXPANDS TTY CONTROL SEQUENCE DEFINITIONS.   93326
.*                                                               93326
         LCLA  &OFFS(26),&OLEN(26)                               93326
         LCLC  &OTXT(26),&OPTS(4)                                93326
         LCLA  &I,&PTR                                           93326
&PTR     SETA  26+4          OFFSET TO FIRST ENTRY               93326
.*             THE MAXIMUM VALUE OF 26 IS MATCHED BY FLAG        93326
.*             TDOPTOFF IN MEMBER TTYFLAGS.                      93326
&OPTS(1) SETC  '0'                                               93326
&OPTS(2) SETC  '0'                                               93326
&OPTS(3) SETC  '0'                                               93326
&OPTS(4) SETC  '0'                                               93326
         AIF   (T'&NAME EQ 'O').NONAME                           93326
         DC    CL8'&NAME'    DEVICE NAME/ACRONYM                 93326
.NONAME  AIF   (T'&ADRTYP EQ 'O').NOADRLN                        93326
&OPTS(2) SETC  '&ADRTYP'                                         93326
.NOADRLN AIF   (T'&TABTYP EQ 'O').NOTABLN                        93326
&OPTS(4) SETC  '&TABTYP'                                         93326
.NOTABLN AIF   (T'&LENSF EQ 'O').NOSFLEN                         93326
&OPTS(3) SETC  '&LENSF'      ATTRIBUTE SIZE ON SCREEN            93326
.NOSFLEN AIF   (T'&CLT EQ 'O').NOCLT                             93326
&OFFS(1) SETA  &PTR                                              93326
&OLEN(1) SETA  (K'&CLT+1)/2                                      93326
&OTXT(1) SETC  '&CLT'                                            93326
&PTR     SETA  &PTR+1+&OLEN(1)                                   93326
.NOCLT   AIF   (T'&CLD EQ 'O').NOCLD                             93326
&OFFS(2) SETA  &PTR                                              93326
&OLEN(2) SETA  (K'&CLD+1)/2                                      93326
&OTXT(2) SETC  '&CLD'                                            93326
&PTR     SETA  &PTR+1+&OLEN(2)                                   93326
.NOCLD   AIF   (T'&CLR EQ 'O').NOCLR                             93326
&OFFS(3) SETA  &PTR                                              93326
&OLEN(3) SETA  (K'&CLR+1)/2                                      93326
&OTXT(3) SETC  '&CLR'                                            93326
&PTR     SETA  &PTR+1+&OLEN(3)                                   93326
.NOCLR   AIF   (T'&BRK EQ 'O').NOBRK                             93326
&OFFS(4) SETA  &PTR                                              93326
&OLEN(4) SETA  (K'&BRK+1)/2                                      93326
&OTXT(4) SETC  '&BRK'                                            93326
&PTR     SETA  &PTR+1+&OLEN(4)                                   93326
.NOBRK   AIF   (T'&TEXT EQ 'O').NOTEXT                           93326
&OFFS(5) SETA  &PTR                                              93326
&OLEN(5) SETA  (K'&TEXT+1)/2                                     93326
&OTXT(5) SETC  '&TEXT'                                           93326
&PTR     SETA  &PTR+1+&OLEN(5)                                   93326
.NOTEXT  AIF   (T'&ERR EQ 'O').NOERR                             93326
&OFFS(6) SETA  &PTR                                              93326
&OLEN(6) SETA  (K'&ERR+1)/2                                      93326
&OTXT(6) SETC  '&ERR'                                            93326
&PTR     SETA  &PTR+1+&OLEN(6)                                   93326
.NOERR   AIF   (T'&HIDE EQ 'O').NOHIDE                           93326
&OFFS(7) SETA  &PTR                                              93326
&OLEN(7) SETA  (K'&HIDE+1)/2                                     93326
&OTXT(7) SETC  '&HIDE'                                           93326
&PTR     SETA  &PTR+1+&OLEN(7)                                   93326
.NOHIDE  AIF   (T'&WARN EQ 'O').NOWARN                           93326
&OFFS(8) SETA  &PTR                                              93326
&OLEN(8) SETA  (K'&WARN+1)/2                                     93326
&OTXT(8) SETC  '&WARN'                                           93326
&PTR     SETA  &PTR+1+&OLEN(8)                                   93326
.NOWARN  AIF   (T'&W80 EQ 'O').NOW80                             93326
&OFFS(9) SETA  &PTR                                              93326
&OLEN(9) SETA  (K'&W80+1)/2                                      93326
&OTXT(9) SETC  '&W80'                                            93326
&PTR     SETA  &PTR+1+&OLEN(9)                                   93326
.NOW80   AIF   (T'&W132 EQ 'O').NOW132                           93326
&OFFS(10) SETA &PTR                                              93326
&OLEN(10) SETA (K'&W132+1)/2                                     93326
&OTXT(10) SETC '&W132'                                           93326
&PTR     SETA  &PTR+1+&OLEN(10)                                  93326
.NOW132  AIF   (T'&TAB1 EQ 'O').NOTAB1                           93326
&OFFS(11) SETA &PTR                                              93326
&OLEN(11) SETA (K'&TAB1+1)/2                                     93326
&OTXT(11) SETC '&TAB1'                                           93326
&PTR     SETA  &PTR+1+&OLEN(11)                                  93326
.NOTAB1  AIF   (T'&TAB2 EQ 'O').NOTAB2                           93326
&OFFS(12) SETA &PTR                                              93326
&OLEN(12) SETA (K'&TAB2+1)/2                                     93326
&OTXT(12) SETC '&TAB2'                                           93326
&PTR     SETA  &PTR+1+&OLEN(12)                                  93326
.NOTAB2  AIF   (T'&P1N EQ 'O').NOP1N                             93326
&OFFS(13) SETA &PTR                                              93326
&OLEN(13) SETA (K'&P1N+1)/2                                      93326
&OTXT(13) SETC '&P1N'                                            93326
&PTR     SETA  &PTR+1+&OLEN(13)                                  93326
.NOP1N   AIF   (T'&P1F EQ 'O').NOP1F                             93326
&OFFS(14) SETA &PTR                                              93326
&OLEN(14) SETA (K'&P1F+1)/2                                      93326
&OTXT(14) SETC '&P1F'                                            93326
&PTR     SETA  &PTR+1+&OLEN(14)                                  93326
.NOP1F   AIF   (T'&P2N EQ 'O').NOP2N                             93326
&OFFS(15) SETA &PTR                                              93326
&OLEN(15) SETA (K'&P2N+1)/2                                      93326
&OTXT(15) SETC '&P2N'                                            93326
&PTR     SETA  &PTR+1+&OLEN(15)                                  93326
.NOP2N   AIF   (T'&P2F EQ 'O').NOP2F                             93326
&OFFS(16) SETA &PTR                                              93326
&OLEN(16) SETA (K'&P2F+1)/2                                      93326
&OTXT(16) SETC '&P2F'                                            93326
&PTR     SETA  &PTR+1+&OLEN(16)                                  93326
.NOP2F    AIF   (T'&FSCMD EQ 'O').NOFSCMD                        93326
&OFFS(17) SETA  &PTR                                             93326
&OLEN(17) SETA  (K'&FSCMD+1)/2                                   93326
&OTXT(17) SETC  '&FSCMD'                                         93326
&PTR     SETA  &PTR+1+&OLEN(17)                                  93326
.NOFSCMD  AIF   (T'&LINMD EQ 'O').NOLINMD                        93326
&OFFS(18) SETA  &PTR                                             93326
&OLEN(18) SETA  (K'&LINMD+1)/2                                   93326
&OTXT(18) SETC  '&LINMD'                                         93326
&PTR     SETA  &PTR+1+&OLEN(18)                                  93326
.NOLINMD  AIF   (T'&KBDUN EQ 'O').NOKBDUN                        93326
&OFFS(19) SETA  &PTR                                             93326
&OLEN(19) SETA  (K'&KBDUN+1)/2                                   93326
&OTXT(19) SETC  '&KBDUN'                                         93326
&PTR     SETA  &PTR+1+&OLEN(19)                                  93326
.NOKBDUN   AIF   (T'&KBDLK EQ 'O').NOKBDLK                       93326
&OFFS(20) SETA &PTR                                              93326
&OLEN(20) SETA (K'&KBDLK+1)/2                                    93326
&OTXT(20) SETC '&KBDLK'                                          93326
&PTR     SETA  &PTR+1+&OLEN(20)                                  93326
.NOKBDLK  AIF   (T'&ADR1 EQ 'O').NOADR1                          93326
&OFFS(21) SETA &PTR                                              93326
&OLEN(21) SETA (K'&ADR1+1)/2                                     93326
&OTXT(21) SETC '&ADR1'                                           93326
&PTR     SETA  &PTR+1+&OLEN(21)                                  93326
.NOADR1  AIF   (T'&ADR2 EQ 'O').NOADR2                           93326
&OFFS(22) SETA &PTR                                              93326
&OLEN(22) SETA (K'&ADR2+1)/2                                     93326
&OTXT(22) SETC '&ADR2'                                           93326
&PTR     SETA  &PTR+1+&OLEN(22)                                  93326
.NOADR2  AIF   (T'&ADR3 EQ 'O').NOADR3                           93326
&OFFS(23) SETA &PTR                                              93326
&OLEN(23) SETA (K'&ADR3+1)/2                                     93326
&OTXT(23) SETC '&ADR3'                                           93326
&PTR     SETA  &PTR+1+&OLEN(23)                                  93326
.NOADR3   AIF   (T'&LAD1 EQ 'O').NOLAD1                          93326
&OFFS(24) SETA &PTR                                              93326
&OLEN(24) SETA (K'&LAD1+1)/2                                     93326
&OTXT(24) SETC '&LAD1'                                           93326
&PTR     SETA  &PTR+1+&OLEN(24)                                  93326
.NOLAD1   AIF   (T'&LAD2 EQ 'O').NOLAD2                          93326
&OFFS(25) SETA &PTR                                              93326
&OLEN(25) SETA (K'&LAD2+1)/2                                     93326
&OTXT(25) SETC '&LAD2'                                           93326
&PTR     SETA  &PTR+1+&OLEN(25)                                  93326
.NOLAD2   AIF   (T'&LAD3 EQ 'O').NOLAD3                          93326
&OFFS(26) SETA &PTR                                              93326
&OLEN(26) SETA (K'&LAD3+1)/2                                     93326
&OTXT(26) SETC '&LAD3'                                           93326
&PTR     SETA  &PTR+1+&OLEN(26)                                  93326
.NOLAD3   ANOP  ,             END OF USER SUPPLIED PARMS         93326
.*                                                               93326
&NM      DC    AL1(&OFFS(1),&OFFS(2),&OFFS(3),&OFFS(4),&OFFS(5))
         DC    AL1(&OFFS(6),&OFFS(7),&OFFS(8),&OFFS(9),&OFFS(10))
         DC    AL1(&OFFS(11),&OFFS(12),&OFFS(13),&OFFS(14),&OFFS(15))
         DC    AL1(&OFFS(16),&OFFS(17),&OFFS(18),&OFFS(19),&OFFS(20))
         DC    AL1(&OFFS(21),&OFFS(22),&OFFS(23),&OFFS(24),&OFFS(25))
         DC    AL1(&OFFS(26))                                    93326
         DC    AL1(&OPTS(1),&OPTS(2),&OPTS(3),&OPTS(4))  OPTIONS 93326
&I       SETA  0                                                 93326
.EXPAND  AIF   (&I GE 26).MEND                                   93326
&I       SETA  &I+1                                              93326
         AIF   (&OLEN(&I) EQ 0 AND &OFFS(&I) EQ 0).EXPAND        93326
         AIF   (&OLEN(&I) EQ 0).EXPNULL                          93326
         DC    AL1(&OLEN(&I)),X'&OTXT(&I)'                       93326
         AGO   .EXPAND                                           93326
.EXPNULL DC    AL1(0)                                            93326
         AGO   .EXPAND                                           93326
.MEND    MEND  ,                                                 93326
         SPACE 1                                                 93326
         MACRO ,                                                 93326
&NM      XSEG  &A,&L                                             93326
         GBLC  &MACPLAB                                          93326
&MACPLAB SETC  '&NM'                                             93326
         MACPARM R15,&A                                          93326
         MACPARM R0,&L                                           93326
         MACPARM RET,XSEG,OP=BAL,OPR=BALR                        93326
         MEND  ,                                                 93326
         SPACE 1                                                 93326
         MACRO ,                                                 93336
&NM      ORCH  &OFF,&VAL,&REP                                    93336
         LCLC  &N                                                93336
&N       SETC  '1'                                               93336
         AIF   (T'&REP EQ 'O').ONCE                              93336
&N       SETC  '&REP'                                            93336
.ONCE    ANOP  ,                                                 93336
         ORG   ORDERS+&OFF
&NM      DC    &N.AL1(&VAL)
         MEND  ,
         SPACE 1
         MACRO ,
&NM      GOTO  &WHO
OFF&WHO  EQU   *-OFFBASE
         B     COMP&WHO
         MEND  ,
         SPACE 1                                                 90154
         MACRO ,                                                 90154
&NM      CKIN  &N,&ERR=COMPBAD  CHECK BUFFER LENGTH FOR INPUT SEQ
&NM      LA    RET,&N.(,RIN)                                     90154
         CR    RET,RLEN      LONG ENOUGH FOR ENTIRE SEQUENCE ?   90154
         MACPARM &ERR,MODE=ONE,OP=BH,OPR=BHR,NULL=YES            90154
         MEND  ,                                                 90154
         SPACE 1                                                 90154
         MACRO ,                                                 90154
&NM      CKOUT &N,&ERR=BADEXIT                                   90154
&NM      LA    RET,&N.(,ROUT)  GET NEW END SEQUENCE              90154
         C     RET,MAXOUT    ENOUGH ROOM ?                       90154
         MACPARM &ERR,MODE=ONE,OP=BH,OPR=BHR,NULL=YES            90154
         MEND  ,                                                 90154
         SPACE 1
         PUNCH '  ALIAS CONT3270 '                              GP04234
         SPACE 1
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 2
***********************************************************************
*                                                                     *
*        COPYRIGHT 1973  GERHARD POSTPISCHIL (ASSIGNED TO E.S.P.)     *
*        COPYRIGHT 1990  EXPERT SYSTEM PROGRAMMING, INC.              *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         SPACE 1
***********************************************************************
*                                                                     *
*        BUFFER COMPRESSION AND COLOR SUBSTITUTION SUBROUTINE         *
*                                                                     *
*   CALL CONV3270(@OUT,@OUTLEN,@IN,@INLEN,@BUFSIZ,@OPTS,@COLTAB,0)    *
*         ALL @ ARE FULLWORD ADDRESSES; SIZES ARE FULLWORD BINARY     *
*         @COLTAB MAY BE ZERO (NO COLOR SUBSTITUTION REQUESTED)       *
*         @OPTS ARE MAPPED BY DSECT OPTIONS                           *
*                                                                     *
*        IF A PT ORDER IS FOUND, OR THE INPUT DOES NOT START WITH THE *
*        EXPECTED ESC|CCW|WCC|SBA SEQUENCE, THE BUFFER IS COPIED AS-IS*
*                                                                     *
*        CURRENTLY, BUFFERS STARTING WITH A WSF COMMAND ARE NOT       *
*        COMPRESSED. THIS MAY BE CHANGED LATER.                       *
*                                                                     *
*        NOTE THAT CONSECUTIVE SEQUENCES OF SBA ORDERS ARE SUPPRESSED;*
*        HENCE TCAM FULL-SCREEN SWITCH ON/OFF FAILS (SEE 'LOOPSBA').  *
*                                                                     *
*   CONTENTS OF @OUTLEN MUST BE SET TO MAXIMUM; TRUE VALUE OF         *
*   LENGTH IS RETURNED IN THIS WORD. THE OPTIONS BYTE CONTAINING      *
*   FUNCTION FLAGS MUST BE INITIALIZED BY THE CALLER; THE BITS        *
*   ARE CHANGED DURING PROCESSING.                                    *
*                                                                     *
*        R15 RETURN:  0      REQUEST COMPLETED                        *
*                     4      REQUEST COMPLETED; BAD CODE POINTS IN    *
*                            BUFFER (UNRECOGNIZED ORDER OR CHARACTER) *
*                     8      INCOMPLETE - OUTPUT BUFFER TOO SHORT     *
*                    12      INCOMPLETE - ERROR IN PARM, LENGTH OR ?  *
*                                                                     *
***********************************************************************
         SPACE 1                                                 90140
***********************************************************************
*                                                                     *
*        THE COLOR TABLE CONSISTS OF 144 BYTES:                       *
*              64 * XY  BYTES - HIGHLIGHT/COLOR REPLACEMENT FOR ATTR  *
*               8 * XY  BYTES - HIGHLIGHT/COLOR FOR COLOR ORDERS      *
*               8 BYTES SPARE                                         *
*              64 * XY  BYTES - OUTLINE/BACKGD COLOR FOR ATTRIBUTES   *
*                                                                     *
*        X : 0 OR F NO CHANGE; 1/9 2/A 4/C EXTENDED HIGH-LIGHTING     *
*        Y : 0-7  3270 COLOR SELECTION                                *
*                                                                     *
***********************************************************************
         EJECT ,
         PRINT &PRTSOR       SAVE A TREE
CONV3270 SAVEM ZERO12,BASE=(R12),PARM=R1,ENTRY=CONT3270,ENTNO=FLAGS
ROUT     EQU   2             OUTPUT BUFFER POINTER
RBUF     EQU   3             CURRENT BUFFER POSITION
RIN      EQU   4             INPUT BUFFER POINTER
RLEN     EQU   5             INPUT SCAN STOP ADDRESS
REV      EQU   6             EVEN WORK REGISTER
ROD      EQU   7             ODD  WORK REGISTER
RON      EQU   8             CONSTANT OF ONE
RTHREE   EQU   9             CONSTANT OF THREE
RFOUR    EQU   10            CONSTANT OF FOUR
RWK      EQU   11            WORK REGISTER
RET      EQU   14            SUBROUTINE CALL RETURN ADDRESS
COMPREST LM    R2,R9,0(R1)   LOAD PASSED PARAMETERS              90140
         USING INPUT,RIN     DECLARE 'INPUT' MAPPING             90140
         USING OUTPUT,ROUT   AND 'OUTPUT' MAPPING                90140
         ICM   R3,15,0(R3)   GET MAXIMUM OUTPUT LENGTH           90141
         BNP   BADEXIT       MUST HAVE ROOM FOR SOMETHING        90141
         ICM   R5,15,0(R5)   GET INPUT LENGTH                    93330
         BNP   BADEXIT       NEED SOMETHING TO DO                90141
         CR    R3,R5          OUTPUT MUST BE GREATER OR EQUAL    93330
         BL    BADEXIT       SHOULD BE LARGER IF COLOR SUBSTITUTION
         MVC   OPTIONS(OPTIONL),0(R7)   COPY OPTION STRING       93326
         MVI   CURPOS,X'FF'  DISABLE CURSOR POSITION ORDER       93331
         AR    R3,R2         MAKE LAST OUTPUT BYTE +1            90154
         TM    OPFUN,OPYCCO  CCW CODE IN O/P BUFFER ?            93331
         BZ    FULLOUT       NO                                  93331
         BCTR  ROUT,0        BACKSPACE TO CCW CODE               93331
         LA    R3,1(,R3)     INCREASE O/P LENGTH ACCORDINGLY     93331
FULLOUT  ST    R3,MAXOUT     SAVE IT                             90140
         ST    R8,@COLTAB    SAVE COLOR TABLE ADDRESS            90140
         SLL   R8,1          GET RID OF SIGN BIT                 90140
         LTR   R8,R8         ANY COLOR TABLE ?                   90140
         BZ    CANCOL        NO; CANCEL COLOR SUBSTITUTION       90140
         TM    OPCOL,OPSEVEN  7-COLOR SUPPORT ?                  90140
         BO    HONCOL        YES; HONOR REQUEST                  90140
         TM    OPHIGH,OPSEVEN  EXTENDED HIGH-LIGHTING ?          90140
         BO    HONCOL        YES; ALLOW COLOR TO HIGH-LIGHTING   90140
CANCOL   OI    OPFUN,OPNCOL  BYPASS SUBSTITUTION                 90140
HONCOL   SLR   R0,R0                                             93326
         ICM   R0,3,2(R6)    LOAD SCREEN SIZE                    93326
         BNP   BADEXIT       TOO BAD                             90141
         ST    R0,SCRSIZ     SAVE IT                             93326
         ICM   R0,3,0(R6)    LOAD SCREEN WIDTH                   93326
         BNZ   *+8           OK?                                 93326
         LA    R0,80         SET DEFAULT                         93326
         ST    R0,SCRWID     SAVE IT                             93326
         LTR   R9,R9         ANY TERMINAL TYPE PASSED ?          93331
         BZ    SETDEFTY                                          93331
         MVC   TERMTYPE,0(R9)  COPY USER'S TYPE                  93331
         CLI   TERMTYPE,0    TERMINAL TYPE PASSED ?              93326
         BNE   *+8            YES                                93326
SETDEFTY MVI   TERMTYPE,TRMTTV92  DEFAULT: TELEVIDEO 925         93326
*                     BUILD CONSTANTS                            90140
         SLR   RBUF,RBUF     NO DATA YET                         93327
         LA    RON,1         PLACE CONSTANT ONE INTO A REGISTER  90140
         LA    RTHREE,3      CONSTANT THREE                      90140
         LA    RFOUR,4       CONSTANT FOUR                       90140
         AR    RLEN,RIN      GET LAST INPUT ADDRESS + 1          90140
         SPACE 1                                                 93336
*   SPLIT TO TTY => 3270 CONVERSION                              93336
*                                                                90140
         TM    FLAGS,FGTO70  BUFFER FROM TTY ?                   93336
         BNZ   MAKE3270      YES                                 93336
         TM    OPFUN,OPNCMP  BYPASS COMPRESSION ?                90140
         BNZ   COMPNCMP      YES; JUST COPY AS IS                90140
         CKIN  6,ERR=COMPNCMP   SEQUENCE AT LEAST 6 BYTES LONG ? 90154
         TM    OPFUN,OPYESC  POSSIBLE BTAM PREFIX ?              90140
         BZ    TESTCCW       NO                                  90140
         MVC   OUTPUT(2),INPUT  COPY A BIT MORE                  90140
         LA    ROD,2         PRESET FOR STX/ESC                  90140
         CLI   INPUT,STX     STX ?                               90140
         BE    MOVESCAP                                          90140
         BCTR  ROD,0         PRESET FOR ESCAPE ONLY              90140
         CLI   INPUT,ESC     IS IT ESCAPE ?                      90140
         BNE   COPYNCMP      NO; BYPASS - USER LIED              90140
MOVESCAP AR    RIN,ROD       INCREMENT                           90140
         AR    ROUT,ROD      IN AND OUT                          90140
TESTCCW  TM    OPFUN,OPYESC+OPYCCW+OPYCCO  LEADING CCW CODE ?    93331
         BZ    TESTWCC       NO                                  90140
         LR    RWK,RIN       LOOK FOR CCW CODE IN INPUT BUFFER   93331
         TM    OPFUN,OPYCCO  BUT IS IT REALLY ?                  93331
         BZ    TESTCCWI      YES                                 93331
         LR    RWK,ROUT      LOOK FOR CCW CODE IN OUTPUT         93333
TESTCCWI CLI   0(RWK),WSF     WRITE STRUCTURED FIELD ?           93331
         BE    COMPNCMP      YES; DON'T HANDLE YET               90140
         CLI   0(RWK),EAU     ERASE ALL UNPROTECTED ?            93331
         BE    COMPNCMP      YES; JUST COPY (SHOULD BE IT, ANYWAY)
         CLI   0(RWK),X'F7'   BSC COPY ?                         93331
         BE    COMPNCMP      YES; JUST COPY CCC AND ADDRESS      90141
         CLI   0(RWK),EWA     ERASE/WRITE ALTERNATE ?            93331
         BE    COPYCCWC      YES; PROCEED                        93330
         CLI   0(RWK),EWR     ERASE/WRITE ?                      93331
         BE    COPYCCWC      YES                                 93330
         CLI   0(RWK),WRT     PLAIN WRITE ?                      93331
         BE    COPYCCWF      YES; HONOR                          90154
COPYNCMP OI    CCODE+L'CCODE-1,4  SET WARNING - NO COMPRESS      93326
         B     COMPNCMP      SKIP COMPRESSION                    90154
         SPACE 1                                                 90154
COPYCCWF TTYSEQ TDOFSCMD,TERMTYPE  GET FULL-SCREEN MODE          93333
         XSEG  (R15),(R0)    STICK INTO BUFFER                   93333
         B     COPYCCW                                           93333
COPYCCWC TTYSEQ TDOFSCMD,TERMTYPE  GET FULL-SCREEN MODE          93333
         XSEG  (R15),(R0)    STICK INTO BUFFER                   93333
         TTYSEQ TDOCLS,TERMTYPE  GET CLEAR SCREEN SEQUENCE       93330
         XSEG  (R15),(R0)    TO SCREEN BUFFER                    93330
         TM    OPFUN,OPYCCO  CCW IN OUTPUT ?                     93331
         BNZ   TESTWCC       YES; NO NEED TO INCREMENT           93331
COPYCCW  AR    RIN,RON         AND INPUT                         90140
TESTWCC  TM    OPFUN,OPYESC+OPYCCW+OPYWCC  WCC/PCC PRESENT ?     90140
         BZ    TESTSBA       NO                                  90140
         CLI   INPUT,SBA     USER LYING AGAIN ?                  90140
         BE    TESTSBA       YES; NO WCC ?                       90140
         AR    RIN,RON       UP INPUT ADDRESS
* ADD KEYBOARD RESTORE HERE                                      93330
TESTSBA  CLI   INPUT,SBA     VALIDITY CHECK FOR ONLY REQUIRED START
         BE    COMPSBA       NEED INITIAL SBA FOR BUFFER LOGIC
         SPACE 1                                                 90140
*---------------------------------------------------------------------*
*                                                                     *
*        PT, PC71 AND TRANSPARENT TEXT ALL ARE COPIED AS-IS.          *
*           NO OTHER PROCESSING IS DONE WHEN THESE ARE FOUND.         *
*                                                                     *
*---------------------------------------------------------------------*
COMPPT   DS    0H            PROGRAM TAB (NO POSITION AVAILABLE) 90140
COMPPC71 DS    0H            VARIABLE LENGTH ORDERS              90140
COMPASIS DS    0H            COPY BUFFER AS-IS                   90140
COMPNCMP LR    R8,RIN        COPY INPUT ADDRESS                  90140
         LR    R0,ROUT       COPY OUTPUT ADDRESS
         SR    RLEN,RIN      GET LENGTH TO MOVE
         LR    R9,RLEN
         LR    R1,RLEN
         AR    ROUT,RLEN     SET POSITION AFTER MOVE
         C     ROUT,MAXOUT   WILL IT FIT ?                       90155
         BH    BADEXIT       NO; NO COPY                         90155
         MVCL  R0,R8         COPY AS IS
     B FAILEXIT ***USELESS OUTPUT?***                            93326
         B     EXIT          AND RETURN
         EJECT ,
*---------------------------------------------------------------------*
*                                                                     *
*        DECODE SBA, SET BUFFER ADDRESS; COPY IF LAST OR ONLY SBA.    *
*                                                                     *
*---------------------------------------------------------------------*
COMPSBA  CKIN  6,ERR=COMPSBA2  LONG ENOUGH FOR TWO SBA SEQUENCES ?
         CLC   =X'115D7F1100',INPUT  IBM 7171 TRANSPARENT MODE ? 90140
         BE    COMPASIS      YES; COPY BUFFER AS-IS              90140
COMPSBA2 CKIN  3             ENOUGH FOR FULL SEQUENCE ?          90154
         BALS  RET,GETSBA    GET CURRENT BUFFER ADDRESS FROM SBA ORDER
         AR    RIN,RTHREE    POINT TO NEXT INPUT BYTE
         SPACE 2
LOOPSBA  CR    RIN,RLEN      END OF INPUT ?
         BNL   LEAVESBA      YES - MAKE TRAILER
         CLI   INPUT,SBA     IS IT SBA FOLLOWED BY ANOTHER SBA ?
         BE    COMPSBA       YES - CONSOLIDATE INTO SINGLE SBA
         SPACE 1
LEAVESBA CKOUT 3             ENOUGH ROOM IN OUTPUT ?             90154
         BALS  RET,SETSBA    PLACE NEW SBA ORDER INTO THE OUTPUT BUFFER
         SPACE 1
*---------------------------------------------------------------------*
*                                                                     *
*        LOOP THROUGH INPUT BYTE BY BYTE; BRANCH AND PROCESS BY TYPE  *
*                                                                     *
*---------------------------------------------------------------------*
LOOPDATA CR    RIN,RLEN      END OF INPUT ?
         BNL   LOOPDONE      YES - GET OUT                       93331
 STM R0,R15,BIGFOOT     *****DEBUG*****                          93330
         CKOUT 1             ROOM FOR ONE MORE ?                 90154
         C     RBUF,SCRSIZ   DID BUFFER WRAP ?                   93330
         BL    LOOPDATB      NO                                  93330
         S     RBUF,SCRSIZ   ADJUST                              93330
LOOPDATB LR    RET,R2        SAVE OVER TRT
         SLR   R2,R2         CLEAR FOR TRT'S IC
         TRT   0(1,RIN),ORDERS  GET BRANCH OFFSET FOR THIS BYTE
         SWAPR R2,RET        RESTORE R2                          90140
OFFBASE  B     *(RET)        BRANCH BY TYPE
         GOTO  TBLK          CHECK FOR BLANK
         GOTO  DATA          UNPRINTABLE CHARACTER
         GOTO  BAD           UNPROCESSABLE CHARACTER
         GOTO  PT            PROGRAM TAB
         GOTO  SBA           SET BUFFER ADDRESS
         GOTO  IC            SET CURSOR
         GOTO  EUA           ERASE UNPROTECTED
         GOTO  SF            START FIELD
         GOTO  RTA           REPEAT TO ADDRESS
         GOTO  GE            GRAPHIC ESCAPE                      90140
         GOTO  SA            SET ATTRIBUTE                       90140
         GOTO  SFE           START FIELD EXTENDED                90140
         GOTO  MF            MODIFY FIELD                        90140
         GOTO  PC71          IBM 7171 SPECIAL ORDER              90140
         GOTO  TEXT          (TN) TEXT CHARACTERS                91100
         SPACE 1                                                 93331
LOOPDONE ICM   RBUF,15,CURPOS  NEED TO APPEND CURSOR POSITION ?  93331
         BM    EXIT                                              93331
         BALS  R14,SETSBA    MAKE TRAILING BUFFER ADDRESS        93331
         B     EXIT          AND GET OUT                         93331
         SPACE 1
*---------------------------------------------------------------------*
*                                                                     *
*        INPUT IS BLANK. TREAT AS DATA, UNLESS FAKE SF - IF BY 3RD    *
*           LINE NO PROT. SF, MAKE ONE TO SHORTEN INPUT BUFFER        *
*                                                                     *
*---------------------------------------------------------------------*
COMPTBLK CLI   INPUT,BLANK   BLANK OR DATA ?
         BNE   COMPDATA      NOT BLANK; PROCESS DATA BYTE
         TM    OPFUN,OPFSF   FALSE SF TO BE INSERTED ?
         BZ    COMPDATA      NO; PROPAGATE BLANK
         CH    RBUF,FSFAD    REACHED THIRD LINE YET ?
         BL    COMPDATA      NO
         CKOUT 2,ERR=COMPDATA  SKIP IF NO ROOM FOR SF            90154
*TEMP*   MVI   OUTPUT,SF     YES - MAKE SF FIELD INSTEAD OF BLANK
*TEMP*   MVI   OUTPUT+1,X'60'  MAKE PLAIN OLD PROTECTED FIELD
         MVC   OUTPUT(2),=X'2750'                                93333
         BCTR  RIN,0         FAKE FOR NEXT INCREMENT
         NI    OPFUN,255-OPFSF   TURN FAKE SF OFF
         MVI   WORK0,X'60'   SET PLAIN PROTECTED FIELD           93334
         B     COMPSFNP      TEST FOR SF COLOR CONVERSION
         SPACE 1
*---------------------------------------------------------------------*
*                                                                     *
*        SF ORDER FOUND - COPY AS IS; IF COLOR SUBSTITUTION APPLIES,  *
*           CONVERT TO SFE ORDER AND MAKE COLOR ATTRIBUTE.            *
*                                                                     *
*---------------------------------------------------------------------*
COMPSF   CKOUT 2             NEED ROOM FOR TWO BYTES             90154
         TM    OPFUN,OPFSF   FAKE SF ACTIVATED ?
         BZ    COMPSFNS      NO
         CH    RBUF,FSFLO    ON SECOND LINE ?                    90140
         BL    COMPSFNS      NO
         CH    RBUF,FSFAD    OR PAST IT ?
         BH    COMPSFNS
         NI    OPFUN,255-OPFSF KLUDGE FOR FULL-SCREEN I/O
COMPSFNS DS    0H                                                93334
COMPSFNT MVC   WORK0(1),INPUT+1  SET ATTRIBUTE BYTE              93334
COMPSFNP MVC   OUTPUT(2),=X'2750'  SET O/P FIELD **TEMP**        93333
         TM    WORK0,X'20'   PROTECTED FIELD ?  **TEMP**         93334
         BNZ   *+8           YES; USE DEFAULT    **TEMP**        93333
         MVI   OUTPUT+1,C''''  RESET PROTECT     **TEMP**        93333
         LA    ROUT,2(,ROUT)   UP OUTPUT POINTER **TEMP**        93333
         TTYSEQ TDOTEXT,TERMTYPE  GET STANDARD TEXT FIELD        93326
         XSEG  (R15),(R0)    PUT IN BUFFER                       93326
         SH    ROUT,=H'2'    FAKE                                93326
         MVI   OUTPUT+1,0    RESET FIELD ATTRIBUT**TEMP**ES      93333
         TM    WORK0,X'0C'  NON-DISPLAY ?      **TEMP**          93333
         BNO   *+8           NO                  **TEMP**        93333
         OI    OUTPUT+1,1    SET NON-DISPLAY     **TEMP**        93333
         TM    WORK0,X'08'  INTENSE ?          **TEMP**          93333
         BZ    *+8           NO                  **TEMP**        93333
         OI    OUTPUT+1,4    YES; FAKE REVERSE   **TEMP**        93333
         TR    OUTPUT+1(1),=C'0123456789:;<=>?'  **TEMP**MAKE PRINTABLE
     B COMPSFNX                                                  93326
         BALS  RET,SUBATTR   GET MATCHING COLOR/HIGHLIGHT        90140
         CLI   WORK0+1,0     FORGET IT ?                         90140
         BE    COMPSFNX      YES; NO REPLACEMENT                 90140
         CKOUT 6             NEED ROOM FOR SIX BYTES             90154
         MVC   OUTPUT(3),=X'2902C0'  SFE/ATTR                    90140
         MVC   OUTPUT+3(3),WORK0  COPY ATTRIBUTE/COL-HILITE/OPT  90140
         AR    ROUT,RFOUR    ADVANCE OUTPUT POINTER              87172
COMPSFNX LA    ROD,2         SKIP TWO INPUT AND OUTPUT BYTES     90140
         AR    RBUF,RON      BUT BUFFER ONLY INCREASES BY ONE BYTE
         B     COMROD        INCREMENT AND CONTINUE              90140
         SPACE 1                                                 90140
*---------------------------------------------------------------------*
*                                                                     *
*        GRAPHIC ESCAPE - COPY NEXT BYTE AS IS. BUFFER INCREMENTS 1   *
*                                                                     *
*---------------------------------------------------------------------*
COMPGE   CKOUT 2             NEED TWO BYTES OUT                  90154
         CKIN  2             MAKE SURE WE REALLY HAVE TWO BYTES  90155
         MVC   OUTPUT(2),INPUT  COPY GRAPHIC ESCAPE AND SUBJECT  90140
         B     COMPSFNX      OTHERWISE LIKE SF                   90140
         SPACE 1                                                 90140
*---------------------------------------------------------------------*
*                                                                     *
*        TEXT CHARACTER - CONVERT TO GRAPHIC ESCAPE SEQUENCE          *
*                                                                     *
*---------------------------------------------------------------------*
COMPTEXT TM    OPMISC,OPFGE  USE GRAPHICS ESCAPE ?               91100
         BZ    COMPDATA      NO; TREAT AS DATA                   91100
         CKOUT 2             NEED TWO BYTES OUT                  91100
         MVI   OUTPUT,GE     MAKE GRAPHIC ESCAPE                 91100
         TR    INPUT,TXTCHARS  MAKE TEXT KEYBOARD CODE           91100
         MVC   OUTPUT+1(1),INPUT  COPY ESCAPED EUQIVALENT        91100
         BCTR  RIN,0         FAKE FOR NEXT INCREMENT             91100
         B     COMPSFNX      OTHERWISE LIKE SF                   90140
         EJECT ,                                                 90140
*---------------------------------------------------------------------*
*                                                                     *
*        MODIFY FIELD - COPY; REPLACE COLOR ORDERS IF SUBSTITUTING.   *
*                                                                     *
*---------------------------------------------------------------------*
COMPMF   DS    0H            TREAT AS SFE                        90140
         SPACE 1
*---------------------------------------------------------------------*
*                                                                     *
*        SF EXTENDED - COPY; REPLACE COLOR ORDERS IF SUBSTITUTING.    *
*                                                                     *
*---------------------------------------------------------------------*
COMPSFE  CKIN  2             LONG ENOUGH FOR LENGTH BYTE ?       90155
         SLR   ROD,ROD
         IC    ROD,1+INPUT  GET LENGTH BYTE
         LR    REV,ROD       SAVE REPEAT COUNT                   87172
         LA    ROD,1(ROD,ROD)  EXECUTE LENGTH OF ORDER
         LA    RET,1(ROD,RIN)  GET END OF INPUT                  90155
         CR    RET,RLEN      REALLY ALL THERE ?                  90155
         BH    COMPBAD       NO                                  90155
         LA    RET,1(ROD,ROUT)  GET END OF SEQUENCE              90154
         C     RET,MAXOUT    WILL IT FIT INTO OUTPUT BUFFER ?    90154
         BH    BADEXIT       NO; FAIL                            90154
         EX    ROD,INMVCOUT   MOVE THE ORDER
         TM    OPFUN,OPNCOL  SKIP CONVERSION ?
         BNZ   COMPSFEX      YES                                 87172
         LTR   REV,REV       ANY AT ALL ?                        87172
         BNP   COMPSFEC      NO; TACK ON A COLOR ORDER           87172
         LR    ROD,ROUT      COPY STARTING ADDRESS               87172
COMPSFEL CLI   2(ROD),X'42'  COLOR ORDER ?                       87172
         BE    COMPSFET      YES; JUST TRANSLATE                 87172
         LA    ROD,2(,ROD)                                       87172
         BCT   REV,COMPSFEL  NO COLOR ORDER                      87172
         IC    REV,1+INPUT   GET COUNT BACK                      87172
         LR    ROD,ROUT      GET START AGAIN                     87172
COMPSFEN CLI   2(ROD),X'C0'  ATTRIBUTE ?                         87172
         BE    COMPSFEA      YES; USE IT FOR COLOR               87172
         LA    ROD,2(,ROD)                                       87172
         BCT   REV,COMPSFEN  TRY AGAIN                           87172
         MVI   WORK0,X'20'   SET DEFAULT ATTRIBUTE               90140
         B     COMPSFEB      JOIN ADD-ON CODE                    90140
COMPSFEA MVC   WORK0(1),3(ROD)  SET ATTRIBUTE TO BE DONE         90140
COMPSFEB BALS  RET,SUBATTR   GET COLOR REPLACEMENT FOR ATTRIBUTE 90140
         CLI   WORK0+1,0     ANY ?                               90140
         BE    COMPSFEX      NO, TERMINATE SEQUENCE              90140
         IC    REV,1+INPUT   REGAIN INPUT COUNT                  87172
         LR    ROD,REV       COPY                                87172
         SLA   ROD,1         DOUBLE                              90140
         LA    ROD,2(ROD,ROUT) NEXT OUTPUT BUFFER POSITION       90140
         C     ROD,MAXOUT    WILL THIS SEQUENCE FIT ?            90154
         BH    BADEXIT       NO; FAIL                            90154
         MVC   0(2,ROD),WORK0+1  COPY NEW FUNCTION               90140
         CLI   WORK0+1,X'41'  HIGH-LIGHTING ?                    90140
         BNE   COMPSFED      NO; EXTEND SEQUENCE                 90140
         LR    RET,REV       COPY COUNT                          90140
         LR    ROD,ROUT      COPY OUTPUT START                   90140
COMPSFEC CLI   2(ROD),X'41'  USER SPECIFIED HIGH-LIGHTING ?      90140
         BE    COMPSFEX      YES; LEAVE AS-IS                    90140
         LA    ROD,2(,ROD)   BUMP                                90140
         BCT   RET,COMPSFEC                                      90140
COMPSFED AR    REV,RON       UP SEQUENCE COUNT                   90140
         STC   REV,1+OUTPUT  SET NEW SEQUENCE COUNT              87172
         B     COMPSFEX      COMMON                              87172
COMPSFET MVC   WORK0(1),3(ROD)  COPY COLOR SELECTION             90140
         BALS  RET,SUBHUE    CHECK FOR REPLACEMENT               90140
         CLI   WORK0+1,0     NONE ?                              90140
         BE    COMPSFEX      NONE. JUST INCREMENT BUFFERS        90140
         CLI   WORK0+1,X'42'  COLOR REPLACEMENT ?                90140
         BNE   COMPSFEU      NO; USE UNLESS USER HIGH-LIGHTING   90140
         MVC   3(1,ROD),WORK0+2  SET NEW COLOR                   90140
         B     COMPSFEX      GET OUT                             90140
COMPSFEU IC    REV,1+INPUT   GET COUNT BACK                      90140
         LA    RET,2+OUTPUT  GET TEXT                            90140
COMPSFEV CLI   0(RET),X'41'  HIGH-LIGHTING REQUEST ?             90140
         BE    COMPSFEX      YES; NO REPLACEMENT                 90140
         LA    RET,2(,RET)                                       90140
         BCT   REV,COMPSFEV  TRY AGAIN                           90140
         MVC   2(2,ROD),WORK0+1  MAKE NEW SEQUENCE               90140
COMPSFEX SLR   ROD,ROD       CLEAR                               90140
         IC    ROD,1+INPUT   GET INPUT LENGTH                    90140
         SLA   ROD,1         DOUBLE                              90140
         LA    RIN,2(ROD,RIN)  ADJUST INPUT                      90140
         IC    ROD,1+OUTPUT  GET NEW OUTPUT LENGTH               90140
         SLA   ROD,1         DOUBLE                              90140
         LA    ROUT,2(ROD,ROUT)  FIX                             90140
         AR    RBUF,RON      INCREMENT BUFFER BY ATTRIBUTE BYTE
         B     LOOPDATA      PROCESS MORE INPUT
         SPACE 1
*---------------------------------------------------------------------*
*                                                                     *
*        INSERT CURSOR - COPY AS IS (NO DUPLICATE CHECK DONE)         *
*                                                                     *
*---------------------------------------------------------------------*
COMPIC   ST    RBUF,CURPOS   SAVE CURSOR POSITION                93331
         AR    RIN,RON       INCREMENT INPUT                     93331
         B     LOOPDATA      ADDRESS IS UNCHANGED                93331
         SPACE 1
*---------------------------------------------------------------------*
*                                                                     *
*        SET ATTRIBUTE - COPY; SUBSTITUTE COLOR AS APPLICABLE         *
*                                                                     *
*---------------------------------------------------------------------*
COMPSA   CKOUT 3             ROOM FOR THREE BYTES ?              90154
         CKIN  3             ALL THERE ?                         90155
         MVC   OUTPUT(3),INPUT  MOVE SA ORDER
         LR    ROD,RTHREE    SET INCREMENT
         CLI   1+OUTPUT,X'42'  COLOR ORDER ?                     87326
         BNE   COMROD        NO; LEAVE AS IS                     87326
         MVC   WORK0(1),OUTPUT+2  PASS COLOR ARGUMENT            90140
         BALS  RET,SUBHUE    CHECK AND SET COLOR REPLACEMENT     90140
         CLI   WORK0+1,0     SKIP SUBSTITUTION ?                 90140
         BE    COMROD        YES; JUST BUMP                      90140
         MVC   OUTPUT+1(2),WORK0+1  MOVE COLOR/HILITE AND ATTRIBUTE
         B     COMROD        SKIP INPUT AND OUTPUT
         SPACE 1
*---------------------------------------------------------------------*
*                                                                     *
*        REPEAT TO ADDRESS - COPY AND SET BUFFER ADDRESS.             *
*                                                                     *
*---------------------------------------------------------------------*
COMPRTA  CKOUT 4             ROOM FOR FOUR BYTES ?               90154
         CKIN  4             ALL THERE ?                         90155
*        MVC   OUTPUT(4),INPUT     COPY RA ORDER
         LR    RWK,RBUF      PRESERVE CURRENT BUFFER ADDRESS     93330
         BALS  RET,GETSBA    SET NEW BUFFER ADDRESS INTO RBUF
         CLI   INPUT+3,GE    ESCAPE ?                            93331
         BNE   COMPRTA1                                          93331
         AR    RIN,RON       ADVANCE TO DATA BYTE                93331
COMPRTA1 SR    RWK,RBUF      CLEAR SEQUENCE ?                    93330
         BNZ   COMPRTA2      NO                                  93330
         TM    INPUT+3,255-C' '  NULL OR BLANK ?                 93330
         BNZ   COMPRTA2      NO                                  93330
         AR    RIN,RFOUR     BUMP INPUT                          93330
         TTYSEQ TDOCLS,TERMTYPE  CLEAR SCREEN                    93330
         XSEG  (R15),(R0)    PUT ON SCREEN                       93330
         SLR   ROD,ROD                                           93330
         B     COMROD        GO TO COMMON                        93330
COMPRTA2 IC    ROD,INPUT+3   SAVE REPEAT CHARACTER               93330
         AR    RIN,RFOUR                                         93330
         LCR   RWK,RWK       GET COUNT TO PROPAGATE              93330
         LTR   RWK,RWK                                           93330
         BP    *+8                                               93330
         A     RWK,SCRSIZ                                        93330
         LA    RET,0(RWK,ROUT)  GET EVENTUAL END                 93330
         C     RET,MAXOUT    WILL IT FIT?                        93330
         BH    BADEXIT       NO                                  93330
COMPRTA3 STC   ROD,OUTPUT                                        93330
         AR    ROUT,RON                                          93330
         BCT   RWK,COMPRTA3                                      93330
         SLR   ROD,ROD                                           93330
         B     COMROD        GO TO INCREASER
         SPACE 1
*---------------------------------------------------------------------*
*                                                                     *
*        ERASE UNPROTECTED TO ADDRESS - COPY AND SET BUFFER ADDRESS   *
*                                                                     *
*---------------------------------------------------------------------*
COMPEUA  CKOUT 3             ROOM FOR THREE ?                    90154
         CKIN  3             ALL THERE ?                         90155
*        MVC   OUTPUT(3),INPUT   COPY EUA ORDER
         LR    RWK,RBUF      PRESERVE CURRENT BUFFER ADDRESS     93331
         BALS  RET,GETSBA    SET NEW BUFFER ADDRESS INTO RBUF
         SR    RWK,RBUF      CLEAR SEQUENCE ?                    93331
         BNZ   COMPEUA2      NO                                  93331
         AR    RIN,RTHREE    BUMP INPUT                          93331
         TTYSEQ TDOCLS,TERMTYPE  CLEAR SCREEN                    93331
         XSEG  (R15),(R0)    PUT ON SCREEN                       93331
         SLR   ROD,ROD                                           93331
         B     COMROD        GO TO COMMON                        93331
COMPEUA2 LA    ROD,C' '      SAVE BLANK - NULL DOESN'T WORK      93331
         AR    RIN,RTHREE                                        93331
         LCR   RWK,RWK       GET COUNT TO PROPAGATE              93331
         BP    *+8                                               93331
         A     RWK,SCRSIZ                                        93331
         LA    RET,0(RWK,ROUT)  GET EVENTUAL END                 93331
         C     RET,MAXOUT    WILL IT FIT?                        93331
         BH    BADEXIT       NO                                  93331
COMPEUA3 STC   ROD,OUTPUT                                        93331
         AR    ROUT,RON                                          93331
         BCT   RWK,COMPEUA3                                      93331
         SLR   ROD,ROD                                           93331
         B     COMROD        GO TO UP IN/OUT
         SPACE 1
*---------------------------------------------------------------------*
*                                                                     *
*        UNRECOGNIZED CHARACTER - DO I HAVE AN OLD MANUAL OR DID      *
*           THE USER DO SOMETHING NASTY ?  SET WARNING, BUT COPY      *
*                                                                     *
*---------------------------------------------------------------------*
COMPBAD  OI    CCODE+L'CCODE-1,4  SET UNRECOGNIZED ORDER/CHARACTER
         SPACE 1
*---------------------------------------------------------------------*
*                                                                     *
*        DATA - CHECK FOR 5 OR MORE CONSECUTIVE; IF SO, REPLACE BY    *
*           RTA ORDER, ELSE COPY AS-IS.                               *
*                                                                     *
*---------------------------------------------------------------------*
COMPDATA CKOUT 5,ERR=INMVCOUT  COPY SINGLE BYTE IF NOT ENOUGH ROOM
*        CKIN  5,ERR=INMVCOUT  DITTO IF INPUT TOO SHORT          90154
*        CLC   INPUT+1(4),INPUT  FIVE OR MORE REPETITIONS ?
*        BE    MAKERTA       YES - CONVERT TO RTA ORDER
INMVCOUT MVC   OUTPUT,INPUT  ELSE COPY ONE BYTE
         LTR   RBUF,RBUF     BEGINNING OF SCREEN ?               93336
         BNZ   COMPDATI                                          93336
         MVI   WORK0,0       SET UNPROTECTED FIELD               93336
         BCTR  RIN,0         FAKE                                93336
         B     COMPSFNP      BUILD AN INPUT FIELD                93336
COMPDATI AR    RBUF,RON      INCREASE BUFFER POSITION
         LR    ROD,RON       SET INCREMENT FOR IN/OUT
         SPACE 1
*---------------------------------------------------------------------*
*                                                                     *
*        COMMON TARGET - INCREASE INPUT AND OUTPUT POSITIONS BY       *
*           AMOUNT IN 'ROD', THEN SCAN NEXT CHARACTER.                *
*                                                                     *
*---------------------------------------------------------------------*
COMROD   AR    RIN,ROD       INCREASE INPUT ADDRESS
         AR    ROUT,ROD      AND OUTPUT ADDRESS
         B     LOOPDATA      GO TO LOOK FOR MORE INPUT
         SPACE 1
MAKERTA  LA    RET,5+INPUT   ENOUGH SPACE IN BUFFER FOR BUNCH ?  90147
         CR    RET,RLEN      CHECK AGAINST END                   90147
         BH    INMVCOUT      HIGH; JUST MOVE ONE                 90147
         MVC   OUTPUT+3(1),INPUT  SAVE THE DATA BYTE SOMEWHERE   90147
         IC    RET,INPUT     LOAD FOR FASTER COMPARES            90147
         SPACE 1
MAKERTAL AR    RIN,RON       GET NEXT INPUT BYTE
         AR    RBUF,RON      SET BUFFER ADDRESS UP TO PREVIOUS ONE
         CR    RIN,RLEN      END OF BUFFER ?
         BNL   MAKERTAX      YES - TERMINATE
         CLM   RET,1,INPUT   SAME CHARACTER AGAIN ?              90147
         BNE   MAKERTAX      NO; FORCE EXPANSION
         TM    OPFUN,OPFSF   FAKE SF TO BE DONE ?
         BZ    MAKERTAL      NO; LOOP SOME MORE
         CH    RBUF,FSFAD    REACHED THIRD LINE YET ?
         BL    MAKERTAL      NO
         CLI   INPUT,BLANK   DOING BLANKS ?
         BNE   MAKERTAL      NO, CONTINUE
         SPACE 1
MAKERTAX MVI   OUTPUT,RTA    PLACE THE RTA ORDER
         BALS  RET,SETBA     BUILD THE BUFFER ADDRESS
         AR    ROUT,RON      CORRECT THE OUTPUT ADDRESS
         B     LOOPDATA      PROCESS NEXT INPUT BYTE
         SPACE 1
***********************************************************************
*                                                                     *
*        EXITS - SET RETURN CODE, OUTPUT BUFFER LENGTH AND RETURN     *
*                                                                     *
***********************************************************************
FAILEXIT MVI   CCODE+L'CCODE-1,4  SET FOR RETURN CODE 12         93326
BADEXIT  OI    CCODE+L'CCODE-1,8  SET ERROR RETURN               93326
EXIT     CLI   CCODE+L'CCODE-1,0  ANY ERROR ? *****DEBUG*****    93327
         BE    EXITGOOD                       *****DEBUG*****    93327
         EX    0,*                            *****DEBUG*****    93327
EXITGOOD L     RIN,SAVE13    RESTORE CALLER'S SAVE AREA          93326
         L     RIN,SAVE1-SAVE(,RIN)  RESTORE CALLER'S PARM ADDRESS
         S     ROUT,0(,RIN)  GET CORRECT BUFFER LENGTH (OR 0)    90140
         BNM   *+6           OK ?                                90140
         SLR   ROUT,ROUT     NOT LESS THAN ZERO                  90140
         L     RIN,4(,RIN)   LOAD ADDRESS OF LENGTH WORD         90140
         ST    ROUT,0(,RIN)  RETURN TO USER                      90140
         LH    R9,CCODE      LOAD RETURN CODE                    93326
         ENDM  RC=(R9)       RETURN TO CALLER                    93326
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*        SUBROUTINE TO GET BUFFER ADDRESS FROM INPUT SBA.             *
*                                                                     *
*---------------------------------------------------------------------*
GETSBA   SLR   RBUF,RBUF     CLEAR THE NEW BUFFER ADDRESS
         TM    OPADD,OPA16   16-BIT ADDRESSING MODE ?            90140
         BNZ   GETSBA16      YES; ZONES ARE DATA                 90140
         TM    INPUT+1,X'C0'  12 OR 14 BIT ADDRESS ?             87154
         BNZ   GETSBA12      12-BIT                              87154
GETSBA16 ICM   RBUF,3,INPUT+1  GET 14-BIT ADDRESS                87154
         BR    RET           RETURN                              87154
GETSBA12 LR    ROD,RBUF      CLEAR THE WORKING REGISTER
         IC    RBUF,INPUT+1  GET HIGH BYTE OF BUFFER ADDRESS
         IC    ROD,INPUT+2   AND LOW BYTE
         N     RBUF,=F'63'   STRIP HIGH BITS OFF                 93326
         N     ROD,=F'63'              BOTH BYTES                93326
         SLL   RBUF,6        SHIFT HIGH BYTE *64
         AR    RBUF,ROD      SET NEW BUFFER ADDRESS
         BR    RET           RETURN TO CALLER
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*        SUBROUTINE TO BUILD SBA SEQUENCE (SETSBA) OR BUILD ADDRESS   *
*           BYTES FROM BUFFER OFFSET (SETBA)                          *
*                                                                     *
*---------------------------------------------------------------------*
SETSBA   ST    R14,SAVER14                                       93326
         TTYSEQ TDOPTION,TERMTYPE  GET THE OPTIONS               93326
         LA    RWK,255        MAKE MASK                          93330
         SRL   R0,16         PUT BYTE 2 ON BOTTOM                93326
         NR    RWK,R0         GET THE TAB OPTION                 93330
         BZ    FAILEXIT      NONE ?  ERROR                       93326
         CLM   RWK,1,=AL1(PROCMAX)  VALID ?                      93330
         BH    RESTR14       NO                                  93326
         SLL   RWK,2          CONVERT TERMINAL INDEX TO OFFSET   93330
         L     RWK,PROCOFF-4(RWK)  GET ROUTINE ADDRESS           93330
         B     FOXLOOP       START PROCESSING                    93326
PROCOFF  DC    A(BYTEMODE,CHARMODE,HPMODE)                       93326
PROCMAX  EQU   (*-PROCOFF)/4                                     93326
         SPACE 1                                                 93326
FOXLOOP  TTYSEQ TDOADR1,TERMTYPE,ERR=RESTR14 GET START OF ADDRESS
         XSEG  (R15),(0)     WRITE HEADER SEQUENCE               93326
         LR    ROD,RBUF      COPY CURRENT BUFFER POSITION        93326
         SLR   REV,REV       CLEAR FOR DIVIDE                    93326
         D     REV,SCRWID    GET COLUMN IN REV, LINE IN ROD      93326
         BR    RWK            GO TO PROCESSOR                    93330
BYTEMODE LA    R1,TTYADR(ROD)  GET VALUE FOR CURSOR              93326
         XSEG  (1),1         MAKE CURSOR ADDRESS                 93326
*N/A     TTYSEQ TDOADR2,TERMTYPE,ERR=RESTR14                     93326
*N/A     XSEG  (R15),(0)     WRITE TRAILER                       93326
         LA    R1,TTYADR(REV)  MAKE COLUMN VALUE                 93326
         XSEG  (1),1         APPEND IT                           93326
         B     COMMMODE                                          93326
CHARMODE AR    R15,RON       HEWLETT-PACKARD - REL. TO 0         93326
HPMODE   CVD   R15,DB     MAKE PACKED                            93326
         UNPK  DB(3),DB+6(2)  UNPACK                             93326
         OI    DB+2,X'F0'   MAKE GOOD ZONE                       93326
         LA    R14,2         SET MAX LENGTH-1                    93326
         LA    R1,DB      DECIMAL OUTPUT FIELD                   93326
CHARMODL CLI   0(R1),C'0'    LEADING ZERO ?                      93326
         BNE   CHARMODY      NO                                  93326
         LA    R1,1(,R1)     TRY NEXT BYTE                       93326
         BCT   R14,CHARMODL                                      93326
CHARMODY LA    R14,1(,R14)   GET TRUE LENGTH BACK                93326
         XSEG  (1),(R14)     STASH IN RTB                        93326
COMMMODE TTYSEQ TDOADR3,TERMTYPE,ERR=RESTR14 GET ENDING SEQUENCE 93326
         XSEG  (R15),(0)     WRITE TRAILER                       93326
FOXLEND  DS    0H            MAKE TRAILING CR                    93326
RESTR14  L     R14,SAVER14   GET RETURN ADDRESS                  93326
         BR    R14                                               93326
         SPACE 1
SETBA    LR    ROD,RBUF      BUILD A BUFFER ADDRESS SEQUENCE
         SLR   REV,REV       CLEAR EVEN REGISTER FOR DIVIDE
         D     REV,SCRSIZ    ONLY REMAINDER IN REV COUNTS        93326
         LR    RBUF,REV      UPDATE THE BUFFER ADDRESS           90147
         TM    OPADD,OPA16   16-BIT ADDRESSING MODE ?            90140
         BNZ   SETBA16       YES; ZONES ARE DATA                 90140
         CH    REV,=H'4096'  DOES 12-BIT ADDRESS SUFFICE ?       87154
         BL    SETBA12       YES                                 87154
SETBA16  STCM  REV,3,OUTPUT+1  NO; STASH AS IS                   87154
         B     SETBACOM                                          87154
SETBA12  SRDL  REV,6         PLACE HIGH BUFFER BYTE IN REV
         SRL   ROD,26                  AND LOW BYTE INTO ROD
         STC   REV,OUTPUT+1  PLACE INTO OUTPUT BUFFER
         STC   ROD,OUTPUT+2
         TR    OUTPUT+1(2),EBCDIC      CONVERT TO CORRECT EBCDIC
SETBACOM AR    ROUT,RTHREE   SET NEW OUTPUT LOCATION
         BR    RET           RETURN TO CALLER
         SPACE 2                                                 90140
*---------------------------------------------------------------------*
*                                                                     *
*    SUBROUTINE TO MOVE TEXT TO OUTPUT                                *
*          ADDRESS IN R15, LENGTH IN R0, RETURN VIA R14               *
*                                                                     *
*---------------------------------------------------------------------*
XSEG     ST    R1,24(,R13)  SAVE A BIT                           93326
         LTR   R1,R0        DON'T TRUST ANYONE                   93326
         BNP   XSEGEX                                            93326
         BCTR  R1,0                                              93326
         EX    R1,XSEGMVC                                        93326
         AR    ROUT,R0                                           93326
XSEGEX   L     R1,24(,R13)                                       93326
         BR    R14                                               93326
XSEGMVC  MVC   OUTPUT(0),0(R15)                                  93326
         SPACE 2                                                 84092
*        TABLE OF ADDRESS BYTES FOR SINGLE CHARACTER ADDRESSING  84092
*        MODE (P.E. FOX, IBM 3101, ETC.)  USED IF TABTYP=1       84092
*                                                                84092
TTYADR   DC    C' !"#$%&&''()*+,-./0123456789:;<=>?@ABCDEFG'     84092
         DC    C'HIJKLMNOPQRSTUVWXYZ\_`abcdefghijklmno'       84092
         SPACE 2                                                 90140
*---------------------------------------------------------------------*
*                                                                     *
*        SUBROUTINES TO PROVIDE COLOR SUBSTITUTION.                   *
*          COLOR IN WORK0; RETURNED IN WORK0+2, TYPE IN WORK0+1       *
*                                                                     *
*---------------------------------------------------------------------*
SUBHUE   MVC   WORK0+2(1),WORK0  COPY FOR CONVERSION             90140
         MVI   WORK0+1,0     PRESET FOR NO SUBSTITUTION          90140
         TM    OPFUN,OPNCOL  SUBSTITUTION REQUIRED ?             90140
         BNZR  RET           NO; RETURN WITH 0 FUNCTION          90140
         L     RWK,@COLTAB   GET CONVERSION TABLE                90140
         B     SUBHUECM      JOIN COMMON CODE                    90140
*---------------------------------------------------------------------*
*                                                                     *
*           ATTR IN WORK0; RETURNED IN WORK0+2, TYPE IN WORK0+1       *
*                                                                     *
*---------------------------------------------------------------------*
SUBATTR  MVC   WORK0+2(1),WORK0  COPY FOR CONVERSION             90140
         MVI   WORK0+1,0     PRESET FOR NO SUBSTITUTION          90140
         TM    OPFUN,OPNCOL  SUBSTITUTION REQUIRED ?             90140
         BNZR  RET           NO; RETURN WITH 0 FUNCTION          90140
         L     RWK,@COLTAB   GET CONVERSION TABLE                90140
         NI    WORK0+2,X'3F'   STRIP ZONES                       90140
         TR    WORK0+2(1),0(RWK)  GET FIRST LEVEL REPLACEMENT    90140
SUBHUECM TM    OPCOL,OPSEVEN   7-COLOR SUPPORT ?                 90140
         BO    SUBSTCOL      YES; DO NORMAL COLOR PROCESSING     90140
*        NOTE THAT HIGH-LIGHTING IS ON OR NCOL WOULD HAVE BEEN SET.
         TM    WORK0+2,X'F0'  ANY HIGH-LIGHTING OPTION ?         90140
         BM    SUBSTHI       YES; HONOR IT                       90140
         NI    WORK0+2,X'07'  GET REPLACEMENT COLOR              90140
         TR    WORK0+2(1),64(RWK)  GET HARDWARE COLOR            90140
         TM    WORK0+2,X'F0'  ANY HIGH-LIGHTING ?                90140
         BNMR  RET           NO; RETURN WITH FUNCTION 0          90140
SUBSTHI  MVI   WORK0+1,X'41'  SET HIGH-LIGHTING                  90140
         PACK  WORK0+2(1),WORK0+2(1)  SWAP HIGH AND LOW NYBBLE   90148
         B     SUBSTCOM      GO TO COMMON                        90140
SUBSTCOL MVI   WORK0+1,X'42'  SHOW COLOR FUNCTION                90140
         NI    WORK0+2,X'07'  ISOLATE COLOR REQUEST              90140
         TR    WORK0+2(1),64(RWK)  REPLACE BY HARDWARE COLOR CHOICE
SUBSTCOM NI    WORK0+2,X'07'  ISOLATE HARD-WARE COLOR/HIGH-LIGHT 90140
         TR    WORK0+2(1),EBCDIG  MAKE PROPER FUNCTION BYTE      90140
         CLI   WORK0+2,X'F0' RESET REQUEST ?                     90140
         BNER  RET           NO                                  90140
         MVI   WORK0+2,0     MAKE PROPER RESET CODE              90140
         BR    RET           RETURN TO CALLER                    90140
         SPACE 2
MOVER    MVC   OUTPUT+1(0),OUTPUT      SLIDING MOVE - SUBJECT OF EX
FSFLO    DC    H'79'         TRIGGER FOR FAKE SF SCAN            90140
FSFAD    DC    H'160'        POSITION FOR FAKE SF INSERT
         EJECT ,                                                 93336
*---------------------------------------------------------------------*
*                                                                     *
*    CONVERT TTY BUFFER TO 3270 DATA STREAM                           *
*                                                                     *
*---------------------------------------------------------------------*
MAKE3270 ST    RIN,BEGIN     SAVE INPUT START                    93336
         ST    ROUT,BEGOUT   AND OUTPUT START                    93336
         MVC   OUTPUT(3),=X'7DC4C2'  MOVE (MY) DEFAULT AID/CURSOR
         AR    ROUT,RTHREE   ADJUST                              93336
         LR    RWK,RLEN      GET LAST BYTE +                     93336
MAKE7END BCTR  RWK,0         LAST BYTE                           93336
         CLI   0(RWK),X'0D'  CARRIAGE RETURN ?                   93336
         BE    MAKE7CR       CONTROL-D INTERRUPT                 87043
         CLI   0(RWK),EOT    CONTROL-D TYPE BREAK ?              93336
         BNE   MAKE7PFK                                          93336
         OI    FLAGS,FGATTN  ATTENTION RECEIVED                  93336
MAKE7CR  BCTR  RLEN,0        DON'T LOOK AT END CHARACTER AGAIN   93336
MAKE7PFK LR    RWK,RLEN      GET END AGAIN                       93336
         SH    RWK,=H'2'     BACK TO START                       93336
         CR    RWK,RIN       NOT TOO LOW ?                       93336
         BL    MAKE7NFK      NO; NO FUNCTION KEY                 93336
         L     R14,=A(INSPFSTX)  POINT TO STX/SOH TABLE          93336
         CLI   0(RWK),X'02'  SOH OR STX ?                        93336
         BH    MAKE7NFK      NO                                  93336
         BE    MAKE7KEY                                          93336
         CLI   0(RWK),X'01'  SOH ?                               93336
         BNE   MAKE7NFK                                          93336
         LA    R14,INSPFSOH-INSPFSTX(,R14)   POINT TO STX TABLE  93336
MAKE7KEY LR    RLEN,RWK      ADJUST END                          93336
         LM    R15,R0,0(R14)    LOAD TABLE START/END ADRESS      93336
         LR    R1,R0         BXLE END                            93336
         LA    R0,2          TABLE INCREMENT                     93336
         IC    RWK,1(,RLEN)  GET DATA CHARACTER                  93336
MAKE7LUP CLM   RWK,1,0(R15)   MATCH ?                            93336
         BE    MAKE7MKY      YES; USE IT                         93336
         BXLE  R15,R0,MAKE7LUP  ELSE TRY AGAIN                   93336
         B     MAKE7NFK      OR QUIT                             93336
MAKE7MKY L     R14,BEGOUT    GET AID ADDRESS                     93336
         MVC   0(1,R14),1(R15)  REPLACE AID WITH PFK             93336
MAKE7NFK TM    FLAGS,FGATTN  ATTENTION SET ?                     93336
         BZ    MAKE7NAT      NO                                  93336
         L     R14,BEGOUT    GET AID ADDRESS                     93336
         MVI   0(R14),X'6C'  SET PA1                             93336
         NI    FLAGS,255-FGATTN  RESET                           93336
MAKE7NAT CR    RIN,RLEN      ANY INPUT DATA ?                    93336
         BNL   EXIT          NO; JUST EXIT                       93336
         CLI   INPUT,ESC     FORMATTED INPUT BUFFER ?            93336
         BE    MAKE7ATB      YES; PROCESS AS SUCH                93336
         MVC   OUTPUT(3),=X'114040'  MOVE FAKE SF                93336
         AR    ROUT,RTHREE   SET NEW BUFFER ADDRESS              93336
         SPACE 1                                                 93336
MAKE7ATB SLR   RWK,RWK       CLEAR FOR CR                        93336
         LA    R1,X'40'      CONSTANT BLANK                      93336
LOOP3767 IC    RWK,INPUT     GET NEXT CHARACTER FOR INSPECTION   93336
         CR    RWK,R1        PRINTABLE CHARACTER ?               93336
         BNL   PUTS3767      YES; PROCESS IT                     93336
         CLI   INPUT,ESC     ESCAPE CHARACTER ?                  93336
         BNE   MAKE7BAK      NO; TEST BACKSPACE                  93336
         CLI   INPUT+1,C')'  PROTECTED (OUTPUT) FIELD START ?    93336
         BE    MAKE7PRO      YES; SWALLOW UNTIL NEXT ESCAPE      93336
         CLI   INPUT+1,C'('  INPUT FIELD ?                       93336
         BNE   PUTS3767      NO; PROPAGATE SEQUENCE              93336
         AR    RBUF,RON      ADJUST BUFFER BY ATB SIZE           93336
         MVI   OUTPUT,SBA    MAKE ATB LEAD                       93336
         BALS  RET,SETSBA    MAKE FIELD ADDRESS                  93336
         LA    RIN,2(,RIN)   SKIP OVER                           93336
         B     MAKE7MOR      ANY MORE INPUT ?                    93336
MAKE7PRO LA    RIN,2(,RIN)   ADJUST INPUT                        93336
         AR    RBUF,RON      UP DISPLAY ADDRESS                  93336
MAKE7PRL CR    RIN,RLEN      EXHAUSTED YET ?                     93336
         BNL   MAKE7MOR      YES; GET OUT                        93336
         CLI   INPUT,ESC     ANOTHER ESCAPE ?                    93336
         BE    MAKE7ATB      YES; GO TO IT                       93336
         AR    RBUF,RON      UP DISPLAY ADDRESS                  93336
         AR    RIN,RON       UP INPUT                            93336
         B     MAKE7PRL      AND TRY AGAIN                       93336
MAKE7BAK CLI   INPUT,X'16'   BACKSPACE ?                         93336
         LR    RWK,R1        BUT REPLACE BY BLANK                93336
         BNE   PUTS3767      NO; JUST PROCESS AS BLANK           93336
*HUH?    STC   R1,INPUT      CLEAR IT OUT                        93336
         C     ROUT,BEGOUT   POSSIBLE TO BACK-UP ?               93336
         BNH   MAKE7MOR      NO; JUST BUMP                       93336
         BCTR  ROUT,0        BACK-UP ONE SPACE                   93336
         STC   R1,OUTPUT     CLEAR OLD BYTE                      93336
         B     MAKE7MOR      AND AVOID ROUT INCREMENTATION       93336
PUTS3767 STC   RWK,OUTPUT    STORE THE NEW BYTE                  93336
         AR    ROUT,RON      SET NEW OUTPUT LOCATION             93336
BUMP3767 AR    RIN,RON       SET NEW INPUT                       93336
MAKE7MOR CR    RIN,RLEN      MORE AVAILABLE ?                    93336
         BL    LOOP3767                                          93336
         B     EXIT          ALL DONE                            93336
         SPACE 1
         LTORG ,
         SPACE 2
*    CHARACTER SET DEPENDENT DATA
*        3270  ORDER CODES
         SPACE 1
SBA      EQU   X'11'         SET BUFFER ADDRESS ORDER
SF       EQU   X'1D'         START FIELD ORDER / ATTRIBUTE BYTE
IC       EQU   X'13'         INSERT CURSOR ORDER
RTA      EQU   X'3C'         REPEAT TO ADDRESS ORDER / ADDR / DATA
EUA      EQU   X'12'         ERASE UNPROTECTED TO ADDRESS / ADDR
GE       EQU   X'08'         GRAPHIC ESCAPE                      90140
SFE      EQU   X'29'         START FIELD EXTENDED/ N / (N)PAIRS
MF       EQU   X'2C'         MODIFY ATTRIBUTE      -"-
SA       EQU   X'28'         SET ATTRIBUTE / TYPE / VALUE
ETX      EQU   X'03'         BTAM/TCAM END OF TEXT SIGNAL
EOT      EQU   X'37'         BTAM/TCAM END OF TRANSMISSION CONTROL
BLANK    EQU   C' '          EBCDIC BLANK
PT       EQU   X'05'         PROGRAM TAB ***** NOT SUPPORTED *****
PC71     EQU   X'2B'         IBM 7171 SPECIAL ORDERS             90140
STX      EQU   X'02'         START TEXT
ESC      EQU   X'27'         ESCAPE (DEVICE CONTROL)
EWR      EQU   X'F5'         ERASE/WRITE CCW CODE
EWA      EQU   X'7E'         ERASE/WRITE ALTERNATE
WSF      EQU   X'F3'         WRITE STRUCTURED FIELD
WRT      EQU   X'F1'         WRITE
EAU      EQU   X'6F'         ERASE ALL UNPROTECTED (CCW)
         SPACE 1
ORDERS   DC    256AL1(OFFBAD)   DEFAULT TO UNPRINTABLE CHARACTER
         ORCH  SF,OFFSF      START FIELD
         ORCH  SBA,OFFSBA    SET BUFFER ADDRESS
         ORCH  IC,OFFIC      INSERT CURSOR
         ORCH  PT,OFFPT      PROGRAM TAB
         ORCH  RTA,OFFRTA    REPEAT TO ADDRESS
         ORCH  EUA,OFFEUA    ERASE UNPROTECTED TO ADDRESS
         ORCH  GE,OFFGE      GRAPHIC ESCAPE                      90140
         ORCH  SFE,OFFSFE    START FIELD EXTENDED
         ORCH  MF,OFFMF      MODIFY FIELD
         ORCH  SA,OFFSA      SET ATTRIBUTE
         ORCH  PC71,OFFPC71  IBM 7171 SPECIAL ORDERS
         ORCH  0,OFFDATA     HEX ZERO=>BLANK
         ORCH  C' ',OFFTBLK  BLANK
         ORCH  C'',OFFDATA,7      CENT THROUGH AMPERSAND        90140
         ORCH  C'!',OFFDATA,8      EXCLAMATION POINT THROUGH SLASH
         ORCH  X'6A',OFFDATA,6     BROKEN STROKE THROUGH QUESTION MARK
         ORCH  X'79',OFFDATA,7     REVERSE QUOTE THROUGH QUOTE   90140
         ORCH  X'81',OFFDATA,9     LOWER CASE A THROUGH I        90140
         ORCH  X'91',OFFDATA,9     LOWER CASE J THROUGH R        90140
         ORCH  X'A1',OFFDATA,9     LOWER CASE S TO Z             90140
         ORCH  X'C0',OFFDATA,10    LEFT BRACE, A TO I            90140
         ORCH  X'D0',OFFDATA,10    RIGHT BRACE, J TO R           90140
         ORCH  X'E0',OFFDATA       REVERSE SLASH                 90140
         ORCH  C'S',OFFDATA,8      S TO Z                        90140
         ORCH  C'0',OFFDATA,10     DIGITS 0 TO 9                 90140
         ORCH  X'0C',OFFDATA       FORM FEED (FF)                90140
         ORCH  X'0D',OFFDATA       CAR. RET. (CR)                90140
         ORCH  X'15',OFFDATA       NEW LINE  (NL)                90140
         ORCH  X'19',OFFDATA       END MEDIUM (EM)               90140
         ORCH  X'1C',OFFDATA       DUP                           90140
         ORCH  X'1E',OFFDATA       FIELD MARK (FM)               90140
         ORCH  X'3F',OFFDATA       SUB                           90140
         SPACE 1                                                 91100
*    TEXT KEYBOARD - TN GRAPHICS - PROCESSED WITH ESCAPE+CHAR    91100
         ORCH  X'8C',OFFTEXT,4  -> LE, SUP LP, SUP +, INTERSECT  91100
         ORCH  X'9C',OFFTEXT,6  -> A1 LOZ, SUP RP, +-, BOX, SUP - 0
         ORCH  X'AB',OFFTEXT,5  -> LEFT CORNERS, BKT, GE, BULLET 91100
         ORCH  X'B0',OFFTEXT,10  -> SUPERSCRIPT DIGITS           91100
         ORCH  X'BB',OFFTEXT,5  -> RT CORNERS, BKT, NE, HOR.LINE 91100
         ORCH  X'FA',OFFTEXT       PLOTTING LINE                 91112
         ORCH  X'8B',OFFTEXT       LEFT BRACE, TN TRAIN          91112
         ORCH  X'9B',OFFTEXT       RIGHT BRACE, TN TRAIN         91112
         ORCH  X'46',OFFTEXT,2   LOCAL JUNCTION                  91112
         ORCH  X'56',OFFTEXT,2   LOCAL JUNCTION                  91112
         ORG   ,
         SPACE 2
*        CHARACTER SET DEPENDENT CONSTANTS
         SPACE 1
*ETXEOT  DC    0AL2(0),AL1(ETX,EOT)    END OF BUFFER SIGNAL
         SPACE 1
*        BUFFER ADDRESS TRANSLATION TABLE
EBCDIC   DC    X'40C1C2C3C4C5C6C7C8C94A4B4C4D4E4F'   0 - 15
         DC    X'50D1D2D3D4D5D6D7D8D95A5B5C5D5E5F'  16 - 31
         DC    X'6061E2E3E4E5E6E7E8E96A6B6C6D6E6F'  32 - 47
EBCDIG   DC    X'F0F1F2F3F4F5F6F7F8F97A7B7C7D7E7F'  48 - 63      90140
         SPACE 1                                                 91100
*    TEXT KEYBOARD - TN TRAIN - CRT VALUES                       91100
TXTCHARS DC    256AL1(*-TXTCHARS)  DEFAULT NO TRANSLATE          91100
         TRENT TXTCHARS,X'8C',X'8C'    LESS THAN OR EQUAL        91100
         TRENT ,X'C1',X'8D'            SUPERSCRIPT LEFT PARENTH. 91100
         TRENT ,X'C2',X'8E'            SUPERSCRIPT PLUS          91100
         TRENT ,X'D3',X'8F'            INTERSECTION              91100
         TRENT ,X'9C',X'9C'            LOZENGE                   91100
         TRENT ,X'D1',X'9D'            SUPERSCRIPT RIGHT PARENTH.
         TRENT ,X'9E',X'9E'            PLUS OR MINUS             91100
         TRENT ,X'C3',X'9F'            SOLID BOX                 91100
         TRENT ,X'D2',X'A0'            SUPERSCRIPT MINUS         91100
         TRENT ,X'A1',X'A1'            DEGREE                    91100
         TRENT ,X'C4',X'AB'            LOWER LEFT CORNER         91100
         TRENT ,X'C5',X'AC'            UPPER LEFT CORNER         91100
         TRENT ,X'AD',X'AD'            LEFT BRACKET              91100
         TRENT ,X'AE',X'AE'            GREATER THAN OR EQUAL     91100
         TRENT ,X'A3',X'AF'            BULLET (?) OR TRY FD      91100
         TRENT ,(*-TXTCHARS-X'B0'+X'F0'),(X'B0',10) SUPERSCRIPT 0-9
         TRENT ,X'D4',X'BB'            LOWER RIGHT CORNER        91100
         TRENT ,X'D5',X'BC'            UPPER RIGHT CORNER        91100
         TRENT ,X'BD',X'BD'            RIGHT BRACKET             91100
         TRENT ,X'BE',X'BE'            NOT EQUAL                 91100
         TRENT ,X'A2',X'BF'            PLOTTING LINE             91100
         TRENT ,X'85',X'FA'            PLOTTING LINE             91112
         TRENT ,X'C0',X'8B'            TN TRAIN LEFT BRACE       91111
         TRENT ,X'D0',X'9B'            TN TRAIN RIGHT BRACE      91111
         TRENT ,X'C6',X'46'      LOCAL LEFT JUNCTION             91111
         TRENT ,X'C7',X'47'      LOCAL RIGHT JUNCTION            91111
         TRENT ,X'D6',X'56'      LOCAL BOTTOM JUNCTION           91111
         TRENT ,X'D7',X'57'      LOCAL TOP JUNCTION              91111
         SPACE 1                                                 93336
INSPFSTX DC    A(INSPFST1,INSPFST9)  STX TYPES                   93336
INSPFSOH DC    A(INSPFSO1,INSPFSO9)  SOH TYPES                   93336
         SPACE 1                                                 93336
INSPFSO1 DC    X'C1F2C2F3C3F4C4F5C5F6C6F7C7F8C8F9C97A'  A-I   2-10
         DC    X'817C82C183C284C385C486C587C688C789C8'  A-I L.C.12-20
         DC    X'D17BD27CD3C1D4C291C9924A934B944C7CF1E07B797B'   93336
         DC    X'D5C3D6C4D7C5D8C6D9C7E2C8E3C9E44AE54BE64C'  O-W  93336
INSPFSO9 DC    X'617B'       /                                   93336
INSPFST1 DC    X'C1F1C2F2C3F3C4F4C5F5C6F6C7F7C8F8C9F9'   A-I     93336
         DC    X'D17AD27BD37CD4C1D5C2D6C3D7C4D8C5D9C6'   J-R     93336
         DC    X'E2C7E3C8E4C9E54AE64BE74C'               S-X     93336
         DC    X'81F182F283F384F485F586F687F788F889F9'   A-I     93336
         DC    X'917A927B937C94C195C296C397C498C599C6'   J-R     93336
         DC    X'A2C7A3C8A4C9A54AA64B'                   S-W     93336
INSPFST9 DC    X'A74C'                                     X     93336
         ORG   ,                                                 91100
         SPACE 1                                                 93326
*ITOE    TRTAB TYPE=ITOE     TRANSLATE ISCII TO EBCDIC (FOR NTO) 93326
         SPACE 2
INSECT   DSECT ,             INPUT BUFFER MAPPING
INPUT    DS    C             SINGLE INPUT CHARACTER AT A TIME
         SPACE 1
OUTSECT  DSECT ,             OUTPUT BUFFER MAPPING
OUTPUT   DS    C             SINGLE BYTE AT A TIME
         SPACE 1
SAVE     DSECT ,             SAVE AREA MAPPING                   90140
DB       DS    D             CONVERSION WORK AREA                93326
BIGFOOT  DS    16F      *****DEBUG*****                          93330
BEGIN    DS    F             START OF INPUT BUFFER               93336
BEGOUT   DS    F             START OF OUTPUT BUFFER              93336
MAXOUT   DS    F             MAXIMUM OUTPUT AREA SIZE            90140
SCRSIZ   DS    F             SCREEN SIZE                         93326
SCRWID   DS    F             SCREEN WIDTH                        93326
@COLTAB  DS    A             ZERO OR ADDRESS OF COLOR TRANSLATION
WORK0    DS    A                          / WORK WORD            90140
WORK1    DS    A             OTHER REGISTERS                     90140
SAVER14  DS    A                                                 93326
CURPOS   DS    F             CURSOR POSITION                     93331
CCODE    DS    H             PROGRAM RETURN CODE                 93326
         SPACE 1                                                 90140
OPTIONS  MAPCMPR DSECT=NO    MAPPING OF OPTION BYTES             93326
OPTIONL  EQU   *-OPTIONS                                         93326
TERMTYPE DS    X             TTY CRT TYPE                        93326
FLAGS    DC    X'00'         PROCESSING FLAGS                    93336
FGATTN   EQU   X'80'           EOT FOUND (SIMULATE AS PA1)       93336
FGTO70   EQU   X'01'           CONVERT TO 3270 DATA STREAM       93336
SAVEND   EQU   *                                                 93326
         EJECT ,                                                 84092
CONV3270 CSECT ,                                                 93326
         PRINT &PRTMAC                                           93326
*        ENTRY TTYSEQ PERFORMS TYPE LOOKUP AND RETURNS           84092
*        THE LENGTH AND ADDRESS OF THE CONTROL STRING.           84092
*                                                                84092
*        CALL SEQUENCE:                                          84092
*                                                                84092
*        IC    R0,...  OR LA R0,TDO--- (SEE MACRO TTYFLAGS)      84092
*        ICM   R0,8,TRM---   TERMINAL TYPE                       84092
*        BALS  R15,TTYSEQ                                        84092
*                                                                84092
*        RETURN:  VALID TTY, SEQUENCE PRESENT :                  84092
*              R0 - LENGTH   R15 - ADDRESS OF CONTROL SEQUENCE   84092
*                                                                84092
*        FOR SEQUENCE NOT PRESENT OR INVALID TERMINAL TYPE:      84092
*              R0=R15=0                                          84092
*                                                                84092
*        FOR SEQUENCE TYPE X'FF' (SPECIAL REQUEST):              84092
*              R0=OPTION BYTES   R15=ADDRESS OF OFFSET TABLE     84092
*              OR R0=R15=0 FOR INVALID TYPE                      84092
*                                                                84092
TTYSEQ   STM   R14,R3,12(R13)  SAVE SOME REGISTERS               84092
         SLR   R1,R1                                             84092
         SLR   R2,R2                                             84092
         SLR   R3,R3                                             84092
         ICM   R1,1,20(R13)  GET TERMINAL TYPE FROM CALLER'S R0  84092
         BZ    VTTTYBAD      INVALID (NULL)                      84092
         SLL   R1,2          CONVERT TO OFFSET                   84092
         LA    R1,TTYTAB(R1)  GET ADDRESS OF TERMINAL'S POINTER  84092
         C     R1,TTYTAB     IS IT LOWER THAN TTYNULL ?          84092
         BNL   VTTTYBAD      NO; BAD TYPE                        84092
         L     R1,0(,R1)     LOAD THE OFFSET ADDRESS             84092
         CLI   3+20(R13),X'FF'  SPECIAL OPTION CALL ?            84092
         BE    VTTTYOPT      YES; LOAD AND RETURN                84092
         IC    R2,3+20(,R13)  GET REQUEST                        84092
         LA    R0,TDOPTOFF   GET OPTION OFFSET                   84092
         CR    R2,R0         VALID REQUEST ?                     84092
         BNL   VTTTYBAD      NO; FAIL                            84092
         IC    R3,0(R1,R2)   GET THE OFFSET                      84092
         LTR   R3,R3         ANY ?                               84092
         BZ    VTTTYBAD      NO                                  84092
         LA    R2,0(R1,R3)   POINT TO THE LENGTH FIELD           84092
         ICM   R0,1,0(R2)    TEST THE LENGTH                     84092
         BZ    VTTTYBAD      NONE                                84092
         LA    R15,1(,R2)    RETURN THE TABLE ADDRESS            84092
         LM    R1,R3,24(R13)  RESTORE MOST REGISTERS             84092
         LTR   R0,R0         SET NON-ZERO COND. CODE             84092
         BR    R14           RETURN                              84092
         SPACE 1                                                 84092
VTTTYBAD LM    R1,R3,24(R13)  RESTORE REGISTERS                  84092
         SLR   R15,R15       SET ERROR INDICATION                84092
         LTR   R0,R15          AND COND. CODE=0                  84092
         BR    R14           RETURN                              84092
         SPACE 1                                                 84092
VTTTYOPT ICM   R0,15,TDOPTOFF(R1)  LOAD THE OPTIONS              84092
         LR    R15,R1        RETURN THE TABLE ADDRESS            84092
         LM    R1,R3,24(R13)  RESTORE REGISTERS                  84092
         LTR   R15,R15       SET NON-ZERO COND. CODE             84092
         BR    R14           RETURN                              84092
         SPACE 2                                                 84092
         LTORG ,                                                 84092
         SPACE 2                                                 84092
TTYTAB   DC    A(TDXNULL)    NO SEQUENCES SPECIFIED              92298
         DC    A(TDXFOX)     1 PERKIN ELMER FOX AND OWL          92298
         DC    A(TDXDEC)     2 DEC-WRITERS                       92298
         DC    A(TDXDAM)     3 DATAMEDIA AND X3.64               92298
         DC    A(TDXESP)     4 HAZELTINE ESPRIT III              92298
         DC    A(TDXIMS)     5 IMS ULTIMA II; TELEVIDEO 950      92298
         DC    A(TDXIPC)     6 IBM P/C - PRELIMINARY             92298
         DC    A(TDXTEK)     7 TEKTRONIX 41XX                    92298
         DC    A(TDXHP)      8 HEWLETT-PACKARD 2621/7221 PLTR    92298
         DC    A(TDXWYS)     9 WYSE 50                           92298
         DC    A(TDXI31)     10 IBM 3101                         92298
         DC    A(TDXV100)    11 DEC VT-100, 102, 131 ???         92298
         DC    A(TDXWY30)    12 WYSE-300                         92298
         DC    A(TDXV200)    13 DEC VT-200 , 220 , 240 (241?)    92298
         DC    A(TDXTV92)    14 TELEVIDEO 920, 925               92298
         DC    A(TDXTVPT)    15 TELEVIDEO PERSONAL TERMINAL      92298
         DC    A(TDXWY60)    16 WYSE-60                          92298
         SPACE 1                                                 84092
         PRINT ON,GEN,DATA                                       84092
         TTYFLAGS ,                                              84092
         SPACE 1                                                 84092
TDXNULL  TTYDEF NAME=ANY-TTY NO SEQUENCES                      84092
         SPACE 1                                                 84092
TDXFOX   TTYDEF NAME=P.E.FOX,TABTYP=1,                                 *
               CLR=27C827D1,                                           *
               CLD=27D227D1,CLT=27D227D1,                              *
               TAB1=27E8,TAB2=27F1                             84092
         SPACE 1                                                 84092
TDXDEC   TTYDEF NAME=DECWRITER,TABTYP=2,                               *
               CLT=27F2,TAB1=27AD,TAB2=A4                        84092
         SPACE 1                                                 84092
TDXDAM   TTYDEF NAME=DATAMED.,TABTYP=2,                                *
               CLR=27ADF1D127ADC8,                                     *
               CLD=27ADF2D127ADF38727ADC8,                             *
               CLT=27ADF2D127ADF38727ADC8,                             *
               TEXT=27ADF2F05EF1F394,      GREEN ON BLACK              *
               ERR=27ADF2F05EF1F294,        RED ON BLACK               *
               HIDE=27ADF2F05EF1F094,      BLACK ON BLACK              *
               WARN=27ADF2F05EF1F594,      SOMETHING ON BLACK          *
               W80=27AD6FF393,W132=27AD6FF388,                         *
               TAB1=27ADF15E,TAB2=C827C8                     84092
         SPACE 1                                                 84092
TDXESP   TTYDEF NAME=ESPRIT3,TABTYP=1,LENSF=1,                         *
               CLR=275C,CLD=275C27F3,CLT=275C27F3,                     *
               TEXT=27C7F0,ERR=27C77A,HIDE=27C7F8,WARN=27C7F2,         *
               TAB1=277E40,TAB2=27F1,                                  *
               P1N=2779,P1F=2781,P2N=277C,P2F=27C1               84092
         SPACE 1                                                 84092
TDXIMS   TTYDEF NAME=ULTIMA2,TABTYP=1,LENSF=1,                         *
               CLR=275C,CLD=275C27F3,CLT=275C27F3,                     *
               TEXT=27C7F0,ERR=27C7F4,HIDE=27C7F1,WARN=27C7F8,         *
               TAB1=277E40,TAB2=27F1,                                  *
               P1N=2779,P1F=2781,P2N=277C,P2F=27C1               84092
         SPACE 1                                                 84267
TDXIPC   TTYDEF TABTYP=2,NAME=IBM5150,  P/C, XT, JR.                   *
               CLR=27ADF2D1,           CLR SCRN                        *
               CLD=27ADF2D127ADF387,   CLEAR TABS + SCRN               *
               CLT=27ADF2D127ADF387,   CLR TABS + SCRN                 *
               TEXT=27ADF094,          NORMAL DISPLAY                  *
               ERR=27ADF794,           REVERSED                        *
               HIDE=27ADF894,          HIDDEN                          *
               WARN=27ADF494,          UNDERLINED                      *
               TAB1=27ADF15E,TAB2=C827C8,  TAB PRE//SUF-FIX            *
               P1N=27ADF489,P1F=27ADF589,  X-PARENT PRINT              *
               P2N=27AD6FF489,P2F=27AD6FF589  COPY PRINT         93326
         SPACE 1                                                 84092
TDXTEK   TTYDEF TABTYP=2,NAME=TEK-4105,                        84125   *
               CLR=276C5AF127ADF2D127AD6FF693,  ANSI; CLEAR            *
               CLD=276C5AF12783276C5AF1,  RESET EVERYTHING             *
               CLT=27ADF2D127ADF28727AD6FF693, CLEAR SCREEN+TABS       *
               BRK=3127D3E5276C5AF1,  BREAK RESET                      *
               TEXT=27ADF3F25EF4F094,      GREEN ON BLACK              *
               ERR=27ADF3F15EF4F094,        RED ON BLACK               *
               HIDE=27ADF3F05EF4F094,      BLACK ON BLACK              *
               WARN=27ADF3F35EF4F094,      SOMETHING ON BLACK          *
               W80=27AD6FF393,W132=27AD6FF388,                         *
               TAB1=27ADF15E,TAB2=8627C8                         84092
         SPACE 1                                                 84092
TDXHP    TTYDEF TABTYP=3,NAME=HP2621,                                  *
               CLR=27C827D1,CLD=27C827D127F3,CLT=27C827D127F3,         *
               BRK=274BD2274BE9,  ABORT GRAPH/PLOT OFF                 *
               TAB1=275081F1A8,TAB2=C327F1,                            *
               P1N=274BE8,P1F=274BE9                             84092
         SPACE 1                                                 84092
TDXWYS   TTYDEF NAME=WYSE-50,LENSF=1,TABTYP=2,                         *
               CLR=275C,CLD=275C27F0,CLT=275C27F0,                     *
               TEXT=27C7F0,ERR=27C7F4,HIDE=27C7F1,WARN=27C7F8,         *
               W80=27797A,W132=27795E,                                 *
               FSCMD=27C2,LINMD=27C3,KBDLK=277B,KBDUN=277F,            *
               ADRTYP=1,ADR1=277E,                                     *
               TAB1=2781F1D9,TAB2=C327F1,                              *
               P1N=18,P1F=14,P2N=12,P2F=14                       84145
         SPACE 1                                                 84092
TDXI31   TTYDEF NAME=IBM3101,TABTYP=1,LENSF=1,                         *
               CLD=27C827D3,CLR=27C827D2,CLT=27C827D3,                 *
               TAB1=27E840,TAB2=27F0                         84092
*        THE 3101 ATTRIBUTES WORK IN BLOCK MODE ONLY; WYLBUR AND 84106
*          TSO (VTAM OR TCAM) USE CHARACTER MODE                 84106
*SOME DAY ?    TEXT=27F37C,ERR=27F3C8,HIDE=27F3D3,WARN=27F3C4,   93326
         SPACE 1                                                 84183
TDXV100  TTYDEF TABTYP=2,NAME=VT-100,  AND WYSE-75                     *
               CLR=27ADC827ADF2D1,   HOME; CLR SCRN                    *
               CLD=27ADC827ADF2D127ADF387,  HOME;CLR S,T   84234       *
               CLT=27ADC827ADF2D127ADF387,  HOME,CLR SCR+TABS          *
               TEXT=27ADF094,          NORMAL DISPLAY                  *
               ERR=27ADF09727ADF194,   REVERSED                  84295 *
               HIDE=27ADF497,          HIDDEN (WYSE-75 ONLY)     84234 *
               WARN=27ADF294,          UNDERLINED                84234 *
               W80=27AD6FF393,W132=27AD6FF388,                         *
               TAB1=27ADF15E,TAB2=C827C8,  TAB PRE//SUF-FIX      84183 *
               P1N=27ADF589,P1F=27ADF489,  X-PARENT PRINT        84234 *
               P2N=27AD6FF589,P2F=27AD6FF489  COPY PRINT         84234
         SPACE 1                                                 84183
TDXWY30  TTYDEF NAME=WYSE-300,LENSF=1,TABTYP=1,                        *
               CLR=274E,CLD=27F0274E,CLT=27F0274E,                     *
               TEXT=27C7F0,ERR=27C7F4,HIDE=27C7F1,WARN=27C797,         *
               TAB1=277E40,TAB2=27F1,                                  *
               P1N=12,P1F=14                                     84183
         SPACE 1                                                 93326
TDXV200  TTYDEF TABTYP=2,NAME=VT-200,  ALSO 220 AND 240, 241?          *
               CLR=27ADC827ADF2D1,   HOME; CLR SCRN                    *
               CLD=27ADC827ADF2D127ADF387,  HOME;CLR S,T               *
               CLT=27ADC827ADF2D127ADF387,  HOME,CLR SCR+TABS          *
               TEXT=27ADF094,          NORMAL DISPLAY                  *
               ERR=27ADF05EF194,       REVERSED                  84289 *
               HIDE=27ADF294,          DIM (HIDDEN N/A)                *
               WARN=27ADF494,          UNDERLINED                      *
               W80=27AD6FF393,W132=27AD6FF388,                         *
               TAB1=27ADF15E,TAB2=C827C8,  TAB PRE//SUF-FIX            *
               P1N=27ADF589,P1F=27ADF489,  X-PARENT PRINT              *
               P2N=27AD6FF589,P2F=27AD6FF489  COPY PRINT         93326
         SPACE 1                                                 85020
TDXTV92  TTYDEF NAME=TVI-920,LENSF=1,TABTYP=1,  TELEVIDEO 920/25       *
               CLR=275C,CLD=275C27F0,CLT=275C27F0,                     *
               TEXT=27C7F0,ERR=27C7F4,HIDE=27C7F1,WARN=27C7F8,         *
               FSCMD=27C2,LINMD=27C3,KBDLK=277B,KBDUN=277F,            *
               ADRTYP=1,ADR1=277E,                                     *
               TAB1=277E40,TAB2=27F1,                                  *
               P2N=277C,P2F=27C1,                                      *
               P1N=2779,P1F=2781   *XPARENT PRINT NOT ON 920*    85020
         SPACE 1                                                 85020
TDXTVPT  TTYDEF NAME=TVI-P/T,LENSF=0,TABTYP=1,  TELEVIDEO PERS.TRM     *
               CLR=275C,CLD=27A4275C27F3,                              *
               CLT=275C27F3,                                           *
               TEXT=27C7FC7C27C6,ERR=27C7F17CC127C8,                   *
               HIDE=27C7F1C47C27C8,WARN=27C7F0C27C27C6,                *
               TAB1=277E40,TAB2=27F1,                                  *
               P1N=2779,P1F=2781,P2N=277C,P2F=27C1               85020
         SPACE 1                                                 87054
TDXWY60  TTYDEF NAME=WYSE-60,LENSF=1,TABTYP=2,                         *
               CLR=275C,CLD=275C27F0,CLT=275C27F0,                     *
               TEXT=27C7F0,ERR=27C7F4,HIDE=27C7F1,WARN=27C7F8,         *
               W80=27797A,W132=27795E,                                 *
               TAB1=2781F1D9,TAB2=C327F1,                              *
               P1N=18,P1F=14,P2N=12,P2F=14                       87054
         SPACE 1
         END   ,
