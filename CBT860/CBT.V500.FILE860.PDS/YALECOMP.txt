YCMP     TITLE 'YALE COMPARE -- BRUCE LELAND -- VERSION 2.0'     82123
         PUNCH ' ORDER YALECOMP(P) '   MAKE DUMPS EASIER        GP04114
         PUNCH ' SETCODE AC(1)     '   FASTER INPUT             GP04114
         SPACE 1
         COPY  OPTIONGB                                          82123
         SPACE 3                                                 82123
         SYSPARM LIST=YES                                        82123
         EJECT ,                                                 82123
*        THIS IS AN IMPROVEMENT TO AN ENHANCEMENT OF THE YALE    82123
*        COMPARE PROGRAM (FILE 45 OF CBT MODS TAPE).             82123
*
*          DATA SET CBT1274    AT LEVEL 001 AS OF 10/12/81
*   THIS IS AN ENHANCEMENT TO THE YALE COMPARE PROGRAM THAT IS
*   DISTRIBUTED AS FILE 226 ON THE CONN. BANK MODS TAPE. THE PURPOSE
*   OF THE ENHANCEMENT IS TO ALLOW THE COMPARE PROGRAM TO PROCESS
*   TWO PDS FILES RATHER THAN TWO SEQUENTIAL FILES.  THE ORIGINAL
*   PROGRAM HAS BEEN MODIFIED A LITTLE TO ACCOMMODATE THE FRONT
*   END PDS PROCESSOR.
*                                                                82123
*   BRENT TOLMAN - IMPLEMENTED DISTRIBUTED PDS FRONT END (CBT FILE 45)
*   TRANSPORTAION MANAGEMENT SERVICES
*   SALT LAKE CITY, UT
*   CHANGES MARKED 81317 DUE TO SEYMOUR J. METZ (PINKERTON COMP. CONS.)
*
*   THE CURRENT VERSION ACCEPTS EITHER TWO SEQUENTIAL OR TWO     82123
*   PARTITIONED DATASETS. CODE WAS CORRECTED TO RUN UNDER SVS;   82123
*   SEVERAL OTHER MAJOR ERRORS (INCLUDING SPELLING), WERE FIXED. 82123
*        THE RECAP (DIRECTORY) LISTING HAS BEEN CHANGED TO GO TO A
*        SEPARATE FILE TO ELIMINATE THE SIZE= PARAMETER.  OUTPUT 82123
*        LINE NUMBERS ARE NOW THE RECORD NUMBER, NOT THE RECORD IN
*        A CHANGED GROUP.                                        82123
*                                                                82123
*        DEPENDENCIES - REQUIRES E.S.P. MACROS AND DYNAMIC SUBROUTINES
*        DISTRIBUTED ON THE SAME TAPE AS THIS PROGRAM. PLEASE CONTACT
*        GERHARD POSTPISCHIL    EXPERT SYSTEM PROGRAMMING       GP02338
*        176 OLD STAGE COACH ROAD,  BRADFORD, VT 05033-8844     GP02338
*              TO REPORT ERRORS AND CORRECTIONS.                 82123
*                                                                82123
*        ATTRIBUTES : RE-ENTRANT, REFRESHABLE, UNAUTHORIZED      82123
*
*   THE JCL NEEDED TO EXECUTE THE PROGRAM FOLLOWS:
*       1) //         EXEC PGM=YALECOMP,PARM='...OPTIONS...',REGION=
*       2) //SUMPRINT OR SYSTERM DD SYSOUT= OR TERM=TS           82123
*       3) //SYSPRINT DD SYSOUT (TERM=TS NOT RECOMMENDED ?)      82123
*       4) //NEW OR SYSUT1 DD  DSN=OLD PS/PO DATA                82123
*       5) //OLD OR SYSUT2 DD  DSN=NEW PS/PO DATA                82123
*       6) //IGNORE OR SYSIN DD (OPTIONAL)                       82136
*       7) //SYSPUNCH (OPTIONAL) OLD+NEW+MATCHED FOR EDITING    GP02365
*                                                                82123
*       1) A REGION SHOULD BE SPECIFIED SUFFICIENT TO CONTAIN THE
*              LARGEST BLOCK OF UNMATCHED RECORDS. THE VALUE DEPENDS
*              ON THE BUFFER SIZE(S), WHICH ROUTINES (@SERVICE, ETC.)
*              ARE IN LPA, AND SO ON. FOR A ROUGH ESTIMATE ALLOW 64K
*              FOR PROGRAMS AND BUFFERS (3120 BYTE BLOCKS); THE  82123
*              REMAINDER OF THE REGION DIVIDED BY 168 GIVES THE NUMBER
*   PARM OPTIONS ARE SELF-DEFINING KEYWORDS SUPPLIED FREE FORMAT,
*              SEPARATED BY BLANKS OR COMMAS. DEFINED ARE :      82123
*        TEXT OR PART - (DEFAULT) COMPARE COLUMNS 1-72 ONLY.     82123
*        FULL - COMPARE COLUMNS 1-80                             82123
*        DATED - LOCAL MAINTENANCE CONVENTION; CHECKS COLUMNS 1-64 ONLY
*        ASM - COMPARES TO END OF OPERAND FIELD ONLY             82123
*        CONTEXT - PRINTS A FEW (10) RECORDS BEFORE AND AFTER MIS-
*              MATCHES TO PLACE THEM INTO CONTEXT.               82123
*        CAPSON - TREATS INPUT AS UPPER CASE FOR COMPARE        GP04114
*        LISTALL, LIST OR ALL - LISTS ALL RECORDS READ           82123
*        LISTOLD OR OLD - MEMBERS FOUND ONLY IN THE OLD PDS ARE LISTED
*        LISTNEW OR NEW - MEMBERS FOUND ONLY IN THE NEW PDS ARE LISTED
*        LISTMEM OR MEM - LIST ANY MEMBER NOT IN THE OTHER PDS   82123
*        ALIAS - PROCESSES ALL ALIAS ENTRIES AS MEMBERS          82123
*        TSO - VB OR U RECFM HAS LEFT-JUSTIFIED SEQUENCE NUMBERS 82123
*        DIR - PRINT DIRECTORY LIST ONLY; SKIPS ALL DATA READS   82123
*        COUNT - PRINT DIRECTORY WITH MEMBER COUNTS; BUT DO NOT PRINT
*              MATCHES OR MISMATCHES.                            82123
*              THIS OPTION MAY ALSO BE ACHIEVED WITH A DUMMY SYSPRINT.
*          WHEN DIR OR COUNT IS CHOSEN, AND SUMPRINT IS OMITTED, THE
*              OUTPUT IS ROUTED TO SYSPRINT INSTEAD.             82123
*   OPTIONS OLD/NEW/MEM/DIR ARE IGNORED FOR SEQUENTIAL PROCESSING.
*
*   DD CARDS :                                                   82123
*       2)  SUMPRINT OR SYSTERM - RECEIVE ERROR MESSAGES, DIRECTORY
*              LISTINGS AND STATISTICS. MANDATORY WHEN SYSPRINT IS
*              OMITTED OR DUMMY.                                 82123
*       3)  SYSPRINT - DETAIL OUTPUT LISTING. OPTIONAL WHEN RECORDS ARE
*              NOT DESIRED AND SYSTERM/SUMPRINT IS AVAILABLE.    82123
*       4)  OLD OR SYSUT1 - THE 'OLD' FILE TO BE COMPARED. MAY BE A PDS
*              WHEN NEW/SYSUT2 IS A PDS; OR MAY BE ANY SEQUENTIAL
*              DATASET, INCLUDING A MEMBER OF A PDS.             82123
*       5)  NEW OR SYSUT2 - THE 'NEW' FILE TO BE COMPARED. TYPE (PDS
*              OR SEQUENTIAL/PDS MEMBER) MUST MATCH THE OLD/SYSUT1
*              SPECIFICATION.                                    82123
*       6)  IGNORE - OPTIONAL INPUT CONTAINING CARDS THAT WILL  GP02365
*              BE TREATED AS MISMATCHES TO KEEP GROUPS OF INSERTIONS
*              CONTIGUOUS. TYPICAL FOR ASSEMBLER PROGRAMS ARE 'SPACE',
*              'EJECT' AND COMMENTS CARDS. A TABLE OF THE MOST FREQUENT
*              OF THESE IS BUILT INTO THE PROGRAM AS A DEFAULT.  82123
*              IGNORE MUST CONTAIN AT LEAST ONE CARD; ELSE THE DEFAULT
*              TABLE IS USED.                                    82123
*   INPUT FORMATS SUPPORTED :                                    82123
*        RECFM=FB - CARD FORMAT; LRECL SHOULD BE 80. SHORTER RECORDS
*              ARE PADDED WITH BLANKS, LONGER ONES TRUNCATED.    82123
*        RECFM=VB - RECORDS PADDED WITH BLANKS IF SHORT, TRUNCATED IF
*              TOO LONG. PARM=TSO SPECIFIES LEFT-JUSTIFIED SEQUENCE #S.
*        RECFM=U - WYLBUR FORMAT. EXCEPT FOR LINE NUMBER PROCESSING,
*              EDIT FORMAT BLOCKS ARE ACCEPTED IN OBS, RAND, OSI AND
*              NIH FORMAT. SHORT RECORDS ARE PADDED WITH BLANKS; LONG
*              RECORDS ARE TRUNCATED (NO ERROR/WARNING MESSAGES GIVEN).
*                                                                82123
*   ABENDS :   USER 1403 - REQUIRED DD CARD MISSING - SEE WTO/WTP
*              USER 2540 - INPUT FILE SPECIFICATION ERROR(MAY BE ISSUED
*               FOR NEW/OLD/IGNORE FILES).                       82123
*                                                                82123
*   RETURN CODES - SEE MESSAGE ON PRINT FILE :                   82123
*        20 - MAJOR ERROR; PO/PS MISMATCH; REGION TOO SMALL; ETC.
*        16 - REGION TOO SMALL FOR COMPARISON OF LARGE MEMBER    82123
*        12 - OR'ED VALUE OF RETURN CODES 8 AND 4                82123
*         8 - MEMBER APPEARED IN ONE PDS, BUT NOT THE OTHER      82123
*         4 - MISMATCHED RECORDS FOUND                           82123
*         0 - NONE OF THE ABOVE                                  82123
         EJECT ,
         PRINT &PRTSOR                                           82123
YALECOMP PGMHEAD ZERO12,BASE=(R11,R12),PARM=R10,BNDRY=PAGE      GP02338
         MVI   BLANKS,C' '
         MVC   BLANKS+1(L'BLANKS-1),BLANKS  CLEAR
         SERVINIT ,          LOAD THE SERVICE ROUTINE            82123
         SERVLOAD @PRINTER,@INPREAD  GET SERVICE ROUTINES       GP04114
         MVC   SYSPRINT(SYSPRINL),PATPRINT  MOVE SYSPRINT WORK AREA
         MVI   ERRUNIT,2     ROUTE ERROR MESSAGES TO SYSTERM     82123
         MVI   DIRUNIT,1     SET DIR/COUNT TO SYSPRINT           82123
         PRTOPEN SYSPRINT,OPT=(DUMMY)  PROCEED WITHOUT PRINT FILE
         CH    R15,=H'4'     PRINT FILE USABLE ?                 82123
         BH    INITNPRT      NO                                  82123
         MVI   ERRUNIT,1     PROVISIONALLY ROUTE ERRORS TO IT    82123
         PRTOPEN SUMPRINT,DEV=2,OPT=(DUMMY)                      82123
         CH    R15,=H'4'     SYSTERM/SUMPRINT USABLE ?           82123
         BH    INIT2PRT      NO                                  82123
         MVI   ERRUNIT,2     RESET MESSAGE ROUTING               82123
         B     INIT2PRT                                          82123
INITNPRT PRTOPEN SUMPRINT,DEV=2,OPT=(ABEND)  ABEND IF NO SUMMARY 82123
         MVI   DIRUNIT,2     SET DIR/COUNT ROUTING               82123
INIT2PRT PRTLIST HEADER,TITLE=1   PRINT MAJOR TITLE              82123
         PRTLIST SUMHEAD,DEV=2,TITLE=1   AND SUMMARY HEADER      82123
         PRTOPEN SYSPUNCH,DEV=4,OPT=(NOWTO) DON'T NEED ?        GP02365
         MVC   GETMAIN(GETMVLEN),GETMEAN  MOVE GETMAIN INFO      82123
         LM    R0,R2,GETMIN    GET RESERVE AND GETMAIN SIZES     82123
         AR    R0,R2         ADD RESERVE TO MINIMUM              82123
         CR    R0,R1         IS NEW MINIMUM HIGHER THAN MAX ?    82123
         BNH   *+6           NO                                  82123
         LR    R1,R0         SET MAX TO MIN                      82123
         STM   R0,R1,GETMIN  SET NEW MIN AND MAX BACK            82123
         MVC   CONMAX,MAXCONTX  SET CONTEXT SIZE                 82123
         MVI   MLENGTH,72-1  SET DEFAULT COMPARE LENGTH          82123
         MVC   OLDINP(PATINPLN),PATINP  MAKE INPUT DATA DCB      82123
         MVC   NEWINP(PATINPLN),PATINP  MAKE OUTPUT DATA DCB     82123
         MVI   OLDDEV,1      SET @INPREAD UNIT NUMBERS           82123
         MVI   NEWDEV,2                                          82123
         SPACE 1                                                 82123
         LA    R7,0(,R10)    CLEAN PARM ADDRESS                  82123
         LTR   R7,R7         ANY PARM ADDRESS ?                  82123
         BZ    PARMDFLT      NO; USE DEFAULTS                    82123
         ICM   R7,7,1(R7)    LOAD AND TEST PARM ADDRESS          82123
         BZ    PARMDFLT      NONE                                82123
         SR    R9,R9                                             82123
         ICM   R9,3,0(R7)    LOAD AND TEST PARM LENGTH           82123
         BZ    PARMDFLT      NONE                                82123
         LA    R8,100        OS MAXIMUM                          82123
         CR    R9,R8                                             82123
         BNH   *+6           OK                                  82123
         LR    R9,R8         TRUNCATE                            82123
         MVI   WORKLINE,C' '                                     82123
         MVC   WORKLINE+1(L'WORKLINE-1),WORKLINE  PRE-CLEAR      82123
         BCTR  R9,0          SET FOR EXECUTE                     82123
         EX    R9,PARMMOVE   MOVE PARM TO WORK AREA              82123
         LA    R7,WORKLINE+1  POINT TO START OF PARM             82123
         SR    R8,R8         CLEAR BXLE INCREMENT REGISTER       82123
         AR    R9,R7         SET LAST PARM ADDRESS               82123
         SPACE 1                                                 82123
PARMLOOP LA    R4,PARMTAB    GET VERB TABLE FOR PARMS            82123
PARMVERB ICM   R8,1,0(R4)    INSERT AND TEST LENGTH BYTE         82123
         BM    PARMERR       NO MATCH - DISPLAY BAD DATA         82123
         EX    R8,PARMCLC    MATCH ?                             82123
         BE    PARMGO        YES; GO TO APPROPRIATE ENTRY        82123
         LA    R4,4(R8,R4)   POSITION TO NEXT PARM               82123
         B     PARMVERB      TRY NEXT ONE                        82123
PARMGO   SR    R15,R15                                           82123
         ICM   R15,3,1(R4)   GET BRANCH OFFSET                   82123
         B     YALECOMP(R15)  ENTER SUBROUTINE                   82123
PARMMOVE MVC   WORKLINE+1(0),2(R7)  MOVE PARM FIELD              82123
PARMCLC  CLC   0(0,R7),3(R4)        COMPARE VERB                 82123
PARMBADM VCON  ' ***** ERROR IN PARM FIELD *****'                82123
PARMBADD VCON  ' ***** CONFLICTING PARM REQUEST *****'           82123
         SPACE 1                                                 82123
PARMTAB  VERBTAB ' ',PARMBUMP,BASE=*,LEN=2    SKIP BLANKS        82123
         VERBTAB ',',PARMBUMP                 SKIP COMMAS        82123
         SPACE 1                                                 82123
         VERBTAB 'FULL',PRMFULL        COMPARE FULL CARDS (80 COL)
         VERBTAB 'PART',PRMTEXT        COMPARE TEXT ONLY (1-72)  82123
         VERBTAB 'TEXT',PRMTEXT        ALIAS (NOTE TEXT IS DEFAULT)
         VERBTAB 'DATED',PRMDATE       LOCAL; COLS 1-64 ONLY     82123
         VERBTAB 'DATE',PRMDATE                                  82123
         VERBTAB 'ASM',PRMASM     USE OPERAND END FOR COMPARES   82123
         VERBTAB 'COBOL',PRMCOB   USE COBOL MARGINS              89160
         VERBTAB 'COB',PRMCOB                                    89160
         SPACE 1                                                 82123
         VERBTAB 'LISTOLD',PRMOLD      LIST UNMATCHED OLD MEMBERS
         VERBTAB 'OLD',PRMOLD                                    82123
         VERBTAB 'LISTNEW',PRMNEW      LIST UNMATCHED NEW MEMBERS
         VERBTAB 'NEW',PRMNEW                                    82123
         VERBTAB 'LISTMEM',PRMMEM      LIST ANY UNMATCHED MEMBER 82123
         VERBTAB 'MEM',PRMMEM                                    82200
         VERBTAB 'LISTALL',PRMALL      LIST ALL RECORDS READ     82123
         VERBTAB 'LIST',PRMALL                                   82123
         VERBTAB 'ALL',PRMALL                                    82123
         VERBTAB 'CONTEXT',PRMCONT     LIST SOME MATCHED RECORDS 82123
         SPACE 1                                                GP04114
         VERBTAB 'CAPSON',PRMCASE      COMPARE USING UPPER CASE GP04114
         VERBTAB 'UPPER',PRMCASE       COMPARE USING UPPER CASE GP04114
         VERBTAB 'UPCASE',PRMCASE      COMPARE USING UPPER CASE GP04114
         VERBTAB 'FOLD',PRMCASE        COMPARE USING UPPER CASE GP04114
         SPACE 1                                                 82123
         VERBTAB 'DIR',PRMDIR          PRINT DIRECTORY/NO DATA READ
         VERBTAB 'COUNT',PRMCOUNT      NO OUTPUT TO SYSPRINT     82123
         VERBTAB 'ALIAS',PRMALIAS      PROCESS ALIAS ENTRIES AS MEMBERS
         VERBTAB 'TSO',PRMTSO          RECFM=VB WITH LEFT SEQUENCE #S
         VERBTAB *END        END OF TABLE                        82123
         SPACE 1                                                 82123
PARMERRD PRTV  PARMBADD,DEV==ERRUNIT  SHOW CONFLICTING PARMS     82123
         B     PARMERRC      DISPLAY BAD PARM                    82123
PARMERR  PRTV  PARMBADM,DEV==ERRUNIT    PRINT ERROR IN PARM      82123
PARMERRC PRTLIST WORKFD,DEV==ERRUNIT  SHOW FULL PARM            GP02338
         MVC   WORKLINE+1(L'WORKLINE-1),WORKLINE  CLEAR          82123
         MVI   0(R7),C'|'    POINT TO BAD PARAMETER              82123
         PRTLIST WORKFD,DEV==ERRUNIT  POINT TO ERROR            GP02338
         B     RC20          QUIT WITH ERROR                     82123
         SPACE 1                                                 82123
PRMFULL  MVI   MLENGTH,80-1  COMPARE FULL CARD COLUMNS (1-80)    82123
         TM    FLAGS,FASM    ASM OPTION SPECIFIED ?              82123
         BNZ   PARMERRD      YES; CONFLICTS                      82123
         B     PARMBUMP      DO ANOTHER                          82123
PRMTEXT  MVI   MLENGTH,72-1  COMPARE CARD TEXT COLUMNS 1-72      82123
         B     PARMBUMP                                          82123
PRMDATE  MVI   MLENGTH,64-1  COMPARE (LOCAL) DATED CARDS COL 1-64
         B     PARMBUMP                                          82123
PRMASM   OI    FLAGS,FASM    SET ASM OPTION                      82123
         TM    FLAGS,FCOB    COBOL OPTION ?                      89160
         BNZ   PARMERRD      YES; ERROR                          89160
         CLI   MLENGTH,72-1  LENGTH SPECIFIED ?                  82123
         BH    PARMERRD      YES; TOO LONG                       82123
         B     PARMBUMP                                          82123
PRMCOB   OI    FLAGS,FCOB    SET COBOL OPTION                    89160
         TM    FLAGS,FASM    ASM OPTION ?                        89160
         BNZ   PARMERRD      YES; ERROR                          89160
         CLI   MLENGTH,72-1  LENGTH SPECIFIED ?                  89160
         BH    PARMERRD      YES; NOT ALLOWED                    89160
         MVI   MLENGTH,72-7-1  LENGTH FROM COL 7 TO 72 (-1)      89160
         B     PARMBUMP                                          89160
         SPACE 1                                                 82123
PRMOLD   OI    FOPT,FMOLD    PRINT UMATCHED OLD MEMBERS          82123
         B     PARMBUMP      DO ANOTHER                          82123
PRMNEW   OI    FOPT,FMNEW    PRINT UNMATCHED NEW MEMBERS         82123
         B     PARMBUMP                                          82123
PRMMEM   OI    FOPT,FMNEW+FMOLD  PRINT ALL UNMATCHED MEMBERS     82123
         B     PARMBUMP                                          82123
         SPACE 1                                                GP04114
PRMCASE  OI    FOPT,FMUPP    COMPARE IN UPPER CASE              GP04114
         B     PARMBUMP                                         GP04114
         SPACE 1                                                 82123
PRMDIR   OI    FOPT,FMDIR    SKIP COUNTS; DO DIRECTORY ONLY      82123
         TM    FOPT,FALL+FMCOUNT+FMTEXT  PREVIOUSLY SPECIFIED ?  82123
         BNZ   PARMERRD      YES; SET FOR CONFLICTING PARM       82123
         B     PARMBUMP                                          82123
PRMCOUNT OI    FOPT,FMCOUNT  SET TO COUNT, BUT NOT PRINT MEMBERS 82123
         TM    FOPT,FALL+FMDIR+FMTEXT  PREVIOUSLY SPECIFIED ?    82123
         BNZ   PARMERRD      YES; SET FOR CONFLICTING PARM       82123
         B     PARMBUMP                                          82123
PRMCONT  OI    FOPT,FMTEXT   PRINT MATCHING RECORDS ALSO         82123
         TM    FOPT,FALL+FMCOUNT+FMDIR  PREVIOUSLY SPECIFIED ?   82123
         BNZ   PARMERRD      YES; SET FOR CONFLICTING PARM       82123
         B     PARMBUMP                                          82123
PRMALL   OI    FOPT,FALL     SET TO LIST ALL RECORDS READ        82123
         TM    FOPT,FMDIR+FMCOUNT+FMTEXT  ANY OTHER SPECIFIED ?  82123
         BNZ   PARMERRD      YES; TOO BAD                        82123
         MVI   CONMAX,X'7F'  MAKE CONTEXT SIZE VERY LARGE        82123
         B     PARMBUMP      DO MORE                             82123
         SPACE 1                                                 82123
PRMALIAS OI    FOPT,FALIAS   PROCESS ALIAS ENTRIES AS MEMBERS    82123
         B     PARMBUMP                                          82123
PRMTSO   OI    PNEDIT,PNFETSO  SET LEFT-ADJUSTED SEQUENCE NUMBERS
         OI    POEDIT,POFETSO                                    82123
*        B     PARMBUMP                                          82123
         SPACE 1                                                 82123
PARMBUMP LA    R8,1(,R8)     GET TRUE LENGTH                     82123
         BXLE  R7,R8,PARMLOOP  EXAMINE NEXT PARM                 82123
         SPACE 2                                                 82123
PARMDONE DS    0H                                                82123
PARMDFLT LA    R3,OLDWORK    POINT TO 'OLD' WORK AREA            82123
         LA    R4,OLDDEOD    SET DIRECTORY EODAD                 82123
         LA    R5,2          DO TWICE                            82123
         MVC   OLDLIT,LITOLD                                     82123
         MVC   NEWLIT,LITNEW                                     82123
INITJFCB MVC   PODDNAM-OLDWORK(8,R3),OLDLIT-OLDWORK(R3)          82123
         DEVTYPE PODDNAM-OLDWORK(R3),DB     LOOK FOR DD CARD     82123
         BXLE  R15,R15,INITJUTP  OK                              82123
         CLI   OLDLIT-OLDWORK(R3),C'O'  OLD ?                    82123
         MVC   PODDNAM-OLDWORK(6,R3),=CL6'SYSUT1'  ALTERNATE DD  82123
         BE    *+8                                               82123
         MVI   PODDNAM-OLDWORK+5(R3),C'2'  MAKE SYSUT2           82123
         DEVTYPE PODDNAM-OLDWORK(R3),DB  LOOK FOR ALTERNATE DD   82123
         BXH   R15,R15,INITNODD  NO DD FOUND                     82123
INITJUTP CLI   DB+2,0        DD DUMMY ?                          82123
         BE    INITNODD      TOO BAD                             82123
         MVC   OLDDIR-OLDWORK(PATDIRLN,R3),PATDIR  MAKE DIR DCB  82123
         MVC   OLDDIR-OLDWORK+DCBDDNAM-IHADCB(8,R3),PODDNAM-OLDWORK(R3)
         LA    R14,OJFCB-OLDWORK(,R3)  DIRECTORY JFCB            82123
         ST    R14,OXLST-OLDWORK(,R3)  DIRECTORY EXIT LIST       82123
         MVI   OXLST-OLDWORK(R3),X'87'  DIR JFCB EXIT            82123
         LA    R14,OXLST-OLDWORK(,R3)  DIR EXIT LIST             82123
         STCM  R14,7,DCBEXLSA-IHADCB+OLDDIR-OLDWORK(R3)          82123
         STCM  R4,7,DCBEODA-IHADCB+OLDDIR-OLDWORK(R3)  DIR EODAD 82123
         LA    R0,OLDJFCB-OLDWORK(,R3) DCB JFCB                  82123
         ST    R0,OLDJFCBP-OLDWORK(R3)  SET JFCB POINTER         82123
         SERVCALL RJFCB,PODDNAM-OLDWORK(R3) GET JFCB FOR IT      82123
         BXH   R15,R15,INITNODD  NOT READ - TOO BAD              82123
         LA    R14,OLDJFCB-OLDWORK(,R3)  POINT TO DATA JFCB      82123
         USING INFMJFCB,R14  MAKE BIT FLIPPING EASIER            82123
         MVC   INFMJFCB(JFCBLGTH),0(R1)  MOVE THE JFCB           82123
         OI    JFCBTSDM,JFCNWRIT  KEEP IT CLEAN                  82123
         MVC   OJFCB-OLDWORK(JFCBLGTH,R3),INFMJFCB  SECOND COPY  82123
         CLI   DB+2,UCB3DACC   DIRECT ACCESS ?                   82123
         BNE   INITJSEQ      NO; FORCE SEQUENTIAL PROCESSING     82123
         SERVCALL DSDJ1,OLDJFCB-OLDWORK(R3)  GET DSCB1 FROM JFCB 82123
         BXH   R15,R15,INITNFMT   NOT FOUND ?                    82123
         USING DS1FMTID,R1   DECLARE THE RETURNED PORTION OF THE DSCB
         TM    DS1DSORG,JFCORGPO  PARTITIONED ?                  82123
         BZ    INITJSEQ      NO; TREAT AS SEQUENTIAL DATASET     82123
         MVC   PDORECFM-OLDWORK(1,R3),DS1RECFM  RECFM FOR PDSDE  82123
         LA    R14,OJFCB-OLDWORK(,R3)  POINT TO DIRECTORY JFCB   82123
         TM    JFCBIND1,JFCPDS  PDS MEMBER SPECIFIED ?           82123
         BNZ   INITJSEQ      YES; DO SEQUENTIALLY                82123
         OI    JFCBTSDM,JFCNWRIT+JFCNDCB  KEEP IT CLEAN          82123
         XC    JFCBELNM,JFCBELNM  CLEAR MEMBER NAME              82123
         NI    JFCBIND1,255-JFCPDS  SHOW NO MEMBER NAME PRESENT  82123
         LA    R14,OLDJFCB-OLDWORK(,R3)  POINT TO DATA JFCB      82123
         OI    JFCBIND1,JFCPDS  SHOW MEMBER NAME PRESENT         82123
         OI    FLAGS,FGPDS   SHOW PDS FOUND                      82123
         TM    FLAGS,FGSEQ   PRIOR SEQUENTIAL ?                  82123
         BZ    INITJUMP      NO; DO NEXT FILE                    82123
INITJERR PRTLIST ERMPOPS,DEV==ERRUNIT  INCOMPATIBLE TYPES        82123
         B     RC20          QUIT                                82123
INITNODD PRTLIST ERMNODD,DEV==ERRUNIT  MISSING/UNUSABLE DD       82123
         B     RC20                                              82123
INITNFMT PRTLIST ERMNOFMT,DEV==ERRUNIT  SHOW LOCATE FAILED       82123
         B     RC20                                              82123
INITJSEQ OI    FLAGS,FGSEQ   SET SEQUENTIAL BIT                  82123
         TM    FLAGS,FGPDS   PRIOR PDS FOUND ?                   82123
         BNZ   INITJERR      YES; TOO BAD                        82123
         NI    FOPT,255-FMDIR-FMOLD-FMNEW                        82123
INITJUMP LA    R4,NEWDEOD    SET NEW DIRECTORY EODAD             82123
         LA    R3,NEWWORK    SET OTHER WORK AREA                 82123
         BCT   R5,INITJFCB   DO SECOND ONE                       82123
         DROP  R1,R14                                            82123
         TM    FOPT,FMDIR+FMCOUNT  COUNT AND DIR ?               82123
         BNO   *+8           NO                                  82123
         NI    FOPT,255-FMDIR  HONOR COUNT                       82123
         TM    FOPT,FMTEXT   TEXT SPECIFIED ?                    82123
         BZ    *+8           NO                                  82123
         NI    FOPT,255-FMDIR-FMCOUNT  RESET SHORT OPTIONS       82123
         TM    FOPT,FMDIR+FMCOUNT  SHORT FORMAT ?                82123
         BNZ   *+10          NO                                  82123
         MVC   DIRUNIT,ERRUNIT  PUT DIRECTORY ON SUMPRINT IF ANY 82123
         PRTLIST SUB2HEAD,TITLE=2,DEV=(1,2)  SET SUBHEADER       82123
         TM    FLAGS,FGSEQ   DOING SEQUENTIAL COMPARE ?          82123
         BNZ   INITDONE      YES; NO COLUMN HEADERS              82123
         PRTLIST /COLHEAD,TITLE=4,DEV==DIRUNIT  MAKE COLUMN HEADERS
         SPACE 2                                                 82123
INITDONE MVC   FIRSTIG(12),IGASMS  SET DEFAULT IGNORE CARDS      82123
         TM    FOPT,FMUPP    UPPER CASE ONLY ?                  GP04114
         BNZ   IGNOPHI       YES; FOLD                          GP04114
IGNOPLO  INPOPEN IGNORE,OPT=(DUMMY,NOWTO)                        82123
         B     IGNOPCM                                          GP04114
IGNOPHI  INPOPEN IGNORE,OPT=(DUMMY,NOWTO,FOLD)                  GP04114
IGNOPCM  CH    R15,=H'4'     IGNORE/SYSIN OPEN ?                 82136
         BH    NONIG         NOT OPEN; NO IGNORE PROCESSING      82136
         GETMAIN VU,LA=GETMIN,A=STARTADD,MF=(E,GETMAIN)  GET STORAGE
         LM    R1,R3,GETRESRV  GET RESERVE, START, SIZE          82123
         LR    R15,R3        COPY SIZE GOTTEN                    82123
         SR    R15,R1        ALLOW FOR RESERVE                   82123
         SR    R14,R14                                           82123
         LA    R4,DFTIG2-DFTIG   SET CARD SIZE                   82136
         DR    R14,R4        GET NUMBER OF CARDS                 82123
         MR    R14,R4        GET EQUIVALENT STORAGE              82123
         LA    R5,0(R2,R15)  SET LAST AVAILABLE ADDRESS+         82123
         SR    R5,R4         MAKE LAST WHOLE CARD ADDRESS        82123
         SR    R2,R4         POINT BEFORE FIRST CARD             82136
         LA    R9,OLDINP     POINT TO FAKE WORK AREA             82200
         SPACE 1                                                 82123
GETIG    INPGET ,            GET NEXT RECORD                     82123
         BXH   R15,R15,ENDIG   ALL DONE OR I/O ERROR             82123
         BXH   R2,R4,PRMNOSTR  SET ADDRESS OF NEXT CARD          82136
         MVC   L'BUFSIZ(DFTIG2-DFTIG-L'BUFSIZ,R2),0(R1)  COPY THE CARD
         LR    R3,R2         SET CARD ADDRESS                    82123
         SH    R3,=Y(BOFSIZ)  POINT TO PSEUDO-BUFFER             82123
         BAS   R10,ASMTEST   SET SIGNIFICANT LENGTH              82123
         NI    OLDOFLGS-OLDINP(R9),255-OFGCONT  RESET CONTINUATION
         B     GETIG         DO ANOTHER                          82136
PRMNOSTR PRTV  ERMNOSTO,DEV==ERRUNIT  PRINT OUT OF STORAGE MESSAGE
         B     RC20          AND QUIT                            82123
         SPACE 1                                                 82123
ENDIG    L     R0,STARTADD   GET STORAGE START                   82123
         LR    R1,R4         COPY CARD SIZE                      82136
         LA    R3,7(R1,R2)   MAKE END OF TABLE AND SET FOR ROUND 82136
         N     R3,=X'FFFFFFF8'  MAKE NEXT DOUBLE-WORD ADDRESS    82136
         LR    R5,R3         SAVE END FOR FREEMAIN               82136
         SR    R3,R0         GET AMOUNT USED                     82123
         BNP   *+8           NONE USED; LEAVE DEFAULT POINTERS   82123
         STM   R0,R2,FIRSTIG  SET START/END IGNORE CARD POINTERS 82123
         L     R0,SIZE       GET GETMAIN SIZE BACK               82123
         SR    R0,R3         SUBTRACT AMOUNT USED                82123
         BNP   SORTIG        ALL ?  WILL BOMB SHORTLY            82123
         FREEMAIN R,LV=(0),A=(R5)  FREE UNUSED PORTION           82136
SORTIG   LM    R14,R0,FIRSTIG  GET START,SIZE,END BACK           82123
         C     R14,IGASMS    DEFAULT LIST ?                      82123
         BE    NONIG         DON'T SORT RE-ENTRANT PROGRAM AREA  82123
         LR    R15,R14       COPY OVER                           82123
         L     R1,=AL1(C'A',L'BUFSIZ,DFTIG2-DFTIG-L'BUFSIZ,DFTIG2-DFTIG*
               )  SET SORT PARMS                                 82123
         STM   R15,R1,DB     MAKE SORT PARM LIST                 82123
         OI    DB+4,X'80'    SET END ADDRESS PRESENT             82136
         SERVCALL SORTH,DB   SORT THE REQUESTS                   82123
         SPACE 1
NONIG    INPCLOSE ,          CLOSE THE INPUT FILE                82123
*        B     NDIR          GO PROCESS NEW DIRECTORY            82123
         EJECT
NDIR     TM    FLAGS,FGPDS   PDS COMPARE ?                       82123
         BNZ   DIRPROC       YES; PROCESS DIRECTORIES            82123
         MVI   DIRF,DFOF+DFNF  SET BOTH END-FILES                82123
         LA    R4,BLANKS                                         82123
         SR    R2,R2                                             82123
         SR    R3,R3                                             82123
         LR    R5,R4                                             82123
         LR    R6,R2                                             82123
         LR    R7,R3         MAKE DUMMY LISTS                    82123
         LA    R1,MSGTABOV   SET DEFAULT MESSAGE                 83037
         ST    R1,ERRPOINT     INTO ERROR POINTER                83037
         B     DIRLSEQ       FALL INTO PDS CODE                  82123
         SPACE 1                                                 82123
DIRPROC  LA    R14,NEWDIR                                        82123
         LA    R15,OLDDIR                                        82123
         STM   R14,R15,DB                                        82123
         OI    DB+4,X'80'                                        82123
         OPEN  MF=(E,DB),TYPE=J                                  82123
         LA    R1,1                                              82123
         LR    R2,R1                                             82123
         SR    R3,R3         SET BXLE TO READ                    82123
         STM   R1,R3,OLDBXLE                                     82123
         STM   R1,R3,NEWBXLE                                     82123
         MVI   DIRF,DFOB+DFNB  REQUEST INCREMENTATION FOR EACH   82123
         SPACE 1                                                 82123
DIRLOOP  TM    FLAGS,FGSEQ   SEQUENTIAL PROCESSING ?             82123
         BNZ   CLOSE         YES; DONE                           82123
         LA    R1,MSGTABOV   SET DEFAULT MESSAGE                 82123
         ST    R1,ERRPOINT   BACK INTO ERROR POINTER             82123
         LM    R1,R3,OLDBXLE  RESTORE MEMBER POINTERS            82123
         LR    R4,R1         FINAGLE                             82123
         TM    DIRF,DFOB     BUMP OLD BLOCK ?                    82123
         BZ    DIRLNEW       NO; CHECK NEW BLOCK                 82123
         NI    DIRF,255-DFOB   RESET REQUEST                     82123
         TM    DIRF,DFOF     PRIOR EOF ?                         82123
         BNZ   DIRLNEW       YES; SKIP INCREMENTATION            82123
DIRLBOLD BXLE  R4,R2,DIRLOSVC  USE CURRENT BLOCK IF NOT EMPTY    82123
         GET   OLDDIR        GET NEXT DIRECTORY BLOCK            82123
         LR    R4,R1         POINT TO FIRST BYTE                 82123
         LH    R3,0(,R1)     GET SIZE OF BLOCK                   82123
         AR    R3,R4         GET LAST BYTE +1                    82123
         BCTR  R3,0          ADJUST                              82123
         LA    R2,2          SET LENGTH OF LENGTH                82123
         B     DIRLBOLD      BUMP TO FIRST MEMBER ENTRY          82123
DIRLOSVC LR    R1,R4         FINAGLE                             82123
         STM   R1,R3,OLDBXLE  UPDATE POINTERS                    82123
         SERVCALL PDSDE,OLDBXLE   DECODE CURRENT BLOCK           82123
         LR    R15,R0                                            82123
         MVC   PDORALIS(PDORBLNK),0(R15)  SAVE RETURNED DATA     82123
         LM    R1,R3,OLDBXLE   RELOAD                            82123
         LR    R4,R1         UNFINAGLE                           82123
         B     DIRLNEW       NOW TEST NEW BLOCK                  82123
         SPACE 1                                                 82123
OLDDEOD  OI    DIRF,DFOF     SET END-FILE                        82123
         LA    R4,ALLONES    POINT TO LAST ENTRY                 82123
         SR    R2,R2         CLEAR LENGTH                        82123
         SR    R3,R3         SET INVALID END ADDRESS             82123
DIRLNEW  LM    R5,R7,NEWBXLE  RESTORE MEMBER POINTERS            82123
         TM    DIRF,DFNB     BUMP NEW BLOCK ?                    82123
         BZ    DIRLCOMP      NO; GO TO COMPARE                   82123
         NI    DIRF,255-DFNB   RESET REQUEST                     82123
         TM    DIRF,DFNF     PRIOR EOF ?                         82123
         BNZ   DIRLCOMP      YES; SKIP INCREMENTATION            82123
DIRLBNEW BXLE  R5,R6,DIRLNSVC  USE CURRENT BLOCK IF NOT EMPTY    82123
         GET   NEWDIR        GET NEXT DIRECTORY BLOCK            82123
         LR    R5,R1         POINT TO FIRST BYTE                 82123
         LH    R7,0(,R5)     GET SIZE OF BLOCK                   82123
         AR    R7,R5         GET LAST BYTE +1                    82123
         BCTR  R7,0          ADJUST                              82123
         LA    R6,2          SET LENGTH OF LENGTH                82123
         B     DIRLBNEW      BUMP TO FIRST MEMBER ENTRY          82123
DIRLNSVC STM   R5,R7,NEWBXLE UPDATE POINTERS                     82123
         SERVCALL PDSDE,NEWBXLE   DECODE CURRENT BLOCK           82123
         LR    R15,R0                                            82123
         MVC   PDNRALIS(PDNRBLNK),0(R15)  SAVE RETURNED DATA     82123
         LM    R5,R7,NEWBXLE   RELOAD POINTERS                   82123
         B     DIRLCOMP      NOW TEST NEW BLOCK                  82123
         SPACE 1                                                 82123
NEWDEOD  OI    DIRF,DFNF     SET END-FILE                        82123
         LA    R5,ALLONES    POINT TO LAST ENTRY                 82123
         SR    R6,R6         CLEAR LENGTH                        82123
         SR    R7,R7         SET INVALID END ADDRESS             82123
         SPACE 1                                                 82123
DIRLCOMP MVC   OLDJFCB+JFCBELNM-INFMJFCB(8),0(R4)  MOVE MEMBER NAME
         MVC   NEWJFCB+JFCBELNM-INFMJFCB(8),0(R5)                82123
DIRLSEQ  LR    R1,R4         FINAGLE                             82123
         STM   R1,R3,OLDBXLE   SAVE POINTERS                     82123
         STM   R5,R7,NEWBXLE                                     82123
         XC    COMXC(COMXCLEN),COMXC  CLEAR MEMBER DATA          82123
         MVI   OLDOFLGS,0    SET CONTROL BLOCK CLOSED            82123
         XC    OLDCNT(L'OLDCNT+L'OLDDIFF),OLDCNT                 82123
         MVI   NEWOFLGS,0                                        82123
         XC    NEWCNT(L'NEWCNT+L'NEWDIFF),NEWCNT                 82123
         NI    FLAGS,255-FGPAGE-FGCOUNT  RESET MEMBER STUFF      82123
         MVC   MEMNAME,0(R5)   SET MEMBER NAME FOR TITLES/MESSAGES
         CLC   0(8,R4),0(R5)   COMPARE OLD TO NEW MEMBER NAME    82123
*        BE    COMPARE       SAME MEMBER NAME IN OLD AND NEW     82123
         BH    NEWONLY       MEMBER IS ONLY IN NEW PDS           82123
         MVC   MEMNAME,0(R4)   SET MEMBER NAME                   82123
         BL    OLDONLY       MEMBER IS ONLY IN OLD PDS           82123
         EJECT
COMPARE  OI    DIRF,DFOB+DFNB      COMPARE OLD AND NEW MEMBERS   82123
         CLI   0(R4),X'FF'   MEMBER NAMES EQUAL AND X'FF'?       82123
         BE    CLOSE         YES; CLOSE FILES AND EXIT           82123
         TM    FOPT,FALIAS   PROCESS ALIAS ENTRIES AS MEMBERS ?  82123
         BNZ   COMPAREA      YES                                 82123
         TM    11(R4),X'80'  OLD ALIAS ?                         82123
         BZ    COMPAREA      NO                                  82123
         TM    11(R5),X'80'  NEW ALIAS ?                         82123
         BNZ   DIRLOOP       BOTH - PRETEND THEY DON'T EXIST     82123
COMPAREA INC   CNTOMEM       COUNT OLD MEMBERS                   82123
         INC   CNTNMEM       COUNT NEW MEMBERS                   82123
         TM    PDOTYPE,PDOTLKED  LOAD MODULE ENTRY ?             89160
         BNZ   COMPLKED      YES                                 89160
         TM    PDNTYPE,PDNTLKED  OTHER A LOAD MODULE ?           89160
         BZ    COMPNLKD      NEITHER; PROCESS NORMALLY           89160
COMPLKED TM    PDNTYPE,PDNTLKED  BOTH LOAD MODULES ?             89160
         LA    R15,4         PRESET FOR MISMATCH                 89160
         BZ    COMPRNEQ      NO; TYPE MISMATCH                   89160
         CLC   PDS2STOR-PDS2(3,R4),PDS2STOR-PDS2(R5)  SAME SIZE ?
         BNE   COMPRNEQ      SET MISMATCH                        89160
         CLC   PDORSSI,PDNRSSI  SAME SSI ?                       89160
         BE    COMPRINT      YES                                 89160
         OI    RETCODE+3,2   SET MINOR ERROR                     89160
         B     COMPRINT                                          89160
COMPNLKD TM    FOPT,FMDIR    DIRECTORY ONLY ?                    82123
         BNZ   COMPRINT      YES; JUST PRINT MEMBER NAME         82123
         TM    FOPT,FMUPP    UPPER CASE ONLY ?                  GP04114
         BNZ   COMPOPUP      YES                                GP04114
         INPOPEN OLDINP,DEV=1,OPT=(JFCB,DUMMY)  OPEN OLD INPUT   82123
         CH    R15,=H'8'     DID IT OPEN ?                       82123
         BH    OPENOLDB      NO; WRITE ERROR MESSAGE             82123
         INPOPEN NEWINP,DEV=2,OPT=(JFCB,DUMMY)  OPEN NEW INPUT   82123
         B     COMPOPCM                                         GP04114
COMPOPUP INPOPEN OLDINP,DEV=1,OPT=(JFCB,DUMMY,FOLD) OPEN OLD INPUT
         CH    R15,=H'8'     DID IT OPEN ?                      GP04114
         BH    OPENOLDB      NO; WRITE ERROR MESSAGE            GP04114
         INPOPEN NEWINP,DEV=2,OPT=(JFCB,DUMMY,FOLD) OPEN NEW INPUT
COMPOPCM CH    R15,=H'8'     DID IT OPEN ?                       82123
         BH    OPENNEWB      NO; WRITE ERROR MESSAGE             82123
         MVI   OLDOFLGS,DCBOFOPN  SET IT OPEN                    82123
         MVI   NEWOFLGS,DCBOFOPN                                 82123
         B     COMPCOMP      COMPARE AND PRINT STATISTICS        82123
         SPACE 1
NEWONLY  OI    DIRF,DFNB     LIST NEW MEMBER ONLY                82123
         TM    FOPT,FALIAS   PROCESS ALIAS ENTRIES AS MEMBERS ?  82123
         BNZ   NEWONLYA      YES                                 82123
         TM    11(R5),X'80'  ALIAS ?                             82123
         BNZ   DIRLOOP       YES; IGNORE IT                      82123
NEWONLYA OI    RETCODE+3,8   SET ERROR - UNMATCHED MEMBER        82123
         INC   CNTNMEM       COUNT NEW MEMBERS                   82123
         INC   CNTNUNM       COUNT UNMATCHED MEMBERS             82123
         TM    FOPT,FMDIR    DIRECTORY ONLY ?                    82123
         BNZ   COMPRINT      YES; JUST PRINT MEMBER NAME         82123
         TM    FOPT,FMNEW    PRINT UNMATCHED NEW MEMBERS ?       82123
         BZ    COMPRINT      NO  -- SKIP IT                      82123
         INPOPEN NEWINP,DEV=2,OPT=(JFCB,DUMMY)  OPEN NEW INPUT ONLY
         CH    R15,=H'8'     DID IT OPEN ?                       82123
         BH    OPENNEWB      NO; WRITE ERROR MESSAGE             82123
         MVI   NEWOFLGS,DCBOFOPN  SET IT OPEN                    82123
         B     COMPCOMP      COMPARE AND PRINT STATISTICS        82123
         SPACE 1
OLDONLY  OI    DIRF,DFOB     PROCESS OLD MEMBER ONLY             82123
         TM    FOPT,FALIAS   PROCESS ALIAS ENTRIES AS MEMBERS ?  82123
         BNZ   OLDONLYA      YES                                 82123
         TM    11(R4),X'80'  ALIAS ?                             82123
         BNZ   DIRLOOP       YES; IGNORE IT                      82123
OLDONLYA OI    RETCODE+3,8   ERROR - UNMATCHED MEMBER            82123
         INC   CNTOMEM       COUNT OLD MEMBERS                   82123
         INC   CNTOUNM       COUNT UNMATCHED MEMBERS             82123
         TM    FOPT,FMDIR    DIRECTORY ONLY ?                    82123
         BNZ   COMPRINT      YES; JUST PRINT MEMBER NAME         82123
         TM    FOPT,FMOLD    PRINT UMATCHED OLD MEMBERS ?        82123
         BZ    COMPRINT      NO  -- SKIP IT                      82123
         INPOPEN OLDINP,DEV=1,OPT=(JFCB,DUMMY)  OPEN OLD INPUT   82123
         CH    R15,=H'8'     DID IT OPEN ?                       82123
         BH    OPENOLDB      NO; WRITE ERROR MESSAGE             82123
         MVI   OLDOFLGS,DCBOFOPN  SET IT OPEN                    82123
COMPCOMP TM    FOPT,FMDIR+FMCOUNT  SHORT OUTPUT ?                82123
         BNZ   COMPCMPR      YES; SKIP HEADER                    82123
         PRTLIST MEMHEAD,TITLE=2                                 82123
COMPCMPR BAS   R10,COMPYALE  GO TO READ AND COMPARE              82123
         LTR   R15,R15       EQUAL ?                             82123
         BNZ   COMPRNEQ      NO                                  82123
         INC   CNTEQUAL      COUNT EQUAL MEMBERS                 82123
         CLC   PDORSSI,PDNRSSI  MATCHING SSI ?                   89160
         BE    *+8           YES                                 89160
         OI    RETCODE+3,2   SET MINOR ERROR CODE                89160
         B     COMPRCOD      GO TO SAVE COMPLETION CODE          82123
COMPRNEQ INC   CNTNEQ        COUNT UNMATCHED MEMBER              82123
COMPRCOD O     R15,RETCODE   OR RETURN CODE WITH PROGRAM RETURN  82123
         ST    R15,RETCODE   STASH IT BACK                       82123
         INPCLOSE DEV=(1,2)  CLOSE BOTH FILES                    82123
         SPACE 1                                                 82123
COMPRINT TM    FLAGS,FGPAGE  ANYTHING PRINTED ON SYSPRINT ?      82123
         BNZ   COMPRINO      YES; SHOW RESULTS                   82123
         CLI   COMPCODE+3,0  ANY ERROR ?                         82123
         BE    COMPRINS      NO; DON'T WRITE A SEPARATE PAGE     82123
COMPRINO TM    FOPT,FMCOUNT+FMDIR  SHORT REQUEST ?               82123
         BNZ   COMPRINS      YES; DON'T PRINT TWICE              82123
         PRTLIST MEMTOT      PRINT MEMBER TOTALS ON SYSPRINT     82123
COMPRINS PRTLIST MEMSUM,DEV==DIRUNIT  PRINT MEMBER SUMMARY       82123
         B     DIRLOOP       GET NEXT MEMBER                     82123
         SPACE 1                                                 82123
OPENOLDB LA    R3,OLDWORK    POINT TO OLD WORK AREA              82123
         B     OPENOLNW      GO TO COMMON                        82123
OPENNEWB LA    R3,NEWWORK    POINT TO NEW WORK AREA              82123
OPENOLNW PRTLIST ERMOPEN,DEV==ERRUNIT  WRITE A NASTY MESSAGE     82123
         INPCLOSE DEV=(1,2)  JUST IN CASE                        82123
         TM    RETCODE+3,32  BEEN HERE BEFORE ?                  82123
         BNZ   RC20          YES; QUIT                           82123
         MVI   RETCODE+3,32    SET MAJOR ERROR                   82123
         B     DIRLOOP       TRY ANOTHER MEMBER                  82123
         SPACE 1                                                 82123
RC20     LA    R9,20         SET MAJOR ERROR                     82123
         B     BYEGONE                                           82123
         SPACE 1
CLOSE    LA    R14,NEWDIR                                        82123
         LA    R15,OLDDIR                                        82123
         STM   R14,R15,DB                                        82123
         OI    DB+4,X'80'                                        82123
         CLOSE MF=(E,DB)     CLOSE THE DIRECTORY                 82123
         ICM   R9,15,RETCODE  GET RETURN CODE                    82123
         BZ    BYEBYE                                            82123
         LA    R15,16        SET MAJOR ERROR VALUE               82123
         CR    R9,R15        SEVERE ERROR ?                      82123
         BNH   BYEGONE       NO                                  82123
         LR    R9,R15        ELIMINATE EFFECT OF OR'ING CODES    82123
BYEGONE  ST    R9,RETCODE    STASH IT BACK                       82123
BYEBYE   PRTLIST SUMCNT,DEV==ERRUNIT   WRITE PDS STATISTICS      83037
         PRTLIST COMPMSG,DEV==ERRUNIT  WRITE END ERROR MESSAGE   82123
         SERVTERM ,          CLOSE AND FREE EVERYTHING           83037
         PGMEXIT RC=(R9)     RETURN WITH APPROPRIATE CODE       GP02338
         SPACE 1
*DEFER*  LTORG ,                                                GP03001
         SPACE 1                                                 82123
GETMEAN  GETMAIN VC,LA=GETMMIN,MF=L   PATTERN GETMAIN            82123
GETMEANL EQU   *-GETMEAN     LENGTH OF PATTERN                   82123
GETMMIN  DC    A(4*1024)     MINIMUM STORAGE TO GETMAIN (LESS RESRV)
         DC    A(4*1024*1024)  MAXIMUM STORAGE TO GETMAIN        85079
         DC    A(24*1024)    AMOUNT TO RESERVE FOR SYSTEM        85079
GETMVLEN EQU   *-GETMEAN     LENGTH TO MOVE                      82123
IGASMS   DC    A(DFTIG,DFTIG2-DFTIG,ENDDFTIG-(DFTIG2-DFTIG))     82204
         EJECT ,                                                 82123
*          DATA SET CBT881     AT LEVEL 003 AS OF 01/31/79
*
*   THIS COMPARE PROGRAM ORIGINATED AT YALE UNIVERSITY AND WAS OBTAINED
*     FROM THE CONNECTICUT BANK AND TRUST COMPANY MODS TAPE (FILE 226)
*
*   THIS COMPARE PROGRAM USES ONLY COLUMNS ONE THROUGH 72; THUS, ANY
*     TWO VERSIONS OF A PROGRAM MAY BE EASILY COMPARED EVEN IF ONE
*     (OR BOTH) OF THE FILES HAS BEEN RESEQUENCED.  IF NEITHER FILE
*     HAS BEEN RESEQUENCED, A PARM OF "FULL" MAY BE CODED TO REQUEST
*     THAT ALL EIGHTY COLUMNS BE USED IN THE COMPARISON.  A FACILITY
*     IS ALSO PROVIDED TO IGNORE TRIVIAL CARD IMAGES (SUCH AS " EJECT "
*     OR " SPACE 3 " IN AN ASSEMBLER SOURCE PROGRAM).
*
*        THIS PROGRAM USES ALL AVAILABLE STORAGE IN ITS REGION,  81317
*              AFTER OPENING ITS INPUT/OUTPUT FILES (EXCEPT FOR  81317
*              A SYSTEM RESERVE).  THE OBTAINED STORAGE AREA     82123
*              IS USED AS A COMPARE BUFFER WHICH IS ONLY DUMPED  81317
*              (WRITTEN TO SYSPRINT) WHEN IDENTICAL RECORDS      81317
*              (BASED ON THE FIRST 72 BYTES, OR ALL 80 BYTES,    81317
*               DEPENDING ON PARM OPTIONS FULL/PART/DATE) ARE FOUND IN
*              THE OLD AND NEW DATA SETS.  IF THE COMPARE BUFFER 81317
*              OVERFLOWS, THE CURRENT COMPARE TERMINATES. THEREFORE, IF
*              IT IS SUSPECTED THAT THE TWO DATA SETS MAY BE     81317
*              VERY FAR OUT OF SYNC, A LARGE REGION SHOULD BE    81317
*              USED.  THE NUMBER OF UNMATCHED RECORDS THAT CAN   81317
*              BE BUFFERED AT ONE TIME CAN BE CALCULATED FROM THE81317
*              EXPRESSION: (REGION-32K-BUFFERS)/168. AS A GENERAL
*              RULE, 30K IS SUFFICIENT FOR ABOUT 100 UNMATCHED   81317
*              RECORDS AT A TIME, AND 100K IS SUFFICIENT FOR     81317
*              ABOUT 500 UNMATCHED RECORDS AT A TIME.            81317
*
*   CARD IMAGES OF THE TRIVIAL DATA ARE INPUT FROM THE IGNORE DD;
*     IF THE DDCARD IS MISSING, A TABLE OF TRIVIAL DATA FOR ASSEMBLER
*     SOURCE CODE WILL BE ASSUMED (THIS INCLUDES " EJECT ", " SPACE   "
*     " SPACE 1 ", " SPACE 2 ", " SPACE 3 " AND BLANK COMMENT CARDS).
*
*
*
*   THE PROGRAM FORMAT ON THE CBT MODS TAPE WAS DISASSEMBLED SOURCE; IT
*     WAS RECOMMENTED, RESYMBOLIZED, REDOCUMENTED AND SLIGHTLY MODIFIED
*     BY  A. BRUCE LELAND (ABL)  OCTOBER, 1978
*
*
*   JCL:
*
*   //STEPNAME   EXEC  PGM=COMPARE,REGION=100K
*   //OLD        DD    DSN=OLD.SEQ.DSN ....
*   //NEW        DD    DSN=NEW.SEQ.DSN ....
*   //SYSPRINT   DD    SYSOUT=A
*   //IGNORE     DD    *      (OR DUMMY  --  OR OMITTED)
*     TRIVIAL CARD IMAGES THAT ARE TO BE IGNORED IN THE COMPARISON
*   /*
*
         EJECT
COMPYALE STM   R0,R14,SAVEYALE   (R15=0 PRESET FOR RETURN)       82123
*TEST    BASR  R11,0         MAKE A NEW BASE REGISTER           GP03001
*TEST    USING *,R11,R12     DECLARE NEW BASES                  GP03001
*TEST    LA    R12,2048                                         GP03001
*TEST    LA    R12,2048(R12,R11)  MAKE SECOND BASE              GP03001
         TM    FLAGS,FGMAIN  INITIALIZATION DONE ?               82123
         BNZ   YALECOM2      YES                                 82123
         OI    FLAGS,FGMAIN                                      82123
         GETMAIN VU,LA=GETMIN,A=STARTADD,MF=(E,GETMAIN)  GET STORAGE
         LM    R0,R2,GETRESRV   LOAD RESERVE, START, SIZE GOTTEN 82123
         SR    R2,R0         SIZE AFTER SYSTEM RESERVE           82123
         ST    R2,SIZE       UPDATE GOTTEN SIZE                  82123
         AR    R1,R2         SET END ADDRESS                     82123
         LA    R15,BUFLEN    GET SIZE OF ONE RECORD AREA         82130
         SR    R1,R15        SET LAST POSSIBLE CARD ADDRESS      82130
         ST    R1,HIGHSTOR   SAVE FOR LATER                      82123
         AR    R1,R15        FIX IT TO TRUE END ADDRESS          82130
         FREEMAIN R,LV=(0),A=(1)  FREE FOR DUMP, RE-OPEN, ETC.   82123
         L     R0,SIZE       GET GOTTEN SIZE BACK                82123
         SRDA  R0,32+1       HALVE AND CLEAR R0 FOR DIVIDE       82123
         TM    FOPT,FMTEXT   CONTEXT ?                           82123
         BZ    YALECOM1      NO; DON'T STEAL SPACE               82123
         L     R2,CONMAX     GET NUMBER OF CONTEXT CARDS IN STACK
         MH    R2,=Y(BOXLEN)  GET SIZE NEEDED                    82123
         SR    R1,R2         SET REMAINDER                       82123
         BNP   PRMNOSTR      TOO BAD                             82123
YALECOM1 LA    R2,BUFLEN                                         82123
         DR    R0,R2         GET NUMBER OF WHOLE CARDS           82123
         BCTR  R1,0          ONE FOR MATCH TO DETECT MISMATCH END
         ST    R1,DB                                             82123
         CH    R1,=H'10'     ENOUGH FOR A LITTLE WORK ?          82123
         BL    PRMNOSTR      QUIT WITH 'NO STORAGE' MESSAGE      82123
         PRTLIST /CAPCYMSG,DEV==ERRUNIT                         GP03001
         LM    R2,R3,STARTADD  GET ADDRESS, SIZE BACK            82123
         CLRL  (R2),(R3)     CLEAR ALL OF IT                     82123
         SPACE 1                                                 82123
YALECOM2 L     R2,STARTADD   GET ADDRESS BACK                    82123
         TM    FOPT,FMTEXT   CONTEXT OPTION ?                    82130
         BZ    YALECOM3      NO; USE ENTIRE AREA                 82130
         L     R0,CONMAX     GET NUMBER OF CONTEXT CARDS         82123
         MH    R0,=Y(BOXLEN)  GET ROOM REQUIRED IN BUFFER        82123
         AR    R2,R0         ADJUST TO FIRST MISMATCH CARD       82123
YALECOM3 ST    R2,LOWOLD                                         82130
         ST    R2,HIGHOLD
         ST    R2,CURROLD
         LA    R2,BUFLEN(,R2)                                    82123
         ST    R2,LOWNEW
         ST    R2,HIGHNEW
         ST    R2,CURRNEW
         LA    R8,OLDINP                                         82123
         LA    R9,NEWINP                                         82123
         LA    R6,2*BUFLEN   GLOBAL BUFFER INCREMENT             82123
         TM    FOPT,FALL     PRINTING ALL RECORDS READ ?         82123
         BZ    *+10          NO                                  82123
         MVC   CONTRAIL,CONMAX  SET FAKE TRAILER COUNT           82123
         SPACE 1                                                 82123
*        FOR UNMATCHED MEMBERS, READ AND PRINT HERE TO PREVENT   82123
*              OVERFLOW OF THE COMPARE BUFFER                    82123
         TM    DIRF,DFOB+DFNB  UNMATCHED MEMBER ?                82123
         BNM   SWAP          NO; PROCESS NORMALLY                82123
         LA    R3,NEWINP     POINT TO NEW INPUT                  82123
         TM    NEWOFLGS,DCBOFOPN  IS NEW OPEN ?                  82123
         BNZ   UNMEM         YES; PROCEED                        82123
         LA    R3,OLDINP     ELSE POINT TO OLD MEMBER            82123
         SWAPR R8,R9         EXCHANGE BUFFER POINTERS           GP03001
UNMEM    LH    R1,COUNT                                          82123
         LA    R1,1(,R1)                                         82123
         STH   R1,COUNT      SET FOR ONE UNMATCHED GROUP         82123
UNMEMGET INPGET DEV==OLDDEV-OLDINP(R3)  GET A CARD               82123
         CH    R15,=H'4'     CHECK RETURN                        82123
         BH    BADIOERR      BOO                                 82123
         BE    EOLDNEW       SHOULD NOT GET HERE                 82123
         LR    R5,R1         COPY CARD IMAGE ADDRESS             82123
         SH    R5,=Y(BOFTEXT)  FAKE PREFIX                       82123
         LA    R0,1                                              82123
         A     R0,NEWDIFF-NEWINP(,R3)                            82123
         ST    R0,NEWDIFF-NEWINP(,R3)  COUNT THE DIFFERING RECORDS
         LA    R14,1                                             82123
         A     R14,NEWCNT-NEWINP(,R9)                            82123
         ST    R14,NEWCNT-NEWINP(,R9)   UPDATE RECORD COUNT      82123
         TM    FOPT,FMCOUNT  COUNT OPTION ONLY ?                 82123
         BNZ   UNMEMGET      YES; KEEP IT UP                     82123
         CVD   R0,DB         PACK THE RECORD NUMBER              82123
         MVC   PRETTY#,=X'40202020212060'   SET EDIT PATTERN     82123
         LA    R1,PRETTY#+L'PRETTY#-2  SET SIGNIFICANCE ADDRESS  82123
         EDMK  PRETTY#(L'PRETTY#-1),DB+5  EDIT IT                82123
         BCTR  R1,0          SPACE BEFORE SIGNIFICANT DIGIT      82123
         MVI   0(R1),C'-'    MAKE IT PRETTY ?                    82123
         PRTLIST DATALINE    PRINT A PRETTY LINE ?               82123
         PRTLIST DATACARD,DEV=4  ALSO PUNCH IT                  GP02365
         TM    FLAGS,FGPAGE  PAGE NUMBER SAVED BEFORE ?          82123
         BNZ   UNMEMGET      YES; DO NEXT LINE                   82123
         OI    FLAGS,FGPAGE  SET PAGE NUMBER SAVED               82123
         PRTSPACE 0          FORCE PAGE NUMBER UPDATE            82123
         MVC   MEMPAGE,PUPAGE-PRTWORK+SYSPRINT  COPY PAGE NUMBER 82123
         B     UNMEMGET      DO NEXT LINE                        82123
         EJECT
SWAP     SWAPR R9,R8         EXCHANGE BUFFER POINTERS           GP03001
         L     R3,CURR(,R9)
         C     R3,HIGH(,R9)
         BL    NOINPUT
         TM    OLDOFLGS-OLDINP(R9),DCBOFOPN  CURRENT INPUT OPEN ?
         BZ    SWAP                    NO, BRANCH (AND SWAP)
         INPGET DEV==OLDDEV-OLDINP(R9)  READ A RECORD            82123
         CH    R15,=H'4'     CHECK RETURN CODE                   82123
         BH    BADIOERR      FATAL ERROR                         82123
         BE    EOLDNEW       - SHOULD NOT HAVE GOTTEN HERE       82123
         LA    R14,1                                             82123
         A     R14,NEWCNT-NEWINP(,R9)                            82123
         ST    R14,NEWCNT-NEWINP(,R9)   UPDATE RECORD COUNT      82123
         LA    R0,BUFLEN(,R3)                                    82123
         C     R0,HIGHSTOR             FIT IN STORAGE?           81317
         BH    OVERFLOW                NO, BRANCH                81317
         STCM  R14,7,BOFLINE(R3)  SET LINE NUMBER                82123
         MVC   BOFTEXT(L'BUFTEXT,R3),0(R1)                       82123
         LA    R0,0(R3,R6)   ADD TWO BUFFERS                     82123
         ST    R0,HIGH(,R9)
         BAS   R10,ASMTEST   SET SIGNIFICANT LINE LENGTH         82123
         SPACE 2
*  SEE IF THIS RECORD IS ANYWHERE IN THE OTHER BUFFER
*
NOINPUT  L     R2,LOW(,R8)   GET START ADDRESS
         L     R7,CURR(,R8)  GET END ADDRESS                     82123
         SR    R2,R6         FINAGLE FOR BXH                     82123
         SR    R7,R6                                             82123
         SR    R1,R1         CLEAR FOR LENGTH AT FOUND8          82123
         IC    R1,BOFSIZ(,R3)  LOAD COMPARE LENGTH-1             82123
         LA    R1,1(,R1)     MAKE LENGTH INCLUDING LENGTH BYTE   82123
NEXT8    BXH   R2,R6,NO8     BRANCH IF NOT FOUND                 82123
         TM    FLAGS,FCOB    COBOL MODE ?                        89160
         BZ    NEXTNCOB      NO                                  89160
         EX    R1,NEXTCOB    COMPARE WITHIN COBOL MARGINS        89160
         B     NEXTNCOB+4                                        89160
NEXTNCOB EX    R1,NEXTCLC    LENGTH AND DATA MATCH ?             82123
         BNE   NEXT8         TRY NEXT CARD                       82123
         B     FOUND8        CARD MATCHES                        82123
NEXTCLC  CLC   BOFSIZ(0,R3),BOFSIZ(R2)  COMPARE LENGTH AND TEXT  82123
NEXTCOB  CLC   BOFSIZ+6(0,R3),BOFSIZ+6(R2)  COMPARE COBOL TEXT   89160
         SPACE 1                                                 82123
NO8      AR    R3,R6         NOT IN THE BUFFER,
         ST    R3,CURR(,R9)           UPDATE THE CURRENT POINTER
         B     SWAP
         SPACE 2
BADIOERR LA    R1,MSGIOERR   POINT TO ERROR MESSAGE              82123
         ST    R1,ERRPOINT   POINT TO IT                         82123
*        B     OVERFLOW                                          82123
         SPACE 1                                                 82123
*    DOES NOT FIT IN THE ALLOCATED STORAGE                       81317
*
OVERFLOW BAS   R10,NOTEQUAL  OUTPUT NOT EQUAL MESSAGE            82123
         MVI   COMPCODE+3,16  SET LEVEL 16 ERROR                 82123
         B     QUITCOMP      TERMINATE THE COMPARE               82123
         EJECT
*   RECORD WAS FOUND -- SEE IF IT IS TO BE IGNORED
*
FOUND8   CLC   CURR(4,R9),LOW(R9)           RECORDS IN ACTIVE BUFFER?
         BNE   CHKIG                        YES, CHECK IGNORES
         ST    R2,CURR(,R8)
         B     NOTIGTXT      GO CHECK FOR CONTEXT REQUEST        82123
CHKIG    L     R10,FIRSTIG   FIRST IGNORE                        82123
         LM    R14,R15,FIRSTIG+4  GET LENGTH/LAST                82123
         LA    R0,72-1       SET MAXIMUM LENGTH OF IGNORE TEXT-1 82123
         CLM   R0,1,MLENGTH  LONGER THAN MAX TO COMPARE ?        82136
         BNH   *+8           NO                                  82136
         IC    R0,MLENGTH    USE ONLY MAXIMUM                    82136
         CR    R1,R0         FULL COMPARE ?                      82123
         BNH   LOOPIG        NO                                  82123
         LR    R1,R0         SET FOR IGNORE TEXT LENGTH ONLY     82123
LOOPIG   TM    FLAGS,FASM    ASM MODE ?                          82123
         BZ    LOOPIGX       NO; USE MLENGTH                     82123
         IC    R0,0(,R10)    GET IGNORE CARD LENGTH              82123
         CLM   R0,1,MLENGTH  LONGER THAN MAX TO COMPARE ?        82136
         BNH   *+8           NO                                  82136
         IC    R0,MLENGTH    USE ONLY MAXIMUM                    82136
         CR    R0,R1         MATCH ?                             82123
         BNE   BUMPIG        NO; TRY AGAIN                       82123
LOOPIGX  EX    R1,CLCIG      IGNORE THIS RECORD ?                82123
         BE    NO8           YES; TREAT AS MISMATCH
*DEBUG*  BL    NOTIG         SKIP IF NOT IN SORTED TABLE         82123
BUMPIG   BXLE  R10,R14,LOOPIG  NO; TRY NEXT ONE                  82123
*
NOTIG    ST    R2,CURR(,R8)               NOT AN IGNORE RECORD
         BAS   R10,NOTEQUAL               OUTPUT "NOT EQUAL"     82123
NOTIGTXT TM    FOPT,FALL+FMTEXT  PRINT CONTEXT OR ALL ?          82123
         BZ    IGEQUAL       NEITHER; SKIP PRINTING              82123
         L     R5,CURROLD    GET OLD CURRENT CARD                82123
         L     R7,CURRNEW    GET NEW CURRENT CARD                82123
         ICM   R0,15,CONTRAIL  ANY TRAILERS LEFT TO PRINT ?      82123
         BP    NOTIGPRT      YES; UPDATE COUNT AND PRINT         82123
         L     R15,CONMAX    GET MAXIMUM LEADING CONTEXT CARDS   82123
         L     R1,CONLEAD    GET LEADING CONTEXT COUNT           82123
         LA    R1,1(,R1)     NUMBER AFTER THIS ONE               82123
         CR    R1,R15        IS SAVE BUFFER FULL ?               82123
         BNH   NOTIGSAV      NO; JUST ADD ON                     82123
         MVI   CONTRAIL,X'80'  SET FLAG - PRINT LINE LOST        82123
         INC   CONSKIP       COUNT LINES SKIPPED                 82123
         L     R14,STARTADD  GET START OF SAVE BUFFER            82123
         BCTR  R15,0         GET NUMBER OF REMAINING CARDS       82123
         MH    R15,=Y(BOXLEN)  GET SIZE                          82123
         LA    R0,BOXLEN(,R14)  FIRST CARD TO KEEP               82123
         LR    R1,R15        SIZE TO MOVE                        82123
         MVCL  R14,R0        SLIDE ALL DOWN ONE                  82123
         L     R1,CONMAX     SET NEW SIZE                        82123
NOTIGSAV ST    R1,CONLEAD    SET LEADING CARDS SAVED             82123
         BCTR  R1,0                                              82123
         MH    R1,=Y(BOXLEN)   GET OFFSET TO LAST CARD           82123
         A     R1,STARTADD   ADDRESS OF LAST                     82123
         MVC   BOXOLD(L'BOXOLD,R1),BOFLINE(R5)  SAVE OLD RECORD NUMBER
         MVC   BOXNEW(L'BOXNEW,R1),BOFLINE(R7)  SAVE NEW RECORD NUMBER
         MVC   BOXTEXT(L'BOXTEXT,R1),BOFTEXT(R7)  SAVE NEW TEXT  82123
         B     IGEQUAL       SKIP TRAILER PROCESSING             82123
NOTIGPRT BCTR  R0,0          REDUCE TRAILER COUNT                82123
         ST    R0,CONTRAIL   SET TRAILERS LEFT TO DO             82123
         PRTLIST EQUALINE    PRINT MATCHED LINE                  82123
*DEEM*   TM    FOPT,FALL+FMTEXT  PRINT CONTEXT OR ALL ?         GP02365
*DEEM*   BZ    IGEQUAL       NEITHER; SKIP PRINTING             GP02365
         TM    FLAGS,FGPAGE  PAGE NUMBER SAVED BEFORE ?          82123
         BNZ   IGEQUAL       YES                                 82123
         OI    FLAGS,FGPAGE  SET PAGE NUMBER SAVED               82123
         PRTSPACE 0          FORCE PAGE NUMBER UPDATE            82123
         MVC   MEMPAGE,PUPAGE-PRTWORK+SYSPRINT  COPY PAGE NUMBER 82123
IGEQUAL  L     R7,CURROLD    FOR EQUALS, PRINT OLD CARD         GP10198
         PRTLIST EQUACARD,DEV=4  ALSO PUNCH IT                  GP02365
         BAS   R10,INBUFF                 ADD TO COMPARE BUFFER  82123
         SWAPR R8,R9         EXCHANGE BUFFERS                   GP03001
         BAS   R10,INBUFF                 ADD TO OTHER BUFFER    82123
         B     SWAP
CLCIG    CLC   BOFTEXT(0,R3),L'BUFSIZ(R10)  EXECUTED CLC FOR IGNORE
         SPACE 2
*  END OF FILE (OLD OR NEW)
*
EOLDNEW  MVI   OLDOFLGS-OLDINP(R9),0   SET DEVICE CLOSED         82123
         SWAPR R9,R8         EXCHANGE BUFFER POINTERS           GP03001
         TM    OLDOFLGS-OLDINP(R9),DCBOFOPN  OTHER STILL OPEN ?  82123
         BNZ   SWAP                       YES, BRANCH
         CLC   LOWOLD(4),HIGHOLD          BUFFER EMPTY?
         BNE   *+14                       NO, BRANCH
         CLC   LOWNEW(4),HIGHNEW          OTHER BUFFER EMPTY TOO?
         BE    EOLDSKIP      YES, QUIT                           82123
         BAS   R10,NOTEQUAL               FLUSH THE CURRENT BUFFERS
         B     ENDCOMP       NO NEED TO CHECK SKIP COUNT         82123
EOLDSKIP TM    FOPT,FMTEXT   CONTEXT ?                           82123
         BZ    ENDCOMP       NO                                  82123
         TM    FLAGS,FGPAGE  ANYTHING PRINTED ?                  82123
         BZ    ENDCOMP       NO; NOTHING LOST                    82123
         L     R0,CONSKIP                                        82123
         A     R0,CONLEAD    ANY UNPRINTED RECORDS ?             82123
         BNP   ENDCOMP       NO                                  82123
         ST    R0,CONSKIP    SET FAKE SKIP COUNT                 82123
         PRTLIST LOSTLINE    PRINT TRAILING GAP                  82123
         SPACE 1                                                 82123
*  TERMINATE -- CALLER WILL PRINT STATISTICS                     82123
*
ENDCOMP  OI    FLAGS,FGCOUNT  SET COUNT FIELDS VALID             82123
QUITCOMP LM    R0,R15,SAVEYALE   RELOAD REGISTERS, SET CODE,     82123
         BR    R10           AND RETURN                          82123
         SPACE 2
*  RE-INITIALIZE THE ACTIVE BUFFER -- SLIDE UNUSED RECORDS DOWN
*
INBUFF   L     R1,LOW(,R9)
         L     R2,CURR(,R9)
         ST    R1,CURR(,R9)
         L     R7,HIGH(,R9)                                      82123
         SR    R7,R6         FOR BXH TEST                        82123
NXTREC   BXH   R2,R6,REINIT  BRANCH IF ALL MOVED                 82123
         MVC   0(BUFLEN,R1),0(R2)                                82123
         AR    R1,R6         SET TO NEXT DESTINATION             82123
         B     NXTREC
REINIT   ST    R1,HIGH(,R9)                  RESET MAXIMUM POINTER
         BR    R10                                               82123
         SPACE 1                                                 82123
*        SUBROUTINE TO CALCULATE SIGNIFICANT LENGTH FOR ASM CARDS
*        MLENGTH IS USED IF NOT ASM OPTION, FOR CONTINUATION AND 82123
*        CONTINUATIONS OF CONTINUATIONS, AND COMMENTS CARDS.     82123
*                                                                82123
ASMTEST  STM   R14,R12,12(R13)  SAVE EVERYTHING                  82123
         TM    FLAGS,FASM    ASSEMBLER OPTION ?                  82123
         BZ    ASETMAX       NO; USE MAXIMUM LENGTH              82123
         LA    R7,BOFTEXT(,R3)  POINT TO CARD TEXT               82123
         CLI   71(R7),C' '   CONTINUATION CARD ?                 82123
         BNE   ASETCONT      YES; USE FULL LENGTH AND FLAG IT    82123
         TM    OLDOFLGS-OLDINP(R9),OFGCONT  PRIOR CONTINUATION ? 82123
         BZ    ASMNCONT      NO                                  82123
         NI    OLDOFLGS-OLDINP(R9),255-OFGCONT  RESET IT         82123
         B     ASETMAX       BUT USE FULL LENGTH FOR THIS ONE    82123
ASMNCONT CLI   0(R7),C'*'    COMMENTS CARD ?                     82123
         BE    ASETMAX       YES; USE FULL LENGTH                82123
         CLC   =C'.*',0(R7)  MACRO COMMENT ?                     89160
         BE    ASETMAX       YES; USE FULL LENGTH                82123
         LR    R5,R7         SET SCAN POINTER                    82123
         LA    R4,72-1       SET SCAN LENGTH (INCL. TRAIL. BLANK)
         LA    R14,2         LOOP FOR TWO FIELDS                 82123
AFNDBLK  XC    SCANTBL,SCANTBL  CLEAR SCAN TABLE                 82123
         MVI   SCANTBL+C' ',4   EXCEPT FOR BLANK                 82123
         EX    R4,SCANTR     LOOK FOR BLANK                      82123
         BNL   ASETMAX       NOT FOUND                           82123
         LA    R4,71(,R7)    GET END COLUMN                      82123
         SR    R4,R1         LESS CURRENT                        82123
         LR    R5,R1         SET NEW CURRENT                     82123
         MVI   SCANTBL,4                                         82123
         MVC   SCANTBL+1(L'SCANTBL-1),SCANTBL                    82123
         MVI   SCANTBL+C' ',0   SCAN FOR NON-BLANK               82123
         EX    R4,SCANTR     NON-BLANK ?                         82123
         BNL   ASETMAX       NO; SET MAXIMUM                     82123
         LA    R4,71(,R7)    GET END COLUMN                      82123
         SR    R4,R1         LESS CURRENT                        82123
         LR    R5,R1         SET NEW SCAN                        82123
         BCT   R14,AFNDBLK   GO TO SKIP SECOND FIELD             82123
*        POSITIONED TO FIRST BYTE OF OPERAND FIELD               82123
         XC    SCANTBL,SCANTBL  CLEAR TABLE AGAIN                82123
         MVI   SCANTBL+C' ',4   STOP ON BLANK                    82123
         MVI   SCANTBL+C'''',8  STOP ON APOSTROPHE               82123
         MVI   SCANTBL+C'(',12  STOP ON LEFT PARENTHESIS         82123
         MVI   SCANTBL+C')',16  STOP ON RIGHT PARENTHESIS        82123
         STH   R14,SCANP     CLEAR OPEN PARENTHESIS COUNT        82123
         SR    R2,R2         CLEAR FOR BRANCH                    82123
AQOFF    MVI   SCANQ,0       RESET QUOTE SWITCH                  82123
ASCAN    EX    R4,SCANTR     FIND NEXT DELIMITER                 82123
         BNL   ASETMAX       NOTHING ?                           82123
         LA    R4,70(,R7)                                        82123
         SR    R4,R1         SET FOR LENGTH AFTER THIS COLUMN    82123
         BM    ASETMAX       ALL DONE ?                          82123
         LA    R5,1(,R1)     SET TO NEXT COLUMN                  82123
         B     *(R2)         BRANCH BY TYPE                      82123
         B     ABLANK        BLANK FOUND                         82123
         B     AQUOTE        APOSTROPHE FOUND                    82123
         B     ALPAR         LEFT PARENTHESIS                    82123
*        B     ARPAR         RIGHT PARENTHESIS                   82123
         SPACE 1                                                 82123
ARPAR    TM    SCANQ,1       QUOTED STRING ?                     82123
         BNZ   ASCAN         YES; IGNORE PARENTHESES IN QUOTES   82123
         LH    R0,SCANP      GET OPEN COUNT                      82123
         SH    R0,=H'1'      REDUCE BY ONE                       82123
         BM    ASETMAX       SHOULD NOT HAVE HAPPENED ?          82123
         B     ASTHP         SET COUNT BACK                      82123
SCANTR   TRT   0(0,R5),SCANTBL  EXECUTED TRT                     82123
         SPACE 1                                                 82123
ALPAR    TM    SCANQ,1       QUOTED STRING ?                     82123
         BNZ   ASCAN         YES; IGNORE                         82123
         LA    R0,1          INCREMENT OPEN COUNT                82123
         AH    R0,SCANP                                          82123
ASTHP    STH   R0,SCANP      SET NEW OPEN COUNT                  82123
         B     ASCAN                                             82123
         SPACE 1                                                 82123
AQUOTE   TM    SCANQ,1       END OF STRING ?                     82123
         BNZ   AQOFF         YES; JUST RESET FLAG                82123
         LR    R14,R5        COPY NEXT COLUMN ADDRESS            82123
         BCTR  R14,0                                             82123
         BCTR  R14,0         BACKSPACE PRIOR TO APOSTROPHE       82123
         CLI   0(R14),C'I'   CHECK FOR ATTRIBUTE NOTATION I'     82123
         BL    AQUOTED       LOWER; STRING                       82123
         BE    ASCAN         INTEGER I'; NOT STRING              82123
         CLI   0(R14),C'T'   TEST FOR T'                         82123
         BH    AQUOTED       HIGHER; STRING                      82123
         BE    ASCAN         TYPE ATTRIBUTE; NOT STRING          82123
         CLI   0(R14),C'K'   COUNT K' ?                          82123
         BE    ASCAN                                             82123
         CLI   0(R14),C'L'   LENGTH L' ?                         82123
         BE    ASCAN                                             82123
         CLI   0(R14),C'N'   NUMBER N' ?                         82123
         BE    ASCAN                                             82123
         CLI   0(R14),C'S'   SCALING S' ?                        82123
         BE    ASCAN                                             82123
AQUOTED  OI    SCANQ,1       SET QUOTED STRING                   82123
         B     ASCAN         SCAN FOR END OF STRING              82123
         SPACE 1                                                 82123
ABLANK   CLC   SCANQ(L'SCANQ+L'SCANP),ZEROES  OPEN PAREN OR QUOTE ?
         BNE   ASCAN         YES; IGNORE BLANK                   82123
         BCTR  R5,0          POSITION TO BLANK                   82123
         SR    R5,R7         SET SIGNIFICANT LENGTH              82123
         B     ASETLEN       GO TO STORE LENGTH                  82123
         SPACE 1                                                 82123
ASETCONT OI    OLDOFLGS-OLDINP(R9),OFGCONT  SET CONTINUATION     82123
ASETMAX  IC    R5,MLENGTH    SET MAXIMUM CARD LENGTH             82123
ASETLEN  STC   R5,BOFSIZ(,R3)  SET LENGTH                        82123
         LM    R14,R12,12(R13)  RESTORE REGS                     82123
         BR    R10           RETURN TO CALLER                    82123
         EJECT
*     FLUSH OUT THE NON-EQUAL RECORDS
*
NOTEQUAL MVI   COMPCODE+3,4  SET RETURN CODE 4 - MISMATCH        82123
         LH    R1,COUNT
         LA    R1,1(,R1)
         STH   R1,COUNT
         ICM   R5,15,CONLEAD  LEADING CONTEXT CARDS STACKED ?    82123
         BZ    NOQNLEAD      NO                                  82123
         TM    CONTRAIL,X'80'  LINES LOST ?                      82123
         BZ    NOQLSET       NO                                  82123
         PRTLIST LOSTLINE    PRINT A GAP                         82123
NOQLSET  L     R7,STARTADD   GET FIRST SAVED CARD                82123
         BCTR  R5,0                                              82123
         MH    R5,=Y(BOXLEN)  OFFSET TO LAST CARD                82123
         AR    R5,R7         ADDRESS OF LAST CARD                82123
         LA    R4,BOXLEN     BXLE INCREMENT                      82123
NOQLLOOP PRTLIST CONTLINE    PRINT LEADING CONTEXT LINE          82123
         PRTLIST CONTCARD,DEV=4  ALSO PUNCH IT                  GP02365
         TM    FLAGS,FGPAGE  PAGE NUMBER SAVED BEFORE ?          82123
         BNZ   NOQLBUMP      YES; DO NEXT LINE                   82123
         OI    FLAGS,FGPAGE  SET PAGE NUMBER SAVED               82123
         PRTSPACE 0          FORCE PAGE NUMBER UPDATE            82123
         MVC   MEMPAGE,PUPAGE-PRTWORK+SYSPRINT  COPY PAGE NUMBER 82123
NOQLBUMP BXLE  R7,R4,NOQLLOOP  DO NEXT CARD                      82123
NOQNLEAD SR    R0,R0                                             82123
         TM    FOPT,FALL+FMTEXT  PRINT ALL OR CONTEXT ?          82123
         BZ    NOTEQTST      NO; CHECK FOR TRAILING IGNORES      82123
         ST    R0,CONLEAD    SET LEADING CARDS SAVED             82123
         ST    R0,CONSKIP    RESET RECORDS SKIPPED               82123
         MVC   CONTRAIL,CONMAX  SET TRAILING CARDS TO DO         82123
NOTEQTST L     R2,CURROLD    POINT TO CURRENT EQUAL CARD         82123
         L     R3,CURRNEW                                        82123
NOTEQLOP SR    R2,R6         GET PRIOR MISMATCHED CARD           82123
         C     R2,LOWOLD     IS THERE ONE ?                      82123
         BL    NOTEQDON      NO                                  82123
         SR    R3,R6         DITTO FOR NEW                       82123
         C     R3,LOWNEW                                         82123
         BL    NOTEQDON                                          82123
         IC    R1,BOFSIZ(,R2)  GET OLD CARD LENGTH               82123
         EX    R1,NEXTCLC    OLD LENGTH/TEXT MATCHES NEW ?       82123
         BNE   NOTEQDON      NO; NO MORE TRAILING IGNORES        82123
         AR    R0,R6         COUNT SIZE OF TRAILING IGNORES      82123
         B     NOTEQLOP      TRY PRIOR PAIR                      82123
NOTEQDON ST    R0,TRAILIG    SAVE TRAILING IGNORE SIZE           82123
         TM    FOPT,FMCOUNT+FALL  COUNT ONLY OR PRINT ALL ?      82123
         BNZ   MOVESOLD      EITHER; SKIP SPACING                82123
         TM    FOPT,FMTEXT   CONTEXT ?                           82123
         BZ    MOVESPC2      NO; DOUBLE-SPACE                    82123
         PRTSPACE 1          SINGLE SPACE FOR CONTEXT            82123
         B     MOVESOLD                                          82123
MOVESPC2 PRTSPACE 2          TRIPLE-SPACE                        82123
         PRTROOM 5           ENSURE SPACE FOR 5 CONTIGUOUS LINES 82123
MOVESOLD LA    R3,OLDINP                                         82123
MOVES    L     R5,LOW(,R3)                                       82123
         L     R7,CURR(,R3)  GET LAST CARD+                      82123
         SR    R5,R6         ADJUST FOR BXH                      82123
         SR    R7,R6                                             82123
         S     R7,TRAILIG    ALLOW FOR TRAILING EQUAL IGNORES    82123
FLUSHED  BXH   R5,R6,GETNEW  BRANCH IF DONE                      82123
         LA    R0,1                                              82123
         A     R0,NEWDIFF-NEWINP(,R3)                            82123
         ST    R0,NEWDIFF-NEWINP(,R3)  COUNT THE DIFFERING RECORDS
         TM    FOPT,FMCOUNT  COUNT OPTION ONLY ?                 82123
         BNZ   FLUSHED       YES; KEEP IT UP                     82123
         ICM   R0,7,BOFLINE(R5)  GET THE RECORD NUMBER           82123
         CVD   R0,DB         MAKE IT PACKED                      82123
         MVC   PRETTY#,=X'40202020212060'   SET EDIT PATTERN     82123
         LA    R1,PRETTY#+L'PRETTY#-2  SET SIGNIFICANCE ADDRESS  82123
         EDMK  PRETTY#(L'PRETTY#-1),DB+5  EDIT IT                82123
         BCTR  R1,0          SPACE BEFORE SIGNIFICANT DIGIT      82123
         MVI   0(R1),C'-'    MAKE IT PRETTY ?                    82123
         PRTLIST DATALINE    PRINT A PRETTY LINE ?               82123
         PRTLIST DATACARD,DEV=4  ALSO PUNCH IT                  GP02365
         TM    FLAGS,FGPAGE  PAGE NUMBER SAVED BEFORE ?          82123
         BNZ   FLUSHED       YES; DO NEXT LINE                   82123
         OI    FLAGS,FGPAGE  SET PAGE NUMBER SAVED               82123
         PRTSPACE 0          FORCE PAGE NUMBER UPDATE            82123
         MVC   MEMPAGE,PUPAGE-PRTWORK+SYSPRINT  COPY PAGE NUMBER 82123
         B     FLUSHED       DO NEXT LINE
         SPACE 1
GETNEW   LA    R1,NEWINP                                         82123
         CR    R3,R1                      NEW DONE?
         BE    GETNEWS       YES; GO TO CLEAN-UP                 82123
         LR    R3,R1                      NOW SETUP THE NEW BUFFER
         TM    FOPT,FMCOUNT  COUNT OPTION ONLY ?                 82123
         BNZ   MOVES         YES; COUNT OTHER SET                82123
         PRTLIST DASHLINE    PRINT A LINE OF DASHES              82123
         B     MOVES
         SPACE 1                                                 82123
GETNEWS  TM    FOPT,FMTEXT   CONTEXT MODE ?                      82123
         BZ    GETNEWT       NO                                  82123
         PRTSPACE 1          MAKE LISTING PRETTIER ?             82123
GETNEWT  ICM   R1,15,TRAILIG   ANY TRAILING IGNORES ?            82123
         BZR   R10           NONE; RETURN                        82123
         XC    TRAILIG,TRAILIG  CLEAR TRAILING IGNORES           82123
         TM    FOPT,FMTEXT+FALL  CONTEXT OR ALL ?                82123
         BZR   R10           NO; SKIP PRINTING                   82123
         L     R5,CURR+OLDINP  GET OLD CURRENT CARD              82123
         SR    R5,R1         SPACE TO FIRST IGNORE CARD          82123
         L     R7,CURR+NEWINP  GET NEW CURRENT CARD              82123
         SR    R7,R1         SPACE TO FIRST IGNORE CARD          82123
GETNEWQ  PRTLIST EQUACARD,DEV=4  ALSO PUNCH IT                  GP02365
*DEEM*   TM    FOPT,FMTEXT+FALL  CONTEXT OR ALL ?               GP02365
*DEEM*   BZ    GETNEWQU      NO; SKIP PRINTING                  GP02365
         PRTLIST EQUALINE    PRINT MATCHED LINE                  82123
         INC   CONTRAIL,INC=-1 COUNT TRAILERS PRINTED            82123
         LTR   R0,R0         DONE ?                              82123
         BNPR  R10           YES; RETURN                         82123
         AR    R5,R6         SPACE TO NEXT CARD                  82123
         AR    R7,R6                                             82123
         C     R7,CURR+NEWINP  ALL DONE ?                        82123
         BL    GETNEWQ       NO; PRINT NEXT ONE                  82123
         BR    R10           RETURN TO CALLER                    82123
         EJECT
         PRINT NOGEN
PATPRINT PRTWORK SYSPRINT,TITLE=4,PAGE=0                         82123
SUMPRINT PRTWORK SYSTERM,SUMPRINT,TITLE=5                        82123
SYSPUNCH PUNWORK SYSPUNCH    COMBINED TEXT (OLD+NEW+MATCHED)    GP02365
PATINP   INPWORK ANYDD,EODAD=EOLDNEW,WIDTH=80,FILL=C' ',EDIT=128,JFCB=0
PATINPLN EQU   *-PATINP                                          82123
IGNORE   INPWORK IGNORE,FILL=C' ',EDIT=128  NO EDITING           82123
         SPACE 1                                                 82123
PATDIR   DCB   DDNAME=MOVED,DEVD=DA,DSORG=PS,EXLST=1,EODAD=3,BUFNO=2,  *
               MACRF=(GL),RECFM=F,LRECL=256,BLKSIZE=256,KEYLEN=8 82123
PATDIRLN EQU   *-PATDIR                                          82123
         SPACE 1                                                 82123
WORKFD   FD    WORKLINE,L'WORKLINE,NL                           GP02338
         FD    *END                                             GP02338
         SPACE 1                                                 82123
MEMHEAD  FDPRT MEMNAME,NL,PADR   MEMBER(PDS) OR BLANKS(SEQ)      82123
         FDTM  DIRF,DFOB+DFNB,BO=MEMHEADC  OK IF BOTH            82123
         FDPRT 'FOUND ONLY IN',PAD                               82123
         FDTM  DIRF,DFNB,BO=MEMHEAD2  SHOW NEW ONLY              82123
         FDEXEC FDOLD,FDOLDX                                     82123
         FDPRT *END                                              82123
MEMHEADC FDEXEC FDOLD,FDOLDX                                     82123
         FDPRT 'COMPARED TO',PAD                                 82123
MEMHEAD2 FDEXEC FDNEW,FDNEWX                                     82123
         FDPRT *END                                              82123
         SPACE 1                                                 82123
SUMCNT   FDTM  FLAGS,FGSEQ,BNZ=SUMCNTZZ  SKIP IF SEQUENTIAL      82123
         FDTM  RETCODE+3,32,BNZ=SUMCNTZZ  ALSO SKIP ON MAJOR ERROR
         FDOPT NL,CC=C'-'                                        82123
         FDPRT 'PDS TOTALS:',PADR                                82123
         FDPRT CNTOMEM,I,RADJ,PADL,LEN=6  SHOW OLD MEMBER COUNT  82123
         FDPRT 'OLD MEMBERS,',PAD                                82123
         FDCLC CNTOUNM,ZEROES,4,BE=SUMCNTNO  SKIP IF NONE UNMATCHED
         FDPRT CNTOUNM,I,PADL                                    82123
         FDPRT 'UNMATCHED,',PAD                                  82123
SUMCNTNO FDPRT CNTNMEM,I,RADJ,PADL,LEN=6  SHOW NEW MEMBER COUNT  82123
         FDPRT 'NEW MEMBERS,',PAD                                82123
         FDCLC CNTNUNM,ZEROES,4,BE=SUMCNTNN  SKIP IF NONE UNMATCHED
         FDPRT CNTNUNM,I,PADL                                    82123
         FDPRT 'UNMATCHED,',PAD                                  82123
SUMCNTNN FDTM  FOPT,FMDIR,BNZ=SUMCNTYY  DONE IF DIRECTORY ONLY   82123
         FDPRT CNTEQUAL,I,RADJ,LEN=8                             82123
         FDPRT 'EQUAL,',PADL                                     82123
         FDGOTO SUMCNTYZ                                         89160
SUMCNTYY FDCLC CNTNEQ,ZEROES,4,BE=SUMCNTZZ  SKIP IF ALL EQUAL    89160
SUMCNTYZ FDPRT CNTNEQ,I,PAD                                      82123
         FDPRT 'UNEQUAL'                                         82123
SUMCNTZZ FDPRT *END                                              82123
         SPACE 1                                                 82123
MEMSUM   FDOPT NL                                                82123
         FDTM  FLAGS,FGSEQ,BNZ=MEMTOT                            82123
         FDTM  DIRF,DFOB,BZ=MEMSUMNO   SKIP IF NOT OLD           82123
         FDPRT PDORALIS,L'PDORALIS+L'PDORNAME  *MEMBER           82123
         FDPRT PDORSSI,L'PDORSSI,PADL   +SSI OR BLANKS           82123
         FDTM  FLAGS,FGCOUNT,BZ=MEMSUMNO  SKIP IF NO COUNTS      82123
         FDPRT OLDCNT,I,RADJ,PADL,LEN=6  SHOW RECORD COUNT       82123
         FDCLC OLDDIFF,ZEROES,4,BE=MEMSUMNO  SKIP IF NONE UNMATCHED
         FDPRT OLDDIFF,I,RADJ,PADL,LEN=6                         82123
MEMSUMNO FDOPT SBA=(*,42)    INDENT A BIT                        82123
         FDTM  DIRF,DFNB,BZ=MEMSUMNN   SKIP IF NOT NEW           82123
         FDPRT PDNRALIS,L'PDNRALIS+L'PDNRNAME                    82123
         FDPRT PDNRSSI,L'PDNRSSI,PADL                            82123
         FDTM  FLAGS,FGCOUNT,BZ=MEMSUMNN  SKIP IF NO COUNTS      82123
         FDPRT NEWCNT,I,RADJ,PADL,LEN=6  SHOW RECORD COUNT       82123
         FDCLC NEWDIFF,ZEROES,4,BE=MEMSUMNN  SKIP IF NONE UNMATCHED
         FDPRT NEWDIFF,I,RADJ,PADL,LEN=6                         82123
MEMSUMNN FDOPT SBA=(*,84)    INDENT A BIT                        82123
         FDTM  DIRF,DFOB+DFNB,BM=MEMSUMNM  UNMATCHED MEMBER ?    82123
         FDTM  FLAGS,FGCOUNT,BNZ=MEMSUMTQ                        82123
         FDTM  FOPT,FMDIR,BNZ=MEMSUMLK  DONE IF DIRECTORY ONLY   89160
MEMSUMOV FDPRT ERRPOINT,L'MSGTABOV,*   INDIRECT MESSAGE          82123
         FDGOTO MEMSUMPG                                         82123
MEMSUMTQ FDCLC COUNT,ZEROES,BE=MEMSUMEQ                          82123
         FDPRT COUNT,I,RADJ,LEN=10                               82123
         FDPRT 'GROUPS OF CHANGES FOUND',PADL                    82123
         FDGOTO MEMSUMPG                                         82123
MEMSUMEQ FDPRT '  EQUAL'                                         82123
         FDCLC PDORSSI,PDNRSSI,L'PDORSSI,BE=MEMSUMPG             82130
         FDPRT ', BUT SSI MISMATCH'                              82130
         FDGOTO MEMSUMPG                                         82123
MEMSUMNM FDSPACE 15                                              82123
         FDPRT 'MEMBER IN'                                       82123
         FDTM  DIRF,DFOB,BZ=MEMSUMMN                             82123
         FDPRT LITOLD,3,PAD                                      82123
         FDGOTO MEMSUMMC                                         82123
MEMSUMMN FDPRT LITNEW,3,PAD                                      82123
MEMSUMMC FDPRT 'PDS ONLY'                                        82123
MEMSUMPG FDCLC MEMPAGE,ZEROES,2,BE=MEMSUMZZ                      82123
         FDOPT SBA=(*,-10)                                      GP08243
         FDPRT 'PAGE'                                            82123
         FDPRT MEMPAGE,I,RADJ,LEN=5                              82123
         FDGOTO MEMSUMZZ                                         89160
MEMSUMLK FDTM  PDOTYPE,PDOTLKED,BZ=MEMSUMYY                      89160
         FDTM  PDNTYPE,PDNTLKED,BZ=MEMSUMYZ                      89160
         FDOPT SBA=(*,21)                                        89160
         FDPRT PDS2STOR-PDS2(R4),3,HEX  MOD. LENGTH              89160
         FDOPT SBA=(*,62)                                        89160
         FDPRT PDS2STOR-PDS2(R5),3,HEX                           89160
         FDCLC PDS2STOR-PDS2(R4),PDS2STOR-PDS2(R5),3,BE=MEMSUMZZ 89160
         FDOPT SBA=(*,98)                                        89160
         FDPRT 'MODULE SIZES DIFFER',PADL                        89160
         FDGOTO MEMSUMZZ                                         89160
MEMSUMYY FDTM  PDNTYPE,PDNTLKED,BZ=MEMSUMZZ  NEITHER LLOAD-MODULE
MEMSUMYZ FDOPT SBA=(*,98)                                        89160
         FDPRT 'MODULE TYPE MISMATCH (LOAD MOD/OTHER)'           89160
MEMSUMZZ FDPRT *END                                              82123
         SPACE 1                                                 82123
MSGTABOV DC    C'COMPARE TERMINATED BY BUFFER OVERFLOW'          82123
MSGIOERR DC    CL(L'MSGTABOV)'COMPARE TERMINATED BY I/O ERROR      '
         SPACE 1                                                 82123
MEMTOT   FDOPT NL,CC=C'-'                                        82123
         FDCLI MEMNAME,C' ',BE=MEMTOTSQ  BRANCH IF SEQUENTIAL    82123
         FDPRT 'MEMBER'                                          82123
         FDPRT MEMNAME,PAD                                       82123
MEMTOTSQ FDTM  DIRF,DFOB,BZ=MEMTOTNO   SKIP IF NOT OLD           82123
         FDTM  FLAGS,FGCOUNT,BZ=MEMTOTNO  SKIP IF NO COUNTS      82123
         FDPRT OLDCNT,I,RADJ,PADL,LEN=6  SHOW RECORD COUNT       82123
         FDPRT 'OLD RECORDS,',PAD                                82123
         FDCLC OLDDIFF,ZEROES,4,BE=MEMTOTNO  SKIP IF NONE UNMATCHED
         FDPRT OLDDIFF,I,PADL                                    82123
         FDPRT 'UNMATCHED,',PAD                                  82123
MEMTOTNO FDTM  DIRF,DFNB,BZ=MEMTOTNN   SKIP IF NOT NEW           82123
         FDTM  FLAGS,FGCOUNT,BZ=MEMTOTNN  SKIP IF NO COUNTS      82123
         FDPRT NEWCNT,I,RADJ,PADL,LEN=6  SHOW RECORD COUNT       82123
         FDPRT 'NEW RECORDS,',PAD                                82123
         FDCLC NEWDIFF,ZEROES,4,BE=MEMTOTNN  SKIP IF NONE UNMATCHED
         FDPRT NEWDIFF,I,PADL                                    82123
         FDPRT 'UNMATCHED,',PAD                                  82123
MEMTOTNN FDTM  DIRF,DFOB+DFNB,BM=MEMTOTNM  UNMATCHED MEMBER ?    82123
         FDTM  FOPT,FMDIR,BNZ=MEMTOTZZ  DONE IF DIRECTORY ONLY   82123
         FDTM  FLAGS,FGCOUNT,BNZ=MEMTOTTQ                        82123
MEMTOTOV FDPRT ERRPOINT,L'MSGTABOV,*   INDIRECT MESSAGE          82123
         FDGOTO MEMTOTZZ                                         82123
MEMTOTTQ FDCLC COUNT,ZEROES,BE=MEMTOTEQ                          82123
         FDPRT COUNT,I,RADJ,LEN=8                                82123
         FDPRT 'GROUPS OF CHANGES FOUND',PADL                    82123
         FDGOTO MEMTOTZZ                                         82123
MEMTOTEQ FDPRT 'EQUAL'                                           82123
         FDCLC PDORSSI,PDNRSSI,L'PDORSSI,BE=MEMTOTZZ             82130
         FDPRT ', BUT SSI MISMATCH'                              82130
         FDGOTO MEMTOTZZ                                         82123
MEMTOTNM FDPRT 'MEMBER IN'                                       82123
         FDTM  DIRF,DFOB,BZ=MEMTOTMN                             82123
         FDPRT LITOLD,3,PAD                                      82123
         FDGOTO MEMTOTMC                                         82123
MEMTOTMN FDPRT LITNEW,3,PAD                                      82123
MEMTOTMC FDPRT 'PDS ONLY'                                        82123
MEMTOTZZ FDPRT *END                                              82123
         SPACE 1                                                 82123
FDOLD    FDPRT JFCBDSNM-INFMJFCB+OJFCB,L'JFCBDSNM,DEB            82123
         FDTM  JFCBIND1-INFMJFCB+OJFCB,JFCPDS,BZ=FDOLDM  PO/MEM ?
         FDPRT '('                                               82123
         FDPRT JFCBELNM-INFMJFCB+OJFCB,L'JFCBELNM,DEB            82123
         FDPRT ')'                                               82123
FDOLDM   FDCLI JFCBVOLS-INFMJFCB+OLDJFCB,C' ',BL=FDOLDX,BE=FDOLDX
         FDPRT 'ON',PAD                                          82123
         FDPRT JFCBVOLS-INFMJFCB+OLDJFCB,6,DEB                   82123
FDOLDX   FDPRT *END                                              82123
         SPACE 1                                                 82123
FDNEW    FDPRT JFCBDSNM-INFMJFCB+NJFCB,L'JFCBDSNM,DEB            82123
         FDTM  JFCBIND1-INFMJFCB+NJFCB,JFCPDS,BZ=FDNEWM  PO/MEM ?
         FDPRT '('                                               82123
         FDPRT JFCBELNM-INFMJFCB+NJFCB,L'JFCBELNM,DEB            82123
         FDPRT ')'                                               82123
FDNEWM   FDCLI JFCBVOLS-INFMJFCB+NEWJFCB,C' ',BL=FDNEWX,BE=FDNEWX
         FDPRT 'ON',PAD                                          82123
         FDPRT JFCBVOLS-INFMJFCB+NEWJFCB,6,DEB                   82123
FDNEWX   FDPRT *END                                              82123
         SPACE 1                                                 82123
DATALINE FDPRT PRETTY#,NL    DISPLAY LOCAL LINE NUMBER           82123
         FDPRT NEWLIT-NEWINP(R3),3,PADL   OLD/NEW ID             82123
         FDREPT 3,C'-'       MORE DASHES                         82123
         FDPRT BOFTEXT(R5),L'BOFTEXT      DATA RECORD            82123
         FDREPT 3,C'-'       STILL MORE DASHES                   82123
         FDPRT NEWLIT-NEWINP(R3),3,PADR   OLD/NEW ID             82123
         FDPRT PRETTY#,DEB   LEFT-ADJUSTED NUMBER                82123
         FDPRT *END                                              82123
         SPACE 1                                                 82123
EQUALINE FDPRT BOFLINE(R5),L'BUFLINE,I,NL,RADJ,LEN=L'PRETTY#-1   82123
         FDPRT LITOLD,3,RADJ,LEN=5                               82123
         FDREPT 3,C'='                                           82130
         FDPRT BOFTEXT(R7),L'BOFTEXT                             82123
         FDREPT 3,C'='                                           82130
         FDPRT LITNEW,5                                          82123
         FDPRT BOFLINE(R7),L'BUFLINE,I                           82123
         FDPRT *END                                              82123
         SPACE 1                                                 82123
DASHLINE FDPRT ' ',NL,LEN=L'PRETTY#+1                            82123
         FDPRT SEPLINE       SEPARATOR                           84065
         FDPRT *END                                              82123
         SPACE 1                                                 82123
CONTLINE FDPRT BOXOLD(R7),L'BOXOLD,I,NL,RADJ,LEN=L'PRETTY#-1     82123
         FDPRT LITOLD,3,RADJ,LEN=5                               82123
         FDREPT 3,C'='                                           82130
         FDPRT BOXTEXT(R7),L'BOXTEXT                             82123
         FDREPT 3,C'='                                           82130
         FDPRT LITNEW,5                                          82123
         FDPRT BOXNEW(R7),L'BOXNEW,I                             82123
         FDPRT *END                                              82123
         SPACE 1                                                 82123
LOSTLINE FDPRT ' ',NL,LEN=L'PRETTY#+1                            82123
         FDREPT 20,C'='                                          82123
         FDPRT '/    /'                                          82123
         FDREPT 40,C'='                                          82123
         FDPRT '/    /'                                          82123
         FDREPT 20,C'='                                          82123
         FDPRT CONSKIP,I,RADJ,LEN=15                             82123
         FDPRT 'RECORDS SKIPPED',PADL                            82123
         FDPRT *END                                              82123
         SPACE 1                                                 82123
ERMNOSTO VCON '0 ***** SEVERE STORAGE SHORTAGE - INCREASE REGION *****'
         SPACE 1                                                 82123
COMPMSG  FDOPT NL,CC=C'0'                                        82123
         FDCLC RETCODE,ZEROES,4,BNE=COMPMSGN                     82123
         FDPRT 'NO PROBLEMS ENCOUNTERED'                         82123
         FDPRT *END                                              82123
COMPMSGN FDPRT 'LEVEL'                                           82123
         FDPRT RETCODE,I,PAD                                     82123
         FDPRT 'ERRORS ENCOUNTERED'                              82123
         FDPRT *END                                              82123
         SPACE 1                                                 82123
ERMNODD  FDOPT NL,CC=C'0'                                        82123
         FDREPT 5,C'*'                                           82123
         FDPRT OLDLIT-OLDWORK(R3),8,DEB,PAD                      82123
         FDPRT 'DD CARD OMITTED OR UNUSABLE ',PAD                82123
         FDREPT 5,C'*'                                           82123
         FDPRT *END                                              82123
         SPACE 1                                                 82123
ERMNOFMT FDOPT NL,CC=C'0'                                        82123
         FDPRT '***** LOCATE FAILED FOR DATASET',PADR            82123
         FDPRT JFCBDSNM-INFMJFCB+OLDJFCB-OLDWORK(R3),L'JFCBDSNM,DEB
         FDPRT 'ON',PAD                                          82123
         FDPRT JFCBVOLS-INFMJFCB+OLDJFCB-OLDWORK(R3),6,DEB       82123
         FDPRT '*****',PADL                                      82123
         FDPRT *END                                              82123
         SPACE 1                                                 82123
ERMPOPS  FDOPT NL,CC=C'0'                                        82123
         FDREPT 5,C'*'                                           82123
         FDPRT 'NEW DD ORGANIZATION NOT SAME AS OLD DD ',PAD     82123
         FDREPT 5,C'*'                                           82123
         FDPRT *END                                              82123
         SPACE 1                                                 82123
ERMOPEN  FDPRT '***** OPEN FAILED FOR',NL,PADR                   82123
         FDPRT JFCBDSNM-INFMJFCB+NEWJFCB-NEWWORK(R3),L'JFCBDSNM,DEB
         FDTM  JFCBIND1-INFMJFCB+NEWJFCB-NEWWORK(R3),JFCPDS,BZ=ERMOPENM
         FDPRT '('                                               82123
         FDPRT JFCBELNM-INFMJFCB+NEWJFCB-NEWWORK(R3),L'JFCBELNM,DEB
         FDPRT ')'                                               82123
ERMOPENM FDCLI JFCBVOLS-INFMJFCB+NEWJFCB-NEWWORK(R3),C' ',             *
               BL=ERMOPENX,BE=ERMOPENX                           82123
         FDPRT 'ON',PAD                                          82123
         FDPRT JFCBVOLS-INFMJFCB+NEWJFCB-NEWWORK(R3),6,DEB       82123
ERMOPENX FDPRT '*****',PADL                                      82123
         FDPRT *END                                              82123
         SPACE 1                                                 82123
MAXCONTX DC    F'10'         NUMBER OF CONTEXT CARDS             82123
ALLONES  DC    8X'FF'   1/2  END OF DIRECTORY - FAKE MEMBER      82123
ZEROES   DC    8X'00'   2/2  CONSTANT ZERO                       82123
LITOLD   DC    CL8'OLD'                                          82123
LITNEW   DC    CL8'NEW'                                          82123
         SPACE 1                                                 82123
SEPLINE  DC    0CL(6+80+6)' ',6C'-',8CL10'----+----:',6C'-'      84065
         ORG   SEPLINE+6     FIRST 'CARD' COLUMN                 84065
         DC    C'|'          SPECIAL FOR COLUMN 1                84065
         ORG   SEPLINE+6+79  GET LAST COLUMN                     84065
         DC    C'|'          SPECIAL FOR LAST COLUMN             84065
         ORG   ,                                                 84065
         SPACE 1                                                 82123
         LTORG ,                                                 81317
         SPACE 2                                                GP02365
DATACARD FDPRT BOFTEXT(R5),72,NL   DATA RECORDSANS SERIAL       GP02365
         FDPRT NEWLIT-NEWINP(R3),1   OLD/NEW ID (O/N)           GP02365
         FDPRT BOFTEXT+73(R5),7        REST OF SEQUENCE NUMBER  GP02365
         FDPRT *END                                             GP02365
         SPACE 1                                                GP02365
EQUACARD FDPRT BOFTEXT(R7),L'BOFTEXT,NL                         GP10198
         FDPRT *END                                             GP02365
         SPACE 1                                                GP02365
DASHCARD FDPRT '*',NL                                           GP02365
         FDPRT SEPLINE       SEPARATOR                          GP02365
         FDPRT *END                                             GP02365
         SPACE 1                                                GP02365
CONTCARD FDPRT BOXTEXT(R7),L'BOXTEXT,NL                         GP10198
         FDPRT *END                                             GP02365
         SPACE 1                                                 82123
HEADER   FDOPT NL,CC=C'#'    REQUEST PAGE NUMBERING              82123
         FDSPACE 50                                              82123
         FDPRT HEADDC        PRINT HEADER                        82123
         FDPRT *END                                              82123
         SPACE 1                                                 82123
SUMHEAD  FDOPT NL,CC=C'#'    REQUEST PAGE NUMBERING              82123
         FDSPACE 28                                              82123
         FDPRT 'SUMMARY LISTING  *** ',PADR                      82123
         FDPRT HEADDC                                            82123
         FDPRT *END                                              82123
HEADDC   DC    C'YALE COMPARE PROGRAM'                           82123
         SPACE 1                                                 82123
SUB2HEAD FDPRT 'COMPARE OF',NL,PADR                              82123
         FDEXEC FDOLD,FDOLDX                                     82123
         FDOPT SBA=(*,65)                                        82123
         FDPRT 'TO',PADR                                         82123
         FDEXEC FDNEW,FDNEWX                                     82123
         FDPRT *END                                              82123
         SPACE 1                                                 82123
COLHEAD  FDPRT 'OLD MEMBER     SSI  COUNT   DIFF',NL,LEN=41      82123
         FDPRT 'NEW MEMBER     SSI  COUNT   DIFF',LEN=41         82123
         FDSPACE 10                                              82123
         FDPRT 'STATUS'                                          82123
         FDPRT *END                                              82123
         SPACE 1                                                 82123
CAPCYMSG FDPRT '----- UP TO',NL                                  82123
         FDPRT DB,4,I,PAD                                        82123
         FDPRT 'UNMATCHED CARDS CAN BE PROCESSED IN THIS REGION -----'
         FDPRT ' ',NL        FORCE A NEW LINE                    82123
         FDPRT ' ',NL        AND ANOTHER                         82123
         FDPRT *END                                              82123
         SPACE 1                                                 82123
*        TABLE OF INSIGNIFICANT CARDS - MUST BE IN COLLATING SEQUENCE
*        CARDS IN THIS TABLE DO NOT TERMINATE A MISMATCH SEQUENCE.
*          FORMAT: AL1-SIGNIFICANT LENGTH-1 / CL72 TEXT          82123
DFTIG    DC    AL1(72-1),CL72'         EJECT   '                    ABL
DFTIG2   DC    AL1(18-1),CL72'         EJECT , '                 81318
         DC    AL1(72-1),CL72'         SPACE   '                    ABL
         DC    AL1(18-1),CL72'         SPACE , '                 81318
         DC    AL1(18-1),CL72'         SPACE 1 '                    ABL
         DC    AL1(18-1),CL72'         SPACE 2 '                    ABL
         DC    AL1(18-1),CL72'         SPACE 3 '                    ABL
         DC    AL1(18-1),CL72'         SPACE 4 '                 89160
         DC    AL1(18-1),CL72'         SPACE 5 '                 89160
         DC    AL1(18-1),CL72'         SPACE 6 '                 89160
         DC    AL1(72-1),CL72'.*               '                 81318
         DC    AL1(72-1),CL72'*                '                    ABL
         DC    AL1(72-1),70C'*',C'  '                            89160
         DC    AL1(72-1),C'*',CL70' ',C'*'                       89160
         DC    AL1(72-1),C'*',CL69' ',C'* '                      89160
         DC    AL1(72-1),71C'*',C' '                             82123
         DC    AL1(72-1),C'*',69C'-',C'* '                       89160
         DC    AL1(72-1),C'*',70C'-',C'*'                        89160
ENDDFTIG EQU   *                                                    ABL
         SPACE 1                                                 82123
*        MAPPING OF RECORD BUFFER                                82123
*              SUBDIVISION OF BUFFER GOTTEN (STARTADD=>HIGHADDR) 82123
*                                                                82123
BUFMAP   DSECT ,                                                 82123
BUFLINE  DC    AL3(0)        RECORD NUMBER                       82123
BUFSIZ   DC    X'00'         COMPARE LENGTH                      82123
BUFTEXT  DC    CL80' '       RECORD TEXT                         82123
BUFLEN   EQU   *-BUFMAP      LENGTH OF ONE ENTRY                 82123
         SPACE 1                                                 82123
BOFLINE  EQU   BUFLINE-BUFMAP,L'BUFLINE,C'F'    OFFSET TO LINE NUMBER
BOFSIZ   EQU   BUFSIZ-BUFMAP,L'BUFSIZ,C'X'  OFFSET FOR COMPARE SIZE
BOFTEXT  EQU   BUFTEXT-BUFMAP,L'BUFTEXT,C'C'    OFFSET TO TEXT   82123
         SPACE 1                                                 82123
BOXOLD   EQU   0,L'BUFLINE,C'F'   CONTEXT - OLD LINE NUMBER      82123
BOXNEW   EQU   L'BUFLINE,L'BUFLINE,C'F'     NEW LINE NUMBER      82123
BOXTEXT  EQU   2*L'BUFLINE,L'BUFTEXT,C'C'   NEW TEXT             82123
BOXLEN   EQU   2*L'BUFLINE+L'BUFTEXT    SIZE OF SAVED ENTRY      82123
         EJECT ,                                                 82123
SAVE     DSECT ,                                                 81317
DB       DS    2D            DOUBLE DOUBLE-WORD WORK AREA        82123
         SERVDEFS ,          SERVICE AREA                       GP04114
SAVEYALE DS    15F      1/2  SAVE AREA FOR COMPARE PROGRAM       82123
COMXC    EQU   *      1.5/2  DEFINITION FOR CLEARING             82123
COMPCODE DS    F        2/2  COMPARE RETURN CODE                 82123
TRAILIG  DS    F             SIZE OF TRAILING EQUAL IGNORE CARDS 82123
CONLEAD  DS    F             NUMBER OF LEADING = CARDS SAVED     82123
CONTRAIL DS    F             NUMBER OF TRAILING = CARDS PRINTED  82123
CONSKIP  DS    F             NUMBER OF LEADING CARDS SKIPPED     82123
MEMPAGE  DS    H             FIRST PRINT PAGE FOR MEMBER         82123
COUNT    DS    H             NUMBER OF MISMATCH GROUPS FOUND     82123
COMXCLEN EQU   *-COMXC       LENGTH TO CLEAR                     82123
         SPACE 1                                                 82123
GETMAIN  DC    0F'0',XL(GETMEANL)'0'   GETMAIN MF=L              82123
GETMIN   DS    A             MINIMUM TO GET                      82123
GETMAX   DS    A             MAXIMUM TO GET                      82123
GETRESRV DC    A(24*1024)    AMOUNT TO RESERVE FOR OS            85079
STARTADD DS    A             START ADDRESS OF GOTTEN STORAGE     82123
SIZE     DS    A             SIZE OBTAINED                       82123
HIGHSTOR DS    A             END OF STORAGE+1                    82123
FIRSTIG  DC    F'0'                     POINTER TO FIRST IGNORE RECORD
         DC    F'80'         BXLE INCREMENT                      82123
LASTIG   DC    F'0'                     POINTER TO LAST IGNORE RECORD
CONMAX   DC    F'10'         NUMBER OF CONTEXT CARDS             82123
CNTOMEM  DS    F             NUMBER OF OLD MEMBERS               82123
CNTOUNM  DS    F             NUMBER OF UNMATCHED OLD MEMBERS     82123
CNTNMEM  DS    F             NUMBER OF NEW MEMBERS               82123
CNTNUNM  DS    F             NUMBER OF UNMATCHED NEW MEMBERS     82123
CNTEQUAL DS    F             NUMBER OF EQUAL MEMBERS             82123
CNTNEQ   DS    F             NUMBER OF UNEQUAL MEMBERS           82123
ERRPOINT DC    A(MSGTABOV)   POINTER TO CURRENT ERROR MESSAGE    82123
BLANKS   DC    CL80' '       SPACES
SYSPRINT PRTWORK SYSPRINT,TITLE=4,PAGE=0                         82123
SYSPRINL EQU   *-SYSPRINT                                        82123
         SPACE 1                                                 82123
MEMNAME  DC    CL8' '        NAME OF CURRENT MEMBER              82123
ERRUNIT  DC    AL1(2)        DEVICE NUMBER OF ERROR PRINT UNIT   82123
DIRUNIT  DC    AL1(2)        DEVICE TO RECEIVE DIR/COUNT OUTPUT  82123
FLAGS    DC    X'00'         PROCESSING FLAGS                    82123
FGSEQ    EQU   X'80'           COMPARING TWO SEQUENTIAL FILES    82123
FGPDS    EQU   X'40'           PDS WITHOUT MEMBER FOUND          82123
FGMAIN   EQU   X'20'           BUFFER GETMAIN DONE               82123
FGPAGE   EQU   X'10'           CURRENT PAGE NUMBER SAVED         82123
FGCOUNT  EQU   X'08'           RECORD COUNTS ARE VALID           82123
FASM     EQU   X'04'           COMPARE TO END OF OPERAND ONLY    82123
FCOB     EQU   X'02'           COMPARE COBOL COLUMNS 7-71 ONLY   89160
DIRF     DC    X'00'         DIRECTORY PROCESSING FLAG           82123
DFOB     EQU   X'80'           INCREMENT TO NEXT MEMBER          82123
DFOF     EQU   X'40'           END-FILE ON OLD DIRECTORY         82123
DFNB     EQU   X'08'           INCREMENT TO NEXT MEMBER          82123
DFNF     EQU   X'04'           END-FILE ON NEW DIRECTORY         82123
FOPT     DC    X'00'         MAJOR OPTION FLAGS                  82123
FALIAS   EQU   X'80'           PROCESS ALIAS ENTRIES AS MEMBERS  82123
FMUPP    EQU   X'40'           COMPARE IN UPPER CASE            GP04114
FMDIR    EQU   X'20'           SKIP MEMBER COMPARES (D.E. LIST ONLY)
FMCOUNT  EQU   X'10'           COUNT, BUT PRINT DIRECTORY ONLY   82123
FMOLD    EQU   X'08'           PRINT OLD (DELETED) MEMBERS       82123
FMNEW    EQU   X'04'           PRINT NEW (ADDED) MEMBERS         82123
FALL     EQU   X'02'           PRINT ALL RECORDS READ            82123
FMTEXT   EQU   X'01'           PRINT MATCHED CARDS ALSO          82123
MLENGTH  DC    AL1(72-1)     COMPARE LENGTH - 1                  82123
PRETTY#  DC    CL7' '        EDITED COUNTER                      82123
SCANQ    DC    X'00'         ASM SCAN - QUOTE FLAG               82123
SCANP    DC    H'0'          ASM SCAN - # OF OPEN PARENTHESES    82123
SCANTBL  DC    XL256'0'      TRT TABLE FOR ASM SCAN              82123
         SPACE 2
OLDWORK  DS    0F            START OF 'OLD' CONTROL BLOCKS       82123
OJFCB    DC    CL176' '      DIRECTORY JFCB
OLDDIR   DCB   BLKSIZE=256,DEVD=DA,DSORG=PS,DDNAME=OLD,EXLST=OXLST,    X
               KEYLEN=8,LRECL=256,MACRF=(GL),RECFM=F,EODAD=OLDDEOD
OLDJFCB  DC    CL176' '
OLDINP   MAPINP PREFIX=PO,DSECT=NO    @INPREAD MEMBER INPUT      82123
OLDJFCBP DC    A(OLDJFCB)    POINTER TO JFCB                     82123
LOWOLD   DC    F'0'                     FIRST OLD RECORD
HIGHOLD  DC    F'0'                     HIGHEST USED OLD RECORD
CURROLD  DC    F'0'                     CURRENT OLD RECORD
OLDLIT   DC    CL8'OLD'      OLD OR SYSUT1 DD NAME               82123
OXLST    DC    0A(0),X'87',AL3(OJFCB)                            82123
OLDCNT   DC    F'0'          COUNT OF OLD RECORDS                82123
OLDDIFF  DC    F'0'          COUNT OF UNMATCHED RECORDS          82123
OLDBXLE  SERVPDS PFX=PDO,DSECT=NO,RETURN=YES   DIRECTORY WORK AREA
OLDDEV   DC    AL1(1)        @INPREAD DEVICE NUMBER              82123
OLDOFLGS DC    X'00'         (FAKE) DCBOFLGS                     82123
         SPACE 2
NEWWORK  DS    0F            START OF 'NEW' CONTROL BLOCKS       82123
NJFCB    DC    CL176' '      DIRECTORY JFCB
NEWDIR   DCB   BLKSIZE=256,DEVD=DA,DSORG=PS,DDNAME=NEW,EXLST=NXLST,    X
               KEYLEN=8,LRECL=256,MACRF=(GL),RECFM=F,EODAD=NEWDEOD
NEWJFCB  DC    CL176' '
NEWINP   MAPINP PREFIX=PN,DSECT=NO    @INPREAD MEMBER WORK AREA  82123
NEWJFCBP DC    A(NEWJFCB)    POINTER TO JFCB (PART OF INPWORK)   82123
LOWNEW   DC    F'0'                     FIRST NEW RECORD
HIGHNEW  DC    F'0'                     HIGHEST USED NEW RECORD
CURRNEW  DC    F'0'                     CURRENT NEW RECORD
NEWLIT   DC    CL8'NEW'      NEW OR SYSUT2 DD NAME               82123
LOW      EQU   LOWNEW-NEWINP      DISPLACEMENT TO LOWOLD AND LOWNEW
HIGH     EQU   HIGHNEW-NEWINP     DISPLACEMENT TO HIGHOLD AND HIGHNEW
CURR     EQU   CURRNEW-NEWINP     DISPLACEMENT TO CURROLD AND CURRNEW
OLDNEW   EQU   NEWLIT-NEWINP      DISPLACEMENT TO OLD OR NEW LITERAL
NXLST    DC    0A(0),X'87',AL3(NJFCB)                            82123
NEWCNT   DC    F'0'          COUNT OF NEW RECORDS                82123
NEWDIFF  DC    F'0'          COUNT OF UNMATCHED RECORDS          82123
NEWBXLE  SERVPDS PFX=PDN,DSECT=NO,RETURN=YES   DIRECTORY WORK AREA
NEWDEV   DC    AL1(2)        @INPREAD DEVICE NUMBER              82123
NEWOFLGS DC    X'00'         (FAKE) DCBOFLGS                     82123
OFGCONT  EQU   X'01'           PRIOR/CURRENT CARD IS CONTINUATION
         SPACE 1
WORKLINE EQU   OLDJFCB,133,C'C'   PARM SCAN WORK AREA            82123
SAVEEND  EQU   *                                                GP02338
         SPACE 2                                                 82123
         PRINT &PRTMAC                                           82123
PRTWORK  MAPPRT PREFIX=PU    EXPAND PRT WORK AREA                82123
         SPACE 2                                                 82123
         PRINT &PRTSYS                                           82123
         DCBD  DEVD=DA,DSORG=PO                                  81317
         SPACE 1                                                 89160
         IHAPDS PDSBLDL=NO   EXPAND PDS DIRECTORY                89160
         SPACE 2                                                 82123
      IEFJFCBN ,                                                 81317
         SPACE 2                                                 82123
CVTDSECT DSECT ,                                                 82123
         CVT   DSECT=YES                                         82123
         SPACE 2                                                 82123
         IHACDE ,                                                82123
         SPACE 1                                                 82123
         IEFUCBOB ,                                              82123
         SPACE 1                                                 82123
         IECSDSL1 1          FORMAT 1 DSCB                       82123
         END
