AOUT     TITLE 'A L L O C O U T  ***  TSO SYSOUT ALLOCATION'
         PUNCH '  ORDER ALLOCOUT(P)  EASIER DUMPS '
         PUNCH ' ALIAS AOUT '
         SPACE 1
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES    SET GLOBALS
         EJECT ,
*        PARSE + INIT CODE COPIED FROM 'GHOST', BY JACOB UNGER
*              TAUB COMPUTER CENTER
*              TECHNION, ISRAEL INSTITUTE OF TECHNOLOGY
*              TECHNION CITY, HAIFA, ISRAEL
*        SVC 99 CODE CODE COPIED FROM FILE 435 (ROCKWELL?) OF THE
*        CONNECTICUT BANK & TRUST MODS TAPE.
         SPACE 1
*        THIS COMMAND PROCESSOR SUPPORTS ALLOCATION OF SYSOUT FILES
*        WITH OPTIONS NOT AVAILABLE UNDER ALLOCATE (FORMS, ETC.).
*        BASIC ALLOCATE PARAMETERS ARE SUPPORTED, BUT ATTRIBUTE NAMES
*        AND PATTERN DSNAMES ARE NOT SUPPORTED.
*
*        USE:    ALLOCOUT DD(DDNAME)
*                         CLASS(X)     - SYSOUT CLASS (DFLT MSGCLASS)
*                         FORM(XXXX)   - FORMS (DEFAULT STD)
*                         DEST(XXXXXXXX) - REMOTE DESTINATION
*                         FCB(XXXX)    - FORMS CONTROL
*                         COPIES(NNN)  - OUTPUT COPIES, DFLT=1
*                         REPLACE | REUSE - FREE AND RE-ALLOCATE
         SPACE 1                                                 83328
         PRINT &PRTSOR
ALLOCOUT PGMHEAD ZERO12,PARM=R9,BASE=R12,BNDRY=PAGE       ADDED  88267
         LA    R10,DYNWORK   USE COMMON ADDRESSABILITY
         DROP  R13           DROP LOCAL MAPPING
         USING DYNWORK,R10
         LA    R11,RENTWORK  DITTO FOR SHARED DATA
         USING RENTWORK,R11
         USING CPPL,R9
         LOAD  EP=IKJPUTL    GET THE PUTLINE LPA ADDRESS         88150
         ST    R0,@PUTLINE   SAVE IT                             88150
         L     R1,CPPLECT    GET THE ECT ADDRESS                 88150
         ST    R1,MYIOPL+IOPLECT-IOPL  INTO THE IOPL             88150
         LA    R1,MYIOPECB   GET DUMMY ECB                       88150
         ST    R1,MYIOPL+IOPLECB-IOPL  DITTO                     88150
         L     R2,CPPLUPT    GET TO UPT                          88150
         ST    R2,MYIOPL+IOPLUPT-IOPL                            88150
         L     R2,PSAAOLD-PSA  GET THE ASCB                      87329
*BATCH*  ICM   R0,15,ASCBTSB-ASCB(R2)  RUNNING UNDER TSO ?       87329
*TMP?*   BZ    FAIL513       NO                                  87329
         TM    CPPLCBUF,X'80'   INVOKED AS A COMMAND PROCESSOR ? 87329
         BZ    DONECHEK      MAYBE                               87329
         WTERM MSG514A
         ICM   R1,15,ASCBASXB-ASCB(R2)  GET THE ASXB             87329
         BZ    EXIT8                                             87329
         ICM   R1,15,ASXBLWA-ASXB(R1)  GET THE LWA               87329
         BZ    EXIT8                                             87329
         L     R14,CPPLCBUF  LOAD INPUT PARAMETER
         ICM   R0,15,LWAPSCB-LWA(R1)                             87329
         BZ    EXIT8                                             87329
         LR    R15,R0                                            87329
         ICM   R15,15,PSCBUPT-PSCB(R15)  GET UPT                 87329
         BZ    EXIT8         TOO BAD                             87329
         ICM   R1,15,LWAPECT-LWA(R1)                             87329
         BZ    EXIT8         TOO BAD                             87329
         STM   R14,R1,FAKECPPL                                   87329
         LA    R9,FAKECPPL                                       87329
         WTERM MSG514B
         SPACE 1                                                 87329
*        PREPARE TO PARSE COMMAND OPERANDS
DONECHEK ST    R9,DFCPPLP    SAVE CPPL ADDRESS FOR ERROR MSGS
         LA    R2,DYNPPL                                         86013
         USING PPL,R2
         MVC   PPLUPT,CPPLUPT
         MVC   PPLECT,CPPLECT
         LA    R1,DYNECB
         ST    R1,PPLECB
         L     R1,=A(ALLOCOUP)
         ST    R1,PPLPCL
         LA    R1,ANSWER
         ST    R1,PPLANS
         MVC   PPLCBUF,CPPLCBUF
         ST    R10,PPLUWA    PASS DYNWORK ADDRESS
         STM   R11,R12,DYNWORK  PASS BASES
         LR    R1,R2               PPL
         LINK  EP=IKJPARS          PARSE
         BXH   R15,R15,BADPARSE
*   TEST ENTERED PARAMETERS
         DROP  R2
         LAT   R7,ANSWER,BADPARSE
         USING ALLOCRET,R7   PARSE DSECT
         SLR   R14,R14
         ICM   R14,3,DDNAMPOS+PDOLEN  ANY FILE NAME ?
         BZ    NODDNAME      NO
         SPACE 1
         CLI   REPLACE+1,0   REPLACE PARAMETER USED ?
         BE    NOFREE        NO
         FREE  PERM,DDN=DDNAMPOS  FREE BY DDNAME
         LTR   R15,R15       OK
         BZ    NOFREE        OK
         MVI   WHODUNIT+1,DFFREE  IDENTIFY CALLER
         BAL   R9,DYNFAIL    FORMAT ERROR MESSAGE
         BXLE  R15,R15,NOFREE  OK IF MESSAGE WRITTEN
         WTERM 'MSG598W Replace failed'
         SPACE 1
NOFREE   ALLOC PERM,DDN=DDNAMPOS,SYSOUT=CLASSPOS,FORMS=FORMPOS,        *
               COPIES=COPYPOS,FCB=FCBPOS,DEST=DESTPOS
         LTR   R15,R15
         BZ    RETURN        (RC=0 IN R15)
         MVI   WHODUNIT+1,DFSVC99  IDENTIFY CALLER
         BAL   R9,DYNFAIL    WRITE AN ERROR MESSAGE
         BXLE  R15,R15,EXIT8  RETURN ERROR
         WTERM 'MSG599E Allocation failed'
EXIT8    LA    R15,8         SET BOOBOO
*        B     RETURN
         SPACE 1                                                 83329
RETURN   LR    R9,R15        SAVE THE RETURN CODE
         IKJRLSA ANSWER      FREE ANY PARSE SPACE
         PGMEXIT  RC=(R9)    RETURN WITH 0 IF OK, ELSE 8        GP05019
         SPACE 1
BADPARSE WTERM BADPMSG
         B     EXIT8         RETURN WITH ERROR
         SPACE 1                                                 87329
FAIL513  WTO   'MSG512E INCORRECT ENVIRONMENT (NOT TSO)',              *
               ROUTCDE=11                                        87329
         B     EXIT8                                             87329
         SPACE 1
NODDNAME WTERM 'MSG517E File/DD name required, but not supplied'
         B     EXIT8
         SPACE 2                                                 83329
DYNFAIL  ST    R15,S99RC
         LA    R1,DYN1RB-DYN1DS+DYNSP1
         ST    R1,DFDAPLP
         LA    R1,S99RC
         ST    R1,DFRCP
         LA    R1,DFZEROES   NO MESSAGE WRITER ADDRESS
         ST    R1,DFJEFF02
         LA    R1,WHODUNIT   CALLER AND OPTION FLAGS
         ST    R1,DFIDP
         LA    R1,DFPARMS          GET DAIRFAIL PARMLIST
         LINK  EP=IKJEFF18
         BR    R9            RETURN TO CALLER (RC IN 15)
         SPACE 2
*        LOCAL EXIT TO CHECK FILE NAME
*
         PUSH  USING
         DROP  ,
         USING VEXFILE,R15
VEXFILE  STM   R14,R12,12(R13)  SAVE EVERYTHING
         LM    R9,R10,0(R1)  GET PDE AND USER ADDRESSES
         USING DYNWORK,R10   DECLARE IT
         L     R11,DYNWORK   GET RENTWORK
         USING RENTWORK,R11
         LR    R12,R15       MAKE LOCAL BASE
         DROP  R15
         USING VEXFILE,R12   DECLARE IT
         L     R2,PDOPTR(,R9)  GET FILE NAME ADDRESS
         SLR   R15,R15       SET OK
         CLI   0(R2),C'*'    DEFAULT ?
         BNE   *+8           NO
         LA    R15,4         REQUEST USER INPUT
         L     R14,12(,R13)  RESTORE RETURN
         LM    R0,R12,20(R13)  RESTORE ALL BUT R15
         BR    R14           RETURN
         POP   USING
         EJECT ,
***********************************************************************
*                                                                     *
*        PUT OUTPUT ON THE TERMINAL (OR BATCH TMP SIMULATION FILE)    *
*                                                                     *
*        ADDRESS SUPPLIED IN R1; LENGTH IN R0                         *
*                                                                     *
***********************************************************************
TPUTCONT OI    PTCHFLG,FGTCONT  CONTROL MODE                     88150
TPUTASIS OI    PTCHFLG,FGTTEXT  ASIS (PROMPT) MODE               88150
TPUTEDIT SUBENT ,            ENTRY TO WRITE LINE TO TERMINAL     88150
         LR    R5,R0         COPY TEXT LENGTH                    88150
         LR    R4,R1         COPY TEXT START                     88150
         LA    R14,PUTBUFF   GET OUTPUT BUFFER                   88150
         LA    R15,L'PUTBUFF  MAXIMUM LENGTH                     88150
         MIN   (R15),(R5)    TRUNCATE IF NECESSARY               88256
         LA    R0,4(,R15)    LENGTH WITH RDW LENGTH              88150
         SPACE 1                                                 88204
TPUTNPC  STH   R0,PUTRDW                                         88150
         MVCL  R14,R4        MOVE TEXT TO OUTPUT                 88150
         LA    R1,MYIOPL     GET MY LIST ADDRESS                 88150
         LA    R0,MYPTPB     GET THE GETLINE PARAMETER BLOCK     88150
         ST    R0,MYIOPL+IOPLIOPB-IOPL  STASH IT IN IOPL         88150
         TM    PTCHFLG,FGTTEXT  DOING TEXT ?                     88150
         BZ    TTPUTT        YES                                 88150
         PUTLINE OUTPUT=(PUTRDW,DATA),TERMPUT=ASIS,MF=(E,(1))
         B     TTPUTR        CHECK RETURN CODE                   88150
TTPUTT   PUTLINE OUTPUT=(PUTRDW,DATA),TERMPUT=EDIT,MF=(E,(1))
TTPUTR   CH    R15,=H'4'     NORMAL RETURN ?                     88150
         BL    TTPUTEX       YES                                 88150
         BH    TTPUTAB       NO; TERMINATE WITH PREJUDICE        88150
*LATER DO SOMETHING ABOUT ATTENTIONS ?                           88150
TTPUTAB  ABEND 2741,DUMP     MY TERMINAL IS BROKEN               88150
TTPUTEX  NI    PTCHFLG,255-FGTTEXT-FGTCONT  RESET MODE FLAGS     88150
         SUBEX RC=(R15)      RETURN WITH ERROR CODE              88150
         SPACE 1
RENTWORK DS    0H            COMMONLY ADDRESSABLE AREA
MSG514A  DC    C'MSG514W Module not invoked as a Command Processor'
MSG514B  DC    C'MSG514I Defaults will be used for all options'  87329
BADPMSG  DC    C'MSG518E Parse failed'
         SPACE 2                                                 83329
         LTORG
         SPACE 2                                                 83329
         PRINT &PRTSYS
ALLOCOUP IKJPARM DSECT=ALLOCRET
FILE     IKJKEYWD DEFAULT='DDNAME'
         IKJNAME 'F',SUBFLD=DDNAMFLD,ALIAS=('FILE','DDNAME','DD','DDN')
FORM     IKJKEYWD
         IKJNAME 'FORM',SUBFLD=FORMFLD,ALIAS='FORMS'
CLASS    IKJKEYWD DEFAULT='CLASS'
         IKJNAME 'CLASS',SUBFLD=CLASSFLD,ALIAS=('SYSOUT')
REPLACE  IKJKEYWD
         IKJNAME 'REUSE',ALIAS=('REPLACE','REP')
COPIES   IKJKEYWD
         IKJNAME 'COPIES',SUBFLD=COPYFLD,ALIAS=('N','COPY')
FCB      IKJKEYWD
         IKJNAME 'FCB',SUBFLD=FCBFLD,ALIAS=('LPI')
DEST     IKJKEYWD
         IKJNAME 'DESTINATION',SUBFLD=DESTFLD,ALIAS=('DEST')
DDNAMFLD IKJSUBF
DDNAMPOS IKJIDENT 'FILE NAME',MAXLNTH=8,FIRST=ALPHA,OTHER=ALPHANUM,    *
               PROMPT='FILE NAME',                                     *
               VALIDCK=VEXFILE,   CHECK DDNAME                         *
               HELP='FILE NAME (DDNAME) TO BE ALLOCATED'
FORMFLD  IKJSUBF
FORMPOS  IKJIDENT 'FORM NAME',MAXLNTH=8,FIRST=NONATNUM,OTHER=NONATNUM, *
               PROMPT='FORM NUMBER',                                   *
               HELP='THE FORM NUMBER ASSIGNED TO THE PAPER (LABELS)'
CLASSFLD IKJSUBF
CLASSPOS IKJIDENT 'SYSOUT CLASS',ASTERISK,MAXLNTH=1,FIRST=NONATNUM,    *
               DEFAULT='*',  SET DEFAULT                               *
               HELP='OUTPUT CLASS (A - STD, C - SPECIAL SETUP)'
COPYFLD  IKJSUBF
COPYPOS  IKJIDENT 'COPIES',INTEG,DEFAULT='1',                          *
               HELP='NUMBER OF OUTPUT COPIES'
FCBFLD   IKJSUBF
FCBPOS   IKJIDENT 'FORMS CONTROL BLOCK',MAXLNTH=8,                     *
               FIRST=NONATNUM,OTHER=NONATNUM,                          *
               PROMPT='FORMS CONTROL BLOCK',                           *
               HELP='FORMS CONTROL (CARRIAGE CONTROL) NAME, E.G., 6LPI'
DESTFLD  IKJSUBF
DESTPOS  IKJIDENT 'DESTINATION',MAXLNTH=8,                             *
               FIRST=NONATABC,OTHER=NONATNUM,                          *
               PROMPT='DESTINATION',                                   *
               HELP=('REMOTE PRINTER NAME')
         IKJENDP
         SPACE 2
         IKJCPPL
         SPACE 2
         IKJPPL
PPLLEN   EQU   *-PPL
         IKJPSCB ,                                               87329
         SPACE 1                                                 88150
         IKJIOPL ,           DEFINE LISTS FOR TSO SERVICES       88150
IOPLSIZE EQU   *-IOPL                                            88150
         SPACE 1                                                 88150
         IKJPTPB ,                                               88150
PTPBSIZE EQU   *-PTPB                                            88150
         SPACE 2
SAVE     DSECT ,
         DC    18F'0'        SECOND SAVE AREA
         DC    18F'0'        THIRD (SHOULD NOT NEED)
DYNWORK  DS    2D            START OF COMMON ADDRESSABILITY
         SPACE 1
         DYNSPACE
         SPACE 1                                              MSG85142
         IKJEFFDF ,          EXPAND IN-LINE                   MSG85142
         ORG   DFPARMS           DAIR FAIL PLIST              MSG85142
         DC   A(DYN1RB)          ADDRESS OF SVC 99 REQ BLK
         DC   A(S99RC)           ADDRESS OF SVC 99 RET CODE
         DC   A(DFZEROES)        ADDR OF UNKNOWN WRITER
         DC   A(WHODUNIT)        ADDR OF DAIRFAIL OPTIONS
ADCPPLP  DC   A(0)               CPPL ADDRESS                 MSG85142
         DC    A(0)          NO MESSAGE COMING BACK
         ORG   DFBUFS
DFZEROES DC   A(0)
S99RC    DC   F'0'
ANSWER   DS    A
DYNPPL   DS    (PPLLEN)X
DYNECB   DS    A             PARSE ECB
FAKECPPL DS    4A            A(BUF,UPT,PSCB,ECT)                 87329
PUTRDW   DC    0A(0),Y(L'PUTBUFF+4,0)
PUTBUFF  DC    CL132' '
PTCHFLG  DC    X'00'         PROCESSING FLAG                     88150
PTCHFIO  EQU   X'80'           RECORD WRITTEN                    88150
PTCHFCR  EQU   X'40'           CARRIAGE RETURN FOUND             88150
PTCHFLF  EQU   X'20'           LINE-FEED FOUND                   88150
FGTTEXT  EQU   X'02'           NEXT TGET/TPUT IS TEXT MODE       88150
FGTCONT  EQU   X'01'           NEXT TPUT IS IN CONTROL MODE      88150
WHODUNIT DC    Y(DFSVC99)    ISSUER OF RETURN CODES
MYIOPL   DC    (IOPLSIZE/4)F'0'  IOPL EXPANSION                  88150
MYIOPECB DC    F'0'          IOPL ECB                            88150
MYPTPB   PUTLINE MF=L        PUTLINE SERVICE                     88150
@PUTLINE DC    A(0)          ADDRESS OF PUTLINE ROUTINE          88150
SAVEEND  EQU   *                                                GP05019
         SPACE 1
PDESECT  DSECT ,             MAP OFFSECTS FOR PDE
PDOPTR   EQU   *-PDESECT,4,C'A'
PDEPTR   DS    A
PDOLEN   EQU   *-PDESECT,2,C'Y'
PDELEN   DS    H
PDOFLAG  EQU   *-PDESECT,1,C'B'
PDEFLAG  DS    X
PDEFPRES EQU   X'80'         PARAMETER PRESENT
         SPACE 1                                                 87329
         PRINT &PRTSYS
*        MACROS FROM SYS1.AMODGEN AND PVTMACS (SOURCE TAPES)     87329
         IHAPSA ,                                                87329
         IHAASCB ,                                               87329
         IHAASXB ,                                               87329
         IKJEFLWA ,                                              87329
         IEFZB4D0
         IEFZB4D2
         END   ,
