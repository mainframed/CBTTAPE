SPOUT    TITLE 'S P A C E O U T  ***  DASD CONVERSION SPACE ESTIMATOR'
         PUNCH '  ORDER SPACEOUT(P) '  EASIER DUMPS             GP09326
         PUNCH '  SETCODE AC(1)     '  FOR @VOLREAD             GP09326
         SPACE 1
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 2
***********************************************************************
*                                                                     *
*                                                                     *
*        COPYRIGHT 1986  EXPERT SYSTEM PROGRAMMING, INC.              *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         EJECT ,
*        THIS PROGRAM READS ALL DASD DEVICES CURRENTLY MOUNTED OR
*        PERMANENTLY RESIDENT, AND DETERMINES THE (APPROXIMATE) NUMBERS
*        OF TRACKS AND CYLINDERS THE SAME DATA REQUIRE ON THE DEVICE
*        SPECIFIED ON THE PARM FIELD.
*
*        REQUIRES:
*              EXEC PGM=SPACEOUT,REGION=1024K,PARM=33XX
*        SYSPRINT SYSOUT OR PERMANENT OUTPUT FILE
*
*        DISKS TO BE SEARCHED ARE SPECIFIED WITH ANY DDNAME OTHER
*          THAN ONES STARTING WITH JOB, STEP, PGM OR SYS
*        THE PRESENCE OF A VOLMOUNT DD CARD CAUSES AUTOMATIC UCB LOOP
*        MODE FOR PERMANENTLY RESIDENT AND MOUNTED VOLUMES.
*          IN AUTOMATIC MODE, DRIVES MAY BE SKIPPED BY INCLUSION OF
*          A DUMMY DD CARD WITH A DDNAME OF 'SKIPCUU', WHERE 'CUU'
*          IS THE DRIVE ADDRESS (PRIMARY).
         SPACE 1
*        MACROS - LOCAL SYSTEM MACRO LIBRARY REQUIRED
*        EXTERNAL REFERENCES - @SERVICE, @PRINTER, @VOLREAD ROUTINES
*        REFRESHABLE, RE-ENTRANT, AUTHORIZED
         SPACE 1
         PRINT &PRTSOR
SPACEOUT PGMHEAD ZERO31,BASE=(R11,R12),PARM=R9,AM=24,RM=24,  NEW 86250 *
               BNDRY=PAGE                                        86268
*                                                                86268
MAXDASD  EQU   512           MAXIMUM NUMBER OF SPINDLES          86268
         SPACE 1
         SERVINIT ,          LOAD THE SERVICE ROUTINE
         SERVCALL LPALD,=CL8'@PRINTER'  LOAD THE PRINTER ROUTINE
         ST    R0,@PRINTER
         PRTOPEN SYSPRINT,OPT=(ABEND) OPEN THE PRINT FILE       GP03030
         VOLREAD LOAD,USE=VTOC  LOAD THE VOLUME READ ROUTINE
         PRTLIST TITLE,TITLE=1   MAKE PRELIMINARY TITLE
         SPACE 1
         MVI   OUTDEV,X'0F'  DEFAULT OUTPUT DEVICE - 3390(SINGLE)
         LA    R9,0(,R9)               PARM ?
         LTR   R9,R9
         BZ    PARMDFLT      NONE; USE DEFAULT ACCT/SUBACCT
         LAT   R9,0(R9),PARMDFLT      PARM ?
         SLR   R8,R8
         ICM   R8,3,0(R9)    GET PARM LENGTH
         BNP   PARMDFLT      NONE; USE DEFAULT
         CH    R8,=H'8'      NOT TOO LONG ?
         BH    BADPARM       TOO BAD
         BCTR  R8,0
         MVC   DB,=CL8' '    PRE-BLANK
         EX    R8,PARMMVC    MOVE
         SERVCALL UCBDT,DB   GET UCB TYPE
         LTR   R0,R0         ANY ?
         BNP   BADPARM       NOT FOUND
         CLM   R0,2,=AL1(UCB3DACC)  DIRECT ACCESS ?
         BE    PARMHAVE      YES; ACCEPT
BADPARM  PRTF  'INVALID PARM FIELD',CC=NO
         MVI   CCODE+1,16    I FEEL ROTTEN
         B     ENDOJOB
PARMMVC  MVC   DB(0),2(R9)   REMOTE MOVE FOR PARM
         SPACE 1
PARMHAVE STC   R0,OUTDEV     STASH OUTPUT DEVICE TYPE
         SPACE 1
PARMDFLT XC    DB,DB
         MVI   DB+2,UCB3DACC
         NI    OUTDEV,X'7F'  TURN OFF (MY) VIO BIT
         MVC   DB+3(1),OUTDEV  MAKE FAKE UCB TBYT3/4
         SERVCALL UCBGN,DB-(UCBTBYT1-UCBOB)  GET GENERIC
         MVC   OUTTYPE,0(R1)
         PRTLIST TITLE,TITLE=1  REFRESH THE TITLE
         PRTLIST SUBHEAD2,TITLE=2
         PRTLIST SUBHEAD3,TITLE=3
         PRTLIST SUBHEAD4,TITLE=4
         LA    R9,DEVARRAY   DASD SAVER                          86268
         LR    R1,R9         ARRAY START                         86268
         LR    R15,R1        DITTO                               86268
         LA    R0,DEVLEN     LENGTH OF ONE ENTRY                 86268
         A     R1,=A(DEVAREND-DEVARRAY-DEVLEN)  LAST ENTRY       86268
         STM   R15,R1,DEVBXLE  SAVE                              86268
         USING DEVSECT,R9    DECLARE IT                          86268
         SLR   R1,R1
         IC    R1,OUTDEV     GET THE DEVICE TYPE
         SERVCALL DVTBL,(R1) GET THE DEVICE TABLE
         CH    R15,=H'4'     SUPPORTED DEVICE ?
         BH    BOOHOO        DEVICE NOT SUPPORTED
         ST    R0,STARDCTA+OUTSTAR-INSTAR   SAVE TABLE ADDRESS
         OI    STARDCTA+OUTSTAR-INSTAR,X'80'  SET DCT ADDRESS PRESENT
         LR    R15,R0
         USING DVCT,R15      DECLARE IT
         LH    R1,DVCTRK     GET TRACKS PER CYLINDER
         ST    R1,OUTTPC     SAVE FOR LATER
         MH    R1,DVCCYL     TIMES CYLINDERS
         SH    R1,DVCALT     LESS ALTERNATES
         ST    R1,OUTTRMAX   STASH MAXIMUM ON VOLUME
         MVI   STARFLGS+OUTSTAR-INSTAR,STARFUNC  RECORDS PER TRACK
         MVC   STARKL+OUTSTAR-INSTAR(3),=X'2C0060'  KEY=44, +96
         SERVCALL DVCAP,OUTSTAR   GET TRACK MAXIMUM
         ST    R0,OUTDSCPT   STASH DSCBS PER TRACK
         XC    STARRKDD+OUTSTAR-INSTAR(4),STARRKDD+OUTSTAR-INSTAR
         SPACE 1
         SERVCALL TIODD,=CL8'VOLMOUNT'  DYNAMIC MOUNTING ?
         LTR   R0,R0         ANY DD FOUND ?
         BZ    LOOKTIOT      NO; SKIP AUTOMATIC MODE
         OI    PROP,PAUTO    SET FOR UCB LOOP MODE
         B     AUTOLOOP      START UCB SCAN
         SPACE 1
LOOPTIOT VOLREAD TCLOSE      CLOSE PRIOR VOLUME
         TM    PROP,PAUTO    ARE WE IN UCB LOOKUP MODE ?
         BZ    LOOKTIOT      NO; GET NEXT TIOT ENTRY
AUTOLOOP L     R1,ADDRUCB    GET FIRST/NEXT UCB ADDRESS
AUTOLOOK SERVCALL UCBUM,(R1)   GET NEXT UCB
         LTR   R4,R0         WAS A UCB FOUND ?
         BZ    AUTOEND       NO; END OF CHAIN
         LR    R1,R4         SET FOR AUTOLOOK BRANCHES
         USING UCBOB,R4      DECLARE IT
         CLI   UCBTBYT3,UCB3DACC  DASD DEVICE ?
         BNE   AUTOLOOK      NO; TRY AGAIN
         TM    UCBSTAT,UCBONLI  ON-LINE ?
         BZ    AUTOLOOK      NO
         TM    UCBSTAT,UCBPRES+UCBRESV  MOUNTED OR RESIDENT ?
         BZ    AUTOLOOK      NO; SKIP IT
         TM    UCBSTAT,UCBCHGS+UCBUNLD  COMING OFF ?
         BNZ   AUTOLOOK      EITHER; SKIP IT
         ICM   R0,3,UCBVOLI  DOS OR BAD VTOC ?                  GP13227
         BZ    AUTOLOOK        YES; IGNORE IT                   GP13227
         AIF   ('&LOCAL' NE 'CCSI').NOSPAR                       90182
         CLC   =C'UCB',UCBVOLI  SPARE SPINDLE ?                  90182
         BE    AUTOLOOK      YES; SKIP IT                        90182
.NOSPAR  ST    R4,ADDRUCB    SAVE IT
         MVC   DB,=CL8'SKIP'
         MVC   DB+4(3),UCBNAME
         SERVCALL TIODD,DB   DOES USER WISH TO SKIP ONE ?
         LTR   R0,R0         WAS TIOT ENTRY FOUND ?
         BNZ   AUTOLOOP      YES; SKIP THIS UCB
         MVC   DB(4),=C'OMIT'  TRY ALTERNATE FORM
         SERVCALL TIODD,DB
         LTR   R0,R0
         BNZ   AUTOLOOP      SKIP ALTERNATE FORM
         B     LOOKCOMM      GO TO COMMON CODE
AUTOEND  NI    PROP,255-PAUTO  RESET AUTO MODE
         B     ALLDONE       QUIT
         DROP  R4
LOOKTIOT L     R1,ADDRTIOT   GET PRIOR ENTRY ADDRESS OR 0
         SERVCALL TIOLK      LOOK FOR PLAIN TIOT ENTRIES
         LTR   R2,R0         ANY MORE TO DO ?
         BZ    ALLDONE       NO; QUIT
         ST    R2,ADDRTIOT   SET FOR NEXT CALL
         LAT   R4,16(R2),LOOKTIOT     SKIP DUMMY UCB
         CLC   =C'SYS0',4(R2)  DYNAMIC ALLOCATION ?              88234
         BE    LOOKTIOT      YES; ASSUME THE WORST AND SKIP IT   88234
         USING UCBOB,R4
         CLI   UCBTBYT3,UCB3DACC       IS IT DASD ?
         BNE   LOOKTIOT      IGNORE NON-DA
         SERVCALL TIODD,4(R2)   LOCATE TIOT ENTRY BY DDNAME
         CR    R2,R0         IF NOT EQUAL, DUPLICATE DDNAME
         BNE   LOOKTIOT      WHICH SHOULD BE IGNORED
LOOKCOMM XC    CLEARVOL(OUTLEN),CLEARVOL  VOLUME COUNTERS        86268
         SERVCALL UCBGN,(R4)   GET GENERIC NAME
         LA    R0,4          GET MINIMUM # DSCBS IN VTOC
         ST    R0,NUMDSCBS   SET IT
         MVC   INTYPE,0(R1)  STASH GENERIC
         MVC   INCUU,UCBNAME SAVE UCB NAME
         MVC   INVOL,UCBVOLI SAVE VOL-SER
         SLR   R1,R1
         IC    R1,UCBTBYT4   GET THE DEVICE TYPE
         SERVCALL DVTBL,(R1) GET THE DEVICE TABLE
         BXH   R15,R15,BOOHOO  DEVICE TYPE NOT IN SYSTEM ?
         ST    R0,STARDCTA   SAVE TABLE ADDRESS
         OI    STARDCTA,X'80'  SET DCT ADDRESS SUPPLIED
         MVC   SPCCALL(4),STARDCTA  MAKE EXTENT CALCULATION LIST
         LR    R15,R0
         USING DVCT,R15      DECLARE IT
         LH    R1,DVCTRK     GET TRACKS PER CYLINDER
         ST    R1,TRKPCYL    SAVE FOR LATER
         MH    R1,DVCCYL     TIMES CYLINDERS
         SH    R1,DVCALT     LESS ALTERNATES
         ST    R1,MAXTRKS    STASH MAXIMUM ON VOLUME
         MVI   STARFLGS,STARMAXS  GET MAX RECORD
         SERVCALL DVCAP,INSTAR    GET TRACK MAXIMUM
         ST    R0,INMAX      STASH INPUT MAXIMUM
         MVI   STARFLGS,STARFUNC  SET FOR CAPACITY CALCULATION
         DROP  R4            DONE WITH UCB
         NI    PROP,255-PDATA-PHEAD  SET NO DATA FOUND ON VOLUME
         SPACE 1
         VOLREAD OPEN,INVOL,USE=VTOC  INITIALIZE VOLUME PROCESSING
         BXH   R15,R15,SKIPVOL   IGNORE IF BAD
         VOLREAD DSCB        READ THE FORMAT 4
         BXH   R15,R15,SKIPVOL   ERROR
         B     NEXTDSC4      JOIN NORMAL PROCESSING
         SPACE 1
NEXTDSCB VOLREAD DSCB        GET ANOTHER DSCB
         CH    R15,=H'4'     DID WE GET ONE ?
         BE    DOTOTALS      NO; END OF VOLUME
         BH    DSCBIOER      NO; I/O ERROR
NEXTDSC4 LR    R1,R0         COPY DSCB ADDRESS
         CLI   DS1FMTID-DS1DSNAM(R1),C'1'   REALLY A FORMAT 1 ?
         BNE   NEXTDSCB      IF NOT, SKIP
         MVC   DS1DSNAM(LENDSCB1),DS1DSNAM-DS1DSNAM(R1)  COPY
         INC   NUMDSNS       INCREMENT DSN COUNT
         INC   NUMDSCBS      COUNT A DSCB SLOT
         OI    PROP,PDATA    INDICATE DATASET FOUND
         SPACE 1
*        HAVE DATASET - GET TRACKS ALLOCATED AND USED
         SPACE 1
         LA    R14,IECSDSL1  POINT TO FORMAT 1
         SLR   R15,R15       CLEAR FORMAT 3 POINTER
         STM   R14,R15,SPCCALL+4   MAKE CALLING LIST
         CLI   DS1NOEPV,3    MORE THAN THREE EXTENTS ?
         BL    FMTRACK       NO; DONE
         BH    FMTRD         YES; READ EXTENSION
         CLI   DS1EXT1,X'40'   LABEL TRACK ?
         BNE   FMTRACK       NO; NO FMT3 EXPECTED
FMTRD    VOLREAD DSC3        GET CHAINED DSCB
         BXH   R15,R15,FMT3ERR  NO; SIGNAL ERROR
         LR    R1,R0         LOAD THE DSCB ADDRESS
         INC   NUMDSCBS      COUNT A DSCB SLOT
         USING IECSDSL3,R1
         CLI   DS3FMTID,C'3'           REALLY A FORMAT 3 ?
         BNE   FMTRD                   BRANCH IF FORMAT 2
         ST    R1,SPCCALL+8  PLACE ADDRESS IN CALLING SEQUENCE
         DROP  R1
         B     FMTRACK       DONE
FMT3ERR  OI    FLAGDSN,FGFMT3  SHOW ERROR GETTING FORMAT 3 DSCB
FMTRACK  SERVCALL DVEXT,SPCCALL  GET EXTENT SIZE IN TRACKS
         BXLE  R15,R15,FMTRACKS  SKIP IF NO ERROR
         OI    FLAGDSN,FGFMT3  SHOW ERROR GETTING EXTENTS
FMTRACKS LR    R5,R0         SAVE TOTAL TRACKS
         LR    R7,R5         SAVE PROVISIONAL NUMBER OF USED TRACKS
         TM    DS1SMSFG,DS1PDSE+DS1PDSEX  PDS/E OR HFS ?        GP03030
         BNZ   SETFULL       YES; ASSUME FULL                   GP03030
         CLI   DS1DSORG+1,0  SAM type ?                         GP09186
         BNE   SETFULL       NO; IGNORE                         GP09186
         TM    DS1DSORG,JFCORGPS+JFCORGPO  LSTAR USEFUL ?
         BZ    SETFULL       NO
         SLR   R7,R7         CLEAR FOR ICM
         ICM   R7,7,DS1LSTAR  GET TRACKS FROM LSTAR             GP09186
         BZ    GETUSE        NO                                 GP09186
         SRL   R7,8                                             GP09186
         LA    R7,1(,R7)     ELSE INCREASE TRACK COUNT
GETUSE   CR    R7,R5         MORE THAN 100% USED ?
         BNH   *+6           NO
         LR    R7,R5         YOU'RE KIDDING ?
         SPACE 1
*        DETERMINE NUMBER OF BLOCKS PER TRACK
         SPACE 1
SETFULL  LA    R1,2048       GET HALF OF DEFAULT BLOCKSIZE
         LA    R1,0(R1,R1)   GET DEFAULT BLOCKSIZE
         SLR   R0,R0
         ICM   R0,3,DS1BLKL  GET BLOCKSIZE
         BZ    SETBLKDF      USE DEFAULT
         CR    R0,R1         LARGER THAN DEFAULT ?
         BNH   SETBLKUS      NO; USE AS IS
         TM    DS1DSORG+1,DS1ACBM  VSAM ?
         BNZ   SETBLKUS      YES; USE SIZE AS IS
         TM    DS1RECFM,JFCFMREC  CHECK RECORD FORMAT
         BM    SETBLKUS      U - ASSUME DEFAULT SIZE
SETBLKDF LR    R0,R1         USE DEFAULT SIZE
SETBLKUS STCM  R0,3,STARDL   SET DATA LENGTH
         STCM  R0,3,STARDL+OUTSTAR-INSTAR
         SERVCALL DVCAP,INSTAR  GET BLOCKS PER TRACK ON INPUT
         LTR   R0,R0         ANY ?
         BNP   SETBLKHF      NO; TRY SMALLER BLOCKSIZE
         ST    R0,INBPT      SAVE IT
         SERVCALL DVCAP,OUTSTAR  GET BLOCKS PER TRACK ON OUTPUT
         LTR   R0,R0         ANY ?
         BP    SETBLKNM      YES; SAVE IT
SETBLKHF ICM   R0,3,STARDL
         SRA   R0,1          TRY HALF OF THIS SIZE
         BP    SETBLKUS      TRY AGAIN
         LA    R0,1          FAKE AS ONE EACH
         ST    R0,INBPT
SETBLKNM ST    R0,OUTBPT
         SPACE 1
*        NOW CALCULATE TOTAL SPACE REQUIREMENTS FOR THESE DATA.
         SPACE 1
         LA    R1,1          SET FOR TRACK ALLOCATION - TRK/CYL
         LA    R2,ALTTRKS-ALCTRKS  SET OFFSET TO TRACK ALLOCATION
         LA    R8,1          DITTO FOR OUTPUT SIDE
         TM    DS1SCALO,JFCBCYL  CYLINDER ALIGNED SECONDARY ?
         BO    SETCYL        YES
         LA    R14,DS1EXT1
         TM    DS1EXT1,X'40'  LABEL TRACK ?
         BZ    *+8           NO
         LA    R14,DS1EXT2-DS1EXT1(,R14)  SKIP OVER
         TM    DS1EXT1-DS1EXT1(R14),X'80' CYLINDER ALIGNED
         BZ    SETTRK        NO
SETCYL   L     R1,TRKPCYL    GET TRACKS PER CYLINDER
         LA    R2,ALCTRKS-ALCTRKS  SET OFFSET TO CYLINDER COUNTERS
         L     R8,OUTTPC
SETTRK   LR    R0,R1         COPY TRACKS PER CYLINDER
         BCTR  R0,0          MAKE ADDER FOR ROUNDING
         SLR   R4,R4
         SLR   R6,R6
         AR    R5,R0         PREPARE FOR ROUNDING
         AR    R7,R0
         DR    R4,R1         GET CYLINDERS
         DR    R6,R1
         LR    R3,R5         GET CYLINDERS USED
         A     R3,ALCTRKS(R2)
         ST    R3,ALCTRKS(R2)
         LR    R3,R7
         A     R3,USCTRKS(R2)
         ST    R3,USCTRKS(R2)
         MR    R4,R1         GET REQUIRED TRACKS BACK
         MR    R6,R1
         LR    R3,R5
         A     R3,ALLTRKS
         ST    R3,ALLTRKS
         LR    R3,R7
         A     R3,USDTRKS
         ST    R3,USDTRKS
         M     R4,INBPT      GET BLOCKS IN DATASET
         M     R6,INBPT
         L     R0,OUTBPT     GET OUTPUT BLOCKS PER TRACK
         BCTR  R0,0          GET VALUE FOR ROUNDING
         AR    R5,R0
         AR    R7,R0
         D     R4,OUTBPT     GET TRACKS IN OUTPUT
         D     R6,OUTBPT
         LR    R0,R8         GET TRACKS PER CYLINDER
         BCTR  R0,0
         SLR   R4,R4         PREPARE FOR DIVIDE
         AR    R5,R0         SET FOR ROUNDING
         SLR   R6,R6
         AR    R7,R0
         DR    R4,R8         CONVERT TO CYLINDERS
         MR    R4,R8         AND BACK TO TRACKS
         A     R5,OUTTRKS
         ST    R5,OUTTRKS
         DR    R6,R8
         MR    R6,R8
         A     R7,OSDTRKS
         ST    R7,OSDTRKS
         B     NEXTDSCB      GET NEXT DATASET
         SPACE 1
*        FORMAT 4 OBTAIN ERROR ROUTINE
         SPACE 1
SKIPVOL  SRL   R15,1         COMPENSATE FOR BXH
         ST    R15,DB        SAVE CODE FOR FORMATTING
         PRTLIST OBT4MSG     VOLUME SKIPPED MESSAGE
         OI    CCODE+1,8     SET NOT SO MINOR ERROR
         B     LOOPTIOT                GET NEXT VOLUME
         SPACE 1
DSCBIOER PRTF  '*********** DSCB I/O ERROR ************',CC=NO
         OI    CCODE+1,8     SET NOT SO MINOR ERROR
         SPACE 1
DOTOTALS L     R1,OUTDSCPT   GET DSCBS PER TRACK
         BCTR  R1,0          LESS ONE
         A     R1,NUMDSCBS   PLUS REQUIRED DSCBS
         SLR   R0,R0
         D     R0,OUTDSCPT   GET TRACKS FOR VTOC
         LA    R1,1(,R1)     PLUS ONE FOR LABEL TRACK
         LR    R2,R1         SAVE
         A     R1,OUTTRKS
         ST    R1,OUTTRKS    GET TRACKS REQUIRED
         M     R0,=F'100'
         D     R0,OUTTRMAX   GET FRACTION OF PACK REQUIRED
         ST    R1,OUTPCT
         LR    R1,R2         GET OVERHEAD TRACKS BACK
         A     R1,OSDTRKS
         ST    R1,OSDTRKS    GET TRACKS REQUIRED
         M     R0,=F'100'
         D     R0,OUTTRMAX   GET FRACTION OF PACK REQUIRED
         ST    R1,OSDPCT
         LA    R3,OUTLEN-4  LAST OFFSET                          86268
         LA    R2,4          INCREMENT
         SLR   R1,R1         STARTING OFFSET
DOTOLOOP L     R0,CLEARVOL(R1)  GET VOLUME COUNTER
         A     R0,CLEARTOT(R1)  ADD TO RUN COUNTER
         ST    R0,CLEARTOT(R1)  STASH BACK
         BXLE  R1,R2,DOTOLOOP  DO ALL
         LM    R0,R1,DEVBXLE+4                                   86268
         BXLE  R9,R0,DOTOSUM  OK                                 86268
         LR    R9,R1         RESET TO LAST ENTRY                 86268
         ICM   R0,15,OUTTRKS ENTRY PREVIOUSLY USED ?             86268
         BZ    DOTOSUM       NO                                  86268
         PRTLIST MSGVOL      PRINT STATISTICS
DOTOSUM  ST    R9,DEVBXLE    SAVE ANYWAY                         86268
         INC   NUMVOLS       COUNT VOLUMES PROCESSED
         B     LOOPTIOT
         SPACE 1
ALLDONE  LA    R4,DEVARRAY   GET FIRST ENTRY                     86268
         LR    R5,R9         FIRST UNUSED ENTRY IN ARRAY         86268
         S     R5,DEVBXLE+4  GET LAST USED ENTRY                 86268
         CR    R5,R4         ANY AT ALL ?                        86268
         BL    ALLDID        TOO BAD                             86268
         STM   R4,R5,DEVBXLE  RE-USE                             86268
         OI    DEVBXLE+4,X'80'  INDICATE END ADDRESS             86268
         MVC   DEVBXLE+8(4),=AL1(C'A',INVOL-DEVSECT,L'INVOL,DEVLEN)
         SERVCALL SORTH,DEVBXLE  SORT IN ASCENDING SEQUENCE      86268
         LR    R9,R4         SET TO FIRST ENTRY                  86268
         LA    R4,DEVLEN     LENGTH OF ONE ENTRY                 86268
ALLPRINT ICM   R0,15,OUTTRKS  IS THIS ENTRY USED ?               86268
         BZ    ALLPRINX      NO; SKIP IT                         86268
         PRTLIST MSGVOL      PRINT THE STATISTICS                86268
ALLPRINX BXLE  R9,R4,ALLPRINT  DO FOR ALL                        86268
ALLDID   LA    R9,DEVARRAY   GET WORK ENTRY                      86268
         MVC   CLEARVOL(OUTLEN),CLEARTOT  MOVE FOR MSG           86268
         MVC   MAXTRKS,OUTTRMAX
         L     R1,OUTTRKS    GET TRACKS REQUIRED
         M     R0,=F'100'
         D     R0,OUTTRMAX   GET FRACTION OF PACK REQUIRED
         ST    R1,OUTPCT
         L     R1,OSDTRKS
         M     R0,=F'100'
         D     R0,OUTTRMAX   GET FRACTION OF PACK REQUIRED
         ST    R1,OSDPCT
         L     R0,NUMVOLS    GET VOLUMES PROCESSED
         CH    R0,=H'1'      TEST
         BE    ENDOJOB       NO SUMMARY REQUIRED
         BH    ENDSOME       PRINT SUMMARY
         PRTF  '******** NO VOLUMES PROCESSED ********',CC=NO
         OI    CCODE+1,12
         B     ENDOJOB
ENDSOME  PRTLIST DONEMSG
ENDOJOB  SERVTERM ,          CLOSE AND FREE EVERYTHING
         LH    R9,CCODE      LOAD COMPLETION CODE
         PGMEXIT RC=(R9)     QUIT                               GP03030
         SPACE 1
BOOHOO   EX    0,*           I'M ALLRIGHT, DAVE...
         SPACE 1
SYSPRINT PRTWORK SYSPRINT,TITLE=5
         PRINT &PRTSYS
         SPACE 1
         LTORG
         EJECT
*        MESSAGES - HEADINGS, ETC.
         SPACE 1
         PRINT NOGEN
TITLE    FDOPT NL,CC=C'#'    AUTOMATIC DATE/TIME/PAGE OPTION
         FDPRT 'S P A C E O U T  ***  DASD CONVERSION SPACE ESTIMATOR'
         FDPRT '  ***  MIGRATION TO A'
         FDPRT OUTTYPE,DEB,PADL
         FDPRT *END
         SPACE 2
SUBHEAD2 FDPRT '|<------------ ALLOCATED SPACE ------------>|',        *
               RADJ,NL,LEN=85
         FDPRT '|<--------------- USED SPACE --------------->|',PAD
         FDPRT *END
         SPACE 1
SUBHEAD3 FDPRT 'INPUT VOLUME',RADJ,LEN=25
         FDPRT 'NO. OF',RADJ,LEN=15
         FDPRT 'CYL',RADJ,LEN=9
         FDPRT 'TRACK',RADJ,LEN=9
         FDPRT 'TOTAL',RADJ,LEN=9
         FDPRT 'OUTPUT',RADJ,LEN=9
         FDPRT OUTTYPE,DEB,RADJ,LEN=9
         FDPRT 'CYL',RADJ,LEN=11
         FDPRT 'TRACK',RADJ,LEN=9
         FDPRT 'TOTAL',RADJ,LEN=9
         FDPRT 'OUTPUT',RADJ,LEN=9
         FDPRT OUTTYPE,DEB,RADJ,LEN=9
         FDPRT *END
         SPACE 1
SUBHEAD4 FDPRT 'TYPE',NL,RADJ,LEN=8
         FDPRT 'ADR',RADJ,LEN=4
         FDPRT 'NAME',PAD,LEN=6
         FDPRT 'MAX TRKS',RADJ,LEN=9
         FDPRT 'DATASETS',RADJ,LEN=11
         FDPRT 'BOUND',RADJ,LEN=9
         FDPRT 'BOUND',RADJ,LEN=9
         FDPRT 'TRACKS',RADJ,LEN=9
         FDPRT 'TRACKS',RADJ,LEN=9
         FDPRT 'FRACTION',RADJ,LEN=9
         FDPRT 'BOUND',RADJ,LEN=11
         FDPRT 'BOUND',RADJ,LEN=9
         FDPRT 'TRACKS',RADJ,LEN=9
         FDPRT 'TRACKS',RADJ,LEN=9
         FDPRT 'FRACTION',RADJ,LEN=9
         FDPRT *END
         SPACE 1
OBT4MSG  FDPRT AST30,NL,LEN=9
         FDPRT 'VOLUME',PAD
         FDPRT INVOL,DEB,PAD
         FDPRT 'SKIPPED DUE TO FORMAT 4 PROCESSING ERROR.',PAD
OBTCOM   FDPRT 'CONDITION CODE=',PADL
         FDPRT DB,4,I,PADR
OBTCOMAS FDPRT AST30,LEN=9
OBTCOMX  FDPRT *END
         SPACE 1
DONEMSG  FDOPT NL,CC=C'0'    DOUBLE-SPACE
         FDPRT NUMVOLS,I,RADJ,LEN=6
         FDPRT 'DRIVE TOTAL:',PAD
         FDGOTO MSGVOLC
MSGVOL   FDPRT INTYPE,DEB,RADJ,NL,LEN=8
         FDPRT INCUU,PAD     UNIT ADDRESS
         FDPRT INVOL,PAD     VOLUME SERIAL
MSGVOLC  FDPRT MAXTRKS,I,RADJ,LEN=9
         FDPRT NUMDSNS,I,RADJ,LEN=11
         FDPRT ALCTRKS,I,RADJ,LEN=9   CYLINDERS
         FDPRT ALTTRKS,I,RADJ,LEN=9   TRACKS ALLOCATED
         FDPRT ALLTRKS,I,RADJ,LEN=9   TRACKS ALLOCATED
         FDPRT OUTTRKS,I,RADJ,LEN=9
         FDPRT OUTPCT,ICN,RADJ,LEN=9  PERCENTAGE
         FDPRT USCTRKS,I,RADJ,LEN=11
         FDPRT USTTRKS,I,RADJ,LEN=9
         FDPRT USDTRKS,I,RADJ,LEN=9
         FDPRT OSDTRKS,I,RADJ,LEN=9
         FDPRT OSDPCT,ICN,RADJ,LEN=9
         FDPRT *END
AST30    DC    C'******************************'
         SPACE 1                                                 86268
DEVSECT  DSECT ,             MAPPING OF SINGLE ENTRY IN TABLE    86268
INTYPE   DC    CL8' '        INPUT VOLUME TYPE
INCUU    DC    CL3' '        INPUT ADDRESS
INVOL    DC    CL6' '        INPUT VOLSER
FLAGDSN  DC    X'00'         DSN PROCESSING FLAG
FGFMT3   EQU   X'40'           ERROR GETTING FORMAT 3
CLEARVOL DS    0F            START OF CLEAR FOR NEW VOLUME       86268
NUMDSNS  DS    F             DSCB1S ON THIS PACK
NUMDSCBS DS    F             DSCBS REQUIRED
MAXTRKS  DS    F             MAXIMUM TRACKS ON INPUT
ALCTRKS  DS    F             CYLINDERS IN CYLINDER ALIGNED DSS
ALTTRKS  DS    F             TRACKS IN TRACK ALIGNED DATASETS
ALLTRKS  DS    F             TOTAL ALLOCATED TRACKS
USCTRKS  DS    F             USED TRACK ALIGNED SPACE
USTTRKS  DS    F             USED TRACK ALIGNED SPACE
USDTRKS  DS    F             USED TRACK TOTAL
OUTTRKS  DS    F             EQUIVALENT OUTPUT TRACKS
OUTPCT   DS    F             PERCENT OF OUTPUT PACK
OSDTRKS  DS    F             USED OUPUT TRACKS
OSDPCT   DS    F             USED PERCENTAGE
         DS    0F                                                86268
OUTLEN   EQU   *-CLEARVOL    SIZE TO CLEAR                       86268
DEVLEN   EQU   *-DEVSECT     LENGTH OF ONE ENTRY                 86268
         SPACE 2
         PRINT &PRTMAC       EXPAND LOCAL MACROS
SAVE     DSECT ,             SAVE/WORK AREA
DB       DC    D'0'
@SERVICE DC    A(0)          SERVICE ROUTINE
@PRINTER DC    A(0)          PRINTER ROUTINE
@VOLREAD DC    A(0)          VTOC READ ROUTINE
@INPREAD DC    A(0)          CARD READER
ADDRTIOT DC    A(0)
ADDRUCB  EQU   ADDRTIOT,4,C'A'  RE-USE REPOSITIONING ADDRESS
NUMVOLS  DS    F             NUMBER OF DISKS PROCESSED
         SPACE 1
INSTAR   SERVCALC MF=D       EXPAND MAPPING FOR TRKCALC
         SPACE 1
OUTSTAR  DC    (OUTSTAR-INSTAR)X'00'  SECOND COPY
OUTDEV   EQU   OUTSTAR+STARTYPE-INSTAR
SPCCALL  DS    3F            LIST FOR SERVCALL EXTENT SIZE
         SPACE 1
CCODE    DC    H'0'          PROGRAM RETURN CODE
         SPACE 1
POPS     DC    AL1(0)        PROCESS OPTIONS
PROP     DC    AL1(0)        PROCESS OPTIONS
PAUTO    EQU   16              AUTOMATIC VOLUME RETRIEVAL (UCB LOOP)
CATOPT   EQU   32              WYLCATLG RATHER THAN WYLVTOC ENTRY
PHEAD    EQU   64              PAGE HEADER PRINTED
PDATA    EQU   128             DATASET FOUND ON THIS VOLUME
OUTTYPE  DC    CL8' '        OUTPUT TYPE
OUTBPT   DS    F             BLOCKS PER TRACK ON OUTPUT DEVICE
OUTTPC   DS    F             TRACKS PER CYLINDER
OUTDSCPT DS    F             DSCBS PER TRACK
OUTTRMAX DS    F             TOTAL OUTPUT TRACKS
INMAX    DS    F             MAX BLOCK SIZE
TRKPCYL  DS    F             TRACKS PER CYLINDER
INBPT    DS    F             INPUT BLOCKS PER TRACK
         SPACE 1
DEVBXLE  DS    3A            BXLE FOR DASD ARRAY                 86268
CLEARTOT DS    (OUTLEN/4)F'0'  RUN TOTALS                        86268
         DS    0D
         IECSDSL1 1          FORMAT 1
CCHHFMT1 DS    XL5           ADDRESS OF FORMAT 1 DSCB
LENDSCB1 EQU   *-DS1DSNAM    LENGTH TO MOVE
         AIF   (&MVSXA).SYSDEF                                  GP04234
DS1FLAG1 EQU   DS1NOBDB+1,1,C'X'  MORE FLAGS
DS1COMPR EQU   X'80'           COMPRESSABLE EXTENDED IF DS1STRP
DS1CPOIT EQU   X'40'           CHECKPOINTED D S
DS1SMSFG EQU   DS1FLAG1+17,1,C'X'  SMS FLAG
DS1SMSDS EQU   X'80'           SMS D S
DS1SMSUC EQU   X'40'           NO BCS ENTRY
DS1REBLK EQU   X'20'           MAY BE REBLOCKED
DS1CRSDB EQU   X'10'           BLKSZ BY DADSM
DS1PDSE  EQU   X'08'           PDS/E
DS1STRP  EQU   X'04'           EXTENDED FORMAT D S
DS1PDSEX EQU   X'02'           HFS D S
DS1DSAE  EQU   X'01'           EXTENDED ATTRIBUTES EXISY
DS1SCEXT EQU   DS1SMSFG+1,3,C'X'  SECONDARY SPACE EXTENSION
DS1SCXTF EQU   DS1SCEXT,1,C'X'  -"- FLAG
DS1SCAVB EQU   X'80'           SCXTV IS AVG BLOCK LEN
DS1SCMB  EQU   X'40'                 IS IN MEGBYTES
DS1SCKB  EQU   X'20'                 IS IN KILOBYTES
DS1SCUB  EQU   X'10'                 IS IN BYTES
DS1SCCP1 EQU   X'08'           SCXTV COMPACTED BY 256
DS1SCCP2 EQU   X'04'                 COMPACTED BY 65536
DS1SCXTV EQU   DS1SCXTF+1,2,C'X'  SEC SPACE EXTNSION VALUE
DS1ORGAM EQU   DS1ACBM         CONSISTENT NAMING - VSAM D S
DS1RECFF EQU   X'80'           RECFM F
DS1RECFV EQU   X'40'           RECFM V
DS1RECFU EQU   X'C0'           RECFM U
DS1RECFT EQU   X'20'           RECFM T   001X XXXX IS D
DS1RECFB EQU   X'10'           RECFM B
DS1RECFS EQU   X'08'           RECFM S
DS1RECFA EQU   X'04'           RECFM A
DS1RECMC EQU   X'02'           RECFM M
*   OPTCD DEFINITIONS   BDAM    W.EFA..R
*                       ISAM    WUMIY.LR
*             BPAM/BSAM/QSAM    WUCHBZTJ
DS1OPTIC EQU   X'80'  FOR DS1ORGAM - CATLG IN ICF CAT
DS1OPTBC EQU   X'40'           ICF CATALOG
DS1RACDF EQU   DS1IND40
DS1SECTY EQU   DS1IND10
DS1WRSEC EQU   DS1IND04
DS1SCAL1 EQU   DS1SCALO,1,C'X'    SEC. ALLOC FLAGS
DS1DSPAC EQU   X'C0'         SPACE REQUEST MASK
DS1CYL   EQU   X'C0'           CYLINDER BOUND
DS1TRK   EQU   X'80'           TRACK
DS1AVRND EQU   X'41'           AVG BLOCK + ROUND
DS1AVR   EQU   X'40'           AVG BLOCK LEN
DS1MSGP  EQU   X'20'
DS1EXT   EQU   X'10'           SEC. EXTENSION EXISTS
DS1CONTG EQU   X'08'           REQ. CONTIGUOUS
DS1MXIG  EQU   X'04'           MAX
DS1ALX   EQU   X'02'           ALX
DS1DSABS EQU   X'00'           ABSOLUTE TRACK
DS1SCAL3 EQU   DS1SCAL1+1,3,C'X'  SEC ALLOC QUANTITY
.SYSDEF  SPACE 1
         SPACE 1                                                 86268
*        ARRAY FOR DASD DATA STORAGE                             86268
*                                                                86268
         DS    0F                                                86268
DEVARRAY DS    (MAXDASD)XL(DEVLEN)'0'  COUNTER ARRAY             86268
DEVAREND EQU   *             END OF ARRAY                        86268
SAVEEND  EQU   *                                                GP03030
         SPACE 1
         PRINT &PRTSYS
CVTDSECT DSECT ,
         CVT   DSECT=YES
         IHACDE ,
         SPACE 1
         IHADVCT ,
         SPACE 1
         IEFUCBOB ,
         AIF   (&MVS).MVSUCB
UCBRR    EQU   X'20'         DEVICE SHARABLE
UCBRPS   EQU   X'10'         DEVICE HAS RPS
         AIF   (&SVS).MVSUCB
UCBVLPWR EQU   X'02'         DEVICE REQUIRES ALTERNATE POWER
UCBDVPWR EQU   X'01'         DEVICE HAS ALTERNATE POWER
.MVSUCB  SPACE 1
         IEFJFCBN ,
         SPACE 1
DSCB3    DSECT ,
         IECSDSL1 3          FORMAT 3
         SPACE 1
         END   ,
