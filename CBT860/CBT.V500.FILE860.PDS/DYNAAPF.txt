DYNAAPF  TITLE 'D Y N A A P F  ***  ADD APF LIBRARY FOR MVS 3.8J'
         PUNCH ' ORDER DYNAAPF(P) '    MAKE DUMPS EASIER        GP06285
         PUNCH '  SETCODE AC(1)   '    NEEDS AUTHORIZATION      GP06285
         SPACE 1                                                GP06285
         COPY  OPTIONGB                                         GP06285
         SPACE 1                                                GP06285
         GBLC  &ROUT,&DESC,&SPL                                 GP06285
         SPACE 1                                                GP06285
         SYSPARM LIST=YES                                       GP06285
         SPACE 1                                                GP06285
*--------------------------------------------------------------------*
*                                                                    *
*    PROGRAM DYNAAPF WILL ADD A PDS, SPECIFIED ON DD APFLIB, TO THE  *
*      AUTHORIZED LIBRARY TABLE (-> CVTAUTHL).                       *
*                                                                    *
*    DYNAAPF REQUIRES AC(1) AND APF AUTHORIZED LIBRARY RESIDENCE.    *
*           ***---> THIS VERSION FOR MVS 3.8J ONLY <---***           *
*                                                                    *
*   Contributed to the Hercules project by Sam Bass. Fixes and       *
*     improvements by G. Postpischil.                                *
*                                                                    *
*--------------------------------------------------------------------*
*   //jobname JOB (acct,suba),APF,CLASS=A,MSGCLASS=X,MSGLEVEL=(1,1)  *
*   //APFADD   EXEC PGM=DYNAAPF                                      *
*   //STEPLIB  DD  DISP=SHR,DSN=SYS1.LINKLIB (AUTHORIZED LIBRARY)    *
*   //APFLIB   DD  DISP=SHR,DSN=APF.LIBRARY.TO.AUTHORIZE             *
*--------------------------------------------------------------------*
         PRINT &PRTSOR       SAVE A TREE                        GP06285
DYNAAPF  PGMHEAD ZERO,PARM=R1                                   GP06285
         MVC   APFLIB(PATSIZE),PATLIB  MOVE PATTERN DATA        GP06285
         STM   R12,R13,LISTESTA   SET INFO FOR ESTAE EXIT       GP06285
         SPACE 1
&ROUT    SETC  '2'     WTO - MASTER CONSOLE INFORMATIONAL       GP06285
&DESC    SETC  '4'           SYSTEM STATUS                      GP06285
&SPL     SETC  '245'         APF TABLE SUBPOOL                  GP06285
         SPACE 1
         BAL   R14,OPEN                                         GP06285
         LTR   R15,R15             OK
         BNZ   ERROPEN              NO, EXIT
         SPACE 1
         BAL   R14,GETAPFAD                STD LINKAGE          GP06285
         LTR   R15,R15
         BNZ   BADTABLE
         SPACE 1
APFOK    BAL   R14,SEARCH    PREVENT DUPLICATES                 GP06285
         CH    R15,=H'4'     IS IT ALREADY THERE ?              GP06285
         BE    DSNNOK
         CH    R15,=H'8'     IS THE APF TABLE FULL ?            GP06285
         BE    APFFULL
         BH    BADTABLE             BAD TABLE
         SPACE 1
         LA    R3,LISTESTA   PARM AREA                          GP06285
         ESTAE ESTAEXIT,CT,PARAM=(R3),TERM=YES                  GP06285
         BAL   R14,GETMAIN   GET NEW STORAGE                    GP06285
         LTR   R15,R15
         BNZ   GETMFAIL
         SPACE 2
         BAL   R14,ASKIFOK   REQUEST PERMISSION                 GP06285
         LTR   R15,R15             OK TO ADD
         BNZ   DENIED               NO, EXIT
         SPACE 2
         BAL   R14,BUILDNEW  BUILD NEW TABLE                    GP06285
         SPACE 1
         STIMER REAL,DINTVL==C'00000500'  WAIT ON TABLE USERS ? GP06285
         SPACE 2
APFADDED MVC   APFL001D,APFDSN
         SLR   R0,R0               THIS MUST BE HERE
         WTO   ('APFL001 THE FOLLOWING DATASET SUCCESSFULLY ADDED',D), X
               ('APFL001 TO SYSTEM APFTABLE',D),                       X
               ('APFL001 DSN:                                          X
                 ',DE),                                                X
               ROUTCDE=&ROUT,DESC=&DESC
APFL001D EQU   *-2-44,44
         SPACE 1
         BAL   R14,FREEMAIN  GET RID OF OLD TABLE               GP06285
         B     MAINRCXX
         EJECT ,
*--------------------------------------------------------------------*
*              APFLIB OPEN FAILED                                    *
*--------------------------------------------------------------------*
ERROPEN  SLR   R0,R0               THIS MUST BE HERE
         WTO   ('APFL230 APFLIB DD MISSING OR INCORRECT, CORRECT ',D), X
               ('APFL230 AND RESUBMIT JOB',DE),                        X
               ROUTCDE=&ROUT,DESC=&DESC
         LA    R15,16
         B     MAINRCXX
         SPACE 2
*--------------------------------------------------------------------*
*              MESSED UP APFTABLE                                    *
*--------------------------------------------------------------------*
BADTABLE MVC   APFL240D,APFDSN
         SLR   R0,R0               THIS MUST BE HERE
         WTO   ('APFL240 THE FOLLOWING DATA SET NOT ADDED TO',D),      X
               ('APFL240 APFTABLE DUE TO UNKNOWN TABLE FORMAT',D),     X
               ('APFL240 DSN:                                          X
                 ',DE),                                                X
               ROUTCDE=&ROUT,DESC=&DESC
APFL240D EQU   *-2-44,44
         LA    R15,20
         B     MAINRCXX
         SPACE 2
*--------------------------------------------------------------------*
*              DSN ALREADY IN APFTABLE                               *
*--------------------------------------------------------------------*
DSNNOK   MVC   APFL250D,APFDSN
         SLR   R0,R0               THIS MUST BE HERE
         WTO   ('APFL250 THE FOLLOWING DATASET IS ALREADY',D),         X
               ('APFL250 IN APFTABLE. REQUEST REJECTED',D),            X
               ('APFL250 DSN:                                          X
                 ',DE),                                                X
               ROUTCDE=&ROUT,DESC=&DESC
APFL250D EQU   *-2-44,44
         LA    R15,24
         B     MAINRCXX
*--------------------------------------------------------------------*
*              GETMAIN FAILURE                                       *
*--------------------------------------------------------------------*
GETMFAIL MVC   APFL260D,APFDSN
         SLR   R0,R0               THIS MUST BE HERE
         WTO   ('APFL260 THE FOLLOWING DATASET NOT ADDED TO ',D),      X
               ('APFL260 APFTABLE DUE TO INSUFFICENT SPACE',D),        X
               ('APFL260 DSN:                                          X
                 ',DE),                                                X
               ROUTCDE=&ROUT,DESC=&DESC
APFL260D EQU   *-2-44,44
         LA    R15,12
         B     MAINRCXX
         SPACE 2
*--------------------------------------------------------------------*
*              APFTABLE IS FULL                                      *
*--------------------------------------------------------------------*
APFFULL  MVC   APFL270D,APFDSN
         SLR   R0,R0               THIS MUST BE HERE
         WTO   ('APFL270 THE FOLLOWING DATASET NOT ADDED TO ',D),      X
               ('APFL270 APFTABLE DUE TO APF TABLE ALREADY FULL',D),   X
               ('APFL270 DSN:                                          X
                 ',DE),                                                X
               ROUTCDE=&ROUT,DESC=&DESC
APFL270D EQU   *-2-44,44
         LA    R15,24
         B     MAINRCXX
         SPACE 2
*--------------------------------------------------------------------*
*              OPERATOR DENIED REQUEST                               *
*--------------------------------------------------------------------*
DENIED   MVC   APFL280D,APFDSN
         SLR   R0,R0               THIS MUST BE HERE
         WTO   ('APFL280 THE FOLLOWING DATASET NOT ADDED TO ',D),      X
               ('APFL280 APFTABLE DUE TO OPERATOR DENIAL',D),          X
               ('APFL280 DSN:                                          X
                 ',DE),                                                X
               ROUTCDE=&ROUT,DESC=&DESC
APFL280D EQU   *-2-44,44
         LA    R15,8
         SPACE 1
*--------------------------------------------------------------------*
*              MAIN RETURN                                           *
*--------------------------------------------------------------------*
MAINRCXX LR    R9,R15              SAVE RC                      GP06285
         ESTAE 0             CANCEL ESTAE                       GP06285
         TM    WFLAG,$ENQ          ENQ BEING HELD
         BZ    MAINRCX1
         DEQ   MF=(E,ENQLIST)  DEQUEUE                          GP06285
         NI    WFLAG,255-$ENQ      TURN OFF ENQ
         SPACE 1
MAINRCX1 LM    R2,R3,NEWAPF@  GET ADDRESS/LENGTH OF NEW TABLE   GP06285
         LTR   R2,R2         ANY ADDRESS ?                      GP06285
         BZ    MAINRCX2      NO; DONE ?                         GP06285
         LTR   R3,R3         ANY LENGTH ?                       GP06285
         BZ    MAINRCX2      NO; DONE ?                         GP06285
         MODESET KEY=ZERO    GET PRIVIED                        GP06285
         FREEMAIN RC,A=(R2),LV=(R3),SP=&SPL                     GP06285
         MODESET KEY=NZERO   GET NORMAL                         GP06285
         XC    NEWAPF@,NEWAPF@                                  GP06285
         SPACE 1
MAINRCX2 PGMEXIT RC=(R9)           RESTORE RC                   GP06285
         SPACE 3
*--------------------------------------------------------------------*
*                                                                    *
*              OPEN APFLIB AND GET DSNAME AND LENGTH                 *
*                                                                    *
*--------------------------------------------------------------------*
         PUSH  USING                                            GP06285
OPEN     LR    R10,R14       SAVE RETURN ADDRESS                GP06285
         LA    R9,APFLIB                                        GP06285
         USING IHADCB,R9                                        GP06285
         LA    R0,APFJFCB    GET ADDRESS OF JFCB                GP06285
         STCM  R0,7,APFEXLST+1  BUILD EXIT LIST                 GP06285
         LA    R0,APFEXLST   GET EXIT LIST ADDRESS              GP06285
         STCM  R0,7,DCBEXLSA
         LR    R10,R14       PRESERVE RETURN ADDRESS            GP06285
         OPEN  ((R9)),MF=(E,OPENLIST)                           GP06285
         TM    DCBOFLGS,DCBOFOPN  DID IT OPEN ?                 GP06285
         BZ    OPENRC04      NO; RETURN ERROR                   GP06285
         SPACE 1
         RDJFCB MF=(E,OPENLIST)                                 GP06285
         LR    R5,R15        PRESERVE RETURN CODE               GP06285
         CLOSE MF=(E,OPENLIST)     CLOSE                        GP06285
         LTR   R15,R5        CHECK RETURN                       GP06285
         BNZ   OPENRC04
         MVC   APFDSN,JFCBDSNM     MOVE IN DSN
         MVC   APFVOL,JFCBVOLS     MOVE IN VOLSER
         SPACE 1
         MVI   APFDSNL,L'APFDSN+L'APFVOL  PRESET MAX LENGTH     GP06285
         SLR   R15,R15       SET GOOD RETURN                    GP06285
         TRT   APFDSN,TRTTBL       FIND 1ST BLANK
         BZ    OPENRCXX      NONE
         LA    R0,APFDSN-L'APFVOL  FINAGLE TO GET COMBINED LEN  GP06285
         SR    R1,R0         MINUS @ OF APFDSN                  GP06285
         STC   R1,APFDSNL          SAVE LENGTH
OPENRCXX BR    R10           RETURN TO CALLER                   GP06285
         SPACE 1
OPENRC04 LA    R15,4
         BR    R10           RETURN TO CALLER                   GP06285
         SPACE 1
OPENLIST OPEN  (PATLIB,(INPUT,REREAD)),MF=L                     GP06285
         SPACE 1
TRTTBL   DC    256X'0'
         ORG   TRTTBL+C' '
         DC    X'04'
         ORG   TRTTBL+256
         POP   USING                                            GP06285
         SPACE 1
*--------------------------------------------------------------------*
*                                                                    *
*              GET OLD APFTABLE ADDRESS                              *
*                                                                    *
*--------------------------------------------------------------------*
GETAPFAD LR    R10,R14       SAVE RETURN ADDRESS                GP06285
         ENQ   MF=(E,ENQLIST),RET=HAVE                          GP06285
         OI    WFLAG,$ENQ
         LA    R15,4                                            GP06285
         ICM   R0,15,OLDAPF@     DONE ALREADY ?                 GP06285
         BNZ   GAPFRCXX                                         GP06285
         L     R1,CVTPTR     CVT ADDRESS                        GP06285
         MVC   OLDAPF@,CVTAUTHL-CVT(R1)                         GP06285
*  NOTA BENE: 0 ADDRESS WILL BE HANDLED CORRECTLY               GP06285
         SLR   R15,R15                                          GP06285
GAPFRCXX BR    R10           RETURN TO CALLER                   GP06285
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              SCAN APFTABLE TO SEE IF DSN ALREADY THERE             *
*                                                                    *
*--------------------------------------------------------------------*
SEARCH   STM   R0,R15,SUBSAVE              SAVE REGS            GP06285
         ICM   R9,15,OLDAPF@     LOAD AND TEST OLD TABLE        GP06285
         BZ    SRCHRC00      NONE; NO NEED TO SEARCH            GP06285
         LA    R8,2(,R9)           POINT TO 1ST ENTRY
         SLR   R6,R6
         ICM   R6,1,1(R9)    CHECK IF VALID                     GP06285
         BZ    SRCHRC12      NO ENTRIES ?                       GP06285
         CH    R6,=H'255'          NON-DYNAMIC MAX ENTRIES      MVS38
         BNL   SRCHRC08                                         GP06285
         SLR   R4,R4                                            GP06285
         IC    R4,APFDSNL    GET LENGTH OF DSN                  GP06285
         SPACE 1
*   SCAN THRU APF TABLE
*
         SLR   R7,R7
SRCH0200 IC    R7,0(,R8)           ENTRY LENGTH
         CH    R7,=AL2(L'APFVOL+L'APFDSN)  VALID LENGTH ?       GP06285
         BH    SRCHRC12             NO, ERROR
         CLM   R4,1,0(R8)    MATCHING LENGTH ?                  GP06285
         BNE   SRCHBUMP      NO; TRY NEXT                       GP06285
         EX    R4,EXCLCDSN   DO VOLUME AND DSN MATCH ?          GP06285
         BE    SRCHRC04             YES,
SRCHBUMP LA    R8,1(R7,R8)          NO, NEXT ENTRY              GP06285
         BCT   R6,SRCH0200         LOOP
         ST    R8,OLDAPFN    SAVE CURRENT END OF APFTBLE
SRCHRC00 SLR   R15,R15                                          GP06285
         B     SRCHEXIT
         SPACE 1
SRCHRC12 LA    R15,12              BAD ENTRIES
         B     SRCHEXIT
SRCHRC08 LA    R15,8               MAX ENTRIES
         B     SRCHEXIT
SRCHRC04 LA    R15,4               DATASET ALREADY THERE
         SPACE 1
SRCHEXIT LM    R0,R14,SUBSAVE              RESTORE REGS         GP06285
         BR    R14                         EXIT
EXCLCDSN CLC   APFDSNL(0),0(R8)    IS IT THE SAME?              GP06285
         SPACE 1
*--------------------------------------------------------------------*
*                                                                    *
*              GETMAIN NEW APFTABLE                                  *
*                                                                    *
*--------------------------------------------------------------------*
GETMAIN  STM   R0,R15,SUBSAVE   SAVE A BIT                      GP06285
         L     R2,OLDAPFN    GET END OF TABLE                   GP06285
         S     R2,OLDAPF@    MINUS BEGINNING                    GP06285
         BP    GETM0100      OK IF SET                          GP06285
         LA    R2,2          OVERHEAD FOR COUNTER               GP06285
         SPACE 1
GETM0100 SLR   R3,R3                                            GP06285
         IC    R3,APFDSNL    GET DSN LENGTH                     GP06285
         LA    R3,1(R2,R3)   ADD LENGTH+VOLSER TO DSNLEN        GP06285
         ST    R3,NEWAPF#    NEW ENTRY LENGTH                   GP06285
         SPACE 1
         MODESET KEY=ZERO,MODE=SUP
         GETMAIN RC,LV=(R3),SP=&SPL                             GP06285
         ST    R1,NEWAPF@
         LR    R5,R15        SAVE RETURN CODE                   GP06285
         MODESET KEY=NZERO,MODE=PROB
         LTR   R15,R5        RESTORE RETURN                     GP06285
         BZ    GETMRCXX                                         GP06285
         LA    R15,4
GETMRCXX LM    R0,R14,SUBSAVE  RESTORE ALL BUT R15              GP06285
         BR    R14                         EXIT
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              ASK IF OK TO ADD DSNAME TO APFTABLE                   *
*                                                                    *
*--------------------------------------------------------------------*
ASKIFOK  STM   R0,R15,SUBSAVE                                   GP06285
         SLR   R0,R0
*
*              PUT WTOR HERE TO ASK IF OK
*
ASKRC00  SLR   R15,R15
ASKRCXX  LM    R0,R14,SUBSAVE                                   GP06285
         BR    R14
         SPACE 2
*--------------------------------------------------------------------*
*                                                                    *
*              BUILD NEW APFTABLE                                    *
*                                                                    *
*--------------------------------------------------------------------*
BUILDNEW STM   R0,R15,SUBSAVE                                   GP06285
*              MOVE CURRENT APFTABLE TO NEW TABLE                    *
*                                                                    *
         L     R2,NEWAPF@       NEW TABLE ADDRESS               GP06285
         LM    R4,R5,OLDAPF@   OLD TABLE ADDRESS AND END        GP06285
         LR    R9,R2         SAVE NEW TABLE ADDRESS             GP06285
         SR    R5,R4               MINUS BEGINNING = LENGTH
         SPACE 1
         MODESET KEY=ZERO,MODE=SUP
*  N.B. R4 MAY BE ZERO IF THERE IS NO OLD TABLE                 GP06285
         LTR   R4,R4         IS THERE A TABLE ?                 GP06285
         BNZ   UPDC0080      YES; PROCEED                       GP06285
         LA    R4,=H'0'      MOVE A ZERO COUNTER                GP06285
         LA    R5,2          FOR NEW TABLE INITIALIZATION       GP06285
         SPACE 1
UPDC0080 LR    R3,R5         MOVE ONLY INPUT LENGTH             GP06285
         MVCL  R2,R4               MOVE CURRENT APF TO NEW      GP06285
*  N.B. MVCL LEAVES R2 SET TO INSERTION ADDRESS FOR NEW ENTRY   GP06285
         IC    R6,APFDSNL    LENGTH OF DSN                      GP06285
         EX    R6,ADDMVC1    APPEND NEW DSN TO TABLE            GP06285
         SPACE 1
         SLR   R2,R2
         IC    R2,1(,R9)                                        GP06285
         LA    R2,1(,R2)                                        MVS38
         STC   R2,1(,R9)     INCREMENT ENTRY COUNT              GP06285
         SPACE 1                   USE COMPARE AND SWAP
         L     R11,CVTPTR          CVT ADDRESS                  GP06285
UPDC0120 L     R1,CVTAUTHL-CVT(,R11)                            GP06285
         CS    R1,R9,CVTAUTHL-CVT(R11)                          GP06285
         BNZ   UPDC0120      ITS CHANGED                        GP06285
         XC    NEWAPF@,NEWAPF@  NO FREEMAIN TO BE DONE          GP06285
         SPACE 1
UPDC0130 MODESET KEY=NZERO,MODE=PROB                            GP06285
         SPACE 1
         TM    WFLAG,$ENQ          ENQ BEING HELD
         BZ    UPDC0140
         DEQ   MF=(E,ENQLIST)  DEQUEUE                          GP06285
         NI    WFLAG,255-$ENQ      TURN OFF ENQ
UPDC0140 LM    R0,R14,SUBSAVE  RESTORE REGS                     GP06285
         BR    R14                         EXIT
ADDMVC1  MVC   0(0,R2),APFDSNL
         SPACE 3
*--------------------------------------------------------------------*
*                                                                    *
*              FREEMAIN OLD APFTABLE                                 *
*                                                                    *
*--------------------------------------------------------------------*
FREEMAIN STM   R0,R15,SUBSAVE  SAVE REGS                        GP06285
         SLR   R15,R15
         ICM   R2,15,OLDAPF@     ANY TO FREE ?                  GP06285
         BZ    FREERCXX           NO; IGNORE                    GP06285
         L     R3,OLDAPFN
         SR    R3,R2
         BNP   FREERCXX           NO; IGNORE                    GP06285
         MODESET KEY=ZERO,MODE=SUP
         FREEMAIN RC,LV=(R3),A=(R2),SP=&SPL
         LR    R5,R15             SAVE RETURN CODE              GP06285
         MODESET KEY=NZERO,MODE=PROB
         XC    OLDAPF@,OLDAPF@  SIGNAL DONE                     GP06285
         LTR   R15,R5             TEST RETURN                   GP06285
         BZ    FREERCXX           GOOD                          GP06285
         LA    R15,4
FREERCXX LM    R0,R14,SUBSAVE                                   GP06285
         BR    R14                         EXIT
         SPACE 1                                                GP06285
PATLIB   DCB   DDNAME=APFLIB,DSORG=PO,EXLST=PATEXLST,MACRF=(R)  GP06285
PATEXLST DC    0F'0',X'87',AL3(APFJFCB-APFJFCB)                 GP06285
PATENQ   ENQ   (ENQMAJ,ENQMIN,E,L'ENQMIN,SYSTEM),RET=HAVE,MF=L  GP06285
PATSIZE  EQU   *-PATLIB      SIZE FOR INITIALIZATION MOVE       GP06285
         SPACE 1
ENQMAJ   DC    CL8'DYNAAPF'                                     GP06285
ENQMIN   DC    CL7'APFLIST'
         SPACE 3                                                GP06285
         PUSH  USING                                            GP06285
         DROP  ,                                                GP06285
         USING ESTAEXIT,R15  DECLARE BASE                       GP06285
ESTAEXIT CH    R0,=H'12'     SDWA PRESENT ?                     GP06285
         BNE   ESTAINIT      YES; PROCEED                       GP06285
         SLR   R15,R15       PERCOLATE                          GP06285
         BR    R14           RETURN                             GP06285
         SPACE 1                                                GP06285
ESTAINIT STM   R14,R12,12(R13)  SAVE A BIT                      GP06285
         LR    R12,R15       MAKE SAFER BASE                    GP06285
         DROP  R15                                              GP06285
         USING ESTAEXIT,R12  NEW BASE                           GP06285
         LR    R10,R1        COPY SDWA                          GP06285
         USING SDWA,R10      DECLARE IT                         GP06285
         L     R1,SDWAPARM   GET PASSED PARM                    GP06285
         LM    R7,R8,0(R1)   RELOA DBASE AND SAVE/WORK          GP06285
         USING SAVE,R8       DECLARE IT                         GP06285
         LM    R2,R3,NEWAPF@  GET ADDRESS/LENGTH OF NEW TABLE   GP06285
         LTR   R2,R2         ANY ADDRESS ?                      GP06285
         BZ    ESTADONE      NO; DONE ?                         GP06285
         LTR   R3,R3         ANY LENGTH ?                       GP06285
         BZ    ESTADONE      NO; DONE ?                         GP06285
         XC    NEWAPF@,NEWAPF@   DISCOURAGE RECURSION           GP06285
         MODESET KEY=ZERO    GET PRIVIED                        GP06285
         FREEMAIN RC,A=(R2),LV=(R3),SP=&SPL                     GP06285
         MODESET KEY=NZERO   GET NORMAL                         GP06285
*DEBUG*  WTO   'DYNAAPF: SP &SPL FREED'                         GP06285
ESTADONE LM    R14,R12,12(R13)   RELOAD                         GP06285
         SLR   R15,R15       JUSTIN CASE                        GP06285
         BR    R14                                              GP06285
         POP   USING                                            GP06285
         SPACE 1                                                GP06285
         LTORG ,
         SPACE 1
SAVE     DSECT ,             SAVE AND WORK AREA                 GP06285
SUBSAVE  DS    16A(0)        SUBROUTINE SAVE AREA               GP06285
LISTESTA DS    A(0,0)        BASE/SAVE ADDRESSES FOR ESTAE      GP06285
OLDAPF@  DC    A(0)    1/2   OLD APF TABLE ADDRESS
OLDAPFN  DC    A(0)    2/2     ADDRESS OF FIRST BYTE AFTER TABLE
NEWAPF@  DC    A(0)    1/2   NEW APF TABLE ADDRESS
NEWAPF#  DC    A(0)    2/2      AND LENGTH
WFLAG    DC    AL1(0)
$ENQ     EQU   X'80'               ENQ IS BEING HELD
APFDSNL  DC    X'00'    ---+       KEEP
APFVOL   DC    CL6' '      |        THESE
APFDSN   DC    CL44' '  <--+         TOGETHER
APFLIB   DCB   DDNAME=APFLIB,DSORG=PO,EXLST=APFEXLST,MACRF=(R)
APFEXLST DC    0F'0',X'87',AL3(APFJFCB)
ENQLIST  ENQ   (1-1,2-2,E,L'ENQMIN,SYSTEM),RET=HAVE,MF=L        GP06285
APFJFCB  DC    0XL176'00'
         IEFJFCBN ,          USE JFCB DEF IN WORK AREA          GP06285
SAVEEND  EQU   *             END OF WORK AREA                   GP06285
         SPACE 2
*--------------------------------------------------------------------*
*    CONTROL BLOCK MAPPINGS                                          *
*--------------------------------------------------------------------*
         PRINT &PRTSYS                                          GP06285
         CVT   DSECT=YES,LIST=NO
         SPACE 1
         DCBD  DEVD=DA,DSORG=PO                                 GP06285
         SPACE 1
         IHASDWA ,                                              GP06285
         END   ,
