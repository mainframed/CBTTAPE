COPYFDR  TITLE 'C O P Y F D R  ***  RESTORE FDR TAPE TO CLEAN PACK'
**   MODIFIED FROM BELL TEL LABS/KARL BARNHARDT - SQUISH UTILITY  S/BTC
*           MODIFIED BY G. POSTPISCHIL FROM COPYVOL.
*
*    CREATES A NEW VOLUME WITH DATA SETS COPIED FROM AN FDR DUMP TAPE.
*    1) TESTED ONLY ON A TAPE WITH A SINGLE BLOCK FOR THE DIRECTORY.
*    2) TESTED ONLY ON A SINGLE VOLUME INPUT FILE - FAIRLY SURE THAT
*       CURRENT @FDRREAD CANNOT HANDLE MULTI-VOLUME INPUT.
*    3) OUTPUT VOLUME MUST BE SAME DEVICE TYPE AS SOURCE VOLUME.
*    4) STILL HAS UNEXERCISED CODE FROM COPYVOL I WAS TOO BUSY TO
*       DELETE. SHOULD MAKE NO DIFFERENCE (E.G., MIGRATE COPIES/SAS).
*
*        QUESTIONS AND CORRECTIONS SHOULD BE ADDRESSED TO:       81047
*              G. POSTPISCHIL   gerhard@postpischil.com         GP03118
*              EXPERT SYSTEM PROGRAMMING                        GP03118
*              176 OLD STAGE COACH ROAD                         GP03118
*              BRADFORD, VT 05033-8844                          GP03118
*
* DESCRIPTION
* ------------
*
*    DEFAULT PROCESSING :                                        81047
*                                                                81047
*        VSAM DATASETS - DSCB COPIED TO SAME VTOC LOCATION (WHEN FREE)
*                        DATA COPIED TO SAME VOLUME LOCATION (ABSTRK)
*        ISAM/TCAM -     DATA COPIED TO SAME VOLUME LOCATION     81047
*        SAM (PO & PS) - UNUSED EXTENTS ARE RELEASED AFTER COPY  81047
*                        CONTIGUOUS EXTENTS ARE MERGED           81047
*        OTHER -         COPIED TO AVAILABLE SPACE               81047
*                                                                81047
*  RESTRICTIONS :                                                81047
*  ____________                                                  81047
*                                                                81047
*  SPLIT CYLINDER/SUBALLOCATION CONVERTED TO SEQUENTIAL DATASETS.
*  END-FILE RECORDS WITH KEYS ARE COPIED WITHOUT KEYS.           81047
*  ALTERNATE TRACK ASSIGNMENTS ON THE OUTPUT VOLUME ARE NOT TESTED;
*        THEY MAY BE HONORED FOR DATA TRACKS. FOR ALTERNATE TRACKS IN
*        THE OUTPUT VTOC, PERMANENT I/O ERRORS ARE POSSIBLE.     81208
*                                                                81047
*  MAJOR OPTIONS :                                               81047
*  ____________                                                  81047
*                                                                81047
*     UNUSED TRACKS OF PS DATASETS MAY BE RELEASED (FREE)        81047
*                                                                81047
* THE CATALOG AND PASSWORD DATASETS ARE COPIED NORMALLY FOLLOWING THE
*  COPIES OF ISAM AND OTHER UNMOVABLE DATASETS.  IF THE CATALOG IS TO
*  BE PLACED IMMEDIATELY FOLLOWING THE VTOC, A CODE CHANGE IS REQUIRED-
*  SEE LABEL SPCLOK1 AND EXPAND THE COMMENTED CODE.
* THE VOLUME MUST BE ANALYZED/FORMATTED AT LEAST ONCE PRIOR TO USE;
* THE VTOC CONTENTS IS IRRELEVANT. THE VTOC WILL BE OVERWRITTEN IN
* ITS ENTIRETY BY COPYFDR.
*
* COPYFDR PERFORMS NO ERROR RECOVERY AT ALL. WHEN AN ERROR OCCURS,
*  DIAGNOSTIC INFORMATION IS PRINTED, AND THE PROGRAM ABENDED.   81047
* RUNNING TIME FOR THIS PROGRAM FOR A 3330-11 IS ABOUT 25 MINUTES
*  UNDER SVS ON A 158 (4 MEG, NO PAGING).
* THE PROGRAM SHOULD NOT BE USED TO COPY A DISK CONTAINING SYSTEM
*  DATASETS THAT WERE CAREFULLY ALLOCATED TO OPTIMIZE DISK PERFORMANCE
*  UNLESS THESE DATASETS ARE FLAGGED NOT MOVABLE (EITHER VIA DSORG,
*  BY SPECIFICATION OF PARM=ASIS OR PARM=SYSU, OR BY INCLUDING A SYSIN
*  OPTION 'U' CARD FOR THE APPROPRIATE DATASETS).
*
* REQUIREMENTS
* ------------
*
* THE PROGRAM USES THE 370 INSTRUCTION SET AND BYTE ALIGNMENT FEATURE.
* TO RUN UNDER VS, THE MODULE MUST BE APF AUTHORIZED AND MUST RESIDE
*  IN AN AUTHORIZED LIBRARY.                                     81047
*                                                                81047
* STORAGE:                                                       81047
*                                                                81047
* FOR NORMAL COPY, WITH 'NUMBUFFS' 5 OR LESS, 128K SUFFICE FOR MOST
*  INPUT VOLUMES. SPACE FOR THE FORMAT 5 TABLE, AND THE INPUT DSCBS
*  IS ACQUIRED DYNAMICALLY; HENCE THE REGION MAY NEED TO BE INCREASED.
*  196K IS SUFFICIENT FOR TYPICAL RUNS. THIS SIZE WILL NOT SUFFICE FOR
*  3380/3390.    UNDER MVS USE A 4096K OR LARGER REGION.
*                                                                81047
* DDNAMES:                                                       81047
* TAvolser - THE INPUT VOLUME, needs DSN, DISP=OLD, UNIT, VOLSER.
* SYSUT2 - THE OUTPUT VOLUME, SPECIFIES UNIT AND VOLSER AND DISP=OLD.
*        THIS VOLUME MUST BE MOUNTED PRIVATE, OR MUST HAVE PRIVATE
*        ON THE DD CARD. PERM.RESIDENT ATTRIBUTE CAUSES ERROR FOR
*        MOUNTABLE DRIVES, BUT IS PERMITTED FOR 3350, 3375 AND 3380.
*                                                                81047
* FOR USERS WHO DISLIKE SYSUT DD NAMES, THERE ARE ALTERNATES:
*        DISKOUT
*        OUTDISK
*        DISKOUT
*        OUTDISK
*                                                                81047
* SYSUDUMP - OPTIONAL, RECOMMENDED TO ANALYZE I/O ERRORS.        81047
* SYSPRINT - VBA FORMAT PRINT FILE; REQUIRED - MUST NOT BE DUMMY 81047
* SYSIN    - OPTIONAL INPUT SPECIFICATIONS, ONE PER CARD         79105
*                                                                79105
* PARM OPTIONS (DEFAULT  - COPY,LIST,RLSE) :                     79105
*  NONE OR MORE VALUES, OPTIONALLY SEPARATED BY COMMAS :
*                                                                79105
*   COPY - (DEFAULT) NORMAL COPYFDR PROCESSING (NO IEBCOPY)      79105
*   VTOC - USE INPUT VTOC EXTENTS ON OUTPUT VOLUME               81047
*   EVEN - CONTINUE PROCESSING EVEN WITH I/O ERRORS. GENERALLY WILL
*             COPY A TRACK, SKIPPING RECORDS WITH DATACHECKS (THOSE ARE
*             COPIED AS READ). MORE SEVERE ERRORS CAUSE THE TRACK TO
*             BE SKIPPED (TREATED AS EMPTY) OR TRUNCATED TO THE LAST
*             PROCESSED RECORD.                                  83094
*   LIST - (DEFAULT) LIST ALL MESSAGES                           79105
*   TERS (OR MSGS) - LIST INDICATIVE, WARNING AND ERROR MESSAGES ONLY
*           SUPPRESSES 'SUCCESSFULLY COPIED' MESSAGES            79105
*   LOTS - PRINTS NORMALLY UNWANTED MESSAGES; E.G. SKIP OF TEMP FILES
*   ASIS - TREAT ALL DATASETS ON THE PACK AS UNMOVABLE           79105
*   SYSU - TREAT ALL SYS1. DATASETS AS UNMOVABLE                 79105
*   SYSP - COPY ALL SYS1. DATASETS PRIOR TO OTHERS OF SAME TYPE  79105
*         (I.E. CYLINDER ALIGNED DATASETS COPIED AFTER ISAM, UNMOVABLE
*         AND CATLG/PASSWORD, BUT PRIOR TO OTHER CYLINDER/TRACK ALIGNED
*         DATA; TRACK ALIGNED ONES COPIED PRIOR TO OTHER TRACK ALIGNED)
*   RLSE - (DEFAULT) RELEASE UNUSED EXTENTS FROM PS/PO DATASETS  79105
*   KEEP - KEEP ALL EXTENTS (ALSO PREVENTS MERGING CONTIGUOUS EXTENTS)
*   FREE (OR TREL) - RELEASE ALL UNUSED TRACKS FOR P.S. DATASETS 79327
*   PORL (OR PREL) - RELEASE UNUSED PDS SPACE                    85289
*   >YYDDD - COPY IF ACCESS DATE HIGHER THAN OR EQUAL YYDDD      81208
*   <YYDDD - COPY IF ACCESS DATE < THAN YYDDD                    81208
*        ACCESS DATE IS DEFINED AS MAX(DS1EXPDT,DS1CREDT,DS1REFD)
*   ORG=XXXX - COPY ONLY DATASETS WITH DSORGS INCLUDED IN THE MASK
*   AUTO - BYPASS WTOR; USE FOR AUTOMATIC NIGHTLY BACKUPS       GP09239
*   DUMP - PRODUCE DUMP AFTER ABNORMAL TERMINATION               81047
*              THIS OPTION IS FORCED FOR AN I/O ERROR.           81047
*   DBUG - PRODUCE DUMP ON ANY TERMINATION (INCLUDING NORMAL)    81047
*                                                                85289
*                                                                79105
*  PLUG : IF YOUR INSTALLATION HAS 'FDR', LOOK INTO THE 'COMPAKTOR'.
*   IT IS NOT QUITE AS CHEAP, BUT IT DOES SOME COPYFDR FUNCTIONS 79105
*   MORE CONVENIENTLY (IT'S A TRIFLE SLOWER WITH PDS COMPRESS).  79105
*                                                                79105
* SYSIN CARDS (FREE FORMAT, COLUMNS 1-80)                        79105
*        SPECIAL PURPOSE CARDS :                                 82298
*                                                                82298
*        VTOC=ABSTRK,EXTENT=TRACKS  SPECIFIES OUTPUT VTOC LOC/SIZE
*          WHERE ABSTRK IS THE TRACK NUMBER RELATIVE TO ZERO     82298
*          (AND GREATER THAN ZERO); TRACKS SPECIFIES THE NUMBER  82298
*           OF TRACKS IN THE NEW VTOC.                           82298
*                                                                83294
*        COND=VALUE    OR CODE=VALUE   - SET AN ERROR CODE AT AND
*          ABOVE WHICH EXECUTION WILL TERMINATE.                 83294
*                                                                82298
*   GENERAL FORMAT:                                              79105
*   FIRST NON-BLANK FIELD - OPTIONS                              79105
*   SECOND NON-BLANK FIELD - DATASET NAME, DSN PREFIX, OR MASK   79105
*   THIRD NON-BLANK FIELD - RENAME OPTION REPLACEMENT STRING     79105
*                                                                79105
*   OPTIONS BYTES :                                              79105
*    * - COMMENTS CARD - PRINTED AS READ                         79105
*    S - SELECT DSN   (ALTERNATE FORM)                           79105
*    I - INCLUDE DATASET                                         79105
*    X - EXCLUDE DSN  (ALTERNATE FORM)                           79105
*    E - EXCLUDE DATASET                                         79105
*    R - RENAME DATASET                                          79105
*    U - TREAT DSN AS UNMOVABLE                                  79105
*    K - KEEP UNUSED EXTENTS (EXTENTS STILL COMPRESSED)          79327
*    F - FREE ALL UNUSED TRACKS FOR PS AND PO DATASETS           85289
*    P - PRIORITY COPY (IF CYL BOUND, PRIOR TO OTHER CYL; IF TRK BOUND,
*                       PRIOR TO OTHERS ON TRK BOUNDARY)         79105
*    N - NORMAL COPY; TURNS OFF PRIORITY AND UNMOVABLE BITS.     79105
*          ALSO RESETS UNMOVABLE RESULTING FROM DSORG=POU        79105
*          BUT DOES NOT INVOKE IEBCOPY TO BE USED FOR DSORG=U    79105
*    A - COPY BY ALLOCATION/ACCESS DATE (IF < OR > OPTION USED)  81208
*    B - BYPASS/RESET DATE CHECK                                 81208
*        A AND B ARE VALID ONLY ON INCLUDE/EXCLUDE CARDS         81208
*    O - PLACE DSCB IN LOWEST FREE VTOC POSITION (AFTER VSAM DONE)
*                                                                79105
*   MASK FIELD                                                   79105
*    1 TO 44 CHARACTERS.  EITHER A QUESTION MARK OR A QUOTE (")  79105
*       REQUEST BYPASS OF CHECK IN THAT POSITION (GENERAL MATCH).
*       COMPARE STARTS WITH LEFT-MOST BYTE FOR DATASETS WHOSE NAME
*       LENGTH IS NO LESS THAN THE MASK LENGTH.                  79105
*        A QUOTE SPECIFIES A GENERAL MATCH CHARACTER WHICH WILL BE
*        COPIED TO THE CORRESPONDING MATCH CHARACTER IN THE RENAME
*        OUTPUT STRING. QUESTION MARKS ARE FOR INPUT SCANNING ONLY,
*        AND ARE NOT VALID IN RENAME OUTPUT STRINGS.             81047
*       EITHER A SEMI-COLON OR AN EXCLAMATION POINT MAY BE USED  81208
*        AS A TERMINAL CHARACTER TO INDICATE A PRECISE NAME;     81208
*        EITHER IS REPLACED BY A BLANK FOR MASK TESTING.         81208
*                                                                79105
*   REPLACEMENT STRING                                           79105
*    1 TO 44 CHARACTERS TO REPLACE MATCHING ONES IN THE MASK FIELD.
*       TRUNCATION OF A NAME DOES NOT RESULT IN AN ERROR.  QUOTES
*       MAY BE USED TO INDICATE REPLACEMENT FROM OLD NAME, AND ARE
*       LIMITED TO THE NUMBER OF QUOTES IN THE INPUT STRING.
*        QUESTION MARKS, SEMI-COLONS AND EXCLAMATION MARKS ARE   81208
*        NOT VALID FOR REPLACEMENT NAMES.                        81208
*                                                                79105
*   PROCESSING :                                                 79105
*                                                                79105
*     GLOBAL PROCESSING MODE IS SET BY THE FIRST INCLUDE/EXCLUDE CARD
*     FOUND.  IF FIRST IS INCLUDE, ONLY DATASETS WHICH MATCH ONE OF
*     THE REQUESTS ARE COPIED.  IF FIRST ONE IS EXCLUDE, DATASETS WILL
*     BE COPIED UNLESS AN EXCLUDE IS FOUND TO MATCH.             79105
*     BOTH INCLUDES AND EXCLUDES MAY BE SUPPLIED IN THE SAME RUN.
*     WHEN A MATCH IS FOUND, COMPARISON CONTINUES UNTIL END OF INPUT.
*     E.G. IF GLOBAL OPTION IS INCLUDE, AND ONLY A MATCH ON INCLUDE IS
*     FOUND, THE DATASET IS COPIED.  IF FOLLOWING THE INCLUDE MATCH
*     ANOTHER MATCH OCCURS ON AN EXCLUDE, THE DATASET WILL BE SKIPPED,
*     BUT COMPARISON CONTINUES; IF THERE IS ANOTHER INCLUDE MATCH, IT
*     WOULD GET COPIED.                                          79105
*                                                                79105
*  EXAMPLES :                                                    79105
*                                                                79105
*     PI  SYS?.   INCLUDE SYSA./SYSB./.../SYS9. DATASETS WITH PRIORITY
*       X  SYS#.   BUT EXCLUDE SYS#. DATASETS                    79105
*     IU  SYS#.COPY  BUT INCLUDE SYS#.COPY; TREAT AS UNMOVABLE   79105
*                                                                79105
*      RU    SYS1.  OLDSYS1.                                     79105
*      * ABOVE RENAMES ALL SYS1. DATASETS TO OLDSYS1. AND TREATS 79105
*      *    THEM AS UNMOVABLE.                                   79105
*      R SYS".   OLD".                                           81047
*      * ABOVE RENAMES ALL DATASETS STARTING WITH 'SYS' HAVING A FOUR
*      * BYTE INDEX LENGTH TO OLD, AND COPIES THE FOURTH BYTE, E.G.
*      * SYS1.SVCLIB IS RENAMED OLD1.SVCLIB; SYS2.PROCLIB TO     81047
*      * OLD2.PROCLIB, ETC.                                      81047
*      R  SYS". NEW".       RENAMES ALL SYS DATASETS TO NEW      81047
*                                                                79105
* DEPENDENCIES:                                                  79105
*                                                                79105
*  SUBROUTINES @SERVICE, @DCBEXIT, @PRINTER AND @INPREAD         81208
*  MACROS : AS DISTRIBUTED WITH TSMDTR, DTSDTR, AND ESPDTR.     GP13433
*     COPY MEMBER SYSPARM SHOULD BE SET CORRECTLY FOR YOUR       81208
*     INSTALLATION (&SYSTEM, &LOCAL, &MVS/&SVS SETB)             82271
*                                                                79105
* UNBELIEVABLY UNSURPASSABLE ERROR MESSAGES :                    79105
*                                                                79105
*   ABEND USER 1  (PRECEDED BY DIAGNOSTIC MESSAGES)              79105
*     ERROR IN PARM FIELD    (NO DUMP)                           79105
*     ERROR IN SYSIN CARDS   (NO DUMP)                           79105
*     SYNSIZE VALUE TOO SMALL FOR INPUT (TABLE OVERFLOW)         79105
*     INPUT/OUTPUT VOLUMES NOT COMPATIBLE                        79105
*     INPUT/OUTPUT VOLUME NOT SUPPORTED                          79105
*     OUTPUT VOLUME IS PERM. RESIDENT OR NOT PRIVATE             81047
*     OPERATOR DENIED COPY REQUEST                               79105
*     MORE THAN MAXFREE EXTENTS REQUIRED (SEE NUMDSCB5)          81208
*     LOGIC OR PROCESSING ERROR ALLOCATING DATASET OR IN FREE    79105
*       EXTENT TABLE                                             79105
*                                                                79105
*   ABEND USER 2             (DUMP FORCED)                       79105
*     UNRECOVERABLE I/O ERROR ON INPUT OR OUTPUT VOLUME          79105
*                                                                83294
*   ABEND USER 8                                                 83294
*     CONDITION CODE REACHED/EXCEEDED USER'S REQUESTED STOP CODE 83294
*                                                                79105
*   ABEND USER 666                                               81047
*     TAKEN AT END OF PROGRAM WHEN DBUG OPTION IS CHOSEN         81047
*                                                                81047
*   ABEND USER 1403                                              81047
*     SYSPRINT DD MISSING OR DUMMY                               81047
*                                                                79105
*   COMPLETION CODES                                             79105
*                                                                79105
*   CODE 0 - EVERYTHING WORKED ?                                 79105
*   CODE 1 - ERROR WRITING FMT 5 DSCBS; PACK USABLE BUT NOT PERFECT
*              OR ALTERNATE TRACK USED FOR DATA.                 81208
*   CODE 2 - ANY ERROR IN IEBCOPY; DATASET COPIED ASIS           79105
*   CODE 4 - AT LEAST ONE DATASET WITH BAD OR SPLIT EXTENTS UNCOPIED
*            OR INCLUDED/EXCLUDED ENTRY NOT FOUND.
*   CODE 8 - UNMOVABLE/ISAM DATASET NOT COPIED - UNABLE TO ALLOCATE
*              OR ALTERNATE TRACK USED IN OUTPUT VTOC.           81208
*    ANY OF THE ABOVE ARE OR'ED TOGETHER                         79105
         TITLE 'C O P Y V O L  ***  MACROS AND ASSEMBLY OPTIONS' 81047
         MACRO
&LABEL   WRTVTOC &LOC,&ADR                                       81047
         GBLC  &MACPLAB                                          81208
&LABEL   MVI   TRAKFLAG,TKDSCBWT  SET DSCB WRITE REQUEST         81047
         LA    R1,&LOC       SET DSCB ADDRESS                    81047
&MACPLAB SETC  ''                                                81208
         MACPARM R2,&ADR,NULL=DB                                 81208
         SUBCALL TRAKCOPY    WRITE THE DSCB                      81208
         SUBCALL NEXTBLOK    GET NEXT BLOCK ADDRESS INTO BLOCKAD 81047
         MEND
         SPACE 1
         MACRO
&LABEL   READVTOC &LOC
&LABEL   LA    R1,&LOC       SET INPUT ADDRESS                   81047
         MVI   TRAKFLAG,TKDSCBRD  SET FOR DSCB READ              81047
         SUBCALL TRAKCOPY    READ THE DSCB                       81047
         MEND
         SPACE 1                                                 81047
         MACRO ,                                                 81047
&NM      SUBENT &SAVE=YES,&WRK=R9,&BASE=R10   SUBROUTINE ENTRY   87135
&NM      STM   R14,R12,12(R13)                                   81047
         AIF   (N'&BASE LE 1).ONEBASE                            87135
         BALR  &BASE(1),0                                        87135
         USING *,&BASE(1),&BASE(2)                               87135
         LA    &BASE(2),2048(,&BASE(1))                          87135
         LA    &BASE(2),2048(,&BASE(2))                          87135
         AGO   .COMBASE                                          87135
.ONEBASE BALR  &BASE,0       MAKE LOCAL BASE                     81047
         DROP  ,             DROP EVERYTHING                     81047
         USING *,&BASE                                           81047
.COMBASE USING WORKAREA,R11  GLOBAL BASE                         81208
         USING SAVE,R12                                          81208
         AIF   ('&SAVE' EQ 'NO').NOSAVE                          81047
         LA    &WRK,72(,R13)   GET LOWER SAVE AREA               81047
         ST    R13,4(,&WRK)                                      81047
         ST    &WRK,8(,R13)  LINK                                81047
         LR    R13,&WRK      FLIP                                81047
.NOSAVE  MEND  ,                                                 81047
         SPACE 1                                                 81047
         MACRO ,                                                 81047
&NM      SUBEX &OFF=,&RC=0                                       81047
         GBLC  &MACPLAB                                          81208
&NM      L     R13,4(,R13)   GET HIGHER SAVE AREA                81047
&MACPLAB SETC  ''                                                81208
         MACPARM R15,&RC     SET RETURN CODE                     81208
         LM    R0,R12,20(R13)  RESTORE                           81047
         L     R14,12(,R13)  SET RETURN ADDRESS                  81047
         AIF   ('&OFF' EQ '').NOOFF                              81047
         B     &OFF.(R14)    RETURN WITH OFFSET                  81047
         AGO   .COMOFF                                           81047
.NOOFF   BR    R14           RETURN TO CALLER                    81047
.COMOFF  MEND  ,                                                 81047
         SPACE 1                                                 81047
         MACRO ,                                                 81047
&NM      SUBCALL &NAME,&R1                                       81208
         GBLC  &MACPLAB                                          81208
&MACPLAB SETC  '&NM'                                             81208
         MACPARM R1,&R1,NULL=SKIP                               GP02241
&MACPLAB L     R15,=A(&NAME)  GET SUBROUTINE ADDRESS             81208
         BALR  R14,R15       INVOKE IT                           81047
         MEND  ,                                                 81047
         SPACE 1                                                 81047
         MACRO ,                                                 81047
&NAME    MSG   &TEXT                                             81047
         LCLA  &A                                                81047
&A       SETA  K'&TEXT-2                                         81047
&NAME    DC    AL1(&A-1),CL(&A)&TEXT                             81047
         MEND  ,                                                 81047
         SPACE 1                                                 83094
         MACRO ,                                                 83094
&NM      EMP   &RETRY2,&RETRY=                                   83094
         AIF   ('&RETRY' EQ 'NO').NOTRY                          83094
&NM      BAL   R14,IOATEST   TEST FOR PRIMARY RECOVERY           83094
         AIF   (T'&RETRY2 EQ 'O').NONE                           83094
         AIF   (K'&RETRY2 EQ 0).NONE                             83094
         DC    Y(&RETRY2-TRAKEXCP)  ADVANCED RETRY ROUTINE       83094
         MEXIT ,                                                 83094
.NONE    DC    Y(TRAKQUIT-TRAKEXCP) BOMB ON ERROR                83094
         MEXIT ,                                                 83094
.NOTRY   ANOP  ,                                                 83094
&NM      BAL   R14,IOAQUIT   QUIT ON ERROR                       83094
         MEND  ,                                                 83094
         SPACE 1                                                 85280
         MACRO ,                                                 85280
&A       VOLWO &DDN                                              85280
V&A.DDNM DC    0A(0),CL8'&DDN '  DDNAME                          85280
V&A.UCB  DC    A(0)          INPUT UCB                           85280
V&A.SIZE DC    F'0'          LARGEST UNKEYED BLOCK               85280
V&A.CYL  DC    H'0'          INPUT CYLINDER COUNT                85280
V&A.TPC  DC    H'0'          TRACKS PER CYLINDER                 85280
V&A.EXT  DC    XL8'0'        VTOC EXTENTS                        85280
V&A.VSER DC    CL6' '        INPUT VOLUME                        85280
V&A.DSCB DC    X'00'         DSCBS PER TRACK                     86341
V&A.PDE  DC    X'00'         DIRECTORY ENTRIES PER TRACK         86341
         MEND  ,                                                 85280
         SPACE 1                                                 86138
         MACRO                                                   86138
&NM     VOLUME &A                                                86138
         LCLA  &K                                                86138
         AIF   ('&A' EQ '' OR K'&A GT 6).BAD                     86138
&K       SETA  K'&A-1                                            86138
&NM      DC    AL1(&K),C'&A'                                     86138
         MEXIT                                                   86138
.BAD     MNOTE 8,'VOL-SER MISSING OR TOO LONG.'                  86138
         MEND                                                    86138
         SPACE 1                                                 86138
         MACRO                                                   86138
&NM      DSNAME &A                                               86138
         LCLA  &K                                                86138
         AIF   ('&A' EQ '' OR K'&A GT 44).BAD                    86138
&K       SETA  K'&A-1                                            86138
&NM      DC    AL1(&K),C'&A'                                     86138
         MEXIT                                                   86138
.BAD     MNOTE 8,'DSNAME MISSING OR TOO LONG.'                   86138
         MEND                                                    86138
         SPACE 1                                                 86138
         MACRO                                                   86138
&NM      INDEX &A                                                86138
         LCLA  &K                                                86138
         AIF   ('&A' EQ '' OR K'&A GT 8).BAD                     86138
&K       SETA  K'&A                                              86138
&NM      DC    AL1(&K),C'&A..'                                   86138
         MEXIT                                                   86138
.BAD     MNOTE 8,'INDEX LEVEL MISSING OR TOO LONG'               86138
         MEND                                                    86138
         SPACE 2                                                 79095
         PUNCH '  ORDER COPYFDR(P) '   EASIER DUMPS             GP06354
         PUNCH '  SETCODE AC(1)  '     MAKE IT WORK             GP06354
         SPACE 2                                                 79095
         COPY  OPTIONGB                                          81208
         SPACE 1                                                 81208
         SYSPARM LIST=YES                                        81208
         EJECT ,                                                 81208
         PRINT &PRTSOR                                           81208
COPYFDR  PGMHEAD BIGZERO,BASE=R10,PARM=R5,BNDRY=PAGE            GP13433
**********************************************************************
*                ASSEMBLY OPTIONS                                    *
SYNSIZE  EQU   4096          SIZE OF GETMAIN FOR SYSIN CARD TABLE79105
NUMCCWS  EQU   120           MAXIMUM CCWS ON ONE TRACK           85280
NUMBUFFS EQU   5             NUMBER OF (65K+) I/O BUFFERS        81047
NUMDSCB5 EQU   10            NUMBER OF FMT5 RECORDS IN STORAGE   81208
WYLBLOCK EQU   6356          MAX WYLBUR EDIT BLOCKSIZE (3380)    92344
WYLRECL  EQU   232           MAXIMUM WYLBUR EDIT LINE LENGTH     89020
         SPACE 1                                                 79105
**********************************************************************
         SPACE 1                                                 81047
*        NOTES ON THE ABOVE ASSEMBLY OPTIONS :                   81047
*                                                                81047
*        SYNSIZE - GETMAIN SIZE OF THE CONTROL CARD TABLE.       81047
*              DEFAULT IS 4K, WHICH IS MORE THAN ENOUGH FOR      81047
*              MOST APPLICATIONS.  MAY BE ZAPPED.                81047
*                                                                81047
*        NUMCCWS - THIS IS THE MAXIMUM NUMBER OF DATA RECORDS    81047
*              WHICH WILL OCCUR ON ANY ONE TRACK. FOR CURRENTLY  81047
*              SUPPORTED DEVICES, THIS IS THE NUMBER OF UNKEYED  81047
*              BLOCKS OF LENGTH 1 (!) WHICH FIT ON THE TRACK.    81047
*              MAXIMUM IS 103 FOR A 3350, AND 96 FOR A 3380      85280
*              NOT ZAPPABLE.                                     81047
*                                                                81047
*        NUMBUFFS - NUMBER OF FULL-TRACK I/O BUFFERS.  THIS VALUE
*              MUST BE AT LEAST TWO, AND SHOULD NOT BE SET TOO   81047
*              HIGH. IF YOU HAVE MANY LARGE DATASETS, AND NO     81047
*              OR EXTREMELY LOW PAGING, THE VALUE MAY BE UPPED.  81047
*              NOT ZAPPABLE.                                     81047
*                                                                81208
*        NUMDSCB5 - NUMBER OF FORMAT 5 DSCBS MAINTAINED IN STORAGE.
*              MINIMUM IS 1, MAXIMUM IS 157.                     81208
*                                                                81047
         SPACE 2                                                 82272
*        REGISTER CONVENTION :                                   81047
*        R13 - LOCAL OR GLOBAL SAVE AREA                         81047
*        R12 - GLOBAL SAVE/WORK AREA                             81208
*        R11 - BASE REGISTER FOR 'WORKAREA'                      81208
*        R10 - BASE REGISTER FOR LOCAL CODE                      81047
*        R9 - RETURN REGISTER FOR SMALL SUBROUTINE CALLS         81047
*        R4 - WHERE APPLICABLE, ADDRESS OF CURRENT DSLIST ENTRY  81047
*        R14-R8 - LOCAL USE                                      81047
         TITLE 'C O P Y V O L  ***  PARM FIELD AND SYSIN CHECKING'
         L     R11,=A(WORKAREA)   GET WORK AREA                  81208
         USING WORKAREA,R11  DECLARE WORK AREA                   81208
         LR    R12,R13       COPY DYNAMIC WORK AREA ADDRESS      81208
         DROP  R13                                               81208
         USING SAVE,R12                                          81208
         MVI   VTOCLTTR+3,8  SET FOR OUTPUT EXTENT               82298
         MVI   DSLBXLE+3,DSLEN   INIT BXLE INCREMENT             81208
         LA    R0,NUMDSCB5*26   MAXIMUM FREE EXTENTS SUPPORTED   81208
         ST    R0,MAXFREE                                        81208
         LA    R0,NEWEXTS                                        81208
         ST    R0,AMSOUTXA                                       81208
         MVI   AMSOUTXA,1    INITIALIZE EXTENT POINTER           81208
         LA    R0,INJFCB                                         81208
         ST    R0,INJFCBAD                                       81208
         MVI   INJFCBAD,X'87'                                    81208
         LA    R0,OUTJFCB                                        81208
         ST    R0,OTJFCBAD                                       81208
         MVI   OTJFCBAD,X'87'                                    81208
         LA    R0,INJFCBAD                                       81208
         L     R1,=A(COMPIN)                                     81208
         STCM  R0,7,DCBEXLST+1-IHADCB(R1)                        81208
         LA    R0,OTJFCBAD                                       81208
         STCM  R0,7,DCBEXLST+1-IHADCB+COMPOUT-COMPIN(R1)         81208
         STCM  R0,7,INOUTDCB+DCBEXLST+1-IHADCB                   81208
         MVI   STOPCODE,X'7F'  MAKE ERROR STOP VERY LARGE        83294
         MVC   MASKORG,=X'FFFF'  COPY ALL DSORGS                 86292
         TIME  TU            GET THE TIME QUICKLY                86138
         SLR   R0,R0                                             86138
         STM   R0,R1,DB      SAVE THE PACKED DATE                86138
         CVB   R3,DB         GET BINARY EQUIVALENT               86138
         SLR   R2,R2                                             86138
         D     R2,=F'1000'   SEPARATE YEAR FROM DAYS             86138
         STCM  R2,3,CURDSDAT+1 SET DAYS                          86138
         STC   R3,CURDSDAT   SET YEAR IN DSCB FORMAT             86138
         SERVINIT ,          LOAD SERVICE ROUTINE                81208
         SERVLOAD @PRINTER,@INPREAD,@DCBEXIT,@FDRREAD  Subs 1/2 GP12294
         L     R14,=A(SASEXLST)                                  86292
         STCM  R0,7,1(R14)   BUILD SASIN EXIT LIST          2/2  86292
         SERVCALL LPALD,=CL8'@DCBVECT'  GET THE ENTRY VECTOR     86292
         LTR   R15,R0        ANY ?                               86292
         BZ    ENTNOBLK                                          86292
         L     R15,8(,R15)   GET BLOCKSIZE TABLE                 86292
         ST    R15,BLKSZTAB  SAVE IT                             86292
ENTNOBLK PRTOPEN SYSPRINT,OPT=(ABEND)   OPEN THE PRINT FILE      82298
         PRTV  SUBPARM,TITLE=3     PARM/OPTION SUBHEADER         81208
         PRTLIST HEADER,TITLE=1  PRINT PAGE HEADER               82298
.NOLOCAU B     NOLOCAUT                                         GP12295
HEADER   FDOPT NL,CC=C'#'    PAGE NUMBERING AND DATE/TIME        82298
         FDPRT 'C O P Y V O L  ***  DISK COMPRESSION PROGRAM'    81208
         FDPRT *END                                              81208
         SPACE 1                                                 81047
NOLOCAUT BALR  R10,0         MAKE NEW BASE                       93002
         USING *,R10                                             93002
         SLR   R2,R2         START TIOT SCAN FROM 0              91046
SCANTIOT SERVCALL TIOLP,(R2)   GET NEXT TIOT ENTRY               91046
         LTR   R2,R0         ANY MORE ?                          91046
         BZ    NODUPDDN      NO                                  91046
         USING TIOELNGH,R2   DECLARE IT                          91046
         CLI   TIOEDDNM,C' '   CONCATENATION ?                   91046
         BE    SCANTIOT      YES; SKIP                           91046
         LR    R3,R2         PRESET FOR SECOND ENTRY             91046
         CLC   =C'TA',TIOEDDNM    TAPE DD ?                     GP13433
         BNE   SCANTIO2             NO; SKIP                    GP13433
         MVC   VINDDNM(8),TIOEDDNM   SAVE FOR LATER             GP13433
         MVC   VINVSER,TIOEDDNM+2 SAVE FOR LATER                GP13433
         MVC   VINUCB+1(3),TIOEFSRT  SAVE UCB                   GP13433
SCANTIO2 SERVCALL TIOLP,(R3)   GET SUBSEQUENT ENTRY              91046
         LTR   R3,R0         IS THERE ONE ?                      91046
         BZ    SCANTIOT      NO; CLEAN                           91046
         CLC   TIOEDDNM,TIOEDDNM-TIOELNGH(R3)  DUPLICATE ?       91046
         BNE   SCANTIO2      NO; TRY ANOTHER                     91046
         PRTLIST MSGDUPDD    WRITE ERROR MESSAGE                 91046
         OI    OPTFLAG3,OPTINBAD  TERMINATE AFTER PARM SCAN      93002
         B     SCANTIOT      LOOK FOR ANOTHER                    91046
MSGDUPDD FD    '**** JCL ERROR - DUPLICATE DD',NL                91046
         FD    TIOEDDNM,DEB,PAD                                  91046
         FD    '****'                                            91046
         FD    *END                                              91046
         DROP  R2                                                91046
         SPACE 1                                                 91046
NODUPDDN LA    R5,0(,R5)     CLEAR HIGH BYTE                     81208
         LTR   R5,R5         ANY PARM ?                          81208
         BZ    PARMNONE      NO; SKIP PARM PROCESSING            81047
         ICM   R5,7,1(R5)    LOAD ADDRESS OF PARM FIELD          81208
         BZ    PARMNONE      NONE; SKIP PARM PROCESSING          81208
         LH    R4,0(,R5)     LOAD PARM LENGTH                    79105
         LTR   R3,R4         CHECK LENGTH                        81047
         BZ    PARMNONE                                          81047
         BM    PARMLOOT      ERROR                               81047
         LA    R14,L'TEXTPARM  SET LENGTH OF PRINT FIELD         81047
         MIN   R3,(R14)      OVER MAX ?  IF SO, TRUNCATE        GP12294
         BCTR  R3,0          EXECUTE LENGTH                      81047
         MVC   TEXTPARM,TEXTPARM-1  CLEAR IT                     81047
         EX    R3,PTEXTMOV   MOVE TEXT                           81047
         PRTV  NONEPARM      SHOW PARM FIELD                     81208
PARMLOOT LTR   R4,R4         ANY PARM LENGTH ?                   79105
         BM    PARMBAD       NO - BOOBOO                         79105
         BZ    PARMCHEK      CHECK FOR CONFLICTS                 79105
         CLI   2(R5),C' '    SEPARATOR ?                        GP12294
         BE    PARMBMP1        YES; SKIP OVER                   GP12294
         CLI   2(R5),C','    SEPARATOR ?                         79105
         BNE   PARM4                                             79105
PARMBMP1 LA    R5,1(,R5)     SKIP OVER IT                        79105
         BCT   R4,PARMLOOT                                       79105
         B     PARMCHEK      ALL DONE                            79105
PARM4    CH    R4,=H'4'      LONG ENOUGH FOR ENTRY ?             79105
         BL    PARMTXTB      NO; TOO BAD                         81047
         LA    R14,PARMFLAG  GET FLAG TABLE                      79105
         LA    R15,(PARMFEND-PARMFLAG)/(PARMFLG2-PARMFLAG)  NUMBER
PARM4TST CLC   2(4,R5),0(R14)  MATCH ?                           79105
         BE    PARMSET       YES                                 79105
         LA    R14,PARMFLG2-PARMFLAG(,R14)                       81208
         BCT   R15,PARM4TST                                      79105
         CLI   2(R5),C'<'    ACCESS DATE SELECTION ?             81208
         BE    PARMDTLO      YES                                 81208
         CLI   2(R5),C'>'    OLD ACCESS DATE TEST ?              81208
         BE    PARMDTHI      YES                                 81208
         CLC   =C'ORG=',2(R5)  EXPLICIT DSORG SELECTION ?        86292
         BE    PARMSORG      YES; CHECK HEX MASK                 86292
PARMBTXT LA    R4,4          SET FOR EX LENGTH                   81208
PARMTXTB BCTR  R4,0                                              81047
         MVC   NONEPARM+5(12),=C'INVALID TEXT'                   81047
         MVC   TEXTPARM,TEXTPARM-1                               81047
         EX    R4,PTEXTMOV   MOVE FRAGMENT                       81047
         B     PARMBAD                                           79105
PARMDTHI OI    OPTFLAG2,OPTDONEW  COPY RECENTLY ACCESSED DATA    81208
         B     PARMDTCM                                          81208
PARMDTLO OI    OPTFLAG2,OPTDOOLD  COPY NOT RECENTLY USED DATA    81208
PARMDTCM TM    OPTFLAG2,OPTDOOLD+OPTDONEW  BOTH ?                81208
         BO    PARMBTXT      YES; ERROR                          81208
         SLR   R14,R14                                           81208
         SLR   R15,R15                                           81208
         LA    R0,5          NEED EXACTLY 5 (YYDDD) DIGITS       81208
         SR    R4,R0         IS TEXT STRING LONG ENOUGH ?        81208
         BNP   PARMBTXT      NO; FAIL                            81208
PARMDTLP CLI   3(R5),C'0'    NUMERIC ?                           81208
         BL    PARMBTXT      NO; FAIL                            81208
         CLI   3(R5),C'9'                                        81208
         BH    PARMBTXT                                          81208
         SLDL  R14,8         SHIFT                               81208
         IC    R15,3(,R5)    INSERT NEXT DIGIT                   81208
         LA    R5,1(,R5)     BUMP                                81208
         BCT   R0,PARMDTLP                                       81208
         BCTR  R4,0          ADJUST RESIDUAL LENGTH              81208
         LA    R5,1(,R5)     EXTRA SKIP FOR < OR >               82129
         XC    DB2,DB2                                           81208
         STCM  R15,7,DB2+5   STASH DAYS                          81208
         PACK  DB,DB2                                            81208
         CVB   R0,DB                                             81208
         STH   R0,ACCDATE+1  SET DAYS                            81208
         SRDL  R14,24                                            81208
         ST    R15,DB2+4                                         81208
         PACK  DB,DB2                                            81208
         CVB   R0,DB                                             81208
         STC   R0,ACCDATE    STASH YEAR                          81208
         B     PARMLOOT      SEE IF ANY MORE TO DO               81208
         SPACE 1                                                 86292
*        ORG=XXXX  -  HEX DSORG PARAMETER                        86292
*                                                                86292
PARMSORG MVC   DB(4),6(R5)   MOVE THE SUPPOSED HEX STRING        86292
         LA    R14,DB                                            86292
         LA    R15,4                                             86292
PARMORGL CLI   0(R14),C'A'                                       86292
         BL    PARMTXTB      BAD TEXT                            86292
         CLI   0(R14),C'F'   VALID ?                             86292
         BNH   PARMORGB      YES                                 86292
         CLI   0(R14),C'0'                                       86292
         BL    PARMTXTB                                          86292
         CLI   0(R14),C'9'                                       86292
         BH    PARMTXTB                                          86292
PARMORGB LA    R14,1(,R14)                                       86292
         BCT   R15,PARMORGL  TRY NEXT                            86292
         NC    DB(4),PARMHEX+8  MASK ALL BUT LOW 5 BITS          86292
         TR    DB(4),PARMHEX  CONVERT TO UNPACKED HEX            86292
         PACK  DB+5(3),DB(5) PACK                                86292
         MVC   MASKORG,DB+5  COPY NEW DSORG                      86292
         LA    R5,8(R5,0)    SKIP OVER PARM                      86292
         SH    R4,*-2                                            86292
         B     PARMLOOT      TRY FOR MORE                        86292
PARMHEX  DC    X'000A0B0C0D0E0F001F1F1F1F1F1F1F1F00010203040506070809'
         SPACE 1                                                 86292
PARMSET  MVC   *+6+2(2),5(R14)  MOVE S-FORM ADDRESS              81208
         LA    R15,*-*       GET FLAG ADDRESS                    81208
         OC    0(1,R15),4(R14)  TURN FLAG ON                     79105
         LA    R5,4(R5,0)    SKIP VALID PARM                     79105
         SH    R4,*-2        GET REMAINING LENGTH                79105
         B     PARMLOOT      CHECK IT                            79105
         SPACE 1                                                 79105
PARMBAD  PRTV  PARMWTOT      TELL ABOUT BAD PARM                 81208
         B     SQUITX        AND EXIT                            79105
         SPACE 2                                                 *AMS*
PARMWTOT VCON  ' INVALID PARM FIELD - EXECUTION TERMINATED'      81208
PARMEXCM VCON  ' CONV AND COMP(CHGD) ARE MUTUALLY EXCLUSIVE'     81208
PARMCMIG VCON  ' COMPRESS (CHGD) REQUEST IGNORED'                81208
PARMFMIG VCON  '0MIGRATE SPECIFIED WITH INVALID PARM OR DD'      85280
SUBPARM  VCON  '-  PARM OPTIONS AND SYSIN REQUESTS'              81278
MISSCOMP VCON  ' COMP (IEBCOPY) REQUESTED - COMPIN/COMPOUT DD MISSING'
NONEPARM VCON  ' PARM OPTIONS :  ',END=NONEPARX                  81208
TEXTPARM DC    CL100'NONE - DEFAULTS WILL BE USED'               81047
         VCON  ,                                                 81208
         SPACE 1                                                 79105
PARMFLAG DC    0A(0),CL4'COPY',AL1(0),SL2(OPTFLAG)  DEFAULT - COPY ONLY
PARMFLG2 DC    CL4'CHGD',AL1(DSCHA),SL2(IIHOPT) COMP IF DSCHA ON 79325
         DC    CL4'VTOC',AL1(OPTVTOC),SL2(OPTFLAG3) VTOC EXTENT DUPL.
         DC    CL4'LIST',AL1(0),SL2(OPTFLAG)  DEFAULT - LIST EVERY MSG
         DC    CL4'TERS',AL1(OPTERSE),SL2(OPTFLAG) INDICATIVE MSG ONLY
         DC    CL4'MSGS',AL1(OPTERSE),SL2(OPTFLAG) INDICATIVE MSG ONLY
         DC    CL4'LOTS',AL1(OPTLOTS),SL2(OPTFLAG)  LOTS OF MSGS 81047
         DC    CL4'CHEK',AL1(OPTCHEK),SL2(OPTFLAG)  INST. CHECKS 86138
         DC    CL4'ASIS',AL1(DSLUNM),SL2(OIALLDS) ALL UNMOVABLE  83094
         DC    CL4'SYSU',AL1(DSLUNM),SL2(OISYS1) SYS1 UNMOVABLE  83094
         DC    CL4'SYSP',AL1(DSLPTY),SL2(OISYS1) SYS1 PRTY       83094
         DC    CL4'RLSE',AL1(0),SL2(OPTFLAG)  RELEASE UNUSED EXTENTS
         DC    CL4'TREL',AL1(OPTPSREL),SL2(OPTFLAG2) UNUSED TRACKS
         DC    CL4'FREE',AL1(OPTPSREL),SL2(OPTFLAG2) UNUSED TRACKS
         DC    CL4'PORL',AL1(OPTPOREL),SL2(OPTFLAG2) FREE PDS    85289
         DC    CL4'PREL',AL1(OPTPOREL),SL2(OPTFLAG2) FREE PDS    85289
         DC    CL4'KEEP',AL1(OPTKEEP),SL2(OPTFLAG)  KEEP UNUSED EXTNTS
         DC    CL4'IPLT',AL1(OPTIPL),SL2(OPTFLAG3)  COPY IPL TRK 79105
         DC    CL4'TRK0',AL1(OPTIPL),SL2(OPTFLAG3)   DITTO       79105
         DC    CL4'EVEN',AL1(OPTEVEN),SL2(OPTFLAG2) ERROR SKIP   83094
         DC    CL4'STAK',AL1(OPTSTACK),SL2(OPTFLAG)  VTOC MEM    87135
         DC    CL4'AUTO',AL1(OPTNWTOR),SL2(OPTFLAG)  SKIP WTOR  GP09239
         DC    CL4'DUMP',AL1(OPTDDUMP),SL2(OPTFLAG2) DUMP ON ERROR
         DC    CL4'DBUG',AL1(OPTDEBUG),SL2(OPTFLAG2) DEBUG MODE  81047
PARMFEND EQU   *             END OF PARMS                        79105
         SPACE 2                                                 *AMS*
PARMCHEK TM    OPTFLAG2,OPTDDUMP+OPTDEBUG  DUMP/DEBUG OPTIONS ?  81047
         BNZ   *+8           EITHER                              81047
         MVI   DUMPCODE,0    CLEAR DUMP REQUEST ON ABEND         81047
         TM    OPTFLAG,OPTKEEP  IS EXTENT KEEP ON ?              79327
         BZ    *+8           NO                                  79327
         NI    OPTFLAG2,255-OPTPSREL-OPTPOREL  KILL RELEASE      85289
         B     TESTSYDD                                          81047
         SPACE 1                                                 *AMS*
PARMNONE PRTV  NONEPARM      SHOW DEFAULTS IN USE                81208
TESTSYDD OI    OPTFLAG,OPTSTACK   FAILS WITHOUT ????            GP13433
         INPOPEN SYSIN,OPT=(DUMMY,NOWTO)  TEST FOR SYSIN         81208
         CH    R15,=H'4'     DID IT OPEN ?                       86292
         BH    SQUIT         NO; IGNORE                          86292
         PRTSPACE 1          SPACER                              81208
         L     R9,=A(SYNSIZE)  GET USER REQUESTED SPACE          79095
         GETMAIN R,LV=(R9)                                       79095
         TM    OPTFLAG2,OPTDEBUG+OPTDDUMP  DEBUG MODE ?          81047
         BZ    TESTSYBG      NO                                  81047
         CLRL  (R1),(R9),WORK1=14,WORK2=4  CLEAR GOTTEN STORAGE  83094
TESTSYBG ST    R1,DSNAMES    SAVE DSN LIST                       81047
         XC    0(8,R1),0(R1)  CLEAR START/CHAIN BYTE             81047
         LR    R8,R1         SET NEXT ENTRY ADDRESS              79095
         SH    R9,=Y(RIXFIX)  ALLOW FOR TERMINATION ENTRY        83094
         USING RIXLIST,R8                                        79105
         SPACE 1                                                 79095
SYNLOOP  INPGET ,            PICK A CARD, ANY CARD               81208
         BXH   R15,R15,SYNEOF   QUIT IF END OR ERROR             81208
         MVC   CARDLINE,0(R1) COPY CARD                          79095
         LA    R6,80         SET LENGTH OF CARD                  79095
         CLI   72(R1),C' '   BLANK SEQUENCE FIELD ?              81047
         BE    SYNSHORT      HOPE SO                             81047
         CLI   72(R1),C'0'   NUMERIC FIRST DIGIT ?               81047
         BL    SYNLONG       NO                                  81047
         MVC   DB,=8C'0'                                         81047
         NC    DB,72(R1)                                         81047
         CLC   DB,=8C'0'     ALL NUMERIC SEQUENCE FIELD ?        81047
         BE    SYNSHORT      YES; SKIP IT                        81047
         CLI   76(R1),C'.'   WYLBUR SEQUENCE ?                   81047
         BNE   SYNLONG       NO                                  81047
SYNSHORT MVI   72(R1),C' '   SKIP SEQUENCE FIELD                 81047
         MVC   73(7,R1),72(R1)  BLANK IT OUT                     81047
SYNLONG  LR    R2,R1         POINT TO FIRST BYTE                 81047
         SLR   R3,R3         SET CURRENT STRING LENGTH           79095
         SLR   R5,R5         SET LENGTH OF SECOND STRING         79095
SYNOPT   CLI   0(R2),C' '     LEADING BLANK ?                    79105
         BNE   SYNOLOOK                                          79105
         LA    R2,1(,R2)                                         79105
         BCT   R6,SYNOPT                                         79105
         B     CARDPUT       COPY BLANK CARD                     79105
SYNOLOOK CLI   0(R2),C'*'    COMMENT ?                           79105
         BE    CARDPUT       YES; PRINT IT                       79105
         CLC   =C'VTOC=',0(R2)  VTOC ADDRESS/SIZE OPTION ?       82298
         BE    SYNVTOC       YES; PROCESS SEPARATELY             82298
         CLC   =C'SIZE=',0(R2)  DISK SIZE/ALTERNATE OPTION      GP09297
         BE    SYNCYLS       YES; PROCESS SEPARATELY            GP09297
         CLC   =C'CYLS=',0(R2)  DISK SIZE/ALTERNATE OPTION      GP09297
         BE    SYNCYLS       YES; PROCESS SEPARATELY            GP09297
         CLC   =C'CODE=',0(R2)  STOP CODE OPTION ?               83294
         BE    SYNCODE       YES; CHECK VALUE                    83294
         CLC   =C'COND=',0(R2)  STOP CODE ?                      83294
         BE    SYNCODE                                           83294
         CLC   =C'EXIT=',0(R2)  USER RENAME/SELECT EXIT ?        93002
         BE    SYNUEX        YES; PROCESS                        93002
SYNOTAB  LA    R14,OPTTAB    GET OPTION ABBREVIATIONS            79105
         LA    R15,(OPTTABE-OPTTAB)/2  AND ENTRIES               79105
SYNOOPT  CLC   0(1,R2),0(R14)  MATCHING OPTION ?                 79105
         BE    SYNOMACH      YES                                 79105
         LA    R14,2(,R14)                                       79105
         BCT   R15,SYNOOPT                                       79105
         LA    R14,=AL1(RIXDODAT,RIXNODAT,RIXORDER) SET OPTIONS  81208
         CLI   0(R2),C'A'    ACCESS DATE SELECTION ?             81208
         BE    SYNOFLG2      YES; SET FLAG 2                     81208
         LA    R14,1(,R14)                                       81208
         CLI   0(R2),C'B'    BYPASS ACCESS DATE CHECK ?          81208
         BE    SYNOFLG2      YES                                 81208
         LA    R14,1(,R14)                                       81208
         CLI   0(R2),C'O'    ORDER DSCB IN VTOC ?                81208
         BNE   SYNOOVOL      NO; CHECK FOR VOLUME SELECTION      81208
SYNOFLG2 OC    RIXF2,0(R14)  SET OPTION ON                       81208
         B     SYNOMAC2      REJOIN COMMON CODE                  81208
SYNOOVOL CLI   0(R2),C'1'    NUMERIC VOLUME SELECT ?             81208
         BL    SYNOOPTI      NO; ERROR                           81047
         CLI   0(R2),C'8'    IN VALID RANGE ?                    81047
         BH    SYNOOPTI      NO                                  81047
         LA    R14,X'100'    MAKE VOLUME MASK                    81047
         IC    R15,0(,R2)    GET NUMERIC VALUE                   81047
         N     R15,=XL4'F'   MASK NUMERIC                        81047
         SRL   R14,0(R15)    SHIFT VOLUME BIT                    81047
         STC   R14,DB                                            81047
         OC    RIXFV,DB      ADD VOLUME SELECT BIT               81047
         B     SYNOMAC2      TRY FOR ANOTHER                     81047
SYNOOPTI MVC   CARDMSG(1),0(R2)  SHOW BAD VALUE                  81047
         MVC   CARDMSG+2(14),=C'INVALID OPTION'                  81047
         B     CARDERRQ      PRINT AND QUIT                      81047
SYNOMACH OC    RIXFG,1(R14)  COPY OPTION BIT                     79105
SYNOMAC2 LA    R2,1(,R2)                                         81047
         BCT   R6,*+8                                            79105
         B     CARDMIS1      SHOW FIRST NAME FIELD MISSING       81047
         CLI   0(R2),C' '    END OF OPTIONS ?                    79105
         BNE   SYNOTAB                                           79105
         CLI   RIXFV,0       ANY VOLUME SELECT OPTIONS ?         81047
         BNE   *+8           YES                                 81047
         MVI   RIXFV,X'FF'   SET FOR ALL VOLUMES                 81047
         MVC   CARDMSG(3),=C'K U'   PRESET OPTIONS               81047
         TM    RIXFG,RIXDSU+RIXKEEP                              79327
         BO    CARDEXCL      SHOW EXCLUSIVE                      81047
         MVC   CARDMSG(3),=C'F U'                                82134
         TM    RIXFG,RIXDSU+RIXPSREL                             79327
         BO    CARDEXCL                                          81047
         MVC   CARDMSG(3),=C'F K'                                82134
         TM    RIXFG,RIXKEEP+RIXPSREL                            79327
         BO    CARDEXCL                                          81047
         MVC   CARDMSG(3),=C'N U'                                81047
         TM    RIXFG,RIXDSU+RIXDNO  EXCLUSIVE OPTIONS ?          79105
         BO    CARDEXCL      TOO BAD                             81047
         MVC   CARDMSG(3),=C'N P'                                81047
         TM    RIXFG,RIXDSP+RIXDNO  EXCLUSIVE OPTIONS ?          79105
         BO    CARDEXCL      TOO BAD                             81047
         MVC   CARDMSG(3),=C'I X'                                81047
         TM    RIXFG,RIXINC+RIXEXC  EXCLUSIVE OPTIONS ?          79105
         BO    CARDEXCL      TOO BAD                             81047
         MVC   CARDMSG(3),=C'A B'                                81208
         TM    RIXF2,RIXDODAT+RIXNODAT                           81208
         BZ    SYNONEXC      NEITHER; OK                         81208
         BO    CARDEXCL      BOTH; ERROR                         81208
         TM    RIXFG,RIXINC+RIXEXC  EITHER ?                     81208
         BNZ   SYNONEXC      OK                                  81208
         MVC   CARDMSG(23),=C'INCLUDE/EXCLUDE MISSING'           81208
         B     CARDERRQ                                          81208
SYNONEXC MVC   CARDMSG(3),CARDMSG-1  CLEAR AGAIN                 81208
         TM    RIXFG,RIXINC+RIXEXC  SELECTION BITS TO BE SET ?   81242
         BZ    SYN1LOOP      NO; CHECK INPUT STRING              81242
         LA    R14,OPTINEX   GET VOLUME SELECT MASKS             81047
         LA    R15,8         NUMBER OF VOLUMES                   81047
         SLR   R1,R1                                             81047
         IC    R1,RIXFV      GET VOLUME SELECT BITS              81047
         SLL   R1,23         ALIGN FOR BXH                       81047
         OR    R1,R15        MAKE LOW ORDER NON-ZERO BIT         81047
SYNOEXLP BXH   R1,R1,SYNOEXBP  BUMP IF NOT SELECTED              81047
         TM    RIXFG,RIXINC   INCLUDE OPTION ?                   79105
         BZ    SYNOEXC       NO                                  79105
         TM    0(R14),OPTEXC+OPTINC  FIRST TIME ?                81047
         BNZ   *+8           NO                                  79105
         OI    0(R14),OPTINCL  SHOW INCLUDE FIRST                81047
         OI    0(R14),OPTINC  SHOW AT LEAST ONE INCLUDE          81047
         B     SYNOEXBP                                          81047
SYNOEXC  OI    0(R14),OPTEXC  SHOW EXCLUDE PRESENT               81047
SYNOEXBP LA    R14,1(,R14)   NEXT MASK BYTE                      81047
         BCT   R15,SYNOEXLP  TRY AGAIN                           81047
SYN1LOOP CLI   0(R2),C' '    LEADING BLANK ?                     79095
         BNE   SYN1TEXT      NO                                  79095
         LA    R2,1(,R2)                                         79095
         BCT   R6,SYN1LOOP                                       79095
CARDMIS1 MVC   CARDMSG(25),=C'DSNAME/MASK FIELD MISSING'         81047
         B     CARDERRQ      PRINT AND QUIT                      81047
SYN1TEXT LR    R4,R2         SAVE START OF FIRST TEXT STRING     79095
         CLI   0(R4),C'0'    VALID DSN START ?                   83094
         BNL   CARDERR       NO; FAIL                            83094
SYN1TEXX CLI   0(R4),C' '    END OF STRING ?                     83094
         BE    SYN2LOOK      YES; SCAN FOR NEXT                  79095
         LA    R3,1(,R3)     UP TEXT LENGTH                      79095
         LA    R4,1(,R4)     UP TEXT LOCATION                    79095
         BCT   R6,SYN1TEXX   TRY AGAIN                           83094
         B     CARDERR                                           79095
SYN2LOOK TM    RIXFG,RIXREN  TWO STRING OPTION ?                 79105
         BZ    SYN3TEST      NO; SKIP SECOND ONE                 79105
SYN2LOOP CLI   0(R4),C' '    START OF SECOND STRING              79095
         BNE   SYN2TEXT                                          79095
         LA    R4,1(,R4)                                         79095
         BCT   R6,SYN2LOOP                                       79095
         MVC   CARDMSG(16),=C'NEW NAME MISSING'                  83094
         B     CARDERRQ                                          83094
SYN2TEXT LR    R15,R4        SAVE SECOND STRING ADDRESS          79095
         CLI   0(R4),C'0'    VALID DSN START ?                   83094
         BNL   CARDERR       NO; FAIL                            83094
SYN2TEXX CLI   0(R15),C' '    END OF SECOND STRING ?             83094
         BE    SYN3TEST      YES; CHECK EVERYTHING               79095
         LA    R15,1(,R15)                                       79095
         LA    R5,1(,R5)     UP STRING LENGTH                    79095
         BCT   R6,SYN2TEXX   TRY AGAIN                           83094
SYN3TEST CH    R3,=H'44'      SHORT ENOUGH ?                     79095
         BH    CARDERR                                           79095
         CH    R5,=H'44'                                         79095
         BH    CARDERR                                           79095
         LA    R15,RIXFIX+1(R3,R5)  LENGTH REQUIRED IN TABLE     79105
         CR    R15,R9        WILL IT FIT ?                       79095
         BH    CARDFULL      NO; QUIT WITH ERROR                 79095
         STC   R3,RIXL1      SET LENGTH OF FIRST STRING          79095
         LA    R15,RIXMASK    SET LOCATION OF FIRST STRING       79105
         EX    R3,MVCSTR1    MOVE FIRST STRING                   79095
         STC   R5,RIXL2      SET LENGTH OF SECOND STRING         79095
         LR    R14,R3        GET LENGTH OF FIRST STRING          79105
         SLR   R0,R0         INIT COUNT OF REPLACABLE MATCH BYTES
SYN1REPL CLI   0(R15),C'?'   GENERAL MATCH BYTE ?                79105
         BE    SYN1REPM      MASK BYTE                           81047
         CLI   0(R15),X'7F'  ALTERNATE MASK BYTE ?               81047
         BNE   SYN1REPN      NO                                  81047
         AH    R0,=H'1'      UP COUNT OF COPYABLE MATCH BYTES    81047
SYN1REPM OI    RIXF2,RIXCMSK  SET COMPARE MASK PRESENT           81047
         B     SYN1REPX      SKIP TO NEXT BYTE                   81208
SYN1REPN CLI   0(R15),C';'   PRECISE STRING ?                    81208
         BE    SYN1REPB      YES; REPLACE BY BLANK               81208
         CLI   0(R15),C'!'   EXCLAMATION ?                       81208
         BNE   SYN1REPX      NO; BUMP                            81208
SYN1REPB MVI   0(R15),C' '   FORCE EXACT MATCH                   81208
SYN1REPX LA    R15,1(,R15)                                       81208
         BCT   R14,SYN1REPL  DO ALL                              79105
         TM    RIXFG,RIXREN  TWO STRING OPTION ?                 79105
         BZ    SYNTINC       NO; SKIP SECOND STRING              79105
         LR    R2,R4                                             79095
         LA    R15,RIXMASK(R3)  POINT TO SECOND STRING           79105
         EX    R5,MVCSTR1    MOVE SECOND STRING                  79095
         LA    R14,1(,R5)    GET NUMBER OF BYTES TO CHECK        81047
SYN3REPM CLI   0(R15),C'?'   BAD REPLACE PARM ?                  81047
         BE    CARDERR       YES; FAIL                           81047
         CLI   0(R15),X'7F'  REPLACEMENT MASK ?                  81047
         BNE   SYN3REPX      NO                                  81047
         BCTR  R0,0          SUBTRACT ONE                        81047
         OI    RIXF2,RIXRMSK  SET REPLACEMENT MASK PRESENT       81047
SYN3REPX LA    R15,1(,R15)   NEXT BYTE                           81047
         BCT   R14,SYN3REPM  REPEAT                              81047
         LTR   R0,R0         EXCESSIVE REPLACES ?                81047
         BNM   SYNTINC       NO                                  81047
         MVC   CARDMSG(25),=C'REPLACE STRING - EXCESS "'         81047
         B     CARDERRQ      FAIL                                81047
SYNTINC  OC    DSNAMES(1),RIXFG  COLLECT GLOBAL FLAGS            79105
         LA    R8,RIXFIX(R8,R5) POINT TO NEXT ENTRY              79095
         AR    R8,R3                                             79105
         XC    RIXFG(RIXFIX),RIXFG SET END OF CHAIN              83094
         LA    R15,RIXFIX(R3,R5)  SET ENTRY LENGTH               79095
         SR    R9,R15         INTO SPACE REMAINING               79095
CARDPUT  PRTV  LINECARD      PRINT THE LINE                      81208
         MVC   CARDMSG,CARDMSG-1  CLEAR MESSAGE AREA             79095
         B     SYNLOOP       GET ANOTHER CARD                    79095
         SPACE 2                                                 82298
*        VTOC RELOCATION OPTION:                                 82298
*        VTOC=ABSTRK,EXTENT=TRACKS                               82298
*        VTOC=ABSTRK,CYL=CYL#                                    86293
*                                                                82298
SYNVTOC  LA    R2,5(,R2)     SKIP OVER VTOC=                     82298
         SH    R6,=H'5'      GET RESIDUAL LENGTH                 82298
         BNP   CARDERR       INVALID TEXT                        82298
         BAL   R7,SYNNUM     GET A NUMERIC PARAMETER             83094
         LTR   R0,R0         VALID ?                             82298
         BNP   CARDERRV      NO; FAIL                            82298
         STCM  R0,15,COPYEXTS+2  SAVE START TRACK                82298
         BAL   R14,SYNNBLK   SKIP BLANKS                         82298
         CLC   =C'CYL=',0(R2)  VTOC ALLOC. BY CYLINDER ?         86292
         BNE   SYNVTOX       NO; CHECK FOR TRACKS                86292
         LA    R2,4(R2,0)    BUMP OVER IT                        86292
         SH    R6,*-2        ADJUST RESIDUAL COUNT               86292
         BNP   CARDERR       HUH ?                               86292
         LA    R3,16         SET SHIFT VALUE                     86292
         B     SYNVTON       GO TO COMMON STORE                  86292
SYNVTOX  CLC   =C'EXTENT=',0(R2)  EXTENT SIZE ?                  82298
         BNE   CARDERR       NO; INVALID TEXT                    82298
         LA    R2,7(,R2)                                         82298
         SH    R6,=H'7'      SKIP OVER IT                        82298
         BNP   CARDERR                                           82298
         SLR   R3,R3         SET SHIFT FACTOR                    86292
SYNVTON  BAL   R7,SYNNUM     GET NUMERIC VALUE                   83094
         LTR   R0,R0         VALID ?                             82298
         BNP   CARDERRV      NO                                  82298
         CLI   0(R2),X'FF'   ERROR SIGNALLED ?                   83094
         BE    CARDERRV      YES; FAIL                           83094
         SLL   R0,0(R3)      SHIFT (FOR CYL OPTION)              86292
         STCM  R0,15,COPYEXTS+6  SAVE TRACKS                     82298
         OI    OPTFLAG3,OPTVTOC  SET VTOC RELOCATION REQUEST     82298
         MVI   COPYEXTS,2    SET VTOC CARD FLAG                  82298
         B     CARDPUT       PRINT THE CARD                      82298
         SPACE 2                                                 82298
*   DISK (RE)SIZE OPTION:                                       GP09297
*        SIZE=cylinders                                         GP09297
*        SIZE=(cylinders,alternates)                            GP09297
*                                                               GP09297
SYNCYLS  LA    R2,5(,R2)     SKIP OVER SIZE=                    GP09297
         SH    R6,=H'5'      GET RESIDUAL LENGTH                GP09297
         BNP   CARDERR       INVALID TEXT                       GP09297
         CLI   0(R2),C'('    (CYL,ALT)  FORMAT ?                GP09297
         BE    SYNCYLA       YES                                GP09297
         BAL   R7,SYNNUM     GET A NUMERIC PARAMETER            GP09297
         LTR   R0,R0         VALID ?                            GP09297
         BNP   CARDERRV      NO; FAIL                           GP09297
         ST    R0,OUTSIZE    SAVE OVERRIDING VALUE              GP09297
         B     CARDPUT       PRINT THE CARD                     GP09297
         SPACE 1                                                GP09297
SYNCYLA  LA    R2,1(,R2)     SKIP PARENTHESIS                   GP09297
         SH    R6,=H'1'      RESIDUAL LENGTH                    GP09297
         BNP   CARDERR       HUH ?                              GP09297
         BAL   R7,SYNNUM     GET A NUMERIC PARAMETER            GP09297
         LTR   R0,R0         VALID ?                            GP09297
         BNP   CARDERRV      NO; FAIL                           GP09297
         ST    R0,OUTSIZE    SAVE OVERRIDING VALUE              GP09297
         BAL   R7,SYNNUM     GET A NUMERIC PARAMETER            GP09297
         LTR   R0,R0         VALID ?                            GP09297
         BNP   CARDERRV      NO; FAIL                           GP09297
         ST    R0,OUTSALT    SAVE OVERRIDING VALUE              GP09297
         B     CARDPUT       PRINT THE CARD                     GP09297
         SPACE 2                                                 93002
*   EXIT=LOADNAME    USER DSN SELECT/RENAME EXIT                 93002
*                                                                93002
SYNUEX   LA    R2,5(,R2)     SKIP VERB                           93002
         CLI   0(R2),C' '    NULL FIELD ?                        93002
         BE    CARDPUT       YES; PRINT AND IGNORE CARD          93002
         LA    R14,UEPRENAM  GET OUTPUT FIELD                    93002
         CLI   0(R14),C' '   BEEN HERE BEFORE ?                  93002
         BH    CARDERR       YES; FAIL DUPLICATE REQUEST         93002
         LA    R0,8                                              93002
SYNUEXL  CLI   0(R2),C' '    END OF TEXT ?                       93002
         BE    SYNUEXM       YES                                 93002
         MVC   0(1,R14),0(R2)  MOVE ONE                          93002
         LA    R14,1(,R14)                                       93002
         LA    R2,1(,R2)                                         93002
         BCT   R0,SYNUEXL                                        93002
         CLI   0(R2),C' '    NOT LONGER THAN 8 ?                 93002
         BNE   CARDERR       TOO BAD                             93002
SYNUEXM  LOAD  EPLOC=UEPRENAM,ERRET=SYNUEXN                      93002
         ST    R0,UEXRENAM   SAVE ADDRESS                        93002
         B     CARDPUT       PRINT AND CONTINUE                  93002
SYNUEXN  MVC   CARDMSG(24),=C'EXIT COULD NOT BE LOADED'          93002
         B     CARDERRQ                                          93002
         SPACE 2                                                 83294
*        COND= / CODE= TASK TERMINATION OPTION                   83294
*             VALUE SPECIFIES LOWEST ERROR CODE CAUSING TERMINATION.
*                                                                83294
SYNCODE  LA    R2,5(,R2)     SKIP VERB                           83294
         SH    R6,=H'5'      RESIDUAL LENGTH                     83294
         BNP   CARDERR       INVALID TEXT                        83294
         BAL   R14,SYNNBLK   LOOK FOR A NON-BLANK CHARACTER      83294
         CLI   0(R2),C'0'    NUMERIC ?                           83294
         BL    CARDERRV      SHOW INVALID VALUE                  83294
         BAL   R7,SYNNUM     CONVERT TO BINARY                   83294
         CLI   0(R2),X'FF'   ERROR SIGNALLED ?                   83294
         BE    CARDERRV      YES; FAIL                           83294
         ST    R0,STOPCODE   SET STOP CODE                       83294
         B     CARDPUT       ECHO THE CARD                       83294
         SPACE 1                                                 82298
SYNNBLK  CLI   0(R2),C' '    LEADING BLANK ?                     82298
         BNER  R14           NO; RETURN                          82298
         LA    R2,1(,R2)     NEXT                                82298
         BCT   R6,SYNNBLK                                        82298
         B     CARDERR       MISSING TEXT                        82298
         SPACE 1                                                 82298
SYNNUM   BAL   R14,SYNNBLK   FIND FIRST NON-BLANK                82298
         SLR   R14,R14                                           82298
         LA    R15,C'0'      INITIALIZE ACCUMULATORS             82298
         LA    R0,9          ACCEPT UP TO EIGHT DIGITS           83094
SYNNUMD  CLI   0(R2),C' '    TRAILING BLANK ?                    82298
         BE    SYNNUME       YES                                 82298
         CLI   0(R2),C','    OR TRAILING COMMA ?                 82298
         BE    SYNNUME       YES; QUIT                           82298
         CLI   0(R2),C')'    OR TRAILING COMMA ?                GP09297
         BE    SYNNUME       YES; QUIT                          GP09297
         CLI   0(R2),C'0'    NUMERIC ?                           82298
         BL    CARDERRV      NO; INVALID                         82298
         CLI   0(R2),C'9'                                        82298
         BH    CARDERRV                                          82298
         SLDL  R14,8         SHIFT                               82298
         IC    R15,0(,R2)    INSERT NEXT VALUE                   82298
         LA    R2,1(,R2)     POINT TO NEXT DIGIT                 82298
         BCT   R6,SYNNUMF    MORE TO DO                          82298
         B     SYNNUML       STOP WITH ERROR                     83094
SYNNUMF  BCT   R0,SYNNUMD    TRY FOR ANOTHER                     82298
CARDERRV MVC   CARDMSG(13),=CL13'INVALID VALUE'                  82298
         B     CARDERRQ      QUIT                                82298
SYNNUME  LA    R2,1(,R2)     SKIP SEPARATOR                      82298
         BCT   R6,SYNNUMG    MORE TO DO                          82298
SYNNUML  LA    R6,1                                              83094
         LA    R2,=X'FF'     FINAGLE IT                          82298
SYNNUMG  STM   R14,R15,DB2   STASH UNPACKED VALUE                82298
         PACK  DB,DB2        PACK IT                             82298
         CVB   R0,DB         MAKE BINARY                         82298
         BR    R7            RETURN TO CALLER                    83094
         SPACE 1                                                 79095
CARDEXCL MVC   CARDMSG+4(21),=C'ARE EXCLUSIVE OPTIONS'           81047
         B     CARDERRQ                                          79105
CARDERR  MVC   CARDMSG(12),=C'INVALID TEXT'                      79095
CARDERRQ OI    OPTFLAG3,OPTINBAD  QUIT AFTER INPUT               93002
         B     CARDPUT                                           79095
         SPACE 1                                                 79095
CARDFULL MVC   CARDMSG(14),=C'DSN TABLE FULL'                    79095
         PRTV  LINECARD                                          81208
         PRTV  SYNSYSMG                                          81208
         B     SQUITX                                            79095
         SPACE 1                                                 79095
SYNEOF   MVC   CARDMSG(LINECARN-CARDMSG),CARDMSG-1  CLEAR FOR WORK
         L     R1,DSNAMES    GET DSN TABLE                       79095
         CLI   0(R1),0       ANY ENTRIES ?                       79095
         BNE   SQUIT         YES; LEAVE IT                       79095
         L     R0,=A(SYNSIZE)  GET USER REQUESTED SPACE          79095
         FREEMAIN R,A=(1),LV=(0)   FREE THE TABLE SPACE          79095
         XC    DSNAMES,DSNAMES  AND CLEAR POINTER                79095
SQUIT    PRTSPACE 1          PRINT SPACER                        81208
         SERVCALL LPADL,=CL8'@INPREAD'   CLOSE AND FREE SYSIN    81208
SQUITB   TM    OPTFLAG3,OPTINBAD  INITIALIZATION FAILURE ?       93002
         BZ    INITDONE      NO; BEGIN REAL PROCESSING           93002
SQUITX   L     R15,=A(CONDDUMP)  GET CONDITIONAL DUMP ADDRESS    82298
         BALR  R2,R15        DUMP OR TERMINATE                   82298
         SPACE 1                                                 79095
MVCSTR1  MVC   0(0,R15),0(R2)                                    79095
PTEXTMOV MVC   TEXTPARM(0),2(R5)  MOVE TEXT                      81047
         DROP  R10,R8                                            79105
         SPACE 1                                                 81047
SYNSYSMG VCON  '0CONTACT THE SYSTEM GROUP FOR ASSISTANCE'        81208
         SPACE 1                                                 79105
OPTTAB   DC    C'X',AL1(RIXEXC)  EXCLUDE                         79105
         DC    C'E',AL1(RIXEXC)  EXCLUDE                         79105
         DC    C'I',AL1(RIXINC)  INCLUDE                         79105
         DC    C'S',AL1(RIXINC)  INCLUDE(SELECT)                 79105
         DC    C'R',AL1(RIXREN)  RENAME                          79105
         DC    C'K',AL1(RIXKEEP)  INHIBIT TRACK & EXTENT RELSE   79327
         DC    C'F',AL1(RIXPSREL)  FREE UNUSED PS TRACKS         79327
         DC    C'U',AL1(RIXDSU)  UNMOVABLE                       79105
         DC    C'P',AL1(RIXDSP)  PRIORITY COPY                   79105
         DC    C'N',AL1(RIXDNO)  RESET U, P OPTIONS              79105
OPTTABE  EQU   *                                                 79105
         SPACE 1                                                 81047
         LTORG ,                                                 81047
         TITLE 'C O P Y V O L  ***  INITIALIZATION AND SETUP'    81047
INITDONE BALR  R10,0         GET NEW LOCAL BASE                  81047
         USING *,R10         DECLARE IT                          81047
         LA    R5,OUTDDS     SET FOR POSSIBLE OUTPUT DD NAMES    81047
         LA    R6,3          LOOP UP TO THREE TIMES              81047
INOUTLP  MVC   VOUTDDNM(8),0(R5)  MOVE NEXT DD NAME              81047
         LA    R5,8(,R5)     PRESET FOR NEXT                     81047
         MVC   OPENDDNM,VOUTDDNM                                 81047
         MVC   OPENNDSK+5(8),OPENDDNM  SET SECOND MESSAGE ALSO   81047
         BAL   R9,TIOTFIND   LOOK FOR DD CARD                    81047
           B   DOJFCBRD      OK; READ JFCB ANYWAY                81047
         BCT   R6,INOUTLP    DD MISSING - TRY NEXT NAME OR FALL THROUGH
         LA    R1,OPENNDSK   SHOW UNUSABLE ENTRY                 81047
         B     DUMPMSG       SHOW ERROR                          81047
DOJFCBRD LA    R8,INOUTDCB   MAKE FOR SHORT STORES               81047
         USING IHADCB,R8     DECLARE MAPPING                     81208
         MVC   DCBDDNAM(8),VOUTDDNM  PROPAGATE DDNAME TO DCB     81047
         USING TCB,R4        SET BY TIOTFIND                     81208
         USING UCBOB,R3      RETURNED BY TIOTFIND                81208
         S     R1,TCBTIO     GET TIOT OFFSET                     81047
         AH    R1,=Y(TIOELNGH-TIOT1)  ALLOW FOR PREFIX           81208
         LR    R2,R1         SAVE OFFSET UNTIL AFTER RDJFCB      81047
         STCM  R3,7,VOUTUCB+1  SAVE UCB ADDRESS                  81047
         MVC   VOUTVSER,UCBVOLI  SAVE OUTPUT VOLSER              81047
         RDJFCB INOUTDCB       SET UP OUTPUT VTOC                81047
         BXH   R15,R15,OPENNOPE  TOO BAD                         81047
         STH   R2,DCBTIOT    SAVE TIOT OFFSET                    81047
         LA    R1,OPENNDSK   SET FOR NON-DISK MESSAGE            81047
         CLI   UCBTBYT3,UCB3DACC  MAKE SURE DIRECT ACCESS        81047
         BNE   DUMPMSG       IF NOT, THEN FORGET IT              81047
         MVC   MSGOPINF+15(8),VOUTDDNM  SET DDNAME IN MESSAGE    81047
         MVC   MSGOPINF+24(6),UCBVOLI   VOLSER                   81047
         MVC   MSGOPINF+31(3),UCBNAME   UNIT                     81047
         PRTV  MSGOPINF      PRINT IT                            81208
         MVC   MSGOPINF+4(4),=C'  IN'  CHANGE OUTPUT TO INPUT MSG
         LA    R1,OPENNSCR   SET FOR NOT SCRATCH MESSAGE         81047
         TM    UCBSTAT,UCBPRES  PERM. RESIDENT                   85049
         BZ    NSCRNRES      NO; CONTINUE                        85049
         LA    R14,=X'0B0C0E0F'  POINT TO FIXED TYPES            86292
         LA    R15,4         NUMBER                              86292
NSCRTRES CLC   UCBTBYT4,0(R14)  FIXED SPINDLE ?                  85049
         BE    NSCRNRES      YES; ALLOW                          85049
         LA    R14,1(,R14)                                       85049
         BCT   R15,NSCRTRES                                      85049
         B     DUMPMSG       NO; FAIL IT                         85049
NSCRNRES TM    UCBSTAT,UCBSYSR  SYSRES ?                         85049
         BNZ   DUMPMSG       YES; FAIL IT                        81047
         AIF   ('&LOCAL' NE 'PID').NOLOCSV                       92279
         TM    OPTFLAG2,OPTCLEAR   CLEARVOL ENTRY ?              82298
         BZ    NSCRAUTH      NO; CHECK AUTHORIZATION             90056
         CLC   =C'WORK',UCBVOLI  CCSI WORK PACK ?                82298
         BE    NSCRPACK      YES; NO AUTHORIZATION REQUIRED      82298
NSCRAUTH TM    AUTHFLAG,VAASYS+VAASUP  PRIVILEGED USER ?         90056
         BNZ   NSCRPRIV      YES; ALLOW ACCESS                   90056
         L     R2,CVTPTR     GET CVT                             90056
         ICM   R2,15,CVTUSER-CVTMAP(R2)  USER EXTENSION ?        90056
         BZ    DUMPMSG       NO; NO ACCESS                       90056
         ICM   R2,15,UCLVOLS-USERCVT(R2)  VOLUME TABLE ?         90056
         BZ    DUMPMSG       NO; NO ACCESS                       90056
         LM    R14,R15,UVOLBXLE+4-USERVOLS(R2) INCR/LAST ADDR    90056
         L     R2,UVOLBXLE-USERVOLS(,R2)  LOAD START ADDRESS     90056
         USING UVOLSER,R2    DECLARE COMMON ENTRY                90056
NSCRULUP CLC   UVOLSER,UCBVOLI  SAME VOLUME SERIAL ?             90056
         BE    NSCRUTST      YES; TEST ACCOUNTS                  90056
         BXLE  R2,R14,NSCRULUP  ELSE TRY AGAIN                   90056
         B     DUMPMSG       ELSE FAIL                           90056
NSCRUTST LA    R0,4          DO MAXIMUM ENTRIES                  90056
         LA    R15,UVOLACT1                                      90056
         USING UVOLACT1,R15                                      90056
NSCRUTAC TM    UVOLACF1,X'F0' UNRESTRICTED ACCESS ?              90056
         BNO   NSCRUTUP      NO                                  90056
         LA    R14,7         SET FOR FULL ACCOUNT/SUBACCOUNT     90056
         TM    UVOLACF1,X'08' ANY SUBACCOUNT ?                   90056
         BZ    *+8           NO; SPECIFIC                        90056
         LA    R14,3         IGNORE SUBACCOUNT                   90056
         EX    R14,NSCRACHK  CHECK ACCOUNTS                      90056
         BE    NSCRPRIV      YIPPEE                              90056
NSCRUTUP LA    R15,UVOLACT2-UVOLACT1(,R15)                       90056
         BCT   R0,NSCRUTAC   TRY AGAIN                           90056
         B     DUMPMSG                                           90056
NSCRACHK CLC   UVOLACT1(0),AUTHACCT                              90056
         DROP  R15                                               90056
.NOLOCSV SPACE 1                                                 82298
NSCRPRIV TM    UCBSTAB,X'4F'   PRIVATE PACK ?                    82298
         BNZ   DUMPMSG       NOT PRIVATE VOLUME; FAIL            81047
NSCRPACK MVI   OUTJFCB,4       SET UP VTOC AS DSNAME             82298
         MVC   OUTJFCB+1(43),OUTJFCB                             81047
         OI    OUTJFCB+(JFCBTSDM-JFCB),JFCNDSCB+JFCNDCB          81047
         AIF   (NOT &MVS).NMVSDEB                                82195
         MVI   GMDEBL,X'E6'  SET MVS DEB SUBPOOL                 81047
.NMVSDEB MODESET KEY=ZERO    GET PRIVILEGED                      81047
         L     R2,CVTPTR                                         81047
         USING CVTMAP,R2                                         81208
         L     R3,VOUTUCB                                       GP13433
         SR    R15,R15                                          GP13433
         IC    R15,UCBTBYT4  GET DASD SUBTYPE                   GP13433
         MVI   VOUTSIZE,0    ALLOW RDM (FAKE READFDR)           GP13433
         TM    OPTFLAG3,OPTSWAP   SWAP OPTION SELECTED ?         81047
         BZ    NSWAPOPN      NO                                  81047
         TM    OPTFLAG3,OPTFLIP   NON-SWAP EXCHANGE ?            87040
         BNZ   NSWAPOPN      YES; VALID FOR ALL SUPPORTED DASD   87040
         LA    R1,OPENNSWP                                       81047
         LA    R14,ZEROES    GET SWAP TABLE                     GP13433
         CLI   0(R14),0      SWAP PERMITTED ?                    81047
         BNE   DUMPMSG       NO; FAIL                            81047
NSWAPOPN L     R14,CVTZDTAB  GET CVT DEVICE POINTER              81047
         IC    R15,0(R15,R14)   GET INDEX TO DEVICE ENTRY        81047
         AR    R14,R15       GET ENTRY FOR THIS TYPE             81047
         ST    R14,DCBDVTBL-IHADCB+INOUTDCB  SET INTO DCB        81047
         MVC   VOUTTPC,2(R14)    SAVE NUMBER OF TRACKS PER CYLINDER
         MVC   VOUTSIZE+2(2),4(R14)  SAVE TRACK SIZE             87135
         LA    R14,9         SET MINIMUM OVERHEAD                87135
         AL    R14,VOUTSIZE  PLUS RAW TRACK SIZE                 87135
         ST    R14,VOUTSIZE  SAVE FOR READ MULTIPLE LENGTH       87135
         SLR   R14,R14           CLEAR 1                         81047
         LA    R15,15        MAKE AND MASK                       81047
         IC    R14,UCBTBYT4  GET LAST PART OF DEVICE TYPE        81047
         NR    R15,R14        MASK DEFINED BITS
         SLL   R15,1            MULT BY 2                        81047
         LH    R14,DACYLS(R15)   GET # OF CYLS                   81047
         AH    R14,ALTCYLS(R15)  DEVICE CAPACITY                 93166
         AIF   (NOT &MVSXA).NODCE1                               93162
         ICM   R1,15,UCBEXTPT  GET UCB EXTENSION                 93162
         ICM   R1,15,UCBCLEXT-UCBCMEXT(R1)  GET DCE              93162
         BZ    COMDVCT       HUH ?                               93162
         LH    R1,DCEUDCYL-DCE(,R1)  ANY COUNT ?                 93162
         LTR   R1,R1         ANY SIZE SET ?                      93162
         BZ    COMDVCT       NO; USE DVCT SIZE                   93162
         LR    R14,R1        USE DCE SIZE                        93162
.NODCE1  SPACE 1                                                 93162
COMDVCT  ICM   R0,15,OUTSIZE DID USER OVERRIDE SIZE?            GP09297
         BZ    *+6           NO                                 GP09297
         LR    R14,R0        USE OVERRIDING VALUE               GP09297
         STH   R14,VOUTACYL  SAVE TOTAL CAPACITY                 93166
         ICM   R0,15,OUTSALT OVERRIDE ALTERNATES ?              GP09297
         BZ    NWSYSALT      NO                                 GP09297
         SR    R14,R0        ADJUST                             GP09297
         B     *+8           DONE                               GP09297
NWSYSALT SH    R14,ALTCYLS(R15)  REMOVE ALTERNATE CYLINDER       93166
         LA    R1,OPENNSUP   SET FOR UNSUPPORTED DEVICE          81047
         LTR   R14,R14                                           81047
         BZ    DUMPMSG       QUIT WITH MESSAGE                   81047
         STH   R14,VOUTCYL   SAVE DATA CYLINDERS                 81047
         MH    R14,VOUTTPC      GET TOTAL # OF TRACKS            85280
         STH   R14,#TRKS        SAVE # OF TRACKS                 81047
         SPACE 1
*---------------------------------------------------------------------*
*   CODE INSERTED TO HANDLE DISKS WITH ARBITRARY NUMBER OF CYLINDERS, *
*   NOT NECESSARILY THE COUNT USED IN THE SYSTEM DEVICE TABLE (OR     *
*   IN DACYLS). NEEDED FOR 3390-02 AND LARGER VOLUMES, 3380S, ETC.    *
*---------------------------------------------------------------------*
         LA    R14,OUTJFCB                                      GP09297
         LA    R15,VOUTVSER                                     GP09297
         LA    R0,DS4IDFMT                                      GP09297
         STM   R14,R0,CAMDSCB4+4                                GP09297
         MVI   DS4IDFMT,0    CLEAR                              GP09297
         MVC   VOUTVSER,UCBVOLI  COPY THE VOLUME SERIAL         GP09297
         SLR   R0,R0         JUST IN CASE                       GP09297
         OBTAIN CAMDSCB4     GET THE VTOC THE HARD WAY          GP09297
         LA    R15,VINVSER                                      GP09297
         ST    R15,CAMDSCB4+8     UPDATE VOLSER LOCATION        GP09297
         ICM   R0,15,OUTSIZE    USER SIZE OVERRIDE?             GP09297
         BNZ   GETMYDEB      YES; KEEP IT                       GP09297
         CLI   DS4IDFMT,C'4'  WAS THE VTOC READ IN ?            GP09297
         BNE   GETMYDEB      NO; TOO BAD                        GP09297
         LH    R14,VOUTACYL  GET OLD SIZE                       GP09297
         SH    R14,VOUTCYL   GET # OF ALTERNATES                GP09297
         ICM   R15,3,DS4DEVSZ     GET DATA CYLINDERS BACK       GP09297
         STH   R15,VOUTACYL  SET TOTAL SIZE (W. ALT)            GP09297
         SR    R15,R14       GET DATA CYLINDERS                 GP09297
         STH   R15,VOUTCYL   SET UPDATED COUNT                  GP09297
         MH    R15,VOUTTPC      GET TOTAL # OF TRACKS           GP09297
         STH   R15,#TRKS        SAVE # OF TRACKS                GP09297
GETMYDEB L     R0,GMDEBL     GET DEB LENGTH                      81047
         GETMAIN R,LV=(0)                                        81047
         ST    R1,GMDEBA     SAVE ADDRESS OF GOTTEN STORAGE      81047
         LR    R6,R1         SAVE                                81047
         LH    R15,GMDEBL+2  LOAD LENGTH TO BE ZEROED            81047
         LR    R14,R6        SET START ADDRESS                   81047
         SLR   R0,R0                                             81047
         SLR   R1,R1                                             81047
         MVCL  R14,R0        CLEAR THE NEW DEB                   81047
         USING DEBAVT,R6     BUT MAP PREFIX                      81047
         L     R14,CVTXAPG   GET DUMMY I/O APPENDAGE ADDRESS(ES) 81047
         MVC   DEBEOEA(DEBXCEA-DEBEOEA+4),0(R14)   DUMMY APPENDAGES
         DROP  R2                                                81047
         ST    R6,DEBAPPAD   SAVE APPENDAGE ADDR.                81047
         LA    R1,DEBBASIC                                       81047
         ST    R1,GMDEBAD    SAVE ADDRESS OF BASIC               81047
         ST    R1,DCBDEBAD   SAVE DEB ADDRESS                    81047
         MVZ   DCBDEVT,UCBTBYT3    COPY DA FLAG                  81047
         MVN   DCBDEVT,UCBTBYT4   COPY DEV. TYPE                 81047
         MVI   DCBOFLGS,X'12'  SET DCB OPEN FOR INPUT            81047
         MVC   DCBMACRF(2),=X'D008'  MACRF=EXCP                  81047
         ST    R4,DEBTCBAD   ALSO SETS MODULES LOADED TO ZERO    81047
         ST    R8,DEBDCBAD   SET DCB ADDRESS INTO DEB            81047
         LH    R15,GMDEBL+2  GET DEB LENGTH                      81047
         LA    R15,7(,R15)   ROUND INSTEAD OF TRUNCATE           81047
         SRL   R15,3         NUMBER OF DOUBLE-WORDS              81047
         STC   R15,DEBLNGTH                                      81047
         MVI   DEBAMLNG,X'10'   ACCESS METHOD LENGTH ?           81047
         MVI   DEBOFLGS,DEBDSOLD+DEBEOF+DEBDCB+DEBRERR BPAM EXTENTS
         MVI   DEBOPATB,DEBACCS+DEBLEAVE   SET DEB OPEN FOR OUTPUT
         MVI   DEBNMEXT,10   SET FOR 10 EXTENTS                  81047
         MVI   DEBPRIOR,X'FC'  SET FAIRLY HIGH PRIORITY          81047
         MVI   DEBDEBID,X'0F'  SET DEB IDENTIFIER AND KEY ZERO   81047
         MVI   DEBEXSCL,4    16 BYTES IN ACCESS METHOD SECTION   81047
         LA    R14,DEBBASND  POINT TO FIRST EXTENT ADDRESS       81047
         USING DEBDASD,R14                                       81047
         LA    R15,10        SET NUMBER OF EXTENTS               81047
         LH    R0,VOUTCYL    GET NUMBER OF CYLINDERS ON OUTPUT   81047
         LR    R1,R0         COPY                                81047
         BCTR  R1,0          LAST CYLINDER NUMBER                81047
         MH    R0,VOUTTPC    TOTAL TRACKS ON PACK                85280
         LH    R2,VOUTTPC    GET TRACKS PER CYLINDER             85280
         BCTR  R2,0          SAVE FOR STORE                      81047
         STH   R2,XOUTTPC    SAVE FOR EXTENT CHECKING            85280
LOOPEXT  ST    R3,DEBUCBAD   PROVISIONALLY SET EACH EXTENT FOR O/P
         MVI   DEBDVMOD,X'40'   ALLOW ALL SEEK, NO WRITES        81047
         STH   R1,DEBENDCC   SET END CYLINDER                    81047
         STH   R2,DEBENDHH    AND END TRACK                      81047
         STH   R0,DEBNMTRK   AND TOTAL TRACKS                    81047
         LA    R14,16(,R14)  POINT TO NEXT ONE                   81047
         BCT   R15,LOOPEXT                                       81047
         MVC   0(9,R14),=X'010203040506070809'  SET PDS CONCATENATION
         LA    R15,2         DO FOR TWO OUTPUT EXTENTS (VOL/VTOC)
         SH    R14,=H'16'    GET LAST (OUTPUT) EXTENT BACK       81047
         MVI   DEBDVMOD,X'C0'  ALLOW EVERYTHING                  81047
         BCT   R15,*-8                                           81047
         DROP  R14                                               81047
         SPACE 1                                                 81047
         LA    R2,TCBDEB-4   GET DEB CHAIN                       81047
ADDEBTCB CLC   5(3,R2),GMDEBAD+1  ALREADY ON CHAIN ?             81047
         BE    ADDEBDON      YES                                 81047
         CLC   5(3,R2),ZEROES  END OF CHAIN ?                    81047
         BE    ADDEBON                                           81047
         L     R2,4(,R2)     GET NEXT DEB                        81047
         B     ADDEBTCB                                          81047
ADDEBON  MVC   5(3,R2),GMDEBAD+1  ADD OUR DEB TO CHAIN           81047
ADDEBDON MODESET MODE=SUP    REQUEST SUPERVISOR MODE             81047
         DEBCHK INOUTDCB,TYPE=ADD,AM=EXCP  SET FOR MVS CHECK     81047
         MODESET MODE=PROB   RESET THINGS                        81047
         LA    R5,VINDDNM    POINT TO FIRST INPUT AREA          GP13433
         LA    R6,1          MAX TO SCAN FOR                    GP13433
ININLOOP MVC   OPENDDNM,0(R5)  MOVE NAME                         81047
         MVC   OPENNDSK+5(8),0(R5)                               81047
         READFDR OPEN,VINVSER     GET THE FORMAT 4 DSCB         GP13433
         CH    R15,=H'4'     DID IT WORK ?                      GP13433
         BNH   ININBUMP                                         GP13433
ININBAD  LA    R1,OPENNDSK   UNUSABLE                            81047
         B     DUMPMSG       QUIT                                81047
ININBUMP L     R4,VOUTUCB    GET OUTPUT UCB BACK                 81047
         L     R3,VINUCB
         MVC   MSGOPINF+15(8),VINDDNM  SET DDNAME IN MESSAGE    GP13433
         MVC   MSGOPINF+24(6),MSGOPINF+23  BLANK VOLSER          81047
         TM    UCBTBYT3,UCB3DACC+UCB3TAPE  SERIAL AVAILABLE ?   GP13433
         BNM   *+10          NO; SKIP VOLSER                    GP13433
         MVC   MSGOPINF+24(6),UCBVOLI   VOLSER                   81047
         MVC   MSGOPINF+31(3),UCBNAME   UNIT                     81047
         PRTV  MSGOPINF      PRINT IT                            81208
         LA    R1,OPENDUPL   SET FOR DUPLICATE MESSAGE           81047
         CLC   UCBNAME,UCBNAME-UCBOB(R4)  SAME UCB ?             81047
         BE    DUMPMSG       YES; EXIT WITH MESSAGE              81047
ININSKP2 CLI   UCBTBYT3,UCB3TAPE  MAKE SURE FDR TAPE            GP13433
         BNE   DUMPMSG       IF NOT, THEN FORGET IT              81047
         MVI   VINSIZE,0     ALLOW RDM                          GP13433
ININTYOK MVI   DS4IDFMT,0    CLEAR                               81047
         READFDR READ,ZEROES GET THE VTOC THE HARD WAY, AGAIN   GP13433
         MVC   DS4IDFMT(96),8+44(R1)  COPY THE DATA             GP13433
         CLI   DS4IDFMT,C'4'  WAS THE VTOC READ IN ?             81047
         BNE 2 OPENNOPE      NO; TOO BAD                         81208
         MVC   VINCYL-VINDDNM(2,R5),DS4DEVSZ                     81047
         MVC   VINTPC-VINDDNM(2,R5),DS4DEVSZ+2  TRACKS/CYLINDER  85280
         MVC   VINDSCB-VINDDNM(2,R5),DS4DEVDT  DSCBS+PDES/TRK    86341
         SLR   R14,R14                                           91099
         ICM   R14,3,DS4DEVTK  GET TRACK CAPACITY                91099
         STCM  R14,3,VINSIZE+2-VINDDNM(R5)   COPY TRACK CAPACITY 91099
         MAX   R14,VINSIZE   GET LARGEST SO FAR                  91099
         ST    R14,VINSIZE   STASH IT BACK                       87135
         CLM   R14,7,VOUTSIZE+1  COMPARE TO OUTPUT SIZE          87135
         BNH   *+8           NOT LARGER                          87135
         STCM  R14,7,VOUTSIZE+1                                  87135
         TM    DS4VTOCI,DS4DIRF+DS4DICVT  REPAIRED DIRF ?        86138
         BNO   *+8           NO                                  86138
         NI    DS4VTOCI,255-DS4DIRF-DS4DICVT  TURN BOTH OFF      86138
         TM    DS4VTOCI,255-DS4DICVT  ANY BITS IN DS4VTOCI ?     83094
         BZ    ININPHUN      NO; ASSUME DS4HPCHR OK              81047
         MVC   DS4HPCHR(4),DS4VTOCE+6                            81047
         MVC   DS4HPCHR+4(1),DS4DEVDT                            81047
ININPHUN MVC   INDKAD,DS4HPCHR    MOVE LAST DS ADDRESS           81047
         CLC   COPYEXTS,ZEROES   VTOC EXTENTS COPIED PREVIOUSLY ?
         BNE   INITOLEX      YES                                 81047
         MVC   COPYEXTS,DS4VTOCE  SAVE INPUT VTOC EXTENTS        81047
         MVI   COPYEXTS,XTTTRK  SET DEFAULT TRACK ALIGNMENT      81047
INITOLEX LH    R15,INDKAD    GET CYL #                           81047
         MH    R15,VINTPC-VINDDNM(,R5)  GET NUMBER OF TRACKS     85280
         AH    R15,INDKAD+2  NOW HAVE REL TRK #                  81047
         MVC   OTDKAD,DS4VTOCE+2  MOVE EXTENT START              81047
         MVC   VINEXT-VINDDNM(8,R5),DS4VTOCE+2                   81047
         LH    R1,OTDKAD     GET CYL # OF START                  81047
         MH    R1,VINTPC-VINDDNM(,R5)  GET TRACK NUMBER          85280
         AH    R1,OTDKAD+2   NOW HAVE REL TRK # OF START         81047
         LA    R15,1(,R15)    UP 1 FOR SUBTRACT                  81047
         SR    R15,R1        R15 HAS # OF TRACKS                 81047
         SLR   R1,R1         CLEAR 1                             81047
         IC    R1,DS4DEVDT   GET DSCBS PER TRACK                 81047
         MR    R14,R1        GET # OF DSCBS                      81047
         A     R15,NUMSKP    ADD TO COUNTER                      81047
         ST    R15,NUMSKP    SAVE FOR LATER                      81047
*OLD     LA    R5,VIN2DDNM-VIN1DDNM(,R5)                         81047
*OLD     BCT   R6,ININLOOP   TRY FOR ANOTHER                     81047
         SLR   R6,R6         SET FOR 8                           81047
         B     ININEND2                                          81047
ININEND  CLI   6(R5),C'1'    LOOKING FOR INDISK1/DISKIN1 ?       81047
         BNE   ININEND2      NO                                  81047
         MVI   6(R5),C' '    CHANGE TO INDISK/DISKIN             81047
         B     ININLOOP      TRY AGAIN                           81047
ININEND2 LA    R7,1                                             GP13433
         STH   R7,VIDDMAX    SAVE MAXIMUM DDS TO PROCESS         81047
         TM    MIGFLAG,OPTMIGR  MIGRATION ?                      86299
         BZ    ASKOP         NO; CONTINUE NORMALLY               85280
         NI    IIHOPT,255-DSCHA  RESET CHANGED BIT               85280
         OI    OPTFLAG,OPTCOPY                                   85280
         OI    IIHOPT,IIHCOMP  FORCE IEBCOPY MODE                85280
         NI    WYLFLAG,255-WYLCON  NO CONVERT HONORED            85280
         NI    OPTFLAG3,255-OPTSWAP-OPTFLIP                      85289
*        DISABLE (LOGICALLY) UNMOVABLE LOGIC                     85280
         PRTF  '0MIGRATE MODE: IEBCOPY FORCED; CHGD AND CONV DISABLED'
         TM    MIGFLAG,OPTMIGF  MIGRATION REQUIRED?              86299
         BNZ   ASKOP         YES                                 85280
         OI    MIGFLAG,OPTMIGF  SET MIGRATION FLAG               86299
         PRTF  ' MIGRATE REQUESTED, BUT NOT NECESSARY'           86299
         B     ASKOP         GET OPERATOR PERMISSION             81047
         DROP  R3,R4,R6,R8                                       81047
         SPACE 1                                                 81047
OPENNOPE BALR  R9,0                                              81047
         USING *,R9                                              81047
         LA    R1,OPENBADM   SET FOR BAD OPEN MESSAGE            81047
         SPACE 1                                                 81047
DUMPMSG  BALR  R9,0                                              81047
         USING *,R9                                              81047
         PRTV  (R1)          PRINT FINAL MESSAGE                 81208
         SPACE 1                                                 81047
CONDDUMP BALR  R9,0                                              81047
         USING *,R9                                              81047
         ICM   R4,7,GMDEBAD+1    GET DEB ADDRESS                 81047
         BZ    DMCLDEBN      NONE GOTTEN; NOT ON CHAIN           81047
         MODESET MODE=SUP,KEY=ZERO                               81047
         DEBCHK INOUTDCB,AM=EXCP,TYPE=DELETE  FREE DEBCHECK ENTRY
         MODESET MODE=PROB                                       81047
         ICM   R1,7,1(R4)    GET TCB ADDRESS                     81047
         BZ    DMCLDEBD      NONE GOTTEN; NOT ON CHAIN           81047
         LA    R1,4(,R1)     GET TCBDEB-4                        81047
DMCLDEBL CLC   5(3,R1),GMDEBAD+1    POINTS TO OUR DEB ?          81047
         BE    DMCLDEBX      YES, UNCHAIN IT                     81047
         LAT   R1,4(R1),DMCLDEBD PREVENT LOOP                    81047
         B     DMCLDEBL       AND TRY AGAIN                      81047
DMCLDEBX L     R15,4(,R1)    GET ADDR. OF MY DEB                 81047
         MVC   5(3,R1),5(R15)    UNCHAIN IT                      81047
DMCLDEBD LM    R0,R1,GMDEBL  GET DEB/LENGTH                      81047
         LTR   R1,R1         WAS ANY ?                           81047
         BZ    DMCLDONE      NO, EXIT                            81047
         FREEMAIN R,LV=(0),A=(1)   FREE THE DEB                  81047
DMCLDONE MODESET KEY=NZERO                                       81047
         SPACE 1                                                 81047
DMCLDEBN SERVTERM ,          CLOSE AND FREEMAIN SERVICES         81208
         L     R1,DUMPCODE   GET ABEND CODE/FLAGS                81208
         SVC   13            GREETINGS                           81047
         DROP  R9                                                81047
         SPACE 1                                                 81047
OPENBADM VCON  '0OPEN FAILED FOR ',END=OPENNOPX                  81208
OPENDDNM DC    CL8' '                                            81047
         VCON  ,                                                 81208
OPENDUPL VCON  '0COPY TO SAME PACK NOT SUPPORTED'                81208
OPENNDSK VCON  '0SYSUT1    DD CARD DOES NOT SPECIFY A DISK OR IS NOT US*
               ABLE AS SUPPLIED'                                 81208
OPENNSWP VCON  '0SWAP REQUEST NOT VALID FOR DEVICE'              81208
OPENNSCR VCON  '0OUTPUT VOLUME IS NOT A PRIVATE PACK'            81208
OPENINCM VCON  '0INPUT AND OUTPUT DEVICE ARE INCOMPATIBLE'       81208
OPENNSUP VCON  '0OUTPUT DEVICE TYPE IS NOT SUPPORTED'            81208
MSGOPINF VCON  ' OUTPUT DD *DDNAME* VOLSER(CUU)'                 81208
BDOPMES  VCON  ' OPERATOR DENIED YOUR REQUEST TO SQUISH VOLUME'  81208
OUTDDS   DC    C'OUTDISK DISKOUT SYSUT2  '  OUTPUT DDNAMES       81047
INPDDS   DC    C'INDISK  DISKIN  SYSUT1  '  CORR. INPUT DD NAMES 81047
CAMDSCB4 CAMLST SEARCH,1,2,3   OUTJFCB,VINVSER,DS4IDFMT          81208
         SPACE 2                                                 81047
TIOTFIND LTCB  R4,USE=YES    GET THE TCB ADDRESS                 86138
         L     R1,TCBTIO     TIOT ADDRESS                        81047
         USING TIOT1,R1                                          81208
         SLR   R2,R2         CLEAR FOR IC                        81047
TIOTLOOP CLC   TIOEDDNM,OPENDDNM  MATCHING DDNAME ?              81047
         BE    TIOTFUND      YES; CHECK A BIT                    81047
TIOTBUMP IC    R2,TIOELNGH   ENTRY LENGTH                        81047
         AR    R1,R2                                             81047
         CLI   TIOELNGH,X'00'   END OF TABLE ?                   81047
         BNE   TIOTLOOP                                          81047
         B     4(,R9)        RETURN ENTRY NOT FOUND              81047
TIOTFUND CLI   TIOELNGH,20   ENTRY FOR SINGLE UCB ?              81047
         BNE   8(,R9)        RETURN UNUSABLE ENTRY               81047
         ICM   R3,7,TIOEFSRT  UCB ADDRESS                        81047
         BZ    8(,R9)        RETURN UNUSABLE                     81047
         CLI   TIOELNGH+20,0  ANOTHER DD AFTER THIS ONE ?        81047
         BER   R9            NO; RETURN OK                       81047
         CLI   TIOEDDNM+20,C' '   CONCATENATION ?                81047
         BNER  R9            NO; USABLE                          81047
         B     8(,R9)        RETURN UNUSABLE                     81047
         DROP  R1                                                81047
         SPACE 1                                                 82298
ASKOP    MVC   WTOR+32(6),VOUTVSER   SET OUTPUT VOLUME SERIAL    82298
         LA    R1,WTOR+44    SET FOR INPUT VOLUMES               81047
         LA    R2,1          DO MAX OF 1                        GP13433
         LA    R3,VINVSER    POINT TO TABLE OF INPUT DDS        GP13433
ASKOPLOP MVC   0(6,R1),0(R3)                                     81047
         LA    R1,7(,R1)                                         81047
         LA    R3,VIN2DDNM-VIN1DDNM(,R3)                        GP13433
         BCT   R2,ASKOPLOP   BUILD FULL LIST                     81047
         LH    R1,VIDDMAX    GET NUMBER OF DD CARDS              81047
         MH    R1,=H'7'      LENGTH OF WTOR TEXT FOR VOLUMES     81047
         LA    R1,35(,R1)    PLUS OVERHEAD                       81047
         TM    OPTFLAG3,OPTFLIP  EXCHANGE VOLSERS ?              87040
         BZ    ASKOPSTH      NO                                  87040
         MVC   WTOR+12+6(6),=C'*FLIP*'                           87040
         MVC   WTOR+12+27(4),=C'WITH'                            87040
ASKOPSTH STH   R1,WTOR+8     SET NEW LENGTH                      82298
         LA    R14,FULL                                          81208
         LA    R15,DB                                            81208
         STM   R14,R15,WTOR  SET REPLY AND ECB ADDRESSES         81208
         MVI   WTOR,3        SET REPLY LENGTH                    81208
ASKOPSIE MVI   DB,0            CLEAR ECB                         81047
         TM    OPTFLAG,OPTNWTOR   BYPASS WTOR FOR AUTOMATIC     GP09239
         BNZ   OPSDYES              NIGHTLY BACKUPS ?           GP09239
         WTOR  MF=(E,WTOR)     ASK OPSIE
         WAIT  ECB=DB          WAIT FOR ANSWER                   81047
         OI    FULL,X'40'      UP CASE REPLY
         CLI   FULL,C'Y'       DID OPER SAY YES
         BE    OPSDYES         GOODIE
         AIF   ('&LOCAL' NE 'PID').NOTUPUR                       92279
         CLI   FULL,C'U'       DID OPER SAY 'UP YOURS'           81047
         BE    OPSDYES         NO; BUT CLOSE                     81047
.NOTUPUR CLI   FULL,C'O'       DID OPER SAY OK ?                 81208
         BE    OPSDYES         GOODIE                            81047
         CLI   FULL,C'N'       DID OPER SAY NO
         BNE   ASKOPSIE        IF NOT, THEN ASK AGAIN            81047
         SPACE 1
         LA    R1,BDOPMES    TELL USER THE SAD NEWS
         B     DUMPMSG        AND QUIT                           81047
         SPACE 1                                                 81047
OPSDYES  CLI   VIDDMAX+1,1   ONE INPUT VOLUME ?                  81047
         BNE   OPSDYESQ                                          81047
*OOPS    L     R0,VINUCB     GET INPUT UCB ADDRESS              GP13433
*OOPS    LA    R1,LSPIN      GET LSPACE INPUT                    79327
*OOPS    SVC   78            DO LSPACE                           79327
OPSDYESQ MVC   LSPUCB,VOUTUCB UCB ADDRESS FOR LSPACE             81047
         LH    R1,VIDDMAX    GET NUMBER OF INPUT DDS             81047
         MH    R1,=H'12'     GET LENGTH OF ENQLIST               81047
         LA    R1,ENQLIST(R1)  POINT TO LAST ENQUEUE LIST ENTRY  81047
         MVI   0(R1),X'FF'   SET END OF LIST                     81047
         LA    R1,ENQLIST    CHECK FOR DUPLICATE VOLUME SERIALS  81047
DUPQNEXT L     R14,8(,R1)    GET RNAME                           81047
         LR    R2,R1         GET COMPARE ADDRESS                 81047
         B     DUPQBUMP      BUMP IT ONCE                        81047
DUPQLOOP L     R15,8(,R2)    GET OTHER RNAME                     81047
         CLC   0(6,R14),0(R15)  DUPLICATE ?                      81047
         BNE   DUPQBUMP      NO; OK                              81047
         BCTR  R15,0         ELSE FINAGLE                        81047
         STCM  R15,7,9(R2)   MODIFY HIGHER ONE                   81047
DUPQBUMP TM    0(R2),X'C0'   LAST ENTRY ?                        81047
         LA    R2,12(,R2)                                        81047
         BNO   DUPQLOOP      NO; TRY AGAIN                       81047
         TM    0(R1),X'C0'   ALL DONE ?                          81047
         LA    R1,12(,R1)                                        81047
         BNO   DUPQNEXT      NO; REPEAT                          81047
         ENQ   MF=(E,ENQLIST)  ENQ ON THE VTOCS                  79327
         SPACE 1                                                 81047
         L     R5,NUMSKP       GET # OF DSCBS IN 5               81047
         SLL   R5,3            MULT 5 BY 8 (DSLEN)               81047
         GETMAIN R,LV=(R5)     GET STORAGE FOR DSCB LIST         81047
         SLR   R8,R8                                             81047
         ST    R8,NUMSKP     RESET COUNTER                       81047
         SLR   R9,R9                                             81047
         LR    R14,R1        START ADDRESS                       81047
         LR    R15,R5        LENGTH                              81047
         MVCL  R14,R8        CLEAR GOTTEN STORAGE                81047
         ST    R1,DSLSTAD      SAVE ADDRESS                      81047
         SPACE 2                                                 79105
         MODESET KEY=ZERO                                        *UNG*
         L     R2,GMDEBAD    GET DEB ADDRESS                     81047
         LA    R14,VINDDNM   POINT TO INPUT TABLE               GP13433
         USING DEBBASIC,R2   DECLARE DEB EXPANSION               81208
         LA    R2,DEBBASND   POINT TO DASD MAPPING               79105
         USING DEBDASD,R2    SET NEW MAPPING                     79105
         LH    R8,VIDDMAX    LOOP ON ALL INPUT                   81047
INDEBLUP MVC   DEBUCBA(3),VINUCB-VINDDNM+1(R14)  SET UCB ADDRESS 81047
         XC    DEBSTRCC(4),DEBSTRCC  ALLOW TRACK ZERO            81047
         LH    R4,VINCYL-VINDDNM(,R14)   NUMBER OF CYLINDERS     81047
         LR    R5,R4
         MH    R4,VINTPC-VINDDNM(,R5)  * TRACKS / CYLINDER       85280
         STH   R4,DEBNMTRK    TRACKS IN EXTENT                   79105
         BCTR  R5,0           LAST CYL NUMBER                    *UNG*
         STH   R5,DEBENDCC     END CYLINDER                      *UNG*
         LH    R5,VINTPC-VINDDNM(,R5)  GET TRACKS/CYLINDER       85280
         BCTR  R5,0          LESS ONE FOR START AT 0             85280
         STH   R5,DEBENDHH   SET LAST TRACK IN CYLINDER          85280
         LA    R14,VIN2DDNM-VIN1DDNM(,R14)                       81047
         LA    R2,16(,R2)    NEXT DEB EXTENT
         BCT   R8,INDEBLUP   DO ALL INPUT VOLUMES
         DROP  R2                                                79105
         MODESET KEY=NZERO                                       *UNG*
         SPACE 2                                                 79105
         SLR   R8,R8         CLEAR FOR ICM                       81047
         ICM   R8,7,VOUTSIZE+1   GET TRACK CAPACITY +OVERHEAD    87135
         LA    R8,8(,R8)     ALLOW FOR ONE MORE                  87135
         A     R8,=A(DWCDATA-BUFFMAP+15)  ADD FIXED BUFFER SIZE+ 81047
         N     R8,=X'00FFFFF8'  TRUNCATE TO DOUBLE-WORD          81047
         LR    R7,R8         COPY                                81047
         LA    R5,NUMBUFFS   NUMBER OF BUFFERS REQUESTED         81047
         TM    OPTFLAG2,OPTCLEAR  CLEAR VOLUME ENTRY ?           82298
         BZ    BUFINGET      NO; NORMAL GETMAIN                  82298
         LA    R5,2          SET FOR TWO BUFFERS                 82298
         MVI   IOECBLST+4,X'80'  SET END OF ECBLIST BIT          82298
BUFINGET MR    R6,R5         TOTAL STORAGE FOR BUFFER POOL       82298
         GETMAIN R,LV=(R7)   GET LOTS OF STORAGE                 81047
         LR    R9,R1         SAVE START ADDRESS                  81047
         LR    R14,R1        COPY                                81047
         LR    R15,R7        SET SIZE                            81047
         SLR   R0,R0                                             81047
         SLR   R1,R1                                             81047
         MVCL  R14,R0        CLEAR ALL OF IT                     81047
         LA    R7,IOECBLST   GET ECB LIST (ALSO BUFFER ADDRESSES)
BUFFINIT STCM  R9,7,1(R7)    SET ADDRESS OF CURRENT BUFFER       81047
         USING BUFFMAP,R9    DECLARE IT                          81047
         ST    R9,DWIECBP    SET ECB POINTER                     81047
         LA    R1,INOUTDCB   GET COMMON EXCP DCB                 81047
         ST    R1,DWIDCB                                         81047
         MVI   DWECB,X'7F'   READY FOR ACTION                    81047
         MVI   DWTOGO,OFFAVAIL  SET AVAILABLE FOR I/O            81047
         LA    R14,DWCRDCNT  POINT TO READ COUNT CHAIN PREFIX    81047
         LA    R15,DWCRDATA-DWCRDCNT(,R14)  READ DATA PREFIX     81047
         LA    R0,DWCCOUNT-DWCRDATA(,R15)  COUNT AREA            81047
         LA    R1,DWCDATA-DWCRDATA(,R15)   TRACK DATA AREA       81047
         STM   R14,R1,DWARDCNT  INITIALIZE ADDRESSES             81047
         LM    R2,R4,=X'310000004000000508000000'  SEEK PATTERN  81047
         LA    R1,DWICCHH    IOB SEEK CCHH PORTION               81047
         OR    R2,R1         BUILD SEEK CCW                      81047
         OR    R4,R14        SET TIC ADDRESS                     81047
         STM   R2,R4,0(R14)  BUILD DWCRDCNT PREFIX               81047
         ICM   R4,7,DWARDATA+1  SET TIC FOR SECOND CHAIN         81047
         STM   R2,R4,0(R15)  BUILD DWCRDATA PREFIX               81047
         STM   R2,R4,DWCERCCW  SET ERROR CCW PREFIX              83094
         LA    R4,DWCERCCW                                       83094
         STCM  R4,7,DWCERCCW+9  MAKE TIC                         83094
         MVI   DWCNUCCW,X'08'  MAKE TIC TO USER                  83094
         LA    R1,DWR0DATA   RECORD 0 DATA AREA                  81047
         LM    R2,R3,=X'0600000040000008'  READ/WRITE R0 DATA CCW
         OR    R2,R1         ADD ADDRESS                         81047
         STM   R2,R3,DWCRDAT0-DWCRDATA(R15)  COMPLETE CHAIN      81047
         STM   R2,R3,DWCRDAT0-DWCRDATA(R14)  SET READ R0         81047
         LA    R4,NUMCCWS    SET NUMBER OF READ COUNTS TO BUILD  81047
         LA    R0,8          QUICK ADD CONSTANT                  81047
         LA    R2,DWCCOUNT-DWCRDATA(,R15)  POINT TO COUNT AREA   81047
         ICM   R2,8,=X'12'   SET READ COUNT OPCODE               81047
BUFFRCNT STM   R2,R3,DWCRDCCW-DWCRDCNT(R14)  MAKE ONE CCW        81047
         AR    R14,R0        NEXT BUILD ADDRESS                  81047
         ALR   R2,R0         NEXT COUNT ADDRESS                  81047
         BCT   R4,BUFFRCNT   BUILD ALL                           81047
         SLR   R14,R0        BACKSPACE                           81047
         MVI   DWCRDCCW-DWCRDCNT+4(R14),0  KILL CHAINING IN LAST 81047
         L     R2,DWARDATA   GET NORMAL CHAIN                    81208
         SH    R2,=H'24'                                         81208
         LA    R14,DWICCHH                                       81208
         LA    R15,4                                             81208
         STM   R14,R15,0(R2)                                     81208
         MVI   0(R2),X'39'   READ HOME ADDRESS                   81208
         MVI   4(R2),X'40'   COMMAND CHAIN                       81208
         ST    R2,8(,R2)                                         81208
         MVI   8(R2),X'08'   TIC *-8                             81208
         LA    R14,DWR0COUN                                      81208
         LA    R15,16                                            81208
         STM   R14,R15,16(R2)                                    81208
         MVI   16(R2),X'16'  READ R0                             81208
         MVI   20(R2),X'40'  COMMAND CHAIN                       81208
*                                                                81047
         LA    R7,4(,R7)     NEXT ECB ADDRESS                    81047
         AR    R9,R8         NEXT BUFFER ADDRESS                 81047
         BCT   R5,BUFFINIT   SET IT                              81047
         DROP  R9                                                81047
         TITLE 'C O P Y V O L  ***  OUTPUT VTOC FORMATTING'      81047
         LA    R0,8          SET 000Z                            81047
         ST    R0,FULL       SET FOR DSCB READ                   81047
         READVTOC FMT4       READ THE FORMAT 4                   81047
         MVC   VOUTDSCB(2),DS4DEVDT  DSCBS+PDES/TRK              86341
         TM    OPTFLAG3,OPTVTOC  VTOC COPY OPTION ?              81047
         BZ    COMVTCOP      NO; SKIP THIS CRUD                  81047
         CLI   COPYEXTS,2    VTOC= CARD SUPPLIED ?               82298
         BNE   COMVTTST      NO; CHECK FOR INPUT COPY            82298
         MVI   COPYEXTS,XTTTRK  SET VTOC ON TRACK BOUNDARY       82298
         SLR   R14,R14                                           82298
         ICM   R15,15,COPYEXTS+2   GET VTOC START                82298
         LH    R0,VOUTTPC    GET TRACKS PER CYLINDER             85280
         DR    R14,R0        GET START ADDRESS                   82298
         SLR   R2,R2                                             82298
         ICM   R3,15,COPYEXTS+6    GET VTOC SIZE                 82298
         ICM   R2,3,COPYEXTS+6  CYL= OPTION ?                    86292
         BZ    NEWVTTRK      NO; TRACKS                          86292
         AR    R2,R15        GET END CYLINDER+1                  86292
         BCTR  R2,0          END CYLINDER                        86292
         LH    R3,VOUTTPC    GET TRACKS PER CYLINDER             86292
         BCTR  R3,0          MAKE LAST TRACK IN CYLINDER         86292
         MH    R2,VOUTTPC    CONVERT CYLINDERS TO TRACKS         86292
         AR    R3,R2         MAKE TOTAL TRACKS                   86292
         SLR   R2,R2         CLEAR FOR DIVIDE                    86292
         B     NEWVTCOM      GO TO COMMON                        86292
NEWVTTRK BCTR  R3,0          MAKE RELATIVE                       82298
         AH    R3,COPYEXTS+4 MAKE END TRACK ADDRESS              86293
         CH    R3,COPYEXTS+4 CHECK FOR VALID RANGE               86293
         BL    BADVTCOP      INVALID                             82298
NEWVTCOM DR    R2,R0         AND END ADDRESS                     82298
         STCM  R15,3,COPYEXTS+2  START CC                        82298
         STCM  R14,3,COPYEXTS+4  START HH                        82298
         STCM  R3,3,COPYEXTS+6  END CC                           82298
         STCM  R2,3,COPYEXTS+8  END HH                           82298
COMVTTST CLC   COPYEXTS+2(4),ZEROES  VTOC ON TRACK ZERO ?        82298
         BE    BADVTCOP      YES; FAIL                           81047
         CLC   VOUTCYL,COPYEXTS+6  NEW VTOC ON DISK ?            81047
         BH    DOVTCOPY      YES; PROCEED WITH VTOC RELOCATION   81047
BADVTCOP OI    CCODE+1,8     SET MAJOR ERROR                     81047
         SUBCALL DUMPMSG,MSGVOLAP  QUIT WITH ERROR               81208
MSGVOLAP VCON  '0VTOC OPTION CANNOT BE SATISFIED - EXTENTS BAD'  81208
DOVTCOPY MVC   DS4VTOCE+2(8),COPYEXTS+2  SET NEW VTOC EXTENTS    81047
         MVI   ABSHD,9       SET VTOC EXTENT                     85296
         MVC   ABSAD(4),DS4VTOCE+2                               81047
         MVI   ABSAD+4,1     SET RECORD 1                        81047
         MVI   ABSHD,9       SET VTOC EXTENT                     85296
         LA    R1,ABSHD                                          81047
         BAL   R9,ABSTTR     GET TTR OF VTOC START               81047
         STCM  R0,14,FULL    STASH IN TTR WORD                   81047
COMVTCOP L     R0,FULL       GET VTOC TTRZ                       81047
         ST    R0,BLOCKAD    JUST IN CASE ?                      81047
         AL    R0,=X'00000100'  GET NEXT BLOCK                   81047
         ST    R0,BLOCKF5    SAVE FORMAT 5 ADDRESS               81047
         MVC   FMT1HWM(4),DS4VTOCE+2                             81047
         MVI   FMT1HWM+4,3   SET PROVISIONAL FMT1 HIGH-WATER MARK
         MVC   LASTFMT1,FMT1HWM  SET FMT 1 HIGH-WATER MARK       83094
         MVI   DS4VTOCI,DS4DOSBT+DS4DIRF  TURN ON BAD VTOC BITS  83094
         XC    DS4F6PTR,DS4F6PTR  SPLIT ALLOCATION UNSUPPORTED   79105
         MVI   DS4NOEXT,1    JUST  IN CASE - 1 EXTENT            82298
         MVI   DS4VTOCE,XTTTRK  ON TRACK BOUNDARY                82298
         MVI   DS4VTOCE+1,0  EXTENT NUMBER                       82298
         MVC   DS4HPCHR(4),DS4VTOCE+6                            *UNG*
         MVC   DS4HPCHR+4(1),DS4DEVDT  SET HIGH RECORD NUMBER    79105
         MVC   ABSAD,DS4HPCHR  SET LAST CCHHR IN VTOC            81047
         LA    R1,ABSHD                                          81047
         BAL   R9,ABSTTR     CONVERT TO VOLUME TTR               81047
         STCM  R0,14,VTOCLTTR  SAVE LAST TTR IN VTOC             81047
         XC    DS4AMTIM(DS4F6PTR-DS4AMTIM),DS4AMTIM  CLEAR VSAM  81047
         MVC   DS4DEVSZ(2),VOUTACYL USE DCE SIZE (WHEN NON-ZERO) 93166
         CLC   DS4HCCHH(2),VOUTCYL     ALTERNATE IN RANGE ?     GP09297
         BL    FIXALT        NO                                 GP09297
         CLC   DS4HCCHH(2),VOUTACYL    IN RANGE ?               GP09297
         BNH   GOODALT       YES                                GP09297
FIXALT   MVC   DS4HCCHH(2),VOUTCYL     OVERLAY ALTERNATES       GP09297
         XC    DS4HCCHH+2(2),DS4HCCHH+2   ZERO TRACK            GP09297
         LH    R0,VOUTACYL   GET NUMBER OF ALT. CYLINDERS       GP09297
         SH    R0,VOUTCYL                                       GP09297
         MH    R0,VOUTTPC    CONVERT TO TRACKS                  GP09297
         STH   R0,DS4NOATK   AND UPDATE                         GP09297
*   AT SOME POINT FIX ALTERNATE CCHH POINTERS?                  GP09297
         SPACE 1
GOODALT  MODESET KEY=ZERO                                        81047
         L     R2,GMDEBAD    GET DEB PROPER                      81047
         USING DEBBASIC,R2                                       81208
         LA    R2,DEBBASND+9*16  GET TENTH EXTENT                81047
         USING DEBDASD,R2                                        81047
         MVC   DEBSTRCC(8),DS4VTOCE+2  COPY VTOC EXTENT          81047
         LH    R1,DEBENDCC                                       81047
         SH    R1,DEBSTRCC                                       81047
         MH    R1,VOUTTPC                                        85280
         AH    R1,DEBENDHH                                       81047
         SH    R1,DEBSTRHH                                       81047
         AH    R1,=H'1'                                          81047
         STH   R1,DEBNMTRK                                       81047
         ST    R1,TRKUSD     SET FOR COPY                        91119
         MODESET KEY=NZERO                                       81047
         MVI   DS1NOEPV,1                                        81047
         MVC   NEWEXTS(XTLEN),DS4VTOCE  SET VTOC EXTENTS         81047
         XC    NEWEXTS+XTLEN(XTLEN),NEWEXTS+XTLEN   CLEAR SECOND 83094
         MVC   OLDEXTS(2*XTLEN),NEWEXTS                          81047
         MVC   VINTPC,VOUTTPC  FAKE TRACKS/CYLINDER              85280
         MVC   DSLCURR,=A(DSLUNM,9)                              81047
         MVI   TRAKFLAG,TKVTOCWT  SET TO REWRITE VTOC            81047
         SUBCALL TRAKCOPY    WRITE IT                            81047
         TM    OPTFLAG3,OPTVTOC  DUPLICATE VTOC ?                81047
         BZ    NOCOPVTO      NO                                  81047
         LA    R4,=A(DSLUNM,8)  POINT TO UNMOVABLE-3             81047
         XC    OLDEXTS(2*XTLEN),OLDEXTS  CLEAR EXTENTS           81047
         XC    NEWEXTS(2*XTLEN),NEWEXTS  DITTO                   81047
         LA    R0,1          SET FOR ONE TRACK                   81047
         ST    R0,TRKUSD      TO BE COPIED                       91119
         MVC   DS1DSNAM(14),=C'    IPL TRACK '  MAKE MESSAGE     81047
         MVC   DS1DSNAM+14(30),DS1DSNAM+13  CLEAR REST           81047
         MVC   INJFCB(44),FMT1  MOVE DATA SET NAME               81047
         MVI   TRAKFLAG,TKIPL  SET IPL TRACK COPY                81047
         SUBCALL TRAKCOPY    COPY IPL TRACK (SETS UCBVTOC)       81047
NOCOPVTO MVI   DS4VTOCI,0    CLEAR VTOC INDICATORS               83094
         MVC   DB,DS4VTOCE+2   MOVE EXTENT INFO                  81047
         LH    R15,DB+4      GET CYL # OF END                    81047
         SH    R15,DB        LESS CYL # OF START                 81047
         MH    R15,VOUTTPC   GET TRACK #                         85280
         AH    R15,DB+6      PLUS END TRACK                      81047
         SH    R15,DB+2      NOW HAVE REL TRK # OF START         81047
         LA    R15,1(,R15)    UP 1                               81047
         SLR   R1,R1         CLEAR 1                             81047
         IC    R1,DS4DEVDT   GET DSCBS PER TRACK                 81047
         MR    R14,R1        GET # OF DSCBS                      81047
         CVD   R15,DB        CONVERT DSCBS TO PACKED             81047
         BCTR  R15,0                                             82298
         BCTR  R15,0         NUMBER OF FREE DSCBS BEFORE COPY    82298
         STH   R15,DS4DSREC  SAVE FOR CLEARVOL                   82298
         PRTLIST MSGOPVOL    DISPLAY THE STATISTICS              83094
         SPACE 1                                                 81047
         TM    OPTFLAG2,OPTCLEAR  CLEARVOL ENTRY ?               82298
         BNZ   CHEKMIGR      YES; SKIP SUBHEAD                   86292
         TM    OPTFLAG,OPTERSE  TERSE MODE ?                     81047
         BZ    VTOSUBHD      NO; EJECT                           81047
         TM    OPTFLAG,OPTLOTS   LOTS OF MESSAGES ?              81208
         BNZ   VTOSUBHD      YES; FORCE EJECT                    81208
         MVI   SUBVREAD+4,C'-'  SET FOR DELAYED SUB-TITLE        81208
         PRTSPACE 1          SEPARATE A BIT                      86299
VTOSUBHD PRTV  SUBVREAD,TITLE=3    PAGE EJECT OR DELAYED TITLE   81208
         B     CHEKMIGR      GO TO READ ALL DSCBS                86292
         SPACE 1                                                 81047
SUBVREAD VCON  '1      INPUT VTOC PROCESSING                           *
               STATUS                                         EXT  TRKS*
                ORG RECFM   LEN BLKSZ'                           86292
MSGOPVOL FDOPT NL,CC=C'0'    DOUBLE-SPACE                        83094
         FDPRT VOUTVSER,PADR                                     83094
         FDPRT 'VTOC FORMAT COMPLETE. EXTENTS='                  83094
         FDPRT DS4VTOCE+2,2,HEX                                  83094
         FDPRT '.'                                               83094
         FDPRT DS4VTOCE+4,2,HEX                                  83094
         FDPRT '/'                                               83094
         FDPRT DS4VTOCE+6,2,HEX                                  83094
         FDPRT '.'                                               83094
         FDPRT DS4VTOCE+8,2,HEX                                  83094
         FDPRT DS4DSREC,I,RADJ,PAD,LEN=10                        83094
         FDPRT 'DSCBS'                                           83094
         FDPRT *END                                              83094
         SPACE 1                                                 81047
         LTORG ,                                                 81047
         SPACE 2                                                 86292
CHEKMIGR BALR  R10,0         SET NEW BASE                        86292
         USING *,R10         DECLARE IT                          86292
         TM    MIGFLAG,OPTMIGR+OPTMIGF  ARE WE MIGRATING ?       86292
         BNO   VTOCREAD      NO                                  86292
         DEVTYPE SYSIN,DB    WAS A CONTROL DD SUPPLIED ?         86292
         BXH   R15,R15,VTOCREAD  NO; DON'T CLOBBER SYSIN         86292
         DEVTYPE SYSIN+8,DB  WAS A SYSIN DD SUPPLIED ?           86292
         BXH   R15,R15,VTOCREAD  ALSO CAN'T OPEN IT              86292
         CLI   DB+2,UCB3DACC  DISK DATASET ?                     86292
         BNE   VTOCREAD      NO; ASSUME NOT REWRITABLE           86292
         SERVCALL RJFCB,SYSIN+8  GET THE JFCB FOR SYSIN          86292
         BXH   R15,R15,VTOCREAD  NONE AVAILABLE ?                86292
         MVC   INJFCB(L'DS1DSNAM),0(R1)  SAVE THE DSN            86292
         SERVCALL DSABB,INJFCB  GET THE ABBREVIATION             86292
         CLI   0(R1),C'&&'   TEMPORARY DATASET ?                 86292
         BNE   VTOCREAD                                          86292
         L     R2,=A(SASIN)  GET THE SAS CONTROL CARD DCB        86292
         OPEN  ((R2),OUTPUT) OPEN IT                             86292
*        B     VTOCREAD                                          86292
         TITLE 'C O P Y V O L  ***   INPUT DSCB PREPARATION'     81047
VTOCREAD BALR  R10,0         SET NEW BASE                        81047
         USING *,R10         DECLARE IT                          81047
         TM    OPTFLAG,OPTCHEK  LOCAL CHECKING REQUESTED ?       86138
         BZ    VTCNOTCK      NO                                  86138
         SERVCALL INITG      ENABLE FASTER, FATTER CODE          86138
VTCNOTCK SLR   R7,R7         SET CURRENT DD ENTRY                86138
         LM    R2,R4,DSLBXLE   POINT TO DSLST AND LENGTH         81047
         USING DSLIST,R4      DECLARE IT                         81047
         TM    OPTFLAG2,OPTCLEAR  CLEARVOL ENTRY ?               82298
         BNZ   VTOCNSCR      YES; FAKE EMPTY PACK                82298
VTOCVOLP LR    R1,R7                                             81047
         MH    R1,=Y(VIN2DDNM-VIN1DDNM)  MAKE OFFSET             81047
         LA    R1,VIN1DDNM(R1)  POINT TO CURRENT ENTRY           81047
         MVC   VINDDNM(VIN2DDNM-VIN1DDNM),0(R1)  MOVE IT         81047
         STH   R7,VIDDCUR    SET CURRENT POINTER                 81047
         XC    OLDEXTS(2*XTLEN),OLDEXTS  CLEAR                   85280
         MVI   DS1NOEPV,1    FAKE ONE EXTENT                     81047
         MVI   OLDEXTS,XTTTRK  SET AS TRACK DATA                 85280
         MVC   OLDEXTS+2(8),VINEXT  SET VTOC EXTENTS             85280
         XC    NEWEXTS(2*XTLEN),NEWEXTS  CLEAR FAKE OUTPUT       85280
         MVI   NEWEXTS+5,1   START ON TRACK ONE                  85280
         MVC   NEWEXTS+2+4(4),VOUTACYL MOVE CYLINDER AND TRACKS  85280
         MVC   DSLCURR,=A(DSLUNM,0)  FAKE UNMOVABLE              81047
         STC   R7,DSLCURR+DSLF2-DSLIST  SET VTOC NUMBER          81047
         XC    NUMVOL,NUMVOL      CLEAR DS ON THIS VOL          GP09205
         MVI   TRKUSD,X'7F'  DO ALL                              91119
         MVI   TRAKFLAG,TKVTOCRD  SET FOR VTOC READ              81047
VTOCEXCP SUBCALL TRAKCOPY    READ A VTOC TRACK                   81047
         ICM   R6,7,TRKCHAIN+1  GET TRACK CHAIN                  81047
         BZ    VTOCDUNG      ALL DONE; DO ANOTHER VOLUME         81047
         USING BUFFMAP,R6    DECLARE BUFFER MAPPING              81047
         L     R6,DWARDATA   GET CKD CCW CHAIN                   81047
         DROP  R6                                                81047
         LA    R6,24(,R6)    SKIP SEARCH/TIC/READ R0             81047
VTOCNEXT CLI   0(R6),X'1E'   READ CKD ?                          81047
         BNE   VTOCBUMP      NO; TRY AGAIN                       81047
         L     R1,0(,R6)     GET DATA ADDRESS                    81047
         CLI   8+DS1FMTID-FMT1(R1),C'1'  DSCB 1 ?                81047
         BE    VTOCFMT1      YES; PROCESS                        81047
         CLI   8+DS1FMTID-FMT1(R1),C'4'  DSCB 4 ?                81047
         BNE   VTOCBUMP      NO; SKIP                            81047
         MVC   VTOCIN(DS1END-FMT1),8(R1) SAVE FMT 4              81047
         B     VTOCBUMP                                          81047
VTOCFMT1 MVC   FMT1(DS1END-FMT1),8(R1)  MOVE DATA                81047
         MVC   INJFCB(L'DS1DSNAM),DS1DSNAM   DSN FOR ERROR MSG   81074
         LH    R8,0(,R1)     GET CYLINDER NUMBER                 81047
         MH    R8,VINTPC     CONVERT TO TRACKS                   85280
         AH    R8,2(,R1)     GET TOTAL TRACKS                    81047
         SLL   R8,16         MAKE TT00                           81047
         ICM   R8,2,4(R1)    MAKE TTR0                           81047
         ST    R8,DSLTTR        SAVE FOR FUTURE
         STC   R7,DSLF2      SAVE DISK NUMBER                    81047
         LA    R1,MSGTEMP    SET TEMP DATASET MESSAGE            81047
         CLI   DS1DSNAM,C'*'   TEMPORARY (IEHMOVE) DATASET ?     79137
         BE    VTOCSKIT       YES; IGNORE                        79327
         CLC   DS1DSNAM(3),SYS1   SYS PREFIX ?                   79095
         BNE   NTSYSDA        NO                                 79095
         CLC   =C'.T',DS1DSNAM+8  TIME FIELD ?                   79095
         BNE   NTSYSDA        NO                                 79095
         LA    R14,DS1DSNAM+3                                    79095
         LA    R15,5                                             79095
CKSYSDA  CLI   0(R14),C'0'    NUMERIC ?                          79095
         BL    NTSYSDA       NO                                  79095
         LA    R14,1(,R14)                                       79095
         BCT   R15,CKSYSDA                                       79095
         B     VTOCSKIT                                          81047
VTOCSKIP LA    R1,MSGEXCL    SHOW EXPLICIT EXCLUDE               81047
VTOCSKIT TM    OPTFLAG,OPTLOTS  LOTS OF MESSAGES ?               81047
         BZ    VTOCSKIZ      NO                                  81047
         SUBCALL MSGIN       PRINT THE MESSAGE                   81047
VTOCSKIZ INC   NUMSKP        COUNT SKIPPED DATASETS              81208
         B     VTOCBUMP      PROBABLY TEMPORARY DSN; IGNORE      81047
NTSYSDA  MVC   DSLFG,OIALLDS SET BYTE AS PER PARM REQUEST (ASIS) 83094
         MVC   DB(2),MASKORG COPY PERMITTED DSORGS               86292
         NC    DB(2),DS1DSORG  MASK DSCB ORG                     86292
         CLC   DB(2),DS1DSORG  SELECTED TYPE ?                   86292
         BNE   VTOCSKIP      NO; SHOW SKIPPED BY USER            86292
         MVC   INJFCB(L'DS1DSNAM),DS1DSNAM  SAVE FOR MSG         81047
         TM    DS1DSORG,JFCORGIS       ISAM DATA SET ?      *TSM*80211
         BZ    NTIS01          IF NOT, SKIP SETTING BIT
         OI    DSLFG,DSLISAM    TURN ON ISAM BIT                 81047
         SPACE 1                                                 81047
         AIF   (&MVS).NJFORG                                     82271
JFCORGCQ EQU   X'08'         QTAM MESSAGE QUEUE                  81047
JFCORGTQ EQU   X'20'         TCAM MESSAGE QUEUE                  81047
         AIF   (&SVS OR '&SYSTEM' EQ 'VS1').NJFORG               82271
JFCORGAM EQU   X'08'         VSAM DATA                           81047
.NJFORG  SPACE 1                                            *TSM*80211
*              TREAT QTAM MESSAGE QUEUES AS UNMOVABLE       *TSM*80211
NTIS01   TM    DS1DSORG,JFCORGCQ+JFCORGU                    *TSM*80211
         BNZ   NTMOV                   UNMOVABLE            *TSM*80211
*              TCAM MESS QUEUES AND VSAM SPACES UNMOVABLE        81047
         TM    DS1DSORG+1,JFCORGTQ  TCAM MESSAGE QUEUE ?    *TSM*80211
         BNZ   NTMOV         YES; DO TELL                        81047
         TM    DS1DSORG+1,JFCORGAM     VSAM DATASET ?            81047
         BZ    NTABS01       NO                                  81047
         OI    DSLFG,DSLVSAM   SET VSAM FLAG                     81047
NTMOV    OI    DSLFG,DSLUNM                                 *TSM*80211
NTABS01  CLC   SYS1,FMT1                IS IT A 'SYS1.' DS ? *AMS* 11/3
         BNE   NTSYS01                  NO, SKIP IT          *AMS* 11/3
         OC    DSLFG,OISYS1  SET FLAG AS PER SYSP/SYSU PARM      83094
NTSYS01  CLC   PASSW,FMT1    PASSWORD DATA SET ?                 *AMS*
         BE    SETSYS01      YES                                 *AMS*
         CLC   CTLG,FMT1       IS IT THE SYSCTLG DATA SET
         BE    SETSYS01      YES; HIGH PRIORITY                  82298
         CLC   =C'SYS1.VTOCIX',FMT1  VTOC INDEX ?                82298
         BNE   NOTCT01         IF NOT, DON'T FLIP BIT
SETSYS01 OI    DSLFG,DSLCAT    TURN ON CTLG BIT
NOTCT01  TM    DS1EXT1,X'86'  FIRST EXTENT ON CYL BOUND ?        81047
         BZ    NOTCYL01        IF NOT, DON'T FLIP BIT
         OI    DSLFG,DSLCYL    TURN ON CYLINDER BIT              81047
         B     NOTCYL02        GO INCR AND READ
NOTCYL01 OI    DSLFG,DSLTRK    TURN ON TRACK BIT                 81047
NOTCYL02 SUBCALL DSNCHOSE    GET DSN SELECT/ATTRIBUTE CODE       81047
           B   VTOCSKIP      SKIP THIS DATASET                   79327
         TM    OPTFLAG,OPTCHEK  PERFORM LOCAL CHECKING ?         86138
         BZ    NOTLOCO       NO                                  86138
         SUBCALL VOLCLEAN    CALL LOCAL CHECKING                 86138
         B     VTOCSKIZ      SKIP THIS DATASET                   86138
NOTLOCO  TM    MIGFLAG,OPTMIGR+OPTMIGF  MIGRATION ?              86138
         BNO   VTOCNMIG      NO                                  85280
         NI    DSLFG,255-DSLUNM  KILL UNMOVABLE                  85280
         TM    DSLFG,DSLVSAM+DSLISAM  COPYABLE ?                 85280
         BZ    VTOCNMIG      MAYBE                               85280
         LA    R1,MSGNMIG    ELSE SET FOR MESSAGE                85280
         B     VTOCSKIT      TELL USER WE SKIPPED THIS           85280
VTOCNMIG TM    DSLFG,DSLVSAM+DSLISAM+DSLUNM  SPECIAL COPIES ?    81047
         BZ    *+8           NO                                  81047
         NI    DSLFG,255-DSLPTY  TURN PRIORITY BIT OFF           81047
         TM    DSLFG,DSLCYL+DSLPTY  CYLINDER PRIORITY ?          81047
         BNO   *+12          NO                                  81047
         NI    DSLFG,255-DSLCYL-DSLTRK-DSLPTY  RESET             81047
         OI    DSLFG,DSLCYLP  SET CYLINDER PRIORITY COPY         81047
         TM    DSLFG,DSLTRK+DSLPTY  TRACK PRIORITY ?             81047
         BNO   *+12          NO                                  81047
         NI    DSLFG,255-DSLTRK-DSLPTY  RESET                    81047
         OI    DSLFG,DSLTRKP  SET TRACK PRIORITY COPY            81047
         TM    DSLFG,DSLVSAM  VSAM DATASET ?                     81047
         BZ    SETDSLFG      YES                                 81047
         CLC   DSLTTR,BLOCKF5  IN OUTPUT VTOC ?                  81047
         BNH   ADSCBNA       NO                                  81047
         CLC   DSLTTR(3),VTOCLTTR  WITHIN OUTPUT VTOC BOUNDS ?   81047
         BH    ADSCBNA       NO                                  81047
         LTR   R7,R7         FIRST VOLUME ?                      81047
         BNP   ADSNDUP       YES; NO DUPLICATION CHECK           81047
         L     R0,DSLTTR     GET DESIRED TTR                     81047
         L     R14,DSLBXLE+8  GET ADDRESS OF FIRST ENTRY         81047
         LA    R14,0(,R14)   CLEAN IT                            81047
         LA    R15,4         SET LENGTH                          81047
ADSNLUP  CR    R14,R4        REACHED CURRENT ENTRY ?             81047
         BNL   ADSNDUP       YES; OK                             81047
         CLM   R0,14,DSLTTR-DSLENT(R14)  SEE IF SAME             81047
         BE    ADSCBNA       YES; DSCB ADDRESS NOT AVAILABLE     81047
         AR    R14,R15                                           81047
         B     ADSNLUP       TRY AGAIN                           81047
ADSNDUP  MVC   DSLOTTR,DSLTTR  SET OUTPUT DSCB TTR               81047
         CLC   =C'Z9999994.VSAMDSPC',DS1DSNAM  VSAM CATALOG ?    81112
         BE    ADSCVSAC      YES                                 81112
         CLC   =C'Z9999996.VSAMDSPC',DS1DSNAM  SVS VSAM CATALOG ?
         BNE   ADSCVSAM      NO                                  81047
ADSCVSAC TM    VSAMFLAG,VSCAT  PRIOR VS CATALOG ?                81112
         BNZ   ADSCBNC       YES; TOO BAD                        81047
         OI    VSAMFLAG,VSCAT  SET FIRST ONE                     81047
         B     ADSCCOM                                           81047
ADSCVSAM TM    VSAMFLAG,VSDSN  PRIOR VSAM DSN ?                  81047
         BZ    ADSCVSAF      NO; USE IT                          81047
         CLM   R7,1,VSAMVOL  SAME AS PRIOR VOLUME ?              81047
         BE    SETDSLFG      YES; JUST SET FOR COPY              81047
         TM    VSAMFLAG,VSWARN  WARNING MESSAGE PRINTED ?        81047
         BNZ   SETDSLFG      YES; PROCESS                        81047
         OI    VSAMFLAG,VSWARN  SET WARNING ISSUED               81047
         SUBCALL MSGIN,FAILVSVL   SHOW BAD VOLUME                81208
         B     SETDSLFG                                          81047
ADSCVSAF TM    VSAMFLAG,VSCAT  PRIOR CATALOG ?                   81047
         BNZ   SETDSLFG      YES                                 81047
ADSCCOM  OI    VSAMFLAG,VSDSN  SET VSAM DSN                      81047
         STC   R7,VSAMVOL    SET VOLUME WE ARE ON                81047
         MVC   DS4AMTIM(DS4F6PTR-DS4AMTIM),DS4AMTIM-FMT4+VTOCIN  80207
         B     SETDSLFG      GO TO COMMON                        81047
ADSCBNC  OI    CCODE+1,4     SET MODERATE ERROR                  81047
         SUBCALL MSGIN,FAILVSCD   PRINT ERROR MESSAGE            81208
         XC    DSLENT,DSLENT  CLEAR ENTRY                        81047
         B     VTOCBUMP      SKIP DATASET                        81047
ADSCBNA  SUBCALL MSGIN,FAILVS  PRINT DSCB ALLOCATION FAILURE     81208
         OI    CCODE+1,8     SET BIG ERROR                       81047
SETDSLFG OC    DSLFLAG,DSLFG   SAVE BITS FOR PASS CHECK          81047
         INC   NUMVOL        COUNT DATA SETS ON THIS VOLUME     GP09205
         AR    R4,R2         GET NEXT ENTRY                      81047
VTOCBUMP TM    4(R6),X'40'   COMMAND CHAINED ?                   81047
         LA    R6,8(,R6)     BUMP ANYWAY                         81047
         BNZ   VTOCNEXT      NOT DONE YET                        81047
         B     VTOCEXCP      TRY ANOTHER BUFFER                  81047
         SPACE 1                                                 83059
VTOCDUNG ICM   R0,15,NUMVOL  HOW MANY QUEUED ?                  GP09205
         BZ    VTOCDUN0      NONE                               GP09205
         PRTDATA '---',(NUMVOL,I,PAD,DEB),'data sets queued from',(VINV*
               SER,PAD,DEB),'DD',(VINDDNM,8,DEB,PAD),'---'      GP09205
         B     VTOCDUMP                                         GP09205
VTOCDUN0 PRTDATA '*** No matching data sets found on volume',(VINVSER,P*
               AD,DEB),'DD',(VINDDNM,8,DEB,PAD),'***'           GP09205
         OI    CCODE+1,4     FLAG MINOR ERROR                   GP09205
VTOCDUMP PRTL  ' ',CC=NO     SPACE                              GP09205
         MVI   TRAKFLAG,TKQUIES  SET FOR BUFFER FREE             83059
         SUBCALL TRAKCOPY    FREE ALL BUFFERS                    83059
         LA    R7,1(,R7)     UP THE VOLUME COUNTER               83059
         CH    R7,VIDDMAX    DONE ALL ?                          81047
         BL    VTOCVOLP      NO; DO NEXT                         81047
VTOCNSCR SR    R4,R2         SET TO LAST USED ENTRY              82298
         ST    R4,DSLBXLE+4  COMPLETE BXLE VALUES                81047
         MVI   WORKFLAG,0    RESET WORK FLAG                     81047
         PRTSPACE 1          SPACE A LINE                        81208
         TM    OPTFLAG,OPTCHEK  LOCAL CHECKING ?                 86138
         BZ    SETFREE       NO                                  86138
         SERVCALL FREEM      FREE ALL STORAGE, EXCEPT @PRT/@SVC  86138
         PRTSPACE 1          SEPARATE A BIT                      86299
         B     SETFREE       NOW BUILD FMT 5 EXTENTS             81047
         SPACE 1                                                 81047
FAILVS   MSG   'UNABLE TO ALLOCATE DSCB IN OUTPUT VTOC'          81047
FAILVSCD MSG   'NOT COPIED - ONLY ONE VSAM CATALOG PER PACK'     81047
FAILVSVL MSG '** THIS AND LATER VSAM DATA MAY NOT BE USABLE UNDER VSAM'
MSGTEMP  MSG   'TEMPORARY DATASET SKIPPED'                       81047
MSGEXCL  MSG   'EXCLUDED DUE TO USER SELECTION'                  81047
MSGNMIG  MSG   'ACCESS METHOD NOT SUPPORTED BY MIGRATE'          85280
         TITLE 'C O P Y V O L  ***  FORMAT 5 AND CONTROL BLOCK BUILD'
SETFREE  L     R2,MAXFREE    GET USER'S NUMBER OF FREE EXTENTS   81047
         MH    R2,=Y(FXLEN)  SIZE OF FREE SPACE AREA             81047
         ST    R2,MAXFRLEN   SAVE FOR MVCL                       81047
         LA    R3,0(R2,R2)   GETMAIN SIZE                        81047
         GETMAIN R,LV=(R3)   GET STORAGE FOR EXTENT DESCRIPTORS  81047
         LA    R8,FXLEN(,R1)  SAVE ADDRESS OF FIRST FREE ENTRY   81047
         ST    R8,FPFMT5AD   SET IT                              81047
         CLRL  (R1),(R3),WORK1=14,WORK2=4  CLEAR GOTTEN STORAGE  83094
         ST    R1,PFMT5ONE   SAVE AREA ADDRESS                   81047
         ST    R1,PFMT5AD    AND FIRST POINTER                   81047
         AR    R2,R1                                             81047
         ST    R2,PFMT5SAD   SET SAVE AREA ADDRESS               81047
         L     R6,MAXFREE                                        81047
         BCTR  R6,0          NUMBER OF AVAILABLE FREE ELEMENTS   81047
         LTR   R6,R6         VALID ?                             81047
         BP    FRSETUP       BUILD CHAIN                         81047
         SUBCALL DUMPMSG,XCESSF5  TOO MANY FORMAT 5 ENTRIES      81208
XCESSF5  VCON  '0MORE THAN MAXFREE FMT5 ENTRIES REQUIRED - CALL SYSTEMS*
               .'                                                81208
         SPACE 1                                                 81047
         USING FXMAP,R8                                          81047
FRSETUP  LA    R9,FXLEN(,R8) GET NEXT ELEMENT                    81047
         ST    R9,FXLINK     FORWARD CHAIN                       81047
         LR    R8,R9                                             81047
         BCT   R6,FRSETUP    DO ALL                              81047
         SH    R8,=Y(FXLEN)  BACKSPACE                           81047
         ST    R6,FXLINK     KILL CHAIN ON LAST ONE              81047
         DROP  R8                                                81047
         USING FXMAP,R1      MAP FIRST ELEMENT                   81047
         MVC   FXCYL,#TRKS   SET ENTIRE PACK FREE                81047
         DROP  R1                                                81047
         XC    NEWEXTS(2*XTLEN),NEWEXTS  CLEAR                   81047
         MVI   DS1NOEPV,1    FAKE ONE EXTENT                     81047
         MVI   NEWEXTS,XTTTRK  SET AS TRACK DATA                 81047
         LA    R4,=A(DSLUNM,8)  FAKE UNMOVABLE DATASET           81047
         SUBCALL ABSALLOC    ALLOCATE IPL TRACK                  81047
           B   NALIPLT       UNABLE TO ALLOCATE IPL TRACK        81047
         MVC   NEWEXTS(XTLEN),DS4VTOCE  MOVE VTOC EXTENT         81047
         SUBCALL ABSALLOC    ALLOCATE IT                         81047
         B     NALVTOC       VTOC NOT ALLOCATED                  81047
         TM    OPTFLAG2,OPTCLEAR  CLEARVOL ENTRY ?               82298
         BNZ   SPACPREP      YES; SKIP SUBHEAD                   82298
         TM    OPTFLAG,OPTERSE  TERSE MODE ?                     81047
         BZ    SPCSUBHD      NO; EJECT                           81208
         TM    OPTFLAG,OPTLOTS   LOTS OF MESSAGES ?              81208
         BNZ   SPCSUBHD      YES; FORCE EJECT                    81208
         MVI   SUBPREAD+4,C'-'  SET FOR DELAYED SUB-TITLE        81208
         PRTSPACE 1          AND SPACE NEXT ENTRY                86292
SPCSUBHD PRTV  SUBPREAD,TITLE=3    PAGE EJECT OR DELAYED TITLE   81208
         B     SPACPREP      OK; SPACE PREPARED                  81047
         SPACE 1                                                 81047
NALVTOC  LA    R1,NALVTOCM   GET VTOC ON TRK0                    81047
         CLC   ZEROES(4),DS4VTOCE+2  ON TRACK ZERO ?             81047
         BE    NALIPLX       YES; NASTY MESSAGE                  81047
NALIPLT  LA    R1,NALIPLTM                                       81047
NALIPLX  SUBCALL DUMPMSG                                         81047
NALVTOCM VCON  '0VTOC ON TRACK 0, CYLINDER 0 NOT SUPPORTED'      81208
NALIPLTM VCON  '0PROGRAM ERROR - UNABLE TO ALLOCATE IPL TRACK OR VTOC'
SUBPREAD VCON  '1VOLUME DATASET                                        *
               STATUS                                         EXT  TRKS*
                ORG RECFM   LEN BLKSZ'                           85288
         SPACE 1                                                 81047
         LTORG ,                                                 81047
         TITLE 'C O P Y V O L  ***  COPY OF VSAM, ISAM AND UNMOVABLE'
SPACPREP BALR  R10,0         MAKE NEW BASE                       81047
         USING *,R10                                             81047
         MVC   BLOCKAD,BLOCKF5  COPY FMT 5 ADDRESS               81047
         SUBCALL NEXTBLOK    GET FIRST AVAILABLE ADDRESS         81047
         SPACE 1                                                 81047
         SLR   R6,R6           CLEAR REG 6                       81047
         LA    R5,3            GO THROUGH 3 TIMES                81047
         TM    DSLFLAG,DSLVSAM  ANY VSAM DATA ?                  81047
         BNZ   AGETDSVS      SKIP FIRST TEST                     81047
         B     ABSLOOP       NO; SKIP ENTIRE GROUP               81047
AGETDS   LA    R4,DSLFLAG-(DSLFG-DSLIST)                         81047
         USING DSLIST,R4                                         81047
         EX    0,ATEST(R6)   IS THIS PASS NECESSARY ?            81047
         BZ    ABSLOOP       NO; SKIP IT                         81047
AGETDSVS LM    R2,R4,DSLBXLE  POINT TO DSLST                     81047
         LA    R4,0(,R4)     CLEAR FLAG BYTE                     81047
         CR    R4,R2         ANY DATASETS AT ALL ?               82298
         BL    PDONE         NO; TAKE FAST EXIT                  82298
AGETLOOP EX    0,ATEST(R6)   MAKE THE TEST                       81047
         BZ    ABSINCR       BRANCH TO LOOK AT NEXT DS           81294
         LTR   R6,R6         FIRST PASS ?                        81047
         BP    AGETDSCB      NO                                  81047
         TM    DSLFG,DSLVSAM  CHECK OTTR FOR VSAM                81047
         BZ    ABSINCR       NOT VSAM; DO LATER                  81047
AGETDSCB SUBCALL GETDSCBS    READ AND CHECK DSCBS                81047
         B     ABSINCR       SKIP OR PROCESS LATER               81047
         LA    R14,OLDEXTS   GET EXTENT ADDRESS                  81047
         LA    R15,16        16 MAX EXTENTS                      81047
         USING EXTMAP,R14    DECLARE MAPPING                     81047
ABSTST1  CLC   XTWHOLE,ZEROES   ARE WE FINISHED                  81047
         BE    ABSMOVE       IF SO, DON'T DO IT AGAIN            81047
         CLI   XTTYPE,XTTSPLIT  SPLIT CYLINDER REQUEST ?         81047
         BE    ABSPLIT       YES; MESSAGE AND DEFER              81047
         LA    R14,XTLEN(,R14)  GET NEXT EXTENT                  81047
         BCT   R15,ABSTST1   KEEP IT UP                          81047
         DROP  R14                                               81047
         SPACE 1                                                 81047
ABSMOVE  LA    R1,16                                             81047
         SR    R1,R15        GET NUMBER OF EXTENTS               81047
         STC   R1,DS1NOEPV   MAKE SURE                           81047
         LA    R0,NEWEXTS                                        81047
         ST    R0,AMSOUTXA   SET ADDRESS                         81047
         STC   R1,AMSOUTX#   SET NUMBER OF EXTENTS               81047
         MVC   NEWEXTS(16*XTLEN),OLDEXTS    MOVE ALL EXTENTS     81047
         TM    MIGFLAG,OPTMIGR+OPTMIGF  MIGRATION ?              85280
         BNO   ABSNMIG       NO                                  85280
         MVI   DSLFG,DSLCYLP  TRY TO ALLOCATE ON CYLINDER BOUNDS 85280
         SUBCALL AMSSIZER    GET DATASET SIZE ROUTINE            85280
         SUBCALL RELTEST     PERFORM TENTATIVE ALLOCATION        85280
         B     ABSAFAIL      DEFER OR SKIP THIS ONE              85280
         L     R2,NUMDSN     SAVE CURRENT COPY COUNT             91167
         SUBCALL PUTDS       WRITE THE DATA SET                  85280
         C     R2,NUMDSN     WAS THIS COPIED ?                   91167
         BE    ABSOUT2       FAILED - NO ALLOCATION              91167
         SUBCALL RELALLOC    NOW DO PERMANENT ALLOCATION         85280
         B     ABSOUT2       SKIP ABSOLUTE ALLOCATION            85280
ABSNMIG  NI    DSLF2,255-DSLPSREL  DISABLE TRACK RELEASE         85280
         SUBCALL AMSSIZER    GET DATASET SIZE                    81047
         SUBCALL ABSALLOC    ALLOCATE SPACE                      81047
         B     ABSAFAIL      FAILED; SEE WHAT TO DO ABOUT IT     81047
         SUBCALL PUTDS       GO WRITE DSCBS                      81047
ABSOUT2  MVI   DSLFG,0         CLEAR LIST                        81047
         LH    R1,CCODE      GET CURRENT CONDITION CODE          83294
         C     R1,STOPCODE   REACHED STOP CODE ?                 83294
         BNL   PDONE         YES; QUIT                           83294
ABSINCR  BXLE  R4,R2,AGETLOOP  GO LOOK FOR NEXT                  81047
         SPACE 1                                                 81047
ABSLOOP  LA    R6,ATEST2-ATEST(,R6)  SET FOR NEXT TEST           81047
         BCT   R5,AGETDS     KEEP GOING                          81047
         B     MOVERELA      GO TO COPY MOVABLE DATASETS         81047
         SPACE 1                                                 81047
ATEST    TM    DSLOTTR+2,X'FF'  ABSOLUTE TTR ALLOCATION ?        81047
ATEST2   TM    DSLFG,DSLVSAM+DSLISAM  VSAM OR ISAM ?             81047
         TM    DSLFG,DSLUNM  OTHER UNMOVABLE ?                   81047
         SPACE 1                                                 81047
ABSPLIT  SUBCALL MSGIN,ABSMSGD    SET FOR SPLIT MESSAGE          81208
         OI    CCODE+1,2     SET LEVEL 2 ERROR                   81047
         NI    DSLFG,255-DSLCYL-DSLCYLP-DSLUNM-DSLTRK            81047
         OI    DSLFG,DSLTRKP  SET TRACK PRIORITY                 81047
         OI    DSLFLAG,DSLTRKP                                   81047
         B     ABSINCR       SKIP THIS ONE FOR NOW               81047
         SPACE 1                                                 81047
ABSAFAIL TM    DSLFG,DSLVSAM+DSLISAM   REALLY UNMOVABLE ?        81047
         BNZ   ABSTFAIL      YES; DON'T MOVE IT                  81047
         LA    R1,ABSMMSG    SET FOR 'MOVED' MESSAGE             81047
         OI    CCODE+1,2     SET MINOR ERROR                     81047
         OI    DSLF2,DSLKEEP  KEEP EVERYTHING                    81047
         NI    DSLF2,255-DSLPSREL  INHIBIT RELEASE               81047
         TM    DSLFG,DSLCYL+DSLCYLP  CYLINDER BOUNDS ?           81047
         BZ    ABSTFTRK      NO; USE TRACK                       81047
         MVI   DSLFG,DSLCYLP  COPY AS CYLINDER PRIORITY          81047
         OI    DSLFLAG,DSLCYLP  SET EXTRA PASS                   81047
         B     ABSTFMSG      GO TO MESSAGE                       81047
ABSTFTRK MVI   DSLFG,DSLTRKP  SET TRACK PRIORITY                 81047
         OI    DSLFLAG,DSLTRKP  SET EXTRA PASS                   81047
         B     ABSTFMSG      PRINT MESSAGE                       81047
ABSTFAIL LA    R1,ABSUMSG    NOT MOVED/COPIED                    81047
         MVI   DSLFG,0       DELETE IT                           81047
         OI    CCODE+1,8      SET LEVEL 8 ERROR                  79105
         INC   NUMNPRO       UP COUNT OF UNPROCESSED DSNS        81208
ABSTFMSG SUBCALL MSGIN       ISSUE ERROR MESSAGE                 81047
         B     ABSINCR       TRY ANOTHER                         81047
         SPACE 1                                                 81047
ABSMMSG  MSG   'ABSTRK NOT FREE; WILL BE MOVED'                  81047
ABSUMSG  MSG   'ABSTRK NOT FREE; NOT COPIED'                     81047
ABSMSGD  MSG   'SPLIT CYLINDER - WILL BE COPIED TRACK ALIGNED'   81047
         SPACE 2                                                 81047
         LTORG ,                                                 81047
         TITLE 'C O P Y V O L  ***  DATASET COPY SELECTION'      81047
MOVERELA BALR  R10,0         UPDATE THE BASE                     81047
         USING *,R10                                             81047
         SLR   R6,R6           CLEAR REG 6
         LA    R5,5            GO THROUGH 5 TIMES                79105
GETDS    LA    R4,DSLFLAG-(DSLFG-DSLIST)                         79105
         USING DSLIST,R4                                         81047
         EX    0,TEST(R6)     IS THIS PASS NECESSARY ?           79105
         BNO   THISDONE       NO; SKIP IT                        79105
         LM    R2,R4,DSLBXLE  POINT TO DSLST                     81047
         LA    R4,0(,R4)     CLEAR FLAG BYTE                     81047
GETDLOOP EX    0,TEST(R6)      MAKE THE TEST                     81047
         BNO   INC2            BRANCH TO LOOK AT NEXT DS         79105
         SUBCALL GETDSCBS    GO READ THE VTOC                    81047
         B     INC2          SKIP OR COPY LATER                  81047
         SUBCALL AMSSIZER    GET DATASET SIZE ROUTINE            81047
         SUBCALL RELTEST     PERFORM TENTATIVE ALLOCATION        81047
         B     INC2          DEFER OR SKIP THIS ONE              81047
         L     R9,NUMDSN     SAVE CURRENT COPY COUNT             91167
         SUBCALL PUTDS       WRITE THE DATA SET                  81047
         C     R9,NUMDSN     WAS THIS COPIED ?                   91167
         BE    INC2DSK       FAILED - NO ALLOCATION              91167
         SUBCALL RELALLOC    NOW DO PERMANENT ALLOCATION         81047
INC2DSK  MVI   DSLFG,0         CLEAR FLAG BYTE
         LH    R1,CCODE      GET CURRENT CONDITION CODE          83294
         C     R1,STOPCODE   REACHED STOP CODE ?                 83294
         BNL   PDONE         YES; QUIT                           83294
INC2     BXLE  R4,R2,GETDLOOP  LOOK FOR NEXT DATA SET            81047
         SPACE 1                                                 81047
THISDONE LA    R6,TEST2-TEST(,R6)        INCR REG 6              81047
         BCT   R5,GETDS         KEEP GOING
         B     PDONE           WE'RE FINISHED COPYING DATA SETS
         SPACE 1                                                 81047
TEST     TM    DSLFG,DSLCAT    IS THIS THE SYSCTLG DATA SET
TEST2    TM    DSLFG,DSLCYLP  CYL PRIORITY ?                     81047
         TM    DSLFG,DSLCYL    IS THIS CYLINDER ALLOCATION
         TM    DSLFG,DSLTRKP  TRK PRIORITY ?                     81047
         TM    DSLFG,DSLTRK     HAS THIS BEEN COPIED             79105
         TITLE 'C O P Y V O L  ***  FINAL RITES - FMT 5, FMT 4, FMT 0'
PDONE    L     R8,PFMT5AD      GET FIRST FORMAT 5 POINTER        79105
         MVC   PREBLOCK,BLOCKF5  SET FIRST FORMAT 5 ADDRESS      81047
         SUBCALL BUILDF5     FORMAT ONE FORMAT 5 DSCB            81047
         LR    R8,R15                                            81047
         LM    R4,R5,PREBLOCK  GET WRITE/NEXT BLOCK ADDRESS      81047
         ST    R4,BLOCKAD    WRITE IT AFTER F4                   79105
         MVC   DSLCURR,=A(DSLUNM,9)  SET FOR VTOC I/O            85280
         WRTVTOC FMT5        WRITE AND TEST                      81047
         ST    R5,BLOCKAD    RESTORE GOOD BLOCK ADDRESS          81047
         CLI   OUTER,0       ANY ERROR ?                         79105
         BNE   P5WERR        YES; SAY SO                         79105
P5MORE   LTR   R8,R8         ANY MORE TO BE DONE ?               79105
         BZ    P5DONE        NO; QUIT                            79105
         SUBCALL NEXTBLOK    ASSIGN ANOTHER BLOCK                81047
         LM    R4,R5,PREBLOCK                                    81047
         ST    R4,BLOCKAD                                        81047
         SUBCALL BUILDF5     BUILD ANOTHER F5                    81047
         LR    R8,R15                                            81047
         WRTVTOC FMT5        WRITE IT OUT                        81047
         ST    R5,BLOCKAD    RESTORE NEXT BLOCK ADDRESS          81047
         CLI   OUTER,0       GOOD WRITE ?                        79105
         BE    P5MORE        YES; CHECK FOR MORE                 79105
P5WERR   OI    DS4VTOCI,DS4DOSBT  SHOW BAD F5S                   83094
P5DONE   CLI   DS4VTOCI,0    ANY F5 ERRORS ?                     79105
         BE    P4DONE        NO; REWRITE FORMAT 4                79105
         OI    CCODE+1,1     SET ERROR                           79105
         PRTV  ERRORF5       WRITE ERROR MESSAGE                 81208
P4DONE   MVC   DS4HPCHR(5),FMT1HWM  SET FMT1 HIGH-WATER MARK
         TM    OPTFLAG2,OPTCLEAR   CLEARVOL ENTRY ?              82298
         BNZ   NOMORESV      YES; SKIP HOLE WRITES               82298
         MVI   OUTER,0        CLEAR ERROR
         SLR   R8,R8           CLEAR 8
         XC    FMT2(140),FMT2  WIPE OUT BIG AREA
WRT0     WRTVTOC FMT2        WRITE A FMT0                        81047
         CLI   OUTER,0        SEE IF GOOD
         BNE   NOMORE          IF NOT, THEN WE RAN OUT OF ROOM
         LA    R8,1(,R8)        ONE MORE GOOD FMT 0
COUNTHOL CLC   BLOCKAD(3),VTOCLTTR  NEXT ADDRESS IN VTOC ?       81047
         BH    NOMORE        NO; DONE                            81047
         SUBCALL NEXTBLOK    GET NEXT FREE BLOCK ADDRESS         81047
         LA    R8,1(,R8)     UP HOLE COUNT                       81047
         B     COUNTHOL        GO TRY AGAIN                      81047
         SPACE 1                                                 81047
ERRORF5  VCON  '0***** ERROR WRITING FREE SPACE POINTERS *****'  81208
         SPACE 2                                                 81047
NOMORE   STH   R8,DS4DSREC      SAVE # OF AVAIL DSCB'S           81047
NOMORESV CLC   NUMDVSAM,ZEROES  ANY VSAM DATASETS COPIED ?       82298
         BNE   *+10          YES                                 81047
         XC    DS4AMTIM(DS4F6PTR-DS4AMTIM),DS4AMTIM  CLEAR VSAM  81047
         LA    R0,8          SET EXTENT FOR FORMAT 4             81047
         ST    R0,BLOCKAD    REWRITE FORMAT4                     81047
         WRTVTOC FMT4          WRITE IT OUT
         MVI   TRAKFLAG,TKQUIES  WAIT FOR WRITE TO FINISH        81047
         SUBCALL TRAKCOPY                                        81047
         TITLE 'C O P Y V O L  ***  IPL TRACK COPY AND TERMINATION'
*        AMS POST-PROCESSING ROUTINE                             *AMS*
*                                                                79105
*        1) IF PARM=IPLT/TRK0 CHOSEN, THE IPL TRACK IS COPIED    79105
*        2) A CONSOLE MESSAGE INDICATING COMPLETION CODE IS ISSUED*AMS*
*                                                                *AMS*
         BALR  R10,0                                             81047
         USING *,R10                                             *AMS*
         LH     R1,CCODE     GET CURRENT CONDITION CODE          83294
         C      R1,STOPCODE  REACHED STOP CODE ?                 83294
         BNL    TERMRUN      YES; TERMINATE RUN                  83294
         ICM    R1,15,NUMDSN  HOW MANY COPIED ?                  83294
         BNZ    IPLSWTST     SOME; PROCEED                       83294
         L      R1,NUMSKP    GET NUMBER SKIPPED                  83294
         A      R1,NUMNPRO     PLUS UNPROCESSABLE ONES           83294
         BNZ    IPLSWTST     OK ?                                83294
         PRTV   CCMNONE      SHOW NOTHING COPIED                 83294
         OI     CCODE+1,4    LEVEL 4 ERROR                       83294
         LH     R1,CCODE     CHECK AGAIN                         83294
         C      R1,STOPCODE  REACHED LIMIT                       83294
         BL     IPLSWTST     NO; PROCEED                         83294
TERMRUN  MVI    DUMPCODE+3,8  SET USER ABEND 8                   83294
         SUBCALL DUMPMSG,CCMSTOP  SHOW STOP DUE TO CODE          83294
IPLSWTST TM    OPTFLAG3,OPTIPL  SWAP OR IPL COPY ?              GP13433
         BZ    SWITCHEX      NO; ISSUE TERMINAL MESSAGE AND EXIT 79105
         MODESET KEY=ZERO     CHEAT                              79095
         L     R3,GMDEBAD    GET DEB ADRESS                      81047
         MVI   DEBBASND-DEBBASIC(R3),X'C0'  ALLOW ALL WRITES     81047
         TM    OPTFLAG3,OPTIPL  IPL TRACK COPY OPTION ?          79105
         BZ    SWITCHN1       NO                                 79105
         MVC   VINDDNM(VIN2DDNM-VIN1DDNM),VIN1DDNM               85296
         LA    R4,=A(DSLUNM,0)  POINT TO UNMOVABLE-3             81047
         MVC   DSLCURR,0(R4)  STASH FOR TRACK COPY               85280
         NI    OPTFLAG,255-OPTERSE  RESET FLAGS                  79105
         NI    OPTFLAG3,255-OPTIPL   RESET FLAGS                 79105
         XC    OLDEXTS(2*XTLEN),OLDEXTS  CLEAR EXTENTS           81047
         XC    NEWEXTS(2*XTLEN),NEWEXTS  DITTO                   81047
         LA    R0,1          SET FOR ONE TRACK                   81047
         ST    R0,TRKUSD      TO BE COPIED                       91119
         MVC   DS1DSNAM(14),=C'    IPL TRACK '  MAKE MESSAGE     79105
         MVC   DS1DSNAM+14(30),DS1DSNAM+13  CLEAR REST           79105
         MVC   INJFCB(44),FMT1  MOVE DATA SET NAME               81047
         MVI   TRAKFLAG,TKIPL  SET IPL TRACK COPY                81047
         SUBCALL TRAKCOPY    COPY ONE TRACK                      81047
         SUBCALL MSGIN,IPLCOMSG  SHOW IPL TRACK COPIED           81208
SWITCHN1 MODESET KEY=NZERO                                       79095
SWITCHEX ICM   R6,15,DSNAMES    POINT TO SELECTION LIST         GP09205
         BZ    CCSHOW                                           GP09205
         PRTL  ' ',CC=NO     SPACE                              GP09205
         PUSH  USING                                            GP09205
         USING RIXLIST,R6                                       GP09205
MATLOOP  CLI   RIXFG,0       DONE ?                             GP09205
         BE    CCSHOW                                           GP09205
         SR    R2,R2                                            GP09205
         IC    R2,RIXL1      GET MASK LENGTH                    GP09205
         TM    RIXFG,RIXINC+RIXEXC  INCLUDE/EXCLUDE SELECT?     GP09205
         BZ    MATBUMP       NO                                 GP09205
         TM    RIXF2,RIXMATCH   MATCHED?                        GP09205
         BNZ   MATBUMP                                          GP09205
         PRTDATA 'No match found for',(RIXMASK,(R2),PAD)        GP09205
         OI    CCODE+1,4     SET MODERATE ERROR                 GP09205
MATBUMP  SR    R14,R14                                          GP09205
         IC    R14,RIXL2     GET REPLACMENT LENGTH              GP09205
         AR    R14,R2        TOTAL VARIABLE LENGTH              GP09205
         LA    R6,RIXFIX(R14,R6)                                GP09205
         B     MATLOOP       TRY AGAIN                          GP09205
         POP   USING                                            GP09205
CCSHOW   MVC   SWITCHEY+40(3),=X'202120'  MAKE EDIT MASK         79327
         LH    R15,CCODE      GET COMPLETION CODE                79105
         CVD   R15,DB        CONVERT                             79105
         ED    SWITCHEY+39(4),DB+6  EDIT CODE                    79327
         MVC   SWITCHEY+9(6),VOUTVSER    IDENTIFY OUTPUT VOLUME  81047
SWITCHEY WTO   ' YYYYYY COPYFDR COMPLETE - CODE  16',ROUTCDE=(1,11)
         SPACE 1                                                 82298
SWITCHEZ MODESET MODE=SUP,KEY=ZERO                               82298
         DEBCHK INOUTDCB,AM=EXCP,TYPE=DELETE  FREE DEBCHECK ENTRY
         MODESET MODE=PROB                                       81047
         READFDR CLOSE       FREE TAPE BUFFERS, ETC.            GP13433
         ICM   R1,7,GMDEBAD+1    GET DEB ADDRESS                 81047
         BZ    IOCLDEBD      NONE GOTTEN; NOT ON CHAIN           81047
         ICM   R1,7,1(R1)    GET TCB ADDRESS                     81047
         BZ    IOCLDEBD      NONE GOTTEN; NOT ON CHAIN           81047
         LA    R1,4(,R1)     GET TCBDEB-4                        81047
IOCLDEBL CLC   5(3,R1),GMDEBAD+1    POINTS TO OUR DEB ?          81047
         BE    IOCLDEBX      YES, UNCHAIN IT                     81047
         LAT   R1,4(R1),IOCLDEBD PREVENT LOOP                    81047
         B     IOCLDEBL       AND TRY AGAIN                      81047
IOCLDEBX L     R15,4(,R1)    GET ADDR. OF MY DEB                 81047
         MVC   5(3,R1),5(R15)    UNCHAIN IT                      81047
IOCLDEBD LM    R0,R1,GMDEBL  GET DEB/LENGTH                      81047
         LTR   R1,R1         WAS ANY ?                           81047
         BZ    IOCLDONE      NO, EXIT                            81047
         FREEMAIN R,LV=(0),A=(1)   FREE THE DEB                  81047
IOCLDONE MODESET KEY=NZERO                                       81047
         SPACE 1                                                 81047
         DEQ   MF=(E,ENQLIST)  DEQ THE VTOCS                     81047
         L     R0,LSPUCB     GET OUTPUT UCB ADDRESS BACK         79327
         LA    R1,LSPOUT     GET LSPACE OUTPUT                   79327
         SVC   78            DO LSPACE                           79327
         PRTROOM 10          PUT TRAILING MESSAGES ON ONE PAGE   81313
GETNSCR  PRTLIST VCOPIED     FORMAT STATISTICS                   82298
         PRTV  LSPACE        WRITE MESSAGE                       81208
         PRTV  SWITCHEY+4    WRITE ENDING MESSAGE                81208
GETOUT   SERVTERM ,          CLOSE AND FREEMAIN SERVICES         81208
         TM    OPTFLAG2,OPTDEBUG   DEBUG MODE ?                  81047
         BZ    GETOUTCC      NO                                  81047
         ABEND 666,DUMP      BEASTLY                             81047
GETOUTCC LH    R9,CCODE      LOAD RETURN CODE                    81208
         PGMEXIT RC=(R9)     RETURN WITH APROPRIATE CODE        GP09297
         SPACE 1                                                 79119
VCOPIED  FDOPT NL,CC=C'0'    DOUBLE-SPACE                        81313
         FD    NUMDSN,I,RADJ,LEN=7                               81313
         FD    'DATASETS COPIED',PAD                             81313
         FD    NUMSKP,I,RADJ,LEN=7                               81313
         FD    'SKIPPED',PAD                                     81313
         FD    NUMNPRO,I,RADJ,LEN=7                              81313
         FD    'FAILED; NOT COPIED',PADL                         81313
*                                                                81313
         FD    NUMDVSAM,I,NL,RADJ,LEN=7                          81313
         FD    'VSAM',PAD,LEN=24                                 81313
         FD    NUMDIS,I,RADJ,LEN=7                               81313
         FD    'ISAM',PAD,LEN=24                                 81313
         FD    NUMDU,I,RADJ,LEN=7                                81313
         FD    'UNMOVABLE',PAD,LEN=24                            81313
         FD    NUMDDA,I,RADJ,LEN=7                               81313
         FD    'DIRECT ACCESS',PADL,LEN=24                       81313
*                                                                81313
         FD    NUMDPS,I,NL,RADJ,LEN=7                            81313
         FD    'SEQUENTIAL',PAD,LEN=24                           81313
         FD    NUMDPO,I,RADJ,LEN=7                               81313
         FD    'PO - COMPRESSED',PAD,LEN=24                      81313
         FD    NUMDPOB,I,RADJ,LEN=7                              81313
         FD    'PO - COPIED AS IS',PAD,LEN=24                    81313
         FD    NUMDOTH,I,RADJ,LEN=7                              81313
         FD    'OTHER',PADL,LEN=24                               81313
*                                                                87135
         FD    NUMEXCP,I,NL,RADJ,LEN=7                           87135
         FD    'I/OS ISSUED,',PAD                                87135
         FDPRT NUMRMOK,I,PAD                                     87135
         FDPRT 'READ MULTIPLES COMPLETED,',PAD                   87135
         FDPRT NUMRMBAD,I,PAD                                    87135
         FDPRT 'RESTARTED'                                       87135
*                                                                87135
         FDTM  OPTFLAG,OPTSTACK,BZ=VCOPCOM  VTOC STACKING ?      87135
         FD    NUMSTACK,I,RADJ,NL,LEN=7                          87135
         FD    'VTOC BUFFERS STACKED,',PAD                       87135
         FD    NUMSTHIT,I,PAD                                    87135
         FD    'DSCB I/OS BYPASSED'                              87135
*                                                                81313
VCOPCOM  FDOPT NL,CC=C'0'                                        82298
         FD    DS4DSREC,I,RADJ,LEN=7                             81313
         FD    'FREE DSCBS;',PAD                                 81313
         FD    DS4NOATK,I,RADJ,LEN=7                             81313
         FD    'FREE ALTERNATE TRACKS STARTING AT',PAD           81313
         FD    DS4HCCHH,2,HEX                                    81313
         FD    '.'                                               81313
         FD    DS4HCCHH+2,2,HEX                                  81313
         FD    *END                                              81313
         SPACE 1                                                 82298
IPLCOMSG MSG   ' COPIED'     (ONLY 1 BLANK TO SKIP DCB/EXT INFO) 85296
CCMSTOP  VCON  '0*** EXECUTION TERMINATED; COND CODE REACHED STOP CODE *
                ***'                                             83294
CCMNONE  VCON  '0>>>   WARNING - NO DATASETS COPIED   <<<'       83294
         SPACE 2                                                 81047
         LTORG ,                                                 81047
         TITLE 'C O P Y V O L  ***  FMT 1, 2, 3 ACQUISITION'     81047
*        GETDSCBS SUBROUTINE - READ DSCBS AND PREPARE EXTENTS    81047
GETDSCBS SUBENT ,                                                81047
         USING DSLIST,R4                                         81047
         MVC   FULL(3),DSLTTR  MOVE DSCB TTR
         MVC   FULL+3(1),DSLF2  ADD DD INDEX                     81047
         NI    FULL+3,7      CHANGE FLAG TO EXTENT BYTE          81047
         SLR   R1,R1                                             81047
         IC    R1,FULL+3     GET DD INDEX                        81047
         MH    R1,=Y(VIN2DDNM-VIN1DDNM)  GET OFFSET              81047
         LA    R1,VIN1DDNM(R1)  GET CURRENT ENTRY                81047
         MVC   VINDDNM(VIN2DDNM-VIN1DDNM),0(R1)  OVERLAY WORK ENTRY
         XC    IECSDSL3(DS3END-IECSDSL3),IECSDSL3   CLEAR FMT 3  85289
         MVI   DS3FMTID,C'3' BUILD FORMAT 3 FOR POSSIBLE EXPANSION
         MVC   IECSDSL3(4),=4X'03'  MAKE KEY                     85289
         READVTOC FMT1         READ THE DSCB                     81047
         CLI   DS1FMTID,C'1' FORMAT 1 DSCB REALLY READ ?         83094
         BE    GETDSCB1      YES; PROCESS                        83094
         PRTLIST GETD1MSG    MAKE ERROR MESSAGE                  83094
         B     GETDBADC      GO TO COMMON ERROR                  83094
GETD1MSG FDPRT '***** ERROR REREADING FORMAT 1 AT',NL            83094
         FDPRT DSLTTR,3,HEX,PAD                                  83094
         FDPRT 'ON'                                              83094
         FDPRT VINVSER,PAD                                       83094
         FDPRT *END                                              83094
         SPACE 1                                                 83094
GETDSCB1 MVC   INJFCB(44),FMT1  SAVE OLD NAME FOR IEBCOPY        81047
         MVC   DSNAME,FMT1        SAVE DS1DSNAM                 GP09209
         MVC   DSLCURR,DSLIST  SAVE CURRENT ENTRY                81047
         CLC   DS1DSSN(6),VINVSER     ALLOCATION ON SOURCE VOL ? 81047
         BNE   GETDSCBQ      NO                                  81047
         TM    OPTFLAG3,OPTSWAP  RUNNING WITH SWAP OPTION ?      81047
         BNZ   GETDSCBQ      YES; LEAVE IT ALONE                 81047
         MVC   DS1DSSN(6),VOUTVSER   UPDATE VOLSER               81047
GETDSCBQ XC    OLDEXTS+3*XTLEN(13*XTLEN),OLDEXTS+3*XTLEN  CLEAR  81047
         MVC   OLDEXTS(3*XTLEN),DS1EXT1  COPY FIRST EXTENT GROUP 81047
         LA    R3,DS1PTRDS     ADDRESS FOR FMT 3 DSCB
         TM    DSLFG,DSLISAM   SEE IF ISAM                       81047
         BZ    GETDNISM        IF NOT, THEN FORGET IT            81047
         BAL   R9,CHKPTRDS   ANY EXTENSION ?                     83094
         B     GETDBADI      NO; INVALID ISAM DATASET            83094
         STCM  R0,14,FULL    SAVE TTR, LEAVE OLD Z               81047
         READVTOC FMT2         READ THE FORMAT2 DSCB
         LA    R3,DS2PTRDS     POINT TO FMT 3 DSCB
         CLI   DS2FMTID,C'2'   REALLY A FORMAT 2 ?
         BE    GETDNISM                                          81047
GETDBADI SUBCALL MSGIN,GETDIMSG   PRINT BAD ISAM MSG             81208
GETDBADC OI    CCODE+1,4     SET MODERATE ERROR                  83094
         MVI   DSLFG,0                                           81047
         INC   NUMNPRO       UP COUNT OF UNPROCESSED DSNS        81208
         SUBEX ,             RETURN WITH ERROR                   81047
GETDIMSG MSG   'DSORG=IS, BUT NO DSCB2 FOUND'                    81047
         SPACE 1                                                 81047
GETDNISM BAL   R9,CHKPTRDS   IS THERE A FMT 3 ?                  83094
         B     GETDSCBX        IF NONE, GO BACK                  83094
         STCM  R0,14,FULL    SAVE TTR; LEAVE Z                   81047
         READVTOC FMT3         READ THE DSCB
         LA    R3,DS3PTRDS   POINT TO EXTENSION ADDRESS
         CLI   DS3FMTID,C'3'   REALLY A FORMAT 3 ?
         BNE   GETDNISM      NO; TRY AGAIN                       81047
         MVC   OLDEXTS+3*XTLEN(4*XTLEN),DS3EXTNT  MOVE SECOND    81047
         MVC   OLDEXTS+7*XTLEN(9*XTLEN),DS3ADEXT  LAST GROUP     81047
         MVI   DS1NOEPV,16   FORCE CHECKING OF ALL EXTENTS       83135
         SPACE 2                                                 81047
GETDSCBX SLR   R5,R5                                             *AMS*
         IC    R5,DS1NOEPV    GET NUMBER OF EXTENTS TO CHECK     *AMS*
         CLI   DS1EXT1+XTTYPE-EXTMAP,XTTLABEL  LABEL EXTENT ?    83135
         BNE   *+8           NO                                  83135
         LA    R5,1(,R5)     ALLOW FOR LABEL EXTENT              83135
         LA    R6,16         SET FOR MAXIMUM EXTENT COUNT        83135
         CR    R5,R6         OVER MAX ?                          83135
         BNH   *+6           NO                                  83135
         LR    R5,R6         DO ONLY MAXIMUM                     83135
         LTR   R5,R5         ANY AT ALL ?                        *AMS*
         BNP   GETDCHOK      NO; TAKE GOOD RETURN                *AMS*
         SLR   R6,R6         CLEAR EXTENT COUNTER                81047
         LA    R8,OLDEXTS    GET EXTENT DESCRIPTORS              81047
         USING EXTMAP,R8                                         81047
GETDCHLP CLI   XTTYPE,0      END EXTENT ?                        81047
         BE    GETDCHDN      YES; DONE                           81047
         CLC   XTLOCYL(4),XTHICYL VALID RANGE ?                  81047
         BH    GETDCHFL      NO; FAIL                            *AMS*
         OC    XTLOCYL(4),XTLOCYL  ZERO ?                        81047
         BZ    GETDCHFL      TOO BAD                             *AMS*
         CLC   XTLOCYL,VINCYL  VALID CYLINDER NUMBER ?           81047
         BNL   GETDCHFL      NO                                  *AMS*
         CLC   XTHICYL,VINCYL  VALID CYL ?                       81047
         BNL   GETDCHFL      NO                                  *AMS*
         CLC   XTLOTRK,VINTPC   VALID TRACK NUMBER ?             85280
         BNL   GETDCHFL      NO                                  *AMS*
         CLC   XTHITRK,VINTPC                                    85280
         BNL   GETDCHFL                                          *AMS*
         CLI   XTTYPE,XTTSPLIT  SPLIT EXTENT ?                   81047
         BNE   GETDCHUP      NO                                  81047
         CLC   XTLOTRK,XTHITRK  VALID RANGE ?                    81047
         BH    GETDCHFL      NO                                  81047
GETDCHUP LA    R6,1(,R6)     UP VALID EXTENT COUNT               81047
         LA    R8,XTLEN(,R8)  TRY NEXT EXTENT                    81047
         BCT   R5,GETDCHLP                                       *AMS*
GETDCHDN STC   R6,DS1NOEPV   ENSURE CORRECT COUNT                81047
GETDCHOK SUBEX OFF=4         RETURN TO CALLER                    81047
GETDCHFL SUBCALL MSGIN,GETDCMSG   PRINT EXTENT ERROR MESSAGE     81208
         OI    CCODE+1,4     SET RETURN CODE=4                   *AMS*
         MVI   DSLFG,0       KILL IT                             81047
         INC   NUMNPRO       UP COUNT OF UNPROCESSED DSNS        81208
         SUBEX ,             RETURN TO CALLER WITH ERROR         81047
GETDCMSG MSG   'NOT COPIED - EXTENTS BAD'                        81047
         SPACE 1                                                 83094
*        CHECK VALIDITY OF PTRDS FIELD                           83094
*              RETURN +0 =0; +4  OK ?                            83094
CHKPTRDS CLC   ZEROES(5),0(R3)  ALL ZERO ?                       83094
         BER   R9            YES; RETURN ZERO                    83094
         CLI   4(R3),0       ANY RECORD VALUE ?                  83094
         BE    CHKPRTBD      NO; FAIL                            83094
         CLC   4(1,R3),DS4DEVDT  VALID DSCB RECORD NUMBER ?      83094
         BH    CHKPRTBD      NO; FAIL                            83094
         ICM   R0,15,0(R3)   LOAD CYLINDER AND HEAD ADDRESS      83094
         CLM   R0,3,VINTPC   VALID HEAD ADDRESS ?                85280
         BNL   CHKPRTBD      NO; FAIL                            83094
         CLM   R0,12,VINEXT  IN INPUT VTOC ?                     83094
         BL    CHKPRTBD      NO                                  83094
         CLM   R0,12,VINEXT+4  IN RANGE ?                        83094
         BH    CHKPRTBD      NO; FAIL                            83094
         MVC   ABSAD,0(R3)     MOVE POINTER ADDRESS              83094
         MVC   ABSHD(1),FULL+3  COPY INPUT EXTENT BYTE           85296
         LA    R1,ABSHD      GET INPUT ADDRESS                   83094
         LA    R9,4(,R9)     SET GOOD RETURN ADDRESS             83094
         B     ABSTTR        RETURN TO CALLER VIA ABSTTR RETURN  83094
         SPACE 1                                                 83094
CHKPRTBD SUBCALL MSGIN,GETPTBAD  SHOW BAD POINTER                83094
         OI    CCODE+1,4     MINOR ERROR                         83094
         XC    0(5,R3),0(R3)  FORCE POINTER ZERO                 83094
         BR    R9            RETURN ZERO                         83094
GETPTBAD MSG   'INVALID DSCB POINTER - POINTER IGNORED'          83094
         TITLE 'C O P Y V O L  ***  DATASET ANALYSIS AND COPY SETUP'
*        PUTDS - BUILD NEW DSCBS AND PREPARE TO COPY             81047
PUTDS    SUBENT ,                                                81047
         USING DSLIST,R4                                         81047
         LA    R2,=CL8'IEBCOPY '  SET FOR PDS COMPRESS           86292
         TM    IIHOPT,DSCHA  DOING CHANGED DATASETS ?            79325
         BZ    PUTDS1                                            79325
         TM    DS1DSIND,DS1DSCHA   DATASET MODIFIED ?            81151
         BZ    PUTDS1F       NO; SKIP IEBCOPY OVERHEAD           79325
         AIF   ('&LOCAL' NE 'PID').NOCHA2                        92279
         NI    DS1DSIND,255-DS1DSCHA  RESET MODIFY BIT           81151
.NOCHA2  ANOP  ,                                                 81127
PUTDS1   TM    MIGFLAG,OPTMIGR+OPTMIGF  MIGRATING ?              86292
         BNO   PUTDS1M       NO                                  86292
         OI    IIHOPT,IIHCOMP                                    86292
         NI    IIHOPT,255-IIHFAIL  MAKE SURE OF FLAGS            86292
         NI    DS1DSORG,255-JFCORGU  RESET UNMOVABLE             86292
         AIF   ('&LOCAL' NE 'PID').NOWYLCP                       92279
         TM    DS1DSORG,JFCORGPO+JFCORGPS  SEQUENTIAL OR PDS ?   86292
         BNM   PUTDS1W       NO; CONTINUE TESTING                86292
         TM    DS1RECFM,DCBRECU  UNDEFINED RECORD FORMAT ?       86292
         BO    PUTDS1WC      YES; USE WYLCOPY (FAILS FOR LOAD-MODS)
         BZ    PUTDS1W       NO RECFM; JUST FAIL                 86292
         TM    DS1RECFM,DCBRECF+DCBRECBR  FIXED BLOCKED ?        86292
         BO    PUTDS1WC      YES; USE WYLCOPY                    86292
         TM    DS1RECFM,DCBRECV  VARIABLE ?                      86292
         BZ    PUTDS1W       NO                                  86292
         TM    DS1RECFM,DCBRECSB  VS OR VBS ?                    86292
         BNZ   PUTDS1W       YES; WYLCOPY WON'T HANDLE           86292
PUTDS1WC LA    R2,=CL8'WYLCOPY '  TRY WYLBUR COPY                86292
         B     PUTDS1C       COPY                                86292
.NOWYLCP ANOP  ,                                                 86292
PUTDS1W  CLI   DS1DSORG+1,0  VSAM OR OTHER ROT ?                GP09186
         BNE   PUTDS1F       YES; JUST COPY AS IS               GP09186
         CLI   DS1DSORG,JFCORGPO  PDS ?                          86292
         BE    PUTDS1M       YES; COMPRESS                       86292
         LA    R2,=CL8'IEBGENER'  SEQUENTIAL ?                   86292
         CLI   DS1DSORG,JFCORGPS                                 86292
         BE    PUTDS1C       YES                                 86292
         LA    R2,=CL8'SAS     '  SPECIAL ?                      86292
         CLI   DS1DSORG,JFCORGDA  POSSIBLE SAS FILE ?            86292
         BNE   PUTDS1F       NO; JUST COPY AS IS                 86292
         TM    DS1RECFM,DCBRECU  RECFM=U ?                       86292
         BNO   PUTDS1F       NO                                  86292
         SLR   R1,R1                                             86292
         ICM   R1,3,DS1LRECL GET RECORD LENGTH                   86292
         LA    R1,4(,R1)     PLUS 4                              86292
         CLM   R1,3,DS1BLKL  SAME AS BLOCKSIZE ?                 86292
         BNE   PUTDS1F       NO                                  86292
         B     PUTDS1S       JOIN COMMON                         86292
PUTDS1M  CLI   DS1DSORG,JFCORGPO  MOVABLE PDS ?                  86292
         BNE   PUTDS1F       NO; SET NON-COMPRESS BITS           81047
PUTDS1C  TM    DS1RECFM,DCBRECLA  ANY RECFM ?                    86292
         BZ    PUTDS1F       NO; SKIP IEBCOPY COMPRESSION        83199
         TM    DS1RECFM,DCBRECF  RECFM=F OR U ?                  83199
         BNZ   PUTDS1U                                           83199
         TM    DS1RECFM,DCBRECV+DCBRECBR+DCBRECSB  RECFM=VBS ?   83199
         BO    PUTDS1F       YES; IEBCOPY WON'T HANDLE           83199
         CLI   DS1EXT1+XTTYPE-EXTMAP,XTTLABEL  LABEL EXTENT ?    83257
         BE    PUTDS1F       YES; I WON'T HANDLE                 83257
PUTDS1U  ICM   R0,7,DS1LSTAR ANY R IN LSTAR ?                   GP09186
         BZ    PUTDS1F       NO; SKIP IEBCOPY                   GP09186
PUTDS1S  MVC   EPLOC,0(R2)   SET COPY PROGRAM NAME               86292
         TM    DS1DSIND,X'40'  RACF DATASET ?                    79137
         BZ    PUTDS2         NO                                 79137
         AIF   (NOT &MVS).NOMVSNQ                                82195
         ORG   *-4                                               81047
         BNZ   PUTDS1F       NO IEBCOPY FOR RACF                 81047
         LA    R2,DS1DSNAM   POINT TO DSN FOR ENQUEUE TEST       81228
         ENQ   (SYSDSN,(R2),S,44,SYSTEM),RET=TEST                81228
         BXLE  R15,R15,PUTDS2   OK IF ENQ AVAILABLE              81047
.NOMVSNQ ANOP  ,                                                 81047
         B     PUTDS1F       FORCED FAILURE                      86292
PUTDS1FL CLC   =C'WYLCOPY',EPLOC  SPECIAL COPY ?                 86292
         BNE   PUTDS1F                                           86292
         LA    R2,=CL8'IEBGENER'                                 86292
         TM    DS1DSORG,JFCORGPS                                 86292
         BNZ   PUTDS1C       RETRY IT                            86292
         LA    R2,=CL8'IEBCOPY '                                 86292
         B     PUTDS1C       ELSE DO AS PDS                      86292
PUTDS1F  TM    IIHOPT,IIHCOMP  CHECK HERE, ELSE LATER 'BM'       79325
         BZ    PUTDS2           WON'T WORK                       79137
         OI    IIHOPT,IIHFAIL  SKIP IEBCOPY ****TEMP****         79137
         TM    MIGFLAG,OPTMIGR+OPTMIGF  MIGRATING ?              91100
         BNO   PUTDS2        NO ?                                91100
         CLC   VINSIZE+2(2),DS4DEVTK  LARGER OUTPUT TRACK ?      93166
         BNH   PUTDS2        YES; TRY IT                         91100
         OI    CCODE+1,8     SET ERROR                           91100
         INC   NUMNPRO                                           91100
         SUBCALL MSGIN,MSGFNSIZ                                  91100
         B     TELLDSX       FAIL THIS                           91100
MSGFNSIZ MSG   'NOT COPIED - INCOMPATIBLE TRACK SIZES'           91100
PUTDS2   MVC   IIHNEXT,BLOCKAD SAVE DISK ADDRESS OF NEXT DSCB    *UNG*
         SLR   R0,R0                                             86138
         ICM   R0,7,DS1EXPDT ANY EXPIRATION ?                    86138
         BZ    PUTDSEXP      NO; IGNORE                          86138
         CLM   R0,7,DS1CREDT IS IT MEANINGFUL ?                  86138
         BH    PUTDSEXP      YES                                 86138
         XC    DS1EXPDT,DS1EXPDT  ELSE ZERO IT                   86138
PUTDSEXP LA    R2,FMT1                                           86138
         SUBCALL DSNCONV     GET DSNAME CONVERT ADDRESS          81047
           B   TELLDSX   +0    REJECTED BY USER EXIT             93002
           B   PUTBDUPL  +4    UNCHANGED                         93002
           B   PUTCDUPL  +8    RENAMED; CHECK FOR DUPLICATE      93002
PUTBDUPL TM    DSLF2,7       ON FIRST PACK ?                     81047
         BZ    PUTNDUPL      YES; NO CHECK                       81047
PUTCDUPL MVI   TRAKFLAG,TKDUPE  SET FOR DUPLICATE CHECK          81047
         SUBCALL TRAKCOPY                                        81047
         CLI   DSLFG-DSLENT+DSLCURR,0  DUPLICATE ?               81047
         BNE   PUTNDUPL      NO                                  81047
         MVI   DS1NOEPV,0    SET ERROR FLAG FOR RELALLOC RETURN  81047
         OI    CCODE+1,8                                         81047
         INC   NUMNPRO       UP COUNT OF UNPROCESSED DSNS        81208
         SUBCALL MSGIN,MSGFDUPE   SET MESSAGE                    81208
         B     TELLDSX       AND EXIT                            81047
MSGFDUPE MSG   'NOT COPIED - ALREADY ON OUTPUT VOLUME'           81047
PUTNDUPL LA    R14,NEWEXTS   GET OUTPUT EXTENTS TO BE USED       81047
         ST    R14,AMSOUTXA  STORE                               *AMS*
         MVC   AMSOUTX#,DS1NOEPV   COPY THE NUMBER OF EXTENTS    *AMS*
         TM    DSLFG,DSLISAM   IS IT ISAM                        81047
         BZ    PTNOIS1         IF NOT, THEN FORGET IT
         LA    R2,FMT2         GET FMT2 ADDRESS IN 2
         B     PTNOISCM      SKIP AROUND EXTENT COMPRESSION      *AMS*
         SPACE 1                                                 81047
PTNOIS1  CLI   DS1NOEPV,1     SEE IF MORE THAN ONE EXTENT        *AMS*
         BNH   PTNOISCM      NO; NO COMPRESSION TO BE DONE       *AMS*
         TM    DS1DSORG+1,JFCORGAM   VSAM ?                      81047
         BNZ   PTNOISCM      DITTO NO COMPRESSION (?)            81047
         TM    DS1SCALO,JFCONTIG  SPACE=,,CONTIG REQ. ?          79192
         BNZ   PTGOCOMC      YES; IGNORE KEEP OPTION             79192
         TM    OPTFLAG,OPTKEEP  KEEP UNUSED EXTENTS ?            79105
         BNZ   PTNOISCM       YES; BUT WHY ?                     79105
PTGOCOMC TM    IIHOPT,IIHCOMP+IIHFAIL  USING IEBCOPY ?           79105
         BM    PTNOISCM      YES; RELEASE EXTENTS AFTER COPY     79105
PTGOCOMP SUBCALL AMSXCOMP    GET COMPRESSION ROUTINE             81047
         SPACE 1                                                 *AMS*
PTNOISCM XC    DS1PTRDS-DS1DSNAM(5,R2),DS1PTRDS-DS1DSNAM(R2)  UNCHAIN
         SUBCALL FIXEXTS     MOVE EXTENTS BACK TO DSCBS          81047
         CLI   DS1NOEPV,3       ARE THERE MORE THAN 3 EXTENTS
         BNH   PTNOFMT3        IF <= 3 - NO FORMAT 3
         WRTVTOC FMT3,DS1PTRDS-DS1DSNAM(R2)  WRITE FORMAT3
PTNOFMT3 TM    DSLFG,DSLISAM   SEE IF ISAM                       81047
         BZ    PTNOIS2         IF NOT, THEN FORGET IT
         WRTVTOC FMT2,DS1PTRDS MOVE AND WRITE FORMAT 2
         SPACE 1                                                 81047
*   BEFORE WRITING THE FORMAT 1 DSCB, CHECK IF IEBCOPY WILL BE INVOKED
*   IF SO, THE OLD FMT1 IS SAVED IN FMT2 (FOR ERROR RECOVERY), AND
*   MISCELLANEOUS DSCB1 FIELDS ARE CLOBBERED AS REQUIRED FOR THE 81047
*   COPY. THESE INCLUDE THE DSN (PREVENT ENQ HANGUPS); PASSWORD BITS;
*   EXPIRATION DATE, AND THE LIKE.                               *UNG*
PTNOIS2  TM    IIHOPT,IIHCOMP+IIHFAIL  UNFAILED COMPRESS ?       79325
         BNM   NOCOPY        NO;  CONTINUE NORMALLY              79325
         MVC   FMT2(DS1END-FMT1),FMT1  SAVE ENTIRE FORMAT 1      81047
         MVC   IIHUSED,TRKUSD  SAVE TRACKS TO BE COPIED          91119
         XC    DS1EXPDT(3),DS1EXPDT                              *UNG*
         XC    DS1LSTAR(5),DS1LSTAR                              *UNG*
         NI    DS1DSIND,255-X'14'-X'40'  RESET PSWD/RACF BITS    83294
         MVC   DS1DSNAM(L'TRASH),TRASH  KILL OUTPUT DSN          81047
         MVC   DS1DSNAM+L'TRASH(6),VOUTVSER  ADD VOLSER          81047
         SLR   R0,R0         CLEAR FOR LRECL/BLKSIZE STORE       87018
         STCM  R0,15,DS1SCALO  KILL SECONDARY ALLOCATION         87018
         CLC   =CL8'SAS     ',EPLOC  SAS ?                       86299
         BE    SETWYTYP      YES; WILL RESET DCBS ANYWAY         86299
         TM    MIGFLAG,OPTNBLK  SUPPRESS REBLOCKING ?            86292
         BNZ   NOCOPY        YES; JUST COPY                      86292
         CLC   =CL8'WYLCOPY ',EPLOC  SPECIAL COPY ?              86292
         BNE   NOTWYCOP                                          86299
         TM    MIGFLAG,OPTRBLK  REBLOCK EVERYTHING ?             86292
         BNZ   SETWYTYP      YES                                 86299
         TM    DS1RECFM,DCBRECU  RECFM=U ?                       86292
         BO    SETWYTYP      YES                                 86299
         TM    MIGFLAG,OPTUBLK  REBLOCK RECFM=U ONLY ?           86292
         BNZ   NOCOPY        YES                                 86292
         CLI   DS1LRECL,0    LRECL<256 ?                         86299
         BE    SETWYTYP      YES                                 86299
         B     NOCOPY        ELSE LEAVE IT ALONE                 87018
NOTWYCOP TM    DS1RECFM,DCBRECU  RECFM=U ?                       86299
         BNM   NOCOPY        YES; DON'T CHANGE IT                86299
         TM    DS1RECFM,DCBRECSB  BLOCKED ?                      86299
         BZ    NOCOPY        NO; DON'T MESS WITH IT              86299
         TM    MIGFLAG,OPTUBLK  REBLOCK RECFM=U ONLY ?           86299
         BNZ   NOCOPY        YES; LEAVE IT ALONE                 86299
         CLI   DS1LRECL,0    LRECL > 256 ?                       86299
         BNE   NOCOPY        YES; LEAVE IT AS IS                 87018
NOTWYLRL ICM   R14,15,BLKSZTAB  DO WE HAVE A BLOCKSIZE TABLE ?   86292
         BZ    NOCOPY        NO; LET WYLCOPY DO IT               86292
         L     R15,VOUTUCB   LOAD OUTPUT UCB                     86292
         ICM   R0,3,UCBTBYT3-UCBOB(R15)  LOAD DEVICE TYPE        86292
NOTWYTYP CLM   R0,3,0(R14)   MATCHING DEVICE TYPE ?              86292
         BE    HAVWYTYP      YES                                 86292
         LA    R14,8(,R14)   GET NEXT ENTRY                      86292
         CLI   0(R14),X'FF'  LAST ENTRY ?                        86292
         BNE   NOTWYTYP      NO                                  86292
         B     NOCOPY        LET WYLCOPY DO IT                   86292
HAVWYTYP SLR   R0,R0                                             86292
         ICM   R0,3,2(R14)   GET PREFERRED WYLBUR SIZE           86292
         SLR   R2,R2         CLEAR FOR INSERT                    86299
         IC    R2,DS1KEYL    GET KEY LENGTH                      86299
         SR    R0,R2         SUBTRACT KEY LENGTH                 86299
         BNP   NOCOPY        TOO BAD                             86299
         SLR   R15,R15                                           86292
         ICM   R15,3,DS1LRECL   GET THE RECORD LENGTH            86292
         BZ    NOCOPY                                            86299
         TM    DS1RECFM,DCBRECV  VARIABLE ?                      86292
         BZ    *+8           NO                                  86292
         SH    R0,=H'4'      ALLOW FOR BDW                       86299
         CR    R15,R0        LRECL > BLKSIZE ?                   86292
         BNH   SETWYBLK      NO                                  86299
         ICM   R0,3,6(R14)   LOAD TRACK MAX INSTEAD              86292
         SR    R0,R2         LESS KEY LENGTH                     86299
         TM    DS1RECFM,DCBRECV  RECFM=V ?                       86299
         BZ    SETWYLEN      NO                                  86299
         SH    R0,=H'4'      ALLOW FOR BDW                       86299
SETWYBLK TM    DS1RECFM,DCBRECV  RECFM=V ?                       86299
         BNZ   SETWYTYP+4    YES; LEAVE BLOCK AS IS              86299
SETWYLEN SRDL  R0,32         FLIP                                86299
         DR    R0,R15        GET RECORDS/BLOCK                   86299
         LTR   R1,R1                                             86299
         BP    *+8                                               86299
         LA    R1,1                                              86299
         MR    R0,R15        NEW BLOCKSIZE                       86299
         LR    R0,R1         FLIP                                86299
         B     SETWYTYP+4                                        86292
SETWYTYP STH   R0,DS1LRECL   FORCE LRECL FOR RECFM=U             86292
         STH   R0,DS1BLKL    SET NEW BLOCKSIZE                   86292
         SPACE 1                                                 *UNG*
NOCOPY   CLI   DSLOTTR+2,0   WRITE DSCB AT SPECIFIC ADDRESS ?    81047
         BE    NOCOPYW       NO; DO NORMALLY                     81047
         LM    R5,R7,PPREBLOK   SAVE ALL ADDRESSES               81047
         MVC   BLOCKAD(3),DSLOTTR  SET SPECIFIC BLOCK ADDRESS    81047
         WRTVTOC FMT1,LASTFMT1 WRITE THE FORMAT 1                81047
         STM   R5,R7,PPREBLOK   RESTORE ALL ADDRESSES            81047
         B     NOCOPYT       GO TO TEST                          81047
NOCOPYW  WRTVTOC FMT1,LASTFMT1 WRITE OUT THE FORMAT 1            81047
NOCOPYT  CLC   LASTFMT1,FMT1HWM  HIGHER ?                        81047
         BNH   *+10          NO                                  81047
         MVC   FMT1HWM,LASTFMT1  SAVE NEW HIGH-WATER MARK        81047
         MVC   DS1NOEPV(1),AMSOUTX# RESTORE ORIGINAL # OF EXTENTS *AMS*
         CLI   DS1NOEPV,0       ARE THERE ANY EXTENTS            79105
         BE    TELLDS          IF NOT, JUST SAY WE COPIED IT     79105
         SPACE 2                                                 81047
*  'IEBCOPY' IS INVOKED TO COPY THE DATASET IF IT IS A LIBRARY AND
*   COMPRESSING WAS NOT PREVIOUSLY CANCELLED.                    *UNG*
         TM    IIHOPT,IIHCOMP+IIHFAIL  UNFAILED COMPRESS ?       79325
         BNM   NOCOMP        NO;  DO NOT COMPRESS                79325
         SUBCALL COMPRESS    CONDENSE IT                         81047
         BXH   R15,R15,PUTDS1FL  IEBCOPY FAILED - FLAG ERROR     86292
         CLI   DS1DSORG,JFCORGPO  PDS ?                          86292
         BNE   TELLDSZ       NO; LEAVE PDS COUNTERS ALONE        86292
         L     R1,NUMDPOB      GET PO COPY COUNT                 79327
         BCTR  R1,0             ADJUST                           79327
         ST    R1,NUMDPOB                                        79327
         INC   NUMDPO        UP COMPRESSED COUNT                 81208
         B     TELLDSZ       AND RETURN                          81047
         SPACE 1                                                 81047
NOCOMP   MVI   TRAKFLAG,0    SET FLAG FOR NORMAL COPY            81047
         SUBCALL TRAKCOPY    COPY TRACKS                         81047
         SPACE 1                                                 81047
TELLDS   LA    R1,TELDSMSG   GET SUCCESSFUL COMPLETION MSG       86292
         TM    MIGFLAG,OPTMIGR+OPTMIGF  MIGRATING ?              86292
         BNO   TELLDSOK      NO; WE'RE OK                        86292
         LA    R1,TELDSMAS   SHOW COPIED AS IS                   86292
TELLDSOK SUBCALL MSGINSUP    PRINT 'SUPERFLUOUS' MESSAGE         86292
TELLDSZ  INC   NUMDSN        COUNT DATASETS COPIED               81208
         LA    R2,NUMDVSAM   POINT TO VSAM COUNTER               81047
         TM    DSLFG,DSLVSAM  IS IT VSAM ?                       81047
         BO    TELLDSUP      YES                                 81047
         LA    R2,NUMDIS       POINT TO ISAM                     81208
         TM    DSLFG,DSLISAM   IS IT ISAM ?                      79327
         BNZ   TELLDSUP      YES                                 81047
         LA    R2,NUMDU        COUNT AS UNMOVABLE                81208
         TM    DSLFG,DSLUNM  IS IT UNMOVABLE ?
         BNZ   TELLDSUP      YES                                 81047
         LA    R2,NUMDOTH     SET FOR OTHER                      81208
         CLI   DS1DSORG+1,0    VSAM OR OTHER FUNNY?             GP09186
         BNE   TELLDSUP      YES; COUNT AS OTHER                GP09186
         TM    DS1DSORG,X'62'  PS/DA/PO ?                        79327
         BNM   TELLDSUP      NO; COUNT AS OTHER                  79327
         LA    R2,NUMDDA     DA ?                                81208
         TM    DS1DSORG,JFCORGDA                                 79327
         BNZ   TELLDSUP      YES                                 79327
         LA    R2,NUMDPOB                                        81208
         TM    DS1DSORG,JFCORGPO  PARTITIONED ?                  79327
         BNZ   TELLDSUP                                          79327
         LA    R2,NUMDPS     ELSE IT IS SEQUENTIAL               81208
TELLDSUP INC   0(,R2)        UP DSN COUNT BY TYPE                81208
TELLDSX  NI    IIHOPT,X'FF'-IIHFAIL MARK NOT FAILED FOR NEXT TIME *UNG*
         SUBEX ,             RETURN TO CALLER                    81047
TELDSMSG MSG   '  COPIED SUCCESSFULLY'                           81047
TELDSMAS MSG   '  COPIED AS IS'                                  86292
         SPACE 2                                                 81047
NEXTBLOK SUBENT ,            ROUTINE TO ASSIGN NEW BLOCK NUMBER  81047
         MVC   PPREBLOK(8),PREBLOCK  SAVE PREVIOUS BLOCK ADDRESSES
         L     R5,BLOCKAD    GET PREVIOUS BLOCK ADDRESS          81047
         CLM   R5,2,DS4DEVDT  LAST DSCB ON TRACK ?               81047
         BL    *+12          NOT YET                             81047
         N     R5,=X'FFFF00FF'  RESET R                          81047
         AL    R5,=X'00010000'  BUMP TT                          81047
         AL    R5,=X'00000100'  INCREASE R                       81047
         TM    DSLFLAG,DSLVSAM  ANY VSAM DATASET ?               81047
         BZ    NEXTBLOQ      NO; JUST STASH IT                   81047
NEXTBLOM LM    R2,R4,DSLBXLE   GET LIST OF ENTRIES               81047
         LA    R4,0(,R4)     CLEAR FLAG BYTE                     81047
         SR    R4,R2         ALLOW FOR FIRST BUMP                81047
         USING DSLIST,R4                                         81047
NEXTBLOP BXH   R4,R2,NEXTBLOQ  STORE ADDRESS IF DONE             81047
NEXTBMAT CLM   R5,14,DSLOTTR  MATCH ?                            81047
         BNE   NEXTBLOP      NO; TRY AGAIN                       81047
         CLM   R5,2,DS4DEVDT  LAST DSCB ON TRACK ?               81047
         BL    *+12          NOT YET                             81047
         N     R5,=X'FFFF00FF'  RESET R                          81047
         AL    R5,=X'00010000'  BUMP TT                          81047
         AL    R5,=X'00000100'  INCREASE R                       81047
         CLI   VIDDMAX+1,1   SINGLE INPUT VOLUME ?               81047
         BNE   NEXTBLOM      NO; SCAN ALL                        81047
         B     NEXTBLOP      CHECK FOR MATCH AGAIN               81047
NEXTBLOQ ST    R5,BLOCKAD    SET NEXT AVAILABLE                  81047
         SUBEX ,             RETURN                              81047
         SPACE 1                                                 81047
         LTORG ,                                                 81047
         TITLE 'C O P Y V O L  ***  IEBCOPY INTERFACE'           81047
*  THIS SECTION IS PART OF THE MODIFICATION TO 'COPYFDR' TO COMPRESS
*   LIBRARIES BY IEBCOPY.                                        *UNG*
*        THE CODE BUILDS A NEW INPUT AND OUTPUT JFCB, THEN USES OPENJ
*        TO FORCE JFCB REWRITE TO THE JOBQUEUE/JSWA (WHICH COULD BE
*        DONE DIRECTLY).                                         81047
*        THE INPUT DIRECTORY IS READ, AND AN EMPTY OUTPUT DIRECTORY
*        OF THE SAME SIZE IS FORMATTED.                          81047
*        THE OUTPUT DSCB IS REWRITTEN WITH THE NEW DS1LSTAR, AND 81047
*        IEBCOPY IS INVOKED.                                     81047
*        IF ANY PROCESSING FAILS, A NON-ZERO RETURN CODE PASSED TO THE
*        CALLER CAUSES NORMAL COPYING OF THE DATASET WITHOUT COMPRESS.
*        WHEN IEBCOPY PROCESSING COMPLETES, UNUSED EXTENTS ARE FREED,
*        AND REMAINING EXTENTS ARE CONSOLIDATED.                 81047
         SPACE 1                                                 *UNG*
COMPRESS SUBENT SAVE=YES     IEBCOPY INTERFACE                   81047
         LA    R3,OUTJFCB    POINT TO OUTPUT JFCB                81047
         USING INFMJFCB,R3   DECLARE THE JFCB MAPPING            81208
         OI    JFCBTSDM,JFCNDSCB+JFCNDCB  INHIBIT MERGE TO JFCB  81047
         XC    JFCKEYLE(JFCNCP-JFCKEYLE),JFCKEYLE   CLEAR SOME   81047
         MVC   JFCKEYLE(1),DS1KEYL                               81047
         MVC   JFCDSORG(1),DS1DSORG                              81047
         MVC   JFCRECFM(1),DS1RECFM                              81047
         MVC   JFCOPTCD(1),DS1OPTCD                              81047
         NI    JFCBIND1,255-JFCRLSE  KEEP EXTENTS                81047
         XC    JFCBPQTY(7),JFCBPQTY  CLEAR SPACE & FLAGS         81096
         MVC   JFCBDSNM,DS1DSNAM  COPY OUTPUT DATASET NAME       81047
         MVC  INJFCB+L'DS1DSNAM(JFCBLGTH-L'DS1DSNAM),OUTJFCB+L'DS1DSNAM
         MVC   JFCBLKSI(2),DS1BLKL                               81047
         MVC   JFCLRECL(2),DS1LRECL                              81047
         TM    MIGFLAG,OPTMIGR+OPTMIGF  MIGRATING ?             GP10017
         BZ    COMPNBLK      NO                                 GP10017
         TM    JFCRECFM,X'40'     POSSIBLE V OR U?              GP10017
         BNZ   COMPNBLK      YES; RETAIN                        GP10017
         TM    JFCRECFM,X'80'     REALLY F?                     GP10017
         BZ    COMPNBLK      SKIP IF D                          GP10017
         CLC   =C'SAS ',EPLOC     SAS FILE ?                    GP10017
         BE    COMPNBLK      SKIP IF SO                         GP10017
         L     R0,VOUTUCB    GET THE OUTPUT UCB                 GP10017
         SR    R1,R1         CLEAR FOR IC                       GP10017
         ICM   R1,4,JFCKEYLE                                    GP10017
         ICM   R1,3,JFCLRECL      BUILD KEY/REC LEN             GP10017
         L     R15,=V(SUBBLKOP)   GET OPTIMIZER                 GP10017
         BALR  R14,R15                                          GP10017
         LTR   R15,R15       DID WE GET SOMETHING ?             GP10017
         BZ    COMPNBLK      NO; LEAVE IT ALONE                 GP10017
         STH   R15,JFCBLKSI  UPDATE BLOCK SIZE                  GP10017
         OI    JFCRECFM,X'10'     SET BLOCKED, JUST IN CASE     GP10017
COMPNBLK LA    R3,INJFCB     SWITCH MAPPING TO INPUT             81047
*OOPS    MVC   JFCBDSNM,FMT2+DS1DSNAM-IECSDSL1  RESTORE DSN      82277
         MVC   JFCBDSNM,DSNAME    RESTORE ORIGINAL NAME         GP09205
      MVC JFCDSORG(JFCLRECL-JFCDSORG+L'JFCLRECL),DS1DSORG-IECSDSL1+FMT2
         IC    R1,DSLCURR+DSLF2-DSLENT  GET VOLUME INDEX         81047
         N     R1,=A(7)      MASK                                81047
         MH    R1,=Y(VIN2DDNM-VIN1DDNM)                          81047
         LA    R1,VIN1DDNM(R1)  GET VOLUME ENTRY                 81047
         MVC   JFCBVOLS(6),VIN1VSER-VIN1DDNM(R1)  GET INPUT VOLUME
         DROP  R3                                                81047
         MVC   COPDDIN,0(R1)  COMPLETE IEBCOPY DDNAME LIST       81047
         MVC   COMPIN+DCBDDNAM-IHADCB(8),0(R1) AND DCB DD        81047
         MVC   COPDDOUT,VOUTDDNM  DITTO FOR OUTPUT               81047
         MVC   COMPOUT+DCBDDNAM-IHADCB(8),VOUTDDNM               81047
         SPACE 1                                                 81047
         AIF   (&MVS AND NOT (&MVSXA OR &MVSSP)).MVSJFUP         83017
*        FORCE REWRITE OF JFCBS TO JOB QUEUE                     81047
*                                                                81047
         OPEN  (COMPIN,,COMPOUT,UPDAT),TYPE=J OPEN BOTH          *UNG*
         MVC   OPENMSG#,COPDDIN   SET INPUT DDNAME               81047
         TM    COMPIN+(DCBOFLGS-IHADCB),DCBOFOPN  SYSUT1 OPENED ?
         BZ    COMPFAYL      NO; CLOSE WITH ERROR MESSAGE        81047
         MVC   OPENMSG#,COPDDOUT  SET OUTPUT DDNAME              81047
         TM    COMPOUT+(DCBOFLGS-IHADCB),DCBOFOPN SYSUT2 OPENED ?
         BZ    COMPFAYL      NO; CLOSE WITH MESSAGE              81047
         CLOSE (COMPIN,,COMPOUT) CLOSE THEM BOTH                 *UNG*
         AGO   .COMJFUP                                          82277
.MVSJFUP ANOP  ,                                                 82277
*        UPDATE JFCBS FOR IEBCOPY                                82277
*                                                                82277
         MODESET KEY=ZERO    ENABLE JSWA UPDATE                  82277
         LA    R5,COPDDIN    POINT TO INPUT DDNAME               82277
         LA    R6,2          PROCESS TWO JFCBS (IN/OUT)          82277
         LA    R7,INJFCB     POINT TO INPUT JFCB                 82277
COMPJFCL MVC   OPENMSG#,0(R5)   COPY DDNAME FOR ERROR MESSAGE    82277
         SERVCALL TIODD,0(R5)   GET TIOT ENTRY FOR IT            82277
         LTR   R1,R0         ANY MATCH ?                         82277
         BNZ   COMPFGUD      YES                                 82277
COMPFBAD MODESET KEY=NZERO   RESET                               82277
         B     COMPFAYL      AND QUIT                            82277
         USING TIOELNGH,R1                                       82277
COMPFGUD ICM   R1,7,TIOEJFCB  GET SWA ADDRESS OF JFCB            82277
         BZ    COMPFBAD      BUT HOW ?                           82277
         DROP  R1                                                82277
         MVC   16(JFCBLGTH,R1),0(R7)  MOVE UPDATED JFCB          82277
         LA    R7,OUTJFCB    POINT TO OUTPUT                     82277
         LA    R5,COPDDOUT   POINT TO OUTPUT DDNAME              82277
         BCT   R6,COMPJFCL   REPEAT FOR OUTPUT                   82277
         MODESET KEY=NZERO                                       82277
.COMJFUP SPACE 1                                                 82277
         CLC   =C'SAS ',EPLOC  DOING SAS COPY ?                  86292
         BNE   NOTSASSY      NO                                  86292
         TM    SASIN+DCBOFLGS-IHADCB,DCBOFOPN  IS DCB OPEN ?     86292
         BZ    NOTSASSY      NO                                  86292
         CLC   COPDDIN,SASDDIN  SAME DD ?                        86292
         BNE   WRITESAS      NO; UPDATE                          86292
         CLC   COPDDOUT,SASDDOUT  SAME OUTPUT ?                  86292
         BE    NOTSASSY      YES; NOT FIRST TIME                 86292
WRITESAS MVC   SASDDIN,COPDDIN                                   86292
         MVC   SASDDOUT,COPDDOUT  UPDATE CONTROL CARD            86292
         WRITE SASDECB,SF,SASIN,SASREC,80  WRITE THE RECORD      86292
         CHECK SASDECB       WAIT FOR COMPLETION                 86292
         CLOSE (SASIN),TYPE=T  WRITE AN END-FILE                 86292
         POINT SASIN,SASTTR  REPOSITION                          86292
*  COUNT NUMBER OF DIRECTORY BLOCKS.                             *UNG*
NOTSASSY MVI   TRAKFLAG,TKQUIES  QUIESCE ALL PRIOR I/O           85287
         SUBCALL TRAKCOPY    FREE ALL BUFFERS                    85287
         MVI   COPYFLAG,0     CLEAR FLAGS.                       79105
         SLR   R7,R7         SET DIRECTORY BLOCK COUNT           *UNG*
         XC    TRKCHAIN(8),TRKCHAIN  CLEAR CHAIN HEADS           83059
         CLI   DS1DSORG,JFCORGPO  PDS COMPRESS ?                 86292
         BNE   CMPMIGNC      NO; SKIP COUNTING                   86292
         MVI   TRAKFLAG,TKPDE   SET FOR SPECIAL DIRECTORY COPY   81047
PDELOOP  SUBCALL TRAKCOPY    READ AND REFORMAT THE DIRECTORY     81047
         ICM   R6,7,TRKCHAIN+1  GET ANOTHER BUFFER               81047
         BZ    PDEQUIES      ALL DONE; QUIESCE WRITES            81047
         USING BUFFMAP,R6    DEFINE BUFFER MAPPING               81047
         MVI   DWTOGO,OFFWDATD  SET FOR WRITE                    81047
         TM    MIGFLAG,OPTMIGR+OPTMIGF  MIGRATION ?              85280
         BNO   *+8           NO; WRITE OUTPUT TRACK NORMALLY     85280
         MVI   DWTOGO,OFFAVAIL  ELSE RELEASE THE BUFFER          85280
         MVI   DWECB,X'7F'   SET GOOD I/O                        81047
         L     R5,DWARDATA   GET CKD CCW CHAIN                   81047
         LA    R5,24(,R5)    SKIP SEARCH/TIC/READ R0 CCWS        81047
READBL   CLI   0(R5),3       NOP CCW ?                           81047
         BE    PDELOOP       YES; GET NEXT BUFFER                81047
         L     R4,0(,R5)     GET COUNT/KEY/DATA AREA             81047
         CLI   0(R5),X'1E'   READ CKD CCW ?                      81047
         BE    READBLK       YES; CHECK CONTENTS                 81047
         CLI   0(R5),X'12'   END-FILE ENTRY ?                    81047
         BE    READBLF       YES; PROCESS END-FILE               81047
BADDIR   OI    COPYFLAG,DIRERR  SET ERROR FLAG                   81047
         MVI   DWTOGO,OFFAVAIL  MAKE THIS BUFFER AVAILABLE       83059
READBLF  NI    4(R5),255-X'40'  TURN OFF CHAINING FOR THIS BLOCK 81047
         B     PDEQUIES      GO TO QUIESCE I/O                   81047
READBLK  CLC   =X'080100',5(R4)  KEYLEN=8, DATALEN=256 ?         81047
         BE    READBLK2      YES; ASSUME VALID D.E.              85287
         CLC   =X'080000',5(R4)  KEYED END-FILE BLOCK ?          85287
         BNE   BADDIR        NO; FAIL                            81047
         MVI   5(R4),0       CHANGE KEY LENGTH TO ZERO           85287
         MVI   0(R5),X'12'   CHANGE CCW TO WRITE COUNT ONLY      85287
         MVC   6(2,R5),=H'8'  SET WRITE LENGTH FOR COUNT         85287
         B     READBLF       TREAT AS NORMAL END-FILE            85287
READBLK2 LTR   R7,R7         FIRST TIME ?                        85287
         BP    DIRBLOK1       NO; JUST UP                        79105
         LH    R0,16(,R4)    GET PRESUMED LENGTH                 81047
         CH    R0,=H'2'      VALID ?                             81047
         BE    DIRBLOK1      SKIP EMPTY                          81047
         CH    R0,=H'14'     MINIMUM ENTRY ?                     81047
         BL    BADDIR        NO; BAD ALSO                        81047
         CH    R0,=H'256'    REASONABLE ?                        81047
         BH    BADDIR        NO                                  81047
         TM    MIGFLAG,OPTMIGR+OPTMIGF MIGRATING ?               86339
         BZ    DIRBNMIG      NO                                  86339
         TM    18+8+3(R4),X'60'  NOTELIST ENTRY ?                86341
         BZ    DIRBNMIG                                          86339
         MVC   EPLOC(3),=C'IEB'  CHANGE WYLCOPY TO IEBCOPY       86339
DIRBNMIG CLC   MAXPDE,18(R4) NULL ?                              81047
         BNE   DIRBLOK0       NO                                 79105
         CLC   8(8,R4),MAXPDE  END KEY ?                         81047
         BNE   DIRBLOK0      NO                                  81047
         OI    COPYFLAG,NULLCOPY  SET EMPTY DS                   81047
DIRBLOK0 XC    16(256,R4),16(R4)  CLEAR NEW DIRECTORY BLOCK      81047
         MVC   8(22,R4),PATPDE  SET EMPTY DIRECTORY BLOCK        81047
         B     DIRBLOK2                                          81047
DIRBLOK1 XC    16(256,R4),16(R4)  CLEAR NEW DIRECTORY BLOCK      81047
         XC    8(8,R4),8(R4)  CLEAR KEY                          81047
DIRBLOK2 LA    R7,1(,R7)      INCREMENT COUNTER                  *UNG*
         TM    4(R5),X'40'   COMMAND CHAINED ?                   81047
         LA    R5,8(,R5)     POINT TO NEXT COMMAND ANYWAY        81047
         BNZ   READBL        YES; PROCESS NEXT BLOCK             81047
         B     PDELOOP       TRY ANOTHER TRACK                   81047
         SPACE 1                                                 81047
PDEQUIES MVI   TRAKFLAG,TKQUIES  SET FOR BUFFER FREE             83059
         SUBCALL TRAKCOPY    FREE ALL BUFFERS                    83059
         ICM   R6,7,TRKCHAIN+1  ANY MORE ON THE CHAIN ?          85280
         BZ    PDEQUIET      NO                                  85280
         MVI   DWECB,X'7F'   NO WAIT                             85280
         MVI   DWTOGO,OFFAVAIL  RELEASE BUFFER                   85280
         B     PDEQUIES      TRY AGAIN                           85280
         DROP  R6                                                81047
PDEQUIET LA    R1,DIRERMSG   SET FOR DIRECTORY ERROR MESSAGE     85280
         LTR   R7,R7         ANY DIRECTORY BLOCKS READ ?         81047
         BNP   COMPFAIL      NO; ERROR                           81047
         TM    COPYFLAG,DIRERR  ANY ERRORS FLAGGED ?             81047
         BNZ   COMPFAIL      YES; ERROR                          81047
         LH    R1,0(,R4)     GET LAST CYLINDER READ              81047
         SH    R1,XTLOCYL-EXTMAP+NEWEXTS  LESS START CYLINDER    81047
         MH    R1,VOUTTPC    CONVERT TO TRACKS                   85287
         AH    R1,2(,R4)     ADD END TRACK                       81047
         SH    R1,XTLOTRK-EXTMAP+NEWEXTS  LESS START TRACK       81047
         STH   R1,DS1LSTAR   SET TT PORTION OF LSTAR             81047
         MVC   DS1LSTAR+2(1),4(R4)  SET 'R'                      81047
         TM    MIGFLAG,OPTMIGR+OPTMIGF  MIGRATION ?              85280
         BNO   DIRNMIG       SKIP IF NOT MIGRATING               85280
         MVI   TRAKFLAG,TKPDEWT  SET FOR SPECIAL DIRECTORY WRITE 85287
         STH   R7,PDEUSD     SET DIRECTORY BLOCKS REQUIRED       85287
         LA    R5,DB         POINT TO A 12 BYTE WORK AREA        86293
         USING TRKCALC,R5    DECLARE IT                          86293
         XC    0(12,R5),0(R5)    CLEAR IT                        86293
         MVC   STARUCBA+1(3),VOUTUCB+1  COPY THE UCB ADDRESS     86293
         MVI   STARFLGS,4    UCB ADDRESS/TRKBAL REQ.             86293
         MVI   STARKL,8      KEYLEN=8                            86293
         MVI   STARDL,1      DATALEN=256  (/256)                 86293
         LR    R15,R7        COPY NUMBER OF DIRECTORY BLOCKS     86293
         SLR   R14,R14                                           86293
         SLR   R1,R1                                             86293
         IC    R1,DS4DEVDB   GET DIRECTORY BLOCKS PER TRACK      86293
         DR    R14,R1        GET RECORDS ON LAST TRACK IN R14    86293
         LTR   R4,R14        HOW MANY RECORDS ON LAST TRACK ?    86293
         BP    *+6           NOT FILLED TRACK                    86293
         LR    R4,R1         SET FOR FULL TRACK                  86293
         STC   R4,STARR      SET NUMBER OF RECORDS               86293
         SERVCALL DVCAP,TRKCALC  GET TRACK BALANCE FOR DIR.BLKS  86293
         OI    STARFLGS,STARUBAL  SET BALANCE PRESENT            86293
         LA    R4,1(,R4)     GET RECORD NUMBER OF EOF RECORD     86293
         STC   R4,STARR      SET RECORD NUMBER OF EOF            86293
         LA    R1,1          SET KEYLEN/DATALEN OF EOF           86293
         STCM  R1,7,STARKL    INTO KL/DL                         86293
         SERVCALL DVCAP,TRKCALC   GET TRACK BALANCE AFTER EOF    86293
         BXLE  R15,R15,COMPYEOF  ROOM FOR EOF                    86293
         DROP  R5                                                86293
         OI    PDEUSD,X'80'  SET NEW EOF-TRACK SWITCH            86293
COMPYEOF MVI   TRKUSD,X'7F'  COPY ON PDE COUNT                   91119
         SUBCALL TRAKCOPY                                        85287
         MVI   TRAKFLAG,TKQUIES  WAIT FOR ALL I/O TO COMPLETE    85287
         SUBCALL TRAKCOPY                                        85287
DIRNMIG  MVC   DLSTAR,DS1LSTAR   SAVE LSTAR BEFORE IEBCOPY       85280
         MVI   DS1NOBDB,0     MARK LAST BLOCK EMPTY              *UNG*
         CH    R7,=H'1'       ONE DIRECTORY BLOCK ?              *UNG*
         BNE   SETTRBAL       NO, DS1NOBDB SHOULD BE ZERO        *UNG*
         MVI   DS1NOBDB,14 14 USED BYTES IN LAST DIR. BLOCK      *UNG*
         B     SETTRBAL                                          86292
CMPMIGNC XC    DLSTAR,DLSTAR  CLEAR SAVED LSTAR                  86292
         XC    DS1LSTAR,DS1LSTAR  CLEAR NEW LSTAR                86292
         SPACE 1                                                 *UNG*
*  THE FOLLOWING CODE COMPUTES THE TRACK BALANCE OF THE LAST TRACK
*   CONTAINING DIRECTORY BLOCKS. THE CODE INCLUDE THE FIX OF PTF *UNG*
*   'UY71097'.                                                   *UNG*
*      ORIGINAL CODE REPLACED BY CALLS TO THE @SERVICE ROUTINE   82272
         SPACE 1                                                 *UNG*
SETTRBAL LA    R5,DB         POINT TO A 12 BYTE WORK AREA        82272
         USING TRKCALC,R5    DECLARE IT                          82272
         XC    0(12,R5),0(R5)    CLEAR IT                        82272
         MVC   STARUCBA+1(3),VOUTUCB+1  COPY THE UCB ADDRESS     82272
         MVI   STARFLGS,4    UCB ADDRESS/TRKBAL REQ.             82272
         MVI   STARKL,8      KEYLEN=8                            82272
         MVI   STARDL,1      DATALEN=256  (/256)                 82272
         SLR   R1,R1                                             82272
         IC    R1,DS1LSTAR+2   GET R OF EOF                      82272
         SH    R1,=H'1'      GET NUMBER OF DIRECTORY BLOCKS      82272
         BNP   SETTREOF      NONE ON THIS TRACK                  82272
         STC   R1,STARR      SET NUMBER OF RECORDS               82272
         SERVCALL DVCAP,TRKCALC  GET TRACK BALANCE FOR DIR.BLKS  82272
         OI    STARFLGS,STARUBAL  SET BALANCE PRESENT            82272
SETTREOF MVC   STARR,DS1LSTAR+2  SET RECORD NUMBER OF EOF        82272
         LA    R1,1          SET KEYLEN/DATALEN OF EOF           82272
         STCM  R1,7,STARKL    INTO KL/DL                         82272
         SERVCALL DVCAP,TRKCALC   GET TRACK BALANCE AFTER EOF    82272
         MVC   DS1TRBAL,STARBAL   SET TRACK BALANCE              82272
         DROP  R5                                                82272
FORMATOK LM    R15,R1,PPREBLOK  GET PRIOR AND CURRENT BLOCKS     81047
         ST    R1,NEXTADDR    SAVE, RESTORED LATER               *UNG*
         ST    R0,FMT1ADDR                                       81047
         ST    R15,FMT3ADDR                                      81047
         MVC   COPYEXT#,DS1NOEPV  SAVE NUMBER OF EXTENTS         79192
         MVC   BLOCKAD,FMT1ADDR   FORMAT 1 ADDRESS               *UNG*
         TM    COPYFLAG,NULLCOPY  EMPTY DSN ?                    79105
         BNZ   COMPOK0       DON'T BOTHER TO CHECK LSTAR         79105
         WRTVTOC FMT1         UPDATE DSCB                        *UNG*
         MVI   TRAKFLAG,TKQUIES  WAIT FOR WRITE TO FINISH        81047
         SUBCALL TRAKCOPY    WAIT FOR COMPLETION                 81047
         XC    COPYECB,COPYECB    CLEAR ECB                      *UNG*
* NOTE THAT THE SYSIN/SYSPRINT FILES ARE OVERRIDDEN WITH COMPIN/COMPOUT
*  COMPIN MUST BE ALLOCATED AS DUMMY. COMPOUT SHOULD BE A DUMMY EXCEPT
*              FOR PROBLEM DETERMINATION.                           .
         CLC   =C'SAS ',EPLOC  DOING SAS ?                       86293
         BNE   DOATTACH      NO; ALL MODULES IN AUTHORIZED LIBS  86293
         MODESET KEY=ZERO    ENABLE RSAPF=YES                    86293
         SERVICE APFOF       RESET OUR APF AUTHORIZATION         86293
DOATTACH ATTACH EPLOC=EPLOC,ECB=COPYECB,PARAM=(OPTLIST,DDLIST),VL=1,   *
               SZERO=NO,RSAPF=YES                                86293
         ST    R1,COPYTCB         SAVE TCB ADDRESS               *UNG*
         WAIT  ECB=COPYECB        WAIT FOR COMPLETION            *UNG*
         CLC   =C'SAS ',EPLOC  DOING SAS ?                       86293
         BNE   SOATTACH      NO; ALL MODULES IN AUTHORIZED LIBS  86293
         SERVICE APFON       SET OUR APF AUTHORIZATION           86293
         MODESET KEY=NZERO   RESET                               86293
SOATTACH DETACH COPYTCB                                          *UNG*
         SLR   R15,R15                                           *UNG*
         ICM   R15,7,COPYECB+1    GET RETURN CODE                *UNG*
         BZ    COMPOK         IF SUCCESSFUL COPY                 *UNG*
*  IEBCOPY FAILED, COMPLETE MESSAGE                              *UNG*
         ST    R15,DB         STORE IEBCOPY RETURN CODE          *UNG*
         UNPK  COPYRETC(7),DB+1(4)     UNPACK IT                 *UNG*
         TR    COPYRETC(6),TRTABLE   COVERT TO HEX               *UNG*
         MVI   COPYRETC+6,C'''' SET ENDING QUOTE                 *UNG*
         LA    R1,FAILMSG      ADDRESS OF ERROR MESSAGE          *UNG*
         B     COMPFAIL                                          *UNG*
         SPACE 2                                                 *UNG*
*   IEBCOPY COMPLETED SUCCESSFULLY. READ BACK FORMAT 1 DSCB,     *UNG*
*   FREE UNUSED EXTENTS AND REWRITE DSCBS IF NECESSARY.          *UNG*
         SPACE 1                                                 *UNG*
*   READ FORMAT 1 DSCB FROM OUTPUT VTOC                          *UNG*
COMPOK   MVC   FULL,FMT1ADDR   SET FORMAT 1 DSCB ADDRESS         81047
         READVTOC FMT1       REREAD FORMAT 1                     81047
         LA    R1,MSGCLOF    SET 'CLOSE FAILED' MSG              81047
         CLC   DLSTAR,DS1LSTAR  CLOSE FAILED IN IEBCOPY ?
         BE    COMPFAIL       LSTAR POINTS TO END OF DIRECTORY   *UNG*
         CLI   COPYEXT#,3    WAS THERE A FORMAT 3 ?              86292
         BNH   COMPOK0       NO; SKIP READING IT                 86292
         MVC   FULL,FMT3ADDR  SET FORMAT 3 DSCB ADDRESS          86292
         READVTOC FMT3       REREAD IT                           86292
COMPOK0  SLR   R4,R4                                             *UNG*
         IC    R4,DS1NOEPV NUMBER OF EXTENTS                     *UNG*
         LR    R3,R4         LOAD FOR NEXT TEST                  79192
         TM    DS1DSORG,JFCORGDA  SAS DATASET ?                  86292
         BNZ   UPDTFMT1      YES; DON'T TOUCH                    86292
         SLR   R2,R2                                             *UNG*
         ICM   R2,3,DS1LSTAR GET LAST USED 'TT'                  *UNG*
         LA    R2,1(,R2)      NUMBER OF USED TRACKS              *UNG*
         TM    OPTFLAG,OPTKEEP  KEEP UNUSED EXTENTS ?            79192
         BNZ   UPMIGTST      YES; SKIP EXTENT RELEASE UNLESS MIGRATE
         TM    DSLF2-DSLIST+DSLCURR,DSLKEEP  KEEP EXTENTS ?      81064
         BZ    EXTCLIP       NO; TRUNCATE AFTER LSTAR            86341
UPMIGTST TM    MIGFLAG,OPTMIGR+OPTMIGF  MIGRATING ?              86341
         BNO   UPDTFMT1      NO; LEAVE EXTENTS AS IS             86341
         L     R15,TRKOLD    GET TRACKS IN OLD DATASET           91119
         SLR   R0,R0                                             86341
         IC    R0,VINPDE     GET DIRECTORY ENTRIES PER TRACK     86341
         MR    R14,R0        HOW MANY WILL FIT ?                 86341
         IC    R0,VOUTDSCB   AND ON OUTPUT TRACK                 86341
         DR    R14,R0        GET EQUIVALENT TRACKS ON OUTPUT     86341
         LTR   R14,R14       ANY REMAINDER ?                     86341
         BZ    *+8           NO                                  86341
         LA    R15,1(,R15)   ALLOW FOR PARTIAL TRACK             86341
         MAX   R2,(R15)      USE LARGER OF USED OR EQUIVALENT SIZE
EXTCLIP  SLR   R3,R3          EXTENT COUNTER                     *UNG*
         SPACE 1                                                 *UNG*
*   SEE WHAT IEBCOPY DID TO OUR EXTENTS, AND FREE UNUSED ENTRIES.
         LA    R7,DS1EXT1   FIRST EXTENT                         *UNG*
         LA    R9,DS1EXT3    LAST EXTENT                         *UNG*
         LA    R8,10          INCREMENT                          *UNG*
EXTENT3  BAL   R6,TESTEXT     PROCESS THE EXTENT                 *UNG*
         BXLE  R7,R8,EXTENT3                                     *UNG*
         CLI   COPYEXT#,3    WAS THERE A FORMAT 3 ?              85289
         BNH   UPDTFMT1      NO; SKIP PROCESSING IT              85289
         OI    COPYFLAG,FMT3WORK MARK WORKING ON FORMAT 3 DSCB   *UNG*
         LA    R7,DS3EXTNT    FIRST EXTENT                       *UNG*
         LA    R9,10*(4-1)(,R7)    LAST EXTENT                   *UNG*
EXTENT4  BAL   R6,TESTEXT                                        *UNG*
         BXLE  R7,R8,EXTENT4                                     *UNG*
         LA    R7,DS3ADEXT   FIRST EXTENT                        *UNG*
         LA    R9,10*(9-1)(,R7)  LAST EXTENT                     *UNG*
EXTENT9  BAL   R6,TESTEXT                                        *UNG*
         BXLE  R7,R8,EXTENT9                                     *UNG*
         B     UPDTFMT1       GO UPDATE FORMAT 1 DSCB            *UNG*
         SPACE 2                                                 *UNG*
*  THE TESTEXT SUBROUTINE IS ENTERED FOR EACH EXTENT. THIS SUBROUTINE
*   UPDATES THE COUNTERS AND SETS THE APROPRIATE FLAGS.          *UNG*
         SPACE 1                                                 *UNG*
         USING EXTMAP,R7                                         81047
TESTEXT  CLI   XTTYPE,0      NOT AN EXTENT ?                     81047
         BER   R6             DO NOTHING                         *UNG*
         TM    COPYFLAG,FLUSH FLUSH MODE ?                       *UNG*
         BO    CALLFREE       YES, JUST FREE THE EXTENT          *UNG*
         LH    R1,XTHICYL     END CC, BYTE ALIGNMENT, 370 CPU FEATURE
         SH    R1,XTLOCYL     CYLS IN EXTENT                     81047
         MH    R1,VOUTTPC     NUMBER OF TRACKS                   85280
         AH    R1,XTHITRK     ADD LAST TT                        81047
         SH    R1,XTLOTRK     MINUS START TT                     81047
         LA    R1,1(,R1)      TRACKS IN EXTENT                   *UNG*
         LR    R14,R2        SAVE NUMBER OF TRACKS LEFT          85289
         SR    R2,R1          REMAINING USED TRACKS              *UNG*
         BP    MORETRK        IF NOT LAST USED EXTENT            *UNG*
         OI    COPYFLAG,FLUSH ENTER FLUSH MODE                   *UNG*
         TM    DSLF2-DSLIST+DSLCURR,DSLPSREL  RELEASE ALL ?      85289
         BNZ   TESTEXTF      YES; CHECK                          85289
         TM    OPTFLAG2,OPTPOREL  RELEASE ALL UNUSED SPACE ?     85289
         BZ    MORETRK       NO                                  85289
TESTEXTF SH    R14,=H'1'     MAKE RELATIVE                       85289
         BM    MORETRK       SHOULDN'T HAPPEN ?                  85289
         SRDA  R14,32        CLEAR HIGH REGISTER FOR DIVIDE      85289
         LH    R0,VOUTTPC    GET TRACKS/CYLINDER                 85289
         DR    R14,R0        GET TRACKS/CYLINDERS USED           85289
         AH    R15,XTLOCYL   RELOCATE                            85289
         AH    R14,XTLOTRK   RELOCATE                            85289
         CR    R14,R0        PAST CYLINDER END ?                 85289
         BL    TESTEXTO      NO                                  85289
         SR    R14,R0        SUBTRACT ONE CYLINDER'S WORTH       85289
         LA    R15,1(,R15)   AND ADD ONE CYLINDER                85289
TESTEXTO BCTR  R0,0          GET LAST TRACK NUMBER               85289
         CH    R0,XTHITRK    CYLINDER END ?                      85289
         BNE   TESTEXTK      NO; TRACK                           85289
         ICM   R0,12,XTLOTRK  START ON CYLINDER BOUND ?          85289
         BZ    TESTEXCY      YES; RESET END CYLINDER ONLY        85289
TESTEXTK STCM  R14,3,XTHITRK   TRUNCATE AFTER LAST TRACK         85289
TESTEXCY STCM  R15,3,XTHICYL   SET LAST USED CYLINDER            85289
         LA    R3,1(,R3)     SET USED EXTENTS UP                 85289
         B     CALLTHEE      AND TEST FOR DSCB 3 REWRITE         85289
MORETRK  LA    R3,1(,R3)      INCREMENT NUMBER OF USED EXTENTS   *UNG*
TESTNEXT BCTR  R4,R6          UPDATE AND TEST EXTENT COUNTER     *UNG*
         B     UPDTFMT1       ALL EXTENTS SCANNED, GO UPDATE FORMAT 1
CALLFREE XC    XTWHOLE,XTWHOLE  WIPE IT OUT                      81047
CALLTHEE TM    COPYFLAG,FMT3WORK WORKING ON FORMAT 3 ?           85289
         BNO   TESTNEXT       NO, GO TEST IF EXTENT COUNT EXHAUSTED
         OI    COPYFLAG,FMT3MODS FORMAT 3 MUST BE UPDATED        *UNG*
         B     TESTNEXT       AND GO TEST IF LAST EXTENT CHECKED *UNG*
         DROP  R7                                                81047
         SPACE 2                                                 *UNG*
*  AT THIS STAGE, ALL THE ORIGINAL EXTENTS HAD BEEN CHECKED. NOW THE
*  FORMAT 1 AND FORMAT 3 DSCBS ARE UPDATED AND WRITTEN TO THE OUTPUT
*  VTOC, AS NECESSARY.                                           *UNG*
         SPACE 1                                                 *UNG*
UPDTFMT1 XC    NEWEXTS+3*XTLEN(13*XTLEN),NEWEXTS+3*XTLEN  CLEAR  81047
         MVC   NEWEXTS(3*XTLEN),DS1EXT1   MOVE FIRST GROUP       81047
         CH    R3,=H'3'      NEED MORE ?
         BNH   UPDTFMT2      NO
         MVC   NEWEXTS+3*XTLEN(4*XTLEN),DS3EXTNT  MOVE SECOND GROUP
         MVC   NEWEXTS+7*XTLEN(9*XTLEN),DS3ADEXT  AND LAST       81047
UPDTFMT2 TM    DS1SCALO-FMT1+FMT2,JFCONTIG  USER CONTIG REQ?     81096
         BNZ   UPDTFMTA      YES; MAKE CONTIGUOUS                79192
         TM    MIGFLAG,OPTMIGR+OPTMIGF  MIGRATING ?              86341
         BO    UPDTFMTA      YES; CONSOLIDATE                    86341
         TM    OPTFLAG,OPTKEEP  KEEP UNUSED EXTENTS ?            79105
         BNZ   UPDTFMTB       YES; BUT WHY ?                     79105
         TM    DSLF2-DSLIST+DSLCURR,DSLKEEP  KEEP EXTENTS ?      81064
         BNZ   UPDTFMTB      YES; BOO                            81064
UPDTFMTA STC   R3,DS1NOEPV   SET EXTENTS FOR COMPRESSION         81064
         SUBCALL AMSXCOMP    COMPRESS CONTIGUOUS EXTENTS         81064
UPDTFMTB SUBCALL FIXEXTS     MOVE EXTENTS BACK TO DSCBS          81047
         IC    R3,DS1NOEPV   GET NEW EXTENT COUNT                81047
         CLM   R3,1,COPYEXT# SAME EXTENT NUMBER ?                81047
         BE    SAMEXT                                            *UNG*
         CLI   DS1NOEPV,3    ARE CURRENT EXTENTS FEWER THAN 4 ?  79105
         BH    SAMEXT        NO                                  79105
         CLI   COPYEXT#,3    WAS PREVIOUS NUMBER HIGHER ?        79192
         BNH   SAMEXT        NO; OK                              79192
         NI    COPYFLAG,255-FMT3MODS  DO NOT REWRITE 3           79105
         XC    DS1PTRDS,DS1PTRDS   DO NOT WRITE FMT3             79105
         CLI   DSLOTTR+2-DSLENT+DSLCURR,0  ABSOLUTE DSCB ?       81047
         BNE   SAMEXT        YES; NO TTR CHANGE                  81047
         MVC   NEXTADDR(8),FMT1ADDR  SLIDE ALL DSCBS DOWN ONE    79192
SAMEXT   MVC   DS1EXPDT(3),DS1EXPDT-FMT1+FMT2  RESTORE EXPDT     81047
         MVC   DS1DSSN(6),DS1DSSN-FMT1+FMT2  RESTORE VOLSER      81047
         MVC   DS1SYSCD(20),DS1SYSCD-FMT1+FMT2   RESTORE SYSCODE 81047
         MVC   DS1DSIND(1),DS1DSIND-FMT1+FMT2  RESTORE FLAGS     81047
         MVC   DS1DSNAM,DS1DSNAM-FMT1+FMT2  RESTORE DATASET NAME 81047
         MVC   DS1SCALO(4),DS1SCALO-FMT1+FMT2  RESTORE SECONDARY 81096
         AIF   ('&LOCAL' NE 'PID').NOCHA                         92279
         TM    IIHOPT,DSCHA  DOING CHANGED DATASETS ONLY ?       79325
         BZ    *+8           NO                                  79325
         NI    DS1DSIND,255-DS1DSCHA  RESET MODIFY BIT           81151
.NOCHA   TM    COPYFLAG,FMT3MODS MUST REWRITE FORMAT 3 DSCB ?    81096
         BNO   TESTFMT1                                          *UNG*
         CLI   DS1NOEPV,3    IS THERE A NEED FOR THIS ?          79192
         BNH   TESTFMT1      DON'T TRUST ME                      79192
         MVC   BLOCKAD,FMT3ADDR  DISK ADDRESS OF FORMAT 3 DSCB   *UNG*
         WRTVTOC FMT3         REWRITE FMT3 DSCB                  *UNG*
TESTFMT1 MVC   BLOCKAD,FMT1ADDR DISK ADDRESS OF FORMAT 1 DSCB    *UNG*
         WRTVTOC FMT1         REWRITE FMT1 DSCB                  *UNG*
GOODRET  MVC   BLOCKAD,NEXTADDR RESTORE ADDRESS OF NEXT DSCB     *UNG*
*  PRINT SUCCESSFUL COPY MESSAGE                                 *UNG*
         SLR   R9,R9          ZERO RETURN CODE                   *UNG*
         TM    OPTFLAG,OPTERSE  INDICATIVE MESSAGES ONLY ?       79105
         BNZ   MSGRETN       YES; SKIP MESSAGE                   79105
         LA    R1,MSGICOPY   SET FOR GOOD COMPRESS               81047
MSGRET   SUBCALL MSGIN       PRINT THE MESSAGE                   *UNG*
MSGRETN  SUBEX RC=(R9)       EXIT WITH RETURN CODE               81047
         SPACE 2                                                 *UNG*
*  THE FOLLOWING CODE RECEIVES CONTROL WHEN SOMETHING FAILS DURING THE
*  PROCESSING OF THIS DATASET.                                   *UNG*
*  R1 POINTS TO A MESSAGE AREA (AL1(LENGTH-1),CL(LENGTH)'TEXT')  81047
COMPFAYL CLOSE (COMPIN,,COMPOUT) CLOSE BOTH FILES                81047
         LA    R1,OPENMSG    SET FOR 'OPEN FAILED' MSG           81047
COMPFAIL MVC   BLOCKAD,IIHNEXT RESTORE NEXT DSCB ADDRESS         *UNG*
         MVC   FMT1(DS1END-FMT1),FMT2  RESTORE ENTIRE DSCB       81047
         MVC   TRKUSD,IIHUSED  RESTORE NUMBER OF TRACKS TO BE COP
         OI    CCODE+1,2     ALSO SET INTO COMPLETION CODE       79105
         LA    R9,4           SET NON-ZERO RETURN CODE           *UNG*
         B     MSGRET         MESSAGE AND RETURN                 *UNG*
         SPACE 2                                                 *UNG*
*  CONSTANTS USED IN THIS ROUTINE                                *UNG*
         SPACE 1                                                 *UNG*
COMPIN   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=E,EXLST=1 (JFCB EX)  86292
COMPOUT  DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=(E),EXLST=1          86292
OPENMSG  DC    AL1(L'OPENMSGT+8-1)  LENGTH OF NEXT MESSAGE       81047
OPENMSGT DC    C'%%%%%%%% - OPEN FAILED FOR DD '                 86292
OPENMSG# DC    CL8' '        DDNAME                              81047
DIRERMSG MSG   'INVALID DIRECTORY FORMAT, NOT COMPRESSED'        81047
MSGICOPY MSG   '  COMPRESSED BY %%%%%%%%'                        86292
FAILMSG  DC    AL1(L'FAILMSGT+L'COPYRETC-1)  MSG LENGTH          81047
FAILMSGT DC    C'%%%%%%%% FAILED, RETURN CODE IS: X'''           86292
COPYRETC DC    CL7' '                                            *UNG*
MSGCLOF  MSG   'CLOSE FAILED IN %%%%%%%%'                        86292
COPYECB  DC    A(0)                                              *UNG*
COPYTCB  DC    A(0)                                              *UNG*
NEXTADDR DC    A(0)          1 OF 3                              *UNG*
FMT1ADDR DC    A(0)          2 OF 3                              *UNG*
FMT3ADDR DC    A(0)          3 OF 3                              *UNG*
COPYFLAG DC    X'00'                                             *UNG*
FMT3MODS EQU   X'40'                                             *UNG*
FLUSH    EQU   X'20'                                             *UNG*
FMT3WORK EQU   X'10'                                             *UNG*
DIRERR   EQU   X'02'         DIRECTORY ERROR                     81047
NULLCOPY EQU   X'01'         NO MEMBERS IN PDS                   79105
COPYEXT# DC    X'0'          NUMBER OF EXTENTS BEFORE IEBCOPY    79192
DLSTAR   DC    XL3'0'        DS1LSTAR BEFORE IEBCOPY             81047
         SPACE 1                                                 81047
PATPDE   DC    0XL22'0',8X'FF',AL2(14)                           81047
MAXPDE   DC    0XL10'0',8X'FF',AL4(0)                            81047
         CNOP  2,4                                               *UNG*
OPTLIST  DC    H'0'                                              *UNG*
         CNOP  2,4                                               *UNG*
DDLIST   DC    Y(DDLEN)                                          *UNG*
DDS      DC    4XL8'0',CL8'COMPIN',CL8'COMPOUT'                  *UNG*
         DC    XL8'0'                                            81047
COPDDIN  DC    CL8'SYSUT1'   INPUT DD NAME                       81047
COPDDOUT DC    CL8'SYSUT2'   OUTPUT DD                           81047
DDLEN    EQU   *-DDS                                             *UNG*
         SPACE 1                                                 86292
SASIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=(WP),EXLST=SASEXLST   86292
SASREC   DC    CL80' '                                           86292
         ORG   SASREC                                            86292
         DC    C'PROC COPY IN='                                  86292
SASDDIN  DC    CL8'DISKIN? ',C'  OUT='                           86292
SASDDOUT DC    CL8'DISKOUT?',C' ; '                              86292
         ORG   ,                                                 86292
SASEXLST DC    0A(0),X'05',AL3(0),X'80',AL3(SASEXDCB)            86292
SASEXDCB DCBEXITD PREFIX=SA,FLAG1=(SA1NOFBS+SA1NOPCI+SA1TRUNC),        *
               DRECFM=128,BLOCKF=1,LRECLF=80                     86292
SASTTR   DC    A(256)        TTR=00000100                        86292
         LTORG                                                   *UNG*
         TITLE 'C O P Y V O L  ***  TRACK COPY ROUTINE'          81047
*        THIS CODE WILL COPY THE DATASET TRACK BY TRACK          79105
TRAKCOPY SUBENT BASE=(R10,R8)                                    87135
         ICM   R4,7,TRKCHAIN+1  HAVE A SPECIAL BUFFER CHAIN ?    83059
         BZ    TRAKCOPZ      NO; PROCEED NORMALLY                83059
         USING BUFFMAP,R4                                        83059
         ICM   R0,15,DWCHAIN   GET NEW CHAIN HEAD                83059
         BNZ   TRAKCOPX      NOT END                             83059
         ST    R0,TRKCHASE   SET LAST BUFFER                     83059
TRAKCOPX ST    R0,TRKCHAIN   SET NEW TOP                         83059
         MVI   DWECB,X'7F'   SET GOOD I/O                        81047
         CLI   DWTOGO,OFFUSERQ  AVAILABLE BUFFER ?               83059
         BNE   TRAKCOPZ      NO                                  83059
         MVI   DWTOGO,OFFAVAIL  MAKE BUFFER AVAILABLE            83059
         SPACE 1                                                 83059
TRAKCOPZ CLI   TRAKFLAG,TKQUIES  QUIESCE ONLY ?                  83059
         BNE   TRAKNQUI      NO                                  81064
         OI    TRAKMODE,TKLAST+TKQUIES  PROPAGATE                85280
         OC    TRAKFLAG,TRAKMODE  SET BOTH                       85287
         LA    R1,IOECBLST   GET ECB LIST                        83059
TRAKCOPQ L     R4,0(,R1)     GET BUFFER ADDRESS                  83059
         CLI   DWTOGO,OFFUSERQ  AWAITING USER ACTION ?           83059
         BNE   TRAKCOPP      NO                                  83059
         MVI   DWTOGO,OFFAVAIL  YES; MAKE BUFFER AVAILABLE       83059
         MVI   DWECB,X'7F'   PREVENT NO-WAIT WAIT                83059
TRAKCOPP TM    0(R1),X'80'   END OF LIST ?                       83059
         LA    R1,4(,R1)     SET TO NEXT ONE ANYWAY              83059
         BZ    TRAKCOPQ      NO; CHECK NEXT ENTRY                83059
         B     TRAKDONQ      SEE IF ALL DONE                     81064
TRAKNQUI MVC   TRAKMODE,TRAKFLAG  SAVE PROCESSING METHOD         85280
         TM    TRAKFLAG,TKCONTU  CONTINUE PRIOR REQUEST ?        85280
         BNZ   TRAKWAIT      YES; SCHEDULE SOMETHING             81047
         NI    TRAKFLAG,255-TKLAST  CLEAR 'LAST' FLAG            81047
         LA    R5,OLDEXTS      GET INPUT EXTENT ADDRESS IN 5     81047
         LA    R6,NEWEXTS      GET OUT EXTENT ADDRESS IN 6       81047
         MVC   INDKAD,XTLOCYL-EXTMAP(R5)  MOVE FIRST EXTENT      81047
         MVC   OTDKAD,XTLOCYL-EXTMAP(R6)  MOVE OUTPUT EXTENT     81047
         STM   R5,R6,SVR5R6  SAVE EXTENT ADDRESS                 81047
         LA    R2,IOECBLST   INITIALIZE BUFFERS                  81047
         L     R1,TRKUSD     CHECK TRACK REQUEST                 91119
         LTR   R1,R1                                             81047
         BP    TRAKSET       OK                                  81047
         LA    R1,1                                              81047
         ST    R1,TRKUSD     DO AT LEAST ONE                     91119
TRAKSET  L     R4,0(,R2)     GET ECB/BUFFER ADDRESS              81047
         USING BUFFMAP,R4    DECLARE IT                          81047
         CLI   DWTOGO,OFFDONE  DORMANT ?                         81047
         BNE   TRAKSETX      NO; TRY ANOTHER                     81047
         MVI   DWECB,X'7F'   READY FOR ACTION                    81047
         MVI   DWTOGO,OFFAVAIL  SET AVAILABLE FOR I/O            81047
TRAKSETX TM    0(R2),X'80'   LAST ECB ENTRY ?                    81047
         LA    R2,4(,R2)     BUMP ANYWAY                         81047
         BZ    TRAKSET       DO NEXT ONE                         81047
         B     TRAKWAIT      START UP                            81047
         SPACE 1                                                 81047
TRAKEXCP NI    DWFLAG,255-DWFERR-DWFER2-DWFERM  RESET ERROR BITS 83094
         CLI   DWIMBB,0      READING TAPE ?                     GP13433
         BH    TRAKEXC2        NO; DO NORMALLY                  GP13433
         READFDR TRACK,DWICCHH   SET REQUESTED ADDRESS          GP13433
         BXH   R15,R15,2     TEMPORARY - CHECK ERROR            GP13433
         CLI   4(R1),0       RECORD 0 IS EXPECTED               GP13433
         BNE   TRAKNO0         BUT NOT WANTED NOW               GP13433
         LA    R1,16(,R1)    SKIP R0                            GP13433
         SH    R0,=H'16'     AND ADJUST LENGTH                  GP13433
TRAKNO0  L     R2,=X'0000FFFF'                                  GP13433
         STH   R2,DWCCOUNT+6                                    GP13433
         SR    R2,R0         UNREAD LENGTH                      GP13433
         STH   R2,DWICSW+6   SET IT                             GP13433
         LR    R15,R0        COPY TRACK LENGTH                  GP13433
         LR    R14,R1        COPY INPUT ADDRESS                 GP13433
         LA    R0,DWCDATA    POINT TO TRACK IMAGE               GP13433
         LR    R1,R15        COPY LENGTH                        GP13433
         MVCL  R0,R14        COPY TRACK IMAGE                   GP13433
         INC   NUMEXCP       COUNT I/OS                         GP13433
         MVI   DWECB,X'7F'   POST IT COMPLETE                   GP13433
         CLI   DWTOGO,OFFRDSCB    READING A DSCB ?              GP13433
         BE    TRAKWOFF        YES; HONOR IT                    GP13433
         MVI   DWTOGO,OFFRDMUL  RETURN TO READ MULTIPLE CHECK   GP13433
         B     TRAKWOFF        AND DO SOMETHING WITH IT         GP13433
         SPACE 1
TRAKEXC2 MVI   DWECB,0       CLEAR ECB                           83094
         MVI   DWIOB,X'42'   CLEAR RESTART AND ERROR BITS        81047
         EXCP  DWIOB         START I/O                           81047
         INC   NUMEXCP       COUNT I/OS                          87135
TRAKWAIT TM    TRAKFLAG,TKCONTU  SPECIAL CHAINING ?              83059
         BZ    TRAKWAIL      NO                                  83059
         ICM   R4,15,TRKCHAIN   GET TOP OF CHAIN                 83059
         BZ    TRAKWAIL      NONE ?                              83059
         CLI   DWTOGO,OFFUSERQ  BUFFER READY FOR USER ?          83059
         BE    TRAKSBEX      YES; LET THE USER HAVE IT           83066
TRAKWAIL LA    R1,IOECBLST   GET ECB LIST                        83059
TRAKWAIF L     R4,0(,R1)     GET BUFFER ADDRESS                  81047
         TM    DWECB,X'40'   POSTED ?                            81047
         BNZ   TRAKWOFF      YES; PROCESS THE ENTRY              81047
         TM    0(R1),X'80'   END OF LIST ?                       81047
         LA    R1,4(,R1)     SET TO NEXT ONE ANYWAY              81047
         BZ    TRAKWAIF      TRY NEXT ENTRY                      81047
         TM    TRAKFLAG,TKLAST  LAST BUFFER SCHEDULED ?          81047
         BZ    TRAKWAIV      NO; WAIT FOR SOMETHING              81047
         TM    TRAKMODE,TKREAD+TKQUIES  QUIESCE REQUIRED ?       85280
         BZ    TRAKSBEX      EXIT (OVERLAP I/O AND PROCESSING)   81047
         LA    R1,IOECBLST   GET LIST AGAIN                      85287
TRAKWAIW L     R4,0(,R1)     GET NEXT BUFFER                     85287
         CLI   DWTOGO,OFFUSERQ  ON USER QUEUE ?                  85288
         BNE   TRAKWAIX      NO                                  85288
         TM    TRAKFLAG,TKQUIES  QUIESCING ?                     85288
         BZ    TRAKWAIX                                          85288
         MVI   DWECB,X'7F'   POST                                85288
         MVI   DWTOGO,OFFDONE  AVAILABLE                         85288
TRAKWAIX CLI   DWTOGO,OFFDONE  QUIESCED ?                        85288
         BNE   TRAKWAIV      NO; WAIT                            85287
         TM    0(R1),X'80'   END OF CHAIN ?                      85287
         LA    R1,4(,R1)     UP ANYWAY                           85287
         BZ    TRAKWAIW      NO; TRY NEXT                        85287
         B     TRAKSBEX      RETURN                              85287
TRAKWAIV WAIT  1,ECBLIST=IOECBLST  WAIT FOR SOMETHING            81047
         B     TRAKWAIT      SEE WHICH ONE IS POSTED             81047
TRAKWOFF IC    R15,DWTOGO    GET INDEX                           81047
         LA    R14,X'3C'     MAKE MASK                           81047
         NR    R15,R14       MASK IT                             81047
         B     *(R15)        BRANCH TO PROCESSOR                 81047
OFFSET   B     TRAKVAIL      BUFFER AVAILABLE FOR I/O            81047
OFFAVAIL EQU   *-OFFSET      BUFFER AVAILABLE                    81047
         B     TRAKDONE      FREEZE BUFFER WHILE AWAITING COMPLETION
OFFDONE  EQU   *-OFFSET      BUFFER AVAILABLE; NO MORE WORK      81047
         B     TRAKRCNT      SCHEDULE READ COUNT                 81047
OFFRDCNT EQU   *-OFFSET      SCHEDULE READ COUNT                 81047
         B     TRAKRDAT      SCHEDULE COUNT/KEY/DATA READ        81047
OFFRDATA EQU   *-OFFSET      SCHEDULE DATA READ                  81047
         B     TRAKPCNT      READ POST PROCESSING                81047
OFFRDPST EQU   *-OFFSET      READ POST PROCESSING                81047
         B     TRAKWDAT      PREPARE TO WRITE DATA               81047
OFFWDATA EQU   *-OFFSET      SCHEDULE DATA WRITE                 81047
         B     TRAKWDAD      PDE WRITE AFTER READ                81047
OFFWDATD EQU   *-OFFSET      OFFSET FOR DIRECTORY REWRITE        81047
         B     TRAKWRT0      WRITE CHECK; R0 COMPARE             81047
OFFWREC0 EQU   *-OFFSET      CHECK/SCHEDULE R0 REWRITE           81047
         B     TRAKUSWT      WAIT ON USER                        81047
OFFUWAIT EQU   *-OFFSET      WAIT FOR USER TO EMPTY BUFFER       81047
OFFUSERQ EQU   OFFDONE+1       WAIT FOR USER TO RELEASE          83059
         B     TRAKDSCR      DSCB READ CHECK                     81047
OFFRDSCB EQU   *-OFFSET      CHECK DSCB READ                     81047
         B     TRAKDSCW      DSCB WRITE CHECK                    81047
OFFWDSCB EQU   *-OFFSET      CHECK DSCB WRITE                    81047
         B     TRAKDUPL      RETURN FROM SEARCH KEY              81047
OFFRDUPE EQU   *-OFFSET       DUPLICATE DSCB CHECK               81047
         B     TRAKRDML      RETURN FROM READ MULTIPLE           87135
OFFRDMUL EQU   *-OFFSET        CREATE CCWS FOR WRITE             87135
         SPACE 2                                                 81047
TRAKVAIL L     R3,DWARDATA   GET READ DATA CHAIN                 81047
         MVI   16(R3),X'06'  SET TO READ DATA                    81047
         MVI   16+4(R3),X'40'  SET COMMAND CHAINING BACK         81208
         TM    TRAKFLAG,TKLAST   ALL TRACKS SCHEDULED ?          81047
         BNZ   TRAKDORM      YES; QUIESCE                        81047
         MVI   DWIDSN,C' '                                       83094
         MVC   DWIDSN+1(L'DWIDSN-1),DWIDSN  BLANK DSN            83094
         MVC   DWIDSN+4(4),VTOC+3   MOVE 'VTOC'                  83094
         TM    TRAKMODE,TKDUPE  SEARCH FOR DUPLICATE FMT 1 ?     85280
         BO    TRAKDUPE      YES                                 81047
         TM    TRAKMODE,TKDSCBRD  READ DSCB ?                    85280
         BO    TRAKRDSC      YES; SPECIAL                        81047
         TM    TRAKMODE,TKPDEWT  SPECIAL DIRECTORY WRITE ?       85287
         BO    TRAKVAIN      YES                                 85287
         TM    TRAKMODE,TKDSCBWT  WRITE DSCB ?                   85280
         BO    TRAKWDSC      YES; SPECIAL                        81047
         TM    TRAKMODE,TKVTOCRD  VTOC FULL-TRACK READ ?         85280
         BO    TRAKDSNN      YES; DON'T CLOBBER                  83094
TRAKVAIN LA    R1,DS1DSNAM   POINT TO EXPECTED DSN LOCATION      85287
         CLC   TRASH,DS1DSNAM  CLOBBERED ?                       83294
         BNE   TRAKDSNM      NO; MOVE IT                         83294
         LA    R1,DS1DSNAM-FMT1+FMT2   POINT TO SAVED DSN        83294
TRAKDSNM MVC   DWIDSN,0(R1)  SAVE NAME FOR ERROR MESSAGES        86292
TRAKDSNN XC    DWR0COUN(DWR0CLRN),DWR0COUN  CLEAR R0 FIELDS      83094
         MVC   DWINAD(4),INDKAD  SET INPUT ADDRESS               81047
         MVC   DWOUTAD(4),OTDKAD  SET OUTPUT ADDRESS             81047
         MVI   DWTOGO,OFFRDCNT  SCHEDULE FIRST READ ON IT        81047
         MVZ   DWFLAG,DSLFG-DSLIST+DSLCURR  SAVE DATASET TYPE FLAG
         NI    DWFLAG,DSLVSAM+DSLISAM+DSLUNM                     81047
         LM    R5,R6,SVR5R6    RESTORE REGS 5 & 6                81047
         L     R9,TRKUSD       GET TRACKS USED                   91119
         BCT   R9,TRAKVUP      REDUCE BY 1                       81047
         B     TRAKVONE        GO WAIT FOR LAST WRITE TO STOP    81047
TRAKVUP  ST    R9,TRKUSD       SAVE REMAINING TRACKS             91119
         CLC   OTDKAD,ZEROES   PHUNNY ?                          81047
         BE    TRAKVONE      YES; QUIT COPYING                   81047
         USING EXTMAP,R5     MAP INPUT EXTENT                    81047
         LH    R9,INDKAD+2     GET INPUT TRACK ADDR              81047
         LA    R9,1(,R9)        INCR BY 1                        81047
         CLI   XTTYPE,XTTSPLIT  SPLIT CYLINDER EXTENT ?          81047
         BNE   TRAKVSEQ      NO; SEQUENTIAL EXTENT               81047
         CH    R9,XTHITRK    IN RANGE ?                          81047
         BNH   TRAKVGD       YES; DO IT                          81047
         MVC   INDKAD+2(2),XTLOTRK  ELSE START WITH NEXT LOW TRACK
         LH    R9,INDKAD     GET CURRENT CYLINDER                81047
         LA    R9,1(,R9)     UP IT                               81047
         STH   R9,INDKAD     STASH IT BACK                       81047
         CH    R9,XTHICYL    DONE WITH THIS EXTENT ?             81047
         BNH   TRAKVOK       NO; DO IT                           81047
         B     TRAKVUPX      YES; TRY NEXT EXTENT                81047
TRAKVSEQ LH    R0,VINTPC     GET INPUT TRACKS/CYLINDER           85280
         TM    DSLCURR+DSLF2-DSLIST,X'08'  ARE WE DOING INPUT ?  85280
         BZ    *+8           YES; AS EXPECTED                    85280
         LH    R0,VOUTTPC    ELSE SPECIAL I/O                    85280
         CR    R9,R0            CHECK WITH HIGHEST TRACK         85280
         BL    TRAKVGD         IF LOW, GOOD                      81047
         LH    R9,INDKAD       GET CYL #                         81047
         LA    R9,1(,R9)        INCR                             81047
         STH   R9,INDKAD       STORE BACK                        81047
         SLR   R9,R9           CLEAR TRK #                       81047
TRAKVGD  STH   R9,INDKAD+2     SAVE TRACK #                      81047
         CLC   INDKAD,XTHICYL  CHECK EXTENT                      81047
         BNH   TRAKVOK         IF NOT HIGH THIS EXTENT STILL GOOD
TRAKVUPX LA    R5,XTLEN(,R5)  POINT AT NEXT EXTENT               81047
         CLI   XTTYPE,0      END OF REQUEST ?                    81047
         BE    TRAKVONE      YES; QUIESCE                        81047
         TM    TRAKMODE,TKPDE  DOING DIRECTORY COPY ?            85280
         BO    TRAKVONE      YES; STAY IN FIRST EXTENT           81047
         MVC   INDKAD,XTLOCYL   MOVE FIRST TRACK ADDR            81047
         DROP  R5                                                81047
         USING EXTMAP,R6                                         81047
TRAKVOK  LH    R9,OTDKAD+2     GET OUTPUT TRACK #                81047
         LA    R9,1(,R9)        INCR                             81047
         CH    R9,VOUTTPC       CHECK WITH HIGHEST TRACK         85280
         BL    TRAKVOGD        IF LOW, GOOD                      81047
         LH    R9,OTDKAD       GET CYLINDER #                    81047
         LA    R9,1(,R9)        INCR                             81047
         STH   R9,OTDKAD        STORE CYL# BACK                  81047
         SLR   R9,R9           CLEAR 9                           81047
TRAKVOGD STH   R9,OTDKAD+2     SAVE TRK                          81047
         CLC   OTDKAD,XTHICYL  CHECK WITH EXTENT END             81047
         BNH   TRAKVONC        IF NOT HIGH, ALL IS WELL          81047
         LA    R6,XTLEN(,R6)  POINT TO NEXT EXTENT               81047
         MVC   OTDKAD,XTLOCYL  MOVE NEW EXTENT START             81047
         B     TRAKVONC      SKIP AROUND                         81047
TRAKVONE OI    TRAKFLAG,TKLAST  NO MORE AFTER THIS               81047
TRAKVONC STM   R5,R6,SVR5R6  SAVE EXTENT ADDRESS                 81047
         TM    TRAKMODE,TKPDE   DOING DIRECTORY ?                85280
         BO    TRAKVONH      YES, PUT ON CHAIN                   81047
         TM    TRAKMODE,TKPDEWT  SPECIAL DIRECTORY WRITE ?       85287
         BO    TRAKPDEW      YES                                 85287
         TM    TRAKMODE,TKVTOCRD  VTOC READ ?                    85280
         BNO   TRAKRCNT      NO; READ                            81047
TRAKVONH ICM   R1,15,TRKCHASE   IS CHAIN EMPTY ?                 83059
         BNZ   TRAKVCHN      NO                                  83059
         STCM  R4,7,TRKCHAIN+1  SET CHAIN TOP                    83059
         LA    R1,TRKCHASE-DWCHAIN+BUFFMAP   SET CHAIN END       83059
TRAKVCHN SLR   R0,R0                                             83059
         STCM  R4,7,DWCHAIN+1-BUFFMAP(R1)  ADD TO END OF CHAIN   83059
         STCM  R4,7,TRKCHASE+1  SET END BUFFER POINTER           83059
         ST    R0,DWCHAIN    TERMINATE CHAIN                     81047
         OI    TRAKFLAG,TKCONTU  SET FOR CONTINUATION            81047
         B     TRAKRCNT      DO MORE                             81047
         SPACE 1                                                 81047
TRAKDORM MVI   DWTOGO,OFFDONE  SET COMPLETE                      81047
         OI    TRAKFLAG,TKLAST   SET LAST WRITE SWITCH           79105
         SPACE 2                                                 81047
TRAKDONE MVI   DWECB,0       WON'T GET POSTED                    81047
         MVI   DWTOGO,OFFDONE  SET COMPLETE FROM USERQ           83059
TRAKDONQ LA    R1,IOECBLST   POINT TO LIST                       81064
TRAKDLOP L     R4,0(,R1)     GET ECB                             81047
         CLI   DWTOGO,OFFDONE  OTHER THAN DONE ?                 81047
         BNE   TRAKWAIT      YES; WAIT FOR COMPLETION            81047
         TM    0(R1),X'80'   END OF ECB LIST ?                   81047
         LA    R1,4(,R1)     UP IT ANYWAY                        81047
         BZ    TRAKDLOP      NO; CHECK REMAINDER                 81047
TRAKSBEX SUBEX ,             EXIT NORMALLY                       81047
         SPACE 2                                                 81047
TRAKRCNT MVI   DWTOGO,OFFRDPST  SET FOR POST-PROCESS AFTER I/O   81047
         MVN   DWIMBB(1),DSLCURR+DSLF2-DSLIST  SET FOR INPUT VOLUME
         MVC   DWICCW,DWARDCNT  READ COUNT CHAIN TO IOBSTART     81047
         MVC   DWICCHH,DWINAD   MOVE TRACK ADDRESS TO IOBSEEK+   81047
         TM    TRAKMODE,TKVTOCWT  WRITE VTOC ?                   85280
         BO    TRAKVTOC      YES; SPECIAL                        81047
         CLC   DWOUTAD(4),ZEROES  SEE IF TRACK ZERO              81047
         BNE   TRAKRCMT      IF NOT, LET THE READ GO             87135
         TM    TRAKMODE,TKIPL  DOING IPL TRACK ?                 85280
         BO    TRAKRCMT      YES; PERMIT COPY                    87135
         PRTV  ZEROTMSG      PRINT SPECIAL ERROR MESSAGE         83094
         NI    DWFLAG,255-DWFERM  FORCE MESSAGE PRINTING         83094
         EMP   RETRY=NO      PRINT MESSAGE AND DUMP              83094
ZEROTMSG VCON  '-***** PROGRAM ERROR - ATTEMPT TO WRITE TRACK 0 *****'
         SPACE 2                                                 81047
TRAKRCMT SLR   R1,R1                                             87135
         IC    R1,FULL+3     GET VOLUME INDEX                    87135
         MH    R1,=Y(VIN2DDNM-VIN1DDNM)  GET OFFSET              87135
         L     R1,VIN1SIZE(R1)  LOAD RDM SIZE AND FLAGS          87135
         LTR   R1,R1                                             87135
         BNP   TRAKRMBY      BYPASS READ MULTIPLE                87135
         MVC   DWICCW,DWARDATA  USE READ CKD CCWS                87135
         LM    R14,R15,=X'5E00000020000000'  PATTERN READ MULTIPLE
         O     R14,DWADATA   DATA ADDRESS                        87135
         OR    R15,R1        GET (O/P) TRACK CAPACITY            87135
         STM   R14,R15,DWCCOUNT  MAKE FINAL CCW                  87135
         MVI   DWTOGO,OFFRDMUL  RETURN TO READ MULTIPLE CHECK    87135
         B     TRAKEXCP      READ THE TRACK                      87135
         SPACE 1                                                 87135
TRAKRDML CLI   DWECB,X'7F'   NORMAL COMPLETION ?                 87135
         BE    TRAKRDM2      YES; BUILD CCWS                     87135
*        CLI   DWECB,X'41'   OTHER ERROR ?                       87135
*        BNE   TRAKRMFL      TREAT AS FAILURE                    87135
*        CLC   =X'0C40',DWICSW+4  OVERFLOW RECORD ?              87135
*        BNE   TRAKRMFL      NO; DO IT THE HARD WAY              87135
TRAKRMFL INC   NUMRMBAD      UNSUCCESSFUL READ MULTIPLES         87135
TRAKRMBY MVI   DWTOGO,OFFRDPST  SET FOR POST-PROCESS AFTER I/O   87135
         MVC   DWICCW,DWARDCNT  READ COUNT CHAIN TO IOBSTART     87135
         B     TRAKEXCP      RUN THE READ COUNT CCWS             87135
         SPACE 1                                                 87135
TRAKRDM2 L     R5,DWADATA    START OF INPUT BUFFER               87135
         L     R6,DWACOUNT   START OF CCWS TO BE BUILT           87135
         SLR   R7,R7                                             87135
         ICM   R7,3,DWCCOUNT+6  REQUESTED INPUT LENGTH           87135
         SLR   R14,R14                                           87135
         ICM   R14,3,DWICSW+6   LENGTH NOT READ                  87135
         SR    R7,R14        LENGTH READ                         87135
         BM    TRAKRMFL      SHOULD NOT HAPPEN                   87135
         SLR   R9,R9         SET EXPECTED RECORD NUMBER (-1)     87135
         L     R1,=X'40000000'  CMD CHAIN                        87135
         L     R2,=X'1E000000'  READ CKD (CHANGED TO WRITE CKD)  87135
         OR    R2,R5         START OF DATA                       87135
         LA    R0,8          MAKE CONSTANT                       87135
TRAKRMLP LTR   R7,R7         ANY MORE DATA ON TRACK ?            87135
         BM    TRAKRMFL      ERROR                               87135
         BZ    TRAKRMND      END OF CHAIN                        87135
         LA    R9,1(,R9)     NEXT RECORD EXPECTED                87135
         CLM   R9,1,4(R5)    MATCH ?                             87135
         BNE   TRAKRMFL      NO; FAIL                            87135
         CLC   0(4,R5),DWINAD  RIGHT TRACK ?                     87135
         BNE   TRAKRMFL      NO; FAIL                            87135
         SLR   R14,R14                                           87135
         SLR   R15,R15                                           87135
         IC    R14,5(,R5)    GET KEY LENGTH                      87135
         ICM   R15,3,6(R5)   AND DATA LENGTH                     87135
         LA    R14,8(R14,R15)  GET WRITE LENGTH                  87135
         LR    R3,R14        COPY THE LENGTH                     87135
         OR    R3,R1         ADD THE COMMAND CHAIN BIT           87135
         STM   R2,R3,0(R6)   MAKE THE NEW CCW                    87135
         LTR   R15,R15       ANY DATA LENGTH ?                   87137
         BNZ   *+8           YES                                 87137
         MVI   0(R6),X'12'   SET FAKE READ COUNT FOR EOF         87137
         AR    R6,R0         POINT TO THE NEXT CCW               87135
         AR    R5,R14        NEXT DATA ADDRESS                   87135
         ALR   R2,R14        NEXT DATA ADDRESS IN CCW            87135
         SR    R7,R14        RESIDUAL TRACK LENGTH               87135
         BP    TRAKRMLP      GO FOR MORE                         87135
TRAKRMND MVC   0(8,R6),NOPCCW  MAKE END OF CHAIN                 87138
         INC   NUMRMOK       COUNT SUCCESSFUL READS              87135
         B     TRAKWDAT      JOIN NORMAL READ COMPLETION         87135
         SPACE 1                                                 87135
TRAKPCNT CLI   DWECB,X'41'     SEE IF ERROR DETECTED             81047
         BE    TRAKPCN8      YES; EXPECTED COMPLETION            81047
         CLI   DWECB,X'7F'   IN RARE CASES                       81047
         BE    TRAKPCNF        THE TRACK IS REALLY CRUMMY        81047
         B     TRAKPCNX      FAIL (OR RETRY)                     83094
TRAKPCN8 CLI   DWIOB+3,X'08'   SEE IF NO RECORD FOUND            81047
         BE    TRAKPCNF      YES; EXPECTED CONDITION             83094
TRAKPCNX EMP   TRAKPERR      PROCESS THE ERROR                   83094
         SPACE 1                                                 83094
TRAKPERR L     R14,DWARDCNT  POINT TO FIRST CCW                  83094
         L     R6,SVIOACCW   GET FAILING CCW ADDRESS             83094
         LA    R14,DWCRDCCW-DWCRDCNT(,R14)  POINT TO R0 CCW      83094
         LA    R7,0(,R6)        FINAGLE TO GET                   83094
         LA    R6,8(,R6)     SET RESTART CCW ADDRESS             83094
         SR    R7,R14           OFFSET FROM R0                   83094
         SRA   R7,3          FAILING RECORD NUMBER               83094
         BM    TRAKPKIP        NOT EVEN R0 READ                  83094
         BP    TRAKPERT      TRUNCATE TRACK AFTER GOOD DATA      83094
         STCM  R6,7,DWCNUCCW+1  SET RECOVERY TIC TO R1 CCW       83094
         LA    R6,DWCERCCW                                       83094
         C     R6,DWICCW     ALREADY IN RETRY ?                  83094
         BNZ   TRAKPKIP      YES; GIVE UP ON THIS ONE            83094
         ST    R6,DWICCW     SET CCW START                       83094
         B     TRAKEXC2      RETRY                               83094
TRAKPKIP SLR   R7,R7         FAKE AN EMPTY TRACK                 83094
TRAKPERT PRTLIST MSGTKNCP    SHOW TRACK NOT COPIED               83094
         B     TRAKPLIP      JOIN COMMON CODE                    83094
         SPACE 1                                                 83094
TRAKPCNF L     R7,DWICSW     GET LAST CCW ADDRESS+8              81047
         LA    R7,0(,R7)        CLEAR HIGH BYTE                  81047
         L     R6,DWARDCNT   GET COUNT CHAIN ADDRESS             81047
         SH    R7,=AL2(DWCRDCCW-DWCRDCNT)  ALLOW FOR PREFIX      81047
         SR    R7,R6           SUBTRACT STARTING ADDRESS         81047
         BM    TRAKPCNX      GO TO ERROR RETRY IF NO R0          83094
         SPACE 1                                                 *AMS*
*        R9 CODE CHECK REQUIRES RECORD NUMBERS TO BE SEQUENTIAL AND
*        TERMINATES CHAIN IF INVALID RECORD NUMBER FOUND. NORMAL WHEN
*        ERROR RECOVERY RESTARTS ENTIRE REQUEST.                 83094
*                                                                *AMS*
TRAKPLIP SLR   R9,R9         CLEAR RECORD NUMBER                 83094
         L     R6,DWACOUNT   GET COUNT FIELDS                    81047
         L     R2,=X'1E000000'  SET READ CKD OPCODE              81047
         O     R2,DWADATA    SET TO START OF DATA AREA           81047
         L     R1,=X'40000000'  SET COMMAND CHAINING             81047
         LA    R0,8          SET CONSTANT                        81047
         SRA   R7,3          DIVIDE BY 8; SET CC=0 IF NO RCD 1   83094
         BNP   TRAKPKIL      NONE; JUST TERMINATE THE CHAIN      81047
TRAKPLOP LA    R9,1(,R9)     UP EXPECTED RECORD NUMBER           81047
         CLM   R9,1,4(R6)    RECORD NUMBER IN COUNT FIELD MATCHES ?
         BNE   TRAKPKIL      NO; TERMINATE THE CHAIN             81047
         SLR   R14,R14                                           81047
         IC    R14,5(,R6)    GET KEY LENGTH                      81047
         SLR   R15,R15         KEEP MAD MAN METZ FROM KVETCHING  83094
         ICM   R15,3,6(R6)       ABOUT 3380 DATA LENGTH          83094
         AR    R15,R14       GET COMBINED KEY AND DATA LENGTH    83129
         LA    R14,8(,R15)   LENGTH OF COUNT/KEY/DATA READ       83129
         LR    R3,R14        COPY LENGTH                         81047
         OR    R3,R1         ADD COMMAND CHAIN BIT               81047
         STM   R2,R3,0(R6)   REPLACE COUNT FIELD BY CCW          81047
         LTR   R15,R15       ANY KEY/DATA LENGTH ?               81047
         BNZ   TRAKPKLN      YES                                 81047
         MVI   0(R6),X'12'   SET FOR READ COUNT ONLY             81047
         TM    TRAKMODE,TKPDE  DOING DIRECTORY ?                 85280
         BNO   TRAKPKLN      NO                                  83129
         OI    TRAKFLAG,TKLAST  NO MORE I/O REQUIRED             81047
TRAKPKLN ALR   R2,R14        MAKE NEXT DATA ADDRESS              81047
         AR    R6,R0         NEXT COUNT FIELD ADDRESS            81047
         BCT   R7,TRAKPLOP   TRY AGAIN                           81047
TRAKPKIL MVC   0(8,R6),NOPCCW  TERMINATE CHANNEL PROGRAM         81047
         MVC   DWICCW,DWARDATA  SET TO READ DATA                 81047
         MVI   DWTOGO,OFFRDATA  SET FOR DATA READ                81047
         B     TRAKEXCP      FIRE OFF I/O                        81047
         SPACE 2                                                 81047
TRAKRDAT CLI   DWECB,X'7F'     LOOK GOOD?                        81047
         BE    TRAKRDOK      IF GOOD, THEN ALL IS WELL           81047
         CLI   DWECB,X'41'   COMPLETE WITH ERRORS ?              81047
         BNE   TRAKRDOX        NOT EXPECTED ERROR; BOMB          83094
         CLC   DWICSW+4(2),=X'0C40'  INCORRECT LENGTH WITHOUT UNIT CHK
         BE    TRAKRDOV      YES; SET OVERFLOW PROCESSING        83094
         CLC   =X'0D000000',DWICSW+4   EOF WITH KEY ?            83129
         BNE   TRAKRDOX      NO; ERROR                           83129
         LA    R14,0(,R4)    GET CLEANED BUFFER ADDRESS          83129
         L     R15,DWICSW    GET LAST CCW+                       83129
         LA    R15,0(,R15)   CLEAN IT                            83129
         SH    R15,=H'8'     BACK UP                             83129
         CR    R15,R14       IN BUFFER ?                         83129
         BNH   TRAKRDOX      NO; BAD ADDRESS                     83129
         LA    R14,4095(,R14)                                    83129
         LA    R14,2049(,R14)                                    83129
         CR    R15,R14       STILL WITHIN BUFFER ?               83129
         BNL   TRAKRDOX      NO; FAIL                            83129
         L     R14,DWACOUNT  GET R1 CCW                          83129
         LA    R1,8(,R15)       FINAGLE TO GET                   83129
         SR    R1,R14           OFFSET FROM R0                   83129
         SRA   R1,3          FAILING RECORD NUMBER               83129
         BNP   TRAKRDOX        NOT EVEN R1 READ                  83129
         CLI   0(R15),3      FAILED ON NO-OP CCW ?               83129
         BE    TRAKRDOX      YES?  NOT EXPECTED                  83129
         CLI   0(R15),X'1E'  FAILED ON READ COUNT/KEY/DATA ?     83129
         BNE   TRAKRDOX      NO; DON'T KNOW WHAT IT IS ?         83129
         STC   R1,DWICCHH+4  UPDATE SEEK ADDRESS                 83129
         L     R2,0(,R15)    GET COUNT FIELD ADDRESS             83129
         MVC   0(5,R2),DWICCHH  MAKE COUNT ADDRESS               83129
         CLI   8(R15),3      IS NEXT THE NO-OP CCW ?             83129
         BE    TRAKRDOK      YES; NO I/O REQUIRED                83129
         LA    R14,8(,R15)   SET RESTART CCW                     83129
         STCM  R14,7,DWCNUCCW+1  INTO RECOVERY TIC               83129
         LA    R6,DWCERCCW                                       83129
         ST    R6,DWICCW     SET CCW START                       83129
         B     TRAKEXC2      RETRY                               83129
TRAKRDOX EMP   TRAKRERR      GO TO ERROR RETRY PROCESSING        83094
         SPACE 1                                                 83094
TRAKRERR L     R14,DWACOUNT  GET R1 CCW                          83094
         L     R15,SVIOACCW  GET FAILING CCW ADDRESS             83094
         CLI   0(R15),3      FAILED ON NO-OP CCW ?               83094
         BE    TRAKRENR      YES, CONTINUE.                      83094
         LA    R1,8(,R15)       FINAGLE TO GET                   83094
         SR    R1,R14           OFFSET FROM R0                   83094
         SRA   R1,3          FAILING RECORD NUMBER               83094
         BM    TRAKSKIP        NOT EVEN R0 READ                  83094
         STC   R1,DWICCHH+4  UPDATE SEEK ADDRESS                 83094
         BZ    TRAKRER0      SKIP FAKE COUNT BUILD               83094
         L     R2,0(,R15)    GET COUNT FIELD ADDRESS             83094
         MVC   0(5,R2),DWICCHH  MAKE COUNT ADDRESS               83094
TRAKRER0 CLI   8(R15),3      IS NEXT THE NO-OP CCW ?             83094
         BE    TRAKRENR      YES; NO I/O REQUIRED                83094
         LA    R14,8(,R15)   SET RESTART CCW                     83094
         STCM  R14,7,DWCNUCCW+1  INTO RECOVERY TIC               83094
         LA    R6,DWCERCCW                                       83094
         ST    R6,DWICCW     SET CCW START                       83094
         B     TRAKEXC2      RETRY                               83094
TRAKSKIP L     R14,DWACOUNT  POINT TO R1 CCW                     83094
         MVC   0(8,R14),NOPCCW   FAKE EMPTY TRACK                83094
         PRTLIST MSGTKNCP    SHOW TRACK NOT COPIED               83094
*        B     TRAKRENR                                          83094
         SPACE 1                                                 83094
TRAKRENR MVI   DWECB,X'7F'   FAKE NORMAL COMPLETION              83094
         B     TRAKRDOK      CONTINUE PROCESSING                 83094
         SPACE 1                                                 83094
MSGTKNCP FDPRT '***** TRACK',NL,PADR                             83094
         FDPRT DWICCHH,2,HEX                                     83094
         FDPRT '.'                                               83094
         FDPRT DWICCHH+2,2,HEX                                   83094
         FDPRT 'NOT COPIED CORRECTLY *****',PADL                 83094
         FDPRT *END                                              83094
         SPACE 1                                                 83094
TRAKRDOV OI    DWFLAG,TKOVER   TURN ON OVERFLOW SWITCH           83094
TRAKRDOK MVI   DWTOGO,OFFWDATA  GO TO WRITE                      81047
*        B     TRAKWAIT      LOOK FOR WORK                       81047
         SPACE 2                                                 81047
TRAKWDAT TM    TRAKMODE,TKVTOCRD  VTOC READ ?                    85280
         BO    TRAKWDAW                                          81047
         TM    TRAKMODE,TKPDE  DOING DIRECTORY ?                 85280
         BNO   TRAKWDAR      NO                                  81047
TRAKWDAW MVI   DWTOGO,OFFUWAIT  PUT ON PROCESSING CHAIN          81047
         TM    TRAKMODE,TKVTOCRD  READ A VTOC TRACK ?            87135
         BNO   TRAKWAIT      NO; CHAIN ANOTHER                   87135
         TM    OPTFLAG,OPTSTACK  VTOC STACK OPTION ?             87135
         BZ    TRAKWAIT      NO                                  87135
         CLI   DWIMBB,8      DOING AN INPUT VOLUME ?             87135
         BNL   TRAKWAIT      NO                                  87135
         SLR   R2,R2                                             87135
         SLR   R1,R1                                             87135
         IC    R1,FULL+3     GET EXTENT                          87135
         MH    R1,=Y(VIN2DDNM-VIN1DDNM)  GET OFFSET              87135
         IC    R2,VIN1DSCB(R1)  GET DSCBS PER TRACK              87135
         MH    R2,=Y(DS1END-DS1DSNAM+8)  SPACE REQUIRED          87135
         LA    R3,STKDATA-STACK(,R2)  TRUE SIZE                  87135
         GETMAIN RC,LV=(R3),SP=3  GET A BUFFER                   87135
         BXH   R15,R15,TRAKWAIT  REGION TOO SMALL ?              87135
         LR    R5,R1         COPY NEW BUFFER ADDRESS             87135
         USING STACK,R5      DECLARE IT                          87135
         XC    0(STKDATA-STACK,R1),0(R1)  CLEAR PREFIX           87135
         ST    R3,STKSPLEN   SET GETMAIN LENGTH                  87135
         MVI   STKSPLEN,3    SET SUBPOOL                         87135
         MVC   STKCCHH,DWICCHH  COPY HARDWARE ADDRESS            87135
         ST    R2,STKLEN     SET SIZE MOVED                      87135
         LA    R14,STKDATA   GET DATA LOCATION                   87135
         LR    R15,R2        AND LENGTH                          87135
         L     R0,DWADATA    GET ADDRESS OF READ DATA            87135
         LR    R1,R15        SAME LENGTH                         87135
         MVCL  R14,R0        SAVE IT                             87135
         MVC   ABSAD(4),STKCCHH  GET ABSOLUTE ADDRESS            87135
         MVC   ABSHD,DWIMBB  COPY EXTENT INDEX                   87135
         LA    R1,ABSHD                                          87135
         BAL   R9,ABSTTR     CONVERT TO TTR                      87135
         ST    R0,STKTTRZ    STASH TT AND Z; R NOT SET           87135
         MVC   STKLINK,STACKAD  CHAIN OLD ENTRY                  87135
         ST    R5,STACKAD    AND ADD NEW ONE                     87135
         DROP  R5                                                87135
         INC   NUMSTACK      COUNT STACKED BUFFERS               87135
         B     TRAKWAIT      DO SOME MORE                        81047
TRAKWDAR TM    DWFLAG,DSLVSAM+DSLISAM+DSLUNM  UNMOVABLE ?        81047
         BNZ   TRAKWDAZ      YES; GO TO WRITE IT OUT AS IS       81047
         CLC   DWR0DATA(4),DWINAD      DISK ADDRESS IN INPUT?    81047
         BNE   TRAKWDAZ        IF NOT, THEN LEAVE IT             81047
         MVC   DWR0DATA(4),DWOUTAD  MOVE IN OUTPUT TRACK ADDRESS 81047
TRAKWDAZ MVC   DWR0ODAT,DWR0DATA  SET R0 DATA FROM OLD           81208
         TM    TRAKMODE,TKIPL  DOING IPL TRACK ?                 85280
         BNO   TRAKWDAD      NO                                  81047
         L     R6,DWARDATA   GET WRITE CHAIN ADDRESS             81208
         L     R6,DWCCOUNT-DWCRDATA+16(,R6)   GET R3 COUNT/KEY/DATA
         CLC   =X'03040050E5D6D3F1',4(R6) PROPER COUNT FIELD ?   81096
         BNE   TRAKBIPL      NO; BAD IPL RECORD                  81090
         CLC   =C'VOL1',8+4(R6)   MAKE SURE IT'S THE VOLUME LABEL
         BNE   TRAKBIPL      NO ?                                81090
         MVC   8+4+4(6,R6),VOUTVSER  SET OUTPUT VOLUME SERIAL    81047
         MVC   8+4+11(4,R6),DS4VTOCE+2  SET VTOC EXTENT          81047
         MVI   8+4+11+4(R6),1  SET RECORD 1                      81047
         MVC   ABSAD(5),8+4+11(R6)  COPY ABSOLUTE ADDRESS        81047
         MVI   ABSHD,9       SET VTOC EXTENT                     85296
         LA    R1,ABSHD                                          81047
         BAL   R9,ABSTTR     CONVERT TO TTRZ                     81047
         LR    R9,R0         SAVE TTR                            81047
         MODESET KEY=ZERO                                        81047
         L     R3,VOUTUCB    GET OUTPUT UCB                      81047
         STCM  R9,14,UCBVTOC-UCBOB(R3)  UPDATE UCB VTOC ADDRESS  81047
         MODESET KEY=NZERO   RESTORE                             81047
TRAKWDAD MVI   DWIMBB,8      SET FOR OUTPUT VOLUME               81047
         MVC   DWICCHH,DWOUTAD  UPDATE SEEK ADDRESS              81047
         L     R6,DWACOUNT   GET INPUT CCW CHAIN                 81047
         LA    R0,8          MAKE CONSTANT                       81047
TRAKWNOP CLI   0(R6),3       END OF CHAIN ?                      81047
         BE    TRAKWDON      YES; QUIT                           81047
         L     R9,0(,R6)     GET COUNT FIELD ADDRESS             81047
         MVC   0(4,R9),DWOUTAD  UPDATE THE RECORD ADDRESS        81047
         MVI   0(R6),X'1D'   CHANGE CCW TO WRITE CKD             81047
         TM    4(R6),X'40'   COMMAND CHAIN ?                     81047
         AR    R6,R0         UP TO NEXT                          81047
         BNZ   TRAKWNOP      YES; TRY AGAIN                      81047
TRAKWDON TM    DWFLAG,TKOVER  OVERFLOW ?                         81047
         BZ    TRAKWNOV      NO                                  81047
         NI    DWFLAG,255-TKOVER  RESET IT                       81047
         SR    R6,R0         GET LAST CCW                        81047
         CLI   0(R6),X'1D'   REALLY WRITE CKD ?                  81047
         BNE   *+8           NO                                  81047
         MVI   0(R6),X'01'   CHANGE TO WRITE SPECIAL             81047
TRAKWNOV TM    WYLFLAG,WYLCON+WYLDSN  WYLBUR CONVERT ?           81047
         BNO   TRAKWNOC      NO                                  81047
         SUBCALL WYLCONV     ATTEMPT TO CONVERT WYLBUR LINE NUMBERS
TRAKWNOC L     R1,DWARDATA   GET NORMAL WRITE CHAIN              81208
         SH    R1,=H'24'     BACKSPACE TO READ R0 CHAIN          81208
         MVI   16(R1),X'16'  RESTORE READ R0                     86292
TRAKWNOR STCM  R1,7,DWICCW+1  SET IOBSTART                       86292
         MVI   DWTOGO,OFFWREC0  CHECK AFTER WRITE                81047
         B     TRAKEXCP      READ R0, WRITE REST OF TRACK        81047
         SPACE 1                                                 81090
TRAKBIPL PRTV  BDIPLTK       PRINT MESSAGE                       81208
         EMP   RETRY=NO      AND QUIT                            83094
BDIPLTK  VCON  '0***** INVALID VOLUME LABEL READ *****'          81208
         SPACE 2                                                 81047
TRAKWRT0 L     R1,DWARDATA   GET NORMAL CHAIN                    86292
         SH    R1,=H'24'     BACKSPACE TO READ R0 CHAIN          86292
         CLI   16(R1),X'15'  SPECIAL OUTPUT ERROR RECOVERY ?     86292
         BNE   TRAKWR0N      NO                                  86292
         CLI   DWECB,X'7F'   SUCCESSFUL REWRITE OF R0 ?          86292
         BNE   TRAKWR0R      NO; DO ERROR RECOVERY               86292
         L     R1,=A(MSGIORCV)                                   86292
         PRTLIST (R1)        SHOW ERROR RECOVERED                86292
         B     TRAKWRT1      CONTINUE FROM POINT OF INTERRUPTION 86292
TRAKWR0N CLI   DWECB,X'7F'   WRITE COMPLETED CORRECTLY ?         86292
         BE    TRAKWRT1      YES                                 83094
         CLI   DWECB,X'41'   I/O ERROR ?                         86292
         BNE   TRAKWR0R      NOT ONE I WISH TO HANDLE            86292
         CLC   =X'0E40',DWICSW+4  UC/LEN ?                       86292
         BNE   TRAKWR0R      NO                                  86292
         CLI   DWIOB+2,X'08' DATA CHECK ?                        86292
         BNE   TRAKWR0R      NO                                  86292
         MVI   16(R1),X'15'  REWRITE R0 (ERASES TRACK)           86292
         B     TRAKWNOR      RESTART WITH SEARCH HA CCW          86292
TRAKWR0R EMP   ,             BOMB                                83094
TRAKWRT1 CLC   DWICCW+1(3),DWARDATA+1  NORMAL IOBSTART ADDRESS ? 83094
         BE    TRAK0WRT      YES; RELEASE BUFFER                 81208
         CLC   DWR0COUN(4),DWOUTAD  SAME CCHH ?                  81208
         BE    TRAKWR0T      YES                                 81208
         L     R1,=A(ALTRACK)                                    85287
         PRTLIST (R1)        SHOW ALTERNATE TRACK ASSIGNMENT     81208
         CLC   DWOUTAD,DS4VTOCE+6    TRACK IN VTOC ?             81208
         BH    TRAKWR02      NO                                  81208
         CLC   DWOUTAD,DS4VTOCE+2                                81208
         BL    TRAKWR02                                          81208
         L     R1,=A(ALTRACKV)                                   85287
         PRTV  (R1),CC=NO    ALTERNATE IN VTOC                   81208
         OI    CCODE+1,8     MAKE ERROR CODE                     81208
         B     TRAKWR08                                          81208
TRAKWR02 OI    CCODE+1,1     SET MINOR ERROR                     82272
TRAKWR08 PRTSPACE 1                                              81208
TRAKWR0T CLC   DWR0ODAT,DWR0DATA  ALREADY CORRECT ?              81208
         BE    TRAK0WRT      YES; DONE WITH THIS TRACK           81208
         TM    DWFLAG,DSLVSAM+DSLISAM+DSLUNM  UNMOVABLE ?        87137
         BZ    TRAK0WRT      NOT SPECIAL CASE; LEAVE R0 AS IS    87137
         MVC   DWR0DATA,DWR0ODAT  SET R0 DATA TO BE WRITTEN      81208
         L     R6,DWARDATA   GET NORMAL CHAIN                    81208
         ST    R6,DWICCW     SET FOR NEXT I/O                    81208
         MVI   16(R6),X'05'  SET TO WRITE R0 DATA                81208
         MVI   16+4(R6),0    TERMINATE THE CHAIN                 81208
         B     TRAKEXCP      AND FIRE OFF AN I/O                 81208
TRAK0WRT MVI   DWTOGO,OFFAVAIL  SET BUFFER AVAILABLE             81208
         B     TRAKVAIL      FREE THE BUFFER                     81047
         SPACE 1                                                 81047
TRAKUSWT TM    TRAKMODE,TKPDE  DOING CHAINING ?                  85280
         BO    TRAKUSWW      YES                                 83059
         TM    TRAKMODE,TKVTOCRD  OTHER CHAIN ?                  85280
         BNO   TRAK0WRT      NO; FREE  (BYPASS LOGIC ERROR)      83059
TRAKUSWW MVI   DWECB,0       UNPOST ECB                          83059
         MVI   DWTOGO,OFFUSERQ  SHOW AWAITING USER ACTION        83059
         CLM   R4,7,TRKCHAIN+1  TOP OF CHAIN ?                   81047
         BNE   TRAKWAIT      NO; DO SOME MORE                    81047
         B     TRAKSBEX      YES; LET THE USER HAVE IT           83066
         SPACE 1                                                 81047
TRAKRDSC OI    TRAKFLAG,TKLAST  SET LAST REQUEST                 81047
         MVI   DWTOGO,OFFRDSCB  SET FOR DSCB READ CHECK          81047
         CLC   FULL(3),ZEROES   REQUEST TO READ FORMAT 4 ?       81047
         BNE   TRAKRDS4      NO                                  81047
         SLR   R1,R1                                             81047
         IC    R1,FULL+3     GET DD INDEX                        81047
         MH    R1,=Y(VIN2DDNM-VIN1DDNM)  GET OFFSET              81047
         L     R3,VIN1UCB(R1)  GET UCB ADDRESS                   81047
         ICM   R0,14,UCBVTOC-UCBOB(R3)  GET VTOC ADDRESS         81047
         IC    R0,FULL+3     ADD CONCATENATION BYTE              81047
         ST    R0,FULL       RETURN THE CORRECT TTR              81047
         B     TRAKRDS2      GO TO COMMON                        81047
TRAKRDS4 L     R0,FULL       GET TTRZ OF DSCB                    81047
TRAKRDS2 LA    R1,DWIMBB     GET FULL DISK ADDRESS FIELD         81047
         BAL   R9,TTRABS     CONVERT TTR TO ABSOLUTE             81047
         L     R2,4(,R13)    GET OLD SAVE AREA                   81047
         L     R2,24(,R2)    GET CALLER'S R1                     81047
         LM    R5,R6,DWARDATA  GET WORK FIELD IN BUFFER          81047
         MVC   0(16,R6),0(R5)  COPY SEARCH/TIC                   81047
         STCM  R6,7,9(R6)    UPDATE TIC ADDRESS                  81047
         MVC   16(8,R6),=X'0E0000000000008C'  READ KEY/DATA      81047
         STCM  R2,7,17(R6)   UPDATE INPUT ADDRESS                81047
         XC    0(L'DS1DSNAM+1,R2),0(R2)  CLEAR INPUT             83094
         ST    R6,DWICCW     SET CCW CHAIN START                 81047
         ICM   R5,15,STACKAD STACKED VTOC BUFFERS ?              87135
         BZ    TRAKEXCP      NO; DO I/O                          87135
         USING STACK,R5      DECLARE IT                          87135
         ICM   R0,15,DWICCHH LOAD DESIRED ADDRESS                87135
         IC    R1,DWIMBB     AND EXTENT                          87135
TRAKRDSS CLM   R0,15,STKCCHH  DESIRED TRACK ?                    87135
         BNE   TRAKRDST      NO                                  87135
         CLM   R1,1,STKEXT   SAME VOLUME ?                       87135
         BE    TRAKRDSF      YES; PROCESS                        87135
TRAKRDST ICM   R5,15,STKLINK GET NEXT TRACK                      87135
         BNZ   TRAKRDSS      TRY IT                              87135
         B     TRAKEXCP      FIRE OFF AN I/O                     81047
TRAKRDSF SLR   R1,R1                                             87135
         IC    R1,FULL+2     GET DESIRED RECORD NUMBER           87135
         BCTR  R1,0          DECREMENT FOR RELATIVITY            87135
         MH    R1,=Y(DS1END-DS1DSNAM+8)  CONVERT INTO OFFSET     87135
         LA    R1,STKDATA+8(R1)  POINT TO KEY OF SAVED RECORD    87135
         MVC   0(DS1END-DS1DSNAM,R2),0(R1)  COPY DATA            87135
         MVI   DWECB,X'7F'   FAKE COMPLETED I/O                  87135
         DROP  R5                                                87135
         INC   NUMSTHIT      COUNT NON-EXCPS                     87135
         SPACE 1                                                 81047
TRAKDSCR CLI   DWECB,X'7F'   NORMAL COMPLETION ?                 83094
         BE    TRAKDSCP      YES; SET DONE                       83094
         EMP   TRAKDSCP      QUICK AND DIRTY ERROR PROCESSING    83094
         SPACE 1                                                 81047
TRAKWDSC OI    TRAKFLAG,TKLAST  SET LAST REQUEST                 81047
         MVI   DWTOGO,OFFWDSCB  SET FOR DSCB WRITE CHECK         81047
         CLC   BLOCKAD(3),ZEROES   REQUEST TO WRITE FORMAT 4 ?   81047
         BNE   TRAKWDS4      NO                                  81047
         SLR   R1,R1                                             81047
         IC    R1,BLOCKAD+3  GET DD INDEX                        81047
         MH    R1,=Y(VIN2DDNM-VIN1DDNM)  GET OFFSET              81047
         L     R3,VIN1UCB(R1)  GET UCB ADDRESS                   81047
         ICM   R0,14,UCBVTOC-UCBOB(R3)  GET VTOC ADDRESS         81047
         IC    R0,BLOCKAD+3  ADD CONCATENATION BYTE              81047
         ST    R0,BLOCKAD    SET FOR 'NEXT ADDRESS' CALCULATION  81047
         B     TRAKWDS2      GO TO COMMON                        81047
TRAKWDS4 L     R0,BLOCKAD    GET TTRZ OF DSCB                    81047
TRAKWDS2 LA    R1,DWIMBB     GET FULL DISK ADDRESS FIELD         81047
         BAL   R9,TTRABS     CONVERT TTR TO ABSOLUTE             81047
         L     R2,4(,R13)    GET OLD SAVE AREA                   81047
         LM    R2,R3,24(R2)  GET CALLER'S R1 AND R2              81047
         CLC   DWICCHH(4),DS4VTOCE+6  SEE IF ADDRESS IN VTOC EXTENT
         BNH   TRAKWDSO      YES                                 81047
         CLI   DS1FMTID-FMT1(R2),C'5'  FORMAT 5 ?                81047
         BE    TRAKWDSB      YES; JUST SET ERROR FLAG            81047
         CLI   DS1FMTID-FMT1(R2),0     WHOLE RECORD ?            81047
         BE    TRAKWDSB      YES; ALSO OK                        81047
         SUBCALL DUMPMSG,FULLVMSG   PRINT MESSAGE AND QUIT       81208
FULLVMSG VCON  '0PROGRAM ERROR OR OUTPUT VTOC TOO SMALL'         81208
TRAKWDSB MVI   OUTER,X'42'   SET INVALID ADDRESS FLAG            81047
         MVI   DWTOGO,OFFDONE   SET BUFFER AVAILABLE             81047
         B     TRAKWAIT      AND LOOK FOR SOMETHING ELSE TO DO   81047
TRAKWDSO MVC   0(5,R3),DWICCHH  SET CHAIN DATA                   81047
         LM    R5,R6,DWARDATA  GET WORK FIELD IN BUFFER          81047
         MVC   0(16,R6),0(R5)  COPY SEARCH/TIC                   81047
         STCM  R6,7,9(R6)    UPDATE TIC ADDRESS                  81047
         MVC   16(8,R6),=X'0D0000000000008C'  WRITE KEY/DATA     81047
         LA    R1,24(,R6)    GET OUTPUT AREA                     81047
         MVC   0(DS1END-FMT1,R1),0(R2)  SAVE OUTPUT DATA         81047
         STCM  R1,7,17(R6)   UPDATE OUTPUT ADDRESS               81047
         ST    R6,DWICCW     SET CCW CHAIN START                 81047
         LA    R6,TRAKEXCP   SET FOR FAST BRANCHES               83135
         CLI   DS1FMTID-FMT1(R1),C'1'  WRITING FORMAT 1 ?        83135
         BNER  R6            NO                                  83135
         CLI   DS1EXT1-FMT1+XTTYPE-EXTMAP(R1),XTTLABEL  LABEL TRACK ?
         BNER  R6            NO                                  83135
         SLR   R0,R0                                             83135
         ICM   R0,1,DS1NOEPV-FMT1(R1)  GET EXTENT COUNT          83135
         BNPR  R6            LEAVE IF NOT PLUS                   83135
         BCTR  R0,0          ELSE ALLOW FOR LABEL TRACK          83135
         STC   R0,DS1NOEPV-FMT1(,R1)  SET IBM'S EXTENT COUNT     83135
         BR    R6            FIRE OFF AN I/O                     83135
         SPACE 1                                                 81047
TRAKDSCW CLI   DWECB,X'7F'   NORMAL COMPLETION ?                 83094
         BE    TRAKDSCP      YES                                 83094
         EMP   ,             QUICK AND DIRTY ERROR PROCESSING    83094
TRAKDSCP MVI   DWTOGO,OFFDONE  SET BUFFER DONE                   83094
         B     TRAKWAIT      GO TO WAIT                          83094
         SPACE 1                                                 81047
TRAKVTOC SLR   R7,R7                                             81047
         IC    R7,DS4DEVDT   GET DSCBS PER TRACK                 81047
         LR    R15,R7        COPY NUMBER OF DSCBS                81047
         LA    R9,8+DS1END-FMT1  DSCB SIZE + COUNT               81047
         MR    R14,R9                                            81047
         L     R14,DWADATA   DATA START                          81047
         SLR   R0,R0                                             81047
         SLR   R1,R1                                             81047
         MVCL  R14,R0        CLEAR DATA AREA                     81047
         L     R14,DWADATA   GET DATA AREA BACK                  81047
         CLC   DWOUTAD(4),DS4VTOCE+2  DOING FIRST VTOC TRACK ?   81047
         BNE   TRAKVTO4      NO                                  81047
         MVC   8(DS1END-FMT1,R14),FMT4  COPY FORMAT 4 DSCB       81047
         MVC   8+8+DS1END-FMT1(4,R14),=4X'05'  DUMMY FORMAT 5 DSCB
         MVI   8+8+DS1END-FMT1+L'DS1DSNAM(R14),C'5'              81047
TRAKVTO4 ICM   R5,15,DWOUTAD  GET OUTPUT ADDRESS                 81047
         L     R6,=X'002C0060'  SET REC#, KEYLEN, DATA LENGTH    81047
         L     R15,DWACOUNT  GET COUNT FIELDS                    81047
         L     R2,=X'1D000000'  SET WRITE CKD OPCODE             81047
         OR    R2,R14        SET TO START OF DATA AREA           81047
         L     R3,=X'40000094'  SET COMMAND CHAINING AND LENGTH  81047
         LA    R0,8          SET CONSTANT                        81047
         LR    R1,R15                                            81047
         SR    R1,R0         BACKSPACE TO READ R0 CCW            81047
         MVI   0(R1),X'06'   REFRESH READ R0 AS A READ           81047
         OI    4(R1),X'40'   TURN COMMAND CHAINING ON            81047
TRAKVTLP AL    R6,=X'01000000'  INCREASE RECORD NUMBER           81047
         STM   R5,R6,0(R2)   SET NEW COUNT FIELD                 81047
         STM   R2,R3,0(R15)  SET OUTPUT CCW                      81047
         ALR   R2,R9         MAKE NEXT DATA ADDRESS              81047
         AR    R15,R0        NEXT COUNT FIELD ADDRESS            81047
         BCT   R7,TRAKVTLP   TRY AGAIN                           81047
         SR    R15,R0        POSITION TO LAST CCW                81047
         MVI   4(R15),0      RESET CMD CHAIN                     81047
         MVI   DWIMBB,9      SET FOR TENTH EXTENT (VTOC)         81047
         XC    DWR0ODAT,DWR0ODAT  SET DESIRED R0 DATA            81208
         B     TRAKWNOC      MAKE WRITE/R0 COMMON CODE           81208
         SPACE 1                                                 85287
TRAKPDEW SLR   R7,R7                                             85287
         IC    R7,DS4DEVDB   GET DIRECTORY BLOCKS PER TRACK      85287
         SLR   R15,R15                                           86293
         SLR   R14,R14                                           86293
         ICM   R15,3,PDEUSD  LOAD NUMBER OF DIRECTORY BLOCKS TO DO
         BNM   TRAKPDEM      NONE (EOF ONLY) OR MORE             86293
         N     R15,=X'00007FFF'  GET TRUE RESIDUAL               86293
         BZ    TRAKPDEM      WE ARE ON LAST TRACK; JUST DO EOF   86293
         L     R14,=X'00008000'  MAKE MASK FOR NEXT STORE        86293
TRAKPDEM MIN   R7,(R15)      USE FULL OR PARTIAL TRACK           86293
         SR    R15,R7        GET NUMBER LEFT TO DO               86293
         OR    R15,R14       OR SPECIAL FLAG BACK IN             86293
         STH   R15,PDEUSD    STASH IT BACK                       86293
         LTR   R15,R15       CHECK FOR ZERO AND EOF              86293
         BNZ   TRAKPDEF      NOT YET (+ MORE TO DO / - EOF NEXT TRK)
         LA    R7,1(,R7)     GENERATE EXTRA CCW FOR EOF          85287
TRAKPDEF LR    R15,R7        COPY NUMBER OF DSCBS                85287
         LA    R9,8+8+256    BLOCKSIZE + KEY + COUNT             85287
         MR    R14,R9                                            85287
         L     R14,DWADATA   DATA START                          85287
         SLR   R0,R0                                             85287
         SLR   R1,R1                                             85287
         MVCL  R14,R0        CLEAR DATA AREA                     85287
         L     R14,DWADATA   GET DATA AREA BACK                  85287
         CLC   DWOUTAD(4),NEWEXTS+XTLOCYL-EXTMAP  FIRST TRACK ?  85287
         BNE   TRAKPDE4      NO                                  85287
         L     R5,=A(PATPDE)  GET LAST PDE PATTERN               85287
         MVC   8(L'PATPDE,R14),0(R5)  DUMMY FIRST/LAST PDE BLOCK 85287
TRAKPDE4 ICM   R5,15,DWOUTAD  GET OUTPUT ADDRESS                 85287
         L     R6,=X'00080100'  SET REC#, KEYLEN, DATA LENGTH    85287
         L     R15,DWACOUNT  GET COUNT FIELDS                    85287
         L     R2,=X'1D000000'  SET WRITE CKD OPCODE             85287
         OR    R2,R14        SET TO START OF DATA AREA           85287
         L     R3,=X'40000110'  SET COMMAND CHAINING AND LENGTH  85287
         LA    R0,8          SET CONSTANT                        85287
         LR    R1,R15                                            85287
         SR    R1,R0         BACKSPACE TO READ R0 CCW            85287
         MVI   0(R1),X'06'   REFRESH READ R0 AS A READ           85287
         OI    4(R1),X'40'   TURN COMMAND CHAINING ON            85287
TRAKPDEP AL    R6,=X'01000000'  INCREASE RECORD NUMBER           85287
         STM   R5,R6,0(R2)   SET NEW COUNT FIELD                 85287
         STM   R2,R3,0(R15)  SET OUTPUT CCW                      85287
         ALR   R2,R9         MAKE NEXT DATA ADDRESS              85287
         AR    R15,R0        NEXT COUNT FIELD ADDRESS            85287
         BCT   R7,TRAKPDEP   TRY AGAIN                           85287
         SR    R15,R0        POSITION TO LAST CCW                85287
         MVI   4(R15),0      RESET CMD CHAIN                     85287
         ICM   R0,3,PDEUSD   DOING LAST TRACK ?                  85287
         BNZ   TRAKPDEN      NO                                  86293
         SR    R2,R9         GET LAST COUNT FIELD                85287
         LA    R0,8          SET LENGTH OF WRITE COUNT ONLY      85287
         STH   R0,6(,R15)    PLACE INTO CCW                      85287
         STCM  R0,14,5(R2)   UPDATE LENGTH IN COUNT ALSO         85287
         LH    R1,0(,R2)     GET LAST CYLINDER READ              85287
         SH    R1,XTLOCYL-EXTMAP+NEWEXTS  LESS START CYLINDER    85287
         MH    R1,VOUTTPC    CONVERT TO TRACKS                   85287
         AH    R1,2(,R2)     ADD END TRACK                       85287
         SH    R1,XTLOTRK-EXTMAP+NEWEXTS  LESS START TRACK       85287
         STH   R1,DS1LSTAR   SET TT PORTION OF LSTAR             85287
         STCM  R6,8,DS1LSTAR+2  SET 'R'                          85287
         OI    TRAKFLAG,TKLAST  SET LAST                         85287
TRAKPDEN MVI   DWIMBB,8      SET FOR PACK                        85287
         MVC   DWICCHH,DWOUTAD  SET SEEK ADDRESS                 85287
         MVC   DWR0COUN(4),DWOUTAD  SET OUTPUT ADDRESS           85287
         XC    DWR0ODAT,DWR0ODAT  SET DESIRED R0 DATA            85287
         B     TRAKWNOC      MAKE WRITE/R0 COMMON CODE           85287
         SPACE 1                                                 81047
TRAKDUPE OI    TRAKFLAG,TKLAST  SET LAST REQUEST                 81047
         MVI   DWTOGO,OFFRDUPE  COME BACK AFTER I/O              81047
         MVI   DWIMBB,9      SET FOR VTOC EXTENT                 81047
         MVC   DWICCHH(4),DS4VTOCE+2  SET FOR VTOC START         81047
         MVI   DWICCHH+4,1   START WITH RECORD 1                 81047
         LM    R5,R6,DWARDATA  GET WORK FIELD AND CCWS           81047
         MVC   0(16,R6),0(R5)  COPY SEARCH/TIC                   81047
         STCM  R6,7,9(R6)    SET TIC ADDRESS                     81047
         LA    R14,DS1DSNAM  GET NAME TO COMPARE ON              81047
         LA    R15,44        SET LENGTH                          81047
         STM   R14,R15,16(R6)  MAKE SKELETON CCW                 81047
         MVI   16(R6),X'A9'  MAKE M/T KEY EQUAL                  81047
         OI    20(R6),X'40'  SET COMMAND CHAIN                   81047
         LA    R14,16(,R6)                                       81047
         ST    R14,24(,R6)   MAKE SECOND TIC                     81047
         MVI   24(R6),8      COMPLETE TIC                        81047
         MVC   32(8,R6),NOPCCW  FOR X'7F' - END WITH NO-OP CCW   81047
         ST    R6,DWICCW     SET START CCW ADDRESS               81047
         B     TRAKEXCP                                          81047
         SPACE 1                                                 81047
TRAKDUPL CLI   DWECB,X'42'   NO RECORD FOUND ?                   83094
         BE    TRAKDUPY      YES; RETURN OK                      83094
         CLI   DWECB,X'7F'   NORMAL I/O COMPLETION ?             83094
         BE    TRAKDUPX      YES; RETURN CODE FOR DUPLICATE DSN  83094
         EMP   ,             NO; QUIT                            83094
TRAKDUPX MVI   DSLFG-DSLENT+DSLCURR,0  SET BAD RETURN            83094
TRAKDUPY MVI   DWTOGO,OFFDONE  SET BUFFER AVAILABLE              83094
         B     TRAKSBEX      RETURN OK                           83094
         SPACE 2                                                 83094
IOATEST  TM    DWFLAG,DWFERR ERROR BIT ON ?                      83094
         BNZ   IOABEND       YES; NO SIMPLE RETRY                83094
         OI    DWFLAG,DWFERR  SET ERROR RETRY FLAG               83094
         B     TRAKEXC2      RETRY THE OPERATION ONCE            83094
         SPACE 1                                                 81047
IOAQUIT  NI    OPTFLAG2,255-OPTEVEN  PREVENT RECOVERY - HARD ERR 83094
IOABEND  ST    R14,SVIOR14   SAVE R14                            81047
         LA    R14,0(,R14)   CLEAN                               81047
         S     R14,=A(COPYFDR)  AND JERK                         81047
         ST    R14,SVIOR14+4  SET RELATIVE OFFSET                81047
         SLR   R6,R6                                             81047
         IC    R6,DWIMBB     GET EXTENT BYTE                     81047
         LA    R14,8         SET MAX SUPPORTED                   81047
         CR    R6,R14        VALID ?                             81047
         BNH   *+6           YES                                 81047
         LR    R6,R14        ELSE SET MAX                        81047
         MH    R6,=Y(VIN2DDNM-VIN1DDNM)  MAKE OFFSET             81047
         LA    R6,VIN1DDNM(R6)  GET CORRECT ENTRY                81047
         L     R3,VIN1UCB-VIN1DDNM(,R6)  GET UCB                 81047
         MVC   IOMVOL,VIN1VSER-VIN1DDNM(R6)  SHOW VOLUME SERIAL  81047
         MVC   IOMUCB,UCBNAME-UCBOB(R3)  UNIT NAME               81047
         ST    R4,SVIOR4     SAVE BUFFER ADDRESS                 81047
         LA    R15,0(,R4)    GET CLEANED BUFFER ADDRESS          81047
         L     R14,DWICSW    GET LAST CCW+                       81047
         LA    R14,0(,R14)   CLEAN IT                            81047
         SH    R14,=H'8'     BACK UP                             81047
         CR    R14,R15       IN BUFFER ?                         81047
         BNH   IOABNCCW      NO; BAD ADDRESS                     81047
         LA    R15,4095(,R15)                                    81047
         LA    R15,2049(,R15)                                    81047
         CR    R14,R15       STILL WITHIN BUFFER ?               81047
         BL    IOABCCWG      YES; USE IT                         81047
IOABNCCW L     R14,DWICCW    USE START CCW                       81047
IOABCCWG ST    R14,SVIOACCW  SAVE ADDRESS FOR RETRY ROUTINES     83094
         MVC   SVIOCCW,0(R14)  COPY CCW TEXT                     83094
         TM    DWFLAG,DWFERM   ERROR MESSAGE PRINTED ALREADY ?   83094
         BNZ   TRAKQPRT      YES; SKIP PRINTING AGAIN            83094
         OI    DWFLAG,DWFERM   SHOW MESSAGE PRINTED              83094
         L     R1,=A(MSGIOERR)  POINT TO THE ERROR MESSAGE       86292
         PRTLIST (R1)        PRINT THE ERROR MESSAGE             86292
         CLI   DWIDSN,C' '   SPECIAL I/O ?                       83094
         BE    TRAKQDTF      YES; BYPASS TEST                    83094
         TM    OPTFLAG,OPTERSE  SUPPRESS NORMAL MESSAGES ?       83094
         BNZ   TRAKQPRT      YES; SKIP DUMP                      83094
TRAKQDTF SLR   R3,R3         CLEAR FOR LENGTH CALCULATION        83094
         L     R14,SVIOACCW  GET FAILING CCW BACK                83094
         ICM   R3,R3,6(R14)  GET REQUESTED LENGTH                83094
         BZ    TRAKQPRT        PROGRAM ERROR ?                   83094
         SLR   R2,R2                                             83094
         ICM   R2,3,DWICSW+6  GET RESIDUAL LENGTH                83094
         SR    R3,R2         GET LENGTH READ                     83094
         BNP   TRAKQPRT      SKIP IF NOTHING READ                83094
         ICM   R2,7,1(R14)   GET BUFFER ADDRESS                  83094
         CLI   DWIDSN,C' '   SPECIAL I/O ?                       83094
         BE    TRAKQDMP      YES; BYPASS TEST                    83094
         TM    OPTFLAG,OPTLOTS  LOTS OF OUTPUT ?                 83094
         BNZ   TRAKQDMP      YES; DUMP TEXT                      83094
         LA    R0,512        SET MAXIMUM LENGTH I WANT TO DUMP   86292
         CR    R3,R0         IS IT LONGER ?                      86292
         BNH   *+6           NO; USE DATA LENGTH                 86292
         LR    R3,R0         TRUNCATE                            86292
TRAKQDMP PRTLIST DMPOFF      DUMP THE BUFFER (SEGMENT)           86292
TRAKQPRT TM    OPTFLAG2,OPTEVEN  USER WISHES TO KEEP RUNNING ?   83094
         BZ    TRAKQUIT      NO; OR HARD ERROR. QUIT             83094
         OI    DWFLAG,DWFER2  SHOW ERROR EXIT TAKEN              83094
         NI    DWFLAG,255-DWFERR-DWFERM                          83094
         OI    CCODE+1,8     SET NASTY ERROR                     83094
         L     R14,SVIOR14   GET CALLER'S RETURN ADDRESS         83094
         LH    R15,0(,R14)   GET BRANCH OFFSET                   83094
         B     TRAKEXCP(R15)   TAKE USER'S ERROR EXIT            83094
         SPACE 1                                                 83094
TRAKQUIT OI    DUMPCODE,X'80'  FORCE A DUMP                      83094
         MVI   DUMPCODE+3,2  SET CODE 2                          81047
         SUBCALL DUMPMSG,IOMQUIT  PRINT AND DUMP                 83094
         SPACE 1                                                 81047
SVIOR4   DC    A(0)          BUFFER ADDRESS                      81047
SVIOR14  DC    A(0,0)        ERROR CALL ABS/REL                  81047
SVIOACCW DC    A(0)          ADDRESS OF FAILING CCW              83094
SVIOCCW  DC    XL8'0'        CCW TEXT                            81047
         SPACE 1                                                 81047
         LTORG ,                                                 81047
         SPACE 1                                                 81047
IOMVOL   DC    C'VOLSER'                                         83094
IOMUCB   DC    C'CUU'                                            83094
IOMQUIT  VCON  ' ***** FATAL ERROR - EXECUTION TERMINATED *****' 83094
ALTRACK  FDOPT NL,CC=C' '    DOUBLE-SPACE                        81208
         FDPRT '***** OUTPUT VOLUME'                             81208
         FDPRT VOUTVSER,DEB,PAD                                  81208
         FDPRT 'TRACK',PADR                                      81208
         FDPRT DWOUTAD,2,HEX                                     81208
         FDPRT '.'                                               81208
         FDPRT DWOUTAD+2,2,HEX                                   81208
         FDPRT 'COPIED TO ALTERNATE TRACK',PAD                   81208
         FDPRT DWR0COUN,2,HEX                                    81208
         FDPRT '.'                                               81208
         FDPRT DWR0COUN+2,2,HEX                                  81208
         FDPRT '*****',PADL                                      81208
         FDPRT *END                                              81208
ALTRACKV VCON  ' ***** THIS IS A BAD VTOC TRACK *****'           81208
DMPOFF   FDSNAP (R2),DUAL,NOABS,OFFSET,BASE=(R2),LEN=(R3)        86292
         SPACE 1                                                 81047
MSGIOERR FDOPT NL,CC=C' '    DOUBLE-SPACE                        83094
         FDPRT '***** I/O ERROR ON',PADR                         83094
         FDPRT IOMVOL,DEB    VOL-SER                             83094
         FDPRT '('                                               83094
         FDPRT IOMUCB        UNIT NAME                           83094
         FDPRT ') SEEK='                                         83094
         FDPRT DWIMBB,1,HEX    EXTENT                            83094
         FDPRT '.'                                               83094
         FDPRT DWIMBB+1,2,HEX  BIN                               83094
         FDPRT '.'                                               83094
         FDPRT DWIMBB+3,2,HEX  CYL                               83094
         FDPRT '.'                                               83094
         FDPRT DWIMBB+5,2,HEX  HEAD                              83094
         FDPRT '.'                                               83094
         FDPRT DWIMBB+7,1,HEX  RECORD                            83094
         FDPRT 'ECB=',PADL                                       83094
         FDPRT DWECB,1,HEX                                       83094
         FDPRT 'SENSE=',PADL                                     83094
         FDPRT DWIOB+2,2,HEX                                     83094
         FDPRT 'CSW=',PADL                                       83094
         FDPRT DWICSW,1,HEX                                      83094
         FDPRT DWICSW+1,3,HEX,PADL                               83094
         FDPRT DWICSW+4,2,HEX,PADL                               83094
         FDPRT DWICSW+6,2,HEX,PADL                               83094
         FDPRT 'CCW=',PADL                                       83094
         FDPRT SVIOCCW,1,HEX                                     83094
         FDPRT SVIOCCW+1,3,HEX,PADL                              83094
         FDPRT SVIOCCW+4,2,HEX,PADL                              83094
         FDPRT SVIOCCW+6,2,HEX,PADL                              83094
         FDPRT 'FLAGS=',PADL,NL,RADJ,LEN=12                      83094
         FDPRT TRAKFLAG,2,HEX   +TRAKMODE                        85280
         FDPRT DWFLAG,1,HEX,PADL                                 83094
         FDPRT 'BUFFER=',PADL                                    83094
         FDPRT SVIOR4,HEX                                        83094
         FDPRT 'ERROR CALL AT',PAD                               83094
         FDPRT SVIOR14,HEX                                       83094
         FDPRT '('                                               83094
         FDPRT SVIOR14+4,4,SHEX,DEB,DEBZ                         83094
         FDPRT ') DSN='                                          83094
         FDPRT DWIDSN,DEBR                                       83094
         FDPRT *END                                              83094
MSGIORCV FDPRT '***** DATA CHECK ON',NL                          86292
         FDPRT VOUTVSER,DEB,PAD                                  86292
         FDPRT 'TRACK',PADR                                      86292
         FDPRT DWOUTAD,2,HEX                                     86292
         FDPRT '.'                                               86292
         FDPRT DWOUTAD+2,2,HEX                                   86292
         FDPRT 'SUCCESSFULLY RECOVERED *****',PADL               86292
         FDPRT *END                                              86292
         TITLE 'C O P Y V O L  ***  FREE SPACE ALLOCATION ROUTINE'
*        RELTEST - ASSIGNS, BUT DOES NOT ALLOCATE, FREELIST EXTENTS
RELTEST  SUBENT ,                                                81047
         USING DSLIST,R4                                         81047
         SLR   R2,R2         SET EXTENTS PROCESSED               81047
         ST    R2,TRKALC     ZERO TRACKS ALLOCATED               91119
         CLI   DS1NOEPV,0    PATTERN DSCB ?                      81047
         BE    RELEXIT4      YES; OK THE REQUEST                 81047
         MVC   PFMT5SAV,PFMT5AD   SAVE CHAIN HEADS               81047
         LM    R14,R0,PFMT5SAD  GET SAVE/LEN/START               81047
         LR    R1,R15        COPY LENGTH                         81047
         MVCL  R14,R0        SAVE IT                             81047
         L     R0,TRKUSD     GET TRACKS TO BE ALLOCATED          91119
         LA    R1,OLDEXTS    POINT TO OLD EXTENTS                81047
         USING EXTMAP,R1                                         81047
         XC    NEWEXTS(16*XTLEN),NEWEXTS  CLEAR NEW EXTENTS      81047
         TM    MIGFLAG,OPTMIGR+OPTMIGF  MIGRATION ?              85280
         BNO   RELTESTL      NO                                  85280
         TM    DSLFG,DSLCYL+DSLCYLP  CYLINDER ALLOCATION ?       85280
         BZ    RELTESTL      NO                                  85280
         LH    R3,VOUTTPC    GET TRACKS/CYLINDER ON OUTPUT       85280
         LR    R15,R0        COPY TRACKS USED                    85280
         SLR   R14,R14                                           85280
         AR    R15,R3        ADD ONE CYLINDER'S WORTH            85280
         BCTR  R15,0         LESS ONE, FOR ROUNDING              85280
         DR    R14,R3        GET TRACKS/CYLINDERS                85280
         MR    R14,R3        GET NUMBER OF TRACKS BACK           85280
         LR    R0,R15                                            85280
         ST    R0,TRKUSD     UPDATE                              91119
RELTESTL STM   R0,R2,RELSAVE   SAVE EXTENT COUNTERS              81047
         CLI   XTTYPE,0      DONE ?                              81047
         BE    RELTEXIT      YES; QUIT***TEMP FIX***WAS BLASTOUT 91140
         LA    R2,1(,R2)     UP EXTENT COUNTER                   81047
         ST    R2,RELSAVE+8  FIX EXTENT COUNT                    81047
         LH    R14,VINTPC    GET TRACKS PER CYLINDER IN 14       85280
         TM    MIGFLAG,OPTMIGR+OPTMIGF  MIGRATE ?                85280
         BNO   RELTESTM      NO                                  85280
         TM    DSLFG,DSLCYL+DSLCYLP  CYLINDER ALIGNMENT ?        85280
         BZ    RELTESTM      NO                                  85280
         L     R3,TRKUSD     GET TRACKS REQUIRED                 91119
         S     R3,TRKALC     DONE YET ?                          91119
         BP    ALNOAD1A      NO; ALLOCATE SOME MORE              85280
         BCTR  R2,0          FIX EXTENT COUNT                    85280
         ST    R2,RELSAVE+8                                      85280
         B     BLASTOUT      QUIT THIS ONE                       85280
RELTESTM LH    R3,XTHICYL    GET ENDING CYL                      81047
         CLI   XTTYPE,XTTSPLIT  DOING A SPLIT ?                  81047
         BNE   RELNSPIT      NO                                  81047
         TM    DSLFG,DSLCYL+DSLCYLP  AS CYLINDER ?               81047
         BNZ   RELNCYL       YES; DEFER IT                       81047
         SH    R3,XTLOCYL                                        81047
         LA    R3,1(,R3)     NUMBER OF CYLINDERS                 81047
         LH    R7,XTHITRK                                        81047
         SH    R7,XTLOTRK                                        81047
         LA    R7,1(,R7)     NUMBER OF TRACKS                    81047
         MR    R2,R7         GET TOTAL TRACKS IN R3              81047
         B     ALNOAD1A                                          81047
RELNSPIT MR    R2,R14        GET REL CYL START                   81047
*        TM    DSLFG,DSLCAT+DSLTRK+DSLTRKP  OTHER THAN CYLINDER ?
*        BNZ   JUSTAD          IF NOT, ADD IN TRACKS             79105
*        AH    R3,VINTPC       GET UP TO LAST TRK                85280
*        BCTR  R3,0            ADJUST                            85280
*        B     ALCOMM                                            79105
JUSTAD   AH    R3,XTHITRK    ADD IN LAST TRACK                   81047
ALCOMM   LA    R3,1(,R3)        NOW HAVE # OF LAST TRACK
         LH    R7,XTLOCYL    GET STARTING CYL                    81047
         MR    R6,R14        GET REL CYL START
*        TM    DSLFG,DSLCAT+DSLTRK+DSLTRKP  CYLINDER ?           81047
*        BZ    ALNOAD1         IF SO, BRANCH                     79105
         AH    R7,XTLOTRK    ADD IN FIRST TRK                    81047
ALNOAD1  SR    R3,R7           3 HAS # OF TRKS TO ALLOCATE
ALNOAD1A TM    DSLF2,DSLPSREL  PS TRACK RELEASE ?                81047
         BZ    ALNOAD1B      NO                                  79327
         ICM   R7,15,RELSAVE    GET SPACE TO BE ALLOCATED        85280
         BNP   ALNOAD1B      NOT VALID; PREVENT ERROR            79327
         CR    R3,R7         COMPARE TO TRACKS NEEDED            79327
         BNH   ALNOAD1B      NOT YET                             79327
         LR    R3,R7         USE ONLY WHAT IS NEEDED             79327
         CLI   RELSAVE+8+3,1   FIRST EXTENT ?                    81047
         BNE   ALNOAD1B      NO; COPY                            81047
         TM    DSLFG,DSLCYL+DSLCYLP  WAS THIS CYLINDER ?         81047
         BZ    ALNOAD1B      NO; CONTINUE                        79327
RELNCYL  NI    DSLFG,255-DSLCYL-DSLCYLP  JUST TO BE SAFE         81047
         OI    DSLFG,DSLTRKP TURN ON TRACK FLAG INSTEAD          81047
         OI    DSLFLAG,DSLTRKP SET EXTRA PASS THROUGH LIST       81047
RELNFAIL MVC   PFMT5AD(8),PFMT5SAV  RESTORE CHAIN HEADS          81047
         LM    R14,R0,PFMT5SAD  GET SAVE/LEN/USED                81047
         LR    R1,R15        COPY LENGTH                         81047
         MVCL  R0,R14        RESTORE FREE SPACE CHAIN            81047
         SUBEX ,             DO THIS WITH TRACK DATASETS         81047
ALNOAD1B L     R7,TRKALC     GET TRACKS PREVIOUSLY ALLOCATED     91119
         AR    R7,R3         INCLUDE CURRENT EXTENT
         ST    R7,TRKALC     UPDATE                              91119
         LA    R6,PFMT5AD    POINT TO START OF FREE LIST         80207
         B     LKNXTFRE      AND CHECK IF ANYTHING ON IT         80207
SPCLOK1  EQU   *
*        TM   DSLFG,DSLCAT   SYSCTLG ?  /  SKIPPED - AMS HAS CTLG *AMS*
         USING FXMAP,R6                                          81047
*        BZ    NTCTLG02      NO, NORMAL /  PRIOR TO VTOC ON SOME *AMS*
*        CLC   VTOCLTTR,FXABS PAST VTOC /   PACKS                *AMS*
*        BNL   LKNXTFRE      NO; NEXT   /   THEY ARE TO STAY THERE*AMS*
NTCTLG02 SLR   R5,R5                                             91119
         ICM   R5,3,FXCYL    GET TRKS ON CYLINDER BOUNDARY       91119
         TM    DSLFG,DSLCYL+DSLCYLP    IS THIS FOR CYLINDER      81047
         BNZ   ALNOAD2                                           81047
         AH    R5,FXBEF      ADD IN TRKS BEFORE CYL              81047
         AH    R5,FXAFT      ADD TRACKS AT END                   86299
ALNOAD2  CR    R3,R5           SEE IF ENOUGH SPACE
         BNH   SPCFND1         IF SO, WE'RE IN
LKNXTFRE LR    R15,R6         SAVE REG. 6
         L     R6,FXLINK     GET NEXT FREE                       81047
         LTR   R6,R6           MAKE SURE SOME LEFT
         BNZ   SPCLOK1         OH GOODIE
         SUBCALL MSGIN,RELUMSG    SET FOR 'NOT ALLOCATED'        81208
         MVI   DSLFG,0       DELETE IT                           81047
         OI    CCODE+1,8      SET LEVEL 8 ERROR                  81047
         INC   NUMNPRO       UP COUNT OF UNPROCESSED DSNS        81208
         LA    R0,1                                              81208
         AH    R0,NUMFAIL                                        81208
         STH   R0,NUMFAIL                                        81208
         CH    R0,MAXFAIL                                        81047
         BNH   RELNFAIL        RETURN                            81047
         SUBCALL DUMPMSG,MAXFAILM  QUIT WITH MESSAGE             81208
NUMFAIL  DC    H'0'          NUMBER FAILED                       81047
MAXFAIL  DC    H'20'         MAX BEFORE TERMINATION              81047
MAXFAILM VCON  '0EXCESSIVE ALLOCATION FAILURE - COPY TERMINATED' 81208
RELUMSG  MSG   'NOT COPIED; INSUFFICIENT SPACE ON OUTPUT VOLUME' 81047
         SPACE 1                                                 81047
SPCFND1  SLR   R9,R9                                             91119
         ICM   R9,3,FXABS    GET EXTENT START                    91119
         LH    R14,VOUTTPC   GET TRACKS/CYLINDER ON OUTPUT       85280
         TM    DSLFG,DSLCYL+DSLCYLP  IS IT CYLINDER              81047
         BZ    ALNOAD3
         AH    R9,FXBEF      ADD TRKS BEFORE CYL                 81047
ALNOAD3  LR    R0,R9           SAVE #
         SLR   R8,R8           CLEAR 8 FOR DIVIDE
         DR    R8,R14          GET STARTING EXTENT ADDRESS       81047
         STH   R9,XTLOCYL+NEWEXTS-OLDEXTS  SET CYL START         81047
         STH   R8,XTLOTRK+NEWEXTS-OLDEXTS   AND TRACK START      81047
         LR    R9,R0           GET START BACK
         AR    R9,R3           ADD IN # OF TRKS
         BCTR  R9,0            REDUCE TO LAST TRACK ADDRESS
         SLR   R8,R8           CLEAR 8 AGAIN
         DR    R8,R14          GET ENDING TRACK ADDRESS
         STH   R9,XTHICYL+NEWEXTS-OLDEXTS  SET END CYLINDER      81047
         STH   R8,XTHITRK+NEWEXTS-OLDEXTS   AND END TRACK        81047
         MVC   XTTYPE+NEWEXTS-OLDEXTS(2),XTTYPE  COPY EXTENT INFO
         CLI   XTTYPE,XTTSPLIT   SPLIT CYLINDER ?                81047
         BNE   *+8                                               81047
         MVI   XTTYPE+NEWEXTS-OLDEXTS,XTTTRK  SET FOR TRACK      81047
         L     R7,FPFMT5AD     GET START OF FREE AREA
         TM    DSLFG,DSLCYL+DSLCYLP  SEE IF CYLINDER ALLOCATION  81047
         BZ    NOHDTRKS        IF NOT,AT BEGINNING
         SLR   R8,R8           CLEAR 8 FOR COMPARE
         CH    R8,FXBEF      SEE IF TRKS BEFORE CYL IS ZERO      81047
         BE    NOHDTRKS        IF NOT, THEN GET OUT
         SLR   R2,R2                                             91119
         ICM   R2,3,FXABS    GET EXTENT START                    91119
         AH    R2,FXBEF      ADD TRKS BEFORE CYL                 81047
         AR    R2,R3           ADD TRACKS ALLOCATED
         STH   R2,DB+2       TEMP STORE FOR FXABS(R7)            81047
         SLR   R2,R2                                             91119
         ICM   R2,3,FXCYL    GET # CYLINDERS BEFORE ALLOCATION   91119
         SR    R2,R3           SUBTRACT THOSE ALLOC
         ST    R2,DB+4       0-BEF(R7), CYL(R7)                  81047
         AH    R2,FXAFT      ADD IN EXTRA TRACKS                 81047
         BZ    FXTHSEXT        IF ZERO, NO NEW EXTENT
         LTR   R7,R7         IS THERE ANOTHER FREE ONE ?
         BNZ   FXTHHAVE      YES; USE IT
         L     R1,=A(XCESSF5)  ELSE SHOW ERROR                   81047
         SUBCALL DUMPMSG     TOO BAD                             81047
FXTHHAVE MVC   FXABS-FXMAP(6,R7),DB+2  SET ABS/BEF=0/CYL VALUES  81047
         MVC   FXAFT-FXMAP(2,R7),FXAFT  MOVE EXTRA TRACKS OVER   81047
         MVC   FPFMT5AD,FXLINK-FXMAP(R7)  MOVE FREE POINTER      81047
         MVC   FXLINK-FXMAP(4,R7),FXLINK   MOVE EXT POINTER      81047
         ST    R7,FXLINK     KEEP LINKING                        81047
FXTHSEXT XC    FXCYL(4),FXCYL  WIPE OUT CYLS & EXT TRKS          81047
         B     BLASTOUT        GO BACK
NOHDTRKS SLR   R2,R2                                             91119
         ICM   R2,3,FXABS    GET EXTENT START                    91119
         AR    R2,R3           UP TO WHERE EXTENT WOULD END
         STH   R2,FXABS      SAVE NEW EXTENT START               81047
         SLR   R2,R2                                             91119
         ICM   R2,3,FXCYL    GET # OF ORIGINAL TRACKS            91119
         AH    R2,FXBEF      ADD IN TRKS BEFORE CYL              81047
         SR    R2,R3           SUBTRACT THOSE ALLOCATED
         BP    HITIT1          IF A GOOD NUMBER, USE IT
         AH    R2,FXAFT      ADD IN TRKS AFTER CYL               81047
         XC    FXCYL(4),FXCYL   CLEAR IT AND ONE BEFORE          81047
         BP    HITIT2          GO STORE TRKS
         BZ    GIVEBACK        GIVE BACK EXTENT IF ZERO
         SUBCALL DUMPMSG,ERRFMT5M   PRINT AND QUIT               81208
ERRFMT5M VCON  '0PROGRAM ERROR MAINTAINING FREE SPACE POINTERS'  81208
         SPACE 1
HITIT1   LR    R3,R2           LOAD 3 WITH LEFT OVER TRACKS
         SLR   R2,R2           CLEAR 2 FOR DIVIDE
         DR    R2,R14          GET CYLS AND LEFT OVER TRACKS
         MH    R3,VOUTTPC      GET BACK TRACKS ON CYL BOUNDARY   85280
         STH   R3,FXCYL      STORE TRACKS ON CYL BOUNDARY        81047
HITIT2   STH   R2,FXBEF      STORE TRACKS BEFORE CYL             81047
         B     BLASTOUT        GET OUT
         SPACE 1
*        IF ZERO WE JUST USED UP AN EXTENT & HAVE TO GIVE IT BACK
GIVEBACK XC    FXABS(8),FXABS  WIPE OUT EXTENT INFO              81047
         L     R2,FXLINK     GET NEXT ENTRY POINTER              81047
         ST    R2,FXLINK-FXMAP(,R15)   STORE IT IN LAST ENTRY    81047
         ST    R7,FXLINK     SAVE IN MINE                        81047
         ST    R6,FPFMT5AD      MAKE NEW START
         SPACE 1
BLASTOUT LM    R0,R2,RELSAVE   RESTORE EXTENT INFO               81047
         LA    R1,XTLEN(,R1)   BUMP TO NEXT EXTENT               81047
         L     R0,TRKUSD     GET TRACKS TO BE USED               91119
         S     R0,TRKALC     GET TRACKS ALLOCATED                91119
         BZ    RELTEXIT      ALL DONE
         BP    RELTESTL      DO ANOTHER EXTENT                   81047
         L     R1,TRKUSD                                         91119
         LA    R1,1(,R1)
         ST    R1,TRKUSD     SET AT LEAST ONE                    91119
RELTEXIT STC   R2,DS1NOEPV   UPDATE EXTENTS IN FORMAT 1 DSCB     81047
         MVC   PFMT5AD(8),PFMT5SAV  RESTORE CHAIN HEADS          81047
         LM    R14,R0,PFMT5SAD  GET SAVE/LEN/USED                81047
         LR    R1,R15        COPY LENGTH                         81047
         MVCL  R0,R14        RESTORE FREE SPACE CHAIN            81047
RELEXIT4 SUBEX OFF=4         EXIT TO CALLER +4                   81047
         TITLE 'C O P Y V O L  ***  FREE SPACE ALLOCATION ROUTINE'
         USING RELALLOC,R15                                      81047
RELALLOC NI    WORKFLAG,255-WFGTEST   SET FOR REAL ALLOCATION    81047
         B     RELALLCM      GO TO COMMON                        81047
ABSALLOC OI    WORKFLAG,WFGTEST       SET FOR TEST MODE          81047
         DROP  R15                                               81047
RELALLCM SUBENT ,                                                81047
         MVC   PFMT5SAV,PFMT5AD   SAVE CHAIN HEADS               81047
         LM    R14,R0,PFMT5SAD   GET SAVE/LEN/USED               81047
         LR    R1,R15        COPY LENGTH                         81047
         MVCL  R14,R0        SAVE IT                             81047
         L     R1,AMSOUTXA   GET EXTENT LIST TO BE USED          81047
         SLR   R0,R0                                             81047
         IC    R0,DS1NOEPV   GET NUMBER OF EXTENTS               81047
         LTR   R0,R0                                             81047
         BNP   RELALCEX      NONE; QUIT                          81047
         LH    R4,VOUTTPC    LOAD TRACKS PER CYLINDER            85280
         USING EXTMAP,R1                                         81047
REALLOOP CLI   XTTYPE,0      DONE ?                              81047
         BE    RELALCEX      YES; QUIT                           81047
         LH    R2,XTLOCYL    GET START CYLINDER                  81047
         MH    R2,VOUTTPC                                        85280
         AH    R2,XTLOTRK    GET ABSOLUTE ADDRESS                81047
         LH    R3,XTHICYL                                        81047
         MH    R3,VOUTTPC                                        85280
         AH    R3,XTHITRK                                        81047
         LA    R3,1(,R3)                                         81047
         SR    R3,R2         EXTENT SIZE                         81047
         LA    R6,PFMT5AD    POINT TO FREE EXTENT LIST           81047
         USING FXMAP,R6      DECLARE IT                          81047
REALNEXT LR    R5,R6         SAVE PRIOR ELEMENT ADDRESS          81047
         L     R6,FXLINK     GET NEXT ELEMENT                    81047
         LTR   R6,R6         IS THERE ANOTHER ?                  81047
         BNZ   REALNXOK      YES                                 81047
         TM    WORKFLAG,WFGTEST  TEST MODE ?                     81047
         BNZ   RELAFAIL      YES; TAKE BAD RETURN                81047
         SUBCALL DUMPMSG,RELAPGMR  QUIT WITH MESSAGE             81208
RELAPGMR VCON  '0PROGRAM ERROR IN FREE-SPACE PROCESSING'         81208
REALNXOK SLR   R15,R15                                           91119
         ICM   R15,3,FXABS                                       91119
         CR    R2,R15        SPACE IN THIS ENTRY ?               91119
         BL    REALNEXT      NO; TRY NEXT ONE                    81047
         BE    REALSTRT      YES; EXACT START ADDRESS            81047
         ICM   R15,3,FXCYL   GET SIZE OF THIS ENTRY              91119
         AH    R15,FXBEF     GET SIZE OF THIS ENTRY              81047
         AH    R15,FXAFT                                         81047
         SLR   R9,R9                                             91140
         ICM   R9,3,FXABS    GET LAST TRACK+1 IN THIS ENTRY      91140
         AR    R15,R9                                            91140
         CR    R2,R15        IN THIS ONE ?                       81047
         BNL   REALNEXT      NO; TRY AGAIN (BUT WON'T WORK)      81047
         ICM   R7,15,FPFMT5AD   GET FREE FREE ELEMENT            85280
         BNZ   REALINOK      HAVE ONE                            81047
         TM    WORKFLAG,WFGTEST  TEST MODE ?                     81047
         BNZ   RELAFAIL      YES; TAKE BAD RETURN                81047
         L     R1,=A(XCESSF5)                                    81047
         SUBCALL DUMPMSG     QUIT WITH MESSAGE                   81047
REALINOK MVC   FPFMT5AD,FXLINK-FXMAP(R7)  UNCHAIN THIS ONE       81047
         ST    R7,FXLINK-FXMAP(R5)  CHAIN AFTER PREVIOUS ONE     81047
         ST    R6,FXLINK-FXMAP(R7)  CHAIN TO NEXT USED ONE       81047
         MVC   FXABS-FXMAP(2,R7),FXABS  SET START ADDRESS        81047
         LR    R14,R2        START OF EXTENT                     81047
         SLR   R15,R15                                           91119
         ICM   R15,3,FXABS                                       91119
         SR    R14,R15       SIZE TO PUT IN NEW ELEMENT          81047
         LR    R15,R14       SAVE                                81047
         SH    R14,FXBEF     SUBTRACT TRACKS BEFORE              81047
         BP    REALINPL      PLUS; WIPE BEFORE AND KEEP REMOVING 81047
         LCR   R14,R14                                           81047
         STH   R15,FXBEF-FXMAP(,R7)  SET NEW BEFORE              81047
         STH   R14,FXBEF     UPDATE OLD BEFORE                   81047
         B     REALSTNW      ADJUST POINTERS AND STUFF           81047
REALINPL MVC   FXBEF-FXMAP(2,R7),FXBEF  USE ENTIRE BEFORE        81047
         XC    FXBEF,FXBEF   KILL OLD ONE                        81047
         LR    R15,R14       SAVE REMAINDER TO BE STOLEN         81047
         SLR   R9,R9                                             91119
         ICM   R9,3,FXCYL                                        91119
         SR    R14,R9        REMOVE CYLINDER SPACE               91119
         BP    REALINPC      MORE THAN CYLINDER SPACE            81047
         LCR   R9,R14        REMAINDER IN OLD CYLINDER SPACE     81047
         SLR   R8,R8                                             81047
         DR    R8,R4         GET TRACKS BEFORE/CYLINDERS         81047
         STH   R8,FXBEF      MAKE NEW BEFORE                     81047
         MR    R8,R4                                             81047
         STH   R9,FXCYL      UPDATE CYLINDER TRACKS              81047
         LR    R9,R15        GET SIZE STOLEN                     81047
         SLR   R8,R8                                             81047
         DR    R8,R4         CONVERT                             81047
         STH   R8,FXAFT-FXMAP(,R7)                               81047
         MR    R8,R4                                             81047
         STH   R9,FXCYL-FXMAP(,R7)  UPDATE CYLINDER TRACKS       81047
         B     REALSTNW      STORE NEW INFO                      81047
REALINPC MVC   FXCYL-FXMAP(2,R7),FXCYL  STEAL CYLINDER AREA      81047
         XC    FXCYL,FXCYL   CLEAR OLD ONE                       81047
         LH    R15,FXAFT     GET TRACKS REMAINING                81047
         SR    R15,R14       LESS REQUIRED                       81047
         STH   R14,FXAFT-FXMAP(,R7)  SET TRACKS STOLEN           81047
         STH   R15,FXBEF     MOVE TO BEFORE FROM AFTER           81047
         XC    FXAFT,FXAFT                                       81047
REALSTNW STH   R2,FXABS      SET NEW START ADDRESS FOR OLD       81047
         LR    R5,R7         SET CREATED ONE AS PREVIOUS         81047
REALSTRT LA    R14,0(R2,R3)  GET NEW START ADDRESS AFTER ALLOC   81047
         STH   R14,FXABS     SET BACK                            81047
         LR    R15,R3        COPY SIZE TO STEAL                  81047
         AH    R2,FXBEF                                          81047
         SH    R3,FXBEF      ACCOUNT FOR BEFORE AREA             81047
         BP    REALSPL       BEFORE AND THEN SOME                81047
         LCR   R9,R3                                             81047
         STH   R9,FXBEF      SET NEW BEFORE SIZE                 81047
         B     REALTEMP      DONE                                81047
REALSPL  XC    FXBEF,FXBEF   CLEAR BEFORE AREA                   81047
         SLR   R0,R0                                             91119
         ICM   R0,3,FXCYL                                        91119
         AR    R2,R0         ADJUST NEW START                    81047
         SR    R3,R0         REMOVE CYLINDER TRACKS              81047
         BP    REALSPC       ALL AND THEN SOME                   81047
         LCR   R9,R3         COPY RESIDUAL                       81047
         SLR   R8,R8                                             81047
         DR    R8,R4                                             81047
         STH   R8,FXBEF      SET NEW BEFORE SIZE                 81047
         MR    R8,R4                                             81047
         STH   R9,FXCYL      UPDATE CYLINDER TRACKS              81047
         B     REALTEMP      DONE                                81047
REALSPC  XC    FXCYL,FXCYL   ALL OF CYLINDER AREA GONE           86299
         AH    R2,FXAFT      GET AFTER                           81047
         SH    R3,FXAFT                                          81047
         BP    REALSPAF      STILL MORE                          81047
         LCR   R9,R3                                             81047
         STH   R9,FXBEF      MAKE NEW BEFORE                     81047
REALSPAF XC    FXAFT,FXAFT   CLEAR AFTER                         81047
REALTEMP OC    FXBEF(6),FXBEF  ANYTHING LEFT IN THIS ONE ?       81047
         BNZ   REALTEMT      YES; LEAVE IT                       81047
         MVC   FXLINK-FXMAP(4,R5),FXLINK  UNCHAIN IT             81047
         XC    FXABS(8),FXABS  CLEAR IT                          81047
         MVC   FXLINK,FPFMT5AD  CHAIN NEXT FREE                  81047
         ST    R6,FPFMT5AD   CHAIN ON FREE QUEUE                 81047
         LR    R6,R5         SET FOR PRIOR                       81047
REALTEMT LTR   R3,R3         ANY MORE IN THIS EXTENT ?           81047
         BP    REALNEXT      YES; TRY NEXT FREE ELEMENT          81047
REALBUMP LA    R1,XTLEN(,R1)   GET NEXT EXTENT                   81047
         BCT   R0,REALLOOP   DO IT                               81047
RELALCEX TM    WORKFLAG,WFGTEST   CALLED IN TEST MODE ?          81047
         BZ    RELALCE0      NO; TAKE +0 (GOOD) EXIT             81047
         SUBEX OFF=4         GOOD RETURN FOR NON-TEST MODE       81047
RELAFAIL MVC   PFMT5AD(8),PFMT5SAV  RESTORE CHAIN HEADS          81047
         LM    R14,R0,PFMT5SAD  GET SAVE/LEN/USED                81047
         LR    R1,R15        COPY LENGTH                         81047
         MVCL  R0,R14        RESTORE FREE SPACE CHAIN            81047
RELALCE0 SUBEX ,             RETURN TO CALLER                    81047
         DROP  R1,R6         DROP MAPPINGS                       81047
         SPACE 2                                                 79105
*        SUBROUTINE TO BUILD FORMAT 5 DSCBS                      79105
*                                                                79105
BUILDF5  SUBENT WRK=R1                                           81047
         XC    FMT5(140),FMT5   CLEAR NEW FORMAT 5               81047
         LH    R5,VOUTTPC    GET TRACKS PER CYLINDER             85280
         LA    R6,DS5AVEXT+1  GET FIRST EXTENT+1 BYTE            79105
         LA    R7,26         MAX # OF FREE EXTENTS IN FORMAT 5   81047
BUILD5LP LTR   R8,R8           SEE IF GOOD EXTENT                79105
         BZ    BUILD5DN        IF FINISHED GET OUT               79105
         USING FXMAP,R8      DECLARE EXTENT MAPPING              81047
         MVC   0(2,R6),FXABS   MOVE EXTENT START                 81047
         SLR   R14,R14       CLEAR FOR DIVIDE                    81047
         SLR   R15,R15                                           91119
         ICM   R15,3,FXCYL   GET TRACKS ON CYLINDER BOUNDARY     91119
         DR    R14,R5        GET CYLS                            81047
         STH   R15,DB        STORE                               81047
         MVC   2(2,R6),DB    MOVE TO EXTENT                      81047
         LH    R15,FXBEF        GET TRACKS BEFORE CYL            81047
         AH    R15,FXAFT        ADD TRACKS AFTER LAST CYL        81047
         STC   R15,4(,R6)       SAVE IN EXTENT                   79105
         LA    R6,5(,R6)        POINT TO NEXT EXTENT             79105
         L     R8,FXLINK        POINT TO NEXT EXTENT             81047
         BCT   R7,BUILD5LP   KEEP MOVING                         81047
         DROP  R8                                                81047
BUILD5DN MVC   DS5AVEXT(40),DS5AVEXT+1  SHIFT UP                 79105
         MVC   DS5KEYID(4),=4X'05'   IDENTIFY FMT5 KEY           79105
         MVI   DS5FMTID,C'5'  SET TYPE                           79105
         LTR   R8,R8         ANY MORE EXTENTS ?                  79105
         BZ    BUILDF5X      NO; RETURN                          81047
         L     R0,BLOCKAD    ADDRESS OF NEXT DSCB                81047
         LA    R1,ABSHD      OUTPUT ADDRESS                      81047
         BAL   R9,TTRABS     CONVERT TO ABSOLUTE                 81047
         MVC   DS5PTRDS,ABSAD   MOVE TO PTR                      79105
         CLC   DS5PTRDS,DS4HPCHR  VALID ADDRESS ?                79105
         BNH   BUILDF5X      YES; RETURN                         81047
         XC    DS5PTRDS,DS5PTRDS  KILL CHAINING                  79105
         SLR   R8,R8         KILL REMAINING SPACE                79105
         OI    DS4VTOCI,DS4DOSBT  SHOW FMT 5S BAD.               83094
BUILDF5X SUBEX RC=(R8)                                           81047
         SPACE 2                                                 83257
         LTORG ,                                                 83257
         TITLE 'C O P Y V O L  ***  CONTIGUOUS EXTENT CONSOLIDATION'
AMSXCOMP SUBENT ,                                                81047
         MVC   AMSOUTX#,DS1NOEPV  COPY EXTENT COUNT              81047
         MVC   AMSOUTX(16*XTLEN),NEWEXTS  COPY NEW EXTENTS       81047
         SLR   R4,R4         LOAD NO. OF EXTENTS                 *AMS*
         IC    R4,AMSOUTX#   INTO REG 4                          *AMS*
         LA    R5,AMSOUTX-XTLEN  POINT TO FIRST ONE - ONE ENTRY  81047
         CLI   DS1EXT1,XTTLABEL  LABEL EXTENT ?                  81047
         BNE   AMSXCOME      NO; PROCEED                         79105
         LA    R5,XTLEN(,R5)  SKIP IT                            81047
         USING EXTMAP,R5                                         81047
AMSXCOME LA    R5,XTLEN(,R5)   POINT TO NEXT EXTENT              81047
         CLI   XTTYPE,0      ARE WE PAST LAST EXTENT ?           81047
         BE    AMSXCOMX      YES; GET OUT                        *AMS*
         TM    XTTYPE,XTTTRK  TRACK OR CYLINDER ALIGNMENT ?      81047
         BZ    AMSXCOME      NEITHER; LEAVE IT ALONE             81047
AMSXCOML CLI   XTTYPE+XTLEN,0  IS CURRENT EXTENT THE LAST ?      81047
         BE    AMSXCOMX      YES; QUIT                           *AMS*
         TM    XTTYPE+XTLEN,XTTTRK  TRACK/CYLINDER ALIGNED ?     81047
         BZ    AMSXCOME      NEITHER; LEAVE IT ALONE             81047
         LH    R1,XTLOCYL+XTLEN   START CYL OF NEXT EXTENT       81047
         LH    R2,XTLOTRK+XTLEN   START TRACK OF NEXT EXTENT     81047
         SH    R2,=H'1'       GET PREVIOUS TRACK                 *AMS*
         BNM   AMSXCOMM      COMPARE TO END OF CURRENT EXTENT    *AMS*
         LH    R2,XOUTTPC    SET TO LAST TRACK IN PREVIOUS CYL   85280
         BCTR  R1,0          SET TO PREVIOUS CYLINDER            *AMS*
AMSXCOMM CH    R1,XTHICYL    NEXT START CYL = CURRENT END ?      81047
         BNE   AMSXCOME      NO; TRY NEXT EXTENT AS CURRENT      *AMS*
         CH    R2,XTHITRK    PREVIOUS END TRACK = CURR. START ?  81047
         BNE   AMSXCOME      NO; TRY WITH NEXT EXTENT            *AMS*
         MVC   XTHICYL(4),XTHICYL+XTLEN  COMBINE THE EXTENTS     81047
         LR    R3,R5        PREPARE TO ELIMINATE THE EXTRA EXTENT*AMS*
AMSXCOMN LA    R3,XTLEN(,R3)  POINT TO NEXT EXTENT               81047
         DROP  R5                                                81047
         USING EXTMAP,R3                                         81047
         MVC   XTWHOLE,XTWHOLE+XTLEN  MOVE IT DOWN               81047
         CLI   XTTYPE,0      WAS THIS THE LAST ONE ?             81047
         BNE   AMSXCOMN      NO; SQUEEZE ONE MORE                *AMS*
         SH    R4,=H'1'      REMOVE ONE EXTENT                   *AMS*
         BNP   AMSXCOMZ      WE GOOFED - FORGET THE WHOLE THING  *AMS*
         B     AMSXCOML      TRY FOR MORE                        *AMS*
         DROP  R3                                                81047
AMSXCOMX LTR   R4,R4         DON'T TRUST ME - I'M OVER THIRTY    *AMS*
         BNP   AMSXCOMZ        TOLD YOU I BLEW IT; RIGHT ?       *AMS*
         LA    R6,AMSOUTX    USE MY EXTENTS FOR FMT1/FMT3        *AMS*
         STCM  R6,7,AMSOUTXA+1  ADDRESS FOR DSCB BUILD ONLY      81064
         STC   R4,DS1NOEPV    SET NEW EXTENT COUNT FOR DSCB BUILD *AMS*
AMSXCOMZ SUBEX ,             RRETURN                             81047
         TITLE 'C O P Y V O L  ***  EXTENT FIXUP AND RESTORE'    81047
FIXEXTS  SUBENT ,                                                81047
         L     R4,AMSOUTXA   GET ADDRESS OF EXTENTS TO BE USED   81047
         SLR   R5,R5         COUNT                               81047
         SLR   R6,R6                                             81047
         XC    DS1EXT1(3*XTLEN),DS1EXT1  CLEAR FMT 1 EXTENTS     81047
         IC    R6,DS1NOEPV   GET NUMBER OF EXTENTS TO DO         81047
         LTR   R6,R6                                             81047
         BNP   FIXEXTSX      NONE; JUST QUIT                     81047
         XC    DS3EXTNT(4*XTLEN),DS3EXTNT  ALSO FORMAT 3         81047
         XC    DS3ADEXT(9*XTLEN),DS3ADEXT                        81047
         LH    R2,VOUTTPC    GET TRKS/CYLINDER                   85287
         BCTR  R2,0          MAKE LAST TRACK IN CYLINDER         85280
         USING EXTMAP,R4                                         81047
FIXEXTLP CLI   XTTYPE,0      NULL ENTRY ?                        81047
         BE    FIXEXTXC      YES; CLEAR AND QUIT                 81047
         OC    XTLOCYL(4),XTLOCYL  CYL/TRK ZERO ?                81047
         BZ    FIXEXTXC      DITTO                               81047
         OC    XTHICYL(4),XTHICYL  ZERO END ?                    81047
         BZ    FIXEXTXC      YES; KILL                           81047
         STC   R5,XTSEQ      SET EXTENT SEQUENCE NUMBER          81047
FIXEXTNF LA    R5,1(,R5)     UP EXTENT COUNTER                   81047
*        TM    DS1DSORG,JFCORGIS  ISAM DATASET ?                 81047
*        BZ    *+8           NO                                  81047
*        STC   R5,XTSEQ      ISAM STARTS WITH 1                  81047
*        THE ABOVE IS DOCUMENTED IN SVS CONTROL BLOCKS, BUT      81047
*        EVEN SVS DOESN'T ALLOCATE THAT WAY !!!                  81047
         TM    XTTYPE,XTTTRK CYLINDER AND OR TRACK EXTENT ?      81047
         BZ    FIXEXTUP      NO; NO FIXUP                        81047
         OC    XTLOTRK,XTLOTRK  START ON CYL BOUNDARY ?          81047
         BNZ   FIXEXTRK      NO; TRACK                           81047
         CH    R2,XTHITRK        END ON CYL BOUNDARY ?           85280
         BNE   FIXEXTRK      NO; TRACK                           81047
         MVI   XTTYPE,XTTCYL SET CYLINDER BOUNDARY               81047
         B     FIXEXTUP                                          81047
FIXEXTRK MVI   XTTYPE,XTTTRK SET TRACK BOUNDARY                  81047
FIXEXTUP LA    R4,XTLEN(,R4)   GET ANOTHER                       81047
         BCT   R6,FIXEXTLP                                       81047
FIXEXTXC XC    XTWHOLE,XTWHOLE  CLEAR LAST                       81047
         STC   R5,DS1NOEPV   UPDATE COUNT                        81047
         LA    R1,DS1EXT1    POINT TO FIRST EXTENT               81047
         L     R2,AMSOUTXA   GET ADDRESS OF FIRST ONE            81047
         LA    R0,3          NUMBER TO MOVE                      81047
         CR    R5,R0                                             81047
         BH    *+6                                               81047
         LR    R0,R5         SET NUMBER TO MOVE                  81047
         SR    R5,R0         NUMBER AFTER                        81047
         BAL   R14,FIXEXTMV  MOVE ONE GROUP                      81047
         LA    R0,4          SIZE OF NEXT GROUP                  81047
         LA    R1,DS3EXTNT   SET FOR SECOND GROUP                81047
         CR    R5,R0         MORE ?                              81047
         BH    *+6                                               81047
         LR    R0,R5         LIMIT                               81047
         SR    R5,R0         SET NUMBER AFTER MOVE               81047
         BAL   R14,FIXEXTMV  MOVE ANOTHER GROUP                  81047
         LA    R0,9          MOVE LAST GROUP                     81047
         LA    R1,DS3ADEXT                                       81047
         CR    R5,R0         MORE ?                              81047
         BH    *+6                                               81047
         LR    R0,R5         LIMIT                               81047
         BAL   R14,FIXEXTMV                                      81047
FIXEXTSX SUBEX ,             RETURN                              81047
         SPACE 1                                                 81047
FIXEXTMV LTR   R0,R0                                             81047
         BNPR  R14           RETURN IF BAD COUNT                 81047
FIXEXTML MVC   0(XTLEN,R1),0(R2)  MOVE ONE EXTENT                81047
         LA    R1,XTLEN(,R1)                                     81047
         LA    R2,XTLEN(,R2)                                     81047
         BCT   R0,FIXEXTML   DO ALL                              81047
         BR    R14                                               81047
         TITLE 'C O P Y V O L  ***  DATASET SIZE CALCULATION'    81047
AMSSIZER SUBENT ,                                                81047
         SLR   R0,R0                                             81047
         ST    R0,TRKOLD     CLEAR DATASET SIZE                  91119
         CLI   DS1NOEPV,0    DUMMY ?                             81047
         BE    AMSSIZEX      NO; EXIT                            81047
         LA    R14,OLDEXTS   POINT TO FIRST EXTENT               81047
         USING DSLIST,R4                                         81047
         USING EXTMAP,R14    DECLARE IT                          83257
         LA    R3,16         ONLY SIXTEEN ALLOWED                *AMS*
         SLR   R1,R1         ZERO FOR COMPARES                   *AMS*
AMSSIZLP C     R1,XTTYPE     ANYTHING IN IT ?                    81047
         BE    AMSSIZOL      YES; SET CURRENT SIZE               86341
         LH    R2,XTHICYL    GET END CYLINDER                    81047
         SH    R2,XTLOCYL    LESS START CYLINDER                 81047
         CLI   XTTYPE,XTTSPLIT  SPLIT EXTENT ?                   81047
         BNE   AMSSZSEQ      NO                                  81047
         AH    R2,=H'1'      GET CYLINDER COUNT                  81047
         LH    R7,XTHITRK    GET END TRACK                       81047
         SH    R7,XTLOTRK    LESS START                          81047
         AH    R7,=H'1'      MAKE TRACKS IN ONE CYLINDER         81047
         MR    R6,R2         GET TOTAL TRACKS                    81047
         AR    R0,R7         ADD TO RUNNING TOTAL                81047
         B     AMSSIZUP      GET NEXT EXTENT                     81047
AMSSZSEQ MH    R2,VINTPC     CONVERT TO TRACKS                   85280
         AH    R2,XTHITRK    ADD IN END TRACK                    81047
         SH    R2,XTLOTRK    LESS START TRACK                    81047
         AH    R2,=H'1'      PLUS ONE                            *AMS*
         AR    R0,R2         ACCUMULATE                          *AMS*
AMSSIZUP LA    R14,XTLEN(,R14)   GET NEXT EXTENT                 81047
         BCT   R3,AMSSIZLP   TRY IT                              *AMS*
AMSSIZOL ST    R0,TRKOLD     SAVE DATASET SIZE                   91119
         TM    DSLFG,DSLCAT+DSLUNM+DSLISAM+DSLVSAM  SPECIAL ?    81047
         BNZ   AMSSIZIT      YES; USE DATASET SIZE, EVEN IF SLOW *AMS*
         TM    DS1DSORG,JFCORGDA  DIRECT ACCESS ORGANIZATION ?   86292
         BZ    AMSSIZSQ      NO; CHECK WHETHER SEQUENTIAL        86292
         TM    MIGFLAG,OPTMIGR+OPTMIGF  MIGRATING ?              86292
         BNO   AMSSIZSQ      NO; LEAVE ALONE                     86292
         SLR   R14,R14                                           86292
         ICM   R14,3,DS1LRECL   GET THE RECORD LENGTH            86292
         LA    R14,4(,R14)   CONVENTION                          86292
         CLM   R14,3,DS1BLKL  SAME AS BLOCK SIZE ?               86292
         BE    AMSSIZDA      YES; SAS DATASET                    86292
AMSSIZSQ TM    DS1DSORG,JFCORGPO+JFCORGPS  PS/PO ?               86292
         BZ    AMSSIZIT      NO  - DON'T TRUST LSTAR             *AMS*
AMSSIZDA TM    OPTFLAG,OPTKEEP  BYPASS COMPRESSION ?             86292
         BNZ   AMSSIZIT      YES; DO IT THE LONG WAY             79105
         SLR   R0,R0         CLEAR FOR IC                        91119
         ICM   R0,7,DS1LSTAR GET LSTAR TRACK AND RECORD         GP10016
         BZ    AMSSIZIT      NONE; USE EXTENT SIZE              GP10016
         SRL   R0,8          ISOLATE TRACKS                     GP10016
         AH    R0,=H'1'      ADD 1 (REL. TO 0)                   *AMS*
         LA    R14,OLDEXTS   POINT TO EXTENT MAP                 83257
         CLI   XTTYPE,XTTLABEL  USER LABEL EXTENT ?              83257
         BNE   AMSSIZUL      NO; OK AS IS ?                      83257
         LH    R2,XTHICYL    GET END CYLINDER                    83257
         SH    R2,XTLOCYL    LESS START CYLINDER                 83257
         MH    R2,VINTPC     CONVERT TO TRACKS                   85280
         AH    R2,XTHITRK    ADD IN END TRACK                    83257
         SH    R2,XTLOTRK    LESS START TRACK                    83257
         AH    R2,=H'1'      PLUS ONE                            83257
         AR    R0,R2         ADD TO LSTAR VALUE                  83257
AMSSIZUL TM    DSLF2,DSLKEEP   KEEP EXTENTS/TRACKS ?             83257
         BNZ   AMSSIZIT      YES; USE EXTENT SIZE FOR COPY       83257
         TM    DSLF2,DSLPSREL  IS RELEASE FLAG ON ?              81047
         BNZ   AMSSIZRT      YES; CHECK ELIGIBILITY              79327
         TM    OPTFLAG2,OPTPSREL  GLOBAL RELEASE ?               79327
         BZ    AMSSIZIT      NO; TURN IT OFF                     79327
         CLC   DS1DSNAM(3),SYS1  SYS PREFIX ?                    79327
         BE    AMSSIZIT      YES; TURN IT OFF                    79327
AMSSIZRT TM    DS1DSORG,JFCORGPS  PS DATASET ?                   79327
         BZ    AMSSIZIT      NO; OFF IT                          79327
         CLI   DS1NOEPV,0    ANY EXTENTS ?                       79327
         BE    AMSSIZIT      NO                                  79327
         OI    DSLF2,DSLPSREL  ENABLE TRACK RELEASE              81047
         B     AMSSIZEX      RETURN                              81047
AMSSIZIT NI    DSLF2,255-DSLPSREL  DISABLE TRACK RELEASE         81047
AMSSIZEX TM    MIGFLAG,OPTMIGR+OPTMIGF  ARE WE MIGRATING ?       87060
         BNO   AMSSIZEZ      NO                                  87060
         TM    DSLF2,DSLPSREL  IS RELEASE ON ?                   87060
         BNZ   AMSSIZRL      YES; CALCULATE EQUIVALENT SPACE     87060
         TM    DS1DSORG,JFCORGPO  ?                              87060
         BZ    AMSSIZPO      NO                                  87060
         TM    OPTFLAG2,OPTPOREL  PDS SPACE RELEASE ?            87060
         BNZ   AMSSIZRL      YES; CALCULATE EQUIVALENT SPACE     87060
         B     AMSSIZEZ      ELSE MAY FAIL                       87060
AMSSIZPO TM    DS1DSORG,JFCORGPS  SEQUENTIAL ?                   87060
         BZ    AMSSIZRL      NO; ALLOW EXTRA SPACE FOR SAS       87060
         TM    OPTFLAG2,OPTPSREL  GLOBAL RELEASE ?               87060
         BZ    AMSSIZEZ      NO; MAY FAIL                        87060
AMSSIZRL LR    R7,R0         PREPARE FOR DIVIDE AND MULTIPLY     91098
         LA    R5,DB         POINT TO A 12 BYTE WORK AREA        91098
         USING TRKCALC,R5    DECLARE IT                          91098
         XC    0(12,R5),0(R5)    CLEAR IT                        91098
         MVC   STARUCBA+1(3),VINUCB+1  COPY THE UCB ADDRESS      91098
         MVI   STARFLGS,4+STARFUNC  UCB ADDRESS/TRKCAP           91098
         MVC   STARKL,DS1KEYL   COPY KEYLENGTH                   91098
         ICM   R14,3,DS1BLKL  IS THERE A BLOCK SIZE ?            91098
         BNZ   *+8           YES                                 91098
         LA    R14,3625      USE AN ARBITRARY SIZE               91098
         STCM  R14,3,STARDL  DATA LENGTH                         91098
         SERVCALL DVCAP,TRKCALC  GET TRACK CAPACITY              91098
         SLL   R0,1          CHEAT A BIT                        GP10016
         MR    R6,R0         GET TOTAL BLOCKS IN DATASET         91098
         MVC   STARUCBA+1(3),VOUTUCB+1  MAKE OUTPUT ADDRESS      91098
         SERVCALL DVCAP,TRKCALC   GET BLOCKS PER OUTPUT TRACK    91098
         DR    R6,R0         GET EQUIVALENT TRACKS               87060
         LTR   R6,R6         ANY REMAINDER ?                     87060
         BZ    *+8           NO                                  87060
         LA    R6,1          ELSE ADD A TRACK                    87060
         AR    R6,R7         MAKE ROUNDED TRACKS                 87060
         LR    R0,R6         COPY                                91098
         TM    XTTYPE-EXTMAP+OLDEXTS,XTTCYL  CYLINDER ALIGN ?    91099
         BZ    AMSSIZNC      NO                                  91099
         AH    R0,VOUTTPC    ADD ONE CYLINDER'S WORTH OF TRACKS  91139
         B     AMSSIZEZ      EXIT                                91099
AMSSIZNC SRDL  R6,32+2       MOVE TO R7 AND DIVIDE BY 4          91139
         LA    R1,6          MINIMUM OF SIX TRACKS               91099
         MAX   R7,(R1)       USE LARGER FOR PADDING              91099
         AR    R0,R7         ADD IT IN                           91099
AMSSIZEZ ST    R0,TRKUSD       SAVE FOR LATER                    91119
         SUBEX ,             ALL DONE                            81047
         DROP  R14,R5                                            81047
         TITLE 'C O P Y V O L  ***  DATASET INCLUSION/EXCLUSION' 81047
DSNCHOSE SUBENT ,            DATASET EXCLUSION/SELECTION TESTS   81208
         USING DSLIST,R4                                         81047
         MVI   DB,RIXINC     PROVISIONALLY SET COPY ON           79105
         MVI   DB+7,RIXDODAT   SET FOR ACCESS DATE CHECK         81208
         TM    DSNAMES,255-RIXREN  ANY OTHER THAN RENAME ?       81208
         BZ    DSCCONX       NO; DO DATE CHECKING                81208
         IC    R14,DSLF2     GET INPUT VOLUME INDEX              81047
         N     R14,=XL4'7'   MASK                                81047
         STC   R14,DB+1      SAVE INDEX                          81047
         LA    R15,OPTINEX(R14)  GET OPTION BITS                 81047
         MVC   DB+2(1),0(R15)  COPY INCLUDE/EXCLUDE OPTION FOR VOLUME
         LA    R15,X'80'     GET MASK BIT                        81047
         SRL   R15,0(R14)    ADJUST                              81047
         STC   R15,DB+3      SAVE FOR TEST                       81047
         LAT   R6,DSNAMES,DSCCONX  GET TABLE OR QUIT             79105
         USING RIXLIST,R6    DECLARE IT                          79105
         TM    DSNAMES,RIXINC+RIXEXC  EITHER SELECTION ?         79105
         BZ    DSNCHOSF      NO; LEAVE ON                        79105
         TM    DB+2,OPTINCL  INCLUDES ONLY ?                     81047
         BZ    DSNCHOSF      NO; LEAVE ON                        79105
         MVI   DB,0          RESET COPY FLAG                     79105
DSNCHOSF LA    R1,FMT1+43    POINT TO LAST BYTE OF DSN           79105
         LA    R8,44         AND MAX LENGTH                      79105
         LR    R14,R8        SAVE LENGTH                         82137
DSCDSCL  CLI   0(R1),C' '    NON-BLANK ?                         79105
         BNE   DSCDSCHL       YES; HAVE LENGTH                   79105
         BCTR  R1,0          TRY PRECEDING BYTE                  79105
         BCT   R8,DSCDSCL    AND CHOP LENGTH                     79105
         B     DSCCONX       DON'T LIKE SHORT NAMES              79105
DSCDSCHL CR    R8,R14        FULL LENGTH ?                       82137
         BNL   *+8           YES; NO CHANGE                      82137
         LA    R8,1(,R8)     INCLUDE TRAILING BLANK FOR EXACT MATCHES
DSCDSCLP CLI   RIXFG,0       END OF LIST ?                       85253
         BE    DSCCONX       YES; EXIT                           79105
         SLR   R14,R14       LENGTH OF MASK                      82137
         IC    R14,RIXL1     GET LENGTH OF MASK                  79105
         SLR   R1,R1         FOR LENGTH OF REPLACEMENT           79105
         IC    R1,RIXL2      GET LENGTH OF REPLACEMENT           79105
         TM    RIXFG,255-RIXREN  ANY OTHER THAN RENAME ?         79327
         BZ    DSCNDSC       NO; SKIP IT                         79105
         MVC   DB+4(1),RIXFV  COPY VOLUME SELECT BITS            81047
         NC    DB+4(1),DB+3  MASK                                81047
         BZ    DSCNDSC       NOT SELECTED; SKIP                  81047
         CR    R14,R8        IS MASK LONGER THAN DSN ?           79260
         BH    DSCNDSC       YES; TRY NEXT ENTRY                 79105
         LA    R2,RIXMASK    GET MASK ADDRESS                    79105
         LR    R3,R14        AND MASK LENGTH                     79105
         LA    R9,FMT1       AND DSNAME                          79105
DSCMDSC  CLI   0(R2),X'7F'   NON-COMPARE ?                       79105
         BE    DSCMDSCM      YES; SKIP TEST                      92075
         CLI   0(R2),C'?'    ALTERNATE FORM ?                    92075
         BE    DSCMDSCM      YES; ACCEPT                         92075
         CLC   0(1,R2),0(R9) .  EQUAL ?                          79105
         BNE   DSCNDSC       NO - RETURN UNEQUAL                 79105
DSCMDSCM LA    R2,1(,R2)     NEXT MASK BYTE                      79105
         LA    R9,1(,R9)     NEXT DATA BYTE                      79105
         BCT   R3,DSCMDSC   NEW MASK LENGTH                      79105
         OI    RIXF2,RIXMATCH    SHOW AT LEAST ONE MATCH        GP09205
         TM    RIXFG,RIXDSP   PRIORITY REQUEST ?                 79105
         BZ    *+8                                               79105
         OI    DSLFG,DSLPTY  SET PRIORITY COPY                   79105
         TM    RIXFG,RIXDSU   UNMOVABLE ?                        79105
         BZ    *+8                                               79105
         OI    DSLFG,DSLUNM                                      79105
         TM    RIXFG,RIXDNO   RESET REQUEST ?                    79105
         BZ    *+8           NO                                  79105
         NI    DSLFG,255-DSLUNM-DSLPTY  RESET P,U OPTIONS        79105
         TM    RIXFG,RIXKEEP   KEEP OPTION ?                     79327
         BZ    DSCKFNK       NO                                  79327
         OI    DSLF2,DSLKEEP                                     81047
         NI    DSLF2,255-DSLPSREL                                81047
DSCKFNK  TM    RIXFG,RIXPSREL   TRK RELEASE ?                    79327
         BZ    DSCKFNR       NO                                  79327
         OI    DSLF2,DSLPSREL                                    81047
         NI    DSLF2,255-DSLKEEP                                 81047
DSCKFNR  TM    RIXFG,RIXDSU    UNMOVABLE ?                       79327
         BZ    *+8           NO                                  79327
         NI    DSLFG,255-RIXPSREL                                79327
         TM    RIXF2,RIXDODAT+RIXNODAT  DATE CHECK OPTION ?      81208
         BZ    DSCNRDAT      NO                                  81208
         NI    DB+7,255-RIXDODAT-RIXNODAT  RESET                 81208
         TM    RIXF2,RIXNODAT    ACCESS DATE NOT TO BE CHECKED ? 81208
         BZ    DSCNRDAT      NO; LEAVE IT ZERO FOR CHECK         81208
         MVI   DB+7,RIXNODAT   ELSE SET TO CHECK DATE            81208
DSCNRDAT TM    RIXFG,RIXINC  INCLUDE REQUEST ?                   81208
         BZ    DSCEXCT       NO; CHECK EXCLUDE                   79105
         OI    DB,RIXINC     SET INCLUDE FLAG                    79105
         TM    DSNAMES,255-RIXREN-RIXINC   ANY MORE ?            79327
         BZ    DSCCONX       NO; EXIT NOW                        79105
         B     DSCNDSC       ELSE CHECK REMAINING ENTRIES        79105
DSCEXCT  TM    RIXFG,RIXEXC  EXCLUDE ?                           79105
         BZ    DSCNDSC       NO; TRY AGAIN                       79105
         MVI   DB,0          RESET COPY FLAG                     79105
         TM    DSNAMES,255-RIXREN-RIXEXC  ANY MORE ?             79327
         BZ    DSCCONX       NO; EXIT                            79105
         SPACE 1                                                 79105
DSCNDSC  LA    R6,RIXFIX(R14,R6)                                 79105
         AR    R6,R1         GET NEXT DSC LOCATION               79105
         B     DSCDSCLP      AND DO IT                           85253
         SPACE 1                                                 79105
DSCCONX  CLI   DB,RIXINC     COPY THIS DATASET ?                 79105
         BNE   DSCCON0       NO                                  81208
         CLI   DB+7,RIXNODAT  BYPASS ACCESS DATE CHECK ?         81208
         BE    DSCCON4       YES; COPY IT                        81208
         TM    OPTFLAG2,OPTDOOLD+OPTDONEW  ANY DATE CHECKING ?   81208
         BZ    DSCCON4       NO; JUST COPY                       81208
         MVC   DB+1(3),DS1EXPDT  USE EXPIRATION DATE             81208
         CLC   DB+1(3),DS1CREDT  COMPARE TO CREATION DATE        81208
         BNL   *+10          NOT LOW; KEEP IT                    81208
         MVC   DB+1(3),DS1CREDT  ELSE USE CREATION DATE          81208
         CLC   DS1REFD,ZEROES    ANY ACCESS DATE ?               81208
         BE    DSCCKDAT      NO; CHECK DATE                      81208
         MVC   DB2+1(3),DS1REFD  PROVISIONALLY MOVE IT           81208
         AIF   ('&LOCAL' NE 'CCSI').NOPKDAT                      81208
         CLI   DS1REFD,99    POSSIBLE PACKED DATE ?              81208
         BNH   DSCCKREF      NO                                  81208
         TM    DS1REFD+2,X'0F'  PROPER SIGN ?                    81208
         BNO   DSCCKREF      NO                                  81208
         XC    DB2,DB2                                           81208
         MVC   DB2+6(2),DS1REFD+1  GET DAYS                      81208
         CVB   R0,DB2                                            81208
         MVO   DB2,DS1REFD(1)   MOVE YEAR                        81208
         CVB   R1,DB2                                            81208
         STC   R1,DB2+1      SET YEAR                            81208
         STH   R0,DB2+2      AND DAYS                            81208
.NOPKDAT ANOP  ,                                                 81208
DSCCKREF CLC   DB+1(3),DB2+1  HIGHER ?                           81208
         BNL   DSCCKDAT      NO                                  81208
         MVC   DB+1(3),DB2+1  USE ACCESS DATE                    81208
DSCCKDAT CLC   DB+1(3),ACCDATE   COMPARE TO USER'S DATE          81208
         BNL   DSCDATHI      NOT LOWER                           81208
         TM    OPTFLAG2,OPTDOOLD  SELECT OLD DATA SETS ?         81208
         BNZ   DSCCON4       YES; DO IT                          81208
         B     DSCCON0       ELSE SKIP IT                        81208
DSCDATHI TM    OPTFLAG2,OPTDONEW  COPY CURRENT DATASETS ONLY ?   81208
         BNZ   DSCCON4       YES; COPY THIS ONE                  81208
DSCCON0  SUBEX ,             ELSE KILL IT                        81208
DSCCON4  SUBEX OFF=4         RETURN WITH COPY REQUEST            81047
         TITLE 'C O P Y V O L  ***  DSN RENAME / WYLBUR CONV. SETUP'
DSNCONV  SUBENT ,            WYLBUR CONVERSION AND RENAME CHECK  81047
*        DATASET CONVERSION CODE                                 79095
*         1) IF DATASET PREFIX IS IN LIST, CONVERT IT            79095
*         2) IF WYLBUR CONVERSION CHOSEN, SET APPROPRIATE FLAGS  79095
         SPACE 1                                                 79095
         NI    WYLFLAG,255-WYLDSN-WYLBAD                         79095
         TM    WYLFLAG,WYLCON  WYLBUR CONVERSION ?               79105
         BZ    DSNCONW        NO; DON'T CHECK DSN                79105
         CLI   DS1RECFM,X'C0'  IS IT RECFM=U ?                   79095
         BNE   DSNCONW       NO                                  79095
         CLC   =Y(WYLBLOCK),DS1BLKL  BLOCKLENGTH REASONABLE ?    81047
         BL    DSNCONW       NO                                  79095
         CLC   =H'2000',DS1BLKL                                  79095
         BH    DSNCONW                                           79095
         CLC   =H'0',DS1LRECL     RECORD LENGTH REASONABLE ?     79095
         BE    DSNCONU        YES; KEEP CHECKING                 79095
         CLC   =H'80',DS1LRECL     OLD SIZE WITH BUG             79095
         BE    DSNCONU                                           79095
         CLC   DS1BLKL(2),DS1LRECL  OR BLKSIZE=LRECL ?           79095
         BNE   DSNCONW                                           79095
DSNCONU  CLI   DS1DSORG+1,0     BAD DSORG ?                      79095
         BNE   DSNCONW                                           79095
         TM    DS1DSORG,X'FE'-X'42'  OTHER THAN PS/PO ?          79095
         BNZ   DSNCONW        YES                                79095
         TM    DS1DSORG,X'42' EITHER PS OR PO ?                  79095
         BNM   DSNCONW       NO                                  79095
         CLI   DS1KEYL,0      KEYLEN=0 ?                         79095
         BNE   DSNCONW       NO                                  79095
         OI    WYLFLAG,WYLDSN  SET FOR WYLBUR CONVERSION         79095
         SPACE 1                                                 93002
DSNCONW  ICM   R15,15,UEXRENAM  USER RENAME EXIT ?               93002
         BZ    DSNCONL       NO                                  93002
         MVC   DSNAME,DS1DSNAM  SAVE OLD NAME                    93002
         MVC   WORKDSN,WORKDSN-1  CLEAR USER'S NAME FIELD        93002
         LA    R14,DS1DSNAM                                      93002
         LA    R15,VINDDNM                                       93002
         LA    R0,WORKDSN                                        93002
         LA    R1,VOUTDDNM                                       93002
         STM   R14,R1,DB     MAKE CALL PARAMETER LIST            93002
         OI    DB+12,X'80'   SET END OF LIST BIT                 93002
         LA    R1,DB         POINT TO LIST                       93002
         L     R15,UEXRENAM  GET EXIT BACK                       93002
         BALR  R14,R15       INVOKE USER EXIT                    93002
         CH    R15,=H'4'     REJECT DATASET ?                    93002
         BH    DSNUKILL      YES                                 93002
         CLC   WORKDSN,WORKDSN-1  DID USER MOVE ANYTHING ?       93002
         BE    DSNUSAME      NO                                  93002
         CLC   WORKDSN,DSNAME  SAME AS PRIOR NAME ?              93002
         BNE   DSNQMDON      NO; ISSUE RENAME MESSAGE            93002
DSNUSAME MVC   DS1DSNAM,DSNAME  MAKE SURE STILL THE SAME         93002
         B     DSNCONX       AND EXIT AS-IS                      93002
         SPACE 1                                                 93002
DSNUKILL SUBCALL MSGIN,UKILLMSG  SHOW COPY SKIPPED BY USER REQUEST
         SUBEX ,             AND RETURN +0                       93002
         SPACE 1                                                 79095
DSNCONL  TM    DSNAMES,RIXREN  ANY RENAME REQUESTS ?             79105
         BZ    DSNCONX       NO; RETURN                          79105
         LAT   R6,DSNAMES,DSNCONX  GET DSN NAME LIST OR EXIT     79095
         USING RIXLIST,R6    DECLARE MAPPING                     79105
         USING DSLIST,R4     DECLARE LIST ENTRY                  81047
         IC    R14,DSLF2     GET INPUT VOLUME INDEX              81047
         DROP  R4                                                81047
         N     R14,=XL4'7'   MASK                                81047
         STC   R14,DB+1      SAVE INDEX                          81047
         LA    R15,OPTINEX(R14)  GET OPTION BITS                 81047
         MVC   DB+2(1),0(R15)  COPY INCLUDE/EXCLUDE OPTION FOR VOLUME
         LA    R15,X'80'     GET MASK BIT                        81047
         SRL   R15,0(R14)    ADJUST                              81047
         STC   R15,DB+3      SAVE FOR TEST                       81047
         LA    R1,FMT1+43    POINT TO LAST BYTE OF DSN           79095
         LA    R8,44         AND MAX LENGTH                      79095
         LR    R14,R8        SAVE LENGTH                         82137
DSNDSNL  CLI   0(R1),C' '    NON-BLANK ?                         79095
         BNE   DSNDSNHL       YES; HAVE LENGTH                   79095
         BCTR  R1,0          TRY PRECEDING BYTE                  79095
         BCT   R8,DSNDSNL    AND CHOP LENGTH                     79095
         B     DSNCONX       DON'T LIKE SHORT NAMES              79095
DSNDSNHL CR    R8,R14        FULL LENGTH ?                       82137
         BNL   *+8           YES; NO CHANGE                      82137
         LA    R8,1(,R8)     INCLUDE TRAILING BLANK FOR EXACT MATCHES
DSNDSNLP CLI   RIXFG,0       END OF LIST ?                       85253
         BE    DSNCONX       YES; EXIT                           79095
         SLR   R14,R14       LENGTH OF MASK                      82137
         IC    R14,RIXL1     GET LENGTH OF MASK                  79095
         SLR   R1,R1         FOR LENGTH OF REPLACEMENT           79095
         IC    R1,RIXL2      GET LENGTH OF REPLACEMENT           79095
         TM    RIXFG,RIXREN   RENAME ENTRY ?                     79105
         BZ    DSNNDSN       NO; SKIP IT                         79105
         MVC   DB+4(1),RIXFV  COPY VOLUME SELECT BITS            81047
         NC    DB+4(1),DB+3  MASK                                81047
         BZ    DSNNDSN       NOT SELECTED; SKIP                  81047
         CR    R14,R8        IS MASK LONGER THAN DSN ?           79260
         BH    DSNNDSN       YES; TRY NEXT ENTRY                 79095
         LA    R2,RIXMASK    GET MASK ADDRESS                    79095
         LR    R3,R14        AND MASK LENGTH                     79095
         LA    R4,FMT1       AND DSNAME                          79095
         TM    RIXF2,RIXCMSK  COMPARE MASK ?                     81047
         BNZ   DSNMDSN       YES; COMPARE BYTE BY BYTE           81047
         BCTR  R3,0          DECREASE MASK LENGTH                81047
         EX    R3,MASKCLC    COMPARE                             81047
         BNE   DSNNDSN       NOT EQUAL                           81047
         B     DSNQDSN       EQUAL - MOVE IT                     81047
MASKCLC  CLC   0(0,R2),0(R4)  COMPARE PREFIX TO DSN              81047
DSNMDSN  CLI   0(R2),X'7F'   NON-COMPARE ?                       79095
         BE    DSNMDSNX      YES; SKIP TEST                      79095
         CLI   0(R2),C'?'    NON-COPY MASK BYTE ?                81047
         BE    DSNMDSNX      YES; ACCEPT                         81047
         CLC   0(1,R2),0(R4) .  EQUAL ?                          79095
         BNE   DSNNDSN       NO - RETURN UNEQUAL                 79095
DSNMDSNX LA    R2,1(,R2)     NEXT MASK BYTE                      81047
         LA    R4,1(,R4)     NEXT DATA BYTE                      79095
         BCT   R3,DSNMDSN   NEW MASK LENGTH                      79095
DSNQDSN  MVC   WORKDSN(88),WORKDSN-1  CLEAR NEW DSNAME WORK FIELD
         LA    R2,43                                             79095
         SR    R2,R14        LENGTH TO MOVE                      79095
         BM    DSNNTRAL       SKIP TRAILER                       79095
         LA    R3,FMT1(R14)  GET FIRST BYTE OF TRAILER           79095
         LA    R4,WORKDSN(R1)  NEW LOCATION                      79095
         EX    R2,TRAILMVC   MOVE IT                             79095
DSNNTRAL LA    R4,WORKDSN    GET HEADER DEST                     79095
         LA    R3,RIXREP(R14)  REPLACEMENT HEADER                79095
         LR    R2,R1         HEADER LENGTH                       79095
         BCTR  R2,0          LESS ONE                            79095
         LTR   R2,R2         ANY ?                               79095
         BM    DSNQMVC       NO; JUST PUT IT BACK                79095
         EX    R2,TRAILMVC   MOVE HEADER                         79095
DSNQMVC  MVC   DSNAME,DS1DSNAM  SAVE OLD DSNAME                  79105
         TM    RIXF2,RIXRMSK  REPLACEMENT STRING IS MASK ?       81047
         BZ    DSNQMDON      NO; SKIP                            81047
         LR    R15,R14       GET MASK LENGTH                     81047
         LR    R0,R1         GET REPLACEMENT LENGTH              81047
         LA    R2,RIXMASK    MASK ADDRESS                        81047
         LA    R3,DSNAME     OLD DSN                             81047
         LA    R4,WORKDSN    NEW DSN                             81047
DSNQMLOP CLI   0(R2),X'7F'   INPUT MASK BYTE ?                   81047
         BNE   DSNQMBMP      NO                                  81047
DSNQMLOO CLI   0(R4),X'7F'   OUTPUT REPLACE BYTE ?               81047
         BE    DSNQMEQ       YES; REPLACE                        81047
         LA    R4,1(,R4)                                         81047
         BCT   R0,DSNQMLOO   TRY AGAIN                           81047
         B     DSNQMDON                                          81047
DSNQMEQ  MVC   0(1,R4),0(R3)  REPLACE THE BYTE                   81047
         LA    R4,1(,R4)                                         81047
         BCT   R0,DSNQMBMP   BUMP IF MORE                        81047
         B     DSNQMDON      ELSE QUIT                           81047
DSNQMBMP LA    R2,1(,R2)                                         81047
         LA    R3,1(,R3)                                         81047
         BCT   R15,DSNQMLOP  REPEAT                              81047
DSNQMDON MVC   DS1DSNAM(44),WORKDSN  SET NEW NAME                81047
         MVC   RENAMSGQ,RENAMPR   SET 'RENAMED' MESSAGE          81047
         MVC   RENAMSGD,WORKDSN   SHOW NEW NAME                  81047
         CLI   WORKDSN+44,C' '  OVERFLOW ?                       79105
         BNE   DSNQSPIL      YES; TOO BAD                        79105
         CLC   WORKDSN+45(43),WORKDSN+44  ALL BLANK ?            79105
         BE    DSNQSPIM      YES; LEAVE IT                       81047
DSNQSPIL MVC   RENAMSGQ,RENAMPT  SET 'TRUNCATED'                 81047
         OI    CCODE+1,2     SET MINOR ERROR                     81047
DSNQSPIM SUBCALL MSGIN,RENAMSG  PRINT RENAMED/TRUNCATED MESSAGE  81208
         SUBEX OFF=8         RETURN RENAMED                      93002
DSNCONX  SUBEX OFF=4         RETURN UNCHANGED                    93002
         SPACE 1                                                 79095
DSNNDSN  LA    R6,RIXFIX(R14,R6)                                 79095
         AR    R6,R1         GET NEXT DSN LOCATION               79095
         B     DSNDSNLP      AND DO IT                           85253
         DROP  R6                                                79105
         SPACE 1                                                 79105
TRAILMVC MVC   0(0,R4),0(R3)  MOVE DSN PORTION                   79095
RENAMSG  DC    AL1(L'RENAMSGD+RENAMSGD-RENAMSGQ-1)               81047
RENAMSGQ DC    C'TRUNCAT',C'ED TO '                              81047
RENAMSGD DC    CL44' '       NEW DSN                             81047
RENAMPR  DC    C'- RENAM'                                        81047
RENAMPT  DC    C'TRUNCAT'                                        81047
UKILLMSG DC    AL1(20),C'REJECTED BY USER EXIT'                  93002
         SPACE 1                                                 86138
         LTORG ,                                                 86138
         TITLE 'C O P Y V O L  ***  LOCAL DSCB CHECKING'         86138
*        THIS SUBROUTINE PERFORMS LOCAL VALIDITY CHECKING:       86138
*                                                                86138
*        ON ANY VOLUME, A GDG PATTERN MUST HAVE A CATALOG ENTRY, 86138
*        OTHER DATASETS MUST HAVE A VALID DSORG, RECFM, LRECL AND
*        BLKSIZE. PS/PO DATASETS MUST ALSO HAVE A NON-ZERO LSTAR.
*                                                                86138
*        FOR SPECIAL VOLUMES (TSO, WYLBUR, LIBPAK), THE DATASET NAME
*        AND DSORG MUST MEET RESTRICTIONS.                       86138
*                                                                86138
***********************************************************************
         SPACE 1                                                 86138
VOLCLEAN SUBENT ,                                                86138
         SERVCALL VSNFG,VINVSER  GET FLAGS FOR VOLUME            86138
         STH   R0,INFLTEST   SAVE FLAGS RETURNED                 86138
         MVC   EXP,EXP-1     CLEAR THE EXPLANATION FIELD         86138
         LA    R1,DSNLIST    CHECK SPECIAL DSNAMES NOT TO BE SCRATCHED
         LA    R2,DSNEND                                         86138
         BAL   R14,DSNCHK    GO CHECK FOR COMMON SPECIAL NAMES   86138
         SPACE 1                                                 86138
         LA    R1,INDICES    CHECK FOR SPECIAL INDEX LEVELS      86138
         LA    R2,INDEND                                         86138
         BAL   R14,DSNCHK    SEE IF SPECIAL INDEX                86138
         SPACE 1                                                 86138
*DEFUNCT TM    INFLTEST+1,VF2DSNW  WYLBUR PACK ?                 86138
*DEFUNCT BNZ   WYLSCR        YES - DO SPECIAL WYLBUR FUNCTIONS   86138
*NOT*    SERVCALL WYLDX,DS1DSNAM,ERR=NO  SEE IF WYLBUR NAME      86138
         TM    INFLTEST+1,VF2DSNT   TSO STORAGE VOLUME ?         86138
         BNZ   TSOTEST       YES; TEST FOR VALID TSO USERID      86138
         TM    INFLTEST+1,VF2DSNA  LIBRARY PACK ?                86138
         BZ    USERVOL       NO; DO SPECIAL USER VOLUME CODE     86138
         CLI   DS1NOEPV,0    PATTERN DSCB ?                      86138
         BE    GDGPAT        YES; CHECK SOME MORE                86138
         SERVCALL  DSLIB,DS1DSNAM,ERR=SERVERR   CHECK LIBPAK DSN 86138
         SPACE 1                                                 86138
COMTEST  CLI   DS1DSORG+1,0     ANY REALLY FUNNY DSORG ?         86138
         BNE   BADORG                                            86138
         TM    DS1DSORG,255-JFCORGIS-JFCORGPS-JFCORGDA-JFCORGPO  ANY ?
         BNZ   BADORG        YES; NOT PERMITTED                  86138
         TM    DS1DSORG,JFCORGIS+JFCORGPS+JFCORGDA+JFCORGPO  ANYTHING ?
         BZ    BADORG        NONE; KILL IT                       86138
COMDCB   TM    DS1DSORG,JFCORGPS+JFCORGPO   DS1LSTAR VALID ?     86138
         BNM   LIBFORM       NO; SKIP DS1LSTAR CHECK             86138
         CLC   DS1LSTAR(3),ZEROES  EVER USED ?                   86138
         BE    BADUSE        NO; KILL IT                         86138
LIBFORM  TM    DS1RECFM,JFCFMREC    RECFORM ?                    86138
         BZ    BADFORM       NO; KILL IT                         86138
         BO    LIBFORL       =U; SKIP LRECL CHECK                86138
         CLC   DS1LRECL,ZEROES  ANY RECORD LENGTH ?              86138
         BNE   LIBFORL       YES; PASS IT                        86138
         TM    INFLTEST+1,VF2DSNT                  USER DSN ?    92344
         BNZ   BADLRECL      NO; ERROR                           87135
         TM    DS1RECFM,JFCFIX  RECFM=U OR F ?                   87135
         BZ    BADLRECL      NO; NOT VALID                       87135
         TM    DS1RECFM,JFCRFB  FB ?                             87135
         BZ    LIBFORL       NO; USABLE DATASET IF BLKL NOT 0    87135
BADLRECL MVC   EXP(9),=C'BAD LRECL'                              86138
         CLC   DS1LRECL,ZEROES  ANY AT ALL ?                     86138
         BZ    BADTONO       CHANGE 'BAD' TO 'NO'                86138
         B     SCRTCH        KILL IT                             86138
LIBFORL  CLC   DS1BLKL(2),ZEROES   ANY BLOCKSIZE ?               86138
         BE    BADBLOCK      NO; TOO BAD                         86138
         SPACE 1                                                 86138
EXPTEST  TM    OPTFLAG2,OPTDOOLD+OPTDONEW  ANY USER DATE SELECTION ?
         BNZ   VOLCEXIT      YES; SKIP EXPIRATION CHECK          86138
         ICM   R0,7,DS1EXPDT  ANY EXPIRATION ?                   86138
         BZ    VOLCEXIT      NONE; KEEP IT                       86138
         CLM   R0,7,DS1CREDT SAME AS CREATION ?                  86138
         BNH   VOLCEXIT      THIS IS IBM'S IDEA OF A JOKE        86138
*                            - SETTING AN UNUSABLE DEFAULT       86138
         CLM   R0,7,CURDSDAT CHECK DATE                          86138
         BNL   VOLCEXIT      NOT PASSED, YET; KEEP               86138
         MVC   EXP(7),=C'EXPIRED'                                86138
         SLR   R0,R0                                             86138
         IC    R0,DS1EXPDT   GET YEAR                            86138
         SLR   R1,R1                                             86138
         ICM   R1,3,DS1EXPDT+1  AND DAY                          86138
         MH    R0,=H'1000'   FINAGLE                             86138
         AR    R0,R1         MAKE YYDDD                          86138
         CVD   R0,DB         MAKE PACKED                         86138
         MVC   EXP+7(6),=X'F02020202120'  MAKE EDIT MASK         86138
         ED    EXP+7(6),DB+5   MAKE DATE PRINTABLE               86138
         MVI   EXP+7,C' '    RESTORE BLANK                       86138
         B     SCRTCH        GET RID OF IT                       86138
         SPACE 2                                                 86138
USERVOL  CLI   DS1NOEPV,0    ANY EXTENTS ?                       86138
         BE    GDGPAT        NO; CHECK GDG                       86138
         CLI   DS1DSORG+1,0  ANY DSORG ?                         86138
         BNE   LIBFORM       YES                                 86138
         TM    DS1DSORG,255-JFCORGU  OTHER THAN JUST U ?         86138
         BZ    BADORG        NO; FAIL                            86138
         B     COMDCB        CHECK DCB AND EXPDT                 86138
         SPACE 1                                                 86138
GDGPAT   CLC   DS1RECFM(6),ZEROES  ANY RECFM/LRECL/BLKSIZE ?     86138
         BNE   GDGDCBS       YES; ALLOW                          86138
         TM    DS1RECFM,JFCFMREC+JFCRFB   ANY RECFORM ?          86138
         BZ    BADFORM       NO                                  86138
         CLC   DS1BLKL(2),ZEROES   ANY BLOCKSIZE ?               86138
         BE    BADBLOCK      NO; TOO BAD                         86138
GDGDCBS  MVC   CATCALL,DS1DSNAM  MOVE DSN                        86138
         LA    R0,43         FIND END OF NAME                    86138
         LA    R1,CATCALL+44                                     86138
         LA    R14,1         CONSTANT                            86138
GDGPATLP BCTR  R1,0          GET PREVIOUS BYTE                   86138
         CLI   0(R1),C' '    TRAILING BLANK ?                    86138
         BNE   GDGPATCH      NO                                  86138
         BCT   R0,GDGPATLP   TRY AGAIN                           86138
         B     NAMEBAD       INVALID NAME                        86138
GDGPATCH MVC   1(2,R2),=X'4B15'  MAKE BAD NAME                   86138
         XC    CATCALL+44(20),CATCALL+44  CLEAR RESULT           86138
         SERVCALL LOCMT,CATCALL  LOCATE INDEX ENTRY              86138
         CLI   CATVOL2,C' '  WAS A CVOL SET ?                    86138
         BNH   GDGELSE       NO; KILL IT                         86138
         CLC   VINVSER,CATVOL2  SAME AS THIS VOLUME ?            86138
         BE    VOLCEXIT      YES; CHECK NEXT DATASET             86138
         CLC   VOUTVSER,CATVOL2  SAME AS OUTPUT VOLUME ?         86138
         BE    VOLCEXIT      YES; CHECK NEXT DATASET             86138
GDGELSE  MVC   EXP(8),=C'BAD CVOL'                               86138
         B     SCRTCH        SCRATCH IT                          86138
         SPACE 2                                                 86138
WYLSCR   SERVCALL  DSWYL,DS1DSNAM,ERR=SERVERR   CHECK WYLBUR NAME
         CLI   DS1DSORG+1,0  ANY REALLY FISHY DSORG ?            86138
         BNE   BADORG        NO; FAIL                            86138
         TM    DS1DSORG,JFCORGPO+JFCORGPS  PS/PO ?               86138
         BNM   BADORGW       NO; TOO BAD                         86138
         TM    DS1DSORG,255-JFCORGPO-JFCORGPS   OTHER THAN PS/PO ?
         BZ    OKDSORG       NO; USE IT                          86138
BADORGW  CLI   DS1DSORG,JFCORGDA  DSORG=DA ?                     86138
         BNE   BADORG        NO                                  86138
         TM    DS1RECFM,JFCUND  RECFM=U ?                        86138
         BNO   BADORG        NO                                  86138
         LH    R15,DS1LRECL   GET RECORD LENGTH                  86138
         LA    R15,4(,R15)    +4                                 86138
         CH    R15,DS1BLKL     MATCHES BLOCK SIZE ?              86138
         BNE   BADORG        NO; FAIL NON-SAS DATASET            86138
         SPACE 1                                                 86138
VOLCEXIT SUBEX OFF=4         TAKE 'ACCEPT' EXIT                  86138
         SPACE 1                                                 86138
BADVALID TM    OPTFLAG2,OPTDEBUG  DEBUGGING ?                    86138
         BZ    VOLCEXIT      NO; IGNORE SERVICE OR PROGRAM ERROR 86138
         STM   R15,R0,DB     SAVE FOR DUMP                       86138
         SUBCALL DUMPMSG,MSGVCBAD  TAKE A DIVE                   86138
         SPACE 1                                                 86138
BADORG   MVC   EXP(9),=C'BAD DSORG'    SET UNACCEPTABLE DSORG    86138
         CLC   DS1DSORG(2),ZEROES  ANY DSORG ?                   86138
         BNE   SCRTCH        YES; SCRATCH                        86138
BADTONO  MVC   EXP(3),DCNO   MOVE 'NO '                          86138
         B     SCRTCH                                            86138
OKDSORG  CLC   DS1LSTAR(3),ZEROES  EVER USED ?                   86138
         BNE   ISUSED        YES                                 86138
BADUSE   MVC   EXP(8),=C'NOT USED'     NO LSTAR, NO SAVE         86138
         B     SCRTCH                                            86138
ISUSED   TM    DS1RECFM,JFCFMREC  CHECK RECORD FORMAT            86138
         BO    OKRECFM       OK; CHECK MORE                      86138
         BM    WYLTLREC      CHECK RECORD LENGTH                 86138
BADFORM  MVC   EXP(9),=C'BAD RECFM'    NO MAJOR RECORD FORMAT    86138
         CLI   DS1RECFM,0       ANY ?                            86138
         BE    BADTONO       NO; CHANGE TO 'NO ...'              86138
         B     SCRTCH        YES; LEAVE 'BAD ...'                86138
WYLTLREC CLC   DS1LRECL,ZEROES  ANY RECORD LENGTH ?              86138
         BE    BADLRECL      NO; FAIL                            86138
OKRECFM  CLI   DS1NOEPV,0    PATTERN DSCB ?                      86138
         BE    BADUSE        YES; SET 'NOT USED'                 86138
         CLC   DS1BLKL(2),ZEROES  ANY BLOCKSIZE AT ALL ?         86138
         BNE   EXPTEST       YES; OK                             86138
BADBLOCK MVC   EXP(9),=C'BAD BLKSZ'    SHOW INVALID SIZE         86138
         CLC   DS1BLKL(2),ZEROES  ANY BLOCKSIZE ?                86138
         BE    BADTONO       NO; CHANGE TO 'NO ...'              86138
         B     SCRTCH        ELSE LEAVE 'BAD ...'                86138
BADINDEX MVC   EXP(9),=C'BAD INDEX'  WRONG INDEX FOR ACCOUNT/USER
         B     SCRTCH        DELETE IT                           86138
         SPACE 2                                                 86138
TSOTEST  MVC   HINDEX(8),=CL8' '   MAKE BLANK                    86138
         LA    R1,HINDEX                                         86138
         LA    R0,1                                              86138
         LA    R2,DS1DSNAM                                       86138
         LA    R15,9                                             86138
TSOTESTL CLI   0(R2),C'.'    TRAILING DOT ?                      86138
         BE    TSOTESTB      YES                                 86138
         MVC   0(1,R1),0(R2)  COPY A BYTE                        86138
         AR    R1,R0                                             86138
         AR    R2,R0                                             86138
         BCT   R15,TSOTESTL                                      86138
         B     BADINDEX      INVALID INDEX                       86138
TSOTESTB DS    0H                                                92344
         AIF   ('&LOCAL' NE 'PID').NOTESTK                       92279
         L     R14,CVTPTR    GET THE CVT                         86138
         ICM   R14,15,CVTUSER-CVTMAP(R14)  GET THE LOCAL CVT EXTENSION
         BZ    TSOTESTK      CAN'T TEST                          86138
         USING USERCVT,R14                                       86138
         ICM   R14,15,UCLUADS  USER LIST PRESENT ?               86138
         BZ    TSOTESTK      NO; SKIP CHECK                      86138
         LM    R15,R1,0(R14) GET THE BXLE POINTERS               86138
TSOTESTX CLC   HINDEX,0(R15)  MATCHING INDEX ?                   92344
         BE    TSOTESTK      YES; OK                             86138
         BXLE  R15,R0,TSOTESTX  ELSE TRY AGAIN                   86138
         B     BADINDEX                                          86138
         DROP  R14                                               86138
.NOTESTK SPACE 1                                                 90186
TSOTESTK CLI   DS1NOEPV,0    PATTERN DSCB ?                      86138
         BE    GDGPAT        YES; GO TO CHECK IT                 86138
         B     COMTEST       ELSE TEST FOR USED DATASET          86138
         SPACE 2                                                 86138
SERVERR  CH    R15,=H'16'    RETURN CODE 8 ?                     86138
         BH    BADVALID      NO; PROGRAM ERROR                   86138
         STC   R0,DB         SAVE RETURN FLAGS                   86138
         CLI   DB,VRPARM     BAD PARM ?                          86138
         BE    BADVALID      YES; PROGRAM ERROR ?                86138
         SLR   R15,R15       CLEAR FOR IC                        86138
         IC    R15,DB        RELOAD CODE                         86138
         MH    R15,=Y(L'VALMSG)                                  86138
         LA    R15,VALMSG-2*L'VALMSG(R15)                        86138
         MVC   EXP(L'VALMSG),0(R15)                              86138
         B     SCRTCH        SCRATCH                             86138
         SPACE 2                                                 86138
NAMEBAD  MVC   EXP(7),=C'BAD DSN'                                86138
         SPACE 1                                                 86138
*        SCRATCH THE DATA SET                                    86138
*                                                                86138
*DELETE* TM    DS1DSIND,X'40'+X'10'  RACF OR PASSWORD PROTECTED ?
SCRTCH   TM    DS1DSIND,X'40'    RACF PROTECTED?                 87245
         BNZ   VOLCEXIT      YES; LEAVE IT ALONE             861386138
         SUBCALL MSGIN,MSGLFAIL   LOCAL FAILURE                  86138
         SUBEX ,             RETURN TO COUNT AND SKIP            86138
         SPACE 1                                                 86138
*        SPECIAL DSNAME CHECKER                                  86138
DSNCHK   SLR   R3,R3         CLEAR FOR IC                        86138
DSNLP    IC    R3,0(,R1)                                         86138
         EX    R3,CLDSN      IS IT SPECIAL ?                     86138
         BE    VOLCEXIT      YES, NO SCRATCH                     86138
         LA    R1,2(R1,R3)   CHECK NEXT ENTRY                    86138
         CR    R1,R2                                             86138
         BL    DSNCHK                                            86138
         BR    R14           RETURN TO CALLER                    86138
CLDSN    CLC   DS1DSNAM(0),1(R1)  TEST FOR SPECIAL NAME/PREFIX   86138
         SPACE 1                                                 86138
VALMSG   DC    CL9'BAD NAME'           2                         86138
         DC    CL9'BAD ACCT'           3                         86138
         DC    CL9'BAD NAME'           4                         86138
         DC    CL9'INVLD DSN'          5                         86138
         DC    CL9'SHORT DSN'          6                         86138
         DC    CL9'DSN LONG'           7                         86138
         DC    CL9'BAD INDX'           8                         86138
DCNO     DC    CL9'NO PSWD'            9                         86138
*        MESSAGES                                                86138
*                                                                86138
MSGVCBAD MSG   'VOLCLEAN - LOGIC ERROR'                          86138
MSGLFAIL DC    AL1(MSGLFAIN-*-1),C'* NOT COPIED - '              86138
EXP      DC    CL13' '                                           86138
MSGLFAIN DC    C' '          EXTRA FOR OVERRUN                   86138
         SPACE 1                                                 86138
DSNLIST DSNAME PASSWORD                                          86138
        DSNAME SYSCTLG                                           86138
DSNEND   EQU   *                                                 86138
*                                                                86138
INDICES  INDEX SYS1                                              86138
INDEND   EQU   *             END OF INDEX LIST                   86138
*DEFER*  USERVOLT ,          DEFINE VOLT CALL/MAP                86138
INFLTEST DC    H'0'          FLAGS RETURNED BY SERVCALL         GP09287
VF2DSNA  EQU   X'08'           ALL DSNS HAVE ACCT INDEX         GP09287
VF2DSNT  EQU   X'01'           ALL DSNS HAVE TSO UID INDEX      GP09287
COPYFDR  CSECT ,                                                 86138
         TITLE 'C O P Y V O L  ***  WYLBUR CONVERSION ROUTINE'   81047
WYLCONV  SUBENT ,                                                81047
         USING BUFFMAP,R4    DECLARE IT                          81047
         L     R2,DWACOUNT    GET OUTPUT CCW STRING              81047
WYLCCW   CLI   0(R2),X'1D'   WRITE C,K,D ?                       79095
         BNE   WYLCNEXT      NO; SKIP                            79095
         L     R3,0(,R2)    GET ADDRESS                          79095
         CLI   5(R3),0       ANY KEY LENGTH ?                    79095
         BNE   WYLCNEXT       YES; SKIP                          79095
         CLC   =H'7',6(R3)   LONG ENOUGH ?                       79095
         BH    WYLCNEXT      NO                                  79095
         CLC   6(2,R3),8(R3)  LENGTH=CONTROL WORD ?              79095
         BNE   WYLCNEXT      NO; SKIP                            79095
         CLC   =Y(WYLBLOCK),6(R3)  NOT TOO LARGE ?               81047
         BL    WYLCNEXT       TOO BAD                            79095
         SLR   R4,R4         CLEAR FOR IC                        79095
         IC    R4,6(,R3)     GET FIRST LENGTH BYTE               79095
         SLL   R4,8           SHIFT OVER                         79095
         IC    R4,7(,R3)     AND SECOND                          79095
         LA    R1,10(,R3)    POINT TO FIRST DATA BYTE            79095
         LA    R6,2          SET LENGTH PROCESSED                79095
         SLR   R14,R14                                           79095
WYLCLOOP IC    R14,4(,R1)    GET LINE LENGTH                     79095
         CH    R14,=Y(WYLRECL)  LEGAL ?                          81047
         BH    WYLCNEXT      NO; SKIP                            79095
         LA    R1,5(R14,R1)  SKIP TO NEXT                        79095
         LA    R6,5(R14,R6) SET NEW LENGTH                       79095
         CR    R6,R4         CHECK AGAINST BLOCK                 79095
         BL    WYLCLOOP      LOW; TRY AGAIN                      79095
         BH    WYLCNEXT      HIGH; INVALID FORMAT                79095
         SPACE 1                                                 79095
         LA    R1,10(,R3)    POINT TO DATA                       79095
         LA    R6,2          CURRENT SIZE PROCESSED              79095
         SLR   R14,R14                                           79095
WYLCCON  IC    R14,4(,R1)    GET LINE LENGTH                     79095
         TM    0(R1),X'80'   OSI FORMAT ?                        79095
         BNZ   WYLCCOSI      YES; JUST KILL FLAG                 79095
         LH    R7,0(,R1)     GET INTEGER PORTION                 79095
         MH    R7,=H'1000'                                       79095
         AH    R7,2(,R1)     ADD FRACTION                        79095
         STCM  R7,15,0(R1)   STASH BACK OBS LINE NUMBER          79095
WYLCCOSI NI    0(R1),X'7F'    KILL OSI FLAG BIT                  79095
         LA    R6,5(R14,R6)    NEW LENGTH                        79095
         LA    R1,5(R14,R1)  NEW RECORD                          79095
         CR    R6,R4         DONE WITH BLOCK ?                   79095
         BL    WYLCCON                                           79095
*        BH    WYLCNEXT     SHOULD NOT HAPPEN                    79095
         SPACE 1                                                 79095
WYLCNEXT TM    4(R2),X'40'  COMMAND CHAIN ?                      79095
         LA    R2,8(,R2)     POINT TO NEXT COMMAND               79095
         BNZ   WYLCCW        YES; PROCESS NEXT COMMAND           79095
         SUBEX ,             RETURN                              81047
         TITLE 'C O P Y V O L  ***  COMMON DSN MESSAGE ROUTINE'  81047
MSGINSUP TM    OPTFLAG,OPTERSE  TERSE MODE ?                     81047
         BNZR  R14           YES; SKIP SUPERFLUOUS MESSAGE       81047
MSGIN    OI    WORKFLAG,WFGMSG  SET INPUT MESSAGE                81047
MSGOUT   SUBENT ,            COMMON MESSAGE ROUTINE              81047
         MVC   VMSGMSG,VMSGMSG-1  CLEAR VARIABLE PORTION         81047
         SLR   R15,R15                                           81047
         IC    R15,0(,R1)    GET LENGTH OF MESSAGE               81047
         LA    R14,L'VMSGMSG GET LENGTH OF MESSAGE AREA          81047
         CR    R15,R14       WILL IT FIT ?                       81047
         BNH   *+6           YES                                 81047
         LR    R15,R14       ELSE TRUNCATE                       81047
         EX    R15,MSGMVC    MOVE VARIABLE TEXT                  81047
         LR    R0,R15        COPY LENGTH-1                       86292
         LA    R14,VMSGMSG   GET FIRST USER BYTE                 86292
MSGNMLUP CLC   =C'%%%%%%%%',0(R14)  INSERTION ?                  86292
         BNE   MSGNMINC      NO                                  86292
         MVC   0(8,R14),EPLOC   MOVE THE PROGRAM NAME            86292
         B     MSGNMDON                                          86292
MSGNMINC LA    R14,1(,R14)                                       86292
         BCT   R0,MSGNMLUP   TRY NEXT BYTE                       86292
MSGNMDON LA    R5,VMSGMSG+2(R15)  GET NEXT BYTE AFTER INSERTION  86292
         LA    R3,VOUTDDNM   SET FOR OUTPUT VOLUME               85288
         LA    R2,DS1DSNAM   AND OUTPUT DSNAME                   81047
         CLC   TRASH,0(R2)   IEBCOPY TEMP NAME ?                 81047
         BNE   *+8           NO                                  81047
         LA    R2,FMT2       POINT TO SAVED COPY OF NAME         81047
         TM    WORKFLAG,WFGMSG  IS IT OUTPUT MESSAGE ?           81047
         BZ    MSGCOM        YES                                 81047
         IC    R3,DSLCURR+DSLF2-DSLENT  GET INPUT VOLUME INDEX   85288
         N     R3,=A(7)      MASK                                85288
         MH    R3,=Y(VIN2DDNM-VIN1DDNM)  MAKE OFFSET             85288
         LA    R3,VIN1DDNM(R3)  GET CURRENT ENTRY                85288
         LA    R2,INJFCB     GET INPUT DSN                       81047
         CLC   =C'* ',VMSGMSG   SPECIAL FLAG ?                   86292
         BE    MSGATT        YES; PROCESS                        86292
         CLC   VMSGMSG(2),VMSGMSG-1  TWO BLANKS ?                81047
         BNE   MSGCOM        NO; ELSE ASSUME 'SUCCESSFULLY'      81047
         CLC   DS1DSNAM,INJFCB  RENAMED ?                        81047
         BE    MSGATT        NO                                  85288
         MVC   0(2,R5),=C'AS'                                    81047
         MVC   3(44,R5),DS1DSNAM  SHOW OUTPUT NAME               81047
MSGATT   CLI   VMSGRFM-1,C' '   BLANK ?                          85288
         BNE   MSGCOM        NO                                  85288
         CLC   VMSGRFM-1(17),VMSGRFM  ALL BLANK ?                85288
         BNE   MSGCOM        NO; SKIP DCB INFO                   85288
         SERVCALL DSFMT,DS1DSORG  FORMAT DSORG/RECFM/LRECL/BLKSI 85288
         BXH   R15,R15,MSGEXT  HUH ?                             85288
         CLC   VMSGDSO-1(L'VMSGDSO+1),VMSGRFM  CLEAR ?           85288
         BNE   *+10          NO                                  85288
         MVC   VMSGDSO,0(R1)  COPY RETURNED VALUE                85288
         MVC   VMSGRFM,3(R1)  RECFM                              85288
         MVC   VMSGLRC,22(R1) LRECL                              85288
         MVC   VMSGBLK,17(R1) BLKSIZE                            85288
MSGEXT   CLI   VMSGEXT-1,C' '   ROOM FOR EXTENT INFO ?           85288
         BNE   MSGCOM        NO                                  85288
         CLC   VMSGEXT-1(8),VMSGEXT  ALL BLANK ?                 85288
         BNE   MSGCOM        NO; SKIP                            85288
         L     R14,VOUTUCB   GET UCB ADDRESS                     85288
         CLI   VMSGMSG,C' '  OUTPUT VOLUME ?                     86292
         BE    *+8           YES                                 86292
         L     R14,VINUCB-VINDDNM(,R3)  GET UCB ADDRESS          86292
         LA    R15,IECSDSL1  GET FORMAT 1 DSCB                   85288
         LA    R0,IECSDSL3     AND FORMAT 3                      85288
         STM   R14,R0,DB     STASH FOR CONVERSION                85288
         SERVCALL DVEXT,DB   CONVERT                             85288
         CH    R15,=H'4'     VALID REQUEST ?                     85288
         BH    MSGCOM        NOW WHAT ?                          85288
         CVD   R0,DB         PACK THE TRACKS                     85288
         MVC   VMSGTRK-1(L'VMSGTRK+1),=X'402020202120'           85288
         ED    VMSGTRK-1(L'VMSGTRK+1),DB+5  MAKE IT PRETTY       85288
         SLR   R0,R0                                             85288
         IC    R0,DS1NOEPV   GET NUMBER OF DATA EXTENTS          85288
         CVD   R0,DB                                             85288
         MVC   DB2(4),=X'40202120'                               85288
         ED    DB2(4),DB+6                                       85288
         MVC   VMSGEXT,DB2+2  SHOW NUMBER OF EXTENTS             85288
MSGCOM   MVC   VMSGVOL,VIN1VSER-VIN1DDNM(R3)  SHOW VOLUME        85288
         MVC   VMSGDSN,0(R2)  AND DSN                            81047
         NI    WORKFLAG,255-WFGMSG  RESET FLAG                   81047
         PRTV  VMSG          PRINT THE MESSAGE                   81208
         SUBEX ,             RETURN TO CALLER                    81047
MSGMVC   MVC   VMSGMSG(0),1(R1)  EXECUTED MESSAGE TEXT MOVE      81047
VMSG     VCON  ' ',END=VMSGENDX                                  81208
VMSGVOL  DC    CL6' ',C' '   VOLSER                              81047
VMSGDSN  DC    CL44' ',C' '  DSNAME                              81047
VMSGMSG  DC    CL(137-*+VMSG)' '  VARIABLE TEXT                  81064
         VCON  ,                                                 81208
         ORG   VMSGENDX-30                                       85288
VMSGEXT  DS    CL2,C         NUMBER OF EXTENTS                   85288
VMSGTRK  DS    CL5,C         NUMBER OF TRACKS                    85288
VMSGDSO  DS    CL3,C         DATASET ORGANIZATION                85288
VMSGRFM  DS    CL5,C         RECORD FORMAT                       85288
VMSGLRC  DS    CL5,C         RECORD LENGTH                       85288
VMSGBLK  DS    CL5           BLOCK SIZE                          85288
         ORG   ,                                                 85288
         SPACE 2                                                 79095
         LTORG ,                                                 79105
         TITLE 'C O P Y V O L  ***  COMMON WORK AREA'            81047
         DROP  ,             DELETE ALL                          81047
         USING WORKAREA,R11  GLOBAL ADDRESSABILITY               81208
         USING SAVE,R12      SAVE/WORK AREA ADDRESSABILITY       81208
WORKAREA DS    0D            WORK AREA USED BY ALL ROUTINES      81047
NOPCCW   CCW   X'03',0,X'20',1   PATTERN NO-OP CCW               81047
         SPACE 1                                                 81047
*        TTR TO MBBCCHHR CONVERSION                              81047
*        R0 - TTRZ   R1 - MBBCCHHR ADDRESS                       81047
TTRABS   SLR   R14,R14       CLEAR FOR DIVIDE                    81047
         LR    R15,R0        COPY TTRZ                           81047
         SRL   R15,16        MAKE 00TT                           81047
         STC   R0,0(,R1)     SET M                               81047
         STCM  R14,3,1(R1)   SET BB                              81047
         STCM  R0,2,7(R1)    SET R                               81047
         LH    R0,VINTPC     GET INPUT TRACKS/CYLINDER           85280
         CLI   0(R1),8       IS IT AN INPUT EXTENT ?             85280
         BL    *+8           YES                                 85280
         LH    R0,VOUTTPC    ELSE GET OUTPUT TRACKS/CYLINDER     85280
         DR    R14,R0                                            81047
         STCM  R15,3,3(R1)   SET CC                              81047
         STCM  R14,3,5(R1)   SET HH                              81047
         BR    R9            RETURN TO CALLER                    81047
         SPACE 1                                                 81047
*        MBBCCHHR TO TTRZ CONVERSION                             81047
*        R1 - MBBCCHHR ADDRESS  R0 - TTRZ RETURN                 81047
ABSTTR   ICM   R14,15,3(R1)  GET CCHH                            81047
         SRDL  R14,16        MAKE 00CC/HH00                      81047
         CLI   0(R1),8       OUTPUT EXTENT ?                     85280
         BL    ABSTTRI       NO; INPUT                           85280
         MH    R14,VOUTTPC   GET OUTPUT TRACKS/CYLINDER          85280
         B     ABSTTRC                                           85280
ABSTTRI  MH    R14,VINTPC    ELSE INPUT TRACKS/CYLINDER          85280
ABSTTRC  SLL   R14,16                                            85280
         LR    R0,R14                                            81047
         ALR   R0,R15        TT00                                81047
         ICM   R0,2,7(R1)    TTR0                                81047
         IC    R0,0(,R1)     TTRZ                                81047
         BR    R9            RETURN                              81047
         SPACE 1                                                 81208
SYSPRINT PRTWORK SYSPRINT,SYSTERM,TITLE=5  PRINT DD DEFINITION   82298
SYSIN    INPWORK CONTROL,SYSIN,WIDTH=80,EODAD=SYNEOF  INPUT      86292
         SPACE 1                                                 81047
VTOC     DC    CL8'SYSVTOC'    MAJOR QUEUE NAME
SYSDSN   DC    CL8'SYSDSN'   MVS MAJOR QUEUE NAME FOR IEBCOPY TEST
SYS1     DC    C'SYS1.'            DSN HEADER FOR FAKE UNMOVABLE   11/3
TRASH    DC    X'27C815160D'  FAKE DSN FOR MVS IEBCOPY           81047
LSPACE   VCON  '   SPACE BEFORE COMPRESSION ',END=LSPACER        81208
LSPIN    DC    CL32'NOT AVAILABLE - FDR TAPE INPUT'             GP13433
         DC    C'    AFTER COMPRESSION '                         81047
LSPOUT   DC    CL32'LSPACE ERROR'                                79327
         VCON  ,                                                 81208
ENQLIST  ENQ   (VTOC,VOUTVSER,E,6,SYSTEM,VTOC,VIN1VSER,E,6,SYSTEM,     *
               VTOC,VIN2VSER,E,6,SYSTEM,VTOC,VIN3VSER,E,6,SYSTEM,      *
               VTOC,VIN4VSER,E,6,SYSTEM,VTOC,VIN5VSER,E,6,SYSTEM,      *
               VTOC,VIN6VSER,E,6,SYSTEM,VTOC,VIN7VSER,E,6,SYSTEM,      *
               VTOC,VIN8VSER,E,6,SYSTEM),MF=L                    81047
WTOR     WTOR  'OK TO SQUISH VOLUME XXXXXX FROM X11111 X22222 X33333 X4*
               4444 X55555 X66666 X77777 X88888',1,3,2,MF=L      81208
WTORCLR  WTOR  'OK TO DESTROY/INITIALIZE VOLUME XXXXXX',1,3,2,MF=L
WTORCLRL EQU   *-WTORCLR                                         82298
EPLOC    DC    CL8'IEBCOPY '  ATTACHED PROGRAM NAME              86292
CTLG     DC    CL9'SYSCTLG'  CATALOG DATA SET                    *AMS*
PASSW    DC    CL9'PASSWORD' PASSWORD DATA SET                   *AMS*
TRTABLE  EQU   *-C'0'                                            *UNG*
         DC    C'0123456789ABCDEF'                               *UNG*
GMDEBL   DC    0A(0),AL1(243),AL3(DEBBASND-DEBAVT+10*16+16) DEBLEN
GMDEBA   DC    A(0)          DEB ADDRESS                         81047
GMDEBAD  DC    A(0)          ADDRESS OF DEBBASIC                 81047
DUMPCODE DC    0A(1),X'80000001'  TERMINATION CODE/FLAGS FOR ABEND
IOECBLST DC    (NUMBUFFS-1)A(0),X'80',AL3(0)  I/O ECB LIST       81047
INOUTDCB DCB   MACRF=E,DDNAME=SYSUT2,DEVD=DA,EXLST=1             81208
OUTSIZE  DC    F'0'          OVERRIDING OUTPUT DISK SIZE (CYL)  GP09297
OUTSALT  DC    F'0'          OVERRIDING OUTPUT ALTERNATE (CYL)  GP09297
DACYLS   DC    H'0,200,25,80,1440'    CYLINDER TABLE 0-4        GP09297
         DC    H'980,48,96,200'       CYLINDER TABLE 5-8        GP06354
         DC    H'404,697,555,959'     CYLINDER TABLE 9-C        GP06354
         DC    H'808,885,1113'        CYLINDER TABLE D-F        GP06354
ALTCYLS  DC    H'00,3,0,0,1,0,1,1,3,7,2,5,1,7,1,1'              GP09297
*                 0 1 2 3 4 5 6 7 8 9 A B C D E F
FROMTO   DC    X'0001020304050607080D0A0B0C090E0F' ALTERNATE O/P 79105
SWAPTO   DC    X'0100020304050607000000000C000E0F' ZERO IF SWAP OK
RDMTAB   DC    X'80808080008080808080800000800000' 0 FOR RD-MULT  09297
         AIF   ('&LOCAL' NE 'CCSI').NOMRX74                      87135
*        FOR ALL MEMOREX 3674 (OR BETTER) 3330S, RDM IS ALLOWED  87135
         ORG   RDMTAB+9      3330                                87135
         DC    X'00'                                             87135
         ORG   RDMTAB+X'0D'  3330-1                              87135
         DC    X'00'                                             87135
         ORG   ,                                                 87135
.NOMRX74 SPACE 1                                                 81047
VIDDCUR  DC    H'0'          CURRENT DD NUMBER                   81047
VIDDMAX  DC    H'0'          MAXIMUM DD NUMBER                   81047
         SPACE 1                                                 *AMS*
IN       VOLWO ,             CURRENT INPUT AREA                  85280
         ORG   VINDDNM         FASTER THAN CHANGING SOURCE      GP13433
IN1      VOLWO ,             CURRENT INPUT AREA                  85280
IN2      VOLWO ,             CURRENT INPUT AREA                  85280
IN3      VOLWO ,             CURRENT INPUT AREA                  85280
IN4      VOLWO ,             CURRENT INPUT AREA                  85280
IN5      VOLWO ,             CURRENT INPUT AREA                  85280
IN6      VOLWO ,             CURRENT INPUT AREA                  85280
IN7      VOLWO ,             CURRENT INPUT AREA                  85280
IN8      VOLWO ,             CURRENT INPUT AREA                  85280
OUT      VOLWO SYSUT2        OUTPUT WORK AREA                    85280
         SPACE 1                                                 79095
LINECARD VCON  ' ',END=LINECARN                                  81208
WORKDSN  DC    0CL88' '       RENAME WORK AREA                   79105
CARDMSG  DC    CL25' '                                           81047
         DC    CL5' '                                            79095
         DC    C''''                                             79095
CARDLINE DC    CL80' '                                           79095
         DC    C''''                                             79095
WORKDMSG EQU   WORKDSN+44                                        79105
LINECARN EQU   *                                                 79105
         SPACE 2                                                 81208
SAVE     DSECT ,             DYNAMIC SAVE/WORK AREA              81208
         DC    9D'0'         PUSHDOWN SAVE AREA STACK 2          81047
         DC    9D'0'         PUSHDOWN SAVE AREA STACK 3          81047
         DC    9D'0'         PUSHDOWN SAVE AREA STACK 4          81047
         DC    9D'0'         PUSHDOWN SAVE AREA STACK 5          81047
         DC    9D'0'         PUSHDOWN SAVE AREA STACK 6          81047
DB       DC    D'0'          WORK SPACE                          79095
DB2      DC    D'0'            DITTO                             81208
         SPACE 1                                                 81208
         SERVDEFS ,          ADDRESS OF @SERVICE ROUTINES       GP12294
CCODE    EQU   RETCODE+2,2,C'H'   RETURN CODE                   GP12294
BLKSZTAB DC    A(0)          ADDRESS OF BLOCKSIZE TABLE IN @DCBEXIT
UEXRENAM DC    A(0)          USER RENAME/SELECT EXIT             93002
UEPRENAM DC    CL8' '        NAME OF USER EXIT MODULE            93002
         SPACE 1                                                 81047
DSNAME   DC    CL44' '
         DS    0D
ABSHD    DC    AL3(0)
ABSAD    DC    XL5'0'
ZEROES   DC    XL8'0',XL8'0'                                     81047
OUTER    DC    X'0'           IOERROR BYTE
WORKFLAG DC    X'0'          GENERAL PROCESSING FLAGS            81047
WFGTEST  EQU   X'80'         ALLOCATION CALLED IN TEST MODE      81047
WFGMSG   EQU   X'01'         OUTPUT VOLUME MSG (INPUT IF OFF)    81047
AUTHFLAG DC    X'00'         (LOCAL) AUTHORIZATION FLAG          82298
AUTHACCT DC    CL8' '        (LOCAL) USER'S ACCOUNT NUMBER       90056
VSAMFLAG DC    X'00'         VSAM AND CATALOG PROCESSING FLAG    81047
VSCAT    EQU   X'80'         VSAM CATALOG FOUND                  81047
OSCAT    EQU   X'40'         OS CATALOG FOUND                    81047
VSDSN    EQU   X'20'         VSAM DSN SCHEDULED FOR COPY         81047
VSWARN   EQU   X'10'         VSAM WARNING MESSAGE ISSUED         81047
VSAMVOL  DC    X'00'         VOLUME INDEX OF FIRST TIMESTAMP     81047
ACCDATE  DC    XL3'0'        CUTOFF DATE FOR ACCESS CHECK        81208
FULL     DC    A(0)
TRKCHAIN DC    A(0)          CHAIN OF SEQUENTIAL BUFFERS         81047
TRKCHASE DC    A(0)             LAST BUFFER ON CHAIN             83059
INDKAD   DC    A(0)          CCHH OF INPUT TRACK BEING COPIED
OTDKAD   DC    A(0)          CCHH OF OUTPUT TRACK BEING WRITTEN
VTOCLTTR DC    A(8)          ADDRESS OF LAST TTR IN VTOC         81047
         SPACE 1                                                 79105
DSLBXLE  DC    A(DSLEN,0)    WITH DSLSTAD, BXLE VALUES           81047
DSLFLAG  DC    0X'0'          PASS CONTROL FLAG                  79105
DSLVSAM  EQU   X'80'         VSAM DATASET                        81047
DSLISAM  EQU   X'40'         ISAM DATASET                        79105
DSLUNM   EQU   X'20'         UNMOVABLE DATASET                   79105
DSLCAT   EQU   X'10'         CATALOG/PASSWORD SPECIAL BDAM DSN   79105
DSLCYLP  EQU   X'08'         CYLINDER BOUNDARY - COPY FIRST      81047
DSLCYL   EQU   X'04'         FIRST EXTENT ON CYLINDER BOUNDARY   79105
DSLPTY   EQU   X'02'         COPY PRIOR TO OTHERS OF SAME TYPE   81047
DSLTRKP  EQU   X'02'         TRACK ALIGNED - PRIORITY COPY       81047
DSLTRK   EQU   X'01'         OTHER BOUNDARY                      79105
DSLKEEP  EQU   X'80'         KEEP UNUSED EXTENTS/TRACKS          79327
DSLPSREL EQU   X'40'         RELEASE ALL UNUSED SPACE FOR PS     79327
DSLSTAD  DC    A(0)          BASE OF TTR/FLAG LIST - 4           79105
DSLCURR  DC    XL8'0'        COPY OF CURRENT DSLIST ENTRY        81047
         SPACE 1                                                 79105
RELSAVE  DS    0A            4-WORD SAVE AREA FOR RELTEST        81047
RGSAV    DC    5A(0)
SVR5R6   DC    2A(0)           OTHER SAVE AREA FOR REGS 5 & 6
MAXFREE  DC    A(26*NUMDSCB5) MAXIMUM NUMBER OF FREE EXTENT DESCRIPTORS
PFMT5SAD DC    A(0)          FREE DESCR. SAVE              1/3   81047
MAXFRLEN DC    A(0)          MOVE LENGTH FOR ABOVE         2/3   81047
PFMT5ONE DC    A(0)          FIRST FMT 5 DESCRIPTOR        3/3   81047
PFMT5AD  DC    A(0)          ADDR OF FIRST FREE DESC        1/2  81047
FPFMT5AD DC    A(0)          FIRST FREE FREE DESCRIPTOR     2/2  81047
PFMT5SAV DC    XL8'0'        PFMT5AD/FPFMT5AD  SAVE AREA         81047
PPREBLOK DC    F'0'          ADDRESS OF PREVIOUS PREVIOUS BLOCK  81047
PREBLOCK DC    F'0'          ADDRESS OF PREVIOUS BLOCK           81047
BLOCKAD  DC    F'0'
BLOCKF5  DC    F'0'          ADDRESS OF FIRST FORMAT 5           81047
STACKAD  DC    F'0'          ADDRESS OF VTOC STACK CHAIN         87135
FMT1     DC    35A(0)
         ORG   FMT1                                              79105
         IECSDSL1 1                                              79105
         AIF   (&MVS OR '&LOCAL' EQ 'CCSI').NOMVSI2              82195
DS1DSCHA EQU   X'02'         DSN CHANGED BIT IN DS1DSIND         81151
DS1REFD  EQU   DS1SYSCD+7,3,C'X'   DATE OF LAST USE              81208
.NOMVSI2 ORG   ,                                                 81151
FMT2     DC    35A(0)
         ORG   FMT2                                              79105
         IECSDSL1 2                                              79105
         ORG   ,                                                 79105
FMT3     DC    35A(0)
         ORG   FMT3                                              79105
         IECSDSL1 3                                              79105
         ORG   FMT2                                              81047
FMT5     DC    35A(0)
         ORG   FMT5                                              79105
         IECSDSL1 5                                              79105
         ORG   ,                                                 79105
FMT4     DC    35A(0)
         ORG   FMT4+L'DS1DSNAM                                   79105
         IECSDSL1 4                                              79105
         ORG   ,                                                 79105
VTOCIN   DC    35A(0)
LSPUCB   DC    A(0)          OUTPUT UCB ADDRESS FOR LSPACE       79327
NUMVOL   DC    F'0'          NUMBER OF DATA SETS ON CURRENT VOL GP09205
NUMDSN   DC    F'0'          NUMBER OF DATASETS COPIED           79327
NUMSKP   DC    F'0'           NUMBER SKIPPED                     79327
NUMNPRO  DC    F'0'          NUMBER NOT PROCESSABLE              81047
NUMDVSAM DC    F'0'          NUMBER OF VSAM DATASETS             81047
NUMDIS   DC    F'0'          NUMBER OF ISAM DATASETS             79327
NUMDU    DC    F'0'          UNMOVABLE OTHER THAN ISAM           79327
NUMDDA   DC    F'0'          NUMBER OF DA                        79327
NUMDPS   DC    F'0'          NUMBER OF PS                        79327
NUMDPO   DC    F'0'          NUMBER OF PO DATASETS COPIED WITH IEBCOPY
NUMDPOB  DC    F'0'          NUMBER OF PO DATASETS JUST COPIED   79327
NUMDOTH  DC    F'0'          NUMBER OF OTHER DATASETS COPIED     79327
NUMEXCP  DC    F'0'          EXCP REQUEST (EXC. SWAP OPTION)     87135
NUMRMOK  DC    F'0'          SUCCESSFUL READ MULTIPLES           87135
NUMRMBAD DC    F'0'          UNSUCCESSFUL READ MULTIPLES         87135
NUMSTACK DC    F'0'          NUMBER OF VTOC TRACKS STACKED       87135
NUMSTHIT DC    F'0'          NUMBER OF DSCBS NOT EXCP'D          87135
PDEUSD   DC    H'0'          DIRECTORY BLOCKS USED               85287
MASKORG  DC    X'FFFF'       MASK OF DSORGS TO COPY              86292
AMSOUTX  DC    0D'0',17XL10'0'  COMPRESSED EXTENTS               81047
OLDEXTS  DC    17XL10'0'     ORIGINAL EXTENTS
NEWEXTS  DC    17XL10'0'     NEW EXTENTS                         81047
COPYEXTS DC    XL10'0'       INPUT VTOC EXTENTS FOR VTOC DUPLICATION
#TRKS    DC    H'0'          TRACKS ON PACK
         SPACE 1                                                 81047
TRKALC   DC    F'0'          TRACKS PROCESSED THROUGH ALLOCATION 91119
TRKUSD   DC    F'0'          TRACKS USED                         91119
TRKOLD   DC    F'0'          SIZE OF OLD DATASET                 91119
         SPACE 1                                                 79095
TRAKFLAG DC    X'00'         TRACK EXCP PROCESSING FLAG          81047
TKQUIES  EQU   X'80'         QUIESCE CURRENT I/O                 81047
TKLAST   EQU   X'40'         LAST TIME                           79105
TKCONTU  EQU   X'20'         CONTINUATION ENTRY                  81047
TKREAD   EQU   X'10'         SPECIAL READ                        81047
TKRITE   EQU   X'08'         SPECIAL WRITE                       81047
TKVTOC   EQU   X'04'         VTOC I/O                            81047
TKDSCB   EQU   X'02'         DSCB I/O                            81047
TKIPL    EQU   1+TKREAD      IPL TRACK COPY                      81047
TKDSCBRD EQU   TKDSCB+TKREAD   READ A DSCB                       81047
TKDSCBWT EQU   TKDSCB+TKRITE  WRITE A DSCB                       81047
TKVTOCRD EQU   TKVTOC+TKREAD   READ ENTIRE VTOC                  81047
TKVTOCWT EQU   TKVTOC+TKRITE  WRITE ENTIRE VTOC                  81047
TKPDE    EQU   TKREAD+TKRITE  DIRECTORY COPY/PREFORMAT FOR IEBCOPY
TKPDEWT  EQU   TKDSCBWT+1     DIRECTORY WRITE - UNLIKE DASD      85287
TKDUPE   EQU   TKREAD+TKRITE+1  SEARCH FOR DUPLICATE DSCB 1      81047
TRAKMODE DC    X'00'         SAVED TRAKFLAG / COPY METHOD        85280
VOUTACYL DC    H'0'          ABS. CYLINDERS ON OUTPUT            93166
DSNAMES  DC    A(0)          START OF DSN CONVERSION LIST        79095
AMSOUTX# DC    0X'01'        NUMBER OF ORIGINAL EXTENTS          *AMS*
AMSOUTXA DC    0A(0),X'1',AL3(NEWEXTS)  ADDR. OF EXTENT LIST TO BE USED
STOPCODE DC    F'0'          CODE AT WHICH TO QUIT               83294
XOUTTPC  DC    H'0'          HI-TRACK IN CYLINDER EXTENT         85280
INJFCBAD DC    0A(0),X'87',AL3(INJFCB)
INJFCB   DC    44A(0)
OTJFCBAD DC    0A(0),X'87',AL3(OUTJFCB)
OUTJFCB  DC    44A(0)
         SPACE 1                                                 79105
IIHNEXT  DC    A(0)           DISK ADDRESS OF NEXT DSCB          *UNG*
IIHUSED  DC    F'0'          SAVE VALUE OF TRKUSD                91119
IIHOPT   DC    X'0'           COMPRESS OPTIONS                   *UNG*
IIHCOMP  EQU   X'80'          COMPRESS REQUESTED                 *UNG*
IIHFAIL  EQU   X'40'          COMPRESS FAILED FOR CURRENT DATASET *UNG*
DSCHA    EQU   X'01'         USE IEBCOPY ONLY IF DS1DSCHA IS ON  79325
LASTFMT1 DC    XL5'0'        LAST FMT1 WRITTEN                   81047
FMT1HWM  DC    XL5'0'        FORMAT 1 HIGH-WATER MARK
WYLFLAG  DC    X'0'          WYLBUR CONVERSION FLAG              79095
WYLCON   EQU   X'80'         CONVERSION REQUESTED                79095
WYLDSN   EQU   X'40'         PROBABLE WYLBUR DSN                 79095
WYLBAD   EQU   X'20'         ERROR DURING CONVERSION             79095
         SPACE 1                                                 83094
OISYS1   DC    X'00'         SET BY PARM=SYSU AND SYSP           83094
OIALLDS  DC    X'00'         GLOBAL FOR ALL DATASETS             83094
         SPACE 1                                                 79105
OPTFLAG  DC    X'0'          PROCESSING OPTIONS                  79105
OPTCOPY  EQU   X'80'         USE IEBCOPY FOR PDS'S               79105
OPTERSE  EQU   X'40'         PRINT IMPORTANT MESSAGES ONLY       79105
OPTLOTS  EQU   X'20'         PRINT LOTS OF MESSAGES              81047
OPTCHEK  EQU   X'10'         PERFORM DSCB INSTALLATION CHECKS    86138
OPTKEEP  EQU   X'08'         BYPASS EXTENT COMPRESSION/RELEASE   79105
OPTNWTOR EQU   X'04'         BYPASS WTOR FOR OPER PERMISSION    GP09239
OPTSTACK EQU   X'01'         STACK VTOC BUFFERS IN MEMORY        87135
         SPACE 1                                                 87040
OPTFLAG2 DC    X'00'         ADDITIONAL OPTIONS                  79327
OPTPSREL EQU   X'80'         RELEASE UNUSED P.S. TRACKS          79327
OPTPOREL EQU   X'40'         RELEASE UNUSED PDS SPACE            85289
OPTEVEN  EQU   X'20'         CONTINUE DESPITE I/O ERRORS         83094
OPTCLEAR EQU   X'10'         CLEAR OUTPUT VTOC ONLY (NO COPY)    82298
OPTDOOLD EQU   X'08'         COPY UNREFERENCED DSNS ONLY         81208
OPTDONEW EQU   X'04'         COPY RECENTLY REFERENCED DSNS ONLY  81208
OPTDDUMP EQU   X'02'         DUMP ON ERROR                       81047
OPTDEBUG EQU   X'01'         DEBUG RUN                           81047
         SPACE 1                                                 87040
OPTFLAG3 DC    X'0'          VTOC AND SPECIAL OPTIONS            87040
OPTVTOC  EQU   X'80'         COPY OUTPUT VTOC EXTENTS FROM INPUT 87040
OPTIPL   EQU   X'10'         COPY IPL TRACK ALSO                 87040
OPTINBAD EQU   X'08'         SYSIN/PARM ERROR                    93002
OPTFLIP  EQU   X'02'         FLIP VOLSERS ON FIXED DISKS         87040
OPTSWAP  EQU   X'01'         EXCHANGE VOLSERS AFTER COPY         87040
         SPACE 1                                                 85280
MIGFLAG  DC    X'00'         MASS MIGRATION FLAG                 85280
OPTMIGR  EQU   X'80'           PARM=MIGR SPECIFIED               85280
OPTMIGF  EQU   X'40'           UNLIKE TYPES FOUND                85280
OPTNBLK  EQU   X'20'           SUPPRESS DATASET REBLOCKING       86292
OPTRBLK  EQU   X'10'           REBLOCK EVERYTHING                86292
OPTUBLK  EQU   X'08'           REBLOCK RECFM=U ONLY              86292
         SPACE 1                                                 81047
OPTINEX  DC    8X'00'        VOLUME 1-8 FLAGS                    81047
OPTEXC   EQU   X'20'         AT LEAST ONE DSN EXCLUDE            79105
OPTINC   EQU   X'10'         AT LEAST ONE DSN INCLUDE            79105
OPTINCL  EQU   X'08'         FIRST WAS INCLUDE                   79105
         SPACE 1                                                 79095
CURDSDAT DS    XL3'0'        TODAY IN DS1 FORMAT YYDDDD          86138
HINDEX   DC    CL7' ',CL4' ' HIGH-LEVEL INDEX                    86138
         ORG   HINDEX                                            86138
CATCALL  DC    CL44' ',XL4'0'  LOCATE PARM AREA            1/3   86138
CATVOL1  DC    CL6' ',XL4'0'   VOLUME CATALOGUED ON        2/3   86138
CATVOL2  DC    CL6' '        CVOL (IF ANY)                 3/3   86138
SAVEEND  EQU   *             END OF SAVE/WORK AREA              GP09297
         TITLE 'C O P Y V O L  ***  MISCELLANEOUS DSECTS'        81047
EXTMAP   MAPEXTNT ,          MAP FOR ONE EXTENT                  86138
         SPACE 2                                                 81047
DSLIST   DSECT ,             MAPPING OF DATASET TTR/FLAG LIST    81047
DSLENT   DS    0XL8          DUMMY FOR XC                        81047
DSLTTR   DS    XL3           TTR OF DSCB1                        81047
DSLFG    DS    X             DATASET TYPE                        81047
DSLF2    DS    X             EXTRA FLAGS                         81047
DSLOTTR  DS    XL3           TTR OF DSCB1 IN OUTPUT VOLUME       81047
DSLEN    EQU   *-DSLTTR      LENGTH OF ONE ENTRY                 81047
         SPACE 2                                                 81047
FXMAP    DSECT ,             FREE EXTENT MAPPING                 81047
FXLINK   DS    A             NEXT ELEMENT                        81047
FXABS    DS    H             ADDRESS OF THIS FREE SPACE          81047
FXBEF    DS    H             FREE TRACKS BEFORE CYLINDER BOUNDARY
FXCYL    DS    H             FREE TRACKS ON CYLINDER BOUNDARIES  81047
FXAFT    DS    H             FREE TRACKS AFTER CYLINDER BOUNDARY 81047
FXLEN    EQU   *-FXLINK      LENGTH OF ONE ENTRY                 81047
         SPACE 2                                                 79105
RIXLIST  DSECT ,     (OPTIONAL) RENAME/INCLUDE/EXCLUDE DSN LIST  79105
RIXFG    DC    X'0'           ENTRY TYPE(S)                      79105
RIXREN   EQU   X'80'         RENAME DSN / INPUT MASK / OUTPUT PFX
RIXINC   EQU   X'40'         INCLUDE DSN / INPUT MASK            79105
RIXEXC   EQU   X'20'         EXCLUDE DSN / INPUT MASK            79105
RIXPSREL EQU   X'10'         TRACK RELEASE                       79327
RIXKEEP  EQU   X'08'         KEEP EXTENTS/TRACKS                 79327
RIXDNO   EQU   X'04'         CANCEL U, P OPTIONS                 79105
RIXDSU   EQU   X'02'         FLAG DSN AS UNMOVABLE               79105
RIXDSP   EQU   X'01'         FLAG DSN AS PRIORITY COPY           79105
RIXF2    DC    X'0'          SECOND FLAG BYTE                    81047
RIXCMSK  EQU   X'80'         COMPARISON IS MASKED                81047
RIXRMSK  EQU   X'40'         REPLACEMENT IS MASKED               81047
RIXDODAT EQU   X'08'         SELECT BY ACCESS DATE               81208
RIXNODAT EQU   X'04'         SKIP ACCESS DATE CHECK              81208
RIXMATCH EQU   X'02'         AT LEAST ONE MATCH FOUND           GP09205
RIXORDER EQU   X'01'         ORDER DSCB AT FRONT OF VTOC         81208
RIXFV    DC    X'0'          VOLUME SELECT BITS                  81047
RIXL1    DC    X'0'          LENGTH OF FIRST ENTRY               79105
RIXL2    DC    X'0'          RENAME ONLY / LENGTH OF REPLACE STRING
RIXMASK  DC    0X'0'          1-44 BYTE MASK                     79105
RIXREP   DC    0X'0'          REPLACEMENT STRING                 79105
RIXFIX   EQU   *-RIXLIST     FIXED LENGTH OVERHEAD PER ENTRY     79105
         SPACE 2                                                 81047
BUFFMAP  DSECT ,             I/O BUFFER AND WORK AREA MAP        81047
DWECB    DS    F             I/O ECB                             81047
DWTOGO   DS    X             WHERE-TO-GO FLAG                    81047
DWFLAG   DS    X             PROCESSING FLAG                     81047
*              ALSO CONTAINS DSLVSAM, DSLISAM AND DSLUNM FLAGS   81047
DWFNORDM EQU   X'10'           DON'T USE READ MULTIPLE IF ON     87135
DWFERR   EQU   X'08'           ERROR RETRY IN PROGRESS           83094
DWFER2   EQU   X'04'           SECOND OR SPECIAL RETRY           83094
DWFERM   EQU   X'02'           ERROR MESSAGE PRINTED WHEN ON     83094
TKOVER   EQU   X'01'           OVERFLOW RECORD                   83094
DWINAD   DS    XL5           INPUT CCHH                          81047
DWOUTAD  DS    XL5           OUTPUT CCHH                         81047
DWCHAIN  DS    A             NEXT BUFFER FOR SEQUENTIAL I/O      81047
DWIDSN   DS    CL44          DSN OF INPUT FOR ERROR MESSAGES     83094
         DS    0D            IOB ALIGN                           81047
DWIOB    DS    X'42',AL3(0)  IOBFLAGS/IOBSENSE                   81047
DWIECBP  DS    A(DWECB)      IOB ECB                             81047
DWICSW   DS    2A(0)         IOB CSW                             81047
DWICCW   DS    A             IOBSTART                            81047
DWIDCB   DS    A(0,0,0)      IOBDCB  (INOUTDCB)                  81047
DWIMBB   DS    XL3'0'        MBB PORTION OF IOBSEEK              81047
*        M IS 0-7 FOR INDISK1-INDISK8, AND 8 FOR OUTDISK         81047
DWICCHH  DS    XL5'0'        CCHHR PORTION OF SEEK               81047
DWARDCNT DS    A(DWCRDCNT)   ADDRESS OF READ COUNT CHAIN         81047
DWARDATA DS    A(DWCRDATA)   ADDRESS OF READ CKD CHAIN           81047
DWACOUNT DS    A(DWCCOUNT)   ADDRESS OF COUNT FIELDS             81047
DWADATA  DS    A(DWCDATA)    ADDRESS OF DATA FIELD               81047
DWR0COUN DS    XL8           1/2 OUTPUT R0 COUNT/DATA            81208
DWR0DATA DS    XL8           2/2 R0 DATA FIELD (INPUT, THEN OUTPUT)
DWR0ODAT DS    XL8           3/2 R0 DATA FROM INPUT; CCHH UPDATED
DWR0CLRN EQU   *-DWR0COUN        LENGTH TO CLEAR                 83094
DWCERCCW CCW   X'31',DWICCHH,X'40',5   SEARCH ID EQUAL           83094
         CCW   X'08',DWCERCCW,0,0        TIC *-8                 83094
DWCNUCCW CCW   X'08',123456,0,0        TIC TO RESTART CCW        83094
DWCRDCNT CCW   X'31',DWICCHH,X'40',5   SEARCH ID EQUAL           81047
         CCW   X'08',DWCRDCNT,0,0      TIC *-8                   81047
         CCW   X'06',DWR0DATA,X'40',8  READ R0 DATA              81047
DWCRDCCW DC    (NUMCCWS)XL8'0'         READ COUNT...             81047
         CCW   X'39',DWICCHH,X'40',4   SEARCH HOME ADDRESS EQUAL 81208
         CCW   X'0',*-8,0,0   TIC *-8                            81208
         CCW   X'16',DWR0COUN,X'40',16  READ R0                  81208
DWCRDATA CCW   X'31',DWICCHH,X'40',5   SEARCH ID EQUAL           81047
         CCW   X'08',DWCRDATA,0,0      TIC *-8                   81047
DWCRDAT0 CCW   X'06',DWR0DATA,X'40',8  READ/WRITE R0 DATA        81047
DWCCOUNT DC    (NUMCCWS+1)XL8'0'    COUNT FIELDS/READ-WRITE CCWS 81047
DWCDATA  DS    0D            DATA (L'TRACKSIZ+8)                 81047
         SPACE 1                                                 87135
STACK    DSECT ,             VTOC STACK BUFFER                   87135
STKLINK  DS    A             LINK TO NEXT BUFFER                 87135
STKSPLEN DS    F             SUBPOOL/LENGTH                      87135
STKCCHH  DS    XL4           CCHH OF TRACK IN BUFFER             87135
STKTTRZ  DS    0F            TTR+EXTENT OF TRACK                 87135
STKTTR   DS    XL3           TTR OF TRACK                        87135
STKEXT   DS    X             VOLUME INDEX (0-7)                  87135
STKLEN   DS    F             CURRENT DATA LENGTH                 87135
STKDATA  DS    0X            VARIABLE DATA                       87135
         SPACE 2                                                 82272
TRKCALC  DSECT ,                                                 82272
         SERVCALC MF=D       EXPAND TRKCALC MAPPING              82272
         SPACE 2                                                 81047
         PRINT NOGEN                                             79095
CVTDSECT DSECT ,                                                 81208
         CVT   DSECT=YES                                         81208
         IHACDE ,                                                81208
         IEFUCBOB ,                                              81208
         IEZDEB ,                                                81208
         DCBD  DSORG=PS                                          81208
         IEFTIOT1 ,                                              81208
JFCB     DSECT ,                                                 81208
         IEFJFCBN ,                                              81208
         IKJTCB ,                                                81208
         IEZJSCB ,                                               81208
         AIF   (&MVS).BIGEND                                     82195
UCBFLA   EQU   UCBFL1        OLD FLAG FIELD                      81047
UCBNRY   EQU   UCBNOTRD      NOT-READY BIT                       81047
         AGO   .COMEND                                           86138
.BIGEND  SPACE 1                                                 81047
         IHAPSA ,                                                86138
.COMEND  IHADVCT ,                                               86138
         AIF   (NOT &MVSXA).NODCE99                              93162
         IECDDCE ,                                               93162
.NODCE99 AIF   (&MVSXA).NODEBDS                                  92279
DEBDSOLD EQU   X'40'         DISP=OLD                            81047
DEBLEAVE EQU   X'30'         DISP=(,LEAVE)                       81047
.NODEBDS AIF   ('&LOCAL' NE 'PID' AND '&LOCAL' NE 'TECH').NOUTCBM
USERCVT  USERCVT ,                                               86138
USERTCB  USERTCB ,                                               81208
USERVOLS USERVOLS ,                                              90056
.NOUTCBM END   ,                                                 81208
