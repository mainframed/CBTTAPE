MPDA TITLE '                 MAPDISK0 -- FASTDISK'               82326
* MAPDISK IS A HIGH PERFORMANCE DASD ALLOCATION MAPPING PROGRAM
*
* FASTDISK IS THE MAIN ROUTINE (AND EP) FOR MAPDISK.  THE
* COMPONENT ROUTINES FOR MAPDISK ARE:
*        FASTDISK - MAIN ROUTINE & VTOC READ; CONTAINS THE DCB
*                   & OTHER I/O CONTROL BLKS FOR PDS & VTOC READING
*        FORMAT   - FORMATS & OUTPUTS LISTING
*        PDSREAD  - DETERMINES PDS DIR BLK ALLOCATION
*        SORTIT   - SORTS THE DSCB INTO COLLATING SEQ.
*        ZTIME    - COMPUTES CURRENT TIME & DATE FOR LISTING
*        PRNT     - HANDLES SYSPRINT FORMATTING & I/O            82326
*
* FASTDISK, F.G.ROSS, 1/10/73
*
* MAPDISK0, GENE CZARCINSKI, 06SEP73
*           GENE CZARCINSKI, 20MAY74, TSO SUPPORT.
*
*        SUPPORTS 2301,2302,2303,2305,2311,2314,2321,3330,3350,3375,
*                 3380,3390
*        LATEST UPDATE 2/6/74  TO FIX SEVERAL ERRORS WHEN 3330 AND
*         2305 SUPPORT WHERE ADDED  F.  ROSS U.S. DEPT OF STATE
*
*  UPDATED 31MAR75, GENE CZARCINSKI, NASA/GSFC, CODE 531.
*        CHANGE ALL REFERENCES TO 'FASTMAP' TO 'MAPDISK' ...
*        THE PROGRAM, ALL COMPONENT MODULES, ETC. HAVE
*        BEEN RENAMED 'MAPDISK' (WHICH IS WHAT IT SHOULD
*        HAVE BEEN IN THE FIRST PLACE)
*        ADD 'ABC' DISPLAY SUPPORT
*        CHECK FOR TSO IN PRINOUT INITIALIZATION.
*        FIXUP CODE/MSG FOR REGION UNUSED
*  UPDATED 07APR75, GENE CZARCINSKI ...
*        FIX '#LIMIT' TO BE ACTUAL *END* OF DYNAMIC AREA, IT WAS
*        'END+1' WHICH CAUSED ABEND0C0 AT TIMES (MOSTLY IN FORMAT).
*
*
* NOTE -
*   1. THE SPECIAL SVC (&SVC ASSEMBLY PARAM) IS USED TO MODIFY SQS
*      (PROTECTED CORE).  IT MUST LOAD R0-R14 AND EXECUTE THE
*      INSTRUCTION POINTED TO BY R15.  IT IS USED BY MAPDISK TO
*      CHANGE UCB POINTERS IN THE TIOT AND BY PDSREAD TO CHANGE
*      DASD EXTENTS IN THE DEB.
*   2. THE '&OPENMOD' ASSEMBLY PARAM IN FORMAT AND PRNT IS USED
*      TO INCLUDE/EXCLUDE GSFC LOCAL CODE FOR FORMATTING AND
*      OUTPUTTING SPECIAL 'USE INFO'.
*   3. THE EXEC PARMS SUPPORTED ARE:
*          PDS/NOPDS (DEFAULT NOPDS) FOR PDS DIRECTORY BLK CNT
*          EXT/NOEXT (DEFAULT NOEXT) FOR LISTING OF EXTENTS WITH DS.
*          DUMP (DEFAULT NODUMP) FOR A ABEND 1,DUMP AT END OF EXEC
*   4. IF DYNAMIC DD CODE IS INCLUDED (&SVC), THE DEVICES/VOLUMES
*      TO BE PROCESSED CAN BE SPECIFIED IN THE PARM -
*      (DISK,DRUM,CELL,2301,2302,2303,2311,2314,2321)  OR
*      'ALL' MAY BE SPECIFIED TO MAP ALL OF THE ABOVE DEVICES
*      (ANY ONLINE).  SPECIFIC VOLUMES MAY BE SPECIFIED BY USING
*      SER=(AA,BB) WHERE "AA" AND "BB" ARE 1-6 CHAR VOLUME SERIAL
*      NUMBERS.  UPTO 12 CAN BE SPECIFIED PER EXECUTION.  IF ONLY
*      ONE IS SPECIFIED, THE PARENS CAN BE OMITTED.
*   5. FEATURES AND DIFFERENCE WITH PREVIOUS MAPDISKS -
*          F1-F6 DSCBS PROCESSED
*          SHARED/SPLIT SPACE REPORTED
*          PDS BLK COUNT HANDLED UP TO 32K
*          2321 PROCESSING HANDLED
*          RECFM=VBA FOR PRINTED OUTPUT
*          VARIABLE DYNAMIC CORE USED FOR WORKAREAS
*          WTO/WTP MSGS ISSUED FOR CORE MANAGEMENT ERRORS
*   6. TO REDUCE REGION REQUIRED FOR RUNNING, BUFNO=1 CAN BE USED
*      FOR SYSPRINT BUT IT DOES DEGRADE PERFORMANCE.
*   7. IF THE VOLUMES TO BE MAPPED ARE SPECIFIED IN THE EXEC PARM,
*      THEN A SYSUT1 DD STATEMENT IS REQUIRED.  IT MAY POINT AT
*      ANY DEVICE BUT A GOOD ONE TO USE IS:
*        //SYSUT1 DD DISP=SHR,VOL=REF=SYS1.SVCLIB
*   8. THE STANDARD WAY (AND IF &SVC IS NOT USED, THE ONLY WAY)
*      TO SPECIFY THE VOLUMES TO BE MAPPED IS TO INCLUDE A DD STMT
*      POINTING AT EACH VOLUME TO BE PROCESSED.  IF ANY DYNAMIC DD
*      PARMS ARE SPECIFIED, THEN DD STATEMENTS ARE IGNORED.  OTHERWISE,
*      A MAP IS GENERATED FOR EACH DD.  DUMMY DDS AND CERTAIN DDNAMES
*   9. THE PARMS NOSTR/STR HAVE BEEN ADDED TO INHIBIT OR ALLOW
*      (RESPECTIVELY) MAPPING OF VOLUMES WITH THE 'STORAGE' ATTRIBUTE
*      (AS OPPSOE TO PRIVATE OR PUBLIC).  THIS PARM IS ONLY VALID
*      IF THE DYNAMIC DD PROCESSING IS IN EFFECT ... SER= WILL
*      OVERRIDE THE EFFECT OF THIS PARM IN ALLOWING MAPPING.
*  10. IF MAPDISK IS RUN IN THE TSO FOREGROUND, ONLY THE DASD
*      VOLUME POINTED TO BY 'SYSUT1' WILL BE MAPPED.  THIS
*      OVERRIDES ALL OTHER PARMS OR CONDITIONS.  THE DATACELL
*      (2321) IS NOT SUPPORTED IN THE FOREGROUND.
*
*      MAJOR CHANGES AND RE-ORGANIZATION FOR VS SUPPORT          82326
*        BY G. POSTPISCHIL, EXPERT SYSTEM PROGRAMMING, INC.      82326
*      &SVC DROPPED - TESTAUTH USED FOR PDS OPTION,              82326
*        DYNAMIC ALLOCATION USED FOR VOLUME MOUNTING.            82326
*      LOCAL AND IBM MACROS USED TO REPLACE ABSOLUTE REFERENCES  82326
*      SIX ASSEMBLIES RECOMBINED INTO ONE MODULE FOR EASIER EDITING.
*      MISCELLANEOUS ENHANCEMENTS FOR VS; MFT/MVT, 2321 DROPPED  82326
*        3350, 3375, 3380 ADDED                                  82326
*        3390 ADDED, NOT TESTED                                  94221
*      DEPENDENCIES ADDED - LOCAL (DYNAMIC) SUBROUTINES          82326
*      @SERVICE, @PRINTER, ETC.                                  82326
*                                                                82326
*      ATTRIBUTES - NOT RENT, NOT REFR,  AC=1                    82326
         EJECT ,                                                 82326
         MACRO
&L       DMSG  &A,&B
         LCLC  &P1                                               85253
         AIF   (K'&A NE 5).NOTNEW                                85253
         AIF   ('&A'(1,2) NE 'T3' AND '&A'(1,4) NE 'T2305').NOTNEW
&P1      SETC  '+1'          TEST DEVSW+1                        85253
.NOTNEW  ANOP  ,                                                 85253
&L       TM    DEVSW&P1,&A                                       85253
         BZ    *+14
         MVC   0(5,R1),=C&B
         LA    R1,5(,R1)
         MEND
         SPACE 1
         PUNCH '   SETCODE AC(1) '     SUPPORT DIRECTORY COUNTS GP05120
         PUNCH '  ORDER MAPDISK(P) '   EASIER DUMPS             GP05120
         SPACE 1
         COPY  OPTIONGB                                          82326
         SPACE 1                                                 82326
         LCLB  &EXT,&PDS,&STR
         LCLC  &RELEASE                                          82326
&PDS     SETB  1                       DEFAULT FOR PDS PROCESSING
&EXT     SETB  0                       DEFAULT TO LIST EXTENTS
&STR     SETB  0                       DEFAULT--NO STORAGE VOLS
&RELEASE SETC  '10SEP85'                                         85253
         SPACE 1                                                 82326
         SYSPARM LIST=YES                                        82326
         EJECT ,
         PRINT &PRTSOR                                           82326
         EXTRN SUBVTOCI      VTOC STATUS ROUTINE                GP05155
MAPDISK  SAVEM ZERO15,BASE=R12,PARM=R9                           82326
         SPACE 1
T2301    EQU   B'10000000'
T2302    EQU   B'01000000'
T2303    EQU   B'00100000'
T2311    EQU   B'00010000'
T2314    EQU   B'00001000'
T2321    EQU   B'00000100'
TSTR     EQU   B'00000010'
TSER     EQU   B'00000001'
T3330    EQU   B'10000000'
T23051   EQU   B'01000000'
T23052   EQU   B'00100000'
T3340    EQU   B'00010000'                                       85253
T3350    EQU   B'00001000'                                       85253
T3375    EQU   B'00000100'                                       85253
T3380    EQU   B'00000010'                                       85253
T3390    EQU   B'00000001'                                       94221
SW0      EQU   B'10000000'
SW1      EQU   B'01000000'
SW2      EQU   B'00100000'
SW3      EQU   B'00010000'
SW4      EQU   B'00001000'             DEBUG
SW5      EQU   B'00000100'             EXT
SW6      EQU   B'00000010'             DUMP
SW7      EQU   B'00000001'             PDS
         SPACE 2                                                 82326
         L     R11,=A(COMMWORK)        INIT SECONDARY BASE REG   82326
         USING COMMWORK,R11                                      82326
         LA    R1,INFMJFCB   GET THE JFCB ADDRESS                82326
         ST    R1,EXLIST     BUILD THE EXIT LIST                 82326
         MVI   EXLIST,X'87'  SET LAST, JFCB TYPE                 82326
         LA    R2,DCB        GET THE DCB ADDRESS                 82326
         ST    R2,OPLIST     MAKE THE OPEN LIST                  82326
         MVI   OPLIST,X'80'  SET INPUT                           82326
         MVI   IOBFLAG1,IOBCMDCH+IOBUNREL  CMD CHAIN/UNRELATED   82326
         LA    R1,ECB                                            82326
         ST    R1,IOBECBPT                                       82326
         ST    R2,IOBDCBPT   SET DCB ADDRESS                     82326
         SERVINIT ,          LOAD THE @SERVICE ROUTINE           82326
         SERVCALL LPALD,=CL8'@PRINTER'  LOAD THE PRINTER ROUTINE 82326
         ST   R0,@PRINTER    SAVE ADDRESS                        82326
         PRTOPEN SYSPRINT,OPT=ABEND   BOMB IF NO SYSPRINT        82326
         PRTOPEN CONSOLE,OPT=(NOWTO,DUMMY),DEV=2                 85256
*        CALL  ZTIME,(HEAD1DT),VL      GET DATE & TIME
         LA    R1,HEAD1DT                                        82326
         L     R15,=A(ZTIME)
         BALR  R14,R15
         PRTLIST HEAD1,TITLE=1   MAKE MAIN TITLE                 82326
         XC    DEVSW(2),DEVSW          INIT THE SWITCHES
         MVC   VOL,BLANKS    CLEAR VOLSER FOR ERROR MESSAGES     82326
* PROCESS THE EXEC PARM
         LA    R1,0(,R9)     CLEAN PARM ADDRESS                  82326
         LTR   R1,R1         ANY PARM ?                          82326
         BZ    PEND          NO; USE DEFAULTS                    82326
         L     R1,0(,R1)     GET PARM                            82326
         LH    R15,0(,R1)
         LTR   R2,R15
         BNP   PEND
         LA    R15,2(R1,R15)
         LA    R1,2(,R1)
         BCTR  R2,0                    FOR THE 'EX' INSTR.
         L     R3,=A(TRTBL)
         EX    R2,*+8
         B     *+10
         TR    0(0,R1),0(R3)
PLOOP    CR    R1,R15                  END OF STRING?
         BNL   PEND                    YES...DONE
         CLI   0(R1),C','              COMMA
         BNE   *+12                    NO
         LA    R1,1(,R1)               SKIP COMMA
         B     PLOOP
         CLC   =C'PDS',0(R1)
         BE    P01A
         CLC   =C'NOPDS',0(R1)
         BE    P01B
         CLC   0(4,R1),=C'DUMP,'
         BE    P02
         CLC   0(5,R1),=C'DEBUG,'
         BE    P02A
         CLC   0(4,R1),=C'2301,'
         BE    P03A
         CLC   0(4,R1),=C'2302,'
         BE    P03B
         CLC   0(4,R1),=C'2303,'
         BE    P03C
         CLC   0(4,R1),=C'2311,'
         BE    P03D
         CLC   0(4,R1),=C'2314,'
         BE    P03E
         CLC   0(4,R1),=C'2321,'
         BE    P03F
         CLC   0(4,R1),=C'3330'
         BE    P03FA
         CLC   0(4,R1),=C'2305'
         BE    P03FB
         CLC   0(4,R1),=C'3340,'  POT (WITH HANDLE) ?            85254
         BE    P03D40        YES                                 85254
         CLC   0(4,R1),=C'3350,'  FIXED DISK ?                   85254
         BE    P03D50        YES                                 85254
         CLC   0(4,R1),=C'3375,'   FASTER FIXED DISK ?           85254
         BE    P03D75        YES                                 85254
         CLC   0(4,R1),=C'3380,'   BIGGER, FASTER FIXED DISK ?   85254
         BE    P03D80        YES                                 85254
         CLC   0(4,R1),=C'3390,'   STILL BIGGER & FASTER DISK ?  94221
         BE    P03D90        YES                                 94221
         CLC   0(4,R1),=C'DRUM,'                                 85254
         BE    P03G
         CLC   0(4,R1),=C'DISK,'                                 85254
         BE    P03H
         CLC   0(4,R1),=C'CELL,'                                 85254
         BE    P03F
         CLC   =C'ALL',0(R1)
         BE    P03I
         CLC   0(3,R1),=C'STR,'
         BE    P03J
         CLC   0(5,R1),=C'NOSTR,'
         BE    P03K
         CLC   =C'SER=',0(R1)
         BE    P04
         CLC   0(3,R1),=C'EXT,'
         BE    P05A
         CLC   0(5,R1),=C'NOEXT,'
         BE    P05B
         CLC   =C'ABC',0(R1)                                EAC/26MAR75
         BNE   PEND                                              82326
PABC     MVI   ABCSW,X'FF'                                  EAC/26MAR75
         LA    R1,3(0,R1)                                   EAC/26MAR75
         B     PLOOP                                        EAC/26MAR75
P01A     OI    SW,SW7                  SET TO PROCESS PDS
P00A     LA    R1,3(,R1)
         B     PLOOP
P01B     NI    SW,255-SW7              SET TO NOT PROCESS PDS
P00B     LA    R1,5(,R1)
         B     PLOOP
P02      OI    SW,SW6                  SET FOR DUMP.
         LA    R1,4(,R1)
         B     P02B                                              82326
P02A     OI    SW,SW4                  SET FOR DEBUG.
         LA    R1,5(,R1)
P02B     MVI   #FREE+4+2,X'40'  LEAVE 16K FREE FOR VS DUMP       82326
         MVI   #SIZE+2,X'40' SET GETMAIN LOW LIMIT ALSO          82326
         B     PLOOP
P03      LA    R1,4(,R1)
         B     PLOOP
P03A     OI    DEVSW,T2301
         B     P03
P03B     OI    DEVSW,T2302
         B     P03
P03C     OI    DEVSW,T2303
         B     P03
P03D     OI    DEVSW,T2311
         B     P03
P03E     OI    DEVSW,T2314
         B     P03
P03F     OI    DEVSW,T2321
         B     P03
P03FA    OI    DEVSW+1,T3330
         B     P03
P03D40   OI    DEVSW+1,T3340                                     85253
         B     P03                                               85253
P03D50   OI    DEVSW+1,T3350                                     85253
         B     P03                                               85253
P03D75   OI    DEVSW+1,T3375                                     85253
         B     P03                                               85253
P03D80   OI    DEVSW+1,T3380                                     85253
         B     P03                                               85253
P03D90   OI    DEVSW+1,T3390                                     94221
         B     P03                                               94221
P03G     OI    DEVSW,T2301+T2303
         OI    DEVSW+1,T23051+T23052
         B     P03
P03H     OI    DEVSW,T2314+T2311
         OI    DEVSW+1,T3330+T3340+T3350+T3375+T3380+T3390       94221
         B     P03
P03I     OI    DEVSW,T2302+T2314+T2311+T2303+T2301+T2321
         OI    DEVSW+1,255   ALL                                 94221
         LA    R1,3(,R1)
         B     PLOOP
P03FB    OI    DEVSW+1,T23051+T23052
         B     P03
P03J     OI    DEVSW,TSTR              FLAG PROCESS STORAGE VOLS
         B     P00A
P03K     NI    DEVSW,255-TSTR          FLAG NO PROCESS STORAGE VOLS
         B     P00B
P04      LA    R1,4(,R1)
         OI    DEVSW,TSER
         MVI   VOLSER,C' '             CLEAR IN CASE OF
         MVC   VOLSER+1(71),VOLSER     DOUBLE SPECIFICATION
         CR    R1,R15
         BH    PEND
         LA    R2,VOLSER
         LA    R3,1
         CLI   0(R1),C'('
         BNE   *+12
         LA    R3,12
         LA    R1,1(,R1)
P04A     LR    R14,R1
P04B     CR    R14,R15
         BH    P04C
         CLI   0(R14),C','
         BE    P04C
         CLI   0(R14),C')'
         BE    P04C
         LA    R14,1(,R14)
         B     P04B
P04C     LR    R4,R14
         LA    R5,5
         SR    R4,R1
         BNP   P04D
         BCTR  R4,0
         CR    R4,R5
         BNH   *+6
         LR    R4,R5
         EX    R4,P04MVC
         LA    R2,6(,R2)
P04D     LR    R1,R14
         CR    R1,R15
         BH    PEND
         CLI   0(R1),C')'
         BNE   *+12
         LA    R1,1(,R1)
         B     PLOOP
         LA    R1,1(,R1)
         CR    R1,R15
         BH    PEND
         BCT   R3,P04A
         B     PLOOP
P04MVC   MVC   0(0,R2),0(R1)
P05A     OI    SW,SW5
         B     P00A
P05B     NI    SW,255-SW5
         B     P00B
*
PEND     XR    R1,R1                   CLEAR FOR PDS.
* INIT DYNAMIC AREA AND PNTRS
*      DYNAMIC STORAGE IS PARTITIONED THREE AREAS.  THE FIRST IS 82326
*        USED FOR CCW CHAINS AND FREE SPACE COUNTERS; THE SECOND 82326
*        WILL CONTAIN ALL DSCBS FOR THE PACK; THE THIRD IS USED  82326
*        FOR A SORTED EXTENT INDEX. THE FIRST AREA IS ARBITRARILY
*        DEFINED AS 4K; THE REMAINDER IS PARTITIONED AFTER VTOC READ.
*                                                                82326
         GETMAIN VU,LA=#SIZE,A=#ADR
         BXLE  R15,R15,GETGOT   STORAGE OBTAINED                 82326
         ABEND 804           LET THE USER KNOW UNGRACEFULLY      82326
GETGOT   LM    R14,R15,#ADR            ADRS/SIZE                 82326
         S     R15,#FREE+4             MINUS 4K FOR OS.
         ST    R15,#ADR+4              SAVE NEW LENGTH
         LA    R14,0(R15,R14)          A(NEW TOP)
         ST    R14,#FREE               FOR FREEMAIN
         BCTR  R14,0                   MINUS 1 FOR END      EAC/07APR75
         ST    R14,#LIMIT              TOP OF DYNAMIC CORE
         FREEMAIN V,A=#FREE            TO GIVE SOME BACK
         L     R14,#ADR+4              LENGTH
         L     R15,#ADR                A(BOTTOM)
         ST    R15,#CCW                A(WORK AREA/CHAN PROGS)
         A     R15,=A(4*1024)    NEW TOP WORK/BOTTOM DSCB        82326
         ST    R15,#AREA               A(TOP WORK/BOTTOM DSCB)
         ST    R15,#COREMAX            INIT FOR MAX USED
         LA    R15,DS4IDFMT-AREASECT(,R15)  POINT TO FORMAT4     82326
         TM    SW,SW4+SW6              DUMP OR DEBUG?
         BZ    NOCLEAR                 NO...DO NOT CLEAR CORE
         L     R14,#ADR                YES, CLEAR CORE
         L     R15,#FREE     GET END+1                           82326
         SR    R15,R14       SIZE TO CLEAR                       82326
         XR    R0,R0                                             82326
         XR    R1,R1                                             82326
         MVCL  R14,R0        CLEAR STORAGE                       82326
*        PROCESS SYSUT1 (TSO); USER VOLUMES (SER= PARM); OR      82326
*        RUN THROUGH NON-SPECIAL TIOT ENTRIES.                   82326
NOCLEAR  L     R1,CVTPTR     GET THE CVT                         82326
         USING CVTMAP,R1                                         82326
         L     R1,CVTTCBP    A(NEW/OLD BOX)                      82326
         AIF   (&MVS).MVSTSO  GET TSO/BATCH FLAG FROM ASCB       82326
         L     R1,4(,R1)     A(MY TCB)                           82326
         USING TCB,R1                                            82326
         TM    TCBTSFLG,TCBTSTSK  TSO SESSION ?                  82326
         BO    TSOA1                   YES.
         AGO   .COMTSO                                           82326
.MVSTSO  L     R1,12(,R1)    A(ASCB)                             82326
         USING ASCB,R1                                           82326
         ICM   R0,15,ASCBTSB  TSO TASK IF TSB PRESENT            92356
         BNZ   TSOA1         UNLESS ITSY-BITSY CHANGES IT ?      82326
.COMTSO  DROP  R1                                                82326
         ICM   R0,3,DEVSW    ANY SELECTION BITS ON ?             85256
         BZ    REDO
         B     TSOA2         BRANCH TO TSO COMMON CODE           82326
         SPACE 2                                                 82326
TSOA1    MVI   SWTSO,255     SET TSO FLAG                        82326
         XC    DEVSW(2),DEVSW  CANCEL DEVICE SELECTION           85256
TSOA2    SERVCALL TIODD,CSYSUT1  LOOK FOR SYSUT1 DD CARD         82326
         LTR   R3,R0         COPY THE TIOT ENTRY ADDRESS         82326
         BZ    TERROR        NOT FOUND                           83277
         USING TIOELNGH,R3   DECLARE THE ENTRY                   82326
         ICM   R4,7,TIOEFSRT  CHECK THE UCB ADDRESS              82326
         BZ    TERROR        SYSUT1 IS DUMMY                     83277
         CLI   TIOELNGH,20   SINGLE UCB ENTRY ?                  82326
         BNE   UERROR        NO; TOO BAD                         82326
         CLI   TIOELNGH+20,0  ANOTHER ENTRY FOLLOWS ?            82326
         BE    TSOA3         NO                                  82326
         CLC   TIOEDDNM+20(8),BLANKS  CONCATENATION ?            82326
         BE    UERROR        YES; NOT SUPPORTED                  82326
TSOA3    ST    R3,ADDTIOT    SAVE A(OUR DD ENTRY)                82326
         TM    SWTSO,255     IN TSO MODE ?                       82326
         BZ    REDO          NO; USE UCB LOOKUP CODE             82326
         USING UCBOB,R4      DECLARE THE UCB                     82326
         ST    R4,ADDUCB                                         82326
         NI    SW,X'0F'
         CLI   UCBID,X'FF'   IS THIS A UCB?                      82326
         BNE   UERROR             NO.
         CLI   UCBTBYT3,UCB3DACC  IS IT DASD?                    82326
         BNE   UERROR             NO.
         MVC   TEMP(4),UCBVTOC    SAVE TTR OF VTOC.              82326
         MVC   VOL,UCBVOLI                                       82326
         DROP  R3,R4                                             82326
         B     OBTAIN
         SPACE 2                                                 83277
*        IF NO SYSUT1 DD CARD WAS USABLE, AND THIS IS NOT A      83277
*        TSO USER, PROCEED WITH A TIOT SCAN. TSO USERS MUST      83277
*        PRE-ALLOCATE SYSUT1, SINCE A TIOT SCAN MIGHT HIT ALL    83277
*        SORTS OF UNINTENDED DISKS.                              83277
*                                                                83277
TERROR   TM    SWTSO,255     TSO FLAG ON ?                       83277
         BNZ   UERROR        YES; SETUP ERROR                    83277
         EJECT ,                                                 82326
REDO     NI    SW,X'0F'                CLEAR SWITCHES FOR RUN
         MVC   VOL,BLANKS    CLEAR VOLSER FOR ERROR MESSAGES     82326
         TM    DEVSW,X'FF'
         BNZ   UT000
         TM    DEVSW+1,X'FF'
         BZ    LOOPTIOT
UT000    L     R1,ADDUCB     GET PRIOR UCB ADDRESS (OR ZERO)     82326
         SERVCALL UCBUM,(R1)  GET NEXT (FIRST) UCB               82326
         LTR   R4,R0         COPY AND TEST UCB ADDRESS           82326
         BZ    ALDON         ALL DONE                            82326
         USING UCBOB,R4      DECLARE UCB MAPPING                 82326
         ST    R4,ADDUCB     SAVE UCB ADDRESS                    82326
         CLI   UCBTBYT3,UCB3DACC   DASD DEVICE ?                 82326
         BNE   UT000                                             82326
         TM    UCBSTAT,UCBONLI         ONLINE?                   82326
         BZ    UT000                   NO.                       82326
         TM    UCBSTAT,UCBUNLD+UCBCHGS   PENDING OFFLINE?        82326
         BNZ   UT000                   YES, SKIP.                82326
         CLI   UCBVOLI,C' '  IS THERE A THERE THERE ?            85256
         BNH   UT000         NO; SKIP IT                         85256
         CLI   UCBVOLI,C'9'                                      85256
         BH    UT000         DITTO                               85256
         TM    DEVSW,TSER    LOCATE SPECIFIC VOLUME SERIAL ?     85256
         BZ    UTNSER        NO                                  85256
         LA    R2,12
         LA    R3,VOLSER
UT01D    TM    0(R3),255-C' '                                    82326
         BZ    UTSERSK       SKIP EMPTY SLOT                     82326
         CLC   UCBVOLI,0(R3)  MATCH ?                            82326
         BE    UT01E
UTSERSK  LA    R3,6(,R3)
         BCT   R2,UT01D
         B     UT000         SKIP IF NOT FOUND                   82326
UT01E    MVI   0(R3),C' '    SHOW FOUND
         B     UT02          PROCESS REGARDLESS OF OTHER CRITERIA
UTNSER   TM    DEVSW+1,X'FF'  TEST DEVICE TYPE ?                 85256
         BNZ   UTTTYP        YES                                 85256
         TM    DEVSW,X'FC'   TEST DEVICE TYPE ?                  85256
         BZ    UTSTR         SEE IF STR TO BE TESTED             85256
UTTTYP   TM    DEVSW,TSTR    TEST FOR STORAGE, ALSO ?            85256
         BZ    UTNSTR        NO                                  85256
         TM    UCBSTAB,UCBBSTR STORAGE ?                         85256
         BZ    UT000         NO; SKIP THIS ONE                   85256
UTNSTR   SLR   R14,R14       CHECK IF THIS TYPE PROCESSED        85256
         IC    R14,UCBTBYT4                                      82326
         N     R14,=A(15)    JUST IN CASE                        82326
         IC    R15,DEVSW2(R14)                                   85256
         LA    R1,DEVSW+1    SET SWITCH TO TEST                  85256
         C     R14,=F'8'             TAKE CARE OF 2314  360/370
         BE    TM360
         C     R14,=F'6'               370HS  HDW                85256
         BNL   TM370         OK IF 370 TYPE                      85256
TM360    BCTR  R1,0          TEST DEVSW, NOT DEVSW+1             85256
TM370    EX    R15,UTEXTM    TEST IF WANTED                      85256
         BZ    UT000         NO MATCH; PASS                      85256
         B     UT02          PROCESS THIS ONE                    85256
UTEXTM   TM    0(R1),0       TEST FOR TYPE BIT                   85256
UTSTR    TM    DEVSW,TSTR    SELECT FOR STORAGE VOLUMES ?        85256
         BZ    UT000         NO; SKIP                            85256
         TM    UCBSTAB,UCBBSTR  STORAGE VOLUME ?                 85256
         BZ    UT000         NO; SKIP                            85256
UT02     MVC   VOL,UCBVOLI  COPY VOLUME SERIAL                   85256
         SERVCALL ALCVS,VOL  FIND/ALLOCATE A TIOT ENTRY          82326
         BXLE  R15,R15,UTGOT   OK                                82326
         PRTLIST ALLFAIL,DEV=(1,2)   UNABLE TO ALLOCATE VOLSER   85256
         STM   R15,R0,TEMP   SAVE RETURN CODE                    85254
         B     ABORT         AND QUIT                            85254
UTGOT    LR    R3,R0         COPY TIOT ADDRESS                   82326
         ST    R3,ADDTIOT    SAVE FOR DDNM                       82326
         XR    R4,R4                                             82326
         ICM   R4,7,TIOEFSRT-TIOELNGH(R3)  GET THE UCB           82326
         B     STARTUP       GO TO COMMON CODE                   82326
         SPACE 1                                                 82326
LOOPTIOT L     R1,ADDTIOT    GET PRIOR TIOT ENTRY (OR 0)         82326
         SERVCALL TIOLK,(R1)  LOCATE NON-SPECIAL TIOT ENTRIES    82326
         LTR   R3,R0         COPY ADDRESS                        82326
         BZ    ALDON         NO MORE                             82326
         ST    R3,ADDTIOT    SAVE FOR RESTART                    82326
         USING TIOELNGH,R3   DECLARE THE ENTRY                   82326
         CLC   =C'SYS0',TIOEDDNM  DYNAMIC DD ?                   87242
         BE    LOOPTIOT      YES; IGNORE                         87242
         XR    R4,R4                                             82326
         ICM   R4,7,TIOEFSRT   GET THE UCB ADDRESS               82326
         BZ    LOOPTIOT      NO GOOD                             82326
         USING UCBOB,R4      DECLARE THE UCB                     82326
         CLI   UCBTBYT3,UCB3DACC  DASD ?                         82326
         BNE   LOOPTIOT      NO; GET ANOTHER                     82326
STARTUP  ST    R4,ADDUCB     SAVE ADDRESS FOR LATER              82326
*
START    MVC   TEMP(4),UCBVTOC    SAVE TTR0 OF VTOC              82326
         MVC   VOL,UCBVOLI                                       82326
         CLI   UCBVOLI,C' '  PRINTABLE VOLUME SERIAL ?           82326
         BE    REDO          NO; SKIP (LATER GET JFCB ?)         82326
         CLC   UCBNAME+1(2),UCBVOLI+1   POSSIBLE ASP NV VOLUME   82326
         BNE   UTSKP1
         CLC   =C'NV',UCBVOLI+4  IF EQ THEN ASP NV VOLUME ?      82326
         BE    REDO                                              82326
UTSKP1   MVC   VOL,UCBVOLI   MOVE IN VOL-SER                     82326
OBTAIN   MVC   DCB(PATDCBL),PATDCB  COMPLETE THE DCB             82326
         MVC   DCBDDNAM-IHADCB+DCB(8),TIOEDDNM  SET THE DDNAME   82326
         LA    R1,EXLIST     HET THE EXIT LIST                   82326
         STCM  R1,7,DCBEXLSA-IHADCB+DCB  COMPLETE THE EXIT LIST  82326
         LA    R1,IOBSTDRD   GET THE IOB ADDRESS                 82326
         STCM  R1,7,DCBIOBAA-IHADCB+DCB  SET IOB FOR DUMPS       82326
         XC    INFMJFCB(JFCBLGTH),INFMJFCB  CLEAR THE JFCB       82326
         MVI   JFCBDSNM,X'04'                                    82326
         MVC   JFCBDSNM+1(L'JFCBDSNM-1),JFCBDSNM  MAKE VTOC NAME 82326
         MVC   JFCBVOLS(6),VOL    SET VOLSER INTO JFCB           82326
         OI    JFCBTSDM,JFCNWRIT+JFCNDSCB+JFCNDCB  KEEP IT CLEAN 82326
         SERVCALL DSDJ1,INFMJFCB  READ FORMAT 4                  82326
         BXLE  R15,R15,CJFCB   READ OK                           82326
         CH    R15,=H'16'    RETURN CODE 8 ?                     82326
         BE    CJFCB         YES; OLD COPY OF SERVICE ROUTINE    82326
         PRTF  '0MAPDISK - VTOC OBTAIN FAILED ',DEV=(1,2)        85256
         B     ABORT
CJFCB    L     R10,#AREA     GET START OF DSCB INPUT             82326
         USING AREASECT,R10  DECLARE IT                          82326
         MVC   DS4IDFMT(96),0(R1)  SAVE RESULT                   82326
         OPEN  MF=(E,OPLIST),TYPE=J   OPEN THE DCB               82326
         TM    DCBOFLGS-IHADCB+DCB,DCBOFOPN  OPENED ?            82326
         BO    OPENOK                  OPEN OK.
UERROR   PRTF  MSG4,MSG4L,CC=NO,DEV=(1,2)                        85256
         B     ABORT
 EJECT
* SET UP DEVICE CHARACTERISTIC FOR RUN
*        (USE INFO & FORMAT 4 DSCB 'OBTAIN'ED)
OPENOK   XC    BB,BB                   CLEAR THE BIN NO.
         MVC   VTEND,DS4HPCHR  SAVE ADDRESS OF LAST NEEDED DSCB  82326
         CLC   VTEND,DS4F6PTR    START OF F6                     82326
         BH    *+10
         MVC   VTEND,DS4F6PTR                                    82326
         MVC   TRKPCYL,DS4DEVSZ+2   TRKS/CYL                     82326
         XC    ADDDEB,ADDDEB   NO PDS DEB MODIFICATION MADE (YET)
*FAILS*  MVC   PDETRK+1(1),DS4DEVDB  DIRECTORY BLKS/TRK          82326
         SLR   R4,R4                                             94221
         IC    R4,DS4DEVDB   GET DIRECTORY BLOCKS PER TRACK      94221
         LA    R4,2(,R4)     ALLOW FOR FUNNY DATA SETS           94221
         STH   R4,PDETRK     AS FOUND ON OLDER VOLUMES (PRE-MVS) 94221
         MVC   DSCBSTRK+1(1),DS4DEVDT  DSCBS/TRK                 82326
         LH    R5,TEMP       TRK START OF VTOC                   82326
         XR    R4,R4                   SETUP TO FIND CYL ADDR
         LH    R6,TRKPCYL    GET NO. OF CC & HH FOR VTOC         82326
         DR    R4,R6                   TO GET THE CCHH
         STH   R4,HH                   TRK ADDRESS
         STH   R5,CC                   CYL ADDRESS
         LH    R4,DS4DEVSZ   GET CYLINDERS ON VOLUME             82326
         LH    R5,DS4DEVSZ+2 GET TRACKS PER CYLINDER             82326
         LR    R1,R4         COPY CYLINDERS                      82326
         MR    R0,R5         GET TRACKS ON PACK                  82326
         STH   R1,NEWDEB+14  MAKE NEW EXTENT SIZE                82326
         BCTR  R4,0                                              82326
         STH   R4,NEWDEB+10  MAKE NEW END CYLINDER               82326
         BCTR  R5,0                                              82326
         STH   R5,NEWDEB+12  AND END TRACK                       82326
         TESTAUTH FCTN=1     ARE WE AUTHORIZED ?                 82326
         BXH   R15,R15,NOTAUTH   NO                              94221
         L     R4,DCB+DCBDEBAD-IHADCB  A(DEB)                    82326
         ST    R4,ADDDEB     SAVE DEB ADDRESS                    82326
         MVC   OLDDEB,DEBBASND-DEBBASIC(R4)   SAVE OLD DEB       82326
         MVC   NEWDEB(6),OLDDEB  COPY DVMOD/UCB/BIN              82326
         MODESET KEY=ZERO    GET PRIVILEGED                      82326
         MVC   DEBBASND-DEBBASIC(L'NEWDEB,R4),NEWDEB  MAKE NEW EXT
         MODESET KEY=NZERO   RESTORE USER KEY                    82326
* CCHHR IS THE START OF THE VTOC
* VTEND IS THE LAST FORMAT ONE (ACTIVE) DSCB IN THE VTOC
         DROP  R3,R10                                            82326
         B     READVTOC                                          94221
         SPACE 1                                                 94221
NOTAUTH  PRTL  '0***** PROGRAM NOT AUTHORIZED - SKIPPING PDS DIRECTORY *
               COUNTS *****'                                     94221
         PRTSPACE 1                                              94221
         EJECT ,
* BUILD AND EXECUTE A CHANNEL PROGRAM TO READ IN THE VTOC
*        (ONE TRACK AT A TIME)
READVTOC XC    IOBSEEK,IOBSEEK         CLEAR THE MBBCCHHR IN THE IOB
         L     R4,#AREA                INPUT AREA
         USING AREASECT,R4
         L     R3,#CCW                 CHANNEL PGM AREA
         ST    R3,IOBSTART                                       82326
         MVC   0(16,R3),CCWSHA         SEARCH (SHA & TIC)
         LA    R15,IOBSEEK+3                                     82326
         STCM  R15,7,1(R3)                                       82326
         STCM  R3,7,9(R3)                                        82326
*
EXCPLOOP L     R3,#CCW
         LA    R3,16(,R3)              START OF READ CCWS
         MVC   IOBSEEK+1(6),BB   INIT THE IOB FOR THIS TRACK     82326
         LA    R10,1                   SET 'R' TO 1
         LR    R5,R4                   SAVE FOR CCHH SCAN
*
CCWLOOP  MVC   0(8,R3),CCWKD           BUILD THE READ CCW
         C     R4,#LIMIT
         BL    CCWLOOP1
         MVC   MSG5+3(6),=CL6'DSCB'
         PRTF  MSG5,MSG5L,CC=NO,DEV=(1,2)                        85256
         C     R4,#COREMAX                                  EAC/31MAR75
         BNH   *+8                                          EAC/31MAR75
         ST    R4,#COREMAX                                  EAC/31MAR75
         B     ABORT
CCWLOOP1 LA    R15,IECSDSL1     A(DSCB INPUT AREA)               82326
         ST    R15,0(,R3)
         MVI   0(R3),@KD
         LA    R3,8(,R3)               UPDATE CCW PNTR
         C     R3,#AREA
         BL    CCWLOOP2
         MVC   MSG5+3(6),=CL6'CCW '
         PRTF  MSG5,MSG5L,CC=NO,DEV=(1,2)                        85256
         B     ABORT
CCWLOOP2 XC    RES0(3),RES0                                      82326
         STC   R10,R                   SET CCHHR FOR CORE BLK
         MVC   CCHHR,CC
         ST    R4,SORPOINT   SET SORT DSCB POINTER               82326
         LA    R4,AREALEN(,R4)                                   82326
         LA    R10,1(,R10)             BUMP 'R' COUNT
         CH    R10,DSCBSTRK            END OF TRACK?
         BNH   CCWLOOP                 NO, LOOP
*
CCWDONE  LH    R15,HH                  UPDATE CCHH               82326
         LA    R15,1(,R15)
         CH    R15,TRKPCYL                                       82326
         BNE   *+18
         LH    R14,CC
         LA    R14,1(,R14)
         STH   R14,CC
         XR    R15,R15
         STH   R15,HH
*
EXCP     SH    R3,=H'4'                BACKUP AND
         NI    0(R3),255-@CC           TURN OFF CMD CHAINING
         XC    ECB,ECB
         EXCP  IOBSTDRD                                          82326
         WAIT  ECB=ECB
         CLI   ECB,X'7F'               READ OK?
         BE    EXCP1                   YES
*
         IC    R1,ECB                  CC
         STC   R1,MSG7A+1
         SRL   R1,4
         STC   R1,MSG7A
         NC    MSG7A,HEXCON1
         TR    MSG7A,HEXCON2
         UNPK  MSG7B(3),IOBSENS0(2)   SENSE                      82326
         IC    R1,IOBSENS1                                       82326
         STC   R1,MSG7B+3
         NC    MSG7B,HEXCON1
         TR    MSG7B,HEXCON2
         UNPK  MSG7C(13),IOBCSW(7)     CSW                       82326
         IC    R1,IOBCSW+L'IOBCSW-1                              82326
         STC   R1,MSG7C+13
         NC    MSG7C,HEXCON1
         TR    MSG7C,HEXCON2
         UNPK  MSG7D(11),IOBSEEK+1(6)                            82326
         IC    R1,IOBSEEK+6                                      82326
         STC   R1,MSG7D+11
         NC    MSG7D,HEXCON1
         TR    MSG7D,HEXCON2
         PRTF  MSG7,MSG7L,CC=NO,DEV=(1,2)                        85256
         MVC   MSG6V,VOL
         PRTF  MSG6,MSG6L,CC=NO,DEV=(1,2)                        85256
         MVI   RETCODE+1,16
         TM    SW,SW4
         BO    ABEND13
         B     CLOSEDCB
*
* SCAN DSCBS TO GET CCHH OF LAST VALID/USED DSCB
EXCP1    LR    R15,R4                  SAVE
         LR    R4,R5
EXCP2    CR    R4,R15
         BNL   EXCP3
         MVN   DATAKEY(1),DS1FMTID   COPY DSCB TYPE              82326
         TR    DATAKEY(1),SORTORD    MAKE INTO SORT ORDER        82326
         CLI   DS4IDFMT,C'4'           SKIP F4                   82326
         BE    EXCP2A
         CLC   VTEND,DS1PTRDS                                    82326
         BNL   *+10
         MVC   VTEND,DS1PTRDS                                    82326
EXCP2A   LA    R4,AREALEN(,R4)
         B     EXCP2
EXCP3    LR    R4,R15                  RESTORE
         CLC   CC(4),VTEND   DONE ?                              82326
         BNH   EXCPLOOP
*
         C     R4,#COREMAX
         BNH   *+8
         ST    R4,#COREMAX
         ST    R4,#TOP
         DROP  R4
         EJECT ,
* DETERMINE THE PDS BLKS USED AND ALLOCATED
         TM    SW,SW7        PROCESS PDS?                        82326
         BZ    NOPDS                   NO.
         L     R15,=A(PDSREAD)
         BALR  R14,R15
* SORT BY DSNAME (CREAT TABLE OF SORTED POINTERS)
NOPDS    L     R15,=A(SORTIT)                                    82326
         BALR  R14,R15
         SPACE 1                                                 82326
         L     R15,=A(FORMAT)
         BALR  R14,R15
         LTR   R15,R15                 CHECK RC
         BZ    NOERR2
         MVC   MSG5+3(6),=CL6'FORMAT'
         PRTF  MSG5,MSG5L,CC=NO,DEV=(1,2)                        85256
         B     ABORT
NOERR2   L     R15,#TOP                PROCESS NEW TOP
         C     R15,#COREMAX
         BNH   *+8
         ST    R15,#COREMAX
*
CLOSEDCB CLOSE MF=(E,OPLIST)                                     82326
         CLI   SWTSO,0                                           82326
         BE    REDO          GO FOR ANOTHER                      82326
         EJECT ,
ALDON    PRTF  MSGEND2,MSGEND2L        OUTPUT END MESSAGES       82326
         L     R14,#ADR+4
         SRL   R14,10
         CVD   R14,TEMP
         OI    TEMP+7,X'0F'
         UNPK  MSG1A,TEMP
         L     R15,#COREMAX
         S     R15,#ADR
         LA    R15,1023(R15)           ROUND UP.
         SRL   R15,10
         CVD   R15,TEMP
         OI    TEMP+7,X'0F'
         UNPK  MSG1U,TEMP
         SR    R14,R15
         SRL   R14,1                                        EAC/28MAR75
         SLL   R14,1                                        EAC/28MAR75
         CVD   R14,TEMP
         OI    TEMP+7,X'0F'
         UNPK  MSG3A,TEMP
         PRTF  MSG1,MSG1L,CC=NO                                  82326
         AIF   (&MVS OR &SVS OR &VS1).NOREGN                     82326
         PRTF  MSG3,MSG3L,CC=NO                                  82326
.NOREGN  LA    R1,MSG2A                                          82326
         TM    SW,SW6
         BZ    *+14
         MVC   0(5,R1),=C'DUMP,'
         LA    R1,5(R1)
         TM    SW,SW4
         BZ    *+14
         MVC   0(6,R1),=C'DEBUG,'
         LA    R1,6(R1)
         CLI   SWTSO,0                                           82326
         BE    *+14
         MVC   0(4,R1),=C'TSO,'
         LA    R1,4(R1)
         MVC   0(4,R1),=C'EXT,'
         TM    SW,X'04'
         BO    *+14
         MVC   0(6,R1),=C'NOEXT,'
         LA    R1,2(R1)
         LA    R1,4(R1)
         MVC   0(4,R1),=C'STR,'                                  85253
         TM    DEVSW,TSTR
         BO    *+14
         MVC   0(6,R1),=C'NOSTR,'
         LA    R1,2(R1)
         LA    R1,4(R1)
         TM    DEVSW,T2301+T2302+T2303+T2311+T2314+T2321
         BNZ   XYSKIP        PROCESS                             85253
         DMSG  T2301,'2301,'
         DMSG  T2302,'2302,'
         DMSG  T2303,'2303,'
         DMSG  T2311,'2311,'
         DMSG  T2314,'2314,'
         DMSG  T2321,'2321,'
XYSKIP   TM    DEVSW+1,X'FF'   ANY OTHERS ?                      85253
         BZ    XSKIP
         DMSG  T3330,'3330,'                                     85253
         DMSG  T23051,'2305-1,'                                  94221
         DMSG  T23052,'2305-2,'                                  94221
         DMSG  T3340,'3340,'                                     85253
         DMSG  T3350,'3350,'                                     85253
         DMSG  T3375,'3375,'                                     85253
         DMSG  T3380,'3380,'                                     85253
         DMSG  T3390,'3390,'                                     94221
XSKIP    TM    DEVSW,TSER
         BZ    *+14
         MVC   0(4,R1),=C'SER,'
         LA    R1,4(R1)
         MVC   0(3,R1),=C'PDS'
         TM    SW,X'01'
         BO    *+10
         MVC   0(5,R1),=C'NOPDS'
         PRTF  MSG2,MSG2L,CC=NO                                  82326
 EJECT
         TM    SW,X'02'                DUMP?
         BZ    NODUMP                  NO.
         ABEND 1,DUMP                  YES.
NODUMP   SERVTERM ,          FREEMAIN AND CLOSE EVERYTHING       82326
         FREEMAIN V,A=#ADR
         LH    R9,RETCODE    GET THE RETURN CODE                 82326
         ENDM  RC=(R9)       RETURN                              82326
*
*
*
ABORT    MVC   MSG6V,VOL
         PRTF  MSG6,MSG6L,CC=NO,DEV=(1,2)                        85256
         TM    SW,SW4                  DEBUG?
         BZ    ABORT1                  NO.
ABEND13  ABEND 13,DUMP                 YES, GIVE A CORE DUMP.
ABORT1   MVI   RETCODE+1,16            RC=16
         B     ALDON
         SPACE 2                                                 82326
HEAD1    FDPRT 'MAPDISK(&RELEASE)   ',NL                         82326
         FDPRT 'NASA/GSFC SYSTEMS SUPPORT DASD-MAP UTILITY',PAD  82326
         FDPRT HEAD1DT,RADJ,LEN=12+L'HEAD1DT  PRINT TIME/DATE    82326
         FDPRT 'PAGE #P###',RADJ,LEN=17                          82326
         FDPRT *END                                              82326
         SPACE 1                                                 82326
ALLFAIL  FDOPT NL,CC=C'0'                                        82326
         FDPRT '*** ALLOCATION FAILED FOR VOLUME'                82326
         FDPRT VOL,PAD,LEN=6                                     82326
         FDPRT '***'                                             82326
         FDPRT *END                                              82326
*
MSGEND2  DC    C'-*** END OF UTILITY ***'
MSGEND2L EQU   *-MSGEND2
         SPACE 1                                                 82326
SORTORD  DC    X'FFF1F2F30405F6',9X'FF'  SORT SEQUENCE F0-F6     82326
         SPACE 2                                                 82326
         LTORG ,                                                 82326
     TITLE '                 MAPDISK1 -- FORMAT'
* * * * * * * * * * * * * * * * * * *
*        PRODUCED AT TRIANGLE UNIVERSITIES COMPUTATION CENTER,
*                    RESEARCH TRIANGLE PARK,
*                    NORTH CAROLINA, 27709
*        SYSTEMS PROGRAMMER - H. L. JACKSON
*
* EXTENTIONS AT THE GODDARD SPACE FLIGHT CENTER
*                   GREENBELT, MARYLAND 20771
*        BY HANK HOOVER
*    MODIFIED BY L. N. FOSTER  10/13/70
*
*    MODIFIED BY F. G. ROSS 1/10/73
*
*    UPDATED AND EXTENSIVELY MODIFIED BY GENE CZARCINSKI
*        MAPDISK1/FORMAT, 23APR73
*
*    UPDATED  12/1/73    TO SUPPORT 2305AND 3330
*
*       BY F. G. ROSS  U.S. DEPT OF STATE
*
*    UPDATED 28MAR75, GENE CZARCINSKI, NASA/GSFC, CODE 531.
*      - CHANGE CSECT NAME TO 'MAPDISK1'
*      - ADD SUPPORT FOR TIME STAMP TO USE-COUNT AND CREATE-DATE
*        MAKE OPENMOD MSG FORMAT SAME AS NO-OPENMOD.
*      - FIX SECURITY CHECK
*
* SUPPORTS: 2301,2302,2303,2311,2314,2321,2305,3330
*
* * * * * * * * * * * * * * * * * * *
*
* FUNCTION:  THIS ROUTINE IS CALLED BY FASTDISK TO PROCESS THE (READ
*        IN) DSCB'S TO GENERATE A FORMATTED LISTING (ALLOCATION MAP)
*        OF A DASD VOLUME.
*
* * * * * * * * * * * * * * * * * * *
 EJECT
         USING AREASECT,R10            TO ADDRESS THE DSCB'S
FORMAT   STM   R0,R15,SAVESUB1   SAVE CALLER'S REGISTERS         82326
         LR    R12,R15       GET LOCAL BASE REGISTER             82326
         USING FORMAT,R12
         MVI   SWF,0                   RESET SWITCH
         TM    SW,SW5        FORMAT EXTENTS ?                    82326
         BNZ   *+8           YES.                                82326
         OI    SWF,1         NO.                                 82326
         LA    R14,1                   INIT COUNTERS
         ST    R14,SRTKNT
         ST    R14,NTRKNT
         MVI   F1SW,0                  INIT THE F1 SWITCH
         L     R14,ADDUCB    GET A(UCB)                          82326
         USING UCBOB,R14
         MVC   UNIT,UCBNAME            MOVE IN THE UNIT NAME
         SERVCALL UCBGN,UCBOB   GET GENERIC NAME OF DEVICE       82326
         MVC   DEVICE,0(R1)  COPY RESULT                         82326
NOMOV    PRTLIST HEAD2,TITLE=3    PRINT THE VOLUME SUB-TITLE     82326
         PRTV  HEAD4,TITLE=5,CC=NO    PRINT THE DATA SET SUB-TITLE
         PRTV  HEAD5,TITLE=6,CC=NO     AND SUB-SUB-TITLE         82326
         DROP  R14
 EJECT
         L     R10,#AREA     POINT TO FIRST DSCB                 82326
         ST    R10,ADATACH   SET CURRENT CHAIN ELEMENT           82326
DONXTF1  L     R10,SORPOINT  GET NTH SORTED ENTRY                82326
         CLI   DS1FMTID,C'1'           FORMAT 1 DSCB?            82326
         BNE   GETNEXT                 NO.
         MVC   WORKLINE,BLANKS   CLEAR WORK LINE                 82326
         PRTROOM 3           NEED THREE CONSECUTIVE LINES ON SAME PAGE
         MVC   DATANAME,DS1DSNAM       MOVE DSNAME TO MSG        82326
         LA    R1,DS1CREDT             A(CREATED DATE)           82326
         LA    R2,CRDATE               MSG AREA
         BAL   R14,CALEN
         LA    R1,DS1EXPDT             A(EXPERATION DATE)        82326
         LA    R2,PURGDATE             MSG AREA
         BAL   R14,CALEN
*
         CLC   DS1REFD,=XL5'0'  ANY REFERENCE DATE ?             82326
         BE    NOOPDATE      NO; SKIP                            82326
         CLC   DS1REFD,BLANKS   REALLY OLD DSCB ?                82326
         BE    NOOPDATE      YES; SKIP                           82326
         AIF   ('&LOCAL' NE 'CCSI').NOSVSDT                      82326
         CLI   DS1REFD,99    OLD PACKED FORMAT DATE ?            82326
         BNH   SUDATE        NO                                  82326
         TM    DS1REFD+2,X'0F'  REALLY ?                         82326
         BNO   NOOPDATE      DON'T KNOW WHAT IT IS               82326
         UNPK  OPENDATE,DS1REFD  MAKE PRINTABLE DATE             82326
         B     NOOPDATE      SKIP AROUND                         82326
SUDATE   DS    0H            LABEL FOR MVS DATE                  82326
.NOSVSDT LA    R1,DS1REFD    GET REFERENCE DATE                  82326
         LA    R2,OPENDATE   GET OUTPUT FIELD                    82326
         BAL   R14,CALEN     CONVERT IT                          82326
NOOPDATE XR    R3,R3                                             82326
         IC    R3,DS1NOEPV        GET EXTENT NO.                 82326
         CLI   DS1EXT1,X'40'   IS THERE A LABEL EXTENT ?         83156
         BNE   *+8           NO                                  83156
         LA    R3,1(,R3)     ALLOW FOR LABEL TRACK               83156
         ST    R3,EXTKNT          STORE EXTENT VALUE FOR REST OF CALCUL
         MVC   SERIALNO,DS1DSSN   MOVE SERIAL NUMBER             82326
         MVC   DB(2),DS1VOLSQ   SEQUENCE NO IN HALF WORD         82326
         LH    R3,DB                                             82326
         LA    R1,CALCU+2
         BAL   R14,EDIT           VOLUME SEQUENCE NUMBER
         MVC   SEQNO,CALCU+3
* NOW READY TO FIND PRIMARY ALLOCATION
         L     R9,#CCW                                           82326
         TM    DS1DSORG,JFCORGPO+JFCORGPS  LSTAR UPDATED ?       82326
         BNM   SEC3          NO; IGNORE                          82326
         CLI   DS1DSORG+1,0       VSAM OR FUNNY?                GP09186
         BNE   SEC3          YES; IGNORE                        GP09186
         OC    DS1LSTAR(5),DS1LSTAR    ARE FIELDS VALID          82326
         BZ    SEC3                    NO
         SLR   R3,R3                                             91162
         ICM   R3,7,DS1LSTAR           REL NO. OF TRKS USED     GP09186
         BZ    SEC2                    EMPTY OR PDS/E           GP09186
         SRL   R3,8                    ALIGN                    GP09186
         LA    R3,1(,R3)               UP TRK COUNT
SEC2     LA    R1,SPUSED          OUT AREA
         BAL   R14,EDIT           PUT OUT USED VALUE
SEC3     XR    R8,R8              CLEAR ACCUM REG
         LA    R1,DS1EXT1         F1 FIRST EXTENT                82326
         L     R5,EXTKNT          NUMBER OF EXTENTS
         LTR   R5,R5              TEST FOR AN EMPTY DATA SET
         BZ    EXTSS3             JUMP IF EMPTY
         CH    R5,=H'4'           CHECK FOR ALL EXTENTS IN F1
         BL    EXTSS1
         LA    R5,3               SET FOR LAST 3
EXTSS1   BAL   R14,ALLSUM         SUM UP ALLOCATION
         CLI   EXTKNT+3,4         CHECK FOR F3 TO BE CHECKED     83156
         BL    EXTSS3             THRU
SRCHF3   BAL   R14,SRCHDSCB       GET NEW FORMAT 3 DSCB
         LR    R10,R3                  SET UP FOR NEW DSCB
         CLI   DS3FMTID,C'3'           IS THIS A FORMAT 3 DSCB
         BE    FOUNDF3                 YES - GO COMPUTE EXTENTS
         CLI   DS3FMTID,C'2'           FORMAT 2 (IS)?
         BE    SRCHF3                  YES, TRY AGAIN.
         B     ERROR4                  NO, ERROR.
FOUNDF3  LA    R1,DS3EXTNT        START OF GROUP
         L     R5,EXTKNT          EXTENT COUNT BACK
         SH    R5,=H'3'           REDUCE BY THOSE ALREADY DONE
         CH    R5,=H'5'           FOUR MORE FIELDS
         BL    EXTSS2
         LA    R5,4               SET TO 4
EXTSS2   BAL   R14,ALLSUM         SUM UP SOME MORE
         L     R5,EXTKNT          LOAD IN COUNT
         SH    R5,=H'7'           SET TO DO LAST 9
         BNP   EXTSS3A                 THRU
         LA    R1,DS3ADEXT
         BAL   R14,ALLSUM         DO LAST OF THEM
EXTSS3A  L     R10,ADATACH   RESTORE THE DSCB POINTER            82326
         L     R10,SORPOINT  RESET THE DSCB ADDRESS              82326
EXTSS3   LR    R3,R8                   PUT IN OUTPUT AREA
         LA    R1,SPALLOC              TOTAL ALLOCATION
         BAL   R14,EDIT                PUT OUT TOTAL
         XC    0(4,R9),0(R9)           INDICATE END OF LIST
         TM    DS1DSORG,JFCORGPO   IS THIS A PDS ?               82326
         BZ    NOTPDS        NO; SKIP                            82326
         TM    SW,SW7        PDS OPTION ?                        82326
         BZ    NOTPDS        NO; SKIP                            82326
         MVI   DRBLOCKS+L'DRBLOCKS-1,C'?'  SHOW NOT AVAILABLE    82326
         TM    RES0,X'40'              WAS THIS PDS PROCESSED?
         BZ    NOTPDS                  NO, DO NOT PROCESS IT.
         LH    R3,BLKUSED              GET CNT OF BLKS USED
         LA    R1,DRBLUSED             A(OUTPUT LINE AREA)
         BAL   R14,EDIT
         LH    R3,BLKALOC              GET CNT OF BLKS ALLOCATED
         LA    R1,DRBLOCKS
         BAL   R14,EDIT
NOTPDS   PRTF  WORKLINE,LEN2   PRINT THE FIRST DATA SET LINE     82326
         TM    RES0,X'80'              ERROR READING THE DIRECTORY?
         BZ    PDSEND                  NO.
         MVC   ERRNAME,DS1DSNAM        YES, OUTPUT MESSAGE       82326
         PRTF  IOERR1,IOERR1L,CC=NO                              82326
         PRTF  IOERR2,IOERR2L,CC=NO                              82326
PDSEND   MVC   WORKLINE,BLANKS  CLEAR THE PRINT LINE AGAIN       82326
         SERVCALL DSFMT,DS1DSORG   FORMAT THE BASIC DCB OPTIONS  82326
         MVC   DSORG,0(R1)   MOVE DSORG                          82326
         TM    DS1SMSFG,DS1PDSE+DS1PDSEX  PDS/E OR HFS?         GP05120
         BZ    NOTSMS        NO; OLD FASHIONED                  GP05120
         MVC   DSORG(3),=C'PDE'                                 GP05120
         TM    DS1SMSFG,DS1PDSEX  HFS ?                         GP05120
         BZ    NOTSMS        NO                                 GP05120
         MVC   DSORG(3),=C'HFS'                                 GP05120
NOTSMS   MVC   RECFM,3(R1)                                       82326
         MVC   LRECL,22(R1)                                      82326
         MVC   BLKSIZE,17(R1)                                    82326
         MVC   OPTCD,9(R1)                                       82326
         MVC   SECURITY,BLANKS                                   82326
         LA    R14,SECURITY                                      82326
         LR    R15,R14                                           82326
         TM    DS1DSIND,DS1IND40      RACF PROTECTED ?           82326
         BZ    NOSECRA       NO                                  82326
         MVC   0(5,R14),=C'RACF/'                                82326
         LA    R14,5(,R14)                                       82326
NOSECRA  TM    DS1DSIND,DS1IND10+DS1IND04  PASSWORD PROTECTED ?  82326
         BZ    NOSECPW                                           82326
         MVC   0(5,R14),=C'READ/'                                82326
         BO    NOSECRD                                           82326
         LA    R14,5(,R14)                                       82326
NOSECRD  MVC   0(6,R14),=C'WRITE/'                               82326
         LA    R14,6(,R14)                                       82326
NOSECPW  CR    R14,R15       ANY PROTECTION ?                    82326
         BE    NOSECPUT      NO; WRITE THE LINE                  82326
         BCTR  R14,0         BACKSPACE                           82326
         MVC   0(10,R14),=C' PROTECTED'                          82326
NOSECPUT PRTLIST TYPESORG                                        82326
         SPACE 1                                                 82326
         MVC   WORKLINE,BLANKS   CLEAR DATA HOLDER               82326
         MVC   TALLOC(3),=C'ABS'                                 82326
         TM    DS1SCALO,X'C0'                                    82326
         BZ    CHBIT14
         MVC   TALLOC(3),=C'CYL'
         BO    CHBIT14
         MVC   TALLOC(3),=C'TRK'
         TM    DS1SCALO,X'80'                                    82326
         BO    CHBIT14
         MVC   TALLOC(3),=C'BLK'
CHBIT14  LA    R15,TALLOC+3
         TM    DS1SCALO,X'08'                                    82326
         BZ    *+18
         MVC   0(7,R15),=C',CONTIG'
         LA    R15,7(,R15)
         B     CHBIT14B
         TM    DS1SCALO,X'04'                                    82326
         BZ    *+18
         MVC   0(5,R15),=C',MXIG'
         LA    R15,5(,R15)
         B     CHBIT14B
         TM    DS1SCALO,X'02'                                    82326
         BZ    *+14
         MVC   0(4,R15),=C',ALX'
         LA    R15,4(,R15)
CHBIT14B TM    DS1SCALO,X'01'                                    82326
         BZ    *+14
         MVC   0(6,R15),=C',ROUND'
         LA    R15,6(,R15)
         MVC   0(6,R15),=C', 2ND='
         LA    R15,6(,R15)
         MVC   2(10,R15),=CL10' '
         LR    R1,R15                  SAVE
         LH    R3,DS1SCALO+2           FORMAT SECONDARY ALLOC    82326
         CVD   R3,DB                                             82326
         MVC   0(L'PAT,R15),PAT
         ED    0(L'PAT,R15),DB+5                                 82326
         LA    R15,L'PAT(,R15)
CHBIT14D MVC   0(10,R1),1(R1)
         BCTR  R15,0
         CLI   0(R1),C' '
         BE    CHBIT14D
         TM    SWF,X'01'               EXT TO BE LISTED?
         BZ    CHBIT14E                YES.
         MVC   0(6,R15),=C', EXT='
         LA    R2,6(,R15)
         LA    R15,11(,R15)
*        L     R3,EXTKNT
         XR    R3,R3                                             83156
         IC    R3,DS1NOEPV   GET EXTENT COUNT (LESS LBL TRK)     83156
         BAL   R14,LFTJXT
CHBIT14E LA    R0,ALLMSG     GET START OFF MESSAGE               82326
         SR    R15,R0        GET MESSAGE LENGTH                  82326
         PRTF  ALLMSG,(R15),CC=NO                                82326
         TM    SWF,X'01'               EXT?
         BZ    *+8                     YES.
         OI    SWF,X'02'               NO, SKIP LISTING.
         BAL   R14,PRNTEXTS       PRINT AND SORT ALL EXTEXTS
         NI    SWF,X'FD'
*
GETNEXT  CLI   DS1FMTID,C'4'           IS IT THE VTOC?           82326
         BE    PRMAP
GETNEXT1 MVI   F1SW,X'FF'              SET THE SWITCH
         L     R10,ADATACH   GET DSCB POINTER                    82326
         LA    R10,AREALEN(,R10)  GET NEXT POSITION              82326
         ST    R10,ADATACH             UPDATE THE POINTER
         C     R10,#TOP      LAST ONE PROCESSED ?                82326
         BL    DONXTF1       NO; LOOP AGAIN                      82326
         B     MAPZ          DONE WITH DSCB MAP.                 82326
         EJECT ,
ABTOREL  MVC   HOLD(8),2(R1)      MOVE FOR BOUNDRIES
ABTOREL0 LH    R2,HOLD            CC
         MH    R2,TRKPCYL                                        82326
         AH    R2,HOLD+2          HH
ABTOREL1 LH    R3,HOLD+4          CC                             82326
         MH    R3,TRKPCYL                                        82326
         AH    R3,HOLD+6          HH
         SLL   R2,16              MOVE TO TOP
         ALR   R2,R3              PUT TOGETHER
         BR    R14                EXIT
*
ALLSUM   ST    R14,HOLD+8         TAKE CARE OF FETCHING ALLOCATIONS
ALLSUM1  BAL   R14,ABTOREL
         ST    R2,0(,R9)          PUT IN TABLE
         LA    R9,4(,R9)          NEXT READY
         BAL   R14,LENGTH         GET LENGTH IN R3
         AR    R8,R3              TOTAL UP ALL LENGTHS FOR DS
         LA    R1,10(R1)          SET FOR NEXT
         BCT   R5,ALLSUM1         LOOP
         L     R14,HOLD+8         RESTORE
         BR    R14                BACK OUT
*
CALEN    SR    R3,R3              COMPUTE YEARS AND DAYS
         IC    R3,0(R1)           PICK UP YEAR
         CVD   R3,DB                                             82326
         OI    DB+7,X'0F'                                        82326
         UNPK  0(2,R2),DB+6(2)  YEAR                             82326
         MVC   DB(2),1(R1)      FETCH LAST TWO BYTES             82326
         LH    R3,DB                                             82326
         CVD   R3,DB              TO DECIMAL                     82326
         OI    DB+7,X'0F'                                        82326
         UNPK  2(3,R2),DB+6(2)  DAYS                             82326
         BR    R14                RETURN
*
EDIT     CVD   R3,DB              MAKE IT DECIMAL                82326
         SH    R1,=H'2'           SET UP CORRECT ADDRESS
         MVC   0(L'PAT,R1),PAT    MOVE PATTERN
         ED    0(L'PAT,R1),DB+5   PUT IT IN SHAPE                82326
         BR    R14                EXIT
*
FULLEDIT ST    R14,HOLD+4              CONVERT TO DECIMAL
         LA    R1,ZEXT-1               EXTENT NO.
         BAL   R14,EDIT
         BAL   R14,LENGTH
         LA    R1,ZLNGTH+2             LENGTH
         BAL   R14,EDIT
         LA    R1,ZFIRST+1             FIRST TRK ADRS
         LH    R3,HOLD
         BAL   R14,EDIT
         LA    R1,ZLAST+1              LAST TRK ADRS
         LH    R3,HOLD+2
         BAL   R14,EDIT
         L     R14,HOLD+4         RESTORE RETURN ADDRESS
         BR    R14
*
LENGTH   ST    R2,HOLD            SAVE VALUE FOR TEMP
         LH    R3,HOLD+2          BRING IN LAST POSITION
         AH    R3,=H'1'           UPDATE
         SH    R3,HOLD            TAKE OFF START TRACK VALUE
         BR    R14                GO ON BACK
*
LFTJXT   ST    R14,HOLD           SAVE EXIT
         MVC   CALCU,BLANKS                                      82326
         LA    R1,CALCU+2         TEMP WORK AREA
         BAL   R14,EDIT           EDIT INTO TEMP
LOOPBIT  CLI   0(R1),C' '         FIND FIRST NONSPACE
         BNE   FOUNDX
         LA    R1,1(R1)           CHECK NEXT CHARACTER
         B     LOOPBIT
FOUNDX   MVC   0(5,R2),0(R1)      LEFT JUSTIFY NUMERALS
         L     R14,HOLD           EXIT BACK
         BR    R14
*
NOTHER   MVC   DB(5),0(R4)      LOAD UP EXTENT VALUE             82326
         LH    R3,DB+2                                           82326
         MH    R3,TRKPCYL                                        82326
         XR    R2,R2
         IC    R2,DB+4                                           82326
         AR    R3,R2              FIRST TRACK PLUS CYLINDER
         BZ    ENDDSCB5           DONE IF NOTHING THERE
         AR    R1,R3              ACCUMULATE TOTAL
         LH    R2,DB                                             82326
         AR    R3,R2              STOP ADDRESS PLUS ONE
         SLL   R2,16              MOVE TO LEFT
         ALR   R2,R3              ALL TOGETHER
         BCTR  R2,0               LESS ONE
         ST    R2,0(,R9)          PUT IT AWAY IN ACCUM
         LA    R9,4(,R9)
         LA    R4,5(,R4)          GO TO NEXT ONE
         BCT   R5,NOTHER          GO DO FOR NEXT
         BR    R14
 EJECT
* PRINT THE 'VTOC'
PRMAP    MVC   WORKLINE,BLANKS                                   82326
         MVC   DATANAME(27),=CL27'VTOC        **UNUSED DSCBS='
         LA    R1,DS4VTOCE        PUT OUT EXTENT SIZE
         BAL   R14,ABTOREL        CONVERT TO RELATIVE TRACKS R2 BACK
         LA    R3,1               PSEUDO EXTENT NO 1
         BAL   R14,STORMAP        STORE EXTENT FOR MAP R1,R2,DATANAME
         BAL   R14,LENGTH         FIND LENGTH OF DATA SET R2-R3 BACK
         LA    R1,SPALLOC
         BAL   R14,EDIT           PREPARE TO PRINT R3,R1
         LR    R9,R2              SAVE TOTAL FOR AWHILE
         MVC   HOLD+4(4),DS4HPCHR GET END MARKER
         SRL   R2,16
         BAL   R14,ABTOREL1       DO ONLY USED
         BAL   R14,LENGTH
         LA    R1,SPUSED
         BAL   R14,EDIT
         LH    R3,DS4DSREC             FREE DSCBS
         LA    R2,DATANAME+27
         BAL   R14,LFTJXT
         PRTF  WORKLINE,LEN1                                     82326
         MVC   WORKLINE,BLANKS                                   82326
         PRTF  LINEX,LINEXL,CC=NO    EXT--FIRST---LAST...        82326
         LR    R2,R9                   RESTORE FIRST/LAST
         LA    R3,1                    EXTENT = 1 FOR VTOC
         BAL   R14,FULLEDIT       DO FULL EDIT
         PRTF  ZOUT,ZLEN,CC=NO     PRINT VTOC EXTENT             82326
         MVC   WORKLINE,BLANKS                                   82326
* COMPUTE AND LIST 'FREE SPACE' ON VOLUME
         LA    R1,DS4VTOCI   POINTTO STATUS BITS                GP05155
         SUBCALL SUBVTOCI    EXAMINE THE STATUS                 GP05155
         CH    R15,=H'4'     USABLE HWM ?                       GP05155
         BNL   SHARED        NO; SKIP SPACE (USE LSPACE ?)      GP05155
         MVC   DATANAME(15),=C'* * *FREE SPACE'
         MVC   SPUSED,SPUSED-1    CLEAR USED SPACE
         LA    R10,AREALEN(,R10)       BUMP TO NEXT...SHOULD BE FORMAT5
         CLI   DS5FMTID,C'5'      CHECK FOR 5
         BNE   SHARED
         L     R9,#CCW        LOAD ADDRESS OF ACCUMULATION TABLE 82326
         SR    R1,R1              CLEAR ACCUM REG.
NEXTDS   LA    R5,8                    8 FIELDS FIRST
         LA    R4,DS5AVEXT        AREA OF EXTENTS
         BAL   R14,NOTHER         DO FIRST 8 EXTENTS
         LA    R5,18              LAST IN DSCB
         LA    R4,DS5MAVET
         BAL   R14,NOTHER         DO LAST 18 EXTENTS
         C     R5,DS5PTRDS+1      CHECK IF ONE MORE DSCB5 AROUND
         BE    ENDDSCB5           NO, QUIT
         BAL   R14,SRCHDSCB       GO SEARCH FOR DSCB 5
         LR    R10,R3                  GET A(NEW DSCB)
         CLI   DS5FMTID,C'5'   REALLY ANOTHER TYPE 5 ?           82326
         BE    NEXTDS        YES; KEEP GOING TILL END            82326
ENDDSCB5 XC    0(4,R9),0(R9)           INDICATE END OF LIST
         LR    R3,R1              COUNT IN PARM AREA
         LA    R1,SPALLOC         OUTPUT AREA
         BAL   R14,EDIT           READY FOR PRINT
* PRINT THE FREE SPACE VALUES
         PRTF  DATANAME,LEN1,CC=NO                               82326
         BAL   R14,PRNTEXTS       PRINT AND SORT ALL EXTENTS
SHARED   L     R10,#AREA     POINT TO FORMAT 4 DSCB AGAIN        82326
         CLC   =XL5'0',DS4F6PTR        ANY F6?
         BE    GETNEXT1                NO.
         PRTF  SHAREM1,SHAREM1L,CC=NO                            82326
         PRTF  SHAREM2,SHAREM2L,CC=NO                            82326
         MVC   DS6PTRDS,DS4F6PTR
         XR    R6,R6                   CLEAR EXT ACCUMULATOR
         MVC   WORKLINE,BLANKS                                   82326
SHARED1  BAL   R14,SRCHDSCB
         LR    R10,R3
         CLI   DS6FMTID,C'6'           F6?
         BNE   SHARED9                 NO??
         LA    R5,8                    DO FIRST 8
         LA    R4,DS6AVEXT
         BAL   R14,PSHARED
         LA    R5,18                   DO NEXT 18
         LA    R4,DS6MAVET
         BAL   R14,PSHARED
         C     R5,DS6PTRDS+1           DONE?
         BNE   SHARED1                 NO.
SHARED9  MVC   WORKLINE,BLANKS                                   82326
         B     GETNEXT1
*
PSHARED  ST    R14,HOLD                SAVE RETURN ADRS
PSHARED1 MVC   HOLD+4(5),0(R4)
         LH    R3,HOLD+4
         LTR   R3,R3                   DONE?
         BZ    SHARED9                 YES.
         LA    R1,ZFIRST+1             FIRST TRK ADRS
         BAL   R14,EDIT
         LA    R6,1(R6)
         LA    R1,ZEXT-1               EXTENT NO.
         LR    R3,R6
         BAL   R14,EDIT
         LA    R1,ZLAST                CYL COUNT
         LH    R3,HOLD+6
         BAL   R14,EDIT
         XR    R3,R3
         IC    R3,HOLD+8               NO. SHARING DS
         LA    R1,ZLNGTH+2
         BAL   R14,EDIT
         PRTF  ZOUT,ZLEN,CC=NO                                   82326
         LA    R4,5(R4)                BUMP TO NEXT ENTRY
         BCT   R5,PSHARED1             LOOP
         L     R14,HOLD                RESTORE
         BR    R14                     AND RETURN
 EJECT
* OUTPUT EXTENT LISTING FOR A DATA SET
PRNTEXTS L     R9,#CCW        SET UP TABLE OF STORED EXTENTS     82326
         XC    EXTKNT,EXTKNT      SET EXTENT COUNT TO 1
         ST    R14,HOLD+8
         TM    SWF,X'02'               PRINT EXTENTS?
         BO    PRNTXT1                 NO.
         PRTF  LINEX,LINEXL,CC=NO    EXT-FIRST----               82326
         MVC   WORKLINE,BLANKS                                   82326
PRNTXT1  L     R2,0(,R9)          LOAD IN EXTENT SIZE
         LTR   R2,R2              CHEKC FOR END OF LIST
         BZ    JUMPS1             NO MORE LINES OUT
         L     R3,EXTKNT          BRING EXTENT COUNT IN
         LA    R3,1(,R3)          BOOST
         ST    R3,EXTKNT          RESTORE
         BAL   R14,STORMAP        STORE EXTENT FOR MAP OUTPUT
         L     R3,EXTKNT          COUNT BACK IN
         LA    R9,4(,R9)               SETUP FOR NEXT LINE.
         TM    SWF,X'02'               PRINT?
         BO    PRNTXT1                 NO.
         BAL   R14,FULLEDIT            YES, FORMAT FOR PRINTING
         PRTF  ZOUT,ZLEN,CC=NO                                   82326
         MVC   WORKLINE,BLANKS                                   82326
         B     PRNTXT1                 JUMP TO NEXT LINE
JUMPS1   L     R14,HOLD+8         RESTORE RETURN ADDRESS
         BR    R14
 EJECT
* COMPUTE CORE ADDRESS OF NEXT DSCB BASED ON CCHHR
*        R10 - A(DSCB)
*        R3  - MAX RECORD NUMBER OF PRESENT DSCB
*        R4  - MAX RECORD NUMBER OF FORMAT3
SRCHDSCB XR    R2,R2
         XR    R3,R3
         XR    R4,R4
         IC    R3,DS1PTRDS+4           GET RECORD NUMBER         82326
         ICM   R2,3,DS1PTRDS+2         GET THE HH                82326
         MH    R2,DSCBSTRK
         AR    R3,R2
         XR    R2,R2
         ICM   R2,3,DS1PTRDS           GET THE CC                82326
         MH    R2,TRKPCYL                                        82326
         MH    R2,DSCBSTRK             GET NUMBER OF DSCBS
         AR    R3,R2
         XR    R2,R2
         IC    R4,CCHHR+4              RECORD NUMBER
         ICM   R2,3,CCHHR+2            HH
         MH    R2,DSCBSTRK             CONVERT HH TO RECORDS
         AR    R4,R2
         XR    R2,R2
         ICM   R2,3,CCHHR              GET THE CC                82326
         MH    R2,TRKPCYL             GET NUM TRKS/CYL           82326
         MH    R2,DSCBSTRK             GET NUM DSCBS/TRK
         AR    R4,R2
* GET THE CORE OFFSET FROM THE PRESENT DSCB
         SR    R3,R4
         MH    R3,=Y(AREALEN)          BUMP TO OFFSET
         AR    R3,R10                  GET CORE ADRS OF FMT 3
         BR    R14
         EJECT ,
* SAVE EXTENT ENTRY IN TABLE FOR TRACK 'ALLOCATION MAP'
STORMAP  L     R4,SRTKNT     GET NUMBER OF EXTENT ENTRIES        82326
         BCTR  R4,0          LESS ONE                            82326
         SLA   R4,3          TIMES ENTRY SIZE (*8)               82326
         A     R4,#TOP       GET ADDRESS OF NEXT ENTRY           82326
         C     R4,#LIMIT     IN STORAGE ?                        82326
         BNL   ERROR8        NO; TOO BAD                         82326
         ST    R10,0(,R4)    SET DSCB BLOCK ADDRESS              82326
         STC   R3,0(,R4)     EXTENT NUMBER                       82326
         ST    R2,4(,R4)     EXTENT ADDRESSES                    82326
         LA    R4,1                                              82326
         A     R4,SRTKNT                                         82326
         ST    R4,SRTKNT     SAVE UPDATED COUNT                  82326
         BR    R14           RETURN TO CALLER                    82326
         EJECT ,
* FORMAT AND OUTPUT THE TRACK 'ALLOCATION MAP'
MAPZ     L     R5,SRTKNT               LOAD UP NO. OF EXTENTS
         BCT   R5,MAPSORT    CHECK ENTRY COUNT                   82326
         B     MAPGOUT       CLEAR OUT IF NOTHING ?              82326
MAPSORT  L     R9,#TOP       START OF SORT BIN                   82326
         ST    R9,DB         START OF EXTENT AREA                82326
         ST    R5,DB+4       NUMBER OF ENTRIES                   82326
         MVC   DB+8(4),=AL1(C'A',4,4,8)  SET SORT OPTIONS        82326
         SERVCALL SORTH,DB   INVOKE HEAPSORT                     82326
         PRTV  HEAD3,TITLE=5,CC=NO                               82326
         PRTV  NULLVCON,TITLE=6                                  82326
MAP1     MVC   WORKLINE,BLANKS                                   82326
         L     R10,0(,R9)    GET DSCB BLOCK                      82326
         L     R2,4(,R9)               GET THE SIZES
         BAL   R14,LENGTH              LENGTH
         LA    R1,LNGTH+1
         BAL   R14,EDIT                LENGTH
         A     R3,TRKTOTAL             ACCUM TRK TOTAL              HHH
         ST    R3,TRKTOTAL             STORE BACK                   HHH
         LH    R3,HOLD                 FIRST TRACK
         LA    R1,STRTT+1
         BAL   R14,EDIT                START TRACK
         LH    R3,HOLD+2               LAST TRACK
         LA    R1,LSTT+1
         BAL   R14,EDIT                LAST TRACK
         XR    R3,R3
         IC    R3,0(,R9)               GET THE EXTENT NO.
         LA    R1,EXTT                 EXTENT NO.
         BAL   R14,EDIT
         CLI   DS1FMTID,C'1'   IS IT A FORMAT 1?                 82326
         BE    MVCDSN                  YES, MOVE IN THE DSNAME
         CLI   DS4IDFMT,C'4'   IS IT A FORMAT 4 (VTOC)?          82326
         BE    MVCVTOC
         MVC   SHLINE+4(15),=C'* * *FREE SPACE'                  82326
         B     MAPPUT
MVCVTOC  MVC   SHLINE+4(4),=C'VTOC'                              82326
         B     MAPPUT
MVCDSN   MVC   SHLINE,DS1DSNAM         MOVE IN DATA SET NAME     82326
MAPPUT   PRTF  WORKLINE,MAPSL                                    82326
         CH    R5,=H'1'                LAST TIME THRU?
         BE    NOHOLE                  YES, DO NOT CHECK LAST ONE
         LH    R3,6(,R9)               GET THE LAST TRK
         LH    R15,12(,R9)             GET NEXT START TRK
         BCTR  R15,0
         SR    R15,R3                  CHECK FOR DIFFERENCE GT 1
         BZ    NOHOLE                  IF ZERO, THEN NO HOLE.
         LA    R1,HSRT+1
         BAL   R14,EDIT
         LH    R3,12(,R9)              GET NEXT START ADRS
         LA    R1,HEND+1
         BAL   R14,EDIT
         PRTF  HOLMES,HOLMESL,CC=NO                              82326
NOHOLE   LA    R9,8(,R9)               GET THE NEXT ENTRY
         BCT   R5,MAP1
         ST    R9,#TOP       SAVE TOP USED                       82326
         MVC   MAPS+1(MAPSL-1),MAPS    CLEAR AREA
         MVC   LSTT,=C'TOTAL'
         L     R3,TRKTOTAL
         LA    R1,LNGTH+1
         BAL   R14,EDIT
MAPGOUT  XC    TRKTOTAL,TRKTOTAL                                 82326
* ALL DONE WITH THIS VOLUME
         PRTF  TOT,TOTL                                          82326
         PRTF  WORKLINE,40                                       82326
         PRTF  MSGEND,MSGENDL                                    82326
*
         XR    R15,R15                 RC=0
GOBAK    ST    R15,SAVESUB1+15*4                                 82326
         PRTV  NULLVCON,TITLE=3                                  82326
         PRTV  NULLVCON,TITLE=4                                  82326
         PRTV  NULLVCON,TITLE=5                                  82326
         PRTV  NULLVCON,TITLE=6                                  82326
         LM    R0,R15,SAVESUB1  RESTORE REGISTERS                82326
         BR    R14           RETURN                              82326
ERROR4   LA    R15,4                   RC=4
         B     GOBAK
ERROR8   LA    R15,8                   RC=8
         B     GOBAK
 EJECT
PAT      DC    X'402020202120'         EDITING PATTERN - 4 COLUMNS
*
HEAD2    FDPRT 'DASD ALLOCATION MAP FOR VOLUME=SER=',NL          82326
         FDPRT VOL,DEBL,LEN=6                                    82326
         FDPRT 'UNIT=',RADJ,LEN=8                                82326
         FDPRT UNIT,DEBL,LEN=8                                   82326
         FDPRT 'DEVICE='                                         82326
         FDPRT DEVICE                                            82326
         FDPRT *END                                              82326
*
HEAD3    VCON  END=HEAD3L                                        82326
         DC    C'FIRST TRACK    LAST TRACK',CL5' '               82326
         DC    CL11'LENGTH',CL14'EXT NO',C'DATA SET NAME'
         VCON  ,                                                 82326
*
HEAD4    VCON  END=HEAD4L                                        82326
         DC    CL53' '                                           82326
        DC    CL9' ',CL7'LAST',CL7'ALLOC',CL7'PURGE',CL8'FILE',CL5'VOL'
         DC    C'---TRACK---',C'  ',C'--PDS DIR--'               82326
         VCON  ,                                                 82326
HEAD5    VCON  END=HEAD5L                                        82326
         DC    CL15' ',CL38'DATA SET NAME'                       82326
         DC    CL9' '                                            82326
         DC    CL7'USED'                                     76019*AMS*
         DC    CL7'DATE',CL7'DATE',CL8'SERIAL',CL5'SEQ'
         DC    CL7'ALLOC',CL6'USED',CL7'ALLOC',C'USED'
         VCON  ,                                                 82326
*
MSGEND   DC    C'0*** END OF VOLUME - DASD IS MAPPED ***'
MSGENDL  EQU   *-MSGEND
*
TOT      DC    CL32' ',C'-----'
TOTL     EQU   *-TOT
         EJECT ,
*
*
*
HOLMES   DC    C'********** UNACCOUNTED SPACE BETWEEN TRACKS '
HSRT     DC    CL5' ',C' AND '
HEND     DC    CL5' ',C' *****'
HOLMESL  EQU   *-HOLMES
*
IOERR1   DC    C'*** I/O ERROR DETECTED READING DIRECTORY FOR '
ERRNAME  DC    CL44' '                 DSNAME
IOERR1L  EQU   *-IOERR1
IOERR2   DC    C'*** THEREFORE, THE DIRECTORY BLOCK COUNT WILL BE INCOR+
               RECT.'
IOERR2L  EQU   *-IOERR2
*
LINEX    DC    CL11' ',C'EXT--FIRST---LAST--LENGTH'
LINEXL   EQU   *-LINEX
*
TYPESORG FDPRT ' ',NL,LEN=5   INDENT; NEW LINE                   82326
         FDPRT 'DSORG='                                          82326
         FDPRT DSORG,DEBL,LEN=3                                  82326
         FDPRT 'RECFM=',PADL                                     82326
         FDPRT RECFM,DEBL,LEN=5                                  82326
         FDPRT 'LRECL=',PADL                                     82326
         FDPRT LRECL,DEBL,LEN=5                                  82326
         FDPRT 'BLKSIZE=',PADL                                   82326
         FDPRT BLKSIZE,DEBL,LEN=5                                82326
         FDCLI OPTCD,C' ',BNE=TYPESYOP                           82326
         FDSPACE 15                                              82326
         FDGOTO TYPESNOP                                         82326
TYPESYOP FDPRT 'OPTCD=',PADL                                     82326
         FDPRT OPTCD,DEBL,LEN=8                                  82326
TYPESNOP FDSPACE 15                                              82326
         FDPRT SECURITY                                          82326
         FDPRT *END                                              82326
*
ALLMSG   DC    CL5' ',C'ALLOC='
TALLOC   DC    CL64' '
ALLMSGL  EQU   *-ALLMSG                                          82326
*
SHAREM1  DC    C'* * *SHARED (SPLIT) SPACE'
SHAREM1L EQU   *-SHAREM1
SHAREM2  DC    CL11' ',C'EXT--FIRST---CYL---NO. SHARING DATA SETS'
SHAREM2L EQU   *-SHAREM2
     TITLE '                 MAPDISK2 -- PDSREAD'
* PDSREAD IS A SUBROUTINE OF MAPDISK WHICH IS CALLED BY FASTDISK
* TO DETERMINE THE NUMBERS OF DIRECTORY BLOCKS USED AND ALLOCATED
* FOR THE PDS'S ON A DISK.
*
* PDSREAD, F.G.ROSS, 1/10/73
*
* MAPDISK2, GENE CZARCINSKI, 07APR73
*
* SUPPORTS: 2301,2302,2303,2311,2314,2321,2305,3330
*
*
*       UPDATE 12/1/73 TO SUPPORT 2305 AND 3330
*
*   BY F. G. ROSS  U.S. DEPT OF STATE
*
*    UPDATED 25MAR75, GENE CZARCINSKI, NASA/GSFC, CODE 531.
*        CHANGE CSECT NAME FROM 'FASTMAP2' TO 'MAPDISK2'
*
* UPON ENTRY:
*        R0 = AL1(NUM OF PDS/TRK),AL3(TABLE OF DATA CHAINS)
*        R1 = AL1(NUM OF TRKS/CYL),AL3(IOB)
*        R3 = A(CCW BUILD AREA)
*
* NOTE:  THIS ROUTINE ASSUMES THAT THE CCW WORK AREA PASSED TO IT
*        HAS AT LEAST 1416 BYTES (50*20 + 52*8) AVAILABLE.
*
         SPACE 2                                                 82326
*
*
PDSREAD  STM   R0,R15,SAVESUB2   SAVE CALLER'S REGISTERS         82326
         LR    R12,R15                 INIT BASE REG
         USING PDSREAD,R12
* INIT PNTRS, VARIABLES, ETC.
         MVI   SWP,0                   CLEAR THE SWITCHES
         LH    R1,TRKPCYL    GET # TRKS/CYL                      82326
         BCTR  R1,0                    MINUS ONE FOR INDEXING
         STC   R1,EXTTRKS+1
         LH    R4,PDETRK     BLKS/TRK (CCWS TO BUILD)            82326
         MH    R4,=H'8'                FINAL ADRS FOR STOP
         L     R3,#CCW       CCW AREA                            82326
         LA    R7,52*8(,R3)  INPUT AREA                          82326
         ST    R3,IOBSTART             SET CCW PNTR IN THE IOB
         MVI   IOBSEEK+7,0   SET 'R' TO ZERO                     82326
*
* CONSTRUCT THE CHANNEL PROGRAM WHICH WILL READ THE PDS DIRECTORY
         MVC   0(16,R3),PDSSHA         SEARCH (SHA & TIC)
         LA    R15,IOBSEEK+3           A(CCHH IN THE IOB)        82326
         STCM  R15,7,1(R3)                                       82326
         STCM  R3,7,9(R3)    TIC RETURN                          82326
         LA    R3,16(,R3)    FIRST READ CCW                      82326
         AR    R4,R3
         LR    R15,R7                  INPUT AREA PNTR
PDSCCWL  MVC   0(8,R3),PDSCKD          READ CCW
         STCM  R15,7,1(R3)                                       82326
         LA    R3,8(,R3)
         LA    R15,20(,R15)                                      82326
         CR    R3,R4
         BL    PDSCCWL
         SH    R3,=H'4'                DONE...BACK UP 4
         NI    0(R3),X'BF'             AND TURN OFF CMD CHAIN
*
         L     R10,#AREA     GET FIRST DSCB                      82326
         ST    R10,ADSCB     SAVE IT, JUST IN CASE               82326
*
PDSSTART NI    SWP,X'0F'               RESET THE SWITCHES
         MVI   RES0,0        ZERO THE RES0 BYTE                  82326
         CLI   DS1FMTID,C'1'   FORMAT 1 DSCB ?                   82326
         BNE   GETMOR                  NO, SKIP IT.
         TM    DS1DSORG,JFCORGPO   IS IT A PDS?                  82326
         BNO   GETMOR                  NO, SKIP IT.
         TM    DS1SMSFG,DS1PDSE+DS1PDSEX  PDS/E OR HFS ?        GP05120
         BNO   GETMOR                  YES; SKIP IT             GP05120
         MVC   CCHH,DS1EXT1+2   GET THE STARTING CCHH            82326
         XR    R8,R8                   CLEAR THE BLK COUNTERS
         XR    R9,R9
         CLI   DS1NOEPV,0    ZERO EXTENTS (PATTERN DSCB)?        82326
         BE    DIRDON                  YES, SKIP
         ICM   R0,15,ADDDEB  DEB EXTENTS MODIFIED ?              82326
         BZ    ERROR47       NO; FAIL                            94221
*
PDSEXCP  MVC   IOBSEEK+3(4),CCHH   MOVE THE STARTING CCHH TO IOB 82326
         LR    R14,R7        GET FIRST DATA ADDRESS              82326
         L     R15,#AREA     GET START OF DSCBS                  82326
         SR    R15,R14       SIZE TO CLEAR                       82326
         XR    R0,R0                                             82326
         XR    R1,R1                                             82326
         MVCL  R14,R0        CLEAR READ INPUT AREA               82326
* EXECUTE THE CHANNEL PROGRAM AND PROCESS THE RETURN CODE.
         XC    ECB,ECB       CLEAR THE ECB                       82326
         EXCP  IOBSTDRD                                          82326
         WAIT  ECB=ECB                                           82326
         CLI   ECB,X'7F'     EXPECTED COMPLETION ?               82326
         BE    READOK                  YES.
         CLI   ECB,X'42'     EXTENT VIOLATION?                   82326
         BE    ERROR                   YES...BAD ERROR
* TREAT ANYTHING ELSE AS EOF OR SALVAGABLE
         XR    R14,R14                 CLEAR
         LR    R15,R7        POINT AT INPUT AREA                 82326
ERRCHK   CLC   CCHH(4),0(R15)   WAS ANYTHING READ IN?            82326
         BNE   ENDCHK                  NO...DONE
         CLC   =H'0',6(R15)            EOF?
         BE    ENDCHK
         CLC   =X'080100',5(R15)  PDS BLK (KEY=8, LEN=256)?      82326
         BE    ERRCHK1                 YES...OK
         OI    SWP,X'80'               NO...BAD...FLAG ERROR
         B     ENDCHK
ERRCHK1  LA    R14,1(R14)              CONTINUE SCAN
         LA    R15,20(,R15)
         CH    R14,PDETRK    MORE THAN FOR PDS?                  82326
         BH    ERROR                   YES...ABORT
         B     ERRCHK
ENDCHK   OI    SWP,X'20'               SET FLAG
         B     READOK1
*
READOK   LH    R14,PDETRK    INIT BLK COUNTER                    82326
READOK1  LR    R15,R7        INIT INPUT AREA POINTER             82326
CNTLOOP  CLC   =H'0',6(R15)  EOF?  (COUNT=ZERO)                  82326
         BE    DIRDON                  YES, DONE
         CLC   =X'080100',5(R15)  KEY=8, LEN=256?                82326
         BNE   ERROR                   NO...ERROR
         TM    SWP,X'40'               LOGICAL END PASSED?
         BO    SKIP2                   YES
         CLI   8(R15),X'FF'   SPECIAL KEY FOR LAST?              82326
         BNE   SKIP1                   NO
         OI    SWP,X'40'               FLAG LOGICAL END
SKIP1    LA    R8,1(,R8)               BUMP USED COUNT
SKIP2    LA    R9,1(,R9)               BUMP ALLOC COUNT
         LA    R15,20(,R15)            POINT AT NEXT AREA
         BCT   R14,CNTLOOP             LOOP TILL DONE
*
GETDIR   TM    SWP,X'80'               ERROR?
         BO    ERROR                   YES.
         TM    SWP,X'30'               EOF HIT?
         BNZ   DIRDON                  YES.
* UPDATE THE CCHH
         LH    R6,CCHH+2               GET THE HH
         CH    R6,TRKPCYL    AT LIMIT OF CYL?                    82326
         BL    TRKOK                   NO.
         LH    R6,CCHH                 GET THE CC
         LA    R6,1(,R6)               BUMP IT
         STH   R6,CCHH                 THEN SAVE IT
         XR    R6,R6                   RESET THE HH
         B     CYLOK
TRKOK    LA    R6,1(,R6)
CYLOK    STH   R6,CCHH+2               STORE THE HH
CHKEXT   CLC   CCHH,DS1EXT1+2+4       AT END OF FIRST EXTENT?    82326
         BE    ERROR                   YES, ERROR.
         B     PDSEXCP
*
ERROR    MVI   RES0,X'80'    SET ERROR FLAG                      82326
         B     DIRDON                                            94221
ERROR47  MVI   RES0,7        SET NOT AVAILABLE (NO APF AUTH)     94221
         B     GETMOR        DON'T SET 'DONE'                    94221
*
DIRDON   OI    RES0,X'40'    SET THE 'DONE' FLAG                 82326
         STH   R8,BLKUSED    # BLKS USED                         82326
         STH   R9,BLKALOC    # BLKS ALLOCATED                    82326
GETMOR   LA    R10,AREALEN(,R10)   GET THE NEXT BLK              82326
         C     R10,#TOP      DONE ALL ?                          82326
         BL    PDSSTART      NO, CONTINUE                        82326
*
ALDONE   LM    R0,R15,SAVESUB2         RESTORE REGS & RETURN     82326
         BR    R14                                               82326
     TITLE '                 MAPDISK3 -- SORTIT'
*     FUNCTION:  TO SORT DSCB ENTRIES INTO TYPE/DSN SEQUENCE     82326
*        COMPLETELY REPLACED (HEAPSORT; DSCB CHAIN USED)
*
SORTIT   SAVE  (14,12),,*
         LR    R12,R15                 INIT THE BASE REG.
         USING SORTIT,R12
* NEW SAVE AREA NOT GOTTEN...NOT NEEEDED
         L     R7,#AREA      GET TABLE START ADDRESS             82326
         LA    R8,AREALEN    SET ENTRY SIZE                      82326
         L     R1,#TOP       GET LAST+1 ENTRY ADDRESS            82326
         SR    R1,R8         GET LAST ENTRY                      82326
         SR    R1,R7         GET TOTAL SIZE                      82326
         BNP   SORTOUT       ERROR OR SINGLE ENTRY               82326
         XR    R0,R0                                             82326
         DR    R0,R8                                             82326
         MR    R0,R8         ENSURE END IS MULTIPLE              82326
         ST    R1,TEMP       SET MAXIMUM OFFSET                  82326
         LR    R6,R1         GET OFFSET TO LAST ELEMENT          82326
         LR    R5,R6         COPY                                82326
         SR    R5,R8         FINAGLE                             82326
         BM    SORTOUT       ONE ENTRY                           82326
         XR    R4,R4         CLEAR FOR DIVIDE                    82326
         DR    R4,R8         GET NUMBER OF ENTRIES IN R5         82326
         SRL   R5,1          HALVE                               82326
         MR    R4,R8         OFFSET TO MIDDLE ELEMENT IN R5      82326
SORTH104 L     R2,SORPOINT-AREASECT(R5,R7)  B=A(L)               82326
         BAL   R9,SORTHEAP   HEAP IT                             82326
         SR    R5,R8         GET PRIOR ELEMENT                   82326
         BNM   SORTH104      REPEAT FOR IT                       82326
*                                                                82326
         XR    R5,R5         L=1                                 82326
         L     R6,TEMP       GET OFFSET TO LAST ENTRY            82326
         SR    R6,R8         GET PRIOR ENTRY                     82326
SORTH110 LA    R1,0(R6,R8)   M+1                                 82326
         L     R2,SORPOINT-AREASECT(R1,R7)  B=A(M+1)             82326
         L     R10,SORPOINT-AREASECT(,R7)  A(1)                  82326
         ST    R10,SORPOINT-AREASECT(R1,R7)   A(M+1)=A(1)        82326
         BAL   R9,SORTHEAP   SORT                                82326
         SR    R6,R8         GET PRIOR ENTRY                     82326
         BNM   SORTH110      REPEAT                              82326
SORTOUT  LR    R2,R7         POINT TO FIRST ENTRY                82326
         RETURN (14,12),T,RC=(15)  RESTORE AND RETURN            82326
         SPACE 1                                                 82326
SORTHEAP LR    R3,R5         I=L                                 82326
SORTH117 LA    R4,0(R3,R3)   J=I+I                               82326
         AR    R4,R8         RELATIVE TO 0                       82326
         CR    R4,R6         J ? M                               82326
         BH    SORTH125      HIGH                                82326
         BE    SORTH121      J=M                                 82326
         L     R1,SORPOINT-AREASECT(R4,R7)   A(J)                82326
         L     R10,AREALEN+SORPOINT-AREASECT(R4,R7)  A(J+1)      82326
         CLC   DATAKEY-AREASECT(L'DATAKEY,R10),DATAKEY-AREASECT(R1)
         BNH   SORTH121      A(J+1) NOT HIGHER THAN A(J)         82326
         AR    R4,R8         J=J+1                               82326
SORTH121 L     R1,SORPOINT-AREASECT(R4,R7)   A(J)                82326
         CLC   DATAKEY-AREASECT(L'DATAKEY,R2),DATAKEY-AREASECT(R1)
         BNL   SORTH125      YES                                 82326
         ST    R1,SORPOINT-AREASECT(R3,R7)   A(I)=A(J)           82326
         LR    R3,R4         I=J                                 82326
         B     SORTH117                                          82326
*                                                                82326
SORTH125 ST    R2,SORPOINT-AREASECT(R3,R7)  A(I)=B               82326
         BR    R9            RETURN                              82326
     TITLE '                 MAPDISK4 -- ZTIME'
*  ROUTINE TO RETURN THE TIME AND DATE IN THE FOLLOWING FORMAT:
*        HH.MM.SS.TH DAY MON DD,YYYY
*
*  F.G.ROSS, 1/10/73
*  UPDATED 20MAY74,  GENE CZARCINSKI
*  UPDATED 25MAR75, GENE CZARCINSKI, NASA/GSFC, CODE 531
*        CHANGE CSECT NAME FROM 'FASTMAP4' TO 'MAPDISK4'
*
*
*
ZTIME    STM   R0,R15,SAVESUB3   SAVE CALLER'S REGISTERS         82326
         LR    R12,R15                  SETUP BASE REG           82326
         USING ZTIME,R12                                         82326
         LR    R2,R1         OUTPUT AREA                         82326
         TIME  DEC
         ST    R1,DATE                 SAVE DATE FOR LATER
         MVC   MONMSK(12),MONPAT  MAKE DAYS/MONTH                82326
         TM    DATE+1,X'01'  ODD YEAR ?                          82326
         BNZ   ZTIMNLIP      YES; NOT LEAP YEAR                  82326
         TM    DATE+1,X'12'  MULTIPLE OF FOUR ?                  82326
         BM    ZTIMNLIP      NO                                  82326
         MVI   MONMSK+10,29  SET FOR LEAP YEAR                   82326
ZTIMNLIP MVC   TIMEHRS(LENTIM),BLANKS                            82326
         MVI   TIMEHRS+L'TIMEHRS,C'.'                            82326
         MVI   TIMMINS+L'TIMMINS,C'.'                            82326
         MVI   TIMSECS+L'TIMSECS,C'.'                            82326
         MVC   CDAYN+L'CDAYN(3),=C',19'                          82326
***FORMAT THE PRESENT TIME (AS SET BY THE OPERATOR)
         ST    R0,DB         CNVT PACKED TO DEC                  82326
         MVI   DB+4,X'0F'                                        82326
         UNPK  WORKWORD(9),DB(5)                                 82326
         MVC   TIMEHRS,WORKWORD        MOVE TIME TO MSG
         MVC   TIMMINS,WORKWORD+2
         MVC   TIMSECS,WORKWORD+4
         MVC   TIMHUNS,WORKWORD+6
***FORMAT TODAY'S DATE INTO THE OUTPUT MSG
         MVC   WORKWORD(4),DATE        MOVE DATE TO WORK AREA.
         MVO   WORKWORD+1(3),WORKWORD(2)   MAKE 00YYDDDS INTO 00000YYS.
         UNPK  CYR(2),WORKWORD+2(2)    FORMAT YEAR.
         XC    DB,DB                                             82326
         MVC   DB+4(4),WORKWORD   CNVT YEAR TO BIN               82326
         CVB   R0,DB                                             82326
         ST    R0,BINYEAR              AND SAVE
         XC    DB,DB                   CNVT DAYS TO BIN          82326
         MVC   DB+6(2),DATE+2                                    82326
         CVB   R7,DB                                             82326
         ST    R7,BINDAYS              SAVE FOR LATER
         XR    R1,R1
         LA    R15,11
MON2     IC    R1,MONMSK(R15)          COMPUTE MON & DAY
         SR    R7,R1
         BNP   MONOVR
         BCT   R15,MON2
         B     *+6
MONOVR   AR    R7,R1                   CORRECT OVERDRAW
         CVD   R7,DB                   FORMAT DAY                82326
         UNPK  CDAYN(2),DB+6(2)                                  82326
         OI    CDAYN+1,X'F0'           FIX UP SIGN
         MH    R15,=H'3'               GET TABLE OFFSET
         LA    R15,MONTAB(R15)         AND POINT AT MONTH
         MVC   CMON(3),0(R15)          MOVE IT TO THE MSG
***DAY OF WEEK
         L     R15,BINYEAR
         SH    R15,=H'69'
         BNP   OUTT                    MUST BE GREATER OR BAD
         XR    R14,R14                 CLEAR DAY REG
         D     R14,=F'4'
         MH    R15,=H'5'
         AR    R14,R15
         A     R14,BINDAYS
         SRDA  R14,32
         D     R14,=F'7'               MODULO 7 FOR WEEK
         MH    R14,=H'3'
         LA    R14,DAYTAB(R14)         POINT AT TODAY
         MVC   CDAY(3),0(R14)          INSERT IN MSG
***
OUTT     MVC   0(LENTIM,R2),TIMEHRS  COPY TIME & DATE TO USER'S AREA
         LM    R0,R15,SAVESUB3         RESTORE REGS & RETURN     82326
         BR    R14                                               82326
         SPACE 2                                                 82326
MONPAT   DC    AL1(31,30,31,30,31,31,30,31,30,31,28,31)          82326
DAYTAB   DC    C'TUEWEDTHRFRISATSUNMON'
MONTAB   DC    C'DECNOVOCTSEPAUGJULJUNMAYAPRMARFEBJAN'
         EJECT ,
*        SHARED WORK AREA
*
COMMWORK DS    0D            COMMON WORK AREA                    82326
SYSPRINT PRTWORK SYSPRINT,SYSTERM,TITLE=6                        82326
CONSOLE  PRTWORK *USER       TPUT IF TSO, ELSE WTO               85256
* CONSTANTS, ETC. FOR CHANNEL PROGRAM
CCWSHA   CCW   @SHA,0,@CC,4            SEARCH HOME ADDRESS
CCWTIC   CCW   @TIC,0,@CC,0            TRANSFER IN CHANNEL
CCWKD    CCW   @KD,0,@CC+@SLI,140      READ: KEY,DATA; LEN=140
PDSSHA   CCW   X'39',0,X'40',4         SEARCH HOME ADDRESS
PDSTIC   CCW   X'08',0,X'40',0         TRANSFER IN CHANNEL
PDSCKD   CCW   X'1E',0,X'60',20        READ: COUNT,KEY,DATA; LEN=20
*
@CC      EQU   X'40'
@SLI     EQU   X'20'
@SHA     EQU   X'39'
@TIC     EQU   X'08'
@KD      EQU   X'0E'
         SPACE 1
         SPACE 1
*
#SIZE    DC    A(04*1024,1984*1024)    FOR CORE MANAGEMENT   91164*DTS*
#ADR     DC    A(0,0)
#FREE    DC    A(0,4096)
*
CSYSUT1  DC    CL8'SYSUT1'             FOR COMPATABILITY WITH MAPDASD
SW       DC    B'00000&EXT.0&PDS' -    RUN SWITCHES.
DEVSW    DC    B'000000&STR.0',X'00'   DEVICE TYPE SWITCHES
DEVSW2   DC    AL1(0,T2311,T2301,T2303,T2302,T2321,T23051)
         DC    AL1(T23052,T2314,T3330,T3340,T3350,T3375)         85253
         DC    AL1(T3330,T3380,T3390)                            94221
HEXCON1  DC    14X'0F'
HEXCON2  DC    C'0123456789ABCDEF'
NULLVCON VCON  ' '           NULL LINE                           82326
BLANKS   DC    CL133' '      LOTS OF BLANKS                      82326
         SPACE 2
* I/O, ETC. CONTROL BLOCKS
*
PATDCB   DCB   DDNAME=SYSUT1,DEVD=DA,DSORG=PO,MACRF=(E),EXLST=1  82326
PATDCBL  EQU   *-PATDCB      LENGTH TO MOVE                      82326
*
* MESSAGES
MSG1     DC    C'*** DYNAMIC STORAGE -- '                        94221
MSG1A    DC    C'000',C'K BYTES ALLOCATED WITH '
MSG1U    DC    C'000',C'K BYTES USED ***'
MSG1L    EQU   *-MSG1
MSG2     DC    C'*** RUN PARMS -- '
MSG2A    DC    CL50' '
MSG2L    EQU   *-MSG2
MSG3     DC    C'*** REGION SIZE MAY BE REDUCED BY '
MSG3A    DC    C'000',C'K BYTES ***'
MSG3L    EQU   *-MSG3
MSG4     DC    C'** UTILITY FILE OPEN ERROR'
MSG4L    EQU   *-MSG4
MSG5     DC    C'** XXXXXX DYNAMIC CORE EXCEEDED **'
         DC    C' INCREASE REGION **'                       EAC/28MAR75
MSG5L    EQU   *-MSG5
MSG6     DC    C'      EXECUTION ABORTED PROCESSING VOL=SER='
MSG6V    DC    CL6' '
MSG6L    EQU   *-MSG6
MSG7     DC    C'**  VTOC I/O ERROR,  ECB='
MSG7A    DC    CL2' ',C',  SENSE='
MSG7B    DC    CL4' ',C',  CSW='
MSG7C    DC    CL14' ',C',  BBCCHH='
MSG7D    DC    CL12' '
MSG7L    EQU   *-MSG7
*
TRTBL    DC    74C' ',C'.<(+|&&',9C' ',C'!$*);-/',9C' '
         DC    C',%_>?',10C' ',C':#@''="',C' ',C'ABCDEFGHI',7C' '
         DC    C'JKLMNOPQR',8C' ',C'STUVWXYZ',23C' ',C'ABCDEFGHI'
         DC    7C' ',C'JKLMNOPQR',8C' ',C'STUVWXYZ',6C' '
         DC    C'0123456789',6C' '
         SPACE 2
         DCBD  DSORG=(QS),DEVD=(DA)
         SPACE 2
SAVE     DSECT ,             WORK/SAVE AREA                      82326
SAVESUB1 DC    18F'0'                  FORMAT'S SAVE AREA
SAVESUB2 DC    18F'0'
SAVESUB3 DC    18F'0'
DB       DS    2D            WORK/CONVERSION AREA                82326
@SERVICE DS    A             ADDRESS OF SERVICE ROUTINE          82326
@PRINTER DS    A             ADDRESS OF PRINT ROUTINE            82326
ADDDEB   DC    A(0)          DEB ADDRESS                         82326
OLDDEB   DC    XL16'0'       OLD DEB EXTENTS
NEWDEB   DC    XL16'0'       NEW DEB EXTENTS FOR PDS OPTION
ADDTIOT  DC    A(0)                                              82326
ADDUCB   DS    A                                                 82326
*
VOL      DC    CL6' '
UNIT     DC    CL3'0C0',CL2' '         UNIT PLUS SPACE FOR BIN NO.
DEVICE   DC    CL8' '        DEVICE TYPE (MODEL NO.)             82326
HEAD1DT  DC    CL28' '       MAIN TITLE TIME/DATE                82326
HOLD     DC    4D'0'                   WORK AREA
EXTKNT   DC    F'0'
SRTKNT   DC    F'1'
NTRKNT   DC    F'1'                    NEXT ENTRY POSITION FOR F1 DSCB
TRKTOTAL DC    F'0'
ADATACH  DC    A(0)                    A(DATA CHAIN)
SWF      DC    X'00'                   INTERNAL SWITCHES
F1SW     DC    X'00'
CCHH     DC    F'0'                    CURRENT CCHH
ADSCB    DC    A(0)                    A(CORE BLK-DSCB)
SWP      DC    X'00'
EXTTRKS  DC    H'0'          TRACKS/CYLINDER-1
BINYEAR  DC    F'0'
BINDAYS  DC    F'0'
DATE     DC    F'0'
WORKWORD DC    2F'0'
MONMSK   DC    AL1(31,30,31,30,31,31,30,31,30,31,28,31)
TIMEHRS  DC    CL2' ',C'.'
TIMMINS  DC    CL2' ',C'.'
TIMSECS  DC    CL2' ',C'.'
TIMHUNS  DC    CL2' ',C' '
CDAY     DC    CL3' ',C' '
CMON     DC    CL3' ',C' '
CDAYN    DC    CL2' ',C',19'
CYR      DC    CL2'  ',C' '
LENTIM   EQU   *-TIMEHRS
TEMP     DC    D'0'
*
#SORTAB  DC    A(0)                    A(START OF SORTED PNTRS)
#AREA    DC    A(0)                    A(TOP WORK/BOTTOM DSCB)
#COREMAX DC    A(0)                    A(MAX CORE EVER USED)
#CCW     DC    A(0)                    A(BOTTOM WORK AREA)
#TOP     DC    A(0)                    A(CURRENT TOP OF AREA)
#LIMIT   DC    A(0)                    A(MAX DYNAMIC CORE)
TRKPCYL  DC    H'0'                    TRKS/CYL                  82326
DSCBSTRK DC    H'0'                    DSCBS/TRK
*
RETCODE  DC    Y(0)                    RETURN CODE.
BB       DC    H'0'                    CURRENT BBCCHHR
CC       DC    H'0'
HH       DC    H'0'
R        DC    HL1'0'
PDETRK   DC    H'0'          PDS BLKS/TRK                        82326
SWTSO    DC    X'00'                                             82326
VTEND    DC    XL5'0'        CCHHR OF LAST REQUIRED DSCB         82326
ABCSW    DC    X'00'                                        EAC/26MAR75
VOLSER   DC    12CL6' '                FOR SER=...
         SPACE 1                                                 82326
         IEFJFCBN ,                                              82326
         SPACE 1                                                 82326
DCB      DCB   DDNAME=SYSUT1,DEVD=DA,DSORG=PO,MACRF=(E),EXLST=EXLIST
         SPACE 1                                                 82326
EXLIST   DC    X'87',AL3(INFMJFCB)                               82326
OPLIST   DC    X'80',AL3(DCB)                                    82326
         IEZIOB DSECT=NO                                         82326
         ORG   IOBSTDRD-4                                        82326
ECB      DC    F'0'                                              82326
         ORG   ,                                                 82326
         SPACE 2                                                 82326
*        TEMPORARY WORK SPACE FOR EBCDIC PRINTS                  82326
*                                                                82326
CALCU    DC    CL10' '                 THIS IS A WORK AREA       82326
WORKLINE DS    CL133         ALLOW FOR ONE FULL PRINT LINE       82326
         ORG   WORKLINE+1                                        82326
DATANAME DC    CL44' ',CL09' '         DATA SET NAME             82326
         DC    CL9' '        SPACER                              82326
OPENDATE DC    CL5' ',CL2' '   DATE LAST OPENED                  82326
CRDATE   DC    CL5' ',CL2' '           CREATE DATE               82326
PURGDATE DC    CL5' ',CL2' '           PURGE  DATE               82326
SERIALNO DC    CL6' ',CL2' '           SERIAL NO. (OF ORIG VOL)  82326
SEQNO    DC    CL3' ',CL3' '           VOL SEQUENCE NO.          82326
SPALLOC  DC    CL4' ',CL2' '           SPACE ALLOCATED - TOTAL TRACKS
SPUSED   DC    CL4' ',CL3' '           SPACE USED - TOTAL TRACKS 82326
LEN1     EQU   *-WORKLINE                                        82326
DRBLOCKS DC    CL4' ',CL2' '           DIRECTORY BLOCKS ALLOCATED
DRBLUSED DC    CL4' '                  DIRECTORY BLOCKS USED     82326
LEN2     EQU   *-WORKLINE                                        82326
         ORG   WORKLINE+1                                        82326
DSORG    DC    CL3' '                                            82326
RECFM    DC    CL5' '                                            82326
LRECL    DC    CL5' '                                            82326
BLKSIZE  DC    CL5' '                                            82326
OPTCD    DC    CL8' '                                            82326
SECURITY DC    C'RACF/READ/WRITE PROTECTED'                      82326
         ORG   WORKLINE+1                                        82326
ZOUT     DC    CL11' '                                           82326
ZEXT     DC    CL3' ',CL2' '           EXTENT NUMBER             82326
ZFIRST   DC    CL5' ',CL2' '           FIRST TRACK HERE          82326
ZLAST    DC    CL5' ',CL2' '           LAST TRACK NEXT           82326
ZLNGTH   DC    CL8' '                  LENGTH OF EXTENT          82326
ZLEN     EQU   *-WORKLINE                                        82326
         ORG   WORKLINE+1                                        82326
MAPS     DC    CL3' '                                            82326
STRTT    DC    CL5' ',CL9' '           START TRACK               82326
LSTT     DC    CL5' ',CL9' '           LAST TRACK                82326
LNGTH    DC    CL5' ',CL7' '           LENGTH OF EXTENT          82326
EXTT     DC    CL2' ',CL7' '           EXTENT NUMBER             82326
SHLINE   DC    CL44' '            DATA SET NAME                  82326
MAPSL    EQU   *-WORKLINE                                        82326
         ORG   ,                                                 82326
SAVEND   EQU   *             END OF DYNAMIC WORK AREA            82326
         SPACE 2                                                 82326
AREASECT DSECT ,
SORPOINT DS    A             NTH ENTRY POINTS TO NTH SORTED DSCB 82326
CCHHR    DS    XL5           CCHHR FOR THIS DSCB                 82326
RES0     DS    X,X           FLAGS                               82326
DATAKEY  DS    0CL45,X       DSCB TYPE FOR SORT                  82326
         IECSDSL1 1          EXPAND FORMAT 1 DSCB
         AIF   (&MVSXA).SKIPDS1
DS1FLAG1 EQU   DS1NOBDB+1,1,C'X'  MORE FLAGS
DS1COMPR EQU   X'80'           COMPRESSABLE EXTENDED IF DS1STRP
DS1CPOIT EQU   X'40'           CHECKPOINTED D S
DS1SMSFG EQU   DS1FLAG1+17,1,C'X'  SMS FLAG
DS1SMSDS EQU   X'80'           SMS D S
DS1SMSUC EQU   X'40'           NO BCS ENTRY
DS1REBLK EQU   X'20'           MAY BE REBLOCKED
DS1CRSDB EQU   X'10'           BLKSZ BY DADSM
DS1PDSE  EQU   X'08'           PDS/E
DS1STRP  EQU   X'04'           EXTENDED FORMAT D S
DS1PDSEX EQU   X'02'           HFS D S
DS1DSAE  EQU   X'01'           EXTENDED ATTRIBUTES EXISY
DS1SCEXT EQU   DS1SMSFG+1,3,C'X'  SECONDARY SPACE EXTENSION
DS1SCXTF EQU   DS1SCEXT,1,C'X'  -"- FLAG
DS1SCAVB EQU   X'80'           SCXTV IS AVG BLOCK LEN
DS1SCMB  EQU   X'40'                 IS IN MEGBYTES
DS1SCKB  EQU   X'20'                 IS IN KILOBYTES
DS1SCUB  EQU   X'10'                 IS IN BYTES
DS1SCCP1 EQU   X'08'           SCXTV COMPACTED BY 256
S1SCCP2 EQU   X'04'                 COMPACTED BY 65536
DS1SCXTV EQU   DS1SCXTF+1,2,C'X'  SEC SPACE EXTNSION VALUE
DS1ORGAM EQU   DS1ACBM         CONSISTENT NAMING - VSAM D S
DS1RECFF EQU   X'80'           RECFM F
DS1RECFV EQU   X'40'           RECFM V
DS1RECFU EQU   X'C0'           RECFM U
DS1RECFT EQU   X'20'           RECFM T   001X XXXX IS D
DS1RECFB EQU   X'10'           RECFM B
DS1RECFS EQU   X'08'           RECFM S
DS1RECFA EQU   X'04'           RECFM A
DS1RECMC EQU   X'02'           RECFM M
*   OPTCD DEFINITIONS   BDAM    W.EFA..R
*                       ISAM    WUMIY.LR
*             BPAM/BSAM/QSAM    WUCHBZTJ
DS1OPTIC EQU   X'80'  FOR DS1ORGAM - CATLG IN ICF CAT
DS1OPTBC EQU   X'40'           ICF CATALOG
DS1RACDF EQU   DS1IND40
DS1SECTY EQU   DS1IND10
DS1WRSEC EQU   DS1IND04
DS1SCAL1 EQU   DS1SCALO,1,C'X'    SEC. ALLOC FLAGS
S1DSPAC EQU   X'C0'         SPACE REQUEST MASK
DS1CYL   EQU   X'C0'           CYLINDER BOUND
DS1TRK   EQU   X'80'           TRACK
DS1AVRND EQU   X'41'           AVG BLOCK + ROUND
DS1AVR   EQU   X'40'           AVG BLOCK LEN
DS1MSGP  EQU   X'20'
DS1EXT   EQU   X'10'           SEC. EXTENSION EXISTS
DS1CONTG EQU   X'08'           REQ. CONTIGUOUS
DS1MXIG  EQU   X'04'           MAX
DS1ALX   EQU   X'02'           ALX
DS1DSABS EQU   X'00'           ABSOLUTE TRACK
DS1SCAL3 EQU   DS1SCAL1+1,3,C'X'  SEC ALLOC QUANTITY
.SKIPDS1 SPACE 1
         ORG   IECSDSL1+140                                      82326
BLKALOC  DS    H             DIRECTORY BLOCKS ALLOCATED
BLKUSED  DS    H             DIRECTORY BLOCKS USED
AREALEN  EQU   *-AREASECT    LENGTH OF ONE DSCB ENTRY
         SPACE 1
         ORG   DS1FMTID                                          82326
         IECSDSL1 4          EXPAND FORMAT 4 DSCB
         SPACE 1
         ORG   IECSDSL1                                          82326
         IECSDSL1 3
         SPACE 1
         ORG   IECSDSL1                                          82326
         IECSDSL1 5
         ORG   IECSDSL1                                          82326
         IECSDSL1 6
         SPACE 2
YOUCB    DSECT ,
         IEFUCBOB ,
         SPACE 1                                                 82326
         CVT   DSECT=YES                                         82326
         SPACE 1                                                 82326
         IKJTCB ,                                                82326
         SPACE 1                                                 82326
         IEFTIOT1 ,                                              82326
         SPACE 1                                                 82326
         IEZDEB ,                                                82326
         SPACE 1                                                 82326
         IHACDE ,                                                82326
         AIF   (NOT &MVS).ENDMAP                                 82326
         IHAASCB ,                                               82326
.ENDMAP  END   ,                                                 82326
