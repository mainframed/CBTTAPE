RIPL     TITLE 'OS RIPPLE PRINT'
***********************************************************************
**                                                                   **
**   THIS PROGRAM WAS WRITTEN BY RICHARD (DICK) OXLEY TO CLEAN 1403  **
**   PRINT TRAINS.                                                   **
**   1) THE DESIRED PRINTER //PRNT DD UNIT=cuu (USUALLY 00E or 00F)  **
**      IS LOADED WITH CLEANING PAPER, AND THE INK RIBBON REMOVED.   **
**   2) THE PATTERN IS SET FOR A PN TRAIN; OTHER TRAINS NEED A       **
**      DIFFERENT PATTERN AND DIFFERENT REPEAT FACTORS.              **
MAIN     START 0
         USING *,12
         STM   14,12,12(13)
         LR    12,15
         LR    6,13
         LA    13,SAVE
         ST    13,8(6)
         ST    6,4(13)
         OPEN  (PRNT,(OUTPUT))
         WTO   'REMOVE THE PRINTER RIBBON, AND ALIGN THE CLEANING'
         WTO   'PAPER FLUSH WITH THE METAL GUIDE BAR, SET CLUTCH'
         WTO   'AT 6 L.P.I., AND READY THE PRINTER.'
LOOP1    XC    ECBLK(4),ECBLK
         WTOR  'TO RIPPLE PRINT, REPLY ''G'' TO GO, AND ''E'' TO END',RX
               EPLY,1,ECBLK
         WAIT  ECB=ECBLK
         OI    REPLY,C' '    BE NICE                            *GYP*
         CLI   REPLY,C'E'
         BE    EOJ
         CLI   REPLY,C'G'
         BNE   LOOP1
         LA    3,20
         LA    4,50
         LA    5,PATTERN
         LA    6,SHIFTER
         LA    7,1
REPEAT   MVI   CCC,C'+'
         B     REPEAT1
REPEAT2  MVI   CCC,C' '
REPEAT1  MVC   0(59,6),0(5)
         MVC   59(59,6),0(5)
         PUT   PRNT,LINE
         MVC   59(1,5),0(5)
         MVC   0(59,5),1(5)
         BCT   3,REPEAT
         LA    3,20
         BCT   4,REPEAT2
         B     LOOP1
EOJ      CLOSE (PRNT,LEAVE)
         L     13,SAVE+4
         LM    14,12,12(13)
         SR    15,15
         BR    14
PRNT     DCB   BLKSIZE=133,DSORG=PS,RECFM=FA,MACRF=(PM),LRECL=133,     ?
               DDNAME=PRNT,EROPT=ACC
SAVE     DS    18F
ECBLK    DC    F'0'
LINE     DS    0CL133
CCC      DC    C' '
SHIFTER  DC    CL132' '
REPLY    DC    C' '
PATTERN  DC    C'123456789&&ABCDEFGHIJKLMNOPQRSTUVWXYZ@#%,*$<.-/+_)>:;Â¬X
               0?''"=('
         DC    CL1' '
         END
