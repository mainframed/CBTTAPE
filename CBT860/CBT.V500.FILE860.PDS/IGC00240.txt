MJFCB    TITLE 'SVCMJFCB - SVC TO MODIFY JFCB'
*              THIS PROGRAM WAS WRITTEN BY:
         SPACE 1
*              SHMUEL (SEYMOUR J.) METZ
*              SEE MEMBER $$RIGHTS FOR CURRENT ADDRESS/PHONE     92272
         SPACE 1
*              THIS SVC IS FURNISHED ON AN AS-IS BASIS.
*              IT DOES NOT CONTAIN ANY PROPRIETARY INFORMATION,
*              AND MAY BE FREELY DISTRIBUTED TO ANY OTHER INSTALLATION.
*              PLEASE FEEL FREE TO CONTACT ME IF ANY PROBLEMS OCCUR.
         EJECT
*              REWRITE JFCB
*              REGISTER 1 POINTS TO A PARAMETER LIST.
*              THE FIRST BYTE OF EACH WORD IN THE LIST IS A CODE:
*              THE LAST WORD IN THE LIST HAS A HIGH-ORDER BIT SET
*              THE LOW THREE BYTES OF EACH WORD ARE AN ADDRESS.
*              THE CODES ARE:
*        0     UNUSED ENTRY
*        1     DDNAME ENTRY
*        2     FILE SEQUENCE NUMBER
*        3     VOLUME SERIAL NUMBER
*        4     MEMBER NAME (UNLESS GDG OR DSORG NOT PO|POU)
*        5     VOLUME SEQUENCE NUMBER
*        6     DSNAME
*        7     BLKSIZE
*        8     CONDITIONAL BLOCKSIZE
*        9     LRECL
*        A     RECFM
*        B     RETURN DSNAME
*        C     LABEL TYPE               AUTHORIZED IN OS/VS      80137
*        D     VOLUME SERIAL NUMBERS (5)                         80137
*        E     SET DISP=MOD IF SEQUENTIAL AND NOT PDS MEMBER     81166
*        F     SET DISP=OLD (USEFUL IF SET TO MOD EARLIER)       84274
         SPACE 1
*              EACH DDNAME ENTRY IS FOLLOWED BY ENTRIES FOR
*                   FIELDS TO BE CHANGED.
*              IN THE CASE OF TAPES, THE FILE MUST NOT BE OPEN.
         SPACE 1
*              IF THE DDNAME IS BLANK(EBCDIC),
*              IT INDICATES CONCATENATION.
         SPACE 1
*              RETURN CODES IN REGISTER 15 ARE:
*        0     OK
*        4     ERROR
         SPACE 1
*              IN THE EVENT OF AN ERROR,
*                1 WILL BE SET TO THE ADDRESS OF THE ENTRY IN ERROR,
*                AND PROCESSING WILL CEASE.
         SPACE 1
*              AS AN ADDED ATTRACTION, 2 NEW ABEND CODES ARE SUPPLIED:
         SPACE 1
*        1F0   SPECIFIED DDNAME DOES NOT EXIST IN TIOT
*              MAY ALSO MEAN THAT THE BASE DDNAME EXISTS,
*              BUT THAT THERE ARE TOO MANY CONCATENATION ENTRIES,
*              OR NOT ENOUGH CONCATENATED DD STATEMENTS.
*              INITIAL DDNAME OF BLANKS WILL DO IT TOO.
*        ABEND 1F0 REPLACED BY RETURN CODE 4   (REQ. BY GYP)     *TSM*
         SPACE 1
*        2F0   I/O ERROR ON SYS1.SYSJOBQE OR INVALID SWAREQ *GYP*92272
*                                                                90301
*        IN SP 2 (XA) AND LATER, THIS MODULE REQUIRES AMODE 31   90301
*                                                                90301
*        CHANGED TO USE GBLC CONSTANT TO DETERMINE SVC NUMBER.  GP99053
*        CODE ADDED TO ALLOW AUTHORIZED PROGRAM TO CALL THIS CODE
*        DIRECTLY WITH SYNCH (SVC NOT INSTALLED). MINOR MODS.   GP99053
*        ABOVE CHANGED SO IT REALLY WORKS                       GP02252
*                                                               GP99053
         EJECT
         MACRO ,                                                 80137
&L       KEY   &KEY                                              80137
         GBLC  &SYSTEM                                           80137
         AIF   ('&SYSTEM' EQ 'MFT' OR '&SYSTEM' EQ 'MVT').OS     80137
.MVS     AIF   ('&KEY' EQ 'ZERO' OR '&KEY' EQ '0').ZERO          80137
         AIF   ('&KEY' EQ 'NONZERO' OR '&KEY' EQ 'NZERO').NZERO  80137
         AIF   ('&KEY' EQ 'PROB' OR '&KEY' EQ 'USER').NZERO      80137
         MNOTE 12,'KEY=&KEY INVALID'                             80137
         MEXIT ,                                                 80137
.ZERO    ANOP  ,                                                 80137
&L       SPKA  0                                                 80137
         MEXIT ,                                                 80137
.NZERO   ANOP  ,                                                 80137
&L       SPKA  0(KEYREG)                                         80140
         MEXIT ,                                                 80137
.OS      AIF   (T'&L EQ 'O').MEXIT                               80137
&L       EQU   *                                                 80137
.MEXIT   MEND  ,                                                 80137
         SPACE 2                                                 80137
         MACRO ,                                                 80137
&L       OPTYP &FUNC,&LEN                                        80137
         GBLC  &INTNAME                                         GP99053
&L       DC    AL2(&FUNC-&INTNAME,&LEN)                         GP99053
         MEND  ,                                                 80137
         SPACE 2                                                 80137
         COPY  OPTIONGB                                          81164
         GBLC  &INTNAME      MAKE INTERNAL (CSECT) NAME FOR MODULE
         GBLC  &EXTNAME      MAKE EXTERNAL (SVC) NAME FOR MODULE
         SPACE 1
         SYSPARM LIST=YES    MERGE AND DISPLAY PARAMETERS        81164
         SPACE 1                                                GP99053
         AIF   (&SVCJFCB GT 0).USENUM                           GP99053
&SVCJFCB SETA  999           MAKE FAKE (WRONG) NUMBER           GP99053
.USENUM  ANOP  ,                                                GP99053
&INTNAME SETC  'IGC00'.'&SVCJFCB'  MAKE INTERNAL NAME           GP99053
&EXTNAME SETC  '&INTNAME'(1,8)                                  GP99053
         AIF   ('&INTNAME'(8,1) NE '0').NOZONE                  GP99053
&EXTNAME SETC  '&INTNAME'(1,7).'{'  MAKE SVC X'C0'              GP99053
.NOZONE  PRINT ON,GEN,DATA  ***TEMP***   &PRTSOR                 81164
         MTITL 'SVC &SVCJFCB - &INTNAME - MODIFY JFCB'
         EJECT ,                                                 81164
 PUNCH ' ALIAS IGC00240,SVCMJFCB' *****DEBUG*****
&INTNAME START 0
         AIF   (NOT &MVSSP).PRESP2  SKIP IF NOT PRE-SP           92281
         SPLEVEL SET         SET CURRENT LEVEL                   92281
         AIF   (NOT &MVSXA).PRESP2                               90301
&INTNAME AMODE 31            31-BIT ADDRESSING REQUIRED (SWA)
.PRESP2  SPACE 1                                                 90301
R0       EQU   0                        SCRATCH
R1       EQU   1                        SCRATCH
R2       EQU   2                        INPUT TO SECTOR CONVERT
CVTREG   EQU   3
R3       EQU   3             WHY BE CONSISTENT?                 GP02252
TCBREG   EQU   4
SVRBREG  EQU   5
BASE     EQU   6                                                 80135
PARAMREG EQU   7                                                 80135
PARMREG  EQU   PARAMREG
WORKBASE EQU   8                                                 80135
WORKREG  EQU   WORKBASE
TIOTREG  EQU   9                                                 80135
UCBREG   EQU   10                                                80135
R11      EQU   11                                                80136
MESSIAH  EQU   R11                      MR. HANDEL               80136
KEYREG   EQU   12                       USER'S KEY               80136
R12      EQU   12                                               GP99053
R13      EQU   13
R14      EQU   14
R15      EQU   15
         AIF   (&MVS).BASTE                                      82116
         BALR  BASE,0
         LA    R0,*-&INTNAME OFFSET FROM START (2)              GP99053
         SLR   BASE,R0                                          GP99053
.BASTE   USING &INTNAME,BASE                                    GP99053
         USING CVTMAP,CVTREG
         USING TCB,TCBREG
         USING RBSECT,SVRBREG
         B     ENTSVC        SKIP AROUND                        GP99056
         DC    AL1(L'LABSVC)                                    GP99056
LABSVC   DC    C'&INTNAME &SYSDATE &SYSTIME'                    GP99056
ENTSVC   LR    PARAMREG,R1                                       80137
         LA    R0,ENDWORK-WORK         GET WORK AREA
         AIF   (NOT &MVS).MVTGET                                 92281
         STORAGE OBTAIN,LENGTH=(0),LOC=ANY  BOMB IF NONE        GP00011
         AGO   .COMGET                                           92281
.MVTGET  GETMAIN R,LV=(0)                                        80137
.COMGET  XC    0(INFMJFCB-WORK,R1),0(R1)                         80137
         LA    WORKBASE,0(,R1)                                   80137
         USING WORK,WORKBASE
         MVI   FLAGS,FGSVC   SHOW ENTERED AS SVC                GP99053
         AIF   ('&SYSTEM' EQ 'MFT' OR '&SYSTEM' EQ 'MVT').OS360  80137
         AIF   (&VS1).VS1                                        82315
         AIF   (NOT &SVS).AUMVS                                  82109
         TESTAUTH FCTN=1     CHECK TCB AUTHORIZATION             81164
         AGO   .AUSVS                                            81164
.AUMVS   TESTAUTH FCTN=1,STATE=YES,KEY=YES,RBLEVEL=2             81164
.AUSVS   BXH   R15,R15,*+8              RC = 0 ?                 81164
         OI    FLAGS,FGAUTH              YES - FLAG AUTHORIZED   80140
         SR    R1,R1                                            GP99056
         ICM   R1,7,RBLINK+1  GET CALLER'S RB                   GP99056
         IC    KEYREG,RBOPSW+1-RBSECT(,R1)                       80136
         B     COMMCODE                                         GP99053
         SPACE 2                                                GP99053
         PUSH  USING                                            GP99053
         DROP  ,                                                GP99053
         ENTRY SVCMJFCB                                         GP99053
SVCMJFCB BSM   R14,0         PRESERVE CALLER'S AMODE            GP99053
         STM   R14,R12,12(R13)  SAVE CALLER'S REGISTERS         GP99053
         LA    BASE,0(,R15)  COPY AND CLEAN ENTRY ADDRESS       GP99053
         LA    R0,SVCMJFCB-&INTNAME  ENTRY OFFSET               GP99053
         SLR   BASE,R0       MAKE TRUE BASE                     GP99053
         USING &INTNAME,BASE                                    GP99053
         L     CVTREG,CVTPTR  LOAD CVT ADDRESS                  GP99053
         USING CVTMAP,CVTREG                                    GP99053
         L     TCBREG,PSATOLD-PSA  LOAD TCB ADDRESS             GP99053
         USING TCB,TCBREG                                       GP99053
         L     SVRBREG,TCBRBP  GET SYNCH SVRB                   GP99056
         USING RBSECT,SVRBREG                                   GP99053
         LR    PARAMREG,R1                                      GP99053
         LA    R0,ENDWORK-WORK         GET WORK AREA            GP99053
         STORAGE OBTAIN,LENGTH=(0),LOC=ANY  BOMB IF NONE        GP00011
         XC    0(INFMJFCB-WORK,R1),0(R1)                        GP99053
         LA    WORKBASE,0(,R1)                                  GP99053
         USING WORK,WORKBASE                                    GP99053
*FAILS*  TESTAUTH FCTN=1     CHECK TCB AUTHORIZATION            GP99053
*FAILS*  BXH   R15,R15,CALLBAD          RC = 0 ?                GP99053
         MVI   FLAGS,FGAUTH+FGMODE       YES - FLAG AUTHORIZED  GP99053
         MODESET MODE=SUP,KEY=ZERO   GET INTO SUPERVISOR MODE   GP99056
CALLBAD  SR    R1,R1                                            GP99056
         ICM   R1,7,RBLINK+1  GET CALLER'S RB                   GP99056
         IC    KEYREG,RBOPSW+1-RBSECT(,R1)                      GP99053
         POP   USING                                            GP99053
         SPACE 2                                                GP99053
COMMCODE DS    0H            BEGIN JOINT SVC AND CALL CODE      GP99053
         AIF   (&SVS).OS360                                      82109
         BAS   R14,LOCK                 GET LOCAL LOCK, USER KEY 90308
         AGO   .MAIN                                             80135
.VS1     MNOTE 12,'USE SYSTEM SERVICES TO ACCESS SWA, SWADS, ETC.'
.OS360   ANOP  ,
         L     MESSIAH,CVTJOB           GET QUEUE DCB
         USING HANDEL,MESSIAH
         ST    MESSIAH,IOBDCBPT
         LA    R1,ECB
         ST    R1,IOBECB
         LA    R1,CCW1
         TM    QMRPS,X'10'              IS QUEUE ON RPS DEVICE?
         BZ    *+8                       NO
         LA    R1,CCW0
         ST    R1,IOBSTART
         LA    R1,SECT                                           77105
         ST    R1,CCW0                                           77105
         LA    R1,IOBSEEK+3
         ST    R1,CCW1
         LA    R1,CCW1
         ST    R1,CCW2
         LA    R1,INFMJFCB
         ST    R1,CCW3
         OC    CCW0(MVC+8-CCW0),MODEL
         KEY   USER          ASSUME USER KEY FOR PARM CHECKING   81166
         SPACE 1                                                 81166
.MAIN    STITL '        MAIN LOGIC'                              80137
         USING TIOT,TIOTREG
         BASR  TIOTREG,0                CAUSE S1F0 IF 1ST DDNAME BLANK
FINDIT   TM    0(PARMREG),X'7E'         MUST BE DDNAME OR NOP
         BNZ   TREF                      OR ELSE
         CLI   0(PARMREG),1             IS IT DDNAME-NOT-LAST?
         BE    LOOKUP
SKIPIT   TM    0(PARMREG),X'80'          NO - THEN IS IT LAST?
         LA    PARMREG,4(,PARMREG)                               82116
         BZ    FINDIT                    NO - TRY NEXT ONE
         SPACE 2
         BAS   R2,CLEANUP               FREE WORK AREA           90308
         DC    H'0'                    TELL USER WE MADE IT      80137
         SPACE 2                                                 80140
FINDNEW  SLR   R1,R1                    NEXT TIOT ENTRY          80140
         IC    R1,TIOELNGH                                       80140
         AR    TIOTREG,R1                                        80140
         CLI   0(PARMREG),1             IS THIS DDNAME-NOT-LAST? 80140
         BNE   SKIPIT                    NO - IGNORE IT          80140
*        B     LOOKUP                                            80140
         SPACE 1
LOOKUP   SR    R1,R1
         ICM   R1,7,1(PARMREG)     LOAD AND CLEAN 24-BIT DDN PTR 90276
         CLC   =CL8' ',0(R1)            IS IT CONCATENATION?
         BE    FOUNDIT
         L     TIOTREG,TCBTIO
ISIT     CLC   0(8,R1),TIOEDDNM         IS IT RIGHT ENTRY?
         BE    FOUNDIT
         SLR   R0,R0                    TRY NEXT ENTRY
         IC    R0,TIOELNGH
         AR    TIOTREG,R0
         LTR   R0,R0
         BNZ   ISIT
         SPACE 1
S1F0     B     TREF                     CHANGE TO MAKE GP HAPPY
*        ABEND (1),DUMP
         SPACE 1
FOUNDIT  CLC   0(8,R1),TIOEDDNM         IS IT CONCATENATION OVER-RUN?
         BNE   S1F0
TESTIT   CLI   4(PARMREG),1
         BE    SKIPIT
         CLI   4(PARMREG),X'81'
         BE    SKIPIT
         SPACE 2
         USING UCBOB,UCBREG  (TAKE NO CHANCES WITH PREFIX)       92282
         SR    UCBREG,UCBREG
         ICM   UCBREG,7,TIOEFSRT  LOAD AND TEST UCB
         BZ    TREF          NONE
         EJECT
         AIF   (&MVS).MVSRSWA                                    82116
*              CONVERT JFCB ADDRESS FROM TTR TO MBBCCHHR.
*              REGISTERS 9 THROUGH 13 ARE DESTROYED AT THIS POINT,
         KEY   ZERO                                              81166
         STM   PARAMREG,R13,TEMPSAVE    BUT WHO CARES?          GP99056
         L     R0,TIOEJFCB
         LA    R1,QMRDEB
         LA    R2,IOBSEEK
         L     R15,CVTPCNVT
         BALR  R14,R15
         LM    PARAMREG,R13,TEMPSAVE    PUT EM BACK, THE WAY THEY WUZ
         SPACE 1
         TM    QMRPS,X'10'              IS QUEUE ON RPS DEVICE?
         BZ    NOTRPS                    NO
         L     R0,TIOEJFCB              COMPUTE DDIR FOR SECTOR CONVERT
         L     PARMREG,CVT0SCR1
         LR    CVTREG,MESSIAH
         L     R15,4(PARMREG)
         BALR  R14,R15
         LM    PARAMREG+1,R13,TEMPSAVE+4   PUT ALMOST ALL BACK   81166
         L     R0,TIOEJFCB
         LR    R15,PARMREG
         LA    R2,SECT                  SET HIGH BYTE TO UCBTYPE+3
         ICM   R2,8,QMRPS+1-HANDEL(CVTREG)
         BALR  R14,R15
         L     CVTREG,CVTPTR                                     81166
         LM    PARMREG,R13,TEMPSAVE                             GP99056
         SPACE 2
*              READ JFCB
NOTRPS   MVI   CCW3,RD
         MVI   IOBFLAG1,X'40'   SET CMD CHAIN/RESET RESTART BIT  81166
         EXCP  IOB
         WAIT  ECB=ECB
         CLI   ECB,X'7F'                HOW DID IT GO?
         BE    NEXT                     OK
S2F0     L     R1,=X'802F0000'          TERRIBLE
         ABEND (1),DUMP
         AGO   .CHANGE                                           81164
.MVSRSWA BAS   R2,GET@JFCB   GET ADDRESS OF JFCB                 90308
         MVC   INFMJFCB(176),0(R1)     MOVE JFCB FROM SWA        90276
         B     NEXT                     PROCESS NEXT PARAMETER   82315
.CHANGE  SPACE 2                                                 80137
*              CHANGE JFCB
CHANGLUP KEY   USER                     USER KEY                 80140
         IC    R1,0(,PARMREG)
         N     R1,=F'63'                IGNORE LAST AND ERROR BITS
         SLL   R1,2                                              83030
         CH    R1,=Y(OPEND-OPTYPES)     IS TYPE KOSHER ?         80137
         BNL   TREF                                              80137
         LH    R14,OPTYPES(R1)          BRANCH OFFSET            80137
         SR    R15,R15
         ICM   R15,7,1(PARMREG)      GET 24-BIT OPERAND ADDRESS  90276
         LH    R1,OPTYPES+2(R1)         OPERAND END              80137
         IC    R0,0(,R15)               VALID OPERAND LHE ?      80137
         IC    R0,0(R1,R15)             VALID OPERAND RHE ?      80137
         KEY   0                        LOCAL LOCK HELD IN MVS   80137
         B     0(R14,BASE)              WHEE !                   80137
         SPACE 2
         AIF   (NOT &MVS).NMVSBS1                                82116
         MOTE  0,'MVS NEEDS SYSZVOLS FIX'                        81164
.NMVSBS1 ANOP  ,                                                 81164
VOLSERS  CLC   JFCBVOLS,0(R15)
         BE    NEXT
         BAS   R14,PRIVATE                                       90308
         MVC   JFCBVOLS,0(R15)
         OI    JFCBIND1,JFCVSL
         B     NEXT
         SPACE 1
LABELTYP CLC   JFCBLTYP,0(R15)
         BE    NEXT
         BAS   R14,NOTAPEOP                                      90308
         AIF   ('&SYSTEM' EQ 'MFT' OR '&SYSTEM' EQ 'MVT').NOAUTH 80137
         TM    0(R15),JFCBLP            DOES USER WANT BLP ?     80137
         BZ    *+12                      NO  - NO TURKEY         80137
         TM    FLAGS,FGAUTH             IS THIS TURKEY APF AUTH? 80140
         BZ    TREF                      NO  - GOBBLE, GOBBLE    80137
.NOAUTH  MVC   JFCBLTYP,0(R15)                                   80137
         B     NEXT
         SPACE 1
         AIF   ('&SYSTEM' EQ 'MFT' OR '&SYSTEM' EQ 'MVT').SAVEOS 80137
SAVEDSN  KEY   USER                                              80137
         MVC   0(44,R15),JFCBDSNM       RETURN DSN TO USER       80137
         AGO   .SAVECOM                                          80137
.SAVEOS  ANOP  ,                                                 80137
SAVEDSN  LR    R1,R15                   TO VALIDITY CHECK THE RETURN,
         SYNCH MVC                       MVC DSN UNDER THE USER'S KEY
.SAVECOM B     NEXT                                              80137
         SPACE 2
RECFM    MVC   JFCRECFM,0(R15)
         B     NEXT
         SPACE 1
LRECL    MVC   JFCLRECL,0(R15)
         B     NEXT
         SPACE 1
CONDBLKS XC    JFCBLKSI,JFCBLKSI        CLEAR PREVIOUS BLKSIZE
         TM    JFCBLTYP,JFCBLP+JFCNL    IS LABEL BLP OR NL?      80137
         BZ    NEXT                      NO - DON'T PROCESS THIS ENTRY
*        B     BLKSIZE
         SPACE 1
BLKSIZE  OC    0(2,R15),0(R15)          IS THIS RESET REQUEST?
         BZ    *+14                      RESET - IGNORE JFCB BLKSIZE
         OC    JFCBLKSI,JFCBLKSI        WAS BLKSIZE ALREADY GIVEN?
         BNZ   NEXT                      YES - HONOR IT
         MVC   JFCBLKSI,0(R15)
         B     NEXT
         SPACE 1
         AIF   (NOT &MVS).NMVSBS2                                82116
         MOTE  0,'MVS NEEDS SYSDSN FIX, ETC.'                    81164
.NMVSBS2 ANOP  ,                                                 81164
DSNAME   MVC   JFCBDSNM,0(R15)
         B     NEXT
         SPACE 1
VOLSEQ   CLC   JFCBVLSQ,0(R15)
         BE    NEXT
         BAS   R14,NOTAPEOP                                      90308
         MVC   JFCBVLSQ,0(R15)
         B     NEXT
         SPACE 1
MEMBER   CLI   JFCDSRG2,0    SOMETHING I DON'T KNOW ABOUT ?      88329
         BNE   NEXT          YES; DON'T HANDLE (IGNORE)          88329
         TM    JFCDSRG1,255-(JFCORGPO+JFCORGU)  NON-PDS ?        88329
         BNZ   NEXT          YES; DON'T HANDLE (IGNORE)          88329
         TM    JFCBIND1,JFCGDG  GDG NAME PRESENT ?               88329
         BNZ   NEXT          YES; DON'T CLOBBER (IGNORE)         88329
         MVC   JFCBELNM,0(R15)                                   88329
*              IN CASE SAME DD USED FOR DIRECTORY & MEMBER       82116
*              THIS CODE DOES NOT PURPORT TO HANDLE GDG MEMBERS  82259
*        SEE ADDITION ABOVE - GENERATION REQUEST NO LONGER CLOBBERED
         MVI   JFCRECFM,0
         XC    JFCBLKSI(4),JFCBLKSI     BLKSIZE,LRECL=0
         NI    JFCBIND1,255-JFCPDS      CLEAR MEMBER FLAG IF BLANK NAME
         TM    JFCBELNM,255-C' '        ZERO/BLANK ?             82187
         BZ    NEXT                      YES - ASSUME NULL NAME  82187
         OI    JFCBIND1,JFCPDS          MEMBER NAME SPECIFD ON DD CARD
         B     NEXT
         SPACE 1
VOLSER   CLC   JFCBVOLS(6),0(R15)
         BE    NEXT
         BAS   R14,PRIVATE                                       90308
         AIF   (NOT &MVS).NMVSBS3                                82116
         MOTE  0,'MVS NEEDS SYSZVOLS FIX'                        81164
.NMVSBS3 ANOP  ,                                                 81164
         MVC   JFCBVOLS(6),0(R15)
         OI    JFCBTSDM,JFCVSL          VOL-SER CHANGED
         B     NEXT
         SPACE 1                                                 81166
DISPMOD  TM    JFCBIND1,JFCPDS   PDS MEMBER NAME PRESENT ?       81166
         BNZ   NEXT          YES; SKIP                           81166
         TM    JFCBIND2,JFCNEW  DISP = NEW ?                     81166
         BO    DISPSET       YES; CHECK A BIT MORE               81166
         TM    JFCBIND2,JFCMOD  ALREADY SET TO MOD ?             81166
         BNZ   NEXT          YES; BYPASS                         81166
DISPSET  CLI   JFCDSRG2,0    ANY FUNNY DSORG ?                   81166
         BNE   NEXT          YES; IGNORE                         81166
         TM    JFCDSRG1,255-JFCORGPS-JFCORGU  OTHER FUNNY ?      81166
         BNZ   NEXT          YES; IGNORE                         81166
         NI    JFCBIND2,255-JFCNEW  RESET                        81166
         OI    JFCBIND2,JFCMOD  SET MOD                          81166
         B     NEXT                                              81166
         SPACE 1                                                 84274
DISPOLD  NI    JFCBIND2,255-JFCNEW  RESET NEW AND MOD            84274
         OI    JFCBIND2,JFCOLD      SET OLD                      84274
         B     NEXT                                              84274
         SPACE 1
FILESEQ  CLC   JFCBFLSQ,0(R15)
         BE    NEXT
         BAS   R14,NOTAPEOP                                      90308
         MVC   JFCBFLSQ,0(R15)
*        B     NEXT
         SPACE 1
NEXT     KEY   USER                                              80137
         TM    0(PARMREG),X'80'
         BO    REWRITE
         LA    PARMREG,4(,PARMREG)                               82187
         B     CHANGLUP
         SPACE 2
*              TEST FOR REMOVABLE
PRIVATE  CLI   UCBTBYT3,UCB3TAPE                                 81164
         BE    NOTAPEOP                                          81164
         CLI   UCBTBYT3,UCB3DACC                                 81164
         BNE   TREF                                              81164
         TM    SRTESTAT,SRTERESV+SRTEPRES+SRTESYSR
         BNZ   TREF
         TM    SRTESTAB,SRTEBSVL        SHARED VOLUME?
         BO    TREF                      TOO BAD - SVC IS SELFISH
         TM    SRTESTAB,SRTEBPRV        IS IT PRIVATE?
         BZ    TREF                      NO - COMPLAIN IN PUBLIC
         CLI   SRTEDMCT,0               IS IT UNOPENED AND UNMOUNTED?
         BER   R14                       YES - MUST BE KOSHER
         B     TREF                      NO  - THEN ITS ALSO UNWANTED
         SPACE 1
NOTAPEOP CLI   UCBTBYT3,UCB3DACC                                 81164
         BER   R14                                               81164
         CLI   UCBTBYT3,UCB3TAPE                                 81164
         BNE   TREF                                              81164
         TM    SRTEDMCT,X'7F'
         BZR   R14
*        B     TREF
         SPACE 2
TREF     BAS   R2,CLEANUP               FREE WORK AREA           90308
         DC    H'4'                     USER BLEW IT             80137
         SPACE 2
         AIF   (&MVS).UNLOCK                                     82116
CLEANUP  KEY   0                        FOR FREEMAIN             80140
         AGO   .FREE                                             80140
.UNLOCK  ANOP  ,                                                 80140
CLEANUP  BAS   R14,UNLOCK               FOR FREEMAIN             90308
.FREE    TM    FLAGS,FGMODE  DID WE ISSUE MODESET?              GP99056
         BZ    FREEMODE      NO                                 GP99056
         MODESET KEY=NZERO,MODE=PROB                            GP99056
FREEMODE LA    R0,ENDWORK-WORK          FREE WORK AREA          GP99056
         TM    FLAGS,FGSVC   ENTERED AS SVC?                    GP99053
         BZ    FREEMAIN                                         GP99053
         SR    R13,R13       SET SVC FLAG                       GP99053
FREEMAIN STORAGE RELEASE,LENGTH=(0),ADDR=(WORKBASE)             GP00011
         LR    R1,PARMREG              TELL USER WHERE TO GET OFF80137
         LH    R15,0(,R2)               SET RETURN COLD          80140
         LTR   R13,R13       SVC ENTRY?                         GP99053
         BNZ   RETCALL       NO                                 GP99053
         SVC 3                          SARTRE                   80140
         SPACE 1                                                GP99053
RETCALL  L     R14,12(,R13)  RESTORE RETURN                     GP99053
         LM    R2,R12,28(R13)  RESTORE ALL BUT R15-R1           GP99053
         BSM   0,R14         RETURN IN CALLER'S AMODE           GP99053
         SPACE 2                                                GP99053
         AIF   (&MVS).MVSWSWA                                    82116
*              REWRITE JFCB
REWRITE  KEY   ZERO                                              81166
         MVI   CCW3,WD                                           81166
         MVI   IOBFLAG1,X'40'   SET CMD CHAIN/RESET RESTART BIT  81166
         EXCP  IOB
         WAIT  ECB=ECB
         CLI   ECB,X'7F'                HOW DID IT GO?
         BNE   S2F0                      TERRRIBLE
         KEY   USER                                              81166
         B     FINDNEW                  NEXT TIOT ENTRY - CONCAT 80140
         EJECT ,                                                 80136
MODEL    CCW   SETC,0,CC,1
         CCW   SID,0,CC,5
         CCW   TIC,0,CC,1
         CCW   RD,0,0,176
TIC      EQU   X'08'                    TRANSFER IN CHANNEL
SETC     EQU   X'23'
SID      EQU   X'31'                    SEARCH ID EQUAL
RD       EQU   X'06'                    READ DATA
WD       EQU   X'05'                    WRITE DATA
CC       EQU   X'40'                    COMMAND CHAINING FLAG
         SPACE 2
         MVC   0(44,R1),JFCBDSNM        MODEL OF EXECUTED        80137
         SVC   3
         AGO   .OPTYPES                                          80137
.MVSWSWA SPACE 2                                                 80136
         PUSH  USING                                            GP99053
         DROP  CVTREG                                           GP99053
*              REWRITE JFCB                                      80136
REWRITE  BAS   R2,GET@JFCB   GET ADDRESS OF JFCB                 90308
         MVC   0(176,R1),INFMJFCB      MOVE JFCB TO SWA          90276
         AIF   (NOT &MVSXA).NOSVA0                               90294
         BAS   R14,UNLOCK    RELEASE LOCAL LOCK
*TEST*   TM    TIOEJFCB+2,1  SWA OR SVA ADDRESS ?                92279
*TEST*   BZ    REWRSVA       SVA; SKIP SWA UPDATE                92279
         STM   R3,R13,TEMPSAVE  PRESERVE VITAL REGISTERS        GP02252
         LA    R13,FAKEOSAV  MAKE FAKE OS SAVE AREA             GP02252
         MVC   LISTSWA+8(2),=C'WL'  SET WRITE LOCATE             92281
         SWAREQ EPA=LISTEPA,MF=(E,LISTSWA)  'REWRITE'           GP02252
         LR    R2,R15        SAVE RETURN CODE                   GP02252
         LM    R3,R13,TEMPSAVE  RESTORE VITAL REGISTERS         GP02252
         LTR   R15,R2        TEST RETURN                        GP02252
         BNZ   S2F0          OOPS                               GP02252
         BAS   R14,LOCK      GET LOCAL LOCK BACK                GP02252
.NOSVA0  ANOP  ,                                                 92279
REWRSVA  KEY   USER                                              82315
         B     FINDNEW                  NEXT TIOT ENTRY - CONCAT 80140
         POP   USING                                            GP99053
         SPACE 1                                                 90276
         PUSH  USING                                            GP99053
         DROP  CVTREG                                           GP99053
*        SUBROUTINE TO OBTAIN JFCB SWA ADDRESS FROM SVA          90276
*                                                                90276
GET@JFCB KEY   ZERO          GET PRIVILEGED                      90276
         SLR   R1,R1                                             82116
         ICM   R1,B'0111',TIOEJFCB     GET SVA ADDRESS OF JFCB   90276
         AIF   (NOT &MVSXA).NOSVA1  PRE-SP 2 ?                   90276
*FILL    TM    TIOEJFCB+2,1 SVA ADDRESS OR SWA TOKEN ?           92279
*EPA     BZ    GET@JSVA     ADDRESS                              92279
         XC    LISTSWA(LISTSWAL),LISTSWA  CLEAR IT               90276
         STCM  R1,7,LISTSVA+4  PLACE IN SECOND WORD OF LIST      90276
         LA    R1,LISTSVA    GET SVA ADDRESS                     90276
         ST    R1,LISTEPA    STORE IN POINTER                    90276
         LA    R1,LISTSWA+8                                      90276
         ST    R1,LISTSWA+4  POINTER TO FUNCTION                 90276
         MVC   LISTSWA+8(2),=C'RL'  SET FUNCTION                 92281
         STM   R3,R13,TEMPSAVE  PRESERVE VITAL REGISTERS        GP02252
         LA    R13,FAKEOSAV  MAKE FAKE OS SAVE AREA             GP02252
         SWAREQ EPA=LISTEPA,MF=(E,LISTSWA),UNAUTH=YES           GP02252
         LM    R3,R13,TEMPSAVE  RESTORE VITAL REGISTERS         GP02252
         BXLE  R15,R15,N2F0  UNEXPECTED FAILURE                  92272
S2F0     L     R1,=X'802F0000'          TERRIBLE                 92272
         ABEND (1)                                               92272
N2F0     L     R1,LISTSVA    GET STORAGE ADDRESS OF JFCB         92272
         BR    R2            RETURN TO CALLER                    92279
.NOSVA1  DS    0H            HAVE SVA                            92279
GET@JSVA LA    R1,16(,R1)    SPACE TO JFCB PROPER                90276
.GOSVA1  BR    R2            RETURN TO CALLER                    90276
         POP   USING                                            GP99053
         SPACE 2                                                 80140
LOCK     STM   R11,R14,TEMPSAVE         SAVE ACROSS SETLOCK     GP99056
LOCKO  SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                    80137 *
               RELATED=(LOCKR,'FREE LOCAL LOCK')                 80137
         LTR   R15,R13                                           80136
         LM    R11,R14,TEMPSAVE                                 GP99056
         KEY   USER                     USER KEY                 80140
         BR    R14                                               80136
         SPACE 2                                                 80136
UNLOCK   KEY   ZERO                                              82116
         STM   R11,R14,TEMPSAVE         SAVE ACROSS SETLOCK     GP99056
LOCKR  SETLOCK RELEASE,TYPE=LOCAL,                               80137 *
               RELATED=(LOCKO,'OBTAIN LOCAL LOCK')               80137
         LTR   R15,R13                                           80136
         LM    R11,R14,TEMPSAVE                                 GP99056
         BR    R14                                               80136
.OPTYPES SPACE 2                                                 80137
OPTYPES  OPTYP NEXT,0        00         NOP                      80137
         OPTYP REWRITE,8     01         DDNAME                   80137
         OPTYP FILESEQ,2     02         FILE SEQUENCE NUMBER     80137
         OPTYP VOLSER,6      03         VOLUME SERIAL NUMBER     80137
         OPTYP MEMBER,8      04         MEMBER NAME              80137
         OPTYP VOLSEQ,2      05         VOLUME SEQUENCE NUMBER   80137
         OPTYP DSNAME,44     06         DSNAME                   80137
         OPTYP BLKSIZE,1     07         BLKSIZE                  80137
         OPTYP CONDBLKS,2    08         CONDITIONAL BLOCKSIZE    80137
         OPTYP LRECL,2       09         LRECL                    80137
         OPTYP RECFM,2       0A         RECFM                    80137
         OPTYP SAVEDSN,44    0B         RETURN DSNAME            80137
         OPTYP LABELTYP,1    0C         LABEL TYPE               80137
         OPTYP VOLSERS,30    0D         VOLUME SERIAL NUM S (5)  80137
         OPTYP DISPMOD,0     0E         SET DISP=MOD; NO PARM    81166
         OPTYP DISPOLD,0     0F         SET DISP=OLD; NO PARM    84274
OPEND    EQU   *                                                 80137
         SPACE 1                                                 81166
.WORK    STITL 'TSM WORK AREA INCLUDES IOB, ECB, AND JFCB'       80137
         AIF   (NOT &MVSXA).NO505                               GP02252
         IEFZB505 LOCEPAX=YES                                   GP02252
ZB505SIZ EQU   *-ZB505                                          GP02252
.NO505   SPACE 1                                                GP02252
         PRINT &PRTSYS                                           92282
WORK     DSECT ,
FLAGS    DS    X                                                 80135
FGAUTH   EQU   X'80'         APF OR SYSTEM KEY                   80137
LOCKED   EQU   X'40'         LOCAL LOCK                          80137
USERKEY  EQU   X'20'         MODJFCB IN KEY 8                    80137
FGMODE   EQU   X'02'         SVCMJFCB ISSUED MODESET            GP99056
FGSVC    EQU   X'01'         CODE WAS ENTERED AS SVC
         AIF   (&MVS).NOIOB                                      82116
SECT     DS    X
         SPACE 1
ECB      DS    F
         SPACE 1
IOB      DS    0D
IOBFLAG1 DS    X
IOBFLAG2 DS    X
IOBSENS0 DS    X
IOBSENS1 DS    X
IOBECBCC DS    0X
IOBECB   DS    F
IOBCSWCM DS    0A                       LAST CCW ADDRESS
IOBFLAG3 DS    X
         DS    AL3
IOBCSWST DS    XL2                      CSW STATUS
IOBCSWCT DS    H                        CSW RESIDUAL COUNT
IOBSIOCC DS    0X
IOBSTART DS    A
IOBDCBPT DS    A
IOBRESTR DS    A
IOBINCAM DS    H
IOBERRCT DS    H
IOBSEEK  DS    0D
         DS    X                        M
         DS    XL2                      BB
         DS    XL2                      CC
         DS    XL2                      HH
         DS    X                        R
         SPACE 2
CCW0     DS    D                        SET SECTOR
CCW1     DS    D                        SEARCH ID EQUAL
CCW2     DS    D                        TIC   *-8
CCW3     DS    D                        READ/WRITE DATA
         SPACE 3
MVC      MVC   0(44,R1),JFCBDSNM        EXECUTED                 80137
         SVC   3
         EJECT
.NOIOB   ANOP  ,                                                 80135
JFCB     DS    0F
      IEFJFCBN
         SPACE 1
         AIF   (NOT &MVSXA).NOSVA2                               90276
LISTSWA  SWAREQ FCODE=RL,EPA=LISTEPA,MF=L                        90276
LISTEPA  DS    A(LISTSVA)                                        90276
LISTSVA  DS    (ZB505SIZ)X   MAKE SURE IT'S LONG ENOUGH         GP02252
LISTSWAL EQU   *-LISTSWA     LENGTH TO CLEAR                     90276
FAKEOSAV DS    18F           FAKE OS SAVE AREA FL               GP02252
.NOSVA2  SPACE 1                                                 90276
TEMPSAVE DS    16F           REPLACE RBEXSAVE FOR SYNCH ENTRY   GP99056
ENDWORK  EQU   *
         AIF   (NOT &MVSXA).NOSVA3                               92272
         SPACE 1                                                 92272
         IEFJESCT ,                                              92272
.NOSVA3  AIF   (NOT &MVS).NOPSA                                  82116
         STITL 'PREFIX STORAGE AREA'                             80140
        IHAPSA DSECT=YES                                         80136
.NOPSA   STITL 'COMMUNICATIONS VECTOR TABLE'                     80137
         CVT   PREFIX=YES,DSECT=YES
         STITL 'TASK CONTROL BLOCK AND REQUEST BLOCK'            80137
        IKJTCB
         IKJRB
         STITL 'TASK I/O TABLE'                                  80137
TIOT     DSECT
      IEFTIOT1
         AIF   (&MVS).NOHANDL                                    82116
         STITL 'QUEUE MANAGER DCB AND MR. HANDEL'                80137
HANDEL   DSECT
      IEFQMRES
.NOHANDL STITL 'UNIT CONTROL BLOCK'                              80137
SRT      DSECT
      IEFUCBOB
         END
