FMT3     TITLE 'DSCB3'
         PRINT NOGEN
         LCLA  &N
         LCLA  &M
         LCLA  &DS3LINE
         LCLC  &NN
 LCLA &L
DSCB3    CSECT
         SAVE  (14,12),T,*
         LR    12,15
         USING DSCB3,12
         LR    11,13
         LA    13,D3SAVE
         ST    11,D3SAVE+4
         ST    13,8(11)
         LM    10,11,0(1)
         USING DS3ADDR,10
         USING FMT3,11
         MVC   DSCB3FMT,DS3FMTID
         SPACE 2
*        THE FOLLOWING MOVE MAKES ALL EXTENT ENTRIES CONTIGUOUS BY
*        MOVING THE HIGHER ONES OVER THE DSCB FORMAT ID BYTE
*        THE DSECT MAPPING HAS BEEN ADJUSTED TO COMPENSATE FOR THIS
*
         MVC   DS3EXT8(DS3PTRDS-DS3EXT8-1),DS3EXT8+1   MAKE ENTRIES CON
         SPACE 2
*        THIS CODE PREVENTS PRINTING LINES FOR UNUSED EXTENTS
*
         LA    2,12          DO ALL BUT FIRST ENTRY
         LA    3,DS3EXT16    GET LAST EXTENT ENTRY ADDRESS
         L     4,DSCB3SZP    GET PATTERN FOR LINE/SKIP COUNT
ZLP      OC    0(10,3),0(3)  ENTRY ZERO ?
         BNZ   ZOUT          NO, STASH COUNT
         S     4,=XL4'10001'  MAKE 1 LINE LESS
         SH    3,=H'10'      SKIP TO PRECEEDING EXTENT
         BCT   2,ZLP         CHECK IT
ZOUT     ST    4,DSCB3SIZ    STASH UPDATED LINE COUNT
         CALL  COMPSIZE,(SIZETAB)
         CALL  PUTADDR,(CVHTAB3)
         CALL  CVD,(CVDTAB3)
         LA    2,13          LOOP FOR ALL 13 ENTRIES, NOW CONTIG
         LA    3,DS3EXT4
         LA    4,XTYPE4
         L     15,=V(SETXTYPE)
         BALR  14,15
         CALL  PRINT,(DSCB3OUT,DSCB3SIZ)
         L     13,D3SAVE+4
         RETURN (14,12),T
D3SAVE   DS    18F
DSCB3SZP DC    H'14,15'      MAX COUNTS, ON WORD BOUNDARY
DSCB3SIZ DC    H'14,15'      ADJUSTED LINE COUNTS
&N       SETA  4
SIZETAB  DS    0H
.SZLP    DC    S(DS3EXT&N+2),S(TRKSZ&N)
&N       SETA  &N+1
         AIF   (&N LT 17).SZLP
         DC    H'0'
CVHTAB3  DC    S(DS3ADDR),H'5',S(DSCB3ADR),H'12'
         DC    S(DS3PTRDS),H'5',S(DSCB3EXT),H'12'
         DC    H'0'
&N       SETA  04
CVDTAB3  DS    0H
.CVDL3   DC    S(DS3EXT&N+1),H'1',S(SEQNUM&N),H'3'
         DC    S(DS3EXT&N+2),H'2',S(LOWCYL&N),H'3'
         DC    S(DS3EXT&N+4),H'2',S(LOWTRK&N),H'3'
         DC    S(DS3EXT&N+6),H'2',S(UPCYL&N),H'3'
         DC    S(DS3EXT&N+8),H'2',S(UPTRK&N),H'3'
&N       SETA  &N+1
         AIF   (&N LT 17).CVDL3
         DC    H'0'
DSCB3OUT DC    14CL132' '
         ORG   DSCB3OUT
         DC    C'0'
DSCB3ADR DC    CL12' '
         DC    C'  FMT='
DSCB3FMT DC    C' '
         DC    CL83' '
         DC    C'DSCB EXTENDED AT '
DSCB3EXT DC    CL12' '
&DS3LINE SETA  1
&N       SETA  04
.DS3LP   ORG   DSCB3OUT+&DS3LINE*132+4
&L       SETA  1
         AIF   (&N LT 10).NOT3
&L       SETA  2
.NOT3    ANOP
&NN      SETC  '0'.'&N'
&NN      SETC  '&NN'(&L,2)
         DC    C'  EXTENT &NN='
XTYPE&N  DC    CL20' '
         DC    C'  SEQ NUM='
SEQNUM&N DC    CL3' '
         DC    C'  LOWER BOUND=(CYL='
LOWCYL&N DC    CL3' '
         DC    C',TRK='
LOWTRK&N DC    CL3' '
         DC    C')  UPPER BOUND=(CYL='
UPCYL&N  DC    CL3' '
         DC    C',TRK='
UPTRK&N  DC    CL3' '
         DC    C')'
         DC    C'  SIZE='
TRKSZ&N  DC    CL4' '
         DC    C' TRACKS'
&N       SETA  &N+1
&DS3LINE SETA  &DS3LINE+1
         AIF   (&N LT 17).DS3LP
         ORG
         LTORG
         EJECT
FMT3     DSECT
DS3KEYID DC    4X'03'
&M       SETA  04
.LPA     ANOP
DS3EXT&M DC    CL10' '
&M       SETA  &M+1
         AIF   (&M LT 8).LPA
DS3FMTID DC    0CL1' '       FAKE FORMAT ID FIELD, ELIMINATED BY MVC
.LPB     ANOP
DS3EXT&M DC    CL10' '
&M       SETA  &M+1
         AIF   (&M LT 17).LPB
         DS    C             FAKE SPACER, CREATED BY MVC
DS3PTRDS DC    CL5' '
DS3ADDR  DSECT
         END
