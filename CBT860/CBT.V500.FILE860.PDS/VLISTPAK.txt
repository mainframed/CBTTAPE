VPAK     TITLE 'V L I S T P A K  ***  COMPRESS VLIST OUTPUT'
         PRINT NOGEN         SAVE A TREE
*        THIS PROGRAM ACCEPTS THE VTAM PORTION OF AN NCP, AS GENERATED
*        BY THE NCR/COMTEN VLIST PRODUCT, AND COMPRESSES THE OUTPUT.
*        NO SPECIAL ERROR CHECKING OR RECOVERY ARE PERFORMED, AS THE
*        OUTPUT IS EXPECTED TO BE SYNTACTICALLY CORRECT.
*        SYSUT1: CARD IMAGE INPUT FILE (VLIST OUTPUT)
*        SYSUT2: CARD IMAGE OUTPUT FILE, COMPRESSED AND RENUMBERED.
*
*        ATTRIBUTES: QUICK AND DIRTY: NON-RENT, NON-REUS
*              G. POSTPISCHIL (703) 938-1928 (NOT MY NICEST WORK)
*
VLISTPAK START 0                                        ADDED ON 89341
         USING VLISTPAK,R12  DECLARE BASE
         STM   R14,R12,12(R13)  SAVE ENTRY REGISTERS
         LR    R12,R15       SET BASE REGISTER
         LA    R9,SAVE       TEMPORARY NEW SAVE AREA
         ST    R9,8(,R13)    SET LINK TO NEW AREA
         ST    R13,4(,R9)    SET BACK LINK
         LR    R13,R9        ACTIVATE NEW SAVE AREA
         SPACE 1
*        SYMBOLIC REGISTER DEFINITIONS (OR USE IBM'S REGEQU)
R0       EQU   0                       WORK
R1       EQU   1                       WORK
R2       EQU   2                       WORK
R3       EQU   3                       WORK
R4       EQU   4                       WORK
R5       EQU   5                       WORK
R6       EQU   6                       WORK
R7       EQU   7             SEQUENCE NUMBER OF PRIOR CARD
R8       EQU   8             POINTER TO NEXT AVAILABLE COLUMN
R9       EQU   9             RETURN REGISTER FOR SUBROUTINE CALLS
R10      EQU   10                      WORK
R11      EQU   11                      WORK
R12      EQU   12            BASE
R13      EQU   13            SAVE AREA
R14      EQU   14                      WORK
R15      EQU   15                      WORK
         SPACE 1
         SLR   R7,R7         COUNTER FOR NEW SEQUENCE NUMBERS
         OPEN  (SYSUT1,(INPUT))  OPEN INPUT BEFORE CREAMING THE OUTPUT
         TM    SYSUT1+DCBOFLGS-IHADCB,DCBOFOPN  DID IT OPEN ?
         BZ    NO1           NO
         OPEN  (SYSUT2,(OUTPUT,REREAD))
         TM    SYSUT2+DCBOFLGS-IHADCB,DCBOFOPN  DID IT OPEN ?
         BZ    NO2           NO
         SPACE 1
LOOP     GET   SYSUT1        PICK A CARD, ANY CARD...
         LR    R5,R1         SAVE THE INPUT RECORD ADDRESS
         USING BUFFER,R5     DECLARE IT
         TM    SEQFG,FGCON   NEW CARD EXPECTED ?
         BNZ   CONTCARD      NO; HANDLE EXPECTED CONTINUATION
         BAL   R9,FLUSHOUT   GET RID OF ANY STACKED CARD
         CLI   BUFFER,C'*'   COMMENTS CARD ?
         BE    PUTONLY       YES; ALSO LAST CARD OF CONTINUATION
         CLI   INCONT,C' '   IS CONTINUATION COLUMN BLANK ?
         BNE   NOTONLY       NO; STACK THIS CARD
         SPACE 1
PUTONLY  LR    R1,R5         COPY INPUT BUFFER
         BAL   R14,PUTCARD   WRITE CARD RESEQUENCED
         NI    SEQFG,255-FGCON  RESET CONTINUATION FLAG
         B     LOOP
         SPACE 1
NOTONLY  OI    SEQFG,FGCON+FGHOLD  HAVE STACKED CARD; NEED CONTINUATION
         MVC   HOLDTEXT,INTEXT  SAVE TEXT
         LA    R5,HOLDTEXT   FINAGLE FOR 'FINDNEXT'
         BAL   R14,FINDNEXT   FIND LAST USED COLUMN; RETURN NEXT
         LR    R8,R15        SAVE POINTER TO NEXT COLUMN
*        THIS CODE ASSUMES THAT EACH FIRST CARD HAS AT LEAST ONE
*        OPERAND IN OR AFTER COLUMN 16, AS OPPOSED TO A LONG SECOND
*        FIELD AND NO OPERAND.
         B     LOOP          GET ANOTHER CARD
         SPACE 1
CONTCARD CLC   =CL15' ',INTEXT  REALLY CONTINUATION CARD ?
         BNE   LOGICERR      NO; BOOBOO
         NI    SEQFG,255-FGCON  RESET CONTINUATION EXPECTED
         CLI   INTEXT+15,C' '  ANY OPERAND ?
         BE    CONTEND       SKIP IF NO OPERAND
*        NOTE THAT TEXT STRINGS (SPLIT) ARE NOT SUPPORTED,
*        NEITHER ARE (INDENTED) COMMENTS.
         BAL   R14,FINDNEXT  FIND NEXT BYTE
         LR    R11,R15       POSITION TO NEXT AVAILABLE
         LA    R10,INTEXT+15 POINT TO FIRST INPUT COLUMN
         SR    R11,R10       LENGTH OF TEXT TO BE MOVED
         BNP   CONTEND       HUH ?
CONTMORE LA    R3,HOLDCONT   POINT PAST OLD TEXT END
         LR    R2,R8         GET NEXT AVAILABLE POSITION
         SR    R3,R2         ANY ROOM ON THIS CARD ?
         BP    CONTMOVE      YES; FILL IT UNTIL IT BURSTS.
         MVI   HOLDCONT,C'*' SET FOR CONTINUATION FOLLOWING
         BAL   R9,FLUSHOUT   PROCESS STACKED CARD
         LA    R15,HOLDTEXT+15  POINT TO NEXT AVAILABLE POSITION
         LR    R8,R15        SET POINTER
         B     CONTMORE      MOVE REST OF INPUT
         SPACE 1
CONTMOVE CR    R3,R11        SEE IF INPUT IS SHORTER
         BNH   *+6           NO; MOVE ONLY WHAT FITS
         LR    R3,R11        ELSE MOVE ONLY AVAILABLE INPUT
         MVCL  R2,R10        MOVE WHATEVER FITS
         OI    SEQFG,FGHOLD  SHOW TEXT IN HOLD AREA
         LR    R8,R2         SAVE NEXT AVAILABLE POSITION
         LTR   R11,R11       ANY MORE TO DO ?
         BP    CONTMORE      YES; DO IT
CONTEND  CLI   INCONT,C' '   CONTINUATION EXPECTED ?
         BE    LOOP          NO
         OI    SEQFG,FGCON   SHOW CONTINUATION EXPECTED
         B     LOOP
         SPACE 2
EOF      BAL   R9,FLUSHOUT   WRITE ANY STACKED CARD
         CLOSE (SYSUT1,,       NOW CLOSE INPUT AND                     *
               SYSUT2,LEAVE)   OUTPUT FILES
         L     R13,4(,R13)   RELOAD OLD SAVE AREA
         LM    R14,R12,12(R13)  RESTORE OLD REGISTERS
         SLR   R15,R15       SET ZERO RETURN CODE
         BR    R14           RETURN TO CALLER
         SPACE 1
FLUSHOUT TM    SEQFG,FGHOLD  CARD IMAGE STACKED ?
         BZR   R9            NO; RETURN TO CALLER
         NI    SEQFG,255-FGHOLD  RESET IT
         LA    R1,HOLDCARD   POINT TO OUTPUT
         BAL   R14,PUTCARD   SEQUENCE AND WRITE CARD
         MVI   HOLDCARD,C' '
         MVC   HOLDCARD+1(L'HOLDCARD-1),HOLDCARD  BLANK IT
         BR    R9            RETURN TO CALLER
         SPACE 1
PUTCARD  LA    R7,1000(,R7)  INCREASE SEQUENCE BY (ARBITRARY) INCREMENT
         CVD   R7,DB         MAKE PACKED
         UNPK  WORK11,DB+3(6)  UNPACK ONE MORE THAN I HAVE
         MVC   HOLDSEQ-HOLDCARD(L'HOLDSEQ,R1),WORK11+1  MAKE SEQ.
         LR    R0,R1         PASS OUTPUT RECORD ADDRESS
         PUT   SYSUT2        WRITE CARD IMAGE
         ORG   *-2           CHEAT QUITE A BIT
         BR    R15           INVOKE PUT ROUTINE AND RETURN VIA R14
         SPACE 1
*        THIS SUBROUTINE EXPECTS THE CURRENT CARD ADDRESS IN R5,
*        AND RETURNS R15 POINTING TO THE FIRST BLANK OPERAND COLUMN
*        OR COLUMN 72, WHICHEVER IS LESS. THIS LOGIC REQUIRES THE
*        RETURNED ADDRESS TO BE TESTED PRIOR TO USE.
FINDNEXT LA    R15,INTEXT+L'INTEXT  SET LAST POSITION + ONE
         LA    R0,1
         LNR   R0,R0         SET INCREMENT=-1
         LA    R1,INTEXT+15-1  SET SCAN STOP
FINDNELP BXLE  R15,R0,FINDNEMP  NADA; SET TO CARD+15
         CLI   0(R15),C' '   TRAILING BLANK ?
         BE    FINDNELP      YES; KEEP SCANNING
FINDNEMP LA    R15,1(,R15)   SET TO NEXT AVAILABLE
         BR    R14           ELSE RETURN WITH NEXT
         SPACE 2
LOGICERR WTO   '** VLISTPAK ** CONTINUATION CARD ERROR',ROUTCDE=7
         ABEND 666           THIS IS A BEASTLY CONDITION
NO2      MVC   NO1+23(6),=C'SYSUT2'  (LONG FORM FOR EASIER MODS)
NO1      WTO   '** VLISTPAK ** SYSUT1 DD MISSING',ROUTCDE=7
         ABEND 16            GOODBYE, CRUEL WORLD
         SPACE 1
         DROP  ,
         USING OPENEXIT,R12
         USING IHADCB,R1
*        THIS EXIT VERIFIES THAT THE DCB PARAMETERS ARE ACCEPTABLE
*        (RECFM=F(B)(S), LRECL=80, BLKSIZE|80). DEFAULTS ARE SUPPLIED
*        IF NOT SPECIFIED BY USER OR OLD FILE (PREVENT 013 ON OUTPUT).
OPENEXIT LR    R12,R15       SET BASE IN LESS VOLATILE REGISTER
         LR    R9,R14        ALSO SAVE RETURN ADDRESS
         TM    DCBRECFM,DCBRECLA  ANY RECFM AT ALL
         BNZ   *+8           YES; SEE IF FLAKY
         OI    DCBRECFM,DCBRECF+DCBRECBR  SET RECFM=FB
*        NOTE THAT THIS FAILS TRACK OVERFLOW, AND DOES NOT FAIL
*        FBS, FBA, FBM, ETC.
         TM    DCBRECFM,DCBRECLA-DCBRECF  OTHER THAN FIXED ?
         BNZ   OPENBAD       YES; FAIL
         LA    R0,80         SET DESIRED LRECL
         CH    R0,DCBLRECL   MATCH ?
         BE    OPENRECL      YES
         CLM   R0,B'1100',DCBLRECL  LRECL=0 ?  (CHEAT)
         BNE   OPENBAD       NO; FAIL
         STH   R0,DCBLRECL   SET DEFAULT CARD FORMAT
OPENRECL SLR   R2,R2         PREPARE FOR DIVIDE
         SLR   R3,R3
         ICM   R3,3,DCBBLKSI  GET BLOCK SIZE
         BNZ   OPENBLKS      HAVE IT - TEST
*        NOTE THAT THE DCBDEVT AT THIS PHASE OF OPEN PROCESSING IS
*        NOT FILLED IN FOR ALL TYPES OF DATASET. IN PARTICULAR,
*        TAPE AND DISK ARE VALID, BUT JES2 ETC. MAY NOT BE.
         IC    R6,DCBDEVT    GET DEVICE TYPE
         LA    R4,DEFBLKSI   GET TABLE OF DEFAULT SIZES
         LA    R5,(DEFBLKND-DEFBLKSI)/(DEFBLK2-DEFBLKSI)
OPENTEST CLM   R6,1,0(R4)    MATCHING DEVICE ?
         BE    OPENHAVE      YES
         LA    R4,DEFBLK2-DEFBLKSI(,R4)  BUMP
         BCT   R5,OPENTEST   TRY AGAIN
OPENHAVE ICM   R3,3,1(R4)    LOAD APPROPRIATE SIZE
         STH   R3,DCBBLKSI   ALSO STASH IN DCB
OPENBLKS DR    R2,R0         CHECK DIVISIBILITY
         LTR   R2,R2         ANY REMAINDER ?
         BZR   R14           NO; CONTINUE OPEN PROCESSING
OPENBAD  WTO   '** VLISTPAK ** UNSUPPORTED DCB PARAMETERS',ROUTCDE=7
         ABEND 013           QUIT
         SPACE 1
*        ARBITRARY BLOCKSIZES BY DEVICE TYPE
*
DEFBLKSI DC    X'2E',AL2(6160)         3380
DEFBLK2  DC    X'2D',AL2(3120)         3330-1
         DC    X'2C',AL2(4080)         3375
         DC    X'2B',AL2(3600)         3350
         DC    X'2A',AL2(4080)         3340
         DC    X'29',AL2(3120)         3330
*OBSOLETE      X'28',AL2(3520)         2314
DEFBLKND DC    X'00',AL2(8000)         USE 8K FOR TAPE AND JES2
         SPACE 2
*        NON-REENTRANT, NON-REUSABLE, NON-REFRESHABLE WORK AREA
*
DB       DC    D'0'          WORK WORD
SAVE     DC    18F'0'
SEQFG    DC    X'00'         PROCESSING FLAG
FGCON    EQU   X'80'           CONTINUATION CARD EXPECTED
FGHOLD   EQU   X'01'           PARTIAL CARD IMAGE STACKED IN HOLDTEXT
WORK11   DC    XL11'0'       WORK AREA FOR SEQUENCE NUMBER
HOLDCARD DC    0CL80' '
HOLDTEXT DC    CL71' '       TEXT
HOLDCONT DC    C' '          CONTINUATION COLUMN
HOLDSEQ  DC    CL8' '        OUTPUT SEQUENCE NUMBER
SYSUT1   DCB   DDNAME=SYSUT1,DSORG=PS,EODAD=EOF,EROPT=ACC,MACRF=GL,    *
               EXLST=EXLIST
SYSUT2   DCB   DDNAME=SYSUT2,DSORG=PS,EROPT=ACC,MACRF=PM,              *
               EXLST=EXLIST
EXLIST   DC    0F'0',X'85',AL3(OPENEXIT)
         SPACE 1
BUFFER   DSECT ,             MAP OF INPUT CARD
INTEXT   DS    CL71          TEXT
INCONT   DS    C             CONTINUATION COLUMN
INSEQ    DS    CL8           SEQUENCE FIELD
         SPACE 1
         DCBD  DEVD=DA,DSORG=PS  MAP DATA CONTROL BLOCK
         END   ,
