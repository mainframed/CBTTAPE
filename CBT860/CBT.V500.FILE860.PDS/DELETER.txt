DELETER  TITLE 'D E L E T E R  ***  DELETE AND UNCATALOG PARM DSN'
 PUNCH ' ALIAS EXISTER,DEL0,DELETE0,DELETENP,EXISTNP '
         COPY  OPTIONGB                                          90364
         SPACE 1                                                 90364
         SYSPARM LIST=YES                                        90364
         SPACE 1                                                 90364
***********************************************************************
*                                                                     *
*    PROGRAM DELETER USES THE PARM FIELD TO OBTAIN THE NAME OF A      *
*    DATASET TO BE SCRATCHED (DELETED FROM DASD) AND UNCATALOGED      *
*    UNLESS CATALOGED AS RESIDING ELSEWHERE.                          *
*                                                                     *
*    A VOLUME SERIAL MAY BE SUPPLIED FOR DATASETS NOT CATALOGED.      *
*                                                                     *
*    SPECIFICATION OF A MEMBER NAME CAUSES THE PROGRAM TO SCRATCH     *
*    THAT MEMBER FROM THE SPECIFIED DATASET. THE DATASET IS NOT       *
*    DELETED OR UNCATALOGED.                                          *
*                                                                     *
*    MULTIPLE REQUESTS MAY BE STACKED, UP TO THE PARM LIMIT OF        *
*    100 BYTES, BY SEPARATING THEM WITH A SEMI-COLON.                 *
*                                                                     *
*    SAMPLE JCL:                                                      *
*                                                                     *
*    //JOBNAME JOB ....                                               *
*    /* OR //* AS PER YOUR INSTALLATION                               *
*    //EXEC PGM=DELETER,PARM='DSN1;DSN2,VOL2;DSN3(MEM)'               *
*    //SYSPRINT DD SYSOUT=*  OPTIONAL PRINT FILE                      *
*    //SYSUDUMP DD SYSOUT=*  OPTIONAL DEBUG FILE                      *
*                                                                     *
*    ENTRY POINT DEL0 WILL ALWAYS RETURN STEP CONDITION CODE 0        *
*                                                                     *
*    ENTRY POINT EXISTER MAY BE USED THE SAME WAY TO CONFIRM THE      *
*    EXISTENCE OF A MEMBER OR DATASET. A NON-ZERO RETURN CODE         *
*    INDICATES THE ABSENCE OF AT LEAST ONE REQUESTED ENTRY, OR        *
*    A BAD PARAMETER FIELD (SYNTAX).                                  *
*                                                                     *
*    ENTRY POINT DELETNP IS SPECIAL FOR KERMIT; NO DEBUG, NO PRINT    *
*    ENTRY POINT EXISTNP IS SPECIAL FOR @INPREAD; NO DEBUG, NO PRINT  *
*                                                                     *
*    DELETER IS RE-ENTRANT, REFRESHABLE AND RE-USABLE, AND MUST       *
*    NOT BE LINKED AUTHORIZED (TO PREVENT UNAUTHORIZED DELETIONS)     *
*                                                                     *
*    THE CODE WAS COPIED, MERGED, CONSOLIDATED AND WHAT-NOT FROM      *
*    PROGRAMS CATCLEAN (AUTHOR J. UNGER, TECHNION) AND WYLCLEAN.      *
*    THE PROGRAM IS IN THE PUBLIC DOMAIN.                             *
*                                                                     *
*    CODE ADDED TO CHECK VOLUME SERIAL FOR MIGRAT. FOR EXISTER,       *
*    SAY IT DOES. FOR DELETE, SEND SYNCHRONOUS REQUEST TO HSM.        *
*                                                                     *
***********************************************************************
*  MAINTENANCE:                                                       *
* 2005-06-11 GYP ADDED CODE TO ALLOW TSO CP FORMAT PARM FIELD         *
*                                                                     *
***********************************************************************
         EJECT ,                                                 90364
         PRINT &PRTSOR                                           90364
DELETER  PGMHEAD ZERO12,BASE=(R11,R12),PARM=R9,BNDRY=PAGE,ENTNO=FLAGS, *
               ENTRY=(EXISTER,DEL0,DELETE0,DELETNP,EXISTNP),EID=NONE,  *
               LOC=BELOW,AM=31,RM=24 (DCB ADDRESSES)            GP02235
      TR  FLAGS,=AL1(0,FGEXIST,FGZERO,FGZERO,FGMULTI,FGEXIST+FGMULTI)
         OI    FLAGS,FGSCR+FGUNC  SET BASIC DELETE               91328
         SPACE 1                                                 90364
         SERVINIT ,                                              90364
         SERVCALL LPALD,=CL8'@PRINTER'                           90364
         ST    R0,@PRINTER                                       90364
         LA    R1,BPAMDCB    MAKE OPEN LIST                      90364
         ST@   R1,@BPAMDCB,MVI=X'8F'  SET OPEN OUTPUT            90364
         TM    FLAGS,FGEXIST VERIFY ?                            91328
         BZ    *+8           NO                                  91328
         NI    @BPAMDCB,X'80'  OPEN INPUT                        93306
         LM    R14,R1,PATUNCAT                                   90364
         ALR   R15,R13       RELOCATE TRUE DSN                   90364
         STM   R14,R1,CAMUNCAT                                   90364
         LM    R14,R1,PATDELIX                                   90364
         ALR   R15,R13       RELOCATE INDEX                      90364
         STM   R14,R1,CAMDELIX                                   90364
         LM    R14,R1,PATDSCB1                                   91328
         ALR   R15,R13       RELOCATE DSNAME                     91328
         ALR   R0,R13        RELOCATE VOLSER                     91328
         ALR   R1,R13        RELOCATE WORK AREA                  91328
         STM   R14,R1,CAMDSCB1  COMPLETE OBTAIN REQUEST          91328
         LM    R14,R1,PATLOCAT                                   90364
         ALR   R15,R13       RELOCATE USER'S DSN                 90364
         ALR   R1,R13        RELOCATE WORK AREA                  90364
         STM   R14,R1,CAMLOCAT                                   90364
         LM    R14,R1,PATSCRAT                                   90364
         ALR   R15,R13       RELOCATE TRUE DSN                   90364
         ALR   R1,R13        RELOCATE WORK AREA                  90364
         STM   R14,R1,CAMSCRAT                                   90364
         TM    FLAGS,FGMULTI  KERMIT MODE ?                      93221
         BNZ   SETKERM       YES; NO DEBUG MODE                  93221
         SERVCALL TIODD,=CL8'SYSUDUMP'  DOES USER WISH DEBUG MODE ?
         LTR   R0,R0         TEST RETURN                         90364
         BNZ   SETBUGGY      FOUND - SET DEBUG MODE              90364
         SERVCALL TIODD,=CL8'SYSMDUMP'  ALTERNATE ?              90364
         LTR   R0,R0                                             90364
         BNZ   SETBUGGY      FOUND                               90364
         SERVCALL TIODD,=CL8'SYSABEND'                           90364
         LTR   R0,R0                                             90364
         BZ    NOTBUGGY      NO                                  90364
SETBUGGY OI    FLAGS,FGDEBUG  SET DEBUG MODE                     90364
NOTBUGGY PRTOPEN SYSPRINT,OPT=NOWTO                              90364
         MVI   ERRDEV,1      SET TO USE PRINTER FOR ERRORS       90364
         CH    R15,=H'4'                                         90364
         BNH   HAVEPRT                                           90364
SETKERM  PRTOPEN CONSOLE,OPT=NOWTO,DEV=2  ALTERNATE OUTPUT       90364
         MVI   ERRDEV,2      ERROR MESSAGES ON CONSOLE/TERMINAL  90364
         TM    FLAGS,FGMULTI  KERMIT MODE ?                      93221
         BNZ   NOTTEST       YES; NEVER TEST MODE                93221
HAVEPRT  LTCB  R2,USE=YES  GET TCB                               90364
         L     R2,TCBTIO     GET THE TIOT                        90364
         DROP  R2                                                90364
         CLC   =C'TEST ',8(R2)  TEST STEP ?                      90364
         BE    SETTEST       YES                                 90364
         CLC   =C'TEST ',16(R2)  OR PROC ?                       90364
         BNE   NOTTEST       NO                                  90364
SETTEST  OI    FLAGS,FGTEST  SET TEST MODE                       90364
NOTTEST  PRTLIST HEADER,TITLE=1  PRINT A TITLE                   90364
         LA    R9,0(,R9)     CLEAN PARM POINTER                  90364
         LTR   R7,R9         ANY ADDRESS ?                       90364
         BZ    PARMNULL      NO; ERROR                           90364
         ICM   R9,15,0(R9)   GET PARM ADDRESS                    90364
         BZ    PARMNULL      NONE; ERROR                         90364
         SLR   R8,R8                                             90364
         ICM   R8,3,0(R9)    TEST PARM LENGTH (<100 ?)           90364
         BNP   PARMNULL      INVALID                             90364
         LTR   R7,R7         POSSIBLE TSO CP INVOCATION ?       GP05162
         BM    NOTSOCP       NO                                 GP05162
         CLI   2(R9),0       SECOND COUNT FIELD ?               GP05162
         BH    NOTSOCP       NO                                 GP05162
         SH    R8,2(,R9)     ADJUST                             GP05162
         AH    R9,2(,R9)     POSITION AFTER COMMAND             GP05162
         SH    R8,=H'4'      ALLOW FOR HEADER LENGTH            GP05162
         LA    R9,2(,R9)     SKIP DISPLACEMENT                  GP05162
NOTSOCP  LA    R9,2(,R9)     MAKE SCANNING EASIER                90364
         STM   R8,R9,PARMPTRS  STASH FOR LOOP                    90364
         SPACE 1                                                 90364
***********************************************************************
*                                                                     *
*    PRELIMINARIES ARE OUT OF THE WAY - LOOP THROUGH PARM FIELD       *
*    SCANNING FOR TEXT SEPARATED BY SEMI-COLONS. EXPECTED FORMS:      *
*    'DSNAME;...'   'DSNAME(MEMBER)'   'GDGNAME(-1)'                  *
*    'DSNAME,VOLSER;...'                                              *
*    NULL FIELDS ';;' ARE SKIPPED; NULL MEMBER NAMES ARE SKIPPED      *
*    NULL SERIALS ARE IGNORED;  OTHER NULLS ARE ERRORS.               *
*    THE SPECIAL FORM 'DSN,*' MAY BE USED TO REQUEST THE PROGRAM      *
*    TO BYPASS ALLOCATION AND MOUNTING, AND MAY BE USED FOR           *
*    MULTI-VOLUME CATALOG ENTRIES. NOTE THAT SCRATCH RETURN CODES     *
*    ARE NOT COMPLETELY OR CORRECTLY PROCESSED IN THIS CASE.          *
*                                                                     *
***********************************************************************
RESTART  CLRL  WORKBLK,WORKBLKN,FILL=C' '  BLANK THE EBCDIC WORK 90364
         CLRL  WORKCLR,WORKCLRN        CLEAR THE REST OF THE WORK AREA
         MVC   CURRFG,FLAGS  PROPAGATE ENTRY FLAGS               90364
         NI    CURRFG,255-FGMULTI  RESET KERMIT BIT              93221
         LM    R8,R9,PARMPTRS  LOAD THE PARM LENGTH AND ADDRESS  90364
         L     R0,=AL1(0,C',',C'(',C';')  SET SCAN STOPS         90364
         BAL   R14,SCANNER   LOOK FOR SOMETHING                  90364
         LTR   R15,R15       NULL DSN ?                          91001
         BP    HAVEDSNP      NO                                  91001
         CLM   R2,1,SCANTAB+C';'  NULL FIELD ?                   90364
         BNE   HAVEDSNP      NO                                  90364
         STM   R8,R9,PARMPTRS  SAVE UPDATED POINTERS             90364
         B     POSTPRMS      LOOK FOR MORE                       90364
         SPACE 1                                                 90364
HAVEDSNP CLM   R2,1,SCANTAB+C'('  DID WE FIND A PARENTHESIS ?    90364
         BNE   PLAINDSN      NO; DO NORMAL DSN PROCESSING        90364
         LTR   R8,R8         ANY MORE AFTER THIS ?               90364
         BNP   PARMBAD       NO; MALFORMED PARM                  90364
         CLI   0(R9),C'-'    GDG REFERENCE ?                     90364
         BE    SCANGDG       YES; RESCAN                         90364
         CLI   0(R9),C'+'    INVALID GDG REFERENCE ?             90364
         BE    SCANGDG                                           90364
         CLI   0(R9),C'0'    OTHER FORMAT GDG ?                  90364
         BL    SCANMEM       NO; SCAN FOR MEMBER NAME            90364
         CLI   0(R9),C'9'    GDG ?                               90364
         BH    SCANMEM       NO; PROBABLY BAD MEMBER NAME        90364
SCANGDG  LM    R8,R9,PARMPTRS  RELOAD PARM POINTERS              90364
         L     R0,=A(0,0,C',',C';')  SCAN FOR VOLUME OR END      90364
         BAL   R14,SCANNER                                       90364
         B     PLAINDSN      PROCESS DSNAME ONLY                 90364
         SPACE 1                                                 90364
SCANMEM  CH    R15,=Y(L'DSNAME)  VALID LENGTH ?                  90364
         BH    PARMBDSN      NO; COMPLAIN                        90364
         SH    R15,=H'1'     MAKE LENGTH FOR EXECUTE             90364
         BM    PARMNULL      NONE; COMPLAIN                      90364
         EX    R15,MVCDSN    MOVE DSN                            90364
         L     R0,=AL1(C'(',C')',C',',C';')  SET SCAN STOPS      90364
         BAL   R14,SCANNER   LOOK FOR END                        90364
         CLM   R2,1,SCANTAB+C')'  CORRECT STOP FOR MEMBER NAME ? 90364
         BNE   PARMBMEM      NO; INVALID MEMBER NAME             90364
         CH    R15,=Y(L'MEMBER)  VALID LENGTH ?                  90364
         BH    PARMBMEM      NO                                  90364
         SH    R15,=H'1'     VALID LENGTH ?                      90364
         BM    PARMBMEM      NO                                  90364
         EX    R15,MVCMEM    MOVE MEMBER NAME                    90364
         NI    CURRFG,255-FGSCR-FGUNC  DON'T DELETE OR UNCATALOG 90364
         OI    CURRFG,FGMEM  SCRATCH MEMBER                      90364
         L     R0,=AL1(0,0,C',',C';')  SCAN FOR POSSIBLE SERIAL  90364
         BAL   R14,SCANNER   LOOK FOR IT                         90364
         LTR   R15,R15       DID WE HIT ANY TEXT ?               90364
         BP    PARMBMEM      YES; BAD NAME                       90364
         B     SCANPVOL      SCAN FOR POSSIBLE VOLUME            90364
         SPACE 1                                                 90364
PLAINDSN CH    R15,=Y(L'DSNAME)  VALID LENGTH ?                  90364
         BH    PARMBDSN      NO; COMPLAIN                        90364
         SH    R15,=H'1'     MAKE LENGTH FOR EXECUTE             90364
         BM    PARMNULL      NONE; COMPLAIN                      90364
         EX    R15,MVCDSN    MOVE DSN                            90364
SCANPVOL CLM   R2,1,SCANTAB+C','  POSSIBLE VOLUME SERIAL         90364
         BNE   SCANNVOL      NO                                  90364
         L     R0,=AL1(C'(',C')',C',',C';')  BUT WANT SEMI-COLON ONLY
         BAL   R14,SCANNER   LOOK FOR END                        90364
         CLM   R2,1,SCANTAB+C';'  EXPECTED FIELD END ?           90364
         BNE   PARMBVOL      NO; ERROR                           90364
         CH    R15,=Y(L'SERIAL)  VALID LENGTH ?                  90364
         BH    PARMBVOL      NO; FAIL                            90364
         SH    R15,=H'1'     ADJUST FOR EXECUTE                  90364
         BM    SCANNVOL      NO SERIAL - ACCEPTABLE              90364
         BP    SCANYVOL      MORE THAN ONE BYTE                  90364
         CLI   0(R1),C'*'    SPECIAL SERIAL ?                    90364
         BNE   SCANYVOL      NO; PROCEED                         90364
         TM    CURRFG,FGMEM  DOING PDS MEMBER DELETE ?           90364
         BNZ   SCANNVOL      YES; NO FLAG, NO SERIAL             90364
         OI    CURRFG,FGMULTI  ALLOW MULTI-VOLUME SCRATCH        90364
         B     SCANNVOL      DON'T WAN'T '*' SERIAL              90364
         SPACE 1                                                 90364
SCANYVOL EX    R15,MVCVOL    MOVE SERIAL                         90364
SCANNVOL DS    0H            NO SERIAL SUPPLIED                  90364
PARMEND  STM   R8,R9,PARMPTRS  SAVE RESIDUAL LENGTH, ETC. FOR NEXT PASS
*DEBUG*  PRTLIST MGCURPRM    SHOW WHAT WE HAVE SO FAR            90364
         SPACE 2
***********************************************************************
*                                                                     *
*    FINISHED PARSING OF DSN / MEMBER /VOLUME                         *
*    CHECK WHETHER THE DATASET IS IN THE CATALOG; IF IT IS,           *
*    KEEP THE UNCATALOG BIT ONLY IF THE CATALOG ENTRY MATCHES         *
*    PRECISELY AND WE ARE DOING A DATASET DELETE.                     *
*      NOTE A MULTI-VOLUME CATALOG ENTRY WILL NEVER MATCH,            *
*    EVEN IF THE USER SPECIFIED ONE OF THE VOLUMES IN THE LIST.       *
*                                                                     *
***********************************************************************
         MVC   TRUENAME,DSNAME  COPY NAME FOR CATALOG MANIPULATION
         LOCATE CAMLOCAT     IS IT CATALOGED ?                   90364
         BXH   R15,R15,EXAMNLOC   NO, OR ERROR                   90364
         TM    CURRFG,FGMULTI  DO MULTI-VOLUME SCRATCH ?         90364
         BNZ   SKIPMNT       YES; SKIP MOUNTING, ETC.            90364
         CLC   =H'1',CATVOL#  ONE VOLUME ENTRY ?
         BNE   EXAMNLOC      NO; TREAT AS UNCATALOGED            90364
         CLC   =C'MIGRAT',CATVOL  MIGRATED DATASET?             GP99062
         BE    EXAMNDEV      YES; BYPASS DEVICE TYPE CHECK      GP99062
         CLI   CATDEV+2,UCB3DACC  DASD DEVICE ?                  90364
         BNE   EXAMNLOC      NO; TREAT AS UNCATALOGED            90364
EXAMNDEV CLC   SERIAL,BLANKS  DID USER SPECIFY A VOLUME ?        90364
         BNE   EXAMYLOC      YES; LOOK AT IT                     90364
         MVC   SERIAL,CATVOL  USE IT                             90364
EXAMYLOC CLC   CATVOL,SERIAL   SAME VOLSER  ?                    90364
         BE    POSTLOC       YES; RETAIN UNCAT REQUEST           90364
EXAMNLOC NI    CURRFG,255-FGUNC  DON'T DELETE OTHER CATALOG ENTRY
         MVC   TRUENAME,DSNAME  RESTORE USER'S NAME              90364
         SPACE 1                                                 90364
POSTLOC  CLC   SERIAL,BLANKS  HAVE WE A VOLUME SERIAL ?          90364
         BNE   SEEMOUNT      TRY TO GET IT MOUNTED               90364
         PRTLIST MGNOSER     NOT LOCATED - ALREADY GONE ?        90364
         OI    CCODE+L'CCODE-1,1  SET VERY MINOR ERROR           90364
         B     POSTPROC      GO TO END OF REQUEST                90364
         SPACE 1                                                 90364
***********************************************************************
*                                                                     *
*    FOR DATASET DELETE, CHECK IF THE PACK IS UP AND USABLE.          *
*    IF IT IS, BYPASS THE OVERHEAD OF AN ALLOCATION.                  *
*                                                                     *
***********************************************************************
SEEMOUNT TM    CURRFG,FGMEM  BPAM MODE ?                         90364
         BNZ   TRYMOUNT      YES; MUST ALLOCATE                  90364
         CLC   =C'MIGRAT',SERIAL  MIGRATED DATASET?             GP99062
         BE    HSMDS         YES; DO DIFFERENTLY                GP99062
         SERVCALL UCBDK,SERIAL  LOOK FOR UCB                     90364
         LTR   R3,R0         ANY FOUND ?                         90364
         BZ    TRYMOUNT                                          90364
         USING UCBOB,R3      DECLARE THE UCB                     90364
         TM    UCBSTAT,UCBCHGS+UCBUNLD  GOING AWAY ?             90364
         BNZ   TRYMOUNT      YES; FORCE FORMAL MOUNT             90364
         TM    UCBSTAT,UCBRESV+UCBPRES+UCBSYSR  WILL BIG BOY STAY ?
         BNZ   HAVEUNIT      YES - SKIP ALLOC OVERHEAD           90364
         DROP  R3                                                90364
         SPACE 1                                                 90364
***********************************************************************
*                                                                     *
*    INVOKE SVC 99 TO ALLOCATE EITHER THE DATASET (MEMBER DELETE),    *
*    OR THE VTOC OF THE REQUIRED PACK.  THE VTOC ALLOCATION           *
*    AVOIDS PROBLEMS WITH SCRATCH, OUT OF SPACE, ETC., BUT            *
*    REQUIRES AUTHORIZATION (HENCE 'SERVICE' CALL).                   *
*                                                                     *
***********************************************************************
TRYMOUNT LA    R14,RETURNDD                                      90364
         LA    R15,TRUENAME                                      90364
         SLR   R0,R0         NO MEMBER                           90364
         LA    R1,SERIAL     SERIAL                              90364
         CLC   =C'MIGRAT',SERIAL  MIGRATED?                     GP99062
         BNE   SERVSER       NO; USE SERIAL                     GP99062
         LA    R1,BLANKS     ELSE DO CATALOG LOOKUP             GP99062
SERVSER  TM    FLAGS,FGEXIST  VERIFICATION ?                     91328
         BNZ   SERVSHR                                           91328
         LA    R2,DISPBPAM   DISPOSITION FOR RE-USED DATASET     90364
         TM    FLAGS,FGEXIST  DOING VERIFICATION ?              GP02235
         BZ    *+8           NO; LEAVE IT                       GP02235
         LA    R2,DISPBPRD   SET BPAM DISP=SHR FOR READ ONLY    GP02235
         TM    CURRFG,FGMEM  DOING BPAM ?                        90364
         BNZ   SERVSTM       YES; MAKE PARAMETER LIST            90364
         LA    R15,=44X'04'   POINT TO VTOC DSN                  90364
SERVSHR  LA    R2,DISPMNT    SET DISP=SHR AND MOUNT/WAITS        90364
SERVSTM  STM   R14,R2,SERVALL  MAKE SERVICE PARM LIST            90364
         SERVICE ALCDD,SERVALL  WAIT OPTIONS REQUIRE PRIVILEGED MODE
         STM   R15,R0,DB     STASH THE RETURN CODES              90364
         BXLE  R15,R15,SERVICED  OK                              90364
         LA    R5,PLALFAIL   TELL USER ALLOCATION FAILED         90364
         CH    R15,=H'8'     RETURN CODE = 4 ?                   91329
         BNE   POSTPATE      NO; FAIL                            91329
         LTR   R0,R0         ANY SVC 99 RETURN CODES ?           91329
         BZ    SERVICED      NO; MAY BE NULL DSORG ?             91329
         B     POSTPATE      GO TO END OF PROCESSING             90364
         SPACE 1                                                 90364
SERVICED TM    CURRFG,FGMEM  MEMBER PROCESSING ?                 90364
         BNZ   MEMDELET      YES; DO DIFFERENTLY                 90364
         SPACE 1
***********************************************************************
*                                                                     *
*    NOW THAT THE REQUIRED VOLUME IS MOUNTED, SCRATCH THE DS.         *
*    IF THE DATASET IS NOT FOUND, SET THE CODE TO 1 AND CONTINUE.     *
*    ANY OTHER ERROR INHIBITS THE UNCATALOGING.                       *
*                                                                     *
***********************************************************************
         LA    R5,PLBADMNT   PRESET MESSAGE FOR FAILED MOUNT     90364
         SERVCALL UCBDK,SERIAL,ERR=POSTPATE  LOCATE UCB BY VOLSER
         LTR   R3,R0         FOUND ?                             81117
         BNP   POSTPATE      NO; SET MESSAGE AND ERROR           90364
         USING UCBOB,R3      DECLARE UCB                         81137
HAVEUNIT MVC   CATDEV,UCBTBYT1  COPY DEVICE TYPE FROM UCB        81117
         NI    CATDEV+1,255-UCBVLPWR-UCBDVPWR-UCBRR              81159
         CLC   =X'200A',UCBTBYT3    3340 ?                       81155
         BNE   *+8           NO                                  81155
         NI    CATDEV+1,255-UCBRPS  WHY DOES MVS DO THIS ?       81155
         MVC   CATVOL,SERIAL   COPY VOLUME SERIAL
         MVC   CATVOL#,=H'1'  SET SINGLE VOLUME                  90364
SKIPMNT  LA    R5,PLBADMNT   PRESET MESSAGE FOR FAILED MOUNT     90364
         TM    FLAGS,FGEXIST   VERIFY MODE ONLY ?                91328
         BNZ   SEQVER        YES; READ DSCB FOR VERIFICATION     91328
         TM    CURRFG,FGTEST  RUNNING IN TEST MODE ?             90364
         BNZ   FAKESCRT      YES; JUST FAKE IT                   90364
         XC    VOLSEQ(2),VOLSEQ      ZERO RETURN INDICATOR
         SLR   R0,R0         PACK IS MOUNTED - DON'T NEED UCB    90364
         SCRATCH CAMSCRAT    SCRATCH FROM DASD                   90364
         CH    R15,=H'4'     CHECK RETURN CODE                   90364
         BL    FAKESCRT      SCRATCHED, NOW CHECK IF UNCATLG NEEDED
         BE    POSTPATE      NOT MOUNTED ?                       90364
         CLC   =H'1',VOLSEQ  DATASET NOT FOUND                   90364
         BE    FAKESCRD      YES; JUST MESSAGE                   90364
         LA    R5,PLBADSCR   ELSE SET FOR BAD SCRATCH            90364
         B     POSTPATE      AND WRITE                           90364
         SPACE 1                                                 90364
***********************************************************************
*                                                                     *
*    MIGRATED DATA SET:                                               *
*    FOR AN EXISTER REQUEST, WE ARE FINISHED.                         *
*    FOR DELETER, INVOKE HSM SERVICES TO DELETE THE DATA SET.         *
*                                                                     *
***********************************************************************
HSMDS    TM    FLAGS,FGEXIST  VERIFY MODE?                      GP99062
         BNZ   HSMDSVER      YES; DONE AFTER MESSAGE            GP99062
         AIF   (&MVSXA).HSM  HAVE HSM UNDER THIS ?              GP05032
         LA    R5,PLNOHSM    SET ERROR MESSAGE                  GP05032
         LA    R15,12        SET BIG ERROR                      GP05032
         AGO   .NOHSM                                           GP05032
.HSM   ARCHDEL DSN=TRUENAME,WKAREA=HSMWORK,WAIT=YES,PURGE=YES   GP99062
         LA    R5,PLMIGDEL   PRESET FOR SCRATCHED               GP99062
         LTR   R15,R15       DID IT TAKE?                       GP99062
         BZ    POSTLIST                                         GP99062
         ST    R15,DB                                           GP99062
         LA    R5,PLMIGBAD                                      GP99062
.NOHSM   B     POSTPATE      SET RC=8 AND MAKE ERROR MESSAGE    GP99062
HSMDSVER LA    R5,PLFMIG     SHOW MIGRATED                      GP99062
         B     POSTLIST      AND GO FOR MORE                    GP99062
         SPACE 1                                                 91328
***********************************************************************
*                                                                     *
*    VERIFY THAT DATASET EXISTS - OBTAIN DSCB                         *
*                                                                     *
***********************************************************************
SEQVER   OBTAIN CAMDSCB1     FIND DSCB 1 FOR DATASET             91328
         LA    R5,PLALLSCR   NOT FOUND                           91328
         BXH   R15,R15,POSTPATE  ISSUE MESSAGE AND ERROR CODE    91328
         LA    R5,PLFOUND                                        91328
         B     POSTLIST      WRITE MESSAGE                       91328
         SPACE 1                                                 90364
FAKESCRD PRTLIST PLALLSCR    SHOW ALREADY GONE                   90364
         OI    CCODE+L'CCODE-1,1  SET MINOR                      90364
         B     POSTSCRT      JOIN COMMON CODE                    90364
         SPACE 1                                                 90364
FAKESCRT PRTLIST PLSCRD      SHOW SCRATCHED                      90364
***********************************************************************
*                                                                     *
*    THE SCRATCH WAS SUCCESSFUL, OR THE DATASET WAS NOT FOUND.        *
*    IF UNCATALOGING IS APPROPRIATE, DO SO.                           *
*                                                                     *
***********************************************************************
POSTSCRT SLR   R15,R15       FOR TEST MODE                       90364
         TM    CURRFG,FGUNC  UNCATALOG REQUIRED ?                90364
         BZ    POSTPROC      NO; NO MESSAGE, EITHER              90364
         TM    FLAGS,FGTEST  TEST MODE ?                         90364
         BNZ   FAKEUNCT      YES; DON'T REALLY DO IT             90364
         CATALOG CAMUNCAT    REMOVE IT FROM CATLG                90364
         LA    R5,PLBADUNC   UNCATALOG FAILED                    90364
         STM   R15,R0,DB     SAVE RETURN CODES                   90364
         LTR   R15,R15                                           90364
         BNZ   POSTFOUR      SET LESSER ERROR                    90364
FAKEUNCT PRTLIST PLNOWUNC    SHOW UNCATALOGED                    90364
         SPACE 1                                                 90364
***********************************************************************
*                                                                     *
*    UNCATALOG WAS SUCCESSFUL - NOW CLEAN UP AND DELETE THE           *
*    HIGH LEVEL INDEX ALSO.  IGNORE THE RETURN CODE.                  *
*                                                                     *
***********************************************************************
         LA    R8,L'TRUENAME  GET LENGTH OF DSN                  90364
         LA    R9,TRUENAME   GET DSN                             90364
         L     R0,=AL1(0,0,C'.',C' ')  LOOK FOR INDEX LEVEL      90364
         BAL   R14,SCANNER                                       90364
         CLM   R2,1,SCANTAB+C'.'  INDEX LEVEL ?                  90364
         BNE   POSTPROC      NO                                  90364
         CH    R15,=Y(L'INDEX)  VALID LENGTH ?                   90364
         BH    POSTPROC      NO                                  90364
         SH    R15,=H'1'     MAKE EXECUTE LENGTH                 90364
         BM    POSTPROC      ?                                   90364
         EX    R15,MVCINX    MOVE INDEX NAME                     90364
         INDEX CAMDELIX      RELEASE THE INDEX                   90364
         B     POSTPROC      GO TO CLEANUP                       90364
         SPACE 1                                                 90364
***********************************************************************
*                                                                     *
*    THE USER HAS REQUESTED THE DELETION OF ONE MEMBER FROM A         *
*    PDS.  NOTE THAT NO DATASET ENQUEUES HAVE BEEN MADE.              *
*                                                                     *
***********************************************************************
MEMDELET OBTAIN CAMDSCB1     VERIFY DATASET EXISTENCE            91328
         LA    R5,PLALLSCR   SET FOR NOT FOUND                   91328
         BXH   R15,R15,POSTPATE                                  91328
         LA    R5,PLNOTPO                                        91328
         CLI   DS1DSORG+1-DS1FMTID+CATWORK,0  VSAM OR OTHER?     91328
         BNE   POSTPATE                                          91328
         TM    DS1DSORG-DS1FMTID+CATWORK,X'02'  BPAM ?           91328
         BZ    POSTPATE      NO                                  91328
         TM    DS1DSORG-DS1FMTID+CATWORK,255-2-1  NOT PO/NOT U ? 91328
         BNZ   POSTPATE      YES; WHAT IS IT ?                   91328
         MVC   BPAMDCB(BPAMDCBN),BPAMPAT  MOVE DCB               90364
         MVC   DCBDDNAM-IHADCB+BPAMDCB,RETURNDD  MAKE DDNAME     90364
         OPEN  MF=(E,@BPAMDCB)  OPEN FOR OUTPUT                  90364
         LA    R5,PLMEMNOP   SET FOR NO OPEN                     90364
         TM    DCBOFLGS-IHADCB+BPAMDCB,DCBOFOPN  OPENED ?        90364
         BZ    POSTPATE      NO; SET ERROR AND MSG               90364
         TM    FLAGS,FGEXIST  VERIFICATION ONLY ?                91328
         BNZ   MEMVER        YES                                 91328
         TM    FLAGS,FGTEST  RUNNING IN TEST MODE ?              90364
         BNZ   MEMGONE       YES; FAKE IT                        90364
         STOW  BPAMDCB,MEMBER,D  DELETE THE MEMBER               90364
         CH    R15,=H'8'     DID IT TAKE ?                       90364
         BL    MEMGONE       YES                                 90364
         BH    MEMFAIL       NO                                  90364
         OI    CCODE+L'CCODE-1,1  SET MEMBER NOT FOUND           90364
         LA    R5,PLMEMGON   SHOW MEMBER GONE ALREADY            90364
         B     POSTLIST                                          90364
MEMGONE  LA    R5,PLMEMDEL   SHOW MEMBER DELETED                 90364
         B     POSTLIST                                          90364
MEMFAIL  ST    R15,DB                                            90364
         LA    R5,PLMEMBAD   SHOW FAILED                         90364
*NEXT    B     POSTPATE      MESSAGE AND SET ERROR               90364
         SPACE 1                                                 91328
***********************************************************************
*                                                                     *
*    VERIFY THAT MEMBER EXISTS - NO SCRATCH                           *
*                                                                     *
***********************************************************************
MEMVER   MVC   CATWORK(4),=Y(1,72)  MAKE BLDL PREFIX             91328
         MVC   CATWORK+4(8),MEMBER  COMPLETE REQUEST             91328
         BLDL  BPAMDCB,CATWORK  FIND THE MEMBER                  91328
         LA    R5,PLMEMGON   MEMBER NOT FOUND                    91328
         BXH   R15,R15,POSTPATE  SET ERROR                       91328
         LA    R5,PLFOUND                                        91328
         B     POSTLIST                                          91328
         SPACE 2                                                 90364
***********************************************************************
*                                                                     *
*    THE REQUEST HAS BEEN PROCESSED (SUCCESSFULLY OR OTHERWISE);      *
*    NOW WE CLEAN UP AND ISSUE MESSAGES, SET RETURN CODE, ETC.        *
*                                                                     *
***********************************************************************
POSTPATE OI    CCODE+L'CCODE-1,8  SET ERROR                      90364
         PRTLIST (R5),DEV==ERRDEV  WRITE CRITICAL MESSAGE        90364
         LA    R0,PLMEMGON   MEMBER NOT FOUND ?                  93306
         CR    R0,R5         OTHER ERROR - KEEP CODE             93306
         BNE   POSTPROC      QUIT THIS                           93306
         OI    CCODE+L'CCODE-1,8+2  MISSING MEMBER: CCODE 10     93306
         B     POSTPROC      AND QUIT THIS REQUEST               90364
         SPACE 1                                                 90364
POSTFOUR OI    CCODE+L'CCODE-1,4  SET LESSER ERROR               90364
POSTLIST PRTLIST (R5)        WRITE NON-CRITICAL MESSAGE          90364
         SPACE 1                                                 90364
POSTPROC PRTLIST MGCURPRM    DUMP STORAGE AREA IF DEBUG MODE     90364
         TM    BPAMDCB+DCBOFLGS-IHADCB,DCBOFOPN  DCB STILL OPEN ?
         BZ    POSTPRDL      NO                                  90364
         CLOSE MF=(E,@BPAMDCB)                                   90364
POSTPRDL CLI   RETURNDD,0    ANY DD ALLOCATED ?                  90364
         BE    POSTPRMS      NO                                  90364
         SERVCALL ALCFD,RETURNDD  FREE THE ALLOCATION            90364
         XC    RETURNDD,RETURNDD                                 90364
POSTPRMS ICM   R8,15,PARMPTRS  ANY MORE TEXT TO SCAN ?           90364
         BP    RESTART       YES; EXAMINE NEXT REQUEST           90364
         SPACE 1                                                 90364
EXIT     SERVTERM ,                                              90364
         LH    R9,CCODE                                          90364
         TM    FLAGS,FGZERO  SET CODE ZERO ?                     93020
         BZ    EXITCODE      NO                                  93020
         SLR   R9,R9         YES                                 93020
EXITCODE PGMEXIT RC=(R9)     RETURN WITH CONDITION               90364
         SPACE 1                                                 90364
PARMNULL LA    R2,MGPRMNUL                                       90364
         B     PARMERR       GO TO COMMON PARM ERROR             90364
PARMBAD  LA    R2,MGPRMSYN                                       90364
         B     PARMERR       SYNTAX ERROR                        90364
PARMBVOL LA    R2,MGPRMVOL                                       90364
         B     PARMERR                                           90364
PARMBMEM LA    R2,MGPRMMEM                                       90364
         B     PARMERR                                           90364
PARMBDSN LA    R2,MGPRMDSN                                       90364
PARMERR  LM    R3,R4,PARMPTRS  GET OLD VALUES FOR MESSAGE        90364
         STM   R8,R9,PARMPTRS  STORE FOR NEXT PARM               90364
         LA    R5,PLBADPRM   SET ERROR MESSAGE                   90364
         B     POSTPATE      GO TO PASS END                      90364
         SPACE 1                                                 90364
PLBADPRM FD    'PARM ERROR',NL,PAD                               91328
         FD    0(R2),L'MGPRMNUL,DEB                              90364
         FD    '''',PADL                                         90364
         FD    0(R4),(R3)                                        90364
         FD    ''''                                              90364
         FD    *END                                              90364
MGPRMNUL DC    C'NULL PARM LENGTH'                               90364
MGPRMSYN DC    C'SYNTAX ERROR    '                               90364
MGPRMVOL DC    C'VOLUME SERIAL   '                               90364
MGPRMMEM DC    C'MEMBER NAME     '                               90364
MGPRMDSN DC    C'DATASET NAME    '                               90364
         SPACE 1                                                 90364
MGCURPRM FDTM  FLAGS,FGDEBUG,BZ=MGCURPRX                         90364
         FDSNAP SAVE,SAVEEND-SAVE,DUAL,NOOFFSET,BASE=0           90364
MGCURPRX FD    *END                                              90364
         SPACE 1                                                 90364
MGNOSER  FD    DSNAME,DEB,NL                                     90364
         FD    'NOT FOUND IN CATALOG; NO ACTION TAKEN',PAD       90364
         FD    *END                                              90364
         SPACE 1                                                 91328
PLFOUND  FDOPT NL                                                91328
         FDTM  CURRFG,FGMEM,BZ=PLFOUNDS                          91328
         FD    'MEMBER'                                          91328
         FD    MEMBER,DEB,PAD                                    91328
         FD    'IN',PAD                                          91328
PLFOUNDS FD    DSNAME,DEB                                        91328
         FD    'FOUND ON',PAD                                    91328
         FD    SERIAL,DEB                                        91328
         FD    *END                                              91328
         SPACE 1                                                 91328
PLFMIG   FDOPT NL                                               GP99062
         FDTM  CURRFG,FGMEM,BZ=PLFMIGS                          GP99062
         FD    'MEMBER'                                         GP99062
         FD    MEMBER,DEB,PAD                                   GP99062
         FD    'IN',PAD                                         GP99062
PLFMIGS  FD    DSNAME,DEB                                       GP99062
         FD    'MIGRATED',PAD                                   GP99062
         FDCLI CATDEV+2,UCB3DACC,BNE=PLFMIGX                    GP99062
         FD    'TO DASD'                                        GP99062
PLFMIGX  FD    *END                                             GP99062
         SPACE 1                                                 91328
PLMIGDEL FD    DSNAME,DEB                                       GP99062
         FD    'DELETED BY HSM',PAD                             GP99062
         FD    *END                                             GP99062
         SPACE 1                                                 91328
PLMIGBAD FD    DSNAME,DEB                                       GP99062
         FD    'NOT DELETED BY HSM, RETURN CODE=',PADL          GP99062
         FD    DB,4,I,DEB                                       GP99062
         FDCLC F100,DB,4,BNE=PLMIGBAX                           GP99062
         FD    ' HSM NOT AVAILABLE - TRY LATER',PAD             GP99062
PLMIGBAX FD    *END                                             GP99062
         SPACE 1                                                GP05032
PLNOHSM  FD    DSNAME,DEB                                       GP05032
         FD    'NOT DELETED - MIGRATED, BUT THIS PROGRAM NOT ASSEMBLED *
               WITH HSM SUPPORT',PADL                           GP05032
         FD    *END                                             GP05032
         SPACE 1                                                 91328
PLNOTPO  FD    'MEMBER OPERATION INVALID -',NL                   91328
         FD    DSNAME,PAD,DEB                                    91328
         FD    'ON'                                              91328
         FD    SERIAL,DEB,PAD                                    91328
         FD    'IS NOT PARTITIONED'                              91328
         FD    *END                                              91328
         SPACE 1                                                 90364
PLALFAIL FD    DSNAME,DEB,NL                                     90364
         FD    'ON',PAD                                          90364
         FD    SERIAL,DEB                                        90364
         FD    'COULD NOT BE ALLOCATED; RETURN CODE',PAD         90364
         FD    DB,4,I,DEB                                        90364
         FD    ', REASON CODE X'''                               90364
         FD    DB+4,4,HEX                                        90364
         FD    ''''                                              90364
         FD    *END                                              90364
         SPACE 1                                                 90364
PLBADMNT FD    SERIAL,NL,DEB                                     90364
         FD    'COULD NOT BE MOUNTED',PAD                        90364
         FD    *END                                              90364
         SPACE 1                                                 90364
PLBADSCR FD    DSNAME,DEB,NL                                     90364
         FD    'ON',PAD                                          90364
         FD    SERIAL,DEB                                        90364
         FD    'COULD NOT BE SCRATCHED - REASON CODE',PAD        90364
         FD    VOLSEQ,I,DEB                                      90364
         FD    '  RETURN CODE',PAD                               90364
         FD    DB,4,I,DEB                                        90364
         FD    *END                                              90364
         SPACE 1                                                 90364
PLALLSCR FD    DSNAME,DEB,NL                                     90364
         FD    'NOT ON',PAD                                      90364
         FD    SERIAL,DEB                                        90364
         FDTM  FLAGS,FGEXIST,BNZ=PLALLEND                        91328
         FD    '; ALREADY SCRATCHED ?'                           90364
PLALLEND FD    *END                                              90364
         SPACE 1                                                 90364
PLSCRD   FD    DSNAME,DEB,NL                                     90364
         FD    'SCRATCHED FROM',PAD                              90364
         FD    SERIAL,DEB                                        90364
         FD    *END                                              90364
PLBADUNC FD    ', BUT NOT UNCATALOGED; RETURN CODE'              90364
         FD    DB,4,I,PADL                                       90364
         FD    ', REASON CODE'                                   90364
         FD    DB+4,I,PADL                                       90364
         FD    *END                                              90364
PLNOWUNC FD    'AND UNCATALOGED',PAD                             90364
         FD    *END                                              90364
         SPACE 1                                                 90364
PLMEMNOP FD    'OPEN FAILED FOR',NL                              90364
         FD    DSNAME,DEB,PAD                                    90364
         FD    'ON'                                              90364
         FD    SERIAL,DEB,PAD                                    90364
         FD    *END                                              90364
         SPACE 1                                                 90364
PLMEMGON FD    MEMBER,NL,DEB,PAD                                 90364
         FD    'NOT FOUND IN'                                    90364
         FD    DSNAME,DEB,PAD                                    90364
         FD    'ON'                                              90364
         FD    SERIAL,DEB,PAD                                    90364
         FD    *END                                              90364
         SPACE 1                                                 90364
PLMEMDEL FD    MEMBER,NL,DEB,PAD                                 90364
         FD    'DELETED FROM'                                    90364
         FD    DSNAME,DEB,PAD                                    90364
         FD    'ON'                                              90364
         FD    SERIAL,PAD,DEB                                    90364
         FD    *END                                              90364
         SPACE 1                                                 90364
PLMEMBAD FD    'DELETION OF MEMBER',NL,PAD                       90364
         FD    MEMBER,DEB                                        90364
         FD    'IN'                                              90364
         FD    DSNAME,DEB,PAD                                    90364
         FD    'ON'                                              90364
         FD    SERIAL,DEB,PAD                                    90364
         FD    'FAILED WITH RETURN CODE'                         90364
         FD    DB,4,I,DEB,PADL                                   90364
         FD    ', REASON CODE'                                   90364
         FD    DB+4,I,DEB,PADL                                   90364
         FD    *END                                              90364
F100     DC    F'100'        FOR HSM RETURN CODE TEST           GP99062
         SPACE 1                                                 90364
*        SCANNER SUBROUTINE                                      90364
*        R0 - 1-4 STOP CHARACTERS, FOURTH IS DEFAULT (UNMATCHED) 90364
*        R8 - RESIDUAL TEXT LENGTH                               90364
*        R9 - START SCAN ADDRESS                                 90364
*                                                                90364
*        RETURN:                                                 90364
*        R15 - LENGTH OF TEXT BEFORE HIT                         90364
*        R0  - ADDRESS OF HIT CHARACTER OR END OF TEXT+1         90364
*        R1  - ADDRESS OF TEXT (R9) BEFORE HIT                   90364
*        R2  - HIT CHARACTER OR DEFAULT                          90364
*        R8, R9 UPDATED TO POINT PAST HIT CHARACTER              90364
SCANNER  XC    SCANTAB,SCANTAB  CLEAR SCANNER TABLE              90364
         LR    R2,R0         COPY DEFAULT CHARACTER              90364
         ICM   R2,IIIO,ZEROES  CLEAR REMAINING BYTES             90364
         LA    R1,0(R8,R9)   POINT TO HIT COLUMN DEFAULT         90364
         STM   R3,R7,20+3*4(R13)  SAVE WORK REGISTERS            90364
         LA    R5,4          REPEAT THIS THING FOR FOUR BYTES    90364
SCANNERC LR    R4,R0         COPY CHRACTERS                      90364
         SRL   R0,8          SHIFT REQUEST TO NEXT ONE           90364
         ICM   R4,IIIO,ZEROES  CLEAR UNUSED BYTES                90364
         LA    R6,SCANTAB(R4)  GET ADDRESS OF BYTE INSERTION     90364
         STC   R4,0(,R6)     ENTER INTO TABLE                    90364
         BCT   R5,SCANNERC   REPEAT FOR ALL FOUR CHARACTERS      90364
         SPACE 1                                                 90364
         LTR   R3,R8         GET LENGTH TO SCAN                  90364
         BNP   SCANNER0      TOO BAD                             90364
         EX    R3,SCANNTRT   LOOK FOR SOMETHING                  90364
SCANNER0 LR    R15,R1        COPY HIT COLUMN                     90364
         SR    R15,R9        LESS START = LENGTH OF TEXT BEFORE  90364
         LR    R0,R1         SAVE HIT ADDRESS FOR USER           90364
         LR    R1,R9         SAVE START SCAN ADDRESS             90364
         LR    R9,R0         SET NEW TEXT ADDRESS(HIT)           90364
         SR    R8,R15        RESIDUAL LENGTH                     90364
         LTR   R2,R2         ANYTHING HIT ?                      90364
         BZ    SCANNERX      NO                                  90364
         BCTR  R8,0          SET RESIDUAL LENGTH                 90364
         LA    R9,1(,R9)     SKIP OVER HIT CHARACTER             90364
SCANNERX LM    R3,R7,20+3*4(R13)  RESTORE OTHER REGISTERS        90364
         BR    R14           RETURN TO CALLER                    90364
SCANNTRT TRT   0(0,R9),SCANTAB  THE END IS NEAR ?                90364
MVCDSN   MVC   DSNAME(0),0(R1)  COPY USER'S DSN                  90364
MVCVOL   MVC   SERIAL(0),0(R1)  VOLUME SERIAL                    90364
MVCMEM   MVC   MEMBER(0),0(R1)  OPTIONAL MEMBER                  90364
MVCINX   MVC   INDEX(0),0(R1)  HIGH-LEVEL INDEX                  90364
         SPACE 2                                                 90364
PATUNCAT CAMLST UCATDX,TRUENAME-SAVE  UNCATALOG                  90364
PATDELIX CAMLST DLTX,INDEX-SAVE   DELETE HIGH-LEVEL INDEX (IF EMPTY)
PATDSCB1 CAMLST SEARCH,DSNAME-SAVE,SERIAL-SAVE,CATWORK-SAVE      91328
PATLOCAT CAMLST NAME,TRUENAME-SAVE,,CATWORK-SAVE  LOCATE CATALOG 90364
PATSCRAT CAMLST SCRATCH,TRUENAME-SAVE,,CATWORK-SAVE,,OVRD  SCRATCH DS
BPAMPAT  DCB   DDNAME=RETURNDD,DSORG=PO,MACRF=(R,W)              93306
ZEROES   DC    F'0'          CONSTANCE                           90364
BLANKS   DC    CL80' '       CONSTANCE                           90364
SYSPRINT PRTWORK SYSPRINT,TITLE=5                                90364
CONSOLE  PRTWORK *USER OR CONSOLE      DEV=2 ERROR MESSAGES ONLY 90364
         SPACE 1                                                GP02235
*  SVC 99 PARAMETER LIST FOR MEMBER SCRATCH                     GP02235
*                                                               GP02235
DISPBPAM DC    X'81,08,08' 1/3  DISP=OLD FOR BPAM ACCESS         90364
         DC    AL1(0)      2/3  S99FLAG1 - RESET DEFAULT NOMOUNT
         DC    AL1(S99WTVOL+S99WTDSN+S99WTUNT+S99OFFLN+S99MOUNT) 3A/3
         DC    AL1(0,0,0)  3B/3  S99FLAGS2                      GP99063
         SPACE 1                                                GP02235
*  SVC 99 PARAMETER LIST FOR MEMBER READ                        GP02235
*                                                               GP02235
DISPBPRD DC    X'88,08,08' 1/3  DISP=SHR FOR BPAM READ          GP02235
         DC    AL1(0)      2/3  S99FLAG1 - RESET DEFAULT NOMOUNT
         DC    AL1(S99WTVOL+S99WTDSN+S99WTUNT+S99OFFLN+S99MOUNT) 3A/3
         DC    AL1(0,0,0)  3B/3  S99FLAGS2                      GP99063
         SPACE 1                                                GP02235
*  SVC 99 PARAMETER LIST FOR DSN OPEN WITH VOL MOUNT            GP02235
*                                                               GP02235
DISPMNT  DC    X'88,08,08'   1/3   DISP=(SHR,KEEP,KEEP)          90364
         DC    AL1(0,255-S99TIONQ-S99CATLG-S99UDEVT,0,0,0)  MOUNT
         SPACE 1                                                 90364
HEADER   FDOPT NL,CC=C'#'    REQUEST AUTOMATIC PAGINATION        90364
         FD    'DATASET/MEMBER'                                  91328
         FDTM  FLAGS,FGEXIST,BNZ=HEADEREX                        91328
         FD    'DELETION',PAD                                    91328
         FDGOTO HEADERCM                                         91328
HEADEREX FD    'VERIFICATION',PAD                                91328
HEADERCM FD    'PROGRAM'                                         91328
         FDTM  FLAGS,FGTEST,BZ=HEADERT                           90364
         FD    '***---> TEST <---***',PAD                       GP02235
HEADERT  FDTM  FLAGS,FGDEBUG,BZ=HEADERD                          90364
         FD    '(DEBUG MODE)',PAD                                90364
HEADERD  FD    *END                                              90364
         SPACE 1                                                 90364
SAVE     DSECT ,                                                 90364
DB       DS    D                                                 90364
DB2      DS    D                                                 90364
@SERVICE DS    A                                                 90364
@TRACE   DS    A                                                 90364
@PRINTER DS    A                                                 90364
@INPREAD DS    A                                                 90364
@BPAMDCB OPEN  (BPAMDCB,OUTPUT),MF=L                             90364
CCODE    DS    H             RETURN CODE                         90364
ERRDEV   DC    X'00'         DEV NUMBER FOR ERROR MESSAGES       90364
FLAGS    DC    X'00'         PROCESSING FLAGS                    90364
FGDEBUG  EQU   X'80'           DEBUG MODE (SNAPS, ETC.)          90364
FGMEM    EQU   X'40'           SCRATCH A MEMBER ONLY             90364
FGMULTI  EQU   X'20'           ACCEPT MULTI-VOLUME SCRATCH       90364
FGSCR    EQU   X'10'           SCRATCH DATASET                   90364
FGUNC    EQU   X'08'           UNCATALOG DATASET                 90364
FGTEST   EQU   X'04'           TEST MODE (NO SCRATCH/DELETE)     90364
FGZERO   EQU   X'02'           ALWAYS RETURN COND.CODE ZERO      93020
FGEXIST  EQU   X'01'           VERIFY EXISTENCE ONLY             91328
CAMUNCAT CAMLST UCATDX,TRUENAME  UNCATALOG                       90364
CAMDELIX CAMLST DLTX,INDEX   DELETE HIGH-LEVEL INDEX (IF EMPTY)  90364
CAMDSCB1 CAMLST SEARCH,DSNAME,SERIAL,CATWORK                     91328
CAMLOCAT CAMLST NAME,TRUENAME,,CATWORK  LOCATE CATALOG ENTRIES   90364
CAMSCRAT CAMLST SCRATCH,TRUENAME,,CATWORK,,OVRD  SCRATCH DATASET 90364
PARMPTRS DS    2A            LENGTH/ADDRESS OF RESIDUAL PARM TEXT
SERVALL  DC    A(RETURNDD,DSNAME,MEMBER,SERIAL,DISPBPAM)         90364
RETURNDD DS    CL8           CURRENT DD NAME                     90364
         SPACE 1                                                 90364
WORKBLK  DS    0C            START OF BLANKING                   90364
DSNAME   DC    CL44' '       DATASET NAME FROM PARM              90364
TRUENAME DC    CL44' '       NAME AFTER CATALOG LOOKUP           90364
SERIAL   DC    CL6' '        USER'S VOLUME SERIAL                90364
INDEX    DC    CL8' ',C' '   HIGH-LEVEL INDEX OF TRUE NAME + DLTX BLK
MEMBER   DC    CL8' '        MEMBER NAME FOR PDS MEMBER DELETE   90364
WORKBLKN EQU   *-WORKBLK     LENGTH TO MAKE BLANK                90364
         SPACE 1                                                 90364
WORKCLR  DS    0D            START OF ZEROING                    90364
BPAMDCB  DCB   DDNAME=RETURNDD,DSORG=PO,MACRF=W                  90364
BPAMDCBN EQU   *-BPAMDCB                                         90364
CATWORK  DS    35D'0'        CATALOG WORK/RETURN AREA            90364
CATVOL#  EQU   CATWORK,2,C'H'  NUMBER OF VOLUMES                 90364
CATDEV   EQU   CATWORK+2,4,C'X'  CATALOG DEVICE TYPE             90364
CATVOL   EQU   CATWORK+6,6,C'C'  VOLUME SERIAL                   90364
VOLSEQ   EQU   CATWORK+12,2,C'H'  SCRATCH RETURN CODE            90364
         SPACE 1                                                 90364
CURRFG   DS    X             FLAGS FOR THIS DSN                  90364
WORKCLRN EQU   *-WORKCLR     LENGTH TO ZERO                      90364
         SPACE 1                                                 90364
SCANTAB  DC    XL256'00'     TRT SCANNING TABLE                  90364
         SPACE 1                                                 90364
HSMWORK  DC    XL200'00'     HSM WORD AREA                      GP99062
SAVEEND  EQU   *
         SPACE 1                                                 90364
         CVT   DSECT=YES                                         90364
         IHACDE ,                                                90364
         IHAPSA ,            LOW                                 90364
         IKJTCB ,                                                90364
         IEFUCBOB ,                                              90364
         IECSDSL1 1          MAP FORMAT 1                        91328
         DCBD  DSORG=PO,DEVD=DA                                  90364
         IEFZB4D0 ,                                              90364
         IEFZB4D2 ,                                              90364
         END   ,                                                 90364
