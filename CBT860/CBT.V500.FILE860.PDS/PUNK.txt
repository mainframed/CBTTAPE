PUNK     TITLE 'P U N K  ***  LIST/PUNCH/SCAN DISK DATASETS'
         MACRO ,                                                 91002
&NM      DESTY &CF,&F1,&F2,&W1,&W2,&WIDTH=0                      91002
&NM      DC    CL9'&CF.* ',AL1(&F1,&F2),AL2(&W1,&W2,&WIDTH)     GP02349
         MEND  ,                                                 91002
         SPACE 1                                                 91002
         PUNCH '  ALIAS PDSSCAN '                               GP05035
         PUNCH '   SETCODE AC(1) '                              GP05035
         PUNCH ' ORDER PUNK(P) '   ***** MAKE DUMPS EASIER *****
         SPACE 1                                                 91002
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES    SET LOCAL OPTIONS
         SPACE 1
***********************************************************************
*                                                                     *
*                                                                     *
*        COPYRIGHT 1981,1989,1990  EXPERT SYSTEM PROGRAMMING, INC.    *
*        COPYRIGHT 2002-2003       EXPERT SYSTEM PROGRAMMING          *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         SPACE 2
*     PDSSCAN SCANS THROUGH CARD AND WYLBUR FORMAT DISK DATASETS
*              FOR CHARACTER STRINGS SPECIFIED VIA SYSIN. ON MATCH
*              PRODUCES MEMBER NAME AND MATCHING CARD(S).        81271
*                                                                90260
*     PUNK  PROCESSES CARD AND WYLBUR FORMAT DISK DATASETS.
*              SCAN OPTIONS CAUSE THE ENTIRE MEMBER TO BE PROCESSED
*              ON A MATCH.  LIST=SCAN PRINTS ONLY SCAN MATCHES.
*                                                                90260
*        MODIFIED TO HANDLE RECFM=F AND RECFM=V WITH LRECL<256, BUT
*        RECORDS TRUNCATED TO 100 BYTES FOR PRINTING, AND TRUNCATED
*        OR PADDED TO 80 BYTES FOR 'PUNCHING'.                   89217
*                                                                90260
*        INPUT: ONE OR MORE DD CARDS SPECIFYING A DISK DATASET
*              DSORG=PS/PO/DA  RECFM=U (WYLBUR EDIT) OR F,80     81271
*              DDNAME IS OF THE FORM SYSUTXXX, WHERE XXX IS UNIQUE.
*              A MEMBER NAME ON THE DD IS TREATED AS A MASK,     81271
*              FILLED WITH TRAILING BLANKS. A MINUS SIGN IS USED TO
*              PERMIT ANY CHARACTER TO MATCH THE MASK POSITION, BUT
*              IS INVALID IN THE FIRST CHARACTER POSITION.
*                (MINUS SIGNS ARE PASSED THROUGH THE JCL SCAN ON SOME
*                VERSIONS OF THE OPERATING SYSTEM; ON OURS SPECIAL
*                CODE IN SMF EXIT IEFUJV PRESERVES THE NAME)     90260
*              DD CARDS MAY SPECIFY DIFFERENT TYPES FOR THE SAME
*              RUN, BUT MUST NOT BE CONCATENATED.
*           DSNAMES ON THE DD CARD MAY ALSO SPECIFY MASK OPTIONS.
*              A TRAILING VALUE OF DSN.ALL WILL TREAT THE LEADING
*              TEXT AS A MASK FILLED WITH MINUS SIGNS.           81271
*              A VALUE OF PDS.ALL WILL RESTRICT THE DSCB SEARCH ON
*              THE VOLUME TO PARTITIONED DATASETS.               81271
*              ANY MINUS SIGN IN THE NAME IS TREATED AS A MASK REQUEST.
*              IF NEITHER PDS.ALL NOR DSN.ALL IS SPECIFIED, THE MASK
*              WILL NOT BE FILLED WITH MINUS SIGNS, BUT BLANKS.  81271
*           A SYSUT DD CARD MAY ALSO SPECIFY A VSAM DATASET, A JES
*              SYSIN DATASET OR AN UNCONCATENATED TAPE DATASET.  90260
*              NO MEMBER NAME OR '.ALL' OPTIONS ARE AVAILABLE FOR THESE
*                                                                90260
*                                                                90260
*        SYSIN:  (OPTIONAL) CARDS SPECIFYING TEXT TO BE SEARCHED FOR.
*          FORM IS 'STRING' (STARTCOL,ENDCOL)   OR
*          "STRING" (STARTCOL)  OR X'HEXTTEXT' (COL...)          81271
*          EMBEDDED QUOTES/APOSTROPHES MUST BE DOUBLED IF THEY MATCH
*          THE FRAMING CHARACTERS; SINGLE IF THE OPPOSITE FRAMING IS
*          USED. E.G. :                                          81271
*            "DON'T"   OR 'DON''T'   BUT NOT 'DON'T'
*              'ENDCOL' IS LAST COLUMN OF START CHARACTER.
*                                                               GP13234
*          P'.....' DEFINES A STRING OF ONE TO n SPECIAL CODES. GP13234
*          THIS FORMAT IS VALID FOR SEARCH STRINGS ONLY, BUT    GP13234
*          NOT REPLACE STRINGS. THE CODES ARE:                  GP13234
*          =  ANY CHARACTER                                     GP13234
*          ¬ or ~   ANY NON-BLANK                               GP13234
*          .  ANY NON-DISPLAYABLE CHARACTER                     GP13234
*          #  ANY NUMERICAL CHARACTER (DIGIT 0-9)               GP13234
*          -  ANY NON-NUMERIC CHARACTER                         GP13234
*          @  ANY ALPHABETIC CHARACTER OF ANY CASE              GP13234
*          <  ANY LOWER CASE ALPHABETIC CHARACTER               GP13234
*          >  ANY UPPER CASE ALPHABETIC CHARACTER               GP13234
*          $  ANY SPECIAL CHARACTER, NOT ALPHA NOR NUMERIC      GP13234
*          ^  ANY UPPER CASE ALPHABETIC OR NATIONAL             GP13234
*          %  ANY NUMERIC, NATIONAL, OR UPPER CASE ALPHABETIC   GP13234
*          \  NO NUMERIC, NATIONAL, OR UPPER CASE ALPHABETIC    GP13234
*                                                               GP13234
*           E.G, TO SEARCH FOR 8-CHAR MEMBER NAMES, REQUEST     GP13234
*             P'^%%%%%%%'                                       GP13234
*                                                               GP13234
*           TO EDIT ANY OF THE ABOVE, FIND THE TRTNnnn TABLE.   GP13234
*           TO ADD ONE, EDIT TRTXWHAT, EXPTAB, AND ADD A TABLE. GP13234
*          A SECOND STRING ON THE SAME CARD IS TREATED AS A REPLACEMENT
*          REQUEST (MUST BE THE SAME LENGTH AS SEARCH STRING).   90201
*           SYNTAX: 'STRING' Ý(COL,SPEC)¨ 'REP.STRING'           90291
*          ON A MATCH, THE REPLACEMENT IS MADE BEFORE PROCESSING THE
*          RECORD (PRINT/PUNCH). CURRENTLY WYLBUR EDIT DATASETS MAY
*          NOT BE UPDATED IN-PLACE. (SEE INPLACE PARM BELOW)     90201
*                                                                90260
*        OUTPUT: AT LEAST ONE OF FOUR DD CARDS MUST BE SUPPLIED:
*          SUMPRINT (OR SYSTERM)- VBA,133  INDEX AND ERROR MESSAGES
*          SYSPRINT - VBA,133 LISTING WITH HEADINGS
*          SYSPUNCH - FB,80   PUNCH DECK, NO SEPARATION BETWEEN MEMBERS
*          SYSUPDTE - FB,80   IEBUPDTE INPUT FORMAT, WITH ./ ADD CARDS
*                                                                90260
*                                                                90260
*        PARM OPTIONS:
*                                                                90260
*          %   PRODUCE MICROFICHE EYEBALL HEADERS (12 LINE MEMBER NAME)
*          &   INCLUDE ONLY MEMBERS WITH ' MACRO ' ON FIRST CARD
*          ¬   EXCLUDE MEMBERS WITH ' MACRO ' ON FIRST CARD (COL 1-16)
*          #   PLACE WYLBUR LINE NUMBERS IN COLUMNS 73-80
*          INTEGER    -"-                                        81292
*          EDIT  PLACE WYLBUR LINE NUMBERS IN 73-80 IN EDIT FORMAT
*          HWD OR HALFWORD - INPUT LINE NUMBERS IN RAND FORMAT   81292
*          OSI   INPUT LINE NUMBERS IN OSI FORMAT                81292
*          >NAME  - PROCESS MEMBERS .GE. SPECIFIED NAME ONLY
*          <NAME  - PROCESS MEMBERS .LE. SPECIFIED NAME ONLY
*          =MASK  - PROCESS MEMBERS MATCHING THE MASK ONLY; THIS MASK
*              IS OVERRIDDEN BY A DD CARD MASK.  MASK IS FILLED WITH
*              TRAILING BLANKS IF SHORT; MINUS SIGN IS USED TO MATCH
*              ANY CHARACTER.
*          /MASK  - SKIP MEMBERS MATCHING THE MASK. MASK IS FILLED WITH
*              MINUS SIGNS IF SHORT. BLANKS ARE PERMITTED (MATCHED),
*              MINUS SIGNS WILL PERMIT ANY CHARACTER.
*          :SSIMASK - MASK TO MATCH SSI FIELD. FILLED WITH MINUS SIGNS
*              IF SHORT. SSI INFORMATION IS UNPACKED PRIOR TO TEST.
*              IF SSI DOES NOT EXIST, A VALUE OF ZERO WILL BE USED.
*              NOTE THAT ONLY MINUS AND HEX BYTES (0-9,A-F) ARE VALID.
*          UP= ADD, REP OR CHG - SPECIFIES THE IEBUPDTE CONTROL VERB
*              TO BE GENERATED.                                  81271
*          UPLIST=ALL - REQUESTS LIST=ALL                        81271
*          UPLIST=NO  - SUPPRESSES LIST=ALL                      81271
*                                                                81347
*          START=    STARTING RELATIVE LINE TO BE PRINTED        81347
*                    DEFAULT IS 1; DOES NOT AFFECT OTHER OUTPUT  81347
*          END=      END LINE TO BE PRINTED                      81347
*          INCR=     INCREMENT AT WHICH TO PRINT LINES           81347
*          LIMIT= OR LIM=  MAXIMUM LINES TO PRINT (PER DSN/MEMBER)
*                                                                81347
*          ALIAS=SKIP   IGNORE ALIAS ENTRIES (DEFAULT)           81347
*          ALIAS= OR ALIAS=MEMBER  TREAT ALIAS ENTRIES AS MEMBERS
*          FOLD     TREAT ALL INPUT (AND SCAN TEXT) AS UPPER CASE
*          UPDATE=INPLACE  OR JUST  INPLACE - CAUSES SCAN CARD STRINGS
*              RESULTING IN SUBSTITUTION TO BE REWRITTEN ON THE INPUT
*              FILE. NOT HONORED EXCEPT APF (AC=1) MODE ON SEQUENTIAL
*              AND PARTITIONED DATASETS. NO WYLBUR SUPPORT (YET)?
*                                                                90260
*    PROCLIST/PROCSCAN USE A LOCAL SSI MASK; OTHERWISE SAME AS
*              PUNK AND PDSSCAN, RESPECTIVELY.
*
*        ATTRIBUTES :  RE-ENTRANT, REFRESHABLE, AC=1
*              WHEN INVOKED UNAUTHORIZED, OR FOR DATASETS NOT    90260
*              PROCESSABLE HERE WITH @VOLREAD, @INPREAD IS USED  90260
*              INSTEAD. VTOC.ALL AND DSN.ALL ARE HANDLED BY      90260
*              @OBTAINS INSTEAD. UPDATE INPLACE MODE IS NOT AVAILABLE
*              WITH THAT INTERFACE.                              90260
*                                                                90260
*        DEPENDENCIES: LOCAL MACROS;
*              LINKED MODULES @SERVICE, @PRINTER, @VOLREAD, @INPREAD
         SPACE 2
         PRINT &PRTSOR
PUNK   PGMHEAD BIGZERO,BASE=(R10,R11,R12),PARM=R9,BNDRY=PAGE,ENTNO=DB, *
               AM=31,RM=24,     DCBS BELOW THE LINE                    *
               ENTRY=(PDSSCAN,PROCLIST,PROCSCAN)    REWRITTEN ON 81264
         MVC   GINCLUDE,GENMATCH  DEFAULT - NO INCLUDE MATCH     81271
         MVC   MEXCLUDE,GENMATCH  AND NO EXCLUDE MATCH           81271
         MVC   SSIMASK,GENMATCH                                  81271
         MVC   HINAME,GENMHIGH  SET FF FOR HIGH END OF RANGE
         MVC   SSIHIGH,GENMHIGH
         LA    R0,1                                              81271
         ST    R0,RECONE     START PRINT RECORD NUMBER           81271
         ST    R0,RECINC     PRINT INCREMENT                     81271
         MVI   WORKLINE-1,C' '  INITIALIZE BLANK
         MVC   WORKLINE,WORKLINE-1  CLEAR WORK AREA
         MVI   MODEFG,MFULIST    ADD LIST=ALL FOR UPDTE          81271
         SERVINIT ,          LOAD AND SET @SERVICE ROUTINE
         IC    R0,DB         LOAD ENTRY FLAG (0-3)              GP13234
         BIX   BASE=*,ERR=SETPUNK,PFX=SET,LOC=(PUNK,SCAN,PROL,PROS)
         SPACE 1
SETPROL  MVI   MODEFG,MFREP+MFULIST  UPDTE: ./ REPL LIST=ALL     81271
         MVI   FLAGS,0       CLEAR FLAG AGAIN
SETPROS  SERVCALL ACGET,DB   GET CURRENT ACCOUNT NUMBER
         SERVCALL ACTST,DB
         STCM  R0,12,DB      STASH BINARY ACCOUNT EQUIVALENT
         UNPK  DB+3(5),DB(3)  UNPACK IT
         MVC   SSIMASK(4),DB+3  MOVE TO MASK FIELD
         TR    SSIMASK(4),HEXTAB  MAKE EBCDIC
         B     SETPUNK                                          GP13234
SETSCAN  MVI   FLAGS,FGSCAN  SET PDSSCAN MODE
         MVI   MODEFG,MFCHG     UPDTE: CHANGE, NO LIST=ALL       81271
SETPUNK  MVC   TABOFFLO,=H'32767'  INITIALIZE SCAN RANGE        GP13234
         MVC   GETMAIN,GETMEAN  MOVE GETMAIN PATTERN
         MVC   SYSPRINT(PATPRTLN),PATPRINT  INIT SYSPRINT WORK
         SERVLOAD @PRINTER,@VOLREAD,(@INPREAD,,N)               GP13234
         L     R0,@VOLREAD   GET IT BACK                        GP10197
         ST    R0,@VOLSAVE   ALSO SAVE FOR BACKUP                90260
         PRTOPEN SYSPRINT,DEV=1,OPT=(NOWTO,DUMMY)
         CH    R15,=H'4'     WAS IT OPENED ?                     91002
         BH    INITNSYP      NO                                  91002
         OI    OUTUNIT,OUPRT   SET SYSPRINT PRESENT
         LR    R15,R0        COPY THE PRINT WORK AREA            91002
         ST    R15,@PRTWRK   SAVE PRINTER WORK AREA ADDRESS      91002
         USING PRTMAP,R15    DECLARE IT                          91002
         MVC   STYLE(1),PWSOUTCL  MOVE SYSOUT CLASS              91002
         MVC   STYLE+1(8),PWSOUTFM  AND FORM NAME                91002
         DROP  R15                                               91002
INITNSYP PRTOPEN SUMPRINT,DEV=2,OPT=(DUMMY,NOWTO)                91002
         CH    R15,=H'4'     NORMAL OR ALIAS OPEN ?              81271
         BH    *+8           NEITHER; ERROR                      81271
         OI    OUTUNIT,OUSUM   SET SUMPRINT PRESENT
         PRTOPEN SYSPUNCH,DEV=3,OPT=(NOWTO,DUMMY)
         BXH   R15,R15,*+8
         OI    OUTUNIT,OUPUN   SET SYSPUNCH PRESENT
         PRTOPEN SYSUPDTE,DEV=4,OPT=(NOWTO,DUMMY)
         BXH   R15,R15,*+8
         OI    OUTUNIT,OUPDT   SET SYSUPDTE PRESENT
         PRTOPEN CONSOLE,DEV=5
         TM    OUTUNIT,OUPRT+OUSUM  ANY PRINT FILE ?
         BNZ   INITHPRT      YES
         PRTV  MSGBDPRT,DEV=5,CC=NO   WRITE MISSING DD MESSAGE
         TM    OUTUNIT,OUPUN+OUPDT  ANY OUTPUT DEVICE ?
         BNZ   INITHPRT      YES; KEEP GOING
         SERVTERM ,          CLOSE EVERYTHING
         ABEND 1403
MSGBDPRT VCON  'NEITHER SYSPRINT NOR SYSTERM/SUMPRINT DD USABLE' 81271
         SPACE 1
INITHPRT MVC   ERRUNIT,OUTUNIT   COPY UNIT BITS
         NI    ERRUNIT,OUPRT+OUSUM  KILL NON-PRINT BITS
         TM    ERRUNIT,OUSUM   SUMPRINT ON ?
         BZ    *+8           NO
         NI    ERRUNIT,255-OUPRT  RESET SYSPRINT
         PRTLIST SUMTIT1,DEV=(1,2),TITLE=1  SUMMARY HEADER
         TESTAUTH FCTN=1     ARE WE AUTHORIZED ?                 81343
         BXLE  R15,R15,HAVEAUTH   YES                            81343
         PRTV  MSGUNAUT,DEV==ERRUNIT  PRINT ERROR MESSAGE        81343
         OI    FLAG2,FGNOTAUT  SHOW UNAUTHORIZED RUN             90260
         SERVCALL LPADL,=CL8'@VOLREAD'  FREE THE VOLUME READER   90260
         L     R0,=A(VOLFAKE)  MAKE A FAKE INTERFACE             90260
         ST    R0,@VOLREAD     INSTEAD                           90260
         ST    R0,@VOLSAVE   ALSO SAVE FOR BACKUP                90260
         SPACE 1                                                 81343
         PUSH  USING                                            GP97241
HAVEAUTH LTCB  R2            GET OUR TCB                         89151
         USING TCB,R2
         L     R2,TCBJSCB
         USING IEZJSCB,R2
         MODESET KEY=ZERO                                       GP11143
         OI    JSCBSWT1,JSCBPASS  CHEAT                         GP11143
         MODESET KEY=NZERO                                      GP11143
         L     R7,JSCBCSCB
         USING CHAIN,R7
         ST    R7,CSCBPTR
         AIF   (NOT &MVSESA).NOCSXB                             GP97218
         L     R6,CHCSCXP    GET EXTENSION                      GP97218
         USING CSCX,R6       DECLARE IT                         GP97218
.NOCSXB  QEDIT ORIGIN=CHCIBP                                    GP97218
         QEDIT ORIGIN=CHCIBP,CIBCTR=1  1 MODIFY AT A TIME
         POP   USING                                            GP97241
         LA    R7,0(,R9)     CLEAN PARM ADDRESS                 GP97219
         LTR   R7,R7         ANY PARM ADDRESS ?
         BZ    PARMDFLT      NO; USE DEFAULTS
         ICM   R7,7,1(R7)    LOAD AND TEST PARM ADDRESS
         BZ    PARMDFLT      NONE
         SLR   R9,R9
         ICM   R9,3,0(R7)    LOAD AND TEST PARM LENGTH
         BZ    PARMDFLT      NONE
         LA    R8,100        OS MAXIMUM
         CR    R9,R8
         BNH   *+6           OK
         LR    R9,R8         TRUNCATE
         BCTR  R9,0          SET FOR EXECUTE
         EX    R9,PARMMOVE   MOVE PARM TO WORK AREA
         LA    R7,WORKLINE+2(R9)  POINT TO LAST BYTE +1
         MVI   0(R7),C','    SET SCAN STOPPER
         LA    R7,WORKLINE+1  POINT TO START OF PARM
         SLR   R8,R8         CLEAR BXLE INCREMENT REGISTER
         AR    R9,R7         SET LAST PARM ADDRESS
         SPACE 1
PARMLOOP L     R4,=A(PARMTAB)  GET VERB TABLE FOR PARMS          90154
PARMVERB ICM   R8,1,0(R4)    INSERT AND TEST LENGTH BYTE
         BM    PARMERR       NO MATCH - DISPLAY BAD DATA
         EX    R8,PARMCLC    MATCH ?
         BE    PARMGO        YES; GO TO APPROPRIATE ENTRY
         LA    R4,4(R8,R4)   POSITION TO NEXT PARM
         B     PARMVERB      TRY NEXT ONE
PARMGO   SLR   R15,R15
         ICM   R15,3,1(R4)   GET BRANCH OFFSET
         B     PUNK(R15)     ENTER SUBROUTINE
PARMMOVE MVC   WORKLINE+1(0),2(R7)  MOVE PARM FIELD
PARMCLC  CLC   0(0,R7),3(R4)        COMPARE VERB
PARMBADM VCON  ' ***** ERROR IN PARM FIELD *****'
         SPACE 1
PARMERR  PRTV  PARMBADM,DEV==ERRUNIT   PRINT ERROR IN PARM
         PRTLIST PRTWORKL,DEV==ERRUNIT SHOW FULL PARM           GP02247
         MVC   WORKLINE,WORKLINE-1  CLEAR PARM FIELD
         MVI   0(R7),C'|'    POINT TO BAD PARAMETER
         PRTLIST PRTWORKL,DEV==ERRUNIT SHOW FULL PARM           GP02247
         SERVTERM ,          CLOSE AND FREE
         ABEND 100           TERMINATE
         SPACE 1
PRMHITS  TM    FLAGS,FGSCAN  SCAN MODE ?
         BNZ   PARMBUMP      YES; LIST=HITS IS NOP
         OI    FLAGS,FGLSCAN SET FOR LIST OF HITS ONLY
         B     PARMBUMP
PRMALIAS OI    FLAGS,FGDOAL  PROCESS ALIAS ENTRIES AS MEMBERS
         B     PARMBUMP
         SPACE 1                                                 81271
PRMUADD  NI    MODEFG,255-MFADD  SET ./ ADD                      81271
         B     PARMBUMP                                          81271
PRMUREP  OI    MODEFG,MFREP      SET ./ REPL                     81271
         B     PARMBUMP                                          81271
PRMUCHG  NI    MODEFG,255-MFADD                                  81271
         OI    MODEFG,MFCHG      SET ./ CHANGE                   81271
         B     PARMBUMP                                          81271
PRMULSTY OI    MODEFG,MFULIST    SET ./ LIST=ALL,                81271
         B     PARMBUMP                                          81271
PRMULSTN NI    MODEFG,255-MFULIST  ./ WITHOUT LIST=ALL           81271
         B     PARMBUMP                                          81271
PRMFOLD  OI    MODEFG,MFFOLD   SET SCAN IN FOLD MODE             87032
         B     PARMBUMP                                          87032
PRMNFOLD NI    MODEFG,255-MFFOLD  RESET FOLD MODE                87032
         B     PARMBUMP                                          87032
PRMUPINP TM    FLAG2,FGNOTAUT  UNAUTHORIZED ?                    90260
         BNZ   PARMBUMP      YES; REJECT UPDATE INPLACE          90260
         OI    FLAG2,FGUPDATE  SET UPDATE INPLACE                90260
         B     PARMBUMP                                          90260
         SPACE 1
PRMEYE   TM    FLAGS,FGEYE   PREVIOUSLY SPECIFIED ?
         BNZ   PARMBUMP      YES; IGNORE DUPLICATE
         OI    FLAGS,FGEYE   SET OPTION
         SERVCALL LPALD,=CL8'IEFSD095' PREVENT LATER FRAGMENTATION
         B     PARMBUMP
PRMMAC   TM    FLAGS,FGNMAC  NON-MACROS SPECIFIED ?
         BNZ   PARMERR       YES; CONFLICTS
         OI    FLAGS,FGSMAC  SEARCH FOR MACROS ONLY
         B     PARMBUMP      DO NEXT PARM
PRMNMAC  TM    FLAGS,FGSMAC  MACROS SPECIFIED ?
         BNZ   PARMERR       YES; CONFLICTS
         OI    FLAGS,FGNMAC  SEARCH FOR NON-MACROS ONLY
         B     PARMBUMP      DO NEXT PARM
PRMWINT  TM    FLAGS,FGWEDIT  PRIOR EDIT SPECIFICATION ?
         BNZ   PARMERR       YES; CONFLICTS
         OI    FLAGS,FGWINT  SET INTEGER OPTION
         B     PARMBUMP      GET ANOTHER
PRMWEDIT TM    FLAGS,FGWINT  PRIOR INTEGER SPECIFICATION ?
         BNZ   PARMERR       YES; CONFLICTS
         OI    FLAGS,FGWEDIT   SET EDIT MODE
         B     PARMBUMP      GET ANOTHER
PRMWHWD  TM    WYLMODE,DCMF1OSI  PRIOR SPECIFICATION ?           81292
         BNZ   PARMERR                                           81292
         OI    WYLMODE,DCMF1HWD  SET HALF-WORD LINE NUMBERS      81292
         B     PARMBUMP                                          81292
PRMWOSI  TM    WYLMODE,DCMF1HWD  PRIOR SPECIFICATION ?           81292
         BNZ   PARMERR                                           81292
         OI    WYLMODE,DCMF1OSI  SET OSI FORMAT                  81292
         B     PARMBUMP                                          81292
PRTNONE  OI    FLAG3,FGNOID+FGNONO  INHIBIT RECORD INFO FIELDS   91002
         B     PARMBUMP                                          91002
PRTNONO  OI    FLAG3,FGNONO         INHIBIT RECORD NUMBER FIELD  91002
         B     PARMBUMP                                          91002
PRTNOID  OI    FLAG3,FGNOID         INHIBIT RECORD ID/OFFSET     91002
         B     PARMBUMP                                          91002
PRMGE    LA    R5,LONAME     SET FOR LOW NAME
         B     PRMCLONG      SET LONG LIST OF VALID BYTES
PRMLE    LA    R5,HINAME     SET FOR HIGHEST NAME
         B     PRMCLONG
PRMMASK  LA    R5,GINCLUDE   SET INCLUDE MASK                    81271
*        TM    0(R5),X'FF'   PREVIOUSLY SPECIFIED ?
*        BM    PARMERR       YES; TOO BAD
*        MVC   GINCLUDE,WORKLINE+102  MAKE ALL BLANKS            81271
         B     PRMCLONG
PRMSKIP  LA    R5,MEXCLUDE   SET EXCLUDE MASK                    81271
         B     PRMCLONG
PRMSSLE  LA    R5,SSIHIGH    SSI HIGH VALUE
         B     PRMSSICM
PRMSSGE  LA    R5,SSILOW     SSI LOW VALUE
         B     PRMSSICM
PRMSSI   LA    R5,SSIMASK    SET SSI MASK
PRMSSICM LA    R15,16+1      ONLY SIXTEEN HEX BYTES AND -  ARE VALID
         B     PRMCTEST
PRMCLONG LA    R15,L'ALPHAS  SET NUMBER OF VALID BYTES
PRMCTEST CLC   GENMATCH,0(R5)  PRESET VALUE ?
         BE    PRMTPASS      YES
         TM    0(R5),X'FF'   PREVIOUSLY SPECIFIED ?
         BM    PARMERR       YES; DUPLICATE
PRMTPASS LA    R8,1(,R8)     MAKE TRUE LENGTH TO SKIP
         BXH   R7,R8,PARMDONE  EXIT IF NULL
         LA    R4,L'GINCLUDE  SET MASK LENGTH                    81271
PRMCWORD SLR   R8,R8         LENGTH OF ONE
         CLI   0(R7),C','    END OF FIELD ?
         BE    PARMBUMP      YES; EXIT
         LA    R14,ALPHAS    POINT TO TABLE OF VALID CHARACTERS
         LR    R0,R15        COPY NUMBER TO CHECK
PRMCLOOP CLC   0(1,R14),0(R7)  VALID BYTE ?
         BE    PRMCMOVE      YES; MOVE IT
         LA    R14,1(,R14)   BUMP
         BCT   R0,PRMCLOOP   TRY AGAIN
         B     PARMBUMP+4    INVALID; SEE IF ANOTHER OPTION
PRMCMOVE MVC   0(1,R5),0(R7)  MOVE ONE BYTE
         LA    R5,1(,R5)
         BCT   R4,*+8        SKIP IF NOT DONE YET
         B     PARMBUMP      ELSE TRY FOR ANOTHER PARM
         LA    R8,1          SET TRUE LENGTH AGAIN
         BXLE  R7,R8,PRMCWORD  TRY NEXT BYTE
         B     PARMDONE      ELSE END OF PARM
         SPACE 1
PRMRONE  LA    R5,RECONE     SET STARTING LIST RECORD NUMBER     81271
         B     PARMINTG        GO TO INTEGER PARM PROCESSING     81271
PRMREND  LA    R5,RECEND     SET END LIST RECORD                 81271
         B     PARMINTG                                          81271
PRMRINC  LA    R5,RECINC     RECORD NUMBER INCREMENT             81271
         B     PARMINTG                                          81271
PRMRLIM  LA    R5,RECLIM     PRINT LIMIT PER MEMBER              81271
         SPACE 1                                                 81271
PARMINTG LA    R7,1(R7,R8)   SPACE TO FIRST BYTE
         BAS   R14,PARMNBLK  GET FIRST NON-BLANK
         SLR   R0,R0
         LA    R1,C'0'       SET FOR NULL PROCESSING             82031
         CLI   0(R7),C','    NULL ?
         BE    PARMINUL      YES
         CLI   0(R7),C' '    END OF PARM ?
         BE    PARMINUL      YES; STOP
         LA    R8,1          SET INCREMENT FOR LOCAL BXLE
         BCTR  R7,0          BACKSPACE
         LA    R14,8         MAX INTEGER LENGTH PERMITTED
PARMILOP BXH   R7,R8,PARMIDON  SKIP IF DONE
         CLI   0(R7),C'0'    NUMERIC ?
         BL    PARMIDON      NO
         CLI   0(R7),C'9'
         BH    PARMIDON
         SLDL  R0,8
         IC    R1,0(,R7)     LOAD NEXT BYTE
         BCT   R14,PARMILOP  TRY AGAIN
         B     PARMINUL
PARMIDON BCTR  R7,0          BACKSPACE
PARMINUL SLR   R8,R8         SET LENGTH-1 TO SKIP AFTER
         STM   R0,R1,UPD     STASH VALUE
         PACK  DB,UPD
         CVB   R0,DB
         ST    R0,0(,R5)     STASH VALUE
         LTR   R0,R0         CHECK VALUE                         81271
         BP    PARMBUMP      POSITIVE; CONTINUE SCAN             81271
         BCTR  R7,0          BACKSPACE FOR ERROR POINTER         81271
         B     PARMERR       SIGNAL ERROR                        81271
         SPACE 1                                                 91002
PRMSTYLE LA    R5,1(R7,R8)   POINT TO NEXT BYTE                  91002
         CLI   0(R5),C' '    NULL PARM ?                         91002
         BE    PARMBUMP      YES; SKIP OUT                       91002
         CLI   0(R5),C','    ALTERNATE ?                         91002
         BE    PARMBUMP      YES                                 91002
         LA    R5,STYLE      POINT TO STYLE ENTRY                91002
         MVC   STYLE,WORKLINE+102  BLANK OLD VALUE               91002
         B     PRMCLONG      PROCESS IT                          91002
         SPACE 1
PARMNBLK LA    R8,1          SET SKIP LENGTH
         BCTR  R7,0          BACKUP ONE
PARMNBLL BXH   R7,R8,0(R14)  RETURN IF END OF FIELD
         CLI   0(R7),C' '    EMBEDDED BLANK ?
         BNER  R14           NO; RETURN
         B     PARMNBLL      ELSE TRY AGAIN
         SPACE 1
PARMBUMP LA    R8,1(,R8)     GET TRUE LENGTH
         BXLE  R7,R8,PARMLOOP  EXAMINE NEXT PARM
         SPACE 1
PARMDONE CLC   GINCLUDE,WORKLINE+102  BLANK MASK ?               81271
         BNE   PARMDFLT      NO
         MVC   GINCLUDE,GENMATCH   RESET IT                      81271
PARMDFLT MVC   WORKLINE,WORKLINE-1   BLANK WORK LINE AGAIN
         L     R8,=A(GETSTYLE)  GET ADDRESS OF STYLE LOOK-UP     91002
         BASR  R14,R8        CALL IT
         L     R8,=A(GETSCAN)  GET THE SCANNER INPUT CODE        90201
         BR    R8            CODE RETURNS TO LOOKTIOT            90201
         SPACE 2
ERRTIOT  PRTSPACE 2,DEV==ERRUNIT
         PRTLIST MTIOTERR,DEV==ERRUNIT   (R3) HAS FD POINTER
         OI    RETCC,CCWARN
         SPACE 1
LOOPTIOT TM    FLAG2,FGNOTAUT+FGNODASD+FGNOSEQ  COMPATIBILITY MODE ?
         BZ    LOOPTTWO      NO                                  90260
         VOLREAD CLOSE       DO REAL CLOSE                       90260
LOOPTTWO NI    FLAG2,255-FGNODASD-FGNOSEQ  RESTORE               90260
         NI    MODEFG,255-MFUND-MFWYL-MFVAR  RESET               90260
         MVC   @VOLREAD,@VOLSAVE RESTORE                         90260
         TM    DSNFLAG,DFDSN  DSN MASK PROCESSING ?              81271
         BNZ   LOOPDSCB      YES; LOOK FOR NEXT DSCB ON VOLUME   81271
         VOLREAD TCLOSE      ELSE RELEASE VOLUME                 81271
LOOKTIOT L     R1,ADDRTIOT   GET PRIOR TIOT ADDRESS
         SERVCALL TIOLP      LOOP THROUGH ALL ENTRIES
         LTR   R2,R0         ANY MORE ?
         BZ    ENDALL        NO
         ST    R2,ADDRTIOT   SAVE FOR RESTART
         USING TIOELNGH,R2   DECLARE IT
         CLC   =C'SYSUT',TIOEDDNM  ONE OF OURS ?
         BNE   LOOKTIOT      NO; SKIP
         LA    R3,MTIODUPL   SET FOR DUPLICATE DD
         SERVCALL TIODD,TIOEDDNM  SCAN BY DDNAME
         CR    R2,R0         DUPLICATE DD ENTRY ?
         BNE   ERRTIOT       YES; SKIP
         SR    R4,R4         CLEAR UCB ADDRESS                   90260
         TM    TIOELINK,TIOESYIN+TIOESSDS  SYSIN (SPOOLED) DATA ?
         BNZ   CONCTIOT      YES; CHECK CONCATENATION            90260
         LA    R3,MTIODUMM   SET FOR DD DUMMY MESSAGE
         CLI   TIOELNGH,20   MINIMUM LENGTH ?
         BL    ERRTIOT       NO; WRITE ERROR MESSAGE
         ICM   R1,OIII,TIOEFSRT  TEST FOR DUMMY DD AND LOAD UCB GP02245
         BZ    ERRTIOT       DUMMY; TOO BAD, DUMMY
         SERVCALL SWAAB,(R1)  CONVERT POSSIBLE TOKEN            GP03105
         BXH   R15,R15,ERRTIOT  OOPS ?                          GP02245
         LR    R4,R1         SAVE FOR LATER                     GP02245
         LA    R3,MTIONDA    POINT TO NON-DASD MSG
         USING UCBOB,R4
         CLI   UCBTBYT3,UCB3TAPE  ALTERNATE SEQUENTIAL INPUT ?   90260
         BE    CONCTIOT      YES                                 90260
         CLI   UCBTBYT3,UCB3DACC  DASD ?
         BNE   ERRTIOT       NO
CONCTIOT LA    R3,MTIOMVOL                                       90260
         CLI   TIOELNGH,20   MORE THAN ONE UNIT ?
         BH    ERRTIOT       YES; NOT SUPPORTED
         CLI   TIOELNGH+20,0  END OF TIOT ?
         BE    USETIOT       YES; USE IT
         CLI   TIOEDDNM+20,C' '  CONCATENATION ?
         BNE   USETIOT       NO; USE IT
         LA    R3,MTIOCONC   SET FOR INVALID CONCATENATION
         PRTSPACE 2,DEV==ERRUNIT
         PRTLIST MTIOTERR,DEV==ERRUNIT   (R3) HAS FD POINTER
         OI    RETCC,CCWARN
USETIOT  MVC   CURDDNM,TIOEDDNM  SAVE DDNAME
         LTR   R4,R4         ANY UCB ?                           90260
         BNZ   HAVEUCB       YES; PROCESS                        90260
         NI    MODEFG,255-MFDUP  PROBABLY NOT SAME DSN           90260
         XC    CURTYPE,CURTYPE  DON'T KNOW                       90260
         MVC   CURUNIT,WORKLINE+102  MOVE BLANKS                 90260
         MVC   CURVOL,=CL6'SUBSYS'                               90260
         MVC   CURDEVT,=CL8'SPOOL'                               90260
         B     NONEUCB                                           90260
HAVEUCB  CLC   CURVOL,UCBVOLI   SAME AS PRIOR VOLUME ?
         BE    *+8           YES
         NI    MODEFG,255-MFDUP  RESET DUPLICATE DD FLAG
         MVC   CURVOL,UCBVOLI  SAVE VOLSER
         UNPK  DB(5),UCBCHAN(3)                                 GP03105
         TR    DB(4),HEXTAB  MAKE PRINTABLE                     GP03105
         MVC   CURUNIT,DB    SAVE DEVICE NUMBER                 GP03105
         MVC   CURTYPE,UCBTBYT1  SAVE DEVICE OPTIONS
         SERVCALL UCBGN,(R4)   GET JCL UNIT= TYPE
         DROP  R2,R4
         MVC   CURDEVT,0(R1)   STASH IT
NONEUCB  MVC   CURDCB(PATDCBLN),PATDCB  MOVE DCB PATTERN
         MVC   DCBDDNAM-IHADCB+CURDCB,CURDDNM  SET DDNAME
         LA    R15,EXITLIST  GET EXIT LIST
         STCM  R15,7,DCBEXLSA-IHADCB+CURDCB
         LA    R14,CURDCB
         LA    R15,INFMJFCB
         STM   R14,R15,OPENLIST
         MVI   OPENLIST,X'80'   SINGLE DCB, INPUT
         MVI   EXITLIST,X'87'  JFCB ADDRESS
         LA    R3,MDDNJFCB   SET FOR JFCB FAILURE
         RDJFCB MF=(E,OPENLIST)  READ THE JFCB
         BXH   R15,R15,ERRDDN    TOO BAD
         OI    JFCBTSDM,JFCNWRIT   KEEP JOBQUEUE CHASTE, JUST IN CASE
         MVI   FONE,FODDN    SHOW AT LEAST ONE DD ENTRY FOUND    81343
         CLC   JFCBDSNM,DS1DSNAM  SAME DSN AS BEFORE ?
         BE    *+8           YES
         NI    MODEFG,255-MFDUP   RESET DUPLICATE FLAG
         TM    JFCDSORG+1,JFCORGAM  VSAM ?                       90260
         BNZ   SINGLEAM      YES; SKIP OPEN                      90260
         LA    R1,CURVOL     LOAD OPEN PARAMETER (USE=DATA)      90201
         CLI   CURTYPE+2,UCB3DACC  DASD ?                        90260
         BE    OPENUP        YES; PROCEED                        90260
         OI    FLAG2,FGNODASD  SHOW NOT DASD                     90260
         MVC   @VOLREAD,=A(VOLFAKE)  USE @INPREAD INSTEAD        90260
         B     OPENIN        ONLY INPUT PROCESSING ALLOWED       90260
OPENUP   TM    FLAG2,FGUPDATE  UPDATING IN-PLACE ?               90201
         BZ    OPENIN        NO                                  90201
         ICM   R1,8,=X'01'   SET MODE=UPDATE                     90201
OPENIN   VOLREAD OPEN,(R1)   OPEN THE VOLUME                     90201
         BXLE  R15,R15,VOLOPEN   PROCESS IT
         LA    R3,MDDNVOPN   SHOW OPEN FAILED
ERRDDN   SRA   R15,1         GET R15 BACK
         ST    R15,DB        SAVE FOR FORMATTING
         PRTSPACE 2,DEV==ERRUNIT
         PRTLIST MDDNERR,DEV==ERRUNIT  SHOW BAD OPEN
         OI    RETCC,CCWARN
         B     LOOPTIOT      TRY ANOTHER
         SPACE 1
VOLOPEN  TM    FLAG2,FGNOSEQ+FGNODASD  NO DSCB SCAN ?            90260
         BNZ   SINGLEDS      YES                                 90260
         LA    R14,JFCBDSNM                                      81271
         LR    R15,R14       PRESET FOR ZERO LENGTH IF VTOC.ALL  82144
         CLC   =C'VTOC.ALL ',JFCBDSNM  VTOC.ALL REQUEST ?        82144
         BE    MAKEMDSN      YES; TREAT AS 'DSN.ALL'             82144
         LA    R15,JFCBDSNM+37  LAST POSSIBLE POSITION FOR MASK  81271
         LA    R0,L'JFCBDSNM-6   NUMBER OF POSITIONS FOR MASK    82144
LOOPMASK CLC   =C'.ALL',3(R15)  MASK REQUEST ?                   81271
         BNE   BUMPMASK      NO; TEST FURTHER                    81271
         CLC   =C'PDS',0(R15)   PO ONLY ?                        81271
         BE    MAKEMPDS      YES                                 81271
         CLC   =C'DSN',0(R15)   PS/DA/PO ?                       81271
         BE    MAKEMDSN      YES                                 81271
BUMPMASK BCTR  R15,0                                             81271
         CLI   7(R15),C' '   TRAILING BLANK ?                    81271
         BNE   *+8           NO; QUIT                            81271
         BCT   R0,LOOPMASK   TRY AGAIN                           81271
         LA    R0,L'JFCBDSNM  LOOK FOR MASK BYTE                 81271
LOOPMSKM CLI   0(R14),C'-'   MASK ?                              81271
         BE    LOOPMSKF                                          81271
         LA    R14,1(,R14)                                       81271
         BCT   R0,LOOPMSKM                                       81271
         B     SINGLEDS      NO MASK OPTION                      81271
LOOPMSKF MVI   DSNFLAG,DFDSN  SEARCH FOR DSCBS MATCHING THE MASK 81271
         MVI   DSNMASKL,L'JFCBDSNM  FULL NAME COMPARE            81271
         MVC   DSNMASK,JFCBDSNM  MASK                            81271
         B     LOOPDSCB      SEARCH FOR FIRST HIT                81271
MAKEMPDS MVI   DSNFLAG,DFPDS+DFDSN  SEARCH FOR PO ONLY           81271
         B     *+8                                               81271
MAKEMDSN MVI   DSNFLAG,DFDSN  SEARCH ALL ELIGIBLE                81271
         SR    R15,R14       GET SPECIFIED MASK LENGTH           81271
         STC   R15,DSNMASKL  SAVE IT                             81271
         MVC   DSNMASK,JFCBDSNM  SAVE MASK                       81271
         B     LOOPDSCB      SEARCH FOR FIRST ONE                81271
         SPACE 1
SINGLEAM OI    MODEFG,MFUND  SET UNDEFINED(NON-WYLBUR) PROCESS   90260
         OI    FLAG2,FGNOSEQ  SHOW NOT SEQUENTIAL ACCESS METHOD  90260
         MVC   @VOLREAD,=A(VOLFAKE)  USE @INPREAD INSTEAD        90260
         VOLREAD OPEN,CURVOL MAKE SURE FAKE OPEN IS CALLED       90260
         SPACE 1                                                 90260
SINGLEDS LA    R3,MDDN213    SET FOR DATASET NOT FOUND           81271
         VOLREAD SEARCH,JFCBDSNM  LOOK FOR DSCB
         BXH   R15,R15,ERRDDN    NOT FOUND
         LR    R1,R0         COPY
         MVC   DS1DSNAM(LENDSCB1),0(R1)  MOVE IT
         MVC   PDSDSNEQ,=C'DSN='   RESET DSN HEADER              81271
         SERVCALL DSFMT,DS1DSORG   FORMAT DSORG/DCB VALUES       81271
         MVC   FMTDSORG(FMTORGLN),0(R1)  SAVE FOR FORMATTING     81271
         TM    DS1DSORG,JFCORGPO   PDS ?                         81271
         BZ    *+10          NO                                  81271
         MVC   PDSDSNEQ(3),=C'PDS'  SET TO PDS=                  81271
         SPACE 1
         BAS   R9,CHECKDS1   SEE IF DATASET USABLE               81271
         B     *+8           NO; CHECK SOME MORE                 90260
         B     GOODDCB       ELSE PROCESS IT                     81271
         TM    DS1DSORG+1,DS1ACBM (X'08')  VSAM DATASET ?        90260
*NEXT*   BZ    ERRDSN        NO; FAIL                            90260
         BNZ   GOODDCB       LET @INPREAD COPE WITH IT           90260
         SPACE 1
ERRDSN   PRTSPACE 2,DEV==ERRUNIT
         PRTLIST MDSNERR,DEV==ERRUNIT  SHOW BAD OPEN
         OI    RETCC,CCWARN
         B     LOOPTIOT      TRY ANOTHER
         SPACE 1                                                 81271
LOOPDSCB VOLREAD DSCB        GET THE NEXT DSCB IN THE VTOC       81271
         BXLE  R15,R15,LOOKDSCB  OK; TEST IT                     81271
         TM    DSNFLAG,DFONE  AT LEAST ONE MATCH ?               81271
         MVI   DSNFLAG,0     RESET SEARCH MODE                   81271
         BNZ   LOOPTIOT      YES; GET NEXT DD CARD               81271
         LA    R3,MDDNMASK   SET FOR NO MATCH ON MASK            81271
         IC    R4,DSNMASKL   GET THE MASK LENGTH FOR FDPRT       81271
         B     ERRDDN        AND QUIT WITH MSG                   81271
LOOKDSCB LR    R1,R0                                             81271
         MVC   DS1DSNAM(LENDSCB1),0(R1)  COPY DSCB               81271
         CLI   DS1FMTID,C'1' FORMAT 1 DSCB ?                     81271
         BNE   LOOPDSCB      NO; GET ANOTHER                     81271
         CLI   DS1DSORG+1,0  VSAM OR FUNNY?                     GP09186
         BNE   LOOPDSCB      YES; SKIP                          GP09186
         TM    DS1DSORG,JFCORGPO+JFCORGDA+JFCORGPS  QUICK CHECK  81271
         BZ    LOOPDSCB      GET ANOTHER                         81271
         TM    DSNFLAG,DFPDS   PO ONLY ?                         81271
         BZ    MTCHDSCB      NO; CHECK NAME MATCH                81271
         TM    DS1DSORG,JFCORGPO  PDS ?                          81271
         BZ    LOOPDSCB      NO; REJECT                          81271
MTCHDSCB LA    R14,DSNMASK                                       81271
         LA    R15,DS1DSNAM  POINT TO NAME                       81271
         SLR   R0,R0                                             81271
         IC    R0,DSNMASKL   GET LENGTH OF MASK                  81271
         BAS   R9,COMPMASK   DO MASK TEST                        81271
         B     LOOPDSCB      NO MATCH; SKIP IT                   81271
         SERVCALL DSFMT,DS1DSORG   FORMAT DSORG/DCB VALUES       81271
         MVC   FMTDSORG(FMTORGLN),0(R1)  SAVE FOR FORMATTING     81271
         BAS   R9,CHECKDS1   CHECK DCB, ETC.                     81271
         B     LOOPDSCB      NOT USABLE; SKIP                    81271
         MVC   PDSDSNEQ,=C'DSN='   RESET DSN HEADER              81271
         TM    DS1DSORG,JFCORGPO   PDS ?                         81271
         BZ    *+10          NO                                  81271
         MVC   PDSDSNEQ(3),=C'PDS'  SET TO PDS=                  81271
         MVC   CURDCB(PATDCBLN),PATDCB  REFRESH DCB PATTERN      81271
         MVC   DCBDDNAM-IHADCB+CURDCB,CURDDNM  SET DDNAME        81271
         LA    R15,EXITLIST  GET EXIT LIST                       81271
         STCM  R15,7,DCBEXLSA-IHADCB+CURDCB                      81271
         LA    R14,CURDCB                                        81271
         LA    R15,INFMJFCB                                      81271
         STM   R14,R15,OPENLIST                                  81271
         MVI   OPENLIST,X'80'   SINGLE DCB, INPUT                81271
         MVI   EXITLIST,X'87'  JFCB ADDRESS                      81271
         LA    R3,MDDNJFCB   SHOW BAD READ                       81271
         RDJFCB MF=(E,OPENLIST)  GET JFCB MEMBER NAME            81271
         MVC   JFCBDSNM,DS1DSNAM  BUT CLOBBER DSN                81271
         ST    R15,DB        SAVE RETURN CODE FOR FORMATTING     81271
         BXH   R15,R15,ERRDSN   TOO BAD                          81271
         OI    DSNFLAG,DFONE   SHOW AT LEAST ONE MATCH FOUND     81271
         SPACE 1
GOODDCB  MVI   FONE,FODSN+FODDN    RESET FLAGS                   81343
         XC    CLRDSN(CLRDSNLN),CLRDSN  RESET FOR NEW DATASET
         XC    CURMEM(CURMEMLN),CURMEM  CLEAR MEMBER ENTRY FOR TITLE
         SLR   R0,R0         CLEAR FOR IC/ICM
         CLC   DS1REFD,WORKLINE+102  BLANK REFERENCE DATE ?
         BE    SEQCRTDT      YES; USE CREATION DATE
         CLC   DS1REFD,ZEROES  ZERO REFERENCE DATE ?
         BE    SEQCRTDT
         AIF   ('&LOCAL' NE 'CCSI').NPAKREF
         CLI   DS1REFD,99    VALID BINARY YEAR ?
         BL    SEQREFDT      YES; USE BINARY FORM
         TM    DS1REFD+2,X'0F'  PACKED DATE ?
         BNO   SEQREFDT      NO; USE BINARY
         ICM   R0,7,DS1REFD  LOAD PACKED VALUE
         B     SEQDATE       GO TO COMMON DATE CODE
.NPAKREF ANOP  ,
SEQREFDT LA    R1,DS1REFD-1  SET TO REFERENCE DATE
         B     *+8
SEQCRTDT LA    R1,DS1CREDT-1 SET CREATION DATE
         IC    R0,1(,R1)     GET YEAR
         MH    R0,=H'1000'   FINAGLE
         AH    R0,2(,R1)     ADD DAY
         CVD   R0,DB         MAKE PACKED
         L     R0,DB+4       GET PACKED VALUE
SEQDATE  SRL   R0,4          DELETE SIGN
         ST    R0,CURMSSI    SET AS NEW SSI
         MVI   CURMFLAG,2    SET SSI PRESENT
         PRTLIST PRTTIT1,DEV=1,TITLE=1,FOOTER=1  SYSPRINT HEADER
         SLR   R3,R3                                             91002
         ICM   R3,3,STYWIDTH   USE DATA WIDTH FOR SCALE LENGTH   91002
         BNZ   *+8           OK                                  91002
         LA    R3,100        USE OLD DEFAULT WIDTH               91002
         TM    STYF2,STYTIT80  ALTERNATE TITLES ?                91002
         BZ    TITOLD1       NO                                  91002
         PRTLIST PRTTIT3,TITLE=2,FOOTER=2                        91002
         PRTLIST NULLLINE,TITLE=3,FOOTER=3                       91002
         B     TITCOM1                                           91002
TITOLD1  SH    R3,=H'20'     ALLOW FOR 'VOL= ' TEXT              91002
         PRTLIST PRTTIT3,TITLE=3          SUBHEADER
         PRTV  NULLLINE,TITLE=2,FOOTER=2   CLEAR SCAN MODE SUB-HEADER
TITCOM1  PRTLIST PRTTIT4,TITLE=4
         PRTROOM 6,DEV=2     LEAVE SPACE FOR A FEW LINES
         PRTSPACE 2,DEV=2    TRIPLE-SPACE
         PRTLIST SUMDDN,DEV=2   PRINT NEW SUMMARY LISTING HEADER
         MVC   MINCLUDE,GINCLUDE  SET GLOBAL MASK PRIOR TO OVERRIDE
         TM    JFCBIND1,JFCGDG  GENERATION DATA GROUP NAME ?     90260
         BNZ   MASKNSET      YES; SKIP MEMBER MASK CODE          90260
         CLI   JFCBELNM,C' ' ANY NAME ?
         BNH   MASKSET       NO
         MVC   MINCLUDE,JFCBELNM   SET DATASET INCLUDE MASK      81271
         LA    R14,MINCLUDE+L'MINCLUDE  POINT PAST NAME          87330
         LA    R0,7          UP TO SEVEN                         87330
MASKFLUP BCTR  R14,0         GET PRIOR BYTE                      87330
         CLI   0(R14),C' '   TRAILING BLANK ?                    87330
         BE    MASKFUMP      YES; UP IT                          87330
         CLI   0(R14),C'#'   TRAILING POUND SIGN ?               87330
         BNE   MASKFAKE      NO; DONE                            87330
         MVI   0(R14),C'-'   REPLACE BY MATCH CHARACTER          87330
MASKFUMP BCT   R0,MASKFLUP   TRY AGAIN                           87330
MASKFAKE PRTLIST SUMDDNMK,DEV=2  SHOW NEW INCLUDE MASK
MASKSET  XC    JFCBELNM,JFCBELNM   CLEAR JFCB NAME               90260
         NI    JFCBIND1,255-JFCPDS  NO MEMBER IN JFCB
MASKNSET OI    JFCBTSDM,JFCNWRIT+JFCNDSCB+JFCNDCB  NO MERGES     90260
         MVC   CURMEM,CURDDNM  USE DDN FOR SEQUENTIAL UPDTE CARDS
         TM    DS1DSORG,JFCORGPO  PDS ?                          81271
         BNZ   NONSQNAM      YES; USE REAL MEMBER NAMES          81271
         TM    DSNFLAG,DFDSN DSN MASK MODE ?                     81271
         BZ    NONSQNAM      NO                                  81271
         INC   NUMSEQ        COUNT SEQUENTIAL DATASETS           81271
         CVD   R0,DB         MAKE PACKED                         81271
         UNPK  CURMEM,DB+4(4)  UNPACK NUMBER                     81271
         OI    CURMEM+7,C'0'  MAKE F ZONE                        81271
         MVC   CURMEM(2),=C'SQ'  INDICATE SEQUENTIAL COUNT       81271
NONSQNAM TM    MODEFG,MFDUP  DUPLICATE DSN/VOL ?                 81271
         BNZ   SKIPOPEN      YES; SKIP SECOND OPEN
         TM    FLAG2,FGNOTAUT+FGNODASD+FGNOSEQ  INPREAD ?        90260
         BNZ   SKIPOPEN      YES; NO NEED TO OPEN TWICE          90260
         AIF   (&MVS).NOIND1                                     82195
         TM    DS1DSIND,X'40'    RACF PROTECTED ?
         BNZ   TESTOPEN      YES; BUT SHOULD NOT DO ANYTHING
         TM    DS1DSIND,X'14'    READ PROTECTED ?
         BNM   SKIPOPEN      NO
         TM    DS1DSIND,X'10'   DOUBLE-CHECK BIT SETTING
         BZ    SKIPOPEN      BAD BIT SETTING IN DSCB
.NOIND1  ANOP  ,
TESTOPEN TM    FLAG2,FGUPDATE  UPDATE IN-PLACE ?                 90201
         BZ    TESTUPDT      NO                                  90201
         OI    OPENLIST,X'04'  SET OPEN UPDATE                   90291
TESTUPDT OPEN  MF=(E,OPENLIST),TYPE=J   CHECK ACCESS AUTHORIZATION
         CLOSE MF=(E,OPENLIST)   DONE WITH IT
         SR    R2,R2                                            GP99158
         ICM   R2,7,OPENLIST+1  LOAD DCB ADDRESS                GP00330
         BAL   R14,FREEPOOL  RELEASE BUFFERS, IF ANY ?          GP05120
SKIPOPEN OI    MODEFG,MFDUP  SET OPEN PERFORMED
         MVC   PDRECFM,DS1RECFM   SAVE RECFM FOR DIRECTORY CODE  82256
         TM    DS1DSORG,JFCORGPO  PDS ?
         BNZ   LOOPMEM       YES; START PROCESSING MEMBERS
         INC   SUBMEM        COUNT 'MEMBER'
         B     BEGINSET      GO PAST MEMBER NAME TESTS
         SPACE 1
*---------------------------------------------------------------------*
*   FREE POOL THE HARD WAY                                            *
*---------------------------------------------------------------------*
         PUSH  USING                                            GP05120
         USING IHADCB,R2                                        GP05120
FREEPOOL TM    DCBBUFCA+L'DCBBUFCA-1,1  FREEPOOLED ?            GP05120
         BNZ   FREEPOOP      YES; DON'T DO IT AGAIN             GP05120
         ICM   R1,7,DCBBUFCA   DIRTY POOL ?                     GP05120
         BZ    FREEPOOP      YES; SKIP FREEPOOL AND 90A ABEND   GP05120
         N     R1,=X'00FFFFFF' CLEAN IT                         GP05120
         SR    R15,R15                 CLEAR REGISTER           GP05120
         IC    R15,5(,R1)    GET BUFNO                          GP05120
         MH    R15,6(,R1)      TIMES BUFLEN                     GP05120
         LA    R0,8(,R15)    PREFIX                             GP05120
         TM    4(R1),X'40'   SIXTEEN-BYTE PREFIX ?              GP05120
         BNO   *+8           NO                                 GP05120
         LA    R0,16(,R15)   SET LONG PREFIX                    GP05120
         SVC   10            FREE THE BUFFERS                   GP05120
FREEPOOP LA    R0,1                                             GP05120
         STCM  R0,7,DCBBUFCA  DE-ACTIVATE POOL                  GP05120
         BR    R14           RETURN TO CALLER                   GP05120
         POP   USING                                            GP05120
         SPACE 1
***********************************************************************
**                                                                   **
**  LOOPMEM - GET NEXT MEMBER NAME, AND TEST VARIOUS CRITERIA        **
**    PROCESS ALIAS ONLY IF REQUESTED                                **
**    CHECK NAME AGAINST RANGE                                       **
**    CHECK NAME AGAINST INCLUSION MASK                              **
**    CHECK NAME AGAINST EXCLUSION MASK                              **
**    CHECK SSI AGAINST RANGE                                        **
**    CHECK SSI AGAINST INCLUSION MASK                               **
**                                                                   **
**    CHECK FOR OPERATOR STOP COMMAND - TERMINATE WITH MESSAGE       **
**    CHECK FOR OPERATOR MODIFY COMMAND - IGNORE TEXT; SHOW MEMBER   **
**                                                                   **
***********************************************************************
LOOPMEM  NI    FONE,FODDN+FODSN  RUTHLESS RESET                 GP02246
         XC    CLRMEM(CLRMEMLN),CLRMEM  CLEAR COUNTERS
         VOLREAD GETDE       GET A MEMBER ENTRY
         BXH   R15,R15,ENDIR   END OF DIRECTORY
         LTR   R1,R0         SWITCH                              90260
         MVC   CURMEM(CURMEMLN),0(R1)  COPY BASIC PORTION
         BM    LOOPMEM2      @INPREAD ALREADY DID PDSDE          90262
         ST    R1,PDNEXT                                         82256
         SERVCALL PDSDE,DEBXLE  DECODE DIRECTORY ENTRY           82256
         LR    R15,R0                                            82256
         MVC   PDRALIS(PDRBLNK),0(R15)   COPY RETURN             82256
LOOPMEM2 INC   SUBMEM
         TM    PDTYPE,PDTLKED  LINK-EDIT ENTRY ?                 82256
         BNZ   ENDMEM        YES; SKIP                           82256
         CLI   PDRDATE,C'0'  ANY DATE ?                          82256
         BL    CHECKALI      NO; LEAVE IT ALONE                  82256
         PACK  CURMSSI(5),PDRDATE(6) USE DATE AS SSI             82256
*VER2*   PACK  CURMSSI(3),PDRYMD(3)  PACK YEAR                  GP10197
*VER2*   PACK  CURMSSI+2(3),PDRJDAY(4)   PACK JULIAN DAY        GP10197
         SPACE 1
CHECKALI TM    CURMFLAG,X'80'  ALIAS ENTRY ?                     82256
         BZ    CHECKMEM      NO; PERFORM MASK/RANGE TESTS
         TM    FLAGS,FGDOAL  PROCESS AS MEMBER ?
         BNZ   CHECKMEM      YES
         INC   SUBASKIP      COUNT SKIPPED ALIAS ENTRY
         B     ENDMEM        GO FOR MORE
         SPACE 1
CHECKMEM CLC   CURMEM,LONAME   IS NAME IN RANGE ?
         BL    ENDMEM        NO; IGNORE IT
         CLC   CURMEM,HINAME   INSIDE RANGE ?
         BH    ENDIR         NO; QUIT THIS DSN
         LA    R14,MINCLUDE  LOAD MASK                           81271
         LA    R15,CURMEM    POINT TO MEMBER NAME
         BAS   R9,COMPMAT8   COMPARE MEMBER TO MASK              90260
          B    ENDIR         PAST LAST MATCH - TREAT AS EOD      90260
           B   ENDMEM        SKIP; NO MATCH                      81271
         CLC   MEXCLUDE,GENMATCH  EXCLUSION MASK SPECIFIED ?     81271
         BE    BEGINSET      NO; SKIP TEST                       81271
         LA    R14,MEXCLUDE  LOAD EXCLUSION MASK                 81271
         LA    R15,CURMEM    POINT TO MEMBER NAME                81271
         BAS   R9,COMPMSK8   TEST FOR MATCH                      81271
          B    BEGINSET      NO MATCH; PROCESS                   81271
           B   ENDMEM        MATCH; SKIP THIS ONE                81271
         PUSH  USING                                            GP97241
BEGINSET ICM   R7,15,CSCBPTR  SEE IF CIB PROCESSING INITIALIZED GP97241
         BZ    NOTCIB        NO
         USING CHAIN,R7
         AIF   (NOT &MVSESA).LOW24C                             GP97218
         L     R6,CHCSCXP    GET EXTENSION                      GP97218
         USING CSCX,R6       DECLARE IT                         GP97218
.LOW24C  TM    CHECB,X'50'   'STOP' ?  OR MODIFY ?              GP97218
         BO    KISSOFF       STOP
         BZ    NOTCIB        NEITHER
         L     R1,CHCIBP
         USING CIB,R1
         CLI   CIBVERB,CIBSTOP
         BE    KISSOFF
         QEDIT ORIGIN=CHCIBP,BLOCK=(1)
         PRTLIST CONRESP,DEV=5   RESPOND WITH CURRENT DD/MEMBER
         POP   USING                                            GP97241
NOTCIB   TM    CURMFLAG,X'1F'-1 MUST BE AT LEAST 2 HALFWORDS    GP97218
         BNZ   DOSSI
         XC    CURMSSI,CURMSSI   SET SSI TO ZERO
DOSSI    UNPK  DB(9),CURMSSI(5)  CONVERT SSI TO HEX
         TR    DB,HEXTAB
         TM    DS1DSORG,JFCORGPO  PDS ?
         BZ    SSIMATCH      NO; SKIP SSI TEST
         CLC   DB,SSILOW     IS SSI IN RANGE ?
         BL    ENDMEM        NO; IGNORE IT
         CLC   DB,SSIHIGH    INSIDE RANGE ?
         BH    ENDIR         NO; QUIT THIS MEMBER
         LA    R14,SSIMASK   LOAD MASK
         LA    R15,DB        POINT TO SSI
         BAS   R9,COMPMSK8   COMPARE 8 BYTES                     81271
         B     ENDMEM        NO MATCH; SKIP                      81271
SSIMATCH INC   SUBMSEL       COUNT SELECTED MEMBER
         BAS   R14,CLEARFON  RESET FLAGS AND COUNTERS            90262
         TM    FLAG3,FGNEGSCN  'NOT' SCAN MODE?                 GP02246
         BZ    SSIMATNG      NO, NOT NOT                        GP02246
         OI    FONE,FOFOUND  SET MEMBER MATCHED                 GP02246
SSIMATNG NI    MODEFG,255-MFWYL  RESET MODE FLAG                GP02246
         CLI   DS1RECFM,DCBRECU  RECFM=U ?                       90260
         BNE   *+8           NO                                  90260
         OI    MODEFG,MFWYL  DO WYLBUR DECOMPRESSION
         SPACE 1                                                 90262
         BAS   R9,READBLOK   READ THE FIRST BLOCK TO TEST FORMAT 90262
*RETURND L     R0,CURTTR     GET NOTED BLOCK ADDRESS             90262
         ST    R0,BEGINTTR   SAVE FOR LATER RESTART AND REREAD
         TM    MODEFG,MFWYL+MFVAR+MFUND  DOING NON-CARD ?        90260
         BNZ   NEXTBLOT      YES; PROCESS IT                     90260
         SLR   R14,R14
         L     R15,LASTBLK#  GET BLOCK SIZE                      90262
         STH   R15,DCMBUFMX  RESET DCM LIST                      90262
         ST    R5,DCMBUFAD   ...JUST IN CASE                     90262
         D     R14,CURRECLN  SEE IF MULTIPLE                     89217
         LTR   R14,R14       REMAINDER ?
         BZ    NEXTBLOR      NO; START PROCESSING                90262
         SERVCALL WDCOM,DECOMP   TRY A DECOMPRESS
         BXLE  R15,R15,NEXTCDWY  EDIT FORMAT IN CARD DSN ?
ERRNOTCD PRTLIST WYLNOTCD,DEV=2  SHOW NOT CARD FORMAT
         OI    RETCC,CCERROR  SET ERROR
         B     ENDMEM        AND QUIT
NEXTCDWY PRTLIST WYLNOTCD,DEV=2  SHOW NOT CARD FORMAT
         PRTLIST WYLTRYED,DEV=2  SHOW TRYING AS EDIT FORMAT
         OI    RETCC,CCWARN  SET WARNING FLAG
         OI    MODEFG,MFWYL  SET WYLBUR MODE
         B     NEXTBLOX      PROCESS FIRST RECORD                90262
         SPACE 1                                                 90262
CLEARFON NI    FONE,FODDN+FODSN  CLEAR ALL DS RELATED FLAGS     GP02246
CLEARSET MVC   DECOMP(DECOMPLN),PATDECOM  RE-INIT DECOMPRESS LIST
         OC    DCMFG1,WYLMODE  SET WYLBUR LINE NUMBER OPTIONS    81292
         NI    FONE,255-FOMEM-FOMEND-FOMATCH-FOPONE-FOONE RESET GP02246
         NI    FLAG2,255-FGZAPPED  RESET BLOCK MODIFIED          90201
         XC    CLRMEM(CLRMEMLN),CLRMEM  CLEAR COUNTERS
         BR    R14           RETURN TO CALLER                    90262
         SPACE 1                                                 90262
***********************************************************************
**                                                                   **
**  BEGINMEM - START (RE)PROCESSING A MEMBER                         **
**    DEBLOCK, DECOMPRESS, UNPACK, OR OTHERWISE GET A RECORD         **
**                                                                   **
**  IF FOMATCH IS ON, MEMBER IS BEING PROCESSED THE SECOND TIME      **
**    AFTER THE FIRST READ FOUND A MATCH ON A TEXT MATCH REQUEST     **
**                                                                   **
***********************************************************************
BEGINMEM MVC   CURTTR,BEGINTTR  SET FOR ERROR MESSAGE            90262
         BAS   R14,CLEARSET  RESET FLAGS AND COUNTERS            90262
         OI    FONE,FOMATCH  IDENTIFY AS SECOND PASS            GP02246
         TM    FLAG3,FGNEGSCN  'NOT' SCAN MODE?                 GP02246
         BZ    BEGINNNG      NO, NOT NOT                        GP02246
         OI    FONE,FOFOUND  SET MEMBER MATCHED                 GP02246
BEGINNNG VOLREAD POINT,BEGINTTR  REPOSITION AND REREAD FIRST BLOCK
         BAS   R9,READBLOC   JOIN COMMON READ CODE               90262
         B     NEXTBLOD      PROCESS DATA                        90262
NEXTBLOK TM    MODEFG,MFWYL  WYLBUR DECOMPRESS ?
         BNZ   NEXTBLOW      YES; DECOMPRESS IT
NEXTBLOS TM    FLAG2,FGUPDATE+FGZAPPED  CHANGED ?                90201
         BNO   NEXTBLOB      NO                                  90201
         NI    FLAG2,255-FGZAPPED  RESET ZAPPED BIT              90201
         TM    FLAG2,FGNOTAUT+FGNODASD+FGNOSEQ  INHIBITED ?      90260
         BNZ   NEXTBLOB      YES; BYPASS IN-PLACE UPDATE         90260
         L     R2,LASTBLK@   GET ADDRESS OF COUNT FIELD          90201
         VOLREAD UPDATE,(R2)  RETURN BLOCK ADDRESS (CCHHR)       90201
         BXLE  R15,R15,NEXTBLOB                                  90201
         PRTLIST MSGUPBAD,DEV=(1,2)   WRITE ADDRESS FROM R2      90201
         ABEND 3330,DUMP     ERROR REWRITING BLOCK               90201
NEXTBLOB BAS   R9,READBLOK   GET ANOTHER BLOCK                   90262
NEXTBLOD TM    MODEFG,MFWYL+MFVAR  NON-CARD MODE ?               89151
         BNZ   NEXTBLOT      YES; SKIP TEST
         SLR   R14,R14
         L     R15,LASTBLK#  GET BLOCK SIZE                      90262
         D     R14,CURRECLN  SEE IF CARD MULTIPLE                89217
         LTR   R14,R14
         BNZ   ERRNOTCD
NEXTBLOT TM    MODEFG,MFVAR  RECFM=V ?                           90262
         BZ    NEXTBLOQ      NO                                  89151
         LA    R6,4          SET LENGTH OF BDW                   89151
         B     SKIPCARD      AND SKIP OVER IT                    89151
NEXTBLOQ TM    MODEFG,MFWYL  WYLBUR DECOMPRESS ?                 90262
         BZ    NEXTBLOR      NO; PROCESS THE RECORD
NEXTBLOW SERVCALL WDCOM,DECOMP   DECOMPRESS
         CH    R15,=H'4'
         BE    NEXTBLOS      READ ANOTHER RECORD
         BL    NEXTBLOX      PROCESS CURRENT RECORD
         ST    R0,DB         SAVE ERROR CODE                     81292
         LA    R3,WORKLINE+102   POINT TO BLANKS                 81292
         CH    R0,=H'3'      BAD BLOCK LENGTH ?                  81292
         BL    NEXTBLKS      NO; UNSPECIFIED ERROR               81292
         CH    R0,=H'6'      GREATER THAN LARGEST CURRENTLY DEFINED ?
         BH    NEXTBLKS      YES; LEAVE BLANKS                   81292
         MH    R0,=H'15'     MULTIPLY BY MESSAGE LENGTH          81292
         LR    R3,R0                                             81292
         LA    R3,NOTWYLCD-3*15(R3)  POINT TO MESSAGE            81292
NEXTBLKS PRTLIST WYLNOTED,DEV=2  MEMBER NOT EDIT FORMAT          81292
         OI    RETCC,CCWARN
         NI    MODEFG,255-MFWYL  RESET WYLBUR MODE
         TM    FONE,FOONE    FIRST RECORD PROCESSED ?
         BNZ   ENDMEM        YES; UNEXPECTED ERROR
         L     R15,LASTBLK#  GET BLOCK SIZE                      90262
         STH   R15,DCMBUFMX  REFRESH BLOCK SIZE                  90262
         SLR   R14,R14
         D     R14,CURRECLN  SEE IF MULTIPLE OF LRECL            89217
         LTR   R14,R14
         BNZ   ENDMEM        NO; CANNOT PROCESS
         PRTLIST WYLTRYCD,DEV=2
         L     R5,DCMBUFAD   GET BLOCK START
         LR    R7,R5
         A     R7,LASTBLK#                                       90262
         L     R6,CURRECLN                                       89217
         SR    R7,R6         SET FOR CARD FORMAT PROCESSING
         B     NEXTBLOR      CONTINUE NORMALLY
NEXTBLOX L     R5,DCMRECAD   GET RECORD ADDRESS
         LR    R7,R5         SET BXLE STOP
         LH    R6,DCMRECLN   GET CURRENT RECORD LENGTH
         LA    R5,8(,R5)     SKIP LINE NUMBER
         SH    R6,=H'8'      ADJUST LENGTH
         MVC   OFFSET+2(2),DCMBUFLN  SET OFFSET
         TM    FLAGS,FGWINT+FGWEDIT  LINE NUMBER IN 73/80 ?
         BZ    NEXTBLOR      NO
         LA    R6,80         FORCE RECORD LENGTH OF 80
         TM    FLAGS,FGWINT  INTEGER FORMAT ?
         BNZ   NEXTBLOY      YES
         MVC   72(8,R5),0(R7)  MOVE EDIT FORMAT
         B     NEXTBLOR
NEXTBLOY MVC   72(8,R5),=8C'0'  MAKE ZEROES
         OC    72(1,R5),DCMLINEH  GET HIGH BYTE
         OC    73(4,R5),DCMLINEB  GET INTEGER PORTION
         OC    77(3,R5),DCMLINEB+5  GET DECIMAL PORTION
NEXTBLOR TM    FONE,FOONE    FIRST RECORD PROCESSED ?
         BNZ   NEXTCARD      YES
         OI    FONE,FOONE    SET PROCESSED
         TM    FLAGS,FGSMAC+FGNMAC  MACRO/NON-MACRO OPTION ?
         BZ    NEXTCARD      NO
         LR    R14,R5        LOAD FIRST BYTE
         LA    R15,16        CHECK FIRST 16 COLUMNS
MACTESTL CLC   =C' MACRO ',0(R14)  IS IT A MACRO CARD ?
         BE    MACTEST3      YES, DECIDE WHAT TO DO ABOUT IT
         LA    R14,1(,R14)   TRY NEXT COLUMN
         BCT   R15,MACTESTL  TRY AGAIN
MACTEST2 TM    FLAGS,FGNMAC  NON-MACROS ?
         BZ    MACTEST4      SKIP THIS ONE
         B     NEXTCARD      ELSE PROCESS
MACTEST3 TM    FLAGS,FGSMAC  MACROS ONLY ?
         BNZ   NEXTCARD      YES; PROCESS
MACTEST4 INC   NUMCARD       COUNT CARDS READ
         B     ENDMEM        AND QUIT THIS MEMBER
NEXTCARD TM    MODEFG,MFWYL  WYLBUR MODE ?
         BNZ   NEXTCARE      YES; OFFSET ALREADY SET
         LR    R0,R5         COPY CURRENT ADDRESS
         S     R0,DCMBUFAD   LESS BUFFER START
         ST    R0,OFFSET     SET OFFSET
         TM    MODEFG,MFVAR  RECFM=V ?                           89151
         BZ    NEXTCARE      NO                                  89151
         ICM   R6,2,0(R5)    GET HIGH BYTE OF RECORD LENGTH      89151
         BM    NEXTBLOK      FILLER BYTE                         89151
         IC    R6,1(,R5)     GET REMAINDER                       89151
         LA    R5,4(,R5)     SKIP RDW                            89151
         SH    R6,=H'4'      ADJUST RESIDUAL LENGTH              89151
         BM    NEXTBLOK      ERROR                               89151
         BP    NEXTCARE      OK                                  89151
         BCTR  R5,0          IF LEN=0, FAKE ONE                  89151
         LA    R6,1(,R6)                                         89151
         MVI   0(R5),C' '    BLANK                               89151
         SPACE 1                                                GP02246
***********************************************************************
**                                                                   **
**  NEXTCARE - PROCESS AN INPUT RECORD                               **
**                                                                   **
**  WHEN FOMATCH IS OFF: (FIRST OR ONLY PASS THROUGH DATA)           **
**    IF SEARCH MODE IS ON, TEST FOR MATCHING TEXT STRING            **
**      IF MATCHED, SET FOMATCH ON, AND RESTART MEMBER PROCESSING    **
**    WHEN SEARCH MODE IS OFF, PRINT/PUNCH THE RECORD                **
**                                                                   **
**    IF GLOBAL 'NOT' PROCESSING IS IN EFFECT, READ ALL CARDS.       **
**      TEST TEXT STRING MATCH, AND RESET FOFOUND ON MATCH           **
**    (IF ANY MATCHES ARE FOUND, TESTS AT LABEL ENDMEM WILL DISCARD  **
**     THE MEMBER, OTHERWISE THE MEMBER IS PREPROCESSED, FOMATCH ON) **
**                                                                   **
**  WHEN FOMATCH IS ON:  (SECOND PASS THROUGH DATA)                  **
**    IF LSCAN (CONDITIONAL PRINT) IS ON, TEST FOR TEXT MATCH        **
**      IF MATCHED, PRINT/PUNCH, OTHERWISE PUNCH                     **
**    ON THE SECOND PASS 'NOT' MODE INVERTS THE PRINT TEST           **
**                                                                   **
***********************************************************************
NEXTCARE INC   NUMCARD
         TM    FLAGS,FGSCAN  SCAN MODE ?
         BNZ   TESTSCAN      YES; PERFORM SCAN
         TM    FONE,FOMATCH  FORCED MATCH ?
         BZ    TESTSCAN      NO; TEST
         OI    FONE,FOFOUND  PROVISIONALLY SET MATCHED
         TM    FLAGS,FGLSCAN  CONDITIONAL LIST ?
         BZ    PUTCARD       NO; TREAT AS MATCH
TESTSCAN ICM   R9,15,TABLE   ANY SCAN SELECTION ?
         BZ    PUTCARD       NO; DO UNCONDITIONALLY
         L     R8,=A(SCANTEST)  GO TO COMPARE ROUTINE            90201
         BASR  R9,R8         AND LOOK FOR A MATCH               GP02247
           B   PUTNOMAT      NO MATCH (VECTORED RETURN)         GP02247
         SPACE 1
***********************************************************************
**                                                                   **
**  TEXT MATCHED; FOR 'NOT' MODE RESET FOFOUND; PROCESS CARD         **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP02247
PUTMATCH TM    FLAG3,FGNEGSCN  'NOT' SCAN MODE ?                GP02247
         BNZ   PUTNEGMT      YES; SKIP THIS MEMBER              GP02247
         TM    FLAGS,FGSCAN  SCAN MODE ?
         BNZ   PUTCARD       YES; PROCESS NORMALLY
         TM    FONE,FOMATCH  PRIOR MATCH ?
         BNZ   PUTCARD       YES; PROCESS IT
         OI    FONE,FOMATCH  FORCE MATCH
         B     BEGINMEM      AND RESTART FROM BEGINNING
PUTNEGMT NI    FONE,255-FOFOUND  RESET SCAN MATCH               GP02247
         B     SKIPCARD      AND SKIP                           GP02247
         SPACE 1
***********************************************************************
**                                                                   **
**  TEXT NOT MATCHED; KEEP FOFOUND IN 'NOT' MODE, ELSE RESET         **
**                                                                   **
***********************************************************************
         SPACE 1
PUTNOMAT TM    FLAG3,FGNEGSCN   'NOT' SCAN ?                    GP02247
         BNZ   SKIPCARD      YES; SKIP                          GP02247
         TM    FONE,FOMATCH  PRIOR MATCH ?
         BZ    SKIPCARD      NO; SKIP
         TM    FLAGS,FGLSCAN  LIST SCAN MATCHES ONLY ?
         BZ    PUTCARD       NO; LIST
         NI    FONE,255-FOFOUND  RESET SCAN MATCH
         SPACE 1
PUTCARD  TM    FONE,FOMEM    IEBUPDTE HEADER PROCESSED PREVIOUSLY ?
         BNZ   PUTCARD1      YES
         OI    FONE,FOMEM    SET PROCESSED
         INC   SUBMHIT       COUNT MEMBERS WITH MATCHES
         TM    OUTUNIT,OUPDT  UPDTE PRESENT ?
         BZ    PUTCARD1
         PRTLIST UPDTEADD,DEV=4   MAKE IEBUPDTE ADD CARD
         INC   NUMUPDTE      COUNT OUTPUT
PUTCARD1 TM    FONE,FOPONE   FIRST PRINT RECORD PROCESSED ?
         BNZ   PUTCARD2      YES; SEE IF NEED TO PRINT THIS ONE
         TM    FONE,FOMATCH+FOFOUND  PRINT THIS ONE ?
         BM    PUTCARD3      NO
         L     R15,NUMCARD   GET CURRENT RECORD NUMBER           81271
         ICM   R0,15,RECEND  GET LIST END RECORD                 81271
         BZ    *+10            NONE                              81271
         CR    R15,R0        END REACHED ?                       81271
         BH    PUTCARDX      YES; NO FURTHER LISTING             81271
         S     R15,RECONE    START RECORD REACHED ?              81271
         BM    PUTCARD3        NO                                81271
         SLR   R14,R14                                           81271
         D     R14,RECINC    N-TH CARD FOUND ?                   81271
         LTR   R14,R14                                           81271
         BNZ   PUTCARD3        NO; SKIP                          81271
         TM    FLAGS,FGEYE   EYEBALL HEADERS ?
         BNZ   PUTCDTIT      YES; EJECT AND TITLE
         TM    FLAGS,FGSCAN  SCAN MODE ?
         BZ    PUTCDTIT      NO; EJECT AND TITLE
         TM    STYF2,STYTIT80  ALTERNATE TITLES ?                91002
         BZ    TITOLD2       NO                                  91002
         PRTLIST PRTSCAB,TITLE=3,FOOTER=3  CONDITIONAL SUBHEAD   91002
         B     TITCOM2                                           91002
TITOLD2  PRTLIST PRTSCAB,TITLE=2,FOOTER=2  MAKE CONDITIONAL SUBHEADER
TITCOM2  PRTROOM 6           LEAVE SPACE
         PRTSPACE 2
         PRTLIST PRTSCAM     MAKE MEMBER HEADER
         PRTSPACE 1
         B     PUTCARD2      NOW GO TO PRINT
PUTCARDX TM    OUTUNIT,OUPUN+OUPDT  NON-PRINTER OUTPUT ?         81271
         BNZ   PUTCARD3      YES; PROCESS                        81271
         B     ENDMEM        ELSE TERMINATE PROCESSING           81271
PUTCDTIT PRTLIST PRTTIT1,DEV=1,TITLE=1,FOOTER=1  PUT PAGE HEADER
         TM    FLAGS,FGEYE   EYE-BALL HEADER ?
         BZ    PUTCARD2      NO
         TM    STYF2,STYTIT80   NARROW OUTPUT ?                  91002
         BZ    PUTEYE        NO                                  91002
         PRTBIG CURMEM,OPT=(CENTER,SMALL)                        91002
         B     PUTCARD2                                          91002
PUTEYE   PRTBIG CURMEM,OPT=CENTER   BIG LETTERS, CENTERED
PUTCARD2 TM    OUTUNIT,OUPRT   SYSPRINT PRESENT ?
         BZ    PUTCARD3      NO
         TM    FONE,FOMATCH+FOFOUND  CARD TO BE PRINTED ?
         BM    PUTCARD3      NO
         ICM   R15,15,RECLIM  ANY PRINT LIMIT ?                  81271
         BZ    *+16          NO                                  81271
         C     R15,NUMPRT    CHECK NUMBER PRINTED                81271
         BL    PUTCARD3      OVER LIMIT; SKIP                    81271
         BE    PUTCARDX      ON LIMIT; CHECK FOR TERMINATION     81271
         L     R15,NUMCARD   GET CURRENT RECORD NUMBER           81271
         ICM   R0,15,RECEND  GET LIST END RECORD                 81271
         BZ    *+10            NONE                              81271
         CR    R15,R0        END REACHED ?                       81271
         BH    PUTCARDX      YES; NO FURTHER LISTING             81271
         S     R15,RECONE    START RECORD REACHED ?              81271
         BM    PUTCARD3        NO                                81271
         SLR   R14,R14                                           81271
         D     R14,RECINC    N-TH CARD FOUND ?                   81271
         LTR   R14,R14                                           81271
         BNZ   PUTCARD3        NO; SKIP                          81271
         SLR   R2,R2         CLEAR FOR IC                        91002
         ICM   R2,3,STYWIDTH   ANY STYLE WIDTH LIMIT ?           91002
         BNZ   *+8           YES; HONOR IT                       91002
         LA    R2,100        MAXIMUM LENGTH OF PRINT LINE        81271
         CR    R6,R2         EXCEEDS MAXIMUM ?                   81271
         BNL   *+6           NO; USE ACTUAL LENGTH               81271
         LR    R2,R6         USE MAXIMUM LENGTH                  81271
         PRTLIST PRTLINE     PRINT THE CARD
         MVI   TESTSTR,0     RESET FLAG                          90201
         INC   NUMPRT        FIX PRINT COUNT
         TM    FONE,FOPONE   STILL FIRST TIME ?
         BNZ   PUTCARD3      NO
         OI    FONE,FOPONE   SET FIRST TIME DONE
         PRTROOM 0           FORCE NEW LINE
         MVC   MEMPAGE,PRPAGE  SAVE MEMBER PAGE NUMBER ON SYSPRINT
PUTCARD3 LA    R2,80         MAXIMUM LENGTH OF CARD IMAGE        89217
         CR    R6,R2         EXCEEDS MAXIMUM ?                   89217
         BNL   *+6           NO; USE ACTUAL LENGTH               89217
         LR    R2,R6         USE MAXIMUM LENGTH                  89217
         LR    R3,R2         COPY LENGTH                         89217
         SH    R3,=H'2'      ALLOW FOR CONTROL PREFIX (IF ANY)   89217
         TM    OUTUNIT,OUPUN  PUNCH PRESENT ?                    94299
         BZ    PUTCARD4      NO                                  94299
         PRTF  (R5),(R2),CC=NO,DEV=3
         INC   NUMPUN        UPDATE PUNCH COUNT
PUTCARD4 TM    OUTUNIT,OUPDT  SYSUPDTE PRESENT ?
         BZ    SKIPCARD      NO; GET ANOTHER CARD
         PRTLIST UPDTECRD,DEV=4     PUT TO IEBUPDTE FILE
         INC   NUMUPDTE
         SPACE 1
SKIPCARD BXLE  R5,R6,NEXTCARD
         B     NEXTBLOK      ELSE READ ANOTHER BLOCK
         SPACE 1
ENDMEM   TM    FLAG3,FGNEGSCN  'NOT' MODE?                      GP02246
         BZ    ENDMEMNG      NO; NORMAL END                     GP02246
         TM    FONE,FOMATCH  SECOND TIME ?                      GP02246
         BNZ   ENDMEMNG      YES; DONE                          GP02246
         TM    FONE,FOFOUND  WAS THERE A MATCH ?                GP02246
         BZ    LOOPMEM       NO; DO ANOTHER MEMBER              GP02246
         OI    FONE,FOMATCH  SET FORCED MATCH                   GP02246
         B     BEGINMEM      AND REPROCESS                      GP02246
ENDMEMNG TM    DS1DSORG,JFCORGPO  PDS ?                         GP02246
         BZ    ENDMEMD       NO; FORCE STATISTICS
         TM    FONE,FOMEM+FOPONE  ANY OUTPUT FOR THIS MEMBER ?
         BNZ   ENDMEMD       YES; DO STATS                      GP10197
         TM    FLAG3,FGNEGSCN   GLOBAL NOT ?                    GP10197
         BZ    ENDMEMT       NO; SKIP
         TM    FONE,FOFOUND+FOMATCH    FORCED MATCH ?           GP10197
         BNO   ENDMEMT       NO; SKIP                           GP10197
ENDMEMD  TM    FONE,FOMEND   PRIOR TRAILER ?
         BNZ   ENDMEMT       BUT HOW ?
         OI    FONE,FOMEND
         PRTLIST SUMMEM,DEV=2  WRITE MEMBER INFO
ENDMEMT  LA    R1,SUBCARD    POINT TO SUBTOTALS FOR DSN         GP02246
         LA    R2,NUMCARD    POINT TO DSN/MEMBER TOTALS
         LA    R3,NUMCOUNT   GET NUMBER OF COUNTERS
         LA    R14,ENDMEMB   SET RETURN FROM IN-LINE SUBROUTINE  81347
ENDMEMA  L     R0,0(,R1)
         A     R0,0(,R2)
         ST    R0,0(,R1)     UPDATE DSN COUNTERS
         LA    R1,4(,R1)
         LA    R2,4(,R2)
         BCT   R3,ENDMEMA
         BR    R14           RETURN TO CALLER                    81347
         SPACE 1
ENDMEMB  TM    DS1DSORG,JFCORGPO  PARTITIONED DATASET ?          81347
         BNZ   LOOPMEM       YES; GET NEXT MEMBER
         TM    ERRUNIT,OUSUM   SUMPRINT AVAILABLE ?
         BNZ   ENDIR         YES; NO TITLE CHANGE
         PRTLIST SUMTIT1,DEV=1,TITLE=1
         PRTLIST NULLLINE,DEV=1,FOOTER=1
         PRTLIST NULLLINE,DEV=1,TITLE=2,FOOTER=2                 81271
         PRTLIST NULLLINE,DEV=1,TITLE=3,FOOTER=3                 91002
ENDIR    LA    R1,TOTCARD    POINT TO TOTALS FOR RUN
         LA    R2,SUBCARD    POINT TO DSN TOTALS
         LA    R3,SUBCOUNT   GET NUMBER OF COUNTERS
         BAS   R14,ENDMEMA   UPDATE COUNTERS                     81347
         TM    DS1DSORG,JFCORGPO  PDS ?
         BZ    LOOPTIOT      YES; SKIP SUBTOTALS
         PRTROOM 3,DEV=2
         PRTSPACE 1,DEV=2
         PRTLIST SUMDSN,DEV=2  PRINT DATASET SUMMARY
         B     LOOPTIOT      ELSE GET NEXT DATASET
         SPACE 1
KISSOFF  PRTLIST PRTSTOP,DEV==ERRUNIT                           GP02246
         OI    RETCC,CCERROR
ENDALL   PRTROOM 5,DEV=2
         TM    FONE,FODDN    ANY DD CARDS PROCESSED ?            81343
         BNZ   ENDALLDD      YES                                 81343
         MVI   RETCC,CCFATAL  SET FATAL ERROR                    81343
         PRTV  MSGNODDS,DEV==ERRUNIT  WRITE NASTY MESSAGE        81343
ENDALLDD PRTSPACE 2,DEV=2                                        81343
         PRTLIST SUMTOT,DEV=2
         TM    PRPAGE+1,1    EVEN NUMBER OF PAGES PRINTED ?
         BZ    EXIT          YES
         PRTV  NULLLINE,TITLE=1,FOOTER=1 CLEAR TITLE
         PRTV  NULLLINE,TITLE=2,FOOTER=2   CLEAR SCAN MODE SUB-HEADER
         PRTV  NULLLINE,TITLE=3
         PRTV  NULLLINE,TITLE=4
         PRTV  NULLLINE      PRINT A BLANK PAGE
EXIT     SERVTERM ,          CLOSE AND FREE EVERYTHING
         LH    R9,RETCODE    GET RETURN CODE
         PGMEXIT RC=(R9)       RETURN WITH CONDITION CODE
         SPACE 2                                                 90260
*        MASK COMPARE WITH RANGE TESTING                         90260
*        R14 - MASK ADDRESS; R15 - TEXT STRING; R0 - LENGTH      81271
*     RETURN 0(R9) - PAST LAST MATCH; 4(R9) - NO MATCH;  8(R9) - MATCH
*                                                                81271
COMPMAT8 LA    R0,8          SCAN 8 BYTE MEMBER NAME             90260
         LTR   R0,R0         VALID LENGTH ?                      90260
         BNP   8(,R9)        NO; TREAT AS MATCH                  90260
COMPMATL CLI   0(R14),C'-'   ANY MATCH IN THIS POSITION ?        90260
         BE    COMPMAT9      YES; PERMIT AND DO NORMAL COMPARE   90260
         CLC   0(1,R15),0(R14)  MATCH ?                          90260
         BL    4(,R9)        NO; RETURN UNMATCHED                90260
         BHR   R9            RETURN PAST LAST MATCH              90260
COMPMATB LA    R14,1(,R14)   TRY NEXT BYTE                       81271
         LA    R15,1(,R15)                                       81271
         BCT   R0,COMPMATL   REPEAT FOR NEXT BYTE                81271
         B     8(,R9)        RETURN MATCHED                      81271
COMPMAT9 LA    R9,4(,R9)     SET FOR 'NORMAL' RETURN             90260
         B     COMPMASB      FINISH WITH OLD CODE                90260
         SPACE 1                                                 81271
*        MASK COMPARE                                            81271
*        R14 - MASK ADDRESS; R15 - TEXT STRING; R0 - LENGTH      81271
*          RETURN 0(R9) - NO MATCH;  4(R9) - MATCH               81271
*                                                                81271
COMPMSK8 LA    R0,8          SCAN 8 BYTE MEMBER NAME             81271
COMPMASK LTR   R0,R0         VALID LENGTH ?                      81271
         BNP   4(,R9)        NO; TREAT AS MATCH                  81271
COMPMASL CLI   0(R14),C'-'   ANY MATCH IN THIS POSITION ?        81271
         BE    COMPMASB      YES; PERMIT                         81271
         CLC   0(1,R14),0(R15)  MATCH ?                          81271
         BNER  R9            NO; RETURN UNMATCHED                81271
COMPMASB LA    R14,1(,R14)   TRY NEXT BYTE                       81271
         LA    R15,1(,R15)                                       81271
         BCT   R0,COMPMASL   REPEAT FOR NEXT BYTE                81271
         B     4(,R9)        RETURN MATCHED                      81271
         SPACE 1                                                 81271
*        CHECK DSN FOR DSORG, DCB OPTIONS, ETC.                  81271
*          RETURN 0(R9) - UNUSABLE, (R3) HAS ERROR MESSAGE POINTER
*           ELSE 4(R9) - USABLE                                  81271
CHECKDS1 LA    R3,MDSNORG    PRESET FOR BAD DSORG                81271
         NI    MODEFG,255-MFVAR  RESET RECFM=V                   89151
         CLI   DS1DSORG+1,0   ANY FUNNY ORG ?                    81271
         BNER  R9            YES; REJECT                         81271
         TM    DS1DSORG,JFCORGPO+JFCORGDA+JFCORGPS  ONE OF OURS ?
         BZR   R9            NO; REJECT                          81271
         TM    DS1DSORG,255-JFCORGPO-JFCORGDA-JFCORGPS-JFCORGU   81271
         BNZR  R9            REJECT ANYTHING ELSE                81271
         MVC   CURRECLN,=F'80'  PRESET FOR CARD FORMAT           90201
         LA    R3,MDSNDCB    PRESET FOR BAD DCB PARAMETERS       81271
         TM    DS1RECFM,DCBRECU  UNDEFINED ?                     81271
         BO    CHECKDSU      YES; CHECK FURTHER                  81271
         BZR   R9            NONE; REJECT THIS ONE               81271
         TM    DS1RECFM,DCBRECF  FIXED ?                         81271
         BZ    CHECKDSV      NO; TRY V FORMAT                    89151
         SLR   R0,R0                                             89217
         ICM   R0,3,DS1LRECL  ANY RECORD LENGTH ?                89217
         BNPR  R9            NO; REJECT IT                       90098
         CH    R0,=H'256'    NOT TOO LARGE ?                     89217
         BHR   R9            NO; REJECT                          89217
         ST    R0,CURRECLN   SAVE FOR TESTING                    89217
         B     CHECKDSB      CHECK BLOCKSIZE                     81271
CHECKDSV LA    R0,256+4      LARGEST RECORD TO BE SUPPORTED      89151
         CH    R0,DS1LRECL   IS IT PROCESSABLE ?                 89151
         BLR   R9            NO                                  89151
         OI    MODEFG,MFVAR  SET RECFM=V                         89151
         B     CHECKDSB      CHECK FURTHER                       89151
CHECKDSU CLI   DS1RECFM,DCBRECU  RECFM=U EXACTLY ?               81271
         BNE   CHECKUDS      MAY BE UA OR UM                     90260
CHECKSAS LA    R0,4                                              81271
         AH    R0,DS1LRECL   GET LRECL+4                         81271
         CH    R0,DS1BLKL    = BLOCKSIZE ?                       81271
         BER   R9            YES; REJECT (PRESUMED) SAS DATASET  81271
CHECKUDS MVC   DS1LRECL,DS1BLKL  FORCE RECORD=BLKSIZE            90260
         CLC   DS1LRECL,=H'256'  WITHIN MY (ARBITRARY) LIMIT ?   90260
         BH    CHECKDSB      HAD BETTER BE WYLBUR EDIT           90260
         OI    MODEFG,MFUND  PROCESS AS UNDEFINED, UNBLOCKED     90260
CHECKDSB SLR   R0,R0                                             81271
         CH    R0,DS1BLKL    ANY BLOCKSIZE AT ALL ?              81271
         BER   R9            NO; REJECT                          81271
         LA    R3,MDSNDSCB   SET FOR OTHER DSCB ERROR            81271
         CLI   DS1NOEPV,0    ANY EXTENTS ?                       81271
         BER   R9            NO; REJECT                          81271
         TM    DS1DSORG,JFCORGDA  DIRECT ACCESS ?                81271
         BNZ   4(,R9)        YES; NO LSTAR CHECK - ACCEPT DSCB   81271
         CLC   ZEROES(7),DS1LSTAR  EVER USED ?                  GP09186
         BZR   R9            NO; TOO BAD                        GP09186
         B     4(,R9)        YES; PROCESS IT                     81271
         SPACE 1                                                 90262
*        SUBROUTINE TO READ NEXT BLOCK AND SET APPROPRIATE ADDRESS
*        AND LENGTH FIELDS.                                      90262
READBLOK VOLREAD READ        GET THE FIRST BLOCK                 90262
READBLOC CH    R15,=H'4'     CHECK RETURN                        90262
         BE    ENDMEM        END-FILE                            90262
         BH    READBLER      ERROR                               90262
         TM    FLAG2,FGNOTAUT+FGNODASD+FGNOSEQ  NON-VOLREAD ?    90262
         BZ    READBLOB      NO; HAVE PHYSICAL BLOCK             90262
         ST    R1,LASTBLK@   SAVE LAST BLOCK ADDRESS             90262
         B     READBLOT                                          90262
READBLOB ST    R0,LASTBLK@   REMEMBER LAST BLOCK ADDRESS         90262
         LR    R5,R0         SAVE BLOCK ADDRESS                  90262
         LA    R1,8(,R5)     GET DATA ADDRESS                    90262
         SLR   R0,R0         CLEAR FOR IC                        90262
         ICM   R0,3,6(R5)    GET LENGTH FROM COUNT FIELD         90262
READBLOT ST    R1,DCMBUFAD   SAVE BUFFER START                   90262
         STH   R0,DCMBUFMX   SET LENGTH FOR DECOMPRESSION        90262
         ST    R0,LASTBLK#   SAVE BUFFER LENGTH                  90262
         LR    R5,R1         COPY TEXT START                     91059
         LR    R7,R5         COPY BUFFER START ADDRESS           91059
         AR    R7,R0         END OF BUFFER+1                     91059
         TM    MODEFG,MFVAR  RECFM=V ?                           90262
         BZ    READBLOU      NO                                  90262
         LA    R0,1          SET FAKE RECORD LENGTH              90262
         B     READBLOR                                          90262
READBLOU TM    MODEFG,MFUND  UNDEFINED NON-WYLBUR ?              90262
         BZ    READBLOV      NO                                  90262
READBLOR ST    R0,CURRECLN   YES; FAKE MULTIPLE                  90262
READBLOV L     R6,CURRECLN   GET RECORD LENGTH IN BLOCK          91059
         SR    R7,R6         LAST RECORD ADDRESS                 90262
         VOLREAD NOTE        GET POSITION                        90262
         ST    R0,CURTTR                                         90262
*   NOTE THAT CALLER EXPECTS RETURN OF R5-R7 FOR BXLE USE, AND R0
         BR    R9            RETURN TO CALLER                    90262
         SPACE 1                                                 90262
READBLER SRL   R15,1         RESTORE CONDITION CODE              90262
         ST    R15,DB        STASH IT                            90262
         PRTLIST MSGBLBAD,DEV==ERRUNIT  FAIL IT                  90262
         OI    RETCC,CCERROR  SET ERROR                          90262
         B     ENDMEM        TREAT AS END-MEMBER                 90262
         EJECT ,
         LTORG ,
         SPACE 2
         PRINT NOGEN                                             81271
SUMTIT1  FDOPT NL,CC=C'#'    SET DATE/TIME/PAGE OPTION           81271
         FDTM  FLAGS,FGSCAN,BZ=SUMTIT1P
         FDPRT '***  P D S S C A N  ***',RADJ,LEN=28             81271
         FDGOTO SUMTIT1S
SUMTIT1P FDPRT '***  P U N K  ***',RADJ,LEN=22                   81271
SUMTIT1S FDPRT 'SUMMARY AND ERROR LISTING',RADJ,LEN=45           81271
         FDPRT *END
         SPACE 1
PRTTIT1  FDOPT NL                                                91002
         FDTM  STYF2,STYTIT80,BNZ=PRTTIT1A                       91002
         FDOPT CC=C'#'       SET AUTOMATIC PAGE/DATE/TIME OPTION 91002
PRTTIT1A FDPRT 'PAGE #S# FOR',NL                                 91002
         FDTM  DS1DSORG,JFCORGPO,BZ=PRTTIT1S
         FDCLI CURMEM,0,BE=PRTTIT1S  SKIP PRE-MEMBER MEMBER
         FDPRT 'MEMBER',PAD
         FDPRT CURMEM,PAD
         FDTM  CURMFLAG,30,BZ=PRTTIT1N
         FDPRT 'SSI='
         FDPRT CURMSSI,HEX
         FDGOTO PRTTIT1X
PRTTIT1N FDPRT 'NO SSI',LEN=12
         FDGOTO PRTTIT1X
PRTTIT1S FDPRT 'DDNAME',PAD
         FDPRT CURDDNM,PAD
         FDCLC CURMSSI,ZEROES,4,BNE=PRTTIT1J
         FDPRT ' ',LEN=12
         FDGOTO PRTTIT1X
PRTTIT1J FDPRT ' DATE='
         FDPRT CURMSSI,HEX,RADJ,LEN=5
         FDPRT ' '           LINE UP WITH MEMBER ENTRY
PRTTIT1X FDPRT PDSDSNEQ,RADJ,PADL,LEN=5                          81271
         FDPRT DS1DSNAM      SHOW DATASET
         FDPRT *END
         SPACE 1
PRTWORKL FDPRT WORKLINE,NL                                      GP02247
         FDPRT *END                                             GP02247
         SPACE 1                                                GP02247
PRTSTOP  FDOPT NL,CC=C'0'                                       GP02247
         FDPRT 'RUN TERMINATED BY STOP COMMAND'                 GP02247
         FDPRT *END                                             GP02247
         SPACE 1                                                GP02247
PRTSCAB  FDTM  DS1DSORG,JFCORGPO,BZ=PRTSCAMX                     81271
         FDOPT NL,CC=C'-'    SET CONDITIONAL SUB-HEADER          81271
PRTSCAM  FDTM  DS1DSORG,JFCORGPO,BZ=PRTSCAMX
         FDPRT 'MEMBER',PAD,NL
PRTSCAMB FDPRT CURMEM,PAD                                        81271
         FDTM  CURMFLAG,30,BZ=PRTSCAMN
         FDPRT ' SSI='
         FDPRT CURMSSI,HEX
         FDGOTO PRTSCAMT
PRTSCAMN FDPRT ' NO SSI',LEN=13
PRTSCAMT FDPRT ' TTR=',PADL
         FDPRT CURMTTR,3,HEX
PRTSCAMX FDPRT *END
         SPACE 1
PRTTIT3  FDOPT NL                                                91002
         FDTM  STYF2,STYTIT80,BZ=PRTTIT3@                        91002
         FDPRT 'VOL=',RADJ,LEN=10                                91002
         FDPRT CURDEVT,DEB                                       91002
         FDPRT '='                                               91002
         FDPRT CURVOL                                            91002
         FDPRT 'UNIT=',PADL,RADJ,LEN=8                           91002
         FDPRT CURUNIT                                           91002
         FDOPT SBA=(*,42)                                        91002
         FDPRT 'RUN ON ##/##/##  AT ##:##:##  PAGE #P##'         91002
         FDPRT *END                                              91002
PRTTIT3@ FDTM  STYFL,STYRECID,BZ=PRTTIT3A                        91002
         FD    ' ',LEN=17                                        91002
PRTTIT3A FDTM  STYFL,STYRECNO,BZ=PRTTIT3B                        91002
         FD    ' ',LEN=15                                        91002
PRTTIT3B FD    LOTSDOTS,(R3)                                     91002
         FDTM  STYF2,STYTIT80,BNZ=PRTTIT3X                       91002
         FDPRT 'VOL=',PADL
         FDPRT CURDEVT,DEB
         FDPRT '='
         FDPRT CURVOL
PRTTIT3X FDPRT *END
LOTSDOTS DC    C'....:....1....:....2....:....3....:....4....:....5'
         DC    C'....:....6....:....7....:....8....:....9....:...10'
         DC    C'....:...11....:...12....:...13....:...14....:...15'
         DC    C'....:...16....:...17....:...18....:...19....:...20'
         DC    C'....:...21....:...22....:...23....:...24....:...25'
         SPACE 1
PRTTIT4  FDOPT NL            SET NEW LINE                        91002
         FDTM  STYFL,STYRECID,BZ=PRTTIT4A  SKIP TTR/OFFSET ?     91002
         FDPRT 'TTTT.RR DISP',LEN=17                             91002
PRTTIT4A FDTM  STYFL,STYRECNO,BZ=PRTTIT4B  SKIP RECORD NUMBER ?  91002
         FDPRT '   RECORD',LEN=15                                91002
PRTTIT4B FDTM  STYF2,STYTIT80,BZ=NUMS9                           91002
         FD    LOTSDOTS,(R3)                                     91002
         FDPRT *END                                              91002
NUMS9    FD    LOTSNUMS,(R3)                                     91002
         FDTM  STYF2,STYTIT80,BNZ=PRTTIT4X                       91002
         FDPRT 'UNIT=',PADL
         FDPRT CURUNIT
PRTTIT4X FDPRT *END
LOTSNUMS DC    25C'1234567890'                                   91002
         SPACE 1
CONRESP  FDPRT 'CURRENT DD IS',NL                                81271
         FDPRT CURDDNM,PAD,DEB                                   81271
         FDPRT PDSDSNEQ,PADL                                     81271
         FDPRT DS1DSNAM,DEB                                      81271
         FDTM  DS1DSORG,JFCORGPO,BZ=CONRESPX
         FDPRT '('                                               81271
         FDPRT CURMEM,DEB                                        81271
         FDPRT ')'                                               81271
CONRESPX FDOPT NL                                                81271
         FDPRT *END
         SPACE 1
PRTLINE  FDOPT NL            REQUEST A NEW LINE                  91002
         FDTM  STYFL,STYRECID,BZ=PRTLINEB  SKIP IF NO TTR/OFFSET 91002
         FDTM  FLAG2,FGNOTAUT+FGNODASD+FGNOSEQ,BZ=PRTLINET  VOLREAD ?
         FDPRT ' ',LEN=7     PLACE HOLDER FOR TTR                90260
         FDGOTO PRTLINEO                                         90260
PRTLINET FDPRT CURTTR,2,HEX
         FDPRT '.'
         FDPRT CURTTR+2,1,HEX
PRTLINEO FDPRT OFFSET,HEX,RADJ,PAD,LEN=4
         FDPRT ' ',LEN=3
         FDTM  MODEFG,MFWYL,BZ=PRTLINEB  WYLBUR FORMAT ?
         FDPRT DCMLINEH,PADL  PRINT HIGH BYTE
         FDPRT DCMLINEB,PADR  PRINT REMAINDER
         FDGOTO PRTLINEC     GO TO COMMON
PRTLINEB FDTM  STYFL,STYRECNO,BZ=PRTLINED                        91002
         FDPRT NUMCARD,I,PAD,RADJ,LEN=9  CURRENT CARD NUMBER
PRTLINEC FDPRT ' ',LEN=5     PADDING
PRTLINED FDPRT 0(R5),(R2)    CARD TEXT; LENGTH IN R2             91002
         FDTM  TESTSTR,2,BZ=PRTNOUP                              90201
         FD    '  NOT',PADL,INT                                  90201
PRTNOUP  FDTM  TESTSTR,1,BZ=PRTDOUP                              90201
         FD    ' UPDATED',PADL                                   90201
PRTDOUP  FDPRT *END                                              90201
         SPACE 1
UPDTEADD FDPRT './',NL,LEN=9
UPDTEADB FDTM  MODEFG,MFADD,BM=UPDTEADC,BO=UPDTEADR              81271
         FDPRT 'ADD',LEN=6                                       81271
         FDGOTO UPDTEADL                                         81271
UPDTEADC FDPRT 'CHANGE',PADR                                     81271
         FDGOTO UPDTEADL                                         81271
UPDTEADR FDPRT 'REPL',LEN=6                                      81271
UPDTEADL FDTM  MODEFG,MFULIST,BZ=UPDTEADN                        81271
         FDPRT 'LIST=ALL,'                                       81271
UPDTEADN FDPRT 'NAME='                                           81271
         FDPRT CURMEM,DEB
         FDTM  CURMFLAG,30,BZ=UPDTEADX
         FDPRT ',SSI='
         FDPRT CURMSSI,HEX
UPDTEADX FDPRT *END
         SPACE 1
UPDTECRD FDCLC 0(R5),DCUPCON,2,BNE=UPDTEC80
         FDPRT '@/',NL       REPLACE ./ BY @/
         FDPRT 2(R5),(R3)    DO REST OF CARD                     89217
         FDPRT *END
UPDTEC80 FDPRT 0(R5),(R2),NL                                     89217
         FDPRT *END
         SPACE 1
SUMDDN   FDPRT 'DDNAME',NL,PADR
         FDPRT CURDDNM
         FDPRT PDSDSNEQ,PADL                                     81271
         FDPRT JFCBDSNM,DEB
         FDPRT 'VOL=',PADL
         FDPRT CURDEVT,DEB
         FDPRT '='
         FDPRT CURVOL
         FDEXEC COMDCB,COMDCBEX   SHOW DSORG, DCB PARMS          81271
SUMDDNEX FDPRT *END
         SPACE 1
SUMDDNMK FDPRT ' INCLUDE MASK=''',PADL
         FDPRT MINCLUDE                                          81271
         FDPRT ''''
         FDPRT *END
         SPACE 1
SUMMEM   FDPRT ' ',NL,LEN=3
         FDTM  DS1DSORG,JFCORGPO,BNZ=SUMMEMPO                    81271
         FDTM  OUTUNIT,OUPDT,BZ=SUMMEMSQ                         81271
SUMMEMPO FDPRT 'MEMBER'                                          81271
         FDPRT CURMEM,PAD
SUMMEMSQ FDPRT NUMCARD,I,RADJ,LEN=8
         FDPRT 'RECORDS READ',PADL
         FDTM  OUTUNIT,OUPRT,BZ=SUMMEMPU
         FDPRT ','
         FDPRT NUMPRT,I,RADJ,LEN=8
         FDPRT 'PRINTED',PADL
SUMMEMPU FDTM  OUTUNIT,OUPUN,BZ=SUMMEMUP
         FDPRT ','
         FDPRT NUMPUN,I,RADJ,LEN=8
         FDPRT 'PUNCHED',PADL
SUMMEMUP FDTM  OUTUNIT,OUPDT,BZ=SUMMEMPG
         FDPRT ','
         FDPRT NUMUPDTE,I,RADJ,LEN=8
         FDPRT 'IEBUPDTE CARDS WRITTEN',PADL
SUMMEMPG FDCLC MEMPAGE,ZEROES,2,BE=SUMMEMEX
         FDOPT SBA=(*,-10)
         FDPRT 'PAGE'
         FDPRT MEMPAGE,I,PADL
SUMMEMEX FDPRT *END
         SPACE 1
SUMDSN   FDPRT '  PDS TOTALS:',NL
         FDPRT SUBCARD,I,RADJ,LEN=8
         FDPRT 'RECORDS READ',PADL
         FDTM  OUTUNIT,OUPRT,BZ=SUMDSNPU
         FDPRT ','
         FDPRT SUBPRT,I,RADJ,LEN=8
         FDPRT 'PRINTED',PADL
SUMDSNPU FDTM  OUTUNIT,OUPUN,BZ=SUMDSNUP
         FDPRT ','
         FDPRT SUBPUN,I,RADJ,LEN=8
         FDPRT 'PUNCHED',PADL
SUMDSNUP FDTM  OUTUNIT,OUPDT,BZ=SUMDSNMB
         FDPRT ','
         FDPRT SUBUPDTE,I,RADJ,LEN=8
         FDPRT 'IEBUPDTE CARDS WRITTEN',PADL
SUMDSNMB FDPRT SUBMEM,I,RADJ,NL,LEN=15
         FDPRT 'MEMBERS READ,',PAD
         FDPRT SUBMSEL,I,RADJ,LEN=8
         FDPRT 'SELECTED FOR PROCESSING,',PAD
         FDPRT SUBMHIT,I,RADJ,LEN=8
         FDPRT 'PROCESSED FOR OUTPUT',PADL
         FDCLC SUBASKIP,ZEROES,4,BE=SUMDSNEX
         FDPRT SUBASKIP,I,RADJ,LEN=9
         FDPRT 'ALIAS ENTRIES IGNORED',PADL
SUMDSNEX FDPRT *END
         SPACE 1
SUMTOT   FDPRT 'RUN TOTALS:',NL
         FDPRT TOTCARD,I,RADJ,LEN=8
         FDPRT 'RECORDS READ',PADL
         FDTM  OUTUNIT,OUPRT,BZ=SUMTOTPU
         FDPRT ','
         FDPRT TOTPRT,I,RADJ,LEN=8
         FDPRT 'LINES PRINTED',PADL
SUMTOTPU FDTM  OUTUNIT,OUPUN,BZ=SUMTOTUP
         FDPRT ','
         FDPRT TOTPUN,I,RADJ,LEN=8
         FDPRT 'CARDS PUNCHED',PADL
SUMTOTUP FDTM  OUTUNIT,OUPDT,BZ=SUMTOTMB
         FDPRT ','
         FDPRT TOTUPDTE,I,RADJ,LEN=8
         FDPRT 'IEBUPDTE CARDS WRITTEN',PADL
SUMTOTMB FDPRT TOTMEM,I,RADJ,NL,LEN=15
         FDPRT 'MEMBERS READ,',PAD
         FDPRT TOTMSEL,I,RADJ,LEN=8
         FDPRT 'SELECTED FOR PROCESSING,',PAD
         FDPRT TOTMHIT,I,RADJ,LEN=8
         FDPRT 'PROCESSED FOR OUTPUT',PADL
         FDCLC TOTASKIP,ZEROES,4,BE=SUMTOTCC
         FDPRT TOTASKIP,I,RADJ,LEN=9
         FDPRT 'ALIAS ENTRIES IGNORED',PADL
SUMTOTCC FDCLC RETCODE,ZEROES,2,BE=SUMTOTEX
         FDPRT '***** COMPLETION CODE',NL
         FDPRT RETCODE,I,PAD
         FDPRT '*****'
SUMTOTEX FDPRT *END
         SPACE 1
WYLNOTED FDPRT ' ',NL,LEN=3
         FDTM  DS1DSORG,JFCORGPO,BZ=WYLNOTSQ
         FDPRT 'MEMBER'
         FDPRT CURMEM,PAD
WYLNOTSQ FDPRT ' ***  DATA NOT IN WYLBUR FORMAT AT TTR='         81292
         FDPRT CURTTR,3,HEX
         FDCLI 0(R3),C' ',BE=WYLNOTSX  EXIT IF NO MESSAGE        81292
         FDPRT ', INVALID',PADR                                  81292
         FDPRT 0(R3),15,DEB                                      81292
WYLNOTSX FDPRT *END                                              81292
NOTWYLCD DC    CL15'BLOCK LENGTH'                                81292
         DC    CL15'RECORD LENGTH'                               81292
         DC    CL15'LINE NUMBER'                                 81292
         DC    CL15'LINE # SEQUENCE'                             81292
         SPACE 1
WYLTRYCD FDPRT ', APPEARS TO BE CARD FORMAT'
         FDPRT *END
         SPACE 1
WYLNOTCD FDPRT ' ',NL,LEN=3
         FDTM  DS1DSORG,JFCORGPO,BZ=WYLNOTCQ
         FDPRT 'MEMBER'
         FDPRT CURMEM,PAD
WYLNOTCQ FDPRT ' ***  DATA NOT IN CARD FORMAT AT TTR='
         FDPRT CURTTR,3,HEX
         FDPRT *END
         SPACE 1
WYLTRYED FDPRT ', APPEARS TO BE WYLBUR EDIT FORMAT'
         FDPRT *END
         SPACE 1                                                 90262
MSGBLBAD FDPRT ' ',NL,LEN=3                                      90262
         FDTM  DS1DSORG,JFCORGPO,BZ=MSGBLBSQ                     90262
         FDPRT 'MEMBER'                                          90262
         FDPRT CURMEM,PAD                                        90262
MSGBLBSQ FDPRT ' ***  I/O ERROR AFTER TTR='                      90262
         FDPRT CURTTR,3,HEX                                      90262
         FDPRT ', RC='                                           90262
         FDPRT DB,4,I                                            90262
         FDPRT '***',PADL                                        90262
         FDPRT *END                                              90262
         SPACE 1
MSGCARD  FDOPT NL,SBA=(*,-80)
         FDPRT 0(R5),80      PRINT SCAN CARD IMAGE
         FDPRT *END
         SPACE 1                                                 90201
MSGUPBAD FDPRT '***** UPDATE IN-PLACE FAILED AT',NL,PADR         90201
         FDPRT 0(R2),2,HEX                                       90201
         FDPRT '.'                                               90201
         FDPRT 2(R2),2,HEX                                       90201
         FDPRT '.'                                               90201
         FDPRT 4(R2),1,HEX                                       90201
         FDPRT '*****',PADL                                      90201
         FDPRT ' ',NL                                            90201
         FD    *END                                              90201
         SPACE 1
MSGBDCRD VCON  '&&CARD IN ERROR'
MSGBDSCN VCON  '-RUN TERMINATED DUE TO ABOVE PROBLEM(S)'        GP02247
MSG2MANY VCON  '0 TOO MANY REQUESTS - RERUN IN LARGER REGION'
MSGHDSCN VCON  '0     SCAN OPTIONS :'                            81271
MSGNODDS VCON  '-***** NO USABLE SYSUT DD CARDS SUPPLIED *****'  81343
MSGUNAUT VCON  '-**** MODULE NOT APF AUTHORIZED - NOT ALL FEATURES AVAI*
               LABLE ****'                                       90260
NULLLINE VCON  '  '
         SPACE 1
MTIOTERR FDPRT 'DDNAME',NL
         FDPRT 4(R2),8,PAD
         FDPRT 'NOT PROCESSED  *** ',PAD
         FDEXEC 0(R3)
         FDPRT *END
MTIONDA  FDPRT 'NOT DIRECT ACCESS'
MTIOCONC FDPRT 'CONCATENATION NOT SUPPORTED'
MTIODUMM FDPRT 'DD DUMMY'
MTIOMVOL FDPRT 'MULTI-VOLUME DATASET'
MTIODUPL FDPRT 'DUPLICATE DD CARD'
         SPACE 1
MDDNERR  FDPRT 'DDNAME',NL
         FDPRT CURDDNM,PAD
         FDPRT ' VOL='
         FDPRT CURDEVT,DEB
         FDPRT '='
         FDPRT CURVOL
         FDPRT 'NOT PROCESSED  *** ',PAD
         FDGOTO 0(R3)
MDDNVOPN FDPRT 'VOLUME OPEN FAILED'
         FDGOTO MDDNCODE
MDDNJFCB FDPRT 'READ JFCB FAILED'
MDDNCODE FDPRT ', RETURN CODE'
         FDPRT DB,4,I,PAD
         FDPRT *END
MDDN213  FDPRT 'DATASET'
         FDPRT JFCBDSNM,DEB,PAD
         FDPRT 'NOT FOUND'
         FDPRT *END
MDDNMASK FDPRT 'NO MATCH FOR MASK'                               81271
         FDPRT '''',PADL                                         81271
         FDPRT DSNMASK,(R4)   LENGTH SET INTO R4                 81271
         FDPRT ''''                                              81271
         FDPRT *END                                              81271
         SPACE 1
MDSNERR  FDPRT 'DDNAME',NL
         FDPRT CURDDNM,PAD
         FDPRT PDSDSNEQ,RADJ,LEN=5                               81271
         FDPRT DS1DSNAM,DEB,PADR
         FDPRT ' VOL='
         FDPRT CURDEVT,DEB
         FDPRT '='
         FDPRT CURVOL
COMDCB   FDPRT '(',PADL                                          81271
         FDPRT FMTDSORG,DEB                                      81271
         FDPRT '/'                                               81271
         FDPRT FMTRECFM,DEB                                      81271
         FDPRT ','                                               81271
         FDPRT FMTLRECL,DEB                                      81271
         FDPRT ','                                               81271
         FDPRT FMTBLKL,DEB                                       81271
         FDPRT ')'                                               81271
COMDCBEX FDOPT IND=6         SET AUTO INDENTATION FOR OVERFLOW TEXT
         FDPRT 'NOT PROCESSED  *** ',PAD
         FDGOTO 0(R3)
MDSNORG  FDPRT 'UNSUPPORTED DATASET ORGANIZATION'
         FDPRT *END
MDSNDCB  FDPRT 'UNSUPPORTED DCB PARAMETERS'
         FDPRT *END
MDSNDSCB FDPRT 'EMPTY OR UNUSABLE DATASET'                       81271
         FDPRT *END                                              81271
         SPACE 1                                                 90260
***********************************************************************
**                                                                   **
**  TABSECT DEFINES ONE ENTRY IN THE SYSIN SCAN REQUEST TABLE        **
**                                                                   **
**  IT IS PLACED HERE SO THAT THE MATCHING TABCLEAR DC IS ADDRESS-   **
**    ABLE.                                                          **
**                                                                   **
***********************************************************************
TABSECT  DSECT ,             TABLE ENTRY MAPPING                GP02245
TABLINK  DC    A(0)          ADDRESS OF NEXT ENTRY, OR 0        GP02245
TABOFFS  DC    0AL4(0)       START/END OFFSET                   GP02245
TABOFF   DC    AL2(0)        START OFFSET                       GP02245
TABOFFE  DC    AL2(0)        END OFFSET                         GP02245
TABFLGS  DC    X'00'         PROCESSING FLAG                     90201
TABAND   EQU   X'80'           'AND' SEARCH CONDITION W/PRIOR    90201
TABNOT   EQU   X'40'           'NOT' - INVERT TEST CONDITION     90201
TABEXP   EQU   X'10'           STRING IS CODED P'.' OR # OR $   GP13234
TABREP   EQU   X'01'           REPLACEMENT TEXT SPECIFIED        90201
TABFLEN  DC    AL1(0)        LENGTH OF FIND TEXT-1              GP02245
TABTEXT  DC    0C' '         VARIABLE LENGTH TEXT STRING        GP02245
TABRLEN  DC    AL1(0)        LENGTH OF REPLACEMENT TEXT          90201
TABTEXT2 DC    0C' '         REPLACEMENT TEXT                    90201
         SPACE 1                                                GP02245
PUNK     CSECT ,                                                GP02245
TABCLRDC DC    A(0),X'FFFFFFFF',X'00',X'FF',C' '                GP02245
TABCLEAR EQU   TABCLRDC,*-TABCLRDC,C'X'  ENTRY CLEAR STRING     GP02245
         SPACE 1
DCUPCON  DC    C'./'
GENMATCH DC    CL8'--------'  GENERAL MATCH MASK
GENMHIGH DC    XL8'FFFFFFFFFFFFFFFF'  HIGH RANGE VALUE
ALPHAS   DC    C'-0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ$#@ '    HEXTAB
HEXTAB   EQU   ALPHAS-C'0'+1 FOR HEX TRANSLATE
         SPACE 2
         PRINT &PRTMAC                                           81271
PATPRINT PRTWORK SYSPRINT,TITLE=6,FOOTER=4,PAGE=0,SPAGE=0  DEV=1
PATPRTLN EQU   *-PATPRINT
SUMPRINT PRTWORK SUMPRINT,SYSTERM,TITLE=3                  DEV=2
SYSPUNCH PUNWORK SYSPUNCH RECFM=FB,WIDTH=80                DEV=3
SYSUPDTE PUNWORK SYSUPDTE RECFM=FB,WIDTH=80                DEV=4
CONSOLE  PRTWORK *CONSOLE                                  DEV=5
SYSIN    INPWORK SYSIN,FILL=C' '
PATDECOM SERVCOMP PFX=PCM,DSECT=NO,   WYLBUR DECOMPRESS PARM LIST      *
               FG1=PCMF1EDT+PCMF1TSO,RECMX=508
DECOMPLN EQU   *-PATDECOM
         PRINT &PRTSYS                                           81271
PATDCB   DCB   DDNAME=SYSUT1,MACRF=(E),DEVD=DA
PATDCBLN EQU   *-PATDCB
         SPACE 1
DCF1     DC    F'1'                                              81271
TABMAIN  DC    A(20*1024,512*1024)  GETMAIN SIZES
GETMEAN  GETMAIN VC,LA=TABMAIN,MF=L
GETMEANL EQU   *-GETMEAN
         SPACE 1
TRANTOHX DC    X'000A0B0C0D0E0F00000000000000000000010203040506070809'
         SPACE 1
PARMTAB  VERBTAB ' ',PARMBUMP,BASE=*,LEN=2    SKIP BLANKS
         VERBTAB ',',PARMBUMP                 SKIP COMMAS
         VERBTAB 'EYEBALL',PRMEYE      FICHE HEADERS
         VERBTAB '%',PRMEYE              SHORT FORM
         VERBTAB 'MACRO',PRMMAC        PROCESS ONLY MACROS
         VERBTAB '&&',PRMMAC             SHORT FORM
         VERBTAB 'NOMACRO',PRMNMAC     PROCESS NONMACROS ONLY
         VERBTAB '¬',PRMNMAC             SHORT FORM
         VERBTAB 'INTEGER',PRMWINT     WYLBUR INTEGER LINE NUMBERS
         VERBTAB '#',PRMWINT             SHORT FORM
         VERBTAB 'EDIT',PRMWEDIT       WYLBUR EDIT FORMAT LINE NUMBERS
         VERBTAB 'HALFWORD',PRMWHWD    WYLBUR INPUT LINES IN HWD FORM
         VERBTAB 'HWD',PRMWHWD           SHORT FORM              81292
         VERBTAB 'OSI',PRMWOSI         WYLBUR INPUT IN OSI FORMAT
         VERBTAB 'GE=',PRMGE           SELECT MEMBERS GE NAME
         VERBTAB 'GT=',PRMGE             ALTERNATE FORM
         VERBTAB '>',PRMGE               SHORT FORM
         VERBTAB 'LE=',PRMLE           SELECT MEMBERS LE NAME
         VERBTAB 'LT=',PRMLE             ALTERNATE FORM
         VERBTAB '<',PRMLE               SHORT FORM
         VERBTAB 'MASK=',PRMMASK       SELECT BY MASK
         VERBTAB 'INCLUDE=',PRMMASK      ALTERNATE FORM
         VERBTAB '=',PRMMASK             SHORT FORM
         VERBTAB 'SKIP=',PRMSKIP       EXCLUDE BY MASK
         VERBTAB 'EXCLUDE=',PRMSKIP      ALTERNATE FORM
         VERBTAB '/',PRMSKIP             SHORT FORM
         VERBTAB 'UPDATE=INPLACE',PRMUPINP  UPDATE INPLACE       90260
         VERBTAB 'INPLACE',PRMUPINP                              90260
         VERBTAB 'UP=ADD',PRMUADD      IEBUPDTE ./ ADD OPTION    81271
         VERBTAB 'UP=REPL',PRMUREP       ./ REPL                 81271
         VERBTAB 'UP=REP',PRMUREP        ./ REPL                 81271
         VERBTAB 'UP=CHANGE',PRMUCHG     ./ CHANGE               81271
         VERBTAB 'UP=CHG',PRMUCHG        ./ CHANGE               81271
         VERBTAB 'UPLIST=ALL',PRMULSTY   ./ ... LIST=ALL,        81271
         VERBTAB 'UPLIST=NO',PRMULSTN    ./ NO LIST=             81271
         VERBTAB 'UPLIST=',PRMULSTY      ./ LIST=ALL             81271
         VERBTAB 'SSI>',PRMSSGE        SSI GREATER THAN
         VERBTAB ':>',PRMSSGE            SHORT FORM
         VERBTAB 'SSI<',PRMSSLE        SSI LESS THAN
         VERBTAB ':<',PRMSSLE            SHORT FORM              81271
         VERBTAB 'SSI=',PRMSSI         SELECT BY SSI
         VERBTAB 'SSI:',PRMSSI           ALTERNATE FORM
         VERBTAB ':',PRMSSI              SHORT FORM
         VERBTAB 'LIST=SCAN',PRMHITS  LIST SCAN HITS ONLY (PUNK)
         VERBTAB 'LIST=YES',PARMBUMP    FULL LIST
         VERBTAB 'LIST=',PARMBUMP
         VERBTAB 'START=',PRMRONE      LIST ONLY - STARTING RECORD
         VERBTAB 'END=',PRMREND        LAST RECORD TO LIST       81271
         VERBTAB 'INC=',PRMRINC        INCREMENT FOR LIST        81271
         VERBTAB 'LIMIT=',PRMRLIM      PRINT LIMIT COUNT PER MEMBER
         VERBTAB 'LIM=',PRMRLIM          SHORT FORM              81271
         VERBTAB 'ALIAS=SKIP',PARMBUMP
         VERBTAB 'ALIAS=MEMBER',PRMALIAS
         VERBTAB 'ALIAS=',PRMALIAS     PROCESS ALIAS AS MEMBER ENTRIES
         VERBTAB 'NOFOLD',PRMNFOLD     RESET FOLD MODE           87032
         VERBTAB 'FOLD',PRMFOLD        SET FOLD MODE (SCAN)      87032
         VERBTAB 'STYLE=',PRMSTYLE     SET PRINT STYLE           91002
         VERBTAB 'FORMAT=',PRMSTYLE   DITTO                      91002
         VERBTAB 'NOID',PRTNOID        SUPPRESS RECORD ID/OFFSET 91002
         VERBTAB 'NONO',PRTNONO        SUPPRESS RECORD NUMBERS   91002
         VERBTAB 'NOREC',PRTNONE       SUPPRESS ALL BUT DATA     91002
         DC    X'FF'         END OF TABLE
         TITLE 'P U N K  ***  LIST/PUNCH/SCAN DISK DATASETS - STYLING'
*        THIS CODE MATCHES THE 'STYLE' PARAMETER AGAINST A LOCAL      *
*        TABLE.  BY DEFAULT, TEH STYLE PARAMETER CONSISTS OF THE      *
*        SYSOUT CLASS (OR MSGCLASS FOR *) PREFIXED TO THE SYSOUT      *
*        FORM 'NUMBER'.  THE RESULTING NINE-BYTE STRING IS COMPARED   *
*        TO THE TABLE.                                                *
*        A BLANK SYSOUT IS REPLACED BY 'A', A BLANK FORM BY 'STD'     *
*                                                                     *
*        THE DEFAULT OUTPUT REQUIRES A 132 BYTE LINE MINIMUM, AND     *
*        PRINTS TTR/OFFSET AND RECORD NUMBER.                         *
         SPACE 1                                                 91002
         PUSH  USING                                            GP97218
         DROP  ,                                                GP97218
         USING SAVE,R13                                         GP97218
         USING GETSTYLE,R8   LOCAL BASE                          91002
GETSTYLE TM    OUTUNIT,OUPRT  PRINT FILE OPEN ?                  91002
         BZR   R14           NO; STYLE IRRELEVANT                91002
         STM   R14,R12,FAKESAVE JUST IN CASE                     91002
         PRTSPACE 1,DEV=2    DOUBLE-SPACE NEXT                   91002
*THIS    CLI   STYLE,C' '    WAS A STYLE OR SYSOUT CLASS GIVEN ? 91002
*IS      BH    *+8           YES                                 91002
*UNNEEDEDMVI   STYLE,C'A'    SET DEFAULT                         91002
         CLI   STYLE,C'A'    LESS THAN A ?                       91002
         BNL   *+8                                               91002
         MVI   STYLE,C'A'    FORCE 'A'                           91002
         CLC   STYLE+1(8),WORKLINE+102 (BLANKS) ANYTHING AT ALL ?
         BNE   *+10          YES                                 91002
         MVC   STYLE+1(8),=CL8'STD.'  SET OLD DEFAULT            91002
         CLC   STYLE+1(8),=CL8'STD.'  OLD DEFAULT ?              91002
         BNE   *+8           NO                                  91002
         MVI   STYLE+1+3,C' '  DELETE TRAILING PERIOD            91002
         LA    R5,TABSTYL1   GET FIRST TABLE ENTRY               91002
         LA    R6,TABSTYL2-TABSTYL1  LENGTH OF ENTRY             91002
         LA    R7,TABSTYLX-(TABSTYL2-TABSTYL1)  NEXT TO LAST ENTRY
GETSTYLP LA    R1,STYLE      POINT TO REQUEST                    91002
         LR    R14,R5        GET MASK ADDRESS                    91002
         LA    R15,L'STYLE   LENGTH OF MASK                      91002
         BAS   R9,CLCMASK    COMPARE WITH MASK                   91002
         BE    GETSTYLT      MATCH                               91002
         BXLE  R5,R6,GETSTYLP  TRY ANOTHER                       91002
         PRTLIST NFSTYLE,DEV=2  WRITE ADVISORY, BUT ON SUMMARY ONLY
GETSTYLT MVC   STYFL(TABSTYL2-TABSTYL1-L'STYLE),L'STYLE(R5)  SET 91002
         ICM   R6,3,STYWIDTH GET DATA WIDTH                      91002
         TM    FLAG3,FGNOID  INHIBIT RECORD ID/OFFSET FIELD ?    91002
         BZ    GETSTNID      NO                                  91002
         TM    STYFL,STYRECID  PRESENT IN SETUP ?                91002
         BZ    GETSTNID      NO; NOTHING TO CHANGE               91002
         NI    STYFL,255-STYRECID  RESET IT                      91002
         LA    R6,17(,R6)    ALLOW FOR LENGTH OF ID              91002
GETSTNID TM    FLAG3,FGNONO  INHIBIT RECORD NUMBER FIELD ?       91002
         BZ    GETSTNNO      NO                                  91002
         TM    STYFL,STYRECNO  PRESENT IN SETUP ?                91002
         BZ    GETSTNNO      NO                                  91002
         NI    STYFL,255-STYRECNO  RESET IT                      91002
         LA    R6,15(,R6)    ALLOW FOR SIZE OF IT                91002
GETSTNNO ICM   R0,3,STYWIDTH  WAS A WIDTH SPECIFIED ?            91002
         BZ    *+8           NO                                  91002
         STCM  R6,3,STYWIDTH SET UPDATED LENGTH                  91002
         PRTLIST SHOWSTYL,DEV=2                                  91002
         ICM   R15,15,@PRTWRK  WORK AREA ADDRESS AVAILABLE ?     91002
         BZ    GETSTYLX      NO                                  91002
         ICM   R0,3,STYCOLS  OUTPUT WIDTH SET ?                  91002
         BZ    GETSTYLX      NO                                  91002
         STMIN R0,PWWIDTH-PRTMAP(R15),TYPE=H  STASH IF NOT OVER ASM
GETSTYLX LM    R14,R12,FAKESAVE RELOAD REGISTERS                 91002
         BR    R14           RETURN TO CALLER                    91002
         SPACE 1                                                 91002
NFSTYLE  FD    '*** NO STYLE ENTRY FOR ',NL                      91002
         FD    STYLE,DEB,PAD                                     91002
         FD    'FOUND; DEFAULT USED ***'                         91002
         FD    *END                                              91002
         SPACE 1                                                 91002
SHOWSTYL FD    'OUTPUT STYLE',NL                                 91002
         FD    STYLE,PAD,DEB                                     91002
         FD    'USING PATTERN',PAD                               91002
         FD    0(R5),L'STYLE,DEB                                 91002
         FD    '. DATA WIDTH'                                    91002
         FD    STYWIDTH,I,PADL,DEB                               91002
*DEFER*  FD    ', INDENT'                                        91002
*DEFER*  FD    STYINDEX,I,PADL,DEB                               91002
         FD    '.',PADR                                          91002
         FDOPT IND=10                                            91002
         FD    'LISTING WILL',PAD,NL                             91002
         FDTM  STYFL,STYRECID,BNZ=SHOWSTYD                       91002
         FD    'NOT',PAD                                         91002
SHOWSTYD FD    'CONTAIN RECORD ID FIELD.',PAD                    91002
         FD    'THE RECORD NUMBER WILL',PAD,NL                   91002
         FDTM  STYFL,STYRECNO,BNZ=SHOWSTYN                       91002
         FD    'NOT',PAD                                         91002
SHOWSTYN FD    'BE INCLUDED.',PAD                                91002
         FDTM  STYF2,STYTIT80,BZ=SHOWSTYW                        91002
         FD    'NARROW',PAD,NL                                   91002
         FDGOTO SHOWSTYS                                         91002
SHOWSTYW FD    'WIDE',PAD,NL                                     91002
SHOWSTYS FD    'TITLES WILL BE USED.',PAD                        91002
         FD    *END                                              91002
         SPACE 1                                                 91002
CLCMASK  CMASK MA=R14,A=R1,ML=R15,L=R15,RET=R9                   91002
         SPACE 1                                                 91002
TABSTYL1 DESTY ASTD,STYRECID+STYRECNO,0,100,0  STANDARD OUTPUT  GP97241
TABSTYL2 EQU   *                                                 91002
         DESTY BSTD,0,STYTIT80,80,3  PSEUDO-PUNCH: 80 COLUMN    GP97241
         AIF   ('&LOCAL' NE 'RTI').NORTSTY                      GP97344
         DESTY C,STYRECID+STYRECNO,0,100,0  STANDARD OUTPUT     GP97344
         DESTY D,0,STYTIT80,80,3  PSEUDO-PUNCH: 80 COLUMN       GP97344
         DESTY H,STYRECID+STYRECNO,0,100,0  STANDARD OUTPUT     GP97344
         DESTY M,0,STYTIT80,80,3  PSEUDO-PUNCH: 80 COLUMN       GP97344
         DESTY P,STYRECID+STYRECNO,0,100,0  STANDARD OUTPUT     GP97344
         DESTY X,STYRECID+STYRECNO,0,100,0  STANDARD OUTPUT     GP97344
.NORTSTY AIF   ('&LOCAL' NE 'RTI').NOLOSTY                       92279
         DESTY ?0048,0,STYTIT80,80,3                             91002
         DESTY ?0080,0,STYTIT80,80,3                             91002
         DESTY ?0081,0,STYTIT80,80,3                             91002
         DESTY ?118?,0,STYTIT80,80,3                             91002
         DESTY ?163?,STYRECID+STYRECNO,0,100,0                   91002
         DESTY ?167?,STYRECID+STYRECNO,0,100,0                   91002
         DESTY X183?,STYRECID+STYRECNO,0,160,0,WIDTH=204         91002
         DESTY X187?,STYRECID+STYRECNO,0,160,0,WIDTH=204         91002
         DESTY X382?,STYRECNO+STYRECID,STYTIT80,100,0            91002
         DESTY X392?,STYRECNO,STYTIT80,80,3                      91002
         DESTY X218?,0,STYTIT80,80,3                             91002
         DESTY ?263?,STYRECID+STYRECNO,0,100,0                   91002
         DESTY ?267?,STYRECID+STYRECNO,0,100,0                   91002
         DESTY X283?,STYRECID+STYRECNO,0,160,0,WIDTH=204         91002
         DESTY X287?,STYRECID+STYRECNO,0,160,0,WIDTH=204         91002
         DESTY X282?,STYRECNO+STYRECID,STYTIT80,100,0            91002
         DESTY X292?,STYRECNO,STYTIT80,80,3                      91002
         DESTY ?0048,0,STYTIT80,80,0                             91002
         DESTY ?0080,0,STYTIT80,80,0                             91002
         DESTY ?0081,0,STYTIT80,80,0                             91002
         DESTY C,STYRECID+STYRECNO,0,100,0  STANDARD OUTPUT     GP97344
         DESTY D,0,STYTIT80,80,3  PSEUDO-PUNCH: 80 COLUMN       GP97344
.NOLOSTY ANOP  ,                                                 91002
         DESTY HSTD,STYRECID+STYRECNO,0,100,0  STANDARD OUTPUT  GP97241
         DESTY XSTD,STYRECID+STYRECNO,0,100,0  STANDARD OUTPUT  GP97241
         DESTY A,STYRECID+STYRECNO,0,100,0  STANDARD OUTPUT     GP97241
         DESTY B,0,STYTIT80,80,3  PSEUDO-PUNCH: 80 COLUMN       GP97241
         DESTY H,STYRECID+STYRECNO,0,100,0  STANDARD OUTPUT     GP97241
         DESTY X,STYRECID+STYRECNO,0,100,0  STANDARD OUTPUT     GP97241
         DESTY ?STD,STYRECID+STYRECNO,0,100,0  STANDARD OUTPUT   91002
TABSTYLX DESTY *,STYRECID+STYRECNO,0,100,0  DEFAULT              91002
         SPACE 1                                                 91002
         LTORG ,                                                 91002
         POP   USING                                            GP97218
         TITLE 'P U N K  ***  LIST/PUNCH/SCAN DISK DATASETS - SCANNER'
         PUSH  USING                                            GP97218
         ORG   PUNK+(*-PUNK+4095)/4096*4096    UNBASED ADDRESS  GP97218
PAGE4    DS    0H                                               GP97218
         ORG   ,             USE IT OR HIGHER                   GP97218
         USING GETSCAN,R8    LOCAL BASE                          90201
GETSCAN  LA    R0,TAB@PREV-(TABLINK-TABSECT)                    GP02245
         ST    R0,TAB@PREV   POINT TO HEAD OF CHAIN             GP02245
         INPOPEN SYSIN,OPT=(DUMMY,NOWTO)  OPEN QUIETLY
         BXH   R15,R15,SCANEOJ   DUMMY OR NOT FOUND
SCANGET  INPGET DEV=1        GET A CARD
         BXH   R15,R15,SCANEOJ   TERMINATE ON ERROR
         LR    R5,R1         SAVE TEXT POINTER
         ICM   R15,15,TABLE  FIRST TIME ?
         BNZ   SCNNTONE      NO
         PRTV  MSGHDSCN,DEV==ERRUNIT  PRINT SCAN HEADER
         MVC   MAINTAB(8),TABMAIN  SET SIZE RANGE (AVOID 604 IN SVS)
         GETMAIN VC,A=TABLE,LA=MAINTAB,MF=(E,GETMAIN)
         BXLE  R15,R15,SCNSTRGE YES; HAVE STORAGE
         PRTV  MSG2MANY,DEV==ERRUNIT  TATTLE
         B     SCANEOJ
SCNSTRGE LM    R3,R4,TABLE   GET START/SIZE
         LH    R2,=AL2(16*1024)   LEAVE ROOM FOR ABENDS
         LA    R1,0(R3,R4)   GET END ADDRESS
         SR    R1,R2         MAKE ADDRESS TO FREE
         FREEMAIN R,LV=(R2),A=(R1)  FREE SOME
         SR    R4,R2         NEW LENGTH
         LR    R14,R3        COPY ADDRESS
         LR    R15,R4        COPY LENGTH
         SLR   R0,R0
         SLR   R1,R1
         MVCL  R14,R0        CLEAR IT
         SH    R4,=H'100'    PREVENT OVERRUN
         AR    R4,R3         MAKE END ADDRESS
         STM   R3,R4,TABLE   SET NEW LIMITS
         ST    R3,TABLECUR   SET CURRENT TABLE POSITION
         MVC   0(L'TABCLEAR,R3),TABCLEAR  INITIALIZE ENTRY      GP02245
         SPACE 1
         USING TABSECT,R4
SCNNTONE PRTLIST MSGCARD,DEV==ERRUNIT  LIST CARD
         LA    R7,71(,R5)    SCAN 72 COLUMNS
         LA    R6,1          SET BXH INCREMENT
         L     R4,TABLECUR   GET NEXT AVAILABLE ENTRY IN TABLE
         BAS   R9,NOBLANK    GET FIRST NON-BLANK
         B     SCANGET       JUST PRINT BLANK CARDS
         CLI   0(R5),C'*'    COMMENTS CARD ?
         BE    SCANGET       YES - JUST GET ANOTHER CARD
         NI    FONE,255-FOMEM  RESET HEX STRING OPTION
SCNNEXT  CLI   0(R5),C'*'    TRAILING COMMENTS ?
         BE    SCNCDONE      YES - CARD DONE
         CLI   0(R5),C'('    CARD COLUMN SPECIFICATION ?
         BE    SCNCOLS
         CLI   0(R5),C'X'    POSSIBLE HEX REQUEST ?
         BNE   SCNNEXTP      NO; TRY FOR EXPRESSION             GP13234
         OI    FONE,FOMEM    SET HEX OPTIONS
         BAS   R9,NXBLANK    SKIP TO NEXT NON-BLANK
         B     SCNBCARD      ERROR IF WIDOW
SCNNEXTP CLC   =C'P''',0(R5) EXPRESSION ?                       GP13234
         BNE   SCNNEXTQ        NO                               GP13234
         OI    TABFLGS,TABEXP  FLAG AS EXPRESSION               GP13234
         LA    R5,1(,R5)     SKIP P                             GP13234
SCNNEXTQ CLI   0(R5),C''''   QUOTED STRING ?
         BE    SCNQUOTE
         CLI   0(R5),C'"'    ALTERNATE QUOTE ?
         BE    SCNQUOTE      YES
         CLC   =C'AND ',0(R5)  COMPOUND TEST ?                   90201
         BE    SCNHVAND      YES                                 90201
         CLC   =C'NOT ',0(R5)  INVERT TEST ?                     90201
         BE    SCNHVNOT      YES                                 90201
SCNBCARD PRTV  MSGBDCRD,DEV==ERRUNIT  BAD CARD
         OI    RETCC,CCERROR  SHOW BAD CARD
         MVC   0(L'TABCLEAR,R4),TABCLEAR  INITIALIZE ENTRY      GP02245
         B     SCANGET
         SPACE 1                                                 90201
SCNHVAND LA    R5,4(,R5)                                         90201
         OI    TABFLGS,TABAND SET COMPOUND TEST                  90201
         B     SCNHVCOM      GO TO COMMON                        90201
         SPACE 1                                                 90201
SCNHVNOT LA    R5,4(,R5)                                         90201
         OI    TABFLGS,TABNOT FLIP TEST CONDITION                90201
         OI    FLAG3,FGNEGSCN  SET GLOBAL TO MATCH              GP02245
SCNHVCOM BAS   R9,NOBLANK    GET NEXT OPERAND                    90201
         B     SCNBCARD      NONE - ERROR                        90201
         B     SCNNEXT       LOOK FOR OPERAND                    90201
         SPACE 1
SCNQUOTE MVC   QUOTE,0(R5)   SAVE DELIMITER
         CLI   TABFLEN,X'FF' BEEN HERE BEFORE ?                 GP02245
         BE    SCNTOLD       YES; GET OLD (SEARCH) TEXT          90201
         TM    TABFLGS,TABREP SECOND TIME HERE ?                 90201
         BNZ   SCNBCARD      YES - FAIL                          90201
         TM    FLAG3,FGNEGSCN  REPLACE AND NOT WON'T WORK       GP02245
         BNZ   SCNBCARD      YES - FAIL                          90201
         OI    TABFLGS,TABREP SET REPLACE OPTION                 90201
         SLR   R15,R15                                           90201
         IC    R15,TABFLEN   GET PRIOR STRING'S LENGTH           90201
         LA    R1,TABTEXT+2(R15)  GET NEW TEXT LOCATION          90201
         LR    R15,R1        SAVE IT                             90201
         B     SCNQUOTT      JOIN COMMON                         90201
SCNTOLD  LA    R1,TABTEXT    POINT TO OUTPUT FIELD               90201
         LR    R15,R1        SAVE START ADDRESS
SCNQUOTT BXH   R5,R6,SCNBCARD  SKIP A BYTE
         CLC   QUOTE,0(R5)   TRAILING/DOUBLED DELIMITER ?
         BNE   SCNQUOTM      NEITHER - MOVE IT
         BXH   R5,R6,SCNQUOTZ  GET NEXT BYTE
         CLC   QUOTE,0(R5)   DOUBLED DELIMITER ?
         BNE   SCNQUOTZ      ELSE END OF INPUT
SCNQUOTM MVC   0(1,R1),0(R5) MOVE ONE BYTE
         TM    TABFLGS,TABREP  REPLACE STRING ?                  90201
         BNZ   SCNQUOLC      YES; LEAVE AS IS                    90201
         TM    MODEFG,MFFOLD   FOLD MODE ?                       87032
         BZ    SCNQUOLC      NO                                  90201
         TR    0(1,R1),UPCASE  MAKE UPPER CASE                   90201
SCNQUOLC TM    FONE,FOMEM    DOING HEX STRING ?                  90201
         BZ    SCNQUOT1      NO
         TR    0(1,R1),UPCASE  MAKE UPPER CASE                   90201
         CLI   0(R1),C'A'    VALID ?
         BL    SCNBCARD      NO
         CLI   0(R1),C'F'    IN LOW RANGE ?
         BNH   SCNQUOT1      YES; KEEP IT
         CLI   0(R1),C'0'    NUMERIC ?
         BL    SCNBCARD      NO; ERROR
         CLI   0(R1),C'9'
         BH    SCNBCARD      DITTO
SCNQUOT1 LA    R1,1(,R1)     SET NEXT O/P LOCATION
         B     SCNQUOTT      TRY NEXT BYTE
SCNQUOTZ SR    R1,R15        GET LENGTH OF STRING
         BNP   SCNBCARD      LESS THAN ONE ?
         BCTR  R1,0          - ONE FOR EXECUTE
         TM    FONE,FOMEM  HEX STRING SUPPLIED ?
         BZ    SCNHVNHX      NO
         ST    R1,DB         SAVE THE LENGTH                     90201
         EX    R1,SCNWKMVC   MOVE TEXT TO WORK AREA+1            90201
         LA    R2,WORKLINE+1 POINT TO START OF TEXT
         TM    DB+3,1        ODD LENGTH STRING ?                 90201
         BNZ   SCNHXEVN      NO (REMEMBER, L-1 IN LENGTH)
         BCTR  R2,0          BACK-SPACE ONE BYTE
         MVI   0(R2),C'0'    PUT A LEADING ZERO IN
SCNHXEVN LA    R1,1(,R1)     GET NUMBER OF INPUT BYTES           90201
         LA    R9,1(,R1)                                         90201
         SRL   R9,1          NUMBER OF OUTPUT BYTES              90201
         LR    R1,R9         SAVE LENGTH                         90201
         BCTR  R1,0          LENGTH-1                            90201
         LR    R3,R15        GET START OUTPUT ADDRESS            90201
SCNDOHEX NC    0(2,R2),=X'1F1F'  CREAM HIGH BITS
         TR    0(2,R2),TRANTOHX  MAKE NUMERICS ONLY
         PACK  0(2,R3),0(3,R2)  PACK                             90201
         LA    R2,2(,R2)     UP INPUT ADDRESS
         LA    R3,1(,R3)     UP OUTPUT                           90201
         BCT   R9,SCNDOHEX   REPEAT                              90201
SCNHVNHX NI    FONE,255-FOMEM  RESET HEX OPTION
         TM    TABFLGS,TABREP  REPLACE STRING ?                  90201
         BZ    SCNQUOLN      NO; SET LENGTH                      90201
         CLM   R1,1,TABFLEN  REPLACE LENGTH = LOOKUP LENGTH ?    90201
         BNE   SCNBCARD      NOT SUPPORTED (YET)                 90201
         BCTR  R15,0         BACKSPACE TO SECOND LENGTH FIELD    90201
         STC   R1,0(,R15)    SET REPLACE LENGTH                  90201
         B     SCNSKIPB      DO MORE                             90201
SCNQUOLN STC   R1,TABFLEN    SAVE STRING LENGTH                 GP02245
         TM    TABFLGS,TABEXP  P'.' REQUEST ?                   GP13234
         BZ    SCNSKIPB      GO TO NEXT FIELD PROCESSOR         GP13234
         STM   R14,R9,12(R13)     SAVE SOME                     GP13234
         SR    R2,R2         FOR CLEAN IC                       GP13234
         SR    R3,R3         FOR CLEAN IC                       GP13234
         IC    R3,TABTEXT    GET CHARACTER OR CODE              GP13234
         L     R14,=A(TRTXWHAT)  GET EXPRESSION INDEX TABLE     GP13234
         TRT   TABTEXT(1),0(R14) WHAT TYPE ?                    GP13234
         L     R14,EXPTAB(R2)  LOAD TABLE FOR THIS CHARACTER    GP13234
         LTR   R14,R14       DEFINED ?                          GP13234
         BNP   TEXCHAR         CHARACTER ONLY                   GP13234
         LA    R4,256        EXAMINE TABLE                      GP13234
TEXTLOOP ICM   R3,1,0(R14)   CHECK NEXT CHARACTER               GP13234
         BNZ   TEXTBUMP        NOT WANTED                       GP13234
         LA    R5,256                                           GP13234
         SR    R5,R4         CHARACTER OFFSET                   GP13234
         STC   R2,TRTTAB(R5) SET NON-ZERO TO TRIP SCANNER       GP13234
TEXTBUMP LA    R14,1(,R14)   NEXT TABLE ENTRY                   GP13234
         BCT   R4,TEXTLOOP     DO ALL                           GP13234
         B     TEXEXIT                                          GP13234
TEXCHAR  STC   R3,TRTTAB(R3) STORE HIT CHARACTER                GP13234
TEXEXIT  LM    R14,R9,12(R13)     RESTORE                       GP13234
         B     SCNSKIPB      GO TO NEXT FIELD PROCESSOR
SCNWKMVC MVC   WORKLINE+1(0),TABTEXT-TABTEXT(R15) MOVE HEX TEXT  90201
         SPACE 1
SCNCOLS  CLI   TABOFF,X'FF'  BEEN HERE BEFORE ?
         BNE   SCNBCARD      YES - TOO BAD
         MVC   TABOFFS,=AL2(0,499)  SET FIRST/LAST VALID COLUMNS
         BAS   R9,NXBLANK    GET NEXT NON-BLANK CHARACTER
         B     SCNBCARD      NONE ?
         BAS   R9,GETNUM     CONVERT TO NUMERIC
         B     SCNBCARD      TOO BAD
         LTR   R0,R0         TEST IT
         BNP   SCNBCARD      ZERO COLUMN ?
         CH    R0,=H'499'    AFTER LAST ?
         BH    SCNBCARD
         BCTR  R0,0          SUBTRACT ONE
         STCM  R0,3,TABOFF   SET INTO START
         STCM  R0,3,TABOFFE  SET DEFAULT END COLUMN
         CLI   0(R5),C')'    SINGLE COLUMN ?
         BE    SCNSKIPL      YES; SKIP ONE AND TRY AGAIN
         CLI   0(R5),C','    ELSE COMMA TO NEXT ONE ?
         BNE   SCNBCARD      NO; TOO BAD
         BAS   R9,NXBLANK    NEXT CHAR
         B     SCNBCARD      TOO BAD - NONE
         BAS   R9,GETNUM     CONVERT TO NUMERICS
         B     SCNBCARD      NONE ?
         LTR   R0,R0         VALID ?
         BNP   SCNBCARD      NO
         CH    R0,=H'499'    IN RANGE ?
         BH    SCNBCARD      NO
         BCTR  R0,0          MINUS ONE
         STCM  R0,3,TABOFFE  SAVE IT
         CLC   TABOFF,TABOFFE  VALID RANGE ?
         BH    SCNBCARD      NO
         CLI   0(R5),C')'    PROPER TERMINATION ?
         BNE   SCNBCARD      NO; FAIL
SCNSKIPL BXH   R5,R6,SCNCDONE  SKIP A COLUMN
SCNSKIPB BAS   R9,NOBLANK
         B     SCNCDONE      NO NON-BLANK
         B     SCNNEXT       TRY MORE PARMS
         SPACE 1
NOBLANK  BCTR  R5,0          BACKSPACE ONE
NXBLANK  BXH   R5,R6,0(R9)   RETURN ERROR IF ALL GONE
NOBLANK1 CLI   0(R5),C' '    BLANK ?
         BNE   4(,R9)        RETURN OK
         BXLE  R5,R6,NOBLANK1  ELSE TRY NEXT ONE
         BR    R9            NO MORE
         SPACE 1
GETNUM   BCTR  R5,0          ANY INPUT REMAINING ?
         BXH   R5,R6,0(R9)   TOO BAD
         CLI   0(R5),C'0'    FIRST DIGIT NUMERIC ?
         BLR   R9            TOO BAD, TOO
         CLI   0(R5),C'9'
         BHR   R9
         SLR   R14,R14
         SLR   R15,R15       INITIALIZE ACCUMULATORS
         LA    R1,8          DO MAX OF EIGHT TIMES
GETNUM1  SLDL  R14,8         MAKE ROOM FOR CURRENT DIGIT
         IC    R15,0(,R5)    TACK THE CURRENT ONE ON
         BXH   R5,R6,0(R9)   ERROR IF NO MORE
         CLI   0(R5),C'0'    NUMERIC ?
         BL    GETNUM2       NO
         CLI   0(R5),C'9'
         BH    GETNUM2
         BCT   R1,GETNUM1    TRY IT
         B     SCNBCARD
GETNUM2  STM   R14,R15,UPD   STASH NUMBERS
         PACK  DB,UPD        MAKE PACKED
         CVB   R0,DB         GET BINARY
         B     4(,R9)
         SPACE 1
SCNCDONE CLI   TABFLEN,X'FF' STRING SUPPLIED ?                  GP02245
         BE    SCNBCARD      NO
         CLI   TABOFF,X'FF'  COLUMNS SUPPLIED ?
         BNE   SCNHVCOL      OK
         MVC   TABOFFS,=AL2(0,499)  SET DEFAULT COLUMNS
SCNHVCOL SLR   R14,R14
         IC    R14,TABFLEN   GET STRING LENGTH - 1              GP02245
         TM    TABFLGS,TABREP REPLACE MODE ?                     90201
         LA    R3,TABTEXT+1(R14)  SET NEW START ADDRESS          90201
         BZ    SCNHVCMT      NOT REPLACE                         90201
         SLR   R15,R15                                           90201
         IC    R15,0(,R3)    GET REPLACE LENGTH                  90201
         LA    R3,2(R3,R15)  SKIP REPLACE LENGTH AND TEXT        90201
         OI    FLAG2,FGUPDATA  SET UPDATE REQUEST                90260
SCNHVCMT LH    R15,=AL2(499-2)  GET LAST COLUMN - 2              90201
         SR    R15,R14       COMPUTE LAST VALID COMPARE POSITION
         ICM   R14,3,TABOFFE  GET LAST REQUEST
         CR    R14,R15       WILL IT FIT ?
         BNH   *+8           YES
         STCM  R15,3,TABOFFE   ELSE SET NEW END LOCATION
         CLC   TABOFF,TABOFFE  VALID RANGE ?
         BH    SCNBCARD      NO; FAIL IT
         SLR   R14,R14
         ICM   R14,3,TABOFF
         CH    R14,TABOFFLO
         BNL   *+8
         STH   R14,TABOFFLO  SET LOW START SCAN COLUMN
         ICM   R14,3,TABOFFE   GET END SCAN
         CH    R14,TABOFFHI
         BNH   *+8
         STH   R14,TABOFFHI  SET HIGH END SCAN COLUMN
         SLR   R14,R14       CLEAR AGAIN                         90201
         IC    R14,TABFLEN   GET LENGTH-1 OF COMPARE             90201
         SLR   R15,R15                                           90201
         ICM   R15,3,TABOFFE  GET COLUMN-1 OF END                90201
         LA    R14,1(R15,R14)   GET LAST COLUMN NEEDED FOR CLC   90201
         STMAX R14,TABOFFND,TYPE=H  SAVE HIGHEST CARD COLUMN NEEDED
         SLR   R14,R14
         IC    R14,TABTEXT
         STC   R14,TRTTAB(R14)   SET TRT STOP
         LTR   R14,R14       HEX ZERO REQUESTED ?                90179
         BNZ   *+8           NO                                  90179
         MVI   TRTTAB,X'40'  STASH A NON-ZERO VALUE              90179
         TM    MODEFG,MFFOLD  FOLD MODE ?                        87032
         BZ    SCNNFOLD      NO                                  87032
         LA    R15,X'40'                                         87032
         XR    R14,R15       FLIP THE FOLD BIT                   87032
         STC   R14,TRTTAB(R14)  CATCH BOTH CASES BEFORE TRANSLATE
         BNZ   *+8           SKIP IF NOT ZERO                    90179
         MVI   TRTTAB,X'40'  STASH A NON-ZERO VALUE              90179
SCNNFOLD C     R3,TABLEND    WILL IT FIT ?
         BNH   SCNCFITS      YES
         PRTV  MSG2MANY,DEV==ERRUNIT  NO MORE ROOM
         MVI   RETCC,CCMAJOR  SET ERROR
         L     R3,TABLE      RESET TO START - DIAGNOSE REMAINING INPUT
         SPACE 1
***********************************************************************
**                                                                   **
**  AT THIS POINT WE HAVE A VALID ENTRY (R4->), AND THE NEXT         **
**  AVAILABLE ADDRESS (R3).                                          **
**                                                                   **
**  THE ENTRY IS CHAINED TO THE BLOCK POINTED TO BY TAB@PREV         **
**                                                                   **
***********************************************************************
SCNCFITS ST    R3,TABLECUR   SET NEW TABLE LOCATION
         MVC   0(L'TABCLEAR,R3),TABCLEAR  INITIALIZE ENTRY      GP02245
         L     R14,TAB@PREV  GET PRIOR ENTRY                    GP02245
         ST    R4,TABLINK-TABSECT(,R14)  CHAIN IT IN            GP02245
         ST    R4,TAB@PREV   SAVE FOR NEXT TIME                 GP02245
         B     SCANGET       GET THE NEXT CARD
         DROP  R4
         SPACE 1
SCANEOJ  CLI   RETCC,0       TEST FOR ERRORS
         BE    SCNCDSOK      OK
         PRTV  MSGBDSCN,DEV==ERRUNIT  TOO BAD
         B     EXIT          EXIT UNGRACEFULLY
         SPACE 1
SCNCDSOK ICM   R4,15,TABLE+4 GET TABLE END ADDRESS-100          GP99158
         BZ    SCNDONE       SKIP IF NONE
         LA    R4,100(,R4)   ADD SAFETY BACK IN
         L     R3,TABLE      GET START OF TABLE
         CLI   TABFLEN-TABSECT(R3),X'FF'   ANY ENTRIES ?        GP02245
         BNE   SCANFRET      YES; FREE SOME
         SR    R4,R3         MAKE LENGTH
         XC    TABLE(8),TABLE   CLEAR TABLE POINTERS
         B     SCANFREE      FREE ALL OF GOTTEN STORAGE
SCANFRET SRL   R4,3
         SLL   R4,3          ROUND TO DOUBLE WORD
         L     R3,TABLECUR   GET STOP ADDRESS
         LA    R3,TABTEXT-TABFLEN+7(,R3)  ALLOW FOR END AND ROUND
         SRL   R3,3
         SLL   R3,3          ROUND TO NEXT HIGHER DOUBLE WORD
         SR    R4,R3         GET LENGTH TO FREE
         BNP   SCNDONE       NONE?  WILL BOMB ON 80A
SCANFREE FREEMAIN R,LV=(R4),A=(R3)  FREE IT UP
SCNDONE  PRTSPACE 1,DEV==ERRUNIT   SPACE A BIT
         PRTLIST FOPTIONS,DEV==ERRUNIT  DISPLAY THE RUN OPTIONS
         B     LOOKTIOT
         SPACE 1
FOPTIONS FDPRT 'RUN OPTIONS FOR',NL
         FDTM  FLAGS,FGSCAN,BZ=FOPUNK
         FDPRT 'PDSSCAN :',PAD
         FDCLC TABLE,ZEROES,4,BNE=FOPCOM
FOPPALL  FDPRT 'SCAN OPTIONS OMITTED; ALL DATA WILL BE PROCESSED'
         FDGOTO FOPCOM
FOPUNK   FDPRT 'PUNK :',PAD
         FDCLC TABLE,ZEROES,4,BE=FOPPALL
         FDPRT 'COMPLETE MEMBERS WITH A SCAN MATCH WILL BE PROCESSED'
         FDTM  FLAGS,FGLSCAN,BZ=FOPCOM
         FDPRT ', BUT ONLY THE MATCHES WILL BE PRINTED'
FOPCOM   FDOPT IND=10        SET AUTOMATIC INDENTATION           81271
         FDCLC RECONE,DCF1,4,BNE=FOPYREC                         81271
         FDCLC RECEND,ZEROES,4,BNE=FOPYREC                       81271
         FDCLC RECINC,DCF1,4,BNE=FOPYREC                         81271
         FDCLC RECLIM,ZEROES,4,BE=FOPNREC                        81271
FOPYREC  FDPRT 'PRINTING LIMITED TO',NL                          81271
         FDCLC RECLIM,ZEROES,4,BE=FOPYRLIM                       81271
         FDPRT RECLIM,I,PADL                                     81271
FOPYRLIM FDPRT 'RECORDS FROM',PADL                               81271
         FDPRT RECONE,I,PAD                                      81271
         FDCLC RECEND,ZEROES,4,BE=FOPYRECN                       81271
         FDPRT 'TO',PAD                                          81271
         FDPRT RECEND,I                                          81271
         FDGOTO FOPYREC0                                         81271
FOPYRECN FDPRT 'ON',PADL                                         81271
FOPYREC0 FDCLC RECINC,ZEROES,4,BE=FOPNREC                        81271
         FDPRT ', INCREMENTED BY'                                81271
         FDPRT RECINC,I,PADL                                     81271
FOPNREC  FDPRT 'MICROFICHE EYEBALL HEADERS WILL',PADR,NL         81271
         FDTM  FLAGS,FGEYE,BNZ=FOPYEYE
         FDPRT 'NOT',PAD
FOPYEYE  FDPRT 'BE PRINTED'
         FDPRT 'ALIAS ENTRIES WILL BE',PADR,NL                   81271
         FDTM  FLAGS,FGDOAL,BZ=FOPNAL
         FDPRT 'PROCESSED AS MEMBERS'
         FDGOTO FOPDAL
FOPNAL   FDPRT 'IGNORED'
FOPDAL   FDTM  OUTUNIT,OUPDT,BZ=FOPNUP  SKIP UPDTE CODE          81271
         FDPRT 'IEBUPDTE CONTROL CARDS WILL SPECIFY ./',PADR,NL  81271
         FDEXEC UPDTEADB,UPDTEADN                                81271
FOPNUP   FDPRT 'WYLBUR LINE NUMBERS',NL                          81292
         FDTM  WYLMODE,DCMF1OSI+DCMF1HWD,BZ=FOPWNCON             81292
         FDPRT ', CONVERTED FROM'                                81292
         FDTM  WYLMODE,DCMF1HWD,BZ=FOPWCOSI                      81292
         FDPRT 'HALFWORD',PAD                                    81292
         FDGOTO FOPWCFRM                                         81292
FOPWCOSI FDPRT 'OSI',PAD                                         81292
FOPWCFRM FDPRT 'FORMAT,',PAD                                     81292
FOPWNCON FDPRT 'WILL',PAD                                        81292
         FDTM  FLAGS,FGWINT+FGWEDIT,BNZ=FOPWNNUM                 81292
         FDPRT 'NOT',PAD                                         81292
FOPWNNUM FDPRT 'BE INSERTED IN COLUMNS 73/80',PAD                81292
         FDTM  FLAGS,FGWINT+FGWEDIT,BZ=FOPNNUM                   81292
         FDTM  FLAGS,FGWINT,BNZ=FOPDINT                          81292
         FDPRT 'IN EDIT FORMAT'                                  81292
         FDGOTO FOPNNUM                                          81292
FOPDINT  FDPRT 'IN INTEGER FORMAT'                               81292
FOPNNUM  FDTM  MODEFG,MFFOLD,BZ=FOPNNFLD                         90201
         FDCLC TABLE,ZEROES,4,BE=FOPNNFLD                        90201
         FDPRT 'SCAN TESTS WILL BE MADE IN UPPER CASE',NL        87032
FOPNNFLD FDTM  FLAG2,FGUPDATE,BZ=FOPNUPDT                        90201
         FDPRT '''UPDATE IN PLACE'' OPTION REQUESTED',NL         90201
FOPNUPDT FDOPT IND=5         RESET INDENTATION                   90201
         FDPRT 'MEMBER SELECTION OPTIONS :',PADR,NL              81271
         FDOPT IND=10        RESTORE INDENTATION                 81271
         FDTM  FLAGS,FGNMAC+FGSMAC,BNZ=FOPSEL
         FDCLC LONAME,ZEROES,8,BNE=FOPSEL
         FDCLC HINAME,GENMHIGH,BNE=FOPSEL
         FDCLC GINCLUDE,GENMATCH,BNE=FOPSEL                      81271
         FDCLC MEXCLUDE,GENMATCH,BNE=FOPSEL                      81271
         FDCLC SSILOW,ZEROES,8,BNE=FOPSEL
         FDCLC SSIHIGH,GENMHIGH,BNE=FOPSEL
         FDCLC SSIMASK,GENMATCH,BNE=FOPSEL
         FDPRT '  NONE;  ALL MEMBERS WILL BE PROCESSED'
         FDGOTO FOPSSEX
FOPSEL   FDTM  FLAGS,FGSMAC,BZ=FOPSSMAC
         FDPRT 'ONLY MACROS WILL BE PROCESSED',NL                81271
FOPSSMAC FDTM  FLAGS,FGNMAC,BZ=FOPSNMAC
         FDPRT 'ONLY NON-MACROS WILL BE PROCESSED',NL            81271
FOPSNMAC FDCLC LONAME,ZEROES,8,BE=FOPSNLOW
         FDPRT 'NAMES GREATER OR EQUAL TO ''',NL                 81271
         FDPRT LONAME,DEBR
FOPROC   FDPRT ''' WILL BE PROCESSED'
FOPSNLOW FDCLC HINAME,GENMHIGH,BE=FOPSNHI
         FDPRT 'NAMES LESS THAN OR EQUAL TO ''',NL               81271
         FDPRT HINAME,DEBR
         FDEXEC FOPROC
FOPSNHI  FDCLC GINCLUDE,GENMATCH,8,BE=FOPSNINC                   81271
         FDPRT 'ONLY NAMES MATCHING THE MASK ''',NL              81271
         FDPRT GINCLUDE                                          81271
         FDEXEC FOPROC
FOPSNINC FDCLC MEXCLUDE,GENMATCH,8,BE=FOPSNEXC                   81271
         FDPRT 'ONLY NAMES NOT MATCHING THE MASK ''',NL          81271
         FDPRT MEXCLUDE                                          81271
         FDEXEC FOPROC
FOPSNEXC FDCLC SSIMASK,GENMATCH,BE=FOPSNSS
         FDPRT 'MEMBERS WITH SSI MATCHING THE MASK ''',NL        81271
         FDPRT SSIMASK
         FDEXEC FOPROC
FOPSNSS  FDCLC SSILOW,ZEROES,8,BE=FOPSNSSL
         FDPRT 'SSI GREATER OR EQUAL TO ''',NL                   81271
         FDPRT SSILOW,DEBR
         FDPRT ''' WILL BE PROCESSED'
FOPSNSSL FDCLC SSIHIGH,GENMHIGH,BE=FOPSSEX
         FDPRT 'SSI LESS THAN OR EQUAL TO ''',NL                 81271
         FDPRT SSIHIGH,DEBR
         FDEXEC FOPROC
FOPSSEX  FDTM  FLAG2,FGUPDATE+FGUPDATA,BZ=FOPUSEX,BO=FOPUSUP     90260
         FDTM  FLAG2,FGUPDATE,BZ=FOPUSNO                         90260
         FD    'TEXT SCAN AND SUBSTITUTION IN OUTPUT REQUESTED',NL
         FDGOTO FOPUSEX                                          90260
FOPUSNO  FD    'UPDATE=INPLACE REQUESTED, BUT NO SCAN UPDATES SUPPLIED'*
               ,NL                                               90260
         FDGOTO FOPUSEX                                          90260
FOPUSUP  FD    'UPDATE=INPLACE REQUESTED FOR MATCHING SCAN SUBSTITUTION*
               ',NL                                              90260
FOPUSEX  FDPRT *END                                              90201
         SPACE 1                                                 90260
         LTORG ,                                                 90260
         POP   USING                                            GP97218
         TITLE 'P U N K  ***  LIST/PUNCH/SCAN DISK DATASETS - SEARCH'
         PUSH  USING                                            GP02245
         USING SCANTEST,R8   SET BY CALLER                       90201
SCANTEST STM   R1,R9,24(R13)  SAVE A BIT                        GP02246
         ICM   R14,15,TABLE  DID WE BUILD A SEARCH TABLE ?      GP02246
         BZ    SCANTMCH      NO; HOW DID THIS HAPPEN? SET MATCH GP02246
         LH    R9,TABOFFLO   GET FIRST SCAN OFFSET               90201
         CR    R9,R6         WILL IT FIT ?
         BNL   SCANTNOG      NO; SKIP TESTS AND RECORD          GP02245
         BCTR  R9,0
TSTNEXT  LA    R9,1(,R9)     GO TO NEXT COLUMN
         LA    R14,0(R5,R9)  GET NEXT COMPARE COLUMN
         LH    R15,TABOFFHI  GET END OF SCAN
         CR    R15,R6        ANY ?
         BL    *+8           YES
         LR    R15,R6
         BCTR  R15,0         ELSE TRUNCATE TO CURRENT LENGTH
         SR    R15,R9        NO. OF COLUMNS TO SCAN-1
         BM    SCANTNOG      DONE WITH THIS CARD                GP02245
         LA    R0,255        SET MAX ON ONE TRT                  82256
         CR    R15,R0        LONGER THAN MAX ?                   82256
         BNH   TSTSCNFF      NO; USE EXECUTE                     82256
         TRT   0(256,R14),TRTTAB  LOOK FOR FIRST CHARACTER       82256
         BNZ   TSTSCNHT      MATCH; NOW CHECK TEXT               82256
         AR    R9,R0         ELSE BUMP POINTER                   82256
         B     TSTNEXT       AND TRY AGAIN                       82256
TSTSCNFF EX    R15,SCANTRT   LOOK FOR SIGNIFICANT CHARACTER      82256
         BZ    SCANTNOG      NONE; NEXT CARD                    GP02245
TSTSCNHT LR    R9,R1         GET HIT COLUMN                      82256
         SR    R9,R5         GET EQUIVALENT OFFSET
         L     R14,TABLE     GET TABLE START ADDRESS
         USING TABSECT,R14   DECLARE TABLE ENTRY
         TM    MODEFG,MFFOLD   FOLD MODE ?                       87032
         BZ    TSTNXTAB      NO; COMPARE ORIGINAL TEXT           90201
         LR    R2,R6         GET BUFFER LENGTH                   90201
         BCTR  R2,0          MAKE RELATIVE                       90201
         MIN   (R2),TABOFFND,TYPE=H  GET SMALLER OF NEEDED TEXT  90201
         SR    R2,R9         NO. OF COLUMNS TO MOVE (-1)         87032
         BM    SCANTNOG      LOGIC ERROR ?                       87032
         EX    R2,SCANCOPY   COPY TO WORK AREA                   87032
         EX    R2,SCANCASE   CHANGE CASE TO UPPER                90201
         LA    R1,TESTSTR    SET COMPARE ADDRESS                 90201
TSTNXTAB CLI   TABFLEN,X'FF' VALID ENTRY ?
         BE    TSTNEXT       NO; TRY NEXT COLUMN
         SLR   R2,R2         CLEAR FOR IC AGAIN                  90262
         IC    R2,TABFLEN    GET ENTRY LENGTH - 1               GP02245
         SLR   R15,R15
         ICM   R15,3,TABOFF  GET START COLUMN
         CR    R9,R15        IN RANGE ?
         BL    TSTNXCOL      NO; SKIP THIS ITEM
         ICM   R15,3,TABOFFE AND END COLUMN
         CR    R9,R15        IN RANGE ?
         BH    TSTNXCOL      NO; SKIP
         TM    TABFLGS,TABEXP  DOING EXPRESSION ?               GP13234
         BZ    TSTFTEST          NO; DO SIMPLE COMPARE          GP13234
         STM   R14,R5,EXPSAVE  SAVE REGISTERS                   GP13234
         LA    R0,1(,R2)     NUMBER OF CHARACTERS TO TEST       GP13234
         LA    R3,TABTEXT    FIRST ONE TO TEST                  GP13234
         LR    R14,R1        COPY INPUT STRING ADDRESS          GP13234
TSTXLOOP SR    R4,R4         CLEAR FOR CHARACTER                GP13234
         SR    R2,R2         CLEAR FOR TRT RESULT               GP13234
         L     R1,=A(TRTXWHAT)    GET RESOLUTION TABLE          GP13234
         TRT   0(1,R3),0(R1) WHAT DO WE DO WITH THIS ?          GP13234
         L     R15,EXPTAB(R2)     LOAD TABLE                    GP13234
         LTR   R15,R15       HAVE ONE ?                         GP13234
         BZ    TSTXCLC         NO; SIMPLE COMPARE               GP13234
         TRT   0(1,R14),0(R15)    WANTED CHARACTER ?            GP13234
         BNZ   TRTXFAIL             NO; STOP                    GP13234
TRTXBUMP LA    R14,1(,R14)   ADVANCE INPUT COLUMN               GP13234
         LA    R3,1(,R3)     ADVANCE TEST COLUMN                GP13234
         BCT   R0,TSTXLOOP     AND TRY AGAIN                    GP13234
TRTXSUCC LM    R14,R5,EXPSAVE     RESTORE REGISTERS             GP13234
         B     SCANSUCC             AND TEST NEXT ENTRY         GP13234
TSTXCLC  CLC   0(1,R14),0(R3)  MATCH FOR CHARACTER ?            GP13234
         BE    TRTXBUMP      YES; CONTINUE                      GP13234
TRTXFAIL LM    R14,R5,EXPSAVE     RESTORE REGISTERS             GP13234
         B     TSTNXCOL             AND TEST NEXT ENTRY         GP13234
         SPACE 1
TSTFTEST EX    R2,SCANCLC    DO CLC
         BNE   TSTNXCOL      NO MATCH; ADVANCE TO NEXT ENTRY     90201
SCANSUCC MVI   TESTSTR,0     RESET FLAG                          90201
         AR    R9,R2         DON'T SCAN MATCHED TEXT AGAIN      GP02245
         TM    TABFLGS,TABREP REPLACE OPTION ?                   90201
         BZ    SCANTMCH      NO                                  90201
         TM    FLAG3,FGNEGSCN  'NOT' OPTION ?                   GP02245
         BNZ   SCANTMCH      YES; DOESN'T WORK WITH REPLACE     GP02245
         LA    R15,TABTEXT+2(R2)  GET REPLACE TEXT               90201
         EX    R2,SCANMVC    REPLACE TEXT                        90201
         OI    TESTSTR,1     SHOW THIS RECORD REPLACED           90201
         TM    MODEFG,MFWYL  DOING WYLBUR COMPRESSED ?           90201
         BZ    TSTDOREP      NO; SET UPDATE                      90201
         OI    TESTSTR,2     SET UPDATE BYPASSED                 90201
         B     SCANTMCH      AND RETURN                          90201
TSTDOREP OI    FLAG2,FGZAPPED  SHOW TEXT REPLACED                90201
         SPACE 1                                                GP02245
SCANTMCH LM    R1,R9,24(R13)  SAVE A BIT                        GP02246
         B     4(,R9)        RETURN +4 - MATCHED REQUEST        GP02246
         SPACE 1                                                 90201
TSTNXCOL ICM   R14,15,TABLINK  LOAD AND TEST NEXT ENTRY         GP02245
         BZ    TSTNEXT       NONE; TRY THE NEXT COLUMN          GP02245
         TM    TABFLGS,TABAND  SUBSIDIARY ENTRY ?               GP02245
         BNZ   TSTNXCOL      YES; IGNORE IT                     GP02245
         B     TSTNXTAB      HAVE ONE; TRY IT                   GP02245
         SPACE 1                                                GP02245
SCANTNOG LM    R1,R9,24(R13)  RESTORE SOME                      GP02246
         BR    R9            RETURN +0 - NO MATCH               GP02245
         SPACE 1
         LTORG ,                                                GP13234
         SPACE 1
SCANCASE MVC   TESTSTR(0),0(R1)  COPY INPUT STRING (IF FOLD)     90201
SCANCOPY TR    TESTSTR(0),UPCASE  UPPER CASE INPUT STRING        90201
SCANCLC  CLC   0(0,R1),TABTEXT  CHECK FOR MATCH                  90201
SCANTRT  TRT   0(0,R14),TRTTAB  TEST ON FIRST CHARACTER
SCANMVC  MVC   0(0,R1),0(R15)  REPLACE TEXT                      90201
         SPACE 1
*   TABLE ADDRESSES FOR P'-' SEARCHES                           GP13234
*        VALUE IS 0 FOR DESIRED CHARACTERS, NON-ZERO OTHERWISE  GP13234
*
EXPTAB   DC    A(0,TRTNANY,TRTNNOB,TRTNPRT,TRTNDIG,TRTYDIG)     GP13234
         DC    A(TRTNALP,TRTNLOW,TRTNUPP,TRTNSPC,TRTNANU)       GP13234
         DC    A(TRTNALM,TRTNNAM)                               GP13234
         SPACE 1
UPCASE   DC    256AL1(*-UPCASE)  NO CHANGE TRANSLATE TABLE       90201
         TRENT UPCASE,*-UPCASE+X'40',(X'81',9),(X'91',9),(X'A2',8)
         ORG   ,             FINISH UPPER-CASE TRANSLATE TABLE   90201
         SPACE 1
TRTNANY  DC    256X'0'       MATCHES EVERYTHING                 GP13234
         SPACE 1
TRTNNOB  DC    256X'0'       TRT TABLE TO SKIP NON-BLANKS       GP13234
         TRENT TRTNNOB,4,C' '          SKIP NON-BLANKS          GP13234
         SPACE 1
TRTNPRT  DC    256X'0'       TRT TABLE TO SKIP NON-ALPHAS       GP13234
         TRENT TRTNPRT,4,C' ',    BLANK IS PRINTABLE ?          GP13234
         TRENT ,4,C'@',C'#',C'$',    NATIONALS                  GP13234
         TRENT ,4,(X'4A',7),(X'5A',8),(X'69',9),(X'7A',6)       GP13234
         TRENT ,4,X'C0',X'D0',X'E0',                            GP13234
         TRENT ,4,X'AD',X'BD',X'8B',X'9B',        BRACKETS      GP13234
         TRENT ,4,(X'81',9),(X'91',9),(X'A2',8),  MINISCULES    GP13234
         TRENT ,4,(X'C1',9),(X'D1',9),(X'E2',8),  MAJUSCULES    GP13234
         TRENT ,4,(C'0',10)     IGNORE DIGITS                   GP13234
         SPACE 1
TRTNDIG  DC    256X'4'       TRT TABLE TO SKIP NON-DIGITS       GP13234
         TRENT TRTNDIG,0,(C'0',10)     IGNORE DIGITS            GP13234
         SPACE 1
TRTYDIG  DC    256X'0'       TRT TABLE TO SKIP DIGITS           GP13234
         TRENT TRTYDIG,4,(C'0',10)     FIND DIGITS              GP13234
         SPACE 1
TRTNALP  DC    256X'4'       TRT TABLE TO SKIP NON-ALPHAS       GP13234
         TRENT TRTNALP,0,(X'81',9),(X'91',9),(X'A2',8),         GP13234
         TRENT TRTNALP,0,(X'C1',9),(X'D1',9),(X'E2',8)          GP13234
         SPACE 1
TRTNLOW  DC    256X'4'       TRT TABLE TO SKIP LOW-ALPHAS       GP13234
         TRENT TRTNLOW,0,(X'81',9),(X'91',9),(X'A2',8)          GP13234
         SPACE 1
TRTNUPP  DC    256X'4'       TRT TABLE TO SKIP UPP-ALPHAS       GP13234
         TRENT TRTNALP,0,(X'C1',9),(X'D1',9),(X'E2',8)          GP13234
         SPACE 1
TRTNSPC  DC    256X'4'       TRT TABLE TO FIND SPECIALS         GP13234
         TRENT TRTNSPC,0,X'C0',X'D0',X'E0'                      GP13234
         TRENT ,0,(X'4A',7),(X'5A',7),(X'69',9)                 GP13234
         SPACE 1
TRTNANU  DC    256X'4'       TRT TABLE TO SKIP ALPHA/NATIONAL   GP13234
         TRENT TRTNANU,0,(X'C1',9),(X'D1',9),(X'E2',8)          GP13234
         TRENT TRTNANU,0,C'@',C'#',C'$'     NATIONALS           GP13234
         SPACE 1
TRTNALM  DC    256X'4'       TRT TABLE TO SKIP ALPHAMERIC/NAT   GP13234
         TRENT TRTNALM,0,(X'C1',9),(X'D1',9),(X'E2',8)          GP13234
         TRENT TRTNALM,0,C'@',C'#',C'$',    NATIONALS           GP13234
         TRENT TRTNALM,0,(C'0',10)     IGNORE DIGITS            GP13234
         SPACE 1
TRTNNAM  DC    256X'4'       TRT TABLE TO FIND ALPHAMERIC/NAT   GP13234
         TRENT TRTNNAM,0,(X'C1',9),(X'D1',9),(X'E2',8)          GP13234
         TRENT TRTNNAM,0,C'@',C'#',C'$',    NATIONALS           GP13234
         TRENT TRTNNAM,0,(C'0',10)     IGNORE DIGITS            GP13234
         SPACE 1
TRTXWHAT DC    256X'0'       TRT TABLE TO CHECK P' ' TEXT       GP13234
         TRENT TRTXWHAT,04,C'=',  FIND ANY CHARACTER            GP13234
         TRENT ,08,C'¬',C'~',     ANY NON-BLANK                 GP13234
         TRENT ,12,C'.',          ANY NON-DISPLAYABLE           GP13234
         TRENT ,16,C'#',          ANY NUMERIC DIGIT             GP13234
         TRENT ,20,C'-',          ANY NON-NUMERIC               GP13234
         TRENT ,24,C'@',          ANY ALPHABETC UPPER OR LOWER  GP13234
         TRENT ,28,C'<',          ANY LOWER CASE ALPHABETIC     GP13234
         TRENT ,32,C'>',          ANY UPPER CASE ALPHABETIC     GP13234
         TRENT ,36,C'$',          ANY NON-ALPHA, NON-NUMERIC    GP13234
         TRENT ,40,C'^',          ANY UPPER ALPHA OR NATIONAL   GP13234
         TRENT ,44,C'%',          ANY UPPER ALPHANUMERIC OR NAT GP13234
         TRENT ,48,C'\'           NO UPPER ALPHANUMERIC OR NAT  GP13234
         POP   USING                                            GP02245
         TITLE 'P U N K  ***  UNAUTHORIZED SUPPORT'              90260
         @VOLRESB ,          UNAUTHORIZED CODE                  GP10197
         TITLE 'P U N K  ***  DATA AREAS'                        90260
         PRINT &PRTMAC                                           81271
SAVE     DSECT ,             WORK AND SAVE AREA
         @VOLREST ,          VOLFAKE SAVE AND WORK AREA         GP10197
VOLSER   DS    CL6           SERIAL                             GP10197
CCHHR3   DS    XL5           FMT 3 ADDRESS                      GP10197
         SPACE 1
DB       DS    D             WORK AREA
UPD      DS    D
ZEROES   DS    D             CONSTANT ZERO
         SERVDEFS ,          ADDRESS OF SERVICE ROUTINES, ETC.  GP10197
RETCC    EQU   RETCODE+3,1,C'B'                                 GP10197
CCWARN   EQU   4               MINOR ERROR
CCERROR  EQU   8               ERROR
CCMAJOR  EQU   12              MAJOR ERROR
CCFATAL  EQU   16              FATAL ERROR
EXPSAVE  DS    8A            EXPRESSION SCAN SAVE AREA          GP13234
         SPACE 1                                                 91002
STYLE    DC    CL9' '        FORMATTING STYLE                    91002
STYFL    DC    X'00'         STYLE FLAGS                         91002
STYRECID EQU   X'80'           RECORD ID/OFFSET                  91002
STYRECNO EQU   X'40'           RECORD NUMBER/SEQUENCE NUMBER     91002
STYF2    DC    X'00'             MORE FLAGS                      91002
STYTIT80 EQU   X'80'           USE 80-BYTE TITLE FORMAT          91002
STYWIDTH DC    HL2'0'        MAXIMUM TEXT WIDTH                  91002
STYINDEX DC    HL2'0'        TEXT INDENTATION                    91002
STYCOLS  DC    HL2'0'        WIDTH OF OUTPUT                     91002
@PRTWRK  DC    A(0)          ADDRESS OF SYSPRINT WORK AREA       91002
CURRECLN DC    F'80'         CURRENT RECORD LENGTH (INPUT)       89217
*SYSPRINT PRTWORK SYSPRINT,TITLE=6,FOOTER=4,PAGE=0,SPAGE=0  DEV=1
SYSPRINT MAPPRT PREFIX=PR,DSECT=NO   EXPAND PRINTER MAPPING
@VOLSAVE DC    A(0)          BACKUP ADDRESS OF VOLUME READ       90260
CSCBPTR  DC    A(0)
ADDRTIOT DC    A(0)          TIOT SCAN - LAST ENTRY USED
DECOMP   SERVCOMP PFX=DCM,DSECT=NO    WYLBUR DECOMPRESS PARM LIST
DEBXLE   SERVPDS PFX=PD,DSECT=NO,RETURN=YES,VER=1                82256
MAINTAB  DC    A(20*1024,512*1024)  GETMAIN SIZES FOR SCAN TABLE
TABLE    DC    A(0)          START OF SCAN REQUEST TABLE
TABLEND  DC    A(0)          END OF TABLE
TABLECUR DC    A(0)          NEXT AVAILABLE ENTRY ADDRESS
TAB@PREV DC    A(0)          ADDRESS OF PREVIOUS REQUEST        GP02245
GETMAIN  DC    XL(GETMEANL)'0'  GETMAIN LIST
TABOFFLO DC    H'499'        LOW SCAN OFFSET
TABOFFHI DC    H'0'          HIGH SCAN OFFSET
TABOFFND DC    H'0'          HIGHEST COLUMN NEEDED IN COMPARE    90201
         SPACE 1
LONAME   DC    XL8'0'        LOW NAME OF RANGE
HINAME   DC    XL8'FFFFFFFFFFFFFFFF'  HIGH NAME OF RANGE
GINCLUDE DC    C'--------'   GLOBAL MASK                         81271
MEXCLUDE DC    C'--------'   GLOBAL EXCLUDE                      81271
SSILOW   DC    XL8'0'        LOW SSI VALUE
SSIHIGH  DC    XL8'FFFFFFFFFFFFFFFF'  HIGH SSI VALUE
SSIMASK  DC    C'--------'   MASK SSI FIELD
MINCLUDE DC    CL8' '        FOR MASKED REQUESTS                 81271
PDSDSNEQ DC    C'DSN='       OR PDS= FOR PO                      81271
         SPACE 1
CURDDNM  DC    CL8' '        CURRENT DD NAME
RECONE   DC    F'0'          START PRINT RECORD                  81271
RECEND   DC    F'0'          END PRINT RECORD OR 0               81271
RECINC   DC    F'0'          PRINT INTERVAL                      81271
RECLIM   DC    F'0'          MAXIMUM PRINT LINES PER MEMBER      81271
NUMSEQ   DS    F             NUMBER OF SEQUENTIAL DATASETS, MASK MODE
BEGINTTR DS    F             TTR OF FIRST BLOCK OF DATASET/MEMBER
CURTTR   DS    F             TTR OF CURRENT BLOCK
LASTBLK@ DS    A             ADDRESS OF CURRENT COUNT FIELD      90201
LASTBLK# DS    A             SIZE OF LAST BLOCK (DATA PORTION)   90262
OFFSET   DC    F'0'          OFFSET IN CURRENT BLOCK
CURDEVT  DS    CL8           JCL UNIT TYPE OF VOLUME
CURTYPE  DS    XL4           UCB TYPE
CURUNIT  DS    CL4           UNIT NAME                          GP03105
CURVOL   DS    CL6           VOLUME SERIAL
OPENLIST DS    0A(0),X'80',AL3(CURDCB)                     1/2
EXITLIST DS    0A(0),X'87',AL3(INFMJFCB)                   2/2
         PRINT &PRTSYS                                           81271
CURDCB   DCB   DDNAME=SYSUT1,MACRF=(E),DEVD=DA
CURMEM   DS    CL8           CURRENT MEMBER NAME (DDNM IF PS/DA)
CURMTTR  DS    XL3           TTR OF FIRST BLOCK OF MEMBER
CURMFLAG DS    X             ALIAS/LENGTH FLAGS
CURMSSI  DS    XL4           SSI OR ZEROES
CURMEMLN EQU   *-CURMEM      BASIC MOVE LENGTH
         DS    X               SPILL BYTE FOR PACK               82256
TOTCARD  DS    F             NUMBER OF CARDS IN RUN
TOTPRT   DS    F             NUMBER PRINTED
TOTPUN   DS    F             NUMBER PUNCHED
TOTUPDTE DS    F             NUMBER WRITTEN TO IEBUPDTE FILE
TOTMEM   DS    F             MEMBERS/SEQ. FILES READ
TOTMSEL  DS    F             NUMBER SELECTED FOR SCAN
TOTMHIT  DS    F             NUMBER WITH OUTPUT
TOTASKIP DS    F             ALIAS ENTRIES SKIPPED
CLRDSN   DS    0F            START OF DATA CLEARED FOR EACH DSN
SUBCARD  DS    F             NUMBER OF CARDS IN CURRENT DATASET
SUBPRT   DS    F             NUMBER PRINTED
SUBPUN   DS    F             NUMBER PUNCHED
SUBUPDTE DS    F             NUMBER WRITTEN TO IEBUPDTE FILE
SUBMEM   DS    F             MEMBERS/SEQ. FILES READ
SUBMSEL  DS    F             NUMBER SELECTED FOR SCAN
SUBMHIT  DS    F             NUMBER WITH OUTPUT
SUBASKIP DS    F             ALIAS ENTRIES SKIPPED
SUBCOUNT EQU   (*-SUBCARD)/4
CLRMEM   DS    0F            START OF DATA CLEARED FOR EACH MEMBER
NUMCARD  DS    F             NUMBER OF CARDS IN CURRENT MEMBER
NUMPRT   DS    F             NUMBER PRINTED
NUMPUN   DS    F             NUMBER PUNCHED
NUMUPDTE DS    F             NUMBER WRITTEN TO IEBUPDTE FILE
NUMCOUNT EQU   (*-NUMCARD)/4
MEMPAGE  DS    H             MEMBER PAGE NUMBER ON SYSPRINT
CLRMEMLN EQU   *-CLRMEM      LENGTH TO CLEAR
CLRDSNLN EQU   *-CLRDSN      LENGTH TO CLEAR FOR NEW DSN
         SPACE 1                                                 81271
DSNFLAG  DC    X'00'         DSN MASK OPTION                     81271
DFDSN    EQU   X'80'           JFCBDSNM SPECIFIED MASK           81271
DFPDS    EQU   X'40'           MASK FOR PDS ONLY                 81271
DFONE    EQU   X'01'           AT LEAST ONE MATCH FOUND          81271
DSNMASKL DC    X'00'         LENGTH OF DSN MASK                  81271
DSNMASK  DC    CL44' '       DSN MASK                            81271
MYJFCB   DC    0A(0)
         IEFJFCBN ,          JFCB FOR CURRENT DD
         SPACE 1
         IECSDSL1 1          CURRENT FORMAT 1 DSCB
LENDSCB1 EQU   *-IECSDSL1
         SPACE 1
         IECSDSL1 3          CURRENT FORMAT 3 DSCB              GP10197
LENDSCB3 EQU   *-IECSDSL3                                       GP10197
         SPACE 1
         IECSDSL1 4          CURRENT FORMAT 3 DSCB              GP10197
LENDSCB4 EQU   *-IECSDSL4                                       GP10197
         SPACE 1
QUOTE    DC    C''''         DELIMITER OF CURRENT STRING
         DC    C' '          WORKLINE-1
WORKLINE DC    CL133' '
         ORG   WORKLINE+1    RE-USE                              81271
FMTDSORG DS    CL3           RETURN FROM SVC DSFMT CALL          81271
FMTRECFM DS    CL6                                               81271
FMTOPTCD DS    CL8                                               81271
FMTBLKL  DS    CL5                                               81271
FMTLRECL DS    CL5                                               81271
FMTORGLN EQU   *-FMTDSORG                                        81271
         ORG   WORKLINE+L'WORKLINE                               81271
         SPACE 1
ERRUNIT  DC    X'00'         ERROR MESSAGE UNIT
         SPACE 1
OUTUNIT  DC    X'00'         UNITS AVAILABLE FOR OUTPUT
OUPRT    EQU   1               SYSPRINT
OUSUM    EQU   2               SUMPRINT
OUPUN    EQU   4               SYSPUNCH
OUPDT    EQU   8               SYSUPDTE
         SPACE 1
FLAGS    DC    X'00'         MAJOR PROCESSING FLAGS
FGSCAN   EQU   X'80'           SCAN INVOKED
FGDOAL   EQU   X'40'           PROCESS ALIAS ENTRIES A MEMBERS
FGSMAC   EQU   X'20'           PROCESS MACROS ONLY
FGNMAC   EQU   X'10'           PROCESS NONMACROS ONLY
FGEYE    EQU   X'08'           PRINT EYEBALL (FICHE) HEADERS
FGWINT   EQU   X'04'           USE INTEGER LINE NUMBERS
FGWEDIT  EQU   X'02'           USE EDIT FORMAT NUMBERS
FGLSCAN  EQU   X'01'           PUNK - LIST SCAN MATCHES ONLY
         SPACE 1                                                 90201
FLAG2    DC    X'00'         MORE FLAGS                          90201
FGNOTAUT EQU   X'80'           WE ARE NOT AUTHORIZED             90260
FGNODASD EQU   X'40'           USE @INPREAD FOR THIS DD CARD     90260
FGNOSEQ  EQU   X'20'           USE @INPREAD FOR THIS DATASET     90260
FGUPDATE EQU   X'04'           UPDATE INPLACE REQUESTED          90201
FGUPDATA EQU   X'02'           UPDATE CONTROL CARDS FOUND        90260
FGZAPPED EQU   X'01'           DATA WERE ALTERED IN THIS BLOCK   90201
         SPACE 1
FONE     DC    X'00'         FIRST TIME FLAGS
FODDN    EQU   X'80'           AT LEAST ONE USABLE DD FOUND      81343
FODSN    EQU   X'40'           DDN/DSN HEADER PROCESSED          81343
FOMEM    EQU   X'20'           MEMBER HEADER PROCESSED           81343
FOMEND   EQU   X'10'           MEMBER TRAILER PROCESSED          81343
FOMATCH  EQU   X'08'           PROCESSING FORCED FOR MEMBER      81343
FOFOUND  EQU   X'04'           SCAN OPTION MATCH                 81343
FOPONE   EQU   X'02'           FIRST PRINT LINE DONE
FOONE    EQU   X'01'           FIRST RECORD PROCESSED
         SPACE 1
MODEFG   DC    X'00'         PROCESSING MODE
MFWYL    EQU   X'80'           WYLBUR DECOMPRESSION
MFFOLD   EQU   X'40'           SCAN OPTION : UPPER-CASE CONVERT  87032
MFADD    EQU   X'30'           ./ ADD IF BOTH OFF                81271
MFREP    EQU   X'30'           ./ REPL IF BOTH ON                81271
MFCHG    EQU   X'10'           ./ CHANGE                         81271
MFULIST  EQU   X'08'           ./ LIST=ALL                       81271
MFDUP    EQU   X'04'           OPEN VOL/DSN DUPLICATION CHECK
MFUND    EQU   X'02'           RECFM=U AND NOT WYLBUR            90260
MFVAR    EQU   X'01'           RECFM=V--                         89151
FLAG3    DC    X'00'         STILL MORE FLAGS                    91002
FGNOID   EQU   X'80'           SUPPRESS RECORD ID/OFFSET FIELD   91002
FGNONO   EQU   X'40'           SUPPRESS RECORD NUMBER FIELD      91002
FGANYSCN EQU   X'04'           FIRST SCAN CARD PROCESSED
FGANDSCN EQU   X'02'           USER REQUESTED 'AND' SCAN
FGNEGSCN EQU   X'01'           USER REQUESTED NOT SCAN          GP02245
         SPACE 1                                                 81292
WYLMODE  DC    X'00'         WYLBUR DECOMPRESS OPTIONS           81292
         SPACE 1
TRTTAB   DC    256X'0'       TRT TABLE
TESTSTR  DC    CL256' '      STRING TO BE TESTED                 87032
SAVEEND  EQU   *             END OF DYNAMIC WORK AREA
         SPACE 1
RDRMAP   MAPINPWK PREFIX=RD  MAP OF INPREAD WORK AREA            90260
         SPACE 1                                                 91002
PRTMAP   MAPPRTWK ,          EXPAND THE FIXED PORTION OF PRINT WORK
         SPACE 1
         DCBD  DSORG=PS,DEVD=DA
         SPACE 1
         IKJTCB ,
         SPACE 1
CSCB     DSECT
         IEECHAIN
         SPACE 1
         IEZJSCB ,
         SPACE 1
CIB      DSECT
         IEZCIB
         SPACE 1
CVTDSECT DSECT ,
         CVT   DSECT=YES
         IHACDE ,
         SPACE 1
         IEFTIOT1 ,
         SPACE 1
BOBSECT  DSECT ,             FORCE DSECT FOR ESA                GP97218
         IEFUCBOB ,
         SPACE 1                                                 89151
         IHAPSA ,                                                89151
         END
