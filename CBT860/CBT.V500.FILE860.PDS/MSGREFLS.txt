MSGREFLS TITLE 'M S G R E F O S  ***  SYSTEM MESSAGE REFERENCE'
         PUNCH '  ORDER MSGREFLS(P) '   MAKE DUMPS EASIER       GP05314
         SPACE 1
         COPY  OPTIONGB      DEFINE GLOBALS                     GP05314
         SPACE 1
         SYSPARM LIST=YES    SET AND SHOW GLOBAL OPTIONS        GP05314
         SPACE 1
***********************************************************************
**                                                                   **
**   PROGRAM MSGREFLS PRINTS CESD (SD, LR, ER) INFORMATION AND       **
**   MESSAGE IDS FROM OBJECT AND LOAD-MODULE LIBRARIES.              **
**                                                                   **
**   SYSIN IS SORTED OUTPUT FROM PROGRAM MSGREFOS                    **
**   SYSPRINT IS PRINT FORMAT, FBA, 121                              **
**                                                                   **
**   THIS CODE (AND THE MATCHING MSGREFOS) WERE DISTRIBUTED AS IBM   **
**   TYPE 3 PROGRAMS UNDER A DIFFERENT NAME. I HAD IT AS SAMSGREF.   **
**                                                                   **
***********************************************************************
**   MAINTENACE:                                                     **
**  2005-11-10  GYP  DISASSEMBLED; MOVED BUFFER TO DYNAMIC STORAGE;  **
**                   CHANGED HEX ZEROES IN OUTPUT TO BLANK           **
***********************************************************************
         SPACE 1
*DEFER*  PRINT &PRTSRC
MSGREFLS PGMHEAD ZERO31,BASE=(R12),AM=24,RM=ANY                 GP05314
         OPEN  MF=(E,OCLIST)  OPEN SYSIN AND SYSPRINT FILES     GP05314
         SPACE 1
LPP      EQU   53            NUMBER OF DATA LINES PER PAGE      GP05314
CPP      EQU   120           NUMBER OF CHARACTERS PER LINE (SANS CC)
         SPACE 1
FILLPAGE L     R3,=A(PRTBUFFN-PRTBUFF)   GET BUFFER LENGTH      GP05314
         CLRL  PRTBUFF,(R3),FILL=C' '    CLEAR BUFFER           GP05314
         MVI   PRTBUFF,C'0' DOUBLE SPACE FIRST DATA LINE
         SPACE 1
***********************************************************************
*   FILL LEFT-HAND COLUMN WITH INPUT DATA                             *
*     NOTE: R3 IS USED TO DETERMINE USED SIZE OF PAGE                 *
***********************************************************************
         LA    R3,LPP
         LA    R10,PRTBUFF
COLLEFT  BAL   R8,GETPART                    PERFORM
         MVC   1(40,R10),0(R1)
         LA    R10,CPP+1(,R10)
         BCT   R3,COLLEFT                    LOOP
         SPACE 1
***********************************************************************
*   FILL MIDDLE COLUMN WITH INPUT                                     *
***********************************************************************
         LA    R4,LPP
         LA    R10,PRTBUFF
COLMID   BAL   R8,GETPART                    PERFORM
         MVC   41(40,R10),0(R1)
         LA    R10,CPP+1(,R10)
         BCT   R4,COLMID                     LOOP
         SPACE 1
***********************************************************************
*   FILL RIGHT-HAND COLUMN                                            *
***********************************************************************
         LA    R4,LPP
         LA    R10,PRTBUFF
COLRIGHT BAL   R8,GETPART                    PERFORM
         MVC   81(40,R10),0(R1)
         LA    R10,CPP+1(,R10)
         BCT   R4,COLRIGHT                   LOOP
         SPACE 1
***********************************************************************
*   WRITE ONE PAGE; TEST FOR END OF INPUT                             *
***********************************************************************
LASTPAGE LA    R4,LPP
         SR    R4,R3
         LTR   R4,R4
         BNP   CHECKEND                                         GP05314
         LA    R10,PRTBUFF
         LA    R0,HEADSECT                                      GP05314
         CLI   1(R10),C' '  MESSAGES OR CESD NAMES ?
         BNE   PUTHEAD      ESD REFERENCE                       GP05314
         LA    R0,HEADMSGS
PUTHEAD  BAL   R14,PUTREC   PAGE TITLE
PUTLOOP  LA    R0,0(,R10)
         BAL   R14,PUTREC                    PERFORM
         LA    R10,CPP+1(,R10)
         BCT   R4,PUTLOOP
CHECKEND TM    PROFLAGS,FGEOF  INPUT END ?                      GP05314
         BZ    FILLPAGE      NO; GET ANOTHER PAGE               GP05314
         CLOSE MF=(E,OCLIST)          CLOSE FILES               GP05314
         PGMEXIT COPYRET=(RETCODE,4)   RETURN AND CLEAN UP      GP05314
         SPACE 2
         PUSH  USING                                            GP05314
***********************************************************************
*   GET INPUT; CHECK FOR BREAK FROM CESD RECORDS TO MESSAGE RECORDS   *
*     FOR BREAK, SET FLAGS TO PRINT CURRENT PAGE AND RETURN STACKED   *
*     RECORD ON THE NEXT CALL.                                        *
***********************************************************************
GETPART  TM    PROFLAGS,FGSTACK  RECORD STACKED ?               GP05314
         BZ    GETPART2      NO                                 GP05314
         L     R1,@STACKED   GET STACKED RECORD ADDRESS         GP05314
         USING SORTREC,R1                                       GP05314
         NI    PROFLAGS,255-FGSTACK                             GP05314
GETPARTN MVC   $STACKED,SORTREC   SAVE PART OF RECORD FOR TEST  GP05314
         OI    PROFLAGS,FGONE  FIRST TIME DONE                  GP05314
         BR    R8            RETURN STACKED RECORD              GP05314
GETPART2 BAL   R14,GETREC    GET ANOTHER RECORD
         OI    SORTKEY,C' '  CHANGE X'00' TO BLANK              GP05314
         CLC   $STACKED(1),SORTSECT  BREAK ?                    GP05314
         BE    GETPARTN      NO; SAVE AND RETURN                GP05314
         CLI   SORTSECT,C' '  INCOMING IS MESSAGE ?             GP05314
         BE    GETPARTB      YES; BREAK NEEDED                  GP05314
         CLI   $STACKED,C' '  PREVIOUS IS MESSAGE ?             GP05314
         BNE   GETPARTN      NO; JUST UNEQUAL ESD               GP05314
GETPARTB ST    R1,@STACKED   SET STACKED RECORD ADDRESS         GP05314
         TM    PROFLAGS,FGONE  FIRST TIME ?                     GP05314
         BZ    GETPARTN      YES; DON'T BREAK                   GP05314
         OI    PROFLAGS,FGSTACK  RECORD STACKED                 GP05314
         B     LASTPAGE      AND PRINT CURRENT PAGE NOW
         POP   USING                                            GP05314
         SPACE 2
***********************************************************************
*   GET NEXT INPUT RECORD                                             *
***********************************************************************
GETREC   ST    R14,GETSAVER
         GET   SYSIN         READ NEXT RECORD
         L     R14,GETSAVER
         BR    R14           RETURN RECORD ADDRESS IN R1
         SPACE 1
INPEODAD OI    PROFLAGS,FGEOF  SHOW INPUT END                   GP05314
         B     LASTPAGE      PUT LAST PAGE
         SPACE 2
         PUSH  USING
***********************************************************************
*    PROCESS ONE OUTPUT RECORD - USE LOCATE MODE TO ACCOMODATE        *
*      LENGTHS OTHER THAN 120  - RECORD ADDRESS IN R0                 *
***********************************************************************
PUTREC   STM   R0,R15,PUTSAVER                                  GP05314
         LA    R6,SYSPRINT   POINT TO THE DCB                   GP05314
         USING IHADCB,R6     DECLARE IT                         GP05314
         LR    R4,R0         COPY INPUT ADDRESS                 GP05314
         LA    R5,CPP+1      SET RECORD SIZE WITH CARR.CNTL     GP05314
         TM    DCBRECFM,DCBRECCC  NEED CONTROL CHARACTER ?      GP05314
         BNZ   PUTRECPC      YES; LEAVE IT                      GP05314
         LA    R4,1(,R4)     SKIP OVER CONTROL CHARACTER        GP05314
         BCTR  R5,0          ADJUST LENGTH                      GP05314
PUTRECPC TM    DCBRECFM,DCBRECU  RECFM IS U ?                   GP05314
         BNM   PUTRECPU      YES                                GP05314
         TM    DCBRECFM,DCBRECF  RECFM IS F ?                   GP05314
         BNZ   PUTRECPT                                         GP05314
         LA    R3,4(,R5)     GET NEEDED SIZE                    GP05314
         MINH  R3,DCBLRECL   GET SMALLER RECORD SIZE            GP05314
         STH   R3,DCBLRECL   STASH FOR PUT                      GP05314
         B     PUTRECPT                                         GP05314
PUTRECPU STH   R5,DCBLRECL   SET RECORD/BLOCK LENGTH FOR U      GP05314
PUTRECPT PUT   SYSPRINT      WRITE ONE RECORD FROM R0 ADDRESS   GP05314
         LR    R2,R1         PRESERVE RECORD ADDRESS            GP05314
         LH    R3,DCBLRECL-IHADCB+SYSPRINT  GET RECORD SIZE     GP05314
         TM    DCBRECFM,DCBRECF  RECFM IS F OR U ?              GP05314
         BNZ   PUTRECMV      YES; JUST MOVE                     GP05314
         STH   R3,0(,R1)     MAKE LEFT HALF OF RDW              GP05314
         STCM  R3,12,2(R1)   ZERO RDW FLAGS                     GP05314
         LA    R2,4(,R2)     SKIP TO OUTPUT DATA                GP05314
         SH    R3,=H'4'      ADJUST OUTPUT LENGTH               GP05314
PUTRECMV ICM   R5,8,=C' '    BLANK FILL IF LARGER               GP05314
         MVCL  R2,R4         MOVE AND PAD, OR TRUNCATE          GP05314
         LM    R0,R15,PUTSAVER                                  GP05314
         BR    R14                                              GP05314
         POP   USING                                            GP05314
         SPACE 1
         PUSH  USING                                            GP05314
         DROP  ,                                                GP05314
         USING PRTDCBEX,R15                                     GP05314
PRTDCBEX SLL   R1,8          CLEAR HIGH BYTE OF DCB ADDRESS     GP05314
         SRL   R1,8                                             GP05314
         USING IHADCB,R1     DECLARE THE DCB                    GP05314
         LA    R2,CPP        SET LRECL DEFAULT, SANS CC         GP05314
         SR    R3,R3         SET BLKSZ ADJUST, SANS CC          GP05314
         CLI   DCBRECFM,0    ANY RECFM ?                        GP05314
         BNE   PRTDCBHR      YES; LEAVE IT ALONE                GP05314
         MVI   DCBRECFM,DCBRECF+DCBRECBR+DCBRECCA  FBA          GP05314
PRTDCBHR TM    DCBRECFM,DCBRECCC  CARRIAGE CONTROL ?            GP05314
         BZ    PRTDCBNC      NO                                 GP05314
         LA    R2,1(,R2)     ALLOW FOR IT                       GP05314
         LA    R3,1(,R3)     ALLOW FOR IT                       GP05314
PRTDCBNC TM    DCBRECFM,DCBRECF  FIXED/UNDEFINED VS. VAR ?      GP05314
         BNZ   PRTDCBNV      YES                                GP05314
         LA    R2,4(,R2)     ALLOW FOR IT                       GP05314
         LA    R3,4(,R2)     ALLOW FOR IT                       GP05314
PRTDCBNV ICM   R0,3,DCBLRECL  ANY RECORD LENGTH ?               GP05314
         BNZ   PRTDCBHL      YES; USE IT                        GP05314
         STH   R2,DCBLRECL                                      GP05314
PRTDCBHL ICM   R0,3,DCBBLKSI  ANY BLOCK SIZE ?                  GP05314
         BNZ   PRTDCBXX      YES; LEAVE IT                      GP05314
         LH    R2,DCBLRECL   GET ACTUAL RECORD LENGTH           GP05314
         AR    R2,R3         ADD ADJUSTMENTS                    GP05314
         STH   R2,DCBBLKSI   MAKE UNBLOCKED                     GP05314
PRTDCBXX BR    R14
         SPACE 1
         LTORG ,
         SPACE 1
OCLIST   OPEN  (SYSIN,INPUT,SYSPRINT,OUTPUT),MF=L               GP05314
         SPACE 1
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PL,EXLST=PRTEXLST GP05314
PRTEXLST DC    0A(0),X'85',AL3(PRTDCBEX)
         SPACE 1
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=GL,EODAD=INPEODAD    GP05314
         SPACE 1
HEADSECT DC    C'1'                                             GP05314
         DC    3CL40' CSECT  TYPE MODULE  COMP  SIZE'           GP05314
HEADMSGS DC    C'1'                                             GP05314
         DC    3CL40' MSG NUM     FICHE   COMP  LOC'            GP05314
         SPACE 2
SAVE     DSECT ,             DYNAMIC WORK/SAVE AREA             GP05314
DB       DS    D                                                GP05314
         SERVDEFS ,          SERVICE DATA                       GP05314
         SPACE 1                                                GP05314
GETSAVER DS    0A            GETREC RETURN SAVER                GP05314
PUTSAVER DS    16A(0)        SAVE EVERYTHING IN PUTREC          GP05314
@STACKED DS    A                                                GP05314
PROFLAGS DS    X             PROCESSING FLAGS                   GP05314
FGEOF    EQU   X'80'           END OF INPUT                     GP05314
FGSTACK  EQU   X'40'           SECTION BREAK - INPUT STACKED    GP05314
FGONE    EQU   X'01'           FIRST TIME BREAK IGNORED         GP05314
$STACKED DS    CL7           SAVE PART OF INPUT RECORD          GP05314
         SPACE 1                                                GP05314
PRTBUFF  DS    (LPP)CL(CPP+1)                                   GP05314
PRTBUFFN EQU   *                                                GP05314
SAVEEND  EQU   *             END OF DYNAMIC AREA                GP05314
         SPACE 1                                                GP05314
         PRINT ON,NOGEN                                         GP05314
         CVT   DSECT=YES,LIST=YES                               GP05314
         IHADCB DSORG=PS,DEVD=DA                                GP05314
         SPACE 1
SORTREC  DSECT ,                                                GP05314
SORTSECT DC    0CL8' ',C' '  RE-USE MSG FOR CESD/MOD NAME       GP05314
SORTMSG  DC    CL7' '                                           GP05314
SORTKEY  DC    CL3' ',C' '                                      GP05314
SORTMEM  DC    CL8' ',C' '                                      GP05314
SORTSRC  DC    CL5' '                                           GP05314
SORTRSV1 DC    C' '                                             GP05314
SORTOFF  DC    CL4' '                                           GP05314
SORTSIZE EQU   *-SORTREC     PRINT SEGMENT SIZE                 GP05314
SORTRSV2 DC    C' '                                             GP05314
         SPACE 1
         END   ,
