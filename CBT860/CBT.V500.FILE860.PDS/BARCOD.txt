BARCOD   TITLE 'B A R C O D  ***  BARCODE GENERATING SUBROUTINE'
*    FORTRAN/COBOL CALLABLE ROUTINE
*           CALL BARCOD(ZIP,BAR)
*           CALL BARCOD USING ZIP BAR
*        ZIP IS 10-BYTE AREA:
*           NNNNN            FIVE DIGITS, FIVE BLANKS
*           NNNNN-           FIVE DIGITS, HYPHEN, BLANKS
*           NNNNN-NNNN       FIVE, HYPHEN, FOUR DIGITS
*           NNNNNNNNN        NINE DIGITS, BLANK
*        BAR IS 26-BYTE OUTPUT AREA
*                                                                93035
*    ENTRY POINT ABCCOD(ZIP11,BAR31) - FOR ADVANCED BAR CODES(!) 93035
*        ZIP11 IS 12-BYTE AREA:                                  93035
*           NNNNN-NNNNNN     FIVE, HYPHEN, SIX                   93035
*           NNNNNNNNNNN      ELEVEN DIGITS, BLANK                93035
*             OR AS ZIP ABOVE WITH TWO EXTRA BLANKS              93035
*        BAR11 IS 31-BYTE OUTPUT AREA                            93035
*
*        INVALID OR NON-NUMERICS RETURN BLANKS IN 'BAR'
*
*        OUTPUT CODES ARE FOR STC 5028 PRINTER WITH 'S11' BAND.
*        ZERO-ZERO (SHORT BARS)  IS X'74'
*        ZERO-ONE  (SHORT/LONG)  IS X'75'
*        ONE-ZERO  (LONG/SHORT)  IS X'76'
*        ONE-ONE   (LONG/LONG)   IS X'77'
*
*        MAY BE ADAPTED FOR OTHER CHARACTER SETS BY CHANGING
*        TRANSLATE TABLE  STCOUT
*                                                                91256
*  GYP - FIXED STUPID CHECKSUM ERROR; MADE CODE A BIT FASTER     91256
*
***********************************************************************
*                                                                     *
*        COPYRIGHT 1991,1993  EXPERT SYSTEM PROGRAMMING, INC.         *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         SPACE 2
BARCOD   START 0                                                 91228
         USING BARCOD,R15    USE CALLER'S SUPPLIED BASE
         STM   R14,R12,12(R13)  SAVE CALLER'S REGISTERS
         LM    R11,R12,0(R1)   LOAD PASSED PARAMETERS
         USING ZIPIN,R11     DECLARE INPUT FIELD
         USING BAROUT,R12    DECLARE OUTPUT
         MVI   BAROUT,C' '   PREPARE TO BLANK USER'S FIELD       92094
         MVC   BAROUT+1(L'BAROUT-1),BAROUT  BLANK IT             92094
         B     BARNINE       SET FOR 9 DIGITS                    93035
         SPACE 1                                                 93035
         ENTRY ABCCOD                                            93035
         USING ABCCOD,R15    TEMPORARY ENTRY-POINT ADDRESSABILITY
ABCCOD   STM   R14,R12,12(R13)  SAVE A BIT                       93035
         LA    R0,ABCCOD-BARCOD  GET ENTRY OFFSET                93035
         SLR   R15,R0        MAKE COMMON BASE                    93035
         USING BARCOD,R15    RESTORE COMMON BASE                 93035
         LM    R11,R12,0(R1)  LOAD PARMS                         93035
         MVI   BAR31,C' '    FIRST BLANK                         93035
         MVC   BAR31+1(L'BAR31-1),BAR31  CLEAR LONGER AREA       93035
         LA    R5,11         SET FOR ELEVEN DIGITS               93035
         CLI   ZIPHY,C'-'    ZIP 'PLUS' ?                        93035
         BE    ABCHYP        YES                                 93035
         MVC   ZIPPER(11),ZIPIN  MOVE AS IS                      93035
         B     ABCCOM                                            93035
ABCHYP   MVC   ZIPPER(5),ZIP5  MOVE LEADING 5                    93035
         MVC   ZIPPER+5(6),ZIP6  MOVE TRAILING SIZ               93035
ABCCOM   CLI   ZIPPER+9,C' '  MORE THAN NINE ?                   93035
         BNE   BARCOM        YES; LEAVE AT 11                    93035
BARNINE  LA    R5,9          SET FOR NINE                        93035
BARGAIN  CLI   ZIPHY,C'-'    ZIP 'PLUS' ?
         BE    BARC59        YES; MAY BE FIVE OR NINE
         MVC   ZIPPER(9),ZIPIN  MOVE NINE
         CLI   ZIPIN+9,C' '  TRAILING BLANK ?                    91256
         BE    BARCOM
         B     QUICKOUT      NO; DEFINITION ERROR                91256
BARC59   MVC   ZIPPER(5),ZIPIN  MOVE FIRST FIVE
         MVC   ZIPPER+5(4),ZIP4  MOVE REMAINING FOUR
BARCOM   CLC   ZIPPER+5(4),BAROUT  SUBCODE BLANK ?               92094
         BNE   *+8           NO
         LA    R5,5          SET FOR BASIC ZIP ONLY
         LR    R9,R5         COPY LENGTH
         LA    R4,ZIPPER     GET HOLD FIELD
         LA    R0,X'F0'      MAKE NUMERIC ZONE                   91256
         LA    R10,10        MAKE DIVISOR
         SLR   R6,R6         CLEAR FOR IC                        91256
         SLR   R3,R3         CHECKSUM ACCUMULATOR
BARNUM   IC    R6,0(,R4)     LOAD DIGIT
         XR    R6,R0         FLIP ZONE BITS (FN -> 0N)           91256
         CR    R6,R10        MORE THAN 9 ?                       91256
         BNL   QUICKOUT      YES; RETURN BLANKS
         AR    R3,R6         ADD TO CHECKSUM ACCUMULATOR
         IC    R6,PARITY(R6)  GET POSTAL CODE                    91256
         STC   R6,0(,R4)     REPLACE IN WORK BUFFER              91256
         LA    R4,1(,R4)     SPACE TO NEXT POSITION              91256
         BCT   R9,BARNUM     GO AGAIN
         SLR   R2,R2         PREPARE FOR CHECKSUM
         DR    R2,R10        GET UNITS DIGIT
         LTR   R3,R2         IS REMAINDER ZERO ?
         BZ    CHKZERO       YES; LEAVE AS IS
         LR    R2,R10        LOAD CONSTANT 10                    91256
         SR    R2,R3         MAKE RESIDUAL CHECK DIGIT
CHKZERO  IC    R2,PARITY(R2)  GET POSTAL BITS FOR CHECKSUM       91256
         STC   R2,0(,R4)     STASH AFTER DATA                    91256
         SPACE 1
*        NOW HAVE VALID 5-BIT CODE FIELD IN 'ZIPPER'
*          CONVERT TO 50-BIT STRING PLUS GUARD DIGIT EACH END    91256
*
         LA    R4,ZIPPER     POINT TO WORK FIELD
         LA    R14,1(,R5)    NUMBER OF DIGITS TO LOAD
         SLR   R0,R0         INTENDED OUTPUT VALUE
         LA    R1,1          SET LEFT GUARD BIT
         SLR   R2,R2         CLEAR WORK REGISTER
NIBLICK  IC    R2,0(,R4)     LOAD NEXT DIGIT
         SLDL  R0,5          SHIFT FIVE BITS FOR NEXT DIGIT
         OR    R1,R2         ADD IN
         LA    R4,1(,R4)     POSITION
         BCT   R14,NIBLICK   DO ALL
         LA    R2,1
         SLDL  R0,1          MAKE ROOM FOR TRAILING GUARD DIGIT
         OR    R1,R2         TRAILING GUARD
         SPACE 1
*        R0/R1 NOW HAS 32 BITS (ZIP) OR 52 BITS (ZIP+4) OR 62 (ABC)
*        PRODUCE 00|01|10|11 GROUPS (16 OR 26 OR 31)
         LA    R3,1(,R5)     NUMBER OF DIGITS TO PROCESS         93035
         MH    R3,=H'5'      NUMBER OF BITS TO DO                93035
*SKIP*   LA    R3,2(,R3)     PLUS GUARD BITS                     93035
         SRA   R3,1          NUMBER OF PAIRS - 1                 93035
         LA    R14,1(,R3)    LOOP COUNTER                        93035
         BCTR  R12,0         FAKE FOR BAROUT-1                   93035
VELCRO   STC   R1,BAROUT(R14)  STASH BIT PATTERN (+CRUD)         93035
         SRDL  R0,2          SHIFT OUT STASHED BITS              93035
         BCT   R14,VELCRO    REPLACE ENTIRE ZIPPER               93035
         LA    R12,1(,R12)   UNFIX BAROUT BASE                   93035
         EX    R3,NCOUT      KILL UNWANTED BITS                  93035
         EX    R3,TROUT      MAKE OUTPUT CODE
QUICKOUT LM    R14,R12,12(R13)  RESTORE USER'S REGISTERS
         SLR   R15,R15       SET GOOD RETURN (LIE?)
         BR    R14           GO BACK
TROUT    TR    BAROUT(0),STCOUT  MAKE PRINTER CODES
NCOUT    NC    BAROUT(0),=31X'03'  MASK FOR SIGNIFICANT BITS     93035
         SPACE 1
ZIPPER   DC    CL12' '       CONVERSION WORK SPACE
         SPACE 1
***********************************************************************
*                                                                     *
*    STC 5028 PRINTER WITH S11 BAND - BAR CODES                       *
*                                                                     *
STCOUT   DC    X'74757677'   00 | 01 | 10 | 11
         SPACE 1
PARITY   DC    X'18030506090A0C111214'  POSTAL DIGITS 0-9 W/PARITY
         SPACE 1
ZIPSECT  DSECT ,             INPUT PARAMETER
ZIPIN    DS    0CL10
ZIP5     DS    CL5                                               93035
ZIPHY    DS    C             USPS 'PLUS'                         93035
ZIP4     DS    0CL4          FOUR MORE                           93035
ZIP6     DS    CL6           ABC DIGITS                          93035
         SPACE 1
BARSECT  DSECT ,             OUTPUT FIELD
BAR31    DS    0CL31         31 BARCODES FOR ABC                 93035
BAROUT   DS    CL26,CL5      16 OR 26 BARCODES
         SPACE 1
         PRINT NOGEN
         REGEQU ,            EXPAND REGISTER MNEMONICS
         END   ,
