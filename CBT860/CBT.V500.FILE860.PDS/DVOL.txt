         TITLE 'DISCLAIMER - INSTALLATION INSTRUCTIONS'
*ORIGINAL SOURCE -- CBT TAPE FILE 058, VERSION 227
*MODIFIED SOURCE -- CBTCOV FILE 059
*REQUIRED MACROS ARE IN CBTCOV.FILE059
         LCLC  &OPSYSTM                 OPERATING SYSTEM MVT/SVS/MVS
&OPSYSTM SETC  'MVS'                    DEFINE OPERATING SYSTEM
*                                         MVT
*                                         SVS (OS/VS2 RELEASE 1)
*                                         MVS (OS/VS2 RELEASE 3)
***********************************************************************
*                                                                     *
*     THIS PROGRAM, DEVELOPED AT FPL OR SUPPLIED BY OTHER USERS       *
*     ON A NON-RESTRICTIVE BASIS, IS OF GENERAL INTEREST              *
*     SUBMITTED FOR UNRESTRICTED DISTRIBUTION.  THIS PROGRAM          *
*     HAS MET A BASIC SET OF PROGRAMMING AND DOCUMENTATION            *
*     STANDARDS, BUT MAY NOT HAVE BEEN PROGRAM TESTED IN ANY          *
*     FORMAL FASHION BY FPL.  THE USER IS EXPECTED TO MAKE THE        *
*     FINAL EVALUATION AS TO THE USEFULLNESS IN HIS OWN               *
*     ENVIRONMENT.                                                    *
*                                                                     *
*     FPL MAKES NO WARRANTY, EXPRESSED OR IMPLIED, INCLUDING, BUT     *
*     NOT LIMITED TO, THE IMPLIED WARRANTIES OR MERCHANTABILITY AND   *
*     FITNESS FOR A PARTICULAR PUTPOSE AS TO THE DOCUMENTATION,       *
*     FUNCTION, OR PERFORMANCE OF THESE PROGRAMS.                     *
*                                                                     *
*     ACCEPTANCE AND USE OF THIS PROGRAM CONSTITUTES A RELEASE        *
*     FROM LIABILITY OF FPL FOR ANY PROBLEMS USE OF THE PROGRAM       *
*     MAY CAUSE AT THE USER'S INSTALLATION.                           *
*                                                                     *
*     USERS ARE INVITED TO SUBMIT SUGGESTIONS OR ERROR DOCUMENTATION  *
*     TO FPL, HOWEVER, NO PROMISE CAN BE MADE THAT SUCH SUGGESTIONS   *
*     WILL BE IMPLEMENTED OR ERRORS CORRECTED.  SUBMIT COMMENTS TO:   *
*              COORDINATOR OF TECHNICAL SYSTEMS                       *
*              SYSTEMS & PROGRAMMING DEPARTMENT                       *
*              FLORIDA POWER & LIGHT COMPANY - GENERAL OFFICE         *
*              P. O. BOX  529100                                      *
*              MIAMI, FLORIDA  33152                                  *
*                                                                     *
*     THIS PROGRAM IS MADE AVAILABLE BY FPL WITHOUT CHARGE OR         *
*     CONSIDERATION.  RECIPIENTS ARE FREE TO MAKE THIS PROGRAM        *
*     AVAILABLE TO OTHERS IN LIKE MANNER.  IT MAY NOT BE SOLD.        *
*                                                                     *
* INSTALLATION INSTRUCTIONS                                           *
*                                                                     *
*     THIS MODULE MAY BE ASSEMBLED AND LINKED INTO A LIBRARY IN THE   *
*     LINK LIST OR SPECIFIED AS A STEPLIB IN THE TSO LOGON PROCEDURE. *
*     THE COMMAND MAY BE INVOKED FROM TSO BY ENTERING THE NAME OR AN  *
*     ALIAS OF THE MODULE.                                            *
*                                                                     *
*     BEFORE ASSEMBLING THE MODULE, ENSURE THAT ALL THE REQUIRED      *
*     MACROS ARE AVAILABLE.  SOME MACROS FROM SYS1.AMODGEN MAY BE     *
*     REQUIRED.  THE $ENTER MACRO USES SYSTEM VARIABLE SYMBOLS        *
*     &SYSDATE AND &SYSTIME.  THESE SYMBOLS ARE NOT SUPPORTED BY ALL  *
*     ASSEMBLERS.  IF THE ASSEMBLER TO BE USED DOES NOT SUPPORT THESE *
*     SYMBOLS, THE $ENTER MACRO MUST BE MODIFIED.                     *
*                                                                     *
*     THE SOURCE PROVIDED IS OPERATING SYSTEM DEPENDENT.  SPECIFY THE *
*     DESIRED OPERATING SYSTEM AS THE VALUE FOR &OPSYSTM.             *
*                                                                     *
*     THE MODULE MAY BE LINKED WITH ATTRIBUTES RENT AND REFR AND IS   *
*     ELIGIBLE FOR INCLUSION IN THE LINK PACK AREA.                   *
*                                                                     *
***********************************************************************
DVOL     TITLE 'DISPLAY VTOC CHARACTERISTICS'
***********************************************************************
*                                                                     *
* DVOL                                                                *
*                                                                     *
* FUNCTION         THE DVOL COMMAND IS USED TO DISPLAY THE AMOUNT OF  *
*                  OF FREE SPACE ON A DIRECT ACCESS DEVICE.           *
*                                                                     *
* DESCRIPTION      DVOL WILL READ THE FORMAT 4 AND FORMAT 5 DSCB'S    *
*                  FROM THE VTOC OF A DIRECT ACCESS VOLUME AND        *
*                  DISPLAY:                                           *
*                   1.  VOLUME SERIAL                                 *
*                   2.  UNIT ADDRESS                                  *
*                   3.  MOUNT STATUS                                  *
*                   4.  USE STATUS                                    *
*                   5.  NUMBER OF BLANK DSCB'S IN THE VTOC            *
*                   6.  CONDITION OF THE VTOC INDICATORS BYTE         *
*                   7.  VSAM DATA FIELDS                              *
*                   8.  TOTAL FREE SPACE IN TRACKS                    *
*                   9.  NUMBER OF FREE EXTENTS                        *
*                  10.  NUMBER OF FREE CYLINDERS                      *
*                  11.  SIZE OF LARGEST EXTENTS (UP TO 5) IN          *
*                       CYLINDERS + TRACKS                            *
*                  12.  SIZE OF LARGEST EXTENTS (UP TO 5) IN TRACKS   *
*                                                                     *
*                  THE RETURN CODE IS SET TO THE TOTAL NUMBER OF      *
*                  TRACKS IN THE LARGEST EXTENTS (UP TO 5) UP TO A    *
*                  MAXIMUM OF 4095.  IF THE NUMBER OF FREE TRACKS     *
*                  EXCEEDS 4095, THE RETURN CODE WILL BE SET TO 4095. *
*                  IF MORE THAN ONE VOLUME IS DISPLAYED, THE RETURN   *
*                  CODE WILL BE REFER TO THE SPACE ON THE LAST        *
*                  VOLUME.  IF AN ERROR CONDITION EXISTS ON THE       *
*                  VOLUME, THE RETURN CODE WILL BE SET TO 0.          *
*                                                                     *
*                  NOTE - IF AN ERROR CONDITION EXISTS ON THE VOLUME, *
*                         THE RETURN CODE WILL BE SET TO 0.           *
*                                                                     *
* ATTRIBUTES       DVOL IS RE-ENTRANT.                                *
*                                                                     *
* SYNTAX           DVOL  (VOLUME-LIST)/*                              *
*                        SHORT/LONG                                   *
*                        HEADER/NOHEADER                              *
*                        PRINT/NOPRINT                                *
*                        SERONLY                                      *
*                                                                     *
*                  REQUIRED - NONE                                    *
*                  DEFAULTS - *  SHORT  HEADER  PRINT                 *
*                                                                     *
* OPERANDS         VOLUME-LIST                                        *
*                       IS A LIST OF ONE OR MOVE VOLUME SERIALS OF    *
*                       MOUNTED DIRECT ACCESS VOLUMES.                *
*                                                                     *
*                       IF NO VOLUME IS ENTERED, OR IF AN * IS        *
*                       SPECIFIED, THE INFORMATION IS DISPLAYED       *
*                       FOR EACH DIRECT ACCESS VOLUME ON THE          *
*                       SYSTEM.                                       *
*                                                                     *
*                  SHORT/LONG                                         *
*                       SPECIFIES THE OUTPUT FORMAT.                  *
*                                                                     *
*                       SHORT - (DEFAULT) SPECIFIES THAT A SINGLE     *
*                               LINE OF OUTPUT IS TO BE DISPLAYED     *
*                               FOR EACH VOLUME.                      *
*                                                                     *
*                               DATA WHICH IS NOT PRODUCED IF SHORT   *
*                               IS USED -                             *
*                                    ONLINE/OFFLINE STATUS            *
*                                    UNLOAD PENDING                   *
*                                    VTOC INDICATORS                  *
*                                    VSAM TIMESTAMPS                  *
*                                    SIZE OF SECOND TO FIFTH EXTENTS  *
*                               THIS DATA IS PRODUCED ONLY WITH THE   *
*                               LONG OUTPUT FORMAT.                   *
*                                                                     *
*                       LONG  - SPECIFIES THAT MULTIPLE LINES OF      *
*                               DATA ARE PRODUCED FOR EACH VOLUME.    *
*                                                                     *
*                  HEADER/NOHEADER                                    *
*                       SPECIFIES WHETHER OR NOT THE HEADER LINES     *
*                       ARE TO BE DISPLAYED.  SINCE HEADERS ARE       *
*                       PRODUCED FOR THE SHORT FORM OF OUTPUT ONLY,   *
*                       THIS OPTION IS MEANINGLESS WHEN LONG IS       *
*                       SPECIFIED.                                    *
*                                                                     *
*                  PRINT/NOPRINT                                      *
*                       NOPRINT MAY BE USED TO SUPPRESS ALL OUTPUT    *
*                       (INCLUDING ERROR MESSAGES).  THIS OPTION      *
*                       MAY BE USED WHEN ONLY THE RETURN CODE IS      *
*                       DESIRED.                                      *
*                                                                     *
*                  SERONLY                                            *
*                       SPECIFIES THAT ONLY A LIST OF MOUNTED         *
*                       VOLUME SERIALS WILL BE DISPLAYED.             *
*                       SERONLY IS IGNORED IF A VOLUME SERIAL IS      *
*                       SPECIFIED.                                    *
*                                                                     *
* SUBCOMMANDS      DVOL HAS NO SUBCOMMANDS.                           *
*                                                                     *
* EXAMPLE          DISPLAY FREE SPACE ON VOLUME WORK01.               *
*                                                                     *
*                  DVOL WORK01                                        *
*                                                                     *
* FPL MACROS USED BY THIS MODULE                                      *
*                                                                     *
*         $ENTER              ENTRY CODING                            *
*         $RTRN               EXIT CODING                             *
*         #TSOBLKS            CONSTRUCT TSO CONTROL BLOCKS            *
*         #TSOMSG             CONSTRUCT MESSAGES IN PUTLINE FORMAT    *
*         #VTCFMT4            MAP FORMAT 4 DSCB                       *
*         #VTCFMT5            MAP FORMAT 5 DSCB                       *
*                                                                     *
* IBM MACROS USED BY THIS MODULE                                      *
*                                                                     *
*         LINK                INVOKE A SERVICE ROUTINE                *
*         OBTAIN              READ A DSCB FROM THE VTOC               *
*         CAMLST              CONSTRUCT OBTAIN PARAMETER LIST         *
*         IKJIOPL             MAP INPUT/OUTPUT PARAMETER LIST         *
*         PUTLINE             OUTPUT A LINE TO THE TERMINAL           *
*         IKJCPPL             MAP COMMAND PROCESSOR PARAMETER LIST    *
*         IKJPPL              MAP PARSE PARAMETER LIST                *
*         IKJRLSA             RELEASE STORAGE OBTAINED BY PARSE       *
*         IKJPARM             BEGIN PARSE PARAMETER CONTROL LIST      *
*         IKJIDENT            DESCRIBE A POSITIONAL PARAMETER         *
*         IKJENDP             END PARSE PARAMETER LIST                *
*         IEFUCBOB     *      MAP UNIT CONTROL BLOCK                  *
*                                                                     *
*         MACROS DESIGNATED BY * MUST BE OBTAINED FROM THE            *
*         SYS1.AMODGEN DISTRIBUTION LIBRARY.                          *
*                                                                     *
* REGISTER ASSIGNMENTS                                                *
*                                                                     *
*          0 - STANDARD LINKAGE                                       *
*          1 - STANDARD LINKAGE                                       *
*          2 - WORK                                                   *
*          3 - WORK                                                   *
*          4 - WORK                                                   *
*          5 - WORK                                                   *
*          6 - WORK                                                   *
*          7 - WORK                                                   *
*          8 - PDE POINTER                                            *
*          9 - PPL/PDL BASE REGISTER                                  *
*         10 - IOPL BASE REGISTER                                     *
*         11 - CPPL BASE REGISTER                                     *
*         12 - PROGRAM BASE REGISTER                                  *
*         13 - SAVE AREA ADDRESS AND WORK AREA DSECT BASE REGISTER    *
*         14 - STANDARD LINKAGE                                       *
*         15 - STANDARD LINKAGE                                       *
*                                                                     *
* MODULE ATTRIBUTES                                                   *
*                                                                     *
*         RENT, REUS, REFR, ENABLED, NON-PRIVILEGED                   *
*                                                                     *
*                                                 9/76 GORDON P. WEST *
***********************************************************************
         TITLE 'UPDATE LOG'
***********************************************************************
*                                                                     *
* 01/20/77  ADD SHORT FORM OF OUTPUT ANA MAKE SHORT FORM DEFAULT      *
*           ADD SHORT/LONG, HEADER/NOHEADER, AND PRINT/NOPRINT KEYWDS *
*           ADD * SUPPORT FOR VOLUME LIST AND MAKE IT DEFAULT         *
*           ADD TEST FOR MOUNT PENDING                                *
*                                                                     *
* 07/15/77  ADD TESTS FOR NOT READY, OFFLINE PENDING                  *
*           ADD SERONLY OPERAND                                       *
*                                                                     *
* 10/24/09  EXPANDED SOME FIELDS TO HANDLE LARGER VOLUMES.            *
*           (FOR CURRENT SYSTEMS SEE DVOL IN CBT429.FILE296           *
*                                                G. POSTPISCHIL       *
*                                                                     *
***********************************************************************
         TITLE 'ENTRY CODING'
DVOL     $ENTER BASE=R12,                                              +
               RENT=DSECTLEN,                                          +
               SAVE=SAVEAREA
         TITLE 'SET UP TSO CONTROL BLOCKS'
***********************************************************************
*                                                                     *
*              SET UP TSO CONTROL BLOCKS                              *
*                                                                     *
***********************************************************************
         SPACE 2
         #TSOBLKS DSECT,ECB=DVOLECB,                                   +
               CPPL=(R11,R1),                                          +
               IOPL=(R10,DVOLIOPL,DVOLPTPB),                           +
               PPL=(R9,DVOLPPL,DVOLPCL,DVOLANS)
         TITLE 'SET UP DATA AREAS'
***********************************************************************
*                                                                     *
*              SET UP DATA AREAS                                      *
*                                                                     *
***********************************************************************
         SPACE 2
         MVC   DVOLPTPB(MODLEN1),MODELS1 INITIALIZE WORK AREAS
         LA    R1,DSNAME                SET UP OBTAIN, SEARCH LIST
         LA    R2,DVOLSER
         LA    R3,VTOCFMT4
         STM   R1,R3,DVOLDCB4+4
         LA    R1,DVOLCCHH              SET UP OBTAIN, SEEK LIST
         LA    R2,DVOLSER
         LA    R3,VTOCFMT5
         STM   R1,R3,DVOLDCB5+4
         XC    TOTAL,TOTAL              ZERO SIZE OF 5 LARGEST EXTENTS
         XC    UCBADDR,UCBADDR          ZERO UCB ADDRESS
         LA    R1,SHORTSER              INITIALIZE SERIAL POINTER
         ST    R1,SERPTR
         TITLE 'PARSE COMMAND OPERANDS'
***********************************************************************
*                                                                     *
*              PARSE COMMAND OPERANDS                                 *
*                                                                     *
***********************************************************************
         SPACE 2
         LA    R1,PPL                   LOAD PPL ADDRESS
         LINK  EP=IKJPARS               PARSE THE OPERANDS
         LTR   R15,R15                  TEST PARSE RETURN CODE
         BNZ   ERRPARS
         L     R9,DVOLANS               LOAD ANSWER AREA ADDRESS
         USING DVOLPDL,R9               DEFINE BASE REGISTER
         SPACE 2
***********************************************************************
*                                                                     *
*              GET VOLUME SERIAL AND UCB ADDRESS                      *
*                                                                     *
***********************************************************************
         SPACE 2
         LA    R8,VOL                   SET POINTER TO FIRST VOLUME
         L     R15,16                   LOAD CVT ADDRESS
         L     R15,40(R15)              LOAD UCB TABLE ADDRESS
         ST    R15,UCBTABLE             SAVE UCB TABLE ADDRESS
         CLC   OUTFORM,=H'1'            TEST OUTPUT FORMAT
         BNE   NEXTVOL
         CLC   NOHEADER,=H'2'           SEE IF HEADER SUPPRESSED
         BE    NEXTVOL
         LA    R15,SHORT1               LOAD OUTPUT LINE ADDRESS
         BAL   R2,PUTL
         LA    R15,SHORT2               LOAD OUTPUT LINE ADDRESS
         BAL   R2,PUTL
NEXTVOL  MVC   LINE1(MODLEN2),MODELS2   COPY OUTPUT LINES
         MVC   FREEMSG1(MODLEN3),MODELS3
         MVC   BLDSCBS(MODLEN4),MODELS4
         MVC   SHORTLIN(SHORTLEN),SHORTMDL
         MVC   DVOLSER,=CL6' '          BLANK OUT VOL SERIAL
         L     R3,0(R8)                 LOAD VOLUME SERIAL ADDRESS
         CLI   0(R3),C'*'               TEST FOR * (ALL VOLUMES)
         BE    GETUCB
         LH    R1,4(R8)                 LOAD VOLUME SERIAL LENGTH
         BCTR  R1,0                     REDUCE LENGTH BY 1
         EX    R1,MOVESER               MOVE SERIAL
         MVC   SHORTSER,DVOLSER         COPY VOLUME SERIAL
         TITLE 'FIND UCB ENTRY FOR VOLUME'
***********************************************************************
*                                                                     *
*              FIND UCB ENTRY FOR VOLUME                              *
*                                                                     *
*        IF DEVICES HAVE MULTIPLE PATHS, THERE WILL BE MULTIPLE       *
*        ENTRIES IN THE UCB LOOKUP TABLE, BUT ONLY ONE UCB FOR        *
*        EACH DEVICE.  DVOL WILL SKIP ALTERNATE PATHS.  THE           *
*        LOGIC TO SKIP ALTERNATE PATHS DEPENDS ON THE RESTRICTIONS    *
*        THAT  1.  THE ALTERNATE ADDRESS MUST BE HIGHER THAN THE      *
*                  PRIMARY ADDRESS, AND                               *
*              2.  THAT UCB'S ARE LOCATED IN STORAGE IN ASCENDING     *
*                  ORDER BY PRIMARY UNIT ADDRESS                      *
*                                                                     *
***********************************************************************
*                                                                     *
*        R2  - VOLUME SERIAL ADDRESS                                  *
*        R3  - VOLUME SERIAL (FROM COMMAND LINE) ADDRESS              *
*        R4  - UCB TABLE ADDRESS                                      *
*        R5  - UCB ADDRESS                                            *
*                                                                     *
***********************************************************************
         SPACE 2
GETUCB   LA    R2,DVOLSER               LOAD SERIAL ADDRESS
         L     R4,UCBTABLE              LOAD ADDRESS TO START SEARCH
         SH    R4,=H'2'      ELIMINATE EXTRA BRANCH IN LOOP     GP09297
UCBHOLE  LA    R4,2(,R4)                INCREMENT TO NEXT UCB   GP09297
UCBCHECK LH    R5,0(,R4)                LOAD UCB ADDRESS
.USING   USING UCB,R5
         N     R5,=X'0000FFFF'          KILL SIGN EXTENSION     GP09297
         BZ    UCBHOLE
         C     R5,=X'0000FFFF'          TEST FOR END OF TABLE   GP09297
         BE    NOUCB
         CLI   UCBTBYT3,UCB3DACC        TEST FOR DIRECT ACCESS
         BNE   UCBHOLE
         CLI   UCBVOLI,X'40'            TEST FOR NO SERIAL      GP09297
         BNH   UCBHOLE                                          GP09297
         CLI   0(R2),C' '               TEST FOR BLANK SERIAL
         BE    UCBSKIP
         CLC   0(6,R2),UCBVOLI          COMPARE SERIALS
         BNE   UCBHOLE
         B     UCBCONT
         SPACE 1
UCBSKIP  C     R5,UCBADDR               CHECK AGAINST LAST UCB ADDRESS
         BNH   UCBHOLE
         MVC   0(6,R2),UCBVOLI          COPY VOLUME SERIAL
UCBCONT  LA    R4,2(,R4)                LOAD ADDR OF NEXT UCB ENTRY
         ST    R4,UCBNEXT               SAVE ADDRESS
         ST    R5,UCBADDR               STORE THIS UCB ADDRESS
*NEXT*   B     GOTUCB
GOTUCB   CLI   0(R3),C'*'               TEST FOR * (ALL VOLUMES)
         BNE   CONTINUE
         MVC   UCBTABLE,UCBNEXT         SET UCBTABLE ADDR TO NEXT UCB
         NC    SERONLY,SERONLY          TEST FOR SERONLY
         BNZ   COPYSER1
         MVC   SHORTSER,DVOLSER         COPY VOLUME SERIAL
         B     CONTINUE
COPYSER1 L     R2,SERPTR                LOAD SERIAL POINTER
         LA    R1,SHORTSER+71           TEST FOR FULL LINE
         CR    R2,R1
         BL    COPYSER2
         LA    R15,SHORTLIN             LOAD OUTPUT LINE ADDRESS
         BAL   R2,PUTL                  DISPLAY LINE
         MVC   SHORTLIN(SHORTLEN),SHORTMDL  CLEAR LINE
         LA    R2,SHORTSER              RESET POINTER
COPYSER2 MVC   0(6,R2),DVOLSER          COPY SERIAL INTO LINE
         LA    R2,10(R2)                INCREMENT POINTER
         ST    R2,SERPTR                SAVE POINTER
         MVC   DVOLSER,=CL6' '          BLANK OUT VOLUME SERIAL
         B     GETUCB
         DROP  R5
         TITLE 'PROCESS UCB INFORMATION'
***********************************************************************
*                                                                     *
*              PROCESS UCB INFORMATION                                *
*                                                                     *
***********************************************************************
*                                                                     *
*        R7  - UCB ADDRESS                                            *
*                                                                     *
***********************************************************************
         SPACE 2
CONTINUE L     R7,UCBADDR               LOAD UCB ADDRESS
         USING UCB,R7                   DEFINE BASE REGISTER
         MVC   UNITADDR,UCBNAME         COPY UNIT ADDRESS
         MVC   SHORTUNT,UCBNAME
*--------MOUNT STATUS
         MVC   SHORTMNT(6),=CL6'REMOV'                          GP09297
         TM    UCBSTAT,UCBRESV          TEST FOR RESERVED
         BZ    TRYRES
         MVC   MNTSTAT,=CL9'RESERVED'   SET MOUNT STATUS
         MVC   SHORTMNT(6),=C'RESRVD'                           GP09297
         B     USESTAT
TRYRES   TM    UCBSTAT,UCBPRES          TEST FOR RESIDENT
         BZ    USESTAT
         MVC   MNTSTAT,=CL9'RESIDENT'   SET MOUNT STATUS
         MVC   SHORTMNT(6),=C'RESDNT'   COPY MOUNT STATUS       GP09297
*--------USE STATUS
USESTAT  TM    UCBSTAB,UCBBPRV          TEST FOR PRIVATE
         BZ    TRYPUB
         MVC   USESTATS,=CL7'PRIVATE'   SET USE STATUS
         B     TESTALOC
TRYPUB   TM    UCBSTAB,UCBBPUB          TEST FOR PUBLIC
         BZ    TRYSTOR
         MVC   USESTATS,=CL7'PUBLIC'    SET USE STATUS
         B     TESTALOC
TRYSTOR  TM    UCBSTAB,UCBBSTR          TEST FOR STORAGE
         BZ    TESTALOC
         MVC   USESTATS,=CL7'STORAGE'   SET USE STATUS
TESTALOC MVC   SHORTUSE(4),USESTATS     COPY USE STATUS         GP09297
*--------ALLOCATED/UNALLOCATED
         TM    UCBSTAT,UCBALOC          TEST FOR ALLOCATED
         BZ    TESTOFF
         MVC   ALLOCATD,=CL11'ALLOCATED  '
*--------ONLINE/OFFLINE
TESTOFF  TM    UCBSTAT,UCBONLI          TEST FOR ONLINE
         BZ    TESTUNLD
         MVC   ONOFF,=CL17'ONLINE'
         TM    UCBSTAT,UCBCHGS          TEST FOR OFFLINE PENDING
         BZ    TESTUNLD
         MVC   ONOFF,=CL17'OFFLINE PENDING'
         MVC   SHORTMNT(17),ONOFF
*--------PENDING MOUNT/UNLOAD
TESTUNLD TM    UCBSTAT,UCBUNLD          TEST FOR UNLOAD PENDING
         BZ    GETFMT4
         MVC   UNLOAD,=CL17'UNLOAD PENDING'
         MVC   SHORTMNT(17),UNLOAD
         TM    DVOLFLGB,UCBTICBT        TEST FOR MOUNT PENDING
         BZ    GETFMT4
         MVC   UNLOAD,=CL17'MOUNT PENDING'
         MVC   SHORTMNT(17),UNLOAD
         TITLE 'OBTAIN AND PROCESS FORMAT 4 DSCB (VTOC DSCB)'
***********************************************************************
*                                                                     *
*              OBTAIN FORMAT 4 DSCB (VTOC DSCB)                       *
*                                                                     *
***********************************************************************
         SPACE 2
GETFMT4  CLC   OUTFORM,=H'2'            TEST OUTPUT FORMAT
         BNE   GETFMT4A
         LA    R15,LINE1                DISPLAY VOLUME SERIAL AND UNIT
         BAL   R2,PUTL
GETFMT4A XC    TOTAL,TOTAL              ZERO 5 LARGEST EXTENT TOTAL
         MVI   DS4IDFMT,X'00'           ZERO OUT FORMAT 4 ID LOCATION
         MVI   VTOCINDS,X'00'           ZERO OUT VTOC INDICATORS
         TM    DVOLFLGA,DVOLNRDY        TEST FOR NOT READY
         BO    NOTREADY
         DROP  R7
         OBTAIN DVOLDCB4                OBTAIN FORMAT 4 DSCB
         CLI   DS4IDFMT,X'F4'           TEST FIELD IDENTIFIER
         BNE   ERRFMT4
         MVC   DVOLCCHH,DS4VTOCE+2      COPY CCHH OF VTOC
         MVI   DVOLCCHH+4,X'02'         SET RECORD TO 2 FOR FIRST FMT 5
         MVC   TRKPRCYL,DS4DEVSZ+2      COPY TRACKS PER CYLINDER
         MVC   MTDSCBS,DS4DSREC         COPY TRACKS PER CYLINDER
         MVC   VTOCINDS,DS4VTOCI        SAVE VTOC INDICATORS
         TM    VTOCINDS,X'80'           TEST FOR NO FORMAT 5 DSCB'S
         BNZ   NOSPACE
         LH    R1,MTDSCBS
         N     R1,=X'0000FFFF'                                  GP09297
         CVD   R1,DVOLDEC
         MVC   EDITFLD,=X'4020202020202120'
         ED    EDITFLD,DVOLDEC+4
         MVC   DSCBS,EDITFLD+3                                  GP09297
         SPACE 2
         CLC   OUTFORM,=H'1'            TEST OUTPUT FORMAT
         BNE   GETVSAM
         MVC   SHORTDCB,DSCBS           COPY NUMBER OF DSCB'S
         EJECT
***********************************************************************
*                                                                     *
*              PROCESS VSAM INFORMATION                               *
*                                                                     *
***********************************************************************
         SPACE 2
GETVSAM  TM    DS4VSIND,X'80'           TEST OWNERSHIP BIT
         BZ    TIMESTMP
         MVC   OWNERSHP,=C'ON '         INDICATE VOLUME IS OWNED
TIMESTMP UNPK  WORKAREA(9),DS4AMTIM(5)  UNPACK TIMESTAMP 1
         UNPK  WORKAREA+8(9),DS4AMTIM+4(5)
         MVZ   WORKAREA,=XL16'00'       MOVE ZEROS TO ZONES
         TR    WORKAREA,=C'0123456789ABCDEF'  CONVERT TO EBCDIC
         MVC   TIMEST1,WORKAREA         COPY TIMESTAMP 1 TO MESSAGE
         UNPK  WORKAREA(9),DS4R2TIM(5)  UNPACK TIMESTAMP 2
         UNPK  WORKAREA+8(9),DS4R2TIM+4(5)
         MVZ   WORKAREA,=XL16'00'       MOVE ZEROS TO ZONES
         TR    WORKAREA,=C'0123456789ABCDEF'  CONVERT TO EBCDIC
         MVC   TIMEST2,WORKAREA         COPY TIMESTAMP2 TO MESSAGE
         SPACE 2
         CLC   OUTFORM,=H'2'            TEST OUTPUT FORMAT
         BE    ZERO5
         MVC   SHORTVSM,OWNERSHP        COPY OWNERSHIP INFORMATION
         TITLE 'OBTAIN FORMAT 5 DSCB (FREE AREA DSCB)'
***********************************************************************
*                                                                     *
*              OBTAIN FORMAT 5 DSCB (FREE AREA DSCB)                  *
*                                                                     *
***********************************************************************
*                                                                     *
*        R0  - TRACKS IN THIS EXTENT                                  *
*        R2  - EXTENTS LEFT TO PROCESS IN THIS DSCB                   *
*        R3  - POINTER TO LOAD ADDRESS TABLE ENTRY                    *
*        R4  - CUMULATIVE CYLINDER COUNT                              *
*        R5  - CUMULATIVE EXCESS TRACK COUNT                          *
*        R6  - ADDRESS OF EXTENT DESCRIPTION (LOADED BY EXECUTE)      *
*        R7  - CUMULATIVE EXTENT COUNT                                *
*                                                                     *
***********************************************************************
         SPACE 2
ZERO5    XC    LARGE5(30),LARGE5        ZERO LARGEST 5 EXTENTS TABLE
         SR    R7,R7                    ZERO EXTENTS
         SR    R4,R4                    ZERO CYLINDERS
         SR    R5,R5                    ZERO TRACKS
OBTFMT5  L     R15,UCBADDR              LOAD UCB ADDRESS
         USING UCB,R15                  ADDRESSABILITY
         TM    DVOLFLGA,DVOLNRDY        TEST NOT READY
         BO    NOTREADY
         DROP  R15
         OBTAIN DVOLDCB5                READ FORMAT 5 DSCB
         LTR   R15,R15                  TEST RETURN CODE
         BNZ   ERRFMT5
         CLI   DS5FMTID,X'F5'           TEST FORMAT 5 ID
         BNZ   ERRFMT5
         LA    R2,26                    LOAD EXTENTS PER DSCB
         SR    R3,R3                    ZERO POINTER
DVOLLOAD EX    R0,FMT5ADDR(R3)          LOAD ADDRESS OF EXTENT DESCR
         NC    0(5,R6),0(R6)            TEST FOR LAST EXTENT
         BE    ENDFMT5
         SR    R0,R0                    CLEAR REGISTER
         MVC   HALF,2(R6)               COPY NUMBER OF CYLINDERS
         LH    R1,HALF                  LOAD NUMBER OF CYLINDERS
         IC    R0,4(,R6)                LOAD NUMBER OF TRACKS
         AR    R4,R1                    ACCUMULATE CYLINDERS
         AR    R5,R0                    ACCUMULATE TRACKS
         LA    R7,1(R7)                 INCREMENT EXTENT COUNT
         MH    R1,TRKPRCYL              MULT CYLS BY TRKS PER CYL
         AR    R0,R1                    TOTAL TRACKS THIS EXTENT
         TITLE 'SEE IF EXTENT IS ONE OF 5 LARGEST'
***********************************************************************
*                                                                     *
*              SEE IF CURRENT EXTENT IS ONE OF 5 LARGEST              *
*                                                                     *
***********************************************************************
*                                                                     *
*        R0  - TRACKS IN THIS EXTENT                                  *
*        R1  - EXTENT TABLE COUNTER                                   *
*        R6  - ADDRESS OF EXTENT DESCRIPTION                          *
*        R14 - CHARACTERS TO BE MOVED, ADDR OF EXTENT ENTRY           *
*              R2, R3, R4, R5, AND R7 MUST BE PRESERVED               *
*                                                                     *
***********************************************************************
         SPACE 2
         SR    R14,R14                  ZERO CHARACTERS
         LA    R1,5                     LOAD COUNT
COMPARE  CH    R0,LARGE5(R14)           COMPARE TOTAL TRACKS
         BNH   NOTLARGE
         LA    R14,6(R14)               INCREMENT COUNT BY 6
         BCT   R1,COMPARE
NOTLARGE SH    R14,=H'6'
         LTR   R14,R14                  TEST COUNT
         BM    NEXTFREE
         BZ    ADDEXTNT
         BCTR  R14,0                    REDUCE COUNT BY 1
         EX    R14,MOVEXTNT             COPY EXTENT DESCRIPTIONS
         LA    R14,1(R14)               ADD 1 TO COUNT
ADDEXTNT LA    R14,LARGE5(R14)          LOAD ADDRESS FOR THIS EXTENT
         STH   R0,0(R14)                STORE NUMBER OF TRACKS
         MVC   2(3,R14),2(R6)           COPY CYLINDERS AND TRACKS
NEXTFREE LA    R3,4(R3)                 INCREMENT POINTER
         BCT   R2,DVOLLOAD              DECREMENT EXTENT COUNTER
         SPACE 2
***********************************************************************
*                                                                     *
*              FINISHED WITH FORMAT 5 DSCB                            *
*                                                                     *
***********************************************************************
         SPACE 2
         NC    DS5PTRDS,DS5PTRDS        TEST FOR ANOTHER FMT5
         BE    ENDFMT5
         MVC   DVOLCCHH,DS5PTRDS        COPY ADDRESS OF NEXT FMT5 DSCB
         B     OBTFMT5
         TITLE 'DEVICE NOT READY'
***********************************************************************
*                                                                     *
*              DEVICE NOT READY                                       *
*                                                                     *
***********************************************************************
NOTREADY MVC   UNLOAD,=CL17'NOT READY'  INDICATE NOT READY
         MVC   SHORTMNT(17),UNLOAD
         CLC   OUTFORM,=H'1'            TEST OUTPUT FORMAT
         BE    INCRVOL
         LA    R15,LINE2                DISPLAY HELD LINE
         BAL   R2,PUTL
         B     INCRVOL
         TITLE 'FORMAT AND DISPLAY LINE'
***********************************************************************
*                                                                     *
*              FORMAT AND DISPLAY LINE                                *
*                                                                     *
***********************************************************************
*                                                                     *
*        R4  - CYLINDER COUNT                                         *
*        R5  - EXCESS TRACK COUNT                                     *
*        R7  - EXTENT COUNT                                           *
*                                                                     *
***********************************************************************
         SPACE 2
ENDFMT5  CLC   OUTFORM,=H'2'            TEST OUTPUT FORMAT
         BNE   ENDFMT5B
         LA    R15,LINE2                DISPLAY HELD LINES
         BAL   R2,PUTL
         LA    R15,LINE3
         BAL   R2,PUTL
*--------PROCESS VTOC INDICATORS
         TM    VTOCINDS,X'08'
         BZ    TESTDIRF
         SPACE 2
         LA    R15,DOS                  LOAD OUTPUT LINE ADDRESS
         BAL   R2,PUTL
TESTDIRF TM    VTOCINDS,X'04'
         BZ    BLKDSCBS
         LA    R15,DAMAGED              LOAD OUTPUT LINE ADDRESS
         BAL   R2,PUTL
BLKDSCBS LA    R15,BLDSCBS              DISPLAY HELD LINE
         BAL   R2,PUTL
ENDFMT5B CLC   LARGE5+24,=H'0'          TEST FOR NO SPACE ON PACK
         BE    NOSPACE
         CVD   R7,DVOLDEC
         MVC   EDITFLD,=X'4020202020202120'
         ED    EDITFLD,DVOLDEC+4
         MVC   EXTENTS,EDITFLD+4
         CVD   R4,DVOLDEC
         MVC   EDITFLD,=X'4020202020202120'
         ED    EDITFLD,DVOLDEC+4
         MVC   CYLS,EDITFLD+3                                   GP09297
         MH    R4,TRKPRCYL              CONVERT TOTAL TRACKS
         AR    R4,R5
         CVD   R4,DVOLDEC
         MVC   EDITFLD,=X'4020202020202120'
         ED    EDITFLD,DVOLDEC+4
         MVC   TRACKS,EDITFLD+3
         SPACE 2
         CLC   OUTFORM,=H'1'
         BNE   DISPLAY4
         MVC   SHORTTOT,TRACKS
         MVC   SHORTEXT,EXTENTS
         MVC   SHORTCYL,CYLS
         B     GETCYLTR
         SPACE 2
DISPLAY4 LA    R15,FREEMSG1             LOAD OUTPUT LINE ADDRESS
         BAL   R2,PUTL
         TITLE 'DISPLAY LARGEST EXTENTS AS CYL+TR'
***********************************************************************
*                                                                     *
*              DISPLAY LARGEST EXTENTS AS CYL+TR                      *
*                                                                     *
***********************************************************************
*                                                                     *
*        R4  - DESCRIPTION COUNTER                                    *
*        R5  - TABLE ENTRY POINTER                                    *
*        R6  - OUTPUT LINE POINTER                                    *
*                                                                     *
***********************************************************************
         SPACE 2
GETCYLTR LA    R4,5                     SET COUNTER
         LA    R5,LARGE5+24             SET POINTER
         LA    R6,LRGXTNT1              SET POINTER
LOOP1    CLC   0(2,R5),=H'0'            TEST FOR END OF LIST
         BE    ENDLOOP1
         LH    R3,2(,R5)                LOAD CYLINDERS
         N     R3,=X'0000FFFF'                                  GP09297
         CVD   R3,DVOLDEC               CONVERT TO DECIMAL
         MVC   EDITFLD,=X'4020202020202120'
         ED    EDITFLD,DVOLDEC+4
         MVC   0(5,R6),EDITFLD+3        MOVE CYLINDERS TO MSG   GP09297
         MVI   5(R6),C'+'               MOVE + SIGN             GP09297
         SR    R3,R3                    CLEAR REGISTER
         IC    R3,4(,R5)                LOAD TRACKS
         CVD   R3,DVOLDEC               CONVERT TO DECIMAL
         MVC   EDITFLD,=X'4020202020202120'  SET UP EDIT MASK
         ED    EDITFLD,DVOLDEC+4        EDIT TRACKS
         MVC   6(2,R6),EDITFLD+6        MOVE TRACKS INTO MSG    GP09297
         MVI   8(R6),C'/'               MOVE /                  GP09297
         SH    R5,=H'6'                 POINT TO NEXT DESCRIPTION
         LA    R6,9(,R6)                POINT TO NEXT OP DESCR  GP09297
         BCT   R4,LOOP1                 DECREMENT COUNT AND LOOP
         SPACE 2
ENDLOOP1 CLC   OUTFORM,=H'1'            TEST OUTPUT FORMAT
         BNE   DISPLAY5
         MVC   SHORT1CL,LRGXTNT1        COPY CYLINDERS IN LARGEST EXTNT
         MVC   SHORT1TR,LRGXTNT1+6      COPY TRACKS IN LARGETS EXTENT
         MVI   SHORT1CL+5,C'+'          ADD A +                 GP09297
         B     GETTRKS
         SPACE 2
DISPLAY5 LA    R15,FREEMSG2             LOAD OUTPUT LINE ADDRESS
         BAL   R2,PUTL
         TITLE 'DISPLAY LARGEST EXTENTS AS TRACKS'
***********************************************************************
*                                                                     *
*              DISPLAY LARGEST EXTENTS AS TRACKS                      *
*                                                                     *
***********************************************************************
*                                                                     *
*        R4  - DESCRIPTION COUNTER                                    *
*        R5  - TABLE ENTRY POINTER                                    *
*        R6  - OUTPUT LINE POINTER                                    *
*        R7  - CUMULATIVE TRACK COUNTER                               *
*                                                                     *
***********************************************************************
         SPACE 2
GETTRKS  LA    R4,5                     SET COUNT
         LA    R5,LARGE5+24             SET POINTER
         LA    R6,LRGXTNT2              SET POINTER
         SR    R7,R7                    ZERO 5 LARGEST EXTENTS TOTAL
LOOP2    CLC   0(2,R5),=H'0'            TEST FOR END OF TABLE
         BE    ENDLOOP2
         LH    R3,0(R5)                 LOAD TOTAL TRACKS IN EXTENT
         N     R3,=X'0000FFFF'          JUST IN CASE            GP09297
         AR    R7,R3                    ACCUMULATE TOTAL
         CVD   R3,DVOLDEC               CONVERT TO DECIMAL
         MVC   EDITFLD,=X'4020202020202120'  SET EDIT MASK
         ED    EDITFLD,DVOLDEC+4        EDIT TRACKS
         MVC   0(8,R6),EDITFLD+0        MOVE INTO MESSAGE       GP09297
         MVI   8(R6),C'/'               MOVE / INTO MESSAGE     GP09297
         SH    R5,=H'6'                 POINT TO NEXT EXTENT ENTRY
         LA    R6,9(,R6)                POINT TO NEXT OP        GP09297
         BCT   R4,LOOP2                 DECREMENT COUNT AND LOOP
         SPACE 2
ENDLOOP2 CLC   OUTFORM,=H'1'            TEST OUTPUT FORMAT
         BNE   DISPLAY6
         MVC   SHORTLRG,LRGXTNT2+3      COPY TRACKS IN LGST EXT GP09297
         CVD   R7,DVOLDEC               CONVERT TOTAL OF 5 LARGEST
         MVC   EDITFLD,=X'4020202020202120'  MOVE IN EDIT MASK
         ED    EDITFLD,DVOLDEC+4        EDIT VALUE
         MVC   SHORT5XT,EDITFLD+3       COPY TO OUTPUT LINE
         B     CALCRC
         SPACE 2
DISPLAY6 LA    R15,FREEMSG3             LOAD OUTPUT LINE ADDRESS
         BAL   R2,PUTL
         SPACE 2
CALCRC   MVC   TOTAL,=H'4095'           SET TOTAL = 4095 (LARGEST)
         C     R7,=F'4095'              COMPARE TOTAL TO 4095
         BH    INCRVOL
         STH   R7,TOTAL                 STORE TOTAL
         B     INCRVOL
         TITLE 'NO SPACE ON VOLUME'
***********************************************************************
*                                                                     *
*              NO SPACE ON VOLUME                                     *
*                                                                     *
***********************************************************************
         SPACE 2
NOSPACE  CLC   OUTFORM,=H'2'            TEST OUTPUT FORMAT
         BE    NOSPACE2
         MVC   SHORTTOT(29),VOLFULL+4   MOVE NO SPACE MESSAGE
         B     INCRVOL
NOSPACE2 LA    R15,VOLFULL              LOAD OUTPUT LINE ADDRESS
         BAL   R2,PUTL
         B     INCRVOL
         TITLE 'END OF UCB TABLE REACHED'
***********************************************************************
*                                                                     *
*              END OF UCB TABLE REACHED                               *
*                                                                     *
***********************************************************************
*                                                                     *
*        R8  - PDE POINTER                                            *
*                                                                     *
***********************************************************************
         SPACE 2
NOUCB    L     R3,0(R8)
         CLI   0(R3),C'*'
         BE    TSERONLY
         MVC   VOLSER,DVOLSER           SERIAL NOT FOUND - SET UP MSG
         SPACE 2
         CLC   OUTFORM,=H'2'            TEST OUTPUT FORMAT
         BE    PRTNOSER
         MVC   SHORTDCB(16),NOSER+13
         B     INCRVOL
PRTNOSER LA    R15,NOSER                LOAD OUTPUT LINE ADDRESS
         BAL   R2,PUTL
         B     INCRVOL
TSERONLY NC    SERONLY,SERONLY          TEST FOR SERONLY
         BZ    RETURN
         LA    R15,SHORTLIN             LOAD LINE ADDRESS
         BAL   R2,PUTL                  DISPLAY LINE
         B     RETURN
         TITLE 'ERRORS'
***********************************************************************
*                                                                     *
*              ERROR READING FORMAT 4 DSCB                            *
*                                                                     *
***********************************************************************
         SPACE 2
ERRFMT4  CLC   OUTFORM,=H'2'            TEST OUTPUT FORMAT
         BE    PRNTFMT4
         MVC   SHORTDCB(27),FMT4BAD+4
         B     INCRVOL
PRNTFMT4 LA    R15,FMT4BAD              LOAD OUTPUT LINE ADDRESS
         BAL   R2,PUTL
         B     INCRVOL
         SPACE 2
***********************************************************************
*                                                                     *
*              ERROR READING FORMAT 5 DSCB                            *
*                                                                     *
***********************************************************************
         SPACE 2
ERRFMT5  CLC   OUTFORM,=H'2'            TEST OUTPUT FORMAT
         BE    PRNTFMT5
         MVC   SHORTDCB(27),FMT5BAD+4
         B     INCRVOL
PRNTFMT5 LA    R15,FMT5BAD              LOAD OUTPUT LINE ADDRESS
         BAL   R2,PUTL
         B     INCRVOL
         EJECT
***********************************************************************
*                                                                     *
*              UNABLE TO PARSE COMMAND OPERANDS                       *
*                                                                     *
***********************************************************************
         SPACE 2
ERRPARS  LA    R15,PARSERR              LOAD OUTPUT LINE ADDRESS
         BAL   R2,PUTL
         LA    R15,0                    SET RETURN CODE
         B     RETURN2
         TITLE 'FINISHED PROCESSING VOLUME'
***********************************************************************
*                                                                     *
*              FINISHED THIS VOLUME - GO TO NEXT VOLUME               *
*                                                                     *
***********************************************************************
*                                                                     *
*        R8  - PDE POINTER                                            *
*                                                                     *
***********************************************************************
         SPACE 2
INCRVOL  CLC   OUTFORM,=H'1'            TEST OUTPUT FORMAT
         BNE   DISPLAY9
         LA    R15,SHORTLIN             LOAD OUTPUT LINE ADDRESS
         BAL   R2,PUTL
         B     NEXTONE
         SPACE 2
DISPLAY9 LA    R15,BLANKLIN             LOAD OUTPUT LINE ADDRESS
         BAL   R2,PUTL
         SPACE 2
NEXTONE  L     R3,0(R8)                 LOAD OPERAND ADDRESS
         CLI   0(R3),C'*'               TEST FOR * (ALL VOLUMES)
         BE    NEXTVOL
         L     R8,8(R8)                 LOAD NEXT PDE ADDRESS
         C     R8,=X'FF000000'          TEST FOR END OF LIST
         BNE   NEXTVOL
         TITLE 'EXIT CODING'
***********************************************************************
*                                                                     *
*              FREE STORAGE, SET RETURN CODE, AND RETURN              *
*                                                                     *
***********************************************************************
         SPACE 2
RETURN   IKJRLSA DVOLANS                RELEASE PARSE STORAGE
         LH    R15,TOTAL                SAVE 5 LARGEST EXTENTS TOTAL
         N     R15,=X'0000FFFF'                                 GP09297
RETURN2  $RTRN RC=(R15),RENT=DSECTLEN   RETURN
         TITLE 'DISPLAY OUTPUT SUBROUTINE'
***********************************************************************
*                                                                     *
*              DISPLAY OUTPUT                                         *
*                                                                     *
***********************************************************************
         SPACE 2
PUTL     CLC   NOPRINT,=H'2'            SEE IF OUTPUT SUPPRESSED
         BER   R2
         PUTLINE OUTPUT=((R15),,,DATA),MF=(E,IOPL)
         BR    R2
         TITLE 'CONSTANT DATA AREAS'
***********************************************************************
*                                                                     *
*              LITERAL POOL                                           *
*                                                                     *
***********************************************************************
         SPACE
         LTORG
         EJECT
***********************************************************************
*                                                                     *
*              CONSTANT STORAGE                                       *
*                                                                     *
***********************************************************************
         SPACE 2
FMT4BAD  #TSOMSG 'ERROR READING FORMAT 4 DSCB'
         SPACE
FMT5BAD  #TSOMSG 'ERROR READING FORMAT 5 DSCB'
         SPACE
VOLFULL  #TSOMSG 'VOLUME CONTAINS NO FREE SPACE'
         SPACE
PARSERR  #TSOMSG 'UNABLE TO INTERPRET COMMAND'
         SPACE
DOS      #TSOMSG '*** WARNING - VOLUME MAY CONTAIN DOS DATA SETS'
         SPACE
DAMAGED  #TSOMSG '*** WARNING - VTOC DAMAGE BIT IS SET.  PLEASE NOTIFY +
               YOUR SYSTEMS PROGRAMMER'
         SPACE
SHORT1   #TSOMSG 'SERIAL UNIT -ATTRIBUTES-- VSAM AVAIL -----TOTALS-----+
               - -LARGEST-EXTENT- 5 EXTS'                       GP09297
SHORT2   #TSOMSG '            MOUNT   USE        DSCBS TRACKS  EXT   CY+
               L   CYL+TR  TRACKS TRACKS'                       GP09297
         SPACE
BLANKLIN #TSOMSG ' '
         SPACE
DSNAME   DC    44X'04'
         EJECT
***********************************************************************
*                                                                     *
*              PUTLINE, CAMLST, AND MESSAGE MODELS                    *
*                                                                     *
***********************************************************************
         SPACE 2
SHORTMDL #TSOMSG ' ',LENGTH=80
SHORTLEN EQU   *-SHORTMDL
         SPACE 2
MODELS1  PUTLINE MF=L
         CAMLST SEARCH,*-*,*-*,*-*
         CAMLST SEEK,*-*,*-*,*-*
MODLEN1  EQU   *-MODELS1
         SPACE 2
MODELS2  #TSOMSG 'VOLUME SERIAL = XXXXXX     UNIT = XXX',LENGTH=80
         SPACE
         #TSOMSG 'STATUS: REMOVABLE           UNALLOCATED  OFFLINE',   +
               LENGTH=80
         SPACE
         #TSOMSG 'VSAM: OWNERSHIP - OFF   TIMESTAMPS - ................+
               /................',LENGTH=80
         SPACE
MODLEN2  EQU   *-MODELS2
         SPACE 2
MODELS3  #TSOMSG 'FREE SPACE IS XXXXX TRACKS IN XXXX EXTENTS INCLUDING +
               XXXXX FULL CYLINDERS',LENGTH=80                  GP09297
         SPACE
         #TSOMSG 'LARGEST EXTENTS ARE: /CYL+TR/',LENGTH=80
         SPACE
         #TSOMSG '                     /TRACKS/',LENGTH=80
         SPACE
MODLEN3  EQU   *-MODELS3
         SPACE 2
MODELS4  #TSOMSG 'VTOC CONTAINS XXXXX BLANK DSCBS'              GP09297
         SPACE
         #TSOMSG 'XXXXXX - VOLUME NOT FOUND'
         SPACE
MODLEN4  EQU   *-MODELS4
         EJECT
***********************************************************************
*                                                                     *
*              INSTRUCTIONS TO BE EXECUTED BY EXECUTE INSTRUCTIONS    *
*                                                                     *
***********************************************************************
         SPACE 2
MOVESER  MVC   DVOLSER(0),0(R3)         COPY VOLUME SERIAL
MOVEXTNT MVC   LARGE5(0),LARGE5+6       COPY EXTENT ENTRIES
FMT5ADDR LA    R6,DS5EXT01              LOAD EXTENT DESCRIPTION ADDRESS
         LA    R6,DS5EXT02
         LA    R6,DS5EXT03
         LA    R6,DS5EXT04
         LA    R6,DS5EXT05
         LA    R6,DS5EXT06
         LA    R6,DS5EXT07
         LA    R6,DS5EXT08
         LA    R6,DS5EXT09
         LA    R6,DS5EXT10
         LA    R6,DS5EXT11
         LA    R6,DS5EXT12
         LA    R6,DS5EXT13
         LA    R6,DS5EXT14
         LA    R6,DS5EXT15
         LA    R6,DS5EXT16
         LA    R6,DS5EXT17
         LA    R6,DS5EXT18
         LA    R6,DS5EXT19
         LA    R6,DS5EXT20
         LA    R6,DS5EXT21
         LA    R6,DS5EXT22
         LA    R6,DS5EXT23
         LA    R6,DS5EXT24
         LA    R6,DS5EXT25
         LA    R6,DS5EXT26
         TITLE 'PARSE CONTROL BLOCKS'
***********************************************************************
*                                                                     *
*              PARSE CONTROL BLOCKS                                   *
*                                                                     *
***********************************************************************
         SPACE 2
DVOLPCL  IKJPARM DSECT=DVOLPDL
VOL      IKJIDENT 'VOLUME SERIAL',                                     +
               ASTERISK,                                               +
               LIST,                                                   +
               MAXLNTH=6,                                              +
               FIRST=ALPHANUM,                                         +
               OTHER=ALPHANUM,                                         +
               DEFAULT='*'
OUTFORM  IKJKEYWD DEFAULT='SHORT'
         IKJNAME  'SHORT'
         IKJNAME  'LONG'
NOHEADER IKJKEYWD DEFAULT='HEADER'
         IKJNAME  'HEADER'
         IKJNAME  'NOHEADER'
NOPRINT  IKJKEYWD DEFAULT='PRINT'
         IKJNAME  'PRINT'
         IKJNAME  'NOPRINT'
SERONLY  IKJKEYWD
         IKJNAME  'SERONLY',INSERT='NOHEADER'
         IKJENDP
         TITLE 'UCB MAP'
***********************************************************************
*                                                                     *
*              UCB MAP                                                *
*                                                                     *
***********************************************************************
         SPACE 2
         PRINT NOGEN
UCB      DSECT
         IEFUCBOB
         PRINT GEN
         SPACE 3
         AIF  ('&OPSYSTM' EQ 'MVS').MVSEQU
DVOLFLGA EQU  UCBFL1
DVOLFLGB EQU  UCBFL5
DVOLNRDY EQU  UCBNOTRD
         AGO  .WORK
.MVSEQU  ANOP
DVOLFLGA EQU  UCBFLA
DVOLFLGB EQU  UCBFLC
DVOLNRDY EQU  UCBNRY
.WORK    ANOP
         TITLE 'WORK AREA'
***********************************************************************
*                                                                     *
*                       WORK AREA DSECT                               *
*                                                                     *
***********************************************************************
DVOLDS   DSECT
SAVEAREA DC    18F'0'
WORKAREA DS    XL16
DVOLDEC  DS    D
EDITFLD  DC    X'4020202020202120'
SERPTR   DS    A                        ADDRESS OF SERIAL FIELD
VTOCFMT4 DS    0D,148C
VTOCFMT5 EQU   VTOCFMT4
DVOLCCHH DS    XL5
VTOCINDS DS    X
MTDSCBS  DS    H
LARGE5   DS    15H
HALF     DS    H
TRKPRCYL DS    H
TOTAL    DS    H
UCBADDR  DS    A
UCBTABLE DS    A
UCBNEXT  DS    A
DVOLECB  DC    F'0'
         SPACE
DVOLPPL  DS    0F
         ORG   *+PPLLEN
         SPACE
DVOLIOPL DS    0F
         ORG   *+IOPLLEN
         SPACE
DVOLANS  DS    A
         SPACE
DVOLPTPB PUTLINE MF=L
         SPACE
DVOLDCB4 CAMLST SEARCH,0,0,0
         SPACE
DVOLDCB5 CAMLST SEEK,0,0,0
         SPACE
LINE1    #TSOMSG ' ',LENGTH=80
         SPACE
LINE2    #TSOMSG ' ',LENGTH=80
         SPACE
LINE3    #TSOMSG ' ',LENGTH=80
         SPACE
FREEMSG1 #TSOMSG ' ',LENGTH=80
         SPACE
FREEMSG2 #TSOMSG ' ',LENGTH=80
         SPACE
FREEMSG3 #TSOMSG ' ',LENGTH=80
         SPACE
BLDSCBS  #TSOMSG ' ',LENGTH=32
         SPACE
NOSER    #TSOMSG ' ',LENGTH=28
         SPACE 2
SHORTLIN #TSOMSG ' ',LENGTH=80
         SPACE
DSECTLEN EQU   *-DVOLDS
         SPACE 2
***********************************************************************
*                                                                     *
*              OUTPUT LINE EQUIVALENCES                               *
*                                                                     *
***********************************************************************
         SPACE 2
         ORG    LINE1
         DC    H'84,0',C'VOLUME SERIAL = '
DVOLSER  DC    CL6'XXXXXX'
         DC    C'     UNIT = '
UNITADDR DC    CL3'XXX'
         SPACE
         ORG   LINE2
         DC    H'84,0',C'STATUS: '
MNTSTAT  DC    CL9'REMOVABLE'
         DC    CL2' '
USESTATS DC    CL7'XXXXXXX'
         DC    CL2' '
ALLOCATD DC    CL11'XXXXXXXXXXX'
         DC    CL2' '
ONOFF    DC    CL17'OFFLINE'
         DC    CL2' '
UNLOAD   DC    CL17'XXXXXXXXXXXXXX'
         SPACE
         ORG   LINE3
         DC    H'84,0',C'VSAM: OWNERSHIP - '
OWNERSHP DC    CL3'OFF'
         DC    C'   TIMESTAMPS - '
TIMEST1  DC    CL16' '
         DC    C'/'
TIMEST2  DC    CL16' '
         ORG   FREEMSG1
         DC    H'84,0',C'FREE SPACE IS '
TRACKS   DC    CL5'XXXXX'
         DC    C' TRACKS IN '
EXTENTS  DC    CL4'XXXX'
         DC    C' EXTENTS INCLUDING '
CYLS     DC    CL5'XXXXX'                                       GP09297
         DC    C' FULL CYLINDERS'
         SPACE
         ORG   FREEMSG2
         DC    H'84,0',C'LARGEST EXTENTS ARE: /CYL+TR/'
LRGXTNT1 EQU   *
         SPACE
         ORG   FREEMSG3
         DC    H'84,0',C'                     /TRACKS/'
LRGXTNT2 EQU   *
         SPACE
         ORG   BLDSCBS
         DC    H'36,0',C'VTOC CONTAINS '
DSCBS    DC    CL5'XXXX'                                        GP09297
         DC    C'BLANK DSCBS'                                   GP09297
         SPACE
         ORG   NOSER
         DC    H'32,0'
VOLSER   DC    CL6'XXXXXX'
         DC    C' - VOLUME NOT FOUND'
         SPACE 2
         ORG   SHORTLIN
         DS    H,H
SHORTSER DS    CL6
         DS    CL2
SHORTUNT DS    CL3
         DS    CL1
SHORTMNT DS    CL7                                              GP09297
         DS    CL1
SHORTUSE DS    CL5                                              GP09297
         DS    CL2
SHORTVSM DS    CL3
         DS    CL1                                              GP09297
SHORTDCB DS    CL5                                              GP09297
         DS    CL1
SHORTTOT DS    CL6                                              GP09297
         DS    CL1
SHORTEXT DS    CL4
         DS    CL1
SHORTCYL DS    CL5                                              GP09297
         DS    CL1
SHORT1CL DS    CL5                                              GP09297
         DC    C'+'
SHORT1TR DS    CL2
         DS    CL3
SHORTLRG DS    CL5
         DS    CL2
SHORT5XT DS    CL5
         TITLE 'FORMAT 4 DSCB MAP'
***********************************************************************
*                                                                     *
*              DESCRIPTION OF FORMAT 4 DSCB                           *
*                                                                     *
***********************************************************************
         SPACE 2
         ORG   VTOCFMT4-44
         #VTCFMT4
         TITLE 'FORMAT 5 DSCB MAP'
***********************************************************************
*                                                                     *
*              DESCRIPTION OF FORMAT 5 DSCB                           *
*                                                                     *
***********************************************************************
         SPACE 2
         ORG   VTOCFMT5
         #VTCFMT5
         END
