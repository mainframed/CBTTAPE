MVSCMD   TITLE 'M V S C M D  ***  ISSUE PARM|SYSIN AS MVS|JES COMMAND'
         PUNCH ' ORDER MVSCMD(P) '   EASY DUMPS                 GP04127
         PUNCH ' SETCODE AC(1)   '   REQUIRED FOR MGCR          GP04127
*  FROM CBT 451, FILE 432, ADAPTED FOR LOCAL MACROS             GP04127
         COPY  OPTIONGB                                         GP04127
         SPACE 1                                                GP04127
         SYSPARM LIST=YES                                       GP04127
         SPACE 1                                                GP04127
*---------------------------------------------------------------------*
*                                                                     *
* AN AGELESS BOX-OFFICE SUCCESS, USED TO ISSUE ANY MVS OR JES2 COMMAND*
*(JUST TRY TO ISSUE THE $TA COMMAND BELOW IN BATCH BY ANOTHER MEANS..)*
*                                                                     *
* //S0 EXEC PGM=MVSCMD,PARM='D T'                                     *
* //S1 EXEC PGM=MVSCMD                                                *
* //SYSIN DD *                                                        *
* $TA,I=200,T=12.05,'$VS,''S MAINT'''                                 *
*                                                                     *
*---------------------------------------------------------------------*
**DEBUG  PRINT &PRTSOR                                          GP04127
MVSCMD   PGMHEAD ZERO12,BASE=R11,PARM=R10,AM=24,RM=24           GP04127
         MVC   DYNMGCR(DYNMGCRL),PATMGCR  FILL IN LIST ID       GP04127
         SERVINIT ,          INIT LOCAL SERVICES                GP04127
         LA    R10,0(,R10)   CLEAN PARM POINTER                 GP04127
         LTR   R10,R10       TEST                               GP04127
         BZ    SYNPUT        NOTHING; LOOK FOR SYSIN            GP04127
         L     R8,0(,R10)    LOAD PARM ADDRESS                  GP04127
         LA    R8,0(,R8)     CLEAN IT                           GP04127
         LTR   R8,R8         TEST                               GP04127
         BZ    SYNPUT        NOTHING; LOOK FOR SYSIN            GP04127
         USING PARM,R8                                          GP04127
         SR    R9,R9                                            GP04127
         ICM   R9,3,PARMLEN  LOAD AND TEST LENGTH OF PARM       GP04127
         BNZ   PARMYES                                          GP04127
         SPACE 1                                                GP04127
* PAS DE PARAMETRES, OUVERTURE SYSIN
SYNPUT   SERVLOAD @INPREAD   LOAD CARD READER                   GP04127
         INPOPEN SYSIN,OPT=ABEND  OPEN READER                   GP04127
LOOPGET  INPGET ,            GET A RECORD                       GP04127
         BXH   R15,R15,LEFIN  OR GET OUT                        GP04127
         LR    R15,R0        COPY RECORD LENGTH                 GP04127
         MINH  R15,=AL2(72)  ALLOW FOR SEQUENCE NUMBERS IN 73+  GP04127
         STH   R15,CMDLEN    STASH LENGTH                       GP04127
         BCTR  R15,0                                            GP04127
         EX    R15,EXMVCCMD  MOVE TO TEXT BUFFER                GP04127
EMETTRE  MODESET MODE=SUP,KEY=ZERO
         LA    R2,CMDLEN                                        GP04127
         MGCRE TEXT=(R2),CONSID=ZERO,MF=(E,DYNMGCR)  ISSUE CMD  GP04127
         MODESET MODE=PROB,KEY=NZERO
         B     LOOPGET                                          GP04127
EXMVCCMD MVC   CMDTEXT(0),0(R1)                                 GP04127
         SPACE 2                                                GP04127
PARMYES  MODESET MODE=SUP,KEY=ZERO
         MGCRE TEXT=(R8),CONSID=ZERO,MF=(E,DYNMGCR)  ISSUE CMD  GP04127
         MODESET MODE=PROB,KEY=NZERO
         SPACE 1                                                GP04127
LEFIN    SERVTERM ,          CLOSE AND FREE STUFF               GP04127
         PGMEXIT RC=0                                           GP04127
         SPACE 1                                                GP04127
*   READ-ONLY DATA                                              GP04127
SYSIN    INPWORK SYSIN,CMDIN,EDIT=128,EODAD=LEFIN               GP04127
PATMGCR  MGCRE MF=L          PATTERN FOR ID                     GP04127
         SPACE 1                                                GP04127
         LTORG ,                                                GP04127
         SPACE 1                                                GP04127
PARM     DSECT ,                                                GP04127
PARMLEN  DS    H             LENGTH OF PARM TEXT                GP04127
COMMANDE DS    CL100         DESIGN LIMIT FOR JCL               GP04127
         SPACE 1                                                GP04127
SAVE     DSECT ,             SAVE/WORK AREA                     GP04127
         SERVDEFS ,          SERVICE AREA                       GP04127
ZERO     DS    F             CONSTANT ZERO                      GP04127
CMDLEN   DC    AL2(126)                                         GP04127
CMDTEXT  DS    CL126                                            GP04127
DYNMGCR  MGCRE MF=L          SAVE SPACE                         GP04127
DYNMGCRL EQU   *-DYNMGCR                                        GP04127
SAVEEND  EQU   *             SAVE/WORK END                      GP04127
         SPACE 1                                                GP04127
         END  ,
