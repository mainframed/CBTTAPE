@BANDAID TITLE '@ B A N D A I D  ***  STAE FORMATTING SERVICE'
         MACRO ,
&NM      TRENT &TAB,&VAL,&OFF                           ADDED ON 86311
.*       THIS MACRO IS USED TO CREATE TRANSLATE AND TRANSLATE AND TEST
.*       TABLES IN COMPACT FASHION.
.*
         LCLC  &N
         LCLA  &I,&J
&J       SETA  N'&SYSLIST
         AIF   (&J EQ 0).ORG
         AIF   (&J LT 3).MEND
&N       SETC  '&NM'
&I       SETA  2
.LOOP    AIF   (&I GE &J).DONE
&I       SETA  &I+1
         AIF   ('&SYSLIST(&I)' EQ '').LOOP
         AIF   (N'&SYSLIST(&I) EQ 2).PAIR
         ORG   &TAB+&SYSLIST(&I)
&N       DC    AL1(&VAL)
&N       SETC  ''
         AGO   .LOOP
.PAIR    ORG   &TAB+&SYSLIST(&I,1)
&N       DC    (&SYSLIST(&I,2))AL1(&VAL)
&N       SETC  ''
         AGO   .LOOP
.DONE    AIF   ('&SYSLIST(&J)' EQ '').MEND
.ORG     ORG   ,
.MEND    MEND  ,
*----------------------------------------------------------------------
         MACRO ,
&NM      BAL#PROC &PROC
&NM      BAL   R14,&PROC     CALL SUBROUTINE
         MEND  ,
*----------------------------------------------------------------------
         MACRO ,
&NM      CAL#PROC &PROC
&NM      L     R15,=A(&PROC)
         BALR  R14,R15       CALL SUBROUTINE
         MEND  ,
*----------------------------------------------------------------------
         MACRO ,
&NAME    BEG#PROC &SAVE=YES
         GBLC  &#PROC
&#PROC   SETC  'IHB&SYSNDX'
         AIF   ('&SAVE' EQ 'NO').NOSAVE
DYNAM31  DSECT ,
&#PROC.S DS    2F                      SAVE AREA FOR R14 AND R10
@BANDAID CSECT ,                                                GP09255
&NAME    LR    R15,R10                 BASE REG
         STM   R14,R15,&#PROC.S        SAVE REGISTERS
         BALR  R10,0
         USING *,R10
         MEXIT ,
.NOSAVE  ANOP  ,
&NAME    DS    0H                                               GP09255
         USING *,R15
         MEND  ,
*----------------------------------------------------------------------
         MACRO ,
&NAME    END#PROC &RC=
         GBLC  &#PROC
&NAME    L     R14,&#PROC.S            RESTORE LINKAGE REGISTER
         L     R10,&#PROC.S+4          RESTORE BASE REGISTER
         BR    R14
         MEND  ,
*----------------------------------------------------------------------
         MACRO ,                                                  MACRO
&NAME   #S0C4  &ERRET,&PUSH                                       MACRO
         LCLC  &DD                                              GP09255
         AIF   ('&ERRET' EQ 'RESET').RESET                        MACRO
         AIF   ('&ERRET' EQ 'POP').POP                            MACRO
&DD      SETC  '(&ERRET-RETRY11)'                                 MACRO
         AIF   ('&PUSH' EQ 'PUSH').PUSH                           MACRO
&NAME    MVI   #RETRY+0,&DD/256
         MVI #RETRY+1,&DD-((&DD/256)*256)
         MEXIT ,                                                  MACRO
.PUSH    ANOP  ,                                                  MACRO
&NAME    MVC   #RETRY+2,#RETRY         PUSH CURRENT ADDRESS
         MVI   #RETRY+0,&DD/256
         MVI   #RETRY+1,&DD-((&DD/256)*256)
         MEXIT ,                                                  MACRO
.RESET   ANOP  ,                                                  MACRO
&NAME    MVI   #RETRY+0,0              RESET RETRY ADDRESS
         MEXIT ,                                                  MACRO
.POP     ANOP  ,                                                  MACRO
&NAME    MVC   #RETRY,#RETRY+2         POP PREVIOUS ADDRESS
         MVI   #RETRY+2,0              RESET RETRY ADDRESS
.MEND    MEND  ,                                                  MACRO
*----------------------------------------------------------------------
         MACRO ,
&NAME    TABLE &FN
         LCLA  &I,&J,&K                                         GP09255
         LCLC  &NAME2                                           GP09255
&I       SETA  2
&J       SETA  &FN
&NAME2   SETC  '&NAME'
.LOOP    AIF   (T'&SYSLIST(&I) EQ 'O').NEXT
         AIF   ('&SYSLIST(&I)'(1,1) EQ '''').HH
&K       SETA  K'&SYSLIST(&I)
&NAME2   DC    AL1(&J,&K),C'&SYSLIST(&I)'
.HH      AIF   ('&SYSLIST(&I)'(1,1) NE '''').NEXT
&K       SETA  K'&SYSLIST(&I)-2
&NAME2   DC    AL1(&J,&K),C&SYSLIST(&I)
.NEXT    ANOP  ,
&NAME2   SETC  ''
&I       SETA  &I+1
&J       SETA  &J+1
         AIF   (&I LE N'&SYSLIST).LOOP
         MEND  ,
***********************************************************************
*                                                                     *
*       GSF-ENV DEBUGGING AID                                         *
*                                                                     *
*       THIS GENERALIZED ESTAE ROUTINE SIMPLIFIES ABEND               *
*       RESOLUTION BY FORMATTING IMPORTANT MVS CONTROL                *
*       BLOCKS IN AN EASIER-TO-READ FASHION.                          *
*                                                                     *
*       THIS IS NOT A SYSTEM-WIDE DEBUGGING TOOL LIKE ABEND-AID.      *
*       TO USE IT, YOU MUST INVOKE IT WITH THE FOLLOWING              *
*       SEQUENCE:                                                     *
*                                                                     *
*                  LOAD  EP=@BANDAID                                  *
*                  LR    R15,R0                                       *
*                  BASSM R14,R15              INVOKE WITH AMODE31     *
*                                                                     *
*       NOTES:                                                        *
*                                                                     *
*         1. THIS FIRST INVOCATION OF THE ROUTINE SETS UP A STANDARD  *
*            RECOVERY ENVIRONMENT (ESTAE) TO TRAP ABENDS.             *
*                                                                     *
*         2. DO NOT TRY TO REPLACE LOAD/BASSM WITH LINK IN THE        *
*            INVOKING SEQUENCE.                                       *
*                                                                     *
*       THE DEBUGGING ROUTINE MAY ALSO BE USED FROM JCL,              *
*       WITHOUT MODIFYING THE PROGRAM, AS FOLLOWS:                    *
*                                                                     *
*         //MYSTEP  EXEC PGM=@BANDAID,                                *
*         //            ACCT=MYPROG,            <== PROGRAM NAME      *
*         //            PARM=(A,B,C,D)          <== PROGRAM'S PARM    *
*         //STEPLIB  DD DISP=SHR,DSN=GILBERT.LOAD                     *
*         //SYSDEBUG DD SYSOUT=*                                      *
*         //SYSUDUMP DD SYSOUT=*                                      *
*                                                                     *
*       THE DEBUGGING REPORT (WRITTEN TO DDN=SYSDEBUG) CONTAINS       *
*       THE FOLLOWING DATA:                                           *
*                                                                     *
*         O  THE ABEND PSW AND REGISTERS                              *
*                                                                     *
*         O  THE TCB TREE AND RB CHAINS (IF THIS IS A MULTI-TASK STEP *
*                                                                     *
*         O  THE RB CHAIN OF THE ABENDING TCB, IN GREATER DETAIL      *
*                                                                     *
*         O  THE JPAQ AND LOAD-LIST                                   *
*                                                                     *
*         O  THE SAVE-AREA CHAIN (FORWARD ONLY)                       *
*                                                                     *
*         O  THE TIOT (ALLOCATED DDS)                                 *
*                                                                     *
*         O  OPEN DATA SETS (WITH CURRENT RECORD)                     *
*                                                                     *
*   REASON CODES FOR NOT PRINTING A RECORD/BUFFER:                    *
*        00 length is 0                01 EOB address invalid         *
*        02 RECAD not valid                                           *
*        06 IOB/ICB not valid                                         *
*                                      11 Buffer,EOBAD,BDW mismatch   *
*        12 RDW length < 4             13 RDW length > LRECL          *
*        14 RDW low bytes not 0        15 VB scan failed              *
*                                                                     *
*                                                                     *
*       THE DEBUGGING ROUTINE HAS A LOOP DETECTION FEATURE            *
*       THAT CAUSES AN U0322 ABEND IF THE PROGRAM CONSUMES            *
*       MORE THAT 5 SECONDS OF CPU TIME WITHOUT DOING AN IO.          *
*       THIS FEATURE IS INACTIVATED WHEN A //DBGNO322 DD IS           *
*       PRESENT IN STEP'S JCL.                                        *
*                                                                     *
***********************************************************************
*                                                                     *
*   I STARTED RETROFITTING THIS FOR MVS 3.8J (TURNKEY PROJECT) USING  *
*   CONDITIONAL ASSEMBLY, BUT IT GOT TOO MESSY. SOME EXTRANEOUS XA,   *
*   ESA, OS/390 AND Z/OS STUFF REMAINS, BUT DOESN'T INTERFERE.        *
*   FUNCTIONS NOT AVAILABLE UNDER MVS HAVE BEEN REPLACED OR SKIPPED.  *
*   UNAVAILABILITY OF LOCTR UNDER ASM/XF REQUIRED MAJOR REARRANGEMENT.*
*   ADDITIONAL WORK REMAINS.                                          *
*   SURPRISES: SVC 99 TEXT UNITS MUST BE IN VOLATILE STORAGE, EVEN    *
*     THOUGH THEY'RE NOT SUPPOSED TO BE ALTERED???  STILL FAILS -     *
*       WHEN DD PRE-ALLOCATED, GET 0C4 IN SVC 99. ADDED DEVTYPE.      *
*   CHANGED SYSDEBUG ABEND TO CANCEL, PREVENTING ABEND LOOP.          *
*                                                      G. POSTPISCHIL *
***********************************************************************
         PUNCH '  ORDER @BANDAID(P) '   EASIER DEBUGGING        GP09255
         SPACE 1                                                GP09255
         COPY  OPTIONGB      LOCAL ASSEMBLY DEFINITIONS         GP09255
         GBLC  &SYSVER,&SYSDATC,&STR,&HDR                       GP09255
         GBLA  &K                                               GP09255
         LCLC  &LOCANY,&LOCBEL                                  GP09255
         AIF   (NOT &MVSXA).NOLOCOP                             GP09255
&LOCANY  SETC  ',LOC=ANY'                                       GP09255
&LOCBEL  SETC  ',LOC=(BELOW,ANY)'                               GP09255
.NOLOCOP SPACE 1                                                GP09255
&HDR  SETC 'GSF UTILITIES - @BANDAID R038'                      GP09255
*ESP  RETROFIT TO RUN ON MVS 3.8J; USE TACHYON ASSEMBLER        GP09255
*NO*  PUNCH ' SETSSI 02510000'  ESP USES 0YYYYDDD
*ESP  CHANGED PROGRAM NAME FROM SYSDEBUG TO @BANDAID - LOCAL PREFERENCE
*$251 4-DIGIT DEVN (REQUIRES MVS/ESA 4.1 OR ABOVE).
*$250 ENQ AND HH.MM.SS
*$249 FIX S0C7 IN OS/390 R3 BUG WHEN JCTJMRJD=0 AND JCTSSD=0
*$248 ADD SUPPORT FOR HLASM R3
*     ADD SUPPORT FOR JES2 OS 1.3.0
*$247 TITLE LINE DISPLAYS DATE IN YYYY-MM-DD FORMAT
*$246 CORRECT RECFM=U RECORD LENGTH
*$245 INITIALISATION ROUTINE HAS BEEN REWRITTEN TO NO LONGER
*     REQUIRE CALLER TO PROVIDE ADDR OF SAVE AREA IN R13.
*$244 ADD SUPPORT FOR CALLERS IN MODE=SUP AND KEY 0-7
*     MINOR CHANGES TO JOB CARD FORMATTING ROUTINE
*     LOCATE DDNAME FOR JPAQ MODULES LOADED FROM A PDSE
*$243 ALLOCATE 4 PAGES BELOW THE LINE AT INITIALIZATION TIME.
*     FREE-UP 3 PAGES IN RECOVERY RTNE TO PREVENT GETMAIN FAILURES.
*     FREE-UP ALL 4 PAGES IF RECOVERY INVOKED WITH R0=12 (NO SDWA)
*$242 USE RTM2WA TO DISTINGUISH BETWEEN TYPE-1 SVC AND PROGRAM CHECK
*     INACTIVATE ESPIE ENVIRONMENT CREATED BY INVOKING PGM
*     CONVERT ESTAE TO ESTAI IF INVOKED FROM AN SVC ROUTINE
*     GET ADDR OF RMODE31 ACB FROM DEBRRQ
*$241 DO NOT DISPLAY OPEN MODE FOR VSAM ACB, IT'S MEANINGLESS
*     DO NOT ISSUE U0322 ABEND IF //DBGNO322 DD PRESENT IN STEP'S JCL
*     PREVENT S30A-14 ABEND WHEN INVOKED BY A USER IN KEY 0-7
*     RETRIEVE BUFFER ADDR FROM ICB WHEN CHAINED SCHEDULING IS USED
*     IMPLEMENT NEXTLLE RTNE TO PREVENT STACK OVERFLOW
*     CHECK FOR R0=12 ON ENTRY TO RECOVERY ROUTINE
*$240 DO NOT PRINT LINES WITH IDENTICAL CONTENTS (PDUMP RTNE)
*     PRINT PLH/RPL SEQ# WHEN STRNO>1
*$239 ENHANCE JOB/STEP INFORMATION REPORTING
*     HASH-CODE NAME TO ACCESS CDX
*     PRINT ALL ACTIVE PLHS IF STRNO>1
*     PRINT CONTENTS OF COM_REG AREA
*     PREVENT S0C4 WHEN IOBECBAD IS BAD
*     PROVIDE SUPPORT FOR QSAM BUFFERS ABOVE THE LINE (DFSMS/MVS)
*     IMPROVE METHOD TO LOCATE V/VB RECORD IN QSAM BUFFERS
*$238 PREVENT S806 WHEN INVOKER DOESN'T SET R13
*$237 STORAGE ALLOCATION TABLE (USING TCTCORE AND LDA)
*     SOME INFORMATION FROM JOB AND EXEC JCL STATEMENTS
*$236 USE ESTAE INSTEAD OF ESPIE TO VALIDATE A(DYNAM31)
*$236 CONVERT DDN TO DSN FOR JPAQ ENTRIES
*$235 IDENTIFY SUB-TASKS ON TCB TREE THAT HAVE TERMINATED ALREADY
*$234 RETRIEVE CDEX TO DISPLAY DDNAME FOR JPAQ MODULES
*$234 FLAG SAVE AREA POINTED TO BY R13 AT TIME OF ABEND
*$233 DISPLAY CONTENTS OF PARM FIELD
*$232 FLAG ABENDING RB, USE NEXTRB ROUTINE
*$231 DISPLAY LABEL=(,SUL) IF PRESENT ON DD STMT FOR DISK DATA SETS
*$230 DISPLAY R14,R15,R0,R1 VALUES FOR EACH RB
*$229 PRINT RPLOPTCD OPTIONS CODES AND DCBE
*$228 DISPLAY ATTRIBUTES OF SYSOUT DATA SETS
*$227 SCAN LPAQ IN ADDITION TO JPAQ AND LPAD
*$226 DISPLAY REG1 FOR DATACOM SUBTASK
*$225 SOME MODIFICATIONS IN VSAM CONTROL BLOCK DISPLAY
*$224 PRINT SDWA AT THE END OF THE REPORT
*$223 IDENTIFY OS/VS COBOL EYE-CATCHERS
*$222 VALIDATE ADDRESS OF WORK AREA TO PREVENT S0C4
*$220 FIXED BUG IN DUMP32 ROUTINE THAT PREVENTED PLH/RPL DISPLAY
*$212 DISPLAY CURRENT SAVE AREA IF NOT IN FWD CHAIN
*$211 USE NUCLKUP TO ENHANCE ADDRESS IDENTIFICATION
*$210 USE ONLY STANDARD MACRO LIBRARIES
*$209 IMPROVE SAVE AREA PROCESSING
*$208 GET RID OF @JDATE ROUTINE
*$206 ALLOW INVOCATION FROM JCL TO FRONT-END PGM
*$205 PRINT SYSOUT RCD FROM JES2 UNPROTECTED BUFFER
*$200 DO NOT USE SUBS SERVICES
***********************************************************************
         SYSPARM LIST=YES    SET LOCAL OPTIONS                  GP09255
         SPACE 1                                                GP09255
         PRINT NOGEN         SAVE A TREE OR TWO <G>             GP03001
@BANDAID CSECT ,                       MAIN CSECT
@BANDAID AMODE 31                      MAIN CSECT               GP03001
@BANDAID RMODE ANY                     MAIN CSECT
DYNAM31  DSECT ,             START IT EARLY (NO LOCTR)          GP09255
         DS    18A           TOP SAVE AREA                      GP09255
@BANDAID CSECT ,             BACK TO CODE                       GP09255
         SPACE 1
         USING *,R15
         B     BEGIN
*
*        USING THE OFFICIAL ASMH/HLASM DISTINCTION TRICK,
*        SIMULATE THE SYSVER AND SYSDATC VARIABLES
*        OF HLASM WHEN ASSEMBLED UNDER ASMH
*
&SYSVER  SETC  'ASMXF'
&SYSDATC SETC  '20'.'&SYSDATE'(7,2)'&SYSDATE'(1,2)'&SYSDATE'(4,2)
         AIF   ('&SYSDATC' LT '20900000').ASMH1X
&SYSDATC SETC  '19'.'&SYSDATE'(7,2)'&SYSDATE'(1,2)'&SYSDATE'(4,2)
.ASMH1X  ANOP  ,
&STR     SETC  '&HDR &SYSDATC &SYSTIME'
&K       SETA  K'&STR
         DC    AL1(&K),C'&HDR &SYSDATC &SYSTIME',0H'0'
*---------------------------------------------------------------------*
*                                                                     *
*        DETERMINE IF RECOVERY IS ALREADY INITIALIZED FOR THIS TASK.  *
*        IF IT IS, SET R15 AND R0 AND GOBACK TO CALLER                *
*                                                                     *
*---------------------------------------------------------------------*
BEGIN    L     R1,PSATOLD-PSA          MY TCB
         USING TCB,R1
         ICM   R1,B'1111',TCBSTAB      FIRST SCB
         BZ    INIT12                  NO SCB FOR THIS TASK, EXIT
         USING SCB,R1
         LA    R0,DBGAID             MY RECOVERY EXIT
*LOOP
INIT11B  CL    R0,SCBEXIT              IS THIS ME?
         BNE   INIT11N                 NO, EXIT
         LR    R15,R0                  A(DBGAID)
         SR    R0,R0         CLEAR HIGH BYTE                    GP09255
         ICM   R0,7,SCBPARM  GET OLD FASHIONED PARAM VALUE      GP09255
         BR    R14           RETURN                             GP09255
         SPACE 1                                                GP09255
INIT11N  ICM   R1,B'1111',SCBCHAIN     NEXT SCB
         BNZ   INIT11B                 LOOP
*ENDLOOP
*---------------------------------------------------------------------*
*                                                                     *
*        SCAN TIOT TO SEE IF A SYSDEBUG DD IS ALLOCATED.              *
*        IF IT IS ALLOCATED TO A DD DUMMY, QUIT.                      *
*        IF IT IS NOT ALLOCATED AND WE'RE UNDER TSO, QUIT.            *
*                                                                     *
*---------------------------------------------------------------------*
INIT12   BALR  R15,0
         USING *,R15
         L     R1,PSATOLD-PSA          MY TCB
         USING TCB,R1
         L     R1,TCBTIO               TIOT
         USING TIOT1,R1
         SLR   R0,R0                   PREPARE IC
*LOOP
INIT12B  CLC   TIOEDDNM,DCBDDNAM-IHADCB+MODELDCB IS THIS MY DDNAME?
         BE    INIT12K                 YES, EXIT LOOP
         IC    R0,TIOELNGH             LOAD LENGTH OF CURRENT ENTRY
         ALR   R1,R0                   BUMP UP TO NEXT ENTRY
         CLI   TIOELNGH,0              IS THIS THE END?
         BNZ   INIT12B                 NOT YET, LOOP THROUGH TIOT
*ENDLOOP
*
*        IF TSO AND DD MISSING, DO NOT ACTIVATE DEBUGGING
*
INIT12D  L     R1,PSAAOLD-PSA          MY ASCB
         L     R1,ASCBCSCB-ASCB(,R1)   MY CSCB
         LA    R0,1                    R0=1
         CLI   CHTRKID-CHAIN(R1),CHTSID  AM I A TSO USER?
         BE    INIT19                  YES, QUIT (R15=0,R0=1)
*
*        IF //SYSDEBUG DD DUMMY, EXIT
*
INIT12K  BALR  R15,0                   (LOCAL BASE)
         USING *,R15
         TM    TIOELINK,TIOESSDS       SUB-SYSTEM DATA SET?
         BO    INIT20                  YES, CONTINUE
         ICM   R0,B'0111',TIOEFSRT     DD DUMMY?
         BNZ   INIT20                  NO, CONTINUE
         DROP  R1                      TIOT
         LA    R0,2                    R0=2
*
*        INITIALISATION ERROR: GOBACK TO CALLER WITH R15=0
*
INIT19   SLR   R15,R15                 YES, QUIT (R15=00)
         SLR   R1,R1                   R1=0
         BSM   0,R14
*---------------------------------------------------------------------*
*                                                                     *
*        ISSUE TESTAUTH TO DETERMINE IF WE SHOULD TAKE THE            *
*        AUTHORIZED OR THE NON-AUTHORIZED INIT PATH.                  *
*                                                                     *
*---------------------------------------------------------------------*
INIT20   BALR  R15,0                   (LOCAL BASE)
         USING *,R15
         TESTAUTH STATE=YES,RBLEVEL=1  CHECK PSW MODE
         LTR   R15,R15                 RUNNING IN MODE=SUP ?
         BALR  R15,0                   (LOCAL BASE)
         USING *,R15
         BNZ   INIT200                 NO, JUMP
***********************************************************************
*                                                                     *
*        AUTHORIZED INITIALISATION PATH (INVOKED WITH MODE=SUP)       *
*                                                                     *
*        1. GETMAIN PROTECTED SAVE AREA (SP 252)                      *
*        2. SAVE CALLER'S REGS                                        *
*        3. SWITCH TO TCB KEY                                         *
*        4. GETMAIN DYNAM31                                           *
*        5. GETMAIN DYNAM24                                           *
*        6. ISSUE ESTAE                                               *
*        7. CHANGE ESTAE TO ESTAI (IF INVOKED FROM SVC ROUTINE)       *
*        8. RESTORE CALLER'S REGS AND PSW KEY                         *
*        9. FREE PROTECTED SAVE AREA AND GOBACK TO CALLER             *
*                                                                     *
***********************************************************************
INIT100  GETMAIN RU,LV=64,SP=252&LOCANY                         GP09255
         LR    R0,R2                   SAVE R2 ACCROSS MODESET
         MODESET EXTKEY=ZERO,SAVEKEY=(2) SWITCH TO TCB KEY
         XR    R2,R0                   RESTORE R2
         XR    R0,R2                   RESTORE R2
         XR    R2,R0                   RESTORE R2
         STM   R0,R15,0(R1)            SAVE CALLER'S REGS AND PSW KEY
         BALR  R11,0
         USING *,R11
         LR    R12,R1                  KEEP SAVE AREA ADDR
*
*        SWITCH TO TCB KEY
*
INIT101  L     R4,PSATOLD-PSA
         USING TCB,R4
         MODESET EXTKEY=TCB,WORKREG=2  SWITCH TO TCB KEY
*
INIT102  LA    R0,DYNAM31L-1
         GETMAIN RC,LV=(0),BNDRY=PAGE,&LOCANY                   GP09255
         LTR   R15,R15                 GETMAIN OK?
         BNZ   INIT190                 NO, QUIT
         LR    R13,R1                  PASS ADDRESS
         USING DYNAM31,R13
         XC    ZEROES,ZEROES ENSURE IT'S CLEAR                  GP03119
*
         BAL   R14,INIT900             ALLOCATE //SYSDEBUG DD
*
*        ALLOCATE 4 PAGES BELOW THE LINE.  MOST OF THIS STORAGE
*        IS A PLACE-HOLDER THAT IS FREEMAIN'D BY THE RECOVERY
*        RTNE WHEN IT IS ENTERED TO PREVENT POSSIBLE S80A OR S878
*        ABENDS DURING OPEN OF THE SYSDEBUG DCB.
*
         GETMAIN RC,LV=DYNAM24L&LOCBEL   RMODE24 WORK AREA      GP09255
         LTR   R15,R15                 GETMAIN OK?
         BNZ   INIT190                 NO, GOBACK
         ST    R1,DYNAM24P             SAVE A(DYNAM24)
*
*        ISSUE ESTAE
*
         ESTAE DBGAID,               RECOVERY ROUTINE                  X
               CT,                     CREATE                          X
               PARAM=(R13),            PARAM FOR RECOVERY ROUTINE      X
               TERM=YES,               PROCESS CANCEL ABENDS           X
               MF=(E,ESTAEL)
         LTR   R0,R15                  ESTAE OK?
         BNZ   INIT190                 NO, GOBACK
*---------------------------------------------------------------------*
*                                                                     *
*        CONVERT THE ESTAE TO AN ESTAI IF I'VE BEEN INVOKED FROM      *
*        AN SVC ROUTINE.                                              *
*                                                                     *
*---------------------------------------------------------------------*
INIT130  L     R5,TCBRBP               MY PRB OR SVRB
         USING RBBASIC,R5
         LA    R0,130                  EXIT CODE
         TM    RBSTAB1,RBFTSVRB        RUNNING UNDER AN SVRB?
         BNO   INIT190                 NO, EXIT
*
INIT135  LA    R0,135                  EXIT CODE
         ICM   R6,B'1111',TCBSTAB      MOST RECENT SCB
         BZ    INIT190                 NO SCB, EXIT (VERY BIZARRE!!!)
         USING SCB,R6
         LA    R15,DBGAID            EXIT ADDR WITHOUT AMODE BIT
         LA    R0,136                  EXIT CODE
         CL    R15,SCBEXIT             IS THIS THE ADDR OF MY EXIT?
         BNE   INIT190                 NO, QUIT
*---------------------------------------------------------------------*
*                                                                     *
*        PATCH THE SCB                                                *
*                                                                     *
*---------------------------------------------------------------------*
         BALR  R15,0
         USING *,R15
         MODESET EXTKEY=ZERO           SWITCH TO KEY ZERO
         STCM  R4,B'0111',SCBOWNRA     ASSOCIATE SCB WITH TCB
         OI    SCBFLGS1,SCBSTAI        ESTAI
         NI    SCBFLGS2,255-SCBSUPER   MODE=PROB
         NI    SCBFLGS2,255-SCBKEY0    KEY=NZERO
*
*        CHECK IF MY SCB IS THE ONLY ONE CHAINED TO MY TCB.
*        IF THERE ARE OTHER SCBS ON THE QUEUE, MOVE MY SCB
*        TO THE END OF THE QUEUE TO ENSURE I'M INVOKED LAST
*        AND PREVENT S0C4 IN IEAVTAS1.
*
         LA    R0,141                  EXIT CODE
         ICM   R2,B'1111',SCBCHAIN     ONLY SCB ON QUEUE?
         BZ    INIT190                 YES, EXIT
*LOOP
INIT144  LR    R1,R2                   PREVIOUS SCB
         ICM   R2,B'1111',SCBCHAIN-SCB(R1) LAST SCB ON QUEUE?
         BNZ   INIT144                 NO, LOOP MORE
*ENDLOOP
         ST    R6,SCBCHAIN-SCB(,R1)    MY SCB AT BOTTOM OF QUEUE
         MVC   TCBSTAB,SCBCHAIN        NEXT SCB AT TOP OF QUEUE
         XC    SCBCHAIN,SCBCHAIN       MY SCB IS LAST ON QUEUE
         LA    R0,145                  EXIT CODE
*---------------------------------------------------------------------*
*                                                                     *
*        RESTORE CALLER'S ENVIRONMENT AND GOBACK                      *
*                                                                     *
*---------------------------------------------------------------------*
         DROP
INIT190  BALR  R15,0
         USING *,R15
         L     R2,0(,R12)              R0 FROM PROTECTED SAVE AREA
*NEW*    MODESET KEYREG=(R2)           SWITCH BACK TO CALLER'S KEY
         MODESET KEYADDR=(2),WORKREG=(2)  BACK TO CALLER'S KEY  GP09255
         LR    R1,R12                  ADDR OF PROTECTED SAVE AREA
         LM    R2,R14,8(R12)           RESTORE CALLER'S R2-R14
 LR R2,R0                              PASS EXIT CODE
         FREEMAIN RU,LV=64,A=(R1),SP=252 FREE PROTECTED SAVE AREA
*
         SLR   R15,R15                 R15=0
         BCTR  R15,0                   R15=-1
         SLR   R0,R0                   R0=0
         SLR   R1,R1                   R1=0
         BSM   0,R14                   GOBACK TO CALLER
         DROP  ,
***********************************************************************
*                                                                     *
*        NON-AUTHORIZED INITIALISATION PATH (INVOKED WITH MODE=PROB)  *
*                                                                     *
*        1. GETMAIN DYNAM31                                           *
*        2. SAVE CALLER'S REGS IN SAVER012                            *
*        3. GETMAIN DYNAM24                                           *
*        4. ISSUE ESTAE                                               *
*        5. PRIME TIMER                                               *
*        6. ISSUE XCTL (IF INVOKED FROM JCL)                          *
*        7. RESTORE CALLER'S REGS AND GOBACK (IF INVOKED FROM PGM)    *
*                                                                     *
***********************************************************************
INIT200  BALR  R15,0
         USING *,R15
         LA    R0,DYNAM31L-1
         GETMAIN RC,LV=(0),BNDRY=PAGE,&LOCANY                   GP09255
         DROP  R15
         LTR   R15,R15                 GETMAIN OK?
         BALR  R15,0
         BZ    INIT210-*(,R15)         NO, QUIT
         SLR   R15,R15                 R15=0
         SLR   R0,R0                   R05=0
         SLR   R1,R1                   R1=0
         BSM   0,R14                   GOBACK
*
INIT210  STM   R0,R15,SAVER012-DYNAM31(R1) SAVE CALLER'S REGS
         LR    R13,R1                  A(DYNAM31)
         USING DYNAM31,R13
*
         BALR  R11,0
         USING *,R11
*
         BAL   R14,INIT900            ALLOCATE //SYSDEBUG DD
*
*        ALLOCATE 4 PAGES BELOW THE LINE.  MOST OF THIS STORAGE
*        IS A PLACE-HOLDER THAT IS FREEMAIN'D BY THE RECOVERY
*        RTNE WHEN IT IS ENTERED TO PREVENT POSSIBLE S80A OR S878
*        ABENDS DURING OPEN OF THE SYSDEBUG DCB.
*
         GETMAIN RC,LV=DYNAM24L&LOCBEL   RMODE24 WORK AREA      GP09255
         LTR   R15,R15                 GETMAIN OK?
         BNZ   INIT209                 NO, QUIT
         ST    R1,DYNAM24P             SAVE A(DYNAM24)
*
*        ISSUE ESTAE XCTL=YES
*
         ESTAE DBGAID,               RECOVERY ROUTINE                  X
               CT,                     CREATE                          X
               PARAM=(R13),            PARAM FOR RECOVERY ROUTINE      X
               TERM=YES,               PROCESS CANCEL ABENDS           X
               XCTL=YES,               STAY ACTIVE ACROSS XCTL         X
               MF=(E,ESTAEL)
         LA    R0,X'0020'              RETCODE FOR XCTL/STACK CONFLICT
         CLR   R15,R0                  XCTL/STACK CONFLICT?
         BNE   INIT220                 NO, CONTINUE
*
*        ISSUE ESTAE XCTL=NO IF THERE IS AT LEAST A STACK ENTRY ACTIVE
*
         ESTAE DBGAID,               RECOVERY ROUTINE                  X
               CT,                     CREATE                          X
               PARAM=(R13),            PARAM FOR RECOVERY ROUTINE      X
               TERM=YES,               PROCESS CANCEL ABENDS           X
               MF=(E,ESTAEL)
         LTR   R15,R15                 ESTAE OK?
         BZ    INIT220                 YES, JUMP
*
INIT209  LM    R2,R14,SAVER012+8       RESTORE CALLER'S REGISTERS
         SLR   R15,R15                 R15=0
         SLR   R0,R0                   R05=0
         SLR   R1,R1                   R1=0
         BSM   0,R14                   GOBACK
*---------------------------------------------------------------------*
*                                                                     *
*        SET-UP TIMER FOR LOOP DETECTION                              *
*                                                                     *
*---------------------------------------------------------------------*
INIT220  L     R4,PSATOLD-PSA          MY TCB
         USING TCB,R4
         L     R5,TCBRBP               MY PRB OR SVRB
         USING RBBASIC,R5
         CLM   R4,7,RBLINK+1           FIRST AND ONLY PRB IN CHAIN?
         BNE   INIT250                 NO, DO NOT SET TIMER
         DEVTYPE =C'DBGNO322',WKCELL1
         LTR   R15,R15                 //DBGNO322 DD SPECIFIED?
         BZ    INIT250                 YES, DO NOT SET TIMER
         L     R1,TCBFSA               FIRST SAVE AREA
         MVI   20+R6*4(R1),255         FOR U322, JUST IN CASE . . .
         SYNCH U322SET                 PRIME THE U322 PUMP
*---------------------------------------------------------------------*
*                                                                     *
*        XCTL TO PGMNM IN ACCT FIELD                                  *
*                                                                     *
*        //GO EXEC PGM=@BANDAID,ACCT=USERPROG,PARM=(USER-PARM)        *
*                                                                     *
*---------------------------------------------------------------------*
INIT250  CL    R4,TCBJSTCB             IS THIS THE JOB-STEP TCB ?
         BNE   INIT290                 NO, GOBACK
         CLC   TCBFSA,SAVER012+R13*4   AM I INVOKED THRU JCL?
         BNE   INIT290                 NO, GOBACK
         L     R5,TCBRBP               POINT TO MY PRB
         SLR   R6,R6
         ICM   R6,B'0111',RBCDE1       DO WE HAVE A CDE?
         BZ    INIT290                 NO, GOBACK
         USING CDENTRY,R6
         L     R14,CDENTPT                                      GP09255
         LA    R14,0(,R14)   CLEAR HIGH                         GP09255
         C     R14,=A(@BANDAID)   CURRENT IS MYSELF?            GP09255
         BNE   INIT290                 NO, GOBACK
         MVC   SAVER012(8),RBGRSAVE    PASS R0-R1
*
         L     R5,TCBJSCB              MY JSCB
         USING IEZJSCB,R5
         LOAD  EPLOC=JSCBPGMN          BUMP MY USE COUNT FOR XCTL
         L     R6,JSCSCT               MY SCT
         USING SCT,R6
         SLR   R7,R7
         ICM   R7,B'0111',SCTAFACT     ACCOUNT DATA
         USING IEFAACTB-16,R7
         LA    R14,WKCELL1             8-BYTE WORK AREA
         LA    R15,L'WKCELL1           LENGTH
         LA    R0,ACTACCNT+1           FIRST ACCOUNT FIELD
         L     R1,=X'40000000'         PADDING
         IC    R1,ACTACCNT+0           LENGTH OF PGM NAME
         MVCL  R14,R0                  MOVE PGM NAME
         LA    R15,WKCELL2             WORK AREA
         LA    R0,WKCELL1              PGM NAME
         SLR   R1,R1
         STM   R0,R1,0(R15)            XCTL LIST
         LM    R0,R14,SAVER012         RESTORE CALLER'S REGISTERS
         XCTL  SF=(E,(15))             EXECUTE TARGET PROGRAM
         DROP  R4,R5,R6,R7
*
*        ESTAE EXECUTED, SET R15/R0 AND GO BACK TO CALLER
*
INIT290  LA    R15,DBGAID            R15=A(DBGAID)
         BSM   R15,0                   SET AMODE BIT
         LA    R0,DYNAM31              R0=A(DYNAM31)
         SLR   R1,R1                   R1=0
         LM    R2,R14,SAVER012+8       RESTORE CALLER'S REGISTERS
         BSM   0,R14                   GOBACK, PASS R15-R0
         DROP  ,
*---------------------------------------------------------------------*
*                                                                     *
*        ALLOCATE SYSOUT DATA SET                                     *
*   SVC 99 0C4s in MVS trying to write back to INIT9901 - made dynam. *
*---------------------------------------------------------------------*
INIT900  BALR  R15,0
         USING *,R15
*OLD*    LA    R1,TENWORDS-DYNAM31(,R13) WORK AREA FOR DYNALLOC
         LA    R1,TENWORDS-10*4-DYNAM31(,R13)  WORK AREA        GP09255
         STM   R14,R3,0(R1)  SAVE SOME REGISTERS                GP09298
         BALR  R2,0                                             GP09298
         USING *,R2                                             GP09298
         LR    R3,R1         SAVE OVER DEVTYPE                  GP09298
         DEVTYPE INIT9901+6,24(R3)     LOOK FOR SYSDEBUG        GP09298
         BXLE  R15,R15,INIT99HV        ALREADY ALLOCATED        GP09298
         LM    R14,R3,0(R3)  RESTORE REGISTERS                  GP09298
         DROP  R2                                               GP09298
         XC    0(60,R1),0(R1)          CLEAR STORAGE
         MVI   0(R1),20                LENGTH
         MVI   1(R1),1                 VERB=ALLOC
         LA    R0,20(,R1)    TEXT UNIT POINTERS                 GP09255
         ST    R0,8(,R1)     TUP                                GP09255
         LA    R0,32(,R1)    FIRST TEXT UNIT                    GP09255
         ST    R0,20(,R1)    FIRST POINTER                      GP09255
         LA    R0,46(,R1)    FIRST TEXT UNIT                    GP09255
         ST    R0,24(,R1)    FIRST POINTER                      GP09255
         LA    R0,50(,R1)    FIRST TEXT UNIT                    GP09255
         ST    R0,28(,R1)    FIRST POINTER                      GP09255
         OI    28(R1),X'80'   END OF POINTERS                   GP09255
         MVC   32(22,R1),INIT9900   MOVE TEXT UNITS             GP09255
         ST    R1,58(,R1)              WORK AREA ADDRESS        GP09255
         LA    R1,58(,R1)              S99RBPTR                 GP09255
         OI    0(R1),X'80'             S99RBPTR
         SVC   99                      ALLOCATE SYSDEBUG
         BR    R14                     CONTINUE
INIT99HV LM    R14,R3,0(R3)  RESTORE REGISTERS                  GP09298
         BR    R14                     RETURN                   GP09298
*
INIT9900 DC    A(INIT9901,INIT9918,INIT9952+X'80000000')
INIT9901 DC    AL2(1,1,8),C'SYSDEBUG'  DDNAME
INIT9918 DC    X'0018,0000'            SYSOUT=*
INIT9952 DC    X'0052,0000'            PERMANENTLY ALLOCATED
*---------------------------------------------------------------------*
*                                                                     *
*        LOOP DETECTOR (ABENDU0322 AFTER 5 SECONDS OF CPU WITHOUT I/O)*
*                                                                     *
*        USES THE R6 SLOT OF THE 1ST SAVE AREA TO HOLD THE EXCP COUNT *
*                                                                     *
*---------------------------------------------------------------------*
U322SET  SAVE  (14,12),,DEBUG.U322SET  SAVE REGISTERS
         LR    R3,R15                  LOCAL BASE
         USING U322SET,R3
         L     R4,PSATOLD-PSA          MY TCB
         L     R5,PSAAOLD-PSA          MY ASCB
         ICM   R1,15,ASCBOUCB-ASCB(R5)  GET RMF OUCB            GP09255
         BZ    U322MFEL      NO COUNT - DON'T FAIL              GP09255
         L     R0,OUCBIOSM-OUCB(,R1)   GET RMS EXCP COUNT       GP09255
         L     R1,TCBFSA-TCB(,R4)      FIRST SAVE AREA
         CL    R0,20+R6*4(,R1)         SAME EXCP COUNT ?
         BE    U322ABND                YES, ABEND
U322MFEL ST    R0,20+R6*4(,R1)         SAVE EXCP COUNT INTO R6 SLOT
         STIMER TASK,U322SET,BINTVL=U322TIME
U322EXIT RETURN (14,12)                GOBACK TO FLIH
*
U322ABND L     R6,TCBRBP-TCB(,R4)      POINT TO MY RB (TIRB)
         LM    R0,R15,RBGRSAVE-RBBASIC(R6)  LOOPING PROGRAM'S REGISTERS
         BALR  R1,0
         ICM   R1,B'1111',U322CODE-*(R1)    LOAD ABEND CODE
         CNOP  6,8
         SVC   13                      ISSUE ABEND SVC
U322TIME DC    F'00500'                TIMER INTVL IN 1/100 OF A SECOND
U322CODE DC    X'80',FL3'322'          ABEND 0322,DUMP
         DROP  ,
         EJECT ,
***********************************************************************
*                                                                     *
*        DEBUGGING ROUTINE                                            *
*                                                                     *
***********************************************************************
*
         USING DBGAID,R15
DBGAID CH    R0,H12                  SDWA ALLOCATED?
         BNE   DBGAID4               YES, JUMP
         USING DYNAM31,R2
         TM    DYNAM24P,X'80'          PLACE-HOLDER FREED ALREADY?
         BNO   DBGAID2               NO, JUMP
         SLR   R15,R15                 RC=0
         BR    R14                     CONTINUE WITH ABEND
DBGAID2 EQU  *
         OI    DYNAM24P,X'80'          PLACE-HOLDER HAS BEEN FREED
         L     R1,DYNAM24P             ADDR OF MY 24-BIT WORK AREA
         LA    R1,DYNAM24H-DYNAM24(,R1) ADDR OF PLACE-HOLDER
         FREEMAIN RC,LV=L'DYNAM24H,A=(1) FREE PLACE HOLDER
         DROP  R15                     LOCAL BASE
         LA    R13,SAVER012            SAVE AREA
         LA    R0,12                   RESTORE R0 CONTENTS
         DROP  R2                      DYNAM31
*
DBGAID4 BALR R15,0                   SET BASE REG FOR SAVE MACRO
         SAVE  (14,12),,'&HDR ESTAE &SYSDATC &SYSTIME'
         LR    R11,R15                 LOCAL BASE
         USING DBGAID4+2,R11
         LR    R8,R1                   SDWA ADDRESS
         USING SDWA,R8
         L     R9,PSATOLD-PSA          MY TCB
         USING TCB,R9
*
*        IF I WAS INVOKED WITHOUT AN SDWA, COPY RTM2'S SDWA
*        INTO A WORK AREA
*
         CH    R0,H12                  DID WE GET AN SDWA FROM RTM2?
         BNE   DBGAID8               YES, JUMP
         USING DYNAM31,R2
         L     R3,TCBRTWA              RTM2WA
         USING RTM2WA,R3
         L     R14,RTM2SDW2            RTM2'S SDWA
         LH    R15,SDWALNTH+1-SDWA(,R14) LENGTH OF THE SDWA
         LA    R0,D31SDWA              MY OWN SDWA
         LA    R1,L'D31SDWA            MY OWN SDWA
         MVCL  R0,R14                  MOVE RTM2 SDWA TO MY WORK AREA
         LA    R8,D31SDWA              MY OWN SDWA
         ST    R2,SDWAPARM             A(DYNAM31)
         OI    SDWAPARM,X'80'          PREVENT FREEMAIN
         MVC   SDWAEC1,RTM2EPSW        ERROR PSW
         MVC   SDWAAEC1,RTM2AEC1       ADDITIONAL ERROR INFORMATION
         MVC   SDWAEC2,RTM2EPSW        ERROR PSW
         MVC   SDWAABCC,RTM2CODE       COMPLETION CODE
         MVC   SDWAGRSV,RTM2EREG       REGISTERS AT TIME OF ABEND
         MVC   SDWAFLGS,RTM2FLGS       PASS FLAGS
         LA    R14,D31SDWAP            FAKE POINTER AREA        GP03014
         DROP  R2,R3                   DYNAM31,RTM2WA           GP09255
DBGAID8 EQU  *
*
         CLC   =X'222000',SDWACMPC     CANCEL COMMAND?
         BE    GOBACK8                 YES, QUIT
*
         CLI   SDWARCDE,0              RETURN CODE SET BY PREVIOUS     +
                                       RECOVERY ROUTINE?
         BNE   GOBACK9                 YES, QUIT
*
*        INACTIVATE ANY ESPIE ENVIRONMENT CREATED EARLIER
*        THAT COULD INTERFERE WITH THE TRAPPING OF MY
*        OWN ADDRESSING AND PROTECTION EXCEPTIONS.
*
         ICM   R0,B'1111',TCBPIE       ANY ESPIE CREATED EARLIER?
         BZ    ESPIE12                 NO, EXIT
*---------------------------------------------------------------------*
*   THE ORIGINAL TESTAUTH FAILS FOR EXHIBIT, WHICH RUNS IN KEY 0 AT   *
*     LEAST SOME OF THE TIME. CHECK SUPERVISOR MODE ONLY.             *
*---------------------------------------------------------------------*
*FAILS*  TESTAUTH KEY=YES,STATE=YES,RBLEVEL=1    CHECK PSW KEY  GP04047
         TESTAUTH STATE=YES,RBLEVEL=1  CHECK SUP. MODE ONLY     GP04047
         LTR   R15,R15                 RUNNING IN MODE=SUP OR KEY 0-7 ?
         BZ    ESPIE12                 YES, EXIT
         ESPIE SET,MF=(E,ESPIE11L)     OVERRIDE EXISTING PICA
         B     ESPIE12
ESPIE11L ESPIE SET,ESPIE11X,MF=L       INTERRUPT MASK ALL ZERO
ESPIE11X LR    R0,R1                   SAVE ADDR OF EPIE
         ICM   R1,B'1111',ESPIE11Z-ESPIE11X(R15)   GET COMPLETION CODE
         ABEND (1)                     ABEND0C0
ESPIE11Z DC    X'80,0C0000'            S0C0 COMPLETION CODE
ESPIE12  EQU   *
*
*        CHAIN SAVE AREAS
*        USE ESTAE TO TRAP S0C4 IF A(DYNAM31) IS INVALID
*
NEW#SAVE ESTAE ESPIEXIT,CT             RECOVERY ROUTINE
         L     R2,SDWAPARM             =A(DYNAM31)
         ST    R2,8(,R13)              BWD CHAIN
         ST    R13,4(,R2)              FWD CHAIN
         LR    R13,R2                  A(DYNAM31)
         USING DYNAM31,R13
         ESTAE 0                       PARM LOOKS GOOD, RESET MY ESPIE
*
         MVC   ENQLIST(ENQLISTL),ENQMODEL ENQ/DEQ LIST
         ENQ   MF=(E,ENQLIST)          PREPARE TO UPDATE WORK AREA
*
DUMPOPT  TM    SDWACMPF,SDWAREQ        DUMP REQUESTED?
         BNO   ESTAE2                  NO, JUMP
         OI    SDWADPFS,SDWADLST       DUMP OPTIONS VALID
         MVC   SDWADDAT,=X'0F00,AF00'  MOVE SDATA/PDATA
         TM    SDWAMWP1,SDWAPGM1       ABEND IN SUPVR MODE?
         BO    ESTAE2                  NO, JUMP
         OI    SDWASDA0,SDWALSQA       YES, DUMP LSQA TOO
*
ESTAE2   ESTAE RETRY00,                RECOVERY ROUTINE                X
               CT,                     CREATE                          X
               PARAM=DYNAM31,          PARAM FOR RECOVERY ROUTINE      X
               MF=(E,ESTAEL)
*
SETK7    TESTAUTH KEY=YES,STATE=YES,RBLEVEL=1    CHECK PSW KEY
         LTR   R15,R15                 CALLER IN SYSTEM KEY?
         BNZ   FREEPH24                NO, JUMP
         MODESET KEY=NZERO,MODE=SUP    YES, SWITCH TO A SAFE KEY
*
*        FREE AMODE24 PLACE-HOLDER IF WE'RE LOW ON MEMORY
*
FREEPH24 L     R12,DYNAM24P            ADDR OF MY 24-BIT WORK AREA
         USING DYNAM24,R12
         AIF   (NOT &MVSXA).NOLOAL                              GP09260
         TM    DYNAM24P,X'80'          PLACE-HOLDER FREED ALREADY?
         BO    OPENDCB                 YES, DO NOT DO IT AGAIN
         L     R1,PSAAOLD-PSA(0,0)     ASCB
         L     R1,ASCBLDA-ASCB(,R1)    LOCAL DATA AREA
         USING LDA,R1
         L     R0,LDALIMIT             LIMIT
         S     R0,LDALOAL              SIZE OF REMAINING MEMORY BELOW
         BM    OPENDCB                 NEGATIVE, EXIT
         DROP  R1                      WAS LDA
         CH    R0,=H'12288'            AT LEAST 12K AVAILABLE?
         BH    OPENDCB                 YES, KEEP PLACE-HOLDER
         LA    R1,DYNAM24H             PLACE-HOLDER (LAST 3 PAGES)
         FREEMAIN RC,LV=L'DYNAM24H,A=(1)   FREE PLACE HOLDER
         OI    DYNAM24P,X'80'          PLACE-HOLDER HAS BEEN FREED
.NOLOAL  ANOP  ,                                                GP09260
*        OPEN THE SYSDEBUG DCB
*
OPENDCB  EQU   *
         MVC   DCBDEBUG(DCBLNGQS),MODELDCB   MOVE MODEL DCB
         LA    R0,DCBDEBUG             OPEN LIST
         ST    R0,OPENLIST             OPEN LIST
         MVI   OPENLIST,X'8F'          OPEN LIST
         OPEN  MF=(E,OPENLIST)         OPEN MY DCB
*
         L     R0,=X'00FFFFFF'         R0= AM24 MASK            GP09260
         ST    R0,MASKADDR             X'FFFFFFFF'
*
         MVI   TRTPRINT,C' '           PRINTABLE CHARACTERS
         MVC   TRTPRINT+1(255),TRTPRINT
         MVC   BLANKS,TRTPRINT         KEEP BLANKS
         SLR   R15,R15
         SLR   R0,R0
         BAL   R1,TRTPRT2
         DC    X'4A,7,5A,8,6A,6,7A,6'  SPECIAL CHARACTERS
         DC    X'81,9,91,9,A2,8'       LOWERCASE
         DC    X'C1,9,D1,9,E2,8'       UPPERCASE
         DC    X'F0,A'                 DIGITS
         DC    X'00,0'                 END OF TABLE
*LOOP
TRTPRT2  IC    R15,0(,R1)              FIRST BYTE
         IC    R0,1(,R1)               ITERATIONS
*--LOOP
TRTPRT3  STC   R15,TRTPRINT(R15)       STORE X'4A' INTO TRTPRINT+X'4A'
         LA    R15,1(,R15)             BUMP INDEX
         BCT   R0,TRTPRT3              NEXT CHARACTER
*--ENDLOOP
         LA    R1,2(,R1)               NEXT ENTRY IN TABLE
         CLI   0(R1),0                 END OF TABLE?
         BNE   TRTPRT2                 NEXT STRING
*ENDLOOP
*
         BAL   R14,DIAG000        <== SET TITLE, DIAGNOSE
         BAL   R14,PSW00          <== PRINT PSW, GPRS
         BAL   R14,JOBSTEP        <== PRINT JOB/STEP DATA
         CAL#PROC PRB00           <== PRINT RB CHAIN
         CAL#PROC PGM00           <== PRINT JPAQ AND LOAD LIST
         CAL#PROC COMREG00        <== PRINT COM_REG AREA
         CAL#PROC LINK00          <== PRINT SAVE AREA TRACE
         CAL#PROC TIOT00          <== PRINT DATA SET ALLOCATIONS
         CAL#PROC DEB000          <== PRINT OPEN DATA SETS
*TEST*   CAL#PROC PRWA00          <== PRINT SDWA
         CAL#PROC SPACE1                                        GP09260
         STRING '    *** END OF SYSDEBUG ***',INTO=LINE121      GP09260
         CAL#PROC SPACE1                                        GP09260
         TESTAUTH KEY=YES,STATE=YES,RBLEVEL=1    CHECK PSW KEY  GP04047
         LTR   R15,R15                 CALLER IN SYSTEM KEY?    GP04047
         BNZ   CLOSENZK                NO, JUMP                 GP04047
         MODESET KEY=NZERO,MODE=SUP    USE SAME KEY FOR CLOSE   GP04047
CLOSENZK CLOSE MF=(E,OPENLIST)         CLOSE DEBUG DD           GP04047
         MVI   DCBBUFNO-IHADCB+DCBDEBUG,00
         FREEPOOL DCBDEBUG
         TESTAUTH KEY=YES,STATE=YES,RBLEVEL=1    CHECK PSW KEY  GP04047
         LTR   R15,R15                 CALLER IN SYSTEM KEY?    GP04047
         BNZ   CLOSENZM                NO, JUMP                 GP04047
         MODESET KEY=ZERO,MODE=PROB    BUT DON'T BOMB ON ESTAE  GP04047
CLOSENZM ESTAE 0                       DELETE RECOVERY ROUTINE  GP04047
*
*----------------------------------------------------------------------
*
*        GOBACK TO RTM
*
*              FREE WORKING-STORAGE (UNLESS HI-ORDER BIT IS ON)
*
*----------------------------------------------------------------------
*
GOBACK   L     R2,DYN#TCB              GETMAIN TCB
         LR    R3,R13                  A(DYNAM31)
         DEQ   MF=(E,ENQLIST)          FREE WORK AREA
         L     R13,4(,R13)             CALLER'S SAVE AREA
         TM    SDWAPARM,X'80'          HI-ORDER BIT ON?
         BO    GOBACK7                 YES, SKIP FREEMAIN
         CL    R2,PSATOLD-PSA          IS THIS THE RIGHT TCB?
         BNE   GOBACK7                 NO, SKIP FREEMAIN
         LA    R0,DYNAM31L-1           LENGTH OF DYNAMIC STORAGE AREA
         FREEMAIN RU,LV=(0),A=(R3)     FREE DYNAMIC STORAGE
*
GOBACK7  TESTAUTH KEY=YES,STATE=YES,RBLEVEL=1    CHECK PSW KEY
         LTR   R15,R15                 CALLER IN SYSTEM KEY?
         BNZ   GOBACK8                 NO, JUMP
         MODESET KEY=ZERO,MODE=PROB    YES, SWITCH TO KEY=0
*
GOBACK8  SETRP WKAREA=(R8),            SDWA ADDRESS                    X
               RC=00                   CONTINUE WITH ABEND
GOBACK9  RETURN (14,12)                GO BACK TO R/TM
H12      DC    H'12'                   NO SDWA ON ENTRY TO DBGAID
*
*----------------------------------------------------------------------
*
*        RETRY ROUTINE (FOR ESPIE)
*
*----------------------------------------------------------------------
*
ESPIEXIT SAVE  (14,12),,*              SAVE REGISTERS
         BALR  R2,0                    LOCAL BASE
         USING *,R2
*WTO 'ESPIEXIT ENTERED'
         SETRP RETADDR=ESPIEBAD,RC=4,  <== RETRY                       X
               FRESDWA=YES,RETREGS=YES,                                X
               DUMP=NO,                SUPPRESS DUMP                   X
               REGS=(14,12)            RESTORE REGISTERS
         DROP  R2
*
ESPIEBAD ESTAE 0                       RESET MY ESPIE
         SETRP WKAREA=(R8),            SDWA ADDRESS                    X
               REGS=(14,12),           RESTORE REGISTERS               X
               RC=00                   CONTINUE WITH ABEND
*
MODELDCB DCB   DSORG=PS,MACRF=PM,DDNAME=SYSDEBUG,                      X
               RECFM=FBA,LRECL=121,BUFNO=1
ENQMODEL ENQ   (ENQNAMES,ENQNAMES,E,,STEP),MF=L
ENQNAMES DC    CL8'SYSDEBUG'           QNAME/RNAME
*----------------------------------------------------------------------
*
*----------------------------------------------------------------------
*        RECOVERY ROUTINE FOR TRAPPING MY OWN ABENDS
*----------------------------------------------------------------------
*
         PUSH  USING                                            GP09256
         DROP  ,                                                GP09256
         CNOP  0,8
         DC    C'RETRY00 '             EYE CATCHER
RETRY00  LA    R15,012                 R0 VALUE FOR 'NO SDWA ALLOCATED'
         CLR   R0,R15                  SDWA ALLOCATED?
         BALR  R15,0                   LOCAL BASE
         BNE   RETRY04-*(,R15)         YES, JUMP
         SR    R15,R15                 SET RC=0 (IF R0=12)
         BR    R14                     RETURN TO EXIT PROLOG
*
RETRY04  BALR  R15,0                   LOCAL BASE
         SAVE  (14,12),,RETRY04        SAVE REGS
         BALR  R7,0
         USING *,R7
RETRY11  LR    R8,R1                   SAVE ADDR OF SDWA
         USING SDWA,R8
         L     R4,SDWAPARM             =A(DYNAM31)
         USING DYNAM31,R4
         CLI   #RETRY,0                ANY RETRY ADDRESS?
         BE    RETRY70                 NO, JUMP
*
RETRY40  CLC   =X'0C4000',SDWACMPC     S0C4 ABEND?
         BNE   RETRY70                 NO, IGNORE
         CLM   R7,7,SDWANXT1+1         RETRY11 > ABEND ?        GP09256
         BH    RETRY70                 YES, IGNORE
         SLR   R10,R10
         ICM   R10,B'0011',#RETRY      GET 16-BIT OFFSET
         ALR   R10,R7                  CHANGE OFFSET TO ADDRESS
         CLM   R10,7,SDWANXT1+1        RETRY < ABEND ?          GP09256
         BL    RETRY70                 YES, IGNORE
         NI    SDWACMPF,255-SDWAREQ    NO DUMP
         B     RETRY77
*
RETRY70  LA    R10,RETRY99             RETRY ADDRESS
         WTO   '==> DEBUGGING ROUTINE ABENDED <==',ROUTCDE=11
         MVC   SDWAGR13,DYNAM31+4      RTM SAVE AREA
         MVI   #RETRY,0                RESET RETRY ADDRESS      GP09256
* PREVENT LOOPING WHILE TESTING MVS VERSION - NO RETRY   G. POSTPISCHIL
         ESTAE 0,OV          CANCEL CURRENT ESTAE               GP09266
         SETRP WKAREA=(R8),RC=0           <== NO RETRY          GP09266
*
RETRY77  MVC   SDWASRSV,SDWAGRSV       MOVE REGISTERS
         MVI   #RETRY,0                RESET RETRY ADDRESS
         SETRP WKAREA=(R8),                                            X
               RETADDR=(R10),RC=4,    <== RETRY                        X
               FRESDWA=YES,RETREGS=YES
RETRY99  RETURN (14,12)                GOBACK TO RTM
         POP   USING                                            GP09256
*
*----------------------------------------------------------------------
*
*        ABEND-SPECIFIC DIAGNOSTIC INFORMATION
*
*              FORMAT AND DISPLAY ABEND-CODE
*              LOCATE LAST PRB, FIRST SVRB
*              DETERMINE IF THIS IS AN OPEN/CLOSE/EOV (OCE) ABEND
*              INVOKE THE CORRESPONDING DIAGNOSIS ROUTINE
*
*----------------------------------------------------------------------
*
DIAG000  BEG#PROC
         L     R1,CVTPTR     GET CVT BACK                       GP09255
         L     R1,CVTGDA-CVTMAP(,R1)   GET GDA                  GP09255
         LM    R0,R1,PASTRT-GDA(R1)    PRIVATE START/SIZE       GP09255
.CMCBRD  ALR   R1,R0                   END                      GP09255
         STM   R0,R1,REGION24          START/END OF MY REGION
         MVC   CURR#R13,SDWAGR13       KEEP ADDR OF CURRENT SAVE AREA
         MVI   MASKADDR,0              AM24 ADDRESS CLEAN-UP    GP09260
         NC    CURR#R13,MASKADDR       CLEAN-UP
*
*        BUILD A FAKE PSW2 IF PSW1=PSW2 OR PSW2=ZERO            @910624
*
         MVC   ORIGIN2,BLANKS          ORIGIN OF PSW2 IS SDWAEC2
         LM    R0,R1,SDWAEC2           PICK UP PSW2
         ALR   R0,R1                   PSW2=ZERO?
         BZ    DIAG005                 YES, JUMP
         CLC   SDWAEC1,SDWAEC2         PSW1=PSW2?
         BNE   DIAG010                 NO, QUIT
DIAG005 #S0C4  DIAG010
         L     R1,SDWAGR13             VALIDATE CALLER'S R13
         L     R1,4(,R1)               CALLER'S SAVE AREA
         CLC   0(72,R1),0(R1)          VALIDATE CALLER'S SAVE AREA
        #S0C4  RESET
         CLM   R1,7,TCBFSA+1           IS THIS THE FIRST SAVE AREA?
         BE    DIAG010                 YES, JUMP
         MVC   ORIGIN2,=C'  (HSA)'     ORIGIN OF PSW2 IS HIGH-SA
         MVC   SDWASR00(4*13),20(R1)   MOVE R0-R12
         ST    R1,SDWASR13             STORE R13
         MVC   SDWASR14(4*2),12(R1)    MOVE R14-R15
         MVC   SDWANXT2,12(R1)         USE R14 AS NEXT ADDRESS
         XC    SDWAAEC2(4),SDWAAEC2    ZERO ILC, INTC
*
*        BUILD RB CHAIN IN REVERSE ORDER
*
DIAG010  L     R6,TCBRBP               TOP OF RB CHAIN (MOST RECENT RB)
         LR    R0,R6                   POINT TO BASIC SECTION
         SH    R0,=AL2(RBBASIC-RBPREFIX) POINT TO RBPREFIX      GP09255
         ST    R0,MY#PRB               MY RBPREFIX
         SLR   R6,R6                   FIRST TIME SWITCH FOR NEXTRB
*
*        GET ADDR OF LAST PRB, FIRST SVRB, ABEND SVRB.
*
*LOOP
DIAG031  CAL#PROC NEXTRB               GET RB ADDRESS           GP09260
         LTR   R6,R6                   EOF?
         BZ    DIAG035                 YES, EXIT
         CL    R6,MY#PRB               END OF CHAIN?
         BE    DIAG035                 YES, EXIT
         USING RBPREFIX,R6
         CLI   RBSTAB1,RBFTPRB         IS THIS A PRB?
         BNE   DIAG031S                NO, JUMP
         ICM   R0,B'1111',FRSTSVRB     SET ALREADY?
         BNZ   DIAG031S                YES, JUMP
         ST    R6,LASTPRB              SAVE ADDR OF LAST PRB
*
DIAG031S TM    RBSTAB1,RBFTSVRB        IS THIS AN SVRB?
         BZ    DIAG031N                NO, JUMP
         ICM   R0,B'1111',FRSTSVRB     SET ALREADY?
         BNZ   DIAG031A                YES, JUMP
         ST    R6,FRSTSVRB             SAVE ADDR OF FIRST SVRB
*LOOP
DIAG031A TM    RBFLAGS1,RBABEND        IS THIS THE ABEND SVRB?
         BZ    DIAG031N                NO, JUMP
         ICM   R0,B'1111',ABNDSVRB     SET ALREADY?
         BNZ   DIAG031N                YES, JUMP
         ST    R6,ABNDSVRB             SAVE ADDR OF ABEND SVRB
*
DIAG031N B     DIAG031                 NEXT ENTRY IN RB STACK
*ENDLOOP
DIAG035  L     R6,LASTPRB              LAST PRB
         MVI   OCE#CODE,C'O'           ABEND DURING OPEN
         CLI   RBWLIC+3,19             O/C/E SVC?
         BE    DIAG040                 YES, JUMP
         CLI   RBWLIC+3,22             O/C/E SVC?
         BE    DIAG040                 YES, JUMP
         MVI   OCE#CODE,C'C'           ABEND DURING CLOSE
         CLI   RBWLIC+3,20             O/C/E SVC?
         BE    DIAG040                 YES, JUMP
         MVI   OCE#CODE,C'E'           ABEND DURING EOV
         CLI   RBWLIC+3,55             O/C/E SVC?
         BE    DIAG040                 YES, JUMP
         MVI   OCE#CODE,00
         DROP  R6                      RBPREFIX
*
DIAG040  STRING 'ABEND U',(SDWACMPC+1,H,R4Z),INTO=ABCODE ABEND=U0046
         CLC   =X'000F',SDWACMPC       SYSTEM CODE ALL ZEROES?
         BNL   DIAG044                 YES, JUMP
         STRING (SDWACMPC,,X),INTO=WKCELL1
         STRING 'ABEND S',(WKCELL1,3),'-',(SDWAGR15+3,1,X),            X
               INTO=ABCODE             ABEND=SD37-04
         TM    SDWAERRA,SDWASVCD       TASK ISSUED SVC 13?
         BNO   DIAG042                 NO, JUMP
*DEFER*  TM    SDWACMPF,SDWARCF        REASON-CODE SPECIFIED?
*DEFER*  BO    DIAG044                 YES, JUMP
         ICM   R15,B'1111',SDWAGR15    R15=00?
         BZ    DIAG042                 YES, JUMP
         ICM   R15,B'1110',SDWAGR15    R15>FF?
         BZ    DIAG044                 NO, JUMP
DIAG042  MVC   ABCODE+10(3),BLANKS     GET RID OF '-04'
*
DIAG044  L     R15,TCBTIO              MY TIOT
         LA    R1,MSG60
         MVC   MSG60,BLANKS
         MVC   0(8,R1),0(R15)          JOBNAME
         CLI   16(R15),C' '
         BNH   DIAG046
         LA    R1,1(,R1)
         CLI   0(R1),C' '
         BNE   *-8
         MVI   0(R1),C'.'
         MVC   1(8,R1),16(R15)         PROCSTEP
DIAG046  LA    R1,1(,R1)
         CLI   0(R1),C' '
         BNE   *-8
         MVI   0(R1),C'.'
         MVC   1(8,R1),8(R15)          STEPNAME
         TIME  DEC
         STM   R0,R1,WKCELL1           TIME, DATE
*
DIAG050  L     R1,CVTPTR               CVT ADDRESS
         USING CVTMAP,R1
DIAG055  SH    R1,=AL2(CVTMAP-CVTFIX)    POINT TO CVT PREFIX    GP09255
         USING CVTFIX,R1
         STRING '1&HDR - ',ABCODE,5X,(MSG60,,L),                       X
               2X,(WKCELL1+4,P,YYYY-MM-DD),            YYYY-MM-DD      X
               ' (',(WKCELL1+4+1,1,X),'.',(WKCELL1+4+2,P,R3Z), YY.DDD  X
               ') ',(WKCELL1,1,X),'.',(WKCELL1+1,1,X),                 X
               '.',(WKCELL1+2,1,X),                                    X
               2X,(CVTMDL,,X),2X,CVTPRODN,                             X
               INTO=TITLE121
         MVC   LINE121,TITLE121        TITLE LINE
         MVI   RLINES,0                RESET LINE COUNTER
         CAL#PROC SPACE1          <== PRINT LINE
         DROP  R1                      CVTFIX
*
*        INVOKE ABEND-SPECIFIC DIAG ROUTINE
*
DIAG060  LA    R1,DIAG094              INDEX
         LA    R14,0008                TABLE ENTRY
         LA    R15,DIAG095             LIMIT FOR BXLE
*LOOP
DIAG062  CLC   ABCODE+6(4),4(R1)       COMPARE ABEND CODE
         BE    DIAG066                 FOUND, JUMP
         BXLE  R1,R14,DIAG062
*ENDLOOP
         LA    R15,S0C0                PROGRAM CHECK RTNE
         TM    SDWAERRA,SDWAPCHK       IS THIS A PROGRAM CHECK?
         BO    DIAG067                 YES, JUMP
         LA    R15,S013                O/C/E ABEND RTNE
         CLI   OCE#CODE,0              IS THIS AN OPEN/CLOSE/EOV ABEND?
         BNE   DIAG067                 YES, JUMP
         B     DIAG999                 NO DIAG FOR THIS ABEND
*
DIAG066  L     R15,000(,R1)            ADDR OF DIAG RTNE
DIAG067  BALR  R14,R15             <== CALL THE ROUTINE
         B     DIAG999                 NO DIAG FOR THIS ABEND
*
DIAG094  DC    A(S001),C'S001'         SAM
DIAG095  DC    A(S001),C'S002'         SAM
*
DIAG999  END#PROC
*
*----------------------------------------------------------------------
*        DIAGNOSTIC FOR S001 & S002 ABENDS
*----------------------------------------------------------------------
*
S001     BEG#PROC
         CLI   OCE#CODE,C'E'           EOV SVC?
         BNE   S001ZZ                  NO, QUIT
         CAL#PROC BLANK1              BLANK LINE
         L     R1,FRSTSVRB             LOAD ADDR OF FIRST SVRB
         SLR   R5,R5                   NO DEB
         L     R6,RBGRS1-RBPREFIX(,R1) R6 POINTS TO DCB
         ICM   R6,B'1000',X00          ZERO HI-ORDER BYTE OF DCB ADDR
         CAL#PROC SWA000          <== LOCATE DEB/DSAB/SIOT/JFCB/TIOT
         CAL#PROC VALDCB          <== VALIDATE DCB
         LTR   R6,R6                   GOOD DCB?
         BZ    S001ZZ                  NO, QUIT
         STRING (ABCODE,,T),' OCCURRED WHILE PROCESSING DDN=',         X
               DDNAME2,INTO=LINE
         CAL#PROC SPACE1          <== PRINT LINE
         CAL#PROC PRDCB           <== PRINT DCB
**      $PRTDSN DDNAME,INTO=LINE       PRINT ALLOCATION
S001ZZ   END#PROC
*
*----------------------------------------------------------------------
*        DIAGNOSTIC FOR O/C/E ABENDS
*----------------------------------------------------------------------
*
S013     BEG#PROC
        #S0C4  S013ZZ
         L     R6,SDWAGR02             R6 POINTS TO DCB
         USING IHADCB,R6
         L     R7,SDWAGR04             R7 POINTS TO O/C/E WORK AREA
         L     R1,FRSTSVRB             POINT TO 1ST SVRB        @921013
         CLI   RBWLIC-RBPREFIX(R1),0   THIS RB WAITING?         @921013
         BE    S013B                   NO, JUMP                 @921013
         L     R6,RBEXSAVE+00-RBPREFIX(,R1)  GET R2 VALUE       @921013
         L     R7,RBEXSAVE+08-RBPREFIX(,R1)  GET R4 VALUE       @921013
S013B    SLR   R5,R5                   NO DEB
         CAL#PROC SWA000          <== LOCATE DEB/DSAB/SIOT/JFCB/TIOT
         CAL#PROC VALDCB          <== VALIDATE DCB
         LTR   R6,R6                   GOOD DCB?
         BZ    S013ZZ                  NO, QUIT
         CAL#PROC BLANK1              BLANK LINE
         STRING (ABCODE,,T),' OCCURRED WHILE PROCESSING DDN=',         X
               DDNAME2,INTO=LINE
         CAL#PROC SPACE1          <== PRINT MESSAGE
         CAL#PROC PRDCB           <== PRINT DCB
        #S0C4  S013ZZ
         CLI   0(R7),0                 ANY TAPE LABEL?
         BE    S013ZZ                  NO, QUIT
         CLC   0(256,R7),0             VALIDATE O/C/E WORK AREA
        #S0C4  RESET
         STRING '0   O/C/E WORK AREA:',INTO=LINE121
         CAL#PROC SPACE1          <== PRINT LINE
         LA    R0,X'298'               LENGTH OF O/C/E WORK AREA
         LR    R1,R7                   PASS ADDRESS
         CAL#PROC PDUMP           <== DUMP IT
S013ZZ   END#PROC
         DROP  R6                      WAS IHADCB
*
*----------------------------------------------------------------------
*        DIAGNOSTIC FOR S0CX (PROGRAM CHECK) ABENDS
*----------------------------------------------------------------------
*
S0C0     BEG#PROC
         CAL#PROC BLANK1              BLANK LINE
         L     R6,SDWANXT1             NEXT INSTRUCTION ADDRESS
         N     R6,MASKADDR             CLEAN-UP ADDRESS
         SR    R1,R1
         IC    R1,SDWAILC1             INSTR LEN CODE
         CLI   SDWAICD1,X'10'          SEGMENT TRANSLATION EXCEPTION?
         BE    S0C0F                   YES, JUMP
         CLI   SDWAICD1,X'11'          PAGE TRANSLATION EXCEPTION?
         BE    S0C0F                   YES, JUMP
         SR    R6,R1                   POINT TO INSTRUCTION
*
S0C0F    LR    R1,R6                   INSTRUCTION ADDR
         CAL#PROC CSVQRY00        <== FIND PROGRAM NAME
*
         IC    R0,SDWAICD1             EXCEPTION CODE IN R0
         L     R1,=A(PCKTABLE)         START OF PROGRAM-CHECK TABLE
         CAL#PROC SCANTBL             FIND EXCEPTION NAME
         BE    S0C0P                   FOUND, JUMP
         LA    R1,=C'..UNKNOWN'        FAKE IT
         LA    R2,7                    FAKE IT
S0C0P    STRING 2X,(2(R1),(R2)),' EXCEPTION OCCURRED AT LOCATION ',    X
               ((R6),,X),2X,MSG20,INTO=LINE
         CAL#PROC SPACE1          <== PRINT MESSAGE
         CAL#PROC PRINTI          <== PRINT INSTRUCTION
         STRING '  INSTRUCTION IS ',(MSG20,(R1)),INTO=LINE
         CAL#PROC SPACE1          <== PRINT LINE
         CAL#PROC EXECUTE         <== PRINT EXECUTED INSTRUCTION
*
         CLI   SDWAICD1,X'10'          SEGMENT TRANSLATION EXCEPTION?
         BE    S0C5                    YES, JUMP
         CLI   SDWAICD1,X'11'          PAGE TRANSLATION EXCEPTION?
         BNE   S0C0ZZ                  NO, EXIT
S0C5     STRING '  ADDRESS CAUSING EXCEPTION: ',                       X
               (SDWATRAN,,X),INTO=LINE
         CAL#PROC SPACE1          <== PRINT LINE
S0C0ZZ   END#PROC
*
*----------------------------------------------------------------------
*        PRINT DATA SET ALLOCATIONS
*----------------------------------------------------------------------
*
         PUSH  USING                                            GP09260
         DROP  R9                 OLD TCB USING                 GP09260
TIOT00   BEG#PROC
         CAL#PROC BLANK1          <== BLANK LINE
*DEFER*  L     R1,CVTPTR               CVT ADDRESS
*DEFER*  L     R1,CVTLINK-CVTMAP(,R1)  LINKLIST DCB
*DEFER*  LOAD  EP=IEFEB4UV,DCB=(1)
*DEFER*  ST    R0,IEFEB4UV
         L     R4,PSATOLD-PSA          MY TCB
         USING TCB,R4
*
TIOT20   L     R5,TCBTIO               TIOT
         USING TIOT1,R5
***      L     R6,PSAAOLD-PSA          MY ASCB
***      L     R6,ASCBOUCB-ASCB(,R6)   MY OUCB
***      USING OUCB,R6
         MVI   MINLINES,8
***      STRING '-  JOBNAME: ',TIOCNJOB,'  STEP: ',(TIOCSTEP+0,8),     X
               '  PROCSTEP: ',(TIOCSTEP+8,8),                          X
               '  PERFORM=',(OUCBNPG,FL1,L),INTO=LINE121        GP09255
         STRING '  DDNAME  DISP VOLSER   EXCP DATA SET NAME',32X,      X
               'DEVICE   DEVN  MISCELLANEOUS',INTO=LINE
         CAL#PROC SPACE1          <== PRINT LINE
         MVI   LINE,C'0'               CTLCHR
         L     R1,PSATOLD-PSA          MY TCB
         L     R1,TCBJSCB-TCB(,R1)     MY JSCB
         L     R1,JSCDSABQ-IEZJSCB(,R1)  DSAB QDB
         L     R5,DSQFRSTP-DSABQDB(,R1)  FIRST DSAB ON CHAIN
         USING DSAB,R5
*LOOP
TIOT#GET ICM   R6,B'1111',DSABSIOT     POINT TO SIOT
         BNZ   TIOT31
         ICM   R6,7,DSABSSVA           GET QUEUE ADDRESS        GP09260
         LA    R6,16(,R6)              SKIP QUEUE HEADER        GP09260
         USING INDMSIOT,R6
*
TIOT31   L     R7,SJFCBPTR             GET JFCB ADDRESS FROM SIOT
         USING INFMJFCB,R7
*
         MVC   MSG60,BLANKS            CLEAR WORK AREA
         MVC   STATUS,BLANKS           CLEAR WORK AREA
         MVC   UNITNAME,BLANKS         CLEAR WORK AREA
         MVC   VOLSER,BLANKS
         SLR   R1,R1                   EXCP=0
         SLR   R3,R3                   NO UCB
         MVC   VOLSER(4),SIOTSSNM      SYSIN/SYSOUT
         TM    JFCBTSDM,JFCSDS         IS THIS A JES DATA SET?
         BO    TIOT61                  YES, JUMP
         MVC   VOLSER,=C'DUMMY   '     DUMMY DATA SET
         TM    SCTSBYT1,SCTDUMMY       DD DUMMY?
         BO    TIOT62                  YES, JUMP
         ICM   R3,B'0111',SIOUCBAD     UCB ADDRESS
         USING UCBOB,R3
         MVC   UNITNAME+8(4),UCBTYP    DEVICE TYPE
         CAL#PROC GETUNIT         <== GET UNITNAME
         MVC   VOLSER,=C'VIO     '     UNIT=VIO
         TM    UCBJBNR,UCBVRDEV        IS THIS A VIO DATA SET?
         BO    TIOT41                  YES, JUMP
         TM    UCBTBYT3,UCB3DACC+UCB3TAPE  TAPE OR DISK?        GP09260
         BNM   TIOT41                  NEITHER; NO SERIAL       GP09260
         MVC   VOLSER,UCBVOLI          NO, MOVE FIRST VOLSER
         TR    VOLSER,TRTPRINT         GET RID OF GARBAGE
         CLC   VOLSER,UCBVOLI          IS THE VOLSER OK?
         BE    TIOT41                  YES, JUMP
         MVC   VOLSER,BLANKS           NO, BLANK IT OUT
*
TIOT41   BAS   R1,TIOT41IC
         DC    C'NEW',AL1(JFCNEW)
         DC    C'MOD',AL1(JFCMOD)
         DC    C'SHR',AL1(JFCOLD+JFCSHARE)
         DC    C'OLD',AL1(JFCOLD)
         DC    C'???',0H'0'
TIOT41TM TM    JFCBIND2,*-*            TEST LABEL TYPE FLAGS
*--LOOP
TIOT41IC IC    R15,3(,R1)              PICK UP MASK FOR "TM"
         EX    R15,TIOT41TM
         BO    TIOT41X
         LA    R1,3+1(,R1)             BUMP TABLE PTR
         CLI   0(R1),C'?'              END OF TABLE?
         BNE   TIOT41IC                NEXT TABLE ENTRY
*--ENDLOOP
TIOT41X  MVC   STATUS,0(R1)            DISP
*
TIOT43V  CLI   JFCDSORG+1,JFCORGAM     VSAM DATA SET?           GP09260
         BNE   TIOT46                  NO, JUMP
         CLI   JFCDSORG,JFCORGPS       VSAM DATA SET +ICF ?     GP09260
         BE    TIOT43W                 NO, JUMP                 GP09260
         STRING (MSG60,,T),',VSAM',INTO=MSG60
         B     TIOT61
TIOT43W  STRING (MSG60,,T),',ICF-Catalog',INTO=MSG60            GP09260
*
TIOT46   CLI   UCBTBYT3,UCB3TAPE       TAPE DEVICE?
         BE    TIOT46B                 YES, JUMP
         CLI   UCBTBYT3,UCB3DACC       DASD DEVICE?
         BNE   TIOT61                  NO, JUMP
*?       TM    JFCBLTYP,JFCSUL         LABEL=(,SUL) ?
*?       BNO   TIOT61                  NO, JUMP
TIOT46B  BAS   R1,TIOT46IC
         DC    C'LTM',AL1(JFCBLTM)
         DC    C'BLP',AL1(JFCBLP)
         DC    C'AUL',AL1(JFCSUL+JFCBAL)
         DC    C'SUL',AL1(JFCSUL)
         DC    C'AL ',AL1(JFCBAL)
         DC    C'NSL',AL1(JFCNSL)
         DC    C'SL ',AL1(JFCSL)
         DC    C'NL ',AL1(JFCNL)
         DC    C'???',0H'0'
TIOT46TM TM    JFCBLTYP,*-*            TEST LABEL TYPE FLAGS
*--LOOP
TIOT46IC IC    R15,3(,R1)              PICK UP MASK FOR "TM"
         EX    R15,TIOT46TM
         BO    TIOT46X
         LA    R1,3+1(,R1)             BUMP TABLE PTR
         CLI   0(R1),C'?'              END OF TABLE?
         BNE   TIOT46IC                NEXT TABLE ENTRY
*--ENDLOOP
TIOT46X  LH    R0,JFCBFLSQ             PICK UP FILE SEQUENCE NUMBER
         LTR   R0,R0                   LABEL=0?
         BNZ   TIOT46Y                 NO, JUMP
         LA    R0,0001                 YES, CHANGE TO LABEL=1
TIOT46Y  STRING ',',((R0),,L),',',((R1),3),INTO=MSG60
*
TIOT61   L     R0,DSABTIOT             POINT TO CURRENT TIOT ENTRY
         SL    R0,TCBTIO               CHANGE ADDRESS TO OFFSET
         CAL#PROC EXCP00          <== GET EXCP COUNT IN R1
*
TIOT62   STRING ((R1),,R6B),INTO=WKCELL2
         CL    R1,=F'100000'           OVER 100K?
         BL    TIOT63                  NO, JUMP
         SLR   R0,R0                   YES, DIVIDE BY 1000
         LA    R1,500(,R1)             YES, DIVIDE BY 1000
         D     R0,=F'1000'             YES, DIVIDE BY 1000
         STRING ((R1),,R5B),'K',INTO=WKCELL2
*
TIOT63   TM    JFCBTSDM,JFCSDS         IS THIS A JES DATA SET?
         BZ    TIOT64                  NO, JUMP
         CLI   SCTOUTPN,C' '           DO WE HAVE A CLASS?
         BE    TIOT64                  NO, JUMP
         STRING ',SYSOUT=',SCTOUTPN,INTO=MSG60
         ICM   R0,B'0111',JFCOUTLI     OUTLIM=0?
         BE    TIOT64                  YES, JUMP
         STRING (MSG60,,T),',OUTLIM=',(JFCOUTLI,FL3,L),INTO=MSG60
*
TIOT64   LTR   R3,R3                   DO WE HAVE A UCB?
         BNZ   TIOT64U                 YES, JUMP
         MVC   UCB#NAME,BLANKS
         LA    R3,BLANKS               NO, POINT TO A DUMMY UCB
         B     TIOT65
TIOT64U  MVC   UCB#NAME+1(3),UCBNAME                            GP09255
TIOT65   MVC   EYECATCH(L'JFCBDSNM),JFCBDSNM MOVE DATA SET NAME
         TM    JFCBIND1,JFCPDS         DO WE HAVE A MEMBER NAME?
         BZ    TIOT71                  NO, JUMP
         STRING (JFCBDSNM,,T),'(',(JFCBELNM,,T),')',INTO=EYECATCH
TIOT65L  CLI   EYECATCH+L'JFCBDSNM,C' ' OVERFLOW?
         BE    TIOT71                  NO, JUMP
         MVC   EYECATCH(60),EYECATCH+1 SHIFT DSN(MBR) TO THE LEFT
         B     TIOT65L                 TRY AGAIN
*
TIOT71   ICM   R0,B'0011',DSABOPCT     ANY DCB OPEN?
         BZ    TIOT#PRT                NO, JUMP
         STRING (MSG60,,T),',OPEN',INTO=MSG60
*
TIOT#PRT STRING 2X,SCTDDNAM,1X,STATUS,1X,VOLSER,1X,(WKCELL2,7),        X
               (EYECATCH,L'JFCBDSNM),1X,UNITNAME,1X,UCB#NAME,2X,       X
               (MSG60+1,L'MSG60-1),INTO=LINE
         CAL#PROC SPACE1          <== PRINT LINE
*
TIOT#NXT L     R5,DSABFCHN             NEXT DSAB                  -
         LTR   R5,R5                   END OF CHAIN?
         BNZ   TIOT#GET                NOT YET, LOOP THROUGH TIOT
*ENDLOOP
         DROP  R5,R6,R7,R3             DSAB, SIOT, JFCB, UCB
*DEFER*  DELETE EP=IEFEB4UV
TIOT90   END#PROC
         LTORG ,                                                GP09255
         POP   USING                                            GP09260
*
*----------------------------------------------------------------------
*
*        PRINT PSW, REGISTERS
*
*              SDWA DATA (PSW1/PSW2)
*              LAST PRB DATA (IF REQUIRED)
*
*----------------------------------------------------------------------
*
PSW00    BEG#PROC
         CAL#PROC BLANK1          <== BLANK LINE
         L     R1,SDWANXT1             NEXT INSTRUCTION
         CAL#PROC CSVQRY00        <== FIND CORRESPONDING CDE
PSW11    STRING 'PSW1: ',(SDWAEC1,4,X),1X,(SDWAEC1+4,4,X),2X,MSG20,    X
               '  ILC: ',(SDWAILC1,FL1,L),                             X
               '  INTC: ',(SDWAINC1,,X),INTO=LINE
         MVI   MINLINES,8
         CAL#PROC SPACE1          <== PRINT LINE
         L     R1,SDWANXT1             NEXT INSTRUCTION
         N     R1,MASKADDR             CLEAN UP ADDRESS
         SH    R1,=H'6'                6 BYTES BEFORE
         BNP   PSW15                   LOW CORE
        #S0C4  PSW15
         CLC   0(12,R1),0(R1)          VALIDATE 12 BYTES
        #S0C4  RESET
         STRING ' DATA AROUND PSW1 ADDR:  ',INTO=LINE,                 X
               (00(R1),2,X),1X,(02(R1),2,X),1X,(04(R1),2,X),1X,        X
               (06(R1),2,X),1X,(08(R1),2,X),1X,(10(R1),2,X),1X
         CAL#PROC SPACE1          <== PRINT LINE
*SW15    CAL#PROC EXECUTE         <== PROCESS 'EX' OPCODE (X'44')
PSW15    STRING '0   GPR00-03: ',(SDWAGR00,,X),2X,(SDWAGR01,,X),2X,    X
               (SDWAGR02,,X),2X,(SDWAGR03,,X),INTO=LINE121
         CAL#PROC SPACE1          <== PRINT LINE
         STRING '   GPR04-07: ',(SDWAGR04,,X),2X,(SDWAGR05,,X),2X,     X
               (SDWAGR06,,X),2X,(SDWAGR07,,X),INTO=LINE
         CAL#PROC SPACE1          <== PRINT LINE
         STRING '   GPR08-11: ',(SDWAGR08,,X),2X,(SDWAGR09,,X),2X,     X
               (SDWAGR10,,X),2X,(SDWAGR11,,X),INTO=LINE
         CAL#PROC SPACE1          <== PRINT LINE
         STRING '   GPR12-15: ',(SDWAGR12,,X),2X,(SDWAGR13,,X),2X,     X
               (SDWAGR14,,X),2X,(SDWAGR15,,X),INTO=LINE
         CAL#PROC SPACE1          <== PRINT LINE
         SPACE 1                                                GP03014
PSW20    CLC   SDWAEC1,SDWAEC2         PSW1=PSW2?
         BE    PSW99                   YES, DON'T BOTHER
         L     R1,SDWANXT2             NEXT INSTRUCTION
         LTR   R1,R1                   ANY DATA THERE?
         BZ    PSW31                   NONE, DON'T BOTHER       @911019
         CAL#PROC CSVQRY00        <== FIND CORRESPONDING CDE
         CAL#PROC BLANK1          <== BLANK LINE
         MVI   MINLINES,8
         STRING 'PSW2: ',(SDWAEC2,4,X),1X,(SDWAEC2+4,4,X),2X,MSG20,    X
               '  ILC: ',(SDWAILC2,FL1,L),                             X
               '  INTC: ',(SDWAINC2,,X),ORIGIN2,INTO=LINE
         CAL#PROC SPACE1          <== PRINT LINE
         L     R1,SDWANXT2             NEXT INSTRUCTION
         N     R1,MASKADDR             CLEAN UP ADDRESS
         SH    R1,=H'6'                6 BYTES BEFORE
        #S0C4  PSW25
         CLC   0(12,R1),0(R1)          VALIDATE 12 BYTES
        #S0C4  RESET
         STRING ' DATA AROUND PSW2 ADDR:  ',INTO=LINE,                 X
               (00(R1),2,X),1X,(02(R1),2,X),1X,(04(R1),2,X),1X,        X
               (06(R1),2,X),1X,(08(R1),2,X),1X,(10(R1),2,X),1X
         CAL#PROC SPACE1          <== PRINT LINE
PSW25    STRING '0   GPR00-03: ',(SDWASR00,,X),2X,(SDWASR01,,X),2X,    X
               (SDWASR02,,X),2X,(SDWASR03,,X),INTO=LINE121
         CAL#PROC SPACE1          <== PRINT LINE
         STRING '   GPR04-07: ',(SDWASR04,,X),2X,(SDWASR05,,X),2X,     X
               (SDWASR06,,X),2X,(SDWASR07,,X),INTO=LINE
         CAL#PROC SPACE1          <== PRINT LINE
         STRING '   GPR08-11: ',(SDWASR08,,X),2X,(SDWASR09,,X),2X,     X
               (SDWASR10,,X),2X,(SDWASR11,,X),INTO=LINE
         CAL#PROC SPACE1          <== PRINT LINE
         STRING '   GPR12-15: ',(SDWASR12,,X),2X,(SDWASR13,,X),2X,     X
               (SDWASR14,,X),2X,(SDWASR15,,X),INTO=LINE
         CAL#PROC SPACE1          <== PRINT LINE
         SPACE 1                                                GP03014
*        PRINT PSW AND REGS FOR LAST PRB IF DIFFERENT FROM PSW1/2
*
PSW31    L     R6,LASTPRB              LAST PRB
         USING RBPREFIX,R6
         CLC   SDWAEC1,RBOPSW          SAME AS PSW1 IN SDWA?
         BE    PSW99                   YES, JUMP
         CLC   SDWAEC2,RBOPSW          SAME AS PSW2 IN SDWA?
         BE    PSW99                   YES, JUMP
         L     R1,RBOPSW+4             NEXT INSTRUCTION
         CAL#PROC CSVQRY00        <== FIND CORRESPONDING CDE
         CAL#PROC BLANK1          <== BLANK LINE
         MVI   MINLINES,8
         STRING 'PSW3: ',(RBOPSW,4,X),1X,(RBOPSW+4,4,X),2X,MSG20,      X
               '  ILC: ',(RBWLIC+1,FL1,L),                             X
               '  INTC: ',(RBWLIC+2,2,X),                              X
               '  (LASTPRB)',INTO=LINE
         CAL#PROC SPACE1          <== PRINT LINE
         L     R1,RBOPSW+4             NEXT INSTRUCTION
         N     R1,MASKADDR             CLEAN UP ADDRESS
         SH    R1,=H'6'                6 BYTES BEFORE
        #S0C4  PSW35
         CLC   0(12,R1),0(R1)          VALIDATE 12 BYTES
        #S0C4  RESET
         STRING ' DATA AROUND PSW3 ADDR:  ',INTO=LINE,                 X
               (00(R1),2,X),1X,(02(R1),2,X),1X,(04(R1),2,X),1X,        X
               (06(R1),2,X),1X,(08(R1),2,X),1X,(10(R1),2,X),1X
         CAL#PROC SPACE1          <== PRINT LINE
PSW35    L     R6,FRSTSVRB             NEXT IRB/SVRB
         STRING '0   GPR00-03: ',(RBGRS0,,X),2X,(RBGRS1,,X),2X,        X
               (RBGRS2,,X),2X,(RBGRS3,,X),INTO=LINE121
         CAL#PROC SPACE1          <== PRINT LINE
         STRING '   GPR04-07: ',(RBGRS4,,X),2X,(RBGRS5,,X),2X,         X
               (RBGRS6,,X),2X,(RBGRS7,,X),INTO=LINE
         CAL#PROC SPACE1          <== PRINT LINE
         STRING '   GPR08-11: ',(RBGRS8,,X),2X,(RBGRS9,,X),2X,         X
               (RBGRS10,,X),2X,(RBGRS11,,X),INTO=LINE
         CAL#PROC SPACE1          <== PRINT LINE
         STRING '   GPR12-15: ',(RBGRS12,,X),2X,(RBGRS13,,X),2X,       X
               (RBGRS14,,X),2X,(RBGRS15,,X),INTO=LINE
         CAL#PROC SPACE1          <== PRINT LINE
         SPACE 1                                                GP03020
         DROP  R6                      PRB
PSW99    END#PROC
*
*----------------------------------------------------------------------
*        PRINT JOB/STEP INFORMATION
*----------------------------------------------------------------------
*
         PUSH  USING                                            GP09260
JOBSTEP  BEG#PROC
         CAL#PROC BLANK1              BLANK LINE
         STRING 'JOB/STEP INFORMATION',INTO=LINE
         CAL#PROC SPACE2              DOUBLE SPACE
*
         L     R4,TCBJSCB              JOB STEP CONTROL BLOCK
         USING IEZJSCB,R4
         L     R5,JSCBJCT              JOB CONTROL TABLE
         USING JCT,R5
         SLR   R7,R7
         ICM   R7,B'0111',JCTACTAD     SVA OF ACT
         USING IEFAACTB-16,R7
*
         STRING '  //',(JCTJNAME,,T),' JOB ',INTO=LINE
         MVI   WKCELL3,C' '            NO COMMA
         MVI   WKCELL3+1,C'('          BEGIN SUB-LIST
         SLR   R3,R3
         ICM   R3,B'0001',ACTJNFLD     # OF ACCT FIELDS
         BZ    JOBS120                 NO ACCOUNT INFORMATION
         CLC   =X'0100',ACTJNFLD       ONE NULL SUB-FIELD?
         BE    JOBS120                 YES, IGNORE
         LA    R1,ACTJNFLD+1           FIRST SUB-PARM
         SLR   R2,R2
*LOOP
JOBS114  ICM   R2,B'0001',0(R1)        SIZE OF SUB-PARM
         STRING (1(R1),(R2)),INTO=MSG60
         BAL#PROC JOBS155             APPEND NEW TEXT TO CURRENT LINE
         LA    R1,1(R2,R1)             SKIP CURRENT SUB-FIELD
         BCT   R3,JOBS114              NEXT SUB-FIELD
*ENDLOOP
         LA    R15,LINE(R15)           LAST POS IN LINE
         MVI   0(R15),C')'             CLOSE SUB-LIST
         B     JOBS121
*
JOBS120  MVI   WKCELL3+1,C','          ACCT FIELD MISSING
*
JOBS121  CLC   ACTPRGNM,BLANKS         DO WE HAVE A PROGRAMMER'S NAME ?
         BE    JOBS122                 NO, JUMP
         STRING (ACTPRGNM,,T),INTO=MSG60
         BAL#PROC JOBS155             APPEND NEW TEXT TO CURRENT LINE
*
JOBS122  CLI   JCTJCSMF,X'E0'          TSO USER?
         BE    JOBS125                 YES, JUMP
         CLI   JCTJCSMF,X'D0'          STARTED TASK?            GP09260
         BE    JOBS125                 YES, JUMP
         STRING 'CLASS=',JCTJCSMF,INTO=MSG60
         BAL#PROC JOBS155             APPEND NEW TEXT TO CURRENT LINE
*
JOBS125  STRING 'MSGCLASS=',JCTJMGPO,INTO=MSG60
         BAL#PROC JOBS155             APPEND NEW TEXT TO CURRENT LINE
*
JOBS130  STRING 'MSGLEVEL=(1,1)',INTO=MSG60     MSGLEVEL=(1,1)
         TM    JCTJMGLV,INCMMGL1       MSGLEVEL=1 ?
         BO    JOBS133                 YES, JUMP
         MVI   MSG60+10,C'2'           MSGLEVEL=(2,1)
         TM    JCTJMGLV,INCMMGL2       MSGLEVEL=2 ?
         BO    JOBS133                 YES, JUMP
         MVI   MSG60+10,C'0'           MSGLEVEL=(0,1)
JOBS133  TM    JCTJMGLV,INCMALL        MSGLEVEL=(,1) ?
         BO    JOBS134                 YES, JUMP
         MVI   MSG60+12,C'0'           MSGLEVEL=(,0)
JOBS134  BAL#PROC JOBS155             APPEND NEW TEXT TO CURRENT LINE
*
JOBS136  SLR   R6,R6
         SLR   R1,R1
         ICM   R1,B'0111',JCTJMRTL     JOB TIME LIMIT (XA,ESA313)
JOBS136T LTR   R1,R1                   TIME=0 (TSO) ?
         BZ    JOBS138                 YES, EXIT                GP09260
         SLR   R0,R0
         D     R0,=F'100'              GET TIME IN SECONDS
         SLR   R0,R0
         D     R0,=F'60'               GET TIME IN MINUTES
         STRING 'TIME=(',((R1),,L),',',((R0),,L),')',INTO=MSG60
         BAL#PROC JOBS155             APPEND NEW TEXT TO CURRENT LINE
*
JOBS138  SLR   R0,R0                                            GP09260
         IC    R0,JCTJPRTY             GET PRIORITY             GP09260
         N     R0,=X'0000000F'         RETAIN LOW NYBBLE        GP09260
         STRING 'PRTY=(',((R0),,L),')',INTO=MSG60               GP09260
         BAL#PROC JOBS155             APPEND NEW TEXT TO CURRENT LINE
*
JOBS140  L     R6,JSCSCT               CURRENT STEP'S SCT
         USING SCT,R6
         LH    R2,JCTEQREG             REGION PARM FROM JOB CARD
         CH    R2,SCTMSSZE   SAME AS EXEC CARD                  GP09255
         BE    JOBS147                 NO; SKIP                 GP09255
         LH    R2,SCTMSSZE             USE IT                   GP09255
         STRING 'REGION=',((R2),,L),'K',INTO=MSG60
         BAL#PROC JOBS155             APPEND NEW TEXT TO CURRENT LINE
         O     R5,=X'80000000'         MAKE R5 NEGATIVE         GP09260
*
JOBS147  TM    JCTSTAT2,JCTPERFM       PERFORM SPECIFIED ON EXEC?
         BZ    JOBS149                 NO, JUMP
         STRING 'PERFORM=',(SCTPRFMF,FL1,L),INTO=MSG60          GP09260
         BAL#PROC JOBS155             APPEND NEW TEXT TO CURRENT LINE
         O     R6,=X'80000000'         MAKE R6 NEGATIVE         GP09260
*
JOBS149  L     R2,PSAAOLD-PSA          MY ASCB
         USING ASCB,R2
         L     R3,ASCBASXB             MY ASXB
         USING ASXB,R3
         STRING 'USER=',(ASXBUSER,,T),INTO=MSG60
         BAL#PROC JOBS155             APPEND NEW TEXT TO CURRENT LINE
         ICM   R3,B'1111',ASXBSENV     MY ACEE
         BNP   JOBS149Z                NO RACF, EXIT
         USING ACEE,R3
         STRING 'GROUP=',(ACEEGRPN,,T),INTO=MSG60
         BAL#PROC JOBS155             APPEND NEW TEXT TO CURRENT LINE
JOBS149Z CAL#PROC SPACE2          <== DOUBLE SPACE
         B     JOBS162
*
*        APPEND MSG60 TO LINE
*
JOBS155  ST    R14,WKCELL3+4           SAVE RETURN ADDRESS
         LTR   R0,R15                  SAVE LENGTH
         BZ    JOBS155C                ZERO LENGTH, JUMP
         LA    R14,MSG60               FIRST POS
JOBS155L CLI   0(R14),C' '             IMBEDDED SPACE?
         BE    JOBS155Q                YES, EXIT
**MSGL   CLI   0(R14),C','             IMBEDDED COMMA?
**MSGL   BE    JOBS155Q                YES, EXIT
         LA    R14,1(,R14)             BUMP POINTER
         BCT   R15,JOBS155L            SCAN AREA
         B     JOBS155C                YES, EXIT
JOBS155Q MVC   EYECATCH,BLANKS         CLEAR WORK AREA          GP09260
         MVC   EYECATCH(60),MSG60      MOVE TO WORK AREA        GP09260
         STRING '''',(EYECATCH,(R0)),'''',INTO=MSG60
         LR    R0,R15                  SAVE LENGTH
JOBS155C STRING (LINE,,T),(WKCELL3,2,T),(MSG60,(R0)),',',INTO=EYECATCH
         CL    R15,=F'80'              OVERFLOW?
         BH    JOBS155O                YES, JUMP
         STRING (LINE,,T),(WKCELL3,2,T),(MSG60,(R0)),INTO=LINE
         B     JOBS155X
JOBS155O STRING (LINE,,T),',',INTO=LINE
         CAL#PROC SPACE1          <== SINGLE SPACE
         STRING '  // ',(MSG60,(R0)),INTO=LINE
JOBS155X MVI   WKCELL3,C','            NEXT PARM NEEDS A COMMA
         MVI   WKCELL3+1,C' '          NEXT PARM NEEDS NO SPACE
         L     R14,WKCELL3+4           RETURN ADDRESS
         BR    R14
*
JOBS162  MVC   WKCELL2(3),=C'NO '      DEFAULT
         TM    JCTSTAT2,JCTBLP         BLP AUTHORIZED?
         BZ    JOBS164                 NO, JUMP
         MVC   WKCELL2(3),=C'YES'      YES, REMEMBER IT
JOBS164  MVC   WKCELL3(3),=C'NO '      DEFAULT
         TM    JSCBJJSB,JSCBJNLF       JOB JOURNALING ACTIVE?
         BO    JOBS168                 NO, JUMP
         MVC   WKCELL3(3),=C'YES'      YES, REMEMBER IT
JOBS168  L     R3,JSCBSSIB             POINT TO THE SSIB
         USING SSIB,R3
         STRING '    SUB-SYSTEM(',SSIBSSNM,')',                        X
               '  JOB-ID(',SSIBJBID,')',                               X
               '  BLP(',(WKCELL2,3,T),')',     BLP(YES/NO)             X
               '  JOURNAL(',(WKCELL3,3,T),')', JOB JOURNAL (YES/NO)    X
               '  ASID(X''',(ASCBASID,,X),''')',                       X
               INTO=LINE
         CAL#PROC SPACE1          <== SINGLE SPACE
*
JOBS170  ZAP   WKCELL1,=P'0'                                    GP13056
         ZAP   WKCELL2,=P'0'                                    GP13056
         TM    JCTJMRJD+L'JCTJMRJD-1,X'0F'  DATE SET ?          GP13056
         BNO   JOBS170M        DATE NOT SET FOR SMFDUMP         GP13056
         ZAP   WKCELL1,JCTJMRJD        YYDDDF   (PRIOR TO HBB4430)
JOBS170M TM    JCTSSD+L'JCTSSD-1,X'0F'  DATE SET ?              GP13056
         BNO   JOBS170N        DATE NOT SET FOR SMFDUMP         GP13056
         ZAP   WKCELL2,JCTSSD          YYDDDF   (PRIOR TO HBB4430)
JOBS170N SLR   R0,R0
         SLR   R1,R1
         ICM   R1,B'0111',JCTJMRJT     JOB START TIME (BINARY)
         D     R0,=F'00360000'         GET HOURS
         LR    R2,R1                   HH
         LR    R1,R0                   REMAINDER
         SLR   R0,R0
         D     R0,=F'6000'             GET MINUTES IN R1
         LR    R3,R1                   MM
         LR    R1,R0                   REMAINDER
         SLR   R0,R0
         D     R0,=F'100'              GET SECONDS IN R1
         STRING '    JOB START DATE: ',(WKCELL1,P,YYYY-MM-DD),         X
               '  TIME: ',((R2),,R2Z),':',((R3),,R2Z),':',((R1),,R2Z), X
               INTO=LINE                                        GP09260
         CAL#PROC SPACE1          <== SINGLE SPACE              GP09260
*
*        CPU TIME USED (JOB)
*
JOBS180  SLR   R0,R0
         SLR   R1,R1                                            GP09260
         ICM   R1,7,ACTJTIME           ACCUMULATED TCB TIME     GP09260
         A     R1,JCTSRBT              ACCUMULATED SRB TIME
         BZ    JOBS200                 ZERO, QUIT
         D     R0,=F'00360000'         GET HOURS
         LR    R2,R1                   HH
         LR    R1,R0                   REMAINDER
         SLR   R0,R0
         D     R0,=F'6000'             GET MINUTES IN R1
         LR    R3,R1                   MM
         LR    R1,R0                   REMAINDER
         SLR   R0,R0
         D     R0,=F'100'              GET SECONDS IN R1
         STRING '    CPU TIME USED: ',((R2),,R3Z),':',((R3),,R2Z),':', X
               ((R1),,R2Z),'.',((R0),,R2Z),                            X
               INTO=LINE                                        GP09260
**           1X,(ACTJTIME,,X),1X,(JCTSRBT,,X), ****DEBUG****         X
         CAL#PROC SPACE2          <== DOUBLE SPACE
*
*        //STEPNAME EXEC PGM=PROGRAM,REGION=1234K,TIME=(30,0)
*
JOBS200  SLR   R7,R7                   SCTX ADDRESS
         ICM   R7,7,SCTXBTTR           GET SCT EXTENSION        GP09260
         LA    R7,16(,R7)              SKIP QUEUE HEADER        GP09260
         USING SCTXIN,R7               DECLARE MAPPING          GP09260
         ICM   R1,B'0111',SCTSTIME     STEP TIME LIMIT (XA/ESA310)
         SLR   R0,R0
         D     R0,=F'100'              GET IT IN SECONDS
         SLR   R0,R0
         D     R0,=F'60'               GET IT IN MINUTES
         STRING '  //',(SCTSNAME,,T),' EXEC PGM=',(SCTPGMNM,,T),       X
               ',TIME=(',((R1),,L),',',((R0),,L),')',                  X
               INTO=LINE
*
         LTR   R5,R5                   REGION SPECIFIED ON JOB CARD?
         BM    JOBS230                 YES, JUMP
         STRING (LINE,,T),',REGION=',(SCTMSSZE,FL2,L),'K',INTO=LINE
JOBS230  LH    R2,SCTNIUSL             MAX NUMBER OF DD STMTS
         SH    R2,SCTLALOC             MINUS # OF GODD DDS
         BZ    JOBS240                 ZERO, JUMP
         STRING (LINE,,T),',DYNAMNBR=',((R2),,L),INTO=LINE
JOBS240  EQU   *
         LTR   R6,R6                   PERFORM SPECIFIED ON JOB?
         BO    JOBS250                 YES, JUMP (JOB OVERRIDES EXEC)
         TM    SCTSDP,SCTEPRFM         PERFORM SPECIFIED ON EXEC?
         BZ    JOBS250                 NO, JUMP
         STRING (LINE,,T),',PERFORM=',(SCTPRFMF,FL1,L),INTO=LINE  09260
JOBS250  EQU   *
         SLR   R2,R2
         ICM   R2,B'0011',SCTSEXEC     PARM LENGTH
         BZ    JOBS270                 NO PARM, JUMP
         STRING (LINE,,T),',',INTO=LINE
         CAL#PROC SPACE1          <== NEXT LINE
         STRING '  // PARM=''',(SCTXPARM,(R2)),'''',                   X
               INTO=LINE
JOBS270  CAL#PROC SPACE2          <== DOUBLE SPACE
*
JOBS300  L     R6,PSAAOLD-PSA          ASCB
         USING ASCB,R6
         L     R7,ASCBOUCB             MY OUCB
         USING OUCB,R7
         STRING '    PERFORM(',(OUCBSPG,FL1,L),')',           GP09260  X
               '   DPRTY(X''',(ASCBDP,,X),''')',  DISPATCHING PRIORITY X
               INTO=LINE
         CAL#PROC SPACE1          <== PRINT LINE
*
JOBS410  SLR   R0,R0
         SLR   R1,R1
         ICM   R1,B'0111',JCTJMRSS     STEP START TIME (BINARY)
         D     R0,=F'00360000'         GET HOURS
         LR    R2,R1                   HH
         LR    R1,R0                   REMAINDER
         SLR   R0,R0
         D     R0,=F'6000'             GET MINUTES IN R1
         LR    R3,R1                   MM
         LR    R1,R0                   REMAINDER
         SLR   R0,R0
         D     R0,=F'100'              GET SECONDS IN R1
         STRING '    STEP START DATE: ',(WKCELL2,P,YYYY-MM-DD),        X
               '  TIME: ',((R2),,R2Z),':',((R3),,R2Z),':',((R1),,R2Z), X
               INTO=LINE                                        GP09260
         CAL#PROC SPACE1          <== PRINT LINE
*
*        CPU TIME (STEP)
*
JOBS420  LM    R0,R1,ASCBEJST          GET ELAPSED JOB TCB TIME
         CLM   R0,B'1100',=F'0'        IS TOD ACCURATE?
         BE    JOBS421                 BRANCH IF YES
         SR    R0,R0                   ZERO OUT R0
         SR    R1,R1                   ZERO OUT R1
JOBS421  DS    0H
         SRDL  R0,0012                 IN MICRO-SECONDS
         LM    R14,R15,ASCBSRBT        GET ELAPSED SRB TIME
         CLM   R14,B'1100',=F'0'       IS TOD ACCURATE?
         BE    JOBS422                 BRANCH IF YES
         SR    R14,R14                 ZERO OUT R14
         SR    R15,R15                 ZERO OUT R15
JOBS422  DS    0H
         SRDL  R14,0012                IN MICRO-SECONDS
         ALR   R1,R15                  ADD TCB AND SRB TIME
         BNO   JOBS423                 BRANCH IF NO OVERFLOW
         AH    R0,=H'1'                HANDLE CARRY
JOBS423  DS    0H
         AR    R0,R14
         D     R0,=F'10000'            GET IN HUNDREDTHS
         CH    R0,=H'5000'          -  ROUND UP NEEDED ?
         BL    JOBS427              -  BRANCH IF NOT
         AH    R1,=H'1'             -  ROUND UP
*
JOBS427  SLR   R0,R0
         D     R0,=F'100'              TIME IN SECOND
         LR    R3,R0                   SAVE REMAINING 1/100
         SLR   R0,R0
         D     R0,=F'60'               TIME IN MINUTES
         LR    R2,R0                   SAVE REMAINING SECONDS
         SLR   R0,R0
         D     R0,=F'60'               TIME IN HOURS
         STRING ((R1),,L),':',((R0),,R2Z),'M',INTO=WKCELL1      GP09260
         LTR   R1,R1                   MORE THAN 1 HOUR?
         BNZ   JOBS429                 YES, JUMP
         STRING ((R0),,L),':',((R2),,R2Z),'S',INTO=WKCELL1      GP09260
         LTR   R0,R0                   MORE THAN 1 MINUTE?
         BNZ   JOBS429                 YES, JUMP
         STRING ((R2),,L),'.',((R3),,R2Z),INTO=WKCELL1
JOBS429  STRING '    CPU TIME USED: ',WKCELL1,INTO=LINE
         CAL#PROC SPACE2          <== DOUBLE SPACE
*
*        STORAGE ALLOCATION
*
JOBS600  MVI   MINLINES,6
         L     R4,CVTPTR               GET CVT                  GP09260
         L     R4,CVTGDA-CVTMAP(,R4)   GET GDA                  GP09260
         USING GDA,R4                                           GP09260
         L     R5,TCBTCT               SMF TIMING CONTROL TABLE
         USING SMFTCT,R5
         ICM   R5,15,TCTCRTBL     SMF STORAGE CONTROL TABLE     GP09260
         BZ    JOBSTP99           SKIP MEM IF NONE              GP09260
         USING TCTCORE,R5
         TM    TCBFLGS6,TCBRV          V=R STEP ?               GP09260
         BNZ   JOBSTP99                YES; DON'T KNOW YET      GP09260
         L     R6,ASCBLDA              LOCAL DATA AREA
         USING LDA,R6
         STRING '  VIRTUAL STORAGE USE:',INTO=LINE              GP09260
         CAL#PROC SPACE1                                        GP09260
         STRING 30X,'LIMIT     IN USE  AVAILABLE      HIGH-WATERMARK', X
               INTO=LINE
         CAL#PROC SPACE1                                        GP09260
         SR    R0,R0                                            GP09260
         ICM   R0,3,TCTRSZ             REGION REQUESTED         GP09260
         SLL   R0,10                   WAS IN 2K BLOCKS         GP09260
         L     R3,PASIZE               GET PRIVATE SIZE         GP09260
         S     R3,TCTHWM               LESS HIGH- WATER MARK    GP09260
         A     R3,TCTLWM               PLUS LOW WATER MARK      GP09260
         LR    R1,R3  ****CHEAT - SET CURRENT USE AS HWM        GP09260
         LTR   R2,R0                   REQUESTED SIZE           GP09260
         BP    JOBS610                                          GP09287
         L     R2,PASIZE               PRIVATE SIZE             GP09287
JOBS610  SR    R2,R1                   LESS CURRENT USE         GP09260
         SRL   R0,10                   CHANGE TO 1K MULTIPLES   GP09260
         SRL   R1,10                   CHANGE TO 1K MULTIPLES   GP09260
         SRL   R2,10                   CHANGE TO 1K MULTIPLES   GP09260
         SRL   R3,10                   CHANGE TO 1K MULTIPLES   GP09260
         STRING 24X,((R0),,R10B),'K',((R1),,R10B),'K',((R2),,R10B),'K',*
               7X,((R3),,R12B),'K',INTO=LINE
         CAL#PROC SPACE1          <== PRINT LINE
         SPACE 1
         L     R3,TCBUKYSP        229, 230                      GP09260
         ICM   R4,15,TCBMSS       GET SPQE CHAIN                GP09260
         STM   R3,R4,TENWORDS     SAVE                          GP09260
         USING SPQESECT,R3        DECLARE IT                    GP09260
JOBSTP80 SR    R0,R0                                            GP09260
         ST    R0,TENWORDS+8                                    GP09260
         IC    R0,SPQEID          SAVE SUBPOOL                  GP09260
         SR    R4,R4              ACCUMULATOR
         ICM   R2,15,SPDQEPTR     FIRST DQE                     GP09260
         BZ    JOBSTP88           DONE?                         GP09260
         TM    SPQEFLGS,SPSHARE   SHARED SUBPOOL?               GP09260
         BZ    JOBSTP82           NO; RUN DQE CHAIN             GP09260
         ICM   R2,15,SPDQEPTR-SPQESECT(R2)  FUNNY CHAINING      GP09260
         BZ    JOBSTP88           DO IT                         GP09260
         USING DQESECT,R2                                       GP09260
JOBSTP82 A     R4,DQELNTH                                       GP09260
         ICM   R2,15,DQEPTR       NEXT DQE                      GP09260
         BNZ   JOBSTP82                                         GP09260
         SRL   R4,10                                            GP09260
         STRING 20X,'SUBPOOL ',((R0),,R3B),'   SIZE ',((R4),,R6B),'K', *
               INTO=LINE                                        GP09260
         CAL#PROC SPACE1          <== PRINT LINE                GP09260
JOBSTP88 ICM   R3,15,SPQEPTR           ANOTHER ?                GP09260
         BNZ   JOBSTP80                YES                      GP09260
         MVC   TENWORDS(8),TENWORDS+4  NEXT GROUP               GP09260
         ICM   R3,15,TENWORDS          IS THERE ONE ?           GP09260
         BNZ   JOBSTP80                YES; DO IT               GP09260
JOBSTP99 END#PROC
         POP   USING                                            GP09260
*
*----------------------------------------------------------------------
*        PRINT RB CHAIN
*----------------------------------------------------------------------
*
         PUSH  USING                                            GP09260
PRB00    BEG#PROC
         CL    R9,TCBJSTCB             MAIN TASK?               @920630
         BNE   PRB1000                 NO, JUMP                 @920630
         ICM   R0,B'1111',TCBLTC       YES, ANY SUB-TASKS?      @921020
         BZ    PRB20                   NONE, JUMP               @921020
*
*        DISPLAY TCB TREE AND RB CHAINS (IF TCB IS NOT JSTCB)
*
PRB1000  CAL#PROC BLANK1          <== BLANK LINE
         MVI   MINLINES,5
         STRING 'TCB TREE AND RB CHAINS:',INTO=LINE
         CAL#PROC SPACE1              PRINT LINE
         STRING '0   TCB ADDRESS',18X,INTO=LINE121,                    X
               'PROGRAM    IC  STAB  FLAGS1 CDFLGS    DDNAME'
         CAL#PROC SPACE1              PRINT LINE
         L     R9,TCBJSTCB             THE JOB STEP TCB
         SLR   R3,R3                   INDENTATION INDEX
*LOOP
PRB1100  STCM  R9,B'0111',WKCELL1      STORE TCB ADDR
         STRING (BLANKS,4(R3)),(WKCELL1,3,X),INTO=LINE121
         TM    TCBFLGS5,TCBFC          TASK ENDED ALREADY?
         BO    PRB1288                 YES, NEXT TCB
         SLR   R6,R6                   FIRST TIME SWITCH FOR NEXTRB
*
*        PROCESS RB CHAIN FROM THE OLDEST RB
*--LOOP
PRB1200  CAL#PROC NEXTRB              POINT TO NEXT RB
         LTR   R6,R6                   END OF CHAIN?
         BZ    PRB1290                 YES, EXIT LOOP
         USING RBPREFIX,R6
         CLI   RBSTAB1,RBFTPRB         IS THIS A PRB?
         BNE   PRB1240                 NO, JUMP
*227     TM    RBCDFLGS,RBCDSYNC       CHECK FLAGS
*227     BO    PRB1260                 JUMP IF IT IS A SYNCH PRB
         SLR   R1,R1
         ICM   R1,B'0111',RBCDE1       DO WE HAVE A CDE?
         BZ    PRB1230                 NO, JUMP (MUST BE A SYNCH PRB)
         MVC   WKCELL1,BLANKS          TASKLIB DDNAME
         ICM   R15,B'1111',TCBJLB      LOAD/TEST DCB ADDRESS
         BZ    PRB1220                 NO TASKLIB, JUMP
         MVC   WKCELL1,=C'-LNKLST-'
         L     R14,CVTPTR              POINT AT CVT
         CLM   R15,7,CVTLINK-CVTMAP+1(R14) SYS1.LINKLIB         GP09260
         BE    PRB1220                 JUMP IF SYS1.LINKLIB DCB ADDR
         LH    R15,DCBTIOT-IHADCB(,R15) LOAD TIOT OFFSET
         N     R15,=X'0000FFFF'        JUST IN CASE             GP09260
         A     R15,TCBTIO              CHANGE OFFSET INTO AN ADDRESS
         MVC   WKCELL1,4(R15)          MOVE TASKLIB DDNAME
*
*        "LINK" PRB
*
PRB1220  STRING INTO=(LINE+30,L'LINE-30),2X,                           X
               (CDNAME-CDENTRY(R1),8),3X,      PGM NAME                X
               (RBWLIC+3,1,X),2X,      IC                              X
               (RBSTAB,2,X),4X,        STATUS BYTE                     X
               (RBFLAGS1,,X),5X,       FLAGS                           X
               (RBCDFLGS,,X),6X,       FLAGS                           X
               WKCELL1                 DDNAME OF TASKLIB
*        DISPLAY REG1 FOR DATACOM SUB-TASK                      @930824
         CLC   =C'DBISBPR ',CDNAME-CDENTRY(R1)    DATACOM/DB?   @930824
         BNE   PRB1270                 NO, JUMP                 @930824
         LA    R1,LINE+30(R15)         END OF TEXT              @930824
         STRING ' R1=',(RBGRS1,,X),INTO=((R1),5+8)              @930824
         B     PRB1270
*
*        "SYNCH" PRB
*
PRB1230  MVC   WKCELL1,RBGRS15         PICK UP ENTRY POINT ADDRESS
         NI    WKCELL1+3,X'FE'         SET BIT 31 TO ZERO
         STRING INTO=(LINE+30,L'LINE-30),2X,                           X
               (WKCELL1,4,X),3X,       EP ADDRESS                      X
               (RBWLIC+3,1,X),2X,                                      X
               (RBSTAB,2,X),4X,        FLAGS                           X
               (RBFLAGS1,,X),5X,       FLAGS                           X
               (RBCDFLGS,,X)
         B     PRB1270
*
*        SVRB
*
PRB1240  TM    RBSTAB1,RBFTSVRB        IS THIS AN SVRB?
         BNO   PRB1250                 NO, JUMP
         STRING INTO=(LINE+30,L'LINE-30),2X,                           X
               WKCELL1,3X,             SVC 055                         X
               (RBWLIC+3,1,X),2X,                                      X
               (RBSTAB,2,X),4X,        FLAGS                           X
               (RBFLAGS1,,X),5X,       FLAGS                           X
               (RBCDFLGS,,X)
         B     PRB1270
*
*        IRB
*
PRB1250  TM    RBSTAB1,RBFTIRB         IS THIS AN IRB?
         BNO   PRB1280                 NO, IGNORE IT
         STRING INTO=(LINE+30,L'LINE-30),2X,                           X
               '-IRB    ',3X,          IRB                             X
               (RBWLIC+3,1,X),2X,                                      X
               (RBSTAB,2,X),4X,        FLAGS                           X
               (RBFLAGS1,,X),5X,       FLAGS                           X
               (RBCDFLGS,,X)
*
PRB1270  CLC   RBOPSW,SDWAEC1          ABENDING RB?
         BNE   PRB1271                 NO, JUMP
         MVI   LINE+29+1,C'*'          YES, FLAG IT
PRB1271  CAL#PROC SPACE1              PRINT LINE
         ICM   R1,B'1111',$RTM2WA      RTM2WA LOCATED?
         BNP   PRB1279                 NO, JUMP
         USING RTM2WA,R1
         TM    RTM2ERRA,RTM2ABTM       ABTERM?
         BO    PRB1275                 YES, JUMP
         TM    RTM2ERRA,RTM2PCHK       PROGRAM CHECK?
         BNO   PRB1279                 NO, JUMP
         L     R0,RTM2AEC1             GET INTERRUPT CODE IN R0
         DROP  R1                      RTM2WA
         STRING '-PCK',(RBWLIC+3,FL1,R3Z),'-',INTO=WKCELL1
         B     PRB1280                 NO, JUMP
PRB1275  MVC   WKCELL1,=C'-ABTERM-'
         B     PRB1280                 NO, JUMP
PRB1279  STRING '-SVC',(RBWLIC+3,FL1,R3Z),'-',INTO=WKCELL1
PRB1280  B     PRB1200                 LOOP THROUGH RB TABLE
*--ENDLOOP
PRB1288  STRING '  TASK TERMINATED',INTO=(LINE+30,L'LINE-30)
         CAL#PROC SPACE1              PRINT LINE
*
PRB1290  CAL#PROC SCANTCB             GET NEXT TCB
         BNZ   PRB1100                 PROCESS NEXT TCB
*ENDLOOP
         L     R9,PSATOLD-PSA          POINT TO ABENDING TCB
*
*        DISPLAY RB CHAIN FOR ABENDING TCB
*
PRB20    CAL#PROC BLANK1          <== BLANK LINE
         MVI   MINLINES,5
         STRING 'ABENDING TCB: ',(PSATOLD-PSA+1,3,X),INTO=LINE
         CAL#PROC SPACE1          <== PRINT LINE
         SLR   R6,R6                   FIRST TIME SWITCH FOR NEXTRB
*
*        PROCESS RB CHAIN FROM THE OLDEST RB
*--LOOP
PRB21    CAL#PROC NEXTRB              POINT TO NEXT RB
         LTR   R6,R6                   END OF CHAIN?
         BZ    PRB99                   YES, EXIT LOOP
         CL    R6,MY#PRB               END OF CHAIN?
         BE    PRB99                   YES, EXIT LOOP
         USING RBPREFIX,R6
         LA    R0,RBBASIC              POINT TO BASIC SECTION
         ST    R0,WKCELL1              KEEP ADDR FOR DISPLAY
         MVI   MINLINES,3
*
         TM    RBSTAB1,RBFTSVRB        IS THIS AN SVRB?
         BO    PRB29                   YES, JUMP
*
         TM    RBSTAB1,RBFTIRB         IS THIS AN IRB?
         BO    PRB30                   YES, JUMP
*
*227     TM    RBCDFLGS,RBCDSYNC       CHECK FLAGS
*227     BO    PRB28                   JUMP IF IT IS A SYNCH PRB
*227     L     R5,RBCDE                GET ADDR OF CDE/LPDE
         SLR   R5,R5
         ICM   R5,B'0111',RBCDE1       DO WE HAVE A CDE?
         BZ    PRB28                   NO, JUMP (MUST BE A SYNCH PRB)
         STRING INTO=LINE121,                                          X
               '0  PRB: ',(WKCELL1+1,3,X),                             X
               '  STAB: ',(RBSTAB,,X),            STATUS BYTES         X
               '  FLAGS1: ',(RBFLAGS1,,X),        FLAGS                X
               '  CDFLGS: ',(RBCDFLGS,,X),        FLAGS                X
               '  CDNAME: ',(CDNAME-CDENTRY(R5),8) PROGRAM NAME
         CAL#PROC SPACE1          <== PRINT LINE
         B     PRB31
*
PRB28    MVC   WKCELL2,RBGRS15         PICK UP ENTRY POINT ADDRESS
         NI    WKCELL2+3,X'FE'         SET BIT 31 TO ZERO
         STRING INTO=LINE121,                                          X
               '0  PRB: ',(WKCELL1+1,3,X),                             X
               '  STAB: ',(RBSTAB,,X),            STATUS BYTES         X
               '  FLAGS1: ',(RBFLAGS1,,X),        FLAGS                X
               '  CDFLGS: ',(RBCDFLGS,,X),        FLAGS                X
               '  EPADDR: ',(WKCELL2,4,X)         ENTRY POINT
         CAL#PROC SPACE1          <== PRINT LINE
         CL    R6,MY#PRB               MY RBPREFIX ?
         BNE   PRB31                   NO, JUMP
         B     PRB39                   YES, QUIT
*
PRB29    STRING INTO=LINE121,                                          X
               '0 SVRB: ',(WKCELL1+1,3,X),                             X
               '  STAB: ',(RBSTAB,,X),            STATUS BYTES         X
               '  FLAGS1: ',(RBFLAGS1,,X),        FLAGS                X
               '  CDFLGS: ',(RBCDFLGS,,X)         FLAGS
         CAL#PROC SPACE1          <== PRINT LINE
         STRING 'SVC ROUTINE',INTO=MSG20
         TM    RBFLAGS1,RBABEND        IS THIS THE ABEND SVRB?
         BNO   PRB32                   NO, JUMP
         STRING 'RTM',INTO=MSG20
         B     PRB32                   JUMP OVER CSVQRY00 IF SVRB
*
PRB30    MVC   WKCELL2(4),RBCDE        IRB EP ADDRESS
         NI    WKCELL2+3,X'FE'         SET BIT 31 TO ZERO
         STRING INTO=LINE121,                                          X
               '0  IRB: ',(WKCELL1+1,3,X),                             X
               '  STAB: ',(RBSTAB,,X),            STATUS BYTES         X
               '  FLAGS1: ',(RBFLAGS1,,X),        FLAGS                X
               '  CDFLGS: ',(RBCDFLGS,,X),        FLAGS                X
               '  EPADDR: ',(WKCELL2,4,X)         ENTRY POINT
         CAL#PROC SPACE1          <== PRINT LINE
*
PRB31    L     R1,RBOPSW+4             PICK UP ADDR OF NEXT INSTR.
         CAL#PROC CSVQRY00        <== FIND CORRESPONDING CDE
*
PRB32    TM    RBFLAGS1,RBXWAIT        THIS RB IN WAIT?
         BNO   PRB33                   NO, JUMP
         STRING 2X,(MSG20,,T),' *ISSUED WAIT',INTO=MSG60
         B     PRB38
*
PRB33    ICM   R1,B'1111',$RTM2WA      RTM2WA LOCATED?
         BNP   PRB37                   NO, JUMP
         USING RTM2WA,R1
         TM    RTM2ERRA,RTM2ABTM       ABTERM?
         BO    PRB36                   YES, JUMP
         TM    RTM2ERRA,RTM2PCHK       PROGRAM CHECK?
         BNO   PRB37                   NO, JUMP
         STRING 2X,(MSG20,,T),' *PROGRAM CHECK*',INTO=MSG60
         L     R0,RTM2AEC1             GET INTERRUPT CODE IN R0
         DROP  R1                      RTM2WA
         L     R1,=A(PCKTABLE)         START OF PCK TABLE
         CAL#PROC SCANTBL             FIND PCK NAME
         BNE   PRB38                   NOT FOUND, JUMP
         STRING (MSG60,,T),' (',(2(R1),(R2)),')',INTO=MSG60
         B     PRB38
*
PRB36    STRING 2X,(MSG20,,T),' *SYSTEM ISSUED SVC 13 (ABEND)',       --
               INTO=MSG60
         B     PRB38
*
PRB37    STRING 2X,(MSG20,,T),' ISSUED SVC ',(RBWLIC+3,FL1,L0),       --
               INTO=MSG60
         L     R0,RBWLIC               GET INTERRUPT CODE IN R0
         L     R1,=A(SVCTABLE)         START OF SVC TABLE
         CAL#PROC SCANTBL             FIND SVC NAME
         BNE   PRB38                   NOT FOUND, JUMP
         STRING (MSG60,,T),' (',(2(R1),(R2)),')',INTO=MSG60
*
PRB38    STRING 5X,INTO=LINE,                                          X
               'W-L-IC ',(RBWLIC,1,X),            WAIT COUNT           X
               '-',(RBWLIC+1,1,X),                INSTRUCTION LENGTH   X
               '-',(RBWLIC+2,2,X),                INTERRUPT CODE       X
               '  PSW ',(RBOPSW,4,X),1X,(RBOPSW+4,4,X),                X
               MSG60                   DIAGNOSTIC
         CAL#PROC SPACE1          <== PRINT LINE
*
*        DISPLAY R14-R1 FROM PREVIOUS RB
*
         ST    R6,WKCELL1              SAVE RB ADDR
         CAL#PROC NEXTRB              NEXT RB
         STRING 5X,INTO=LINE,                                          X
               ' R14=',(RBGRS14,,X),                                   X
               '  R15=',(RBGRS15,,X),                                  X
               '  R0=',(RBGRS0,,X),                                    X
               '  R1=',(RBGRS1,,X)
         CAL#PROC SPACE1          <== PRINT LINE
         L     R6,WKCELL1              RESTORE RB ADDR
*
PRB39    B     PRB21                   PROCESS NEXT RB
*ENDLOOP
PRB99    END#PROC
         POP   USING                                            GP09260
*----------------------------------------------------------------------
*        TCB TREE SCAN ROUTINE
*----------------------------------------------------------------------
SCANTCB  BEG#PROC SAVE=NO
         LR    R1,R9                   SAVE TCB ADDRESS
         L     R9,TCBLTC-TCB(,R9)      DAUGHTER
         LA    R3,1(,R3)               INDENTATION INDEX
*LOOP
SCANTCB2 LTR   R9,R9                   CHECK FOR END OF CHAIN
         BNZR  R14                     PASS VALID TCB ADDRESS
         L     R9,TCBNTC-TCB(,R1)      SISTER
         L     R1,TCBOTC-TCB(,R1)      MOTHER
         BCT   R3,SCANTCB2             INDENTATION INDEX
*ENDLOOP
         SR    R9,R9                   SET CC=0
         BR    R14                     GOBACK
*
*----------------------------------------------------------------------
*        SCAN RB CHAIN BACKWARDS (FROM OLDEST RB)
*              INPUT:  R9 POINTS TO THE TCB
*                      R6 POINTS TO CURRENT RBPREFIX
*              OUTPUT: R6 POINT TO NEXT RB
*----------------------------------------------------------------------
*
         USING RBPREFIX,R6
NEXTRB   BEG#PROC SAVE=NO
         LA    R0,RBBASIC              POINT TO BASIC SECTION
         SLR   R6,R6                   END OF CHAIN
         CL    R0,TCBRBP               END OF CHAIN?
         BER   R14                     YES, GOBACK WITH R6=0
         ICM   R6,B'1111',TCBRBP       POINT TO TOP RB (MOST RECENT)
         BZR   R14                     NO RB FOR THIS TCB, GOBACK
*--LOOP
NEXTRB2  SH    R6,=AL2(RBBASIC-RBPREFIX) POINT TO RBPREFIX
         CLM   R0,B'0111',RBLINK+1     IS THIS THE NEXT RB?
         BE    NEXTRB4                 YES, EXIT LOOP
         TM    RBSTAB2,RBTCBNXT        OLDEST RB (END OF CHAIN) ?
         BO    NEXTRB4                 YES, EXIT LOOP
         LA    R1,RBBASIC              PREVIOUS RB IN CHAIN
         ICM   R6,B'0111',RBLINK+1     POINT TO PREVIOUS RB (OR TCB)
         BNZ   NEXTRB2                 NEXT RB IN CHAIN
*--ENDLOOP
         BR    R14                     GOBACK WITH R6=0
*
*        RETRIEVE ADDR OF RTM2WA ASSOCIATED WITH PREVIOUS SVRB
*
NEXTRB4  ST    R1,$RTM2WA              PREVIOUS RB
         ICM   R1,B'1111',TCBRTWA      RTM2WA
         BZ    NEXTRB4N                LOST, EXIT
         USING RTM2WA,R1
*LOOP
NEXTRB4L CLC   RTM2VRBC,$RTM2WA        SVRB ADDR?
         BE    NEXTRB4X                YES, EXIT
         ICM   R1,B'1111',RTM2PREV     END OF RTM2WA CHAIN?
         BNZ   NEXTRB4L                NO, PROCESS NEXT ONE
*ENSLOOP
NEXTRB4N BCTR  R1,0                    R1=FFFFFFFF
*
NEXTRB4X ST    R1,$RTM2WA              RTM2WA ADDRESS OR FFFFFFFF
         BR    R14                     GOBACK
         DROP  R6,R1                   RBPREFIX,RTM2WA
         LTORG ,                                                GP09255
*
*----------------------------------------------------------------------
*        SCAN SVC/PCK TABLES
*----------------------------------------------------------------------
*
SCANTBL  BEG#PROC SAVE=NO
         SLR   R2,R2
*LOOP
SCANTBL2 IC    R2,1(,R1)               PICK UP ENTRY LENGTH
         CLM   R0,B'0001',0(R1)        COMPARE SVC NUMBER WITH INTCODE
         BER   R14                     EQUAL, QUIT LOOP
         LA    R1,2(R2,R1)             BUMP TO NEXT ENTRY IN SVC TABLE
         CLI   0(R1),FF                END OF TABLE?
         BNE   SCANTBL2                NO, TRY NEXT ENTRY
*ENDLOOP
         LTR   R14,R14                 SET CC
         BR    R14
*
*----------------------------------------------------------------------
*        PRINT JPAQ AND LOAD-LIST
*----------------------------------------------------------------------
*
PGM00    BEG#PROC
         CAL#PROC BLANK1          <== BLANK LINE
         MVI   MINLINES,6
         STRING 'JOB PACK AREA FOR ABENDING STEP',INTO=LINE
         CAL#PROC SPACE1          <== PRINT LINE
         STRING '0   NAME     ENTPT     ATTRB SP ATTR ATTR2',          X
               '  USE   MAJ-CDE   LENGTH   LOAD-PNT  DATA SET NAME',   X
               INTO=LINE121
         CAL#PROC SPACE1          <== PRINT LINE
         SLR   R5,R5                   FIRST-TIME SWITCH
*LOOP
JPAQ21   CAL#PROC NEXTCDE          <- GET NEXT CDE
         LTR   R5,R5                   END OF JPAQ?
         BZ    JPAQ31                  YES, EXIT
         USING CDENTRY,R5
         MVC   EYECATCH,BLANKS         NO DDNAME
         LA    R1,CDNAME     POINT TO NAME                      GP09260
         CAL#PROC FINDLIB    FIND LIBRARY FOR THIS MODULE       GP09260
*
JPAQ21TR TR    EYECATCH(44),TRTPRINT   MAKE SURE IT'S PRINTABLE
         B     JPAQ22
*
JPAQ21XX MVC   EYECATCH(44),BLANKS     ORIGIN OF LOAD-MODULE IS UNKNOWN
*
JPAQ22   L     R6,CDXLMJP              POINT TO XL (OR MAJOR CDE)
         TM    CDATTR2,CDXLE  MJP POINTS TO EXTENT LIST ?       GP09255
         BO    JPAQ21MJ      YES - HAPPENS IN MVS 3.8J          GP09255
         TM    CDATTR,CDMIN            CHECK ATTRIBUTES
         BO    JPAQ25                  JUMP IF THIS IS A MINOR CDE
         USING XTLST,R6
JPAQ21MJ STRING 3X,CDNAME,1X,(CDENTPT,,X),4X,(CDATTRB,,X),2X,          X
               (CDSP,,X),2X,(CDATTR,,X),4X,(CDATTR2,,X),3X,            X
               (CDUSE,,X),12X,(XTLMSBLN,,X),3X,(XTLMSBAD,,X),          X
               2X,EYECATCH,            DDNAME FROM CDEX                X
               INTO=LINE
         CAL#PROC SPACE1          <== PRINT LINE
         B     JPAQ29
JPAQ25   STRING 3X,CDNAME,1X,(CDENTPT,,X),4X,(CDATTRB,,X),6X,          X
               (CDATTR,,X),4X,(CDATTR2,,X),9X,(CDNAME-CDENTRY(R6),8),  X
               21X,EYECATCH,           DDNAME FROM CDEX                X
               INTO=LINE
         CAL#PROC SPACE1          <== PRINT LINE
JPAQ29   B     JPAQ21                  NEXT JPAQ ENTRY
*ENDLOOP
JPAQ31   CAL#PROC BLANK1          <== BLANK LINE
         MVI   MINLINES,6
         STRING 'LOAD LIST FOR ABENDING TCB',INTO=LINE
         CAL#PROC SPACE1          <== PRINT LINE
         STRING '0   NAME     ENTPT     ATTRB SP ATTR ATTR2',          X
               '  COUNT  SYSCT    LENGTH   LOAD-PNT',INTO=LINE121
         CAL#PROC SPACE1          <== PRINT LINE
         SLR   R7,R7                   FIRST-TIME SWITCH
*
*        PRINT LLE CHAIN
*LOOP
PGM51    CAL#PROC NEXTLLE          <- GET NEXT LLE
         LTR   R7,R7                   END OF LOAD-LIST ?
         BZ    PGM80                   YES, EXIT
         USING LLE,R7
         L     R5,LLECDPT              CDE PTR
         TM    CDATTR2,CDXLE  MJP POINTS TO EXTENT LIST ?       GP09255
         BO    PGM52         YES - HAPPENS IN MVS 3.8J          GP09255
         TM    CDATTR,CDMIN            CHECK ATTRIBUTES
         BNO   PGM52                   JUMP IF THIS IS A MAJOR CDE
         L     R5,CDXLMJP              POINT TO MAJOR CDE
PGM52    L     R6,CDXLMJP              POINT TO XL
         TM    CDATTRB,CDELPDE         CHECK ATTRIBUTES
         BO    PGM55                   JUMP IF THIS IS A LPA MODULE
         MVC   EYECATCH,BLANKS         NO DDNAME
         LA    R1,CDNAME     POINT TO NAME                      GP09260
         CAL#PROC FINDLIB    FIND LIBRARY FOR THIS MODULE       GP09260
         TR    EYECATCH(44),TRTPRINT   MAKE SURE IT'S PRINTABLE
         STRING 3X,CDNAME,1X,(CDENTPT,,X),4X,(CDATTRB,,X),2X,          X
               (CDSP,,X),2X,(CDATTR,,X),4X,(CDATTR2,,X),3X,            X
               (LLECOUNT,,X),3X,(LLESYSCT,,X),5X,                      X
               (XTLMSBLN,,X),3X,(XTLMSBAD,,X),2X,EYECATCH,INTO=LINE
         CAL#PROC SPACE1          <== PRINT LINE
         B     PGM51                   LOOP THROUGH LOAD LIST
         USING LPDE,R5
PGM55    STRING 3X,LPDENAME,1X,(LPDENTP,,X),4X,(LPDEATTB,,X),2X,       X
               (LPDESP,,X),2X,(LPDEATTR,,X),4X,(LPDEATT2,,X),3X,       X
               (LLECOUNT,,X),3X,(LLESYSCT,,X),5X,                      X
               (LPDEXTLN+1,3,X),3X,(LPDEXTAD,,X),INTO=LINE
         CAL#PROC SPACE1          <== PRINT LINE
         B     PGM51                   LOOP THROUGH LOAD LIST
*ENDLOOP
PGM80    END#PROC
*
*----------------------------------------------------------------------
*        SCAN JPAQ BACKWARDS (FROM OLDEST CDE)
*              INPUT:  R5 POINTS TO CURRENT CDENTRY
*              OUTPUT: R5 POINT TO NEXT CDE
*----------------------------------------------------------------------
*
NEXTCDE  BEG#PROC SAVE=NO
         USING *,R15
         LR    R0,R5                   POINT TO CURRENT CDE
         L     R5,PSATOLD-PSA          MY TCB
         L     R5,TCBJSTCB-TCB(,R5)    JOB-STEP TCB
         L     R5,TCBJPQ-TCB(,R5)      TOP CDE (MOST RECENT)
         LTR   R5,R5                   EMPTY JPAQ?
         BZR   R14                     YES, GOBACK WITH R5=0
         USING CDENTRY,R5
*--LOOP
NEXTCDE2 CL    R0,CDCHAIN              IS THIS THE NEXT RB?
         BER   R14                     YES, GOBACK
         ICM   R5,B'1111',CDCHAIN      PREVIOUS CDE
         BNZ   NEXTCDE2                NOT FOUND YET, LOOP MORE
*--ENDLOOP
         BR    R14                     GOBACK WITH R5=0
*
*----------------------------------------------------------------------
*        SCAN LOAD-LIST BACKWARDS (FROM OLDEST LLE)
*              INPUT:  R7 POINTS TO CURRENT LLE
*              OUTPUT: R7 POINT TO NEXT LLE
*----------------------------------------------------------------------
*
NEXTLLE  BEG#PROC SAVE=NO
         USING *,R15
         LR    R0,R7                   POINT TO CURRENT LLE
         L     R7,PSATOLD-PSA          MY TCB
         L     R7,TCBLLS-TCB(,R7)      TOP LLE (MOST RECENT)
         LTR   R7,R7                   EMPTY LOAD LIST?
         BZR   R14                     YES, GOBACK WITH R7=0
         USING LLE,R7
*--LOOP
NEXTLLE2 CL    R0,LLECHN               IS THIS THE NEXT RB?
         BER   R14                     YES, GOBACK
         ICM   R7,B'1111',LLECHN       PREVIOUS LLE
         BNZ   NEXTLLE2                NOT FOUND YET, LOOP MORE
*--ENDLOOP
         BR    R14                     GOBACK WITH R7=0
         DROP  R5,R6,R7                CDE, XL, LLE
*
*----------------------------------------------------------------------
*   FIND THE LIBRARY FOR A LOAD MODULE POINTED TO BY R1               *
*----------------------------------------------------------------------
*
FINDLIB  BEG#PROC ,
         STM   R5,R8,TENWORDS  SAVE A BIT                       GP09260
         MVC   EYECATCH(2+2),=AL2(1,80)  BLDL HDR
         MVC   EYECATCH+4(8),0(R1)     EPNAME                   GP09260
         PUSH  USING                                            GP09260
         L     R8,PSATOLD-PSA                                   GP09260
         ICM   R8,7,TCBJLB+1      TRY STEPLIB                   GP09260
         BNZ   FINDBLDL                NOT FOUND, JUMP          GP09260
         L     R8,CVTPTR                                        GP09260
         L     R8,CVTLINK-CVTMAP(,R8)  LINK-LIST DCB            GP09260
FINDBLDL BLDL  (R8),EYECATCH           ISSUE BLDL (TO LLA)      GP09260
         LTR   R15,R15                                          GP09260
         BNZ   FIND99                  NOT FOUND, JUMP          GP09260
         IC    R2,EYECATCH+4+8+3       PICK-UP CONCAT#          GP09260
         N     R2,=F'255'              KEEP ONLY CONCAT#        GP09260
*                                                               GP09260
         MVC   EYECATCH,BLANKS                                  GP09260
         L     R7,DCBDEBAD-IHADCB(,R8)  GET THE DEB ADDRESS     GP05060
         N     R7,=X'00FFFFFF'  CLEAN IT                        GP05060
         USING DEBBASIC,R7   DECLARE IT                         GP05060
         SR    R6,R6                                            GP05060
         IC    R6,DEBAMLNG   GET NUMBER OF LIBRARIES -1         GP05060
         CR    R2,R6         VALID ?                            GP09260
         BNL   FIND99        NO, JUMP                           GP09260
         SR    R0,R0                                            GP05060
         IC    R0,DEBEXSCL-DEBBASIC(,R7)   WORDS PER EXTENT     GP05060
         SLL   R0,2          CONVERT TO BYTES                   GP05060
         STH   R0,SZDEBEXT   SAVE EXTENT SIZE                   GP05060
         LA    R5,DEBBASND DEBDASD    POINT TO FIRST EXTENT     GP05060
         SR    R15,R15                                          GP05060
         IC    R15,DEBNMEXT  # OF EXTENTS                       GP05060
         MH    R15,SZDEBEXT  GET OFFSET TO PDS INDICES          GP05060
         LA    R4,DEBBASND(R15)  POINT TO INDEX LIST            GP05060
         LTR   R15,R2        FIRST ONE ?                        GP09260
         BNZ   LNKBASLK      NO; CONVERT                        GP09260
         CLM   R8,7,TCBJLB+1      USING CVT LINK OR TCB STEPLIB GP09260
         BE    LNKBASTI           NO; USE TIOT LOOKUP           GP09260
         B     LNKBASND      YES
LNKBASLK IC    R15,0(R2,R4)   GET PDS INDEX                     GP05060
         CLM   R8,7,TCBJLB+1      USING CVT LINK OR TCB STEPLIB GP09260
         BNE   LNKBASOF                USE SLOW METHOD ( TIOT) GP09260
LNKBASTI L     R1,TCBTIO     GET TIOT                           GP09260
         ICM   R2,3,DCBTIOT-IHADCB(R8)   TIOT OFFSET            GP09260
         AR    R2,R1         TIOT ADDRESS                       GP09260
         SR    R14,R14                                          GP09260
         USING TIOENTRY,R2                                      GP09260
LNKTIOLP LTR   R15,R15       HAVE CORRECT ONE ?                 GP09260
         BNP   LNKTIOJF      YES; GET JFCB                      GP09260
         IC    R14,TIOELNGH  ENTRY LENGTH                       GP09260
         BCT   R15,LNKTIOLP                                     GP09260
LNKTIOJF ICM   R14,7,TIOEJFCB                                   GP09260
         ICM   R3,7,TIOEFSRT                                    GP09260
         USING UCBOB,R3      DECLARE IT                         GP05060
         MVC   LOCSER,UCBVOLI                                   GP09260
         MVC   LOCDSN,JFCBDSNM-JFCB+16(R14)                     GP09260
         B     FINDEX                                           GP09260
         DROP  R2,R3                                            GP09260
LNKBASOF MH    R15,SZDEBEXT  GET OFFSET                         GP05060
LNKBASND LA    R5,DEBBASND(R15)  POINT TO FIRST EXTENT THIS PDS GP05060
         DROP  R7                                               GP05060
         USING DEBDASD,R5    MAP INDIVIDUAL EXTENT              GP05060
         L     R3,DEBUCBAD   GET THIS UCB                       GP05060
         N     R3,=X'00FFFFFF'  CLEAN IT                        GP05060
         USING UCBOB,R3      DECLARE IT                         GP05060
         MVC   LOCSER,UCBVOLI  IDENTIFY VOLUME SERIAL           GP05060
         BAS   R14,FINDDSCB  LOCATE VOLUME AND UNIT             GP05060
         B     FINDEX        MAKE IT PRINTABLE                  GP05060
         SPACE 2                                                GP05060
FINDDSCB STM   R0,R15,SUBSAVE1  SAVE ALL REGISTERS              GP05060
         XC    CCHHR,CCHHR   CLEAR FOR ERROR MESSAGE            GP05060
         L     R14,=X'C1000000'   SEARCH                        GP05060
         LA    R15,DS1DSNAM   PHONY FORMAT 4 ADDRESS            GP05060
         LA    R0,LOCSER      VOLSER ADDRESS                    GP05060
         LA    R1,DS4IDFMT   OUTPUT+WORKAREA                    GP05060
         STM   R14,R1,LOOKFOUR    COMPLETE LIST                 GP05060
         MVI   DS1DSNAM,X'04'                                   GP05060
         MVC   DS1DSNAM+1(L'DS1DSNAM-1),DS1DSNAM                GP05060
         LR    R0,R3         SOME RELEASES OF OS EXPECT UCB ADDRESS
         OBTAIN LOOKFOUR     TRY IT                             GP05060
         CLI   DS4IDFMT,C'4' READ THE FORMAT 4 CORRECTLY ?      GP05060
         BNE   FINDEXIT      NO; TOO BAD                        GP05060
         MVC   HIGHMARK,DS4HPCHR  SAVE ADDR. OF LAST DSCB 1     GP05060
         MVC   HIGHR,DS4DEVDT   AND HIGH DSCB/TRK               GP05060
         MVC   HIGHTRK,DS4DEVSZ+2   TRACKS PER CYLINDER         GP05060
         MVC   CCHHR(4),DS4VTOCE+2  USE VTOC CCHHR              GP05060
         L     R14,=X'C0800000'   SEEK                          GP05060
         LA    R15,CCHHR      POINT TO REQUESTED CCHHR          GP05060
         LA    R0,LOCSER      AND VOLSER                        GP05060
         LA    R1,DS1DSNAM   AND INPUT AREA                     GP05060
         STM   R14,R1,LOOKCCHH    INTO OBTAIN LIST              GP05060
         MVI   CCHHR+4,1     SET FOR RECORD-1 IF NO FORMAT 5    GP05060
FINDNEXT LR    R0,R3         SOME RELEASES OF OS EXPECT UCB ADDRESS
         OBTAIN LOOKCCHH     LOOK FOR 1                         GP05060
         BXH   R15,R15,FINDEXIT QUIT WITH ERROR                 GP05060
         CLI   DS1FMTID,C'1'  DID WE GET ONE ?                  GP05060
         BNE   DSCBINCR      NO; IGNORE                         GP05060
         LA    R2,DS1EXT1    POINT TO FIRST EXTENT              GP05060
         USING MAPEXTNT,R2   MAP THE EXTENT                     GP05060
         CLI   XTTYPE,XTTLABEL  LABEL EXTENT ?                  GP05060
         BNE   CHEKSTRT                                         GP05060
         LA    R2,L'XTWHOLE(,R2)  SKIP LABEL EXTENT             GP05060
CHEKSTRT CLC   DEBSTRCC(4),XTLOCYL  SAME START?                 GP05060
         BE    FINDHAVE      TAKE GOOD EXIT                     GP05060
         SPACE 1                                                GP05060
DSCBINCR BAL   R9,CCINC      INCREMENT TO NEXT CCHHR            GP05060
         CLC   CCHHR,HIGHMARK  PAST LAST DSCB 1 ?               GP05060
         BNH   FINDNEXT      ELSE TRY AGAIN                     GP05060
         SPACE 1                                                GP05060
FINDHAVE MVC   LOCDSN,DS1DSNAM  SHOW NAME                       GP05060
FINDEXIT LM    R0,R15,SUBSAVE1  RESTORE REGISTERS               GP05060
         BR    R14           TAKE GOOD RETURN                   GP05060
         SPACE 2                                                GP05060
CCINC    SR    R1,R1                                            GP05060
         IC    R1,CCHHR+4    GET CURRENT RECORD NO.             GP05060
         LA    R1,1(,R1)     BUMP TO NEXT ONE                   GP05060
         STC   R1,CCHHR+4    SAVE IT                            GP05060
         CLM   R1,1,HIGHR    DID IT EXCEED MAX ?                GP05060
         BNHR  R9            NO, USE IT                         GP05060
         MVI   CCHHR+4,1     RESET TO REC. 1                    GP05060
         ICM   R1,3,CCHHR+2  GET TRACK NO.                      GP05060
         LA    R1,1(,R1)     BUMP BY ONE                        GP05060
         STCM  R1,3,CCHHR+2  SAVE IT                            GP05060
         CLM   R1,3,HIGHTRK  STILL IN THIS CYLINDER ?           GP05060
         BLR   R9            YES                                GP05060
         STCM  R1,12,CCHHR+2   ZERO TRACK NO.                   GP05060
         ICM   R1,3,CCHHR    GET CYLINDER                       GP05060
         CLM   R1,3,=X'FFFF'   LAST CYLINDER ON HUGE DISK?      GP05060
         BE    FINDEXIT      NO; QUIT                           GP05060
         LA    R1,1(,R1)     UP IT BY ONE                       GP05060
         STCM  R1,3,CCHHR                                       GP05060
         BR    R9            RETURN                             GP05060
FIND99   MVC   EYECATCH,BLANKS    NO MATCH                      GP09260
FINDEX   LM    R5,R8,TENWORDS     RESTORE                       GP09260
         END#PROC ,               RETURN                        GP09260
         POP   USING                                            GP09260
*
*----------------------------------------------------------------------
*        PRINT COM-REG
*----------------------------------------------------------------------
*
COMREG00 BEG#PROC
         L     R5,PSATOLD-PSA          MY TCB
         L     R5,TCBJSTCB-TCB(,R5)    JOB-STEP TCB
         L     R5,TCBJPQ-TCB(,R5)      TOP CDE (MOST RECENT)
         LTR   R5,R5                   EMPTY JPAQ?
         BZ    COMREG99                YES, EXIT
         USING CDENTRY,R5
*LOOP
COMREG12 CLC   =C'COM_REG ',CDNAME     IS THIS THE NEXT RB?
         BE    COMREG21                YES, EXIT LOOP
         ICM   R5,B'1111',CDCHAIN      PREVIOUS CDE
         BNZ   COMREG12                NOT FOUND YET, LOOP MORE
*ENDLOOP
         B     COMREG99                NOT FOUND, EXIT
COMREG21 L     R6,CDXLMJP              POINT TO XL
         USING XTLST,R6
         MVI   MINLINES,6
         CAL#PROC BLANK1          <== BLANK LINE
         STRING 'COM_REG',INTO=LINE
         CAL#PROC SPACE1          <== PRINT HEADER
         SLR   R0,R0
         ICM   R0,B'0111',XTLMSBLN     LENGTH
         L     R1,XTLMSBAD-1+&MVSXA    ADDRESS                  GP09255
         CAL#PROC PDUMP           <== PRINT COM-REG
COMREG99 END#PROC
         DROP  R5,R6
*
*----------------------------------------------------------------------
*        PRINT PROGRAM LINKAGE INFORMATION (SAVE AREA TRACE)
*----------------------------------------------------------------------
*
LINK00   BEG#PROC
         CAL#PROC BLANK1          <== BLANK LINE
         L     R6,TCBFSA               FIRST SAVE AREA
         LA    R7,STACK128             LOOP PREVENTION
         XC    0(256,R7),0(R7)         CLEAN UP
         MVI   MINLINES,10
         STRING '0SAVE AREA FORWARD TRACE (STARTING FROM TCBFSA)',     X
               INTO=LINE121
         CAL#PROC SPACE1          <== PRINT LINE
*LOOP
LINK10   CLM   R6,B'1000',=X'FF'       LO-SA FLAGGED?
         BNE   LINK11                  NO, JUMP
         ICM   R6,B'1000',X00          YES, ZERO IT
LINK11   LA    R6,0(,R6)               CLEAR HI-ORDER BIT
         CL    R6,REGION24             DOES THIS LOOK RIGHT?
         BL    LINK90                  NO, QUIT
         EX    R6,EXTM03               ANY LOW BITS ON ?        GP09260
         BNZ   LINK90                  NO; SKIP INVALID LINK    GP09260
        #S0C4  LINK90
         CLC   0(72,R6),0              VALIDATE 72 BYTES
        #S0C4  RESET
         CAL#PROC LINK200         <== EDIT SAVE AREA DATA
         LA    R0,128                  MAX NUMBER OF SAVE AREAS
         LA    R1,STACK128             START OF STACK
*--LOOP
LINK18   CLC   0(4,R1),8(R6)           ALREADY PROCESSED OR ZERO?
         BE    LINK90                  YES, QUIT
         LA    R1,4(,R1)               BUMP INDEX
         BCT   R0,LINK18               NEXT STACK ENTRY
*--ENDLOOP
         ST    R6,0(,R7)               REMEMBER THIS SAVE AREA
         CL    R6,CURR#R13             IS THIS THE CURRENT SAVE?
         BNE   LINK19                  NO, JUMP
         MVI   CURR#R13,FF             YES, REMEMBER WE SAW IT
LINK19   LR    R0,R6                   CURRENT SAVE AREA
         L     R6,8(,R6)               NEXT SAVE AREA
         CLR   R6,R0                   POINTS TO ITSELF?
         BE    LINK90                  YES, EXIT
         LA    R7,4(,R7)               NEXT STACK ENTRY
         B     LINK10                  DUMP IT TOO
*ENDLOOP
*        PROCESS SAVE AREA AT TIME OF ABEND, IF NOT PROCESSED ALREADY
*
LINK90   CLI   CURR#R13,FF             CURRENT SAVE AREA PROCESSED?
         BE    LINK99                  YES, EXIT
         L     R6,CURR#R13             POINT TO CURRENT SAVE AREA
         CL    R6,REGION24             DOES THIS LOOK RIGHT?
         BL    LINK99                  NO, QUIT
        #S0C4  LINK99
         CLC   0(72,R6),0              VALIDATE 72 BYTES
        #S0C4  RESET
         STRING '0CURRENT SAVE AREA (R13 FROM LAST PRB)',              X
               INTO=LINE121
         CAL#PROC SPACE1          <== PRINT LINE
         CAL#PROC LINK200         <== EDIT SAVE AREA DATA
LINK99   END#PROC
EXTM03   TM    =X'03',*-*    TEST LOW BITS IN REGISTER          GP09260
*
*----------------------------------------------------------------------
*        EDIT SAVE-AREA FIELDS
*----------------------------------------------------------------------
*
LINK200  BEG#PROC
         MVI   MINLINES,8
         MVC   WKCELL1,BLANKS          CLEAR WORK AREA
         L     R1,FRSTSVRB             ABEND SVRB
         CLM   R6,B'0111',RBGRS13+1-RBPREFIX(R1)  R13 AT TIME OF ABEND?
         BNE   LINK203                 NO, JUMP
         MVC   WKCELL1+3(3),=C'<=='    CURRENT SAVE AREA
LINK203  STRING INTO=LINE121,                                          X
               '0 SAVE AREA: ',((R6),,X),                              X
               '  WORD1: ',(0(R6),4,X),                                X
               '  PREVIOUS: ',(4(R6),4,X),                             X
               '  NEXT: ',(8(R6),4,X),WKCELL1
         CAL#PROC SPACE1          <== PRINT LINE
*
*        DECODE ENTRY ADDRESS
*
         MVC   VIACALL,=C'CALL '       CALL/LINK/SYNCH
         MVC   REGS(52),20(R6)         MOVE R0-R12
         MVC   REGS+56(8),12(R6)       MOVE R14-R15
         L     R14,CVTPTR          <== GET CVT ADDRESS
         LA    R0,CVTEXIT-CVTMAP(,R14) GET ADDR OF CVTEXIT FIELD
         CLM   R0,B'0111',13(R6)       IS THIS THE RETURN ADDRESS?
         BNE   LINK222                 NO, JUMP
         TM    12(R6),X'7F'            IS THIS A 24-BIT ADDR?
         BNZ   LINK222                 NO, JUMP
         MVC   VIACALL,=C'LINK '       YES, CHANGE 'CALL' TO 'LINK'
         B     LINK228
*
*        CHECK IF REGISTERS SAVED WITH "STM R14,R10,20(R13)"
*
LINK222 #S0C4  LINK228
**       L     R2,LASTPRB              POINT TO LAST PRB
**       CLI   RBOPSW+4-RBPREFIX(R2),0 AMODE24?
**       BNE   LINK228                 NO, JUMP
         L     R2,FRSTSVRB             POINT TO FIRST SVRB
**       CL    R6,RBGRS13-RBPREFIX(,R2) SAVE AREA AT TIME OF ABEND?
**       BNE   LINK228                 NO, JUMP
**       CLI   20(R6),X'00'            BAL/BALR?
**       BE    LINK228                 NO, JUMP
         LM    R14,R1,20(R6)           R14-R1 FROM R0-R3 SLOTS
         CLI   32(R6),FF               R1 IN COMPLEMENT FORM?
         BNE   LINK223                 NO, JUMP
         LPR   R1,R1                   YES, MAKE IT POSITIVE
*
LINK223  ICM   R1,B'1000',X00          CLEAN UP HI-ORDER BYTE
**       CL    R1,RBGRS1-RBPREFIX(,R2) IS THIS MY DCB ADDRESS?
**       BNE   LINK224                 NO, JUMP
         CLM   R15,B'0111',DCBPUT+1-IHADCB(R1)   R15 IN R1 SLOT?
         BE    LINK225                 YES, JUMP
*READ/WRITE/CHECK MACRO
         USING DECB,R1
LINK224  TM    DECBDCBA,X'7F'          VALID DCB ADDR?
         BNZ   LINK228                 NO, JUMP
         TM    DECBDCBA+3,X'03'        VALID DCB ADDR?
         BNZ   LINK228                 NO, JUMP
         L     R1,DECBDCBA             DCB ADDRESS FROM DECB
         DROP  R1                      WAS DECB
         CL    R1,REGION24             DOES THIS LOOK RIGHT?
         BL    LINK228                 NO, QUIT
         CL    R1,REGION24+4           DOES THIS LOOK RIGHT?
         BH    LINK228                 NO, QUIT
         CLM   R15,B'0111',DCBREAD+1-IHADCB(R1)  R15 IN R1 SLOT?
         BE    LINK225                 YES, JUMP
         CLM   R15,7,1+DCBCHECK-IHADCB(R1) DCBCHECK IN R1 SLOT? GP09260
         BNE   LINK228                 NO, JUMP
*
*        VALIDATE DCB/DEB POINTERS
*
LINK225  ICM   R14,B'0111',DCBDEBAD+1-IHADCB(R1)   DEB ADDRESS
         BZ    LINK228                 BAD, QUIT
         CLM   R1,B'0111',DEBDCBAD+1-DEBBASIC(R14) COMP W/ DCB ADDR
         BNE   LINK228                 BAD, QUIT
*
*        MAKE SURE R15 POINTS TO IGG019XX
*
         LR    R1,R15                  PASS EP ADDR
         ICM   R1,B'1000',X00          CLEAN UP
         CAL#PROC CSVQRY00        <== FIND LOAD MODULE
         LTR   R5,R5                   CDE FOUND?
         BZ    LINK228                 NO, JUMP
         CLC   =C'IGG019',CDNAME-CDENTRY(R5)
         BNE   LINK228                 NO, JUMP
         MVC   REGS(44),28(R6)                   MOVE R0-R10
         MVC   REGS+44(8),RBGRS11-RBPREFIX(R2)        R11-R12
         MVC   REGS+56(8),20(R6)                      R14-R15
         B     LINK231                                          @921003
*
LINK228 #S0C4  LINK237
         ICM   R1,B'1111',REGS+60      R15 (ENTRY ADDRESS)
         BZ    LINK241                 NO EP ADDR, JUMP
         CLM   R1,B'1111',BLANKS       R15 (ENTRY ADDRESS)
         BE    LINK241                 NO EP ADDR, JUMP
         TM    REGS+56,X'80'           AMODE31 IN R14?
         BZ    LINK228C                NO, JUMP
         TM    REGS+56,X'40'           BAL R14,8(,R15) ?
         BO    LINK228C                YES, JUMP
         CAL#PROC CSVQRY00        <== FIND LOAD MODULE
         LTR   R5,R5                   CDE FOUND?
         BP    LINK231                 YES, JUMP
LINK228C MVI   MASKADDR,X'00'          24-BIT MODE
         L     R1,REGS+60              R15 (ENTRY ADDRESS)
         CAL#PROC CSVQRY00        <== FIND LOAD MODULE
         LTR   R5,R5                   CDE FOUND?
         BNP   LINK237                 NO, JUMP
*
*        EDIT EYE CATCHER AT ENTRY POINT
*
LINK231  MVC   EYECATCH,BLANKS
         TM    REGS+63,1               R15 HAS ODD VALUE?
         BO    LINK234                 YES, DO NOT IDENTIFY ENTRY ADDR
         L     R1,REGS+60              R15 (ENTRY ADDRESS)
         N     R1,MASKADDR             CLEAN-UP ADDRESS
        #S0C4  LINK234
         CLC   0(24,R1),0              VALIDATE 24 BYTES
        #S0C4  RESET
*SUB-ROUTINE: B 4(15)
LINK232B LA    R0,64                   DEFAULT LENGTH FOR EYE-CATCHER
         LA    R2,4(,R1)               FIRST BYTE OF EYE-CATCHER
         CLC   =X'47FF00',0(R1)        IS THIS A BRANCH?
         BE    LINK233                 YES, JUMP
*OS/VS COBOL INIT1
LINK232C LA    R0,8+4                  MODULENMVSR1
         LA    R2,12(,R1)              FIRST BYTE OF EYE-CATCHER
         CLC   =C'VSR1',8(R2)          OS/VS INIT1 SIGNATURE?
         BE    LINK233                 YES, JUMP
*SAVE MACRO
LINK232S IC    R0,4(,R1)               LENGTH OF EYE CATCHER
         LA    R2,5(,R1)               FIRST BYTE OF EYE-CATCHER
         CLC   =X'47F0',0(R1)          IS THIS A BRANCH?
         BNE   LINK232P                NO, JUMP
         CLC   =X'47F0',4(R1)          SECOND BRANCH?           GP09260
         BE    LINK232P                YES; NOT EYE CATCHER     GP09260
         TM    2(R1),X'F0'             IS THIS A BRANCH?
         BO    LINK233                 YES, JUMP
*PROGID MACRO
LINK232P LA    R0,X'001A'              EYE CATCHER LENGTH
         LR    R2,R1                   ENTRY ADDRESS (R15)
         SLR   R2,R0                   POINT TO CSECT ORIGIN
         CLC   0(4,R1),=X'90EC,D00C'   PROGID PROLOG?
         BNE   LINK234                 NO, JUMP
         CLC   8(10,R1),=X'50E0D008,50D0E004,18DE'
         BNE   LINK234                 NO, JUMP
         CLC   20(4,R1),=X'41F0001A'   PROGID PROLOG?
         BNE   LINK234                 NO, JUMP
*
LINK233  STRING ', EYE CATCHER IS "',((R2),(R0)),'"',INTO=EYECATCH
         TR    EYECATCH,TRTPRINT       KEEP ONLY PRINTABLE CHARACTERS
*
*INK234  STRING 2X,EPNAME,' WAS ENTERED VIA ',VIACALL,                 X
               ' AT OFFSET ',OFFSET,EYECATCH,INTO=LINE
LINK234  STRING 1X,(EPNAME,,T),(OFFSET,,T),                            X
               ' WAS ENTERED VIA ',(VIACALL,,T),EYECATCH,INTO=LINE
         CAL#PROC SPACE1L         <== PRINT WITH LOWER-CASE
         B     LINK241
*
LINK237  STRING ' *UNKNOWN WAS ENTERED VIA ',VIACALL,                  X
               ' AT LOCATION ',(REGS+60,4,X),INTO=LINE
         CAL#PROC SPACE1          <== PRINT LINE
*
*        DECODE RETURN ADDRESS
*
LINK241  MVC   EYECATCH,BLANKS         CLEAR WORK AREA
         CLI   VIACALL,C'C'            WAS IT A CALL?
         BNE   LINK261                 NO, JUMP
         L     R1,REGS+56              RETURN ADDRESS
         SRL   R1,1                    CLEAN UP "T" BIT
         SLL   R1,1                    CLEAN UP "T" BIT
         CAL#PROC CSVQRY00        <== FIND LOAD MODULE
         LTR   R5,R5                   CDE FOUND?
         BP    LINK248                 YES, JUMP
         SLR   R1,R1
         ICM   R1,B'0111',REGS+57 TRY 24-BIT ADDRESS
         CAL#PROC CSVQRY00        <== FIND LOAD MODULE
         LTR   R5,R5                   CDE FOUND?
         BZ    LINK261                 NO, JUMP
LINK248  STRING 4X,'RETURN ADDRESS IS ',MSG20,INTO=EYECATCH
*
LINK261  STRING '0   GPR00-03: ',(REGS,4,X),2X,(REGS+4,4,X),2X,        X
               (REGS+8,4,X),2X,(REGS+12,4,X),INTO=LINE121
         CAL#PROC SPACE1          <== PRINT LINE
         STRING '   GPR04-07: ',(REGS+16,4,X),2X,(REGS+20,4,X),2X,     X
               (REGS+24,4,X),2X,(REGS+28,4,X),INTO=LINE
         CAL#PROC SPACE1          <== PRINT LINE
         STRING '   GPR08-11: ',(REGS+32,4,X),2X,(REGS+36,4,X),2X,     X
               (REGS+40,4,X),2X,(REGS+44,4,X),INTO=LINE
         CAL#PROC SPACE1          <== PRINT LINE
         STRING '   GPR12-15: ',(REGS+48,4,X),12X,                     X
               (REGS+56,4,X),2X,(REGS+60,4,X),EYECATCH,INTO=LINE
         CAL#PROC SPACE1          <== PRINT LINE
LINK299  END#PROC
*
*----------------------------------------------------------------------
*        PRINT OPEN FILES
*----------------------------------------------------------------------
*
         PUSH  USING                                            GP09260
DEB000   BEG#PROC
         ICM   R5,B'1111',TCBDEB       FIRST DEB
         BZ    DEB099                  NO DEB, JUMP
*LOOP
DEB011   SH    R5,=AL2(DEBBASIC-DEBPREFX) POINT TO DEB PREFIX
         USING DEBPREFX,R5
         MVI   MINLINES,9
         LA    R0,DEBBASIC             POINT TO BASIC SECTION
         CLM   R0,B'0111',DCBDEBAD+1-IHADCB+DCBDEBUG  IS IT MY OWN DCB?
         BE    DEB080                  YES, SKIP IT
         ST    R0,WKCELL1+4            DEB ADDRESS FOR $PRINT
*
         BAS   R3,DEB032
         DC    C'VSAM',AL1(DEBAMVSM)
         DC    C'EXCP',AL1(DEBAMXCP)
         DC    C'SAM ',AL1(DEBAMSAM)
         DC    C'BDAM',AL1(DEBAMBDM)
         DC    C'JES ',AL1(DEBAMSUB)
         DC    C'VTAM',AL1(DEBAMVTM)
         DC    C'????',0H'0'
*--LOOP
DEB032   CLC   DEBAMTYP,4(R3)          IS THIS MY AMTYPE?
         BE    DEB040                  YES, JUMP
         LA    R3,4+1(,R3)             BUMP TABLE PTR
         CLI   0(R3),C'?'              END OF TABLE?
         BNE   DEB032                  NEXT TABLE ENTRY
*--ENDLOOP
DEB040   SLR   R6,R6                   DCB/ACB ADDR SET BY VALDCB RTNE
         CAL#PROC SWA000          <== LOCATE DEB/DSAB/SIOT/JFCB/TIOT
         CAL#PROC VALDCB          <== LOCATE/VALIDATE DCB (OR ACB)
         LTR   R6,R6                   CHECKED OUT OK?
         BZ    DEB048                  NO, SKIP IT
         ST    R6,WKCELL1              FOR $PRINT
         LH    R0,DCBTIOT-IHADCB(,R6)  GET TIOT OFFSET FOR THIS DCB
         N     R0,=X'0000FFFF'                                  GP09260
         CAL#PROC EXCP00          <== GET EXCP COUNT IN R0
         B     DEB050
*
DEB048   MVC   WKCELL1(4),DEBDCBAD     FOR $PRINT
         MVC   DDNAME2(8),=C'********' DCB IS BAD
         SLR   R0,R0                   EXCP=0
         ICM   R15,B'1111',$SIOT       SIOT ADDR GOOD?
*EX 0,*
         BNP   DEB050                  NO, EXIT
         MVC   DDNAME2(8),SCTDDNAM-SIOT(R15)
*
  AIF (NOT &MVSXA).DFIOCNT     *DEFER*                          GP09255
         L     R1,PSATOLD-PSA          MY TCB
         L     R1,TCBTCT-TCB(,R1)      TCBTCT
         USING SMFTCT,R1
         L     R1,TCTIOTBL             START OF I/O MEASUREMENT TABLE
         USING TCTTIOT,R1
         ICM   R15,B'1111',$DSAB       DSAB POINTER
         BNP   DEB050                  NO, EXIT
         USING DSAB,R15
         LH    R15,DSABTCTL            OFFSET TO TCT DD ENTRY
         LA    R15,TCTTIOT(R15)        DD LOOKUP TABLE ENTRY
         USING TCTIODSP,R15
         LH    R15,TCTIOTSD            OFFSET TO I/O CTR ENTRY  GP09260
         LA    R1,TCTTIOT(R15)         ADDRESS OF I/O CTR ENTRY
         USING TCTDDENT,R1
         L     R0,TCTDCTR              EXCP COUNT
         DROP  R15,R1
.DFIOCNT ANOP  ,             *DEFER*                            GP09255
*
DEB050   IC    R1,DEBOPATB             OPEN OPTION
         LA    R15,X'0F'               MASK FOR 'NR'
         NR    R1,R15                  KEEP ONLY X'0F' BITS
         IC    R1,OPENOPT1(R1)         GET OFFSET TO OPENOPT2 ENTRY
         LA    R1,OPENOPT2(R1)         POINT TO OPEN OPTION
*
         CAL#PROC BLANK1          <== BLANK LINE
         CLI   DEBAMTYP,DEBAMVSM       VSAM?
         BE    DEB060                  YES, JUMP
         CLI   DEBAMTYP,DEBAMSUB       SUB-SYSTEM DATA SET?
         BE    DEB070                  YES, JUMP
***      STRING '0',INTO=LINE121,                                     X
***            ((R3),4),' DEB: ',(WKCELL1+5,3,X),                     X
***            2X,((R1),6),            IN/OUT/UPD                     X
***            '  DCB: ',(WKCELL1+1,3,X),                             X
***            '  DDN: ',DDNAME2,                                     X
***            '  EXCP: ',((R0),,L)
         STRING '0DEB=',(WKCELL1+5,3,X),',',((R3),4,T),',',((R1),6,T), +
               ',DCB=',(WKCELL1+1,3,X),',DDN=',(DDNAME2,,T),           +
               ',EXCP=',((R0),,L),INTO=LINE121
         TM    DEBFLGS1,DEBEOFDF       EOF FLAG ON?     IECEQU=DEBF1EOF
*LASTVOL BO    DEB056                  YES, JUMP
*LASTVOL TM    DEBOFLGS,DEBEOF         EOF FLAG ON?     IECEQU=DEBOFEOF
         BZ    DEB057                  NO, JUMP
DEB056   LA    R15,LINE121(R15)        END OF TEXT
         MVC   0(4,R15),=C',EOF'       APPEND INDICATOR
DEB057   CAL#PROC SPACE1          <== PRINT LINE
         CAL#PROC FORMDEB    FORMAT DEB                         GP09260
         CLI   DDNAME2,C'*'            DCB OK?
         BE    DEB080                  NO, EXIT
         CAL#PROC PRDCB           <== PRINT DCB DATA
         B     DEB080
*
OPENOPT1 DC    AL1(0,06,0,12,18,0,0,24,0,0,0,0,0,0,30,36)
OPENOPT2 DC    C'INPUT RDBACKINOUT UPDAT OUTIN EXTENDOUTPUT'
*
DEB060   STRING '0DEB=',(WKCELL1+5,3,X),                           VSAM+
               ',',((R3),4,T),         VSAM                            +
               ',ACB=',(WKCELL1,4,X),                                  +
               ',DDN=',(DDNAME2,,T),                                   +
               ',EXCP=',((R0),,L),                                     +
               INTO=LINE121
         CAL#PROC SPACE1          <== PRINT LINE
         CAL#PROC FORMDEB    FORMAT DEB                         GP09260
         CLI   DDNAME2,C'*'            ACB OK?
         BE    DEB080                  NO, EXIT
         CAL#PROC PRACB           <== PRINT ACB/PLH (VSAM/JES)
         B     DEB080
*
DEB070   STRING '0DEB=',(WKCELL1+5,3,X),                         SUBSYS+
               ',',((R3),4,T),         SUBSYS                          +
               ',',((R1),6,T),         INPUT/OUTPUT/UPDATE/EXTEND/OUTIN+
               ',ACB=',(WKCELL1,4,X),                                  +
               ',DDN=',(DDNAME2,,T),                                   +
               ',EXCP=',((R0),,L),                                     +
               INTO=LINE121
         CAL#PROC SPACE1          <== PRINT LINE
         CAL#PROC FORMDEB    FORMAT DEB                         GP09260
         CLI   DDNAME2,C'*'            ACB OK?
         BE    DEB080                  NO, EXIT
         CAL#PROC PRACB           <== PRINT ACB/PLH (VSAM/JES)
*
DEB080   ICM   R5,B'0111',DEBDEBAD+1   NEXT DEB IN CHAIN
         BNZ   DEB011                  TRY NEXT DCB
*ENDLOOP
DEB099   END#PROC
         SPACE 2
FORMDEB  BEG#PROC ,
         STM   R0,R5,SUBSAVE1                                   GP09260
         STRING '0   -10 ',38X,(DEBPREFX+0,4,X),1X,(DEBPREFX+4,4,X),1X,X
               (DEBPREFX+8,4,X),1X,(DEBPREFX+12,4,X),INTO=LINE121
         CAL#PROC SPACE1          <== PRINT LINE
         SR    R0,R0                                            GP09260
         IC    R0,DEBLNGTH                                      GP09260
         SLL   R0,3          CONVERT FROM DOUBLE WORDS TO BYTES GP09260
         SH    R0,=H'16'     ALLOW FOR PREFIX LENGTH            GP09260
         LA    R1,DEBBASIC             ADDRESS                  GP09260
         O     R1,=X'80000000'         SUPPRESS DOUBLE SPACE    GP09260
         CAL#PROC DUMP32          <== PRINT IT                  GP09260
         LM    R0,R5,SUBSAVE1                                   GP09260
         END#PROC ,                                             GP09260
         POP   USING                                            GP09260
*
*----------------------------------------------------------------------
*        PRINT SDWA
*----------------------------------------------------------------------
*
PRWA00   BEG#PROC
         STRING '0SCHEDULER WORK AREA',INTO=LINE121
         CAL#PROC SPACE1          <== PRINT LINE
         LA    R0,SDWALEN              LENGTH
         LA    R1,SDWA                 ADDRESS
         CAL#PROC DUMP32          <== PRINT SDWA
PRWA99   END#PROC
*
*----------------------------------------------------------------------
*        GET EXCP COUNT FROM TCT
*----------------------------------------------------------------------
*
         PUSH  USING                                            GP09260
EXCP00   BEG#PROC
         L     R1,PSATOLD-PSA          MY TCB
         L     R1,TCBTCT-TCB(,R1)      TCBTCT
         L     R1,TCTIOTBL-SMFTCT(,R1) START OF I/O MEASUREMENT TABLE
         LA    R1,TCTIODSP-TCTTIOT(,R1) FIRST DD ENTRY
         USING TCTDCBTD,R1
*LOOP
EXCP11   CH    R0,TCTDCBTD             SAME TIOT OFFSET?        GP09260
         BE    EXCP21                  YES, EXIT LOOP
         LA    R1,TCTDCBLE             NEXT LOOKUP TABLE ENTRY
         ICM   R15,B'1111',TCTDCBLE    END OF TABLE ?
         BNZ   EXCP11                  NOT YET, TRY NEXT DD ENTRY
*ENDLOOP
         SLR   R0,R0                   EXCP COUNT IS ZERO
         B     EXCP99
*
EXCP21   LH    R1,TCTIOTSD             OFFSET IN I/O MEASURE.   GP09255
*XCP21   L     R1,TCTIOTSD             OFFSET IN I/O MEASURE. TABLE
         L     R15,PSATOLD-PSA         MY TCB
         L     R15,TCBTCT-TCB(,R15)    MY TCT
         A     R1,TCTIOTBL-SMFTCT(,R15) START OF I/O MEASUREMENT TABLE
         USING TCTDDENT,R1
         ICM   R0,B'1111',TCTDCTR      EXCP COUNT IN R0
EXCP99   LR    R1,R0                   EXCP COUNT IN R1 TOO
         END#PROC
         LTORG ,                                                GP09260
         POP   USING                                            GP09260
*
*----------------------------------------------------------------------
*        CONVERT UCBTYP TO UNITNAME
*----------------------------------------------------------------------
*
         PUSH  USING                                            GP09260
GETUNIT  BEG#PROC
         USING UCBOB,R3                                         GP09260
         LA    R15,TYPETBLE        DEVICE TYPE TABLE            GP09260
         ICM   R0,3,UCBTBYT3       LOAD MAJOR TYPE              GP09260
GETU20   CLI   0(R15),X'FF'        TABLE END ??                 GP09260
         BE    GETUNIT6            PRINT UNKNOWN DEVICE         GP09260
         CLM   R0,3,0(R15)         IF DEVICE FOUND              GP09260
         BE    GETU30              PRINT DESC.                  GP09260
         LA    R15,TYPETBL2-TYPETBLE(,R15)   NEXT TABLE ENTRY   GP09260
         B     GETU20              LOOP                         GP09260
GETU30   CLI   UCBTBYT3,X'10'  GRAPHICS ?                       GP09260
         BNE   GETU30C       NO                                 GP09260
         CLI   UCBTBYT4,X'09'  3277/78 ?                        GP09260
         BE    GETU30T       YES; TEST CONTROLLER               GP09260
         CLI   UCBTBYT4,X'0B'  3286/87 ?                        GP09260
         BNE   GETU30C                                          GP09260
GETU30T  TM    UCBAOF1,UCBOFPTR  PREPARE-TO-READ ?              GP09260
         BZ    GETU30C       NO                                 GP09260
         LA    R15,TYPETBL2-TYPETBLE(,R15)   NEXT TABLE ENTRY   GP09260
GETU30C  MVC   UNITNAME(6),2(R15)                               GP09260
GETU36   CLI   UCBTBYT3,UCB3COMM  COM DEVICE ??                 GP09260
         BNE   GETU60                                           GP09260
         LA    R15,COMMTBLE         GET COMM TYPE TABLE         GP09260
GETU40   ICM   R0,2,UCBTBYT1       MISH-MASH                    GP09260
         IC    R0,UCBTBYT4         COMM IS FUNNY                GP09260
         LH    R1,0(,R15)                                       GP09260
         NR    R0,R1               MASK                         GP09260
         CLM   R0,3,0(R15)          MATCH ?                     GP09260
         BE    GETU45        ALL SET - MOVE TO NAME             GP09260
         LA    R15,TYPETBL2-TYPETBLE(,R15)   NEXT TABLE ENTRY   GP09260
         CLI   0(R15),X'FF'   ANY MORE ?                        GP09260
         BNE   GETU40        YES                                GP09260
         B     GETUNIT9      NO; KEEP COMM ID                   GP09260
GETU45   MVC   UNITNAME(6),2(R15)  PRINT TP DEVICE              GP09260
         B     GETUNIT9                                         GP09260
GETU60   TM    UCBTBYT2,UCBRVDEV   REAL DEVICE ?                GP09260
         BZ    GETUNIT9      YES ?                              GP09260
         MVI   UNITNAME+6,C'V'                                  GP09260
GETU300  B     GETUNIT9                                         GP09260
         SPACE 1
*DEFER*  MVI   TENWORDS+20,X'01'       RETURN A LOOK-UP VALUE (BIT7)
*DEFER*  MVI   TENWORDS+21,X'00'       CLEAR UNUSED BYTE
*DEFER*  LA    R14,UNITNAME        <== UNITNAME+DEVTYPE
*DEFER*  LA    R15,TENWORDS+20         FLAGS
*DEFER*  STM   R14,R15,TENWORDS    <==
*DEFER*  OI    TENWORDS+4,X'80'        END-OF-LIST FLAG
*DEFER*  LA    R1,TENWORDS             PARM LIST ADDRESS
*DEFER*  L     R15,IEFEB4UV            LOAD ROUTINE ADDRESS
*DEFER*  BALR  R14,R15             <== GET UNIT NAME
*DEFER*  LTR   R15,R15
*DEFER*  BNZ   GETUNIT6                BAD RETURN CODE, QUIT
*DEFER*  MVI   TENWORDS+20,X'20'       RETURN A UNIT NAME (BIT2)
*DEFER*  LA    R1,TENWORDS             PARM LIST ADDRESS
*DEFER*  L     R15,IEFEB4UV            LOAD ROUTINE ADDRESS
*DEFER*  BALR  R14,R15             <== GET UNIT NAME
*DEFER*  LTR   R15,R15
*DEFER*  BZ    GETUNIT9                GOOD RETURN CODE, GOBACK
GETUNIT6 MVC   UNITNAME,BLANKS         CONVERSION DID NOT WORK
GETUNIT9 DS    0H
         END#PROC
         SPACE 1                                                GP09260
COMMTBLE DC    X'0810',CL6'2740X '                              GP09260
         DC    X'0790',CL6'BSC3  '                              GP09260
         DC    X'0710',CL6'1050X '                              GP09260
         DC    X'0690',CL6'BSC2  '                              GP09260
         DC    X'0610',CL6'2741P '                              GP09260
         DC    X'0590',CL6'BSC1  '                              GP09260
         DC    X'0510',CL6'2741C '                              GP09260
         DC    X'0410',CL6'2740  '                              GP09260
         DC    X'0310',CL6'2740CC'                              GP09260
         DC    X'0240',CL6'115A  '                              GP09260
         DC    X'0210',CL6'1060  '                              GP09260
         DC    X'0180',CL6'2260  '                              GP09260
         DC    X'0170',CL6'3270  '                              GP09260
         DC    X'0160',CL6'WTTA  '                              GP09260
         DC    X'0150',CL6'TWX   '                              GP09260
         DC    X'0140',CL6'83B3  '                              GP09260
         DC    X'0130',CL6'1050  '                              GP09260
         DC    X'0120',CL6'1030  '                              GP09260
         DC    X'0110',CL6'1050  '                              GP09260
         DC    X'FF'                                            GP09260
         SPACE 1                                                GP09260
TYPETBLE DC    X'0801',CL6'2540R'                               GP09260
TYPETBL2 DC    X'0802',CL6'2540P'                               GP09260
         DC    X'0803',CL6'1443'                                GP09260
         DC    X'0804',CL6'2501'                                GP09260
         DC    X'0805',CL6'2520'                                GP09260
         DC    X'0806',CL6'3505'                                GP09260
         DC    X'0808',CL6'1403'                                GP09260
         DC    X'0809',CL6'3211'                                GP09260
         DC    X'080A',CL6'1443'                                GP09260
         DC    X'080B',CL6'3203'                                GP09260
         DC    X'080C',CL6'3525'                                GP09260
         DC    X'080E',CL6'3800'                                GP09260
         DC    X'080F',CL6'AFPrt'                               GP09260
         DC    X'0810',CL6'2671'                                GP09260
         DC    X'0811',CL6'4245'                                GP09260
         DC    X'0813',CL6'4248'                                GP09260
         DC    X'0816',CL6'3890'                                GP09260
         DC    X'0817',CL6'3886'                                GP09260
         DC    X'0818',CL6'2495'                                GP09260
         DC    X'0819',CL6'3895'                                GP09260
         DC    X'081A',CL6'1285'                                GP09260
         DC    X'081B',CL6'1287'                                GP09260
         DC    X'081C',CL6'1288'                                GP09260
         DC    X'081D',CL6'1419P'                               GP09260
         DC    X'081E',CL6'1419S'                               GP09260
         DC    X'081F',CL6'1275'                                GP09260
         DC    X'0820',CL6'1052'                                GP09260
         DC    X'0821',CL6'2150'                                GP09260
         DC    X'0822',CL6'3210'                                GP09260
         DC    X'0823',CL6'3215'                                GP09260
         DC    X'0830',CL6'3213'                                GP09260
         DC    X'083D',CL6'7443'                                GP09260
         DC    X'0842',CL6'3851 '                               GP09260
         DC    X'0844',CL6'3540'                                GP09260
         DC    X'084C',CL6'3838'                                GP09260
         DC    X'8001',CL6'2400'                                GP09260
         DC    X'8003',CL6'3400'                                GP09260
         DC    X'8080',CL6'3480'                                GP09260
         DC    X'8081',CL6'3490'                                GP09260
         DC    X'8082',CL6'3423'                                GP09260
         DC    X'8083',CL6'3590'                                GP09260
         DC    X'2001',CL6'2311'                                GP09260
         DC    X'2002',CL6'2301'                                GP09260
         DC    X'2003',CL6'2303'                                GP09260
*OLD*    DC    X'2004',CL6'2302'                                GP09260
         DC    X'2004',CL6'9345'                                GP09260
         DC    X'2005',CL6'2321'                                GP09260
         DC    X'2006',CL6'2305-1'                              GP09260
         DC    X'2007',CL6'2305-2'                              GP09260
         DC    X'2008',CL6'2314'                                GP09260
         DC    X'2009',CL6'3330'                                GP09260
         DC    X'200A',CL6'3340'                                GP09260
         DC    X'200B',CL6'3350'                                GP09260
         DC    X'200C',CL6'3375'                                GP09260
         DC    X'200D',CL6'3330-1'                              GP09260
         DC    X'200E',CL6'3380'                                GP09260
         DC    X'200F',CL6'3390'                                GP09260
         DC    X'1002',CL6'2250'                                GP09260
         DC    X'1003',CL6'2260'                                GP09260
         DC    X'1004',CL6'1053'                                GP09260
         DC    X'1005',CL6'2280'                                GP09260
         DC    X'1006',CL6'2282'                                GP09260
*OLD*    DC    X'1007',CL6'Mdl85C'  CONSOLE                     GP09260
         DC    X'1008',CL6'3066'    165/168 CONSOLE             GP09260
         DC    X'1009',CL6'3277'           IF PTREAD OPTION OFF GP09260
         DC    X'1009',CL6'3278'           IF PTREAD OPTION ON  GP09260
         DC    X'100A',CL6'3284'                                GP09260
         DC    X'100B',CL6'3286'           IF PTREAD OFF        GP09260
         DC    X'100B',CL6'3287'           IF PTREAD ON         GP09260
         DC    X'100C',CL6'3158'                                GP09260
         DC    X'100D',CL6'3036'                                GP09260
         DC    X'100E',CL6'3138'                                GP09260
         DC    X'100F',CL6'3148'                                GP09260
         DC    X'4011',CL6'2702B1'                              GP09260
         DC    X'4012',CL6'2701B1'                              GP09260
         DC    X'4013',CL6'2703B1'                              GP09260
         DC    X'4014',CL6'2955'                                GP09260
         DC    X'401F',CL6'5098'                                GP09260
         DC    X'4015',CL6'3705-1'                              GP09260
         DC    X'4021',CL6'2702B2'                              GP09260
         DC    X'4022',CL6'2701B2'                              GP09260
         DC    X'4023',CL6'2703B2'                              GP09260
         DC    X'4025',CL6'3705-2'                              GP09260
         DC    X'4031',CL6'2702TTY'                             GP09260
         DC    X'4032',CL6'2701TTY'                             GP09260
         DC    X'4033',CL6'2703TTY'                             GP09260
         DC    X'4041',CL6'2702T1'                              GP09260
         DC    X'4042',CL6'2701T1'                              GP09260
         DC    X'4043',CL6'2703T1'                              GP09260
         DC    X'4051',CL6'2702T2'                              GP09260
         DC    X'4052',CL6'2701T2'                              GP09260
         DC    X'4053',CL6'2703T2'                              GP09260
         DC    X'4061',CL6'2702WT'                              GP09260
         DC    X'4062',CL6'2701WT'                              GP09260
         DC    X'4063',CL6'2703WT'                              GP09260
         DC    X'4071',CL6'2702S1'                              GP09260
         DC    X'4072',CL6'2701S1'                              GP09260
         DC    X'4073',CL6'2703S1'                              GP09260
         DC    X'4081',CL6'2702B3'                              GP09260
         DC    X'4082',CL6'2701B3'                              GP09260
         DC    X'4083',CL6'2703B3'                              GP09260
         DC    X'4091',CL6'2702S2'                              GP09260
         DC    X'4092',CL6'2701S2'                              GP09260
         DC    X'4093',CL6'2703S2'                              GP09260
         DC    X'40F1',CL6'3791L'                               GP09260
         DC    X'4100',CL6'CTCA'                                GP09260
         DC    X'4101',CL6'CTC-S'                               GP09260
         DC    X'4102',CL6'CTC-E'                               GP09260
         DC    X'4103',CL6'RS6CTC'                              GP09260
         DC    X'4104',CL6'3172 CTC'                            GP09260
         DC    X'4105',CL6'OSA'                                 GP09260
         DC    X'4106',CL6'OSADIA'                              GP09260
         DC    X'4107',CL6'OSA 7'                               GP09260
         DC    X'4108',CL6'OSA 8'                               GP09260
         DC    X'4109',CL6'OSA 9'                               GP09260
         DC    X'410A',CL6'OSA A'                               GP09260
         DC    X'410B',CL6'OSA B'                               GP09260
         DC    X'410C',CL6'OSA C'                               GP09260
         DC    X'410D',CL6'OSA D'                               GP09260
         DC    X'410E',CL6'OSA E'                               GP09260
         DC    X'410F',CL6'OSA F'                               GP09260
         DC    X'0000',CL6'DUMMY'                               GP09260
         DC    X'0100',CL6'DUMMY'                               GP09260
UNKNOWN  DC    X'FFFF',CL6' '   UNKNOWN DEVICE TYPE'            GP09260
         POP   USING                                            GP09260
         SPACE 1
*
*----------------------------------------------------------------------
*        VALIDATE DCB, FIND TIOT OFFSET
*----------------------------------------------------------------------
*
         PUSH  USING                                            GP09260
VALDCB   BEG#PROC
        #S0C4  VALDCB8
         CLI   $ACB,FF                 VSAM/SUBSYS?
         BNZ   VALDCB3                 YES, JUMP
         USING DEBPREFX,R5
         USING IHADCB,R6
         TM    DCBTIOT,X'C0'           IS DCB OPEN?
         BNZ   VALDCB5                 NO, JUMP
*
*        VALIDATE DCB/DEB CROSS POINTERS
*
         CLI   OCE#CODE,C'O'           OPEN SVC?                @921015
         BE    VALDCB3                 YES, SKIP DEB VALIDATION
         LTR   R5,R5                   DEB ADDRESS SET ALREADY?
         BNZ   VALDCB3                 YES, SKIP DEB VALIDATION
         SLR   R1,R1
         ICM   R1,B'0111',DCBDEBAD+1   PICK UP DEB ADDRESS
         BZ    VALDCB8                 BAD, QUIT
         CLM   R6,B'0111',DEBDCBAD+1-DEBBASIC(R1) COMP W/ DCB ADDR
         BNE   VALDCB8                 BAD, QUIT
         SLR   R5,R5
         ICM   R5,B'0111',DCBDEBAD+1   GET DEB ADDRESS          @920408
         SH    R5,=AL2(DEBBASIC-DEBPREFX) POINT TO DEB PREFIX   @920408
*
*        LOCATE CORRESPONDING TIOT ENTRY
*
VALDCB3  L     R1,TCBTIO               TIOT ADDRESS
         LA    R1,0024(,R1)            FIRST DD ENTRY
*LOOP
VALDCB3B CLI   4(R1),C' '              BLANK DDNAME?
         BNH   VALDCB3F                YES, JUMP
         MVC   DDNAME,4(R1)            SAVE DDNAME
         SLR   R15,R15                 CONCAT#
         B     VALDCB3G
VALDCB3F LA    R15,1(,R15)             BUMP CONCAT#
VALDCB3G CL    R1,$TIOT                IS IT MY DD ENTRY?
         BE    VALDCB4                 YES, QUIT
         SLR   R0,R0
         IC    R0,0(,R1)               LOAD TIOELNGH
         AR    R1,R0                   NEXT TIOT ENTRY
         CLI   0(R1),00                END-OF-TIOT REACHED?
         BNE   VALDCB3B                NOT YET, LOOP FURTHER
*ENDLOOP
         B     VALDCB8                 END OF TIOT, QUIT
*
VALDCB4  MVC   DDNAME2,BLANKS          CLEAR WORK AREA
         MVC   DDNAME2(8),DDNAME       SAVE DDNAME
         LTR   R0,R15                  PASS CONCAT#
         BZ    VALDCB9                 FIRST IN CONCAT, JUMP
         STRING (DDNAME,,T),'+',((R0),,R3Z),INTO=DDNAME2 SYSIN+002
         B     VALDCB9
*
*        NON-OPEN DCB
*
VALDCB5  CLI   DCBOFLGS,2              OPEN FLAGS OK?
         BNE   VALDCB8                 NO, JUMP
         MVC   DDNAME,DCBDDNAM         SAVE DDNAME
         MVC   DDNAME2,DCBDDNAM        SAVE DDNAME
         TR    DDNAME2,TRTPRINT
         CLC   DDNAME2,DCBDDNAM        GOOD DDNAME?
         BE    VALDCB9                 YES, JUMP
*
VALDCB8  SR    R6,R6                   NOT A DCB, GOBACK
         MVC   DDNAME,BLANKS           CLEAR DDN
         MVC   DDNAME2,BLANKS          CLEAR DDN
VALDCB9 #S0C4  RESET
         END#PROC
         POP   USING                                            GP09260
*
*----------------------------------------------------------------------
*        LOCATE DSAB/SIOT/JFCB/TIOT FROM DEB EXTENSION
*----------------------------------------------------------------------
*
         PUSH  USING                                            GP09260
SWA000   BEG#PROC
         MVI   $DCB,FF                 INVALIDATE DCB ADDRESS
         MVI   $ACB,FF                 INVALIDATE ACB ADDRESS
         MVI   $DSAB,FF                INVALID ADDRESS
         MVI   $SIOT,FF                INVALID ADDRESS
         MVI   $JFCB,FF                INVALID ADDRESS
         MVI   $TIOT,FF                INVALID ADDRESS
*
*        RETRIEVE DCB/ACB ADDRESS FROM DEB IF R5=A(DEBPREFX)
*
SWA010   LTR   R5,R5                   DEB ADDR VALID?
         BZ    SWA020                  NO, JUMP
         USING DEBPREFX,R5
         LTR   R6,R6                   DCB/ACB ADDR VALID?
         BNZ   SWA025                  YES, JUMP
         ICM   R6,B'0111',DEBDCBAD+1   24-BIT DCB/ACB ADDR
         USING IHADCB,R6                                        GP09260
         CLI   DEBAMTYP,DEBAMSUB       AM=SUBSYS?
         BE    SWA018                  YES, JUMP
         CLI   DEBAMTYP,DEBAMVSM       AM=VSAM?
         BNE   SWA025                  NO, PROCESS DCB
*
*        AM=VSAM
*
SWA017   ST    R6,$ACB                 ACB ADDRESS (24-BIT)
         ICM   R0,B'1111',DEBRRQ       DO WE HAVE AN RMODE31 ACB?
         BZ    SWA030                  NO, USE RMODE24 ACB
         LR    R6,R0                   YES, USE RMODE31 ACB
         B     SWA030                  PROCESS ACB
*
*        AM=SUBSYS
*
SWA018   ST    R6,$ACB                 ACB ADDRESS
         ICM   R0,B'1111',DEBRRQ       DO WE HAVE A DCB?
         BZ    SWA030                  NO, USE ACB
         LR    R6,R0                   YES, USE DCB
         B     SWA025                  PROCESS ACB
*
*        IF INVOKED WITH R5=0, SET DEB ADDR FROM DCB/ACB
*
SWA020   LTR   R6,R6                   DCB/ACB ADDR VALID?
         BZ    SWA090                  NO, EXIT
         TM    DCBOFLGS,DCBOFOPN       OPEN DCB?
         BZ    SWA090                  NO, EXIT
         ICM   R5,B'0111',DCBDEBAD+1   DEB ADDRESS FROM DCB/ACB
         SH    R5,=AL2(DEBBASIC-DEBPREFX)  POINT AT DEB PREFIX
*
SWA025   ST    R6,$DCB                 DCB ADDRESS
         USING IHADCB,R6
*
*        RETRIEVE DSAB ADDR FROM DEB EXTENSION
*
SWA030   TM    DEBFLGS1,DEBXTNIN       DO WE HAVE A DEB EXTENSION?
         BZ    SWA090                  NO, EXIT
         ICM   R1,B'1111',DEBXTNP      DEB EXTENSION
         BZ    SWA090                  ZERO, EXIT
         USING DEBXTN,R1
         ICM   R2,B'1111',DEBXDSAB     OPENJ DSAB POINTER
         BNZ   SWA035                  NON-ZERO, JUMP
         ICM   R2,B'1111',DEBXDSAB     DSAB POINTER
         BZ    SWA090                  NO DSAB, EXIT
         USING DSAB,R2
SWA035   CLC   DSABID,=C'DSAB'         IS THIS A DSAB?
         BNE   SWA090                  NO DSAB, JUMP
         ICM   R1,B'1111',DSABSIOT     SIOT PTR
         BNZ   SWA040                  OK, JUMP
         AIF   (&MVSESA).DOSVA3                                 GP09255
         L     R1,DSABSSVA-1 GET SIOT Q ADDRESS                 GP09255
         LA    R1,16(,R1)    SKIP Q HEADER                      GP09255
         AGO   .CMSVA3                                          GP09255
.DOSVA3  LA    R1,TENWORDS+4           WORK AREA
         ST    R1,TENWORDS             WORK AREA
         USING ZB505,R1
         XC    SWAEPAX,SWAEPAX         CLEAR WORK AREA
         MVC   SWVA,DSABSSVA           SVA OF THE SIOT
         DROP  R1
         SWAREQ FCODE=RL,EPA=TENWORDS,MF=(E,SWAREQL1),UNAUTH=YES
         L     R1,SWBLKPTR-ZB505+TENWORDS+4  31-BIT ADDRESS OF SIOT
.CMSVA3  ANOP  ,                                                GP09255
SWA040   ST    R2,$DSAB                DSAB ADDRESS
         ST    R1,$SIOT                SIOT ADDRESS
         USING INDMSIOT,R1
         MVC   $JFCB,SJFCBPTR          JFCB POINTER
         MVC   $TIOT,DSABTIOT          TIOENTRY POINTER
         DROP  R2,R1                   DSAB,SIOT
*
SWA090   END#PROC
         POP   USING                                            GP09260
*
*----------------------------------------------------------------------
*        PRINT DCB INFORMATION
*----------------------------------------------------------------------
*
PRDCB    BEG#PROC
         USING DEBPREFX,R5
         USING IHADCB,R6
         LTR   R6,R6                   DCBADDR=0?
         BNP   PRDCB99                 YES, IGNORE
         ST    R6,WKCELL1              STORE DCB ADDRESS
*
         LA    R15,=C'VSISPSDAPO??'                             GP09260
         TM    DCBDSORG+1,X'08'        VS                       GP09260
         BO    PRDCB07                 VS
         LA    R15,2(,R15)
         TM    DCBDSORG+0,X'80'        IS
         BO    PRDCB07                 IS
         LA    R15,2(,R15)
         TM    DCBDSORG+0,X'40'        PS
         BO    PRDCB07                 PS
         LA    R15,2(,R15)
         TM    DCBDSORG+0,X'20'        DA
         BO    PRDCB07                 DA
         LA    R15,2(,R15)
         TM    DCBDSORG+0,X'02'        PO
         BO    PRDCB07                 PO
         LA    R15,2(,R15)
PRDCB07  MVC   XDSORG,0(R15)           MOVE DSORG
*
         MVC   XRECFM,BLANKS
         LA    R1,XRECFM               START OF WORK AREA
         TM    DCBRECFM,DCBRECF        F/V/U
         BNO   PRDCB08V                F/V/U
         MVI   0(R1),C'F'              F/V/U
PRDCB08V TM    DCBRECFM,DCBRECV        F/V/U
         BNO   PRDCB08U                F/V/U
         MVI   0(R1),C'V'              F/V/U
PRDCB08U TM    DCBRECFM,DCBRECU        F/V/U
         BNO   PRDCB08B                F/V/U
         MVI   0(R1),C'U'              F/V/U
PRDCB08B TM    DCBRECFM,DCBRECBR       .B
         BNO   PRDCB08S                .B
         MVI   1(R1),C'B'              .B
         LA    R1,1(,R1)
PRDCB08S TM    DCBRECFM,X'08'          ..S
         BNO   PRDCB08A                ..S
         MVI   1(R1),C'S'              ..S
         LA    R1,1(,R1)
PRDCB08A TM    DCBRECFM,X'04'          ...A
         BNO   PRDCB08M                ...A
         MVI   1(R1),C'A'              ...A
PRDCB08M TM    DCBRECFM,X'02'          ...M
         BNO   PRDCB08Z                ...M
         MVI   1(R1),C'M'              ...M
PRDCB08Z EQU   *
         TM    DCBMACRF,DCBMRECP       EXCP DCB?
         BO    PRDCB31                 YES, JUMP
         TM    DCBMACRF,DCBMRGET       QSAM DCB?
         BO    PRDCB41                 YES, JUMP
         TM    DCBMACRF+1,DCBMRPUT     QSAM DCB?
         BO    PRDCB41                 YES, JUMP
         MVI   WKCELL1,C'P'            A "P" FOR BPAM
         TM    DCBDSORG,JFCORGPO       BPAM DCB?
         BO    PRDCB11                 YES, JUMP
         MVI   WKCELL1,C'D'            A "D" FOR BDAM
         TM    DCBDSORG,JFCORGDA       BDAM DCB?
         BO    PRDCB11                 YES, JUMP
         MVI   WKCELL1,C'S'            A "S" FOR BSAM
PRDCB11  MVI   MINLINES,6
         STRING '0  B',(WKCELL1,1),'AM',                               X
               ' DCB AT LOCATION ',(WKCELL1+1,3,X),                    X
               6X,'RECFM=',(XRECFM,,T),                                X
               ',LRECL=',(DCBLRECL,H,L),                               X
               ',BLKSIZE=',(DCBBLKSI,H,L),                             X
               ',BUFNO=',(DCBBUFNO,FL1,L),                             X
               ',NCP=',(DCBNCP,FL1,L),                                 X
               ',BUFL=',(DCBBUFL,H,L),                                 X
               ',DSORG=',(XDSORG,,T),INTO=LINE121
         CAL#PROC SPACE1          <== PRINT LINE
         LA    R0,DCBLNGPO             LENGTH
         LA    R1,IHADCB               ADDRESS
         CAL#PROC DUMP32          <== PRINT IT
*
*        LOOK FOR DECB IF S001-XX ABEND
*
PRDCB16  CLI   OCE#CODE,C'E'           S001 ABEND?
         BNE   PRDCB21                 NO, JUMP
         L     R1,FRSTSVRB             LOAD ADDR OF FIRST SVRB
         L     R1,RBGRS4-RBPREFIX(,R1) R1 POINTS TO DECB
        #S0C4  PRDCB21
         CLM   R6,7,DECDCBAD-DECB+1(R1)  IS THIS MY DCB?        GP09260
         BNE   PRDCB21                 NO, QUIT
         ST    R1,WKCELL1              STORE DECB ADDRESS
         STRING '0  DECB AT LOCATION ',(WKCELL1+1,3,X),INTO=LINE121
         CAL#PROC SPACE1          <== PRINT LINE
         LA    R0,020                  LENGTH OF DECB
         CAL#PROC DUMP32          <== PRINT DECB
         L     R2,DECIOBPT-DECB(,R1)   PICK UP IOB ADDRESS FROM DECB+16
         USING IOBSTDRD-8,R2
         CLM   R6,7,IOBDCBPT+1         IS THIS MY DCB?          GP09260
         BE    PRDCB24                 YES, JUMP
*
*        LOCATE CURRENT IOB FROM DCBIOBA (DCB+68)
*
PRDCB21 #S0C4  RESET
         SLR   R2,R2
         ICM   R2,B'0111',DCBIOBA+1    GET IOB ADDRESS (DCB+68)
         CLI   DCBNCP,1                NCP>1?
         BNH   PRDCB24                 NO, JUMP
         ICM   R2,B'0111',DCBICQE+1    YES, GET ICQE ADDRESS (DCB+28)
         STRING '0  ICQE AT LOCATION ',(DCBICQE+1,3,X),INTO=LINE121
         CAL#PROC SPACE1          <== PRINT LINE
         LA    R0,032                  ICQE SIZE
         LR    R1,R2                   ICQE ADDRESS
         CAL#PROC DUMP32          <== PRINT ICQE
         L     R2,ICQFIRST-ICQE(,R2)   POINT TO FIRST IOB IN CHAIN
         ICM   R2,B'1000',X00          ZERO FIRST BYTE
*SAMB    L     R2,ICQIOBAD-ICQE(,R2)   POINT TO IOB IN SAMB
*SAMB    SH    R2,=H'16'               POINT TO START OF SAMB
*SAMB    ST    R2,WKCELL1              STORE FOR STRING
*SAMB    STRING '0  SAMB AT ADDRESS ',(WKCELL1+1,3,X),INTO=LINE
*SAMB    CAL#PROC SPACE1          <== PRINT LINE
*SAMB    LA    R0,256                  SAMB SIZE
*SAMB    LR    R1,R2                   SAMB ADDRESS
*SAMB    CAL#PROC DUMP32          <== PRINT ICQE
*SAMB    LA    R2,8(,R2)               SAMIOB (IOB PREFIX)
*
*        PRINT CURRENT IOB
*
PRDCB24  CL    R2,REGION24             DOES THIS LOOK RIGHT?
         BL    PRDCB99                 NO, QUIT
         CLI   DEBAMTYP,DEBAMSUB       JES DEB?
         BE    PRDCB99                 YES, QUIT
         LA    R0,IOBSTDRD             STANDARD SECTION
         ST    R0,WKCELL1              STORE FOR STRING
         MVI   MINLINES,6
         STRING '0  IOB AT LOCATION ',(WKCELL1+1,3,X),INTO=LINE121
         CAL#PROC SPACE1          <== PRINT LINE
         STRING '0   -08 ',56X,(IOBPREFX+08,4,X),1X,(IOBPREFX+12,4,X), X
               INTO=LINE121
         CAL#PROC SPACE1          <== PRINT LINE
         LA    R0,064                  IOB SIZE
         LA    R1,IOBSTDRD             IOB ADDRESS
         O     R1,=X'80000000'         NO CTLCHR
         CAL#PROC DUMP32          <== PRINT IOB
         ICM   R3,B'0111',IOBECBPT+1   PICK UP DECB ADDRESS
        #S0C4  PRDCB99                 DECB ADDR MAY BE BAD
         LA    R0,IOBSTDRD             STANDARD SECTION
         CLM   R0,7,1+DECIOBPT-DECB(R3)   DOES THIS LOOK RIGHT? GP09260
         BNE   PRDCB99                 NO, QUIT
         MVI   MINLINES,4
         STRING '0  DECB AT LOCATION ',(IOBECBPT+1,3,X),INTO=LINE121
         CAL#PROC SPACE1          <== PRINT LINE
         LA    R0,020                  DECB SIZE
         LR    R1,R3                   DECB ADDRESS             GP09260
         CAL#PROC DUMP32          <== PRINT DECB
         CLI   0(R3),X'7F'        GOOD COMPLETION ?             GP09260
         BNE   PRDCB99            NO                            GP09260
         STRING '0     CURRENT BLOCK:',INTO=LINE121             GP09260
         MVI   MINLINES,6                                       GP09260
         CAL#PROC SPACE1                                        GP09260
         L     R1,DECAREA-DECB(,R3)    GET BUFFER ADDRESS       GP09260
         SR    R0,R0                                            GP09260
         ICM   R0,3,DECLNGTH-DECB(R3)  GET LENGTH               GP09260
         TM    DECTYPE1-DECB(R3),DECLNS                         GP09260
         BZ    PRDCB29                                          GP09260
         ICM   R0,3,DCBBLKSI           GET DEFAULT SIZE         GP09260
PRDCB29  SR    R15,R15                                          GP09260
         ICM   R15,3,IOBCSW+5          GET RESIDUAL COUNT       GP09260
         SR    R0,R15                  DUMP ONLY WHAT'S PROCESSED
         CAL#PROC PDUMP                DUMP THE BUFFER          GP09260
         B     PRDCB99
         DROP  R2                      IOB
*
PRDCB31  MVI   MINLINES,5
         STRING '0  EXCP DCB AT LOCATION ',(WKCELL1+1,3,X),            X
               INTO=LINE121
         CAL#PROC SPACE1          <== PRINT LINE
         LA    R0,DCBLNGXE             LENGTH
         LA    R1,IHADCB               ADDRESS
         CAL#PROC DUMP32          <== PRINT IT
         SLR   R2,R2
         ICM   R2,B'0111',DCBIOBAA     GET IOB ADDRESS          GP09260
         BZ    PRDCB99                 NONE - DONE              GP09260
         SH    R2,=H'8'                FINAGLE                  GP09260
         USING IOBSTDRD-8,R2                                    GP09260
         CLM   R6,7,IOBDCBPB           BACK LINK?               GP09260
         BNE   PRDCB99                 NO; IGNORE               GP09260
         LA    R0,IOBSTDRD             STANDARD SECTION         GP09260
         ST    R0,WKCELL1              STORE FOR STRING         GP09260
         MVI   MINLINES,6                                       GP09260
         STRING '0  IOB AT LOCATION ',(WKCELL1+1,3,X),INTO=LINE121
         CAL#PROC SPACE1          <== PRINT LINE                GP09260
         STRING '0   -08 ',56X,(IOBPREFX+08,4,X),1X,(IOBPREFX+12,4,X), X
               INTO=LINE121                                     GP09260
         CAL#PROC SPACE1          <== PRINT LINE                GP09260
         LA    R0,064                  IOB SIZE                 GP09260
         LA    R1,IOBSTDRD             IOB ADDRESS              GP09260
         O     R1,=X'80000000'         NO CTLCHR                GP09260
         CAL#PROC DUMP32          <== PRINT IOB                 GP09260
         ICM   R1,B'0111',IOBSTART+1   PICK UP CCW ADDRESS      GP09260
         CAL#PROC FORMCCW              FORMAT CCW CHAIN/DATA    GP09260
         B     PRDCB99
*
PRDCB41  MVI   MINLINES,6
         STRING '(GL,PL)',INTO=XMACRF
         CLI   DEBOPATB,DEBUPDAT       OPENED FOR UPDATE?
         BE    PRDCB41S                YES, JUMP
*
*        EDIT MACRF CODES
*
         BAS   R1,PRDCB41B             BR AROUND TABLE
         DC    C'G',AL1(DCBMRGET)      40 GET
         DC    C'M',AL1(DCBMRMVG)      10 MOVE MODE
         DC    C'L',AL1(DCBMRLCG)      08 LOCATE MODE
         DC    C'T',AL1(DCBMRSBG)      04 SUBSTITUTE MODE
         DC    C'C',AL1(DCBMRCRL)      02 CNTRL
         DC    C'D',AL1(DCBMRDMG)      01 DATA MODE
         DC    2X'00'
PRDCB41B MVC   WKCELL3,DCBMACF1        MOVE MACRF BYTE (INPUT/UPDATE)
         TM    DEBOPATB,DEBXTEND       OPENED FOR EXTEND/OUTPUT?
         BNO   PRDCB41C                NO, JUMP
         MVC   WKCELL3,DCBMACF2        MOVE MACRF BYTE (OUTPUT/EXTEND)
         BAS   R1,PRDCB41C             BR AROUND TABLE
         DC    C'P',AL1(DCBMRPUT)      40 PUT
         DC    C'M',AL1(DCBMRMVP)      10 MOVE MODE
         DC    C'L',AL1(DCBMRLCP)      08 LOCATE MODE
         DC    C'T',AL1(DCBMRTMD)      04 SUBSTITUTE MODE
         DC    C'C',AL1(DCBMRCTL)      02 CNTRL
         DC    C'D',AL1(DCBMRDMD)      01 DATA MODE
         DC    2X'00'
PRDCB41C TM    WKCELL3,*-*             DCBMACF1/DCBMACF2
         MVC   XMACRF,BLANKS
*LOOP
PRDCB41D IC    R14,1(,R1)              MASK
         EX    R14,PRDCB41C
         BZ    PRDCB41E
         STRING (XMACRF,,T),((R1),1),INTO=XMACRF
PRDCB41E LA    R1,1+1(,R1)             NEXT ENTRY
         CLI   0(R1),C'A'              END OF TABLE?
         BNL   PRDCB41D                NOT YET, LOOP MORE
*ENDLOOP
PRDCB41S MVC   OPNBLKSI+2(2),DCBBLKSI  MOVE BLKSI FROM DCB
         MVC   OPNLRECL+2(2),DCBLRECL  MOVE LRECL FROM DCB
         CLI   DEBAMTYP,DEBAMSUB       JES DEB?
         BE    PRDCB41Z                YES, EXIT
         CLI   DEBLNGTH,16             DEB LONG ENOUGH?
         BL    PRDCB41Z                NO, EXIT
         SR    R1,R1                   CLEAR REGISTER
         SR    R2,R2                   CLEAR REGISTER
         IC    R1,DEBNMEXT             NUMBER OF EXTENTS
         IC    R2,DEBEXSCL             EXTENT SCALE
         SLL   R1,0(R2)                SIZE OF EXTENT
         LA    R2,DEBBASND(R1)         ADJ TO PROPER OFFSET
         USING DEBACSMD,R2             ACCESS METHOD EXTENSION
         LH    R14,DEBLRECL            GET DCBLRECL BEFORE OPEN
         N     R14,=X'0000FFFF'                                 GP09260
         LH    R15,DEBBLKSI            GET DCBBLKSI BEFORE OPEN
         DROP  R2                      DEB
         LTR   R14,R14                 WAS ZERO?
         BNZ   PRDCB41X                NO, JUMP
         LH    R14,DCBLRECL            YES, USE CURRENT DCB VALUE
         N     R14,=X'0000FFFF'                                 GP09260
PRDCB41X ST    R14,OPNLRECL            SAVE IT FOR LATER
         LTR   R15,R15                 WAS ZERO?
         BNZ   PRDCB41Y                NO, JUMP
         LH    R15,DCBBLKSI            YES, USE CURRENT DCB VALUE
PRDCB41Y ST    R15,OPNBLKSI            SAVE IT FOR LATER
*
PRDCB41Z STRING '0  QSAM',                                             X
               ' DCB AT LOCATION ',(WKCELL1+1,3,X),                    X
               6X,'MACRF=',(XMACRF,,T),                                X
               ',RECFM=',(XRECFM,,T),                                  X
               ',LRECL=',(OPNLRECL,F,L),                               X
               ',BLKSIZE=',(OPNBLKSI,F,L),                             X
               ',BUFNO=',(DCBBUFNO,FL1,L),                             X
               ',DSORG=',(XDSORG,,T),INTO=LINE121
         CAL#PROC SPACE1          <== PRINT HEADER
         LA    R0,DCBLNGQS             LENGTH
         LA    R1,IHADCB               ADDRESS
         CAL#PROC DUMP32          <== PRINT DCB
         AIF   (NOT &MVSXA).NDCBE1                              GP09255
*
*        PRINT DCBE, IF PRESENT
*
         TM    DCBEODAD,DCBH0+DCBH1    IS DCBDCBE VALID ?
         BNO   PRDCB42                 NO, JUMP
         L     R1,DCBDCBE              A(DCBE)
         USING DCBE,R1
         OC    DCBE(DCBEMINL),DCBE     VALIDATE
         MVI   MINLINES,6
         STRING '0  DCBE AT LOCATION ',(DCBDCBE,,X),INTO=LINE121
         CAL#PROC SPACE1          <== PRINT HEADER
         LH    R0,DCBELEN              LENGTH OF DCBE
         CAL#PROC DUMP32          <== PRINT DCBE
.NDCBE1  SPACE 1                                                GP09255
*        PRINT CURRENT RECORD FOR A QSAM DCB
*
PRDCB42  CLI   OCE#CODE,C'O'           ABEND DURING OPEN?
         BNE   PRDCB42J                NO, JUMP
         CLM   R6,7,1+SDWAGR02         WAS THIS FOR THIS DCB?   GP09260
         BE    PRDCB99                 YES, QUIT
*
*        IF IT'S A SAM-CI DCB WITH MACRF=PM, DO NOT PRINT THE RECORD
*        POINTED TO BY DCBRECAD; THE DFP V3 VERSION OF IGG019DJ (THE
*        SAM-CI QSAM PROCESSOR) DOES NOT MOVE ANY DATA TO THE BUFFER.
*        THE RECORD SHOULD BE PRINTED FROM THE JES2 UNPROTECTED
*        BUFFER IN THE PRACB ROUTINE.
*
PRDCB42J CLI   DEBAMTYP,DEBAMSUB       SUB-SYSTEM DATA SET?
         BNE   PRDCB43                 NO, JUMP
         TM    DEBOPATB,DEBXTEND       OPENED FOR EXTEND/OUTPUT?
         BNO   PRDCB43                 NO, JUMP
         TM    DCBMACRF+1,DCBMRPUT+DCBMRMVP  MACRF=PM?
         BNO   PRDCB43                 NO, JUMP
*
PRDCB43  LM    R0,R1,DCBEOBAD          R0=EOBAD,R1=RECAD
         ICM   R0,B'1000',X00          ZERO FIRST BYTE (DCBNCP)
         ICM   R1,B'1000',X00          ZERO FIRST BYTE (DCBRECBT)
*
*        LOCATE START OF CURRENT BUFFER
*
PRDCB43C MVC   REASON,=C'01'           INVALID VALUE IN DCBEOBAD
         CL    R0,REGION24             EOBAD VALID?
         BL    PRDCB90                 NO RECORD YET, QUIT
         CLI   DEBAMTYP,DEBAMSUB       SUB-SYSTEM DATA SET?
         BNE   PRDCB43J      MVS 3.8 - DIFFERENT PROCESSING     GP09260
         TM    DEBOPATB,DEBXTEND  OPEN FOR OUTPUT ?             GP09260
         BO    PRDCB99       YES; LET ACB ROUTINE PRINT IT      GP09260
         B     PRDCB43J      ICB PROCESSING DIFFEENT IN MVS     GP09260
***********************************************************************
*NEW*    BE    PRDCB43J                YES, JUMP
         L     R2,DCBIOBA              CURRENT IOB
         ICM   R2,B'1000',X00          ZERO FIRST BYTE
         USING IOBSTDRD-8,R2
         CLM   R6,7,1+IOBDCBPT         DOES THIS IOB LOOK RIGHT?
         BNE   PRDCB43F                NO, JUMP
         MVC   WKCELL2,IOBCCW          MOVE R/W CCW
         B     PRDCB43G
*
*        PROCESS ICB POINTED TO BY DCBIOBS (CHAINED SCHEDULING)
*
PRDCB43F MVC   REASON,=C'06'           INVALID IOB/ICB
         TM    DCBCIND2,DCBCNCHS       CHAINED SCHEDULING (I.E. TAPE) ?
         BZ    PRDCB90                 NO, WE'RE LOST
         USING IOBCCW-32,R2            CCW IS AT ICB+16
         MVC   WKCELL2,IOBCCW          MOVE R/W CCW
         DROP  R2                      IOB
*
*        GET BUFFER ADDR FROM R/W CCW (OR IDAW)
*
PRDCB43G SLR   R14,R14
         ICM   R14,B'0111',WKCELL2+1   GET BUFFER ADDR FROM CCW
         B     PRDCB43S
*
PRDCB43J LR    R14,R0                  PASS DCBEOBAD
         SH    R14,DCBBLKSI            SUBTRACT BUFFER LENGTH FROM EOB
         BNP   PRDCB90                 NO RECORD YET, QUIT
*
PRDCB43S ST    R14,BUFFER              BEGINNING OF CURR BUFFER
*
         TM    DCBRECFM,DCBRECU        U?
         BO    PRDCB60                 YES, JUMP
         TM    DCBRECFM,DCBRECV        V/VB?
         BO    PRDCB70                 YES, JUMP
         LH    R2,DCBLRECL             RECORD LENGTH IF FB
         N     R2,=X'0000FFFF'                                  GP09260
         TM    DCBRECFM,DCBRECF+DCBRECBR   FB?
         BO    PRDCB50                 YES, JUMP
         LH    R2,DCBBLKSI             RECORD LENGTH IF F
*F/FB
PRDCB50  TM    DEBOPATB,DEBXTEND       OPENED FOR EXTEND/OUTPUT?
         BNO   PRDCB80                 NO, JUMP
         SR    R1,R2                   GET ADDR OF CURRENT RECORD
         MVC   REASON,=C'02'           INVALID VALUE IN DCBRECAD
         C     R1,BUFFER               TOO LOW?
         BL    PRDCB90                 YES, JUMP
*
*        LOCATE PREVIOUS RECORD (RECFM=F/FB)
*
         TM    DCBRECFM,DCBRECF+DCBRECBR   FB?
         BNO   PRDCB80                 NO, JUMP
         LR    R0,R1                   GET ADDR OF CURRENT RECORD
         SR    R0,R2                   GET ADDR OF PREVIOUS RECORD
         C     R0,BUFFER               PREV RCD IN CURR BUFFER?
         BL    PRDCB80                 NO, JUMP
         MVI   MINLINES,6
         STRING '0    PREVIOUS RECORD:',INTO=LINE121
         CAL#PROC SPACE1          <== PRINT HEADER
         LR    R1,R0                   PASS ADDRESS
         LR    R0,R2                   LENGTH
         CAL#PROC PDUMP           <== DUMP PREVIOUS RECORD
         AR    R1,R2                   GET ADDR OF CURRENT RECORD
         B     PRDCB80
*
*        LOCATE CURRENT RECORD (RECFM=U)
*
PRDCB60  LH    R2,DCBLRECL             RECORD LENGTH IF U
         N     R2,=X'0000FFFF'                                  GP09260
         C     R1,BUFFER               IS THIS THE START OF THE BUFFER?
         BE    PRDCB80                 YES, JUMP
         SLR   R1,R2                   NO, POINT TO START OF RECORD (U)
         B     PRDCB80
*
*        LOCATE CURRENT RECORD (RECFM=V/VB)
*
*          MACRF=GM       DCBRECAD=NEXT
*          MACRF=GL       DCBRECAD=CURRENT
*          MACRF=(GL,PL)  DCBRECAD=CURRENT
*          MACRF=PM       DCBRECAD=NEXT
*          MACRF=PL       DCBRECAD=CURRENT
*
PRDCB70  TM    DEBOPATB,DEBXTEND       OPENED FOR EXTEND/OUTPUT?
         BO    PRDCB71                 YES, JUMP
         LH    R15,0(,R14)             PICK UP BLOCK LENGTH FROM BDW
         ALR   R15,R14                 1ST BYTE AFTER CURRENT BLOCK
         MVC   REASON,=C'11'           BUFFER/BDW/EOBAD INCOMPATIBLE
         CLR   R15,R0                  SAME AS DCBEOBAD?
         BNE   PRDCB90                 NO, QUIT
*
PRDCB71  CLI   XMACRF+1,C'L'           LOCATE MODE?
         BE    PRDCB78                 YES, JUMP
*
*        DCBRECAD=NEXT, SCAN CURRENT BUFFER TO LOCATE RECORD
*
         LR    R0,R1                   FIRST BYTE AFTER CURRENT RECORD
         LA    R1,4(,R14)              FIRST RECORD (SKIP BDW)
         SLR   R2,R2
*--LOOP
PRDCB73L ICM   R2,B'0011',0(R1)        LENGTH OF CURRENT RECORD (RDW)
         MVC   REASON,=C'12'           RDW<4
         CH    R2,=H'4'                IS RECORD AT LEAST 4 BYTES?
         BL    PRDCB90                 NO, QUIT
         MVC   REASON,=C'13'           RDW IS NOT LLLL0000
         CL    R2,OPNLRECL             RECORD LONGER THAN LRECL?
         BH    PRDCB90                 YES, QUIT
         MVC   REASON,=C'14'           RDW IS NOT LLLL0000
         ICM   R2,B'1100',2(R1)        3RD-4TH BYTES OF RDW
         BNZ   PRDCB90                 NO, QUIT
         ALR   R1,R2                   1ST BYTE AFTER RECORD
         CLR   R1,R0                   IS THIS THE RECORD I WANT?
         BL    PRDCB73L                NOT YET, KEEP ON SCANNIN'
*--ENDLOOP
         MVC   REASON,=C'15'           V/VB SCAN FAILED
         BH    PRDCB90                 TOO HIGH, EXIT
         SLR   R1,R2                   POINT TO RECORD AGAIN
*
PRDCB78  LH    R2,0(,R1)               RECORD LENGTH IF V/VB
         CLM   R2,B'0011',DCBLRECL     GOOD RDW?
         BH    PRDCB90                 NO, QUIT
*
*        PRINT CURRENT RECORD (F/V/U)
*
PRDCB80  MVC   REASON,=C'00'           LENGTH=0
         LTR   R0,R2                   LENGTH=0?
         BZ    PRDCB90                 YES, JUMP
         MVI   MINLINES,6
         STRING '0    CURRENT RECORD:',INTO=LINE121
** STRING '0    CURRENT RECORD ',((R0),,X),1X,((R1),,X),1X,((R2),,X),  X
               INTO=LINE
         CAL#PROC SPACE1          <== PRINT LINE
         CAL#PROC PDUMP           <== DUMP IT
         ST    R6,CURR#REC             CURRENT RECORD HAS BEEN PRINTED
         B     PRDCB99
*
PRDCB90  STRING '0    CURRENT RECORD IS NOT AVAILABLE (',REASON,')',   X
               INTO=LINE121
*ORG PRDCB90
*        STM   R14,R15,WKCELL1
*        STRING '0    CURRENT RECORD IS NOT AVAILABLE (',REASON,')',   X
               2X,(BUFFER,,X),1X,(WKCELL1,4,X),1X,(WKCELL1+4,4,X),     X
               1X,((R0),,X),1X,((R1),,X),1X,((R2),,X),INTO=LINE121
         CAL#PROC SPACE1          <== PRINT LINE
PRDCB99  END#PROC
         LTORG ,                                                GP09255
*
*----------------------------------------------------------------------
*        PRINT ACB INFORMATION (VSAM, JES)
*----------------------------------------------------------------------
*
PRACB    BEG#PROC
         USING DEBPREFX,R5
         L     R6,$ACB                 PICK UP ACB ADDRESS
         STRING '0  ACB AT LOCATION ',((R6),,X),'   MACRF=(',          X
               INTO=LINE121
         USING IFGACB,R6
*
*        DISPLAY MACRF
*
         BAS   R1,PRACB11A             BR AROUND TABLE
         DC    C'KEY',AL1(ACBKEY)      80 00 00
         DC    C'ADR',AL1(ACBADR)      40 00 00
         DC    C'CNV',AL1(ACBCNV)      20 00 00
         DC    C'SEQ',AL1(ACBSEQ)      10 00 00
         DC    C'DIR',AL1(ACBDIR)      08 00 00
         DC    C'IN ',AL1(ACBIN)       04 00 00
         DC    C'OUT',AL1(ACBOUT)      02 00 00
         DC    C'UBF',AL1(ACBUBF)      01 00 00
PRACB11A TM    ACBMACR1,*-*
*LOOP
PRACB11B IC    R14,3(,R1)              MASK
         EX    R14,PRACB11A
         BZ    PRACB11C
         STRING (LINE,,T),((R1),3,T),',',INTO=LINE
PRACB11C LA    R1,3+1(,R1)             NEXT ENTRY
         CLI   0(R1),C'A'              END OF TABLE?
         BNL   PRACB11B                NOT YET, LOOP MORE
*ENDLOOP
         BAS   R1,PRACB12A             BR AROUND TABLE
         DC    C'BWO',AL1(ACBBWO)      00 20 00
         DC    C'SKP',AL1(ACBSKP)      00 10 00
         DC    C'RST',AL1(ACBRST) NRS  80 04 00
         DC    C'DSN',AL1(ACBDSN)      00 02 00
         DC    C'AIX',AL1(ACBAIX) NRM  00 01 00
PRACB12A TM    ACBMACR2,*-*
*LOOP
PRACB12B IC    R14,3(,R1)              MASK
         EX    R14,PRACB12A
         BZ    PRACB12C
         STRING (LINE,,T),((R1),3,T),',',INTO=LINE
PRACB12C LA    R1,3+1(,R1)             NEXT ENTRY
         CLI   0(R1),C'A'              END OF TABLE?
         BNL   PRACB12B                NOT YET, LOOP MORE
*ENDLOOP
         BAS   R1,PRACB13A             BR AROUND TABLE
         DC    C'NLW',AL1(ACBNLW)      00 00 80    MACR3
         DC    C'LSR',AL1(ACBLSR) NSR  00 00 40
         DC    C'GSR',AL1(ACBGSR)      00 00 20
         DC    C'ICI',AL1(ACBICI)      00 00 10
         DC    C'DFR',AL1(ACBDFR) NDF  00 00 08
         DC    C'SIS',AL1(ACBSIS)      00 00 04
**       DC    C'A31',AL1(ACBA31)      00 00 01
PRACB13A TM    ACBMACR3,*-*
*LOOP
PRACB13B IC    R14,3(,R1)              MASK
         EX    R14,PRACB13A
         BZ    PRACB13C
         STRING (LINE,,T),((R1),3,T),',',INTO=LINE
PRACB13C LA    R1,3+1(,R1)             NEXT ENTRY
         CLI   0(R1),C'A'              END OF TABLE?
         BNL   PRACB13B                NOT YET, LOOP MORE
*ENDLOOP
         LA    R15,LINE-1(R15)         LAST COMMA
         MVI   0(R15),C')'             CLOSE SUB-LIST
*
         MVI   $AMBL,FF                INVALIDATE AMBL ADDRESS
         MVI   $AMB#D,FF               INVALIDATE DATA AMB ADDRESS
         MVI   $AMB#I,FF               INVALIDATE INDX AMB ADDRESS
         MVI   $AMDSB#D,FF             INVALIDATE DATA AMDSB ADDRESS
         MVI   $AMDSB#I,FF             INVALIDATE INDX AMDSB ADDRESS
         MVI   $PLHDR,FF               INVALIDATE PLH HEADER ADDRESS
*
*        LOCATE/VALIDATE AMBL (VSAM DATA SET ONLY)
*
         L     R2,ACBAMBL              POINT TO AMBL
         USING AMBL,R2
         C     R2,REGION24             VALIDATE AMBL
         BL    PRACB18                 LOST, QUIT
        #S0C4  PRACB18
         CLC   AMBL(64),0              VALIDATE
         CLI   AMBLID,X'50'            AM I LOST?
         BNE   PRACB18                 YES, EXIT
         CL    R6,AMBLACB              AM I LOST?
         BNE   PRACB18                 YES, EXIT
         ST    R2,$AMBL                AMBL ADDRESS OK
*
*        BUFNI/BUFND/STRNO
*
PRACB16  CLI   DEBAMTYP,DEBAMVSM       VSAM?
         BNE   PRACB18                 NO, JUMP
        #S0C4  PRACB18
PRACB16I ICM   R3,B'1111',AMBLIX       INDEX AMB
         BZ    PRACB16D                NO INDEX, JUMP
         USING AMB,R3
         CLI   AMBID,X'40'             AM I LOST?
         BNE   PRACB16D                YES, JUMP
         L     R1,AMBBUFC              INDEX BUFC HEADER
         CLI   0(R1),X'70'             AM I LOST?
         BNE   PRACB16D                YES, EXIT
         ST    R3,$AMB#I               INDEX AMB ADDRESS OK
         MVC   $AMDSB#I,AMBDSB         INDEX AMDSB ADDRESS OK
         STRING (LINE,,T),',BUFNI=',(BUFCBUFN-BUFC(R1),FL1,L),         X
               INTO=LINE
PRACB16D L     R3,AMBLDTA              DATA AMB
         C     R3,REGION24             VALIDATE AMB
         BL    PRACB16S                LOST, QUIT
         CLI   AMBID,X'40'             VALIDATE AMB
         BNE   PRACB16S                LOST, QUIT
         CLC   AMB(188),0              VALIDATE DATA AMB
         ST    R3,$AMB#D               DATA AMB ADDRESS OK
         MVC   $AMDSB#D,AMBDSB         DATA AMDSB ADDRESS OK
         L     R1,AMBBUFC              DATA BUFC HEADER
         CLI   0(R1),X'70'             VALIDATE BUFC
         BNE   PRACB16S                LOST, QUIT
         STRING (LINE,,T),',BUFND=',(BUFCBUFN-BUFC(R1),FL1,L),         X
               INTO=LINE
         TM    AMBFLG1,AMBCREAT        CREATE MODE?
         BZ    PRACB16S                NO, JUMP
         STRING (LINE,,T),',CREATE',INTO=LINE
PRACB16S L     R1,AMBPH                PICK UP ADDR OF PLH HEADER
         CLI   PLHID-IDAPLHDR(R1),X'30' VALIDATE
         BNE   PRACB18                 LOST, QUIT
         STRING (LINE,,T),',STRNO=',(PLHCNT-IDAPLHDR(R1),H,L),         X
               INTO=LINE
         ST    R1,$PLHDR               PLH HEADER ADDR OK
*
PRACB18  MVI   MINLINES,6
         CAL#PROC SPACE1          <== PRINT ACB ADDRESS & MACRF
         LH    R0,ACBLENG              ACB LENGTH
         LA    R1,IFGACB               ACB ADDRESS
         CAL#PROC DUMP32          <== PRINT ACB
*
         CLI   DEBAMTYP,DEBAMSUB       SUB-SYSTEM DATA SET?
         BE    PRACB70                 YES, JUMP
         CLI   DEBAMTYP,DEBAMVSM       VSAM?
         BNE   PRACB99                 NO, EXIT
         CL    R2,$AMBL                AMBL ADDRESS OK ?
         BNE   PRACB99                 NO, EXIT
*
*        PRINT VSAM CONTROL BLOCKS
*
PRACB20  MVC   MSG60,BLANKS            OPTIONAL ATTRIBUTES
         LA    R1,=C'DUMMY'
         TM    AMBLFLG1,AMBLDUMY       DD DUMMY?
         BO    PRACB20X                YES, JUMP
         LA    R1,=C'KSDS '
         TM    AMBLQ,AMBLKSDS          IS THIS A KSDS?
         BO    PRACB20X                YES, JUMP
         ICM   R3,B'1111',$AMB#D       DATA AMB
         BNP   PRACB26                 BAD, JUMP
         LA    R1,=C'LDS  '
         TM    AMBFLG0,AMBLDS          IS THIS AN LDS?
         BO    PRACB20X                YES, JUMP
         L     R14,$AMDSB#D            A(AMDSB)
         LA    R1,=C'RRDS '
         TM    AMDATTR-AMDSB(R14),AMDRRDS IS THIS AN RRDS?
         BO    PRACB20X                YES, JUMP
         LA    R1,=C'ESDS '
PRACB20X MVC   MSG60(5),0(R1)          DUMMY/KSDS/LDS/RRDS/ESDS
*
         TM    AMBLSHAR,AMBLWRIT       UPDATE MODE?
         BNO   PRACB22                 NO, JUMP
         STRING (MSG60,,T),',UPDATE',INTO=MSG60
*
PRACB22  EQU   *
*
PRACB26  MVI   MINLINES,6
         STRING '0  AMBL AT LOCATION ',(ACBAMBL,,X),6X,MSG60,          X
               INTO=LINE121
         CAL#PROC SPACE1          <== PRINT LINE
         SLR   R0,R0
         IC    R0,AMBLLEN              AMBL LENGTH
         LA    R1,AMBL                 AMBL ADDRESS
         CAL#PROC DUMP32          <== PRINT AMBL
*
*        PRINT DATA AMB
*
         LA    R0,188                  AMB LENGTH
         ICM   R1,B'1111',$AMB#D       DATA AMB
         BNP   PRACB99                 BAD, QUIT
         MVI   MINLINES,9
         STRING '0  DATA AMB AT LOCATION ',((R1),,X),                  X
               '  DSNAME=',AMBDSNM,                                    X
               INTO=LINE121
         CAL#PROC SPACE1          <== PRINT HEADER
         CAL#PROC DUMP32          <== PRINT AMB
*????    DROP  R1,R2                   AMBL                     GP09255
*
         L     R2,AMBPH-AMB(,R1)       PICK UP ADDR OF PLH HEADER
         USING IDAPLHDR,R2
         MVI   MINLINES,4
         STRING '0  PLH HEADER AT LOCATION ',((R2),,X),INTO=LINE121
         CAL#PROC SPACE1          <== PRINT LINE
         C     R2,REGION24             VALIDATE
         BL    PRACB99                 LOST, QUIT
        #S0C4  PRACB99
         CLC   IDAPLHDR(16),0          VALIDATE
         LA    R0,016                  PLHDR LENGTH
         LA    R1,IDAPLHDR             PLH HEADER
         CAL#PROC DUMP32          <== PRINT PLHDR
         CLI   PLHID,X'30'             VALIDATE
         BNE   PRACB99                 LOST, QUIT
         LA    R3,IDAPLH               POINT PAST PLH HEADER
         USING IDAPLH,R3
         MVI   STRNO,0                 PLH/RPL SEQUENCE NUMBER
*
*        PRINT PLH (ONE FOR EACH STRING)
*LOOP
PRACB40  C     R3,REGION24             VALIDATE
         BL    PRACB99                 LOST, QUIT
         IC    R14,STRNO               PLH SEQUENCE NUMBER
         LA    R14,1(,R14)             PLH SEQUENCE NUMBER
         STC   R14,STRNO               PLH SEQUENCE NUMBER
        #S0C4  PRACB99
         CLC   0(210,R3),0             VALIDATE
         CLC   =C'PLH',PLHIDENT        VALIDATE
         BNE   PRACB99                 LOST, QUIT
         STM   R2,R3,SAVPLHPT          SAVE A(IDAPLHDR,IDAPLH)
         CLI   IDAPLH,FF               VALIDATE
         BNE   PRACB69                 PLH NOT IN USE, TRY NEXT ONE
*
*        CHECK IF THE PLH ENTRY POINTS TO MY ACB;
*        IF MACRF=LSR/GSR, THE PLH ENTRY COULD POINT TO ANOTHER
*        ACB THAT SHARES THE SAME BUFFER POOL.
*
         C     R6,PLHACB               VALIDATE
         BE    PRACB45                 OK, JUMP
         LA    R0,IFGACB+1             VALIDATE
         C     R0,PLHACB               VALIDATE
         BNE   PRACB69                 NOT FOR THIS ACB, IGNORE IT
*
PRACB45  MVI   MINLINES,10
         STRING '0  PLH AT LOCATION ',((R3),,X),INTO=LINE121
         CLC   PLHCNT,=H'1'            STRNO=1?
         BE    PRACB45P                YES, DO NOT COUNT PLHS
         STRING '0  PLH',(STRNO,FL1,L),' AT LOCATION ',((R3),,X),      X
               INTO=LINE121
PRACB45P CAL#PROC SPACE1          <== PRINT LINE
*
PRACB46  LA    R0,210                  PLH LENGTH
         LA    R1,IDAPLH               PASS PLH ADDRESS
         CAL#PROC DUMP32          <== PRINT PLH
*
*        PRINT RPL
*
PRACB50  L     R4,PLHCRPL              PICK UP RPL ADDRESS
         USING IFGRPL,R4
         C     R4,REGION24             VALIDATE
         BL    PRACB99                 LOST, QUIT
        #S0C4  PRACB99
         CLC   IFGRPL(76),0            VALIDATE RPL
*
         BAL   R1,PRACB51R             BRANCH AROUND TABLE
         DC    C'GET  ',AL1(RPLGET)    REQUEST
         DC    C'PUT  ',AL1(RPLPUT)    REQUEST
         DC    C'POINT',AL1(RPLPOINT)  REQUEST
         DC    C'ERASE',AL1(RPLERASE)  REQUEST
         DC    C'*REQ ',AL1(255)       UNKNOWN REQUEST
*--LOOP
PRACB51R CLC   RPLREQ,5(R1)            COMPARE RPL REQUEST CODE
         BE    PRACB51S                EQUAL, EXIT LOOP
         LA    R1,5+1(,R1)             NEXT ENTRY
         CLI   5(R1),255               END OF TABLE?
         BNE   PRACB51R                NO, TRY NEXT ENTRY
*--ENDLOOP
PRACB51S MVI   MINLINES,6
         STRING '0  RPL AT LOCATION ',((R4),,X),3X,                    X
               ((R1),5,T),',OPTCD=(',INTO=LINE121
         CLC   PLHCNT,=H'1'            STRNO=1?
         BE    PRACB51J                YES, DO NOT COUNT PLHS
         STRING '0  RPL',(STRNO,FL1,L),' AT LOCATION ',((R4),,X),3X,   X
               ((R1),5,T),',OPTCD=(',INTO=LINE121
*
*        DISPLAY OPTCD
*
PRACB51J BAS   R1,PRACB51A             BR AROUND TABLE
         DC    C'LOC',AL1(RPLLOC)      80 00
         DC    C'DIR',AL1(RPLDIR)      40 00
         DC    C'SEQ',AL1(RPLSEQ)      20 00
         DC    C'SKP',AL1(RPLSKP)      10 00
         DC    C'KGE',AL1(RPLKGE)      04 00
         DC    C'GEN',AL1(RPLGEN)      02 00
PRACB51A TM    RPLOPT1,*-*
*LOOP
PRACB51B IC    R14,3(,R1)              MASK
         EX    R14,PRACB51A
         BZ    PRACB51C
         STRING (LINE,,T),((R1),3,T),',',INTO=LINE
PRACB51C LA    R1,3+1(,R1)             NEXT ENTRY
         CLI   0(R1),C'A'              END OF TABLE?
         BNL   PRACB51B                NOT YET, LOOP MORE
*ENDLOOP
         BAS   R1,PRACB52A             BR AROUND TABLE
         DC    C'KEY',AL1(RPLKEY)      00 80
         DC    C'ADR',AL1(RPLADR)      00 40
         DC    C'CNV',AL1(RPLCNV)      00 20
         DC    C'BWD',AL1(RPLBWD)      00 10
         DC    C'UPD',AL1(RPLUPD)      00 02
         DC    C'NSP',AL1(RPLNSP)      00 01
PRACB52A TM    RPLOPT2,*-*
*LOOP
PRACB52B IC    R14,3(,R1)              MASK
         EX    R14,PRACB52A
         BZ    PRACB52C
         STRING (LINE,,T),((R1),3,T),',',INTO=LINE
PRACB52C LA    R1,3+1(,R1)             NEXT ENTRY
         CLI   0(R1),C'A'              END OF TABLE?
         BNL   PRACB52B                NOT YET, LOOP MORE
*ENDLOOP
         LA    R15,LINE-1(R15)         LAST COMMA
         MVI   0(R15),C')'             CLOSE SUB-LIST
         OC    RPLFDBK,RPLFDBK         ANY ERROR?
         BZ    PRACB53                 NO, JUMP
         STRING (LINE,,T),',FDBK=',(RPLFDBK,,X),INTO=LINE
PRACB53  CAL#PROC SPACE1          <== PRINT RPL HEADER
*
         SLR   R0,R0
         IC    R0,RPLLEN               RPL LENGTH
         LA    R1,IFGRPL               RPL ADDRESS
         CAL#PROC DUMP32          <== PRINT RPL
*
         CL    R6,RPLDACB              VALIDATE
         BNE   PRACB99                 LOST, QUIT
         L     R1,RPLARG               SEARCH ARGUMENT
         C     R1,REGION24             VALIDATE
         BL    PRACB60                 NONE FOUND, JUMP
        #S0C4  PRACB60
         CLI   0(R1),0                 VALIDATE SEARCH ARGUMENT
         L     R14,$AMDSB#D            DATA AMDSB
         LH    R0,AMDKEYLN-AMDSB(,R14) KEY LENGTH
         MVI   MINLINES,5
         STRING '0    SEARCH ARGUMENT:',INTO=LINE121
         CAL#PROC SPACE1          <== PRINT LINE
         LTR   R0,R0                   IS THIS A KSDS?
         BNZ   PRACB59                 YES, JUMP
         LA    R0,004                  NO, ARG IS AN RBA
PRACB59  CAL#PROC PDUMP           <== PRINT KEY
         DROP  R4                      RPL
*
*        PRINT CURRENT RECORD (FROM PLH)
*
PRACB60  MVI   MINLINES,6
         LM    R2,R3,SAVPLHPT          RESTORE A(PLHDR,PLH)
         ICM   R0,B'1111',PLHLRECL     PICK UP RECORD LENGTH
         BNP   PRACB68                 ZERO, QUIT
         L     R1,PLHRECP              PICK UP RECORD ADDRESS
         C     R1,REGION24             VALIDATE
         BL    PRACB68                 LOST, QUIT
        #S0C4  PRACB68
         CLI   0(R1),0                 VALIDATE
         STRING '0    CURRENT RECORD:',INTO=LINE121
         CAL#PROC SPACE1          <== PRINT LINE
         CAL#PROC PDUMP           <== PRINT RECORD
         B     PRACB69
PRACB68  STRING '0    CURRENT RECORD IS NOT AVAILABLE',INTO=LINE121
 STRING (LINE121,,T),' R0=',((R0),,X),' R1=',((R1),,X),INTO=LINE121,   X
               ' R2=',((R2),,X),' R3=',((R3),,X),' R4=',((R4),,X)
         CAL#PROC SPACE1          <== PRINT LINE
*
PRACB69  LM    R2,R3,SAVPLHPT          RESTORE A(PLHDR,PLH)
*ORG PRACB69
         ICM   R3,B'1111',PLHCHAIN     DO WE HAVE ANOTHER PLH?
         BNZ   PRACB40                 YES, PROCESS IT
*ENDLOOP
         DROP  R3                      IDAPLH
         B     PRACB99
*
*        PRINT DCB IF THIS IS A JES ACB
*
PRACB70  SLR   R1,R1
         ICM   R1,B'0111',DEBRRQ+1     DCB ADDRESS (JES)
         BZ    PRACB71                 NO DCB, JUMP
        #S0C4  PRACB71
         CLC   ACBDEB,ACBDEB-IFGACB(R1) DOES THIS LOOK LIKE A DCB?
         BNE   PRACB71                 NO, JUMP
         CLC   ACBTIOT,ACBTIOT-IFGACB(R1) DOES THIS LOOK LIKE A DCB?
         BNE   PRACB71                 NO, JUMP
         LR    R6,R1                   PASS DCB ADDRESS
         CAL#PROC PRDCB           <== PRINT DCB ALSO
**       L     R7,DCBCICB              CICB
**       USING CICB,R7
         CL    R6,CURR#REC             CURRENT RECORD PRINTED ALREADY?
         BE    PRACB99                 YES, QUIT
         ICM   R6,B'0111',DEBDCBAD+1   RELOAD ACB ADDRESS (JES)
*
*        RETRIEVE CURRENT RECORD: DEB->SDB->UBF->BFD
*
PRACB71 #S0C4  PRACB99
         TM    DEBOPATB,DEBXTEND       OPEN FOR EXTEND/OUTPUT?
         BNO   PRACB99                 NO, QUIT
         L     R7,DEBIRBAD             SDB (SUB-SYSTEM DATA SET BLOCK)
         ICM   R7,B'1000',X00          CLEAN UP ADDRESS
         USING SDB,R7
         CLC   =C'SDB ',SDBID          SDBID
         BNE   PRACB99                 I'M LOST, QUIT
        #S0C4  PRACB72N
*        OFFSETS TO SDBDEB,SDBUBF,BFDDATA,SDBLOC
         BAL   R2,PRACB72              BR AROUND TABLE
         DC    X'007C,00B4,0036,0018'  XA 2.2
         DC    X'0084,00E4,0084,002C'  ESA 420
         DC    X'008C,00EC,0088,002C'  OS/390 1.3
*LOOP
PRACB72  LH    R1,0(,R2)               SDBDEB-SDB
         L     R1,SDB(R1)              SDBDEB
         CLM   R1,B'0111',ACBDEB       SDBDEB
         BNE   PRACB72N                NOT MY JES2, JUMP
         LH    R1,2(,R2)               SDBUBF-SDB
         L     R4,SDB(R1)              SDBUBF (BUFFER ADDRESS)
         USING UBF,R4
         CLC   =C'UBF ',UBFID          BFDID
         BE    PRACB81                 THIS IS MY JES2, JUMP
PRACB72N LA    R2,2+2+2+2(,R2)         NEXT TABLE ENTRY
         CLC   PRACB72,0(R2)           SDBDEB-SDB
         BNE   PRACB72                 SCAN BUFFER
*ENDLOOP
         B     PRACB99                 I'M LOST, QUIT
*
*        SCAN JES2 UNPROTECTED BUFFER TO RETRIEVE THE CURRENT RECORD
*
PRACB81  LH    R1,4(,R2)               BFDDATA-UBF
         LA    R0,UBF(R1)              BFDDATA
         LH    R1,6(,R2)               SDBLOC-UBF
         L     R3,UBF(R1)              SDBLOC (CURR POS IN BUFFER)
         SLR   R1,R1
         CR    R3,R0                   ANY RECORD YET?
         BE    PRACB98                 NO, QUIT
*LOOP
PRACB81C LR    R2,R0
         USING LRC,R2
         IC    R1,LRCTLENG             LENGTH OF TEXT
         TM    LRCFLAG1,LRC1CCTL       CTLCHR PRESENT?
         BNO   PRACB81N                NO, JUMP
         LA    R1,1(,R1)               YES, ADD 1 TO LENGTH
PRACB81N LA    R0,LRCTEXT(R1)          POINT PAST RECORD
         CLR   R0,R3                   WAS THIS THE LAST RECORD?
         BL    PRACB81C                NO, LOOP FURTHER
*ENDLOOP
         MVI   MINLINES,6
         STRING '0    CURRENT RECORD:',INTO=LINE121
         CAL#PROC SPACE1          <== PRINT LINE
         LR    R0,R1                   PASS LENGTH
         LA    R1,LRCTEXT              PASS ADDRESS
         CAL#PROC PDUMP           <== DUMP CURRENT RECORD
         B     PRACB99                 NO, QUIT
*
PRACB98  STRING '0    NO RECORD WRITTEN YET.',INTO=LINE121
         CAL#PROC SPACE1          <== PRINT LINE
*
PRACB99  END#PROC
         DROP  R4,R5,R6,R7             UBF, DEB, ACB, SDB
*
*----------------------------------------------------------------------
*    FORMAT CCW CHAIN WITH DATA
*----------------------------------------------------------------------
*
         PUSH  USING
FORMCCW  BEG#PROC ,
         STM   R0,R8,SUBSAVE1     JUST IN CASE
         #S0C4 FORMCC99,PUSH
         LR    R6,R1              COPY CCW START ADDRESS
         SRL   R1,3
         SLL   R1,3
         CR    R6,R1              DOUBLE-WORD BOUNDARY?
         BNE   FORMCC99           NO; QUIT
         ST    R6,TENWORDS
         STRING '0  CCWs AT LOCATION ',(SUBSAVE1+5,3,X),INTO=LINE121
         CAL#PROC SPACE1
         LA    R7,20         LIMIT CCW IN CASE OF BAD TICs
FORMCCL0 SR    R8,R8         SET CONDITIONAL CCW FLAG OFF
FORMCCLP LM    R3,R4,0(R6)   LOAD CCW
         N     R3,=X'00FFFFFF'    MASK ADDRESS
         N     R4,=X'0000FFFF'    AND LENGTH
         LA    R5,0(R4,R3)
         STM   R3,R5,TENWORDS
         STRING 7X,(TENWORDS+9,3,X),1X,(0(R6),1,X),1X,(1(R6),3,X),     *
               1X,(4(R6),1,X),1X,(5(R6),1,X),1X,(6(R6),2,X),           *
               INTO=LINE121
         CAL#PROC SPACE1
         CLI   0(R6),8                 TIC?
         BE    FORMCCUP                YES; GET NEXT CCW
         CLI   4(R6),X'10'             SKIP ?
         BE    FORMCCUP                YES; GET NEXT CCW
*LOOP
FORMCCDL LR    R0,R3                   GET ADDRESS
         SL    R0,TENWORDS             MAKE OFFSET
         LR    R1,R3                   COPY ADDRESS
         STM   R0,R1,TENWORDS+12       OFFSET, ADDRESS
         L     R4,TENWORDS+8           END ADDRESS
         SR    R4,R3                   # OF REMAINING BYTES
         BNP   FORMCCUP                FINISHED, QUIT BUFFER
         MVC   MSG20,BLANKS            CLEAR WORK AREA
         CH    R4,=H'16'
         BL    *+8
         LH    R4,=H'16'
         LR    R14,R4                  PASS LENGTH FOR MVC
         BCTR  R14,0                   FOR EX
         EX    R14,*+4                 MOVE FIELD
         MVC   MSG20(*-*),0(R3)        MOVE FIELD
         TR    MSG20,TRTPRINT
         LR    R0,R4                   PASS LENGTH
         CH    R0,=H'12'
         BH    FRMCC16                 SNAP 16 BYTES
         CH    R0,=H'08'
         BH    FRMCC12                 SNAP 12 BYTES
         CH    R0,=H'04'
         BH    FRMCC08                 SNAP  8 BYTES
FRMCC04  STRING ((R3),(R0),X),INTO=(MSG60,36)
         B     FRMCC33
FRMCC08  SH    R0,=H'04'
         STRING ((R3),4,X),1X,(4(R3),(R0),X),INTO=(MSG60,36)
         B     FRMCC33
FRMCC12  SH    R0,=H'08'
         STRING ((R3),4,X),1X,(4(R3),4,X),2X,(8(R3),(R0),X),           X
               INTO=(MSG60,36)
         B     FRMCC33
FRMCC16  SH    R0,=H'12'
         STRING (0(R3),4,X),1X,(4(R3),4,X),2X,                         X
               (8(R3),4,X),1X,(12(R3),(R0),X),INTO=(MSG60,36)
FRMCC33  STRING 13X,'  +',(TENWORDS+12+2,2,X),1X,(TENWORDS+12+4,4,X),3XX
               ,(MSG60,36),'   *',(MSG20,16),'*',                      X
               INTO=LINE
         CAL#PROC SPACE1L         <== PRINT WITH LOWER-CASE
*
*        DO NOT PRINT LINES WITH IDENTICAL CONTENTS
*
         CH    R4,=H'16'               INCREMENT
         BNE   FRMCC39                 NO, JUMP
         LA    R14,48(,R3)             48 BYTES BEYOND CURRENT ADDR
         CL    R14,TENWORDS+8          MORE THAN 48 BYTES LEFT?
         BH    FRMCC39                 NO, JUMP
         CLC   16(32,R3),0(R3)         3 IDENTICAL LINES?
         BNE   FRMCC39                 NO, JUMP
         LA    R0,16(,R3)              START OF -SAME- AREA
*--LOOP
FRMCC37L CLC   16(16,R3),0(R3)         SAME CONTENTS?
         BNE   FRMCC38                 YES, EXIT
         ALR   R3,R4                   NEXT LINE
         LA    R14,32(,R3)             32 BYTES BEYOND CURRENT ADDR
         CL    R14,TENWORDS+8          MORE THAN 32 BYTES LEFT?
         BNH   FRMCC37L
*--ENDLOOP
FRMCC38  LR    R14,R0                  FIRST -SAME- LINE
         SL    R14,TENWORDS            MAKE OFFSET
         LR    R15,R3                  LAST -SAME LINE
         ALR   R15,R4                  FIRST NON-SAME- LINE
         SLR   R15,R0                  SIZE OF -SAME- AREA
         STM   R14,R0,TENWORDS+12      OFFSET, SIZE, START
         LA    R0,15(,R3)              LAST -SAME- BYTE
         STRING 13X,'  +',(TENWORDS+12+2,2,X),1X,                      X
               (TENWORDS+20,4,X),'-',((R0),,X),                        X
               ' (X''',(TENWORDS+18,2,X),''' BYTES) SAME AS ABOVE',    X
               INTO=LINE
         CAL#PROC SPACE1          <== PRINT LINE
FRMCC39  BXLE  R3,R4,FORMCCDL
*ENDLOOP
FORMCCUP LR    R4,R6                   SAVE OLD CCW
         LA    R6,8(,R6)               POSITION TO NEXT CCW
         LTR   R8,R8                   SKIP FLAG ON ?
         BNZ   FORMCCL0                YES; RESET AND PROCESS NEXT
         CLI   0(R4),8                 TIC ?
         BNE   FORMCCDT                NO
         ICM   R6,7,1(R4)              GO TO TIC ADDRESS
         BCT   R7,FORMCCLP             AND PROCESS
         B     FORMCC99                TOO MANY CCWS OR LOOP IN CHAIN
FORMCCDT MVC   TENWORDS(1),0(R4)       COPY CCW
         L     R14,=A(CCWTRTAB)        GET SKIP TABLE
         TR    TENWORDS(1),0(R14)      CONVERT
         IC    R8,TENWORDS             LOAD FLAG
         TM    4(R6),X'C0'             DATA OR CCW CHAIN ?
         BNZ   FORMCCLP                YES; DO NEXT
         LTR   R8,R8                   DO SKIP ?
         BNZ   FORMCCLP
FORMCC99 #S0C4 POP
         LM    R0,R8,SUBSAVE1     UST IN CASE
         END#PROC ,
*
*----------------------------------------------------------------------
*        SCAN JPAQ/LPAD TO FIND CALLER'S CDE
*----------------------------------------------------------------------
*
CSVQRY00 BEG#PROC
         STM   R6,R7,TENWORDS          SAVE R6, R7
         N     R1,MASKADDR             CLEAN-UP ADDRESS
         L     R5,PSATOLD-PSA          MY TCB
         L     R5,TCBJSTCB-TCB(,R5)    JOB STEP TCB
         ICM   R5,B'1111',TCBJPQ-TCB(R5)  FIRST CDE ON JPA QUEUE
         BZ    CSVQRY20                EMPTY QUEUE, SKIP SEARCH
         USING CDENTRY,R5
*LOOP
CSVQRY11 TM    CDATTR2,CDXLE           IS THERE AN EXTENT LIST?
         BNO   CSVQRY12                NO, JUMP
         TM    CDATTR,CDNIC            THIS MODULE BEING LOADED?
         BO    CSVQRY12                YES, JUMP
         L     R7,CDXLMJP              EXTENT LIST
         USING XTLST,R7
         C     R1,XTLMSBAD-1+&MVSXA    WITHIN BOUNDARIES?       GP09255
         BL    CSVQRY12                NO, IGNORE
         L     R15,XTLMSBLA            LENGTH OF LOAD MOD
         LA    R15,0(,R15)             CLEAR HI-ORDER BIT
         A     R15,XTLMSBAD-1+&MVSXA   ADD LOAD POINT ADDRESS   GP09255
         CR    R1,R15                  WITHIN BOUNDARIES?
         BL    CSVQRY75                YES, PROCESS
CSVQRY12 ICM   R5,B'1111',CDCHAIN      NEXT CDE IN QUEUE
         BNZ   CSVQRY11                GOOD ADDR, PROCESS
*ENDLOOP
*
*        SCAN ACTIVE LPA QUEUE (MLPA/FLPA)
*
CSVQRY20 L     R5,CVTPTR
         L     R5,CVTQLPAQ-CVTMAP(,R5) ACTIVE LPA QUEUE
         ICM   R5,B'1111',0(R5)        FIRST CDE ON QUEUE
         BZ    CSVQRY60                EMPTY QUEUE, SKIP SEARCH
         USING CDENTRY,R5
*LOOP
CSVQRY21 TM    CDATTR2,CDXLE           IS THERE AN EXTENT LIST?
         BNO   CSVQRY22                NO, JUMP
         L     R7,CDXLMJP              EXTENT LIST
         USING XTLST,R7
         C     R1,XTLMSBAD-1+&MVSXA    WITHIN BOUNDARIES?       GP09255
         BL    CSVQRY22                NO, IGNORE
         L     R15,XTLMSBLA            LENGTH OF LOAD MOD
         LA    R15,0(,R15)             CLEAR HI-ORDER BIT
         A     R15,XTLMSBAD-1+&MVSXA   ADD LOAD POINT ADDRESS   GP09255
         CR    R1,R15                  WITHIN BOUNDARIES?
         BL    CSVQRY75                YES, PROCESS
CSVQRY22 ICM   R5,B'1111',CDCHAIN      NEXT CDE IN QUEUE
         BNZ   CSVQRY21                GOOD ADDR, PROCESS
*ENDLOOP
*
*        SCAN LPA DIRECTORY IF MODULE NOT FOUND IN JPAQ
*
CSVQRY60 L     R6,CVTPTR
         L     R6,CVTLPDIA-CVTMAP(,R6)
         USING LPDE,R6
*LOOP
CSVQRY61 TM    LPDEATTR,LPDEMIN        MINOR LPDE?
         BO    CSVQRY62                YES, JUMP
         LM    R15,R0,LPDEXTLN         LENGTH/LOAD ADDR
         CR    R0,R1
         BH    CSVQRY62                OUTSIDE BOUNDARIES, JUMP
         AR    R0,R15
         CR    R0,R1
         BH    CSVQRY71                MODULE FOUND, JUMP
CSVQRY62 LA    R6,LPDEXTAD+4           BUMP LPDE ADDR
         CLI   LPDENAME,FF             END OF LPA DIRECTORY?
         BNE   CSVQRY61                NO, LOOP FURTHER
*ENDLOOP
         AIF   (&MVSXA).GONUC                                   GP09255
         B     CSVQRY95                NOT FOUND                GP09255
         AGO   .SKNUC                                           GP09255
.GONUC   B     CSVQRY81                NOT FOUND IN LPA
.SKNUC   ANOP  ,                                                GP09255
*
*        FOUND IN LPAD
*
CSVQRY71 LR    R5,R6                   CDE=LPDE
         S     R1,LPDEXTAD             GET OFFSET
         MVC   EPNAME,LPDENAME         PASS EP NAME
         B     CSVQRY85
*
*        FOUND IN JPAQ
*
CSVQRY75 S     R1,XTLMSBAD-1+&MVSXA    GET OFFSET               GP09255
         MVC   EPNAME,CDNAME           PASS EP NAME
         AIF   (NOT &MVSESA).NONUCLK                            GP09255
         B     CSVQRY85
         DROP  R5,R6,R7                CDE, LPDE, XTLST
*
*        LOOK FOR A NUCLEUS MODULE
*
CSVQRY81 LR    R5,R1                   SAVE ADDRESS
         LR    R0,R1                   PASS ADDRESS
         NUCLKUP BYADDR,ADDR=(R0),NAME=EPNAME
         LTR   R15,R15                 ADDR FOUND IN NUCLEUS?
         BNZ   CSVQRY95                NO, QUIT
         LR    R1,R5                   RESTORE ADDRESS
         SLR   R1,R0                   GET OFFSET IN R1
*
.NONUCLK ANOP  ,                                                GP09255
CSVQRY85 ST    R1,TENWORDS+12          STORE OFFSET
         STRING '+X''',(TENWORDS+14,2,X),'''',INTO=OFFSET
         CLI   TENWORDS+13,00
         BE    CSVQRY88                OFFSET LT 64K, JUMP
         STRING '+X''',(TENWORDS+13,3,X),'''',INTO=OFFSET
CSVQRY88 STRING (EPNAME,,L),OFFSET,INTO=MSG20
         B     CSVQRY99                QUIT
*        NOT FOUND ANYWHERE
*
CSVQRY95 SR    R5,R5
         MVC   MSG20,BLANKS            NO PGM FOUND
         MVC   EPNAME,=C'*UNKNOWN'     NO PGM FOUND
*
CSVQRY99 LM    R6,R7,TENWORDS          RESTORE R6, R7
         END#PROC
*
*----------------------------------------------------------------------
*        PRINT OBJECT INSTRUCTION IF X'44' OPCODE
*----------------------------------------------------------------------
*
EXECUTE  BEG#PROC
         CLI   SDWAILC1,4              ILC=4?
         BNE   EXEC999                 NO, QUIT
        #S0C4  EXEC999
         CLI   0(R6),X'44'             IS THIS AN EXECUTE?
         BNE   EXEC999                 NO, QUIT
*
         ICM   R0,B'0001',1(R6)        R1,RX
         LA    R2,X'000F'              MASK FOR NR
         NR    R2,R0                   R2=(RX)
         BZ    EXEC12                  NO RX, JUMP
         AR    R2,R2                   R*4
         AR    R2,R2                   R*4
         L     R2,SDWAGRSV(R2)         POINT TO RX SLOT
*
EXEC12   SR    R14,R14
         ICM   R14,B'0011',2(R6)       D2(B2)
         LA    R15,X'0FFF'             MASK FOR "NR"
         NR    R15,R14                 R15=D2
         SRL   R14,12                  R14=(B2)
         AR    R14,R14                 R14*2
         BZ    EXEC30                  B2=R0, JUMP
         AR    R14,R14                 R14*4
         AL    R2,SDWAGRSV(R14)        ADD VALUE(B2)
*
EXEC30   LA    R6,0(R15,R2)            ADD D2, CLEAN UP
         N     R6,MASKADDR             CLEAN-UP ADDRESS
         CAL#PROC PRINTI          <== PRINT INSTRUCTION (USES R6)
         STRING '   EXECUTED INSTRUCTION AT LOCATION ',((R6),,X),      X
               ' IS ',(MSG20,(R1)),INTO=LINE121
         CAL#PROC SPACE1          <== PRINT LINE
EXEC999  END#PROC
*
*----------------------------------------------------------------------
*        PRINT INSTRUCTION
*----------------------------------------------------------------------
*
PRINTI   BEG#PROC
         STRING '**** **** ****',INTO=MSG20 INIT OUTPUT AREA
         LA    R1,014                  MAX LENGTH
        #S0C4  PRINTI9
         MVC   WKCELL1(2),0(R6)        1ST HALFWORD
         UNPK  MSG20+0(5),WKCELL1+0(3) CONVERT TO HEX
         TR    MSG20+0(4),TABHEX-240   CONVERT TO HEX
         LA    R1,004                  4 BYTES
         CLI   0(R6),X'40'             TEST INSTRUCTION LENGTH
         BL    PRINTI9                 OK, JUMP
         MVC   WKCELL1(4),0(R6)        2 HALFWORDS
         MVI   MSG20+4,C' '            CLEAN UP
         UNPK  MSG20+5(5),WKCELL1+2(3) CONVERT TO HEX
         TR    MSG20+5(4),TABHEX-240   CONVERT TO HEX
         LA    R1,009                  9 BYTES
         CLI   0(R6),X'C0'             TEST INSTRUCTION LENGTH
         BL    PRINTI9                 OK, JUMP
         MVC   WKCELL1(6),0(R6)        6 HALFWORDS
         MVI   MSG20+9,C' '            CLEAN UP
         UNPK  MSG20+10(5),WKCELL1+4(3) CONVERT TO HEX
         TR    MSG20+10(4),TABHEX-240  CONVERT TO HEX
         MVI   MSG20+14,C' '           CLEAN UP
         LA    R1,014                  55 BYTES
PRINTI9 #S0C4  RESET
         END#PROC
*
*----------------------------------------------------------------------
*
*        PDUMP ROUTINE    R0=LENGTH,R1=ADDRESS
*
*+0000 00036800  C1C2C3C4 812653D8  00000000 00000000  *ABCDA  Q      *
*+0010 00036810-0003685F SAME AS ABOVE (X'0050' BYTES)
*+0060 00036860  00FD3BF0 00000000  078D2000 00C941A2  *   0          *
*----------------------------------------------------------------------
*
PDUMP    BEG#PROC
        #S0C4  PDUMP99,PUSH
         STM   R0,R3,TENWORDS          KEEP LENGTH AND ADDRESS
         LTR   R0,R0                   LENGTH=0?
         BNP   PDUMP99                 YES, QUIT
         MVI   LINE121,C'0'            CTLCHR
         L     R3,TENWORDS             LENGTH
         AL    R3,TENWORDS+4           LIMIT
*FAILS*  BCTR  R3,0                    BXLE LIMIT               GP09260
*LOOP
PDUMP4   LR    R0,R1                   GET ADDRESS
         SL    R0,TENWORDS+4           MAKE OFFSET
         STM   R0,R1,TENWORDS+12       OFFSET, ADDRESS
         L     R2,TENWORDS             LENGTH
         AL    R2,TENWORDS+4           1ST BYTE AFTER AREA
         SR    R2,R1                   # OF REMAINING BYTES
         BNP   PDUMP99                 FINISHED, QUIT
*
         MVC   MSG20,BLANKS            CLEAR WORK AREA
         CH    R2,=H'16'
         BL    *+8
         LH    R2,=H'16'
         LR    R14,R2                  PASS LENGTH FOR MVC
         BCTR  R14,0                   FOR EX
**      #S0C4  PDUMP99
         EX    R14,*+4                 MOVE FIELD
         MVC   MSG20(*-*),0(R1)        MOVE FIELD
**      #S0C4  RESET
         TR    MSG20,TRTPRINT
*
         LR    R0,R2                   PASS LENGTH
         CH    R0,=H'12'
         BH    PDUMP16                 SNAP 16 BYTES
         CH    R0,=H'08'
         BH    PDUMP12                 SNAP 12 BYTES
         CH    R0,=H'04'
         BH    PDUMP08                 SNAP  8 BYTES
PDUMP04  STRING ((R1),(R0),X),INTO=(MSG60,36)
         B     PDUMP33
PDUMP08  SH    R0,=H'04'
         STRING ((R1),4,X),1X,(4(R1),(R0),X),INTO=(MSG60,36)
         B     PDUMP33
PDUMP12  SH    R0,=H'08'
         STRING ((R1),4,X),1X,(4(R1),4,X),2X,(8(R1),(R0),X),           X
               INTO=(MSG60,36)
         B     PDUMP33
PDUMP16  SH    R0,=H'12'
         STRING (0(R1),4,X),1X,(4(R1),4,X),2X,                         X
               (8(R1),4,X),1X,(12(R1),(R0),X),INTO=(MSG60,36)
PDUMP33  STRING '  +',(TENWORDS+12+2,2,X),1X,(TENWORDS+12+4,4,X),3X,   X
               (MSG60,36),'   *',(MSG20,16),'*',                       X
               INTO=LINE
         CAL#PROC SPACE1L         <== PRINT WITH LOWER-CASE
*
*        DO NOT PRINT LINES WITH IDENTICAL CONTENTS
*
         CH    R2,=H'16'               INCREMENT
         BNE   PDUMP39                 NO, JUMP
         LA    R14,48(,R1)             48 BYTES BEYOND CURRENT ADDR
         CLR   R14,R3                  MORE THAN 48 BYTES LEFT?
         BH    PDUMP39                 NO, JUMP
         CLC   16(32,R1),0(R1)         3 IDENTICAL LINES?
         BNE   PDUMP39                 NO, JUMP
         LA    R0,16(,R1)              START OF -SAME- AREA
*--LOOP
PDUMP37L CLC   16(16,R1),0(R1)         SAME CONTENTS?
         BNE   PDUMP38                 YES, EXIT
         ALR   R1,R2                   NEXT LINE
         LA    R14,32(,R1)             32 BYTES BEYOND CURRENT ADDR
         CLR   R14,R3                  MORE THAN 32 BYTES LEFT?
         BNH   PDUMP37L
*--ENDLOOP
PDUMP38  LR    R14,R0                  FIRST -SAME- LINE
         SL    R14,TENWORDS+4          MAKE OFFSET
         LR    R15,R1                  LAST -SAME LINE
         ALR   R15,R2                  FIRST NON-SAME- LINE
         SLR   R15,R0                  SIZE OF -SAME- AREA
         STM   R14,R0,TENWORDS+12      OFFSET, SIZE, START
         LA    R0,15(,R1)              LAST -SAME- BYTE
         STRING '  +',(TENWORDS+12+2,2,X),1X,                          X
               (TENWORDS+20,4,X),'-',((R0),,X),                        X
               ' (X''',(TENWORDS+18,2,X),''' BYTES) SAME AS ABOVE',    X
               INTO=LINE
         CAL#PROC SPACE1          <== PRINT LINE
PDUMP39  BXLE  R1,R2,PDUMP4
*ENDLOOP
PDUMP99  LM    R0,R3,TENWORDS          LENGTH/START ADDRESS
        #S0C4  POP
         END#PROC
*
*----------------------------------------------------------------------
*        DUMP32 ROUTINE    R0=LENGTH,R1=ADDRESS
*----------------------------------------------------------------------
*
DUMP32   BEG#PROC
         LTR   R1,R1                   HI-ORDER BIT ON?
         LA    R1,0(,R1)               CLEAN UP HI-BIT
         STM   R0,R2,TENWORDS          KEEP LENGTH AND ADDRESS
         BM    DUMP32A                 YES, NO DOUBLE SPACE
         MVI   LINE121,C'0'            CTLCHR
*LOOP
DUMP32A  LR    R0,R1                   GET ADDRESS
         SL    R0,TENWORDS+4           MAKE OFFSET
         STM   R0,R1,TENWORDS+12       OFFSET, ADDRESS
         L     R0,TENWORDS             LENGTH (R0)
         AL    R0,TENWORDS+4           1ST BYTE AFTER AREA (ADD R1)
         SR    R0,R1                   # OF REMAINING BYTES
         BNP   DUMP32Z                 FINISHED, QUIT
         STRING '   +',(TENWORDS+12+3,1,X),INTO=LINE
         OC    TENWORDS(3),TENWORDS    OFFSET > FF ?
         BZ    DUMP32L                 NO, JUMP
         STRING '  +',(TENWORDS+12+2,2,X),INTO=LINE
DUMP32L  LA    R2,LINE+8               FIRST WORD
         CAL#PROC DUMP32X             00-03
         CAL#PROC DUMP32X             04-07
         CAL#PROC DUMP32X             08-11
         CAL#PROC DUMP32X             12-15
         LA    R2,1(,R2)               WIDE MARGIN IN THE MIDDLE
         CAL#PROC DUMP32X             16-19
         CAL#PROC DUMP32X             20-23
         CAL#PROC DUMP32X             24-27
         CAL#PROC DUMP32X             28-31
         CAL#PROC SPACE1          <== PRINT LINE
         LTR   R0,R0                   # OF REMAINING BYTES
         BP    DUMP32A                 PRINT NEXT 32 BYTES
*ENDLOOP
DUMP32Z  LM    R0,R2,TENWORDS          LENGTH/START ADDRESS
         END#PROC
*
DUMP32X  LTR   R0,R0                   # OF REMAINING BYTES
         BNPR  R14                     FINISHED, QUIT
         MVC   0(4,R2),0(R1)           PREVENT S0C4 IF R1=.....FFC
         UNPK  0(9,R2),0(5,R2)         TRANSLATE TO HEX
         TR    0(8,R2),TABHEX-240      TRANSLATE TO HEX
         SH    R0,=H'4'                DECREMENT LENGTH
         LA    R1,4(,R1)               PTR IN INPUT AREA
         MVI   8(R2),C' '
         LA    R2,9(,R2)               PTR IN OUTPUT LINE
         BR    R14
         LTORG ,                                                GP09255
*
*----------------------------------------------------------------------
*        PRINT ROUTINE (COPIED TO DYNAM24)
*----------------------------------------------------------------------
*
         PUSH  USING                                            GP09255
         DROP  ,                                                GP09255
         USING DYNAM24,R12                                      GP09255
         USING DYNAM31,R13                                      GP09255
SPACE2   ST    R14,SPACE2RE            SAVE R14
         BALR  R15,0              TEMPORARY BASE                GP09255
         USING *,R15                                            GP09255
         CAL#PROC SPACE1          <== PRINT CURRENT LINE
         L     R14,SPACE2RE            RESTORE R14
         BALR  R15,0              TEMPORARY BASE                GP09255
         USING *,R15                                            GP09255
         B     SPACE1L                 PRINT BLANK LINE
*        PRINT BLANK LINE
BLANK1   MVC   LINE121,BLANKS          BLANK LINE
         BALR  R15,0              TEMPORARY BASE                GP09255
         USING *,R15                                            GP09255
         B     SPACE1L                 PRINT BLANK LINE
*        CONVERT TO CAPS
SPACE1   TR    LINE,TRTPRINT           GET RID OF NON-PRINTABLE CHARS
         OC    LINE,BLANKS             MAKE IT ALL CAPS
*
SPACE1L  BALR  R15,0
         USING *,R15                                            GP09255
         SAVE  (14,3),,SPACE1                                   GP09255
         BALR  R3,0          NEW LOCAL BASE                     GP09255
         DROP  R15                                              GP09255
         USING *,R3                                             GP09255
         CLC   MINLINES,RLINES         ENOUGH LINES LEFT?
         BH    SPACE1D                 NO, JUMP
         LA    R0,3                    COUNT LINES
         CLI   LINE121,C'-'            COUNT LINES
         BE    SPACE1B                 COUNT LINES
         LA    R0,2                    COUNT LINES
         CLI   LINE121,C'0'            COUNT LINES
         BE    SPACE1B                 COUNT LINES
         LA    R0,1                    COUNT LINES
SPACE1B  SR    R1,R1
         ICM   R1,1,RLINES             ANY LINES LEFT?
         BZ    SPACE1D                 NO, JUMP
         SR    R1,R0                   DECREMENT LINE COUNT
         BNM   SPACE1K                 NOT END-OF-PAGE YET, JUMP
SPACE1D  MVI   LINE121,C'1'            NEW PAGE
         LA    R1,060-1                RESET LINE COUNTER
         CLC   LINE,BLANKS             BLANK LINE?
         BNE   SPACE1K                 NO, JUMP
         MVI   LINE121,C'+'            YES, KEEP IT
         SR    R1,R1                   RESET LINE COUNTER
SPACE1K  STCM  R1,1,RLINES             NUMBER OF REMAINING LINES
         MVI   MINLINES,0              RESET MINLINES
*
         ST    R13,DYNAM24+4           OLD SAVE AREA (DYNAM31)
         LA    R13,DYNAM24             NEW SAVE AREA (DYNAM24)
         PUSH  USING
         DROP  R13
         LA    R15,PRINT24             AMODE24
         MVC   0(PRINT24L,R15),PRINTPUT
         BASSM R2,R15                  AMODE24
         L     R13,4(,R13)             OLD SAVE AREA (DYNAM31)
         POP   USING
         MVC   LINE121,BLANKS          BLANK OUT
         RETURN (14,3)                                          GP09255
PRINTPUT PUT   DCBDEBUG,LINE121        ISSUE PUT WITH AMODE24
         BSM   0,R2                    AMODE31
PRINT24L EQU   *-PRINTPUT
         POP   USING                                            GP09255
         DROP  ,
TABHEX   DC    C'0123456789ABCDEF'
*---------------------------------------------------------------------*
*        BEG#PROC BOOTSTRAP                                         *
*---------------------------------------------------------------------*
BEG#PROC AH  R15,0(,R15)             ADD OFFSET TO FAR ROUTINE
         BR    R15                     BRANCH TO FAR ROUTINE
***********************************************************************
***********************************************************************
TABLES   CSECT ,                                                GP09260
SVCTABLE TABLE 0,EXCP,WAIT,POST,EXIT,GETMAIN,FREEMAIN,LINK,XCTL,       X
               LOAD,DELETE,GETMAIN/FREEMAIN,TIME,SYNCH,ABEND,SPIE,     X
               ERREXCP,PURGE,RESTORE,BLDL/FIND,OPEN,CLOSE,STOW,        X
               'OPEN TYPE=J','CLOSE TYPE=T',DEVTYPE,TRKBAL,            X
               LOCATE/CATALOG,OBTAIN,CVOL,SCRATCH,RENAME,FEOV,REALLOC, X
               IOHALT,MGCR/QEDIT,WTO/WTOL,WTL,SEGLD/SEGWT,,LABEL,      X
               EXTRACT,IDENTIFY,ATTACH,CIRB,CHAP,OVLYBRCH,TTIMER,      X
               STIMER,DEQ,,,SNAP/SDUMP,RESTART,RELEX,DISABLE,EOV,      X
               ENQ/RESERVE,FREEDBUF,RELBUF/REQBUF,OLTEP,STAE/ESTAE,    X
               IKJEGS6A,DETACH,CHKPT,RDJFCB,,BTAMTEST,,                X
               SYNADAF/SYNADRLS,BSP,GSERV
         TABLE 79,STATUS,,SETPRT,,SMFWTM,GRAPHICS,DDRSWAP,ATLAS,DOM
         TABLE 91,VOLSTAT,TCBEXCP,TGET/TPUT,STCC,SYSEVENT,STAX,        X
               'TSO TEST',PROTECT,DYNALLOC,IKJEFFIB,,,XLATE,,IMGLIB,,  X
               MODESET,,'TYPE 3 ESR',DSTATUS,HASPSSSM,PGRLSE,PGFIX,    X
               EXCPVR,,'TYPE 1 ESR',DEBCHK,,TESTAUTH,                  X
               GETMAIN/FREEMAIN,VSAM,'TYPE 2 ESR',PURGEDEQ,,EVENTS
         TABLE 130,RACHECK,RACINIT,RACLIST,RACDEF
         TABLE 138,PGSER,CVAF,,,,CIPHER
         DC    X'FF',0D'0'             END OF TABLE
PCKTABLE TABLE 1,OPERATION,'PRIVILEGED OPERATION',EXECUTE,             X
               PROTECTION,ADDRESSING,SPECIFICATION,DATA,               X
               'FIXED-PT OVERFLOW','FIXED-PT DIVIDE',                  X
               'DECIMAL OVERFLOW','DECIMAL DIVIDE',                    X
               'EXPONENT OVERFLOW','EXPONENT UNDERFLOW',               X
               SIGNIFICANCE,'FLOATING-PT DIVIDE',                      X
               'SEGMENT TRANSLATION','PAGE TRANSLATION',               X
               'TRANSLATION SPECIFICATION','SPECIAL OPERATION'
         DC    X'FF',0D'0'             END OF TABLE
FF       EQU   X'FF'
         SPACE 1
CCWTRTAB DC    256AL1(0)     CCW IS NOT A SKIP TYPE
         TRENT CCWTRTAB,1,X'07',X'0B',X'1B',     SEEK TYPE
         TRENT CCWTRTAB,2,X'29',X'31',X'39',X'49',X'51',X'69',X'71',
         TRENT CCWTRTAB,2,X'A9',X'B1',X'B9',X'C9',X'D1',X'E9',X'F1'
         SPACE 1
*----------------------------------------------------------------------
*        DYNAMIC STORAGE
*----------------------------------------------------------------------
DYNAM31  DSECT ,                   <== R13                      GP09255
         DS    18F                     WORK AREA                GP09260
TENWORDS DS    10F                     WORK
WKCELL1  DS    D                       WORK CELL
WKCELL2  DS    D                       WORK CELL
WKCELL3  DS    D                       WORK CELL
REGION24 DS    A,A                     START/END OF 24-BIT REGION
REGION31 DS    A,A                     START/END OF 31-BIT REGION
DYN#TCB  DS    A(TCB)                  TCB FOR FREEMAIN
DYNAM24P DS    A(DYNAM24)              24-BIT WORK AREA
SPACE2RE DS    A(R14)                  SAVE AREA FOR R14
ESTAEL   ESTAE MF=L
MASKADDR DS    A(X'00FFFFFF')          MASK FOR CLEANING UP ADDRESSES
LASTPRB  DS    A(RBPREFIX)             ADDR OF LAST PRB
FRSTSVRB DS    A(RBPREFIX)             ADDR OF FIRST SVRB
ABNDSVRB DS    A(RBPREFIX)             ADDR OF ABEND SVRB
MY#PRB   DS    A(RBPREFIX)             MY OWN RBPREFIX
CURR#R13 DS    A(R13)                  R13 AT TIME OF ABEND
BUFFER   DS    A                       START OF CURRENT BUFFER
CURR#REC DS    A(IHADCB)               CURRENT RCD PRINTED FOR THIS DCB
PREV#REC DS    A(IHADCB)               PREVIOUS RECORD
OPNLRECL DS    F                       LRECL FROM DEBLRECL/DCBLRECL
OPNBLKSI DS    F                       BLKSIZE FROM DEBBLKSI/DCBBLKSI
REGS     DS    16F
IEFEB4UV DS    V(IEFEB4UV)             IEFEB4UV
         AIF   (NOT &MVSESA).NOSWA6                             GP09255
SWAREQL1 SWAREQ MF=L
.NOSWA6  ANOP  ,                                                GP09255
ENQLIST  ENQ   (ENQNAMES,ENQNAMES,E,,STEP),MF=L
ENQLISTL EQU   *-ENQLIST
SAVPLHPT DS    A(IDAPLHDR,IDAPLH)      SAVE AREA
$DSAB    DS    A(DSAB)                 SET BY VALDCB
$JFCB    DS    A(INFMJFCB)             SET BY VALDCB
$SIOT    DS    A(INDMSIOT)             SET BY VALDCB
$TIOT    DS    A(TIOENTRY)             SET BY VALDCB
$DCB     DS    A(IHADCB)
$ACB     DS    A(IFGACB)
$AMBL    DS    A(AMBL)
$AMB#D   DS    A(AMB)
$AMB#I   DS    A(AMB)
$AMDSB#D DS    A(AMDSB)
$AMDSB#I DS    A(AMDSB)
$PLHDR   DS    A(IDAPLHDR)
$RPL     DS    A(IFGRPL)
$RTM2WA  DS    A(RTM2WA)               SET BY NEXTRB
#RETRY   DS    H,H                     OFFSET TO RETRY ADDRESS
RLINES   DS    FL1'60'                 LINE COUNT
MINLINES DS    FL1'01'                 MINIMUM NUMBER OF LINES
STRNO    DS    FL1                     STRING NUMBER (PLH/RPL)
DDNAME   DS    C'SYSPRINT'             SAVED DDNAME
DDNAME2  DS    C'SYSPRINT+002'         SAVED DDNAME
ABCODE   DS    C'ABEND=S013-18'        ABEND CODE
XDSORG   DS    C'PS'                   DATA SET ORG
XMACRF   DS    C'(GMC,PLC)'            DCBMACRF
XRECFM   DS    C'UTBSAM'               RECORD FORMAT
REASON   DS    C'00'                   REASON CODE
OCE#CODE DS    C                       O/C/E CODE
ORIGIN2  DS    C'  (HSA)'              ORIGIN OF PSW2
EPNAME   DS    CL8                     EP NAME
OFFSET   DS    C'+X''123456'''         OFFSET FROM LOAD POINT
VIACALL  DS    C'SYNCH'                CALL/LINK/SYNCH
MSG20    DS    CL20
MSG60    DS    CL60
VOLSER   DS    CL6
STATUS   DS    C'OLD'
UCB#NAME DS    C'1234'                 4-CHARACTER DEVICE NUMBER (ESA4)
EYECATCH DS    CL120
TRTPRINT DS    XL256
BLANKS   DS    CL121                   LOTS OF BLANKS
ZEROES   DS    XL64'0'       USE CLC ZEROES INSTEAD OF OC X,X   GP03119
UNITNAME DS    CL8,XL4,XL4             IEFEB4UV
TITLE121 DS    CL121                   TITLE LINE
X00      EQU   16,1                    X'00'
STACK128 DS    128A
SAVER012 DS    16F                     SAVE AREA FOR R0=12 ENTRY
         SPACE 1                                                GP05060
LOOKFOUR CAMLST SEARCH,1,2,3   DSCB 4 OBTAIN LIST               GP05060
LOOKCCHH CAMLST SEEK,1,2,3   DSCB 1/2/3 OBTAIN BY CCHHR         GP05060
SUBSAVE1 DC    16A(0)        SUBROUTINE SAVE AREA               GP05060
SZDEBEXT DC    H'0'          SIZE OF DEB EXTENT (=16)           GP05060
HIGHMARK DC    XL5'0'        LAST LOGICAL OR PHYSICAL DSCB 1 IN VTOC
HIGHTRK  DC    XL2'0'        HIGH HH IN CYL                     GP05060
HIGHR    EQU   HIGHMARK+4,1,C'X'  R NUMBER                      GP05060
CCHHR    DC    XL5'0'        VTOC CURSOR                        GP05060
         SPACE 1                                                GP05060
LOCSER   EQU   EYECATCH+0,6,C'C'     VOLSER                     GP05060
LOCDSN   EQU   EYECATCH+7,44,C'C'    DSN                        GP05060
         IECSDSL1 1                                             GP09260
         IECSDSL1 4                                             GP09260
D31SDWAP DS    16F           FAKE SDWAXPAD TARGET SDWAPTRS      GP03014
D31SDWC1 DS    XL456         OS/390 SIZE                        GP09255
*31SDWC1 DS    ((SDWASEND-SDWARC1)/4)A FAKE SDWARC1 EXTENSION   GP03014
         DS    0D
D31SDWA  DS    XL1000                  COPY OF RTM2'S SDWA
DYN31FAR DS    0C                                               GP09255
         DS    (DYNAM31+4096-*)C       MAKE IT A FULL PAGE
DYNAM31L EQU   *-DYNAM31
*
*        24-BIT WORKING STORAGE        <-- R12
*
DYNAM24  DSECT ,
         DS    18F                     SAVE AREA
OPENLIST OPEN  (DCBDEBUG,OUTPUT),MF=L
DCBDEBUG DCB   DSORG=PS,MACRF=PM,DDNAME=SYSDEBUG
PRINT24  DS    XL(PRINT24L)            CODE COPIED BY INIT RTNE
LINE121  DS    CL121                   PRINT LINE (CONTROL CHARACTER)
LINE     EQU   LINE121+1,L'LINE121-1   PRINT LINE
         DS    0D                      ALIGN PLACE-HOLDER FOR FREEMAIN
DYNAM24H DS    CL(DYNAM24+16384-*)     PLACE HOLDER
DYNAM24L EQU   *-DYNAM24
         STRING GENERATE
***********************************************************************
         PRINT NOGEN
*IFO012: PRINT    OPSYN ANOP
         IHAPSA DSECT=YES              PREFIXED STORAGE AREA
         CVT   PREFIX=YES,DSECT=YES,LIST=NO
CVTPRODN EQU   CVTNUMB,4,C'C'                                   GP09255
         IHAGDA ,                                               GP09255
*@@@     IHALLT DSECT=YES              LNKLST/LPALST TABLES
LLT      DSECT ,
LLTLLT   DS    C'LLT '                 BLOCK ACRONYM
LLTNO    DS    F                       NUMBER OF ENTRIES
LLTENTRY DS    0CL45                   DSNAME ENTRY
LLTDSNL  DS    FL1                     DSNAME LENGTH AFTER TRUNCATION
LLTDSNAM DS    CL44                    DATA SET NAME
LLTNEXT  EQU   *                       NEXT ENTRY
         IHAASCB DSECT=YES             ADDRESS SPACE CONTROL BLOCK
         IHAASXB DSECT=YES             ADDRESS SPACE EXTENSION BLOCK
         IHAACEE                       ACCESSOR ENVIRONMENT ELEMENT
         IHALDA                        LOCAL DATA AREA
         IHASPQE ,                     SUBPOOL QUEUE ELEMENT    GP09260
         IHADQE ,                      DESCRIPTOR QUEUE ELEMENT GP09260
CSCB     DSECT ,
         IEECHAIN                      COMMAND SCHEDULER CONTROL BLOCK
         IKJTCB DSECT=YES              TASK CONTROL BLOCK
         AIF   (NOT &MVSESA).NOSTCB                             GP09255
         IHASTCB                       SECONDARY TCB
.NOSTCB  AIF   (NOT &MVSESA).CDEX1                              GP09255
         AIF   (D'STCBCDXH).CDEX1
STCBCDXH EQU   STCB+X'DC'              CDE EXTENSION HASH TABLE (ESA43)
.CDEX1   ANOP  ,
         IKJRB  DSECT=YES              REQUEST BLOCK
         AIF   (&MVSXA).HVWLIC                                  GP09255
RBWLIC   EQU   RBRTICIL,4,C'X'                                  GP09255
.HVWLIC  AIF   (NOT &MVSESA).NOXSB                              GP09255
         IHAXSB ,                      RB EXTENSION             GP03020
.NOXSB   IHACDE                        CONTENTS DIRECTORY ENTRY
         AIF   (NOT &MVSESA).CDEX2                              GP09255
         AIF   (D'CDCDEX).CDEX2
CDCDEX   EQU   X'10'                   CDE EXTENSION EXISTS (CDATTRB)
.CDEX2   AIF   (&MVSXA).CDEX3     AT LEAST XA ?                 GP09255
CDEOM    EQU   X'80'                                            GP09255
CDRACDTY EQU   X'20'                                            GP09255
CDCDEX   EQU   X'10'                                            GP09255
CDELPDE  EQU   X'08'                                            GP09255
CDGLOBAL EQU   X'04'                                            GP09255
CDRACF   EQU   X'01'                                            GP09255
CDSP     EQU   CDRESV1,1,C'X'                                   GP09255
.CDEX3   ANOP  ,
CDXH     DSECT ,                       CDE EXTENSION HASH TABLE
         DS    C'CDXHT 01'             CB ID
CDXHCNT  DS    F'103'                  NUMBER OF ENTRIES
CDXH$0C  DS    X'00000010'             WHATEVER
CDXHFRST DS    A(CDX1)                 HEAD OF CHAIN
CDXHLAST DS    A(CDX1)                 LAST ENTRY IN CHAIN
*
CDX1     DSECT ,                       CDE EXTENSION
CDX1NEXT DS    A(CDX1)                 FWD PTR
CDX1PREV DS    A(CDXHFRST)             BWD PTR
         DS    C'CDX1'                 CB ID
CDX1$0C  DS    A(CDX1NAME)             ADDR OF ENTRY POINT NAME
CDX1$10  DS    X'00080022'             10
CDX1$14  DS    X'80000000'             14
CDXCDE@  DS    A(CDENTRY)              BWD PTR
CDX1$1C  DS    X'00000000'             1C
CDX1$20  DS    X'00000002'             20
CDX1FLG4 DS    X'00020000'             24
CDX1PDSE EQU   X'80'                   LOADED FROM PDSE
CDX1TTRN DS    X'00010100'             TTRN
CDXDDNAM DS    C'STEPLIB '             DDNAME
CDX1SEQ  DS    X'00001C31'             SEQUENCE NUMBER
CDX1NAME DS    C'@BANDAID'             ENTRY NAME
CDX1#LEN EQU   *-CDX1
         IHALLE                        LOAD-LIST ELEMENT
         IHAXTLST                      EXTENT LIST
         IEFTCT                        SMF TCT
         IEFTIOT1                      TASK INPUT-OUTPUT TABLE
******** IEFZB4D5
DSABQDB  DSECT ,                       DSAB QUEUE DESCRIPTOR BLOCK
DSQDBID  DS    C'DSAB'                 ACRONYM
DSQATTRS DS    XL2                     ATTRIBUTES
DSQQDBLN DS    H'32'                   QDB LENGTH
DSQNELMS DS    F                       NUMBER OF ELEMENTS ON QUEUE
DSQFRSTP DS    A(DSAB)                 FIRST DSAB
DSQLASTP DS    A(DSAB)                 LAST DSAB
         IHADSAB                       DATA SET ASSOCIATION BLOCK
SIOT     DSECT ,
         IEFASIOT                      STEP I/O TABLE
JFCB     DSECT ,
         IEFJFCBN                      JOB FILE CONTROL BLOCK
         DCBD  DSORG=PS,DEVD=DA        IHADCB
         AIF   (&MVSXA).NDCBLN                                  GP09255
DCBICQE  EQU   IHADCB+28,4,C'A'                                 GP09255
DCBLNGQS EQU   *-IHADCB                DCB LENGTH               GP09255
DCBLNGPO EQU   *-IHADCB                DCB LENGTH               GP09255
DCBLNGXE EQU   *-IHADCB                DCB LENGTH               GP09255
.NDCBLN  AIF   (NOT &MVSESA).SMS33                              GP09255
         AIF   (D'DCBDCBE).SMS33
DCBDCBE  EQU   DCBRELAD                DCBE ADDRESS
.SMS33   ANOP  ,
         IEZDEB                        DATA EXTENT BLOCK
DEBAMNON EQU   0         ACCESS METHOD TYPE NOT KNOWN           GP09255
DEBAMVSM EQU   1                  VSAM ACCESS METHOD TYPE       GP09255
DEBAMXCP EQU   2                  EXCP ACCESS METHOD TYPE       GP09255
DEBAMTCM EQU   4                  TCAM ACCESS METHOD TYPE       GP09255
DEBAMGAM EQU   8                  GRAPHICS ACCESS METHOD TYPE   GP09255
DEBAMTAM EQU   16                 BTAM ACCESS METHOD TYPE       GP09255
DEBAMBPM EQU   32                 BPAM ACCESS METHOD TYPE       GP09255
DEBAMSAM EQU   32                 SEQUENTIAL ACCESS METHOD TYPE GP09255
DEBAMBDM EQU   64                 DIRECT ACCESS METHOD TYPE     GP09255
DEBAMISM EQU   128                ISAM ACCESS METHOD TYPE       GP09255
DEBAMSUB EQU   129                SUBSYSTEM ACCESS METHOD TYPE  GP09255
DEBAMVTM EQU   130                VTAM ACCESS METHOD TYPE       GP09255
DEBAMTAP EQU   132                TCAM APPLICATION ACC METHOD TYPE
         SPACE 1                                                GP09255
DEBINPUT EQU   0                  INPUT                         GP09255
DEBOUTPT EQU   X'0F'              OUTPUT                        GP09255
DEBXTEND EQU   X'0E'              EXTEND                        GP09255
DEBINOUT EQU   X'03'              INOUT                         GP09255
DEBOUTIN EQU   X'07'              OUTIN                         GP09255
DEBOTINX EQU   X'06'              OUTINX                        GP09255
DEBRDBCK EQU   X'01'              RDBACK                        GP09255
DEBUPDAT EQU   X'04'              UPDAT                         GP09255
         IHADECB DSECT=YES             READ/WRITE
         IEZIOB  DSECT=YES             I/O BLOCK
         IEFUCBOB LIST=NO,PREFIX=NO    UNIT CONTROL BLOCK
         IEFJESCT                      JES VECTOR TABLE
         IHASCB  DSECT=YES             STAE CONTROL BLOCK
         IHASDWA DSECT=YES VRAMAP=NO   SDWA DSECT               GP09255
*@@      IHARTM2A
RTM2WA   DSECT ,
RTM2ID   DS    C'RTM2'                 CB ID
RTM2ADDR DS    A(RTM2WA)               ADDRESS
RTM2SPID DS    AL1(255)                SUB-POOL
RTM2LGTH DS    FL3'972'                LENGTH
RTM2CVT  DS    V(CVT)                  CVT ADDR
RTM2TCBC DS    V(TCB)                  TCB ADDR
RTM2VRBC DS    V(RBBASIC)              SVRB ADDR
RTM2ASC  DS    V(ASCB)                 ADDR OF ASCB
RTM2CODE DS    0XL4                    COMPLETION CODE
RTM2CCF  DS    X                       FLAGS
RTM2CC   DS    XL3                     COMPLETION CODE
RTM2EREG DS    16F                     GPRS AT TIME OF ABEND
         DS    7F
RTM2EPSW DS    XL8                     EC PSW AT TIME OF ERROR
RTM2AEC1 DS    XL8                     ADDITIONAL EC MODE INFORMATION
         DS    10F
RTM2FLGS DS   0BL4                     ERROR FLAGS
RTM2ERRA DS    B                       ERROR TYPE CAUSING ENTRY TO RTM2
RTM2PCHK  EQU  X'40'                   PROGRAM CHECK
RTM2ABTM  EQU  X'08'                   ABTERM (SVC 13)
         DS    45F
RTM2PREV DS    A                       PREVIOUS RTM2WA
RTM2ARER EQU   RTM2WA+X'2C4',64,C'A'   ACCESS REGISTERS
RTM2ARE0 EQU   RTM2WA+X'2C4',4,C'A'      ACCESS REGISTER
RTM2ARE1 EQU   RTM2WA+X'2C8',4,C'A'      ACCESS REGISTER
RTM2ARE2 EQU   RTM2WA+X'2CC',4,C'A'      ACCESS REGISTER
RTM2ARE3 EQU   RTM2WA+X'2D0',4,C'A'      ACCESS REGISTER
RTM2ARE4 EQU   RTM2WA+X'2D4',4,C'A'      ACCESS REGISTER
RTM2ARE5 EQU   RTM2WA+X'2D8',4,C'A'      ACCESS REGISTER
RTM2ARE6 EQU   RTM2WA+X'2DC',4,C'A'      ACCESS REGISTER
RTM2ARE7 EQU   RTM2WA+X'2E0',4,C'A'      ACCESS REGISTER
RTM2ARE8 EQU   RTM2WA+X'2E4',4,C'A'      ACCESS REGISTER
RTM2ARE9 EQU   RTM2WA+X'2E8',4,C'A'      ACCESS REGISTER
RTM2AREA EQU   RTM2WA+X'2EC',4,C'A'      ACCESS REGISTER
RTM2AREB EQU   RTM2WA+X'2F0',4,C'A'      ACCESS REGISTER
RTM2AREC EQU   RTM2WA+X'2F4',4,C'A'      ACCESS REGISTER
RTM2ARED EQU   RTM2WA+X'2F8',4,C'A'      ACCESS REGISTER
RTM2AREE EQU   RTM2WA+X'2FC',4,C'A'      ACCESS REGISTER
RTM2AREF EQU   RTM2WA+X'300',4,C'A'      ACCESS REGISTER
RTM2SDW2 EQU   RTM2WA+X'3DC',4,C'A'    RTM2'S SDWA
         IRAOUCB DSECT=YES             OUCB DSECT
         IHALPDE                       LPDE DSECT
         AIF   (&MVSXA).SKLPDEM   SKIP IF AT LEAST XA           GP09255
LPDEATTB EQU   LPDEUSE+2,1,C'X'                                 GP09255
LPDESP   EQU   LPDEUSE+3,1,C'X'                                 GP09255
.SKLPDEM AIF   (NOT &MVSESA).NOTCBRD                            GP09255
         IHARD ,                       REGION DESCRIPTOR
IFGACBVS OPSYN  ANOP
.NOTCBRD IFGACB DSECT=YES              ACB DSECT
         AIF   (&MVSXA).SKACBFG                                 GP09255
ACBBWO   EQU   X'20'         BACKUP WHILE OPEN                  GP09255
ACBNLW   EQU   X'80'         NO EXCL CNTL WAIT                  GP09255
.SKACBFG AIF   (NOT &MVSESA).NORPLVS                            GP09255
IFGRPLVS OPSYN  ANOP
.NORPLVS IFGRPL DSECT=YES              RPL DSECT
         AIF   (NOT &MVSESA).NOSWAMP                            GP09255
         IEFZB505 LOCEPAX=YES          EPA MAPPING FOR SWAREQ
.NOSWAMP ANOP  ,                                                GP09255
JCT      DSECT ,                       <--  JSCBJCT, LCTJCTAD
         DS    F,A,F,C'JCT '           PREFIX
JCTEQREG EQU   *+92,2,C'H'             REGION IN K (MAX VALUE IS 16383)
         IEFAJCTB                      JOB CONTROL TABLE
         AIF   (NOT &MVSESA).JCT2                               GP09255
         AIF   (D'JCTSWAUP).JCT2
JCTSISO  EQU   2                       JCTSTAT2, NEW WITH DFP 2.3
JCTSWAUP EQU   1                       JCTSTAT2, NEW WITH DFP 2.3
.JCT2    ANOP  ,
         AIF   (NOT &MVSESA).JCT3                               GP09255
         AIF   (D'JCTJMRTL).JCT3
JCTJMRTL EQU   INJMJCT+X'8C',3,C'F'    TIME LIMIT (XA,ESA313)
.JCT3    ANOP  ,
         AIF   (NOT &MVSXA).JCTX3                               GP09255
JCTX     DSECT ,                       <--  JCTJCTX
         DS    F,A,F,C'JCTX'           PREFIX
         IEFJCTX                       JCT EXTENSION
         AIF   (D'JCTXJMRD).JCTX3
JCTXSSD  EQU   JCTXIN+X'58',4,C'P'     STEP START DATE  (HBB4430)
JCTXJMRD EQU   JCTXIN+X'5C',4,C'P'     JOB START DATE  (HBB4430)
         AIF   (D'JCTXJTL).JCTX3
JCTXJTL  EQU   JCTXIN+X'3C',4,C'F'     TIME LIMIT (ESA410)
.JCTX3   ANOP  ,
SCT      DSECT ,                       STEP CONTROL TABLE
         DS    XL16                    PREFIX
         IEFASCTB                      SCT
SCTXIN   DSECT ,    NO IEFSCTX IN MVS  STEP CONTROL TABLE EXTENSION
         DS    XL4                     SVA/BLOCK ID 0C
SCTXPARM DS    CL100                   PARM FIELD
         DS    XL4
SCTXSTL  DS    XL4                     TIME LIMIT
SCTXABCC DS    XL4                     ABEND COMLETION CODE
         SPACE 1
         IEZJSCB                       JSCB
         IEFJSSIB                      SUB-SYSTEM IDENTIFICATION BLOCK
*@@      IGGICQE                       INTERRUPT CONTROL QUEUE ELEMENT
ICQE     DSECT ,                       INTERRUPT CONTROL QUEUE ELEMENT
ICQECB   DS    F                       ECB POINTED TO BY IOB IN SAMB
ICQIOBAD DS    A(IOB)                  ADDRESS OF IOB IN SAMB
ICQFIRST DS    A(IOB)                  FIRST USER IOB
ICQFLG   DS    X                       FLAG BYTE
ICQEXND  EQU   X'80'                   EXCPVR PROCESSING NEEDED
ICQENDAD DS    AL3(IOB)                LAST USER IOB
ICQFSTQ  DS    A(IOB)                  FIRST USER IOB IN QUEUE
ICQMAXQ  DS    FL1                     MAX NUMBER OF IOBS ON QUEUE
ICQNOQ   DS    FL1                     CURRENT NUMBER OF IOBS ON QUEUE
ICQSAVQ  DS    X,X                     SAVE AREA FOR ICQMAXQ (EOV)
ICQSAVCT DS    C'MBBCCHHR'             SAVE AREA FOR MBBCCHHR
ICQSAV   DS    18F                     SAVE AREA FOR EOB PROCESSING
*@@      VSAM MACROS
AMBL     DSECT ,
         DS    2F
AMBLACB  DS    V(IFGACB)               ADDRESS OF THE ACB
         DS    1F
AMBLDDNM DS    0CL8                    THE ACB'S DDNAME
AMBLCACB DS    A,XL3                   CATALOG ACB ADDR, CI NO
AMBLQ    DS    X                       QUALIFIER
AMBLDDC  EQU   X'80'                   DD CONNECT ONLY
AMBLGSR  EQU   X'40'                   GLOBAL SHARED RESOURCES
AMBLLSR  EQU   X'20'                   LOCAL SHARED RESOURCES
AMBLFSTP EQU   X'10'                   FAST PATH
AMBLUBF  EQU   X'08'                   USER BUFFERING
AMBLKSDS EQU   X'04'                   KEY-SEQUENCED DATA SET
AMBLESDS EQU   X'02'                   ENTRY-SEQUENCED DATA SET
AMBLDFR  EQU   X'01'                   DEFERRED WRITES
         DS    F,H                     XPT,LVC
AMBLTYPE DS    X                       TYPE OF CB ATRUCTURE OPENED
AMBLPATH EQU   X'80'                   PATH
AMBLUPGR EQU   X'40'                   UPGRADE SET
AMBLAIX  EQU   X'20'                   ALTERNATE INDEX
AMBLBASE EQU   X'10'                   BASE CLUSTER
AMBLQ2   DS    X                       QUALIFIED EXTENSION
AMBLSHR  EQU   X'20'                   SHR(3) OR SHR(4)
AMBLCRE8 EQU   X'10'                   CREATE MODE
AMBLID   DS    X'50'                   ID
AMBLSHAR DS    X                       SHARING INDICATORS
AMBLPRIM EQU   X'80'                   PRIMARY AMBL
AMBLWRIT EQU   X'20'                   OUTPUT OR UPDATE MODE
AMBLLEN  DS    FL1'80'                 LENGTH OF THE AMBL
AMBLFLG1 DS    X,X                     FLG1, FLG2
AMBLDUMY EQU   X'01'                   DD DUMMY
AMBLNST  DS    FL1                     STRNO
         DS    H,A,A,F                 LNUM, MMIB, DSAB, SMFT
AMBLDTA  DS    A(AMB)                  DATA'S AMB
AMBLIX   DS    A(AMB)                  INDEX'S AMB
AMBLBIB  DS    V(BIB)                  BASE INFORMATION BLOCK
AMBLCMB  DS    V(CMB)                  CLUSTER MANAGEMENT BLOCK
*
AMB      DSECT ,
AMBID    DS    X'40',X,H,A             ID, TS, LEN, LINK
AMBBUFC  DS    A(BUFC)                 ADDRESS OF BUFC HEADER
AMBPH    DS    A(IDAPLHDR)             ADDRESS OF THE PLH HEADER
AMBCACB  DS    A(IFGACB)               ADDRESS OF THE ACB
AMBDSB   DS    A(AMDSB)                DATA SET STATISTICS BLOCK
AMBFLG0  DS    X                       FLAGS
AMBLDS   EQU   X'04'                   LINEAR DATA SET
AMBFLG1  DS    X                       FLAGS
AMBCREAT EQU   X'80'                   CREATE MODE
AMBSPEED EQU   X'08'                   SPEED OPTION
AMBDSORG DS    X'0008'                 DSORG
         DS    A,XL3,AL3,H             IOB/IOMB, *, DDSN, *
AMBTIOT  DS    AL2,X,X,V(TCB)          TIOT, INFL, AMETH, TCB
AMBOFLGS DS    X,X,XL2                 OFLGS, FLG2
         DS    XL(136-52)
AMBDSNM  DS    CL44                    DATA SET NAME
*
BUFC     DSECT ,                       BUFC HEADER
BUFCID   DS    X'70'                   ID
BUFCBUFN DS    FL1                     BUFNI/BUFND
BUFCLEN  DS    H'88'                   LENGTH OF BUFC ENTRY
         DS    A,A,A,A                 MORE STUFF
*
AMDSB    DSECT ,
AMDSBID  DS    X'60'                   ID
AMDATTR  DS    X                       ATTR
AMDRRDS  EQU   X'02'                   RRDS
AMDLEN   DS    H,H                     LEN, NEST
AMDRKP   DS    H                       KEY OFFSET
AMDKEYLN DS    H                       KEY LENGTH
*
IDAPLHDR DSECT ,
PLHID    DS    X'30',X,H               PLH HEADER
PLHCNT   DS    H                       COUNT OF PLH ENTRIES BEHIND HDR
PLHDRREQ DS    H                       COUNT OF DEFERRED REQUESTS
PLHDRMAX DS    H                       MAX NO OF PLH ENTRIES IN USE
PLHDRCUR DS    H                       NO OF ACTIVE ENTRIES
PLHIOSDQ DS    A                       DEFERRAL QUEUE HEADER
*
IDAPLH   DS    X'FF'               +16
PLHIDENT DS    C'PLH'
         DS    X,X,X,X,X,X,XL2         FLG0,1,2,3,4,5,EFLGS
         DS    X,X,X,FL1               RCODE, RMIN, FRCNT, BFRNO
PLHMRPL  DS    V(IFGRPL),0A            RPL HEADER, JRNAD
PLHCRPL  DS    V(IFGRPL)               CURRENT RPL
         DS    A,A,A,A,X,X,X,X,A
PLHLRECL DS    F,A,A                   LENGTH OF RECORD
PLHRECP  DS    A                       ADDRESS OF CURRENT RECORD
         DS    A,A,H,H,A,A,6F
PLHACB   DS    V(IFGACB),X,3X          ADDRESS OF THE ACB
PLHCHAIN DS    A(IDAPLH)               ADDRESS OF THE NEXT PLH
MAPEXTNT DSECT ,             MAPPING OF DSCB1/DSCB3 EXTENT ENTRY
XTWHOLE  DC    0XL10'0'      DUMMY FOR CLC/XC                   GP09260
XTTYPE   DC    X'0'          EXTENT TYPE                        GP09260
XTTTRK   EQU   X'01'           TRACK ALIGNMENT                  GP09260
XTTLABEL EQU   X'40'           LABEL EXTENT                     GP09260
XTTCYL   EQU   X'81'           CYLINDER ALIGNMENT               GP09260
XTTSPLIT EQU   X'80'           SPLIT CYLINDER ALLOCATION        GP09260
XTSEQ    DC    X'0'          EXTENT SEQUENCE (0-15)             GP09260
XTLOCYL  DC    H'0'          LOW CYLINDER                       GP09260
XTLOTRK  DC    H'0'          LOW TRACK                          GP09260
XTHICYL  DC    H'0'          HIGH CYLINDER                      GP09260
XTHITRK  DC    H'0'          HIGH TRACK                         GP09260
XTLEN    EQU   *-MAPEXTNT    LENGTH OF ONE ENTRY                GP09260
*        IHADCBE              DFSMS/MVS DCB EXTENSION
DCBE     DSECT ,
DCBEID   DS    CL4            DCBE EYECATCHER 'DCBE'.
DCBELEN  DS    H              LENGTH OF DCBE.
DCBERSV1 DS    XL2            RESERVED.
DCBEDCB  DS    A              DCB ADDRESS. SET BY SYSTEM.
DCBERELA DS    CL4            PARTITIONED DATA SET - TTRN
DCBEFLG1 DS    B              FLAGS SET BY SYSTEM.
DCBEOPEN EQU   X'80'          DCBE HAS BEEN SUCCESSFULLY OPENED.
DCBEMD31 EQU   X'40'          USER MAY CALL ACCESS METHOD IN AMODE31.
DCBEFLG2 DS    B              FLAGS SET BY USER.
DCBEBU31 EQU   X'80'          RMODE31=BUFF.
DCBENEOD EQU   X'40'          PASTEOD=YES.
DCBENVER EQU   X'10'          NOVER=YES.
DCBEGSIZ EQU   X'08'          GETSIZE=YES.
DCBENSTR DS    H              THE NUMBER OF STRIPES
DCBERSV2 DS    XL12           RESERVED.
DCBERSV3 DS    XL4            RESERVED FOR FUTURE EXPANSION OF DCBESIZE
DCBESIZE DS    F              NUMBER OF BLOCKS IN CURRENT DATA SET.
DCBEEODA DS    A              ADDRESS OF USER PROVIDED END-OF-DATA
DCBESYNA DS    A              ADDRESS OF USER PROVIDED SYNAD ROUTINE.
DCBERSV4 DS    XL6            RESERVED.
DCBEMACC DS    XL1            ACCUMULATION NUMBER MULTIPLIER.
DCBEMSDN DS    XL1            MULTIPLIER OF SYSTEM DETERMINED NCP.
DCBEMINL EQU   *-DCBE         MINIMAL LENGTH OF DCBE.
DCBEEND  EQU   *              END OF DCBE.
         YREGS ,
@BANDAID CSECT ,             GET SYSECT CORRECT                 GP09255
        $LRC   ,                       HASP LOGICAL RECORD
         AIF   (&MVSESA).NOLRC                                  GP09255
LRC      EQU   LRCDSECT      DEFINE NEW NAME                    GP09255
.NOLRC   SPACE 1                                                GP09255
SDB      DSECT ,                       JES2
SDBID    EQU   SDB+X'0048',4           "SDB "
UBF      DSECT ,                       JES2
UBFID    EQU   UBF,4                   "UBF "
         END   ,
