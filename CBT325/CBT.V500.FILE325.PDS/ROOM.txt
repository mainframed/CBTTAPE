         TITLE 'WELLS FARGO BANK ROOM COMMAND'
ROOM     CSECT
         SPACE 2
*** MODULE NAME *******************************************************
*                                                                     *
*   R O O M  -  WELLS FARGO BANK ROOM COMMAND FOR TSO FOREGROUND      *
*               EXECUTION                                             *
*                                                                     *
*                        (WRITTEN BY VIC COZZOLI, FEBRUARY 1983)      *
*                                                                     *
*** FUNCTION **********************************************************
*                                                                     *
*   THIS COMMAND EITHER LISTS OR MODIFIES THE ROOM NUMBER FIELD IN    *
*   THE JES2 JCT (JCTROOMN) FOR THE ISSUERS LOGON TASK.  THE ROOM     *
*   NUMBER FIELD IS LISTED IF NO OPERAND IS ENTERED WITH THE COMMAND; *
*   AND IT IS UPDATED WITH THE OPERAND ENTERED WHEN THE OPERAND IS    *
*   PRESENT.                                                          *
*                                                                     *
*** PURPOSE ***********************************************************
*                                                                     *
*   THIS COMMAND IS NECESSARY IN ORDER TO ALLOW TSO USERS THE         *
*   ABILITY TO MAINTAIN THE ROOM NUMBER FIELD DURING THEIR LOGON      *
*   SESSION.  THE ROOM NUMBER FIELD IS OVERLAYED BY WELLS FARGO       *
*   ACCOUNTING INFORMATION AT LOGON TIME. THEREFORE, IT IS NORMALLY   *
*   UNUSABLE FOR SYSOUT DELIVERY INFORMATION WHEN PRESENT ON THE      *
*   SYSOUT HEADER AND TRAILER PAGES.  THIS COMMAND ALLOWS THE TSO     *
*   USER THE ABILITY TO RESTORE THE ROOM NUMBER SO THAT IT CAN BE     *
*   USED FOR SYSOUT DELIVERY INFORMATION WHEN PRESENT ON THE SYSOUT   *
*   HEADER AND TRAILER PAGES.                                         *
*                                                                     *
*** OPERATION *********************************************************
*                                                                     *
*   HOUSEKEEPING:                                                     *
*      - SAVE CALLER'S REGISTERS                                      *
*      - GETMAIN DYNAMIC WORKAREA                                     *
*      - CHAIN SAVEAREAS                                              *
*      - INITIALIZE GENERAL FAIL PARAMETER LIST TO BINARY ZEROS       *
*      - DISABLE ATTENTION INTERRUPTS                                 *
*                                                                     *
*   MAIN PROCESS:                                                     *
*      - BUILD PARS AND PUTLINE PARAMETER LISTS                       *
*      - CALL IKJPARS (VIA CALLTSSR) TO PARSE THE COMMAND             *
*      - GET THE JES2 JCT ADDRESS                                     *
*      - IF THE COMMAND WAS ENTERED WITHOUT OPERAND,                  *
*            THEN LIST THE CURRENT ROOM NUMBER                        *
*      - IF THE COMMAND WAS ENTERED WITH AN OPERAND,                  *
*            THEN UPDATE THE ROOM NUMBER WITH THE ONE SPECIFIED       *
*                 IN THE COMMAND OPERAND FIELD                        *
*                                                                     *
*   RETURN:                                                           *
*      - FREE DYANMIC WORKAREA                                        *
*      - ENABLE ATTENTION INTERRUPTS                                  *
*      - RESTORE CALLER'S REGISTERS                                   *
*      - RETURN TO CALLER                                             *
*                                                                     *
         EJECT
*** NOTES *************************************************************
*                                                                     *
*   REGISTER CONVENTIONS:                                             *
*       REGISTER 0 TO 3   - WORK REGISTERS                            *
*       REGISTER 4        - PTR TO PARSE PARAMETER LIST (PPL)         *
*       REGISTER 5        - PTR TO I/O SERVICE ROUTINE PARAMETER LIST *
*                           (IOPL)                                    *
*       REGISTER 6        - PTR TO PARAMETER DESCRIPTOR LIST (PDL)    *
*       REGISTER 7        - PTR TO JES2 JCT                           *
*       REGISTER 8        - PTR TO COMMAND PROCESSOR PARAMETER LIST   *
*                           (CPPL)                                    *
*       REGISTER 9        - CONTAINS RETURN POINT FROM SUBROUTINE CALL*
*       REGISTER 10 TO 11 - NOT USED                                  *
*       REGISTER 12       - BASE REGISTER                             *
*       REGISTER 13 TO 15 - WORK REGISTERS                            *
*                                                                     *
*** TSO COMMAND SYNTAX ************************************************
*                                                                     *
*   ROOM <OPERAND>                                                    *
*                                                                     *
*** ENVIRONMENT *******************************************************
*                                                                     *
*   MODULE TYPE: PROCEDURE                                            *
*   PROCESSOR:   ASM                                                  *
*   MODULE SIZE: .3K                                                  *
*   ATTRIBUTES:  REENTRANT, PROBLEM PROGRAM STATE, AND                *
*                SCHEDULER KEY 8 (EXCEPT DURING UPDATE OF JES2 JCT    *
*                WHEN SCHEDULER KEY 0)                                *
*                                                                     *
*   ENTRY POINT: ROOM (ONLY ENTRY POINT)                              *
*   LINKAGE:     FROM TERMINAL MONITOR PROGRAM (TMP) AS A COMMAND     *
*                                                                     *
*** INPUT *************************************************************
*                                                                     *
*   REGISTER 1 POINTS TO THE COMMAND PROCESSOR PARAMETER LIST (CPPL)  *
*   MAPPED BY IKJCPPL MACRO                                           *
*                                                                     *
*** OUTPUT ************************************************************
*                                                                     *
*   NONE                                                              *
*                                                                     *
*** EXIT **************************************************************
*                                                                     *
*   NORMAL:                                                           *
*      - AT PROGRAM END VIA BRANCH REGISTER 14                        *
*      - RETURN CODE IN REGISTER 15 IS ZERO (0)                       *
*                                                                     *
*   ERROR:                                                            *
*      - PROGRAM WILL DISPLAY ERROR MESSAGE AND DECIMAL RETURN CODE   *
*      - AND RETURN VIA BRANCH REGISTER 14                            *
*      - RETURN CODE IN REGISTER 15 IS TWELVE (12)                    *
*                                                                     *
         EJECT
*** EXTERNAL REFERENCES ***********************************************
*                                                                     *
*   ROUTINES:       IKJPARS, GNRLFAIL (IKJEFF19), PUTLINE (IKJPUTL)   *
*   DATA AREAS:     NONE                                              *
*   CONTROL BLOCKS: CPPL, PPL, GFPARMS, IOPL, IOPB,                   *
*                   CVT, TCB, JSCB, SSIB, $SJB, $JCT                  *
*   MACROS:         SAVE, GETMAIN, FREEMAIN, STAX, CALLTSSR, MODESET, *
*                   LINK, TPUT, IKJPARM, IKJIDENT, IKJENDP, IKJCPPL,  *
*                   IKJPPL, IKJIOPL, PUTLINE                          *
*                                                                     *
*** CHANGE ACTIVITY ***************************************************
*                                                                     *
*                                                                     *
*     DATE  |     BY     | REASON                                     *
*   --------|------------|--------------------------------------------*
*   05/29/84|VIC COZZOLI | REMOVE HARD CODED DISPLACEMENTS           @1
*                                                                     *
*** MESSAGES **********************************************************
*                                                                     *
*   THE GNRLFAIL SERVICE ROUTINE IS USED TO ISSUE SOME MESSAGES.      *
*   THOSE MESSAGES ISSUED BY THE COMMAND ARE FOUND FOLLOWING THE      *
*   LABEL GNRLERR AND ARE ISSUED BY TPUT AND PUTLINE RESPECTIVELY.    *
*                                                                     *
*** ABEND CODES *******************************************************
*                                                                     *
*   NONE                                                              *
*                                                                     *
***********************************************************************
         EJECT
*** HOUSEKEEPING ******************************************************
*                                                                     *
*   -SAVE CALLER'S REGISTERS                                          *
*   -GETMAIN DYNAMIC WORKAREA                                         *
*   -CHAIN SAVEAREAS                                                  *
*   -INITIALIZE GENERAL FAIL PARAMETER LIST TO BINARY ZEROS           *
*   -DISABLE ATTENTION INTERRUPTS                                     *
*                                                                     *
***********************************************************************
*
HSEKPING DS    0H
         SAVE  (14,12),,ROOM.&SYSTIME_&SYSDATE SAVE REGISTERS
         LR    R12,R15                 LOAD R12 WITH ENTRY POINT
         USING ROOM,R12                ESTABLISH ADDRESSABILITY
         LR    R8,R1                   SAVE PTR TO CPPL
         SPACE
         GETMAIN R,LV=LDYNAMIC         GETMAIN DYNAMIC WORKAREA
         SPACE
         LR    R10,R1                  SAVE PTR TO GETMAINED AREA
         ST    R13,4(R10)              SAVE PTR TO PREVIOUS SAVEAREA
         ST    R10,8(,R13)             SAVE CURRENT SAVEARE IN PREVIOUS
         LR    R13,R10                 LOAD R13 W/ PTR TO CUR. SAVEAREA
         USING WORKAREA,R13            ESTABLISH ADDRESSABILITY
         SPACE
         XC    GFPARMS(GFLENGF),GFPARMS INITIALIZE GNRLFAIL PARMLIST
         SPACE
         STAX  DEFER=YES               DISABLE ATTENTION INTERRUPTS
         EJECT
*** MAIN PROCESS ******************************************************
*                                                                     *
*   - BUILD PARS AND PUTLINE PARAMETER LISTS                          *
*   - CALL IKJPARS (VIA CALLTSSR) TO PARSE THE COMMAND                *
*   - GET THE JES2 JCT ADDRESS                                        *
*   - IF THE COMMAND WAS ENTERED WITHOUT OPERAND,                     *
*         THEN LIST THE CURRENT ROOM NUMBER                           *
*   - IF THE COMMAND WAS ENTERED WITH AN OPERAND,                     *
*         THEN UPDATE THE ROOM NUMBER WITH THE ONE SPECIFIED          *
*              IN THE COMMAND OPERAND FIELD                           *
*                                                                     *
***********************************************************************
*
MAINPROC DS    0H
         BAL   R9,PROCESS              BRANCH AND LINK TO MAIN PROCESS
*                                      ROUTINES
         SPACE 2
*** RETURN ************************************************************
*                                                                     *
*   - FREE DYANMIC WORKAREA                                           *
*   - ENABLE ATTENTION INTERRUPTS                                     *
*   - RESTORE CALLER'S REGISTERS                                      *
*   - RETURN TO CALLER                                                *
*                                                                     *
***********************************************************************
*
RETURN   DS    0H
         LR    R1,R13                  LOAD ADDRESS OF GETMAINED AREA
         LA    R0,LDYNAMIC             LOAD LENGTH OF GETMAINED AREA
         SPACE
         FREEMAIN R,LV=(0),A=(1)       FREE GETMAINED AREA
         SPACE
         STAX  DEFER=NO                ALLOW ATTENTION INTERRUPTS AGAIN
         SPACE
         L     R13,4(,R13)             RELOAD ADDR OF PREVIOUS SAVEAREA
         L     R14,12(,R13)            LOAD R14 WITH RETURN ADDRESS
         LM    R0,R12,20(R13)          RESTORE REGISTERS
         BR    R14                     RETURN TO CALLER
         EJECT
*** PROCESS - (INVOKED AT LABEL MAINPROC) *****************************
*                                                                     *
*   -BUILD PARSE PARAMETER LIST USING COMMAND PROCESSOR PARAMETER     *
*        LIST (CPPL)                                                  *
*   -BUILD PUTLINE PARAMETER LIST USING I/O SERVICE ROUTINE PARAMETER *
*        LIST (IOPL)                                                  *
*                                                                     *
***********************************************************************
*
PROCESS  DS    0H
         USING CPPL,R8                 ESTABLISH ADDRESSABILITY TO CPPL
         LA    R4,PPLSECT              LOAD ADDRESS OF PPL
         USING PPL,R4                  ESTABLISH ADDRESSABILITY TO PPL
         LA    R5,IOPLSECT             LOAD ADDRESS OF IOPL
         USING IOPL,R5                 ESTABLISH ADDRESSABILITY TO IOPL
         L     R1,CPPLUPT              LOAD PTR TO USER PROF FROM CPPL
         L     R2,CPPLECT              LOAD POINTER TO ECT FROM CPPL
         SLR   R3,R3                   ZERO REGISTER 3
         ST    R3,ECB                  STORE REGISTER AS ECB FOR PPL
         LA    R3,ECB                  LOAD ADDRESS OF ECB
         STM   R1,R3,PPLSECT           STORE ADDRESS OF UPT, ECT, AND
*                                      ECB IN PPL
         STM   R1,R3,IOPLSECT          STORE ADDRESS OF UPT, ECT, AND
*                                      ECB IN IOPL
         LA    R3,PTPB                 LOAD ADDR. OF PUTLINE PARM BLOCK
         ST    R3,IOPLIOPB             STORE PTPB ADDRESS IN IOPL
         MVC   PPLPCL,PCLADDR          MOVE PTR TO PCL INTO PPL
         LA    R2,PARSBACK             LOAD ADDR OF FULLWORD TO CONTAIN
*                                      ADDRESS OF PDE RETURNED BY PARS
         ST    R2,PPLANS               STORE POINTER TO RETURN ADDRESS
         MVC   PPLCBUF,CPPLCBUF        MOVE PTR TO CMD BUFFER INTO PPL
         LA    R2,PPLUWA               LOAD ADDRESS OF USER WORK AREA
         ST    R2,PPLUWA               STORE ADDRESS IN PPL
         DROP  R4,R5,R8                DROP ADDRESSABILITY TO CPPL,
*                                      IOPL, AND PPL
         EJECT
*** CALL PARSE ********************************************************
*                                                                     *
*   -CALL IKJPARS (VIA CALLTSSR) TO PARSE THE COMMAND                 *
*        ON RETURN: - PARSBACK CONTAINS POINTER TO PDL                *
*                   - REGISTER 15 CONTAINS PARSE RETURN CODE          *
*                                                                     *
***********************************************************************
*
         CALLTSSR  EP=IKJPARS,MF=(E,PPLSECT) CALL PARS TO PARSE COMMAND
         LTR   R15,R15                 SEE IF RETURN CODE IS ZERO
         BZ    GOODPARS                YES....GO TO GOOD PARS ROUTINE
         C     R15,=F'04'              DID PARS ISSUE ERROR MESSAGE?
         BE    BADPARS                 YES....CLEANUP AND RETURN
         C     R15,=F'20'             DID VALIDITY CHECK RTN ISSUE MSG?
         BE    BADPARS                 YES....CLEANUP AND RETURN
         LA    R1,GFPARSE              LOAD CALLER ID FOR PARS
         STH   R1,GFCALLID             STORE CALLER ID IN PARMLIST
         B     GNRLFAIL                GO TO GENERAL FAIL ROUTINE
BADPARS  L     R15,=F'12'              TELL TMP WE HAD A PROBLEM
         BR    R9                      RETURN TO MAINLINE
         EJECT
*** GOOD COMMAND PARSE ************************************************
*                                                                     *
*   -SET UP ADDRESSABILITY TO JES2 JCT                                *
*                                                                     *
***********************************************************************
*
GOODPARS DS    0H
         L     R7,CVTPTR               LOAD ADDR OF CVT POINTER
         L     R7,CVTTCBP-CVTMAP(R7)   LOAD ADDR OF NEXT/CURR TCB'S @1
         L     R7,4(R7)                LOAD ADDR OF CURRENT TCB @1
         L     R7,TCBJSCB-TCB(R7)      LOAD ADDR OF JSCB @1
         L     R7,JSCBACT-IEZJSCB(R7)  LOAD ADDR OF ACTIVE JSCB @1
         L     R7,JSCBSSIB-IEZJSCB(R7) LOAD ADDR OF LIFE-OF-JOB SSIB @1
         L     R7,SSIBSUSE-SSIB(R7)    LOAD ADDR OF JES2 SJB @1
         L     R7,SJBJCT-SJB(R7)       LOAD ADDR OF JES2 JCT @1
         SPACE 2
*** OPERAND CHECK *****************************************************
*                                                                     *
*   SEE IF OPERAND WAS SPECIFIED WITH COMMAND                         *
*      - IF SO, UPDATE CURRENT ROOM NUMBER WITH THE ONE ENTERED       *
*        IF NOT, JUST LIST THE CURRENT ROOM NUMBER                    *
*                                                                     *
***********************************************************************
*
         L     R6,PARSBACK             LOAD ADDR OF PDL
         USING IKJPARMD,R6             ESTABLISH ADDRESSABILITY
         TM    ROOMNO+6,X'80'          DID PARS FIND AN OPERAND?
         BO    UPDATE                  YES....GO UPDATE ROOM NUMBER
         SPACE 2
*** LIST ROOM NUMBER **************************************************
*                                                                     *
*   -INITIALIZE THE PUTLINE OLD (OUTPUT LINE DESCRIPTOR)              *
*   -IF CURRENT ROOM NUMBER IS VALID (I.E. NOT BINARY ZEROS)          *
*         THEN -LIST THE CURRENT ROOM NUMBER                          *
*         ELSE -TELL THE USER IT ISN'T SPECIFIED                      *
*   -USE PUTLINE TO TELL THE USER                                     *
*        ON RETURN: - REGISTER 15 CONTAINS PUTLINE RETURN CODE        *
*                                                                     *
***********************************************************************
LIST     DS    0H
         L     R2,=F'2'                GET NO. OF PUTLINE MSG SEGMENTS
         ST    R2,MSG1OLD              STORE NUMBER OF SEGS IN O.L.D.
         LA    R2,MSG1SEG1             GET ADDRESS OF PRIMARY SEGEMENT
         ST    R2,MSG1OLDP             STORE IT IN O.L.D. PRIMARY ADDR
         CLI   JCTROOMN-JCT(R7),X'00'  ROOM SPECIFIED BEFORE?   @1
         BE    NOTSMSG                 NO, THEN WRITE 'NOT SPECIFIED'
ROOMNMSG DS    0H
         LA    R2,MSG1SEG2             GET ADDRESS OF 2ND SEGMENT
         ST    R2,MSG1OLDS             STORE IN 2NDARY ADDRESS OF OLD
         LA    R2,MSG1SG2L             LOAD LENGTH OF SECOND SEGMENT
         STH   R2,MSG1SEG2             STORE IN 2ND SEGMENT'S HEADER
         LA    R2,MSG1TX1L             LOAD LENGTH OF PRIMARY TEXT
         STH   R2,MSG1SEG2+2           STORE IN 2ND SEGMENT'S HEADER
         MVC   MSG1ROOM-2(2),=C': '    MOVE PUNCTUATION TO MESSAGE
         MVC   MSG1ROOM(4),JCTROOMN-JCT(R7) MOVE ROOM NO TO MSG @1
         B     PUTLMSG                 GO PUTLINE THE MESSAGE
         SPACE
NOTSMSG  DS    0H
         LA    R2,MSG1SEG3             GET ADDRESS OF 2ND SEGMENT
         ST    R2,MSG1OLDS             STORE IN 2NDARY ADDRESS OF OLD
         SPACE
PUTLMSG  DS    0H
         PUTLINE OUTPUT=(MSG1OLD),MF=(E,IOPLSECT)
         SPACE
         LTR   R15,R15                 SEE IF RETURN CODE IS ZERO
         BZR   R9                      YES...RETURN TO MAINLINE
*                                      NO....GNRLFAIL WILL HANDLE ERROR
         LA    R1,GFPUTL               LOAD CALLER ID FOR PUTLINE
         STH   R1,GFCALLID             STORE CALLER ID IN PARMLIST
         B     GNRLFAIL                GO TO GENERAL FAIL ROUTINE
         SPACE 2
*** UPDATE ROOM NUMBER ************************************************
*                                                                     *
*   -UPDATE THE ROOM NUMBER WITH THE OPERAND SPECIFIED                *
*        (USE MODESET TO OBTAIN SCHEDULER KEY 0 IN ORDER TO           *
*         MODIFY JES2 JCT)                                            *
*                                                                     *
***********************************************************************
UPDATE   DS    0H
         L     R2,ROOMNO               GET ADDRESS OF OPERAND
         MODESET KEY=ZERO              SET KEY FOR ZERO (TO MODIFY JCT)
         MVC   JCTROOMN-JCT(4,R7),0(R2)  MOVE ROOM FROM OPERAND @1
         MODESET KEY=NZERO             RESTORE KEY BACK TO WHAT IT WAS
         SR    R15,R15                 TELL TMP EVERYTHING WENT A-OK.
         BR    R9                      RETURN TO MAINLINE
         EJECT
*** GNRLFAIL **********************************************************
*                                                                     *
*   GENERAL FAIL SERVICE ROUTINE HANDLES ALL ERRORS RETURNED BY       *
*       IKJPARS AND PUTLINE.                                          *
*   BUILD THE GF PARMLIST AND LINK TO THE GENERAL FAIL SERVICE ROUTINE*
*                                                                     *
***********************************************************************
GNRLFAIL DS    0H
         LA    R1,GFPARMS              LOAD ADDRESS OF PARMLIST
         ST    R1,GFPARMP              STORE ADDRESS AS PTR TO PARMLIST
         ST    R15,GFRCODE             STORE RETURN CODE IN PARMLIST
         L     R1,SAVEAREA+4           LOAD ADDR OF PREVIOUS SAVEAREA
         L     R1,24(R1)               LOAD POINTER TO CPPL
         ST    R1,GFCPPLP              STORE PTR TO CPPL IN PARMLIST
         LA    R1,ECB                  LOAD ADDRESS OF OPTIONAL ECB
         ST    R1,GFECBP               STORE PTR TO ECB IN PARMLIST
         SLR   R1,R1                   ZERO REGISTER 1
         ST    R1,ECB                  SET OPTIONAL ECB TO ZERO
         LINK  EP=IKJEFF19,MF=(E,GFPARMP)  LINK TO GNRLFAIL TO PROCESS
*
         LTR   R15,R15                 SEE IF RETURN CODE IS ZERO
         BZR   R9                      YES....RETURN TO MAINLINE
         CVD   R15,WORKDEC             CONVERT RETURN CODE TO DECIMAL
         UNPK  RETURNCD(8),WORKDEC(8)  UNPACK RETURN CODE
         MVZ   RETURNCD+1(7),RETURNCD  MOVE ZONES IN CONVERTED RC
         SPACE
         TPUT  GNRLERR,GNRLERRL        TPUT GNRLFAIL FAILED MESSAGE
         SPACE
         TPUT  RETURNCD,8              TPUT RETURN CODE OF FAILURE
         SPACE
         L     R15,=F'12'              TELL THE TMP WE HAD A PROBLEM
         BR    R9                      RETURN TO MAINLINE
         EJECT
*************************** EQUATES ***********************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
****************** POINTER TO PARS PARAMETER CSECT ********************
PCLADDR  DC    A(PARMTAB)
         SPACE 2
************************ PARS PARAMETER LIST **************************
PARMTAB  IKJPARM  DSECT=IKJPARMD
         SPACE
ROOMNO   IKJIDENT 'ROOM NUMBER',MAXLNTH=4,                             X
               FIRST=ALPHANUM,OTHER=ALPHANUM
         SPACE
         IKJENDP
         EJECT
*************************  TPUT MESSAGE  ******************************
GNRLERR  DC    C'AN ERROR WAS ENCOUNTERED IN THE GENERAL FAIL SERVICE RX
               OUTINE.  THE RETURN CODE FROM GNRLFAIL IS: '
*        THE ACTUAL RETURN CODE FOR THIS MESSAGE IS STORED IN THE
*        GETMAINED WORKAREA AS SYMBOL 'RETURNCD' IN ORDER TO PRESERVE
*        PROGRAM REENTRANCY AND REUSABILITY.
GNRLERRL EQU   *-GNRLERR
         SPACE 2
***********************  PUTLINE MESSAGE  *****************************
*   THE PUTLINE MESSAGE DISPLAYED COMES IN TWO FORMATS; ONE WITH THE
*   ACTUAL ROOM NUMBER LISTED AND ONE THAT SAYS THE ROOM NUMBER IS
*   NOT SPECIFIED.  THE PUTLINE TEXT INSERTION FUNCTION IS USED TO
*   CREATE THE PROPER MESSAGE.
*   THE CONSTANT MESSAGE SEGMENTS ARE LISTED FOLLOWING THIS COMMENT.
*   THE VARIABLE MESSAGE SEGEMENT IS LISTED IN THE DYNAMIC WORKAREA
*   AT LABEL 'MSG1SEG2'.
*   THE PUTLINE OUTPUT LINE DESCRIPTOR (OLD) IS ALSO IN THE DYNAMIC
*   WORKAREA AND IS AT LABEL 'MSG1OLD'.
***********************************************************************
*
MSG1SEG1 DC    H'19'                   LENGTH OF PRIMARY SEGMENT
         DC    H'0'                    OFFSET (ALWAYS 0 IN 1ST SEGMENT)
MSG1TXT  DS    0H
         DC    CL15' ROOM NUMBER IS'   PRIMARY SEGMENT TEXT
MSG1TX1L EQU   *-MSG1TXT               PRIMARY SEGMENT TEXT LENGTH
         SPACE
MSG1SEG3 DC    H'18'                   LENGTH OF  SEGMENT
         DC    AL2(MSG1TX1L)           OFFSET INTO PRIMARY SEGMENT
         DC    CL14' NOT SPECIFIED'    SEGMENT TEXT
         SPACE 2
*********************** DSECTS (MAPPING MACROS) ***********************
         IKJCPPL
CPPLLEN  EQU   *-CPPL
         IKJPPL
PPLLEN   EQU   *-PPL
         IKJIOPL
IOPLLEN  EQU   *-IOPL
         EJECT
*        PRINT NOGEN
         PRINT GEN
         $MODULE
         $TQE
         CVT
         IKJTCB ,                                                    @1
         IEZJSCB ,                                                   @1
         IEFJSSIB ,                                                  @1
         $BUFFER ,                                                   @1
         $SJB ,                                                      @1
         $JCT ,                                                      @1
         EJECT
*********************** DSECT FOR DYNAMIC WORKAREA ********************
WORKAREA DSECT
SAVEAREA DS    18F                     REGISTER SAVE AREA
         SPACE 2
*                                      MSG1'S OUTPUT LINE DESCRIPTOR
MSG1OLD  DS    F                       NUMBER OF SEGMENTS IN MESSAGE
MSG1OLDP DS    A                       ADDRESS OF PRIMARY SEGMENT
MSG1OLDS DS    A                       ADDRESS OF SECONDARY SEGMENT
         SPACE
MSG1SEG2 DS    H                       LENGTH OF SEGMENT
         DS    H                       OFFSET INTO PRIMARY SEGMENT
         DS    CL2                     SEGMENT TEXT (: )
MSG1ROOM DS    CL4                     SEGMENT TEXT (ROOM NUMBER)
MSG1SG2L EQU   *-MSG1SEG2              SECOND SEGMENT LENGTH
         SPACE 2
PTPB     PUTLINE MF=L                  INPUT/OUTPUT PARAMTER BLOCK
PPLSECT  DS    0F                      PARSE PARAMETER LIST AREA
         ORG   PPLSECT+PPLLEN
IOPLSECT DS    0F                      INPUT/OUTPUT PARAMETER LIST AREA
         ORG   IOPLSECT+IOPLLEN
         SPACE 2
RETURNCD DS    D                       DECIMAL RETURN CD FROM IKJEFF19
WORKDEC  DS    2F                      DECIMAL WORK AREA
ECB      DS    F                       ECB FOR PARS AND PUTLINE
PARSBACK DS    F                       PTR TO PDL AFTER GOOD PARSE
GFPARMP  DS    F                       PTR TO GNRLFAIL PARAMETER LIST
         EJECT
         IKJEFFGF
         SPACE 2
LDYNAMIC EQU   *-WORKAREA
         END   ROOM
