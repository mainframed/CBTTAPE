ISREDIT MACRO
/*--------------------------------------------------------------------*/
/* NOTEIMAC is a PDF EDIT initialization macro called by "NOTE" CLIST */
/* WRITTEN BY: Sam Lepore, Wells Fargo Bank, San Francisco CA    1/86 */
/*--------------------------------------------------------------------*/
CONTROL NOCAPS                      /* Allow lower case messages
ISPEXEC VGET TYPECALL
/*--------------------------------------------------------------------*/
/*  Set common profile parms for INITIALIZE, REEDIT, SUBCHANGE calls. */
/*--------------------------------------------------------------------*/
IF &TYPECALL = INITIALIZE OR &TYPECALL = REEDIT +
 OR &TYPECALL = SUBCHANGE THEN DO
  ISREDIT BOUNDS 1 79
  ISREDIT CAPS OFF
  ISREDIT NULLS ON
  ISREDIT NUMBER OFF
  ISREDIT TABSLINE = <80 '*'>
  ISREDIT TABS ON ALL
  SET &ZEDSMSG = &STR(Special format! Hit PF 1)  /* Maximum length is 24
  SET &ZEDLMSG = &STR(Mail data is specially formatted. Do not RENUM +
                 or NUM. Data in col 80 is lost.)  /* Maximum len is 78.
  ISPEXEC SETMSG MSG(ISRZ000)       /* Use ISPF defined dynamic message
  IF &TYPECALL = REEDIT THEN +
    EXIT
/*--------------------------------------------------------------------*/
/*  Seek subject line for insert (INITIALIZE) or update (SUBCHANGE).  */
/*--------------------------------------------------------------------*/
  ISREDIT SEEK SUBJECT: FIRST WORD 1
  IF &LASTCC = 0 THEN +
    IF &TYPECALL = INITIALIZE THEN DO
      ISREDIT (LINE) = CURSOR
      ISREDIT INSERT &EVAL(&LINE+1) 5  /* Skip blank line after subject.
      ISREDIT CURSOR = &EVAL(&LINE+1) 3  /* Put cursor on blank line.
      END
    ELSE +
      ISREDIT LINE .ZCSR = 'SUBJECT: ' + <10 (SUBJECT)>
  EXIT
  END
/*--------------------------------------------------------------------*/
/*  Add "To: node.userid   name" lines.                               */
/*--------------------------------------------------------------------*/
IF &TYPECALL = ADDTOLINES THEN DO
  SET &COUNT = 1
  SET &LINE = 0                     /* Start at ** TOP OF DATA **
  SET &FIRST4 = &STR(To: )          /* Put "To:" on first line only
  ISREDIT CAPS OFF                  /* Allow lower case in names
  ISPEXEC VGET TOLINES              /* Get the names of passed variables
  DO WHILE &SUBSTR(&COUNT:&COUNT+7,&TOLINES.ENDLINES) ¬= ENDLINES
    SET &LEFT = &SUBSTR(&COUNT:&COUNT+7,&TOLINES)
    IF &SUBSTR(&COUNT+8:&COUNT+15,&TOLINES.ENDLINES) ¬= ENDLINES THEN DO
      SET &RIGHT = &SUBSTR(&COUNT+8:&COUNT+15,&TOLINES)
      SET &COUNT = &COUNT + 8       /* Point to next nickname variable
      END
    ELSE DO
      SET &SPACE35 = &STR(                                   )
      SET &RIGHT = SPACE35          /* Use variable set to blanks
      END
    ISREDIT LINE_AFTER &LINE = <1 (FIRST4) 6 (&LEFT) 46 (&RIGHT)>
    SET &COUNT = &COUNT + 8         /* Point to next nickname variable
    SET &LINE = &LINE + 1           /* Increment current line counter
    SET &FIRST4 = &STR(    )        /* Blank columns 1 through 4
    END
  ISREDIT LINE_AFTER &LINE = ' '    /* Add blank line after "to" lines
  ISREDIT END
  EXIT
  END
/*--------------------------------------------------------------------*/
/*  Add PROFS header and trailer. These lines use hex chars FD FE FF. */
/*--------------------------------------------------------------------*/
IF &TYPECALL = PROFSFORMAT THEN DO
  ISREDIT BOUNDS                    /* Turn BOUNDS off
  ISREDIT CHANGE 'From-' 'From:' FIRST WORD 1  /* Change dash to colon
  SET &LINE = 0
  ISREDIT (LAST) = LINENUM .ZLAST   /* Get line number of last line
  DO WHILE &LINE < &LAST
    SET &LINE = &LINE + 1
    ISREDIT SHIFT ) &LINE 1         /* Shift line right, blank column 1
    END
  ISPEXEC VGET PROFNODE
  ISREDIT LINE_BEFORE .ZFIRST = 'ÚMSG:FROM: &SYSUID' + <20 '--TSO' +
          31 'TO: PROFS   --&PROFNODE' 63 '&SYSDATE' 72 '&SYSTIME'>
  ISREDIT LINE_AFTER .ZLAST = '    ' + <7 (SUBJECT) 78 'ÙÙÙ'>
  ISREDIT CHANGE X'FDFDFD' X'000000' .ZLAST .ZLAST 78 80
  ISREDIT END
  END
