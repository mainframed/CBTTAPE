STATS TITLE 'VERSION 2.0 --DISK DATA SET STATISTICS--  A. BRUCE LELAND'
* DATA SET STATS AT LEVEL 2 AS OF  6/15/82  (ABL - MORE STATS)        *
* DATA SET STATS AT LEVEL 1 AS OF  9/23/77  (ORIGINAL VERSION)        *
*                                                                     *
*                                                                     *
* PROGRAMMER:  A. BRUCE LELAND                                        *
*                                                                     *
*                                                                     *
* DESCRIPTION:  THIS PROGRAM FORMATS INFORMATION ON ANY DISK DATA     *
*     SET.  IT READS THROUGH THE ENTIRE DATA SET AND OUTPUTS DISK     *
*     TRACK USAGE, RECORD SIZES, COUNTS AND OTHER STATISTICS.         *
*                                                                     *
*     IN ADDITION, SEVERAL DATA SET VALIDITY CHECKS ARE PERFORMED     *
*     DURING INPUT PROCESSING TO INSURE THAT THE DATA SET WILL BE     *
*     USABLE BY THE SYSTEM FOR NON-EXCP PROCESSING.                   *
*                                                                     *
*     IF ANY ERRORS ARE ENCOUNTERED, THE RETURN CODE IS SET TO 4095   *
*     (OR THE PROGRAM ABENDS); OTHERWISE, THE RETURN CODE IS SET      *
*     TO THE MINIMUM OF 4094 AND THE NUMBER OF TRACKS WHICH SHOULD    *
*     COMPRESS OUT FOR PARTITIONED DATA SETS.                         *
*                                                                     *
*                                                                     *
*                                                                     *
* DEFINITIONS (FOR PARTITIONED DATA SETS):                            *
*                                                                     *
*     A.  REAL MEMBER - A NON-ALIAS MEMBER NAME WHICH IS PRESENT IN   *
*         THE DIRECTORY.                                              *
*                                                                     *
*     B.  GAS MEMBER - A MEMBER OF A PARTITIONED DATA SET WHICH HAS   *
*         BEEN REPLACED OR DELETED FROM THE DATA SET.  A GAS MEMBER   *
*         DOES NOT HAVE AN ENTRY IN THE DIRECTORY POINTING TO IT;     *
*         DISK STORAGE OCCUPIED BY GAS MEMBERS IS MADE USABLE FOR     *
*         OTHER MEMBERS BY AN IEBCOPY COMPRESS OPERATION.             *
*                                                                     *
*         NOTE: GAS MEMBERS CAN BE RESURRECTED BY THE TSO PDS COMMAND *
*         IF IT IS GIVEN THE BEGINNING TTR ADDRESS AND A MEMBER NAME. *
*                                                                     *
*                                                                     *
*                                                                     *
* PROGRAM PARM (ONLY THE FIRST PARM CHARACTER IS SIGNIFICANT;         *
*               AT MOST ONE OF THE FOLLOWING MAY BE SPECIFIED):       *
*                                                                     *
*     A.  LABELONLY  - LABEL INFORMATION IS TO BE FORMATTED BUT NO    *
*                      DATA SET READS ARE TO BE PERFORMED (EXCEPT     *
*                      THE READ FOR ANY ISAM FORMAT 2 DSCB).          *
*                                                                     *
*     B.  NOGAS      - NO GAS MEMBER REPORT IS TO BE PROVIDED FOR     *
*                      PARTITIONED DATA SETS.                         *
*                                                                     *
*     C.  ERRORSONLY - ONLY ERROR MESSAGES ARE TO BE OUTPUT.          *
*                                                                     *
*     D.  ALLEXTENTS - ALL EXTENTS OF THE DATA SET ARE TO BE READ     *
*                      REGARDLESS OF THE DS1LSTAR SETTING.            *
*                                                                     *
         EJECT                                                        *
*                                                                     *
* OPERATION:                                                          *
*                                                                     *
*     A.  THE PROGRAM PERFORMS A RDJFCB TO GET THE DSNAME AND VOLUME  *
*         NAME; AN OBTAIN TO GET THE FORMAT 1 DSCB; A DEVTYPE TO GET  *
*         THE DEVICE CHARACTERISTICS; AND AN OPEN TO INITIALIZE THE   *
*         DATA SET'S DATA EXTENT BLOCK (DEB) INFORMATION.             *
*                                                                     *
*     B.  THE PROGRAM FORMATS AND OUTPUTS DEB AND DSCB INFORMATION.   *
*                                                                     *
*     C.  THE PROGRAM THEN READS THROUGH THE DATA SET AND OUTPUTS     *
*         DISK TRACK USAGE, RECORD SIZES, COUNTS AND OTHER STATISTICS.*
*                                                                     *
*     D.  ADDITIONAL PROCESSING:                                      *
*                                                                     *
*         1. FOR PHYSICAL SEQUENTIAL, DIRECT OR VSAM DATA SETS, NO    *
*            ADDITIONAL PROCESSING IS PERFORMED.                      *
*                                                                     *
*         2. FOR ISAM DATA SETS, THE PROGRAM READS THROUGH THE        *
*            ENTIRE DATA SET (THERE MAY BE SEVERAL FILES OF DATA)     *
*            AND REPORTS ON EACH FILE.  ALSO, THE PROGRAM INPUTS      *
*            THE ISAM LABEL (FORMAT TWO DSCB) RECORD AND PROVIDES     *
*            A DATA SET PROFILE WHICH INCLUDES DATA SET               *
*            REORGANIZATION DATA AND DATA SET CHARACTERISTICS.        *
*                                                                     *
*         3. FOR PARTITIONED DATA SETS, IF THE DATA SET NAME AND A    *
*            MEMBER NAME IS ALLOCATED TO THE INPUT DATA SET, THE      *
*            MEMBER IS PROCESSED LIKE A SEQUENTIAL DATA SET.          *
*                                                                     *
*         4. FOR OTHER PARTITIONED DATA SETS, THE PROGRAM COMPARES    *
*            DIRECTORY TTR'S AGAINST ACTUAL DISK ADDRESSES TO         *
*            PROVIDE A REPORT BY GAS MEMBER:                          *
*            A.  FOR LOAD LIBRARIES, THE LINKAGE-EDIT DATE AND THE    *
*                NAMES OF THE FIRST FEW CSECTS ARE PROVIDED.          *
*            B.  FOR OTHER LIBRARIES, THE FIRST 79 CHARACTERS OF      *
*                EACH GAS MEMBER IS OUTPUT.                           *
*            STATISTICS ARE MAINTAINED ON THE SIZE OF GAS AND REAL    *
*            MEMBERS AND THE NUMBER OF ALIAS MEMBERS.  STATS CHECKS   *
*            FOR ALIASES WHICH HAVE NO REAL ENTRIES AND APPARENT      *
*            ALIASES (TWO REAL MEMBERS WITH THE SAME TTR).            *
*                                                                     *
*                                                                     *
*** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** ***
*                                                                     *
* PLEASE REPORT ANY PROBLEMS, ENHANCEMENTS, SUGGESTIONS OR COMMENTS   *
* CONCERNING THE STATS PROGRAM TO:                                    *
*                                                                     *
*     A. BRUCE LELAND           OR         A. BRUCE LELAND            *
*     HITACHI AMERICA, LTD.                6084 CRIMSON DRIVE         *
*     1800 BERING DRIVE                    SAN JOSE, CALIF 95120      *
*     SAN JOSE, CALIF. 95112                                          *
*     (408) 292-6404                       (408) 997-2366             *
*                                                                     *
*** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** ***
         EJECT
*                                                                     *
*   CHANGE SECTION:                                                   *
*                                                                     *
*     VERSION 1.0: STATS WAS WRITTEN FOR A UCLA PROGRAMMING CLASS     *
*                                                                     *
*     VERSION 1.1: MODIFIED TO ALLOW 3000 MEMBERS (INSTEAD OF 1000)   *
*                                                                     *
*     VERSION 2.0: ADDED MORE STATISTICS:                             *
*                                                                     *
*       A.  ADDED THE ALLEXTENTS AND ERRORSONLY PARM CONTROLS         *
*                                                                     *
*       B.  ADDED VSAM SUPPORT                                        *
*                                                                     *
*       C.  ADDED RECORD SIZE DISTRIBUTION OUTPUTS                    *
*                                                                     *
*       D.  ADDED LRECL STATISTICS AND VALIDITY CHECKS.               *
*                                                                     *
*       E.  ADDED CREATION, EXPIRATION AND LAST USE DATES.            *
*                                                                     *
*       F.  SUPPORTED DISK DEVICES: 2305-1, 2305-2, 2314,             *
*           3330, 3330-1, 3340, 3350, 3375 AND 3380.                  *
*                                                                     *
*       G.  ADDED MORE INFORMATION FOR I/O AND OTHER ERROR CONDITIONS.*
*                                                                     *
*       H.  ADDED SUPPORT FOR TRACK OVERFLOW DATA SETS.               *
*                                                                     *
*       I.  UNPRINTABLE CHARACTERS (FOR A 3270-TYPE DEVICE) IN THE    *
*           OUTPUT ARE TRANSLATED TO PERIODS.                         *
*                                                                     *
*                                                                     *
*       J.  MADE THE FOLLOWING CHANGES FOR PARTITIONED DATA SETS:     *
*                                                                     *
*           1.  ADDED GAS MODULE LINKAGE-EDIT DATE AND CSECT NAMES    *
*                                                                     *
*           2.  FIXED A BUG: THE REPORTED TTR OF GAS MEMBERS WAS      *
*               SOMETIMES INCORRECT (A TRACK SWITCH CHECK WAS MISSED).*
*                                                                     *
*           3.  ENHANCED THE COMPRESS ALGORITHM: INSTEAD OF ESTI-     *
*               MATING POTENTIAL FREE TRACKS FROM (TOTAL GAS SIZE)/   *
*               (AVERAGE BYTES ON A TRACK); A COMPRESS IS NOW SIMU-   *
*               LATED.  THE FORMER CALCULATION WORKED WELL ONLY FOR   *
*               LIBRARIES WITH BLOCKSIZES WHICH DID NOT VARY.         *
*                                                                     *
*           4.  ADDED DIRECTORY BLOCK STATISTICS.                     *
*                                                                     *
*           5.  ADDED A CHECK FOR APPARENT ALIAS MEMBERS (TWO REAL    *
*               MEMBERS WITH THE SAME TTR).                           *
*                                                                     *
*           6.  ADDED REAL AND GAS MEMBER TOTAL BYTE COUNTS.          *
*                                                                     *
*           7.  THE MEMBER TABLE IS NOW DYNAMICALLY ALLOCATED; ITS    *
*               SIZE IS DOUBLED EACH TIME IT IS EXCEEDED.             *
*                                                                     *
*** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** ***
         EJECT
         MACRO
&LBL     OUTPT   &R0,&R1,&ERR=RTN   OUTPUT MACRO -- NUMBER AND STRING
&LBL     LA      R1,&R1             STRING TO FOLLOW
         LA      R15,L'&R1          LENGTH OF STRING
         AIF     ('&R0' EQ 'R0').NOR0
         L       R0,&R0             NUMBER TO BE OUTPUT
.NOR0    BAL     R14,OUT&ERR        OUTPUT ROUTINE
         AIF     ('&R1' NE 'WORK').MEND
         MVC     WORK(80),BLANKS    BLANK THE WORK AREA
.MEND    MEND
         SPACE 3
         MACRO
&LBL     OUTSTR  &R1,&LN=0,&ERR=RTN OUTPUT MACRO -- CHARACTER STRING
&LBL     LA      R1,&R1             STRING TO FOLLOW
         AIF     ('&LN' EQ '0').LENR1
         LA      R15,L'&LN          LENGTH OF STRING
         AGO     .BAL
.LENR1   LA      R15,L'&R1          LENGTH OF STRING
.BAL     BAL     R14,OUT&ERR.S      OUTPUT ROUTINE
         AIF     ('&R1' NE 'WORK').MEND
         MVC     WORK(80),BLANKS    BLANK THE WORK AREA
.MEND    MEND
         SPACE 3
         MACRO
&LBL     TTRMBB  &TTR,&MBB=DOUBLE  TTR TO MBBCCHHR CONVERSION MACRO
&LBL     STM     R14,R12,12(R13)
         ICM     R0,7,&TTR         GET FIRST THREE BYTES
         SLL     R0,8              ADJUST
         L       R1,DCBDEBAD       DEB ADDRESS
         LA      R2,&MBB           RESULT ADDRESS
         LR      R3,R13            SAVE SAVEAREA REGISTER
         L       R15,16            CVT POINTER
         L       R15,28(,R15)      TTRN->MBBCCHHR ROUTINE ADDRESS
         BALR    R14,R15
         LR      R13,R3
         LM      R14,R12,12(R13)
         MEND
         EJECT
         MACRO
&LBL     MBBTTR  &MBB,&TTR=TEMP    MBBCCHHR TO TTR CONVERSION MACRO
&LBL     STM     R14,R12,12(R13)
         LA      R2,&MBB           MBBCCHHR TO CONVERT
         L       R1,DCBDEBAD       DEB ADDRESS
         LR      R3,R13            SAVE SAVEAREA REGISTER
         L       R15,16            CVT POINTER
         L       R15,32(,R15)      MBBCCHHR->TTRN ROUTINE ADDRESS
         BALR    R14,R15
         LR      R13,R3
         ST      R0,12+4+4(,R13)   SAVE R0
         LM      R14,R12,12(R13)
         ST      R0,&TTR           SAVE RESULT TTRN
         MEND
         SPACE 3
         MACRO
&LBL     HEXES   &SRC,&NUM,&OUT     OUTPUT LOOKS LIKE C1.C2.C3 . . .
         LCLC    &ONE                    FOR NUM SOURCE CHARACTERS
&ONE     SETC    'HX'.'&SYSNDX'
&LBL     SR      R15,R15
         LA      R14,&NUM
         LR      R1,R14
         BCTR    R1,0
         LA      R1,&NUM.(R1,R1)    LAST PERIOD AT (N-1)*3+1
         LA      R1,&OUT.-2(R1)     OUTPUT START LOCATION
&ONE     LA      R15,&SRC           SOURCE BASE ADDRESS
         SR      R0,R0
         BCTR    R15,0
         IC      R0,0(R14,R15)      INPUT CHARACTER
         LR      R15,R0
         N       R15,=F'15'
         IC      R15,HEXTBL(R15)
         STC     R15,2(,R1)         LOW NIBBLE
         LR      R15,R0
         SRL     R15,4
         IC      R15,HEXTBL(R15)
         STC     R15,1(,R1)         HIGH NIBBLE
         C       R14,=F'1'          LAST DIGIT?
         BE      *+8                YES, BRANCH
         MVI     0(R1),C'.'         SEPARATOR
         SH      R1,=H'3'           BACK UP 3 IN OUTPUT ADDRESS
         BCT     R14,&ONE           DO PREVIOUS INPUT CHARACTER
         MEND
         EJECT
         PRINT NOGEN
STATS    CSECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SAVE  (14,12),,*
         LR    R9,R15
         LA    R12,2048
         LA    R10,2048(R9,R12)
         LA    R11,2048(R10,R12)
         LA    R12,2048(R11,R12)
         USING STATS,R9,R10,R11,R12
         ST    R13,SAVE+4
         LR    R2,R13
         LA    R13,SAVE
         ST    R13,8(R2)
         LA    R8,IN
         USING IHADCB,R8
         L     R2,0(,R1)            PARM ADDRESS
         LH    R3,0(,R2)
         LTR   R3,R3                IS LENGTH ZERO?
         BZ    OPENIT               YES, BRANCH
         SPACE 2
         CLI   2(R2),C'N'           GAS RECORDS DESIRED?
         BNE   *+8                  YES, BRANCH
         NI    FLAGS,255-GAS        NO, TURN OFF GAS RECORDS
         SPACE 1
         CLI   2(R2),C'A'           ALL EXTENTS DESIRED?
         BNE   *+8                  YES, BRANCH
         OI    FLAGS,ALLXTNT        NO, TURN ON ALLXTNT
         SPACE 1
         CLI   2(R2),C'E'           ERROR MESSAGES ONLY?
         BNE   *+8                  YES, BRANCH
         OI    FLAGS,ERRONLY        NO, TURN ON ERRONLY
         SPACE 1
         CLI   2(R2),C'L'           LABEL INFORMATION ONLY?
         BNE   *+8                  NO, BRANCH
         OI    FLAGS,LABELS         YES, SET THE LABELONLY FLAG
         EJECT
OPENIT   BAL   R14,OPENIN           OPEN THE INPUT FILE
         OUTSTR PROGRAM             PROGRAM NAME AND PURPOSE
         SPACE 1
         LTR   R3,R3                ANY PARM?
         BZ    OPENIT2              NO, BRANCH
         OUTSTR BLANKS
         BCTR  R3,0                 MACHINE LENGTH
         MVC   WORK+5(5),=C'PARM='  PARM IDENTIFIER
         MVC   WORK+10(*-*),2(R2)   <<EXECUTED>>
         EX    R3,*-6               MOVE IN THE PARM FIELD
         CLI   FLAGS,GAS            FLAGS CHANGED?
         BNE   *+10                 YES, BRANCH
         MVC   WORK+24(32),=C'(INVALID, DEFAULTS WILL BE USED)'
         OUTSTR WORK
OPENIT2  OUTSTR BLANKS
         OUTSTR BLANKS
         SPACE 1
         DEVTYPE INDD,DEVAREA,DEVTAB  GET DEVICE CHARACTERISTICS
         SPACE 2
         MVC   WORK(12),=C'//IN DD DSN='
         MVC   WORK+12(44),JFCBDSNM DATA SET NAME
         LA    R1,WORK+12
         SPACE 1
         A     R1,=F'1'             SCAN FOR
         CLI   0(R1),X'40'                  FINAL
         BNE   *-8                               BLANK
         SPACE 1
         TM    DS1DSORG,DS1DSGPO    PARTITIONED DATA SET ORGANIZATION?
         BNO   NOELMENT             NO, BRANCH
         CLI   JFCBELNM,X'40'       ANY PDS MEMBER NAME
         BE    NOELMENT             NO, BRANCH
         MVI   DS1DSORG,DS1DSGPS    CHANGE DSORG TO SEQUENTIAL
         MVC   BLDLMEM,JFCBELNM     MEMBER NAME
         MVI   0(R1),C'('           FIRST PARENTHESIS
         MVC   1(8,R1),BLDLMEM      MEMBER NAME
         SPACE 1
         A     R1,=F'1'             SCAN FOR
         CLI   0(R1),X'40'                  FINAL
         BNE   *-8                               BLANK
         SPACE 1
         MVI   0(R1),C')'           LAST PARENTHESIS
         A     R1,=F'1'             ALLOW FOR IT
NOELMENT MVC   0(9,R1),=CL9',VOL=SER='
         MVC   9(6,R1),JFCBVOLS     ADD THE VOLUME SERIAL
         LA    R1,9(,R1)            SKIP OVER ,VOL=SER=
         SPACE 1
         A     R1,=F'1'             SCAN FOR
         CLI   0(R1),X'40'                  FINAL
         BNE   *-8                               BLANK
         SPACE 1
         MVI   0(R1),C','
         OUTSTR WORK
         EJECT
         MVC   WORK(2),=C'//'      FORMAT THE DCB INFORMATION
         MVC   WORK+8(12),=CL12'DCB=(RECFM=*'
         LA    R1,WORK+8+11
         TM    DS1RECFM,X'C0'     RECFM=U?
         BNO   *+12               NO, BRANCH
         MVI   0(R1),C'U'
         B     DCBCONVA
         TM    DS1RECFM,X'80'     RECFM=F?
         BNO   *+12               NO, BRANCH
         MVI   0(R1),C'F'
         B     DCBCONVA
         TM    DS1RECFM,X'40'     RECFM=V?
         BNO   *+8                NO, BRANCH
         MVI   0(R1),C'V'
         SPACE  1
DCBCONVA A     R1,=F'1'
         TM    DS1RECFM,X'10'     RECFM=.B?
         BNO   *+12               NO, BRANCH
         MVI   0(R1),C'B'
         A     R1,=F'1'
         TM    DS1RECFM,X'08'     RECFM=.S?
         BNO   *+12               NO, BRANCH
         MVI   0(R1),C'S'
         A     R1,=F'1'
         TM    DS1RECFM,X'20'     RECFM=.T?
         BNO   *+12               NO, BRANCH
         MVI   0(R1),C'T'
         A     R1,=F'1'
         TM    DS1RECFM,X'04'     RECFM=.A?
         BNO   *+12               NO, BRANCH
         MVI   0(R1),C'A'
         A     R1,=F'1'
         TM    DS1RECFM,X'02'     RECFM=.M?
         BNO   *+12               NO, BRANCH
         MVI   0(R1),C'M'
         A     R1,=F'1'
         SPACE 1
         TM    DS1RECFM,X'C0'     RECFM=U?
         BO    DCBCONVC           YES, NO LRECL MESSAGE
         MVC   0(7,R1),=CL7',LRECL='
         LA    R1,7(,R1)
         LH    R15,DS1LRECL
         CVD   R15,DOUBLE
         UNPK  DOUBLE(5),DOUBLE+5(3)
         LA    R15,DOUBLE-1
         A     R15,=F'1'          SCAN
         CLI   0(R15),C'0'            PAST LAST
         BE    *-8                             LEADING ZERO
         OI    DOUBLE+4,X'F0'
DCBCONVB MVC   0(1,R1),0(R15)     MOVE IN EACH CHARACTER
         A     R1,=F'1'
         A     R15,=F'1'
         TM    0(R15),X'F0'       NEXT CHARACTER NUMERIC?
         BO    DCBCONVB           NO, BRANCH
         EJECT
DCBCONVC MVC   0(9,R1),=CL9',BLKSIZE='
         LA    R1,9(,R1)
         LH    R15,DS1BLKL
         CVD   R15,DOUBLE
         UNPK  DOUBLE(5),DOUBLE+5(3)
         LA    R15,DOUBLE-1
         A     R15,=F'1'          SCAN
         CLI   0(R15),C'0'            PAST LAST
         BE    *-8                             LEADING ZERO
         OI    DOUBLE+4,X'F0'
DCBCONVD MVC   0(1,R1),0(R15)     MOVE IN EACH CHARACTER
         A     R1,=F'1'
         A     R15,=F'1'
         TM    0(R15),X'F0'       NEXT CHARACTER NUMERIC?
         BO    DCBCONVD           NO, BRANCH
         SPACE 1
         SR    R0,R0
         ICM   R0,1,DS1KEYL       KEYLENGTH=0?
         BZ    DCBCONVG           YES, BRANCH
         MVC   0(8,R1),=CL8',KEYLEN='
         LA    R1,8(,R1)
         CVD   R0,DOUBLE
         UNPK  DOUBLE(5),DOUBLE+5(3)
         LA    R15,DOUBLE-1
         A     R15,=F'1'          SCAN
         CLI   0(R15),C'0'            PAST LAST
         BE    *-8                             LEADING ZERO
         OI    DOUBLE+4,X'F0'
DCBCONVE MVC   0(1,R1),0(R15)     MOVE IN EACH CHARACTER
         A     R1,=F'1'
         A     R15,=F'1'
         TM    0(R15),X'F0'       NEXT CHARACTER NUMERIC?
         BO    DCBCONVE           NO, BRANCH
         SPACE 1
         MVC   0(5,R1),=CL5',RKP='
         LA    R1,5(,R1)
         ICM   R0,3,DS1RKP
         CVD   R0,DOUBLE
         UNPK  DOUBLE(5),DOUBLE+5(3)
         LA    R15,DOUBLE-1
         A     R15,=F'1'          SCAN
         CLI   0(R15),C'0'            PAST LAST
         BE    *-8                             LEADING ZERO
         OI    DOUBLE+4,X'F0'
DCBCONVF MVC   0(1,R1),0(R15)     MOVE IN EACH CHARACTER
         A     R1,=F'1'
         A     R15,=F'1'
         TM    0(R15),X'F0'       NEXT CHARACTER NUMERIC?
         BO    DCBCONVF           NO, BRANCH
         EJECT
DCBCONVG MVC   0(9,R1),=CL9',DSORG=**'
         LA    R1,7(,R1)
         TM    DS1DSORG,DS1DSGPS  PS?
         BNO   *+10               NO, BRANCH
         MVC   0(2,R1),=CL2'PS'
         TM    DS1DSORG,DS1DSGPO  PO?
         BNO   *+10               NO, BRANCH
         MVC   0(2,R1),=CL2'PO'
         TM    DS1DSORG,DS1DSGDA  DA?
         BNO   *+10               NO, BRANCH
         MVC   0(2,R1),=CL2'DA'
         TM    DS1DSORG,DS1DSGIS  IS?
         BNO   *+10               NO, BRANCH
         MVC   0(2,R1),=CL2'IS'
         TM    DS1DSORG+1,DS1ACBM VSAM?
         BNO   *+10               NO, BRANCH
         MVC   0(2,R1),=CL2'VS'
         TM    DS1DSORG,DS1DSGU   UNMOVEABLE?
         BNO   *+12               NO, BRANCH
         MVI   2(R1),C'U'
         A     R1,=F'1'
         MVI   2(R1),C')'
         OUTSTR WORK
         EJECT
         OUTSTR BLANKS
         OUTSTR DATEHDR
         OUTSTR DATEUND
         LA    R1,DS1CREDT
         BAL   R14,CONVLABL
         MVC   WORK+10(8),0(R1)
         LA    R1,DS1EXPDT
         BAL   R14,CONVLABL
         MVC   WORK+20(8),0(R1)
         LA    R1,DS1REFD
         BAL   R14,CONVLABL
         MVC   WORK+31(8),0(R1)
         MVC   WORK+42(2),=C'ON'  ASSUME DATA SET IS CHANGED
         TM    DS1DSIND,DS1DSCHA  CORRECT?
         BO    *+10               YES, BRANCH
         MVC   WORK+42(3),=C'OFF' NO, SAY UPDATE BIT IS OFF
         MVC   WORK+55(4),=C'NONE'  ASSUME NO SECURITY
         TM    DS1DSIND,DS1IND40+DS1IND10+DS1IND04  CORRECT?
         BZ    DATEWORK                             YES, BRANCH
         MVC   WORK+55(12),=C'RACF DEFINED'         ASSUME RACF-DEFINED
         TM    DS1DSIND,DS1IND40                    CORRECT?
         BO    DATEWORK                             YES, BRANCH
         MVC   WORK+55(15),=C'WRITE PROTECTED'      ASSUME WRITE PROT.
         TM    DS1DSIND,DS1IND10+DS1IND04           CORRECT?
         BO    DATEWORK                             YES, BRANCH
         MVC   WORK+55(20),=C'READ/WRITE PROTECTED' NO, READ AND WRITE
DATEWORK OUTSTR WORK
         OUTSTR BLANKS
         L     R1,DCBDEBAD
         L     R14,NUMEXT           NUMBER OF EXTENTS
         SR    R0,R0
         AH    R0,46(,R1)           TRACKS IN THE EXTENT
         LA    R1,16(,R1)           OFFSET BETWEEN EXTENTS
         BCT   R14,*-8              REPEAT FOR EACH EXTENT
         ST    R0,TOTALTRK          SAVE TRACK TOTAL
         LH    R2,DS1LSTAR          LAST TT USED
         CLI   DS1LSTAR+2,X'00'     LAST TRACK ACTUALLY USED?
         BE    *+8                  NO, BRANCH
         A     R2,=F'1'             YES, ADD ONE MORE TRACK
         SR    R0,R2                AVAILABLE SPACE
         ST    R0,AVAILTRK
         TTRMBB DS1LSTAR,MBB=LASTMBB      LAST USED MBBCCHHR
         OUTPT TOTALTRK,NUMTRKS
         TM    DS1DSORG,DS1DSGIS          ISAM DATA SET?
         BO    ALLOC1                     YES, BRANCH
         TM    DS1DSORG+1,DS1ACBM         VSAM DATA SET?
         BO    ALLOC1                     YES, BRANCH
         ICM   R0,15,AVAILTRK             ANY AVAILABLE TRACKS?
         BZ    FREENONE                   NO, BRANCH
         OUTPT R0,FRETRKS                 NUMBER OF FREE TRACKS
         B     FREESOME
FREENONE OUTSTR FRENONE                   NO FREE TRACKS
         EJECT
FREESOME SR    R0,R0
         ICM   R0,3,DS1TRBAL
         OUTPT R0,TRBALS                  BYTES LEFT ON LAST USED TRACK
ALLOC1   TM    DS1SCALO,X'C0'                         CYLINDER ALLOC?
         BNO   *+14                                   NO, BRANCH
         MVC   SCALOS+25(9),=CL9'CYLINDERS'           YES
         B     ALLOCS
         SPACE 1
         BNZ   *+14                                   ABS. TRACKS
         MVC   SCALOS+25(15),=CL15'ABSOLUTE TRACKS'   YES
         B     ALLOCS
         SPACE 1
         TM    DS1SCALO,X'80'                         TRACKS?
         BO    ALLOCS                                 YES, BRANCH
         SPACE 1
         MVC   SCALOS+25(6),=CL6'BLOCKS'              BLOCKS
         TM    DS1SCALO,X'01'                         ROUND TO CYL?
         BNO   ALLOCS                                 NO, BRANCH
         MVC   SCALOS+32(12),=CL12'(WITH ROUND)'      YES
         SPACE 2
ALLOCS   OUTSTR SCALOS
         SR    R0,R0
         ICM   R0,7,DS1SCALO+1    ANY SECONDARIES ALLOCATED?
         BNZ   SECS               YES, BRANCH
         OUTSTR NOSEC             NO, MESSAGE
         B     ALLSECS
SECS     OUTPT R0,EXTENTS
         SPACE 2
ALLSECS  HEXES DS1LSTAR,3,LSTAR+12  TTR FORM
         CVD   R2,DOUBLE            TRACKS ACTUALLY USED
         MVC   LSTAR+26+1(5),=X'2020202120'
         ED    LSTAR+26(6),DOUBLE+5
         OUTSTR LSTAR
         OUTSTR BLANKS
         EJECT
         OUTSTR EXTHDR
         OUTSTR EXTUND
         L     R2,DCBDEBAD
         L     R3,NUMEXT
         SR    R4,R4              EXTENT NUMBER
         SR    R5,R5              BEGINNING TT OF TTR
         SPACE 2
HEXAGN   CVD   R4,DOUBLE
         A     R4,=F'1'           NEXT EXTENT
         MVC   WORK+1(5),=X'2020202120'
         ED    WORK(6),DOUBLE+5
         L     R1,32(R2)          UCB ADDRESS
         MVC   WORK+08(3),13(R1)  UCB
         MVC   WORK+13(6),28(R1)  VOLUME SERIAL
         LH    R6,46(,R2)         NUMBER OF TRACKS IN THIS EXTENT
         LR    R0,R5              START OF THIS EXTENT
         AR    R5,R6              START OF NEXT EXTENT
         LA    R1,1
         SLL   R1,24
         SLDL  R0,8
         ST    R0,TEMP            TTR WHERE R=01
         HEXES TEMP+1,3,WORK+21   CONVERT TO TT.TT.RR
         LR    R0,R5              START OF NEXT EXTENT
         BCTR  R0,0               START OF NEXT EXTENT -1
         LA    R1,X'FF'           RR=X'FF'
         SLL   R1,24
         SLDL  R0,8
         ST    R0,TEMP            TTR WHERE R=FF
         HEXES TEMP+1,3,WORK+31   CONVERT TO TT.TT.RR
         CVD   R6,DOUBLE          TRACKS IN THIS EXTENT
         MVC   WORK+41+1(5),=X'2020202120'
         ED    WORK+41(6),DOUBLE+5
         HEXES 38(R2),4,WORK+49   FIRST CC.HH FOR THIS EXTENT
         HEXES 42(R2),4,WORK+62   SECOND CC.HH FOR THIS EXTENT
         MVC   WORK+75(3),=C'YES' ASSUME CYLINDER BOUNDARIES
         CLC   40(2,R2),=H'0'     FIRST HHHH=0?
         BE    *+10               YES, BRANCH
         MVC   WORK+75(3),=C'NO ' NOT CYLINDER BOUNDARIES
         LH    R1,44(,R2)         SECOND HHHH
         A     R1,=F'1'           PLUS ONE
         CH    R1,DEVTRKCY        HHHH=MAXIMUM FOR DEVICE?
         BE    *+10               YES, BRANCH
         MVC   WORK+75(3),=C'NO ' NOT CYLINDER BOUNDARIES
         OUTSTR WORK
         LA    R2,16(,R2)         NEXT EXTENT DISPLACEMENT
         BCT   R3,HEXAGN
         EJECT
         TM    FLAGS,LABELS       LABELONLY DESIRED?
         BO    END2               YES, DO NOT DO ANY READS
         OUTSTR BLANKS
         OUTSTR BLANKS
         TM    DS1DSORG,DS1DSGPO  PARTITIONED DATA SET?
         BO    PO                 YES, BRANCH
         CLI   BLDLMEM,X'40'      ANY MEMBER NAME ENTERED?
         BE    INPUTS             NO, BRANCH
         BLDL  IN,BLDLPARM        GET THE TTR FOR THIS MEMBER
         LTR   R15,R15            FIND IT?
         BZ    MEMBER2            YES, BRANCH
         MVC   MEMBERNO(8),BLDLMEM  MEMBER NAME
         OUTSTR MEMBERNO,ERR=ERR
         ABEND 13
         SPACE 2
MEMBER2  TTRMBB BLDLTTR,MBB=IOBSEEK  CONVERT TTR TO MBBCCHHR
         L     R1,DCBDEBAD        DEB ADDRESS
         LA    R1,16(,R1)         CURRENT EXTENT POINTER - 16
         SPACE 1
         SR    R15,R15
MEMBER4  LA    R1,16(,R1)         NEXT EXTENT POINTER
         CLM   R15,1,IOBSEEK      THIS EXTENT?
         LA    R15,1(,R15)
         BNE   MEMBER4            NO, BRANCH
         SPACE 2
         ST    R1,CUREXT          UPDATE FOR LATER
         SR    R0,R0
         IC    R0,IOBSEEK+7       REDUCE
         S     R0,=F'1'                 INITIAL RECORD
         STC   R0,IOBSEEK+7                           NUMBER
         LA    R15,4              EARLY EXIT FLAG
         ST    R15,EXCPR15        SAVE FOR EXCP ROUTINE
         LA    R14,INPUTS         RETURN ADDRESS
         ST    R14,EXCPR14        SAVE FOR EXCP ROUTINE
         B     INITREAD
         EJECT
INPUTS   BAL   R14,EXCPSTAT       READ EACH BLOCK AND COLLECT STATS
         LTR   R15,R15            END OF FILE?
         BZ    INPUTS             NO, CONTINUE READING
         LR    R2,R15             SAVE FOR LATER
         OUTSTR EOFSS
         TM    FLAGS,ALLXTNT      ALL EXTENTS DESIRED?
         BO    INPUTS2            YES, BRANCH
         TM    DS1DSORG,DS1DSGIS  ISAM DATA SET?
         BNO   ENDDATA            NO, BRANCH -- END OF DATA SET
         SPACE 1
INPUTS2  CLC   MINBLKI,=F'33000'  EVER CHANGED?
         BNE   *+10               YES, BRANCH
         SR    R14,R14            NO, RESET TO ZERO
         ST    R14,MINBLKI
         OUTPT MINBLKI,STATBLKS
         LA    R14,1
         C     R14,READTOTI
         BH    *+8
         L     R14,READTOTI       TAKE MAX OF READTOTI AND 1
         L     R1,READTOTI
         SRL   R1,1
         A     R1,BYTECNTI
         SR    R0,R0
         DR    R0,R14             AVG BLK= (BYTECNTI + READTOTI/2) /
         LR    R0,R1                        MAX( READTOTI, 1)
         OUTPT R0,STATBLKS
         OUTPT MAXBLKI,STATBLKS
         OUTPT READTOTI,NUMBLKS
         MVC   STATBLKS+7(7),=C'MINIMUM'  RESET THE MESSAGE TEXT
         MVC   MINBLKI,=F'33000'  RESET
         SR    R14,R14                 THE
         ST    R14,READTOTI               FILE
         ST    R14,MAXBLKI                    STATISTICS
         ST    R14,BYTECNTI                             VARIABLES
         CH    R2,=H'4'           END OF DATA SET?
         BH    ENDDATA            YES, BRANCH
         OUTSTR BLANKS
         B     INPUTS             NO, REPORT ON NEXT SEGMENT
*
ENDDATA  OUTSTR BLANKS
         L     R0,READTOT
         LTR   R0,R0              ANY RECORDS READ?
         BP    ENDDSET            YES, NORMAL DATA SET
         OUTSTR NULL              NO RECORDS IN THE DATA SET
         B     END2               QUIT
         EJECT
PO       OI    FLAGS,DIRECTOR       TURN ON THE "IN DIRECTORY" FLAG
         SR    R0,R0
         ICM   R0,1,DS1NOBDB        LAST DIRECTORY BLOCK IN USE?
         BZ    GETMTBL              NO, BRANCH
         OUTPT R0,NOBDBS
         OUTSTR BLANKS
         SPACE 2
GETMTBL  L     R0,MBRCOUNT          ELEMENTS IN THE MEMBER TABLE
         SLL   R0,4                 16 WIDE
         GETMAIN R,LV=(0)
         SH    R1,=H'16'            BACK UP ONE ENTRY
         ST    R1,SAVETBL           SAVE TABLE START -16
         LR    R5,R1                INITIALIZE R5
         SPACE 2
NEXTMBR  BAL   R14,MEMBERS          GET THE NEXT MEMBER
         B     *+4(R15)             BRANCH TABLE
         B     GOODMBR              SUCCESSFUL
         B     MBREOF               EOF IN DIRECTORY
         B     LASTUSED             LAST USED ENTRY FOUND
         B     NOTDIREC             NOT A DIRECTORY RECORD
*
GOODMBR  LA    R2,ALIASES
         OI    0(R1),X'40'           INSURE SECOND BIT IS ON
         TM    11(R1),X'80'          ALIAS ENTRY?
         BO    *+12                  YES, BRANCH
         LA    R2,REALMBR            NO, A REAL ENTRY --
         NI    0(R1),X'FF'-X'40'         TURN OFF THE SECOND BIT
         L     R14,0(,R2)
         A     R14,=F'1'             INCREMENT ALIASES OR REALMBR
         ST    R14,0(,R2)
         LR    R2,R1
         TTRMBB 8(R1),MBB=DOUBLE     CONVERT TTR TO MBBCCHHR
         L     R1,SAVETBL
TBLINC   LA    R1,16(,R1)
         CR    R1,R5                 EMPTY TABLE ENTRY?
         BH    INSERT                YES, BRANCH
         CLC   8(8,R1),DOUBLE        IS IT THIS ENTRY?
         BNE   TBLINC                NO, CHECK NEXT ENTRY
         TM    11(R2),X'80'          ALIAS?
         BO    NEXTMBR               YES, BRANCH
         MVC   ALIASERR(8),0(R1)     ASSUME ALIAS ERROR
         MVC   0(8,R1),0(R2)         USE THE REAL MEMBER NAME
         TM    ALIASERR,X'40'        ALIAS ENTRY?
         BO    NEXTMBR               YES, BRANCH
         MVC   ALIASERR+13(8),0(R2)  OTHER MEMBER NAME
         OI    ALIASERR,X'40'        MAKE PRINTABLE
         OI    ALIASERR+13,X'40'     MAKE PRINTABLE
         OUTSTR ALIASERR,ERR=ERR     APPARENT ALIASES BUT NO ALIAS FLAG
         OUTSTR BLANKS                 FOR EITHER MEMBER
         B     NEXTMBR
         SPACE 5
INSERT   MVC   0(8,R1),0(R2)         INSERT THE NAME
         MVC   8(8,R1),DOUBLE        INSERT THE MBBCCHHR
         LR    R5,R1                 SAVE THE HIGH-TABLE USE ENTRY
         L     R1,REALENT
         A     R1,=F'1'              COUNT THE NUMBER OF TABLE ENTRIES
         ST    R1,REALENT
         C     R1,MBRCOUNT
         BL    NEXTMBR
         L     R0,MBRCOUNT           TRY AGAIN WITH TWICE AS MANY
         SLL   R0,1                  ENTRIES*2
         ST    R0,MBRCOUNT
         L     R1,SAVETBL            START OF OLD MEMBER TABLE -16
         AH    R1,=H'16'             START OF OLD MEMBER TABLE
         SLL   R0,3                  ENTRIES*8 (*2 ABOVE)
         FREEMAIN R,LV=(0),A=(1)     RELEASE THIS STORAGE
         SR    R0,R0
         ST    R0,READTOT            BLOCKS READ
         ST    R0,TRACKNUM           CURRENT TRACK NUMBER
         ST    R0,BYTECNT            BYTE COUNT
         ST    R0,DIRPTRS+4+4        REINITIALIZE MEMBERS POINTER (R1)
         ST    R0,TOTENTRY           NUMBER OF MEMBERS
         MVI   MINENTRY+3,X'FF'      MINIMUM NUMBER OF ENTRIES
         MVI   CURENTRY+3,X'FF'      CURRENT NUMBER OF ENTRIES
         ST    R0,LASTR              LAST RECORD ON THIS TRACK
         ST    R0,CURBYTE            LAST BYTE ON THIS TRACK
         ST    R0,TRKBYTE            TOTAL BYTES READ
         ST    R0,ALIASES            TOTAL ALIASES
         ST    R0,REALMBR            TOTAL REAL MEMBERS
         ST    R0,REALENT            MEMBER TABLE ENTRIES
         ST    R0,COMPR              COMPRESS RECORDS ON TRACK
         MVC   COMPTRK,=F'1'         COMPRESS OUTPUT TRACK
         ST    R0,COMPTRBL           COMPRESS TRACK BALANCE
         XC    MBBCCHH(7),MBBCCHH    CLEAR THE SAVED DISK ADDRESS
         B     GETMTBL               TRY AGAIN
         SPACE 2
NOTDIREC SR    R15,R15               SIMULATED END OF FILE (GOOD READ)
MBREOF   L     R1,READTOT            END OF FILE IN DIRECTORY
         ST    R1,USEDBLK            USED COUNT
         B     PRESORT
         SPACE 2
LASTUSED L     R0,READTOT            END OF USED ENTRIES IN DIRECTORY
         ST    R0,USEDBLK
RPTEXCP  BAL   R14,EXCPSTAT          READ AND COLLECT STATISTICS
         LTR   R15,R15
         BZ    RPTEXCP
         EJECT
PRESORT  SR    R0,R0
         NI    FLAGS,X'FF'-DIRECTOR  TURN OFF THE "IN DIRECTORY" FLAG
         LR    R6,R15                SAVE FOR LATER
         L     R1,TRACKNUM           TRACKS READ TO THIS POINT
         CLI   CURMBB+7,X'01'        EOF ONLY ON THIS TRACK (SO FAR)?
         BNE   *+8                   NO, BRANCH
         S     R1,=F'1'              YES, USE THE PREVIOUS TRACK NUMBER
         ST    R1,TRACKDIR           TRACKS USED IN THE DIRECTORY
         L     R1,READTOT            ALLOCATED BLOCK COUNT
         ST    R1,ALLOCBLK           ALLOCATED COUNT
         ST    R0,READTOT            REINITIALIZE READTOT
         ST    R0,MAXBLK                                 MAXBLK
         ST    R0,BYTECNT                                      BYTECNT
         ST    R0,BLKDIST            DO NOT USE DIRECTORY ENTRIES
         MVC   MINBLK(4),=F'33000'   REINITIALIZE MINBLK
         SPACE 1
*
*     SORT THE MEMBER ARRAY BY MBBCCHHR  (USES A SHELLSORT)
*
         L     R2,REALENT           NUMBER OF ELEMENTS TO SORT
         SLA   R2,4                 GAP IS  16 * ELEMENTS
         L     R3,SAVETBL           TABLE BASE ADDRESS
         LR    R0,R2                N=GAP
SHELLG   SRL   R2,5                 GAP=GAP/2  (DROP FRACTION BITS)
         SLA   R2,4                 GAP LESS THAN ONE?
         BZ    SORTED               YES, DONE
         LR    R4,R2                I=GAP
SHELLI   LA    R4,16(,R4)           I=I+1   (TABLE WIDTH IS 16)
         CR    R4,R0                I<=N?
         BH    SHELLG               NO, HALVE THE GAP
         LR    R1,R4                YES, J=I
SHELLJ   SR    R1,R2                J=J-GAP
         BNP   SHELLI               IF J<=0, INCREMENT I
         LA    R15,0(R3,R1)         ADDRESS OF SAVETBL(J)
         LA    R14,0(R2,R1)         JG=J+GAP
         LA    R14,0(R3,R14)        ADDRESS OF SAVETBL(JG)
         CLC   8(8,R15),8(R14)      MBBCCHHR(J) <= MBBCCHHR(JG)
         BNH   SHELLI               YES, INCREMENT I
         XC    0(16,R14),0(R15)     INTERCHANGE
         XC    0(16,R15),0(R14)                SAVETBL ENTRIES
         XC    0(16,R14),0(R15)                               J AND JG
         B     SHELLJ               INCREMENT J
         EJECT
SORTED   ST    R5,LASTREF           SAVE FOR LATER
         L     R3,REALENT
         L     R2,SAVETBL
         CR    R5,R2                ANY MEMBERS?
         BE    NULLPDS              NO, BRANCH
TRYAGN   LA    R2,16(,R2)
         TM    0(R2),X'40'          ALIAS (IN THE TABLE)?
         BNO   REALAGN              NO, BRANCH
         MVC   ALNOREAL(8),0(R2)    YES, ALIAS WITH NO REAL
         OUTSTR ALNOREAL
         OUTSTR BLANKS
REALAGN  BCT   R3,TRYAGN
         SPACE 2
         L     R2,SAVETBL
         MVC   LASTMEMS(8),0(R5)    LAST MEMBER IN THE PDS
         OI    LASTMEMS,X'40'       MAKE PRINTABLE (IF REAL NAME)
         OUTSTR LASTMEMS
         OUTSTR BLANKS
         SPACE 2
NULLPDS  OUTPT ALLOCBLK,ALLBLK
         OUTPT USEDBLK,USEBLK
         L     R0,ALLOCBLK          ALLOCATED BLOCKS
         SLL   R0,8                 TIMES 256
         OUTPT R0,DIRBYTES          BYTES USED FOR THE DIRECTORY
         OUTPT TRACKDIR,DIRTRACK
         OUTSTR BLANKS
         OUTPT MINENTRY,STATDIR
         L     R15,USEDBLK
         SRL   R15,1
         A     R15,TOTENTRY         (TOTAL MEMBERS + BLOCKS/2) / BLOCKS
         SR    R14,R14
         D     R14,USEDBLK
         LR    R0,R15
         OUTPT R0,STATDIR
         OUTPT MAXENTRY,STATDIR
         OUTPT TOTENTRY,USENAME
         ICM   R0,15,ALIASES
         BZ    NOTALIAS
         OUTPT R0,ALSNAME
NOTALIAS OUTSTR BLANKS
         LR    R15,R6               RESTORE THE RETURN CODE
         TM    DS1RECFM,X'C0'       RECFM=U?
         BNO   NXTREAL              NO, BRANCH
         MVC   GASMEM+28(15),=C'THIS MEMBER WAS'
         EJECT
NXTREAL  LA    R2,16(,R2)            LAST MEMBER WAS REAL
NXTONE   L     R0,BYTECNT            LAST MEMBER WAS GAS
         ST    R0,SAVETOT            SAVE FOR LATER
         CH    R15,=H'8'             END OF DATA SET?
         BE    LASTMTCH              YES, BRANCH
         OI    FLAGS,REALONE         ASSUME A REAL MEMBER
         MVC   CURRMBR(8),0(R2)      SAVE THE CURRENT MEMBER NAME
         CLC   8(8,R2),IOBSEEK       NEXT MBBCCHHR IN THE TABLE?
         BE    REALRD                YES, BRANCH TO READ LOOP
         BH    NOTREAL               NO, HIGHER -- A GAS MEMBER
         C     R2,LASTREF            BEYOND MEMBER TABLE ENTRIES
         BH    NOTREAL               YES,       -- A GAS MEMBER
         OUTSTR TTRISLOW,ERR=ERR
         OUTSTR DSBAD,ERR=ERR        DATA SET IS PROBABLY DESTROYED
         ABEND 13            **  TABLED TTR < CURRENT TTR  **
NOTREAL  L     R1,GASCNT             A GAS MEMBER
         A     R1,=F'1'
         ST    R1,GASCNT             ADD ONE TO THE GAS MEMBER COUNT
         XI    FLAGS,REALONE         TURN OFF THE ACTUAL MEMBER FLAG
         SPACE 1
         TM    FLAGS,GAS             GAS RECORDS DESIRED?
         BNO   GASRD                 NO, BRANCH
         MBBTTR IOBSEEK,TEMP         CONVERT MBBCCHHR TO TTR
         HEXES TEMP,3,GASMEM+18
         OUTSTR BLANKS
         OUTSTR GASMEM               OUTPUT A HEADER
         TM    DS1RECFM,X'C0'        LRECL=U?
         BNO   DODATA                NO, BRANCH
         OI    FLAGS,DORECFMU        YES, SET A FLAG
         MVC   WORK(43),=C'LINKAGE-EDITED ON ??/??/??; CSECTS ARE:  ??'
         LA    R7,WORK+38            START OF CSECT DATA
         B     GASRD
         SPACE 2
DODATA   BAL   R14,EXCPSTAT          INPUT THE NEXT REAL BLOCK
         MVC   WORK(13),=C'(NULL MEMBER)' ASSUME NULL
         LTR   R6,R15                     GOOD READ?
         BNZ   GASWRITE                   NO, BRANCH
         MVC   WORK(80),BLANKS            CLEAR THE WORK AREA
         ICM   R15,15,LSBLK               CURRENT LENGTH OK?
         BNZ   *+8                        YES, BRANCH
         L     R15,=F'1'                  NO, USE 1 BYTE
         CH    R15,=H'79'                 BEYOND 79 BYTES?
         BNH   *+8                        NO, BRANCH
         LH    R15,=H'79'                 YES, USE 79 BYTES
         S     R15,=F'1'                  REDUCE FOR EXECUTE
         MVC   WORK(*-*),BUFFER           <<EXECUTED>>
         EX    R15,*-6                    MOVE IN THE BUFFER DATA
         OUTSTR WORK
         LR    R15,R6                RESTORE RETURN CODE
         B     GASRD+4               SKIP THE FIRST READ (ALREADY DONE)
         SPACE 1
GASRD    BAL   R14,EXCPSTAT          GAS MEMBER
         L     R1,GASRDS
         A     R1,=F'1'              ADD ONE TO THE GAS RECORD COUNT
         LTR   R15,R15               END OF FILE OR ERROR?
         BP    *+8                   YES, BRANCH
         ST    R1,GASRDS
         TM    FLAGS,DORECFMU        RECORD FORMAT=U?
         BNO   GASRDF                NO, BRANCH
         LTR   R6,R15                GOOD READ?
         BZ    GASLOADM              YES, BRANCH
         CLI   WORK+41,C'?'          NULL MEMBER?
         BNE   GASWRITE              NO, BRANCH
         MVC   WORK(80),BLANKS       CLEAR THE LINE
         MVC   WORK(4),=C'NULL'      NULL IDENTIFIER
         B     GASWRITE
GASLOADM CLI   WORK+41,C'?'          FIRST TIME HERE?
         BNE   *+8                   NO, BRANCH
         MVI   WORK+41,X'40'         YES, BLANK THE FIRST ? MARK
         CLI   BUFFER,X'40'          TEST SYM RECORD?
         BE    GASRDR6               YES, IGNORE THIS RECORD
         CLI   BUFFER,X'20'          CESD RECORD?
         BNE   GASIDR                NO, BRANCH
         LA    R3,BUFFER+8           START OF ESD DATA
         LH    R5,BUFFER+6           LENGTH OF DATA IN BUFFER
         AR    R5,R3
         LA    R4,16                 LENGTH OF ONE ENTRY
         SR    R5,R4
         SPACE 2
         USING ESDNAME,R3
ESDSCAN  IC    R0,ESDTYPE
         LA    R1,CODESEG            CHECK FOR SEGTAB/ENTAB
         NI    ESDTYPE,X'0F'
         NR    R0,R1
         CR    R0,R1                 THIS SEGTAB/ENTAB ENTRY?
         BE    ESDUSE1               YES, BRANCH
         SPACE
         CLI   ESDTYPE,CODESD        SD ENTRY?
         BNE   NEXTESD               NO, BRANCH
         SPACE 1
ESDUSE1  LA    R0,WORK               START OF OUTPUT MESSAGE
         LR    R15,R7                CURRENT OUTPUT POSTION
         SR    R15,R0                LENGTH OF MESSAGE SO FAR
         MVC   0(5,R7),=C', ...'     COMMA AND SPACE FOR ESD ENTRIES
         MVI   WORK+38,C':'          RESTORE THE FIRST COLON
         CH    R15,=H'64'            MORE THAN 64 BYTES?
         BH    GASRDR6               YES, BRANCH
         MVC   2(8,R7),ESDNAME       NO, ADD ANOTHER ENTRY
         A     R7,=F'1'              SKIP OVER COMMA
         SPACE 1
         A     R7,=F'1'              SCAN
         CLI   0(R7),X'40'               FOR FIRST
         BNE   *-8                                NON-BLANK
         SPACE 1
NEXTESD  BXLE  R3,R4,ESDSCAN         GET ALL ESD'S IN THIS RECORD
         DROP  R3
         B     GASRDR6               GET NEXT ESD RECORD
         SPACE 2
GASIDR   CLI   BUFFER,X'80'          IDR RECORD?
         BNE   GASWRITE              NO, LINKAGE-EDIT DATE NOT FOUND
         NI    BUFFER+2,X'0F'        CLEAR MISCELLANEOUS FLAGS
         CLI   BUFFER+2,X'02'        LINKAGE-EDITOR IDR RECORD?
         BNE   GASRDR6               NO, NOT YET
         LA    R1,BUFFER+15          ADDRESS OF DATE
         BAL   R14,CONVDATE          CONVERT THE LINKAGE-EDIT DATE
         MVC   WORK+18(8),0(R1)      MOVE INTO MESSAGE LINE
         SPACE 1
GASWRITE OUTSTR WORK                 OUTPUT THE DATA ACCUMULATED
         NI    FLAGS,X'FF'-DORECFMU  OUTPUT FOR RECFM=U IS DONE
GASRDR6  LR    R15,R6                RESTORE REG 15
GASRDF   LTR   R15,R15               GOOD READ?
         BZ    GASRD                 YES, BRANCH
         SPACE 2
         L     R0,BYTECNT
         S     R0,SAVETOT            GAS BYTES THIS MEMBER
         L     R1,GASBYTE
         AR    R1,R0
         ST    R1,GASBYTE            TOTAL GAS BYTES
         C     R0,MAXGAS
         BNH   *+8
         ST    R0,MAXGAS             MAX BYTES/GAS MEMBER
         C     R0,MINGAS
         BNL   *+8
         ST    R0,MINGAS             MIN BYTES/GAS MEMBER
         B     NXTONE
         SPACE 3
REALRD   BAL   R14,EXCPSTAT          REAL MEMBER
         SPACE 1
         LTR   R15,R15               END OF FILE?
         BZ    REALRD                NO, CONTINUE
         L     R0,BYTECNT
         S     R0,SAVETOT
         L     R1,RELBYTE
         AR    R1,R0
         ST    R1,RELBYTE            TOTAL BYTES FOR REAL MEMBERS
         C     R0,MAXREL
         BNH   *+8
         ST    R0,MAXREL             MAX BYTES/REAL MEMBER
         C     R0,MINREL
         BNL   *+8
         ST    R0,MINREL             MIN BYTES/REAL MEMBER
         B     NXTREAL
         EJECT
LASTMTCH C     R2,LASTREF         ALL MEMBERS ACCOUNTED FOR?
         BH    ENDDSET            YES, BRANCH
         OUTSTR BLANKS
         OUTSTR NOTFOUND,ERR=ERR
         SPACE 1
LOSTMBR  MVC   NOMEMBER(8),0(R2)  MEMBER NAME
         OI    NOMEMBER,X'40'     MAKE PRINTABLE
         MBBTTR 8(R2),TEMP        CONVERT MBBCCHHR TO TTR
         HEXES  TEMP,3,NOMEMBER+16
         OUTSTR NOMEMBER,ERR=ERR
         LA    R2,16(,R2)         NEXT MEMBER
         C     R2,LASTREF         LAST ONE?
         BNH   LOSTMBR            NO, BRANCH
         SPACE 2
         OUTSTR BLANKS
         OUTSTR LSTARBAD,ERR=ERR
         OUTSTR RERUN,ERR=ERR
         ABEND 19           **  SOME MEMBERS IN THE TABLE NOT FOUND  **
         EJECT
ENDDSET  ICM    R0,15,GASRDS      ANY GAS MEMBERS FOUND?
         BNP    ENDDSET2          NO, BRANCH
         OUTSTR BLANKS            YES, OUTPUT ANOTHER BLANK LINE
         SPACE 1
ENDDSET2 OUTSTR BLANKS
         SPACE 1
         OUTSTR STATTRKH
         OUTSTR STATTRKU
         MVC   WORK(13),=C'RECORDS/TRACK'
         L     R15,TRACKNUM
         SRL   R15,1
         A     R15,ALLR           (ALLR + TRACKNUM/2) / TRACKNUM
         SR    R14,R14
         D     R14,TRACKNUM       AVERAGE RECORDS PER TRACK
         L     R1,MINR
         CVD   R1,DOUBLE
         MVC   WORK+20+1(5),=X'2020202120'
         ED    WORK+20(6),DOUBLE+5
         CVD   R15,DOUBLE
         MVC   WORK+31+1(5),=X'2020202120'
         ED    WORK+31(6),DOUBLE+5
         L     R1,MAXR
         CVD   R1,DOUBLE
         MVC   WORK+42+1(5),=X'2020202120'
         ED    WORK+42(6),DOUBLE+5
         OUTSTR WORK
         MVC   WORK(11),=C'BYTES/TRACK'
         L     R15,TRACKNUM
         SRL   R15,1
         A     R15,TRKBYTE        (TRKBYTE + TRACKNUM/2) / TRACKNUM
         SR    R14,R14
         D     R14,TRACKNUM       AVERAGE BYTES PER TRACK
         ST    R15,AVGBTRK
         L     R1,MINBYTE
         CVD   R1,DOUBLE
         MVC   WORK+20+1(5),=X'2020202120'
         ED    WORK+20(6),DOUBLE+5
         CVD   R15,DOUBLE
         MVC   WORK+31+1(5),=X'2020202120'
         ED    WORK+31(6),DOUBLE+5
         L     R1,MAXBYTE
         CVD   R1,DOUBLE
         MVC   WORK+42+1(5),=X'2020202120'
         ED    WORK+42(6),DOUBLE+5
         OUTSTR WORK
         OUTSTR BLANKS
         OUTSTR BLANKS
         EJECT
LRECL0   TM    DS1RECFM,X'C0'     RECFM=U OR NO RECFM?
         BNM   LRECL1             YES, DON'T DO LRECL RANGES
         TM    DS1RECFM,X'80'     RECFM=F?
         BO    LRECL1             YES, DON'T DO LRECL RANGES
         L     R0,MINLRECL
         C     R0,=F'33000'       EVER CHANGED?
         BNE   *+6                YES, BRANCH
         SR    R0,R0              NO, RESET TO ZERO
         OUTPT R0,STATLREC
         LA    R14,1
         C     R14,TOTLRECL
         BH    *+8
         L     R14,TOTLRECL    TAKE MAX OF TOTLRECL AND 1
         L     R1,TOTLRECL
         SRL   R1,1
         A     R1,ALLLRECL
         SR    R0,R0
         DR    R0,R14          AVG LRECL=(ALLLRECL + TOTLRECL/2) /
         LR    R0,R1                     MAX(TOTLRECL, 1)
         OUTPT R0,STATLREC
         OUTPT MAXLRECL,STATLREC
LRECL1   OUTPT TOTLRECL,TOTLRECM
         OUTSTR BLANKS
         SPACE 1
LRECL2   ICM   R0,15,EXCEEDBL       ANY RECORD LENGTHS EXCEED BLKSIZE?
         BZ    LRECL3               NO, BRANCH
         OUTPT R0,EXCEEDMS,ERR=ERR  YES, SAY HOW MANY
         OUTSTR BLANKS
         SPACE 1
LRECL3   ICM   R0,15,DIVIDEBL       ALL RECORD SIZES DIVISIBLE/LRECL?
         BZ    LRECL4               YES, BRANCH
         OUTPT R0,DIVIDEMS,ERR=ERR  NO, SAY HOW MANY
         OUTSTR BLANKS
         SPACE 1
LRECL4   ICM   R0,15,INVBLOCK       ANY INVALID BLOCK HEADERS?
         BZ    LRECL5               NO, BRANCH
         OUTPT R0,INVBLOCM,ERR=ERR  YES, SAY HOW MANY
         OUTSTR BLANKS
         SPACE 1
LRECL5   ICM   R0,15,OVRLRECL       ANY ACTUAL LRECL EXCEED MAXIMUM?
         BZ    ALLBYTES             NO, BRANCH
         OUTPT R0,OVRLRECM,ERR=ERR  YES, SAY HOW MANY
         OUTSTR BLANKS
         EJECT
ALLBYTES OUTSTR BLANKS
         L     R0,TRKBYTE         BYTES FOR ALL TRACKS
         L     R1,ALLOCBLK        ALLOCATED DIRECTORY BLOCKS (IF ANY)
         SLL   R1,8               IN BYTES
         SR    R0,R1              BYTES FOR ALL TRACKS LESS DIRECTORY
         OUTPT R0,TOTBYTS         TOTAL DATA BYTES LESS DIRECTORY
         OUTPT TRACKNUM,TOTTRKS   TOTAL TRACKS READ
         SPACE 2
         OUTSTR BLANKS
         L     R0,MINBLK
         C     R0,=F'33000'       EVER CHANGED?
         BNE   *+6                YES, BRANCH
         SR    R0,R0              NO, RESET TO ZERO
         OUTPT R0,STATBLKS
         LA    R14,1
         C     R14,READTOT
         BH    *+8
         L     R14,READTOT     TAKE MAX OF READTOT AND 1
         L     R1,READTOT
         SRL   R1,1
         A     R1,BYTECNT
         SR    R0,R0
         DR    R0,R14          AVG BLK= (BYTECNT + READTOT/2) /
         LR    R0,R1                     MAX( READTOT, 1)
         OUTPT R0,STATBLKS
         OUTPT MAXBLK,STATBLKS
         OUTSTR BLANKS
         OUTSTR BLANKS
         EJECT
         ICM   R0,15,READTOT    ANY RECORDS READ?
         BZ    TOTALR           NO, BRANCH
         OUTSTR RECSIZE
         SR    R2,R2            OFFSET
         LA    R3,65            ENTRIES IN BLKSIZE TABLE
         LA    R4,BLKDIST       FIRST ENTRY
         L     R5,READTOT
         A     R5,=F'1'         (READTOT+1)/2 FOR ROUNDING
         SRL   R5,1
         SPACE 1
NEXT500  CVD   R2,DOUBLE        FIRST NUMBER IN RANGE
         LA    R2,499(,R2)      NEXT NUMBER IN RANGE
         ICM   R1,15,0(R4)      ANY DATA?
         BNP   NEXT1            NO, BRANCH
         UNPK  WORK(5),DOUBLE+5(3)
         OI    WORK+4,X'F0'     MAKE PRINTABLE
         MVI   WORK+5,C'-'
         CVD   R2,DOUBLE        SECOND NUMBER IN RANGE
         UNPK  WORK+6(5),DOUBLE+5(3)
         OI    WORK+6+4,X'F0'   MAKE PRINTABLE
         M     R0,=F'100'
         AR    R1,R5
         D     R0,READTOT       OCCURANCES*100 + (TOTALIO/2) / TOTALIO
         C     R1,=F'100'          IS PERCENT ROUNDED
         BNH   *+8
         L     R1,=F'100'       MAXIMUM PERCENT IS 100
         LR    R14,R1
         A     R14,=F'1'
         SRA   R14,1            PERCENT/2 ROUNDED = ZERO?
         BNZ   *+8              NO, BRANCH
         L     R14,=F'1'        YES, USE ONE
         MVC   WORK+12(*-*),BLKPER  <<EXECUTED>>
         EX    R14,*-6          MOVE IN THE PERCENT LINE
         LA    R14,WORK+12(R14) POINT TO LAST CHARACTER MOVED
         MVI   0(R14),C'>'      ADD A TERMINATOR
         CVD   R1,DOUBLE        PERCENT
         MVC   2(4,R14),=X'2021206C'
         ED    1(4,R14),DOUBLE+6
         L     R1,0(,R4)
         CVD   R1,DOUBLE        OCCURANCES
         MVC   8(5,R14),=X'2020202120'
         ED    7(6,R14),DOUBLE+5
         OUTSTR WORK
NEXT1    A     R2,=F'1'         NEXT ONE
         LA    R4,4(,R4)        NEXT TABLE ENTRY
         BCT   R3,NEXT500       DO ALL ENTRIES
         OUTSTR BLANKS
         OUTSTR BLANKS
TOTALR   OUTPT READTOT,NUMBLKS
         TM    DS1DSORG,DS1DSGPO  PARTITIONED DATA SET?
         BNO   END2               NO, BRANCH
         EJECT
         ICM   R0,15,GASRDS       ANY GAS RECORDS?
         BNZ   SOMEGAS            YES, BRANCH
         OUTSTR NOGASBLK          NO, MESSAGE
         B     NONEGAS
SOMEGAS  OUTPT R0,GASBLKS
NONEGAS  OUTPT EOFS,ENDMBR
         ICM   R0,15,GASCNT       ANY GAS MEMBERS?
         BZ    NOGASM             NO, BRANCH
         OUTPT R0,GASMBR
         OUTSTR BLANKS
         OUTPT MINGAS,STATGAS     MIN BYTES PER GAS MEMBER
         L     R15,GASCNT
         SRL   R15,1
         A     R15,GASBYTE
         SR    R14,R14
         D     R14,GASCNT
         LR    R0,R15
         OUTPT R0,STATGAS         AVG BYTES PER GAS MEMBER
         OUTPT MAXGAS,STATGAS     MAX BYTES PER GAS MEMBER
         OUTPT GASBYTE,STATGAST   TOTAL GAS BYTES
         TM    DS1RECFM,DCBRECTO  TRACK OVERFLOW DATA SET?
         BO    NOGASM             YES, BRANCH
         OUTSTR BLANKS
         L     R1,TOTALTRK        TRACKS IN THIS DATA SET
         S     R1,COMPTRK         FREE TRACKS AFTER COMPRESS
         S     R1,AVAILTRK        TRACKS WHICH WOULD COMPRESS OUT
         M     R0,=F'1000'        COMPRESS TRACKS *1000
         L     R15,COMPTRBL       ESTIMATED NEW DS1TRBAL
         SR    R14,R14
         ICM   R14,3,DS1TRBAL     CURRENT BYTES LEFT ON LAST TRACK
         SR    R15,R14            GAIN OR LOSS IN BYTES
         M     R14,=F'1000'       GAIN OR LOSS *1000
         SR    R2,R2
         ICM   R2,3,DEVMAXT       MAXIMUM TRACK LENGTH
         DR    R14,R2             -999 TO +999 (FOR -0.999 TO +0.999)
         AR    R1,R15             ADD THE PARTIAL TRACKS -- VALID?
         BNM   *+6                YES, BRANCH
         SR    R1,R1              NO, USE ZERO
         AH    R1,=H'5'           ROUND TO NEAREST HUNDREDTH
         CVD   R1,DOUBLE
         D     R0,=F'1000'        WHOLE TRACKS COMPRESS OUT
         CH    R1,=H'4094'        ABOVE MAXIMUM?
         BNH   *+8                NO, BRANCH
         LH    R1,=H'4094'        YES, USE THE MAXIMUM
         CLI   RETURNCD+3,0       ANY ERRORS YET?
         BNE   *+8                YES, BRANCH
         ST    R1,RETURNCD        NO, SAVE FOR RETURN CODE
         ED    GASTRK(12),DOUBLE+3  MAX OUTPUT IS 999,999.99X TRACKS
         MVI   GASTRK+11,X'40'    CLEAR THE THOUSANDTHS DIGIT
         LA    R1,GASTRK
         A     R1,=F'1'           SCAN FOR
         CLI   0(R1),X'40'                FIRST
         BE    *-8                             NON-BLANK
         OUTSTR 0(R1),LN=GASTRK   OUTPUT GAS TRACK TOTAL
         EJECT
NOGASM   OUTSTR BLANKS
         SPACE 1
         L     R0,EOFS
         S     R0,GASCNT
         BNP   NOREAL
         ST    R0,TEMP
         OUTPT MINREL,STATREAL    MIN BYTES PER REAL MEMBER
         L     R15,TEMP
         SRL   R15,1
         A     R15,RELBYTE
         SR    R14,R14
         D     R14,TEMP
         LR    R0,R15
         OUTPT R0,STATREAL        AVG BYTES PER MEMBER
         OUTPT MAXREL,STATREAL    MAX BYTES PER MEMBER
         OUTPT RELBYTE,STATRELT   TOTAL BYTES FOR REAL MEMBERS
         B     END1
         SPACE 1
NOREAL   OUTSTR NOREALS
         SPACE 2
END1     L     R1,SAVETBL         START OF MEMBER TABLE -16
         AH    R1,=H'16'          START OF MEMBER TABLE
         L     R0,MBRCOUNT        NUMBER OF MEMBER ENTRIES
         SLL   R0,4               TABLE WIDTH IS 16
         FREEMAIN R,LV=(0),A=(1)  YES, ALIAS WITH NO REAL
         SPACE 2
END2     OUTSTR BLANKS
         OUTSTR BLANKS
         TM    FLAGS,ALLXTNT        ALL EXTENTS TO HAVE BEEN READ?
         BZ    END3                 NO, BRANCH
         MBBTTR CURMBB,TTR=TEMP     LAST GOOD READ ADDRESS
         HEXES TEMP,3,LASTREAD+32   CONVERT TO TT.TT.RR
         OUTSTR LASTREAD            OUTPUT
         OUTSTR BLANKS
         L     R0,TOTALTRK          TOTAL TRACKS IN THE DATA SET
         S     R0,TRACKNUM          ANY NOT READ?
         BNP   END3                 NO, BRANCH
         OUTPT R0,NOTREAD,ERR=ERR   TOTAL TRACKS NOT READ
         OUTSTR BLANKS
         EJECT
END3     CLOSE IN                 END POINT IF DSORG=PO, PS OR DA
         TM    DS1DSORG,DS1DSGIS  ISAM DATA SET?
         BNO   END4               NO, BRANCH
         OI    JFCBTSDM,X'08'     PREVENT REVERSE MERGE TO JFCB
         MVI   JFCBDSNM,X'04'     SET-UP DSNAME FOR
         MVC   JFCBDSNM+1(43),JFCBDSNM             FORMAT 4 DSCB
         OPEN  (IN,INPUT),TYPE=J  DO AN OPEN JFCB
         TM    DCBOFLGS,X'10'     OPENED CORRECTLY?
         BZ    ABE42              NO, QUIT
         BAL   R14,REREAD
         MVC   IOBSEEK+3(5),DS1PTRDS  POINTER TO FORMAT 2 DSCB
         BAL   R14,EXCP           READ THE FORMAT 2 DSCB
         LTR   R15,R15
         BZ    NOABE11
         OUTSTR NODSCB2,ERR=ERR   FORMAT 2 DSCB NOT READ CORRECTLY
         ABEND 11
NOABE11  OUTSTR BLANKS
         OUTSTR DSCB2
         SR    R0,R0
         IC    R0,DS2NOLEV
         OUTPT R0,DSSNOLEV         INDEX LEVELS
         IC    R0,DS2DVIND
         OUTPT R0,DSSDVIND         TRACKS FOR MASTER INDEX
         IC    R0,DS2CYLOV
         OUTPT R0,DSSCYLOV         TRACKS OF CYL. OVERFLOW PER CYLINDER
         ICM   R0,3,DS2TAGDT
         OUTPT R0,DSSTAGDT         TAGGED FOR DELETION
         ICM   R0,7,DS2RORG3
         OUTPT R0,DSSRORG3         OVERFLOW RECORD ACCESSES
         LH    R0,DS2NOBYT
         OUTPT R0,DSSNOBYT         CORE TO HOLD THE HIGHEST INDEX
         SR    R0,R0
         IC    R0,DS2NOTRK
         OUTPT R0,DSSNOTRK         TRACKS OCCUPIED BY THE HIGHEST INDEX
         ICM   R0,15,DS2PRCTR
         OUTPT R0,DSSPRCTR         RECORDS IN THE PRIME AREA
         LH    R0,DS2BYOVL
         OUTPT R0,DSSBYOVL         BYTES LEFT ON CUR. IND. OVER. TRACK
         LH    R0,DS2RORG2
         OUTPT R0,DSSRORG2         TRACKS LEFT IN IND. OVERFLOW AREA
         LH    R0,DS2OVRCT
         OUTPT R0,DSSOVRCT         RECORDS IN THE OVERFLOW AREA
         LH    R0,DS2RORG1
         OUTPT R0,DSSRORG1         CYLINDER OVERFLOW AREAS (FULL)
         CLOSE IN
         OUTSTR BLANKS
         EJECT
END4     ICM   R0,15,TOTALIO        ANY I/O ERRORS?
         BZ    END5                 NO, BRANCH
         OUTPT R0,TOTALIOS,ERR=ERR  YES, SAY HOW MANY
         OUTSTR BLANKS
         SPACE 2
END5     L     R2,RETURNCD
         CVD   R2,DOUBLE
         MVC   WORK+5(5),=X'2020202120'
         ED    WORK+4(6),DOUBLE+5
         LA    R1,WORK
         SPACE 1
         A     R1,=F'1'            SCAN
         CLI   3(R1),X'40'              FOR FIRST
         BE    *-8                               NON-BLANK
         SPACE 1
         MVC   0(3,R1),=C'RC='
         OUTSTR 0(R1),LN=DOUBLE
         CLOSE OUT
         L     R13,4(,R13)
         LR    R15,R2
         RETURN (14,12),RC=(15)
         EJECT
EXCPSTAT ST    R14,STATR14    ISSUES EXCP'S AND GATHERS STATISTICS
EXCPBAL  BAL   R14,EXCP
         B     *+4(R15)
         B     R15IS0             GOOD READ
         B     R15IS4             END OF FILE OR END OF MEMBER
         B     R15IS8             END OF DATA SET
R15IS12  L     R14,TOTALIO        I/O OR OTHER DISK ERROR
         A     R14,=F'1'          UPDATE TOTAL NUMBER OF ERRORS
         ST    R14,TOTALIO
         L     R14,INAROW         I/O OR OTHER DISK ERROR
         A     R14,=F'1'          UPDATE NUMBER OF CONSECUTIVE ERRORS
         ST    R14,INAROW
         CH    R14,=H'10'         ALLOW ONLY 10 CONSECUTIVE ERRORS
         BL    CONTINUE
         OUTSTR TOOMANY,ERR=ERR   TOO MANY CONSECUTIVE ERRORS
         ABEND 999
CONTINUE HEXES SAVECB,1,IOERROR+16    ECB ERROR CODE
         HEXES CURMBB+3,5,IOERROR+26  CURRENT DISK CCHHR
         MBBTTR CURMBB,TTR=TEMP       CONVERT TO TTR
         HEXES TEMP,3,IOERROR+49      CURRENT DISK TTR
         OUTSTR IOERROR,ERR=ERR
         TM    DS1DSORG,DS1DSGPO  PARTITIONED DATA SET?
         BNO   BLKNUM             NO, BRANCH
         TM    FLAGS,DIRECTOR     ERROR IN THE DIRECTORY?
         BO    DIRERR             YES, BRANCH
         TM    FLAGS,REALONE      ACTUAL MEMBER?
         BNO   GASERR             NO, BRANCH
         OI    CURRMBR,X'40'      MAKE PRINTABLE
         OUTSTR CURRMBR,ERR=ERR   OUTPUT THE CURRENT MEMBER NAME
         B     R15IS4$
GASERR   OUTSTR GASERRS,ERR=ERR   ERROR IN READING GAS DATA
         B     R15IS4$
DIRERR   OUTSTR DIRERRS,ERR=ERR   DIRECTORY I/O ERROR (IGNORE BLOCK)
BLKNUM   L     R0,READTOT         RECORDS READ SO FAR
         A     R0,=F'1'           THIS RECORD NUMBER
         OUTPT R0,BLKNUMS,ERR=ERR ERROR MESSAGE
         B     EXCPBAL            CONTINUE--USING THE NEXT BLOCK
         SPACE 1
R15IS8   SR    R0,R0              END OF DATA SET
         ST    R0,LSTRK           UPDATE THE RECORD LENGTH
         CLC   MBBCCHH(7),CURMBB  SAME TRACK?
         BNE   NEWTRACK           NO, BRANCH
         B     TRKNOINC           FINISH UP PROCESSING
         SPACE 2
R15IS4   XC    INAROW,INAROW      END OF MEMBER
R15IS4$  LA    R15,4              TREAT AS AN END OF MEMBER CONDITION
         L     R1,EOFS
         A     R1,=F'1'           END OF MEMBER COUNTER
         ST    R1,EOFS
         SR    R0,R0              DATA LENGTH IS ZERO
         ST    R0,LSTRK           UPDATE THE RECORD LENGTH TOO
         B     CHECKTRK           CHECK FOR A TRACK SWITCH
         EJECT
R15IS0   XC    INAROW,INAROW      GOOD READ
R15IS0$  L     R1,READTOT
         A     R1,=F'1'
         ST    R1,READTOT         COUNT OF BLOCKS READ
         L     R1,LSBLK           CURRENT BLOCK LENGTH
         ST    R1,LSTRK           CURRENT LENGTH ON TRACK
         SPACE 3
         TM    DS1RECFM,DCBRECTO  TRACK OVERFLOW DATA SET?
         BNO   OVERTRK            NO, BRANCH
         CLI   IOBSEEK+7,2        NEXT R OF CCHHR = 2?
         BNE   OVERTRK            NO, BRANCH
         CLI   CURMBB+7,1         THIS R OF CCHHR = 1?
         BE    OVERTRK            YES, BRANCH
         SPACE 2
*  TRACK OVERFLOW RECORDS READ OK; HOWEVER, THE LENGTH OF AN OVERFLOW
*    RECORD IS INCORRECT (IT EQUALS THE FIRST PART OF THE RECORD).
         SPACE 1
         A     R1,CURBYTE
         ST    R1,CURBYTE         ACCUMULATE TRACK BYTE COUNT
         L     R1,LASTR
         A     R1,=F'1'
         ST    R1,LASTR           ACCUMULATE RECORD COUNT/TRACK
         MVC   LSBLK(2),DATALN    SAVE NEXT DATA LENGTH
         MVI   IOBSEEK+7,0        WANT TO GET R=1 LENGTH
         LA    R15,4              EARLY EXIT FROM READ
         ST    R15,EXCPR15        SAVE FOR ROUTINE USE
         LA    R14,OVERNXT        RETURN ADDRESS
         ST    R14,EXCPR14        SAVE FOR ROUTINE USE
         B     INITREAD           READ THE FIRST RECORD OF THIS TRACK
         SPACE 2
OVERNXT  SR    R1,R1              RETURN FROM TRACK OVERFLOW READ
         MVC   CURMBB,IOBSEEK     CHANGE CURRENT MBBCCHHR TO NEXT TRACK
         ICM   R1,3,DATALN        DATA LENGTH OF R =1
         MVC   DATALN(2),LSBLK    RESTORE NEXT LENGTH
         MVI   IOBSEEK+7,2        RESET R OF NEXT CCHHR BACK TO 2
         ST    R1,LSTRK           DATA LENGTH OF R =1
         AH    R1,LSBLK+2         ADDED TO PREVIOUS PART
         ST    R1,LSBLK           FIX BLOCKSIZE
         EJECT
OVERTRK  LR    R0,R1              SAVE FOR LATER
         C     R1,MINBLK
         BH    *+8
         ST    R1,MINBLK          MINIMUM LENGTH BLOCK READ
         C     R1,MAXBLK
         BL    *+8
         ST    R1,MAXBLK          MAXIMUM LENGTH BLOCK READ
         A     R1,BYTECNT
         ST    R1,BYTECNT         TOTAL NUMBER OF BYTES READ
         L     R1,READTOTI    *** FOR SUMMARY BY FILE INFORMATION
         A     R1,=F'1'
         ST    R1,READTOTI        COUNT OF BLOCKS READ
         LR    R1,R0              CURRENT BLKSIZE
         C     R1,MINBLKI
         BH    *+8
         ST    R1,MINBLKI         MINIMUM LENGTH BLOCK READ
         C     R1,MAXBLKI
         BL    *+8
         ST    R1,MAXBLKI         MAXIMUM LENGTH BLOCK READ
         A     R1,BYTECNTI
         ST    R1,BYTECNTI        TOTAL NUMBER OF BYTES READ
         LR    R15,R0             CURRENT BLKSIZE
         SR    R14,R14
         D     R14,=F'500'        SEPARATE BY INTERVALS OF 500
         SLL   R15,2              RESULT *4
         L     R14,BLKDIST(R15)
         A     R14,=F'1'          ADD 1 TO PROPER SLOT
         ST    R14,BLKDIST(R15)
         TM    FLAGS,DIRECTOR     IN THE DIRECTORY?
         BO    NOBLKCHK           YES, BRANCH
         CH    R0,DS1BLKL         RECORD SIZE EXCEEDING BLKSIZE?
         BNH   DIVIDECK           NO, BRANCH
         L     R14,EXCEEDBL       RECORD SIZE
         A     R14,=F'1'                     EXCEEDED
         ST    R14,EXCEEDBL                          BLKSIZE
         SPACE 1
DIVIDECK SR    R14,R14
         L     R15,=F'1'          ASSUME COUNTING RECFM=U LRECL'S
         TM    DS1RECFM,X'C0'     RECFM=U OR NO RECFM?
         BNM   LRECLCNT           YES, BRANCH
         TM    DS1RECFM,X'40'     RECFM=V?
         BO    CHECKV             YES, BRANCH
         SR    R14,R14            MUST BE RECFM=F
         LR    R15,R0             CURRENT BLKSIZE
         LH    R1,DS1LRECL        LRECL FROM THE LABEL
         LTR   R1,R1              ZERO?
         BZ    DIVIDECT           YES, BRANCH
         DR    R14,R1             DIVIDE
         EJECT
LRECLCNT A     R15,TOTLRECL       COUNT
         ST    R15,TOTLRECL            LOGICAL RECORDS
         LTR   R14,R14            DIVIDE EVENLY?
         BZ    NOBLKCHK           YES, BRANCH
         SPACE 2
DIVIDECT L     R14,DIVIDEBL       RECORD SIZE
         A     R14,=F'1'                     NOT DIVISIBLE
         ST    R14,DIVIDEBL                               BY LRECL
         B     NOBLKCHK           DONE                    BY LRECL
         SPACE 2
CHECKV   CLC   BUFFER+2(2),=H'0'  VALID BLOCK HEADER?
         BNE   BADBLOCK           NO, BRANCH
         CH    R0,BUFFER          LENGTH EQUAL TO READ LENGTH?
         BE    CHECKV1            YES, BRANCH
BADBLOCK L     R1,INVBLOCK
         A     R1,=F'1'
         ST    R1,INVBLOCK        UPDATE BAD RECFM=V BLOCK FORMAT COUNT
         B     NOBLKCHK
         SPACE 1
CHECKV1  LR    R1,R0              BUFFER LENGTH
         LA    R14,BUFFER+4       START OF LOGICAL RECORDS
         AR    R1,R14             END OF BUFFER ADDRESS +4
         SH    R1,=H'5'           END OF BUFFER ADDRESS -1
         SPACE 1
CHECKV2  SR    R0,R0
         ICM   R0,3,0(R14)        GET LOGICAL RECORD LENGTH
         L     R15,ALLLRECL       TOTAL LOGICAL RECORD LENGTHS
         AR    R15,R0             ADD THIS ONE
         ST    R15,ALLLRECL       UPDATE
         L     R15,TOTLRECL       ANOTHER LOGICAL RECORD
         A     R15,=F'1'
         ST    R15,TOTLRECL       UPDATE LRECL COUNT
         L     R15,OVRLRECL       ASSUME LONGER THAN THE LRECL
         A     R15,=F'1'
         CH    R0,DS1LRECL              EXCEED THE DATA SET LRECL?
         BNH   *+8                      NO, BRANCH
         ST    R15,OVRLRECL             YES, INCREMENT INVALID COUNT
         C     R0,MINLRECL
         BH    *+8
         ST    R0,MINLRECL              MINIMUM LOGICAL RECORD LENGTH
         C     R0,MAXLRECL
         BL    *+8
         ST    R0,MAXLRECL              MAXIMUM LOGICAL RECORD LENGTH
         LTR   R0,R0                    ACTUAL RECORD?
         BP    *+8                      YES, BRANCH
         LA    R0,4                     NO, USE THE HEADER ONLY
         BXLE  R14,R0,CHECKV2           SKIP TO NEXT RECORD
         L     R0,LSBLK                 RESET R0
         EJECT
NOBLKCHK SR    R15,R15            RESET RETURN CODE
CHECKTRK CLC   MBBCCHH(7),CURMBB  SAME MBBCCHH?
         BNE   NEWTRACK           NO, BRANCH
         A     R0,CURBYTE
         ST    R0,CURBYTE         ACCUMULATE TRACK BYTE COUNT
         L     R1,LASTR
         A     R1,=F'1'
         ST    R1,LASTR           ACCUMULATE RECORD COUNT/TRACK
         B     COMPRESS
         SPACE 2
NEWTRACK L     R1,TRACKNUM        SWITCH TRACKS --
         A     R1,=F'1'
         ST    R1,TRACKNUM        ACTUAL TRACKS USED
         SPACE 2
TRKNOINC MVC   MBBCCHH(7),CURMBB  SAVE CURRENT TRACK NUMBER
         L     R0,CURBYTE
         MVC   CURBYTE,LSTRK
         L     R1,TRKBYTE
         AR    R1,R0
         ST    R1,TRKBYTE         COUNT OF ALL CHARACTERS READ
         L     R1,LASTR
         MVC   LASTR,=F'1'
         L     R14,ALLR
         AR    R14,R1
         ST    R14,ALLR           COUNT OF ALL READS
         LTR   R1,R1              FIRST TRACK?
         BZ    NOMINS             YES, BRANCH
         C     R0,MINBYTE
         BNL   *+8
         ST    R0,MINBYTE         MIN BYTES/TRACK
         C     R1,MINR
         BNL   *+8
         ST    R1,MINR            MIN RECORD NUMBER/TRACK
NOMINS   C     R0,MAXBYTE
         BNH   *+8
         ST    R0,MAXBYTE         MAX BYTES/TRACK
         C     R1,MAXR
         BNH   *+8
         ST    R1,MAXR            MAX RECORD NUMBER/TRACK
         EJECT
COMPRESS TM    DS1DSORG,DS1DSGPO  PARTITIONED DATA SET?
         BNO   RETURNS            NO, BRANCH
         TM    DS1RECFM,DCBRECTO  TRACK OVERFLOW DATA SET?
         BO    RETURNS            YES, BRANCH
         SPACE 1
         TM    FLAGS,REALONE+DIRECTOR  REAL MEMBER OR DIRECTORY ENTRY?
         BZ    RETURNS                 NO, GAS MEMBER
         CLC   CURMBB,LASTMBB     PAST THE DS1LSTAR MARKER?
         BH    RETURNS            YES, CANNOT COMPRESS ANY MORE
         ST    R15,TEMP           SAVE RETURN CODE FOR LATER
         SPACE 3
COMPREC  L     R0,COMPR
         A     R0,=F'1'           CURRENT COMPRESS RECORDS ON TRACK
         ST    R0,COMPR
         L     R0,LSBLK           LENGTH OF RECORD IN RKDD (DD)
         SPACE 1
         TM    FLAGS,DIRECTOR     IN THE DIRECTORY?
         BNO   COMPCALL           NO, BRANCH (KEYLENGTH IS ZERO)
         OC    LSBLK,LSBLK        END OF FILE?
         BNZ   COMPADD8           NO, BRANCH
         NI    FLAGS,X'FF'-DIRECTOR  TURN OFF THE "IN DIRECTORY" FLAG
         CLC   COMPTRK,TRACKNUM   SAME TRACK NUMBER?
         BNE   COMPSWCH           NO, BRANCH
         B     COMPCALL           YES, GET THE RECORD NUMBER
         SPACE 1
COMPADD8 ICM   R0,B'1100',=H'8'   KEYLENGTH IN RKDD (K)
COMPCALL ICM   R0,B'1000',COMPR+3 RECORD NUMBER IN RKDD (R)
         SPACE 2
         TRKCALC FUNCTN=TRKBAL,TYPE=DEVTYPE+3,REGSAVE=YES,             X
               BALANCE=COMPTRBL+2,RKDD=(0)
         SPACE 2
         LTR   R15,R15            FIT ON CURRENT TRACK?
         BZ    COMPADD            YES, BRANCH
         SPACE 2
COMPSWCH L     R1,COMPTRK
         A     R1,=F'1'           START A NEW TRACK IN OUTPUT
         ST    R1,COMPTRK
         MVI   COMPR+3,0          RECORD ZERO
         B     COMPREC            TRY AGAIN
         SPACE 1
COMPADD  ST    R0,COMPTRBL        SAVE THE TRACK BALANCE
         L     R15,TEMP           RESTORE THE RETURN CODE
         SPACE 2
RETURNS  L     R14,STATR14
         BR    R14
         EJECT
*
*   MEMBER INPUT ROUTINE
*
MEMBERS  ST    R14,MEMBR14
         LM    R15,R1,DIRPTRS     PICK UP ADDR, INCR, LIMIT
         LTR   R1,R1              INITIALIZED?
         BNZ   DEBLOCK            YES, BRANCH
         BAL   R14,REREAD         OPEN IN FOR INPUT OF THE DIRECTORY
GETBLK   BAL   R14,EXCPSTAT       GET A DIRECTORY BLOCK
         L     R14,CURENTRY       ENTRIES IN PREVIOUS BLOCK
         C     R14,MINENTRY       LESS THAN MINIMUM?
         BNL   *+8                NO, BRANCH
         ST    R14,MINENTRY       YES, NEW MINIMUM
         LTR   R15,R15            END OF FILE?
         BP    MBRDONE            YES, END OF FILE INDICATION
         CLC   LSBLK+2(2),=H'256' GOOD DIRECTORY BLOCK?
         BNE   NODIRBLK           NO, BRANCH
         LA    R0,2               INCREMENT IS 2 FIRST
         LH    R1,BUFFER          LENGTH HALFWORD
         LA    R1,BUFFER-1(R1)    LIMIT
         LA    R15,BUFFER         START
         XC    CURENTRY,CURENTRY  CLEAR THE ENTRY COUNTER
DEBLOCK  BXH   R15,R0,GETBLK
         IC    R14,11(,R15)
         N     R14,=XL4'0000001F' GET LENGTH BITS
         LA    R0,12(R14,R14)     LENGTH * 2  +  12
         STM   R15,R1,DIRPTRS     SAVE FOR LATER
         CLI   0(R15),X'FF'       LAST MEMBER
         BNE   MEMNAME            NO, BRANCH
         LA    R15,8              LAST MEMBER USED INDICATION
         L     R14,CURENTRY       ENTRIES IN THIS BLOCK
         C     R14,MINENTRY       LESS THAN MINIMUM?
         BNL   *+8                NO, BRANCH
         ST    R14,MINENTRY       YES, NEW MINIMUM
         B     MBRDONE
         SPACE 1
MEMNAME  LR    R1,R15             ADDRESS OF MEMBER NAME (RETURNED)
         SR    R15,R15            GOOD READ INDICATION
         L     R14,TOTENTRY       ENTRY COUNT
         A     R14,=F'1'
         ST    R14,TOTENTRY
         L     R14,CURENTRY       CURRENT ENTRY IN THIS BLOCK
         A     R14,=F'1'
         ST    R14,CURENTRY
         C     R14,MAXENTRY       ABOVE PREVIOUS MAXIMUM?
         BNH   MBRDONE            NO, BRANCH
         ST    R14,MAXENTRY       YES, NEW MAXIMUM
         B     MBRDONE
         SPACE 1
NODIRBLK OUTSTR EOFSIMS,ERR=ERR   RECORD LENGTH IS NOT 256
         OUTPT LSBLK,BLOCKLEN,ERR=ERR  CURRENT LRECL
         LA    R15,12             ERROR INDICATION
MBRDONE  L     R14,MEMBR14        RETURN ADDRESS
         BR    R14
         EJECT
*   EXCP SUBROUTINES  (OPENIN, REREAD AND EXCP)
*
OPENIN   ST    R14,EXCPR14                  OPEN THE INPUT FILE
         RDJFCB (IN)                        GET DSN AND VOLUME SERIAL
         LTR   R15,R15                      SUCCESSFUL?
         BZ    NOABE40                      YES, BRANCH
ABE40    OUTSTR NOTOPENR,ERR=ERR
         ABEND 40                  ** RDJFCB FAILED **
ABE41    OUTSTR NOTOPENB,ERR=ERR
         ABEND 41                  ** OBTAIN FAILED **
ABE42    OUTSTR NOTOPENO,ERR=ERR
         ABEND 42                  ** OPEN FAILED **
NOABE40  OBTAIN MYLABEL                     READ IN THE FORMAT 1 DSCB
         LTR   R15,R15                      SUCCESSFUL?
         BNZ   ABE41                        NO, ERROR
         OPEN  (IN,INPUT)
         TM    DCBOFLGS,X'10'               IN OPEN?
         BZ    ABE42                        NO, ERROR
         L     R14,EXCPR14
*
*   ENTRY TO REREAD THE OPEN DATA SET (DDNAME IS IN)
*
REREAD   ST    R14,EXCPR14
         L     R1,DCBDEBAD
         MVC   NUMEXT+3(1),16(R1)           NUMBER OF EXTENTS
         LA    R14,16(R1)                   CURRENT EXTENT POINTER - 16
         ST    R14,CUREXT                   SAVE FOR LATER
         MVI   IOBSEEK,X'00'                RESET THE EXTENT NUMBER
         LA    R15,4                        EARLY EXIT FLAG
         L     R1,32(,R1)                   UCB ADDRESS (FIRST EXTENT)
         CLI   19(R1),X'08'                 2314 DISK DRIVE?
         BNE   FIRSTONE                     NO, BRANCH
         LA    R1,CCW1                      FIRST CCW IS
         ST    R1,STARTCCW                              SEARCH ID EQUAL
         NI    CCW4+4,X'FF'-X'40'           TURN OFF COMMAND CHAINING
         B     FIRSTONE                     DO THE INITIAL EXTENT
*
*
*    FOR EACH EXTENT, INITIALIZE CCHHR, SECTOR NUMBER AND READ IN THE
*        LENGTH OF THE FIRST RECORD.
*
EACHXTNT SR    R1,R1              AFTER THE FIRST EXTENT --
         IC    R1,IOBSEEK
         A     R1,=F'1'
         STC   R1,IOBSEEK         INCREMENT THE EXTENT NUMBER
         C     R1,NUMEXT          PAST LAST EXTENT?
         BNL   EOF$DS             YES, NO END OF DATA SET RECORDED
         EJECT
FIRSTONE ST    R15,EXCPR15                  SAVE ENTRY CODE FOR LATER
         L     R1,CUREXT
         LA    R1,16(R1)                    CURRENT EXTENT ENTRY
         MVC   IOBSEEK+3(4),6(R1)           INITIAL CCHH
         ST    R1,CUREXT                    UPDATE FOR LATER
         MVI   IOBSEEK+7,0                  WANT LENGTH OF RECORD 1
         MVI   SNO,0                        SET SECTOR NUMBER=ZERO
         SPACE 1
INITREAD LA    1,CCW1A                      INITIALIZATION
         ST    1,CPADDR                                   CCW
         EXCP  IOB
         WAIT  ECB=ECB
         CLI   ECB,X'7F'                    GOOD READ?
         BE    NOABE30                      YES, BRANCH
         OUTSTR FIRSTRD,ERR=ERR
         ABEND 30          **  FIRST READ OF EXTENT DID NOT WORK  **
NOABE30  MVC   CPADDR,STARTCCW              RESET THE CCW ADDRESS
         L     R15,EXCPR15
         L     R14,EXCPR14                  RETURN ADDRESS FOR OPENIN,
*                                           REREAD OR EOF (NEW EXTENT)
         LTR   R15,R15                      EARLY EXIT DESIRED?
         BNZR  R14                          YES, RETURN
*
*   ENTRY FOR EACH EXCP TO BE PERFORMED
*
EXCP     ST    R14,EXCPR14
         CLI   ECB,X'42'                    EXTENT VIOLATION LAST TIME?
         BE    EACHXTNT                     YES, DO THE NEXT EXTENT
         MVC   CURMBB(8),IOBSEEK  SAVE THE DISK ADDRESS OF THIS RECORD
         MVC   LSBLK+2(2),DATALN  SAVE THE BLOCK LENGTH FOR THIS READ
         EXCP  IOB
         WAIT  ECB=ECB
         SR    R15,R15            GOOD READ FLAG
         CLI   ECB,X'42'          EXTENT VIOLATION (NEXT TIME)?
         BE    ENDEXCP            YES, QUIT
         TM    FLAGS,ALLXTNT      READ ALL EXTENTS?
         BO    EXCP7F             YES, IGNORE DS1LSTAR FOR DATA SET
         TM    DS1DSORG+1,DS1ACBM VSAM DATA SET?
         BO    EXCP7F             YES, IGNORE DS1LSTAR CHECK FOR VSAM
         TM    DS1DSORG,DS1DSGIS  ISAM DATA SET?
         BO    EXCP7F             YES, IGNORE DS1LSTAR CHECK FOR ISAM
         CLC   CURMBB,LASTMBB     IN THE CURRENT DATA SET?
         BNL   EOF$DS             NO, END OF DATA SET
EXCP7F   CLI   ECB,X'7F'          GOOD READ?
         BE    ENDEXCP            YES, QUIT
         SPACE 1
         CLI   ECB,X'41'          PERMANENT ERROR?
         BE    NOABE10            YES, BRANCH
         OUTSTR BLANKS
         OUTSTR BADERROR,ERR=ERR
         B     SOMERR         **  BAD ERROR; ECB NOT HEX 41, 42 OR 7F
         EJECT
NOABE10  TM    CSW+4,X'01'        ACTUALLY EOF?
         BO    EOF
         CLC   CSW+4(4),=X'00200000'  NULL MEMBER OR DATA SET?
         BE    EOF                    YES, BRANCH
         OUTSTR BLANKS
         OUTSTR PERMERR,ERR=ERR
SOMERR   LA    R15,12         **  PERMANENT ERROR--NOT END OF FILE  **
         XC    LSBLK,LSBLK                 NO CHARACTERS READ
         B     REISSUE
*
EOF      TM    FLAGS,ALLXTNT               READ ALL EXTENTS?
         BO    EOF2                        YES, NO END OF FILE CHECK
         TM    DS1DSORG,DS1DSGPS+DS1DSGDA  SEQUENTIAL OR DIRECT?
         BM    EOF$DS                      YES, END DATA SET
         TM    DS1DSORG+1,DS1ACBM          VSAM DATA SET?
         BO    EOF$DS                      YES, END OF DATA SET
         SPACE 1
EOF2     LA    R15,4              END OF MEMBER FLAG
         CLC   =H'0',DATALN       NEXT BLOCK LENGTH = 0?
         BNE   ENDEXCP            NO, BRANCH
REISSUE  ST    R15,EXCPR15
         MVC   SAVECB(1),ECB      SAVE ECB CODE FOR LATER
         LA    R1,CCW1A           NEED TO REISSUE THE INITIALIZATION
         ST    R1,CPADDR            EXCP TO GET LENGTH AND ADDRESS
         EXCP  IOB                  OF THE NEXT BLOCK
         WAIT  ECB=ECB
         L     R15,EXCPR15        READ ERROR (12) OR END OF MEMBER (4)
         MVC   CPADDR,STARTCCW    RESET THE CCW ADDRESS
         CLI   ECB,X'42'          EXTENT VIOLATION?
         BE    EACHXTNT           YES, DO THE NEXT EXTENT -- EARLY EXIT
         CLI   ECB,X'7F'          GOOD READ?
         BE    ENDEXCP            YES, QUIT
         CLI   ECB,X'41'          PERMANENT ERROR?
         BE    NOABE70            YES, CHECK EOF
ABE70    OUTSTR BLANKS            SOME UNDEFINED READ ERROR
         OUTSTR BADERROR,ERR=ERR
         HEXES SAVECB,1,IOERROR+16    ECB ERROR CODE
         HEXES CURMBB+3,5,IOERROR+26  CURRENT DISK CCHHR
         MBBTTR CURMBB,TTR=TEMP       CONVERT TO TTR
         HEXES TEMP,3,IOERROR+49      CURRENT DISK TTR
         OUTSTR IOERROR,ERR=ERR
         ABEND 70             **  SOME OTHER BAD ERROR  **
NOABE70  TM    CSW+4,X'01'        NULL MEMBER (ANOTHER EOF)?
         BO    ENDEXCP
ABE80    OUTSTR BLANKS            PERMANENT I/O ERROR
         OUTSTR PERMERR,ERR=ERR
         HEXES SAVECB,1,IOERROR+16    ECB ERROR CODE
         HEXES CURMBB+3,5,IOERROR+26  CURRENT DISK CCHHR
         MBBTTR CURMBB,TTR=TEMP       CONVERT TO TTR
         HEXES TEMP,3,IOERROR+49      CURRENT DISK TTR
         OUTSTR IOERROR,ERR=ERR
         ABEND 80             **  PERMANENT ERROR AFTER EOF  **
EOF$DS   LA    R15,8              END OF FILE AND DATA SET FLAG
ENDEXCP  L     R14,EXCPR14        RETURN ADDRESS
         BR    R14
         EJECT
* OUTPUT ROUTINES:
*   A. OUTRTNS -- OUTPUTS A STRING ONLY
*   B. OUTERRS -- OUTPUTS A STRING ONLY PRECEEDED BY ***
*   C. OUTRTN  -- OUTPUTS A NUMERIC VALUE AND A STRING
*   D. OUTERR  -- OUTPUTS A NUMERIC VALUE AND A STRING PRECEEDED BY ***
*
*      NOTE: R0 NUMBER TO OUTPUT
*            R1 CONTAINS THE ADDRESS OF THE STRING TO OUTPUT
*            R14 CONTAINS THE RETURN ADDRESS
*            R15 CONTAINS THE LENGTH OF THE STRING
         SPACE 2
OUTRTNS  TM    FLAGS,ERRONLY          ERROR MESSAGES ONLY?
         BOR   R14                    YES, RETURN
         MVI   ERRORIND,C'N'          NOT AN ERROR MESSAGE
         SPACE 1
OUTERRS  STM   R14,R1,OUTR14R1        SAVE R14 THROUGH R1
         B     OUTSTMT                BRANCH INTO MAIN ROUTINE
         SPACE 2
OUTRTN   TM    FLAGS,ERRONLY          ERROR MESSAGES ONLY?
         BOR   R14                    YES, RETURN
         MVI   ERRORIND,C'N'          NOT AN ERROR MESSAGE
         SPACE 1
OUTERR   STM   R14,R1,OUTR14R1        SAVE R14 THROUGH R1
         CVD   R0,DOUBLE
         MVC   OUTLINE+8(11),=X'2020206B2020206B202120'
         ED    OUTLINE+7(12),DOUBLE+3 OUTPUT UP TO 9 NUMERIC DIGITS
OUTSTMT  BCTR  R15,0                  MACHINE LENGTH
         MVC   OUTLINE+20(*-*),0(R1)  <<EXECUTED>>
         EX    R15,*-6                MOVE IN THE STRING
         LA    R15,OUTLINE+17
         SPACE 1
OUTSCAN  S     R15,=F'1'              SCAN BACKWARDS
         CLC   0(4,R15),=CL4' '                     FOR FOUR
         BNE   OUTSCAN                                      BLANKS
         SPACE 1
         LA    R0,4(,R15)             POINT TO FIRST NON-BLANK
         CLI   ERRORIND,C'*'          ERROR MESSAGE?
         BNE   OUTRESET               NO, BRANCH
         MVC   0(3,R15),=C'***'       ERROR IDENTIFIER
         LR    R0,R15                 POINT TO FIRST NON-BLANK
         MVC   RETURNCD+2(2),=H'4095' RETURN CODE=4095
OUTRESET MVI   ERRORIND,C'*'              ASSUME AN ERROR NEXT TIME
         TM    DCBOFLGS-IHADCB+OUT,X'10'  OUT OPEN?
         BO    OUTOPEN                    YES, BRANCH TO PUT
         ST    R0,OUTR14R1+4              SAVE R0 IN R15 SLOT
         OPEN  (OUT,OUTPUT)               OPEN
         L     R0,OUTR14R1+4              RESTORE R0
         TM    DCBOFLGS-IHADCB+OUT,X'10'  OPEN NOW?
         BO    OUTOPEN                    YES, BRANCH
         WTO   'DDNAME OUT COULD NOT BE OPENED',ROUTCDE=(11)
         ABEND 101           ** OUTPUT FILE DID NOT OPEN  **
         EJECT
OUTOPEN  TR    OUTLINE+20(80),TRLINE  TRANSLATE UNPRINTABLES TO PERIODS
         TM    DCBRECFM-IHADCB+OUT,X'04'  RECFM=..A?
         BNO   *+8                        NO, BRANCH
         S     R0,=F'1'                   YES, USE A BLANK ASA BYTE
         PUT   OUT,(0)                OUTPUT THE LINE
         MVC   OUTLINE+1(159),OUTLINE BLANK IT
         LM    R14,R1,OUTR14R1        RESTORE REGISTERS
         CLC   7(7,R1),=C'AVERAGE'    AVERAGE IN TEXT?
         BNE   *+10                   NO, BRANCH
         MVC   7(7,R1),=C'MAXIMUM'    YES, MOVE IN MAXIMUM
         CLC   7(7,R1),=C'MINIMUM'    MINIMUM IN TEXT?
         BNE   *+10                   NO, BRANCH
         MVC   7(7,R1),=C'AVERAGE'    YES, MOVE IN AVERAGE
         BR    R14
         SPACE 3
OUTEXIT  LH    R15,DCBLRECL-IHADCB+OUT
         CH    R15,=H'133'                ABOVE MAXIMUM ALLOWED?
         BH    DCBBAD                     YES, BRANCH
         TM    DCBRECFM-IHADCB+OUT,X'FF'  ANY RECFM?
         BM    *+8                        YES, BRANCH
         MVI   DCBRECFM-IHADCB+OUT,X'90'  RECFM=FB IS THE DEFAULT
         TM    DCBRECFM-IHADCB+OUT,X'C0'  RECFM=U?
         BO    OUTEXIT2                   YES, THAT IS OK
         TM    DCBRECFM-IHADCB+OUT,X'40'  RECFM=V?
         BO    DCBBAD                     YES, ERROR
OUTEXIT2 TM    DCBRECFM-IHADCB+OUT,X'02'  RECFM=..M?
         BO    DCBBAD                     YES, ERROR
         LTR   R15,R15                    ANY LRECL?
         BNZ   *+8                        YES, USE IT
         LH    R15,=H'80'                 LRECL=80 IS THE DEFAULT
         LH    R1,DCBBLKSI-IHADCB+OUT     BLOCKSIZE
         LTR   R1,R1                      BLKSIZE SPECIFIED?
         BP    *+8                        YES, BRANCH
         LH    R1,=H'6233'                DEFAULT BLKSIZE=6233
         SR    R0,R0
         DR    R0,R15                     COMPUTE BLOCKING FACTOR
         LTR   R1,R1                      LRECL > BLKSIZE?
         BP    *+8                        NO, BRANCH
         L     R1,=F'1'                   YES, USE BLKSIZE=LRECL
         MR    R0,R15
         STH   R1,DCBBLKSI-IHADCB+OUT     UPDATE BLKSIZE
         STH   R15,DCBLRECL-IHADCB+OUT    UPDATE LRECL
         BR    R14
         SPACE 2
DCBBAD   WTO   'DDNAME OUT HAS AN INVALID DCB',ROUTCDE=(11)
         ABEND 102           ** OUTPUT FILE DID NOT OPEN  **
         EJECT
* DATE CONVERT ROUTINES:
*  A. CONVLABL CONVERTS A DATE FROM BYBDDD (WHERE BY IS BINARY YEAR
*              AND BDDD IS BINARY DATE) TO YYDDDF FORMAT
*  B. CONVDATE EXPECTS A DATE IN YYDDDF FORMAT
*
*      NOTE: R14 CONTAINS THE RETURN ADDRESS
*            R1 ON EXIT POINTS TO RESULT IN MM/DD/YY FORMAT
         SPACE 2
CONVLABL ST    R14,CONVR14
         SR    R15,R15               CLEAR R15
         ICM   R15,7,0(R1)           ZERO HIGH BYTE
         ST    R15,CONVWORK          SAVE FOR LATER
         SRL   R15,16                SHIFT FOR YEAR ONLY
         MH    R15,=H'1000'          SHIFT FOR ADD
         AH    R15,CONVWORK+2        GET DDDD
         CVD   R15,CONVWORK          IN PACKED FORM
         MVC   CONVEDIT(4),CONVWORK+4  IN 00YYDDDF FORMAT NOW
         LA    R1,CONVEDIT+1         GET YEAR IN BINARY
         L     R14,CONVR14
         SPACE 2
CONVDATE ST    R14,CONVR14
         LA    R15,CONVEDIT+9
         XC    CONVWORK,CONVWORK
         SR    R14,R14               SETUP FOR LEAP YEAR TEST
         IC    R14,0(,R1)
         SLL   R14,4
         ST    R14,CONVWORK+4
         OI    CONVWORK+7,X'0F'
         LR    R14,R1
         CVB   R0,CONVWORK           GET YEAR IN BINARY
         SR    R1,R1
         SRDL  R0,2                SHIFT OFF REMAINDER OF YEAR/4
         SPACE 1
         MVI   DAYTABLE+1,28       FEB USUALLY HAS 28 DAYS
         LTR   R1,R1               THIS A LEAP YEAR?
         BNZ   *+8                 NO, BRANCH
         MVI   DAYTABLE+1,29       YES, FEB HAS 29 DAYS
         SPACE 1
         MVC   2(1,R15),0(R14)     YEAR RECORD CREATED
         MVC   CONVWORK+6(2),1(R14)
         CLI   CONVWORK+7,0        BAD DATE?
         BE    DAYSKIP             YES, DON'T 0C7
         CVB   R1,CONVWORK         GET RELATIVE DAYS IN BINARY
         LA    R14,DAYTABLE
         SR    R0,R0
         SPACE 1
DAYLOOP  IC    R0,0(R14)           DAYS THIS MONTH
         SR    R1,R0               WITHIN THIS MONTH?
         BNP   DAYNOW
         A     R14,=F'1'
         B     DAYLOOP
         SPACE 3
DAYNOW   AR    R1,R0               GET DAY OF THE MONTH
         LA    R0,10
         MR    R0,R0
         CVD   R1,CONVWORK
         MVC   1(1,R15),CONVWORK+6
         LTR   R1,R1               DAY=0?
         BZ    MONZERO             YES, USE MONTH=0 TOO
         LA    R1,DAYTABLE-1
         SR    R14,R1
         LA    R1,10
         MR    R0,R14
         CVD   R1,CONVWORK
MONZERO  MVC   0(1,R15),CONVWORK+6
         SPACE 1
DAYSKIP  MVC   CONVEDIT(9),=X'402120612020612020'
         ED    CONVEDIT(9),0(R15)
         LA    R1,CONVEDIT+1
         L     R14,CONVR14
         BR    R14
         SPACE 3
DAYTABLE DC    AL1(31,28,31,30,31,30,31,31,30,31,30,31)
         SPACE 2
CONVR14  DC    F'0'
CONVEDIT DC    CL16' '
CONVWORK DC    D'0'
         EJECT
CCW1A    CCW   X'31',IOBSEEK+3,X'60',5      SEARCH ID EQUAL (CCHHR)
CCW2A    CCW   X'08',*-8,X'60',1            TIC
CCW3A    CCW   X'92',IOBSEEK+3,X'20',8      MT READ NEXT COUNT
         SPACE 1
CCW0     CCW   X'23',SNO,X'60',1            SET SECTOR
CCW1     CCW   X'31',IOBSEEK+3,X'60',5      SEARCH ID EQUAL (CCHHR)
CCW2     CCW   X'08',*-8,X'60',1            TIC
CCW3     CCW   X'06',BUFFER,X'60',32760     READ DATA
CCW4     CCW   X'92',IOBSEEK+3,X'60',8      MT READ NEXT COUNT
CCW5     CCW   X'22',SNO,X'20',1            READ SECTOR NUMBER
         SPACE 1
IOB      DC    X'C2000000'    IOB FLAGS
         DC    A(ECB)         ECB ADDRESS
CSW      DC    2A(0)          CHANNEL STATUS
CPADDR   DC    A(CCW1)        CCW CHAIN ADDRESS (CCW1A, CCW0 OR CCW1)
         DC    A(IN),2A(0)    DCB ADDRESS, 8 FILL CHARACTERS
IOBSEEK  DC    2A(0)          NEXT MBBCCHHR ADDRESS
KEYLN    DS    X              NEXT KEYLENGTH
DATALN   DS    2X             NEXT RECORD LENGTH
SNO      DC    X'00'          NEXT SECTOR NUMBER
*
CURMBB   DC    CL8' '         CURRENT MBBCCHHR ADDRESS
MBBCCHH  DC    CL7' '         SAVE AREA FOR MBBCCHH
         SPACE 1
ERRORIND DC    C'*'           ERROR INDICATION FOR OUTPUT ROUTINES
INDD     DC    CL8'IN'        IN DDNAME FOR DEVTYPE MACRO
         SPACE 2
BLDLPARM DC    H'1',H'12'     ONE MEMBER NAME, 12 BYTES OF INFORMATION
BLDLMEM  DC    CL8' '         MEMBER NAME
BLDLTTR  DC    XL4'00'        TTR OF THIS MEMBER
         SPACE 2
HEXTBL   DC    CL16'0123456789ABCDEF'
SAVECB   DC    X'00'                      SAVE AREA FOR ECB
         SPACE 3
FLAGS    DC    X'01'              *** FLAG BYTE
GAS      EQU   X'01'                      GAS RECORDS DESIRED (DEFAULT)
LABELS   EQU   X'02'                      LABELS ONLY (NO DATA READ)
ALLXTNT  EQU   X'04'                      READ ALL EXTENTS OF DATA SET
ERRONLY  EQU   X'08'                      OUTPUT ONLY ERROR MESSAGES
DORECFMU EQU   X'10'                      OUTPUT A MESSAGE FOR RECFM=U
REALONE  EQU   X'20'                      CURRENTLY IN A REAL MEMBER
DIRECTOR EQU   X'40'                      CURRENTLY IN THE DIRECTORY
         EJECT
SAVE     DC    18F'0'                    SAVE AREA
LASTR    DC    F'0'                      CURRENT NUMBER OF RECORDS/TRK
LASTREF  DC    F'0'                      LAST MEMBER TABLE REFERENCE
EXCPR14  DC    F'0'                      RETURN REGISTER FOR EXCP
EXCPR15  DC    F'0'                      EXIT FLAG REGISTER SAVEAREA
STATR14  DC    F'0'                      RETURN REGISTER FOR EXCPSTAT
CUREXT   DC    F'0'                      ADDRESS OF THE CURRENT EXTENT
NUMEXT   DC    F'0'                      TOTAL EXTENTS IN THE DATA SET
         SPACE 2
READTOT  DC    F'0'                      TOTAL RECORDS READ
MINBLK   DC    F'33000'                  MINIMUM LENGTH BLOCK
MAXBLK   DC    F'0'                      MAXIMUM LENGTH BLOCK
BYTECNT  DC    F'0'                      TOTAL BYTES READ
READTOTI DC    F'0'                      TOTAL RECORDS READ (ISAM)
MINBLKI  DC    F'33000'                  MINIMUM LENGTH BLOCK (ISAM)
MAXBLKI  DC    F'0'                      MAXIMUM LENGTH BLOCK (ISAM)
BYTECNTI DC    F'0'                      TOTAL BYTES READ (ISAM)
         SPACE 2
TEMP     DC    F'0'                      WORK STORAGE
GASBYTE  DC    F'0'                      TOTAL BYTES IN GAS MEMBERS
RELBYTE  DC    F'0'                      TOTAL BYTES IN REAL MEMBERS
MINGAS   DC    X'7F000000'               MIN BYTES IN GAS MEMBERS
MAXGAS   DC    F'0'                      MAX BYTES IN GAS MEMBERS
MINREL   DC    X'7F000000'               MIN BYTES IN REAL MEMBERS
MAXREL   DC    F'0'                      MAX BYTES IN REAL MEMBERS
SAVETOT  DC    F'0'                      STARTING BYTE COUNT HOLD
         SPACE 2
MAXR     DC    F'0'                      MAXIMUM RECORDS/TRACK
MINR     DC    F'255'                    MINIMUM RECORDS/TRACK
TRKBYTE  DC    F'0'                      SUM OF ALL BLOCKSIZES
INAROW   DC    F'0'                      NUMBER OF CONSECUTIVE ERRORS
TOTALIO  DC    F'0'                      TOTAL RECOVERED I/O ERRORS
ALLR     DC    F'0'                      SUM OF ALL RECORDS READ
CURBYTE  DC    F'0'                      CURRENT TRACK BYTE COUNT
MAXBYTE  DC    F'0'                      MAXIMUM BYTES/TRACK
         SPACE 2
MINBYTE  DC    X'7F000000'               MINIMUM BYTES/TRACK
TRACKNUM DC    F'0'                      ACTUAL COUNT OF TRACKS USED
EOFS     DC    F'0'                      COUNT OF END OF FILES
GASCNT   DC    F'0'                      NUMBER OF GAS MEMBERS
GASRDS   DC    F'0'                      NUMBER OF GAS RECORDS
AVGBTRK  DC    F'0'                      AVERAGE BYTES PER TRACK
AVAILTRK DC    F'0'                      FREE TRACKS
TOTALTRK DC    F'0'                      TOTAL TRACKS
         EJECT
ALIASES  DC    F'0'                      ALIASES IN THE DIRECTORY
REALMBR  DC    F'0'                      REAL ENTRIES (DIRECTORY)
REALENT  DC    F'0'                      MEMBER TABLE ENTRIES
ALLOCBLK DC    F'0'                      DIRECTORY BLOCKS ALLOCATED
USEDBLK  DC    F'0'                      DIRECTORY BLOCKS USED
TRACKDIR DC    F'0'                      TRACKS USED FOR DIRECTORY
RETURNCD DC    F'0'                      FINAL RETURN CODE
         SPACE 2
DIRPTRS  DC    3A(0)                     MEMBERS -- CURRENT POINTERS
MEMBR14  DC    A(0)                      MEMBERS -- RETURN REGISTER
MINENTRY DC    F'255'                    MEMBERS -- MIN ENTRIES/BLOCK
CURENTRY DC    F'255'                    MEMBERS -- CURRENT ENTRY
MAXENTRY DC    F'0'                      MEMBERS -- MAX ENTRIES/BLOCK
TOTENTRY DC    F'0'                      MEMBERS -- TOTAL ENTRIES
         SPACE 2
COMPR    DC    F'0'                      COMPRESS -- RECORDS ON TRACK
COMPTRK  DC    F'1'                      COMPRESS -- OUTPUT TRACK #
COMPTRBL DC    F'0'                      COMPRESS -- TRACK BALANCE
EXCEEDBL DC    F'0'                      RECORDS WHICH EXCEED BLOCKSIZE
DIVIDEBL DC    F'0'                      BLOCKSIZE/LRECL NOT EVEN
MBRCOUNT DC    F'3000'                   INITIAL MEMBER TABLE SIZE
OUTR14R1 DC    4A(0)                     SAVEAREA FOR OUTPUT ROUTINES
STARTCCW DC    A(CCW0)                   CCW1 FOR 2314 DEVICES
         SPACE 2
ECB      DC    F'0'                      ECB
LSBLK    DC    F'0'                      BLKSIZE OF THE CURRENT RECORD
LSTRK    DC    F'0'                      LENGTH OF THE CURRENT RECORD
SAVETBL  DC    A(0)                      MEMBER TABLE START -16
MINLRECL DC    F'33000'                  MINIMUM LENGTH LRECL
MAXLRECL DC    F'0'                      MAXIMUM LENGTH LRECL
OVRLRECL DC    F'0'                      ACTUAL LRECL EXCEEDS MAXIMUM
TOTLRECL DC    F'0'                      TOTAL LOGICAL RECORDS
         SPACE 2
INVBLOCK DC    F'0'                      TOTAL INVALID V BLOCK HEADERS
ALLLRECL DC    F'0'                      BYTE COUNT OF LOGICAL RECORDS
BLKDIST  DC    65F'0'                    BLKSIZE DISTRIBUTION TABLE
         SPACE 2
DEVAREA  DC    5F'0'                     DEVTYPE RESULT AREA
DEVTYPE  EQU   DEVAREA,4                 DEVTYPE UCBTYP FIELD
DEVMAXB  EQU   DEVAREA+4,4               DEVTYPE MAXIMUM BLKSIZE
DEVCYLS  EQU   DEVAREA+8,2               DEVTYPE TOTAL CYLINDERS
DEVTRKCY EQU   DEVAREA+10,2              DEVTYPE TRACKS/CYLINDER
DEVMAXT  EQU   DEVAREA+12,2              DEVTYPE MAXIMUM TRACK LENGTH
DEVBLKOV EQU   DEVAREA+14,1 (OR 2)       DEVTYPE KEYED BLOCK OVERHEAD
DEVBLNOK EQU   DEVAREA+16,1              DEVTYPE BLOCK OVERHEAD-NO KEY
DEVFLAGS EQU   DEVAREA+17,1              DEVTYPE DEVICE FLAGS
DEVTOL   EQU   DEVAREA+18,2              DEVTYPE TOLERANCE/BLK OVERHEAD
         EJECT
PROGRAM  DC  C'STATS VERSION 2.0  --  DISK DATA SET STATISTICS PROGRAM'
NODSCB2  DC    C'THE ISAM LABEL RECORD COULD NOT BE INPUT'
DSCB2    DC    C'THE FOLLOWING DATA IS FROM THE ISAM LABEL RECORD:'
DSSNOLEV DC    C'INDEX LEVELS PRESENT IN THIS DATA SET'
DSSDVIND DC    C'TRACKS FOR EACH MASTER INDEX'
DSSCYLOV DC    C'TRACKS OF CYLINDER OVERFLOW AREA ON EACH CYLINDER'
DSSTAGDT DC    C'RECORDS TAGGED FOR DELETION'
DSSRORG3 DC    C'OVERFLOW ACCESSES (NOT COUNTING FIRST IN CHAIN)'
         SPACE 2
DSSNOBYT DC    C'BYTES NEEDED TO HOLD THE HIGHEST INDEX IN CORE'
DSSNOTRK DC    C'TRACKS OCCUPIED BY THE HIGHEST-LEVEL INDEX'
DSSPRCTR DC    C'RECORDS IN THE PRIME AREA'
DSSBYOVL DC    C'BYTES LEFT ON CURRENT INDEPENDENT OVERFLOW TRACK'
DSSRORG2 DC    C'TRACKS REMAINING IN THE INDEPENDENT OVERFLOW AREA'
DSSOVRCT DC    C'RECORDS IN THE OVERFLOW AREA'
DSSRORG1 DC    C'CYLINDER OVERFLOW AREAS ARE FULL'
EXCEEDMS DC    C'RECORDS EXCEEDED THE DATA SET BLOCKSIZE'
         SPACE 2
DIVIDEMS DC    C'RECORD BLOCKSIZES ARE NOT EVENLY DIVISIBLE BY THE LRECX
               L'
STATGAS  DC    C'IS THE MINIMUM NUMBER OF BYTES IN A GAS MEMBER'
STATGAST DC    C'IS THE TOTAL NUMBER OF BYTES IN GAS MEMBERS'
STATREAL DC    C'IS THE MINIMUM NUMBER OF BYTES IN A REAL MEMBER'
STATRELT DC    C'IS THE TOTAL NUMBER OF BYTES IN REAL MEMBERS'
STATBLKS DC    C'IS THE MINIMUM BLOCK LENGTH'
STATDIR  DC   C'IS THE MINIMUM NUMBER OF MEMBER ENTRIES IN A DIRECTORY X
               BLOCK'
         SPACE 2
NOREALS  DC    C'NO MEMBERS ARE IN THIS PARTITIONED DATA SET'
NOGASBLK DC    C'NONE OF THESE ARE GAS BLOCKS'
TOTBYTS  DC    C'DATA BYTES ARE IN THIS DATA SET'
TOTTRKS  DC    C'TRACKS WERE READ IN THIS DATA SET'
NUMBLKS  DC    C'DATA BLOCKS ARE IN THIS DATA SET'
GASBLKS  DC    C'OF THESE ARE GAS BLOCKS'
EOFSS    DC    C'END OF FILE'
ENDMBR   DC    C'END OF MEMBER INDICATORS (FILE MARKS) ARE RECORDED'
         SPACE 2
GASMBR   DC    C'OF THESE ARE FOR GAS MEMBERS'
ALLBLK   DC    C'DIRECTORY BLOCKS ARE ALLOCATED'
USEBLK   DC    C'OF THESE ARE IN USE'
USENAME  DC    C'MEMBER NAMES ARE IN THE DIRECTORY'
ALSNAME  DC    C'OF THESE ARE ALIASES'
NUMTRKS  DC    C'TRACKS ARE ALLOCATED FOR THIS DATA SET'
EXTENTS  DC    C'IS THE SECONDARY ALLOCATION QUANTITY'
NOSEC    DC    C'NO SECONDARY ALLOCATION QUANTITY IS IN THE LABEL'
         EJECT
FRENONE  DC    C'ALL OF THESE ARE IN USE'
FRETRKS  DC    C'OF THESE ARE NOT IN USE'
ALNOREAL DC    C'12345678 IS AN ALIAS WHICH HAS NO CORRESPONDING REAL EX
               NTRY'
ALIASERR DC   C'12345678 AND 12345678 ARE APPARENT ALIASES BUT NEITHER X
               HAS AN ALIAS BIT SET'
SCALOS   DC    C'DATA SET IS ALLOCATED IN TRACKS              '
NOBDBS   DC    C'BYTES ARE USED IN THE LAST PDS DIRECTORY BLOCK'
         SPACE 2
TRBALS   DC    C'BYTES ARE AVAILABLE ON THE LAST USED TRACK'
IOERROR  DC    C'READ ERROR--ECB=12; CCHHR=12.45.78.01.34  OR TTR=TT.TTX
               .RR '
TOOMANY  DC    C'TOO MANY CONSECUTIVE I/O ERRORS (10)'
TOTALIOS DC    C'I/O ERRORS WERE ENCOUNTERED'
NOTOPENR DC    C'RDJFCB FOR DDNAME IN FAILED'
NOTOPENB DC    C'OBTAIN FOR DDNAME IN FAILED'
NOTOPENO DC    C'OPEN FOR DDNAME IN FAILED'
         SPACE 2
FIRSTRD  DC    C'FIRST READ OF AN EXTENT WAS NOT SUCCESSFUL'
TTRISLOW DC    C'TTR IN MEMBER TABLE IS LESS THAN THE CURRENT TTR'
DSBAD    DC    C'THIS DATA SET MAY HAVE BEEN DESTROYED BY A COMPRESS'
NOTFOUND DC    C'THE FOLLOWING MEMBERS WERE NOT FOUND:'
NOMEMBER DC    C'12345678 AT TTR=TT.TT.RR '
LSTARBAD DC    C'THE DS1LSTAR INDICATOR MAY BE INCORRECT'
RERUN    DC    C'RETRY STATS WITH PARM=ALLEXTENTS'
BADERROR DC    C'UNDEFINED READ ERROR'
         SPACE 2
PERMERR  DC    C'PERMANENT I/O ERROR'
STATLREC DC    C'IS THE MINIMUM LOGICAL RECORD LENGTH'
TOTLRECM DC    C'LOGICAL RECORDS ARE IN THIS DATA SET'
OVRLRECM DC    C'LOGICAL RECORD LENGTHS EXCEED THE MAXIMUM LRECL'
INVBLOCM DC    C'VARIABLE BLOCK HEADER RECORDS ARE INVALID'
         SPACE 2
EOFSIMS  DC    C'BAD DIRECTORY RECORD -- RECORD LENGTH IS NOT 256'
BLOCKLEN DC    C'IS THE BLOCK LENGTH -- END OF DIRECTORY SIMULATED'
DIRERRS  DC    C'READ ERROR IN THE DIRECTORY'
BLKNUMS  DC    C'IS THE BLOCK NUMBER -- BLOCK IGNORED'
GASERRS  DC    C'ERROR IN A GAS MEMBER -- END OF MEMBER SIMULATED'
CURRMBR  DC    C'12345678 IS THE MEMBER -- END OF MEMBER SIMULATED'
LASTMEMS DC    C'12345678 IS THE LAST MEMBER IN THE DATA SET'
NULL     DC    C'EMPTY DATA SET'
         EJECT
*                                            THIS MEMBER WAS
GASMEM   DC    C'GAS MEMBER AT TTR TT.TT.RR, DATA:           '
LASTREAD DC    C'LAST SUCCESSFUL READ WAS AT TTR TT.TT.RR '
NOTREAD  DC    C'TRACKS WERE NOT READ'
EXTHDR   DC    C'EXTENT  UCB  VOLUME  TT.TT.RR--TT.TT.RR  TRACKS  CC.CCX
               .HH.HH--CC.CC.HH.HH  CYLB'
EXTUND   DC    C'------  ---  ------  --------  --------  ------  -----X
               ------  -----------  ----'
         SPACE 2
DATEHDR  DC   C'DATA SET:  CREATED   EXPIRES   LAST USE   UPDATE BIT   X
               SECURITY'
DATEUND  DC   C'           -------   -------   --------   ----------   X
               --------'
STATTRKH DC    C'TRACK USAGE:       MINIMUM    AVERAGE    MAXIMUM'
STATTRKU DC    C'                   -------    -------    -------'
DIRTRACK DC    C'TRACKS ARE ALLOCATED FOR THE DIRECTORY'
DIRBYTES DC    C'BYTES ARE ALLOCATED FOR THE DIRECTORY'
MEMBERNO DC    C'12345678 IS NOT IN THIS DATA SET'
         SPACE 2
LSTAR    DC    C'DS1LSTAR IS TT.TT.RR  (FOR 12345 TRACKS IN USE)'
RECSIZE  DC    C'RECORD SIZE DISTRIBUTION:'
BLKPER   DC    C' ----1----2----3----4----5----6----7----8----9----0'
GASTRK   DC    C' 123,456.78XTRACKS COULD BE REGAINED BY COMPRESSING',CX
               L8' '        8 PADDING CHARACTERS
         ORG   GASTRK
         DC    X'40202020',C',',X'202120',C'.',X'202020'
         ORG   ,
         SPACE 2
BLANKS   DC    CL80' '
OUTLINE  DC    CL160' '
         EJECT
***      TRANSLATE TABLE TO CONVERT UNPRINTABLES TO PERIODS
TRLINE   DC    256C'.'            ASSUME ALL UNPRINTABLE FIRST
         ORG   TRLINE+C' '
         DC    1X'40'             BLANK IS OK
         ORG   TRLINE+C''
         DC    C'.<(+|&&'        .<(+|&  ARE OK
         ORG   TRLINE+C'!'
         DC    C'!$*);-/'        !$*);-/  ARE OK
         ORG   TRLINE+C','
         DC    C',%_>?'           ,%_>?  ARE OK
         ORG   TRLINE+C':'
         DC    C':#@''="'         :#@'="  ARE OK
         ORG   TRLINE+X'81'       LOWER-CASE ABCDEFGHI
         DC    X'818283848586878889'
         ORG   TRLINE+X'91'       LOWER-CASE JKLMONPQR
         DC    X'919293949596979899'
         ORG   TRLINE+X'A2'       LOWER-CASE STUVWXYZ
         DC    X'A2A3A4A5A6A7A8A9'
         ORG   TRLINE+C'A'
         DC    C'ABCDEFGHI'       ABCDEFGHI  ARE OK
         ORG   TRLINE+C'J'
         DC    C'JKLMNOPQR'       JKLMNOPQR  ARE OK
         ORG   TRLINE+C'S'
         DC    C'STUVWXYZ'        STUVWXYZ  ARE OK
         ORG   TRLINE+C'0'
         DC    C'0123456789'      0123456789  ARE OK
         ORG   ,
         SPACE 3
WORK     DC    CL80' '
DOUBLE   DS    D
LASTMBB  DS    D
JFCBXIT  DC    X'87',AL3(JFCB)            JFCB BLOCK
JFCB     DS    0D
         IEFJFCBN                         JFCB MAPPING MACRO
*
IN       DCB DDNAME=IN,DSORG=PS,MACRF=E,EXLST=JFCBXIT
OUT      DCB DDNAME=OUT,DSORG=PS,MACRF=PM,EXLST=OUTCHECK
OUTCHECK DC  X'85',AL3(OUTEXIT)
         SPACE 3
         PRINT GEN
MYLABEL  CAMLST SEARCH,JFCBDSNM,JFCBVOLS,WORKAREA
         EJECT
WORKAREA DS    0D,CL148
         ORG   WORKAREA-44    RECORD KEY IS NOT INPUT
         IECSDSL1 (1)         FORMAT 1 DSCB
         ORG   ,
         SPACE 3
         LTORG
         EJECT
BUFFER   DS    0D,32760X      MAXIMUM RECORD SIZE FOR 3380 DEVICES
         ORG   BUFFER-44      RECORD KEY IS NOT INPUT
         IECSDSL1 (2)         FORMAT 2 DSCB
         ORG   ,
         SPACE 3
ESDENTRY DSECT
ESDNAME  DS    CL8            NAME OF EXTERNAL SYMBOL
ESDTYPE  DS    X              ESD TYPE
CODESD   EQU   X'00'          ENTERNAL DEFINITION
CODELR   EQU   X'03'          EXTERNAL REFERENCE
CODEPC   EQU   X'04'          PRIVATE CODE DEFINITION
CODECM   EQU   X'05'          COMMON BLOCK
CODESEG  EQU   X'14'          OVERLAY SEGMENT TABLE
CODEENTB EQU   CODESEG        OVERLAY ENTRY TABLE
ESDADDR  DS    XL3            RELATIVE OFFSET
ESDSEG#  DS    X              SEGMENT NUMBER
ESDLEN   DS    XL3            LENGTH OF SD ENTRY
LENESD1  EQU   *-ESDNAME      LENGTH OF ESD DATA
         SPACE 3
         PRINT NOGEN
         DCBD  DSORG=PS,DEVD=DA
         END   STATS
