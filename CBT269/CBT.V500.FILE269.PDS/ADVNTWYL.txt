WELLPUT  TITLE 'W E L L P U T -- WYLBUR/TSO I/O INTERFACE FROM PLI'
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
***********************************************************************
* WELLPUT -                                                           *
* THE PURPOSE OF THIS MODULE IS TO SIMULATE THE I/O ROUTINES TREAD    *
* AND TWRITE USED BY THE ADVENTURE GAME.                              *
*                                                                     *
* CALLING SEQUENCES:                                                  *
*                                                                     *
* TREAD (PROMPT_MESSAGE,PROMPT_LENGTH, MESSAGE_AREA,LENGTH,RTN_CODE)  *
*                                                                     *
* TWRITE (MESSAGE,MESSAGE LENGTH,RETURN CODE)                         *
*                                                                     *
***********************************************************************
TREAD    CSECT ,
         SAVE  (14,12),,*
         LR    R12,R15
         USING TREAD,R12
***********************************************************************
*  FIRST WE WILL DO A TPUT FOR THE PROMPT MESSAGE (IF IT EXISTS)      *
*  THEN.. ASK FOR THE INPUT.                                          *
*                                                                     *
***********************************************************************
         LR    R5,R1               SAVE INPUT PARM PTR
         L     R1,0(,R5)           GET THE PTR TO THE MSG TEXT
         L     R2,4(,R5)           GET THE LENGTH
         L     R0,0(,R2)           ...
         LTR   R0,R0               IS IT THERE ?
         BZ    TRE0                NO.. JUST DO TGET
         TPUT  (1),(0),R
TRE0     EQU   *
         L     R1,8(,R5)           PICK UP THE ADDRESS OF THE INPUT
*                                   TEXT
         LR    R3,R1               SAVE THE BUFFER ADDRESS
         LA    R0,133              HOW MUCH WE CAN STAND
         ICM   R1,B'1000',TGETFLAG SHOW PROPER TGET STUFF
         TGET  (1),(0),R
         OC    0(133,R3),BLANKS    UPPER CASE THIS SUCKER
         L     R2,16(,R5)          GET THE RETURN CODE PTR
         LA    R15,1               SET SUPER RETURN CODE
         ST    R15,0(,R2)          STORE IT..
         L     R2,12(,R5)          GET HOW MUCH TCAM BOUGHT
         ST    R1,0(,R2)           AND GIVE IT TO PLI
         RETURN (14,12),RC=0
         SPACE 4
TGETFLAG DC    X'80'               SHOW THIS IS A TGET
BLANKS   DC    CL133' '
         LTORG ,
         EJECT ,
***********************************************************************
* TWRITE -                                                            *
*                                                                     *
* ISSUE A TPUT FOR THE CALLING PROGRAM..                              *
*                                                                     *
*                                                                     *
***********************************************************************
TWRITE   CSECT ,
         SAVE  (14,12)
         LR    R12,R15
         USING TWRITE,R12
         LR    R5,R1               SAVE PTR TO INPUT PARMS
         L     R1,0(R5)            PT TO OUTPUT TEXT
         L     R2,4(R5)            GET OUTPUT LENGTH
         L     R0,0(R2)            ...
         TPUT  (1),(0),R           DO IT
         LA    R15,1               SET CRAZY RETURN CODE
         L     R1,8(R5)            GET THE CCODE PTR
         ST    R15,0(R1)           SAVE IT.
         RETURN (14,12),RC=0
         SPACE 4
         LTORG ,
         END   ,
