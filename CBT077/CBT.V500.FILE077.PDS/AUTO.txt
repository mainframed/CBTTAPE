//SYZYGYBA JOB (PS01),'SYZYGYB',
//            CLASS=A,MSGCLASS=A,
//            NOTIFY=SYZYGY1,REGION=4096K
//*-----------------------------------------------*
//*   LIB: SYZYGY.FILE332.PDS(AUTO)
//*   DOC: ASSEMBLE AND LINK
//*-----------------------------------------------*
//ASSEMB  EXEC ASSEMBLE,PARM.LINK='LIST,XREF,AC=1'
//ASM.SYSLIB  DD
//          DD
//          DD
//SYSIN    DD  *
*----------------------------------------------------------------------
*    BW- MACRO  CODE STARTS ABOUT 400 LINES DOWN
*    BW- ACTUAL CODE STARTS ABOUT 1015 LINES DOWN
*----------------------------------------------------------------------
*        THIS ROUTINE PROVIDES FOR JOB SUBMISSION AND AUTOMATIC
*        COMMANDS IN AN MVS/JES2 ENVIRONMENT.  IT'S FUNCTIONS
*        ARE THREE-FOLD AND ARE EXPLAINED BELOW.
*
*
*        1=RDR  ALLOW AN EASY WAY OF PRESENTING JOBS TO JES2 VIA AN
*               OPERATOR MODIFY COMMAND.
*
*        2=CMD  ALLOW AN EASY WAY OF PRESENTING MULTIPLE COMMANDS TO
*               MVS/JES2 VIA AN OPERATOR MODIFY COMMAND.
*
*        3=AUTO ALLOW FOR TIME-OF-DAY/DAY-OF-WEEK AUTOMATIC COMMANDS
*
*
*        THE USER MUST PROVIDE A STARTED TASK PROCEDURE IN SYS1.PROCLIB
*        WHICH IS STARTED AT IPL TIME.  ONCE ACTIVE, IT WILL PROCESS
*        USER DEFINED COMMANDS BY DAY OF WEEK (USER DEFINED).
*        ALSO, IT WILL SUPPORT OPERATOR MODIFY COMMANDS (DEMAND)
*        EITHER SUBMIT JOBS (MEMBERS) TO THE INTERNAL READER OR
*        EXECUTE COMMANDS (MEMBER LISTS).
*
*        FOR THE JOB SUBMISSION FEATURE, ONE OR MORE PARTITIONED
*        DATASET(S) ARE USED.  FOR THE COMMAND/AUTO-COMMAND
*        FUNCTIONS, ANOTHER PARTITIONED DATASET IS USED.  EACH
*        ARE DESCRIBED BELOW (BY DDNAME).
*
***********************************************************************
         EJECT
***********************************************************************
*
*
*
*        DDNAME     USE AND FUNCTION
*        --------   -------------------------------------------------
*
*
*        INTRDR     REQUIRED IF THE JOB-SUBMISSION FUNCTION IS TO
*                   BE USED.  IT SHOULD SPECIFY THE JES2 INTERNAL
*                   READER.
*
*        JOB00      REQUIRED IF THE JOB-SUBMISSION FUNCTION IS TO
*                   BE USED.  IT SHOULD SPECIFY THE NAME OF A
*                   PARTITIONED DATASET WHICH CONTAINS MEMBERS
*                   TO BE SUBMITTED TO JES2.
*
*        JOB??      OPTIONAL WHEN THE JOB-SUBMISSION FUNCTION IS USED.
*                   ANY NUMBER OF ADDITIONAL 'JOB??' DDCARDS MAY BE
*                   USED TO ALLOW FOR SPECIALIZED FUNCTION (WITH
*                   DUPLICATE MEMBER NAMES).  THIS OPTION IS SIMILAR
*                   TO THE MULTIPLE 'PROC??' DDCARDS WITHIN THE JES2
*                   STARTUP PROC.  USE OF THESE OPTIONAL LIBRARIES
*                   IS CONTROLED BY THE OPERATOR (VIA THE 'D=??'
*                   KEYWORD, AS DETAILED BELOW).
*
*        COMMANDS   THIS DD STATEMENT IS REQUIRED FOR EITHER THE
*                   OPERATOR COMMAND-LIST OR THE AUTO-COMMAND
*                   FUNCTION.  IT SHOULD CONTAIN MEMBERS (LISTS
*                   OF COMMANDS) IN THE FORMAT DESCRIBED UNDER THE
*                   HEADING 'COMMAND RECORD SYNTAX' BELOW.
*
*
*        THE FOLLOWING IS AN EXAMPLE OF THE TYPE OF PROC TO
*        BE ADDED TO YOUR SYS1.PROCLIB.  NOTE THAT THE 'JOB00'
*        DDNAME IS REQUIRED TO USE THE JOB SUBMISSION FUNCTION
*        WHILE 'JOB01' AND 'JOB02' ARE OPTIONAL (USED IN THIS
*        CASE AS REMOTE JOB CONTROL LIBRARIES).
*
*        //AUTO     EXEC PGM=AUTO,TIME=1440,DPRTY=(7,9)
*        //STEPLIB  DD DISP=SHR,DSN=USER.LIBRARY    <---- AUTHORIZED
*        //INTRDR   DD SYSOUT=(A,INTRDR)
*        //COMMANDS DD DISP=SHR,DSN=SYS1.COMMANDS
*        //JOB00    DD DISP=SHR,DSN=RMT0.JCL.LIB
*        //JOB01    DD DISP=SHR,DSN=RMT1.JCL.LIB
*        //JOB02    DD DISP=SHR,DSN=RMT2.JCL.LIB
*
*        USE OF THE OPTIONAL 'JOB??' DDNAMES IS SPECIFIED BY THE
*        OPERATOR USING THE 'D=' OPERAND (DETAILED BELOW).
*
***********************************************************************
         EJECT
***********************************************************************
*
*        THE FOLLOWING ARE EXAMPLES OF NORMAL OPERATOR COMMANDS
*        JOB SUBMISSION USE:
*
*        FUNCTION  COMMAND      OPERATION
*        --------  ------------ --------------------------------------
*
*        START     S AUTO.A     NORMAL OPERATOR START-UP.  THE FIRST
*                               AUTOMATIC COMMAND TO BE PROCESSED
*                               WILL BE THE MEMBER WITH A NAME THAT
*                               SPECIFIED A TIME GREATER THAN CURRENT.
*                               HOWEVER, DEMAND COMMAND EXECUTION
*                               AS WELL AS JOB SUBMISSION IS AVAILABLE
*                               IMMEDIATELY.
*
*        IPL       S AUTO.A,PARM=IPLMEM
*                               SAME AS ABOVE, EXCEPT THAT THE COMMAND
*                               MEMBER NAMED IN THE PARM FIELD WILL BE
*                               EXECUTED BEFORE ANY OTHER REQUESTS
*                               (AUTOMATIC OR OPERATOR REQUESTED
*                               COMMAND).  THIS PROVIDES AN ALTERNATIVE
*                               TO THE 'SYS1.PARMLIB(COMMND??)' OPTION
*                               PROVIDED BY MVS.  IT ALSO PROVIDES FOR
*                               ANY TYPE OF COMMAND (JES2 COMMANDS AS
*                               WELL AS MVS COMMANDS).
*
*        SUBMIT    F A,MEMBER1  SUBMIT 'MEMBER1' TO THE JES2 INTERNAL
*                               READER.  SINCE THE 'D=??' OPTION WAS
*                               NOT USED, THE MEMBER WILL BE READ FROM
*                               THE 'JOB00' DD (WHICH WILL REMAIN OPEN
*                               OPEN UNTIL SOMETHING OTHER UNTIL 'D=??'
*                               IS SPECIFIED AND IS NOT THE SAME AS THE
*                               ONE USED PREVIOUSLY).
*
*        SUBMIT    F A,MEMBER1,MEMBER2,....,MEMBERN
*                               SUBMIT MULTIPLE MEMBERS TO THE JES2
*                               INTERNAL READER.  SINCE 'D=??' WAS NOT
*                               SPECIFIED, THE 'JOB00' IS AGAIN USED.
*
*        SUBMIT    F A,MEMBER1,D=01
*                               SUBMIT A MEMBER (JOB) TO THE JES2
*                               INTERNAL READER.  SINCE 'D=01' WAS
*                               SPECIFIED, THE 'JOB00' FILE IS CLOSED
*                               AND THE 'JOB01' FILE IS OPENED TO
*                               READ THE MEMBER.  THIS FILE WILL
*                               REMAIN OPEN UNTIL THE 'D=??' IS
*                               CHANGED (FROM 'D=01') OR IS NOT
*                               SPECIFIED ('D=00' IS THE DEFAULT).
*
*        SUBMIT    F A,MEMBER1,MEMBER02,.....,MEMBERN,D=01
*                               SAME AS THE ABOVE, HOWEVER, MULTIPLE
*                               MEMBERS WILL BE READ FROM THE 'JOB01'
*                               FILE AND SUBMITTED.  NOTE THAT THE
*                               'D=??' KEYWORD MUST BE SPECIFIED AS
*                               THE LAST OPTION.
*
***********************************************************************
         EJECT
***********************************************************************
*
*        THE FOLLOWING ARE EXAMPLES OF OPERATOR COMMANDS TO SCHEDULE
*        COMMAND LISTS.
*
*        FUNCTION  COMMAND      OPERATION
*        --------  ------------ --------------------------------------
*
*        COMMAND   F A,-MEMBER1 CAUSE A LIST OF COMMANDS TO BE EXECUTED
*                               BY MVS (OR JES2).  NOTE THAT THE DASH
*                               (HYPHEN) AS A PREFIX TO THE MEMBER
*                               NAME IDENTIFIES THE REQUEST AS A
*                               COMMAND REQUEST (DEMAND).  THE NAMED
*                               MEMBER WILL BE READ FROM THE FILE
*                               IDENTIFIED BY THE DD 'COMMANDS'.  EACH
*                               COMMAND READ WILL BE EXECUTED IF RE-
*                               QUESTED FOR THE CURRENT DAY (THE SYNTAX
*                               SYNTAX OF THE 'COMMANDS' RECORDS ARE
*                               DEFINED BELOW UNDER 'COMMAND RECORD
*                               SYNTAX').
*
*        COMMAND   F A,-MEMBER1,MEMBER2,.....,MEMBERN
*                               CAUSE A LIST OF MEMBERS TO BE PROCESSED
*                               BY MVS (OR JES2).  EACH MEMBER MAY
*                               CONTAIN MULTIPLE COMMANDS. AGAIN NOTE
*                               THAT THE DASH IDENTIFIES THE REQUEST
*                               AS A DEMAND COMMAND REQUEST.
*
*        SHUTDOWN  P A          TO TERMINATE THIS ROUTINE, SIMPLY
*                               ENTER AN OPERATOR STOP COMMAND.
*
*        SHUTDOWN  F A,-SHUTDOWN
*                               TO ALLOW A LIST OF 'SHUTDOWN' COMMANDS
*                               TO BE EXECUTED PRIOR TO STOPPING THIS
*                               ROUTINE (SUCH AS $PI, I SMF, P RMF ETC)
*                               THE 'P A' COMMAND MAY BE ADDED TO THE
*                               END OF THE SHUTDOWN MEMBER.
*
***********************************************************************
         EJECT
***********************************************************************
*
*        THE AUTOMATIC COMMAND FUNCTION IS NOT CONTROLLED BY THE
*        OPERATOR.  INSTEAD, IT OPERATES ON A 1440 MINUTE CYCLE
*        (60 MINUTES/HOUR AND 24 HOURS/DAY) WITHIN DAY-OF-WEEK
*        AND MONTH-OF-YEAR CYCLES.  THIS MEANS THAT COMMANDS MAY
*        BE SCHEDULED AT A SPECIFIED TIME OF DAY ON ANY (OR ALL)
*        DAY(S) OF THE WEEK OR ANY DAY OF THE MONTH.
*
*        COMMANDS TO BE EXECUTED ARE CONATAINED IN MEMBERS THAT
*        RESIDE IN THE 'COMMANDS' LIBRARY.  EACH MEMBER TO BE
*        AUTOMATICALLY PROCESSED IS NAMED AS FOLLOWS.
*
*            @0100   THIS LIST OF COMMANDS IS TO BE PROCESSED AT
*                    1:00 IN THE MORNING.
*            @1423   THIS MEMBER WILL BE PROCESSED AT 2:23 PM.
*
*            @0000   THIS MEMBER WILL BE PROCESSED AT MIDNIGHT.
*
*        NOTE THAT THE MEMBER NAMES TO BE PROCESSED AUTOMATICALLY
*        MUST BE PREFIXED WITH THE 'AT SIGN' (@), HOWEVER, THESE
*        MEMBERS ARE NOT RESTRICTED FROM 'DEMAND' USE.
*
*
*        COMMAND RECORD SYNTAX
*
*        EACH MEMBER CONTAINED IN THE 'COMMANDS' LIBRARY MUST FOLLOW
*        THE SYNTAX DEFINED BELOW.  THIS IS TRUE FOR BOTH DEMAND
*        COMMAND LISTS AS WELL AS AUTOMATIC COMMAND LISTS.
*
*        AN ASTERISK (*) IN COLUMN 1 DENOTES A COMMENT RECORD AND IS
*        IGNORED BY THE PROCESSOR.  COMMANDS (RECORDS) ARE PROCESSED
*        ONLY ON SPECIFIED DAYS OF THE WEEK AS INDICATED IN THE DAYS
*        FIELD.  DAYS OF THE WEEK MUST BE IN THE ORDER SHOWN.
*
*        A NON-BLANK IN THE 'DAY-OF-THE-WEEK' FIELD REQUESTS PROCESSING
*        ON THAT DAY.
*
*        FORMAT:
*
*        1    ===>BLANK            (AN ASTERISK SIGNIFIES A COMMENT)
*        2-6  ===>MM/DD            (MONTH/DAY SPEC - SLASH REQ'D!)
*        15   ===>(BLANK)
*        8-14 ===>MTWTFSS          (DAY-OF-WEEK SPECIFICATION)
*        15   ===>(BLANK)
*        16   ===>COMMAND DATA     (THROUGH COLUMN 71)
*        72   ===>CONTINUATION     (ONLY ONE CONTINUATION ALLOWED)
*
*        AN ASTERISK IN EITHER THE MM FIELD OR THE DD FIELD ALLOWS
*        EXECUTION FOR ALL MONTHS OR ALL DAYS.  ASTERISKS IN BOTH
*        FIELDS EFFECTIVELY NULLIFIES DAY-WITHIN-MONTH CRITERIA
*        (IF '**/**' IS SPECIFIED, THE COMMAND IS SUBJECT ONLY TO
*        DAY OF WEEK CRITERIA WHICH IS THE SAME AS ALL BLANKS IN
*        THE MM/DD FIELD).  IF THE MM/DD FIELD IS NON-BLANK,
*        THE REQUEST WILL BE PROCESSED ON THE MONTH/DAY SPECIFIED
*        BUT IS ALSO SUBJECT TO DAY OF WEEK SPECIFICATIONS.
*        IF A REQUEST IS TO BE PROCESSED ON THE SAME DAY OF EVERY
*        MONTH, SPECIFY THE MONTH AS ASTERISKS ('**/05').  IF A
*        REQUEST IS TO BE PROCESSED EVERY DAY OF A GIVEN MONTH,
*        SPECIFY THE DAY AS ASTERISKS ('09/**').
*
*        CONTINUATION IS SUPPORTED ONLY IF THE SUBSEQUENT RECORD
*        HAS 'ALL' BLANKS IN COLUMNS 1 THROUGH 15 AND CONTINUED
*        DATA BEGINS IN COLUMN 16.
*
***********************************************************************
         EJECT
***********************************************************************
*
*        EXAMPLES OF AUTOMATIC COMMANDS:
*
*   COLUMNS:  1      8       16                                   71
*             ..     .       .                                    .
*   COL-1---->*MM/DD MTWTFSS COMMAND------------------------------>72
*
* MEMBER=@0300
*   COL-1---->       XXXXXX  F A,BACKUP1,BACKUP2,BACKUP3
*   COL-1---->             X F A,BACKUPWK
*   COL-1----> **/01 ******* F A,MONTH01
*   COL-1----> **/01      S  F A,FIRSTSAT  SCHEDULE JOB ONLY ON THE
*   COL-1----> **/02      S  F A,FIRSTSAT  1ST SATURDAY OF THE MONTH
*   COL-1----> **/03      S  F A,FIRSTSAT            "
*   COL-1----> **/04      S  F A,FIRSTSAT            "
*   COL-1----> **/05      S  F A,FIRSTSAT            "
*   COL-1----> **/06      S  F A,FIRSTSAT            "
*   COL-1----> **/07      S  F A,FIRSTSAT            "
* MEMBER=@0755
*   COL-1---->       MTWTF   START TCAM
* MEMBER=@0800
*   COL-1---->       MTWTF   F TCAM,TS=START
*   COL-1---->       MTWTF   $HQ,P
*   COL-1---->       MTWTF   $AQ,T
* MEMBER=@0900
*   COL-1---->       -       SEND 'TODAY IS BLUE MONDAY...KEEP SMILING*
*   COL-1---->               '
*   COL-1---->        -      SEND 'TODAY IS TUESDAY...FOUR MORE TO GO'
*   COL-1---->         -     SEND 'TODAY IS WEDNESDAY...YOUR HALF WAY'
*   COL-1---->          -    SEND 'TODAY IS THURSDAY...HANG IN THERE'
*   COL-1---->           -   SEND 'THANK GOD IT''S FRIDAY'
*   COL-1---->            -  SEND 'IT''S SATURDAY... DIDN''T YOU FINIS*
*   COL-1---->               H?'
*   COL-1---->             - SEND 'WOW... EVEN SUNDAY YOU WORK!!!!!!'
* MEMBER=@1645
*   COL-1---->       MTWTF   SE 'TSO WILL BE GOING DOWN AT FIVE SHARP'
* MEMBER=@1655
*   COL-1---->       MTWTF   SE 'TSO WILL BE GOING DOWN IN FIVE MINUTE*
*   COL-1---->               S'
* MEMBER=@1659
*   COL-1---->       MTWTF   SE '**** TSO IS GOING DOWN IN ONE MINUTE X
*   COL-1---->               ****'
*   COL-1---->       MTWTF   SE '****   YOU WILL RECEIVE NO WARNING   X
*   COL-1---->               ****'
* MEMBER=@1700
*   COL-1---->       MTWTF   F TCAM,TS=STOP
*   COL-1---->       MTWTF   $HQ,T
*   COL-1---->       MTWTF   $AQ,P
*   COL-1----> 05/17 ******* F A,TESTJOB  SCHEDULE ONLY ON MAY 17
*   COL-1----> 05/16 *****   F A,TESTWKDY SCHEDULE ONLY ON MAY 16 WD
*   COL-1----> 05/16      ** F A,TESTWKND SCHEDULE ONLY ON MAY 16 WE
* MEMBER=@1702
*   COL-1---->       MTWTF   Z TP
*
***********************************************************************
         EJECT
***********************************************************************
*
*        EXAMPLES OF OPERATOR 'DEMAND' COMMANDS:
*
*   COL-1---->*      MTWTFSS COMMAND--------------------------->72
*
* MEMBER=IPL                            (  S AUTO.A,PARM=IPL  )
*   COL-1---->       XXXXXXX S RMF.RMF
*   COL-1---->       XXXXXXX S OPSWTR.OPSWTR
*   COL-1---->       XXXXXXX F A,-VARYCONS,VARYALL,SETJES
* MEMBER=SETJES                         (  F A,-SETJES )
*   COL-1---->       XXXXXXX $TPRT1,Q=A
*   COL-1---->       XXXXXXX $TI9,Z
*   COL-1---->       XXXXXXX $SI9
*   COL-1---->       XXXXXXX $SPRT1
* MEMBER=SHUTDOWN                       (  F A,-SHUTDOWN  )
*   COL-1---->       XXXXXXX $P
*   COL-1---->       XXXXXXX $IPRT1
*   COL-1---->       XXXXXXX $PLNE1
*   COL-1---->       XXXXXXX $ELNE1
*   COL-1---->       XXXXXXX P OPSWTR
*   COL-1---->       XXXXXXX P RMF
*   COL-1---->       XXXXXXX P A
* MEMBER=UNLOAD                         (  F A,-UNLOAD  )
*   COL-1---->       XXXXXXX U 180
*   COL-1---->       XXXXXXX U 181
*   COL-1---->       XXXXXXX U 182
* MEMBER=VARYALL                        (  F A,-VARYALL  )
*   COL-1---->       XXXXXXX V 180-188,ONLINE
*   COL-1---->       XXXXXXX V 150-158,OFFLINE
*   COL-1---->       XXXXXXX V 160-16F,ONLINE
* MEMBER=VARYCONS                       (  F A,-VARYCONS  )
*   COL-1---->       XXXXXXX V 120,CONSOLE,ROUT=(1,2,8,9,10,12,13,14,1*
*   COL-1---->               5)
*   COL-1---->       XXXXXXX V 121,CONSOLE,ROUT=(3,4,5,6)
*   COL-1---->       XXXXXXX V 122,CONSOLE,ROUT=(7)
*   COL-1---->       XXXXXXX V O-123,CONSOLE,ROUT=ALL
* MEMBER=VARYOFF                        (  F A,-VARYOFF  )
*   COL-1----> 09/**      XX V 150-158,OFFLINE  ONLY FOR SEPT WKEND
*   COL-1---->       XXXXXXX V 160-168,OFFLINE
*   COL-1---->       XXXXXXX V 170-178,OFFLINE
*
*  NOTE THAT THE COMMAND-LIST MEMBER MAY INCLUDE COMMANDS FOR THIS
*  ROUTINE AS WELL.  THIS MAY OR MAY NOT BE DESIRABLE SINCE IF NOT
*  CAUTIOUS, A LOOP COULD RESULT.
*
***********************************************************************
         EJECT
***********************************************************************
*
*        THIS ROUTINE IS 'NOT' RE-ENTRANT AND MUST BE LINKED AS
*        'AUTHORIZED'.
*
*        BOB BECKER MAY ,1979  ALLEN-BRADLEY COMPANY .. MILWAUKEE
*
*        MODIFIED:
*
*        BRIAN WESTERMAN JUNE 1996 REPLACED STIMER EXIT ROUTINE
*                        CALLED ENTRY950 WITH NEW ROUTINE TO FIX
*                        S102 ABEND UNDER MVS/ESA V5.1+
*
*
*        GERALD URBANEK JANUARY, 1988 WESTERN ATLAS INTERNATIONAL ..
*        HOUSTON, TEXAS
*
*        CORRECTED TIMING PROBLEMS AT MIDNIGHT ON LIGHTLY LOADED
*        HIGH SPEED PROCESSORS.
*
*
*
*
*
*        REGISTER USAGE
*
*        R0   - WORK
*        R1   - WORK
*        R2   - WORK
*        R3   - WORK
*        R4   - WORK
*        R5   - WORK
*        R6   - WORK
*        R7   - WORK
*        R8   - UNUSED
*        R9   - QEDIT ORIGIN BASE
*        R10  - DCBS
*        R11  - BASE # 1
*        R12  - BASE # 2
*        R13  - SAVE AREA
*        R14  - BAL
*        R15  - WORK
*
*----------------------------------------------------------------------
*   BW-MACROS ADED HERE SO THEY DON'T GET LOST
*----------------------------------------------------------------------
         MACRO
&LABEL   $PROLOG &LV=0
.**********************************************************************
.*
.*       THIS MACRO WILL PROVIDE ENTRY LINKAGE AND OPTIONALLY
.*       MULTIPLE BASE REGISTERS.  ALSO, VIA THE 'LV=' KEYWORD
.*       PROVIDE ADDITIONAL USER STORAGE (APPENDED TO THE
.*       SAVE AREA) ADDRESSABLE FROM REG 13.  IF NO OPERANDS
.*       ARE CODED, REG 12 IS ASSUMED THE BASE. EXAMPLE:
.*              SECTNAME $PROLOG          = STANDARD REG 12 BASE
.*              SECTNAME $PROLOG 5        = STANDARD, REG 5 BASE
.*              SECTNAME $PROLOG 10,LV=20 = ADD 20 BYTES TO SAVE AREA
.*                                             REG 10 IS BASE
.*              SECTNAME $PROLOG R10,R11  = REGS 10 AND 11 ARE BASES
.*
.**********************************************************************
         LCLA  &AA,&AB,&AC
         GBLB  &PRORG
&AC      SETA  4096
&LABEL   CSECT
         B     32(R15)             BRANCH AROUND
         DC    AL1(26)
         DC    CL8'&LABEL'         CSECT NAME
         DC    C'-'
         DC    CL8'&SYSDATE'       COMPILE DATE
         DC    C'-'
         DC    CL8'&SYSTIME'       COMPILE TIME
         CNOP  0,4                 ALIGNMENT
         STM   R14,R12,12(R13)     SAVE REGISTERS
         LR    R12,R15             LOAD BASE REG
         USING &LABEL,R12          INFORM ASSEMBLER
         AIF   (&LV GT 4023).MERR
         LA    R0,&LV+72           LOAD REG 0 WITH LENGTH VARIABLE
         GETMAIN R,LV=(0)          GET CORE FOR SAVEAREA AND USER
         AIF   (&LV+72 LE 256).XC2
         AIF   (&LV+72 LE 512).XC1
         MVI   0(R1),X'00'         MOVE X'00' TO FIRST BYTE
         LR    R2,R1               SAVE POINTER IN EVEN REG
         LA    R4,1(R1)            SET RECEIVING POINTER
         LR    R5,R0               SET RECEIVING LENGTH
         BCTR  R5,R0               DECREMENT LENGTH
         LA    R5,0(R5)            CLEAR HIGH ORDER BYTE
         LA    R3,1                SET SENDING LENGTH
         MVCL  R4,R2               INSTRUCTION PADS WITH X'00'
         AGO   .STORE
.XC1     ANOP
         XC    256(&LV-184,R1),256(R1)  CLEAR SAVE AREA
         XC    0(256,R1),0(R1)          CLEAR SAVE AREA
         AGO   .STORE
.XC2     ANOP
         XC    0(&LV+72,R1),0(R1)       CLEAR SAVE AREA
.STORE   ANOP
         ST    R13,4(R1)           SAVE BACK CHAIN
         ST    R1,8(R13)           SET FORWARD CHAIN
         LR    R11,R1              SAVE NEW SAVEAREA ADDRESS
         L     R15,16(R13)         RESTORE REG 15
         ST    R0,16(R13)          SAVE SAVEAREA LENGTH
         LM    R0,R1,20(R13)       RESTORE REGS USED IN GETMAIN
         LR    R13,R11             SET SAVEAREA POINTER
         AIF   (N'&SYSLIST EQ 0).MEND
         AIF   ('&SYSLIST(1)' EQ 'R12').SKIPIT
         AIF   ('&SYSLIST(1)' EQ '12').SKIPIT
         LA    &SYSLIST(1),&LABEL  LOAD REQUESTED BASE REG
         DROP  R12                 DROP ASSUMED BASE REG
         USING &LABEL,&SYSLIST(1)  INFORM ASSEMBLER
.SKIPIT  ANOP
&AA      SETA  2
.LOOP    ANOP
         AIF   (&AA GT N'&SYSLIST).MEXIT
&AB      SETA  &AA-1
         LA    &SYSLIST(&AA),2048(&SYSLIST(&AB))  LOAD NEXT BASE REG
         LA    &SYSLIST(&AA),2048(&SYSLIST(&AA))  LOAD NEXT BASE REG
         USING &LABEL+&AC,&SYSLIST(&AA) INFORM ASSEMBLER
&AC      SETA  &AC+4096
&AA      SETA  &AA+1
         AGO   .LOOP
.MEXIT   ANOP
         AIF   (&PRORG).MEX2
         SPACE
*        $REGS
         SPACE
.MEX2    ANOP
&AA      SETA  &LV+72
         MNOTE *,'TOTAL STORAGE AREA RECEIVED = &AA'
         MEXIT
.MEND    ANOP
         MNOTE *,'NO REGISTER SPECIFIED - R12 ASSUMED'
         AGO   .MEXIT
.MERR    ANOP
         MNOTE 12,'LV > 4023 - REQUEST IGNORED'
         AGO   .MEXIT
         MEND
         EJECT
*----------------------------------------------------------------------
*
*----------------------------------------------------------------------
         MACRO
&LABEL   $EPILOG &RC
&LABEL   LR    R1,R13              GET SAVEAREA ADDRESS
         L     R13,4(R13)          GET BACK CHAIN POINTER
         L     R0,16(R13)          GET SAVEAREA LENGTH
         ST    R15,16(R13)         SAVE REGISTER 15 (RETCODE)
         FREEMAIN R,LV=(0),A=(1)   FREE SAVEAREA
         LM    R14,R12,12(R13)     RESTORE CALLERS REGS
         AIF   (T'&RC EQ 'O').SPEC
         LA    R15,&RC             SET RETURN CODE
.SPEC    ANOP
         BR    R14                 RETURN TO CALLER
         MEND
         EJECT
*----------------------------------------------------------------------
*
*----------------------------------------------------------------------
         MACRO
&LABEL   $STCK  &NOGEN
         LCLC   &L
.**********************************************************************
.****                                                             *****
.****    THIS MACRO WILL PROVIDE THE DAY, DATE, AND TIME FROM     *****
.****    THE TIME-OF-DAY CLOCK IN GREGORIAN (ENGLISH) FORMAT.     *****
.****                                                             *****
.****    IF INVOKED WITHOUT THE OPTIONAL 'NOGEN' KEYWORD, THE     *****
.****    CONSTANTS NAMED BELOW WILL BE GENERATED.  IF THE         *****
.****    'NOGEN' KEYWORD IS USED, THE USER MUST PROVIDE THIS      *****
.****    ROUTINE WITH ADDRESSABILITY TO THEM AS PRE-DEFINED       *****
.****    CONSTANTS.                                               *****
.****                                                             *****
.****    DAY    DS   X      A BINARY NUMBER (HEX) RELATIVE TO     *****
.****                       THE DAY OF THE WEEK AS FOLLOWS        *****
.****                       0=MONDAY, 1=TUESDAY, 2=WEDNESDAY,     *****
.****                       3=THURSDAY, 4=FRIDAY,                 *****
.****                       5=SATURDAY, 6=SUNDAY                  *****
.****                                                             *****
.****    DATE   DS   CL8    AN EIGHT CHARACTER FIELD CONTAINING   *****
.****                       THE DATE IN MM/DD/YY FORMAT           *****
.****                                                             *****
.****    TIME   DS   CL8    AN EIGHT CHARACTER FIELD CONTAINING   *****
.****                       THE TIME IN HH:MM:SS FORMAT           *****
.****                                                             *****
.****    NOTE THAT THE CALLER MUST PROVIDE A REGISTER SAVE AREA   *****
.****    THAT BEGINS ON A DOUBLEWORD BOUNDARY TO BE USED AS A     *****
.****    WORK AREA BY THIS ROUTINE (R-13 BASED).                  *****
.****                                                             *****
.**********************************************************************
&L       SETC  '&SYSNDX'
&LABEL   STCK  8(13)              STORE THE TIME-OF-DAY CLOCK
         LM    0,1,8(13)          LOAD INTO WORK REGS
         L     15,16              GET CVT ADDRESS
         A     0,304(15)          ADD LOCAL TIME DEVIATION
         SRDL  0,12               ISOLATE NUMBER OF MICROSECONDS
         D     0,=F'60000000'     DIVIDE BY 60M (R1=MINUTES AFT EPOCH)
         LR    15,0               COPY REMAINDER OF MICS TO GET SECS
         SR    14,14              CLEAR FOR DIVIDE
         D     14,=F'951424'      DIVIDE TO GET REMAINING SECONDS (R5)
         LR    14,15              COPY TO WORK REG
S&L.B    SL    14,=F'60'          DECREMENT BY 60 SECONDS
         BM    S&L.S              LESS THAN SIXTY, CONTINUE
         SL    15,=F'60'          MORE THAN SIXTY, ADJUST FOR LEAP
         AL    1,=F'1'            BUMP MINUTES
         B     S&L.B              CHECK AGAIN
S&L.S    CVD   15,8(13)           CONVERT SECONDS TO PACKED FORMAT
         UNPK  24(4,13),14(2,13)  UNPACK SECONDS FOR PRINT
         OI    27(13),X'F0'       SET UP FOR PRINTING
         MVC   TIME+6(2),26(13)   MOVE THE SECONDS
         CVD   1,8(13)            CONVERT MINUTES TO PACKED FORMAT
         DP    8(8,13),=P'60'     DIVIDE INTO HOURS AND MINUTES
         UNPK  24(4,13),14(2,13)  UNPACK THE MINUTES
         OI    27(13),X'F0'       SET UP FOR PRINTING
         MVC   TIME+3(2),26(13)   MOVE THE MINUTES
         ZAP   8(8,13),8(6,13)    RESET TO FULL LENGTH
         DP    8(8,13),=P'24'     DIVIDE INTO DAYS AND HOURS
         UNPK  24(4,13),14(2,13)  UNPACK THE HOURS
         OI    27(13),X'F0'       SET UP FOR PRINTING
         MVC   TIME(2),26(13)     MOVE THE HOURS
         MVI   TIME+2,C':'        ADD COLIN
         MVI   TIME+5,C':'        ADD COLIN
         ZAP   8(8,13),8(6,13)    RESET TO FULL LENGTH
         DP    8(8,13),=P'7'      DIVIDE BY NUMBER OF DAYS IN A WEEK
         ZAP   8(8,13),15(1,13)   FILL DOUBLEW WITH THE REMAINDER
         CVB   0,8(13)            CONVERT RELATIVE DAY TO BINARY
         STC   0,DAY              SET RELATIVE DAY OF WEEK
         ZAP   16(8,13),8(8,13)   SAVE THE NUMBER OF DAYS
         L     1,16               GET CVT ADDRESS
         MVC   24(4,13),57(1)     MOVE CVT DATE TO WORK AREA
         ZAP   8(8,13),=P'0'      ZERO WORK AREA
         MVO   14(2,13),24(1,13)  MOVE YEAR PORTION
         UNPK  DATE+6(2),14(2,13) UNPACK INTO OUTPUT AREA
         LA    15,S&L.T           LOAD ADDR OF TABLE
         CVB   1,8(13)            CONVERT TO BIN
         SLL   1,30               SHIFT ALL BUT LAST TWO
         LTR   1,1                TEST RESIDUAL
         BNZ   *+8                NOT 0, BRANCH
         LA    15,S&L.TL          LEAP YEAR
         LR    14,15              SAVE TABLE IN USE
         ZAP   8(8,13),25(2,13)   MOVE DAY PORTION
         CVB   0,8(13)            CONVERT TO BIN
         SR    1,1                CLEAR REG
         IC    1,0(15)            LOAD CHAR FROM TABLE
         LA    15,1(15)           INDEX TABLE POINTER
         SR    0,1                SUBTRACT FROM WORK VALUE
         BP    *-10               STILL POSITIVE, DO AGAIN
         AR    0,1                ELSE, ADD IT BACK
         CVD   0,8(13)            CONVERT TO DEC
         UNPK  DATE+3(2),14(2,13) UNPACK DAY INTO OUTPUT
         SR    15,14              FIND VALUE OF CURRENT INDEX
         CVD   15,8(13)           CONVERT TO DEC
         UNPK  DATE(2),14(2,13)   UNPACK MONTH INTO OUTPUT
         OI    DATE+1,C'0'        INSURE NUMERICS
         OI    DATE+4,C'0'        INSURE NUMERICS
         OI    DATE+7,C'0'        INSURE NUMERICS
         MVI   DATE+2,C'/'        ADD SLASH
         MVI   DATE+5,C'/'        ADD SLASH
         B     S&L.E              BRANCH AROUND CONSTANTS
S&L.T    DC    AL1(31,28,31,30,31,30,31,31,30,31,30,31) MONTH TABLE
S&L.TL   DC    AL1(31,29,31,30,31,30,31,31,30,31,30,31) LEAP YEAR
         AIF    (T'&NOGEN NE 'O').NOGEN   NOGEN SPECIFIED, SKIP
DAY      DC    XL1'00'            RELATIVE DAY OF THE WEEK
DATE     DC    C'  /  /  '        CURRENT DATE
TIME     DC    C'  :  :  '        CURRENT TIME
.NOGEN   ANOP
S&L.E    DS    0H
         MEND
         EJECT
*----------------------------------------------------------------------
*
*----------------------------------------------------------------------
*          DATA SET SFALLOC    AT LEVEL 006 AS OF 05/17/78
***********************************************************************
*                                                                     *
*                                                                     *
*               DDDDDDDDD                     AAAAAAAAAA              *
*              DDDDDDDDDD                   AAAAAAAAAAAA              *
*             DD       DD                  AA        AA               *
*            DD        DD                 AA        AA                *
*           DD        DD                 AA        AA                 *
*          DD        DD                 AAAAAAAAAAAA                  *
*         DD        DD                 AAAAAAAAAAAA                   *
*        DD        DD                 AA        AA                    *
*       DD        DD                 AA        AA                     *
*      DD        DD                 AA        AA                      *
*     DDDDDDDDDDD                  AA        AA                       *
*    DDDDDDDDDD                   AA        AA                        *
*                                                                     *
* 1978                                                                *
*        USAGE -THIS MACRO MADE BE USED  MULTIPLE TIMES IN ONE        *
*              PROGRAM AND GENERATE UNIQUE LABELS VIA THE 'UNQSUF'    *
*              PARAMETER.  PRIOR TO ISSUING SVC 99 REGISTER 1 MUST    *
*              CONTAIN THE ADDRESS OF   THIS PARAMETER LIST.          *
*              EX. -      PARMLIB   DA  DSN='SYS1.PARMLIB'            *
*                                   ... ANY OTHER CODE                *
*                                   LA  R1,PARMLIB                    *
*                                   SVC 99                            *
*                                   ... ANY OTHER CODE                *
*        DEFAULTS -THERE ARE NO DEFAULTS FOR ANY PARM.    THE ONLY    *
*              PARMETERS THAT WILL BE GENERATED FOR DYNAMIC           *
*              ALLOCATION ARE THE ONES  THAT ARE SPECIFICALLY CODED.  *
*              THE TYPE OF CALL TO DYNAMIC ALLOCATION DEFAULTS TO     *
*              ALLOCATION.  BY CODING IN A VALUE FOR DEALLOC, THE     *
*              PARAMETERS WILL DEALLOCATE THE RESOURCE INSTEAD OF     *
*              ALLOCATING IT.                                         *
*                                                                     *
*        LABELS - LABELS HAVE BEEN PROVIDED FOR OFTEN USED FIELDS.    *
*              SOME OF THESE ARE :                                    *
*                      1. DAER    WHICH  POINTS TO THE ERROR CODE     *
*                                       FROM SVC 99                   *
*                      2. DAIN    WHICH  POINTS TO THE INFORMATION    *
*                                       CODE FROM SVC 99              *
*              ALSO, EACH VALUE SUCH AS THE DSNAME TO ALLOCATE        *
*              HAS A LABEL ASSOCIATED WITH IT FOR EASY REFERENCE.     *
*                                                                     *
*                                                                     *
***********************************************************************
         MACRO
&NAME    DA    &DSN=,              THE DATASET NAME                    X
               &DDN=,              DDNAME TO ALLOCATE OR DEALLOCATE    X
               &MEM=,              MEMBER NAME FOR PDS OR BIAS FOR GDG X
               &STATUS=,           PRIMARY DISPOSITION                 X
               &DISP=,             DISPOSITION IF NORMAL EOJ           X
               &CDISP=,            DISPOSITION IF ABNORMAL EOJ         X
               &SPACE=,            PRIMARY SPACE REQUIREMENT           X
               &TYPSP=,            TYPE OF SPACE TO ALLOCATE           X
               &SECSP=,            SECONDARY SPACE REQUIREMENT         X
               &VOLSER=,           VOLUME SERIAL #                     X
               &UNIT=,             UNIT DESIGNATION                    X
               &SYSOUT=,           CLASS SPECIFICATION FOR SYSOUT      X
               &FREE=,             DEALLOCATE AT CLOSE                 X
               &COPIES=,           COPIES OF YOUR SYSOUT               X
               &LABEL=,            TYPE LABEL - SL,NL,ETC.             X
               &EXPDT=,            EXPIDATION DATE                     X
               &RETPD=,            RETENTION PERIOD                    X
               &DUMMY=,            ALLOCATE A DUMMY DATA SET           X
               &BLKSIZE=,          BLOCK SIZE                          X
               &FUNC=,             FOR PUNCH OUTPUT INTERPRETING       X
               &FORM=,             FORM NAME SPECIFICATION.            X
               &LRECL=,            LOGICAL RECORD LENGTH               X
               &DIRSP=,            # OF DIRECTORY BLOCKS OR INDEX SPACEX
               &RLSE=,             TO REQUEST THE RELEASE OF UNUSED SP X
               &UNQSUF=,           UNIQUE SUFFIX FOR LABEL NAMES LEN=4 X
               &DCB=,              SPECIFY A DSNAME TO COPY DCB FROM   X
               &DEALLOC=
         LCLA  &A,&K,&A2
         LCLC  &X,&NM
&X       SETC  '&UNQSUF'
&NAME    DS    0F
         DC    X'80'
         DC    AL3(DARB&X)
DARB&X   DS    0F
         DC    AL1(20)
         AIF   (T'&DEALLOC EQ 'O').ALLOC
         DC    AL1(02)             THIS CONTROL BLOCK IS FOR DEALLOCATE
         AGO   .PAST
.ALLOC   DC    AL1(01)             THIS CONTROL IS FOR ALLOCATION
.PAST    DC    AL2(0)
DAER&X   DC    AL2(0)              ERROR CODE FROM DYNAMIC ALLOCATION
DAIN&X   DC    AL2(0)              INFORMATION CODE FROM DYNALLOC
         DC    A(TS&X)             POINTER TO CALL LIST FOR SVC 99
         DC    XL8'0'              RESERVED
&A       SETA  1
         DS    0F                  FULLWORD ALIGNED
TS&A&X   DC    X'0001'             KEY FOR DDNAME SPECIFICATION
         DC    XL2'1'
&K       SETA  K'&DDN
         DC    XL2'&K'             LENGTH OF DDNAME SPECIFIED
DADN&X   DC    C'&DDN'             DDNAME PLUGGED IN HERE
&A       SETA  &A+1
         AIF   (T'&DSN EQ 'O').A
         DS    0F
TS&A&X   DC    X'0002'             KEY FOR DSNAME SPECIFICATION
         DC    XL2'1'
         DC    AL2(44)             LENGTH OF DSNAME MAX IS 44
DADS&X   DC    CL44'&DSN'          DSNAME IS PLUGGED IN HERE
&A       SETA  &A+1
.A       AIF   (T'&MEM EQ 'O').B
         DS    0F
TS&A&X   DC    X'0003'             KEY FOR MEMBER OR GDG SPECIFICATION
         DC    XL2'1'
&K       SETA  K'&MEM
         DC    XL2'&K'             LENGTH OF SPECFIED MEMBER NAME
DAMB&X   DC    C'&MEM'             THE MEMBER NAME IS PLUGGED IN HERE
&A       SETA  &A+1
.B       AIF   (T'&STATUS EQ 'O').C
         DS    0F
TS&A&X   DC    X'0004'             KEY FOR PRIMARY DISPOSITION OF DDN
         DC    XL2'1'
         DC    XL2'1'              THIS IS ALWAYS A ONE BYTE CODE
&A       SETA  &A+1
         AIF   ('&STATUS' EQ 'OLD').B1       OLD = X'01'
         AIF   ('&STATUS' EQ 'MOD').B2       MOD = X'02'
         AIF   ('&STATUS' EQ 'NEW').B3       NEW = X'04'
         AIF   ('&STATUS' EQ 'SHR').B4       SHR = X'08'
         MNOTE 8,'&STATUS INVALID FOR STATUS'
         AGO   .C
.B1      DC    X'1'                CONSTANT FOR DISP=OLD
         AGO   .C
.B2      DC    X'2'                CONSTANT FOR DISP=MOD
         AGO   .C
.B3      DC    X'4'                CONSTANT FOR DISP=NEW
         AGO   .C
.B4      DC    X'8'                CONSTANT FOR DISP=SHR
         AGO   .C
.C       AIF   (T'&DISP EQ 'O').D
         DS    0F
TS&A&X   DC    X'0005'             KEY FOR NORMAL EOJ SPECIFICATION
&A       SETA  &A+1
         DC    XL2'1'
         DC    XL2'1'              THIS WILL ALWAYS BE A ONE BYTE KEY
         AIF   ('&DISP' EQ 'UNCATLG').C1     UNCATLG = X'01'
         AIF   ('&DISP' EQ 'CATLG').C2       CATLG   = X'02'
         AIF   ('&DISP' EQ 'DELETE').C3      DELETE  = X'04'
         AIF   ('&DISP' EQ 'KEEP').C4        KEEP    = X'08'
         MNOTE 8,'&DISP INVALID FOR DISP'    ALL ELSE IS WRONG
         AGO   .D
.C1      DC    X'1'                CONSTANT FOR UNCATLG AT NORMAL EOJ
         AGO   .D
.C2      DC    X'2'                CONSTANT FOR CATLG  AT NORMAL EOJ
         AGO   .D
.C3      DC    X'4'                CONSTANT FOR DELETE AT NORMAL EOJ
         AGO   .D
.C4      DC    X'8'                CONSTANT FOR KEEP   AT NORMAL EOJ
         AGO   .D
.D       AIF   (T'&CDISP EQ 'O').E
         DS    0F
TS&A&X   DC    X'0006'             KEY FOR ABNORMAL EOJ DISPOSITION
&A       SETA  &A+1
         DC    XL2'1'
         DC    XL2'1'              THIS IS ALWAYS A ONE BYTE KEY
         AIF   ('&CDISP' EQ 'UNCATLG').D1      UNCATLG = X'01'
         AIF   ('&CDISP' EQ 'CATLG').D2        CATLG   = X'02'
         AIF   ('&CDISP' EQ 'DELETE').D3       DELETE  = X'04'
         AIF   ('&CDISP' EQ 'KEEP').D4         KEEP    = X'08'
         MNOTE 8,'&CDISP INVALID FOR DISP'     ALL ELSE IS WRONG
         AGO   .E
.D1      DC    X'1'                CONSTANT FOR UNCATLG IF ABEND
         AGO   .E
.D2      DC    X'2'                CONSTANT FOR CATLG  IF ABEND
         AGO   .E
.D3      DC    X'4'                CONSTANT FOR DELETE IF ABEND
         AGO   .E
.D4      DC    X'08'               CONSTANT FOR KEEP   IF ABEND
         AGO   .E
.E       AIF   (T'&TYPSP EQ 'O').I
         DS    0F
         AIF   ('&TYPSP' EQ 'TRK').E1  IF TYPE OF SPACE SPECIFICATION
         AIF   ('&TYPSP' EQ 'CYL').E2  IS NOT TRACKS OR CYLINDERS THEN
TS&A&X   DC    X'0009'                 IT MUST BE IN BLOCKS
         DC    XL1'1'
         DC    XL1'3'
         DC    AL3(&TYPSP)
&A       SETA  &A+1
         AGO   .F
.E1      ANOP
TS&A&X   DC    X'0007'             SPACE IS TO BE ALLOCATED IN TRKS
         DC    XL2'0'
&A       SETA  &A+1
         AGO   .F
.E2      ANOP
TS&A&X   DC    X'0008'             SPACE IS TO BE ALLOCATED IN CYLS
         DC    XL2'0'
&A       SETA  &A+1
.F       AIF   (T'&SPACE EQ 'O').FO
         DS    0F
TS&A&X   DC    X'000A'             KEY TO SPECIFY PRIMARY SPACE
         DC    XL2'1'
         DC    XL2'3'              MAXIMUM LENGTH IS THREE
DASP&X   DC    AL3(&SPACE)
&A       SETA  &A+1
         AGO   .G
.FO      MNOTE 8,'PRIMARY SPACE OMITTED'   TYPSP REQUIRES AT LEAST
         AGO   .I                  THE PRIMARY SPACE SPECIFICATION
.G       AIF   (T'&SECSP EQ 'O').H
         DS    0F
TS&A&X   DC    X'000B'             KEY TO SPECIFY SECONDARY SPACE
         DC    XL2'1'
         DC    XL2'3'              MAXIMUM LENGTH IS THREE
DASS&X   DC    AL3(&SECSP)
&A       SETA  &A+1
.H       AIF   (T'&DIRSP EQ 'O').I
         DS    0F
TS&A&X   DC    X'000C'             KEY TO SPECIFY DIRECTORY SPACE
         DC    XL2'1'
         DC    XL2'3'              MAXIMUM LENGTH IS THREE
DADI&X   DC    AL3(&DIRSP)
&A       SETA  &A+1
.I       AIF   (T'&VOLSER EQ 'O').J
         DS    0F
TS&A&X   DC    X'0010'             KEY TO SPECIFY A PARTICULAR VOLSER
         DC    XL2'1'
&K       SETA  K'&VOLSER           PICK UP LENGTH OF NAME SPECIFIED
         DC    XL2'&K'             AND USE THAT LENGTH
DAVO&X   DC    C'&VOLSER'          AND NAME
&A       SETA  &A+1
.J       AIF   (T'&UNIT EQ 'O').K
         DS    0F
TS&A&X   DC    X'0015'             KEY FOR UNIT SPECIFICATION
         DC    XL2'1'
&K       SETA  K'&UNIT             PICK UP LENGTH OF UNIT NAME GIVEN
         DC    AL2(&K)             THEN USE THAT LENGTH
DAUN&X   DC    C'&UNIT'            AND NAME
&A       SETA  &A+1
.K       AIF   (T'&SYSOUT EQ 'O').L
         DS    0F
TS&A&X   DC    X'0018'             KEY FOR SYSOUT CLASS SPECIFICATION
         DC    XL2'1'
&K       SETA  K'&SYSOUT
         DC    AL2(&K)
DASY&X   DC    C'&SYSOUT'
&A       SETA  &A+1
.L       AIF   (T'&FORM EQ 'O').M
         DS    0F
TS&A&X   DC    X'001A'             KEY FOR FORMS ID SPECIFICATION
         DC    XL2'1'
&K       SETA  K'&FORM             PICK UP LENGTH OF FORMS ID
         DC    AL2(&K)             AND USE THAT LENGTH
DAFM&X   DC    C'&FORM'            AND THAT FORM NUMBER
&A       SETA  &A+1
.M       AIF   (T'&FREE EQ 'O').N
         AIF   ('&FREE' NE 'CLOSE').M2
         DS    0F
TS&A&X   DC    X'001C'             KEY TO SPECIFY FREE=CLOSE
DACL&X   DC    XL2'0'              MUST BE CODED FREE=CLOSE
&A       SETA  &A+1                ALL ELSE WILL GENERATE MNOTE
         AGO   .N
.M2      MNOTE 8,'&FREE INVALID FOR FREE'
.N       AIF   (T'&COPIES EQ 'O').O
         DS    0F
TS&A&X   DC    X'001D'             KEY TO SPECIFY MULTIPLE COPIES
         DC    XL2'1'              OF OUTPUT.
         DC    XL2'1'
DACO&X   DC    AL1(&COPIES)        NUMBER OF COPIES REQUESTED
&A       SETA  &A+1
.O       AIF   (T'&LABEL EQ 'O').P
         DS    0F
TS&A&X   DC    X'001E'             KEY TO SPECIFY TYPE LABEL
         DC    XL2'1'
         DC    XL2'1'              LENGTH OF THIS FIELD IS ALWAYS ONE
&A       SETA  &A+1
         AIF   ('&LABEL' EQ 'NL').O1     NL  = X'01'
         AIF   ('&LABEL' EQ 'SL').O2     SL  = X'02'
         AIF   ('&LABEL' EQ 'NSL').O3    NSL = X'04'
         AIF   ('&LABEL' EQ 'SUL').O4    SUL = X'08'
         AIF   ('&LABEL' EQ 'BLP').O5    BLP = X'10'
         AIF   ('&LABEL' EQ 'LTM').O6    LTM = X'21'
         AIF   ('&LABEL' EQ 'AL').O7     AL  = X'40'
         AIF   ('&LABEL' EQ 'AUL').O8    AUL = X'48'
         MNOTE 8,'&LABEL INVALID FOR LABEL'
         AGO   .P
.O1      DC    X'1'               LABEL = NL
         AGO   .P
.O2      DC    X'2'                LABEL = SL
         AGO   .P
.O3      DC    X'04'               LABEL = NSL
         AGO   .P
.O4      DC    X'08'               LABEL = SUL
         AGO   .P
.O5      DC    X'10'               LABEL = BLP
         AGO   .P
.O6      DC    X'21'               LABEL = LTM
         AGO   .P
.O7      DC    X'40'               LABEL = AL
         AGO   .P
.O8      DC    X'48'               LABEL = AUL
         AGO   .P
.P       AIF   (T'&EXPDT EQ 'O').Q
         DS    0F
TS&A&X   DC    X'0022'             KEY TO SPECIFY EXPIRATION DATE
         DC    XL2'1'
         DC    XL2'5'              FIELD IS FIVE NUMERICS
DAEX&X   DC    CL5'&EXPDT'         AND IS PUT IN HERE
&A       SETA  &A+1
.Q       AIF   (T'&RETPD EQ 'O').R
         DS    0F
TS&A&X   DC    X'0023'             KEY TO SPECIFY A RETENTION PERIOD
         DC    XL2'1'
         DC    XL2'2'              MAXIMUM LENGTH IS TWO
DARP&X   DC    AL2(&RETPD)         AND IS PLACED HERE.
&A       SETA  &A+1
.R       AIF   (T'&DUMMY EQ 'O').S
         DS    0F
TS&A&X   DC    X'0024'             KEY TO ALLOCATE A DUMMY DATA SET
         DC    XL2'0'
         DC    XL2'0'
         DC    X'0'
&A       SETA  &A+1
.S       AIF   (T'&BLKSIZE EQ 'O').T
         DS    0F
TS&A&X   DC    X'0030'             KEY TO SPECIFY THE BLOCKSIZE
         DC    XL2'1'
         DC    XL2'2'
DABS&X   DC    AL2(&BLKSIZE)
&A       SETA  &A+1
.T       AIF   (T'&FUNC EQ 'O').U
         DS    0F
         AIF   ('&FUNC' EQ 'I').T2
         MNOTE 8,'&FUNC INVALID FOR FUNC'
         AGO   .U
.T2      ANOP
TS&A&X   DC    X'005A'
         DC    XL2'1'
         DC    XL2'1'
         DC    X'80'
&A       SETA  &A+1
.U       AIF   (T'&LRECL EQ 'O').V
         DS    0F
TS&A&X   DC    X'0042'             KEY TO SPECIFY THE LRECL
         DC    XL2'1'
         DC    XL2'2'
         DC    AL2(&LRECL)
&A       SETA  &A+1
.V       AIF   (T'&RLSE EQ 'O').W
         DS    0F
TS&A&X   DC    X'000D'             KEY  TO SPECIFY RELEASE SPACE
         DC    XL2'0'
&A       SETA  &A+1
.W       AIF   (T'&DCB EQ 'O').X
         DS    0F
TS&A&X   DC    X'002C' KEY TO SPECIFY DCB REFERENCE NAME
         DC    XL2'1'
&K       SETA  K'&DCB
         DC    XL2'&K'             LENGTH OF DSN SPECIFIED
DCB&X    DC    C'&DCB'             DSNAME TO COPY DCB FROM
&A       SETA  &A+1
.X       ANOP
.*
.*
.*
.*
         DS    0F
&A2      SETA  1
&A       SETA  &A-1
&NM      SETC  'TS&X'
.CM1     AIF   (&A2 EQ &A).CM2
&NM      DC    A(TS&A2&X)
&NM      SETC  ' '
&A2      SETA  &A2+1
         AGO   .CM1
.CM2     DC    X'80'
         DC    AL3(TS&A2&X)
         MEND
*----------------------------------------------------------------------
*   BW - END OF MACROS
*----------------------------------------------------------------------
         EJECT
*----------------------------------------------------------------------
*
*        INITIALIZATION
*
*----------------------------------------------------------------------
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
AUTO     $PROLOG R11,R12          ENTRY LINKAGE
         USING IHADCB,R10         DCB DSECT
         B     ENTRY010
*XOFFSET  EQU   BWPOST-AUTO
*
BWPOST   STM   R14,R12,12(R13)
         LR    R11,R15
         B     SKOFFST
XOFFSET  EQU   BWPOST-AUTO
SKOFFST  LA    R15,XOFFSET
         SR    R11,R15
         LA    R12,4095
         LA    R12,1(R11,R12)
         LR    R3,R13
         LA    R13,EXITSA
         ST    R3,4(0,R13)
         ST    R13,8(0,R3)
         POST  TIMEECB
         L     R13,4(0,R13)
         LM    R14,R12,12(R13)
         BR    R14
EXITSA   DS    18F
ENTRY010 DS    0H
         L     R1,0(R1)           PARM POINTER
         XR    R2,R2              CLEAR R2
         ICM   R2,3,0(R1)         ANY PARM ?
         BZ    BBB010             NO - NOT STARTED AT IPL
         CH    R2,=H'8'           MAX PARM LENGTH ?
         BNH   AAA010             NO - OK
         WTO   '** AUTO ** - INVALID PARM LENGTH, IGNORED'
         B     BBB010
AAA010   EQU   *
         BCTR  R2,R0              DECR FOR EXECUTE
         EX    R2,MVC010          MOVE PARM TO MEMBER
BBB010   EQU   *
         OPEN  (COMMANDS,,DIR,,INTRDR,(OUTPUT))
         TM    COMMANDS+DCBOFLGS-IHADCB,DCBOFOPN  IS IT OPEN ?
         BZ    ERROR1             NO - ERROR
         BAL   R14,ENTRY999       SET UP THE DATE/TIME/DAY FIELDS
         L     R0,BUFSIZE         MAX BLKSIZE
         GETMAIN R,LV=(0)
         ST    R1,BUFFER          STORE FOR USE AS READ BUFFER
EXIT010  EQU   *
         B     ENTRY020
         LTORG
MVC010   MVC   MEMBER(0),2(R1)
         EJECT
*----------------------------------------------------------------------
*
*        INITIALIZE CONSOLE COMMUNICATION INTERFACE
*
*----------------------------------------------------------------------
ENTRY020 DS    0H
         EXTRACT COMM,'S',FIELDS=(COMM)
         L     R1,COMM            GET ADDRESS OF COMMUNICATIONS FIELDS
         L     R0,0(R1)           GET COMMUNICATIONS ECB ADDRESS
         ST    R0,COMMECB         SAVE ECB ADDRESS IN WAIT LIST
         LA    R9,4(R1)           GET ORIGIN ADDRESS FOR QEDIT
         ICM   R1,15,0(R9)        IS THERE A START COMMAND CIB WAITING
         BZ    AAA020             NO, MUST BE BATCH EXECUTION
         QEDIT ORIGIN=(R9),BLOCK=(R1)  YES, FREE START COMMAND CIB
AAA020   EQU   *
         QEDIT ORIGIN=(R9),CIBCTR=255  ALLOW STACKED COMMANDS (255)
EXIT020  EQU   *
         B     ENTRY030
COMM     DC    F'0'
         LTORG
         EJECT
*----------------------------------------------------------------------
*
*        IPL COMMAND EXECUTION
*
*----------------------------------------------------------------------
ENTRY030 DS    0H
         CLC   MEMBER,BLANKS      ANY PARM ?
         BE    EXIT030            NO - THIS IS NOT IPL START
         BAL   R14,ENTRY900       PROCESS IPL MEMBER
EXIT030  EQU   *
         B     ENTRY040
         LTORG
         EJECT
*----------------------------------------------------------------------
*
*        SET UP TIMER
*
*----------------------------------------------------------------------
ENTRY040 DS    0H
         POINT DIR,=X'00000100'   POINT TO FIRST RECORD IN DIRECTORY
         USING DIRDSECT,R3
         TIME  DEC
         STCM  R0,12,HHMM         STORE TIME HHMM
         UNPK  @TIME(5),HHMM      MOVE TO STIMER TIME FIELD
         MVC   @TIME+4(2),=2C'0'
AAA040   EQU   *
         L     R3,BUFFER          ADDR OF BUFFER
         READ  DIRDECB,SF,DIR,(R3),'S'  READ DIRECTORY
         CHECK DIRDECB            AND OF COURSE WAIT
         XR    R1,R1              CLEAR A WORK REG
         ICM   R1,3,0(R3)         LNGTH OF DATA IN DIR BLK
         LA    R5,0(R1,R3)        END OF DATA IN DIR BLK
         LA    R3,2(R3)           INCR TO FIRST MEMBER NAME
LOOP040A EQU   *
         CLC   DIRNAME,=8X'FF'    END OF MEMBERS ?
         BE    DDD040             YES - CHECK FOR LO MEMBER
         CLI   DIRNAME,C'@'       TOD MEMBER ?
         BNE   CCC040             NO -SKIP IT
         LA    R1,DIRNAME+1       SECOND BYTE OF DIRNAME
         LA    R15,4              LOOP CONTROL - HHMM
LOOP040B EQU   *
         TM    0(R1),X'F0'        NUMERIC ?
         BNO   CCC040             NO - SKIP THIS MEMBER
         LA    R1,1(R1)           BUMP TO NEXT BYTE
         BCT   R15,LOOP040B
         CLC   DIRNAME+6(2),BLANKS  MUST BE BLANKS
         BE    BBB040             YES - OK
         MVC   WTO040+28(8),DIRNAME  NO - INFORM OPER
WTO040   WTO   '** AUTO ** - MEMBER //////// INVALID FOR TOD PROCESSINGX
               , IGNORED'
         B     CCC040
BBB040   EQU   *
         CLC   DIRNAME+1(4),@TIME  MUST BE HIGHER
         BH    GGG040             PROCESS THIS MEMBER
CCC040   EQU   *
         NI    DIRC,X'1F'         TURN OFF UNNEEDED BITS
         XR    R1,R1              CLEAR R1
         IC    R1,DIRC            NUMBER OF USER HALFWORDS
         SLL   R1,1               NUMBER OF USER BYTES
         LA    R3,12(R1,R3)       INCR TO NEXT DIR ENTRY
         CR    R3,R5              PAST END OF DIR BLOCK ?
         BL    LOOP040A           NO - CHECK THIS DIR ENTRY
         B     AAA040             YES - GET NEXT DIR BLOCK
DDD040   EQU   *                  ALSO EODAD FOR DIR READS
         AP    HHMM,=P'1000'      ADD 1 HOUR
         MVI   HHMM+1,X'00'       CLEAR MM
         CP    HHMM,=P'24000'     MIDNITE ?
         BL    EEE040             NO - OK
*    FOLLOWING TWO INSTRUCTIONS ADDED TO CORRECT TIMING PROBLEMS AT
*    MIDNIGHT ON LIGHTLY LOADED HIGH SPEED PROCESSORS
*
         CLC   @TIME(4),=C'2359'  IS IT MIDNIGHT  ????      GRU 01/88
         BE    ENTRY040           YES GET PAST MIDNIGHT     GRU 01/88
*
*
         MVC   @TIME,=C'23595999' SET MIDNIGHT AS TIMER VALUE
         B     III040             GO TO IT
EEE040   EQU   *
         UNPK  @TIME(5),HHMM      SET DEFAULT TO CHECK AGAIN NEXT HOUR
         B     HHH040
GGG040   EQU   *
         MVC   @TIME(4),DIRNAME+1  YES - NEW TIMER VALUE
HHH040   EQU   *
         MVC   @TIME+4(4),=4C'0'
III040   EQU   *
         XC    TIMEECB,TIMEECB
         STIMER  REAL,BWPOST,TOD=@TIME
EXIT040  EQU   *
         B     ENTRY050
         DROP  R3
         LTORG
HHMM     DC    PL3'0'
@TIME    DC    D'0'
         EJECT
*----------------------------------------------------------------------
*
*        WAIT FOR MODIFY OR TIMER POP
*
*----------------------------------------------------------------------
ENTRY050 DS    0H
         WAIT  1,ECBLIST=ECBLIST,LONG=YES  WAIT UNTIL AN ECB IS POSTED
         L     R1,COMMECB         ADDR OF COMMUNICATIONS ECB
         CLI   0(R1),X'00'        CHECK FOR OPERATOR REQUEST
         BNE   ENTRY060           YES--GO TO PROCESS OPER REQ
         CLI   TIMEECB,X'00'      DID THE TIMER POP ?
         BNE   ENTRY300           YES--GO PROCESS TIME REQUEST
         B     ENTRY050           HMMM.. DON'T KNOW, RETRY
         LTORG
ECBLIST  DS    0F
COMMECB  DC    A(0)
         DC    X'80',AL3(TIMEECB)
         EJECT
*----------------------------------------------------------------------
*
*        CHECK FOR OPERATOR REQUEST
*
*----------------------------------------------------------------------
ENTRY060 DS    0H
         L     R3,0(R9)           GET ADDRESS OF THE CIB
         USING CIBSECT,R3         ALLOW ADDRESSABILITY
         CLI   CIBVERB,X'40'      IS IT THE STOP COMMAND ?
         BE    RETURN             YES - TO CLEAN UP ROUTINE
         MVC   COMMDATA,BLANKS    CLEAR THE DATA BUFFER
         IC    R15,CIBDSIZE+1     GET SIZE OF MODIFY DATA
         BCTR  R15,R0             DECREMENT FOR EXECUTE
         EX    R15,MOVE060        MOVE TO COMMDATA
         QEDIT ORIGIN=(R9),BLOCK=(R3)  FREE THE CIB
         OC    COMMDATA,BLANKS    UPPER CASE
         CLI   COMMDATA,C'-'      IS IT A COMMAND REQUEST ?
         BE    ENTRY200           YES - PROCESS IT
         B     ENTRY100           NO - ASSUME JOB
         LTORG
MOVE060  MVC   COMMDATA(0),CIBDATA  MOVE DATA TO WORK AREA
         DROP  R3                 FREE THE USING REGISTER
         EJECT
*----------------------------------------------------------------------
*
*        DETERMINE WHICH DDNAME TO USE FOR SUBMIT
*
*----------------------------------------------------------------------
ENTRY100 DS    0H
         LA    R10,JOBDCB         ADDR OF JOB DCB
         LA    R3,COMMDATA        ADDR OF INPUT DATA
LOOP100A EQU   *
         CLI   0(R3),C' '         END OF DATA ?
         BE    AAA100             YES - DEFAULT DDNAME
         CLC   0(3,R3),=C',D='    IS THIS DDNAME OVERRIDE?
         BE    BBB100             NO, CONTINUE UNTIL ONE IS FOUND
         LA    R3,1(R3)           BUMP TO NEXT CHARACTER
         B     LOOP100A           BRANCH BACK
AAA100   EQU   *
         MVC   0(5,R3),=C',D=00'  ASSUME DEFAULT
BBB100   EQU   *
         TM    DCBOFLGS,DCBOFOPN  JOB CNTL DCB OPEN ?
         BZ    CCC100             NO - OPEN IT
         CLC   OPENDCB,3(R3)      SAME AS LAST ONE ?
         BE    EXIT100            YES - OK
         CLOSE ((R10))            NO - CLOSE IT
         XC    OPENDCB,OPENDCB    CLEAR LAST OPENED
CCC100   EQU   *
         MVC   DADNJCL+3(2),3(R3)   MODIFY DYNAMIC ALLOCATION PARMS
         LA    R4,DSNTABLE          TABLE OF DSNS TO DDNAMES
CCC100L  DS    0H
         CLC   0(2,R4),3(R3)        RIGHT DDNAME SUFFIX ?
         BE    CCC100A              GO DO ALLOCATION
         CLC   0(2,R4),=X'FFFF'     END OF TABLE ?
         BNE   CCC100B              NO BUMP TO NEXT ENTRY
         MVC   CCC100M+33(2),3(R3)  MOVE DD SUFFIX TO WTO
*               89012345678901234567890123456789012345678901234567890
CCC100M  WTO   '** AUTO ** DDNAME SUFFIX // NOT DEFINED IN TABLE USING X
               00 AS A DEFAULT'
         MVC   3(2,R3),=C'00'       SET UP DEFAULT
         B     CCC100
CCC100B  DS    0H
         LA    R4,46(R4)            NEXT ENTRY IN TABLE PLEASE.
         B     CCC100L              TRY THAT ENTRY ON FOR SIZE
CCC100A  DS    0H
         MVC   DADSJCL(44),2(R4)    SAVE THE DATASET TO ALLOCATE.
         LA    R1,JCLALOC
         DYNALLOC
         LTR   R15,R15              ALLOCATION GOOD ?
         BZ    CCC100D
         MVC   DATASET(44),DADSJCL MOVE DSNAME TO TITLE
         WTO   MF=(E,TITLE)
         LH    R6,TITLE            GET LENGTH OF WTO.
         LH    R7,TITLE+2          GET MCS FLAGS FROM WTO
         SH    R6,HALF4            GET LENGTH OF MESSAGE TEXT
         STC   R6,TITLE+3          SAVE LENGTH FOR SDUMP
         SDUMP HDRAD=TITLE+3
         STH   R7,TITLE+2          RESTORE MCS FLAGS FROM WTO.
         B     ENTRY050            GO WAIT FOR MORE WORK
CCC100D  DS    0H
         MVC   DCBDDNAM+3(2),3(R3)  MODIFY DDNAME
         OPEN  ((R10))
         TM    DCBOFLGS,DCBOFOPN  DID IT OPEN THIS TIME ?
         BZ    ERROR2             NO - ERROR
         MVC   OPENDCB,3(R3)      SAVE LAST OPENED
EXIT100  EQU   *
         MVC   0(5,R3),=CL5' '    CLEAR THE 'D=XX' PARM
         B     ENTRY110
         LTORG
         EJECT
*----------------------------------------------------------------------
*
*        SUBMIT EACH MEMBER SPECIFIED IN THE PARMLIST
*
*----------------------------------------------------------------------
ENTRY110 DS    0H
         TM    INTRDR+DCBOFLGS-IHADCB,DCBOFOPN  IS THE INTRDR OPEN ?
         BZ    ENTRY050           NO, IGNORE REQUEST
         LA    R3,COMMDATA        ADDRESS OF FIRST (OR ONLY) OPERAND
AAA110   EQU   *
         LR    R4,R3              COPY TO WORK REG
         SR    R15,R15            CLEAR WORK REGISTER
         LA    R14,9              SET LOOP CONTROL
LOOP110  EQU   *
         CLI   0(R3),C' '         END OF MEMBER NAME?
         BE    BBB110             YES, PROCESS ONLY (LAST) MEMBER
         CLI   0(R3),C','         DELIMITER ?
         BE    BBB110             YES, PROCESS THIS ENTRY
         LA    R3,1(R3)           BUMP TO NEXT CHARACTER
         LA    R15,1(R15)         BUMP CHAR COUNT REG
         BCT   R14,LOOP110        BACK TO CHECK NEXT CHAR
         B     ERROR3             MEMBER NAME LONGER THAN 8 CHARS
BBB110   EQU   *
         BCTR  R15,R0             DECREMENT FOR MOVE
         MVC   MEMBER(8),BLANKS   CLEAR MEMBER NAME AREA
         EX    R15,MOVE110        MOVE MEMBER NAME
         FIND  ((R10)),MEMBER,D
         LTR   R15,R15            MEMBER FOUND ?
         BZ    CCC110             YES - OK
         MVC   WTO110+25(8),MEMBER
*               890123456789012345
WTO110   WTO   '** AUTO ** - JOB //////// NOT FOUND'
         B     EEE110             GO TO EODAD ROUTINE
CCC110   EQU   *
         L     R5,BUFFER          ADDR OF BUFFER
         READ  READDECB,SF,(R10),(R5),'S'
         CHECK READDECB
         LH    R6,DCBLRECL        LRECL
         LH    R7,DCBBLKSI        BLOCK SIZE
         LA    R7,0(R7,R5)        END OF FULL BLOCK
         L     R1,READDECB+16     ADDR OF IOB
         LH    R1,14(R1)          RESIDUAL COUNT FOR SHORT BLOCK
         SR    R7,R1              ADJUSTED BLOCK LENGTH
         SR    R7,R6              DECR TO BEGINNING OF LAST RECORD    *
DDD110   EQU   *
         PUT   INTRDR,(R5)        SUBMIT JCL
         BXLE  R5,R6,DDD110       BUMP TO NEXT REC IN BLOCK AND PUT
         B     CCC110             READ ANOTHER BLOCK
EEE110   EQU   *
         CLI   0(R3),C','         IS THERE ANOTHER MEMBER TO PROCESS ?
         BNE   FFF110             NO, FLUSH THE INTERNAL READER
         LA    R3,1(R3)           BUMP TO START OF NEXT MEMBER
         CLI   0(R3),C' '         NULL MEMBER NAME ?
         BNE   AAA110             NO, PROCESS IT
FFF110   EQU   *
         PUT   INTRDR,EOF         /*EOF
EXIT110  EQU   *                  END OF ALL SUBMITS
         CLOSE ((R10))            CLOSE AND DE-ALLOCATE JOBDCB.
         B     ENTRY050           CHECK FOR ANY MORE COMMANDS
MOVE110  MVC   MEMBER(0),0(R4)    EXECUTED MOVE OF MEMBER NAME
         LTORG
         EJECT
*----------------------------------------------------------------------
*
*        PROCESS COMMAND EXECUTION REQUEST BY OPERATOR
*
*----------------------------------------------------------------------
ENTRY200 DS    0H
         LA    R3,COMMDATA+1      ADDRESS OF FIRST (OR ONLY) OPERAND
AAA200   EQU   *
         LR    R4,R3              COPY TO WORK REG
         SR    R15,R15            CLEAR WORK REGISTER
         LA    R14,9              SET LOOP CONTROL
LOOP200  EQU   *
         CLI   0(R3),C' '         END OF MEMBER NAME?
         BE    BBB200             YES, PROCESS ONLY (LAST) MEMBER
         CLI   0(R3),C','         DELIMITER ?
         BE    BBB200             YES, PROCESS THIS ENTRY
         LA    R3,1(R3)           BUMP TO NEXT CHARACTER
         LA    R15,1(R15)         BUMP CHAR COUNT REG
         BCT   R14,LOOP200        BACK TO CHECK NEXT CHAR
         B     ERROR4             MEMBER NAME LONGER THAN 8 CHARS
BBB200   EQU   *
         BCTR  R15,R0             DECREMENT FOR MOVE
         MVC   MEMBER(8),BLANKS   CLEAR MEMBER NAME AREA
         EX    R15,MOVE200        MOVE MEMBER NAME
         BAL   R14,ENTRY900       PROCESS COMMAND
         CLI   0(R3),C','         IS THERE ANOTHER MEMBER TO PROCESS ?
         BNE   EXIT200            NO, DONE
         LA    R3,1(R3)           BUMP TO START OF NEXT MEMBER
         CLI   0(R3),C' '         NULL MEMBER NAME ?
         BNE   AAA200             NO, PROCESS IT
EXIT200  EQU   *
         B     ENTRY050
         LTORG
MOVE200  MVC   MEMBER(0),0(R4)    EXECUTED MOVE OF MEMBER NAME
         EJECT
*----------------------------------------------------------------------
*
*        PROCESS TIME INTERVAL REQUESTS (THE TIMER HAS POPPED)
*
*----------------------------------------------------------------------
ENTRY300 DS    0H
         MVC   MEMBER,BLANKS      CLEAR
         MVI   MEMBER,C'@'        PREFIX
         MVC   MEMBER+1(4),@TIME  BUILD NAME
         FIND  COMMANDS,MEMBER,D
         LTR   R15,R15            MEMBER FOUND ?
         BNZ   EXIT300            NO, MUST BE NULL TIMER
         BAL   R14,ENTRY900
EXIT300  EQU   *
         B     ENTRY040
         LTORG
         EJECT
*----------------------------------------------------------------------
*
*        PROCESS COMMANDS
*
*----------------------------------------------------------------------
ENTRY900 DS    0H
         ST    R14,ST900          SAVE RETURN ADDR
         LA    R10,COMMANDS       DCB ADDR
         TM    DCBOFLGS,DCBOFOPN  IS THE COMMANDS OPEN ?
         BZ    EXIT900            NO, IGNORE REQUEST
         BAL   R14,ENTRY999       RE-ESTABLISH DATE, TIME, AND DAY
         FIND  COMMANDS,MEMBER,D
         LTR   R15,R15            MEMBER FOUND ?
         BZ    AAA900             YES - PROCESS IT
         MVC   WTO900+25(8),MEMBER
WTO900   WTO   '** AUTO ** - CMD //////// NOT FOUND'
         B     EXIT900            GO TO EODAD ROUTINE
AAA900   EQU   *
         L     R5,BUFFER          ADDR OF BUFFER
         READ  CMDDECB,SF,(R10),(R5),'S'
         CHECK CMDDECB
         LH    R6,DCBLRECL        LRECL
         LH    R7,DCBBLKSI        BLOCK SIZE
         LA    R7,0(R7,R5)        END OF FULL BLOCK
         L     R1,CMDDECB+16      ADDR OF IOB
         LH    R1,14(R1)          RESIDUAL COUNT FOR SHORT BLOCK
         SR    R7,R1              ADJUSTED BLOCK LENGTH
         SR    R7,R6              DECR TO BEGINNING OF LAST RECORD    *
BBB900   EQU   *
         CLI   CONTFLAG,X'FF'     IS THE CONTINUATION FLAG ON ?
         BNE   EEE900             NO, NORMAL PROCESSING
         CLC   0(15,R5),BLANKS    IS IT A VALID CONT RECORD ?
         BE    DDD900             YES, OK
CCC900   EQU   *
         MVC   WTO900A+25(8),MEMBER MOVE MEMBER NAME
WTO900A  WTO   '** AUTO ** - CMD //////// INVALID CONTINUATION, SKIP'
         MVI   CONTFLAG,X'00'     RESET CONT FLAG
         B     JJJ900             SKIP THE RECORD
DDD900   EQU   *
         MVC   CMDBUF+56(56),15(R5)  MOVE COMMAND DATA TO BUFR
         CLI   71(R5),C' '        IS THIS RECORD CONTINUED ?
         BNE   CCC900             YES - ERROR ONLY ONE CONT ALLOWED
         MVI   CONTFLAG,X'00'     RESET THE CONTINUATION FLAG
         B     III900             PROCESS COMPLETE COMMAND
EEE900   EQU   *
         CLI   0(R5),C'*'         COMMENT ?
         BE    KKK900             YES - SKIP IT
         CLC   1(5,R5),BLANKS     ANY MONTH/DAY PROCESSING?
         BE    HHH900             NO, PROCESS DAY OF WEEK ONLY
         CLI   3(R5),C'/'         VALID MONTH/DAY SPECIFICATION?
         BE    FFF900             YES - OK
         MVC   WTO900B+25(8),MEMBER MOVE MEMBER NAME
WTO900B  WTO   '** AUTO ** - CMD //////// INVALID MONTH/DAY, SKIPPED'
         B     KKK900             SKIP THE RECORD
FFF900   EQU   *
         CLC   1(5,R5),=C'**/**'  ANY MONTH/DAY PROCESSING?
         BE    HHH900             YES, PROCESS DAY OF WEEK ONLY
         CLC   1(5,R5),DATE       IS THIS A HIT?
         BE    HHH900             YES, CHECK FOR CORRECT DAY OF WEEK
         CLC   4(2,R5),=C'**'     WAS ANY DAY SPECIFIED ?
         BNE   GGG900             NO, CHECK FOR ANY MONTH
         CLC   1(2,R5),DATE       YES, IS THIS THE CORRECT MONTH
         BE    HHH900             YES, PROCESS DAY OF WEEK NEXT
         B     KKK900             NO - SKIP IT
GGG900   EQU   *
         CLC   1(2,R5),=C'**'     WAS ANY MONTH SPECIFIED ?
         BNE   KKK900             NO - SKIP IT
         CLC   4(2,R5),DATE+3     YES, IS THIS THE CORRECT DAY
         BNE   KKK900             NO - SKIP IT
HHH900   EQU   *
         SR    R1,R1              CLEAR FOR DAY INDEX
         IC    R1,DAY             GET DAY INDEX
         LA    R1,7(R5,R1)        POINT TO TODAYS PROCESS FLAG
         CLI   0(R1),C' '         PROCESS FLAG ON FOR TODAY ?
         BE    KKK900             NO -SKIP IT
         MVC   CMDBUF(56),15(R5)  MOVE COMMAND DATA TO BUFR
         CLI   71(R5),C' '        IS THIS RECORD CONTINUED ?
         BE    III900             NO - PROCESS COMMAND
         MVI   CONTFLAG,X'FF'     SET THE CONTINUATION FLAG
         B     KKK900             GET NEXT RECORD
III900   MODESET KEY=ZERO         GET KEY ZERO AND AUTH
         LM    R0,R1,SVCREGS      GET SVC 34 REGS
         SVC   34                 SCHEDULE COMMAND
         MODESET KEY=NZERO        RELEASE AUTHORIZATION
JJJ900   EQU   *
         MVC   CMDBUF(115),BLANKS CLEAR THE COMMAND BUFFER
KKK900   EQU   *
         BXLE  R5,R6,BBB900       BUMP TO NEXT REC IN BLOCK
         B     AAA900             READ ANOTHER BLOCK
EXIT900  EQU   *                  EODAD
         L     R14,ST900          RETURN ADDR
         BR    R14
         LTORG
ST900    DC    F'-1'
CONTFLAG DC    X'00'
SVCREGS  DC    F'0'               REG 0 FOR SVC 34
         DC    A(SVCCIB)          REG 1 FOR SVC 34
SVCCIB   DC    AL2(118)           MAXIMUM LENGHT OF COMMAND
         DC    H'0'               SVC 34 PADDING
CMDBUF   DC    CL120' '           COMMAND BUFFER
         EJECT
*----------------------------------------------------------------------
*
*        TIMER EXIT
**** THIS ROUTINE REPLAED BY BWPOST AT BEGINNING OF PROGRAM
*----------------------------------------------------------------------
***********************************************************************
*  ENTRY950 DS    0H                                                  *
*           USING *,R15                                               *
*           STM   R14,R12,4(R13)     SAVE REGS                        *
*           L     R12,=A(AUTO)                                        *
*           DROP  R15                                                 *
*           POST  TIMEECB                                             *
*           LM    R14,R12,4(R13)     RESTORE REGS                     *
*  EXIT950  EQU   *                                                   *
*           BR    R14                                                 *
***********************************************************************
         LTORG
         EJECT
*----------------------------------------------------------------------
*
*        DAY/DATE ROUTINE (FROM THE MACHINE CLOCK)
*
*----------------------------------------------------------------------
ENTRY999 DS    0H
         ST    R14,SAVE999        SAVE BAL REG
         $STCK NOGEN              GENERATE THE DATE, TIME AND DAY
EXIT999  EQU   *
         L     R14,SAVE999        RELOAD BAL REG
         BR    R14
         LTORG
SAVE999  DC    F'-1'
DAY      DC    X'99'              RELATIVE DAY OF WEEK (0=MONDAY)
DATE     DC    D'0'               DATE MM/DD/YY
TIME     DC    D'0'               TIME HH:MM:SS
         EJECT
*----------------------------------------------------------------------
*
*        END OF JOB
*
*----------------------------------------------------------------------
RETURN   DS    0H
         TM    DCBOFLGS,DCBOFOPN  IS THE JCL DCB OPEN?
         BZ    AAARET             NO, SKIP THE CLOSE
         CLOSE ((10))             CLOSE THE DCB
AAARET   EQU   *
         TM    COMMANDS+DCBOFLGS-IHADCB,DCBOFOPN  COMMANDS OPEN ?
         BZ    BBBRET             NO, SKIP THE CLOSE
         CLOSE (COMMANDS,,DIR)    CLOSE BOTH DCBS
BBBRET   EQU   *
         TM    INTRDR+DCBOFLGS-IHADCB,DCBOFOPN  HOW ABOUT THE INTRDR ?
         BZ    CCCRET             NO, SKIP THAT CLOSE
         CLOSE (INTRDR)           CLOSE THE INTERNAL READER
CCCRET   EQU   *
         L     R1,BUFFER
         L     R0,BUFSIZE
         FREEMAIN  R,LV=(0),A=(1)
         SR    R15,R15            CLEAR RETCODE
         $EPILOG
         LTORG
         EJECT
*----------------------------------------------------------------------
*
*        ERROR ROUTINES
*
*----------------------------------------------------------------------
ERROR1   DS    0H
         WTO   '** AUTO ** - COMMANDS DD CARD MISSING, TERMINATING'
         B     RETURN
*----------------------------------------------------------------------
ERROR2   DS    0H
         MVC   WTO2+23(2),3(R3)
WTO2     WTO   '** AUTO ** - D=// OPEN ERROR, JOB NOT SUBMITTED'
         B     ENTRY050           PROCESS NEXT COMMAND
*----------------------------------------------------------------------
ERROR3   DS    0H
         MVC   WTO3+25(8),0(R4)
WTO3     WTO   '** AUTO ** - JOB //////// AND ALL FOLLOWING NOT PROCESSX
               ED, LENGTH EXCEEDED'
         B     ENTRY050           PROCESS NEXT COMMAND
*----------------------------------------------------------------------
ERROR4   DS    0H
         MVC   WTO4+25(8),0(R4)
WTO4     WTO   '** AUTO ** - CMD //////// AND ALL FOLLOWING NOT PROCESSX
               ED, LENGTH EXCEEDED'
         B     ENTRY050           PROCESS NEXT COMMAND
         EJECT
*----------------------------------------------------------------------
*
*        STORAGE AREAS
*
*----------------------------------------------------------------------
         DC    C'TIMEECB IS +4 AFTER ='
BUFSIZE  DC    F'32760'
TIMEECB  DC    F'0'
BUFFER   DC    F'0'
MEMBER   DC    CL8' '
OPENDCB  DC    CL2' '
HOLDCIB  DC    CL256' '
COMMDATA DC    CL256' '
BLANKS   DC    CL256' '
*                        1         2         3         4         5
*               123456789012345678901234567890123456789012345678901234
TITLE    WTO   '                                             CAN NOT BEX
                ALLOCATED AT THIS TIME TRY LATER',                     X
               ROUTCDE=(1,2,11),MF=L
DATASET  EQU   TITLE+4
HALF4    DC    H'4'
EOF      DC    CL80'/*EOF'
DSNTABLE DS    0F
         DC    C'00',CL44'SYZYGY.AUTO.JOBS'
         DC    C'01',CL44' '
         DC    C'02',CL44' '
         DC    C'03',CL44' '
         DC    C'04',CL44' '
         DC    C'05',CL44' '
         DC    C'06',CL44' '
         DC    C'07',CL44' '
         DC    C'08',CL44' '
         DC    C'09',CL44' '
         DC    C'10',CL44' '
         DC    X'FFFF'
JCLALOC  DA    DDN=JOB00,                                              X
               DSN=SYZYGY.AUTO.JOBS,                                   X
               STATUS=SHR,                                             X
               UNQSUF=JCL,                                             X
               FREE=CLOSE
         EJECT
         PRINT NOGEN
COMMANDS DCB   DDNAME=COMMANDS,DSORG=PO,MACRF=R,EODAD=EXIT900
DIR      DCB   DDNAME=COMMANDS,DSORG=PS,MACRF=RP,EODAD=DDD040,         X
               LRECL=256,BLKSIZE=256,RECFM=FB
JOBDCB   DCB   DDNAME=JOB00,DSORG=PO,MACRF=R,EODAD=EEE110
INTRDR   DCB   DDNAME=INTRDR,DSORG=PS,MACRF=PM,                        X
               LRECL=80,BLKSIZE=80,RECFM=F
         EJECT
CIBSECT  DSECT ,                  COMMAND INPUT BUFFER MAPPING
CIBCIB   DS    F                  ADDRESS OF NEXT CIB IN CHAIN
CIBVERB  DS    C                  CIB VERB CODE
CIBSIZE  DS    C                  SIZE OF THE COMMAND INPUT BUFFER
CIBRSVD1 DS    H                  RESERVED
CIBRSVD2 DS    C                  RESERVED
CIBTSOID DS    CL3                TSO TERMINAL ID
CIBCONID DS    C                  CONSOLE ID OF STARTED TASK
CIBRSVD3 DS    C                  RESERVED
CIBDSIZE DS    H                  SIZE OF USER DATA WITHIN CIB
CIBDATA  DS    C                  COMMAND DATA AS ENTERED BY OPERATOR
DIRDSECT DSECT
DIRNAME  DS    CL8
DIRTTR   DS    CL3
DIRC     DS    CL1
         DCBD  DSORG=PO
*        IKJTCB DSECT=YES,LIST=YES ENERATES TCB DSECT
         CVT   DSECT=YES          GENERATES CVT DSECT
         END
//LINK.SYSLMOD DD  DSN=SYZYGY.LINKLIB(AUTO),DISP=SHR
