        PRINT ON,GEN
********U C B********TSO VERSION*******************
**FORMAT:  UCB  VOLSER    - WILL RETURN UCB ADDRESS
**FORMAT:  UCB  UCBADDR   - WILL RETURN VOLSER
****
**THIS PROGRAM RETURNS A VOLSER IF A UCB ADDRESS IS ENTERED OR
**A UCB ADDRESS IF A VOLSER IS PASSED.
*****DEPENDENCIES
**THIS PROGRAM IS CURRENTLY USING AN SVC (223) TO GET AUTHORIZATION
**SO THAT IT DOES NOT HAVE TO BE RUN FROM AN AUTHORIZED LOADLIB.
**THE SVC CODE IS IN MEMBER SVC ON THIS PDS OR TAPE
**THE PROGRAM DOES NOT SEEM TO WORK ON TSO IF THE
**SVC CALL IS OMITTED AND THE PROGAM JUST LINKED INTO AN AUTHORIZED
**LIBRARY WITH AC=1. THE SOURCE CODE SUPPLIED IN SVC IS FOR
**SVC 241. THE CSECT NAME SHOULD BE CHANGED AND LINKED UNDER THE
**NEW SVC NUMBER. THE NEW SVC NUMBER SHOULD REPLACE SVC 223 USED
**IN THIS CODE. THE SVC CODE SETS THE AUTH BIT ON IN THE JSCB.
**ITS USAGE IS DOCUMENTED IN MEMBER SVC.
*
**THE PROGRAM MAY BE RUN FROM LINKLIST OR CALLED FROM A CLIST.
**HOWEVER, IT MAY NOT WORK UNDER ISPF IF CALLED
**FROM A CLIST.
***REGISTERS******
BASEREG  EQU   11
LINKREG  EQU   8
UCBREG   EQU   1
CVTREG   EQU   2
RET      EQU   10
TABREG   EQU   15
****************************
        CVT DSECT=YES,LIST=YES
UCB    DSECT
      IEFUCBOB LIST=YES,PREFIX=NO
         IEFZB4D0
         IEFZB4D2
         EJECT
TSOUCB   CSECT
         USING *,15
         STM   14,12,12(13)
         ST    13,SAVEMAIN+8
         LA    13,SAVEMAIN
         LR    BASEREG,15
         USING TSOUCB,BASEREG
         DROP  15
         ST    1,SAVEMAIN
         L      1,0(1)            ADDRESS OF PARM LIST
         LH     2,0(1)          LENGHT OF PARM FIELD
         STH    2,ULEN
*IF INVOKED AS COMMAND PROCESSOR, LEN IS AT 2 AND PARM AT 4 NOT 0-2
         CLI    2(1),C'A'         AND PROGRAM NAME IS PART OF PARM
         BNL    GOTIT
         LA     1,2(1)
         LH     2,0(1)   LEN
         STH    2,ULEN
         L      TABREG,=A(TRANTAB)
         TRT    2(9,1),0(TABREG)  SEARCH FOR DELIMITER IN PGM NAME
         BZ     ENDIT
         LA     3,1(1)      POINT TO PARM FIELD
         TRT    0(7,3),0(TABREG)  SEARCH FOR END OF UCB OR VOLSER
         BZ     ENDIT
         SR     1,3       LEN OF PARM FIELD
         LR     2,1
         STH    2,ULEN
         LR     1,3      BEG OF PARM
         SH     1,=H'2'     -2
         B      GOTIT
GOTIT    DS     0H
         LTR    2,2
         BZ     ENDIT
         BCTR   2,0
         EX     2,MOVEIT       MOVE PARM
         L      TABREG,=A(CONVTAB)  CONVERT LOWER CASE TO UPPER CASE
         TR     PRM,0(TABREG)
         LH     2,ULEN
         MVI    RUNSW,UCBAD
         CH     2,=H'3'
         BNH    *+8
         MVI    RUNSW,VOLSER
         L      LINKREG,=A(ALLOCOUT)
         BALR   RET,LINKREG
         OPEN  (OUTPUT,OUTPUT)
***MUST BE AUTHORIZED TO USE UCBLOOK
         L      LINKREG,=A(AUTHIT)
         BALR   RET,LINKREG
*****FOR DYNAMIC UCBS, USE UCBLOOK MACRO
         USING UCB,UCBREG
         TM  RUNSW,VOLSER    VOLSER CHECK
         BO   VOLLOOK
UL  UCBLOOK NOPIN,DEVNCHAR=PRMP,DYNAMIC=YES,UCBPTR=RETUCBA
         LTR  15,15
           BNZ  PUTIT
          L   UCBREG,RETUCBA
         MVC   RESULT(L'UCBVOLI),UCBVOLI
          B   PUTIT
VOLLOOK   DS   0H
VL  UCBLOOK NOPIN,VOLSER=PRM,DYNAMIC=YES,UCBPTR=RETUCBA
         LTR  15,15
           BNZ  PUTIT
          L   UCBREG,RETUCBA
         MVC   RESULT(L'UCBNAME),UCBNAME
          B   PUTIT
******************************************
*        L     CVTREG,16 CVT POINTER
*        USING CVT,CVTREG
*        L     UCBREG,CVTUCBA  FIRST UCB
*        USING UCB,UCBREG
*       DROP   CVTREG
*        B     FINDIT
*NEXTUCB  DS    0H
*        L     UCBREG,UCBNXUCB
*FINDIT   DS    0H
*        CLI   UCBID,UCBSTND
*        BNE   PUTIT
*         TM    RUNSW,VOLSER  VOL SER  GIVEN?
*        BNO   TESTUCB
*        CLC   UCBVOLI,PRM
*        BNE   NEXTUCB
*        MVC   RESULT(L'UCBNAME),UCBNAME
*        B     PUTIT
*TESTUCB  DS    0H
*        CLC   UCBNAME,PRM
*        BNE   NEXTUCB
*        MVC   RESULT(L'UCBVOLI),UCBVOLI
*        B     PUTIT
PUTIT    DS    0H
         CLI   RESULT,C' '
         BNE   *+10
         MVC   RESULT(10),=CL10'NOT FOUND'
         PUT  OUTPUT,OUTLINE
         B    ENDIT
ENDIT    DS    0H
         CLOSE OUTPUT
         L     13,SAVEMAIN+8
         LM    14,12,12(13)
         SR    15,15
         BR    14
MOVEIT   MVC   PRM(0),2(1)  MOVE COMMAND FROM PARM AREA
*
SAVEMAIN DS   18F
RETUCBA  DS   F
ULEN    DS    H
OUTLINE  DC  CL80' '
       ORG  OUTLINE+1
PRMP     DC   C' '
PRM      DC   CL6' '
         DC   C' '
RESULT   DS   0C
         ORG
RUNSW    DC   X'00'
UCBAD    EQU   X'01'
VOLSER   EQU   X'02'
OUTPUT   DCB   DDNAME=TERMFILP,DSORG=PS,MACRF=PM,RECFM=FA,             XJRE06080
               LRECL=080,BLKSIZE=080
        LTORG
        EJECT
ALLOCOUT DS    0H
         USING  *,LINKREG
         STM  14,12,ALCSAVE+12
         LA    1,RBPOINT        (R1 => A(REQ BLOCK )
         DYNALLOC
         LM    14,12,ALCSAVE+12
         BR    RET
         DS  0F
ALCSAVE  DS  18F
DYNBLKS  EQU  *
RBPOINT  DC   A(RBS)  ADR  REQ BLOCK PASSED IN R1
         ORG   RBPOINT   S99RBP  DSECT
         DC    X'80'   HI ORDER BIT MUST BE ON
          ORG
         DS    0F
RBS     EQU  *
RB       DS    0CL20  S99RB DSECT  LOC BY RBPOINT
RBLN     DC    AL1(RBEL)         LEN OF RB
RBVERB   DC    AL1(S99VRBAL)         FUNC 01-07 01 FOR ALLOC
RBFLAG1  DC    AL1(S99NOMNT) FLAG BYTE-DONT CONSIDER OFFLINE UNITS
RBFLAG2  DS    X       2ND   FLAG BYTE
RBERROR  DS    XL2     ERROR REASON CODE
RBINFO   DS    XL2     INFO  REASON CODE
RBTXTPP  DC    A(TXPOINTS)     LIST OF TEXT PTRS
         DS    F       RESERVED
RBAUTHFL DS    XL4     FLAGS FOR AUTH FUNCTIONS
RBEL     EQU   *-RB
**TEXT UNIT POINTERS
TXPOINTS DS    0F      S99TUPL  DSECT  TU PTR LIST
DDNPOINT DC  A(DDNTEXT)
SYSPOINT DC  A(SYSOTEXT)
TERPOINT DC  A(TERMTEXT)
         ORG  TERPOINT
         DC    X'80'   LAST TEXT UNIT
         ORG
TXUNITS  DS    0F   S99TUNIT DSECT  TEXT UNITS
**DDN TEXT UNIT
DDNTEXT  DS    0F   DDN TEXT UNIT
DDNTXKEY DC    AL2(DALDDNAM)  KEY FOR DDN TEXT
DDNTXNUM DC    XL2'0001'   NUM LENGTH + PARM ENTRIES
DDNTXENT DS    0C
DDNPLEN  DC    AL2(L'DDNTXPRM)  LENGTH OF PARM
DDNTXPRM DC    CL8'TERMFILP'
SYSOTEXT  DS   0F
SYSOXKEY DC    AL2(DALSYSOU) KEY FOR SYSOUT ALLOC
SYSOXNUM DC    XL2'0001'   NUM LENGTH + PARM ENTRIES
SYSOXENT DS    0C
SYSOLEN  DC    AL2(L'SYSOXPRM)  LENGTH OF PARM
SYSOXPRM DC    C'A'          MUST BE 1
TERMTEXT  DS   0F   OMITTED
TERMXKEY DC    AL2(DALTERM) KEY FOR TERM=TS
TERMXNUM DC    XL2'0000'   NUM LENGTH + PARM ENTRIES
TERMXENT DS    0C
TERPLEN  DC    AL2(L'TERTXPRM)  LENGTH OF PARM
TERTXPRM DS    0C       NO PARM
         DC    CL6' '   FOR PATCHING
TXLEN    EQU   *-TXUNITS
DYNEL    EQU  *-DYNBLKS
RBLEN  EQU  (S99RBEND-S99RB)
         LTORG
         DROP  LINKREG
         EJECT
AUTHIT    DS   0H
         USING *,LINKREG
           STM  14,12,LINKSAVE+12
******FOR TSO
***USE SVC 223 TO GET AUTH R4=TCB, R0=1 OR 0 FOR ON/OFF, R1='AUTH'
***SVC USES TCB PROPER+B4 TO GET JSCB ADDR
         L     4,16    CVT  POINTER
         L     4,0(4)  POINT TO TCB
         L     4,4(4) POINT TO TCB PROPER OF ACTIVE TCB
         LA    0,1   SET AUTH ON
         ICM   1,15,=C'AUTH'   CONSTANT FOR SVC
         SVC   223      SET ON AUTH
         MODESET KEY=ZERO,MODE=SUP
        LM   14,12,LINKSAVE+12
         BR   RET
LINKSAVE  DS   18F
           LTORG
         EJECT
TRANTAB  DS   0H
         USING *,TABREG
TRTTAB   DC   256X'FF'      ALPHA CHARS GO IN AS LOWER CASE
         ORG  TRTTAB+X'81'
         DC   9X'00'   A-I
         ORG  TRTTAB+X'91'
         DC   9X'00'   J-R
         ORG  TRTTAB+X'A2'
         DC   9X'00'   S-Z
         ORG  TRTTAB+C'A'
         DC   9X'00'   A-I
         ORG  TRTTAB+C'J'
         DC   9X'00'   J-R
         ORG  TRTTAB+C'S'
         DC   9X'00'   S-Z
         ORG  TRTTAB+C'0'
         DC  10X'00'   0-9
         ORG
         LTORG
         DROP  TABREG
CONVTAB  DS    0H   TRANSLATE LOWER CASE TO UPPER CASE
          USING *,TABREG
CNVTAB   DC  256AL1(*-CNVTAB)
         ORG  CNVTAB+X'81'
         DC   C'ABCDEFGHI'
         ORG  CNVTAB+X'91'
         DC   C'JKLMNOPQR'
         ORG  CNVTAB+X'A2'
         DC   C'STUVWXYZ'
         ORG
         LTORG
         DROP  TABREG
         END
