/** REXX                                                 **/
/**********************************************************/
/** EXEC NAME        : T52ADD                            **/
/** LAST MODIFIED    :  2 Jul 2003                       **/
/**********************************************************/
/** Added smarts to routine to insert the password parm  **/
/** on the create if it doesn't exist and honor it if it **/
/** does dir july 2 2003                                 **/
/**********************************************************/
/** added variable for default department vget           **/
/**********************************************************/
/** changed acid variable manipulation so that the acid  **/
/** variable remains a little more stable.               **/
/**                                .....DIR 19 AUGUST 96 **/
/**********************************************************/
/**********************************************************/
/** ADDED LOGIC TO DETERMINE IF ID HAS DSNS CATLGD IN    **/
/** MASTER CAT YET HAS NO ALIAS        DIR  11 MAY 95    **/
/**********************************************************/
/**********************************************************/
/**                                                      **/
/**----------------INPUT REQUIRED -----------------------**/
/** PASSED ARGS        VALID VALUES                      **/
/** ACID    : THE TSS ACID TO BE ADDED.                  **/
/** MODE    : BATCH OR ONLINE                            **/
/** ISPF VGETS         VALID VALUES                      **/
/** TO NUMEROUS TO MENTION LOTS OF TSSINI VALUES         **/
/**----------------OUTPUT PRODUCED-----------------------**/
/** VALID CONDITION CODES - 00                           **/
/** ISPF VARIABLES        - SECRC                        **/
/**                         DATAAREA                     **/
/**----------------REQUIREMENTS--------------------------**/
/** CAN RUN STANDALONE : NO                              **/
/** ISPF ENVIRONMENT   : YES                             **/
/** EXECS CALLED WITHIN THIS EXEC :         TSSAUDU      **/
/**                                                      **/
/**********************************************************/
ARG ACID MODE
SAY "T52ADD: CONTROL RECEIVED PARMS = " ACID MODE
ADDRESS ISPEXEC
"VGET AUD       PROFILE"
"VGET DEF       PROFILE"
"VGET DS1       PROFILE"
"VGET DS2       PROFILE"
"VGET DS3       PROFILE"
"VGET DS4       PROFILE"
"VGET DS5       PROFILE"
"VGET DS6       PROFILE"
"VGET MSG2      PROFILE"
"VGET PROD      PROFILE"
"VGET TSOSIZE   PROFILE"
"VGET TSOUNIT   PROFILE"
"VGET TSOPROC   PROFILE"
"VGET TVER      PROFILE"
SIGNAL OFF ERROR
/**********************************************************/
/** below I am checking that the id doesnot have dsns    **/
/** cataloged to the master catalog. if it does the      **/
/** user wont be able to catalog his own datasets. this  **/
/** will cause allocate errors that are hard to track.   **/
/** i may make this a seperate callable exec but for     **/
/** now it is included here    dave 11 may 95            **/
/**********************************************************/
DATEHOLD = DATE('U')
MONTHNUM = SUBSTR(DATEHOLD,1,2)
ADDRESS TSO "%T52GDAT "ACID "NAME"
OLDID = RC
outct = 40
BOOK = "NIL"
BOOK1 = "NIL"
SAY "TSA101D: T52ADD  EXISTANCE CHECK RC IS " OLDID
IF OLDID = 0 THEN DO
  msg2 = "This id exists currently, readdition will fail upon",
  "execution"
  ADDRESS ISPEXEC "VPUT MSG2 PROFILE"
END
/**************************************/
/* get the default department         */
/**************************************/
ADDRESS ISPEXEC "VGET DEFDEPT PROFILE"
ADDRESS ISPEXEC "VGET TSOALIAS PROFILE"
IF TSOALIAS = "YES" THEN DO
  ADDRESS TSO "LISTC ENT('"ACID"')"
  SAY "TSA101D: T52ADD  ALIAS CHECK IS " RC
  ALIAS_CODE = RC
  ADDRESS TSO "LISTC LEVEL('"ACID"')"
  SAY "TSA101D: T52ADD  LEVEL CHECK IS " RC
  LEVEL_CODE = RC
  IF (ALIAS_CODE ¬= 0) & (LEVEL_CODE =0)  THEN DO
    msg2 = "The id has no alias, yet has dsns cataloged, expect",
    "severe allocation errors"
    ADDRESS ISPEXEC "VPUT MSG2 PROFILE"
  END
END
SIGNAL ON ERROR NAME ABORT
/**********************************************************/
/** END OF DSN ALIAS / LEVEL CHECKING                    **/
/**********************************************************/
LINE.  = " "
ALEN   = LENGTH(ACID)
BILLA  = "N"
CODE   = 0
CURSOR = "NAME"
DEFINE = "NAME('"NAME"') DEPT(D0) TYPE(USER)"
FIRST2 = SUBSTR(ACID,1,2)
GOTDEF = "NO"
LOGON  = TSOPROC
NDEPT  = FIRST2||"DEPT"
if defdept /= "NONE" then ndept = defdept
PROF   = " "
PWORD  = "PASSWORD,60,EXP"
SECDEF. = " "
secrc  = "T52ADD did not complete normally. please check the id."
S2     = ALEN - 1
TYPE   =  "USER"
WARNED = "NO"
"VPUT SECRC PROFILE"
PW = "PASSWORD"
DPNL   = "SPIADD"
BASIC_ACID = " "
AUDU.0 =   1
AUDU.1 =   "%ADDLOCAL" ACID NDEPT
DEFINE = "NAME('"NAME"') DEPT("NDEPT") TYPE(USER) PASSWORD("PW",60,EXP)"
/****************************************************/
/** the tso authority stream is below              **/
/****************************************************/
/*                                                                  */
/*batchtso.0 = 3                                                    */
/*batchtso.1 = " "                                                  */
/*batchtso.2 = "ISPSTART CMD(%"TVER"TSO DEFAULTS +"                 */
/*batchtso.3 = " "ACID LOGON BOOK1 TSOUNIT TSOSIZE") NEWAPPL(TSS)"  */
/*onlintso.0 = 1                                                    */
/*onlintso.1 ="%"TVER"TSO DEFAULTS" ACID LOGON BOOK1 TSOUNIT TSOSIZE*/
/*                                                                  */
/****************************************************/
/** beginning of the panel loop                    **/
/** beginning of the panel loop                    **/
/****************************************************/
AGAIN: NOP
CALL FILLSCR
"DISPLAY PANEL("DPNL")  CURSOR("CURSOR")"
RCODE = RC
if enum = " " then enum = "000000    initial"
NAME = TRANSLATE(NAME," ","'") /* REPLACE TICS WITH SPACES */
SAY "TSA101D: T52ADD  PANEL RCODE IS "RCODE
IF OPTION = "X" THEN SIGNAL ABORT
IF OPTION = "END" THEN SIGNAL ABORT
IF RCODE = 8 THEN SIGNAL ABORT
IF RCODE = 4 THEN SIGNAL ERRMSG
  /******************************************************/
  /******************************************************/
  /******************************************************/
  /******************************************************/
GETNAME: NOP
CNT1 = 0
NORMAL: NOP
MEMBER = mem
IF MEM = "" THEN MEMBER = NDEPT
mem = member
SAY "TSA101D: CHECKING FOR DEFAULT DEPT AUTHORITIES" DEF"("MEMBER")"
IF SYSDSN("'"DEF"("MEMBER")'") = "OK" THEN DO
  say "tsa101d: T52ADD  default authority group found for in " def bin
  address tso "alloc ddn(secdef) dsn('"def"("member")') shr reus"
  address mvs "execio * diskr secdef ( finis stem secdef.)"
  address tso "free ddn(secdef)"
  /******************************************************/
  /***                                                ***/
  /*** NOTE: THE FIRST LINE OF THE DSN MUST HAVE A    ***/
  /*** CREATE PARM IN IT! IF THERE IS NO MEMBER I     ***/
  /*** ASSIGN A VALUE TO SECDEF.1                     ***/
  /***                                                ***/
  /******************************************************/
  SECDEF.1 = "NAME('"NAME"')" SECDEF.1
  GOTDEF = "YES"
END
ELSE Do
  secdef.1 = define /* if no member we just create a 1 liner */
  if enum /= " " then
    secdef.2 = "TSS add("acid") instdata('"enum"')"
END
say "T52ADD: secdef.1 is" secdef.1
say "T52ADD: secdef.2 is" secdef.2
DO T = 1 TO 40
  /******************************************************/
  /***                                                ***/
  /*** ONCE HERE THERE HAS TO BE A SECDEF.1 VALUE AND ***/
  /*** IT HAS IS    COMPRISED OF VALID 'CREATE' PARMS ***/
  /*** FOR TOP SECRET. THE REST OF THE SECDEF. STRING ***/
  /*** MAY CONTAIN commands to be issued by the       ***/
  /*** system sight unseen.                           ***/
  /******************************************************/
  if index(secdef.t,"$$") /= 0 then do
    say " T52ADD: $$ substitution string found in secdef"
    parse var secdef.t q1 '$$' q2
    secdef.t = q1||acid||q2
    say " T52ADD: resolved secdef is "secdef.t
  end
  if index(secdef.t,"$DEPT$") /= 0 then do
    say " T52ADD: $DEPT$ substitution string found in secdef"
    parse var secdef.t q1 '$DEPT$' q2
    secdef.t = q1||ndept||q2
    say " T52ADD: resolved secdef is "secdef.t
  end
  if index(secdef.t,"$ENUM$") /= 0 then do
    say " T52ADD: $ENUM$ substitution string found in secdef"
    parse var secdef.t q1 '$ENUM$' q2
    secdef.t = q1||Enum||q2
    say " T52ADD: resolved secdef is "secdef.t
  end
  if index(secdef.t,"$MEM$") /= 0 then do
    say " T52ADD: $MEM$ substitution string found in secdef"
    parse var secdef.t q1 '$MEM$' q2
    secdef.t = q1||member||q2
    say " T52ADD: resolved secdef is "secdef.t
  end
  if index(secdef.t,"$OPID$") /= 0 then do
    say " T52ADD: $OPID$ substitution string found in secdef"
    parse var secdef.t q1 '$OPID$' q2
    opid = substr(acid,5,3)
    secdef.t = q1||opid||q2
    say " T52ADD: resolved secdef is "secdef.t
  end
  LINE.T =  strip(secdef.t)
  say t line.t
END
SCRDISP: NOP
CALL FILLSCR
MSG="CHANGE DEFAULT COMMAND STREAM IF NEEDED AND HIT ENTER."
IF GOTDEF = "NO" THEN
  MSG="NO DEFAULT AUTHORITY STREAM FOUND. ENTER COMMANDS."
PNLDISP: NOP
"ISPEXEC DISPLAY PANEL("DPNL")"
RCODE = RC
SAY "T52ADD: PANEL RCODE IS "RCODE
IF MEM /= MEMBER THEN SECDEF. = " "
IF MEM /= MEMBER THEN SIGNAL NORMAL
NAME = SUBSTR(NAME                  ,1,20)
CALL FILLDOT
IF RCODE = 8 THEN SIGNAL ABORT
IF RCODE = 4 THEN SIGNAL ABORT
IF OPTION = "X" THEN SIGNAL ABORT
IF OPTION = "END" THEN SIGNAL ABORT
/******************************************************************/
/******************************************************************/
COMMIT: NOP
/*
DEFINE = "NAME('"NAME"') DEPT("NDEPT") TYPE(USER)"
DEFINE = DEFINE "              "
LINE.1 = DEFINE
*/
IF PW ¬= " " THEN PWORD = PW||",60,EXP"
ELSE PW = "PASSWORD"
IF MODE = "BATCH" THEN DO
  SAY "T52ADD: BATCH MODE DETECTED. ALLOCATING " DS5 "TO BJCL DDN"
  ADDRESS TSO "ALLOC DDN(BJCL) DSN("DS5") MOD REUS"
  BJCL.1= "TSS CREATE("ACID") password("pword")  +"
  ADDRESS MVS "EXECIO 1 DISKW BJCL ( FINIS STEM BJCL.)"
  ADDRESS MVS "EXECIO "OUTCT" DISKW BJCL ( FINIS STEM LINE.)"
  /**********************************************************/
  /*ADDRESS MVS,                                               */
  /* "EXECIO "batchtso.0" DISKW BJCL ( FINIS STEM batchtso.)"  */

  SAY "T52ADD: WRITING DATA TO BJCL DDNAME"
  ADDRESS MVS "EXECIO * DISKW BJCL ( FINIS STEM AUDU.)"
  CODE = RC
  IF CODE = 0 THEN SECRC ="BATCH FILE UPDATED WITH ADD COMMANDS."
  SIGNAL OFF ERROR
  SIGNAL BYE1
END
TSSCMD = "YES"
ADDRESS TSO
SAY "T52ADD: ISSUING THE TSS ADD COMMAND FOR THE ID"
if index(line.1,"PASSWORD") = 0 then do
  line.1 = line.1 "PASSWORD("PW",60,EXP)"
  SAY "T52ADD: Inserting default password into create stream"
end
SAY "T52ADD: TSS CRE("ACID")" LINE.1
"TSS CREATE("ACID")"  LINE.1
CODE = RC
SAY "T52ADD: THE TSS ADD COMMAND FINISHED WITH A RCODE OF " CODE
SIGNAL OFF ERROR
DO T = 2 TO OUTCT
   say "T52ADD: issuing command:" line.t
   IF LINE.T < > " " THEN
   ADDRESS TSO " "LINE.T
END
/* ADDRESS TSO "%TSSAUDU " ACID    */
SPACES= "                                                          "
SYSDATE = DATE(U)
BYE:  NOP
SIGNAL OFF ERROR
IF  MODE= "SKIPIT" THEN DO
  SAY "T52ADD: VERSION 5.2 TSO DEFAULTS BEING SET"
  BOOK1 = WORD(BOOK,1)
  rcode = 0
  do cmdnum = 1 to audu.0
    say "T52ADD: issuing cmd : "onlintso.cmdnum
    address tso onlintso.cmdnum
    holdrc = rc
    rcode = holdrc + rcode
    say "T52ADD: rcode from cmd is " holdrc
  end
  do cmdnum = 1 to audu.0
    say "T52ADD: issuing cmd : "audu.cmdnum
    address tso audu.cmdnum
    holdrc = rc
    rcode = holdrc + rcode
    say "T52ADD: rcode from cmd is " holdrc
  end
  ADDRESS ISPEXEC
  IF RCODE > 0 THEN DO
    "VGET SECRC PROFILE"
    msg2 = "defaults tso setup has failed, check book authorities"
    "VPUT MSG2 PROFILE"
    SIGNAL BYE1
  END
  SAY "T52ADD: VERSION 5.0 TSO DEFAULTS HAVE BEEN SET"
END
JUSTUADS: NOP
CODE = RC
SIGNAL ON ERROR NAME BADALIAS

if code = 0 then secrc ="TSA202I: ACID added. no errors encountered."
BYE1:  NOP
SIGNAL OFF ERROR
ADDRESS ISPEXEC
"VPUT SECRC PROFILE"
 DATAAREA = ACID
"VPUT DATAAREA PROFILE"
EXIT 0
ERRMSG: NOP
  CODE = 9 /* TELL THE BYE ROUTINE THERE IS AN ERROR.*/
  secrc= "TSA201E: Errors encountered. check acid status "
  SIGNAL BYE1
BADALIAS: NOP
  IF CODE = 0 THEN
    secrc= "alias addition failed check acid status "
  else secrc = secrc "and alias addition failed"
  CODE = 9 /* TELL THE BYE ROUTINE THERE IS AN ERROR.*/
  SIGNAL BYE1
ABORT: NOP
  CODE = 9 /* TELL THE BYE ROUTINE THERE IS AN ERROR.*/
  secrc= "TSA200E: Function aborted. acid not added. SIGL:" sigl
  SIGNAL BYE1
/****************************************************************/
/*  ERROR ROUTINE MESSAGE DISPLAYS.........DIR                  */
/****************************************************************/
FILLDOT: PROCEDURE EXPOSE LINE1 LINE2 LINE3 LINE4 LINE5 LINE6,
  LINE7 LINE8 LINE9 LINE10 LINE11 LINE12 LINE13 LINE14 LINE15 LINE.
  LINE.1 = LINE1
  LINE.2 = LINE2
  LINE.3 = LINE3
  LINE.4 = LINE4
  LINE.5 = LINE5
  LINE.6 = LINE6
  LINE.7 = LINE7
  LINE.8 = LINE8
  LINE.9 = LINE9
  LINE.10= LINE10
  LINE.11= LINE11
  LINE.12= LINE12
  LINE.13= LINE13
  LINE.14= LINE14
  LINE.15= LINE15
RETURN
FILLSCR: PROCEDURE EXPOSE LINE1 LINE2 LINE3 LINE4 LINE5 LINE6,
  LINE7 LINE8 LINE9 LINE10 LINE11 LINE12 LINE13 LINE14 LINE15 LINE.
  LINE1 = LINE.1
  LINE2 = LINE.2
  LINE3 = LINE.3
  LINE4 = LINE.4
  LINE5 = LINE.5
  LINE6 = LINE.6
  LINE7 = LINE.7
  LINE8 = LINE.8
  LINE9 = LINE.9
  LINE10= LINE.10
  LINE11= LINE.11
  LINE12= LINE.12
  LINE13= LINE.13
  LINE14= LINE.14
  LINE15= LINE.15
RETURN
