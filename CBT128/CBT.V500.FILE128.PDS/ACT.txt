*          DATA SET WHFACT     AT LEVEL 009 AS OF 03/31/89
*          DATA SET WHFACT     AT LEVEL 005 AS OF 07/18/86
*          DATA SET TOACT      AT LEVEL 030 AS OF 01/24/85
***********************************************************************
*                                                                     *
*    PROGRAM NAME:                ACT - BASED ON CHIMP                *
*                                                                     *
*    AUTHOR:                      FRANCIS                             *
*                                                                     *
*    PURPOSE:                     TSO ACTIVITY DISPLAY COMMAND        *
*                                                                     *
*    DATE WRITTEN:                03/11/83                            *
*                                                                     *
*    SOURCE LIBRARY:              SKG.SYSPAN (PANDD7)                 *
*                                                                     *
*    LOAD LIBRARY:                SYS1.COMMON.CMDLIB                  *
*                                                                     *
*    ATTRIBUTES:                  STD                                 *
*                                                                     *
*    UPDATES:             02/84 - ADD FULL SCREEN AND PF3 SUPPORT     *
*                         07/86 - UPDATE FOR MVS/XA                   *
*                               - UPDATE FOR XA 2.2                   *
*                                                                     *
*    INPUT:                       COMMANDS FROM TSO TERMINAL          *
*                                                                     *
*    OUTPUT:                      SCREEN SHOWING ACTIVE TASKS         *
*                                                                     *
*    MESSAGES:                    NONE                                *
*                                                                     *
*    METHOD:                                                          *
*                                                                     *
*        LOOP THROUGH ASCB'S USING THE ASVT.  FORMAT A LINE ON THE    *
*        OUTPUT SCREEN FOR EACH SELECTED MEMORY.  THE DISPLAY MAY     *
*        BE CHANGED THROUGH TERMINAL INPUT AND IS CONTROLED BY THE    *
*        VARIABLE 'FLAG'.                                             *
*                                                                     *
*    REGISTER USAGE:                                                  *
*                                                                     *
*        R0  - LINKAGE                                                *
*        R1  - LINKAGE, WORK                                          *
*        R2  - WORK                                                   *
*        R3  - COUNTER OF ASCB'S TO PROCESS                           *
*        R4  - POINTER INTO ASVT, WORK                                *
*        R5  - BASE FOR ASCB, WORK                                    *
*        R6  - BASE FOR OUCB, WORK                                    *
*        R7  - WORK, BASE FOR CSCB                                    *
*        R8  - WORK                                                   *
*        R9  - WORK                                                   *
*        R10 - WORK                                                   *
*        R11 - WORK                                                   *
*        R12 - ADDRESS OF BUFFER WHILE BUILDING OUTPUT                *
*        R13 - SAVE AREA, PROGRAM BASE                                *
*        R14 - LINKAGE                                                *
*        R15 - LINKAGE                                                *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*
* THIS COMMAND HAS SEVERAL DIFFERENT MODES OF DISPLAY:
*
*     I   - THIS MODE WILL DISPLAY ONLY THOSE MEMORIES WHICH ARE EITHER
*           SWAPPED IN AND RUNNING OR SWAPPED OUT BUT READY TO RUN.
*     O   - (DEFAULT) - THIS MODE WILL DISPLAY ALL ACTIVE MEMORIES IN
*           THE SYSTEM, REGARDLESS OF LOCATION.
*
*   TO SWITCH BETWEEN MODES, TYPE EITHER I OR O.
*
*
*      YOU MAY ALSO ENTER OPTIONS TO RESTRICT THE OUTPUT TO ONLY
*      TSO MEMORIES (IN EITHER IN/OUT OR WAITING STATUS ACCORDING
*      TO YOUR USE OF THE I AND O OPTIONS ABOVE) OR ONLY STARTED
*      TASK/BATCH JOB MEMORIES:
*
*     T  - RESTRICTS THE OUTPUT TO ONLY TSO MEMORIES
*
*     B  - RESTRICTS THE OUTPUT TO BATCH MEMORIES
*
*     S  - RESTRICTS THE OUTPUT TO STARTED TASK AND ONLINE MEMORIES
*
*     A  - GIVES ALL THE ABOVE
*
*   TO EXIT FROM THIS COMMAND, SIMPLY TYPE E
*
***********************************************************************
ACT      TITLE 'ACT - MAIN STORAGE MONITOR FOR TSO - 3270S'
ACT      SYSENTER SYMREG=YES
ACT      AMODE 31
ACT      RMODE 24
         PRINT NOGEN
         STFSMODE ON,INITIAL=YES   SET FULLSCREEN MODE
         EJECT
#ACT0010 DS    0H
         SR    R1,R1               R1<--A(PSA)
         USING PSA,R1
         L     R1,FLCCVT           R1<--A(CVT)
         USING CVT,R1
         L     R1,CVTASVT          R1<--A(ASVT)
         USING ASVT,R1
         L     R3,ASVTMAXU      MAXIMUM NUMBER OF ENTRIES
         LA    R4,ASVTFRST      FIRST ENTRY
         LA    R12,BUFFER       LOAD ADDRESS OF OUTPUT BUFFER.
         DROP  R1
***********************************************************************
*    R4 CONTAINS THE POINTER INTO THE ADDRESS SPACE VECTOR TABLE.
*    IF THE HIGH ORDER BIT IS ON THEN THE ASID IS AVAILABLE AND
*    NOT ASSIGNED TO A JOB OR TASK.
***********************************************************************
#ACT0020 DS    0H
         LA    R4,4(,R4)           BUMP BY FOUR.
         ICM   R5,B'1111',0(R4)    R5<--A(ASCB).
         BM    #ACT0350            BRANCH IF NOT IN USE.
         USING ASCB,R5
         MVI   LINE+2,C' '         CLEAR OUTPUT LINE TO BLANKS.
         MVC   LINE+3(78),LINE+2   DITTO
***********************************************************************
*    TEST FOR SWAPPED IN/SWAPPED OUT
***********************************************************************
         L     R6,ASCBOUCB        R6<--A(OUCB)
         USING OUCB,R6
         TM    OUCBQFL,OUCBLSW    CHECK FOR A LOGICAL SWAP
         BO    #ACT0030           CONSIDER LOGICAL SWAPS AS BEING IN
         TM    ASCBRCTF,ASCBOUT   TEST FOR SWAPPED OUT.
         BNO   #ACT0030           BRANCH IF SWAPPED IN
         TM    FLAG,O             SWAPPED OUT MEMORIES REQUESTED ???
         BNO   #ACT0350           ...NO: SKIP THIS ASCB
#ACT0030 DS    0H
         MVC   JOB(8),=CL8'STARTING'  TO BYPASS GARBAGE
         ICM   R7,B'1111',ASCBJBNI    LOAD PTR TO JOBNAME.
         BZ    #ACT0033            BRANCH IF ZERO POINTER.
         MVC   JOB,0(R7)           OTHERWISE, MOVE IN JOBNAME.
         S     R7,=A(CHKEY-CSCB)   POINT TO START OF CSCB
         B     #ACT0036            BYPASS FURTHER JOBNAME PROCESSING.
#ACT0033 DS    0H
         ICM   R7,B'1111',ASCBJBNS    1TR IF S/M/L.
         BZ    #ACT0036            BYPASS THIS MOVE IF POINTER IS ZERO.
         MVC   JOB,0(R7)           MOVE IN JOBNAME FOR S/M/L MEMORIES.
         S     R7,=A(CHCLS-CSCB)   POINT TO START OF CSCB
         CLI   JOB,C'*'           IS THIS THE MASTER SCHEDULER ???
         BNE   #ACT0036           ...NO: CONTINUE ON
         SR    R7,R7              MASTER SCHED DOES NOT POINT TO CSCB
         EJECT
***********************************************************************
*    TEST FOR TSO USER (LOGON CREATED USER BIT)
***********************************************************************
#ACT0036 DS    0H
         TM    OUCBYFL,OUCBLOG    CHECK FOR TSO (LOGON CREATED)
         BNO   #ACT0040           BRANCH IF NOT TSO
         TM    FLAG,T             TSO MEMORIES REQUESTED ???
         BNO   #ACT0350           ...NO: SKIP THIS ASCB
         LA    R7,8(R7)           READJUST CSCB POINTER
         MVI   TYPE,C'T'          SHOW TSO MEMORY
         B     #ACT0060           ..YES: PROCESS THIS ASCB
***********************************************************************
*    TEST FOR STARTED TASK OR MOUNT (NON-INITIATED JOB)
***********************************************************************
#ACT0040 DS    0H
         LTR   R7,R7              WAS CSCB FOUND ???
         BZ    #ACT0045           ...NO: ASSUME STC
         USING CSCB,R7
         CLI   CHTRKID,CHJOBID    INITIATED JOB ???
         BE    #ACT0050           ..YES: GO TEST FOR IT
#ACT0045 DS    0H
         TM    FLAG,S             STC/MOUNT MEMORIES REQUESTED ???
         BNO   #ACT0350           ...NO: SKIP THIS ASCB
         CLC   JOB,=CL8'INIT'     CHECK FOR INITIATOR
         BE    #ACT0350           NEVER SHOW INACTIVE INITIATORS
         MVI   TYPE,C'S'          SHOW STC MEMORY
         B     #ACT0060           ..YES: PROCESS THIS ASCB
***********************************************************************
*    BATCH JOB - CHECK JOBNAME FOR SPECIAL PROCESSING
***********************************************************************
#ACT0050 DS    0H
         CLI   CHVCD,X'04'        CHECK TO SEE IF BATCH JOB OR S/M/L
         BE    #ACT0045           JOB IS START OR MOUNT
         CLI   CHVCD,X'0C'        CHECK TO SEE IF BATCH JOB OR S/M/L
         BE    #ACT0045           JOB IS START OR MOUNT
         MVI   TYPE,C'B'          SHOW BATCH MEMORY
         CLC   JOB,=CL8'CICS'     CICS ???
         BE    #ACT0053           ..YES: SPECIAL TEST
         CLC   JOB,=CL8'CICSTEST' CICSTEST ???
         BE    #ACT0053           ..YES: SPECIAL TEST
         CLC   JOB,=CL8'CICSAP'   CICSAP ???
         BE    #ACT0053           ..YES: SPECIAL TEST
         CLC   JOB,=CL8'NCCF'     NCCF ???
         BE    #ACT0053           ..YES: SPECIAL TEST
         CLC   JOB,=CL8'HCF'      HCF ???
         BE    #ACT0053           ..YES: SPECIAL TEST
         CLC   JOB,=CL8'PHOENIX'  PHOENIX ???
         BE    #ACT0053           ..YES: SPECIAL TEST
         B     #ACT0056           ...NO: CONTINUE ON
         EJECT
***********************************************************************
*    ONLINE JOB - SHOW ONLY IF STARTE TASKS AND MOUNTS WERE REQUESTED
***********************************************************************
#ACT0053 DS    0H
         TM    FLAG,S             STC/MOUNT/ONLINE REQUESTED ???
         BNO   #ACT0350           ...NO:SKIP IT
         B     #ACT0060           ..YES: PROCESS THIS ASCB
***********************************************************************
*    BATCH JOB - CHECK TO SEE IF BATCH JOBS ARE TO BE DISPLAYED
***********************************************************************
#ACT0056 DS    0H
         TM    FLAG,B             DISPLAY BATCH MEMORIES ???
         BNO   #ACT0350           ...NO: SKIP IT
***********************************************************************
*    MEMORY IS TO BE DISPLAYED - BUILD THE OUTPUT LINE FOR THIS MEMORY
*    JOBNAME HAS BEEN SET - GET STEP AND PROCSTEP
***********************************************************************
#ACT0060 DS    0H
         LTR   R7,R7              WAS CSCB FOUND ???
         BZ    #ACT0120           ...NO: SKIP MOVES
         CLI   CHVCD,X'04'        CHECK TO SEE IF BATCH JOB OR S/M/L
         BE    #ACT0090           JOB IS START OR MOUNT
         CLI   CHVCD,X'0C'        CHECK TO SEE IF BATCH JOB OR S/M/L
         BE    #ACT0090           JOB IS START OR MOUNT
         CLI   CHSTEP,X'00'       IS STEPNAME GIVEN ??
         BE    #ACT0100           ...NO
         MVC   STEP(8),CHSTEP     STEPNAME IS CHSTEP
         B     #ACT0100           BYPASS FURTHER CSCB PROCESSING.
#ACT0090 DS    0H
         CLI   CHKEY,X'00'        IS STEPNAME GIVEN ??
         BE    #ACT0100           ...NO
         MVC   STEP(8),CHKEY      STEPNAME IS CHKEY
#ACT0100 DS    0H
         CLI   TYPE,C'T'                 TSO USER ???
         BNE   #ACT0110                  ...NO: GO GET PROCSTEP
         MVC   PROCSTEP,=CL8'TSO USER'   ..YES: SHOW IT
         CLI   CHCLS,X'00'               IS STEP GIVEN ??
         BE    #ACT0120                  ...NO
         MVC   STEP,CHCLS                ..YES: SET STEP
         B     #ACT0120           CONTINUE ON
#ACT0110 DS    0H
         CLI   CHPROCSN,X'00'     IS PROCSTEP GIVEN ??
         BE    #ACT0120           ...NO
         MVC   PROCSTEP,CHPROCSN  SET PROCSTEP
         DROP  R7
         EJECT
***********************************************************************
*    SHOW IF JOB IS SWAPPED IN OR OUT, OR IS IN TRANSITION
***********************************************************************
#ACT0120 DS    0H
         MVI   LINE+1,X'E8'        DEFAULT TO HI-INTENSITY PROTECTED.
         TM    OUCBQFL,X'E0'       CHECK FOR TRANSITIONING STATUS
         BZ    #ACT0122            BRANCH IF MEMORY NOT TRANSITIONING.
         MVC   STATUS,=CL3'<->'    INDICATE TRANSITIONING.
         B     #ACT0160            BY-PASS REST OF POSITION PROCESSING.
#ACT0122 DS    0H
         TM    OUCBQFL,OUCBLSW     CHECK FOR LOGICAL SWAP INDICATOR.
         BZ    #ACT0124            IF NOT, GO CHECK SWAP STATUS
         MVC   STATUS,=CL3'LSW'    MOVE IN LOGICAL SWAP INDICATOR.
         B     #ACT0160            BY-PASS REST OF POSITION PROCESSING.
#ACT0124 DS    0H
         TM    ASCBRCTF,ASCBOUT    TEST FOR SWAPPED OUT
         BO    #ACT0130            BRANCH IF SWAPPED OUT.
         CLC   OUCBNDS,=H'0'       TEST FOR NON-SWAP
         BE    #ACT0126            BRANCH IF NOT NON-SWAP.
         MVC   STATUS,=CL3'N/S'    MOVE IN NON-SWAP INDICATION.
         B     #ACT0160            BY-PASS REST OF POSITION PROCESSING.
#ACT0126 DS    0H
         MVC   STATUS,=CL3'IN'     SET MEMORY POSITION TO IN.
         B     #ACT0160            BY-PASS REST OF POSITION PROCESSING.
#ACT0130 DS    0H
         MVI   LINE+1,X'60'        MOVE IN 3270 LOW-INTENSITY ATTR BYTE
         MVC   STATUS,=CL3'OUT'    MOVE IN OUT QUEUE POSITION.
         B     #ACT0170            GO GET SWAP REASON
#ACT0160 DS    0H
         SR    R1,R1              CLEAR R1
         ICM   R1,3,ASCBFMCT       LOAD ALLOC FRAME COUNT.
         SLL   R1,2                MULTIPLY BY FOUR.
         CVD   R1,WORK                 CONVERT TO DECIMAL FORMAT.
         MVC   SIZE,=X'2020202120D2'   GET READY FOR EDIT.
         ED    SIZE-1(6),WORK+5        EDIT THE RESULT.
         B     #ACT0180
***********************************************************************
*    JOB IS SWAPPED OUT - PUT SWAP REASON IN PLACE OF MEMORY USAGE
***********************************************************************
#ACT0170 DS    0H
         L     R1,=A(SWAPTBL)     R1<--A(SWAP REASON TABLE)
#ACT0172 DS    0H
         CLC   OUCBSRC(1),0(R1)   CHECK REASON CODE
         BE    #ACT0174           ...MATCH
         LA    R1,6(R1)           BUMP TO NEXT ENTRY
         CLI   0(R1),X'FF'        END OF TABLE ???
         BNE   #ACT0172           ...NO: CONTINUE SCAN
#ACT0174 DS    0H
         MVI   STATUS+4,C'-'
         MVC   SIZE(5),1(R1)      GIVE SWAP REASON
         EJECT
***********************************************************************
*    GET SWAP COUNT
***********************************************************************
#ACT0180 DS    0H
         LH    R7,OUCBSWC          LOAD SWAP COUNT.
         CVD   R7,WORK                CONVERT IT TO DECIMAL.
         MVC   SWAPCNT(3),=X'202120'  MOVE IN THE EDIT MASK.
         ED    SWAPCNT-1(4),WORK+6    EDIT THE OUTPUT FIELD.
***********************************************************************
*    GET SERVICE UNITS
***********************************************************************
#ACT0310 DS    0H
         L     R7,OUCBWMS       LOAD SERVICE UNITS THIS XACTION
         CVD   R7,WORK                         CONVERT TO DECIMAL.
         MVC   SERVICE(7),=X'20202020202120'   MOVE IN THE EDIT MASK.
         ED    SERVICE-1(8),WORK+4             EDIT THE RESULT.
***********************************************************************
*    GET NON-SPOOL I/O COUNT
***********************************************************************
         L     R7,ASCBIOSC      LOAD I/O COUNT
         CVD   R7,WORK                         CONVERT TO DECIMAL.
         MVC   IOCNT(7),=X'20202020202120'     MOVE IN THE EDIT MASK.
         ED    IOCNT-1(8),WORK+4               EDIT THE RESULT.
***********************************************************************
*    CALCULATE CPU TIME
***********************************************************************
#ACT0320 DS    0H
         LM    R8,R9,ASCBEJST   LOAD UP EJST (CPU TIME - TASK TYPE)
         SRDL  R8,12            TO GET MICROSEC
         D     R8,=F'10000'     TO GET MICROSEC
         LM    R10,R11,ASCBSRBT LOAD UP SRB TIME.
         SRDL  R10,12           TO GET MICROSEC
         D     R10,=F'10000'    TO GET HUNDREDTHS
         AR    R9,R11           ADD TOGETHER.
         MVC   CPUTIME(11),=C'  .  .  .  '
         SR    R8,R8
         D     R8,=F'100'         GET MICROSECS
         CVD   R8,WORK          CONVERT MICROSECS TO DECIMAL
         UNPK  CPUTIME+9(2),WORK   CONVERT THE RESULT
         OI    CPUTIME+10,X'F0'    MAKE IT PRINTABLE
         SR    R8,R8
         D     R8,=F'60'          GET SECONDS
         CVD   R8,WORK          CONVERT SECONDS TO DECIMAL
         UNPK  CPUTIME+6(2),WORK   CONVERT THE RESULT
         OI    CPUTIME+7,X'F0'     MAKE IT PRINTABLE
         SR    R8,R8
         D     R8,=F'60'          GET MINUTES AND HOURS
         CVD   R8,WORK          CONVERT MINUTES TO DECIMAL
         UNPK  CPUTIME+3(2),WORK   CONVERT THE RESULT
         OI    CPUTIME+4,X'F0'     MAKE IT PRINTABLE
         CVD   R9,WORK          CONVERT HOURS TO DECIMAL
         UNPK  CPUTIME(2),WORK   CONVERT THE RESULT
         OI    CPUTIME+1,X'F0'     MAKE IT PRINTABLE
         EJECT
***********************************************************************
*    THE LINE FOR THIS MEMORY IS FINISHED - SET IT IN OUTPUT
*    BUFFER AND GO GET NEXT MEMORY IF THERE IS STILL ROOM
***********************************************************************
         TR    LINE+2(79),TRTAB PREVENT GARBAGE IN LINE
         MVC   0(81,R12),LINE   MOVE THE LINE TO THE 3270 BUFFER.
         LH    R7,SCRSIZE       BUMP BUFFER COUNTER.
         LA    R7,81(,R7)
         STH   R7,SCRSIZE
         LA    R12,81(,R12)     SEE IF THE BUFFER IS FULL YET.
         C     R12,ADBUF
         BE    #ACT0380         IF YES, THE GO PUT THIS SCREEN.
#ACT0350 DS    0H
         BCT   R3,#ACT0020      BRANCH BACK TO LOOK AT NEXT MEMORY.
         LA    R4,BUFFER        LOAD ADDRESS OF OUTPUT BUFFER.
         CR    R4,R12           ARE WE STILL AT THE BEGINNING.
         BNE   #ACT0360         IF NOT CONTINUE ON
         MVC   JOB(20),=CL20'** NO JOBS ACTIVE **'
         TR    LINE+2(79),TRTAB PREVENT GARBAGE IN LINE
         MVC   0(81,R12),LINE   MOVE THE LINE TO THE 3270 BUFFER.
         LH    R7,SCRSIZE       BUMP BUFFER COUNTER.
         LA    R7,81(,R7)
         STH   R7,SCRSIZE
         DROP  R6                 DROP BASE FOR OUCB
***********************************************************************
**  LOOK THRU SRM CONTROL BLOCKS TO BUILD HEADER INFO
***********************************************************************
#ACT0360 DS    0H
         SR    R1,R1               R1<--A(PSA)
         USING PSA,R1
         L     R1,FLCCVT           R1<--A(CVT)
         USING CVT,R1
         L     R1,CVTOPCTP         R1<--A(RMCT)
         DROP  R1
         L     R1,4(R1)            R1<--A(CCT)  FROM RMCTCCT
         LH    R6,102(R1)          CPU UTILIZATION FROM CCTUTILP
         CVD   R6,WORK             CONVERT TO DECIMAL...
         OI    WORK+7,X'0F'        FIX THE SIGN
         UNPK  HDRCPU,WORK+6(2)     ...AND EDIT
         EJECT
***********************************************************************
*    WRITE THE SCREEN, WAIT FOR INPUT, AND PROCESS ANY RESPONSES
***********************************************************************
#ACT0380 DS    0H
         LA    R1,CLEAR         PREPARE
         LA    0,LENGTH            FOR
         AH    0,SCRSIZE              THE
         ICM   R1,8,TPUTFLG              FULL SCREEN
         TPUT  (R1),(0),R                   TPUT
         TGET  BUFFER,1,ASIS
         CLI   BUFFER,X'F3'       END KEY ???
         BE    #ACT0460           ..YES: END THIS THING
         CLI   BUFFER,X'C3'       END KEY ???
         BE    #ACT0460           ..YES: END THIS THING
         TGET  BUFFER,1         READ THE INPUT DATA FROM USER.
         OI    BUFFER,X'40'     GET TO UPPER CASE BEFORE COMPARE.
         CLI   BUFFER,C'E'      CHECK IF TERMINATION REQUESTED.
         BE    #ACT0460         BYE BYE BABY...
         CLI   BUFFER,C'T'      CHECK FOR TSO ONLY.
         BNE   #ACT0390         IF NOT, TRY FOR BATCH ONLY MODE.
         NI    FLAG,255-A       RESET FLAG
         OI    FLAG,T           ONLY TSO MEMORIES.
         OI    MODESW,X'FF'     SET CHANGE OF MODES.
         B     #ACT0440         BY-PASS FURTHER INPUT CHECKING.
#ACT0390 DS    0H
         CLI   BUFFER,C'B'      CHECK FOR BATCH ONLY
         BNE   #ACT0400         BRANCH TO CHECK OTHER OPTIONS.
         NI    FLAG,255-A       RESET FLAG
         OI    FLAG,B           ONLY BATCH MEMORIES.
         OI    MODESW,X'FF'     SET CHANGE OF MODES.
         B     #ACT0440         BY-PASS FURTHER INPUT CHECKING.
#ACT0400 DS    0H
         CLI   BUFFER,C'S'      CHECK FOR STC/MOUNT ONLY
         BNE   #ACT0410         BRANCH TO CHECK OTHER OPTIONS.
         NI    FLAG,255-A       RESET FLAG
         OI    FLAG,S           ONLY STC/MOUNT MEMORIES.
         OI    MODESW,X'FF'     SET CHANGE OF MODES.
         B     #ACT0440         BY-PASS FURTHER INPUT CHECKING.
#ACT0410 DS    0H
         CLI   BUFFER,C'A'      CHECK FOR EVERYTHING
         BNE   #ACT0420         IF NOT, TRY FOR IN OR OUT MODES.
         OI    FLAG,A           ALL MEMORIES.
         OI    MODESW,X'FF'     SET CHANGE OF MODES.
#ACT0420 DS    0H
         CLI   BUFFER,C'O'      CHECK FOR OUT MODE
         BNE   #ACT0430
         NI    FLAG,255-I       RESET FLAG
         OI    FLAG,O           IN OR OUT MEMORIES
         OI    MODESW,X'FF'     SET CHANGE OF MODES.
         B     #ACT0440
#ACT0430 DS    0H
         CLI   BUFFER,C'I'      CHECK FOR IN OR READY MODE
         BNE   #ACT0440
         NI    FLAG,255-O       RESET FLAG
         OI    FLAG,I           SWAPPED IN ONLY
         OI    MODESW,X'FF'     SET CHANGE OF MODES.
#ACT0440 DS    0H
         MVC   SCRSIZE,=H'0'    RESET SCREEN SIZE.
         LA    R12,BUFFER       PREPARE
         LA    R6,BUFFER           FOR
         ICM   R6,8,C' '              THE
         LA    R7,BUFLEN                 MOVE
         LA    R8,BUFFER                    CHARACTER
         SR    R9,R9                           LONG
         MVCL  R6,R8                              TO CLEAR BUFFER.
         CLI   MODESW,X'FF'     CHECK FOR CHANGE OF MODES.
         BNE   #ACT0450         BRANCH IF SAME MODE.
         MVI   MODESW,X'00'     RESET MODE CHANGE.
         B     #ACT0010         START OVER FROM BEGINNING.
#ACT0450 DS    0H
         LTR   R3,R3            CHECK TO SEE IF MORE MEMORIES TO PROCES
         BZ    #ACT0010         NO, TAKE IT FROM THE TOP
         BCT   R3,#ACT0020      WAS THERE ONLY ONE LEFT AT BUFFER FULL
         B     #ACT0010         YES, START OVER FROM THE TOP.
#ACT0460 DS    0H
         STFSMODE OFF           TERMINATE FULLSCREEN MODE
         TCLEARQ INPUT
         PRINT GEN
         SYSRETRN
         PRINT NOGEN
         TITLE 'CONSTANTS'
         LTORG
         EJECT
***********************************************************************
MODESW   DC    X'00'  MODE CHANGE.
***********************************************************************
SWAPTBL  DS    0C
         DC    X'01',CL5'TRMOT'
         DC    X'02',CL5'TRMIN'
         DC    X'03',CL5'LWAIT'
         DC    X'04',CL5'AUXST'
         DC    X'05',CL5'RSTOR'
         DC    X'06',CL5'DWAIT'
         DC    X'07',CL5'RQSWP'
         DC    X'08',CL5'ENQEX'
         DC    X'09',CL5'EXCHG'
         DC    X'0A',CL5'UNLAT'
         DC    X'FF',CL5'UNK  '
***********************************************************************
FLAG     DC    AL1(O+B)
***********************************************************************
I        EQU   X'80'              DISPLAY ONLY SWAPPED IN MEMORIES
O        EQU   X'40'              DISPLAY REGARDLESS OF SWAP STATUS
T        EQU   X'20'              DISPLAY ONLY TSO MEMORIES
B        EQU   X'10'              DISPLAY ONLY BATCH MEMORIES
S        EQU   X'08'              DISPLAY ONLY STC/MOUNT MEMORIES
A        EQU   X'38'              DISPLAY ALL MEMORIES
***********************************************************************
TRTAB    DS    0CL256
         DC    C'                '     X'00' - X'0F'
         DC    C'                '     X'10' - X'1F'
         DC    C'                '     X'20' - X'2F'
         DC    C'                '     X'30' - X'3F'
         DC    C'          ¢.<(+|'     X'40' - X'4
         DC    C'&&         !$*);¬'    X'50' - X'5F'
         DC    C'-/        ¦,%_>?'     X'60' - X'6F'
         DC    C'          :#@''="'    X'70' - X'7F'
         DC    C'{abcdefghi      '     X'80' - X'8F'
         DC    C' jklmnopqr      '     X'90' - X'9F'
         DC    C'  stuvwxyz      '     X'A0' - X'AF'
         DC    C'                '     X'B0' - X'BF'
         DC    C'}ABCDEFGHI      '     X'C0' - X'CF'
         DC    C' JKLMNOPQR      '     X'D0' - X'DF'
         DC    C'  STUVWXYZ      '     X'E0' - X'EF'
         DC    C'0123456789      '     X'F0' - X'FF'
***********************************************************************
         EJECT
***********************************************************************
WORK     DS    D
HEX      DC    C'0123456789ABCDEF'
SAVE     DS    18F
LINE     DC    X'1D60'
JOB      DC    CL8' '       8
         DC    CL2' '      10
STEP     DC    CL8' '      18
         DC    CL2' '      20
PROCSTEP DC    CL8' '      28
         DC    C' '        29
TYPE     DC    C' '        30
         DC    C' '        31
STATUS   DC    CL3' '      34
         DC    CL3' '      37
SIZE     DC    CL6' '      43
         DC    CL2' '      45
SWAPCNT  DC    CL3' '      48
         DC    CL2' '      50
SERVICE  DC    CL7' '      57
         DC    CL2' '      59
IOCNT    DC    CL7' '      66
         DC    CL2' '      68
CPUTIME  DC    CL11' '     79
         DC    C' '        80
SCRSIZE  DC    H'0'
TPUTFLG  DC    X'03'
ADBUF    DC    A(Z)
*   C3= WCC FOR CLEAR  11= SET BUFFER ADDRESS 4040 = BUFADD POS 0000
*   3C= REPEAT TO ADDRESS 4040(0000) CHAR(00) 11 = SET BUFFER ADDRESS
*   4040 = POSITION 0000(R=1 C=1)  1D = START FIELD 40= UNPROTECTED
*   13 = INSERT CURSOR 40 = BLANK 1D = START FIELD E8 = PROTECT HILITE
* CLEAR    DC    X'27F1C31140403C4040001140401D4013'
*          DC    X'401DE8'
CLEAR    DC    X'27F5C31140401D4013'
         DC    X'401DE8'
SYSHDR   DS    0CL77
         DC    CL4'CPU='         CPU UTILIZATION
HDRCPU   DC    CL3'XXX',C'%'
         DC    CL69' '
         DC    80C'-'
         DC    X'404040'
HEADING DC CL77'JOB     STEP      PROCSTEP T STATUS  SIZE SWAPS SERVICEX
                 IOCOUNT   CPU TIME '
BUFFER   DC    21CL81' '
LENGTH   EQU   BUFFER-CLEAR
Z        DS    X
BUFLEN   EQU   Z-BUFFER
         TITLE 'DSECTS'
         PRINT NOGEN
         IHAPSA
         CVT   DSECT=YES
         IHAASVT
         IHAASCB
         IRAOUCB
CSCB     DSECT
         IEECHAIN
        END
