PROC 0 D() TODAY YESTERDAY TOMORROW DEBUG
GLOBAL MONTH DAY YEAR
GLOBAL JULIAN SHOP WEEKDAY
GLOBAL CDEBUG
/**********************************************************************/
/*>>> AUTHOR    06/27/85 - HOWARD M. DEAN, SYNTEX CORP.               */
/*>>> INSTALLED 06/27/85 - HOWARD M. DEAN                             */
/*>>> UPDATED   07/01/85 - HOWARD M. DEAN                             */
/*>>> UPDATED   12/30/86 - GENE L. LEW, MCI TELECOMMUNICATIONS CORP.  */
/**********************************************************************/
/*                                                                    */
/**********************************************************************/
/*    CLIST TO INQUIRE INTO THE DUMP INDEX DATASET                    */
/**********************************************************************/
/*                                                                    */
/* )STARTHELP                                                         */
/* )F FUNCTION  -                                                     */
/*    THE XIX COMMAND IS A CLIST USED TO INQUIRE INTO THE SYSTEM      */
/*    DUMP INDEX DATASET. THE DUMP TITLES ASSOCIATED WITH A TAPE      */
/*    VOLUME ARE LISTED ALONG WITH THE TAPE VOLUME SERIAL             */
/*    NUMBER.                                                         */
/*                                                                    */
/*    THE DUMP DATASET INDEX, "OP.P.XFERDUMP.INDEX", IS A WRAP-AROUND */
/*    PARTITIONED DATASET. EACH MEMBER IS ASSOCIATED WITH A DAY       */
/*    OF THE MONTH.  ONE MEMBER  MAY CONTAIN AN INDEX OF              */
/*    MULTIPLE DUMP TAPES FOR A PARTICULAR DAY. THE PREVIOUS          */
/*    MONTH'S INDEX MEMBER IS OVERLAYED BY THE CURRENT DUMP TAPE      */
/*    INDEX MEMBER.                                                   */
/*                                                                    */
/*    THIS CLIST WORKS IN CONJUNCTION WITH THE "XFERDUMP" CATALOGED   */
/*    PROCEDURE. THAT PROCEDURE UPDATES THE INDEX DATA SET BASED ON   */
/*    THE CURRENT DAY OF THE WEEK.                                    */
/*                                                                    */
/*    IF THE PROPER OPERANDS ARE ENTERED, ISPF "BROWSE" SERVICES ARE  */
/*    INVOKED AGAINST THE SPECIFIED DUMP INDEX MEMBER.                */
/*                                                                    */
/* )X SYNTAX -  %XIX D(XX)/TODAY/YESTERDAY/TOMORROW                   */
/*                                                                    */
/*    REQUIRED - NONE                                                 */
/*    ALIAS    - NONE                                                 */
/*    DEFAULTS - TODAY                                                */
/*                                                                    */
/*    ALL OPERANDS ARE KEYWORDS                                       */
/*    ONLY ONE OPERAND MAY BE SPECIFIED, BUT NONE ARE REQUIRED        */
/*                                                                    */
/*    UPON ENTERING "BROWSE", YOU MAY SEE THE CHARACTERS "PAD"        */
/*    FOLLOWED BY 77 DOTS. THIS MEANS THAT THERE HAS NEVER BEEN       */
/*    A DUMP TRANSFERED ON THAT PARTICULAR DAY SINCE THE INDEX        */
/*    WAS INITIALIZED.                                                */
/*                                                                    */
/* )O OPERANDS -                                                      */
/*                                                                    */
/*    ALL OPERANDS ARE KEYWORDS                                       */
/*    NONE ARE REQUIRED; ONLY ONE MAY BE SPECIFIED                    */
/*                                                                    */
/* ))D(XX) -                                                          */
/*    SPECIFIES THE DAY OF THE MONTH FOR WHICH YOU WOULD LIKE THE     */
/*    DUMP INDEX DISPLAYED. IF THIS OPERAND IS SPECIFIED, THE INDEX   */
/*    DATASET IS SEARCHED FOR A MEMBER CORRESPONDING TO THE DAY       */
/*    NUMBER "XX". THAT MEMBER IS SUBSEQUENTLY "BROWSED" VIA ISPF/PDF.*/
/*                                                                    */
/*    "XX" MUST BE A NUMERIC VALUE BETWEEN 1 AND 31.                  */
/*                                                                    */
/* ))TODAY -                                                          */
/*    SPECIFIES THE DUMP INDEX MEMBER CORRESPONDING TO TODAY'S        */
/*    TRANSFERED DUMP DATASETS. THIS MEMBER MAY OR MAY NOT            */
/*    CONTAIN A CURRENT ENTRY. HOWEVER, YOU CAN EASILY TELL IF        */
/*    YOU ARE LOOKING AT LAST MONTH'S ENTRY OR THE INITIALIZING       */
/*    PAD ENTRY BY EXAMINING THE CONTENTS UNDER "BROWSE".             */
/*                                                                    */
/* ))YESTERDAY -                                                      */
/*    LIKE THE "TODAY" OPERAND, THIS KEYWORD SELECTS A MEMBER         */
/*    FROM THE DUMP INDEX DATASET. HOWEVER, THE SELECTED MEMBER       */
/*    CORRESPONDS TO A DAY NUMBER ONE LESS THAN THE CURRENT DAY.      */
/*    IF THE FIRST OF THE MONTH, "YESTERDAY" BROWSES THE LAST         */
/*    DAY OF THE PREVIOUS MONTH.  THUS, YOU "BROWSE" THE ENTRY        */
/*    CORRESPONDING TO "YESTERDAY'S" DAY OF THE MONTH.                */
/*                                                                    */
/* ))TOMORROW -                                                       */
/*    SPECIFIES THAT YOU WOULD LIKE TO SEE "TOMORROW'S" DUMP          */
/*    DATASET INDEX. IN EFFECT, THIS OPERAND SHOULD PREDICT           */
/*    WHICH DUMPS WILL OCCUR TOMORROW AND ON WHICH TAPE               */
/*    VOLUMES THEY WILL RESIDE. THIS SEEMS LIKE AN IMPOSSIBLE         */
/*    TASK, BUT IN MVS ALL THINGS ARE POSSIBLE.                       */
/*                                                                    */
/* )ENDHELP                                                           */
/*                                                                    */
/******************************************************/
/** SET UP DEBUG ENVIRONMENT - FOR THIS LEVEL ONLY   **/
/******************************************************/
/*                                                                    */
IF &DEBUG EQ DEBUG THEN +
   CONTROL LIST CONLIST SYMLIST NOFLUSH PROMPT MSG
   ELSE CONTROL NOMSG NOPROMPT NOFLUSH NOLIST
/*                                                                    */
/**********************************************/
/** IF NO OPERANDS ENTERED; DEFAULT TO TODAY **/
/**********************************************/
IF &D EQ  &STR() -
      AND &TODAY     EQ &STR() -
      AND &YESTERDAY EQ &STR() -
      AND &TOMORROW  EQ &STR() THEN SET &TODAY EQ TODAY
/*                                                                    */
/************************************************************/
/** CALL SUBROUTINE TO GET THE CURRENT DATE                **/
/** "JULDATE" WILL FORMAT THE GLOBAL VARIABLES             **/
/************************************************************/
%JULDATE DATE(SYSDATE)
/**                                                                  **/
/** SET UP VARIALBES                                                 **/
/**                                                                  **/
SET  &NULL EQ &STR()
SET  &SW   EQ 0
DO WHILE &SW EQ 0
IF &SW EQ 0 THEN -
  DO
   IF &D NE &NULL THEN -
     DO
       IF &DATATYPE(&D) NE NUM  OR &LENGTH(&STR(&D)) GT 2) THEN -
         DO
           WRITE ******************************************
           WRITE ENTRY FOR DAY NUMBER NOT VALID - TRY AGAIN
           WRITE ******************************************
           EXIT CODE(94)
         END
       ELSE -
       IF  &D LT 1 OR &D GT 31 THEN -
         DO
           WRITE *********************************************
           WRITE ENTRY FOR DAY NUMBER OUT OF RANGE - TRY AGAIN
           WRITE *********************************************
           EXIT CODE(95)
          END
       IF &TODAY NE &NULL -
                 OR &YESTERDAY NE &NULL -
                 OR &TOMORROW  NE &NULL -
                 THEN -
         DO
           WRITE *************************************************
           WRITE IF YOU ENTER A SPECIFIC DAY NUMBER, YOU CANNOT
           WRITE ENTER TODAY, YESTERDAY, OR TOMORROW
           WRITE TRY THIS CLIST AGAIN - THIS TIME CORRECTLY!!
           WRITE **************************************************
           EXIT CODE(91)
         END
       ELSE -
         DO
           SET &D EQ &STR(&D)
           IF &LENGTH(&STR(&D)) EQ 1 THEN SET &D EQ &STR(0&D)
           %BXFER 'OP.P.XFERDUMP.INDEX(DAY&D)'
           SET &SW EQ 1
         END
     END
  END
IF &SW EQ 0 THEN -
   IF &TODAY EQ TODAY THEN -
      IF &YESTERDAY NE &NULL OR &TOMORROW NE &NULL THEN -
         DO
           WRITE ******************************************************
           WRITE CHOOSE ONLY ONE OPTION - YESTERDAY, TODAY, OR TOMORROW
           WRITE ******************************************************
           EXIT CODE(98)
           SET &SW EQ 1
         END
       ELSE -
         DO
           SET &DAY EQ &STR(&DAY)
           IF  &LENGTH(&STR(&DAY)) EQ 1 THEN SET &DAY EQ &STR(0&DAY)
           %BXFER 'OP.P.XFERDUMP.INDEX(DAY&DAY)'
           SET &SW EQ 1
         END
IF &SW EQ 0 THEN -
   IF  &YESTERDAY EQ YESTERDAY THEN -
     IF &TODAY NE &NULL OR &TOMORROW NE &NULL THEN -
         DO
           WRITE ******************************************************
           WRITE CHOOSE ONLY ONE OPTION - YESTERDAY, TODAY, OR TOMORROW
           WRITE ******************************************************
           EXIT CODE(98)
         END
     ELSE -
         DO
           SET &DAY EQ &DAY - 1
           IF  &DAY EQ 0 THEN -
              DO
                /**************************************************/
                /** CALCULATE THE LAST DAY OF THE PREVIOUS MONTH **/
                /**************************************************/
                SET &MONTH EQ &MONTH - 1
                IF  &MONTH EQ 0 THEN SET &MONTH EQ 12
                SET &DAY   EQ &SUBSTR(&EVAL(&MONTH*2):+
                    &EVAL((&MONTH*2)+1),0312831303130313130313031)
                IF &YEAR//4 EQ 0 AND &MONTH EQ 2 THEN SET &DAY EQ 29
              END
           SET &DAY EQ &STR(&DAY)
           IF  &LENGTH(&STR(&DAY)) EQ 1 THEN SET &DAY EQ &STR(0&DAY)
           %BXFER 'OP.P.XFERDUMP.INDEX(DAY&DAY)'
           SET &SW EQ 1
         END
IF &SW EQ 0 THEN -
   IF &TOMORROW EQ TOMORROW THEN -
      IF &TODAY NE &NULL OR &YESTERDAY NE &NULL THEN -
         DO
           WRITE ******************************************************
           WRITE CHOOSE ONLY ONE OPTION - YESTERDAY, TODAY, OR TOMORROW
           WRITE ******************************************************
           EXIT CODE(98)
         END
     ELSE -
         DO
           WRITE ***********************************************
           WRITE SILLY! I CAN'T FORTELL THE FUTURE
           WRITE DOES THIS TERMINAL LOOK LIKE A CRYSTAL BALL?
           WRITE (DON'T ANSWER THAT!!)
           WRITE ***********************************************
           EXIT CODE(97)
           END
END
EXIT CODE(0)
