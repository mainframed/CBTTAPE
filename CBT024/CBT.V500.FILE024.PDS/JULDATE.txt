PROC 0 DATE()
GLOBAL MONTH DAY YEAR
GLOBAL JULIAN SHOP WEEKDAY
GLOBAL CDEBUG
/*********************************************************************/
/*  WRITTEN 05/03/85,  HOWARD M. DEAN, SYNTEX CORPORATION            */
/*  MODIFIED 12/30/86, GENE L. LEW, MCI TELECOMM CORP.               */
/*********************************************************************/
IF &CDEBUG EQ YES THEN CONTROL LIST CONLIST SYMLIST PROMPT MSG
    ELSE CONTROL NOMSG NOLIST
/*-------------------------------------------------------------------*/
/* THE FOLLOWING CLIST PERFORMS THESE FUNCTIONS:                     */
/*                                                                   */
/* TAKES ANY VALID GREGORIAN DATE IN THIS CENTURY AND                */
/* CALCULATES THE JULIAN DATE, THE DAY OF THE WEEK, AND              */
/* A 'SHOP DATE' IE- THE NUMBER OF DAYS ELAPSED BETWEEN              */
/* THE INPUT DATE AND JAN. 1, 1900. THUS IF YOU FEED                 */
/* THIS CLIST 2 DATES, THE DIFFERENCE BETWEEN THE TWO                */
/* SHOP DATES EQUALS THE NUMBER OF ELAPSED DAYS BETWEEN              */
/* THE TWO.                                                          */
/*                                                                   */
/* THE FORMATS OF THE GLOBAL VARIABLES ARE AS FOLLOWS:               */
/*                                                                   */
/* INPUT - MONTH=MM, DAY=DD, YEAR=YY                                 */
/*         FOR EXAMPLE: 01 01 76                                     */
/*         NOTE - 1 1 76 WILL NOT WORK.                              */
/*                                                                   */
/* INPUT - DATE(SYSDATE)  - SETS VARIABLES FOR TODAY'S DATE          */
/*         DATE(MM/DD/YY) - SETS VARIABLES FOR THE DATE SPECIFIED    */
/*                                                                   */
/* OUTPUT - JULIAN=YYDDD, IE 99365 FOR 12/31/99                      */
/* SHOP   - DD..DD, IE, 36523 FOR THE SAME DATE                      */
/* WEEKDAY - MONDAY, TUESDAY, WEDNESDAY, ETC.                        */
/*-------------------------------------------------------------------*/
ERROR +
DO
  SET &LASTCC EQ 4
  EXIT
END
IF  &STR(&DATE) NE &STR() THEN                                         -
   DO
      IF &STR(&DATE) EQ &STR(SYSDATE) THEN                             -
         DO
           SET &MONTH EQ &SUBSTR(1:2,&SYSDATE)
           SET &DAY   EQ &SUBSTR(4:5,&SYSDATE)
           SET &YEAR  EQ &SUBSTR(7:8,&SYSDATE)
         END
      ELSE                                                             -
         DO
           SET &MONTH EQ &SUBSTR(1:2,&STR(&DATE))
           SET &DAY   EQ &SUBSTR(4:5,&STR(&DATE))
           SET &YEAR  EQ &SUBSTR(7:8,&STR(&DATE))
         END
   END
SET &JULIAN = &SUBSTR(&EVAL((&MONTH*3)-2):+
              &EVAL(&MONTH*3),000031059090120151181212243273304334)
SET &JULIAN = &JULIAN + &DAY
IF &YEAR//4 = 0 AND &MONTH GT 2 THEN SET &JULIAN = &JULIAN + 1
SET &SHOP = &JULIAN
SET &L = &LENGTH(&JULIAN)
SET &JULIAN = &STR(X00&JULIAN)
SET &JULIAN = &STR(&YEAR)+
              &STR(&SUBSTR(&EVAL(&L+1):+
              &EVAL(&LENGTH(&JULIAN)),&JULIAN))
SET &SHOP = &EVAL(&YEAR*365) + &EVAL((&YEAR-1)/4) + &SHOP - 1
SET &WEEKDAY = &SUBSTR(&EVAL(((&SHOP//7)*6)+1):+
               &EVAL(((&SHOP//7)*6)+6),   +
               MON  TUESWEDNES THURS   FRI SATUR   SUN)
SET &WEEKDAY = &SUBSTR(&EVAL(6-&LENGTH(&WEEKDAY)+1):6,&WEEKDAY)DAY
