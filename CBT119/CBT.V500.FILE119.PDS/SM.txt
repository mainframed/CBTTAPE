//ASMSM    JOB (1),'ASM/LINK SM UTIL  ',CLASS=A,MSGLEVEL=(1,1),
//     MSGCLASS=A,TIME=1439,NOTIFY=BSMITH,REGION=4M
//ASM      EXEC PGM=ASMA90,
//     PARM=(NOOBJECT,DECK,NOESD,NORLD)
//SYSPRINT  DD  SYSOUT=*
//SYSTERM   DD  SYSOUT=*
//SYSUT1    DD  UNIT=SYSALLDA,SPACE=(CYL,(10,10))
//SYSLIB    DD  DISP=SHR,DSN=SYS1.MACLIB
//          DD  DISP=SHR,DSN=SYS1.MODGEN
//          DD  DISP=SHR,DSN=CBTTAPE.FILE119.PDS   <-- MACROS --- !!!!
//SYSPUNCH  DD  DSN=&&OBJ(SM),DISP=(,PASS),
//     UNIT=SYSALLDA,SPACE=(CYL,(1,1,45)),
//     DCB=(DSORG=PO,RECFM=FB,LRECL=80,BLKSIZE=27920)
//SYSIN     DD  *
SM       TITLE 'SESSION MANAGER SWITCH ROUTINE'
***********************************************************************
*                                                                     *
*   MODULE NAME  =  SM                                                *
*                                                                     *
*   DESCRIPTIVE  =  THE SM COMMAND IS A TSO COMMAND TO TURN-OFF AND   *
*                   SUBSEQUENTLY TURN-ON THE TSO SESSION MANAGER      *
*                                                                     *
*   COMMAND SYNTAX  =  SM  OFF/ON                                     *
*                                                                     *
*   CSECT NAME  =  SM                                                 *
*                                                                     *
*   COPYRIGHT  =  NONE                                                *
*                                                                     *
*   STATUS  =  z/OS 2.2  (PUT 1607)                                   *
*           =  MVS / SP 1.3  & MVS / SP 2.1.2                         *
*           =  AMODE 24, RMODE 24 (MODE CHANGE TO ACCESS CONTROL BLKS)*
*                                                                     *
*   FUNCTION  =                                                       *
*      TO PROVIDE A TSO COMMAND THAT WILL TEMPORARILY DISABLE (AND    *
*      SUBSEQUENTLY RE-ENABLE) THE TSO SESSION MANAGER (NOTE:  THIS   *
*      COMMAND HAS NO EFFECT IF USED IN A TSO SESSION WHERE THE TSO   *
*      SESSION MANAGER WAS NOT INITIALLY ACTIVE.)                     *
*                                                                     *
*      OPERATION  =                                                   *
*         1.  ESTABLISH LOCAL ADDRESSIBILITY                          *
*         2.  LOCATE THE ONLINE TSO PROTECTED-STEP-CONTROL-BLOCK      *
*             (PSCB) AND EXIT (WITH A MESSAGE) IF NOT FOUND           *
*         3.  TEST APF-AUTHORIZATION:                                 *
*             3.1  IF AUTHORIZED, THEN PROCEED TO STEP 4              *
*             3.2  IF NOT AUTHORIZED THEN DISPLAY ERROR MESSAGE       *
*                  AND EXIT                                           *
*         4.  GET KEY 0 USING MODESET MACRO                           *
*         5.  LOCATE THE TSO TERMINAL-STATUS-BLOCK (TSB), THE TSO     *
*             LOGON-WORK-AREA (LWA), THE TSO SESSION MANAGER MAIN     *
*             WORK AREA (RDFBLOCK), AND THE SAVED RDFBLOCK PTR        *
*             (LAST FOUR BYTES OF PSCB USER FIELD - PCSBU)            *
*         6.  TEST THE ACTIVE ENVIRONMENT AND GO PERFORM ERROR EXIT   *
*             PROCESSING IF THE TSO SESSION MANAGER IS NOT ACTIVE     *
*             OR CAN NOT BE RESTORED                                  *
*         7.  IF THE TSO SESSION MANAGER IS ACTIVE, THEN:             *
*             7.1  RESET PSW KEY AND EXIT IF "SM ON" SPECIFIED        *
*             7.2  PROCEED THROUGH THE STEPS NEEDED TO DISABLE THE    *
*                  TSO SESSION MANAGER:                               *
*                  7.2.1  STATUS-STOP THE SESSION MANAGER TCB         *
*                  7.2.2  SET FLAGS SO THAT SESSION MANAGER THINKS    *
*                         THAT A FULL-SCREEN COMMAND IS ACTIVE        *
*                  7.2.3  SAVE RDFBLOCK PTR IN THE PSCBU+4 FIELD      *
*                  7.2.4  ISSUE TPUT TO CLEAR THE TERMINAL SCREEN     *
*                  7.2.5  RESET TERMINAL DISPLAY MANAGER OPTIONS      *
*                         FLAGS IN THE TSB                            *
*                  7.2.6  IF VTAM, DISABLE FULL-SCREEN MODE           *
*                  7.2.7  RESET PSW KEY AND EXIT                      *
*         8.  IF THE TSO SESSION MANAGER IS NOT ACTIVE, THEN:         *
*             8.1  RESET PSW KEY AND EXIT IF "SM OFF" SPECIFIED       *
*             8.2  PROCEED THROUGH THE STEPS NEEDED TO ENABLE THE     *
*                  TSO SESSION MANAGER:                               *
*                  8.2.1  ENABLE TERMINAL DISPLAY MANAGER OPTIONS     *
*                         FLAGS IN THE TSB                            *
*                  8.2.2  RESTORE RDFBLOCK PTR FROM THE PSCBU+4 FIELD *
*                  8.2.3  STATUS-START THE SESSION MANAGER TCB        *
*                  8.2.4  MODIFY TSB FLAGS SO THAT SESSION MANAGER    *
*                         MAY RESUME TERMINAL DISPLAY CONTROL         *
*                  8.2.5  IF VTAM, ENABLE FULL-SCREEN MODE            *
*                  8.2.6  ISSUE TPUT TO CLEAR THE TERMINAL SCREEN     *
*                  8.2.7  RESET PSW KEY AND EXIT                      *
*                                                                     *
*   MACROS USED  =                                                    *
*         CVT                   SYSTEM CVT MAPPING DSECT              *
*         TSOLEAVE              SYNTEX TSO RESTORE SEQUENCE           *
*         EXTRACT               SYSTEM CONTROL-BLOCK LOCATOR          *
*         FREEMAIN              SYSTEM FREE MEMORY OPERATION          *
*         GETMAIN               SYSTEM GET MEMORY OPERATION           *
*         IHAASCB               SYSTEM ASCB MAPPING DSECT             *
*         IHAASXB               SYSTEM ASXB MAPPING DSECT             *
*         IHAPSA                SYSTEM PSA MAPPING DSECT              *
*         IKJPSCB               TSO PSCB MAPPING DSECT                *
*         IKJTSB                TSO TSB MAPPING DSECT                 *
*         IKJWFLWA              TSO LOGON WORK AREA MAPPING DSECT     *
*         MODESET               SYSTEM PSW-KEY/PSW-MODE CONTROL       *
*         TSOENTER              SYNTEX TSO SAVE SEQUENCE              *
*         RDFBLOCK              EQUS FOR SESSION MGR RDFBLOCK AREA    *
*         STATUS                SYSTEM TCB DISPATCH CONTROL           *
*         STAX                  SYSTEM ATTENTION-EXIT CONTROL         *
*         STFSMODE              TSO SET-TERM OPTIONS OPERATION        *
*         STTMPPMD              TSO SET-TERM OPTIONS OPERATION        *
*         TESTAUTH              SYSTEM APF-AUTHORIZATION CHECKER      *
*         TPUT                  TSO TERMINAL OUTPUT OPERATION         *
*                                                                     *
*   NOTES  =                                                          *
*      REGISTER CONVENTIONS  =  STANDARD CONVENTIONS                  *
*         REGISTERS 0 - 1    =  WORK REGISTERS                        *
*         REGISTER  2        =  TEMPORARY PSCB/ASCB/CVT BASE REGISTER *
*         REGISTER  3        =  TEMPORARY ASXB BASE REGISTER          *
*         REGISTER  4        =  TSO LWA BASE REGISTER                 *
*         REGISTER  5        =  TSO TSB BASE REGISTER                 *
*         REGISTER  6        =  SESSION MANAGER RDFBLOCK BASE         *
*         REGISTERS 7 - 9    =  UNUSED                                *
*         REGISTER 10        =  ADDRESS OF PSCBU+4 LOCATION           *
*         REGISTER 11        =  WORK/SAVE AREA BASE ADDRESS           *
*         REGISTER 12        =  CSECT BASE ADDRESS                    *
*         REGISTER 13        =  SAVE AREA BASE ADDRESS                *
*         REGISTER 14        =  RETURN ADDRESS                        *
*         REGISTER 15        =  ENTRY POINT ADDRESS REGISTER          *
*                                                                     *
***********************************************************************
*                                                                     *
* Updated 18Jun2012, William Smith, Hitachi Data Systems,             *
*   Santa Clara, CA, 95050-2627, bill.smith@hds.com                   *
*                                                                     *
* . Reassembled with z/OS 1.13, z/196, PUT/RSU 1204+                  *
* . Requires macros from CBT TAPE file 119 (see above for list)       *
* . TSO version 3.13.0 (from the TASID 5.19 display)                  *
* . No assembly errors and no need to uncomment LWA mapping macro     *
*   which has changed substantially since 1985; now shipped with      *
*   z/OS in SYS1.MODGEN (IKJEFLWA service level 2011/HTE7780)         *
* . Requires APF authorization:  Hence, issue a PARMLIB UPDATE TSO    *
*   command after adding SM to the AUTHCMD section in IKJTSO00 and    *
*   linking into an APF authorized LINK or LPA list data set followed *
*   by a LLA REFRESH                                                  *
*                                                                     *
***********************************************************************
*                                                                     *
* UPDATED 21FEB85, WILLIAM SMITH, SYNTEX (USA), INC., PALO ALTO, CA.  *
* UPDATED 27OCT85, HOWARD DEAN,   SYNTEX (USA), INC., PALO ALTO, CA.  *
*                                                                     *
* . SOURCE:  OCCIDENTAL PETROLEUM, HOUSTON, TX.; COURTESY OF SAM BASS *
* . THIS CODE HAS BEEN TESTED AT THE 8409 PUT LEVEL OF MVS/SP 1.3.3   *
*   JES2 WITH TSO/E RELEASE 2.0                                       *
*   AND AT THE 8505 PUT LEVEL OF MVS/XA 2.1.2 WITH TSO/E RELASE 2.1   *
* . MODIFICATION HISTORY AND NOTES                                    *
*   - AN IN-LINE DSECT FOR THE "LWA" (LOGON WORK AREA) HAS BEEN       *
*     REPLACED WITH COMMENTS AND THE IBM MAPPING MACRO, "IKJEFLWA",   *
*     HAS BEEN CODED IN ITS PLACE                                     *
*   - WARNING:  IKJEFLWA IS *RESTRICTED* MATERIAL AND CANNOT BE       *
*     DISTRIBUTED WITH THIS MODULE.  IKJEFLWA IS DISTRIBUTED ON THE   *
*     OPTIONAL MATERIALS TAPE, FEATURE 7029 (1600 BPI) OR 7031 (6250  *
*     BPI), VOL=SER=SYM-01, FILE 1 IN THE "AAPVT" (PRIVATE MACROS),   *
*     FILE FOR FMID JBB1213.  IF YOU ARE NOT LICENSED FOR THE OPTION- *
*     AL MATERIALS, SIMPLY REMOVE THE COMMENTS FROM THE IN-LINE LWA   *
*     DSECT TO CREATE THE MAPPING DSECT FOR USE DURING ASSEMBLY.      *
*                                                                     *
*     'IKJEFLWA' IS IN SYS1.MACLIB FOR JBB2267 (TSO/E FOR MVS/XA)     *
*                                                                     *
* . TO ELIMINATE NEED FOR THE AUTHORIZATION SVC, LINK SM AS AN APF    *
*   AUTHORIZED COMMAND AND ADD THE COMMAND NAME TO THE IKJEFTE2 TSO   *
*   AUTHORIZED COMMAND TABLE; THE SVC CALL SHOULD BE NO-OP'ED.        *
* . IF SM IS EXECUTED FROM ISPF OPTION 4 OR 6 AND THE SM MODULE IS    *
*   LOCATED IN THE PLPA, BE SURE TO UPDATE THE ISPF TABLE "ISPTCM" TO *
*   ALLOW ISPF TO LOCATE THE MODULE; CONSULT THE ISPF INSTALLATION    *
*   GUIDE FOR FURTHER DETAILS ON UPDATING THIS TABLE                  *
*                                                                     *
* UPDATED 24OCT85, WILLIAM SMITH/HOWARD DEAN, SYNTEX (USA)            *
*                                                                     *
* . ELIMINATED NEED FOR AUTHORIZATION SVC - PUT INTO IKJTABLS         *
*                                                                     *
* UPDATED 27OCT85, WILLIAM SMITH/HOWARD DEAN, SYNTEX (USA)            *
* . USE 31 BIT MODE FOR ACCESSING CONTROL BLOCKS UNDER MVS/XA         *
* . RUN 'SM' IN AMODE 24 RMODE 24 BUT SWITCH MODES WHEN APPROPRIATE   *
*                                                                     *
***********************************************************************
SM       TSOENTER REGS=YES,PL=(CPPL,IO),WORK=SMWORKL,SP=1
         SPACE 1
         LA    R0,TSOWORK          WORK AREA ADDRESS
         LA    R1,SMWORKL          R1 = LENGTH OF WORKAREA
         SLR   R15,R15             PAD BYTE AND LENGTH = 0
         SLR   R14,R14             CLEAR 'TO' ADDRESS
         MVCL  R0,R14              CLEAR WORKAREA
         SPACE 1
         XC    SMWOPRND,SMWOPRND   CLEAR OPERAND
         SPACE 1
         L     R1,CPPLCBUF         GET CBUF ADDR
         LH    R2,0(R1)            GET BUFFER LENGTH
         SH    R2,=H'4'            MINUS CONTROL WORD LENGTH
         CH    R2,2(,R1)           COMPARE TO OFFSET VALUE
         BE    SMEXTRA             NO OPERANDS
         SPACE 1
         LH    R3,2(,R1)           R3 = OFFSET TO OPERAND
         LA    R3,4(R1,R3)         R3 = ADDRESS OF OPERAND
         LA    R2,4(R1,R2)         R2 = ADDRESS OF BUFFER END
         SR    R2,R3               R2 = LENGTH OF OPERAND
         CH    R2,=H'2'            AT LEAST TWO?
         BL    SMEXTRA             NON MEANINGFUL OPERAND
         SPACE 1
         MVC   SMWOPRND,0(R3)      COPY OPERAND
         OI    SMWONOFF,C' '       UPCASE OPERAND
SMEXTRA  EXTRACT SMWPSCBA,'S',FIELDS=(PSB),MF=(E,SMWXTRCT)
         L     R2,SMWPSCBA         GET PSCB ADDRESS
         LA    R2,0(R2)            CLEAR HIGH-ORDER BYTE
         LTR   R2,R2               IS PSCB ADDRESS VALID?
         BZ    SMNOTSO               NO, GO PRINT NON-TSO MESSAGE
         USING PSCB,R2               YES, USE IKJPSCB MAPPING
         LA    R10,PSCBU+4         COMPUTE ADDRESS OF PSCB USER WORD
         DROP  R2                  DROP PSCB
         TESTAUTH FCTN=1,RBLEVEL=1 TEST THE COMMANDS AUTHORIZATION
         LTR   R15,R15             CAN MODESET BE ISSUED ?
         BNZ   SMNOAPF             NO, ISSUE MESSAGE FOR NO AUTH
SMKEY0   DS    0H
         MODESET KEY=ZERO,MODE=SUP
         MVI   SMWFLAGS,C'S'       DEFAULT TO MVS/SP
         SPACE 1
         USING CVTMAP,R3           ADDRESS THE CVT
         L     R3,CVTPTR           LOAD CVT ADDRESS
         TM    CVTDCB,CVTMVSE      ARE WE RUNNING MVS/XA
         BZ    SMKEY0S             NO, USE 24 BIT MODE
         DROP  R3                  DROP   CVT
         MVI   SMWFLAGS,C'X'       INDICATE MVS/XA MODE
         XABSM MODE=31             SWITCH TO 31 BIT MODE
         SPACE 1
SMKEY0S  DS    0H
         USING PSA,R0              USE IHAPSA MAPPING
         L     R2,PSAAOLD          GET ASCB ADDRESS
         DROP  R0                  DROP PSA
         USING ASCB,R2             USE IHAASCB MAPPING
         L     R3,ASCBASXB         GET ASXB ADDRESS
         USING ASXB,R3             USE IHAASXB MAPPING
         L     R4,ASXBLWA          GET LOGON WORK AREA ADDRESS
         DROP  R3                  DROP ASXB
         L     R5,ASCBTSB          GET TSB ADDRESS
         DROP  R2                  DROP ASCB
         USING LWA,R4              USE LWA DSECT MAPPING
         CLC   =C'   LWA  ',LWALWA DOES LWA CONTAIN A VALID ID?
         BE    SMLWA0              YES, CONTINUE
         CLI   SMWFLAGS,C'S'       ARE WE RUNNING MVX/XA?
         BE    SMERROR             NO, DO NOT RESET MODE
         XABSM MODE=24             RESET BACK TO 24 BIT MODE
         B     SMERROR             AND SEND ERROR MESSAGE
         SPACE 1
         USING TSB,R5              USE IKJTSB MAPPING
SMLWA0   DS    0H
         L     R6,LWAXXXX          GET SESSION MANAGER RDFBLOCK ADDR.
         LA    R6,0(R6)            CLEAR HIGH-ORDER BYTE (OR BIT)
         LTR   R6,R6               IS RDFBLOCK ADDRESS VALID?
         BZ    SMSWON                NO, GO SEE IF CAN BE RESTORED
         CLC   =C'RDF ',RDFRDF(R6) DOES RDF CONTAIN A VALID ID?
         BNE   SMSWON                NO, GO SEE IF RDF CAN BE RESTORED
         TM    TSBFLG5,TSBATMP     IS SESSION MANAGER IN CONTROL?
         BZ    SMSTART               NO, RE-START SESSION MANAGER
         CLI   SMWONOFF,C'N'       WAS C'ON' SPECIFIED?
         BE    SMRESET             IMPOSSIBLE SINCE WE ARE ALREADY ON
         SPACE 1
         L     R7,RDFTCB(R6)       GET RDF TCB ADDRESS
         STATUS STOP,TCB=(R7)      STOP SESSION MANAGER TASK
         OI    RDFFLGS(R6),RDFFSCR INDICATE FULLSCREEN APPLICATION
         ST    R6,0(R10)           SAVE RDFBLOCK ADDRESS INTO PSCB
         MVC   LWAXXXX,=F'0'       ZERO RDFBLOCK ADDRESS
         TPUT  CLEAR,CLEARL,FULLSCR     ERASE SCREEN
         STTMPMD OFF,KEYS=NO       DISABLE SESSION MANAGER
         TM    TSBFLG5,TSBVTAM     IS THIS A VTAM SESSION?
         BZ    SMRESET               NO, GO RESET KEY AND EXIT
         STFSMODE OFF                YES, FULLSCREEN PROTECT MODE OFF
         B     SMRESET             GO RESET STORAGE KEY AND EXIT
SMSWON   DS    0H
         L     R6,0(R10)           GET SAVED RDFBLOCK ADDRESS
         LA    R6,0(R6)            CLEAR HIGH-ORDER BYTE
         LTR   R6,R6               WAS RDFBLOCK ADDRESS SAVED?
         BNZ   SMSWON1             YES, RESTORE SESSION MGR
         CLI   SMWFLAGS,C'S'       ARE WE IN 31 BIT MODE?
         BE    SMERROR             NO, DO NOT RESET
         XABSM MODE=24
         B     SMERROR             SEND MESSAGE, THEN EXIT
         SPACE 1
SMSWON1  CLC   =C'RDF ',RDFRDF(R6) DOES RDF CONTAIN A VALID ID?
         BE    SMSWON2             YES, RESTORE SESSION MGR
         CLI   SMWFLAGS,C'S'       ARE WE IN 31 BIT MODE?
         BE    SMERROR             NO, DO NOT RESET
         XABSM MODE=24
         B     SMERROR             SEND MESSAGE, THEN EXIT
         SPACE 1
SMSWON2  CLI   SMWONOFF,C'F'       WAS C'OFF' SPECIFIED?
         BE    SMRESET               YES, SKIP BECAUSE ALREADY OFF
         STTMPMD ON,KEYS=ALL         NO, ENABLE SESSION MANAGER
         ST    R6,LWAXXXX          RESTORE RDFBLOCK ADDRESS
         MVC   0(4,R10),=F'0'      ZERO PSCB USER WORD
         L     R7,RDFTCB(R6)       GET RDF TCB ADDRESS
         STATUS START,TCB=(R7)     START SESSION MANAGER TASK
         B     SMRESET             GO RESET STORAGE KEY AND EXIT
SMSTART  DS    0H
         OI    TSBFLG5,TSBATMP+TSBKEYS  ENABLE SESSION MANAGER
         NI    RDFFLG1(R6),X'FF'-(RDFFSCRA+RDFFSCRK)
         TM    TSBFLG5,TSBVTAM          IS THIS A VTAM SESSION?
         BZ    SMCLEAR                    NO, SKIP STFSMODE MACRO
         STFSMODE ON,INITIAL=YES        ENABLE FULL SCREEN MODE
SMCLEAR  TPUT  CLEAR,CLEARL,FULLSCR     ERASE SCREEN
         B     SMRESET             GO RESET STORAGE KEY AND EXIT
         DROP  R4,R5
SMERROR  DS    0H
         MODESET KEY=NZERO,MODE=PROB     RESET KEY
         LA    R0,ERROR            FIRST MESSAGE LINE
         LA    R1,ERRORM           SECOND MESSAGE LINE
         B     PUTLINEI
SMRESET  DS    0H
         CLI   SMWFLAGS,C'S'       ARE WE MVS/SP MODE?
         BE    SMRMODE             YES, DO NOT RESET MODE
         XABSM MODE=24             RESET TO 24 BIT MODE
         SPACE 1
SMRMODE  DS    0H
         MODESET KEY=NZERO,MODE=PROB
         SPACE 1
         B     SMEPILOG            GO EXIT
SMNOTSO  DS    0H
         LA    R0,NOPSCB           FIRST MESSAGE LINE
         LA    R1,NOBATCH          SECOND MESSAGE LINE
         B     PUTLINEI            PRINT MESSAGE AND EXIT
SMNOAPF  DS    0H
         LA    R0,NOAUTH           FIRST MESSAGE LINE
         LA    R1,NOAUTHM          SECOND MESSAGE LINE
         B     PUTLINEI            PRINT MESSAGE AND EXIT
         EJECT                     SUBROUTINE TO WRITE TO TERMINAL
PUTLINEI DS    0H                  DROP THROUGH TO EPILOG
         XC    SMWOLDN,SMWOLDN     END OF THE LINES
         ST    R0,SMWOLDM+8        ADDRESS OF FIRST LINE
         ST    R1,SMWOLDN+8        ADDRESS OF SECOND LINE
         LA    R2,SMWOLDN          LOAD NEXT MESSAGE LINE
         ST    R2,SMWOLDM          SAVE ADDRESS IN OLD
         MVC   SMWOLDM+4,=F'1'     NUMBER OF SEGMENTS
         MVC   SMWOLDN+4,=F'1'     NUMBER OF SEGMENTS
         SPACE 1
         PUTLINE PARM=PTPB,OUTPUT=(SMWOLDM,TERM,MULTLVL,INFOR),        X
               MF=(E,IOPL)
         SPACE 1
         B     SMEPILOG
         EJECT
SMEPILOG TSOLEAVE ISN=LTORG        LEAVE THIS MESS
         EJECT
RDFRDF   EQU   X'00'               OFFSET WITHIN RDF OF RDF BLOCK ID
RDFFLGS  EQU   X'90'               OFFSET WITHIN RDF OF RDFFLAG BYTE
RDFSLEEP EQU   X'80'                 ADFMDF01 IS IN A WAIT
RDFFSCR  EQU   X'40'                 TPUT FULLSCR BEING USED
RDFWAITF EQU   X'20'                 ADFMDF01 TCB IS WAITING
RDFLOCKF EQU   X'10'                 THE LOCAL LOCK IS HELD
RDFTWAIT EQU   X'08'                 DO NOT ISSUE SYSEVENT TERMWAIT
RDFTGET  EQU   X'04'                 OUTSTANDING TGET FLAG
RDFEXIT  EQU   X'02'                 SESSION MANAGER IS TO QUIT
RDFFSREF EQU   X'01'                 RETURNING TO FULL SCREEN MODE
RDFFLG1  EQU   X'91'               OFFSET WITHIN RDF OF RDFFLG1 BYTE
RDFTPUT  EQU   X'80'                 WINBLOCK(S) UPDATED BUT SCREEN
*                                    * NOT YET UPDATED
RDFTSOIN EQU   X'40'                 LINE TO THE TMP
RDFMODAL EQU   X'20'                 MODE INDICATOR
RDFFSCRA EQU   X'10'                 DO NOT INTERCEPT I/O
RDFFSCRK EQU   X'08'                 LEAVE TSBKEYS=1 WHEN GOING TO
*                                    * FULL SCREEN MODE FLAG
RDFATTN  EQU   X'04'                 ATTN HAS BEEN ENTERED
RDFTCB   EQU   X'94'               OFFSET WITHIN RDF OF SM. TCB ADDR.
CLEAR    DC    X'40115D7E1140401D4013' KEY STRING
         DC    X'3CC76000'
         DC    X'3C4F4000'
         DC    X'3CD66000'
         DC    X'3C404000'
         DC    X'13'
CLEARL   EQU   *-CLEAR
         EJECT
ERROR    DC    AL2(ERRORL,0)
         DC    C'TSO052I '
         DC    C'SESSION MANAGER CANNOT BE RESTORED OR IS UNAVAILABLE+'
ERRORL   EQU   *-ERROR
*
ERRORM   DC    AL2(ERRORML,0)
         DC    C'TSO053I '
         DC    C'THE SESSION MANAGER ''END'' COMMAND MAY HAVE BEEN ENTEC
               RED'
ERRORML  EQU   *-ERRORM
*
NOPSCB   DC    AL2(NOPSCBL,0)
         DC    C'TSO054I '
         DC    C'UNABLE TO LOCATE TSO CONTROL BLOCKS+'
NOPSCBL  EQU   *-NOPSCB
*
NOBATCH  DC    AL2(NOBATCHL,0)
         DC    C'TSO055I '
         DC    C'THE ''SM'' COMMAND CANNOT BE RUN IN BATCH MODE'
NOBATCHL EQU   *-NOBATCH
*
NOAUTH   DC    AL2(NOAUTHL,0)
         DC    C'TSO056I '
         DC    C'UNABLE TO OBTAIN MVS AUTHORIZATION+'
NOAUTHL  EQU   *-NOAUTH
*
NOAUTHM  DC    AL2(NOAUTHML,0)
         DC    C'TSO057I '
         DC    C'THE ''SM'' COMMAND WAS NOT INVOKED IN AN APF AUTHORIZEC
               D MODE'
NOAUTHML EQU   *-NOAUTHM
         EJECT
TSODSECT DSECT                     CONTINUE USER DSECT
SMWXTRCT EXTRACT 0,'S',FIELDS=(PSB),MF=L
XTRACTL  EQU   *-SMWXTRCT          LENGTH TO CLEAR
PTPB     PUTLINE MF=L              PUTLINE PARAMETER LIST
SMWPSCBA DS    A                   PSCB ADDRESS
SMWOPRND DS    0CL2                COMMAND OPERAND
SMWFLAGS DS    XL1                 LOCAL ROUTINE FLAGS
SMWONOFF DS    CL1                 OPERAND BYTE
SMWOLDM  DS    F                   POINTER FOR MULTILINE PUTLINE
         DS    F                   NUMBER OF SEGMENTS
         DS    A                   ADDRESS OF MESSAGE SEGMENT
         SPACE 1
SMWOLDN  DS    F                   POINTER FOR MULTILING PUTLINE
         DS    F                   NUMBER OF SEGMENTS
         DS    A                   ADDRESS OF MESSAGE SEGMENT
SMWORKL  EQU   *-TSOWORK
         EJECT
*LWA     DSECT
*********************************************************************
*      THE LOGON WORK AREA WILL CONTAIN INFORMATION WHICH IS        *
*      NECESSARY FOR THE LOGON/LOGOFF PROCESSING ROUTINES.  IT      *
*      WILL CONTAIN CONTROL BLOCK POINTERS, ENTRANCE LISTS AND      *
*      PARAMETER LISTS.                                             *
*********************************************************************
*LWAPPTR  DS    A
*LWALWA   DC    CL8'   LWA  '         /*LWA CONTROL BLOCK ID      */
*
*         LWA AREA DELETED
*
*         ORG   LWA+X'94'
*LWAXXXX  DS    A                     /*POINTER TO SESSION MANAGER*/
*                                    /*RDFBLOCK AREA             */
*
*         ORG   LWA+X'9C'
*LWATCB02 DS    A                     /*POINTER TO TCB FOR        */
*                                    /*IKJEFT02                  */
*
*         ORG   LWA+X'A0'
*LWAPIOWA DS    A                     /*POINTER TO I/O DCB        */
*                                    /*WORK TABLES               */
*         DS    F                     /*RESERVED                  */
*         DS    F                     /*RESERVED                  */
*
*         DS    0D
*LWALEN   EQU   *-LWA
*         EJECT
         CVT   DSECT=YES,LIST=YES
         EJECT
         IHAPSA
         EJECT
         IHAASCB
         EJECT
         IHAASXB
         EJECT
         PRINT GEN
         EJECT
         IKJEFLWA
         EJECT
         IKJPSCB
         EJECT
         IKJTSB LIST=YES
         END
/*
//LINKEDIT EXEC PGM=IEWL,
//         PARM='LET,LIST,XREF'
//SYSPRINT  DD  SYSOUT=*
//SYSLMOD   DD  DISP=SHR,DSN=BSMITH.LOADLIB
//SYSPUNCH  DD  DSN=&&OBJ,DISP=(OLD,PASS)
//SYSUT1    DD  UNIT=SYSALLDA,SPACE=(CYL,(1,1))
//SYSLIN    DD   *
  INCLUDE  SYSPUNCH(SM)
  SETCODE  AC(1)
  NAME     SM(R)
/*
//
