)Attr DEFAULT(%+_)
  $ type(input) intens(low) hilite(uscore) caps(on)
)Body expand(\\) window(54,5)
+Enter/Verify Data Set Name:
%===>$verdsn                                        +
+
 +Enter any data set name and press enter to verify.
                     +F3 to Exit.
)INIT
  &zwinttl = 'Verify DSN Exists via Panel Rexx'
)PROC
 &resp = .resp
*REXX(* resp zedsmsg zedlmsg)
 if resp = 'END' then exit
  parse value '' with zedsmsg zedlmsg
  msgvalue = msg()              /* Save the current TSO Message State */
  call msg 'off'                /* Turn off TSO Messages */
  if sysdsn(verdsn) = 'OK' then do
     zedsmsg = 'Confirmed'
     zedlmsg = verdsn 'does exist.'
     end
  else do
     zedsmsg = 'Failure'
     zedlmsg = verdsn sysdsn(verdsn)
     end
  call msg msgvalue             /* Restore TSO Message State */
*ENDREXX
 if (&zedsmsg NE &Z)
    .MSG = ISRZ001
)END
