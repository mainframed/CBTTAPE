/* REXX :     LNKLOOK                                                */
/* FUNCTION:  FINDS ALL OCCURRENCES OF THE SPECIFIED MODULE IN THE   */
/*            LINKLST CONCATENATION                                  */
/*-------------------------------------------------------------------*/
CLEAR
ARG MODULE
CVT     = STORAGE(10,4)                   /* DECIMAL 16 ==> CVT      */
CCVT    = C2X(CVT)                        /* CHAR CVT ADDRESS        */
DCVT    = C2D(CVT)                        /* DECIMAL CVT ADDRESS     */
DLLT    = DCVT + 1244                     /* DECIMAL LLT ADDRESS     */
LLT     = D2C(DLLT,4)                     /* LLT TAB ADDRESS         */
CLLT    = C2X(LLT)                        /* CHAR LLT ADDRESS        */
ADDR1   = STORAGE(CLLT,4)                 /* ADDRESS OF LLT          */
CADDR1  = C2X(ADDR1)                      /* CHAR ADDR OF LLT        */
DATA    = STORAGE(CADDR1,8)               /* GET LUMP OF DATA        */
NUMENT  = SUBSTR(DATA,5,4)                /* NUMBER OF ENTRIES       */
DNUMENT = C2D(NUMENT)                     /* CONVERT TO DECIMAL      */
LEN     = (DNUMENT * 45) + 8              /* MAX SIZE REQUIRED       */
DATA    = STORAGE(CADDR1,LEN)             /* GET LUMP OF DATA        */
START   = 10                              /* START OF FIRST ENTRY    */
IF MODULE ¬= '' THEN,
  SAY "LINKLIST SEARCH FOR "MODULE
ELSE,
  SAY "    LINKLIST LIBRARIES "
SAY "-----------------------------"
DO N = 1 TO DNUMENT
   DSN.N = SUBSTR(DATA,START,44)
   START = START + 45
   CTR = INDEX(DSN.N,' ') - 1
   DSN.N = SUBSTR(DSN.N,1,CTR)
   IF MODULE ¬= '' THEN,
     Y = SYSDSN("'"DSN.N"("MODULE")'")
   ELSE Y = SYSDSN("'"DSN.N"'")
   W = LISTDSI("'"DSN.N"'")
   X = LEFT(DSN.N,40)
   IF Y > 0 THEN Z = '      '
   ELSE,
     Z = SYSVOLUME
   SAY X Z Y
   END
