/* REXX :     JESLOOK                                                */
/* FUNCTION:  FINDS ALL OCCURRENCES OF THE SPECIFIED MODULE IN THE   */
/*                   PROC00 JES2 PROCLIB CONCATENATION               */
/*-------------------------------------------------------------------*/
TRACE 'OFF'
SAY "JES2 SEARCH ROUTINE"
SAY "-----------------------"
ARG MODULE .
DSNMBR = 'SYS1.PROCLIB(JES2)'
"ALLOC FILE(INPUTDD) DATASET('"DSNMBR"') SHR"
RETURN = RC
IF (RETURN ¬= 0) THEN DO
  SAY 'ALLOC OF SYS1.PROCLIB FAILED RC='RETURN
  EXIT 4
  END
"EXECIO * DISKR INPUTDD (STEM DSN. FINIS"
"FREE FILE(INPUTDD)"
PROC00 = 'NO'
DO I = 1 TO DSN.0
  IF PROC00 = 'YES' THEN DO
   IF SUBSTR(DSN.I,3,1)¬='*' THEN DO
    IF SUBSTR(DSN.I,3,1) = ' ' THEN DO
      N = N + 1
      P = INDEX(DSN.I,'DSN=')
      DSN.I = SUBSTR(DSN.I,P+4)
      Q = INDEX(DSN.I,',')
      PRO.N = SUBSTR(DSN.I,1,Q-1)
      END
    ELSE PROC00 = 'NO'
    END
   END
  ELSE DO
    IF SUBSTR(DSN.I,3,6) = 'PROC00' THEN DO
      PROC00 = 'YES'
      N = 1
      P = INDEX(DSN.I,'DSN=')
      DSN.I = SUBSTR(DSN.I,P+4)
      Q = INDEX(DSN.I,',')
      PRO.N = SUBSTR(DSN.I,1,Q-1)
      END
    END
  END
DO CTR = 1 TO N
  IF MODULE ¬= '' THEN,
    Y = SYSDSN("'"PRO.CTR"("MODULE")'")
  ELSE Y = SYSDSN("'"PRO.CTR"'")
  X = LEFT(PRO.CTR,44)
  IF Y = 'DATASET NOT FOUND' THEN Z = '      '
  ELSE DO
    WHATTHAT = OUTTRAP("LD.")
    "LISTD '"PRO.CTR"'"
    Z = SUBSTR(LD.5,3,6)
    OUT = OUTTRAP(OUT)
    END
  SAY X Z Y
END
EXIT
