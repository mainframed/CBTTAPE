QDUMP    TITLE 'QSAM RECORD DUMP PROGRAM'
QDUMP    CSECT
*=====================================*
*      I N I T I A L I Z A T I O N
*=====================================*
         PRINT NOGEN
         REGS
SOJ      SAVE  (14,12)
         BALR  R12,R0
         USING *,12
         LA    R14,SAVEAREA
         ST    R13,SAVEAREA+4
         ST    R14,8(0,13)
         LR    R13,R14
         LR    R3,R1
         BAL   R11,PARMS
*=====================================*
*   M A I N    P R O C E D U R E
*=====================================*
MAIN     EQU   *
         LA    R10,SYSIN
         USING IHADCB,R10
         OPEN  (SYSIN,INPUT,SYSPRINT,OUTPUT)
         LA    R8,80
NEWREC   EQU   *
         GET   SYSIN
         LR    R7,R1
         ST    R7,BUFFER
         AP    RECOUNT,=P'1'
         ZAP   BYTES,=P'1'
LASTREC  EQU   *
         LH    R9,DCBLRECL
         TM    DCBRECFM,DCBRECV
         BNO   NOTVB
         LH    R9,0(R7)            RDW
NOTVB    EQU   *
         STH   R9,RECLEN
         LR    R4,R9
         LA    R9,0(R7,R9)
         BCTR  R9,0
         MVC   PUTLEN,=H'85'
         CP    RECOUNT,START
         BL    NEWREC
         CP    RECOUNT,STOP
LASTREC2 BNH   RECID
         CLI   LASTSW,1
         BNE   EOJ1
         BE    NEWREC
RECID    EQU   *
         MVC   RECLINE+15(16),EDMASK1
         ED    RECLINE+15(16),RECOUNT
         LH    R0,RECLEN                  R9=LRECL OR RDW
         CVD   R0,DWORD
         UNPK  RECLINE+45(5),DWORD+5(3)
         OI    RECLINE+49,X'F0'
         MVC   DATALINE(50),RECLINE
         MVC   DATALEN,=H'55'
         MVI   DATASA,C'-'
         BAL   R11,PUTLINE
SETLEN   EQU   *
         CR    R4,R8
         BNL   RULER
         LA    R1,5(R4)
         STH   R1,PUTLEN
RULER    EQU   *
         MVI   DATASA,C'0'
         MVC   DATALEN,PUTLEN
         UNPK  RULE(5),BYTES
         OI    RULE+4,X'F0'
         MVC   DATALINE(80),RULE
         BAL   R11,PUTLINE
         AP    BYTES,=P'80'
LINE1    EQU   *
         MVC   DATALINE(80),0(R7)
         CLI   ASCISW,1
         BE    TRASCII
         TR    DATALINE(80),EBCDIC           prevent printer problems
         B     NOASCII
TRASCII  EQU   *
         TR    DATALINE(80),ASCII
NOASCII  EQU   *
         MVI   DATASA,C' '
         MVC   DATALEN,PUTLEN
         BAL   R11,PUTLINE
LINE2    EQU   *
         MVC   DATALINE(80),0(R7)
         TR    DATALINE,HEXUPPER
         MVI   DATASA,C' '
         MVC   DATALEN,PUTLEN
         BAL   R11,PUTLINE
LINE3    EQU   *
         MVC   DATALINE(80),0(R7)
         TR    DATALINE,HEXLOWER
         MVI   DATASA,C' '
         MVC   DATALEN,PUTLEN
         BAL   R11,PUTLINE
ENDREC   EQU   *
         SR    R4,R8
         BXLE  R7,R8,SETLEN
LASTREC1 BC    0,EOJ1
         B     NEWREC
*=====================================*
*       T E R M I N A T I O N
*=====================================*
EOJ      EQU   *
         CLI   LASTSW,1
         BNE   EOJ1
         MVI   LASTSW,0
         MVI   LASTREC1+1,X'F0'
         MVI   LASTREC2+1,X'F0'
         L     R7,BUFFER
         B     LASTREC
EOJ1     EQU   *
         CLOSE (SYSIN,,SYSPRINT)
         L     R13,SAVEAREA+4
         RETURN (14,12),RC=0
*=====================================*
*       S U B R O U T I N E S
*=====================================*
PUTLINE  EQU   *
         AP    LINECNT,=P'1'
         CLI   DATASA,C'-'              NEW RECORD
         BE    ADD2
         CLI   DATASA,C'0'              NEW LINE GROUP - RULER
         BE    ADD1
         B     NOPAGE
ADD2     EQU   *
         AP    LINECNT,=P'1'
ADD1     EQU   *
         AP    LINECNT,=P'1'
         CP    LINECNT,=P'59'
         BL    NOPAGE
NEWPAGE  EQU   *
         ZAP   LINECNT,=P'1'
         MVI   DATASA,C'1'
NOPAGE   EQU   *
         PUT   SYSPRINT,DATALEN
         BR    R11
*=====================================*
PARMS    EQU   *
         L     R3,0(R3)
         LH    R0,0(R3)
         LA    R3,2(R3)
         C     R0,=F'0'
         BE    ENDPARMS
         CH    R0,=H'9'
         BL    ENDPARMS
         PACK  START(5),0(9,R3)
         CH    R0,=H'19'
         BL    ENDPARMS
         LA    R3,10(R3)
         PACK  STOP(5),0(9,R3)
         CH    R0,=H'24'
         BL    ENDPARMS
         LA    R3,10(R3)
         CLC   0(4,R3),=C'LAST'
         BNE   ASCI
         MVI   LASTSW,1
ASCI     EQU   *
         CH    R0,=H'29'
         BL    ENDPARMS
         LA    R3,5(R3)
         CLC   0(4,R3),=C'ASCI'
         BNE   ENDPARMS
         MVI   ASCISW,1
ENDPARMS EQU   *
         BR    R11
         EJECT
*=====================================*
*       S P I E   E X I T
*=====================================*
*      S T O R A G E   A R E A S
*=====================================*
SAVEAREA DC    18F'0'
         DS    0F
HEXUPPER DC    16CL1'0'
         DC    16CL1'1'
         DC    16CL1'2'
         DC    16CL1'3'
         DC    16CL1'4'
         DC    16CL1'5'
         DC    16CL1'6'
         DC    16CL1'7'
         DC    16CL1'8'
         DC    16CL1'9'
         DC    16CL1'A'
         DC    16CL1'B'
         DC    16CL1'C'
         DC    16CL1'D'
         DC    16CL1'E'
         DC    16CL1'F'
HEXLOWER DC    16CL16'0123456789ABCDEF'
DATALEN  DC    H'0',H'0'
DATASA   DC    CL1' '
DATALINE DC    CL80' '
RULE     DC    ZL5'0'
         DC    CL45'....10........20........30........40........5'
         DC    CL30'0........60........70........|'
RECLINE  DC    CL50'RECORD NUMBER  ................      LRECL = .....'
EDMASK1  DC    X'40',15X'20'
RECOUNT  DC    PL8'0'
RECLEN   DC    H'0'
LINECNT  DC    PL8'99'
PUTLEN   DC    H'0'
START    DC    PL5'1'
STOP     DC    PL5'1'
BYTES    DC    PL3'1'
LASTSW   DC    X'00'
ASCISW   DC    X'00'
BUFFER   DC    A(0)
DWORD    DC    D'0'
ASCII    DC    48X'00'
         DC    C'0123456789'
         DC    7X'00'
         DC    C'ABCDEFGHIJKLMNOPQRSTUVWXYZ'
         DC    6X'00'
         DC    C'abcdefghijklmnopqrstuvwxyz'
         DC    134X'00'
ASCIIL   EQU   *-ASCII
         EJECT
EBCDIC   DC    X'4040404040404040' 00-07
         DC    X'4040404040404040' 08-0F
         DC    X'4040404040404040' 1
         DC    X'4040404040404040' 1
         DC    X'4040404040404040' 2
         DC    X'4040404040404040' 2
         DC    X'4040404040404040' 3
         DC    X'4040404040404040' 3
         DC    X'4040404040404040' 4
         DC    X'4040404040404040' 4
         DC    X'4040404040404040' 5
         DC    X'4040405B40404040' 5
         DC    X'4040404040404040' 6
         DC    X'4040404040404040' 6
         DC    X'4040404040404040' 7
         DC    X'4040407B7C404040' 7
         DC    X'4081828384858687' 8
         DC    X'8889404040404040' 8
         DC    X'4091929394959697' 9
         DC    X'9899404040404040' 9
         DC    X'40A1A2A3A4A5A6A7' A
         DC    X'A8A9404040404040' A
         DC    X'4040404040404040' B
         DC    X'4040404040404040' B
         DC    X'40C1C2C3C4C5C6C7' C
         DC    X'C8C9CACBCCCDCECF' C
         DC    X'40D1D2D3D4D5D6D7' D
         DC    X'D8D9DADBDCDDDEDF' D
         DC    X'4040E2E3E4E5E6E7' E
         DC    X'E8E9EAEBECEDEEEF' E
         DC    X'F0F1F2F3F4F5F6F7' F
         DC    X'F8F9404040404040' F
SYSPRINT DCB   DDNAME=SYSPRINT,MACRF=(PM),DSORG=PS,RECFM=VBA,LRECL=85, +
               BLKSIZE=6233
SYSIN    DCB   DDNAME=SYSIN,MACRF=(GL),DSORG=PS,EODAD=EOJ
         DCBD  DSORG=PS,DEVD=DA
         PRINT GEN
         END
