R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9        PARM LENGTH
R10      EQU   10       PARM STRING PTR
R11      EQU   11       RETURN CODE
R12      EQU   12       BASE REG
R13      EQU   13       SAVE
R14      EQU   14       RETURN
R15      EQU   15       EP
*===========================================*
$DBWAIT  CSECT        D. LEES 08/91  NUSCO  *
*===========================================*
* WORKS IN CONJUNCTION WITH AO PROCEDURES.  *
* OPERATION: ISSUE A WTOR WITH MESSAGE TEXT *
* FROM EXEC PARM=, THEN
* WAIT FOR A REPLY FROM AO BEFORE*
* ALLOWING EXECUTION TO CONTINUE.           *
* REPLIES ARE: RELEASE : CONTINUE RC=0      *
*              RC=NNNN : CONTINUE RC=N-NNNN *
*              ABORT   : TERMINATE U101     *
* 3 INCORRECT REPLIES AND IT ABORTS.......  *
*===========================================*
* 10/91 ADD TIME=NN MINUTES OPTION    | DZL *
*===========================================*
         PRINT  GEN
         SAVE   (14,12)
         BALR   R12,0
         USING  *,R12
         ST     R13,SAVE+4
         LA     R0,SAVE
         ST     R0,8(R13)
         LR     R13,R0
         LR     R10,R1
         L      R10,0(R10)
INIT     EQU    *
         XR     R11,R11                      RC=0
         XR     R9,R9
         IC     R9,1(R10)
         CH     R9,=H'100'                   MAX LEN OF WTOR = 100
         BH     ABEND  PARMERR
         CH     R9,=H'6'                     SEEMS SHORT
         BL     ABEND  PARMERR
         BCTR   R9,0
         CLC    TIMER,2(R10)                 TIME=NN OPTION
         BE     SETPOP
         EX     R9,MOVE1
         B      WTOR1
MOVE1    MVC    WTORMSG(0),2(R10)
WTOR1    WTOR '                                                        C
                                                           ',          C
               REPLY,RPLYLEN,ECB1,ROUTCDE=3,DESC=(7)
WTORMSG  EQU    WTOR1+16                      VOLATILE..
         WAIT   ECB=ECB1
         XC     ECB1,ECB1
RESPOND  EQU    *
         CLC    ABORT,REPLY
         BE     ABEND
         CLC    RELEASE,REPLY
         BE     GOBACK
         CLC    RETCODE,REPLY
         BE     SETCODE
UNKNWN   AP     TRIES,ONE
         CP     TRIES,LIMIT
         BNH    WTOR1
         B      ABEND
*===========================================*
GOBACK   L      R13,SAVE+4
         LR     R15,R11
         RETURN (14,12),RC=(15)
*===========================================*
PARMERR  EQU   *
         LA    R11,20
         B     GOBACK
*===========================================*
ABEND    EQU   *
         ABEND 101,DUMP,STEP
         B     GOBACK
*===========================================*
SETCODE  EQU   *
         LA    R3,DWORD+7
         LA    R2,REPLY+6
DIGIT    EQU   *
         CLI   0(R2),C' '
         BE    NEXT
         CLI   0(R2),C'='
         BE    PACKIT
         MVC   0(1,R3),0(R2)
         BCTR  R3,0
NEXT     EQU   *
         BCTR  R2,0
         B     DIGIT
PACKIT   EQU   *
         PACK  DWORD,DWORD
         CVB   R11,DWORD
         B     GOBACK
*===========================================*
SETPOP   EQU   *
         LA    R3,DWORD+7
         LA    R2,8(R10)                     PARM+6-> NN
NN       EQU   *
         CLI   0(R2),X'00'
         BE    NXT
         CLI   0(R2),C'='
         BE    PACKS                         DEFAULTS TO 0
         MVC   0(1,R3),0(R2)
         BCTR  R3,0
NXT      EQU   *
         BCTR  R2,0
         B     NN
PACKS    EQU   *
         OC    DWORD+6(2),=X'F0F0'           MAKE IT ZONED
         NC    DWORD+6(2),=X'F7F7'           MAKE IT 0-9
         PACK  DWORD,DWORD
         CP    DWORD,=P'10'                  10 MIN. MAX
         BH    PARMERR
SETTIME  EQU   *
         UNPK  INTVL(4),DWORD                SET INTERVAL
         MVC   POPMSG+17(4),INTVL
         OI    POPMSG+20,X'F0'
POPMSG   WTO   'ENTERING 0000 MINUTE WAIT.',ROUTCDE=1
         STIMER WAIT,DINTVL=INTVL
         B     GOBACK
*===========================================*
SAVE     DC    9D'0'
DWORD    DC    CL8'00000000'   <------------ ALIGNED D
INTVL    DS    D
PARMLEN  DC    F'0'
TRIES    DC    PL2'0'
LIMIT    DC    PL2'2'                        ALLOWS 3 TRIES
ONE      DC    P'1'
*
REPLY    DC    CL119' '                      MVS LIMIT
RPLYLEN  EQU   L'REPLY
ECB1     DC    F'0'
*
ABORT    DC    CL7'ABORT  '
RELEASE  DC    CL7'RELEASE'
RETCODE  DC    CL3'RC='
TIMER    DC    CL5'TIME='
*===========================================*
         LTORG
         END
