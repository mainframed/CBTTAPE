COPYANY  CSECT   D.LEES 1981,1993,2000
         USING   COPYANY,12
         PRINT   NOGEN
SETUP    STM     14,12,12(13)
         LR      12,15
         ST      13,SAVE+4
         LA      13,SAVE
         L       1,0(1)
         CLC     0(1,1),=H'1'              ONE BYTE PARM?
         BNE     INDCB                     NOPE, DEFAULT TO X'00'
         LA      1,2(1)
         MVC     PAD,0(1)                  PARM=' ' BECOMES PAD
         CLI     PAD,C'B'                  PARM='B' FOR BLANKS
         BNE     INDCB
         MVI     PAD,C' '
INDCB    OPEN    (INFILE,INPUT)
         LA      10,INFILE
         USING   IHADCB,10
         L       11,STORADR
         USING   STORD,11
         MVC     RECFM,DCBRECFM
         MVC     LRECL,DCBLRECL
         MVC     BLKSZ,DCBBLKSI
OUTDCB   OPEN    (OUTFILE,OUTPUT)
         LA      10,OUTFILE
         USING   IHADCB,10
         MVC     RECFM2,DCBRECFM
         MVC     LRECL2,DCBLRECL
         MVC     BLKSZ2,DCBBLKSI
SELECT   TM      RECFM,B'10000000'      INDCB
         BC      14,VARIN
         BC      1,FIXIN
VARIN    TM      DCBRECFM,B'10000000'   OUTDCB
         BC      14,VBVB
         BC      1,VBFB
FIXIN    TM      DCBRECFM,B'10000000'
         BC      14,FBVB
         BC      1,FBFB
VBVB     GET     INFILE
         LR      6,1
         AP      COUNT,=P'1'
         PUT     OUTFILE
         LR      4,1
         LH      7,0(6)
         CH      7,DCBLRECL
         BC      13,NOTRUNC
         LH      7,DCBLRECL
         STH     7,0(6)                  SET RDW FOR TRUNC RECORD
NOTRUNC  LR      5,7
         ICM     7,8,PAD
         MVCL    4,6
         B       VBVB
VBFB     GET     INFILE
         LR      6,1
         AP      COUNT,=P'1'
         PUT     OUTFILE
         LR      4,1
         LH      5,DCBLRECL
         LH      7,0(6)
         A       6,=F'4'
         S       7,=F'4'
         ICM     7,8,PAD
         MVCL    4,6
         B       VBFB
FBVB     GET     INFILE
         LR      6,1
         AP      COUNT,=P'1'
         PUT     OUTFILE
         LR      4,1
         LH      5,DCBLRECL
         LH      7,LRECL
         MVC     RDW(2),DCBLRECL
         MVC     0(2,4),RDW
         A       4,=F'4'
         ICM     7,8,PAD
         MVCL    4,6
         B       FBVB
FBFB     GET     INFILE
         LR      6,1
         AP      COUNT,=P'1'
         PUT     OUTFILE
         LR      4,1
         LH      5,DCBLRECL
         LH      7,LRECL
         ICM     7,8,PAD
         MVCL    4,6
         B       FBFB
WRAPUP   CLOSE   (INFILE,,OUTFILE)
         SPACE   3
         PRINT ON,GEN
         UNPK    BANNER+180(15),COUNT(8)
         OI      BANNER+194,X'F0'
         TM      RECFM,B'10000000'
         BC      14,VB
         MVC     BANNER+120(2),=C'FB'
         B       FB
VB       MVC     BANNER+120(2),=C'VB'
FB       EQU     *
         TM      RECFM2,B'10000000'
         BC      14,VB2
         MVC     BANNER+151(2),=C'FB'
         B       FB2
VB2      MVC     BANNER+151(2),=C'VB'
FB2      EQU     *
         LH      3,LRECL
         CVD     3,DWORD
         OI      DWORD+7,X'0F'
         UNPK    BANNER+123(5),DWORD+5(3)
         LH      3,LRECL2
         CVD     3,DWORD
         OI      DWORD+7,X'0F'
         UNPK    BANNER+154(5),DWORD+5(3)
         LH      3,BLKSZ
         CVD     3,DWORD
         OI      DWORD+7,X'0F'
         UNPK    BANNER+129(5),DWORD+5(3)
         LH      3,BLKSZ2
         CVD     3,DWORD
         OI      DWORD+7,X'0F'
         UNPK    BANNER+160(5),DWORD+5(3)
         XR      0,0
BANNER   WTO   ('***************************',D),                      X
               ('*     C O P Y A N Y       *',D),                      X
               ('***************************',D),                      X
               ('* INFILE : XX-XXXXX-XXXXX *',D),                      X
               ('* OUTFILE: XX-XXXXX-XXXXX *',D),                      X
               ('* COUNT= 000000000000000  *',D),                      X
               ('***************************',D),                      X
               ('D.LEES',DE),                                          X
               ROUTCDE=11,DESC=6
         SPACE   3
         L       13,SAVE+4
         LM      14,12,12(13)
         XR      15,15
         BR      14
* * * * * END EXECUTABLE CODE  * * * * * * * * *
SAVE     DS      18F
COUNT    DC      PL8'0'
RDW      DC      F'0'
DWORD    DC      D'0'
PAD      DC      X'00'       DEFAULT PAD IS HEX 0'S
INFILE   DCB     DDNAME=INFILE,EODAD=WRAPUP,MACRF=(GL),DSORG=PS,       X
               BUFNO=20
*
         PRINT   GEN
OUTFILE  DCB     DDNAME=OUTFILE,MACRF=(PL),DSORG=PS,EXLST=EXITLIST,    X
               BUFNO=20
         PRINT   GEN
EXITLIST DS     0F
         DC     AL1(5)
         DC     VL3(EXITRTN)
         DC     X'80000000'
STORADR  DC     V(STOR)
         LTORG
*---------------------------------------------------------------------*
EXITRTN  CSECT
         USING   EXITRTN,15
         ST      14,SAVE14
         LR      10,1
         USING   IHADCB,10
         L       11,STORADDR
         USING   STORD,11
DEFAULTS CLC     DCBRECFM,=X'00'
         BNE     D1
         MVC     DCBRECFM,RECFM
D1       CLC     DCBBLKSI,=H'0'
         BNE     D2
         MVC     DCBBLKSI,BLKSZ
D2       CLC     DCBLRECL,=H'0'
         BNE     D3
         MVC     DCBLRECL,LRECL
D3       L       14,SAVE14
         BR      14
SAVE14   DS      F
STORADDR DC      V(STOR)
         LTORG
*---------------------------------------------------------------------*
STOR     CSECT
         DS      H
         DS      H
         DS      BL1
         DS      H
         DS      H
         DS      BL1
*---------------------------------------------------------------------*
STORD    DSECT
LRECL    DS      H                          INFILE
BLKSZ    DS      H
RECFM    DS      BL1
LRECL2   DS      H                          OUTFILE
BLKSZ2   DS      H
RECFM2   DS      BL1
*---------------------------------------------------------------------*
         DCBD    DSORG=PS,DEVD=DA
         END     COPYANY
