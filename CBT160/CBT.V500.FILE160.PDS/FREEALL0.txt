         TITLE 'FREEALL - FREE ALLOCATED FILES WITH SVC99'
**********************************************************************
***                                                                ***
***                   FREEALL COMMAND                              ***
***                                                                ***
***  THIS COMMAND IS A NEW VERSION OF FREEALL WHICH UTILIZES SVC99 ***
***  TO UNALLOCATE FILES.  THIS VERSION HAS THE ADVANTAGE OF USING ***
***  ALL IBM SUPPORTED CODE.  SVC99 DOES ALL THE DEALING WITH THE  ***
***  TIOT, ETC., SO THAT THIS PROGRAM CAN BE MAINTAINED BY MORE    ***
***  JUNIOR SYSTEMS PROGRAMMERS.  THIS VERSION ALSO ALLOWS THE     ***
***  DESIGNATION OF MASKS FOR INSTALLATION EXCEPTED DDNAMES,       ***
***  INSTEAD OF JUST EXPLICIT NAMES.                               ***
***                                                                ***
***  THE USER IS ABLE TO SUPPLY A LIST OF DDNAMES AND/OR MASKS TO  ***
***  BE EXCEPTED FROM THE UNALLOCATION PROCESS.  THE USER CAN ALSO ***
***  CHOOSE WHETHER TO UNALLOCATE CONCATENATIONS, AND WHETHER TO   ***
***  UNALLOCATE DATASETS NOT DYNAMICALLY ALLOCATED (USEFUL WHEN    ***
***  RUNNING TSO IN BATCH, OR WHEN YOU NEED TO FREE DATASETS       ***
***  ALLOCATED DURING LOGON).  THE USER CAN ALSO SEE A LIST OF THE ***
***  DEFAULT INSTALLATION MASKS/DDNAMES WHICH WILL ALWAYS BE       ***
***  EXCEPTED FROM THE UNALLOCATIONS.  (SEE THE HELP DOCUMENT FOR  ***
***  INSTRUCTIONS ON HOW TO CONSTRUCT DDNAME MASKS.  INSTALLATION  ***
***  MASKS ARE DEFINED WITH MACRO 'EXNAM'.)                     TS05204
***                                                                ***
***  THE TEMP (ALIAS SCRATCH), DUMMY, SYSOUT AND TERM OPERANDS  TS05203
***  HAVE BEEN ADDED TO THIS VERSION TO MAKE IT COMPATABLE WITH TS05203
***  THE J/TIP PRODUCT.                                         TS05203
***                                                             TS05203
***                                                                ***
***  SYNTAX:                                                       ***
***                                                                ***
***  FREEALL  EXCEPT(MASK1,MASK2,...)                           TS05203
***           SYSMASKS                                          TS05203
***           NOLIST | LIST                                     TS05203
***           CONCATS | NOCONCATS                               TS05203
***           DYNAM | ALL                                       TS05203
***           TEMP | NOTEMP  (OR  SCRATCH | NOSCRATCH)          TS05203
***           DUMMY | NODUMMY                                   TS05203
***           SYSOUT | NOSYSOUT                                 TS05203
***           TERM | NOTERM                                     TS05203
***                                                                ***
***                                                                ***
***  ALIAS: FA                                                     ***
***                                                                ***
***  REQUIRED OPERANDS:  NONE                                      ***
***   DEFAULT OPERANDS:  NOLIST,CONCATS,DYNAM,TEMP,             TS05203
***                      DUMMY,SYSOUT,TERM                      TS05203
***                                                                ***
*************************************************************** TS05203
         EJECT                                                  TS05203
*************************************************************** TS05203
***                                                                ***
***  NON-IBM MACROS:  (ALL FROM CBT MODS TAPE, SAME AUTHOR)        ***
***    LINKSAVE - LINKAGE CONVENTIONS IN                           ***
***    LINKBACK - LINKAGE CONVENTIONS BACK                         ***
***    GTEDADAT - CREATE SVC99 AND DAIRFAIL CONTROL BLOCKS         ***
***    GTEDASET - LINK SVC99 AND DAIRFAIL CONTROL BLOCKS TOGETHER  ***
***    GTEDAALC - EXECUTE SVC99 AND DAIRFAIL                       ***
***                                                                ***
***                                                                ***
***  ASM OPTIONS:   RENT                                           ***
***  LKED OPTIONS:  RENT REUS REFR                                 ***
***                                                                ***
***                                                                ***
***  RECOMMENDED LIBRARY:                                          ***
***        (1)  LPALIB                                             ***
***        (2)  LINKLIB                                            ***
***        (3)  CMDLIB                                             ***
***                                                                ***
**********************************************************************
         EJECT 6                                                TS05203
**********************************************************************
***                                                                ***
***  AUTHOR:                                                       ***
***     CHUCK HOFFMAN, CONSULTANT                                  ***
***     USER SERVICES CENTER                                       ***
***     BUREAU OF SYSTEMS OPERATIONS (BSO)                         ***
***     OFFICE OF MANAGEMENT INFORMATION SYSTEMS (OMIS)            ***
***     EXECUTIVE OFFICE FOR ADMINISTRATION AND FINANCE (A&F)      ***
***     COMMONWEALTH OF MASSACHUSETTS                              ***
***     ONE ASHBURTON PLACE, ROOM 1619                             ***
***     BOSTON, MA  02108                                          ***
***                                 (617) 727-5725  WORK           ***
***                                 (617) 769-8547  HOME           ***
***  ALIAS:                                                     TS05203
***     CHUCK HOFFMAN, SYSTEMS PROGRAMMER                       TS05203
***     GTE LABORATORIES, INC.                                  TS05203
***     TECHNICAL COMPUTATION CENTER                            TS05203
***     40 SYLVAN ROAD                                          TS05203
***     WALTHAM, MA  02254                                      TS05203
***                                 (617) 466-2131              TS05203
***                                                                ***
**********************************************************************
         EJECT
**********************************************************************
***                                                                ***
***  REGISTER USAGE:                                               ***
***                                                                ***
***     R0-R3  LINKAGE CONVENTIONS, MACROS, WORK                   ***
***     R4-R6  WORK                                                ***
***     R7-R8  (UNUSED)                                            ***
***        R9  BASE OF IKJPARMD DSECT                              ***
***       R10  BAL REGISTER                                        ***
***       R11  (UNUSED)                                            ***
***       R12  BASE OF PROGRAM                                     ***
***       R13  LINKAGE CONVENTIONS, BASE OF WORKD DSECT            ***
***   R14-R15  LINKAGE CONVENTIONS, WORK                           ***
***                                                                ***
*************************************************************** TS05203
         SPACE 3                                                TS05203
*************************************************************** TS05203
***                                                                ***
***  MAINTENANCE HISTORY:                                          ***
***                                                             TS05201
***  1.  ADD FT05F001 AND FT06F001 TO SYSTEM MASKS.             TS05201
***                                                             TS05201
***  2.  A.  ADD TEMP|NOTEMP (SCRATCH|NOSCRATCH), TERM|NOTERM,  TS05203
***          SYSOUT|NOSYSOUT, DUMMY|NODUMMY OPERANDS FOR        TS05203
***          COMPATABILITY WITH J/TIP.  (J/TIP 'SCHEDULE'       TS05203
***          COMMAND USES A HARDCODED CALL TO 'FREEALL' WITH    TS05203
***          THESE OPERANDS.)                                   TS05203
***                                                             TS05203
***      B.  ADD NOTATION ON OUTPUT OF WHETHER THE FILE IS      TS05203
***          A DATASET, CONCATENATION, TERMINAL, TEMP,          TS05203
***          SYSOUT OR DUMMY FILE.  REMOVE THE 'CONCATENATION'  TS05203
***          FOOTNOTE PROCESSING.                               TS05203
***                                                             TS05203
***      C.  SHOW EXPANSION OF TEXT UNITS FOR DYNAMIC           TS05203
***          ALLOCATION.                                        TS05203
***                                                             TS05203
***      D.  VARIOUS PAGE EJECTS, ETC., TO MAKE THE LISTING     TS05203
***          EASIER TO READ.                                    TS05203
***                                                             TS05203
***  3.  ADD MACROS EXNAM AND EXTBL TO MAKE IT EASIER TO DEFINE TS05204
***      INSTALLATION-EXCEPTED MASKS.                           TS05204
***                                                             TS05204
***                                                                ***
**********************************************************************
         EJECT                                                  TS05204
*************************************************************** TS05204
***                                                         *** TS05204
***   LOCAL MACROS                                          *** TS05204
***                                                         *** TS05204
*************************************************************** TS05204
*                                                               TS05204
         MACRO                                                  TS05204
         EXNAM  &MASK                                           TS05204
.************************************************************** TS05204
.** CREATE NAME TABLE FOR INST.-EXCEPTED NAMES OR MASKS     *** TS05204
.************************************************************** TS05204
         GBLA  &EXNUM                                           TS05204
         LCLA  &EXLEN                                           TS05204
         AIF   (&EXNUM NE 0).EXNAME1                            TS05204
EXNAMES  DS    0D                                               TS05204
.EXNAME1 ANOP                                                   TS05204
&EXLEN   SETA  K'&MASK                                          TS05204
         DC    CL8'&MASK',H'&EXLEN'                             TS05204
&EXNUM   SETA  &EXNUM+1                                         TS05204
         MEND                                                   TS05204
*                                                               TS05204
         SPACE 4                                                TS05204
*                                                               TS05204
         MACRO                                                  TS05204
         EXTBL ,                                                TS05204
.************************************************************** TS05204
.**   CREATE PSEUDO-PDE'S FOR INST.-EXCEPTED NAMES OR MASKS *** TS05204
.************************************************************** TS05204
         GBLA  &EXNUM                                           TS05204
EXNUMNAM EQU   &EXNUM                                           TS05204
EXNAMTBL DS    0D                       TBL FOR EXCEPTED NAMES  TS05204
.EXNTBL1 DS    A,H,XL2,A                  PSEUDO-PDE            TS05204
&EXNUM   SETA  &EXNUM-1                                         TS05204
         AIF   (&EXNUM GT 0).EXNTBL1                            TS05204
         MEND                                                   TS05204
         EJECT
**********************************************************************
***                                                                ***
***   PROLOG                                                       ***
***                                                                ***
**********************************************************************
         PRINT NOGEN
FREEALL  LINKSAVE  BASE=12,GETMAIN=YES,GETAMT=WORKDLEN
         USING WORKD,R13
         USING PARMPDL,R9               ADDRESSABILITY OF PRM DESC LST
         EQ$R
*
         ST    R1,CPPLPTR               SAVE THE CPPL POINTER
         XC    COMPCODE,COMPCODE        CLEAR THE COMPLETION CODE
         XC    RETCDE,RETCDE            CLEAR THE INTERNAL RETURN CODE
         MVI   SWITCH1,B'00000000'      INITIALIZE SWITCHES
         B     MAINLINE                 BRANCH TO MAINLINE ROUTINE
         EJECT
**********************************************************************
***                                                                ***
***   MAINLINE ROUTINE                                             ***
***                                                                ***
**********************************************************************
*
MAINLINE MVC   TUI(TUILEN),TUA          INITIALIZE INFO RETRIEVAL BLKS
         GTEDASET TUI,CPPLPTR=CPPLPTR   LINK BLOCKS TOGETHER
         MVC   TUU(TUULEN),TUB          INITIALIZE UNALLOCATION BLOCKS
         GTEDASET TUU,CPPLPTR=CPPLPTR   LINK BLOCKS TOGETHER
*
         BAL   R10,PPLSETUP             SET UP PARSE PARM LIST
         BAL   R10,PARSE                PARSE THE INPUT PARAMETERS
*
         BAL   R10,IEXCEPTS             INITIALIZE NAME EXCEPTION TABLE
*
         CLC   KSYSMASK,H1              IF 'SYSMASKS' OPERAND IS IN USE
         BNE   MINFOINI                 THEN
         BAL   R10,SYSMASKS               PRINT LIST OF DEFAULT MASKS
         B     END00                      AND GO TO ENDING
*
MINFOINI XC    RELNO,RELNO              INITIALIZE RELATIVE ALLOC NBR.
MINFO    BAL   R10,INFO                 GET INFO ABOUT NEXT DATASET
         L     R4,RETCDE                RETURN CODE TO R4
         B     *+4(R4)                    GO TO:
         B     MINFO                        00 - PROCESS NEXT DSN
         B     END00                        04 - END OF DATASETS
         B     MINFOINI                     08 - START OVER FROM TOP
         B     END0C                        0C - ERROR RETURN
         EJECT
**********************************************************************
***                                                                ***
***   ENDING                                                       ***
***                                                                ***
**********************************************************************
END00    BAL   R10,FOOTNOTE             WRITE FOOTNOTE
         XR    R15,R15                  R15 = 00
         ST    R15,COMPCODE             SAVE IT
         B     ENDING                   BRANCH TO ENDING
*
END0C    BAL   R10,FOOTNOTE             WRITE FOOTNOTE
         LA    R15,X'0C'                R15 = 0C
         ST    R15,COMPCODE             SAVE IT
         B     ENDING                   BRANCH TO ENDING
*
ENDING   LA       R4,MYPPL+(PPLANS-PPL)   ADDRESS OF PTR TO PDL
         L        R4,0(0,R4)              R4 POINTS TO PDL
         IKJRLSA  (R4)                    FREE STORAGE OF PDL
         LINKBACK RCADDR=COMPCODE
         EJECT
**********************************************************************
***                                                                ***
***        CREATE PARSE PARAMETER LIST                             ***
***                                                                ***
**********************************************************************
PPLSETUP ST    R10,PPR10SAV             SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         L     R4,CPPLPTR               ADDRESS OF CMD PROC PARM LIST
         USING CPPL,R4                    ADDRESSABILITY
         MVC   MYPPL+(PPLUPT-PPL)(4),CPPLUPT              UPT  (CPPL)
         MVC   MYPPL+(PPLECT-PPL)(4),CPPLECT              ECT  (CPPL)
         LA    R5,MYECB
         ST    R5,MYPPL+(PPLECB-PPL)                      ECB  (MINE)
         MVC   MYPPL+(PPLPCL-PPL)(4),VPARMPCL             PCL  (CSECT)
         LA    R5,MYANS
         ST    R5,MYPPL+(PPLANS-PPL)                      ANS  (MINE)
         MVC   MYPPL+(PPLCBUF-PPL)(4),CPPLCBUF            CBUF (CPPL)
         XC    MYPPL+(PPLUWA-PPL)(4),MYPPL+(PPLUWA-PPL)   UWA  (MINE)
         DROP  R4
*
PPEND    L     R10,PPR10SAV             RESTORE RETURN ADDRESS
         BR    R10                      RETURN
         EJECT
**********************************************************************
***                                                                ***
***    PARSE THE INPUT PARAMETER STRING                            ***
***                                                                ***
**********************************************************************
PARSE    ST    R10,PAR10SAV             SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         XC    MYECB,MYECB              ZERO THE ECB FOR PARSE
PARSEIT  CALLTSSR EP=IKJPARS,MF=(E,MYPPL)  PARSE THE PARMS
         L     R9,MYPPL+(PPLANS-PPL)    POINTER TO PDL ADDRESS
         L     R9,0(0,R9)               ADDRESSABILITY OF PDL
*
PAEND    L     R10,PAR10SAV             RESTORE RETURN ADDRESS
         BR    R10                      RETURN
         EJECT
**********************************************************************
***                                                                ***
***   INITIALIZE LIST OF BLOCKS RELATING TO EXCEPTION DDNAMES.     ***
***   THESE BLOCKS LOOK LIKE PDE'S, AND IF THE 'EXCEPT' KEYWORD    ***
***   IS USED, WILL BE CHAINED AHEAD OF THE IKJIDENT PDE'S WHICH   ***
***   REFER TO DDNAMES ENTERED AS SUBPARAMETERS.                   ***
***                                                                ***
**********************************************************************
IEXCEPTS ST    R10,IER10SAV             SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         LA    R4,EXNAMES               R4 POINTS TO LIST OF LEN+NAMES
         LA    R5,EXNAMTBL              R5 IS ADDRESS OF PSEUDO-PDE'S
         LA    R2,EXNUMNAM              R2 HAS NUMBER OF NAMES
IELOOP   ST    R4,0(0,R5)               ADDR OF NAME TO EXNAMTBL ENTRY
         MVC   4(2,R5),8(R4)            LGTH OF NAME TO EXNAMTBL ENTRY
         MVC   6(2,R5),X8000            FLAGS+RESERVED TO EXNAMTBL NTRY
         LA    R4,10(0,R4)              R4 POINTS TO NEXT NAME+LENGTH
         LR    R3,R5                    R3 POINTS TO CURR EXNAMTBL NTRY
         LA    R5,12(0,R5)              R5 POINTS TO NEXT EXNAMTBL NTRY
         ST    R5,8(0,R3)               ADDR OF NEXT NTRY TO CURR NTRY
         BCT   R2,IELOOP                IF NOT DONE, LOOP BACK UP
*
         MVC   8(4,R3),FF000000         MARK END OF LIST FOR NOW
*
         CLC   KEXCEPT,H1               IF 'EXCEPT' KEYWORD NOT IN USE
         BNE   IEEND                      BRANCH TO ENDING
*
         LA    R4,SBEXMASK              R4 HAS ADDR OF MASK PDE CHAIN
         ST    R4,8(0,R3)               CHAIN TO EXNAMTBL
*
IEEND    L     R10,IER10SAV             RESTORE RETURN ADDRESS
         BR    R10                      RETURN
         EJECT
**********************************************************************
***                                                                ***
***  GIVE A LISTING OF THE DEFAULT DDNAME MASKS IN TABLE EXNAMES.  ***
***                                                                ***
**********************************************************************
SYSMASKS ST    R10,SYR10SAV             SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         TPUT  SYSM1,72                 WRITE HEADING
         TPUT  BLANKS,72                WRITE BLANK LINE
*
         LA    R4,EXNUMNAM(0,0)         R4 HAS NUMBER OF NAMES
         LA    R5,EXNAMES-10            R5 POINTS TO -1 ENTRY
SYLOOP   LA    R5,10(0,R5)              INCR POINTER
         MVC   RPTLINE(80),BLANKS       CLEAR REPORT LINE
         MVC   RPTLINE+2(8),0(R5)       MOVE DDNAME TO REPORT LINE
         TPUT  RPTLINE,72               WRITE REPORT LINE
         BCT   R4,SYLOOP                IF NAMES REMAIN, LOOP BACK UP
*
         TPUT  BLANKS,72                TRAILING BLANK LINE
*
SYEND    L     R10,SYR10SAV             RESTORE RETURN ADDRESS
         BR    R10                      RETURN
         EJECT
**********************************************************************
***                                                                ***
***      SVC99 INFO RETRIEVAL, THEN PERFORM DDNAME UNALLOCATION,   ***
***      IF APPROPRIATE.                                           ***
***                                                                ***
**********************************************************************
INFO     ST    R10,INR10SAV             SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         LH    R4,RELNO                   GET LAST ALLOC NUMBER
         LA    R4,1(0,R4)                   INCR BY 1
         MVC   TUITU(TUIRBP-TUITU),TUATU  INITIALIZE INFO TEXT UNITS
         STCM  R4,3,TUIRELNO              ALLOC NUMBER TO TEXT UNIT
         STH   R4,RELNO                   ALSO SAVE IT IN STORAGE
         GTEDAALC TUI,VERB=IN,ERRMSG=YES  GET INFO ABOUT ALLOCATION
         CLC   TUIS99RC(4),F0             IF RETURN NOT ZERO
         BNE   INRET0C                      ABORT INFORMATION LOOP
*
INCKUSE  TM    TUIRTATT,X'40'           IF IN USE
         BO    INNEXT                     SKIP THIS DSN
*                                                               TS05203
INSWINIT MVI   TYPESW,X'00'             INITIALIZE SWITCHES     TS05203
*                                                               TS05203
INCKDUMY TM    TUIRTTYP,X'80'           IF DUMMY DATASET        TS05203
         BNO   INCKTERM                 THEN                    TS05203
         MVI   TYPESW,X'10'               SET TYPE SWITCH       TS05203
         CLC   KDUMMY(2),H2               IF NODUMMY OPERAND    TS05203
         BE    INNEXT                       SKIP THIS DSN       TS05203
         B     INCKDYNA                   ELSE CONTINUE CHECKS  TS05203
*                                       ELSE                    TS05203
INCKTERM TM    TUIRTTYP,X'40'           IF TERMINAL DATASET     TS05203
         BNO   INCKSYSO                 THEN                    TS05203
         MVI   TYPESW,X'18'               SET TYPE SWITCH       TS05203
         CLC   KTERM(2),H2                IF NOTERM OPERAND     TS05203
         BE    INNEXT                       SKIP THIS DSN       TS05203
         B     INCKDYNA                   ELSE CONTINUE CHECKS  TS05203
*                                       ELSE                    TS05203
INCKSYSO TM    TUIRTTYP,X'10'           IF SYSOUT DATASET       TS05203
         BNO   INCKSCR                  THEN                    TS05203
         MVI   TYPESW,X'28'               SET TYPE SWITCH       TS05203
         CLC   KSYSOUT(2),H2              IF NOSYSOUT OPERAND   TS05203
         BE    INNEXT                       SKIP THIS DSN       TS05203
         B     INCKDYNA                   ELSE CONTINUE CHECKS  TS05203
*                                       ELSE                    TS05203
INCKSCR  XR    R4,R4                    CLEAR R4 FOR INSERT     TS05203
         ICM   R4,3,TUIINDSN-2          R4 HAS LENGTH OF DSN    TS05203
         CH    R4,H17                   IF LENGTH LT 17         TS05203
         BL    INCKDYNA                   BYPASS THIS CHECK     TS05203
         MVC   DSN17(17),TUIINDSN       MOVE FIRST 17 CHARACTRS TS05203
         NC    DSN17(17),DSNNCM         KILL NUMERIC BITS       TS05203
         CLC   DSN17(17),DSNTMASK       IF=C'SYS00000.T000000.' TS05203
         BNE   INCKDYNA                 THEN                    TS05203
         MVI   TYPESW,X'20'               SET TYPE SWITCH       TS05203
         CLC   KTEMP(2),H2                IF NOTEMP OPERAND     TS05203
         BE    INNEXT                       SKIP THIS DSN       TS05203
         B     INCKDYNA                   ELSE CONTINUE CHECKS  TS05203
*
         EJECT                                                  TS05203
INCKDYNA TM    TUIRTATT,X'08'           IF NOT DYNAMICALLY ALLOCATED
         BO    INCKDDN                  THEN
         CLC   KSCOPE(2),H1               IF 'DYNAM' KEYWORD IN USE
         BE    INNEXT                       SKIP THIS DSN
*
INCKDDN  BAL   R10,EXCEPT               CHECK DDNAME FOR EXCEPTION
         CLC   RETCDE,F0                IF RETURN NOT 00
         BNE   INNEXT                     SKIP THIS DSN
*
INCKCAT  TM    TUIRTATT,X'80'           IF A MEMBER OF A CONCATENATION
         BNO   INUNALC                  THEN
         CLC   KCONCAT,H1                 IF 'CONCAT' KEYWORD IN USE
         BNE   INCKCAT1                   THEN
         MVI   TYPESW,X'08'                 SET TYPE SWITCH     TS05203
         BAL   R10,DECONCAT                 DECONCATENATE
         CLC   RETCDE,F0                      IF RC NOT 00
         BNE   INNEXT                         THEN SKIP THIS DSN
         B     INRET08                      RETURN, START CHAIN OVER
INCKCAT1 B     INNEXT                   ELSE SKIP THIS DSN
*
INUNALC  MVC   TUUTU(TUURBP-TUUTU),TUBTU  INIT TEXT UNITS FROM PROTO
         MVC   TUUDDNAM-2(10),TUIINDDN-2  INIT DDNAME FROM INFO
         GTEDAALC TUU,VERB=UN,ERRMSG=YES  UNALLOCATE THE DDNAME
*
         CLC   TUUS99RC(4),F0           IF RETURN NOT ZERO
         BNE   INNEXT                     GO TO PROCESS NEXT DSN
         BAL   R10,REPORT               ELSE PERFORM REPORT
         LH    R4,RELNO                   LOAD COUNTER TO R4
         BCTR  R4,0                       DECR BY 1
         STH   R4,RELNO                   SAVE IT AGAIN
*
INNEXT   TM    TUIRTLST,X'80'           IF LAST ENTRY
         BO    INRET04                    BRANCH TO RETURN=04
         B     INRET00                  ELSE BRANCH TO RETURN=00
*
INRET00  LA    R15,X'00'                INDICATE LOOK FOR NEXT DSN
         ST    R15,RETCDE               SAVE IT INTO RETCDE
         B     INEND                    GO TO ENDING
*
INRET04  LA    R15,X'04'                INDICATE LAST ENTRY
         ST    R15,RETCDE               SAVE IT INTO RETCDE
         B     INEND                    GO TO ENDING
*
INRET08  LA    R15,X'08'                INDICATE DECONCAT AND RESTART
         ST    R15,RETCDE               SAVE IT INTO RETCDE
         B     INEND                    GO TO ENDING
*
INRET0C  LA    R15,X'0C'                INDICATE INFORMATION ERROR
         ST    R15,RETCDE               SAVE IT INTO RETCDE
         B     INEND                    GO TO ENDING
*
INEND    L     R10,INR10SAV             RESTORE RETURN ADDRESS
         BR    R10                      RETURN
         EJECT
**********************************************************************
***                                                                ***
***    CHECK FOR EXCEPTIONS TO DDNAMES TO BE FREED                 ***
***                                                                ***
**********************************************************************
EXCEPT   ST    R10,EXR10SAV             SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         XR    R4,R4                    CLEAR R4 FOR INSERT
         ICM   R4,3,TUIINDDN-2          R4 HAS LEN OF DDNAME
         LTR   R4,R4                    IF LENGTH ZERO
         BZ    EXRET04                    RETURN = 04
         BCTR  R4,0                     R4 MINUS 1 FOR EXEC
         MVC   EXDDN(8),BLANKS          CLEAR THE WORK FIELD
         B     *+10                     BRANCH AROUND MOVE
         MVC   EXDDN(0),TUIINDDN          MOVE DDNAME TO WORK
         EX    R4,*-6                   EXECUTE THE MOVE
*
         LA    R3,EXNAMTBL              ADDRESS OF PSEUDO-PDE'S
         USING EXPDE,R3                 ADDRESSABILITY
         B     EXLOOP1A                 BRANCH INTO LOOP
*
EXLOOP1  CLC   EXNEXT,FF000000          IF END OF CHAIN
         BE    EXEND                      RETURN
         CLC   EXNEXT,F0                ELSE IF NO SUBFIELD
         BE    EXEND                      RETURN
         L     R3,EXNEXT                POINT TO NEXT PSEUDO-PDE
*
EXLOOP1A L     R4,EXADDR                R4 POINTS TO DDN MASK
         BCTR  R4,0                       MINUS 1 FOR LOOP
         LH    R5,EXLEN                 R5 IS LENGTH OF MASK
         LA    R6,EXDDN-1               R6 POINTS TO WORK DDN MINUS 1
*
EXLOOP2  LA    R6,1(0,R6)               POINT TO NEXT WORK DDN CHAR
         LA    R4,1(0,R4)               POINT TO NEXT MASK CHAR
         CLI   0(R4),C'-'               IF MASK CHAR IS HYPHEN
         BE    EXMATCH                    TREAT AS MATCH
         CLI   0(R6),X'40'              IF WORK CHAR BLANK
         BE    EXNOMTCH                   LEAVE THE LOOP
         CLI   0(R4),C'*'               IF MASK CHAR IS ASTERISK,
         BE    EXLOOP2N                   STAY IN LOOP
         CLC   0(1,R4),0(R6)            IF CHARS DON'T MATCH
         BNE   EXNOMTCH                   LEAVE THE LOOP
EXLOOP2N BCT   R5,EXLOOP2               MATCH SO FAR, LOOP BACK UP
*                       ***FALL OUT OF LOOP ***
         CLC   TUIINDDN-2(2),EXLEN      IF DDNAME LEN GT MASK LEN
         BH    EXNOMTCH                   NO MATCH AT END OF LOOP
EXMATCH  B     EXRET04                  ALL CHARS MATCH, RETURN = 04
*
EXNOMTCH B     EXLOOP1                  MISMATCH, STAY IN OUTER LOOP
*
EXRET04  LA    R15,X'04'                RETURN CODE TO R15
         ST    R15,RETCDE               SAVE IT
         B     EXEND                    BRANCH TO ENDING
*
EXEND    L     R10,EXR10SAV             RESTORE RETURN ADDRESS
         BR    R10                      RETURN
         EJECT
**********************************************************************
***                                                                ***
***    UNALLOCATE A CONCATENATION.  IF UNALLOCATION IS SUCCESSFUL, ***
***    THE FILE RELATIVE NUMBERS MAY CHANGE UNPREDICTABLY.         ***
***    THEREFORE, THE RETURN CODE OF 4 WILL CAUSE AN EVENTUAL      ***
***    RETURN CODE TO THE MAINLINE PROCEDURE WHICH WILL INDICATE   ***
***    THAT THE RELATIVE NUMBER OF THE FILES SHOULD BE RESET TO    ***
***    ONE, AND THE PROCESS OF OBTAINING INFORMATION SHOULD BE     ***
***    BEGUN AGAIN FROM THE TOP.                                   ***
***                                                                ***
**********************************************************************
DECONCAT ST    R10,DER10SAV             SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         MVC   TUUTU(TUURBP-TUUTU),TUBTU  INIT TEXT UNITS FROM PROTO
         MVC   TUUDDNAM-2(10),TUIINDDN-2  MOVE DDNAME INTO CNTRL BLOCK
         GTEDAALC TUU,VERB=UN,ERRMSG=YES  DECONCATENATE
         CLC   TUUS99RC(4),F0             IF RETURN CODE NOT ZERO
         BNE   DERET04                      RETURN = 04
*
         OI    SWITCH1,X'40'            INDICATE CONCATENATION FREED
         BAL   R10,REPORT               WRITE A MESSAGE
         B     DEEND                    RETURN = 00
*
DERET04  LA    R15,X'04'                RETURN CODE TO R15
         ST    R15,RETCDE               SAVE IT
         B     DEEND                    BRANCH TO ENDING
*
DEEND    L     R10,DER10SAV             RESTORE RETURN ADDRESS
         BR    R10                      RETURN
         EJECT
**********************************************************************
***                                                                ***
***    CREATE ONE LINE OF A LISTING OF FREED DDNAMES/DSNAMES       ***
***                                                                ***
**********************************************************************
REPORT   ST    R10,RER10SAV             SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         CLC   KLIST,H1                 IF 'LIST' OPERAND NOT IN USE
         BNE   REEND                      BYPASS THIS WHOLE PROCEDURE
*
         TM    SWITCH1,X'80'            IF HEADING ALREADY PRINTED
         BO    REBODY                     SKIP WRITING HEADING
         TPUT  BLANKS,1                 WRITE BLANK LINE
         LA    R4,HDG1                  POINT TO HEADING
         LA    R5,L'HDG1                LENGTH OF HEADING
         TPUT  (R4),(R5)                  WRITE THE HEADING
         TPUT  BLANKS,1                 WRITE BLANK LINE
         OI    SWITCH1,X'80'            SET HEADING-PRINTED SWITCH
*
REBODY   MVC   RPTLINE(80),BLANKS       INITIALIZE REPORT LINE
         XR    R4,R4                    CLEAR R4 FOR INSERT
         ICM   R4,3,TUIINDDN-2          LENGTH OF DDNAME TO R4
         BCTR  R4,0                       MINUS 1 FOR EXEC
         B     *+10                     BRANCH AROUND MOVE
         MVC   RPTLINE+2(0),TUIINDDN      MOVE DDNAME TO REPORT
         EX    R4,*-6                   PERFORM THE MOVE
*                                                               TS05203
         XR    R4,R4                    CLEAR R4 FOR INSERT     TS05203
         IC    R4,TYPESW                OFFSET INTO TYPETABL    TS05203
         LA    R4,TYPETABL(R4)          ADDR OF TYPE LITERAL    TS05203
         MVC   RPTLINE+12(8),0(R4)      MOVE TYPE TO REPORT     TS05203
*
         XR    R4,R4                    CLEAR R4 FOR INSERT
         ICM   R4,3,TUIINDSN-2          LENGTH OF DSNAME TO R4
         BCTR  R4,0                       MINUS 1 FOR EXEC
         B     *+10                     BRANCH AROUND MOVE
         MVC   RPTLINE+22(0),TUIINDSN     MOVE DSNAME TO REPORT TS05203
         EX    R4,*-6                   PERFORM THE MOVE
*
*        TM    SWITCH1,X'40'     IF A CONCATENATION WAS FREED   TS05203
*        BNO   *+12              THEN                           TS05203
*        MVI   RPTLINE,C'*'        PUT AN '*' ON THE PRINT LINE TS05203
*        OI    SWITCH1,X'20'       REMEMBER FOOTNOTE            TS05203
*        NI    SWITCH1,X'BF'       TURN OFF CONCAT-FREED SWITCH TS05203
*
         LA    R4,RPTLINE               POINT TO REPORT LINE
         LA    R5,80(0,0)               LENGTH OF LINE
         TPUT  (R4),(R5)                WRITE THE REPORT LINE
*
         B     REEND
*
REEND    L     R10,RER10SAV             RESTORE RETURN ADDRESS
         BR    R10                      RETURN
         EJECT
**********************************************************************
***                                                                ***
***   WRITE THE FOOTNOTE                                           ***
***                                                                ***
**********************************************************************
FOOTNOTE ST    R10,FOR10SAV             SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         CLC   KLIST,H1                 IF 'LIST' OPERAND NOT IN USE
         BNE   FOEND                      BYPASS THIS WHOLE PROCEDURE
*
         TM    SWITCH1,X'80'            IF HEADING NOT WRITTEN
         BNO   FOEND                      SKIP ALL
         TM    SWITCH1,X'20'            IF A CONCATENATION NOT FREED
         BNO   FOBLANK                    SKIP FOOTNOTE #1 AND #2
*        TPUT  FOOT1,72                 WRITE FOOTNOTE #1       TS05203
*        TPUT  FOOT2,72                 WRITE FOOTNOTE #2       TS05203
*
FOBLANK  TPUT  BLANKS,72                POINT TO BLANK LINE
*
FOEND    L     R10,FOR10SAV             RESTORE RETURN ADDRESS
         BR    R10                      RETURN
         EJECT
**********************************************************************
***                                                                ***
***    DATA CONSTANTS                                              ***
***                                                                ***
**********************************************************************
DATACNST DS    0D
VPARMPCL DC    V(PARMPCL)               ADDR OF PARM CONTROL LIST
F0       DC    F'0'                     CONSTANT
H1       DC    H'1'                     CONSTANT
H2       DC    H'2'                     CONSTANT                TS05203
H17      DC    H'17'                    CONSTANT                TS05203
DSNNCM   DC    X'FFFFFFF0F0F0F0F0FFFFF0F0F0F0F0F0FF'            TS05203
*                S Y S # # # # # . T # # # # # # .              TS05203
DSNTMASK DC    CL17'SYS00000.T000000.'  FORMAT OF TEMP DSN (17) TS05203
TYPETABL DS    0D                                               TS05203
         DC    CL8'Dataset'             LITERAL                 TS05203
         DC    CL8'Concat.'             LITERAL                 TS05203
         DC    CL8'Dummy'               LITERAL                 TS05203
         DC    CL8'Terminal'            LITERAL                 TS05203
         DC    CL8'Temp.'               LITERAL                 TS05203
         DC    CL8'Sysout'              LITERAL                 TS05203
*                                                               TS05203
BLANKS   DC    CL80' '                  LITERAL
HDG1     DC    CL72'The following files have been freed:'
SYSM1    DC    CL72'Installation defined masks in use are:'
*   FOOT1    DC    CL72'_____________________________'          TS05203
*   FOOT2    DC    CL72'* Indicates a concatenation'            TS05203
*
*                                                               TS05204
         PUSH  PRINT                                            TS05204
         PRINT GEN                                              TS05204
*    +-----------------------------------------+                TS05204
*    |                                         |
*    |  SEE THE HELP DOCUMENT FOR INSTRUCTIONS |
*    |  ON HOW TO CONSTRUCT THESE MASKS.       |
*    |                                         |
*    +-----------------------------------------+
*                                                               TS05204
         EXNAM  ISP-                    ISPF FILES              TS05204
         EXNAM  SYSPROC                 TSO CLISTS              TS05204
         EXNAM  SYSHELP                 TSO HELP                TS05204
         EXNAM  SYSPRINT                UTILITY OUTPUT          TS05204
         EXNAM  SYSIN                   UTILITY INPUT           TS05204
         EXNAM  FT05F001                FORTRAN INPUT           TS05201
         EXNAM  FT06F001                FORTRAN OUTPUT          TS05201
         EXNAM  PANDD*                  PANVALET FILES          TS05204
         POP    PRINT                                           TS05204
*
*
FF000000 DC    XL4'FF000000'
X8000    DC    XL2'8000'
*                                       CHANGE MACRO, BELOW:    TS05203
TUA      GTEDADAT  RELNO=X,RTLST=X,INDDN=X,INDSN=X,RTATT=X,            X
               RTTYP=X                                          TS05203
TUB      GTEDADAT  DDNAM=X,UNALC=X
         EJECT
**********************************************************************
***                                                                ***
***    COMMAND OPERANDS                                            ***
***                                                                ***
***  SYNTAX:                                                       ***
***                                                                ***
***  FREEALL  EXCEPT(MASK1,MASK2,...)                           TS05203
***           SYSMASKS                                          TS05203
***           NOLIST | LIST                                     TS05203
***           CONCATS | NOCONCATS                               TS05203
***           DYNAM | ALL                                       TS05203
***           TEMP | NOTEMP  (OR  SCRATCH | NOSCRATCH)          TS05203
***           DUMMY | NODUMMY                                   TS05203
***           SYSOUT | NOSYSOUT                                 TS05203
***           TERM | NOTERM                                     TS05203
***                                                                ***
***  REQUIRED OPERANDS:  NONE                                   TS05203
***   DEFAULT OPERANDS:  NOLIST,CONCATS,DYNAM,TEMP,             TS05203
***                      DUMMY,SYSOUT,TERM                      TS05203
**********************************************************************
PARMPCL  IKJPARM  DSECT=PARMPDL
*
KLIST    IKJKEYWD DEFAULT='NOLIST'
         IKJNAME 'LIST'
         IKJNAME 'NOLIST'
*
KSCOPE   IKJKEYWD DEFAULT='DYNAM'
         IKJNAME 'DYNAM'
         IKJNAME 'ALL'
*
KCONCAT  IKJKEYWD DEFAULT='CONCATS'
         IKJNAME 'CONCATS'
         IKJNAME 'NOCONCATS'
*
KEXCEPT  IKJKEYWD
         IKJNAME 'EXCEPT',SUBFLD=SBEXCEPT
*
KSYSMASK IKJKEYWD
         IKJNAME 'SYSMASKS'
*                                                               TS05203
*     ----  OPERANDS FOR COMPATABILITY WITH J/TIP ---           TS05203
KTEMP    IKJKEYWD DEFAULT='TEMP'                                TS05203
         IKJNAME 'TEMP',ALIAS=('SCRATCH')                       TS05203
         IKJNAME 'NOTEMP',ALIAS=('NOSCRATCH')                   TS05203
*                                                               TS05203
KTERM    IKJKEYWD DEFAULT='TERM'                                TS05203
         IKJNAME 'TERM'                                         TS05203
         IKJNAME 'NOTERM'                                       TS05203
*                                                               TS05203
KDUMMY   IKJKEYWD DEFAULT='DUMMY'                               TS05203
         IKJNAME 'DUMMY'                                        TS05203
         IKJNAME 'NODUMMY'                                      TS05203
*                                                               TS05203
KSYSIN   IKJKEYWD DEFAULT='SYSIN'                               TS05203
         IKJNAME 'SYSIN'                                        TS05203
         IKJNAME 'NOSYSIN'                                      TS05203
*                                                               TS05203
KSYSOUT  IKJKEYWD DEFAULT='SYSOUT'                              TS05203
         IKJNAME 'SYSOUT'                                       TS05203
         IKJNAME 'NOSYSOUT'                                     TS05203
*
SBEXCEPT IKJSUBF
SBEXMASK IKJIDENT 'FILE/DD NAME OR MASK',LIST,                         X
               UPPERCASE,MAXLNTH=8,                                    X
               FIRST=ANY,OTHER=ANY,                                    X
               PROMPT='FILE/DD NAME OR MASK',                          X
               HELP=('1-8 CHARACTER FILE NAME (DDNAME) OR MASK')
*
         IKJENDP
         EJECT
**********************************************************************
***                                                                ***
***    WORK DATA AREA, POINTED TO BY R13                           ***
***                                                                ***
**********************************************************************
WORKD    DSECT
MYSAVE   DS    18F                      MY SAVE AREA
COMPCODE DS    F                        PROGRAM COMPLETION CODE
CPPLPTR  DS    F                        POINTER TO CPPL
RETCDE   DS    F                        INTERNAL RETURN CODE
INR10SAV DS    F                        RETURN ADDRESS SAVE AREA
EXR10SAV DS    F                        RETURN ADDRESS SAVE AREA
IER10SAV DS    F                        RETURN ADDRESS SAVE AREA
SYR10SAV DS    F                        RETURN ADDRESS SAVE AREA
DER10SAV DS    F                        RETURN ADDRESS SAVE AREA
FOR10SAV DS    F                        RETURN ADDRESS SAVE AREA
PAR10SAV DS    F                        RETURN ADDRESS SAVE AREA
PPR10SAV DS    F                        RETURN ADDRESS SAVE AREA
RER10SAV DS    F                        RETURN ADDRESS SAVE AREA
RELNO    DS    H                        RELATIVE NUMBER OF ALLOCATION
RPTLINE  DS    CL80                     REPORT LINE
MYPPL    DS    7F                       PARSE PARAMETER LIST
MYECB    DS    F                        ECB FOR PARSE
MYANS    DS    F                        POINTER TO THE PDL
DSN17    DS    CL17                     WORK AREA FOR TEMP DSN  TS05203
SWITCH1  DS    B'00000000'              SWITCHES
*                1.......                X'80' - HEADING LINE PRINTED
*                .1......                X'40' - CONCATENATION IS FREED
*                ..1.....                X'20' - CONCAT WAS FREED ONCE
*                ...11111                UNUSED
TYPESW   DS    XL1                      INDICATE FILE TYPE      TS05203
*                                         X'00' - NOT SPECIAL   TS05203
*                                         X'08' - CONCATENATION TS05203
*                                         X'10' - DUMMY FILE    TS05203
*                                         X'18' - TERMINAL FILE TS05203
*                                         X'20' - TEMP FILE     TS05203
*                                         X'28' - SYSOUT FILE   TS05203
EXDDN    DS    CL8                      WORK AREA FOR DDNAME
*
*
         PUSH  PRINT                                            TS05204
         PRINT GEN                                              TS05204
*    +-----------------------------------------+                TS05204
*    |  TABLE OF PSEUDO-PDE'S FOR INSTALLATION |                TS05204
*    |  EXCEPTED DDNAMES OR MASKS.             |                TS05204
*    +-----------------------------------------+                TS05204
         EXTBL                                                  TS05204
         POP   PRINT                                            TS05204
*                                                               TS05204
*                                                               TS05204
*                                                               TS05204
*                                                               TS05204
*                                                               TS05204
*
         PUSH  PRINT                                            TS05203
         PRINT GEN                                              TS05203
         EJECT                                                  TS05203
*                                       CHANGE MACRO, BELOW:    TS05203
TUI      GTEDADAT  MAP=ONLY,RELNO=X,RTLST=X,INDDN=X,INDSN=X,RTATT=X,   X
               RTTYP=X                                          TS05203
         EJECT                                                  TS05203
TUU      GTEDADAT  MAP=ONLY,DDNAM=X,UNALC=X
         POP   PRINT                                            TS05203
*
         DS    0D                       ALIGN FOR GETMAIN
WORKDLEN EQU   *-WORKD                  LENGTH OF AREA
         EJECT
**********************************************************************
***                                                                ***
***    MAPPING DSECTS                                              ***
***                                                                ***
**********************************************************************
         PRINT NOGEN
         IEFZB4D0
         IKJEFFDF DFDSECT=YES,DFDSEC2=YES
         IKJCPPL                        COMMAND PROCESSOR PARM LIST
         IKJPPL                         PARSE PARM LIST
*
EXPDE    DSECT                          PDE FOR LIST OF DDNAME MASKS
EXADDR   DS    F                          ADDRESS OF MASK
EXLEN    DS    H                          LENGTH OF MASK
EXFLAGS  DS    BL1                        FLAGS X'80'
         DS    XL1                        NOT USED X'00'
EXNEXT   DS    F                          ADDR OF NEXT PDE
*
         CVT   DSECT=YES              , CVTMAP FOR IKJPARS
         END
