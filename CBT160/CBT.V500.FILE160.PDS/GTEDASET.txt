         MACRO
&LABEL   GTEDASET &NM,&CPPLPTR=
.*
.*          +-----------------------------------------------+
.*          |  See macro GTEDADOC for documentation of the  |
.*          |  use of this and other GTEDAxxx macros.       |
.*          +-----------------------------------------------+
.*
*
**********************************************************************
***   SET POINTERS AND DATA FOR DYNAMIC ALLOCATION IN DATA AREAS   ***
***   CREATED BY MACRO GTEDADAT.                                   ***
**********************************************************************
*
**********************************************************************
***      PLACE ADDRESS OF S99RB INTO S99RBPTR.                     ***
**********************************************************************
*
&LABEL   LA    1,&NM.RB                       ==> R1 POINTS TO S99RB
         ST    1,&NM.RBP                          FILL IN S99RBPTR
         OI    &NM.RBP,X'80'                      TURN ON HI BIT
*
**********************************************************************
***      FILL IN S99RB REQUEST BLOCK.                              ***
***      MAPPING MACRO IEFZB4D0 MUST APPEAR IN SOURCE.             ***
**********************************************************************
*
         MVI   (S99RBLN-S99RB)(1),X'14'           FILL IN LENGTH
         MVI   (S99VERB-S99RB)(1),X'00'           ZERO VERB CODE
         XC    (S99FLAG1-S99RB)(4,1),(S99FLAG1-S99RB)(1)   ZERO FLAGS1
         OI    (S99FLAG1-S99RB)(1),S99ONCNV       SET FLAGS1 OK CONVRT
         OI    (S99FLAG1-S99RB)(1),S99NOMNT       SET FLAGS1 NO MOUNT
         LA    2,&NM.TUPL                     ==> R2 POINTS TO TUPL
         ST    2,(S99TXTPP-S99RB)(0,1)            TUPL ADDR INTO RB
         XC    (S99FLAG2-S99RB)(4,1),(S99FLAG2-S99RB)(1)   ZERO FLAGS2
*
**********************************************************************
***  BUILD TUPL.                                                   ***
***  KEY OF LAST TEXT UNIT MUST BE X'FF..'                         ***
**********************************************************************
*
         LA    1,&NM.TU                       ==> R1 POINTS TO TUNITS
         XR    0,0                                CLEAR R0 FOR COUNTER
         XR    15,15                              CLEAR R15 WORK REG
*
RBA&SYSNDX CLI (S99TUKEY-S99TUNIT)(1),X'FF'       IF KEY IS X'FF..'
         BE    RBD&SYSNDX                           BRANCH OUT OF LOOP
         ST    1,0(0,2)                           ADDR OF KEY TO TUPL
         LA    2,4(0,2)                           INCR POINTER TO TUPL
*
         ICM   0,3,(S99TUNUM-S99TUNIT)(1)         TU NUM TO R0
         LA    1,4(0,1)                           R1 POINTS BEYOND NUM
         LTR   0,0                                IF TU NUM IS 0
         BZ    RBA&SYSNDX                         PROCESS NEXT TU
*
         ICM   15,3,0(1)                          TU LEN TO R15
         LA    1,2(15,1)                          PNT BEYND LEN & PARM
         BCT   0,*-8                              DO FOR EACH LEN+PARM
*
         B     RBA&SYSNDX                         PROCESS NEXT TU
*
RBD&SYSNDX LA  15,4(0,0)                          POINT TO LAST
         SR    2,15                                 TUPL ENTRY
         OI    0(2),S99TUPLN                      MARK LAST TUPL ENTRY
*
**********************************************************************
***  FILL IN THE DAIRFAIL PARAMETER BLOCK.                         ***
***  IKJEFFDF DFDSECT=YES,DFDSEC2=YES MUST APPEAR IN SOURCE.       ***
**********************************************************************
*
         MVC   &NM.DFPRM+(DFS99RBP-DFDSECTD)(4),&NM.RBP *PTR TO RB
*
         LA    1,&NM.S99RC
         ST    1,&NM.DFPRM+(DFRCP-DFDSECTD)             *PTR TO S99RC
*
         LA    1,&NM.F02EP
         ST    1,&NM.DFPRM+(DFJEFF02-DFDSECTD)          *PTR TO EP
*
         LA    1,&NM.ID
         ST    1,&NM.DFPRM+(DFIDP-DFDSECTD)             *PTR TO SW+ID
*
         MVC   &NM.DFPRM+(DFCPPLP-DFDSECTD)(4),&CPPLPTR *PTR TO CPPL
*
         XC  &NM.DFPRM+(DFBUFP-DFDSECTD)(4),&NM.DFPRM+(DFBUFP-DFDSECTD)
*                                                       *ZERO BUFF ADDR
         MEND
