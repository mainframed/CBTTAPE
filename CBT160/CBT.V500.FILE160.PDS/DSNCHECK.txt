         TITLE 'DSNCHECK - CHECK FOR DSN OR MEMBER NAME'     CAH83351A
         PRINT NOGEN
**********************************************************************
***                      DSNCHECK                                  ***
***                                                                ***
***   THIS TSO COMMAND PROCESSOR CHECKS THE SYSTEM CATALOG FOR     ***
***   THE EXISTENCE OF A NAME OR ALIAS.  IF A MEMBER NAME IS CAH83351A
***   SPECIFIED, DSNCHECK ATTEMPTS TO ALLOCATE THE DATASET,  CAH83351A
***   OPEN IT, AND CHECK FOR A MEMBER NAME WITH BLDL.        CAH83351A
***                                                                ***
***   SYNTAX:                                                      ***
***                                                                ***
***        DSNCHECK  DSNAME  LIST    VOLCHECK                CAH84310A
***        DSNC              NOLIST  NOVOLCHECK              CAH84310A
***                                                                ***
***                                                                ***
***   ASM OPTIONS:   RENT                                          ***
***   LKED OPTIONS:  RENT, REUS, ALIAS(DSNC)                       ***
***                                                                ***
***   RETURN CODES:                                                ***
***        00 - SUCCESSFUL                                   CAH83351A
***        12 - UNSUCCESSFUL                                 CAH83351A
***                                                                ***
***   NON-IBM MACROS:  GTEDADAT - CREATE SVC99/DAIRFAIL      CAH83351A
***                               CONTROL BLOCKS             CAH83351A
***                    GTEDASET - LINK GTEDADAT BLOCKS       CAH83351A
***                    GTEDAALC - DYNAMIC ALLOCATE/DAIRFAIL  CAH83351A
***                                                                ***
***   REGISTER USAGE:                                              ***
***        R0-R1 - LINKAGE CONVENTIONS, MACROS                     ***
***        R2-R3 - (UNUSED)                                        ***
***        R4-R5 - WORK                                            ***
***        R6-R8 - (UNUSED)                                        ***
***           R9 - BASE OF DSECT IKJPARMD                          ***
***          R10 - BAL REGISTER                                    ***
***          R11 - BASE OF CSECT MYPCL                             ***
***          R12 - BASE OF CSECT CHECKDSN                          ***
***          R13 - BASE OF REG SAVE AREA AND WORK0                 ***
***          R14 - LINKAGE CONVENTIONS                             ***
***          R15 - LINKAGE CONVENTIONS, RETURN CODE                ***
***                                                                ***
***   AUTHOR:                                                      ***
***      CHUCK HOFFMAN, CONSULTANT                                 ***
***                                                                ***
***      USER SERVICES CENTER                                      ***
***      BUREAU OF SYSTEMS OPERATIONS                              ***
***      OFFICE OF MANAGEMENT INFORMATION SYSTEMS                  ***
***      EXECUTIVE OFFICE FOR ADMINISTRATION AND FINANCE           ***
***      COMMONWEALTH OF MASSACHUSETTS (PHEW!)                     ***
***                                                               ***
***      ONE ASHBURTON PLACE                                       ***
***      BOSTON, MA  02108                                         ***
***                                                                ***
**********************************************************************
         EJECT
**********************************************************************
***                                                                ***
***   MAINTENANCE HISTORY:                                         ***
***                                                                ***
***   1.  A.  ADD FEATURE TO ALLOCATE, OPEN, AND BLDL IF A   CAH83351A
***           MEMBER NAME IS SPECIFIED IN THE DSNAME.        CAH83351A
***                                                          CAH83351A
***       B.  MODIFY REPORTING TO ACCOUNT FOR MEMBER NAMES.  CAH83351A
***                                                          CAH83351A
***       C.  CHANGE DEFAULT FROM NOLIST TO LIST.            CAH83351A
***                                                          CAH83351A
***   2.  A.  ADD OBTAIN|NOOBTAIN OPERAND,                   CAH84310A
***           ALIAS VOLCHECK|NOVOLCHECK.                     CAH84310A
***                                                          CAH84310A
***       B.  CHANGE WORDING OF OUTPUT MESSAGE.              CAH84310A
***                                                          CAH84310A
***       C.  CHANGE OUTPUT BUFFER LENGTH FROM 80 TO 120.    CAH84310A
***                                                                ***
***                                                                ***
***                                                                ***
***                                                                ***
***                                                                ***
***                                                                ***
***                                                                ***
***                                                                ***
***                                                                ***
***                                                                ***
***                                                                ***
***                                                                ***
***                                                                ***
***                                                                ***
**********************************************************************
         EJECT
**********************************************************************
***      MACROS                                                    ***
**********************************************************************
         MACRO
&LABL    PRFRM &NAME,&ERRAD=
&LABL    BAL   RBAL,&NAME               PERFORM PROCEDURE
         CLC   RETCDE(4),F0               IF RETURN CODE 0
         BE    *+12                         BRANCH AROUND
         BAL   RBAL,ERROR                 ELSE PERFORM ERROR ROUTINE
         B     &ERRAD                       AND GO TO ERROR ADDRESS
         MEND
         EJECT
**********************************************************************
***            EQUATES                                             ***
**********************************************************************
RBASE    EQU   12                       BASE REGISTER NUMBER
RBAL     EQU   10                       BAL REGISTER NUMBER
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
**********************************************************************
***      LINKAGE CONVENTIONS IN                                    ***
**********************************************************************
DSNCHECK CSECT                        , CSECTID
         B     28(0,R15)                BRANCH AROUND ID
         DC    AL1(8)                     LENGTH
         DC    CL8'DSNCHECK'              ID
         DC    CL8'&SYSDATE'              SYSTEM DATE MM/DD/YY
         DC    CL6' &SYSTIME'             SYSTEM TIME HH.MM
         STM   R14,R12,12(R13)          SAVE R0-R12, R14-R15
         LR    RBASE,R15                LOAD BASE ADDRESS BASER
         USING DSNCHECK,RBASE           ADDRESSABILTIY OF CSECT
         GETMAIN R,LV=WORK0LEN          GET STORAGE FOR S.A. + WORK
         ST    R13,4(0,R1)              ADDR OF PREV S.A. TO MY S.A.
         ST    R1,8(0,R13)              ADDR OF MY S.A. TO PREV S.A.
         LM    R0,R1,20(R13)            RESTORE R0-R1 TO PRE-GETMAIN
         L     R13,8(0,R13)             R13 POINTS TO MY S.A.
         USING WORK0,R13                ADDRESSABILITY OF WORK0
         ST    R1,R1SAVE                INITIAL VAL OF R1 TO MY STORAGE
         XC    COMPCODE,COMPCODE        CLEAR COMPLETION CODE
         XC    RETCDE,RETCDE            CLEAR INTERNAL RETURN CODE
         XC    OBCDE,OBCDE              CLEAR OBTAIN RET CDE  CAH84310A
         EJECT
**********************************************************************
***      MAINLINE PROCESSING                                       ***
**********************************************************************
MAIN     CNOP  0,4                      ALIGN
*
         LA    R11,MYPPL                ADDRESSABILITY OF
         USING PPL,R11                    MY PARS PARM LIST
         USING IKJPARMD,R9                PARM DESCRIPTOR LIST
*
         PRFRM PARSE,ERRAD=MAEND        PARSE THE PARMS
         PRFRM DSN,ERRAD=MAEND          GET DATASET NAME
         BAL   RBAL,INITCIR             INITIALIZE CIR PARM LIST
         PRFRM CIR,ERRAD=MALIST         GET CATALOG INFORMATION
         PRFRM MEMBR,ERRAD=MALIST       GET MEMBER INFO      CAH83351A
*                                                            CAH83351A
MALIST   BAL   RBAL,LIST                PRODUCE MESSAGE
*
         B     MAEND                    GO TO END OF MAINLINE
*
MAEND    B     ENDING                   GO TO LINKAGE BACK TO CALLER
         EJECT
**********************************************************************
***      LINKAGE CONVENTIONS BACK                                  ***
**********************************************************************
ENDING   XC    COMPCODE,COMPCODE        CLEAR COMPLETION CDE CAH83351A
         CLC   OBCDE(4),F0              IF OBTAIN R/C BAD     CAH84310A
         BE    *+14                       THEN                CAH84310A
         MVC   COMPCODE(4),F12            SET COMP CODE 12    CAH84310A
         B     ENDRLSE                  ELSE                  CAH84310A
         CLC   BLDLCDE(4),F0            IF BLDL R/C BAD      CAH83351A
         BE    *+14                       THEN               CAH83351A
         MVC   COMPCODE(4),F12            SET COMP CODE 12   CAH83351A
         B     ENDRLSE                  ELSE                 CAH83351A
         CLC   CIRCDE(4),F0             IF CIR R/C BAD       CAH83351A
         BE    ENDRLSE                    THEN               CAH83351A
         MVC   COMPCODE(4),F12            SET COMP CODE 12   CAH83351A
*                                                            CAH83351A
ENDRLSE  L     R4,MYANS                 ADDRESS OF PDL       CAH83351A
         IKJRLSA  (R4)                    RELEASE SPACE
*
         LR    R1,R13                   LOAD MY S.A. ADDR FOR FREEMAIN
         L     R14,COMPCODE             SAVE PROGRAM COMPLETION CODE
         L     R13,4(0,R13)             RESTORE R13 FROM MY S.A.
         FREEMAIN R,LV=WORK0LEN,A=(R1)  FREE STORAGE FOR WORK0
         LR    R15,R14                  SET PROGRAM COMPLETION CODE
         LM    R0,R12,20(R13)           RESTORE R0-R12 FM CALLERS S.A.
         L     R14,12(0,R13)            RESTORE R14 FROM CALLERS S.A.
         MVI   12(R13),X'FF'            RETURN INDCATR TO CALLERS S.A.
         BR    R14                      RETURN TO CALLER
         EJECT
**********************************************************************
***    ERROR PROCEDURE,  --- RETCDE IS NOT CLEARED ---             ***
**********************************************************************
ERROR    ST    RBAL,ERBALSAV            SAVE BAL REGISTER VALUE
*
         L     R15,RETCDE               R15 HAS RETURN CODE
         C     R15,COMPCODE             IF R15 GREATER THAN COMP CODE
         BNH   *+8                        THEN
         ST    R15,COMPCODE               REPLACE CC WITH HIGHER R15
*
EREND    L     RBAL,ERBALSAV            RELOAD BAL REGISTER
         BR    RBAL                     RETURN
         EJECT
**********************************************************************
***    PARSE THE PARMS                                             ***
**********************************************************************
PARSE    ST    RBAL,PABALSAV            SAVE BAL REGISTER VALUE
         XC    RETCDE,RETCDE            CLEAR INTERNAL RETURN CODE
*
         L     R4,R1SAVE                ADDRESSABILITY
         USING CPPL,R4                    OF CPPL
*
         MVC   PPLUPT(4),CPPLUPT        ADDR OF UPT TO MYPPL
         MVC   PPLECT(4),CPPLECT          .     ECT     .
         LA    R1,MYECB                   .             .
         ST    R1,PPLECB                  .     ECB     .
         MVC   PPLPCL(4),VMYPCL           .     PCL     .
         LA    R1,MYANS                   .             .
         ST    R1,PPLANS                  .     ANS     .
         MVC   PPLCBUF(4),CPPLCBUF        .     CBUF    .
         XC    PPLUWA(4),PPLUWA         ZERO TO UWA IN MYPPL
*
         CALLTSSR  EP=IKJPARS,MF=(E,MYPPL)
         LTR   R15,R15                  IF BAD PARSE
         BNZ   PAERR0C                    GO TO ERROR
         L     R9,MYANS                 ELSE ADDR OF IKJPARMD TO R9
*
         B     PAEND                    BRANCH TO ENDING
*
PAERR0C  LA    R15,X'0C'(0,0)           CREATE RETURN CODE
         ST    R15,RETCDE               PUT INTO STORAGE
         B     PAEND                    BRANCH TO END OF ROUTINE
*
PAEND    L     RBAL,PABALSAV            RELOAD BAL REGISTER
         BR    RBAL                     RETURN
*
         DROP  R4                       DROP ADDRESSABILITY OF CPPL
         EJECT
**********************************************************************
***    GET DATASET NAME                                            ***
**********************************************************************
DSN      ST    RBAL,DSBALSAV            SAVE BAL REGISTER VALUE
         XC    RETCDE,RETCDE            CLEAR INTERNAL RETURN CODE
*
         MVC   DSNARG(45),BLANKS        CLEAR DSN PLUS TRAILING SPACE
         LA    R4,PDSN                  R4 POINTER TO PDE FOR DSN
         LH    R5,4(0,R4)               R5 IS LEN OF DSN
         STH   R5,DSNARGLN              SAVE IT
         BCTR  R5,0                     R5-1 FOR EXEC
         L     R4,0(0,R4)               R4 POINTS TO DSN
         B     *+10                     BRANCH AROUND MOVE
         MVC   DSNARG(0),0(R4)            MOVE DSN TO STORAGE
         EX    R5,*-6                   EXECUTE THE MOVE
*
         B     DSEND                    BRANCH TO ENDING
*
DSERR0C  LA    R15,X'0C'(0,0)           CREATE RETURN CODE
         ST    R15,RETCDE               PUT INTO STORAGE
         B     DSEND                    BRANCH TO END OF ROUTINE
*
DSEND    L     RBAL,DSBALSAV            RELOAD BAL REGISTER
         BR    RBAL                     RETURN
         EJECT
**********************************************************************
***    INITIALIZE CIR PARM LIST                                    ***
**********************************************************************
INITCIR  ST    RBAL,INBALSAV            SAVE BAL REGISTER VALUE
         XC    RETCDE,RETCDE            CLEAR INTERNAL RETURN CODE
*
         MVC   CIRPARM(CIRPARML),CIRDATA  CONSTANTS TO PARM LIST
         LA    R1,CIRWORK                 ADDR OF WORK AREA
         ST    R1,CIRWA                     INTO PARM LIST
         ST    R13,CIRSAVE                ADDR OF S.A. INTO PARM LIST
         LA    R1,DSNARG                  ADDR OF DSN
         ST    R1,CIRSRCH                   INTO PARM LIST
*
         MVC   AREALN(2),H255           SET LENGTH OF WORK AREA
*
         B     INEND                    BRANCH TO ENDING
*
INERR00  LA    R15,X'00'(0,0)           CREATE RETURN CODE
         ST    R15,RETCDE               PUT INTO STORAGE
         B     INEND                    BRANCH TO END OF ROUTINE
*
INEND    L     RBAL,INBALSAV            RELOAD BAL REGISTER
         BR    RBAL                     RETURN
         EJECT
**********************************************************************
***    GET INFORMATION FROM CATALOG                                ***
**********************************************************************
CIR      ST    RBAL,CIBALSAV            SAVE BAL REGISTER VALUE
         XC    RETCDE,RETCDE            CLEAR INTERNAL RETURN CODE
*
CIREXEC  CALLTSSR EP=IKJEHCIR,MF=(E,CIRPARM)
         ST    R15,CIRCDE               SAVE RETURN CODE     CAH83351A
         LTR   R15,R15                  IF NOT ZERO
         BNZ   CIERR0C                    GO TO ERROR
*
         B     CIEND                    BRANCH TO ENDING
*
CIERR0C  LA    R15,X'0C'(0,0)           CREATE RETURN CODE
         ST    R15,RETCDE               PUT INTO STORAGE
         B     CIEND                    BRANCH TO END OF ROUTINE
*
CIEND    L     RBAL,CIBALSAV            RELOAD BAL REGISTER
         BR    RBAL                     RETURN
         EJECT
**********************************************************************
***    LIST DSN AND VOLUME, OR ERROR MESSAGE                       ***
**********************************************************************
LIST     ST    RBAL,LIBALSAV            SAVE BAL REGISTER VALUE
         XC    RETCDE,RETCDE            CLEAR INTERNAL RETURN CODE
*
         CLC   KLIST(2),H1              IF 'LIST' OPERAND NOT USED
         BNE   LIEND                      GO TO ENDING
*
         MVC   MSGAREA(120),BLANKS      CLEAR MESSAGE AREA
         LH    R4,DSNARGLN              R4 IS LENGTH OF DSNARG
         B     *+10                     BRANCH AROUND MOVE
         MVC   MSGAREA(0),DSNARG          MOVE DSN PLUS TRAILING BLANK
         EX    R4,*-6                   EXECUTE THE MOVE
         LA    R4,MSGAREA+1(R4)         R4 POINTS TO NEXT PLACE IN MSG
         ST    R4,NXTMSGCH                SAVE IT
         CLC   CIRCDE(4),F0             IF CIR R/C NOT ZERO   CAH84310A
         BNE   LINOTFND                   GO TO 'NOT FOUND'   CAH84310A
*                                                             CAH84310A
         CLC   KOBTAIN,H1               IF 'OBTAIN' NOT USED  CAH84310A
         BNE   LIFOUND                    GO TO 'FOUND'       CAH84310A
*                                                             CAH84310A
         BAL   RBAL,OBTAIN              CHECK VTOC INFO       CAH84310A
         CLC   RETCDE,F0                IF VTOC OBTAIN GOOD   CAH84310A
         BE    LIFOUND                    GO TO 'FOUND'       CAH84310A
         B     LINVOL                   ELSE 'NOT ON VOLUME'  CAH84310A
*                                                             CAH84310A
LIFOUND  BAL   RBAL,CATLGMSG            BUILD 'FOUND' MSG     CAH84310A
         B     LIMSG                    GO TO WRITE MSG       CAH84310A
*                                                             CAH84310A
LINOTFND BAL   RBAL,NOTFOUND            BUILD 'NOT FOUND' MSG CAH84310A
         B     LIMSG                    GO TO WRITE MSG       CAH84310A
*                                                             CAH84310A
LINVOL   BAL   RBAL,NVOLMSG             BUILD 'NOT ON VOL' MS CAH84310A
         B     LIMSG                    GO TO WRITE MSG       CAH84310A
*                                                             CAH84310A
LIMSG    LA    R4,MSGAREA               POINT TO MSG AREA     CAH84310A
         TPUT  (R4),120                 WRITE THE MESSAGE
*
         B     LIEND                    BRANCH TO ENDING
*
LIERR0C  LA    R15,X'0C'(0,0)           CREATE RETURN CODE
         ST    R15,RETCDE               PUT INTO STORAGE
         B     LIEND                    BRANCH TO END OF ROUTINE
*
LIEND    L     RBAL,LIBALSAV            RELOAD BAL REGISTER
         BR    RBAL                     RETURN
         EJECT
************************************************************* CAH84310A
***    OBTAIN INFORMATION FROM VTOC                       *** CAH84310A
************************************************************* CAH84310A
OBTAIN   ST    RBAL,OBBALSAV            SAVE BAL REG VALUE    CAH84310A
         XC    RETCDE,RETCDE            CLEAR INTERNAL R/C    CAH84310A
*                                                             CAH84310A
         XC    OBWORK,OBWORK            CLEAR WORK AREA       CAH84310A
         MVC   OBDSN(44),BLANKS         CLEAR DSN-44 AREA     CAH84310A
         LH    R4,DSNARGLN              R4 LEN OF DSN         CAH84310A
         BCTR  R4,0                       -1 FOR EXEC         CAH84310A
         L     R5,PDSN                  R5 POINTS TO DSN      CAH84310A
         B     *+10                     BRANCH AROUND MOVE    CAH84310A
         MVC   OBDSN(0),0(R5)             MAKE 44-BYTE DSN    CAH84310A
         EX    R4,*-6                   EXECUTE THE MOVE      CAH84310A
         MVC   OBLIST(OBLISTLN),@OBLIST INITIALIZE CAMLIST    CAH84310A
         LA    R4,OBDSN                 ADDR OF DSN-44        CAH84310A
         ST    R4,OBLIST+4                INTO CAMLIST        CAH84310A
         LA    R4,VOLSER                ADDR OF VOLSER        CAH84310A
         ST    R4,OBLIST+8                INTO CAMLIST        CAH84310A
         LA    R4,OBWORK                ADDR OF WORK AREA     CAH84310A
         ST    R4,OBLIST+12               INTO CAMLIST        CAH84310A
*                                                             CAH84310A
OBVTOC   OBTAIN OBLIST                  OBTAIN VTOC INFO      CAH84310A
         ST    R15,OBCDE                SAVE RETURN CODE      CAH84310A
         LTR   R15,R15                  IF R/C NOT ZERO       CAH84310A
         BNZ   OBERR0C                    GO TO ERROR         CAH84310A
         B     OBEND                    ELSE GO TO END        CAH84310A
*                                                             CAH84310A
OBERR0C  LA    R15,X'0C'(0,0)           CREATE RETURN CODE    CAH84310A
         ST    R15,RETCDE               PUT INTO STORAGE      CAH84310A
         B     OBEND                    GO TO ENDING          CAH84310A
*                                                             CAH84310A
OBEND    L     RBAL,OBBALSAV            RELOAD BAL REGISTER   CAH84310A
         BR    RBAL                     RETURN                CAH84310A
         EJECT
**********************************************************************
***    CONSTRUCT 'CATALOGED' MESSAGE                               ***
**********************************************************************
CATLGMSG ST    RBAL,CABALSAV            SAVE BAL REGISTER VALUE
         XC    RETCDE,RETCDE            CLEAR INTERNAL RETURN CODE
*
         L     R4,NXTMSGCH              R4 POINTS TO NEXT MSG CHAR
*                                                             CAH84310A
CACAT    CLC   PDSN+12(4),H0            IF MEMBER NAME USED   CAH84310A
         BE    CACAT2                        AND              CAH84310A
         CLI   DAWRB+(S99ERROR-S99RB),X'67'  NO 67XX ERROR    CAH84310A
         BNE   CAONVOL                    SKIP 'CATALOGED'    CAH84310A
CACAT2   CLC   KOBTAIN,H1               IF 'OBTAIN' USED      CAH84310A
         BE    CAONVOL                    SKIP 'CATALOGED'    CAH84310A
         MVC   0(10,R4),CATALOGD        ELSE 'CATALOGED' MSG  CAH84310A
         LA    R4,10(0,R4)                INCR POINTER        CAH84310A
*                                                             CAH84310A
CAONVOL  MVC   0(10,R4),CATMSG          'ON VOLUME ' TO MSG   CAH84310A
         LA    R4,10(0,R4)              INCR POINTER          CAH84310A
         MVC   0(6,R4),VOLSER           VOLSER TO MSG LINE
         LA    R4,6(0,R4)               INCR POINTER         CAH83351A
*                                                            CAH83351A
         TM    PDSN+14,X'80'            IF NO MEMBER NAME    CAH83351A
         BNO   CAEND                      GO TO END          CAH83351A
*                                                            CAH83351A
         MVC   0(2,R4),COMMA            ', ' TO MSG          CAH83351A
         LA    R4,2(0,R4)               INCR POINTER         CAH83351A
         LH    R5,PDSN+12               R5 IS LEN OF MEMNAME CAH83351A
         BCTR  R5,0                     R5 - 1 FOR EXEC      CAH83351A
         L     R6,PDSN+8                R6 POINTS TO MEMNAME CAH83351A
         B     *+10                     BRANCH AROUND MOVE   CAH83351A
         MVC   0(0,R4),0(R6)              MEMNAME TO LINE    CAH83351A
         EX    R5,*-6                   EXECUTE THE MOVE     CAH83351A
         LA    R4,1(R5,R4)              INCR POINTER         CAH83351A
*                                                            CAH83351A
         TM    SWITCH1,X'80'            IF MEMBR WAS ABORTED CAH83351A
         BNO   *+14                       THEN               CAH83351A
         MVC   0(12,R4),NOCHECK           ' NOT CHECKED' MSG CAH83351A
         B     CAEND                    ELSE                 CAH83351A
         CLC   BLDLCDE(4),F0            IF BLDL WAS BAD      CAH83351A
         BE    *+14                       THEN               CAH83351A
         MVC   0(10,R4),NOFIND            ' NOT FOUND' MSG   CAH83351A
         B     CAEND                    ELSE                 CAH83351A
         MVC   0(12,R4),ISMEMBER          ' IS A MEMBER' MSG CAH83351A
*
CAEND    L     RBAL,CABALSAV            RELOAD BAL REGISTER
         BR    RBAL                     RETURN
         EJECT                                                CAH84310A
**********************************************************************
***    CONSTRUCT 'NOT FOUND' MESSAGE                               ***
**********************************************************************
NOTFOUND ST    RBAL,NOBALSAV            SAVE BAL REGISTER VALUE
         XC    RETCDE,RETCDE            CLEAR INTERNAL RETURN CODE
*
         L     R4,NXTMSGCH              R4 POINTS TO NEXT MSG CHAR
         MVC   0(20,R4),NOTMSG          'NOT FOUND IN CATALOG'
*
NOEND    L     RBAL,NOBALSAV            RELOAD BAL REGISTER
         BR    RBAL                     RETURN
         SPACE 3                                              CAH84310A
************************************************************* CAH84310A
***    CONSTRUCT 'NOT ON VOLUME' MESSAGE                  *** CAH84310A
************************************************************* CAH84310A
NVOLMSG  ST    RBAL,NVBALSAV            SAVE BAL REGISTER     CAH84310A
         XC    RETCDE,RETCDE            CLEAR INTERNAL R/C    CAH84310A
*                                                             CAH84310A
         L     R4,NXTMSGCH              R4 -> NEXT MSG CHAR   CAH84310A
         MVC   0(14,R4),NVOLTEXT        'NOT ON VOLUME '      CAH84310A
         LA    R4,14(0,R4)              INCREMENT POINTER     CAH84310A
         MVC   0(6,R4),VOLSER           FILL IN VOLSER        CAH84310A
*                                                             CAH84310A
NVEND    L     RBAL,NVBALSAV            RELOAD BAL REGISTER   CAH84310A
         BR    RBAL                     RETURN                CAH84310A
         EJECT                                               CAH83351A
************************************************************ CAH83351A
***    GET MEMBER NAME INFORMATION                       *** CAH83351A
************************************************************ CAH83351A
MEMBR    ST    RBAL,MEBALSAV            SAVE BAL REG VALUE   CAH83351A
         XC    RETCDE,RETCDE            0 INTERNAL RET CODE  CAH83351A
*                                                            CAH83351A
MEBYPASS TM    PDSN+14,X'80'            IF NO MEMBER NAME    CAH83351A
         BNO   MEEND                      BR TO ENDING       CAH83351A
*                                                            CAH83351A
MENONZRO MVC   BLDLCDE(4),F1            SET NON 0 BLDL R/C   CAH83351A
*                                                            CAH83351A
         MVC   DAW(DAWLEN),DAD          INITIALIZE CNTL BLKS CAH83351A
MESET    GTEDASET DAW,CPPLPTR=R1SAVE    LINK CNTL BLOCKS     CAH83351A
         L     R4,PDSN+00               R4 POINTS TO DSN     CAH83351A
         LH    R5,PDSN+04               R5 IS DSN LENGTH     CAH83351A
         BCTR  R5,0                     R5 - 1 FOR EXEC      CAH83351A
         B     *+10                     BRANCH AROUND MOVE   CAH83351A
         MVC   DAWDSNAM(0),0(R4)          MOVE DSN TO T.U.   CAH83351A
         EX    R5,*-6                   EXECUTE MOVE         CAH83351A
MEALLOC  GTEDAALC DAW,VERB=AL,ERRMSG=NO   DO DYNAMIC ALC     CAH83351A
         CLC   DAWS99RC(4),F0           IF BAD RETURN CODE   CAH83351A
         BNE   MEABORT                    GO TO ABORT        CAH83351A
         TM    DAWRTORG,X'02'           IF DSORG NE PO/POU   CAH83351A
         BNO   MEABORT                    GO TO ABORT        CAH83351A
*                                                            CAH83351A
         MVC   INDCB(INDCBLN),DCBMODEL  INITIALIZE DCB       CAH83351A
         LA    R4,INDCB                 R5 POINTS TO IN DCB  CAH83351A
         USING IHADCB,R4                ADDRESSABILITY       CAH83351A
         MVC   DCBDDNAM(8),DAWRTDDN     FILL IN DDNAME       CAH83351A
*                                                            CAH83351A
MEOPEN   MVC   OPENLIST(4),OPENLI       OPTIONS TO PARM LIST CAH83351A
         LA    R5,INDCB                 ADDRESS OF INPUT DCB CAH83351A
         STCM  R5,7,OPENLIST+1            INTO PARM LIST     CAH83351A
MEOPEN2  OPEN  MF=(E,OPENLIST)        , OPEN THE DATSET      CAH83351A
         TM    DCBOFLGS,X'10'           IF NOT SUCCESSFUL    CAH83351A
         BZ    MEABORT                    GO TO ABORT        CAH83351A
*                                                            CAH83351A
         DROP  R4                       DROP ADDRESSABILITY  CAH83351A
*                                                            CAH83351A
MEBLDL   MVC   BLDLLIST(16),BLDLDAT     INITIALIZE BLDL LIST CAH83351A
         L     R4,PDSN+08               R4 POINT TO MEM NAME CAH83351A
         LH    R5,PDSN+12               R5 IS MEM NAME LEN   CAH83351A
         BCTR  R5,0                     R5 - 1 FOR EXEC      CAH83351A
         B     *+10                     BRANCH AROUND MOVE   CAH83351A
         MVC   BLDLLIST+4(0),0(R4)        NAME TO BLDL LIST  CAH83351A
         EX    R5,*-6                   EXECUTE MOVE         CAH83351A
MEBLDL2  BLDL  INDCB,BLDLLIST           DO BLDL              CAH83351A
         ST    R15,BLDLCDE              SAVE RETURN CODE     CAH83351A
         LTR   R15,R15                  IF BAD RETURN        CAH83351A
         BZ    *+8                        THEN SET           CAH83351A
         ST    R15,BLDLCDE                  INTO LOCAL R/C   CAH83351A
*                                                            CAH83351A
MECLOSE  LA    R4,INDCB                 ADDRESS OF INPUT DCB CAH83351A
         MVC   CLOSLIST(4),CLOSPRTI     INITIALIZE PARM LIST CAH83351A
MECLOSE2 CLOSE ((R4)),MF=(E,CLOSLIST)   CLOSE THE DATASETS   CAH83351A
         LTR   R15,R15                  IF RET CODE NOT ZERO CAH83351A
         BNZ   MEERR0C                    GO TO ERROR        CAH83351A
*                                                            CAH83351A
MEUNALC  MVC   DUW(DUWLEN),DUD          INITIALIZE CNTL BLKS CAH83351A
         GTEDASET DUW,CPPLPTR=R1SAVE    LINK CNTL BLOCKS     CAH83351A
         MVC   DUWDDNAM(8),DAWRTDDN     SET DDNAME           CAH83351A
MEUNALC2 GTEDAALC DUW,VERB=UN,ERRMSG=YES  DO DYNAMIC FREE    CAH83351A
         CLC   DUWS99RC(4),F0           IF BAD RETURN CODE   CAH83351A
         BNE   MEERR0C                    GO TO ERROR        CAH83351A
*                                                            CAH83351A
MELOCBAD CLC   BLDLCDE,F0               IF BLDL WAS BAD      CAH83351A
         BNE   MEERR0C                    SET BAD RETCDE     CAH83351A
         B     MEEND                    ELSE GO TO END       CAH83351A
*                                                            CAH83351A
MEABORT  OI    SWITCH1,X'80'            TURN ON ABORT SW     CAH83351A
         B     MEERR0C                  GO TO ERROR          CAH83351A
*                                                            CAH83351A
MEERR0C  LA    R15,X'0C'(0,0)           CREATE RETURN CODE   CAH83351A
         ST    R15,RETCDE               PUT INTO STORAGE     CAH83351A
         B     MEEND                    BRANCH TO END        CAH83351A
*                                                            CAH83351A
MEEND    L     RBAL,MEBALSAV            RELOAD BAL REGISTER  CAH83351A
         BR    RBAL                     RETURN               CAH83351A
         EJECT
**********************************************************************
***    DATA CONSTANTS                                              ***
**********************************************************************
CONST    DS    0D
D0       DS    0D                       DOUBLEWORD OF ZEROES
F0       DS    0F                       FULLWORD OF ZEROES
H0       DS    0H                       HALFWORD OF ZEROES
         DC    D'0'
F1       DC    F'1'                     FULLWORD 1           CAH83351A
F4       DC    F'4'                     FULLWORD 4
F8       DC    F'8'                     FULLWORD 8
F12      DC    F'12'                    FULLWORD 12
F16      DC    F'16'                    FULLWORD 16
XFFS     DC    XL4'FFFFFFFF'            FULLWORD X'FF'
BLANKS   DC    CL120' '                 120 BLANKS
H255     DC    H'255'                   CONSTANT
H1       DC    H'1'                     KEYWORD CHOICE 1
CATALOGD DC    C'CATALOGED '            LITERAL               CAH84310A
CATMSG   DC    C'ON VOLUME '            LITERAL              CAH83351A
COMMA    DC    C', '                    LITERAL              CAH83351A
ISMEMBER DC    C' IS A MEMBER'          LITERAL              CAH83351A
NOFIND   DC    C' NOT FOUND'            LITERAL              CAH83351A
NOCHECK  DC    C' NOT CHECKED'          LITERAL              CAH83351A
NOTMSG   DC    C'NOT FOUND IN CATALOG'  LITERAL
NVOLTEXT DC    C'NOT ON VOLUME '        LITERAL               CAH84310A
*
CIRDATA  DS    0D                       CONSTANTS FOR CIR PARM
         DC    X'04'                    OPTIONS = GET VOLUME
         DS    X'0000'                  RESERVED
         DC    X'00'                    LOCATE RETURN CODE
         DS    A                        ADDRESS OF SEARCH ARGUMENT
         DC    A(0)                     ADDR OF VOLID OF CVOL
         DS    A                        ADDR OF WORK AREA
         DS    A                        ADDR OF SAVE AREA
         DC    A(0)                     ADDR OF PASSWORD
*
DAD      GTEDADAT  DSNAM=X,STATS=SHR,RTDDN=YES,RTORG=YES     CAH83351A
DUD      GTEDADAT  DDNAM=X                                   CAH83351A
*                                                            CAH83351A
DCBMODEL DCB   DSORG=PO,MACRF=R                              CAH83351A
OPENLI   OPEN  (,(INPUT)),MF=L                               CAH83351A
CLOSPRTI CLOSE (,),MF=L                                      CAH83351A
*                                                            CAH83351A
BLDLDAT  DS    0H                                            CAH83351A
BLDLFF   DC    H'1'                     1 ENTRY              CAH83351A
BLDLLL   DC    H'12'                    SHORT FORMAT         CAH83351A
BLDLNAME DC    CL8' '                   NAME                 CAH83351A
BLDLTTRK DC    XL4'00'                  TT, R, K             CAH83351A
*                                                            CAH83351A
VMYPCL   DC    V(MYPCL)                 ADDRESS OF PCE LIST
*
@OBLIST  CAMLST SEARCH,0,0,0            CAMLIST FOR OBTAIN    CAH84310A
*                                                             CAH84310A
         DS    0D                       ALIGN
CONSTLEN EQU   *-CONST                  LENGTH OF CONSTANT DATA AREA
         EJECT
**********************************************************************
***    PARMS                                                       ***
**********************************************************************
MYPCL    IKJPARM  DSECT=IKJPARMD
*
PDSN     IKJPOSIT DSNAME,USID,PROMPT='NAME OF DATASET TO BE LOCATED'
*
KLIST    IKJKEYWD DEFAULT='LIST'                             CAH83351A
         IKJNAME  'LIST'
         IKJNAME  'NOLIST'
*                                                             CAH84310A
KOBTAIN  IKJKEYWD DEFAULT='OBTAIN'                            CAH84310A
         IKJNAME  'OBTAIN',ALIAS=('VOLCHECK')                 CAH84310A
         IKJNAME  'NOOBTAIN',ALIAS=('NOVOLCHECK')             CAH84310A
*
         IKJENDP
         EJECT
**********************************************************************
***    DATA AREA (BASE ADDR IS R13)                                ***
**********************************************************************
WORK0    DSECT
MYSAVE   DS    18F                      REGISTER SAVE AREA
COMPCODE DS    F                        PROGRAM COMPLETION CODE
RETCDE   DS    F                        INTERNAL RETURN CODE
R1SAVE   DS    F                        INITIAL VALUE OF R1
PABALSAV DS    F                        BAL REGISTER SAVE AREA
DSBALSAV DS    F                        BAL REGISTER SAVE AREA
INBALSAV DS    F                        BAL REGISTER SAVE AREA
CIBALSAV DS    F                        BAL REGISTER SAVE AREA
NOBALSAV DS    F                        BAL REGISTER SAVE AREA
NVBALSAV DS    F                        BAL REG SAVE AREA     CAH84310A
CABALSAV DS    F                        BAL REGISTER SAVE AREA
LIBALSAV DS    F                        BAL REGISTER SAVE AREA
OBBALSAV DS    F                        BAL REG SAVE AREA     CAH84310A
ERBALSAV DS    F                        BAL REGISTER SAVE AREA
MEBALSAV DS    F                        BAL REG SAVE AREA    CAH83351A
*
BLDLCDE  DS    F                        BLDL RETURN CODE     CAH83351A
CIRCDE   DS    F                        IKJEFCIR RET CODE    CAH83351A
SWITCH1  DC    B'00000000'              SWITCHES             CAH83351A
*                1.......                 NO CHECK MEM NAME  CAH83351A
*                .1111111                 UNUSED             CAH83351A
*                                                            CAH83351A
CIRPARM  DS    0D
CIROPT   DC    X'04'                    OPTIONS = GET VOLUME
         DS    X'0000'                  RESERVED
CIRLOCRC DC    X'00'                    LOCATE RETURN CODE
CIRSRCH  DS    A                        ADDRESS OF SEARCH ARGUMENT
CIRCVOL  DC    A(0)                     ADDR OF VOLID OF CVOL
CIRWA    DS    A                        ADDR OF WORK AREA
CIRSAVE  DS    A                        ADDR OF SAVE AREA
CIRPSWD  DC    A(0)                     ADDR OF PASSWORD
CIRPARML EQU   *-CIRPARM                LENGTH OF PARM BLOCK
*
DAW      GTEDADAT  MAP=ONLY,                                 CAH83351A X
               DSNAM=X,STATS=SHR,RTDDN=YES,RTORG=YES         CAH83351A
*
DUW      GTEDADAT  MAP=ONLY,DDNAM=X                          CAH83351A
*                                                            CAH83351A
INDCB    DCB   DSORG=PO,MACRF=R                              CAH83351A
         DS    0D                                            CAH83351A
INDCBLN  EQU   *-INDCB                                       CAH83351A
OPENLIST DS    2F                       OPEN PARM LIST       CAH83351A
CLOSLIST DS    2F                       CLOSE PARM LIST      CAH83351A
*                                                            CAH83351A
CIRWORK  DS    0H                       CIR WORK AREA
AREALN   DS    H                        LENGTH OF AREA
DATALN   DS    H                        LENGTH OF RETURNED DATA
VOLDATA  DS    0CL251                   RETURNED DATA
         DS    CL1                      RESERVED
DEVTYPE  DS    XL4                      DEVICE TYPE
VOLSER   DS    CL6                      VOLUME SERIAL NUMBER
FILESEQ  DS    XL2                      FILE SEQUENCE NUMBER (TAPE)
         DS    CL2                      RESERVED
         DS    CL236
*
DSNARGLN DS    H                        LENGTH OF DSN
DSNARG   DS    CL45                     DSN PLUS TRAILING SPACE
*
NXTMSGCH DS    A                        ADDR OF NEXT MESSAGE CHARACTER
MSGAREA  DS    CL120                    OUTPUT MESSAGE LINE
*
MYPPL    DS    7A                       MY PARSE PARM LIST
MYECB    DS    F                        EVENT CONTROL BLOCK
MYANS    DS    F                        PTR TO PDL
*                                                            CAH83351A
BLDLLIST DS    4F                       BLDL ENTRY LIST      CAH83351A
*                                                            CAH83351A
OBCDE    DS    F                        OBTAIN RETURN CDE     CAH84310A
OBWORK   DS    XL140                    WORK AREA FOR VTOC    CAH84310A
OBDSN    DS    CL44                     WORK AREA FOR DSN     CAH84310A
OBLIST   CAMLST SEARCH,0,0,0            CAMLIST FOR OBTAIN    CAH84310A
OBLISTLN EQU   *-OBLIST                 LENGTH OF CAMLIST     CAH84310A
*                                                             CAH84310A
         DS    0D                       ALIGN FOR GETMAIN
WORK0LEN EQU   *-WORK0                  LENGTH OF WORK0 DATA AREA
         EJECT
**********************************************************************
***    MAPPING MACROS                                              ***
**********************************************************************
         PRINT NOGEN
         CVT   DSECT=YES                COMM VECTOR TABLE
         IKJPPL                         PARSE PARAMETER LIST
         IKJCPPL                        COMMAND PROCESSOR PARM LIST
         IEFZB4D0                       MAPS SVC-99 BLOCKS   CAH83351A
         IKJEFFDF DFDSECT=YES,DFDSEC2=YES  MAPS IKJEFF18 BLK CAH83351A
         IHADCB DSORG=PS,DEVD=DA      , MAPS THE DCB FOR DA  CAH83351A
         END
