         MACRO
&LABEL   GTEDAALC  &NM,&VERB=AL,&ERRMSG=YES
.*
.*          +-----------------------------------------------+
.*          |  See macro GTEDADOC for documentation of the  |
.*          |  use of this and other GTEDAxxx macros.       |
.*          +-----------------------------------------------+
.*
*
**********************************************************************
***   PERFORM DYNAMIC ALLOCATION USING A DATA AREA CREATED         ***
***   BY MACRO GTEDADAT, AND POINTERS/DATA SET BY MACRO GTEDASET.  ***
**********************************************************************
*
**********************************************************************
***   SET VERB CODE INTO S99RB.                                    ***
***   MAPPING MACRO IEFZB4D0 MUST APPEAR IN THE SOURCE.            ***
**********************************************************************
*
&LABEL   LA    1,&NM.RB
.*
.VBAL    AIF   ('&VERB' NE 'AL').VBUN
         MVI   (S99VERB-S99RB)(1),S99VRBAL   SET VERB, DSN ALLOC
         AGO   .VBEND
.VBUN    AIF   ('&VERB' NE 'UN').VBCC
         MVI   (S99VERB-S99RB)(1),S99VRBUN   SET VERB, DSN UNALLOC
         AGO   .VBEND
.VBCC    AIF   ('&VERB' NE 'CC').VBDC
         MVI   (S99VERB-S99RB)(1),S99VRBCC   SET VERB, CONCATENATE
         AGO   .VBEND
.VBDC    AIF   ('&VERB' NE 'DC').VBRI
         MVI   (S99VERB-S99RB)(1),S99VRBDC   SET VERB, DECONCATENATE
         AGO   .VBEND
.VBRI    AIF   ('&VERB' NE 'RI').VBDN
         MVI   (S99VERB-S99RB)(1),S99VRBRI   SET VERB, REMOVE IN-USE
         AGO   .VBEND
.VBDN    AIF   ('&VERB' NE 'DN').VBIN
         MVI   (S99VERB-S99RB)(1),S99VRBDN   SET VERB, DDN ALLOC
         AGO   .VBEND
.VBIN    AIF   ('&VERB' NE 'IN').VBEND
         MVI   (S99VERB-S99RB)(1),S99VRBIN   SET VERB, REQUEST INFO
         AGO   .VBEND
.VBEND   ANOP
.*
*
**********************************************************************
***    DO DYNAMIC ALLOCATION                                       ***
**********************************************************************
*
         LA    1,&NM.RBP                ADDRESS OF PTR TO SVC-99 RB
         SVC   99                       DYNALLOC
         ST    15,&NM.S99RC             SAVE RETURN CODE FROM SVC-99
.*
.*********************************************************************
.**                                                                ***
.**   IF &ERRMSG=NO, DO NOT EVER GENERATE CODE FOR IKJEFF18        ***
.**                                                                ***
.*********************************************************************
.*
         AIF   ('&ERRMSG' EQ 'NO').NOPR
.*
*
**********************************************************************
***    IF R/C FROM DYNAMIC ALLOCATION NOT ZERO,                    ***
***    EXECUTE THE IKJEFF18 (DAIRFAIL) PROGRAM TO PRODUCE          ***
***    ERROR MESSAGES.                                             ***
**********************************************************************
*
         CNOP  2,4                      ALIGN FOR BRANCH INSTRUCTION
         LTR   15,15                    IF RETURN CODE IS GOOD
         BZ    EXZ&SYSNDX                 BRANCH TO END OF ROUTINE
         BAL   1,EXA&SYSNDX  BRANCH AROUND LIST
*
**********************************************************************
***    EXCEPTIONS, DON'T USE IKJEFF18 FOR THESE VERB/ERROR COMBOS  ***
***                                                                ***
         DC    H'1'          NUMBER OF MEMBERS IN THIS LIST
         DC    X'02,0438'    VERB 02 ERR 0438 DDNAME NOT FOUND
***                                                                ***
**********************************************************************
*
EXA&SYSNDX LA    2,2(0,1)                    R2 IS POINTER TO COMBOS
           LH    1,0(0,1)                    R1 IS COUNT OF COMBOS
           LA    15,&NM.RB                   R15 IS BASE OF RB
*
           CLC   (S99VERB-S99RB)(1,15),0(2)  IF NO MATCH ON VERB
           BNE   *+14                          BRANCH TO END OF LOOP
           CLC   (S99ERROR-S99RB)(2,15),1(2) ELSE IF ERRCDE MATCHES
           BE    EXZ&SYSNDX                    BRANCH TO ENDING
           LA    2,6(0,2)                    POINT TO NEXT ENTRY
           BCT   1,*-24                      LOOP BACK UP TIL EOL
*
         LA    1,&NM.DFPRM              POINT TO IKJEFF18 PARM BLOCK
         CNOP  0,4                      ALIGN FOR FOLLOWING BAL
         BAL   15,*+20                  LOAD CONST ADDR AND BRANCH
         DC    A(*+8)                   ADDR OF LINK PARM LIST
         DC    A(0)                     ADDR OF DCB (NONE)
         DC    CL8'IKJEFF18'            EP NAME
         SVC   6                        LINK
*
.NOPR    ANOP
EXZ&SYSNDX NOPR  0                        END POINT
         MEND
