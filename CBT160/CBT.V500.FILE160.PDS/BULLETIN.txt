         TITLE 'BULLETIN - COMMAND PROCESSOR FOR BULLETINS'
         PRINT NOGEN
**********************************************************************
***                                                                ***
***  Syntax -                                                      ***
***                                                                ***
***  BULLETIN  CLEAR(NN)  BROADCAST(dsname)                        ***
***            K(NN)      BC(DSNAME)                               ***
***            NOCLEAR    NOBROADCAST                              ***
***            NOK        NOBC                                     ***
***                                                                ***
**********************************************************************
         SPACE 3
**********************************************************************
***                                                                ***
***  The BULLETIN command is an interface between TSO and SVC-34,  ***
***  the console operator SVC.                                     ***
***                                                                ***
***  BULLETIN reads text from a dataset, then builds and executes  ***
***  operator  SEND 'text',SAVE  commands.  When reading entries   ***
***  from the input dataset, BULLETIN will process start/stop      ***
***  dates for individual items.                                   ***
***                                                                ***
***  The CLEAR and BROADCAST functions of BULLETIN require the     ***
***  TSO OPERATOR privilege, or require the JOB/STC name to be     ***
***  'BULLETIN'.                                                   ***
***                                                                ***
**********************************************************************
         SPACE 3
**********************************************************************
***                                                                ***
***  Set the following global variables at the beginning of the    ***
***  program:                                                      ***
***                                                                ***
***    &STC     - The name of a batch JOB or STC which will be     ***
***               allowed to execute the CLEAR and BROADCAST       ***
***               functions.  This would be a JOB or STC to run    ***
***               the TMP (IKJEFT01) to execute the BULLETIN       ***
***               command nightly from the system bulletin         ***
***               dataset.                                         ***
***                                                                ***
***    &SYSBC   - The name of the system bulletin dataset, which   ***
***               will be the default used with the BROADCAST      ***
***               operand.                                         ***
***                                                                ***
***    &WAIT    - The number of hundredths of seconds to wait      ***
***               between succeeding SEND commands.                ***
***                                                                ***
**********************************************************************
         EJECT
**********************************************************************
***                                                                ***
***  Installation considerations:                                  ***
***                                                                ***
***  This program uses the MODESET macro to switch into supervisor ***
***  state and to run under storage protect key zero.  This is     ***
***  required by SVC-34.  It uses MODESET again to switch back     ***
***  to problem state, key nonzero (8).  Authorize the program to  ***
***  use the MODESET macro in one of the following ways:           ***
***                                                                ***
***  1.  If you do not use an installation-provided SVC for        ***
***      dynamically switching on the JSCBAUTH bit:                ***
***                                                                ***
***      A.  Link the program with AC=1 into SYS1.LINKLIB, or      ***
***          into a library on the linklist (LNKLSTxx).  Under     ***
***          MVS/XA, the library must also be on the APF list      ***
***          (IEAAPFxx).  Under MVS/370, the library must also     ***
***          be on the APF list only if you are using the          ***
***          library as a STEPLIB, tasklib, etc.                   ***
***                                                                ***
***      B.  TSO needs to know about authorized programs which     ***
***          run as commands.  Add the name 'BULLETIN' to the      ***
***          APF command list in module IKJEFTE2.  SMP will link   ***
***          IKJEFTE2 into IKJEFT02.                               ***
***                                                                ***
***  2.  If you do use an installation-provided SVC for dynamic-   ***
***      ally switching on the JSCBAUTH bit:                       ***
***                                                                ***
***      A.  Replace the NOPR instructions at labels SVC1, SVC2,   ***
***          SVC3 and SVC4 with the installation SVC instruction.  ***
***                                                                ***
***      B.  Link the program with AC=0 into SYS1.LPALIB,          ***
***          SYS1.LINKLIB or any library named on the linklist     ***
***          or named in a LOGON STEPLIB statement.                ***
***                                                                ***
***                      (continued)                               ***
***                                                                ***
**********************************************************************
         EJECT
**********************************************************************
***                                                                ***
***  Installation considerations (continued):                      ***
***                                                                ***
***  This program uses the STIMER macro because of the asynchron-  ***
***  ous nature of SVC-34.  Programs using STIMER are not          ***
***  supported under ISPF (e.g. from Option 6 or from the command  ***
***  line).  Module ISPTCM is a list of command names, and         ***
***  attribute bytes defining whether the commands are or are not  ***
***  allowed to run under ISPF.  If your default entry at the      ***
***  bottom of ISPTCM (X'FFFFFFFFFFFFFFFF') is followed            ***
***  immediately by an attribute byte with the X'02' bit on, you   ***
***  must add the following entry into ISPTCM.  The entries do not ***
***  have to be in alphabetic order:                               ***
***                                                                ***
***          CL8'BULLETIN',B'00001000'  NOT SUPPORTED UNDER ISPF   ***
***                                                                ***
***                                                                ***
***  The following ZAP would do the same thing.  ZAP into one of   ***
***  the blank entries provided, or ZAP over the name of a command ***
***  you do not use.  Use the DUMPT function of AMASPZAP to locate ***
***  the blank entries and compute their offset (xxxx).            ***
***                                                                ***
***          NAME ISPTCM ISPTCM                                    ***
***          VER xxxx 40404040,40404040,02                         ***
***          REP xxxx C2E4D3D3,C5E3C9D5,08                         ***
***                                                                ***
**********************************************************************
         EJECT
**********************************************************************
***                                                                ***
***  ASM options:   RENT                                           ***
***                                                                ***
***  LKED options:  RENT, REUS, REFR, AC=(1 or 0, see note above)  ***
***                                                                ***
***  Return codes:  04 - A. Error in parsing operands.             ***
***                      B. Error in dynamic allocation.           ***
***                      C. ABEND was trapped by the command.      ***
***                 00 - Anything else.                            ***
***                                                                ***
***  Register Usage:                                               ***
***      R0-R1 - Linkage conventions, macros, work registers.      ***
***      R2-R3 - Macros, work registers.                           ***
***      R4-R6 - Work registers.                                   ***
***         R7 - (unused)                                          ***
***         R8 - (unused)                                          ***
***         R9 - Base of PARMPDL, returned by IKJPARS.             ***
***        R10 - BAL register.                                     ***
***        R11 - Second base register (BULLET01+4096)              ***
***        R12 - First base register (BULLET01)                    ***
***        R13 - Linkage conventions, base of WORKD work area.     ***
***    R14-R15 - Linkage conventions, macros, work registers.      ***
***                                                                ***
***                                                                ***
***  Non-IBM macros used:                                          ***
***    GTEDADAT - Creates dynamic allocation control blocks.       ***
***    GTEDASET - Links together control blocks created by         ***
***               GTEDADAT.                                        ***
***    GTEDAALC - Executes dynamic allocation using control        ***
***               blocks created by GTEDADAT and linked together   ***
***               by GTEDASET.                                     ***
***    GTEDADOC - Not actually used.  This macro is documentation  ***
***               for the other GTEDAxxx macros.                   ***
***                                                                ***
**********************************************************************
         EJECT
**********************************************************************
***                                                                ***
***   PROLOG                                                       ***
***                                                                ***
***     1.  Linkage conventions and addressability.                ***
***     2.  Clear the completion code                              ***
***     3.  Save the pointer to the CPPL.                          ***
***                                                                ***
**********************************************************************
         LCLC  &STC,&SYSBC,&WAIT        DEFINE LOCAL CONSTANTS
&STC     SETC  'BULLETIN'               STC AUTH TO USE CLEAR/BROADCAST
&SYSBC   SETC  'SYS1.BULLETIN.DATA'     SYSTEM BULLETIN DSN (DSORG=PS)
&WAIT    SETC  '020'                    NO OF 100THS OF SECONDS WAIT
*
BULLET01 CSECT
         STM   R14,R12,12(R13)          SAVE INTO CALLERS S.A.
         B     BASE-BULLET01(0,R15)     BRANCH TO AROUND EYECATCHER
         DC    AL1(L'NAME)                LENGTH OF NAME
NAME     DC    C'BULLET01'                MODULE NAME
         DC    C' &SYSDATE &SYSTIME '     DD.MM.YY HH.MM
BASE     LR    RBASE,R15                RBASE IS BASE REGISTER
         USING BULLET01,RBASE             TELL ASSEMBLER
         GETMAIN  R,LV=WORKDLEN,SP=0    GET AREA FOR MYSAVE AND WORK
         ST    R13,4(0,R1)              CALLERS S.A. ADDR TO MY S.A.
         ST    R1,8(0,R13)              MY S.A. ADDR TO CALLERS S.A.
         LM    R15,R1,16(R13)           RESTORE REGS USED BY GETMAIN
         L     R13,8(0,R13)             R13 POINTS TO MY S.A.
         USING WORKD,R13                  TELL ASSEMBLER
*
         L     RBASE2,VBASE2            R11 HAS SECOND BASE ADDRESS
         USING BULLET01+4096,RBASE2       TELL ASSEMBLER
*
         USING PARMPDL,R9               ADDRESSABILITY OF PDE LIST
*
         XC    COMPCODE,COMPCODE        CLEAR COMPLETION CODE TO ZERO
         ST    R1,CPPLPTR               SAVE CPPL POINTER
*
         B     MAINLINE
*
VBASE2   DC    A(BULLET01+4096)         BEGINNING OF 2ND BASE REG RANGE
         EJECT
**********************************************************************
***                                                                ***
***   MAIN LINE ROUTINE                                            ***
***                                                                ***
**********************************************************************
*
MAINLINE BAL   RBAL,ABEND               SET UP ABEND EXIT
         BAL   RBAL,PPLSETUP            SET UP PARSE PARM LIST
*
MAPARSE  BAL   RBAL,PARSE               PARSE THE INPUT PARAMETERS
         CLC   RETCDE(4),F0             IF RETURN CODE IS BAD
         BNE   ENDING04                   GO TO CC=4 ENDING
*
MAALLOC  BAL   RBAL,DSNALLOC            ALLOCATE THE INPUT DSNAME
         CLC   RETCDE(4),F0             IF RETURN CODE IS BAD
         BE    MACLEAR                    THEN
         MVC   COMPCODE(4),F4               SET CC=4
         B     MAUNALC                      GO TO UNALLOCATION
*
MACLEAR  BAL   RBAL,CLEAR               CLEAR THE OLD MESSAGES
*
MABC     BAL   RBAL,BC                  SEND BROADCAST MESSAGES
*
MAUNALC  BAL   RBAL,UNALLOC             UNALLOCATE THE FILE NAMES
*
         B     ENDING                   BRANCH TO ENDING
         EJECT
**********************************************************************
***                                                                ***
***   EPILOG                                                       ***
***                                                                ***
***     1.  Release storage used by IKJPARS.                       ***
***     2.  Linkage conventions.                                   ***
***                                                                ***
**********************************************************************
ENDING04 LA       R15,X'04'(0,0)          RETURN CODE TO R15
         ST       R15,COMPCODE              SAVE IN COMPLETION CODE
*
ENDING   LA       R2,MYPPL+(PPLANS-PPL)   ADDRESS OF PTR TO PDL
         L        R2,0(0,R2)              R4 POINTS TO PDL
         IKJRLSA  (R2)                    FREE STORAGE OF PDL
*
ENDABEND ESTAE    0                       CANCEL THE ABEND EXIT
*
         L        R14,COMPCODE            R14 TEMPORARILY HAS COMP CODE
         LR       R15,R13                 R15 HAS MY SAVE AREA ADDRESS
         L        R13,4(0,R13)            R13 RESTORE, PNT TO CALLER SA
         FREEMAIN R,LV=WORKDLEN,SP=0,A=(R15)  FREE MYSAVE,COMPCODE,ETC
         LM       R0,R12,20(R13)          R0-R12 RESTORE FROM CALLER SA
         LR       R15,R14                 R15 GETS COMP CODE
         L        R14,12(0,R13)           R14 RESTORE FROM CALLERS S.A.
         MVI      12(R13),X'FF'           SIGNAL MODULE COMPLETE
         BR       R14                     RETURN
*
         EJECT
**********************************************************************
***                                                                ***
***           SET UP EXIT FOR ABENDS                               ***
***                                                                ***
***  Use ESTAE to set up an environment to trap ABENDs.            ***
***                                                                ***
***  1.  The exit is ESTXIT.                                       ***
***  2.  The return point is at label ENDING04 in main routine.    ***
***  3.  The parm list passed to the exit will be:                 ***
***                                                                ***
***         ESTUPL                                                 ***
***         +-----------+                                          ***
***         |           |==> Return point                          ***
***         +-----------+                                          ***
***         |           |==> User work area                        ***
***         +-----------+    +-----------+                         ***
***                          | Register  |                         ***
***                          | save area |                         ***
***                         ---         ---                        ***
***                         ---         ---                        ***
***                          |           |                         ***
***                          +-----------+                         ***
***                          | Other data|                         ***
***                          +-----------+                         ***
***                                                                ***
**********************************************************************
ABEND    ST    RBAL,ABRBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         MVC   ESTAEL(ESTAELLN),ESTAED  INITIALIZE ESTAE PARM LIST
         LA    R15,ENDING04             ADDRESS OF RETURN POINT
         ST    R15,ESTUPL                 INTO USER PARM LIST
         LA    R15,ESTUWK               ADDRESS OF USER WORK AREA
         ST    R15,ESTUPL+4               INTO USER PARM LIST
         LA    R4,ESTUPL                R4 HAS ADDRESS OF USER PARM LST
         L     R5,VESTXIT               R5 HAS ADDRESS OF ESTAE EXIT
*
ABESTAE  ESTAE (R5),PARAM=(R4),MF=(E,ESTAEL)  EXECUTE ESTAE
*
ABENDEND L     RBAL,ABRBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
         EJECT
**********************************************************************
***                                                                ***
***        CREATE PARSE PARAMETER LIST AND I/O PARM LIST           ***
***                                                                ***
***        PARSE                                                   ***
***          1.  Copy addresses of UPT, ECT and CBUF from CPPL.    ***
***          2.  Add addresses of own ECB, ANS and UWA, and        ***
***              address of PCE List (PCL) created by macros.      ***
***                                                                ***
***        I/O                                                     ***
***          1.  Copy addresses of UPT and ECT from CPPL.          ***
***          2.  Add address of own I/O ECB.                       ***
***          3.  Zero the IOPLIOPB pointer.  This will point to    ***
***              the parm block for the appropriate I/O routine.   ***
***              It will be filled in by the execute form of the   ***
***              STACK, GETLINE, PUTLINE or PUTGET macro when      ***
***              executed.                                         ***
**********************************************************************
PPLSETUP ST    RBAL,PPRBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         L     R4,CPPLPTR               ADDRESS OF CMD PROC PARM LIST
         USING CPPL,R4                    ADDRESSABILITY
         LA    R6,MYPPL                 ADDRESS OF MY PARSE PARM LIST
         USING PPL,R6                     ADDRESSABILITY
*
         MVC   PPLUPT(4),CPPLUPT        UPT  (CPPL)
         MVC   PPLECT(4),CPPLECT        ECT  (CPPL)
         LA    R5,MYECB
         ST    R5,PPLECB                ECB  (MINE)
         MVC   PPLPCL(4),VPARMPCL       PCL  (CSECT)
         LA    R5,MYANS
         ST    R5,PPLANS                ANS  (MINE)
         MVC   PPLCBUF(4),CPPLCBUF      CBUF (CPPL)
         LA    R5,MYUWA
         ST    R5,PPLUWA                UWA  (MINE)
         DROP  R6                       DROP ADDRESSABILITY
*
         LA    R6,MYIOPL                ADDRESS OF MY IO PARM LIST
         USING IOPL,R6                    ADDRESSABILITY
         MVC   IOPLUPT(4),CPPLUPT       UPT  (CPPL)
         MVC   IOPLECT(4),CPPLECT       ECT  (CPPL)
         LA    R15,MYIOECB
         ST    R15,IOPLECB              ECB  (MINE)
         XC    IOPLIOPB(4),IOPLIOPB     IOPB (0000)
*
         DROP  R4,R6
*
PPEND    L     RBAL,PPRBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
         EJECT
**********************************************************************
***                                                                ***
***    PARSE THE INPUT PARAMETER STRING                            ***
***                                                                ***
***      1.  Execute IKJPARS with CALLTSSR using own PPL.          ***
***      2.  Load address of Parm Descriptor Element List (PDL)    ***
***          from own ANS word into R9 for addressability.         ***
***                                                                ***
**********************************************************************
PARSE    ST    RBAL,PARBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         XC    MYECB,MYECB              ZERO THE ECB FOR PARSE
PARSEIT  CALLTSSR EP=IKJPARS,MF=(E,MYPPL)  PARSE THE PARMS
         L     R9,MYPPL+(PPLANS-PPL)    POINTER TO PDL ADDRESS
         L     R9,0(0,R9)               ADDRESSABILITY OF PDL
*
PAEND    L     RBAL,PARBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
         EJECT
**********************************************************************
***                                                                ***
***   UNALLOCATE FILE NAME                                         ***
***                                                                ***
**********************************************************************
UNALLOC  ST    RBAL,UNRBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
*                                     INITIALIZE SVC-99 CONTROL BOLCKS
UNSET    MVC   MY0(MY0LEN),@M0          INITIALIZE DATA
         GTEDASET MY0,CPPLPTR=CPPLPTR   LINK BLOCKS TOGETHER
*
UNINPUT  MVC   MY0DDNAM(8),DSDDNAM1     DDNAME TO T.U.
         BAL   R4,UNDYNALC              PERFORM DYNAMIC UNALLOCATION
*
UNEND    L     RBAL,UNRBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
*
UNDYNALC GTEDAALC MY0,VERB=UN,ERRMSG=NO PERFORM SVC-99/DAIRFAIL
         BR    R4
         EJECT
**********************************************************************
***                                                                ***
***   ALLOCATE THE INPUT DSNAME WITH GTEDAxxx                      ***
***                                                                ***
**********************************************************************
DSNALLOC ST    RBAL,DSRBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
DSDSNBC  TM    KEYBC+1,X'01'            IF NOT USING BC KEYWORD
         BNO   DSEND                      GO TO NEXT ROUTINE
*
         MVC   DSDDNAM1(8),DSDDNAM      DUMMY DDNAME TO SAVE AREA
*
*                                     INITIALIZE SVC-99 CONTROL BOLCKS
DSBCSET  MVC   MY1(MY1LEN),@M1          INITIALIZE DATA
         GTEDASET MY1,CPPLPTR=CPPLPTR   LINK BLOCKS TOGETHER
*
DSDSNB   L     R4,SBCDSN                R4 POINTS TO BC-DSN
         LH    R5,SBCDSN+4              R5 HAS LENGTH
         BCTR  R5,0                       MINUS 1 FOR EXEC
         B     *+10                     BRANCH AROUND MOVE
         MVC   MY1DSNAM(0),0(R4)          MOVE DSNAME TO T.U.
         EX    R5,*-6                   EXECUTE THE MOVE
         TM    SBCDSN+14,X'80'          IF MEMBER NAME NOT USED
         BNO   DSDSNBCX                   BRANCH AROUND
         L     R4,SBCDSN+8              R4 POINTS TO BC-MEMBNAME
         LH    R5,SBCDSN+12             R5 HAS LENGTH
         BCTR  R5,0                       MINUS 1 FOR EXEC
         B     *+10                     BRANCH AROUND MOVE
         MVC   MY1MEMBR(0),0(R4)          MOVE MEMBER NAME TO T.U.
         EX    R5,*-6                   EXECUTE THE MOVE
         B     DSNALC1                  BRANCH TO ALLOCATE
DSDSNBCX XC    MY1MEMKY(2),MY1MEMKY     NO MEMBER, ZERO THE KEY
DSNALC1  GTEDAALC MY1,VERB=AL,ERRMSG=YES  PERFORM SVC-99/DAIRFAIL
         MVC   DSDDNAM1(8),MY1RTDDN     SAVE THE RETURNED DDNAME
         CLC   MY1S99RC,F0              IF SVC-99 R/C NON-ZERO
         BNE   DSERR04                    GO TO ERROR
*
         B     DSEND
*
DSERR04  LA    R15,X'04'(0,0)           SET VALUE
         ST    R15,RETCDE                 INTO INTERNAL RETURN CODE
*
DSEND    L     RBAL,DSRBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
         EJECT
**********************************************************************
***                                                                ***
***   Execute the operator SEND command.                           ***
***                                                                ***
**********************************************************************
BC       ST    RBAL,BCRBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
BCCHECK  TM    KEYBC+1,X'01'            IF BROADCAST KEYWORD NOT USED
         BNO   BCEND                      BYPASS THE WHOLE ROUTINE
*
BCOPERX  L     R1,CPPLPTR                    R1 POINTS TO CPPL
         L     R1,(CPPLPSCB-CPPL)(R1)        R1 POINTS TO PSCB
         TM    (PSCBATR1-PSCB)(R1),PSCBCTRL  IF USER HAS OPER
         BO    BCOPERXN                        BRANCH TO CONTINUE
         LA    R4,TIOTADDR                   ELSE POINT TO RETURN AREA
         MVC   EXTRACTL(EXTRLEN),EXTRACTD      INITIALIZE EXTRACT PARM
         EXTRACT (R4),'S',FIELDS=(TIOT),MF=(E,EXTRACTL)  GET TIOT ADDR
         L     R1,TIOTADDR                   R1 POINTS TO TIOT
         CLC   (TIOCNJOB-TIOT)(8,R1),STCNAM  IF STC NAME IS OK
         BE    BCOPERXN                        BRANCH TO CONTINUE
         LA    R15,3                         ELSE SELECT MESSAGE #3
         BAL   RBAL,PUTLPROC                   WRITE IT
         B     BCRET04                         GO TO R/C=4 ENDING
BCOPERXN B     BCTIME                        NORMAL END OF ROUTINE
*
BCTIME   TIME                         , R1 GETS 00YYDDDF
         ST    R1,BCYYDDD               SAVE IT
         NI    BCYYDDD+3,X'FC'          SET POSITIVE SIGN
         MVC   LOYYDDD,P0               INITIALIZE LOW YYDDD
         MVC   HIYYDDD,P99365           INITIALIZE HIGH YYDDD
         XC    BITEXTWK(8),BITEXTWK     INITIALIZE TEXT WORK SAVE AREA
*
BCDCBSET MVC   BCDCB(BCDCBL),@BCDCB     INITIALIZE DCB
         MVC   BCDCB+(DCBDDNAM-IHADCB)(8),DSDDNAM1  INITIALIZE DDN
*
BCOPEN   MVC   OPENL(OPENLLEN),@OPENL   INITIALIZE OPEN PARM LIST
         LA    R4,BCDCB                 POINT TO DCB
         OPEN  ((R4),INPUT),MF=(E,OPENL)  OPEN THE FILE
         TM    (DCBRECFM-IHADCB)(R4),DCBRECF   IF RECFM NOT FIXED
         BNO   BCBADFIL                          OR
         CLC   (DCBLRECL-IHADCB)(2,R4),H80     IF LRECL NOT 80
         BNE   BCBADFIL                          GO TO ERROR ROUTINE
*
SVC1     NOPR  0                        SWITCH JSCBAUTH BIT
BCSUP    MODESET MODE=SUP,KEY=ZERO      GET SUP STATE, KEY 0
BCIO     BAL   RBAL,BIO                 PERFORM IO ROUTINE
         TM    BISWITCH,X'80'           IF END OF FILE
         BO    BCEOF                      GET OUT OF LOOP
         B     BCIO                     ELSE LOOP UP TILL END OF FILE
BCEOF    NOPR  0                        EOF RETURN POINT
BCPROB   MODESET MODE=PROB,KEY=NZERO    RETURN TO PROB STATE, KEY 8
SVC2     NOPR  0                        SWITCH JSCBAUTH BIT
*
BCCLOSE  MVC   CLOSL(CLOSLLEN),@CLOSL   INITIALIZE CLOSE PARM LIST
         LA    R4,BCDCB                 POINT TO DCB
         CLOSE ((R4)),MF=(E,CLOSL)      CLOSE THE FILE
*
         B     BCEND
*
BCBADFIL LA    R15,4                    SELECT MESSAGE #4
         BAL   RBAL,PUTLPROC              WRITE THE MESSAGE
         B     BCRET04                  GO TO ERROR RETURN
*
BCRET04  LA    R15,X'04'                RETURN CODE TO R15
         ST    R15,RETCDE               SAVE IT
         B     BCEND                    BRANCH TO ENDING
*
BCEND    L     RBAL,BCRBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
         EJECT
**********************************************************************
***                                                                ***
***  If the CLEAR keyword has been used, clear old messages.       ***
***                                                                ***
**********************************************************************
CLEAR    ST    RBAL,CLRBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
CLEARCK  TM    KEYC+1,X'01'             IF CLEAR KEYWORD NOT USED
         BNO   CLEND                      GO TO END OF ROUTINE
*
CLOPERX  L     R1,CPPLPTR                    R1 POINTS TO CPPL
         L     R1,(CPPLPSCB-CPPL)(R1)        R1 POINTS TO PSCB
         TM    (PSCBATR1-PSCB)(R1),PSCBCTRL  IF USER HAS OPER
         BO    CLOPERXN                        BRANCH TO CONTINUE
         LA    R4,TIOTADDR                   ELSE POINT TO RETURN AREA
         MVC   EXTRACTL(EXTRLEN),EXTRACTD      INITIALIZE EXTRACT PARM
         EXTRACT (R4),'S',FIELDS=(TIOT),MF=(E,EXTRACTL)  GET TIOT ADDR
         L     R1,TIOTADDR                   R1 POINTS TO TIOT
         CLC   (TIOCNJOB-TIOT)(8,R1),STCNAM  IF STC NAME IS OK
         BE    CLOPERXN                        BRANCH TO CONTINUE
         LA    R15,3                         ELSE SELECT MESSAGE #3
         BAL   RBAL,PUTLPROC                   WRITE IT
         B     CLRET04                         GO TO R/C=4 ENDING
CLOPERXN B     CLCOUNT                       NORMAL END OF ROUTINE
*
CLCOUNT  L     R3,SC                    R3 POINTS TO CLEAR COUNT
         L     R3,0(0,R3)               R3 CONTAINS BINARY COUNT
         LTR   R3,R3                    IF COUNT NOT POSITIVE
         BP    CLMSGNO                    THEN
         LA    R15,7                        SELECT MESSAGE #7
         BAL   RBAL,PUTLPROC                WRITE THE MESSAGE
         LA    R15,0                        SELECT BLANK LINE
         BAL   RBAL,PUTLPROC                WRITE THE MESSAGE
         B     CLRET04                      GO TO RC=4 ENDING
CLMSGNO  XR    R4,R4                    CLEAR R4 MSG NUMBER
*
SVC3     NOPR  0                        SWITCH JSCBAUTH BIT
CLSUP    MODESET MODE=SUP,KEY=ZERO      GET SUP STATE, KEY 0
*
CLLOOP1  LA    R4,1(0,R4)               INCR MSG NUMBER BY 1, BINARY
         CVD   R4,CLDOUBLE              CLDOUBLE HAS MSG NUMBER PACK
         OI    CLDOUBLE+7,X'0F'         KILL THE SIGN
         UNPK  CLZONE(3),CLDOUBLE+6(2)  CLZONE HAS MSG NUMBER ZD
         MVC   CLCMD(18),@CLCMD         INITIALIZE THE COMMAND
         MVC   CLCMD+9(2),CLZONE+1      INSERT THE MSG NUMBER
         LA    R1,CLCMD                 POINT TO THE COMMAND FOR SVC-34
         XR    R0,R0                    CLEAR R0 FOR SVC-34
         SVC   34                       OPERATOR
         STIMER WAIT,BINTVL=BIWAITIM    WAIT A LITTLE BIT
         BCT   R3,CLLOOP1               LOOP BACK UP
*
CLPROB   MODESET MODE=PROB,KEY=NZERO    RETURN TO PROB STATE, KEY 8
SVC4     NOPR  0                        SWITCH JSCBAUTH BIT
*
         LA    R15,2                    SELECT MESSAGE #2
         BAL   RBAL,PUTLPROC            WRITE THE MESSAGE
         LA    R15,0                    SELECT BLANK LINE
         BAL   RBAL,PUTLPROC            WRITE THE MESSAGE
*
         B     CLEND
*
CLRET04  LA    R15,X'04'                RETURN CODE TO R15
         ST    R15,RETCDE               SAVE IT
         B     CLEND                    BRANCH TO ENDING
*
CLEND    L     RBAL,CLRBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
         EJECT
**********************************************************************
***                                                                ***
***  Transfer BROADCAST input to output.                           ***
***                                                                ***
**********************************************************************
BIO      ST    RBAL,BIRBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         LA    R4,BCDCB                 POINT TO DCB
         NI    BISWITCH,X'7F'           INITIALIZE EOF SWITCH OFF
         GET   (R4),BITEXT              GET INPUT
         B     BIEDIT                   BRANCH AROUND EOF ROUTINE
*
BIEOF    OI    BISWITCH,X'80'           SET EOF SWITCH ON
         CLC   BITEXTWK(5),ETENDE       IF PREV USED CARD WAS '-END '
         BE    BIEND                      GO TO ENDING
         CLI   BITEXTWK,X'00'           IF NO PREV CARD WAS USED
         BE    BIEND                      GO TO ENDING
         MVC   BIMAIN(87),@BIHYPH       INITIALIZE COMMAND
         B     BIMSG                    BRANCH AROUND EDITING
*
BIEDIT   BAL   RBAL,ETEXT               EDIT THE INPUT
         CLC   RETCDE(4),F0             IF NOT PRINT RETURN CODE
         BNE   BIEND                      GO TO ENDING
*
BIMSG    MVC   MSG0001B(2),H75          SET MESSAGE SEGMENT LENGTH
         MVC   MSG0001B+2(2),H9           SET MESSAGE SEGMENT OFFSET
         MVC   MSG0001B+4(71),BIDATA-3    SET TEXT
         LA    R15,1(0,0)                 SET MESSAGE NUMBER
         BAL   RBAL,PUTLPROC              WRITE THE MESSAGE
         LH    R4,BIMAIN                R4 HAS LENGTH OF SEND COMMAND
         C     R4,F133                  IF LENGTH NOT GREATER THAN 133
         BNH   BICMD                      GO EXECUTE THE COMMAND
         LA    R15,6                    ELSE SELECT MESSAGE #6
         BAL   RBAL,PUTLPROC              WRITE IT
         LA    R15,0                      SELECT BLANK LINE
         BAL   RBAL,PUTLPROC              WRITE IT
         B     BIRET04                    GO TO HIGH RETURN
*
BICMD    LA    R1,BIMAIN                POINT TO COMMAND
         XR    R0,R0                    CLEAR R0 FOR SVC-34
BICONSOL SVC   34                       EXECUTE CONSOLE SVC
         ST    R15,BIRC                 SAVE THE RETURN CODE
         MVC   BITEXTWK(8),ETTEXTWK     SAVE THE TEXT WORK AREA
BIWAIT   STIMER WAIT,BINTVL=BIWAITIM    WAIT FOR A LITTLE
*
         B     BIEND                    GO TO ENDING
*
BIRET04  LA    R15,X'04'                RETURN CODE TO R15
         ST    R15,RETCDE               SAVE IT
         B     BIEND                    BRANCH TO ENDING
*
BIEND    L     RBAL,BIRBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
         EJECT
**********************************************************************
***                                                                ***
***  Edit the text.                                                ***
***                                                                ***
***  1.  Process control cards.                                    ***
***                                                                ***
***     The card type is designated by characters in the beginning ***
***     of the card image.  Types can be in lower/UPPER case.      ***
***                                                                ***
***     '*'      = Comment.                                        ***
***     '-com'   = Comment.                                        ***
***     '-dates' = MM/DD/YY dates for message to begin and end.    ***
***                (Default is the previous -dates card.)          ***
***     blank    = Message text.  65-bytes, beginning with cc 2.   ***
***     '-end'   = End of message text.  Causes line of hyphens.   ***
***                                                                ***
***  2.  Expand apostrophes to double apostrophes in text.         ***
***  3.  Set RETCDE:                                               ***
***        00 - Convert this to a SEND command.                    ***
***        04 - Don't convert this to a SEND command.              ***
***                                                                ***
**********************************************************************
ETEXT    ST    RBAL,ETRBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
ETFORMX  MVC   ETTEXTWK(8),BITEXT       GET FIRST 8 CHARACTERS TO WORK
         OC    ETTEXTWK(8),BLANKS       SHIFT TO UPPER CASE
*
         CLI   ETTEXTWK,X'40'           IF IT BEGINS WITH A BLANK
         BE    ETTEXT                     GO PROCESS THE TEXT CARD
         CLC   ETTEXTWK(7),ETDATES      IF IT IS A '-DATES ' CARD
         BE    ETDATE                     GO PROCESS THE DATE CARD
         CLC   ETTEXTWK(5),ETENDE       IF IT IS A '-END' CARD
         BE    ETEOD                      GO PROCESS THE EOD CARD
         CLI   ETTEXTWK,C'*'            IF IT IS A '*' CARD
         BE    ETRET04                  OR
         CLC   ETTEXTWK(5),ETCOM        IF IT IS A '-COM ' CARD
         BE    ETRET04                    GO TO NO-SEND RETURN
*                                       IF NO MATCH ABOVE
         B     ETRET04                    GO TO NO-SEND RETURN
*
ETTEXT   CP    LOYYDDD(4),BCYYDDD       IF LOW DATE HIGHER THAN CURRENT
         BH    ETRET04                    GO TO NO-SEND RETURN
         CP    HIYYDDD(4),BCYYDDD       IF HIGH DATE LESS THAN CURRENT
         BL    ETRET04                    GO TO NO-SEND RETURN
         MVC   BIMAIN(87),@BIMAIN       INITIALIZE COMMAND
         MVC   BIDATA(65),BITEXT+1      MOVE TEXT TO OUTPUT AREA
         BAL   RBAL,APOST               EXPAND TEXT APOSTROPHES
         B     ETEND                    ELSE GO TO SEND RETURN
*
ETEOD    CP    LOYYDDD(4),BCYYDDD       IF LOW DATE HIGHER THAN CURRENT
         BH    ETRET04                    GO TO NO-SEND RETURN
         CP    HIYYDDD(4),BCYYDDD       IF HIGH DATE LESS THAN CURRENT
         BL    ETRET04                    GO TO NO-SEND RETURN
         MVC   BIMAIN(87),@BIHYPH       INITIALIZE COMMAND
         B     ETEND                    GO TO SEND RETURN
*
ETDATE   MVC   DATE(8),BITEXT+7         MOVE BEGIN DATE TO WORK AREA
         BAL   RBAL,DATECONV            CONVERT TO 00YYDDDC
         CLC   RETCDE(4),F0             IF BAD RETURN
         BNE   ETDATERR                   GO TO ERROR ROUTINE
         MVC   LOYYDDD(4),DATEPACK      ELSE MOVE TO STORAGE
         MVC   DATE(8),BITEXT+16        MOVE END DATE TO WORK AREA
         BAL   RBAL,DATECONV            CONVERT TO 00YYDDDC
         CLC   RETCDE(4),F0             IF BAD RETURN
         BNE   ETDATERR                   GO TO ERROR ROUTINE
         MVC   HIYYDDD(4),DATEPACK      ELSE MOVE TO STORAGE
         B     ETRET04                  BRANCH TO NO-SEND RETURN
*
ETDATERR MVC   MSG0005B(13),WRK0005B    INITIALIZE MESSAGE SEGMENT
         MVC   MSG0005B+4(8),DATE       MOVE BAD DATE INTO SEGMENT
         MVC   LOYYDDD(4),BCYYDDD       MOVE CURRENT DATE TO START DATE
         MVC   HIYYDDD(4),BCYYDDD         AND TO END DATE
         LA    R15,0                    SELECT BLANK LINE
         BAL   RBAL,PUTLPROC              WRITE IT
         LA    R15,5                    SELECT MESSAGE #5
         BAL   RBAL,PUTLPROC              WRITE IT
         LA    R15,0                    SELECT BLANK LINE
         BAL   RBAL,PUTLPROC              WRITE IT
         B     ETRET04                  BRANCH TO NO-SEND RETURN
*
ETRET04  LA    R15,X'04'                RETURN CODE TO R15
         ST    R15,RETCDE               SAVE IT
         B     ETEND                    BRANCH TO ENDING
*
ETEND    L     RBAL,ETRBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
         EJECT
**********************************************************************
***                                                                ***
***  Expand single apostrophes to double apostrophes.              ***
***                                                                ***
**********************************************************************
APOST    ST    RBAL,APRBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         LA    R3,BIDATA-1              R3 POINTS TO TEXT-1
         LA    R4,65(0,R3)              R4 POINTS TO LAST TEXT CHAR
         LA    R1,65(0,0)               R1 IS COUNTER FOR OUTER LOOP
APLOOP1  LA    R3,1(0,R3)               R3 POINTS TO NEXT CHAR
         CLI   0(R3),X'7D'              IF NOT APOSTROPHE
         BNE   APLOOP1X                   GO TO OUTER LOOP END
*
         LR    R2,R1                    R2 IS INNER LOOP COUNTER
         LR    R5,R4                    R5 IS INNER LOOP POINTER
         LA    R4,1(0,R4)               R4 POINTS TO NEW LAST CHAR
APLOOP2  MVC   1(1,R5),0(R5)            COPY FROM CHAR TO CHAR+1
         BCTR  R5,0                     DECR POINTER BY ONE
APLOOP2X BCT   R2,APLOOP2               LOOP BACK UP TO INNER LOOP
*
         LA    R3,1(0,R3)               MOVE POINTER TO 'NEW' APOST
*
APLOOP1X BCT   R1,APLOOP1               LOOP BACK UP TO OUTER LOOP
*
*
APSAVE   MVC   1(9,R4),APSENDSV         MOVE B--,'SAVE TO SEND COMMAND
         LA    R4,10(0,R4)              R4 POINTS JUST BEYOND LAST CHAR
*
APLEN    LA    R1,BIMAIN                R1 POINTS TO SEND COMMAND LEN
         SR    R4,R1                    R4 IS LENGTH OF COMMAND
         STH   R4,BIMAIN                  SAVE LEN IN COMMAND BUFFER
         B     APEND
*
APRET04  LA    R15,X'04'                RETURN CODE TO R15
         ST    R15,RETCDE               SAVE IT
         B     APEND                    BRANCH TO ENDING
*
APEND    L     RBAL,APRBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
         EJECT
**********************************************************************
***                                                                ***
***   Convert MM/DD/YY (ZD) to 00YYDDDC (packed)                   ***
***                                                                ***
**********************************************************************
DATECONV ST    RBAL,DARBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
DAFORMAT XC    DATEPACK,DATEPACK        ZERO THE RETURN DATE
         MVC   DATEWORK(8),DATE         COPY DATE TO TEST AREA
         NC    DATEWORK(8),DATSTPAT     'AND' IT WITH TEST PATTERN
         CLC   DATEWORK(8),DARESPAT     IF RESULT IS NOT '00/00/00'
         BNE   DARET04                    GO TO HIGH RETURN
*
DAYEAR   NI    DASWITCH,X'7F'           TURN OFF LEAP YEAR SWITCH
         PACK  DADOUBLE(8),DAYY(2)      CONVERT TO 00000000 00000YYF
         MVC   DAYYPACK(4),DATEPACK       SAVE IT
         CVB   R3,DADOUBLE              R3 HAS BINARY YEAR
DALEAP   XR    R2,R2                    CLEAR R2 FOR DIVIDE
         D     R2,F4                    CHECK FOR LEAP YEAR
         LTR   R2,R2                    IF LEAP YEAR
         BNZ   *+8                        THEN
         OI    DASWITCH,X'80'               TURN ON LEAP YEAR SWITCH
*
DAMONTH  CLC   DAMM(2),ZD12             IF MM GREATER THAN 12
         BNH   DAMONPK                    THEN
         XC    DATEPACK,DATEPACK            ZERO THE RETURN DATE
         B     DARET04                      AND GO TO HIGH RETURN
DAMONPK  PACK  DADOUBLE(8),DAMM(2)      CONVERT TO 00000000 00000MMF
         CVB   R3,DADOUBLE              R3 HAS BINARY MONTH 1 TO 12
         BCTR  R3,0                     R3 IS BINARY 0 TO 11
         SLL   R3,1                     R3 IS OFFSET INTO TABLE
         TM    DASWITCH,X'80'           IF LEAPYEAR
         BNO   *+12                       THEN
         LA    R2,DALEAPTB                  R2 HAS ADDR OF LEAP TABLE
         B     *+8                      ELSE
         LA    R2,DAREGTB                   R2 HAS ADDR OF REGULAR TBL
         LH    R2,0(R3,R2)              R2 HAS DDD FOR DAY 0 OF MONTH
*
DADAY    CLC   DADD(2),ZD31             IF DD GREATER THAN 31
         BNH   DADAYPK                    THEN
         XC    DATEPACK,DATEPACK            ZERO THE RETURN DATE
         B     DARET04                      AND GO TO HIGH RETURN
DADAYPK  PACK  DADOUBLE(8),DADD(2)      CONVERT TO 00000000 00000DDF
         CVB   R3,DADOUBLE              R3 HAS BINARY DAY OF MONTH
         AR    R3,R2                    R3 HAS BINARY DAY OF YEAR
         CVD   R3,DADOUBLE              DADOUBLE HAS 00000000 0000DDDC
         L     R3,DATEPACK              R3 HAS 0000DDDC
*
DACOMB   L     R2,DAYYPACK              R2 HAS 00000YYF
         SRL   R2,4                     R2 HAS 000000YY
         SLL   R2,16                    R2 HAS 00YY0000
         OR    R2,R3                    R2 HAS 00YYDDDC
         ST    R2,DATEPACK                SAVE IT
*
         B     DAEND
*
DARET04  LA    R15,X'04'                RETURN CODE TO R15
         ST    R15,RETCDE               SAVE IT
         B     DAEND                    BRANCH TO ENDING
*
DAEND    L     RBAL,DARBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
         EJECT
**********************************************************************
***                                                                ***
***   WRITE MESSAGE WITH PUTLINE                                   ***
***                                                                ***
***    1.  IOPL was initialized at beginning of program, IOPLIOPB  ***
***        field will be filled in by PUTLINE execute macro.       ***
***    2.  PTPB will be filled in by PUTLINE execute macro.        ***
***    3.  OLD will be filled in by this procedure.                ***
***                                                                ***
***    Message number in R15 is multiplied by four and used as     ***
***    offset into PUTABLE.  PUTABLE contains branch addresses     ***
***    for processing appropriate message.                         ***
**********************************************************************
PUTLPROC ST    RBAL,PURBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         SLL   R15,2                    R15 IS NOW OFFSET INTO PUTABLE
         L     R15,PUTABLE(R15)         R15 NOW POINTS TO PROCESSING
         BR    R15                      GO TO APPROPRIATE PROCESSING
*
PUTABLE  DC    A(PU0000)                PROCESS ADDR FOR BLANK LINE
         DC    A(PU0001)                PROCESS ADDR FOR BUL0001I
         DC    A(PU0002)                PROCESS ADDR FOR BUL0002I
         DC    A(PU0003)                PROCESS ADDR FOR BUL0003E
         DC    A(PU0004)                PROCESS ADDR FOR BUL0004E
         DC    A(PU0005)                PROCESS ADDR FOR BUL0005C
         DC    A(PU0006)                PROCESS ADDR FOR BUL0006E
         DC    A(PU0007)                PROCESS ADDR FOR BUL0007C
*
PU0000   LA    R15,1(0,0)               SEGMENTS = 1
         ST    R15,MYOLD                  INTO O.L.D.
         LA    R15,MSG0000              ADDR OF BLANK LINE
         ST    R15,MYOLD+4                INTO O.L.D.
         B     PUOUTPUT                 GO WRITE IT
*
PU0001   LA    R15,2(0,0)               SEGMENTS = 2
         ST    R15,MYOLD                  INTO O.L.D.
         LA    R15,MSG0001A             ADDR OF BUL0001I, SEG #1
         ST    R15,MYOLD+4                INTO O.L.D.
         LA    R15,MSG0001B             ADDR OF BUL0001I, SEG #2
         ST    R15,MYOLD+8                INTO OLD
         B     PUOUTPUT                 GO WRITE IT
*
PU0002   LA    R15,1(0,0)               SEGMENTS = 1
         ST    R15,MYOLD                  INTO O.L.D.
         LA    R15,MSG0002              ADDR OF BUL0002I
         ST    R15,MYOLD+4                INTO O.L.D.
         B     PUOUTPUT                 GO WRITE IT
*
PU0003   LA    R15,1(0,0)               SEGMENTS = 1
         ST    R15,MYOLD                  INTO O.L.D.
         LA    R15,MSG0003              ADDR OF BUL0003E
         ST    R15,MYOLD+4                INTO O.L.D.
         B     PUOUTPUT                 GO WRITE IT
*
PU0004   LA    R15,1(0,0)               SEGMENTS = 1
         ST    R15,MYOLD                  INTO O.L.D.
         LA    R15,MSG0004              ADDR OF BUL0004E
         ST    R15,MYOLD+4                INTO O.L.D.
         B     PUOUTPUT                 GO WRITE IT
*
PU0005   LA    R15,2(0,0)               SEGMENTS = 2
         ST    R15,MYOLD                  INTO O.L.D.
         LA    R15,MSG0005A             ADDR OF BUL0005C, SEG #1
         ST    R15,MYOLD+4                INTO O.L.D.
         LA    R15,MSG0005B             ADDR OF BUL0005C, SEG #2
         ST    R15,MYOLD+8                INTO OLD
         B     PUOUTPUT                 GO WRITE IT
*
PU0006   LA    R15,1(0,0)               SEGMENTS = 1
         ST    R15,MYOLD                  INTO O.L.D.
         LA    R15,MSG0006              ADDR OF BUL0006E
         ST    R15,MYOLD+4                INTO O.L.D.
         B     PUOUTPUT                 GO WRITE IT
*
PU0007   LA    R15,1(0,0)               SEGMENTS = 1
         ST    R15,MYOLD                  INTO O.L.D.
         LA    R15,MSG0007              ADDR OF BUL0007C
         ST    R15,MYOLD+4                INTO O.L.D.
         B     PUOUTPUT                 GO WRITE IT
*
PUOUTPUT XC    MYPTPB(MYPTPBLN),MYPTPB  ZERO THE PUTLINE PARM BLOCK
         XC    MYIOECB,MYIOECB          ZERO THE ECB
         PUTLINE PARM=MYPTPB,OUTPUT=MYOLD,MF=(E,MYIOPL)
         LTR   R15,R15                  IF RETURN IS ZERO
         BZ    PUEND                      GO TO END
         B     PUERR04                  ELSE GO TO ERROR
*
PUERR04  LA    R15,X'04'(0,0)           SET VALUE
         ST    R15,RETCDE                 INTO INTERNAL RETURN CODE
*
PUEND    L     RBAL,PURBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
         EJECT
**********************************************************************
***                                                                ***
***   DATA CONSTANTS                                               ***
***                                                                ***
**********************************************************************
CONSTDTA DS    0D                       AREA FOR DATA CONSTANTS
VPARMPCL DC    V(PARMPCL)               ADDR OF PARM CONTROL LIST
VESTXIT  DC    V(ESTXIT)                ADDR OF ABEND EXIT
*
F0       DC    F'0'                     CONSTANT
F4       DC    F'4'                     CONSTANT
F133     DC    F'133'                   CONSTANT
H2       DC    H'2'                     CONSTANT
H9       DC    H'9'                     CONSTANT
H75      DC    H'75'                    CONSTANT
H80      DC    H'80'                    CONSTANT
P0       DC    PL4'+0'                  CONSTANT
P99365   DC    PL4'+99365'              CONSTANT
BLANKS   DC    CL80' '                  BLANK LINE
ZDZEROES DC    XL8'F0F0F0F0F0F0F0F0'    ZONE DECIMAL ZEROES
ZD12     DC    CL2'12'                  CONSTANT
ZD31     DC    CL2'31'                  CONSTANT
*
STCNAM   DC    CL8'&STC'                ALLOWED STC NAME
*                              0....+....1....+....2....+....3....+
*              4....+....5....+....6....+
MSG0000  DC    H'06',H'00',CL02'  '
MSG0001A DC    H'79',H'00',CL75'BUL0001I '
MSG0002  DC    H'38',H'00',CL34'BUL0002I CLEAR operation complete.'
MSG0003  DC    H'67',H'00',CL63'BUL0003E OPERATOR authority is requiredx
                for CLEAR or BROADCAST.'
MSG0004  DC    H'64',H'00',CL60'BUL0004E Input file must have 80-byte, X
               fixed length records.'
MSG0005A DC    H'68',H'00',CL64'BUL0005C Unable to process date:   CurrX
               ent date assumed.'
WRK0005B DC    H'13',H'33',CL09'        .'
MSG0006  DC    H'59',H'00',CL55'BUL0006E Line ignored.  Too many apostrX
               ophes (46, max).'
MSG0007  DC    H'63',H'00',CL59'BUL0007C CLEAR function ignored.  QuantX
               ity was less than 1.'
*
EXTRACTD EXTRACT MF=L                   PARM LIST FOR EXTRACT
*
BIWAITIM DC    F'&WAIT'                 WAIT TIME (100THS OF SECONDS)
*
*                0-   --       -+--- -1----+----2----+----3----+----4--
*              --+----5----+----6----+----7----+----8 ----+-
@BIMAIN  DC    H'87',H'0',CL83'SEND ''-- ....+....1....+....2....+....3X
               ....+....4....+....5....+....6....+ --'',SAVE'
@BIHYPH  DC    H'87',H'0',CL83'SEND ''---------------------------------X
               --------------------------------------'',SAVE'
*
@CLCMD   DC    H'18',H'0',CL14'SEND NN,DELETE'
*
@M0      GTEDADAT DDNAM=X,UNALC=YES
@M1      GTEDADAT DSNAM=X,MEMBR=X,STATS=SHR,RTDDN=X
*
ETDATES  DC    CL7'-DATES '             LITERAL
ETCOM    DC    CL5'-COM '               LITERAL
ETENDE   DC    CL5'-END '               LITERAL
*
APSENDSV DC    CL9' --'',SAVE'          SUFFIX OF SEND TEXT
*
DAREGTB  DC    H'000,031,059,090,120,151,181,212,243,273,304,334'
DALEAPTB DC    H'000,031,060,091,121,152,182,213,244,274,305,335'
DATSTPAT DC    XL8'F0F0FFF0F0FFF0F0'    'AND' TEST FOR 'NN/NN/NN'
DARESPAT DC    CL8'00/00/00'            RESULT OF GOOD TEST
*
ESTAED   ESTAE MF=L
*
DSDDNAM  DC    CL8'SYS99999'            DUMMY DDNAME
*
@OPENL   OPEN  (),MF=L                  OPEN PARM LIST
@CLOSL   CLOSE (),MF=L                  CLOSE PARM LIST
*
@BCDCB   DCB   DSORG=PS,MACRF=(GM),EODAD=BIEOF
*
CONSTEND DS    0D
CONSTLEN EQU   *-CONSTDTA               TOTAL LENGTH OF CONSTANTS
*
         EJECT
**********************************************************************
***                                                                ***
***    COMMAND OPERANDS                                            ***
***                                                                ***
***    See syntax description at beginning of program.             ***
***                                                                ***
**********************************************************************
PARMPCL  IKJPARM  DSECT=PARMPDL
*
KEYC     IKJKEYWD DEFAULT='NOCLEAR'
         IKJNAME  'CLEAR',ALIAS=('K'),SUBFLD=SUBC
         IKJNAME  'NOCLEAR',ALIAS=('NOK')
*
KEYBC    IKJKEYWD DEFAULT='NOBROADCAST'
         IKJNAME  'BROADCAST',ALIAS=('BC'),SUBFLD=SUBBCDSN
         IKJNAME  'NOBROADCAST',ALIAS=('NOBC')
*
SUBBCDSN IKJSUBF
SBCDSN   IKJPOSIT DSNAME,                                              X
               USID,                                                   X
               DEFAULT='''&SYSBC''',                                   X
               HELP=('1-44 CHARACTER DSNAME AND 1-8 CHARACTER MEMBER')
*
SUBC     IKJSUBF
SC       IKJIDENT 'NUMBER OF MESSAGES',                                X
               MAXLNTH=2,                                              X
               FIRST=NUMERIC,OTHER=NUMERIC,                            X
               INTEG,                                                  X
               DEFAULT='30',                                           X
               HELP=('NUMBER OF MESSAGES TO DELETE')
*
         IKJENDP
*
         EJECT
**********************************************************************
***                                                                ***
***   DATA AREA IN SUBPOOL 000                                     ***
***                                                                ***
**********************************************************************
WORKD    DSECT                          AREA-13 FOR VARIABLES
MYSAVE   DS    18F                      REGISTER SAVE AREA
CPPLPTR  DS    F                        INITIAL VALUE OF R1 (CPPLADDR)
RETCDE   DS    F                        INTERNAL RETURN CODE
COMPCODE DS    F                        PROGRAM COMPLETION CODE
*
ABRBALSV DS    F                        RETURN ADDRESS SAVE AREA
PPRBALSV DS    F                        RETURN ADDRESS SAVE AREA
PARBALSV DS    F                        RETURN ADDRESS SAVE AREA
UNRBALSV DS    F                        RETURN ADDRESS SAVE AREA
DSRBALSV DS    F                        RETURN ADDRESS SAVE AREA
BCRBALSV DS    F                        RETURN ADDRESS SAVE AREA
CLRBALSV DS    F                        RETURN ADDRESS SAVE AREA
ETRBALSV DS    F                        RETURN ADDRESS SAVE AREA
APRBALSV DS    F                        RETURN ADDRESS SAVE AREA
DARBALSV DS    F                        RETURN ADDRESS SAVE AREA
BIRBALSV DS    F                        RETURN ADDRESS SAVE AREA
PURBALSV DS    F                        RETURN ADDRESS SAVE AREA
*
TIOTADDR DS    F                        ADDRESS OF TIOT
*
MYPPL    DS    7F                       PARSE PARAMETER LIST
MYECB    DS    F                        ECB FOR PARSE
MYANS    DS    F                        POINTER TO THE PDL
*
MYUWA    DS    0D                       WORK/SAVE AREA FOR PARSE EXITS
MYUWASV  DS    18F                      SAVE AREA FOR EXITS
MYUWAWRK DS    14F                      WORK AREA FOR EXITS
*
ETTEXTWK DS    CL8                      WORK AREA FOR TEXT
BCYYDDD  DS    PL4                      CURRENT 00YYDDDC
LOYYDDD  DS    PL4                      LOW 00YYDDDC
HIYYDDD  DS    PL4                      HIGH 00YYDDDC
*
DASWITCH DS    B'00000000'              SWITCHES
*                1.......                 X'80' - IT IS A LEAPYEAR
*
DADOUBLE DS    0D
         DS    PL4                      00YYDDDF
DATEPACK DS    PL4                      WORK AREA FOR PACKED VALUES
DAYYPACK DS    PL4                      PACKED YEAR
*
DATEWORK DS    CL8
*
DATE     DS    0CL8
DAMM     DS    CL2
         DS    CL1
DADD     DS    CL2
         DS    CL1
DAYY     DS    CL2
*
MY0      GTEDADAT MAP=ONLY,DDNAM=X,UNALC=YES
MY1      GTEDADAT MAP=ONLY,DSNAM=X,MEMBR=X,STATS=SHR,RTDDN=X
*
MSG0001B DS    H,H,CL71                 SEGMENT TWO OF MESSAGE
MSG0005B DS    H,H,CL9                  SEGMENT TWO OF MESSAGE
*
MYIOPL   DS    4F                       IO PARM LIST
MYIOECB  DS    F                        ECB FOR I/O ROUTINES
MYPTPB   PUTLINE MF=L                   PUTLINE PARM BLOCK (PTPB)
MYPTPBLN EQU   *-MYPTPB                   LENGTH OF PTPB
*
*                                       OUTPUT LINE DESCRIPTOR, 1 LEVEL
MYOLD    DS    F                          NUMBER OF SEGMENTS
         DS    5A                         ADDRS OF UP TO FIVE SEGMENTS
*
EXTRACTL EXTRACT MF=L                   PARM LIST FOR EXTRACT
EXTRLEN  EQU   *-EXTRACTL
*
BIMAIN   DC    H'87',H'0'               LENGTH, OFFSET
         DC    CL9'SEND ''-- '          SEND '--B
BIDATA   DS    CL65                     TEXT
         DC    CL9' --'',SAVE'          B--',SAVE
         DS    CL65                     EXPANSION AREA DUE TO APOST'S
*
BIRC     DS    F
BISWITCH DS    B '00000000'             SWITCH AREA
*                 1.......                X'80' - EOF
*                 .1111111                      - NOT USED
BITEXTWK DS    CL8                      SAVE WORK AREA FOR TEXT
*
CLCMD    DS    2H,CL14                  SEND DELETE COMMAND
CLZONE   DS    CL3                      MESSAGE NUMBER, ZD
CLDOUBLE DS    D                        MESSAGE NUMBER, PACKED
*
ESTAEL   ESTAE MF=L
ESTAELLN EQU   *-ESTAEL
ESTUPL   DS    0D                       ESTAE USER PARM LIST
         DS    A                          ADDRESS OF RETURN POINT
         DS    A                          ADDRESS OF USER WORK AREA
ESTUWK   DS    0D                       ESTAE USER WORK AREA
         DS    18F                        REGISTER SAVE AREA
         DS    D                          DOUBLE WORD FOR UNPACK
         DS    F                          BAL REGISTER SAVE AREA
         WTO   '.+....1....+....2....+....3....+....4....+....5',      X
               ROUTCDE=(11),DESC=(7),MF=L
*
DSDDNAM1 DS    CL8                      SAVE AREA FOR RETURNED DDNAME
DSDDNAM2 DS    CL8                      SAVE AREA FOR RETURNED DDNAME
*
OPENL    OPEN  (),MF=L                  OPEN PARM LIST
OPENLLEN EQU   *-OPENL
CLOSL    CLOSE (),MF=L                  CLOSE PARM LIST
CLOSLLEN EQU   *-CLOSL
*
BCDCB    DCB   DSORG=PS,MACRF=(GM),EODAD=BIEOF
BCDCBL   EQU   *-BCDCB
*
BITEXT   DS    CL255                    BROADCAST TEXT WORK AREA
*
WORKDEND DS    0D
WORKDLEN EQU   *-WORKD                  TOTAL LENGTH OF WORK-13 AREA
         EJECT
**********************************************************************
***                                                                ***
***   MAPPING DSECTS                                               ***
***                                                                ***
**********************************************************************
         CVT   DSECT=YES              , CVTMAP FOR IKJPARS
         IKJCPPL                        COMMAND PROCESSOR PARM LIST
         IKJPPL                         PARSE PARM LIST
         IKJIOPL                        I/O PARM LIST
         IKJPSCB                        PROTECTED STEP CONTROL BLOCK
TIOT     DSECT
         IEFTIOT1                       TASK I/O TABLE
*
         IKJEFFDF DFDSECT=YES,DFDSEC2=YES  MAPS DAIRFAIL CONTROL BLOCKS
         IEFZB4D0                       MAPS SVC99 CONTROL BLOCKS
*
         IHASDWA                      , SDWA FOR ESTAE EXIT
*
         IHADCB DSORG=PS,DEVD=DA        MAPS DCB
         EJECT
**********************************************************************
***                                                                ***
***   EQUATES                                                      ***
***                                                                ***
**********************************************************************
RBASE    EQU   12                       BASE REGISTER
RBASE2   EQU   11                       BASE REGISTER #2
RBAL     EQU   10                       BAL REGISTER
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
**********************************************************************
***                  ESTAE (ABEND) EXIT                            ***
***                                                                ***
***   This exit will receive control from RTM during ABEND         ***
***   processing.  Data areas available to this exit will be       ***
***   the System Diagnostic Work Area (SDWA) provided by the       ***
***   RTM, and the user parm list and the areas it points to       ***
***   which are provided by the main routine.                      ***
***                                                                ***
***   This exit will format an ABEND message, and write it         ***
***   with a WTO.  It will then return to RTM.                     ***
***                                                                ***
***   The register save area used by this exit has already been    ***
***   obtained by a GETMAIN by the main program, prior to the      ***
***   execution of the ESTAE macro.                                ***
***                                                                ***
***   This exit is entered by RTM with standard MVS linkage        ***
***   conventions.  Upon entry, the following relationships        ***
***   exist:                                                       ***
***                                                                ***
*** R1              SDWA (IHASDWA)   ESTUSRPL                      ***
*** +-----------+   +------------+   +-----------+                 ***
*** |           |==>| SDWAPARM   |==>| ESURETA   |==> Return point ***
*** +-----------+   +------------+   +-----------+                 ***
***                 | SDWAABCC   |   | ESUWRKA   |==> ESUWKD       ***
***                 +------------+   +-----------+   +-----------+ ***
***                 |            |                   | Save area | ***
***                ---          ---                 ---         ---***
***                ---          ---                 ---         ---***
***                 |            |                   |           | ***
***                 +------------+                   +-----------+ ***
***                 | SDWAGR15   |                   | Other work| ***
***                 +------------+                   |   areas   | ***
***                 |            |                   +-----------+ ***
***                ---          ---                                ***
***                ---          ---                                ***
***                 |            |                                 ***
***                 +------------+                                 ***
***                                                                ***
**********************************************************************
         EJECT
**********************************************************************
***                                                                ***
***          SEE DOCUMENTATION ON PREVIOUS PAGE                    ***
***                                                                ***
**********************************************************************
ESTXIT   CSECT
         STM   R14,R12,12(R13)          SAVE REGS INTO CALLER'S S.A.
         LR    R12,R15                  R12 HAS BASE ADDRESS
         USING ESTXIT,R12                 ADDRESSABILITY
         LR    R11,R1                   R11 POINTS TO SDWA
         USING SDWA,R11                   ADDRESSABILITY
         L     R10,SDWAPARM             R10 POINTS TO USER PARM LIST
         USING ESTUSRPL,R10               ADDRESSABILITY
         L     R15,ESUSRWKA             R15 POINTS TO USER WORK AREA
         ST    R13,4(0,R15)             CALLER'S S.A. ADDR TO OWN S.A.
         ST    R15,8(0,R13)             OWN S.A. ADDR TO CALLER'S S.A.
         LR    R13,R15                  R13 POINTS TO OWN S.A.
         USING ESUWKD,R13                 ADDRESSABILITY
*
ESTMESSG BAL   R9,ESMSG                 FORMAT AND WRITE A MESSAGE
*
         PRINT GEN
         L     R4,ESURETA               R4 CONTAINS RETURN ADDRESS
ESTSETRP SETRP WKAREA=(R11),RC=4,RETADDR=(R4),RETREGS=YES,FRESDWA=YES
         PRINT NOGEN
*
ESTEND   XR    R15,R15                  CLEAR RETURN CODE
         L     R13,4(0,R13)             RESTORE R13, PNT TO CALLER'S SA
         LM    R0,R12,20(R13)           RESTORE R0-R12 FROM CALLER'S SA
         L     R14,12(0,R13)            RESTORE R14 FROM CALLER'S S.A.
         BR    R14                      RETURN
         EJECT
**********************************************************************
***                                                                ***
***                  WRITE ABEND MESSAGE                           ***
***                                                                ***
**********************************************************************
ESMSG    ST    R9,ESUBALS1              SAVE BAL REGISTER
*
         MVC   WTOL(WTOLLEN),WTOD       INITIALIZE WTO PARM LIST
*
ESSYSCDE L     R3,SDWAABCC              R3 HAS ABEND CODES
         SLL   R3,8                     LEFT JUSTIFY SYSTEM CODE
         LA    R4,WTOL+26               R4 POINTS INTO MESSAGE
         LA    R5,3                     R5 COUNTER SET TO 3
         BAL   R9,ESHEX                 CONVERT TO DISPLAYABLE HEX
*
ESR15CDE L     R3,SDWAGR15              R3 HAS ABEND R15 CONTENTS
         LA    R4,WTOL+42               R4 POINTS INTO MESSAGE
         LA    R5,8                     R5 COUNTER SET TO 8
         BAL   R9,ESHEX                 CONVERT TO DISPLAYABLE HEX
*
ESUSRCDE L     R3,SDWAABCC              R3 HAS ABEND CODES
         N     R3,ESXFFF                R3 HAS ONLY USER CODE
         CVD   R3,ESUWKDBL              CONVERT TO DECIMAL
         OI    ESUWKDBL+7,X'0F'         KILL THE SIGN
         UNPK  WTOL+33(4),ESUWKDBL(8)   UNPACK INTO MESSAGE
*
ESWTO    WTO   MF=(E,WTOL)              WRITE THE MESSAGE
*
ESMSGEND L     R9,ESUBALS1              RESTORE THE BAL REGISTER
         BR    R9                       RETURN
*
*
ESHEX    XR    R2,R2                    CLEAR EVEN REG OF EVEN/ODD
         SLDL  R2,4                     R2 RECEIVES ONE HEX DIGIT
         LA    R4,1(0,R4)               R4 POINTS TO NEXT MESSAGE CHAR
         LA    R2,ESHEXTBL(R2)          R2 POINTS TO CHAR IN TABLE
         MVC   0(1,R4),0(R2)            MOVE CHAR FROM TABLE TO MESSAGE
         BCT   R5,ESHEX                 LOOP BACK UP
         BR    R9                       RETURN
*
         EJECT
**********************************************************************
***                                                                ***
***          DATA AREAS AND DSECTS FOR ESTAE EXIT                  ***
***                                                                ***
**********************************************************************
ESHEXTBL DC    CL16'0123456789ABCDEF'   TRANSLATE TABLE
         DS    0F                       ALIGN
ESXFFF   DC    X'00000FFF'              MASK TO CLEAR BITS 0-19
WTOD     WTO   '*** ABEND ***  Codes: SXXX, U9999, R15=XXXXXXXX',      X
               ROUTCDE=(11),DESC=(7),MF=L
*
*
*
ESTUSRPL DSECT                          USER PARM LIST
ESURETA  DS    A                          RETURN ADDRESS FOR RTM
ESUSRWKA DS    A                          WORK AREA ADDRESS FOR ME
*
*
*
ESUWKD   DSECT                          USER WORK AREA
ESUWKSAV DS    18F                        REGISTER SAVE AREA
ESUWKDBL DS    D                          DOUBLE WORD FOR UNPACK
ESUBALS1 DS    F                          BAL REGISTER SAVE AREA
WTOL     WTO   '.+....1....+....2....+....3....+....4....+....5',      X
               ROUTCDE=(11),DESC=(7),MF=L
WTOLLEN  EQU   *-WTOL                     LENGTH OF WTO PARM LIST
*
         END
