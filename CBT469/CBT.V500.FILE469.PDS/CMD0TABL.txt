CMD0TABL TITLE ' VIEW LIST OF CURRENT SYSTEM COMMANDS'
CMD0TABL CSECT
***********************************************************************
*        STANDARD ENTRY CODE FOR A RE-ENTRANT PROGRAM
*                 REGISTER 12 IS ONLY BASE REGISTER
***********************************************************************
         SPACE
         USING *,15                     TEMPORARY BASE REGISTER
         B     PASSLIT                  BRANCH AROUND LITERALS
         DC    C'CMD0TABL'              CSECT NAME
         DC    C'09/03/85 '             DATE WRITTEN
         DC    C'VERSION 3.0 '          VERSION NUMBER
         DC    C'&SYSDATE '             DATE ASSEMBLED
         DC    C'&SYSTIME '             TIME ASSEMBLED
PASSLIT  DS    0H
         STM   14,12,12(13)             SAVE CALLERS REGISTERS
         LR    11,1                     SAVE PARAMETERS PASSED
         LR    12,15                    REGISTER 12 IS BASE REGISTER
         DROP  15                       END TEMPORARY ADDRESSING
         USING CMD0TABL,12              PERMANENT ADDRESSING
         SPACE
         GETMAIN R,LV=WSLEN,SP=0        GET WORKING STORAGE AREA
         SPACE
         ST    1,8(13)                  SET FORWARD CHAIN IN SAVEAREA
         ST    13,4(1)                  SET BACKWARD CHAIN
         LM    13,1,8(13)               RESTORE CLOBBERED REGISTERS
         USING WORKSTOR,13              SETUP WORKING STORAGE
         SPACE 3
         LA    14,COPY
         LA    15,COPYLEN
         LA    0,PROT
         LA    1,PROTLEN
         MVCL  14,0
         MVC   PANELID,=CL8'CMDB'
         SPACE 3
         ISPF  LOAD,ISPEXEC=(YES,ISPEXEC)
         SPACE
         MVC   COMMENT(160),SPACES
         VDEFINE '(PANELID)',PANELID,CHAR,8,MF=E
         SPACE
         VDEFINE '(UMSG1)',UMSG1,CHAR,24,MF=E
         SPACE
         VDEFINE '(LMSG1)',LMSG1,CHAR,78,MF=E
         SPACE
         VDEFINE '(TYPE)',TYPE,CHAR,2,MF=E
         SPACE
         VDEFINE '(COMMENT COMMEN2)',COMMENT,CHAR,80,MF=E
         SPACE
         VDEFINE '(ZCMD)',ZCMD,CHAR,48,MF=E
         SPACE
         VDEFINE '(TBNAME PANEL MSG CSR AUTOSEL CTVERB)',              *
               TBNAME,CHAR,8,MF=E
         SPACE
         VDEFINE '(ROW ZTDTOP THECSR)',ROW,FIXED,4,MF=E
         SPACE
         VDEFINE '(LSEL)',LSEL,CHAR,1,MF=E
         SPACE
         ISPEXEC ,'LMINIT DATAID(TBNAME) DDNAME(ISPPROF)'
         SPACE
         ISPEXEC ,'LMFREE DATAID(&&TBNAME)'
         SPACE
         ISPEXEC ,'TBCREATE &&TBNAME NOWRITE KEYS(ZCTVERB) NAMES(ZCTACT*
                ZCTDESC TYPE) REPLACE'
         SPACE
         ISPEXEC ,'TBSORT &&TBNAME FIELDS(ZCTVERB)'
         SPACE
         ISPEXEC ,'TBSTATS &&ZAPPLID.CMDS STATUS2(TYPE)'
         SPACE
         CLI   TYPE,C'1'
         BE    ISP
         SPACE
         MVC   TYPE,=CL2' *'
         MVC   COMMENT(4),=CL4'$ *¢'
         MVC   COMMEN2(4),=CL4'$**¢'
         ISPEXEC ,'TBQUERY &&ZAPPLID.CMDS POSITION(CRP)'
         SPACE
         ISPEXEC ,'TBTOP &&ZAPPLID.CMDS'
         SPACE
LOOPAPPL DS    0H
         ISPEXEC ,'TBSKIP &&ZAPPLID.CMDS'
         SPACE
         LTR   15,15
         BNZ   RESAPPL
         ISPEXEC ,'TBADD &&TBNAME ORDER'
         SPACE
         B     LOOPAPPL
RESAPPL  DS    0H
         ISPEXEC ,'TBSKIP &&ZAPPLID.CMDS NUMBER(&&CRP)'
         SPACE 3
ISP      DS    0H
         MVC   TYPE,SPACES
         ISPEXEC ,'TBQUERY ISPCMDS POSITION(CRP)'
         SPACE
         ISPEXEC ,'TBTOP ISPCMDS'
         SPACE
LOOPISP  DS    0H
         ISPEXEC ,'TBSKIP ISPCMDS'
         SPACE
         LTR   15,15
         BNZ   RESISP
         ISPEXEC ,'TBADD &&TBNAME ORDER'
         SPACE
         LTR   15,15
         BZ    LOOPISP
         TBGET TBNAME,MF=E
         SPACE
         MVC   TYPE,=CL2'**'
         ISPEXEC ,'TBPUT &&TBNAME ORDER'
         SPACE
         MVC   TYPE,SPACES
         B     LOOPISP
         SPACE 3
RESISP   DS    0H
         ISPEXEC ,'TBSKIP ISPCMDS NUMBER(&&CRP)'
         EJECT
         MVC   ZTDTOP,=F'1'
         MVC   ROW,=F'0'
         MVC   PANEL(32),SPACES
         MVC   PANEL,PANELID
LOOP     TBTOP TBNAME,MF=E
         SPACE
         TBSKIP TBNAME,NUMBER=ZTDTOP,MF=E
         SPACE
         TBDISPL TBNAME,PANEL=PANEL,                                   *
               MSG=MSG,                                                *
               CURSOR=CSR,                                             *
               CSRROW=ROW,                                             *
               AUTOSEL=AUTOSEL,MF=E
         SPACE
         LR    10,15
         MVC   ROW,=F'0'
         MVC   PANEL(32),SPACES
         MVC   PANEL,PANELID
         C     10,=F'4'
         BH    EXITPGM
         CLC   ZCMD,SPACES
         BE    NOERR
         MVC   MSG,=CL8'ISPZ001'
         MVC   PANEL,SPACES
         B     LOOP
NOERR    DS    0H
         TBQUERY TBNAME,POSITION='THECSR',MF=E
         SPACE
         CLC   THECSR,=F'0'
         BE    ENDLOOP
         SPACE
         CLI   LSEL,C'S'
         BNE   ENDLOOP
         SPACE
         CONTROL DISPLAY,SAVE
         SPACE
         OPEN  (ISPPLIB,INPUT),MF=(E,OPENLIST)
         SPACE
         MVC   BLDLFF,=Y(1)
         MVC   BLDLLL,=Y(12)
         MVC   BLDLNAME,CTVERB
         BLDL  ISPPLIB,BLDLAREA
         SPACE
         C     15,=F'0'
         BNE   NOTFOUND
         SELECT ,'PGM(ISPTUTOR) PARM(&&CTVERB)'
         SPACE
         MVC   CSR,=CL8'LSEL'
         MVC   ROW,THECSR
         MVC   AUTOSEL,=CL8'NO'
         SPACE
ENDBLDL  DS    0H
         CLOSE (ISPPLIB),MF=(E,OPENLIST)
         SPACE
         CONTROL DISPLAY,RESTORE
         SPACE
ENDLOOP  DS    0H
         C     10,=F'4'
         BNE   LOOP
         MVC   PANEL,=CL8' '
         B     LOOP
         SPACE
NOTFOUND DS    0H
         MVC   MSG,=CL8'DTSM001'
         MVC   CSR,=CL8'LSEL'
         MVC   PANEL,SPACES
         MVC   ROW,THECSR
         CLOSE (ISPPLIB),MF=(E,OPENLIST)
         SPACE
         CONTROL DISPLAY,RESTORE
         SPACE
         B     LOOP
         SPACE 3
EXITPGM  DS    0H
         TBEND TBNAME,MF=E
         SPACE
         VRESET ,
         ISPF  DELETE,ISPEXEC=YES
         SPACE
         LR    1,13                     GET ADDRESS FOR FREEMAIN
         L     13,WORKSTOR+4            GET CALLERS SAVE AREA
         FREEMAIN R,A=(1),LV=WSLEN,SP=0 FREE WORKING STORAGE
         SPACE
         SR    15,15                    GET RETURN CODE
         L     14,12(,13)               RESTORE RETURN ADDRESS
         LM    0,12,20(13)              RESTORE REMAINING REGISTERS
         MVI   12(13),X'FF'             FLAG AS RETURNED FOR SNAP SVC
         BR    14                       RETURN TO CALLER
         EJECT
SPACES   DC    CL160' '
UMSG1    DC    CL24'HELP NOT AVAILABLE'
LMSG1    DC    CL78'THERE IS NOT HELP STORED FOR THIS COMMAND'
WORKSTOR DSECT                          WORKING STORAGE DSECT
SAVEAREA DS    18F                      PROGRAM SAVE AREA
         DS    0D                       DOUBLE WORD ALIGN
         ISPF  PARMS,ISPEXEC=YES
         SPACE
TYPE     DS    CL2
COMMENT  DS    CL80
COMMEN2  DS    CL80
ZCMD     DS    CL48
TBNAME   DS    CL8
PANEL    DS    CL8
MSG      DS    CL8
CSR      DS    CL8
AUTOSEL  DS    CL8
CTVERB   DS    CL8
LSEL     DS    CL1
ROW      DS    F
ZTDTOP   DS    F
THECSR   DS    F
BLDLAREA DS    0D
BLDLFF   DS    Y
BLDLLL   DS    Y
BLDLNAME DS    CL8
BLDLTTR  DS    CL3
BLDLK    DS    CL1
PANELID  DS    CL8
         SPACE
COPY     DS    0D
         ORG   *+PROTLEN
COPYLEN  EQU   *-COPY
         SPACE
         DS    0D
WSLEN    EQU   *-WORKSTOR               LENGTH OF WORKING STORAGE
CMD0TABL CSECT ,                        RESUME CSECT
         SPACE
PROT     DS    0D
         PRINT NOGEN
ISPPLIB  EQU   *-PROT+COPY
ISPPLIB@ DCB   DDNAME=ISPPLIB,MACRF=R,DSORG=PO
         PRINT GEN
         SPACE
OPENLIST EQU   OPENLIS@-PROT+COPY
OPENLIS@ OPEN  (0),MF=L
         SPACE
PROTLEN  EQU   *-PROT
         END   CMD0TABL
