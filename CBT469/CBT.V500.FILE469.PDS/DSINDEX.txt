         TITLE '                           D A T A   S E T   I N D E X'
***********************************************************************
***                                                                 ***
**  PROGNAME: DSINDEX                                                **
**  PROGAUTH: DAVID ALCOCK                                           **
**  PROGDATE: 04/14/85                                               **
**                                                                   **
**      THIS ISPF DIALOG ALLOWS A USER TO BROWSE OR EDIT DATA SETS   **
**  FROM AN INDEX LIST.  THE USER IS FIRST PROMPTED FOR AN INDEX     **
**  LEVEL.  THE PROGRAM WILL THEN SEARCH THROUGH THE CATALOG/CVOL    **
**  TO FIND ENTRIES THAT MATCH THE SPECIFIED INDEX.  THE USER CAN    **
**  THEN EDIT OR BROWSE A DATASET BY SELECTING IT FROM THE LIST.     **
**                                                                   **
**  EXTERNAL POINTS:                                                 **
**           PROGRAM: LOCINDEX    *CVOL SEARCH*                      **
**                    MINIXDMP    *MINI HEX DUMP*                    **
**           PANELS:  DSINDX01    *POM*                              **
**                    DSINDX02    *DATASET LIST*                     **
**                    DSINDX03    *COMFIRM DATASET DELETE*           **
**                    DSINDX04    *DATASET DSCB DISPLAY*             **
***                                                                 ***
***********************************************************************
         SPACE 2
T_CON    EQU  CPYAREAL
T_VAR    EQU  CPYTOL
         EJECT
DSINDEX  ENTER 'EDIT/BROWSE FROM INDEX LIST ... DAVID ALCOCK',         @
               RENT=YES,              INDICATE WE WANT GETMAIN         @
               LV=WORKDSL,            LENGTH TO GETMAIN                @
               BASE=(12,11,10)        GRAP SOME BASE REGISTERS
         USING WORKDS,R13             ADDRESSABILITY TO WORK AREA
         PRINT NOGEN
         EJECT
***********************************************************************
***                                                                 ***
**       INITIALIZATION SECTION                                      **
***                                                                 ***
***********************************************************************
         SPACE 2
*
**  CLEAR GETMAINED WORK AREA TO BINARY ZEROS (EXCEPT REGISTERS)
*
         LA    R2,WORKDS+72             GET ADDR OF WORK AREA+RSA
         LA    R3,WORKDSL-72            GET WORK AREA LENGTH-RSA
         SLR   R5,R5                    SET LENGTH AND PAD TO ZERO
         MVCL  R2,R4                    CLEAR WORK AREA TO ZEROS
*
**      COPY CONTANTS TO GETMAINED WORKAREA
*
         LA    R2,CPYTO                    GET ADDRESS TO INITALIZE
         LA    R3,CPYAREAL                 GET LENGTH OF CPY AREA
         LA    R4,CPYAREA                  LOCATE CONTANTS
         LR    R5,R3                       GET LENGTH OF CPY AREA
         MVCL  R2,R4                       COPY TO WORK AREA
*
**  ISSUE START MESSAGE
*
         MVC   LOGMSG1(8),=CL8'DSI@001A'
         MVI   LOGMSG2,X'00'
         MVC   FUNCT(8),=CL8'STARTED-'
         BAL   R9,LOGMSG              ISSUE MESSAGE
*
** GET VARIABLES FROM PROFILE BEFORE DISPLAYING SCREEN
*
         CALL  ISPLINK,                                                @
               (=CL8'VGET',                                            @
               =C'(DSILVL DSIVOL DSITAPE DSIDASD)',                    @
               =CL8'PROFILE'),                                         @
               VL,MF=(E,PARMLIST)
         EJECT
***********************************************************************
***                                                                 ***
**       DISPLAY FIRST PANEL                                         **
***                                                                 ***
***********************************************************************
         SPACE 2
DISPPOM  EQU   *
*
**  DISPLAY FIRST PANEL
*
         CALL  ISPLINK,                                                @
               (=CL8'DISPLAY',=CL8'DSINDX01'),                         @
               VL,MF=(E,PARMLIST)
         LTR   R15,R15            SEE IF END KEY WAS HIT
         BZ    EPNL000            NO, CARRY ON
*
**  TERMINATE: END KEY HIT FROM POM (PRIMARY OPTION MENU)
*
         MVC   LOGMSG1(8),=CL8'DSI@001A'
         MVI   LOGMSG2,X'00'
         MVC   FUNCT(8),=CL8'ENDED---'
         BAL   R9,LOGMSG              ISSUE MESSAGE
         PRINT GEN
         LEAVE RC=0
         PRINT NOGEN
*
**  GET VALUES FROM ISPF INTO OUR PROGRAM DATA AREAS
*
EPNL000  EQU   *
         LA    R15,44
         ST    R15,DSILVLL
         CALL  ISPLINK,                                                @
               (=CL8'VCOPY',=C'(DSILVL)',DSILVLL,DSILVL,=CL8'MOVE'),   @
               VL,MF=(E,PARMLIST)
         LA    R15,6
         ST    R15,DSIVOLL
         CALL  ISPLINK,                                                @
               (=CL8'VCOPY',=C'(DSIVOL)',DSIVOLL,DSIVOL,=CL8'MOVE'),   @
               VL,MF=(E,PARMLIST)
         LA    R15,3
         ST    R15,DSITAPEL
         CALL  ISPLINK,                                                @
               (=CL8'VCOPY',                                           @
               =C'(DSITAPE)',DSITAPEL,DSITAPE,=CL8'MOVE'),             @
               VL,MF=(E,PARMLIST)
         LA    R15,3
         ST    R15,DSIDASDL
         CALL  ISPLINK,                                                @
               (=CL8'VCOPY',                                           @
               =C'(DSIDASD)',DSIDASDL,DSIDASD,=CL8'MOVE'),             @
               VL,MF=(E,PARMLIST)
         EJECT
***********************************************************************
***                                                                 ***
**       FIGURE OUT WHAT WE ARE GONNA DO                             **
***                                                                 ***
***********************************************************************
         SPACE 2
*
** EITHER DASD OR TAPE MUST BE YES
*
         CLC   DSITAPE(3),=CL3'YES'
         BE    INIT000
         CLC   DSIDASD(3),=CL3'YES'
         BE    INIT000
         BAL   R14,CLRMSG                  SET MESSAGES TO BLANKS
         MVC   DSIMSGS(21),=C'CONFLICT AT TAPE/DASD'
         MVC   DSIMSGL(31),=C'MUST HAVE A YES IN TAPE OR DASD'
         LA    R0,21
         LA    R1,31
         MVC   MSGID(8),=CL8'DSI@000A'
         BAL   R9,SETMSG
         B     DISPPOM
INIT000  EQU   *
* I WAS GOING TO ADD A "READVTOC" ROUTINE BUT WE ARE ABOUT TO GET
* ISPF VERSION 2 WHICH OBSOLETES MOST OF THIS PROGRAM.
         B     READCVOL
         EJECT
***********************************************************************
***                                                                 ***
**       DISPLAY THE DSNAME TABLE                                    **
***                                                                 ***
***********************************************************************
         SPACE 2
DSNLOOP  EQU   *
         MVC   TSEL(7),=CL7' '            SET TO BLANKS
         CLI   MSGID,X'00'                IS THERE A MESSAGE WAITING?
         BE    DSNLOOPN                   NO, MOVE ON
         CLI   MSGID,C' '                 IS THERE A MESSAGE WAITING?
         BE    DSNLOOPN                   NO, MOVE ON
         CALL  ISPLINK,                                                @
               (=CL8'TBDISPL',=CL8'DSINDEX',=CL8'DSINDX02',MSGID),     @
               VL,MF=(E,PARMLIST)
         MVI   MSGID,X'00'
         B     DSNLOOPR
DSNLOOPN EQU   *
         CALL  ISPLINK,                                                @
               (=CL8'TBDISPL',=CL8'DSINDEX',=CL8'DSINDX02'),           @
               VL,MF=(E,PARMLIST)
DSNLOOPR EQU   *
         LTR   R15,R15                 DID USER HIT THE END KEY
         BZ    SELKTDSN                NO, HE SELECTED A DATASET
         C     R15,=F'4'               DID HE SELECT MORE THAN 1
         BE    SELKTDSN                YES, MOVE ON
         CALL  ISPLINK,                                                @
               (=CL8'TBEND',=CL8'DSINDEX'),                            @
               VL,MF=(E,PARMLIST)
         B     DISPPOM
*
SELKTDSN EQU   *
         BAL   R9,ROWPROC                 GET ISPF DATA FOR ROW
*
*        CHECK FOR VALID INPUT
*
         OC    TSEL(7),=CL7' '            CONVERT TO UPPERCASE
         CLC   TSEL(3),=CL3'DEL'
         BE    DELETE
         CLC   TSEL(5),=CL5'UNCAT'
         BE    UNCAT
         CLC   TSEL(7),=CL7'SCRATCH'
         BE    SCRATCH
         CLI   TSEL,C'?'
         BE    GETDSCB
         CLI   TSEL,C'S'
         BE    BROWSEIT
         CLI   TSEL,C'E'
         BE    EDIT
         CLC   TSEL(4),=CL4'EDIT'
         BE    EDIT
         CLI   TSEL,C'B'
         BE    BROWSEIT
         CLC   TSEL(6),=CL6'SELECT'
         BE    BROWSEIT
*
*        IF TSEL HAS SOMETHING, THAT SOMETHING IS INVALID
*
         L     R1,TSELL                   GET LENGTH
         LTR   R1,R1                      CHECK LENGTH
         BNZ   INVALIDS                   NOT ZERO, WHATS UP DOC
         B     CHKCMDL                    OTHERWISE, CHECK COMMAND LINE
*
*        INVALID SELECTION FOUND
*
INVALIDS EQU   *
         BAL   R14,CLRMSG                  SET MESSAGES TO BLANKS
         MVC   DSIMSGS(22),=C'INVALID SELECTION CODE'
         MVC   DSIMSGL(19),=C'INVALID SELECTION:'
         MVC   DSIMSGL+21(4),TSEL
         LA    R0,22
         LA    R1,30
         MVC   MSGID(8),=CL8'DSI@000A'
         BAL   R9,SETMSG
         B     DSNLOOP                               TRY AGAIN
         EJECT
***********************************************************************
***                                                                 ***
**       EDIT THE DATASET                                            **
***                                                                 ***
***********************************************************************
EDIT     EQU   *
         CLI   TYPE,C'T'
         BE    EDITERR
*
**   PUT TICKS AROUND DATASET NAME
*
         BAL   R9,DSNAMEED
*
**   TURN OFF ISPF ERROR HANDLING, SO THAT WE HAVE CONTROL
*
         CALL  ISPLINK,                                                @
               (=CL8'CONTROL',=CL8'ERRORS',=CL8'RETURN'),              @
               VL,MF=(E,PARMLIST)
*
**   EDIT THE DATASET
*
         CALL  ISPLINK,                                                @
               (=CL8'EDIT',EDDSN),                                     @
               VL,MF=(E,PARMLIST)
         C     R15,=F'5'
         BL    EDIT000
         MVC   MSGID(8),=CL8'DSI@003A'   TURN ON IBM MESSAGS
         BAL   R9,SETMSGI                IBM ISPF MESSAGES
EDIT000  EQU   *
*
**   TURN ON ISPF ERROR HANDLING
*
         CALL  ISPLINK,                                                @
               (=CL8'CONTROL',=CL8'ERRORS',=CL8'CANCEL'),              @
               VL,MF=(E,PARMLIST)
         MVC   LASTA(11),=CL11'EDITED'
EDITX    EQU   *
         LA    R15,11
         ST    R15,LASTAL
         CALL  ISPLINK,                                                @
               (=CL8'TBMOD',=CL8'DSINDEX'),                            @
               VL,MF=(E,PARMLIST)
*
**   LOG MESSAGE
*
         MVC   FUNCT(8),=CL8'EDIT----'
         MVC   LOGMSG1(8),=CL8'DSI@002C'
         MVI   LOGMSG2,X'00'
         BAL   R9,LOGMSG              ISSUE MESSAGE
         MVC   LOGMSG1(8),=CL8'DSI@002A'
         MVC   LOGMSG2(8),=CL8'DSI@002B'
         B     DSNLOOP                 GO BACK TO DSN SELECTION LIST
EDITERR  EQU   *
         MVC   MSGID(8),=CL8'DSI@000A'
         MVC   LASTA(11),=CL11'¬ EDITED'
         MVC   DSIMSGS(18),=C'CAN NOT EDIT TAPES'
         MVC   DSIMSGL(31),=C'ISPF CAN NOT EDIT TAPE DATASETS'
         LA    R0,18
         LA    R1,31
         BAL   R9,SETMSG
         B     EDITX
         EJECT
***********************************************************************
***                                                                 ***
**       BROWSE THE DATASET                                          **
***                                                                 ***
***********************************************************************
BROWSEIT EQU   *
         CLI   TYPE,C'T'
         BE    BROWSEER
*
**   PUT TICKS AROUND DATASET NAME
*
         BAL   R9,DSNAMEED
*
**   TURN OFF ISPF ERROR HANDLING, SO THAT WE HAVE CONTROL
*
         CALL  ISPLINK,                                                @
               (=CL8'CONTROL',=CL8'ERRORS',=CL8'RETURN'),              @
               VL,MF=(E,PARMLIST)
*
**   BROWSE THE DATASET
*
         CALL  ISPLINK,                                                @
               (=CL8'BROWSE',EDDSN),                                   @
               VL,MF=(E,PARMLIST)
         C     R15,=F'5'
         BL    BROWSE0
         MVC   MSGID(8),=CL8'DSI@003A'   TURN ON IBM MESSAGS
         BAL   R9,SETMSGI                IBM ISPF MESSAGES
BROWSE0  EQU   *
*
**   TURN ON ISPF ERROR HANDLING
*
         CALL  ISPLINK,                                                @
               (=CL8'CONTROL',=CL8'ERRORS',=CL8'CANCEL'),              @
               VL,MF=(E,PARMLIST)
         MVC   LASTA(11),=CL11'BROWSED'
BROWSEX  EQU   *
         LA    R15,11
         ST    R15,LASTAL
         CALL  ISPLINK,                                                @
               (=CL8'TBMOD',=CL8'DSINDEX'),                            @
               VL,MF=(E,PARMLIST)
*
**   LOG MESSAGE
*
         MVC   FUNCT(8),=CL8'BROWSE--'
         MVC   LOGMSG1(8),=CL8'DSI@002C'
         MVI   LOGMSG2,X'00'
         BAL   R9,LOGMSG              ISSUE MESSAGE
         MVC   LOGMSG1(8),=CL8'DSI@002A'
         MVC   LOGMSG2(8),=CL8'DSI@002B'
         B     DSNLOOP                 GO BACK TO DSN SELECTION LIST
BROWSEER EQU   *
         MVC   MSGID(8),=CL8'DSI@000A'
         MVC   LASTA(11),=CL11'¬ BROWSED'
         MVC   DSIMSGS(20),=C'CAN NOT BROWSE TAPES'
         MVC   DSIMSGL(33),=C'ISPF CAN NOT BROWSE TAPE DATASETS'
         LA    R0,20
         LA    R1,33
         BAL   R9,SETMSG
         B     BROWSEX
         EJECT
         EJECT
***********************************************************************
***                                                                 ***
**       GET DSCB AND THEN DISPLAY INFO                              **
***                                                                 ***
***********************************************************************
         SPACE 2
GETDSCB  EQU   *
         CLI   TYPE,C'T'                  TAPE DATASET?
         BE    DSNLOOP                    YES, FORGET IT
*
         MVC   FUNCT(8),=CL8'SHOW DS-'    FUNCTION
         MVC   LOGMSG1(8),=CL8'DSI@002C'
         MVI   LOGMSG2,X'00'              DISABLE SECOND MESSAGE
         BAL   R9,LOGMSG                  ISSUE MESSAGE
         MVC   LOGMSG1(8),=CL8'DSI@002A'
         MVC   LOGMSG2(8),=CL8'DSI@002B'
         BAL   R9,OBDSCB                  OBTAIN DSCB INFO
         BAL   R9,SHOWDSCB                SHOW DSCB INFO
         CALL  ISPLINK,                                                @
               (=CL8'DISPLAY',=CL8'DSINDX04'),                         @
               VL,MF=(E,PARMLIST)
         MVC   LASTA(11),=CL11'* DS INFO *'
GETDX    EQU   *
         LA    R15,11
         ST    R15,LASTAL
         CALL  ISPLINK,                                                @
               (=CL8'TBMOD',=CL8'DSINDEX'),                            @
               VL,MF=(E,PARMLIST)
         B     DSNLOOP                    GET NEXT COMMAND
         EJECT
***********************************************************************
***                                                                 ***
**       OBTAIN FORMAT1 DSCB                    .                    **
***                                                                 ***
***********************************************************************
         SPACE  2
OBDSCB   EQU   *
         LA    R1,DSNAME                  -----+
         ST    R1,FINDDSCB+4                   |
         LA    R1,VOLSER                       | SET UP DYNAMIC
         ST    R1,FINDDSCB+8                   | CAMLST PARM LIST
         LA    R1,AREADSCB                     |
         ST    R1,FINDDSCB+12             -----+
         OBTAIN FINDDSCB                  GET FORMAT 1 DSCB
         LTR   R15,R15                    WAS IT SUCESSFULL?
         BZ    OBDSCBX                    YEA, MOVE ON
         MVC   MSGID(8),=CL8'DSI@000C'
         MVC   LASTA(11),=CL11'¬ SHOWN'
         MVC   DSIMSGS(17),=C'CAN''T OBTAIN DSCB'
         MVC   DSIMSGL(25),=C'OBTAIN OF F1DSCB FAILED RC='
         LA    R6,DSIMSGL+25              LOCATE OUTPUT LOCATION
         LR    R0,R15                     GET RC
         BAL   R14,EDREG0                 CONVERT RC
         LA    R0,17                      LENGTH OF DSIMSGS
         LR    R1,R6                      GET END OF DSIMSGL
         LA    R15,DSIMSGL                GET BEGINNING OF DSIMSGL
         SR    R1,R15                     LENGTH OF DSIMSGL = END-BEG
         LA    R15,20                     SET NON-ZERO RETURN CODE
OBDSCBX  EQU   *
         BR    R9
         EJECT
***********************************************************************
***                                                                 ***
**       MODIFY DATASET: DELETE, SCRATCH UNCAT...                    **
***                                                                 ***
***********************************************************************
DELETE   EQU   *
         OI    EXECFLAG,$DELETE
SCRATCH  EQU   *
         MVC   FUNCT(8),=CL8'SCRATCH-'
         BAL   R9,OBDSCB
         LTR   R15,R15                    SUCCESSFULL?
         BZ    DELDSFND                   YES, MOVE ON
         MVC   LASTA(11),=CL11'¬ SCRATCHED'
         MVC   DSIMSGS(17),=C'DATASET NOT FOUND'
         MVC   DSIMSGL(31),=C'DATASET NOT ON CATLOGUED VOLSER'
         LA    R0,17
         LA    R1,31
         B     MODDSX
DELDSFND EQU   *
         BAL   R9,SHOWDSCB
         MVC   FUNCT(8),=CL8'SCRATCH'
         CALL  ISPLINK,                                                @
               (=CL8'DISPLAY',=CL8'DSINDX03'),                         @
               VL,MF=(E,PARMLIST)
         MVC   FUNCT(8),=CL8'SCRATCH-'
         LTR   R15,R15                    SEE IF END KEY WAS HIT
         BNZ   DELNO                      YES, DONT DELETE HIM
*
** SCRATCH THE DATASET
*
         MVC   VNUMB(2),=H'1'             SET NUMBER OF VOLUMES
         MVC   VDEVCODE(4),=X'30,30,20,0E' SET DEVICE CODE = 3380
* PLEASE NOTE THAT DEVICE CODE FOR 3380 IS HARD-CODED - X'30,30,20,0E'
* WE ARE A ALL 3380 SHOP, YOU CAN GET THIS INFO FROM THE UCB
* OR PUT IN IBM'S "SYSALLDA"
         XC    VSTATCDE(2),VSTATCDE       SET SCRATCH CODE
         LA    R1,DSNAME                  ------+
         ST    R1,SCRATCHD+4                    |  SET UP CAMLST
         LA    R1,VOLLIST                       |  PARMS
         ST    R1,SCRATCHD+12             ------+
         SLR   R0,R0                      INDICATE NO UCB
         SCRATCH SCRATCHD                 SCRATCH DATASET
         LTR   R15,R15                    WAS IT SUCESSFULL?
         BNZ   DELFAIL                    NO, ISSUE MESSAGE
         TM    EXECFLAG,$DELETE           IF WE WANT DELETE
         BO    UNCAT                      ..THEN WE NEED AN UNCATLG
         MVC   LASTA(11),=CL11'SCRATCHED'
         MVC   DSIMSGS(17),=C'DATASET SCRATCHED'
         MVC   DSIMSGL(17),=C'DATASET SCRATCHED'
         LA    R0,17
         LA    R1,17
         B     MODDSX             LEAVE
*
** DELETE OF DATASET FAILED
*
DELFAIL  EQU   *
         BAL   R14,CLRMSG
         MVC   LASTA(11),=CL11'¬ SCRATCHED'
         MVC   DSIMSGS(21),=C'DATASET NOT SCRATCHED'
         MVC   DSIMSGL(18),=C'NOT SCRATCHED, RC='
         LA    R6,DSIMSGL+18
         LR    R0,R15
         BAL   R14,EDREG0
         LA    R0,21              LENGTH OF DSIMSGS
         LA    R15,DSIMSGL        GET BEGINNING OF MESSAGE
         LR    R1,R6              GET END OF MESSAGE
         SR    R1,R15             LENGTH OF DSIMSGL = END - BEG
         B     MODDSX             LEAVE
*
** USER DID NOT WANT THE DATASET SCRATCHED...HE HIT THE END KEY
*
DELNO    EQU   *
         MVC   LASTA(11),=CL11'¬ SCRATCHED'
         MVC   DSIMSGS(21),=C'DATASET NOT SCRATCHED'
         MVC   DSIMSGL(21),=C'DATASET NOT SCRATCHED'
         LA    R0,21
         LA    R1,21
         B     MODDSX
*
** UNCATALOG DATASET
*
UNCAT    EQU   *
         MVC   FUNCT(8),=CL8'UNCATLG-'
         LA    R1,DSNAME                  --+ SET UP
         ST    R1,UNCATIT+4               --+ CAMLST PARMS
         CATALOG UNCATIT                  UNCATALOGUE IT
         LTR   R15,R15
         BZ    UNCATOK
         MVC   LASTA(11),=CL11'¬ UNCATLG'
         BAL   R14,CLRMSG
         TM    EXECFLAG,$DELETE
         BO    UNCATND
         MVC   DSIMSGS(18),=C'DATASET NOT UNCATLG'
         MVC   DSIMSGL(22),=C'UNCATALOG  FAILED, RC='
         LA    R6,DSIMSGL+23
         LR    R0,R15
         BAL   R14,EDREG0
         LA    R0,18
         LA    R15,DSIMSGL        GET BEGINNING OF MESSAGE
         LR    R1,R6              GET END OF MESSAGE
         SR    R1,R15             LENGTH OF DSIMSGL = END - BEG
         B     MODDSX             LEAVE
*
** SCRATCHED THE DATASET, BUT THE UNCATALOGUE FUNCTION FAILED
*
UNCATND  EQU   *
         MVC   DSIMSGS(22),=C'SCRTCHED, NOT UNCATLGD'
         MVC   DSIMSGL(28),=C'SCRATCHED, BUT NOT UNCAT, RC='
         LA    R6,DSIMSGL+28
         LR    R0,R15
         BAL   R14,EDREG0
         LA    R0,22
         LA    R15,DSIMSGL        GET BEGINNING OF MESSAGE
         LR    R1,R6              GET END OF MESSAGE
         SR    R1,R15             LENGTH OF DSIMSGL = END - BEG
         B     MODDSX             LEAVE
*
**       ISSUE MESSAGE FOR UNCATLG OF DATASET
*
UNCATOK  EQU   *
         TM    EXECFLAG,$DELETE
         BO    UNCATOKD
         MVC   LASTA(11),=CL11'UNCATLGED'
         MVC   DSIMSGS(19),=C'DATASET UNCATALOGED'
         LA    R0,19
         LA    R1,0
         B     MODDSX
*
**       ISSUE MESSAGE FOR DELETE OF DATASET
*
UNCATOKD EQU   *
         MVC   FUNCT(8),=CL8'DELETE--'
         MVC   LASTA(11),=CL11'DELETED'
         MVC   DSIMSGS(15),=C'DATASET DELETED'
         MVC   DSIMSGL(15),=C'DATASET DELETED'
         LA    R0,15
         LA    R1,15
         B     MODDSX
*
*        COMMON EXIT FOR SCRATCH, DELETE, UNCATALOGUE FUNCTIONS
*
MODDSX   EQU   *
         MVC   MSGID(8),=CL8'DSI@000C'
         BAL   R9,SETMSG
         BAL   R9,LOGMSG
         LA    R15,11
         ST    R15,LASTAL
         CALL  ISPLINK,                                                @
               (=CL8'TBMOD',=CL8'DSINDEX'),                            @
               VL,MF=(E,PARMLIST)
         NI    EXECFLAG,TURNOFF-$DELETE
         B     DSNLOOP
         EJECT
***********************************************************************
***                                                                 ***
**       SHOW DSCB                                                   **
***                                                                 ***
***********************************************************************
         SPACE 2
SHOWDSCB EQU   *
         ST    R9,S_DSCB                        SAVE RETURN ADDRESS
         LA    R2,FULLDSCB                      LOCATE DSCB WITH DSN
         SR    R0,R0                            CLEAR REG
         LH    R0,DS1LRECL-IECSDSF1(R2)         GET LRECL
         ST    R0,LRECL                         SAVE IT FOR ISPF
         SR    R0,R0                            CLEAR REG
         LH    R0,DS1BLKL-IECSDSF1(R2)          GET BLKSIZE
         ST    R0,BLKSZ                         SAVE IT FOR ISPF
*
** FORMAT OPTION BYTE
*
         TM    DS1DSIND-IECSDSF1(R2),DS1IND40
         BO    SDSCB000
         TM    DS1DSIND-IECSDSF1(R2),DS1IND40
         BO    SDSCB001
         MVC   OPTION(4),=CL4'NONE'
         B     SDSCB00X
SDSCB000 EQU   *
         MVC   OPTION(4),=CL4'RACF'
         B     SDSCB00X
SDSCB001 EQU   *
         MVC   OPTION(4),=CL4'PSWD'
         B     SDSCB00X
SDSCB00X EQU   *
*
** GET DATASET ORGANIZATION
*
         TM    DS1DSORG-IECSDSF1(R2),DS1DSGIS
         BO    SDSCB100
         TM    DS1DSORG-IECSDSF1(R2),DS1DSGPS
         BO    SDSCB101
         TM    DS1DSORG-IECSDSF1(R2),DS1DSGDA
         BO    SDSCB102
         TM    DS1DSORG-IECSDSF1(R2),DS1DSGPO
         BO    SDSCB103
         TM    DS1DSORG-IECSDSF1(R2),DS1DSGU
         BO    SDSCB104
         MVC   DSORG(4),=CL4'???'
         B     SDSCB10X
SDSCB100 EQU   *
         MVC   DSORG(4),=CL4'ISAM'
         B     SDSCB10X
SDSCB101 EQU   *
         MVC   DSORG(4),=CL4'PS'
         B     SDSCB10X
SDSCB102 EQU   *
         MVC   DSORG(4),=CL4'DA'
         B     SDSCB10X
SDSCB103 EQU   *
         MVC   DSORG(4),=CL4'PDS'
         B     SDSCB10X
SDSCB104 EQU   *
         MVC   DSORG(4),=CL4'UNMV'
         B     SDSCB10X
SDSCB10X EQU   *
*
** GET CREATION JOB,DATE / LAST REF JOB,DATE
*
         MVC   CJOB(5),DS1SYSCD+8-IECSDSF1(R2) GET CREATING*DMS*
         MVC   CJOB+5(3),DS1DSSN-IECSDSF1(R2)  ..JOBNAME   *DMS*
         LA    R1,DS1CREDT-IECSDSF1(R2)        --+
         LA    R15,CDATE                         | CREATION DATE
         BAL   R14,CONVDATE                    --+
         MVC   LJOB(8),DS1SYSCD-IECSDSF1(R2)   GET LAST MOD JOB
         LA    R1,DS1DSSN+3-IECSDSF1(R2)       --+
         LA    R15,LDATE                         | LAST MOD DATE
         BAL   R14,CONVDATE                    --+
         LA    R1,DS1EXPDT-IECSDSF1(R2)        --+
         LA    R15,XDATE                         | EXPIRATION DATE
         BAL   R14,CONVDATE                    --+
*
** DUMP DSCB (EXCLUDING DATASET NAME)
*
         MVC   MINIOFF(2),=H'44'                START AFTER DSNAME
         MVC   MINILEN(2),=H'16'                LENGTH TO DUMP
         LA    R7,AREADSCB                      INPUT: OBTAIN DSCB
         LA    R8,XLINE1                        OUTPUT: ISPF DATA AREA
         LA    R2,6                             NUMBER OF LINES
LOOPDSCB EQU   *
         MVI   0(R8),C' '                       GET BLANK
         MVC   1(64,R8),0(R8)                   ..PROPAGATE IT
         BAL   R9,DUMPIT                        HEX DUMP
         LA    R7,16(R7)                        BUMP TO NEXT 16 BYTES
         LA    R8,65(R8)                        BUMP TO NEXT LINE
         BCT   R2,LOOPDSCB                      DUMP 6 LINES
*
** DEFINE DEM GUYS TO ISPF
*
         LA    R15,4
         ST    R15,LRECLL
         CALL  ISPLINK,                                                @
               (=CL8'VDEFINE',=C'(LRECL)',LRECL,=CL8'FIXED',LRECLL),   @
               VL,MF=(E,PARMLIST)
         LA    R15,4
         ST    R15,BLKSZL
         CALL  ISPLINK,                                                @
               (=CL8'VDEFINE',=C'(BLKSZ)',BLKSZ,=CL8'FIXED',BLKSZL),   @
               VL,MF=(E,PARMLIST)
         LA    R15,4
         ST    R15,OPTIONL
         CALL  ISPLINK,                                                @
               (=CL8'VDEFINE',=C'(OPTION)',OPTION,=CL8'CHAR',OPTIONL), @
               VL,MF=(E,PARMLIST)
         LA    R15,4
         ST    R15,DSORGL
         CALL  ISPLINK,                                                @
               (=CL8'VDEFINE',=C'(DSORG)',DSORG,=CL8'CHAR',DSORGL),    @
               VL,MF=(E,PARMLIST)
         LA    R15,6
         ST    R15,CDATEL
         CALL  ISPLINK,                                                @
               (=CL8'VDEFINE',=C'(CDATE)',CDATE,=CL8'CHAR',CDATEL),    @
               VL,MF=(E,PARMLIST)
         LA    R15,8
         ST    R15,CJOBL
         CALL  ISPLINK,                                                @
               (=CL8'VDEFINE',=C'(CJOB)',CJOB,=CL8'CHAR',CJOBL),       @
               VL,MF=(E,PARMLIST)
         LA    R15,6
         ST    R15,LDATEL
         CALL  ISPLINK,                                                @
               (=CL8'VDEFINE',=C'(LDATE)',LDATE,=CL8'CHAR',LDATEL),    @
               VL,MF=(E,PARMLIST)
         LA    R15,8
         ST    R15,LJOBL
         CALL  ISPLINK,                                                @
               (=CL8'VDEFINE',=C'(LJOB)',LJOB,=CL8'CHAR',LJOBL),       @
               VL,MF=(E,PARMLIST)
         LA    R15,6
         ST    R15,XDATEL
         CALL  ISPLINK,                                                @
               (=CL8'VDEFINE',=C'(XDATE)',XDATE,=CL8'CHAR',XDATEL),    @
               VL,MF=(E,PARMLIST)
         LA    R15,65
         ST    R15,XLINE1L
         CALL  ISPLINK,                                                @
               (=CL8'VDEFINE',=C'(XLINE1)',XLINE1,=CL8'CHAR',XLINE1L), @
               VL,MF=(E,PARMLIST)
         LA    R15,65
         ST    R15,XLINE2L
         CALL  ISPLINK,                                                @
               (=CL8'VDEFINE',=C'(XLINE2)',XLINE2,=CL8'CHAR',XLINE2L), @
               VL,MF=(E,PARMLIST)
         LA    R15,65
         ST    R15,XLINE3L
         CALL  ISPLINK,                                                @
               (=CL8'VDEFINE',=C'(XLINE3)',XLINE3,=CL8'CHAR',XLINE3L), @
               VL,MF=(E,PARMLIST)
         LA    R15,65
         ST    R15,XLINE4L
         CALL  ISPLINK,                                                @
               (=CL8'VDEFINE',=C'(XLINE4)',XLINE4,=CL8'CHAR',XLINE4L), @
               VL,MF=(E,PARMLIST)
         LA    R15,65
         ST    R15,XLINE5L
         CALL  ISPLINK,                                                @
               (=CL8'VDEFINE',=C'(XLINE5)',XLINE5,=CL8'CHAR',XLINE5L), @
               VL,MF=(E,PARMLIST)
         LA    R15,65
         ST    R15,XLINE6L
         CALL  ISPLINK,                                                @
               (=CL8'VDEFINE',=C'(XLINE6)',XLINE6,=CL8'CHAR',XLINE6L), @
               VL,MF=(E,PARMLIST)
*
         L     R9,S_DSCB                        GET RETURN ADDRESS
         BR    R9
         EJECT
***********************************************************************
***                                                                 ***
**       CHECK DATASET AGAINST SEARCH ARG.                           **
***                                                                 ***
***********************************************************************
         SPACE 2
* NOTE: I WAS GOING TO PUT A "GENERIC" SEARCH PATTERN CHECKING ROUTINE
*       HERE BUT NEVER GOT AROUND TO IT.
CHKDSN   EQU   *
         LA    R7,1(R7)
         ST    R7,ROWNUM
         SR    R15,R15
         BR    R9
         EJECT
***********************************************************************
***                                                                 ***
**       GET COMMAND LINE                                            **
***                                                                 ***
***********************************************************************
         SPACE 2
CHKCMDL  EQU   *
         LA    R15,79
         ST    R15,ZCMDL
         CALL  ISPLINK,                                                @
               (=CL8'VCOPY',=C'(ZCMD)',ZCMDL,ZCMD,=CL8'MOVE'),         @
               VL,MF=(E,PARMLIST)
*
**       ENSURE WE HAVE A COMMAND AND INITIALIZE FOR SEARCH OF IT
*
         L     R2,ZCMDL                   LENGTH OF ZCMD
         LTR   R2,R2                      IS IT ZERO?
         BZ    CHKCMDLX                   YES, LEAVE
         LA    R3,ZCMD                    ADDRESS(ZCMD)
*
** SEARCH FOR BEGINNING OF FIRST OPERAND
*
         CLC   0(2,R3),=C'L '
         BE    CC1000
         CLC   0(7,R3),=C'LOCATE '
         BE    CC1001
         CLC   0(2,R3),=C'F '
         BE    CC1100
         CLC   0(5,R3),=C'FIND '
         BE    CC1101
         BAL   R14,CLRMSG                  SET MESSAGES TO BLANKS
         MVC   DSIMSGS(18),=C'"12345678" INVALID'
         MVC   DSIMSGS+1(8),0(R3)
         MVC   DSIMSGL(25),=C'COMMAND IS NOT ACCEPTABLE'
         LA    R0,18
         LA    R1,25
         MVC   MSGID(8),=CL8'DSI@000A'
         BAL   R9,SETMSG
         B     CHKCMDLX
CC1000   EQU   *
         LA    R3,2(R3)                   BUMP PAST "L "
         S     R2,=F'2'                   ..SUBTRACT ITS LENGTH
         B     CC1000X                    LEAVE
CC1001   EQU   *
         LA    R3,7(R3)                   BUMP PAST "LOCATE "
         S     R2,=F'7'                   ..SUBRACT ITS LENGTH
         B     CC1000X                    LEAVE
CC1100   EQU   *
         OI    EXECFLAG,$FIND             TURN ON FLAG
         LA    R3,2(R3)                   BUMP PAST "F "
         S     R2,=F'2'                   ..SUBTRACT ITS LENGTH
         B     CC1000X                    LEAVE
CC1101   EQU   *
         OI    EXECFLAG,$FIND             TURN ON FLAG
         LA    R3,5(R3)                   BUMP PAST "FIND "
         S     R2,=F'5'                   ..SUBTRACT ITS LENGTH
         B     CC1000X                    LEAVE
CC1000X  EQU   *
         LTR   R2,R2                      CHECK OUT LENGTH
         BZ    CC2001                     ZERO? NO SECOND OPERAND...
         BM    CC2001                     MINUS? NO SECOND OPERAND...
*
** SEARCH FOR BEGINNING OF SECOND OPERAND
*
CC2000   EQU   *
         CLI   0(R3),C' '
         BNE   CC3000
         LA    R3,1(R3)
         BCT   R2,CC2000
CC2001   EQU   *
         BAL   R14,CLRMSG                  SET MESSAGES TO BLANKS
         MVC   DSIMSGS(18),=C'NO SEARCH ARGUMENT'
         MVC   DSIMSGL(33),=C'REQUIRED SECOND OPERAND NOT FOUND'
         LA    R0,18
         LA    R1,33
         MVC   MSGID(8),=CL8'DSI@000A'
         BAL   R9,SETMSG
         B     CHKCMDLX
*
** ISOLATE SECOND OPERAND
*
CC3000   EQU   *
         LA    R4,44                      LENGTH OF TBSRCH
         ST    R3,S_TBSRCH                SAVE ADDRESS OF SEARCH ARG
CC3001   EQU   *
         CLI   0(R3),C' '
         BE    CC3002
         LA    R3,1(R3)
         BCTR  R4,0
         LTR   R4,R4
         BZ    CC3002
         BCT   R2,CC3001
CC3002   EQU   *
         LA    R5,44                      MAX LENGTH TO FIND
         SR    R5,R4                      ..MINUS USED = ACTUAL
         BCTR  R5,0                       ..DECREMENT FOR EX
         L     R4,S_TBSRCH                OBTAIN SEARCH ARG LOCATION
         LA    R3,DSNAME                  FIND ROW DATASET
         L     R1,DSILVLL                 GET LENGTH OF LEVEL
         LA    R3,1(R1,R3)                BUMP TO COMPARISON LOCATION
         CALL  ISPLINK,                                                @
               (=CL8'TBTOP',=CL8'DSINDEX'),                            @
               VL,MF=(E,PARMLIST)
*
** SKIP TO NEXT ROW
** R3 : OFFSET IN ROW DSNAME, PAST LEVEL
** R4 : ADDRESS OF VALUE TO FIND
** R5 : LENGTH OF VALUE TO FIND
*
CCS000   EQU   *
         CALL  ISPLINK,                                                @
               (=CL8'TBSKIP',=CL8'DSINDEX'),                           @
               VL,MF=(E,PARMLIST)
         LTR   R15,R15
         BNZ   CCSX
         CLC   0(0,R3),0(R4)
         EX    R5,*-6
         BE    CHKCMDLX
         BH    CHKCMDLX
         B     CCS000
CCSX     EQU   *
         MVC   DSIMSGS(9),=C'NOT FOUND'
         MVC   DSIMSGL(29),=C'COULDN''T FIND DSNAME BY SEARCH'
         MVC   MSGID(8),=CL8'DSI@000B'
         LA    R0,9
         LA    R1,29
         BAL   R9,SETMSG
*
** COMMON EXIT
*
CHKCMDLX EQU   *
         NI    EXECFLAG,TURNOFF-$FIND
         B     DSNLOOP
         EJECT
***********************************************************************
***                                                                 ***
**       PROCESS ROW                                                 **
***                                                                 ***
***********************************************************************
         SPACE 2
ROWPROC  EQU   *
         LA    R15,4
         ST    R15,ROWNUML
         CALL  ISPLINK,                                                @
               (=CL8'VCOPY',=C'(ROWNUM)',ROWNUML,ROWNUM,=CL8'MOVE'),   @
               VL,MF=(E,PARMLIST)
         LA    R15,7
         ST    R15,TSELL
         CALL  ISPLINK,                                                @
               (=CL8'VCOPY',=C'(TSEL)',TSELL,TSEL,=CL8'MOVE'),         @
               VL,MF=(E,PARMLIST)
         LA    R15,44
         ST    R15,DSNAMEL
         CALL  ISPLINK,                                                @
               (=CL8'VCOPY',=C'(DSNAME)',DSNAMEL,DSNAME,=CL8'MOVE'),   @
               VL,MF=(E,PARMLIST)
         LA    R15,6
         ST    R15,VOLSERL
         CALL  ISPLINK,                                                @
               (=CL8'VCOPY',=C'(VOLSER)',VOLSERL,VOLSER,=CL8'MOVE'),   @
               VL,MF=(E,PARMLIST)
         LA    R15,11
         ST    R15,LASTAL
         CALL  ISPLINK,                                                @
               (=CL8'VCOPY',=C'(LASTA)',LASTAL,LASTA,=CL8'MOVE'),      @
               VL,MF=(E,PARMLIST)
         LA    R15,4
         ST    R15,TYPEL
         CALL  ISPLINK,                                                @
               (=CL8'VCOPY',=C'(TYPE)',TYPEL,TYPE,=CL8'MOVE'),         @
               VL,MF=(E,PARMLIST)
         BR    R9
         EJECT
***********************************************************************
***                                                                 ***
**        MESSAGE PROCESSING                                         **
***                                                                 ***
***********************************************************************
         SPACE 2
*
**       TELL ISPF ABOUT OUR SCREEN MESSAGE
**       INPUT: R0 - LENGTH OF SHORT MESSAGE.
**              R1 - LENGTH OF LONG MESSAGE.
*
SETMSG   EQU   *
         ST    R0,DSIMSGSL
         ST    R1,DSIMSGLL
         CALL  ISPLINK,                                                @
               (=CL8'VDEFINE',                                         @
               =C'(DSIMSGS)',DSIMSGS,=CL8'CHAR',DSIMSGSL),             @
               VL,MF=(E,PARMLIST)
         CALL  ISPLINK,                                                @
               (=CL8'VDEFINE',                                         @
               =C'(DSIMSGL)',DSIMSGL,=CL8'CHAR',DSIMSGLL),             @
               VL,MF=(E,PARMLIST)
SETMSGI  EQU   *
         CALL  ISPLINK,                                                @
               (=CL8'SETMSG',MSGID),                                   @
               VL,MF=(E,PARMLIST)
         BR    R9
*
**       LOG MESSAGE TO LOG FILE
*
LOGMSG   EQU   *
         LA    R1,8
         ST    R1,FUNCTL
         CALL  ISPLINK,                                                @
               (=CL8'VDEFINE',                                         @
               =C'(FUNCT)',FUNCT,=CL8'CHAR',FUNCTL),                   @
               VL,MF=(E,PARMLIST)
         CALL  ISPLINK,                                                @
               (=CL8'LOG',LOGMSG1),                                    @
               VL,MF=(E,PARMLIST)
         CLI   LOGMSG2,X'00'
         BE    LOGMSGX
         CALL  ISPLINK,                                                @
               (=CL8'LOG',LOGMSG2),                                    @
               VL,MF=(E,PARMLIST)
* DEFINING ZERR MESSAGE TO ZERO IN TEST MODE TO CORRECT ISPF ABEND
         XC    DSIMSGSL(4),DSIMSGSL
         XC    DSIMSGLL(4),DSIMSGLL
         CALL  ISPLINK,                                                @
               (=CL8'VDEFINE',                                         @
               =C'(DSIMSGS)',DSIMSGS,=CL8'CHAR',DSIMSGSL),             @
               VL,MF=(E,PARMLIST)
         CALL  ISPLINK,                                                @
               (=CL8'VDEFINE',                                         @
               =C'(DSIMSGL)',DSIMSGL,=CL8'CHAR',DSIMSGLL),             @
               VL,MF=(E,PARMLIST)
LOGMSGX  EQU   *
         BR    R9
         EJECT
***********************************************************************
***                                                                 ***
**       LINK TO MINI HEX DUMP PROGRAM                               **
**       INPUT  R7       ADDRESS OF INPUT AREA                       **
**       OUTPUT R8       ADDRESS OF OUTPUT AREA                      **
***                                                                 ***
***********************************************************************
         SPACE 2
         PRINT GEN
DUMPIT   EQU   *
         CALL  MINIXDMP,(MINILEN,(7),(8),MINIOFF),                     @
               MF=(E,PARMLIST)
         BR    R9
         PRINT NOGEN
         EJECT
***********************************************************************
***                                                                 ***
**       MISC SUBROUTINES                                            **
***                                                                 ***
***********************************************************************
         SPACE 2
*
**       GETWRK: GETMAIN WORKING STORAGE FOR LOCINDEX WORK AREA
**       FREEWRK: FREEMAIN WORKING STORAGE FOR LOCINDEX WORK AREA
*
GETWRK   EQU   *
         L     R0,LOCWRKL               GET WORK AREA LENGTH-RSA
         GETMAIN RC,LV=(0)
         ST    R1,LOCWRK
         LR    R2,R1                    GET ADDR OF WORK AREA
         LR    R3,R0                    GET WORK AREA LENGTH-RSA
         SLR   R5,R5                    SET LENGTH AND PAD TO ZERO
         MVCL  R2,R4                    CLEAR WORK AREA TO ZEROS
         BR    R9
FREEWRK  EQU   *
         L     R0,LOCWRKL
         L     R1,LOCWRK
         FREEMAIN R,LV=(0),A=(1)
         BR    R9
*
** PUT TICKS AROUND DATASET NAME (FOR ISPF EDIT/BROWSE)
*
DSNAMEED EQU   *
         MVI   EDDSN,C' '              <    BLANK OUT
         MVC   EDDSN+1(46),EDDSN       <    EDITED DSNAME FIELDS
         LA    R4,EDDSN+1              PLACE TO PUT DSNAME
         LA    R5,DSNAME               LOCATION OF DSNAME
         L     R6,DSNAMEL              LENGTH OF DSNAME FROM PANEL
         BCTR  R6,0                    SUBTRACT 1 FOR "EX" INSTRUCTION
         MVC   0(0,R4),0(R5)           MOVE EDITED 'DSNAME'
         EX    R6,*-6                  ..BY LENGTH
         MVI   EDDSN,X'7D'             APOSTROPHE AT FRONT OF DSN
         LA    R4,1(R6,R4)             LOCATE END OF EDITED DSNAME
         MVI   0(R4),X'7D'             MOVE IN THE APOSTROPHE
         BR    R9
*
**       CONVERT DATE FROM X'YY,DD,DD' TO C'YY.DDD'
**       INPUT:  R1  - ADDRESS OF YDD
**       OUTPUT: R15 - ADDRESS TO PUT YY.DDD
*
CONVDATE EQU  *
         CLI   0(R1),X'00'               YEAR = ZEROS?
         BE    CVDEXIT                   YES, DONT EVEN BOTHER
         SLR   R3,R3                     ----+
         ICM   R3,B'0001',0(R1)              |
         CVD   R3,DOUBLE                     |  FORMAT YEAR
         UNPK  0(2,R15),DOUBLE+6(2)          |
         OI    1(R15),X'F0'              ----+
*
         SLR   R3,R3                     ----+
         ICM   R3,B'0011',1(R1)              |
         CVD   R3,DOUBLE                     |  FORMAT DAY
         UNPK  3(3,R15),DOUBLE+6(2)          |
         OI    5(R15),X'F0'              ----+
*
         MVI   2(R15),C'.'
CVDEXIT  EQU  *
         BR    R14
*
**       CLEAR ISPF MESSAGE AREAS
*
CLRMSG   EQU   *
         MVI   DSIMSGS,C' '
         MVC   DSIMSGS+1(29),DSIMSGS
         MVI   DSIMSGL,C' '
         MVC   DSIMSGL+1(78),DSIMSGL
         BR    R14
*
**       PRETTY PRINT CONTENTS OF REGISTER 0
**       R0 --> INPUT:REGISTER TO PRINT
**       R6 --> OUTPUT: ADDRESS WHERE OUTPUT IS TO BE PUT
**              (IT WILL BE ADVANCED TO NEXT BYTE AFTER NUMBER)
*
EDREG0   EQU   *
         LTR   R0,R0                      CHECK FOR 0
         BZ    EDREG00                    YES, PUT ZERO
         CVD   R0,DOUBLE                  NO, CONVERT TO PACKEC
         MVC   WRKNUM(7),=X'40,20,20,20,20,21,20' GET EDIT PATTERN
         LA    R1,WRKNUM+6                LOCATE SIG DIGIT
         EDMK  WRKNUM(7),DOUBLE+5         EDIT IT
         LR    R4,R1                      SAVE POINTER TO START
         LA    R15,WRKNUM+7               LOCATE LAST POSSIBLE BYTE + 1
         SR    R15,R1                     LENGTH = END - START
         BCTR  R15,0                      DECREMENT FOR EX
         MVC   0(0,R6),0(R4)
         EX    R15,*-6                    MOVE EDIT NUMBER TO OUTPUT
         LA    R6,1(R15,R6)               BUMP POINTER PAST NUMBER
         BR    R14                        RETURN TO CALLER
EDREG00  EQU   *
         MVI   0(R6),C'0'                 PUT IN ZERO
         LA    R6,2(R6)                   BUMP POINTER PAST ZERO
         BR    R14                        RETURN TO CALLER
         EJECT
***********************************************************************
***                                                                 ***
**       READ CVOL FOR INDEX ENTRIES                                 **
***                                                                 ***
***********************************************************************
         SPACE 2
READCVOL EQU   *
*
** CREATE DSNAME INDEX TABLE
*
         CALL  ISPLINK,                                                @
               (=CL8'TBCREATE',                                        @
               =CL8'DSINDEX',                   *TABLE NAME*           @
               =C'(ROWNUM)',                    *KEYWORD LIST*         @
               =C'(DSNAME VOLSER LASTA TYPE)',  *NAME LIST*            @
               =CL8'NOWRITE'),                  *TEMP TABLE*           @
               VL,MF=(E,PARMLIST)
*
** DEFINE FIELDS TO ISPF
*
         LA    R15,4
         ST    R15,ROWNUML
         CALL  ISPLINK,                                                @
               (=CL8'VDEFINE',                                         @
               =C'(ROWNUM)',ROWNUM,=CL8'CHAR',ROWNUML),                @
               VL,MF=(E,PARMLIST)
         LA    R15,7
         ST    R15,TSELL
         CALL  ISPLINK,                                                @
               (=CL8'VDEFINE',=C'(TSEL)',TSEL,=CL8'CHAR',TSELL),       @
               VL,MF=(E,PARMLIST)
         LA    R15,44
         ST    R15,DSNAMEL
         CALL  ISPLINK,                                                @
               (=CL8'VDEFINE',=C'(DSNAME)',DSNAME,=CL8'CHAR',DSNAMEL), @
               VL,MF=(E,PARMLIST)
         LA    R15,6
         ST    R15,VOLSERL
         CALL  ISPLINK,                                                @
               (=CL8'VDEFINE',=C'(VOLSER)',VOLSER,=CL8'CHAR',VOLSERL), @
               VL,MF=(E,PARMLIST)
         LA    R15,11
         ST    R15,LASTAL
         CALL  ISPLINK,                                                @
               (=CL8'VDEFINE',=C'(LASTA)',LASTA,=CL8'CHAR',LASTAL),    @
               VL,MF=(E,PARMLIST)
         LA    R15,4
         ST    R15,TYPEL
         CALL  ISPLINK,                                                @
               (=CL8'VDEFINE',=C'(TYPE)',TYPE,=CL8'CHAR',TYPEL),       @
               VL,MF=(E,PARMLIST)
*
** SET UP FOR LOCINDEX SUBROUTINE
*
*        LOAD  EPLOC==CL8'LOCINDEX',ERRET=INDXLOAD
*        ST    R0,S_LOC
         MVC   S_LOC(4),=V(LOCINDEX)
         OC    PARMLIST(L'PARMLIST),PARMLIST
         BAL   R9,GETWRK                  GET WORKAREA FOR LOCINDEX
         SLR   R8,R8                      CLEAR OUT NUMBER-FOUND
         SLR   R7,R7                      CLEAR OUT NUMBER-FOUND
*
** CALL SUBROUTINE TO SEARCH CVOL/CATALOG FOR OUR INDEX
*
INDXSRCH EQU   *
         L     R2,LOCWRK                  GETMAINED WORK AREA ADDRESS
         L     R15,S_LOC
         CALL  (15),                                                   @
               (DSILVL,DSNAME,VOLSER,(2)),                             @
               VL,MF=(E,PARMLIST)
         B     *+4(R15)
         B     INDXDASD                   00-DSNAME FOUND AND ON DISK
         B     INDXNONE                   04-INDEX NOT FOUND AT ALL
         B     INDXTAPE                   08-DSNAME FOUND AND ON TAPE
         B     INDXSRCH                   12-DSNAME RESIDES ON MULTVOL
         B     INDXEND                    16-ALL INDEX LEVELS FOUND
         B     INDXIOE                    20-I/O ERROR ON CATALOG
         B     INDXINTE                   24-INTERNAL ERROR
*
** LOCINDEX FOUND A DASD DATASET
*
INDXDASD EQU   *
         CLC   DSIDASD(3),=CL3'YES'       ARE WE LOOKING FOR DASD DS?
         BNE   INDXSRCH                   NO, GET NEXT INDEX
         MVC   TYPE(4),=CL4'DASD'         PROSE
         B     INDXOK                     CHECK THIS DATASET OUT...
*
** LOCINDEX FOUND A TAPE DATASET
*
INDXTAPE EQU   *
         CLC   DSITAPE(3),=CL3'YES'       ARE WE LOOKING FOR TAPE DS?
         BNE   INDXSRCH                   NO, GET NEXT INDEX
         MVC   TYPE(4),=CL4'TAPE'         PROSE
         B     INDXOK                     CHECK THIS DATASET OUT...
*
** INDEX LOOKS OKAY
*
INDXOK   EQU   *
         BAL   R9,CHKDSN           SEE IF THIS DATASET MATCHS SEARCH
         LTR   R15,R15             DID IT MATCH?
         BNZ   INDXSRCH            ..NO, TRY NEXT DATASET
         LA    R8,1(R8)            ADD 1 TO NUMBER OF DATASETS FOUND
         LR    R5,R8               LOAD INTO WORK REGISTER
         SLR   R4,R4               CLEAR OUT EVEN REGISTER
         D     R4,=F'10'           DIVIDE TO FIND REMAINDER
         LTR   R4,R4               TEST FOR ZERO
         BNZ   ADDROW              NO, DONT ISSUE MESSAGE
*
*        PRINT STATUS IF THIS IS A VERY LARGE INDEX GROUP
*
         BAL   R14,CLRMSG                  SET MESSAGES TO BLANKS
         LA    R6,DSIMSGS
         LR    R0,R8
         BAL   R14,EDREG0
         MVC   0(15,R6),=C'DATASETS SO FAR'
         LA    R6,16(R6)
         LA    R1,DSIMSGS
         SR    R6,R1
         LR    R0,R6
         LA    R1,0
         MVC   MSGID(8),=CL8'DSI@000A'
         BAL   R9,SETMSG
         CALL  ISPLINK,                                                @
               (=CL8'CONTROL',=CL8'DISPLAY',=CL8'LOCK'),               @
               VL,MF=(E,PARMLIST)
         CALL  ISPLINK,                                                @
               (=CL8'DISPLAY',=CL8'DSINDX01'),                         @
               VL,MF=(E,PARMLIST)
*
*        ADD ROW
*
ADDROW   EQU   *
         MVC   LASTA(11),=CL11' '
         CALL  ISPLINK,                                                @
               (=CL8'TBADD',=CL8'DSINDEX'),                            @
               VL,MF=(E,PARMLIST)
         CALL  ISPLINK,                                                @
               (=CL8'TBBOTTOM',=CL8'DSINDEX'),                         @
               VL,MF=(E,PARMLIST)
         B     INDXSRCH
*
*        LOCINDEX FOUND I/O ERROR ON CATALOG
*
INDXIOE  EQU   *
         BAL   R14,CLRMSG                  SET MESSAGES TO BLANKS
         MVC   DSIMSGS(18),=C'I/O ERROR ON CATLOG'
         MVC   DSIMSGL(34),=C'LOCINDEX FOUND I/O ERROR ON CATALOG'
         MVC   MSGID(8),=CL8'DSI@000A'
         LA    R0,18
         LA    R1,34
         B     INDXEXIT
*
*        LOCINDEX HAD INTERNAL ERROR
*
INDXINTE EQU   *
         BAL   R14,CLRMSG                  SET MESSAGES TO BLANKS
         MVC   DSIMSGS(14),=C'INTERNAL ERROR'
         MVC   DSIMSGL(30),=C'LOCINDEX HAD AN INTERNAL ERROR'
         MVC   MSGID(8),=CL8'DSI@000A'
         LA    R0,18
         LA    R1,30
         B     INDXEXIT
*
*        NO INDEX FOUND FOR LEVEL
*
INDXNONE EQU   *
         BAL   R14,CLRMSG                  SET MESSAGES TO BLANKS
         MVC   DSIMSGS(20),=C'LEVEL DOES NOT EXIST'
         MVC   DSIMSGL(20),=C'LEVEL DOES NOT EXIST'
         MVI   DSIMSGL+20,C':'
         MVC   DSIMSGL+22(44),DSILVL
         MVC   MSGID(8),=CL8'DSI@000A'     GET MESSAGE ID
         LA    R0,20                       LENGTH OF SHORT MESSAGE
         LA    R1,66                       LENGTH OF LONG  MESSAGE
INDXEXIT EQU   *
         MVC   MSGID(8),=CL8'DSI@000A'
         BAL   R9,SETMSG                   SET MESSAGE
         BAL   R9,FREEWRK                  FREE WORK AREA
         B     DISPPOM
*
*        NO DATASETS FOUND TO MEET GENERIC SEARCH
*
INDXNFND EQU   *
         BAL   R14,CLRMSG
         MVC   DSIMSGS(17),=C'NO DATASETS FOUND'
         MVC   DSIMSGL(32),=C'NONE FOUND IN SEARCH, NEXT NODE:'
         LA    R6,DSIMSGL+33
         LR    R0,R7
         BAL   R14,EDREG0
         LA    R0,17
         LA    R1,40
         B     INDXEXIT
*
*   END OF LOCINDEX PROCESSING
*
INDXEND  EQU   *
         LTR   R7,R7                    TEST FOR ZERO (NO MATCHES)
         BZ    INDXNFND                 ZERO, GOTO INDXNONE ROUTINE
         BAL   R9,FREEWRK                  FREE WORK AREA
         BAL   R14,CLRMSG
         LA    R6,DSIMSGS
         LR    R0,R8
         BAL   R14,EDREG0
         MVC   0(17,R6),=C'DATASETS FOUND OF'
         LA    R6,18(R6)
         LR    R0,R7
         BAL   R14,EDREG0
         LA    R1,DSIMSGS
         SR    R6,R1
         LR    R2,R6
         BCTR  R2,0
         MVC   DSIMSGL(0),DSIMSGS
         EX    R2,*-6
         LR    R0,R6
         LR    R1,R6
         MVC   MSGID(8),=CL8'DSI@000A'
         BAL   R9,SETMSG
*
** ISSUE LOG MESSAGES
*
         MVC   FUNCT(8),=CL8'LOCINDEX'
         MVC   LOGMSG1(8),=CL8'DSI@002A'
         MVC   LOGMSG2(8),=CL8'DSI@001B'
         BAL   R9,LOGMSG
         MVC   LOGMSG1(8),=CL8'DSI@001C'
         MVI   LOGMSG2,X'00'
         BAL   R9,LOGMSG
         MVC   LOGMSG1(8),=CL8'DSI@002A'
         MVC   LOGMSG2(8),=CL8'DSI@002B'
*
*        GET TO THE TOP OF THINGS
*
         CALL  ISPLINK,                                                @
               (=CL8'TBTOP',=CL8'DSINDEX'),                            @
               VL,MF=(E,PARMLIST)
*        DELETE EPLOC==CL8'LOCINDEX'
         B     DSNLOOP
INDXLOAD EQU   *
         BAL   R14,CLRMSG
         MVC   DSIMSGS(19),=C'LOCINDEX NOT LOADED'
         MVC   DSIMSGL(34),=C'COULD NOT LOAD LOCINDEX SUBROUTINE'
         LA    R0,19
         LA    R1,34
         MVC   MSGID(8),=CL8'DSI@000A'
         BAL   R9,SETMSG
         B     DISPPOM
         EJECT
***********************************************************************
***                                                                 ***
**       D A T A    D E C L A R A T I O N S                          **
***                                                                 ***
***********************************************************************
         SPACE 2
LOCWRKL  DC    F'4096'         LENGTH OF LOCINDEX WORK AREA
*
*
** COPY AREA (TO BE COPIED INTO GETMAINED WORK AREA)
*
         DS    0D
CPYAREA  EQU   *
         PRINT GEN
         CAMLST SEARCH,*-*,*-*,*-*
         CAMLST SCRATCH,*-*,,*-*,,OVRD
         CAMLST UNCAT,*-*
         PRINT NOGEN
CPYAREAL EQU   *-CPYAREA
         EJECT
***********************************************************************
***                                                                 ***
**       LITERALS                                                    **
***                                                                 ***
***********************************************************************
         SPACE 2
         LTORG ,
         EJECT
***********************************************************************
***                                                                 ***
**       GETMAIN WORK AREA                                           **
***                                                                 ***
***********************************************************************
         DS    0D
WORKDS   DSECT ,
         DS    18F                        REGISTER SAVE AREA
PARMLIST DS    8F                         PARMLIST FOR CALLS
DOUBLE   DS    D                          DOUBLE WORD WORK AREA
LOCWRK   DS    F                          GETMAIN WORK AREA (LOCINDEX)
S_DSCB   DS    F                          RETURN ADDRESS
S_LOC    DS    F                          EPA OF LOCINDEX
S_TBSRCH DS    F                          ADDRESS OF SEARCH ARG
MINILEN  DS    H'16'                      MINIXDMP LENGTH TO DUMP
MINIOFF  DS    H'00'                      MINIXDMP OFFSET NUMBER
WRKNUM   DS    CL15                       JUST A WORK NUMBER
LOGMSG1  DS    CL8                        LOG MESSAGE ID
LOGMSG2  DS    CL8                        LOG MESSAGE ID
MSGID    DS    CL8                        ISPF MESSAGE ID
*
** CAMLST, OBTAIN DATA AREAS
*
*-- PLEASE NOTE THAT LABELS FULLDSCB, DSNAME, AREADSCB MUST REMAIN IN
*-- THIS ORDER SO THAT THE IECSDSL1 MACRO CAN BE USED ASIS.
FULLDSCB DS    0CL140                     FULL FORMAT 1 DSCB
DSNAME   DS    44C' '                     DATASET NAME
AREADSCB DS    140C' '                    AREA RETURNED BY CAMLST
         DS    0H'0'                      GET ALIGNED
VOLLIST  EQU   *                          VOLUME LIST FOR SCRATCH
VNUMB    DS    H'1'                       NUMBER OF VOLUMES IN LIST
VDEVCODE DS    X'30,30,20,0E'             DEVICE CODE
VOLSER   DS    CL6                        VOLSER
VSTATCDE DS    H'0'                       SCRATCH STATUS CODE
*
** PROGRAM FLAGS
*
EXECFLAG DS    B'00000000'                EXECUTION FLAG
$DELETE  EQU   B'10000000'                ..DELETE: SCRATCH, AND UNCAT
$FIND    EQU   B'01000000'                ..FIND IF ON, LOCATE IF OFF
TURNOFF  EQU   B'11111111'                TURN OFF A FLAG
*
** ISPF DATA AREAS
*
         DS    0F
EDDSN    DS    CL76                       EDITED DSNAME (WITH TICKS)
TSEL     DS    CL7                        USER SELECTION CODE
LASTA    DS    CL11                       LAST ACTION
TYPE     DS    CL4                        TYPE (OF DATASET: TAPE|DASD)
FUNCT    DS    CL8                        CURRENT DSINDEX FUNCTION
DSIMSGS  DS    CL30                       SHORT MESSAGE
DSIMSGL  DS    CL79                       LONG MESSAGE
ZCMD     DS    CL79                       COMMAND FROM COMMAND LINE
BUFFER   DS    CL79                       SELECT PGM(...) BUFFER
DSILVL   DS    CL80                       UNEDITED DSNAME
DSIVOL   DS    CL6                        VOLSER TO LIMIT SEARCH
DSITAPE  DS    CL3                        TAPE DATASETS?
DSIDASD  DS    CL3                        DASD DATASETS?
XLINE1   DS    CL65                       DSCB HEX DUMP LINE 1
XLINE2   DS    CL65                        DSCB HEX DUMP LINE 2
XLINE3   DS    CL65                         DSCB HEX DUMP LINE 3
XLINE4   DS    CL65                          DSCB HEX DUMP LINE 4
XLINE5   DS    CL65                           DSCB HEX DUMP LINE 5
XLINE6   DS    CL65                            DSCB HEX DUMP LINE 6
CDATE    DS    CL6                        DSCB - CREATION DATE
CJOB     DS    CL8                             - CREATING JOB
LDATE    DS    CL6                             - LAST MOD DATE
LJOB     DS    CL8                             - LAST MOD JOB
XDATE    DS    CL6                             - EXPIRATION DATE
OPTION   DS    CL4                             - OPTION BYTE:SECURITY
DSORG    DS    CL4                             - DATASET ORGAINIZATION
         DS    0F
LRECL    DS    F                               - LOGICAL RECORD LENGTH
BLKSZ    DS    F                               - BLKSIZE
ROWNUM   DS    F                               - ROW NUMBER
*                                         LENGTH OF
DSILVLL  DS    F                          ..DSILVL
DSIVOLL  DS    F                          ..DSIVOL
DSITAPEL DS    F                          ..DSITAPE
DSIDASDL DS    F                          ..DSIDASD
TSELL    DS    F                          ..TSEL
ROWNUML  DS    F                          ..ROWNUM
DSNAMEL  DS    F                          ..DSNAME
VOLSERL  DS    F                          ..VOLSER
LASTAL   DS    F                          ..LASTA
TYPEL    DS    F                          ..TYPE
DSIMSGSL DS    F                          ..DSIMSGS
DSIMSGLL DS    F                          ..DSIMSGL
ZCMDL    DS    F                          ..ZCMD
BUFFERL  DS    F                          ..BUFFER
FUNCTL   DS    F                          ..FUNCT
OPTIONL  DS    F                          ..OPTION
DSORGL   DS    F                          ..DSORG
CDATEL   DS    F                          ..CDATE
CJOBL    DS    F                          ..CJOB
LDATEL   DS    F                          ..LDATE
XDATEL   DS    F                          ..XDATE
LJOBL    DS    F                          ..LJOB
LRECLL   DS    F                          ..LRECL
BLKSZL   DS    F                          ..BLKSZ
XLINE1L  DS    F                          ..XLINE1
XLINE2L  DS    F                          ..XLINE2
XLINE3L  DS    F                          ..XLINE3
XLINE4L  DS    F                          ..XLINE4
XLINE5L  DS    F                          ..XLINE5
XLINE6L  DS    F                          ..XLINE6
*
** COPY TO AREA
*
         DS    0D
CPYTO    EQU   *
         PRINT GEN
FINDDSCB CAMLST SEARCH,*-*,*-*,*-*
SCRATCHD CAMLST SCRATCH,*-*,,*-*,,OVRD
UNCATIT  CAMLST UNCAT,*-*
         PRINT NOGEN
CPYTOL   EQU   *-CPYTO
         DS    0D                         ALIGN DOUBLE WORD
WORKDSL  EQU   *-WORKDS                   LENGTH OF DSECT AREA
         EJECT ,
         PRINT GEN
         IECSDSL1 (1)
         END
