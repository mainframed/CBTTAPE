*PROCESS;
 /*
   TITLE      -   NEWMSGS

   FUNCTION   -   THIS PROGRAM WILL INITIALIZE AN EMPTY MEMBER OF A PDS
                  WITH 10 BLANK MESSAGES.  EACH MESSAGE CONSISTS OF A
                  MESSAGE ID, SHORT MESSAGE, HELP KEYWORD, ALARM
                  KEYWORD, AND LONG MESSAGE.

   SPECIFIER  -   CAROLINE WILLIAMS
                  EXXON COMPANY, USA (EDPC)

   DEVELOPER  -   CAROLINE WILLIAMS

   DATE       -   FEBRUARY 28, 1986

   LANGUAGE   -   PLI

   PANVALET   -

   ENTRY      -   PLISTART

   LINKAGE    -   THIS PROGRAM IS AN EDIT MACRO.

   EXT. REF.  -   ISPLINK

   DATA SETS  -   NONE

   SPF TABLES -   NONE

   SPF PANELS -   NONE

   SPF MSGS   -   NONE

   UPDATES    -   NONE

 */
1NEWMSGS: PROC OPTIONS(MAIN);
0   DCL ISPLINK       EXTERNAL ENTRY OPTIONS(ASM RETCODE INTER),
        PLIRETV       BUILTIN,
        REPEAT        BUILTIN,
        SUBSTR        BUILTIN,
        RETCODE       FIXED BIN(31),
        LVAR          FIXED BIN(31),
        ZERO          FIXED BIN(31)   INIT(0);
0   DCL MEMNAME       CHAR(8),
        BUF80A        CHAR(80),
        BUF80B        CHAR(80),
        BUF80C        CHAR(80),
        NUMB          PIC'9',
        PIC_2         PIC'99',
        QUOTE         CHAR(1)   INIT(''''),
        HELP          CHAR(8)   INIT(' .HELP=*'),
        ALARM         CHAR(10)  INIT('.ALARM=NO '),
        LPTR          CHAR(2),
        LMEMBER       FIXED BIN(31),
        LSPACE        FIXED BIN(31),
        (I,J,K)       FIXED BIN(31);
1   CALL ISPLINK('ISREDIT ',ZERO,'¢MACRO¢');
    CALL ISPLINK('ISREDIT ',ZERO,'¢LOCATE 0¢');
    RETCODE = PLIRETV;
0   IF RETCODE > 4
       THEN DO;
            LVAR = 2;
            CALL ISPLINK('VDEFINE ','LPTR    ',LPTR,'CHAR',LVAR);
            LVAR = 80;
            CALL ISPLINK('VDEFINE ','BUF80A  ',BUF80A,'CHAR',LVAR);
            CALL ISPLINK('VDEFINE ','BUF80B  ',BUF80B,'CHAR',LVAR);
            CALL ISPLINK('VDEFINE ','BUF80C  ',BUF80C,'CHAR',LVAR);
            CALL ISPLINK('ISREDIT ',ZERO,'¢(MEMNAME) = MEMBER¢');
            LVAR = 8;
            CALL ISPLINK('VCOPY   ','(MEMNAME)',LVAR,MEMNAME,'MOVE');
            LMEMBER = LVAR;
            LSPACE = 11 - LMEMBER;
            J = 0;
0           DO I = 0 TO 9;
               NUMB = I;
               BUF80A = SUBSTR(MEMNAME,1,LMEMBER) || NUMB ||
                        REPEAT(' ',LSPACE) || QUOTE || REPEAT(' ',23)||
                        QUOTE || HELP || REPEAT(' ',8) || ALARM ||
                        REPEAT(' ',13);
               BUF80B = QUOTE || REPEAT(' ',69) || QUOTE ||
                        REPEAT(' ',7);
               BUF80C = REPEAT('~',79);
               PIC_2 = J;
               LPTR = PIC_2;
               CALL ISPLINK('ISREDIT ',ZERO,
                            '¢LINE_AFTER (LPTR) = (BUF80A)¢');
               J = J + 1;
               PIC_2 = J;
               LPTR = PIC_2;
               CALL ISPLINK('ISREDIT ',ZERO,
                            '¢LINE_AFTER (LPTR) = (BUF80B)¢');
               J = J + 1;
               PIC_2 = J;
               LPTR = PIC_2;
               CALL ISPLINK('ISREDIT ',ZERO,
                            '¢LINE_AFTER (LPTR) = (BUF80C)¢');
               J = J + 1;
            END;
0           CALL ISPLINK('ISREDIT ',ZERO,'¢CHANGE "~" " " ALL¢');
            CALL ISPLINK('ISREDIT ',ZERO,'¢RESET¢');
            END;
0END NEWMSGS;
