000010 IDENTIFICATION DIVISION.                                         04/04/86
000020 PROGRAM-ID.    LST0A.                                            LST0A
000030 AUTHOR.        BOB ZIMMERMAN.                                       LV005
000040 INSTALLATION.  CNA INSURANCE.
000050 DATE-COMPILED.
000060
000070******************************************************************
000080*    BOB ZIMMERMAN                                               *
000090*    CNA INSURANCE                                               *
000100*    (312) 822-5719                                              *
000110*    LAST MODIFIED 8506                                          *
000120******************************************************************
000130/
000140 ENVIRONMENT DIVISION.
000150 DATA DIVISION.
000160 WORKING-STORAGE SECTION.
000170
000180******************************************************************
000190*    THE FOLLOWING TWO ALPHABETS ARE USED TO TRANSLATE ANY PARMS *
000200*    PASSED THRU LINKAGE FROM LOWER-CASE TO UPPER-CASE. THE 05   *
000210*    LEVEL BELOW MARKED LOWER-CASE SHOULD BE THE ALPHABET IN     *
000220*    LOWER-CASE.                                                 *
000230******************************************************************
000240 01  THE-ALPHABET.
000250     05  UPPER-CASE                    PIC X(26) VALUE
000260         'ABCDEFGHIJKLMNOPQRSTUVWXYZ'.
000270     05  LOWER-CASE                    PIC X(26) VALUE
000280         'abcdefghijklmnopqrstuvwxyz'.
000290
000300
000310
000320******************************************************************
000330*    SPF-VARIABLES IS THE WORKING STORAGE AREA USED TO CONTAIN   *
000340*    THE CONTENTS OF THE ISPF VARIABLES USED IN THIS DIALOG.     *
000350*    THESE VARIABLES MAY BE ACCESSED USING EITHER THE VDEFINE    *
000360*    SERVICE OR THE VCOPY SERVICE.                               *
000370******************************************************************
000380
000390 01  SPF-VARIABLES.
000400     05  SPF-ZTDTOP                    PIC S9(09) COMP.
000410     05  SPF-CSR                       PIC S9(09) COMP.
000420     05  SPF-SEQUENCE                  PIC S9(09) COMP VALUE ZERO.
000430     05  SPF-LAST-CSR                  PIC S9(09) COMP VALUE ZERO.
000440     05  SPF-DDNAME                    PIC X(08) VALUE SPACES.
000450     05  SPF-ALTDDNAM                  PIC X(08) VALUE SPACES.
000460     05  SPF-SCAN                      PIC X(04) VALUE 'SCAN'.
000470     05  SPF-SCAN-BYTE                 PIC X(01) VALUE ' '.
000480     05  SPF-MORE                      PIC X(01) VALUE SPACES.
000490     05  SPF-DSNAME                    PIC X(56) VALUE SPACES.
000500     05  SPF-LSEL                      PIC X(02) VALUE SPACES.
000510     05  SPF-VOLSER                    PIC X(06) VALUE SPACES.
000520     05  WORK-NUM                      PIC ZZZ9.
000530
000540
000550
000560******************************************************************
000570*    THE FOLLOWING WORKING STORAGE AREA IS USED TO CONTAIN THE   *
000580*    LIST OF DDNAMES CURRENTLY FOUND IN THE TIOT. THIS AREA IS   *
000590*    PASSED TO PROGRAM DDNLIST (WHICH FILLS IN THE DDNAMES).     *
000600******************************************************************
000610
000620 01  DDNLIST-AREA.
000630     05  DDN-LIST-COUNT                PIC S9(04) COMP VALUE +0.
000640     05  DDN-TABLE.
000650         10  DDN-DDNAME                PIC X(08)
000660                                       OCCURS 500 TIMES
000670                                       INDEXED BY DDN-INX.
000680/
000690******************************************************************
000700*    THE FOLLOWING WORKING STORAGE AREA IS USED IN PROCESSING    *
000710*    AND PARSING THE COMMAND LINE.                               *
000720******************************************************************
000730
000740 01  ZCMD-PROCESS-AREA                 SYNC.
000750     05  ZCMD-COMMAND-LINE             PIC X(80) VALUE SPACES.
000760     05  ZCMD-OVERFLOW-SWITCH          PIC 9(01) VALUE 0.
000770     05  ZCMD-COMMAND                  PIC X(80) VALUE SPACES.
000780     05  ZCMD-PARM-COUNT               PIC S9(04) COMP VALUE +0.
000790     05  ZCMD-PARM-TABLE.
000800         10  ZCMD-PARM                 PIC X(80)
000810                                       OCCURS 20 TIMES
000820                                       INDEXED BY ZCMD-INX.
000830
000840
000850
000860******************************************************************
000870*    THE FOLLOWING AREA IS USED TO RETURN THE DSNAME AND         *
000880*    VOLSER FOR ANY GIVEN DDNAME IN THE TIOT. KDSNSUB            *
000890*    (SUBROUTINE) IS CALLED TO RETURN THIS INFORMATION.          *
000900******************************************************************
000910
000920 01  KDSNSUB-AREA                     SYNC.
000930     05  KDSNSUB-DDNAME                PIC X(08) VALUE SPACES.
000940     05  KDSNSUB-COUNT                 PIC S9(04) COMP VALUE +15.
000950     05  KDSNSUB-DSN-TABLE.
000960         10  KDSNSUB-DSN               PIC X(44)
000970                                       OCCURS 15 TIMES
000980                                       INDEXED BY DSN-INX.
000990     05  KDSNSUB-VOL-TABLE.
001000         10  KDSNSUB-VOL               PIC X(06)
001010                                       OCCURS 15 TIMES
001020                                       INDEXED BY VOL-INX.
001030/
001040******************************************************************
001050*    THE FOLLOWING AREA IS USED FOR ISPF DIALOG CALLS. IT        *
001060*    CONTAINS LITERALS, NAMELISTS AND WORKING STORAGE AREAS      *
001070*    THAT THE DIALOG CALLS REQUIRE.                              *
001080******************************************************************
001090
001100 01  PDF-WORK-AREAS                   SYNC.
001110     05  PDF-DUMMY-PARAMETER          PIC X(8) VALUE '        '.
001120     05  PDF-BROWSE                   PIC X(8) VALUE 'BROWSE'.
001130     05  PDF-CANCEL                   PIC X(8) VALUE 'CANCEL'.
001140     05  PDF-CHAR                     PIC X(8) VALUE 'CHAR'.
001150     05  PDF-CONTROL                  PIC X(8) VALUE 'CONTROL'.
001160     05  PDF-DISPLAY                  PIC X(8) VALUE 'DISPLAY'.
001170     05  PDF-EDIT                     PIC X(8) VALUE 'EDIT'.
001180     05  PDF-EDIT-PROFILE             PIC X(8) VALUE '    '.
001190     05  PDF-ERRORS                   PIC X(8) VALUE 'ERRORS'.
001200     05  PDF-LINE                     PIC X(8) VALUE 'LINE'.
001210     05  PDF-MOVE                     PIC X(8) VALUE 'MOVE'.
001220     05  PDF-NEXT                     PIC X(8) VALUE 'NEXT'.
001230     05  PDF-NOWRITE                  PIC X(8) VALUE 'NOWRITE'.
001240     05  PDF-ORDER                    PIC X(8) VALUE 'ORDER'.
001250     05  PDF-PREVIOUS                 PIC X(8) VALUE 'PREVIOUS'.
001260     05  PDF-REPLACE                  PIC X(8) VALUE 'REPLACE'.
001270     05  PDF-RESTORE                  PIC X(8) VALUE 'RESTORE'.
001280     05  PDF-RETURN                   PIC X(8) VALUE 'RETURN'.
001290     05  PDF-SAVE                     PIC X(8) VALUE 'SAVE'.
001300     05  PDF-SELECT                   PIC X(8) VALUE 'SELECT'.
001310     05  PDF-TBADD                    PIC X(8) VALUE 'TBADD'.
001320     05  PDF-TBBOTTOM                 PIC X(8) VALUE 'TBBOTTOM'.
001330     05  PDF-TBCREATE                 PIC X(8) VALUE 'TBCREATE'.
001340     05  PDF-TBDISPL                  PIC X(8) VALUE 'TBDISPL'.
001350     05  PDF-TBEND                    PIC X(8) VALUE 'TBEND'.
001360     05  PDF-TBQUERY                  PIC X(8) VALUE 'TBQUERY'.
001370     05  PDF-TBSARG                   PIC X(8) VALUE 'TBSARG'.
001380     05  PDF-TBSCAN                   PIC X(8) VALUE 'TBSCAN'.
001390     05  PDF-TBSORT                   PIC X(8) VALUE 'TBSORT'.
001400     05  PDF-TBSKIP                   PIC X(8) VALUE 'TBSKIP'.
001410     05  PDF-TBTOP                    PIC X(8) VALUE 'TBTOP'.
001420     05  PDF-TBVCLEAR                 PIC X(8) VALUE 'TBVCLEAR'.
001430     05  PDF-FIXED                    PIC X(8) VALUE 'FIXED'.
001440     05  PDF-VCOPY                    PIC X(8) VALUE 'VCOPY'.
001450     05  PDF-VDEFINE                  PIC X(8) VALUE 'VDEFINE'.
001460     05  PDF-VREPLACE                 PIC X(8) VALUE 'VREPLACE'.
001470     05  PDF-VRESET                   PIC X(8) VALUE 'VRESET'.
001480     05  PDF-FULLWORD                 PIC S9(09) COMP.
001490     05  PDF-RETURN-CODE              PIC S9(09) COMP.
001500     05  PDF-BROWSE-RETURN-CODE       PIC S9(09) COMP.
001510     05  PDF-TBDISPL-ROW              PIC S9(09) COMP.
001520     05  PDF-SELECT-BUFFER-LENGTH     PIC S9(09) COMP.
001530     05  PDF-NAME-LIST                PIC X(256).
001540     05  PDF-COND-LIST                PIC X(80).
001550     05  PDF-TABLE-ID.
001560         10  PDF-TABLE-ID-PREFACE     PIC X(5).
001570         10  FILLER                   PIC X(3).
001580     05  PDF-TBDISPL-PANEL            PIC X(8).
001590     05  PDF-TBDISPL-AUTOSEL          PIC X(3).
001600     05  PDF-TBDISPL-MESSAGE          PIC X(8).
001610     05  PDF-TBDISPL-CURSOR           PIC X(8).
001620     05  PDF-TBQUERY-CRP              PIC X(8).
001630     05  PDF-SHORT-MESSAGE            PIC X(24).
001640     05  PDF-LONG-MESSAGE             PIC X(78).
001650     05  PDF-SELECT-BUFFER            PIC X(256).
001660     05  PDF-DSNAME                   PIC X(56).
001670     05  PDF-VOLSER                   PIC X(6).
001680
001690/
001700******************************************************************
001710*    DDNAME TABLE RECEIVED THRU PARMS                            *
001720******************************************************************
001730 01  MATCH-SW                         PIC X.
001740 01  DDNAME-TABLE.
001750     05  PASSED-DDNAME                PIC X(08)
001760                                      OCCURS 50 TIMES
001770                                      INDEXED BY PDDN-INX.
001780 01  DDNAME-TABLE-1.
001790     05  PASSED-DDNAME-LET-1          PIC X(01)
001800                                      OCCURS 8 TIMES
001810                                      INDEXED BY PDDN-1.
001820 01  DDNAME-TABLE-2.
001830     05  PASSED-DDNAME-LET-2          PIC X(01)
001840                                      OCCURS 8 TIMES
001850                                      INDEXED BY PDDN-2.
001860/
001870******************************************************************
001880*    THE ONLY PARAMETER ALLOWED PASSED IS THE WORD FULL. IT      *
001890*    WILL INITALIZE THE TBDISPL TO BE IN FULL MODE AS APPOSED    *
001900*    TO COMPRESS MODE. THE PARAMETER IS PASSED USING STANDARD    *
001910*    TSO (EXEC PGM= PARM=) CONVENTIONS.                          *
001920******************************************************************
001930
001940 LINKAGE SECTION.
001950 01  PASSED-PARMS.
001960     05  PARM-LENGTH                   PIC S9(04) COMP.
001970     05  PARM-DATA.
001980         10  PARM-DATA-LETTER          PIC X(01)
001990                                       OCCURS 1 TO 256 TIMES
002000                                       DEPENDING ON PARM-LENGTH.
002010/
002020 PROCEDURE DIVISION USING PASSED-PARMS.
002030
002040******************************************************************
002050*    INITALIZE WORKING STORAGE AREAS.                            *
002060*    CAPITOLIZE THE PARMS PASSED (IN CASE THEY ARE LOWER         *
002070*    CASE).                                                      *
002080*    VDEFINE AND VCOPY ANY ISPF VARIABLES THAT ARE REQUIRED.     *
002090******************************************************************
002100
002110 0100-INIT-AND-VDEFINE.
002120
002130     TRANSFORM PARM-DATA FROM LOWER-CASE TO UPPER-CASE.
002140
002150     CALL 'ISPLINK' USING             PDF-CONTROL
002160                                      PDF-ERRORS
002170                                      PDF-CANCEL.
002180
002190     IF PARM-LENGTH > 0 THEN
002200        MOVE '(PASSPARM)' TO PDF-NAME-LIST
002210        MOVE PARM-LENGTH TO PDF-FULLWORD
002220        CALL 'ISPLINK' USING          PDF-VREPLACE
002230                                      PDF-NAME-LIST
002240                                      PDF-FULLWORD
002250                                      PARM-DATA.
002260
002270     IF PARM-DATA = 'FULL'
002280         MOVE SPACES TO PARM-DATA
002290         MOVE 'ALL' TO SPF-SCAN.
002300
002310     IF PARM-DATA NOT = SPACES
002320         UNSTRING PARM-DATA DELIMITED BY ALL SPACES
002330         INTO PASSED-DDNAME (1) PASSED-DDNAME(2) PASSED-DDNAME(3)
002340           PASSED-DDNAME (4) PASSED-DDNAME(5) PASSED-DDNAME(6)
002350           PASSED-DDNAME (7) PASSED-DDNAME(8) PASSED-DDNAME(9)
002360           PASSED-DDNAME (10) PASSED-DDNAME(11) PASSED-DDNAME(12)
002370           PASSED-DDNAME (13) PASSED-DDNAME(14) PASSED-DDNAME(15)
002380           PASSED-DDNAME (16) PASSED-DDNAME(17) PASSED-DDNAME(18)
002390           PASSED-DDNAME (19)
002400           PASSED-DDNAME(20) PASSED-DDNAME(21) PASSED-DDNAME (22)
002410           PASSED-DDNAME(23) PASSED-DDNAME(24) PASSED-DDNAME (25)
002420           PASSED-DDNAME(26) PASSED-DDNAME(27) PASSED-DDNAME (28)
002430           PASSED-DDNAME(29)
002440           PASSED-DDNAME(30) PASSED-DDNAME(31) PASSED-DDNAME (32)
002450           PASSED-DDNAME(33) PASSED-DDNAME(34) PASSED-DDNAME (35)
002460           PASSED-DDNAME(36) PASSED-DDNAME(37) PASSED-DDNAME (38)
002470           PASSED-DDNAME(39)
002480           PASSED-DDNAME(40) PASSED-DDNAME(41) PASSED-DDNAME (42)
002490           PASSED-DDNAME(43) PASSED-DDNAME(44) PASSED-DDNAME (45)
002500           PASSED-DDNAME(46) PASSED-DDNAME(47) PASSED-DDNAME (48)
002510           PASSED-DDNAME(49)
002520           PASSED-DDNAME(50)
002530         MOVE 'ALL' TO SPF-SCAN
002540       ELSE MOVE SPACES TO DDNAME-TABLE.
002550
002560******************************************************************
002570*    ZTDTOP   -  IS THE ISPF VARIABLE FOR TABLE HANDELING        *
002580*    CSR      -  REPRESENTS THE CURRENT CRP FROM TBDISPL         *
002590*    SEQNUM   -  IS USED TO IDENTIFY THE DSNAMES WITHIN DDNAME   *
002600******************************************************************
002610
002620     MOVE '(ZTDTOP CSR SEQNUM)' TO PDF-NAME-LIST.
002630     MOVE +4 TO PDF-FULLWORD.
002640     CALL 'ISPLINK' USING              PDF-VDEFINE
002650                                       PDF-NAME-LIST
002660                                       SPF-ZTDTOP
002670                                       PDF-FIXED
002680                                       PDF-FULLWORD.
002690
002700
002710******************************************************************
002720*    SCAN     -  THE VARIABLE DETERMINING IF THIS ROW IS THE     *
002730*                FIRST ROW OF A CONCATENATION OR NOT.            *
002740*                USED TO CONTROL FULL/COMPRESS OPTIONS           *
002750******************************************************************
002760
002770     MOVE '(SCAN)' TO PDF-NAME-LIST.
002780     CALL 'ISPLINK' USING              PDF-VDEFINE
002790                                       PDF-NAME-LIST
002800                                       SPF-SCAN
002810                                       PDF-CHAR
002820                                       PDF-FULLWORD.
002830
002840******************************************************************
002850*    ZCMD     -  ISPF COMMAND LINE - PRIMARY COMMANDS            *
002860******************************************************************
002870
002880     MOVE '(ZCMD)' TO PDF-NAME-LIST.
002890     MOVE +80 TO PDF-FULLWORD.
002900     CALL 'ISPLINK' USING              PDF-VDEFINE
002910                                       PDF-NAME-LIST
002920                                       ZCMD-COMMAND-LINE
002930                                       PDF-CHAR
002940                                       PDF-FULLWORD.
002950
002960
002970******************************************************************
002980*    SCANSW   -  SWITCH IN TABLE TO DETERMINE IF THE ROW         *
002990*                SHOULD BE DISPLAYED IN COMPRESS MODE.           *
003000******************************************************************
003010
003020     MOVE '(SCANSW MORE)' TO PDF-NAME-LIST.
003030     MOVE +1 TO PDF-FULLWORD.
003040     CALL 'ISPLINK' USING              PDF-VDEFINE
003050                                       PDF-NAME-LIST
003060                                       SPF-SCAN-BYTE
003070                                       PDF-CHAR
003080                                       PDF-FULLWORD.
003090
003100******************************************************************
003110*    DDNAME   -  THE DDNAME DISPLAYED (TBDISPL)                  *
003120*    ALTDDNAME-  SECOND DDNAME FOR IDENTIFYING THE ROW (TBSORT)  *
003130******************************************************************
003140
003150     MOVE '(DDNAME ALTDDNAM)' TO PDF-NAME-LIST.
003160     MOVE +8 TO PDF-FULLWORD.
003170     CALL 'ISPLINK' USING              PDF-VDEFINE
003180                                       PDF-NAME-LIST
003190                                       SPF-DDNAME
003200                                       PDF-CHAR
003210                                       PDF-FULLWORD.
003220
003230
003240******************************************************************
003250*    VOLSER   -  THE VOLSER FOR TBDISPL                          *
003260******************************************************************
003270
003280     MOVE '(VOLSER)' TO PDF-NAME-LIST.
003290     MOVE +6 TO PDF-FULLWORD.
003300     CALL 'ISPLINK' USING              PDF-VDEFINE
003310                                       PDF-NAME-LIST
003320                                       SPF-VOLSER
003330                                       PDF-CHAR
003340                                       PDF-FULLWORD.
003350
003360
003370******************************************************************
003380*    DSNAME   -  THE DSNAME FOR TBDISPL                          *
003390******************************************************************
003400
003410     MOVE '(DSNAME)' TO PDF-NAME-LIST.
003420     MOVE +56 TO PDF-FULLWORD.
003430     CALL 'ISPLINK' USING              PDF-VDEFINE
003440                                       PDF-NAME-LIST
003450                                       SPF-DSNAME
003460                                       PDF-CHAR
003470                                       PDF-FULLWORD.
003480
003490
003500******************************************************************
003510*    LSEL     -  THE LINE COMMAND VARIABLE                       *
003520******************************************************************
003530
003540     MOVE '(LSEL)' TO PDF-NAME-LIST.
003550     MOVE +2 TO PDF-FULLWORD.
003560     CALL 'ISPLINK' USING              PDF-VDEFINE
003570                                       PDF-NAME-LIST
003580                                       SPF-LSEL
003590                                       PDF-CHAR
003600                                       PDF-FULLWORD.
003610
003620
003630******************************************************************
003640*    UMSG     -  SHORT (UPPER) MESSAGE FOR ERRORS                *
003650******************************************************************
003660
003670     MOVE '(UMSG)' TO PDF-NAME-LIST.
003680     MOVE +24 TO PDF-FULLWORD.
003690     CALL 'ISPLINK' USING              PDF-VDEFINE
003700                                       PDF-NAME-LIST
003710                                       PDF-SHORT-MESSAGE
003720                                       PDF-CHAR
003730                                       PDF-FULLWORD.
003740
003750
003760******************************************************************
003770*    LMSG     -  LONG (LOWER) MESSAGE FOR ERRORS                 *
003780******************************************************************
003790
003800     MOVE '(LMSG)' TO PDF-NAME-LIST.
003810     MOVE +78 TO PDF-FULLWORD.
003820     CALL 'ISPLINK' USING              PDF-VDEFINE
003830                                       PDF-NAME-LIST
003840                                       PDF-LONG-MESSAGE
003850                                       PDF-CHAR
003860                                       PDF-FULLWORD.
003870
003880
003890******************************************************************
003900*    RET0TABL -  PROGRAM RETURNS UNIQUE TABLE ID                 *
003910*                THIS SUPPORTS RECURSIVE USE OF THIS FUNCTION    *
003920******************************************************************
003930
003940     CALL 'RET0TABL' USING            PDF-TABLE-ID.
003950     MOVE 'LISTA' TO PDF-TABLE-ID-PREFACE.
003960
003970     MOVE 'LSTDA' TO PDF-TBDISPL-PANEL.
003980     MOVE SPACES TO PDF-TBDISPL-MESSAGE.
003990     MOVE SPACES TO PDF-TBDISPL-CURSOR.
004000     MOVE ZEROS TO PDF-TBDISPL-ROW.
004010
004020     PERFORM 2000-BUILD-TABLE THRU 2999-EXIT.
004030
004040     MOVE ZEROS TO PDF-RETURN-CODE.
004050     PERFORM 1000-DISPLAY-PANEL THRU 1999-EXIT
004060         UNTIL PDF-RETURN-CODE > 4.
004070
004080 EXIT-PROGRAM.
004090
004100
004110     CALL 'ISPLINK' USING PDF-TBEND PDF-TABLE-ID.
004120     CALL 'ISPLINK' USING              PDF-VRESET.
004130     GOBACK.
004140/
004150
004160
004170 1000-DISPLAY-PANEL.
004180
004190     CALL 'ISPLINK' USING              PDF-TBTOP
004200                                       PDF-TABLE-ID.
004210
004220     CALL 'ISPLINK' USING              PDF-TBSKIP
004230                                       PDF-TABLE-ID
004240                                       SPF-ZTDTOP.
004250
004260     IF PDF-TBDISPL-MESSAGE NOT = SPACES
004270         MOVE 'YES' TO PDF-TBDISPL-AUTOSEL
004280     ELSE
004290         MOVE 'NO' TO PDF-TBDISPL-AUTOSEL
004300         MOVE SPF-LAST-CSR TO PDF-TBDISPL-ROW
004310         IF SPF-LAST-CSR NOT = 0
004320             MOVE 'LSEL' TO PDF-TBDISPL-CURSOR.
004330
004340     CALL 'ISPLINK' USING              PDF-TBDISPL
004350                                       PDF-TABLE-ID
004360                                       PDF-TBDISPL-PANEL
004370                                       PDF-TBDISPL-MESSAGE
004380                                       PDF-TBDISPL-CURSOR
004390                                       PDF-TBDISPL-ROW
004400                                       PDF-DUMMY-PARAMETER
004410                                       PDF-TBDISPL-AUTOSEL.
004420     MOVE RETURN-CODE TO PDF-RETURN-CODE.
004430     IF PDF-RETURN-CODE > 4
004440         GO TO 1999-EXIT.
004450
004460
004470     MOVE 'LSTDA' TO PDF-TBDISPL-PANEL.
004480     MOVE SPACES TO PDF-TBDISPL-MESSAGE.
004490     MOVE SPACES TO PDF-TBDISPL-CURSOR.
004500     MOVE ZEROS TO PDF-TBDISPL-ROW.
004510     MOVE ZEROS TO SPF-CSR, SPF-LAST-CSR.
004520
004530
004540
004550 1100-TBQUERY-CRP.
004560     MOVE 'CSR' TO PDF-TBQUERY-CRP.
004570     CALL 'ISPLINK' USING              PDF-TBQUERY
004580                                       PDF-TABLE-ID
004590                                       PDF-DUMMY-PARAMETER
004600                                       PDF-DUMMY-PARAMETER
004610                                       PDF-DUMMY-PARAMETER
004620                                       PDF-DUMMY-PARAMETER
004630                                       PDF-DUMMY-PARAMETER
004640                                       PDF-TBQUERY-CRP.
004650
004660     IF SPF-CSR = 0 GO TO 1500-PROCESS-ZCMD.
004670     MOVE SPF-CSR TO SPF-LAST-CSR.
004680
004690     PERFORM 4000-LINE-COMMAND THRU 4999-EXIT.
004700
004710     IF PDF-TBDISPL-MESSAGE NOT = SPACES
004720         GO TO 1999-EXIT.
004730
004740     IF PDF-RETURN-CODE = 4
004750         MOVE SPACES TO PDF-TBDISPL-PANEL
004760         GO TO 1999-EXIT.
004770
004780 1500-PROCESS-ZCMD.
004790     IF ZCMD-COMMAND-LINE = SPACES
004800         GO TO 1999-EXIT.
004810
004820     MOVE ZEROS TO ZCMD-PARM-COUNT.
004830     MOVE ZEROS TO ZCMD-OVERFLOW-SWITCH.
004840     MOVE SPACES TO ZCMD-COMMAND.
004850     MOVE SPACES TO ZCMD-PARM-TABLE.
004860
004870     UNSTRING ZCMD-COMMAND-LINE
004880              DELIMITED BY ALL SPACES
004890              INTO ZCMD-COMMAND
004900              ZCMD-PARM (1)
004910              ZCMD-PARM (2)
004920              ZCMD-PARM (3)
004930              ZCMD-PARM (4)
004940              ZCMD-PARM (5)
004950              ZCMD-PARM (6)
004960              ZCMD-PARM (7)
004970              ZCMD-PARM (8)
004980              ZCMD-PARM (9)
004990              ZCMD-PARM (10)
005000              ZCMD-PARM (11)
005010              ZCMD-PARM (12)
005020              ZCMD-PARM (13)
005030              ZCMD-PARM (14)
005040              ZCMD-PARM (15)
005050              ZCMD-PARM (16)
005060              ZCMD-PARM (17)
005070              ZCMD-PARM (18)
005080              ZCMD-PARM (19)
005090              ZCMD-PARM (20)
005100              TALLYING ZCMD-PARM-COUNT
005110              ON OVERFLOW
005120                  MOVE +1 TO ZCMD-OVERFLOW-SWITCH.
005130
005140     IF ZCMD-PARM-COUNT > 0
005150         SUBTRACT 1 FROM ZCMD-PARM-COUNT.
005160
005170     IF ZCMD-OVERFLOW-SWITCH NOT = 0
005180         MOVE 'TOO MANY PARAMETERS' TO PDF-SHORT-MESSAGE
005190         MOVE 'THERE IS A LIMIT OF 20 PARAMETERS TO ANY PRIMARY CO
005200-        'COMMAND' TO PDF-LONG-MESSAGE
005210         MOVE 'TLNM019E' TO PDF-TBDISPL-MESSAGE
005220         MOVE SPACES TO PDF-TBDISPL-PANEL
005230         GO TO 1999-EXIT.
005240
005250 1600-COMPRESS-COMMAND.
005260     IF ZCMD-COMMAND = 'COMPRESS' OR 'CMP'
005270         IF ZCMD-PARM-COUNT NOT = 0
005280             MOVE 'INVALID PARAMETER' TO PDF-SHORT-MESSAGE
005290             MOVE 'THERE ARE NO VALID PARMS FOR THIS COMMAND'
005300             TO PDF-LONG-MESSAGE
005310             MOVE 'TLNM019E' TO PDF-TBDISPL-MESSAGE
005320             MOVE SPACES TO PDF-TBDISPL-PANEL
005330             GO TO 1999-EXIT.
005340
005350     IF ZCMD-COMMAND = 'COMPRESS' OR 'CMP'
005360         MOVE 'SCAN' TO SPF-SCAN
005370         MOVE SPACES TO ZCMD-COMMAND-LINE
005380         GO TO 1999-EXIT.
005390
005400 1605-FULL-COMMAND.
005410     IF ZCMD-COMMAND = 'FULL'
005420         IF ZCMD-PARM-COUNT NOT = 0
005430             MOVE 'INVALID PARAMETER' TO PDF-SHORT-MESSAGE
005440             MOVE 'THERE ARE NO VALID PARMS FOR THIS COMMAND'
005450             TO PDF-LONG-MESSAGE
005460             MOVE 'TLNM019E' TO PDF-TBDISPL-MESSAGE
005470             MOVE SPACES TO PDF-TBDISPL-PANEL
005480             GO TO 1999-EXIT.
005490
005500     IF ZCMD-COMMAND = 'FULL'
005510         MOVE 'ALL' TO SPF-SCAN
005520         MOVE SPACES TO ZCMD-COMMAND-LINE
005530         GO TO 1999-EXIT.
005540
005550 1610-REFRESH-COMMAND.
005560     IF ZCMD-COMMAND = 'REFRESH' OR '*' OR 'REF'
005570         IF ZCMD-PARM-COUNT NOT = 0
005580             MOVE 'INVALID PARAMETER' TO PDF-SHORT-MESSAGE
005590             MOVE 'THERE ARE NO VALID PARMS FOR THIS COMMAND'
005600             TO PDF-LONG-MESSAGE
005610             MOVE 'TLNM019E' TO PDF-TBDISPL-MESSAGE
005620             MOVE SPACES TO PDF-TBDISPL-PANEL
005630             GO TO 1999-EXIT.
005640
005650     IF ZCMD-COMMAND = 'REFRESH' OR '*' OR 'REF'
005660         PERFORM 2000-BUILD-TABLE THRU 2999-EXIT
005670         IF ZCMD-COMMAND NOT = '*'
005680             MOVE SPACES TO ZCMD-COMMAND-LINE
005690             GO TO 1999-EXIT
005700         ELSE
005710             GO TO 1999-EXIT.
005720
005730 1620-LOCATE-COMMAND.
005740     IF ZCMD-COMMAND = 'LOCATE' OR 'L' OR 'LOC'
005750         IF ZCMD-PARM-COUNT = 0
005760             MOVE 'MISSING PARAMETER' TO PDF-SHORT-MESSAGE
005770             MOVE 'EXPECTED DDNAME OR PARTIAL DDNAME TO LOCATE'
005780             TO PDF-LONG-MESSAGE
005790             MOVE 'TLNM019E' TO PDF-TBDISPL-MESSAGE
005800             MOVE SPACES TO PDF-TBDISPL-PANEL
005810             GO TO 1999-EXIT
005820
005830         ELSE
005840         IF ZCMD-PARM-COUNT > 1
005850             MOVE 'TOO MANY PARAMETERS' TO PDF-SHORT-MESSAGE
005860             MOVE 'EXPECTED ONLY ONE PARAMETER FOR THIS COMMAND'
005870             TO PDF-LONG-MESSAGE
005880             MOVE 'TLNM019E' TO PDF-TBDISPL-MESSAGE
005890             MOVE SPACES TO PDF-TBDISPL-PANEL
005900             GO TO 1999-EXIT.
005910
005920     IF ZCMD-COMMAND = 'LOCATE' OR 'L' OR 'LOC'
005930         PERFORM 5000-LOCATE THRU 5999-EXIT
005940         GO TO 1999-EXIT.
005950
005960 1630-SETPROF-COMMAND.
005970     IF ZCMD-COMMAND = 'SET' OR 'SETPROF'
005980         IF ZCMD-PARM-COUNT = 0
005990             MOVE 'MISSING PARAMETER' TO PDF-SHORT-MESSAGE
006000             MOVE 'EXPECTED EDIT PROFILE NAME'
006010             TO PDF-LONG-MESSAGE
006020             MOVE 'TLNM019E' TO PDF-TBDISPL-MESSAGE
006030             MOVE SPACES TO PDF-TBDISPL-PANEL
006040             GO TO 1999-EXIT
006050
006060         ELSE
006070         IF ZCMD-PARM-COUNT > 1
006080             MOVE 'TOO MANY PARAMETERS' TO PDF-SHORT-MESSAGE
006090             MOVE 'EXPECTED ONLY ONE PARAMETER FOR THIS COMMAND'
006100             TO PDF-LONG-MESSAGE
006110             MOVE 'TLNM019E' TO PDF-TBDISPL-MESSAGE
006120             MOVE SPACES TO PDF-TBDISPL-PANEL
006130             GO TO 1999-EXIT.
006140
006150     IF ZCMD-COMMAND = 'SET' OR 'SETPROF'
006160         IF ZCMD-PARM (1) = 'SPACES'
006170             MOVE SPACES TO PDF-EDIT-PROFILE
006180             GO TO 1999-EXIT
006190         ELSE
006200             MOVE ZCMD-PARM (1) TO PDF-EDIT-PROFILE
006210             GO TO 1999-EXIT.
006220
006230 1990-INVALID-COMMAND.
006240     MOVE 'ISPZ001' TO PDF-TBDISPL-MESSAGE.
006250     MOVE SPACES TO PDF-TBDISPL-PANEL.
006260
006270 1999-EXIT.
006280     IF PDF-RETURN-CODE = 0 AND
006290        PDF-TBDISPL-MESSAGE = SPACES
006300         PERFORM 2000-BUILD-TABLE THRU 2999-EXIT.
006310
006320/
006330 2000-BUILD-TABLE.
006340
006350     MOVE '(DDNAME DSNAME MORE SCANSW VOLSER ALTDDNAM SEQNUM)'
006360         TO PDF-NAME-LIST.
006370     CALL 'ISPLINK' USING              PDF-TBCREATE
006380                                       PDF-TABLE-ID
006390                                       PDF-DUMMY-PARAMETER
006400                                       PDF-NAME-LIST
006410                                       PDF-NOWRITE
006420                                       PDF-REPLACE.
006430
006440     CALL 'ISPLINK' USING              PDF-TBVCLEAR
006450                                       PDF-TABLE-ID.
006460
006470     MOVE '1' TO SPF-SCAN-BYTE.
006480     CALL 'ISPLINK' USING              PDF-TBSARG
006490                                       PDF-TABLE-ID.
006500
006510     MOVE SPACES TO SPF-SCAN-BYTE.
006520
006530     MOVE '(ALTDDNAM,C,A,SEQNUM,N,A)' TO PDF-NAME-LIST.
006540     MOVE 0 TO SPF-SEQUENCE.
006550     MOVE +500 TO DDN-LIST-COUNT.
006560     MOVE SPACES TO DDN-TABLE.
006570     CALL 'DDNLIST' USING              DDN-LIST-COUNT
006580                                       DDN-TABLE.
006590
006600     PERFORM 3000-BUILD-TABLE THRU 3999-EXIT
006610         VARYING DDN-INX FROM 1 BY 1
006620         UNTIL DDN-INX > DDN-LIST-COUNT.
006630
006640     CALL 'ISPLINK' USING             PDF-TBSORT
006650                                      PDF-TABLE-ID
006660                                      PDF-NAME-LIST.
006670
006680
006690 2999-EXIT.
006700     EXIT.
006710/
006720 3000-BUILD-TABLE.
006730     IF DDNAME-TABLE NOT = SPACES
006740        MOVE SPACES TO MATCH-SW
006750        PERFORM 8000-MATCH THRU 8099-EXIT
006760                VARYING PDDN-INX FROM 1 BY 1
006770                UNTIL PDDN-INX > 50
006780        IF MATCH-SW = SPACES
006790           GO TO 3999-EXIT.
006800     MOVE SPACES TO SPF-LSEL.
006810     MOVE DDN-DDNAME (DDN-INX) TO KDSNSUB-DDNAME.
006820     MOVE +15 TO KDSNSUB-COUNT.
006830     CALL 'KDSNSUB' USING              KDSNSUB-DDNAME
006840                                       KDSNSUB-COUNT
006850                                       KDSNSUB-DSN-TABLE
006860                                       KDSNSUB-VOL-TABLE.
006870     MOVE ' ' TO SPF-MORE.
006880     MOVE DDN-DDNAME (DDN-INX) TO SPF-DDNAME, SPF-ALTDDNAM.
006890     PERFORM 10000-TBADD THRU 10099-EXIT
006900         VARYING DSN-INX FROM 1 BY 1
006910         UNTIL DSN-INX > KDSNSUB-COUNT.
006920
006930 3999-EXIT. EXIT.
006940/
006950 4000-LINE-COMMAND.
006960     IF SPF-LSEL = SPACES
006970         GO TO 4999-EXIT.
006980
006990     IF SPF-LSEL = 'F' AND SPF-DDNAME = SPACES
007000         MOVE 'NO DDNAME FOUND' TO PDF-SHORT-MESSAGE
007010         MOVE SPACES TO PDF-LONG-MESSAGE
007020         MOVE 'USE THE ENTRY WITH A DDNAME PRESENT'
007030             TO PDF-LONG-MESSAGE
007040         MOVE 'TLNM019E' TO PDF-TBDISPL-MESSAGE
007050         MOVE 'LSEL' TO PDF-TBDISPL-CURSOR
007060         MOVE SPF-CSR TO PDF-TBDISPL-ROW
007070         MOVE SPACES TO PDF-TBDISPL-PANEL
007080         GO TO 4999-EXIT.
007090
007100     IF SPF-LSEL = 'F'
007110         MOVE SPACES TO PDF-SELECT-BUFFER
007120         MOVE +3 TO PDF-FULLWORD
007130         CALL 'ISPLINK' USING PDF-CONTROL PDF-DISPLAY
007140              PDF-LINE PDF-FULLWORD
007150         MOVE SPACES TO PDF-SELECT-BUFFER
007160         STRING 'CMD(FREE F(' DELIMITED BY SIZE
007170             SPF-DDNAME DELIMITED BY SPACES
007180             '))' DELIMITED BY SIZE
007190         INTO PDF-SELECT-BUFFER
007200         MOVE +80 TO PDF-SELECT-BUFFER-LENGTH
007210         CALL 'ISPLINK' USING PDF-SELECT
007220                              PDF-SELECT-BUFFER-LENGTH
007230                              PDF-SELECT-BUFFER
007240         GO TO 4999-EXIT.
007250
007260     IF SPF-LSEL = 'B'
007270         MOVE SPACES TO PDF-DSNAME
007280         STRING QUOTE DELIMITED BY SIZE
007290                SPF-DSNAME DELIMITED BY SPACES
007300                QUOTE DELIMITED BY SIZE
007310                INTO PDF-DSNAME
007320         MOVE SPF-VOLSER TO PDF-VOLSER
007330         CALL 'ISPLINK' USING         PDF-CONTROL
007340                                      PDF-DISPLAY
007350                                      PDF-SAVE
007360         CALL 'ISPLINK' USING         PDF-CONTROL
007370                                      PDF-ERRORS
007380                                      PDF-RETURN
007390         CALL 'ISPLINK' USING         PDF-BROWSE
007400                                      PDF-DSNAME
007410                                      PDF-VOLSER
007420         MOVE RETURN-CODE TO PDF-BROWSE-RETURN-CODE
007430         CALL 'ISPLINK' USING         PDF-CONTROL
007440                                      PDF-ERRORS
007450                                      PDF-CANCEL
007460         CALL 'ISPLINK' USING         PDF-CONTROL
007470                                      PDF-DISPLAY
007480                                      PDF-RESTORE
007490         IF PDF-BROWSE-RETURN-CODE NOT = 0
007500             MOVE +8 TO PDF-FULLWORD
007510             MOVE '(ZERRMSG)' TO PDF-NAME-LIST
007520             CALL 'ISPLINK' USING     PDF-VCOPY
007530                                      PDF-NAME-LIST
007540                                      PDF-FULLWORD
007550                                      PDF-TBDISPL-MESSAGE
007560                                      PDF-MOVE
007570             MOVE SPACE TO PDF-TBDISPL-PANEL
007580             MOVE SPF-CSR TO PDF-TBDISPL-ROW
007590             MOVE 'LSEL' TO PDF-TBDISPL-CURSOR
007600             GO TO 4999-EXIT
007610         ELSE
007620         MOVE SPACES TO SPF-LSEL
007630             GO TO 4999-EXIT.
007640
007650     IF SPF-LSEL = 'E'
007660         MOVE SPACES TO PDF-DSNAME
007670         STRING QUOTE DELIMITED BY SIZE
007680                SPF-DSNAME DELIMITED BY SPACES
007690                QUOTE DELIMITED BY SIZE
007700                INTO PDF-DSNAME
007710         MOVE SPF-VOLSER TO PDF-VOLSER
007720         CALL 'ISPLINK' USING         PDF-CONTROL
007730                                      PDF-DISPLAY
007740                                      PDF-SAVE
007750         CALL 'ISPLINK' USING         PDF-CONTROL
007760                                      PDF-ERRORS
007770                                      PDF-RETURN
007780         CALL 'ISPLINK' USING         PDF-EDIT
007790                                      PDF-DSNAME
007800                                      PDF-VOLSER
007810                                      PDF-DUMMY-PARAMETER
007820                                      PDF-DUMMY-PARAMETER
007830                                      PDF-DUMMY-PARAMETER
007840                                      PDF-EDIT-PROFILE
007850         MOVE RETURN-CODE TO PDF-BROWSE-RETURN-CODE
007860         CALL 'ISPLINK' USING         PDF-CONTROL
007870                                      PDF-ERRORS
007880                                      PDF-CANCEL
007890         CALL 'ISPLINK' USING         PDF-CONTROL
007900                                      PDF-DISPLAY
007910                                      PDF-RESTORE
007920         IF PDF-BROWSE-RETURN-CODE > 4
007930             MOVE +8 TO PDF-FULLWORD
007940             MOVE '(ZERRMSG)' TO PDF-NAME-LIST
007950             CALL 'ISPLINK' USING     PDF-VCOPY
007960                                      PDF-NAME-LIST
007970                                      PDF-FULLWORD
007980                                      PDF-TBDISPL-MESSAGE
007990                                      PDF-MOVE
008000             MOVE SPACE TO PDF-TBDISPL-PANEL
008010             MOVE SPF-CSR TO PDF-TBDISPL-ROW
008020             MOVE 'LSEL' TO PDF-TBDISPL-CURSOR
008030             GO TO 4999-EXIT
008040         ELSE
008050         MOVE SPACES TO SPF-LSEL
008060             GO TO 4999-EXIT.
008070
008080     MOVE '(ZSEL)' TO PDF-NAME-LIST.
008090     MOVE +256 TO PDF-SELECT-BUFFER-LENGTH
008100     CALL 'ISPLINK' USING             PDF-VCOPY
008110                                      PDF-NAME-LIST
008120                                      PDF-SELECT-BUFFER-LENGTH
008130                                      PDF-SELECT-BUFFER
008140                                      PDF-MOVE.
008150
008160     MOVE +3 TO PDF-FULLWORD.
008170     CALL 'ISPLINK' USING             PDF-CONTROL
008180                                      PDF-DISPLAY
008190                                      PDF-LINE
008200                                      PDF-FULLWORD.
008210     CALL 'ISPLINK' USING             PDF-CONTROL
008220                                      PDF-DISPLAY
008230                                      PDF-SAVE.
008240     CALL 'ISPLINK' USING             PDF-CONTROL
008250                                      PDF-ERRORS
008260                                      PDF-CANCEL.
008270     CALL 'ISPLINK' USING             PDF-SELECT
008280                                      PDF-SELECT-BUFFER-LENGTH
008290                                      PDF-SELECT-BUFFER.
008300     MOVE RETURN-CODE TO PDF-BROWSE-RETURN-CODE.
008310     CALL 'ISPLINK' USING             PDF-CONTROL
008320                                      PDF-ERRORS
008330                                      PDF-RETURN.
008340     CALL 'ISPLINK' USING             PDF-CONTROL
008350                                      PDF-DISPLAY
008360                                      PDF-RESTORE.
008370
008380     IF PDF-BROWSE-RETURN-CODE NOT = 0
008390         MOVE SPACES TO PDF-SHORT-MESSAGE
008400         MOVE PDF-BROWSE-RETURN-CODE TO WORK-NUM
008410         STRING 'RETURN CODE = ' DELIMITED BY SIZE
008420                 WORK-NUM DELIMITED BY SIZE
008430                 INTO PDF-SHORT-MESSAGE
008440         MOVE 'THE REQUESTED FUNCTION ENDED WITH A NON-ZERO RETURN
008450-    'CODE'
008460         TO PDF-LONG-MESSAGE
008470         MOVE 'TLNM019E' TO PDF-TBDISPL-MESSAGE
008480         MOVE 'LSEL' TO PDF-TBDISPL-CURSOR
008490         MOVE SPACES TO PDF-TBDISPL-PANEL
008500         MOVE SPF-CSR TO PDF-TBDISPL-ROW.
008510
008520 4999-EXIT.
008530     EXIT.
008540/
008550 5000-LOCATE.
008560     CALL 'ISPLINK' USING              PDF-TBTOP
008570                                       PDF-TABLE-ID.
008580
008590     MOVE ZCMD-PARM (1) TO SPF-ALTDDNAM.
008600     MOVE '(ALTDDNAM)' TO PDF-NAME-LIST.
008610     MOVE '(GE)' TO PDF-COND-LIST.
008620     MOVE 'ZTDTOP' TO PDF-TBQUERY-CRP.
008630     CALL 'ISPLINK' USING              PDF-TBSCAN
008640                                       PDF-TABLE-ID
008650                                       PDF-NAME-LIST
008660                                       PDF-DUMMY-PARAMETER
008670                                       PDF-DUMMY-PARAMETER
008680                                       PDF-NEXT
008690                                       PDF-DUMMY-PARAMETER
008700                                       PDF-TBQUERY-CRP
008710                                       PDF-COND-LIST.
008720
008730     IF RETURN-CODE = 8
008740         CALL 'ISPLINK' USING          PDF-TBBOTTOM
008750                                       PDF-TABLE-ID
008760                                       PDF-DUMMY-PARAMETER
008770                                       PDF-DUMMY-PARAMETER
008780                                       PDF-DUMMY-PARAMETER
008790                                       PDF-TBQUERY-CRP
008800     ELSE
008810     IF SPF-ZTDTOP NOT = 1 AND
008820         SPF-DDNAME NOT = ZCMD-PARM (1)
008830         MOVE SPACES TO SPF-DDNAME
008840         MOVE '(DDNAME)' TO PDF-NAME-LIST
008850         MOVE '(NE)' TO PDF-COND-LIST
008860         CALL 'ISPLINK' USING          PDF-TBSCAN
008870                                       PDF-TABLE-ID
008880                                       PDF-NAME-LIST
008890                                       PDF-DUMMY-PARAMETER
008900                                       PDF-DUMMY-PARAMETER
008910                                       PDF-PREVIOUS
008920                                       PDF-DUMMY-PARAMETER
008930                                       PDF-TBQUERY-CRP
008940                                       PDF-COND-LIST.
008950
008960     MOVE SPACES TO ZCMD-COMMAND-LINE.
008970
008980 5999-EXIT.
008990     EXIT.
009000/
009010 8000-MATCH.
009020     MOVE DDN-DDNAME (DDN-INX) TO DDNAME-TABLE-1.
009030     MOVE PASSED-DDNAME (PDDN-INX) TO DDNAME-TABLE-2.
009040
009050     PERFORM 8100-CHECK THRU 8199-EXIT
009060          VARYING PDDN-1 FROM 1 BY 1 UNTIL PDDN-1 > 8.
009070
009080     IF DDNAME-TABLE-1 = DDNAME-TABLE-2
009090        MOVE '1' TO MATCH-SW.
009100
009110 8099-EXIT. EXIT.
009120
009130 8100-CHECK.
009140     SET PDDN-2 TO PDDN-1.
009150     IF PASSED-DDNAME-LET-2 (PDDN-2) = '*'
009160        AND (PDDN-2 = 8 OR
009170             PASSED-DDNAME-LET-2 (PDDN-2 + 1) = SPACES)
009180
009190        PERFORM 8200-SPACE THRU 8299-EXIT
009200                VARYING PDDN-1 FROM PDDN-2 BY 1
009210                UNTIL PDDN-1 > 8
009220        MOVE SPACES TO PASSED-DDNAME-LET-2 (PDDN-2).
009230
009240 8199-EXIT. EXIT.
009250
009260 8200-SPACE.
009270     MOVE SPACES TO PASSED-DDNAME-LET-1 (PDDN-1).
009280 8299-EXIT. EXIT.
009290/
009300 10000-TBADD.
009310     ADD 1 TO SPF-SEQUENCE.
009320     MOVE KDSNSUB-DSN (DSN-INX) TO SPF-DSNAME.
009330     SET VOL-INX TO DSN-INX.
009340     MOVE KDSNSUB-VOL (VOL-INX) TO SPF-VOLSER.
009350
009360     IF DSN-INX > 1
009370         MOVE SPACES TO SPF-DDNAME, SPF-SCAN-BYTE
009380     ELSE
009390         MOVE '1' TO SPF-SCAN-BYTE.
009400
009410     IF KDSNSUB-COUNT > 1
009420         MOVE '+' TO SPF-MORE.
009430
009440     IF KDSNSUB-COUNT = DSN-INX
009450         MOVE SPACE TO SPF-MORE.
009460
009470     CALL 'ISPLINK' USING              PDF-TBADD
009480                                       PDF-TABLE-ID
009490                                       PDF-ORDER.
009500
009510 10099-EXIT. EXIT.
