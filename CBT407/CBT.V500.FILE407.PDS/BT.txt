PROC 1 NUMBER LIST SYMLIST CONLIST

/*        THIS CLIST CALLS PROGRAM BLDLTEST A USER-SPECIFIED
/*        NUMBER OF TIMES.  BLDLTEST, IN TURN, DOES A SINGLE BLDL
/*        LOCATING A FIXED SET OF LINK LIST MODULES.  UPDATE
/*        BLDLTEST TO INCLUDE A SET OF AT LEAST 100 MODULES FROM
/*        VARIOUS LINK LIST LIBRARIES AT YOUR SHOP.  NOTE THAT
/*        BLDL DEMANDS THAT THE LIST IS SORTED IN ASCENDING
/*        SEQUENCE.  TO CALL BLDLTEST 10 TIMES, FOR EXAMPLE,
/*        ASSUMING THAT THIS CLIST IS PLACED IN A LIBRARY
/*        CONCATENATED TO DDNAME SYSPROC, JUST TYPE %BT 10
/*
/*        NOTE THAT THE LIBRARY SPECIFIED IN THE CALL BELOW
/*        SHOULD BE THE LIBRARY INTO WHICH YOU LINK-EDITED
/*        BLDLTEST.

IF &SYMLIST ¬= THEN CONTROL SYM
IF &CONLIST ¬= THEN CONTROL CON
IF &LIST    ¬= THEN CONTROL LIST
SET &C0 = &SYSCPU
SET &S0 = &SYSSRV
SET &T0 = &SYSTIME
SET &IX = 0
TIME
DO WHILE &IX < &NUMBER


    CALL 'SSFP.SFP.LOADLIB(BLDLTEST)' /* <=== CHANGE LIBRARY DSN.


    SET &IX = &IX + 1
END
TIME
SET &C1 = &SYSCPU
SET &S1 = &SYSSRV
SET &T1 = &SYSTIME
SET &S = &S1 - &S0
SET &T0 = &EVAL(&EVAL(&SUBSTR(1:2,&T0)) * 3600 +
        + &EVAL(&SUBSTR(4:5,&T0)) * 60   +
        + &EVAL(&SUBSTR(7:8,&T0)))
SET &T1 = &EVAL(&EVAL(&SUBSTR(1:2,&T1)) * 3600 +
        + &EVAL(&SUBSTR(4:5,&T1)) * 60   +
        + &EVAL(&SUBSTR(7:8,&T1)))
SET &T = &T1 - &T0
SET &L = &LENGTH(&STR(&C0))
SET &L1 = &L - 3
SET &L2 = &L - 1
SET &C0 = &EVAL(&EVAL(&SUBSTR(1:&L1,&STR(&C0))) * 100 +
        + &EVAL(&SUBSTR(&L2:&L,&STR(&C0))))
SET &L = &LENGTH(&STR(&C1))
SET &L1 = &L - 3
SET &L2 = &L - 1
SET &C1 = &EVAL(&EVAL(&SUBSTR(1:&L1,&STR(&C1))) * 100 +
        + &EVAL(&SUBSTR(&L2:&L,&STR(&C1))))
SET &C = &C1 - &C0
WRITE CPU: &C..  SERVICE: &S..  SECONDS: &T..
