  /* --------------------------- REXX -------------------------------- *
  | Macro:         BODY                                               |
  | Syntax:        BODY                                               |
  | Parameter:     none                                               |
  | Function:      Counts the number of lines in the )BODY section of |
  |                an ISPF-Panel and the width of those lines.        |
  | Author:        Norbert Haas, noaDatex.de                          |
  | Updates:       Lionel Dyck                                        |
  |                - 02/29/20 Recommend addpop row/column             |
  |                - 02/26/20 check for and support expand            |
  |                - 01/26/20 add RESET                               |
  |                - 11/26/19 add msgline with WINDOW(dd,ww)          |
  |                - 7/7/17 change to exclude all non BODY records    |
  * ----------------------------------------------------------------- */
  Address ISREdit
  "MACRO"

  /* ---------------------------------------------------- *
  | Find the )BODY statement and extract the Expand info |
  | and save the 1st record of the body text             |
  * ---------------------------------------------------- */
  'Reset'
  "FIND FIRST ')BODY' 1"
  IF rc = 0 THEN DO
    "(z1) = LINENUM .ZCSR"
    '(data) = line' z1
    data = translate(data)
    if pos('EXPAND(',data) > 0
    then do
      parse value data with . "EXPAND("exp")" .
      expand = 'Expand('exp')'
    end
    else expand = ''
    /* --------------------------------- *
    | Now find the end of the body text |
    * --------------------------------- */
    "LABEL "z1" = .LINET"
    'exclude .zfirst .LINET all'
    "FIND NEXT ')' 1"
    IF rc = 0 THEN DO
      "(z2) = LINENUM .ZCSR"
      "LABEL "z2+1" = .LINEE"
      'exclude .LINEE .zlast all'
      anz = z2 - z1 - 1
      "(panel) = MEMBER"
      len = 1
      do i = z1+1 to z2-1
        '(data) = line' i
        data = strip(data,'T')
        if length(data) > len then len = length(data)
      end
      /* -------------------------------------------------------- *
      | Found the end of the body section so report:             |
      | 1. Suggested Addpop if the user chooses to define Window |
      | 2. Suggested )BODY with Window and Expand                |
      * -------------------------------------------------------- */
      report = ')Body Window('len','anz')' expand
      'Line_After' z1 '= noteline (report)'
      x = get_pop(len anz)
      report = 'Suggest Using AddPop Row('word(x,2)') Column('word(x,1)')' ,
        'if this is a popup panel.'
      'Line_before' z1 '= noteline (report)'
      zedsmsg = 'BODY' anz 'lines'
      zedlmsg = 'Panel' panel 'has' anz 'line in the BODY-Section.'
      Address ISPEXEC "SETMSG MSG(isrz000)"
      "FIND FIRST ')BODY' 1"
    END
    /* ---------------------------------------- *
    | If no next panel section report that out |
    * ---------------------------------------- */
    ELSE DO
      zedsmsg = 'Error (END missing)'
      zedlmsg = 'No )END-Section found in this panel.'
      Address ISPEXEC "SETMSG MSG(isrz001)"
    END
  END
  /* ----------------------------------- *
  | If no )Body section report that out |
  * ----------------------------------- */
  ELSE DO
    zedsmsg = 'Error (BODY missing)'
    zedlmsg = 'No )BODY-Section found in this panel.'
    Address ISPEXEC "SETMSG MSG(isrz001)"
  END

  EXIT

  /* -------------------------------- *
  | Get the suggested AddPop setting |
  * -------------------------------- */
Get_Pop: procedure
  arg col  row
  col = ((80 - col) % 2 ) +1
  row = ((23 - row) % 4 ) +1
  data = col row
  return data
