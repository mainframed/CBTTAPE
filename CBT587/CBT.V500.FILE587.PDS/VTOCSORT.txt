         TITLE 'VTOC COMMAND  SORT  ROUTINE'
***********************************************************************
*         MODIFICATIONS
* NUMBER    DATE    PERSON    REASON
*  001G    050284   GATES     RENAME VTFMT MACRO TO VTCFMT,
*                              RENAME VTLEAVE MACRO TO VTCLEAVE,
*                              RENAME VTOCOM MACRO TO VTCCOM,
*                              RENAME VTOCPARS MACRO TO VTCPARS,
*                              RENAME PDEDSNAM MACRO TO VTCPDEDS,
*                               RENAME ENTER TO VTCENTER,
*                              VTOC MORE TRANSPORTABLE.
*                                                                     *
*                                                                     *
* TITLE -      VTOC COMMAND  SORT  ROUTINE                            *
*                                                                     *
* FUNCTION -   PUT THIS FORMATTED DSCB INTO THE SORTED LIST.          *
*                                                                     *
* OPERATION -  IF THIS IS A NOSORT RUN, JUST CALL THE PRINT ROUTINE.  *
*              TO BUILD THE SORTED LIST, FIRST DO A SIMPLE HASH       *
*              ON THE FIRST CHARACTER.  BUILD UP TO 256 SEPARATE      *
*              LISTS TO SAVE SORT TIME.  THEN SEARCH THROUGH THESE    *
*              LISTS SEQUENTIALLY.                                    *
*                                                                     *
* INPUT -      VTOC COMMON AREA ( VTOCOM )                            *
*              POINTED TO BY REGISTER 1                               *
*              USE PARSE DATA, CURRENT FORMATTED DSCB, SORTED LIST    *
*                                                                     *
* OUTPUT -     THE FORMATTED DSCB IS PLACED INTO THE SORTED LIST.     *
*                                                                     *
* ATTRIBUTES - REENTRANT, REUSEABLE, REFRESHABLE.                     *
*                                                                     *
*                                                                     *
*         PROGRAMMED BY R. L. MILLER  (415) 485-6241                  *
*                                                                     *
*                                                                     *
***********************************************************************
*
*   NOTE - EXTENSION -
*   THIS VERSION SORTS BY DSNAME ONLY.
*   HOPEFULLY IT CAN BE MADE TO SORT FOR MULTIPLE KEYS AND MOST OF THE
*   ITEMS IN VTFMT.
*
         EJECT
VTOCSORT VTCENTER 12,24       DO THE HOUSEKEEPING                  001G
         LR    R11,R1         SAVE ADDR OF VTOCOM
         USING VTOCOM,R11     SET ITS ADDRESSABILITY
         L     R9,ADDRANSR    POINT TO THE PARSE ANSWER
         USING PDL,R9         SET ITS ADDRESSABILITY
         USING SORTWORK,R13   SET ADDRESSABILITY FOR LOCAL WORK AREA
         SPACE 3
*
*        IS THIS A NOSORT RUN ?
*        IF SO, JUST CALL PRINT
*
         CLI   SORTK+1,2      IS THIS NOSORT?
         BNE   GOSORT         NO, KEEP ON TRUCKIN'
         VTCALL PRNT          YES, CALL PRINT AND GET OUT
         B     SORTRET        GET OUT OF HERE
*
*        PUT THIS ENTRY WHERE IT BELONGS
*
GOSORT   L     R3,FORMATAD    POINT TO THE FORMATTED DSCB
         USING VTFMT,R3       SET ADDRESSABILITY
         LA    R4,VTFDSN      POINT TO THE FIRST CHARACTER
         SR    R2,R2          CLEAR A REGISTER
         IC    R2,0(R4)       GET THE FIRST CHARACTER
         SLA   R2,2           MULTIPLY BY FOUR
         LA    R2,VTCSORTH(R2)  POINT TO THE CORRECT LIST
         ICM   R5,B'1111',0(R2) GET THE HEAD OF THE LIST
         BNZ   NOTFIRST       IF NON-ZERO, SEARCH THE LIST
*
*        FIRST ENTRY ON THE LIST, IT'S EASY
*
         ST    R3,0(R2)       START UP THE LIST
         B     SORTRET        THEN RETURN
*
*        FIND A SLOT FOR THIS ENTRY
*              FIRST GET THE SHORTER DSN LENGTH
*
NOTFIRST LH    R1,VTFDSNL     LENGTH OF NEW ENTRY
         CH    R1,VTFDSNL-VTFMT(R5)  IS IT SMALLER THAN THIS LIST ENTRY
         BNH   GOTLEN         YES, NEW ONE IS OK
         LH    R1,VTFDSNL-VTFMT(R5)  NO, GET THE LIST ENTRY LENGTH
GOTLEN   BCTR  R1,0           SUBTRACT ONE FOR EX
         EX    R1,COMPDSN     COMPARE THE DSN'S
         BL    NEXTENT        LIST ENTRY IS LOWER, UP THE CHAIN
         BE    CHECKLEN       IDENTICAL, SHORTER ONE WINS
*
*        THE NEW ENTRY GOES HERE
*
INSERT   ST    R3,0(R2)       SAVE THE NEW POINTER
         ST    R5,VTFNEXT     JUST BEFORE THIS LIST ENTRY
         B     SORTRET        THEN EXIT
*
*        THE DSNAMES ARE IDENTICAL AS FAR AS THEY COMPARE
*              THE ONE WITH THE SHORTER DSNAME IS FIRST
*
CHECKLEN LA    R1,1(R1)       GET BACK TO THE ORIGINAL LENGTH
         CH    R1,VTFDSNL     IS THIS THE NEW ENTRY LENGTH
         BE    INSERT         YES, USE IT (TIE GOES TO NEW ENTRY)
*
*        GET THE NEXT ENTRY ON THIS LIST
*
NEXTENT  LA    R2,VTFNEXT-VTFMT(R5)  POINT BACK TO THIS ENTRY
         ICM   R5,B'1111',VTFNEXT-VTFMT(R5)  GET THE NEXT ENTRY
         BNZ   NOTFIRST       THERE IS ONE, CHECK IT
         ST    R3,0(R2)       LAST ENTRY ON THE LIST, PUT IT THERE
*
*        RETURN
*
SORTRET  VTCLEAVE EQ,RC=0                                          001G
*
*
*
*        PROGRAM CONSTANTS
*
COMPDSN  CLC   VTFDSN-VTFMT(0,R5),VTFDSN    EXECUTED COMPARE
*
*
         EJECT
*
*
*        P A R S E   C O N T R O L   L I S T
*
*
         COPY  VTCPARS                                             001G
*
*        DYNAMIC WORK AREA
*
         SPACE 3
SORTWORK DSECT
         DS    18A            PRINT ROUTINE SAVE AREA
         SPACE
         DS    0D
LENWORK  EQU   *-SORTWORK
*
*        VTOC COMMAND COMMON AREA
*
         VTCCOM                                                    001G
         SPACE 3
*
*        FORMATTED DSCB
*
         VTCFMT                                                    001G
         SPACE 3
         VTCPDEDS                                                  001G
         SPACE 3
         END
