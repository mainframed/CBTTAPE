KSXS     TITLE 'KSXS - SINGLE KEYWORD SET FUNCTION (TOPSEC).'
KSXS     START 0
         SPACE 1
*- - - - - -  - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
*                                                                    *
* PURPOSE :    PRIVATE USE ONLY.                                     *
* ---------                                                          *
*                                                                    *
* AUTHOR :     P.A. MOINIL                                           *
* --------     COMPUTING CENTRE                                      *
*              J.R.C. - ISPRA ESTABLISHMENT                          *
*              21020 ISPRA (VA), ITALY                               *
*                                                                    *
*- - - - - -  - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
         SPACE 1
         PRINT NOGEN
        $DEFREG
        $XENT  BASE=(R11,R12)
         PRINT GEN
         EJECT
        CALL   ISPLINK,(VDEF,LL,BUFFER,TYPEC,LENBUF),VL
         LTR   R15,R15
         BNZ   ERR1
        CALL   ISPLINK,(VDEF,DL,LINENO,TYPEF,LEN4),VL
         LTR   R15,R15
         BNZ   ERR2
        CALL   ISPLINK,(EDIT,LEN0,EDMAC),VL
         LTR   R15,R15
         BNZ   ERR3
        CALL   ISPLINK,(EDIT,LEN0,EDDST),VL
         LTR   R15,R15
         BNZ   ERR4
        CALL   ISPLINK,(EDIT,LEN0,EDBDS),VL
         LTR   R15,R15
         BNZ   ERR5
         OC    LINENO,LINENO
         BZ    ERR6
         OC    COLNO,COLNO
         BZ    ERR6
         L     R3,RIGHTB
         CL    R3,LENBUF
         BH    ERR7
        CALL   ISPLINK,(EDIT,LEN0,EDGLN),VL
         LTR   R15,R15
         BNZ   ERR8
        CALL   ISPLINK,(EDIT,LEN0,EDMSK),VL
         LTR   R15,R15
         BNZ   ERR9
         MVC   WKAREA,BUFFER
         LA    R2,1
         LA    R4,WKAREA-1
         ALR   R3,R4
         AL    R4,LEFTB
         CLI   0(R4),C'*'
         BE    *+L'*+8
         CLI   0(R4),C' '
         BNE   *+L'*+8
         BXLE  R4,R2,*-8
         B     ERR10
         LR    R14,R4
         BXLE  R4,R2,*+L'*+4
         B     ERR11
         CLI   0(R4),C' '
         BNE   *-12
         LR    R15,R4
         SLR   R15,R14
         CH    R15,=Y(L'KEYW)
         BH    ERR11
         BCT   R15,*+L'*+6
         MVC   KEYW(*-*),0(R14)
         EX    R15,*-6
         MVI   SWEN1,2
         MVI   SWEN2,2
AGAIN    LM    R2,R3,KEYW
         STM   R2,R3,DBLWRD
         SRDL  R2,*-*
SWEN1    EQU   *-1
         STM   R2,R3,DBLWRD
         XC    DBLWRD,KEYW
         LM    R2,R3,DBLWRD
         SRDL  R2,*-*
SWEN2    EQU   *-1
         STM   R2,R3,DBLWRD
         XC    DBLWRD,KEYW
         CLI   SWEN1,2
         BNE   *+L'*+18
         XI    SWEN1,3
         OI    SWEN2,1
         MVC   KEYW,DBLWRD
         B     AGAIN
         XC    DBLWRD,=XL8'FFFFFFFFFFFFFFFF'
         MVI   SWEN1,*-*
         MVI   SWEN2,*-*
         MVI   BUFFER,C' '
         MVC   BUFFER+1(L'BUFFER-1),BUFFER
         MVC   BUFFER+9(2),=CL2'DC'
         MVC   BUFFER+15(4),=CL4'XL8'''
         LA    R1,BUFFER+19
         LA    R2,DBLWRD
         LA    R3,L'DBLWRD
LOOP     XR    R0,R0
         IC    R0,0(R2)
         STC   R0,1(R1)
         SRL   R0,4
         STC   R0,0(R1)
         NC    0(2,R1),=XL2'0F0F'
         TR    0(2,R1),=CL16'0123456789ABCDEF'
         LA    R1,2(R1)
         LA    R2,1(R2)
         BCT   R3,LOOP
         MVI   0(R1),C''''
        CALL   ISPLINK,(EDIT,LEN0,EDPLN),VL
         LTR   R15,R15
         BNZ   ERR12
         SPACE 1
         XR    R10,R10             SET RETURN CODE
         PRINT NOGEN
LEAVE   $XRET  CC=(R10)            RETURN
         PRINT GEN
         EJECT
ERR1     MVC   MLOP,=CL8'BUFFER  '
         B     ERRA
ERR2     MVC   MLOP,=CL8'... ... '
ERRA     MVC   MLFU,VDEF
         B     ERRXR
ERR3     MVC   MLOP,=CL8'MACRO   '
         B     ERRE
ERR4     MVC   MLOP,=CL8'CURSOR  '
         B     ERRE
ERR5     MVC   MLOP,=CL8'BOUNDS  '
         B     ERRE
ERR6     MVC   MLOP,=CL8'CURSOR  '
         B     ERRB
ERR7     MVC   MLOP,=CL8'BUFFER  '
ERRB     MVC   MLFU,=CL8'CHECK   '
         B     ERRC
ERR8     MVC   MLOP,=CL8'GET LINE'
         B     ERRE
ERR9     MVC   MLOP,=CL8'MASKLINE'
         B     ERRE
ERR10    MVC   MLOP,=CL8'BUFFER  '
         MVC   MLFU,=CL8'EMPTY   '
         B     ERRC
ERR11    MVC   MLOP,=CL8'ARGUMENT'
         MVC   MLFU,=CL8'INVALID '
ERRC     XR    R15,R15
         B     ERRXR
ERR12    MVC   MLOP,=CL8'PUT LINE'
ERRE     MVC   MLFU,EDIT
ERRXR    CVD   R15,DBLWRD          EDIT RETURN CODE RECEIVED
         MVC   MSRC,=XL4'40202120'
         ED    MSRC,DBLWRD+6
        CALL   ISPLINK,(VREP,MS,MSL,MSV),VL
         LR    R10,R15
        CALL   ISPLINK,(VREP,ML,MLL,MLV),VL
         SLL   R10,8
         OR    R10,R15
        CALL   ISPLINK,(SETM,MSGID),VL
         SLL   R10,8
         OR    R10,R15
         LTR   R10,R10
         BNZ   LEAVE
         LA    R10,100             SET RETURN CODE
         B     LEAVE
         EJECT
* ------ CONSTANTS AND WORK-AREAS.
         SPACE 1
DBLWRD   DC    D'0'
         SPACE 1
VDEF     DC    CL8'VDEFINE '
EDIT     DC    CL8'ISREDIT '
VREP     DC    CL8'VREPLACE'
SETM     DC    CL8'SETMSG  '
MSGID    DC    CL8'ISRZ001 '
TYPEC    DC    CL8'CHAR    '
TYPEF    DC    CL8'FIXED   '
LEN0     DC    F'0'
LEN4     DC    F'4'
LENBUF   DC    A(BUFLEN)
MSL      DC    A(MSVL)
MLL      DC    A(MLVL)
         SPACE 1
KEYW     DC    CL8' '
         SPACE 1
LINENO   DC    F'0'
COLNO    DC    F'0'
LEFTB    DC    F'0'
RIGHTB   DC    F'0'
BUFFER   DC    CL80' '
BUFLEN   EQU   *-BUFFER
WKAREA   DC    CL(BUFLEN)' '
         SPACE 1
LL       DC    C'(BUFFER)'
DL       DC    C'(LINENO,COLNO,LEFTB,RIGHTB)'
MS       DC    C'(ZEDSMSG)'
ML       DC    C'(ZEDLMSG)'
         SPACE 1
EDMAC    DC    C'¢MACRO NOPROCESS¢'
EDDST    DC    C'¢(LINENO,COLNO) = CURSOR¢'
EDBDS    DC    C'¢(LEFTB,RIGHTB) = BOUNDS¢'
EDGLN    DC    C'¢(BUFFER) = LINE &&LINENO¢'
EDMSK    DC    C'¢MASKLINE = " "¢'
EDPLN    DC    C'¢LINE_AFTER &&LINENO = MASKLINE + (BUFFER)¢'
         SPACE 1
MSV      DC    C'RC ='
MSRC     DC    CL4' '
MSVL     EQU   *-MSV
MLV      DC    C'-- ERROR AT FUNCTION : '
MLFU     DC    CL8' ',C' / OPERATION : '
MLOP     DC    CL8' ',C' --'
MLVL     EQU   *-MLV
         EJECT
         LTORG
         SPACE 1
         DROP  R11,R12 - - - - - - END OF BASE REGISTER ADDRESSABILITY
         SPACE 2
         END
