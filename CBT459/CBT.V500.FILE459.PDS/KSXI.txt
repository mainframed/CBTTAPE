KSXI     TITLE 'KSXI - INVERT HEXADECIMAL FUNCTION (TOPSECT).'
KSXI     START 0
         SPACE 1
*- - - - - -  - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
*                                                                    *
* PURPOSE :    PRIVATE USE ONLY.                                     *
* ---------                                                          *
*                                                                    *
* AUTHOR :     P.A. MOINIL                                           *
* --------     COMPUTING CENTRE                                      *
*              J.R.C. - ISPRA ESTABLISHMENT                          *
*              21020 ISPRA (VA), ITALY                               *
*                                                                    *
*- - - - - -  - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
         SPACE 1
         PRINT NOGEN
        $DEFREG
        $XENT  BASE=(R11,R12)
         PRINT GEN
         EJECT
        CALL   ISPLINK,(VDEF,LL,BUFFER,TYPEC,LENBUF),VL
         LTR   R15,R15
         BNZ   ERR1
        CALL   ISPLINK,(VDEF,DL,LINENO,TYPEF,LEN4),VL
         LTR   R15,R15
         BNZ   ERR2
        CALL   ISPLINK,(EDIT,LEN0,EDMAC),VL
         LTR   R15,R15
         BNZ   ERR3
        CALL   ISPLINK,(EDIT,LEN0,EDDST),VL
         LTR   R15,R15
         BNZ   ERR4
        CALL   ISPLINK,(EDIT,LEN0,EDBDS),VL
         LTR   R15,R15
         BNZ   ERR5
         OC    LINENO,LINENO
         BZ    ERR6
         OC    COLNO,COLNO
         BZ    ERR6
         L     R3,RIGHTB
         CL    R3,LENBUF
         BH    ERR7
        CALL   ISPLINK,(EDIT,LEN0,EDGLN),VL
         LTR   R15,R15
         BNZ   ERR8
        CALL   ISPLINK,(EDIT,LEN0,EDMSK),VL
         LTR   R15,R15
         BNZ   ERR9
         MVC   WKAREA,BUFFER
         LA    R2,1
         LA    R4,WKAREA-1
         ALR   R3,R4
         AL    R4,LEFTB
         CLI   0(R4),C'*'
         BE    *+L'*+8
         CLI   0(R4),C' '
         BNE   *+L'*+8
         BXLE  R4,R2,*-8
         B     ERR10
         CLI   0(R4),C''''
         BNE   *+L'*+8
         BXLE  R4,R2,*+L'*+4
         B     ERR10
         CLI   0(R3),C' '
         BNE   *+L'*+12
         SLR   R3,R2
         CLR   R3,R4
         BNL   *-12
         B     ERR10
         CLI   0(R3),C''''
         BNE   *+L'*+12
         SLR   R3,R2
         CLR   R3,R4
         BNL   *+L'*+4
         B     ERR10
         SLR   R3,R4
         LA    R3,1(R3)
         LA    R2,16
NEXT     CLR   R3,R2
         BNH   *+L'*+6
         LR    R1,R2
         B     *+L'*+2
         LR    R1,R3
         SR    R3,R1
         MVI   BUFFER,C' '
         MVC   BUFFER+1(L'BUFFER-1),BUFFER
         MVC   BUFFER+9(2),=CL2'DC'
         CVD   R1,WORKD
         MVC   WORKD(4),=XL4'40202120'
         ED    WORKD(4),WORKD+6
         CLI   WORKD+2,C' '
         BNE   *+L'*+20
         MVC   BUFFER+15(2),=CL2'XL'
         MVC   BUFFER+17(1),WORKD+3
         LA    R5,BUFFER+18
         B     *+L'*+16
         MVC   BUFFER+15(2),=CL2'XL'
         MVC   BUFFER+17(2),WORKD+2
         LA    R5,BUFFER+19
         MVI   0(R5),C''''
         LA    R5,1(R5)
LOOP     XR    R0,R0
         XI    0(R4),X'FF'
         IC    R0,0(R4)
         STC   R0,1(R5)
         SRL   R0,4
         STC   R0,0(R5)
         NC    0(2,R5),=XL2'0F0F'
         TR    0(2,R5),=CL16'0123456789ABCDEF'
         LA    R5,2(R5)
         LA    R4,1(R4)
         BCT   R1,LOOP
         MVI   0(R5),C''''
        CALL   ISPLINK,(EDIT,LEN0,EDPLN),VL
         LTR   R15,R15
         BNZ   ERR11
         LTR   R3,R3
         BNP   EXIT
         L     R0,LINENO
         AL    R0,=F'1'
         ST    R0,LINENO
         B     NEXT
         SPACE 1
EXIT     XR    R10,R10             SET RETURN CODE
         PRINT NOGEN
LEAVE   $XRET  CC=(R10)            RETURN
         PRINT GEN
         EJECT
ERR1     MVC   MLOP,=CL8'BUFFER  '
         B     ERRA
ERR2     MVC   MLOP,=CL8'... ... '
ERRA     MVC   MLFU,VDEF
         B     ERRET
ERR3     MVC   MLOP,=CL8'MACRO   '
         B     ERRD
ERR4     MVC   MLOP,=CL8'CURSOR  '
         B     ERRD
ERR5     MVC   MLOP,=CL8'BOUNDS  '
         B     ERRD
ERR6     MVC   MLOP,=CL8'CURSOR  '
         B     ERRB
ERR7     MVC   MLOP,=CL8'BUFFER  '
ERRB     MVC   MLFU,=CL8'CHECK   '
         B     ERRC
ERR8     MVC   MLOP,=CL8'GET LINE'
         B     ERRD
ERR9     MVC   MLOP,=CL8'MASKLINE'
         B     ERRD
ERR10    MVC   MLOP,=CL8'BUFFER  '
         MVC   MLFU,=CL8'EMPTY   '
ERRC     XR    R15,R15
         B     ERRET
ERR11    MVC   MLOP,=CL8'PUT LINE'
ERRD     MVC   MLFU,EDIT
ERRET    CVD   R15,WORKD
         MVC   MSRC,=XL4'40202120'
         ED    MSRC,WORKD+6
        CALL   ISPLINK,(VREP,MS,MSL,MSV),VL
         LR    R10,R15
        CALL   ISPLINK,(VREP,ML,MLL,MLV),VL
         SLL   R10,8
         OR    R10,R15
        CALL   ISPLINK,(SETM,MSGID),VL
         SLL   R10,8
         OR    R10,R15
         LTR   R10,R10
         BNZ   LEAVE
         LA    R10,100             SET RETURN CODE
         B     LEAVE
         EJECT
* ------ CONSTANTS AND WORK-AREAS.
         SPACE 1
WORKD    DC    D'0'
         SPACE 1
VDEF     DC    CL8'VDEFINE '
EDIT     DC    CL8'ISREDIT '
VREP     DC    CL8'VREPLACE'
SETM     DC    CL8'SETMSG  '
MSGID    DC    CL8'ISRZ001 '
TYPEC    DC    CL8'CHAR    '
TYPEF    DC    CL8'FIXED   '
LEN0     DC    F'0'
LEN4     DC    F'4'
LENBUF   DC    A(BUFLEN)
MSL      DC    A(MSVL)
MLL      DC    A(MLVL)
         SPACE 1
LINENO   DC    F'0'
COLNO    DC    F'0'
LEFTB    DC    F'0'
RIGHTB   DC    F'0'
BUFFER   DC    CL80' '
BUFLEN   EQU   *-BUFFER
WKAREA   DC    CL(BUFLEN)' '
         SPACE 1
LL       DC    C'(BUFFER)'
DL       DC    C'(LINENO,COLNO,LEFTB,RIGHTB)'
MS       DC    C'(ZEDSMSG)'
ML       DC    C'(ZEDLMSG)'
         SPACE 1
EDMAC    DC    C'¢MACRO NOPROCESS¢'
EDDST    DC    C'¢(LINENO,COLNO) = CURSOR¢'
EDBDS    DC    C'¢(LEFTB,RIGHTB) = BOUNDS¢'
EDGLN    DC    C'¢(BUFFER) = LINE &&LINENO¢'
EDMSK    DC    C'¢MASKLINE = " "¢'
EDPLN    DC    C'¢LINE_AFTER &&LINENO = MASKLINE + (BUFFER)¢'
         SPACE 1
MSV      DC    C'RC ='
MSRC     DC    CL4' '
MSVL     EQU   *-MSV
MLV      DC    C'-- ERROR AT FUNCTION : '
MLFU     DC    CL8' ',C' / OPERATION : '
MLOP     DC    CL8' ',C' --'
MLVL     EQU   *-MLV
         EJECT
         LTORG
         SPACE 1
         DROP  R11,R12 - - - - - - END OF BASE REGISTER ADDRESSABILITY
         SPACE 2
         END
