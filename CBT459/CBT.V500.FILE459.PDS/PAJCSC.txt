PROC     0
/* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - */
/*       SHORT : System Sector Short Communication.                  */
/*       Pre-requisite : TSO command EUSID.                          */
/*       Note : Short Communication is stored in member SC$PROF of   */
/*              user ISPF profile data-set (user.SPF.PROF).      (PM)*/
/* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - */
         CONTROL MAIN NOMSG
         IF &SYSISPF EQ &STR(NOT ACTIVE) THEN DO
           WRITE *** NOT AVAILABLE OUTSIDE ISPF ***
           EXIT CODE(0)
           END
         EUSID
         SET &RC = &LASTCC
         IF &RC NE 0 THEN DO
           ISPEXEC SETMSG MSG(PAJC000Z)
           EXIT CODE(0)
           END
         IF &EURGRNM NE SYS AND THEN DO
           ISPEXEC SETMSG MSG(PAJC000X)
           EXIT CODE(0)
           END
         SET &SCID = &Z
         SET &SCDSS = &STR(SPF)
         SET &SCDST = &STR(PROF)
         SET &SCMEM = &STR(SC$PROF)
         SET &SCTEST = &Z
         ISPEXEC VGET (SCLANG) PROFILE
         ISPEXEC CONTROL ERRORS RETURN
AGAIN:   IF &SYSUID EQ WIZARD THEN ISPEXEC DISPLAY PANEL(PAJSC00)
         ELSE ISPEXEC DISPLAY PANEL(PAJSC0)
         SET &RC = &LASTCC
         IF &RC LE 4 THEN GOTO AGAIN
         ISPEXEC CONTROL ERRORS CANCEL
         IF &RC GT 8 THEN DO
           ISPEXEC SETMSG MSG(PAJC000W)
           EXIT CODE(0)
           END
         IF &SCTEST EQ TEST THEN CONTROL LIST CONLIST SYMLIST MSG
         IF &SCID EQ THEN DO
           SET &SCSWU = YES
           SET &SCSWC = NO
           SET &SCID = &SYSUID
           END
         ELSE SET &SCSWU = NO
         SET &SCSWT = NO
         SET &SCSWM = NO
         ISPEXEC CONTROL ERRORS RETURN
         IF &SCSWU EQ YES THEN -
         ISPEXEC LMINIT DATAID(SCFIN) -
           DATASET('&SCID..&SCDSG..&SCDST') ENQ(EXCLU)
         ELSE ISPEXEC LMINIT DATAID(SCFIN) -
           DATASET('&SCID..&SCDSG..&SCDST')
         SET &RC = &LASTCC
         IF &RC NE 0 THEN DO
           IF &SCTEST EQ TEST THEN ISPEXEC SETMSG MSG(PAJC001A)
           ELSE ISPEXEC SETMSG MSG(PAJC002A)
           SET &SCSWM = YES
           GOTO ERIFI
           END
         ISPEXEC LMOPEN DATAID(&SCFIN)
         SET &RC = &LASTCC
         IF &RC NE 0 THEN DO
           IF &SCTEST EQ TEST THEN ISPEXEC SETMSG MSG(PAJC001B)
           ELSE ISPEXEC SETMSG MSG(PAJC002B)
           SET &SCSWM = YES
           GOTO EROFI
           END
         ISPEXEC LMMFIND DATAID(&SCFIN) MEMBER(&SCMEM)
         SET &RC = &LASTCC
         IF &SCSWU EQ YES THEN DO
           IF &RC EQ 8 THEN DO
             SET &SCSWC = YES
             GOTO ERFFI
             END
           ELSE IF &RC EQ 4 THEN DO
             ISPEXEC SETMSG MSG(PAJC002C)
             SET &SCSWM = YES
             GOTO ERFFI
             END
           END
         IF &RC NE 0 THEN DO
           IF &SCTEST EQ TEST THEN ISPEXEC SETMSG MSG(PAJC001D)
           ELSE ISPEXEC SETMSG MSG(PAJC002D)
           SET &SCSWM = YES
           GOTO ERFFI
           END
         ISPEXEC TBCREATE SCTABLE REPLACE NOWRITE NAMES(SCAROU)
         CONTROL ASIS
GETFI:   ISPEXEC LMGET DATAID(&SCFIN) MODE(INVAR) +
           DATALOC(SCARIN) DATALEN(SCALIN) MAXLEN(80)
         SET &RC = &LASTCC
         IF &RC EQ 8 THEN GOTO EOFFI
         IF &RC NE 0 THEN DO
           IF &SCTEST EQ TEST THEN ISPEXEC SETMSG MSG(PAJC001E)
           ELSE ISPEXEC SETMSG MSG(PAJC002E)
           SET &SCSWM = YES
           SET &SCSWT = NO
           GOTO EOFFI
           END
         SET &SCARLIN = &LENGTH(&STR(&SCARIN))
         IF &SCARLIN GE 11 THEN DO
           SET &SCTT = &SUBSTR(1:11,&SCARIN)
           IF &SCSWU EQ NO THEN DO
             IF &STR(&SCTT) EQ &STR(STOP       ) THEN GOTO EOFFI
             END
           IF &SCSWU EQ YES THEN DO
             IF &STR(&SCTT) EQ &STR(Aggiornato ) THEN GOTO GETFI
             IF &STR(&SCTT) EQ &STR(Ajourn! au ) THEN GOTO GETFI
             IF &STR(&SCTT) EQ &STR(Updated on ) THEN GOTO GETFI
             END
           END
         IF &SCARLIN GT 72 THEN SET &SCARLIN = 72
         SET &SCAROU = &STR(&SUBSTR(1:&SCARLIN,&SCARIN))
         ISPEXEC TBADD SCTABLE
         SET &SCSWT = YES
         GOTO GETFI
EOFFI:   CONTROL CAPS
ERFFI:   ISPEXEC LMCLOSE DATAID(&SCFIN)
         SET &RC = &LASTCC
         IF &RC NE 0 THEN DO
           IF &SCSWM EQ NO THEN DO
             IF &SCTEST EQ TEST THEN ISPEXEC SETMSG MSG(PAJC001F)
             ELSE ISPEXEC SETMSG MSG(PAJC002F)
             SET &SCSWM = YES
             END
           END
         IF &SCSWU EQ NO THEN GOTO EROFI
         IF &SCSWC EQ YES THEN GOTO UPDFI
         IF &SCSWT EQ YES THEN GOTO UPDFI
EROFI:   ISPEXEC LMFREE DATAID(&SCFIN)
         SET &RC = &LASTCC
         IF &RC NE 0 THEN DO
           IF &SCSWM EQ NO THEN DO
             IF &SCTEST EQ TEST THEN ISPEXEC SETMSG MSG(PAJC001G)
             ELSE ISPEXEC SETMSG MSG(PAJC002G)
             SET &SCSWM = YES
             END
           END
         IF &SCSWT EQ NO THEN GOTO ENDTB
         ISPEXEC TBTOP SCTABLE
DSTSC:   ISPEXEC TBDISPL SCTABLE PANEL(PAJSC1) POSITION(CRP)
         SET &RC = &LASTCC
         IF &RC LE 4 THEN GOTO DSTSC
         IF &RC NE 8 THEN DO
           IF &SCSWM EQ NO THEN DO
             IF &SCTEST EQ TEST THEN ISPEXEC SETMSG MSG(PAJC001H)
             END
           END
ENDTB:   ISPEXEC TBEND SCTABLE
ERIFI:   ISPEXEC CONTROL ERRORS CANCEL
         IF &SCSWM EQ NO THEN ISPEXEC SETMSG MSG(PAJC009A)
         EXIT CODE(0)
UPDFI:   CONTROL ASIS
         IF &SCSWC EQ YES THEN DO
           SET &SCTIT = &STR(Create)
           ISPEXEC TBCREATE SCTABLE REPLACE NOWRITE NAMES(SCAROU)
           SET &SCAROU = &STR( )
           ISPEXEC TBADD SCTABLE
           END
         ELSE SET &SCTIT = &STR(Update)
         ISPEXEC VPUT (SCTIT SCID)
         ISPEXEC VGET (SCLANG) PROFILE
         SET &RC = 0
         SET &ZTDTOP = 0
ULOOP:   IF &RC EQ 4 THEN ISPEXEC TBDISPL SCTABLE POSITION(CRP)
         ELSE DO
           ISPEXEC TBTOP SCTABLE
           ISPEXEC TBSKIP SCTABLE NUMBER(&ZTDTOP)
           ISPEXEC VGET (ZVERB ZSCROLLN)
           IF &ZVERB EQ UP THEN -
             ISPEXEC TBSKIP SCTABLE NUMBER(-&ZSCROLLN)
           ELSE IF &ZVERB EQ DOWN THEN -
             ISPEXEC TBSKIP SCTABLE NUMBER(&ZSCROLLN)
           ISPEXEC TBDISPL SCTABLE PANEL(PAJSC2) POSITION(CRP)
           END
         SET &RC = &LASTCC
         IF &RC GT 8 THEN DO
           ISPEXEC SETMSG MSG(PAJC009B)
           SET &SCSWM = YES
           GOTO XLOOP
           END
         ELSE IF &RC EQ 8 OR &CMD EQ SAVE THEN GOTO TSAVE
         ELSE IF &CMD EQ CANCEL THEN DO
           ISPEXEC SETMSG MSG(PAJC009C)
           SET &SCSWM = YES
           GOTO XLOOP
           END
         ELSE IF &ZTDSELS GE 1 THEN DO
           IF &SCA EQ A OR &SCA EQ B THEN DO
             SET &HIGH = &MM*9
             SET &LOW = &HIGH-8
             IF &SCLANG EQ I THEN DO
               SET &GN = &STR(Gennaio  Febbraio Marzo    Aprile   )
               SET &GN = &GN&STR(Maggio   Giugno   Luglio   Agosto   )
               SET &GN = &GN&STR(SettembreOttobre  Novembre Dicembre )
               END
             ELSE IF &SCLANG EQ F THEN DO
               SET &GN = &STR(Janvier  Fevrier  Mars     Avril    )
               SET &GN = &GN&STR(Mai      Juin     Juillet  Aout     )
               SET &GN = &GN&STR(SeptembreOctobre  Novembre D!cembre )
               END
             ELSE DO
               SET &GN = &STR(January  February March    April    )
               SET &GN = &GN&STR(May      June     July     August   )
               SET &GN = &GN&STR(SeptemberOctober  November December )
               END
             SET &MONTH = &SUBSTR(&LOW:&HIGH,&GN)
             SET &MONTH = &MONTH          /* TO LOSE TRAILING BLANKS */
             SET &ISEC = 1900
             IF &AA = 0 THEN SET &ISEC = 2000
             SET &AA1 = &AA+&ISEC
             SET &N0 = ((&AA1/4)*4)
             SET &N2 = &MM*2
             SET &N1 = &N2-1
             IF &AA1 EQ &N0 THEN -
               SET &GGT = &SUBSTR(&N1:&N2,312931303130313130313031)
             ELSE SET &GGT = &SUBSTR(&N1:&N2,312831303130313130313031)
             IF &GG GT &GGT THEN DO
               ISPEXEC SETMSG MSG(PAJC000Y)
               GOTO ULOOP
               END
             SET &MM1 = &SUBSTR(&MM,032503514624)
             SET &GGX = &GG+&MM1
             SET &AA2 = ((&AA1*100)+&MM-3)/100
             SET &N0 = &AA2/100
             SET &N01 = &AA2//100
             SET &N1 = &N0//4
             SET &N2 = &N01//4
             SET &N4 = &N01/4 +&N1
             SET &N7 = &GGX+&N2+&N4*5
             SET &N8 = &N7//7        /* DAY NUMBER, WHERE SUNDAY = 0 */
             SET &LOW = &N8*10+1
             SET &HIGH = &LOW+9
             IF &SCLANG EQ I THEN DO
               SET &GN = &STR(Domenica  Lunedi'   Martedi'  Mercoledi')
               SET &GN = &GN&STR(Giovedi'  Venerdi'  Sabato    )
               END
             ELSE IF &SCLANG EQ F THEN DO
               SET &GN = &STR(Dimanche  Lundi     Mardi     Mercredi  )
               SET &GN = &GN&STR(Jeudi     Vendredi  Samedi    )
               END
             ELSE DO
               SET &GN = &STR(Sunday    Monday    Tuesday   Wenesday  )
               SET &GN = &GN&STR(Thursday  Friday    Saterday  )
               END
             SET &SCD = &SUBSTR(&LOW:&HIGH,&GN)
             SET &SCD = &SCD              /* TO LOSE TRAILING BLANKS */
             IF &SUBSTR(1,&GG) ¬= 0 THEN DO
               IF &LENGTH(&GG) = 1 THEN SET &GG = &STR(0&GG)
               END
             IF &SUBSTR(1,&MM) ¬= 0 THEN DO
               IF &LENGTH(&MM) = 1 THEN SET &MM = &STR(0&MM)
               END
             IF &SUBSTR(1,&AA) ¬= 0 THEN DO
               IF &LENGTH(&AA) = 1 THEN SET &AA = &STR(0&AA)
               END
             SET &SCAROU = &STR(--- &SCD &GG &MONTH &AA1 :)
             IF &SCA EQ B THEN ISPEXEC TBSKIP SCTABLE NUMBER(-1)
             ISPEXEC TBADD SCTABLE
             END
           ELSE IF &SCA EQ D THEN DO
             DO WHILE &SCN GE 1
               ISPEXEC TBDELETE SCTABLE
               ISPEXEC TBSKIP SCTABLE
               IF &LASTCC NE 0 THEN SET &SCN = 0
               ELSE SET &SCN = &SCN - 1
               END
             END
           ELSE IF &SCA EQ I THEN DO
             DO WHILE &SCN GE 1
               SET &SCAROU = &STR( )
               ISPEXEC TBADD SCTABLE
               SET &SCN = &SCN - 1
               END
             END
           ELSE IF &SCA EQ R THEN DO
             DO WHILE &SCN GE 1
               ISPEXEC TBADD SCTABLE
               SET &SCN = &SCN - 1
               END
             END
           ELSE IF &SCA EQ S THEN DO
             SET &SCAROU = &STR(STOP       )
             ISPEXEC TBADD SCTABLE
             END
           ELSE IF &SCA EQ &STR( ) THEN ISPEXEC TBPUT SCTABLE
           END
         GOTO ULOOP
TSAVE:   SET &GG = &SUBSTR(4:5,&SYSDATE)
         SET &MM = &SUBSTR(1:2,&SYSDATE)
         SET &AA = &SUBSTR(7:8,&SYSDATE)
         SET &ISEC = 1900
         IF &AA = 0 THEN SET &ISEC = 2000
         SET &MM1 = &SUBSTR(&MM,032503514624)
         SET &GGX = &GG+&MM1
         SET &AA1 = &AA+&ISEC
         SET &AA2 = ((&AA1*100)+&MM-3)/100
         SET &N0 = &AA2/100
         SET &N01 = &AA2//100
         SET &N1 = &N0//4
         SET &N2 = &N01//4
         SET &N4 = &N01/4 +&N1
         SET &N7 = &GGX+&N2+&N4*5
         SET &N8 = &N7//7            /* DAY NUMBER, WHERE SUNDAY = 0 */
         SET &LOW = &N8*10+1
         SET &HIGH = &LOW+9
         IF &SCLANG EQ I THEN DO
           SET &GN = &STR(Domenica  Lunedi'   Martedi'  Mercoledi')
           SET &GN = &GN&STR(Giovedi'  Venerdi'  Sabato    )
           END
         ELSE IF &SCLANG EQ F THEN DO
           SET &GN = &STR(Dimanche  Lundi     Mardi     Mercredi  )
           SET &GN = &GN&STR(Jeudi     Vendredi  Samedi    )
           END
         ELSE DO
           SET &GN = &STR(Sunday    Monday    Tuesday   Wenesday  )
           SET &GN = &GN&STR(Thursday  Friday    Saterday  )
           END
         SET &SCD = &SUBSTR(&LOW:&HIGH,&GN)
         SET &SCD = &SCD                  /* TO LOSE TRAILING BLANKS */
         SET &HIGH = &MM*9
         SET &LOW = &HIGH-8
         IF &SCLANG EQ I THEN DO
           SET &GN = &STR(Gennaio  Febbraio Marzo    Aprile   )
           SET &GN = &GN&STR(Maggio   Giugno   Luglio   Agosto   )
           SET &GN = &GN&STR(SettembreOttobre  Novembre Dicembre )
           END
         ELSE IF &SCLANG EQ F THEN DO
           SET &GN = &STR(Janvier  Fevrier  Mars     Avril    )
           SET &GN = &GN&STR(Mai      Juin     Juillet  Aout     )
           SET &GN = &GN&STR(SeptembreOctobre  Novembre D!cembre )
           END
         ELSE DO
           SET &GN = &STR(January  February March    April    )
           SET &GN = &GN&STR(May      June     July     August   )
           SET &GN = &GN&STR(SeptemberOctober  November December )
           END
         SET &MONTH = &SUBSTR(&LOW:&HIGH,&GN)
         SET &MONTH = &MONTH              /* TO LOSE TRAILING BLANKS */
         IF &SUBSTR(1,&GG) ¬= 0 THEN DO
           IF &LENGTH(&GG) = 1 THEN SET &GG = &STR(0&GG)
           END
         IF &SUBSTR(1,&MM) ¬= 0 THEN DO
           IF &LENGTH(&MM) = 1 THEN SET &MM = &STR(0&MM)
           END
         IF &SUBSTR(1,&AA) ¬= 0 THEN DO
           IF &LENGTH(&AA) = 1 THEN SET &AA = &STR(0&AA)
           END
         SET &SCO = &SUBSTR(1:5,&SYSTIME)
         IF &SCLANG EQ I THEN DO
         SET &SCUPDT = &STR(Aggiornato &SCD &GG &MONTH &AA1 ore &SCO..)
           END
         ELSE IF &SCLANG EQ F THEN DO
         SET &SCUPDT = &STR(Ajourn! au &SCD &GG &MONTH &AA1 { &SCO..)
           END
         ELSE DO
         SET &SCUPDT = &STR(Updated on &SCD &GG &MONTH &AA1 at &SCO..)
           END
         SET &SCSWI = NO
         ISPEXEC LMOPEN DATAID(&SCFIN) OPTION(OUTPUT)
         SET &RC = &LASTCC
         IF &RC NE 0 THEN DO
           IF &SCTEST EQ TEST THEN DO
             WRITE *** OPEN ERROR - RC=&RC ***
             WRITE &ZERRLM
             END
           ELSE WRITE "&SCID" communication cannot be saved
           GOTO XLOOP
           END
         SET &SCSWW = NO
         ISPEXEC TBTOP SCTABLE
WLOOP:   ISPEXEC TBSKIP SCTABLE NUMBER(+1)
         SET &RC = &LASTCC
         IF &RC NE 0 THEN GOTO WLEND
         ISPEXEC TBGET SCTABLE
         SET &RC = &LASTCC
         IF &RC GT 4 THEN DO
           IF &SCTEST EQ TEST THEN ISPEXEC SETMSG MSG(PAJC003A)
           ELSE ISPEXEC SETMSG MSG(PAJC004A)
           SET &SCSWM = YES
           GOTO WRCLS
           END
         SET &SCSWW = YES
         IF &SCSWI EQ NO THEN DO
           SET &SCARLOU = &LENGTH(&STR(&SCAROU))
           IF &SCARLOU GE 11 THEN DO
             SET &SCTT = &SUBSTR(1:11,&SCAROU)
             IF &STR(&SCTT) EQ &STR(STOP       ) THEN DO
               SET &SCARIN = &STR(&SCUPDT)
               ISPEXEC LMPUT DATAID(&SCFIN) MODE(INVAR) +
                 DATALOC(SCARIN) DATALEN(80) NOBSCAN
               SET &RC = &LASTCC
               IF &RC NE 0 THEN DO
                 IF &SCSWM EQ NO THEN DO
                   IF &SCTEST EQ TEST THEN -
                     ISPEXEC SETMSG MSG(PAJC003B)
                   ELSE ISPEXEC SETMSG MSG(PAJC004B)
                   SET &SCSWM = YES
                   END
                 GOTO WRCLS
                 END
               SET &SCSWI = YES
               END
             END
           END
         SET &SCARIN = &STR(&SCAROU)
         ISPEXEC LMPUT DATAID(&SCFIN) MODE(INVAR) +
           DATALOC(SCARIN) DATALEN(80) NOBSCAN
         SET &RC = &LASTCC
         IF &RC EQ 0 THEN GOTO WLOOP
         IF &SCSWM EQ NO THEN DO
           IF &SCTEST EQ TEST THEN ISPEXEC SETMSG MSG(PAJC003B)
           ELSE ISPEXEC SETMSG MSG(PAJC004B)
           SET &SCSWM = YES
           END
         GOTO WRCLS
WLEND:   IF &RC GT 8 THEN DO
           IF &SCSWM EQ NO THEN DO
             IF &SCTEST EQ TEST THEN ISPEXEC SETMSG MSG(PAJC003C)
             ELSE ISPEXEC SETMSG MSG(PAJC004C)
             SET &SCSWM = YES
             END
           GOTO WRCLS
           END
         IF &SCSWW EQ NO THEN DO
           IF &SCSWC EQ NO THEN DO
             ISPEXEC LMMDEL DATAID(&SCFIN) MEMBER(&SCMEM)
             SET &RC = &LASTCC
             IF &RC EQ 0 THEN ISPEXEC SETMSG MSG(PAJC009D)
             ELSE IF &SCSWM EQ NO THEN DO
               IF &SCTEST EQ TEST THEN ISPEXEC SETMSG MSG(PAJC003D)
               ELSE ISPEXEC SETMSG MSG(PAJC004D)
               END
             END
           ELSE ISPEXEC SETMSG MSG(PAJC009E)
           SET &SCSWM = YES
           GOTO WRCLS
           END
         IF &SCSWI EQ NO THEN DO
           SET &SCARIN = &STR(&SCUPDT)
           ISPEXEC LMPUT DATAID(&SCFIN) MODE(INVAR) +
             DATALOC(SCARIN) DATALEN(80) NOBSCAN
           SET &RC = &LASTCC
           IF &RC NE 0 THEN DO
             IF &SCSWM EQ NO THEN DO
               IF &SCTEST EQ TEST THEN ISPEXEC SETMSG MSG(PAJC003B)
               ELSE ISPEXEC SETMSG MSG(PAJC004B)
               SET &SCSWM = YES
               END
             GOTO WRCLS
             END
           END
         IF &SCSWC EQ YES THEN -
           ISPEXEC LMMADD DATAID(&SCFIN) MEMBER(&SCMEM)
         ELSE ISPEXEC LMMREP DATAID(&SCFIN) MEMBER(&SCMEM)
         SET &RC = &LASTCC
         IF &RC EQ 0 THEN GOTO WRCLS
         ELSE IF &SCSWM EQ NO THEN DO
           IF &SCTEST EQ TEST THEN ISPEXEC SETMSG MSG(PAJC003E)
           ELSE ISPEXEC SETMSG MSG(PAJC004E)
           SET &SCSWM = YES
           END
WRCLS:   ISPEXEC LMCLOSE DATAID(&SCFIN)
         SET &RC = &LASTCC
         IF &RC NE 0 THEN DO
           IF &SCSWM EQ NO THEN DO
             IF &SCTEST EQ TEST THEN ISPEXEC SETMSG MSG(PAJC003F)
             SET &SCSWM = YES
             END
           END
XLOOP:   CONTROL CAPS
         ISPEXEC LMFREE DATAID(&SCFIN)
         SET &RC = &LASTCC
         IF &RC NE 0 THEN DO
           IF &SCSWM EQ NO THEN DO
             IF &SCTEST EQ TEST THEN ISPEXEC SETMSG MSG(PAJC003G)
             SET &SCSWM = YES
             END
           END
         ISPEXEC TBEND SCTABLE
         ISPEXEC CONTROL ERRORS CANCEL
         IF &SCSWM EQ NO THEN ISPEXEC SETMSG MSG(PAJC009F)
         EXIT CODE(0)
