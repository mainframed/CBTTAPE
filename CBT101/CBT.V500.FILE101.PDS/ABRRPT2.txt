PROC 0 DEBUG
CONTROL NOLIST NOMSG NOFLUSH PROMPT
IF &DEBUG NE &STR() THEN +
  CONTROL LIST MSG NOFLUSH PROMPT CONLIST SYMLIST
ISPEXEC CONTROL ERRORS RETURN
SET &OPT EQ &STR()
SET &ACLA EQ &STR(A)
SET &AVOL EQ &STR()
SET &AVOLG EQ &STR()
SET &DSPRFX EQ &STR()
RESTART: +
SET &LISTOPT EQ &STR()
SET &ACMD EQ &STR( PRINT CATLG)
SET &COMMA EQ &STR()
ISPEXEC DISPLAY PANEL(ABRRPT2)
IF &LASTCC EQ 8 THEN +
  EXIT
IF &SYSPREF EQ &STR() THEN +
  DO
    SET &DSPRFX EQ &SYSUID
  END
ELSE +
  IF &SYSPREF EQ &SYSUID THEN +
    DO
      SET &DSPRFX EQ &SYSUID
    END
  ELSE +
    DO
      SET &DSPRFX EQ &SYSPREF
    END
SET &DSYS EQ &STR(D&SUBSTR(1:2,&SYSDATE)&SUBSTR(4:5,&SYSDATE)&SUBSTR(7:+
8,&SYSDATE))
SET &TSYS EQ &STR(T&SUBSTR(1:2,&SYSTIME)&SUBSTR(4:5,&SYSTIME)&SUBSTR(7:+
8,&SYSTIME))
IF &DEBUG EQ &STR() THEN +
  CONTROL NOMSG
FREE F(SYSPRINT,SYSIN,SYSOUT,ARCHIVE)
FREE A(FB80,FBA121)
IF &DEBUG EQ &STR() THEN +
  CONTROL MSG
ATTR FB80 LRECL(80) BLKSIZE(4000) RECFM(F B)
ATTR FBA121 LRECL(121) BLKSIZE(18997) RECFM(F B A)
ALLOC F(SYSIN) UNIT(VIO) SPACE(1 1) TRACKS NEW DELETE USING(FB80)
ALLOC F(ARCHIVE) DA('FDRABR.ARCHIVE') SHR
ALLOC F(SYSOUT) DUMMY
ALLOC F(SYSPRINT) DA('&DSPRFX..&DSYS..&TSYS..SYSPRINT') NEW CATALOG +
 SPACE(5 2) TRACKS USING(FBA121)
IF &AVOL NE &STR() THEN +
  DO
    SET &ACMD EQ &STR(&ACMD,&STR(VOL=(&AVOL)))
  END
IF &AVOLG NE &STR() THEN +
  DO
    SET &ACMD EQ &STR(&ACMD,&STR(VOLG=(&AVOLG)))
  END
OPENFILE SYSIN OUTPUT
SET &SYSIN EQ &STR(&ACMD)
PUTFILE SYSIN
CLOSFILE SYSIN
$ FDRABRP
ISPEXEC BROWSE DATASET('&DSPRFX..&DSYS..&TSYS..SYSPRINT')
IF &LISTOPT EQ &STR() THEN +
  DO
    GOTO CONTIN
  END
IF &ACLA EQ &STR(X) THEN +
  DO
    SET &HOLD EQ &STR(HOLD KEEP)
  END
ELSE +
  DO
    SET &HOLD EQ &STR()
  END
IF &DEBUG EQ &STR() THEN +
  CONTROL NOMSG
FREE F(SYSIN,SYSPRINT,SYSUT1,SYSUT2)
ALLOC F(SYSUT1) DA('&DSPRFX..&DSYS..&TSYS..SYSPRINT') SHR
ALLOC F(SYSUT2) SYS(&ACLA) &HOLD
ALLOC F(SYSIN) DUMMY
ALLOC F(SYSPRINT) DUMMY
$ IEBGENER
FREE F(SYSIN,SYSUT1,SYSUT2,SYSPRINT)
ALLOC F(SYSIN) DA(*)
ALLOC F(SYSPRINT) DA(*)
ALLOC F(SYSUT1) UNIT(VIO) SPACE(10 2) CYLINDERS
ALLOC F(SYSUT2) UNIT(VIO) SPACE(10 2) CYLINDERS
CONTIN: +
IF &DEBUG EQ &STR() THEN +
  CONTROL NOMSG
DELETE '&DSPRFX..&DSYS..&TSYS..SYSPRINT'
FREE F(SYSIN,SYSPRINT,ARCHIVE,SYSOUT)
FREE A(FB80,FBA121)
IF &DEBUG EQ &STR() THEN +
  CONTROL MSG
GOTO RESTART
END
