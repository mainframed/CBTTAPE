PROC 0 DEBUG
CONTROL NOLIST NOMSG NOFLUSH PROMPT
IF &DEBUG NE &STR() THEN +
  CONTROL LIST MSG NOFLUSH PROMPT CONLIST SYMLIST
ISPEXEC CONTROL ERRORS RETURN
SET &OPT EQ &STR( )
SET &ADSN EQ &STR()
SET &ADSG EQ &STR()
SET &AVOL EQ &STR()
SET &AVOLG EQ &STR()
SET &DSPRFX EQ &STR()
RESTART: +
SET &ACMD EQ &STR( RESET BACKUP)
ISPEXEC DISPLAY PANEL(ABRBKUP3)
IF &LASTCC EQ 8 THEN +
  EXIT
IF &SYSPREF EQ &STR() THEN +
  DO
    SET &DSPRFX EQ &SYSUID
  END
ELSE +
  IF &SYSPREF EQ &SYSUID THEN +
    DO
      SET &DSPRFX EQ &SYSUID
    END
  ELSE +
    DO
      SET &DSPRFX EQ &SYSPREF
    END
SET &DSYS EQ &STR(D&SUBSTR(1:2,&SYSDATE)&SUBSTR(4:5,&SYSDATE)&SUBSTR(7:+
8,&SYSDATE))
SET &TSYS EQ &STR(T&SUBSTR(1:2,&SYSTIME)&SUBSTR(4:5,&SYSTIME)&SUBSTR(7:+
8,&SYSTIME))
IF &DEBUG EQ &STR() THEN +
  CONTROL NOMSG
FREE F(SYSPRINT,SYSIN,ABRARCH,ABRREST)
FREE A(FB80,FBA121)
IF &DEBUG EQ &STR() THEN +
  CONTROL MSG
ATTR FB80 LRECL(80) BLKSIZE(4000) RECFM(F B)
ATTR FBA121 LRECL(121) BLKSIZE(18997) RECFM(F B A)
ALLOC F(SYSIN) UNIT(VIO) SPACE(1 1) TRACKS NEW DELETE USING(FB80)
ALLOC F(ABRARCH) DA('FDRABR.ABRARCH.USERDATA') SHR
ALLOC F(ABRREST) DA('FDRABR.ABRREST.USERDATA') SHR
ALLOC F(SYSPRINT) DA('&DSPRFX..&DSYS..&TSYS..SYSPRINT') NEW CATALOG +
 SPACE(5 2) TRACKS USING(FBA121)
IF &ADSN NE &STR() THEN +
  DO
    SET &ACMD EQ &STR(&ACMD,&STR(DSN=&ADSN))
  END
ELSE +
  DO
    SET &ACMD EQ &STR(&ACMD,&STR(DSG=&ADSG))
  END
IF &AVOL NE &STR() THEN +
  DO
    SET &ACMD EQ &STR(&ACMD,&STR(VOL=&AVOL))
  END
IF &AVOLG NE &STR() THEN +
  DO
    SET &ACMD EQ &STR(&ACMD,&STR(VOLG=&AVOLG))
  END
OPENFILE SYSIN OUTPUT
SET &SYSIN EQ &STR(&ACMD)
PUTFILE SYSIN
CLOSFILE SYSIN
$ FDRABRUT
FREE F(SYSPRINT)
ALLOC F(SYSPRINT) DA('&DSPRFX..&DSYS..&TSYS..SYSPRINT') MOD
OPENFILE SYSPRINT OUTPUT
SET &SYSPRINT EQ &STR(0 BACKUP OF THE ABOVE DATASET WILL NOT BE FORCED +
DURING THE REGULAR EVENING BACKUP RUN)
PUTFILE SYSPRINT
CLOSFILE SYSPRINT
ISPEXEC BROWSE DATASET('&DSPRFX..&DSYS..&TSYS..SYSPRINT')
IF &DEBUG EQ &STR() THEN +
  CONTROL NOMSG
DELETE '&DSPRFX..&DSYS..&TSYS..SYSPRINT'
FREE F(SYSIN,SYSPRINT,ABRARCH,ABRREST)
FREE A(FB80,FBA121)
IF &DEBUG EQ &STR() THEN +
  CONTROL MSG
GOTO RESTART
END
