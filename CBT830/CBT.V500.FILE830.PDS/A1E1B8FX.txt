Scanning the PPT

The Program Properties Table (PPT) contains a list of programs
that require special properties such as:

1      Being non-swappable

2      Having a program protect key other than 8

3      Running in privileged state

4      Dataset integrity

5      Bypass security protection

6      CPU affinity.

MVS/ESA is packaged with a default PPT that contains IBM
programs that require these special properties. Examples of these
programs are:

1      IEEMB860 - Master

2      HASJES20 - JES2

3      CSVLLCRE - LLA

4      IFASMF - SMF.

The properties are assigned at job-step attach time by the initiator
module IEFSD162. An installation can supplement the IBM
default list at IPL time by using the PARMLIB member
SCHEDxx or dynamically using the operator SET SCH=(xx,L)
command. This article contains an ISPF/PDF windowed dialog
that will display all the entries in the PPT and, for each program,
will display the assigned properties. To obtain the PPT entries, the
IEFPPSCN macro is used. This prevents the system from
dynamically updating the PPT while it is being scanned.

This dialog is made up of the following code elements:

o      PPTPAN01 - panel.

o      PPTPAN02 - panel.

o      PPTSCAN - Assembler routine which obtains the PPT
      information by executing macro IEFPPSCN. It communicates
      the PPT details back to the REXX program PPTSCANR by
      calling IKJCT441 to create REXX stem variables. PPTSCAN
      will set the following return codes:

      0 - successful invocation

      4 - stem build error

      8 - PPT scan error.

o      PPTSCANR - REXX EXEC which calls PPTSCAN to
      obtain the PPT information for display.

The Assembler routine PPTSCAN must be linked into an
authorized library with the attribute AC(1); this is because of the
requirements of the IEFPPSCN macro. The TSO PARMLIB
member IKJTSO00 must be updated to include command
PPTSCAN in the AUTHCMD section of this member. To invoke
this dialog execute the REXX EXEC PPTSCANR from an ISPF
menu panel or from option 6 of TSO. The REXX EXEC
PPTSCANR includes a LIBDEF command for panel invocation.
This can be removed or updated as required.

         TITLE 'SCAN THE PPT TABLE'
ZERO     EQU   X'00'                   ZERO
SPACE    EQU   C' '                    SPACE
SIGNF    EQU   X'F0'                   POSITIVE SIGN
PPTSCAN  CSECT
PPTSCAN  AMODE 31
PPTSCAN  RMODE 24
         BAKR  R14,0                   SAVE CALLER'S ARS + GPRS
*                                      IN THE LINKAGE STACK
         USING PPTSCAN,R12             SET UP PROGRAM BASE REGISTER
         LAE   R12,0(R15,0)            SET UP PROGRAM BASE REGISTER
GETWORK  EQU   *
         L     R9,=AL4(WORKALEN)       WORK AREA LENGTH
         STORAGE OBTAIN,LENGTH=(R9),ADDR=(R10),SP=0,KEY=8,             X
               LOC=BELOW,COND=NO,RELATED=(FREEWORK,'FREE WORK AREA')
         LR    R13,R1                  @ THE WORKAREA
         USING SAVEAREA,R13            INFORM THE ASSEMBLER
         LA    R0,SAVEAREA             @ THE WORKAREA
         LA    R1,WORKALEN(0,0)        LENGTH
         LA    R14,=X'00'              ZERO FILL
         SR    R15,R15                 PROPAGATE
         MVCL  R0,R14                  CLEAR THE AREA
         MVC   PREVSA,=C'F1SA'         PUT ACRONYM INTO SAVEAREA
*                                      TO INDICATE STATUS SAVED ON
*                                      THE LINKAGE STACK.
CONTROL  EQU   *                       MAIN PROGRAM CONTROL
         BAS   R2,SCANPPT              SCAN THE PPT
         B     RETURN                  RETURN TO CALLER
RETURN   EQU   *
         LAE   R1,0(R13,0)             ADDRESS TO FREE
         L     R9,=AL4(WORKALEN)       WORK AREA LENGTH
         STORAGE RELEASE,ADDR=(R1),LENGTH=(R9),SP=0,KEY=8,             X
               COND=NO,RELATED=(GETWORK,'OBTAIN WORK AREA')
         L     R15,RETCODE             SET RC
         PR                            RESTORE CALLER'S AR'S
*                                      GPRS 2-14 AND RETURN
*                                      TO CALLER
SCANPPT  EQU  *                        SCAN THE PPT TABLE
         STCM  R2,B'1111',RET@         STORE RETURN @
         MVC   PPTMACRO,PPTABLE        MOVE THE IEFPPSCN MACRO AREA
         XC    PPTTOKEN,PPTTOKEN       START AT TOP OF PPT
PPTRUN   EQU  *
         MODESET MODE=SUP,KEY=ZERO     SUPERVISOR KEY 0 STATE
         LA    R9,PPTINFO              @ PPT RETURN AREA
         LA    R10,PPTMACRO            @ PPT MACRO AREA
         IEFPPSCN REQUEST=NEXT,TOKEN=PPTTOKEN,PPTINFO=(R9),            X
               MF=(E,(R10),COMPLETE)
         LTR   R15,R15                 END OF SCAN?
         BNZ   PPTEND                  END THE PPT SCAN
         MODESET MODE=PROB,KEY=NZERO   PROBLEM KEY 8 STATE
         LA    R8,PPTENT               @ OF PPT INFO
         USING PPT1,R8                 INFORM THE ASSEMBLER
         LA    R9,FMTPPT               @ FORMATTED PPT ENTRY
         USING GENPPT,R9               INFORM THE ASSEMBLER
         MVC   GPPTNAME,PPTNAME        PROGRAM NAME
         MVC   GPPTNCNC,=CL3'NO '      DEFAULT
         TM    PPTBYTE1,PPTNCNCL       NON CANCEL?
         BNO   SPECPKEY                NO-
         MVC   GPPTNCNC,=CL3'YES'      SET
SPECPKEY EQU   *
         MVC   GPPTSKEY,=CL3'NO '      DEFAULT
         TM    PPTBYTE1,PPTSKEY        SPECIAL STORAGE KEY?
         BNO   NONSWAP                 NO-
         MVC   GPPTSKEY,=CL3'YES'      SET
NONSWAP  EQU   *
         MVC   GPPTNSWP,=CL3'NO '      DEFAULT
         TM    PPTBYTE1,PPTNSWP        NON-SWAPPABLE?
         BNO   PRIV                    NO-
         MVC   GPPTNSWP,=CL3'YES'      SET
PRIV     EQU   *
         MVC   GPPTPRIV,=CL3'NO '      DEFAULT
         TM    PPTBYTE1,PPTPRIV        PRIVILEGED?
         BNO   SYSTASK                 NO-
         MVC   GPPTPRIV,=CL3'YES'      SET
SYSTASK  EQU   *
         MVC   GPPTSYST,=CL3'NO '      DEFAULT
         TM    PPTBYTE1,PPTSYSTK       SYSTEM TASK?
         BNO   NDSI                    NO-
         MVC   GPPTSYST,=CL3'YES'      SET
NDSI     EQU   *
         MVC   GPPTNDSI,=CL3'NO '      DEFAULT
         TM    PPTBYTE1,PPTNDSI        DATASET INTEGRITY?
         BO    NOPASS                  NO-
         MVC   GPPTNDSI,=CL3'YES'      SET
NOPASS   EQU   *
         MVC   GPPTNOPS,=CL3'NO '      DEFAULT
         TM    PPTBYTE1,PPTNOPAS       BYPASS PASSWORD PROTECTION?
         BNO   PROTKY                  NO-
         MVC   GPPTNOPS,=CL3'YES'      SET
PROTKY   EQU   *
         SR    R7,R7                   ZEROIZE
         IC    R7,PPTKEY               PICK UP THE PROTECT KEY
         SRL   R7,4(0)                 MOVE INTO LOW BYTE
         CVD   R7,DW                   CONVERT TO DECIMAL
         UNPK  UNPKKEY,PROTKEY         MAKE IT READABLE
         OI    UNPKKEY+L'UNPKKEY-1,X'F0'  FORCE SIGN TO F
         MVC   GPPTKEYS,UNPKKEY+1      MOVE TO REXX AREA
CPUAFF   EQU   *
         LA    R5,PPTCPUA              @ CPU AFFINITY
         LA    R6,L'PPTCPUA            LENGTH TO CONVERT
         LA    R7,GPPTCPUA             @ OUTPUT AREA
CPUAFFCN EQU   *
         SR    R4,R4                   ZEROIZE
         IC    R4,0(R5)                CHARACTER TO CONVERT
         SRL   R4,4(0)                 REMOVE LOW NIBBLE
         STC   R4,CHAR                 STORE
         BAL   R2,HEXCONV              DO THE HEX CONVERSION
         MVC   0(1,R7),CHAR            MOVE TO OUTPUT AREA
         LA    R7,1(,R7)               NEXT POSITION IN OUTPUT AREA
         MVC   CHAR,0(R5)              NEXT NIBBLE
         NI    CHAR,X'0F'              REMOVE LOW NIBBLE
         BAL   R2,HEXCONV              DO THE HEX CONVERSION
         MVC   0(1,R7),CHAR            MOVE TO OUTPUT AREA
         LA    R7,1(,R7)               NEXT POSITION IN OUTPUT AREA
         LA    R5,1(,R5)               NEXT BYTE TO CONVERT
         BCT   R6,CPUAFFCN             DO WHILE R6 > 0?
P2LPU    EQU   *
         MVC   GPPT2LPU,=CL3'NO '      DEFAULT
         TM    PPTPUBYT,PPT2LPU        2ND LEVEL PREFERED STORAGE
         BNO   P1LPU                   NO-
         MVC   GPPT2LPU,=CL3'YES'      SET
P1LPU    EQU   *
         MVC   GPPT1LPU,=CL3'NO '      DEFAULT
         TM    PPTPUBYT,PPT1LPU        1ST LEVEL PREFERED STORAGE   PG1
         BNO   PORIG                   NO-
         MVC   GPPT2LPU,=CL3'YES'      SET                          PG1
PORIG    EQU  *
         MVC   GPPTORIG,=CL3'YES'      DEFAULT
         TM    PPTORIG,PPTDEFLT        ORIGIN?
         BO    BLDSTEM                 BUILD THE STEM VARIABLE
         MVC   GPPTORIG,=CL3'NO '      SET
BLDSTEM  EQU   *
         XR    R7,R7                   ZEROIZE
         ICM   R7,B'0011',NEXTSTEM     CURRENT STEM VALUE
         LA    R7,1(,R7)               UPDATE STEM
         STCM  R7,B'0011',NEXTSTEM     STORE BACK
         BAL   R2,STEMBLD              BUILD REXX STEM VARIABLE
         LTR   R15,R15                 STEM BUILD OKAY?
         BZ    PPTRUN                  YES-
         ST    R15,RETCODE2            STORE THE RETURN CODE
         MVC   RETCODE,=AL4(4)         SCAN ERROR
         B     RSETMODE                RETURN TO CALLER
PPTEND   EQU   *
         C     R15,=F'4'               TRUE END OF SCAN?
         BE    STEM0                   YES-
         ST    R15,RETCODE2            STORE THE RETURN CODE
         MVC   RETCODE,=AL4(8)         SCAN ERROR
         B     RSETMODE                RETURN TO CALLER
STEM0    EQU   *
         MVC   STEMCNT,NEXTSTEM        NO OF STEMS CREATED
         XC    NEXTSTEM,NEXTSTEM       CLEAR
         BAL   R2,STEMBLD              BUILD REXX STEM VARIABLE
         LTR   R15,R15                 STEM BUILD OKAY?
         BZ    ALLDONE                 YES-
         ST    R15,RETCODE2            STORE THE RETURN CODE
         MVC   RETCODE,=AL4(4)         SCAN ERROR
         B     RSETMODE                RETURN TO CALLER
ALLDONE  EQU   *
         SR    R15,R15                 RC=0
         ST    R15,RETCODE2            STORE THE RETURN CODE
         XC    RETCODE,RETCODE         SET TO ZERO
RSETMODE EQU   *
         MODESET MODE=PROB,KEY=NZERO   PROBLEM KEY 8 STATE
         ICM    R2,B'1111',RET@        RESET RETURN @
         BR     R2                     RETURN TO CALLER
HEXCONV  EQU   *                       PRINT HEX ROUTINE
         CLI   CHAR,X'09'              > 9?
         BH    HEX10                   YES-
         OI    CHAR,X'F0'              RANGE F0-F9
         BR    R2                      RETURN TO CALLER
HEX10    EQU   *
         XR    R4,R4                   ZEROIZE
         IC    R4,CHAR                 INSERT CHAR INTO REG
         SH    R4,=H'9'                GET CHAR NO
         STC   R4,CHAR                 STORE
         OI    CHAR,X'C0'              RANGE C0-C6
         BR    R2                      RETURN TO CALLER
STEMBLD  EQU   *                       BUILD STEM ROUTINE
         SR    R7,R7                   ZEROIZE
         ICM   R7,B'0011',NEXTSTEM     NEXT STEM(EX .XXX)
         CVD   R7,DW                   CONVERT TO DECIMAL
         MVC   STEM,=XL4'40202120'     EDIT PATTERN
         LA    R1,STEM+L'STEM-1        @ LAST CHARACTER
         EDMK  STEM,STEMNO             CONVERT TO CHAR
         LA    R14,STEM+L'STEM-1       CALCULATE THE LENGTH
         SR    R14,R1                  OF THE NUMBER
         XC    STEMNAME,STEMNAME       CLEAR
         MVC   STEMNAME(L'STEMLIT),STEMLIT  BUILD PPTENT.
         EX    R14,MOVESTEM            BUILD PPTENT.XXX
         LA    R7,STEMNAME             @ OF STEM NAME
         STCM  R7,B'1111',STEMPTR      STEM @
         LA    R14,L'STEMLIT+1(,R14)   LENGTH OF STEM NAME
         STCM  R14,B'1111',STEMLEN     STEM LENGTH
         CLC   NEXTSTEM,=XL2'0000'     CREATING STEM 0?
         BNE   NOTSTEM0                NO-
         SR    R7,R7                   ZEROIZE
         ICM   R7,B'0011',STEMCNT      NEXT STEM(EX .XXX)
         CVD   R7,DW                   CONVERT TO DECIMAL
         MVC   STEM,=XL4'40202120'     EDIT PATTERN
         LA    R1,STEM+L'STEM-1        @ LAST CHARACTER
         EDMK  STEM,STEMNO             CONVERT TO CHAR
         LA    R14,STEM+L'STEM-1       CALCULATE THE LENGTH
         SR    R14,R1                  OF THE NUMBER
         STCM  R1,B'1111',STEMVAL@     @ OF STEM VALUE
         LA    R14,1(,R14)             LENGTH OF STEM NAME
         STCM  R14,B'1111',STEMVALL    STEM VALUE LENGTH
         B     SETTOKEN                SET THE TOKEN
NOTSTEM0 EQU   *
         STCM  R9,B'1111',STEMVAL@     ADDRESS OF STEM VALUE
         MVC   STEMVALL,=AL4(GPPTLEN)  STEM VALUE LENGTH
SETTOKEN EQU   *
         XC    IKJTOKEN,IKJTOKEN       NO REQUIRED FOR THIS CALL
         XC    RCODE441,RCODE441       RETURN CODE
         L     R15,CVTPTR              CVT @
         USING CVT,R15                 INFORM THE ASSEMBLER
         L     R15,CVTTVT              TSCT @
         USING TSVT,R15                INFORM THE ASSEMBLER
         L     R15,TSVTVACC            IKJCT441 @
         LTR   R15,R15                 ENTRY POINT FOUND?
         BNZ   CALL441                 YES- DO A CALL
LINK441  EQU   *
         MVC   LINKAREA,LINKL          LINK SL=L
         MVC   CALLAREA,CALLL          PROG PROGRAM LIST
         LINK  EP=IKJCT441,                                            X
               PARAM=(ECODE,           ENTRY CODE                      X
               STEMPTR,                POINTER TO STEM NAME            X
               STEMLEN,                LENGTH  OF STEM NAME            X
               STEMVAL@,               POINTER TO STEM VALUE           X
               STEMVALL,               LENGTH  OF STEM VALUE           X
               IKJTOKEN,               TOKEN                           X
               ECTPARM,                NOT REQUIRED                    X
               RCODE441),              RETURN CODE                     X
               VL=1,                   EOL                             X
               MF=(E,CALLAREA),        CALL AREA                       X
               SF=(E,LINKAREA)         LINK AREA
         B     CHKRET                  CHECK THE RETURN CODE
CALL441  EQU   *
         MVC   CALLAREA,CALLL          PROG PROGRAM LIST
         CALL  (15),                                                   X
               (ECODE,                 ENTRY CODE                      X
               STEMPTR,                POINTER TO STEM NAME            X
               STEMLEN,                LENGTH  OF STEM NAME            X
               STEMVAL@,               POINTER TO STEM VALUE           X
               STEMVALL,               LENGTH  OF STEM VALUE           X
               IKJTOKEN,               TOKEN                           X
               ECTPARM,                NOT REQUIRED                    X
               RCODE441),              RETURN CODE                     X
               VL,                     EOL                             X
               MF=(E,CALLAREA)
         B     CHKRET                  CHECK THE RETURN CODE
CHKRET   EQU   *
         BR    R2                      RETURN TO CALLER
MOVESTEM MVC   STEMNAME+L'STEMLIT(0),0(R1)
         LTORG
STEMLIT  DC    CL7'PPTENT.'            STEM
ECTPARM  DC    X'FFFFFFFF'             ECT
ECODE    DC    AL4(TSVEUPDT)           CREATE CODE

PPTSCAN ASSEMBLER ROUTINE

         TITLE 'LINK LIST AREA'
LINKL    LINK SF=L
LINKLEN EQU  *-LINKL                   LENGTH
CALLL    CALL ,(,,,,,,,),MF=L
CALLLEN EQU  *-CALLL                   LENGTH
         TITLE 'IEFPPSCN LIST AREA'
         IEFPPSCN MF=(L,PPTABLE,0D)
         TITLE 'WORKAREA DSECT'
WORKAREA DSECT
SAVEAREA DS    CL72                    SAVEAREA
PREVSA   EQU   SAVEAREA+4,4            @ OF PREVIOUS SAVEAREA
RET@     DS    F                       RETURN @
RETCODE  DS    F                       RETURN CODE AREA
RETCODE2 DS    F                       USER RETURN CODE
DW       DS    D                       WORK AREA
UNPKKEY  EQU   DW,3                    UNPACKED PROTECT KEY
UNPKSTEM EQU   DW,3                    UNPACKED STEM
PROTKEY  EQU   DW+6,2                  PROTECT KEY
STEMNO   EQU   DW+6,2                  STEM NO
CHAR     DS    C                       A LONE CHARACTER
NEXTSTEM DS    XL2                     NEXT STEM VARIABLE NO
STEMCNT  DS    XL2                     MAX STEM VARIABLES
STEM     DS    CL4                     CONSTRUCTED STEM NO
STEMNAME DS    CL10                    CONSTRUCTED STEM NAME
PPTTOKEN DS    XL4                     IEFPPSCN TOKEN
PPTMACRO DS    CL(PPTABLEL)            IEFPPSCN MACRO AREA
PPTINFO  DS    0X                      PPT RETURN AREA
PPTELEN  DS    AL2                     MAX PPT ENTRY LENGTH
PPTENT   DS    CL(L'PPTENTRY)          PPT ENTRY LENGTH
FMTPPT   DS    CL(GPPTLEN)             FORMATTED PPT AREA
PARMLIST DS    0F
STEMPTR  DS    F                       STEM PTR
STEMLEN  DS    F                       STEM LEN
STEMVAL@ DS    F                       STEM VALUE @
STEMVALL DS    F                       STEM VAL LENGTH
IKJTOKEN DS    F                       TOKEN
RCODE441 DS    F                       RETURN CODE
         DS    0F
LINKAREA DS    CL(LINKLEN)             LINK AREA
CALLAREA DS    CL(CALLLEN)             PARM LIST AREA
WORKALEN EQU   *-WORKAREA              WORK AREA LENGTH
GENPPT   DSECT                         GENERATED PPT TABLE ENTRY
GPPTNAME DS    CL8                     PROGRAM NAME
GPPTNCNC DS    CL3                     NON/CANCEL
GPPTSKEY DS    CL3                     SPECIAL PROTECT KEY
GPPTNSWP DS    CL3                     NON/SWAPPABLE
GPPTPRIV DS    CL3                     PRIVILEGED
GPPTSYST DS    CL3                     SYSTEM TASK
GPPTNDSI DS    CL3                     DSI
GPPTNOPS DS    CL3                     BYPASS PASSWORD PROTECTION
GPPTKEYS DS    CL2                     PROTECT KEY
GPPTCPUA DS    CL4                     CPU AFFINITY
GPPT2LPU DS    CL3                     2ND LEVEL STORAGE
GPPT1LPU DS    CL3                     1ST LEVEL STORAGE
GPPTORIG DS    CL3                     ENTRY ORIGIN
GPPTLEN  EQU   *-GENPPT                ENTRY LENGTH
         YREGS
         CVT   DSECT=YES,LIST=NO       CVT DSECT
         IEFJESCT                      JES2 CT
         IEFZB610                      PPT
         IKJTSVT                       TSVT
         END   PPTSCAN

PPTSCANR REXX EXEC

/**rexx**/
/*  Program-id           PPTSCANR                                  */
/*  Remarks              Display the PPT                           */
/*trace i*/
address "ISPEXEC" "LIBDEF ISPPLIB DATASET ID('XXXXXX.PANELS')"
ppttable:
ADDRESS "ISPEXEC" "CONTROL DISPLAY LOCK" /* LOCK THE TERMINAL      */
if (rc > 8) then do                      /* error?                 */
   say 'Lock error    rc = 'rc''         /* yes- output message    */
   ADDRESS "ISPEXEC" "LIBDEF ISPPLIB"    /* remove allocation      */
   exit(rc)                              /* and quit               */
end
ADDRESS "ISPEXEC" "ADDPOP ROW(6) COLUMN(10)" /* pop up position    */
ADDRESS "ISPEXEC" "DISPLAY PANEL(PPTPAN02)"
if (rc > 8) then do                      /* error?                 */
   say 'Display error    rc = 'rc''      /* yes- output message    */
   ADDRESS "ISPEXEC" "LIBDEF ISPPLIB"    /* remove allocation      */
   exit(rc)                              /* and quit               */
end
PPTSCAN                                  /* scan the ppt           */
if (rc ¬= 0) then do                     /* error?                 */
   say 'PPTSCAN Error    rc = 'rc''      /* yes- output message    */
   ADDRESS "ISPEXEC" "LIBDEF ISPPLIB"    /* remove allocation      */
   exit(rc)                              /* and quit               */
end
call ppttable_build                      /* build the ppt table    */
ADDRESS "ISPEXEC" "REMPOP"               /* remove popup           */
ADDRESS "ISPEXEC" "ADDPOP ROW(3) COLUMN(1)"  /* pop up position    */
ADDRESS "ISPEXEC" "TBDISPL PPTTAB PANEL(PPTPAN01)"
if (rc > 8) then do                      /* error?                 */
   say 'tbdispl error    rc = 'rc''      /* yes- output message    */
   ADDRESS "ISPEXEC" "LIBDEF ISPPLIB"    /* remove allocation      */
   exit(rc)                              /* and quit               */
end
ADDRESS "ISPEXEC" "REMPOP"               /* remove menu popup      */
ADDRESS "ISPEXEC" "LIBDEF ISPPLIB"       /* remove allocation      */
exit(0)                                  /* quit                   */
ppttable_build:
ADDRESS "ISPEXEC" "TBCREATE PPTTAB NOWRITE REPLACE"
if (rc >  4) then do                     /* call okay?             */
   say 'tbcreate error    rc = 'rc''     /* no- inform the user    */
   ADDRESS "ISPEXEC" "LIBDEF ISPPLIB"    /* remove allocation      */
   exit(rc)                              /* lets quit              */
end
ztdmark=    '-------------------------------------------------------------------
--------'
ADDRESS  "ISPEXEC" "VPUT    (PPTENTRY,ZTDMARK) SHARED"
tabrows=  100                            /* max table rows         */
pptdibm=  0                              /* init                   */
pptndibm= 0                              /* init                   */
do i= 1 to pptent.0                      /* add the ppt ents to an */
                                         /* ispf table for display */
   pptentry= substr(pptent.i,1,8)        /* program name           */
                                         /* non-cancel             */
   pptentry= pptentry||'  '||substr(pptent.i,9,3)
                                         /* special protect key    */
   pptentry= pptentry||'   '||substr(pptent.i,12,3)
                                         /* non-swappable          */
   pptentry= pptentry||'     '||substr(pptent.i,15,3)
                                         /* privileged             */
   pptentry= pptentry||'   '||substr(pptent.i,18,3)
                                         /* system task            */
   pptentry= pptentry||'  '||substr(pptent.i,21,3)
                                         /* dsi                    */
   pptentry= pptentry||'   '||substr(pptent.i,24,3)
                                         /* bypass password prot   */
   pptentry= pptentry||'  '||substr(pptent.i,27,3)
                                         /* protection key         */
   pptentry= pptentry||'    '||substr(pptent.i,30,2)
                                         /* cpu affinity           */
   pptentry= pptentry||'  '||substr(pptent.i,32,4)
                                         /* 2LPU                   */
   pptentry= pptentry||'  '||substr(pptent.i,36,3)
                                         /* 1LPU                   */
   pptentry= pptentry||'  '||substr(pptent.i,39,3)
                                         /* default entry          */
   pptentry= pptentry||' '||substr(pptent.i,42,3)
   if substr(pptent.i,42,3) = 'YES' then /* IBM-supplied default?  */
      pptdibm= pptdibm + 1               /* yes                    */
   else
      pptndibm= pptndibm + 1             /* no                     */
   ADDRESS "ISPEXEC" "TBADD PPTTAB       /* add the entries        */
            SAVE(PPTENTRY)
            MULT("TABROWS")"
   if (rc ¬= 0) then do                  /* call okay?             */
      say 'tbadd error    rc = 'rc''     /* no- inform the user    */
      ADDRESS "ISPEXEC" "LIBDEF ISPPLIB" /* remove allocation      */
      exit(rc)                           /* let's quit             */
   end
end
ADDRESS "ISPEXEC" "TBTOP PPTTAB"         /* position to top of tab */
if (rc ¬= 0) then do                     /* call okay?             */
   say 'tbtop error    rc = 'rc''        /* no- inform the user    */
   ADDRESS "ISPEXEC" "LIBDEF ISPPLIB"    /* remove allocation      */
   exit(rc)                              /* let's quit             */
end
return

PPTPAN01 PANEL

)ATTR
_ TYPE(INPUT)   INTENS(HIGH) COLOR(YELLOW)
% TYPE(TEXT)    COLOR(RED)
* TYPE(TEXT)    COLOR(YELLOW)
+ TYPE(TEXT)    COLOR(WHITE)
# TYPE(OUTPUT)  INTENS(HIGH) COLOR(BLUE)
? TYPE(OUTPUT)  INTENS(HIGH) COLOR(RED)
@ TYPE(OUTPUT)  COLOR(RED)
)BODY WINDOW(77,18)
%
%COMMAND ===>_ZCMD      %                                SCROLL ===>_AMT   +
%
%IBM Supplied Default Entries=#z   %
%Non IBM Supplied Default Entries=#z   %
%
*Program   Non  Special  Non  Priv System DSI Bypass Prot CPU  2LPU 1LPU IBM
* Name     Cncl Prot/Key Swap SRM   Task       Pass  Key  Aff            DEF
%
)MODEL
#Z                                                                          %
)INIT
.ZVARS= '(PPTDIBM,PPTNDIBM,PPTENTRY)'
&ZCMD= ' '
&ZWINTTL=  'PPT Table'
)REINIT
&ZCMD= ' '
)PROC
)END

PPTPAN02 PANEL

)ATTR
_ TYPE(INPUT)   INTENS(HIGH) COLOR(YELLOW)
% TYPE(TEXT)    COLOR(RED)
* TYPE(TEXT)    COLOR(YELLOW)
+ TYPE(TEXT)    COLOR(WHITE)
# TYPE(OUTPUT)  INTENS(HIGH) COLOR(BLUE)
? TYPE(OUTPUT)  INTENS(HIGH) COLOR(RED)
@ TYPE(OUTPUT)  COLOR(RED)
)BODY WINDOW(55,5)
%
%    ----------------------------------------------
%    |  PPT Search in progress. Please wait.      |
%    ----------------------------------------------
%
)INIT
&ZCMD= ' '
&ZWINTTL=  'PPT Search'
)REINIT
&ZCMD= ' '
)PROC
)END


R F Perretta
Senior Systems Programmer (UK)                                           c Xepho

