//JOBCARD  JOB (ACCOUNT),NAME
//ASM1    EXEC PGM=IEV90,REGION=2048K,
//        PARM='OBJECT,NODECK,TERM,XREF(SHORT),RENT'
//SYSLIB   DD  DSN=SYS1.SMPMTS,DISP=SHR
//         DD  DSN=SYS1.MACLIB,DISP=SHR
//         DD  DSN=SYS1.AMODGEN,DISP=SHR
//SYSUT1   DD  SPACE=(CYL,(5,5)),
//             UNIT=SYSDA
//SYSUT2   DD  SPACE=(CYL,(5,5)),
//             UNIT=SYSDA
//SYSUT3   DD  SPACE=(CYL,(5,5)),
//             UNIT=SYSDA
//SYSTERM  DD  SYSOUT=*
//SYSPRINT DD  SYSOUT=*
//SYSPUNCH DD  DUMMY
//SYSLIN   DD   DSN=&&OBJSET,UNIT=SYSDA,SPACE=(CYL,(5,5)),
//             DISP=(MOD,PASS)
//SYSIN    DD  *
         TITLE 'CONSOLE DISPLAY FACILITY (CDF)'
***********************************************************************
*                                                                     *
*                                                                     *
*                 CONSOLE DISPLAY FACILITY (CDF)                      *
*                                                                     *
*                                                                     *
* ABSTRACT: DISPLAY MVS MASTER CONSOLE ACTIVITY AND PROCESS MVS       *
*           AND JES COMMANDS.                                         *
*                                                                     *
*                                                                     *
* FUNCTION: CDF IS DESIGNED TO OPERATE UNDER ISPF USING DISPLAY       *
*           MANAGEMENT SERVICES. IT PROVIDES A CONSOLE DISPLAY        *
*           SIMILAR TO JESOPER, DIDOCS,ETC.,        BUT SINCE IT      *
*           RUNS UNDER ISPF, IT CAN BE INVOKED WITH SPLIT-SCREEN.     *
*                                                                     *
*           CDF LOOKS AT VARIOUS MVS XA 2.1.3 CONTROL BLOCKS.      DWM*
*                                                                     *
*                                                                     *
* EXTERNAL REFERENCE: ISPLINK                                         *
*                                                                     *
*                                                                     *
* MACROS USED:                                                     DWM*
*              ABEND                                               DWM*
*              FREEMAIN                                            DWM*
*              GETMAIN                                             DWM*
*              LOAD                                                DWM*
*              DELETE                                              DWM*
*              SAVE                                                DWM*
*              RETURN                                              DWM*
*                                                                  DWM*
* DATA AREA                                                        DWM*
* MACROS USED:                                                     DWM*
*              CVT                                                 DWM*
*              IEECDCM                                             DWM*
*              IEECRDCM                                            DWM*
*              IEECUCM                                             DWM*
*              IEFUCBOB                                            DWM*
*              IHAASVT                                             DWM*
*              IHAASCB                                             DWM*
*                                                                     *
* ATTRIBUTES:                                                         *
*              RENT,REUS                                           DWM*
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
* CHANGE LOG:                                                         *
*                                                                     *
*  4/11/86 DAVID W.MEYER                                           DWM*
*    MADE PROGRAM REENTRANT                                        DWM*
*    LOAD ISPLINK AT EXECUTION TIME TO GET MOST CURRENT VERSION    DWM*
*    SEARCH FOR "CONSOLE" ASID AND DETERMINE ASID NUMBER           DWM*
*      (USED BY CROSS MEMORY SERVICES)                             DWM*
*    CODE SKIPPED LAST UCME - CORRECTED                            DWM*
*    CHANGED MASTER CONSOLE SEARCH TO USE MORE DIRECT APPROACH     DWM*
*    ADDED INACTIVE CONSOLE CHECK - IF SO, SKIP BUFFER MOVE AND    DWM*
*      TABLE BUILD                                                 DWM*
*    ADDED BUFFER LENGTH CHECK                                     DWM*
*    USED CONTROL BLOCK FIELDS TO CALCULATE LINE OFFSETS AND       DWM*
*      BUFFER LENGTH                                               DWM*
*    ADDED "CON" COMMAND TO SELECT OTHER CONSOLES                  DWM*
*                                                                     *
***********************************************************************
         EJECT
CDF      CSECT
         SAVE  (14,12),,CDF_&SYSDATE_&SYSTIME                      DWM
         LR    R12,R15        SET UP BASE REGISTER                 DWM
         USING CDF,R12                                             DWM
         GETMAIN R,LV=WORKLEN GET STORAGE FOR CDFWORK              DWM
         LR    R11,R1         SETUP CDFWORK REGISTER               DWM
         USING CDFWORK,R11                                         DWM
         LR    R0,R1          AREA TO BE CLEARED                   DWM
         LA    R1,WORKLEN     GET LENGTH TO BE CLEARED             DWM
         XR    R15,R15        ZERO PAD AND FROM LENGTH             DWM
         MVCL  R0,R14         ZERO CDFWORK                         DWM
         MVC   CDFWT,CDFWORKT MOVE IN CDFWORK EYECATCHER           DWM
         MVC   BUFS,BUFST     MOVE IN BUFFER START EYECATCHER      DWM
         MVC   BUFE,BUFET     MOVE IN BUFFER END EYECATCHER        DWM
         ST    R13,SAVEAREA+4 SAVE PREVIOUS SAVEAREA @             DWM
         LA    R15,SAVEAREA   GET @ OF MY SAVEAREA                 DWM
         ST    R15,8(R13)     LINK SAVE AREAS                      DWM
         LR    R13,R15        SETUP SAVEAREA REGISTER              DWM
*
*
*---------------------------------------------------------------------*
*                                                                     *
*                       PROGRAM INITIALIZATION                        *
*                                                                     *
*---------------------------------------------------------------------*
*
SHOWINIT DS    0H
*
         MVC   CMDLINE,BLANKS   CLEAR CMDLINES                     DWM
         LOAD  EP=ISPLINK       LOAD CURRENT ISPLINK MODULE        DWM
         ST    R0,ISPLINK       SAVE ISPLINK ADDRESS               DWM
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VCOPY,ZUSER,F8,USERID,SPFMOVE),                    DWM X
               VL,MF=(E,ISPARMS)
         LA    R2,ATABNUM                                          DWM
         LA    R3,ATABLE                                           DWM
ALOOP    DS    0H                         QUICK                    DWM
         MVC   AWORK,0(R3)                AND                      DWM
         XC    AWORK,AKEY                 DIRTY                    DWM
         CLC   USERID,AWORK               SECURITY                 DWM
         BE    ADOIT                                               DWM
         LA    R3,8(0,R3)                                          DWM
         BCT   R2,ALOOP                                            DWM
         L     R15,ISPLINK                                         DWM
         CALL  (15),                                               DWM X
               (SETMSG,                                            DWM X
               CDF001),                                            DWM X
               VL,MF=(E,ISPARMS)                                   DWM
         B     SHOWRTRN                                            DWM
*                                                                  DWM
*  LOCATE ASID NUMBER FOR 'CONSOLE' ADDRESS SPACE                  DWM
*                                                                  DWM
ADOIT    DS    0H                                                  DWM
         L     R3,CVTPTR        GET CVT ADDRESS                    DWM
         USING CVTMAP,R3                                           DWM
         L     R3,CVTASVT       GET ASVT ADDRESS                   DWM
         DROP  R3                                                  DWM
         USING ASVT,R3                                             DWM
         L     R9,ASVTMAXU      GET MAX NUMBER OF ADDRESS SPACES   DWM
         SLL   R9,2             * 4 TO GET IN ADDRESS FORM         DWM
         LA    R8,4             LOAD INCREMENT                     DWM
         LA    R6,4             START INDEX                        DWM
ASCBLP   DS    0H                                                  DWM
         LA    R7,ASVTFRST(R6)  GET ASCB POINTER                   DWM
         TM    0(R7),ASVTAVAL   IS ASCB AVAILABLE?                 DWM
         BO    ASCBNXT          YES..DO NEXT ASCB                  DWM
         L     R4,0(R7)         GET ASCB ADDRESS                   DWM
         USING ASCB,R4                                             DWM
         L     R5,ASCBJBNS      GET JOBNAME POINTER                DWM
         LTR   R5,R5            IS THE POINTER ZERO?               DWM
         BZ    ASCBNXT          YES..DO NEXT ASCB                  DWM
         CLC   CONSNAME,0(R5)   IS IT 'CONSOLE' ?                  DWM
         BE    CONSFND          FINALLY FOUND IT                   DWM
         DROP  R4                                                  DWM
ASCBNXT  DS    0H                                                  DWM
         BXLE  R6,R8,ASCBLP     KEEP SEARCHING                     DWM
         DROP  R3                                                  DWM
         L     R15,ISPLINK                                         DWM
         CALL  (15),                                               DWM X
               (SETMSG,                                            DWM X
               CDF002),                                            DWM X
               VL,MF=(E,ISPARMS)                                   DWM
         B     SHOWRTRN         NEVER FOUND 'CONSOLE'.. QUIT NOW   DWM
CONSFND  DS    0H                                                  DWM
         SRL   R6,2             DIVIDE INDEX BY 4 TO GET ASID NUMBERWM
         ST    R6,CONSASID      SAVE 'CONSOLE' ASID NUMBER         DWM
*
*  NOW THAT WE HAVE FOUND 'CONSOLE', WE MAY BEGIN
*
         MVI   ATABFL,C'0'        NO ACTIVE TABLE
*
         B     SHOWCONS       DISPLAY THE MASTER CONSOLE
*
SHOWDONE DS    0H
         CLI   ATABFL,C'0'        NO ACTIVE TABLE
         BE    SHOWRTRN
*                                  DELETE THE TABLE VARIABLES
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               JCMD),                                                  X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               CONLINEP),                                              X
               VL,MF=(E,ISPARMS)
*
*                                  DELETE THE TABLE ITSELF
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBEND,                                                 X
               CDFTABLE),                                              X
               VL,MF=(E,ISPARMS)
*
SHOWRTRN DS    0H
         DELETE EP=ISPLINK    DELETE ISPLINK MODULE                DWM
         LR    R1,R11         SETUP FOR FREEMAIN                   DWM
         L     R13,4(R13)     RESTORE THE CALLER'S R13             DWM
         FREEMAIN R,A=(1),LV=WORKLEN   FREE CDFWORK STORAGE        DWM
         RETURN (14,12),RC=0  RETURN TO OUR CALLER                 DWM
*
*
         TITLE 'CDF   START OF SHOW CONSOLE LOOP'
SHOWCONS DS    0H
*        ST    R7,CDF7HOLD SAVE THE RETURN ADDRESS                 DWM
*
         MVC   SELCODE(3),SPACES  SET SELECTION CODE TO SPACES
*
         CLI   ATABFL,C'0'        DO I HAVE AN ACTIVE TABLE?
         BE    ANOTAB             NOPE
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               JCMD),                                                  X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               CONLINEP),                                              X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBEND,                                                 X
               CDFTABLE),                                              X
               VL,MF=(E,ISPARMS)
         MVI   ATABFL,C'0'        NO ACTIVE TABLE
         TITLE 'CDF   DEFINE THE CONSOLE TABLE  '
ANOTAB   DS    0H
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               CONLINEP,TABAREA,CHAR,L79),                             X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               JCMD,CMDLINE,CHAR,L51),                             DWM X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBCREATE,                                              X
               CDFTABLE,                                               X
               NULLENT,                                                X
               DANAMLST,                                               X
               NOWRITE),                                               X
               VL,MF=(E,ISPARMS)
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBVCLEAR,                                              X
               CDFTABLE),                                              X
               VL,MF=(E,ISPARMS)
         MVI   ATABFL,C'1'        AN ACTIVE TABLE
*
*
         TITLE 'CDF   BUILD UCM TABLE           '
TAB1LOOP DS    0H
*
*---------------------------------------------------------------------*
*                                                                     *
*          BUILD A TABLE OF UCM ADDRESSES (ONE PER CONSOLE)           *
*                                                                     *
*---------------------------------------------------------------------*
BLDUCMS  L     R4,16              R4 = ADDR OF CVT
         USING CVT,R4
         L     R4,CVTCUCB         R4 = ADDR OF 'CUCB' (UCM BASE)
         DROP  R4
         USING UCM,R4
         L     R5,UCMVEA          R5 = ADDR OF FIRST UCM ENTRY
         L     R6,UCMVEZ          R6 = LENGTH OF EACH UCM ENTRY
         L     R7,UCMVEL          R7 = ADDR OF LAST UCM ENTRY
         LA    R8,UCMTAB+4        R8 = ADDR OF UCMTAB
         LA    R9,UCMTABE         R9 = ADDR OF END OF UCMTAB
         XR    R2,R2              R2  = 0 (NUMBER OF VALID UCMS)
UCMLOOP  ST    R5,0(0,R8)         SAVE UCM ADDRESS IN UCMTAB
         LA    R2,1(0,R2)         R2  = R2  + 1  (ONE MORE UCM)
         LA    R8,4(0,R8)         R8 = ADDR OF NEXT UCMTAB ENTRY
         CR    R8,R9              DOES R8 POINT PAST END OF UCMTAB?
         BNL   UCMDONE            YES; LEAVE LOOP
         AR    R5,R6              R5 = ADDR OF NEXT UCM ENTRY
         CR    R5,R7              DOES R5 POINT PAST UCM ENTRIES?
         BNH   UCMLOOP            NOPE; KEEP GOING                 DWM
UCMDONE  STH   R2,NUMUCMS         SAVE NUMBER OF UCMS FOUND
*---------------------------------------------------------------------*
*        SCAN THE UCM FOR THE MASTER CONSOLE AS THE DEFAULT        GMM*
*        CONSOLE WHEN ENTERING THE CONSOLE DISPLAY FACILITY        DWM*
*---------------------------------------------------------------------*
         L     R10,CONSOLE        GET CURRENT CONSOLE              DWM
         LTR   R10,R10            IS IT ZERO (FIRST TIME THROUGH) ?DWM
         BNZ   NEXTPAGE           NO...DONT NEED THIS              DWM
         LR    R10,R4             SETUP UCM POINTER                DWM
         S     R10,=F'4'          BACKUP POINTER TO MCS PREFIX POINTER
         L     R10,0(R10)         GET MCS PREFIX ADDRESS           DWM
         USING UCMPRFX,R10                                         DWM
         L     R9,UCMMCENT        GET ADDRESS OF MASTER CONSOLE UCMDWM
         S     R9,UCMVEA          FIND OFFSET FROM BEGINNING       DWM
         DROP  R4,R10             DONT NEED UCM ADDRESSABILITY NOW DWM
         M     R8,=F'1'           SETUP SIGN FOR DIVIDE            DWM
         DR    R8,R6              DIVIDE BY UCM ENTRY LENGTH       DWM
         LA    R9,1(0,R9)         POSITIVE INTEGERS ONLY           DWM
         ST    R9,CONSOLE         SET VALID CONSOLE NUMBER (MASTER)DWM
         TITLE 'CDF   PICK A CONSOLE            '
*---------------------------------------------------------------------*
*                                                                     *
*                            TOP OF LOOP                              *
*                                                                     *
*---------------------------------------------------------------------*
NEXTPAGE DS    0H
         LA    R5,UCMTAB          R5 = ADDR OF UCMTAB
         L     R4,CONSOLE         R4 = CONSOLE TO BE DISPLAYED     DWM
         ST    R4,OLDCONS         R4 = LAST CONSOLE DISPLAYED      DWM
         BZ    WRTERR4            WE HAVE NO CONSOLE ZERO (MASTER) GMM
         CH    R4,NUMUCMS         IS NUMBER TOO HIGH?
         BNH   GETUCM             NO, CONTINUE
*        MVI   CLEAR,X'C5'        TURN ON BELL BIT IN WCC          DWM
RESETCN  L     R4,OLDCONS         RESET TO OLD CONSOLE
         ST    R4,CONSOLE         AND SAVE IT
GETUCM   SLL   R4,2               MULTIPLY BY 4
         LA    R5,0(R5,R4)        R5 = ADDR OF ADDR OF UCM
         L     R5,0(0,R5)         R5 = ADDR OF UCM
         USING UCMLIST,R5
         L     R6,UCMXB           R6 = ADDR OF RDCM
         USING IEERDCM,R6                                          GMM
         LTR   R6,R6              IS THIS A GRAPHICS CONSOLE?
         BP    GRAPHICS           YES
         LA    R5,UCMTAB          R5 = ADDR OF UCMTAB
WRTERR4  DS    0H
*        MVI   CLEAR,X'C5'        TURN ON BELL BIT IN WCC          DWM
         B     RESETCN            RESET THE CONSOLE NUMBER
         SPACE
GRAPHICS EQU   *
         MVC   TITLE(TITLEL),BLANKS BLANK OUT CONSOLE TITLE        DWM
         MVC   TITLEC1,=C' CONSOLE='                               DWM
         MVC   TITLEC2,=C' AUTH='                                  DWM
         MVC   TITLEC3,=C' ADDR='                                  DWM
         TM    UCMDISP1,UCMDISPA  IS THIS A MASTER CONSOLE?
         BNO   AUTH0              NO
         MVC   MASTER,=CL6'MASTER' YES                             DWM
AUTH0    TM    UCMAUTHA,UCMAUTH1  IS THIS CONSOLE SYSTEM AUTHORIZED?
         BNO   AUTH1              NO
         MVC   SYS,=CL3'SYS'   YES                                 DWM
AUTH1    TM    UCMAUTHA,UCMAUTH2  IS IT I/O AUTHOZRIZED?
         BNO   AUTH2              NO
         MVC   IO,=CL3'I/O'    YES                                 DWM
AUTH2    TM    UCMAUTHA,UCMAUTH3  IS IT CONS AUTHORIZED?
         BNO   AUTHDONE           NO
         MVC   CONS,=CL4'CONS' YES                                 DWM
AUTHDONE EQU   *
         L     R7,UCMUCB          R7 = ADDR OF UCB
         USING UCBCMSEG,R7                                         DWM
         MVC   UNIT(3),UCBNAME    MOVE UNIT ADDR INTO LINE         DWM
         DROP  R7                                                  DWM
         L     R3,CONSOLE         LOAD THE CONSOLE NUMBER
         CVD   R3,WORK            CONVERT TO DECIMAL IN WORK
         MVC   SCRATCH(4),PATTERN MOVE IN EDIT PATTERN
         ED    SCRATCH(4),WORK+6  EDIT IN CONSOLE NUMBER
         MVC   CONNUM(2),SCRATCH+2 MOVE CONSOLE NUMBER INTO PLACE
         NI    FLAG1,255-CONOTACT TURN OFF NOT ACTIVE FLAG         DWM
         TM    UCMATR,UCMUF       IS CONSOLE ACTIVE?               DWM
         BO    ACTIVE1            YES..IS ACTIVE                   DWM
         OI    FLAG1,CONOTACT     INDICATE CONSOLE NOT ACTIVE      DWM
         B     TITLE1             SKIP BUFFER MOVE                 DWM
ACTIVE1  DS    0H                                                  DWM
         NI    FLAG1,255-BUFSHORT TURN OFF BUFFER TOO SHORT FLAG   DWM
         LA    R10,BUFLEN         LOAD BUFFER LENGTH FOR COMPARE   DWM
         L     R7,DCMADTRN        GET ADDRESS OF TDCM              GMM
         USING STRTDCM,7
         SPACE 2
         TITLE 'CDF   CROSS MEMORY SERVICES     '
*---------------------------------------------------------------------*
*                                                                     *
*                 FILLIN TRAILER AT BOTTOM OF SCREEN                  *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
         LA    R0,1                    REQUEST AUTHORIZATION ON    JL1
         SVC   235                     SET AUTHORIZATION ON        JL1
         MODESET MF=(E,SUPRMOD)   GET INTO SUPV STATE AND KEY ZERO JPB
*
*                                      SET UP FOR CROSS MEMORY MOVE
*
         AXRES AXLIST=AXLIST           RESERVE AN AX               JPB
         SPACE 2
         L     R1,CONSASID             'CONSOLE' ASID              DWM
         AXEXT ASID=(1)                EXTRACT AX OF 'CONSOLE'IN R0JPB
         SPACE 2
         AXSET AX=(0)                  SET OUT AX TO THAT OF CONSOLEPB
         SPACE 2
         L     R1,CONSASID             'CONSOLE' ASID              DWM
         SSAR  R1                      SET 'CONSOLE' AS SECONDARY  JPB
         SPACE 2
         PRINT ON,GEN
*
*                                      PICK UP LAST LINE
*
         SLR   R0,R0                   SECONDARY KEY
         LA    R1,L'LASTLINE           TRUE LENGTH
         MVCP  LASTLINE(R1),DCMINPUT-STRTDCM(R7),R0
*
*                                      PICK UP START ADDRESS
*
         LA    R1,4               SET TRUE LENGTH                  JPB
         SLR   R0,R0              SET STORAGE KEY                  JPB
         MVCP  SIBPTR(R1),DCMASCRN-STRTDCM(R7),R0 PT TO SCRN ADRJPB
         LA    R1,2               SET UP FOR HALF WORD MOVES       DWM
         MVCP  OUTLEN(R1),DCMLGNTH-STRTDCM(R7),R0                  DWM
         MVCP  OUTCLEN(R1),DCMCORLN-STRTDCM(R7),R0                 DWM
         MVCP  OUTNUM(R1),DCMMSGAL-STRTDCM(R7),R0                  DWM
*
*                                      SET UP FOR XMEM MOVE LOOP
*
         L     R8,SIBPTR          R8 = ADDR OF SCREEN IMAGE
         LA    R4,BUF             R4 = ADDR OF OUTPUT BUFFER
         LH    R5,OUTCLEN         GET INCORE LINE LENGTH           DWM
         MH    R5,OUTNUM          * NUMBER OF LINES = BUFFER LENGTHDWM
         CR    R5,R10             IS OUR BUFFER LONG ENOUGH        DWM
         BNH   MOVEBUFF           YES..GO DO IT                    DWM
         OI    FLAG1,BUFSHORT     INDICATE BUFFER TOO SHORT        DWM
         B     RESETSAR           DONT MOVE BUFFER                 DWM
MOVEBUFF DS    0H
         LH    R2,OUTCLEN         R2 = INCORE LINE LENGTH          DWM
         SH    R2,OUTLEN        - DATA LENGTH = # OF CONTROL CHARS DWM
         SR    R8,R2           BACKUP POINTER TO BEGINNING OF DATA DWM
         SLR   R0,R0              RESET THE KEY
*
MVCPLOOP DS    0H
         MVCP  0(R5,R4),0(R8),R0  LOOP THRU THE CONSOLE BUFFER
*
         BZ    RESETSAR
         AL    R8,=F'256'         MAX LEN ALLOWED
         AL    R4,=F'256'         MAX LEN ALLOWED
         SL    R5,=F'256'         MAX LEN ALLOWED
         B     MVCPLOOP
*
RESETSAR DS    0H
*---------------------------------------------------------------------*
*                                                                     *
*        IN ORDER TO ISSUE SVCS THE PRIMARY AND                       *
*        SECONDARY ADDRSPC MUST BE THE SAME                           *
*                                                                     *
*---------------------------------------------------------------------*
*
         EPAR  R1                 EXTRACT PRIMARY ASN
         SSAR  R1
         MODESET MF=(E,PROBMOD)   RETURN TO PROBLEM STATE          DWM
         SR    R0,R0                   REQUEST AUTHORIZATION OFF   JL1
         SVC   235                     SET AUTHORIZATION OFF       JL1
*
         DROP  R5,R6,R7                                            GMM
*
         TITLE 'CDF   BUILD ISPF TABLE'                            DWM
*                                                                  DWM
*  INSERT TITLE LINE                                               DWM
*                                                                  DWM
TITLE1   DS   0H                                                   DWM
         MVI  TABAREA,C'*'        PUT IN STARRED LINE              DWM
         MVC  TABAREA+1(L'TABAREA),TABAREA                         DWM
         MVC  TABAREA+TITLEB(TITLEL),TITLE  MOVE IN TITLE          DWM
         L     R15,ISPLINK                                         DWM
         CALL  (15),                                               DWM X
               (VPUT,                                              DWM X
               DANAMLST),                                          DWM X
               VL,MF=(E,ISPARMS)                                   DWM
         L     R15,ISPLINK                                         DWM
         CALL  (15),                                               DWM X
               (TBADD,                                             DWM X
               CDFTABLE),                                          DWM X
               VL,MF=(E,ISPARMS)                                   DWM
         MVI   TABAREA,C' '                                        DWM
         MVC   TABAREA+1(L'TABAREA-1),TABAREA                      DWM
*                                                                  DWM
*  CHECK FOR ERROR CONDITIONS AND ISSUE MESSAGES IF APPROPRIATE    DWM
*                                                                  DWM
         TM    FLAG1,CONOTACT     IS CONSOLE NOT ACTIVE?           DWM
         BZ    CHKSHORT         NO...BUILD LINES FOR ACTIVE CONSOLEDWM
         L     R15,ISPLINK        YES..GIVE THEM THE BAD NEWS      DWM
         CALL  (15),                                               DWM X
               (SETMSG,                                            DWM X
               CDF003),                                            DWM X
               VL,MF=(E,ISPARMS)                                   DWM
         B     SHOWSCRN                                            DWM
CHKSHORT DS    0H                                                  DWM
         TM    FLAG1,BUFSHORT     WAS THE BUFFER TOO SHORT         DWM
         BZ    BUILD1             NO...BUILD LINES FOR CONSOLE     DWM
         L     R15,ISPLINK        YES..GIVE THEM THE BAD NEWS      DWM
         CALL  (15),                                               DWM X
               (SETMSG,                                            DWM X
               CDF004),                                            DWM X
               VL,MF=(E,ISPARMS)                                   DWM
         B     SHOWSCRN                                            DWM
*                                                                  DWM
*  BUILD CONSOLE LINES                                             DWM
*                                                                  DWM
BUILD1   DS    0H                                                  DWM
         LH    R8,OUTNUM          GET NUMBER OF LINES              DWM
         LA    R3,BUF             SET POINTER TO FIRST LINE        DWM
         LH    R5,OUTCLEN         GET INCORE LINE LENGTH           DWM
         LH    R6,OUTLEN          GET DATA LINE LENGTH             DWM
         LR    R7,R5              SET UP FOR OFFSET CALCULATION    DWM
         SR    R7,R6              CALCULATE OFFSET TO DATA         DWM
         LA    R9,L'TABAREA       GET TABAREA LENGTH               DWM
         CR    R6,R9              DATA LENGTH > TABAREA LENGTH ?   DWM
         BNH   DOLINE             NO...USE DATA LENGTH             DWM
         LR    R6,R9              YES..USE TABAREA LENGTH          DWM
DOLINE   DS    0H                                                  DWM
         BCTR  R6,0               LENGTH - 1 FOR EXECUTE           DWM
*                                                                  DWM
NEXTL    DS    0H                                                  DWM
         LA    R4,0(R7,R3)        SET UP POINTER TO DATA           DWM
         EX    R6,MOVELINE        MOVE IT TO SPF AREA              DWM
*******************************************************************DWM*
*                                                                  DWM*
*   ADD A LINE TO THE TABLE                                        DWM*
*                                                                  DWM*
*******************************************************************DWM*
*                                                                  DWM
ADDL     DS    0H                                                  DWM
         L     R15,ISPLINK                                         DWM
         CALL  (15),                                               DWM X
               (VPUT,                                              DWM X
               DANAMLST),                                          DWM X
               VL,MF=(E,ISPARMS)                                   DWM
         L     R15,ISPLINK                                         DWM
         CALL  (15),                                               DWM X
               (TBADD,                                             DWM X
               CDFTABLE),                                          DWM X
               VL,MF=(E,ISPARMS)                                   DWM
         MVI   TABAREA,C' '                                        DWM
         MVC   TABAREA+1(L'TABAREA-1),TABAREA                      DWM
*                                                                  DWM
         LA    R3,0(R5,R3)        POINT TO NEXT LINE               DWM
*                                                                  DWM
         BCT   R8,NEXTL           PRINT NEXT LINE                  DWM
*                                                                  DWM
         TITLE 'CDF  DISPLAY SPF TABLE          '
*
***********************************************************************
*
*   NOW SHOW THE ISPPLIB FORMAT AND THE TABLE ON THE SCREEN
*
***********************************************************************
*
SHOWSCRN DS    0H
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBTOP,                                                 X
               CDFTABLE),                                              X
               VL,MF=(E,ISPARMS)
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBDISPL,                                               X
               CDFTABLE,                                               X
               CDFDATAB),                                              X
               VL,MF=(E,ISPARMS)
*
***********************************************************************
*
*   DETERMINE WHETHER ANYTHING WAS ENTERED ON THE SCREEN
*
***********************************************************************
*
         C     R15,F8         END KEY
         BE    SHOWDONE       WE ARE FINISHED RETURN               DWM
         C     R15,FZEROS     COMMAND ENTERED/LINE MODIFIED??
         BNE   SHOWDONE       NOPE..ANY OTHER CODE TERMINATES      DWM
GOTSTUFF DS    0H
         TR    CMDLINE,CAPSONLY  UPPER-CASE ALL INPUT
*
*  NOTE THE TR TABLE IS SET UP TO DELETE SEMI-COLONS, IN ORDER TO
*       PREVENT COMMAND CONCATENATION.
*
         CLI   CMDLINE,C' '   IF COMMAND AREA IS SPACES
         BE    SHOWCONS       THEN A COMMAND WAS NOT ENTERED       DWM
*                                                                  DWM
         CLC   CMDLINE(L'CONCMD),CONCMD    NEW CONSOLE REQUEST ?   DWM
         BNE   GOTCMD            NO...MUST BE CONSOLE COMMAND      DWM
         XC    WORK,WORK         CLEAR DOUBLEWORD                  DWM
         PACK  WORK+7(1),CMDLINE+L'CONCMD(1) PACK NEW CONSOLE NUMBERWM
         CVB   R1,WORK           CONVERT TO BINARY                 DWM
         ST    R1,CONSOLE        STORE NEW CONSOLE NUMBER          DWM
*                                                                  DWM
         B     CMDFINSH                                            DWM
*                                                                  DWM
*                                                                  DWM
GOTCMD   DS    0H                                                  DWM
*                                                                  DWM
         LA    R1,CMDLEN        LENGTH OF REPLY COMMAND            DWM
         STH   R1,CMDPARM       STICK IT IN THERE                  DWM
         SR    R0,R0            CLEAR REGISTER                     DWM
         STH   R0,CMDRESV       CLEAR RESERVED HALFWORD            DWM
*                                                                  DWM
         LA    R0,1                    REQUEST AUTHORIZATION ON    DWM
         SVC   235                     SET AUTHORIZATION ON        DWM
         MODESET MF=(E,SUPRMOD)   GET INTO SUPV STATE AND KEY ZERO DWM
*                                                                  DWM
         L     R0,CONSOLE              INSERT CONSOLE NUMBER       DWM
         MGCR  CMDPARM                 ISSUE COMMAND VIA SVC 34    DWM
*                                                                  DWM
         MODESET MF=(E,PROBMOD)        RETURN TO PROBLEM STATE     DWM
         SR    R0,R0                   REQUEST AUTHORIZATION OFF   DWM
         SVC   235                     SET AUTHORIZATION OFF       DWM
*                                                                  DWM
CMDFINSH DS    0H                                                  DWM
         MVI   CMDLINE,C' '                                        DWM
         MVC   CMDLINE+1(L'CMDLINE-1),CMDLINE                      DWM
*                                                                  DWM
         B     SHOWCONS                                            DWM
*                                                                  DWM
         SPACE 5
         EJECT
*
         TITLE 'MISCELLANEOUS CONSTANTS'
         LTORG
*
         DS    0F
*
*
*
FZEROS   DC    F'0'
F8       DC    F'8'
L79      DC    F'79'
L51      DC    F'51'                                               DWM
*                                                                  DWM
MOVELINE MVC   TABAREA(0),0(R4)      MOVE IT TO SPF AREA           DWM
*                                                                  DWM
TBADD    DC    C'TBADD    '
TBCREATE DC    C'TBCREATE '
TBDISPL  DC    C'TBDISPL  '
TBEND    DC    C'TBEND    '
TBTOP    DC    C'TBTOP    '
TBVCLEAR DC    C'TBVCLEAR '
VDEFINE  DC    C'VDEFINE  '
VDELETE  DC    C'VDELETE  '
VPUT     DC    C'VPUT     '
AKEY     DC    X'9459277829C12B52'                                 DWM
*
NOWRITE  DC    C'NOWRITE  '
*
NULLENT  DC    C'()'
*
DANAMLST DC    C'('             CDF NAME LIST
         DC    C'CONLINE )'     JOB INFO
CONLINEP DC    C'(CONLINE )'    JOB INFO
JCMD     DC    C'(JCMD    )'    SELECTION CODE
*
SPACES   DC    C'        '
*
CHAR     DC    C'CHAR    '
*
CDFDATAB DC    C'CDFDATAB'
CDFTABLE DC    C'CDFTABLE'
CDFWORKT DC    CL4'CDFW'                                           DWM
BUFST    DC    CL4'BUFS'                                           DWM
BUFET    DC    CL4'BUFE'                                           DWM
*
*
*---------------------------------------------------------------------*
*                                                                     *
*                         C O N S T A N T S                           *
*                                                                     *
*---------------------------------------------------------------------*
PATTERN  DC    X'40202020'         EDIT PATTERN FIELD
BLANKS   DC    CL80' '
*---------------------------------------------------------------------*
*                                                                     *
*        FOLLOWING IS IN SUPPORT OF C M S                             *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
AXLIST   DC    H'1',H'0'           AXRES PARMLIST
         SPACE 2
         SPACE 5
         TITLE 'EQUATES'
***********************************************************************
*                                                                     *
*        REGISTER EQUATES                                             *
*                                                                     *
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         DS    0F
SUPRMOD  MODESET KEY=ZERO,MODE=SUP,MF=L                            GMM
PROBMOD  MODESET KEY=NZERO,MODE=PROB,MF=L                          GMM
*                |...|...|...|...|...|...|...|...|...|...|...
CAPSONLY DC    CL64' '
         DC    CL10' '
         DC    C'¢.<(+|&&'
         DC    CL9' '
         DC    C'!$*)'
         DC    C' '             NOTE SEMICOLON CHANGED TO BLANK
         DC    C'¬-/'
         DC    CL9' ',C',%_>?',CL10' ',C':#@''="'
         DC    CL16' ABCDEFGHI      '
         DC    CL16' JKLMNOPQR      '
         DC    CL16'  STUVWXYZ      '
         DC    CL16'                '
         DC    CL16' ABCDEFGHI      '
         DC    CL16' JKLMNOPQR      '
         DC    CL16'  STUVWXYZ      '
         DC    CL16'0123456789      '
         SPACE 2
*
ATABLE   DC    X'70BBE2A1E0056B12'                                 DWM
ATABLEN  EQU   *-ATABLE                                            DWM
ATABNUM  EQU   ATABLEN/8                                           DWM
*                                                                  DWM
CDF001   DC    CL8'CDF001 '                                        DWM
CDF002   DC    CL8'CDF002 '                                        DWM
CDF003   DC    CL8'CDF003 '                                        DWM
CDF004   DC    CL8'CDF004 '                                        DWM
*                                                                  DWM
SETMSG   DC    CL8'SETMSG  '                                       DWM
*                                                                  DWM
*
VCOPY    DC    CL8'VCOPY '
ZUSER    DC    CL8'ZUSER '
SPFMOVE  DC    CL8'MOVE   '
CONSNAME DC    CL8'CONSOLE'                                        DWM
CONCMD   DC    C'CON '                                             DWM
         TITLE 'WORKING STORAGE AREAS'
*                                                                  DWM
*   WORKING STORAGE DSECT                                          DWM
*                                                                  DWM
CDFWORK  DSECT                                                     DWM
CDFWT    DS    CL4                                                 DWM
SAVEAREA DS    18F                                                 DWM
*                                                                  DWM
WORK     DS    D                                                   DWM
SCRATCH  DS    D                   SCRATCH AREA FOR CHAR. MANIP    DWM
ISPARMS  DS    10F            PARM LIST FOR ISPLINK                DWM
ISPLINK  DS    F              ISPLINK ADDRESS                      DWM
UCMTAB   DS    36F            UCM TABLE                            DWM
UCMTABE  EQU   *              UCM TABLE END                        DWM
OLDCONS  DS    F                   PREVIOUS CONSOLE NUMBER         DWM
CONSOLE  DS    F                   CONSOLE TO BE LOOKED AT         DWM
SIBPTR   DS    F                   PRT TO SCREEN IMAGE BUFFER      DWM
CONSASID DS    F                   'CONSOLE' ASID FOR CMS          DWM
NUMUCMS  DS    H              NUMBER OF UCMS                       DWM
OUTLEN   DS    H                  CONSOLE LINE IMAGE LENGTH        DWM
OUTCLEN  DS    H                  INCORE LINE LENGTH               DWM
OUTNUM   DS    H                  NUMBER OF LINES FOR CONSOLE      DWM
LASTLINE DS    CL79               OPERATORS COMMAND INPUT LINE     DWM
TITLE    DS    0CL1               CONSOLE TITLE                    DWM
TITLEC1  DS    CL9                ' CONSOLE='                      DWM
CONNUM   DS    CL2                CONSOLE NUMBER                   DWM
TITLEC2  DS    CL6                ' AUTH='                         DWM
SYS      DS    CL3                SYS  AUTHORIZATION               DWM
         DS    CL1                                                 DWM
IO       DS    CL3                I/O  AUTHORIZATION               DWM
         DS    CL1                                                 DWM
CONS     DS    CL4                CONS AUTHORIZATION               DWM
         DS    CL1                                                 DWM
MASTER   DS    CL6                MASTER CONSOLE                   DWM
TITLEC3  DS    CL6                ' ADDR='                         DWM
UNIT     DS    CL3                UNIT ADDR OF CONSOLE             DWM
         DS    CL1                                                 DWM
TITLEL   EQU   *-TITLE                                             DWM
TITLEB   EQU   (L'TABAREA-TITLEL)/2                                DWM
*                                                                  DWM
*     THE ISP TABLE FLAGS INDICATE NO ACTIVE TABLE (0), OR ONE EXISTSM
ATABFL   DS    XL1     DO I HAVE A CURRENT "DISPLAY ACTIVE" TABLE  DWM
FLAG1    DS    XL1     GENERAL PURPOSE FLAG                        DWM
CONOTACT EQU   X'80'   CONSOLE NOT ACTIVE FLAG                     DWM
BUFSHORT EQU   X'40'   BUFFER TOO SHORT CONDITION                  DWM
*                                                                  DWM
TABAREA  DS    CL79' '                                             DWM
*                                                                  DWM
SELCODE  DS    CL3            LINE SELECTION CODE                  DWM
CMDPARM  DS    H                                                   DWM
CMDRESV  DS    H                                                   DWM
CMDLINE  DS    CL51        COMMAND SVC 34 INPUT                    DWM
CMDLEN   EQU   *-CMDPARM                                           DWM
USERID   DS    CL8                                                 DWM
AWORK    DS    CL8                                                 DWM
*---------------------------------------------------------------------*
*                                                                  DWM*
*               DISPLAY SCREEN - IMAGE BUFFER SECTION              DWM*
*                                                                  DWM*
*---------------------------------------------------------------------*
BUFS     DS    CL4                                                 DWM
BUF      DS    35CL80             OPERATORS SCREEN BUFFER          DWM
BUFLEN   EQU   *-BUF              SCREEN BUFFER LENGTH             DWM
BUFE     DS    CL4                                                 DWM
*                                                                  DWM
*                                                                  DWM
*                                                                  DWM
WORKLEN  EQU   *-CDFWORK                                           DWM
         EJECT
         PRINT NOGEN
         TITLE '*** COMMUNICATION VECTOR TABLE ***'                GMM
         CVT   DSECT=YES                                           GMM
         TITLE '*** ADDRESS SPACE VECTOR TABLE ***'                DWM
         IHAASVT DSECT=YES                                         DWM
         TITLE '*** ADDRESS SPACE CONTROL BLOCK ***'               JL1
         IHAASCB DSECT=YES                                         JL1
         TITLE '*** RDCM DSECT ***'                                GMM
IEERDCM  DSECT ,                                                   GMM
         IEECRDCM                                                  GMM
         TITLE '*** TDCM DSECT ***'                                GMM
IEETDCM  DSECT ,                                                   GMM
         IEECDCM                                                   GMM
         TITLE '*** MULTIPLE CONSOLE SUPPORT (MCS) UCM PREFIX ***' GMM
         IEECUCM FORMAT=NEW
         TITLE '*** UNIT CONTROL BLOCK ***'                        DWM
         IEFUCBOB                                                  DWM
         END
/*
//LKED1    EXEC PGM=HEWLF064,PARM=(XREF,LET,LIST,TERM),
//            COND=(4,LT,ASM1),REGION=1024K
//SYSLIN   DD   DSN=&&OBJSET,DISP=(OLD,DELETE)
//SYSLMOD  DD   DISP=SHR,DSN=SYS1.TEST.LOAD(CDF)
//SYSUT1   DD   DSN=&&SYSUT1,UNIT=VIO,SPACE=(1024,(50,20))
//SYSTERM  DD   SYSOUT=*
//SYSPRINT DD   SYSOUT=*
