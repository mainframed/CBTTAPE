/*   REXX                                                             */
/*===================================================================*/
/*                                                                   */
/*                  SUB-SYSTEM CONTROL TABLE LOOKUP                  */
/*   THIS REXX WILL DISPLAY EACH SUB-SYSTEM ID AND THE SSVT          */
/*   ADDRESSSD RELATED.                                              */
/*                                                                   */
/*===================================================================*/
/*                  CONTROL BLOCK CHAINING USED                      */
/*  CVT        : X'10' ABSOLUTE ADDRESS                              */
/*  JESCT      : CVT+X'128' JES CONTROL TABLE (CVTJESCT)             */
/*  JESNRSS    : JESCT+X'44' TOTAL ACTIVE SUBSYSTEM                  */
/*  JESSSCT    : JESCT+X'18' SUB SYSTEM CONTROL TABLE                */
/*   SSCTID    : SSCT+X'00'  EYE CATCHER CONTAINING C'SSCT'          */
/*   SSCTNXT   : SSCT+X'04'  NEXT SSCT ENTRY ADDRESS                 */
/*   SSCTNAM   : SSCT+X'08'  SUB SYSTEM NAME                         */
/*   SSCTSVT   : SSCT+X'10'  SUB SYSTEM VECTOR TABLE ADDRESS         */
/*   SSCTSUSE  : SSCT+X'14'  RESERVED FOR SUB SYSTEM USE             */
/*   SSCTSYN   : SSCT+X'18'  HASH TABLE SYNONYM ADDRESS              */
/*   SSCTSUS2  : SSCT+X'1C'  RESERVED FOR SUB SYSTEM USE             */
/*   SSCTSUS3  : SSCT+X'20'  RESERVED                                */
/*********************************************************************/
Parse Upper ARG parm1 parm2;Parse SOURCE . . xcmd .

If parm1 = '??' Then Signal proc_actual
If parm1 = '/E' Then parm2 = parm1
ADDRESS TSO "%BR %"xcmd "??" parm2
Return 0

CLEAR                                          /* CLEAR SCREEN       */

PROC_ACTUAL:
CVT    = STORAGE(10,4)                         /* ADDR CVT           */
DCVT   = C2D(CVT)                              /* CONVERT TO DECIMAL */
JESCT  = STORAGE(C2X(D2C(DCVT+X2D(128))),4)    /* ADDR JESCT FROM CVT*/
DJESCT = C2D(JESCT)                            /* CONVERT TO DECIMAL */
JESSCT = STORAGE(C2X(D2C(DJESCT+X2D(18))),4)   /* ADDR SSCT FROM JSCT*/
JESNRSS= STORAGE(C2X(D2C(DJESCT+X2D(44))),2)   /* ADDR TOTAL A/SUBS  */
DNRSS  = C2D(JESNRSS)                          /* CONVERT TO DECIMAL */
LA     = 0                                     /* INIT LINE CONTROL  */
LINEC  = 0                                     /* INIT LINE CONTROL  */
LINE   = ''                                    /* INIT LINE BUFFER   */
SAY    ''                                      /*                    */
HEADER = 'Subsystem Control Table look up'     /* INIT HEADER LINE   */
HEADER = CENTER(HEADER,80)                     /* CENTERS THE LINE   */
SAY    HEADER                                  /* PUT OUT THE LINE   */
HEADER = '+------------------------------' ||, /* INIT HEADER LINE 1 */
         '-------------------------------' ||, /*        "           */
         '-------+'                            /*        "           */
HEADER = CENTER(HEADER,79)                     /* CENTERS THE LINE   */
SAY    HEADER                                  /* PUT OUT THE LINE   */
HEADER = COPIES('|Name¦  Addr  ¦  SSVT  ',3)   /* INIT TITLE LINES   */
HEADER = CENTER(HEADER || '|',79)              /* CENTERS THE LINE   */
SAY    HEADER                                  /* PUT OUT THE LINE   */
HEADER = COPIES('+----+--------+--------',3)   /* INIT TITLE LINES   */
HEADER = CENTER(HEADER || '|',79)              /* CENTERS THE LINE   */
SAY    HEADER                                  /* PUT OUT THE LINE   */
SSCTCNT= 0                                     /* INIT SSCT COUNT    */
DO UNTIL JESSCT = '00000000'X |,               /* DO UNTIL NO SSCTNXT*/
         SSCTCNT > 150                         /*    OR COUNT > 150  */
   DSSCT  = C2D(JESSCT)                        /* CONVERT TO DECIMAL */
   SSCTID = STORAGE(C2X(D2C(DSSCT+X2D(00))),4) /* ADDR SSCTID        */
   SSCTNXT= STORAGE(C2X(D2C(DSSCT+X2D(04))),4) /* LOAD NEXT SSCT ADDR*/
   SSCTNAM= STORAGE(C2X(D2C(DSSCT+X2D(08))),4) /* ADDR SSCT NAME     */
   SSCTSVT= STORAGE(C2X(D2C(DSSCT+X2D(10))),4) /* LOAD SSVT ADDRESS  */
   SSCTSUE= STORAGE(C2X(D2C(DSSCT+X2D(14))),4) /* LOAD SSVT SUSE ADDR*/
   SSCTSYN= STORAGE(C2X(D2C(DSSCT+X2D(18))),4) /* LOAD HASHTABLE ADDR*/
   SSCTSU2= STORAGE(C2X(D2C(DSSCT+X2D(1C))),4) /* LOAD SSVT SUS2 ADDR*/
   SSCTSU3= STORAGE(C2X(D2C(DSSCT+X2D(20))),4) /* LOAD SSVT SUS3 ADDR*/
   ADDR   = STRIP(C2X(JESSCT),L,'0')           /* FORMAT ADDRESS     */
   ADDR   = CENTER(ADDR,8)                     /* FORMAT ADDRESS     */
   SSVT   = C2X(SSCTSVT)                       /* FORMAT ADDRESS     */
   LINE   = LINE || '|' || SSCTNAM || '¦' ||,  /* FORMAT OUTPUT LINE */
            ADDR || '¦' || SSVT                /* FORMAT OUTPUT LINE */
   LINEC  = LINEC + 1                          /* INCREASE LINE CNT  */
   IF    LINEC = 3 THEN                        /* ARE WE HAVE 4 SSCT */
         DO                                    /* YES, DO FORMATING  */
            LINEC = 0                          /* RESET LINE CONTROL */
            LINE  = LINE || '|'                /* FORMAT LINE BUFFER */
            LINE = CENTER(LINE,79)             /* CENTERS IT         */
            SAY LINE                           /* PUT OUT THE LINE   */
            LINE = ''                          /* RE-INIT LINE BUFFER*/
         END                                   /* END OF OUTPUT      */
   JESSCT = SSCTNXT                            /* ADDRESS NEXT SSCT  */
END
IF    LINEC \= 0 THEN                          /* ARE WE HAVE 4 SSCT */
      DO                                       /* YES, DO FORMATING  */
         TRLR  = '|    |        |        '     /* INIT EMPTY TRAILER */
         LINE   = LINE || COPIES(TRLR,3-LINEC) /* EMPTY THE TRAILER  */
         LINE  = LINE || '|'                   /* FORMAT LINE BUFFER */
         LINE = CENTER(LINE,79)                /* CENTERS IT         */
         SAY LINE                              /* PUT OUT THE LINE   */
            LINE = ''                          /* RE-INIT LINE BUFFER*/
         END                                   /* END OF OUTPUT      */
HEADER = '+------------------------------' ||, /* INIT HEADER LINE   */
         '-------------------------------' ||, /*        "           */
         '-------+'                            /*        "           */
HEADER = CENTER(HEADER,79)                     /* CENTERS THE LINE   */
SAY    HEADER                                  /* PUT OUT THE LINE   */
SAY    ''                                      /* PUT OUT BREAKER    */
HEADER = 'Total number of active sub-syst' ||, /* INIT TRAILR LINE   */
         'em' DNRSS                            /*        "           */
HEADER = CENTER(HEADER,79)                     /* CENTERS THE LINE   */
SAY    HEADER                                  /* PUT OUT THE LINE   */
RETURN(0)
