/******************************** REXX ********************************/
/* )X  LINEMAC.COMMANDS(LMEXIT) <- Output (created if NF)             */
/* )I  $$              <- Infrastructure                              */
/*--------------------------------------------------------------------*/
lmac_R:lm1_ver='V3.05'                      /* LM1 copybook version   */
    Parse SOURCE . xenv xcmd# xdd xdsn .
    def_exit_rc=0                           /* Default exit return RC */
    next_exit='LMEXITMP'                    /* Next exit in chain     */
    help_expd='LMHELP01'                    /* Expanded HELP driver   */
                                            /* Leave blank if none.   */
    Signal Process

l_3:ms=mark(1) /* Super Search launcher
)M LMQS
)RC 4
*/; End_3:me=mark(-1);Return put_lines()

l_4:ms=mark(1) /* Repeat last LINEMAC command
)RC 4
*/; End_4:me=mark(-1);Return put_lines()

Start_User_Code:
/*--------------------------------------------------------------------*/
/* Place code between labels "Start_User_Code:" and "End_User_Code:". */
/* For valid Control Characters (CC) and syntax enter "HELP+" as a    */
/* line command.                                                      */
/*--------------------------------------------------------------------*/

l_JOE:ms=mark(1) /* Generate define dataset ALIAS JCL
)NA/*
)* */
)NB    Hello world
)NB    X 2
*/;End_JOE:me=mark(-1);Return put_lines()

l_paul:ms=mark(1) /* paul's demo to insert filler
)NB         03 FILLER    PIC X(20).
*/; End_PAUL:me=mark(-1);Return put_lines()

l_SD:   /* Show difference between adjacent lines  (marks with '*')   */
l_SDA:  /* - As "SD" but Adjacent lines are compared                  */
    "(caps)=CAPS";"CAPS=OFF";"(statu)=USER_STATE"
    "(caps)=CAPS";"CAPS=OFF";"(coll,colr)=DISPLAY_COLS"
    "(lineo)=LINE" line#s;If line#e=line#s Then line#e=line#s+1
    If lcmd='SDA' Then adj=1;Else Adj=0
    Do i=line#s+1 To line#e
        "(linen)=LINE" i;linex=
        If linen=lineo Then linex='Same as 1st. line'
        Else Do
            Do j=coll To colr
                If Substr(lineo,j,1)/=Substr(linen,j,1) Then linex=linex'*'
                Else linex=linex' '
            End
            If Strip(linex)='' Then linex='Offscreen differences'
        End
        ls=i;x=n_b(linex);If adj Then lineo=linen
    End
    "CAPS=(caps)";"CURSOR="line#s xcol;"USER_STATE=(status)"
    Return 4
End_SD:

l_SN:  /* Shift Right Non-excluded lines                   */
l_SLN: /* Shift Left Non-excluded lines                    */
l_SRN: /* Shift Right Non-excluded lines                   */
    "(caps)=CAPS";"CAPS=OFF";"(statu)=USER_STATE"
    "(caps)=CAPS";"CAPS=OFF"
    "(lineo)=LINE" line#s
    If Index(lcmd,'L')>0 Then dir='(';Else dir=')'
    #cols=Word(lineo,1)
    If lineo/=lcmd1stl & line#s=lcmd1l# Then Do
        "LINE &lcmd1l#=(lcmd1stl)";xcol=0
    End
    Else Do
        "(,#cols)=CURSOR";xcol=#cols;"(lbnd)=BOUNDS";#cols=#cols-lbnd
    End
    Do i=line#s To line#e
        "(xst)=XSTATUS" i;If xst='X' Then Iterate
        "SHIFT" dir i #cols
    End
    "CAPS=(caps)";"CURSOR="line#s xcol;"USER_STATE=(status)"
    Return 4
End_SN:

l_PI:             /* Progress Indicator (after - default)             */
l_PIA:            /* Progress Indicator (after)                       */
l_PIB:            /* Progress Indicator (before)                      */
l_PIL:            /* Progress Indicator (locate)                      */
l_PIN:            /* Progress Indicator (after)  as NOTELINE          */
l_PIAN:           /* Progress Indicator (after)  as NOTELINE          */
l_PIBN:           /* Progress Indicator (before) as NOTELINE          */
l_PIS:            /* Progress Indicator (after - default)        2nd. */
l_PISA:           /* Progress Indicator (after)                  2nd. */
l_PISB:           /* Progress Indicator (before)                 2nd. */
l_PISL:           /* Progress Indicator (locate)                 2nd. */
l_PISN:           /* Progress Indicator (after)  as NOTELINE     2nd. */
l_PISAN:          /* Progress Indicator (after)  as NOTELINE     2nd. */
l_PISBN:ms=mark(1) /* Progress Indicator (before) as NOTELINE    2nd. */
    "(caps)=CAPS";"CAPS=OFF";"(statu)=USER_STATE";"(lin#)=LINENUM .ZCSR"
    date=Date('e')
    If Substr(#lcmd,1,3)='PIS' Then,
         pi='*-- Progress indicator #2'
    Else pi='*-- Progress indicator'
    xxx=#lcmd
    If lcmd='PIL' Then Do;"FIND '"pi"' FIRST";Return 4;End
    If Substr(xxx,Length(xxx))='N' Then Do
        xxx=Substr(xxx,1,Length(xxx)-1);nl='NOTELINE'
    End;Else nl=
    If xxx='PIB' Then Do;where='BEFORE';lin#=lin#-1;End
    Else where='AFTER'
    pid=Substr(pi '('date') ',1,71,'-')'*'
    "LINE_"where" .ZCSR=&NL (pid)"
    If nl='' Then Do
        "LABEL" lin#"=.FMARK"
        Do Forever
            "SEEK '"pi"' PREV .ZFIRST .FMARK";If rc/=0 Then Leave
            "DEL .ZCSR .ZCSR"
            lin#=lin#-1
        End
    End
    If where='BEFORE' Then lin#=lin#+2
    "USER_STATE=(statu)";"CAPS=(caps)";"CURSOR="lin# 0
    Return 4
End_PI:

l_DAL:ms=mark(1) /* Generate define dataset ALIAS JCL
)NA/*
)* */
)NA          RELATE(primary.dataset         ))
)NA           (NAME(alias.dataset           ) -
)NA   DEFINE ALIAS -
)NA
)NA   IF MAXCC<9 THEN SET MAXCC=0
)NA   DELETE alias.dataset ALIAS
)NA//SYSIN    DD *
)NA//SYSPRINT DD SYSOUT=*
)NA//ALIAS    EXEC PGM=IDCAMS,REGION=1024K
)NA//*
)NA//*  Define alias without version number
)NA//*
)NA//*                                                                     00006
)* */ */ */ */                                                             00006
*/;End_DAL:me=mark(-1);Return put_lines()

l_QH: /* Quick Help_me instream NOTELINE Help processing */
 "(##caps##)=CAPS";"CAPS=OFF";"MASKLINE=<67,'/*QH*/'>"
 x=n_bm('n_st=mark(2)    /* Mark Start */')
 x=n_bm('/*')
 #_ls=ls;ls=line#e+1
 x=n_bm('*/')
 x=n_bm('n_en=mark(-2)   /* Mark End   */')
 x=n_bm('Address ISPEXEC "CONTROL ERRORS RETURN";ima_macro=0')
 pname='parm#';la=0;lm=0
 "F ' arg ' FIRST"
 If rc=0 Then Do
     "(line)=LINE .ZCSR"
     If Word(line,1)/='/*' Then Do
         "(la,ca)=CURSOR";Interpret "Parse VAR line ." ca+5 "pname" .
         Parse Var pname pname ';';Parse Var pname pname ','
         ls=la+1
         #pname='@_'pname'_@'
     End
     Else x=n_bm('Arg' pname)
 End
 Else Do
      x=n_bm('Arg' pname)
    #pname=
 End
 "F 'ISREDIT MACRO' FIRST";rcm=rc
 If rcm=0 Then Do
     "(line)=LINE .ZCSR"
     If Word(line,1)/='/*' Then Do
         Parse VAR line . '(' pname ')'
         "(lm)=LINENUM .ZCSR"
         If lm+1>ls Then ls=lm+1
     End
 End
 Else Do
     x=n_bm("If" pname"='' Then Do")
     x=n_bm('  "ISREDIT MACRO ('pname') NOPROCESS";If rc=0 Then ima_macro=1')
     x=n_bm('End')
 End
 If lm+la>0 Then Do
     "F 'ini1:' FIRST"
     If rc=0 Then Do;"(ls)=LINENUM .ZCSR";ls=ls+1;Signal qh_con1;End
     "F 'ini' 1 FIRST"
     If rc=0 Then Do;"(ls)=LINENUM .ZCSR";ls=ls+1;Signal qh_con1;End
     "F 'byp1:' FIRST"
     If rc=0 Then Do;"(ls)=LINENUM .ZCSR";ls=ls+1;Signal qh_con1;End
     "F ':' 1 FIRST"
     If rc=0 Then Do;"(ls)=LINENUM .ZCSR";ls=ls+1;Signal qh_con1;End
 End
qh_con1:
 If lm=0 Then Do
     x=n_bm("If" pname"='' Then Do")
     x=n_bm('  "ISREDIT MACRO ('pname') NOPROCESS";If rc=0 Then ima_macro=1')
     x=n_bm('End')
 End
 x=n_bm(' ')
 x=n_bm('w1=Word(parm#,1);If Wordpos(w1,"? ??")=0 Then Signal No_help_#1')
 x=n_bm('If w1="??" Then Signal Help_me;"ISREDIT (m)=MEMBER";rcm=rc')
 x=n_bm('If rcm=0 Then Signal Help_me;Signal On Syntax Name Help_me')
 x=n_bm('Parse Source . . xcmd .;x=$BR(xcmd "??");Return')
 x=n_bm('Mark:If Datatype(Arg(1))/="NUM" Then Arg(1)=1;Return sigl+Arg(1)')
 x=n_bm('Help_me:Signal Off Syntax;#tr=Trace("o")')
 x=n_bm('Do i=n_st To n_en;nl=Sourceline(i)')
 x=n_bm('If rcm=0 Then "ISREDIT LINE_BEFORE 1=NOTELINE (nl)"')
 x=n_bm('Else Say Substr(nl,1,79);End;Return 0')
 x=n_bm(' ')
 x=n_bm('No_help_#1:')
 ls=#_ls
 "CAPS="##caps##;"CURSOR="line#s 0;"MASKLINE=''"
 Return 4
End_QH:

l_CON:           /* Concatenate datasets using the "CONC" command. */
l_CONC:ms=mark(1) /* Concatenate datasets using the "CONC" command.
)M CONC /F P(ISRDDN)
)C 0,0
)RC 4
*/; End_CON:me=mark(-1);Return put_lines()

l_TEX:ms=mark(1) /* Testing numerics in command.
)*)EN')E' line.....
)O <45 '/*' 71 '*/'>+
)RC 4
*/; End_TEX:me=mark(-1);Return put_lines()

l_OCJ: /* Issue Operator Command in JCL                               */
l_OCJS: /* Issue Operator Command in JCL & automatically submit it    */
    xx=Strip(line@)
    If Substr(xx,1,2)='*' Then Parse VAR xx '*' xx
    If Substr(xx,1,3)='/*' Then Parse VAR xx '/*' xx '*/'
    If Substr(xx,1,3)='//*' Then Parse VAR xx '//*' xx '/*'
    Parse VAR xx xx '/*'
    Parse VAR xx xx '/OCJ'
    If lcmd='OCJS' Then yy='/S';Else yy=
    x=OCJ(Strip(xx),yy)
    Address ISREDIT "CURSOR="line# 0
    Return 4
End_OCJ:

l_REX:ms=mark(1) /* Execute member as REXX command.
)*)EN')E' line.....
)O <45 '/*' 71 '*/'>+
)RC 4
*/; End_TEX:me=mark(-1);Return put_lines()

l_TVOL:ms=mark(1) /* Generate commands to test validity of a VOLSER
)NA    Else volume='VOLUME('volser')'
)NA    End
)NA       Signal scrm
)NA       csr1="VOLSER"
)NA               "defined to the system."
)NA       zedlmsg="Volume '"volser"' not mounted or "||,
)NA    If sysreason=22 Then Do
)NA    x=Listdsi("'SYS1.LINKLIB' VOLUME("volser")")
)C 0,0
)RC 4
*/; End_TVOL:me=mark(-1);Return put_lines()

l_$OUT:ms=mark(1) /* Generate OUTTRAP commands (alternate to :OUT").  */
l_OUT:ms=mark(1) /* Generate OUTTRAP commands.
)NA    End
)NA        Say pref')' output
)NA        output=Substr(line.i,1,70);pref=Right(i,3,'0')
)NA    Do i=1 To line.0
)NA    x=Outtrap('Off');x=Msg(#m1)
)NA    Address TSO "......"
)NA    #m1=Msg('On');x=Outtrap('line.',500,'NOCONCAT')
)C 0,0
)RC 4
*/; End_$OUT:me=mark(-1);Return put_lines()

l_XP:ms=mark(1) /* Generate eXtract Parm parameter (in Notelines)
)NA    End
)NA        jf=1;parm#=Delword(parm#,Wordpos('/F',parm#),1)
)NA    If Wordpos('/F',parm#)>0 Then Do
)C 0,0
)RC 4
*/; End_XP:me=mark(-1);Return put_lines()

l_PRM:ms=mark(1) /* Generate parsed LABEL macro commands.
)A 4
)NB "LABEL" zfrange"=.MARKF"
)NB If zfrange/=zlrange Then Do
)NB     "LABEL" zlrange"=.MARKT";range='.MARKF .MARKT'
)NB End
)NB Else range='.MARKF .MARKF'
)C 0,0
)RC 4
*/; End_LAB:me=mark(-1);Return put_lines()

l_LI:ms=mark(1) /* Generate LISTDSI command and test for valid dataset
)A 4
)NB xx=Listdsi(dd file "DIRECTORY NORECALL SMSINFO")
)NB xx=Listdsi(dsn volume "DIRECTORY NORECALL SMSINFO")
)NB If xx<5 | sysreason=9 | sysreason=25 Then yy=;Else yy='Not found'
)C 0,0
)RC 4
*/; End_LI:me=mark(-1);Return put_lines()

l_SID:ms=mark(1) /* Get SYSID using REXX (as NoteLine)
)A 4
)NB sysid=Strip(storage(d2x(x2d(c2x(storage(10,4)))+x2d(154)),8))
)C 0,0
)RC 4
*/; End_SID:me=mark(-1);Return put_lines()

l_SRC:ms=mark(1) /* Generate SOURCE REXX command (as NoteLine)
)A 4
)NB Parse SOURCE op_sys op_mode xcmd_name dd_name dsn_name .
)NB Parse SOURCE . xenv xcmd xdd xdsn .
)C 0,0
)RC 4
*/; End_SRC:me=mark(-1);Return put_lines()

l_XAM: /* eXecute As Macro                                       */
    Do line#e-line#s+1
        "ISREDIT (line)=LINE" line#
        If Index(Word(line,1),'*')>0 Then line=Subword(line,2)
        "ISREDIT" line
    End
    "ISREDIT CURSOR="line# 0
    Return 4
End_XAM:

l_XAC: /* eXecute As Command                                     */
    Signal Off Syntax
    Do line#e-line#s+1
        "ISREDIT (line)=LINE" line#
        If Word(line,1)='/*' Then Parse VAR line '/*' line '*/'
        If Index(Word(line,1),'*')>0 Then line=Subword(line,2)
        Interpret line
        line#=line#+1
    End
    "ISREDIT CURSOR="line# 0
    Return 4
End_XAC:

l_XAT: /* eXecute As a Tso command                               */
    #m1=Msg('On')
    Do line#e-line#s+1
        "ISREDIT (line)=LINE" line#
        If Index(Word(line,1),'*')>0 Then line=Subword(line,2)
        line=Strip(line);Interpret 'Address TSO "'line'"'
    End
    "ISREDIT CURSOR="line# 0
    x=Msg(#m1)
    Return 4
End_XAT:

l_$DSEL:ms=mark(1) /* Invoke $DSNSEL
)* ------------------------------------------------------
)R "ISREDIT (oline)=LINE .ZCSR"
)R dsn=XB('/PV',oline)
)R If dsn='' Then dsn=XB('/PV',lcmd1stl)
)R If dsn='' Then dsn=XB('/QV',oline)
)R If dsn='' Then dsn=XB('/QV',lcmd1stl)
)RL
)WA dsnc
)R If dsn='' Then dsn=dsnc
)R If dsn/='' Then Interpret "Parse VAR lcmd1stl '"dsn"' xx"
)R If dsn/='' Then Address TSO "%$DSNSEL" Strip(dsn,,"'") xx
)RC 4
)C 0,0
*/; End_$DSEL:me=mark(-1);Return put_lines()

l_NL: /* Convert line into a NOTELINE  (can block)               */
    Do line#e-line#s+1
        "ISREDIT (line)=LINE" line#
        "ISREDIT LINE_BEFORE" line#"=NOTELINE (line)"
        "ISREDIT CURSOR=" line# 0
        "ISREDIT DEL ALL .ZCSR .ZCSR"
    End
    Return 4
End_NL:

End_User_Code:lm2_ver='V3.11'               /* LM2 copybook version   */
/*--------------------------------------------------------------------*/
/* DO NOT change any code after this line (or the above line).        */
/*--------------------------------------------------------------------*/

Process:
    Address ISREDIT
    "MACRO (LCMD) NOPROCESS"
    action='RETURN'
    Address ISPEXEC "CONTROL ERRORS" action
    zedsmsg=

    "(lev) = MACRO_LEVEL"
    If lev=1 Then Do
        "(lcmd1stl)=LINE .ZCSR";"(lcmd1l#,lcmd1c#)=CURSOR";
        Address ISPEXEC "VPUT (lcmd1stl,lcmd1l#,lcmd1c#,xcmd#) SHARED"
        "PROCESS RANGE" lcmd         /* Get range for command           */
        If rc>0 Then Do;Address ISPEXEC "SETMSG MSG(ISRZ002)";Exit 8;End
        "(ls) = LINENUM .ZFRANGE";"(le) = LINENUM .ZLRANGE"
        "(dw) = DATA_WIDTH"
        ima_master=1
    End
    Else Do
        Address ISPEXEC "VGET (lcmd1stl,lcmd1l#,lcmd1c#,xcmd#) SHARED"
        Parse VAR lcmd ls ',' le ',' lcmd
        ima_master=0
    End

    If lcmd='`' & ima_master Then Do
         Address ISPEXEC "VGET (prevlcmd) PROFILE"
         If prevlcmd/='' Then lcmd=prevlcmd
    End
    prevlcmd=lcmd;Address ISPEXEC "VPUT (prevlcmd) PROFILE"
    #lcmd=lcmd
    sp_chrs='!+/`%'
    sp_olay='12345'
    where_e='LINE_AFTER A LINE_AFTER B LINE_BEFORE R LINE N LINE'
    where_n='LINE_BEFORE A LINE_AFTER B LINE_BEFORE'
    If Verify(lcmd,sp_chrs,'M')>0 Then lcmd=label_gen(lcmd,'E')

    #msg=Msg('Off')
    retlab=error_off_01
    Signal on Syntax Name error_off_01
    line#s=ls;line#e=le
    #lcmd@=Address()
    "(lline)=LINENUM .ZLAST"
    i=ls-1
    Do While i<le
        i=i+1;line#=i;"(line@)=LINE" i
        Interpret 'x=l_'lcmd'()'
        Address(#lcmd@)
        If x/=0 Then Leave;Else Iterate
error_off_01:Signal Off Syntax
        If ima_master | next_exit/='' Then Do
            Signal on Syntax Name error_off_01a
            Interpret '"'next_exit'"' "ls','le','#lcmd"
            Signal Off Syntax
            If rc=4 Then Exit 4
            If rc>4 Then Do
               "CURSOR =" ls 0
                zinfo=lcmd;Address ISPEXEC "SETMSG MSG(ISRE041)";Exit 8
            End
            Exit rc
        End
        "CURSOR =" ls 0
        zinfo=lcmd;Address ISPEXEC "SETMSG MSG(ISRE041)";Exit 8
    End
    Return 0

error_off_01a:
    Signal Off Syntax
    Zedsmsg=
    Zedlmsg='Exit "next_exit" not found.  Line command terminated.'
    Address Ispexec "Setmsg Msg(Isrz001)"
    Exit

put_lines:
    i_pl=i
    col=1;maskline=;source_line=1;i_memb=0
    g_cc=                                   /* Global Control Char   */
    x_rc=def_exit_rc                        /* Exit rc set by )RC    */
    Do j=ms by 1 Until j>=me
/*      If s_b_s=j Then Do                                            */
/*          j=s_b_e;ms=s_b_e                                          */
/*          Parse VAR s_blocks s_b_s '-' s_b_e s_blocks               */
/*          Iterate                                                   */
/*      End                                                           */
        If j>me Then Leave
        If source_line  Then line=Sourceline(j)
        Else                 line=g_cc||linei.j
        cc=Substr(line,1,3)                 /* Control character     */
        Parse VAR cc cc1 +2 cc3
        where='LINE_AFTER';pmask=;smask=
        pad=Substr(' ',1,col-1)
        Select
            When cc1=')*' Then Iterate
            When cc=')A' Then Do
                col=Word(line,2);diff=Word(line,3)
                If diff='' Then diff=0
                If col='*' Then Do
                    "(linec) = LINE" ls
                    col=Verify(linec,' ')+diff;If col=0 Then col=1
                End
                Iterate
            End
            When cc=')E#' Then Do
                linex=pad||Substr(line,4)
                nol=(le-ls)
                where='LINE_AFTER'
                Do nol
                    where i_pl '=' pmask '(linex)' smask
                    i_pl=i_pl+1
                End
                Iterate
            End
            When cc1=')E' Then Do
                If g_cc='' Then linex=pad||Substr(line,4)
                Else            linex=pad||Substr(line,Length(g_cc)+1)
                where=Trans(cc3,1,where_e)
                If cc3='N' Then "LINE_BEFORE" i_pl"=NOTELINE (lcmd1stl)"
            End
            When cc1=')N' Then Do
                pmask='NOTELINE'
                where=Trans(cc3,1,where_n)
                If g_cc='' Then linex=pad||Substr(line,4)
                Else            linex=Substr(line,Length(g_cc)+1)
            End
            When cc=')C' Then Do
                rpc=0
                Parse Var line '+' c_l .
                If c_l/='' Then Do;i_pl=i_pl+c_l;Drop c_l;rpc=1;End
                Parse Var line '-' c_l .
                If c_l/='' Then Do;i_pl=i_pl-c_l;Drop c_l;rpc=1;End
                Parse Var line . c_l ',' c_c .
                If Substr(Translate(Strip(c_l)),1,1)='L' Then Do
                    "(x) = LINENUM .ZLAST";i_pl=x
                    Drop c_l;rpc=1
                End
                If rpc Then "CURSOR =" i_pl
                Iterate
            End
            When cc=')RC' Then Do
                If Datatype(word(line,2))='NUM' Then x_rc=Word(line,2)
                Else x_rc=0
                Iterate
            End
            When cc=')RL' Then Do
                "(line)=LINE .ZCSR"
                If line/=lcmd1stl Then "LINE &lcmd1l#=(lcmd1stl)"
                Iterate
            End
            When cc=')WA' Then Do
                "(iline)=LINE .ZCSR";"(,col#)=CURSOR"
                Interpret Word(line,2)'=$wordat(iline,col#)'
                Iterate
            End
            When cc=')M' Then Do
                macro=Word(line,2);macro_parms=Subword(line,3)
                Interpret "ISREDIT" macro "'"ls","le","#lcmd macro_parms"'"
                x_rc=rc
                Iterate
            End
            When cc=')H' Then Do
                xx=Subword(line,2)
                zedlmsg=zedlmsg Strip(xx,'t')
                Address ISPEXEC 'SETMSG MSG (ISRZ001)'
                Iterate
            End
            When cc=')D' Then Do
                line=Subword(line,2)
                If Strip(line)='' Then line='*'
                Do While line/=''
                    l#=Word(line,Words(line))
                    line=Delword(line,Words(line))
                    If l#/='*' Then "CURSOR =" ls+l# 0
                    "XSTATUS .ZCSR = NX"
                    "DELETE ALL NX .ZCSR .ZCSR"
                    Iterate
                End
                Iterate
            End
            When cc=')R' Then Do
                Interpret Subword(line,2)
                Iterate
            End
            When cc=')O' Then Do
                xx=Subword(line,2)
                Parse VAR xx '+' maskline
                If maskline/='' Then smask='+ MASKLINE'
                Else Do
                    Parse VAR xx maskline '+'
                    pmask='MASKLINE +'
                End
                "MASKLINE = "maskline
/*              "(linex) = LINE" ls                                   */
                linex=line@
                where='LINE'
            End
            When cc=')T' Then Do
                Parse VAR line . t1 t2 .
                If t1/='' & t1/='.' Then Interpret 'Trace' Word(line,2)
                If t2/='' Then Do
                    If t2/='C' Then cactn="CANCEL"
                    Else            cactn="RETURN"
                    Address ISPEXEC "CONTROL ERRORS" cactn
                End
                Iterate
            End
            When cc1=')I' Then Do
                i_#ms=j+1;i_#me=me
                Parse Value Subword(line,3) With s_b_s '-' s_b_e s_blocks
                xx=import_data(Word(line,2))
                If xx/=0 Then Leave
                i_memb=1;i=i+1
                Iterate
            End
            Otherwise Do
                If g_cc='' Then linex=pad||line
                Else            linex=pad||Substr(line,Length(g_cc)+1)
            End
        End
        where i_pl '=' pmask '(linex)' smask
        If cc1/=')N' Then i_pl=i_pl+1
    End
    If i_memb Then Do
         ms=i_#ms;me=i_#me;i=i-1
         Signal put_lines
    End
    If c_l/='C_L' Then Do
        c_c=c_c+Length(pad)-1;If c_c<0 Then c_c=0
        "CURSOR =" ls+c_l c_c
    End
    If maskline/='' Then "MASKLINE = ''"
    Return x_rc

import_data: Arg dsn
    dd='ID'Time('s')
    If Sysdsn(dsn)/='OK' Then Do
        zedlmsg=dsn "-" Sysdsn(dsn)"."
        Address ISPEXEC "SETMSG MSG(ISRZ001)"
        Exit 12
    End
    Address TSO "ALLOC F("dd") DSN("dsn") SHR REU"
    Address TSO "EXECIO * DISKR" dd "(STEM linei. FINIS)"
    rc1=rc
    Address TSO "FREE F("dd")"
    j=0;me=linei.0
    source_line=0
    Parse Value Word(line,1) With '(' xx ')'
    If xx='' | xx = '*' Then g_cc=;Else g_cc=')'xx' '
    If s_b_s='' Then Return 0
    If Datatype(s_b_s,'W') & Datatype(s_b_e,'W') Then Nop
    Else Do
        Parse VAR s_b_s s_b_s '(' o1 ')';Parse VAR s_b_e s_b_e '(' o2 ')'
        If \Datatype(o1,'w') Then o1=0;If \Datatype(o2,'w') Then o2=0
        dlm=0;i_i=0;s1=0;s2=0
        Do While dlm<2
            i_i=i_i+1
            If \s1 Then Do
                If Index(linei.i_i,s_b_s,1)=1 Then Do
                    s_b_s=i_i+o1;s1=1;dlm=dlm+1
                End
            End
            If \s2 Then Do
                If Index(linei.i_i,s_b_e,1)=1 Then Do
                    s_b_e=i_i+o2;s2=1;dlm=dlm+1
                End
            End
        End
        If \s1 Then s_b_s=1
        If \s2 Then s_b_e=linei.0
    End
    j=s_b_s-1;me=s_b_e
    Return rc1

/*--------------------------------------------------------------------*/
/* Special character's lebel encriptor/decriptor                      */
/*--------------------------------------------------------------------*/
label_gen: Arg v1,v2
    sp_subs='E  P  F  B  C '                /* 'EX PL FS BQ CP'       */
    If v2/='D' Then Do;s1=sp_chrs;s2=sp_olay;End
    Else            Do;s2=sp_chrs;s1=sp_olay;End
    Do Forever
        xx=Verify(v1,s1,'M')
        If xx=0 Then leave
        ch=Substr(v1,xx,1)
        yy=Index(s1,ch)
        v1=Overlay(Substr(s2,yy,1),v1,xx)
    End
    Return v1

/*--------------------------------------------------------------------*/
/* In-built help that should not be removed/mistaken as sample code.  */
/* This uses the command feature but should stay part of the infra.   */
/* You may place your own help version in the user area.              */
/*--------------------------------------------------------------------*/
l_HELP: /* Disp avail cmds as NL in edit session. Part of LINEMAC.    */
    If Â¬ima_master Then Signal Help_Exit
Help_Master: ms=mark(1)    /*

*----------------------------------------------------------------------*
*                                                                      *
* Line commands are brought to you with the compliments of Choon Lim,  *
* Doug Nadel and Alex Kara.  - 2003 -                                  *
*                                                                      *
*----------------------------------------------------------------------*

  The following line commands have been self extracted out of the
  LINEMAC macro. They are alphabetic sequenced for presentation purposes
  if the '$SORT' command is available. Nevertheless, they should be
  organised as the most used first.

  Cmd    Description
  ------ ---------------------------------------------------------------
*/;   me=mark(-1);Signal Help_Process

Help_Exit: ms=mark(1)    /*

  LMEXIT line commands:
  ---------------------

*/;   me=mark(-1);Signal Help_Process

Help_Process:
    "(caps)=CAPS";"CAPS=OFF"
    "(last)=LINENUM .ZLAST";If ls<last Then ls=ls+1
    Do i_h=ms To me
        line=(Sourceline(i_h))
        If Index(line,' LMEXIT line commands: ')>0 Then Do
            Parse SOURCE . . xcmd#_h .;zz=xcmd#_h 'line commands:'
            x=n_b('  'zz);i_h=i_h+1;line=('  'Copies('-',Length(zz)))
        End
        x=n_b(line)
    End
#tr=Trace('o')

l_HELP_R:lmh=;x_sep='01'x        /* Help return                       */
    excl='HELP_R HELP'
    If \ima_master Then excl=excl 'HELPX HELP+ / `'
    #tr=Trace(o)
    hstr=;sort=0;Signal on Syntax Name error_off_02;x=$sort();sort=1
error_off_02:Signal off Syntax
    Do i_h=1 To Sourceline()
        line=Sourceline(i_h)
        If Translate(Substr(line,1,2))/='L_' Then Iterate
        Parse Var line '_' cc ':' . '/*' line
        If Verify(cc,sp_olay,'M')>0 Then cc=label_gen(cc,'D')
        If Wordpos(cc,excl)>0 Then Iterate
        If line='' Then line='????? - Not documented...'
        If Word(line,Words(line))='*/' Then line=Delword(line,Words(line))
        If lcmd='HELP_R' Then lmh=lmh cc Strip(line) x_sep
        Else If sort Then hstr=hstr||Substr(Substr(cc,1,6) Strip(line),1,70)
             Else x=n_b('  'Substr(cc,1,6) Strip(line))
    End
    If sort Then Do
        s_hstr=$sort(hstr,,,70)
        Do While s_hstr/=''
          Parse VAR s_hstr oline +70 s_hstr;x=n_b('  'oline)
        End
    End
    If Verify(#tr,'NO')>0 Then x=Trace('r')
    If lcmd='HELP_R' Then Do;Address ISPEXEC "VPUT (lmh) SHARED";Return 4;End
    x=n_b(' ')
    If next_exit/='' Then Do
        Interpret '"'next_exit'"' " ls-1','le','#lcmd"
        "CURSOR=" ls-1 0
    End
    Return 4

l_HELPX: /* HELP eXpanded. Help for line commands in greater detail */
    Signal Off Syntax
    "(caps)=CAPS";"CAPS=OFF"
    If help_expd='HELP_EXPD' | help_expd='' Then Nop
    Else Do
         "(last)=LINENUM .ZLAST";If ls=last Then ls_o=ls;Else ls_o=ls+1
         Interpret help_expd ls_o
    End
    If next_exit/='' Then Interpret '"'next_exit'"' " ls','le','#lcmd"
    "CAPS=(caps)";"CURSOR=" ls 0
    Return 4

Mark:If Datatype(Arg(1))/='NUM' Then Arg(1)=1;Return sigl+Arg(1)
n_b:xx=Arg(1);"LINE_BEFORE" ls "= NOTELINE (xx)";Return 0
n_bm:xx=Arg(1);"LINE_BEFORE" ls"=NOTELINE (xx) + MASKLINE";Return 0
n_a:xx=Arg(1);"LINE_AFTER"  ls "= NOTELINE (xx)";Return 0
d_b:xx=Arg(1);"LINE_BEFORE" ls "= (xx)";Return 0
d_a:xx=Arg(1);"LINE_AFTER"  ls "= (xx)";Return 0
d_o:xx=Arg(1);"LINE"        ls "= (xx)";Return 0
Trans: Arg v1,v2,v3;Return Word(v3,Wordpos(Substr(v1,1,v2),v3)+1)

l_HELP2:lm3_ver='V3.04'   /* Display HELP for building line commands. */
    "(caps)=CAPS";"CAPS=OFF"
    "(last)=LINENUM .ZLAST";If ls<last Then ls=ls+1
    "LMM ?" ls
    "CAPS=(caps)";"CURSOR=" ls-1 0;Return 4

