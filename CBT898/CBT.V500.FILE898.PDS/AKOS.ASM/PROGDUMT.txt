***********************************************************************
*                                                                     *
*  Title   : PROGDUMP DEBUGGER                                        *
*  System  : Program debugging                                        *
*  Module  : PROGDUMT                                                 *
*  Program : PROGDUMP                                                 *
*  Function: Tutorial for PROGDUMP. This tutorial was derived from    *
*            the following ISPF panels:-                              *
*                DMP100, DMP110, DMP120, DMP130, DMP140, DMP150,      *
*                DMP160, DMP165 and DMP170                            *
*            and converted to 3270 data streams using the BUFFISPF    *
*            routine.                                                 *
*  Company : ASYSTS Pty. Ltd.                                         *
*  Author  : A. Kara                                                  *
*  Date    : 08/09/94                                                 *
*                                                                     *
*#####################################################################*
PROGDUMT KICKOFF (R10,R11,R12),WORKAREA,WORKALEN,LIST=NO
         STC   1,CICSREQ               STORE CICS USAGE FLAG
         $WTOGET PROG=PROGDUMT,TRACE=CICSREQ,HEX=ON,TRACEL=2
TOPOVER  DS    0H
         MVC   SCRGEN(SCRSZGE1),SCRGEN1 MOVE CONSTANT TO GETMAIN AREA
         LA    R7,DMP100               ADDRESS OF FIRST SCREEN
         A     R7,PANGEN               "SCRGEN" DISPLACEMENT FROM 100
TOPSCRN  DS    0H
         MVI   NORMSCR,C'Y'            NORMAL SCREEN                           รง
         LA    R5,0                    R5 ROUTINE NUMBER POINTER
SCRDISP  DS    0H
*---------------------------------------------------------------------*
* Display screen loop                                                 *
*---------------------------------------------------------------------*
         LA    R6,PANSTART             FIRST PANEL
         LR    R4,R5                   ROUTINE NUMBER
         MH    R4,=H'8'                DISPLACEMENT FROM 1ST ROUTINE
         AR    R6,R4                   ADDRESS OF ROUTINE ADDRESS
WRITESCR DS    0H
         LA    R8,DMP100               FIRST PANEL ADDRESS
         A     R8,0(R6)                PANEL ADDRESS DISPLACEMENT
         L     R9,4(R6)                ROUTINE LENGTH
TOPOVERL DS    0H
         MVC   ZCMDDISP(66,R7),SPACES  SPACE FILL ZCMD
*-----\|/--------------------<=====-----------------------------------*
*<=====  LA    R3,DMP100               ADDRESS OF FIRST SCREEN
*<=====  A     R3,PANHED               "SCRHED" DISPLACEMENT FROM 100
*<=====  TPUT  (3),SCRSZHED,NOEDIT,WAIT,,BREAKIN,HIGHP
*-----/|\--------------------<=====-----------------------------------*
         LA    2,DMP100                ADDRESS OF FIRST SCREEN
         A     2,PANHED                "SCRHED" DISPLACEMENT FROM 100
         LA    3,SCRSZHED              LOAD SIZE
         BAL   R14,TPUTSCR             CALL TPUT ROUTINE
DISPLAY  DS    0H
*-----\|/--------------------<=====-----------------------------------*
*<=====  TPUT  (7),SCRSZGEN,NOEDIT,WAIT,,BREAKIN,HIGHP
*<=====  TPUT  (8),(9),NOEDIT,WAIT,,BREAKIN,HIGHP
*-----/|\--------------------<=====-----------------------------------*
         LR    2,7                     ADDRESS OF SCREEN
         LA    3,SCRSZGEN              LOAD SIZE
         BAL   R14,TPUTSCR             CALL TPUT ROUTINE
         LR    2,8                     ADDRESS OF SCREEN
         LR    3,9                     LOAD SIZE
         BAL   R14,TPUTSCR             CALL TPUT ROUTINE
         MVC   SMSGGEND(79,R7),SPACES  CLEAR MESSAGE AREA
GETREPLY DS    0H
         MVC   REPLY,SPACES            CLEAR REPLY RECEIPT AREA
*-----\|/--------------------<=====-----------------------------------*
*<=====  TGET  REPLY,L'REPLY,ASIS,WAIT
*-----/|\--------------------<=====-----------------------------------*
         LA    2,REPLY                 ADDRESS OF REPLY AREA
         LA    3,L'REPLY               LOAD SIZE
         BAL   R14,TGETSCR             CALL TPUT ROUTINE
                                                                SPACE 1
*---------------------------------------------------------------------*
* Parse reply                                                         *
*---------------------------------------------------------------------*
PARSEREP DS    0H
         OI    REPLY,X'30'             CONVERT HI PFKEY TO LOW
         CLI   REPLY,X'7D'             Q. "ENTER" ENTERED
         BE    TESTENTR                   Y. ANY COMMAND
         CLI   REPLY,C'3'              Q. PF3/PF15 ENTERED
         BE    EXITPROG                EXIT
         CLI   NORMSCR,C'N'            Q. DISPLAY IF SEL ONLY SCREEN
         BE    TOPSCRN                    Y. GO TO TOP SCREEN
         MVI   NORMSCR,C'Y'            NORMAL SCREEN
         CLI   REPLY,C'8'              Q. PF8/PF20 ENTERED
         BE    NEXTSCRN                   Y. NEXT SCREEN
         CLI   REPLY,C'7'              Q. PF7/PF19 ENTERED
         BE    PREVSCRN                   Y. PREV SCREEN
TESTENTR DS    0H
         OI    REPLY+6,C' '            LOWER TO UPPER
         CLI   REPLY+6,C' '            Q. NEXT ENTERED
         BE    NEXTSCRN                   Y. NEXT SCREEN
         CLI   REPLY+6,C'E'            Q. "END" ENTERED
         BE    EXITPROG                EXIT
         CLI   REPLY+6,C'T'            Q. TOP  ENTERED
         BE    TOPSCRN                    Y. NEXT SCREEN
         CLI   REPLY+6,C'B'            Q. BACK ENTERED
         BE    PREVSCRN                   Y. PREV SCREEN
         CLI   REPLY+6,C'N'            Q. NEXT ENTERED
         BE    NEXTSCRN                   Y. NEXT SCREEN
         CLI   REPLY+6,C'D'            Q. DOWN ENTERED
         BE    NEXTSCRN                   Y. NEXT SCREEN
         CLI   REPLY+6,C'U'            Q. UP ENTERED
         BE    PREVSCRN                   Y. PREV SCREEN
         CH    R5,=H'0'                Q. PANEL DISPLAYED TOP
         BE    TTOPSEL                    Y. TEST VALID SELECTION
INVINPT  DS    0H
         LA    R7,DMP100               ADDRESS OF FIRST SCREEN
         A     R7,PANGEN               "SCRGEN" DISPLACEMENT FROM 100
         MVC   SMSGGEND(24,R7),=C'INVALID ENTRY/SELECTION.'
         B     TOPOVERL
NEXTSCRN DS    0H
         CH    R5,NOFPANS              Q. PANEL DISPLAYED < MAX NO
         BNL   TOPSCRN                    N. GO TO TOP
         LA    R5,1(R5)                NEXT PANEL COUNT
         B     SCRDISP                 DISPLAY SCREEN
PREVSCRN DS    0H
         LTR   R5,R5                   Q. PANEL DISPLAYED TOP
         BZ    TOPSCRN                    Y. GO TO TOP
         BCTR  R5,0                    PREV PANEL COUNT
         B     SCRDISP                 DISPLAY SCREEN
*---------------------------------------------------------------------*
* Display master menu                                                 *
*---------------------------------------------------------------------*
TTOPSEL  DS    0H
SELECT1  DS    0H
         CLC   REPLY+6(2),=C'1 '       Q. SELECTION
         BNE   SELECT2                    N. TEST NEXT SELECTION
         LA    R5,PAN110#              PANEL NUMBER
         B     SCRDISP                 VALID SELECTION
SELECT2  DS    0H
         CLC   REPLY+6(2),=C'2 '       Q. SELECTION
         BNE   SELECT3                    N. TEST NEXT SELECTION
         LA    R5,PAN120#              PANEL NUMBER
         B     SCRDISP                 VALID SELECTION
SELECT3  DS    0H
         CLC   REPLY+6(2),=C'3 '       Q. SELECTION
         BNE   SELECT4                    N. TEST NEXT SELECTION
         LA    R5,PAN160#              PANEL NUMBER
         B     SCRDISP                 VALID SELECTION
SELECT4  DS    0H
         CLC   REPLY+6(2),=C'4 '       Q. SELECTION
         BNE   SELECT5                    N. TEST NEXT SELECTION
         LA    R5,PAN170#              PANEL NUMBER
         B     SCRDISP                 VALID SELECTION
SELECT5  DS    0H
SELECT## DS    0H
         B     INVINPT                 INVALID SELECTION
                                                                SPACE 1
EXITPROG RACKOFF RC=0,LIST=NO
*---------------------------------------------------------------------*
TPUTSCR  DS    0H
         ST    R14,R14SAVER            STORE RETURN ADDRESS
         CLI   CICSREQ,C'Y'            Q. CICS ENVIRONMENT
         BNE   TSOTPUT                    N. BYPASS
*---------------------------------------------------------------------*
*   CALL PROGRAM THAT ISSUES A 'EXEC CICS SEND' COMMAND               *
*---------------------------------------------------------------------*
         ST    2,TARGET                STORE SCREEN ADDRESS
         ST    3,TARGET+4              STORE SCREEN LENGHT
         ST    13,TARGET+8             STORE SCREEN LENGHT
         LA    1,TARGET                POINT TO PARAMETER LIST
         L     15,=V(PROGCPUT)         LOAD TPUT ADDRESS                DPSO7746
         BALR  14,15                   GO PUT TO SCREEN
         B     LEAVTPUT                EXIT TPUT ROUTINE
TSOTPUT  DS    0H
         TPUT  (R2),(R3),NOEDIT,WAIT,,BREAKIN,HIGHP
LEAVTPUT DS    0H
         L     R14,R14SAVER            RESTORE RETURN ADDRESS
         BR    R14                     RETURN
*---------------------------------------------------------------------*
TGETSCR  DS    0H
         ST    R14,R14SAVER            STORE RETURN ADDRESS
         CLI   CICSREQ,C'Y'            Q. CICS ENVIRONMENT
         BNE   TSOTGET                    N. BYPASS
*---------------------------------------------------------------------*
*   CALL PROGRAM THAT ISSUES A 'EXEC CICS RECEIVE' COMMAND            *
*---------------------------------------------------------------------*
         ST    2,TARGET                STORE SCREEN ADDRESS
         ST    3,TARGET+4              STORE SCREEN LENGHT
         ST    13,TARGET+8             STORE SCREEN LENGHT
         LA    1,TARGET                POINT TO PARAMETER LIST
         L     15,=V(PROGCGET)                                          DPSO7746
         BALR  14,15
         B     LEAVTGET                EXIT TGET ROUTINE
TSOTGET  DS    0H
         TGET  (R2),(R3),ASIS,WAIT
LEAVTGET DS    0H
         L     R14,R14SAVER            RESTORE RETURN ADDRESS
         BR    R14                     RETURN
*#####################################################################*
         LTORG
*---------------------------------------------------------------------*
SPACES   DC    CL256' '
                                                                SPACE 1
*---------------------------------------------------------------------*
* Map of the screens                                                  *
*  NOTE : FULLWORD IS USED FOR SCREEN LENGTH BECAUSE WHEN ASSEMBLING  *
*         WITH OTION "ALIGN" A SKIP IS MADE TO THE NEXT FULLWORD AND  *
*         THEREFORE THE "DIVISOR" NEEDS TO BE 8 (CONSISTENCY).        *
*---------------------------------------------------------------------*
PANSTART DS    0D
PAN100A  EQU   (*-PANSTART)            DISPLACEMENT FROM START
PAN100   DC    A(PAN100A)              SELECTION  T (TOP OF PAGE)
PAN100L  DC    A(DMPSZ100)             LENGTH OF PANEL
PAN100#  EQU   ((*-PANSTART)/8)-1      PANEL NUMBER WITHIN TABLE
PAN110A  EQU   (PAN100A+DMPSZ100)      DISPLACEMENT FROM START OF TABLE
PAN110   DC    A(PAN110A)              SELECTION  1
PAN110L  DC    A(DMPSZ110)             LENGTH OF PANEL
PAN110#  EQU   ((*-PANSTART)/8)-1      PANEL NUMBER WITHIN TABLE
PAN120A  EQU   (PAN110A+DMPSZ110)      DISPLACEMENT FROM START OF TABLE
PAN120   DC    A(PAN120A)              SELECTION  2
PAN120L  DC    A(DMPSZ120)             LENGTH OF PANEL
PAN120#  EQU   ((*-PANSTART)/8)-1      PANEL NUMBER WITHIN TABLE
PAN130A  EQU   (PAN120A+DMPSZ120)      DISPLACEMENT FROM START OF TABLE
PAN130   DC    A(PAN130A)              ............
PAN130L  DC    A(DMPSZ130)             LENGTH OF PANEL
PAN130#  EQU   ((*-PANSTART)/8)-1      PANEL NUMBER WITHIN TABLE
PAN140A  EQU   (PAN130A+DMPSZ130)      DISPLACEMENT FROM START OF TABLE
PAN140   DC    A(PAN140A)              ............
PAN140L  DC    A(DMPSZ140)             LENGTH OF PANEL
PAN140#  EQU   ((*-PANSTART)/8)-1      PANEL NUMBER WITHIN TABLE
PAN150A  EQU   (PAN140A+DMPSZ140)      DISPLACEMENT FROM START OF TABLE
PAN150   DC    A(PAN150A)              ............
PAN150L  DC    A(DMPSZ150)             LENGTH OF PANEL
PAN150#  EQU   ((*-PANSTART)/8)-1      PANEL NUMBER WITHIN TABLE
PAN160A  EQU   (PAN150A+DMPSZ150)      DISPLACEMENT FROM START OF TABLE
PAN160   DC    A(PAN160A)              ............
PAN160L  DC    A(DMPSZ160)             LENGTH OF PANEL
PAN160#  EQU   ((*-PANSTART)/8)-1      PANEL NUMBER WITHIN TABLE
*---------------------------------------------------------------------*
PAN165A  EQU   (PAN160A+DMPSZ160)      DISPLACEMENT FROM START OF TABLE
PAN165   DC    A(PAN165A)              ............
PAN165L  DC    A(DMPSZ165)             LENGTH OF PANEL
PAN165#  EQU   ((*-PANSTART)/8)-1      PANEL NUMBER WITHIN TABLE
*---------------------------------------------------------------------*
PAN170A  EQU   (PAN165A+DMPSZ165)      DISPLACEMENT FROM START OF TABLE
PAN170   DC    A(PAN170A)              SELECTION  4
PAN170L  DC    A(DMPSZ170)             LENGTH OF PANEL
PAN170#  EQU   ((*-PANSTART)/8)-1      PANEL NUMBER WITHIN TABLE
PAN180A  EQU   (PAN170A+DMPSZ170)      DISPLACEMENT FROM START OF TABLE
PAN180   DC    A(PAN180A)              ............
PAN180L  DC    A(DMPSZ180)             LENGTH OF PANEL
PAN180#  EQU   ((*-PANSTART)/8)-1      PANEL NUMBER WITHIN TABLE
*---------------------------------------------------------------------*
PANS     EQU   ((*-PANSTART)/8)-1      NUMBER OF PANELS RELATIVE TO 0
PANGENA  EQU   (PAN180A+DMPSZ180)      DISPLACEMENT FROM START OF TABLE
PANGEN   DC    A(PANGENA)              OVERLAY MESSAGE PANEL
PANGENL  DC    A(SCRSZGEN)             LENGTH OF PANEL
PANGEN#  EQU   ((*-PANSTART)/8)-1      PANEL NUMBER WITHIN TABLE
PANHEDA  EQU   (PANGENA+SCRSZGEN)      DISPLACEMENT FROM START OF TABLE
PANHED   DC    A(PANHEDA)              OVERLAY MESSAGE PANEL
PANHEDL  DC    A(SCRSZHED)             LENGTH OF PANEL
PANHED#  EQU   ((*-PANSTART)/8)-1      PANEL NUMBER WITHIN TABLE
NOFPANS  DC    AL2(PANS)               NUMBER OF PANELS
*---------------------------------------------------------------------*
         COPY DMP100                TUTORIAL MASTER SELECTION PANEL
         COPY DMP110                DIRECT ADDRESSING
         COPY DMP120                MISCELLANEOUS COMMANDS
         COPY DMP130                MISCELLANEOUS COMMANDS (CONT'D)
         COPY DMP140                MISCELLANEOUS COMMANDS (CONT'D)
         COPY DMP150                MISCELLANEOUS COMMANDS (CONT'D)
         COPY DMP160                PF KEYS  1 to 12
         COPY DMP165                PF KEYS 13 to 24
         COPY DMP170                PREDEFINED COMMANDS
         COPY DMP180                PREDEFINED COMMANDS (CONT'D)
                                                                SPACE 1
         COPY SCRGEN1               GENERAL SCREEN OVERLAY
ZCMDDISP EQU (ZCMD-SCRGEN)          ZCMD'S DISPLACEMENT INTO PANEL
SMSGGEND EQU  (SMSGGEN-SCRGEN)      SMSGGEN'S DISPLACEMENT INTO PANEL
         COPY SCRHED                TUTORIAL HEAD SCREEN OVERLAY
                                                                SPACE 1
*#####################################################################*
WORKAREA DS    0D
         DS    18F
R14SAVER DS    F
CICSREQ  DS    CL1
TARGET   DS    CL80
REPLY    DS    CL79
NORMSCR  DS    CL1
         COPY SCRGEN                GENERAL SCREEN OVERLAY
WORKALEN EQU   *-WORKAREA
*---------------------------------------------------------------------*
         END
