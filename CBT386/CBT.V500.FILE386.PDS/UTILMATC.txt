UTILMATC TITLE 'MATCH SOURCE/PATTERN'
UTILMATC CSECT
UTILMATC AMODE 31
UTILMATC RMODE ANY
         $KEEQUS
******************
* HOUSEKEEPING   *
******************
         BAKR  R14,0
         LR    R12,R15
         LR    R03,R01
         USING UTILMATC,R12
         GETMAIN RU,LV=LDATA,LOC=ANY
         LR    R09,R01
         USING DYNAREA,R09
         LR    R01,R03
******************
* LOAD PARMS     *
******************
         MVC   MATPAT,MAT8BLNK
         MVC   MATSTR,MAT8BLNK
         L     R03,0(R01)                -> SOURCE
         L     R02,4(R01)                -> PATTERN
******************
* INIT SOURCE    *
******************
         SR    R06,R06
         L     R11,8(R01)
         IC    R06,0(R11)                LOAD LENGTH OF SOURCE
         BCTR  R06,0
         EX    R06,MOVESTR               MOVE TO WORK
         LA    R10,MATSTR
         LA    R06,1(R06)
         AR    R10,R06
         MVI   0(R10),MATNULL            NULL AT END OF STRING
         LA    R03,MATSTR
MENOSSTR BCTR  R10,0
         CR    R10,R03
         BL    INIPAT
         CLI   0(R10),MATNULL
         BE    MENOSSTR
         CLI   0(R10),MATBLANK
         BNE   INIPAT
         MVI   0(R10),MATNULL            NULL AT END OF STRING
         B     MENOSSTR
******************
* INIT PATTERN   *
******************
INIPAT   SR    R07,R07
         L     R11,12(R01)
         IC    R07,0(R11)                LOAD  LENGTH PATTERN
         BCTR  R07,0
         EX    R07,MOVEPAT               MOVE TO WORK
         LA    R10,MATPAT
         LA    R07,1(R07)
         AR    R10,R07
         MVI   0(R10),MATNULL            NULL AT END
         LA    R02,MATPAT
MENOSPAT BCTR  R10,0
         CR    R10,R02
         BL    COMPARE
         CLI   0(R10),MATNULL
         BE    MENOSPAT
         CLI   0(R10),MATBLANK
         BNE   COMPARE
         MVI   0(R10),MATNULL            NULL AT END
         B     MENOSPAT
********************
* BEGIN COMPARE    *
********************
COMPARE  DS    0H
         LA    R08,MATSCANS
         CLI   0(R02),MATGEN
         BNE   CHKNXT1
         CLI   1(R02),MATNULL
         BE    MATCH
CHKNXT1  DS    0H
         CR    R06,R07
         BCTR  R06,0
         BNE   CHKNXT5
         EX    R06,CHKMATCH
         BE    MATCH
CHKNXT5  DS    0H
         EX    R06,CHKBLNK2
         BE    NOMATCH
CHKNXT2  DS    0H
         BCTR  R07,0
         EX    R07,CHKBLNKS
         BE    MATCH
         SR    R04,R04
SPCHARCK DS    0H
         CLI   0(R02),MATGEN
         BE    FNDASTR
         CLI   0(R02),MATPLACE
         BNE   REGALPHA
         SR    R04,R04
NXTFCHAR DS    0H
         LA    R03,1(,R03)
NXTPCHAR DS    0H
         LA    R02,1(,R02)
         CLI   0(R02),MATNULL
         BE    PATRNEND
         CLI   0(R02),MATGEN
         BNE   CHKNXT4
         CLI   1(R02),MATNULL
         BE    MATCH
CHKNXT3  DS    0H
         CLI   0(R02),MATGEN
         BNE   CHKNXT4
         CLC   1(R02),MATGEN
         BE    NXTPCHAR
CHKNXT4  DS    0H
         CLI   0(R03),MATNULL
         BE    FAIL
         B     SPCHARCK
PATRNEND DS    0H
         CLI   0(R03),MATNULL
         BE    MATCH
         BCTR  R02,0
         B     FAIL1
REGALPHA DS    0H
         CLC   0(1,R02),0(R03)
         BNE   FAIL1
         SR    R04,R04
         B     NXTFCHAR
FNDASTR  DS    0H
         BCTR  R04,0
         CLI   1(R02),MATPLACE
         BNE   MULTICHK
         XC    0(1,R02),1(R02)
         XC    1(1,R02),0(R02)
         XC    0(1,R02),1(R02)
         B     NXTFCHAR
MULTICHK DS    0H
         STM   R02,R03,0(R08)
         LA    R08,8(,R08)
         B     NXTPCHAR
FAIL1    DS    0H
         LTR   R04,R04
         BZ    FAIL
         BCTR  R02,0
         B     NXTFCHAR
FAIL     DS    0H
         LA    R03,MATSCANS
         CR    R08,R03
         BE    NOMATCH
         SH    R08,MATH8
         LM    R02,R03,0(R08)
         BCTR  R02,0
         B     NXTFCHAR
         EJECT
NOMATCH  DS    0H
         LR    R01,R09
         FREEMAIN RU,LV=LDATA,A=(1)
         LA    R15,4(0)
         PR
MATCH    DS    0H
         LR    R01,R09
         FREEMAIN RU,LV=LDATA,A=(1)
         SLR   R15,R15
         PR
MOVESTR  MVC   MATSTR(0),0(R03)
MOVEPAT  MVC   MATPAT(0),0(R02)
CHKMATCH CLC   MATSTR(0),MATPAT
CHKBLNKS CLC   MATPAT(0),MAT8BLNK
CHKBLNK2 CLC   MATSTR(0),MAT8BLNK
MATNULL  EQU   X'00'
MATBLANK EQU   C' '
MATGEN   EQU   C'*'
MATPLACE EQU   C'%'
MATH8    DC    H'8'
MAT8BLNK DC    CL256' '
DYNAREA  DSECT
MATSCANS DS    18F
MATPAT   DS    CL256
MATSTR   DS    CL256
LDATA    EQU   *-DYNAREA
         END   UTILMATC
