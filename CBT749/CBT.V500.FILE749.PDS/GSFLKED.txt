//GILBERTL JOB (ACCT#),GSFLKED,
// NOTIFY=&SYSUID,
// CLASS=A,MSGCLASS=X,COND=(0,NE)
//ASMXF EXEC PGM=IFOX00,PARM=(OBJECT,NODECK,NOESD,NORLD,ALIGN,NOXREF)
***********************************************************************
*                                                                     *
*    THIS PROGRAM IS A FRONT-END TO THE LINKAGE-EDITOR WHICH          *
*    PROVIDES THE FOLLOWING FACILITIES:                               *
*                                                                     *
*    1. IF THERE IS AN //INCLUDE DD THAT POINTS TO AN EXISTING        *
*       PDS MEMBER, THEN THIS MEMBER IS APPENDED TO THE SYSLIN FILE.  *
*                                                                     *
*    2. IF THERE IS A //COMPRESS DD IN THE STEP AND THE LINK-EDIT     *
*       ABENDS WITH SD37-04, THEN IEBCOPY IS INVOKED TO COMPRESS      *
*       THE SYSLMOD DATA SET AFTER WHICH THE LINK-EDIT IS INVOKED     *
*       AGAIN.  THE COMPRESS DD MUST CONTAIN THE IEBCOPY SYSIN,       *
*       AS FOLLOWS:                                                   *
*                                                                     *
*            //COMPRESS DD *                                          *
*              COPY I=SYSLMOD,O=SYSLMOD                               *
*                                                                     *
*    3. IF THERE IS AN //RC0 DD DUMMY CARD IN THE STEP'S JCL,         *
*       THE RETURN CODE OF THE STEP WILL BE RESET TO ZERO.            *
*                                                                     *
***********************************************************************
GSFLKED  CSECT
         SAVE  (14,12),,'GSF UTILITIES - GSFLKED'
         BALR  R12,0
         USING *,R12
         LR    R14,R13
         LA    R13,=CL72'APPEL LINK-EDIT AVEC CODE RETOUR A ZERO'
         ST    R14,4(,R13)
         ST    R13,8(,R14)
         ST    R1,@PARM
*
*        TEST FOR THE //INCLUDE DD CARD.
*        IF PRESENT, TEST FOR AN EXISTING MEMBER.
*          IF MEMBER EXISTS, APPEND IT TO SYSLIN DD
*
         RDJFCB MF=(E,OPLPO)             SI PRESENTE,
         LTR   R15,R15                      CARTE DD 'INCLUDE'
         BNZ   LINK0
         OPEN  MF=(E,OPLPO)               CHERCHER SI LE
         MVC   BLDLIST+4(8),EXL1+4+44      MEMBRE EST
         BLDL  INPO,BLDLIST                 PRESENT
         LR    R2,R15
         CLOSE MF=(E,OPLPO)
         LTR   R2,R2                       SI PRESENT,
         BNZ   LINK0                      LIRE SON CONTENU
         OPEN  MF=(E,OPLPS)                ET LE
GETPS    GET   INCLUDE                      REECRIRE
         PUTX  SYSLIN,INCLUDE                 SUR 'SYSLIN'
         B     GETPS
FINPS    CLOSE MF=(E,OPLPS)
         FREEPOOL INCLUDE
         FREEPOOL SYSLIN
*
*        INVOKE THE LINKAGE-EDITOR
*
*          1. AS A SUB-TASK IF //COMPRESS DD PRESENT
*
*          2. VIA "LINK" IF //COMPRESS DD  IS NOT PRESENT
*
LINK0    DEVTYPE =C'COMPRESS',16(,13)    COMPRESS PREVU ?
         LTR   R15,R15                      SI NON,
         BNZ   LINK8                       PAS DE 'ATTACH'
         L     R1,@PARM                   EXEC DU LKED
         ATTACH EP=IEWL,ECB=ECB1          COMME 'SUBTASK'
         ST    R1,ECB1+4                    POUR PIEGER
         WAIT  ECB=ECB1                     LES ABENDS
         DETACH ECB1+4
         L     R1,ECB1                   SI LE CODE-RETOUR
         N     R1,=X'00FFF000'             EST ZERO,
         BZ    RETURN                       RETURN
         CL    R1,=X'00D37000'           SI ABEND
         BNE   ABENDR1                    CODE 'SD37'
*
*        COMPRESS SYSLMOD PDS
*
         MVC   INPO+40(8),=C'SYSLMOD '     PROTECTION
         RDJFCB MF=(E,OPLPO)                DU PDS
         ENQ   MF=(E,@ENQL1)                 ET COMPRESS
         LINK  EP=IEBCOPY,PARAM=(PARM0,COPYDD),VL=1
         DEQ   MF=(E,@ENQL1)
*
LINK8    L     R1,@PARM
         LINK  EP=IEWL                  EXECUTION DU LINK-EDIT
         ST    R15,ECB1                 RETURN CODE
*
RETURN   DEVTYPE =CL8'RC0',16(,R13)
         LTR   R15,R15                  '//RC0   DD DUMMY'
         BNZ   RETURN2
         XC    ECB1,ECB1                IF //RC0 PRESENT, RC=0
*
RETURN2  L     R15,ECB1                 RETURN CODE
         L     R13,4(,R13)
         RETURN (14,12),RC=(15)
ABENDR1  ABEND (1)
*
         PRINT NOGEN
INPO     DCB   DSORG=PO,MACRF=E,DDNAME=INCLUDE,EXLST=EXL1
INCLUDE  DCB   DSORG=PS,MACRF=GL,DDNAME=INCLUDE,EODAD=FINPS
SYSLIN   DCB   DSORG=PS,MACRF=PM,DDNAME=SYSLIN,LRECL=80,RECFM=FB
OPLPO    OPEN  MF=L,INPO
OPLPS    OPEN  MF=L,(INCLUDE,,SYSLIN,EXTEND)
EXL1     DC    A(X'87000004'+*),CL176' '
BLDLIST  DC    Y(1,14),CL14' '
PARM0    DC    Y(0)               PARM VIDE
COPYDD   DC    Y(8*11)
         DC    XL8'00'            1   UNUSED
         DC    XL8'00'            2   UNUSED
         DC    XL8'00'            3   UNUSED
         DC    XL8'00'            4   UNUSED
COPYDDK  DC    CL8'COMPRESS'      5   SYSIN
         DC    XL8'00'            6   UNUSED
         DC    XL8'00'            7   UNUSED
         DC    XL8'00'            8   UNUSED
         DC    XL8'00'            9   UNUSED
         DC    CL8'SYSUT1  '     10   SYSUT3
         DC    XL8'00'           11   UNUSED
QLKED    DC    C'SYSIEWLP'
@ENQL1   ENQ   (QLKED,EXL1+4,E,44,SYSTEMS, LKED / SHARED               X
               QLKED,EXL1+4,E,44,SYSTEM), LKED / NON SHARED            X
               MF=L
@PARM    DS    A
ECB1     DS    2A
*        YREGS
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
//SYSLIB   DD DSN=SYS1.MACLIB,DISP=SHR
//SYSUT1   DD UNIT=VIO,SPACE=(CYL,15)
//SYSUT2   DD UNIT=VIO,SPACE=(CYL,15)
//SYSUT3   DD UNIT=VIO,SPACE=(CYL,15)
//SYSGO    DD UNIT=VIO,SPACE=(TRK,1),DISP=(,PASS),DCB=BLKSIZE=3120
//SYSPRINT DD SYSOUT=*
//*
//LKED    EXEC PGM=HEWL,PARM='MAP,REUS,AC=1'
//SYSPRINT DD SYSOUT=*
//SYSLIN   DD DSN=*.ASMXF.SYSGO,DISP=(OLD,PASS)
//SYSLMOD  DD DSN=GILBERT.LOAD2(GSFLKED),DISP=SHR
//*
//GO      EXEC PGM=GSFLKED,PARM=(LIST,XREF)
//STEPLIB  DD DSN=GILBERT.LOAD2,DISP=SHR
//SYSLIN   DD DSN=*.ASMXF.SYSGO,DISP=(OLD,DELETE)
//SYSLMOD  DD DISP=(,PASS),UNIT=VIO,SPACE=(TRK,(1,,1))
//SYSPRINT DD SYSOUT=*
//COMPRESS DD *
 COPY INDD=SYSLMOD,OUTDD=SYSLMOD,LIST=NO
