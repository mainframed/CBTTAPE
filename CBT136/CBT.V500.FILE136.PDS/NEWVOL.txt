         TITLE 'NEW VOLUME PROGRAM'
NEWVOL   START
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         PRINT GEN
         STM   R14,R12,12(R13)           SAVE CALLERS REGISTERS
         LR    R9,R15                    INITIALIZE BASE REGISTER
         USING NEWVOL,R9,R10             ESTABLISH ADDRESSABILITY
         ST    R13,MYSAVE+4              LINK TO MINE
         LA    R11,MYSAVE                MY SAVE AREA
         ST    R11,8(R13)                LINK TO CALLERS
         LR    R13,R11                   POINT R13 TO MY SAVE AREA
         LR    R10,R9                    PUT CONTENTS OF R9 INTO R10
         AH    R10,K4                    ADD 4096
         B     BEGIN                     START EM UP
MYNAME   DC    C'NEWVOL'
VERSION  DC    C'1.0'
         DC    C' &SYSDATE, &SYSTIME'
K4       DC    H'4096'
MYSAVE   DC    18F'0'
*********************************************************************
* OPEN FILES                                                        *
*********************************************************************
BEGIN    DS    0H
         OPEN  (FATSIN,INPUT,TMCOUT,OUTPUT,SYSPRINT,OUTPUT)
         OPEN  (DSNBOUT,OUTPUT)
*********************************************************************
* INITIALIZE THE TMC PARAMETER LIST                                 *
*********************************************************************
OPENTMC  DS    0H
         OPENTMC NOTACT=READERR3         INITIALIZE PARAMETR LIST
*********************************************************************
* MAINLINE ROUTINE                                                  *
* SEARCH FATAR OUTPUT UNTIL WE HAVE FOUND AN INPUT VOLUME           *
*********************************************************************
READCARD DS    0H
         GET   FATSIN,INFAT              GET INPUT RECORD
         MVC   SYSOUT(121),INFAT
         PUT   SYSPRINT,INFAT            PRINT INFAT RECORD
         CLI   FLAG,C'Y'                 IS 'VOLSER FOUND' FLAG SET?
         BE    GETNEW                    YES - GET THE NEW VOLSER
         CLC   INFAT+63(4),=C'6250'
         BE    FOUNDVOL                  YES, GO LOOK FOR NEW VOLUME
         CLC   INFAT+63(4),=C'1600'
         BE    FOUNDVOL                  YES, GO LOOK FOR NEW VOLUME
         CLC   INFAT+63(5),=C'38000'
         BE    FOUNDVOL                  YES, GO LOOK FOR NEW VOLUME
         B     READCARD                  NO- TRY NEXT RECORD
FOUNDVOL MVC   VOLOLD,INFAT+55
         MVI   FLAG,C'Y'                 SET 'VOLSER FOUND' FLAG
         B     READCARD                  GET ANOTHER CARD
*********************************************************************
* WE HAVE FOUND THE INPUT VOLUME, NOW SEARCH FOR THE OUTPUT VOLUME
*********************************************************************
GETNEW   DS    0H
         CLC   INFAT+1(7),=C'FATS026'    IS S VOL BEGIN WITH 'A'?
         BE    FATS026                   YES, THIS IS IT.
         CLC   INFAT+16(26),=C'FATS044 END OF OUTPUT TAPE'
         BE    FATS044                   YES, THIS IS IT.
         B     READCARD                  NO, GO BACK FOR MORE.
FATS026  MVC   VOLNEW,INFAT+69           SET VOLNEW FIELD
         B     UPDATE
FATS044  MVC   VOLNEW,INFAT+43           SET VOLNEW FIELD
         B     UPDATE
VOLERROR ABEND 99,DUMP
****************************************************************
* NOW WE HAVE AN INPUT TAPE AND AN OUTPUT TAPE
* GET THE TMC RECORD FOR VOLSER OF INPUT TAPE.                 *
****************************************************************
UPDATE   DS    0H
         MVI   FLAG,C'N'                 RESET FLAG
         LA    R12,RECWORK               LOAD RECWORK AREA ADDRESS
         USING TMREC,R12
READTMC  GETTMC VOLOLD,RECWORK,READERR1,ENQ=NO
         CLC   TMPRVVOL,HEXZEROS         IS THIS THE FIRST INPUT VOL?
         BNE   READCARD                  NO, FORGET IT
****************************************************************
* BUILD JCL AND CONTROL CARDS FOR TMSUPDTE STEPS
****************************************************************
CONTROLS DS    0H
         MVC   OVOL,VOLNEW               MOVE VOLSER TO OUTPUT
         MVC   ODSN,TMDSN                MOVE DSN TO DSN/CHRG VERIFY CD
*        MVC   OPGMR,TMSPROGM            MOVE PGMR TO OUTPUT
         MVC   OPGMR(20),TMUSER          MOVE PGMR TO OUTPUT
*        MVC   OCHRG(4),TMSACCTG         MOVE 1ST 4 CHARACTERS OF CHRG
         MVC   OCHRG(4),TMUSER+20        MOVE 1ST 4 CHARACTERS OF CHRG
         MVC   OEXPDT(5),DATEMASK
         ED    OEXPDT(5),TMEXPDT         FORMAT EXPDT TO OUTPUT
         MVC   OCJOB,TMJOBNM             MOVE CJOB TO OUTPUT
         MVC   OCRTDT(5),DATEMASK
         ED    OCRTDT(5),TMCRTDT         FORMAT CRTDT TO OUTPUT
         MVC   OLJOB,TMLASUSJ            MOVE LJOB TO OUTPUT
         MVC   OLDATE(5),DATEMASK
         ED    OLDATE(5),TMLASUSD        FORMAT LDATE TO OUTPUT
         UNPK  CTIME(5),TMCRTTI          UNPACK CREATE TIME
         OI    CTIME+4,X'F0'             RESET SIGN BIT
         MVC   OCTIME(4),CTIME+1         MOVE CREATE TIME TO OCRTDT
         PUT   TMCOUT,CNTL2              WRITE VOLSER CARD
         PUT   TMCOUT,CNTL3              WRITE DSN VERIFY CARD
         PUT   TMCOUT,CNTL4              WRITE PGMR CARD
         PUT   TMCOUT,CNTL5              WRITE CHARGE CODE CARD
         PUT   TMCOUT,CNTL6              WRITE EXPDT CARD
         PUT   TMCOUT,CNTL7              WRITE CREATING JOB CARD
         PUT   TMCOUT,CNTL8              WRITE CREATION DATE CARD
         PUT   TMCOUT,CNTL15             WRITE CREATION TIME CARD
         PUT   TMCOUT,CNTL16             WRITE LAST USED JOB CARD
         PUT   TMCOUT,CNTL17             WRITE LAST USED DATE CARD
         MVC   OVOL,VOLOLD               MOVE VOLSER TO OUTPUT
         PUT   TMCOUT,CNTL2              WRITE VOLSER CARD
         PUT   TMCOUT,CNTL3              WRITE DSN VERIFY CARD
* DETERMINE DATE AND TIME
         TIME ,
         STCM  1,B'0111',RUNDATE
         UNPK  OEXPDT2(5),RUNDATE(3)
         MVZ   OEXPDT2+4(1),OEXPDT2
         PUT   TMCOUT,CNTL9              WRITE EXPDT2 CARD
         PUT   DSNBOUT,CNTL10            WRITE CNTL10 TO OUTPUT
         CLC   TMADSNB,HEXZEROS          ARE THERE MORE DATASETS?
         BE    LSTCHK                    NO - GO TO LAST CHECK
         MVC   RELBLK,TMADSNB            MOVE DSNB ADDR TO RELBLK
****************************************************************
*  GET THE NEW TMC STUFF FOR NEW DSNB UPDATES                  *
****************************************************************
READTMC1 GETTMC VOLNEW,RECWORK,READERR1,ENQ=NO
         MVC   NXTBLK,TMADSNB            MOVE DSNB ADDR TO NXTBLK
****************************************************************
*  GET DSNB STUFF
****************************************************************
GETDSNB  DS    0H
*        WTO   'GETDSNB',ROUTCDE=(11)
         CLC   RELBLK,HEXZEROS           IS THERE ANOTHER DATASET?
         BE    LSTCHK                    NO - DO THE LAST CHECK
         GETDSNB DSNBWORK,RELBLK,(R11),ENQ=NO
         USING TMSDSNB,R11
         SR    R6,R6                     CLEAR R6
         L     R6,RELBLK                 LOAD BINARY DSNB # INTO R6
         CVD   R6,PDSNBNUM               LOAD R6 IN PDSNBNUM
         UNPK  DSNBNUM(4),PDSNBNUM       UNPACK PDSNBNUM INTO CNTL11
         OI    DSNBNUM+3,X'F0'           RESET SIGN BIT FOR PRINTING
         MVC   DDSN,DSNBDSN              MOVE DSN TO CNTL12
         SR    R6,R6                     CLEAR R6
         L     R6,DSNBNEXT               LOAD NEXT DSNB # INTO R6
         CVD   R6,PDSNBNUM               LOAD R6 IN PDSNBNUM
         UNPK  DSNBNXT(4),PDSNBNUM       UNPACK PDSNBNUM INTO CNTL13
         OI    DSNBNXT+3,X'F0'           RESET SIGN BIT FOR PRINTING
         MVC   OLDEXPDT,DSNBEXDT         SAVE OLD EXPDT
         PUT   DSNBOUT,CNTL11            WRITE CNTL11 TO OUTPUT
         PUT   DSNBOUT,CNTL12            WRITE CNTL12 TO OUTPUT
         PUT   DSNBOUT,CNTL13            WRITE CNTL13 TO OUTPUT
         PUT   DSNBOUT,CNTL9             WRITE CNTL9 TO OUTPUT
         MVC   RELBLK,DSNBNEXT           STORE ADDR OF NXT DSNB
****************************************************************
*  GET NEW DSNB STUFF
****************************************************************
GETDSNB1 DS    0H
*        WTO   'GETDSNB1',ROUTCDE=(11)
         GETDSNB DSNBWORK,NXTBLK,(R11),ENQ=NO
         SR    R6,R6                     CLEAR R6
         L     R6,NXTBLK                 LOAD BINARY DSNB # INTO R6
         CVD   R6,PDSNBNUM               LOAD R6 IN PDSNBNUM
         UNPK  DSNBNUM(4),PDSNBNUM       UNPACK PDSNBNUM INTO CNTL11
         OI    DSNBNUM+3,X'F0'           RESET SIGN BIT FOR PRINTING
         SR    R6,R6                     CLEAR R6
         L     R6,DSNBNEXT               LOAD NEXT DSNB # INTO R6
         CVD   R6,PDSNBNUM               LOAD R6 IN PDSNBNUM
         UNPK  DSNBNXT(4),PDSNBNUM       UNPACK PDSNBNUM INTO CNTL13
         OI    DSNBNXT+3,X'F0'           RESET SIGN BIT FOR PRINTING
         PUT   DSNBOUT,CNTL11            WRITE CNTL11 TO OUTPUT
         PUT   DSNBOUT,CNTL12            WRITE CNTL12 TO OUTPUT
         PUT   DSNBOUT,CNTL13            WRITE CNTL13 TO OUTPUT
         MVC   OEXPDT3(5),DATEMASK       MOVE DATEMASK TO OEXPDT3
         ED    OEXPDT3(5),OLDEXPDT       FORMAT OLDEXPDT TO OUTPUT
         PUT   DSNBOUT,CNTL14            WRITE CNTL14 TO OUTPUT
         MVC   NXTBLK,DSNBNEXT           STORE ADDR OF NXT DSNB
         B     GETDSNB                   GO GET 'EM
****************************************************************
*  ONCE LAST CHECK, COMPARE NEW TAPE DSN TO OLD TAPE DSN       *
****************************************************************
LSTCHK   GETTMC VOLNEW,RECWORK,READERR1,ENQ=NO
         CLC   ODSN(44),TMDSN
         BNE   WRONGDSN
         B     READCARD
****************************************************************
*  READ ERROR ROUTINE                                          *
****************************************************************
READERR1 DS    0H
         L     13,MYSAVE+4               RESTORE SAVE AREA
         LM    14,12,12(13)              RESTORE REGS
         LA    15,99                     SET BAD RETURN CODE - 99
         BR    14                        GO BACK TO CALLER
READERR3 DS    0H
         L     13,MYSAVE+4               RESTORE SAVE AREA
         LM    14,12,12(13)              RESTORE REGS
         LA    15,77                     SET BAD RETURN CODE - 77
         BR    14                        GO BACK TO CALLER
WRONGDSN WTO   'TAPE DSNS DO NOT MATCH'
         MVC   TEMPDSN(44),TMDSN
         B     READCARD                  DEBUGGING
         ABEND 88,DUMP
****************************************************************
* CLOSE FILES AND RETURN TO CALLER                             *
****************************************************************
FINISH   DS    0H
         CLOSE (FATSIN,,TMCOUT)         CLOSE FILES FATSIN/FATSOUT
         L     13,MYSAVE+4               RESTORE SAVE AREA
         LM    14,12,12(13)              RESTORE REGS
         LA    15,0                      SET R15 TO 0
         BR    14                        GO BACK TO CALLER
****************************************************************
*  DCB DEFINITIONS                                             *
****************************************************************
FATSIN   DCB   DDNAME=FATSIN,MACRF=GM,DSORG=PS,EODAD=FINISH,           X
               LRECL=121,BLKSIZE=1210
TMCOUT   DCB   DDNAME=TMCOUT,LRECL=80,MACRF=PM,DSORG=PS,               X
               RECFM=FB,BLKSIZE=16000
DSNBOUT  DCB   DDNAME=DSNBOUT,LRECL=80,MACRF=PM,DSORG=PS,              X
               RECFM=FB,BLKSIZE=16000
SYSPRINT DCB   DDNAME=SYSPRINT,LRECL=133,MACRF=PM,DSORG=PS,            X
               RECFM=FB,BLKSIZE=13300
****************************************************************
*  RECORD FORMATS                                              *
****************************************************************
INTMC    DS    CL80
INFAT    DS    CL133
INFAT2   DS    CL80
****************************************************************
* MISC. PACKED AND ZONED DEFINITIONS
****************************************************************
         DS    0D                        DOUBLEWORD ALIGNMENT
PDSNBNUM DC    PL8'0'                    PACKED DSNB NUMBER
TEMPDSN  DC    CL44' '
RUNDATE  DC    PL3'0'                    TODAY'S DATE
OLDEXPDT DC    PL3'0'                    EXPDT OF ORIGINAL DSNB
RELBLK   DC    F'0'                      ADDR OF OLD DSNBS
NXTBLK   DC    F'0'                      ADDR OF NEW DSNBS
BLANKS   DC    CL6' '                    BLANKS
HEXZEROS DC    XL6'00'                   HEX XEROES
ZEROS    DC    PL16'0'                   ZEROS OUT RECORD
VOLOLD   DS    CL6                       INPUT VOLUME SERIAL
VOLNEW   DS    CL6                       OUTPUT VOLUME SERIAL
FLAG     DC    CL1'N'
DATEMASK DC    XL5'2020202020'
CTIME    DC    CL5' '
****************************************************************
* MISC. DEFINITIONS
****************************************************************
CNTL2    DS    0CL80                     VOLSER CARD
         DC    CL4'VOL '
OVOL     DC    CL6' '
         DC    CL70' '
*
CNTL3    DS    0CL80
         DC    CL9'VER DSN=|'
ODSN     DC    CL44' '
         DC    CL27'| '
*
CNTL4    DS    0CL80
         DC    CL10'REP PGMR=|'
OPGMR    DC    CL20' '
         DC    CL50'| '
*
CNTL5    DS    0CL80
         DC    CL10'REP CHRG=|'
OCHRG    DC    CL4' '
         DC    CL66'| '
*
CNTL6    DS    0CL80
         DC    CL10'REP EXPDT='
OEXPDT   DC    CL5' '
         DC    CL65' '
*
CNTL7    DS    0CL80
         DC    CL10'REP CJOB=|'
OCJOB    DC    CL8' '
         DC    CL62'| '
*
CNTL8    DS    0CL80
         DC    CL10'REP CRTDT='
OCRTDT   DC    CL5' '
         DC    CL65' '
*
CNTL9    DS    0CL80
         DC    CL10'REP EXPDT='
OEXPDT2  DC    CL5' '
         DC    CL65' '
*
CNTL10   DC    CL80'PASSWORD=DISABLED '
*
CNTL11   DS    0CL80
         DC    CL5'DSNB '
DSNBNUM  DC    CL4' '
         DC    CL71' '
*
CNTL12   DS    0CL80
         DC    CL9'VER DSN=|'
DDSN     DC    CL44' '
         DC    CL27'| '
*
CNTL13   DS    0CL80
         DC    CL9'VER NEXT='
DSNBNXT  DC    CL4' '
         DC    CL67' '
*
CNTL14   DS    0CL80
         DC    CL10'REP EXPDT='
OEXPDT3  DC    CL5' '
         DC    CL65' '
*
CNTL15   DS    0CL80
         DC    CL10'REP CTIME='
OCTIME   DC    CL4' '
         DC    CL66' '
*
CNTL16   DS    0CL80
         DC    CL10'REP LJOB=|'
OLJOB    DC    CL8' '
         DC    CL62'| '
*
CNTL17   DS    0CL80
         DC    CL10'REP LDATE='
OLDATE   DC    CL5' '
         DC    CL65' '
****************************************************************
* TMRECORD AND TMSDSNB WORK I/O AREAS
****************************************************************
RECWORK  DS    CL200
DSNBWORK DS    CL200
SYSOUT   DC    CL135'  '
         LTORG
****************************************************************
*  TMC AND DSNB RECORD FORMATS                                 *
****************************************************************
TMREC    DSECT ,
         TMRECORD
TMSDSNB  DSECT ,
         TMSDSNB
         END
