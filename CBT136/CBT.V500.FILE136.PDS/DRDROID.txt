//AJEEDRDR JOB 0001,'SMD01-329-J.ELSWORTH',
//    CLASS=0,MSGCLASS=A,
//    NOTIFY=*******
/*ROUTE PRINT RMT99
//*
//* REF: ASYSJEE.LIB( DRDROID )
//*
//* DOC: VTAM DR DECK BUILDER
//*
//ASM      EXEC  ASMHAL
//ASM.SYSLIB DD DSN=SYS1.MACLIB,DCB=(BLKSIZE=8000),DISP=SHR
//         DD DSN=SYS1.AMODGEN,DISP=SHR
//ASM.SYSIN  DD *
DRDROID  TITLE 'VTAM DYNAMIC RECONFIGURATION DECK BUILDER  -  P R O L O&
                G U E'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*  MODULE NAME  . . . . DRDROID
*
*  DATE WRITTEN . . . . 11/87
*
*  AUTHOR . . . . . . . JIM ELSWORTH
*
*  ENVIRONMENT  . . . . BATCH
*
*  PURPOSE:
*    AUTOMATICALLY BUILD VTAM DYNAMIC RECONFIGURATION MEMBERS FOR ALL
*    DROPS OF THE NCP SOURCE DECK POINTED TO BY SYSUT1.
*
*
*  INVOCATION:
*
* //DRDROID  EXEC PGM=DRDROID
* //STEPLIB  DD DISP=SHR,DSN=...
* //SYSPRINT DD SYSOUT=*
* //SYSUT1   DD DISP=SHR,DSN=SYS4.VTAMLST(N.....)
* //SYSUT2   DD DISP=SHR,DSN=SYS4.VTAMLST
*
*
*  DESCRIPTION:
*
*    SYSUT1 IS ASSUMED TO POINT TO AN NCP SOURCE DECK.
*    SYSUT2 CAN BE VTAMLST OR ANY OTHER PDS.
*
*    FOR EACH PU (THAT HAS SUBORDINATE LU'S) IN THE SYSUT1 INPUT
*    2 MEMBERS WILL BE BUILT AND PLACED IN SYSUT2.
*
*    THE MEMBER NAMES WILL BE DRAAABBC
*
*      WHERE:
*
*    'DR' : CONSTANT
*     AAA : 3 DIGIT LINE LUMBER FOR THE PU
*      BB : 2 DIGIT CONTROLLER ADDRESS
*       C : 'M' - MOVE MEMBER
*            1) - DELETES ALL LU'S FROM THE DISABLED PU
*            2) - ADDS A PU TO THE DR LINE
*            3) - ADDS LU'S TO THE PU ON THE DR LINE
*
*           'R' - RESTORE MEMBER
*            1) - DELETES THE PU ADDED IN M-2
*            2) - ADDS THE LU'S BACK TO THEIR ORIGINAL PU
*
*    IF THE CONSTANT '*NODR* APPEAR IN COLUMN 40 ON THE PU STATEMENT
*    NO DR MEMBERS WILL BE GENERATED FOR THAT PU.
*
*
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*  ASSUMPTIONS:
*
*   ¢ ALL LU/PU SPECIFICATIONS IN THE SOURCE DECK ARE ASSUMED TO
*     FIT ON ONE LINE.
*
*   ¢ BATCH DEVICES ARE ASSUMED TO EXPLICITLY SPECIFY PACING.
*
*   ¢ LU AND PU RECORD TYPES ARE EXPECTED TO CONTAIN EITHER 'LU' OR
*     'PU' IN COLUMN 10.
*
*  CAVEATS:
*
*   ¢ SYSUT2 DATASET INTEGRITY IS MAINTAINED AS LONG AS THE ONLY OTHER
*     MANNER OF SYSUT2 UPDATING IS VIA SPF EDIT.
*
*   ¢ SEVERE SYNTAX ERRORS ON SYSUT1 WILL CAUSE UNPREDICTABLE RESULTS.
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 3
***********************************************************************
*  USER MODIFIABLE PARAMETERS
***********************************************************************
         SPACE 1
         GBLC  &DRLNO             DR LINE NUMBER
&DRLNO   SETC  '???'
         GBLC  &DRLNM             DR LINE NAME
&DRLNM   SETC  'LNA'.'&DRLNO'
         GBLC  &DLOGMOD           DEFAULT LOGMODE
&DLOGMOD SETC  'T3278M2'
         SPACE 1
* -- OUTPUT OPTIONS
         SPACE 1
OUTDISK  EQU   X'80'              .DISK ONLY
OUTPRT   EQU   X'40'              .PRINT ONLY
*
OUTOPT   EQU   OUTDISK
         SPACE 1
DRMAXLU  EQU   128                MAXIMUM LU'S PER PU
         TITLE 'VTAM DYNAMIC RECONFIGURATION DECK BUILDER  -  M A C R O&
                '' S'
         MACRO
&LABEL   @PUT  &TEXT,                   TEXT TO BE PUT                 &
               &LENGTH,                 LENGTH OF TEXT                 &
               &EJECT=NO                EJECT CC
.* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
.*
.* --> @ P U T
.*
.* SETS UP CALL TO LOCAL PRINT ROUTINE
.*
.* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
.*
.* MESSAGE TEXT
.*
&LABEL   LA    R1,&TEXT                 ADDRESS OF TEXT TO PUT
.*
.* MESSAGE LENGTH
.*
         AIF   ('&LENGTH' NE '').A200
         LA    R0,L'&TEXT               LENGTH OF TEXT
         AGO   .A299
.A200    ANOP
         LA    R0,&LENGTH               LENGTH OF TEXT
.A299    ANOP
.*
         AIF   ('&EJECT' NE 'YES').A600
         MVI   DRSYSM,C'1'              EJECT CC
.*
.A600    ANOP
.*
.* LOCAL CALL
.*
.A2000   ANOP
         BAL   R14,DR@PUT               CALL LOCAL PRINT ROUTINE
         AIF   ('&EJECT' NE 'YES').AMEND
         MVI   DRSYSM,C' '              RESTORE CC
.*
.AMEND   ANOP
         MEND
         EJECT
         MACRO
.* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
.*
.* --> @ W T O
.*
.* SETS UP CALL TO LOCAL WTO ROUTINE
.*
.* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
&LABEL   @WTO  &TEXT,                   TEXT TO BE PUT                 &
               &LENGTH,                 LENGTH OF TEXT                 &
               &MSGNO                   MESSAGE NUMBER
.*
         AIF   ('&TEXT' NE '-').A100
&LABEL   SR    R1,R1                    INDICATE A DELIMITER LINE
         AGO   .A2000
.*
.A100    ANOP
&LABEL   LA    R1,&TEXT                 ADDRESS OF TEXT TO PUT
.*
.* MESSAGE LENGTH
.*
         AIF   ('&LENGTH' NE '').A200
         LA    R0,L'&TEXT               LENGTH OF TEXT
         AGO   .A299
.A200    ANOP
         LA    R0,&LENGTH               LENGTH OF TEXT
.A299    ANOP
.*
.* MESSAGE NUMBER
.*
         AIF   ('&MSGNO' NE '').A600
         LA    R2,=C'--'                DEFAULT MESSAGE NUMBER
         AGO   .A2000
.*
.A600    ANOP
         LA    R2,=CL2'&MSGNO'          MESSAGE NUMBER
.*
.A2000   ANOP
         BAL   R14,DR@WTO               CALL LOCAL WTO ROUTINE
.*
.AMEND   ANOP
         MEND
         EJECT
         MACRO
.* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
.*
.* --> @ E D I T
.*
.* PREPARE A PACKED FIELD FOR PRINTING
.*
.* ! DESTROYS R15
.*
.* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
.*
&LABEL   @EDIT &DISPLAY,               DISPLAY FIELD                   &
               &PACKED                 PRINT FIELD
.*
&LABEL   MVC   DRWORK(L'DRMASK1),DRMASK1    EDIT MASK
         ED    DRWORK(L'DRMASK1),&PACKED    EDIT THE PACKED FIELD
         MVC   &DISPLAY,DRWORK+8-L'&DISPLAY MOVE TO DISPLAY FIELD
         MEND
         SPACE 3
         MACRO
.* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
.*
.* --> @CHECK
.*
.* CHECK RETURN CODE IN R15
.*
.* IF NON ZERO THEN SET UP FOR ABEND PROCESSING
.*
.* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
.*
&LABEL   @CHECK
.*
         LCLC  &L1,&L2
&L1      SETC  'DR'.'&SYSNDX'.'C1'
&L2      SETC  'DR'.'&SYSNDX'.'C2'
.*
&L1      EQU   *
&LABEL   LTR   R15,R15                 RETURN CODE OK
         BZ    &L2                     -YUP
         STM   R14,R15,DRABRSA+(4*14)  -NOPE, SAVE 14
         LA    R14,&L1                 ERROR LOCATION
         B     DRZABEND                GO TO ABEND PROCESSING
&L2      EQU   *
         MEND
         TITLE 'VTAM DYNAMIC RECONFIGURATION DECK BUILDER  -  LOCAL DSE&
               CTS AND EQUATES'
*
*  REGISTER SYMBOLICS
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3                   WORK REGISTER
R4       EQU   4                   WORK REGISTER
RN       EQU   9                   LU ENTRY NUMBER
RSYSUT1  EQU   10                  @DETAIL SYSUT1 RECORD
RJFCB    EQU   11                  @JFCB
RBASE2   EQU   12                  2ND BASE REGISTER
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 3
*
*  VTAM / NCP DETAIL RECORD LAYOUT (SUCH AS IT IS)
*
DRDREC   DSECT
DRDRES   DS    CL8                 RESOURCE NAME
         ORG   DRDREC+1
DRDLD    DS    CL5                 LINE / DROP
         ORG
         DS    C                   .FILLER
DRDRTYP  DS    CL5'LU   '          RESOURCE TYPE
         DS    C                   .FILLER
         DS    24C                 .FILLER
DRDCOM   DS    31C                 COMMENT FIELD
DRDRECL  EQU   *-DRDREC            RECORD LENGTH
         SPACE 3
*
*  MISCELLANEOUS DSECTS
*
         PUSH  PRINT
         PRINT NOGEN
         DCBD  DSORG=PS,DEVD=DA         DCB DSECTS
         POP   PRINT
         SPACE 5
         TITLE 'VTAM DYNAMIC RECONFIGURATION DECK BUILDER  -  M A I N L&
                I N E'
***********************************************************************
*  OBSERVE LINKAGE CONVENTIONS
***********************************************************************
         SPACE 1
DRDROID  CSECT
         USING DRDROID,R15              TEMP ADDRESSABILITY
         B     *+28                     BRANCH AROUND EYECATCHED
         DC    CL24'DRDROID-&SYSDATE-&SYSTIME'
         STM   14,12,12(13)             SAVE CALLERS REGS
         BAL   R3,DRSLRSA+72            BR AROUND RSA
DRSLRSA  DC    18F'-1'                  REGISTER SAVE AREA
         DROP  R15                      END TEMP USING
         ST    3,8(13)                  @OUR SAVE AREA
         ST    13,4(3)                  @CALLERS SAVE AREA
         LR    13,3                     @OUR SAVE AREA
         USING DRSLRSA,13               SAVE AREA IS ALSO THE BASE
         LA    RBASE2,4095(R13)         ESTABLISH
         LA    RBASE2,1(RBASE2)         ..SECOND
         USING DRSLRSA+4096,RBASE2      ..BASE
         SPACE 2
***********************************************************************
*  OPEN DATASETS
***********************************************************************
         SPACE 1
         OPEN  (SYSPRINT,OUTPUT)
         OPEN  (SYSUT1,INPUT)
         SPACE 1
***********************************************************************
*  GET JFCB FOR SYSUT2
***********************************************************************
         SPACE 1
         LA    R3,SYSUT2
         USING IHADCB,R3                MAP DCB
         MVC   DCBEXLST+1(R3),=AL3(DREXLST) FILL IN EXIT LIST ADDRESS
         DROP  R3                       END DCB ADDRESSABILITY
         RDJFCB ((R3))                  READ THE JFCB
         LA    RJFCB,DRJFCB             @JFCB
         USING INFMJFCB,RJFCB           MAP JFCB
         OI    JFCBIND1,JFCPDS          INDICATE THIS IS A PDS
         MVC   DRMINOR(44),JFCBDSNM     EXTRACT DATASET NAME FOR ENQ
         SPACE 2
***********************************************************************
* INITIALIZE FOR DETAIL PROCESSING
***********************************************************************
         SPACE 1
* DATE STAMP
         TIME
         ST    R1,DRPDATE
         OI    DRPDATE+3,X'0F'         SPF DIRECTORY ENTRY FORMAT
         UNPK  DRWORK(5),DRPDATE       MAKE PRINTABLE
         MVC   DRDATE(2),DRWORK        YY
         MVC   DRDATE+3(3),DRWORK+2    DDD
*
* TIME STAMP
*
         ST    R0,DRPTIME
         UNPK  DRWORK(7),DRPTIME       MAKE PRINTABLE
         MVC   DRTIME(2),DRWORK        HH
         MVC   DRTIME+3(2),DRWORK+2    MM
*
         @WTO  DRMP2,DRMP2L,00
*
         @WTO  -                       DELIMITER
*
* KOUNTERS AND POINTERS
*
         SR    RN,RN                   CLEAR LU
         ST    RN,DRKLU                  PROCESSED COUNT
         LA    R3,DRLUTAB-80           TABLE ENTRY (PRIMED)
         EJECT
***********************************************************************
* HEAD OF DETAIL READ LOOP
***********************************************************************
         SPACE 1
READLOOP EQU   *
         GET   SYSUT1
         LR    RSYSUT1,R1              COPY RECORD ADDRESS
         USING DRDREC,RSYSUT1          MAP DETAIL RECORD
         CLI   DRDREC,C'*'             COMMENT ?
         BE    READLOOP                -YES
         CLC   DRDRTYP(3),=C'PU '      PU SPECIFICATION
         BE    H100                    -NO
         CLC   DRDRTYP(3),=C'LU '      LU SPECIFICATION
         BNE   READLOOP                 -NO
         SPACE 1
*  L U  PROCESSING
         SPACE 1
         CLC   DRDRES(3),=C'LU4'        NPA LU ?
         BE    READLOOP                 -YES
         TM    DRFLAGS,DRFNODR          *NODR* FLAG ON?
         BO    READLOOP                 -YES
*
         LA    R3,80(R3)                BUMP TABLE POINTER
         LA    RN,1(RN)                 BUMP TABLE
         C     RN,=A(DRMAXLU)           AT MAX?
         BNH   D200                     -NO
         @WTO  DRME10,,44
         ABEND 13
         SPACE 2
D200     EQU   *
         ST    RN,DRKLU                 ENTRY COUNT
         MVC   0(80,R3),DRDREC          MOVE DETAIL RECORD TO TABLE
         B     READLOOP
         TITLE 'VTAM DYNAMIC RECONFIGURATION DECK BUILDER  -  M A I N L&
                I N E  -  (M)-OVE MEMBER : PASS-1'
**********************************************************************
*  (M)-OVE MEMBER PASS-1
**********************************************************************
         SPACE 1
DRHM1    DC    C'* --( '
DRHM1A   DC    C'????????'
         DC    C' - '
DRHM1B   DC    C'......'
         DC    C' - '
DRHM1C   DC    C'HH:MM'
         DC    C' )'
DRHM1L   EQU   *-DRHM1
         SPACE 1
DRHM2    DC    C'* DELETE LOGICAL UNITS FROM '
DRHM2A   DC    C'????????'
         DC    C' AND ADD THEM TO '
DRHM2B   DC    C'????????'
DRHM2L   EQU   *-DRHM2
         SPACE 1
DRHM3    DC    C'         VBUILD TYPE=DR'
DRHM3L   EQU   *-DRHM3
         SPACE 1
DRHM4    DC    C'DELLUS   DELETE FROM='
DRHM4A   DC    CL8'????????'            PUNAME
DRHM4L   EQU   *-DRHM4
         SPACE 2
H000     DS    0H
         LTR   RN,RN                    ANY LU'S FOR LAST PU?
         BZ    W100                     -NO, JUST QUIT
         OI    DRFLAGS,DRFEOF           SET EOF SWITCH
         B     H200
H100     EQU   *
         AP    DRKPUX,=P'1'             BUMP PU IN SOUREC KOUNT
         LTR   RN,RN                    ANY LU'S FOR LAST PU?
         BZ    M100                     -NO
         SPACE 2
* CLOSE SYSUT2
         SPACE 1
H200     EQU   *
         CLOSE SYSUT2
         SPACE 1
         MVC   DRLD,DRPU+3              BREAK OUT LINE.DROP NAME
         MVC   DRMEMBER(2),=C'DR'       MEMNAME PREFIX
         MVC   DRMEMBER+2(5),DRLD       LINE.DROP NAME
         MVI   DRMEMBER+7,C'M'
         MVC   JFCBELNM,DRMEMBER        MOVE TO JFCB
*
* ATTEMPT TO ENQUEUE ON THIS MEMBER
*
*  NOTE ! THIS ONLY PROTECTS AGAINST PEOPLE UPDATING THIS MEMBER
*         VIA SPF.
*
         MVC   DRMINOR+44(8),DRMEMBER   MEMBER NAME TO ENQ MINOR
         ENQ   (DRMAJOR,DRMINOR,E,44+8,SYSTEMS),RET=USE
         CLI   3(R15),0                 GET IT ?
         BE    H250                     -YES
         MVC   DRMP6A,DRMEMBER          MEMBER NAME TO WTO
         @WTO  DRMP6,DRMP6L,10
         AP    DRKECF,=P'1'             BUMP THE ERROR COUNT
         B     M100
*
H250     EQU   *
         OPEN  (SYSUT2,OUTPUT),TYPE=J
         AP    DRKPUP,=P'1'             BUMP PU'S PROCESSED KOUNT
         MVC   DRDRPU,DRPU              BUILD DR PUNAME
         MVC   DRDRPU+3(3),=C'&DRLNO'   LINE NUMBER PORTION OF PU
*
         @PUT  DRCOM,L'DRCOM,EJECT=YES
*
         MVC   DRHM1A,DRMEMBER          DR MEMBER NAME
         MVC   DRHM1B,DRDATE            DATESTAMP
         MVC   DRHM1C,DRTIME            TIMESTAMP
         @PUT  DRHM1,DRHM1L
*
         @PUT  DRCOM,L'DRCOM
*
         MVC   DRHM2A,DRPU              FROM PUNAME
         MVC   DRHM2B,DRDRPU            DR PUNAME
         @PUT  DRHM2,DRHM2L
*
         @PUT  DRCOM,L'DRCOM
*
         @PUT  DRHM3,DRHM3L
*
         @PUT  DRCOM,L'DRCOM
*
         MVC   DRHM4A,DRPU              SOURCE PU
         @PUT  DRHM4,DRHM4L
*
         @PUT  DRCOM,L'DRCOM
*
         SR    RN,RN                    CLEAR
         LA    R3,DRLUTAB-80            TABLE POINTER *(PRIMED)
         SPACE 1
H300     EQU   *
         LA    RN,1(RN)                 BUMP RECORD COUNT
         C     RN,DRKLU                 FINISHED ?
         BH    J100                     -NO
*
         SPACE 2
H400     EQU   *
         LA    R3,80(R3)                BUMP TABLE POINTER
         @PUT  0(R3),15
         B     H300
         TITLE 'VTAM DYNAMIC RECONFIGURATION DECK BUILDER  -  M A I N L&
                I N E  -  (M)-OVE MEMBER : PASS-2'
**********************************************************************
*  (M)-OVE MEMBER PASS-2
**********************************************************************
         SPACE 1
DRJM1    DC    C'ADDPU    ADD   TO='
DRJM1A   DC    C'??????'
DRJM1L   EQU   *-DRJM1
*
DRJM3    EQU   *
DRJM3A   DC    CL8'????????'
         DC    C' PU    ADDR='
DRJM3B   DC    CL2'??'
         DC    C','
DRJM3L   EQU   *-DRJM3
*
DRJM04   DC    C'               ANS=CONTINUE,'
DRJM06   DC    C'               PUTYPE=2,'
DRJM08   DC    C'               MAXOUT=7,'
DRJM10   DC    C'               MAXDATA=265,'
DRJM14   DC    C'               PACING=0,'
DRJM16   DC    C'               PASSLIM=7,'
DRJM18   DC    C'               RETRIES=(,1,4),'
DRJM19   DC    C'               SSCPFM=USSSCS'
*
DRJM20   DC    C'ADDLUS   ADD   TO='
DRJM20A  DC    C'????????'      PUNAME
DRJM20L  EQU   *-DRJM20
*
         SPACE 2
J100     DS    0H
         @PUT  DRCOM,L'DRCOM
*
         MVC   DRJM1A,=C'&DRLNM'        DR LINE NAME
         @PUT  DRJM1,DRJM1L
*
         @PUT  DRCOM,L'DRCOM
*
         MVC   DRJM3A,DRDRPU            DR PUNAME
         MVC   DRJM3B(2),DRPUADR        CONTROLLER ADDRESS
         @PUT  DRJM3,DRJM3L
*
         @PUT  DRJM04,L'DRJM04
         @PUT  DRJM06,L'DRJM06
         @PUT  DRJM08,L'DRJM08
         @PUT  DRJM10,L'DRJM10
         @PUT  DRJM14,L'DRJM14
         @PUT  DRJM16,L'DRJM16
         @PUT  DRJM18,L'DRJM18
         @PUT  DRJM19,L'DRJM19
*
         @PUT  DRCOM,L'DRCOM
*
         MVC   DRJM20A,DRDRPU           DR PUNAME
         @PUT  DRJM20,DRJM20L
*
         SR    RN,RN                    CLEAR
         LA    R3,DRLUTAB-80            TABLE POINTER *(PRIMED)
         SPACE 1
J200     EQU   *
         LA    RN,1(RN)                 BUMP RECORD COUNT
         C     RN,DRKLU                 FINISHED ?
         BH    J900                     -NO
*
         SPACE 2
J400     EQU   *
         @PUT  DRCOM,L'DRCOM
*
         LA    R3,80(R3)                BUMP TABLE POINTER
         TRT   15(70,R3),DRTRTBL        FIND LAST BLANK
         LR    R2,R1                    COPY ADDRESS OF THE BLANK
         BCTR  R2,0                     BACK UP BY 1
         CLI   0(R2),C','               A COMMA ALREADY ?
         BE    J450                     -YES
         MVI   0(R1),C','               AND APPEND A COMMA
J450     EQU   *
         @PUT  0(R3),80
         BAL   R14,LOGMODE
         LTR   R15,R15
         BNZ   J500
         @PUT  DRMG10,L'DRMG10
         B     J200
J500     EQU   *
         @PUT  DRMG20,L'DRMG20
         B     J200
         SPACE 2
*  MEMBER CLOSE PROCESSING
         SPACE 1
J900     EQU   *
         BAL   R14,DR@MCP               CALL MEMBER CLOSE PROCESSING
         B     L100
         TITLE 'VTAM DYNAMIC RECONFIGURATION DECK BUILDER  -  M A I N L&
                I N E  -  (R)-ESTORE MEMBER'
**********************************************************************
*  (R)-ESTORE MEMBER  PASS-1
**********************************************************************
         SPACE 1
DRML4    DC    C'*  DELETE THE DYNAMICALLLY ADDED PU AND LU''S FROM '
DRML4A   DC    C'??????'
DRML4L   EQU   *-DRML4
*
DRML5    DC    C'         VBUILD TYPE=DR'
*
DRML6    DC    C'DELPU   DELETE FROM=&DRLNM'
DRML6L   EQU   *-DRML6
*
DRML7    EQU   *
DRML7A   DC    C'????????'
         DC    C' PU'
DRML7L   EQU   *-DRML7
*
DRML8    DC    C'ADDLUS   ADD   TO='
DRML8A   DC    C'????????'
DRML8L   EQU   *-DRML8
         SPACE 1
L100     DS    0H
         MVI   DRMEMBER+7,C'R'          CHANGE MEMBER NAME
         MVC   JFCBELNM,DRMEMBER        MOVE TO JFCB
*
* ATTEMPT TO ENQUEUE ON THIS MEMBER
*
*  NOTE ! THIS ONLY PROTECTS AGAINST PEOPLE UPDATING THIS MEMBER
*         VIA SPF.
*
         MVC   DRMINOR+44(8),DRMEMBER   MEMBER NAME TO ENQ MINOR
         ENQ   (DRMAJOR,DRMINOR,E,44+8,SYSTEMS),RET=USE
         CLI   3(R15),0                 GET IT ?
         BE    L150                     -YES
         MVC   DRMP6A,DRMEMBER          MEMBER NAME TO WTO
         MVC   DRJLMN(3),=C'100'        MOVE MESSAGE TO WTO
         @WTO  DRMP6,DRMP6L,20
         AP    DRKECF,=P'1'             BUMP THE ERROR COUNT
         B     M100
*
L150     EQU   *
         OPEN  (SYSUT2,OUTPUT),TYPE=J
*
         @PUT  DRCOM,1,EJECT=YES
*
         MVC   DRHM1A,DRMEMBER          DR MEMBER NAME
         MVC   DRHM1B,DRDATE            DATESTAMP
         MVC   DRHM1C,DRTIME            TIMESTAMP
         @PUT  DRHM1,DRHM1L
*
         @PUT  DRCOM,L'DRCOM
*
         @PUT  DRCOM,1
*
         MVC   DRML4A,=C'&DRLNM'        DR LINE NAME
         @PUT  DRML4,DRML4L
*
         @PUT  DRCOM,1
*
         @PUT  DRML5,L'DRML5
*
         @PUT  DRCOM,1
*
         @PUT  DRML6,DRML6L
*
         @PUT  DRCOM,1
*
         MVC   DRML7A,DRDRPU            DR PUNAME
         @PUT  DRML7,DRML7L
*
         @PUT  DRCOM,1
*
         MVC   DRML8A,DRPU              ORIGINAL PUNAME
         @PUT  DRML8,DRML8L
         SPACE 2
         SR    RN,RN                    CLEAR
         LA    R3,DRLUTAB-80            TABLE POINTER *(PRIMED)
L200     EQU   *
         LA    RN,1(RN)                 BUMP RECORD COUNT
         C     RN,DRKLU                 FINISHED ?
         BH    L900                     -NO
*
         SPACE 2
L400     EQU   *
         @PUT  DRCOM,1
*
         LA    R3,80(R3)                BUMP TABLE POINTER
         TRT   15(70,R3),DRTRTBL        FIND LAST BLANK
         LR    R2,R1                    COPY ADDRESS OF THE BLANK
         BCTR  R2,0                     BACK UP BY 1
         CLI   0(R2),C','               A COMMA ALREADY ?
         BE    L450                     -YES
         MVI   0(R1),C','               AND APPEND A COMMA
L450     EQU   *
         @PUT  0(R3),80
         BAL   R14,LOGMODE
         LTR   R15,R15
         BNZ   L500
         @PUT  DRMG10,L'DRMG10
         B     L200
L500     EQU   *
         @PUT  DRMG20,L'DRMG20
         B     L200
         SPACE 2
L900     EQU   *
         BAL   R14,DR@MCP               MEMBER CLOSE PROCESSING
         TITLE 'VTAM DYNAMIC RECONFIGURATION DECK BUILDER  -  M A I N L&
                I N E  -  PU RE-INITIALIZATION'
**********************************************************************
*  CLEAN UP FOR THIS PU AND INITIALIZE FOR NEXT ONE
**********************************************************************
         SPACE 1
M100     EQU   *
         SR    RN,RN                    CLEAR
         ST    RN,DRKLU                   RECORD COUNT
         LA    R3,DRLUTAB-80            TABLE POINTER *(PRIMED)
*
         NI    DRFLAGS,255-DRFNODR      ASSUME THAT WE WILL BUILD DR
         CLC   DRDCOM(6),=C'*NODR*'     REQUEST NOT TO?
         BNE   M200                     -NO
         OI    DRFLAGS,DRFNODR          SET *NODR* FLAG
         AP    DRKNOP,=P'1'             BUMP *NODR* KOUNT
         MVC   DRMP4A,DRDRES            PUNAME TO MESSAGE
         @WTO  DRMP4,DRMP4L,30
         SPACE 1
M200     EQU   *
         MVC   DRPU,DRDRES              SAV PU NAME
         TM    DRFLAGS,DRFEOF           AT EOF
         BNO   READLOOP                 -NO
         TITLE 'VTAM DYNAMIC RECONFIGURATION DECK BUILDER  -  M A I N L&
                I N E  -  TERMINATION'
***********************************************************************
*  WARM FUZZIES
***********************************************************************
         SPACE 1
W100     EQU   *
         @WTO  -                        DELIMITER
*
         @EDIT DRMT2A,DRKPUX            PU'S IN THE SOURCE DECK
         @WTO  DRMT2,DRMT2L,92
*
         @EDIT DRMT4A,DRKNOP            PU'S REQUESTING *NODR*
         @WTO  DRMT4,DRMT4L,92
*
         @EDIT DRMT6A,DRKPUP            PU'S RESULTING IN DRMEMBERS
         @WTO  DRMT6,DRMT6L,92
*
         @EDIT DRMT8A,DRKECF            ISPF ENQUEUE CONFLICTS
         @WTO  DRMT8,DRMT8L,92
*
         @WTO  -                       DELIMITER
         EJECT
***********************************************************************
*  CLOSE ANY DATASETS THAT WE HAVE DISTURBED
***********************************************************************
         CLOSE (SYSPRINT,,SYSUT1,,SYSUT2)
         SPACE 2
***********************************************************************
*  RETURN TO CALLER
***********************************************************************
         SPACE 1
         SR    R15,R15                 SET RETURN CODE
         L     R13,4(R13)
         RETURN (14,12),RC=(15)
         SPACE 2
***********************************************************************
*  EXIT VIA ABEND / DUMP
***********************************************************************
         SPACE 1
DRZABEND EQU   *
         S     R14,=A(DRDROID)          OFFSET
         ABEND 77,DUMP
         TITLE 'VTAM DYNAMIC RECONFIGURATION DECK BUILDER  -  CONSTANTS&
                AND WORK AREAS'
***********************************************************************
*  CONSTANTS AND NON-REENTRANT ETC.
***********************************************************************
         SPACE 1
DRWORK   DC    D'0'                    WORK AREA
DRRSA    DC    16F'0'                  COMMON REG SAVE AREA
DRABRSA  DC    16F'0'                  ABEND REG SAVE AREA
DRPDATE  DC    PL4'0'                  00YYDDDF PACKED DATE
DRPTIME  DC    PL4'0'                  000HHMMF PACKED TIME
DREXLST  DC    X'87',AL3(DRJFCB)       DCB EXIT LIST
*
* -- KOUNTERS
*
DRKLU    DC    F'0'                    NUMBER OF DRLUNUM ENTRIES
*
DRKPUP   DC    PL4'0'                  PU'S PROCESSED
DRKECF   DC    PL4'0'                  ISPF ENQUEUE CONFLICTS
DRKNOP   DC    PL4'0'                  *NODR* SPECIFICATIONS
DRKPUX   DC    PL4'0'                  PU STMNTS IN SOURCE DECK
*
DRKHLO   DC    H'0'                    #LINES OUTPUT (PER MEMBER)
*
DRMASK1  DC    X'4020202020202120'     EDIT MASK
DRBLANK  DC    C' '                    BLANK
DRCOM    DC    C'*'                    COMMENT CARD
DRLD     DC    CL5'.....'              PREVIOUS LINE / DROP
DRDATE   DC    CL6'YY.JJJ'             YY.JJJ
DRTIME   DC    CL6'HH:MM'               HH:MM
*
DRMAJOR  DC    CL8'SPFEDIT'
DRMINOR  DC    CL44'SYSUT2 DSNAME?',CL8'NCPNAME?'
*
DRPU     DC    CL8'????????'           PREVIOUS PU
         ORG   DRPU+6
DRPUADR  DC    CL2'??'                 PU ADDRESS
DRDRPU   DC    CL8'????????'           DR PUNAME
*
DRMEMBER DC    CL8' '                  DR MEMBER NAME
*
DRLM     DC    CL8'?'                  LOGMODE
*
* -- FLAGS
*
DRFLAGS  DC    X'00'                   STATUS FLAGS
DRFEOF   EQU   X'80'                   .EOF ON SYSUT1
DRFNODR  EQU   X'40'                   .*NODR* FLAG
*
DROUTOPT DC    AL1(OUTOPT)              OUTPUT OPTIONS
*
* -- TRANSLATE TABLES
*
DRTRTBL  DC    256X'00'                 LOCATE BLANKS
         ORG   DRTRTBL+C' '
         DC    C' '
         ORG
         EJECT
***********************************************************************
* M E S S A G E S
***********************************************************************
*
* -- ERROR MESSAGES
*
DRMA10   DC    CL70'ERROR ENCOUNTERED: R14 IS DETECTION ADDRESS - R0-R1&
               5 STORED AT DRRSA'
DRME10   DC    C'LU PER PU COUNT EXCEEDED'
*
* -- COMMON MEMBER GENERATION TEXT
*
* INTERACTIVE DEVICE
*
DRMG10   DC    C'               MODETAB=MODEREM1,USSTAB=USSSNA,SSCPFM=U&
               SSSCS'
*
* BATCH DEVICE
*
DRMG20   DC    C'               MODETAB=MODEREM1,USSTAB=USSSNA,SSCPFM=U&
               SSSCS'
*
* -- PROCESSING MESSAGES
*
DRMP2    DC    C'VERSION &SYSDATE-&SYSTIME BEGINNING'
DRMP2L   EQU   *-DRMP2
*
DRMP4    EQU   *
DRMP4A   DC    C'XXXXXXXX'
         DC    C' : REQUESTS *NODR*'
DRMP4L   EQU   *-DRMP4
*
DRMP6    EQU   *
DRMP6A   DC    C'XXXXXXXX'
         DC    C' : IN USE - PROCESSING SKIPPED'
DRMP6L   EQU   *-DRMP6
*
* -- MESSAGE BUFFERS
*
DRSYSM   DS    0CL80
         DC    C' '
DRSYSM1  DS    CL80
         ORG
*
* -- TERMINATION MESSAGES
*
DRMT2    EQU   *
DRMT2A   DC    C'???'
         DC    C' PU''S IN SOURCE DECK'
DRMT2L   EQU   *-DRMT2
*
DRMT4    EQU   *
DRMT4A   DC    C'???'
         DC    C' PU''S REQUESTED *NODR*'
DRMT4L   EQU   *-DRMT4
*
DRMT6    EQU   *
DRMT6A   DC    C'???'
         DC    C' PU''S HAD DR MEMBERS GENERATED FOR THEM'
DRMT6L   EQU   *-DRMT6
*
DRMT8    EQU   *
DRMT8A   DC    C'???'
         DC    C' PU''S SKIPPED DUE TO ISPF ENQUEUE CONFLICTS'
DRMT8L   EQU   *-DRMT8
         SPACE 2
***********************************************************************
* L I T E R A L S
***********************************************************************
         SPACE 1
         LTORG
         EJECT
DRJFCB   DS    0A                       @JFCB
         IEFJFCBN LIST=NO
         TITLE 'VTAM DYNAMIC RECONFIGURATION DECK BUILDER - LOCAL SUBRO&
               UTINE - L O G M O D E'
***********************************************************************
*
* LOGMODE
*
* THIS ROUTINE LOCATES THE 'DLOGMOD=XXXXXXXX' PORTION OF THE LU
* STATEMENT.
*
* IF THERE IS NONE THEN DEFAULT LOGMODE IS APPENDED TO THE END OF
* THE STATEMENT.
*
* ALSO THE RETURN CODE IS SET TO INDICATE WHETHER THE LOGMODE DENOTES
* AND INTERACTIVE OR BATCH DEVICE.
*
* INPUT ARGUMENTS ------------------------------
*
*     R3: @LU TYPE RECORD
*    R14: RETURN ADDRESS
*
*
* OUTPUT ARGUMENTS -----------------------------
*
*   DRLM: CONTAINS THE LOGMODE
*
*    R15: RETURN CODE:
*
*            0=INTERACTIVE DEVICE
*            4=BATCH DEVICE
*
***********************************************************************
         SPACE 1
LOGMODE  DS    0H
         STM   R0,R14,DRRSA             SAVE REGS
         LR    R4,R3                    COPY @RECORD
         LA    R14,71-8-4(R4)           ENDING SCAN POSITION
         LA    R4,14(R4)                BEGINNING SCAN POSITION
LOG200   EQU   *
         LA    R4,1(R4)                 BUMP TEXT POINTER
         CLC   0(8,R4),=C'DLOGMOD='     HIT ?
         BE    LOG400                   -YES
         CR    R4,R14                   BEYOND END?
         BL    LOG200                   -NO, TRY AGAIN
         SPACE 1
         TRT   15(71-16,R3),DRTRTBL     FIND THE 1ST BLANK
         BC    B'0100',LOG300           -YES AND NOT IN THE LAST POS
         B     LOG600                   RETURN
         SPACE 1
LOG300   EQU   *
         LR    R2,R1                    COPY ADDRESS OF THE BLANK
         BCTR  R2,0                     AND BACK UP BY ONE
         CLI   0(R2),C','               A COMMA THERE ?
         BE    LOG350                   -YES
         MVI   0(R1),C','               -NO SO PUT ONE
         LA    R1,1(R1)                 BUMP POINTER
LOG350   EQU   *
         MVC   0(8,R1),=C'DLOGMOD='     APPEND THE DEFAULT
         MVC   8(8,R1),=CL8'&DLOGMOD'     LOGMODE SPECIFICATION
         MVC   DRLM,=CL8'&DLOGMOD'      FILL IN RETURN PARM LIST
         B     LOG600                   RETURN
         SPACE 1
*  ARGUMENT FOUND - NOW EXTRACT DLOGMOD VALUE AND RETURN
         SPACE 1
LOG400   EQU   *
         LA    R4,8(R4)                 POINT PAST 'DLOGMOD='
         TRT   1(9,R4),LOGDEL           FIND THE DELIMITER
         BC    B'1000',LOG600           -NO
         MVC   DRLM,=CL8' '             CLEAR
         SR    R1,R4                    LENGTH
         BCTR  R1,0                     REDUCED FOR MVC
         EX    R1,LOGMOV                MOVE TO RETURN ARG
         SPACE 2
LOG600   EQU   *
         SR    R15,R15                  RC=0 => INTERACTIVE DEVICE
         CLI   DRLM,C'T'                LOGMODE START WITH 'T' ?
         BE    LOGRET                   -YES
         LA    R15,4                    ASSUME BATCH DEVICE
         SPACE 1
* RETURN
         SPACE 1
LOGRET   EQU   *
         LM    R0,R14,DRRSA             RESTORE REGS
         BR    R14                      RETURN TO CALLER
         SPACE 2
LOGMOV   MVC   DRLM(*-*),0(R4)          MOVE ARGUMENT VALUE
*
LOGDEL   DC    256X'00'                 FIND DELIMITER TABLE
         ORG   LOGDEL+C','
         DC    C','
         ORG   LOGDEL+C' '
         DC    C' '
         ORG
         SPACE 1
         LTORG
         TITLE 'VTAM DYNAMIC RECONFIGURATION DECK BUILDER - LOCAL SUBRO&
               UTINE - D R @ P U T'
***********************************************************************
*
* DR@PUT
*
* PRINT A LINE WITH SOME MINOR EDITING
*
* IF THE LINE IS NOT A COMMENT STAETMENT AND THE LAST NON BLANK CHAR
* IS A ',' THEN PUT A CONTINUATION CHARACTER IN COLUMN 72.
*
* INPUT ARGUMENTS ------------------------------
*
*     R0: LENGTH OF MESSAGE TO PRINT
*     R1: ADDRESS OF MESSAGE TO PRINT
*    R14: RETURN ADDRESS
*    R15: DCB ADDRESS OR 0 FOR BOTH SYSPRINT AND SYSUT2
*
* OUTPUT ARGUMENTS: NONE
*
***********************************************************************
         SPACE 1
DR@PUT   DS    0H
         STM   R14,R3,DRRSA             SAVE REGS
         MVI   DRSYSM1,C' '             CLEAR
         MVC   DRSYSM1+1(L'DRSYSM1-1),DRSYSM1 OUTPUT BUFFER
*
         LR    R2,R0                    COPY LENGTH
         BCTR  R2,0                     REDUCE FOR MOVE
         EX    R2,DRPMOVE               MOVE TO PRINT BUFFER
*
         CLI   DRSYSM1,C'*'             A COMMENT ?
         BE    DRP600                   -NO
*
         TRT   DRSYSM1+16(72-16),DRTRTBL LOCATE THE NEXT BLANK
         BCTR  R1,0                     BACK UP THE POINTER BY 1
         CLI   0(R1),C','               CONTINUATION
         BNE   DRP600                   -NO
         MVI   DRSYSM1+71,C'&&'         -YES, FLAG COLUMN 72
         SPACE 1
* -- OUTPUT DESTINATION : SYSOUT
         SPACE 1
DRP600   EQU   *
         TM    DROUTOPT,OUTPRT          PRINT ?
         BNO   DRP700                   -NO
         PUT   SYSPRINT,DRSYSM
DRP700   EQU   *
         SPACE 1
* -- OUTPUT DESTINATION : DISK
         SPACE 1
         TM    DROUTOPT,OUTDISK         DISK  ?
         BNO   DRP800
         PUT   SYSUT2,DRSYSM1
         SPACE 1
DRP800   EQU   *
         LH    R15,DRKHLO               BUMP
         LA    R15,1(R15)                 LINE OUTPUT
         STH   R15,DRKHLO                 COUNTER
*
         LM    R14,R3,DRRSA             RESTORE REGS
         BR    R14                      RETURN TO CALLER
         SPACE 1
DRPMOVE  MVC   DRSYSM1(*-*),0(R1)       MOVE TEXT TO PRINT BUFFER
         SPACE 1
         LTORG
         TITLE 'VTAM DYNAMIC RECONFIGURATION DECK BUILDER - LOCAL SUBRO&
               UTINE - D R @ W T O'
***********************************************************************
*
* DR@WTO
*
* BUILD WTO AND ISSUE
*
* INPUT ARGUMENTS ------------------------------
*
*     R0: LENGTH OF MESSAGE TO PRINT
*
*     R1: ADDRESS OF MESSAGE TO PRINT
*           R1=0 => GENERATE DELIMITER
*
*     R2: ADDRESS OF MESSAGE NUMBER
*    R14: RETURN ADDRESS
*
* OUTPUT ARGUMENTS: NONE
*
***********************************************************************
         SPACE 1
DR@WTO   DS    0H
         STM   R14,R3,DRRSA             SAVE REGS
*  CLEAR WTO BUFFER AREA
         MVI   DRJLMN,C' '              MESSAGE NUMBER
         MVC   DRJLMN+1(L'DRJLMN-1),DRJLMN
*
         MVI   DRJLMT,C' '              MESSAGE TEXT
         MVC   DRJLMT+1(L'DRJLMT-1),DRJLMT
         LTR   R1,R1                    REAL MESSAGE ?
         BNZ   DRW400                   -YES
         SPACE 1
*  BUILD DELIMITER
         SPACE 1
         MVC   DRJLMN,=C'---'
         MVC   DRJLMD,=C'-:-'
         MVI   DRJLMT,C'-'              SET TEXT
         MVC   DRJLMT+1(L'DRJLMT-1),DRJLMT  TO DASHES
         B     DRW800
         SPACE 2
DRW400   EQU   *
         MVC   DRJLMN,0(R2)             MESSAGE NUMBER
         MVC   DRJLMD,=C' : '
*
         LR    R15,R0                   COPY LENGTH
         BCTR  R15,0                    REDUCE FOR MOVE
         EX    R15,DRWMOVE              MOVE TO PRINT BUFFER
*
DRW800   WTO   MF=(E,DRWTO)
*
         LM    R14,R3,DRRSA             RESTORE REGS
         BR    R14                      RETURN TO CALLER
         SPACE 1
DRWMOVE  MVC   DRJLMT(*-*),0(R1)        MOVE TEXT TO WTO BUFFER
         SPACE 1
DRWTO    WTO   '                                                       &
                                         ',ROUTCDE=11,MF=L
         ORG   DRWTO+4
DRJLMH   DC    C'DROID'                 JOB LOG MESSAGE HEADER
DRJLMN   DC    C'12'                    JOB LOG MESSAGE NUMBER
DRJLMD   DC    C' : '                   JOB LOG MESSAGE DELIMITER
DRJLMT   DC    CL50'X'                  JOB LOG MESSAGE TEXT
         ORG
         LTORG
         TITLE 'VTAM DYNAMIC RECONFIGURATION DECK BUILDER - LOCAL SUBRO&
               UTINE - D R @ M C P'
***********************************************************************
*
* DR@MCP
*
* MEMBER CLOSE PROCESSING
*
* DESCRIPTION:
*
* .. CLOSE SYSUT2 FOR QSAM
*
* .. UPDATE SPF TYPE DIRECTORY INFORMATION
*
* .. FREE SPF EDIT ENQUEUE
*
*
* INPUT ARGUMENTS ------------------------------
*
*    R14: RETURN ADDRESS
*
* OUTPUT ARGUMENTS: NONE
*
***********************************************************************
         SPACE 1
DR@MCP   DS    0H
         STM   R14,R3,DRRSA             SAVE REGS
*
* -- CLOSE DATASET FOR QSAM
*
         CLOSE (SYSUT2)
*
* -- OPEN FOR BPAM
*
         OPEN  (SYSUT2A,OUTPUT)
*
* -- UPDATE TTR IN DCB VIA FIND
*
         FIND  SYSUT2A,DRMEMBER,D
*
* -- UPDATE SPF-TYPE DIRECTORY INFO
*
         MVC   DRSTMEM,DRMEMBER         MEMBER NAME
         MVC   DRSTCRD,DRPDATE          CREATE DATE = TODAY'S DATE
         MVC   DRSTCHD,DRPDATE          CHANGE DATE = TODAY'S DATE
         TIME
         STCM  R0,B'1100',DRSTIME       TIME
         LH    R2,DRKHLO                LINES OUTPUT COUNTER
         STH   R2,DRSTCLK               AS #CHANGED LINES
         STH   R2,DRSRILK               AS #INITIAL LINES
         STH   R2,DRSTMLK               AS #MODIFIED LINES
         SPACE 1
         STOW  SYSUT2A,DRSTOW,R
         @CHECK
*
         SR    R0,R0                    RESET
         STH   R0,DRKHLO                  LINE COUNT
*
* -- CLOSE DATASET AS BPAM
*
         CLOSE (SYSUT2A)
*
* -- RELEASE SPFEDIT ENQUEUE
*
         DEQ   (DRMAJOR,DRMINOR,44+8,SYSTEMS)
         @CHECK
*
* -- RETURN TO CALLER
*
         LM    R14,R3,DRRSA             RESTORE REGS
         BR    R14                      RETURN TO CALLER
         SPACE 2
*  CONSTANTS AND NON-REENTRANT ETC.
         SPACE 1
*  STOW PARAMETER LIST
         SPACE 1
DRSTOW   DS    0F
DRSTMEM  DC    CL8'DR??????'            MEMBER NAME
         DC    X'000000'                TTR
         DC    X'0F'
DRSTDIR  EQU   *
         DC    AL1(1)                   VERSION
         DC    AL1(1)                   REVISION
         DC    AL2(0)                   .RESERVED
DRSTCRD  DC    XL4'00'                  CREATE DATE
DRSTCHD  DC    XL4'00'                  CHANGE DATE
DRSTIME  DC    XL2'1812'                CHANGE TIME
DRSTCLK  DC    AL2(0)                   #CURRENT LINE KOUNT
DRSRILK  DC    AL2(0)                   #INITAL LINES
DRSTMLK  DC    AL2(0)                   #MODIFIED LINES
         DC    CL10'DRDROID'            LAST PERSON TO UPDATE
         SPACE 2
         LTORG
         EJECT
***********************************************************************
* DATASET CONTROL BLOCKS
***********************************************************************
         PUSH  PRINT
         PRINT NOGEN
SYSPRINT DCB   DDNAME=SYSPRINT,                                        X
               DSORG=PS,MACRF=PM,                                      X
               RECFM=FA,BLKSIZE=80,LRECL=80
         SPACE 2
SYSUT1   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=GL,EODAD=H000
         SPACE 2
SYSUT2   DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=PM
         SPACE 2
SYSUT2A  DCB   DDNAME=SYSUT2,DSORG=PO,MACRF=W
         SPACE 2
         POP   PRINT
DRLUTAB  EQU   *
         DS    (128*80)C
         END   DRDROID

//*LKED.DRDROID DD DSN=ASYSJEE.DRDROID,
//*       UNIT=SYSDA,SPACE=(TRK,(2,,2)),
//*       DISP=(MOD,DELETE)
//LKED.SYSLIB  DD DSN=ASYSJEE.LOADLIB,DISP=SHR
//LKED.SYSLMOD DD DSN=ASYSJEE.LOADLIB,DISP=SHR
//LKED.SYSIN DD *
 NAME DRDROID(R)
/*
//*
//*
//DRDROID  EXEC PGM=DRDROID,COND=(0,LT)       ASM.LKED)
//STEPLIB  DD DSN=ASYSJEE.LOADLIB,DISP=SHR
//SYSPRINT DD SYSOUT=*
//SYSUDUMP DD SYSOUT=*
//SYSUT1   DD DISP=SHR,DSN=SYS4.VTAMLST(N00001)
//SYSUT2   DD DSN=ASYSJEE.DRDROID,DISP=SHR
//*           UNIT=SYSDA,VOL=SER=APLTSA,
//*           DCB=SYS1.MACLIB,
//*           SPACE=(CYL,(1,,2)),
//*           DISP=(,CATLG)
