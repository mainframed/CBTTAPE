AAHLXXX  TITLE 'WTO EXIT FOR GTF MESSAGE PROCESSING'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*  MODULE NAME  . . . . AAHLXXX
*
*  DATE WRITTEN . . . . 04/85
*
*  AUTHOR . . . . . . . JIM ELSWORTH
*
*  ENVIRONMENT  . . . . MVS/XA WTO EXIT
*
*  FUNCTION
*    RECEIVES CONTROL FOR VARIOUS GTF MESSAGES ( AHLXXX ).
*    MAY RESPOND TO WTOR'S OR TAKE OTHER ACTIONS.
*
*
*  DESCRIPTION:
*    1) GETMAIN A SAVE AND WORK AREA
*    2) BRANCH DEPENDING ON MESSAGE ID
*
*    AHL125I:
*      FOR GTFVTAMA, GTFVTAMB, GTF2TAPE, GTFTAPEA AND GTFTAPEB
*      RESPOND U TO THE STARTUP PROMPT.
*
*    3) FREEMAIN STORAGE
*    4) RETURN
*
*
*  REGISTERS AT ENTRY:
*     R1 : @CTXT COMMTAS USER EXIT PARAMETER LIST
*    R13 : @STANDARD SAVE AREA
*    R14 : RETURN ADDRESS
*    R15 : ENTRY POINT
*
*
*  REGISTERS AT EXIT: ALL REGISTERS RESTORED
*
*
*
*
*
*
*
*
*
*
*
*
*
*
*            ( CONTINUED ... )
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*  REGISTERS USED:
*
R0       EQU   0                       WORK REGISTER
R1       EQU   1                       INPUT PARMS AND WORK REGISTER
R2       EQU   2                       MESSAGE ATTRIBUTES BAR
R4       EQU   4                       MESSAGE TEXT BAR
CTXTPTR  EQU   5                       CTXT BAR
R7       EQU   7                       WORK REGISTER
R11      EQU   11                      DYNAMIC STORAGE BAR
R12      EQU   12                      BASE REGISTER
R13      EQU   13                      SAVE AREA POINTER
R14      EQU   14                      RETURN ADDRESS
R15      EQU   15                      ENTRY ADDRESS
*
*
*  CALLED MODULES: NONE
*
*  ASSUMPTIONS: MUST BE RUN UNDER AN MVS/XA SYSTEM
*
*
*  ATTRIBUTES:
*    REENTRANT
*    AMODE=31
*    RMODE=ANY
*
*  ABENDS: NONE
*
*
*  MESSAGES (WTO'S):
*    AAHLXXX - NO MESSAGE MATCH FOUND
*      ISSUED IF MODULE INVOKED FOR OTHER THAN ITS INTENDED PURPOSES
*
*    AAHLXXX - NO TCT FURTHER PROCESSING ABORTED
*       ISSUED IF TCBTCT ADDRESS IS NULL.
*
*
*  CAVEATS:
*    DO NOT ISSUE ANY WAITS IN THIS ROUTINE
*    BE SURE TO LINK AS REENTRANT
*
*
*
*
*
*
*
*
*
*
*            ( CONTINUED ... )
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*
*  IMPLEMENTATION NOTES:
*    AFTER ASSEMBLY AND LINK LLA MUST BE RESTARTED
*
*    IN SYS1.PARMLIB(MPFLST??) MUST CONTAIN AT LEAST ONE OF THE
*    FOLLOWING STATEMENTS:
*
*      AHL????,SUP(NO),USEREXIT(AAHLXXX)
*
*      WHERE AHL???? IS THE MESSAGE ID TO CAUSE OUR INVOCATION
*
*      TO ACTIVATE THE MPFLIST; T MPF=??  WHERE ?? IS THE MPFLST SUFFIX
*
*
*
*
*
*
*
*
*
*
*
*
*
*
*
*
*
*
*
*
*
*
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
         IEZVX100
         EJECT
         IKJTCB                        TIMING CONTROL TABLE
         EJECT
         IEFTCT                        TIMING CONTROL TABLE
         EJECT
         IEFJMR                        JOB MANAGEMENT RECORD
         EJECT
IHAPSA   DSECT
         IHAPSA
         EJECT
***********************************************************************
*  MAP SAVE AND WORK AREAS THAT ARE ACQUIRED
***********************************************************************
         SPACE 1
DYNAMIC  DSECT
SAVE1    DS   18F                      SAVE AREA
         SPACE 1
COMHDR   DS    AL2(COMLEN),AL2(0)      COMMAND HEADER                   1770.
COMREP   DS    C'??'                   COMMAND TEXT (REPLY NUMBER)
COMRES   DS    C'U'                    COMMAND TEXT (REPLY)
COMLEN   EQU   *-COMHDR                COMMAND BLOCK LENGTH             1810.
DYNAMICL EQU  *-DYNAMIC                LENGTH OF DYNAMIC AREA
         SPACE 5
MSGTEXT  DSECT
MSGID    DS    CL8                     MESSAGE ID
         EJECT
AAHLXXX  CSECT
AAHLXXX  AMODE 31                      31 BIT ADDRESSING MODE
AAHLXXX  RMODE ANY                     CAN RUN ABOVE OR BELOW
         SPACE 2
***********************************************************************
*  ESTABLISH ADDRESSABILITY, AND CHAIN SAVE AREAS
***********************************************************************
         SPACE 1
         USING *,R15                   EST. ADDRESSABILITY
         B     PROLOG                  BYPASS PROLOGUE
         DC    AL1(L'ID)               PROLOGUE LENGTH
ID       DC    C'&SYSDATE-&SYSDATE'    MODULE NAME AND COMPILE DATE
         DS    0H                      ALLIGNMENT
PROLOG   SAVE  (14,12)                 SAVE CALLERS REGS
         LR    R12,R15                 SAVE ENTRY ADRESS
         DROP  R15                     LOSE OLD BASE
         USING AAHLXXX,R12             REAL BASE ADDRESS=R12
         L     CTXTPTR,0(R1)           @CTXT
         USING CTXT,CTXTPTR            MAP CTXT
         SPACE 2
***********************************************************************
*
*  GETMAIN A SAVEAREA AND WORK AREA
*
* NOTE! STORAGE IS ACQUIRED BELOW THE LINE DUE TO SVC34 REQUIREMENTS
*
***********************************************************************
         SPACE 1
GTEMAIN  GETMAIN RU,LV=DYNAMICL,SP=230,LOC=BELOW,RELATED=(FREEMAIN)
         LR    R11,R1                  @ACQUIRED STORAGE
         USING DYNAMIC,R11             MAP DYNAMIC AREA
         ST    R13,SAVE1+4             SETUP
         LA    R15,SAVE1                 @OUR SAVE AREA
         ST    R15,8(R13)                SET FORWARD POINTER
         LR    R13,R15                   @OUR SAVE AREA
         EJECT
***********************************************************************
*  DETERMINE WHICH MESSAGE TO PROCESS
***********************************************************************
         SPACE 1
         L     R2,CTXTTXPJ             @MESSAGE ATTRIBUTESS
         USING CTXTATTR,R2             MAP MESSAGE ATTRIBUTES
         LA    R4,CTXTTMSG             @BASE TEXT
         USING MSGTEXT,R4              MAP BASE TEXT
         CLC   MSGID,=CL8'AHL125A'     MESSAGE AHL125A ?
         BE    AHL125A                 -YES
         SPACE 3
***********************************************************************
*  PUT TESTS FOR NEW MESSAGE TYPES HERE
***********************************************************************
         SPACE 1
         WTO   'AAHLXXX - NO MESSAGE MATCH FOUND'
         B     RETURN
         EJECT
***********************************************************************
*
*           1         2         3         4
*  1234567890123456789012345678901234567890123
*
*  AHL125A  RESPECIFY TRACE OPTIONS OR REPLY U
*
***********************************************************************
         SPACE 1
AHL125A  EQU   *
         MVC   COMHDR(COMLEN),CCOMHDR  MOVE CMD DATA TO DYNAMIC AREA
         SPACE 1
*  DETERMINE WHICH FLAVOR OF GTF ISSUED THIS COMMAND
         SPACE 1
         L     R7,PSATOLD-PSA          @OUR TCB
         ICM   R7,15,TCBTCT-TCB(R7)    @OUR TCT
         BNZ   A21510
         WTO   'AAHLXXX - NO TCT - FURTHER PROCESSING ABORTED'
         B     RETURN
         SPACE 2
A21510   EQU   *
         L     R7,TCTJMR-SMFTCT(R7)    @JMR
         USING JMR,R7
         CLC   =C'GTFVTAM',JMRJOB      VTAM TRACE?
         BE    A21590                  -YES
         CLC   =C'GTF2TAP',JMRJOB      VTAM TRACE?
         BE    A21590                  -YES
         CLC   =C'GTFTAPE',JMRJOB      VTAM TRACE?
         BE    A21590                  -YES
         B     RETURN                  -NO
         SPACE 1
*  RESPOND TO THE WTOR
         SPACE 1
A21590   EQU   *
         MVC   COMREP,CTXTRPID         MOVE IN REPLY NUMBER
         SR    R0,R0                   NO UCMID ADDRESS                 1380.
         MGCR  COMHDR                  SEND COMMAND VIA SVC34
         EJECT
***********************************************************************
*  RETURN
***********************************************************************
         SPACE 1
RETURN   EQU   *
         L     R13,4(R13)              @CALLERS SAVE AREA
         SPACE 2
*  FREE THE SAVE AREA AND WORK AREA THAT WAS ACQUIRED EARLIER
         SPACE 1
         FREEMAIN RU,LV=DYNAMICL,A=(R11),SP=230,RELATED=(GETMAIN)
         RETURN (14,12)                RESTORE REGS AND RETURN
         SPACE 5
***********************************************************************
*  CONSTANTS AND RENTRANT MISCELLANEOUS
***********************************************************************
         SPACE 1
CCOMHDR  DC    AL2(COMLEN),AL2(0)      COMMAND HEADER                   1770.
CCOMREP  DC    C'??'                   COMMAND TEXT (REPLY NUMBER)
CCOMRES  DC    C'U'                    COMMAND TEXT (REPLY)
         SPACE 2                                                        1820.
         LTORG
         END
