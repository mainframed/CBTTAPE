NUL8     TITLE 'NUL8 - CHANGE PSCBUSER AND PSCBUSRL TO USERID YOU WANT'
         SPACE 1
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*  DOCUMENTATION:   THIS PROGRAM IS MEANT TO BE RUN AS A TSO          *
*                   COMMAND.   MUST BE RUN AUTHORIZED.                *
*                                                                     *
*         SYNTAX:   NUL8     USERID                                   *
*                                                                     *
*       FUNCTION:   CHANGE USERID IN PSCB AND LWA                     *
*                     AND CHANGE THE UPT PREFIX AS WELL.              *
*                                                                     *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
         YREGS
SP000    EQU   0
         SPACE 1
NUL8     CSECT
         USING NUL8,R12
         B     BEGINN-*(,R15)
         DC    AL1(16),CL16'NUL8 VER 1.1'
         DC    CL16' &SYSDATE &SYSTIME '
         DC    CL16' Public Domain '
BEGINN   STM   R14,R12,12(R13)
         LR    R12,R15
         LR    R11,R1              PRESERVE CPPL POINTER.
         GETMAIN RU,LV=DATALEN,SP=SP000,LOC=BELOW
         ST    R13,4(,R1)
         ST    R1,8(,R13)
         LR    R13,R1
         USING DATAAREA,R13
         SPACE 1
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*              TEST FOR 8-CHARACTER USERID SUPPORT                  *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
         XC    FLAG1,FLAG1         ZERO FLAG BYTE
         L     R6,16               CVT
         L     R6,X'9C'(,R6)       TSVT
         ST    R6,TSVTADDR         STORE TSVT ADDRESS
         TM    6(R6),X'08'         IS THIS FIELD 08?
         BO    TEST8CHR            FLIP FLAG BIT
         CLI   6(R6),X'00'         IS IT 00, THE OLD WAY?
         BE    TEST8END            YES. DON'T FLIP ANYTHING
TEST7CHR DS    0H                  NOT 00 AND NOT 08.
         OI    FLAG1,X'01'         SUPPORT ON BUT 7-CHAR UID LIMIT
         B     TEST8END            THEN GET OUT
TEST8CHR DS    0H                  8-CHAR SUPPORT IS ON
         OI    FLAG1,X'02'         SUPPORT ON AND 8-CHAR UID LIMIT
TEST8END DS    0H                  END OF ROUTINE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
         L     R1,16               POINT TO CVT.
         L     R1,0(,R1)           POINT TO TCB/ASCB WORDS
         L     R10,12(,R1)         POINT TO ASCB.
         L     R9,4(,R1)           POINT TO TCB.
         L     R1,X'B4'(,R9)       POINT TO JSCB.
         L     R2,X'108'(,R1)      POINT TO PSCB.
         LA    R2,0(,R2)           CLEAR HIGH BYTE.
         LTR   R2,R2               ANY PSCB?
         BZ    EOJ8                GET OUT.  NOT TSO.
         USING PSCB,R2
         TM    PSCBATR1,PSCBACCT   ACCT USER?
         BO    CONT0               YES.  CONTINUE.
         TM    PSCBATR1,PSCBCTRL   OPER USER?
         BZ    EOJ4                NO.  GET OUT.
CONT0    LTR   R11,R11             ANY CPPL?
         BZ    EOJ8                NO.  GET OUT
         L     R1,0(,R11)          PT TO CMD BUFFER.
         LTR   R1,R1               ANY CMD BUFFER?
         BZ    EOJ4                GET OUT IF NONE
         LH    R3,0(,R1)           LOAD LENGTH.
         CH    R3,=H'4'            ANYTHING?
         BNH   EOJ4                NO.  GET OUT.
         LR    R5,R3               LENGTH OF COMMAND + 4
         ICM   R5,14,FULLZERO      CLEAR HI ORDER 3 BYTES
         ST    R1,CMBUFAD          STORE COMMAND BUFFER ADDRESS
         L     R1,CMBUFAD          RELOAD COMMAND BUFFER ADDRESS
         LH    R3,2(,R1)           LOAD OFFSET.
         SR    R5,R3               SUBTRACT OFFSET FROM FULL LENGTH+4
         SH    R5,=H'4'            CORRECT FOR LENGTH OF CMDBUFR HEADER
         BC    13,EOJ8             LESS THAN ZERO OR OVERFLOW, OUT.
         AR    R1,R3               POINT ...
         LA    R1,4(,R1)           ... TO CHAR.
         LA    R6,8                LOAD REG FOR BCT LOOP
         LR    R7,R1               INITIALIZE POINTER
         LA    R7,0(,R7)                TO FIRST CHARACTER OF ID
         LA    R8,0                INITIALIZE CHARACTER COUNT
CUTIDLEN CLI   0(R7),X'40'         IS THIS CHARACTER A BLANK?
         BE    NOMORCUT            YES, WE ARE AT THE END OF THE ID.
         LA    R7,1(,R7)           BUMP ANOTHER CHARACTER
         LA    R8,1(,R8)           COUNT ANOTHER CHARACTER
         CR    R8,R5               END OF COMMAND BUFFER ?
         BNL   NOMORCUT            YES. GET OUT NOW.
         BCT   R6,CUTIDLEN         TRY FOR BLANK AGAIN
NOMORCUT DS    0H
         MVC   NEWID(8),BLANKS     CLEAR FIELD
         BCTR  R8,0                SUBTRACT 1 FOR EXECUTE
         EX    R8,MOVEID           MAKE SUBCOMMAND INTO NEW ID
         OC    NEWID(8),BLANKS     UPPERCASE IT.
         L     R4,X'34'(,R2)       LOAD UPT ADDRESS
         USING UPT,R4
KZ       MODESET KEY=ZERO
         LA    R8,1(,R8)           BUMP REG BACK TO WHERE IT WAS
         TM    FLAG1,X'03'         IS NEW SUPPORT IN EFFECT?
         BZ    BATCHCNT            NO. DO OLD PROCESSING.
         C     R8,=F'8'            IS LENGTH 8?
         BL    BATCHNL7            NO. UPDATE BOTH FIELDS.
         TM    FLAG1,X'01'         IS SUPPORT OFF?
         BO    BATCHERR            THEN POST ERROR MESSAGE.
         MVC   PSCBUSER(7),=C'>7BYTES'  PUT INDICATOR IN OLD FIELD
         MVC   PSCBUSRL(1),=X'07'       PUT STANDARD LENGTH OF 7
         MVC   PSCB+X'54'(8),NEWID      PUT WHOLE ID IN NEW FIELD
         STC   R8,PSCB+X'20'            PUT LENGTH IN NEW FIELD
         MVC   UPTPREFX(7),=C'>7BYTES'  PUT INDICATOR IN OLD FIELD
         MVC   UPTPREFL(1),=X'07'       PUT STANDARD LENGTH OF 7
         MVC   UPT+X'21'(8),NEWID       PUT WHOLE ID IN NEW FIELD
         STC   R8,UPT+X'29'             PUT LENGTH IN NEW FIELD
         B     BATCHCU8                 BYPASS OTHER PROCESSING
BATCHERR DS    0H                  8-CHARACTER ID BUT NOT SUPPORTED
         TPUT  MSG0,L'MSG0
         B     EOJ4
BATCHNL7 DS    0H
         MVC   PSCB+X'54'(8),NEWID PUT WHOLE ID IN NEW FIELD
         STC   R8,PSCB+X'20'       PUT LENGTH IN NEW FIELD
         MVC   PSCBUSER(7),NEWID   SET USERID TO PARAMETER VALUE
         STC   R8,PSCBUSRL         PUT USERID LENGTH INTO PSCB
         MVC   UPT+X'21'(8),NEWID  PUT WHOLE ID IN NEW FIELD
         STC   R8,UPT+X'29'        PUT LENGTH IN NEW FIELD
         MVC   UPTPREFX(7),NEWID   SET PREFIX TO PARAMETER VALUE
         STC   R8,UPT+X'17'        PUT PREFIX LENGTH INTO UPTB
         B     BATCHCU8            FILL IN OLD FIELDS TOO.
BATCHCNT DS    0H                  CHECK INVALID LENGTH.
         C     R8,=F'8'            IS LENGTH 8?
         BE    BATCHERR            YES. SEND ERROR MESSAGE.
BATCHOLD DS    0H                  LENGTH NOT 8. FILL OLD FIELDS.
         MVC   PSCBUSER(7),NEWID   SET USERID TO PARAMETER VALUE
         STC   R8,PSCBUSRL         PUT USERID LENGTH INTO PSCB
         MVC   PSCB+X'54'(8),ZEROES    NULLIFY NEW PSCBUID8
         MVI   PSCB+X'20',X'00'        NULLIFY PSCBU8L
         MVC   UPTPREFX(7),NEWID   SET PREFIX TO PARAMETER VALUE
         STC   R8,UPT+X'17'        PUT PREFIX LENGTH INTO UPTB
         MVC   UPT+X'21'(8),ZEROES ZERO NEW PREFIX FIELD
         MVI   UPT+X'29',X'00'     ZERO NEW PREFIX LENGTH
BATCHCU8 DS    0H
* ---------------------------- >   LWA SUPPORT
         L     R1,X'6C'(,R10)      POINT TO ASXB
         L     R1,X'14'(,R1)       POINT TO LWA
         TM    FLAG1,X'02'         8-CHARACTER ID IN EFFECT?
         BNO   NOLWA8              NOPE. SKIP THAT UPDATE.
NOTEQ8   DS    0H
         C     R8,=F'8'            DID WE CODE 8 CHARS IN NEWID?
         BL    LESTH8              YES. EXIT WITH ERROR MESSAGE.
         STC   R8,X'48'(,R1)       CHANGE USERID LENGTH
         MVC   X'49'(7,R1),BLANKS  INVALIDATE OLD FIELD
         B     LESTH8A
LESTH8   DS    0H
         STC   R8,X'48'(,R1)       CHANGE USERID LENGTH
         MVC   X'49'(7,R1),NEWID   USE OLD FIELD TOO
LESTH8A  DS    0H
         LA    R3,X'1DC'(,R1)      NEW LWA USERID FIELD
         MVC   0(8,R3),NEWID       LOAD NEW FIELD TOO
         B     YESLWA8
NOLWA8   DS    0H
         TM    FLAG1,X'01'         NEW SYSTEM BUT NO 8-CHAR
         BZ    NO8SUP              NOPE. SAME UPDATE AS BEFORE.
         C     R8,=F'8'            DID WE CODE 8 CHAR ID?
         BNL   BATCHERR            YES. NO SUPPORT, SO ERRMSG.
         STC   R8,X'48'(,R1)       CHANGE USERID LENGTH
         LA    R3,X'49'(,R1)       POINT TO LWA USERID
         MVC   0(7,R3),NEWID       CHANGE USERID
         LA    R3,X'1DC'(,R1)      NO. FILL NEW LWA USERID FIELD TOO
         MVC   0(8,R3),NEWID       AND LOAD WITH USERID
         B     YESLWA8
NO8SUP   DS    0H
         C     R8,=F'8'            DID WE CODE 8 CHAR ID?
         BNL   BATCHERR            YES. NO SUPPORT, SO ERRMSG.
         STC   R8,X'48'(,R1)       CHANGE USERID LENGTH
         LA    R3,X'49'(,R1)       POINT TO LWA USERID
         MVC   0(7,R3),NEWID       CHANGE USERID
         LA    R3,X'1DC'(,R1)      POINT TO THE NEW USERID FIELD
         MVC   0(8,R3),ZEROES      AND NULLIFY WITH ZEROES
YESLWA8  DS    0H
NZ       MODESET KEY=NZERO
         B     EOJ0                NORMAL END
EOJ4     LA    R15,4               SET CC = 4.
EOJ      DS    0H                  BYE.
         LR    R1,R13
         L     R13,4(,R13)
         ST    R15,16(,R13)        PUT RC INTO PASSED SAVE AREA
         L     R14,12(,R13)
         FREEMAIN RU,LV=DATALEN,A=(R1),SP=SP000
         LM    R0,R12,20(R13)
         BR    R14
EOJ0     SR    R15,R15             SET CC = 0.
         B     EOJ                 CONT.
EOJ8     LA    R15,8               SET CC = 8.
         B     EOJ                 CONT
         EJECT
FULLZERO DC    F'0'
* - - - - - - - - - - - - - - - - - - - - - - - - *
MOVEID   MVC   NEWID(*-*),0(R1)    EXECUTED
* - - - - - - - - - - - - - - - - - - - - - - - - *
MSG0     DC    C'8-CHARACTER USERIDS ARE NOT SUPPORTED.'
BLANKS   DC    C'        '         8 BLANKS
ZEROES   DC    XL8'00'             8 ZEROES
BPARM    DC    AL2(BPARME-BPARM,0),C'NON'
BPARME   EQU   *
         SPACE 3
         LTORG
         SPACE 3
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*                     GETMAINED DATA AREA                           *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
DATAAREA DSECT
SAVE     DS    9D
PSCBADDR DS    F
TSVTADDR DS    F
CMBUFAD  DS    F                   COMMAND BUFFER ADDRESS
FLAG1    DS    X
NEWID    DC    C'        '         STORE NEW USERID HERE
         DS    CL72
         DS    0D
         SPACE 1
DATALEN  EQU   *-DATAAREA          LENGTH OF DATA AREA
         IKJUPT
         IKJPSCB
         END
