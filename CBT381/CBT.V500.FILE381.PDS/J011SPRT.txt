J011SPRT TITLE 'SPOOL PARTITIONING FOR SYSLOG AND STARTED TASKS'
         SPACE
         COPY  $HASPGBL
J011SPRT $MODULE NOTICE=SP220,ENVIRON=JES2,                            X
               SYSP=(GEN,GEN,DATA,GEN,GEN),                            X
               RPL,                                                    X
               $HASPEQU,                                               X
               $PCE,               CONTAINS USING PCEDSECT,R13         X
               $CMB,                                                   X
               $DAS,                                                   X
               $DCT,                                                   X
               $PDDB,                                                  X
               $IOT,                                                   X
               $RDRWORK,                                               X
               $BUFFER,                                                X
               $JCT,                                                   X
               $CAT,                                                   X
               $JOE,                                                   X
               $NIT,                                                   X
               $TAB,                                                   X
               $MIT,                                                   X
               $KIT,                                                   X
               $HCT,                                                   X
               $XECB,                                                  X
               $PADDR
***********************************************************************
*
* PURPOSE:
*
*  J011SPRT IS A SPOOL PARTITIONING EXIT DESIGNED TO WORK FROM EXIT 11
*  THIS EXIT WILL TELL JES TO TRY TO USE ONLY TRACK GROUPS ON THE SPOOL
*  VOLUMES THAT ARE DESIGNATED IN THE SPLTBL (SPOOL TABLE) FOR SYSLOG
*  AND STARTED TASKS.
*
* ENVIRONMENT: JES2 MAIN TASK
*
* POINT OF PROCESSING: THIS EXIT IS TAKEN FROM THE JES2 MAIN TASK,
*                      FROM THE $TRACK SUBROUTINE IN HASPTRAK,
*                      WHEN THERE IS NO SPACE AVAILABLE ON THE SPOOL
*                      VOLUMES FROM WHICH THE CURRENT JOB IS PERMITTED
*                      TO ALLOCATE SPACE.
*
* METHOD USED:
*
*  WHEN EXIT IS ENTERED, A CHECK IS MADE TO SEE IF JOB IS A STARTED
*  TASK OR ANYTHING OTHER THAN A JOB OR TSO SESSION. IF NOT, SET RC=0.
*  IF IT IS, THEN ZERO OUT THE JCTSAMSK (SPOOL PARTITIONING MASK -
*  ACTUALLY THE MASK WORK AREA FOR THIS EXIT).  GO THROUGH SPLTBL
*  AND DAS ENTRIES TO FIND MATCHES.  ONCE A MATCH IS FOUND, CHECK TO
*  SEE IF THE SPOOL IS SUITABLE FOR USE.  IF SO, "OR" THE DASMASK ONTO
*  THE MASK WORK AREA AND GET THE NEXT ENTRY IN SPLTBL.  IF THE SPOOL
*  VOLUME IS NOT AVAILABLE, SKIP THIS PARTICULAR SPLTBL ENTRY AND GET
*  ANOTHER.  ONCE THE LIST FROM SPLTBL HAS BEEN EXHAUSTED, RETURN
*  CONTROL TO JES WTIH RC=8, TELLING JES THAT AN UPDATED VERSION OF
*  THE SPOOL PARTITIONING MASK - WITH AT LEAST ONE ADDITIONAL BIT
*  TURNED ON - HAS BEEN PASSED TO JES IN THE SPOOL MASK WORK AREA
*  AND WILL NOW DETERMINE SUBSEQUENT SPOOL ALLOCATION.  IT ALSO TELLS
*  JES TO REISSUE THE $TRACK REQUEST.  IF NO MATCH IS FOUND, THE MASK
*  WORK AREA IS LEFT UNALTERED (SET TO IOTSAMSK FIELD AS ON ENTRY) AND
*  RC=0 TELLS JES TO SET THE SPOOL PARTITIONING MASK AS INDICATED
*  BY THE FENCE PARAMETER ON THE SPOOLDEF INITIALIZATION STATEMENT
*  AND TO REISSUE THE $TRACK REQUEST.
*
*  WRITTEN BY: DAVID VAN ZILE
*  ADDRESS:    FIRST UNION NATIONAL BANK
*              301 SOUTH COLLEGE STREET CHARLOTTE, N.C. 28288
*  PHONE:      (704) 374-4695
*
***********************************************************************
*
* REGISTER USAGE IN J011SPRT
*
*  R0    - N/A
*  R1  - ADDRESS OF A 3-WORD PARMAMETER LIST IN THE FOLLOWING FORMAT:
*        WORD 1 (+0) ADDRESS OF THE IOT
*        WORD 2 (+4) ADDRESS OF THE JCT (IF AVAILABLE); ELSE, 0
*        WORD 3 (+8) ADDRESS OF 32-BYTE SPOOL PARTITIONING MASK WORK
*                    AREA, INITIALLY SET TO THE IOTSAMSK FIELD
*
*  R2  - DAS
*  R3  - ADDRESS OF SPOOL VOLUME TABLE
*  R4  - NUMBER OF ENTRIES IN SPOOL VOLUME TABLE
*  R5  -
*  R6  - NUMBER OF SPOOL VOLUMES ALLOWED IN RUNNING JES
*  R7  - ADDRESS OF SPOOL PARTITIONING MASK WORK AREA (FROM PARMLIST)
*  R8  -
*  R9  -
*  R10 - JCT  (LOADED FROM PARAMETER LIST)
*  R11 - ADDRESS OF THE HCT
*  R12 - BASE
*  R13 - ADDRESS OF THE PCE
*  R14 - RETURN
*  R15 - ENTRY POINT; BALR; RETURN CODE
*
***********************************************************************
*                                                                     *
*        MAIN ENTRY POINT OF THE EXIT                                 *
*                                                                     *
***********************************************************************
E011SPRT $ENTRY BASE=(R12),CSECT=YES
         $SAVE                     SAVE CALLER'S REGISTERS
         LR    R12,R15             LOAD FIRST BASE REGISTER
*
         USING JCT,R10             SET UP ADDRESSABILITY TO JCT
         USING HCT,R11             SET UP ADDRESSABILITY TO HCT
*                                  SET UP ADDRESSABILITY TO
         ICM   R10,B'1111',4(R1)   LOAD ADDRESS OF JCT FROM PARM LIST
         BZ    GOPART              IF JCT NOT AVAILABLE (0), PARTITION
         CLI   JCTJOBID,C'J'       IS THIS A JOB?
         BE    EXIT0               YES.. FORGET IT
         CLI   JCTJOBID,C'T'       IS THIS A TSO SESSION?
         BE    EXIT0               YES.. FORGET IT
GOPART   EQU   *
         L     R7,8(R1)            LOAD ADDRESS OF MASK WORK FROM PARM
         XC    0(32,R7),0(R7)      CLEAR OUT SPOOL MASK WORK AREA
         LA    R3,SPLTBL           LOAD ADDRESS OF SPOOL VOLUME TABLE
         LA    R4,#ENTRYS          GET THE NUMBER OF VOLUMES IN TABLE
RESETDAS L     R2,$DASFRST         LOAD ADDRESS OF FIRST DAS
         USING DAS,R2              SET ADDRESSABILITY TO DAS
         LH    R6,$SPOLNUM         GET NUMBER OF SPOOL VOLUMES
CHKSPOOL CLC   0(6,R2),0(R3)       COMPARE DAS VOL TO SPLTBL VOL
         BNE   NXTDAS              NO MATCH HERE.  TRY ANOTHER DAS.
         TM    DASFLAG,DASACTIV+DASEXSTS+DASRPSF CAN WE USE THIS ONE?
         BNO   NXTSPOL             SORRY WE'RE CLOSED.  COME AGAIN...
         OC    0(32,R7),DASMASK    TURN ON CORRESPONDING BIT
         B     NXTSPOL             GO GET ANOTHER VOLUME FROM TABLE
NXTDAS   LA    R2,DASSIZ(,R2)      INCREMENT TO NEXT DAS ENTRY
         BCT   R6,CHKSPOOL         GO TRY NEXT DAS
NXTSPOL  LA    R3,ENTLEN(,R3)      POINT TO NEXT VOL IN SPOOL TABLE
         BCT   R4,RESETDAS         GO START WITH FIRST DAS
         LA    R15,8               SET RC=8
         B     EXIT8               MASK HAS BEEN SET - TELL JES
*
* RETURN TO CALLER
*
EXIT0    SLR   R15,R15                 SET RETURN CODE TO 0
EXIT8    EQU   *
         $RETURN RC=(R15)          RETURN TO JES2
         EJECT
SPLTBL   EQU   *
         DC    C'SPOOL1' <---------
ENTLEN   EQU   *-SPLTBL           |
         DC    C'SPOOL2'          |------ENTER SPOOL VOLUMES HERE
         DC    C'SPOOL3'          |
         DC    C'SPOOL4'          |
         DC    C'SPOOL5' <---------
#ENTRYS  EQU   (*-SPLTBL)/ENTLEN   CALCULATE THE NUMBER OF VOLUMES
*
         LTORG
         EJECT
         $MODEND
         END
