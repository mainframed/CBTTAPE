J002SPRT TITLE 'SPOOL PARTITIONING'
         SPACE
         COPY  $HASPGBL
J002SPRT $MODULE $HASPEQU,                                             X
               $PCE,               CONTAINS USING PCEDSECT,R13         X
               $CMB,                                                   X
               $DAS,                                                   X
               $DCT,                                                   X
               $PDDB,                                                  X
               $IOT,                                                   X
               $RDRWORK,                                               X
               $BUFFER,                                                X
               $JQE,                                                   X
               $JCT,                                                   X
               $CAT,                                                   X
               $JOE,                                                   X
               $NIT,                                                   X
               $TAB,                                                   X
               $MIT,                                                   X
               $KIT,                                                   X
               $HCT,                                                   X
               $PADDR,                                                 X
               RPL
***********************************************************************
*
* PURPOSE:
*
*  J002SPRT IS A SPOOL PARTITIONING EXIT DESIGNED TO WORK OFF OF EXIT 2
*  THIS EXIT WILL TELL JES TO TRY AND ONLY USE SPOOL SPACE ON THE
*  VOLUMES THAT ARE DESIGNATED IN THE SPLTBL (SPOOL TABLE) FOR STARTED
*  TASKS.
*
* METHOD USED:
*
*  WHEN EXIT IS ENTERED, A CHECK IS MADE TO SEE IF JOB IS A STARTED
*  TASK.  IF NOT A STC, THEN LEAVE EXIT WITH RC=0.  IF STC THEN ZERO
*  OUT THE JCTSAMSK (SPOOL PARTITIONING MASK).  GO THROUGH SPLTBL
*  AND DAS ENTRIES TO FIND MATCHES.  ONCE A MATCH IS FOUND, CHECK TO
*  SEE IF THE SPOOL IS SUITABLE FOR USE.  IF SO, "OR" THE DASMASK ONTO
*  THE JCTSAMSK (SPOOLMASK) AND GO GET NEXT ENTRY IN SPLTBL.  IF THE
*  SPOOL VOLUME IS NOT AVAILABLE, SKIP THIS PARTICULAR SPLTBL ENTRY
*  AND GO GET ANOTHER.  ONCE THE LIST FROM SPLTBL HAS BEEN EXHAUSTED,
*  RETURN CONTROL BACK TO JES.  IF NO MATCH IS FOUND, THE JCTSAMSK IS
*  LEFT TO BE ALL ZERO'S.  JES WILL THEN ASSIGN THE CORRECT DEFAULT
*  VALUE CORRESPONDING TO THE FENCE=YES/NO PARAMETER.
*
*  WRITTEN BY: JEFF CANIPE
*  ADDRESS:    FIRST UNION NATIONAL BANK
*              301 SOUTH TRYON STREET CHARLOTTE, N.C. 28288
*  PHONE:      (704) 374-4320
*
***********************************************************************
*
* REGISTER USAGE IN J002SPRT
*
*  R0  - A CODE INDICATING THE TYPE OF JOB STATEMENT BEING SCANNED
*        0  INDICATES AN INITIAL JOB STATEMENT IMAGE
*        4  INDICATES AN SUBSEQUENT JOB CONTINUATION STATEMENT
*  R1  - ADDRESS OF A 3-WORD PARMAMETER LIST IN THE FOLLOWING FORMAT:
*        WORD 1 (+0) POINTS TO THE JOB STATEMENT IMAGE BUFFER
*        WORD 2 (+4) POINTS TO THE EXIT FLAG BYTE, RDWFLAGX, IN THE PCE
*        WORD 3 (+8) POINTS TO THE JCTXWRK FIELD IN THE JCT
*  R2  - DAS
*  R3  - ADDRESS OF SPOOL VOLUME TABLE
*  R4  - NUMBER OF ENTRIES IN SPOOL VOLUME TABLE
*  R5  -
*  R6  - NUMBER OF SPOOL VOLUMES ALLOWED IN RUNNING JES
*  R7  -
*  R8  -
*  R9  -
*  R10 - JCT
*  R11 - HCT
*  R12 - BASE
*  R13 - PCE
*  R14 - RETURN
*  R15 - ENTRY POINT; BALR; RETURN CODE
*
***********************************************************************
*                                                                     *
*        EXIT MAIN ENTRY POINT                                        *
*                                                                     *
***********************************************************************
E002SPRT $ENTRY BASE=(R12),CSECT=YES
         $SAVE                     SAVE CALLER'S REGISTERS
         LR    R12,R15             LOAD FIRST BASE REGISTER
         USING JCT,R10             SET UP ADDRESSABILITY TO JCT
         USING HCT,R11             SET UP ADDRESSABILITY TO HCT
         CLI   JCTJOBID,C'S'       IS THIS A STARTED TASK?
         BNE   EXIT0               NO... BETTER LEAVE
         LA    R3,SPLTBL           LOAD ADDRESS OF SPOOL VOLUME TABLE
         LA    R4,#ENTRYS          GET THE NUMBER OF VOLUMES IN TABLE
         XC    JCTSAMSK,JCTSAMSK   CLEAR OUT SPOOL MASK
RESETDAS L     R2,$DASFRST         LOAD ADDRESS OF FIRST DAS
         USING DAS,R2              SET ADDRESSABILITY TO DAS
         LH    R6,$SPOLNUM         GET NUMBER OF SPOOL VOLUMES
CHKSPOOL CLC   0(6,R2),0(R3)       COMPARE DAS VOL TO SPLTBL VOL
         BNE   NXTDAS              NO MATCH HERE.  TRY ANOTHER DAS.
         TM    DASFLAG,DASACTIV+DASEXSTS+DASRPSF CAN WE USE THIS ONE?
         BNO   NXTSPOL             SORRY WE'RE CLOSED.  COME AGAIN...
         OC    JCTSAMSK,DASMASK    TURN ON CORRESPONDING BIT
         B     NXTSPOL             GO GET ANOTHER VOLUME FROM TABLE
NXTDAS   LA    R2,DASSIZ(,R2)      INCREMENT TO NEXT DAS ENTRY
         BCT   R6,CHKSPOOL         GO TRY NEXT DAS
NXTSPOL  LA    R3,ENTLEN(,R3)      POINT TO NEXT VOL IN SPOOL TABLE
         BCT   R4,RESETDAS         GO START WITH FIRST DAS
EXIT0    SR    R15,R15             SET RETURN CODE TO 0
RETURN   $RETURN RC=(R15)          RETURN TO JES2
SPLTBL   EQU   *
         DC    CL6'SPOOL1' <---------
ENTLEN   EQU   *-SPLTBL             |
         DC    CL6'SPOOL2'          |------ENTER SPOOL VOLUMES HERE
         DC    CL6'SPOOL3'          |
         DC    CL6'SPOOL4'          |
         DC    CL6'SPOOL5' <---------
#ENTRYS  EQU   (*-SPLTBL)/ENTLEN   CALCULATE THE NUMBER OF VOLUMES
         EJECT
         DROP  R2
         LTORG
         EJECT
         $MODEND
         END
