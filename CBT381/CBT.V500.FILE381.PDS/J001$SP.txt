         TITLE 'JES2 EXIT 1 PRINT/PUNCH SEPARATOR EXIT'
***********************************************************************
*                                                                     *
*        ENVIRONMENT   MVS SP 2.2.0                                   *
*                                                                     *
*        THIS JES2 EXIT PROGRAM IS DESIGNED TO REPLACE THE IBM        *
*        STANDARD PRINT AND PUNCH SEPARATOR ROUTINES.  THE PUNCH      *
*        SEPARATOR LACE CARD AND BLANK CARD IS PRODUCED EXACTLY       *
*        AS IBM PRODUCED THEM.  THE PRINT SEPARATOR WILL SUPPLY       *
*        EXTRA INFORMATION WHICH WILL BE SUPPLIED BY WHAT IS          *
*        CALLED A JOB INFORMATION TASK WHICH WILL SUPPLY EXTRA        *
*        DISTRIBUTION INFORMATION ON THE BASIS OF JOBNAME AND         *
*        OUTPUT GROUP NAME.  THE USE OF SMALLER BLOCK LETTERS         *
*        (5 X 7) WILL ALLOW UP TO 19 CHARACTERS ACROSS THE PAGE.      *
*        ONLY AS MANY LINES OF BLOCK LETTERS WILL BE PRODUCED         *
*        AS THERE IS ROOM FOR AS DEFINED BY THE NUMBER OF LINES       *
*        ON THE LOCAL OR REMOTE SEPARATOR IN THE PRINTDEF SEPLINE     *
*        AND RSEPLINE PARAMETERS.                                     *
*                                                                     *
*        STARTED TASKS AND TSO LOGONS DO NOT NORMALLY PROVIDE         *
*        INFORMATION SUCH AS PROGRAMMER NAME OR BIN NUMBER.           *
*        THIS EXIT WILL REQUEST THIS INFORMATION FROM THE JOB         *
*        INFORMATION TASK.  IF AN ENTRY IS NOT FOUND FOR THESE        *
*        JOBS IN THE VSAM FILE SEARCHED BY THE JOB INFORMATION        *
*        TASK, DEFAULT VALUES WILL BE SET.                            *
*                                                                     *
*        WRITTEN BY:                                                  *
*                        JOHN V. HOOPER                               *
*                        FIRST UNION NATIONAL BANK                    *
*                        301 SOUTH TRYON STREET                       *
*                        CHARLOTTE, N.C.  28288                       *
*                        (704) 374-2427                               *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*                          KOMSTAR                                    *
*                                                                     *
*        SUPPORT EXISTS IN THIS EXIT FOR ONLINE KOMSTAR (KODAK).      *
*        THE DEVICE NAMES ARE CHECKED FOR THOSE RESERVED FOR          *
*        THIS DEVICE TYPE.  THE STANDARD HEADER AND TRAILER           *
*        PAGES FOR THE KOMSTAR WILL BE BYPASSED AND BASED UPON THE    *
*        SYSOUT CLASS OF THE DATA BEING PROCESSED, DIFFERENT          *
*        ACTION MAY BE TAKEN IN THIS EXIT.                            *
*                                                                     *
*        SYSOUT CLASSES               ACTION                          *
*                                                                     *
*        Q           NO ACTION IS TAKEN BY THIS EXIT FOR ANY          *
*                    ENTRY FOR THESE CLASSES.  THESE CLASSES ARE      *
*                    RESERVED FOR OUTPUT WHICH IS DEBLOCKED           *
*                    INFOLINK FORMATTED DATA.  THE KOMSTAR MUST       *
*                    EMULATION MODE.                                  *
*                                                                     *
*        G           ONLY A TITLE RECORD IS SENT TO THE KOMSTAR       *
*                    FOR THESE CLASSES.  THESE CLASSES ARE            *
*                    RESERVED FOR OUTPUT FROM PME OR STARLINK1        *
*                    WHICH REQUIRES NO SETUP FOR THE KOMSTAR        . *
*                    RUNNING IN NATIVE MODE                           *
*                                                                     *
*        OTHERS      NATIVE COMMANDS TO SET HEADINGS FOR JOB          *
*                    NAME, PROGRAMMER NAME, ROOM NUMBER, AND          *
*                    DATE ARE ISSUED AT 'START' OR 'CONT'             *
*                    SEPARATOR ENTRY AND A NATIVE COMMAND TO          *
*                    EJECT THE FICHE AT 'END' SEPARATOR ENTRY.        *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*                    DATAGRAPHIX ARIS II / XL                         *
*                                                                     *
*        SUPPORT EXISTS IN THIS EXIT FOR ONLINE DATAGRAPHIX           *
*        MICROFICHE UNITS.  CERTAIN DEVICE NAMES HAVE BEEN            *
*        RESERVED FOR THE DATAGRAPHIX UNITS.  STANDARD                *
*        HEADER AND TRAILER PAGES ARE BYPASSED, AND A SPECIAL         *
*        PROCESSING SJOB RECORD WILL BE SENT TO THE PRINTER BASED     *
*        UPON THE WRITER NAME FOR THE OUTPUT GROUP.  SPECIAL          *
*        TITLE LINES WILL BE GENERATED AND SENT TO THE PRINTER TO     *
*        ALLOW THE UNIT TO EXTRACT ANY DESIRED INFORMATION SUCH       *
*        AS JOBNAME, JOB NUMBER, DATE, TIME, PROGRAMMER NAME AND      *
*        BIN NUMBER.  THE DEFAULT PROCESS ROUTINE ON THE DEVICE       *
*        WILL EXTRACT INFORMATION FROM THESE TITLE RECORDS TO         *
*        GENERATE THE APPROPRIATE TITLES.                             *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*                XJCF SUPPORT FOR THE XEROX 9700                      *
*                                                                     *
*        SUPPORT EXISTS IN THIS EXIT TO UTILIZE THE XJCF PRODUCT      *
*        FROM XENOS COMPUTER SYSTEMS TO SUPPORT THE XEROX 9700        *
*        PAGE PRINTER.  THE SUPPORT IS GENERATED BY SETTING THE       *
*        LOCAL ASSEMBLY VARIABLE &XJCF TO 'YES'.                      *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*               A S S E M B L Y     O P T I O N S                     *
*                                                                     *
*        SEVERAL ASSEMBLY OPTIONS ARE PROVIDED FOR EASE IN            *
*        PROVIDING THE DESIRED NUMBER OF SEPARATORS ON THE            *
*        START/CONT OR END SEPARATOR FOR BOTH LOCAL AND REMOTE        *
*        DEVICES.                                                     *
*                                                                     *
*        THE DEFAULTS SET ARE AS FOLLOWS:                             *
*                                                                     *
*        NPRSEPSL (1) - NUMBER OF START/CONT PAGES FOR LOCAL PRTRS    *
*        NPRSEPEL (1) - NUMBER OF END        PAGES FOR LOCAL PRTRS    *
*        NPRSEPSR (1) - NUMBER OF START/CONT PAGES FOR REMOTE PRTRS   *
*        NPRSEPER (1) - NUMBER OF END        PAGES FOR REMOTE PRTRS   *
*        IPLINES  (0) - VALUE TO ADD TO $PRIDCT FOR IMPACT PRINTERS   *
*                       TO ALLOW PRINTING ON THE PERFORATION.         *
*        JIT          - GENERATE SUPPORT FOR THE JOB INFORMATION      *
*                       TASK TO PROVIDE DISTRIBUTION INFORMATION.     *
*                       THE VALUES SHOULD BE YES/NO.                  *
*        UCC7         - GENERATE SUPPORT FOR THE UCC7 TASK WHICH      *
*                       WILL DEMAND JOB NETWORKS AT END OF PRINT      *
*                       PROCESSING.  THE VALUES SHOULD BE YES/NO.     *
*        UJQE         - GENERATE SUPPORT FOR USER FIELDS WHICH        *
*                       HAVE BEEN ADDED TO THE JQE - ESPECIALLY       *
*                       THE TEST/PRODUCTION INDICATOR.                *
*                       THE VALUES SHOULD BE YES/NO.                  *
*        XJCF         - GENERATE XJCF SUPPORT FOR THE XEROX 9700      *
*                       PRINTER.  THE VALUES SHOULD BE YES/NO.        *
*                                                                     *
***********************************************************************
         SPACE 3
***********************************************************************
*                                                                     *
*        ALSO, TWO START/CONT JOB SEPARATOR PAGES WILL BE GENERATED   *
*        WHENEVER A PRODUCTION JOB IS PROCESSED.  THIS ALLOWS ONE     *
*        PAGE TO BE KEPT BY DISTRIBUTION.                             *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*              M A C R O S   U S E D   I N   J 0 0 1 S E P            *
*                                                                     *
*        01)  $BUFFER      SYS1.HASPSRC                               *
*        02)  $CALL        SYS1.HASPSRC                               *
*        03)  $CAT         SYS1.HASPSRC                               *
*        04)  $DCT         SYS1.HASPSRC                               *
*        05)  $DTE         SYS1.HASPSRC                               *
*        06)  $ENTRY       SYS1.HASPSRC                               *
*        07)  $ERA         SYS1.HASPSRC                               *
*        08)  $FCSDTE      SYS1.HASPSRC (USER)                        *
*        09)  $FCSJITB     SYS1.HASPSRC (USER)                        *
*        10)  $FCSUCT      SYS1.HASPSRC (USER)                        *
*        11)  $FREEBUF     SYS1.HASPSRC                               *
*        12)  $GETBUF      SYS1.HASPSRC                               *
*        13)  $GETWORK     SYS1.HASPSRC                               *
*        14)  $HASPEQU     SYS1.HASPSRC                               *
*        15)  $HASPGBL     SYS1.HASPSRC                               *
*        16)  $HCT         SYS1.HASPSRC                               *
*        17)  $HFAME       SYS1.HASPSRC                               *
*        18)  $JCT         SYS1.HASPSRC                               *
*        19)  $JOE         SYS1.HASPSRC                               *
*        120  $JQE         SYS1.HASPSRC                               *
*        21)  $MIT         SYS1.HASPSRC                               *
*        22)  $MODEND      SYS1.HASPSRC                               *
*        23)  $MODULE      SYS1.HASPSRC                               *
*        24)  $PADDR       SYS1.HASPSRC                               *
*        25)  $PBLOCK      SYS1.HASPSRC                               *
*        26)  $PCE         SYS1.HASPSRC                               *
*        27)  $PDDB        SYS1.HASPSRC                               *
*        28)  $PPPWORK     SYS1.HASPSRC                               *
*        29)  $PRPUT       SYS1.HASPSRC                               *
*        30)  $RDT         SYS1.HASPSRC                               *
*        31)  $RETURN      SYS1.HASPSRC                               *
*        32)  $SAVE        SYS1.HASPSRC                               *
*        33)  $SEPPDIR     SYS1.HASPSRC                               *
*        34)  $SCAT        SYS1.HASPSRC                               *
*        35)  $SVT         SYS1.HASPSRC                               *
*        36)  $SYSPARM     SYS1.HASPSRC                               *
*        37)  $XECB        SYS1.HASPSRC                               *
*        38)  CVT          SYS1.AMODGEN                               *
*        39)  IFGRPL       SYS1.MACLIB                                *
*        40)  TIME         SYS1.MACLIB                                *
*                                                                     *
*                                                                     *
*              L I N K A G E    E D I T O R    I N F O                *
*                                                                     *
*        SIZE:       4K                                               *
*        ATTRIBUTES: REENTRANT, REUSEABLE, REFRESHABLE                *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
         COPY  $HASPGBL
J001$SP  $MODULE SYSP=(GEN,GEN,DATA,GEN,GEN),                          X
               ENVIRON=JES2,                                           X
               TITLE='JES2 EXIT1 PRINT/PUNCH SEPARATOR EXIT',          X
               $BUFFER,            GENERATE HASP BUFFER DSECT          X
               $CAT,               GENERATE HASP CAT DSECT             X
               $DCT,               GENERATE HASP DCT DSECT             X
               $DTE,               GENERATE HASP DTE DSECT             X
               $ERA,               GENERATE HASP ERA DSECT             X
               $HASPEQU,           GENERATE HASP EQUATES               X
               $HCT,               GENERATE HASP HCT DSECT             X
               $HFAME,             GENERATE HASP FAME DSECT            X
               $JCT,               GENERATE HASP JCT DSECT             X
               $JOE,               GENERATE HASP JOE DSECT             X
               $JQE,               GENERATE HASP JQE DSECT             X
               $MIT,               GENERATE HASP MIT DSECT             X
               $PADDR,             GENERATE HASP PADDR DSECT           X
               $PCE,               GENERATE HASP PCE DSECT             X
               $PDDB,              GENERATE HASP PDDB DSECT            X
               $PPPWORK,           GENERATE HASP PPPWORK DSECT         X
               $RDT,               GENERATE HASP RDT DSECT             X
               $SCAT,              GENERATE HASP SCAT DSECT            X
               $SVT,               GENERATE HASP SSVT DSECT            X
               $XECB,              GENERATE HASP XECB DSECT            X
               CVT,                GENERATE MVS CVT DSECT              X
               RPL                 GENERATE MVS RPL DSECT
         TITLE 'JES2 EXIT 1 PRINT/PUNCH SEPARATOR EXIT'
***********************************************************************
*                                                                     *
*        USER ADDITION TO THE DTE                                     *
*                                                                     *
***********************************************************************
DTE      DSECT
         ORG   DTEWORK
         DS    0CL2048             2048 BYTE EXTENSION
         $FCSDTE ,                 MAP THE DTE EXTENSION AREA
J001$SP  CSECT ,                   RESTORE THE CSECT ENVIRONMENT
         EJECT
***********************************************************************
*                                                                     *
*        USER CONTROL TABLE (UCT)                                     *
*                                                                     *
***********************************************************************
         $FCSUCT ,                 MAP THE USER CONTROL TABLE
J001$SP  CSECT ,                   RESTORE THE CSECT ENVIRONMENT
         EJECT
***********************************************************************
*                                                                     *
*        JOB INFORMATION TASK CONTROL BLOCK                           *
*                                                                     *
***********************************************************************
         $FCSJITB ,                MAP THE JITB CONTROL BLOCK
J001$SP  CSECT ,                   RESTORE THE CSECT ENVIRONMENT
         EJECT
***********************************************************************
*                                                                     *
*        WORK AREA USED BY THIS JES2 EXIT                             *
*                                                                     *
***********************************************************************
HDRDSECT DSECT                     HASP HEADER DSECT
         DS    CL(BUFSTART-BFPDSECT) *****  RESERVED BY JES2 *****
DEVTYPE  DS    C                   DEVICE TYPE
DEV3700  EQU   X'80'               XEROX 3700 DEVICE
HDR      DS    0CL133
HDRLEN   DS    AL1                 LENGTH OF PRINT LINE
HDRFRAME DS    XL1                 PP1 FRAME CHARACTERS
HDRCLASS DS    XL1                 PP2 SYSOUT CLASS
         DS    X                   PP3 BLANK
HDRFORM  DS    XL5                 PP4-8  FORM TYPE START/CONT/END
         DS    X                   PP9  BLANK
HDRNUM   DS    XL8                 PP10-17 JOB NUMBER
         DS    X                   PP18 BLANK
HDRNAME  DS    XL8                 PP19-26 JOB NAME
         DS    X                   PP27 BLANK
HDRJNAME DS    XL8                 PP28-35 JOE NAME
         DS    X                   PP36 BLANK
HDRJID1  DS    XL3                 PP37-39 JOE ID 1
         DS    X                   PP40 BLANK
HDRJID2  DS    XL3                 PP41-43 JOE ID 2
         DS    X                   PP44 BLANK
HDRJROUT DS    XL8                 PP45-52 JOE ROUTE
         DS    X                   PP53 BLANK
HDRPNAME DS    XL20                PP54-73 PROGRAMMER NAME
         DS    X                   PP74 BLANK
HDRRKEY  DS    XL4                 PP75-78 'ROOM'
         DS    X                   PP79 BLANK
HDRROOM  DS    XL4                 PP80-83 ROOM NUMBER
         DS    X                   PP84 BLANK
         DS    X                   PP85 BLANK
HDRPTTN  DS    XL8                 PP86-93 HH.MM.SS
         DS    X                   PP94 BLANK
HDRAM    DS    XL2                 PP95-96 AM/PM
         DS    X                   PP97 BLANK
HDRDATE  DS    XL9                 PP98-106 DD MMM YY
         DS    X                   PP107 BLANK
HDRDNAME DS    XL8                 PP108-115 DEVICE NAME
         DS    X                   PP116 BLANK
HDRSYS   DS    XL4                 PP117-120 SYSTEM NAME
         DS    X                   PP121 BLANK
HDRFORMX DS    XL5                 PP122-126 FORM
         DS    X                   PP127 BLANK
HDRCLASX DS    X                   PP128  JOE CLASS
HDRFRAMX DS    XL4                 PP129-132 FRAME CHARACTERS
HDRLENTH EQU   *-HDR               LEN FOR CLEARING HEADER
         ORG
EXITWRK1 DS    D                   EXIT WORK AREA 1
EXITWRK2 DS    D                   EXIT WORK AREA 2
EXITWRK3 DS    D                   EXIT WORK AREA 3
EXITWRK4 DS    F                   EXIT WORK AREA 4
EXITWRK5 DS    D                   EXIT WORK AREA 5
DBL      DS    D                   DOUBLE WORD WORK AREA
SAVETBL  DS    12F                 AREA FOR COPY OF DATE TABLE
FEB      EQU   SAVETBL+4,4         DATE TABLE ENTRY FOR FEBRUARY
REASON   DS    H                   EXIT ENTRY REASON CODE (0/4/8)
BINNBR   DS    CL8                 BIN NUMBER FROM JOB CARD OR ACCT
CDATE    DS    CL8                 CALENDAR DATE (MM/DD/YY)
JDATE    DS    CL6                 JULIAN DATE   (YY.DDD)
MONTH    DS    CL3                 NAME OF MONTH (MMM)
TOD      DS    CL11                TIME OF DAY (HH.MM.SS XM)
TIMEWORK DS    CL10                WORK AREA FOR TIME OF DAY ROUTINE
BUFWRK   DS    F                   ADDRESS OF A BUFFER WORK AREA
         DS    0F
DESTWORK DS    CL8                 CNVDEST WORK AREA
DEST     DS    CL8                 JOE DESTINATION NAME
TREC1    DS    0CL133              DATAGRAPHIX TITLE RECORD
TRECCTL1 DS    CL2                 CONTROL CHARACTERS
TRECID1  DS    CL5                 TREC ID CHARACTERS 'TREC1'
TRJOBNM  DS    CL8                 JOBNAME
TRJOBID  DS    CL8                 JOBID
TRPNAME  DS    CL20                PROGRAMMER NAME
TRPBIN   DS    CL9                 PRIMARY BIN NUMBER
TRGROUP  DS    CL8                 OUTPUT GROUP NAME
TRCDATE  DS    CL8                 CALENDAR DATE (MM/DD/YY)
TRJDATE  DS    CL6                 JULIAN DATE   (YY.DDD)
TRTIME   DS    CL11                TIME OF DAY (HH.MM.SS XM)
         ORG   TREC1
TREC2    DS    0CL133              DATAGRAPHIX TITLE RECORD
TRECCTL2 DS    CL2                 CONTROL CHARACTERS
TRECID2  DS    CL5                 TREC ID CHARACTERS 'TREC2'
TRTPIND  DS    C                   TEST/PRODUCTION IND (T/P)
TROWNER  DS    CL8                 OWNERID OF JOB
TRSBIN   DS    CL9                 SECONDARY BIN NUMBER (FROM JITB)
TROWNAME DS    CL20                OWNERID NAME         (FROM JITB)
TRLOC    DS    CL15                LOCATION             (FROM JITB)
TRDEPT   DS    CL20                DEPARTMENT           (FROM JITB)
TRMAIL   DS    CL10                MAIL CODE            (FROM JITB)
TRPHONE  DS    CL15                PHONE NUMBER         (FROM JITB)
         ORG   TREC2+133
STAT1    DS    CL132               STATISTICS LINE 1
STAT2    DS    CL132               STATISTICS LINE 2
STAT3    DS    CL132               STATISTICS LINE 3
STAT4    DS    CL132               STATISTICS LINE 4
STAT5    DS    CL132               STATISTICS LINE 5
STAT6    DS    CL132               STATISTICS LINE 6
STAT7    DS    CL132               STATISTICS LINE 7
STAT8    DS    CL132               STATISTICS LINE 8
STAT9    DS    CL132               STATISTICS LINE 9
STAT10   DS    CL132               STATISTICS LINE 10
J001$SP  CSECT ,                   RESTORE THE CSECT ENVIRONMENT
         PRINT OFF
         EJECT
***********************************************************************
*                                                                     *
*        ASSEMBLY VARIABLES                                           *
*                                                                     *
***********************************************************************
         LCLC  &JIT
         LCLC  &UCC7
         LCLC  &UJQE
         LCLC  &XJCF
NPRSEPSL EQU   1                   NBR OF LOCAL PRTR START SEPARATORS
NPRSEPEL EQU   1                   NBR OF LOCAL PRTR END   SEPARATORS
NPRSEPSR EQU   1                   NBR OF RMT   PRTR START SEPARATORS
NPRSEPER EQU   1                   NBR OF RMT   PRTR END   SEPARATORS
IPLINES  EQU   0                   EXTRA SEP LINES ON LOCAL IMPACT PTR
&JIT     SETC  'YES'               GENERATE JIT INTERFACE CODE
&UCC7    SETC  'YES'               GENERATE UCC7 INTERFACE CODE
&UJQE    SETC  'YES'               USE USER EXTENSIONS TO JQE
&XJCF    SETC  'YES'               GENERATE XJCF/XEROX 9700 SUPPORT
         PRINT ON
         SPACE 3
         USING JCT,R10             SET ADDRESSABILITY TO JCT
         EJECT
***********************************************************************
*                                                                     *
*        EXIT MAIN ENTRY POINT                                        *
*                                                                     *
***********************************************************************
E001$PR  $ENTRY BASE=(R9,R12)
         SPACE 1
         $SAVE                     SAVE CALLER'S REGISTERS
         LR    R9,R15              LOAD FIRST BASE REGISTER
         LA    R12,4095(R9)        LOAD SECOND
         LA    R12,1(R12)               BASE REGISTER
         LR    R2,R0               SAVE INPUT PARAMETER
         LR    R3,R1               SAVE INPUT DCT ADDRESS
         USING DCT,R3              SET ADDRESSABILITY TO DCT
         $GETBUF TYPE=HASP,FIX=YES,WAIT=YES GET A WORK BUFFER
         LR    R8,R1               SAVE ADDRESS OF BUFFER
         USING HDRDSECT,R8         SET ADDRESSABILITY TO HEADER LINE
         LA    R14,HDRDSECT+(BUFSTART-BFPDSECT) SET 'TO' ADDRESS
         LH    R15,$BUFSIZE        SET   'TO'   LENGTH
         SLR   R0,R0               CLEAR 'FROM' ADDRESS
         SLR   R1,R1               CLEAR 'FROM' LENGTH
         MVCL  R14,R0              CLEAR THE BUFFER
         $GETBUF TYPE=HASP,FIX=YES,WAIT=YES GET A WORK BUFFER
         LR    R4,R1               SAVE ADDRESS OF BUFFER
         LA    R1,BUFSTART-BFPDSECT(R4) POINT PAST BUFFER HEADER
         ST    R1,BUFWRK           SAVE ADDRESS OF BUFFER WORK AREA
         TM    DCTPPSW,DCTPPSWS    SEE IF SEPARATORS REQUESTED
         BO    EXIT                NO, GET OUT
         STH   R2,REASON           SAVE ENTRY REASON CODE
***********************************************************************
*        GET A JITB FOR THE DEVICE IF NOT ALREADY ONE                 *
***********************************************************************
GETJIT   ICM   R2,B'1111',DCTUSER1 LOAD THE ADDRESS OF THE JITB
         BNZ   GETJIT1             GOOD, BRANCH
         USING JITB,R2             SET ADDRESSABILITY TO JITB
         $GETWORK WORDS=128,USE=JITB,WAIT=YES GET A 512 BYTE WORK AREA
         ST    R1,DCTUSER1         SAVE THE ADDRESS
         LR    R2,R1               MOVE THE JITB ADDRESS TO REG 2
         MVC   JITBID,=CL4'JITB'   SET IDENTIFIER
GETJIT1  CLC   JITBID,=CL4'JITB'   SEE IF REALLY A JITB
         BE    GETJIT2             YES, BRANCH
GETJIT2  CLC   REASON,=H'8'        SEE IF "END" SEPARATOR
         BE    TODRTN              YES, JITB ALREADY SET
***********************************************************************
*                                                                     *
*        SET THE JITB KEY                                             *
*                                                                     *
*        TEST - THE OUTPUT GROUP NAME (IF SPECIFIED) WILL             *
*               BE THE FIRST KEY TO ALLOW A DEVELOPER TO              *
*               SEND OUTPUT TO ANOTHER USER OR DEPARTMENT.            *
*               IF THERE IS NO MATCH, THEN THE OWNERID WILL           *
*               BE THE KEY TO EXTRACT JITB INFORMATION FOR            *
*               THE JOB.                                              *
*                                                                     *
*        PROD - THE JOBNAME AND OUTPUT GROUP NAME (IF SPECIFIED)      *
*               WILL BE THE KEY.  IF THERE IS NO JITB AT THAT         *
*               LEVEL, THE JOBNAME BY ITSELF WILL BE USED TO          *
*               SEE IF A JOB DEFAULT EXISTS.  IF THERE IS NO          *
*               JITB AT THAT LEVEL, THEN THE FIRST TWO DIGITS         *
*               OF THE JOBNAME (SYSTEM CODE) WILL BE USED TO          *
*               GET A DEFAULT FOR THAT APPLICATION.                   *
*                                                                     *
***********************************************************************
         MVC   JITBJOBN,JCTJNAME   ASSUME STC OR TSU
         MVC   JITBGRP,=CL8' '     BLANK THE GROUP NAME
         TM    JCTJOBFL,JCTSTCJB+JCTTSUJB SEE IF TSO OR STC
         BNZ   GETJIT4             YES, GO CALL JOB INFORMATION TASK
         L     R7,JCTJQE           LOAD OFFSET OF JQE
         AL    R7,$JOBQPTR         CALCULATE ADDRESS OF JQE
         USING JQE,R7              SET ADDRESSABILITY TO JQE
         CLI   JQEXFLG1,JQEXPROD   SEE IF PRODUCTION JOB
         BE    GETJIT3             YES, GO SET GROUP ID
         L     R6,PPPWKJOE         LOAD ADDRESS OF WORK JOE
         USING JOE,R6              SET ADDRESSABILITY TO JOE
         CLI   JOENAME,C'0'        SEE IF JOE GROUP NAME NUMERIC
         BNL   GETJIT6             YES, GO TRY OWNERID
         MVC   JITBJOBN,JOENAME    NO,  SET KEY TO OWNERID
         B     GETJIT4             GO CALL JOB INFORMATION TASK
         DROP  R6                  DROP ADDRESSABILITY TO JOE
         DROP  R7                  DROP ADDRESSABILITY TO JQE
GETJIT3  L     R6,PPPWKJOE         LOAD ADDRESS OF WORK JOE
         USING JOE,R6              SET ADDRESSABILITY TO JOE
         CLI   JOENAME,C'0'        SEE IF JOE GROUP NAME NUMERIC
         BNL   GETJIT4             YES, LEAVE BLANK
         MVC   JITBGRP,JOENAME     MOVE JOE OUTPUT NAME TO JITB KEY
         DROP  R6                  DROP ADDRESSABILITY TO JOE
***********************************************************************
*        CALL JITB COMMUNICATION TASK                                 *
***********************************************************************
GETJIT4  $CALL CALLJIT             CALL JOB INFORMATION TASK
         TM    JITBSTAT,JITBOK     SEE IF ACTUALLY SUCCESSFUL
         BO    TODRTN              YES, CONTINUE
         L     R7,JCTJQE           LOAD OFFSET OF JQE
         AL    R7,$JOBQPTR         CALCULATE ADDRESS OF JQE
         USING JQE,R7              SET ADDRESSABILITY TO JQE
         CLI   JQEXFLG1,JQEXPROD   SEE IF PRODUCTION JOB
         BNE   GETJIT6             NO, GO TRY OWNERID FOR KEY
         CLI   JITBGRP,C' '        SEE IF GROUP NAME SPECIFIED
         BNH   GETJIT5             NO, BRANCH
         MVC   JITBGRP,=CL8' '     BLANK THE GROUP NAME FOR JOB DFLT
         $CALL CALLJIT             CALL JOB INFORMATION TASK
         TM    JITBSTAT,JITBOK     SEE IF ACTUALLY SUCCESSFUL
         BO    TODRTN              YES, CONTINUE
GETJIT5  MVC   JITBJOBN+2(6),=CL8' ' BLANK ALL BUT SYSTEM CODE
         $CALL CALLJIT             CALL JOB INFORMATION TASK
         B     TODRTN              TAKE WHAT WE HAVE
GETJIT6  MVC   JITBJOBN,JQEXOWNR   SET KEY TO OWNERID
         $CALL CALLJIT             CALL JOB INFORMATION TASK
         B     TODRTN              TAKE WHAT WE HAVE
         DROP  R7                  DROP ADDRESSABILITY TO JQE
         EJECT
***********************************************************************
*        GET TIME AND DATE                                            *
***********************************************************************
TODRTN   TIME  DEC                 GET TIME AND DATE
***********************************************************************
*        TIME (HH.MM.SS XM)                                           *
***********************************************************************
         ST    R0,EXITWRK3         SAVE TIME (HHMMSSTT)
         ST    R1,EXITWRK3+4       SAVE DATE
         MVO   EXITWRK2(5),EXITWRK3(4) SHIFT TO ADD SIGN
         OI    EXITWRK2+4,X'0F'    SET SIGN
         MVC   TOD+9(2),=CL2'AM'   SET TO 'AM'
         CP    EXITWRK2(5),=PL5'12000000' TEST TIME
         BL    TODRTN1             BRANCH IF AM
         MVI   TOD+9,C'P'          CHANGE FROM AM TO PM
         CP    EXITWRK2(5),=PL5'13000000' SEE IF AFTER 1 PM
         BL    TODRTN1             YES, DO NOT ADJUST FOR PM
         SP    EXITWRK2(5),=PL5'12000000' SUBTRACT 12 HOURS
TODRTN1  MVC   TIMEWORK,=X'402021204B20204B2020' SET EDIT MASK
         ED    TIMEWORK,EXITWRK2   EDIT TIME INTO WORK AREA
         MVC   TOD(8),TIMEWORK+2   SET TIME
***********************************************************************
*        DATE (MM/DD/YY AND YY.DDD)                                   *
***********************************************************************
         MVC   SAVETBL(48),YEARTBL SAVE A COPY OF DATE TABLE FOR UPDATE
         TM    EXITWRK3+5,X'01'    IT THIS AN ODD YEAR
         BO    TODRTN2             YES, CANNOT BE LEAP YEAR
         TM    EXITWRK3+5,X'12'    MAGIC LEAP YEAR MASK
         BM    TODRTN2             MIXED, CANNOT BE LEAP YEAR
         MVI   FEB,29              CHANGE FEB TO 29 DAYS
TODRTN2  MVC   EXITWRK5+5(3),=X'402120' MOVE EDIT PATTERN
         ED    EXITWRK5+6(3),EXITWRK3+5 EDIT THE YEAR (YY) TO WORK
         MVC   CDATE+6(2),EXITWRK5+6 MOVE YEAR (YY) TO CALENDAR DATE
         MVC   JDATE(2),EXITWRK5+6 MOVE YEAR (YY) TO JULIAN DATE
         MVC   EXITWRK3(6),$ZEROS  CLEAR ALL DATE EXCEPT JULIAN DAY
         OI    EXITWRK3+7,X'0F'    MAKE PRINTABLE
         UNPK  JDATE+3(3),EXITWRK3+6(2) MOVE DAY (DDD) TO JULIAN DATE
         MVI   JDATE+2,C'.'        SET DELIMITER
         CVB   R1,EXITWRK3         CONVERT TO BINARY DAY
         LA    R6,SAVETBL-4        LOAD ADDRESS OF DATE TABLE
         SLR   R0,R0               CLEAR FOR IC
         SLR   R15,R15             ZERO THE MONTH COUNTER
TODRTN3  SLR   R1,R0               SUBTRACT DAY/MONTH FROM TOTAL
         LA    R6,4(,R6)           INCREMENT TO NEXT DATE ENTRY
         IC    R0,0(,R6)           LOAD NUMBER OF DAYS IN THAT MONTH
         LA    R15,1(R15)          ADD 1 TO THE MONTH COUNTER
         CLR   R0,R1               SEE IF ONLY PARTIAL MONTH REMAINDING
         BL    TODRTN3             NO, LOOP
         MVC   MONTH,1(R6)         SAVE NAME OF MONTH (MMM)
         CVD   R15,EXITWRK3        CONVERT TO DECIMAL MONTH
         OI    EXITWRK3+7,X'0F'    MAKE MONTH PRINTABLE
         UNPK  CDATE+0(2),EXITWRK3+6(2) PLACE MONTH (MM)
         CVD   R1,EXITWRK3         CONVERT TO DECIMAL DAY
         OI    EXITWRK3+7,X'0F'    MAKE DAY PRINTABLE
         UNPK  CDATE+3(2),EXITWRK3+6(2) PLACE DAY (DD)
         MVI   CDATE+2,C'/'        INSERT DELIMITER
         MVI   CDATE+5,C'/'        INSERT DELIMITER
         EJECT
***********************************************************************
*                                                                     *
*        DETERMINE THE DESTINATION NAME FOR THE JOE                   *
*                                                                     *
***********************************************************************
GDEST    MVC   DESTWORK,=CL8'LOCAL' SET DEFAULT DESTINATION NAME
         L     R6,PPPWKJOE         LOAD ADDRESS OF WORK JOE
         USING JOE,R6              SET ADDRESSABILITY TO JOE
         CLC   JOEROUT(4),$ZEROS   SEE IF DESTINATION SPECIFIED
         BE    GDEST2              NO, LEAVE IT "LOCAL"
         CLC   JOEROUT(2),$OWNNODE SEE IF OUR NODE
         BNE   GDEST1              NO, GO LOOK AT DESTID TABLE
         CLC   JOEROUT+2(2),$ZEROS SEE IF REMOTE NUMBER
         BE    GDEST2              NO, LEAVE IT "LOCAL"
GDEST1   MVC   DESTWORK(4),JOEROUT SET DESTID FOR CNVDEST ROUTINE
         $CALL CNVDEST             CONVERT DESTINATION CODE
GDEST2   MVC   DEST,DESTWORK       SAVE THE DEST CODE
         DROP  R6                  DROP ADDRESSABILITY TO JOE
         EJECT
***********************************************************************
*                                                                     *
*        DETERMINE THE DEFAULT BIN NUMBER FOR THE JOB                 *
*                                                                     *
***********************************************************************
         $CALL DETBIN              DETERMINE THE DEFAULT BIN NUMBER
         EJECT
***********************************************************************
*        TEST FOR PRINTER OR PUNCH                                    *
***********************************************************************
         TM    PCEID,PCEPUSID      IS THIS A PUNCH
         BO    PUNCH               YES, BRANCH
         CLC   $OWNNODE,=H'2'      SEE IF NODE LE 2 (JACKSONVILLE FLA)
         BNH   PRINTER             YES, BRANCH
         CLC   DCTDEVN(6),=CL6'R20.PR'  IS THIS A 3700
         BE    DEVT3700            YES, BRANCH
         CLC   DCTDEVN(6),=CL6'R21.PR'  IS THIS A 3700
         BNE   PRINTER             NO, BRANCH
DEVT3700 OI    DEVTYPE,DEV3700     TURN ON A 3700 BIT
         EJECT
***********************************************************************
*                                                                     *
*                      P R I N T E R                                  *
*                                                                     *
***********************************************************************
         SPACE 3
***********************************************************************
*                                                                     *
*        GO PROCESS LOCAL MICROFICHE DEVICES                          *
*                                                                     *
***********************************************************************
PRINTER  CLC   $OWNNODE,=H'1'      SEE IF NODE = 1 (JACKSONVILLE FLA)
         BE    PRNTJAX             YES, BRANCH
         CLC   $OWNNODE,=H'2'      SEE IF NODE = 2 (JACKSONVILLE FLA)
         BE    PRNTJAX             YES, BRANCH
PRNTCHL  CLC   DCTDEVN,=CL8'PRINTR18' IS THIS A DATAGRAPHIX ARIS / XL
         BE    DX                  YES, BRANCH
         CLC   DCTDEVN,=CL8'PRT18'  ' IS THIS A DATAGRAPHIX ARIS / XL
         BE    DX                  YES, BRANCH
         CLC   DCTDEVN,=CL8'PRINTR19' IS THIS A DATAGRAPHIX ARIS / XL
         BE    DX                  YES, BRANCH
         CLC   DCTDEVN,=CL8'PRT19'    IS THIS A DATAGRAPHIX ARIS / XL
         BE    DX                  YES, BRANCH
         CLC   DCTDEVN,=CL8'PRINTR25' IS THIS A DATAGRAPHIX ARIS / XL
         BE    DX                  YES, BRANCH
         CLC   DCTDEVN,=CL8'PRT25'    IS THIS A DATAGRAPHIX ARIS / XL
         BE    DX                  YES, BRANCH
         CLC   DCTDEVN,=CL8'PRINTR26' IS THIS A DATAGRAPHIX ARIS / XL
         BE    DX                  YES, BRANCH
         CLC   DCTDEVN,=CL8'PRT26'    IS THIS A DATAGRAPHIX ARIS / XL
         BE    DX                  YES, BRANCH
         CLC   DCTDEVN,=CL8'PRINTR1'  IS THIS A KOMSTAR
         BE    KOMSTAR             YES, BRANCH
         CLC   DCTDEVN,=CL8'PRT1'     IS THIS A KOMSTAR
         BE    KOMSTAR             YES, BRANCH
         CLC   DCTDEVN,=CL8'PRINTR10' IS THIS A KOMSTAR
         BE    KOMSTAR             YES, BRANCH
         CLC   DCTDEVN,=CL8'PRT10'    IS THIS A KOMSTAR
         BE    KOMSTAR             YES, BRANCH
         CLC   DCTDEVN,=CL8'PRINTR14' IS THIS A KOMSTAR
         BE    KOMSTAR             YES, BRANCH
         CLC   DCTDEVN,=CL8'PRT14'    IS THIS A KOMSTAR
         BE    KOMSTAR             YES, BRANCH
         B     NORMAL              NO, JUST A 'NORMAL' PRINTER
PRNTJAX  CLC   DCTDEVN,=CL8'PRINTR7'  IS THIS A DATAGRAPHIX ARIS / XL
         BE    DX                  YES, BRANCH
         CLC   DCTDEVN,=CL8'PRT7'     IS THIS A DATAGRAPHIX ARIS / XL
         BE    DX                  YES, BRANCH
         CLC   DCTDEVN,=CL8'PRINTR10' IS THIS A DATAGRAPHIX ARIS / XL
         BE    DX                  YES, BRANCH
         CLC   DCTDEVN,=CL8'PRT10'    IS THIS A DATAGRAPHIX ARIS / XL
         BE    DX                  YES, BRANCH
         CLC   DCTDEVN,=CL8'PRINTR6'  IS THIS A DATAGRAPHIX ARIS / XL
         BE    DX                  YES, BRANCH
         CLC   DCTDEVN,=CL8'PRT6'     IS THIS A DATAGRAPHIX ARIS / XL
         BE    DX                  YES, BRANCH
***********************************************************************
*                                                                     *
*        PRODUCE THE SETUP HEADER FOR A SNA REMOTE IF REQUIRED.       *
*                                                                     *
***********************************************************************
NORMAL   TM    MDCTFEAT,DCTPSHDR   SEE IF SETUP HDR REQUIRED
         BNO   SETSEPCT            NO, BYPASS $SEPPDIR ROUTINE
         $SEPPDIR (R4)             SEND PDIR TO SNA RMT
***********************************************************************
*                                                                     *
*        DETERMINE THE NUMBER OF LINES TO BE PRODUCTED ON THE         *
*        SEPARATOR PAGE.  THIS MAY VARY DEPENDING ON WHETHER OR       *
*        NOT THE DEVICE IS A LOCAL PRINTER OR A REMOTE PRINTER.       *
*                                                                     *
***********************************************************************
SETSEPCT TM    DCTDEVID,DCTRMTID   IS THIS A REMOTE DEVICE?
         BZ    SETLOCAL            NO, BRANCH
         TM    DEVTYPE,DEV3700     SEE IF A XEROX 3700 DEVICE
         BO    SETLOCAL            YES, CONSIDER A LOCAL DEVICE
SETRMT   LA    R1,NPRSEPSR         LOAD NBR OF START SEP PGS FOR REMOTE
         CLC   REASON,=H'8'        TEST ENTRY REASON CODE
         BNE   SETCOUNT            NOT EIGHT, NOT END, BRANCH
         LA    R1,NPRSEPER         LOAD NBR OF END SEP PGS FOR REMOTE
         B     SETCOUNT            GO SET SEPARATOR PAGE COUNT
SETLOCAL LA    R1,NPRSEPEL         LOAD NBR OF END SEP PGS FOR LOCAL
         CLC   REASON,=H'8'        TEST ENTRY REASON CODE
         BE    SETCOUNT            EIGHT, END SEPARATOR, BRANCH
         LA    R1,NPRSEPSL         LOAD NBR OF START SEP PGS FOR LOCAL
         AIF   ('&XJCF' NE 'YES').XJCF#1
         AIF   ('&UJQE'  NE 'YES').XJCF#1
         TM    DCTPPSW9,DCT9700    SEE IF 9700
         BO    SET9700             YES, GO SET SEPARATOR COUNT
         TM    DEVTYPE,DEV3700     SEE IF A XEROX 3700 DEVICE
         BNO   SETCOUNT            NO, KEEP THE CURRENT SEPARATOR CNT
SET3700  LA    R1,1                SET SEPARATOR COUNT TO 1
         B     SETCOUNT            GO PROCESS SEPARATOR PAGE
SET9700  LA    R1,1                SET SEPARATOR COUNT TO 1
         L     R7,JCTJQE           LOAD OFFSET OF JQE
         AL    R7,$JOBQPTR         CALCULATE ADDRESS OF JQE
         USING JQE,R7              SET ADDRESSABILITY TO JQE
         CLI   JQEXFLG1,JQEXPROD   SEE IF PRODUCTION JOB
         BNE   SETCOUNT            NO, LEAVE SEPARATOR COUNT EQUAL TO 1
         LA    R1,2                SET SEPARATOR COUNT TO 2 FOR PROD
         DROP  R7                  DROP ADDRESSABILITY TO JQE
.XJCF#1  ANOP
SETCOUNT BCTR  R1,0                REDUCE COUNT BY 1
         ST    R1,EXITWRK4         AND SAVE COUNT
         EJECT
         AIF   ('&XJCF' NE 'YES').XJCF#2
***********************************************************************
*                                                                     *
*        PRODUCE THE DJDE FOR THE SEPARATOR PAGE.  THE $DJDE          *
*        MACRO COULD BE USED TO DO THIS EASILY BUT DUE TO THE         *
*        WAY WE GENERATE XJCF BANNERS, IT CANNOT GENERATE A           *
*        FONTINDEX OTHER THAN "NONE".  TO GET AROUND THIS             *
*        PROBLEM, WE WILL ESSENTIALLY DUPLICATE THE DJDE THAT         *
*        THE $DJDE MACRO WOULD HAVE GENERATED AND SET THE             *
*        XJCF BITS THAT REFLECT THAT ACTION.                          *
*                                                                     *
*                                                                     *
*        IF THIS IS THE 'END' SEPARATOR FOR A 9700, MAKE SURE         *
*        THAT THE CHANNEL 1 IS ON LINE 1.  A PROBLEM OCCURS IF        *
*        THE FCB FOR A PREVIOUS PRINT HAS CHANNEL 1 ON ANOTHER        *
*        LINE.  THE DJDE IS NOT RECOGNIZED FOR THE SEPARATOR AND      *
*        THE FOLLOWING JOB.                                           *
*                                                                     *
***********************************************************************
         TM    DCTPPSW9,DCT9700    SEE IF 9700
         BO    N9700#0             YES, BRANCH
         TM    DEVTYPE,DEV3700     SEE IF A XEROX 3700 DEVICE
         BNO   N9700#8             NO, BYPASS DJDE ROUTINE
N9700#0  CLC   REASON,=H'8'        SEE IF ENTRY FOR END-OF-JOB
         BNE   N9700#1             NO, BRANCH
         MVC   HDR(LDJDECRD),DJDECRD SET DJDE LINE
         LA    R0,LDJDECRD         LOAD LINE LENGTH
         LA    R1,HDR              LOAD ADDRESS OF DJDE LINE
         $PRPUT DATA=(R1),LEN=(R0),COUNT=1,WAIT=YES,CC=M
N9700#1  DS    0H
*        $DJDE BUFFER=(R4)         ISSUE A DJDE FOR SEPARATOR
         MVI   HDR,C' '            BLANK THE ENTIRE
         MVC   HDR+1(132),HDR         DJDE WORK AREA
         LA    R15,HDR             LOAD ADDRESS OF DJDE AREA
         MVC   HDR(LDJDE1A),DJDE1A  SET SPECIAL DJDE LINE PART 1
         LA    R15,LDJDE1A(R15)    INCREMENT PAST DJDE PART 1
         TM    DEVTYPE,DEV3700     SEE IF A XEROX 3700 DEVICE
         BO    N9700#3             YES, BYPASS DUPLEX ROUTINE
         L     R6,PPPCHJOE         LOAD ADDRESS CHARACTERISTICS JOE
         USING JOE,R6              SET ADDRESSABILITY TO JOE
         TM    JOEFLAGC,JOEFCDPX   SEE IF DUPLEX
         BNO   N9700#2             NO,  BRANCH
         MVC   0(11,R15),=C',DUPLEX=YES' SET TO DUPLEX=YES
         LA    R15,11(R15)         INCREMENT PAST "DUPLEX=YES"
         B     N9700#3             GO WRITE DJDE
         DROP  R6                  DROP ADDRESSABILITY TO JOE
N9700#2  MVC   0(10,R15),=C',DUPLEX=NO' SET TO DUPLEX=NO
         LA    R15,10(R15)         INCREMENT PAST "DUPLEX=NO"
N9700#3  MVC   0(LDJDE1B,R15),DJDE1B  SET REST OF DJDE
         LA    R15,LDJDE1B(R15)    INCREMENT PAST DJDE PART 2
         MVI   0(R15),C';'         ASSUME JOBID MESSAGE REQUIRED
         TM    DEVTYPE,DEV3700     SEE IF A XEROX 3700 DEVICE
         BO    N9700#3A            YES, NEVER PRODUCE JOBID MESSAGE
         CLC   REASON,=H'8'        SEE IF 'END' SEPARATOR
         BNE   N9700#4             NO, JOBID MESSAGE IS REQUIRED
N9700#3A MVC   0(4,R15),=C'END;'   YES, SET END OF DJDE
N9700#4  LA    R0,132              LOAD LINE LENGTH
         LA    R1,HDR              LOAD ADDRESS OF DJDE LINE
         $PRPUT DATA=(R1),LEN=(R0),COUNT=1,WAIT=YES,CC=M
         TM    DEVTYPE,DEV3700     SEE IF A XEROX 3700 DEVICE
         BO    N9700#7             YES, NEVER PRODUCE JOBID MESSAGE
         CLC   REASON,=H'8'        SEE IF 'END' SEPARATOR
         BE    N9700#7             YES, NO, JOBID MESSAGE REQUIRED
         MVC   HDR(LDJDE2),DJDE2   SET SPECIAL DJDE LINE
         MVC   HDR+21(8),JCTJNAME  SET JOB NAME
         MVC   HDR+30(8),JCTJOBID  SET JOB ID
         MVC   HDR+39(7),=C'''),END;' SET END OF DJDE
         TM    PPXFLAG2,PPX2SET    SEE IF SETUP REQUIRED
         BNO   N9700#6             NO, BRANCH
         MVC   HDR+39(7),=CL20' '  BLANK END OF RECORD
         MVC   HDR+40(2),=C'F='    SET TITLE
         L     R6,PPPCHJOE         LOAD ADDRESS CHARACTERISTICS JOE
         USING JOE,R6              SET ADDRESSABILITY TO JOE
         MVC   HDR+42(8),JOEFORM   SET FORM NUMBER
         MVI   HDR+51,C'('         SET DELIMETER
         MVC   HDR+52(8),=C'NOSHIFT)' ASSUME NOSHIFT
         TM    JOEFLAGC,JOEFCSFT   SEE IF SHIFT=YES
         BNO   N9700#5             NO, LEAVE IT NOSHIFT
         MVC   HDR+52(8),=C'SIMPLEX)' ASSUME SIMPLEX
         TM    DEVTYPE,DEV3700     SEE IF A XEROX 3700 DEVICE
         BO    N9700#5             YES, LEAVE IT SIMPLEX
         TM    JOEFLAGC,JOEFCDPX   SEE IF DUPLEX
         BNO   N9700#5             NO, LEAVE IT SIMPLEX
         MVC   HDR+52(8),=C'DUPLEX) ' SET TO DUPLEX
N9700#5  MVC   HDR+60(12),=C''',WAIT),END;' SET END OF DJDE
N9700#6  LA    R0,LDJDE2           LOAD LINE LENGTH
         LA    R1,HDR              LOAD ADDRESS OF DJDE LINE
         $PRPUT DATA=(R1),LEN=(R0),COUNT=1,WAIT=YES,CC=M
         DROP  R6                  DROP ADDRESSABILITY TO JOE
N9700#7  NI    PPXFLAG2,255-PPX2SET-PPX2GJID TURN OFF SETUP/MSG BITS
         OI    PPXFLAG2,PPX2GJDL   GENERATE JDL/JDE OPNDS ON NXT DJDE
N9700#8  DS    0H
.XJCF#2 ANOP
         B     PRINTRTN            GO PRINT FIRST SEPARATOR
CALLPRNT ICM   R1,15,EXITWRK4      LOAD NUMBER OF SEP PAGES LEFT TO DO
         BZ    ENDPRNT             ZERO, DONE, EXIT
         BM    ENDPRNT             NEGATIVE, ERROR, BUT EXIT ANYWAY
         BCTR  R1,0                REDUCE COUNT BY 1
         ST    R1,EXITWRK4         AND SAVE NEW COUNT
         AIF   ('&XJCF' NE 'YES').XJCF#3
***********************************************************************
*                                                                     *
*        IF THIS IS A 9700 PRINTER IN DUPLEX MODE, A BLANK            *
*        SEPARATOR PAGE MUST BE CREATED BETWEEN EACH PAGE SO          *
*        THAT MULTIPLE SEPARATOR PAGES WILL ALL BE ON THE FRONT       *
*        OF A PHYSICAL PAGE.                                          *
*                                                                     *
***********************************************************************
         TM    DEVTYPE,DEV3700     SEE IF A XEROX 3700 DEVICE
         BO    NOBLANK             YES, BYPASS BLANK PAGE ROUTINE
         TM    DCTPPSW9,DCT9700    SEE IF 9700
         BNO   NOBLANK             NO, BRANCH
CKBLANK  L     R6,PPPCHJOE         LOAD ADDRESS CHARACTERISTICS JOE
         USING JOE,R6              SET ADDRESSABILITY TO JOE
         TM    JOEFLAGC,JOEFCDPX   SEE IF IN DUPLEX MODE
         BNO   NOBLANK             NO, BRANCH
         DROP  R6                  DROP ADDRESSABILITY TO CHAR JOE
         MVC   HDR(2),=X'8B40'     MOVE EJECT COMMAND TO BUFFER
         LA    R0,2                LOAD LINE LENGTH
         LA    R1,HDR              LOAD ADDRESS OF EJECT COMMAND
         $PRPUT DATA=(R1),LEN=(R0),COUNT=1,WAIT=YES,CC=M
         MVI   HDR,X'40'           MOVE BLANK LINE TO BUFFER
         LA    R0,1                LOAD LINE LENGTH
         LA    R1,HDR              LOAD ADDRESS OF EJECT COMMAND
         $PRPUT DATA=(R1),LEN=(R0),COUNT=1,WAIT=YES
.XJCF#3 ANOP
NOBLANK  MVC   HDR(2),=X'8B40'     MOVE EJECT COMMAND TO BUFFER
         LA    R0,2                LOAD LINE LENGTH
         LA    R1,HDR              LOAD ADDRESS OF EJECT COMMAND
         $PRPUT DATA=(R1),LEN=(R0),COUNT=1,WAIT=YES,CC=M
         B     PRINTRTN            GO PRINT A SEPARATOR
         EJECT
***********************************************************************
*                                                                     *
*        FINISH PROCESSING ON ALL SEPARATOR PAGES                     *
*                                                                     *
***********************************************************************
ENDPRNT  DS    0H
***********************************************************************
*                                                                     *
*        IF WE HAVE JUST FINISHED PROCESSING END SEPARATOR            *
*        PAGE(S), AN EJECT COMMAND MUST BE ISSUED TO PROPERLY         *
*        POSITION FOR EDGE MARKING.                                   *
*                                                                     *
***********************************************************************
ENDPRNT1 CLC   REASON,=H'8'        TEST ENTRY REASON CODE
         BNE   ENDPRNT3            NOT EIGHT, NOT END, BRANCH
***********************************************************************
*                                                                     *
*        FORCE PAGE EJECT AFTER END SEPARATOR PAGE                    *
*                                                                     *
***********************************************************************
ENDPRNT2 MVC   HDR(2),=X'8B40'     MOVE EJECT COMMAND TO BUFFER
         LA    R0,2                LOAD LINE LENGTH
         LA    R1,HDR              LOAD ADDRESS OF EJECT COMMAND
         $PRPUT DATA=(R1),LEN=(R0),COUNT=1,WAIT=YES,CC=M
         SPACE 3
         AIF   ('&UCC7' NE 'YES').UCC7#1
***********************************************************************
*                                                                     *
*        CALL UCC7 TASK TO DEMAND A JOB NETWORK IF THE JITB           *
*        THAT HAS BEEN READ FOR THE JOB REQUESTS THIS ACTION.         *
*                                                                     *
***********************************************************************
         TM    JITBSTAT,JITBOK     DO WE HAVE A GOOD JITB
         BNO   EXIT                NO, GET OUT
         CLI   JITBU7FL,C'Y'       SEE IF UCC7 NETWORK REQUESTED
         BNE   EXIT                NO, GET OUT
         $CALL CALLUC7             CALL UCC7 TASK
.UCC7#1  ANOP
         B     EXIT                GET OUT
         EJECT
***********************************************************************
*                                                                     *
*        ACTIVATE PRINTING OF THE JES2 NEWS DATASET FOR TEST          *
*        JOBS WITH FORMS=STD.                                         *
*                                                                     *
*        NO NEWS WILL BE CREATED FOR STARTED TASKS OR TSO             *
*        OUTPUT.                                                      *
*                                                                     *
***********************************************************************
ENDPRNT3 TM    JCTJOBFL,JCTSTCJB+JCTTSUJB IS THIS A STC OR TSO USER?
         BM    EXIT                YES, NO NEWS
         AIF   ('&UJQE' NE 'YES').UJQE#1
         L     R7,JCTJQE           LOAD OFFSET OF JQE
         AL    R7,$JOBQPTR         CALCULATE ADDRESS OF JQE
         USING JQE,R7              SET ADDRESSABILITY TO JQE
         CLI   JQEXFLG1,JQEXPROD   SEE IF PRODUCTION JOB
         BE    EXIT                YES, NO NEWS
         DROP  R7                  DROP ADDRESSABILITY TO JQE
.UJQE#1  ANOP
         L     R6,PPPCHJOE         LOAD ADDRESS CHARACTERISTICS JOE
         USING JOE,R6              SET ADDRESSABILITY TO JOE
         CLC   JOEFORM(4),=C'STD ' SEE IF STANDARD FORM
         BNE   EXIT                NO, NO NEWS
         DROP  R6                  DROP ADDRESSABILITY TO CHAR JOE
         L     R15,$NEWSTTR        GET JES2 NEWS MTTR
         LTR   R15,R15             SEE IF JES2 NEWS EXISTS
         BZ    EXIT                NO, GET OUT
         ST    R15,PPPLEBTR        SET JES2 NEWS MTTR
         OI    PPPFLAG1,X'01'      SET JES2 NEWS FLAG
         MVC   PPPEBRCB,$H1        SET RCB OFFSET
         MVC   PPPKEY(6),=C'$$NEWS'   SET SPECIAL NEWS KEY
         MVC   PPPLNECT,=F'-1'     SET LARGE PAGE SIZE
         B     EXIT                GET OUT
         EJECT
***********************************************************************
*                                                                     *
*        PRINT SEPARATOR ROUTINE - MAY BE CALLED MULTIPLE TIMES       *
*                                                                     *
***********************************************************************
PRINTRTN SLR   R5,R5               CLEAR REG FOR IC
         TM    DEVTYPE,DEV3700     SEE IF A XEROX 3700 DEVICE
         BO    NOTRMT              YES, TREAT LIKE A LOCAL PRINTER
         IC    R5,$TPIDCT          LOAD REMOTE SEPARATOR LINE COUNT
         TM    DCTDEVID,DCTRMTID   SEE IF THIS IS A REMOTE DEVICE
         BO    STARTPRT            YES, BRANCH
NOTRMT   IC    R5,$PRIDCT          LOAD LOCAL SEPARATOR LINE COUNT
         AIF   ('&XJCF' NE 'YES').XJCF#4
         TM    DCTPPSW9,DCT9700    SEE IF 9700
         BO    PR9700              YES, GO PRODUCE SPECIAL SEPARATOR
         TM    DEVTYPE,DEV3700     SEE IF A XEROX 3700 DEVICE
         BNO   NOT9700             NO, GO PRODUCE NORMAL SEPARATOR
***********************************************************************
*                                                                     *
*        PRODUCE THE 9700 SEPARATOR PAGE                              *
*                                                                     *
***********************************************************************
PR9700   L     R5,BUFWRK           POINT TO BUFFER WORK AREA
         MVI   0(R5),C' '          BLANK THE ENTIRE
         MVC   1(132,R5),0(R5)         BUFFER WORK AREA
         MVI   0(R5),C'6'          SET FONT INDEX
         LA    R0,132              LOAD LINE LENGTH
         $PRPUT DATA=(R5),LEN=(R0),WAIT=YES,COUNT=1 WRITE HEADING LINE
         MVI   0(R5),C' '          BLANK THE ENTIRE
         MVC   1(132,R5),0(R5)         BUFFER WORK AREA
         MVI   0(R5),C'8'          SET FONT INDEX
         MVC   1(4,R5),BINNBR      MOVE BIN NUMBER TO BIN AREA 1
         MVC   49(4,R5),BINNBR     MOVE BIN NUMBER TO BIN AREA 2
         MVI   5(R5),C'-'          SET DELIMETER
         MVI   53(R5),C'-'         SET DELIMETER
         MVC   6(4,R5),BINNBR+4    MOVE BIN NUMBER  TO BIN AREA2
         MVC   54(4,R5),BINNBR+4   MOVE BIN NUMBER  TO BIN AREA2
         TM    JITBSTAT,JITBOK     DO WE HAVE A GOOD JITB
         BNO   PR9700A             NO, BRANCH
         CLC   JITBPSID,=CL20' '  SEE IF PRIMARY SEP ID SPECIFIED
         BNH   PR9700A             NO, USE DEFAULT
         MVC   1(9,R5),=CL20' '    BLANK PRIMARY SEPARATOR ID
         MVC   49(9,R5),=CL20' '   BLANK PRIMARY SEPARATOR ID
         MVC   1(4,R5),JITBPSID    MOVE PRIM SEP ID1 TO BIN AREA 1
         MVC   49(4,R5),JITBPSID   MOVE PRIM SEP ID1 TO BIN AREA 1
         MVC   6(4,R5),JITBPSID+4  MOVE PRIM SEP ID2 TO BIN AREA 2
         MVC   54(4,R5),JITBPSID+4 MOVE PRIM SEP ID2 TO BIN AREA 2
         CLC   JITBPSID(4),=CL20' ' SEE IF PRIMARY SEP ID1 SPECIFIED
         BE    PR9700A             NO, NO DELIMETER
         CLC   JITBPSID+4(4),=CL20' ' SEE IF PRIMARY SEP ID2 SPECIFIED
         BE    PR9700A             NO, NO DELIMETER
         MVI   5(R5),C'-'          SET DELIMETER
         MVI   53(R5),C'-'         SET DELIMETER
PR9700A  LA    R0,132              LOAD LINE LENGTH
         $PRPUT DATA=(R5),LEN=(R0),WAIT=YES,COUNT=1 WRITE HEADING LINE
         MVI   0(R5),C' '          BLANK THE ENTIRE
         MVC   1(132,R5),0(R5)         BUFFER WORK AREA
         MVI   0(R5),C'8'          SET FONT INDEX
PR9700B  LA    R0,132              LOAD LINE LENGTH
         $PRPUT DATA=(R5),LEN=(R0),WAIT=YES,COUNT=1 WRITE HEADING LINE
         MVI   0(R5),C' '          BLANK THE ENTIRE
         MVC   1(132,R5),0(R5)         BUFFER WORK AREA
         MVI   0(R5),C'7'          SET FONT INDEX
         MVC   35(8,R5),JCTJNAME   MOVE JOB NAME TO MSG AREA
PR9700C  LA    R0,132              LOAD LINE LENGTH
         $PRPUT DATA=(R5),LEN=(R0),WAIT=YES,COUNT=1 WRITE HEADING LINE
         MVI   0(R5),C' '          BLANK THE ENTIRE
         MVC   1(132,R5),0(R5)         BUFFER WORK AREA
         MVI   0(R5),C'7'          SET FONT INDEX
PR9700D  LA    R0,132              LOAD LINE LENGTH
         $PRPUT DATA=(R5),LEN=(R0),WAIT=YES,COUNT=1 WRITE HEADING LINE
         MVI   0(R5),C' '          BLANK THE ENTIRE
         MVC   1(132,R5),0(R5)         BUFFER WORK AREA
         MVI   0(R5),C'7'          SET FONT INDEX
         L     R6,PPPWKJOE         LOAD ADDRESS OF WORK JOE
         USING JOE,R6              SET ADDRESSABILITY TO JOE
         CLI   JOENAME,C'0'        SEE IF JOE GROUP NAME NUMERIC
         BNL   PR9700E             YES, LEAVE BLANK
         MVC   35(8,R5),JOENAME    MOVE JOE OUTPUT NAME TO MSG AREA
         DROP  R6                  DROP ADDRESSABILITY TO JOE
PR9700E  LA    R0,132              LOAD LINE LENGTH
         $PRPUT DATA=(R5),LEN=(R0),WAIT=YES,COUNT=1 WRITE HEADING LINE
         MVI   0(R5),C' '          BLANK THE ENTIRE
         MVC   1(132,R5),0(R5)         BUFFER WORK AREA
         MVI   0(R5),C'7'          SET FONT INDEX
PR9700F  LA    R0,132              LOAD LINE LENGTH
         $PRPUT DATA=(R5),LEN=(R0),WAIT=YES,COUNT=1 WRITE HEADING LINE
         MVI   0(R5),C' '          BLANK THE ENTIRE
         MVC   1(132,R5),0(R5)         BUFFER WORK AREA
         MVI   0(R5),C'7'          SET FONT INDEX
         MVC   35(8,R5),JCTJOBID   MOVE JOB ID TO MSG AREA
PR9700G1 CLI   39(R5),C' '         SEE IF LEFT JUSTIFIED
         BNE   PR9700G             YES, GET OUT
         MVC   39(4,R5),40(R5)     SHIFT JOB NUMBER
         B     PR9700G1            LOOP
PR9700G  LA    R0,132              LOAD LINE LENGTH
         $PRPUT DATA=(R5),LEN=(R0),WAIT=YES,COUNT=1 WRITE HEADING LINE
         MVI   0(R5),C' '          BLANK THE ENTIRE
         MVC   1(132,R5),0(R5)         BUFFER WORK AREA
         MVI   0(R5),C'7'          SET FONT INDEX
PR9700H  LA    R0,132              LOAD LINE LENGTH
         $PRPUT DATA=(R5),LEN=(R0),WAIT=YES,COUNT=2 WRITE HEADING LINE
         MVI   0(R5),C' '          BLANK THE ENTIRE
         MVC   1(132,R5),0(R5)         BUFFER WORK AREA
         MVI   0(R5),C'7'          SET FONT INDEX
         MVC   35(20,R5),JCTPNAME  MOVE PGMR NAME TO MSG AREA
PR9700I  LA    R0,132              LOAD LINE LENGTH
         $PRPUT DATA=(R5),LEN=(R0),WAIT=YES,COUNT=1 WRITE HEADING LINE
         MVI   0(R5),C' '          BLANK THE ENTIRE
         MVC   1(132,R5),0(R5)         BUFFER WORK AREA
         MVI   0(R5),C'7'          SET FONT INDEX
PR9700J  LA    R0,132              LOAD LINE LENGTH
         $PRPUT DATA=(R5),LEN=(R0),WAIT=YES,COUNT=1 WRITE HEADING LINE
         MVI   0(R5),C' '          BLANK THE ENTIRE
         MVC   1(132,R5),0(R5)         BUFFER WORK AREA
         MVI   0(R5),C'7'          SET FONT INDEX
         TM    JITBSTAT,JITBOK     DO WE HAVE A GOOD JITB
         BNO   PR9700K             NO, BRANCH
         CLI   JITBPGMR,C' '       SEE IF PROGRAMMER NAME SPECIFIED
         BNH   PR9700K             NO, USE BLANKS
         MVC   35(20,R5),JITBPGMR  MOVE PROGRAMMER NAME TO MSG AREA
PR9700K  LA    R0,132              LOAD LINE LENGTH
         $PRPUT DATA=(R5),LEN=(R0),WAIT=YES,COUNT=1 WRITE HEADING LINE
         MVI   0(R5),C' '          BLANK THE ENTIRE
         MVC   1(132,R5),0(R5)         BUFFER WORK AREA
         MVI   0(R5),C'7'          SET FONT INDEX
PR9700L  LA    R0,132              LOAD LINE LENGTH
         $PRPUT DATA=(R5),LEN=(R0),WAIT=YES,COUNT=2 WRITE HEADING LINE
         MVI   0(R5),C' '          BLANK THE ENTIRE
         MVC   1(132,R5),0(R5)         BUFFER WORK AREA
         MVI   0(R5),C'7'          SET FONT INDEX
         MVC   35(8,R5),CDATE      MOVE DATE TO MSG AREA
PR9700M  LA    R0,132              LOAD LINE LENGTH
         $PRPUT DATA=(R5),LEN=(R0),WAIT=YES,COUNT=1 WRITE HEADING LINE
         MVI   0(R5),C' '          BLANK THE ENTIRE
         MVC   1(132,R5),0(R5)         BUFFER WORK AREA
         MVI   0(R5),C'7'          SET FONT INDEX
PR9700N  LA    R0,132              LOAD LINE LENGTH
         $PRPUT DATA=(R5),LEN=(R0),WAIT=YES,COUNT=1 WRITE HEADING LINE
         MVI   0(R5),C' '          BLANK THE ENTIRE
         MVC   1(132,R5),0(R5)         BUFFER WORK AREA
         MVI   0(R5),C'7'          SET FONT INDEX
         MVC   35(11,R5),TOD       MOVE TIME TO MSG AREA
         OI    35(R5),X'F0'        ELIMINATE POSSIBLE LEADING BLANK
PR9700O  LA    R0,132              LOAD LINE LENGTH
         $PRPUT DATA=(R5),LEN=(R0),WAIT=YES,COUNT=1 WRITE HEADING LINE
         MVI   0(R5),C' '          BLANK THE ENTIRE
         MVC   1(132,R5),0(R5)         BUFFER WORK AREA
         MVI   0(R5),C'7'          SET FONT INDEX
PR9700P  LA    R0,132              LOAD LINE LENGTH
         $PRPUT DATA=(R5),LEN=(R0),WAIT=YES,COUNT=1 WRITE HEADING LINE
         MVI   0(R5),C' '          BLANK THE ENTIRE
         MVC   1(132,R5),0(R5)         BUFFER WORK AREA
         MVI   0(R5),C'7'          SET FONT INDEX
         TM    JITBSTAT,JITBOK     DO WE HAVE A GOOD JITB
         BNO   PR9700Q             NO, BRANCH
         CLC   JITBSSID,=CL20' '   SEE IF SECONDARY SEP ID SPECIFIED
         BNH   PR9700Q             NO, LEAVE IT BLANK
         MVC   35(4,R5),JITBSSID   MOVE SECOND SEP ID1 TO BIN AREA
         MVC   40(4,R5),JITBSSID+4  MOVE SECOND SEP ID2 TO BIN AREA
         CLC   JITBSSID(4),=CL20' ' SEE IF SECOND SEP ID1 SPECIFIED
         BE    PR9700Q             NO, NO DELIMETER
         CLC   JITBSSID+4(4),=CL20' ' SEE IF SECOND SEP ID2 SPECIFIED
         BE    PR9700Q             NO, NO DELIMETER
         MVI   39(R5),C'-'         SET DELIMETER
PR9700Q  LA    R0,132              LOAD LINE LENGTH
         $PRPUT DATA=(R5),LEN=(R0),WAIT=YES,COUNT=1 WRITE HEADING LINE
         MVI   0(R5),C' '          BLANK THE ENTIRE
         MVC   1(132,R5),0(R5)         BUFFER WORK AREA
         MVI   0(R5),C'7'          SET FONT INDEX
PR9700R  LA    R0,132              LOAD LINE LENGTH
         $PRPUT DATA=(R5),LEN=(R0),WAIT=YES,COUNT=1 WRITE HEADING LINE
         MVI   0(R5),C' '          BLANK THE ENTIRE
         MVC   1(132,R5),0(R5)         BUFFER WORK AREA
         MVI   0(R5),C'8'          SET FONT INDEX
PR9700S  LA    R0,132              LOAD LINE LENGTH
         $PRPUT DATA=(R5),LEN=(R0),WAIT=YES,COUNT=1 WRITE HEADING LINE
         MVI   0(R5),C' '          BLANK THE ENTIRE
         MVC   1(132,R5),0(R5)         BUFFER WORK AREA
         MVI   0(R5),C'6'          SET FONT INDEX
PR9700T  LA    R0,132              LOAD LINE LENGTH
         $PRPUT DATA=(R5),LEN=(R0),WAIT=YES,COUNT=4 WRITE HEADING LINE
         SPACE 3
***********************************************************************
*                                                                     *
*        BUILD THE DETAILED STATISTICS LINES WHICH FOLLOW THE         *
*        HEADING LINES.                                               *
*                                                                     *
***********************************************************************
         $CALL STATLNE             GO BUILD STATISTICS LINES
         LA    R6,STAT1            POINT TO FIRST STATISTICS LINE
         LA    R7,10               SET LOOP COUNTER
PR9700Z  MVI   0(R5),C'6'          SET FONT INDEX
         MVC   1(132,R5),0(R6)     MOVE STAT LINE TO MSG AREA
         LA    R0,133              SET LENGTH OF RECORD
         $PRPUT DATA=(R5),LEN=(R0),COUNT=1,WAIT=YES
         LA    R6,132(R6)          INCREMENT TO NEXT STATISTICS LINE
         BCT   R7,PR9700Z          LOOP UNTIL ALL 10 ARE PRINTED
         B     CALLPRNT            DONE WITH PAGE, RETURN TO CALLER
         EJECT
.XJCF#4 ANOP
***********************************************************************
*                                                                     *
*        PRODUCE SEPARATOR PAGE FOR ALL PRINTERS EXCEPT 9700          *
*                                                                     *
***********************************************************************
NOT9700  TM    DCTPPSW2,DCTNIPRT   SEE IF THIS IS A NON-IMPACT PRINTER
         BO    STARTPRT            YES, BRANCH
         LA    R5,IPLINES(R5)      ADD EXTRA LINES FOR INPACT PRINTER
***********************************************************************
*                                                                     *
*        PRODUCE FROM 1 TO 5 SMALL 5X7 BLOCK LETTER LINES.  THE       *
*        8 STATISTICS LINES WILL ALWAYS BE WRITTEN.  THE BLOCK        *
*        LETTER LINES WILL BE PRODUCED AS THE NUMBER OF HEADING       *
*        LINES ALLOWS.                                                *
*                                                                     *
***********************************************************************
STARTPRT L     R1,BUFWRK           LOAD ADDRESS OF BUFFER WORK AREA
         MVI   0(R1),X'40'         SET BLANK LINE
         LA    R0,1                LOAD LINE LENGTH
         $PRPUT DATA=(R1),LEN=(R0),WAIT=YES,COUNT=1 WRITE 1 BLANK LNE
         SH    R5,=H'1'            SUBTRACT NUMBER OF BLANK LINES
         CH    R5,=H'23'           ROOM FOR 1 LINE PLUS STATS
         BL    BLDSTAT             NO, JUST GO PRINT STATS
BLOCK1   MVI   PPPMSG,C' '         BLANK THE ENTIRE
         MVC   PPPMSG+1(39),PPPMSG     MSG WORK AREA
         MVC   PPPMSG(8),JCTJNAME  MOVE JOB NAME TO MSG AREA
         MVC   PPPMSG+10(4),BINNBR MOVE BIN NUMBER  TO BIN AREA
         MVI   PPPMSG+14,C'-'      SET DELIMETER
         MVC   PPPMSG+15(4),BINNBR+4  MOVE BIN NUMBER  TO BIN AREA
         TM    JITBSTAT,JITBOK     DO WE HAVE A GOOD JITB
         BNO   BLOCK1A             NO, BRANCH
         CLC   JITBPSID,=CL20' '   SEE IF PRIMARY SEP ID SPECIFIED
         BNH   BLOCK1A             NO, USE DEFAULT
         MVC   PPPMSG+10(9),=CL20' ' BLANK BIN AREA
         MVC   PPPMSG+10(4),JITBPSID MOVE PRIM SEP ID1 TO BIN AREA
         MVC   PPPMSG+15(4),JITBPSID+4 MOVE PRIM SEP ID2 TO BIN AREA
         CLC   JITBPSID(4),=CL20' ' SEE IF PRIMARY SEP ID1 SPECIFIED
         BE    BLOCK1A             NO, NO DELIMETER
         CLC   JITBPSID+4(4),=CL20' ' SEE IF PRIMARY SEP ID2 SPECIFIED
         BE    BLOCK1A             NO, NO DELIMETER
         MVI   PPPMSG+14,C'-'      SET DELIMETER
BLOCK1A  $CALL BLK5X7              CALL 5X7 BLOCK ROUTINE
         SH    R5,=H'9'            SUBTRACT LINES FOR BLOCK AND BLANKS
         CH    R5,=H'23'           ROOM FOR ANOTHER LINE PLUS STATS
         BL    BLDSTAT             NO, JUST GO PRINT STATS
BLOCK2   MVI   PPPMSG,C' '         BLANK THE ENTIRE
         MVC   PPPMSG+1(39),PPPMSG     MSG WORK AREA
         L     R6,PPPWKJOE         LOAD ADDRESS OF WORK JOE
         USING JOE,R6              SET ADDRESSABILITY TO JOE
         CLI   JOENAME,C'0'        SEE IF JOE GROUP NAME NUMERIC
         BNL   BLOCK2A             YES, LEAVE BLANK
         MVC   PPPMSG(8),JOENAME   MOVE JOE OUTPUT NAME TO MSG AREA
         DROP  R6                  DROP ADDRESSABILITY TO JOE
BLOCK2A  $CALL BLK5X7              CALL 5X7 BLOCK ROUTINE
         SH    R5,=H'9'            SUBTRACT LINES FOR BLOCK AND BLANKS
         CH    R5,=H'23'           ROOM FOR ANOTHER LINE PLUS STATS
         BL    BLDSTAT             NO, JUST GO PRINT STATS
BLOCK3   MVI   PPPMSG,C' '         BLANK THE ENTIRE
         MVC   PPPMSG+1(39),PPPMSG     MSG WORK AREA
         MVC   PPPMSG(20),JCTPNAME MOVE PGMR NAME TO MSG AREA
         $CALL BLK5X7              CALL 5X7 BLOCK ROUTINE
         SH    R5,=H'9'            SUBTRACT LINES FOR BLOCK AND BLANKS
         CH    R5,=H'23'           ROOM FOR ANOTHER LINE PLUS STATS
         BL    BLDSTAT             NO, JUST GO PRINT STATS
BLOCK4   MVI   PPPMSG,C' '         BLANK THE ENTIRE
         MVC   PPPMSG+1(39),PPPMSG     MSG WORK AREA
         TM    JITBSTAT,JITBOK     DO WE HAVE A GOOD JITB
         BNO   BLOCK4A             NO, BRANCH
         CLI   JITBPGMR,C' '       SEE IF PROGRAMMER NAME SPECIFIED
         BNH   BLOCK4A             NO, USE BLANKS
         MVC   PPPMSG(20),JITBPGMR MOVE PROGRAMMER NAME TO MSG AREA
BLOCK4A  $CALL BLK5X7              CALL 5X7 BLOCK ROUTINE
         SH    R5,=H'9'            SUBTRACT LINES FOR BLOCK AND BLANKS
         CH    R5,=H'23'           ROOM FOR ANOTHER LINE PLUS STATS
         BL    BLDSTAT             NO, JUST GO PRINT STATS
BLOCK5   MVI   PPPMSG,C' '         BLANK THE ENTIRE
         MVC   PPPMSG+1(39),PPPMSG     MSG WORK AREA
         MVC   PPPMSG(8),JCTJOBID  MOVE JOBID TO MSG AREA
BLOCK5A  CLI   PPPMSG+4,C' '       SEE IF LEFT JUSTIFIED
         BNE   BLOCK5B             YES, GET OUT
         MVC   PPPMSG+4(4),PPPMSG+5 SHIFT JOB NUMBER
         B     BLOCK5A             LOOP
BLOCK5B  DS    0H
         TM    JITBSTAT,JITBOK     DO WE HAVE A GOOD JITB
         BNO   BLOCK5C             NO, BRANCH
         CLC   JITBSSID,=CL20' '   SEE IF SECOND SEP ID1 SPECIFIED
         BNH   BLOCK5C             NO, LEAVE IT BLANK
         MVC   PPPMSG+10(4),JITBSSID MOVE SECOND SEP ID1 TO BIN AREA
         MVC   PPPMSG+15(4),JITBSSID+4 MOVE SECOND SEP ID2 TO BIN AREA
         CLC   JITBSSID(4),=CL20' ' SEE IF SECOND SEP ID1 SPECIFIED
         BE    BLOCK5C             NO, NO DELIMETER
         CLC   JITBSSID+4(4),=CL20' ' SEE IF SECOND SEP ID2 SPECIFIED
         BE    BLOCK5C             NO, NO DELIMETER
         MVI   PPPMSG+14,C'-'      SET DELIMETER
BLOCK5C  $CALL BLK5X7              CALL 5X7 BLOCK ROUTINE
         SH    R5,=H'9'            SUBTRACT LINES FOR BLOCK AND BLANKS
         SPACE 3
***********************************************************************
*                                                                     *
*        BUILD THE DETAILED STATISTICS LINES WHICH FOLLOW THE         *
*        BLOCK LETTERS.                                               *
*                                                                     *
***********************************************************************
BLDSTAT  L     R1,BUFWRK           LOAD ADDRESS OF BUFFER WORK AREA
         MVI   0(R1),X'40'         SET BLANK LINE
         LA    R0,1                LOAD LINE LENGTH
         $PRPUT DATA=(R1),LEN=(R0),WAIT=YES,COUNT=4 WRITE 4 BLANK LNES
         SH    R5,=H'4'            SUBTRACT NUMBER OF BLANK LINES
         $CALL STATLNE             GO BUILD STATISTICS LINES
         LA    R6,STAT1            POINT TO FIRST STATISTICS LINE
         LA    R7,10               SET LOOP COUNTER
BLDSTAT1 LA    R0,132              SET LENGTH OF RECORD
         LR    R1,R6               LOAD ADDRESS OF STATISTICS LINE
         $PRPUT DATA=(R1),LEN=(R0),COUNT=1,WAIT=YES
         LA    R6,132(R6)          INCREMENT TO NEXT STATISTICS LINE
         BCT   R7,BLDSTAT1         LOOP UNTIL ALL 10 ARE PRINTED
         SH    R5,=H'10'           SUBTRACT LINES FOR STATISTICS
         LTR   R5,R5               TEST REMAIN LINE COUNT IN REG 5
         BNP   CALLPRNT            NOT GREATER THAN ZERO, DONE
         L     R1,BUFWRK           LOAD ADDRESS OF BUFFER WORK AREA
         MVI   0(R1),X'40'         SET BLANK LINE
         LA    R0,1                LOAD LINE LENGTH
         $PRPUT DATA=(R1),LEN=(R0),WAIT=YES,COUNT=1 WRITE 1 BLANK LNES
         SH    R5,=H'1'            SUBTRACT NUMBER OF BLANK LINES
         LTR   R5,R5               TEST REMAIN LINE COUNT IN REG 5
         BNP   CALLPRNT            NOT GREATER THAN ZERO, DONE
***********************************************************************
*                                                                     *
*        CALL SEPLINE ROUTINE TO BUILD THE SINGLE SEPARATOR LINE      *
*        WHICH WILL BE PRODUCED MULTIPLE TIMES DEPENDING ON THE       *
*        SPECIFIED NUMBER OF LINES ON THE SEPARATOR PAGE FOR THE      *
*        DEVICE.                                                      *
*                                                                     *
***********************************************************************
BLDSEPLN $CALL SEPLINE             GO BUILD SINGLE SEPARATOR LINE
         EJECT
***********************************************************************
*                                                                     *
*        PRODUCE THE SINGLE LINE WHICH WAS JUST CREATED AS MANY       *
*        TIMES AS REQUIRED TO FILL THE REST OF THE PAGE AS DEFINED    *
*        BY JES2 INITIALIZATION PARAMETERS.  THE NUMBER OF LINES      *
*        LEFT TO PRINT IS IN REGISTER R5.                             *
*                                                                     *
***********************************************************************
         LTR   R5,R5               TEST REMAIN LINE COUNT IN REG 5
         BNP   CALLPRNT            NOT GREATER THAN ZERO, DONE
SEPLP    SLR   R0,R0               CLEAR REG 0 FOR IC
         IC    R0,HDR              LOAD LINE LENGTH
         LA    R1,HDR+1            LOAD ADDRESS OF TRUE LINE
         $PRPUT DATA=(R1),LEN=(R0),COUNT=1,WAIT=YES
         BCT   R5,SEPLP            LOOP UNTIL DONE
         B     CALLPRNT            DONE WITH PAGE, RETURN TO CALLER
         EJECT
***********************************************************************
*                                                                     *
*                        P U N C H                                    *
*                                                                     *
***********************************************************************
         SPACE 3
***********************************************************************
*                                                                     *
*        PRODUCE THE SETUP HEADER FOR A SNA REMOTE IF REQUIRED.       *
*                                                                     *
***********************************************************************
PUNCH    TM    MDCTFEAT,DCTPSHDR   SEE IF SETUP HDR REQUIRED
         BNO   PUNCH1              NO, BYPASS $SEPPDIR ROUTINE
         $SEPPDIR (R4)             SEND PDIR TO SNA RMT
PUNCH1   MVC   EXITWRK3(4),JCTROOMN SET ROOM NUMBER
         MVC   EXITWRK3+4(4),JCTJOBID+4 AND JOB NUMBER
         EJECT
***********************************************************************
*                                                                     *
*        CONVERT EACH OF THE EIGHT CHARACTERS IN PEXITWRK3 TO         *
*        A STRING OF 8 CHARACTERS WHICH WILL PRINT AS A 12-11-X       *
*        PUNCH.  EACH CHARACTER WILL BE PRECEDED BY A NULL            *
*        12-11 PUNCH TO DIVIDE THE CHARACTERS.                        *
*                                                                     *
*        NOTE THAT THAT THE ZONE PORTION OF ALPHA CHARACTERS WILL     *
*        BE LOST LEAVING ONLY THE NUMERIC PORTION ALONG WITH THE      *
*        12-11 ZONE PUNCH.  ALSO NOTE THAT ALL CHARACTERS LESS        *
*        AN "A" WILL PRINT AS A NULL 12-11 PUNCH.                     *
*                                                                     *
***********************************************************************
         LA    R6,80               SET LENGTH TO 80
         STC   R6,HDRLEN           AND SAVE IT IN THE BUFFER
         LA    R6,HDR+1            LOAD START OF PUNCH LINE IN BUFFER
         SLR   R5,R5               ZERO INDEX POINTER
PUNCH2   MVI   0(R6),X'6A'         START THE FIELD WITH 12-11 PNCH
         IC    R0,EXITWRK3(R5)     LOAD NEXT CHARACTER
         STC   R0,1(,R6)           MOVE CHARACTER TO BUFFER
         OI    1(R6),X'30'         MASK LETTERS TO NUMERIC
         CLI   1(R6),X'F0'         SEE IF NOW ZERO
         BE    PUNCH3              YES, GO SET TO X'E0'
         BH    PUNCH4              GT ZERO, GO MASK TO 12-11-X
         MVI   1(R6),X'EA'         LT, SET TO X'EA'
PUNCH3   XI    1(R6),X'E0'         MASK OFF X'E0'
PUNCH4   XI    1(R6),X'60'         MASK RESULTING CHARACTER TO 12-11-X
         MVC   2(7,R6),1(R6)       PROPAGATE CHARACTER 7 MORE TIMES
         MVI   9(R6),X'6A'         END FIELD WITH 12-11 PUNCH
         LA    R6,10(,R6)          INCREMENT TO NEXT OUTPUT AREA
         LA    R5,1(,R5)           INCREMENT TO NEXT INPUT CHARACTER
         CH    R5,=H'8'            SEE IF END OF INPUT
         BL    PUNCH2              NO, LOOP
***********************************************************************
*                                                                     *
*        PRODUCE PUNCH LACE CARD                                      *
*                                                                     *
***********************************************************************
         SLR   R0,R0               CLEAR REG 0 FOR IC
         IC    R0,HDR              LOAD LINE LENGTH
         LA    R1,HDR+1            LOAD ADDRESS OF TRUE LINE
         $PRPUT DATA=(R1),LEN=(R0),COUNT=1,WAIT=YES
***********************************************************************
*                                                                     *
*        PRODUCE BLANK SEPARATOR CARD IF NOT A 3525                   *
*                                                                     *
***********************************************************************
         CLI   PPPDEVTP+3,X'0C'    IS THIS A 3525
         BE    PUNCH6              YES, DO NOT PUNCH A BLANK CARD
         MVC   HDR(2),=X'0140'     MOVE BLANK CARD INTO BUFFER
         LA    R0,2                LOAD LINE LENGTH
         LA    R1,HDR              LOAD ADDRESS OF BLANK CARD
         $PRPUT DATA=(R1),LEN=(R0),COUNT=1,WAIT=YES,CC=M
PUNCH6   DS    0H
         AIF   ('&UCC7' NE 'YES').UCC7#2
***********************************************************************
*                                                                     *
*        CALL UCC7 TASK TO DEMAND A JOB NETWORK IF THE JITB           *
*        THAT HAS BEEN READ FOR THE JOB REQUESTS THIS ACTION.         *
*                                                                     *
***********************************************************************
         TM    JITBSTAT,JITBOK     DO WE HAVE A GOOD JITB
         BNO   EXIT                NO, GET OUT
         CLI   JITBU7FL,C'Y'       SEE IF UCC7 NETWORK REQUESTED
         BNE   EXIT                NO, GET OUT
         $CALL CALLUC7             CALL UCC7 TASK
.UCC7#2  ANOP
         B     EXIT                DONE, GET OUT
         EJECT
***********************************************************************
*                                                                     *
*                      K O M S T A R                                  *
*                                                                     *
***********************************************************************
         SPACE 3
***********************************************************************
*                                                                     *
*        PRODUCE THE PROPER NATIVE MODE COMMANDS FOR THE KODAK        *
*        KOMSTAR UNIT IF REQUIRED FOR THE SYSOUT CLASS BEING          *
*        PROCESSED.                                                   *
*                                                                     *
***********************************************************************
KOMSTAR  L     R6,PPPWKJOE         LOAD ADDRESS OF WORK JOE
         USING JOE,R6              SET ADDRESSABILITY TO JOE
         CLI   JOECURCL,C'Q'       PREFORMATTED KOM80 CLASS
         BE    EXIT                DO NOTHING
         DROP  R6                  DROP ADDRESSABILITY TO JOE
         CLC   REASON,=H'8'        TEST ENTRY REASON CODE
         BE    KOMEND              EIGHT, END, BRANCH
         B     KOMBEG              START OR CONT, BRANCH
         SPACE 3
***********************************************************************
*                                                                     *
*        FORCE FICHE EJECT AT END OF PROCESSING                       *
*                                                                     *
***********************************************************************
KOMEND   MVC   HDR(LKOMRCD5),KOMRCD5 MOVE EJECT SEQUENCE TO      BUFFER
         LA    R0,LKOMRCD5         LOAD LINE LENGTH
         LA    R1,HDR              LOAD ADDRESS OF EJECT COMMAND
         $PRPUT DATA=(R1),LEN=(R0),COUNT=1,WAIT=YES,CC=M
         AIF   ('&UCC7' NE 'YES').UCC7#3
         SPACE 3
***********************************************************************
*                                                                     *
*        CALL UCC7 TASK TO DEMAND A JOB NETWORK IF THE JITB           *
*        THAT HAS BEEN READ FOR THE JOB REQUESTS THIS ACTION.         *
*                                                                     *
***********************************************************************
         TM    JITBSTAT,JITBOK     DO WE HAVE A GOOD JITB
         BNO   EXIT                NO, GET OUT
         CLI   JITBU7FL,C'Y'       SEE IF UCC7 NETWORK REQUESTED
         BNE   EXIT                NO, GET OUT
         $CALL CALLUC7             CALL UCC7 TASK
.UCC7#3  ANOP
         B     EXIT                DONE, GET OUT
         EJECT
***********************************************************************
*                                                                     *
*        SET FCB, FICHE FORMAT AND TITLES AT START OF PROCESSING      *
*                                                                     *
***********************************************************************
***********************************************************************
*        WRITE KOMSTAR TITLE RECORD                                   *
***********************************************************************
KOMBEG   MVC   HDR(LKOMRCD0),KOMRCD0 MOVE TITLE RECORD TO BUFFER
         MVC   JNAME,JCTJNAME      MOVE JOBNAME TO BUFFER
         MVC   JOBID(1),JCTJOBID   SET JOB ID CHARACTER
         MVC   JOBID+1(4),JCTJOBID+4 SET JOB ID NUMBER
         LA    R0,LKOMRCD0         LOAD LINE LENGTH
         LA    R1,HDR              LOAD ADDRESS OF TITLE RECORD
         $PRPUT DATA=(R1),LEN=(R0),COUNT=1,WAIT=YES,CC=M
         L     R6,PPPWKJOE         LOAD ADDRESS OF WORK JOE
         USING JOE,R6              SET ADDRESSABILITY TO JOE
         CLI   JOECURCL,C'G'       PREFORMATTED PME CLASS
         BE    EXIT                YES, BYPASS OUR HEADING ROUTINES
         DROP  R6                  DROP ADDRESSABILITY TO JOE
***********************************************************************
*        WRITE KOMSTAR SETUP RECORD 1                                 *
***********************************************************************
KR1      MVC   HDR(LKOMRCD1),KOMRCD1 MOVE 1ST SETUP TO BUFFER
         AIF   ('&UJQE' NE 'YES').UJQE#2
         L     R7,JCTJQE           LOAD OFFSET OF JQE
         AL    R7,$JOBQPTR         CALCULATE ADDRESS OF JQE
         USING JQE,R7              SET ADDRESSABILITY TO JQE
         CLI   JQEXFLG1,JQEXPROD   SEE IF PRODUCTION JOB
         BNE   KR1A                NO, LEAVE THE REVERSE IMAGE ALONE
         MVI   RIMAGE,C' '         YES, TURN OFF REVERSE IMAGE
         DROP  R7                  DROP ADDRESSABILITY TO JQE
.UJQE#2  ANOP
KR1A     LA    R0,LKOMRCD1         LOAD LINE LENGTH
         LA    R1,HDR              LOAD ADDRESS OF SETUP COMMAND
         $PRPUT DATA=(R1),LEN=(R0),COUNT=1,WAIT=YES,CC=M
***********************************************************************
*        WRITE KOMSTAR SETUP RECORD 2                                 *
***********************************************************************
KR2      MVC   HDR(LKOMRCD2),KOMRCD2 MOVE 2ND SETUP TO BUFFER
         MVC   KJOBNM,JCTJNAME     MOVE JOBNAME TO BUFFER
         MVC   KJOBID(1),JCTJOBID  SET JOB ID CHARACTER
         MVC   KJOBID+1(4),JCTJOBID+4 SET JOB ID NUMBER
         L     R6,PPPWKJOE         LOAD ADDRESS OF WORK JOE
         USING JOE,R6              SET ADDRESSABILITY TO JOE
         CLI   JOENAME,C'0'        SEE IF DEFAULT NAME
         BNL   NOKGRP              YES, LEAVE IT ALONE
         MVC   KGROUPID,JOENAME    SET OUTPUT GROUP NAME
         DROP  R6                  DROP ADDRESSABILITY TO JOE
NOKGRP   LA    R0,LKOMRCD2         LOAD LINE LENGTH
         LA    R1,HDR              LOAD ADDRESS OF SETUP COMMAND
         $PRPUT DATA=(R1),LEN=(R0),COUNT=1,WAIT=YES,CC=M
***********************************************************************
*        WRITE KOMSTAR SETUP RECORD 3                                 *
***********************************************************************
KR3      MVC   HDR(LKOMRCD3),KOMRCD3 MOVE 3ND SETUP TO BUFFER
         AIF   ('&UJQE' NE 'YES').UJQE#3
         L     R7,JCTJQE           LOAD OFFSET OF JQE
         AL    R7,$JOBQPTR         CALCULATE ADDRESS OF JQE
         USING JQE,R7              SET ADDRESSABILITY TO JQE
         CLI   JQEXOWNR,C' '       SEE IF SPECIFIED
         BNH   KR3A                NO, BRANCH
         MVC   KOWNER,JQEXOWNR     MOVE OWNERID TO RECORD
         CLI   JQEXFLG1,JQEXPROD   SEE IF PRODUCTION JOB
         BNE   KR3A                NO, LEAVE OWNERID
         MVC   KOWNER,=CL20' '     BLANK OWNERID
         DROP  R7                  DROP ADDRESSABILITY TO JQE
.UJQE#3  ANOP
KR3A     CLC   JCTPNAME,=CL20' '   SEE IF PGMR NAME SPECIFIED
         BNE   KR3C                YES, BRANCH
         CLI   JCTJOBID,C'J'       IS THIS A BATCH JOB?
         BE    KR3C                YES, BRANCH
         CLI   JCTJOBID,C'T'       IS THIS A TSO USER?
         BE    KR3B                YES, BRANCH
         MVC   JCTPNAME,=CL20'TECH SUP' SET PGMR NAME TO 'TECH SUP'
         B     KR3C                BRANCH
KR3B     MVC   JCTPNAME,=CL20'TSO USER' SET PGMR NAME TO 'TSO USER'
KR3C     MVC   KPNAME1,JCTPNAME    MOVE PROGRAMMER NAME PART1 TO BUFFER
         MVC   KPNAME2,JCTPNAME+10 MOVE PROGRAMMER NAME PART2 TO BUFFER
         TM    JITBSTAT,JITBOK     DO WE HAVE A GOOD JITB
         BNO   KR3D                NO, BRANCH
         MVC   KONAME1,JITBPGMR    MOVE OWNER NAME PART1 TO BUFFER
         MVC   KONAME2,JITBPGMR+10 MOVE OWNER NAME PART2 TO BUFFER
KR3D     LA    R0,LKOMRCD3         LOAD LINE LENGTH
         LA    R1,HDR              LOAD ADDRESS OF SETUP COMMAND
         $PRPUT DATA=(R1),LEN=(R0),COUNT=1,WAIT=YES,CC=M
***********************************************************************
*        WRITE KOMSTAR SETUP RECORD 4                                 *
***********************************************************************
KR4      MVC   HDR(LKOMRCD4),KOMRCD4 MOVE 4TH SETUP TO BUFFER
         MVC   KPBIN(4),BINNBR     SET FIRST PART OF DFLT BIN NUMBER
         MVI   KPBIN+4,C'-'        SET BIN NUMBER SEPARATOR
         MVC   KPBIN+5(4),BINNBR+4 SET SECOND PART OF DFLT BIN NUMBER
         TM    JITBSTAT,JITBOK     DO WE HAVE A GOOD JITB
         BNO   KR4B                NO, BRANCH
         CLC   JITBPSID,=CL20' '   SEE IF PRIM SEP ID1 SPECIFIED
         BNH   KR4A                NO, LEAVE IT ALONE
         MVC   KPBIN(4),JITBPSID   MOVE PRIM SEP ID1 TO BIN AREA
         MVC   KPBIN+5(4),JITBPSID+4 MOVE PRIM SEP ID1 TO BIN AREA
         CLC   JITBPSID(4),=CL20' ' SEE IF PRIM SEP ID1 SPECIFIED
         BE    KR4A                NO, NO DELIMETER
         CLC   JITBPSID+4(4),=CL20' ' SEE IF PRIM SEP ID2 SPECIFIED
         BE    KR4A                NO, NO DELIMETER
         MVI   KPBIN+4,C'-'        SET DELIMETER
KR4A     CLC   JITBSSID,=CL20' '   SEE IF SECOND SEP ID1 SPECIFIED
         BNH   KR4B                NO, LEAVE IT ALONE
         MVC   KSBIN(4),JITBSSID   MOVE SECOND SEP ID1 TO BIN AREA
         MVC   KSBIN+5(4),JITBSSID+4 MOVE SECOND SEP ID1 TO BIN AREA
         CLC   JITBSSID(4),=CL20' ' SEE IF SECOND SEP ID1 SPECIFIED
         BE    KR4B                NO, NO DELIMETER
         CLC   JITBSSID+4(4),=CL20' ' SEE IF SECOND SEP ID2 SPECIFIED
         BE    KR4B                NO, NO DELIMETER
         MVI   KSBIN+4,C'-'        SET DELIMETER
KR4B     MVC   KTIME1,TOD          MOVE TIME (HH.MM.SS) TO BUFFER
         OI    KTIME1,X'F0'        ELIMINATE POSSIBLE LEADING BLANK
         MVC   KTIME2,TOD+9        MOVE TIME (AM/PM)    TO BUFFER
         MVC   KDATE,CDATE         MOVE DATE (MM/DD/YY) TO BUFFER
         LA    R0,LKOMRCD4         LOAD LINE LENGTH
         LA    R1,HDR              LOAD ADDRESS OF SETUP COMMAND
         $PRPUT DATA=(R1),LEN=(R0),COUNT=1,WAIT=YES,CC=M
         B     EXIT                DONE, GET OUT
         EJECT
***********************************************************************
*                                                                     *
*            D A T A G R A P H I X     A R I S   /   X L              *
*                                                                     *
***********************************************************************
         SPACE 3
***********************************************************************
*                                                                     *
*        DETERMINE THE PROPER DATAGRAPHIX "JOB" ID AND SEND THE       *
*        COMMAND TO THE UNIT.  IF JES2 INFORMATION IS REQUIRED        *
*        BY THE "JOB" TO DETERMINE THE HEADINGS, A STANDARD           *
*        HEADING LINE WILL ALSO BE SENT.                              *
*                                                                     *
*        THE DATAGRAPHIX "JOB" ID WILL BE DETERMINED FROM THE         *
*        WRITER NAME.  IF THERE IS NO NAME SPECIFIED, THE             *
*        DEFAULT NAME OF 'STD' SILL BE USED.                          *
*                                                                     *
*        TITLE RECORDS WILL BE SENT TO THE DEVICE AFTER THE           *
*        JOBID IS SENT.  THE DEFAULT DATAGRAPHIX JOB OR EVEN          *
*        THE SPECIFIED JOB CAN OPTIONALLY EXTRACT JOB RELATED         *
*        INFORMATION FROM THESE RECORDS.                              *
*                                                                     *
***********************************************************************
DX       CLC   REASON,=H'8'        TEST ENTRY REASON CODE
         BE    DXEND               EIGHT, END, EXIT
         MVC   HDR(LDXR1),DXR1     MOVE DATAGRAPHIX START JOB TO BUF
         L     R6,PPPCHJOE         LOAD ADDRESS CHARACTERISTICS JOE
         USING JOE,R6              SET ADDRESSABILITY TO JOE
         MVC   HDR+14(8),JOEWTRID  SET DX JOB ID FROM WRITERID
         CLI   JOEWTRID,C'A'       SEE IF WRITERID SPECIFIED
         BNL   DX1                 YES, BRANCH
         MVC   HDR+14(6),=C'STD   ' SET DEFAULT JOB NAME
         DROP  R6                  DROP ADDRESSABILITY TO JOE
DX1      LA    R0,LDXR1            LOAD LINE LENGTH
         LA    R1,HDR              LOAD ADDRESS OF DX CONTROL RECD
         $PRPUT DATA=(R1),LEN=(R0),COUNT=1,WAIT=YES,CC=M
         SPACE 3
***********************************************************************
*        BUILD THE FIRST TITLE RECORD FOR THE DATAGRAPHIX DEVICE.     *
***********************************************************************
         MVI   TREC1,C' '          CLEAR ENTIRE
         MVC   TREC1+1(132),TREC1        BUFFER
         MVC   TRECID1,=CL5'TREC1' SET RECORD ID
         MVC   TRJOBNM,JCTJNAME    SET JOB NAME
         MVC   TRJOBID,JCTJOBID    SET JOB NUMBER
         MVC   TRPNAME,JCTPNAME    SET PROGRAMMER NAME
         MVC   TRPBIN(8),BINNBR    SET PRIMARY BIN NUMBER
         TM    JITBSTAT,JITBOK     SEE IF JITB READ SUCCESSFULLY
         BNO   DX2                 NO, BRANCH
         CLC   JITBPSID,=CL20' '   SEE IF PRIMARY SEPID SPECIFIED
         BE    DX2                 NO, LEAVE THE DEFAULT
         MVC   TRPBIN,=CL20' '     BLANK THE BIN NUMBER
         MVC   TRPBIN(4),JITBPSID  SET PRIMARY BIN NUMBER PART 1
         MVC   TRPBIN+5(4),JITBPSID+4   SET PRIMARY BIN NUMBER PART 2
         CLC   JITBPSID(4),=CL20' ' SEE IF FIRST PART EXISTS
         BE    DX2                 NO, NO DELIMETER
         CLC   JITBPSID+4(4),=CL20' ' SEE IF SECOND PART EXISTS
         BE    DX2                 NO, NO DELIMETER
         MVI   TRPBIN+4,C'-'       SET DELIMETER
DX2      L     R6,PPPWKJOE         LOAD ADDRESS OF WORK JOE
         USING JOE,R6              SET ADDRESSABILITY TO JOE
         CLI   JOENAME,C'0'        SEE IF DEFAULT NAME
         BNL   DX3                 YES, LEAVE IT ALONE
         MVC   TRGROUP,JOENAME     SET OUTPUT GROUP NAME
         DROP  R6                  DROP ADDRESSABILITY TO JOE
DX3      MVC   TRCDATE,CDATE       SET CALENDAR DATE (MM/DD/YY)
         MVC   TRJDATE,JDATE       SET JULIAN DATE   (YY.DDD)
         MVC   TRTIME,TOD          SET TIME
         OI    TRTIME,X'F0'        ELIMINATE POSSIBLE LEADING BLANK
***********************************************************************
*        WRITE THE FIRST TITLE RECORD FOR THE DATAGRAPHIX DEVICE.     *
***********************************************************************
         MVI   TRECCTL1,X'C9'      SET DX CHANNEL COMMAND CODE
         MVI   TRECCTL1+1,X'15'    SET CONTROL RECORD ID
         LA    R0,133              LOAD LINE LENGTH
         LA    R1,TREC1            LOAD ADDRESS OF DX CONTROL RECD
         $PRPUT DATA=(R1),LEN=(R0),COUNT=1,WAIT=YES,CC=M
         SPACE 3
         AIF   ('&UJQE' NE 'YES' AND '&JIT' NE 'YES').JIT#1
***********************************************************************
*        BUILD THE SECOND TITLE RECORD FOR THE DATAGRAPHIX DEVICE.    *
***********************************************************************
         MVI   TREC2,C' '          CLEAR ENTIRE
         MVC   TREC2+1(132),TREC2        BUFFER
         MVC   TRECID2,=CL5'TREC2' SET RECORD ID
         AIF   ('&UJQE' NE 'YES').UJQE#4
         L     R7,JCTJQE           LOAD OFFSET OF JQE
         AL    R7,$JOBQPTR         CALCULATE ADDRESS OF JQE
         USING JQE,R7              SET ADDRESSABILITY TO JQE
         MVC   TRTPIND,JQEXFLG1    SAVE TEST/PRODUCTION INDICATOR
         MVC   TROWNER,JQEXOWNR    SET OWNERID
         DROP  R7                  DROP ADDRESSABILITY TO JQE
.UJQE#4  ANOP
         TM    JITBSTAT,JITBOK     SEE IF JITB READ SUCCESSFULLY
         BNO   DX5                 NO, BRANCH
         CLC   JITBSSID,=CL8' '    SEE IF SECONDARY SEPID SPECIFIED
         BE    DX4                 NO, LEAVE IT BLANK
         MVC   TRSBIN(4),JITBSSID  SET SECONDARY SEPARATOR ID PART 1
         MVC   TRSBIN+5(4),JITBSSID+4 SET SECOND SEPARATOR ID PART 2
         CLC   JITBSSID(4),=CL20' ' SEE IF FIRST PART EXISTS
         BE    DX4                 NO, NO DELIMETER
         CLC   JITBSSID+4(4),=CL20' ' SEE IF SECOND PART EXISTS
         BE    DX4                 NO, NO DELIMETER
         MVI   TRSBIN+4,C'-'       SET DELIMETER
DX4      MVC   TROWNAME,JITBPGMR   SET OWNERID NAME
         MVC   TRLOC,JITBLOC       SET LOCATION
         MVC   TRDEPT,JITBDEPT     SET DEPARTMENT
         MVC   TRMAIL,JITBMAIL     SET MAIL CODE
         MVC   TRPHONE,JITBPHON    SET PHONE NUMBER
***********************************************************************
*        WRITE THE SECOND TITLE RECORD FOR THE DATAGRAPHIX DEVICE.    *
***********************************************************************
DX5      MVI   TRECCTL2,X'C9'      SET DX CHANNEL COMMAND CODE
         MVI   TRECCTL2+1,X'15'    SET CONTROL RECORD ID
         LA    R0,133              LOAD LINE LENGTH
         LA    R1,TREC2            LOAD ADDRESS OF DX CONTROL RECD
         $PRPUT DATA=(R1),LEN=(R0),COUNT=1,WAIT=YES,CC=M
.JIT#1   ANOP
         B     EXIT                DONE, EXIT
         EJECT
***********************************************************************
*                                                                     *
*        FORCE FICHE EJECT AT END OF PROCESSING                       *
*                                                                     *
***********************************************************************
DXEND   MVC    HDR(LDXR2),DXR2     MOVE EJOB RECORD TO BUFFER
         LA    R0,LDXR2            LOAD LINE LENGTH
         LA    R1,HDR              LOAD ADDRESS OF EJOB RECORD
         $PRPUT DATA=(R1),LEN=(R0),COUNT=1,WAIT=YES,CC=M
         AIF   ('&UCC7' NE 'YES').UCC7#4
         SPACE 3
***********************************************************************
*                                                                     *
*        CALL UCC7 TASK TO DEMAND A JOB NETWORK IF THE JITB           *
*        THAT HAS BEEN READ FOR THE JOB REQUESTS THIS ACTION.         *
*                                                                     *
***********************************************************************
         TM    JITBSTAT,JITBOK     DO WE HAVE A GOOD JITB
         BNO   EXIT                NO, GET OUT
         CLI   JITBU7FL,C'Y'       SEE IF UCC7 NETWORK REQUESTED
         BNE   EXIT                NO, GET OUT
         $CALL CALLUC7             CALL UCC7 TASK
.UCC7#4  ANOP
         B     EXIT                DONE, GET OUT
         EJECT
***********************************************************************
*                                                                     *
*                           E X I T                                   *
*                                                                     *
*             RETURN CODE OF 8 TELLS JES2 TO PRODUCE ANY USER         *
*             SEPARATOR THAT HAS BEEN CREATED AND TO SUPPRESS         *
*             THE STANDARD SEPARATOR.                                 *
*                                                                     *
***********************************************************************
EXIT     $FREEBUF (R4)             FREE FIRST WORK BUFFER
         $FREEBUF (R8)             FREE SECOND WORK BUFFER
         LA    R15,8               SET RETURN CODE TO 8
         $RETURN RC=(R15)          RETURN TO JES2
TRTX     EQU   *-240
         DC    C'0123456789ABCDEF'
         EJECT
***********************************************************************
*                                                                     *
*        CONSTANTS USED BY THIS JES2 EXIT                             *
*                                                                     *
***********************************************************************
YEARTBL  DC    AL1(31),C'JAN',AL1(28),C'FEB'   JULIAN DATE
         DC    AL1(31),C'MAR',AL1(30),C'APR'       TO
         DC    AL1(31),C'MAY',AL1(30),C'JUN'  DAY AND MONTH
         DC    AL1(31),C'JUL',AL1(31),C'AUG'    CONVERSION
         DC    AL1(30),C'SEP',AL1(31),C'OCT'      TABLE
         DC    AL1(30),C'NOV',AL1(99),C'DEC'
         SPACE 3
JNAME    EQU   HDR+6,8                      JOBNAME
JOBID    EQU   HDR+14,5                     JOBID
KOMRCD0  DS    0H
         DC    X'01'
         DC    X'4163FF'
         DC    CL20'W1'                     TITLE INFORMATION RECORD
         DC    X'FC10'                      CONTINUE
LKOMRCD0 EQU   *-KOMRCD0
RIMAGE   EQU   HDR+93,1                     REVERSE IMAGE IND
KOMRCD1  DS    0H
         DC    X'01'
         DC    X'4163FF'
         DC    CL20'11013335162728141116'   SYSTEM
         DC    CL20'20101020303090417'      FCB PART 1
         DC    CL20'20525063307410849'      FCB PART 2
         DC    CL20'21055115612600964'      FCB PART 3
         DC    CL20'H10001  YY'             SEQUENCE NBR & REV IMAGE
         DC    CL20'G101133B'               GRID
         DC    X'FC10'                      CONTINUE
LKOMRCD1 EQU   *-KOMRCD1
KJOBNM   EQU   HDR+54,8                     JOB NAME
KJOBID   EQU   HDR+74,5                     JOB ID
KGROUPID EQU   HDR+94,8                     OUTPUT GROUPID
KOMRCD2  DS    0H
         DC    X'01'
         DC    X'4163FF'
         DC    CL20'AR1160002132'
         DC    CL20'H2 3 Y 64'              MAXIMUM LINES = 64
         DC    CL20'B108014T            '   JOBNAME
         DC    CL20'B205014B            '   JOB NUMBER
         DC    CL20'B308034T1           '   OUTPUT GROUP ID
         DC    X'FC10'                      CONTINUE
LKOMRCD2 EQU   *-KOMRCD2
KOWNER   EQU   HDR+14,10                    OWNER ID
KPNAME1  EQU   HDR+34,10                    PROGRAMMER NAME PART 1
KPNAME2  EQU   HDR+54,9                     PROGRAMMER NAME PART 2
KONAME1  EQU   HDR+74,10                    OWNER NAME PART 1
KONAME2  EQU   HDR+94,9                     OWNER NAME PART 2
KOMRCD3  DS    0H
         DC    X'01'
         DC    X'4163FF'
         DC    CL20'B408034B1           '   ONWER ID
         DC    CL20'B510054T2           '   PROGRAMMER NAME PART 1
         DC    CL20'B609084T            '   PROGRAMMER NAME PART 2
         DC    CL20'B710054B2           '   OWNER NAME PART 1
         DC    CL20'B809084B            '   OWNER NAME PART 2
         DC    X'FC10'                      CONTINUE
LKOMRCD3 EQU   *-KOMRCD3
KPBIN    EQU   HDR+14,9                     PRIMARY BIN NUMBER
KSBIN    EQU   HDR+34,9                     SECONDARY BIN NUMBER
KTIME1   EQU   HDR+54,8                     TIME PART 1
KTIME2   EQU   HDR+74,2                     TIME PART 2
KDATE    EQU   HDR+94,8                     DATE
KOMRCD4  DS    0H
         DC    X'01'
         DC    X'4163FF'
         DC    CL20'B909104T2           '   PRIMARY BIN NUMBER
         DC    CL20'BA09104B2           '   SECONDARY BIN NUMBER
         DC    CL20'BB08134T            '   TIME PART 1
         DC    CL20'BC02154T1           '   TIME PART 2
         DC    CL20'BD08134B            '   DATE
         DC    CL20'BE04164T  S$$$      '   SEQUENCE NUMBER
         DC    X'1040'                      END
LKOMRCD4 EQU   *-KOMRCD4
KOMRCD5  DS    0H
         DC    X'01'
         DC    X'4163FF'
         DC    CL20'11013335162728141116'   SYSTEM
         DC    CL20'20101020303090417'      FCB PART 1
         DC    CL20'20525063307410849'      FCB PART 2
         DC    CL20'21055115612600964'      FCB PART 3
         DC    CL20'ASM0013???'             ELIMINATE BLANK FICHE
         DC    X'1040'                      END
LKOMRCD5 EQU   *-KOMRCD5
DXR1     DS    0H
         DC    X'C9'
         DC    X'0D4040'
         DC    CL10'SJOB LOAD '             START JOB COMMAND
         DC    CL6'       '                 DATAGRAPHIX PROCESS NAME
         DC    CL2'  '
LDXR1 EQU      *-DXR1
DXR2     DS    0H
         DC    X'C9'
         DC    X'0D4040'
         DC    CL10'EJOB'                   END JOB COMMAND
LDXR2 EQU      *-DXR2
         AIF   ('&XJCF' NE 'YES').XJCF#5
DJDECRD  DC    X'01'
         DC    C'$X9700$DJDE ASSIGN=(1,1),;'
LDJDECRD EQU   *-DJDECRD
DJDE1A   DC    X'01'
         DC    C'$X9700$DJDE COPIES=1,SHIFT=YES'
LDJDE1A  EQU   *-DJDE1A
DJDE1B   DC    C',FORMAT=LF148,FONTINDEX=0,DATA=(1,150),FORMS=NONE,MODIX
               FY=NONE,'
LDJDE1B  EQU   *-DJDE1B
DJDE2    DC    X'01'
         DC    C'$X9700$DJDE OTEXT=(''JJJJJJJJ(JOB NNNN)               X
                                                  '
LDJDE2   EQU   *-DJDE2
.XJCF#5 ANOP
         SPACE 3
***********************************************************************
*        ROUTINE CONSTANTS                                            *
***********************************************************************
         LTORG
         EJECT
         TITLE '5 X 7 BLOCK LETTER SUBROUTINE'
***********************************************************************
*                                                                     *
*              5 X 7 BLOCK LETTER ROUTINE                             *
*                                                                     *
*        ON INPUT:                                                    *
*                                                                     *
*        R2     - JITB ADDRESS                                        *
*        R3     - DCT ADDRESS                                         *
*        R4     - BUFFER ADDRESS                                      *
*        R8     - COMMON EXIT WORK AREA                               *
*        R11    - HCT                                                 *
*        R13    - PCE                                                 *
*        R15    - ENTRY POINT ADDRESS                                 *
*        PPPMSG - CHARACTERS TO PRINT                                 *
*                                                                     *
***********************************************************************
         SPACE 1
BLK5X7   $SAVE
         USING BLK5X7,R12          SET ADDRESSABILITY TO ROUTINE
         LR    R12,R15             SET BASE REGISTER
         L     R3,BUFWRK           LOAD ADDRESS OF PRINT BUFFER
         LA    R4,132              LOAD LENGTH OF PRINT LINE
         LA    R5,7                SET LINE CTR TO 7
         LA    R10,BLK5X7T4-65     INITIALIZE TABLE POINTER
         MVC   PPPMSG+20(20),PPPMSG DUPLICATE 19 CHARS
         NC    PPPMSG+20(20),BLK5X7T1   REDUCE TO 64 CHAR SET
         TR    PPPMSG+20(20),BLK5X7T2   CONVERT TO PATRN OFFST
         NC    PPPMSG(20),BLK5X7T1      REDUCE TO 64 CHAR SET
         TR    PPPMSG(20),BLK5X7T3      CONVERT TO DISPLY CHAR
BLK5X7A  LA    R10,64(R10)         INCREMENT TABLE POINTER
         SLR   R6,R6               SET INPUT  CHAR INDEX TO 0
         SLR   R8,R8               SET OUTPUT CHAR INDEX TO 0
         MVI   0(R3),C' '          BLANK THE ENTIRE
         MVC   1(131,R3),0(R3)         OUTPUT LINE
BLK5X7B  IC    R2,PPPMSG(R6)       GET DISPLAY CHAR
         SLR   R15,R15             RESET TABLE INDEX
         IC    R15,PPPMSG+20(R6)   SET CURRENT LETTER TBL POINTER
         IC    R15,0(R10,R15)      TRANSLATE TO BIT PATTERN
         LA    R7,7                SET BIT COUNTER TO 7
         SLL   R15,24+2            PUT IN TOP OF REG
BLK5X7C  SLL   R15,1               PUSH OUT 1 BIT
         LTR   R15,R15             IS NEXT BIT ON?
         BNM   BLK5X7D             NO, GO TO NEXT POSITION
         STC   R2,0(R3,R8)         STORE THE CHAR INTO PRINT LINE
BLK5X7D  LA    R8,1(,R8)           INCREMENT TO NEXT OUTPUT POSITION
         BCT   R7,BLK5X7C          LOOP UNTIL ALL 7 BITS PROCESSED
BLK5X7E  LA    R6,1(,R6)           POINT TO NEXT INPUT CHARACTER
         CH    R6,=H'19'           HAVE WE DONE ALL 19 YET?
         BL    BLK5X7B             NO, LOOP
BLK5X7F  $PRPUT DATA=(R3),LEN=(R4),WAIT=YES   WRITE PRINT LINE
         BCT   R5,BLK5X7A          LOOP UNTIL ALL 7 LINES PROCESSED
         MVI   0(R3),X'40'         MOVE BLANK LINE
         LA    R4,1                SET LENGTH OF LINE
         $PRPUT DATA=(R3),LEN=(R4),WAIT=YES,COUNT=2 WRITE 2 BLANK LNES
         $RETURN                   RETURN TO CALLER
         EJECT
***********************************************************************
*                                                                     *
*                   5 X 7 BLOCK LETTER TABLE                          *
*                                                                     *
***********************************************************************
         SPACE 1
BLK5X7T1 DC    20X'3F'             PUT ALL IN ONE QUADRANT
BLK5X7T2 EQU   *                   XLATE TO PATTERN TBL OFFSET
         DC    AL1(37,01,02,03,04,05,06,07,08,09,48,45,49,50,39,51)
         DC    AL1(41,10,11,12,13,14,15,16,17,18,52,44,46,53,47,54)
         DC    AL1(38,40,19,20,21,22,23,24,25,26,64,55,56,57,58,59)
         DC    AL1(27,28,29,30,31,32,33,34,35,36,60,42,43,61,62,63)
         SPACE 3
BLK5X7T3 EQU   *                   XLATE TO EXPANSION CHAR.
         DC    C' ABCDEFGHI*.*(+I'       ABCDEFGHI.<(+|
         DC    C'&&JKLMNOPQR*$*),-'     &JKLMNOPQR!$*);
         DC    C'-/STUVWXYZX,O-*0'      -/STUVWXYZ,%_>?
         DC    C'0123456789.#@''='''    0123456789:#@'="
         SPACE 3
BLK5X7T4 EQU *                     7 X 64 BIT PATTERN TABLE
         DC    X'041E0E1E1F1F0E110E07111011111F1E0E1E0E1F'  LINE 1 BITS
         DC    X'11111111111F0E040E0E021F0E1F0E0E00000000'
         DC    X'0E0A0E0E00000E040201040E1000001800080E00'
         DC    X'07000A00'
         DC    X'0A11111110101111040212101111111111111104'  LINE 2 BITS
         DC    X'111111111101110C111106101101111100000401'
         DC    X'120A111500110E0F0402040E080000190004110E'
         DC    X'0E000A00'
         DC    X'1111101110101011040214101B19111111111004'  LINE 3 BITS
         DC    X'1111110A0A02130401010A101002111100000402'
         DC    X'121F0114000A00150804040E040000020002010E'
         DC    X'1C1F0A00'
         DC    X'1F1E10111E1E101F040218101515111E111E0E04'  LINE 4 BITS
         DC    X'1111150404041504020E1F1E1E040E0F001F1F04'
         DC    X'0C0A0D0E001F0E141004040E041F0E0400010600'
         DC    X'00000000'
         DC    X'1111101110101711040214101113111015140104'  LINE 5 BITS
         DC    X'11111B0A04081904040102011108110100000408'
         DC    X'0D1F1505000A0E150804040E04010E080002040E'
         DC    X'001F0000'
         DC    X'1111111110101111041212101111111012121104'  LINE 6 BITS
         DC    X'110A111104101104081102111108111100000410'
         DC    X'120A11150E11020F04020400080002130004040E'
         DC    X'00000000'
         DC    X'111E0E1E1F100E110E0C111F11111F100D110E04'  LINE 7 BITS
         DC    X'0E041111041F0E0E1F0E020E0E080E0E00000000'
         DC    X'0D0A0F0E0E000C040201040E10000C031F080400'
         DC    X'00000000'
         SPACE 3
***********************************************************************
*        ROUTINE CONSTANTS                                            *
***********************************************************************
         LTORG
         EJECT
         TITLE 'SEPARATOR RIPPLE LINE SUBROUTINE'
***********************************************************************
*                                                                     *
*        PRODUCE THE SINGLE LINE FOR THE JOB SEPARATOR ROUTINE        *
*        WHICH WILL BE REPRODUCED MULTIPLE TIMES BASED UPON THE       *
*        NUMBER OF LINES SET FOR THE SEPARATOR ROUTINE.               *
*                                                                     *
*        ON INPUT:                                                    *
*                                                                     *
*        R2     - JITB ADDRESS                                        *
*        R3     - DCT ADDRESS                                         *
*        R4     - BUFFER ADDRESS                                      *
*        R8     - COMMON EXIT WORK AREA                               *
*        R11    - HCT                                                 *
*        R13    - PCE                                                 *
*        R15    - ENTRY POINT ADDRESS                                 *
*                                                                     *
***********************************************************************
         SPACE 3
SEPLINE  $SAVE
         USING SEPLINE,R12         SET ADDRESSABILITY TO ROUTINE
         LR    R12,R15             SET BASE REGISTER
         SPACE 3
         EJECT
***********************************************************************
*                                                                     *
*        BUILD THE OUTPUT SINGLE LINE WHICH WILL BE REPEATED ENOUGH   *
*        TIMES TO FILL THE REST OF THE PAGE AS DEFINED BY THE JES2    *
*        INITIALIZATION PARAMTERS FOR &PRIDCT FOR LOCAL PRINTERS OR   *
*        &TPIDCT FOR REMOTE PRINTERS.                                 *
*                                                                     *
***********************************************************************
SEPLINE8 MVI   HDR,C' '            CLEAR ENTIRE
         MVC   HDR+1(132),HDR            BUFFER
         LA    R6,132              LOAD LENGTH OF PRINT LINE
         STC   R6,HDRLEN           AND SAVE IT
***********************************************************************
*        FRAME CHARACTERS                                             *
***********************************************************************
         MVC   HDRFRAME(2),=CL2'* ' SET FRAME CHARACTER
         MVC   HDRFRAMX(1),HDRFRAME SET END FRAME CHARACTER
***********************************************************************
*        SYSOUT CLASS                                                 *
***********************************************************************
         L     R6,PPPWKJOE         LOAD ADDRESS OF WORK JOE
         USING JOE,R6              SET ADDRESSABILITY TO JOE
         MVC   HDRCLASS,JOECURCL   SET SYSOUT CLASS
         MVC   HDRCLASX,JOECURCL       TWICE
***********************************************************************
*        SYSOUT JOE NAME                                              *
***********************************************************************
         MVC   HDRJNAME,JOENAME    SET JOE OUTPUT NAME
***********************************************************************
*        SYSOUT JOE ID                                                *
***********************************************************************
         LH    R1,JOEID1           LOAD 1ST JOEID
         CVD   R1,EXITWRK1         CONVERT TO PACKED
         OI    EXITWRK1+7,X'0F'    SET SIGN
         UNPK  HDRJID1,EXITWRK1+6(2) SET JOEID1
         LH    R1,JOEID2           LOAD 2ND JOEID
         CVD   R1,EXITWRK1         CONVERT TO PACKED
         OI    EXITWRK1+7,X'0F'    SET SIGN
         UNPK  HDRJID2,EXITWRK1+6(2) SET JOEID2
***********************************************************************
*        SYSOUT JOE ROUTE AS DETERMINED BY PREVIOUS ROUTINE           *
***********************************************************************
         MVC   HDRJROUT,DEST       SET JOE ROUTE OR DESTID
         DROP  R6                  DROP ADDRESSABILITY TO JOE
***********************************************************************
*        SEPARATOR FORM TYPE (START/CONT/END)                         *
***********************************************************************
         MVC   HDRFORM,=CL5'START' ASSUME "START" ENTRY
         CLC   REASON,=H'0'        TEST ENTRY REASON CODE
         BE    SEPLINE9            ZERO, START, BRANCH
         MVC   HDRFORM,=CL5'CONT'  ASSUME "CONT " ENTRY
         CLC   REASON,=H'4'        TEST ENTRY REASON CODE
         BE    SEPLINE9            FOUR, CONT, BRANCH
         MVC   HDRFORM,=CL5'END'   MUST BE "END  " ENTRY
SEPLINE9 MVC   HDRFORMX,HDRFORM    SET FORM TYPE TWICE
***********************************************************************
*        SYSOUT JOB NUMBER                                            *
***********************************************************************
         MVC   HDRNUM,JCTJOBID     SET JOB NUMBER
***********************************************************************
*        SYSOUT JOB NAME                                              *
***********************************************************************
         MVC   HDRNAME,JCTJNAME    SET JOB NAME
***********************************************************************
*        SYSOUT SYSTEM ID                                             *
***********************************************************************
         MVC   HDRSYS,$SID         SET SYSTEM ID
***********************************************************************
*        SYSOUT PROGRAMMER NAME                                       *
***********************************************************************
         MVC   HDRPNAME,JCTPNAME   SET PROGRAMMER NAME
***********************************************************************
*        SYSOUT ROOM NUMBER                                           *
***********************************************************************
         MVC   HDRRKEY,=CL4'ROOM'  SET THE ROOM LABEL
         MVC   HDRROOM,JCTROOMN    SET ROOM NUMBER
***********************************************************************
*        SYSOUT TIME (HH.MM.SS XM)                                    *
***********************************************************************
         MVC   HDRPTTN(11),TOD     MOVE TIME INTO HEADER LINE
***********************************************************************
*        SYSOUT DATE (DD MMM YY)                                      *
***********************************************************************
         MVC   HDRDATE(2),CDATE+3  MOVE DAY (DD)
         MVI   HDRDATE+2,C' '      SET DELIMITER
         MVC   HDRDATE+3(3),MONTH  MOVE MONTH (MMM)
         MVI   HDRDATE+6,C' '      SET DELIMITER
         MVC   HDRDATE+7(2),CDATE+6  MOVE YEAR (YY)
***********************************************************************
*        SYSOUT DEVICE NAME                                           *
***********************************************************************
         MVC   HDRDNAME,DCTDEVN    DEVICE NAME
         $RETURN ,                 RETURN TO CALLER
         EJECT
***********************************************************************
*        ROUTINE CONSTANTS                                            *
***********************************************************************
         LTORG
         EJECT
         TITLE 'BUILD STATISTICS LINE SUBROUTINE'
***********************************************************************
*                                                                     *
*        PRODUCE THE 8 LINES OF JOB STATISTICS TO BE PRINTED ON       *
*        THE SEPARATOR PAGE.  THEY WILL BE STORED IN FIELDS           *
*        STAT1 THROUGH STAT10.                                        *
*                                                                     *
*        ON INPUT:                                                    *
*                                                                     *
*        R2     - JITB ADDRESS                                        *
*        R3     - DCT ADDRESS                                         *
*        R4     - BUFFER ADDRESS                                      *
*        R8     - COMMON EXIT WORK AREA                               *
*        R11    - HCT                                                 *
*        R13    - PCE                                                 *
*        R15    - ENTRY POINT ADDRESS                                 *
*                                                                     *
***********************************************************************
         SPACE 3
STATLNE  $SAVE
         USING STATLNE,R12         SET ADDRESSABILITY TO ROUTINE
         LR    R12,R15             SET BASE REGISTER
         SPACE 3
         EJECT
***********************************************************************
*                                                                     *
*        BUILD THE STATISTICS LINE 1                                  *
*                                                                     *
***********************************************************************
STATL1   LA    R7,STAT1            POINT TO STAT LINE
         USING SL1,R7              SET ADDRESSABILITY TO STAT LINE
         MVI   0(R7),C' '          CLEAR ENTIRE
         MVC   1(131,R7),0(R7)           LINE AREA
         MVC   SL1TYPE,=CL5'START' ASSUME "START" ENTRY
         CLC   REASON,=H'0'        TEST ENTRY REASON CODE
         BZ    STATL1A             ZERO, START, BRANCH
         MVC   SL1TYPE,=CL5'CONT'  ASSUME "CONT " ENTRY
         CLC   REASON,=H'4'        TEST ENTRY REASON CODE
         BE    STATL1A             FOUR, CONT, BRANCH
         MVC   SL1TYPE,=CL5'END'   MUST BE "END  " ENTRY
STATL1A  MVC   SL1T1,=C'JOB:'      SET TITLE 1
         MVC   SL1JOBNM,JCTJNAME   SET JOB NAME
         MVI   SL1JOBID,C'('       SET JOBID DELIMITER
         MVC   SL1JOBID+1(1),JCTJOBID MOVE JOBID CHARACTER (J/S/T)
         MVC   SL1JOBID+2(4),JCTJOBID+4 MOVE JOB NUMBER
         OC    SL1JOBID+2(4),=X'F0F0F0F0' TURN BLANKS INTO ZEROS
         MVI   SL1JOBID+6,C')'     SET JOBID DELIMITER
         MVC   SL1T2,=C'REPORTID:' SET TITLE 2
         L     R6,PPPWKJOE         LOAD ADDRESS OF WORK JOE
         USING JOE,R6              SET ADDRESSABILITY TO JOE
         MVC   SL1GRPID,JOENAME    SET OUTPUT GROUP NAME
         DROP  R6                  DROP ADDRESSABILITY TO JOE
         L     R6,PPPWKJOE         LOAD ADDRESS OF WORK JOE
         USING JOE,R6              SET ADDRESSABILITY TO JOE
         LH    R1,JOEID1           LOAD 1ST JOEID
         CVD   R1,EXITWRK1         CONVERT TO PACKED
         OI    EXITWRK1+7,X'0F'    SET SIGN
         UNPK  SL1GRPI1,EXITWRK1+6(2) SET JOEID1
         DROP  R6                  DROP ADDRESSABILITY TO JOE
         L     R6,PPPWKJOE         LOAD ADDRESS OF WORK JOE
         USING JOE,R6              SET ADDRESSABILITY TO JOE
         LH    R1,JOEID2           LOAD 2ND JOEID
         CVD   R1,EXITWRK1         CONVERT TO PACKED
         OI    EXITWRK1+7,X'0F'    SET SIGN
         UNPK  SL1GRPI2,EXITWRK1+6(2) SET JOEID2
         DROP  R6                  DROP ADDRESSABILITY TO JOE
         MVC   SL1T3,=C'USER:'     SET TITLE 3
         AIF   ('&UJQE' NE 'YES').UJQE#5
         L     R6,JCTJQE           LOAD OFFSET OF JQE
         AL    R6,$JOBQPTR         CALCULATE ADDRESS OF JQE
         USING JQE,R6              SET ADDRESSABILITY TO JQE
         MVC   SL1OWNER,JQEXOWNR   SET OWNERID
         DROP  R6                  DROP ADDRESSABILITY TO JQE
.UJQE#5  ANOP
         MVC   SL1T4,=C'DATE:'     SET TITLE 4
         MVC   SL1CDATE,CDATE      SET CALENDAR DATE
         MVC   SL1JDATE,JDATE+2    SET JULIAN DATE
         MVC   SL1T5,=C'TIME:'     SET TITLE 5
         MVC   SL1TIME,TOD         SET TIME OF DAY
         DROP  R7                  DROP ADDRESSABILITY TO STAT LINE
         EJECT
***********************************************************************
*                                                                     *
*        BUILD THE STATISTICS LINE 2                                  *
*                                                                     *
***********************************************************************
STATL2   LA    R7,STAT2            POINT TO STAT LINE
         MVI   0(R7),C' '          CLEAR ENTIRE
         MVC   1(131,R7),0(R7)           LINE AREA
         EJECT
***********************************************************************
*                                                                     *
*        BUILD THE STATISTICS LINE 3                                  *
*                                                                     *
***********************************************************************
STATL3   LA    R7,STAT3            POINT TO STAT LINE
         USING SL3,R7              SET ADDRESSABILITY TO STAT LINE
         MVI   0(R7),C' '          CLEAR ENTIRE
         MVC   1(131,R7),0(R7)           LINE AREA
         MVC   SL3T1,=C'DEV:'      SET TITLE 1
         MVC   SL3DEVNM,DCTDEVN    SET DEVICE NAME
         MVC   SL3SYSID,$SID       SET SYSTEM ID
         MVC   SL3T2,=C'AT:'       SET TITLE 2
         XC    DESTWORK,DESTWORK   CLEAR CNVDEST WORK AREA
         MVC   DESTWORK(2),$OWNNODE SET OUR NODE AS OUTPUT NODE NUMBER
         $CALL CNVDEST             CONVERT DESTINATION CODE
         MVC   SL3ONODE,DESTWORK   SAVE THE DESTINATION NAME
         MVC   SL3T3,=C'FOR:'      SET TITLE 3
         MVC   SL3DEST,DEST        SET OUTPUT DESTINATION NAME
         MVC   SL3T4,=C'FROM:'     SET TITLE 4
         XC    DESTWORK,DESTWORK   CLEAR CNVDEST WORK AREA
         MVC   DESTWORK(2),JCTXNODE GET POSSIBLE EXECUTION NODE NUMBER
         CLC   JCTXNODE,$ZEROS     SEE IF EXECUTION NODE SPECIFIED
         BNE   STATL3A             YES, BRANCH
         MVC   DESTWORK(2),JCTXEQND GET POSSIBLE EXECUTION NODE NUMBER
         CLC   JCTXEQND,$ZEROS     SEE IF EXECUTION NODE SPECIFIED
         BNE   STATL3A             YES, BRANCH
         MVC   SL3ENODE,=CL8'UNKNOWN' SET UNKNOWN NODE NAME
         B     STATL3B             GET OUT
STATL3A  $CALL CNVDEST             CONVERT DESTINATION CODE
         MVC   SL3ENODE,DESTWORK   SAVE THE DESTINATION NAME
STATL3B  MVC   SL3T5,=C'CLASS:'    SET TITLE 5
         L     R6,PPPWKJOE         LOAD ADDRESS OF WORK JOE
         USING JOE,R6              SET ADDRESSABILITY TO JOE
         MVC   SL3CLASS,JOECURCL   SET SYSOUT CLASS
         DROP  R6                  DROP ADDRESSABILITY TO JOE
         MVC   SL3T6,=C'FORMS:'    SET TITLE 6
         L     R6,PPPCHJOE         LOAD ADDRESS CHARACTERISTICS JOE
         USING JOE,R6              SET ADDRESSABILITY TO JOE
         MVC   SL3FORM,JOEFORM     SET SYSOUT FORM
         DROP  R6                  DROP ADDRESSABILITY TO CHAR JOE
         MVC   SL3T7,=C'LINES:'    SET TITLE 7
         L     R6,PPPWKJOE         LOAD ADDRESS OF WORK JOE
         USING JOE,R6              SET ADDRESSABILITY TO JOE
         L     R15,JOERECCT        LOAD RECORD COUNT
         CVD   R15,DBL             CONVERT TO PACKED
         MVC   SL3LNES(12),=X'402020206B2020206B202120'
         ED    SL3LNES(12),DBL+3   EDIT LINE COUNT
STATL3C  CLI   SL3LNES,C' '        START OF NUMBER
         BNE   STATL3D             YES, BRANCH
         MVC   SL3LNES(12),SL3LNES+1 SHIFT LINE COUNT
         B     STATL3C             LOOP
         DROP  R6                  DROP ADDRESSABILITY TO JOE
STATL3D  MVC   SL3T8,=C'PAGES:'    SET TITLE 8
         L     R6,PPPWKJOE         LOAD ADDRESS OF WORK JOE
         USING JOE,R6              SET ADDRESSABILITY TO JOE
         L     R15,JOEPGCT         LOAD PAGE COUNT
         CVD   R15,DBL             CONVERT TO PACKED
         MVC   SL3PGS(9),=X'40202020206B202120'
         ED    SL3PGS(9),DBL+4    EDIT PAGE COUNT
STATL3E  CLI   SL3PGS,C' '         START OF NUMBER
         BNE   STATL3F             YES, BRANCH
         MVC   SL3PGS(9),SL3PGS+1 SHIFT PAGE COUNT
         B     STATL3E             LOOP
STATL3F  DS    0H
         DROP  R6                  DROP ADDRESSABILITY TO JOE
         DROP  R7                  DROP ADDRESSABILITY TO STAT LINE
         EJECT
***********************************************************************
*                                                                     *
*        BUILD THE STATISTICS LINE 4                                  *
*                                                                     *
***********************************************************************
STATL4   LA    R7,STAT4            POINT TO STAT LINE
         MVI   0(R7),C' '          CLEAR ENTIRE
         MVC   1(131,R7),0(R7)           LINE AREA
         EJECT
***********************************************************************
*                                                                     *
*        BUILD THE STATISTICS LINE 5                                  *
*                                                                     *
***********************************************************************
STATL5   LA    R7,STAT5            POINT TO STAT LINE
         USING SL5,R7              SET ADDRESSABILITY TO STAT LINE
         MVI   0(R7),C' '          CLEAR ENTIRE
         MVC   1(131,R7),0(R7)           LINE AREA
         MVC   SL5T1,=C'SPECIAL INSTRUCTIONS:' SET TITLE 1
         MVC   SL5T2,=C'NOTE:'                 SET TITLE 2
         MVC   SL5T3,=C'RETURN THIS PAGE TO A&&O' SET TITLE 3
         DROP  R7                  DROP ADDRESSABILITY TO STAT LINE
         EJECT
***********************************************************************
*                                                                     *
*        BUILD THE STATISTICS LINE 6                                  *
*                                                                     *
***********************************************************************
STATL6   LA    R7,STAT6            POINT TO STAT LINE
         USING SL6,R7              SET ADDRESSABILITY TO STAT LINE
         MVI   0(R7),C' '          CLEAR ENTIRE
         MVC   1(131,R7),0(R7)           LINE AREA
         MVC   SL6T1,=C'DISTRIBUTION:' SET TITLE 1
         TM    JITBSTAT,JITBOK     DO WE HAVE A GOOD JITB
         BNO   STATL6A             NO, BRANCH
         MVC   SL6INST1,JITBINS1   SET SPECIAL INSTRUCTIONS 1
STATL6A  MVC   SL6T2,=C'QUALITY CONTROL AT MAIL' SET TITLE 2
         CLC   $OWNNODE,=H'2'      SEE IF NODE LE 2 (JACKSONVILLE FLA)
         BH    STATL7              NO, BRANCH
         MVC   SL6T2,=C'OUTPUT CONTROL  AT MAIL' SET TITLE 2
         DROP  R7                  DROP ADDRESSABILITY TO STAT LINE
         EJECT
***********************************************************************
*                                                                     *
*        BUILD THE STATISTICS LINE 7                                  *
*                                                                     *
***********************************************************************
STATL7   LA    R7,STAT7            POINT TO STAT LINE
         USING SL7,R7              SET ADDRESSABILITY TO STAT LINE
         MVI   0(R7),C' '          CLEAR ENTIRE
         MVC   1(131,R7),0(R7)           LINE AREA
         MVC   SL7PBIN(4),BINNBR   SET PRIMARY BIN PART 1
         MVI   SL7PBIN+4,C'-'      SET SEPARATOR
         MVC   SL7PBIN+5(4),BINNBR+4  SET PRIMARY BIN PART 2
         TM    JITBSTAT,JITBOK     DO WE HAVE A GOOD JITB
         BNO   STATL7B             NO, BRANCH
         CLC   JITBPSID,=CL20' '   SEE IF PRIMARY SEP ID SPECIFIED
         BE    STATL7A             NO, USE DEFAULT
         MVC   SL7PBIN(9),=CL20' ' BLANK BIN NUMBER
         MVC   SL7PBIN(4),JITBPSID SET PRIMARY BIN PART 1
         MVC   SL7PBIN+5(4),JITBPSID+4 SET PRIMARY BIN PART 2
         CLC   JITBPSID(4),=CL20' ' SEE IF PRIMARY SEP ID1 SPECIFIED
         BE    STATL7A             NO, NO DELIMETER
         CLC   JITBPSID+4(4),=CL20' ' SEE IF PRIMARY SEP ID2 SPECIFIED
         BE    STATL7A             NO, NO DELIMETER
         MVI   SL7PBIN+4,C'-'      SET DELIMETER
STATL7A  CLC   JITBSSID,=CL20' '   SEE IF SECONDARY SEP ID SPECIFIED
         BNH   STATL7B             NO, LEAVE BLANK
         MVC   SL7SBIN(4),JITBSSID SET SECOND BIN PART 1
         MVC   SL7SBIN+5(4),JITBSSID+4 SET SECOND BIN PART 2
         CLC   JITBSSID(4),=CL20' ' SEE IF SECOND BIN PART 1 SPECIFIED
         BE    STATL7B             NO, NO DELIMTER
         CLC   JITBSSID+4(4),=CL20' ' SEE IF SECOND BIN PART 2 SPEC
         BE    STATL7B             NO, NO DELIMTER
         MVI   SL7SBIN+4,C'-'      SET DELIMETER
STATL7B  MVC   SL7T1,=C'LOCATION:' SET TITLE 1
         TM    JITBSTAT,JITBOK     DO WE HAVE A GOOD JITB
         BNO   STATL7C             NO, BRANCH
         MVC   SL7LOC,JITBLOC      SET LOCATION NAME
         MVC   SL7INST2,JITBINS2   SET SPECIAL INSTRUCTIONS 2
STATL7C  MVC   SL7T2,=C'CODE - FCS11  WITH  ANY' SET TITLE 2
         CLC   $OWNNODE,=H'2'      SEE IF NODE LE 2 (JACKSONVILLE FLA)
         BH    STATL8              NO, BRANCH
         MVC   SL7T2,=C'CODE - 32231-0445  WITH' SET TITLE 2
         DROP  R7                  DROP ADDRESSABILITY TO STAT LINE
         EJECT
***********************************************************************
*                                                                     *
*        BUILD THE STATISTICS LINE 8                                  *
*                                                                     *
***********************************************************************
STATL8   LA    R7,STAT8            POINT TO STAT LINE
         USING SL8,R7              SET ADDRESSABILITY TO STAT LINE
         MVI   0(R7),C' '          CLEAR ENTIRE
         MVC   1(131,R7),0(R7)           LINE AREA
         MVC   SL8T1,=C'DEPT:'     SET TITLE 1
         TM    JITBSTAT,JITBOK     DO WE HAVE A GOOD JITB
         BNO   STATL8A             NO, BRANCH
         MVC   SL8DEPT,JITBDEPT    SET DEPARTMENT NAME
STATL8A  MVC   SL8T2,=C'MAIL CODE:' SET TITLE 2
         TM    JITBSTAT,JITBOK     DO WE HAVE A GOOD JITB
         BNO   STATL8B             NO, BRANCH
         MVC   SL8MAILC,JITBMAIL   SET MAIL CODE
         MVC   SL8INST3,JITBINS3   SET SPECIAL INSTRUCTIONS 3
STATL8B  MVC   SL8T3,=C'CHANGES MARKED.        ' SET TITLE 3
         CLC   $OWNNODE,=H'2'      SEE IF NODE LE 2 (JACKSONVILLE FLA)
         BH    STATL9              NO, BRANCH
         MVC   SL8T3,=C'ANY CHANGES MARKED.    ' SET TITLE 3
         DROP  R7                  DROP ADDRESSABILITY TO STAT LINE
         EJECT
***********************************************************************
*                                                                     *
*        BUILD THE STATISTICS LINE 9                                  *
*                                                                     *
***********************************************************************
STATL9   LA    R7,STAT9            POINT TO STAT LINE
         USING SL9,R7              SET ADDRESSABILITY TO STAT LINE
         MVI   0(R7),C' '          CLEAR ENTIRE
         MVC   1(131,R7),0(R7)           LINE AREA
         MVC   SL9T1,=C'PHONE:'    SET TITLE 1
         TM    JITBSTAT,JITBOK     DO WE HAVE A GOOD JITB
         BNO   STATL9A             NO, BRANCH
         MVC   SL9PHONE,JITBPHON   SET PHONE NUMBER
STATL9A  MVC   SL9T2,=C'ATTN:'     SET TITLE 2
         TM    JITBSTAT,JITBOK     DO WE HAVE A GOOD JITB
         BNO   STATL10             NO, BRANCH
         MVC   SL9ATTN,JITBPGMR    SET ATTENTION NAME
         MVC   SL9INST4,JITBINS4   SET SPECIAL INSTRUCTIONS 4
         DROP  R7                  DROP ADDRESSABILITY TO STAT LINE
         EJECT
***********************************************************************
*                                                                     *
*        BUILD THE STATISTICS LINE 10                                 *
*                                                                     *
***********************************************************************
STATL10  LA    R7,STAT10           POINT TO STAT LINE
         MVI   0(R7),C' '          CLEAR ENTIRE
         MVC   1(131,R7),0(R7)           LINE AREA
         EJECT
***********************************************************************
*        RETURN TO CALLER                                             *
***********************************************************************
         $RETURN ,                 RETURN TO CALLER
         EJECT
***********************************************************************
*        ROUTINE CONSTANTS                                            *
***********************************************************************
         LTORG
         EJECT
***********************************************************************
*                                                                     *
*        CONVERT A ROUTE CODE TO EBCIDIC BY SCANNING THE              *
*        REMOTE DESTINATION TABLE FOR A MATCH.  IF A DESTID           *
*        IS FOUND THEN THAT NAME WILL BE USED.  IF A DESTID IS        *
*        NOT FOUND FOR A ROUTE CODE THEN THE ROUTE IS CONVERTED       *
*        ACCORDING TO THE FOLLOWING FORMAT                            *
*                                                                     *
*             NNNNN  (NODE)                                           *
*             RNNNN  (REMOTE)                                         *
*             UNNNN  (SPECIAL LOCAL)                                  *
*             'LOCAL' (LOCAL DEVICE)                                  *
*             CCCCCCCC (DESTID)                                       *
*                                                                     *
*        ON INPUT:                                                    *
*                                                                     *
*        R2     - JITB ADDRESS                                        *
*        R3     - DCT ADDRESS                                         *
*        R4     - BUFFER ADDRESS                                      *
*        R8     - COMMON EXIT WORK AREA                               *
*        R11    - HCT                                                 *
*        R13    - PCE                                                 *
*        R15    - ENTRY POINT ADDRESS                                 *
*                                                                     *
*        DESTWORK - THE 4 BYTE JES2 ROUTE CODE                        *
*                                                                     *
***********************************************************************
         SPACE 2
CNVDEST  $SAVE
         USING CNVDEST,R12         SET ADDRESSABILITY TO ROUTINE
         LR    R12,R15             SET BASE REGISTER
         L     R7,$SVT             GET ADDRESS OF SSVT
         LM    R14,R15,SVTRDT-SSVT(R7)  LOAD ADDRESS OF FIRST RDT
         USING RDT,R14             SET ADDRESSABILITY TO RDT
CNVDEST1 CR    R14,R15             END OF TABLE?
         BH    CNVDEST4            YES, GET OUT
         ICM   R7,15,RDTROUTE      LOAD ROUTE CODE FROM RDT
         TM    RDTFLAG,RDTFLAGR    IS THIS A REMOTE DESTINATION ENTRY
         BZ    CNVDEST2            NO, BRANCH
         TM    RDTFLAG,RDTFLAGN    IS NODE SPECIFIED IN THE ENTRY
         BO    CNVDEST2            YES, BRANCH
         ICM   R7,B'1100',$OWNNODE MOVE DEFAULT NODE TO ROUTE CODE
CNVDEST2 CL    R7,DESTWORK         SEE IF DESTID MATCHES THIS ENTRY
         BNE   CNVDEST3            NO, GO CHECK NEXT RDT ENTRY
         MVC   EXITWRK3,RDTNAME    SAVE DESTID
         B     CNVDEST9            GET OUT
CNVDEST3 LA    R14,RDTSIZ(R14)     INCREMENT TO NEXT RDT
         B     CNVDEST1            GO CHECK NEXT RDT ENTRY
CNVDEST4 CLC   DESTWORK(4),$ZEROS  IS THIS A LOCAL DESTID
         BE    CNVDEST8            YES, GO SET TO LOCAL
         CLC   DESTWORK(2),$ZEROS  SEE IF NODE NUMBER SPECIFIED
         BE    CNVDEST6            NO, GO PROCESS SPECIAL OR RMT
         CLC   DESTWORK(2),$OWNNODE  SEE IF OUR NODE
         BNE   CNVDEST5            NO, BRANCH
         CLC   DESTWORK+2(2),$ZEROS  IS THIS A LOCAL DESTID
         BE    CNVDEST8            YES, GO SET TO LOCAL
         B     CNVDEST6            NO, GO PROCESS SPECIAL OR RMT
CNVDEST5 SLR   R15,R15             CLEAR REG FOR ICM
         ICM   R15,3,DESTWORK      LOAD NODE NUMBER
         CVD   R15,EXITWRK3        CONVERT NODE NUMBER TO PACKED
         MVC   EXITWRK3-1(6),=X'402120202020' SET EDIT PATTERN
         ED    EXITWRK3-1(6),EXITWRK3+5  MAKE REMOTE NUMBER PRINTABLE
         MVI   EXITWRK3,C'N'       SET 'N' LABEL
         MVC   EXITWRK3+5(3),=CL20' ' BLANK REST OF FIELD
         B     CNVDEST9            GET OUT
CNVDEST6 SLR   R15,R15             CLEAR REG FOR ICM
         ICM   R15,3,DESTWORK+2    MOVE REMOTE NUMBER TO A REG
         CVD   R15,EXITWRK3        CONVERT REMOTE NUMBER TO PACKED
         MVC   EXITWRK3-1(6),=X'402120202020' SET EDIT PATTERN
         ED    EXITWRK3-1(6),EXITWRK3+5  MAKE REMOTE NUMBER PRINTABLE
         CLC   DESTWORK(2),$ZEROS  SEE IF UNIT SPECIFICATION
         BE    CNVDEST7            YES, GO SET  'U' LABEL
         MVI   EXITWRK3,C'R'       NO,  REMOTE, 'R' LABEL
         B     CNVDEST9            GO BUILD SEPARATOR LINE
CNVDEST7 MVI   EXITWRK3,C'U'       SET 'U' LABEL
         MVC   EXITWRK3+5(3),=CL20' ' BLANK REST OF FIELD
         B     CNVDEST9            GO BUILD SEPARATOR LINE
CNVDEST8 MVC   EXITWRK3,=CL8'LOCAL' SET 'LOCAL' LABEL
CNVDEST9 MVC   DESTWORK,EXITWRK3   SAVE THE DEST CODE
         $RETURN ,                 RETURN TO CALLER
         DROP  R14                 DROP ADDRESSABILITY TO RDT
         SPACE 3
         LTORG
         EJECT
***********************************************************************
*                                                                     *
*        DETERMINE THE BIN NUMBER FROM THE JOB CARD OR THE            *
*        USERID.                                                      *
*                                                                     *
*        ON INPUT:                                                    *
*                                                                     *
*        R2     - JITB ADDRESS                                        *
*        R3     - DCT ADDRESS                                         *
*        R4     - BUFFER ADDRESS                                      *
*        R8     - COMMON EXIT WORK AREA                               *
*        R11    - HCT                                                 *
*        R13    - PCE                                                 *
*        R15    - ENTRY POINT ADDRESS                                 *
*                                                                     *
***********************************************************************
         SPACE 2
DETBIN   $SAVE
         USING DETBIN,R12          SET ADDRESSABILITY TO ROUTINE
         LR    R12,R15             SET BASE REGISTER
         MVC   BINNBR(8),=CL8'BIN ???' SET DEFAULT BIN NUMBER
         CLC   JCTROOMN,=F'0'      SEE IF ROOM NUMBER SPECIFIED
         BE    DETBIN1             NO, BRANCH
         MVC   BINNBR+4(4),JCTROOMN SAVE ROOM NUMBER
         B     DETBIN5             DONE
DETBIN1  DS    0H
         AIF   ('&UJQE' NE 'YES').UJQE#6
         L     R6,JCTJQE           LOAD OFFSET OF JQE
         AL    R6,$JOBQPTR         CALCULATE ADDRESS OF JQE
         USING JQE,R6              SET ADDRESSABILITY TO JQE
         CLI   JQEXFLG1,JQEXPROD   SEE IF PRODUCTION JOB
         BNE   DETBIN2             NO, BRANCH
         MVC   BINNBR+4(4),=CL4'PCHL' SET PRODUCTION BIN NUMBER
         CLC   $OWNNODE,=H'2'      SEE IF NODE LE 2 (JACKSONVILLE FLA)
         BH    DETBIN5             NO, BRANCH
         MVC   BINNBR+4(4),=CL4'PJAX' SET PRODUCTION BIN NUMBER
         B     DETBIN5             DONE
DETBIN2  LA    R5,BINTBL           LOAD ADDRESS OF BIN NUMBER TBL
DETBIN3  CLI   0(R5),X'FF'         END OF TABLE
         BE    DETBIN5             YES, DONE
         CLC   JQEXOWNR(1),0(R5)   SEE IF MATCH ON OWNERID CHARACTER
         BE    DETBIN4             YES, BRANCH
         LA    R5,4(R5)            INCREMENT TO NEXT TABLE ENTRY
         B     DETBIN3             LOOP
DETBIN4  MVC   BINNBR+4(3),1(R5)   SAVE BIN NUMBER FROM TABLE
.UJQE#6  ANOP
DETBIN5  $RETURN ,                 RETURN TO CALLER
         SPACE 3
         AIF   ('&UJQE' NE 'YES').UJQE#7
BINTBL   DS    0F                  BIN NUMBER TABLE
         DC    C'A',CL3'ADM'
         DC    C'B',CL3'MKT'
         DC    C'D',CL3'SDD'
         DC    C'M',CL3'MSD'
         DC    C'N',CL3'ACT'
         DC    C'O',CL3'OPD'
         DC    C'R',CL3'RSD'
         DC    C'S',CL3'SSD'
         DC    C'Z',CL3'AUD'
         DC    X'FF'
.UJQE#7  ANOP
         LTORG
         EJECT
***********************************************************************
*                                                                     *
*        CALL THE JOB INFORMATION TASK                                *
*                                                                     *
*        ON INPUT:                                                    *
*                                                                     *
*        R2     - JITB ADDRESS                                        *
*        R3     - DCT ADDRESS                                         *
*        R4     - BUFFER ADDRESS                                      *
*        R8     - COMMON EXIT WORK AREA                               *
*        R11    - HCT                                                 *
*        R13    - PCE                                                 *
*        R15    - ENTRY POINT ADDRESS                                 *
*                                                                     *
***********************************************************************
         SPACE 1
CALLJIT  $SAVE
         USING CALLJIT,R12         SET ADDRESSABILITY TO ROUTINE
         LR    R12,R15             SET BASE REGISTER
         AIF   ('&JIT' NE 'YES').JIT#2
         ICM   R5,B'1111',$UCT     LOAD UCT ADDRESS
         BZ    CALLJITB            NONE, BRANCH
         USING UCT,R5              SET ADDRESSABILITY TO UCT
         ICM   R4,B'1111',UCTJITDT LOAD JIT DTE ADDRESS
         BZ    CALLJITB            NONE, BRANCH
         USING DTE,R4              SET ADDRESSABILITY TO DTE
         MVI   JITBFLG,JITBREAD    TURN ON READ OPERATION REQUEST
         XC    JITBXECB(L'XECBECB),JITBXECB CLEAR THE XECB
         L     R1,DTEFQUE          LOAD THE WORK QUEUE ADDRESS
CALLJITA ST    R1,JITBCH           PUSH HEAD QUEUE DOWN
         CS    R1,R2,DTEFQUE       ADD TO WORK QUEUE
         BNE   CALLJITA            CHANGED, TRY AGAIN
         POST  DTEWECB             POST SUBTASK
         $WAIT OPER,XECB=JITBXECB,INHIBIT=YES
         DROP  R4                  DROP ADDRESSABILITY TO DTE
         DROP  R5                  DROP ADDRESSABILITY TO UCT
         SPACE 1
.JIT#2   ANOP
CALLJITB $RETURN ,                 RETURN TO CALLER
         EJECT
***********************************************************************
*                                                                     *
*        CALL THE JOB UCC7 TASK                                       *
*                                                                     *
*        ON INPUT:                                                    *
*                                                                     *
*        R2     - JITB ADDRESS                                        *
*        R3     - DCT ADDRESS                                         *
*        R4     - BUFFER ADDRESS                                      *
*        R8     - COMMON EXIT WORK AREA                               *
*        R11    - HCT                                                 *
*        R13    - PCE                                                 *
*        R15    - ENTRY POINT ADDRESS                                 *
*                                                                     *
***********************************************************************
         SPACE 1
CALLUC7  $SAVE
         USING CALLUC7,R12         SET ADDRESSABILITY TO ROUTINE
         LR    R12,R15             SET BASE REGISTER
         AIF   ('&UCC7' NE 'YES').UCC7#5
         ICM   R5,B'1111',$UCT     LOAD UCT ADDRESS
         BZ    CALLUC7B            NONE, BRANCH
         USING UCT,R5              SET ADDRESSABILITY TO UCT
         ICM   R4,B'1111',UCTUC7DT LOAD UCC7 DTE ADDRESS
         BZ    CALLUC7B            NONE, BRANCH
         USING DTE,R4              SET ADDRESSABILITY TO DTE
         MVI   JITBFLG,JITBDMD     TURN ON DEMAND NETWORK REQUEST
         XC    JITBXECB(L'XECBECB),JITBXECB CLEAR THE XECB
         L     R1,DTEFQUE          LOAD THE WORK QUEUE ADDRESS
CALLUC7A ST    R1,JITBCH           PUSH HEAD QUEUE DOWN
         CS    R1,R2,DTEFQUE       ADD TO WORK QUEUE
         BNE   CALLUC7A            CHANGED, TRY AGAIN
         POST  DTEWECB             POST SUBTASK
         $WAIT OPER,XECB=JITBXECB,INHIBIT=YES
         DROP  R4                  DROP ADDRESSABILITY TO DTE
         DROP  R5                  DROP ADDRESSABILITY TO UCT
         SPACE 1
.UCC7#5  ANOP
CALLUC7B $RETURN ,                 RETURN TO CALLER
         EJECT
***********************************************************************
*                                                                     *
*        STATISTICS LINE  1 DSECT                                     *
*                                                                     *
***********************************************************************
SL1      DSECT
SL1TYPE  DS    CL5      001-005    SL1 TITLE 1 "START/CONT/END"
         DS    CL1
SL1T1    DS    CL4      007-010    SL1 TITLE 1 "JOB:"
         DS    CL1
SL1JOBNM DS    CL8      012-019    SL1 JOB NAME
SL1JOBID DS    CL7      020-026    SL1 JOBID
         DS    CL2
SL1T2    DS    CL9      029-037    SL1 TITLE 2 "REPORTID:"
         DS    CL1
SL1GRPID DS    CL8      039-046    SL1 OUTPUT GROUP NAME
         DS    CL1
SL1GRPI1 DS    CL3      048-050    SL1 OUTPUT GROUP ID 1
         DS    CL1
SL1GRPI2 DS    CL3      052-054    SL1 OUTPUT GROUP ID 2
         DS    CL5
SL1T3    DS    CL5      060-064    SL1 TITLE 3 "USER:"
         DS    CL1
SL1OWNER DS    CL8      066-073    SL1 OWNER ID
         DS    CL2
SL1T4    DS    CL5      076-080    SL1 TITLE 4 "DATE:"
         DS    CL1
SL1CDATE DS    CL8      082-089    SL1 CALENDAR DATE (MM/DD/YY)
SL1JDATE DS    CL4      090-093    SL1 JULIAN   DATE (.DDD)
         DS    CL2
SL1T5    DS    CL5      096-100    SL1 TITLE 5 "TIME:"
         DS    CL1
SL1TIME  DS    CL11     102-112    SL1 TIME OF DAY (HH.MM.SS XM)
         EJECT
***********************************************************************
*                                                                     *
*        STATISTICS LINE  3 DSECT                                     *
*                                                                     *
***********************************************************************
SL3      DSECT
SL3T1    DS    CL4      001-004    SL3 TITLE 1 "DEV:"
         DS    CL1
SL3DEVNM DS    CL8      006-013    SL3 DEVICE NAME
         DS    CL1
SL3SYSID DS    CL4      015-018    SL3 SYSTEM ID
         DS    CL4
SL3T2    DS    CL3      023-025    SL3 TITLE 2 "ON:"
         DS    CL1
SL3ONODE DS    CL8      027-034    SL3 OUTPUT NODE NAME
         DS    CL2
SL3T3    DS    CL4      037-040    SL3 TITLE 3 "FOR:"
         DS    CL1
SL3DEST  DS    CL8      042-049    SL3 DESTINATION NAME
         DS    CL2
SL3T4    DS    CL5      052-056    SL3 TITLE 3 "FROM:"
         DS    CL1
SL3ENODE DS    CL8      058-065    SL3 EXECUTION NODE
         DS    CL2
SL3T5    DS    CL6      068-073    SL3 TITLE 3 "CLASS:"
         DS    CL1
SL3CLASS DS    CL1      075-075    SL3 OUTPUT CLASS
         DS    CL2
SL3T6    DS    CL6      078-083    SL3 TITLE 3 "FORMS:"
         DS    CL1
SL3FORM  DS    CL8      085-092    SL3 OUTPUT FORM NUMBER
         DS    CL2
SL3T7    DS    CL6      095-100    SL3 TITLE 3 "LINES:"
         DS    CL1
SL3LNES  DS    CL11     102-112    SL3 NUMBER OF LINES (NNN,NNN,NNN)
         DS    CL1
SL3T8    DS    CL6      114-119    SL3 TITLE 3 "PAGES:"
         DS    CL1
SL3PGS   DS    CL8      121-126    SL3 NUMBER OF PAGES (NNNN,NNN)
         EJECT
***********************************************************************
*                                                                     *
*        STATISTICS LINE  5 DSECT                                     *
*                                                                     *
***********************************************************************
SL5      DSECT
         DS    CL68
SL5T1    DS    CL21     069-089    SL5 TITLE 1 "SPECIAL INSTRUCTIONS:"
         DS    CL13
SL5T2    DS    CL5      103-107    SL5 TITLE 2 "NOTE:"
         DS    CL1
SL5T3    DS    CL23     109-131    SL5 T 3 "RETURN THIS PAGE TO A&O"
         EJECT
***********************************************************************
*                                                                     *
*        STATISTICS LINE  6 DSECT                                     *
*                                                                     *
***********************************************************************
SL6      DSECT
SL6T1    DS    CL12     001-012    SL6 TITLE 1 "DISTRIBUTION"
         DS    CL57
SL6INST1 DS    CL25     070-094    SL6 SPECIAL INSTRUCTIONS PART 1
         DS    CL14
SL6T2    DS    CL23     109-131    SL6 T 3 "QUALITY CONTROL AT MAIL"
         EJECT
***********************************************************************
*                                                                     *
*        STATISTICS LINE  7 DSECT                                     *
*                                                                     *
***********************************************************************
SL7      DSECT
         DS    CL1
SL7PBIN  DS    CL9      002-010    SL7 PRIMARY BIN NUMBER
         DS    CL2
SL7SBIN  DS    CL9      013-021    SL7 SECONDARY BIN NUMBER
         DS    CL13
SL7T1    DS    CL9      035-043    SL7 TITLE 1 "LOCATION:"
         DS    CL1
SL7LOC   DS    CL15     045-059    SL7 LOCATION NAME
         DS    CL10
SL7INST2 DS    CL25     070-094    SL7 SPECIAL INSTRUCTIONS PART 1
         DS    CL14
SL7T2    DS    CL23     109-131    SL7 T 2 "CODE - FCS11  WITH  ANY"
         EJECT
***********************************************************************
*                                                                     *
*        STATISTICS LINE  8 DSECT                                     *
*                                                                     *
***********************************************************************
SL8      DSECT
         DS    CL1
SL8T1    DS    CL5      002-006    SL8 TITLE 1 "DEPT:"
         DS    CL1
SL8DEPT  DS    CL20     008-027    SL8 DEPARTMENT NAME
         DS    CL7
SL8T2    DS    CL10     035-044    SL8 TITLE 2 "MAIL CODE:"
         DS    CL1
SL8MAILC DS    CL10     046-055    SL8 MAIL CODE
         DS    CL14
SL8INST3 DS    CL25     070-094    SL8 SPECIAL INSTRUCTIONS PART 3
         DS    CL14
SL8T3    DS    CL23     109-131    SL8 T 3 "CHANGES MARKED.        "
         EJECT
***********************************************************************
*                                                                     *
*        STATISTICS LINE  9 DSECT                                     *
*                                                                     *
***********************************************************************
SL9      DSECT
         DS    CL1
SL9T1    DS    CL6      002-007    SL9 TITLE 1 "PHONE:"
         DS    CL1
SL9PHONE DS    CL15     009-023    SL9 PHONE NUMBER
         DS    CL11
SL9T2    DS    CL5      035-039    SL9 TITLE 2 "ATTN:"
         DS    CL1
SL9ATTN  DS    CL20     041-060    SL9 ATTENTION NAME
         DS    CL09
SL9INST4 DS    CL25     070-094    SL9 SPECIAL INSTRUCTIONS PART 4
         EJECT
J001$SP  CSECT ,                   RESTORE THE CSECT ENVIRONMENT
         $MODEND
         END
