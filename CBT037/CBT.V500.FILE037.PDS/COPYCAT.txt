*           COPYCAT  07.42.07 80.171 TSO001 TS.CBT.COPYCAT.PDS
COPYCAT  CSECT
CCAT     TITLE 'COPYCAT - COPY OR SPLIT THE OS CATALOG'
*
*
*                   COPYCAT - OS CATALOG UTILITY PROGRAM
*                                    BY
*                            DOUGLAS E. ENGERT
*                       APPLIED MATHEMATICS DIVISION
*                       ARGONNE NATIONAL LABORATORY
*                          9700 SOUTH CASS AVENUE
*                         ARGONNE, ILLINOIS 60439
*
*
*   THIS 12-77 VERSION CONTAINS SEVERAL MINOR FIXES FROM ARGONNE
*   ("F00001" & "F00003") AND A MAJOR SPEED ENHANCEMENT TO THIS
*   ALREADY FAST PROGRAM IN THAT THE CATALOG I/O IS NOW DONE AT
*   EXCP LEVEL ("EXCP01").  A FIX FROM UCLA FOR A LITTLE MORE
*   SPEED YET IS ALSO INCLUDED ("CCN").  THE HARDCODED BLKSIZES
*   FOR SYSIN AND SYSPRINT HAVE BEEN CHANGED TO 3200 AND 605.
*                             F. PAJERSKI   NASA/GSFC   01DEC77
*
*
*                            TABLE OF CONTENTS
*
*     ABSTRACT
*
*     1.0 INTRODUCTION
*
*     2.0 USER MANUAL
*
*       2.1 FUNCTIONS
*
*         2.1.1 REDISTRIBUTION
*         2.1.2 RESTRUCTURING
*         2.2.3 MASTER AND SECONDARY CATALOGS
*         2.1.4 MASTER MODE
*         2.1.5 SECONDARY MODE
*         2.1.6 MODEL GENERATION DATA GROUP DSCBS
*         2.1.7 ADDITIONAL CVOL PROCESSING
*         2.1.8 ALIAS PROCESSING
*         2.1.9 MULTIPLE INPUTS
*
*       2.2 CONTROL CARDS
*
*         2.2.1 CONTROL
*         2.2.2 INPUT
*         2.2.3 OUTPUT
*         2.2.4 CVOL
*         2.2.5 LEVEL
*
*       2.3 JOB CONTROL STATEMENTS
*
*       2.4 MESSAGES AND CODES
*
*       2.5 EXAMPLES
*
*         2.5.1 COPY A CATALOG FROM ONE VOLUME TO ANOTHER
*         2.5.2 BACK UP A CATALOG
*         2.5.3 REDISTRIBUTE A CATALOG
*
*     3.0 PROGRAM LOGIC
*
*
*               COPYCAT IS AN  OS  UTILITY  PROGRAM  DESIGNED  TO
*          IMPROVE   PERFORMANCE  OF  OS  CATALOG  MANAGEMENT  BY
*          PRODUCING A RESTRUCTURED COPY OF A CATALOG IN ORDER TO
*          USE  THE  EXISTING  ACCESS  METHODS  MOST EFFECTIVELY.
*          MULTIPLE INPUT AND OUTPUT CATALOGS ARE  PROCESSED  AND
*          THE  REDISTRIBUTION  OF  THE  ENTRIES  IS  UNDER  USER
*          CONTROL. MODEL DSCBS FOR GENERATION  DATA  GROUPS  AND
*          ALIAS  ENTRIES  ARE  ALSO  PROCESSED.  CATALOGS ON ALL
*          DIRECT  ACCESS  DEVICES,  INCLUDING  DATA  CELLS,  ARE
*          SUPPORTED. BACKUP COPIES MAY ALSO BE MADE.
*
*
*     1.0 INTRODUCTION
*
*          COPYCAT IS AN OS UTILITY PROGRAM  DESIGNED  TO  PRODUCE  AN
*     EFFICENT  SYSTEM  WIDE CATALOG WHICH MAY RESIDE ON MANY VOLUMES.
*     SUBSTANTIAL IMPROVEMENT IN PERFORMANCE MAY ALSO BE OBTAINED ON A
*     SYSTEM  WITH  ONLY  A  SINGLE CATALOG. THIS IS ACCOMPLISHED IN A
*     NUMBER OF WAYS.  FIRST,  CATALOG  ENTRIES  FROM  MANY  DIFFERENT
*     CATALOGS  MAY  BE  REDISTRIBUTED  TO  EQUALIZE  THE LOAD ON EACH
*     INDIVIDUAL  CATALOG.  SECOND,   EACH   INDIVIDUAL   CATALOG   IS
*     RESTRUCTURED IN A WAY DESIGNED TO MINIMIZE THE I/O TIME REQUIRED
*     BOTH  FOR  SEARCHING  AND  UPDATING.  THE   REDISTRIBUTION   AND
*     RESTRUCTURING  PARAMETERS  ARE UNDER THE CONTROL OF THE USER WHO
*     SHOULD HAVE SOME KNOWLEDGE OF OS CATALOG MANAGEMENT.
*
*          OUR INSTALLATION AT ARGONNE CONSISTS OF A 370/195, USED FOR
*     BATCH  PROCESSING,  AND A 360/75 USED FOR TIME SHARING. THE TIME
*     SHARING SYSTEM, TSO, SHARES ALL ITS ONLINE DEVICES WITH THE 195.
*     SINCE  ALL  DATASETS  USED  BY  TSO  MUST BE CATALOGED, THEY ARE
*     CATALOGED ON THE RESIDENT VOLUME FOR THE 75 TO ALLOW BATCH  JOBS
*     TO  ACCESS TSO DATASETS. THIS HAS PLACED AN EXTREMELY HEAVY LOAD
*     ON THIS SHARED CATALOG, WHICH OCCUPIES ABOUT 20 CYLINDERS  ON  A
*     2314.  AFTER  SOME  STUDY OF THE SITUATION IT WAS FOUND THAT THE
*     ORGANIZATION OF THE  SHARED  CATALOG  WAS  ONE  OF  OUR  BIGGEST
*     BOTTLENECKS.  AFTER  RUNNING COPYCAT, SOFTWARE MONITORING SHOWED
*     THE PERCENT OF UNIT BUSY TIME OF THE SYSTEM RESIDENT VOLUME  FOR
*     THE  75  DROPPED  FROM  ABOUT  80%  TO ABOUT 20%, THE PERCENT OF
*     ENQUEUE WAIT FOR THE CATALOG DROPPED FROM  ABOUT  10%  TO  ABOUT
*     .1%,  AND  THE  NUMBER OF EXCPS REQUIRED TO LOGON TO TSO DROPPED
*     FROM ABOUT 120 TO 70.
*
*     2.0 USER MANUAL
*
*     2.1 FUNCTIONS
*
*          TWO DIFFERENT TYPES OF CATALOG SYSTEMS CAN BE PROCESSED  BY
*     COPYCAT.  THE FIRST IS THE STANDARD OS CATALOG SYSTEM CONSISTING
*     OF THE MASTER CATALOG ON THE SYSTEM RESIDENT VOLUME AND ZERO  OR
*     MORE  CATALOGS  ON  OTHER  VOLUMES.  THE  SECOND TYPE OF CATALOG
*     SYSTEM DOES NOT HAVE A MASTER CATALOG, BUT INSTEAD HAS  A  TABLE
*     BUILT  INTO  THE  CATALOG MANAGEMENT ROUTINES USED TO SELECT THE
*     FIRST CATALOG TO BE SEARCHED. THE CHANGES NEEDED FOR  THIS  TYPE
*     OF CATALOG SYSTEM MUST BE LOCALLY WRITTEN.
*
*
*     2.1.1 REDISTRIBUTION
*
*          WHEN MORE THAN ONE CATALOG IS BEING CREATED, REDISTRIBUTION
*     PARAMETERS  ARE  REQUIRED.  THESE PARAMETERS ARE SUPPLIED ON THE
*     OUTPUT CARDS IN THE FORM OF A RANGE OF NAMES. THE UPPER BOUND IS
*     DEFINED  ON  EACH  OUTPUT  CARD,  AND THE RANGE EXTENDS FROM THE
*     UPPER BOUND OF THE PREVIOUS CARD TO THIS UPPER BOUND. AN  EBCDIC
*     COLLATING SEQUENCE IS USED.
*
*     2.1.2 RESTRUCTURING
*
*          THE MAIN FUNCTION OF COPYCAT IS TO RESTRUCTURE A CATALOG SO
*     AS  TO TAKE ADVANTAGE OF THE CATALOG MANAGEMENT I/O ROUTINES. TO
*     DO THIS THREE PARAMETERS MAY BE SPECIFIED FOR EACH INDEX  LEVEL.
*     THE  FIRST  IS  THE  NUMBER  OF BYTES TO LEAVE IN EACH BLOCK FOR
*     EXPANSION.  BY  LEAVING  ENOUGH  ROOM,  THE  CATALOG  MANAGEMENT
*     ROUTINES  WILL  BE ABLE TO ADD ENTRIES WITHOUT HAVING TO ADD NEW
*     BLOCKS. EACH CVOL  POINTER  TAKES  TWENTY-TWO  (22)  BYTES,  AND
*     SINGLE  VOLUME  DATASET  ENTRIES  TAKE  TWENTY-SIX  (26)  BYTES.
*     SECOND, THE NUMBER OF DUMMY BLOCKS TO BE LEFT AFTER AN INDEX MAY
*     BE SPECIFIED SO THE INDEX CAN EXPAND AND NEW LOWER LEVEL INDEXES
*     CAN BE ADDED IN CLOSE PROXIMITY. THIRD,  THE  NUMBER  OF  BLOCKS
*     THAT  MUST  BE  ON A TRACK BEFORE STARTING THE NEXT INDEX MAY BE
*     GIVEN. SINCE INDEXES MAY REQUIRE MORE THEN  ONE  BLOCK,  AND  AN
*     INDEX  CAN  BE  SEARCHED  IN  ONE  I/O OPERATION IF IT IS ON ONE
*     TRACK, THIS NUMBER SHOULD BE SET LARGE ENOUGH SO MOST INDEXES AT
*     A PARTICULAR LEVEL CAN FIT ON ONE TRACK.
*
*     2.1.3 MASTER AND SECONDARY CATALOGS
*
*          WHEN MORE THEN ONE  OUTPUT  CATALOG  IS  PRODUCED,  ONE  IS
*     USUALLY  DEFINED  AS  THE  MASTER  CATALOG,  AND  THE  OTHERS AS
*     SECONDARY CATALOGS. IN THE CASE OF ONLY ONE OUTPUT  CATALOG,  IT
*     IS  ALWAYS A MASTER CATALOG. COPYCAT MUST BE RUN ONCE TO PRODUCE
*     THE MASTER CATALOG IN MASTER MODE, AND IF  THERE  ARE  SECONDARY
*     CATALOGS, ONCE IN SECONDARY MODE.
*
*     2.1.4 MASTER MODE
*
*          WHEN COPYCAT IS OPERATING IN MASTER MODE, ONLY  THE  MASTER
*     CATALOG  IS  PRODUCED  BASED  ON THE OUTPUT MASTER CARD(S). CVOL
*     POINTERS FOR INDEXES COVERED BY THE OUTPUT SECONDARY  CARDS,  IF
*     ANY, ARE ALSO ADDED TO THE MASTER CATALOG.
*
*     2.1.5 SECONDARY MODE
*
*          SECONDARY MODE IS USED TO PRODUCE THE  SECONDARY  CATALOGS.
*     THE  CHOICE  OF  WHICH  INDEXES ARE PLACED ON WHICH CATALOGS ARE
*     DEFINED ON THE OUTPUT CARDS.
*
*     2.1.6 MODEL GENERATION DATA GROUP DSCBS
*
*          MODEL DSCBS FOR GENERATION DATA GROUPS  ARE  ALSO  HANDLED.
*     SINCE THE MODEL DSCB MUST RESIDE ON THE VOLUME THAT CONTAINS THE
*     GENERATION INDEX, COPYCAT WILL MOVE OR  COPY  THESE  DSCBS.  THE
*     CHOICE  OF  WHETHER TO MOVE OR COPY THE DSCBS SHOULD BE BASED ON
*     THE APPLICATION. WHEN A SINGLE CATALOG IS BEING REBUILT  ON  THE
*     SAME PHYSICAL VOLUME, NOTHING NEED BE DONE, SINCE THE DSCBS WILL
*     STILL BE ON THE VOLUME. WHEN A CATALOG  IS  BEING  COPIED  TO  A
*     REPLACEMENT  VOLUME, SUCH AS A NEW RELEASE OF A SYSTEM, THEN THE
*     DSCBS SHOULD BE COPIED. WHEN THE WHOLE CATALOG SYSTEM  IS  BEING
*     REORGINIZED, THEN THE DSCBS SHOULD BE MOVED.
*
*     2.1.7 ADDITIONAL CVOL PROCESSING
*
*          SINCE EVERY DIRECT ACCESS VOLUME CAN HAVE A CATALOG, OR ONE
*     SYSTEM MAY HAVE ACCESS TO ANOTHER SYSTEMS CATALOG, IT MAY BECOME
*     NECESSARY TO CHANGE MANY CVOL POINTERS  ON  ONE  SYSTEM  IF  THE
*     OTHER SYSTEMS CATALOG IS REDISTRIBULED. THE CVOL CARDS HAVE BEEN
*     PROVIDED TO ACCOMPLISH THIS.
*
*     2.1.8 ALIAS PROCESSING
*
*          SPECIAL HANDLING OF ALIAS  ENTRIES  HAS  BEEN  INCLUDED  IN
*     COPYCAT.  IN  A  MULTIPLE  CATALOG SYSTEM, AN ALIAS NAME AND ITS
*     TRUE NAME SOMETIMES RESIDE IN DIFFERENT CATALOGS. CVOL  POINTERS
*     WITH  THE  ALIAS  NAME ARE PLACED IN THE MASTER CATALOG DURING A
*     MASTER OPERATION AND IN A SECONDARY CATALOG DURING  A  SECONDARY
*     OPERATION, POINTING AT THE TRUE NAMES VOLUME. THE ALIAS ENTRY IS
*     THEN PLACED IN THE TRUE NAMES CATALOG.
*
*     2.1.9 MULTIPLE INPUTS
*
*          THIRTY-TWO (32) CATALOGS MAY BE DEFINED AS  INPUT.  COPYCAT
*     HAS BEEN DESIGNED TO WORK ON A CATALOG SYSTEM WHERE A HIGH LEVEL
*     INDEX NAME, OTHER THAN A CVOL POINTER, MAY ONLY  APPEAR  IN  ONE
*     CATALOG.  WHEN  A  CONFLICT  IS  FOUND, THE ENTRY FROM THE FIRST
*     DEFINED INPUT CATALOG IS USED. AN EXAMPLE OF THIS WOULD BE IN  A
*     TWO  CPU  SYSTEM,  AND  BOTH SYSTEM RESIDENT VOLUME CATALOGS ARE
*     DEFINED AS INPUT TO COPYCAT. IF BOTH CATALOGS HAVE  ENTRIES  FOR
*     SYS1, THEN ONLY THE ENTRIES FROM THE FIRST DEFINED CATALOG WOULD
*     BE USED.
*
*     2.2 CONTROL CARDS
*
*          THE FORMAT FOR ALL CONTROL CARDS IS:
*
*       ÝNAME¨ OPERATION OPERANDS COMMENTS
*
*          THE NAME FIELD, IF PRESENT, MUST BEGIN IN COLUMN  ONE.  THE
*     OPERATION  MUST  BE  SEPARATED  FROM THE NAME AND OPERANDS BY AT
*     LEAST ONE BLANK, AND MAY START ANYWHERE AFTER  COLUMN  ONE.  THE
*     OPERANDS MUST BE SEPARATED BY COMMAS WITH NO INTERVENING BLANKS.
*     A NULL OPERAND MUST BE DELIMITED BY COMMAS UNLESS IT IS THE LAST
*     OPERAND.  THE  COMMENTS  ARE  SEPARATED  FROM THE OPERANDS BY AT
*     LEAST ONE BLANK, AND ANY CARD WITH AN ASTERISK '*' IN COLUMN ONE
*     IS TREATED AS A COMMENT CARD.
*
*     2.2.1 CONTROL
*
*          THE  CONTROL  CARD  SPECIFIES  THE  MAIN  FUNCTIONS  TO  BE
*     PERFORMED, AND MUST BE THE FIRST NON-COMMENT CARD.
*
*       ÝNAME¨ CONTROL »MASTER|SECONDARYº,»GDGMOVE|GDGCOPYº,
*               »KEEP|SPLIT|UNCATLGº
*
*          MASTER|SECONDARY - COPYCAT IS TO BE EXECUTED IN THE  MASTER
*                    MODE  TO  PRODUCE  THE  MASTER CATALOG, OR IN THE
*                    SECONDARY MODE TO PRODUCE THE SECONDARY CATALOGS.
*
*          GDGMOVE|GDGCOPY - GENERATION DATA GROUP MODEL DSCBS ARE  TO
*                    BE  MOVED OR COPIED. IF THIS OPERAND IS NULL THEN
*                    THEY ARE NOT PROCESSED.
*
*          KEEP|SPLIT|UNCATLG - THE FUNCTION TO BE PERFORMED ON SINGLE
*                    NAME  DATASETS.  THEY  MAY  BE KEPT IN THE MASTER
*                    CATALOG (KEEP),  THEY  MAY  BE  REDISTRIBUTED  TO
*                    SECONDARY   CATALOGS  (SPLIT),  OR  THEY  MAY  BE
*                    UNCATALOGED (UNCATLG).
*
*          WHEN  COPYCAT  IS  RUN  TWICE  FOR  A  MASTER  CATALOG  AND
*     SECONDARY  CATALOGS,  THESE LAST TWO OPERANDS SHOULD BE THE SAME
*     FOR BOTH RUNS. IF LOCAL MODIFICATIONS TO THE CATALOG  MANAGEMENT
*     ROUTINES  HAVE  BEEN  ADDED SO A MASTER CATALOG IS NOT REQUIRED,
*     COPYCAT NEED ONLY BE RUN ONCE IN SECONDARY MODE, AND  THE  THIRD
*     CONTROL CARD PARAMETER MUST BE SPLIT.
*
*     2.2.2 INPUT
*
*          THE INPUT CARD DEFINES AN INPUT CATALOG  TO  BE  PROCESSED.
*     THERE  MUST BE AT LEAST ONE AND AS MANY AS THIRTY-TWO (32) INPUT
*     CARDS. THE INPUT DATASETS  DEFINED  BY  THESE  CARDS  ARE  NEVER
*     CHANGED  BY THE PROGRAM. THEY MUST NOT BE THE SAME DATASETS USED
*     FOR OUTPUT. THE INPUT CARD(S) MUST FOLLOW THE CONTROL CARD.
*
*       ÝNAME¨ INPUT DDNAME,VOLID
*
*          DDNAME - THE NAME OF A DD STATEMENT THAT DEFINES THIS INPUT
*                    DATASET.
*
*          VOLID - THE VOLUME NAME ON WHICH THIS CATALOG RESIDES  WHEN
*                    USED  BY  THE SYSTEM. THIS VOLUME NEED NOT BE THE
*                    SAME AS THAT DEFINED BY DDNAME.
*
*     2.2.3 OUTPUT
*
*          THE  OUTPUT  CARDS  DEFINE  THE  OUTPUT  DATASETS  AND  THE
*     REDISTRIBUTION TABLE. THESE CARDS MUST FOLLOW THE INPUT CARDS.
*
*       ÝNAME¨ OUTPUT »MASTER|SECONDARYº,DDNAME,VOLIDÝ,MAXNAME¨
*
*          MASTER - THIS CARD DEFINES THE MASTER CATALOG, OR THE  ONLY
*                    CATALOG IF ONLY ONE IS BEING PRODUCED.
*
*          SECONDARY - THIS CARD DEFINES A SECONDARY CATALOG.
*
*          DDNAME - THE NAME OF A DD STATEMENT THAT DEFINES THE OUTPUT
*                    DATASET.
*
*          VOLID - THE VOLUME NAME ON WHICH THIS CATALOG WILL BE  USED
*                    BY  THE SYSTEM. THE CATALOG MAY BE PLACED ON SOME
*                    OTHER   VOLUME   TEMPORARILY   FOR   BACKUP    OR
*                    CONVENIENCE.
*
*          MAXNAME - THE HIGHEST NAME IN EBCDIC COLLATING SEQUENCE  TO
*                    WHICH THIS OUTPUT CARD APPLIES. THIS MAXNAME MUST
*                    BE GREATER  THEN  THE  MAXNAME  ON  THE  PREVIOUS
*                    OUTPUT  CARD, AND ON THE LAST OUTPUT CARD IT MUST
*                    BE NULL.
*
*          EACH OUTPUT CARD DEFINES A RANGE OF INDEX NAMES THAT ARE TO
*     BE  PLACED  IN ITS CATALOG. THIS RANGE EXTENDS FROM THE PREVIOUS
*     MAXNAME UP TO AND INCLUDING THE CURRENT MAXNAME. THE SAME DDNAME
*     AND  VOLID  PAIR  MAY  APPEAR ON MORE THEN ONE CARD. THIS ALLOWS
*     MORE THEN ONE RANGE OF INDEX NAMES TO BE PLACED ON ONE  CATALOG.
*     ALL MASTER CARDS MUST HAVE THE SAME DDNAME AND VOLID SINCE THERE
*     CAN ONLY BE ONE MASTER CATALOG. THE LAST OUTPUT CARD MUST HAVE A
*     NULL MAXNAME.
*
*     2.2.4 CVOL
*
*          THE CVOL CARD MAY BE USED TO ALTER CONTROL  VOLUME  POINTER
*     ENTRIES THAT POINT TO CATALOGS OUTSIDE THE CURRENT SYSTEM.
*
*       ÝNAME¨ CVOL ÝVOLID¨,NEWVOL,ÝMAXNAME¨,DDNAME
*
*          VOLID - THE VOLUME NAME THAT IS BEING CHANGED.
*
*          NEWVOL - THE NEW VOLUME NAME FOR THE CVOL ENTRY.
*
*          MAXNAME - THE HIGHEST NAME IN  THE  RANGE  OF  INDEX  NAMES
*                    COVERED  BY THIS CVOL CARD. ALL THE CVOL POINTERS
*                    THAT POINT AT THIS VOLUME, WHOSE NAME IS  GREATER
*                    THAN  THE PREVIOUS MAXNAME AND LESS THEN OR EQUAL
*                    TO THIS MAXNAME, WILL  BE  CHANGED  TO  POINT  AT
*                    NEWVOL.  THE  LAST CVOL CARD IN A SET MUST HAVE A
*                    NULL VALUE FOR MAXNAME, WHICH IS THE  HIGHEST  IN
*                    THE COLLATING SEQUENCE.
*
*          DDNAME - THE NAME OF A DD STATEMENT USED  FOR  DEVICE  TYPE
*                    INFORMATION. THIS DD STATEMENT MUST DEFINE A UNIT
*                    OF THE SAME TYPE AS NEWVOL.
*
*          MANY SETS OF CVOL CARDS MAY BE DEFINED. EACH SET MUST  HAVE
*     VOLID  SPECIFIED  ON THE FIRST CARD, AND MAXNAME MUST BE NULL ON
*     THE LAST. CVOL CARDS MUST FOLLOW THE OUTPUT CARDS.
*
*     2.2.5 LEVEL
*
*          THE  LEVEL  CARD  IS  USED  TO  SPECIFY  THE  RESTRUCTURING
*     INFORMATION.  THE GREATEST PERFORMANCE IMPROVEMENTS DERIVED FROM
*     RUNNING COPYCAT DEPEND ON PROPERLY CHOOSING THESE PARAMETERS. UP
*     TO TWENTY-TWO LEVEL CARDS MAY BE DEFINED.
*
*       ÝNAME¨ LEVEL LEVEL,BYTES,DUMMIES,BLOCKS
*
*          LEVEL - THE INDEX LEVEL BETWEEN ONE AND TWENTY-TWO TO WHICH
*                    THIS CARD APPLIES.
*
*          BTYES - THE NUMBER OF BYTES TO  LEAVE  IN  EACH  BLOCK  FOR
*                    FUTURE EXPANSION, BETWEEN 0 AND 200.
*
*          DUMMIES - THE NUMBER OF DUMMY BLOCKS TO  LEAVE  AFTER  THIS
*                    INDEX FOR FUTURE EXPANSION.
*
*          BLOCKS - THE NUMBER OF BLOCKS THAT MUST BE ON  THE  CURRENT
*                    OUTPUT TRACK BEFORE STARTING THIS INDEX.
*
*          THE LEVEL CARDS MUST FOLLOW  THE  CVOL  CARDS,  IF  ANY.  A
*     DEFAULT VALUE OF ZERO IS APPLIED FOR ANY NULL OR MISSING VALUES.
*
*     2.3 JOB CONTROL STATEMENTS
*
*          A JOB CARD AND AN EXEC CARD ARE  REQUIRED.  A  SYSPRINT  DD
*     STATEMENT  IS REQUIRED FOR A LISTING OF THE INPUT, INFORMATIONAL
*     MESSAGES AND ERROR MESSAGES. THE DCB  PARAMETERS  ARE  FIXED  AT
*     RECFM=FBA,LRECL=605,BLKSIZE=121. THE CONTROL CARDS ARE READ FROM
*     THE   SYSIN   FILE   WHICH   HAS   FIXED   DCB   PARAMETERS   OF
*     RECFM=FB,LRECL=80,BLKSIZE=3200.  ADDITIONAL  DD  STATEMENTS  ARE
*     REQUIRED AS SPECIFIED BY THE INPUT, OUTPUT AND CVOL  CARDS.  THE
*     DATASETS  DEFINED  BY  THESE  DD  STATEMENTS  NEED  NOT BE NAMED
*     SYSCTLG, BUT THEY MUST BE  ON  A  DIRECT  ACCESS  VOLUME.  IF  A
*     CATALOG  IS  CREATED  ON A VOLUME OTHER THEN THE ONE ON WHICH IT
*     WILL BE USED BY THE SYSTEM, THE TEMPORARY VOLUME  SHOULD  BE  OF
*     THE SAME DEVICE TYPE AS THE FINAL VOLUME.
*
*
*     2.4 MESSAGES AND CODES
*
*          AFTER A SUCCESSFUL RUN OF COPYCAT CERTAIN STATISTICS  ABOUT
*     EACH  OUTPUT  DATASET  ARE  LISTED. THE LAST TTR PLUS ONE IN THE
*     DATASET AND THE TTR OF THE LAST USED BLOCK PLUS ONE  ARE  LISTED
*     IN  HEXADECIMAL.  THE  NUMBER OF CONTROL VOLUME POINTER ENTRIES,
*     DUMMY BLOCKS,  BLOCKS  AT  LEVELS  ONE  AND  TWO,  AND  DATASETS
*     CATALOGED ARE LISTED IN DECIMAL.
*
*          THE FOLLOWING IS A LIST OF ERROR  MESSAGES  AND  COMPLETION
*     CODES. THE MAXIMUM COMPLETION CODE FOUND DURING EXECUTION OF THE
*     PROGRAM IS RETURNED TO OS.
*
*     ERR01 ERROR FOUND IN PARAMETER AT '$'
*
*          AN ERROR WAS FOUND IN A PARAMETER OR A  REQUIRED  PARAMETER
*          WAS MISSING. THE PREVIOUS LINE WILL CONTAIN A '$' UNDER THE
*          ERROR.
*          (COMPLETION CODE = 8)
*
*     ERR02 ERROR PROCESSING DDNAME='DDNAME'
*
*          A PROBLEM WAS FOUND WITH A DD STATEMENT. EITHER IT WAS  NOT
*          FOUND,  COULD  NOT BE OPENED, OR WAS NOT ON A DIRECT ACCESS
*          VOLUME.
*          (COMPLETION CODE = 8)
*
*     ERR03 UNEXPECTED END OF FILE ON SYSIN
*
*          MORE CONTROL CARDS WERE EXPECTED, BUT WERE NOT FOUND.
*          (COMPLETION CODE = 8)
*
*     ERR04 NAME=TRUENAME NOT FOUND FOR ALIAS=ALIASNAME
*
*          WHILE PROCESSING AN ALIAS ENTRY,  THE  TRUE  NAME  FOR  THE
*          ALIAS COULD NOT BE FOUND.
*          (COMPLETION CODE = 12)
*
*     ERR05 ALIAS COUNT ERROR FOUND FOR NAME=TRUENAME
*
*          THE ALIAS COUNT FIELD IN THE NAMED INDEX IS  NOT  EQUAL  TO
*          THE NUMBER OF ALIASES FOUND FOR THE NAME.
*          (COMPLETION CODE = 12)
*
*     ERR06 TOO MANY INPUT DATASETS
*
*          MORE THEN 32 INPUT CARDS WERE FOUND.
*          (COMPLETION CODE = 8)
*
*     ERR07 INDEX=INDEX FOUND ON VOLS=(X,Y) THE ENTRY  ON  X  WILL  BE
*     USED
*
*          THE SAME HIGH  LEVEL  INDEX  WAS  FOUND  ON  TWO  DIFFERENT
*          CATALOGS. THE ONE ON THE X VOLUME WILL BE USED.
*          (COMPLETION CODE = 4)
*
*     ERR08 NO INPUT CARDS FOUND
*
*          NO INPUT CARDS WERE FOUND,  OR  THEY  DID  NOT  FOLLOW  THE
*          CONTROL CARD.
*          (COMPLETION CODE = 8)
*
*     ERR09 FUNCTION FAILED  WITH  (CODE)  ON  VOLUME  FOR  GDG  MODEL
*     DSCB=NAME
*
*          WHILE MOVING OR COPYING MODEL DSCBS, AN OBTAIN, ALLOCATE OR
*          SCRATCH  OPERATION  FAILED  WITH  THE RETURN CODE OF 'CODE'
*          (DECIMAL).
*          (COMPLETION CODE = 4)
*
*     ERR10 MODEL DSCB FOR GDG DOES NOT HAVE ZERO EXTENTS DSN=NAME
*
*          THE NAMED MODEL DSCB IS NOT COPIED BECAUSE IT DOES NOT HAVE
*          ZERO EXTENTS.
*          (COMPLETION CODE = 4)
*
*     ERR11 MODEL DSCB FOR GDG EXISTS ON OUTPUT VOLUME WITH  DIFFERENT
*     ATTRIBUTES FOR DSN=NAME
*
*          WHILE MOVING OR COPYING MODEL DSCBS A DSCB  WITH  THE  SAME
*          NAME  WAS  FOUND  ON  THE OUTPUT VOLUME, BUT THE ATTRIBUTES
*          DEFINED BY THE TWELVE  BYTES  IN  THE  DSCB  AT  OFFSET  82
*          (DECIMAL)  ARE  NOT  THE  SAME.  THE  DSCB  IS NOT MOVED OR
*          COPIED.
*          (COMPLETION CODE = 4)
*
*     ERR12 ABEND - NNN - OCCURED. SEE SYSMSG. CURRENT OR LAST NAME IS
*     NAME
*
*          A D37, B37 OR 213 ABEND OCCURED. THE LAST  INDEX  STRUCTURE
*          BEING PROCESSED IS GIVEN.
*          (COMPLETION CODE = 16)
*
*     ERR13 I/O ERROR - SYNAD MESSAGE
*     ERR13 I/O ERROR CURRENT OR LAST NAME IS NAME
*
*          AN I/O ERROR OCCURED. THE STANDARD MESSAGE  PRODUCED  BY  A
*          SYNAD  MACRO  IS GIVEN, ALONG WITH THE LAST INDEX STRUCTURE
*          PROCESSED.
*          (COMPLETION CODE = 16)
*
*     ERR14 NO OUTPUT MASTER CARD WAS FOUND
*
*          WHEN IN MASTER MODE, AT LEAST ONE OUTPUT MASTER  CARD  MUST
*          BE DEFINED.
*          (COMPLETION CODE = 8)
*
*     THE COMPLETION CODES ARE AS FOLLOWS:
*
*         0 - NO ERRORS.
*
*         4 - ALL CATALOGS ARE USEABLE, BUT DUPLICATE INDEXES HAVE
*             BEEN  LEFT  OUT  OR  MODEL  DSCBS HAVE NOT BEEN MOVED OR
*             COPIED.
*
*         8 - ERRORS IN PROCESSING CONTROL CARDS. THE OUTPUT DATASETS
*             HAVE NOT BEEN CHANGED.
*
*  12 - ALL CATALOGS ARE USEABLE, BUT PROBLEMS EXIST WITH ALIAS
*             ENTRIES.
*
*  16 - OUTPUT CATALOGS HAVE BEEN PARTLY BUILT, AND ARE NOT
*             USEABLE.
*
*     2.5 EXAMPLES
*
*     2.5.1 COPY A CATALOG FROM ONE VOLUME TO ANOTHER
*
*          THIS FIRST EXAMPLE  ILLUSTRATES  THE  MOST  COMMON  USE  OF
*     COPYCAT.  A  CATALOG  ON  ONE  VOLUME IS TO BE COPIED TO ANOTHER
*     VOLUME, SUCH AS WHEN A NEW SYSTEM IS BEING INSTALLED.  THIS  JOB
*     WOULD  BE RUN UNDER THE OLD SYSTEM TO COPY THE CATALOG ON SYSRES
*     TO THE NEW SYSTEM PACK, NEWRES, WHICH WILL  LATER  BE  RELABELED
*     SYSRES.
*
*     //EX1    JOB   XXX
*     //       EXEC  PGM=COPYCAT,REGION=60K
*     //SYSPRINT DD  SYSOUT=A
*     //OLDRES DD    DSN=SYSCTLG,VOL=SER=SYSRES,
*     //             DISP=OLD,UNIT=DISK
*     //NEWRES DD    DSN=SYSCTLG,VOL=SER=NEWRES,
*     //             DISP=OLD,UNIT=DISK
*     //SYSIN  DD    *
*     *              EXAMPLE ONE
*              CONTROL MASTER,GDGCOPY,KEEP
*              INPUT OLDRES,SYSRES
*              OUTPUT MASTER,NEWRES,SYSRES
*              LEVEL 1,26,100
*              LEVEL 2,52,5,5
*
*          IN THIS EXAMPLE MODEL DSCBS FOR GENERATION DATA GROUPS  ARE
*     COPIED. 100 DUMMY BLOCKS ARE LEFT AFTER THE HIGHEST LEVEL INDEX,
*     WITH 26 BYTES LEFT IN EACH BLOCK FOR FUTURE EXPANSION. 52  BYTES
*     ARE  LEFT  IN  EACH  SECOND LEVEL INDEX BLOCK, 5 BLOCKS ARE LEFT
*     AFTER EACH INDEX, AND A SECOND LEVEL INDEX WILL NOT  BE  STARTED
*     ON A TRACK WITHIN 5 BLOCKS OF THE END.
*
*     2.5.2 BACK UP A CATALOG
*
*          THIS EXAMPLE DEMONSTRATES THE USE OF COPYCAT FOR BACKING UP
*     A  CATALOG.  NOTE  THAT  THE  BACKUP DATASET DOES NOT NEED TO BE
*     CALLED SYSCTLG, THUS MANY CATALOGS COULD BE  BACKED  UP  ON  THE
*     SAME VOLUME.
*
*     //EX2    JOB   XXX,REGION=60K
*     //       EXEC  PGM=COPYCAT
*     //SYSPRINT DD  SYSOUT=A
*     //CATALOG DD   DSNAME=SYSCTLG,VOL=SER=PACK01,
*     //             UNIT=DISK,DISP=OLD
*     //BACKUP DD    DSNAME=CAT.PACK01,VOL=SER=BACKUP,
*     //             UNIT=DISK,SPACE=(CYL,(3,2)),DISP=(NEW,KEEP)
*     //SYSIN  DD    *
*     *              EXAMPLE 2
*              CONTROL MASTER,GDGCOPY,KEEP
*              INPUT CATALOG,PACK01
*              OUTPUT MASTER,BACKUP,PACK01
*     /*
*
*     2.5.3 REDISTRIBUTE A CATALOG
*
*          IN THIS EXAMPLE A SINGLE CATALOG IS TO  BE  SPLIT  UP  INTO
*     FOUR  SEPARATE  CATALOGS.  THE FIRST STEP PRODUCES THE SECONDARY
*     CATALOGS, AND THE SECOND STEP PRODUCES THE MASTER  CATALOG.  THE
*     LAST  STEP IS EXECUTED ONLY IF THE FIRST TWO STEPS RAN PROPERLY,
*     AND IT RENAMES THE DATASET ON RES075.
*
*     //EX3    JOB   XXX
*     //STEP1  EXEC  PGM=COPYCAT,REGION=60K
*     //SYSPRINT DD  SYSOUT=A
*     //INPUT  DD    VOL=SER=RES075,UNIT=DISK,DISP=OLD,
*     //             DSNAME=SYSCTLG
*     //OLDRES DD    VOL=SER=RES075,UNIT=DISK,
*     //             DSNAME=NEWCAT,DISP=(NEW,KEEP),
*     //             SPACE=(CYL,(5,2))
*     //P87    DD    UNIT=DISK,VOL=SER=PACK87,
*     //             DSNAME=SYSCTLG,DISP=(NEW,KEEP),
*     //             SPACE=(CYL,(5,2))
*     //P88    DD    UNIT=DISK,VOL=SER=PACK88,
*     //             DSNAME=SYSCTLG,DISP=(NEW,KEEP),
*     //             SPACE=(CYL,(5,2))
*     //P89    DD    UNIT=DISK,VOL=SER=PACK89,
*     //             DSNAME=SYSCTLG,DISP=(NEW,KEEP),
*     //             SPACE=(CYL,(5,2))
*     //SYSIN  DD    *
*     *              EXAMPLE 3
*     *              PRODUCE THE SECONDARY CATALOGS
*              CONTROL SECONDARY,GDGMOVE,KEEP
*              INPUT INPUT,RES075
*              OUTPUT SECONDARY,P87,PACK87,B9999999
*              OUTPUT SECONDARY,P88,PACK88,H9999999
*              OUTPUT MASTER,OLDRES,RES075,K9999999
*              OUTPUT SECONDARY,P89,PACK89,R9999999
*              OUTPUT MASTER,OLDRES,RES075,S9999999
*              OUTPUT SECONDARY,P88,PACK88
*              LEVEL 1,26,100
*              LEVEL 2,54,5,5
*              LEVEL 3,,2,2
*     /*
*     //STEP2  EXEC  PGM=COPYCAT,REGION=60K
*     //SYSPRINT DD  SYSOUT=A
*     //INPUT  DD    VOL=SER=RES075,UNIT=DISK,DISP=OLD,
*                    DSNAME=SYSCTLG
*     //OLDRES DD    VOL=SER=RES075,UNIT=DISK,
*                    DSNAME=NEWCAT,DISP=OLD
*     //P87    DD    UNIT=DISK,VOL=SER=PACK87,
*                    DSNAME=SYSCTLG,DISP=OLD
*     //P88    DD    UNIT=DISK,VOL=SER=PACK88,
*                    DSNAME=SYSCTLG,DISP=OLD
*     //P89    DD    UNIT=DISK,VOL=SER=PACK89,
*                    DSNAME=SYSCTLG,DISP=OLD
*     //SYSIN  DD    *
*     *              EXAMPLE 3
*     *              PRODUCE THE MASTER CATALOG
*              CONTROL MASTER,GDGMOVE,KEEP
*              INPUT INPUT,RES075
*              OUTPUT SECONDARY,P87,PACK87,B9999999
*              OUTPUT SECONDARY,P88,PACK88,H9999999
*              OUTPUT MASTER,OLDRES,RES075,K9999999
*              OUTPUT SECONDARY,P89,PACK89,R9999999
*              OUTPUT MASTER,OLDRES,RES075,S9999999
*              OUTPUT SECONDARY,P88,PACK88
*              LEVEL 1,26,50
*              LEVEL 2,54,10,10
*     /*
*     //STEP3  EXEC  PGM=IEHPROGM,COND=(5,LT)
*     //SYSPRINT DD  SYSOUT=A
*     //OLDRES DD    UNIT=DISK,VOL=SER=RES075,DISP=OLD
*     //SYSIN  DD    *
*              RENAME VOL=2314=RES075,DSNAME=SYSCTLG,
*                    NEWNAME=OLDCATLG
*              RENAME VOL=2314=RES075,DSNAME=NEWCAT,
*                    NEWNAME=SYSCTLG
*     /*
*
*          ALL INDEXES UP TO C WILL BE ON PACK87, INDEXES FROM C TO  I
*     AND FROM T UPWARDS WILL BE ON PACK88, AND FROM L TO S WILL BE ON
*     PACK89. CVOL POINTERS FOR THESE INDEXES WILL BE PLACED ON RES075
*     ALONG  WITH  ALL  THE  OTHER INDEXES. STEP 3 WILL BE EXECUTED IF
*     THERE ARE NO SEVERE ERRORS.
*
*     3.0 PROGRAM LOGIC
*
*          COPYCAT IS WRITTEN ENTIRELY IN ASSEMBLER LANGUAGE. QSAM  IS
*     USED FOR SYSPRINT AND SYSIN, WHILE BSAM IN THE LOAD MODE IS USED
*     FOR THE OUTPUT DATASETS. BDAM IS  USED  FOR  THE  INPUT  AND  TO
*     ACCESS PREVIOUSLY WRITTEN OUTPUT BLOCKS.
*
*          THE HEART OF THE PROGRAM CONSISTS OF  A  RECURSIVE  ROUTINE
*     CALLED  RR, WHICH IS CALLED FOR EACH LEVEL. THE TTR OF THE FIRST
*     BLOCK OF THE LEVEL IS PASSED TO RR. AFTER AN  INDEX  IS  WRITTEN
*     WITH  THE OLD TTRS, IT IS REREAD AND FOR EACH ENTRY WITH A LOWER
*     LEVEL, RR IS RECURSIVELY CALLED. UPON RETURN,  THE  NEW  TTR  IS
*     PLACED IN THE ENTRY AND THE BLOCK IS REWRITTEN.
*
*          WHEN PROCESSING THE HIGHEST  LEVEL  INDEX,  THE  ADDITIONAL
*     INFORMATION  OF  WHICH  INPUT  DATASET THIS ENTRY CAME FROM MUST
*     ALSO BE SAVED. THIS IS DONE BY CHANGING THE COUNT FIELD  OF  THE
*     INDEX.  ONLY THREE TYPES OF ENTRIES NEED THIS INFORMATION SAVED:
*     THE INDEX POINTER ENTRY, THE VOLUME CONTROL BLOCK POINTER ENTRY,
*     AND THE GENERATION INDEX POINTER ENTRY. BY MAKING THE LAST THREE
*     BITS OF THE VOLUME CONTROL BLOCK POINTER ENTRY '110',  ONLY  THE
*     ENTRIES  WITH  A LAST BIT OF '0' CONTAIN ADDITIONAL INFORMATION.
*     TWO BITS ARE USED TO DISTINGUISH  BETWEEN  THE  THREE  TYPES  OF
*     ENTRIES,  AND  THE  OTHER FIVE BITS IN THE LENGTH FIELD ARE THEN
*     USED TO LOCATE THE PROPER INPUT DATASET.
*
*          WHEN PROCESSING  MODEL  DSCBS,  THE  OBTAIN,  ALLOCATE  AND
*     SCRATCH   SVCS   ARE   USED.   BEFORE   RUNNING  COPYCAT,  LOCAL
*     RESTRICTIONS ON ALLOCATING AND  SCRATCHING  DATASETS  SHOULD  BE
*     CONSIDERED.  THESE  SVCS  ARE  ONLY  USED  IN THE ROUTINE CALLED
*     GDSPROC.
*
*          TWO OTHER IMPORTANT ROUTINES ARE INR AND  PUTC.  INR  FINDS
*     THE  NEXT NAME IN THE HIGHEST LEVEL INDEXES TO BE PROCESSED FROM
*     AMONG ALL THE INPUTS. IT IS CALLED BY  RR  AND  IT  HANDLES  THE
*     PROBLEM  OF DUPLICATE INDEX NAMES. PUTC IS ALSO CALLED BY RR AND
*     IT TAKES AN INDEX AND ADDS IT TO THE NEXT BLOCK FOR OUTPUT. PUTC
*     PROCESSES THE PARAMETERS OF THE LEVEL CARDS.
*
*
         EJECT
***********************************************************************
*              EQUATE STATEMENTS                                      *
***********************************************************************
*              GENERAL
NOP      EQU   0 -                 NO OPERATION
FF       EQU   X'FF' -             ALL BITS ON
*              AFTER COMPARE INSTRUCTIONS
GT       EQU   2 -                 A HIGH
LT       EQU   4 -                 A LOW
NE       EQU   7 -                 A NOT EQUAL B
EQ       EQU   8 -                 A EQUAL B
GE       EQU   11 -                A NOT LOW
LE       EQU   13 -                A NOT HIGH
*              AFTER ARITHMETIC INSTRUCTIONS
OV       EQU   1 -                 OVERFLOW
PLUS     EQU   2 -                 PLUS
MINUS    EQU   4 -                 MINUS
NZERO    EQU   7 -                 NOT ZERO
ZERO     EQU   8 -                 ZERO
ZEROS    EQU   8 -                 ZERO
NMINUS   EQU   11 -                NOT MINUS
NPLUS    EQU   13 -                NOT PLUS
*              AFTER TEST UNDER MASK INSTRUCTIONS
ALLON    EQU   1 -                 ALL ON
MIXED    EQU   4 -                 MIXED
NALLOFF  EQU   5 -                 ALLON+MIXED
ALLOFF   EQU   8 -                 ALL OFF
NALLON   EQU   12 -                ALLOFF+MIXED
         EJECT
***********************************************************************
*              REGISTER EQUIVALENTS                                   *
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 2
         EJECT
*---------------------------------------------------------------------*
*              CATALOG ENTRIES
*---------------------------------------------------------------------*
CATENTRY DSECT
CATNAME  DS    CL8                 NAME FIELD
CATTTR   DS    XL3                 TTR
CATCOUNT DS    X                   COUNT (HALFWORDS THAT FOLLOW
CATVAR   DS    0X                  VARIABLE PARTS
*              VICE - VOLUME INDEX CONTROL ENTRY
CVICE    EQU   5                   COUNT FOR VOLUME INDEX CONTROL
CVICUP   DS    XL3                 LAST BLOCK ADDR
         DS    X'00'
CVICFRST DS    XL3                 FIRST AVAILABLE BLOCK
         DS    X'00'
CVICULB  DS    XL2                 COUNT OF BYTES IN LAST BLOCK
*              ICE - INDEX CONTROL ENTRY
         ORG   CATVAR
CICE     EQU   3                   CUUNT FOR ICE
CICEILL  DS    XL3                 TTR OF THIS BLOCK
CICEALIA DS    X                   # OF ALIASES
CICEUBL  DS    XL2                 # OF UNUSED BYTES IN LAST BLOCK
*              ILE - INDEX LINK ENTRY
         ORG   CATVAR  -NAME HAS FF
CILE     EQU   0                   COUNT FOR INDEX LINK ENTRY
*              IPE - INDEX POINTER ENTRY
         ORG   CATVAR
CIPE     EQU   0
*              DSPE  - DATA SET POINTER ENTRY
         ORG   CATVAR
CDSPE    EQU   7                   6*M+1 (M 1 THRU 5)
CDSPEVC  DS    XL2                 #OF VOLUMES (M)
CDSPEVE  DS    0XL12               FIRST ENTRY
*              VCBPE - VOLUME CONTROL BLOCK POINTER ENTRY
         ORG   CATVAR
CVCBPE   EQU   1                   TTR POINTS AT VCB
         DS    XL2'00'             UNUSED- ZERO
*              VCPE - CONTROL VOLUME POINTER ENTRY
         ORG   CATVAR
CCVPE    EQU   3
CCVPEVOL DS    CL6                CVOL NAME
*              NCVPE  NEW CONTROL VOLUME POINTER ENTRY
         ORG   CATVAR
CNCVPE   EQU   5
CNCVPEDT DS    XL4                 DEVICE TYPE CODE
CNCVPEVO DS    CL6                 CVOL NAME
*              AE - ALIAS ENTRY
         ORG   CATVAR
CAE      EQU   4
CAENAME  DS    CL8                 TRUE NAME
*              GIPE - GENERATION INDEX POINTER ENTRY
         ORG   CATVAR
CGIPE    EQU   2
CGIPEFLA DS    X                   FLAG -EMPTY,DELETE
CGIPEMAX DS    X                   MAX # OF GENERATIONS
CGIPECUR DS    XL2                 CURRENT # OF ENTRIES
*              VCB - VOLUME CONTROL BLOCK
         ORG   CATNAME
CVCNUM   DS    XL2                 #OF VOLUMES
CVCBVOLI DS    XL240               FIRST 20 ENTRIES
         DS    XL10                ZEROS
CVCBNEXT DS    XL3                 TTR OF NEXT VCB IF CVCBNUM >20
         DS    X                   ZEROS
*              USED FOR SPLITING AND OUTPUT
         EJECT
VOLLIST  DSECT
         PRINT NOGEN
VOLLDCB  DCB   DSORG=PS,DDNAME=X,MACRF=(E),DEVD=DA,RECFM=F       EXCP01
         DS    0F
VOLLDCBE EQU   *
         PRINT GEN
VOLLMBLK DS    H                   # OF BLOCKS/TRK               EXCP01
VOLLTTR  DS    XL3                 NEXT TTR FOR OUTPUT           EXCP01
VOLLNEXT DS    A                   NEXT VOLLIST
VOLLNAME DS    CL8                 MAX NAME FOR THIS ENTRY
VOLLRNXT DS    A                   REAL VOLLIST ENTRY TO USE FOR IO
VOLALCHN DS    A                   ALIAS NAME CHAIN
VOLANCHN DS    A                   REAL NAME FOR ALIAS CHAIN
VOLLFRST DS    XL3                 FIRST AVAILABLE BLOCK
VOLLVOL  DS    CL6
VOLLFLAG DS    X                   FLAGS
VOLLKEEP EQU   X'80'               1=KEEP 0=SPLIT
VOLLSECN EQU   X'40'               NEXT WRITE NEEDS NEW EXTENT
VOLLDEVT DS    F                   DEVICE TYPE
         DS    F                   MAX BLK SIZE
         DS    H                   #OF CYL
VOLLTRKP DS    H                   TRK / CYL
VOLLTRKL DS    H                   TRK LENGTH
VOLLBOH  DS    X                   BLOCK OVER HEAD
VOLLBOHL DS    X                   BLOCK OVER HEAD  LAST BLOCK
         DS    X                   BLOCK OVER HEAD  NOT KEYED
VOLLDFLG DS    X                   FLAG
VOLLTOL  EQU   X'01'               TOLERANCE FACTOR NEEDED
VOLLBOHH EQU   X'08' -             BLOCK OERHEAD IS HALF WORD    F00003
VOLLTOLF DS    H                   TOLERANCE FACTOR
         DS    0F                  PCAREA USED FOR HIGH INDEX
VOLLITTR DS    XL3
         DS    X
VOLLIKEY DS    CL8
VOLLIBUF DS    XL256
VOLLEXLT DS    A                   EXIT LIST FOR DCB
VOLLJFCB DS    XL176               JFCB
VOLLICVL DS    H                   # OF CVOL POINTERS
VOLLIDUM DS    H                   # OF DUMMY BLOCKS
VOLLI1   DS    H                   # OF BLOCKS AT LEVEL 1
VOLLI2   DS    H                   # OF BLOCKS AT LEVEL 2
VOLLDS   DS    F                   # OF DATA SETS
VOLLEND  DS    0F
VOLLSIZE EQU   VOLLEND-VOLLIST     SIZE OF ENTRY
         EJECT
*---------------------------------------------------------------------*
*              INPUT DD WORK AREA
*---------------------------------------------------------------------*
INLIST   DSECT
         PRINT NOGEN
INDCB    DCB   DSORG=PS,DDNAME=X,MACRF=(E),DEVD=DA,RECFM=F       EXCP01
INDCBE   EQU   *
         PRINT GEN
INMBLK   DS    H                   # OF BLOCKS PER TRACK         EXCP01
INOFF    DS    H                   OFFSET TO NEXT ENTRY
INTTR    DS    XL3                 TTR OF THIS RECORD
ININDEX  DS    X                   INDEX NUMBER
INVOL    DS    CL6                 VOLUME THAT THIS DATA SET IS ON
INKEY    DS    CL8                 KEY
INBUF    DS    CL256               BUFFER
INEND    EQU   *
INSIZE   EQU   INEND-INLIST
         EJECT
LEVTABE  DSECT
LEVBYTES DS    H                   BYTES TO LEAVE IN EACH BLOCK
LEVBLOCK DS    H                   BLOCKS TO LEAVE AT END OF INDEX
LEVBLONT DS    H                   BLOCKS THAT MUST BE LEFT ON THE TRK
LEVTABL  EQU   *-LEVBYTES
         SPACE 3
*---------------------------------------------------------------------*
*              PCAREA SUPLIED BY CALLER (CLEARED AT START OF INDEX)
*---------------------------------------------------------------------*
PCAREA   DSECT
PCTTR    DS    XL3                 TTR RETURN AREA FOR USER
PCFLAG   DS    X                   FLAG
PCCLBUF  EQU   X'80'               BUFFER BEING USED
PCVCB    EQU   X'40'               THIS IS A VCB (SET BY CALLER)
PCKEY    DS    CL8                 KEY
PCBUF    DS    XL256               BUFFER
         SPACE 3
*---------------------------------------------------------------------*
*              OLD CVOL NAMES THAT MUST BE CHANGED
*---------------------------------------------------------------------*
CVOLD    DSECT
CVOLDCHN DS    A                   FIRST CVNEW
CVOLDDUM DS    CL8                 DUMMY SO LOOKS LIKE CVNEW
CVOLDNXT DS    A                   NEXT CVOLD
CVOLDNAM DS    CL6                 NAME
CVOLDE   DS    0F                  END
CVOLDSIZ EQU   CVOLDE-CVOLD        SIZE
         SPACE 3
*---------------------------------------------------------------------*
*              NEW NAMES AND MAX NAMES FOR OLD CVOLS
*---------------------------------------------------------------------*
CVNEW    DSECT
CVNEWNXT DS    A                   NEXT CVNEW
CVNEWDSN DS    CL8                 MAX NAME FOR THIS NAME
CVNEWDEV DS    F                   DEVICE TYPE
CVNEWNAM DS    CL6                 NEW CVOL NAME
CVNEWE   DS    0F                  END
CVNEWSIZ EQU   CVNEWE-CVNEW        SIZE
         EJECT
*---------------------------------------------------------------------*
*              ALIAS PROCESSING DSECT
*---------------------------------------------------------------------*
ALENTRY  DSECT
ALNEXT   DS    A                   NEXT ALENTRY OR ZERO
ALTTR    DS    XL3                 TTR IN OUTPUT DATA SET
         DS    X
ALOFF    DS    H                   OFFSET TO ENTRY IN OUTPUT BLOCK
ALNAME   DS    CL8                 NAME
ALRNAME  DS    CL8                 REAL NAME
ALEND    DS    0F
ALSIZE   EQU   ALEND-ALENTRY       SIZE
         SPACE 3
*---------------------------------------------------------------------*
*              DSECT FOR NAMES THAT HAVE ALIASES
*---------------------------------------------------------------------*
ANENTRY  DSECT
ANNEXT   DS    A                   NEXT OR ZERO
ANTTR    DS    XL3                 TTR IN OUTPUT DATA SET
ANCOUNT  DS    H                   COUNT OF ALIASES
ANNAME   DS    CL8                 NAME
ANEND    DS    0F                  END
ANSIZE   EQU   ANEND-ANENTRY       SIZE
         EJECT
*---------------------------------------------------------------------*
*              RRWORK - WORK AREA FOR RECURSIVE ROUTINE
*---------------------------------------------------------------------*
RRWORK   DSECT
         DS    18F
RRLEV    DS    H                   LEVEL
RRBYTES  DS    H                   BYTES LEFT IN LAST BLOCK
RRSVTTR  DS    XL3                 TTR FOR READ + WRITE
         DS    X
RRRTTR   DS    XL3
RRFSTTR  DS    A                   ADDRESS OF CALLERS TTR
RRNAME   DS    A                   SPOT FOR NAME
RRKEY    DS    CL8
RRBUF    DS    XL256               BUFFER
RRPTTR   DS    XL3                 PCAREA
         DS    X
RRPKEY   DS    CL8
RRPBUF   DS    XL256
RRWORKE  DS    0F
RRWKSIZE EQU   RRWORKE-RRWORK
         EJECT
*----------------------------------------------------------------EXCP01
*              BUFFER - TRACK BUFFER HEADER                      EXCP01
*----------------------------------------------------------------EXCP01
BUFFER   DSECT                                                   EXCP01
BUFNEXT  DS    A                   NEXT TRACK BUFFER             EXCP01
BUFADCB  DS    A                   ADDR OF DCB FIR THIS TRACK    EXCP01
BUFTT    DS    H                   TT OF THACK                   EXCP01
BUFFLAG  DS    X                   FLAGS                         EXCP01
BUFRETN  EQU   X'80'               RETAIN THIS REC OF BUF        EXCP01
BUFWTR   EQU   X'40'               REC OR BUF NEEDS TO BE WRITEN EXCP01
BUFNRTR  EQU   X'20'               INPUT REC NOT YET READ        EXCP01
BUFNCOMP EQU   X'10'               TRACK NOT COMPLETE            EXCP01
         DS    X                                                 EXCP01
BUFEND   EQU   *                   END OF HEADER OF BUFFER       EXCP01
BUFSIZE  EQU   BUFEND-BUFFER                                     EXCP01
         SPACE 2                                                 EXCP01
*----------------------------------------------------------------EXCP01
*              RBUFFER - RECORD ENTRIES IN THE TRACK BUFFER      EXCP01
*----------------------------------------------------------------EXCP01
RBUFFER  DSECT                                                   EXCP01
RBUFFLAG DS    X                   FLAGS                         EXCP01
         DS    XL3                                               EXCP01
RBUFKEY  DS    XL8                 KEY                           EXCP01
RBUFDATA DS    XL256               DATA                          EXCP01
RBUFEND  EQU   *                   END OF THIS REC               EXCP01
RBUFSIZE EQU   RBUFEND-RBUFFER                                   EXCP01
         EJECT                                                   EXCP01
*----------------------------------------------------------------EXCP01
*              WORK AREA FOR EXCP READS AND WRITES               EXCP01
*----------------------------------------------------------------EXCP01
WRDSECT  DSECT                                                   EXCP01
WRECB    DS    F                   ECB                           EXCP01
WRIOB    DS    0XL40               IOB                           EXCP01
         DC    XL16'00'                                          EXCP01
IOBSTART DC    A(0) -              ADDR OF CHANNEL PROGRAM       EXCP01
IOBDCBPT DC    A(0) -              ADDR OF DCB FOR THIS IOB      EXCP01
         DC    XL8'00'                                           EXCP01
IOBMBBCC DS    XL8                 MBBCCHHR                      EXCP01
WRBUF    DS    F                   ADDR OF CURRENT BUFFER        EXCP01
WREND    EQU   *                                                 EXCP01
         EJECT                                                   EXCP01
*----------------------------------------------------------------EXCP01
*              THE CCWS USED FOR WRITE                           EXCP01
*----------------------------------------------------------------EXCP01
WTRCCWS  DSECT                                                   EXCP01
         CCW   X'31',IOBMBBCC+3,X'40',5 SEARCH ID EQUAL          EXCP01
         CCW   X'08',*-8,X'00',0       TIC                       EXCP01
WTRCCW   CCW   X'1D',*+16,X'80',8  COUNT                         EXCP01
         CCW   X'08',*+16,X'00',0  TIC AROUND COUNT              EXCP01
WTRCCWC  DS    XL8                 COUNT FIELD                   EXCP01
WTRCCWD  CCW   X'08',*-*,X'00',264     KEY AND DATA              EXCP01
WTRCCWE  EQU   *                   START OF NEXT WTRCCW          EXCP01
         SPACE  2                                                EXCP01
*----------------------------------------------------------------EXCP01
*              THE CCWS USED FOR READ                            EXCP01
*----------------------------------------------------------------EXCP01
RDCCWS   DSECT                                                   EXCP01
         CCW   X'31',IOBMBBCC+3,X'40',5 SEARH ID EQ              EXCP01
         CCW   X'08',*-8,X'00',0       TIC                       EXCP01
RDCCW    CCW   X'0E',*-*,X'00',264     READ KEY + DATA           EXCP01
RDCCWE   EQU   *                   NEXT RDCCW                    EXCP01
         EJECT                                                   EXCP01
IHADCB   DCBD  DEVD=(DA),DSORG=(PS)                              EXCP01
         EJECT
*---------------------------------------------------------------------*
*
*                       *** COPYCAT ***
*
*                              BY
*
*                       DOUGLAS E. ENGERT
*
*                  ARGONNE NATIONAL LABORATORY
*
*                            MAY 1974
*
*---------------------------------------------------------------------*
*
*                COPYCAT IS AN OS UTILITY PROGRAM WHICH CAN BE USED
*              TO COPY AN OS CATALOG WHILE REFORMATING IT FOR
*              FASTER ACCESSING. ONE OR MORE CATALOGS CAN BE USED
*              FOR INPUT AND MORE THEN ONE CATALOG CAN BE
*              PRODUCED.
*                THERE ARE TWO OPERATING MODES:
*                COPY  - ONLY THE MASTER CATALOG DEFINED BY THE
*                        KEEP CARD IS PRODUCED.
*                SPLIT - THE CATALOGS DEFINED BY THE SPLIT CARDS
*                        ARE PRODUCED.
*
*---------------------------------------------------------------------*
COPYCAT  CSECT
         SAVE  (14,12),,*
         LA    R12,2048
         LA    R11,2048(R12,R15)
         LA    R12,2048(R12,R11)
         LR    R10,R15
         USING COPYCAT,R10,R11,R12
         LA    R15,SAVEAREA
         ST    R13,SAVEAREA+4
         ST    R15,8(R13)
         LR    R13,R15
         STAE  STAEEXIT,CT
         LA    R15,SETXX           DUMP PHONE NUMBER             F00003
         BAL   R9,SETXX
         OPEN  (SYSIN,(INPUT),SYSPRINT,(OUTPUT))
         MVC   PRNTLINE+30-L'TITLE1/2(L'TITLE1),TITLE1
         BAL   R9,PUTSP
         MVI   PRNTLINE,C'0'
         MVC   PRNTLINE+30-L'TITLE2/2(L'TITLE2),TITLE2
         BAL   R9,PUTSP
         MVI   PRNTLINE,C'0'       SET FOR 2 SPACES
         MVC   PRNTLINE+30-L'TITLE3/2(L'TITLE3),TITLE3
         BAL   R9,PUTSP
         MVI   PRNTLINE,C'-'
*---------------------------------------------------------------------*
*              GET THE CONTROL RECORD
*---------------------------------------------------------------------*
         BAL   R9,GETCARD          GET CONTROL CARD
         CLC   OPER,=CL8'CONTROL'  CONTROL?
         BC    NE,ERROROP          NO,ERROR
         LA    R1,9                SET LENGTH
         BAL   R9,SCANOPER
         CLC   OPER(9),=CL9'SECONDARY'
         BC    EQ,CON005
         CLC   OPER,=CL8'MASTER'
         BC    NE,ERROR
         OI    FLAG,COPY           SET COPY OPERATION
CON005   BAL   R9,SCANOPR8         GET NEXT OPERAND
         BC    ZERO,CON010         NONE,BRANCH
         CLC   OPER,=CL8'GDGMOVE'
         BC    NE,CON008           NO,BRANCH
         OI    FLAG,GDSMOVE        SET MOVE
         B     CON010
CON008   CLC   OPER,=CL8'GDGCOPY'
         BC    NE,ERROR
         OI    FLAG,GDSCOPY        SET COPY ONLY
CON010   BAL   R9,SCANOPR8         GET DATA SET OPERAND
         BC    ZERO,CON014         NULL,BRANCH
         CLC   OPER,=CL8'KEEP'
         BC    NE,CON012           NO,BRACNH
         OI    FLAG,DSKEEP         SET KEEP FLAG
         B     CON015
CON012   CLC   OPER,=CL8'SPLIT'
         BC    EQ,CON014           YES,BRANCH
         CLC   OPER,=CL8'UNCATLG'
         BC    EQ,CON015           YES,BRANCH
         BC    NE,ERROR            NO,ERROR
CON014   OI    FLAG,DSSPLIT        SPLIT THEM
*---------------------------------------------------------------------*
*              READ INPUT LIST CARDS
*---------------------------------------------------------------------*
CON015   BAL   R9,GETCARD          READ INPUT CARD
         CLC   OPER,=CL8'INPUT'
         BC    NE,CON018           NO,BRANCH
         GETMAIN R,LV=INSIZE,SP=2
         LR    R3,R1
         USING INLIST,R3
         MVC   INDCB(INDCBE-INLIST),PATDCB MOVE PATTERN DCB      EXCP01
         L     R1,INDCBA
         CLI   INDCBA,31*8         TO MANY?
         BC    LT,CON016           NO,BRANCH
         MVC   PRNTLINE+2(L'ERRMSG6),ERRMSG6
         BAL   R9,PUTSP
         CLI   RETCODE+1,8
         BC    GE,TERM
         MVI   RETCODE+1,8
         B     TERM
CON016   ST    R3,0(R1)            SAVE ADDRESS
         OI    0(R1),X'80'
         SL    R1,=F'4'
         NI    0(R1),FF-X'80'      CLEAR LAST EOF BIT
         MVC   ININDEX,INDCBA      MOVE INDEX
         AL    R1,=X'08000008'     INC INDEX AND ADDRESS
         ST    R1,INDCBA           SAVE
         LA    R2,DCBDDNAM-IHADCB+INLIST POINT AT DDNAME SPOT
         BAL   R9,SCAN8            GO FIND DDNAME OPERAND
         BC    ZERO,ERROR          NONE,ERROR
         LA    R2,INVOL            POINT AT SPOT FOR VOL NAME
         BAL   R9,SCAN6            GO FIND VOLID OPERAND
         BC    ZERO,ERROR          NONE,ERROR
         LR    R8,R3               POINT AT DCB                  EXCP01
         BAL   R9,CON045B          GO GET RECS/TRACK             EXCP01
         USING IHADCB,R3
         OPEN  ((R3))              OPEN IT
         TM    DCBOFLGS,X'10'      OPEN?
         BC    ALLOFF,DCBERR       NO,BRANCH
         B     CON015              GO LOOK FOR MORE
CON018   CLC   INTAB+1(3),=XL3'00'  ANY?
         BC    NE,CON022           YES,BRANCH
         MVC   PRNTLINE+2(L'ERRMSG8),ERRMSG8
         BAL   R9,PUTSP
         CLI   RETCODE+1,8
         BC    GE,TERM
         MVI   RETCODE+1,8
         B     TERM
*---------------------------------------------------------------------*
*-             READ  VOLLIST  ENTRIES.THE MUST BE IN ORDER
*---------------------------------------------------------------------*
CON020   BAL   R9,GETCARD          GET VOLLIST CARD
CON022   CLC   OPER,=CL8'OUTPUT'
         BC    NE,ERROROP          NO,ERROR
         LA    R1,9
         BAL   R9,SCANOPER         GET NEXT OPERAND
         GETMAIN R,LV=VOLLSIZE,SP=2  GET A VOLLIST AREA
         LR    R8,R1
         USING VOLLIST,R8
         XC    VOLLIST(256),VOLLIST
         XC    VOLLIST+256(256),VOLLIST+256
         XC    VOLLIST+512(VOLLSIZE-512),VOLLIST+512
         MVC   VOLLDCB(VOLLDCBE-VOLLDCB),PATDCB MOVE DCBS
         ST    R8,VOLLRNXT         POINT AT SELF
         LA    R1,VOLLJFCB
         ST    R1,VOLLEXLT         SAVE JFCB ADDRESS
         MVI   VOLLEXLT,X'87'      SET JFCB EXIT AND END
         CLC   OPER(9),=CL9'SECONDARY' SPLIT CARD?
         BC    EQ,CON030           YES BR
         CLC   OPER,=CL8'MASTER' KEEP CARD?
         BC    NE,ERROR            NO,ERROR ON OPERATION
         OI    VOLLFLAG,VOLLKEEP   SET KEEP THIS ENTRY
CON030   LA    R3,VOLLDCB
         LA    R1,VOLLEXLT         POINT AT EXIT LIST
         O     R1,DCBRECFM         ADD RECFM TO HIGH BYTE
         ST    R1,DCBEXLST         SAVE IN DCB
         LA    R2,DCBDDNAM         POINT AT DDNAME SPOT
         BAL   R9,SCAN8            GO FIND DDNAME OPERAND
         BC    ZERO,ERROR          NULL,ERROR
         LA    R2,VOLLVOL          SPOT FOR VOLID
         BAL   R9,SCAN6            GO FIND VOLID OPERAND
         BC    ZERO,ERROR          NONE,BRANCH
         BAL   R9,SCANOPR8         FIND MAX NAME FOR THIS CARD
         BC    NZERO,CON040        NAME FOUND,BRANCH
         MVC   OPER,=XL8'FFFFFFFFFFFFFFFF' SET FOR MAX
CON040   CLC   OPER,VOLNAMEL       IS THIS NAME GREATER THEN LAST?
         BC    LE,ERROR            NO,BR
         L     R1,VOLLAST          LOAD LAST ENTRY
         LTR   R1,R1               ANY
         BC    NZERO,CON045        YES,BRANCH
         LA    R1,VOLCHAIN-VOLLNEXT+VOLLIST SET R1 FOR STORE
CON045   ST    R8,VOLLNEXT-VOLLIST(R1)  CHAIN IN
         ST    R8,VOLLAST
         MVC   VOLLNAME,OPER       MOVE MAX NAME SO FAR
         MVC   VOLNAMEL,OPER       SET LAST NAME SO FAR
         LA    R9,CON045I          SET RETURN FOR FOLLOWING      EXCP01
CON045B  EQU   *                                                 EXCP01
         DEVTYPE DCBDDNAM,VOLLDEVT,DEVTAB
         LTR   R15,R15             OK?
         BC    NZERO,DCBERR        NO,BR
         CLI   VOLLDEVT+2,X'20'    DASDI?
         BC    NE,DCBERR           NO,BRANCH
         LA    R15,264             DATA+KEY LENGTH
         MH    R15,VOLLTOLF        *TOLDRANGE *2**9
         SRA   R15,9               / 2**9
         SR    R1,R1
         SR    R0,R0
         SR    R14,R14
         IC    R14,VOLLBOH         OVER HEAD PER BLOCK
         IC    R1,VOLLBOHL         OVER HEAD LAST BLOCK
         TM    VOLLDFLG,VOLLBOHH   HALF WORD OVERHEAD?           F00003
         BC    ALLOFF,CON045A      NO,BRANCH                     F00003
         LH    R14,VOLLBOH         LOAD BLOCK OVER HEAD          F00003
         LR    R1,R14              LOAD LAST BLOCK OVER HEAD     F00003
CON045A  EQU   *                                                 F00003
         AR    R1,R15              TOTAL LENGTH FOR LAST BLOCK
         AR    R14,R15             TOTAL LENGTH FOR OTHER BLOCKS
         SH    R1,VOLLTRKL        -TRK SIZE = - SIZE LEFT
         LCR   R1,R1               COMPLEMENT
         DR    R0,R14              =#OF BLOCKS -1
         LA    R1,1(R1)            +1 FOR LAST
         STH   R1,VOLLMBLK         # OF RECORDS / TRK MAX
         CH    R1,MAXRECTK         THIS THE BIGGEST?             EXCP01
         BC    LE,CON045E          NO,BRANCH                     EXCP01
         STH   R1,MAXRECTK         SAVE MAX REC/TRK              EXCP01
CON045E  BR    R9                  RETURN                        EXCP01
CON045I  EQU   *                   RETURN FOR INLINE             EXCP01
         MVI   VOLLTTR+2,X'01'     SET NEXT TO 001
         TM    FLAG,COPY           COPY?
         BC    ALLON,CON050        YES,BRANCH
         TM    VOLLFLAG,VOLLKEEP   SHOULD WE OPEN DCB?
         BC    ALLON,CON070        NO,BR
         L     R1,VOLCHAIN
CON046   CR    R1,R8               OURS?
         BC    EQ,CON060           YES,BRANCH
         TM    VOLLFLAG-VOLLIST(R1),VOLLKEEP KEEP?
         BC    ALLOFF,CON048       NO,BRANCH
CON047   L     R1,VOLLNEXT-VOLLIST(R1)
         B     CON046              GO LOOK AT NEXT
CON048   CLC   VOLLVOL,VOLLVOL-VOLLIST(R1) SAME?
         BC    NE,CON047           NO,LOOK AGAIN
         ST    R1,VOLLRNXT         YES,POINT AT IT
         B     CON070              DONT OPEN
CON050   TM    VOLLFLAG,VOLLKEEP   IS THIS THE KEEP ENTRY?
         BC    ALLOFF,CON070       NO,GET NEXT
         OC    VOLKEEP,VOLKEEP     FIRST KEEP?
         BC    ZERO,CON055         YES,OPEN IT
         MVC   VOLLRNXT,VOLLKEEP   POINT AT REAL ONE
         B     CON070              GET NEXT CARD
CON055   ST    R8,VOLKEEP          SAVE ADDRESS OF FIRST KEEP
CON060   RDJFCB ((3))              READ JFCB
         OI    VOLLJFCB+52,X'02'   DONT MERGE
         OPEN  ((3),(OUTPUT)),TYPE=J
         TM    DCBOFLGS,X'10'      OPEN?
         BC    ALLOFF,DCBERR
         LA    R1,1                                              EXCP01
         AH    R1,MINBUFFS         ADD 1 TO COUNT OF MIN BUFS    EXCP01
         STH   R1,MINBUFFS         SAVE                          EXCP01
CON070   CLI   VOLLNAME,X'FF'      LAST?
         BC    NE,CON020           NO,GO GET NEXT
         TM    FLAG,COPY           COPY OPERATION?
         BC    ALLOFF,CON078       NO,BRANCH
         OC    VOLKEEP,VOLKEEP     KEEP FOUND?
         MVC   VOLLAST,VOLKEEP
         BC    NZERO,CON078        YES,BRANCH
         MVC   PRNTLINE+2(L'ERRMSG14),ERRMSG14
         BAL   R9,PUTSP
         CLI   RETCODE+1,8
         BC    GE,TERM
         MVI   RETCODE+1,8
         B     TERM
CON078   EQU   *
*---------------------------------------------------------------------*
*              FINC CVOL ENTRIES (IF ANY)
*---------------------------------------------------------------------*
         SR    R3,R3
CON080   BAL   R9,GETCARD          GET CVOL CARD
         CLC   OPER,=CL8'CVOL'     CVOL CARD?
         BC    NE,CON120           MVST BE END
         BAL   R9,SCANOPR6         GO FIND VOL
         LTR   R3,R3               ANY CVNEW S SO PAR?
         BC    NZERO,CON090        YES,BRANCH
         CLI   OPER,C' '           ANY?
         BC    EQ,ERROR            NO,BRANCH
         GETMAIN R,LV=CVOLDSIZ,SP=2  GET OLD ENTRY
         USING CVOLD,R1
         MVC   CVOLDNXT,CVOLCHAN   CHAIN IN
         ST    R1,CVOLCHAN         CHAIN IN
         MVC   CVOLDNAM,OPER       MOVE OLD CVOL NAME
         XC    CVOLDDUM,CVOLDDUM   CLEAR
         LR    R3,R1               SET SO CVNEW S ARE CHAINED IN
         DROP  R1
         USING CVNEW,R3
CON090   GETMAIN R,LV=CVNEWSIZ,SP=2
         LR    R4,R1               SAVE
         LA    R2,CVNEWNAM-CVNEW(R4) SPOT FOR NAME
         BAL   R9,SCAN6            GO FIND NEW NAME
         BC    ZERO,ERROR          NULL,ERROR
         BAL   R9,SCANOPR8         FIND MAX NAME OPERAND
         BC    NZERO,CON100        FOUND,BRANCH
         MVC   OPER,=XL8'FFFFFFFFFFFFFFFF' SET MAX
CON100   CLC   CVNEWDSN,OPER       GT THEN LAST
         BC    GE,ERROR            NO,ERROR
         ST    R4,CVNEWNXT         CHAIN IN (MAY BE CVOLDCHAN)
         LR    R3,R4
         XC    CVNEWNXT,CVNEWNXT   CLEAR CHAIN
         MVC   CVNEWDSN,OPER       MOVE HIGH NAME
         BAL   R9,SCANOPR8         IFND DD NAME FOR DEVICE TYPE
         BC    ZERO,ERROR          NULL,ERROR
         DEVTYPE OPER,WORKAREA
         MVC   CVNEWDEV,WORKAREA
         LTR   R15,R15             OK?
         BC    NE,DCBERRO          NO,BRANCH
         CLI   CVNEWDSN,X'FF'      LAST?
         BC    NE,CON080           NO,GO GET NEXT
         SR    R3,R3               SET CVOL DONE
         B     CON080
CON120   LTR   R3,R3               CVOL COMPLETE?
         BC    NZERO,ERROROP       NO,ERROR
         B     CON210              CARD MUST BE LEVEL CARD
*---------------------------------------------------------------------*
*              GET THE LEVTAB ENTRIES
*---------------------------------------------------------------------*
CON200   BAL   R9,GETCARD          GET LEVEL CARD
CON210   CLC   OPER,=CL8'LEVEL'
         BC    NE,ERROROP          NOT LEVEL ERROR
         BAL   R9,SCANOPR8         GET FIRST OPERAND
         BAL   R9,CONV3            CONVERT TO BIN
         CH    R1,=H'0'
         BC    LE,ERROR
         CH    R1,=H'22'
         BC    GT,ERROR
         MH    R1,=AL2(LEVTABL)
         LA    R7,LEVTAB-LEVTABL(R1) POINT AT ENTRY
         USING LEVTABE,R7
         BAL   R9,SCANOPR8         BYTES TO LEAVE IN EACH BLOCK
         BAL   R9,CONV3
         CH    R1,=H'200'
         BC    GT,ERROR
         STH   R1,LEVBYTES         SAVE BYTES TO LEAVE ON A BLOCK
         BAL   R9,SCANOPR8         GET BLOCKS TO LEAVE AT END
         BAL   R9,CONV3
         STH   R1,LEVBLOCK         SAVE BLOCKS +0 LEAVE AT END OF INDEX
         BAL   R9,SCANOPR8         GET BLOCKS THAT MUST BE ON A TRK
         BAL   R9,CONV3
         STH   R1,LEVBLONT     SAVE BLOCKS THAT MUST BE ON TRK
         B     CON200
*---------------------------------------------------------------------*
*              END OF FILE ON SYSIN
*---------------------------------------------------------------------*
EOFIN    CLI   VOLLNAME,X'FF'      WAS LAST VOLLIST CARD READ?
         BC    NE,EOFINE           NO,ERROR
         LTR   R3,R3               CVOLS READ OK?
         BC    NZERO,EOFINE        NO,ERROR,BRANCH               EXCP01
*----------------------------------------------------------------EXCP01
*              BUILD CCWS                                        EXCP01
*----------------------------------------------------------------EXCP01
         LA    R6,=AL2(RDCCWE-RDCCW,RDCCW-RDCCWS) SIZES          EXCP01
         LA    R5,ECBLISTR         FIRST WRDSECT                 EXCP01
INE010   L     R4,0(R5)                                          EXCP01
         USING WRDSECT,R4                                        EXCP01
         LH    R0,MAXRECTK         MAX REC/TRACK                 EXCP01
         MH    R0,0(R6)            SIZE OF CCW                   EXCP01
         AH    R0,2(R6)            +FIXED SIZE                   EXCP01
         GETMAIN R,LV=(0)          GET CCW AREA                  EXCP01
         ST    R1,IOBSTART         SAVE IN IOB                   EXCP01
         XC    0(16,R1),0(R1)      CLEAR FIRST 2 CCWS            EXCP01
         ST    R1,8(R1) SAVE IN TIC                              EXCP01
         LA    R7,IOBMBBCC+3       CCHH ADDR                     EXCP01
         ST    R7,0(R1)            SAVE IN CCW                   EXCP01
         OC    0(9,R1),=X'310000004000000508'  SEARCH AND TIC    EXCP01
         LA    R1,16(R1)           POINT AT VARIABLE SEC         EXCP01
         LA    R8,1                                              EXCP01
         LH    R0,MAXRECTK                                       EXCP01
         C     R4,ECBLISTR         READ PASS?                    EXCP01
         BC    NE,INE040           NO,BRANCH                     EXCP01
INE030   MVC   0(8,R1),=X'0E00000000000108' READ KEY+DATA,264    EXCP01
         LA    R1,8(R1)            NEXT CCW                      EXCP01
         BCT   R0,INE030           LOOP FOR ALL CCWS             EXCP01
         B     INE060                                            EXCP01
INE040   XC    0(32,R1),0(R1)      CLEAR CCWS                    EXCP01
         LA    R15,16(R1)          COUNT FIELD                   EXCP01
         ST    R15,0(R1)           SAVE IN CCW                   EXCP01
         LA    R15,24(R1)          WTRCCWD                       EXCP01
         ST    R15,8(R1)           SAVE IN TIC                   EXCP01
         STC   R8,20(R1)           SAVE REC NUMBER               EXCP01
         OC    0(32,R1),PATCCW     COMPLETE CCWS                 EXCP01
         LA    R1,32(R1)           NEXT CCW SET                  EXCP01
         LA    R8,1(R8)            REC NUM                       EXCP01
         BCT   R0,INE040                                         EXCP01
INE060   LA    R6,=AL2(WTRCCWE-WTRCCW,WTRCCW-WTRCCWS)            EXCP01
         LTR   R4,R4               LAST?                         EXCP01
         LA    R5,4(R5)                                          EXCP01
         BC    PLUS,INE010         NO,BRANCH                     EXCP01
*----------------------------------------------------------------EXCP01
*              GET BUFFERS                                       EXCP01
*----------------------------------------------------------------EXCP01
         L     R0,MINCORE          LOAD REQUIRED CORE            EXCP01
         GETMAIN R,LV=(0)                                        EXCP01
         LR    R2,R1               SAVE FOR LATER                EXCP01
         LA    R3,RBUFSIZE         SIZE OF REC AREA              EXCP01
         MH    R3,MAXRECTK         *NUMBER                       EXCP01
         LA    R3,BUFSIZE(R3)      +FIXED=BUFFER SIZE            EXCP01
         GETMAIN VC,LA=GETMLA,A=GETMA GET THE BUFFERS            EXCP01
         L     R0,MINCORE                                        EXCP01
         FREEMAIN R,LV=(0),A=(2)   FREE MIN FREE CORE            EXCP01
         LM    R14,R15,GETMA                                     EXCP01
         SRL   R15,3               /8                            EXCP01
         SR    R0,R0                                             EXCP01
         SR    R1,R1                                             EXCP01
INE070   STM   R0,R1,0(R14)        ZERO                          EXCP01
         LA    R14,8(R14)                                        EXCP01
         BCT   R15,INE070                                        EXCP01
         L     R1,GETMS            SIZE GOTTEN                   EXCP01
         SR    R0,R0                                             EXCP01
         DR    R0,R3               /SIZE OF BUF=#OF BUFFS        EXCP01
         CH    R1,MINBUFFS         MIN # OF BUFFS                EXCP01
         BC    LT,INE200           NO,ERROR                      EXCP01
         LA    R5,FREEBUFQ                                       EXCP01
         L     R6,GETMA            POINT AT BUFFER AREA          EXCP01
INE080   ST    R6,0(R5)            SAVE CHAIN ADDR               EXCP01
         LR    R5,R6                                             EXCP01
         AR    R6,R3               NEXT BUFFER                   EXCP01
         BCT   R1,INE080           LOOP FOR ALL                  EXCP01
         XC    0(4,R5),0(R5)       CLEAR LAST                    EXCP01
         B     MAIN010                                           EXCP01
INE200   MVC   PRNTLINE+2(L'ERRMSG15),ERRMSG15                   EXCP01
         BAL   R9,PUTSP                                          EXCP01
         B     EOF010                                            EXCP01
EOFINE   MVC   PRNTLINE+2(L'ERRMSG3),ERRMSG3
         BAL   R9,PUTSP
EOF010   EQU   *                                                 EXCP01
         CLI   RETCODE+1,8
         BC    GE,TERM
         MVI   RETCODE+1,8
         B     TERM
*
CONV3    SR    R1,R1
         LTR   R15,R0              LOAD LENGTH
         BCR   ZERO,R9             NULL,RETURN ZERO
         BCTR  R15,0               -1
         XC    WORKAREA,WORKAREA
         EX    R15,CONVMVZ         MOVE ZONES
         EX    R15,CONVCLC         CHECH FOR NUMERIC
         BC    NE,ERROR
         EX    R15,CONVPCK         PACK NUMBER
         CVB   R1,WORKAREA         CONVERT TO BIN
         BR    R9                  RETURN
CONVMVZ  MVZ   WORKAREA(0),OPER
CONVCLC  CLC   WORKAREA(0),=CL8'00000000'
CONVPCK  PACK  WORKAREA,OPER(0)
*---------------------------------------------------------------------*
*              EVERYTHING OR, START
*---------------------------------------------------------------------*
MAIN010  LA    R1,MAINTTR          POINT AT TTR
         LH    R0,=H'1'            SET FOR LEVEL 1
         LA    R15,RR
         BALR  R14,R15             GO COPY CATALOG
         MVC   PRNTLINE(L'HEAD1),HEAD1
         BAL   R9,PUTSP
         MVC   PRNTLINE(L'HEAD2),HEAD2
         BAL   R9,PUTSP
         MVC   PRNTLINE(L'HEAD3),HEAD3
          BAL   R9,PUTSP
         MVC   PRNTLINE(L'HEAD4),HEAD4
         BAL   R9,PUTSP
         MVI   PRNTLINE,C'0'
         LA    R7,LEVTAB           POINT AT LEVEL TABLE
         TM    FLAG,COPY           COPY?
         MVC   LEVBLONT,=H'999'    SET SO PUTC WILL FILL TRK
         L     R8,VOLCHAIN
         BC    ALLOFF,MAIN050      NO,BRANCH
         L     R8,VOLLAST          POINT AT LAST
MAIN015  LA    R1,VOLLTTR          LOAD LAST USED TTR+1
         LA    R2,PRNTLINE+16      POINT AT SPOT IN LINE
         BAL   R9,CONVTTR          GO FORMAT
MAIN020  TM    VOLLFLAG,VOLLSECN   AT END OF EXTENT?
         BC    ALLON,MAIN030
         LH    R1,=H'-1'           SET TO FILL TRK
         LA    R15,PUTC
         BALR  R14,R15
         B     MAIN020
MAIN030  LA    R1,MAINTTR
         LA    R3,VOLLDCB                                        EXCP01
         LA    R2,VOLLIKEY
         AL    R2,=X'80000000'     RETAIN                        EXCP01
         LA    R15,READC
         BALR  R14,R15             READ IN VICE
         LA    R5,VOLLIBUF+2
         USING CATENTRY,R5
         MVC   CVICFRST,VOLLFRST   SET FIRST AVAILABLE TTR
         LH    R0,VOLLTTR          POINT AT NEXT TTR
         SH    R0,=H'1'            -1 FOR PREVIOUS TRK
         STH   R0,CVICUP           SAVE TT
         MVC   CVICUP+2(1),VOLLMBLK+1 SET R TO LAST ON TRK
         LA    R1,MAINTTR
         LA    R2,VOLLIKEY
         AL    R2,=X'80000000'     RETAIN                        EXCP01
         LA    R15,WRITEC
         BALR  R14,R15             WRITE VICE BACK
*---------------------------------------------------------------------*
*              HANDLE ALIAS ENTRIES FOR THIS VOLUME
*---------------------------------------------------------------------*
         L     R4,VOLALCHN
         USING ALENTRY,R4
ALE010   LTR   R4,R4               ANY?
         BC    ZERO,ALE050         NO,BRANCH
         L     R6,VOLANCHN         POINT AT REAL NAME CHAIN
         USING ANENTRY,R6
ALE020   LTR   R6,R6               ANY?
         BC    ZERO,ALE040         NO,ERROR
         CLC   ALRNAME,ANNAME      IS THIS ALIAS FOR THIS NAME?
         BC    EQ,ALE030           YES,GO CHANGE
         L     R6,ANNEXT           LOOK AT NEXT
         B     ALE020
ALE030   LA    R2,VOLLIKEY         POINT AT KEY AND BUFFER
         AL    R2,=X'80000000'     RETAIN                        EXCP01
         LA    R1,ALTTR            POINT AT TTR
         LA    R15,READC
         BALR  R14,R15             READ BLOCK WITH ALIAS
         LH    R5,ALOFF            LOAD OFFSET TO ALIAS ENTRY
         LA    R5,VOLLIBUF(R5)     POINT AT ENTRY
         MVC   CATTTR,ANTTR        MOVE TTR
         LA    R15,WRITEC
         BALR  R14,R15             WRITE BACK THE BLOCK
         LH    R0,ANCOUNT          LOAD COUNT
         SH    R0,=H'1'            -1
         STH   R0,ANCOUNT          SAVE COUNT
ALE035   L     R4,ALNEXT           LOOK AT NEXT ALIAS
         B     ALE010
ALE040   MVC   PRNTLINE+2(L'ERRMSG4),ERRMSG4
         MVC   PRNTLINE+13(8),ALRNAME
         MVC   PRNTLINE+42(8),ALNAME
         BAL   R9,PUTSP
         CLI   RETCODE+1,12
         BC    GE,ALE035
         MVI   RETCODE+1,12
         B     ALE035
ALE050   L     R6,VOLANCHN         LOOK AT REAL NAMES
ALE052   LTR   R6,R6               ANY?
         BC    ZERO,ALE070         NO,BRANCH
         CH    R4,ANCOUNT          ALL ALIASES FOUND?
         BC    EQ,ALE060           YES,BRANCH
         MVC   PRNTLINE+2(L'ERRMSG5),ERRMSG5
         MVC   PRNTLINE+41(8),ANNAME MOVE NAME
         BAL   R9,PUTSP
         CLI   RETCODE+1,12
         BC    GE,ALE060
         MVI   RETCODE+1,12
ALE060   L     R6,ANNEXT           LOOK AT NEXT NAME
         B     ALE052
         DROP  R4,R6
ALE070   EQU   *
         MVC   PRNTLINE+2(6),VOLLVOL
         LA    R1,VOLLTTR
         LA    R2,PRNTLINE+9
         BAL   R9,CONVTTR          GO CONVERT TTR FOR OUTPUT
         LH    R1,VOLLICVL
         LA    R2,PRNTLINE+23
         BAL   R9,CONVDEC
         LH    R1,VOLLIDUM
         LA    R2,PRNTLINE+31
         BAL   R9,CONVDEC
         LH    R1,VOLLI1
         LA    R2,PRNTLINE+39
         BAL   R9,CONVDEC
         LH    R1,VOLLI2
         LA    R2,PRNTLINE+47
         BAL   R9,CONVDEC
         L     R1,VOLLDS
         LA    R2,PRNTLINE+55
         BAL   R9,CONVDEC
         BAL   R9,PUTSP            GO PRINT LINE
         MVI   VOLLDCB+DCBFDAD+7-IHADCB,X'FF' SET CATLOG FORMATED
         TM    FLAG,COPY           COPY OPERATION?
         BC    ALLON,MAIN060
MAIN040  L     R8,VOLLNEXT         LOOK AT NEXT VOLLIST
         LTR   R8,R8               ANY MORE?
         BC    ZERO,MAIN060        NO,BRANCH
MAIN050  TM    VOLLFLAG,VOLLKEEP   KEEP OR SPLIT?
         BC    ALLON,MAIN040       KEEP,BRANCH
         C     R8,VOLLRNXT         REAL VOLLIST?
         BC    NE,MAIN040          NO,BRANCH LOOK AT NEXT
         B     MAIN015             GO PROCESS THIS VOL
*---------------------------------------------------------------------*
         USING IHADCB,R3
TERM     EQU   *
MAIN060  LA    R2,PRNTLINE+16
         L     R5,USEBUFQ                                        EXCP01
         B     TERM003                                           EXCP01
         USING BUFFER,R6                                         EXCP01
TERM001  TM    BUFFLAG,BUFWTR      WRITE THIS BUFFER?            EXCP01
         L     R5,BUFNEXT          POINT AT NEXT                 EXCP01
         BC    ALLOFF,TERM002      NO,BRANCH                     EXCP01
         MVC   BUFNEXT,OUTBUFQ     QUEUE IT                      EXCP01
         ST    R6,OUTBUFQ          CHAIN IN AS FIRST             EXCP01
         B     TERM003                                           EXCP01
TERM002  MVC   BUFNEXT,FREEBUFQ                                  EXCP01
         ST    R6,FREEBUFQ         CHAIN IN AS FREE              EXCP01
TERM003  LTR   R6,R5               ANY MORE?                     EXCP01
         BC    NZERO,TERM001       YES,LOOK AT THEM              EXCP01
TERM004  LA    R15,WRITEB          FINISH WRITES                 EXCP01
         BALR  R14,R15                                           EXCP01
         LA    R5,ECBLISTW         POINT AT LIST OF ECB          EXCP01
TERM005  L     R4,0(R5)                                          EXCP01
         USING WRDSECT,R4                                        EXCP01
         OC    WRBUF,WRBUF         ACTIVE?                       EXCP01
         BC    NZERO,TERM006       YES,GO WAIT                   EXCP01
         LTR   R4,R4               ANOTHER?                      EXCP01
         LA    R5,4(R5)            NEXT ECB ADDR                 EXCP01
         BC    PLUS,TERM005        GO LOOK AT IT                 EXCP01
         B     TERM007                                           EXCP01
TERM006  WAIT  ECBLIST=ECBLISTW    WAIT FOR WRITES               EXCP01
         B     TERM004                                           EXCP01
TERM007  EQU   *                                                 EXCP01
         LH    R1,RETCODE          LOAD RETURN CODE
         BAL   R9,CONVDEC
         MVC   PRNTLINE(L'MSGOK),MSGOK
         BAL   R9,PUTSP
         CLOSE (SYSIN,,SYSPRINT)
         OC    INTAB,INTAB         ANY?
         BC    ZERO,TERM010        NO,BRANCH
         CLOSE MF=(E,INTAB)        CLOSE OPEN INPUTS
TERM010  L     R8,VOLCHAIN
TERM015  LTR   R8,R8               ANY?
         BC    ZERO,TERM030        NO,BRANCH
         LA    R3,VOLLDCB
         TM    DCBOFLGS,X'10'      OPEN?
         BC    ALLOFF,TERM020      NO,BRANCH
         CLOSE ((R3))                                            EXCP01
TERM020  L     R8,VOLLNEXT
         B     TERM015
TERM030  FREEMAIN R,SP=2           FREE ALL OF SUBPOOL2
         L     R13,SAVEAREA+4
         LH    R15,RETCODE         LOAD RETURN CODE
         RETURN (14,12),T,RC=(15)
         EJECT
ERROROP  L     R15,SCANOP          LOAD SCAN POINTER
         B     ERROR1
ERROR    L     R15,SCANREG2        POINT AT CHAR IN ERROR
ERROR1   S     R15,SCANSTRT        - START OF CARD
         LA    R0,C'$'
         STC   R0,PRNTLINE+10(R15) SET $ UNDER COL IN ERROR
         BAL   R9,PUTSP
         MVC   PRNTLINE+2(L'ERRMSG1),ERRMSG1
         BAL   R9,PUTSP
         CLI   RETCODE+1,8
         BC    GE,TERM
         MVI   RETCODE+1,8
         B     TERM                GO TERMINATE
         SPACE 3
DCBERRO  LA    R3,OPER-DCBDDNAM+IHADCB
DCBERR   MVC   ERRMSG2+31(8),DCBDDNAM   SET DDNAME
         MVC   PRNTLINE+2(L'ERRMSG2),ERRMSG2
         BAL   R9,PUTSP
         CLI   RETCODE+1,8
         BC    GE,TERM
         MVI   RETCODE+1,8
         B     TERM                TERMINATE
         SPACE 3
CONVTTR  ZAP   WORKAREA,=P'0'
         MVC   WORKAREA+4(3),0(R1) MOVE TTR
         UNPK  0(7,R2),WORKAREA+4(4)
         TR    0(6,R2),TRTAB-240
         MVI   6(R2),C' '
         BR    R9
TRTAB    DC    C'0123456789ABCDEF'
         SPACE 3
CONVDEC  CVD   R1,WORKAREA
         UNPK  0(7,R2),WORKAREA
         OI    6(R2),C'0'
         BR    R9
         EJECT
PUTSP    PUT   SYSPRINT,PRNTLINE
         MVI   PRNTLINE,C' '
         MVC   PRNTLINE+1(120),PRNTLINE
         BR    R9
         EJECT
*---------------------------------------------------------------------*
*              GET CARD READS A CARD AND FINDS THE OPERATOR
*              AND SAVES IT IN OPER
*---------------------------------------------------------------------*
GETCARD  GET   SYSIN
         LR    R2,R1               POINT AT CARD
         LA    R1,70(R1)           END OF CARD
         ST    R2,SCANSTRT         SAVE STATR OF CARD
         MVC   PRNTLINE+10(80),0(R2) MOVE CARD
         STM   R1,R2,SCANREG1      SAVE
         LR    R2,R9               SAVE R9
         BAL   R9,PUTSP            PRINT CARD
         LR    R9,R2               RESTORE R9
         LM    R0,R2,SCANREG0      LOAD BXLE REGS
         CLI   0(R2),C'*'          COMMENT?
         BC    EQ,GETCARD          YES,GET NEXT CARD
GET005   CLI   0(R2),C' '          BLANK?
         BC    EQ,GET010           YES,BRANCH
         BXLE  R2,R0,GET005        SKIP NAME IF ANY
         B     GETCARD             BLANK CARD SKIP IT
GET010   CLI   0(R2),C' '          NONE BLANK
         BC    NE,GET020           YES,BRANCH
         BXLE  R2,R0,GET010        LOOK FOR FIRST CHAR
         B     SCANERR             NO NAME BRANCH
GET020   ST    R2,SCANOP           SAVE ADDR OF FIRST OPERATOR
         LR    R14,R2              SAVE START
         LA    R15,8(R2)           END
         CR    R15,R1              PASS END OF CARD?
         BC    GT,GET030           YES,BRANCH
         LR    R1,R15              SET END AT SHORTEST
GET030   CLI   0(R2),C' '          BLANK?
         BC    EQ,GET040           END
         BXLE  R2,R0,GET030        NO,BRANCH
GET040   LR    R15,R2              START
         SR    R15,R14             FIND LENGRH
         BC    ZERO,SCANERR        ERROR
         MVC   OPER,=CL8' '
         BCTR  R15,0               -1
         EX    R15,MVCOPER
         L     R1,SCANREG1         RESTORE END
GET050   CLI   0(R2),C' '          FIND OPERAND
         BC    NE,GET060           NO,BRANCH
         BXLE  R2,R0,GET050
         B     SCANERR             NO,E,BRANCH
GET060   ST    R2,SCANREG2         SAVE FOR SCAN
         BR    R9                  RETURN
MVCOPER  MVC   OPER(0),0(R14)
         EJECT
*---------------------------------------------------------------------*
*              SCAN - FIND NEXT OPERAND ON CARD
*              SCAN R1=MAX LENGTH R2->SPOT FOR OPERAND R9 = RETURN
*              CONDITION CODE OF LENGTH IS SET
*---------------------------------------------------------------------*
SCAN     LR    R14,R2              SAVE SPOT
         LR    R15,R1              SAVE LENGTH
         BCTR  R15,0
         EX    R15,MVCBLANK        CLEAR AREA
         LM    R0,R2,SCANREG0      LOAD BXLE REGS
         AR    R15,R0              +1
SCAN020  CR    R1,R2               AT END?
         BC    LT,SCAN070          YES,BRANCH
SCAN030  CLI   0(R2),C' '          END?
         BC    EQ,SCAN040          YES,BRANCH
         CLI   0(R2),C','          COMMA?
         BC    EQ,SCAN040          YES,BRANCH
         BXLE  R2,R0,SCAN030
SCAN040  LR    R0,R2
         L     R1,SCANREG2         OLD START
         SR    R0,R1               LENGTH
         BC    ZERO,SCAN045        ZERO BRANCH
         CR    R0,R15              TO LONG?
         BC    GT,SCANERR          YES,BRANCH
         LR    R15,R0
         BCTR  R15,0
         EX    R15,MVCSCAN         MOVE OPERAND
SCAN045  CLI   0(R2),C','          MORE?
         BC    NE,SCAN050          NO,BRANCH
         LA    R2,1(R2)            +1
SCAN050  ST    R2,SCANREG2         SAVE START
         LTR   R0,R0               SET CONDITION CODE
         BR    R9                  RETURN
SCAN070  SR    R0,R0               SET LENGTH OF ZERO
         BR    R9                  RETURN
MVCSCAN  MVC   0(0,R14),0(R1)      MOVE OPERANDS
MVCBLANK MVC   0(0,R2),=CL9' '     BLANK OUT SPOT FOR OPERAND
         SPACE 1
SCANERR  ST    R2,SCANREG2         SAVE START
         B     ERROR
         SPACE 3
SCANOPR8 LA    R1,8                SET FOR SCAN OF LENGTH OF 8
         B     SCANOPER
SCANOPR6 LA    R1,6
SCANOPER LA    R2,OPER
         B     SCAN
SCAN8    LA    R1,8
         B     SCAN
SCAN6    LA    R1,6
         B     SCAN
SCANSTRT DS    A                   START OF CARD
SCANREG0 DC    F'1'                BXLE INC
SCANREG1 DS    A                   BXLE END
SCANREG2 DS    A                   BXLE INDEX
SCANOP   DS    A                   ADDR OF OPERATOR
OPER     DS    CL8                 OPERAND
         DS    C                   USED FOR 9TH CHAR
         EJECT
*---------------------------------------------------------------------*
*              STAE PROCESSING
*---------------------------------------------------------------------*
STAEEXIT LA    R4,12
         CR    R0,R4               WORKAREA?
         BC    EQ,STAE100          NO,BRANCH
         LM    R10,R12,STAEBASE-STAEEXIT(R15) LOAD BASE REGS
         MVC   STAEWK(2),5(R1)     MOVE ABEND CODE
         OI    STAEWK+1,X'0F'      SET SIGN
         UNPK  STAECD,STAEWK       CONVERT CODE
         TR    STAECD,TRTAB-240
         CLC   STAECD+1(2),=C'37'  EOV ERROR?
         BC    NE,STAE020          NO,BEANCH
         CLI   STAECD,C'D'         D37?
         BC    EQ,STAE150          YES,BRANCH
         CLI   STAECD,C'E'         E37?
         BC    EQ,STAE150          YES,BRANCH
STAE020  CLC   STAECD,=C'213'      213?
         BC    EQ,STAE150          YES,BRANCH
STAE100  SR    R15,R15             SET CONTINUE WITH ABEND
         BR    R14                 RETURN
STAE150  LA    R0,STAERTRY         SET RETRY ROUTINE ADDRESS
         LA    R15,4               SET RETRY
         BR    R14                 RETURN
*---------------------------------------------------------------------*
*              STAE RETRY ROUTINE
*---------------------------------------------------------------------*
STAERTRY LM    R10,R13,STAEBASE-STAERTRY(R15) SET BASE
         L     R1,25*4(R1)         POINT AT IO RESTORE CHAIN
         SVC   17                  RESTORE IO
         MVC   ERRMSG12+14(3),STAECD
         MVC   PRNTLINE+2(L'ERRMSG12),ERRMSG12
         MVC   PRNTLINE+2+L'ERRMSG12(L'NAME),NAME
         BAL   R9,PUTSP            PRINT ABEND MSG
         CLI   RETCODE+1,16
         BC    GE,TERM
         MVI   RETCODE+1,16
         B     TERM
STAEBASE DC    A(COPYCAT,COPYCAT+4096,COPYCAT+8192,SAVEAREA)
STAEWK   DS    H
STAECD   DC    CL3'   '            ABEND CODE
         EJECT
*---------------------------------------------------------------------*
*              SYNAD EXITS
*---------------------------------------------------------------------*
SYNADEX  SYNADAF ACSMETH=EXCP                                    EXCP01
         B     SYNAD
SYNADPS  SYNADAF ACSMETH=QSAM
SYNAD    MVC   PRNTLINE+2(L'ERRMSG13),ERRMSG13
         MVC   PRNTLINE+2+L'ERRMSG13(128-68),68(R1) MOVE ERROR MSG
         BAL   R9,PUTSP
         MVC   PRNTLINE+2(L'ERRMSG13+L'ERRMSGD),ERRMSG13
         MVC   PRNTLINE+2+L'ERRMSG13+L'ERRMSGD(L'NAME),NAME
         BAL   R9,PUTSP
         SYNADRLS
         CLI   RETCODE+1,16
         BC    GE,TERM
         MVI   RETCODE+1,16        SET RETURN CODE
         B     TERM
         EJECT
*---------------------------------------------------------------------*
*              READ A BLOCK + KEY  R1=A(TTR) R3=A(DCB) R14=RETURN
*                                  R2=AREA
*---------------------------------------------------------------------*
READC    SAVE  (14,12),,*
         LA    R14,READSAVE
         ST   R13,READSAVE+4
         ST    R14,8(R13)
         LR    R13,R14
         LA    R15,WRITEB          CHECK ANY WRITES              EXCP01
         BALR  R14,R15                                           EXCP01
         LA    R3,0(R3)            CLEAR BITS                    EXCP01
         ST    R2,RDAREA           SAVE FOR LATER                EXCP01
         LR    R7,R1               SAVE TTR ADDRESS              EXCP01
         LH    R0,0(R7)            LOAD TT                       EXCP01
         LA    R6,USEBUFQ          CHAIN PTR FOR USED BUFFERS    EXCP01
         LA    R1,2                SET LOOP COUNTER              EXCP01
         B     RD020                                             EXCP01
         USING BUFFER,R6                                         EXCP01
RD010    CH    R0,BUFTT            THIS TRACK?                   EXCP01
         BC    NE,RD020            NO,BRANCH                     EXCP01
         C     R3,BUFADCB          THIS DCB?                     EXCP01
         BC    EQ,RD140            YES,FOUND IT                  EXCP01
RD020    LR    R5,R6                                             EXCP01
         L     R6,BUFNEXT                                        EXCP01
         LTR   R6,R6               ANOTHER?                      EXCP01
         BC    NZERO,RD010         YES,CHECK IT                  EXCP01
         LA    R6,OUTBUFQ          NEXT QUEUE TO SEARCH          EXCP01
         BCT   R1,RD020            GO CHECK IT                   EXCP01
         LA    R9,ECBLISTW                                       EXCP01
RD025    L     R4,0(R9)            POINT AT NEXT WRITE AREA      EXCP01
         L     R6,WRBUF            POINT AT ACTIVE BUFFER        EXCP01
         LTR   R6,R6               ANY?                          EXCP01
         BC    ZERO,RD030          NO,BRANCH                     EXCP01
         CH    R0,BUFTT            THIS TRACK?                   EXCP01
         BC    NE,RD030            NO,BRANCH                     EXCP01
         C     R3,BUFADCB          THIS DCB?                     EXCP01
         BC    EQ,RD032            YES,THIS IS OUR BUF           EXCP01
RD030    LA    R9,4(R9)            NEXT ECB                      EXCP01
         LTR   R4,R4               END?                          EXCP01
         BC    PLUS,RD025          NO,BRANCH                     EXCP01
         B     RD035                                             EXCP01
RD032    OI    WRBUF,X'80'         SET DO NOT FREE               EXCP01
         WAIT  ECB=WRECB           WAIT FOR WRITE                EXCP01
         LA    R15,WRITEB                                        EXCP01
         BALR  R14,R15             GO AND CHECK                  EXCP01
         B     RD145               USE THIS BUFFER               EXCP01
RD035    EQU   *                                                 EXCP01
*----------------------------------------------------------------EXCP01
*              NOT FOUND IN CORE, HAVE TO READ TRACK             EXCP01
*----------------------------------------------------------------EXCP01
         LA    R15,GETBUF          GET A BUFFER                  EXCP01
         BALR  R14,R15                                           EXCP01
         LR    R6,R1               POINT AT IT                   EXCP01
         MVC   BUFNEXT,USEBUFQ     CHAIN IN AT HEAD              EXCP01
         ST    R6,USEBUFQ          CHAIN IN                      EXCP01
         ST    R3,BUFADCB          SAVE DCB ADDRESS              EXCP01
         LA    R4,RECB             POINT AT ECB,IOB ETC          EXCP01
         USING WRDSECT,R4                                        EXCP01
         ST    R6,WRBUF            SAVE BUF ADDR                 EXCP01
         STH   R0,BUFTT            SAVE TT ADDRESS               EXCP01
         SLL   R0,16                                             EXCP01
         AL    R0,=X'00000100'     REC ONE                       EXCP01
         L     R1,DCBDEBAD         DEB                           EXCP01
         LA    R2,IOBMBBCC         SPOT FO R MBBCCHHR            EXCP01
         STM   R9,R13,READSAVE+12                                EXCP01
         L     R9,16               CVT                           EXCP01
         L     R15,28(R9)          CVTPCNVT                      EXCP01
         BALR  R14,R15                                           EXCP01
         USING *,R14                                             EXCP01
         LM    R9,R13,READSAVE+12                                EXCP01
         DROP  R14                                               EXCP01
         LH    R1,VOLLMBLK-VOLLIST(R3)  REC/TRK                  EXCP01
         L     R5,IOBSTART         CCWS                          EXCP01
         USING RDCCWS,R5                                         EXCP01
         LA    R2,BUFEND+RBUFKEY-RBUFFER                         EXCP01
         B     RD050                                             EXCP01
RD040    LA    R2,RBUFEND-RBUFFER(R2) NEXT IN BUFFER             EXCP01
         LA    R5,RDCCWE-RDCCW(R5)                               EXCP01
RD050    ST    R2,RDCCW                                          EXCP01
         MVI   RDCCW,X'0E'         READ KEY + DATA               EXCP01
         OI    RDCCW+4,X'40'       COMMAND CHAIN                 EXCP01
         BCT   R1,RD040                                          EXCP01
         NI    RDCCW+4,FF-X'40'    OFF LAST COMMAND CHAIN        EXCP01
         ST    R3,IOBDCBPT                                       EXCP01
         LA    R1,WRIOB                                          EXCP01
         SPACE 2                                                 EXCP01
         EXCP  (1)                                               EXCP01
         SPACE 2                                                 EXCP01
RD080    WAIT  ECBLIST=ECBLISTR    WAIT FOR IO                   EXCP01
         TM    WRECB,X'40'         POSTED?                       EXCP01
         BC    ALLON,RD090         YES,TEST IT                   EXCP01
         LA    R15,WRITEB          MUST BE WRITE POSTED          EXCP01
         BALR  R14,R15             GO CHECK IT                   EXCP01
         B     RD080               GO WAIT FOR READ              EXCP01
RD090    CLI   WRECB,X'7F'         OK?                           EXCP01
         BC    NE,SYNADEX          NO,ERROR                      EXCP01
         L     R4,RDAREA                                         EXCP01
         LTR   R4,R4               ANY?                          EXCP01
         BC    ZERO,RD170          NO,BRANCH                     EXCP01
         LH    R1,VOLLMBLK-VOLLIST(R3) REC/TRK                   EXCP01
         LA    R5,BUFEND                                         EXCP01
         L     R2,DCBDEBAD         DEB                           EXCP01
         USING RBUFFER,R5                                        EXCP01
         MVI   BUFFLAG,X'00'                                     EXCP01
RD100    MVI   RBUFFLAG,X'00'      CLEAR FLAG                    EXCP01
         CLC   RBUFKEY,=XL8'00'    DUMMY REC?                    EXCP01
         BC    EQ,RD120            YES,BRANCH                    EXCP01
         TM    12(R2),X'0F'        INPUT?                        EXCP01
         BC    NALLOFF,RD120       NO,BRANCH                     EXCP01
         OI    RBUFFLAG,BUFNRTR    NOT YET READ                  EXCP01
RD120    LA    R5,RBUFEND                                        EXCP01
         BCT   R1,RD100            LOOP ON ALL RECS              EXCP01
         B     RD150                                             EXCP01
RD140    MVC   BUFNEXT-BUFFER(,R5),BUFNEXT DECHAIN               EXCP01
RD145    MVC   BUFNEXT,USEBUFQ     CHAIN IN AS FIRST             EXCP01
         ST    R6,USEBUFQ                                        EXCP01
RD150    L     R4,RDAREA                                         EXCP01
         USING PCAREA,R4                                         EXCP01
         LTR   R4,R4                                             EXCP01
         BC    ZERO,RD170          JUST READ TRACK               EXCP01
         SR    R1,R1                                             EXCP01
         IC    R1,2(R7)                                          EXCP01
         BCTR  R1,0                -1                            EXCP01
         MH    R1,=AL2(RBUFSIZE)                                 EXCP01
         LA    R5,BUFEND(R1)       POINT AT PROPER REC           EXCP01
         NI    RBUFFLAG,FF-BUFNRTR SET READ ONCE                 EXCP01
         LTR   R4,R4               RETAIN THIS REC IF POSSIBLE?  EXCP01
         BC    PLUS,RD160          NO,BRANCH                     EXCP01
         OI    RBUFFLAG,BUFRETN    RETAIN THIS BUF               EXCP01
RD160    MVC   0(8,R4),RBUFKEY     MOVE KEY                      EXCP01
         MVC   8(256,R4),RBUFDATA  MOVE DATA                     EXCP01
RD170    LH    R1,VOLLMBLK-VOLLIST(R3)                           EXCP01
         LA    R5,BUFEND                                         EXCP01
         NI    BUFFLAG,FF-BUFRETN-BUFWTR-BUFNRTR CLEAR BITS      EXCP01
RD190    OC    BUFFLAG,RBUFFLAG                                  EXCP01
         LA    R5,RBUFEND                                        EXCP01
         BCT   R1,RD190                                          EXCP01
         TM    BUFFLAG,BUFRETN+BUFNRTR ALL DONE WITH BUF?        EXCP01
         BC    NALLOFF,RD200       NO,BRANCH                     EXCP01
         LTR   R4,R4               JUST READ THE TRACK?          EXCP01
         BC    ZERO,RD200          YES,BRANCH                    EXCP01
         MVC   USEBUFQ,BUFNEXT     DECHAIN THIS BUF (FIRST)      EXCP01
         TM    BUFFLAG,BUFWTR      WRITE?                        EXCP01
         LA    R1,FREEBUFQ                                       EXCP01
         BC    ALLOFF,RD195        NO,BRANCH                     EXCP01
         LA    R1,OUTBUFQ                                        EXCP01
RD195    MVC   BUFNEXT,0(R1)       CHAIN ON FREE OR OUT          EXCP01
         ST    R6,0(R1)                                          EXCP01
RD200    LA    R15,WRITEB          GO CHECK WRITES               EXCP01
         BALR  R14,R15                                           EXCP01
READ020  L     R13,READSAVE+4
         RETURN (14,12),T
READSAVE DS    18F
RDAREA   DS    A =0 THEN READ TRACK,ELSE READ REC,IF <0 THEN RETAEXCP01
         EJECT
*---------------------------------------------------------------------*
*              WRITE A BLOCK  R2->AREA R1=A(TTR),R3=A(DCB)
*              IF R1 = 0 THEN ONLY CHECK
*---------------------------------------------------------------------*
WRITEC   SAVE  (14,12),,*
         LA    R14,WRITSAVE
         ST    R13,WRITSAVE+4
         ST    R14,8(R13)
         LR    R13,R14
         LR    R7,R1               POINT AT TTR                  EXCP01
         ST    R2,WTRAREA          SAVE AREA                     EXCP01
         LA    R15,WRITEB          CHECK WRITES                  EXCP01
         BALR  R14,R15                                           EXCP01
         LH    R0,0(R7)            LOAD TT                       EXCP01
         LA    R3,0(R3)            CLEAR HIGH BYTE               EXCP01
         LA    R6,USEBUFQ                                        EXCP01
         B     WTR020              GO AND TEST                   EXCP01
WTR010   CH    R0,BUFTT            THIS TRACK?                   EXCP01
         BC    NE,WTR020           NO,BRANCH                     EXCP01
         C     R3,BUFADCB          THIS DCB?                     EXCP01
         BC    EQ,WTR070           YES,FOUND IN CORE             EXCP01
WTR020   LR    R5,R6                                             EXCP01
         L     R6,BUFNEXT                                        EXCP01
         LTR   R6,R6               ANY MORE?                     EXCP01
         BC    NZERO,WTR010        NO,BRANCH                     EXCP01
         CH    R0,VOLLTTR-VOLLIST(R3) NEW TRACK?                 EXCP01
         BC    LT,WTR060           NO,BRANCH                     EXCP01
         LA    R15,GETBUF                                        EXCP01
         BALR  R14,R15             GET A BUFFER                  EXCP01
         LR    R6,R1               POINT AT IT                   EXCP01
         ST    R3,BUFADCB          SAVE DCB ADDRESS              EXCP01
         STH   R0,BUFTT            SAVE TT                       EXCP01
         MVC   BUFNEXT,USEBUFQ     CHAIN IN                      EXCP01
         ST    R6,USEBUFQ                                        EXCP01
         MVI   BUFFLAG,BUFNCOMP    SET TRACK NOT COMPLETE        EXCP01
         TM    VOLLFLAG-VOLLIST(R3),VOLLSECN NEED NEW EXTENT?    EXCP01
         BC    ALLOFF,WTR040       NO,BRANCH                     EXCP01
         LR    R1,R3               POINT AT DCB                  EXCP01
         EOV   (1)                 GET NEW EXTENT                EXCP01
         NI    VOLLFLAG-VOLLIST(R3),FF-VOLLSECN  CLEAR END OF EXTEXCP01
WTR040   LA    R5,BUFEND                                         EXCP01
         USING RBUFFER,R5                                        EXCP01
         LH    R1,VOLLMBLK-VOLLIST(R3) REC/TRK                   EXCP01
WTR050   MVI   RBUFFLAG,BUFNCOMP   SET NOT COMPLETE              EXCP01
         MVC   RBUFFLAG+1(3),=X'EEEEEE' FOR DUMP                 EXCP01
         XC    RBUFKEY,RBUFKEY     CLEAR BUFF                    EXCP01
         XC    RBUFDATA,RBUFDATA                                 EXCP01
         LA    R5,RBUFEND          NEXT REC BUF                  EXCP01
         BCT   R1,WTR050                                         EXCP01
         B     WTR080                                            EXCP01
WTR060   LR    R1,R7               POINT AT TTR                  EXCP01
         SR    R2,R2               ONLY READ TRACK               EXCP01
         LA    R15,READC                                         EXCP01
         BALR  R14,R15             READ TRACK                    EXCP01
         L     R6,USEBUFQ          POINT AT IT                   EXCP01
         B     WTR080                                            EXCP01
WTR070   MVC   BUFNEXT-BUFFER(,R5),BUFNEXT                       EXCP01
         MVC   BUFNEXT,USEBUFQ     CHAIN IN AT HEAD              EXCP01
         ST    R6,USEBUFQ                                        EXCP01
WTR080   SR    R1,R1                                             EXCP01
         IC    R1,2(R7)            LOAD R                        EXCP01
         BCTR  R1,0                                              EXCP01
         MH    R1,=AL2(RBUFSIZE)                                 EXCP01
         LA    R5,BUFEND(R1)                                     EXCP01
         L     R2,WTRAREA          POINT AT INPUT DATA           EXCP01
         MVC   RBUFKEY,0(R2)       MOVE KEY                      EXCP01
         MVC   RBUFDATA,8(R2)      MOVE DATA                     EXCP01
         MVI   RBUFFLAG,BUFWTR     SET NEEDS TO BE WRITTEN       EXCP01
         LTR   R2,R2               RETAIN?                       EXCP01
         BC    PLUS,WTR090         YES,BRANCH                    EXCP01
         OI    RBUFFLAG,BUFRETN    SET RETAIN                    EXCP01
WTR090   MVI   BUFFLAG,X'00'       CLEAR FLAG                    EXCP01
         LA    R5,BUFEND                                         EXCP01
         LH    R1,VOLLMBLK-VOLLIST(R3) REC/TRK                   EXCP01
WTR100   OC    BUFFLAG,RBUFFLAG                                  EXCP01
         LA    R5,RBUFEND          NEXT REC                      EXCP01
         BCT   R1,WTR100                                         EXCP01
*----------------------------------------------------------------EXCP01
*              CHECK IF THIS IS LAST REC OF EXTENT               EXCP01
*----------------------------------------------------------------EXCP01
         CLI   VOLLTTR+2-VOLLIST(R3),X'01' IS NEXT REC 1?        EXCP01
         BC    NE,WTR130           NO,BRANCH                     EXCP01
         LH    R0,0(R7)            LOAD TT                       EXCP01
         AH    R0,=H'1'            +1                            EXCP01
         CH    R0,VOLLTTR-VOLLIST(R3) IS THIS THE NEXT TRACK?    EXCP01
         BC    NE,WTR130           NO,BRANCH                     EXCP01
         SLL   R0,16                                             EXCP01
         L     R1,DCBDEBAD         DEB                           EXCP01
         LA    R2,WRITSAVE+12      SPOT FOR MBBCCHHR             EXCP01
         STM   R9,R13,WRITSAVE+20                                EXCP01
         L     R9,16                                             EXCP01
         L     R15,28(R9)          CVTPCNVT                      EXCP01
         BALR  R14,R15                                           EXCP01
         USING *,R14                                             EXCP01
         LM    R9,R13,WRITSAVE+20                                EXCP01
         DROP  R14                                               EXCP01
         LTR   R15,R15             OK?                           EXCP01
         BC    ZERO,WTR130         IN EXTENT,BRANCH              EXCP01
         LA    R2,DCBFDAD          SPOT FOR MMBBCCHHR IN DCB     EXCP01
         LH    R0,0(R7)            TT                            EXCP01
         SLL   R0,16               MOVE                          EXCP01
         STM   R9,R13,WRITSAVE+20                                EXCP01
         L     R9,16                                             EXCP01
         L     R15,28(R9)          CVTPCNVT                      EXCP01
         BALR  R14,R15                                           EXCP01
         USING *,R14                                             EXCP01
         LM    R9,R13,WRITSAVE+20                                EXCP01
         DROP  R14                                               EXCP01
         OI    VOLLFLAG-VOLLIST(R3),VOLLSECN SET LAST REC IN EXTEEXCP01
WTR130   TM    BUFFLAG,BUFRETN+BUFNCOMP ANY REASON NOT TO WRITE? EXCP01
         BC    NALLOFF,WTR190      YES,BRANCH                    EXCP01
         MVC   USEBUFQ,BUFNEXT     DECHAIN                       EXCP01
         LR    R1,R6                                             EXCP01
         LA    R5,OUTBUFQ                                        EXCP01
         L     R6,0(R5)                                          EXCP01
WTR150   LTR   R6,R6               LAST?                         EXCP01
         BC    ZERO,WTR160         YES,BRANCH                    EXCP01
         LR    R5,R6                                             EXCP01
         L     R6,BUFNEXT                                        EXCP01
         B     WTR150              GO FIND END                   EXCP01
WTR160   ST    R1,BUFNEXT-BUFFER(R5)                             EXCP01
         ST    R6,BUFNEXT-BUFFER(R1)                             EXCP01
         LA    R15,WRITEB          GO START A WRITE              EXCP01
         BALR  R14,R15             GO START THE WRITE            EXCP01
WTR190   L     R13,WRITSAVE+4                                    EXCP01
         RETURN (14,12),T                                        EXCP01
WRITSAVE DS    18F                 SAVE AREA
WTRAREA  DS    A                   INPUT AREA ADDR               EXCP01
         EJECT                                                   EXCP01
*----------------------------------------------------------------EXCP01
*              CHECK ANY ACTIVE WRITES, AND START ANY IF POSSIBLEEXCP01
*----------------------------------------------------------------EXCP01
WRITEB   SAVE  (14,12),,*                                        EXCP01
         LA    R14,WTRBSAVE                                      EXCP01
         ST    R13,WTRBSAVE+4                                    EXCP01
         ST    R14,8(R13)                                        EXCP01
         LR    R13,R14                                           EXCP01
         LA    R9,ECBLISTW         POINT AT LIST OF ECB ADDRS    EXCP01
WTRB010  L     R4,0(R9)            POINT AT NEXT ECB-IOB AREA    EXCP01
         USING WRDSECT,R4                                        EXCP01
         L     R6,WRBUF            LOAD BUFFER ADDRESS           EXCP01
         USING BUFFER,R6                                         EXCP01
         LTR   R6,R6               ACTIVE?                       EXCP01
         BC    ZERO,WTRB030        NO,BRANCH                     EXCP01
         TM    WRECB,X'40'         POSTED?                       EXCP01
         BC    ALLOFF,WTRB070      NO,BRANCH                     EXCP01
         CLI   WRECB,X'7F'         OK?                           EXCP01
         LA    R1,WRIOB            POINT AT IOB IF ERROR         EXCP01
         BC    NE,SYNADEX          ERROR,BRANCH                  EXCP01
         XC    WRBUF,WRBUF         CLEAR BUFFER ADDR             EXCP01
         MVI   WRECB,X'00'         CLEAR ECB                     EXCP01
         LTR   R6,R6               DOES READ WANT THIS BUFFER?   EXCP01
         BC    NPLUS,WTRB030       YES,DONT CHAIN TO FREE BUFS   EXCP01
         MVC   BUFNEXT,FREEBUFQ    ADD THIS BUFFER TO FREE QUEUE EXCP01
         ST    R6,FREEBUFQ                                       EXCP01
WTRB030  L     R6,OUTBUFQ          ANY BUFFERS TO WRITE?         EXCP01
         LTR   R6,R6                                             EXCP01
         BC    ZERO,WTRB070        NO,BRANCH                     EXCP01
         MVC   OUTBUFQ,BUFNEXT     DECHAIN THIS BUFFER           EXCP01
         ST    R6,WRBUF                                          EXCP01
         L     R3,BUFADCB          POINT AT DCB FOR THIS BUFFER  EXCP01
         LH    R0,BUFTT            TRACK ADDR                    EXCP01
         SLL   R0,16               TT00                          EXCP01
         L     R1,DCBDEBAD         POINT AT DEB                  EXCP01
         LA    R2,IOBMBBCC         SPOT FOR MBBCCHHR             EXCP01
         STM   R9,R13,WTRBSAVE+12  SAVE REGS                     EXCP01
         L     R9,16               CVT ADDR                      EXCP01
         L     R15,28(R9)           CVTPCNVT                     EXCP01
         BALR  R14,R15             CONVERT TTR TO MBBCCHHR       EXCP01
         USING *,R14                                             EXCP01
         LM    R9,R13,WTRBSAVE+12                                EXCP01
         DROP  R14                                               EXCP01
         LH    R1,VOLLMBLK-VOLLIST(R3) LOAD REC/TRK FOR THIS DEVIEXCP01
         L     R5,IOBSTART         POINT AT CCWS                 EXCP01
         USING WTRCCWS,R5                                        EXCP01
         LA    R2,BUFEND+RBUFKEY-RBUFFER FIRST REC               EXCP01
         B     WTRB060                                           EXCP01
WTRB050  LA    R2,RBUFEND-RBUFFER(R2) NEXT RECORD IN BUFFER      EXCP01
         LA    R5,WTRCCWE-WTRCCW(R5) NEXT CCW SET                EXCP01
WTRB060  ST    R2,WTRCCWD          SAVE KEY AND DATA ADDRESS     EXCP01
         MVC   WTRCCWC(4),IOBMBBCC+3 MOVE CCHH                   EXCP01
         OI    WTRCCWD+4,X'40'     SET COMMAND CHAIN             EXCP01
         BCT   R1,WTRB050          LOOP ON ALL RECORDS           EXCP01
         NI    WTRCCWD+4,FF-X'40'  OFF COMMAND CHAIN ON LAST     EXCP01
         ST    R3,IOBDCBPT         SAVE DCB ADDRESS              EXCP01
         LA    R1,WRIOB            POINT AT IOB                  EXCP01
         SPACE 2                                                 EXCP01
         EXCP  (1)                 WRITE A TRACK                 EXCP01
         SPACE 2                                                 EXCP01
WTRB070  LA    R9,4(R9)            NEXT ECB                      EXCP01
         LTR   R4,R4               WAS THIS THE LAST             EXCP01
         BC    PLUS,WTRB010        NO,LOOK AT NEXT               EXCP01
         L     R13,WTRBSAVE+4                                    EXCP01
         RETURN (14,12),T                                        EXCP01
WTRBSAVE DS    18F                                               EXCP01
         EJECT                                                   EXCP01
*----------------------------------------------------------------EXCP01
*              GETBUF - GET A BUFFER AND RETURN IN R1            EXCP01
*----------------------------------------------------------------EXCP01
GETBUF   SAVE  (14,12),,*                                        EXCP01
         LA    R14,GBSAVE                                        EXCP01
         ST    R13,GBSAVE+4                                      EXCP01
         ST    R14,8(R13)                                        EXCP01
         LR    R13,R14                                           EXCP01
GB010    LA    R15,WRITEB          CHECKS WRITES                 EXCP01
         BALR  R14,R15                                           EXCP01
         L     R6,FREEBUFQ         POINT AT FREE BUFS            EXCP01
         LTR   R6,R6               ANY?                          EXCP01
         BC    NZERO,GB090         YES,BRANCH                    EXCP01
*----------------------------------------------------------------EXCP01
*              NO BUFFERS AVAILABLE,FIND ONE                     EXCP01
*----------------------------------------------------------------EXCP01
         LA    R2,ECBLISTW                                       EXCP01
GB020    L     R4,0(R2)                                          EXCP01
         USING WRDSECT,R4                                        EXCP01
         OC    WRBUF,WRBUF         ACTIVE?                       EXCP01
         BC    NZERO,GB040         YES,BRANCH                    EXCP01
         LTR   R4,R4               END OF LIST                   EXCP01
         BC    NPLUS,GB050         YES,NO WRITES ACTIVE          EXCP01
         LA    R2,4(R2)            NEXT WRITE ECB                EXCP01
         B     GB020                                             EXCP01
GB040    WAIT  ECBLIST=ECBLISTW    WAIT FOR WRITE TO COMPLETE    EXCP01
         B     GB010               GO CHECK IT, AND USE ITS BUF  EXCP01
*              NO BUFFS BEING WRITEN,GET ONE IN USE              EXCP01
GB050    LA    R5,USEBUFQ                                        EXCP01
         L     R6,0(R5)            FIRST BUF                     EXCP01
         SR    R1,R1               CLEAR FOUND BUF               EXCP01
         SR    R2,R2               CLEAR LAST ONE                EXCP01
GB060    TM    BUFFLAG,BUFNCOMP    NOT COMPLETE?                 EXCP01
         BC    ALLON,GB070         YES,CANT USE IT               EXCP01
         LR    R2,R5               SAVE                          EXCP01
         LR    R1,R6               SAVE                          EXCP01
GB070    LR    R5,R6                                             EXCP01
         L     R6,BUFNEXT          NEXT                          EXCP01
         LTR   R6,R6               MORE?                         EXCP01
         BC    NZERO,GB060         LOOK AT MORE                  EXCP01
         LR    R6,R1                                             EXCP01
         MVC   BUFNEXT-BUFFER(,R2),BUFNEXT DECHAIN               EXCP01
         TM    BUFFLAG,BUFWTR      NEED TO BE WRITTEN?           EXCP01
         BC    ALLOFF,GB100        NO,USE IT                     EXCP01
         MVC   BUFNEXT,OUTBUFQ     QUEUE FOR OUTPUT              EXCP01
         ST    R6,OUTBUFQ                                        EXCP01
         B     GB010               GO START WRITE AND WAIT       EXCP01
GB090    MVC   FREEBUFQ,BUFNEXT    DECHAIN                       EXCP01
GB100    LR    R1,R6                                             EXCP01
         L     R13,GBSAVE+4                                      EXCP01
         LM    R14,R0,12(R13)                                    EXCP01
         RETURN (2,12),,T                                        EXCP01
GBSAVE   DS    18F                                               EXCP01
         DROP  R4,R5,R6                                          EXCP01
         EJECT
*---------------------------------------------------------------------*
*              INR - FIND NEXT ENTRY IN CATALOGS DEFINED BY INTAB
*              RETURN IN R1 ADDR OF ENTRY,AND SET INDCBA
*---------------------------------------------------------------------*
INR      SAVE  (14,12),,*
         ST    R13,INRSAVE+4
         LA    R14,INRSAVE
         ST    R14,8(R13)
         LR    R13,R14
         L     R5,INRLAST          POINT AT LAST ENTRY
         USING CATENTRY,R5                                       EXCP01
         L     R3,INDCBA           POINT AT LAST INLIST
         USING INLIST,R3
         LTR   R5,R5               FIRST TIME?
         BC    ZERO,INR200         YES,GO SETUP
INR010   SR    R1,R1
         IC    R1,CATCOUNT         # OF HALF WORDS
         AR    R1,R1               *2
         LA    R5,12(R1,R5)        POINT AT NEXT ENTRY
INR020   CLI   CATCOUNT,CILE       ILE OR IPE?
         BC    NE,INR030           NO,BRANCH
         CLI   CATNAME,X'FF'       ILE?
         BC    NE,INR030           NO,BRANCH
         CLC   CATTTR,=XL3'00'     END?
         BC    EQ,INR030           YES,BRANCG
         MVC   INTTR,CATTTR        MOVE TTR
         LA    R1,INTTR            POINT AT TTR
         LA    R2,INKEY            POINT AT KEY AND BUFFER
         LA    R15,READC
         BALR  R14,R15             READ NEXT BLOCK
         LA    R5,INBUF+2          POINT AT NEXT ENTRY
         B     INR020              GO LOOK AT THIS ONE
INR030   LA    R1,0(R3)            CLEAR HIGH BYTE
         SR    R5,R1
         STH   R5,INOFF            SAVE OFFSET
*---------------------------------------------------------------------*
*              FIND LOWEST NAME
*---------------------------------------------------------------------*
INR035   MVC   INLOWNAM,=XL8'FFFFFFFFFFFFFFFF'
         LA    R4,INTAB            POINT AT FIRST INLIST
INR040   L     R3,0(R4)            POINT AT INLIST
         LH    R5,INOFF            LOAD OFFSET TO NEXT ENTRY
         LA    R5,0(R3,R5)         POINT AT ENTRY
         CLC   INLOWNAM,CATNAME    LOOK AT THIS NAME?
         BC    LT,INR050           NO,LOOK AT NEXT INLIST
         BC    EQ,INR080           SANE CHECK CONFLICTS
         MVC   INLOWNAM,CATNAME    THIS IS NOW LOWEST
         ST    R3,INDCBA           SAVE
         ST    R5,INRLAST          SAVE ENTRY ADDRESS
         MVC   INRLASTC,CATCOUNT   SAVE COUNT ALSO
INR050   TM    0(R4),X'80'         LAST ENTRY?
         BC    ALLON,INR060        YES,USE THIS ENTRY
         LA    R4,4(R4)            LOOK AT NEXT
         B     INR040
INR060   SR    R1,R1
         CLI   INLOWNAM,X'FF'      END OF ALL?
         BC    EQ,INR070           YES,BEANCH
         L     R1,INRLAST          LOAD ADDRESS OF ENTRY
INR070   L     R13,INRSAVE+4
         LM    R14,R0,12(R13)      LOAD R14-R0
         RETURN (2,12),T
*---------------------------------------------------------------------*
*              SAME NAME FOUND TWICE
*---------------------------------------------------------------------*
INR080   CLI   CATNAME,X'FF'       END;
         BC    EQ,INR050           OK FOR END
INR082   CLI   CATCOUNT,CCVPE      CVOL?
         BC    EQ,INR090           YES,BEANCH
         CLI   CATCOUNT,CNCVPE     NEW CVOL?
         BC    EQ,INR100           YES,BRANCH
         CLI   INRLASTC,CCVPE      FIRST A CVOL?
         BC    EQ,INR130           YES,BRANCH
         CLI   INRLASTC,CNCVPE     FIRST A NEW CVOL?
         BC    EQ,INR130           YES,BRANCH
INR085   MVC   ERRMSG7+43(6),INVOL MOVE SECOND VOLUME NAME
         MVC   ERRMSG7+12(8),INLOWNAM MOVE INDEX NAME
         L     R1,INDCBA           ON THE FIRST
         MVC   ERRMSG7+36(6),INVOL-INLIST(R1) FIRST VOLUME NAME
         MVC   ERRMSG7+64(6),INVOL-INLIST(R1) FIRST VOLUME NAME
         MVC   PRNTLINE+2(L'ERRMSG7),ERRMSG7
         BAL   R9,PUTSP            PRINT ERROR
         CLI   RETCODE+1,4
         BC    GE,INR010
         MVI   RETCODE+1,4
         B     INR010              GET RID OF SECOND NAME
INR090   LA    R2,CCVPEVOL         POINT AT VOLUME
         B     INR110
INR100   LA    R2,CNCVPEVO         POINT AT VOL NAME
INR110   LA    R1,INTAB            POINT AT INPUT TABLE
INR115   L     R6,0(R1)            POINT AT INLIST
         CLC   0(6,R2),INVOL-INLIST(R6) CVOL FOR SOME INPUT?
         BC    EQ,INR010           YES,DONT NEED IT
INR120   TM    0(R1),X'80'         LAST?
         BC    ALLON,INR085        YES,BRANCH
         LA    R1,4(R1)            POINT AT NEXT ENTRY
         B     INR115
INR130   L     R0,INRLAST          INTER CHANGE ENTRIES
         ST    R5,INRLAST
         MVC   INRLASTC,CATCOUNT
         LR    R5,R0
         L     R1,INDCBA
         ST    R3,INDCBA
         LR    R3,R1
         B     INR082              GO CLEAN UP
*---------------------------------------------------------------------*
*              INITIALIZE DCBS
*---------------------------------------------------------------------*
INR200   LA    R4,INTAB
INR210   L     R3,0(R4)            POINT AT NEXT
         MVC   INTTR,=XL3'000001'  SET FOR FIRST LEVEL
INR215   LA    R1,INTTR
         LA    R2,INKEY
         LA    R15,READC
         BALR  R14,R15             READ BLOCK
         LA    R5,INBUF+2          POINT AT FIRST ENTRY
         TM    0(R4),X'80'         LAST?
         BC    ALLON,INR240        YES, PASS A VICE BACK
         CLI   CATCOUNT,CVICE      VICE?
         BC    NE,INR220           NO,BRANCH
         CLI   CATNAME,X'00'       VICE?
         BC    NE,INR230           NO,BRANCH
         LA    R5,CVICE*2+12(R5)   POINT AT NEXT ENTRY
INR220   CLI   CATCOUNT,CILE       ILE?
         BC    NE,INR230           NO MUST BE ENTRY
         CLI   CATNAME,X'FF'       ILE OR IPE?
         BC    NE,INR230           IPE OK
         CLC   CATTTR,=XL3'00'     END?
         BC    EQ,INR230           YES,LET STAND
         MVC   INTTR,CATTTR
         B     INR215              GO READ NEXT
INR230   LA    R1,0(R3)
         SR    R5,R1               FIND OFFSET
         STH   R5,INOFF
         LA    R4,4(R4)            POINT AT NEXT INLIST
         B     INR210
INR240   ST    R5,INRLAST          SAVE ADDRESS
         ST    R3,INDCBA           SAVE ADDRESS OF DCB
         LR    R1,R5               SET RETURN
         B     INR070              GO RETUEN VICE
INRSAVE  DS    18F
         EJECT
*---------------------------------------------------------------------*
*              PUTC - PUT ROUTINE FOR CATALOG
*              R1=A(ENTRY) R0=A(PUTC AREA) R7=A(LEVTAB),
*              R8=A(VOLLIST)
*              IF R1=0 THEN END THIS INDEX
*              IF R1<0 THEN FILL THIS TRACK
*---------------------------------------------------------------------*
PUTC     SAVE  (14,12),,*
         LA    R14,PUTCSAVE
         ST    R13,PUTCSAVE+4
         ST    R14,8(R13)
         LR    R13,R14
         LR    R6,R0               SAVE PUT AREA ADDRESS
         USING PCAREA,R6
         LTR   R5,R1
         USING CATENTRY,R5
         BC    PLUS,PC004
         BC    MINUS,PC002
         LH    R4,LEVBLOCK         LOAD NUMBER OF BLOCKS TO LEV
         AH    R4,=H'1'            +1 FOR BCT
         MVC   PCKEY,=XL8'FFFFFFFFFFFFFFFF'
         B     PC050               GO END UP
PC002    LA    R6,PUTTTR           POINT AT DUMMY RECORD
         LH    R0,LEVBLONT         LOAD NUMBER OF BLOCKS NEEDED ON A T
         SR    R4,R4
         IC    R4,VOLLTTR+2        LOAD NEXT RECORD NUMBER
         SH    R4,VOLLMBLK         - MAX
         LCR   R4,R4               R4 HAS # LEFT -1
         SH    R0,=H'1'            R0 HAS # NEEDED-1
         CR    R0,R4               ARE THERE ENOUGH?
         BC    LE,PCRET            YES,BRANCH
         AH    R4,=H'1'            ADD ONE FOR BCT
PC003    LH    R15,VOLLIDUM        COUNT DUMMY RECORDS
         AH    R15,=H'1'
         STH   R15,VOLLIDUM        SAVE
         CLC   VOLLFRST,=XL3'00'   ANY AVABILE BLKS SO FAR?
         BC    NE,PC060            YES,BRANCH
         MVC   VOLLFRST,VOLLTTR    SET THIS AS FIRST
         B     PC060               NO,GO ADD DUMMY RECORDS
PC004    LA    R1,CVCBNEXT-CATNAME+PCBUF-8 A(TTR-8) IF VCB       F00001
         TM    PCFLAG,PCVCB        PCV?
         BC    ALLON,PC060         YES,GO WRITE
         SR    R1,R1
         IC    R1,CATCOUNT         LOAD # OF HALF WORDS
PC005    LA    R1,12(R1,R1)        LENGTH=12+2*CATCOUNT
         TM    PCFLAG,PCCLBUF
         BC    ALLON,PC008         BUFFER IN USE
         XC    PCKEY,PCKEY         CLEAR KEY
         XC    PCBUF,PCBUF         CLEAR BUFFER
         MVI   PCBUF+1,X'02'       SET LENGTH USED
         OI    PCFLAG,PCCLBUF      SET BUFFER IN USE
PC008    LH    R2,PCBUF            LOAD COUNT USED SO FAR
PC010    LA    R3,12(R2,R1)        SET NEW LENGTH IF ADDED TO BLOCK
         LH    R0,LEVBYTES         LOAD BYTES TO LEAVE IN A BLOCK
         AR    R0,R3
         CH    R0,=H'256'          WILL IT FIT?
         BC    GT,PC050            NO,BRANCH
         LA    R0,0(R2,R1)         SET NEW LENGTH
         STH   R0,PCBUF            SAVE IN BUFFER
         LA    R2,PCBUF(R2)        POINT AT TO
         BCTR  R1,0                -1 FOR EXECUTE
         EX    R1,PCMVC            MOVE ENTRY
         MVC   PCKEY,0(R5)         MOVE KEY ALSO
         CLI   LEVEL+1,X'01'       LEVEL ONE?
         BC    NE,PCRET            NO,BRANCH
         CLI   CATCOUNT,CIPE       IPE?
         BC    EQ,PC020            YES,BRANCH
         CLI   CATCOUNT,CGIPE      GIPE?
         BC    EQ,PC020            YES,BRANCH
         CLI   CATCOUNT,CVCBPE     VCBPE?
         BC    NE,PCRET            NO,BRANCH
         MVI   CATCOUNT-CATNAME(R2),X'06' RESET TO 06
PC020    L     R1,INDCBA           POINT AT INDCB
         OC    CATCOUNT-CATNAME(1,R2),ININDEX-INLIST(R1) SAVE INDEX
         B     PCRET               GO RETURN
*              ENTRY DOES NOT FIT,WRITE CURRENT,ADD NEXT
PC050    LH    R2,PCBUF            LOAD LENGTH SO FAR
         LA    R1,PCBUF(R2)        POINT AT NEXT SPOT
         LA    R2,12(R2)           +LENGTH FOR ILE
         STH   R2,PCBUF            SAVE IT
         MVC   0(8,R1),=XL8'FFFFFFFFFFFFFFFF' SET NAME AT FF
PC060    MVC   PUTTTR,VOLLTTR      POINT AT TTR FOR THIS RECORD
         SR    R0,R0
         IC    R0,VOLLTTR+2        LOAD RECORD NUMBER
         AH    R0,=H'1'            +1
         CH    R0,VOLLMBLK         # OF BLOCKS / TRK
         BC    LE,PC070            OK,BRANCH
         LH    R0,VOLLTTR          LOAD IT
         AH    R0,=H'1'            +1
         STH   R0,VOLLTTR          SAVE NEW TTR
         LTR   R5,R5                TEST OPERATION
         BC    NPLUS,PC065         DONT SET KEY
         TM    PCFLAG,PCVCB        VCB?
         BC    ALLON,PC065         YES,BRANCH
*******************************************************************CCN*
*                                                                  CCN
*  THE FOLLOWING INSTRUCTION CAUSES A SEEK TO OCCUR AT THE END OF  CCN
*  EACH TRACK.  THIS CUASES UNNECESSARY OVERHEAD, AND IS NOT       CCN
*  REQUIRED BY BLDL, WHO KNOWS HOW TO DO MULTITRACK SEARCHES       CCN
*  JUST FINE.  CHRIS THOMAS, UCLA/CCN, 03/23/77.                   CCN
**CCN**  MVC   PCKEY,=XL8'FFFFFFFFFFFFFFFF'                        CCN
*******************************************************************CCN*
PC065    EQU   *
         MVI   VOLLTTR+2,X'01'     SET RECORD TO 1
         B     PC080
PC070    STC   R0,VOLLTTR+2        SAVE RECORD #
PC080    LTR   R5,R5               END OF FILE?
         BC    NPLUS,PC085         YES,BRANCH
         TM    PCFLAG,PCVCB        VCB?
         BC    ALLOFF,PC084        NO,BRANCH
         OC    CVCBNEXT-CATENTRY+PCBUF,CVCBNEXT-CATENTRY+PCBUF
         BC    ZERO,PC085          THIS IS LAST,BRANCH
PC084    MVC   8(3,R1),VOLLTTR     MOVE NEXT BLOCK
PC085    LA    R1,PUTTTR           POINT AT TTR
         LA    R2,PCKEY            POINT AT KEY
         CLC   PCKEY,=XL8'00'      DUMMY?                        EXCP01
         BC    EQ,PC090            YES,BRANCH                    EXCP01
         AL    R2,=X'80000000'     RETAIN                        EXCP01
PC090    EQU   *                                                 EXCP01
         LA    R3,VOLLDCB          POINT AT DCB
         LA    R15,WRITEC
         BALR  R14,R15             WRITE THIS BLOCK
PC095    CLC   PCTTR,=X'000000'    NEED TTR FOR CALLER?
         BC    NE,PC100            NO,BRANCH
         MVC   PCTTR,PUTTTR        SAVE
PC100    NI    PCFLAG,X'FF'-PCCLBUF
         TM    PCFLAG,PCVCB        VCB?                          F00001
         BC    ALLON,PCRET         YES,ALL DONE                  F00001
         LTR   R5,R5               EOF?
         BC    PLUS,PC004          NO,BRANCH
         LA    R6,PUTTTR           SET DUMMY RECORD POINTER
         BCT   R4,PC003            GO ADD DUMMY RECORDS
PCRET    L     R13,PUTCSAVE+4
         RETURN (14,12),T
PCMVC    MVC   0(0,R2),0(R5)       USED BY EXECUTE
PUTCSAVE DS    18F
PUTTTR   DS    XL3                 USED AS DUMMY PCAREA AND FOR TTR
         DC    X'00'               FLAGS
         DC    XL8'00'             DUMMY KEY
         DC    XL256'00'           DUMMY RECORD
         DROP  R6
         EJECT
*---------------------------------------------------------------------*
*              GDSPROC - MOVE OR COPY GDS DUMMY DSCBS
*              INDCBA=A(INPUT DCB) R8-> VOLLIST FOR OUTPUT
*              NAME HAS DATA SET NAME
*---------------------------------------------------------------------*
GDSPROC  SAVE  (14,12),,*
         LA    R14,GDSSAVE
         ST    R13,GDSSAVE+4
         ST    R14,8(R13)
         LR    R13,R14
*---------------------------------------------------------------------*
*              FIND INPUT VOLUME AND DSCB
*---------------------------------------------------------------------*
         L     R3,INDCBA
         L     R4,DCBDEBAD-IHADCB(R3) POINT AT DEB
         L     R1,DCBDEBAD-IHADCB(R8) POINT AT OUTPUT DEB
         CLC   33(3,R1),33(R4)     SAME UCB?
         BC    EQ,GDS100           YES,NOTHING TO DO
         MVC   ALLOCUCB+1(3),33(R1) MOVE UCB ADDRESS
         L     R4,32(R4)           POINT AT UCB
         CLI   2(R4),X'FF'         UCB?
         BC    NE,GDS010           NO,BRANCH
         MVC   SCRDEV,16(R4)       MOVE DEVICE TYPE
         MVC   OBTVOL,28(R4)       MOVE VOLID
         B     GDS020
GDS010   MVC   OBTVOL,4(R4)        MOVE CELL VOLID
         LH    R1,0(R4)            LOAD BIN NUMBER
         MH    R1,=H'-16'          SIZE OF CELL ENTRY
         AR    R1,R4
         AL    R1,=F'-56'          POINT AT START OF UCB
         MVC   SCRDEV,16(R1)       MOVE DEVICE TYPE
GDS020   MVC   ERRMSG9+36(6),OBTVOL
         SR    R0,R0
         OBTAIN OBTCLIST           FIND OLD DSCB
         LTR   R15,R15             FOUND?
         BC    ZERO,GDS030         YES,BRANCH
         CH    R15,=H'8'           NOT FOUND?
         BC    EQ,GDS100           YES,BRANCH
         MVC   ERRMSG9+6(8),=CL8'OBTAIN' SET COMMENT
         B     GDSERR9             GO PRINT ERR MSG
GDS030   CLI   NAME+59,X'00'       ZERO EXTENTS?
         BC    NE,GDSERR10         NO,ERROR BRANCH
         XC    NAME+108(8),NAME+108
         L     R1,ALLOCUCB         POINT AT UCB
         CLI   2(R1),X'FF'         UCB?
         BC    EQ,GDS050           YES,BRANCH
         MVC   OBTVOL2,4(R1)       MOVE CELL VOLID
         B     GDS060
GDS050   MVC   OBTVOL2,28(R1)      MOVE VOLID
GDS060   MVC   ERRMSG9+36(6),OBTVOL2
         LM    R0,R1,ALLOCR0       LOAD PARM REGS
         SVC   32                  ALLOCATE
         LTR   R15,R15             OK?
         BC    EQ,GDS090           YES,BRANCH
         CH    R15,=H'4'           ALL READY THERE?
         MVC   ERRMSG9+6(8),=CL8'ALLOCATE' SET COMMENT
         BC    NE,GDSERR9          NO,ERROR BRANCH
         SR    R0,R0
         OBTAIN OBTCLST2           READ SECOND DSCB
         LTR   R15,R15             OK?
         MVC   ERRMSG9+6(8),=CL8'OBTAIN 2'
         BC    NZERO,GDSERR9       ERROR,BRANCH
         CLC   OBTWORK+82(12),OBTWORK2+82 SAME ATTRIBUTES?
         BC    NE,GDSERR11         NO,ERROR
*---------------------------------------------------------------------*
*              DSCB HAS BEEN ADDED OR IS ALREADY THERE
*---------------------------------------------------------------------*
GDS090   TM    FLAG,GDSMOVE        MOVE?
         BC    ALLOFF,GDS100       NO,MUST BE COPY SO RETURN
         MVC   ERRMSG9+36(6),OBTVOL
         XC    SCRRET,SCRRET
         SR    R0,R0
         SCRATCH SCRCLIST          SCRATCH THE OLD ONE
         LTR   R15,R15             OK?
         BC    ZERO,GDS100         YES,BRANCH
         LH    R15,SCRRET          LOAD RETURN CODE
         MVC   ERRMSG9+6(8),=CL8'SCRATCH' SET COMMENT
         B     GDSERR9
*---------------------------------------------------------------------*
*              RETURN
*---------------------------------------------------------------------*
GDS100   L     R13,GDSSAVE+4
         RETURN (14,12),T
*---------------------------------------------------------------------*
*              ERROR PROCESSING
*---------------------------------------------------------------------*
GDSERR9  CVD   R15,WORKAREA
         UNPK  ERRMSG9+28(3),WORKAREA+6(2) SET RETURN CODE
         MVC   PRNTLINE+2(L'ERRMSG9),ERRMSG9
         MVC   PRNTLINE+2+L'ERRMSG9(L'NAME),NAME
GDS200   BAL   R9,PUTSP
         CLI   RETCODE+1,4
         BC    GE,GDS100
         MVI   RETCODE+1,4
         B     GDS100              RETURN
GDSERR10 MVC   PRNTLINE+2(L'ERRMSG10),ERRMSG10
         MVC   PRNTLINE+2+L'ERRMSG10(L'NAME),NAME
         B     GDS200
GDSERR11 MVC   PRNTLINE+2(L'ERRMSG11),ERRMSG11
         MVC   PRNTLINE+2+L'ERRMSG11(L'NAME),NAME
         B     GDS200
GDSSAVE  DS    18F
OBTCLIST CAMLST SEARCH,NAME,OBTVOL,OBTWORK
SCRCLIST CAMLST SCRATCH,NAME,,SCRLIST,,OVRD
OBTCLST2 CAMLST SEARCH,NAME,OBTVOL2,OBTWORK2
ALLOCUCB DC    A(0)                UCB ADDR FOR ALLOCATE
ALLOCR0  DC    X'80',AL3(NAME)     DSCB ADDR
ALLOCR1  DC    A(ALLOCUCB)         UCB ADDRESS
SCRLIST  DC    H'1'                # OF VOLUMES
SCRDEV   DS    XL4                 DEVICE TYPE FOR SRATCH
OBTVOL   DS    CL6                 VOLID
SCRRET   DC    H'0'                SEQ NUMBER AND RETCODE
OBTVOL2  DS    CL6                 SECOND VOLID
NNAME    DC    A(NAME-1)           SPOT FOR FIRST LEVEL NAME
         DS    0D
         DS    CL4                 PADDING
NAME     DC    CL44' '
OBTWORK  DS    148C                WORK AREA FOR OBTAIN
         DS    0D
OBTWORK2 DS    148C                SECOND OBTAIN WORK AREA
         EJECT
*---------------------------------------------------------------------*
*              RR -RECURSIVE ROUTINE - CALLED FOR EACH LEVEL
*              R1=A(TTR) FOR NEXT LEVEL DOWN,
*              R0=LEVEL NUMBER (HIGHEST IS 1)
*              R8=A(VOLLIST) FOR LEVEL GT ONE
*        RETURNS IN THE TTR POINTED AT BY R1 THE NEW TTR
*---------------------------------------------------------------------*
RR       SAVE  (14,12),,*
         LR    R4,R1               SAVE R1
         LR    R7,R0               SAVE LEVEL #
         LA    R0,RRWKSIZE
         GETMAIN R,LV=(0)
         XC    0(256,R1),0(R1)
         XC    256(256,R1),256(R1)
         XC    512(RRWKSIZE-512,R1),512(R1)
         ST    R13,4(R1)
         ST    R1,8(R13)
         LR    R13,R1              POINT AT WORK AREA
         USING RRWORK,R13
         ST    R4,RRFSTTR          SAVE ADDRESS OF CALLERS TTR
         STH   R7,RRLEV            SAVE LEVEL
         STH   R7,LEVEL
         MH    R7,=AL2(LEVTABL)
         LA    R7,LEVTAB-LEVTABL(R7) POINT AT LEVEL TABLE
         CLI   RRLEV+1,X'01'       LEVEL ONE?
         BC    EQ,RR050            YES,BRANCH
         LH    R1,=H'-1'           SET TO CHECK FOR SPACE ON THIS TRK
         LA    R15,PUTC
         BALR  R14,R15
RR010    LR    R1,R4               POINT AT TTR
         LA    R2,RRKEY            POINT AT KEY+BUFFER
         L     R3,INDCBA           POINT AT DCB
         LA    R15,READC           READ THE BLOCK
         BALR  R14,R15
         LA    R5,RRBUF+2          POINT AT FIRST ENTRY
         USING CATENTRY,R5
RR005    CLI   CATCOUNT,CILE       IS THIS AN ILE OR CIPE
         BC    NE,RR020            NO,BRANCH
         CLI   CATNAME,X'FF'       ILE?
         BC    NE,RR020            NO,BRANCH
         CLC   CATTTR,=XL3'00'     END OF LEVEL?
         BC    EQ,RR400            YES,BRANCH,GO MAKE SECOND PASS
         LA    R4,CATTTR           POINT AT NEXT TTR
         B     RR010               GO READ NEXT BLOCK
RR020    CLI   RRLEV+1,X'01'       LEVEL ONE?
         BC    EQ,RR100            YES,GO PROCESS
*---------------------------------------------------------------------*
*              ADD THIS ENTRY TO OUTPUT
*---------------------------------------------------------------------*
         LA    R0,RRPTTR           POINT AT PCAREA
RR030    LR    R1,R5
RR035    LA    R15,PUTC
         BALR  R14,R15             CALL PUTC TO ADD ENTRY
RR040    CLI   RRLEV+1,X'01'       LEVEL 1
         BC    EQ,RR050            YES,GO GET NEXT ENTRY
         SR    R1,R1
         IC    R1,CATCOUNT         LOAD # OF HALF WORDS
         AR    R1,R1               TIMES TWO
         LA    R5,12(R1,R5)        POINT AT NEXT ENTRY
         B     RR005               GO PROCESS IT
RR050    LA    R15,INR             POINT AT INPUT ROUTINE
         BALR  R14,R15             CALL IT
         LTR   R5,R1               ANY MORE?
         BC    ZERO,RR400          NO,END UP
         B     RR005               GO PROCESS IT
*---------------------------------------------------------------------*
*              LEVEL ONE PROCESSING
*---------------------------------------------------------------------*
RR100    TM    FLAG,COPY           IS THIS COPY OR SPLIT
         BC    ALLON,RR200         COPY,BRANCH
*---------------------------------------------------------------------*
*              OPERATION IS SPLIT
*---------------------------------------------------------------------*
         CLI   CATCOUNT,CVICE      VICE OR NCVPE?
         BC    NE,RR130            NO,BRANCH
         CLI   CATNAME,X'00'       VICE?
         BC    NE,RR132            NO,SKIP ON NCVPE
         L     R8,VOLCHAIN         POINT AT FIRST VOLLIST
RR110    TM    VOLLFLAG,VOLLKEEP KEEP OR SPLIT?
         BC    ALLON,RR120         KEEP,BRANCH
         C     R8,VOLLRNXT         REAL VOLLIST?
         BC    NE,RR120            NO,BRANCH
         LA    R0,VOLLITTR         POINT AT PCAREA FOR HIGH INDEX
         LR    R1,R5               POINT AT ENTRY
         LA    R15,PUTC
         BALR  R14,R15             GO ADD VICE
RR120    L     R8,VOLLNEXT         POINT AT NEXT VOLLIST
         LTR   R8,R8               ANY?
         BC    NZERO,RR110         YES,KEEP GOING
         B     RR040               ALL VICES ADDED,GO GET NEXT ENTRY
RR130    CLI   CATCOUNT,CIPE       IPE?
         BC    EQ,RR135            YES,CHECKIT
         CLI   CATCOUNT,CAE        ALIAS?
         BC    EQ,RR160            YES,BRANCH
         CLI   CATCOUNT,CVCBPE     VCB POINTER?
         BC    EQ,RR132            YES,BRANCH
         CLI   CATCOUNT,CGIPE      GENERATION GROUP INDEX?
         BC    EQ,RR135            YES,CHECK
RR132    TM    FLAG,DSSPLIT        SPLIT DATA SETS?
         BC    ALLOFF,RR040        NO,DELETE THEN
RR135    L     R8,VOLCHAIN         POINT AT VOLLISTS
RR140    CLC   CATNAME,VOLLNAME    USE THIS VOLLIST?
         BC    LE,RR150            YES,BRANCH
         L     R8,VOLLNEXT         LOOK AT NEXT
         B     RR140
RR150    TM    VOLLFLAG,VOLLKEEP   KEEP OR SPLIT
         BC    ALLON,RR040         KEEP,BRANCH DONT ADD
         L     R8,VOLLRNXT         POINT AT REAL VOLLIST
         LA    R0,VOLLITTR         POINT AT PCAREA
         CLI   CATCOUNT,CNCVPE     CVOL?
         BC    EQ,RR155            YES,BRANCH
         CLI   CATCOUNT,CCVPE      CVOL?
         BC    NE,RR030            NO,BRANCH
RR155    BAL   R9,RR180            CHECK CVOL
         B     RR040               IF SAME VOL SKIP IT
         LH    R15,VOLLICVL        LOAD CVOL COUNT
         AH    R15,=H'1'
         STH   R15,VOLLICVL        SAVE COUNT
         B     RR030               GO ADD ENTRY
RR160    L     R8,VOLCHAIN
RR162    CLC   CAENAME,VOLLNAME    TRUE NAME HERE?
         BC    LE,RR165            YES,BRANCH
         L     R8,VOLLNEXT         LOOK AT NEXT
         B     RR162
RR165    L     R3,VOLLRNXT         POINT AT REAL NAMES VOLLIST
         L     R8,VOLCHAIN
RR168    CLC   CATNAME,VOLLNAME    THIS VOLLIST?
         BC    LE,RR170            YES,BRANCH
         L     R8,VOLLNEXT         LOK AT NEXT
         B     RR168
RR170    L     R8,VOLLRNXT         POINT AT ALIAS OLLIST
         TM    VOLLFLAG,VOLLKEEP   ALIS TO BE KEPT?
         BC    ALLOFF,RR175        NO,BRANCH
         TM    VOLLFLAG-VOLLIST(R3),VOLLKEEP REAL NAME KEPT?
         BC    ALLON,RR040         YES,DONT HAVE TO ADD
         LR    R8,R3               POINT AT REAL NAMES VOLLIST
         LA    R0,VOLLITTR         POINT AT PCAREA
         B     RR030               GO ADD
RR175    CR    R3,R8               BOTH SPLIT TO SAME VOLLIST?
         LA    R0,VOLLITTR         POINT AT PCAREA
         BC    EQ,RR030            YES,GO ADD ALIAS
         MVC   RRCVNAME,CATNAME    MOVE CVOL NAME
         MVC   RRCVDEV,VOLLDEVT-VOLLIST(R3) MOVE DEVICE TYPE
         MVC   RRCVVOL,VOLLVOL-VOLLIST(R3) MOVE CVOL VOL NAME
         LH    R15,VOLLICVL        LOAD COUNT OF CVOLS
         AH    R15,=H'1'
         STH   R15,VOLLICVL
         LA    R1,RRCVNAME         POINT AT CVOL ENTRY
         LA    R0,VOLLITTR         POINT AT PCAREA
         LA    R15,PUTC
         BALR  R14,R15             ADD CVOL ENTRY ON ALIAS VOLLIST
         LR    R8,R3               POINT AT REAL NAMES VOLLIST
         LA    R0,VOLLITTR         POINT AT PCAREA
         TM    VOLLFLAG,VOLLKEEP   REAL KEPT?
         BC    ALLOFF,RR030        NO,GO ADD ALIAS
         B     RR040               YES,SKIP IT
*---------------------------------------------------------------------*
*              CHECK CVOLS AND REMAP IF NEEDED
*---------------------------------------------------------------------*
RR180    LA    R1,CCVPEVOL         POINT AT VOL NAME
         CLI   CATCOUNT,CNCVPE     NEW ONE?
         BC    NE,RR182            NO,BRANCH
         LA    R1,CNCVPEVO         POINT AT VOLUME FOR NCVPE
RR182    LA    R14,INTAB           POINT AT LIST OF INLISTS
RR183    L     R15,0(R14)          POINT AT INLIST
         CLC   0(6,R1),INVOL-INLIST(R15) POINT AT INPUT VOL?
         BC    EQ,RR185            YES,BRANCH
         TM    0(R14),X'80'        END?
         BC    ALLON,RR195         YES,DOES NOT POINT AT INPUT VOL
         LA    R14,4(R14)          INC TO NEXT INLIST PTR
         B     RR183
RR185    L     R14,VOLCHAIN        POINT AT VOLLIST
RR187    CLC   CATNAME,VOLLNAME-VOLLIST(R14) USE THIS?
         BC    LE,RR190            YES,BRANCH
         L     R14,VOLLNEXT-VOLLIST(R14) LOOK AT NEXT
         B     RR187
RR190    CLC   VOLLVOL,VOLLVOL-VOLLIST(R14) POINT AT SAME VOLUME?
         BCR   EQ,R9               YES,DELETE CVOL
         MVC   0(6,R1),VOLLVOL-VOLLIST(R14) SET NEW VOLUME
         CLI   CATCOUNT,CNCVPE     NEW CVOL?
         BC    NE,4(R9)            NO,RETURN
         MVC   CNCVPEDT,VOLLDEVT-VOLLIST(R14) SET DEVICE TYPE
         B     4(R9)               RETURN
RR195    CLC   0(6,R1),VOLLNAME    DOES CVOL POINT AT OITPUT VOL?
         BCR   EQ,R9               YES,BY PASS ADD
         B     4(R9)               NO,RETURN OK
*---------------------------------------------------------------------*
*              OPERATION IS COPY
*---------------------------------------------------------------------*
RR200    CLI   CATCOUNT,CVICE      VICE OR NCVPE?
         BC    NE,RR210            NO,BRANCH
         CLI   CATNAME,X'00'       VICE?
         BC    NE,RR250            NO,NCVPE GO COUNT
RR205    L     R8,VOLLAST          POINT AT VOLLIST
         LA    R0,VOLLITTR         POINT AT PCAREA
         B     RR030               GO ADD THIS VICE OR NCVPE
RR210    CLI   CATCOUNT,CIPE       INDEX POINTER?
         BC    EQ,RR215            YES,BRANCH
         CLI   CATCOUNT,CAE        ALIAS?
         BC    EQ,RR260            YES,BRANCH
         CLI   CATCOUNT,CVCBPE
         BC    EQ,RR212
         CLI   CATCOUNT,CGIPE
         BC    EQ,RR215
         CLI   CATCOUNT,CDSPE      DATA SET?
         BC    GE,RR212            YES,BRANCH
         B     RR250
RR212    TM    FLAG,DSKEEP         KEEP DATA SETS?
         BC    ALLON,RR205         YES,BRANCH
         TM    FLAG,DSSPLIT        SPLIT THEM THEN?
         BC    ALLOFF,RR040        NO MUST BE UNCATLG SO SKIP THEM
RR215    L     R8,VOLCHAIN         POINT AT VOLLIST
RR220    CLC   CATNAME,VOLLNAME    USE THIS ENTRY?
         BC    LE,RR230            YES,BRANCH
         L     R8,VOLLNEXT         LOOK AT NEXT
         B     RR220
RR230    TM    VOLLFLAG,VOLLKEEP   KEEP OR SPLIT?
         BC    ALLON,RR205         KEEP GO ADD THIS ENTRY
RR240    MVC   RRCVNAME,CATNAME    MOVE NAME TO CVOL LIST
         MVC   RRCVDEV,VOLLDEVT    MOVE DEVICE TYPE CODE
         MVC   RRCVVOL,VOLLVOL     MOVE CVOL NAME
         L     R8,VOLLAST          SET VOLLIST PTR
         LH    R15,VOLLICVL        COUNT NUMBER OF CVOLS
         AH    R15,=H'1'
         STH   R15,VOLLICVL
         LA    R1,RRCVNAME         POINT AT NEW ENTRY
         LA    R0,VOLLITTR
         B     RR035               GO ADD CVOL INSTEAD OF IPE
RR250    L     R8,VOLLAST
         BAL   R9,RR180
         B     RR040
         LH    R15,VOLLICVL
         AH    R15,=H'1'
         STH   R15,VOLLICVL
         B     RR205               ALL OTHERS,GO ADD
RR260    L     R8,VOLCHAIN
RR270    CLC   CAENAME,VOLLNAME    IS THIS TRUE NAME HERE?
         BC    LE,RR280            YES,BRANCH
         L     R8,VOLLNEXT         LOOK AT NEXT
         B     RR270
RR280    TM    VOLLFLAG,VOLLKEEP   KEEP?
         L     R8,VOLLRNXT
         BC    ALLOFF,RR240        NO,GO ADD CVOL
         L     R8,VOLLAST
         B     RR030               GO ADD
*---------------------------------------------------------------------*
*              FIRST LEVEL IS ALL DONE, END UP THE INDEXES
*---------------------------------------------------------------------*
RR400    CLI   RRLEV+1,X'01'       LEVEL ONE?
         LA    R5,RRPTTR           POINT AT PCAREA
         BC    NE,RR430            NO,BRANCH
         TM    FLAG,COPY           COPY?
         BC    ALLOFF,RR410        NO,BRANCH
         L     R8,VOLLAST          LOAD LAST
         B     RR420
RR410    L     R8,VOLCHAIN         POINT AT FIRST VOLLIST
         B     RR630               GO FIND FIRST VOLLIST TO PROCESS
RR420    LA    R5,VOLLITTR         POINT AT PCAREA
         DROP  R5
         USING PCAREA,R5
RR430    LR    R0,R5               POINT AT PCAREA
         MVC   RRSVTTR,PCTTR       SAVE TTR
         XC    PCTTR,PCTTR
         SR    R1,R1               SET EOF FOR PUTC
         LA    R15,PUTC
         BALR  R14,R15             GO CLEAN UP AND ADD EOF
         LA    R1,256              MAX BYTES
         SH    R1,PCBUF            -BYTES=BYTES LEFT IN LAST BLOCK
         STH   R1,RRBYTES          SAVE
*---------------------------------------------------------------------*
*              AT THIS POINT PCTTR HAS TTR OF LAST BLOCK IN INDEX
*              AND RRSVTTR HAS FIRST TTR OR ZERO IF FIRST=LAST
*---------------------------------------------------------------------*
         OC    RRSVTTR,RRSVTTR     FIRST BLOCK SAME AS LAST?
         BC    ZERO,RR450          YES,DONT HAVE TO READ
         LA    R1,RRSVTTR          POINT AT TTR
         LA    R3,VOLLDCB                                        EXCP01
         LA    R2,RRKEY            POINT AT AREA
         AL    R2,=X'80000000'     RETAIN                        EXCP01
         LA    R15,READC
         BALR  R14,R15             READ IN FIRST BLOCK
         B     RR460
RR450    MVC   RRKEY,PCKEY         MOVE KEY
         MVC   RRBUF,PCBUF         MOVE BUF
         MVC   RRSVTTR,PCTTR
RR460    MVC   RRBUF+2+CATTTR-CATNAME(3),PCTTR SET LAST BLK TTR
         MVC   RRRTTR,RRSVTTR
         DROP  R5                  MVCRRRTTR,RRSVTTR
         USING CATENTRY,R5
         LA    R5,RRBUF+2          POINT AT VICE OR ICE
         CLI   CATCOUNT,CVICE      VICE?
         BC    NE,RR470            NO,MUST BE ICE
         MVC   CVICULB,RRBYTES     #OF UNUSED BYTES LAST BLOCK
         B     RR480
RR470    MVC   CICEUBL,RRBYTES     #UNUSED BYTES,LAST BLOCK
         MVC   CICEILL,RRSVTTR     TTR OF THIS BLOCK
         CLI   CICEALIA,X'00'      ANY ALIASES?
         BC    EQ,RR480            NO,BRANCH
         GETMAIN R,LV=ANSIZE,SP=2
         USING ANENTRY,R1
         MVC   ANNEXT,VOLANCHN
         ST    R1,VOLANCHN         CHAIN IN
         MVC   ANTTR,RRSVTTR       SAVE TTR OF THIS BLOCK
         L     R15,RRFSTTR         POINT AT TTR
         SH    R15,=H'8'           POINT AT NAME
         MVC   ANNAME,0(R15)       MOVE NAME
         MVC   ANCOUNT+1(1),CICEALIA
         MVI   ANCOUNT,X'00'       SAVE # OF ALIASES IN HLF WRD
         DROP  R1
RR480    SR    R1,R1
         IC    R1,CATCOUNT
         AR    R1,R1               TIMES TWO
         LA    R5,12(R1,R5)        POINT AT NEXT ENTRY
RR490    CLI   LEVEL+1,X'01'       LEVEL ONE?
         BC    NE,RR491            NO,BRANCH
         TM    CATCOUNT,X'01'      EVEN?
         BC    ALLON,RR491         NO,BRANCH
         CLI   CATCOUNT,CAE        ALIAS?
         BC    EQ,RR491            YES,BRANCH
         IC    R1,CATCOUNT
         N     R1,=X'000000F8'     CLEAR UNWANTED BITS
         SRL   R1,1
         LA    R1,INTAB(R1)        POINT AT INPUT DCB ADDRESS
         MVC   INDCBA,0(R1)        MOVE ADDRESS
         NI    CATCOUNT,X'07'      RESET INDEX BITS
         CLI   CATCOUNT,X'06'      DUMMY FOR VCBPE?
         BC    NE,RR491            NO,BRANCH
         MVI   CATCOUNT,CVCBPE     RESET COUNT FIELD
RR491    CLI   CATCOUNT,CILE       ILE OR IPE?
         BC    NE,RR500            NO,BRANCH
         CLI   CATNAME,X'FF'       ILE?
         BC    NE,RR510            NO,BRANCH MUST BE IPE
         LA    R1,RRRTTR           TTR OF THIS RECORD
         LA    R2,RRKEY
         LA    R3,VOLLDCB                                        EXCP01
         LA    R15,WRITEC
         BALR  R14,R15             WRITE THIS RECORD AGAIN
         LA    R14,VOLLI1          POINT AT AREA FOR LEV ONE COUNT
         CLI   RRLEV+1,X'02'       LEVEL 2
         BC    GT,RR495            NO BRANCH
         BC    LT,RR492            ONE,BRANCH
         LA    R14,VOLLI2          POINT AT LEVEL 2 AREA
RR492    LH    R15,0(R14)          COUNT OF BLOCKS AT THIS LEVEL
         AH    R15,=H'1'           ADD ONE
         STH   R15,0(R14)          SAVE
RR495    CLC   CATTTR,=XL3'00'     ANY MORE?
         BC    EQ,RR600            NO,END UP
         MVC   RRRTTR,CATTTR       MOVE TTR FOR NEXT
         LA    R15,READC
         BALR  R14,R15             READ NEXT BLOCK
         LA    R5,RRBUF+2          POINT AT FIRST ENTRY
         B     RR490               GO CHECK IT
RR500    CLI   CATCOUNT,CGIPE      GENERATION INDEX?
         BC    NE,RR520
RR510    L     R1,NNAME            POINT AT NEXT SPOT FOR NAME
         ST    R1,RRNAME           SAVE IN RRWORK
         MVI   0(R1),C'.'          SET .
         MVC   1(8,R1),CATNAME     MOVE NAME
         LA    R1,8(R1)            POINT PAST NAME
RR512    CLI   0(R1),C' '          END?
         BC    NE,RR513            YES,BRANCH
         BCT   R1,RR512            NOT THE LAST CHAR YET
RR513    AH    R1,=H'1'            POINT AT FIRST BLANK
         ST    R1,NNAME            SAVE FOR NEXT LEVEL
         LA    R1,CATTTR           POINT AT TTR
         CLC   CATTTR,=XL3'000000' ANY?
         BC    EQ,RR515            NO,DONT PROCESS
         LH    R0,RRLEV            LOAD LEVEL
         AH    R0,=H'1'            +1
         LA    R15,RR
         BALR  R14,R15             CALL RR FOR NEXT LEVEL,TTR CHANGED
RR515    CLI   CATCOUNT,CGIPE      GENERATION INDEX?
         BC    NE,RR517            NO,BRANCH
         TM    FLAG,GDSMOVE+GDSCOPY MOVE OR COPY DUMMY DSCBS?
         BC    ALLOFF,RR517        NO,BRANCH
         LA    R15,GDSPROC         POINT AT SUBROUTINE
         BALR  R14,R15             CALL IT
RR517    L     R1,RRNAME
         MVC   0(9,R1),=CL9' '     CLEAR LAST NAME AND .
         ST    R1,NNAME            RESTORE
         B     RR480               GO LOOK AT NEXT ENTRY
RR520    CLI   CATCOUNT,CVCBPE     VCBPE?
         BC    NE,RR530
         XC    VCBPTTR,VCBPTTR
         LA    R1,CATTTR           POINT AT FIRST TTR
RR522    LA    R2,VCBKEY           POINT AT KEY
         L     R3,INDCBA
         LA    R15,READC
         BALR  R14,R15             READ VCB
         MVC   VCBTTRSV,CVCBNEXT-CATENTRY+VCBBUF SAVE TTR OF NEXTF00001
         LA    R0,VCBPTTR          POINT AT VCB AREA
         LA    R1,1                SET R1 TO PLUS
         LA    R15,PUTC
         BALR  R14,R15             WRITE VCB
         LA    R1,VCBTTRSV         POINT AT TTR OF NEXT VCB      F00001
         OC    0(3,R1),0(R1)       MORE?
         BC    NZERO,RR522         YES,BRANCH
         MVC   CATTTR,VCBPTTR      MOVE NEW TTR
         B     RR480
RR530    CLI   CATCOUNT,CCVPE      CVOL?
         BC    EQ,RR550            YES,BRANCH
         CLI   CATCOUNT,CAE        ALIAS?
         BC    EQ,RR540            YES,BRANCH
         CLI   CATCOUNT,CNCVPE     NEW CVOL?
         BC    EQ,RR560            YES,BRANCH
         CLI   CATCOUNT,CDSPE      DATA SET?
         BC    LT,RR480
         L     R15,VOLLDS          COUNT DATA SETS
         AH    R15,=H'1'
         ST    R15,VOLLDS          SAVE
*              CODE COULD BE ADDED HERE TO CHANGE DEVICE TYPES
         B     RR480
RR540    GETMAIN R,LV=ALSIZE,SP=2  GET ALIAS AREA
         USING ALENTRY,R1
         MVC   ALNEXT,VOLALCHN     SAVE ALIAS CHAIN
         ST    R1,VOLALCHN         CHAIN IN
         MVC   ALTTR,RRRTTR        SAVE ADDRESS
         MVC   ALNAME,CATNAME      SAVE ALIAS NAME
         MVC   ALRNAME,CAENAME     SAVE REAL NAME
         LR    R14,R5              POINT AT ENTRY
         LA    R15,RRBUF           POINT AT BUFFER
         SR    R14,R15
         STH   R14,ALOFF           SAVE OFFSET INTO BUFFER
         B     RR480
         DROP  R1
RR550    LA    R1,CCVPEVOL         POINT AT VOL
         B     RR565
RR560    LA    R1,CNCVPEVO         POINT AT VOL
RR565    L     R2,CVOLCHAN         POINT AT CHAIN
         USING CVOLD,R2
RR570    LTR   R2,R2               ANY?
         BC    ZERO,RR480          NO,END
         CLC   CVOLDNAM,0(R1)      SAME?
         BC    EQ,RR580            YES,BRANCH GO FIND NEW CVOL NAME
         L     R2,CVOLDNXT
         B     RR570               GO LOOK AT NEXT
RR580    L     R2,CVOLDCHN         POINT AT NEW CVOLS
         DROP  R2
         USING CVNEW,R2
RR585    CLC   CATNAME,CVNEWDSN    USE THIS ENTRY?
         BC    LE,RR590            YES,BRANCH
         L     R2,CVNEWNXT         LOOK AT NEXT
         B     RR585
RR590    CLI   CATCOUNT,CCVPE      CVPE OR NCVPE?
         BC    EQ,RR595            CVPE,BRANCH
         MVC   CNCVPEDT(10),CVNEWDEV MOVE DEVTYPE AND NAME
         B     RR480
RR595    MVC   CCVPEVOL,CVNEWNAM   MOVE NAME
         B     RR480
         DROP  R2
*---------------------------------------------------------------------*
*              ALL DONE AT THIS LEVEL
*---------------------------------------------------------------------*
RR600    LA    R1,RRRTTR
         LA    R2,RRKEY
         LA    R3,VOLLDCB                                        EXCP01
         LA    R15,WRITEC
         BALR  R14,R15             WRITE LAST BLOCK
         CLI   RRLEV+1,X'01'       LEVEL ONE?
         BC    NE,RR650            NO,BRANCH
         TM    FLAG,COPY
         BC    ALLON,RR650
RR620    L     R8,VOLLNEXT         LOOK AT NEXT
         LTR   R8,R8               ANYMORE?
         BC    ZERO,RR650          NO,BRANCH
RR630    TM    VOLLFLAG,VOLLKEEP   KEEP?
         BC    ALLON,RR620         YES,ONLY NEED TO LOOK AT SPLIT
         C     R8,VOLLRNXT         REAL VOLLIST?
         BC    NE,RR620            NO,BRANCH
         B     RR420               GO PROCESS THIS LEVEL
RR650    L     R1,RRFSTTR          A(CALLERS TTR)
         MVC   0(3,R1),RRSVTTR     SET TTR FOR CALLER
         LH    R1,LEVEL
         SH    R1,=H'1'
         STH   R1,LEVEL
         LR    R1,R13              POINT AT RR WORK
         LA    R0,RRWKSIZE
         L     R13,4(R13)          LOAD RETURN BASE
         FREEMAIN R,LV=(0),A=(1)
         RETURN (14,12),T
RRCVNAME DS    CL8                 NAME USED FOR ADDING NEW CVOLS
         DC    XL4'00000005'       TTR AND CATCOUNT FOR NCVPE
RRCVDEV  DS    XL4                 DEVICE TYPE
RRCVVOL  DS    CL6                 NEW CVOL NAME
         EJECT
*---------------------------------------------------------------------*
*        CONSTANTS AND DATA AREA
*---------------------------------------------------------------------*
SAVEAREA DS    18F
         PRINT NOGEN
SYSIN    DCB   DDNAME=SYSIN,MACRF=(GL),LRECL=80,RECFM=FB,              X
               BLKSIZE=3200,EODAD=EOFIN,DSORG=PS,SYNAD=SYNADPS      FJP
SYSPRINT DCB   DDNAME=SYSPRINT,MACRF=(PM),LRECL=121,RECFM=FBA,         X
               BLKSIZE=605,DSORG=PS,SYNAD=SYNADPS           FJP/01DEC77
         PRINT GEN
PATDCB   DCB   DDNAME=X,DSORG=PS,MACRF=(E),DEVD=DA,RECFM=F       EXCP01
PRNTLINE DC    CL121'1 '
TITLE1   DC    C'ARGONNE  NATIONAL  LABORATORY'
TITLE2   DC    C'**  COPYCAT  12-77  **'
TITLE3   DC    C'OS  CATALOG  UTILITY'
ERRMSG1  DC    C'ERR01 ERROR FOUND IN PARAMETER AT ''$'''
ERRMSG2  DC    C'ERR02 ERROR PROCESSING DDNAME=''XXXXXXXX'''
ERRMSG3  DC    C'ERR03 UNEXPECTED END OF FILE ON SYSIN'
ERRMSG4  DC    C'ERR04 NAME=XXXXXXXX NOT FOUND FOR ALIAS=XXXXXXXX'
ERRMSG5  DC    C'ERR05 ALIAS COUNT ERROR FOUND FOR NAME='
ERRMSG6  DC    C'ERR06 TO MANY INPUT DATASETS'
ERRMSG7  DC    C'ERR07 INDEX=XXXXXXXX FOUND ON VOLS=(XXXXXX,XXXXXX) THEX
                ENTRY ON XXXXXX WILL BE USED'
ERRMSG8  DC    C'ERR08 NO INPUT CARDS FOUND'
ERRMSG9  DC    C'ERR09 XXXXXXXX FAILED WITH (XXX) ON XXXXXX FOR GDG MODX
               EL DSCB='
ERRMSG10 DC    C'ERR10 MODEL DSCB FOR GDG DOES NOT HAVE ZERO EXTENTS DSX
               N='
ERRMSG11 DC    C'ERR11 MODEL DSCB FOR GDG EXISTS ON OUTPUT VOLUME WITH X
               DIFFERENT ATTRIBUTES FOR DSN='
ERRMSG12 DC    C'ERR12 ABEND - XXX - OCCURED. SEE SYSMSG. CURRENT OR LAX
               ST NAME IS '
ERRMSG13 DC    C'ERR13 I/O ERROR - '
ERRMSGD  DC    C'CURRENT OR LAST NAME IS '
ERRMSG14 DC    C'ERR14 NO OUTPUT MASTER CARD WAS FOUND'
ERRMSG15 DC    C'ERR15 UNABLE TO ALLOCATE MINIMUM BUFFERS'       EXCP01
HEAD1    DC    C'- OUTPUT STATISTICS'
HEAD2    DC    C'0         LAST   LAST  CONTROL  DUMMY   BLOCKS  BLOCKSX
                   DATA'
HEAD3    DC    C'          TTR+1  USED  VOLUME   BLOCKS  AT LEV  AT LEVX
                   SETS'
HEAD4    DC    C'                 TTR+1 ENTRIES           ONE     TWO'
MSGOK    DC    C'0  COMPLETION CODE - '
WORKAREA DS    D
MAINTTR  DC    XL3'000001'         TTR FOR START
FLAG     DC    X'00'               FLAGS
COPY     EQU   X'80'               CONTROL RECORD FOR COPY
DSSPLIT  EQU   X'40'               SPLIT DS IN LEVEL ONE
DSKEEP   EQU   X'20'               KEEP DS AT HIGHEST LEVEL
GDSMOVE  EQU   X'10'               MOVE GDG MODEL DSCBS
GDSCOPY  EQU   X'08'               COPY GDG MODEL DSCBS
CVOLCHAN DC    A(0)                CHAIN OV OLD CVOL NAMES
VOLCHAIN DC    A(0)                CHAIN OF VOLLIST ENTRIES
VOLLAST  DC    A(0)                ADDRESS OF LAST VOLLIST
VOLKEEP  DC    A(0)                ADDR OF FIRST KEEP ENTRY
VOLNAMEL DC    XL8'00'             HIGHEST NAME SO FAR
RETCODE  DC    H'0'                RETURN CODE
         DS    0H
LEVTAB   DC    22XL(LEVTABL)'00'   ONE ENTRY FOR EACH LEVEL
         DS    F                   DUMMY
INTAB    DC    32A(0)              LIST OF DCBS FOR INPUT
INDCBA   DC    A(INTAB)            ADDRESS OF CURRENT INPUT DCB
LEVEL    DS    H                   CURRENT LEVEL
INRLASTC DS    X                   COUNT FROM INLAST
INRLAST  DC    A(0)                LAST ENTRY RETURNED BY INR
INLOWNAM DS    CL8                 NAME SO FAR FOR INR
         DS    0F
VCBPTTR  DS    XL3                 TTR
         DC    AL1(PCVCB)          SET THIS IS VCB PCAREA
VCBKEY   DS    XL8
VCBBUF   DS    XL256               BUFFER
VCBTTRSV DS    XL3                 VCB TTR SAVE AREA             F00001
MAXRECTK DC    H'0'                MAX REC/TRK ON ALL UNITS OPEN EXCP01
MINBUFFS DC    H'1'                MIN BUFFS=INDDS+OUTDDS+1      EXCP01
ECBLISTR DC    A(RECB)             START OF ECBLIST              EXCP01
ECBLISTW DC    A(WECB1)            START OF ECB LIST FOR WRITES  EXCP01
         DC    A(WECB2)                                          EXCP01
         DC    X'80',AL3(WECB3)    LAST ECB                      EXCP01
RECB     DC    F'0'                READ ECB                      EXCP01
RIOB     DC    X'C2000000',A(RECB),8F'0' IOB                     EXCP01
         DC    F'0'                                              EXCP01
WECB1    DC    F'0'                WRITE ECB                     EXCP01
WIOB1    DC    X'C0000000',A(WECB1),8F'0' IOB                    EXCP01
         DC    F'0'                                              EXCP01
WECB2    DC    F'0'                WRITE ECB                     EXCP01
WIOB2    DC    X'C0000000',A(WECB2),8F'0' IOB                    EXCP01
         DC    F'0'                                              EXCP01
WECB3    DC    F'0'                WRITE ECB                     EXCP01
WIOB3    DC    X'C0000000',A(WECB3),8F'0' IOB                    EXCP01
         DC    F'0'                                              EXCP01
FREEBUFQ DC    A(0)                FREE BUFFER QUEUE             EXCP01
OUTBUFQ  DC    A(0)                OUTPUT BUFFER QUEUE           EXCP01
USEBUFQ  DC    A(0)                IN USE BUFFER QUEUE           EXCP01
PATCCW   CCW   X'1D',*-*,X'80',8   WCKD -> CCHHRKDL              EXCP01
         CCW   X'08',*-*,X'00',0        TIC                      EXCP01
         DC    XL8'0000000000080100'    COUNT FIELD              EXCP01
         CCW   X'00',*-*,X'00',264      -> KEY DATA              EXCP01
MINCORE  DC    F'12288'            12K MIN FREE                  EXCP01
GETMLA   DC    A(8),X'00FFFFF0'    GETMAIN LA LIST               EXCP01
GETMA    DC    A(0)                ADDR OF BUFS FROM GETMAIN     EXCP01
GETMS    DC    A(0)                SIZE OF BUFS FROM GETMAIN     EXCP01
         LTORG
         USING SETXX,R15                                         F00003
               PRINT OFF
SETXX    LA    R1,XXE
         LA    R2,XX
         LA    R0,32
SETXXL   TR    0(32,R2),TR2TAB-240
         BXLE  R2,R0,SETXXL
         BR    R9
TR2TAB   DC    C'  XX   X  '
         DS    D
XX       EQU   *
         DC    C'01445689014456895641568989890100'
         DC    C'02014702322456723083233312372300'
         DC    C'87898743144310178945671444525464'
         DC    C'83434347722456920505026655870145'
         DC    C'82121263626898830165879865478955'
         DC    C'10787892425401723456838908924561'
         DC    C'01456895605501894016854995966800'
         DC    C'52372382981201456895624562372358'
         DC    C'97001103756345588901752808071666'
         DC    C'02374447525345645617456745624566'
         DC    C'07010102083266559102277700030011'
         DC    C'62223383456789108183585394929110'
         DC    C'01568811004956801551551100549090'
         DC    C'01120008989892456348889942100145'
         DC    C'00783956644443745389898987001144'
         DC    C'87899745645887828211008892454488'
         DC    C'92327289270102683787755663011919'
         DC    C'62666266228882989247711002323311'
         DC    C'89456664411900895461044560055566'
         DC    C'04456689080902222012889244400115'
         DC    C'85555668856982010755353564141441'
         DC    C'45568989654013332441171115566440'
         DC    C'52745145056053588389975544080988'
         DC    C'02288556644012737555578888665556'
         DC    C'01566489605144451455688996058410'
         DC    C'02372441237002588211777444110590'
         DC    C'02411312000292988242555744110546'
         DC    C'52668763658782665263654101455665'
         DC    C'12655247566353541717552265988996'
         DC    C'82273556222111333456237745566655'
         DC    C'01145689895556691918844188801489'
         DC    C'42723782456244237552323242223456'
         DC    C'03445663355352555243144113666345'
         DC    C'47775557878783011117231117773000'
         DC    C'02145552552247113252541112574144'
         DC    C'02323347111311237743777753662666'
         DC    C'44411005055016548568658889851400'
         DC    C'02372310044050323055266632745645'
         DC    C'00074445456452110202300245471111'
         DC    C'00128889686545572111245889245501'
         DC    C'88934411565652001300789893456669'
         DC    C'99874488050155232897325627374401'
         DC    C'01465544100445986985461450548698'
         DC    C'02323344323654723556656662323214'
         DC    C'48857883555312111744411004568358'
         DC    C'14475566673555232356273114425656'
         DC    C'89789442000388991348085191309090'
         DC    C'99700114232456237898965445244114'
         DC    C'45655880808188119004404550506606'
         DC    C'02323244434144431586859442111255'
         DC    C'88987899328100220011445566353555'
         DC    C'88938989571441570044565685178585'
         DC    C'44256565621114520011448958212111'
         DC    C'01345411232114327885566863141344'
         DC    C'56401456056055066541014560546896'
         DC    C'11442894333555777444777454545111'
         DC    C'45623012000262655363451451458541'
         DC    C'44757851023011122553322458896600'
         DC    C'82727787888212066342881245588968'
         DC    C'11007015772566277444323485689999'
XXE      EQU   *
         DC    C'01456450019181414564561458585689'
         END
