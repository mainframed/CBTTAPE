./ CHANGE      SEQFLD=738,NAME=COPYCAT
./ NUMBER INSERT=YES,SEQ1=00001155,NEW1=00001156,INCR=1
*  08/09/82 - UCLA/OAC - MAS - ADD HASH CVOL SUPPORT            HASH001
./ NUMBER INSERT=YES,SEQ1=00003119,NEW1=00003120,INCR=1
*                                                               HASH001
*  UCLA/OAC MODIFICATIONS FOR HASH CVOL ROUTING SUPPORT.        HASH001
*                                                               HASH001
*  WE RUN A MOD IN CATALOG WHICH BYPASSES THE MASTER CATALOG    HASH001
*  LOOKUP FOR MOST HIGH LEVEL INDEXES. THIS REMOVES THE         HASH001
*  REQUIRMENT TO ADD AN ALIAS POINTER FOR EACH NEW USER AND     HASH001
*  REDUCES THE EXPOSURE TO THE MASTER CATALOG.  THE PERFORMANCE HASH001
*  IS ALSO BETTER.                                              HASH001
*                                                               HASH001
*  THESE HASH MODS ALLOW SPLITTING THE CATALOGS VIA A HASHING   HASH001
*  PSUEDO-RANDOM WAY, THUS EQUALIZING THE LOAD.                 HASH001
*                                                               HASH001
*  A NEW CONTROL CARD IS ACCEPTED BY THIS VERSION OF COPYCAT.   HASH001
*                                                               HASH001
*  |NAME| HASH <MODULE NAME>                                    HASH001
*                                                               HASH001
*    <MODULE NAME> - HASHING ROUTINE LOAD MODULE NAME           HASH001
*                                                               HASH001
* HASH ROUTINE LINKAGE:                                         HASH001
*                                                               HASH001
* ENTRY: R0  - MAX LENGTH OF INDEX (STRING LENGTH)              HASH001
*        R1  - @ HIGH LEVEL INDEX FOLLOWED BY '.' OR ' '        HASH001
*        R13 - @ 72 BYTE SAVE AREA                              HASH001
*        R14 - RETURN ADDRESS                                   HASH001
*        R15 - @ HASH ROUTINE                                   HASH001
*                                                               HASH001
* EXIT:  R15 - RETURN CODE (CONDITION CODE SET)                 HASH001
*              0 - HASH SUCCESSFUL, R1 = @ CL6 CVOL             HASH001
*              4 - HASH UNSUCCESSFUL                            HASH001
*                  (COPYCAT WILL USE NORMAL SPLIT LOGIC         HASH001
*                    AS DETERMINED BY THE OUTPUT CARDS)         HASH001
*                                                               HASH001
         SPACE 1                                                HASH001
         MACRO                                                  HASH001
&LBL     CCHASH &KEY,&REG                                       HASH001
.*  &KEY = @ CL8 NAME TO LOOK UP (OR HASH)                      HASH001
.*  &REG = REGISTER TO RETURN @ VOLLIST (R1-R13 OR R14 ONLY)    HASH001
&LBL     STM   R14,R0,RRHASHSV    TEMP SAVE R14-R0              HASH001
         LA    R0,&KEY            @ KEY TO HASH/SEARCH          HASH001
         L     R15,=A(HASHSUB)    @ HASH/SEARCH RTN             HASH001
         BALR  R14,R15                                          HASH001
         LR    &REG,R15           SET RETURN REGISTER           HASH001
         AIF   ('&REG' EQ 'R14').XR14                           HASH001
         LM    R14,R0,RRHASHSV    RESTORE TEMP REGS             HASH001
         MEXIT                                                  HASH001
.XR14    LM    R15,R0,RRHASHSV+4  RESTORE TEMP REGS             HASH001
         MEND                                                   HASH001
*--- PROCESS "HASH <MODULE NAME>" CONTROL CARD                  HASH001
         CLC   OPER,=CL8'HASH'    HASH REQUEST?                 HASH001
         BNE   CON015A            BIF NOT HASH REQUEST          HASH001
         CLI   HASHMOD,C' '       ALREADY HASH?                 HASH001
         BNE   ERROROP            BIF DUP HASH, ERROR           HASH001
         LA    R2,HASHMOD         POINT AT MODULE NAME SPOT     HASH001
         BAL   R9,SCAN8           SCAN FOR MODULE NAME          HASH001
         BZ    ERROR              BIF NAME MISSING, ERROR       HASH001
         LOAD  EPLOC=HASHMOD      LOAD MODULE                   HASH001
         ST    R0,HASHEPA         SAVE @ HASH ROUTINE           HASH001
         B     CON015              AND CONTINUE                 HASH001
CON015A  DC    0H'0'                                            HASH001
RR135    CCHASH CATNAME,R8        FIND CVOL FOR NAME      (REP) HASH001
*              THIS LINE DELETED BY                             HASH001
*              THIS LINE DELETED BY                             HASH001
*              THIS LINE DELETED BY                             HASH001
*              THIS LINE DELETED BY                             HASH001
RR160    CCHASH CAENAME,R8        FIND CVOL FOR NAME      (REP) HASH001
*              THIS LINE DELETED BY                             HASH001
*              THIS LINE DELETED BY                             HASH001
*              THIS LINE DELETED BY                             HASH001
*              THIS LINE DELETED BY                             HASH001
         CCHASH CATNAME,R8        FIND CVOL FOR NAME      (REP) HASH001
*              THIS LINE DELETED BY                             HASH001
*              THIS LINE DELETED BY                             HASH001
*              THIS LINE DELETED BY                             HASH001
*              THIS LINE DELETED BY                             HASH001
RR185    CCHASH CATNAME,R14       FIND CVOL FOR NAME      (REP) HASH001
*              THIS LINE DELETED BY                             HASH001
*              THIS LINE DELETED BY                             HASH001
*              THIS LINE DELETED BY                             HASH001
*              THIS LINE DELETED BY                             HASH001
RR215    CCHASH CATNAME,R8        FIND CVOL FOR NAME      (REP) HASH001
*              THIS LINE DELETED BY                             HASH001
*              THIS LINE DELETED BY                             HASH001
*              THIS LINE DELETED BY                             HASH001
*              THIS LINE DELETED BY                             HASH001
RR260    CCHASH CAENAME,R8        FIND CVOL FOR NAME      (REP) HASH001
*              THIS LINE DELETED BY                             HASH001
*              THIS LINE DELETED BY                             HASH001
*              THIS LINE DELETED BY                             HASH001
*              THIS LINE DELETED BY                             HASH001
RRHASHSV DC    3F'0'              R14-R0 TEMP SAVE              HASH001
./ NUMBER INSERT=YES,SEQ1=02018000,NEW1=02018010,INCR=10
         EJECT ,                                                HASH001
         PUSH USING                                             HASH001
         DROP R8                  VOLLIST                       HASH001
*-------------------------------------------------------------* HASH001
*                                                             * HASH001
* HASHSUB - SUBROUTINE USED BY RR TO DETERMINE CVOL FOR       * HASH001
*           NAME FOUND IN CATALOG.  FIRST TRYS HASH, THEN     * HASH001
*           SEARCH VOLLIST-VOLLNAME'S.                        * HASH001
*                                                             * HASH001
* ENTRY: R15 - @ HASHSUB                                      * HASH001
*        R14 - RETURN ADDRESS                                 * HASH001
*        R13 - ?                                              * HASH001
*        R0  - @ CL8 NAME TO FIND CVOL FOR                    * HASH001
*                                                             * HASH001
* EXIT:  R15 - @ VOLLIST ENTRY FOR CVOL.                      * HASH001
*                (R1 - R13 RESTORED).                         * HASH001
*                                                             * HASH001
*-------------------------------------------------------------* HASH001
         SPACE 1                                                HASH001
HASHSUB  STM   R1,R14,HASHSAV     SAVE REGS                     HASH001
         LR    R12,R15                                          HASH001
         USING HASHSUB,R12                                      HASH001
         LA    R13,HASHSAV2       @ SAVE AREA                   HASH001
         L     R3,=A(PRNTLINE)                                  HASH001
         USING PRNTLINE,R3                                      HASH001
         LR    R2,R0              @ NAME                        HASH001
         MVC   HASHWRK(8),0(R2)   COPY NAME                     HASH001
         LA    R1,HASHWRK                                       HASH001
         LA    R0,9               MAX STRING LENGTH             HASH001
         ICM   R15,15,HASHEPA     @ HASH ROUTINE                HASH001
         BZ    HASHSUB2           BIF NO HASH ROUTINE           HASH001
         BALR  R14,R15            TRY FOR HASH                  HASH001
         BNZ   HASHSUB2           BIF NOT HASHED INDEX          HASH001
* R1 = @ CL6 OF CVOL TO USE                                     HASH001
         L     R15,VOLCHAIN       @ 1ST ON VOLLISTS             HASH001
         USING VOLLIST,R15                                      HASH001
HASHSUB1 CLC   VOLLVOL,0(R1)      THIS THE CVOL?                HASH001
         BE    HASHSUB9           BIF CVOL FOUND                HASH001
         ICM   R15,15,VOLLNEXT    @ NEXT (OR ZERO)              HASH001
         BNZ   HASHSUB1           BIF NEXT CVOL EXISTS          HASH001
* ---              HASH RTN RETURNED UNKNOWN CVOL       --- *   HASH001
         MVC   PRNTLINE+1(L'ERRMSG20),ERRMSG20                  HASH001
         MVC   PRNTLINE+1+L'ERRMSG20(6),0(R1)   UNKNOWN CVOL    HASH001
         MVC   PRNTLINE+1+L'ERRMSG20+8(3),=C'FOR'               HASH001
         MVC   PRNTLINE+1+L'ERRMSG20+13(8),0(R2) NAME           HASH001
         BAL   R9,PUTSP           PRINT ERROR MSG               HASH001
* ---             PROCESS NON-HASHED NAME               --- *   HASH001
HASHSUB2 L     R15,VOLCHAIN       @ 1ST ON VOLLISTS             HASH001
         CLC   HASHWRK,VOLLNAME   <= NAMES IN THIS VOL?         HASH001
         BNH   HASHSUB9           BIF CVOL FOUND                HASH001
         L     R15,VOLLNEXT       @ NEXT (NEVER ZERO)           HASH001
         B     HASHSUB2                                         HASH001
HASHSUB9 LM    R1,R14,HASHSAV     RESTORE REGS                  HASH001
         BR    R14                                              HASH001
         POP   USING                                            HASH001
         SPACE 1                                                HASH001
HASHMOD  DC    CL8' '             HASH MODULE NAME (OR BLANKS)  HASH001
HASHEPA  DC    A(0)               @ HASH MODULE (OR ZERO)       HASH001
HASHSAV  DC    16F'0'             REG SAVE R1-R14               HASH001
HASHSAV2 DC    18F'0'             72 BYTE SAVEAREA              HASH001
HASHWRK  DC    CL8' ',C' '        HASH WORK AREA (HLI, DELIM)   HASH001
ERRMSG20 DC    C'ERR20 UNKNOWN CVOL FROM HASH RTN, CVOL '       HASH001
         LTORG ,                                                HASH001
