PROC 0 DEBUG ARCDASD(NO) RCODE(0)
/*
/* REFER: ABL.CMDPROC(ARCLIST) JUNE 1989
/* DOC  : WRITTEN BY MARK DIEHL @ HERCULES/ALLEGANY BALLISTICS LAB (ABL)
/* DOC  : "POOR MAN'S HSM" DIALOG: MAIN DRIVER CLIST
/* DOC  : REFERENCE DIALOG.ARCDASD($DOC) FOR A SYSTEM OVERVIEW
/*
CONTROL MAIN NOMSG NOLIST
IF &DEBUG = DEBUG THEN +
   CONTROL MAIN MSG LIST SYMLIST CONLIST PROMPT
ISPEXEC CONTROL ERRORS RETURN
ATTN +
  DO
   WRITE TERMINATING PER YOUR REQUEST
   EXIT CODE(0)
  END
ERROR +
  DO
   SET &RCODE = &LASTCC
   RETURN
  END
/* COMPUTE CPU TIME USED
SET BCPU = &SUBSTR(1:&LENGTH(&SYSCPU)-3,&SYSCPU)
/*********************************************************************/
/*
/* PRE-ALLOCATE REPORT DATASET VIA SEPERATE CLIST TO AVOID ENQ HASSLES
/*
/*
IF &DEBUG = DEBUG THEN +
   ARCALLOC DEBUG
  ELSE +
   ARCALLOC
/* CHECK THAT CLIST "ARCALLOC" CREATED DSN=ARCHIVED.DATASETS
IF &SYSDSN(ARCHIVED.DATASETS) NE OK THEN +
 DO
  ISPEXEC SETMSG MSG(ARC017)
  EXIT CODE(0)
 END
/*********************************************************************/
/*
/* ALLOCATE THE REPORT DATASET (FOR USE BY PDF LIBRARY MGMNT. SERVICES)
/*
ISPEXEC LMINIT DATAID(USERRPT) DATASET(ARCHIVED.DATASETS) ENQ(MOD)
 IF &RCODE GE 8 THEN +
   DO
     ISPEXEC SETMSG MSG(ARC019A)
     SET &RCODE = 0
     GOTO NOREPORT
   END
ISPEXEC LMOPEN DATAID(&USERRPT) OPTION(OUTPUT)
 IF &RCODE GE 8 THEN +
   DO
     ISPEXEC SETMSG MSG(ARC019B)
     SET &RCODE = 0
     GOTO NOREPORT
   END
/*********************************************************************/
/*
/* ALLOCATE THE INPUT DATASET (FOR USE BY PDF LIBRARY MGMNT. SERVICES)
/*
ISPEXEC LMINIT DATAID(ARCRPTS) DATASET('ABL.ARCDASD.REPORTS') ENQ(SHR)
 IF &RCODE GE 8 THEN +
   DO
     ISPEXEC SETMSG MSG(ARC019)
     SET &RCODE = 0
     GOTO NOREPORT
   END
ISPEXEC LMOPEN DATAID(&ARCRPTS) OPTION(INPUT) +
               LRECL(INLRECL) RECFM(INRECFM) ORG(INDSORG)
 IF &RCODE GE 8 THEN +
   DO
     ISPEXEC SETMSG MSG(ARC011A)
     SET &RCODE = 0
     GOTO NOREPORT
   END
/*
/* READ EACH MEMBER FROM THE DF/DSS ARCHIVE REPORT PDS, EXTRACTING ONLY
/* RECORDS CONTAINING &SYSUID.
/*
GETMEM: +
ISPEXEC LMMLIST DATAID(&ARCRPTS) OPTION(LIST) MEMBER(RPTMEMBR)
 IF &RCODE = 8 THEN +
   DO
    /* END OF MEMBER LIST. CLOSE FILE.
    ISPEXEC LMMLIST DATAID(&ARCRPTS) OPTION(FREE)
    ISPEXEC LMCLOSE DATAID(&ARCRPTS)
    SET &RCODE = 0
    GOTO USERVIEW
   END
 IF &RCODE GT 10 THEN +
   DO
    /* BIG TROUBLE IN LMMLIST LAND! CAN'T CONTINUE
    ISPEXEC SETMSG MSG(ARC011B)
    SET &RCODE = 0
    EXIT CODE(0)
   END
/*
 ISPEXEC LMMFIND DATAID(&ARCRPTS) MEMBER(&RPTMEMBR)
 /* READ A RECORD FROM CURRENT MEMBER
 GETREC: +
 ISPEXEC LMGET DATAID(&ARCRPTS) MODE(INVAR) DATALOC(RPTTEXT) +
  DATALEN(DATALEN) MAXLEN(&INLRECL)
   IF &RCODE = 8 THEN +
       DO
         /* END-OF-MEMBER INPUT CC = 8
         /* ISPEXEC LMCLOSE DATAID(&ARCRPTS)
         SET &RCODE = 0
         GOTO GETMEM
       END
   IF &RCODE GT 8 THEN +
       DO
         ISPEXEC SETMSG MSG(ARC018)
         ISPEXEC LMCLOSE DATAID(&ARCRPTS)
         SET &RCODE = 0
         GOTO NOREPORT
       END
 IF &SYSINDEX(&STR(&SYSUID),&STR(&RPTTEXT)) Â¬= 0 THEN +
  DO
   SET &ARCDASD= YES   /* INDICATE THAT AT LEAST ONE DS HAS BEEN ARC'D
   /* FIND COLUMN NUMBER WHERE USERID BEGINS
   SET &DSNBEGIN = &SYSINDEX(&STR(&SYSUID),&STR(&RPTTEXT))
   /* ALLOW FOR VARIABLE LENGTH DATASET NAMES, ELSE CLIST ERROR 932'S!
   IF &LENGTH(&STR(&RPTTEXT)) LT &EVAL(&DSNBEGIN + 44) THEN +
     DO
      SET &RECLEN = &LENGTH(&RPTTEXT)
      SET &DSNARC = &SUBSTR(&DSNBEGIN:&RECLEN,&STR(&RPTTEXT))
     END
   ELSE +
     DO
      /* A RARE 44 CHARACTER DSN!
      SET &DSNEND = 44
      SET &DSNARC  = &SUBSTR(&DSNBEGIN:&DSNEND,&STR(&RPTTEXT))
     END
  SET &ARCVOL  = &SUBSTR(1:4,&RPTMEMBR) /* EXTRACT VOL  FROM MEMNAME */
  SET &ARCDATE = &SUBSTR(5:8,&RPTMEMBR) /* EXTRACT DATE FROM MEMNAME */
  SET &ALLINFO = &STR(ARCHIVED ON &ARCDATE FROM VOL=&ARCVOL:  &DSNARC)
  SET &RPTTEXT = &STR(&ALLINFO)
  ISPEXEC LMPUT DATAID(&USERRPT) DATALOC(RPTTEXT) +
          MODE(INVAR) DATALEN(&INLRECL)
   IF &RCODE GT 10 THEN +
      DO
        ISPEXEC SETMSG MSG(ARC016)
        SET &RCODE = 0
        GOTO NOREPORT
      END
  END
 GOTO GETREC
END
/*********************************************************************/
/*
/* ALL MEMBERS HAVE BEEN PROCESSED, SHOW THE USER AN ARCHIVE REPORT
/*
USERVIEW: +
 IF &ARCDASD = NO THEN +
   DO
     /* AN ACTIVE USER! TELL THEM THAT NOTHING WAS EVER ARCHIVED.
     /* THEN DELETE THE ONLINE (NULL) REPORT
     ISPEXEC SETMSG MSG(ARC013)
     SE '#ABLUTIL: ARCHIVE LIST REQUESTED. NO DSNS FOUND',CN(01)
     ISPEXEC LMFREE DATAID(&USERRPT)
     ISPEXEC LMCLOSE DATAID(&USERRPT)
     DELETE  ARCHIVED.DATASETS
     GOTO NOREPORT
   END
SET ECPU = &SUBSTR(1:&LENGTH(&SYSCPU)-3,&SYSCPU)
/* COMPUTE THE CPU TIME USED TO CONSTRUCT THE REPORT
SE '#ABLUTIL: ARCHIVE LIST REQUESTED (&EVAL(&ECPU-&BCPU) SECS.)',CN(01)
ISPEXEC LMCLOSE DATAID(&USERRPT)
ISPEXEC SETMSG MSG(ARC014)
ISPEXEC BROWSE DATASET(ARCHIVED.DATASETS)
SET &ZHDSN = ARCHIVED.DATASETS
SET &ZUHOPT = PD
ISPEXEC SETMSG MSG(ARC019C)
ISPEXEC VPUT (ZHDSN ZUHOPT) SHARED /* SAVE KEYSTROKES ON NEXT PANEL */
ISPEXEC SELECT PGM(ISRUHC)   /* DISPLAY SPF'S 3.6 HARDCOPY PANEL   */
/*
ISPEXEC LMFREE DATAID(&USERRPT)
ISPEXEC LMCLOSE DATAID(&USERRPT)
NOREPORT: +
ISPEXEC LMFREE DATAID(&ARCRPTS)
ISPEXEC LMCLOSE DATAID(&ARCRPTS)
EXIT CODE(0)
