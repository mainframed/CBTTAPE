/*REXX SPLTJOIN-------------------------------------------------*/
/* (For the XEDIT folks)                                        */
/* Split the line at the cursor location, OR, if there are only */
/* blanks following the cursor then  Join the following line to */
/* the cursor line at the cursor position                       */
/* Hint: Set a PF key, maybe PF14, to execute the VMSPLIT macro */
/*       then you can simply position the cursor at the         */
/*       location of the SPLIT/JOIN and press PF14              */
/*--------------------------------------------------------------*/
   Trace Off
   Address isredit           /* Default command environment is ISREDIT */
   "MACRO PROCESS"                              /* Required by ISREDIT */
   "(cursl,cursc)=cursor"                   /* Get the cursor location */
   "(curline)=line .zcsr"          /* Get the line where the cursor is */
   If 'SUBSTR'(curline,cursc,'LENGTH'(curline)-cursc)>" " Then
     "tsplit"          /* There was something following the cursor col */
     Else  Do             /* If nothing followed the cursor, all blank */
      "(cursl,cursc)=cursor"                /* Get the cursor position */
      "(curline)=line .zcsr"       /* Get the line where the cursor is */
      "(nxtline)=line" cursl+1    /* Get the line following the cursor */
      joined='SUBSTR'(curline,1,cursc-1)||'STRIP'(nxtline)
      "line .zcsr = (joined)"      /* Overlay the line with the cursor */
      "delete" cursl+1                       /* Delete the joined line */
   End
   Exit 0
