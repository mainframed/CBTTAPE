//SCOTTZ   JOB 'TSM0029',NOTIFY=SCOTT,
//   CLASS=S,MSGCLASS=X,MSGLEVEL=(1,1),
//   USER=SCOTT,PASSWORD=SCOTTY
//*
//*   NAME:      SYS2.USERMODS(TSM0029)
//*   PURPOSE:   TO INSTALL USERMOD TSM0029 WHICH INSTALLS A MODIFIED
//*              IEECVETC MODULE.  CHANGE OF CODE TO PICK UP THE VALUES
//*              SET IN THE DCM MACRO.
//*
//*   *NOTE(S):  RE-IPL WITH A CLPA IS REQUIRED.
//*
//*    STATUS:
//*
//RR     EXEC SMPAPP
//SMPCNTL  DD *
  RESTORE S(TSM0029) .
   RESETRC .
  REJECT  S(TSM0029) .
/*
//RECV   EXEC SMPREC
//SMPPTFIN DD *
++USERMOD(TSM0029) .
++VER(Z038) FMID(EBB1102) PRE(UZ35148) .
++SRC(IEECVETC) SYSLIB(SAMPLIB) DISTLIB(AOST4) .
IEECVETC CSECT
*A185600,185700,191700,191600,191800,197100-197420,210020     MH Y02173
*C140500,184000,185500                                        MH Y02173
*D183000,181000,181500,180500,182000,182500                   MH Y02173
*A107100-107500                                              MC SA52527
*A 195100-195520                                              JE YM6718
         TITLE 'IEECVETC          ASYNCHRONOUS ERROR'
***********************************************************************
*                                                                     *
* STATUS CHANGE LEVEL=0                                               *
*                                                                     *
* APARS FIXED  OZ14126                                                *
*                                                                     *
* FUNCTION     THIS ROUTINE INITIALIZES THE DCM ON A RE-OPEN CONDITION*
*              HANDLES ASYNCHRONOUS ERRORS, AND PERFORMS CONSOLE      *
*              SWITCHING AFTER A PERMANENT SYNCHRONOUS ERROR.         *
*              FOR AN ASYNCHRONOUS ERROR THE SENSE FIELD IN THE UCB   *
*                 IS TESTED. EQUIPMENT CHECKS HAVE NO LIMIT ON THE    *
*                 NUMBER OF RETRIES TO BE PERFORMED. OTHER ERRORS     *
*                 WILL BE RETRIED ONCE. WHEN AN ASYNCHRONOUS ERROR    *
*                 OCCURS INDICATORS  WILL BE SET TO BLANK THE SCREEN  *
*                 AND TO DISPLAY AN APPROPRIATE ERROR MESSAGE.        *
*              IF THE TIMER IS WORKING, AN EXIT WILL BE MADE TO TIMER/*
*                 INTERPRETER ROUTINE TO SET THE TIMER FOR 30 SECONDS *
*                 WHEN THE TIMER ELAPSES(IF NO INTERVENING CANCEL HAS *
*                 BEEN PERFORMED), A CANCEL WILL BE PERFORMED, REMOV- *
*                 ING THE ASSYNCHRONOUS ERROR MESSAGE.  CONTROL WILL  *
*                 BE RETURNED TO THE ASSYNCHRONOUS ERROR ROUTINE      *
*                 TO RESTORE THE FIELDS TO ORIGINAL STATUS BEFORE     *
*                 ASSYNCHRONOUS ERROR OCCURRED.                       *
*                                                                     *
*ENTRY POINT   IEECVETC                                               *
*                                                                     *
*INPUT         REGISTER 1 POINTS TO CXSA                              *
*                                                                     *
*OUTPUT        NONE                                                   *
*                                                                     *
*EXTERNAL REFERENCES  NONE                                            *
*                                                                     *
*EXITS - NORMAL  IEECVETH OR P OR R DEVICE DEPENDENT IO               *
*                IEAVSWCH CONSOLE SWITCH ROUTINE                      *
*                IEECVFTG CLEAN UP ROUTINE FOR DIDOCS                 *
*       - ERROR  IEECVETD MESSAGE MODULE 1                            *
*                IEECVETK TIMER/INTERPRETER ROUTINE                   *
*                                                                     *
*TABLES/WORK AREAS DISPLAY CONTROL MODULE (DCM)                       *
*                   SCREEN AREA CONTROL BLOCK TABLE (SACB)            *
*                                                                     *
*ATTRIBUTES   REFRESHABLE, PRIVILEGED, TYPE 4 SVC                     *
*                                                                     *
*NOTES        THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A     *
*              PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL     *
*              CHARACTER SET.                                         *
*                                                                     *
*  CHANGE LOG:                                                        *
*                                                                     *
********************************************************************* *
         EJECT
***
***      REGISTERS
***
XF       EQU   15                  WORK REG
X1PARM   EQU   1
XFBRANCH EQU   15
XERTRN   EQU   14                  WORK REG
XD       EQU   13                  WORK REG
XC       EQU   12                  PROGRAM BASE REG
XB       EQU   11                  CXSA BASE REG
XA       EQU   10                  UCMENTRY BASE REG
X9       EQU   9                   DCM TRANSIENT BASE REG        S21002
X8       EQU   8                   UCM BASE REG
X7       EQU   7                   WORK REG
X6       EQU   6                   WORK REG
X5       EQU   5                   WORK REG
X4       EQU   4                   DCM RESIDENT BASE REG         S21002
X3       EQU   3                   WORK REG
X2       EQU   2                   WORK REG
X1       EQU   1                   WORK REG
X0       EQU   0                   WORK REG
         EJECT
         BALR  XC,N0               ESTABLISH BASE REGISTER
         USING *,XC                ESTABISH ADDRESSIBILITY
         B     BEGIN               BRANCH AROUND PATCH AREA
ICATCH   DC    C'IEECVETC'         EYECATCHER AND FIELD MAINTENANCE
         DC    C'UZ35148 '                                          SKV
         DC    CL8'&SYSDATE'
BEGIN    DS    0F                                                   SKV
         LR    XB,X1
         USING CXSA,XB              ESTABLISH CXSA BASE REG
         L     XA,CSAUCM
         USING UCMLIST,XA
         L     X4,UCMXB            ADDRESS OF RESIDENT DCM        21002
         USING DCMTSRT,X4          ADDRESSIBILITY OF RESIDENT DCM
         L     X9,DCMADTRN         ADDRESS OF TRANSIENT DCM      S21002
         USING DCMSTRT,X9
         L     X8,CSACTLM          GET UCM POINTER
         USING UCM,X8              DECLARE UCM BASE
         MVC   DCMTRACE(DCMTRLEN),DCMTRAC2 SHIFT OLD TRACE ENTRIES
         MVI   DCMTREN1,ID1        PUT CSECT ID INTO
         MVI   DCMTREN2,ID2        NEW TRACE ENTRY
         L     XFBRANCH,CSAXB      ADDRESS GETLOCK SUBROUTINE
         BALR  XERTRN,XFBRANCH     OBTAIN LOCKS
***
***   DETERMINE REASON FOR ENTRY
***
         TM    DCMCOM2,DCMAE       IS THIS ENTRY FROM CLEAN UP RTS21002
         BO    CLEANEN             BRANCH YES
         TM    DCMCS,DCMCSO        Q. REOPEN REQUEST
         BNO   ERROR               NO, THEN MUST BE ERROR
REOPEN   EQU   *                   YES,INIT DCM AND REWRITE SCREEN
         CLI   DCMIONDX,NDX2250        DEVICE A 2250
         BNE   NOCHANGE                NO A CONTINUR
         L     X7,UCMUCB               YES GET UCB
         TM    N16(X7),MOD3            IS 2250 A MOD 3
         BNO   NOCHANGE            NO-SKIP BUFFER UPDATE
         L     X6,N36(X7)              POINT TO BUFFER TABLE
         LA    X6,N0(X6)           CLEAR HI ORDER BYTE
         LTR   X6,X6                   VALID ADDRESS
         BZ    NOCHANGE            NO-SKIP UPDATE
         SR    X5,X5                   CLEAR REG
         IC    X5,N36(X7)              GET ID
         MH    X5,EIGHT                EIGHT
         AR    X5,X6                   GET DISPLACEMENY
         LH    X7,N2(X5)               GET BUFFER DISPLACEMENT
         SH    X7,FORTY                CORRECT GARBAGE
         MH    X7,T256                 GET ACTUAL BEGINNING OF BUFER
         L     X5,DCMBUFAD         POINT TO START OF BUF AD TAB
         LA    X2,N6               LOOP CONTROL REG              S21002
         LH    X1,N0(X5)           GET INITIAL VALUE FROM TABLE  S21002
LOOP4    EQU   *                                                 S21002
         LH    X6,N0(X5)           LOAD BUFFER ADDRESS           S21002
         SR    X6,X1               SUBT INITIAL VALUE FROM EACH  S21002
         AR    X6,X7               GET NEXT ADDRESS              S21002
         STH   X6,N0(X5)           UPDATE NEXT ADDRESS           S21002
         LA    X5,N2(X5)           INCREMENT ADDRESS BY TWO      S21002
         BCT   X2,LOOP4            BRANCH IF NOT ZERO            S21002
         L     X6,DCMAWARN POINT TO WARNING LINE
         STH   X7,N76(X6)              UPDATE GTRU
***
***   THIS  ROUTINE WILL REINIT THE DCM
***
NOCHANGE EQU   *
         XC    8(4,X4),8(X4)                                        SKV
         MVI   DCMINPUT,BLANK      BLANK INPUT AREA SO IT CAN BE USED
         MVC   DCMINPUT+N1(N125),DCMINPUT    TO BLANK SCREEN IMAGE BUFR
         LH    X6,DCMCORLN         GET LENGTH OF ONE LINE     MB  M2070
         LH    X5,DCMLGNTH         PUT LENGTH FOR MOVE IN REG MB  M2070
         BCTR  X5,N0                AND DECREMENT BY ONE
         L     X2,DCMASCRN         GET ADDR 1ST LINE IN SCREEN IMAG BUF
         LH    X7,DCMMSGAL         NUMBER OF MESSAGE AREA LINES
         MH    X7,DCMCORLN         TIMES WIDTH OF EACH LINE
         AR    X7,X2               PLUS START ADDRESS OF MSGS
         ST    X7,DCMWTBUF         SET END ADDRESS FOR MSG AREA
         ST X7,DCMWTINT            SAVE WTBUF INITIAL VALUE
         SR    X7,X6               DECREMENT BY ONE LINE      MB  M2070
LOOP     EQU   *
         EX    X5,BLANKLNE         EXECUTE INTO MOVE INSTRUC
         BXLE  X2,X6,LOOP          LOOP UNTIL BUFFER IS BLANK
***
***      BLANK DELETE REQUEST BUFFER
***
         MVC   DCMRQDEL(N8),DCMINPUT
***
***      INITIALIZE OPTIONS
***
         MVI   DCMDEL,CHARY        SET DEL EQUAL TO YES          S21002
*        MVI   DCMDEL+N1,BLANK     SET TRAILING BLANK   WAS 21002   SKV
         BCR   X0,X0               KEEP WITH WHAT DCM HAS          SKV
         BCR   X0,X0               KEEP WITH WHAT DCM HAS          SKV
         MVC   DCMSEG(N1),DCMSEGDF SET SEG EQUAL TO DEFAULT VALUE
*        MVI   DCMCON,CHARY        SET CON EQUAL TO YES             SKV
         BCR   X0,X0               KEEP WITH WHAT DCM HAS          SKV
         BCR   X0,X0               KEEP WITH WHAT DCM HAS          SKV
         MVI   DCMOPTST,CHARE0     BITS INDICATING STATUS
         TM    DCMR3FLG,DCMKVIP    IS THIS A K V CDR PROCESSING  S21002
         BO    ZEROSCT1            BRANCH YES DON'T INIT OPITION S21002
         MVC   DCMRNUM(N3),DCMRNUMD  INITIALIZE OPTIONS          S21002
ZEROSCT1 EQU   *                                                 S21002
         SR    X2,X2               CLEAR REGISTER            MC SA52527
         IC    X2,DCMRNUM          GET CURRENT ROLL NUMB     MC SA52527
         CH    X2,DCMMSGAL         LARGER THAN SCREEN        MC SA52527
         BNH   OFFKV               NO, CONTINUE              MC SA52527
         LH    X2,DCMMSGAL         YES, CHANGE ROLL NUMB     MC SA52527
         STC   X2,DCMRNUM          TO MAX SCREEN SIZE        MC SA52527
OFFKV    NI    DCMR3FLG,N255-DCMKVIP TURN OFF K V FLAG       MC SA52527
***
***      ZERO SCREEN CONTROL TABLE ENTRIES
***
         SR    X5,X5               ZERO MOVE-FROM LENGTH      MB YM4086
         L     X2,DCMDOMPK         START OF CONTROL TABLES    MB YM4086
         LH    X3,DCMLEN           TOTAL DCM LENGTH           MB YM4086
         AR    X3,X9               END OF DCM                 MB YM4086
         SR    X3,X2               LENGTH OF CONTROL TABLES   MB YM4086
         MVCL  X2,X4               USE UCM AS DUMMY ADDRESS   MB YM4086
         LH    X7,DCMMSGAL         NUMBER OF SCT ENTRIES      MB YM4086
         MH    X7,DCMSCTCN         TIMES SIZE OF EACH         MB YM4086
         A     X7,DCMAMTAB         START ADDRESS PLUS LENGTH  MB YM4086
         MVI   N0(X7),DCMMSGST     PUT STOPPER AFTER SCT TABLE
         SH    X7,DCMSCTCN         BACK UP TO LAST TABLE ENTRY
         ST    X7,DCMADDRL         SAVE LAST SCT ENTRY POINTER
         LH    X7,DCMMSGAL         NUMBER OF ENTRIES
         A     X7,DCMADSEC         POINT PAST LAST SSCT ENTRY
         MVI   N0(X7),DCMSECST     SET STOPPER AFTER LAST
***      ZERO  COMMUNICATIONS BYTES
***
         MVI   DCMDSTAT,N0         ZERO SCREEN STATUS BYTE
         NI    DCMIOUNQ,N255-DCMIO226-DCMRPCUR-DCMFRSCN-DCMRDARM
         NI    UCMDEVC,N255-UCMDEVA   TURN OFF SCREEN         MG  M2504
         NI    DCMR2FLG,N255-DCMRXSFL   FULL FLAGS IN DCMS    MG  M2504
         XC    DCMIOCM1(N14),DCMIOCM1   ZERO OTHER COMMUNICATIONS BYTES
***
***      REINITIALIZE INSTRUC LINE
***
REINIT   EQU   *
         OI    DCMIOCM2,DCMINSSH   SET BIT- INITIALIZE INSTRUC LINE
         L     X1,DCMADPFK         GET POINTER TO PFK LIST       S21003
         LTR   X1,X1               SEE IF TO LIGHT ANY PFKS      S21003
         BZ    SKIP                BRANCH NO-GO CONTINUE         S21003
         OI    DCMIOCM3,DCMLTPFK   YES-INDICATE SO FOR I/O ROUT  S21003
***
***      TURN  ON BITS TO INDICATE WHAT SHOULD BE WRITTEN BY I/O RTN
***
SKIP     EQU   *                                                 S21003
         XC    DCMIRCTR(N2),DCMIRCTR CLEAR INT REQ COUNT         A36163
         OI    DCMIOCM3,DCMOPRMI   INDICATE PERFORM RMI, INSURE KB
*                                  KEYBOARD IS UNLOCKED
         NI    DCMCS,N255-DCMCSO   TURN OFF REOPEN BIT
         MVI   DCMCULNO,N1         SET UP CURSOR POSITION- LINE 1 OF
         MVI   DCMPOSCU,N1          ENTRY AREA, POSITION ZERO
***
***      SET BITS TO INDICATE WRITE ENTIRE SCREEN AND INSERT CURSOR
***
NONIP    EQU   *
         OI    DCMIOCM1,WRITEALL   INDICATE PERFRM WRITES OF SCREEN
         OI    DCMIOCM2,DCMBLENT+DCMBLWRL+DCMBLWRR BLANK ENTRY AREA
IOEXIT   EQU   *
         L     XFBRANCH,DCMIORTN   LOAD IO ROUTINE ADDRESS
         L     X1PARM,DCMCXSVE     RESTORE CXSA ADDRESS
         BR    XFBRANCH            EXIT TO IEECVET(H,P,R, OR U)
ERROR    EQU   *
         TM    DCMTIMES,DCMTASYN   Q. RETURN AFTER ASYNC ERROR
         BO    TURNOFF             YES, GO INITIALIZE SWITCHED
*        TM    UCMSTS,UCMAF        ATTENTION PENDING           @ZA14126
*        BO    LABEL1              YES, BYPASS ECB ERROR CHECK @ZA14126
LABEL    TM    UCMECB,ERRINC       Q. A PERMANENT SYNCHRONOUS ERROR
         BO    PERMSYNC            YES, GO SWITCH CONSOLES
LABEL1   L     X3,UCMUCB           NO, GET POINTER TO UCB      @ZA14126
         USING UCBOB,X3            UCB ADDRESSABILITY         MB YM5671
         NI    UCMSTS,N255-UCMAF   TURN OFF ATTENTION FLAG
***
***   TEST FOR TYPE OF ASYNCHRONOUS ERROR
***
         TM    UCMDISP,UCMDISPD    IS CONSOLE OUTPUT ONLY     MH  M2095
         BO    PERMSYNC             YES, DON'T RETRY          MH  M2095
         TM    UCBSNS,DCHK         A DATA CHECK               MB YM5671
         BO    ERRDATA             YES
         TM    UCBSNS,ECHK         EQUIP CHECK OR COMBINATION MB YM5671
         BO    CONDERR                OF EQUIP CHK AND INVAL BUF ADDR)
         TM    UCBSNS,INBUFADR     INVALID BUFFER ADDR CHECK  MB YM5671
         BO    ERRINVB             YES
         TM    UCBSNS,BUFADRPT     BUFFER ADDR PARITY CHECK   MB YM5671
         BNO   PERMSYNC            NO, GO SWITCH CONSOLES
ERRBAPC  EQU   *
         TM    DCMASYNC,DCMASBA    Q. IS THIS 2ND BUFFER ADR PAR CHK
         BO    PERMSYNC            YES, PERMANENT ERROR        @ZA14126
         OI    DCMASYNC,DCMASBA    NO, TURN OF RETRY INDICATOR
RETWRIT  EQU   *
         OI    DCMIOCM2,DCMERASE+DCMWRASY  PERFORM ERASE AND WRITE MSG
         OI    DCMR3FLG,DCMRXSCN   INDICATE AN ASYNC ERROR HAS OCCURRED
         OI    DCMCMSG2,DCMASYRT   INCIDATE MOVE IN ASYNC ERROR MSG
MSG2EXIT EQU   *
         MVI   UCBSNS,N0           ZERO SENSE INFO IN IOB     MB YM5671
*********************** DELETE 1 LINE OF CODE **************** @ZA14126
         OI    UCMDEVC,UCMDEVA       INDICATE SCREEN FULL SO THAT ENTRY
         OI    DCMR2FLG,DCMRXSFL   SCREEN FULL IN RDCM        ME S21003
         OI    DCMDSTAT,DCMDSAUT+DCMDSINR  WILL NOT BE MADE
         OI    DCMR2FLG,DCMRXSFL     FOR DISPLAYING MESSAGES WHILE
*                                    ASYNC ERROR MSG ON SCREEN
         OI    DCMTIMES,DCMOTTMM+DCMOPTTI+DCMTASYN  INDICATE CHECK FOR
*                                  CHANGE IN TIMER INTERVAL AND EXIT
*                                  TO MESSAGE MODULE 2
         OI    DCMCOM2,DCMAE       SET IND FOR CLEAN UP RTN      S21002
         L     XFBRANCH,DCMNCLN    LOAD CLEANUP ADDRESS
         L     X1PARM,DCMCXSVE     RESTORE CXSA ADDRESS
         BR    XFBRANCH            EXIT TO IEECVFTG
***
***    CONTINUATION AFTER EXIT AND RETURN FROM CLEAN UP ROUTINE  S21002
***
CLEANEN  EQU   *
         NI    DCMCOM2,N255-DCMAE  TURN OFF CLEAN UP IND         S21002
         MVC   DCMDSAV+N15(N2),DCMDEL   SAVE STATUS OF OPTIONS
         MVC   DCMDSAV+N17(N2),DCMRTME
         MVC   DCMDSAV+N19(N1),DCMOPTST
***                                                              S21002
***      ZERO SECONDARY SCREEN CONTROL TABLE ENTRIES             S21002
***                                                              S21002
         L     X3,DCMADSEC         GET ADDR OF FIRST SSCT ENTRY  S21002
         LH    X2,DCMMSGAL         GET NUMBER OF MESSAGES
SSCLOOP  EQU   *
         MVI   N0(X3),N0           ZERO SSCT ENTRY               S21002
         LA    X3,N1(X3)           INCREMENT TO NEXT SSCT ENTRY  S21002
         BCT   X2,SSCLOOP          LOOP UNTIL, DOM IDS ARE BLANK
***
***      ZERO  COMMUNICATIONS BYTES
***
TIMCONT  EQU   *                                                 S21002
         MVI   DCMDEL,CHARR        SET DEL EQUAL TO ROLL MODE    S21002
         MVI   DCMDEL+N1,BLANK     SET TRAILING BLANK            S21002
         OI    DCMOPTST,DCMOPRLL   BE SET
         MVC   DCMRTME(N2),H30     SET INTERVAL TO 30 SCEONDS
TIMEXIT  EQU   *
         L     XFBRANCH,DCMNTIMR   LOAD TIMER INTERPRETER ADDRESS
         L     X1PARM,DCMCXSVE     RESTORE CXSA ADDRESS
         BR    XFBRANCH            EXIT TO IEECVETK
ERRINVB  EQU   *
         TM    DCMASYNC,DCMASIN    Q. HAS THIS ERROR BEEN RETRIED
         BO    PERMSYNC            YES, PERMANENT ERROR        @ZA14126
         OI    DCMASYNC,DCMASIN    NO, INDICATE ERROR RETRY
         B     RETWRIT             GO WRITE ERROR MSG TO SCREEN
CONDERR  EQU   *
         TM    24(X9),X'04'                                         SKV
         BO    720(0,XC)                                            SKV
         OI    DCMIOCM2,DCMERASE+DCMWRASY  PERFORM ERASE AND WRITE MSG
         OI    DCMR3FLG,DCMRXSCN   INDICATE ASYNC ERROR ON SCREEN
         OI    DCMCMSG2,DCMASYCD   INDICATE MOVE IN COND ASYNC ERROR MG
         B     MSG2EXIT            EXIT
ERRDATA  EQU   *
         CLI   DCMIONDX,NDX2250         Q. IS DEVICE A 2250    @ZA14126
         BNO   DATAERR                  NO
         CLC   UCBSNS+N2,DCMDSAV+N8 ERROR AT SAME BYTE AS FOR MB YM5671
*                                       PREVIOUS ENTRY
         BNE   SAVEADDR                 NO, RETRY
DATAERR  TM    DCMASYNC,DCMASDA    Q. HAS THIS ERROR BEEN RETRIED ONCE
         BZ    SETFLAG            NO, SET ERROR RETRY         MH Y02173
         B     PERMSYNC           YES, PERM ERROR,SWITCH CNSLS @ZA14126
***********************************************************************
**************** FOUR  LINES OF CODE DELETED HERE ************ @ZA14126
***********************************************************************
SAVEADDR MVC   DCMDSAV+N8(N2),UCBSNS+N2 SAVE BUFFER ADDRESS   MB YM5671
SETFLAG  OI    DCMASYNC,DCMASDA    NO, INDICATE ERROR RETRY   MH Y02173
         B     RETWRIT             GO WRITE ERROR MSG TO SCRN MH Y02173
PERMSYNC EQU   *              PERFORM CONSOLE SWITCH
         OI    DCMR3FLG,DCMCLPR    INDICATE CLOSE IN PROGRESS MF  M0736
         OI    DCMCOM2,DCMERPF     INDICATE ERASE PERFORMED   MF  M0736
*                                  SO PROCESSOR WILL NOT TRY  MF  M0736
*                                  TO DO I/O TO A DEVICE WITH MF  M0736
*                                  AN I/O ERROR               MF  M0736
         L     XFBRANCH,CSAXC      ADDRESS FREELOCKS SUBROUTINE  YM6335
         LR    X1,XB               SET INPUT PARM             MB YM6335
         BALR  XERTRN,XFBRANCH     RELEASE LOCKS              MB YM6335
         NI    DCMMCSST,N255-DCMDUSE DIDOCS NO LONGER IN CONTROL
         MVI   UCMECB,N0           ZERO ECB
         ICM   X5,15,CSANPTR       PTR TO RECOVERY AREA       JE YM6718
         BZ    NOESTAE             NOT THERE, BYPASS NSI      JE YM6718
         MVC   PARMRTAD-PARMLIST(8,X5),CSANAME RESTORE RETRY & REGSAV
*                                                             JE YM6718
NOESTAE  DS    0H                                             JE YM6718
*        MVI   CSACODE,CSASWTCH    INDICATE CONSOLE SWITCH    JE YM6718
         MVI   CSACODE,X'05'                                        SKV
         MVC   CSANAME(8),CONSWTCH MODULE NAME                JE YM6718
         L     XERTRN,CSAXA        RESET RETURN REG           MB YM6335
         L     XFBRANCH,UCMSWCH    ADDRESS CONSOLE SWITCH ROUTINE
         L     X1PARM,DCMCXSVE     ADDRESS CXSA IN REG 1
         BR    XFBRANCH            EXIT TO CONSOLE SWITCH
TURNOFF  EQU   *
         TM    0(XA),X'40'                                          SKV
         BO    PERMSYNC                                             SKV
         L     X3,UCMUCB           LOAD UCB ADDRESS           MH Y02173
         TM    UCBSNS,ASYERR       ANY BAD SENSE DATA         MB YM5671
         BNZ   LABEL1               YES, MAY BE PERM  SKV     MH Y02173
         NI    DCMASYNC,X'FF'-DCMASDA-DCMASIN-DCMASBA         MH Y02173
*              TURN OFF ALL RETRY FLAGS                       MH Y02173
         NI    DCMR2FLG,N255-DCMRXSFL   TURN OFF
         NI    UCMDEVC,N255-UCMDEVA      SCREEN FULL BITS
         NI    DCMR2FLG,N255-DCMRXSFL SCREEN FULL IN R DCM    ME S21003
         NI    DCMTIMES,N255-DCMTASYN    TURN OFF BIT
         NI    DCMR2FLG,N255-DCMRXRLL-DCMRXTMR    TURN OFF BITS
         NI    DCMR3FLG,N255-DCMRXSCN  TURN OFF ASYNC CANCEL BIT
         MVC   DCMDEL(N2),DCMDSAV+N15  INITIALIZE OPTIONS TO
         MVC   DCMRTME(N2),DCMDSAV+N17   WHAT THEY HAD BEEN BEFORE THE
         MVC   DCMOPTST(N1),DCMDSAV+N19   ASYNCHRONOUS ERROR OCCURRED
         OI    DCMTIMES,DCMOPTTI
         B     TIMEXIT             EXIT TO TIMER/INTERPRETER RTN
ZEROSCT  XC    N0(N0,X2),N0(X2)    ZERO TABLE ENTRY OF SCT       S21002
BLANKLNE MVC   N0(N0,X2),DCMINPUT  INSTRUC TO BLANK LINES
         EJECT
*CONSWRTN DC    C'XL'              NAME OF CONSOLE SWITCH RTN       SKV
T256     DC    H'256'              NUMBER CONSTANT
FORTY    DC    H'40'               NUMBER CONSTANT
EIGHT    DC    H'8'                NUMBER EIGHT
H30      DC    H'30'               CONSTANT F  THIRTY
CONSWTCH DC    CL8'IGC0407B'       NAME OF CONSOLE SWITCH     JE YM6718
         EJECT
***
***      EQUATES
***
ASYERR   EQU   X'1B'               BAD SENSE DATA             MH Y02173
ID1      EQU   C'E'                1ST CHARACTER OF CSECT ID
ID2      EQU   C'C'                2ND CHARACTER OF CSECT ID
BLANK    EQU   C' '                BLANK CHARACTER
UCBINTER EQU   X'10'               UCB INTERCEPT FLAG
NDX2250  EQU   X'08'               DCM I/O ROUTINE INDEX FOR 2250
ERRINC   EQU   X'40'               ERROR CONDITION
INBUFADR EQU   X'02'               INVALID BUFFER ADDRESS
BUFADRPT EQU   X'01'               BUFFER ADDRESS PARITY CHECK
IOBCSW   EQU   8                   EQUATE
CSWUCK   EQU   X'02'               EQUATE
DOMLEN   EQU   8                   DOM ENTRY LENGTH
N0       EQU   0         NUMBER
N1       EQU   1         NUMBER
N2       EQU   2         NUMBER
N3       EQU   3         NUMBER
N4       EQU   4         NUMBER
N6       EQU   6         NUMBER
N8       EQU   8         NUMBER
N14      EQU   14                  NUMBER CONSTANT
N17      EQU   17                  NUMBER CONSTANT
N22      EQU   22                  NUMBER CONSTANT
N24      EQU   24                  NUMBER CONSTANT
N125     EQU   125                 NUMBER CONSTANT
N255     EQU   255                 BYTE SIZE
CHARY    EQU   C'Y'                CHARACTER Y
CHARE0   EQU   X'E0'               HEX E0
ECHK     EQU   X'10'               EQUIPMENT CHECK
DCHK     EQU   X'08'               DATA CHECK
MOD3     EQU   X'33'               MOD 3 CODE
N15      EQU   15                  NUMBER CONSTANT
N16      EQU   16                  NUMBER CONSTANT
N19      EQU   19                  NUMBER CONSTANT
N36      EQU   36                  NUMBER CONSTANT
N76      EQU   76                  NUMBER CONSTANT
WRITEALL EQU   X'77'               INDICATE TO WRITE FULL SCREEN
CHARR    EQU   C'R'                CHARACTER R FOR ROLL          S21002
         EJECT
         IHACTM FTPT                                          JE YM6718
         EJECT
         IEERDCM
         EJECT
         IEETDCM
         EJECT
*        PRINT NOGEN                                                SKV
         IEECUCM  FORMAT=NEW
         EJECT
         IHACTM  CXSA
         EJECT
ETCUCB   DSECT
         IEFUCBOB
         END
/*
//SMPCNTL  DD *
  RECEIVE S(TSM0029) .
/*
//APP    EXEC SMP
//SMPCNTL  DD *
  APPLY S(TSM0029)
       CHECK .
  APPLY S(TSM0029) .
/*
