*+--------------------------------------------------------------------+    1
*| FUNCTION:                                                          |    2
*|   PRINTS NAMES OF ALL MODULES LOADED IN ANOTHER ADDRESS SPACE.     |    3
*| INPUTS:                                                            |    4
*|   ADDRESS SPACE NAME FROM PARM= ON // EXEC CARD.                   |    5
*| OUTPUTS:                                                           |    6
*|   MODULE NAME LIST TO OUTPUT FILE.                                 |    7
*|   ACCESS LIST ENTRY FOR TARGET ADDRESS SPACE.                      |    8
*|      (THIS CAN BE DELETED WITH ALESERV DELETE.)                    |    9
*|   R15 - RETURN CODE                                                |   10
*|          0 - MODULE NAMES WRITTEN TO OUTPUT FILE.                  |   11
*|          8 - TARGET ADDRESS SPACE NAME NOT IN PARMLIST.            |   12
*|         12 - XTALK ROUTINE NOT LINK-EDITED.                        |   13
*|         16 - XTALK ROUTINE ERROR. OTHER REGISTERS CONTAIN:         |   14
*|              0 - RETURN CODE FROM XTALK.                           |   15
*|              1 - REASON CODE FROM XTALK.                           |   16
*|         20 - PRINT ROUTINE NOT LINK-EDITED.                        |   17
*|         24 - PRINT ROUTINE ERROR.                                  |   18
*| PROGRAM LOGIC:                                                     |   19
*|   1) PUT ADDRESS SPACE NAME FROM // EXEC PARM= INTO PARMLIST.      |   20
*|   2) CALL XTALK TO SET UP ACCESS LIST ENTRY AND RETURN ALET.       |   21
*|   3) GO INTO ACCESS REGISTER MODE.                                 |   22
*|   4) CHAIN THROUGH TCBS IN TARGET ADDRESS SPACE, MOVING            |   23
*|      MODULE NAMES FROM CDES FOR EACH TASK TO LIST IN THIS          |   24
*|      ADDRESS SPACE.                                                |   25
*|   5) GO INTO PRIMARY SPACE MODE.                                   |   26
*|   6) INVOKE PRINT TO PRINT MODULE NAME LIST.                       |   27
*| MODULES CALLED:                                                    |   28
*|   XTALK, PRINT                                                     |   29
*| ATTRIBUTES:                                                        |   30
*|   REENTERABLE, AMODE=31, RMODE=24, APF AUTHORIZED                  |   31
*+--------------------------------------------------------------------+   32
*+--------------------------------------------------------------------+
*|  Copyright (c) 1992, 1993, 2002 by Mitchell Marx & Penelope Davis  |
*|                                                                    |
*|  Address questions to: mmarx@mdpaladin.com                         |
*|                                                                    |
*|  Permission is hereby granted, free of charge, to any person       |
*|  obtaining a copy of this software and associated documentation    |
*|  files (the "Software"), to deal in the Software without           |
*|  restriction, including without limitation the rights to use,      |
*|  copy, modify, merge, publish, distribute, sublicense, and/or sell |
*|  copies of the Software, and to permit persons to whom the         |
*|  Software is furnished to do so, subject to the following          |
*|  conditions:                                                       |
*|                                                                    |
*|  The above copyright notice and this permission notice shall be    |
*|  included in all copies or substantial portions of the Software.   |
*|                                                                    |
*|  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,   |
*|  EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES   |
*|  OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND          |
*|  NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT       |
*|  HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,      |
*|  WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING      |
*|  FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR     |
*|  OTHER DEALINGS IN THE SOFTWARE.                                   |
*+--------------------------------------------------------------------+
         MODULE ESAMOVE,BASE=12,LOC=BELOW,AMODE=31,RMODE=24,           X  33
               TEXT='MODULE NAMES IN ANOTHER ADDRESS SPACE'               34
*        *---------------------------------------*                        35
*        *     GET ADDRESS SPACE NAME AND LENGTH *                        36
*        *     FROM PARM= ON // EXEC CARD.       *                        37
*        *---------------------------------------*                        38
         LAE   R3,PARMLIST             ADDRESS OF PARMLIST => R3          39
         USING XMSESA,R3               MAP PARMLIST                       40
         MVI   XMNAME,X'40'                  ..SET MODULE NAME            41
         MVC   XMNAME+1(L'XMNAME-1),XMNAME   ..IN PARMLIST TO SPACES      42
         L     R1,0(R1)                ADDRESS OF PARM STRING => R1       43
         XR    R2,R2                   CLEAR R2                           44
         ICM   R2,B'0011',0(R1)        LENGTH OF PARM                     45
         BZ    EXIT08                  =0; PROGRAM NAME NOT GIVEN         46
         ST    R2,XMNAMEL              SAVE IN PARMLIST                   47
         BCTR  R2,0                    LESS 1 FOR EXECUTE                 48
         EX    R2,MVIT1                MOVE NAME TO PLIST                 49
         B     MVIT1A                  BRANCH AROUND EXECUTED INSTR       50
MVIT1    MVC   XMNAME(0),2(R1)         ** EXECUTE ONLY **                 51
MVIT1A   DS    0H                                                         52
         MVC   MODLIST(L'XMNAME),XMNAME  MOVE ASCB NAME TO MODNAME LIST   53
*        *---------------------------------------*                        54
*        *     CALL ROUTINE TO BUILD             *                        55
*        *     ACCESS LIST ENTRY                 *                        56
*        *---------------------------------------*                        57
         MODESET MODE=SUP              GET A SUPERVISOR STATE PSW         58
         LAE   R1,PARMLIST             PARMLIST FOR XTALK ROUTINE         59
         ICM   R15,15,=V(XTALK)        ADDRESS OF ROUTINE TO R15          60
         BZ    EXIT0C                                                     61
         O     R15,=A(X'80000000')     SET FOR 31 BIT MODE                62
         BASSM R14,R15                 INVOKE ROUTINE                     63
         LTR   R15,R15                 RC = 0 ?                           64
         BNZ   EXIT10                  NO; EXIT RC=8                      65
         MODESET MODE=PROB             GET A SUPERVISOR STATE PSW         66
*        *---------------------------------------*                        67
*        *     GO INTO ACCESS REGISTER MODE      *                        68
*        *---------------------------------------*                        69
         SAC   512                     GO INTO AR ASC MODE                70
         LAE   R12,0(R12,0)            LOAD BASE AR WITH X'00'            71
         CPYA  A13,A12                 LOAD WORK AREA AR WITH X'00'       72
         SYSSTATE ASCENV=AR            SET FOR AR MACRO EXPANSIONS        73
*---------------------------------------------------------------------*   74
*************  THE FOLLOWING CODE RUNS IN ACCESS REGISTER MODE  *******   75
*---------------------------------------------------------------------*   76
*        *---------------------------------------*                        77
*        *     CHAIN THRU TCBS IN TARGET ADDRESS *                        78
*        *     SPACE, WRITING MODULE NAMES TO    *                        79
*        *     LIST IN THIS ADDRESS SPACE        *                        80
*        *---------------------------------------*                        81
         LAE   R7,MODNAMES             ADDRESS OF TABLE OF MODULES        82
         LAM   A6,A6,XMALET            LOAD ACCESS REGISTER W ALET        83
         L     R6,XMASCB               ADDR ASCB FROM PARMLIST            84
         L     R6,ASCBASXB-ASCB(,R6)   ADDR ASXB FROM PARMLIST            85
         L     R6,ASXBFTCB-ASXB(,R6)   ADDR FIRST TCB                     86
         USING TCB,R6                  MAP TCB                            87
*                                      ..COPY ALET FOR TARGET ADDRESS     88
         CPYA  A8,A6                   ..SPACE INTO ACCESS REGISTER A8    89
         XR    R9,R9                   CLEAR COUNTER                      90
T$CBLOOP DS    0H                                                         91
         USING CDENTRY,R8              MAP CDE                            92
         ICM   R8,15,TCBJPQ            ADDRESS OF JOB-PACK-AREA QUEUE     93
         BZ    N$EXTCB                 =0; NO MODULES FOR THIS TCB        94
C$DELOOP DS    0H                                                         95
         MVC   0(L'CDNAME,R7),CDNAME   MOVE CDE MODULE NAME TO LIST       96
         LA    R9,1(R9,0)              INCREMENT COUNTER                  97
         LAE   R7,L'CDNAME(,R7)        POINT AT NEXT ENTRY IN LIST        98
         ICM   R8,15,CDCHAIN           ADDRESS OF NEXT CDE ON JPAQ        99
         BNZ   C$DELOOP                ¬=0; PROCESS NEXT CDE             100
N$EXTCB  DS    0H                                                        101
         ICM   R6,15,TCBTCB            ADDRESS IF NEXT TCB ON TCB QUEUE  102
         BNZ   T$CBLOOP                ¬=0; POINT AT NEXT TCB            103
         ST    R9,MODCOUNT             SAVE NUMBER OF MODULES IN LIST    104
*---------------------------------------------------------------------*  105
*************  END OF ACCESS REGISTER MODE CODE ***********************  106
*---------------------------------------------------------------------*  107
*        *---------------------------------------*                       108
*        *     GO INTO PRIMARY SPACE MODE        *                       109
*        *---------------------------------------*                       110
         SAC   0                       GO INTO PRIMARY ASC               111
         SYSSTATE ASCENV=P             SET FOR PRIMARY MACRO EXPANSIONS  112
*        *---------------------------------------*                       113
*        *     PRINT LIST OF MODULE NAMES        *                       114
*        *---------------------------------------*                       115
         LA    R1,MODLIST              LIST OF MODULE NAMES              116
         ICM   R15,15,=V(PRTMOD)       ADDR PRINT MODULE TO R15          117
         BZ    EXIT14                  =0; NOT LINK-EDITED               118
         BASSM R14,R15                 INVOKE ROUTINE                    119
         LTR   R15,R15                 RC = 0 ?                          120
         BNZ   EXIT08                  NO; EXIT RC=8                     121
         B     EXIT00                                                    122
*---------------------------------------------------------------------*  123
*        EXIT ROUTINES                                                *  124
*---------------------------------------------------------------------*  125
EXIT00   DS    0H                      SUCCESSFUL                        126
         LA    R15,X'00'                                                 127
         B     EXIT                                                      128
EXIT08   DS    0H                      TARGET ADDRESS SPACE NAME         129
         LA    R15,X'08'               NOT IN PARMLIST                   130
         B     EXIT                                                      131
EXIT0C   DS    0H                      XMSESA ROUTINE NOT LINK EDITED    132
         LA    R15,X'0C'                                                 133
         B     EXIT                                                      134
EXIT10   DS    0H                      PERCOLATE RETURN AND REASON       135
         PERCRC RC=X'10'               CODE FROM XMSESA ROUTINE          136
         B     EXIT                                                      137
EXIT14   DS    0H                      PRINT ROUTINE NOT LINK EDITED     138
         LA    R15,X'14'                                                 139
         B     EXIT                                                      140
EXIT18   DS    0H                      ERROR IN PRINT ROUTINE            141
         LA    R15,X'18'                                                 142
         B     EXIT                                                      143
*---------------------------------------------------------------------*  144
*        COMMON EXIT                                                  *  145
*---------------------------------------------------------------------*  146
EXIT     LR    R2,R15                  PRESERVE REGISTER 15              147
         MODESET MODE=PROB             GET A PROBLEM STATE PSW           148
         LR    R15,R2                  RESTORE  REGISTER 15              149
         ENDMOD                                                          150
*---------------------------------------------------------------------*  151
*        WORK AREA                                                    *  152
*---------------------------------------------------------------------*  153
WESAMOVE DSECT                                                           154
MODMAX   EQU   500                     MAXIMUM ENTRIES IN MODULE LIST    155
PARMLIST DS    0F,CL(XMSESAL)          PARMLIST FOR XMSESA               156
MODLIST  DS    CL8,F,CL(MODMAX)        LIST OF MODULE NAMES              157
MODCOUNT EQU   MODLIST+8,4             COUNT OF MODULE NAMES IN LIST     158
MODNAMES EQU   MODLIST+12,8            FIRST MODULE NAME IN LIST         159
LESAMOVE EQU   *-WESAMOVE                                                160
*---------------------------------------------------------------------*  161
*        OTHER DSECTS                                                 *  162
*---------------------------------------------------------------------*  163
         XMSESA               MAP USER PARMLIST                          164
         IHAASCB              ADDRESS SPACE CONTROL BLOCK (ASCB)         165
         IHAASXB              ADDRESS SPACE EXTENSION BLOCK (ASXB)       166
         IKJTCB               TASK CONTROL BLOCK (TCB)                   167
         IHACDE               CONTENTS DIRECTORY ENTRY (CDE)             168
         END                                                             169
