*+--------------------------------------------------------------------+    2
*|                      MAINTENANCE LOG                               |    3
*+----------+-------------------------------------+---------+---------+    4
*|  DATE    |       DESCRIPTION                   | CHANGED | REFER   |    5
*+----------+-------------------------------------+---------+---------+    6
*| 12/14/11 | CORRECTED FOR S0C4 ABENDS IN DSPUT  | EDTOBIAS| @001    |    7
*|          | THE WHOLE EXIT ROUTINE WAS CORRECTED|         |         |    7
*|          | WTO CLOBBERS THE RETURN CODE REG15  |         |         |    7
*|          | GIVING BACK DSPUT RC=0 FOR ABNORMAL |         |         |    7
*|          | EXIT FROM DSPFIND. CAUSING S0C4 IN  |         |         |    7
*|          | THE CALLER'S (DSPUT) PROGRAM CODE   |         |         |    7
*+----------+-------------------------------------+---------+---------+    8
*+--------------------------------------------------------------------+    1
*| FUNCTION:                                                          |    2
*|   FINDS THE DSPTBLE CONTOL BLOCK FOR A DATASPACE CREATED BY        |    3
*|   THE DSPACE ROUTINE.  DSPFIND WILL OPTIONALLY BUILD OR DELETE     |    4
*|   AN ACCESS LIST ENTRY ON THE CALLER'S DU-AL OR PS-AL ACCORDING    |    5
*|   TO A FUNCTION CODE SUPPLIED IN THE INPUT PARAMETER LIST.         |    6
*| INPUTS:                                                            |    7
*|   R1  - ADDRESS OF A PARMLIST MAPPED BY DSPTBLE MACRO              |    8
*|         (FOR DELETE REQUEST CALLER MUST SUPPLY ALET IN PARMLIST)   |    9
*| OUTPUTS:                                                           |   10
*|   R15 - RETURN CODE                                                |   11
*|          0 - DATASPACE FOUND / ACCESS LIST ENTRY BUILT             |   12
*|          8 - LPAQ CDE NOT FOUND FOR DSPTBLE BLOCK CHAIN            |   13
*|         12 - DSPTBLE BLOCK FOR THE TARGET DATASPACE NOT ON CHAIN   |   14
*|         16 - ACCESS LIST ENTRY CREATION FAILED                     |   15
*|         20 - ACCESS LIST ENTRY DELETION FAILED                     |   16
*| LOGIC:                                                             |   17
*|    1) ENQUEUE ON THE CHAIN OF DSPTBLE BLOCKS                       |   18
*|    2) CALL LPAQ SEARCH ROUTINE TO FIND ANCHOR FOR DSPTBLE CHAIN    |   19
*|    3) SEARCH FOR DSPTBLE BLOCK FOR TARGET DATASPACE                |   20
*|    4) ACCORDING TO FUNCTION CODE ISSUE ALSERV ADD OR DELETE        |   21
*| ATTRIBUTES:                                                        |   22
*|    AMODE 31, RMODE ANY, REENTRANT, REQUIRES APF OR PKM 0 AUTHORITY |   23
*+--------------------------------------------------------------------+   24
*+--------------------------------------------------------------------+
*|  Copyright (c) 1992, 1993, 2002 by Mitchell Marx & Penelope Davis  |
*|                                                                    |
*|  Address questions to: mmarx@mdpaladin.com                         |
*|                                                                    |
*|  Permission is hereby granted, free of charge, to any person       |
*|  obtaining a copy of this software and associated documentation    |
*|  files (the "Software"), to deal in the Software without           |
*|  restriction, including without limitation the rights to use,      |
*|  copy, modify, merge, publish, distribute, sublicense, and/or sell |
*|  copies of the Software, and to permit persons to whom the         |
*|  Software is furnished to do so, subject to the following          |
*|  conditions:                                                       |
*|                                                                    |
*|  The above copyright notice and this permission notice shall be    |
*|  included in all copies or substantial portions of the Software.   |
*|                                                                    |
*|  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,   |
*|  EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES   |
*|  OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND          |
*|  NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT       |
*|  HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,      |
*|  WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING      |
*|  FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR     |
*|  OTHER DEALINGS IN THE SOFTWARE.                                   |
*+--------------------------------------------------------------------+
         MODULE DSPFIND,BASE=12,LOC=BELOW,AMODE=31,RMODE=ANY,          X  25
               TEXT='FIND STOKEN FOR DATASPACE'                           26
         LR    R3,R1                   ADDRESS OF PARMLIST                27
*        *---------------------------------------*                        28
*        *     ENQUEUE ON TABLE CHAIN            *                        29
*        *---------------------------------------*                        30
         ENQ   (QNAME,DSPTNAME,E,L'DSPTNAME,SYSTEM)                       31
*        *---------------------------------------*                        32
*        *     FIND LPAQ CDE FOR DSPTBLE CHAIN   *                        33
*        *---------------------------------------*                        34
         L     R2,CVTPTR               ADDRESS OF CVT                     35
         LA    R9,DSPTNAME             ADDRESS OF ANCHOR CDE NAME         36
         L     R8,CVTQLPAQ-CVT(R2)     HEAD OF LPAQ                       37
         L     R15,CVTQCDSR-CVT(R2)    ADDRESS OF THE LPAQ SCAN RTN       38
         BALR  R14,R15                 INVOKE LPAQ SCAN ROUTINE           39
         B     FNDCDE                  CDE FOUND                          40
         B     EXIT08                  CDE NOT FOUND                      41
FNDCDE   DS    0H                                                         42
*        *---------------------------------------*                        43
*        *     SEARCH DSPTBLE BLOCKS FOR TARGET  *                        44
*        *---------------------------------------*                        45
         L     R5,CDENTPT-CDENTRY(R11) A(FIRST BLOCK FROM CDE E.P)        46
         USING DSPTBLE,R5              MAP DSPTBLE BLOCK                  47
CKNXTE   DS    0H                                                         48
*                                      ..OWNER NAME IN DSPTBLE SAME AS    49
*                                      ..OWNER NAME IN PARMLIST?          50
         CLC   DSP_OWNER_NAME,DSP_OWNER_NAME-DSPTBLE(R3)                  51
         BNE   CKNXT                   NO; CHECK THE NEXT ENTRY           52
*                                      ..DSPACE NAME IN DSPTBLE SAME AS   53
*                                      ..DSPACE NAME IN PARMLIST?         54
         CLC   DSP_XNAME,DSP_XNAME-DSPTBLE(R3)                            55
         BE    FOUNDIT                 YES; CHECK FUNCTION CODE           56
CKNXT    DS    0H                                                         57
*                                      LAST DSPTBLE BLOCK?                58
         ICM   R5,B'1111',DSP_NEXT_ENTRY                                  59
         BZ    EXIT0C                  YES; EXIT                          60
         B     CKNXTE                  CHECK NEXT DSPTBLE BLOCK           61
FOUNDIT  DS    0H                                                         62
         MVC   0(LDSPTABE,R3),DSPTBLE  COPY BLOCK TO INPUT PARMLIST       63
         DROP  R5                      DROP ADDRESSING ON DSPTBLE         64
         USING DSPTBLE,R3              MAP INPUT PARMLIST                 65
         ST    R5,DSP_TBLENTRY         SAVE DSPTBLE ADDRESS IN PARMLIST   66
         TM    DSP_FUNCTION,X'FF'      ANY FUNCTION CODE?                 67
         BZ    ENDFUNCT                NO; EXIT                           68
         SPKA  0                       GET A KEY ZERO PSW                 69
*        *---------------------------------------*                        70
*        *     CREATE ENTRY IN DU-AL ACCESS LIST *                        71
*        *---------------------------------------*                        72
         TM    DSP_FUNCTION,@GETDUAL   CREATE DU-AL ACCESS LIST ENTRY?    73
         BNE   NOTDUAL                 NO; TEST NEXT CODE                 74
         ALESERV ADD,                  CREATE ACCESS LIST ENTRY        X  75
               STOKEN=DSP_XSTOKEN,     STOKEN FROM COPIED DSPTBLE BLOCKX  76
               ALET=DSP_ALET,          SAVE ALET IN INPUT PARMLIST     X  77
               CHKEAX=NO,              DO NOT CHECK EAX                X  78
               AL=WORKUNIT,            BUILD DU-AL ACCESS LIST ENTRY   X  79
               ACCESS=PUBLIC,          PUBLIC ACCESS LIST ENTRY        X  80
               MF=(E,ALELIST)                                             81
         LTR   R15,R15                 RETURN CODE = 0?                   82
         BNZ   EXIT10                  NO; EXIT PEROLATING ALESERV RC     83
         B     ENDFUNCT                EXIT RC=0                          84
NOTDUAL  DS    0H                                                         85
*        *---------------------------------------*                        86
*        *     CREATE ENTRY IN PS-AL ACCESS LIST *                        87
*        *---------------------------------------*                        88
         TM    DSP_FUNCTION,@GETPSAN   CREATE PS-AL ACCESS LIST ENTRY?    89
         BNE   NOTPSAN                 NO; TEST NEXT FLAG                 90
         ALESERV ADD,                                                  X  91
               STOKEN=DSP_XSTOKEN,     STOKEN FROM COPIED DSPTBLE BLOCKX  92
               ALET=DSP_ALET,          SAVE ALET IN INPUT PARMLIST     X  93
               CHKEAX=NO,              DO NOT CHECK EAX                X  94
               AL=PASN,                BUILD PS-AL ACCESS LIST ENTRY   X  95
               ACCESS=PUBLIC,          PUBLIC ACCESS LIST ENTRY        X  96
               MF=(E,ALELIST)                                             97
         LTR   R15,R15                 RETURN CODE = 0?                   98
         BNZ   EXIT10                  NO; EXIT PEROLATING ALESERV RC     99
         B     ENDFUNCT                EXIT RC=0                         100
NOTPSAN  DS    0H                                                        101
*        *---------------------------------------*                       102
*        *     DELETE ACCESS LIST ENTRY          *                       103
*        *---------------------------------------*                       104
         TM    DSP_FUNCTION,@FREEAL    DELETE ACCESS LIST ENTRY ?        105
         BNE   NOTFREE                 NO; TEST NEXT FLAG                106
         ALESERV DELETE,               DELETE ACCESS LIST ENTRY        X 107
               ALET=DSP_ALET,          CALLER MUST SUPPLY ALET         X 108
               MF=(E,ALELIST)                                            109
         LTR   R15,R15                 RETURN CODE = 0?                  110
         BNZ   EXIT14                  NO; EXIT PEROLATING ALESERV RC    111
         B     ENDFUNCT                EXIT RC=0                         112
NOTFREE  DS    0H                                                        113
ENDFUNCT DS    0H                                                        114
         B     EXIT00                                                    115
*---------------------------------------------------------------------*  116
*        EXIT ROUTINES                                                *  117
*---------------------------------------------------------------------*  118
EXIT00   DS    0H                      SUCCESSFUL                        119
         LA    15,X'00'                                                  120
         B     EXIT                                                      121
EXIT08   DS    0H                      ANCHOR FOR DSPTBLE NOT FOUND      122
         WTO   'DSPFIND: CDE NOT FOUND'                            @001  124
         PERCRC RC=X'08'                                           @001  123
         B     EXIT                                                      125
EXIT0C   DS    0H                      NO DSPTBLE BLOCK FOR DATASPACE    126
         WTO   'DSPFIND: DSPACE ENTRY NOT FOUND'                   @001  128
         LA    15,X'0C'                                            @001  127
         B     EXIT                                                      129
EXIT10   DS    0H        CREATION OF ACCESS LIST ENTRY FAILED            130
         WTO   'DSPFIND: ALESERV CREATE FAILED'                    @001  132
         PERCRC RC=X'10'                                           @001  131
         B     EXIT                                                      133
EXIT14   DS    0H        DELETION OF ACCESS LIST ENTRY FAILED            134
         WTO   'DSPFIND: ALESERV DELETE FAILED'                    @001  136
         PERCRC RC=X'14'                                           @001  135
         B     EXIT                                                      137
*---------------------------------------------------------------------*  138
*        COMMON EXIT                                                  *  139
*---------------------------------------------------------------------*  140
EXIT     DS    0H                                                        141
         LR    R2,R15                  SAVE RETURN CODE                  142
*        *---------------------------------------*                       143
*        *     DEQUEUE ON TABLE CHAIN            *                       144
*        *---------------------------------------*                       145
         DEQ   (QNAME,DSPTNAME,L'DSPTNAME,SYSTEM),RET=HAVE               146
         LR    R15,R2                  RESTORE RETURN CODE               147
         ENDMOD                                                          148
*---------------------------------------------------------------------*  149
*        CONSTANTS                                                    *  150
*---------------------------------------------------------------------*  151
QNAME    DC    CL8'DSPCDE'             ENQ/DEQ MAJOR NAME                152
DSPTNAME DC    CL8'DSPTAB00'           NAME OF CDE THAT ANCHORS DSPTBL   153
*---------------------------------------------------------------------*  154
*        WORK AREA                                                    *  155
*---------------------------------------------------------------------*  156
WDSPFIND DSECT                                                           157
ALELIST  ALESERV MF=L                  ALESERV PARMLIST                  158
LDSPFIND EQU   *-WDSPFIND                                                159
*---------------------------------------------------------------------*  160
*        OTHER DSECTS                                                 *  161
*---------------------------------------------------------------------*  162
         DSPTBLE                       DSPTBLE ENTRY                     163
         IHACDE                        MAP CDE                           164
LCDE     EQU   *-CDENTRY               LENGTH OF CDE                     165
         CVT   DSECT=YES,LIST=NO       MAP CVT                           166
         END                                                             167
