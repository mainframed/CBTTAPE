*PROCESS A,X(S),OPT(2),M,NIS,MAR(2,72,1),LANGLVL(SPROG),N('DELINKI');
     /* PROGRAM TO DELINK OBJECT CODE FROM LOAD MODULES. */
     /* STARTOPT:  */
     /* COMPOPT: OPT(2),M,NIS  */
     /* LINKOPT: XREF,LIST,RENT,REFR  */
     /* ENDOPT:  */
     /* WRITTEN 1984-1989 BY DAVID W. NOON                        */
     /* UPDATED 1991 BY NIGEL FORBES                              */
     /* UPDATED 10APR1999 BY GREG PRICE (GP@P6)                   */
     /*                   - MORE LOAD MODULE ATTRS TO SYSLIN IN   */
     /*                     IEWBLINK (NOT HEWLKED) CONTROL STMTS  */
     /*                   - SUPPORT FOR DDNAME OVERRIDES          */
     /* UPDATED 01MAR2006 BY GREG PRICE (GP@P6)                   */
     /*                   - SUPPORT AMODE(64)                     */
     /*                   - SUPPRESS TXT RECORD IF ZERO BYTES     */
     /*                     (OK FOR HEWLKED, ERROR FOR IEWBLINK)  */
 DELINK :
 PROC (INPARM) OPTIONS(MAIN REENTRANT) REORDER;
0    DFT  RANGE(*)            BIN FIXED;
-    DCL  PLIXOPT             CHAR(22) VAR EXT
                              INIT('ISA(56K) HEAP(1M)');
1    DCL  CARD_PTR            PTR,
          CARD                CHAR(80) BASED(CARD_PTR),
          (MORE_CARDS,MORE_LINKCNTL) BIT(1) INIT('1'B),
          LINKCNTL_PTR        PTR,
          LINKCNTL_REC        CHAR(80) BASED(LINKCNTL_PTR),
          EOM_FLAG            BIT(1) INIT('0'B),
          INPARM              CHAR(100) VAR CONN,
          DDSYSPRT            CHAR(8) INIT('SYSPRINT'),
          DDSYSIN             CHAR(8) INIT('SYSIN   '),
          DDSYSLIN            CHAR(8) INIT('SYSLIN  '),
          DDLNKCTL            CHAR(8) INIT('LINKCNTL'),
          (FULL_STOP,NONAME_FLAG,EXTRACT_FLAG)
                              BIT(1) ALIGNED,
          ERROR_FLAGS         BIT(16) ALIGNED,
          ERROR_MSG(16)       CHAR(41) VAR STATIC INIT(
                              'DUPLICATE MEMBER OPTION',
                              '"MEMBER" NOT FOLLOWED BY "(NAME)"',
                              'MEMBER NAME TOO LONG',
                              'BOTH "NAME" & "NONAME" SPECIFIED',
                              'DUPLICATE NONAME OPTION',
                              'DUPLICATE NAME OPTION',
                              '"NAME" NOT FOLLOWED BY "(NEWNAME)"',
                              'NEW NAME TOO LONG',
                              'DUPLICATE CSECT SELECTION OPTION',
                              'UNRECOGNIZED KEYWORD',
                              'SUPERFLUOUS "("',
                              'EMPTY CSECT SPECIFICATION LIST',
                            'MISSING ")" FROM CSECT SPECIFICATION LIST',
                              'INVALID LIST ITEM',
                              'INVALID CSECT SIZE',
                              'INVALID MASK ITEM'),
          (I,J,K,L,P,MASK_COUNT,CSECT_COUNT,LIST_TYPE_NO)
                              BIN FIXED(15,0),
          (ADCON,LD_ADDR,SD_ADDR,SIZE_VALUE) BIN FIXED(31,0),
          TEMP_CHAR           CHAR(4),
          LIST_TYPE           CHAR(1),
          REUS_ATR            CHAR(6) VAR,
          LOAD_ATR            CHAR(4) VAR,
          (MEMBER,NEW_NAME)   CHAR(8) VAR,
          TOKEN               CHAR(80) VAR,
          NEW_SIZE            CHAR(6),
          DELIMS              CHAR(5) STATIC INIT(' (),.'),
          (RUN_DATE,RUN_TIME) CHAR(8),
          (BEFORE_CNT,AFTER_CNT) DEC FIXED(5,0),
          PAGE_CTR            DEC FIXED(5,0) INIT(0),
          PAGE_TYPE           PIC '9' INIT(0),
          RETURN_CODE         BIN FIXED(31,0) INIT(0B),
-         (HOLD_PTR,CSECT_HEAD_PTR,CSECT_LIST_PTR,MASK_HEAD_PTR,
                    MASK_LIST_PTR)  PTR INIT(NULL),
0         1    CSECT_LIST_ENTRY    BASED(CSECT_LIST_PTR),
               2    CSECT_LIST_SIZE     BIN FIXED(31,0) INIT(0B),
               2    CSECT_NEXT_PTR      PTR INIT(NULL),
               2    CSECT_LIST_NAME     CHAR(8),
0         1    MASK_LIST_ENTRY     BASED(MASK_LIST_PTR),
               2    MASK_NEXT_PTR       PTR INIT(NULL),
               2    MASK_LIST_NAME      CHAR(8),
0         1    CSECT_ARRAY(*)      CTL,
               2    CSECT_SIZE          BIN FIXED(31,0),
               2    CSECT_NAME          CHAR(8),
               2    CSECT_MATCHED       BIT(1),
0         1    MASK_ARRAY(*)       CTL,
               2    MASK_NAME           CHAR(8),
               2    MASK_MATCH_COUNT    BIN FIXED(31,0),
1         1    SYSLIB,
               2    PDS_DDNAME          CHAR(8) INIT('SYSLIB  '),
               2    MEMBER_NAME         CHAR(8) INIT(''),
               2    SYSLIB_WORK(2)      BIN FIXED(31,0) INIT((2)0B),
0         (RECLEN,PDS_RET_CODE) BIN FIXED(31,0),
          LMOD_RECORD         CHAR(32760),
0         DIRECTORY_INFO(19)  BIN FIXED(31,0),
-         (PDS_BLDL_PTR,PDS_BLDL_SCTR_PTR,PDS_BLDL_ALIAS_PTR,
                        PDS_BLDL_SSI_PTR,PDS_BLDL_APF_PTR) PTR,
0         1    PDS2_BLDL_FMT       BASED(PDS_BLDL_PTR),
               2    PDS2NAME            CHAR(8),
               2    PDS2TTRP            BIT(24) ALIGNED,
               2    PDS2CNCT            BIT(8) ALIGNED,
               2    PDS2LIBF            CHAR(1),
               2    PDS2INDC,
                    3    PDS2ALIS            BIT(1),
                    3    PDS2NTTR            BIT(2),
                    3    PDS2LUSR            BIT(5),
0              /* FROM HERE ON IT IS LINKAGE EDITOR USER DATA */
               2    PDS2USRD,
                    3    PDS2TTRT            BIT(24) ALIGNED,
                    3    PDS2ZERO            CHAR(1),
                    3    PDS2TTRN            BIT(24) ALIGNED,
                    3    PDS2NL              BIT(8) ALIGNED,
                    3    PDS2ATR1,
                         4    PDS2RENT            BIT(1),
                         4    PDS2REUS            BIT(1),
                         4    PDS2OVLY            BIT(1),
                         4    PDS2TEST            BIT(1),
                         4    PDS2LOAD            BIT(1),
                         4    PDS2SCTR            BIT(1),
                         4    PDS2EXEC            BIT(1),
                         4    PDS21BLK            BIT(1),
                    3    PDS2ATR2,
                         4    PDS2FLVL            BIT(1),
                         4    PDS2ORG0            BIT(1),
                         4    PDS2EP0             BIT(1),
                         4    PDS2NRLD            BIT(1),
                         4    PDS2NREP            BIT(1),
                         4    PDS2TSTN            BIT(1),
                         4    PDS2LEF             BIT(1),
                         4    PDS2REFR            BIT(1),
                    3    PDS2STOR            BIT(24) ALIGNED,
                    3    PDS2FTBL            BIN FIXED(15,0) UNAL,
                    3    PDS2EPA             BIT(24) ALIGNED,
                    3    PDS2FTB1,
                         4    PDSAOSLE            BIT(1),
                         4    PDS2BIG             BIT(1),
                         4    PDS2PAGA            BIT(1),
                         4    PDS2SSI             BIT(1),
                         4    PDSAPFLG            BIT(1),
                         4    PDS2PGMO            BIT(1),
                         4    FILLER#2            BIT(2),
                    3    PDS2FTB2,
                         4    FILLER#3            BIT(3),
                         4    PDSLRMOD            BIT(1),
                         4    PDSAAMOD            BIT(2),
                         4    PDSMAMOD            BIT(2),
                    3    PDS2FTB3            BIT(8) ALIGNED,
0         /* EXTENSION FOR SCATTER FORMAT LOAD MODULES */
          1    PDSS01              BASED(PDS_BLDL_SCTR_PTR),
               2    PDS2SLSZ            BIN FIXED(15,0) UNAL,
               2    PDS2TTSZ            BIN FIXED(15,0) UNAL,
               2    PDS2ESDT            BIT(16) ALIGNED,
               2    PDS2ESDC            BIT(16) ALIGNED,
0         /* EXTENSION FOR ALIAS ENTRIES */
          1    PDSS02              BASED(PDS_BLDL_ALIAS_PTR),
               2    PDS2EPM             BIT(24) ALIGNED,
               2    PDS2MNM             CHAR(8),
0         /* EXTENSION FOR SSI DATA */
          1    PDSS03              BASED(PDS_BLDL_SSI_PTR),
               2    PDSCHLVL            BIT(8) ALIGNED,
               2    PDSSSIFB,
                    3    FILLER#4            BIT(1),
                    3    PDSFORCE            BIT(1),
                    3    PDSUSRCH            BIT(1),
                    3    PDSEMFIX            BIT(1),
                    3    PDSDEPCH            BIT(1),
                    3    PDSSYSGN,
                         4    PDSCMSGN            BIT(1),
                         4    PDSPTSGN            BIT(1),
                    3    PDSIBMMB            BIT(1),
               2    PDSMBRSN            BIT(16) ALIGNED,
0         /* EXTENSION FOR APF AUTHORIZATION */
          1    PDSS04              BASED(PDS_BLDL_APF_PTR),
               2    PDSAPFCT            BIT(8) ALIGNED,
               2    PDSAPFAC            CHAR((1) REFER (PDSAPFCT)),
          SSI_DATA            CHAR(8),
          (EP_ADDR,EP_OFFSET) BIN FIXED(31,0) INIT(0B),
          EP_ESDID            BIN FIXED(15,0) INIT(0B),
          AMODE               BIT(2),
          SYM_AMODE           BIT(3),                          /* GP */
0         ATTRIB(16,0:1)      CHAR(11) STATIC
                              INIT('NORENT','RENT','NOREUS','REUS',
                              'NOOVLY','OVLY','NOTEST','TEST',
                              'NOOL','OL','BLOCK','SCATTER',
                              'NOEXEC','EXEC','MULTI TXT','ONE TXT',
                              'DOWN COMPAT','NO E LEVEL','NONZERO ORG',
                                   'ZERO ORG',
                              'NONZERO EP','ZERO EP','RLD','NO RLD',
                              'EDIT','NE','NO TESTRAN','TESTRAN',
                              'H LEVEL','F LEVEL','NOREFR','REFR'),
0         RMODE_TYPE(0:1)     CHAR(3) STATIC INIT(' 24','ANY'),
          AMODE_TYPE(0:3)     CHAR(3) STATIC INIT(' 24',' 64',
                              ' 31','ANY'),                    /* GP */
          SYM_AMODE_TYPE(0:7) CHAR(3) STATIC INIT(' 24',' 24',' 31',
                              'ANY',' 64',' 64',' 64',' 64'),  /* GP */
1         1    CESD_LIST_ITEM      BASED(CESD_LIST_PTR),
               2    CESD_NEXT_PTR       PTR INIT(NULL),
               2    CESD_ESDID          BIN FIXED(15,0),
               2    CESD_NEW_ESDID      BIN FIXED(15,0) INIT(0B),
               2    CESD_NAME           CHAR(8),
               2    CESD_TYPE           BIT(8),
               2    CESD_ADDR           BIT(24),
               2    CESD_AMODE_RMODE,
                    3    FILLER#1            BIT(2),
                    3    CESD_RMODE64        BIT(1),           /* GP */
                    3    CESD_AMODE64        BIT(1),           /* GP */
                    3    CESD_RSECT          BIT(1),
                    3    CESD_RMODE          BIT(1),
                    3    CESD_AMODE          BIT(2),
               2    CESD_LENGTH         BIT(24) ALIGNED,
               2    CESD_DELINK_FLAG    BIT(1),
          (CESD_LIST_PTR,CESD_HEAD_PTR) PTR INIT(NULL),
0         1    RLD_LIST_ITEM       BASED(RLD_LIST_PTR),
               2    RLD_NEXT_PTR        PTR INIT(NULL),
               2    RLD_LIST_ITEM_CNT   BIN FIXED(15,0),
               2    RLD_LIST_REL_ID     BIN FIXED(15,0),
               2    RLD_LIST_POS_ID     BIN FIXED(15,0),
               2    RLD_LIST_DATA((RLD_ITEM_CNT)
                              REFER (RLD_LIST_ITEM_CNT)),
                    3    RLD_LIST_FLAGS      BIT(8),
                    3    RLD_LIST_ADCON_ADDR    BIT(24),
          RLD_ITEM_CNT        BIN FIXED(15,0),
          (RLD_LIST_PTR,RLD_HEAD_PTR) PTR INIT(NULL),
0         1    TXT_LIST_ITEM       UNAL BASED(TXT_LIST_PTR),
               2    TXT_NEXT_PTR        PTR INIT(NULL),
               2    TXT_ADDR_1ST_BYTE   BIT(24),
               2    TXT_BYTE_CNT        BIN FIXED(15,0),
               2    TXT_ESDID           BIN FIXED(15,0),
               2    TXT_BYTES           CHAR((TXT_LENGTH)
                                             REFER (TXT_BYTE_CNT)),
          TXT_LENGTH          BIN FIXED(15,0),
          (TXT_SIZE,LAST_TXT_ADDR) BIN FIXED(31,0),
          (TXT_LIST_PTR,TXT_HEAD_PTR) PTR INIT(NULL),
0         1    IDR_LIST_ITEM       BASED(IDR_LIST_PTR),
               2    IDR_NEXT_PTR        PTR INIT(NULL),
               2    IDR_LIST_ESDID      BIN FIXED(15,0),
               2    IDR_LIST_TRANS_CNT  BIN FIXED(15,0),
               2    IDR_DATA((IDR_TRANS_CNT)
                         REFER (IDR_LIST_TRANS_CNT)),
                    3    IDR_NAME            CHAR(10),
                    3    IDR_VVMM(4)         BIT(4) UNAL,
                    3    IDR_DATE            DEC FIXED(5,0),
          IDR_TRANS_CNT       BIN FIXED(15,0),
          (IDR_LIST_PTR,IDR_HEAD_PTR) PTR INIT(NULL),
0         1    IDR_USER_ITEM       BASED(IDR_USER_PTR),
               2    IDR_USER_NEXT_PTR   PTR INIT(NULL),
               2    IDR_USER_LIST_ESDID BIN FIXED(15,0),
               2    IDR_USER_LIST_LENGTH BIN FIXED(15,0),
               2    IDR_USER_LIST_DATA  CHAR((IDR_USER_LENGTH)
                                        REFER (IDR_USER_LIST_LENGTH)),
          IDR_USER_LENGTH     BIN FIXED(15,0) DEF IDR_TRANS_CNT,
          (IDR_USER_PTR,IDR_USER_HEAD_PTR) PTR INIT(NULL),
          SEQ#                DEC FIXED(5,0) INIT(0),
          (TXT_ADDR,TXT_END)  BIN FIXED(31,0),
          TXT_ADDR_BITS       BIT(24),
          (NBYTES,NESD,LAST_ESDID,REPL_ESDID) BIN FIXED(15,0),
1         1    ESD_OBJ             UNAL BASED(CARD_PTR),
               2    OBJ_FLAG            BIT(8) INIT('02'BX),
               2    ESD_ESD             CHAR(3) INIT('ESD'),
               2    FILLER#1            CHAR(6) INIT((6)' '),
               2    ESD_BYTE_CNT        BIN FIXED(15,0) INIT(0B),
               2    FILLER#2            CHAR(2) INIT('  '),
               2    ESD_1ST_ESDID       BIN FIXED(15,0) INIT(0B),
               2    ESD_BYTES(3)        CHAR(16) INIT((3)(16)' '),
               2    FILLER#3            CHAR(8) INIT((8)' '),
               2    ESD_MOD_ID          CHAR(4)
                                        INIT(SUBSTR(CESD_NAME,1,4)),
               2    ESD_SEQ#            PIC '(4)9' INIT(SEQ#),
0         1    ESD_DATA            BASED(ESD_PTR),
               2    ESD_NAME            CHAR(8),
               2    ESD_TYPE            BIT(8),
               2    ESD_ADDR            BIT(24),
               2    ESD_AMODE_RMODE     BIT(8),
               2    ESD_LENGTH          BIT(24),
0         ESD_PTR             PTR INIT(NULL),
-         1    TXT_OBJ             UNAL BASED(CARD_PTR),
               2    OBJ_FLAG            BIT(8) INIT('02'BX),
               2    TXT_TXT             CHAR(3) INIT('TXT'),
               2    FILLER#1            CHAR(1) INIT(' '),
               2    TXT_1ST_BYTE        BIT(24)
                                        INIT(TXT_ADDR_BITS),
               2    FILLER#2            CHAR(2) INIT('  '),
               2    TXT_CNT             BIN FIXED(15,0) INIT(56),
               2    FILLER#3            CHAR(2) INIT('  '),
               2    TXT_OBJ_ESDID       BIN FIXED(15,0) INIT(1B),
               2    TXT                 CHAR(56) INIT((56)' '),
               2    TXT_MOD_ID          CHAR(4)
                                        INIT(SUBSTR(CESD_NAME,1,4)),
               2    TXT_SEQ#            PIC '(4)9' INIT(SEQ#),
-         1    TXT_OBJ_NULL        UNAL BASED(CARD_PTR),
               2    OBJ_FLAG            BIT(8) INIT('02'BX),
               2    TXT_TXT             CHAR(3) INIT('TXT'),
               2    FILLER#1            CHAR(1) INIT(' '),
               2    TXT_1ST_BYTE        BIT(24)
                                        INIT(TXT_ADDR_BITS),
               2    FILLER#2            CHAR(2) INIT('  '),
               2    TXT_CNT_NULL        BIN FIXED(15,0) INIT(0B),
               2    FILLER#3            CHAR(2) INIT('  '),
               2    TXT_OBJ_ESDID       BIN FIXED(15,0) INIT(1B),
               2    TXT_NULL            CHAR(56) INIT((56)' '),
               2    TXT_MOD_ID          CHAR(4)
                                        INIT(SUBSTR(CESD_NAME,1,4)),
               2    TXT_SEQ#            PIC '(4)9' INIT(SEQ#),
-         1    RLD_OBJ             UNAL BASED(CARD_PTR),
               2    OBJ_FLAG            BIT(8) INIT('02'BX),
               2    RLD_RLD             CHAR(3) INIT('RLD'),
               2    FILLER#1            CHAR(6) INIT((6)' '),
               2    RLD_BYTE_CNT        BIN FIXED(15,0) INIT(0B),
               2    FILLER#2            CHAR(4) INIT((4)' '),
               2    RLD_BYTE(56)        CHAR(1) INIT((56)(' ')),
               2    RLD_MOD_ID          CHAR(4)
                                        INIT(SUBSTR(CESD_NAME,1,4)),
               2    RLD_SEQ#            PIC '(4)9' INIT(SEQ#),
0         1    RLD_R_P             UNAL BASED(RLD_R_P_PTR),
               2    RLD_OBJ_REL_ID      BIN FIXED(15,0),
               2    RLD_OBJ_POS_ID      BIN FIXED(15,0),
0         1    RLD_F_A             UNAL BASED(RLD_F_A_PTR),
               2    RLD_OBJ_FLAGS       BIT(8),
               2    RLD_OBJ_ADDR        BIT(24),
0         (RLD_R_P_PTR,RLD_F_A_PTR) PTR INIT(NULL),
-         1    END_OBJ_TYPE_1      UNAL BASED(CARD_PTR),
               2    OBJ_FLAG            BIT(8) INIT('02'BX),
               2    END_END             CHAR(3) INIT('END'),
               2    FILLER#1            CHAR(1) INIT(' '),
               2    END_EP_OFFSET       BIT(24)
                                   INIT(SUBSTR(UNSPEC(EP_OFFSET),9,24)),
               2    FILLER#2            CHAR(6) INIT((6)' '),
               2    END_EP_ESDID        BIN FIXED(15,0) INIT(EP_ESDID),
               2    FILLER#3            CHAR(56) INIT((56)' '),
               2    END_MOD_ID          CHAR(4)
                                        INIT(SUBSTR(CESD_NAME,1,4)),
               2    END_SEQ#            PIC '(4)9' INIT(SEQ#),
0         1    END_OBJ_TYPE_2      UNAL BASED(CARD_PTR),
               2    OBJ_FLAG            BIT(8) INIT('02'BX),
               2    END_END             CHAR(3) INIT('END'),
               2    FILLER#1            CHAR(12) INIT((12)' '),
               2    END_EP_NAME         CHAR(8) INIT((8)' '),
               2    FILLER#2            CHAR(4) INIT((4)' '),
               2    END_SD_LENGTH       BIT(32) INIT((4)'40'BX),
               2    END_IDR_CNT         CHAR(1) INIT(' '),
               2    END_IDR_DATA(2),
                    3    IDR_NAME            CHAR(10) INIT((2)(10)' '),
                    3    END_IDR_VVMM        CHAR(4) INIT((2)('    ')),
                    3    IDR_DATE            PIC '(5)Z' INIT(0,0),
               2    FILLER#3            CHAR(1) INIT(' '),
               2    END_MOD_ID          CHAR(4)
                                        INIT(SUBSTR(CESD_NAME,1,4)),
               2    END_SEQ#            PIC '(4)9' INIT(SEQ#),
1         DWNSCAN             ENTRY(CHAR(*),CHAR(*))
                              RETURNS(BIN FIXED(15,0)),
          DWNSHEX             ENTRY(BIT(*) ALIGNED)
                              RETURNS(CHAR(16) VAR),
          DWNSPDSR            ENTRY OPTIONS(ASM INTER RETCODE),
          (ADDR,ALL,ALLOCN,DATE,HIGH,INDEX,LENGTH,LOW,MAX,MIN,MOD,NULL,
                    PLIDUMP,PLIRETC,PLIRETV,PTRADD,PTRVALUE,
                    STG,STRING,SUBSTR,TIME,UNSPEC,VERIFY) BUILTIN,
-         SYSIN               FILE RECORD INPUT ENV(FB RECSIZE(80)),
          SYSLIN              FILE RECORD OUTPUT
                              ENV(FB RECSIZE(80) TOTAL),
          LINKCNTL            FILE RECORD INPUT INT ENV(FB RECSIZE(80)),
          SYSPRINT            PRINT ENV(VB RECSIZE(137) BLKSIZE(6854));
1    ON ERROR SNAP
     BEGIN;
          ON ERROR SYSTEM;
          CALL PLIDUMP('TFCHB');
     END;
0    PDS_BLDL_PTR = ADDR(DIRECTORY_INFO);
-    IF LENGTH(INPARM) = 40 THEN /* PROCESS DDNAME OVERRIDES */ /*GP*/
     DO;                                                        /*  */
          SYSLIB.PDS_DDNAME = SUBSTR(INPARM,1,8);               /*  */
          DDSYSIN  = SUBSTR(INPARM,9,8);                        /*  */
          DDSYSLIN = SUBSTR(INPARM,17,8);                       /*  */
          DDLNKCTL = SUBSTR(INPARM,25,8);                       /*  */
          DDSYSPRT = SUBSTR(INPARM,33,8);                       /*  */
     END;                                                       /*GP*/
-    OPEN FILE(SYSIN) TITLE(DDSYSIN),
          FILE(SYSLIN) TITLE(DDSYSLIN),
          FILE(LINKCNTL) TITLE(DDLNKCTL),
          FILE(SYSPRINT) TITLE(DDSYSPRT) PAGESIZE(65);
0    ON ENDFILE(LINKCNTL)
          MORE_LINKCNTL = '0'B;
     DO WHILE(MORE_LINKCNTL)
               UNTIL(SUBSTR(LINKCNTL_REC,1,8) = 'MEMBER: ');
          READ FILE(LINKCNTL) SET(LINKCNTL_PTR);
     END;
0    PUT STRING(RUN_DATE) EDIT
     (DATE)
     (P'99/99/99');
0    PUT STRING(RUN_TIME) EDIT
     (SUBSTR(TIME,1,6))
     (P'99/99/99');
     SUBSTR(RUN_TIME,3,1), SUBSTR(RUN_TIME,6,1) = ':';
0    ON ENDPAGE(SYSPRINT)
     BEGIN;
          DCL  LINENO              BUILTIN;
          PAGE_CTR = PAGE_CTR + 1;
          IF LINENO(SYSPRINT) >= 2 THEN
               PUT FILE(SYSPRINT) PAGE;
          PUT FILE(SYSPRINT) LINE(2) EDIT
          (RUN_DATE,RUN_TIME,'DELINK UTILITY','PAGE',PAGE_CTR)
          (A,X(1),A,COL(60),A,COL(124),A,F(5));
          SELECT(PAGE_TYPE);
          WHEN(0)
               PUT FILE(SYSPRINT) SKIP(2);
          WHEN(1)
               PUT FILE(SYSPRINT) SKIP(2) EDIT
               ('<',(32)'-',' COMMAND LINE ',(32)'-','>')
               (COL(27),5 A);
          WHEN(2)
               PUT FILE(SYSPRINT) SKIP(2) EDIT
               (('CSECT','A/R','OLD','NEW' DO I = 1 TO 3))
               (X(2),4(A,X(6)))
               (('NAME','MODE','SIZE','SIZE' DO I = 1 TO 3))
               (SKIP,3(X(2),A,2(X(6),A),X(5),A,X(5)));
          END;
     END;
-OBTAIN_A_CMD :
     ON ENDFILE(SYSIN)
          GO TO END_OF_JOB;
     READ FILE(SYSIN) SET(CARD_PTR);
     ON ENDFILE(SYSIN)
          MORE_CARDS = '0'B;
     PAGE_TYPE = 1;
0    SIGNAL ENDPAGE(SYSPRINT);
0    MEMBER,NEW_NAME = '';
     MASK_COUNT,CSECT_COUNT = 0B;
     BEFORE_CNT,AFTER_CNT = 0;
     LIST_TYPE_NO = 1B;
     LIST_TYPE = ' ';
     FULL_STOP,NONAME_FLAG,EXTRACT_FLAG = '0'B;
     ERROR_FLAGS = (16)'0'B;
1    DO WHILE(MORE_CARDS);
          PUT FILE(SYSPRINT) EDIT
          (CARD)
          (COL(27),A);
          IF CARD ¬= (80)' ' THEN
          DO P = VERIFY(CARD,' ') REPEAT P + L
                    WHILE(P <= 80);
               IF (LIST_TYPE = 'X' | LIST_TYPE = 'S') &
                         SUBSTR(CARD,P,1) = '(' &
                         (TOKEN = 'EXCLUDE' | TOKEN = 'SELECT') THEN
                    L = 1;
               ELSE
               DO;
                    L = DWNSCAN(SUBSTR(CARD,P+1),DELIMS);
                    IF L = 0 THEN
                         L = 81 - P;
               END;
               TOKEN = SUBSTR(CARD,P,L);
               SELECT(LIST_TYPE);
               WHEN(' ')
               SELECT(TOKEN);
               WHEN('MEMBER')
               DO;
                    IF MEMBER ¬= '' THEN
                         SUBSTR(ERROR_FLAGS,1,1) = '1'B;
                    ELSE IF SUBSTR(CARD,P+6,1) ¬= '(' THEN
                         SUBSTR(ERROR_FLAGS,2,1) = '1'B;
                    ELSE
                    DO;
                         L = DWNSCAN(SUBSTR(CARD,P+7),DELIMS);
                         IF L < 2 | L > 9 |
                                   SUBSTR(CARD,P+L+6,1) ¬= ')' THEN
                              SUBSTR(ERROR_FLAGS,3,1) = '1'B;
                         ELSE
                              MEMBER = SUBSTR(CARD,P+7,L-1);
                         L = L + 7;
                    END;
               END;
               WHEN('NONAME')
               DO;
                    IF NEW_NAME ¬= '' THEN
                         SUBSTR(ERROR_FLAGS,4,1) = '1'B;
                    ELSE IF NONAME_FLAG THEN
                         SUBSTR(ERROR_FLAGS,5,1) = '1'B;
                    ELSE
                         NONAME_FLAG = '1'B;
               END;
               WHEN('NAME')
               DO;
                    IF NEW_NAME ¬= '' THEN
                         SUBSTR(ERROR_FLAGS,6,1) = '1'B;
                    ELSE IF NONAME_FLAG THEN
                         SUBSTR(ERROR_FLAGS,4,1) = '1'B;
                    ELSE IF SUBSTR(CARD,P+4,1) ¬= '(' THEN
                         SUBSTR(ERROR_FLAGS,7,1) = '1'B;
                    ELSE
                    DO;
                         L = DWNSCAN(SUBSTR(CARD,P+5),DELIMS);
                         IF L < 2 | L > 9 |
                                   SUBSTR(CARD,P+L+4,1) ¬= ')' THEN
                              SUBSTR(ERROR_FLAGS,8,1) = '1'B;
                         ELSE
                              NEW_NAME = SUBSTR(CARD,P+5,L-1);
                         L = L + 5;
                    END;
               END;
               WHEN('ALL','EXCLUDE','SELECT')
               DO;
                    IF CSECT_COUNT ¬= 0 | MASK_COUNT ¬= 0 |
                              LIST_TYPE_NO > 1B THEN
                         SUBSTR(ERROR_FLAGS,9,1) = '1'B;
                    ELSE
                    DO;
                         LIST_TYPE = SUBSTR(TOKEN,1,1);
                         IF LIST_TYPE = 'E' THEN
                              LIST_TYPE = 'X';
                         LIST_TYPE_NO = INDEX('AXS',LIST_TYPE);
                    END;
               END;
               WHEN('EPCSECT','MAP','NOMAP')
               DO;
                    IF CSECT_COUNT ¬= 0 | MASK_COUNT ¬= 0 |
                              LIST_TYPE_NO > 1B THEN
                         SUBSTR(ERROR_FLAGS,9,1) = '1'B;
                    ELSE
                         LIST_TYPE_NO = INDEX('EMN',SUBSTR(TOKEN,1,1)) +
                                        3;
               END;
               WHEN('.')
                    FULL_STOP = '1'B;
               OTHERWISE
                    SUBSTR(ERROR_FLAGS,10,1) = '1'B;
               END; /* SELECT(TOKEN) */
1              WHEN('A')
               SELECT;
               WHEN(TOKEN = '(')
               DO;
                    IF CSECT_COUNT ¬= 0B THEN
                         SUBSTR(ERROR_FLAGS,11,1) = '1'B;
               END;
               WHEN(TOKEN = ')')
               DO;
                    IF CSECT_COUNT = 0B THEN
                         SUBSTR(ERROR_FLAGS,12,1) = '1'B;
                    LIST_TYPE = ' ';
               END;
               WHEN(TOKEN = '.')
               DO;
                    IF CSECT_COUNT ¬= 0B THEN
                         SUBSTR(ERROR_FLAGS,13,1) = '1'B;
                    LIST_TYPE = ' ';
                    FULL_STOP = '1'B;
               END;
               WHEN(SUBSTR(TOKEN,1,1) ¬= '(' |
 /*NIGEL */              SUBSTR(CARD,P+L,1) ¬= ' ')
                    SUBSTR(ERROR_FLAGS,14,1) = '1'B;
               OTHERWISE
               DO;
                    K = DWNSCAN(SUBSTR(CARD,P+L+1),DELIMS);
                    IF K < 2 | SUBSTR(CARD,P+L+K,1) ¬= ')' |
 /*NIGEL */                        SUBSTR(CARD,P+L,1) ¬= ' ' THEN
                         SUBSTR(ERROR_FLAGS,14,1) = '1'B;
                    ELSE
                    DO;
                         SIZE_VALUE =
                              EXTRACT_SIZE(SUBSTR(CARD,P+L+1,K-1),
                              EXTRACT_FLAG);
                         SUBSTR(ERROR_FLAGS,15,1) =
                              SUBSTR(ERROR_FLAGS,15,1) | EXTRACT_FLAG;
                         IF ERROR_FLAGS = (16)'0'B THEN
                         DO;
                              CSECT_COUNT = CSECT_COUNT + 1;
                              ALLOC CSECT_LIST_ENTRY SET(HOLD_PTR);
                              IF CSECT_HEAD_PTR = NULL THEN
                                   CSECT_HEAD_PTR = HOLD_PTR;
                              ELSE
                                   CSECT_NEXT_PTR = HOLD_PTR;
                              CSECT_LIST_PTR = HOLD_PTR;
                              CSECT_LIST_SIZE = SIZE_VALUE;
                              CSECT_LIST_NAME = SUBSTR(TOKEN,2);
                         END;
                    END;
                    L = L + K + 1;
 /*NIGEL */         IF SUBSTR(CARD,P+L,1) = ' ' THEN
                         L = L + 1;
               END;
               END; /* SELECT */
1              WHEN('X') /* EXCLUDE LIST */
               SELECT;
               WHEN(TOKEN = '(')
               DO;
                    IF CSECT_COUNT ¬= 0B THEN
                         SUBSTR(ERROR_FLAGS,11,1) = '1'B;
               END;
               WHEN(TOKEN = ')')
               DO;
                    IF CSECT_COUNT = 0B & MASK_COUNT = 0B THEN
                         SUBSTR(ERROR_FLAGS,12,1) = '1'B;
                    LIST_TYPE = ' ';
               END;
               WHEN(TOKEN = '.')
               DO;
                    LIST_TYPE = ' ';
                    SUBSTR(ERROR_FLAGS,13,1),FULL_STOP = '1'B;
               END;
               WHEN(TOKEN = 'MASK' & SUBSTR(CARD,P+4,1) = '(')
               DO;
                    L = DWNSCAN(SUBSTR(CARD,P+5),DELIMS);
                    IF L < 2 | L > 9 |
                              SUBSTR(CARD,P+L+4,1) ¬= ')' |
                              (SUBSTR(CARD,P+L+5,1) ¬= ')' &
 /*NIGEL */                   SUBSTR(CARD,P+L+5,1) ¬= ' ') THEN
                         SUBSTR(ERROR_FLAGS,16,1) = '1'B;
                    ELSE
                    DO;
                         MASK_COUNT = MASK_COUNT + 1;
                         ALLOC MASK_LIST_ENTRY SET(HOLD_PTR);
                         IF MASK_HEAD_PTR = NULL THEN
                              MASK_HEAD_PTR = HOLD_PTR;
                         ELSE
                              MASK_NEXT_PTR = HOLD_PTR;
                         MASK_LIST_PTR = HOLD_PTR;
                         MASK_LIST_NAME = SUBSTR(CARD,P+5,L-1);
                    END;
                    L = L + 5;
 /*NIGEL */         IF SUBSTR(CARD,P+L,1) = ' ' THEN
                         L = L + 1;
               END;
0              OTHERWISE
               DO;
                    IF L < 1 | L > 8 |
                              (SUBSTR(CARD,P+L,1) ¬= ')' &
  /*NIGEL FIX , TAKEN OUT*/   SUBSTR(CARD,P+L,1) ¬= ' ') THEN
                         SUBSTR(ERROR_FLAGS,14,1) = '1'B;
                    ELSE
                    DO;
                         CSECT_COUNT = CSECT_COUNT + 1;
                         ALLOC CSECT_LIST_ENTRY SET(HOLD_PTR);
                         IF CSECT_HEAD_PTR = NULL THEN
                              CSECT_HEAD_PTR = HOLD_PTR;
                         ELSE
                              CSECT_NEXT_PTR = HOLD_PTR;
                         CSECT_LIST_PTR = HOLD_PTR;
                         CSECT_LIST_NAME = SUBSTR(CARD,P,L);
                         CSECT_LIST_SIZE = 0B;
                    END;
 /*NIGEL */         IF SUBSTR(CARD,P+L,1) = ' ' THEN
                         L = L + 1;
               END;
               END; /* SELECT */
1              WHEN('S') /* SELECT LIST */
               SELECT;
               WHEN(TOKEN = '(')
               DO;
                    IF CSECT_COUNT ¬= 0B THEN
                         SUBSTR(ERROR_FLAGS,11,1) = '1'B;
               END;
               WHEN(TOKEN = ')')
               DO;
                    IF CSECT_COUNT = 0B & MASK_COUNT = 0B THEN
                         SUBSTR(ERROR_FLAGS,12,1) = '1'B;
                    LIST_TYPE = ' ';
               END;
               WHEN(TOKEN = '.')
               DO;
                    LIST_TYPE = ' ';
                    SUBSTR(ERROR_FLAGS,13,1),FULL_STOP = '1'B;
               END;
               WHEN(TOKEN = 'MASK' & SUBSTR(CARD,P+4,1) = '(')
               DO;
                    L = DWNSCAN(SUBSTR(CARD,P+5),DELIMS);
                    IF L < 2 | L > 9 |
                              SUBSTR(CARD,P+L+4,1) ¬= ')' |
                              (SUBSTR(CARD,P+L+5,1) ¬= ')' &
 /*NIGEL */                   SUBSTR(CARD,P+L+5,1) ¬= ' ') THEN
                         SUBSTR(ERROR_FLAGS,16,1) = '1'B;
                    ELSE
                    DO;
                         MASK_COUNT = MASK_COUNT + 1;
                         ALLOC MASK_LIST_ENTRY SET(HOLD_PTR);
                         IF MASK_HEAD_PTR = NULL THEN
                              MASK_HEAD_PTR = HOLD_PTR;
                         ELSE
                              MASK_NEXT_PTR = HOLD_PTR;
                         MASK_LIST_PTR = HOLD_PTR;
                         MASK_LIST_NAME = SUBSTR(CARD,P+5,L-1);
                    END;
                    L = L + 5;
 /*NIGEL */         IF SUBSTR(CARD,P+L,1) = ' ' THEN
                         L = L + 1;
               END;
0              WHEN(SUBSTR(TOKEN,1,1) = '(')
               DO;
                    K = DWNSCAN(SUBSTR(CARD,P+L+1),DELIMS);
                    IF K < 2 | SUBSTR(CARD,P+L+K,1) ¬= ')' |
 /*NIGEL */                        SUBSTR(CARD,P+L,1) ¬= ' ' THEN
                         SUBSTR(ERROR_FLAGS,14,1) = '1'B;
                    ELSE
                    DO;
                         SIZE_VALUE =
                              EXTRACT_SIZE(SUBSTR(CARD,P+L+1,K-1),
                              EXTRACT_FLAG);
                         SUBSTR(ERROR_FLAGS,15,1) =
                              SUBSTR(ERROR_FLAGS,15,1) | EXTRACT_FLAG;
                         IF ERROR_FLAGS = (16)'0'B THEN
                         DO;
                              CSECT_COUNT = CSECT_COUNT + 1;
                              ALLOC CSECT_LIST_ENTRY SET(HOLD_PTR);
                              IF CSECT_HEAD_PTR = NULL THEN
                                   CSECT_HEAD_PTR = HOLD_PTR;
                              ELSE
                                   CSECT_NEXT_PTR = HOLD_PTR;
                              CSECT_LIST_PTR = HOLD_PTR;
                              CSECT_LIST_SIZE = SIZE_VALUE;
                              CSECT_LIST_NAME = SUBSTR(TOKEN,2);
                         END;
                    END;
                    L = L + K + 1;
 /*NIGEL */         IF SUBSTR(CARD,P+L,1) = ' ' THEN
                         L = L + 1;
               END;
0              OTHERWISE
               DO;
                    IF L < 1 | L > 8 |
                              (SUBSTR(CARD,P+L,1) ¬= ')' &
 /*NIGEL */                   SUBSTR(CARD,P+L,1) ¬= ' ') THEN
                         SUBSTR(ERROR_FLAGS,14,1) = '1'B;
                    ELSE
                    DO;
                         CSECT_COUNT = CSECT_COUNT + 1;
                         ALLOC CSECT_LIST_ENTRY SET(HOLD_PTR);
                         IF CSECT_HEAD_PTR = NULL THEN
                              CSECT_HEAD_PTR = HOLD_PTR;
                         ELSE
                              CSECT_NEXT_PTR = HOLD_PTR;
                         CSECT_LIST_PTR = HOLD_PTR;
                         CSECT_LIST_NAME = SUBSTR(CARD,P,L);
                         CSECT_LIST_SIZE = 0B;
                    END;
 /*NIGEL */         IF SUBSTR(CARD,P+L,1) = ' ' THEN
                         L = L + 1;
               END;
               END; /* SELECT */
               OTHERWISE
                    CALL PLIDUMP('TFSHB',
                         'LOGIC ERROR - LIST_TYPE = '||LIST_TYPE);
               END; /* SELECT(LIST_TYPE) */
               IF SUBSTR(CARD,P+L,1) = ' ' THEN
               DO;
                    K = VERIFY(SUBSTR(CARD,P+L+1),' ');
                    IF K = 0 THEN
                         L= 81 - P;
                    ELSE
                         L = L + K;
               END;
          END; /* DO P = REPEAT WHILE(P <= 80) */
          IF FULL_STOP THEN
               LEAVE;
          READ FILE(SYSIN) SET(CARD_PTR);
     END; /* DO WHILE(MORE_CARDS) */
1    IF ERROR_FLAGS ¬= (16)'0'B | ¬ FULL_STOP | MEMBER = '' THEN
     DO;
          PUT FILE(SYSPRINT) EDIT
          ('** ERROR ** STATEMENT ABOVE IGNORED.')
          (SKIP,A)
          (((ERROR_MSG(I),'.' DO J = 1 WHILE(SUBSTR(ERROR_FLAGS,I,1)))
                    DO I = 1 TO 16))
          (SKIP,2 A);
          DO I = 1 TO CSECT_COUNT;
               CSECT_LIST_PTR = CSECT_HEAD_PTR;
               CSECT_HEAD_PTR = CSECT_NEXT_PTR;
               FREE CSECT_LIST_ENTRY;
          END;
          DO I = 1 TO MASK_COUNT;
               MASK_LIST_PTR = MASK_HEAD_PTR;
               MASK_HEAD_PTR = MASK_NEXT_PTR;
               FREE MASK_LIST_ENTRY;
          END;
          RETURN_CODE = MAX(RETURN_CODE,12);
          IF MORE_CARDS THEN
               GO TO OBTAIN_A_CMD;
          GO TO END_OF_JOB;
     END;
0    IF CSECT_COUNT > 0 THEN
     DO;
          ALLOC CSECT_ARRAY(CSECT_COUNT);
          DO I = 1 TO CSECT_COUNT;
               CSECT_LIST_PTR = CSECT_HEAD_PTR;
               CSECT_NAME(I) = CSECT_LIST_NAME;
               CSECT_SIZE(I) = CSECT_LIST_SIZE;
               CSECT_MATCHED(I) = '0'B;
               CSECT_HEAD_PTR = CSECT_NEXT_PTR;
               FREE CSECT_LIST_ENTRY;
          END;
     END;
0    IF MASK_COUNT > 0 THEN
     DO;
          ALLOC MASK_ARRAY(MASK_COUNT);
          DO I = 1 TO MASK_COUNT;
               MASK_LIST_PTR = MASK_HEAD_PTR;
               MASK_NAME(I) = MASK_LIST_NAME;
               MASK_MATCH_COUNT(I) = 0B;
               MASK_HEAD_PTR = MASK_NEXT_PTR;
               FREE MASK_LIST_ENTRY;
          END;
     END;
0    IF NEW_NAME = '' THEN
          NEW_NAME = MEMBER;
1    /* WE HAVE NOW PARSED THE USER'S INPUT AND FOUND IT TO BE
          SATISFACTORY. WE WILL NOW HAVE A GO AT DELINKING THE LOAD
          MODULE AS REQUESTED BY THE USER. */
     IF MEMBER_NAME = MEMBER THEN
     DO;
          MEMBER_NAME = HIGH(8);
          CALL DWNSPDSR(SYSLIB,PDS_RET_CODE,RECLEN,LMOD_RECORD);
     END;
0    MEMBER_NAME = MEMBER;
     RECLEN = STG(LMOD_RECORD);
     CALL DWNSPDSR(SYSLIB,PDS_RET_CODE,RECLEN,LMOD_RECORD,
               SSI_DATA,DIRECTORY_INFO);
0    PAGE_TYPE = 0;
     IF PLIRETV = 0B THEN
     DO;
          PUT FILE(SYSPRINT) SKIP(2) EDIT
          ('MEMBER ',MEMBER,' FOUND.')
          (A);
          IF PDS2SCTR THEN /* IT'S SCATTER FORMAT */
          DO;
               PDS_BLDL_SCTR_PTR = PTRADD(ADDR(PDS2FTB3),1);
               PDS_BLDL_ALIAS_PTR =
                    PTRADD(ADDR(PDS2FTB3),STG(PDSS01)+1);
          END;
          ELSE /* IT'S BLOCK FORMAT */
          DO;
               PDS_BLDL_SCTR_PTR = NULL;
               PDS_BLDL_ALIAS_PTR = PTRADD(ADDR(PDS2FTB3),1);
          END;
          IF PDS2ALIS THEN
          DO;
               L = INDEX(PDS2MNM,' ');
               IF L = 0B THEN
                    L = 8;
               PUT FILE(SYSPRINT) EDIT
               (' THIS IS AN ALIAS FOR MEMBER ',SUBSTR(PDS2MNM,1,L),
                         '.')
               (A);
               AMODE = PDSAAMOD;
          END;
          ELSE
          DO;
               PDS_BLDL_ALIAS_PTR = NULL;
               AMODE = PDSMAMOD;
          END;
          UNSPEC(EP_ADDR) = (8)'0'B || PDS2EPA;
          EP_ESDID = 0B;
          IF PDS2SSI THEN
          DO;
               PUT FILE(SYSPRINT) EDIT
               (' SSI = "',SSI_DATA,'".')
               (A);
 /*GP*/        IF PDS2ALIS
 /*  */        THEN
 /*  */             PDS_BLDL_SSI_PTR = PTRADD(PDS_BLDL_ALIAS_PTR,11);
 /*  */        ELSE              /* ENSURE HALFWORD ALIGNMENT */
 /*  */             PDS_BLDL_SSI_PTR = PTRADD(ADDR(PDS2FTB3),2);
 /*  */        IF PDS2SCTR THEN  /* ALLOW FOR SCATTER SECTION */
 /*GP*/             PDS_BLDL_SSI_PTR = PTRADD(PDS_BLDL_SSI_PTR,8);
          END;
          ELSE
          DO;
               PUT FILE(SYSPRINT) EDIT
               (' NO SSI DATA.')
               (A);
               PDS_BLDL_SSI_PTR = NULL;
          END;
          IF PDS2PAGA THEN
               PUT FILE(SYSPRINT) EDIT
               (' PAGE ALIGNMENT REQUIRED.')
               (A);
          IF PDSAPFLG THEN
          DO;
               PDS_BLDL_APF_PTR = PTRADD(ADDR(PDS2FTB3),1);
               IF PDS2SCTR THEN /* ALLOW FOR SCATTER TABLE */
                    PDS_BLDL_APF_PTR =
                         PTRADD(PDS_BLDL_APF_PTR,STG(PDSS01));
               IF PDS2ALIS THEN /* ALLOW FOR ALIAS INFO. */
                    PDS_BLDL_APF_PTR =
                         PTRADD(PDS_BLDL_APF_PTR,STG(PDSS02));
               IF PDS2SSI THEN /* ALLOW FOR SSI BYTES */
                    PDS_BLDL_APF_PTR =
                         PTRADD(PDS_BLDL_SSI_PTR,STG(PDSS03));   /*GP*/
               PUT FILE(SYSPRINT) EDIT
               (' AUTHORIZATION CODE IS ',DWNSHEX(UNSPEC(PDSAPFAC)),'.')
               (A);
          END;
          PUT FILE(SYSPRINT) SKIP(2) EDIT
          ('LOAD MODULE ATTRIBUTES :',
               'ENTRY POINT : X''',DWNSHEX(PDS2EPA),'''',
               'STORAGE REQUIRED : X''',DWNSHEX(PDS2STOR),'''')
          (A,SKIP,COL(21),3 A,COL(51),3 A)
          ((ATTRIB(I,SUBSTR(STRING(PDS2ATR1)||STRING(PDS2ATR2),I,1))
               DO I = 1 TO 16))
          (SKIP,X(10),8 A(15))
          ('AMODE : ',AMODE_TYPE(AMODE),
               'RMODE : ',RMODE_TYPE(PDSLRMOD))
          (SKIP,X(20),2 A,COL(41),2 A);
          REUS_ATR = 'NONE';
 /* GP */ IF PDS2REUS THEN REUS_ATR = 'SERIAL';
 /*    */ IF PDS2RENT THEN REUS_ATR = 'RENT';
 /*    */ IF PDS2REFR THEN REUS_ATR = 'REFR';
 /*    */ IF PDS2LOAD THEN LOAD_ATR = 'OL';
 /* GP */             ELSE LOAD_ATR = 'NOOL';
     END;
     EOM_FLAG = '0'B;
-    DO WHILE(PLIRETV = 0B) UNTIL(EOM_FLAG);
0         SELECT(SUBSTR(LMOD_RECORD,1,1));
-         WHEN('40'X) /* SYM RECORD */
          DO;
          END;
-         WHEN('10'X) /* SCATTER/TRANSLATION RECORD */
          DO;
          END;
1         WHEN('20'X) /* CESD RECORD */
          BEGIN;
               DCL  1    CESD_LMOD           UNAL
                                             BASED(ADDR(LMOD_RECORD)),
                         2    REC_ID              BIT(8),
                         2    FLAG_BYTE           BIT(8),
                         2    FILLER              BIN FIXED(15,0),
                         2    CESD_1ST_ESDID      BIN FIXED(15,0),
                         2    CESD_BYTE_CNT       BIN FIXED(15,0),
                         2    CESD_DATA(15),
                              3    CESD_SYMBOL         CHAR(8),
                              3    CESD_SYM_FLAGS,
                                   4    CESD_SYM_MAP        BIT(1),
                                   4    CESD_SYM_CHAIN      BIT(1),
                                   4    CESD_SYM_INSERT     BIT(1),
                                   4    CESD_SYM_REPLACE    BIT(1),
                              3    CESD_SYM_TYPE       BIT(4),
                              3    CESD_SYM_ADDR       BIT(24),
                              3    CESD_SYM_ATTR       BIT(8),
                              3    CESD_SYM_LENGTH     BIT(24),
                    (CSECT_ADDR,CSECT_LENGTH) BIN FIXED(31,0) INIT(0B),
                    (I,J,K,NBYTES,THIS_ESDID) BIN FIXED(15,0) INIT(0B);
               DO THIS_ESDID = CESD_1ST_ESDID BY 1
                         WHILE(NBYTES < CESD_BYTE_CNT);
                    I = I + 1;
                    IF CESD_SYM_TYPE(I) ¬= '7'BX THEN
                    DO;
                         ALLOC CESD_LIST_ITEM SET(HOLD_PTR);
                         IF CESD_HEAD_PTR = NULL THEN
                              CESD_HEAD_PTR = HOLD_PTR;
                         ELSE
                         DO;
                              CARD_PTR = NULL;
                              DO CESD_LIST_PTR = CESD_HEAD_PTR
                                        REPEAT CESD_NEXT_PTR
                                        WHILE(CESD_LIST_PTR ¬= NULL &
                                        CESD_ADDR < CESD_SYM_ADDR(I));
                                   CARD_PTR = CESD_LIST_PTR;
                              END;
                              SELECT;
                              WHEN(CARD_PTR = NULL)
                              DO;
                                   HOLD_PTR -> CESD_NEXT_PTR =
                                             CESD_HEAD_PTR;
                                   CESD_HEAD_PTR = HOLD_PTR;
                              END;
                              WHEN(CESD_LIST_PTR = NULL)
                                   CARD_PTR -> CESD_NEXT_PTR = HOLD_PTR;
                              OTHERWISE
                              DO;
                                   HOLD_PTR -> CESD_NEXT_PTR =
                                        CESD_LIST_PTR;
                                   CARD_PTR -> CESD_NEXT_PTR = HOLD_PTR;
                              END;
                              END; /* SELECT */
                         END;
                         CESD_LIST_PTR = HOLD_PTR;
                         CESD_ESDID = THIS_ESDID;
                         CESD_NAME = CESD_SYMBOL(I);
                         CESD_TYPE = (4)'0'B || CESD_SYM_TYPE(I);
                         CESD_ADDR = CESD_SYM_ADDR(I);
                         STRING(CESD_AMODE_RMODE) = CESD_SYM_ATTR(I);
                         CESD_LENGTH = CESD_SYM_LENGTH(I);
                         CESD_DELINK_FLAG = '0'B;
                         IF SUBSTR(CESD_TYPE,5) = (4)'0'B THEN
                         DO;
                              UNSPEC(CSECT_ADDR) = (8)'0'B ||
                                        CESD_SYM_ADDR(I);
                              UNSPEC(CSECT_LENGTH) = (8)'0'B ||
                                        CESD_SYM_LENGTH(I);
                              IF EP_ADDR >= CSECT_ADDR &
                                        EP_ADDR <
                                        (CSECT_ADDR + CSECT_LENGTH)
                                        THEN
                              DO;
                                   EP_ESDID = THIS_ESDID;
                                   EP_OFFSET = EP_ADDR - CSECT_ADDR;
                                   IF LIST_TYPE_NO = 4 THEN
                                        CESD_DELINK_FLAG = '1'B;
                              END;
                              DO J = 1 TO CSECT_COUNT
                                        WHILE(¬ CESD_DELINK_FLAG);
                                   IF CESD_NAME = CSECT_NAME(J) THEN
                                   DO;
                                        CESD_DELINK_FLAG = '1'B;
                                        CSECT_MATCHED(J) = '1'B;
                                   END;
                              END;
                              DO J = 1 TO MASK_COUNT
                                        WHILE(¬ CESD_DELINK_FLAG);
                                   IF MASK_MATCH(CESD_NAME,MASK_NAME(J))
                                             THEN
                                   DO;
                                        CESD_DELINK_FLAG = '1'B;
                                        MASK_MATCH_COUNT(J) =
                                                  MASK_MATCH_COUNT(J) +
                                                            1;
                                   END;
                              END;
                              SELECT(LIST_TYPE_NO);
                              WHEN(1) /* TYPE ALL */
                                   CESD_DELINK_FLAG = '1'B;
                              WHEN(2) /* TYPE EXCLUDE */
                                   CESD_DELINK_FLAG =
                                             ¬ CESD_DELINK_FLAG;
                              WHEN(3,4) /* TYPE SELECT OR EPCSECT */
                                   /* DO NOTHING */;
                              WHEN(5) /* TYPE MAP */
                                   CESD_DELINK_FLAG = CESD_SYM_MAP(I);
                              WHEN(6) /* TYPE NOMAP */
                                   CESD_DELINK_FLAG = ¬ CESD_SYM_MAP(I);
                              END;
                         END;
                    END; /* IF CESD_SYM_TYPE ¬= '7'BX */
                    NBYTES = NBYTES + 16;
               END; /* DO THIS_ESDID = */
          END;
1         WHEN('80'X) /* IDR RECORD */
          BEGIN;
               DCL  1    IDR_LMOD            UNAL
                                             BASED(ADDR(LMOD_RECORD)),
                         2    IDR_REC_ID          BIT(8),
                         2    IDR_BYTE_CNT        BIT(8),
                         2    IDR_SUB_TYPE        BIT(8),
                         2    IDR_BYTES(3:255)    CHAR(1),
                    1    IDR_TRANS_ESDID     UNAL BASED(IDR_TRANS_PTR),
                         2    IDR_ESDID(127)      BIN FIXED(15,0),
                    1    IDR_TRANSLATOR_DATA BASED(IDR_DATA_PTR),
                         2    FILLER#1            BIT(7),
                         2    IDR_2_TRANS         BIT(1),
                         2    IDR_TRANS_DATA(2),
                              3    IDR_TRANS_NAME      CHAR(10),
                              3    IDR_TRANS_VVMM(4)   BIT(4) UNAL,
                              3    IDR_TRANS_DATE      DEC FIXED(5,0),
                    1    IDR_USER_ENTRY      UNAL
                                             BASED(IDR_TRANS_PTR),
                         2    IDR_USER_ESDID      BIN FIXED(15,0),
                         2    IDR_USER_DATE       BIT(24),
                         2    IDR_USER_LENGTH_BITS BIT(8),
                         2    IDR_USER_DATA       CHAR(40),
                    IDR_OVFL_REC        CHAR(256) BASED(IDR_OVFL_PTR),
                    IDR_OVFL_PTR        PTR,
                    IDR_OVFL_FLAG       BIT(1) INIT('0'B),
                    (IDR_TRANS_PTR,IDR_DATA_PTR) PTR INIT(NULL),
                    (I,J,K,NBYTES,NSCAN) BIN FIXED(15,0);
0              NSCAN = 2;
               UNSPEC(NBYTES) = (8)'0'B || IDR_BYTE_CNT;
               IDR_OVFL_PTR = ADDR(IDR_BYTES(NBYTES+1));
-              SELECT(SUBSTR(IDR_SUB_TYPE,5,4));
0                   WHEN('0100'B) /* TRANSLATOR IDR */
                    DO IDR_TRANS_PTR = ADDR(IDR_BYTES(3))
                              REPEAT ADDR(IDR_BYTES(NSCAN+1))
                              WHILE(NSCAN < NBYTES);
                         I = 1;
                         DO UNTIL(I <= K);
                              K = (NBYTES - NSCAN)/2;
                              DO I = I TO K UNTIL(IDR_ESDID(I) < 0);
                              END;
                              IF I > K THEN
                              DO;
                                   IDR_OVFL_FLAG = '1'B;
                                   CALL DWNSPDSR(SYSLIB,PDS_RET_CODE,
                                        RECLEN,IDR_OVFL_REC);
                                   UNSPEC(J) = (8)'0'B ||
                                       UNSPEC(SUBSTR(IDR_OVFL_REC,2,1));
                                   NBYTES = NBYTES + J - 2;
                                   IDR_OVFL_REC =
                                        SUBSTR(IDR_OVFL_REC,4,253);
                                   IDR_OVFL_PTR =
                                        ADDR(IDR_BYTES(NBYTES+1));
                              END;
                         END;
                         IDR_DATA_PTR = ADDR(IDR_ESDID(I+1));
                         UNSPEC(IDR_ESDID(I)) = UNSPEC(IDR_ESDID(I)) &
                                   '7FFF'BX;
                         IF IDR_2_TRANS THEN
                              IDR_TRANS_CNT = 2;
                         ELSE
                              IDR_TRANS_CNT = 1;
                         NSCAN = NSCAN + I + I + 1 + IDR_TRANS_CNT*15;
                         IF NSCAN > NBYTES THEN
                         DO;
                              IDR_OVFL_FLAG = '1'B;
                              CALL DWNSPDSR(SYSLIB,PDS_RET_CODE,
                                   RECLEN,IDR_OVFL_REC);
                              UNSPEC(J) = (8)'0'B ||
                                   UNSPEC(SUBSTR(IDR_OVFL_REC,2,1));
                              NBYTES = NBYTES + J - 2;
                              IDR_OVFL_REC = SUBSTR(IDR_OVFL_REC,4,253);
                              IDR_OVFL_PTR = ADDR(IDR_BYTES(NBYTES+1));
                         END;
                         DO J = 1 TO I;
                              DO HOLD_PTR = CESD_HEAD_PTR
                                        REPEAT
                                             HOLD_PTR -> CESD_NEXT_PTR
                                        WHILE(HOLD_PTR ¬= NULL)
                                        UNTIL(HOLD_PTR -> CESD_ESDID =
                                        IDR_ESDID(J));
                              END;
                              IF HOLD_PTR ¬= NULL &
                                       HOLD_PTR -> CESD_DELINK_FLAG THEN
                              DO;
                                   ALLOC IDR_LIST_ITEM SET(HOLD_PTR);
                                   IF IDR_HEAD_PTR = NULL THEN
                                        IDR_HEAD_PTR = HOLD_PTR;
                                   ELSE
                                        IDR_NEXT_PTR = HOLD_PTR;
                                   IDR_LIST_PTR = HOLD_PTR;
                                   IDR_LIST_ESDID = IDR_ESDID(J);
                                   DO K = 1 TO IDR_TRANS_CNT;
                                        IDR_DATA(K) =
                                             IDR_TRANS_DATA(K);
                                   END;
                              END;
                         END;
                         IF IDR_OVFL_FLAG THEN
                         DO;
                              IDR_OVFL_FLAG = '0'B;
                              NBYTES = NBYTES - NSCAN;
                              SUBSTR(LMOD_RECORD,4,NBYTES) =
                                   SUBSTR(LMOD_RECORD,NSCAN+2,NBYTES);
                              NBYTES = NBYTES + 2;
                              IDR_OVFL_PTR = ADDR(IDR_BYTES(NBYTES+1));
                              NSCAN = 2;
                         END;
                    END;
-                   WHEN('1000'B) /* IDENTIFY (USER) IDR */
                    DO IDR_TRANS_PTR = ADDR(IDR_BYTES(3))
                              REPEAT ADDR(IDR_BYTES(NSCAN+1))
                              WHILE(NSCAN < NBYTES);
                         K = NSCAN + 6;
                         IF K >= NBYTES THEN
                         DO;
                              IDR_OVFL_FLAG = '1'B;
                              CALL DWNSPDSR(SYSLIB,PDS_RET_CODE,
                                   RECLEN,IDR_OVFL_REC);
                              UNSPEC(J) = (8)'0'B ||
                                   UNSPEC(SUBSTR(IDR_OVFL_REC,2,1));
                              NBYTES = NBYTES + J - 2;
                              IDR_OVFL_REC = SUBSTR(IDR_OVFL_REC,4,253);
                              IDR_OVFL_PTR = ADDR(IDR_BYTES(NBYTES+1));
                         END;
                         UNSPEC(IDR_USER_LENGTH) = (8)'0'B ||
                                   IDR_USER_LENGTH_BITS;
                         NSCAN = K + IDR_USER_LENGTH;
                         IF NSCAN > NBYTES THEN
                         DO;
                              IDR_OVFL_FLAG = '1'B;
                              CALL DWNSPDSR(SYSLIB,PDS_RET_CODE,
                                   RECLEN,IDR_OVFL_REC);
                              UNSPEC(J) = (8)'0'B ||
                                   UNSPEC(SUBSTR(IDR_OVFL_REC,2,1));
                              NBYTES = NBYTES + J - 2;
                              IDR_OVFL_REC = SUBSTR(IDR_OVFL_REC,4,253);
                              IDR_OVFL_PTR = ADDR(IDR_BYTES(NBYTES+1));
                         END;
                         DO HOLD_PTR = CESD_HEAD_PTR
                                   REPEAT HOLD_PTR -> CESD_NEXT_PTR
                                   WHILE(HOLD_PTR ¬= NULL)
                                   UNTIL(HOLD_PTR -> CESD_ESDID =
                                   IDR_USER_ESDID);
                         END;
                         IF HOLD_PTR ¬= NULL &
                                   HOLD_PTR -> CESD_DELINK_FLAG THEN
                         DO;
                              ALLOC IDR_USER_ITEM SET(HOLD_PTR);
                              IF IDR_USER_HEAD_PTR = NULL THEN
                                   IDR_USER_HEAD_PTR = HOLD_PTR;
                              ELSE
                                   IDR_USER_NEXT_PTR = HOLD_PTR;
                              IDR_USER_PTR = HOLD_PTR;
                              IDR_USER_LIST_ESDID = IDR_USER_ESDID;
                              IDR_USER_LIST_DATA =
                                SUBSTR(IDR_USER_DATA,1,IDR_USER_LENGTH);
                         END;
                         IF IDR_OVFL_FLAG THEN
                         DO;
                              IDR_OVFL_FLAG = '0'B;
                              NBYTES = NBYTES - NSCAN;
                              SUBSTR(LMOD_RECORD,4,NBYTES) =
                                   SUBSTR(LMOD_RECORD,NSCAN+2,NBYTES);
                              NBYTES = NBYTES + 2;
                              IDR_OVFL_PTR = ADDR(IDR_BYTES(NBYTES+1));
                              NSCAN = 2;
                         END;
                    END;
0                   OTHERWISE
                         /* DROP THIS IDR RECORD */;
               END;
          END;
1         WHEN('01'X,'05'X,'0D'X) /* CTL RECORD */
          BEGIN;
               DCL  1    CTL_LMOD            UNAL BASED(CTL_PTR),
                         2    CTL_REC_ID          BIT(8),
                         2    FILLER#1            BIT(8),
                         2    RLD_CNT             BIN FIXED(15,0),
                         2    CTL_BYTE_CNT        BIN FIXED(15,0),
                         2    FILLER#2            BIN FIXED(15,0),
                         2    FILLER#3            BIT(8),
                         2    CTL_1ST_ADDR        BIT(24),
                         2    FILLER#4            CHAR(4),
                         2    CTL_BYTE((NBYTES) REFER (CTL_BYTE_CNT))
                                                  CHAR(1),
                    1    CTL_DATA            UNAL BASED(CTL_DATA_PTR),
                         2    CTL_ESDID           BIN FIXED(15,0),
                         2    CTL_TXT_LENGTH      BIN FIXED(15,0),
                    (CTL_PTR,CTL_DATA_PTR) PTR INIT(ADDR(LMOD_RECORD)),
                    (I,J,NBYTES,NSCAN)  BIN FIXED(15,0),
                    (SD_ADDR,TEMP)      BIN FIXED(31,0);
0              IF CTL_REC_ID = '0D'X & RLD_CNT = 0B THEN
                    EOM_FLAG = '1'B;
0              NBYTES = CTL_BYTE_CNT;
               ALLOC CTL_LMOD;
               CTL_LMOD = CTL_DATA_PTR -> CTL_LMOD;
               RECLEN = STG(LMOD_RECORD);
               CALL DWNSPDSR(SYSLIB,PDS_RET_CODE,RECLEN,LMOD_RECORD);
               IF PLIRETV ¬= 0 THEN
                    CALL PLIDUMP('TFSHB','MISSING TXT');
               J,NSCAN,NBYTES = 0B;
               UNSPEC(TXT_ADDR) = (8)'0'B || CTL_1ST_ADDR;
               DO CTL_DATA_PTR = ADDR(CTL_BYTE(1))
                         REPEAT ADDR(CTL_BYTE(NSCAN+1))
                         WHILE(NSCAN < CTL_BYTE_CNT);
                    DO HOLD_PTR = CESD_HEAD_PTR
                              REPEAT HOLD_PTR -> CESD_NEXT_PTR
                              WHILE(HOLD_PTR ¬= NULL)
                              UNTIL(HOLD_PTR -> CESD_ESDID = CTL_ESDID);
                    END;
                    IF HOLD_PTR ¬= NULL &
                              HOLD_PTR -> CESD_DELINK_FLAG THEN
                    DO;
                         UNSPEC(SD_ADDR) = (8)'0'B ||
                                   HOLD_PTR -> CESD_ADDR;
                         TXT_LENGTH = CTL_TXT_LENGTH;
                         ALLOC TXT_LIST_ITEM SET(HOLD_PTR);
                         IF TXT_HEAD_PTR = NULL THEN
                              TXT_HEAD_PTR = HOLD_PTR;
                         ELSE
                              TXT_NEXT_PTR = HOLD_PTR;
                         TXT_LIST_PTR = HOLD_PTR;
                         TEMP = TXT_ADDR + J - SD_ADDR;
                         TXT_ADDR_1ST_BYTE =
                                   SUBSTR(UNSPEC(TEMP),9,24);
                         TXT_ESDID = CTL_ESDID;
                         TXT_BYTES = SUBSTR(LMOD_RECORD,J+1,TXT_LENGTH);
                    END;
                    J = J + CTL_TXT_LENGTH;
                    NSCAN = NSCAN + 4;
               END;
               FREE CTL_LMOD;
          END;
1         WHEN('03'X,'07'X,'0F'X) /* CTL/RLD RECORD */
          BEGIN;
               DCL  1    CTL_RLD_LMOD        UNAL BASED(CTL_RLD_PTR),
                         2    CTL_RLD_REC_ID      BIT(8),
                         2    FILLER#1            BIT(24),
                         2    CTL_BYTE_CNT        BIN FIXED(15,0),
                         2    RLD_BYTE_CNT        BIN FIXED(15,0),
                         2    FILLER#2            BIT(8),
                         2    CTL_RLD_1ST_ADDR    BIT(24),
                         2    FILLER#3            CHAR(4),
                         2    RLD_BYTE((NSCAN) REFER (RLD_BYTE_CNT))
                                                  CHAR(1),
                         2    CTL_BYTE((NBYTES) REFER (CTL_BYTE_CNT))
                                                  CHAR(1),
                    1    CTL_DATA            UNAL BASED(CTL_DATA_PTR),
                         2    CTL_ESDID           BIN FIXED(15,0),
                         2    CTL_TXT_LENGTH      BIN FIXED(15,0),
                    1    RLD_DATA            UNAL BASED(RLD_PTR),
                         2    RLD_REL_ID          BIN FIXED(15,0),
                         2    RLD_POS_ID          BIN FIXED(15,0),
                         2    RLD_ITEM(59),
                              3    RLD_FLAGS           BIT(8),
                              3    RLD_ADCON_ADDR      BIT(24),
                    (CTL_RLD_PTR,CTL_DATA_PTR,RLD_PTR) PTR
                                        INIT(ADDR(LMOD_RECORD)),
                    (I,J,NBYTES,NSCAN)  BIN FIXED(15,0),
                    (SD_ADDR,TEMP)      BIN FIXED(31,0);
0              IF CTL_RLD_REC_ID = '0F'X THEN
                    EOM_FLAG = '1'B;
0              NBYTES = CTL_BYTE_CNT;
               NSCAN = RLD_BYTE_CNT;
               ALLOC CTL_RLD_LMOD;
               CTL_RLD_LMOD = CTL_DATA_PTR -> CTL_RLD_LMOD;
               RECLEN = STG(LMOD_RECORD);
               CALL DWNSPDSR(SYSLIB,PDS_RET_CODE,RECLEN,LMOD_RECORD);
               IF PLIRETV ¬= 0 THEN
                    CALL PLIDUMP('TFSHB','MISSING TXT');
               J,NSCAN,NBYTES = 0B;
               UNSPEC(TXT_ADDR) = (8)'0'B || CTL_RLD_1ST_ADDR;
               DO CTL_DATA_PTR = ADDR(CTL_BYTE(1))
                         REPEAT ADDR(CTL_BYTE(NSCAN+1))
                         WHILE(NSCAN < CTL_BYTE_CNT);
                    DO HOLD_PTR = CESD_HEAD_PTR
                              REPEAT HOLD_PTR -> CESD_NEXT_PTR
                              WHILE(HOLD_PTR ¬= NULL)
                              UNTIL(HOLD_PTR -> CESD_ESDID = CTL_ESDID);
                    END;
                    IF HOLD_PTR ¬= NULL &
                              HOLD_PTR -> CESD_DELINK_FLAG THEN
                    DO;
                         UNSPEC(SD_ADDR) = (8)'0'B ||
                                   HOLD_PTR -> CESD_ADDR;
                         TXT_LENGTH = CTL_TXT_LENGTH;
                         ALLOC TXT_LIST_ITEM SET(HOLD_PTR);
                         IF TXT_HEAD_PTR = NULL THEN
                              TXT_HEAD_PTR = HOLD_PTR;
                         ELSE
                              TXT_NEXT_PTR = HOLD_PTR;
                         TXT_LIST_PTR = HOLD_PTR;
                         TEMP = TXT_ADDR + J - SD_ADDR;
                         TXT_ADDR_1ST_BYTE =
                                   SUBSTR(UNSPEC(TEMP),9,24);
                         TXT_ESDID = CTL_ESDID;
                         TXT_BYTES = SUBSTR(LMOD_RECORD,J+1,TXT_LENGTH);
                    END;
                    J = J + CTL_TXT_LENGTH;
                    NSCAN = NSCAN + 4;
               END;
-              NSCAN = 0B;
               DO RLD_PTR = ADDR(RLD_BYTE(1))
                         REPEAT ADDR(RLD_BYTE(NSCAN+1))
                         WHILE(NSCAN < RLD_BYTE_CNT);
                    I = (RLD_BYTE_CNT - NSCAN - 4)/4;
                    DO RLD_ITEM_CNT = 1 TO I
                         WHILE(SUBSTR(RLD_FLAGS(RLD_ITEM_CNT),8,1));
                    END;
                    IF RLD_ITEM_CNT > I THEN
                         SIGNAL ERROR;
                    ALLOC RLD_LIST_ITEM SET(HOLD_PTR);
                    IF RLD_HEAD_PTR = NULL THEN
                         RLD_HEAD_PTR = HOLD_PTR;
                    ELSE
                    DO;
                         CARD_PTR = NULL;
                         DO RLD_LIST_PTR = RLD_HEAD_PTR
                                   REPEAT RLD_NEXT_PTR
                                   WHILE(RLD_LIST_PTR ¬= NULL &
                                   (RLD_LIST_POS_ID < RLD_POS_ID |
                                   (RLD_LIST_POS_ID = RLD_POS_ID &
                                   RLD_LIST_REL_ID < RLD_REL_ID)));
                              CARD_PTR = RLD_LIST_PTR;
                         END;
                         SELECT;
                         WHEN(CARD_PTR = NULL)
                         DO;
                              HOLD_PTR -> RLD_NEXT_PTR = RLD_HEAD_PTR;
                              RLD_HEAD_PTR = HOLD_PTR;
                         END;
                         WHEN(RLD_LIST_PTR = NULL)
                              CARD_PTR -> RLD_NEXT_PTR = HOLD_PTR;
                         OTHERWISE
                         DO;
                              HOLD_PTR -> RLD_NEXT_PTR = RLD_LIST_PTR;
                              CARD_PTR -> RLD_NEXT_PTR = HOLD_PTR;
                         END;
                         END; /* SELECT */
                    END;
                    RLD_LIST_PTR = HOLD_PTR;
                    RLD_LIST_REL_ID = RLD_REL_ID;
                    RLD_LIST_POS_ID = RLD_POS_ID;
                    DO HOLD_PTR = CESD_HEAD_PTR
                              REPEAT HOLD_PTR -> CESD_NEXT_PTR
                              WHILE(HOLD_PTR ¬= NULL)
                              UNTIL(HOLD_PTR -> CESD_ESDID =
                                   RLD_POS_ID);
                    END;
                    IF HOLD_PTR = NULL THEN
                         SIGNAL ERROR;
                    UNSPEC(SD_ADDR) = (8)'0'B ||
                              HOLD_PTR -> CESD_ADDR;
                    DO I = 1 TO RLD_ITEM_CNT;
                         RLD_LIST_FLAGS(I) = RLD_FLAGS(I);
                         UNSPEC(ADCON) = (8)'0'B || RLD_ADCON_ADDR(I);
                         ADCON = ADCON - SD_ADDR;
                         RLD_LIST_ADCON_ADDR(I) =
                              SUBSTR(UNSPEC(ADCON),9,24);
                    END;
                    NSCAN = NSCAN + 4 + RLD_ITEM_CNT*4;
               END;
               FREE CTL_RLD_LMOD;
          END;
1         WHEN('02'X,'06'X,'0E'X) /* RLD RECORD */
          BEGIN;
               DCL  1    RLD_LMOD            UNAL
                                             BASED(ADDR(LMOD_RECORD)),
                         2    RLD_REC_ID          BIT(8),
                         2    FILLER#1            BIT(8),
                         2    RLD_CNT             BIN FIXED(15,0),
                         2    FILLER#2            BIN FIXED(15,0),
                         2    RLD_BYTE_CNT        BIN FIXED(15,0),
                         2    FILLER#3            CHAR(8),
                         2    RLD_BYTE(240 REFER (RLD_BYTE_CNT))
                                                  CHAR(1),
                    1    RLD_DATA            UNAL BASED(RLD_PTR),
                         2    RLD_REL_ID          BIN FIXED(15,0),
                         2    RLD_POS_ID          BIN FIXED(15,0),
                         2    RLD_ITEM(59),
                              3    RLD_FLAGS           BIT(8),
                              3    RLD_ADCON_ADDR      BIT(24),
                    RLD_PTR             PTR INIT(NULL),
                    (I,NSCAN)           BIN FIXED(15,0);
0              IF RLD_REC_ID = '0E'X THEN
                    EOM_FLAG = '1'B;
0              NSCAN = 0B;
               DO RLD_PTR = ADDR(RLD_BYTE(1))
                         REPEAT ADDR(RLD_BYTE(NSCAN+1))
                         WHILE(NSCAN < RLD_BYTE_CNT);
                    I = (RLD_BYTE_CNT - NSCAN - 4)/4;
                    DO RLD_ITEM_CNT = 1 TO I
                         WHILE(SUBSTR(RLD_FLAGS(RLD_ITEM_CNT),8,1));
                    END;
                    IF RLD_ITEM_CNT > I THEN
                         SIGNAL ERROR;
                    ALLOC RLD_LIST_ITEM SET(HOLD_PTR);
                    IF RLD_HEAD_PTR = NULL THEN
                         RLD_HEAD_PTR = HOLD_PTR;
                    ELSE
                    DO;
                         CARD_PTR = NULL;
                         DO RLD_LIST_PTR = RLD_HEAD_PTR
                                   REPEAT RLD_NEXT_PTR
                                   WHILE(RLD_LIST_PTR ¬= NULL &
                                   (RLD_LIST_POS_ID < RLD_POS_ID |
                                   (RLD_LIST_POS_ID = RLD_POS_ID &
                                   RLD_LIST_REL_ID < RLD_REL_ID)));
                              CARD_PTR = RLD_LIST_PTR;
                         END;
                         SELECT;
                         WHEN(CARD_PTR = NULL)
                         DO;
                              HOLD_PTR -> RLD_NEXT_PTR = RLD_HEAD_PTR;
                              RLD_HEAD_PTR = HOLD_PTR;
                         END;
                         WHEN(RLD_LIST_PTR = NULL)
                              CARD_PTR -> RLD_NEXT_PTR = HOLD_PTR;
                         OTHERWISE
                         DO;
                              HOLD_PTR -> RLD_NEXT_PTR = RLD_LIST_PTR;
                              CARD_PTR -> RLD_NEXT_PTR = HOLD_PTR;
                         END;
                         END; /* SELECT */
                    END;
                    RLD_LIST_PTR = HOLD_PTR;
                    RLD_LIST_REL_ID = RLD_REL_ID;
                    RLD_LIST_POS_ID = RLD_POS_ID;
                    DO HOLD_PTR = CESD_HEAD_PTR
                              REPEAT HOLD_PTR -> CESD_NEXT_PTR
                              WHILE(HOLD_PTR ¬= NULL)
                              UNTIL(HOLD_PTR -> CESD_ESDID =
                                   RLD_POS_ID);
                    END;
                    IF HOLD_PTR = NULL THEN
                         SIGNAL ERROR;
                    UNSPEC(SD_ADDR) = (8)'0'B ||
                              HOLD_PTR -> CESD_ADDR;
                    DO I = 1 TO RLD_ITEM_CNT;
                         RLD_LIST_FLAGS(I) = RLD_FLAGS(I);
                         UNSPEC(ADCON) = (8)'0'B || RLD_ADCON_ADDR(I);
                         ADCON = ADCON - SD_ADDR;
                         RLD_LIST_ADCON_ADDR(I) =
                              SUBSTR(UNSPEC(ADCON),9,24);
                    END;
                    NSCAN = NSCAN + 4 + RLD_ITEM_CNT*4;
               END;
          END;
1         OTHERWISE /* ERROR */
          DO;
               PUT FILE(SYSPRINT) SKIP(2) EDIT
               ('** ERROR ** INVALID RECORD TYPE X''',
                    DWNSHEX(UNSPEC(SUBSTR(LMOD_RECORD,1,1))),'''.')
               (A);
               RETURN_CODE = MAX(RETURN_CODE,16);
          END;
-         END; /* SELECT(REC_ID) */
0         RECLEN = STG(LMOD_RECORD);
          CALL DWNSPDSR(SYSLIB,PDS_RET_CODE,RECLEN,LMOD_RECORD);
     END; /* DO WHILE(PLIRETV = 0) */
1    /* NOW WE'LL SORT OUT THE RETURN CODE */
     SELECT(PLIRETV);
0    WHEN(0,4) /* NORMAL END OF MEMBER - GENERATE THE DECK */
     DO;
          PUT FILE(SYSPRINT) SKIP(2) EDIT
          (('CSECT','A/R','OLD','NEW' DO I = 1 TO 3))
          (X(2),4(A,X(6)))
          (('NAME','MODE','SIZE','SIZE' DO I = 1 TO 3))
          (SKIP,3(X(2),A,2(X(6),A),X(5),A,X(5)));
          PAGE_TYPE = 2;
          P = 81;
          IF MORE_LINKCNTL & SUBSTR(LINKCNTL_REC,9,8) = MEMBER THEN
          DO;
               DO WHILE(MORE_LINKCNTL)
                         UNTIL(SUBSTR(LINKCNTL_REC,1,7) = 'BEFORE ' |
                               SUBSTR(LINKCNTL_REC,1,6) = 'AFTER ' |
                               SUBSTR(LINKCNTL_REC,1,8) = 'MEMBER: ');
                    READ FILE(LINKCNTL) SET(LINKCNTL_PTR);
               END;
               IF SUBSTR(LINKCNTL_REC,1,7) = 'BEFORE ' THEN
               DO;
                    READ FILE(LINKCNTL) SET(LINKCNTL_PTR);
                    DO WHILE(MORE_LINKCNTL &
                              SUBSTR(LINKCNTL_REC,1,6) ¬= 'AFTER ' &
                              SUBSTR(LINKCNTL_REC,1,8) ¬= 'MEMBER: ');
                         WRITE FILE(SYSLIN) FROM(LINKCNTL_REC);
                         BEFORE_CNT = BEFORE_CNT + 1;
                         READ FILE(LINKCNTL) SET(LINKCNTL_PTR);
                    END;
               END;
          END;
          DO CESD_LIST_PTR = CESD_HEAD_PTR REPEAT CESD_NEXT_PTR
                    WHILE(CESD_LIST_PTR ¬= NULL);
               IF CESD_DELINK_FLAG THEN
               DO;
                    LAST_TXT_ADDR = 0B;
                    UNSPEC(TXT_SIZE) = (8)'0'B || CESD_LENGTH;
                    DO I = 1 TO CSECT_COUNT
                              UNTIL(CSECT_NAME(I) = CESD_NAME);
                    END;
                    IF I > CSECT_COUNT | CSECT_SIZE(I) = 0B THEN
                         NEW_SIZE = (6)' ';
                    ELSE
                    DO;
                         TXT_SIZE = MAX(TXT_SIZE,CSECT_SIZE(I));
                         NEW_SIZE =
                              DWNSHEX(SUBSTR(UNSPEC(TXT_SIZE),9,24));
                    END;
                    P = P + 40;
                    IF P > 81 THEN
                         P = 1;
                    SYM_AMODE = CESD_AMODE64 || CESD_AMODE;   /* GP */
                    PUT FILE(SYSPRINT) EDIT
                    (CESD_NAME,SYM_AMODE_TYPE(SYM_AMODE),'/',
                         RMODE_TYPE(CESD_RMODE),DWNSHEX(CESD_LENGTH),
                         NEW_SIZE)
                    (COL(P),A,X(3),3 A,2(X(3),A));
                    SEQ# = 1;
                    LOCATE ESD_OBJ FILE(SYSLIN);
                    LAST_ESDID,NESD,ESD_1ST_ESDID = 1;
                    ESD_PTR = ADDR(ESD_BYTES(1));
                    ESD_BYTE_CNT = 16;
                    ESD_NAME = CESD_NAME;
                    ESD_TYPE = (8)'0'B;
                    ESD_ADDR = (24)'0'B;
                    ESD_AMODE_RMODE = STRING(CESD_AMODE_RMODE);
                    ESD_LENGTH = SUBSTR(UNSPEC(TXT_SIZE),9,24);
0                   DO HOLD_PTR = CESD_HEAD_PTR
                              REPEAT HOLD_PTR -> CESD_NEXT_PTR
                              WHILE(HOLD_PTR ¬= NULL);
                         HOLD_PTR -> CESD_NEW_ESDID = 0B;
                         IF SUBSTR(HOLD_PTR->CESD_TYPE,5,4) = '0011'B &
                                   SUBSTR(HOLD_PTR->CESD_LENGTH,9,16) =
                                   UNSPEC(CESD_ESDID) THEN
                         DO;
                              NESD = NESD + 1;
                              IF NESD > 3 THEN
                              DO;
                                   NESD = 1;
                                   SEQ# = SEQ# + 1;
                                   LOCATE ESD_OBJ FILE(SYSLIN);
                                   ESD_1ST_ESDID = 2;
                              END;
                              ESD_PTR = ADDR(ESD_BYTES(NESD));
                              ESD_BYTE_CNT = ESD_BYTE_CNT + 16;
                              ESD_NAME = HOLD_PTR -> CESD_NAME;
                              ESD_TYPE = '01'BX;
                              UNSPEC(LD_ADDR) = (8)'0'B ||
                                        HOLD_PTR -> CESD_ADDR;
                              UNSPEC(SD_ADDR) = (8)'0'B || CESD_ADDR;
                              LD_ADDR = LD_ADDR - SD_ADDR;
                              ESD_ADDR =
                                   SUBSTR(UNSPEC(LD_ADDR),9,24);
                              ESD_LENGTH = '000001'BX;
                              ESD_AMODE_RMODE = '40'BX;
                         END;
                    END; /* DO HOLD_PTR = */
-                   DO RLD_LIST_PTR = RLD_HEAD_PTR
                              REPEAT RLD_NEXT_PTR
                              WHILE(RLD_LIST_PTR ¬= NULL);
                         IF RLD_LIST_POS_ID = CESD_ESDID &
                                   RLD_LIST_REL_ID ¬= CESD_ESDID &
                                   RLD_LIST_REL_ID ¬= 0B THEN
                         DO;
                              DO HOLD_PTR = CESD_HEAD_PTR
                                        REPEAT HOLD_PTR -> CESD_NEXT_PTR
                                        WHILE(HOLD_PTR ¬= NULL)
                                        UNTIL(HOLD_PTR->CESD_ESDID =
                                             RLD_LIST_REL_ID);
                              END;
                              IF HOLD_PTR = NULL THEN
                                   SIGNAL ERROR;
                              IF HOLD_PTR -> CESD_NEW_ESDID ¬= 0B THEN
                                   LEAVE;
                              HOLD_PTR -> CESD_NEW_ESDID,
                                   LAST_ESDID = LAST_ESDID + 1;
                              NESD = NESD + 1;
                              IF NESD > 3 THEN
                              DO;
                                   NESD = 1;
                                   SEQ# = SEQ# + 1;
                                   LOCATE ESD_OBJ FILE(SYSLIN);
                                   ESD_1ST_ESDID = LAST_ESDID;
                              END;
                              ESD_PTR = ADDR(ESD_BYTES(NESD));
                              ESD_BYTE_CNT = ESD_BYTE_CNT + 16;
                              ESD_NAME = HOLD_PTR -> CESD_NAME;
                              ESD_ADDR = (24)'0'B;
                              SELECT(SUBSTR(HOLD_PTR->CESD_TYPE,5));
                              WHEN('0000'B,'0011'B,'0100'B,'0010'B)
                                   ESD_TYPE = '02'BX;
                              WHEN('0101'B)
                              DO;
                                   ESD_TYPE = '05'BX;
                                   ESD_LENGTH = HOLD_PTR -> CESD_LENGTH;
                              END;
                              WHEN('0110'B)
                              DO;
                                   ESD_TYPE = '06'BX;
                                   ESD_LENGTH = HOLD_PTR -> CESD_LENGTH;
                                   ESD_AMODE_RMODE =
                                     STRING(HOLD_PTR->CESD_AMODE_RMODE);
                              END;
                              WHEN('1010'B)
                                   ESD_TYPE = '0A'BX;
                              END; /* SELECT */
                         END;
                    END; /* DO RLD_LIST_PTR = */
1                   IF CESD_LENGTH = 0B THEN;              /* GP */
  /* MARCH 2006     DO                                        GP */
  /*                     SEQ# = SEQ# + 1                      GP */
  /* ERROR FOR BINDER ==>LOCATE TXT_OBJ_NULL FILE(SYSLIN)<==  GP */
  /*                END                                       GP */
                    ELSE
0                   DO TXT_LIST_PTR = TXT_HEAD_PTR
                              REPEAT TXT_NEXT_PTR
                              WHILE(TXT_LIST_PTR ¬= NULL);
                         IF TXT_ESDID = CESD_ESDID THEN
                         DO;
                              UNSPEC(TXT_ADDR) = (8)'0'B ||
                                        TXT_ADDR_1ST_BYTE;
                              TXT_END = TXT_ADDR + TXT_BYTE_CNT;
                              DO RLD_LIST_PTR = RLD_HEAD_PTR
                                        REPEAT RLD_NEXT_PTR
                                        WHILE(RLD_LIST_PTR ¬= NULL);
                                 IF RLD_LIST_POS_ID = TXT_ESDID &
                                           RLD_LIST_REL_ID ¬= 0B THEN
                                 DO;
                                   DO HOLD_PTR = CESD_HEAD_PTR
                                        REPEAT HOLD_PTR -> CESD_NEXT_PTR
                                        WHILE(HOLD_PTR ¬= NULL)
                                        UNTIL(HOLD_PTR -> CESD_ESDID =
                                                  RLD_LIST_REL_ID);
                                   END;
                                   IF HOLD_PTR = NULL THEN
                                        SIGNAL ERROR;
                                   UNSPEC(SD_ADDR) = (8)'0'B ||
                                             HOLD_PTR -> CESD_ADDR;
-                                  DO I = 1 TO RLD_LIST_ITEM_CNT;
                                        UNSPEC(LD_ADDR) = (8)'0'B ||
                                             RLD_LIST_ADCON_ADDR(I);
                                        IF LD_ADDR >= TXT_ADDR &
                                                  LD_ADDR < TXT_END &
                             SUBSTR(RLD_LIST_FLAGS(I),1,3) = '000'B THEN
                                        DO;
                                             UNSPEC(L) = (14)'0'B ||
                                             SUBSTR(RLD_LIST_FLAGS(I),
                                                       5,2);
                                             L = L + 1;
                                             TEMP_CHAR = LOW(4-L) ||
                                                  SUBSTR(TXT_BYTES,
                                                  LD_ADDR-TXT_ADDR+1,L);
                                             UNSPEC(ADCON) =
                                                      UNSPEC(TEMP_CHAR);
                                             IF
                                           SUBSTR(RLD_LIST_FLAGS(I),7,1)
                                                     THEN
                                                ADCON = ADCON + SD_ADDR;
                                             ELSE
                                                ADCON = ADCON - SD_ADDR;
                                             UNSPEC(TEMP_CHAR) =
                                                       UNSPEC(ADCON);
                                             SUBSTR(TXT_BYTES,
                                                LD_ADDR-TXT_ADDR+1,L) =
                                                SUBSTR(TEMP_CHAR,5-L,L);
                                        END;
                                   END;
                                 END;
                              END;
                              DO I = 1 BY 56 TO TXT_BYTE_CNT;
                                   SEQ# = SEQ# + 1;
                                   TXT_ADDR_BITS =
                                        SUBSTR(UNSPEC(TXT_ADDR),9,24);
                                   LOCATE TXT_OBJ FILE(SYSLIN);
                                   TXT_CNT = MIN(56,TXT_BYTE_CNT-I+1);
                                   TXT = SUBSTR(TXT_BYTES,I,TXT_CNT);
                                   TXT_ADDR = TXT_ADDR + TXT_CNT;
                                   IF TXT_ADDR > CESD_LENGTH THEN
                                   DO;
                                        K = TXT_ADDR - CESD_LENGTH;
                                        TXT_CNT = TXT_CNT - K;
                                        SUBSTR(TXT,TXT_CNT+1,K) = '';
                                        TXT_ADDR = CESD_LENGTH;
                                   END;
                              END;
                              LAST_TXT_ADDR = TXT_ADDR;
                         END;
                    END;
-                   /* PAD WITH BLANKS TO EXPANDED SIZE */
                    TXT_SIZE = TXT_SIZE - CESD_LENGTH;
                    DO I = 1 BY 56 TO TXT_SIZE;
                         SEQ# = SEQ# + 1;
                         TXT_ADDR_BITS =
                              SUBSTR(UNSPEC(LAST_TXT_ADDR),9,24);
                         LOCATE TXT_OBJ FILE(SYSLIN);
                         TXT_CNT = MIN(56,TXT_SIZE-I+1);
                         LAST_TXT_ADDR = LAST_TXT_ADDR + TXT_CNT;
                    END;
1                   K = 57;
                    DO RLD_LIST_PTR = RLD_HEAD_PTR
                              REPEAT RLD_NEXT_PTR
                              WHILE(RLD_LIST_PTR ¬= NULL);
                         IF RLD_LIST_POS_ID = CESD_ESDID THEN
                         DO;
                              SELECT(RLD_LIST_REL_ID);
                              WHEN(0B)
                                   REPL_ESDID = 0B;
                              WHEN(CESD_ESDID)
                                   REPL_ESDID = 1B;
                              OTHERWISE
                              DO;
                                   DO HOLD_PTR = CESD_HEAD_PTR
                                        REPEAT HOLD_PTR -> CESD_NEXT_PTR
                                        WHILE(HOLD_PTR ¬= NULL)
                                        UNTIL(HOLD_PTR -> CESD_ESDID =
                                             RLD_LIST_REL_ID);
                                   END;
                                   IF HOLD_PTR = NULL THEN
                                        SIGNAL ERROR;
                                   IF HOLD_PTR -> CESD_NEW_ESDID = 0B
                                             THEN
                                        SIGNAL ERROR;
                                   REPL_ESDID =
                                        HOLD_PTR -> CESD_NEW_ESDID;
                              END;
                              END;
                              NBYTES = RLD_LIST_ITEM_CNT*4 + 4;
                              J = 1;
                              DO I = 1 BY 13 TO RLD_LIST_ITEM_CNT - 12;
                                   SEQ# = SEQ# + 1;
                                   LOCATE RLD_OBJ FILE(SYSLIN);
                                   K,RLD_BYTE_CNT = 56;
                                   RLD_R_P_PTR = ADDR(RLD_BYTE(1));
                                   RLD_OBJ_POS_ID = 1B;
                                   RLD_OBJ_REL_ID = REPL_ESDID;
                                   DO J = I TO I + 12;
                                        RLD_F_A_PTR =
                                             ADDR(RLD_BYTE((J-I)*4+5));
                                        RLD_OBJ_FLAGS =
                                             RLD_LIST_FLAGS(J);
                                        IF SUBSTR(RLD_OBJ_FLAGS,1,4) =
                                                  '0010'B THEN
                                             SUBSTR(RLD_OBJ_FLAGS,1,4) =
                                                  (4)'0'B;
                                        RLD_OBJ_ADDR =
                                             RLD_LIST_ADCON_ADDR(J);
                                   END;
                                   SUBSTR(RLD_OBJ_FLAGS,8,1) = '0'B;
                                   NBYTES = NBYTES - 52;
                              END;
                              IF NBYTES <= 4 THEN
                                   LEAVE;
                              IF (K + NBYTES) > 56 THEN
                              DO;
                                   SEQ# = SEQ# + 1;
                                   LOCATE RLD_OBJ FILE(SYSLIN);
                                   K = 0;
                              END;
                              RLD_R_P_PTR = ADDR(RLD_BYTE(K+1));
                              RLD_OBJ_POS_ID = 1B;
                              RLD_OBJ_REL_ID = REPL_ESDID;
                              L = K - J*4 + 5;
                              DO I = J TO RLD_LIST_ITEM_CNT;
                                   RLD_F_A_PTR =
                                             ADDR(RLD_BYTE(I*4+L));
                                   RLD_OBJ_FLAGS = RLD_LIST_FLAGS(I);
                                   IF SUBSTR(RLD_OBJ_FLAGS,1,4) =
                                             '0010'B THEN
                                        SUBSTR(RLD_OBJ_FLAGS,1,4) =
                                             (4)'0'B;
                                   RLD_OBJ_ADDR =
                                             RLD_LIST_ADCON_ADDR(I);
                              END;
                              SUBSTR(RLD_OBJ_FLAGS,8,1) = '0'B;
                              RLD_BYTE_CNT,K = K + NBYTES;
                         END;
                    END; /* DO RLD_LIST_PTR = */
                    SEQ# = SEQ# + 1;
                    LOCATE END_OBJ_TYPE_2 FILE(SYSLIN);
                    IF EP_ESDID = CESD_ESDID THEN
                    DO;
                         END_EP_ESDID = 1B;
                         END_EP_OFFSET =
                              SUBSTR(UNSPEC(EP_OFFSET),9,24);
                    END;
                    DO IDR_LIST_PTR = IDR_HEAD_PTR
                              REPEAT IDR_NEXT_PTR
                              WHILE(IDR_LIST_PTR ¬= NULL)
                              UNTIL(IDR_LIST_ESDID = CESD_ESDID);
                    END;
                    IF IDR_LIST_PTR ¬= NULL THEN
                    DO;
                         PUT STRING(END_IDR_CNT) EDIT
                         (IDR_LIST_TRANS_CNT)
                         (P'9');
                         DO I = 1 TO IDR_LIST_TRANS_CNT;
                              END_IDR_DATA(I) = IDR_DATA(I), BY NAME;
                              UNSPEC(END_IDR_VVMM(I)) =
                                             '1111'B || IDR_VVMM(I,1) ||
                                             '1111'B || IDR_VVMM(I,2) ||
                                             '1111'B || IDR_VVMM(I,3) ||
                                             '1111'B || IDR_VVMM(I,4);
                         END;
                    END;
                    DO IDR_USER_PTR = IDR_USER_HEAD_PTR
                              REPEAT IDR_USER_NEXT_PTR
                              WHILE(IDR_USER_PTR ¬= NULL)
                              UNTIL(IDR_USER_LIST_ESDID = CESD_ESDID);
                    END;
                    IF IDR_USER_PTR ¬= NULL THEN
                    DO;
                         L = INDEX(CESD_NAME,' ');
                         IF L = 0 THEN
                              L = 8;
                         ELSE
                              L = L + -1B;
                         LOCATE CARD FILE(SYSLIN);
                         CARD = ' IDENTIFY ' || SUBSTR(CESD_NAME,1,L) ||
                              '(''' || IDR_USER_LIST_DATA || ''')';
                    END;
                    IF PDS2PAGA &
                              SUBSTR(CESD_ADDR,13,12) = (12)'0'B THEN
                    DO;
                         LOCATE CARD FILE(SYSLIN);
                         CARD = ' PAGE ' || CESD_NAME;
                    END;
               END; /* IF CESD_DELINK_FLAG */
          END; /* DO CESD_LIST_PTR = */
 /*GP*/   IF LIST_TYPE_NO = 1B   /* FOR SELECT(ALL) ADD LOAD MODULE */
 /*  */   THEN DO;               /* ATTRS BEFORE "AFTER" LINKCNTL   */
 /*  */        LOCATE CARD FILE(SYSLIN);
 /*  */        CARD = ' SETOPT PARM(REUS(' || REUS_ATR || '),'
 /*  */                                    || LOAD_ATR || ')';
 /*  */        LOCATE CARD FILE(SYSLIN);
 /*  */        SELECT(AMODE);
 /*  */             WHEN('01'B)
 /*  */             DO;
 /*  */                  CARD = ' MODE AMODE(64),RMODE(24)';
 /*  */                  IF PDSLRMOD THEN
 /*  */                       SUBSTR(CARD,23,4) = 'ANY)';
 /*  */             END;
 /*  */             WHEN('10'B)
 /*  */             DO;
 /*  */                  CARD = ' MODE AMODE(31),RMODE(24)';
 /*  */                  IF PDSLRMOD THEN
 /*  */                       SUBSTR(CARD,23,4) = 'ANY)';
 /*  */             END;
 /*  */             WHEN('11'B)
 /*  */                  CARD = ' MODE AMODE(ANY)';
 /*  */             OTHERWISE
 /*  */                  CARD = ' MODE AMODE(24)';
 /*  */        END; /* SELECT(AMODE) */
 /*  */        IF PDSAPFLG THEN
 /*  */        DO;
 /*  */             IF PDSAPFCT = '00000001'B  &  PDSAPFAC = '01'X
 /*  */             THEN DO;
 /*  */                  LOCATE CARD FILE(SYSLIN);
 /*  */                  CARD = ' SETCODE AC(1) ';
 /*  */             END;
 /*  */        END;
 /*  */        IF PDS2SSI
 /*  */        THEN DO;
 /*  */             LOCATE CARD FILE(SYSLIN);
 /*  */             CARD = ' SETSSI ' || SSI_DATA;
 /*  */        END;
 /*GP*/   END;   /* LIST_TYPE_NO = 1 */
          IF MORE_LINKCNTL & SUBSTR(LINKCNTL_REC,1,6) = 'AFTER ' THEN
          DO;
               READ FILE(LINKCNTL) SET(LINKCNTL_PTR);
               DO WHILE(MORE_LINKCNTL &
                         SUBSTR(LINKCNTL_REC,1,8) ¬= 'MEMBER: ');
                    WRITE FILE(SYSLIN) FROM(LINKCNTL_REC);
                    AFTER_CNT = AFTER_CNT + 1;
                    READ FILE(LINKCNTL) SET(LINKCNTL_PTR);
               END;
          END;
          IF ¬ NONAME_FLAG THEN /* ADD LINKCNTL AND NAME */
          DO;
               LOCATE CARD FILE(SYSLIN);
               CARD = ' NAME ' || NEW_NAME || '(R)';
          END;
          IF CSECT_COUNT > 0B & ¬ ALL(CSECT_MATCHED(*)) THEN
          DO;
               PUT FILE(SYSPRINT) SKIP(2) EDIT
               ('CSECT NAMES NOT MATCHED : ')
               (A);
               DO I = 1 TO CSECT_COUNT;
                    IF ¬ CSECT_MATCHED(I) THEN
                         PUT FILE(SYSPRINT) EDIT
                         (CSECT_NAME(I))
                         (A(11));
               END;
               RETURN_CODE = MAX(RETURN_CODE,4);
          END;
          PAGE_TYPE = 0;
          IF MASK_COUNT > 0 THEN
               PUT FILE(SYSPRINT) SKIP(2) EDIT
               (('MASK','MATCHES' DO I = 1 TO 6))
               (X(2),A,X(4),A,X(3))
               ((MASK_NAME(I),MASK_MATCH_COUNT(I)
                         DO I = 1 TO MASK_COUNT))
               (SKIP,6(A,X(2),F(6),X(4)));
          IF BEFORE_CNT ¬= 0 | AFTER_CNT ¬= 0 THEN
               PUT FILE(SYSPRINT) SKIP(2) EDIT
               (BEFORE_CNT,
                    ' LINK DIRECTIVES INSERTED BEFORE OBJECT CODE AND ',
                    AFTER_CNT,
                    ' LINK DIRECTIVES INSERTED AFTER OBJECT CODE.')
               (F(5),A);
     END;
0    WHEN(8,12) /* MEMBER NOT FOUND */
     DO;
          PUT FILE(SYSPRINT) SKIP(2) EDIT
          ('** ERROR ** MEMBER ',MEMBER,' NOT FOUND.')
          (A);
          RETURN_CODE = MAX(RETURN_CODE,8);
     END;
0    WHEN(16) /* UNABLE TO OPEN PDS */
     DO;
          PUT FILE(SYSPRINT) SKIP(2) EDIT
          ('** ERROR ** UNABLE TO OPEN SYSLIB.')
          (A);
          RETURN_CODE = MAX(RETURN_CODE,20);
     END;
0    WHEN(28) /* I/O ERROR */
     DO;
          PUT FILE(SYSPRINT) SKIP(2) EDIT
          ('** ERROR ** I/O ERROR READING SYSLIB DIRECTORY.')
          (A);
          RETURN_CODE = MAX(RETURN_CODE,24);
     END;
0    OTHERWISE
          CALL PLIDUMP('TFSHB',
                    'RETURN CODE '||PLIRETV||' FROM DWNSPDSR');
     END; /* SELECT(PLIRETV) */
1    /* NOW WE'LL CLEAN UP THE MEMORY WE'VE BEEN USING */
0    DO WHILE(ALLOCN(CSECT_ARRAY) > 0);
          FREE CSECT_ARRAY;
     END;
0    DO WHILE(ALLOCN(MASK_ARRAY) > 0);
          FREE MASK_ARRAY;
     END;
0    DO WHILE(CESD_HEAD_PTR ¬= NULL);
          CESD_LIST_PTR = CESD_HEAD_PTR;
          CESD_HEAD_PTR = CESD_NEXT_PTR;
          FREE CESD_LIST_ITEM;
     END;
0    DO WHILE(RLD_HEAD_PTR ¬= NULL);
          RLD_LIST_PTR = RLD_HEAD_PTR;
          RLD_HEAD_PTR = RLD_NEXT_PTR;
          FREE RLD_LIST_ITEM;
     END;
0    DO WHILE(TXT_HEAD_PTR ¬= NULL);
          TXT_LIST_PTR = TXT_HEAD_PTR;
          TXT_HEAD_PTR = TXT_NEXT_PTR;
          FREE TXT_LIST_ITEM;
     END;
0    DO WHILE(IDR_HEAD_PTR ¬= NULL);
          IDR_LIST_PTR = IDR_HEAD_PTR;
          IDR_HEAD_PTR = IDR_NEXT_PTR;
          FREE IDR_LIST_ITEM;
     END;
0    DO WHILE(IDR_USER_HEAD_PTR ¬= NULL);
          IDR_USER_PTR = IDR_USER_HEAD_PTR;
          IDR_USER_HEAD_PTR = IDR_USER_NEXT_PTR;
          FREE IDR_USER_ITEM;
     END;
-    IF MORE_CARDS & RETURN_CODE < 20 THEN
          GO TO OBTAIN_A_CMD;
-END_OF_JOB :
     MEMBER_NAME = HIGH(8);
     CALL DWNSPDSR(SYSLIB,PDS_RET_CODE,RECLEN,LMOD_RECORD);
     RELEASE DWNSPDSR;
     CALL PLIRETC(RETURN_CODE);
     CLOSE FILE(SYSIN),
          FILE(SYSLIN),
          FILE(LINKCNTL),
          FILE(SYSPRINT);
     RETURN;
1EXTRACT_SIZE :
     PROC(SRC,BAD_SRC) RETURNS(BIN FIXED(31,0)) RED;
          DCL  SRC                 CHAR(*),
               BAD_SRC             BIT(1) ALIGNED;
0         DCL  (I,J,L)             BIN FIXED(15,0),
               RESULT              BIN FIXED(31,0) INIT(0B),
               (INDEX,LENGTH,VERIFY) BUILTIN;
          ON FOFL
               GO TO OVERFLOW_EXIT;
          BAD_SRC = '0'B;
          L = LENGTH(SRC);
0         SELECT;
          WHEN(VERIFY(SRC,'0123456789') = 0)
          DO;
               GET STRING(SRC) LIST(RESULT);
               IF RESULT > 16777216 THEN
                    BAD_SRC = '1'B;
          END;
0         WHEN(SUBSTR(SRC,L,1) = 'K' &
                    VERIFY(SUBSTR(SRC,1,L-1),'0123456789') = 0)
          DO;
               GET STRING(SUBSTR(SRC,1,L-1)) LIST(RESULT);
               IF RESULT > 16384 THEN
                    BAD_SRC = '1'B;
               ELSE
                    RESULT = RESULT*1024;
          END;
0         WHEN(SUBSTR(SRC,L,1) = 'M' &
                    VERIFY(SUBSTR(SRC,1,L-1),'0123456789') = 0)
          DO;
               GET STRING(SUBSTR(SRC,1,L-1)) LIST(RESULT);
               IF RESULT > 16 THEN
                    BAD_SRC = '1'B;
               ELSE
                    RESULT = RESULT*1024*1024;
          END;
0         WHEN(SUBSTR(SRC,1,2) = 'X''' &
                    SUBSTR(SRC,L,1) = '''' & L <= 9 &
                    VERIFY(SUBSTR(SRC,3,L-3),'0123456789ABCDEF') = 0)
          DO I = 3 TO L - 1;
               J = INDEX('0123456789ABCDEF',SUBSTR(SRC,I,1)) - 1;
               RESULT = RESULT*16 + J;
          END;
0         OTHERWISE
               BAD_SRC = '1'B;
          END;
          RETURN(RESULT);
0    OVERFLOW_EXIT :
          BAD_SRC = '1'B;
          RETURN(0B);
     END EXTRACT_SIZE;
1MASK_MATCH :
     PROC(CSECT_NAME,MASK_NAME) RED RETURNS(BIT(1) ALIGNED);
          DCL  (CSECT_NAME,MASK_NAME) CHAR(8),
0              P                   BIN FIXED(15,0),
               RESULT              BIT(1) ALIGNED INIT('1'B),
               (SUBSTR,VERIFY)     BUILTIN;
0         DO P = 1 TO 8 WHILE(RESULT);
               SELECT(SUBSTR(MASK_NAME,P,1));
               WHEN('?',SUBSTR(CSECT_NAME,P,1))
                    /* THIS CHARACTER'S OK */;
               WHEN('!')
               DO;
                    IF VERIFY(SUBSTR(CSECT_NAME,P,1),
                              'ABCDEFGHIJKLMNOPQRSTUVWXYZ') ¬= 0 THEN
                         RESULT = '0'B;
               END;
               WHEN('|')
               DO;
                    IF VERIFY(SUBSTR(CSECT_NAME,P,1),
                              '0123456789') ¬= 0 THEN
                         RESULT = '0'B;
               END;
               OTHERWISE /* NO MATCH */
                    RESULT = '0'B;
               END;
          END;
          RETURN(RESULT);
     END MASK_MATCH;
-END DELINK;
