KLAH     TITLE 'K A L A H -- ANCIENT EGYPTIAN BOARD GAME'
         SPACE 1
        PRINT  OFF
*** COPYRIGHT 1976 - REGENTS OF THE UNIVERSITY OF CALIFORNIA,     ***
*** CAMPUS COMPUTING NETWORK   (SHARE CODE 'UR')  -  1/15/76      ***
*** ALTHOUGH WE ARE RUNNING THIS MODULE IN OUR PRODUCTION SYSTEM, ***
*** NO WARRANTY, EXPRESSED OR IMPLIED, IS MADE BY CCN AS TO THE   ***
*** FUNCTIONING OR CORRECTNESS OF THE FOLLOWING CODE.             ***
         MACRO
&LAB    @MSG   &TEXT
         GBLC  &LABL1,&LABL2
         GBLA  &LEN(30),&I,&L
         AIF   ('&LAB' EQ '').GO
&I       SETA  0
&LABL1   SETC  '&LAB'
&LABL2   SETC  '&LAB&SYSNDX'
&LABL2   EQU   *
.GO      ANOP
&L       SETA  K'&TEXT-2
&I       SETA  &I+1
&LEN(&I) SETA  &L
         DC    C&TEXT
         MEND
         MACRO
        @MSGGEN &DUMMY
         GBLC  &LABL1,&LABL2
         GBLA  &LEN(30),&I,&L
&LABL1   DC    A(&LABL2)
         DC    AL2(&I)
&L       SETA  1
.LOOP    AIF   (&L GT &I).EXIT
         DC    AL2(&LEN(&L))
&L       SETA  &L+1
         AGO   .LOOP
.EXIT    MEND
         MACRO
&NAME   #CALL  &WHERE
         AIF   ('&WHERE' NE '').W1
         MNOTE 8,'--- "WHERE" OPERAND MISSING ---'
         MEXIT
.W1      AIF   ('&WHERE'(1,1) EQ '(').W2
&NAME    L     R15,=A(&WHERE)      GET ENTRY POINT
         AGO   .W3
.W2      AIF   ('&WHERE' EQ '(15)').W4
         AIF   ('&WHERE' EQ '(R15)').W4
&NAME    LR    R15,&WHERE          SET ENTRY POINT
.W3      BASR  R14,R15             GO TO ROUTINE
         MEXIT
.W4      ANOP
&NAME    BASR  R14,R15             GO TO ROUTINE
         MEND
         MACRO
&NAME   #DSP   &A,&N
&NAME    BAS   R14,&A
         NOP   &N*4
         MEND
         MACRO
&NAME   #XENT  &DUMMY
         CNOP  0,8
&NAME    STM   R14,R12,12(R13)     SAVE REGISTERS
         B     16(,R15)            BRANCH AROUND ID
         DC    CL8'&NAME'          IDENTIFIER
         LR    R8,R15              SET BASE REGISTER
         USING &NAME,R8            SET ADDRESSABILITY
         LR    R15,R13             PREVIOUS SAVE AREA
         LA    R13,18*4(R13)       NEW CURRENT SAVE AREA
         ST    R13,8(R15)          LINK SAVE AREAS
         ST    R15,4(R13)
         SPACE 1
         MEND
         MACRO
&NAME   #XRET  &RC=
&NAME    L     R13,4(R13)          PREVIOUS SAVE AREA
         AIF   ('&RC' EQ '').N1
         AIF   ('&RC'(1,1) EQ '(').N2
         AIF   ('&RC' NE '0').N3
.N1      XR    R15,R15             SET RC=0
         ST    R15,16(R13)         STORE IT (R15)
         AGO   .N4
.N2      ST    &RC(1),16(R13)      STORE RC (R15)
         AGO   .N4
.N3      MVC   16(4,R13),=AL4(&RC) SET RC (R15)
.N4      LM    R14,R12,12(R13)     RESTORE REGISTERS
         MVI   12(R13),X'FF'       SET RETURN INDICATOR
         BR    R14                 RETURN
         SPACE 1
         MEND
         MACRO
        #XEND  &DUMMY
        LTORG  ,                   LITERALS
         SPACE 1
         DROP  R8                  END OF LOCAL ADDRESSABILITY
         MEND
        PRINT  ON
         SPACE 1
KALAH    START 0
         SPACE 1
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
*   KALAH :    THIS PROGRAM ALLOWS A USER TO PLAY THE COMPUTER IN     *
*              THE GAME OF KALAH. THE USER IS ASKED VARIOUS INITIAL   *
*              PARAMETERS VALUES AND THEN THE PLAY ALTERNATES WITH    *
*              THE USER GIVING HIS MOVES AS PIT NUMBERS.              *
*   AUTHOR :   DON D WORTH                                            *
*              FULL SCREEN BY : MOINIL P.A.                           *
*                               COMPUTING CENTRE                      *
*                               J.R.C. - ISPRA ESTABLISHMENT          *
*                               21020 ISPRA (VA), ITALY               *
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
         SPACE 1
        PRINT  NOGEN
        $DEFREG
*------- MAIN ENTRY, INITIALIZE
*        HOUSEKEEPING
         SPACE 1
        $XENT  BASE=(R11,R12),LV=DATALEN,TYPE=RENT
         LR    R9,R13              SET DATA ADDRESSABILITY
         USING DATA,R9
         MVC   EXTR(LEXTR),EXTRP
         XC    ATSO,ATSO
        EXTRACT ATSO,'S',FIELDS=(TSO),MF=(E,EXTR)
         L     R2,ATSO
         TM    0(R2),X'80'
         BZ    NOTTSO              WE ARE'NT IN TSO
         XC    EXTR(LEXTR),EXTR
         XC    ATSO,ATSO
        GTSIZE ,
         LTR   R15,R15
         BNZ   ERGTSZ              ERROR RETURN CODE
         LTR   R15,R0
         BZ    NDTERM              NOT DISPLAY TERMINAL
         CL    R1,=F'80'           TEST LINE LENGTH
         BE    TSTSC
         CL    R1,=F'132'
         BNE   NDTERM
         CL    R0,=F'27'           TEST NUMBER OF LINES
         BNE   NDTERM
         B     SETSCT
TSTSC    CL    R0,=F'24'
         BE    SETSCT
         CL    R0,=F'32'
         BE    SETSCT
         CL    R0,=F'43'
         BNE   NDTERM
SETSCT   OI    FLAGS,SCTERM
         B     NDTERM+L'NDTERM
NDTERM   NI    FLAGS,255-SCTERM
         XC    LIST0(5*4),LIST0    RESET LIST
         MVI   VPL,C'B'            LET HIM GO FIRST
         MVC   MAXLV(2),=H'2'      DEFAULT LEVEL
         MVC   PPP(2),=H'6'        DEFAULT PEBS/PIT
         LA    R1,INMSG            INITIAL MESSAGE
         LA    R0,LGINMSG          LENGTH
        #DSP   DSPLAY,0            GIVE IT TO HIM          CALL FS = 0
         LA    R1,REPLY            WHERE TO REPLY
         ST    R1,LIST0+2*4        INTO LIST
         LA    R1,L'REPLY          READ LENGTH
         ST    R1,LIST0+3*4        INTO LIST
         SPACE 1
*------- ASK USER FOR NEW GAME PARAMETERS
*        SEE IF USER WANTS TO CHANGE DEFAULTS
         SPACE 1
NEWGAME  L     R1,=A(ASKPARMS)     FIND MESSAGE
         LA    R0,LASKPARM         LENGTH
        #DSP   DSPLAY,1            ASK HIM                 CALL FS = 1
         MVI   OPTS,0              NO OPTIONS SELECTED YET
         LA    R1,REPLY            FIND STRING
         L     R0,LIST0+4*4        GET READ LENGTH
         LTR   R0,R0               NULL?
         BNP   START               LEAVE THINGS ALONE
SELOPTS  CLI   0(R1),C'L'          LOOK-LEVEL?
         BNE   *+L'*+4             NO
         OI    OPTS,OPTSL          YES
         CLI   0(R1),C'P'          PEBBLES PER PIT?
         BNE   *+L'*+4             NO
         OI    OPTS,OPTSP          YES
         CLI   0(R1),C'T'          TRACE LEVEL?
         BNE   *+L'*+4             NO
         OI    OPTS,OPTST          YES
         CLI   0(R1),C'W'          WHO FIRST?
         BNE   *+L'*+4             NO
         OI    OPTS,OPTSW          YES
         CLI   0(R1),C'B'          BOARD LAYOUT?
         BNE   *+L'*+4             NO
         OI    OPTS,OPTSB          YES
         CLI   0(R1),C'V'          VERIFY MOVES?
         BNE   *+L'*+4             NO
         OI    OPTS,OPTSV          YES
         LA    R1,1(R1)            NEXT CHAR
         BCT   R0,*+L'*+4          GO ON
         B     SELEND
         CLI   0(R1),C' '
         BNE   *-16
         LA    R1,1(R1)            NEXT CHAR
         BCT   R0,*+L'*+4          GO ON
         B     SELEND
         CLI   0(R1),C' '
         BE    *-16
         B     SELOPTS             GO LOOK
SELEND   CLI   OPTS,0              NOTHING SELECTED?
         BE    START               NOPE, SKIP ALL OF EM
         SPACE 1
*------- ASK USER FOR LOOK-AHEAD LEVEL
         SPACE 1
         TM    OPTS,OPTSL          ASK LEVEL?
         BZ    REASKPPP            NO
         LA    R0,LASKLHL          USE FULL LENGTH
         B     RELHL+L'RELHL       SKIP LENGTH LOAD
RELHL    LA    R0,80               MINIMUM LENGTH
         L     R1,=A(ASKLHL)       GET MESSAGE
        #DSP   DSPLAY,2                                    CALL FS = 2
         TM    REPLY,X'F0'         NUMERIC VALUE?
         BNO   RELHL               NO
         PACK  DBLW(8),REPLY(1)    CONVERT IT
         CVB   R1,DBLW             GET IT
         LTR   R1,R1               MUST BE NON-ZERO
         BZ    RELHL               NO GOOD
         STH   R1,MAXLV            SAVE IT
         SPACE 1
*------- ASK FOR NUMBER OF PEBBLES PER PIT
         SPACE 1
REASKPPP TM    OPTS,OPTSP          PEBBLES PER PIT
         BZ    REVER               NO, GO ON
         LA    R1,ASKPPP           WHERE IS MESSAGE
         LA    R0,L'ASKPPP         LENGTH
        #DSP   DSPLAY,2            GO GET NUMBER           CALL FS = 2
         TM    REPLY,X'F0'         NUMERIC?
         BNO   REASKPPP            NO
         PACK  DBLW(8),REPLY(1)    GET IT
         CVB   R1,DBLW             LOAD INTO REG
         STH   R1,PPP              SAVE FOREVER
         SPACE 1
*------- ASK USER IF HE WANTS VERIFICATION
         SPACE 1
REVER    TM    OPTS,OPTSV          VERIFY?
         BZ    RETRAC              NO
         LA    R1,VERMSG
         LA    R0,L'VERMSG
        #DSP   DSPLAY,2            ASK HIM                 CALL FS = 2
         CLI   REPLY,C'N'          NO?
         BNE   *+L'*+8
         NI    FLAGS,255-VERIFY    NO MORE VERIFY
         B     RETRAC
         CLI   REPLY,C'Y'          YES?
         BNE   REVER               NOPE, ITS ????
         OI    FLAGS,VERIFY        SET FLAG
         SPACE 1
*------- ASK FOR TRACE LEVEL MAXIMUM
         SPACE 1
RETRAC   TM    OPTS,OPTST          TRACE?
         BZ    REFIRST             NO
         LA    R1,ASKTRAC
         LA    R0,LASKTRAC         GET MESSAGE LENGTH
        #DSP   DSPLAY,2            ASK HIM                 CALL FS = 2
         CLI   REPLY,C'R'          RESET?
         BNE   *+L'*+12            NO
         MVI   TRACE,0             RESET COMPARES
         NI    FLAGS,255-TESTING   SHUT IT UP
         B     REFIRST
         TM    REPLY,X'F0'         OK?
         BNO   RETRAC              NO
         PACK  DBLW(8),REPLY(1)    GET IT
         CVB   R1,DBLW             GET IT IN REG
         STC   R1,TRACE            SAVE FOR COMPARES
         OI    FLAGS,TESTING       DO IT EACH TURN
         SPACE 1
*------- ASK WHO GOES FIRST
         SPACE 1
REFIRST  TM    OPTS,OPTSW          WHO FIRST?
         BZ    REBOARD             NO
         LA    R1,ASKFIR
         LA    R0,L'ASKFIR
        #DSP   DSPLAY,2                                    CALL FS = 2
         CLI   REPLY,C'M'          HIM?
         BNE   *+L'*+8             NO
         MVI   VPL,C'B'            SET HIM FIRST
         B     REBOARD
         CLI   REPLY,C'Y'          ME?
         BNE   REFIRST             NO, INVALID
         MVI   VPL,C'A'            SET ME FIRST
         SPACE 1
*------- ASK FOR INTITIAL BOARD SETTINGS
         SPACE 1
REBOARD  TM    OPTS,OPTSB          BOARD SETUP?
         BZ    START               NOPE
         LA    R1,ASKBRD           MESSAGE
         LA    R0,LASKBRD          LENGTH
        #DSP   DSPLAY,2            GO ASK                  CALL FS = 2
         L     R0,LIST0+4*4        GET REPLY LENGTH
         LTR   R0,R0               NULL?
         BZ    REBOARD             YES, ASK AGAIN
         LA    R1,REPLY            FIND REPLY BUFFER
         LA    R2,VARR             FIRST PIT
         LA    R15,14              NUMBER OF PITS I NEED
FILLPITS TM    0(R1),X'F0'         NUMERIC YET?
         BO    GOTDIG              YES
         LA    R1,1(R1)            ELSE, SKIP AHEAD
         BCT   R0,FILLPITS         GO ON
         B     REBOARD             NOT ENOUGH
GOTDIG   LR    R14,R1              SAVE START
GOTDIG1  TM    0(R1),X'F0'         NUMERIC?
         BNO   GOTBLK              NO
         LA    R1,1(R1)            NEXT CHAR
         BCT   R0,GOTDIG1          DO IT
GOTBLK   LR    R3,R1               GET FINAL PTR
         SR    R3,R14              FIND TRUE LENGTH
         CH    R3,=H'3'            TOO LARGE?
         BH    REBOARD             YES, UGLY
         BCT   R3,*+L'*+6          EXECUTE LENGTH
         PACK  DBLW(8),0(0,R14)         << EXECUTED >>
         EX    R3,*-6              CONVERT TO DECIMAL
         CVB   R3,DBLW             GET IT IN REG
         STC   R3,0(R2)            SAVE IN ARRAY
         LA    R2,1(R2)            NEXT ARRAY MEMBER
         BCT   R15,FILLPITS        CONTINUE
         MVC   SCREEN+480(80),BLANKS
         MVC   SCREEN+480(14),=C'IS THIS RIGHT?'
         LA    R0,480+14           LENGTH
         XR    R1,R1               FIRST BOARD
        #DSP   DBOARD,2            DISPLAY BOARD           CALL FS = 2
BOARDOK  CLI   REPLY,C'Y'          YES?
         BE    START1              YES, GO TO IT
         CLI   REPLY,C'N'          NO?
         BE    REBOARD             NO, GO ASK AGAIN
         LA    R0,14               LENGTH
         LA    R1,SCREEN+480       MESSAGE
        #DSP   DSPLAY,3            ASK AGAIN               CALL FS = 3
         B     BOARDOK             SEE IF SO
         SPACE 1
*------- SET UP INITIAL BOARD
         SPACE 1
START    LA    R15,VARR            FIND BOARD ARRAY
         LH    R1,PPP              GET PEBBLES PER PIT
         LA    R0,14               NUMBER OF PITS
         STC   R1,0(R15)           FILL A PIT
         LA    R15,1(R15)          NEXT PIT
         BCT   R0,*-8
         MVI   VAK,0               NOTHING IN KALAHS
         MVI   VBK,0               EITHER ONE
START1   CLI   VPL,C'A'            WHO FIRST?
         BE    MEFIRST             ME
         SPACE 1
*------- GIVE HIM A MOVE FIRST
*        ASK USER FOR HIS MOVE
         SPACE 1
DISP     MVC   SCREEN+480(80),BLANKS
         MVC   SCREEN+480(L'YOURMOVE),YOURMOVE
         LA    R1,SCREEN+480+L'YOURMOVE WHERE TO PUT WISE CRACK
DISP1    OC    VB1(6),VB1          CAN HE MOVE AT ALL?
         BZ    ENDGAME             NO, STOP THEN
         CLC   VAK(1),VBK          COMPARE SCORES SO FAR
         BE    DISP2               NECK AND NECK
         MVI   0(R1),C','          I CAN'T RESIST...
         LA    R1,2(R1)            I HAVE TO SAY SOMETHING
         L     R0,=A(MSGA)         ASSUME I'M WINNING
         BH    *+L'*+4             YES, I AM
         L     R0,=A(MSGB)         NO I'M NOT
         BAS   R14,WISECRAK        GENERATE SOMETHING
DISP2    LA    R0,SCREEN           FIND LENGTH
         SR    R1,R0               GET IT
         LR    R0,R1
         XR    R1,R1               SHOW MAIN BOARD
        #DSP   DBOARD,4            DO IT TO IT             CALL FS = 4
DISP3    OC    LIST0+4*4(4),LIST0+4*4 NULL READ?
         BZ    DISP                THEN RE-DISPLAY SCREEN
         TM    REPLY,X'F0'         NUMERIC?
         BO    PITOK1              YES
NONO     LA    R1,BADPIT           NO, UGLY
         LA    R0,L'BADPIT         LENGTH OF MESSAGE
        #DSP   DSPLAY,5            GIVE ERROR, GET REPLY   CALL FS = 5
         B     DISP3               CHECK NEW REPLY
PITOK1   PACK  DBLW(8),REPLY(1)    GET IT
         CVB   R1,DBLW             CONVERT TO REG
         LTR   R1,R1               HOWS IT LOOK?
         BNP   NONO                ICKY
         CH    R1,=H'6'            MUST BE WITHIN 1-6 RANGE
         BH    NONO                ICKY
         LA    R15,7               GET A CONSTANT
         SR    R15,R1              GET INVERSE OF HIS NUM SYSTEM
         STC   R15,VCP             SET UP FOR THE DRIBBLE
         XR    R1,R1               DO THE MAIN BOARD
         MVI   VPL,C'B'            ITS HIS TURN
         BAS   R14,DRIBBLE         MAKE HIS MOVE
         B     NONO                +0 - EMPTY PIT
         SPACE 1
*------- DO VERIFICATION OF USER'S MOVE, HANDLE EXTRA MOVES
         SPACE 1
         TM    FLAGS,VERIFY        +4 - VERIFY HIS MOVES FOR HIM?
         BZ    NOVER               NOPE
         CLC   VCP(1),VERPIT       THIS WHAT I EXPECTED?
         BE    NOVER               YES, GO ON
         CLI   VERPIT,0            FIRST MOVE?
         BE    NOVER
         MVC   MSWRK(12),=C'I GUESSED XX'
         IC    R15,VERPIT          GET WHAT I THOT
         LA    R14,7               GET A CONSTANT
         SR    R14,R15             CONVERT NUMBER SYSTEMS
         CVD   R14,DBLW            CONVERT NUMBER
         UNPK  MSWRK+10(2),DBLW(8) MOVE TO LINE
         OI    MSWRK+11,X'F0'      FIX THE SIGN
         LA    R1,MSWRK
         LA    R0,12               LENGTH
         MVC   SVRDP(L'SVRDP),LIST0+2*4 SAVE REPLY ADDRESS/LENGTH
         XC    LIST0+2*4(2*4),LIST0+2*4 NO READ
        #DSP   DSPLAY,6            SEND THE MESSAGE        CALL FS = 6
         MVC   LIST0+2*4(L'SVRDP),SVRDP RESTORE REPLY ADDRESS/LENGTH
NOVER    CLI   VPL,C'B'            STILL HIM?
         MVI   VERPIT,0            I HAVE NO EXPECTATIONS
         BNE   MEFIRST             IF NOT HIM, MY TURN
         MVC   SCREEN+480(80),BLANKS ELSE, SET UP TO ASK FOR NOTHER
         MVC   SCREEN+480(L'BAGAIN),BAGAIN
         LA    R1,SCREEN+480+L'BAGAIN
         B     DISP1               LET HIM GO AGAIN
         SPACE 1
*------- MY TURN, SEE IF I HAVE NO CHOICES
*        MY TURN, SHOW BOARD FIRST
         SPACE 1
MEFIRST  MVC   SCREEN+480(80),BLANKS
         MVC   SCREEN+480(L'MYMOVE),MYMOVE
         LA    R1,SCREEN+480+L'MYMOVE
MEFIRST1 OC    VA1(6),VA1          CAN I MOVE AT ALL?
         BZ    ENDGAME             NO, STOP THEN
         L     R0,=A(MSGD)         HERE ARE WISECRACKS
         BAS   R14,WISECRAK        DO IT
         LA    R0,SCREEN           START OF DISPLAY
         SR    R1,R0               LENGTH
         LR    R0,R1
         XR    R1,R1               DO MAIN BOARD
         MVC   SVRDP(L'SVRDP),LIST0+2*4 SAVE REPLY ADDRESS/LENGTH
         XC    LIST0+2*4(2*4),LIST0+2*4 NO READ
        #DSP   DBOARD,7            SHOW BOARD, GO ON...    CALL FS = 7
         MVC   LIST0+2*4(L'SVRDP),SVRDP RESTORE REPLY ADDRESS/LENGTH
         SPACE 1
*------- SEE IF I HAVE A CHOICE IN MOVES
         SPACE 1
         XR    R1,R1               POINT TO MY FIRST PIT
         STC   R1,VERPIT           NO PIT FOR VERIFY YET
         XR    R15,R15             CLEAR SEARCH REG
         LA    R0,6                NUMBER OF PITS
         BCTR  R15,0               ITS EMPTY
SEARCH   LA    R14,VARR(R1)        FIND THIS PIT
         CLI   0(R14),0            EMPTY?
         BE    SEARON              YES, CONTINUE
         LTR   R15,R15             TEST FOR PREVIOUS HIT
         BNZ   CHOSE               MORE THAN ONE
         LR    R15,R1              ELSE, SAVE ITS INDEX
SEARON   LA    R1,1(R1)            NEXT ONE
         BCT   R0,SEARCH           CONTINUE SEARCHING
         LA    R15,1(R15)          NOT AN OFFSET
         STC   R15,VOP             SAVE THE ONE I FOUND
         B     CHOSEN              GO USE IT
*------- OPTIMIZE MY MOVE
*        INITIALIZE THE ROOT VERTEX BLOCK
*              R5 = LAST NODE INDEX VALUE
*              R6 = NEXT NODE INDEX VALUE (R5+1)
         SPACE 1
CHOSE    XR    R5,R5               LAST PTR IS ROOT
         LH    R6,VBLEN            NEXT IS FIRST
         MVI   VLV,0               LEVEL IS 0
         MVI   VOP,0               NO OPTIMUM PIT YET
         MVC   VOS(2),NONE         NO OPTIMUM SCORE
         MVI   VPL,C'A'            MAKE SURE OF THE PLAYER
         MVI   VCP,1               START WITH FIRST PIT
         SPACE 1
*------- GO UP TO NEXT NODE IN THE GAME TREE
         SPACE 1
NEXTUP   XR    R1,R1               CLEAR FOR AN IC
         IC    R1,VLV(R5)          GET LAST LEVEL
         LA    R1,1(R1)            NEXT LEVEL
         STC   R1,VLV(R6)          SAVE FOR NEXT ONE
         CH    R1,MAXLV            TOO FAR?
         BH    BACKDOWN            YES, BACKDOWN THE TREE
         IC    R1,VCP(R5)          GET CHOSEN PIT
         STC   R1,VCP(R6)          COPY TO NEW NODE
         IC    R1,VPL(R5)          MERGE UP PLAYER...
         STC   R1,VPL(R6)          TEMPORARILY
         LA    R14,VARR(R5)        GET LAST ARRAY
         LA    R15,VARR(R6)        AND NEXT
         MVC   0(14,R15),0(R14)    COPY BOARD UP
         LR    R1,R6               DO IT TO NEXT
         BAS   R14,DRIBBLE         MAKE LAST NODE'S MOVE TO NEXT
         B     ABORT               +0 - INVALID TRY, BACK OFF
         XR    R1,R1               +4 - GET A ZERO
         STC   R1,VCP(R6)          NO CURRENT PIT YET
         STC   R1,VOP(R6)          NO OPTIMUM PIT
         LA    R1,VOS(R6)          FIND OPTIMUM SCORE
         MVC   0(2,R1),NONE
         LA    R14,VA1(R6)         FIND THE ONE I JUST DID
         OC    0(6,R14),0(R14)     END OF GAME SITUATION?
         BNZ   TRYONE              NO, CONTINUE
         LA    R15,VCP(R6)         FIND CURRENT PTR
         MVI   0(R15),6            TRIED LAST PIT ALREADY
         AH    R6,VBLEN            NEXT NEXT
         AH    R5,VBLEN            NEXT LAST
         B     BACKDOWN            PRETEND EXCEEDED MAXLV
         SPACE 1
*------- TRY OUT A MOVE AND GO UP TREE
         SPACE 1
TRYONE   XR    R1,R1               CLEAR A REG
         IC    R1,VCP(R6)          GET LAST PIT NUMBER TRIED
         LA    R1,1(R1)            NEXT PIT
         STC   R1,VCP(R6)          SET IT UP
         CH    R1,=H'7'            TOO MUCH?
         BNL   DOWN                YES, ALL HAVE BEEN TRIED
         AH    R6,VBLEN            NEXT N
         AH    R5,VBLEN            AND L
         B     NEXTUP              GO UP ANOTHER NODE
         SPACE 1
*------- IF END OF BRANCH REACHED, START BACK DOWN
         SPACE 1
BACKDOWN XR    R1,R1               CLEAR FOR IC
         XR    R14,R14             CLEAR FOR ANOTHER ONE
         IC    R1,VAK(R5)          GET A KALAH VALUE
         IC    R14,VBK(R5)         AND B KALAH VALUE
         SR    R1,R14              GET SCORE
         STH   R1,VOS(R6)          UNTRIED NODE WORTH 0
         LA    R1,VPL(R5)          FIND THE PLAYER
         CLI   0(R1),C'A'          ME?
         BE    SCOREA              YES
         LH    R1,VOS(R6)          GET THE NEW SCORE
         SH    R1,=H'1'            HIS TURN, I LOSE A 1/2 POINT
         STH   R1,VOS(R6)          SAVE IT
         B     DOWN                CONTINUE DOWN TREE
SCOREA   LH    R1,VOS(R6)
         AH    R1,=H'1'            MY TURN, I GET A 1/2 POINT
         STH   R1,VOS(R6)          SAVE IT
         SPACE 1
*------- WHEN ALL POSIBILITIES HAVE BEEN TRIED, MERGE BACK BEST SCORE
         SPACE 1
DOWN     LTR   R6,R6               AT END?
         BZ    CHOSEN              YES, EXIT
         TM    FLAGS,TESTING       TESTING?
         BZ    NOTEST              NO
         LA    R15,VLV(R5)         FIND MERGED TO LEVEL
         CLC   0(1,R15),TRACE      DO THIS?
         BH    NOTEST              NO
         LH    R15,VOS(R6)         GET SCORE
         CVD   R15,DBLW            CONVERT IT
         MVC   MSWRK(8),=C'LN PN+VV' MOVE IN MASK
         UNPK  MSWRK+6(2),DBLW(8)  MOVE SCORE TO SCREEN
         OI    MSWRK+7,X'F0'       FIX THE SIGN
         IC    R1,VPL(R5)          GET PLAYER CODE
         LTR   R15,R15             NEGATIVE?
         BNM   *+L'*+4             NO
         MVI   MSWRK+5,C'-'        SHOW MINUS
         STC   R1,MSWRK+3          PUT IN LINE
         IC    R1,VCP(R5)          GET PIT CHOICE
         CLI   MSWRK+3,C'A'        MY MOVE?
         BE    TESTA               YES
         LR    R0,R1               SAVE IT
         LA    R1,7                GET COMPLEMENT
         SR    R1,R0               THIS WAY
TESTA    STC   R1,MSWRK+4          PUT IN LINE
         OI    MSWRK+4,X'F0'       FIX SIGN
         IC    R1,VLV(R5)          GET LEVEL NUMBER
         STC   R1,MSWRK+1          PUT IN LEVEL
         OI    MSWRK+1,X'F0'       FIX SIGN
         LA    R1,MSWRK            FIND TEXT
         LA    R0,8                LENGTH
         MVC   SVRDP(L'SVRDP),LIST0+2*4 SAVE REPLY ADDRESS/LENGTH
         XC    LIST0+2*4(2*4),LIST0+2*4 NO READ
        #DSP   DSPLAY,8            SEND THE MESSAGE        CALL FS = 8
         MVC   LIST0+2*4(L'SVRDP),SVRDP RESTORE REPLY ADDRESS/LENGTH
NOTEST   LA    R1,VOS(R5)          FIND PREVIOUS BEST SCORE
         LH    R15,VOS(R6)         PICK UP NEW ONE FOR MERGE
         CLC   0(2,R1),NONE        NO PREVIOUS?
         BE    MERGE               NONE, MERGE THEN
         LA    R1,VPL(R5)          WHOSE MOVE?
         CLI   0(R1),C'A'          MINE?
         LH    R14,VOS(R5)         GET VALUE FOR COMPARISON
         BE    *+L'*+6             IF ITS ME, DIFFERENT CRITERIA
         CR    R15,R14             VOS(N) > VOS(L)?
         B     *+L'*+2             CONTINUE
         CR    R14,R15             VOS(L) > VOS(N)?
         BH    ABORT               NOPE
MERGE    STH   R15,VOS(R5)         NEW SCORE
         IC    R1,VCP(R5)          LAST SELECTED PIT
         STC   R1,VOP(R5)          PRODUCED THIS SCORE
         TM    FLAGS,TESTING       TESTING?
         BZ    NOTEST1
         LA    R15,VLV(R5)         TEST THIS LEVEL
         CLC   0(1,R15),TRACE
         BH    NOTEST1             NOPE
         LA    R1,=CL6'PICKED'     INDICATE MERGE DONE
         LA    R0,6                LENGTH
         MVC   SVRDP(L'SVRDP),LIST0+2*4 SAVE REPLY ADDRESS/LENGTH
         XC    LIST0+2*4(2*4),LIST0+2*4 NO READ
        #DSP   DSPLAY,8            SEND THE MESSAGE        CALL FS = 8
         MVC   LIST0+2*4(L'SVRDP),SVRDP RESTORE REPLY ADDRESS/LENGTH
NOTEST1  LTR   R5,R5               MERGING TO END ONE?
         BNZ   ABORT               NO
         IC    R1,VOP(R6)          GET B'S BEST MOVE FOR THIS CASE
         STC   R1,VERPIT           AND SAVE IT
         SPACE 1
*------- BACK DOWN ONE NODE AND TRY AGAIN
         SPACE 1
ABORT    SH    R5,VBLEN            BACK DOWN LAST VERTEX
         SH    R6,VBLEN            AND NEXT
         BNM   TRYONE              IF NOT BACK TO START...
         SPACE 1
*------- MAKE MY MOVE
*        I'VE GOT A MOVE, MOVE IT THEN
         SPACE 1
CHOSEN   TM    FLAGS,TESTING+SCTERM TESTING ON SCREEN?
         BNO   SKIPTR
         TM    SWFSW,SWTRC
         BZ    SKIPTR
        #DSP   DSPLAY,9            LAST MESSAGE(S)         CALL FS = 9
SKIPTR   MVC   VCP(1),VOP          USE OPTIMUM PIT
         MVC   MSWRK(L'IMOVED),IMOVED GET TEXT
         XR    R1,R1               CLEAR FOR IC
         IC    R1,VCP              GET MY PIT NUMBER
         CVD   R1,DBLW             CONVERT IT
         UNPK  MSWRK+14(2),DBLW(8) MOVE CHOICE TO SCREEN
         OI    MSWRK+15,X'F0'      FIX THE SIGN
         LA    R0,L'IMOVED
         LA    R1,MSWRK            HERE IS MESSAGE
         MVC   SVRDP(L'SVRDP),LIST0+2*4 SAVE REPLY ADDRESS/LENGTH
         XC    LIST0+2*4(2*4),LIST0+2*4 NO READ
        #DSP   DSPLAY,6            SEND THE MESSAGE        CALL FS = 6
         MVC   LIST0+2*4(L'SVRDP),SVRDP RESTORE REPLY ADDRESS/LENGTH
         XR    R1,R1               DISPLAY MAJOR BOARD
         BAS   R14,DRIBBLE         MAKE MY MOVE
         EX    0,*                 +0 - THIS CAN'T HAPPEN (I HOPE)
         CLI   VPL,C'A'            +4 - ME AGAIN?
         BNE   DISP                NO, GO ON
         MVC   SCREEN+480(80),BLANKS
         MVC   SCREEN+480(L'AAGAIN),AAGAIN YES, I GET ANOTHER TURN
         LA    R1,SCREEN+480+L'AAGAIN
         B     MEFIRST1            GO DISPLAY AND ASK HIM
         SPACE 1
*------- GIVE END OF GAME STATISTICS
*        WHEN ENTIRE BOARD IS CLEAR, SHOW FINAL SCORE
         SPACE 1
ENDGAME  LA    R0,SCREEN
         LA    R14,GAMEMSG
         LA    R1,LGAMEMSG
         LR    R15,R1
         MVCL  R0,R14
         CLC   VAK(1),VBK          HOW'D HE/I DO?
         BE    ENDGAME1            NECK AND NECK
         MVI   SCREEN+89,C','       I WILL TALK
         LA    R1,SCREEN+91        WHERE GOES THE MESSAGE
         L     R0,=A(MSGE)         ASSUME I WON
         MVI   VPL,C'B'            HE GOES FIRST IF SO
         BH    IWIN                I DID
         MVI   VPL,C'A'            I GO FIRST IF HE WON
         L     R0,=A(MSGC)         HE WON
         MVC   0(13,R1),=C'GIVE THAT MAN'
         LA    R1,14(R1)           NEXT LOCATION
IWIN     BAS   R14,WISECRAK        MAKE A DIRTY CRACK
ENDGAME1 XR    R14,R14             CLEAR FOR IC
         XR    R15,R15             SAME HERE
         IC    R14,VAK             GET MY SCORE
         IC    R15,VBK             AND HIS
         CVD   R14,DBLW            CONVERT MINE
         MVC   SCREEN+326(3),=X'202120' EDIT MASK
         ED    SCREEN+325(4),DBLW+6 GET IT
         CVD   R15,DBLW            CONVERT HIS
         MVC   SCREEN+332(3),=X'202120' MOVE IN EDIT MASK
         ED    SCREEN+331(4),DBLW+6 CONVERT TO SCREEN
         AH    R14,AMATCH          MY MATCH SCORE
         AH    R15,BMATCH          HIS MATCH SCORE
         STH   R14,AMATCH          UPDATE THEM
         STH   R15,BMATCH          BOTH
         CVD   R14,DBLW            CONVERT MINE
         MVC   SCREEN+406(3),=X'202120' MOVE EDMSK TO SCREEN
         ED    SCREEN+405(4),DBLW+6 EDIT IT INTO LINE
         CVD   R15,DBLW            CONVERT HIS
         MVC   SCREEN+412(3),=X'202120' GET EDIT MASK
         ED    SCREEN+411(4),DBLW+6 CONVERT NUMBER
         CR    R14,R15             HOWS HE DOING?
         LA    R1,SCREEN+LGAMEMSG  ASSUME NECK AND NECK
         BE    EGAME               THEY ARE EQUAL
         MVC   0(2,R1),=C', '      OH BOY, I GET TO CRACK
         LA    R1,2(R1)            JUMP TO SPOT
         L     R0,=A(MSGA)         ASSUME I'M WINNING
         BH    *+L'*+4             I AM
         L     R0,=A(MSGB)         I'M NOT
         BAS   R14,WISECRAK        GET A CRACK
EGAME    MVI   0(R1),C'?'          END THE SENTENCE
         LA    R1,1(R1)            COUNT IT
         LA    R0,SCREEN           BEGINNING OF DISPLAY
         SR    R1,R0               GET LENGTH
         LR    R0,R1               GET IN RIGHT REG
         LA    R1,SCREEN           WHERE IS MESSAGE?
REEG    #DSP   DSPLAY,10           ASK FOR NEXT ONE        CALL FS = 10
         CLI   REPLY,C'Y'          NEW GAME?
         BE    NEWGAME             YES,  GO TO IT
         CLI   REPLY,C'N'          NO?
         BE    LEAVE               THEN LEAVE
         L     R0,LIST0+4          GET WRITE LENGTH
         CL    R0,=A(7*80)         JUST LAST LINE DONE?
         BNH   *+L'*+4             YES
         S     R0,=A(7*80)         NO, JUST LAST LINE
         LA    R1,SCREEN           FIND IT
         A     R1,=A(7*80)
         B     REEG                ASK AGAIN
LEAVE    TM    FLAGS,SCTERM        FULL SCREEN?
         BZ    QUIT                NO
         B     RSTFS
         SPACE 1
*------- ERROR MESSAGE, EXIT
         SPACE 1
NOTTSO  TPUT   ERRMS0,L'ERRMS0
         B     QUIT
ERGTSZ   MVC   MSWRK(L'ERRMS1),ERRMS1
         MVC   MSWRK+5(6),=CL6'GTSIZE'
         CVD   R15,DBLW
         MVC   MSWRK+19(4),=XL4'40202120'
         ED    MSWRK+19(4),DBLW+L'DBLW-2
SHTDWN  TPUT   MSWRK,L'ERRMS1
         SPACE 1
*------- WHEN DONE, EXIT
*        IF USER IS THROUGH, EXIT
         SPACE 1
QUIT    $XRET  CC=0,LV=DATALEN,TYPE=RENT
         EJECT
*======= DRIBBLE : MAKE A MOVE - MOVE FOR THE CALLER (LOCAL ROUTINE)
*                  INPUT - R1  = VER INDEX LEVEL
*                          R14 = RETURN ADDRESS
*                                +0 PIT EMPTY
*                                +4 NORMAL
         SPACE 1
DRIBBLE  STM   R14,R12,12(R13)     SAVE CALLER'S REGS
         LA    R3,VER(R1)          FIND HIS VER
         USING VER,R3
         SPACE 1
*------- LOCATE PIT TO BE MOVED
         SPACE 1
         CLI   VPL,C'A'            THIS FOR ME?
         BNE   *+L'*+8             NO
         LA    R2,14               LOAD I REGISTER
         B     *+L'*+4             JUMP NEXT ONE
         LA    R2,7                IF B, USE 7
         XR    R0,R0               CLEAR TO GET CP
         IC    R0,VCP              GET CHOSEN PIT
         SR    R2,R0               GET ITS REAL OFFSET
         SPACE 1
*------- IF THE CHOSEN PIT IS EMPTY, RETURN WITH ERROR
         SPACE 1
         LA    R1,VARR(R2)         FIND IT
         CLI   0(R1),0             EMPTY?
         BNE   *+L'*+6             NO
         LM    R14,R12,12(R13)     RELOAD HIS REGS
         BR    R14                 RETURN DIRECTLY (EMPTY)
         SPACE 1
*------- PICK UP THE PEBBLES IN THE PIT
         SPACE 1
         XR    R4,R4               ZILTCH K REGISTER
         IC    R4,0(R1)            GET CONTENTS OF PIT
         MVI   0(R1),0             PIT NOW EMPTY
         XR    R0,R0               ASSUME A PLAYER'S KALAH
         CLI   VPL,C'A'            A PLAYER?
         BNE   *+L'*+4             NO, B PLAYER, SKIP A'S KALAH
         LA    R0,7                ELSE, SKIP B'S KALAH
         SPACE 1
*------- DISTRIBUTE PEBBLES AROUND THE BOARD
         SPACE 1
         XR    R1,R1               CLEAR FOR ICS
DLOOP    LA    R2,1(R2)            NEXT PIT OFFSET
         CH    R2,=H'13'           OFF THE END?
         BNH   *+L'*+2             NO
         XR    R2,R2               YES, WRAP AROUND BACK AGAIN
         CR    R2,R0               OPPONENTS KALAH?
         BE    DLOOP               YES, SKIP IT
         IC    R1,VARR(R2)         GET THIS PIT'S CONTENTS
         LA    R1,1(R1)            ADD ONE TO IT
         STC   R1,VARR(R2)         PUT IT BACK
         BCT   R4,DLOOP            DO ALL PEBBLES PICKED UP
         SPACE 1
*------- DETERMINE IF I CAPTURED ANYTHING
         SPACE 1
         LA    R1,VARR(R2)         FIND LAST PIT FILLED
         CLI   0(R1),1             WAS EMPTY?
         BNE   NOCAP               NO, NO CAPTURE MADE
         CLI   VPL,C'A'            MY TURN?
         BE    *+L'*+8             NO
         LA    R4,1                LOCATION OF HIS SIDE
         B     *+L'*+4             JUMP A'S
         LA    R4,8                LOCATION OF MY SIDE
         CR    R2,R4               ENDED UP ON HIS SIDE MAYBE?
         BL    NOCAP               NOT A CHANCE
         LA    R1,5(R4)            GET LIMIT OF SIDE
         CR    R2,R1               HOWS THIS?
         BH    NOCAP               NOT A CHANCE
         XR    R5,R5               CLEAR FOR IC
         IC    R5,OPPS(R2)         GET THE OPPOSITE PIT
         LA    R4,6(R4)            FIND HIS KALAH
         CH    R4,=H'13'           TOO FAR?
         BNH   *+L'*+4             NO
         SH    R4,=H'14'           YES, ADJUST DOWNWARDS
         LA    R1,VARR(R5)         FIND THE OTHER SIDE
         CLI   0(R1),0             EMPTY?
         BE    DOUT                YES, NOTHING TO CATCH
         XR    R1,R1               CLEAR FOR IC
         IC    R1,VARR(R4)         GET HIS KALAH'S CONTENTS
         LR    R14,R1              COPY IT
         IC    R1,VARR(R5)         GET OPPONENTS PIT
         LA    R14,1(R1,R14)       ADD TO HIS KALAH +CAPTURING PEB
         XR    R1,R1               GET A ZERO
         STC   R14,VARR(R4)        RESTORE KALAH COUNT
         STC   R1,VARR(R2)         NOTHING ON THIS SIDE
         STC   R1,VARR(R5)         OR THE OTHER
         B     DOUT                RETURN TO CALLER
         SPACE 1
*------- IF NO CAPTURE WAS MADE, SEE IF ANOTHER TURN IS WARRENTED
         SPACE 1
NOCAP    CLI   VPL,C'A'            A PLAYER?
         BNE   NCNOTA              NO
         LTR   R2,R2               HIS KALAH WAS LAST?
         BZ    DOUT1               YES, NO PLAYER SWITCH
RESA     MVI   VPL,C'B'            ITS B'S TURN
         B     DOUT1               CONTINUE
NCNOTA   CH    R2,=H'7'            HIS KALAH?
         BE    DOUT1               YES, HE GETS ANOTHER TURN
         MVI   VPL,C'A'            A'S TURN NOW
         B     DOUT1               GET OUT
DOUT     CLI   VPL,C'A'            WHOSE TURN IS IT?
         BE    RESA                A'S
         MVI   VPL,C'A'            ELSE, ITS B'S (NOW A'S)
         SPACE 1
*------- IF GAME IS OVER, CLEAR THE BOARD
         SPACE 1
DOUT1    OC    VB1(6),VB1          THIS SIDE EMPTY?
         BZ    BGO                 YES
         OC    VA1(6),VA1          A'S EMPTY?
         BNZ   DOUT2               NO
         LA    R1,VB1              ADD UP B
         B     BGO+L'BGO
BGO      LA    R1,VA1              ADD UP A
         XR    R0,R0               CLEAR FOR ICS
         XR    R2,R2               ZERO SUM REGISTER
         LA    R4,6                NUMBER TO DO
ADDUP    IC    R0,0(R1)            GET THIS PIT
         MVI   0(R1),0             ITS EMPTY NOW
         AR    R2,R0               ADD TO SUM
         LA    R1,1(R1)            NEXT ONE
         BCT   R4,ADDUP            ADD UP ALL PITS
         LA    R15,VA6             POINT TO END
         CR    R1,R15              PAST END?
         BNH   *+L'*+4             NO
         LA    R1,VAK              YES, HERE IT IS
         IC    R0,0(R1)            GET KALAH KONTENTS
         AR    R2,R0               GET TOTAL
         STC   R2,0(R1)            PUT IT INTO THE KALAH
         SPACE 1
*------- WHEN DONE DRIBBLING, RETURN
         SPACE 1
DOUT2    LM    R14,R12,12(R13)     RESTORE REGS
         B     4(R14)              RETURN OK
         DROP  R3
         EJECT
*======= WISECRAK : PICK A PHRASE (LOCAL ROUTINE)
*                   PICK A NASTY CRACK FROM A TABLE AT RANDOM
*                   INPUT - R1  = OUTPUT LINE LOCATION
*                           R0  = TABLE LOCATION
*                           R14 = RETURN ADDRESS
*                   OUTPUT - R1  = NEXT OUTPUT LOCATION
         SPACE 1
WISECRAK STM   R14,R12,12(R13)     SAVE CALLER'S REGS
         LR    R2,R0               SAVE TABLE LOC
         LR    R3,R1               SAVE OUTPUT LOCATION
         SPACE 1
*------- GENERATE RANDOM NUMBER FROM 1-N
*              N=NUMBER-OF-WISECRACKS
*              R=RANDOM-NUMBER (0-99)
*              NUM=((R*N)/100)+1
         SPACE 1
        TIME   BIN
         LR    R15,R0              SET UP TIME FOR DIVIDE
         XR    R14,R14             CLEAR FOR DIVIDE
         D     R14,=F'100'         R (REMAINDER)
         MH    R14,4(R2)           R*N
         LR    R15,R14             SET UP FOR DIVIDE
         XR    R14,R14
         D     R14,=F'100'         (R*N)/100=NUM-1
         LA    R14,1(R15)          GET ANSWER
         SPACE 1
*------- LOCATE PROPER TEXT IN THE TABLE
         SPACE 1
         L     R4,0(R2)            GET FIRST MESSAGE
         LA    R15,6(R2)           FIRST LENGTH
         B     *+L'*+10            SKIP FIRST ITERATION
         LH    R0,0(R15)           GET TEXT LENGTH
         LA    R15,2(R15)          NEXT LENGTH
         AR    R4,R0               NEXT TEXT
         BCT   R14,*-10            CONTINUE
         SPACE 1
*------- MOVE TEXT TO LINE, AND RETURN
         SPACE 1
         LH    R1,0(R15)           GET TEXT LENGTH
         BCT   R1,*+L'*+6          EXECUTE LENGTH
         MVC   0(0,R3),0(R4)            << EXECUTED >>
         EX    R1,*-6              GET THE TEXT
         LA    R1,1(R1,R3)         NEW POINTER
         ST    R1,24(R13)          RETURN IT TO HIM
         LM    R14,R12,12(R13)     RESTORE REGS
         BR    R14                 RETURN
         EJECT
*======= DBOARD : BOARD DISPLAY GENERATOR (LOCAL ROUTINE)
*                CALLED TO SET UP DISPLAY FOR SCREEN
*                INPUT - R1  = VER INDEX VALUE
*                        R0  = LENGTH OF DISPLAY
*                        R14 = RETURN ADDRESS
         SPACE 1
*======= DSPLAY : DISPLAY SCREEN (LOCAL ROUTINE)
*                 INPUT - R0  = LENGTH OF DISPLAY SCREEN
*                         R1  = ADDRESS OF DISPLAY
*                         R14 = RETURN ADDRESS
         SPACE 1
DBOARD   MVC   SCREEN(80),BLANKS   CLEAR SCREEN TO DO DISPLAY
         MVC   SCREEN+80(200),SCREEN
         MVC   SCREEN+280(200),SCREEN
         LA    R1,VARR(R1)         POINT TO PROPER ARRAY
         ST    R0,LIST0+4          SAVE LENGTH
         LR    R0,R14              SAVE RETURN ADDRESS
         XR    R15,R15             START AT FIRST ARRAY ELEMENT
BOARDLP  XR    R14,R14             CLEAR FOR IC
         IC    R14,0(R1,R15)       GET ARRAY ELEMENT
         CVD   R14,DBLW            CONVERT IT
         LR    R14,R15             GET INDEX
         AR    R14,R14             *2 FOR HALFWORDS
         LH    R14,OFFSETS(R14)    GET ITS SCREEN OFFSET
         LTR   R14,R14             ALL DONE?
         BZ    BOARDIT             YES, DISPLAY IT
         LA    R14,SCREEN(R14)     POINT TO IT
         UNPK  0(2,R14),DBLW(8)    MOVE IT TO SCREEN
         OI    1(R14),X'F0'        FIX THE SIGN
         CLI   0(R14),C'0'         CHEATERS EDIT
         BNE   *+L'*+4
         MVI   0(R14),C' '
         LA    R15,1(R15)          NEXT ARRAY MEMBER
         B     BOARDLP             DO ALL PITS
BOARDIT  LR    R14,R0              RESTORE RETURN
         L     R1,LIST0+4          GET LENGTH
         BCTR  R1,0                LAST CHAR
         LA    R1,SCREEN(R1)       POINT TO IT
         LA    R1,SCREEN           POINT TO SCREEN
         L     R0,LIST0+4          ITS LENGTH AND GO DO IT
         SPACE 1
DSPLAY   MVC   REPLY(L'REPLY),BLANKS NOTHING READ YET
         ST    R1,LIST0            SAVE SCREEN ADDR
         ST    R0,LIST0+4          AND LENGTH
         SPACE 1
*------- DISPLAY AND EVENTUALLY READ
         SPACE 1
         TM    FLAGS,SCTERM        FULL SCREEN?
         BZ    NOTFS               NO
         ST    R14,SVR14           YES, SAVE RETURN AROUND CALL
        #CALL  DOFS
         L     R14,SVR14           RESTORE RETURN AFTER CALL
         CH    R15,=H'4'           WHERE GO?
         BE    SHTDWN              +4 - ERROR, MESSAGE AND EXIT
         BH    *+L'*+6             +8 - ERROR, MESSAGE AND EXIT
         CLI   REPLY,C'E'          +0 - OK, END?
         BNER  R14                 NO, RETURN
RSTFS    MVI   OPTFS,FSEXIT
        @FS    OPTFS,MF=(E,FSPARM)
         LR    R10,R15             RETAIN RETURN CODE
         XR    R0,R0
         ICM   R0,B'0011',ERRMSL
         BZ    NOMSGE
        TPUT   MSWRK,(0)
NOMSGE   LTR   R10,R10
         BZ    QUIT
         MVC   MSWRK(L'ERRMS2),ERRMS2
         CVD   R10,DBLW
         MVC   MSWRK+29(4),=XL4'40202120'
         ED    MSWRK+29(4),DBLW+L'DBLW-2
         XR    R0,R0
         IC    R0,OPTFS
         STC   R0,MSWRK+21
         SRL   R0,4
         STC   R0,MSWRK+20
         NC    MSWRK+20(2),=XL2'0F0F'
         TR    MSWRK+20(2),HEXTB
        TPUT   MSWRK,L'ERRMS2
         B     QUIT
NOTFS    STM   R14,R12,12(R13)     SAVE HIS REGS
         L     R3,LIST0            GET BUFFER LOCATION
         L     R4,LIST0+4          GET LENGTH TO DISPLAY
TPLOOP   LA    R0,80               ASSUME FULL LINE
         CR    R4,R0               JUST THIS LINE?
         BH    *+L'*+2             YES
         LR    R0,R4               ELSE DO REMAINDER
         LA    R1,0(R3)            GET ADDRESS
        TPUT   (1),(0),R           DUMP THE LINE
         LA    R3,80(R3)           NEXT LINE
         SH    R4,=H'80'           GET REMAINING LENGTH
         BP    TPLOOP              YES, CONTINUE
         OC    LIST0+2*4(2*4),LIST0+2*4 READ?
         BZ    TSOXT               NO
         L     R1,LIST0+2*4        GET READ ADDRESS
         L     R0,LIST0+3*4        GET LENGTH
         O     R1,=X'80000000'     TGET
        TGET   (1),(0),R           READ
         ST    R1,LIST0+4*4        AND SAVE RETURNED LENGTH
TSOXT    LM    R14,R12,12(R13)     RESTORE REGS
         OC    REPLY(L'REPLY),BLANKS MAKE IT UPPER CASE
         CLI   REPLY,C'E'          END?
         BE    QUIT                YES
         BR    R14                 RETURN
         EJECT
*- - - - C O N S T A N T S - - - - - - - - - - - - - - - - - - - - - -*
*------- MESSAGES
         SPACE 1
ASKPPP   DC    C'ENTER PEBBLES (1-9) PER PIT'
VERMSG   DC    C'SHALL I VERIFY YOUR MOVES AFTER YOU''VE MADE THEM?'
ASKBRD   DC    CL80'ENTER CONTENTS OF PITS STARTING WITH MY KALAH AND'
         DC    CL80'PROCEEDING COUNTERCLOCKWISE TO MY PIT 1.'
         DC    CL80'KALAH BOARD :                1 2 3 4 5 6  <--- MY NX
               UMBERED PITS'
         DC    CL80'              MY KALAH -> 0               0 <- YOURX
                KALAH'
         DC    C'                             1 2 3 4 5 6  <--- YOUR NUX
               MBERED PITS'
LASKBRD  EQU   *-ASKBRD
ASKTRAC  DC    CL80'ENTER MAXIMUM LEVEL TO WHICH TO TRACE (0-9),'
         DC    C'OR ENTER ''R'' (RESET) TO SET TRACE OFF.'
LASKTRAC EQU   *-ASKTRAC
ASKFIR   DC    C'WHO GOES FIRST? YOU OR ME?'
BADPIT   DC    C'INVALID OR EMPTY PIT, RE-ENTER'
BAGAIN   DC    C'YOU GET ANOTHER MOVE'
AAGAIN   DC    C'I GET ANOTHER MOVE, '
MYMOVE   DC    C'MY MOVE, '
YOURMOVE DC    C'YOUR MOVE'
IMOVED   DC    C'I''LL MOVE PIT XX'
GAMEMSG  DC    CL80' '
         DC    CL80'GAME OVER'
BLANKS   DC    CL80' '
         DC    CL80'SCORE  ME   YOU'
         DC    CL80'GAME               '
         DC    CL80'MATCH'
         DC    CL80' '
         DC    C'NEW GAME'
LGAMEMSG EQU   *-GAMEMSG
INMSG    DC    CL80'TYPE ''END'' AT ANY TIME TO EXIT THE GAME.'
         DC    C'ENTER NULL LINE IF YOU WANT TO RE-DISPLAY THE SCREEN.'
LGINMSG  EQU   *-INMSG
ERRMS0   DC    C' => NOT OUTSIDE TSO ENVIRONMENT | BYE-BYE'
ERRMS1   DC    C' => "      " - RC = ... - EXIT -'
ERRMS2   DC    C' => "FSRTN" - OPT = .. , RC = ... - EXIT -'
         EJECT
*------- VERTEX BOARD OPPOSITES
         SPACE 1
OPPS     DC    AL1(07),AL1(13),AL1(12),AL1(11),AL1(10),AL1(09),AL1(08)
         DC    AL1(00),AL1(06),AL1(05),AL1(04),AL1(03),AL1(02),AL1(01)
         SPACE 1
OFFSETS  DS    0H
         DC    AL2(240),AL2(324),AL2(328),AL2(332),AL2(336),AL2(340)
         DC    AL2(344),AL2(268),AL2(184),AL2(180),AL2(176),AL2(172)
         DC    AL2(168),AL2(164),AL2(0)
         SPACE 1
*------- LITERALS
         SPACE 1
EXTRP   EXTRACT *-*,'S',MF=L
NONE     DC    0H'0',X'7FFF'
VBLEN    DC    H'20'
HEXTB    DC    CL16'0123456789ABCDEF'
         SPACE 1
        LTORG
         EJECT
*- - - - D O     F U L L     S C R E E N - - - - - - - - - - - - - - -*
         SPACE 1
DOFS    #XENT  ,
         XR    R10,R10
         TM    SWFSW,SW1ST
         BO    DOFSW
        @FSI   ,
         LTR   R15,R15
         BNZ   DOFIMM
         OI    OPTFS,FSSKIP
         LM    R2,R3,INITRA
        @FS    OPTFS,(R2),(R3),MF=(E,FSPARM)
         LTR   R15,R15
         BNZ   DOFERR
         OI    SWFSW,SW1ST
DOFSW    LM    R2,R3,LIST0
         CH    R3,=H'80'
         BNH   *+L'*+22
         CLC   0(80,R2),BLANKS
         BNE   *+L'*+12
         LA    R2,80(R2)
         SH    R3,=H'80'
         B     DOFSW+L'DOFSW
         STM   R2,R3,DFSTL
         XR    R7,R7
         L     R1,SVR14
         ICM   R7,B'0011',2(R1)    GET CALL FS NUMBER
         CL    R7,=A(DOFSMX)
         BNL   DOFVER
         B     DOFSV(R7)
DOFSV    B     DOFS0
         B     DOFS1
         B     DOFS2
         B     DOFS3
         B     DOFS4
         B     DOFS5
         B     DOFS6
         B     DOFS7
         B     DOFS8
         B     DOFS9
         B     DOFS10
DOFSMX   EQU   *-DOFSV
DOFS0    L     R1,=A(FSMS)
         BAS   R14,DOFSRZ
         L     R1,=A(FSMS)
         BAS   R7,DOFSMV
         OI    SWFSW,SWNMS
         B     DOFSX
DOFS1    L     R1,=A(FSG1)
         BAS   R14,DOFSRZ
         L     R1,=A(FSG2)
         BAS   R14,DOFSRZ
         TM    SWFSW,SWNMS
         BO    *+L'*+8
         L     R1,=A(FSMS)
         BAS   R14,DOFSRZ
DOFS1A   L     R1,=A(FSG1)
         BAS   R7,DOFSMV
DOFS3    NI    OPTFS,255-FSSKIP
         BAS   R7,DOFSTM
         B     DOFSX
DOFS2    L     R1,=A(FSG1)
         BAS   R14,DOFSRZ
         L     R1,=A(FSG2)
         BAS   R14,DOFSRZ
         L     R1,=A(FSMS)
         BAS   R14,DOFSRZ
         B     DOFS1A
DOFS4    TM    SWFSW,SWNMS
         BO    *+L'*+8
         L     R1,=A(FSMS)
         BAS   R14,DOFSRZ
         BAS   R7,DOFSIN
         B     *+L'*+4             +0
         BAS   R7,DOFSSH           +4
         NI    SWFSW,255-SWNMS
         B     DOFS3
DOFS5    L     R1,=A(FSMS)
         BAS   R14,DOFSRZ
         L     R1,=A(FSMS)
         B     DOFS1A+L'DOFS1A
DOFS6    L     R1,=A(FSMS)
         CLC   0(80,R1),BLANKS
         BE    *+L'*+8
         LA    R1,80(R1)
         B     *-14
         BAS   R7,DOFSMV
         OI    SWFSW,SWNMS
         B     DOFSX
DOFS7    TM    SWFSW,SWNMS
         BO    *+L'*+8
         L     R1,=A(FSMS)
         BAS   R14,DOFSRZ
         BAS   R7,DOFSIN
         B     *+L'*+4             +0
         BAS   R7,DOFSSH           +4
         NI    SWFSW,255-SWNMS
         B     DOFS3
DOFS8    TM    SWFSW,SWTRC
         BO    DOFS8A
         LA    R0,20
         L     R1,=A(TSMS)
         MVC   0(80,R1),BLANKS
         LA    R1,80(R1)
         BCT   R0,*-10
         OI    SWFSW,SWTRC
DOFS8A   L     R1,=A(TSMS)
         LA    R0,20*8
         CLC   0(10,R1),BLANKS
         BE    DOFS8B
         LA    R1,10(R1)
         BCT   R0,*-14
         NI    SWFSW,255-SWTRC
         NI    OPTFS,255-FSSKIP
         LM    R2,R3,TSCRA
        @FS    OPTFS,(R2),(R3),MF=(E,FSPARM)
         LTR   R15,R15
         BNZ   DOFERR
         B     DOFS8
DOFS8B   LM    R2,R3,DFSTL
         BCT   R3,*+L'*+6
         MVC   1(*-*,R1),0(R2)          << EXECUTED >>
         EX    R3,*-6
         B     DOFSX
DOFS9    NI    SWFSW,255-SWTRC
         NI    OPTFS,255-FSSKIP
         LM    R2,R3,TSCRA
        @FS    OPTFS,(R2),(R3),MF=(E,FSPARM)
         LTR   R15,R15
         BNZ   DOFERR
         B     DOFSX
DOFS10   CLC   DFSTL+4(4),=A(6*80)
         BNH   DOFS3
         LM    R2,R3,DFSTL
         A     R2,=A(6*80)
         S     R3,=A(6*80)
         STM   R2,R3,DFSML
         MVC   DFSTL+4(4),=A(5*80)
         TM    SWFSW,SWNMS
         BO    *+L'*+8
         L     R1,=A(FSMS)
         BAS   R14,DOFSRZ
         BAS   R7,DOFSIN
         B     *+L'*+4             +0
         BAS   R7,DOFSSH           +4
         NI    SWFSW,255-SWNMS
         L     R1,=A(FSMS)
         CLC   0(80,R1),BLANKS
         BE    *+L'*+8
         LA    R1,80(R1)
         B     *-14
         LM    R2,R3,DFSML
         BAS   R7,DOFSMV1
         B     DOFS3
         SPACE 1
DOFSRZ   LA    R0,6                          R14 = LINK REGISTER
         MVC   0(80,R1),BLANKS
         LA    R1,80(R1)
         BCT   R0,*-10
         BR    R14
DOFSSH   L     R2,=A(FSG1)                   R7 = LINK REGISTER
         L     R3,=A(6*80)
         L     R4,=A(FSG2)
         L     R5,=A(6*80+X'40000000')
         MVCL  R2,R4
         L     R1,=A(FSG2)
         BAS   R14,DOFSRZ
         L     R1,=A(FSG2)
DOFSMV   LM    R2,R3,DFSTL                   R7 = LINK REGISTER
DOFSMV1  CH    R3,=H'80'
         BNH   DOFSMV2
         MVC   0(80,R1),0(R2)
         LA    R1,80(R1)
         LA    R2,80(R2)
         SH    R3,=H'80'
         B     DOFSMV1
DOFSMV2  CLC   0(80,R1),BLANKS
         BE    *+L'*+6
         MVC   0(80,R1),BLANKS
         BCT   R3,*+L'*+6
         MVC   0(*-*,R1),0(R2)          << EXECUTED >>
         EX    R3,*-6
         BR    R7
DOFSIN   L     R1,=A(FSG1)                   R7 = LINK REGISTER
         BAS   R14,DOFSINS
         L     R1,=A(FSG2)
         BAS   R14,DOFSINS
         L     R1,=A(FSG1)
         BAS   R14,DOFSINC
         L     R1,=A(FSG2)
         BAS   R14,DOFSINC
         B     4(R7)
DOFSINS  LM    R2,R3,DFSTL                   R14 = LINK REGISTER
         LR    R4,R1
         L     R5,=A(6*80+X'40000000')
         CLCL  R2,R4
         BER   R7
         LM    R2,R3,DFSTL
         CL    R3,=A(4*80)
         BNHR  R14
         L     R3,=A(4*80)
         LR    R4,R1
         L     R5,=A(4*80+X'40000000')
         CLCL  R2,R4
         BNER  R14
         LM    R2,R3,DFSTL
         A     R1,=A(4*80)
         A     R2,=A(4*80)
         S     R3,=A(4*80)
         B     DOFSMV1
DOFSINC  LA    R2,BLANKS                     R14 = LINK REGISTER
         LA    R3,80
         LR    R4,R1
         L     R5,=A(6*80+X'40000000')
         CLCL  R2,R4
         BE    DOFSMV
         BR    R14
DOFSTM   LM    R2,R3,FSCRA                   R7 = LINK REGISTER
        @FS    OPTFS,(R2),(R3),MF=(E,FSPARM)
         LTR   R15,R15
         BNZ   DOFERR
         XC    LIST0+4*4(4),LIST0+4*4
         TM    OPTFS,FSSKIP
         BOR   R7
         OC    LIST0+2*4(2*4),LIST0+2*4 NO READ?
         BZR   R7
         LM    R2,R3,LIST0+2*4
         BASR  R4,0
         LA    R5,C' '
         SLL   R5,24
         MVCL  R2,R4
         LTR   R1,R1
         BZ    DOFSTM1
         CLI   0(R1),X'F3'         PF-KEY 3?
         BE    *+L'*+8
         CLI   0(R1),X'C3'         PF-KEY 15 (ALT. 3)?
         BNE   DOFSTM1
         L     R2,LIST0+2*4
         MVC   0(3,R2),=CL3'END'
         LA    R2,3
         ST    R2,LIST0+4*4
         BR    R7
DOFSTM1 @FSR   ,
         LTR   R15,R15
         BZR   R7
         LTR   R1,R1
         BNPR  R7
         L     R2,LIST0+2*4
DOFSTM2  OC    0(1,R2),3(R15)
         CLI   0(R2),C' '
         BNE   DOFSTM3
         LA    R15,1(R15)
         BCT   R1,DOFSTM2
         BR    R7
DOFSTM3  LR    R0,R2
         LA    R2,1(R2)
         LA    R15,1(R15)
         BCT   R1,*+L'*+4
         B     *+L'*+10
         OC    0(1,R2),3(R15)
         B     DOFSTM3+L'DOFSTM3
         SR    R2,R0
         ST    R2,LIST0+4*4
         BR    R7
         SPACE 1
DOFIMM   MVC   MSWRK(L'ERRMS1),ERRMS1
         MVC   MSWRK+5(6),=CL6'FSRTNI'
         CVD   R15,DBLW
         MVC   MSWRK+19(4),=XL4'40202120'
         ED    MSWRK+19(4),DBLW+L'DBLW-2
         LA    R10,4
         B     DOFSX
DOFERR   MVC   MSWRK(L'ERRMS2),ERRMS2
         CVD   R15,DBLW
         MVC   MSWRK+29(4),=XL4'40202120'
         ED    MSWRK+29(4),DBLW+L'DBLW-2
         XR    R0,R0
         IC    R0,OPTFS
         STC   R0,MSWRK+21
         SRL   R0,4
         STC   R0,MSWRK+20
         NC    MSWRK+20(2),=XL2'0F0F'
         TR    MSWRK+20(2),HEXTB
         LA    R0,L'ERRMS2
         B     DOFERX
DOFVER   MVC   MSWRK(L'DOFMSE),DOFMSE
         SRL   R7,2
         CVD   R7,DBLW
         MVC   MSWRK+21(6),=XL6'402020202120'
         ED    MSWRK+21(6),DBLW+L'DBLW-3
         LA    R0,L'DOFMSE
DOFERX   STH   R0,ERRMSL
         LA    R10,8
DOFSX   #XRET  RC=(R10)
INITRA   DC    A(INITR,*+4,INITRL)
FSCRA    DC    A(FSCR,*+4,FSCRL)
TSCRA    DC    A(TSCR,*+4,TSCRL)
DOFMSE   DC    C' => "DOFS" - NUMBER = ..... - EXIT -'
         SPACE 1
        #XEND  ,
         SPACE 1
         DROP  R9,R11,R12          KILL ALL ADDRESSABILITIES
         EJECT
*------- MESSAGES
         SPACE 1
ASKPARMS DC    CL80'WHICH INITIAL PARAMETER(S) DO YOU WISH TO CHANGE?'
         DC    CL80'L - LOOK AHEAD           P - PEBBLES/PIT          TX
                - TRACE'
         DC    CL80'W - WHO GOES FIRST       B - BOARD (INITIAL SET)  VX
                - VERIFY'
         DC    CL80'ENTER NEW LINE TO ACCEPT ALL DEFAULTS'
         DC    C'AT LEAST ONE SPACE MUST SEPARATE MULTIPLE PARAMETERS SX
               PECIFICATION.'
LASKPARM EQU   *-ASKPARMS
ASKLHL   DC    CL80'GIVE ME A LEVEL OF SMARTS'
         DC    CL80'1 = IMBECILE             2 = BEGINNER (DEFAULT)   3X
                = AVERAGE'
         DC    CL80'4 = FAIR TO GOOD         5 = CLEVER               6X
                = INTELLEGENT'
         DC    CL80'7 = GENIUS               8 = KALAH MASTER         9X
                = COMPUTER UNBOUND'
         DC    C'NOTE : LEVELS ABOVE 5 ARE VERY EXPENSIVE AND SLOW'
LASKLHL  EQU   *-ASKLHL
         SPACE 1
*------- WISECRACKS
         SPACE 1
MSGA    @MSG   'THIS SHOULD BE FUNNY'
        @MSG   'DUMB DUMB'
        @MSG   'OH SLOW OF WIT'
        @MSG   'MR. LOSER'
        @MSG   '(GIVE UP YET?)'
        @MSG   'OR SHOULD I SAY STUMBLE?'
        @MSG   'OH KALAH KLUTZ'
        @MSG   'PEBBLE HEAD'
        @MSG   'GENERAL CUSTER'
        @MSG   'MENTAL MIDGET'
        @MSG   'CHICKEN KALAH-KING'
        @MSG   'LITTLE ONE'
        @MSG   'ARE YOU GOING TO GUESS AGAIN?'
        @MSG   'DUMMKOPF'
        @MSGGEN
         SPACE 1
MSGB    @MSG   'KALAH-MATY JANE'
        @MSG   'CHAMP'
        @MSG   'MR. WIZARD'
        @MSG   'MASTER'
        @MSG   'BOBBY FISCHER'
        @MSG   'OH KING OF KALAH'
        @MSG   '(HAVE MERCY!)'
        @MSG   'KILLER'
        @MSG   'BOSS'
        @MSG   'TIGER'
        @MSG   'GRAND VIZAR'
        @MSG   'OH PROFOUND ONE'
        @MSG   'WHIZZ KID'
        @MSG   'SUPERMAN'
        @MSG   '(DO YOU HAVE ANOTHER COMPUTER HELPING?)'
        @MSG   'GARANAPLATZZZ'
        @MSGGEN
         SPACE 1
MSGC    @MSG   'A FREE TRIP THRU THE NEW YORK SLUMS'
        @MSG   'A CEE-GAR'
        @MSG   'A RIPE BANANA'
        @MSG   'A PLATINUM PEBBLE'
        @MSG   'A KEWPIE DOLL'
        @MSG   'AN HONORARY DEGREE'
        @MSG   'A CHOCOLATE COVERED DEWEY BUTTON'
        @MSG   'A STANDING OVATION'
        @MSG   'A CHAUFERED TRICYCLE'
        @MSG   'A MEDAL'
        @MSG   'A M.U.S.'
        @MSG   'AN ALL EXPENSE-PAID TRIP TO EAST LEFTOVERSHOE'
        @MSG   'A FREE CERTIFICATE TO GET HIS TYPING FINGER BRONZED'
        @MSGGEN
         SPACE 1
MSGD    @MSG   'THINKING...'
        @MSG   'CONTEMPLATING...'
        @MSG   'COGITATING...'
        @MSG   'MEDITATING...'
        @MSG   'PONDERING...'
        @MSG   'CONSIDERING...'
        @MSG   'ANALYZING...'
        @MSG   'CALCULATING...'
        @MSG   'RUNNING UP YOUR BILL...'
        @MSG   'SLEEPING...'
        @MSG   'ZZZZZZZZ...'
        @MSG   'SPINNING MY WHEELS...'
        @MSG   'LOOPING AND LOOPING AND LOOPING AND...'
        @MSG   'HMMMMMMM....'
        @MSG   'GRUNT, GRUNT, GRUNT...'
        @MSGGEN
         SPACE 1
MSGE    @MSG   'I WIN (OF COURSE)'
        @MSG   'I WIN (WANT MY AUTOGRAPH?)'
        @MSG   'I WIN! I WIN! I WIN!'
        @MSG   'I WON. THAT WAS EASY.'
        @MSGGEN
         EJECT
*- - - - S C R E E N     D A T A - - - - - - - - - - - - - - - - - - -*
         SPACE 1
INITR   $FS    CC=EW,WCC=(AL,RMDT),SBA=(24,79),MF=L
        $FS    SBA=(1,1),RA=(1,1,00),MF=L
        $FS    SBA=(1,1),SF=(IC),MF=L
INITRL   EQU   *-INITR
         SPACE 1
FSCR    $FS    CC=EW,WCC=(KBR,RMDT),SBA=(1,1),RA=(1,1,00),MF=L
        $FS    SBA=(1,1),SF=(PROT,INT),MF=L
        $FS    SBA=(1,21),MF=L
        $FS    TEXT='K A L A H -- ANCIENT EGYPTIAN BOARD GAME',MF=L
        $FS    SBA=(2,1),SF=(PROT,INT),RA=(2,80,-),MF=L
        $FS    SF=(PROT),MF=L
FSG1    $FS    TEXT=(' ',80),MF=L       1 -> (3,1)
        $FS    TEXT=(' ',80),MF=L       2
        $FS    TEXT=(' ',80),MF=L       3
        $FS    TEXT=(' ',80),MF=L       4
        $FS    TEXT=(' ',80),MF=L       5
        $FS    TEXT=(' ',80),MF=L       6 -> (8,1)
        $FS    SF=(PROT,INT),RA=(9,80,-),MF=L
        $FS    SF=(PROT),MF=L
FSG2    $FS    TEXT=(' ',80),MF=L       1 -> (10,1)
        $FS    TEXT=(' ',80),MF=L       2
        $FS    TEXT=(' ',80),MF=L       3
        $FS    TEXT=(' ',80),MF=L       4
        $FS    TEXT=(' ',80),MF=L       5
        $FS    TEXT=(' ',80),MF=L       6 -> (15,1)
        $FS    SF=(PROT,INT),RA=(16,80,=),MF=L
        $FS    SF=(PROT),MF=L
FSMS    $FS    TEXT=(' ',80),MF=L       1 -> (17,1)
        $FS    TEXT=(' ',80),MF=L       2
        $FS    TEXT=(' ',80),MF=L       3
        $FS    TEXT=(' ',80),MF=L       4
        $FS    TEXT=(' ',80),MF=L       5
        $FS    TEXT=(' ',80),MF=L       6 -> (22,1)
        $FS    SF=(PROT,INT),MF=L
        $FS    TEXT='===>',MF=L
        $FS    SF=NORMAL,MF=L
        $FS    SBA=(23,80),SF=(PROT),MF=L
        $FS    SBA=(1,1),SF=(PT,IC),MF=L
FSCRL    EQU   *-FSCR
         SPACE 1
TSCR    $FS    CC=EW,WCC=(KBR,RMDT),SBA=(1,1),RA=(1,1,00),MF=L
        $FS    SBA=(1,1),SF=(PROT,INT),MF=L
        $FS    SBA=(1,28),MF=L
        $FS    TEXT='K A L A H -- TRACE OPTION',MF=L
        $FS    SBA=(2,1),SF=(PROT,INT),RA=(2,80,-),MF=L
        $FS    SF=(PROT),MF=L
TSMS    $FS    TEXT=(' ',80),MF=L       1 -> (3,1)
        $FS    TEXT=(' ',80),MF=L       2
        $FS    TEXT=(' ',80),MF=L       3
        $FS    TEXT=(' ',80),MF=L       4
        $FS    TEXT=(' ',80),MF=L       5
        $FS    TEXT=(' ',80),MF=L       6
        $FS    TEXT=(' ',80),MF=L       7
        $FS    TEXT=(' ',80),MF=L       8
        $FS    TEXT=(' ',80),MF=L       9
        $FS    TEXT=(' ',80),MF=L      10
        $FS    TEXT=(' ',80),MF=L      11
        $FS    TEXT=(' ',80),MF=L      12
        $FS    TEXT=(' ',80),MF=L      13
        $FS    TEXT=(' ',80),MF=L      14
        $FS    TEXT=(' ',80),MF=L      15
        $FS    TEXT=(' ',80),MF=L      16
        $FS    TEXT=(' ',80),MF=L      17
        $FS    TEXT=(' ',80),MF=L      18
        $FS    TEXT=(' ',80),MF=L      19
        $FS    TEXT=(' ',80),MF=L      20 -> (22,1)
        $FS    SF=(PROT,INT),MF=L
        $FS    TEXT='===>',MF=L
        $FS    SF=NORMAL,MF=L
        $FS    SBA=(23,80),SF=(PROT),MF=L
        $FS    SBA=(1,1),SF=(PT,IC),MF=L
TSCRL    EQU   *-TSCR
         EJECT
*- - - - V A R I A B L E S - - - - - - - - - - - - - - - - - - - - - -*
         SPACE 1
DATA     DSECT
         DS    18F                 SAVE AREA
         DS    18F                 ADDITIONAL SAVE AREA
EXTR    EXTRACT *-*,'S',MF=L
LEXTR    EQU   *-EXTR
SVR14    EQU   EXTR,4
ATSO     DS    F
SWFSW    EQU   ATSO,1              SCREEN FUNCTIONS
SWTRC    EQU   X'80'
SW1ST    EQU   X'10'
SWNMS    EQU   X'08'
ERRMSL   EQU   ATSO+2,2
FLAGS    DS    XL1                 GENERAL FUNCTIONS
VERIFY   EQU   X'80'                    VERIFY USERS MOVES FOR HIM
TESTING  EQU   X'40'                    TESTING, SHOW PATH
SCTERM   EQU   X'08'                    SCREEN TERMINAL
OPTS     DS    XL1                 OPTIONS SELECTED
OPTSL    EQU   X'80'                    LEVEL GIVEN
OPTSP    EQU   X'40'                    PEBBLES PER PIT GIVEN
OPTST    EQU   X'20'                    TRACE LEVEL GIVEN
OPTSW    EQU   X'10'                    WHO GOES FIRST GIVEN
OPTSB    EQU   X'08'                    INITIAL BOARD GIVEN
OPTSV    EQU   X'04'                    VERIFICATION REQUESTED
VERPIT   DS    XL1                 B'S BEST MOVE
TRACE    DS    XL1                 MAXIMUM LEVEL TO TRACE
MAXLV    DS    H                   MAXIMUM LOOK AHEAD
PPP      DS    H                   PEBBLES/PIT
AMATCH   DS    H                   MY MATCH SCORE SO FAR
BMATCH   DS    H                   HIS MATCH SCORE SO FAR
SCREEN   DS    800C
LIST0    DS    A         TUPT-TGET LIST +0 = SCREEN ADDRESS
         DS    F                        +4 = SCREEN TEXT LENGTH
         DS    A                        +8 = REPLY ADDRESS
         DS    F                        +12 = MAX. REPLY LENGTH
         DS    F                        +16 = TRUE REPLY LENGTH
REPLY    DS    CL80
MSWRK    DS    CL80
SVRDP    DS    XL8
DBLW     DS    D
         EJECT
*------- VERTEX BLOCKS
         SPACE 1
SIZE     EQU   10                  NUMBER OF POSSIBLE LEVELS
VER      DS    0F
VLV      DS    XL1
VOP      DS    XL1
VOS      DS    H
VPL      DS    CL1
VCP      DS    XL1
VARR     EQU   *
VAK      DS    XL1
VB1      DS    XL1
VB2      DS    XL1
VB3      DS    XL1
VB4      DS    XL1
VB5      DS    XL1
VB6      DS    XL1
VBK      DS    XL1
VA1      DS    XL1
VA2      DS    XL1
VA3      DS    XL1
VA4      DS    XL1
VA5      DS    XL1
VA6      DS    XL1
         DS    (SIZE*5)F
         SPACE 1
DFSTL    DS    2F                  TEXT ADDRESS / LENGTH
DFSML    DS    2F                  MESSAGE ADDRESS / LENGTH
FSPARM  @FS    ,,,MF=L
OPTFS   @FSO   ,
         SPACE 1
DATALEN  EQU   (((*-DATA)+7)/8)*8
         SPACE 2
         END
