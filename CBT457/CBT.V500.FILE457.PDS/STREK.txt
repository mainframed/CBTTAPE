STRK     TITLE '--- TSO FULL-SCREEN STAR-TREK COMMAND ---'
         PRINT OFF
* Here follows the Assembly SYSPARM(...) options process which allows
*        you to select :
*        1. NONE or omitted = no list of macros development
*           FULL = list all macros development
*        2. US or omitted = use USA display characters
*           EU = use European display characters
*        How to define options : i.e - SYSPARM(FULL)
*                                      SYSPARM(,EU)
*        If you want add another selection, you can easily do it
*        adjusting below the statements marked "<===", define the new
*        OPT(n) default, and supply the new option coding just before
*        the comment card marked "---> Add option(s)".
         GBLC  &EULST,&EUDCH                                  <===
         LCLA  &CN,&CT,&LN,&MX,&SB,&ST
         LCLC  &OPT(2)                                        <===
&MX      SETA  2                                              <===
&OPT(1)  SETC  'NONE'
&OPT(2)  SETC  'US'
&CN      SETA  K'&SYSPARM
         AIF   (K'&SYSPARM EQ 0).SP99
&CT      SETA  1
&SB      SETA  1
&ST      SETA  &CT
.SP1     AIF   ('&SYSPARM'(&CT,1) EQ ',').SP2
&CT      SETA  &CT+1
         AIF   (&CT LE &CN).SP1
.SP2     ANOP
&LN      SETA  &CT-&ST
         AIF   (&LN EQ 0).SP3
&OPT(&SB) SETC '&SYSPARM'(&ST,&LN)
.SP3     ANOP
&SB      SETA  &SB+1
         AIF   (&SB GT &MX).SP99
&CT      SETA  &CT+1
         AIF   (&CT GT &CN).SP99
&ST      SETA  &CT
         AGO   .SP1
.SP99    ANOP
&EULST   SETC  'GEN'
         AIF   ('&OPT(1)' EQ 'FULL').SL1
&EULST   SETC  'NOGEN'
.SL1     ANOP
&EUDCH   SETC  'EUROPE'
         AIF   ('&OPT(2)' EQ 'EU').SL99
&EUDCH   SETC  'USA'
.SL99    ANOP
* ---> Add option(s)
         MACRO
&LABEL   #GO   &NAME,&AR=I
         AIF   ('&NAME' NE '').A
         MNOTE 8,'--- NAME of routine missing (it is mandatory) ---'
         AGO   .MEND
.A       AIF   ('&AR' EQ 'D').B
&LABEL   L     R15,=A(&NAME)
         AGO   .C
.B       ANOP
&LABEL   L     R15,&NAME
.C       BASR  R14,R15
.MEND    MEND
         PRINT ON
         SPACE 1
STREK    START 0
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
*    Name :    STREK                                                  *
*                                                                     *
*    Author :  Ted BESTANI                                            *
*                                                                     *
*    Purpose : This program is designed to play STAR-TREK using the   *
*              hardware characteristics of 3270 terminals.            *
*                                                                     *
*    Attributes : Non-reusable                                        *
*                                                                     *
*    Requirements : STAR-TREK requires that VTAM or TCAM support the  *
*              FULL-SCREEN option (macros used : TGET/TPUT).          *
*              It is designed for 3278-2 terminals.                   *
*                                                                     *
*    Origin :  Extracted from tape CBT 93 AUG, file 173.              *
*              Updates : use of FSRTN routine (FULL-SCREEN).          *
*                   by : MOINIL P.A.                                  *
*                        Computing Centre (TP 361)                    *
*                        J.R.C. - Ispra Establishment                 *
*                        21020 Ispra (VA), Italy                      *
*                                                                     *
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
         SPACE 1
         PRINT &EULST
        $DEFREG
         EJECT
        $XENT  BASE=(R12)
         LM    R10,R11,=A(WORKDBLW,WORKDBLW+4*1024)
         USING WORKDBLW,R10,R11
*        --> CHECK IF WE ARE ENTERED AS TSO COMMAND
         LR    R2,R1               GET ENTRY R1
        EXTRACT ATIOT,'S',FIELDS=(TIOT,TSO,PSB)
*        Results : BATCH-exec       TSO-background   TSO-foreground
*                +----------------+----------------+----------------+
*          APSCB +     0          +     a.PSCB     +     a.PSCB     +
*           ATSO +     a.Flag     +     a.Flag     +     a.Flag     +
*           Flag +     X'00'      +     X'00'      +     x'80'      +
*                +----------------+----------------+----------------+
*        Initial R1 -> a.CPPL (IKJCPPL) if TSO-command
*                   -> a.Comm. Area (IKJEBECA) if TSO-subcommmand
*                   -> a.Parameters List in all other cases
         L     R1,ATSO             TSO-FLAG ADDRESS
         TM    0(R1),X'80'         TSO FOREGROUND?
         BZ    ERREX               NO, BACKGROUND, FLUSH
         L     R1,APSCB            YES, GET PSCB ADDRESS
         LTR   R1,R1               TSO RUNNING?
         BZ    ERREX               NO, WHAT'S THAT? FLUSH
         USING CPPL,R2
         USING PSCB,R1
         CLC   PSCBUPT,CPPLUPT     CPPL+4 = UPT ADDRESS?
         BNE   *+L'*+10            NO
         DROP  R1
         CLC   APSCB(L'APSCB),CPPLPSCB CPPL+8 = PSCB ADDRESS?
         BE    INITOK              YES, TSO COMMAND
         LR    R1,R2
         USING IKJEBECA,R1
         L     R2,CAPTTMP          CPPL ADDRESS
         DROP  R1
         CLC   APSCB(L'APSCB),CPPLPSCB CPPL+8 = PSCB ADDRESS?
         BE    INITOK              YES, TSO SUB-COMMAND
         DROP  R2
ERREX   WTO    ' -> Only TSO foreground command allowed',              X
               ROUTCDE=11,DESC=7
         B     SRC16
INITOK   ST    R2,ACPPL            SAVE CPPL ADDRESS FOR LATER
        #GO    GETCMD              HANDLE OPERAND(S) IF ANY
        @FSI   FSBFSZ,TERMAT,RQSZ,MF=(E,FSPARM) INITIALIZE FULL-SCREEN
         LTR   R15,R15
         BZ    INFSOK
         MVI   MSG1F1,C'I'
         CVD   R15,WORKDBLW
         MVC   MSG1F2,=XL4'40202120'
         ED    MSG1F2,WORKDBLW+L'WORKDBLW-2
        TPUT   MSG1,L'MSG1         PUT OUT ERROR MESSAGE
         B     SRC16
INFSOK  #GO    VERIFY              GO VERIFY TERMINAL
         B     *+L'*(R15)          HOW COMPLETE?
         B     VERFOK              0 - OK, CONTINUE
         B     DRERROR             4 - ERROR
         B     DRCLEAR             8 - CLEAR AND END
VERFOK   L     R2,ATIOT            SEARCH DD IF SNAP AVAILABLE
         USING TIODSECT,R2
SCSNAP   CLC   TIOELNGH(4),=F'0'
         BE    DRIVER
         CLC   TIOEDDNM(L'TIOEDDNM),=CL8'SYSSNAP '
         BNE   *+L'*+4
         OI    OPTFS,FSSNAP        SET USER WANT SNAP SCREEN INPUT
         XR    R1,R1
         IC    R1,TIOELNGH
         ALR   R2,R1
         B     SCSNAP
         DROP  R2
DRIVER   L     R0,STARCHRL
         LR    R2,R0
        GETMAIN R,LV=(0)
         ST    R1,STARCHRT
         AR    R1,R2
         ST    R1,STARCHRX
         TM    SWEXEC,SWCOMP       GO DIRECTLY TO COMPUTER?
         BO    REINIT              YES
        #GO    HEADER              DISPLAY HEADER
         B     *+L'*(R15)          HOW COMPLETE?
         B     REINIT              0 - OK, CONTINUE
         B     DRERROR             4 - ERROR
         B     DRCLEAR             8 - CLEAR AND END
REINIT  #GO    INITMEM             INITIALIZE MEMORY
*        --> STAR-TREK main process.
DSLOOP  #GO    DSPLAYS             BUILD AND INVOKE PRIMARY SCREEN
         B     *+L'*(R15)          HOW COMPLETE?
         B     *+L'*+8             0 - OK, CONTINUE
         B     DRERROR             4 - ERROR
         B     DRCLEAR             8 - CLEAR AND END
        #GO    CLLCMND             INVOKE REQUESTED COMMAND
         B     *+L'*(R15)          HOW COMPLETE?
         B     *+L'*+12            0 - OK, CONTINUE
         B     DRERROR             4 - ERROR
         B     DRCLEAR             8 - CLEAR AND END
         B     DSLOOP              12 - DIAGNOSE (SYNTAX)
         CLI   SWITCH,C' '
         BNE   ENDGAME             INTERUPT
        #GO    REACTIO             THE KLINGONS FIRE BACK
         B     *+L'*(R15)          HOW COMPLETE?
         B     *+L'*+4             0 - OK
         B     DRCLEAR             4 - SEVERE ERROR, CLEAR AND END
         CLI   SWITCH,C' '
         BE    DSLOOP              LOOP
ENDGAME #GO    DSPLEND             END OF GAME
         B     *+L'*(R15)          HOW COMPLETE?
         B     REINIT              0 - OK, ANOTHER ONE
         B     DRERROR             4 - ERROR
         B     DRCLEAR             8 - CLEAR AND END
         EJECT
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
*                        T E R M I N A T I O N                        *
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
         SPACE 1
DRERROR  CVD   R15,WORKDBLW
         MVC   MSG2F2,=XL4'40202120'
         ED    MSG2F2,WORKDBLW+L'WORKDBLW-2
         XR    R0,R0
         IC    R0,OPTFS
         STC   R0,MSG2F1+1
         SRL   R0,4
         STC   R0,MSG2F1
         NC    MSG2F1,=XL2'0F0F'
         TR    MSG2F1,=CL16'0123456789ABCDEF'
         LA    R0,L'MSG2
         STH   R0,ERRMSL
         LA    R0,MSG2
         ST    R0,ERRMSA
DRCLEAR  MVI   OPTFS,FSEXIT        EXIT FULL-SCREEN MODE
        @FS    OPTFS,MF=(E,FSPARM)
         LR    R2,R15              RETAIN RETURN CODE
         ICM   R1,B'1111',STARCHRT
         BZ    NOSTCH
         L     R0,STARCHRL
        FREEMAIN R,LV=(0),A=(1)
NOSTCH   XR    R0,R0
         ICM   R0,B'0011',ERRMSL
         BZ    NOMSGE
         L     R1,ERRMSA
        TPUT   (1),(0)             PUT OUT ERROR MESSAGE
NOMSGE   LTR   R2,R2
         BZ    QUIT
         CVD   R2,WORKDBLW
         MVC   MSG2F2,=XL4'40202120'
         ED    MSG2F2,WORKDBLW+L'WORKDBLW-2
         XR    R0,R0
         IC    R0,OPTFS
         STC   R0,MSG2F1+1
         SRL   R0,4
         STC   R0,MSG2F1
         NC    MSG2F1,=XL2'0F0F'
         TR    MSG2F1,=CL16'0123456789ABCDEF'
        TPUT   MSG2,L'MSG2
SRC16    LA    R2,16               SET RC=16
QUIT    $XRET  CC=(R2)
         SPACE 1
        LTORG
         SPACE 1
         DROP  R12
         EJECT
*------> THIS ROUTINE HANDLES EVENTUAL COMMAND OPERAND(S).
*        AT EXIT : R15 = 0.
         SPACE 1
GETCMD  $XENT  BASE=(R9)
         L     R2,ACPPL            GET CPPL ADDRESS
         USING CPPL,R2
         L     R1,CPPLCBUF         GET COMMAND BUFFER ADDRESS
         DROP  R2
         LH    R3,0(R1)            BUFFER LENGTH
         SH    R3,=H'4'            PREFIX LENGTH
         LH    R2,2(R1)            OFFSET TO OPERAND(S)
         SR    R3,R2               OPERAND(S) LENGTH
         BNP   GETCMDX             NONE, JUMP
         LA    R0,L'CMOBUF         MAX. LENGTH ACCEPTABLE
         CLR   R3,R0
         BNH   *+L'*+2
         LR    R3,R0               TRUNCATE IF HIGHER
         LA    R2,4(R1,R2)         START ADDRESS OF OPERAND(S)
         BCT   R3,*+L'*+6          COMPUTE MACHINE LENGTH
         OC    CMOBUF(*-*),0(R2)   <<EXECUTED>>
         EX    R3,*-6              MOVE AND UPPERCASE OPERAND(S)
         LA    R3,CMOBUF(R3)       SET SCAN POINTERS
         LA    R2,1
         LA    R4,CMOBUF
OPSCN1   CLI   0(R4),C' '
         BNE   *+L'*+8
         BXLE  R4,R2,*-8
         B     GETCMDX
         LR    R14,R4
         BXLE  R4,R2,*+L'*+4
         B     *+L'*+8
         CLI   0(R4),C' '
         BNE   *-12
         LR    R15,R4
         SLR   R15,R14
         CH    R15,=H'1'
         BNE   OPSCN2
         CLI   0(R14),C'H'         BYPASS SCREEN TITLE?
         BNE   *+L'*+8             NO
         OI    SWEXEC,SWHELP       YES, SET BYPASS
         B     OPSCNX
         CLI   0(R14),C'G'         GO DIRECTLY TO COMPUTER?
         BNE   *+L'*+8             NO
         OI    SWEXEC,SWCOMP       YES, SET GO
         B     OPSCNX
         CLI   0(R14),C'B'         TEST BLOW SCREEN?
         BNE   OPSCNX              NO
         OI    SWEXEC,SWBLOW       YES, SET TEST
         B     OPSCNX
OPSCN2   CH    R15,=H'2'
         BNE   OPSCN3
         CLC   0(2,R14),=CL2'GO'   GO DIRECTLY TO COMPUTER?
         BNE   OPSCNX              NO
         OI    SWEXEC,SWCOMP       YES, SET GO
         B     OPSCNX
OPSCN3   CH    R15,=H'4'
         BNE   OPSCNX
         CLC   0(4,R14),=CL4'HELP' BYPASS SCREEN TITLE?
         BNE   *+L'*+8             NO
         OI    SWEXEC,SWHELP       YES, SET BYPASS
         B     OPSCNX
         CLC   0(4,R14),=CL4'BLOW' TEST BLOW SCREEN?
         BNE   OPSCNX              NO
         OI    SWEXEC,SWBLOW       YES, SET TEST
OPSCNX   CLR   R4,R3
         BL    OPSCN1
GETCMDX  XR    R15,R15             SET RC=0
        $XRET  CC=(R15)
         SPACE 1
CMOBUF   DC    CL80' '             COMMAND OPERAND(S)
         SPACE 1
        LTORG
         SPACE 1
         DROP  R9
         EJECT
*------> THIS ROUTINE VERIFY TERMINAL FEATURES.
*        AT EXIT : R15 = 0, 4 OR 8.
         SPACE 1
VERIFY  $XENT  BASE=(R9)
         NI    OPTFS,FSFULL+FSSNAP
         OI    OPTFS,FSSKIP
        @FS    OPTFS,SCRNI,SCRNIL,MF=(E,FSPARM)
         LTR   R15,R15
         BNZ   VERIFYE
         TM    FSATTRB3,FSTQUERY   CAN DO QUERY ON THIS TUBE?
         BZ    VERIFYR             NO, SKIP (NO COLORS, ...)
        @FSQ   ,                   YES, DO IT
         LTR   R15,R15
         BNZ   VERIFYQ
         LR    R2,R1               QUERY REPLY DATA
         LR    R3,R1
         ALR   R3,R0
         LA    R1,1
         SLR   R3,R1
VERIFY1  ALR   R2,R1
         CLR   R2,R3
         BH    VERIFYR
         CLC   2(2,R2),=XL2'8186'  COLORS DEFINED ?
         BE    SETEXC              YES
         CLC   2(2,R2),=XL2'8187'  HIGHLIGHTING DEFINED ?
         BE    SETEXH              YES
VERIFY2  XR    R1,R1
         ICM   R1,B'0011',0(R2)
         B     VERIFY1
SETEXC   CLC   0(2,R2),=H'6'
         BL    VERIFY2
         CLI   5(R2),8
         BL    VERIFY2
         OI    SWEXEC,FSEXC
         B     VERIFY2
SETEXH   CLC   0(2,R2),=H'5'
         BL    VERIFY2
         CLI   4(R2),4
         BL    VERIFY2
         OI    SWEXEC,FSEXH
         B     VERIFY2
VERIFYQ  MVI   MSG1F1,C'Q'
         CVD   R15,WORKDBLW
         MVC   MSG1F2,=XL4'40202120'
         ED    MSG1F2,WORKDBLW+L'WORKDBLW-2
         LA    R0,L'MSG1
         STH   R0,ERRMSL
         LA    R0,MSG1
         ST    R0,ERRMSA
         LA    R15,8               ERROR, SET RC=8
         B     VERIFYX
VERIFYE  LA    R15,4               ERROR, SET RC=4
         B     VERIFYX
VERIFYR  XR    R15,R15             SET RC=0
VERIFYX $XRET  CC=(R15)
         SPACE 1
        LTORG
         SPACE 1
         DROP  R9
         SPACE 1
SCRNIL   DC    A(LSCRNI)
SCRNI    $FS   CC=EW,WCC=(AL,RMDT),SBA=(24,79),MF=L
         $FS   SBA=(1,1),RA=(1,1,00),MF=L
         $FS   SBA=(1,1),SF=(IC),MF=L
LSCRNI   EQU   *-SCRNI
         EJECT
*------> THIS ROUTINE TPUT'S HEADERS.
*        AT EXIT : R15 = 0, 4 OR 8.
         SPACE 1
HEADER  $XENT  BASE=(R9)
         TM    SWEXEC,SWHELP       BYPASS SCREEN TITLE?
         BO    HEAD20              YES, BYPASS
         L     R8,=A(SCRNT,LSCRNTL,RSCRTH)
         USING SCRNT,R8
        $FS    SF=NORMAL,MF=(I,SCTLN01I) SET NORMAL INPUT
         MVI   INPUT+0,C' '        RESET INPUT FIELD
HEAD10   MVC   SCTLN01R,INPUT+0
         LM    R0,R2,=A(SCRNT,LSCRNTL,RSCRTH)
         TM    SWEXEC,FSEXC+FSEXH  DOES THIS TUBE SUPPORT COLOR?
         BO    *+L'*+10            YES
         L     R1,=A(LSCRNTS)      NO, USE SHORT LENGTH
         MVC   SCTCOLOR(L'SCTLA+L'SCTIC),SCTLA
         STM   R0,R2,ASCRIPTR      STORE PTR'S
        #GO    FULLSCR             HEADER (TITLE) SCREEN
         B     *+L'*(R15)          HOW COMPLETE?
         B     HEAD20              0 - CONTINUE
         B     HEADERX             4 - ERROR
         LTR   R1,R1               8 - DID USER TYPE IN?
         BZ    HEAD20              NO, CONTINUE
         BM    HEADERX             YES, IMMEDIATE TERMINATION
        $FS    SF=(INT),MF=(I,SCTLN01I) SET ERROR INPUT
         B     HEAD10
         DROP  R8
HEAD20   L     R8,=A(SCRNH,LSCRNH,RSCRTH)
         USING SCRNH,R8
        $FS    SF=NORMAL,MF=(I,SCHLN01I) SET NORMAL INPUT
         MVI   INPUT+0,C' '        RESET INPUT FIELD
HEAD21   MVC   SCHLN01R,INPUT+0
         LM    R0,R2,=A(SCRNH,LSCRNH,RSCRTH)
         STM   R0,R2,ASCRIPTR      STORE PTR'S
        #GO    FULLSCR             HEADER (DESCRIPTION) SCREEN
         B     *+L'*(R15)          HOW COMPLETE?
         B     HEAD30              0 - CONTINUE
         B     HEADERX             4 - ERROR
         LTR   R1,R1               8 - DID USER TYPE IN?
         BZ    HEAD30              NO, CONTINUE
         BM    HEADERX             YES, IMMEDIATE TERMINATION
        $FS    SF=(INT),MF=(I,SCHLN01I) SET ERROR INPUT
         B     HEAD21
         DROP  R8
HEAD30   XR    R15,R15             SET RC=0
HEADERX $XRET  CC=(R15)
         SPACE 1
        LTORG
         SPACE 1
         DROP  R9
         SPACE 1
SCRNT    $FS   CC=EW,WCC=(KBR,RMDT),SBA=(1,1),RA=(1,1,00),MF=L
         $FS   SBA=(1,1),MF=L
SCTLN01I $FS   SF=NORMAL,MF=L
SCTLN01R $FS   TEXT=(' ',1),MF=L
         $FS   SF=(SKIP,INT),MF=L
         $FS   TEXT=' SSSSSSSSSSS      TTTTTTTTTTTTTTTT   ',MF=L
         $FS   TEXT='   AAAAAAAAAAAA      RRRRRRRRRRRRRR     ',MF=L
         $FS   TEXT='   SS         SS     TTTTTTTTTTTTTTTT   ',MF=L
         $FS   TEXT='  AAAAAAAAAAAAAA     RR           RR    ',MF=L
         $FS   TEXT='  SS           SS           TT          ',MF=L
         $FS   TEXT=' AA            AA    RR            RR   ',MF=L
         $FS   TEXT='  SS           SS           TT          ',MF=L
         $FS   TEXT=' AA            AA    RR            RR   ',MF=L
         $FS   TEXT='   SS                       TT          ',MF=L
         $FS   TEXT=' AA            AA    RR           RR    ',MF=L
         $FS   TEXT='    SSSSSSSSSSS             TT          ',MF=L
         $FS   TEXT=' AAAAAAAAAAAAAAAA    RRRRRRRRRRRRRR     ',MF=L
         $FS   TEXT='              SS            TT          ',MF=L
         $FS   TEXT=' AAAAAAAAAAAAAAAA    RR       RR        ',MF=L
         $FS   TEXT='  SS           SS           TT          ',MF=L
         $FS   TEXT=' AA            AA    RR        RR       ',MF=L
         $FS   TEXT='  SS           SS           TT          ',MF=L
         $FS   TEXT=' AA            AA    RR         RR      ',MF=L
         $FS   TEXT='   SS         SS            TT          ',MF=L
         $FS   TEXT=' AA            AA    RR          RR     ',MF=L
         $FS   TEXT='    SSSSSSSSSSS             TT          ',MF=L
         $FS   TEXT=' AA            AA    RR           RR    ',MF=L
         $FS   SBA=(14,1),MF=L
         $FS   TEXT='TTTTTTTTTTTTTTTTT    RRRRRRRRRRRRRR     ',MF=L
         $FS   TEXT=' EEEEEEEEEEEEEEEE    KK            KK   ',MF=L
         $FS   TEXT='TTTTTTTTTTTTTTTTT    RR           RR    ',MF=L
         $FS   TEXT=' EEEEEEEEEEEEEEEE    KK          KK     ',MF=L
         $FS   TEXT='       TT            RR            RR   ',MF=L
         $FS   TEXT=' EE                  KK        KK       ',MF=L
         $FS   TEXT='       TT            RR            RR   ',MF=L
         $FS   TEXT=' EE                  KK      KK         ',MF=L
         $FS   TEXT='       TT            RR           RR    ',MF=L
         $FS   TEXT=' EE                  KK    KK           ',MF=L
         $FS   TEXT='       TT            RRRRRRRRRRRRRR     ',MF=L
         $FS   TEXT=' EEEEEEEEEE          KK  KKK            ',MF=L
         $FS   TEXT='       TT            RR      RR         ',MF=L
         $FS   TEXT=' EE                  KKKK  KK           ',MF=L
         $FS   TEXT='       TT            RR       RR        ',MF=L
         $FS   TEXT=' EE                  KK      KK         ',MF=L
         $FS   TEXT='       TT            RR        RR       ',MF=L
         $FS   TEXT=' EE                  KK        KK       ',MF=L
         $FS   TEXT='       TT            RR         RR      ',MF=L
         $FS   TEXT=' EEEEEEEEEEEEEEEE    KK          KK     ',MF=L
         $FS   TEXT='       TT            RR          RR     ',MF=L
         $FS   TEXT=' EEEEEEEEEEEEEEEE    KK            KK   ',MF=L
LSCRNTS  EQU   (*-SCRNT)+L'SCTLA+L'SCTIC
SCTCOLOR $FS   SBA=(1,3),MF=L
         $FS   MFE=(HILITE,DEFAULT,COLOR,GREEN),MF=L
         $FS   SBA=(13,80),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,TURQ),MF=L
SCTLA    $FS   SBA=(1,2),MF=L
SCTIC    $FS   SF=(IC),MF=L
LSCRNTL  EQU     *-SCRNT
         SPACE 1
SCRNH    $FS   CC=EW,WCC=(KBR,RMDT),SBA=(1,1),RA=(1,1,00),MF=L
         $FS   SBA=(1,1),MF=L
SCHLN01I $FS   SF=NORMAL,MF=L
SCHLN01R $FS   TEXT=(' ',1),MF=L
         $FS   SF=(SKIP,INT),MF=L
         $FS   SBA=(1,26),TEXT='**** Welcome to STAR-TREK ****',MF=L
         $FS   SBA=(2,5),MF=L
         AIF   ('&EUDCH' EQ 'EUROPE').DCH1
         $FS   TEXT='HI!',MF=L
         $FS   SF=(SKIP),TEXT='Welcome to',MF=L
         $FS   SF=(SKIP,INT),TEXT='STAR-TREK!',MF=L
         AGO   .DCH2
.DCH1    ANOP
         $FS   TEXT='HI|',MF=L
         $FS   SF=(SKIP),TEXT='Welcome to',MF=L
         $FS   SF=(SKIP,INT),TEXT='STAR-TREK|',MF=L
.DCH2    $FS   SF=(SKIP),TEXT='This game is based on the',MF=L
         $FS   SF=(SKIP,INT),TEXT='STAR-TREK TV',MF=L
         $FS   SF=(SKIP),TEXT='series, and',MF=L
         $FS   SBA=(3,1),MF=L
         $FS   TEXT='is intended for those of us that ',MF=L
         $FS   TEXT='just never were able to get enough.',MF=L
         $FS   SBA=(4,5),MF=L
         $FS   TEXT='The context of the game is a space ',MF=L
         $FS   TEXT='battle between the',MF=L
         $FS   SF=(SKIP,INT),TEXT='Federation starship',MF=L
         $FS   SBA=(5,1),TEXT='ENTERPRISE',MF=L
         $FS   SF=(SKIP),TEXT='and several',MF=L
         $FS   SF=(SKIP,INT),TEXT='KLINGON',MF=L
         $FS   SF=(SKIP),TEXT='vessels. The',MF=L
         $FS   SF=(SKIP,INT),TEXT='ENTERPRISE',MF=L
         $FS   SF=(SKIP),TEXT='has been patrolling the',MF=L
         $FS   SBA=(6,1),MF=L
         $FS   TEXT='ROMULA ANDROS IV sector at the outbreak ',MF=L
         $FS   TEXT='of interstellar war. The mission is now',MF=L
         $FS   SBA=(7,1),TEXT='to defeat the',MF=L
         $FS   SF=(SKIP,INT),TEXT='KLINGON',MF=L
         $FS   SF=(SKIP),TEXT='vessels in a specific number of',MF=L
         $FS   SF=(SKIP,INT),TEXT='"PARALLAX YEARS".',MF=L
         $FS   SBA=(8,4),SF=(SKIP),MF=L
         $FS   TEXT='Following this page, you will be introduced',MF=L
         $FS   TEXT=' to the main screen of the ships ',MF=L
         $FS   SBA=(9,1),MF=L
         $FS   TEXT='battle computer. By skillfully using ti',MF=L
         $FS   TEXT='me, photon torpedoes, and phasers, you',MF=L
         $FS   SBA=(10,1),TEXT='can defeat the',MF=L
         $FS   SF=(SKIP,INT),TEXT='KLINGONS',MF=L
         $FS   SF=(SKIP),TEXT='and save the',MF=L
         $FS   SF=(SKIP,INT),TEXT='Federation.',MF=L
         $FS   SBA=(11,4),SF=(SKIP),MF=L
         $FS   TEXT='The screen of the battle computer is',MF=L
         $FS   TEXT=' made up of four distinct sections. The',MF=L
         $FS   SBA=(12,1),MF=L
         $FS   TEXT='upper left section contains the long ',MF=L
         $FS   TEXT='range sensors. This is a display of the',MF=L
         $FS   SBA=(13,1),MF=L
         $FS   TEXT='status (XYZ, where X=KLINGONS, Y=BASES ',MF=L
         $FS   TEXT='and Z=STARS) of the sector, broken into',MF=L
         $FS   SBA=(14,1),MF=L
         $FS   TEXT='quadrants, and referred to by their',MF=L
         $FS   SF=(SKIP,INT),TEXT='(x,y)',MF=L
         $FS   SF=(SKIP),TEXT='axis coordinates (1,1 to 4,4). The',MF=L
         $FS   SBA=(15,1),MF=L
         $FS   TEXT='upper right section contains the short ',MF=L
         $FS   TEXT='range sensors. This is a display of the',MF=L
         $FS   SBA=(16,1),MF=L
         $FS   TEXT='status of the quadrant you are in.',MF=L
         $FS   SF=(SKIP,INT),TEXT='STARS',MF=L
         $FS   SF=(SKIP),TEXT='are represented by an',MF=L
         $FS   SF=(SKIP,INT),TEXT='"*",',MF=L
         $FS   SF=(SKIP),TEXT='the',MF=L
         $FS   SF=(SKIP,INT),MF=L
         $FS   SBA=(17,1),TEXT='ENTERPRISE',MF=L
         $FS   SF=(SKIP),TEXT='by',MF=L
         $FS   SF=(SKIP,INT),TEXT='"E",',MF=L
         $FS   SF=(SKIP),TEXT='and',MF=L
         $FS   SF=(SKIP,INT),TEXT='KLINGONS',MF=L
         $FS   SF=(SKIP),TEXT='by',MF=L
         $FS   SF=(SKIP,INT),TEXT='"K".',MF=L
         $FS   SF=(SKIP),MF=L
         $FS   TEXT='The lower right section is a display',MF=L
         $FS   SBA=(18,1),TEXT='of the current operating ',MF=L
         $FS   TEXT='statistics of the',MF=L
         $FS   SF=(SKIP,INT),TEXT='ENTERPRISE.',MF=L
         $FS   SF=(SKIP),TEXT='The lower left section',MF=L
         $FS   SBA=(19,1),MF=L
         $FS   TEXT='is the area you use to communicate ',MF=L
         $FS   TEXT='with the battle computer.',MF=L
         $FS   SBA=(20,5),MF=L
         $FS   TEXT='If you run out of supplies or energy ',MF=L
         $FS   TEXT='you can get refueled by going to a',MF=L
         $FS   SF=(SKIP,INT),TEXT='STAR',MF=L
         $FS   SBA=(21,1),TEXT='BASE,',MF=L
         $FS   SF=(SKIP),TEXT='represented by a',MF=L
         $FS   SF=(SKIP,INT),TEXT='"B"',MF=L
         $FS   SF=(SKIP),TEXT='(move on every side, ',MF=L
         $FS   TEXT='unless diagonally).',MF=L
         $FS   SBA=(22,5),MF=L
         $FS   TEXT='Note, however, that every time you use the',MF=L
         $FS   SF=(SKIP,INT),TEXT='WARP DRIVE',MF=L
         $FS   SF=(SKIP),TEXT='it costs you at least',MF=L
         $FS   SBA=(23,1),TEXT='a year. To bypass these ',MF=L
         $FS   TEXT='introduction screens, call',MF=L
         $FS   SF=(SKIP,INT),TEXT='STREK',MF=L
         $FS   SF=(SKIP),TEXT='with option',MF=L
         $FS   SF=(SKIP,INT),TEXT='GO.',MF=L
         $FS   SF=(SKIP),MF=L
         $FS   SBA=(24,27),SF=(SKIP,INT),MF=L
         $FS   TEXT='G O O D    L U C K    ',MF=L
         AIF   ('&EUDCH' EQ 'EUROPE').DCH3
         $FS   TEXT='!!!!',MF=L
         AGO   .DCH4
.DCH3    ANOP
         $FS   TEXT='||||',MF=L
.DCH4    $FS   SF=(SKIP),MF=L
         $FS   SBA=(1,2),SF=(IC),MF=L
LSCRNH   EQU   *-SCRNH
         EJECT
*------> READ "SCRNT" AND "SCRNH" PROCESS ROUTINE.
*        AT EXIT : R1=INPUT TRUE LENGTH.
         SPACE 1
RSCRTH  $XENT  BASE=(R9)
         XR    R2,R2               INITIAL COUNT
RHLOOP  @FSR   ,                   GET REPLY TEXT
         LTR   R15,R15             NO MORE FIELDS MODIFIED?
         BNZ   RHPRCD              NO
         L     R1,4(R13)           YES, END, GET PREVIOUS SAVE AREA
         ST    R2,24(R1)           PASS BACK COUNT IN R1
         XR    R15,R15             EXIT
        $XRET  CC=(R15)
RHPRCD   CLC   SCHCSF1,0(R15)
         BNE   RHLOOP              UNEXPECTED (CHECK SCREEN IMAGE)
         LTR   R1,R1
         BZ    RHFC1               FIELD CLEARED (CANC FC)
         CH    R1,=H'1'
         BNE   RHLOOP              UNEXPECTED (CHECK SCREEN IMAGE)
         MVI   INPUT+0,C' '        FOR UPPERCASE
         OC    INPUT+0(1),3(R15)   COPY OPTION
         B     RHBF1
RHFC1    MVI   INPUT+0,C' '        OPTION CLEARED
RHBF1    LA    R2,1(R2)            BUMP COUNT
         B     RHLOOP
         SPACE 1
SCHCSF1  $FS   SBA=(1,2),MF=L
         SPACE 1
        LTORG
         SPACE 1
         DROP  R9
         EJECT
*------> THIS ROUTINE INITIALIZES MEMORY.
*        AT EXIT : R15 = 0.
         SPACE 1
INITMEM $XENT  BASE=(R9)
         LM    R0,R1,STARCHRT      CLEAR ALL TO INITIAL STATE
         LA    R14,*
         XR    R15,R15
         MVCL  R0,R14
         MVI   INPUT,C' '
         MVC   INPUT+1(L'INPUT-1),INPUT+1
         MVC   SCRTOI,=CL7'_______'
         MVI   SWITCH,C' '
         MVI   SCMSG1,C' '
         MVC   SCMSG1+1(L'SCMSG1-1),SCMSG1
         MVC   SCMSG2,SCMSG1
         MVC   SCMSG3,SCMSG1
         XC    KIRKPTR,KIRKPTR
         ZAP   GAMENRGY,=PL3'5000'
         MVC   GAMEDATE,=CL6'5537.1'
         ZAP   GAMETORP,=PL2'10'
         MVI   STATUS,C'1'
         ZAP   DEFLECT,=PL2'100'
        #GO    RANDOMR             GET RANDOM NUMBER (0-9)
         LTR   R2,R1               COPY REGISTER
         BZ    *+L'*+4
         MH    R2,=Y(L'GAMETBL)    MULTIPLY BY NODE LENGTH
         LA    R3,GAMETBL          GET START ADDRESS
         AR    R3,R2               ADD TO BASE
         MVC   GAMESTAT,0(R3)      MOVE THE GAME STATISTICS
        TIME   MIC,TIMEAREA        GET BASE TIME
         MVC   TIMEAREA(2),TIMEAREA+5 ALIGN
         NI    TIMEAREA,X'03'      CLEAR BITS DOWN TO MOST 1K-1
         LH    R2,TIMEAREA         LOAD INTO WORK REGISTER
         ZAP   INITKLNG,GAMEKLNG   INITIALIZE COUNTERS
         ZAP   INITBASE,GAMEBASE    " "
         ZAP   INITSTRS,GAMESTRS    " "
         B     INTL2
INTL1    SRL   R2,2                JUMBLE DISP PTR
        TIME   MIC,TIMEAREA        GET TIME FOR RANDOM FACTOR
         MVC   TIMEAREA(2),TIMEAREA+5 ALIGN
         NI    TIMEAREA,X'0F'      CLEAR HIGH ORDER NIBBLE
         LH    R4,TIMEAREA         LOAD INTO WORK REGISTER
         SRL   R4,4                CLEAR LOW ORDER NIBBLE (0<=X<256)
         AR    R2,R4               ADD RANDOM FACTOR
INTL2    CP    INITSTRS,=PL1'0'    ALL THE STARS USED UP?
         BE    TRYKLING
         LA    R2,129(R2)          BUMP DISPLACEMENT COUNTER
         CL    R2,STARCHRL         EXCEEDED TABLE?
         BH    INTL1               YES, CLEAR
         L     R3,STARCHRT         START CHART PTR
         AR    R3,R2               ADD DISPLACEMENT
         CLI   0(R3),X'00'         HAS THIS POINT BEEN USED YET?
         BNE   TRYKLING
         MVI   0(R3),X'03'         MARK AS A STAR
         SP    INITSTRS,=PL1'1'    DECREMENT STAR COUNTER
TRYKLING CP    INITKLNG,=PL1'0'    ALL THE KLINGONS USED UP?
         BE    TRYBASES
         LA    R2,129(R2)          BUMP DISPLACEMENT COUNTER
         CL    R2,STARCHRL         EXCEEDED TABLE?
         BH    INTL1               YES, CLEAR
         L     R3,STARCHRT         START CHART PTR
         AR    R3,R2               ADD DISPLACEMENT
         CLI   0(R3),X'00'         HAS THIS POINT BEEN USED YET?
         BNE   TRYBASES
         MVI   0(R3),X'01'         MARK AS A KLINGON
         SP    INITKLNG,=PL1'1'    DECREMENT KLINGON COUNTER
TRYBASES CP    INITBASE,=PL1'0'    ALL THE BASES USED UP?
         BE    INTL3
         LA    R2,129(R2)          BUMP DISPLACEMENT COUNTER
         CL    R2,STARCHRL         EXCEEDED TABLE?
         BH    INTL1               YES, CLEAR
         L     R3,STARCHRT         STAR CHART PTR
         AR    R3,R2               ADD DISPLACEMENT
         CLI   0(R3),X'00'         HAS THIS POINT BEEN USED YET?
         BNE   INTL2
         MVI   0(R3),X'02'         MARK AS A BASE
         SP    INITBASE,=PL1'1'    DECREMENT BASE COUNTER
         B     INTL2
INTL3    CP    INITKLNG,=PL1'0'    NO KLINGONS ALSO?
         BNE   INTL2
         CP    INITSTRS,=PL1'0'    NO STARS EITHER
         BNE   INTL2
         L     R3,STARCHRT         STAR CHART PTR
INTKIRK TIME   MIC,TIMEAREA        GET BASE TIME
         MVC   TIMEAREA(2),TIMEAREA+5 ALIGN
         NI    TIMEAREA,X'03'      CLEAR BITS DOWN TO MOST 1K-1
         LH    R2,TIMEAREA         ADD DISP
         AR    R2,R3               GET ADDRESS IN STAR CHART
         CLI   0(R2),X'00'         IS IT EMPTY
         BNE   INTKIRK
         MVI   0(R2),X'04'         PUT CAPT. KIRK IN IT
*        --> NOW LET'S JUMBLE IT UP A BIT.
         LR    R2,R3
         AH    R3,=H'768'
         LA    R4,128
JUMBLE1  XC    0(1,R2),0(R3)       FLIP THE TWO BYTES
         XC    0(1,R3),0(R2)        " "
         XC    0(1,R2),0(R3)        " "
         LA    R2,2(R2)            BUMP 1ST PTR
         LA    R3,2(R3)             " " 2ND PTR
         BCT   R4,JUMBLE1          ITERATE
         L     R2,STARCHRT
         LR    R3,R2
         AH    R2,=H'512'
         AH    R3,=H'256'
         LA    R4,128
JUMBLE2  XC    0(1,R2),0(R3)       FLIP THE TWO BYTES
         XC    0(1,R3),0(R2)        " "
         XC    0(1,R2),0(R3)        " "
         LA    R2,2(R2)            BUMP 1ST PTR
         LA    R3,2(R3)             " " 2ND PTR
         BCT   R4,JUMBLE2          ITERATE
         XR    R15,R15             SET RC=0
        $XRET  CC=(R15)
         SPACE 1
        LTORG
         SPACE 1
         DROP  R9
         EJECT
*------> THIS ROUTINE REBUILDS THE MAIN DISPLAY SCREEN.
*        AT EXIT : R15 = 0, 4 OR 8.
DSPLAYS $XENT  BASE=(R9)
         L     R8,=A(SCRN1,LSCRN1L,RSCRT1)
         USING SCRN1,R8
*        --> FORMAT LOGON RANGE SENSORS.
         L     R1,STARCHRT         INIT PTR TO STAR CHART
         LA    R2,SC1LRS            " "             SCREEN
         LA    R3,4                4 LINES PER SECTOR
DSPLOOP1 LA    R4,4                QUADRANTS PER LINE
DSPLOOP2 LA    R5,64               64 POSITIONS PER QUADRANT
         ZAP   ACCUM1,=PL1'0'
         ZAP   ACCUM2,=PL1'0'
         ZAP   ACCUM3,=PL1'0'
DSPLOOP3 CLI   0(R1),X'00'         EMPTY?
         BE    DSPEMPTY
         CLI   0(R1),X'01'         KLINGON?
         BNE   *+L'*+10
         AP    ACCUM1,=PL1'1'      COUNT KLINGONS
         B     DSPEMPTY
         CLI   0(R1),X'02'         BASE
         BNE   DSPTYPE3
         CP    ACCUM2,=PL1'0'      CAN'T HAVE TWO BASE IN 1 QUAD
         BNE   *+L'*+10
         AP    ACCUM2,=PL1'1'      LET USER HAVE AT LEAST 1
         B     DSPEMPTY
         MVI   0(R1),X'00'         SORRY - CLEAR IT OUT
         SP    GAMEBASE,=PL1'1'    SUBTRACT FROM TOTAL PER GAME
         B     DSPEMPTY
DSPTYPE3 CLI   0(R1),X'03'         STAR?
         BNE   *+L'*+10
         AP    ACCUM3,=PL1'1'      COUNT STARS
         B     DSPEMPTY
         CLI   0(R1),X'04'         ENTERPRISE?
         BE    *+L'*+8
         MVI   MSG3F1,C'A'         IMPOSSIBLE - BOMB
         B     DSPLAYZ
         ST    R1,KIRKPTR          STORE ADDRESS OF ENTERPRISE
DSPEMPTY LA    R1,1(R1)            BUMP STAR CHART PTR
         BCT   R5,DSPLOOP3         LOOP THRU MINOR INDEX
         UNPK  0(1,R2),ACCUM1      PUT KLINGONS IN LONG RANGE
         OI    0(R2),X'F0'
         UNPK  1(1,R2),ACCUM2          BASES
         OI    1(R2),X'F0'
         UNPK  2(1,R2),ACCUM3          STARS
         OI    2(R2),X'F0'
         LA    R2,SC1LRSN(R2)      BUMP SCREEN PTR
         BCT   R4,DSPLOOP2         ITERATE THRU SECOND LEVEL INDEX
         LA    R2,SC1LRSNL(R2)     MOVE SCREEN PTR TO NEXT LINE
         BCT   R3,DSPLOOP1         ITERATE THRU MAJOR INDEX
*        --> FORMAT QUADRANT INDICATOR.
         L     R2,KIRKPTR          GET ADDRESS OF ENTERPRISE
         S     R2,STARCHRT         DO RELATIVE ADDRESS
         SRL   R2,6                DIVIDE BY 64
         ST    R2,KIRKPTR          SAVE RELATIVE QUADRANT
         LTR   R3,R2
         BZ    *+L'*+10
         AR    R3,R2
         MH    R2,=Y(L'QUADCVT1)   MULTIPLY BY LENGTH
         MH    R3,=Y(L'QUADCVT3)   MULTIPLY BY LENGTH
         LA    R2,QUADCVT1(R2)     GET ADDRESS IN CONVERSION TABLE
         MVC   SC1LN13N,0(R2)      PUT CURRENT QUADRANT IN SCREEN
         LA    R3,QUADCVT3(R3)     GET ADDRESS IN SBA TABLE
         TM    SWEXEC,FSEXC+FSEXH  DOES THIS TUBE SUPPORT COLOR?
         BO    DSPLCOL             YES
         LA    R4,SC1COLOR         NO, SET CURRENT QUADRANT IN LOW
         MVC   0(L'QUADCVT3,R4),0(R3)
         LA    R4,L'QUADCVT3(R4)
        $FS    SF=(SKIP),MF=(E,(R4))
         MVC   0(L'QUADCVT3,R4),0+L'QUADCVT3(R3)
         LA    R4,L'QUADCVT3(R4)
        $FS    SF=(SKIP,INT),MF=(E,(R4))
DSPLCOL  MVC   SC1LNCXY,0(R3)      PUT SBA CURRENT QUADRANT IN SCREEN
*        --> FORMAT SHORT RANGE SENSORS AND POST COMBAT STATUS.
         L     R4,KIRKPTR          GET RELATIVE QUADRANT
         SLL   R4,6                MULTIPLY BY 64
         A     R4,STARCHRT         GET ADDRESS OF QUADRANT
         CLI   STATUS,C'4'         IS THE ENTERPRISE DOCKED?
         BE    DSPSTBYP            YES, BYPASS
         LR    R3,R4
         XC    TRTABLE,TRTABLE     CLEAR SEARCH TABLE
         MVI   TRTABLE+X'01',X'01' POST KLINGON FOR SEARCH
         TRT   0(64,R3),TRTABLE    SEARCH QUADRANT FOR KLINGON
         BNZ   DSPR1               FOUND, GO POST COND RED
         CP    GAMENRGY,=PL3'1000' LOW ON ENERGY
         BNH   DSPY1               GO POST COND YELLOW
         CP    GAMEYRS,=PL1'5'     LOW ON YEARS
         BNH   DSPY1               GO POST COND YELLOW
         MVI   STATUS,C'1'         POST COND GREEN
        $FS    SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,GREEN),       X
               MF=(I,SC1ATT)
         B     DSPSTBYP
DSPY1    MVI   STATUS,C'2'         POST COND YELLOW
        $FS    SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,YELLOW),      X
               MF=(I,SC1ATT)
         B     DSPSTBYP
DSPR1    MVI   STATUS,C'3'         POST COND RED
        $FS    SFE=(ATTR,(SKIP,INT),HILITE,BLINK,COLOR,RED),           X
               MF=(I,SC1ATT)
*        --> COMPUTE SCREEN EFFICIENCY PERCENTAGE.
         ZAP   WORKDBLW,GAMENRGY   GET REMAINING ENERGY
         MP    WORKDBLW,=PL2'100'  * 100
         DP    WORKDBLW,=PL3'5000' DIVIDE BY STARTING ENERGY
         ZAP   DEFLECT,WORKDBLW+3(2) SAVE THE PERCENTAGE
         MVC   SCMSG3,=CL44'You are under attack'
DSPSTBYP LA    R5,SC1SRS           GET ADDRESS OF SHORT RANGE SENSORS
         LA    R2,8                8 LINES PER QUADRANT
DSPLOOP4 LA    R3,8                8 POSITIONS PER LINE
DSPLOOP5 CLI   0(R4),X'00'         IS IT EMPTY?
         BNE   *+L'*+8
         MVI   0(R5),C'.'          MARK AS EMPTY
         B     DSPLOOP6
         CLI   0(R4),X'01'         IS IT A KLINGON?
         BNE   *+L'*+8
         MVI   0(R5),C'K'          MARK AS KLINGON
         B     DSPLOOP6
         CLI   0(R4),X'02'         IS IT A STAR BASE?
         BNE   *+L'*+8
         MVI   0(R5),C'B'          MARK AS STAR BASE
         B     DSPLOOP6
         CLI   0(R4),X'03'         IS IT A STAR?
         BNE   *+L'*+8
         MVI   0(R5),C'*'          MARK AS STAR
         B     DSPLOOP6
         CLI   0(R4),X'04'         IS IT THE ENTERPRISE?
         BNE   *+L'*+8
         MVI   0(R5),C'E'          MARK AS ENTERPRISE
         B     DSPLOOP6
         MVI   MSG3F1,C'B'         IMPOSSIBLE - BOMB
DSPLAYZ  LA    R0,L'MSG3
         STH   R0,ERRMSL
         LA    R0,MSG3
         ST    R0,ERRMSA
DSPLXIT  LA    R15,8               ERROR, SET RC=8
         B     DSPLAYX
DSPLOOP6 LA    R4,1(R4)            BUMP STARCHRT PTR
         LA    R5,SC1SRSN(R5)       " " SCREEN PTR
         BCT   R3,DSPLOOP5         ITERATE THRU LOW INDEX
         LA    R5,SC1SRSNL(R5)     GO TO NEXT LINE
         BCT   R2,DSPLOOP4         ITERATE THRU HIGH INDEX
*        --> FORMAT OPERATING STATISTICS.
         UNPK  SC1LN17N,GAMENRGY   DISPLAY ENERGY RESERVES
         OI    SC1LN17N+L'SC1LN17N-1,C'0'
         UNPK  SC1LN18N,GAMETORP   PHOTON TORPEDOES
         OI    SC1LN18N+L'SC1LN18N-1,C'0'
         UNPK  SC1LN19N,GAMEYRS    REMAINING YEARS
         OI    SC1LN19N+L'SC1LN19N-1,C'0'
         UNPK  SC1LN20N,GAMEKLNG   KLINGONS
         OI    SC1LN20N+L'SC1LN20N-1,C'0'
         UNPK  SC1LN21N,GAMEBASE   BASES
         OI    SC1LN21N+L'SC1LN21N-1,C'0'
         UNPK  SC1LN22N,DEFLECT    DEFLECTION PERCENTAGE
         OI    SC1LN22N+L'SC1LN22N-1,C'0'
         MVC   SC1LN23N,GAMEDATE   STAR DATE
*        --> FORMAT BATTLE CONDITION.
         CLI   STATUS,C'1'
         BNE   *+L'*+10
         MVC   SC1LN24N,=CL6'GREEN '
         B     STATUS1
         CLI   STATUS,C'2'
         BNE   *+L'*+10
         MVC   SC1LN24N,=CL6'YELLOW'
         B     STATUS1
         CLI   STATUS,C'3'
         BNE   *+L'*+10
         MVC   SC1LN24N,=CL6'RED   '
         B     STATUS1
         CLI   STATUS,C'4'
         BNE   STATUS1
         MVC   SC1LN24N,=CL6'DOCKED'
*        --> PUT MESSAGES IN SCREEN.
STATUS1  MVC   SC1LN22M,SCMSG1
         MVC   SC1LN23M,SCMSG2
         MVC   SC1LN24M,SCMSG3
*        --> PUT OUT THE SCREEN AND THEN RETURN IT.
SCPUT1   MVC   SC1LN17R,SCRTOI+0   MOVE TO SCREEN
         MVC   SC1LN18R,SCRTOI+1    " "
         MVC   SC1LN19R,SCRTOI+2    " "
         MVC   SC1LN20R,SCRTOI+3    " "
         MVC   INPUT+0(L'SCRTOI),SCRTOI COPY TO INPUT
         LM    R0,R2,=A(SCRN1,LSCRN1L,RSCRT1)
         TM    SWEXEC,FSEXC+FSEXH  DOES THIS TUBE SUPPORT COLOR?
         BO    *+L'*+10            YES
         L     R1,=A(LSCRN1S)      NO, USE SHORT LENGTH
         MVC   SC1COLOR+LSSC1N(L'SC1LA+L'SC1IC),SC1LA
         STM   R0,R2,ASCRIPTR      STORE PTR'S
        #GO    FULLSCR             PAINT THE SCREEN
         B     *+L'*(R15)          HOW COMPLETE?
         B     PFKEYS              0 - PF-KEY
         B     DSPLAYX             4 - ERROR
         LTR   R1,R1               8 - DID USER TYPE IN?
         BZ    SCPUT1              NO, RESHOW
         BM    DSPLAYX             YES, IMMEDIATE TERMINATION
         CLI   INPUT+0,C'0'        USER REQUEST DIRECTORY?
         BE    SCHELP1             YES
         MVC   SCRTOI,INPUT        MOVE COMMAND
        $FS    SF=NORMAL,MF=(I,SC1LN17I)
        $FS    SF=NORMAL,MF=(I,SC1LN18I)
        $FS    SF=NORMAL,MF=(I,SC1LN19I)
        $FS    SF=NORMAL,MF=(I,SC1LN20I)
        $FS    SBA=(17,5),                                             X
               MFE=(ATTR,NORMAL,HILITE,DEFAULT,COLOR,GREEN),           X
               MF=(I,SC1CMD1)
        $FS    SBA=(18,5),                                             X
               MFE=(ATTR,NORMAL,HILITE,DEFAULT,COLOR,GREEN),           X
               MF=(I,SC1CMD2)
        $FS    SBA=(19,5),                                             X
               MFE=(ATTR,NORMAL,HILITE,DEFAULT,COLOR,GREEN),           X
               MF=(I,SC1CMD3)
        $FS    SBA=(20,2),                                             X
               MFE=(ATTR,NORMAL,HILITE,DEFAULT,COLOR,GREEN),           X
               MF=(I,SC1CMD4)
         MVI   SCMSG1,C' '         CLEAR MSG LINES
         MVC   SCMSG1+1(L'SCMSG1-1),SCMSG1
         MVC   SCMSG2,SCMSG1
         MVC   SCMSG3,SCMSG1
         XR    R15,R15             SET RC=0
         B     DSPLAYX
PFKEYS   CLI   INPUT+0,C'9'        IMMEDIATE TERMINATION?
         BE    DSPLXIT             YES
         CLI   INPUT+0,C'1'        USER REQUEST DIRECTORY?
         BNE   SCPUT1              NO, RESHOW
         DROP  R8
SCHELP1  L     R8,=A(SCRN2,LSCRN2,RSCRTH)
         USING SCRN2,R8
        $FS    SF=NORMAL,MF=(I,SC2LN01I) SET NORMAL INPUT
         MVI   INPUT+0,C' '        RESET INPUT FIELD
SCHELP2  MVC   SC2LN01R,INPUT+0
         LM    R0,R2,=A(SCRN2,LSCRN2,RSCRTH)
         STM   R0,R2,ASCRIPTR      STORE PTR'S
        #GO    FULLSCR             COMMAND (HELP) SCREEN
         B     *+L'*(R15)          HOW COMPLETE?
         B     SCHELP3             0 - CONTINUE
         B     DSPLAYX             4 - ERROR
         LTR   R1,R1               8 - DID USER TYPE IN?
         BZ    SCHELP3             NO, CONTINUE
         BM    DSPLAYX             YES, IMMEDIATE TERMINATION
        $FS    SF=(INT),MF=(I,SC2LN01I) SET ERROR INPUT
         B     SCHELP2
         DROP  R8
SCHELP3  L     R8,=A(SCRN1,LSCRN1L,RSCRT1)
         B     SCPUT1
DSPLAYX $XRET  CC=(R15)
         SPACE 1
        LTORG
         SPACE 1
         DROP  R9
         SPACE 1
*------- MAIN BATTLE COMPUTER SCREEN LAYOUT.
SCRN1    $FS   CC=EW,WCC=(KBR,RMDT),SBA=(1,1),RA=(1,1,00),MF=L
         $FS   SBA=(1,1),MF=L
SC1LSF   $FS   SF=(SKIP,INT),MF=L
         $FS   TEXT='=========================          U   ',MF=L
         $FS   TEXT='U          ============================ ',MF=L
SC1LN02  $FS   TEXT=' = XXX = XXX = XXX = XXX = 4--Y     S   ',MF=L
SC1LRS   EQU   SC1LN02+3           INITIAL POINT
SC1LRSN  EQU   6                   NEXT ON SAME LINE
SC1LRSNL EQU   136                 TO FIRST ON NEXT LINE
         $FS   TEXT='S     Y--8 =  .  .  .  .  .  .  .  .  = ',MF=L
SC1SRS   EQU   SC1LN02+54          INITIAL POINT
SC1SRSN  EQU   3                   NEXT ON SAME LINE
SC1SRSNL EQU   56                  TO FIRST ON NEXT LINE
         $FS   TEXT=' =========================          S   ',MF=L
         $FS   TEXT='S        7 =  .  .  .  .  .  .  .  .  = ',MF=L
         $FS   TEXT=' = XXX = XXX = XXX = XXX = 3            ',MF=L
         $FS   TEXT='         6 =  .  .  .  .  .  .  .  .  = ',MF=L
         $FS   TEXT=' =========================          E   ',MF=L
         $FS   TEXT='E        5 =  .  .  .  .  .  .  .  .  = ',MF=L
         $FS   TEXT=' = XXX = XXX = XXX = XXX = 2        N   ',MF=L
         $FS   TEXT='N        4 =  .  .  .  .  .  .  .  .  = ',MF=L
         $FS   TEXT=' =========================          T   ',MF=L
         $FS   TEXT='T        3 =  .  .  .  .  .  .  .  .  = ',MF=L
         $FS   TEXT=' = XXX = XXX = XXX = XXX = 1        E   ',MF=L
         $FS   TEXT='E        2 =  .  .  .  .  .  .  .  .  = ',MF=L
         $FS   TEXT=' =========================          R   ',MF=L
         $FS   TEXT='R        1 =  .  .  .  .  .  .  .  .  = ',MF=L
         $FS   TEXT='    1     2     3     4   ----X     P   ',MF=L
         $FS   TEXT='P          ============================ ',MF=L
         $FS   TEXT=' Sector : Romula Andros IV          R   ',MF=L
         $FS   TEXT='R     X----   1  2  3  4  5  6  7  8    ',MF=L
         $FS   TEXT=' Long Range Sensors :               I   ',MF=L
         $FS   TEXT='I             Short Range Sensors :     ',MF=L
         $FS   TEXT='  Position 1 - Klingons             S   ',MF=L
         $FS   TEXT='S              Quadrant = (',MF=L
SC1LN13N $FS   TEXT='X,Y',MF=L
         $FS   TEXT=')         ',MF=L
         $FS   TEXT='  Position 2 - Star Bases           E   ',MF=L
         $FS   TEXT='E                                       ',MF=L
         $FS   TEXT='  Position 3 - Stars                    ',MF=L
         $FS   TEXT='                                        ',MF=L
         $FS   TEXT='************* Communications ***********',MF=L
         $FS   TEXT='******    ==== Operating Statistics ====',MF=L
         $FS   TEXT='*   ',MF=L
SC1LN17I $FS   SF=NORMAL,MF=L
SC1LN17R $FS   TEXT=' ',MF=L
         $FS   SF=(SKIP,INT),MF=L
         $FS   TEXT='<---Enter command (0 for director',MF=L
         $FS   TEXT='y)   *    Energy..................(',MF=L
SC1LN17N $FS   TEXT='0000',MF=L
         $FS   TEXT=')',MF=L
         $FS   TEXT='*   ',MF=L
SC1LN18I $FS   SF=NORMAL,MF=L
SC1LN18R $FS   TEXT=' ',MF=L
         $FS   SF=(SKIP,INT),MF=L
         $FS   TEXT='<---Enter X axis coordinate      ',MF=L
         $FS   TEXT='     *    Photon torpedoes..........(',MF=L
SC1LN18N $FS   TEXT='00',MF=L
         $FS   TEXT=')',MF=L
         $FS   TEXT='*   ',MF=L
SC1LN19I $FS   SF=NORMAL,MF=L
SC1LN19R $FS   TEXT=' ',MF=L
         $FS   SF=(SKIP,INT),MF=L
         $FS   TEXT='<---Enter Y axis coordinate      ',MF=L
         $FS   TEXT='     *    Years remaining...........(',MF=L
SC1LN19N $FS   TEXT='00',MF=L
         $FS   TEXT=')',MF=L
         $FS   TEXT='*',MF=L
SC1LN20I $FS   SF=NORMAL,MF=L
SC1LN20R $FS   TEXT='    ',MF=L
         $FS   SF=(SKIP,INT),MF=L
         $FS   TEXT='<---Enter energy quantity        ',MF=L
         $FS   TEXT='     *    Klingons..................(',MF=L
SC1LN20N $FS   TEXT='00',MF=L
         $FS   TEXT=')',MF=L
         $FS   TEXT='****************************************',MF=L
         $FS   TEXT='******    Star bases................(',MF=L
SC1LN21N $FS   TEXT='00',MF=L
         $FS   TEXT=')',MF=L
SC1LN22M $FS   TEXT=(' ',44),MF=L
         $FS   TEXT='      Deflection energy (%)....(',MF=L
SC1LN22N $FS   TEXT='000',MF=L
         $FS   TEXT=')',MF=L
SC1LN23M $FS   TEXT=(' ',44),MF=L
         $FS   TEXT='      Star date.............(',MF=L
SC1LN23N $FS   TEXT='0000.0',MF=L
         $FS   TEXT=')',MF=L
SC1LN24M $FS   TEXT=(' ',44),MF=L
         $FS   TEXT='      Combat status.........(',MF=L
SC1LN24N $FS   TEXT='YELLOW',MF=L
         $FS   TEXT=')',MF=L
LSSC1N   EQU   (2*L'QUADCVT3)+(2*L'SC1LSF)
LSCRN1S  EQU   (*-SCRN1)+LSSC1N+L'SC1LA+L'SC1IC
*------- EXTENDED COLOR FIELDS FOR MAIN BATTLE COMPUTER.
*        --> LONG RANGE SENSORS.
SC1COLOR $FS   SBA=(1,1),MFE=(HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(1,27),SF=(SKIP,INT),MF=L
         $FS   SBA=(2,1),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(2,3),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,TURQ),MF=L
         $FS   SBA=(2,7),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(2,9),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,TURQ),MF=L
         $FS   SBA=(2,13),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(2,15),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,TURQ),MF=L
         $FS   SBA=(2,19),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(2,21),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,TURQ),MF=L
         $FS   SBA=(2,25),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(2,27),SF=(SKIP,INT),MF=L
         $FS   SBA=(3,1),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(3,27),SF=(SKIP,INT),MF=L
         $FS   SBA=(4,1),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(4,3),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,TURQ),MF=L
         $FS   SBA=(4,7),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(4,9),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,TURQ),MF=L
         $FS   SBA=(4,13),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(4,15),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,TURQ),MF=L
         $FS   SBA=(4,19),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(4,21),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,TURQ),MF=L
         $FS   SBA=(4,25),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(4,27),SF=(SKIP,INT),MF=L
         $FS   SBA=(5,1),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(5,27),SF=(SKIP,INT),MF=L
         $FS   SBA=(6,1),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(6,3),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,TURQ),MF=L
         $FS   SBA=(6,7),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(6,9),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,TURQ),MF=L
         $FS   SBA=(6,13),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(6,15),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,TURQ),MF=L
         $FS   SBA=(6,19),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(6,21),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,TURQ),MF=L
         $FS   SBA=(6,25),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(6,27),SF=(SKIP,INT),MF=L
         $FS   SBA=(7,1),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(7,27),SF=(SKIP,INT),MF=L
         $FS   SBA=(8,1),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(8,3),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,TURQ),MF=L
         $FS   SBA=(8,7),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(8,9),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,TURQ),MF=L
         $FS   SBA=(8,13),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(8,15),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,TURQ),MF=L
         $FS   SBA=(8,19),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(8,21),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,TURQ),MF=L
         $FS   SBA=(8,25),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(8,27),SF=(SKIP,INT),MF=L
         $FS   SBA=(9,1),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(9,27),SF=(SKIP,INT),MF=L
         $FS   SBA=(13,1),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,GREEN),MF=L
         $FS   SBA=(13,30),SF=(SKIP,INT),MF=L
         $FS   SBA=(14,1),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,GREEN),MF=L
         $FS   SBA=(14,30),SF=(SKIP,INT),MF=L
         $FS   SBA=(15,1),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,GREEN),MF=L
         $FS   SBA=(15,30),SF=(SKIP,INT),MF=L
*        --> REVERSE VIDEO THE USS ENTERPRISE.
         $FS   SBA=(1,36),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,BLUE),MF=L
         $FS   SBA=(1,38),SF=(SKIP,INT),MF=L
         $FS   SBA=(1,40),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,BLUE),MF=L
         $FS   SBA=(1,42),SF=(SKIP,INT),MF=L
         $FS   SBA=(2,36),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,BLUE),MF=L
         $FS   SBA=(2,38),SF=(SKIP,INT),MF=L
         $FS   SBA=(2,40),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,BLUE),MF=L
         $FS   SBA=(2,42),SF=(SKIP,INT),MF=L
         $FS   SBA=(3,36),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,BLUE),MF=L
         $FS   SBA=(3,38),SF=(SKIP,INT),MF=L
         $FS   SBA=(3,40),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,BLUE),MF=L
         $FS   SBA=(3,42),SF=(SKIP,INT),MF=L
         $FS   SBA=(5,36),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,BLUE),MF=L
         $FS   SBA=(5,38),SF=(SKIP,INT),MF=L
         $FS   SBA=(5,40),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,BLUE),MF=L
         $FS   SBA=(5,42),SF=(SKIP,INT),MF=L
         $FS   SBA=(6,36),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,BLUE),MF=L
         $FS   SBA=(6,38),SF=(SKIP,INT),MF=L
         $FS   SBA=(6,40),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,BLUE),MF=L
         $FS   SBA=(6,42),SF=(SKIP,INT),MF=L
         $FS   SBA=(7,36),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,BLUE),MF=L
         $FS   SBA=(7,38),SF=(SKIP,INT),MF=L
         $FS   SBA=(7,40),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,BLUE),MF=L
         $FS   SBA=(7,42),SF=(SKIP,INT),MF=L
         $FS   SBA=(8,36),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,BLUE),MF=L
         $FS   SBA=(8,38),SF=(SKIP,INT),MF=L
         $FS   SBA=(8,40),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,BLUE),MF=L
         $FS   SBA=(8,42),SF=(SKIP,INT),MF=L
         $FS   SBA=(9,36),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,BLUE),MF=L
         $FS   SBA=(9,38),SF=(SKIP,INT),MF=L
         $FS   SBA=(9,40),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,BLUE),MF=L
         $FS   SBA=(9,42),SF=(SKIP,INT),MF=L
         $FS   SBA=(10,36),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,BLUE),MF=L
         $FS   SBA=(10,38),SF=(SKIP,INT),MF=L
         $FS   SBA=(10,40),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,BLUE),MF=L
         $FS   SBA=(10,42),SF=(SKIP,INT),MF=L
         $FS   SBA=(11,36),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,BLUE),MF=L
         $FS   SBA=(11,38),SF=(SKIP,INT),MF=L
         $FS   SBA=(11,40),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,BLUE),MF=L
         $FS   SBA=(11,42),SF=(SKIP,INT),MF=L
         $FS   SBA=(12,36),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,BLUE),MF=L
         $FS   SBA=(12,38),SF=(SKIP,INT),MF=L
         $FS   SBA=(12,40),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,BLUE),MF=L
         $FS   SBA=(12,42),SF=(SKIP,INT),MF=L
         $FS   SBA=(13,36),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,BLUE),MF=L
         $FS   SBA=(13,38),SF=(SKIP,INT),MF=L
         $FS   SBA=(13,40),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,BLUE),MF=L
         $FS   SBA=(13,42),SF=(SKIP,INT),MF=L
         $FS   SBA=(14,36),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,BLUE),MF=L
         $FS   SBA=(14,38),SF=(SKIP,INT),MF=L
         $FS   SBA=(14,40),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,BLUE),MF=L
         $FS   SBA=(14,42),SF=(SKIP,INT),MF=L
*        --> SHORT RANGE SENSORS.
         $FS   SBA=(1,51),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(1,80),SF=(SKIP,INT),MF=L
         $FS   SBA=(2,51),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(2,53),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,YELLOW),MF=L
         $FS   SBA=(2,78),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(2,80),SF=(SKIP,INT),MF=L
         $FS   SBA=(3,51),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(3,53),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,YELLOW),MF=L
         $FS   SBA=(3,78),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(3,80),SF=(SKIP,INT),MF=L
         $FS   SBA=(4,51),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(4,53),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,YELLOW),MF=L
         $FS   SBA=(4,78),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(4,80),SF=(SKIP,INT),MF=L
         $FS   SBA=(5,51),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(5,53),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,YELLOW),MF=L
         $FS   SBA=(5,78),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(5,80),SF=(SKIP,INT),MF=L
         $FS   SBA=(6,51),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(6,53),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,YELLOW),MF=L
         $FS   SBA=(6,78),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(6,80),SF=(SKIP,INT),MF=L
         $FS   SBA=(7,51),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(7,53),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,YELLOW),MF=L
         $FS   SBA=(7,78),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(7,80),SF=(SKIP,INT),MF=L
         $FS   SBA=(8,51),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(8,53),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,YELLOW),MF=L
         $FS   SBA=(8,78),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(8,80),SF=(SKIP,INT),MF=L
         $FS   SBA=(9,51),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(9,53),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,YELLOW),MF=L
         $FS   SBA=(9,78),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(9,80),SF=(SKIP,INT),MF=L
         $FS   SBA=(10,51),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,GREEN),MF=L
         $FS   SBA=(10,80),SF=(SKIP,INT),MF=L
*        --> UNDERLINE THE QUADRANT INDICATOR.
         $FS   SBA=(13,53),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,GREEN),MF=L
         $FS   SBA=(13,66),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,USCORE,COLOR,PINK),MF=L
         $FS   SBA=(13,72),SF=(SKIP,INT),MF=L
*        --> COMMAND ENTRY CHARACTERS.
         $FS   SBA=(15,80),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,TURQ),MF=L
         $FS   SBA=(16,47),SF=(SKIP,INT),MF=L
         $FS   SBA=(16,50),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,TURQ),MF=L
         $FS   SBA=(17,2),SF=(SKIP),MF=L
SC1CMD1  $FS   SBA=(17,5),                                             X
               MFE=(ATTR,NORMAL,HILITE,DEFAULT,COLOR,GREEN),MF=L
         $FS   SBA=(17,45),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,TURQ),MF=L
         $FS   SBA=(17,47),SF=(SKIP,INT),MF=L
         $FS   SBA=(17,80),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,TURQ),MF=L
         $FS   SBA=(18,2),SF=(SKIP),MF=L
SC1CMD2  $FS   SBA=(18,5),                                             X
               MFE=(ATTR,NORMAL,HILITE,DEFAULT,COLOR,GREEN),MF=L
         $FS   SBA=(18,45),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,TURQ),MF=L
         $FS   SBA=(18,47),SF=(SKIP,INT),MF=L
         $FS   SBA=(18,80),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,TURQ),MF=L
         $FS   SBA=(19,2),SF=(SKIP),MF=L
SC1CMD3  $FS   SBA=(19,5),                                             X
               MFE=(ATTR,NORMAL,HILITE,DEFAULT,COLOR,GREEN),MF=L
         $FS   SBA=(19,45),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,TURQ),MF=L
         $FS   SBA=(19,47),SF=(SKIP,INT),MF=L
         $FS   SBA=(19,80),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,TURQ),MF=L
SC1CMD4  $FS   SBA=(20,2),                                             X
               MFE=(ATTR,NORMAL,HILITE,DEFAULT,COLOR,GREEN),MF=L
         $FS   SBA=(20,45),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,TURQ),MF=L
         $FS   SBA=(20,47),SF=(SKIP,INT),MF=L
         $FS   SBA=(20,80),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,REVERSE,COLOR,TURQ),MF=L
         $FS   SBA=(21,47),SF=(SKIP,INT),MF=L
*        --> STATISTICS.
         $FS   SBA=(17,75),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,GREEN),MF=L
         $FS   SBA=(18,77),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,GREEN),MF=L
         $FS   SBA=(19,77),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,GREEN),MF=L
         $FS   SBA=(20,77),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,GREEN),MF=L
         $FS   SBA=(21,77),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,GREEN),MF=L
         $FS   SBA=(21,80),SF=(SKIP,INT),MF=L
         $FS   SBA=(22,76),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,GREEN),MF=L
         $FS   SBA=(22,80),SF=(SKIP,INT),MF=L
         $FS   SBA=(23,73),MF=L
         $FS   SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,GREEN),MF=L
         $FS   SBA=(23,80),SF=(SKIP,INT),MF=L
         $FS   SBA=(24,73),MF=L
SC1ATT   $FS   SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,GREEN),MF=L
         $FS   SBA=(24,80),SF=(SKIP,INT),MF=L
*        --> PICK OUT CURRENT QUADRANT OF LONG RANGE SENSORS
SC1LNCXY $FS   SBA=(2,3),MFE=(COLOR,PINK),MF=L
*        --> INSERT CURSOR AT COMMAND ENTRY FIELD.
SC1LA    $FS   SBA=(17,6),MF=L
SC1IC    $FS   SF=(IC),MF=L
LSCRN1L  EQU   *-SCRN1
         SPACE 1
SCRN2    $FS   CC=EW,WCC=(KBR,RMDT),SBA=(1,1),RA=(1,1,00),MF=L
         $FS   SBA=(1,1),MF=L
SC2LN01I $FS   SF=NORMAL,MF=L
SC2LN01R $FS   TEXT=(' ',1),MF=L
         $FS   SF=(SKIP,INT),MF=L
         $FS   SBA=(1,22),MF=L
         $FS   TEXT='**** STAR-TREK command directory ****',MF=L
         $FS   SBA=(3,1),MF=L
         $FS   SF=(SKIP,INT),TEXT='9 <--- END',MF=L
         $FS   SF=(SKIP),TEXT='the game (this implies that ',MF=L
         $FS   TEXT='you are surrendering) / PF9-PA1-PA2-PA3',MF=L
         $FS   SBA=(5,1),MF=L
         $FS   SF=(SKIP,INT),TEXT='1 <--- WARP DRIVE,',MF=L
         $FS   SF=(SKIP),TEXT='interquadrant travel',MF=L
         $FS   TEXT=' (between quadrants)',MF=L
         $FS   SBA=(6,9),TEXT='Operand 1 - enter the X axis of ',MF=L
         $FS   TEXT='the destination quadrant',MF=L
         $FS   SBA=(7,9),TEXT='Operand 2 - enter the Y axis of ',MF=L
         $FS   TEXT='the destination quadrant',MF=L
         $FS   SBA=(9,1),MF=L
         $FS   SF=(SKIP,INT),TEXT='2 <--- IMPULSE POWER,',MF=L
         $FS   SF=(SKIP),TEXT='quadrant travel',MF=L
         $FS   TEXT=' (within the current quadrant)',MF=L
         $FS   SBA=(10,9),TEXT='Operand 1 - enter the X axis of ',MF=L
         $FS   TEXT='the destination position',MF=L
         $FS   SBA=(11,9),TEXT='Operand 2 - enter the Y axis of ',MF=L
         $FS   TEXT='the destination position',MF=L
         $FS   SBA=(13,1),MF=L
         $FS   SF=(SKIP,INT),TEXT='3 <--- FIRE PHOTON TORPEDO',MF=L
         $FS   SF=(SKIP),MF=L
         $FS   SBA=(14,9),TEXT='Operand 1 - enter the X axis ',MF=L
         $FS   TEXT='coordinate of the target',MF=L
         $FS   SBA=(15,9),TEXT='Operand 2 - enter the Y axis ',MF=L
         $FS   TEXT='coordinate of the target',MF=L
         $FS   SBA=(17,1),MF=L
         $FS   SF=(SKIP,INT),TEXT='4 <--- FIRE PHASERS',MF=L
         $FS   SF=(SKIP),MF=L
         $FS   SBA=(18,9),TEXT='Operand 1 - enter the X axis ',MF=L
         $FS   TEXT='coordinate of the target',MF=L
         $FS   SBA=(19,9),TEXT='Operand 2 - enter the Y axis ',MF=L
         $FS   TEXT='coordinate of the target                   ',MF=L
         $FS   SBA=(20,9),TEXT='Operand 3 - enter the amount of ',MF=L
         $FS   TEXT='energy for phaser burst',MF=L
         $FS   SBA=(23,1),SF=(SKIP,INT),TEXT='PF1 :',MF=L
         $FS   SF=(SKIP),TEXT='display directory',MF=L
         $FS   SBA=(1,2),SF=(IC),MF=L
LSCRN2   EQU   *-SCRN2
         EJECT
*------> INPUT "SCRN1" PROCESS ROUTINE.
*        AT EXIT : R1=INPUT TRUE LENGTH.
         SPACE 1
RSCRT1  $XENT  BASE=(R9)
         XR    R2,R2               INITIAL COUNT
R1LOOP  @FSR   ,                   GET REPLY TEXT
         LTR   R15,R15             NO MORE FIELDS MODIFIED?
         BNZ   R1PRCD              NO
         L     R1,4(R13)           YES, END, GET PREVIOUS SAVE AREA
         ST    R2,24(R1)           PASS BACK COUNT IN R1
         XR    R15,R15             EXIT
        $XRET  CC=(R15)
R1PRCD   CLC   SC1CSF1,0(R15)
         BE    R1FL1
         CLC   SC1CSF2,0(R15)
         BE    R1FL2
         CLC   SC1CSF3,0(R15)
         BE    R1FL3
         CLC   SC1CSF4,0(R15)
         BE    R1FL4
         B     R1LOOP              UNEXPECTED (CHECK SCREEN IMAGE)
R1FL1    LTR   R1,R1
         BZ    R1FC1               FIELD CLEARED (CANC FC)
         CH    R1,=H'1'
         BNE   R1LOOP              UNEXPECTED (CHECK SCREEN IMAGE)
         MVI   INPUT+0,C' '        FOR UPPERCASE
         OC    INPUT+0(1),3(R15)   COPY COMMAND
         B     R1BF1
R1FC1    MVI   INPUT+0,C' '        COMMAND CLEARED
R1BF1    LA    R2,1(R2)            BUMP COUNT
         B     R1LOOP
R1FL2    LTR   R1,R1
         BZ    R1FC2               FIELD CLEARED (CANC FC)
         CH    R1,=H'1'
         BNE   R1LOOP              UNEXPECTED (CHECK SCREEN IMAGE)
         MVI   INPUT+1,C' '        FOR UPPERCASE
         OC    INPUT+1(1),3(R15)   COPY X AXIS COORDINATE
         B     R1BF2
R1FC2    MVI   INPUT+1,C' '        X AXIS COORDINATE CLEARED
R1BF2    LA    R2,1(R2)            BUMP COUNT
         B     R1LOOP
R1FL3    LTR   R1,R1
         BZ    R1FC3               FIELD CLEARED (CANC FC)
         CH    R1,=H'1'
         BNE   R1LOOP              UNEXPECTED (CHECK SCREEN IMAGE)
         MVI   INPUT+2,C' '        FOR UPPERCASE
         OC    INPUT+2(1),3(R15)   COPY Y AXIS COORDINATE
         B     R1BF3
R1FC3    MVI   INPUT+2,C' '        Y AXIS COORDINATE CLEARED
R1BF3    LA    R2,1(R2)            BUMP COUNT
         B     R1LOOP
R1FL4    LTR   R1,R1
         BZ    R1FC4               FIELD CLEARED (CANC FC)
         CH    R1,=H'4'
         BH    R1LOOP              UNEXPECTED (CHECK SCREEN IMAGE)
         MVC   INPUT+3(4),=CL4' '  FOR UPPERCASE
         SH    R1,=H'1'
         EX    R1,R1OF4            COPY ENERGY QUANTITY
         B     R1BF4
R1FC4    MVC   INPUT+3(4),=CL4' '  ENERGY QUANTITY CLEARED
R1BF4    LA    R2,4(R2)            BUMP COUNT
         B     R1LOOP
R1OF4    OC    INPUT+3(*-*),3(R15) <<EXECUTED>>
         SPACE 1
SC1CSF1  $FS   SBA=(17,6),MF=L
SC1CSF2  $FS   SBA=(18,6),MF=L
SC1CSF3  $FS   SBA=(19,6),MF=L
SC1CSF4  $FS   SBA=(20,3),MF=L
         SPACE 1
        LTORG
         SPACE 1
         DROP  R9
         EJECT
*------> THIS ROUTINE ROUTES CONTROL TO THE COMMAND ROUTINES.
*        AT EXIT : R15 = 0, 4 OR 8.
CLLCMND $XENT  BASE=(R9)
         CLI   INPUT+0,C'9'        END OF STAR-TREK
         BNE   *+L'*+8
         LA    R15,8               SET RC=8
         B     CLLCMNX
         CLI   INPUT+0,C'1'        COMMAND 1
         BE    CCMND1
         CLI   INPUT+0,C'2'        COMMAND 2
         BE    CCMND2
         CLI   INPUT+0,C'3'        COMMAND 3
         BE    CCMND3
         CLI   INPUT+0,C'4'        COMMAND 4
         BE    CCMND4
         L     R2,=A(SC1LN17I)
        $FS    SF=(INT),MF=(I,(R2))
         L     R2,=A(SC1CMD1)
        $FS    SBA=(17,5),                                             X
               MFE=(ATTR,(INT),HILITE,BLINK,COLOR,RED),                X
               MF=(I,(R2))
         MVC   SCMSG1,=CL44'Invalid command'
         LA    R15,12              SET RC=12
         B     CLLCMNX
CCMND1  #GO    CMD1                * WARP DRIVE INTER-QUADRANT
         B     CLLCMNX
CCMND2  #GO    CMD2                * WARP DRIVE INTRA-QUADRANT
         B     CLLCMNX
CCMND3  #GO    CMD3                * PHOTON TORPEDOES
         B     CLLCMNX
CCMND4  #GO    CMD4                * PHASER CONTROL
CLLCMNX $XRET  CC=(R15)
         SPACE 1
        LTORG
         SPACE 1
         DROP  R9
         EJECT
*------> THIS ROUTINE ROUTES HANDLES TRAVEL BETWEEN QUADRANTS.
*        AT EXIT : R15 = 0, 8 OR 12.
CMD1    $XENT  BASE=(R9)
         CLI   INPUT+1,C'1'        X MUST BE 1 - 4
         BL    CMD1BADX
         CLI   INPUT+1,C'4'
         BH    CMD1BADX
         CLI   INPUT+2,C'1'        Y MUST BE 1 - 4
         BL    CMD1BADY
         CLI   INPUT+2,C'4'
         BH    CMD1BADY
         L     R1,=A(SC1LN13N)     AXIS OF KIRK
         PACK  WORKDBLW,0(1,R1)    GET X AXIS OF KIRK
         CVB   R2,WORKDBLW
         PACK  WORKDBLW,INPUT+1(1) GET X AXIS OF DESTINATION
         CVB   R3,WORKDBLW
         SR    R2,R3               GET DIFFERENCE
         BNM   *+L'*+4
         MH    R2,=H'-1'           GET ABSOLUTE VALUE
         STH   R2,WRKHALF1         STORE
         LTR   R2,R2
         BZ    *+L'*+8
         MH    R2,WRKHALF1         GET SQUARE
         STH   R2,WRKHALF1         SAVE IT
         PACK  WORKDBLW,0+L'SC1LN13N-1(1,R1) GET Y AXIS OF KIRK
         CVB   R2,WORKDBLW
         PACK  WORKDBLW,INPUT+2(1) GET Y AXIS OF DESTINATION
         CVB   R3,WORKDBLW
         SR    R2,R3               GET DIFFERENCE
         BNM   *+L'*+4
         MH    R2,=H'-1'           GET ABSOLUTE VALUE
         STH   R2,WRKHALF2         STORE
         LTR   R2,R2
         BZ    *+L'*+4
         MH    R2,WRKHALF2         GET SQUARE
         AH    R2,WRKHALF1         ADD SQUARES
         LR    R1,R2               MOVE FOR SQRT RTN
        #GO    SQRTRTN             GET SQUARE ROOT
         CVD   R1,WORKDBLW         CONVERT RADIX
         SP    GAMEYRS,WORKDBLW    SUBTRACT FROM YEARS LEFT
         BNP   CMD1KRKD            < 1 FEDERATION IS CONQUERED
         PACK  WRKFULL1,GAMEDATE(4) PACK DATE
         AP    WRKFULL1,WORKDBLW   SUBTRACT YEARS
         UNPK  GAMEDATE(4),WRKFULL1 REINSTATE DATE
         OI    GAMEDATE+3,X'F0'    CLEAR SIGN BIT
         LTR   R1,R1
         BZ    *+L'*+4
         MH    R1,=H'50'           ENERGY = 50 UNITS PER YEAR
         CVD   R1,WORKDBLW         CONVERT RADIX
         SP    GAMENRGY,WORKDBLW   SUBTRACT FROM ENERGY TOTAL
         BP    *+L'*+8
CMD1KRKD MVI   SWITCH,C'D'         < 0 FEDERATION IS CONQUERED
         B     CMD1RC
         L     R2,KIRKPTR          GET RELATIVE QUADRANT
         SLL   R2,6                MULTIPLY BY 64
         A     R2,STARCHRT         ADD BASE ADDRESS
         LA    R3,64               INIT BCT REGISTER
CMD1KRK1 CLI   0(R2),X'04'         IS THIS KIRK
         BE    CMD1KRK2            YES, GO CLEAR HIM FROM QUAD
         LA    R2,1(R2)            NO, BUMP BY 1
         BCT   R3,CMD1KRK1         BCT
         MVI   MSG3F1,C'0'         IMPOSSIBLE - BOMB
         B     CMD1Z
CMD1KRK2 MVI   0(R2),X'00'         CLEAR KIRK
         MVC   QUADFIND+0(1),INPUT+1 BUILD NEW QUADRANT ADDRESS
         MVI   QUADFIND+1,C','
         MVC   QUADFIND+2(1),INPUT+2
         LA    R2,QUADCVT1         GET ADDRESS OF CONVERT TABLE
         XR    R3,R3               CLEAR FIND REGISTER
CMD1KRK3 CH    R3,=Y(QUADLVT1)     END OF TABLE?
         BE    CMD1KRK4            YES, LEAVE
         CLC   QUADFIND,0(R2)      THIS ENTRY IN TABLE?
         BE    CMD1KRK5            YES, THIS IS IT
         LA    R2,L'QUADCVT1(R2)   NO, BUMP POINTER
         LA    R3,1(R3)
         B     CMD1KRK3
CMD1KRK4 MVI   MSG3F1,C'1'         IMPOSSIBLE - BOMB
         B     CMD1Z
CMD1KRK5 ST    R3,KIRKPTR          STORE NEW RELATIVE QUADRANT
         SLL   R3,6                MULTIPLY BY 64
         A     R3,STARCHRT         ADD BASE ADDRESS
         LA    R4,64(R3)           SET UP END+1 OF QUADRANT
        TIME   MIC,TIMEAREA        GET BASE TIME
         MVC   TIMEAREA(2),TIMEAREA+5 ALIGN
         NI    TIMEAREA,X'03'      CLEAR EXTRA BITS
         LH    R1,TIMEAREA         LOAD INTO WORK REGISTER
         SRL   R1,4                RANDOM FACTOR
         AR    R1,R3               ADD QUADRANT ADDRESS
         LR    R2,R1               SAVE STARTING SEARCH ADDRESS
CMD1KRK6 CLI   0(R1),X'00'         IS THIS SLOT EMPTY
         BE    CMD1KRK8            YES, PUT KIRK IN IT
         LA    R1,1(R1)            BUMP PTR
         CLR   R1,R4
         BL    CMD1KRK6            ITERATE
         LR    R1,R3
CMD1KRK7 CLI   0(R1),X'00'         IS THIS SLOT EMPTY
         BE    CMD1KRK8            YES, PUT KIRK IN IT
         LA    R1,1(R1)            BUMP PTR
         CLR   R1,R2
         BL    CMD1KRK7            ITERATE
         MVI   MSG3F1,C'2'         IMPOSSIBLE - BOMB
CMD1Z    LA    R0,L'MSG3
         STH   R0,ERRMSL
         LA    R0,MSG3
         ST    R0,ERRMSA
         LA    R15,8               ERROR, SET RC=8
         B     CMD1X
CMD1KRK8 MVI   0(R1),X'04'         PUT KIRK IN SLOT
         L     R1,=A(SC1LN13N)
         CLC   QUADFIND,0(R1)
         BNE   *+L'*+6
         MVC   SCMSG1,=CL44'You were already in this quadrant'
         MVC   SCRTOI,=CL7'_______' RESET COMMAND DOTS
CMD1RC   XR    R15,R15             SET RC=0
CMD1X   $XRET  CC=(R15)
CMD1BADX L     R2,=A(SC1LN18I)
        $FS    SF=(INT),MF=(I,(R2))
         L     R2,=A(SC1CMD2)
        $FS    SBA=(18,5),                                             X
               MFE=(ATTR,(INT),HILITE,BLINK,COLOR,RED),                X
               MF=(I,(R2))
         B     CMD1BADC
CMD1BADY L     R2,=A(SC1LN19I)
        $FS    SF=(INT),MF=(I,(R2))
         L     R2,=A(SC1CMD3)
        $FS    SBA=(19,5),                                             X
               MFE=(ATTR,(INT),HILITE,BLINK,COLOR,RED),                X
               MF=(I,(R2))
CMD1BADC MVC   SCMSG1,=CL44'The coordinates you supplied are not'
         MVC   SCMSG2,=CL44'in the ROMULA ANDROS IV sector'
         LA    R15,12              SET RC=12
         B     CMD1X
         SPACE 1
        LTORG
         SPACE 1
         DROP  R9
         EJECT
*------> THIS ROUTINE HANDLES TRAVEL INSIDE QUADRANTS.
*        AT EXIT : R15 = 0, 8 OR 12.
CMD2    $XENT  BASE=(R9)
         CLI   INPUT+1,C'1'        X MUST BE 1 - 8
         BL    CMD2BADX
         CLI   INPUT+1,C'8'
         BH    CMD2BADX
         CLI   INPUT+2,C'1'        Y MUST BE 1 - 8
         BL    CMD2BADY
         CLI   INPUT+2,C'8'
         BH    CMD2BADY
         MVC   QUADFIND+0(1),INPUT+1 FORMAT POSITION ADDRESS
         MVC   QUADFIND+2(1),INPUT+2
         MVI   QUADFIND+1,C','
         LA    R2,QUADCVT2
         XR    R3,R3               CLEAR FIND REGISTER
CMD2FULL CH    R3,=Y(QUADLVT2)     END OF TABLE?
         BE    CMD2ERR             YES, LEAVE
         CLC   QUADFIND,0(R2)      THIS ENTRY IN TABLE?
         BE    CMD2FND             YES, THIS IS IT
         LA    R2,L'QUADCVT2(R2)   NO, BUMP POINTER
         LA    R3,1(R3)
         B     CMD2FULL
CMD2ERR  MVI   MSG3F1,C'3'         IMPOSSIBLE - BOMB
         B     CMD2Z
CMD2FND  L     R2,KIRKPTR          GET RELATIVE QUADRANT
         SLL   R2,6                MULTIPLY BY 64
         A     R2,STARCHRT
         LR    R5,R2
         AR    R2,R3
         CLI   0(R2),X'00'
         BE    CMD2NULL
         L     R2,=A(SC1LN18I)
        $FS    SF=(INT),MF=(I,(R2))
         L     R2,=A(SC1CMD2)
        $FS    SBA=(18,5),                                             X
               MFE=(ATTR,(INT),HILITE,BLINK,COLOR,RED),                X
               MF=(I,(R2))
         L     R2,=A(SC1LN19I)
        $FS    SF=(INT),MF=(I,(R2))
         L     R2,=A(SC1CMD3)
        $FS    SBA=(19,5),                                             X
               MFE=(ATTR,(INT),HILITE,BLINK,COLOR,RED),                X
               MF=(I,(R2))
         MVC   SCMSG1,=CL44'The point that you want to move to'
         MVC   SCMSG2,=CL44'is already occupied'
         LA    R15,12              SET RC=12
         B     CMD2X
CMD2NULL LA    R4,64
CMD2FIND CLI   0(R5),X'04'         IS THIS KIRK?
         BE    CMD2FND2            YES, END OF SEARCH
         LA    R5,1(R5)
         BCT   R4,CMD2FIND
         MVI   MSG3F1,C'4'         WHAT'S THAT?
CMD2Z    LA    R0,L'MSG3
         STH   R0,ERRMSL
         LA    R0,MSG3
         ST    R0,ERRMSA
         LA    R15,8               ERROR, SET RC=8
         B     CMD2X
*        --> COMPUTE ENERGY COST OF MOVE.
CMD2FND2 LR    R6,R5               SAVE PTR
         S     R6,STARCHRT         SUBTRACT START ADDRESS
         L     R7,KIRKPTR          GET RELATIVE QUADRANT
         SLL   R7,6                MULTIPLY BY 64
         SR    R6,R7               SUBTRACT FROM RESIDUAL
         BZ    *+L'*+4
         MH    R6,=Y(L'QUADCVT2)   * NODE LENGTH
         LA    R7,QUADCVT2         TABLE BEGIN AREA
         AR    R6,R7               ADD OFFSET
         PACK  WORKDBLW,QUADFIND+0(1)
         CVB   R8,WORKDBLW
         PACK  WORKDBLW,0(1,R6)
         CVB   R7,WORKDBLW
         SR    R8,R7
         BNM   *+L'*+4
         MH    R8,=H'-1'
         STH   R8,WRKHALF1         STORE
         LTR   R8,R8
         BZ    *+L'*+4
         MH    R8,WRKHALF1         GET SQUARE
         STH   R8,WRKHALF1         SAVE IT
         PACK  WORKDBLW,QUADFIND+2(1)
         CVB   R8,WORKDBLW
         PACK  WORKDBLW,2(1,R6)
         CVB   R7,WORKDBLW
         SR    R8,R7               GET DIFFERENCE
         BNM   *+L'*+4
         MH    R8,=H'-1'           GET ABSOLUTE VALUE
         STH   R8,WRKHALF2         STORE
         LTR   R8,R8
         BZ    *+L'*+4
         MH    R8,WRKHALF2         GET SQUARE
         AH    R8,WRKHALF1         ADD SQUARES
         LR    R1,R8               MOVE FOR SQRT RTN
        #GO    SQRTRTN             GET SQUARE ROOT
         LTR   R1,R1
         BZ    *+L'*+4
         MH    R1,=H'10'
         CVD   R1,WORKDBLW
         SP    GAMENRGY,WORKDBLW
         BP    *+L'*+8
         MVI   SWITCH,C'D'
         B     CMD2RC
         MVI   0(R5),X'00'         CLEAR OLD POSITION
         MVI   0(R2),X'04'         MARK NEW POSITION
         MVC   SCRTOI,=CL7'_______' RESET COMMAND DOTS
CMD2RC   XR    R15,R15             SET RC=0
CMD2X   $XRET  CC=(R15)
CMD2BADX L     R2,=A(SC1LN18I)
        $FS    SF=(INT),MF=(I,(R2))
         L     R2,=A(SC1CMD2)
        $FS    SBA=(18,5),                                             X
               MFE=(ATTR,(INT),HILITE,BLINK,COLOR,RED),                X
               MF=(I,(R2))
         B     CMD2BADC
CMD2BADY L     R2,=A(SC1LN19I)
        $FS    SF=(INT),MF=(I,(R2))
         L     R2,=A(SC1CMD3)
        $FS    SBA=(19,5),                                             X
               MFE=(ATTR,(INT),HILITE,BLINK,COLOR,RED),                X
               MF=(I,(R2))
CMD2BADC MVC   SCMSG1,=CL44'The coordinates you supplied are not'
         MVC   SCMSG2,=CL44'in the current quadrant'
         LA    R15,12              SET RC=12
         B     CMD2X
         SPACE 1
        LTORG
         SPACE 1
         DROP  R9
         EJECT
*------> THIS ROUTINE HANDLES PHOTON TORPEDOES.
*        AT EXIT : R15 = 0, 4, 8 OR 12.
CMD3    $XENT  BASE=(R9)
         CLI   INPUT+1,C'1'        X MUST BE 1 - 8
         BL    CMD3BADX
         CLI   INPUT+1,C'8'
         BH    CMD3BADX
         CLI   INPUT+2,C'1'        Y MUST BE 1 - 8
         BL    CMD3BADY
         CLI   INPUT+2,C'8'
         BH    CMD3BADY
         MVC   QUADFIND+0(1),INPUT+1 FORMAT POSITION ADDRESS
         MVC   QUADFIND+2(1),INPUT+2
         MVI   QUADFIND+1,C','
         LA    R2,QUADCVT2
         XR    R3,R3               CLEAR FIND REGISTER
CMD3FULL CH    R3,=Y(QUADLVT2)     END OF TABLE?
         BE    CMD3ERR             YES, LEAVE
         CLC   QUADFIND,0(R2)      THIS ENTRY IN TABLE?
         BE    CMD3FND             YES, THIS IS IT
         LA    R2,L'QUADCVT2(R2)   NO, BUMP POINTER
         LA    R3,1(R3)
         B     CMD3FULL
CMD3ERR  MVI   MSG3F1,C'5'         IMPOSSIBLE - BOMB
CMD3Z    LA    R0,L'MSG3
         STH   R0,ERRMSL
         LA    R0,MSG3
         ST    R0,ERRMSA
         LA    R15,8               ERROR, SET RC=8
         B     CMD3X
CMD3FND  L     R2,KIRKPTR          GET RELATIVE QUADRANT
         SLL   R2,6                MULTIPLY BY 64
         A     R2,STARCHRT
         LR    R5,R2
         AR    R2,R3
         SP    GAMENRGY,=PL2'10'   COST IS 10 UNITS - ENERGY
         BP    *+L'*+8
         MVI   SWITCH,C'D'
         B     CMD3RC
         CP    GAMETORP,=PL1'0'    1 TORPEDO
         BNE   *+L'*+10
         MVC   SCMSG1,=CL44'You falls through ... no more torpedoes'
         B     CMD3CLR
         SP    GAMETORP,=PL1'1'    -1 TORPEDO
         CLI   0(R2),X'00'         IS SLOT EMPTY
         BNE   CMD3TRYE
         MVC   SCMSG1,=CL44'You missed by supplying wrong coordinates'
         B     CMD3CLR
CMD3TRYE CLI   0(R2),X'04'         IS IT THE ENTERPRISE
         BNE   CMD3TRYS
         MVC   SCMSG1,=CL44'You cannot shoot yourself with a torpedo'
         B     CMD3CLR
CMD3TRYS CLI   0(R2),X'03'         IS IT A STAR
         BNE   CMD3TRYB
         MVI   0(R2),X'00'         CLEAR STAR
         MVC   SCMSG1,=CL44'You just walloped a star'
         SP    GAMESTRS,=PL1'1'    DECREMENT STAR COUNT
         B     CMD3CLR
CMD3TRYB CLI   0(R2),X'02'         IS IT A BASE
         BNE   CMD3TRYK
         MVI   0(R2),X'00'         CLEAR BASE
         MVC   SCMSG1,=CL44'Congratulations, idiot, you just'
         MVC   SCMSG2,=CL44'destroyed one of your own bases'
         SP    GAMEBASE,=PL1'1'
         BNM   *+L'*+6
         ZAP   GAMEBASE,=PL1'0'
        #GO    BLOWSCR
         MVC   SCRTOI,=CL7'_______' RESET COMMAND DOTS
         B     CMD3X
CMD3TRYK CLI   0(R2),X'01'         IS IT A KLINGON?
         BE    *+L'*+8             YES
         MVI   MSG3F1,C'6'         IMPOSSIBLE - BOMB
         B     CMD3Z
        #GO    RANDOMR             GET RANDOM NUMBER (0-9)
         CH    R1,=H'7'
         BE    CMD3MISS
         CH    R1,=H'8'
         BE    CMD3MISS
         CH    R1,=H'9'
         BE    CMD3MISS
         MVI   0(R2),X'00'         CLEAR IT
         SP    GAMEKLNG,=PL1'1'    DECREMENT KLINGON COUNT
         BNZ   *+L'*+8
         MVI   SWITCH,C'W'
         B     CMD3RC
         MVC   SCMSG1,=CL44'You destroyed the KLINGON'
         B     CMD3CLR
CMD3MISS MVC   SCMSG1,=CL44'You missed by being unlucky'
CMD3CLR  MVC   SCRTOI,=CL7'_______' RESET COMMAND DOTS
CMD3RC   XR    R15,R15             SET RC=0
CMD3X   $XRET  CC=(R15)
CMD3BADX L     R2,=A(SC1LN18I)
        $FS    SF=(INT),MF=(I,(R2))
         L     R2,=A(SC1CMD2)
        $FS    SBA=(18,5),                                             X
               MFE=(ATTR,(INT),HILITE,BLINK,COLOR,RED),                X
               MF=(I,(R2))
         B     CMD3BADC
CMD3BADY L     R2,=A(SC1LN19I)
        $FS    SF=(INT),MF=(I,(R2))
         L     R2,=A(SC1CMD3)
        $FS    SBA=(19,5),                                             X
               MFE=(ATTR,(INT),HILITE,BLINK,COLOR,RED),                X
               MF=(I,(R2))
CMD3BADC MVC   SCMSG1,=CL44'The coordinates you supplied are not'
         MVC   SCMSG2,=CL44'in the current quadrant'
         LA    R15,12              SET RC=12
         B     CMD3X
         SPACE 1
        LTORG
         SPACE 1
         DROP  R9
         EJECT
*------> THIS ROUTINE HANDLES PHASERS.
*        AT EXIT : R15 = 0, 4, 8 OR 12.
CMD4    $XENT  BASE=(R9)
         CLI   INPUT+1,C'1'        X MUST BE 1 - 8
         BL    CMD4BADX
         CLI   INPUT+1,C'8'
         BH    CMD4BADX
         CLI   INPUT+2,C'1'        Y MUST BE 1 - 8
         BL    CMD4BADY
         CLI   INPUT+2,C'8'
         BH    CMD4BADY
         LA    R2,INPUT+3
         LA    R3,4
CMD4SCN1 CLI   0(R2),C'_'
         BNE   *+L'*+4
         MVI   0(R2),C' '
         LA    R2,1(R2)
         BCT   R3,CMD4SCN1
         LA    R2,INPUT+3
         LA    R3,4
CMD4SCN2 CLI   0(R2),C' '
         BE    CMD4SCN3
         CLI   0(R2),C'0'
         BL    CMD4SCNE
         CLI   0(R2),C'9'
         BH    CMD4SCNE
CMD4SCN3 LA    R2,1(R2)
         BCT   R3,CMD4SCN2
CMD4SCN4 CLI   INPUT+6,C' '
         BNE    CMD4SCN5
         MVC   INPUT+6(1),INPUT+5
         MVC   INPUT+5(1),INPUT+4
         MVC   INPUT+4(1),INPUT+3
         MVI   INPUT+3,C' '
         B      CMD4SCN4
CMD4SCN5 LA    R2,INPUT+3
         LA    R3,4
CMD4SCN6 CLI   0(R2),C' '
         BNE   CMD4SCN7
         MVI   0(R2),C'0'
         LA    R2,1(R2)
         BCT   R3,CMD4SCN6
         B     CMD4SCNS
CMD4SCN7 LA    R2,1(R2)
         BCT   R3,*+L'*+4
         B     CMD4SCNS
         CLI   0(R2),C' '
         BNE   CMD4SCN7
CMD4SCNE L     R2,=A(SC1LN20I)
        $FS    SF=(INT),MF=(I,(R2))
         L     R2,=A(SC1CMD4)
        $FS    SBA=(20,2),                                             X
               MFE=(ATTR,(INT),HILITE,BLINK,COLOR,RED),                X
               MF=(I,(R2))
         MVC   SCMSG1,=CL44'Invalid energy quantity specified'
         LA    R15,12              SET RC=12
         B     CMD4X
CMD4SCNS MVC   QUADFIND+0(1),INPUT+1  FORMAT POSITION ADDRESS
         MVC   QUADFIND+2(1),INPUT+2
         MVI   QUADFIND+1,C','
         LA    R2,QUADCVT2
         XR    R3,R3               CLEAR FIND REGISTER
CMD4FULL CH    R3,=Y(QUADLVT2)     END OF TABLE?
         BE    CMD4ERR             YES, LEAVE
         CLC   QUADFIND,0(R2)      THIS ENTRY IN TABLE?
         BE    CMD4FND             YES, THIS IS IT
         LA    R2,L'QUADCVT2(R2)   NO, BUMP POINTER
         LA    R3,1(R3)
         B     CMD4FULL
CMD4ERR  MVI   MSG3F1,C'7'         IMPOSSIBLE - BOMB
CMD4Z    LA    R0,L'MSG3
         STH   R0,ERRMSL
         LA    R0,MSG3
         ST    R0,ERRMSA
         LA    R15,8               ERROR, SET RC=8
         B     CMD4X
CMD4FND  L     R2,KIRKPTR          GET RELATIVE QUADRANT
         SLL   R2,6                MULTIPLY BY 64
         A     R2,STARCHRT
         LR    R5,R2
         AR    R2,R3
         CLI   0(R2),X'00'         IS SLOT EMPTY
         BNE   CMD4TRYS
         MVC   SCMSG1,=CL44'You missed by supplying wrong coordinates'
         B     CMD4CLR
CMD4TRYS CLI   0(R2),X'03'         IS IT A STAR
         BNE   CMD4TRYB
         MVC   SCMSG1,=CL44'You cannot destroy a star with phasers'
         B     CMD4CLR
CMD4TRYB CLI   0(R2),X'02'         IS IT A BASE
         BNE   CMD4TRYK
         MVC   SCMSG1,=CL44'You cannot destroy a base with phasers'
         B     CMD4CLR
CMD4TRYK CLI   0(R2),X'01'         IS IT A KLINGON
         BNE   CMD4MISS
         CLC   INPUT+3(4),=CL4'0499' MINIMUM ENERGY
         BL    CMD4LOW
         CLC   INPUT+3(4),=CL4'1000' MAXIMUM ENERGY
         BH    CMD4BLW
        #GO    RANDOMR             GET RANDOM NUMBER (0-9)
         CH    R1,=H'6'            60% CHANCE
         BNH   CMD4BLW
CMD4LOW  MVC   SCMSG1,=CL44'Insufficient energy to phasers'
         B     CMD4CLR
CMD4BLW  MVI   0(R2),X'00'         CLEAR KLINGON FROM MAP
        #GO    BLOWSCR
         LTR   R15,R15
         BNZ   CMD4X               ERROR COMPLETION
         MVC   SCMSG1,=CL44'You destroyed the KLINGON'
         SP    GAMEKLNG,=PL1'1'
         BNZ   CMD4CLR
         MVI   SWITCH,C'W'
         B     CMD4RC
CMD4MISS CLI   0(R2),X'04'         IS IT THE ENTERPRISE?
         BE    *+L'*+8             YES
         MVI   MSG3F1,C'8'         IMPOSSIBLE - BOMB
         B     CMD4Z
         MVC   SCMSG1,=CL44'You cannot destroy yourself with phasers'
         B     CMD4CLR
CMD4CLR  PACK  WORKDBLW,INPUT+3(4) SUBTRACT RATED ENERGY
         SP    GAMENRGY,WORKDBLW
         BP    *+L'*+8
         MVI   SWITCH,C'D'
         B     CMD4RC
         MVC   SCRTOI,=CL7'_______' RESET COMMAND DOTS
CMD4RC   XR    R15,R15             SET RC=0
CMD4X   $XRET  CC=(R15)
CMD4BADX L     R2,=A(SC1LN18I)
        $FS    SF=(INT),MF=(I,(R2))
         L     R2,=A(SC1CMD2)
        $FS    SBA=(18,5),                                             X
               MFE=(ATTR,(INT),HILITE,BLINK,COLOR,RED),                X
               MF=(I,(R2))
         B     CMD4BADC
CMD4BADY L     R2,=A(SC1LN19I)
        $FS    SF=(INT),MF=(I,(R2))
         L     R2,=A(SC1CMD3)
        $FS    SBA=(19,5),                                             X
               MFE=(ATTR,(INT),HILITE,BLINK,COLOR,RED),                X
               MF=(I,(R2))
CMD4BADC MVC   SCMSG1,=CL44'The coordinates you supplied are not'
         MVC   SCMSG2,=CL44'in the current quadrant'
         LA    R15,12              SET RC=12
         B     CMD4X
         SPACE 1
        LTORG
         SPACE 1
         DROP  R9
         EJECT
*------> BLOW-SCREEN ROUTINE
*        AT EXIT : R15 = 0 - OK, TPUT DONE
*                        4 - ERROR
*                        8 - TERMINATE (WHEN TEST)
         SPACE 1
BLOWSCR $XENT  BASE=(R9)
         TM    SWEXEC,SWBLOW       TEST BLOW SCREEN?
         BZ    BLWSC2              NO
         L     R8,=A(SCRNB,LSCRNB,RSCRTH)
         USING SCRNB,R8
        $FS    SF=NORMAL,MF=(I,SCBLN01I) SET NORMAL INPUT
         MVI   INPUT+0,C' '        RESET INPUT FIELD
BLWSC1   MVC   SCBLN01R,INPUT+0
         LM    R0,R2,=A(SCRNB,LSCRNB,RSCRTH)
         STM   R0,R2,ASCRIPTR      STORE PTR'S
        #GO    FULLSCR             HEADER (TITLE) SCREEN
         B     *+L'*(R15)          HOW COMPLETE?
         B     BLWSCX              0 - CONTINUE
         B     BLWSCX              4 - ERROR
         LTR   R1,R1               8 - DID USER TYPE IN?
         BZ    BLWSCZ              NO, CONTINUE
         BM    BLWSCX              YES, IMMEDIATE TERMINATION
        $FS    SF=(INT),MF=(I,SCBLN01I) SET ERROR INPUT
         B     BLWSC1
         DROP  R8
*        --> DO FULL-SCREEN TPUT (OUTPUT) ONLY.
BLWSC2   NI    OPTFS,FSFULL+FSSNAP
         OI    OPTFS,FSSKIP
        @FS    OPTFS,SCRNB,SCRNBL,MF=(E,FSPARM)
         LTR   R15,R15
         BZ    *+L'*+8
BLWSCE   LA    R15,4               ERROR, SET RC=4
         B     BLWSCX
        STIMER  WAIT,,BINTVL=TIMEWT,ERRET=BLWSCE
BLWSCZ   XR    R15,R15             SET RC=0
BLWSCX  $XRET  CC=(R15)
         SPACE 1
SCRNBL   DC    A(LSCRNB)
TIMEWT   DC    A(2*100)            2.00 SEC WAIT INTERVAL
         SPACE 1
        LTORG
         SPACE 1
         DROP  R9
         SPACE 1
SCRNB    $FS   CC=EW,WCC=(KBR,RMDT),SBA=(1,1),RA=(1,1,00),MF=L
         $FS   SBA=(1,1),MF=L
SCBLN01I $FS   SF=NORMAL,MF=L
SCBLN01R $FS   TEXT=(' ',1),MF=L
         $FS   SF=(SKIP,INT),MF=L
         $FS   TEXT='        *****    *                   ',MF=L
         $FS   TEXT='    .                  .                ',MF=L
         $FS   TEXT=' .        ***..*** *               *    ',MF=L
         $FS   TEXT='            ***                         ',MF=L
         $FS   TEXT='          *** ...**           .   *|*   ',MF=L
         $FS   TEXT='          #*#*#     . .                 ',MF=L
         $FS   TEXT=' .  .      *.** . **.     .        |    ',MF=L
         $FS   TEXT='.        #*#*# .                        ',MF=L
         $FS   TEXT='              * **** *             |    ',MF=L
         $FS   TEXT='          .      . .                    ',MF=L
         $FS   TEXT='                .****** .     .    |    ',MF=L
         $FS   TEXT='    .    .        .                     ',MF=L
         $FS   TEXT='     .     .              .        |    ',MF=L
         $FS   TEXT='        .    .                          ',MF=L
         $FS   TEXT='                  .       .             ',MF=L
         $FS   TEXT=' .         % .       .                  ',MF=L
         $FS   TEXT='                             .    @@@   ',MF=L
         $FS   TEXT='         .%%%                           ',MF=L
         $FS   TEXT='                                 @@@@@  ',MF=L
         $FS   TEXT='        .                               ',MF=L
         $FS   TEXT='  <---------------------------- @@@@@@@ ',MF=L
         $FS   TEXT='----------------------------->          ',MF=L
         $FS   TEXT='                  .              @@@@@  ',MF=L
         $FS   TEXT='                                        ',MF=L
         $FS   TEXT='              .      .      .     @@@   ',MF=L
         $FS   TEXT='      .      .             .            ',MF=L
         $FS   TEXT='              .            .           .',MF=L
         $FS   TEXT='   .    .            .                  ',MF=L
         $FS   TEXT='      .   .                        |    ',MF=L
         $FS   TEXT='                                        ',MF=L
         $FS   TEXT='           .        . *   .   *    |    ',MF=L
         $FS   TEXT='          **        .      .            ',MF=L
         $FS   TEXT='                      * .   *      |    ',MF=L
         $FS   TEXT='  .       ***                           ',MF=L
         $FS   TEXT='                     *    *        |   .',MF=L
         $FS   TEXT='            **       .         .        ',MF=L
         $FS   TEXT='      **          ***  . *         |    ',MF=L
         $FS   TEXT='               *          .             ',MF=L
         $FS   TEXT='       **  .     ****              |    ',MF=L
         $FS   TEXT='       .        *     .                 ',MF=L
         $FS   TEXT='                ****              *|*   ',MF=L
         $FS   TEXT='          .      *                      ',MF=L
         $FS   TEXT='                **                 *    ',MF=L
         $FS   TEXT='                  *    .     .          ',MF=L
         $FS   TEXT='         .                              ',MF=L
         $FS   TEXT='                                        ',MF=L
         $FS   TEXT='                                        ',MF=L
         $FS   TEXT='                                        ',MF=L
         $FS   SBA=(1,2),SF=(IC),MF=L
LSCRNB   EQU   *-SCRNB
         EJECT
*------> THIS ROUTINE RETURNS A NUMBER FROM 0 - 9 IN REGISTER 1.
         SPACE 1
RANDOMR $XENT  BASE=(R9)
        TIME   MIC,TIMEAREA        GET TOD CLOCK
         XR    R1,R1               CLEAR WORK REGISTER
         IC    R1,TIMEAREA+6       LOAD LEFT NIBBLE
         SRL   R1,4                CLEAR RIGHT MIC NIBBLE
         CH    R1,=H'10'           IF IT'S GREATER THAN 10 ...
         BL    *+L'*+4             NO
         SRL   R1,1                YES, DIVIDE BY TWO
         L     R14,4(R13)          PREVIOUS SAVE AREA
         ST    R1,24(R14)          SET BACK R1
        $XRET  CC=0
         SPACE 1
        LTORG
         SPACE 1
         DROP  R9
         EJECT
*------> THIS ROUTINE RETURNS THE SQUARE ROOT OF A NUMBER.
*        R1 = ENTRY NUMBER / EXIT RESULT
         SPACE 1
SQRTRTN $XENT  BASE=(R9)
         CH    R1,=H'1'            SQRT OF 1 = 1
         BNH   SQRTRTX
         XR    R2,R2               INITIALIZE LOW REG TO 0
         LR    R3,R1                " "       HIGH REG TO X
SQRTLOOP LR    R6,R3               IF DISTANCE BETWEEN
         SR    R6,R2                   LOW AND HIGH
         CH    R6,=H'1'                EQUAL TO 1 ...
         BNE   *+L'*+6
         LR    R4,R3                   PUT HIGH IN R4
         B     SQRTEND             GOTO END
         LR    R4,R3               GET HIGH
         AR    R4,R2               GET LOW
         SRL   R4,1                AVERAGE - TRUNCATED
         LTR   R5,R4               SAVE IT
         STH   R5,SQRTHALF         AGAIN - IN MEMORY
         BZ    *+L'*+4
         MH    R5,SQRTHALF         GET SQUARE
         CR    R5,R1               COMPARE
         BE    SQRTEND             EQUAL
         BL    *+L'*+6             LOW
         LR    R3,R4               RE-INIT HIGH PTR
         B     SQRTLOOP
         LR    R2,R4               RE-INIT LOW PTR
         B     SQRTLOOP
SQRTEND  LR    R1,R4               PUT ANSWER INTO R1
SQRTRTX  L     R14,4(R13)          PREVIOUS SAVE AREA
         ST    R1,24(R14)          SET BACK R1
        $XRET  CC=0
         SPACE 1
        LTORG
         SPACE 1
         DROP  R9
         EJECT
*------> THIS ROUTINE GIVES ANY KLINGONS PRESENT IN THE CURRENT QUAD
*              THE OPPORTUNITY TO FIRE BACK AT THE ENTERPRISE.
*        AT EXIT : R15 = 0 OR 4.
REACTIO $XENT  BASE=(R9)
         L     R2,KIRKPTR          GET REALTIVE QUADRANT
         SLL   R2,6                MULTIPLY BY 64
         A     R2,STARCHRT         ADD BASE ADDRESS
         LA    R3,64               SET TO 64
         LR    R5,R2               SAVE BASE
RCTFND1  CLI   0(R2),X'02'         IS IT A BASE?
         BE    RCTFND2             YES, GO LOCATE
         LA    R2,1(R2)            BUMP PTR
         BCT   R3,RCTFND1          ITERATE
         B     RCTCOUNT            NO BASE, GO RETALIATE
RCTFND2  SR    R2,R5               GET OFFSET
         BZ    *+L'*+4
         MH    R2,=Y(L'QUADCVT2)   * NODE LENGTH
         A     R2,=A(QUADCVT2)     ADD BASE ADDRESS
         PACK  ACCUM1,0(1,R2)      X AXIS
         PACK  ACCUM2,2(1,R2)      Y AXIS
         L     R2,KIRKPTR          GET (X,Y) OF ENTERPRISE
         SLL   R2,6                PROCESS SAME AS ABOVE
         A     R2,STARCHRT
         LA    R3,64
         LR    R5,R2
RCTFND3  CLI   0(R2),X'04'
         BE    RCTFND4
         LA    R2,1(R2)
         BCT   R3,RCTFND3
         MVI   MSG3F1,C'9'         IMPOSSIBLE - BOMB
         LA    R0,L'MSG3
         STH   R0,ERRMSL
         LA    R0,MSG3
         ST    R0,ERRMSA
         LA    R15,4               ERROR, SET RC=4
         B     REACTIZ
RCTFND4  SR    R2,R5
         BZ    *+L'*+4
         MH    R2,=Y(L'QUADCVT2)
         A     R2,=A(QUADCVT2)
         PACK  ACCUM3,0(1,R2)
         PACK  ACCUM4,2(1,R2)
         CP    ACCUM1,ACCUM3       ARE X COOR'S SAME
         BE    RCTFND5
         CP    ACCUM2,ACCUM4       ARE Y COOR'S SAME
         BNE   RCTCOUNT
         SP    ACCUM1,ACCUM3
         OI    ACCUM1,X'0F'
         CLI   ACCUM1,X'1F'        IS ABSOLUTE DIFFERENCE 1
         BNE   RCTCOUNT            NO, GO RETALIATE
         B     RCTDOCK             YES, GO POST DOCKED STATUS
RCTFND5  SP    ACCUM2,ACCUM4
         OI    ACCUM2,X'0F'
         CLI   ACCUM2,X'1F'        IS ABSOLUTE DIFFERENCE 1
         BNE   RCTCOUNT            NO, GO RETALIATE
RCTDOCK  MVI   STATUS,C'4'
         L     R2,=A(SC1ATT)
        $FS    SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,GREEN),       X
               MF=(I,(R2))
         ZAP   GAMENRGY,=PL3'5000' REPLENISH ENERGY
         ZAP   GAMETORP,=PL2'10'    " "      TORPEDOES
         ZAP   DEFLECT,=PL2'100'    " "      DEFLECTION %
         B     REACTIX
RCTCOUNT L     R2,KIRKPTR          GET RELATIVE QUADRANT
         SLL   R2,6                MULTIPLY BY 64
         A     R2,STARCHRT
         LA    R3,64
         XR    R4,R4
RCTCNT1  CLI   0(R2),X'01'         COUNT KLINGONS
         BNE   RCTCNT2
         LA    R4,1(R4)
RCTCNT2  LA    R2,1(R2)
         BCT   R3,RCTCNT1
         LTR   R4,R4
         BZ    *+L'*+8
         MH    R4,=H'500'          RETALIATION ENERGY = 500
         LA    R4,0(R4)            PER KLINGON MINUS
         CVD   R4,WORKDBLW         SCREEN EFFICIENCY
         ZAP   WRKFULL1,=PL2'100'
         SP    WRKFULL1,DEFLECT
         MP    WORKDBLW,WRKFULL1
         DP    WORKDBLW,=PL2'100'
         SP    GAMENRGY,WORKDBLW(6)
         BP    *+L'*+8
         MVI   SWITCH,C'D'
         B     REACTIX
         MVI   STATUS,C'1'
         L     R2,=A(SC1ATT)
        $FS    SFE=(ATTR,(SKIP,INT),HILITE,DEFAULT,COLOR,GREEN),       X
               MF=(I,(R2))
REACTIX  XR    R15,R15             SET RC=0
REACTIZ $XRET  CC=(R15)
         SPACE 1
        LTORG
         SPACE 1
         DROP  R9
         EJECT
*------> THIS ROUTINE TPUT'S TRAILERS.
*        AT EXIT : R15 = 0, 4 OR 8.
         SPACE 1
DSPLEND $XENT  BASE=(R9)
         L     R8,=A(SCRNX,LSCRNX,RSCRTX)
         USING SCRNX,R8
         MVI   SCXLN05,C' '
         MVC   SCXLN05+1(L'SCXLN05-1),SCXLN05
         MVC   SCXLN07,SCXLN05
         MVC   SCXLN09,SCXLN05
        $FS    SF=NORMAL,MF=(I,SCXLN20I) SET NORMAL INPUT
         MVC   INPUT+0(3),=CL3' '  RESET INPUT FIELD
         CLI   SWITCH,C'W'
         BE    DSPLWIN
         MVC   SCXLN05(L'DEADMSG1),DEADMSG1
         MVC   SCXLN07(L'DEADMSG2),DEADMSG2
         MVC   SCXLN09(L'DEADMSG3),DEADMSG3
         B     DSPLE10
DSPLWIN  MVC   SCXLN05(L'WINMSG1),WINMSG1
         MVC   SCXLN07(L'WINMSG2),WINMSG2
         MVC   SCXLN09(L'WINMSG3),WINMSG3
DSPLE10  MVC   SCXLN20R,INPUT+0
DSPLE11  LM    R0,R2,=A(SCRNX,LSCRNX,RSCRTX)
         STM   R0,R2,ASCRIPTR      STORE PTR'S
        #GO    FULLSCR             TRAILER SCREEN
         B     *+L'*(R15)          HOW COMPLETE?
         B     DSPLE12             0 - PF-KEY
         B     DSPLEX              4 - ERROR
         LTR   R1,R1               8 - DID USER TYPE IN?
         BZ    DSPLEZ              NO, CONTINUE
         BM    DSPLEX              YES, IMMEDIATE TERMINATION
         MVC   WORKDBLW(3),INPUT+0
         MVI   WORKDBLW+3,C' '
         LA    R0,3
         CLI   WORKDBLW,C' '
         BNE   *+L'*+14
         MVC   WORKDBLW(3),WORKDBLW+1
         BCT   R0,*-14
         B     DSPLEZ              ALL BLANKS, CONTINUE
         CLC   WORKDBLW(2),=CL2'9 '
         BE    DSPLEX              IMMEDIATE TERMINATION
         CLC   WORKDBLW(2),=CL2'Y '
         BE    DSPLEZ
         CLC   WORKDBLW(2),=CL2'N '
         BE    DSPLEY
         CLC   WORKDBLW(3),=CL3'NO '
         BE    DSPLEY
         CLC   WORKDBLW(4),=CL4'YES '
         BE    DSPLEZ
        $FS    SF=(INT),MF=(I,SCXLN20I) SET ERROR INPUT
         B     DSPLE10
DSPLE12  CLI   INPUT+0,C'9'        IMMEDIATE TERMINATION?
         BNE   DSPLE11             NO, RESHOW
DSPLEY   LA    R15,8               YES, SET RC=8
         B     DSPLEX
         DROP  R8
DSPLEZ   XR    R15,R15             SET RC=0
DSPLEX  $XRET  CC=(R15)
         SPACE 1
        LTORG
         SPACE 1
         DROP  R9
         EJECT
SCRNX    $FS   CC=EW,WCC=(KBR,RMDT),SBA=(1,1),RA=(1,1,00),MF=L
         $FS   SBA=(1,1),MF=L
         $FS   SF=(SKIP,INT),MF=L
         $FS   SBA=(1,25),TEXT='**** STAR-TREK end of game ****',MF=L
         $FS   SBA=(5,21),MF=L
SCXLN05  $FS   TEXT=(' ',40),MF=L
         $FS   SBA=(7,21),MF=L
SCXLN07  $FS   TEXT=(' ',40),MF=L
         $FS   SBA=(9,21),MF=L
SCXLN09  $FS   TEXT=(' ',40),MF=L
         $FS   SBA=(20,1),SF=(SKIP),MF=L
         $FS   TEXT='Another mission ?',MF=L
SCXLN20I $FS   SF=NORMAL,MF=L
SCXLN20R $FS   TEXT=(' ',3),MF=L
         $FS   SF=(SKIP),TEXT='<= N(o) / Y(es) or just ENTER',MF=L
         $FS   SBA=(20,20),SF=(IC),MF=L
LSCRNX   EQU   *-SCRNX
         EJECT
*------> READ "SCRNX" PROCESS ROUTINE.
*        AT EXIT : R1=INPUT TRUE LENGTH.
         SPACE 1
RSCRTX  $XENT  BASE=(R9)
         XR    R2,R2               INITIAL COUNT
RXLOOP  @FSR   ,                   GET REPLY TEXT
         LTR   R15,R15             NO MORE FIELDS MODIFIED?
         BNZ   RXPRCD              NO
         L     R1,4(R13)           YES, END, GET PREVIOUS SAVE AREA
         ST    R2,24(R1)           PASS BACK COUNT IN R1
         XR    R15,R15             EXIT
        $XRET  CC=(R15)
RXPRCD   CLC   SCXCSF1,0(R15)
         BNE   RXLOOP              UNEXPECTED (CHECK SCREEN IMAGE)
         LTR   R1,R1
         BZ    RXFC1               FIELD CLEARED (CANC FC)
         CH    R1,=H'3'
         BH    RXLOOP              UNEXPECTED (CHECK SCREEN IMAGE)
         MVC   INPUT+0(3),=CL3' '  FOR UPPERCASE
         SH    R1,=H'1'
         EX    R1,RXOF1            COPY RESPONSE
         B     RXBF1
RXFC1    MVC   INPUT+0(3),=CL3' '  RESPONSE CLEARED
RXBF1    LA    R2,3(R2)            BUMP COUNT
         B     RXLOOP
RXOF1    OC    INPUT+0(*-*),3(R15) <<EXECUTED>>
         SPACE 1
SCXCSF1  $FS   SBA=(20,20),MF=L
         SPACE 1
        LTORG
         SPACE 1
         DROP  R9
         EJECT
*------> FULL-SCREEN SERVICE ROUTINE (STANDARD 24 X 80 SCREEN).
*        AT ENTRY, THE ROUTINE HAS THREE INPUTS :
*              "ASCRIPTR" POINTS TO THE SCREEN IMAGE,
*              "ASCRLGTH" POINTS TO THE SCREEN IMAGE LENGTH,
*              "ASCRRTN" POINTS TO THE INPUT SCREEN PROCESS ROUTINE.
*        AT EXIT : R15 = 0 - PF-KEY PROCESS (FUNCTION IN INPUT)
*                        4 - ERROR
*                        8 - R1 = ZERO -> RESHOW
*                                 MINUS -> CLEAR AND END
*                                 PLUS -> ANALYZE INPUT
         SPACE 1
FULLSCR $XENT  BASE=(R9)
*        --> DO FULL-SCREEN TPUT (OUTPUT) AND TGET (INPUT).
         NI    OPTFS,FSFULL+FSSNAP
         L     R2,ASCRIPTR
        @FS    OPTFS,(R2),ASCRLGTH,MF=(E,FSPARM)
         LTR   R15,R15
         BZ    FSCAID
         CH    R15,=H'8'           END REQUESTED?
         BE    *+L'*+8             YES
         LA    R15,4               NO, ERROR, SET RC=4
         B     FSCX                EXIT
         L     R1,=F'-1'           YES (PA-KEY)
         B     FSCRC
FSCAID   LTR   R1,R1               AID?
         BZ    FSNONE              NO, NONE
         CLI   0(R1),X'7D'         YES, ENTER KEY?
         BE    FSCENT              YES
         CLI   0(R1),X'F1'         NO, PF-KEY 1-9?
         BL    FSNONE              NO
         CLI   0(R1),X'F9'
         BNH   FSCPFK              YES
FSNONE   XR    R1,R1               NO, RE-DISPLAY
         B     FSCRC
FSCPFK   MVC   INPUT+0(1),0(R1)    MOVE THE REQUESTED PF-KEY
         B     FSCX                EXIT
FSCENT  #GO    ASCRRTN,AR=D        GO TO REPLY ROUTINE
*        -->   AT EXIT, R1=INPUT TRUE LENGTH.
FSCRC    LA    R15,8               NOT FOUND, SET RC=8
         L     R14,4(R13)          PREVIOUS SAVE AREA
         ST    R1,24(R14)          SET BACK R1
FSCX    $XRET  CC=(R15)
         SPACE 1
        LTORG
         SPACE 1
         DROP  R9
         EJECT
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
*          W O R K I N G     S T O R A G E     S E C T I O N          *
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
         SPACE 1
WORKDBLW DC    D'0'
TIMEAREA DC    D'0'                RETURN AREA FOR TIME MACRO
TERMAT  @FST   ,
ATIOT    DC    A(*-*)
ATSO     DC    A(*-*)
APSCB    DC    A(*-*)
ACPPL    DC    A(*-*)
ASCRIPTR DC    A(*-*)              PTR TO ACTIVE SCREEN IMAGE
ASCRLGTH DC    A(*-*)              PTR TO LENGTH OF ACTIVE SCREEN IMAGE
ASCRRTN  DC    A(*-*)              PTR TO PROCESS READ SCREEN ROUTINE
STARCHRT DC    A(*-*)              ADDRESS OF MATRIX AREA
STARCHRL DC    A(16*64)            DOT MATRIX LENGTH
STARCHRX DC    A(*-*)              ADDRESS OF END OF MATRIX AREA + 1
ERRMSA   DC    F'0'
FSPARM  @FS    ,,,MF=L
FSMSBA   EQU   4*L'SCHCSF1         MAX. = 4 * SCREEN BUFFER ADDRESSES
FSBFSZ   DC    A((((FSMSBA+L'INPUT)+7)/8)*8) MAX. => L.SBA'S + TEXT
RQSZ     DC    0H'0',AL1(24,80)    REQUESTED SCREEN SIZE
ERRMSL   DC    H'0'
SQRTHALF DC    H'0'                "SQRTRTN" WORK USE
INPUT    DC    CL16' '             INPUT FROM TERMINAL VIA TGET
SCRTOI   DC    CL7'_______'        TERMINAL OUTPUT/INPUT
         PRINT GEN
OPTFS   @FSO   ,
         PRINT &EULST
SWITCH   DC    CL1' '              W=WIN, D=DEAD
MSG1     DC    C' -> "FSRTN " routine failed / RC =    '
MSG1F1   EQU   MSG1+10,1
MSG1F2   EQU   MSG1+L'MSG1-4,4
MSG2     DC    C' -> "FSRTN" routine failed - OPT =    , RC =    '
MSG2F1   EQU   MSG2+35,2
MSG2F2   EQU   MSG2+L'MSG2-4,4
MSG3     DC    C' -> Error . -> Impossible - Bomb'
MSG3F1   EQU   MSG3+10,1
         SPACE 1
SCMSG1   DC    CL44' '
SCMSG2   DC    CL44' '
SCMSG3   DC    CL44' '
DEADMSG1 DC    CL33'The ENTERPRISE has been defeated'
DEADMSG2 DC    CL33'The FEDERATION will be conquered'
DEADMSG3 DC    CL33'May be in your next life ...'
WINMSG1  DC    CL33'The KLINGONS have been defeated '
WINMSG2  DC    CL33'The FEDERATION is victorious    '
WINMSG3  DC    CL35'May be the KLINGONS will try again'
INITKLNG DC    PL2'+0'             INIT AREA FOR KLINGONS
INITBASE DC    PL1'+0'             INIT AREA FOR STAR BASES
INITSTRS DC    PL2'+0'             INIT AREA FOR STARS
ACCUM1   DC    PL1'+0'
ACCUM2   DC    PL1'+0'
ACCUM3   DC    PL1'+0'
ACCUM4   DC    PL1'+0'
TRTABLE  DC    XL256'0'            WORK AREA FOR TRT'S
WRKHALF1 DC    H'0'                WORK AREA
WRKHALF2 DC    H'0'                 " "
WRKFULL1 DC    F'0'                 " "
QUADFIND DC    CL3' '              WORK AREA FOR QUAD CONVERSIONS
SWEXEC   DC    XL1'0'
FSEXC    EQU   X'80'               COLORS
FSEXH    EQU   X'40'               HIGHLIGHTING
SWBLOW   EQU   X'08'               BLOW SCREEN TEST
SWHELP   EQU   X'02'               SKIP TITLE
SWCOMP   EQU   X'01'               GO COMPUTER
KIRKPTR  DC    F'0'
         SPACE 1
GAMESTAT DS    0CL7
GAMEKLNG DC    PL2'+0'             NUMBER OF KLINGONS FOR THIS GAME
GAMESTRS DC    PL2'+0'              " "      STARS
GAMEBASE DC    PL1'+0'              " "      BASES
GAMEYRS  DC    PL2'+0'              " "      YEARS
         SPACE 1
GAMENRGY DC    PL4'5000'           AMOUNT OF ENERGY TO START
GAMEDATE DC    CL6'5537.1'         STAR DATE OF THE GAME
GAMETORP DC    PL2'10'             PHOTON TORPEDOES
STATUS   DC    CL1'1'              1=GREEN, 2=YELLOW, 3=RED, 4=DOCKED
DEFLECT  DC    PL5'100'            DEFLECTION ENERGY PERCENTAGE
         SPACE 1
*------- THIS IS THE GAME TABLE - IT CONTAINS 10 GAMES.
         SPACE 1
GAMETBL  DS    0CL7
         DC    PL2'08',PL2'50',PL1'2',PL2'20'
         DC    PL2'09',PL2'51',PL1'2',PL2'20'
         DC    PL2'10',PL2'52',PL1'2',PL2'21'
         DC    PL2'11',PL2'53',PL1'2',PL2'21'
         DC    PL2'12',PL2'54',PL1'2',PL2'22'
         DC    PL2'13',PL2'55',PL1'3',PL2'22'
         DC    PL2'14',PL2'56',PL1'3',PL2'23'
         DC    PL2'15',PL2'57',PL1'3',PL2'23'
         DC    PL2'16',PL2'58',PL1'4',PL2'24'
         DC    PL2'17',PL2'59',PL1'4',PL2'24'
         SPACE 1
*------- CONVERSION TABLES FROM INTERNAL QUAD NUMBERS TO WORLD
*              COORDINATES.
         SPACE 1
QUADCVT1 DC    CL3'1,4',CL3'2,4',CL3'3,4',CL3'4,4'
         DC    CL3'1,3',CL3'2,3',CL3'3,3',CL3'4,3'
         DC    CL3'1,2',CL3'2,2',CL3'3,2',CL3'4,2'
         DC    CL3'1,1',CL3'2,1',CL3'3,1',CL3'4,1'
QUADLVT1 EQU   (*-QUADCVT1)/L'QUADCVT1
         SPACE 1
QUADCVT2 DC    CL3'1,8',CL3'2,8',CL3'3,8',CL3'4,8'
         DC    CL3'5,8',CL3'6,8',CL3'7,8',CL3'8,8'
         DC    CL3'1,7',CL3'2,7',CL3'3,7',CL3'4,7'
         DC    CL3'5,7',CL3'6,7',CL3'7,7',CL3'8,7'
         DC    CL3'1,6',CL3'2,6',CL3'3,6',CL3'4,6'
         DC    CL3'5,6',CL3'6,6',CL3'7,6',CL3'8,6'
         DC    CL3'1,5',CL3'2,5',CL3'3,5',CL3'4,5'
         DC    CL3'5,5',CL3'6,5',CL3'7,5',CL3'8,5'
         DC    CL3'1,4',CL3'2,4',CL3'3,4',CL3'4,4'
         DC    CL3'5,4',CL3'6,4',CL3'7,4',CL3'8,4'
         DC    CL3'1,3',CL3'2,3',CL3'3,3',CL3'4,3'
         DC    CL3'5,3',CL3'6,3',CL3'7,3',CL3'8,3'
         DC    CL3'1,2',CL3'2,2',CL3'3,2',CL3'4,2'
         DC    CL3'5,2',CL3'6,2',CL3'7,2',CL3'8,2'
         DC    CL3'1,1',CL3'2,1',CL3'3,1',CL3'4,1'
         DC    CL3'5,1',CL3'6,1',CL3'7,1',CL3'8,1'
QUADLVT2 EQU   (*-QUADCVT2)/L'QUADCVT2
         SPACE 1
QUADCVT3 $FS   SBA=(2,3),MF=L
         $FS   SBA=(2,7),MF=L
         $FS   SBA=(2,9),MF=L
         $FS   SBA=(2,13),MF=L
         $FS   SBA=(2,15),MF=L
         $FS   SBA=(2,19),MF=L
         $FS   SBA=(2,21),MF=L
         $FS   SBA=(2,25),MF=L
         $FS   SBA=(4,3),MF=L
         $FS   SBA=(4,7),MF=L
         $FS   SBA=(4,9),MF=L
         $FS   SBA=(4,13),MF=L
         $FS   SBA=(4,15),MF=L
         $FS   SBA=(4,19),MF=L
         $FS   SBA=(4,21),MF=L
         $FS   SBA=(4,25),MF=L
         $FS   SBA=(6,3),MF=L
         $FS   SBA=(6,7),MF=L
         $FS   SBA=(6,9),MF=L
         $FS   SBA=(6,13),MF=L
         $FS   SBA=(6,15),MF=L
         $FS   SBA=(6,19),MF=L
         $FS   SBA=(6,21),MF=L
         $FS   SBA=(6,25),MF=L
         $FS   SBA=(8,3),MF=L
         $FS   SBA=(8,7),MF=L
         $FS   SBA=(8,9),MF=L
         $FS   SBA=(8,13),MF=L
         $FS   SBA=(8,15),MF=L
         $FS   SBA=(8,19),MF=L
         $FS   SBA=(8,21),MF=L
         $FS   SBA=(8,25),MF=L
         SPACE 1
*- - - - DSECT'S - - - - - - - - - - - - - - - - - - - - - - - - - - -*
         PRINT NOGEN
        IKJCPPL
        IKJPSCB
        IKJEBECA
TIODSECT DSECT
        IEFTIOT1
         SPACE 1
         END
