         TITLE 'EXIT15: JES2 EXIT 15 OUTPUT DATA SET / COPY SELECT'
***********************************************************************
* +-----------------------------------------------------------------+ *
* |                         E X I T 1 5                             | *
* +-----------------------------------------------------------------+ *
***********************************************************************
*                                                                     *
*        JES2 EXIT 15 OUTPUT DATA SET / COPY SELECT                   *
*                                                                     *
*        ENVIRONMENT:  JES2/SP 1.3.6  FMID HJE1367  PUT 8806  PDO8840 *
*                      MVS/SP 1.3.5 (NON-XA)                          *
*                                                                     *
*        THIS JES2 EXIT PROGRAM IS DESIGNED TO INSERT A DJDE COMMAND  *
*        IN FRONT OF EACH REPORT OR COPY GOING TO THE XEROX PRINTER   *
*        TO CAUSE THE PRINTER TO SKIP TO THE FRONT OF THE NEXT CUT    *
*        SHEET WHEN THE XEROX PRINTER IS PRINTING IN DUPLEX MODE      *
*        AND/OR MULTIPLE LOGICAL PAGES PER PHYSICAL PAGE/SIDE.  THIS  *
*        IS TO INSURE THAT THE FIRST LOGICAL PAGE OF A REPORT DOES    *
*        NOT OCCUR ON THE SAME PHYSICAL SHEET AS THE LAST LOGICAL     *
*        PAGE OF THE PREVIOUS REPORT.                                 *
*                                                                     *
*        THIS EXIT IS COPIED INTO EXIT1MIT AT ASSEMBLY TIME.          *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*         BENJ. FRANKLIN FEDERAL SAVINGS AND LOAN ASSOCIATION         *
*                                                                     *
*                           DISCLAIMER                                *
*                                                                     *
*  THE XEROX FORMS CONTROL JES2 EXITS WE'VE DEVELOPED ARE AVAILABLE   *
*  TO THE PUBLIC DOMAIN ON AN "AS IS" BASIS.  WE MAKE NO WARRANTY     *
*  OR GUARANTEE THAT THE EXITS WILL FUNCTION CORRECTLY IN YOUR        *
*  ENVIRONMENT, NOR CAN WE BE HELD LIABLE FOR ANY ERRORS OR LOSSES    *
*  RESULTING FROM THEIR IMPLEMENTATION AND/OR USAGE.                  *
*                                                                     *
*  YOU USE THESE EXITS AT YOUR OWN RISK AND MUST SUPPORT THEM BY      *
*  YOURSELF.                                                          *
*                                                                     *
***********************************************************************
***********************************************************************
*                                                                     *
*         BENJ. FRANKLIN FEDERAL SAVINGS AND LOAN ASSOCIATION         *
*                                                                     *
*                           DISCLAIMER                                *
*                                                                     *
*  THIS MODULE IS AVAILABLE IN THE PUBLIC DOMAIN ON AN "AS IS"        *
*  BASIS.  WE MAKE NO WARRANTY OR GUARANTEE THAT IT WILL FUNCTION     *
*  CORRECTLY IN YOUR ENVIRONMENT, NOR CAN WE BE HELD LIABLE FOR ANY   *
*  ERRORS OR LOSSES RESULTING FROM ITS IMPLEMENTATION AND/OR USAGE.   *
*                                                                     *
*  YOU USE THIS MODULE AT YOUR OWN RISK AND MUST SUPPORT IT BY        *
*  YOURSELF.                                                          *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        SYMBOLIC REGISTER DEFINITIONS                                *
*                                                                     *
***********************************************************************
*
IC       EQU   R2                  INPUT CODE (FROM R0)
IP       EQU   R3                  INPUT PARAMETERS LIST ADDRESS
*JOE      EQU   R4                  JOE ADDRESSABILITY REGISTER
PDDB     EQU   R5                  PDDB ADDRESSABILITY REGISTER
DCTREG   EQU   R6                  DCT ADDRESSABILITY REGISTER
BUFREG   EQU   R7                  BUFFER ADDRESSABILITY REGISTER
*JCT      EQU   R10                 JCT ADDRESSABILITY REGISTER
*BASE1    EQU   R11                 HCT ADDRESSABILITY REGISTER
*BASE2    EQU   R12                 PROCESSOR ADDRESSABILITY REGISTER
*SAVE     EQU   R13                 PCE ADDRESSABILITY REGISTER
*LINK     EQU   R14                 LINK REGISTER
         SPACE 2
***********************************************************************
*                                                                     *
*        EXIT 15 ENTRY POINT                                          *
*                                                                     *
***********************************************************************
*
EXIT15   $ENTRY BASE=R12,CSECT=YES
         SPACE 1
         $SAVE                     SAVE CALLER'S REGISTERS
         LR    R12,R15             LOAD BASE REGISTER
         LR    IC,R0               SAVE INPUT CODE
         LR    IP,R1               SAVE INPUT PARAMETER LIST ADDRESS
         L     DCTREG,16(0,IP)     LOAD DCT ADDRESSABILITY REGISTER
         USING DCT,DCTREG          SET ADDRESSABILITY TO DCT
         USING JCT,R10             SET ADDRESSABILITY TO JCT
*
***********************************************************************
*                                                                     *
*        TEST TO SEE IF EXIT 15 SHOULD HAVE BEEN TAKEN                *
*                                                                     *
***********************************************************************
*
         TM    DCTPPSW2,DCTSDSSW   SEPDS SPECIFIED ?
         BZ    EX15RET0            NO - LEAVE EXIT.  DOESN'T WANT SEP.
*
***********************************************************************
*                                                                     *
*        TEST TO SEE IF OUTPUT IS GOING TO THE XEROX PRINTER          *
*                                                                     *
***********************************************************************
*
         C     IC,=F'0'            DATA SET SELECT ?
         BE    EX15RET0            YES - LEAVE EXIT.  CANNOT SEP HERE.
*
         TM   PCEID,PCEPUSID       IF A PUNCH DEVICE ?
         BO    EX15RET0            YES - LEAVE EXIT.  NO SEP NECESSARY.
*
         CLC   DCTDEVN(3),=CL3'PRT'     ARE WE GOING TO A PRINTER ?
         BE    CH15PRTE      * YES - GO LOOK FOR PRINTER TABLE ENTRY
         CLC   DCTDEVN(7),=CL7'PRINTER' ARE WE GOING TO A PRINTER ?
         BE    CH15PRTE      * YES - GO LOOK FOR PRINTER TABLE ENTRY
         BNE   EX15RET0      * NO -  NO SKIP NECESSARY.
*
         USING PTBDSECT,PC2  * MAKE PRINTER TABLE ENTRY ADDRESSABLE
CH15PRTE L     PC2,=A(PRNTRTAB) FIND PRINTER ENTRY IN PRINTER TABLE
         C     PC2,=F'0'     * PRINTER TABLE ADDRESSABLE?
         BE    EX15RET0      * NO - MIGHT AS WELL RETURN TO JES2
CH15NEXT CLI   PTBENTRY,C'0' * DEFAULT TO PRINTER 0 IF BEYOND LAST
         BE    CH15TYPE      *   PRINTER TABLE ENTRY
         CLC   PTBENTRY,DCTDEVN+3 IF CORRECT PRINTER ENTRY
         BE    CH15TYPE      *   THEN GO CHECK PRINTER TYPE
         CLC   PTBENTRY,DCTDEVN+7 IF CORRECT PRINTER ENTRY
         BE    CH15TYPE      *   THEN GO CHECK PRINTER TYPE
         LA    PC2,12(0,PC2) * ELSE GET SET TO CHECK NEXT ENTRY
         B     CH15NEXT      *
CH15TYPE CLI   PTBTYPE,C'X'  * IF NOT A XEROX PRINTER
         BNE   EX15RET0      *   THEN LEAVE EXIT.                        ACC0577
*
         TM    PREVMODE,MFNUFRNT IF NO SKIP TO NEXT CUT SHEET IS NEEDED
         BZ    EX15RET0      *   THEN LEAVE EXIT.
*
***********************************************************************
*                                                                     *
*        SEND THE COMMAND TO CAUSE THE SKIP TO NEW FRONT OF SHEET     *
*                                                                     *
***********************************************************************
*
SENDX15D $GETBUF TYPE=HASP,FIX=YES,WAIT=YES GET A WORK BUFFER
         LR    BUFREG,R1           SAVE ADDRESS OF BUFFER
         USING BFPDSECT,BUFREG     SET ADDRESSABILITY TO BUFFER
*
         MVC   BUFSTART(28),DJDEEX15 MOVE DJDE COMMAND TO BUFFER
         LA    R0,28               LOAD LINE LENGTH
         LA    R1,BUFSTART         LOAD ADDRESS OF EJECT COMMAND
         $PRPUT DATA=(R1),LEN=(R0),COUNT=1,WAIT=YES,CC=M
*
         $FREEBUF (BUFREG)         FREE THE WORK BUFFER
*
***********************************************************************
*                                                                     *
*        RETURN TO JES2                                               *
*                                                                     *
***********************************************************************
*
EX15RET0 LA    R15,0               SET RETURN CODE TO 0
         $RETURN RC=(R15)          RETURN TO JES2
*
         DROP  DCTREG,PC2,BUFREG,R10
         EJECT
***********************************************************************
*                                                                     *
*        EXIT 15 DATA AREAS                                           *
*                                                                     *
***********************************************************************
*
DJDEEX15 DS    0CL28               XEROX DJDE COMMAND
         DC    XL1'89'               SKIP TO CHANNEL 1 AFTER WRITE
         DC    CL27'  DJDE SIDE=(NUFRONT),END; '
         EJECT
         LTORG
*
*********************** END OF MODULE EXIT15 **************************
