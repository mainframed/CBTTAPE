         TITLE 'EXIT5: JES2 EXIT 5 JES2 COMMAND PREPROCESSOR'
***********************************************************************
* +-----------------------------------------------------------------+ *
* |                          E X I T 5                              | *
* +-----------------------------------------------------------------+ *
***********************************************************************
*                                                                     *
*        JES2 EXIT 5 JES2 COMMAND PREPROCESSOR                        *
*                                                                     *
*        ENVIRONMENT:  JES2/SP 1.3.6  FMID HJE1367  PUT 8806  PDO8840 *
*                      MVS/SP 1.3.5                                   *
*                                                                     *
*        THIS JES2 EXIT PROGRAM IS DESIGNED WORK IN CONJUCTION WITH   *
*        OUR SPECIAL JES2 EXITS 1, 15, AND 250 FOR XEROX FORMS        *
*        CONTROL.  IT WHENEVER THE OPERATOR ISSUES A STOP OR HALT     *
*        COMMAND TO A XEROX 8700/9700 PRINTER THIS EXIT FLAGS THE     *
*        FORMS ID IN THE EXIT DEFINED PRINTER TABLE AS UNKNOWN (????) *
*        SO THAT THE NEXT JOB PRINTED CAUSES AN OPERATOR SETUP        *
*        MESSAGE TO APPEAR ON THE XEROX PRINTER CONSOLE (THUS FORCING *
*        THE OPERATOR TO VERIFY THE FORMS SETUP.                      *
*                                                                     *
*        THIS EXIT IS COPIED INTO EXIT1MIT AT ASSEMBLY TIME.          *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*         BENJ. FRANKLIN FEDERAL SAVINGS AND LOAN ASSOCIATION         *
*                                                                     *
*                           DISCLAIMER                                *
*                                                                     *
*  THE XEROX FORMS CONTROL JES2 EXITS WE'VE DEVELOPED ARE AVAILABLE   *
*  TO THE PUBLIC DOMAIN ON AN "AS IS" BASIS.  WE MAKE NO WARRANTY     *
*  OR GUARANTEE THAT THE EXITS WILL FUNCTION CORRECTLY IN YOUR        *
*  ENVIRONMENT, NOR CAN WE BE HELD LIABLE FOR ANY ERRORS OR LOSSES    *
*  RESULTING FROM THEIR IMPLEMENTATION AND/OR USAGE.                  *
*                                                                     *
*  YOU USE THESE EXITS AT YOUR OWN RISK AND MUST SUPPORT THEM BY      *
*  YOURSELF.                                                          *
*                                                                     *
***********************************************************************
***********************************************************************
*                                                                     *
*         BENJ. FRANKLIN FEDERAL SAVINGS AND LOAN ASSOCIATION         *
*                                                                     *
*                           DISCLAIMER                                *
*                                                                     *
*  THIS MODULE IS AVAILABLE IN THE PUBLIC DOMAIN ON AN "AS IS"        *
*  BASIS.  WE MAKE NO WARRANTY OR GUARANTEE THAT IT WILL FUNCTION     *
*  CORRECTLY IN YOUR ENVIRONMENT, NOR CAN WE BE HELD LIABLE FOR ANY   *
*  ERRORS OR LOSSES RESULTING FROM ITS IMPLEMENTATION AND/OR USAGE.   *
*                                                                     *
*  YOU USE THIS MODULE AT YOUR OWN RISK AND MUST SUPPORT IT BY        *
*  YOURSELF.                                                          *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        SYMBOLIC REGISTER DEFINITIONS                                *
*                                                                     *
***********************************************************************
*
**IC       EQU   R2                  INPUT CODE (FROM R0)
**IP       EQU   R3                  INPUT PARAMETERS LIST ADDRESS
**JOE      EQU   R4                  JOE ADDRESSABILITY REGISTER
**PDDB     EQU   R5                  PDDB ADDRESSABILITY REGISTER
**DCTREG   EQU   R6                  DCT ADDRESSABILITY REGISTER
**BUFREG   EQU   R7                  BUFFER ADDRESSABILITY REGISTER
*JCT      EQU   R10                 JCT ADDRESSABILITY REGISTER
*BASE1    EQU   R11                 HCT ADDRESSABILITY REGISTER
*BASE2    EQU   R12                 PROCESSOR ADDRESSABILITY REGISTER
*SAVE     EQU   R13                 PCE ADDRESSABILITY REGISTER
*LINK     EQU   R14                 LINK REGISTER
         SPACE 2
***********************************************************************
*                                                                     *
*        EXIT 5 ENTRY POINT                                           *
*                                                                     *
***********************************************************************
*
EXIT5    $ENTRY BASE=R12,CSECT=YES
         SPACE 1
         $SAVE                     SAVE CALLER'S REGISTERS
         LR    R12,R15             LOAD BASE REGISTER
         USING JCT,R10             SET ADDRESSABILITY TO JCT
*
***********************************************************************
*                                                                     *
*        TEST TO SEE IF OPERATOR ISSUED STOP OR HALT COMMAND          *
*                                                                     *
***********************************************************************
*
         CLI   COMVERB,C'P'        STOP COMMAND?
         BE    EXT5OPRN            YES - GO CHECK OPERANDS
         CLI   COMVERB,C'Z'        HALT COMMAND?
         BE    EXT5OPRN            YES - GO CHECK OPERANDS
         B     EXT5RET0            ELSE RETURN TO JES2
*
***********************************************************************
*                                                                     *
*        TEST OPERANDS TO SEE ANY APPLY TO A XEROX PRINTER            *
*                                                                     *
***********************************************************************
*
EXT5OPRN C     R5,=F'0'            VERIFY THERE IS AN OPERAND POINTER
         BE    EXT5RET0            NO OPERAND POINTER - RETURN TO JES2
         L     R8,0(R5)            LOAD OPERAND POINTER
         C     R8,=F'0'            VERIFY THERE IS AN OPERAND
         BE    EXT5RET0            NO OPERAND - RETURN TO JES2
         CLC   0(3,R8),=CL3'PRT'   IF NOT PRT (SHORT FORM)
         BNE   EXT5LONG            THEN TRY LONG FORM OF NAME
         LA    R8,3(,R8)           ELSE MOVE R8 TO PRINTER NUMBER
         B     EXT5CHKN              AND GO CHECK PRINTER NUMBER
EXT5LONG CLC   0(7,R8),=CL7'PRINTER' IF NOT PRINTER (LONG FORM)
         BNE   EXT5OPRC            THEN TRY NEXT OPERAND
         LA    R8,7(,R8)           ELSE MOVE R8 TO PRINTER NUMBER
*
         USING PTBDSECT,R2   * MAKE PRINTER TABLE ENTRY ADDRESSABLE
EXT5CHKN L     R2,=A(PRNTRTAB) FIND PRINTER ENTRY IN PRINTER TABLE
         C     R2,=F'0'      * PRINTER TABLE ADDRESSABLE?
         BE    EXT5RET0      * NO - MIGHT AS WELL RETURN TO JES2
EXT5NEXT CLI   PTBENTRY,C'0' * DEFAULT TO PRINTER 0 IF BEYOND LAST
         BE    EXT5TYPE      *   PRINTER TABLE ENTRY
         CLC   PTBENTRY,0(R8)  IF CORRECT PRINTER ENTRY
         BE    EXT5TYPE      *   THEN GO CHECK PRINTER TYPE
         LA    R2,12(0,R2)   * ELSE GET SET TO CHECK NEXT ENTRY
         B     EXT5NEXT      *
EXT5TYPE CLI   PTBTYPE,C'X'  * IF NOT A XEROX PRINTER
         BNE   EXT5OPRC      *   THEN TRY NEXT OPERAND                   ACC0577
*
         MVC   PREVFORM,=CL4'????' XEROX PRINTER - FORCE FORMS SETUP
*
EXT5OPRC CR    R5,R7               WAS THAT THE LAST OPERAND?
         BE    EXT5RET0            YES - RETURN TO JES2
         AR    R5,R6               INCREMENT R5 BY R6 (VALUE OF 4)
         B     EXT5OPRN               AND GO CHECK NEXT OPERAND
*
***********************************************************************
*                                                                     *
*        RETURN TO JES2                                               *
*                                                                     *
***********************************************************************
*
EXT5RET0 LA    R15,0              SET RETURN CODE TO 0 - NEXT EXIT 5
         $RETURN RC=(R15)         RETURN TO JES2
*
         DROP  R2,R10
         EJECT
***********************************************************************
*                                                                     *
*        EXIT 5 DATA AREAS                                            *
*                                                                     *
***********************************************************************
*
         LTORG
*
************************ END OF MODULE EXIT5 **************************
