         TITLE 'EXIT250: JES2 EXIT 250 FORMS SETUP SUPPRESSION'
***********************************************************************
* +-----------------------------------------------------------------+ *
* |                        E X I T 2 5 0                            | *
* +-----------------------------------------------------------------+ *
***********************************************************************
*                                                                     *
*        JES2 EXIT 250 FORMS SETUP SUPPRESSION                        *
*                                                                     *
*        ENVIRONMENT:  JES2/SP 1.3.6  FMID HJE1367  PUT 8806  PDO8840 *
*                      MVS/SP 1.3.5 (NON-XA)                          *
*                                                                     *
*        THIS JES2 EXIT PROGRAM IS DESIGNED TO EITHER ALLOW OR        *
*        SUPPRESS THE OPERATOR SETUP MESSAGE (HASP190) ON A FORMS     *
*        CHANGE TO A NON-3800 PRINTER.  IT WAS SPECIFICALLY DEVELOPED *
*        TO SUPPORT THE XEROX 8700/9700 PRINTER AND IS USED IN        *
*        CONJUNCTION WITH OUR SPECIAL JES2 EXITS 1 AND 15 FOR XEROX   *
*        FORMS CONTROL.  JES2 EXIT 250 IS NOT A STANDARD JES2 EXIT.   *
*        IT IS A USER DEVELOPED MODIFICATION TO JES2.  THE EXIT 250   *
*        CALL IS DONE IN SYS1.HASPSRC(HASPPRPU).  SEE OUR JES2 SMP/E  *
*        USERMOD SYS2.TECH.USERPTF(ZZ03821) FOR THE DETAILS OF        *
*        INSERTING EXIT 250 INTO JES2 MODULE SYS1.HASPSRC(HASPPRPU).  *
*                                                                     *
*        THIS EXIT IS COPIED INTO EXIT1MIT AT ASSEMBLY TIME.          *
*                                                                     *
* REGISTERS AT ENTRY TO EXIT 250:                                     *
*                                                                     *
* R0 - ?                             R8  - ?                          *
* R1 - ADRS OF 'SETUP' PARM LIST     R9  - ?                          *
* R2 - ?                             R10 - JCT 'JCTDSECT'             *
* R3 - BUFFER ADRS 'BUFDSECT'        R11 - HCT 'HCTDSECT'             *
* R4 - DCT 'DCTDSECT'                R12 - 'PRPUDSV'                  *
* R5 - ?                             R13 - PCE/PPPWORK                *
* R6 - ?                             R14 - LINK REGISTER              *
* R7 - ADRS OF 'SETUP' PARM LIST     R15 - RETURN REGISTER            *
*                                                                     *
* ON RETURN EXIT 250 SHOULD RESTORE ALL REGISTERS EXCEPT R15.         *
* REGISTER 15 IS USED TO INDICATE WHETHER TO SUPPRESS OR ALLOW AN     *
* OPERATOR SETUP MESSAGE (HASP190) TO BE ISSUED FOR A FORMS CHANGE AS *
* FOLLOWS:                                                            *
*                                                                     *
*    R15 = 0  INDICATES ALLOW SETUP MESSAGE                           *
*    R15 = 4  INDICATES SUPPRESS SETUP MESSAGE                        *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*         BENJ. FRANKLIN FEDERAL SAVINGS AND LOAN ASSOCIATION         *
*                                                                     *
*                           DISCLAIMER                                *
*                                                                     *
*  THE XEROX FORMS CONTROL JES2 EXITS WE'VE DEVELOPED ARE AVAILABLE   *
*  TO THE PUBLIC DOMAIN ON AN "AS IS" BASIS.  WE MAKE NO WARRANTY     *
*  OR GUARANTEE THAT THE EXITS WILL FUNCTION CORRECTLY IN YOUR        *
*  ENVIRONMENT, NOR CAN WE BE HELD LIABLE FOR ANY ERRORS OR LOSSES    *
*  RESULTING FROM THEIR IMPLEMENTATION AND/OR USAGE.                  *
*                                                                     *
*  YOU USE THESE EXITS AT YOUR OWN RISK AND MUST SUPPORT THEM BY      *
*  YOURSELF.                                                          *
*                                                                     *
***********************************************************************
***********************************************************************
*                                                                     *
*         BENJ. FRANKLIN FEDERAL SAVINGS AND LOAN ASSOCIATION         *
*                                                                     *
*                           DISCLAIMER                                *
*                                                                     *
*  THIS MODULE IS AVAILABLE IN THE PUBLIC DOMAIN ON AN "AS IS"        *
*  BASIS.  WE MAKE NO WARRANTY OR GUARANTEE THAT IT WILL FUNCTION     *
*  CORRECTLY IN YOUR ENVIRONMENT, NOR CAN WE BE HELD LIABLE FOR ANY   *
*  ERRORS OR LOSSES RESULTING FROM ITS IMPLEMENTATION AND/OR USAGE.   *
*                                                                     *
*  YOU USE THIS MODULE AT YOUR OWN RISK AND MUST SUPPORT IT BY        *
*  YOURSELF.                                                          *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        SYMBOLIC REGISTER DEFINITIONS                                *
*                                                                     *
***********************************************************************
*
**IC       EQU   R2                  INPUT CODE (FROM R0)
**IP       EQU   R3                  INPUT PARAMETERS LIST ADDRESS
**JOE      EQU   R4                  JOE ADDRESSABILITY REGISTER
**PDDB     EQU   R5                  PDDB ADDRESSABILITY REGISTER
**DCTREG   EQU   R6                  DCT ADDRESSABILITY REGISTER
**BUFREG   EQU   R7                  BUFFER ADDRESSABILITY REGISTER
*JCT      EQU   R10                 JCT ADDRESSABILITY REGISTER
*BASE1    EQU   R11                 HCT ADDRESSABILITY REGISTER
*BASE2    EQU   R12                 PROCESSOR ADDRESSABILITY REGISTER
*SAVE     EQU   R13                 PCE ADDRESSABILITY REGISTER
*LINK     EQU   R14                 LINK REGISTER
         SPACE 2
***********************************************************************
*                                                                     *
*        EXIT 250 ENTRY POINT                                         *
*                                                                     *
***********************************************************************
*
EXIT250  $ENTRY BASE=R12,CSECT=YES
         SPACE 1
         $SAVE                     SAVE CALLER'S REGISTERS
         LR    R12,R15             LOAD BASE REGISTER
         USING DCT,R4              SET ADDRESSABILITY TO DCT
         USING JCT,R10             SET ADDRESSABILITY TO JCT
*
***********************************************************************
*                                                                     *
*        TEST TO SEE IF OUTPUT IS GOING TO THE XEROX PRINTER          *
*                                                                     *
***********************************************************************
*
         TM    PCEID,PCEPUSID      IF A PUNCH DEVICE ?
         BO    E250RET0            LEAVE EXIT (ALLOW SETUP)
*
         CLC   DCTDEVN(3),=CL3'PRT'     ARE WE GOING TO A PRINTER ?
         BE    C250PRTE      * YES - GO LOOK FOR PRINTER TABLE ENTRY
         CLC   DCTDEVN(7),=CL7'PRINTER' ARE WE GOING TO A PRINTER ?
         BE    C250PRTE      * YES - GO LOOK FOR PRINTER TABLE ENTRY
         BNE   E250RET0      * NO -  LEAVE EXIT (ALLOW SETUP)
*
         USING PTBDSECT,R2   * MAKE PRINTER TABLE ENTRY ADDRESSABLE
C250PRTE L     R2,=A(PRNTRTAB) FIND PRINTER ENTRY IN PRINTER TABLE
         C     R2,=F'0'      * PRINTER TABLE ADDRESSABLE?
         BE    E250RET0      * NO - MIGHT AS WELL RETURN TO JES2
C250NEXT CLI   PTBENTRY,C'0' * DEFAULT TO PRINTER 0 IF BEYOND LAST
         BE    C250TYPE      *   PRINTER TABLE ENTRY
         CLC   PTBENTRY,DCTDEVN+3 IF CORRECT PRINTER ENTRY
         BE    C250TYPE      *   THEN GO CHECK PRINTER TYPE
         CLC   PTBENTRY,DCTDEVN+7 IF CORRECT PRINTER ENTRY
         BE    C250TYPE      *   THEN GO CHECK PRINTER TYPE
         LA    R2,12(0,R2)   * ELSE GET SET TO CHECK NEXT ENTRY
         B     C250NEXT      *
C250TYPE TM    DCTPPSW2,DCTSDSSW IF SEPDS=Y (I.E.: TAKE EXIT 15)
         BO    C250XERO      *   THEN THIS SHOULD BE A XEROX PRINTER
         MVC   PREVFORM,=CL4'????' RESET FORMS SETUP - JUST TO BE SURE
         CLI   PTBTYPE,C'X'  * IF TABLE INDICATES NOT A XEROX TABLE
         BNE   E250RET0      *   THEN LEAVE EXIT (ALLOW SETUP)
         MVI   PTBTYPE,C' '  * ELSE MAKE THIS NOT BE A XEROX PRINTER
         B     E250RET0      *   AND LEAVE EXIT (ALLOW SETUP)
C250XERO MVI   PTBTYPE,C'X'  * MAKE THIS A XEROX PRINTER
         B     E250RET4      *   AND SUPPRESS SETUP
***********************************************************************
*                                                                     *
*        RETURN TO JES2                                               *
*                                                                     *
***********************************************************************
*
E250RET0 LA    R15,0              SET RETURN CODE TO 0 - ALLOW SETUP
         $RETURN RC=(R15)         RETURN TO JES2
*
E250RET4 LA    R15,4              SET RETURN CODE TO 4 - SUPPRESS SETUP
         $RETURN RC=(R15)         RETURN TO JES2
*
         DROP  R2,R4,R10
         EJECT
***********************************************************************
*                                                                     *
*        EXIT 250 DATA AREAS                                          *
*                                                                     *
***********************************************************************
*
         LTORG
*
********************** END OF MODULE EXIT250 **************************
