*
* CODING EXAMPLE 8.4.1
*
*
READPDSQ CSECT
*
*
***********************************************************************
*    INITIALIZATION
***********************************************************************
*
         INITL 3,EQU=R             INITIALIZE PROGRAM
         USING IHADCB,R10          DEFINE REG FOR DCB DSECT
         USING INFMJFCB,R11        DEFINE REG FOR JFCB DSECT
*
*
***********************************************************************
*    MAINSTREAM OF PROGRAM
***********************************************************************
*
         BAL   R6,RDJFCB           READ JFCB INTO PROG AREA
NEXTMEMB BAL   R6,GETNAME          GET NEXT MEMBER NAME FROM PDS DIR
         BAL   R6,SETNAME          PUT MEMBER NAME INTO JFCB
         BAL   R6,OPEN             OPEN PDS, POS TO MEMBER
NEXTREC  BAL   R6,READ             READ A RECORD OF MEMBER
         BAL   R6,PROCMEMB         PROCESS RECORD
         B     NEXTREC             GET NEXT MEMBER RECORD
MEMBEND  BAL   R6,CLOSE            ALL MEMB RECS READ, CLOSE PDS
         B     NEXTMEMB            GET NEXT MEMBER NAME
MEMBENDX BAL   R6,CLOSE            ALL MEMBERS READ, CLOSE PDS
         B     RETURN              RETURN TO MVS OR CALLING PROGRAM
*
*
***********************************************************************
*    THIS ROUTINE READS THE JFCB AND COPIES IT FROM THE SWA INTO THE
*    PROGRAMMER-SUPPLIED AREA.
***********************************************************************
*
RDJFCB   RDJFCB PDSQDCB
         LTR   R15,R15             CHK IF DD STMT IN JCL
         BNZ   NODDSTMT            DD STMT MISSING
         BR    R6                  RETURN TO CALLING RTN
NODDSTMT WTO   '*** DDNAME "PDSDD" IS NOT IN JOBSTREAM ***',ROUTCDE=11
         ABEND 901
*
*
***********************************************************************
*    THIS ROUTINE USES THE READDIR MACRO TO OBTAIN THE MEMBER NAME IN
*    THE NEXT PDS DIRECTORY ENTRY. ALIAS NAMES ARE IGNORED.
***********************************************************************
*
GETNAME  READDIR PDSDD
         LTR   R15,R15             CHECK IF PRIMARY NAME
         BZR   R6                  IF YES, RET TO CALLING RET
         C     R15,BIN4            CHECK IF ALIAS
         BE    GETNAME             IF YES, IGNORE GET NEXT NAME
         B     MEMBENDX            END OF DIR, BRNCH TO END-OF-MEMB RTN
*
*
***********************************************************************
*    THIS ROUTINE MODIFIES THE JFCB. THE NAME OF THE NEXT PDS MEMBER TO
*    BE READ IS INSERTED INTO THE PDS-MEMBER-NAME FIELD OF THE JFCB.
***********************************************************************
*
SETNAME  LA    R11,JFCB            SET ADRBLTY FOR PDS DCB DSECT
         MVC   JFCBELNM,$MEMBER    MOVE CURRENT MEMB NAME INTO JFCB
         OI    JFCBIND1,JFCPDS     IND THAT MEMB NAME SPEC IN JFCB
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE OPENS THE PDS DCB USING THE INFORMATION IN THE
*    MODIFIED JFCB. THE OPEN WILL CAUSE THE NEXT GET TO READ THE FIRST
*    LOGICAL RECORD OF THE MEMBER WHOSE NAME IS SPECIFIED IN THE JFCB.
***********************************************************************
*
OPEN     OPEN  PDSQDCB,TYPE=J      TELL OPEN TO USE MODIFIED JFCB
         ...                       CHK FOR GOOD OPEN
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE USES QSAM TO READ A LOGICAL RECORD OF THE MEMBER.
***********************************************************************
*
READ     GET   PDSQDCB             GET A MEMBER RECORD
         LR    R2,R1               SAVE ADR OF RECORD IN REG 2
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE PROCESSES EACH LOGICAL RECORD OF THE MEMBER.
***********************************************************************
*
PROCMEMB ...                       PROC RECORDS OF MEMBER
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE CLOSES PDS DCB.
***********************************************************************
*
CLOSE    CLOSE PDSQDCB             CLOSE PDS
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE RESTORES THE REGISTERS AND RETURNS CONTROL.
***********************************************************************
*
RETURN   RCNTL RC=0                RETURN TO MVS OR CALLING PROG
*
*
***********************************************************************
*    DC/DS STATEMENTS
***********************************************************************
*
BIN4     DC    F'4'
*
*
***********************************************************************
*    THE JFCB-LIST AND THE DCB FOR THE PDS
***********************************************************************
*
JFCB     DS    44F
JFCBPTR  DC    X'87'
         DC    AL3(JFCB)
*
PDSQDCB  DCB   DSORG=PS,MACRF=GL,EODAD=MEMBEND,EXLST=JFCBPTR,          -
               DDNAME=PDSDD
*
*
***********************************************************************
*    THE DSECTS
***********************************************************************
*
         PRINT NOGEN
         DCBD  DSORG=PS            GENERATES PS-DCB DSECT
*
         DSECT
         IEFJFCBN                  GENERATES JFCB DSECT
*
*
***********************************************************************
*    END OF PROGRAM
***********************************************************************
*
         END
