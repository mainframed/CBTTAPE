*
* CODING EXAMPLE 15.3.1
*
*
IGC0025C CSECT
*
***********************************************************************
*    INITIALIZATION
***********************************************************************
*
         REGEQU EQU=R              GENERATE EQU'S FOR REGS
         USING *,R6                DEFINE BASE REG
         USING CVT,R3              DEFINE REG FOR CVT DSECT
         USING TCB,R4              DEFINE REG FOR TCB DSECT
         USING SRB,R10             DEFINE REG FOR SRB DSECT
         USING CSACOMAR,R11        DEFINE REG FOR CSACOMAR DSECT
         USING WORKAREA,R12        DEFINE REG FOR WORKAREA DESCT
*
*
***********************************************************************
*    MAINSTREAM OF SVC ROUTINE
***********************************************************************
*
         BAL   R8,SAVREGS1         SAVE CONTENTS OF CONTROL REGS
         BAL   R8,GETWKAR          ALLO WORKAREA FOR CALLING PROG
         BAL   R8,GETCSA           ALLO CSA FOR WKAR AND SRB'S
         BAL   R8,SAVEADRS         SAVE PROCESSING ADRS
         BAL   R8,INITSRB1         SET UP SRB CONTROL BLOCK
         BAL   R8,SCHDSRB1         SCHEDULE SRB IN TARGET A.S.
         B     RETURN1             RETURN TO CALLING PROG VIA SVC FLIH
*
*
***********************************************************************
*    THIS ROUTINE SAVES THE CONTENTS OF ALL REGISTERS WHICH CONTAIN
*    CONTROL INFORMATION.
***********************************************************************
*
SAVREGS1 LR    R2,R14              SAVE RETURN ADR
         BR    R8                  RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS ROUTINE ALLOCATES A WORKAREA FROM THIS ADDRESS SPACE FOR THE
*    CALLING PROGRAM IN THE PSW STOR PROT KEY OF THE CALLING PROGRAM.
***********************************************************************
*
GETWKAR  MODESET KEY=NZERO         SET PSW KEY TO THAT OF CALLER
         GETMAIN RU,LV=WKARLEN,SP=0    ALLOC STOR FOR CALLER
         LR    R12,R1              SET ADBLTY OF WKAR DSECT
         ST    R12,WGMADR          SAVE WKAR ADR IN WORKAREA
         MODESET KEY=ZERO          SET PSW KEY BACK TO ZERO
         BR    R8                  RETURN TO CALLING PROG
*
*
***********************************************************************
*    THIS ROUTINE ALLOCATES VIRTUAL STORAGE FROM FIXED, FETCH-PROTECTED
*    CSA FOR USE BY THE SRB ROUTINES. THIS AREA IS USED TO CONTAIN THE
*    SRB CONTROL BLOCK AND A WORKAREA. THE NAME OF THE DSECT FOR THE
*    WORKAREA SECTION OF THIS STORAGE IS CALLED CSACOMAR.
***********************************************************************
*
GETCSA   GETMAIN RU,LV=CSALEN,SP=227
         LR    R10,R1              SET ADBLTY FOR SRB
         LA    R11,SRBSIZE(0,R10)  SET ADBLTY FOR CSA WKAR
         ST    R10,CCSAADR         SAVE ADR OF ALLO CSA
         BR    R8                  RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS ROUTINE SAVES THE REQUIRED ADDRESSES FOR PROCESSING.
***********************************************************************
*
SAVEADRS ST    R4,CTCB1ADR         SAVE ADR OF TCB OF ISSUING A.S.
         ST    R7,CASCB1AD         SAVE ADR OF ASCB OF ISSUING A.S.
         ST    R12,TCBUSER         SAVE ADR OF CALLER'S WKAR IN TCB
         BR    R8                  RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS ROUTINE SETS UP THE SRB FOR THE SRB ROUTINE WHICH IS TO
*    EXECUTE IN THE TARGET ADDRESS SPACE.
***********************************************************************
*
INITSRB1 MVC   SRBID,SRB1          SET SRB ID
         MVC   SRBCPAFF,BINONES    IND SRB OK TO EXEC IN ALL CPU'S
         MVC   SRBPKF,BINZEROS     SET PSW KEY OF SRB RTN TO ZERO
         MVC   SRBASCB,CVTUSER     SET ADR OF TARGET ASCB
         LA    R5,DATASRB          LOAD EPA OF SRB ROUTINE
         ST    R5,SRBEP            SET EPA OF SRB ROUTINE
         LA    R5,DUMYRMTR         LOAD EPA OF DUMMY RECV RTN
         ST    R5,SRBRMTR          SET ADR OF RECV RTN
         ST    R11,SRBPARM         LOAD ADR OF CSA WKAR
         BR    R8                  RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS ROUTINE SCHEDULES THE SRB IN THE TARGET ADDRESS SPACE.
***********************************************************************
*
SCHDSRB1 SCHEDULE SRB=(R10),SCOPE=LOCAL
         BR    R8                  RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS ROUTINE RETURNS CONTROL TO THE CALLER VIA THE SVC FLIH.
***********************************************************************
*
RETURN1  LR    R14,R2              RESTORE RETURN ADDRESS
         LA    R15,0               SET RC=0
         LR    R1,R12              RETURN ADR OF CALLER'S WKAR
         BR    R14                 RETURN TO CALLING PROG VIA FLIH
*
*
***********************************************************************
*    DUMMY RECOVERY ROUTINE
***********************************************************************
*
DUMYRMTR BR    R14
*
*
***********************************************************************
*    CONSTANTS
***********************************************************************
*
BINZEROS DC    F'0'
BINONES  DC    4XL1'FF'
SRB1     DC    C'SRB1'           ID OF SRB WHICH EXEC IN TARGET A.S.
*
*
         LTORG                   FORCE LITERALS TO ASM HERE FOR ADRBLTY
         DROP                    RELEASE ALL REGS USED WITH USING STMTS
*
*
*
*
***********************************************************************
*    THE FOLLOWING CONTAINS THE CODE FOR THE SRB ROUTINE WHICH EXECUTES
*    IN THE TARGET ADDRESS SPACE. ITS FUNCTIONS ARE THE FOLLOWING:
*
*        * MOVE THE DATA FROM THE TARGET A.S. TO THE CSA WKAR;
*        * SCHEDULE AN SRB IN THE ORIGINATING A.S.
*
***********************************************************************
*
*
DATASRB  DS    0H
*
*
***********************************************************************
*    INITIALIZATION
***********************************************************************
*
         USING *,R6                DEFINE BASE REG
         USING SRB,R10             DEFINE REG FOR SRB DSECT
         USING CSACOMAR,R11        DEFINE REG FOR CSACOMAR DSECT
         LR    R6,R15              LOAD BASE REGISTER
*
*
***********************************************************************
*    MAINSTREAM OF SRB ROUTINE
***********************************************************************
*
         BAL   R8,SAVREGS2         SAVE CONTENTS OF CONTROL REGS
         BAL   R8,GETJNAME         GET JOBNAME OF TARGET A.S.
         BAL   R8,GETDATA          GET DATA FROM TARGET A.S.
         BAL   R8,INITSRB2         INIT SRB FOR ORIG A.S.
         BAL   R8,SCHDSRB2         SCHEDULE SRB FOR ORIG A.S.
         B     RETURN2             RETURN TO DISPATCHER
*
*
***********************************************************************
*    THIS ROUTINE SAVES THE CONTENTS OF THE CONTROL REGISTERS AND SETS
*    ADDRESSABILITY FOR THE VARIOUS DSECTS.
***********************************************************************
*
SAVEREG2 LR    R2,R14              SAVE RETURN ADDRESS
         LR    R10,R0              SET ADBLTY FOR SRB DSECT
         LR    R11,R1              SET ADBLTY FOR CSACOMAR DSECT
         BR    R8                  RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS ROUTINE EXTRACTS THE JOBNAME ASSOCIATED WITH THE TARGET
*    ADDRESS SPACE.
***********************************************************************
*
GETJNAME L     R9,SRBASCB          LD ADR OF ASCB OF TARGET A.S.
         L     R9,ASCBXTCB-ASCBEGIN(R9)     LOAD ADR OF TCB
         L     R9,TCBTIO-TCB(R9)            LOAD ADR OF TIOT
         MVC   CJOBNAME,0(R9)               MOVE JOBNAME INTO CSA WKAR
         BR    R8                           RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS ROUTINE MOVES THE DATA FROM THE TARGET ADDRESS SPACE INTO THE
*    CSA WORKAREA.
***********************************************************************
*
GETDATA  L     R9,SRBASCB          LD ADR OF ASCB OF TARGET A.S.
         L     R9,ASCBXTCB-ASCBEGIN(R9)     GET ADR OF TCB
         L     R9,TCBUSER-TCB(R9)           GET ADR OF DATA BUFFER
         MVC   CJOBDATA,0(R9)               MOVE DATA INTO CSA WKAR
         BR    R8                           RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS ROUTINE SETS UP THE SRB FOR THE SRB ROUTINE WHICH IS TO
*    EXECUTE IN THE ORIGINATING ADDRESS SPACE. THE PREVIOUS SRB IS USED
*    AND SELECTED FIELDS ARE UPDATED.
***********************************************************************
*
INITSRB2 MVC   SRBID,SRB2          SET SRB ID
         MVC   SRBASCB,CASCB1AD    SET ADR OF ORIG ASCB
         LA    R5,POSTSRB          LD EPA OF SRB ROUTINE
         ST    R5,SRBEP            SET EPA OF SRB ROUTINE
         BR    R8                  RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS ROUTINE SCHEDULES THE SRB IN THE ORIGINATING ADDRESS SPACE.
***********************************************************************
*
SCHDSRB2 SCHEDULE SRB=(R10),SCOPE=LOCAL
         BR    R8
*
*
***********************************************************************
*    THIS ROUTINE RETURNS CONTROL TO THE DISPATCHER
***********************************************************************
*
RETURN2  LR    R14,R2              RESTORE RETURN ADDRESS
         LA    R15,0               SET RC=0
         BR    R14                 RETURN CONTROL TO DISPATCHER
*
*
***********************************************************************
*    CONSTANTS
***********************************************************************
*
SRB2     DC    C'SRB2'           ID OF SRB WHICH EXEC IN ORIG A.S.
*
*
         LTORG                   FORCE LITERALS TO ASM HERE FOR ADRBLTY
         DROP                    RELEASE ALL REGS USED WITH USING STMTS
*
*
*
*
***********************************************************************
*    THE FOLLOWING CONTAINS THE CODE FOR THE SRB ROUTINE WHICH EXECUTES
*    IN THE ORIGINATING ADDRESS SPACE. ITS FUNCTIONS ARE THE FOLLOWING:
*
*        * MOVE THE DATA FROM THE CSA WKAR TO THE WKAR OF THE ORIG
*          ADDRESS SPACE;
*        * POST THE ECB TO INDICATE THAT THE DATA MOVEMENT IS COMPLETE;
*        * RELEASE THE ALLOCATED CSA VIRTUAL STORAGE.
*
***********************************************************************
*
*
POSTSRB  DS    0H
*
*
***********************************************************************
*    INITIALIZATION
***********************************************************************
*
         USING *,R6                DEFINE BASE REGISTER
         USING CVT,R3              DEFINE REG FOR CVT DSECT
         USING TCB,R4              DEFINE REG FOR TCB DSECT
         USING SRB,R10             DEFINE REG FOR SRB DSECT
         USING CSACOMAR,R11        DEFINE REG FOR CSACOMAR DSECT
         USING WORKAREA,R12        DEFINE REG FOR ORIG A.S. WKAR DSECT
         LR    R6,R15              LOAD BASE REGISTER
*
*
***********************************************************************
*    MAINSTREAM OF SRB ROUTINE
***********************************************************************
*
         BAL   R8,SAVREGS3         SAVE CONTENTS OF CONTROL REGS
         BAL   R8,LOADADRS         LOAD PROCESSING ADDRESSES
         BAL   R8,MOVEINFO         MOVE INFO FROM CSA TO ORIG A.S.
         BAL   R8,SETLLOCK         OBTAIN LOCAL LOCK FOR POST MACRO
         BAL   R8,POSTAS1          POST ECB TO IND INFO MOVED
         BAL   R8,RELLLOCK         RELEASE LOCAL LOCK
         BAL   R8,FREECSA          RELEASE ALLO CSA STORAGE
         B     RETURN3             RETURN TO DISPATCHER
*
*
***********************************************************************
*    THIS ROUTINE SAVES THE CONTENTS OF THE CONTROL REGISTERS AND SETS
*    ADDRESSABILITY FOR THE VARIOUS DSECTS.
***********************************************************************
*
SAVREGS3 LR    R2,R14              SAVE RETURN ADDRESS
         LR    R10,R0              SET ADBLTY FOR SRB DSECT
         LR    R11,R1              SET ADBLTY FOR CSACOMAR DSECT
         BR    R8                  RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS ROUTINE LOADS THE REQUIRED ADDRESSES FOR PROCESSING.
***********************************************************************
*
LOADADRS L     R4,CTCB1ADR         SET ADBLTY FOR TCB DSECT
         L     R12,TCBUSER         LD ADR FOR WKAR OF ORIG A.S.
         BR    R8                  RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS ROUTINE MOVES THE JOBNAME AND DATA OF THE TARGET A.S. FROM
*    THE CSA TO THE WORKAREA IN THE ORIGINATING A.S.
***********************************************************************
*
MOVEINFO MVC   WJOBNAME,CJOBNAME       MOVE JOBNAME FROM CSA TO ORIG
*                                      A.S.
         MVC   WJOBDATA,CJOBDATA       MOVE DATA FROM CSA TO ORIG A.S.
         BR    R8                      RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS ROUTINE OBTAINS THE LOCAL LOCK. THIS IS REQUIRED WHEN A
*    BRANCH-ENTRY POST MACRO IS ISSUED.
***********************************************************************
*
SETLLOCK SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,REGS=USE,               -
               RELATED=(POSTAS1,RELLLOCK)
         BR    R8                  RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS ROUTINE POSTS THE ECB LOCATED IN THE WKAR OF THE ORIGINATING
*    A.S. TO INDICATE THAT THE DATA MOVEMENT FROM THE TARGET A.S. TO
*    THE WORKAREA IN THE ORIGINATING A.S. VIA THE CSA IS COMPLETE.
***********************************************************************
*
POSTAS1  LA    R5,WECB             LOAD ADR OF ECB
         POST  (R5),X'00',BRANCH=YES,RELATED=(WAIT-IN-ASCB1)
         BR    R8                  RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS ROUTINE RELEASES THE LOCAL LOCK.
***********************************************************************
*
RELLLOCK SETLOCK RELEASE,TYPE=LOCAL,REGS=USE,                          -
               RELATED=(POSTAS1,SETLLOCK)
         BR    R8                  RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS ROUTINE RELEASES THE CSA STORAGE ALLOCATED BY THE SVC ROUTINE
*    INVOKED BY THE ORIGINATING A.S.
***********************************************************************
*
FREECSA  LA    R10,CCSAADR         LD ADR OF CSA POINTER
         FREEMAIN RU,LV=CSALEN,SP=227,A=(R10)
*        L     R3,16               LOAD ADR OF CVT
*        XC    CVTUSER,CVTUSER     CLEAR CVTUSER TO IND DATA COPIED
         BR    R8                  RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS ROUTINE RETURNS CONTROL TO THE DISPATCHER.
***********************************************************************
*
RETURN3  LR    R14,R2              RESTORE RETURN ADDRESS
         LA    R15,0               SET RC=0
         BR    R14                 RETURN CONTROL TO DISPATCHER
*
*
***********************************************************************
*    THE DSECTS
***********************************************************************
*
WORKAREA DSECT
WGMADR   DS    F
WECB     DS    F
WJOBNAME DS    CL8
WJOBDATA DS    CL100
WKARLEN  EQU   *-WORKAREA
*
CSACOMAR DSECT
CCSAADR  DS    F
CTCB1ADR DS    F
CASCB1AD DS    F
CJOBNAME DS    CL8
CJOBDATA DS    CL100
COMARLEN EQU   *-CSACOMAR
*
CSALEN   EQU   COMARLEN+SRBSIZE
*
         PRINT NOGEN
         IHAASCB                  GENERATES ASCB DSECT
         IHASRB                   GENERATES SRB DSECT
         CVT   DSECT=YES          GENERATES CVT DSECT
         IHAPSA                   GENERATES PSA DSECT (REQD BY SETLOCK)
         IKJTCB                   GENERATES TCB DSECT
*
*
***********************************************************************
*    END OF SVC ROUTINE
***********************************************************************
*
         END
