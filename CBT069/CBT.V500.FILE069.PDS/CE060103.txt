*
* CODING EXAMPLE 6.1.3
*
*
ESPIE01  CSECT
*
*
***********************************************************************
*    INITIALIZATION
***********************************************************************
*
         INITL 3,EQU=R             INITIALIZE PROGRAM
         USING EPIE,R11            DEFINE REG FOR EPIE DSECT
*
*
***********************************************************************
*    MAINSTREAM OF PROGRAM
***********************************************************************
*
         ...
         BAL   R6,SETESPIE         ESTAB ESPIE ENVIR
NEXTREC  BAL   R6,READMSTR         READ A REC FROM MASTR FILE
         BAL   R6,PROCESS          PROCESS RECORD
         B     NEXTREC             READ NEXT RECORD
         ...
*
*
***********************************************************************
*    THIS ROUTINE ESTABLISHES THE ESPIE RECOVERY ENVIRONMENT. IT
*    SPECIFIES THAT THE FOLLOWING INTERRUPTION TYPES ARE TO BE
*    INTERCEPTED:
*        * DATA EXCEPTION (7);
*        * DECIMAL OVERFLOW (10);
*        * DECIMAL DIVIDE (11).
*    WHEN ONE OF THE SPECIFIED INTERRUPTION TYPES IS ENCOUNTERED,
*    CONTRO IS PASSED TO THE RECOVERY ROUTINE DATACHK WHICH PROCESSES
*    THE ABEND. THE RETRY ADDRESS IS PASSED TO DATACHK VIA THE PARAM
*    PARAMETER.
***********************************************************************
*
SETESPIE ESPIE SET,DATACHK,(7,10,11),PARAM=RETRYADR
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE RECEIVES CONTROL VIA THE ESPIE SERVICE ROUTINE WHEN
*    THE PROGRAM ENCOUNTERS A S0C7, S0CA OR S0CB ABEND. WHEN THE ABEND
*    PROCESSING IS COMPLETE, DATACHK RETURNS CONTROL THE MAIN PROGRAM
*    AT THE ADDRESS SPECIFIED VIA THE PARAM PARAMETER PASSED BY ESPIE.
***********************************************************************
*
DATACHK  ST    R14,SAVR14          SAVE RETURN ADR TO ESPIE
         LR    R11,R1              SET ADRBLTY FOR EPIE DSECT
         L     R10,EPIEPARM        LOAD ADR OF PARM-LIST
         L     R10,0(0,R10)        LOAD RETRY ADR TO MAIN PROG
         CLI   EPIEINT+3,X'07'     CHK IF 07 INTERRUPTION TYPE
         BE    INCR0C7             IF YES, INCR 0C7 ERROR CTR
         AP    ERR2CTR,ONE         IF NO, INCR 0CA/0CB ERROR CTR
         B     DODUMP              BYPASS THE INCR OF 0C7 ERR CTR
INCR0C7  AP    ERR1CTR,ONE         INCR 0C7 ERROR CTR
DODUMP   BAL   R7,DUMP             PRODUCE HEX DUMP OF REC
         ST    R10,EPIEPSW+4       PUT RETRY RTN ADR INTO EPIE
         L     R14,SAVR14          RESTORE REG 14
         BR    R14                 PASS CNTRL TO NEXTREC RTN VIA ESPIE
*
*
***********************************************************************
*    THE SUBROUTINE PRODUCES A HEX DUMP OF EACH RECORD WHICH CONATINS
*    BAD DATA.
***********************************************************************
*
DUMP     ...                       DO REQUIRED DUMP
         BR    R7                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    DC/DS STATEMENTS
***********************************************************************
*
         ...
ONE      DC    P'1'
ERR1CTR  DC    PL3'0'              ERROR CTR FOR S0C7 ABENDS
ERR2CTR  DC    PL3'0'              ERROR CTR FOR S0CA/S0CB ABENDS
RETRYADR DC    A(NEXTREC)
         ...
*
*
***********************************************************************
*    THE DSECTS
***********************************************************************
*
         IHAEPIE                   GENERATES EPIE DSECT
*
*
***********************************************************************
*    END OF PROGRAM
***********************************************************************
*
         END
