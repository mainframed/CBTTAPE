*
* CODING EXAMPLE 4.15.3
*
*
         BAL   R6,SETTIMER         SET PROGRAM TIMER
CONTIN   BAL   R6,PROCESS          PERFORM SOME PROCESSING
         BAL   R6,CHKTIME          CHK IF A TIME INTV HAS ELAPSED
         B     CONTIN              PERFORM SOME MORE PROCESSING
         ...
SETTIMER STIMER REAL,DINTVL=TIMEINTV    *** NOTICE, NO EXIT ADR ***
         LA    R10,TIMETBL         LOAD BG ADR OF TIME TBL
         ST    R10,TMTBLPTR        STORE ADR OF 1ST ENTRY
         BR    R6                  RETURN TO CALLING RTN
*
PROCESS  ...
         BR    R6                  RETURN TO CALLING RTN
*
CHKTIME  CLI   TMENDSW,C'1'        CHK IF ALL TIME INTV PROCESSED
         BER   R6                  IF YES, RET TO CALLING RTN
         TTIMER ,MIC,TIMELEFT      IF NO, GET TIME LEFT
         BAL   R7,CVTOSECS         CONVERT MICROSECS INTO PACKED SECS
         L     R10,TMTBLPTR        LOAD CURRENT TIME TBL ENTRY ADR
         CP    SECSLEFT,0(8,R10)   CHK IF CURRENT TIME INTV ELAPSED
         BNH   DOACTION            IF YES, PERFORM ACTION
         BR    R6                  IF NO, RET TO CALLING RTN
DOACTION BAL   R7,PERFORM          PERFORM TIME DEPENDENT ACTION
         CP    0(8,R10),PACKZERO   CHK IF LAST TIME INTERVAL
         BE    ALLDONE             IF YES, INDICATE IT
         LA    R10,8(0,R10)        IF NO, INCR TO NXT TIME TBL ENT ADR
         ST    R10,TMTBLPTR        STORE NEW TIME TBL ENTRY ADR
         BR    R6                  RETURN TO CALLING RTN
ALLDONE  MVI   TMENDSW,C'1'        INDICATE ALL TIME INTV PROCESSED
         BR    R6                  RETURN TO CALLING RTN
*
CVTOSECS ...                       *** SEE C.E. 4.15.1 ***
         BR    R7                  RETURN TO CALLING RTN
*
PERFORM  ...                       PERFORM ACTION FOR EACH INTERVAL
         BR    R7                  RETURN TO CALLING RTN
         ...
*
TMENDSW  DC    C'0'
TIMEINTV DC    C'01000000'         1 HOUR TIME INTERVAL FOR STIMER
TIMETBL  DC    PL8'3000'           10 MINUTES ELAPSED (3000 SECS LEFT)
         DC    PL8'900'            45 MINUTES ELAPSED (900 SECS LEFT)
         DC    PL8'0'              1 HOUR ELAPSED (O SECS LEFT)
TMTBLPTR DS    F
TIMELEFT DS    D
SECSLEFT DS    D                   PACKED SECONDS FROM CVTOSECS RTN
PACKZERO DC    P'0'
