*
* CODING EXAMPLE 13.4.2
*
*
ALLOCSA2 CSECT
*
*
***********************************************************************
*    INITIALIZATION
***********************************************************************
         INITL 5,EQU=R             INITIALIZE PROGRAM
         USING R10,CVT             DEFINE REG FOR CVT DSECT
*
*
***********************************************************************
*    MAINSTREAM OF PROGRAM
***********************************************************************
*
         BAL   R6,SETMODE          SET PROG INTO KEY ZERO, SUPV STATE
         BAL   R6,SETLOCK          OBTAIN LOCAL LOCK
         BAL   R6,GETCSA           ALLOC STORAGE FROM CSA
         BAL   R6,RELLOCK          RELEASE LOCAL LOCK
         BAL   R6,SETCVT           PUT CSA STRG ADR INTO CVT USER FLD
         B     RETURN              RETURN TO MVS OR CALLING PROG
*
*
***********************************************************************
*    THIS ROUTINE PUTS THE TASK INTO SUPERVISOR STATE AND PSW
*    PROTECTION KEY ZERO VIA THE MODESET MACRO. THIS IS REQUIRED TO
*    MOVE THE ADDRESS OF THE ALLOCATED CSA INTO THE CVT USER-FIELD.
***********************************************************************
*
SETMODE  MODESET MODE=SUP,KEY=ZERO
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE ISSUES THE SETLOCK MACRO TO OBTAIN THE LOCAL LOCK
*    WHICH IS REQUIRED FOR A BRANCH-ENTRY GETMAIN MACRO.
***********************************************************************
*
SETLOCK  SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,REGS=USE,               -
               RELATED=(GETMAIN,CSA,RELLOCK)
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE ALLOCATES VIRTUAL STORAGE FROM THE CSA AND ASSIGNS
*    PSW PROTECTION KEY=8 (NORMAL V=V JOBS) TO IT.
***********************************************************************
*
GETCSA   GETMAIN RC,LV=4096,SP=231,BNDRY=PAGE,BRANCH=YES               -
               KEY=8
         LTR   R15,R15             CHECK FOR SUCCESSFUL GETMAIN
         BNZ   NOCSA               IF NON-ZERO, CSA STRG NOT AVAIL
         ST    R1,R11              SAVE ADR OF CSA AREA IN REG 11
         BR    R6                  RETURN TO CALLING RTN
NOCSA    LA    R15,4               SET UNSUCCESSFUL RC INDICATION
         B     RETURNX             RETURN TO MVS WITH BAD RC
*
*
***********************************************************************
*    THIS ROUTINE RELEASES THE LOCAL LOCK.
***********************************************************************
*
RELLOCK  SETLOCK RELEASE,TYPE=LOCAL,REGS=USE,                          -
               RELATED=(GETMAIN,CSA,SETLOCK)
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE PLACES THE ADDRESS OF THE ALLOCATED CSA INTO THE
*    CVT USER-FIELD.
***********************************************************************
*
SETCVT   L     R10,16              LOAD REG 10 WITH CVT ADR
         ST    R11,CVTUSER         STORE CSA STRG ADR INTO CVT USER FLD
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE RESTORES THE REGISTERS AND RETURNS CONTROL.
***********************************************************************
*
RETURN   LA    R15,0               SET RC=0
RETURNX  L     R13,4(0,R13)        RESTORE PREV SA ADR
         L     R14,12(0,R13)       RESTORE REG 14
         LM    R0,R12,20(R13)      RESTORE REGS 0-12, R15 HAS RC
         BR    R14                 RETURN TO MVS OR CALLING PROG


*
*
***********************************************************************
*    THE DSECTS
***********************************************************************
*
         PRINT NOGEN
         CVT   DSECT=YES           GENERATES CVT DSECT
         IHAPSA                    GENERATES PSA DSECT
*                                  REQUIRED FOR SETLOCK MACRO
*
***********************************************************************
*    END OF PROGRAM
***********************************************************************
*
         END
