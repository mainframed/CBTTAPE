*
* CODING EXAMPLE 5.10.1
*
*
         USING ENTRYMAP,R11        DEFINE REG FOR DSECT
*
PUTENTRY L     R10,LLENTONE        LOAD ADR OF FIRST LOG ENTRY OF LL
         L     R12,LLENTNXT        LOAD ADR OF NEXT AVAIL SLOT OF LL
         LA    R11,NEWENTRY        LOAD ADR OF NEW ENTRY
NXTENTRY CLC   4(ENTKYLEN,R10),ENTRYKEY   COMPARE KEY OF A LL ENTRY
*                                         TO KEY OF NEW ENTRY
         BH    INSERT              IF HIGH, INSERT NEW ENTRY
         BE    DUPENTRY            IF EQUAL, ENTRY ALREADY EXISTS
         ST    R10,PRVENTRY        STORE ADR OF CURRENT ENTRY
         L     R10,0(0,R10)        LOAD ADR OF NEXT ENTRY IN SEQ
         B     NXTENTRY            COMPARE NEXT ENTRY KEY TO NEW KEY
*
INSERT   L     R10,PRVENTRY        LOAD ADR OF ENTRY BEFORE NEW ENTRY
         MVC   0(4,R12),0(R10)     MOVE NEXT ENTRY PTR TO NEW ENTRY
*                                  LINK FIELD
         MVC   4(ENTRYLEN,R12),ENTRY      MOVE NEW ENTRY INTO NEXT
*                                         SLOT+4 (PASSED LINK FIELD)
         ST    R12,0(0,R10)        MOVE ADR OF NEW ENTRY INTO LINK
*                                  FIELD OF PREV ENTRY
         LA    R12,LLENTLEN(0,R12) CALC ADR OF NEW NEXT AVAIL SLOT
         ST    R12,LLENTNXT        SAVE ADR OF NEXT AVAIL SLOT
         BR    R6                  RETURN TO CALLING RTN
*
LLENTONE DS    F
LLENTNXT DS    F
PRVENTRY DS    F
NEWENTRY DS    CL6
*
ENTRYMAP DSECT
ENTRY    EQU   *
ENTKEY   DS    CL2
ENTKYLEN EQU   *-ENTKEY
ENTFLD01 DS    CL4
ENTRYLEN EQU   *-ENTRYMAP
LLENTLEN EQU   ENTRYLEN+4
