*
* CODING EXAMPLE 7.1.4
*
*
READVTOC CSECT
*
*
***********************************************************************
*    INITIALIZATION
***********************************************************************
*
         INITL 3,EQU=R             INITIALIZE PROGRAM
         USING INFMJFCB,R10        DEFINE REG FOR JFCB DSECT
         USING IECSDSF1,R11        DEFINE REG FOR DSCB-1 DSECT
*
*
***********************************************************************
*    MAINSTREAM OF PROGRAM
***********************************************************************
*
         BAL   R6,RDJFCB           READ JFCB INTO PROG AREA
         BAL   R6,MDFYJFCB         MODIFY JFCB WITH VTOC NAME
         BAL   R6,OPEN             OPEN VTOC
NEXTDSCB BAL   R6,READVTOC         READ A DSCB
         BAL   R6,PROCESS          PROCESS FORMAT-1 DSCBS
         B     NEXTDSCB            READ NEXT DSCB
DSCBEND  BAL   R6,CLOSE            CLOSE VTOC
         B     RETURN              RETURN TO MVS OR CALLING PROG
*
*
***********************************************************************
*    THIS ROUTINE READS THE JFCB AND COPIES IT FROM THE SWA INTO THE
*    PROGRAMMER-SUPPLIED AREA.
***********************************************************************
*
RDJFCB   RDJFCB VTOCDCB
         LTR   R15,R15             CHK IF DD STMT IN JCL
         BNZ   NODD                IF RC NOT 0, DD STMT MISSING
         BR    R6                  RETURN TO CALLING RTN
NODD     WTO   '*** VTOCDD NOT SPECIFIED IN JCL ***'
         ABEND 901
*
*
***********************************************************************
*    THIS ROUTINE MODIFIES THE JFCB. THE DATASET NAME OF THE VTOC IS
*    PUT INTO THE DATASET-NAME FIELD OF THE JFCB.
************************************************************************
*
MDFYJFCB LA    R10,JFCB            SET ADRBLTY FOR JFCB DSECT
         MVC   JFCBDSNM,VTOCNAME   PUT VTOC LABEL INTO JFCB DSNAME
*                                  FIELD
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE OPENS THE VTOC BY USING THE MODIFIED JFCB SUPPLIED
*    BY THE PROGRAMMER.
***********************************************************************
*
OPEN     OPEN  VTOCDCB,TYPE=J
         ...                       CHECK FOR GOOD OPEN
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE READS DSCBS FROM THE VTOC AND CHECKS THEIR TYPE. ONLY
*    FORMAT-1 DSCBS ARE RETURNED TO THE CALLING ROUTINE.
***********************************************************************
*
READVTOC LA    R11,DSCB            SET ADRBLTY FOR DSCB-1 DSECT
         READ  VTOCDECB,SF,VTOCDCB,(R11)  READ A DSCB
         CHECK VTOCDECB                   CHECK FOR GOOD READ
         CLI   DS1FMTID,C'1'       CHECK IF FORMAT-1 DSCB
         BNE   READVTOC            IF NO, READ ANOTHER DSCB
         BR    R6                  IF YES, RET TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE PROCESS THE FORMAT-1 DSCBS.
***********************************************************************
*
PROCESS  DS    0H
         ...                       PROCESS FORMAT-1 DSCB
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE CLOSES THE VTOC DCB.
***********************************************************************
*
CLOSE    CLOSE VTOCDCB
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE RESTORES THE REGISTERS AND RETURNS CONTROL.
***********************************************************************
*
RETURN   RCNTL RC=0                RETURN TO MVS OR CALLING PROG
*
*
***********************************************************************
*    DC/DS STATEMENTS
***********************************************************************
*
VTOCNAME DC    44XL1'04'           THE LABEL OF THE VTOC
DSCB     DS    CL140
*
*
***********************************************************************
*    THE JFCB-LIST AND THE DCB
***********************************************************************
*
JFCB     DS    44F
JFCBPTR  DC    X'87'
         DC    AL3(JFCB)
*
VTOCDCB  DCB   DSORG=PS,MACRF=R,EXLST=JFCBPTR,EODAD=DSCBEND,           -
               KEYLEN=44,BLKSIZE=96,LRECL=96,RECFM=F,DDNAME=VTOCDD
*
*
***********************************************************************
*    THE DSECTS
***********************************************************************
*
         DSECT
         IEFJFCBN        GENERATES DSECT FOR JFCB
*
         DSECT
         IECSDSL1 1      GENERATES DSECT FOR FORMAT-1 DSCB
*
*
***********************************************************************
*    END OF PROGRAM
***********************************************************************
*
         END
