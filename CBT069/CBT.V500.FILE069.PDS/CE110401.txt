*
* CODING EXAMPLE 11.4.1
*
*
READMTV  CSECT
*
*
***********************************************************************
*    INITIALIZATION
***********************************************************************
*
         INITL 3,EQU=R             INITIALIZE PROGRAM
         USING IOBSTDRD,R12        DEFINE REG FOR IOB DSECT
*
*
***********************************************************************
*    MAINSTREAM OF PROGRAM
***********************************************************************
*
         BAL   R6,SETDSCTS         LOAD DSECT REGISTER
         BAL   R6,SETIOB           SET REQUIRED IOB FIELDS
         BAL   R6,OPEN             OPEN DCBS
NEXTREC  BAL   R6,READREC          READ A PHYSICAL RECORD
         BAL   R6,PRINTREC         PRINT THE RECORD
         B     NEXTREC             READ NEXT RECORD
ENDDATA  BAL   R6,REWIND           EOD, REWIND TAPE
         BAL   R6,CLOSE            CLOSE DCBS
         B     RETURN              RETURN TO MVS OR CALLING PROG
*
*
***********************************************************************
*    THIS ROUTINE LOADS THE REGISTER FOR THE IOB DSECT.
***********************************************************************
*
SETDSCTS LA    R12,READIOB         LOAD IOB ADR FOR DSECT ADRBLTY
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE SETS THE REQUIRED FIELDS OF THE IOB
***********************************************************************
*
SETIOB   MVI   IOBFLAG1,X'42'      IND CCW-CHAINING, NOT RELATED
         MVC   IOBSTART,CCWADR     LOAD BG ADR OF CCW LIST
         MVC   IOBDCBPT,DCBADR     LOAD DCB ADR
         MVC   IOBECBPT,ECBADR     LOAD ECB ADR
         MVC   IOBINCAM,BINONE     READ FORWARD ONE BLOCK AT A TIME
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE OPENS ALL THE DCBS.
***********************************************************************
*
OPEN     OPEN  (READDCB,,SYSPRINT,(OUTPUT))
         ...                       CHK FOR GOOD OPEN
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE READS THE NEXT PHYSICAL RECORD FROM THE TAPE DATASET.
*    IN ORDER TO PERFORM THAT TASK, THIS ROUTINE BRANCHES AND LINKS TO
*    THE SUBROUTINES WHICH PERFORM THE FOLLOWING FUNCTIONS:
*      * ISSUE THE EXCP MACRO TO READ THE NEXT PHYSICAL RECORD OF THE
*        TAPE DATASET.
*      * CALCULATE THE LENGTH OF THE RECORD READ.
***********************************************************************
*
READREC  BAL   R7,DOEXCP           ISSUE EXCP TO READ RECORD
         BAL   R7,GETLEN           CALCULATE LENGTH OF RECORD READ
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS SUBROUTINE ISSUES THE EXCP MACRO TO READ A PHYSICAL RECORD OF
*    THE TAPE DATASET, CHECKS THE ECB COMPLETION CODE, AND PERFORMS THE
*    APPROPRIATE ACTION INDICATED BY THE COMPLETION CODE.
***********************************************************************
*
DOEXCP   XC    READECB,READECB     CLEAR ECB FOR EXCP MACRO
         EXCP  READIOB             ISSUE EXCP
         WAIT  1,ECB=READECB       WAIT FOR I/O COMPLETION
         TM    READECB,X'7F'       CHECK FOR GOOD I/O
         BNO   CHKECB              IF NO, EXAMINE ECB
         BR    R7                  IF I/O GOOD, RET TO CALLING RTN
CHKECB   TM    READECB,X'41'       CHK FOR EXCEPTION COND
         BO    CHKERR41            IF YES, CHK FOR END-OF-DATA
         B     EXCPERR             IF NO, I/O ERROR, DUMP DIAG INFO
CHKERR41 TM    IOBUSTAT,X'01'      CHK FOR TAPE MARK (END-OF-DATA)
         BO    ENDDATA             IF YES, GO TO END-OF-DATA RTN
         B     EXCPERR             IF NO, I/O ERROR, DUMP DIAG INFO
*
*
***********************************************************************
*    THIS SUBROUTINE CALCULATES THE LENGTH OF THE RECORD READ BY
*    SUBTRACTING THE RESIDUAL COUNT FROM THE MAXIMUM RECORD LENGTH.
***********************************************************************
*
GETLEN   LH    R10,IOBCSWCT        LOAD RESIDUAL COUNT FROM CSW
         LA    R11,MXRECLEN        LOAD MAXIMUM RECORD LENGTH
         SR    R11,R10             SUBTRACT RES CT FROM MAX LEN
         ST    R11,RECLEN          SAVE ACTUAL NUMB OF BYTES READ
         BR    R7                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE PRINTS THE VARIABLE-LENGTH RECORD READ FROM THE TAPE
*    DATASET.
***********************************************************************
*
PRINTREC L     R10,RECLEN          LOAD REC LEN
         BCTR  R10,0               DECR REC LEN FOR EX INSTR
         EX    R10,EXMVC           MOVE RECORD INTO PRINT LINE
         PUT   SYSPRINT,LINE       PRINT RECORD
         MVC   LINE,LINE-1         CLEAR PRINT LINE
         BR    R6                  RETURN TO CALLING RTN
EXMVC    MVC   LINE+1(0),RECORD    MVC FOR EX INSTR
*
*
***********************************************************************
*    THIS ROUTINE RECEIVES CONTROL IF THE ATTEMPTED I/O IS
*    UNSUCCESSFUL. THIS ROUTINE LOAD VARIOUS DIAGNOSTIC INFORMATION
*    INTO REGISTERS 8 AND 9 AND THEN CAUSES AN ABEND WITH A DUMP.
***********************************************************************
*
EXCPERR  L     R8,READECB            LOAD CONTENTS OF ECB INTO A REG
         MVC   ERRINFO+1(1),IOSUSTAT      UNIT STATUS FIELD OF CSW
         MVC   ERRINFO+2(1),IOBSENS0      BYTE-1 OF HARDWARE SENSE CODE
         MVC   ERRINFO+3(1),IOBSENS1      BYTE-2 OF HARDWARE SENSE CODE
         L     R9,ERRINFO            LOAD ERROR INFO WORD INTO A REG
         L     R10,ERRID             LOAD ID TO FIND REGS IN DUMP
         ABEND 900,DUMP              ISSUE ABEND MACRO WITH A DUMP
*
*
***********************************************************************
*    THIS ROUTINE CAUSES THE REWINDING OF THE TAPE VOLUME.
***********************************************************************
*
REWIND   MVC   IOBSTART,CCWADR2
         BAL   R7,DOEXCP
         BR    R6
*
*
***********************************************************************
*    THIS ROUTINE CLOSES ALL THE DCBS.
***********************************************************************
*
CLOSE    CLOSE (READDCB,,SYSPRINT)
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE RESTORES THE REGISTERS AND RETURNS CONTROL.
***********************************************************************
*
RETURN   RCNTL RC=0
*
*
***********************************************************************
*    THE CCW, IOB AND ECB
***********************************************************************
*
READCCW  CCW   X'02',RECORD,X'20',MXRECLEN    READ CCW
REWDCCW  CCW   X'07',0,X'00',1                REWIND CCW
*
READIOB  DC    10F'0'              AREA FOR IOB
READECB  DC    F'0'                FULLWORD FOR ECB
*
*
***********************************************************************
*    OTHER DC/DS STATEMENTS
***********************************************************************
*
MXRECLEN EQU   120
BINONE   DC    H'1'
RECLEN   DS    F
RECORD   DS    CL(MXRECLEN)
         DC    C' '
LINE     DC    CL133' '
*
CCWADR   DC    A(READCCW)
CCWADR2  DC    A(REWDCCW)
DCBADR   DC    A(READDCB)
ECBADR   DC    A(READECB)
*
ERRINFO  DC    F'0'
ERRID    DC    4XL1'FF'
*
*
***********************************************************************
*    THE DCBS
***********************************************************************
*
READDCB  DCB   MACRF=E,DDNAME=TESTFILE
SYSPRINT DCB   DSORG=PS,MACRF=PM,BLKSIZE=1330,LRECL=133,RECFM=FB,      -
               DDNAME=SYSPRINT
*
*
***********************************************************************
*    THE DSECTS
***********************************************************************
*

         PRINT NOGEN
         IEZIOB                GENERATES IOB DSECT
         ORG   IOBCSTAT+1      POS TO RESIDUAL COUNT FIELD OF CSW
IOBCSWCT DS    CL2             PROVIDE LABEL FOR RES CT FIELD
*
*
***********************************************************************
*    END OF PROGRAM
***********************************************************************
*
*
         END
