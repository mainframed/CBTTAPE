*
* CODING EXAMPLE 6.1.1
*
*
SPIE01   CSECT
*
*
***********************************************************************
*    INITIALIZATION
***********************************************************************
*
         INITL 3,EQU=R             INITIALIZE PROGRAM
         USING PIE,R11             DEFINE REG FOR PIE DSECT
*
*
***********************************************************************
*    MAINSTREAM OF PROGRAM
***********************************************************************
*
         ...
         BAL   R6,SETSPIE          ESTAB SPIE ENVIR
NEXTREC  BAL   R6,READMSTR         READ A REC FROM MASTR FILE
         BAL   R6,PROCESS          PROCESS RECORD
         B     NEXTREC             READ NEXT REC
         ...
*
*
***********************************************************************
*    THIS ROUTINE ESTABLISHES THE SPIE RECOVERY ENVIRONMENT. IT
*    SPECIFIES THAT THE FOLLOWING INTERRUPTION TYPES ARE TO BE
*    INTERCEPTED:
*        * DATA EXCEPTION (7);
*        * DECIMAL OVERFLOW (10);
*        * DECIMAL DIVIDE (11).
*    WHEN ONE OF THE SPECIFIED INTERRUPTION TYPES IS ENCOUNTERED,
*    CONTROL IS PASSED TO THE RECOVERY ROUTINE DATACHK WHICH PROCESSES
*    THE ABEND.
***********************************************************************
*
SETSPIE  SPIE  DATACHK,(7,10,11)
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE RECEIVES CONTROL VIA THE SPIE SERVICE ROUTINE WHEN
*    THE PROGRAM ENCOUNTERS AN S0C7, S0CA OR S0CB ABEND. WHEN THE ABEND
*    PROCESSING IS COMPLETE, DATACHK RETURNS CONTROL TO THE NEXTREC
*    ROUTINE OF THE MAIN PROGRAM.
***********************************************************************
*
DATACHK  ST    R14,SAVR14          SAVE RETURN ADR TO SPIE
         LR    R11,R1              SET ADRBLTY FOR PIE DSECT
         CLI   PIEPSW+3,X'07'      CHK IF 07 INTERRUPTION TYPE
         BE    INCR0C7             IF YES, INCR 0C7 ERROR CTR
         B     INCR0CAB            IF NO, INCR 0CA/OCB ERROR CTR
INCR0C7  AP    ERR1CTR,ONE         INCR 0C7 ERROR CTR
         B     DODUMP              DUMP RECORD
INCR0CAB AP    ERR2CTR,ONE         INCR 0CA/0CB ERROR CTR
DODUMP   BAL   R7,DUMP             PRODUCE HEX DUMP OF RECORD
         L     R10,=A(NEXTREC)     LOAD RETRY ADR TO MAIN PROG
         ST    R10,PIEPSW+4        PUT RETRY ADR INTO PIE
         L     R14,SAVR14          RESTORE REG 14
         BR    R14                 PASS CNTRL TO NEXTREC RTN VIA SPIE
*
*
***********************************************************************
*    THE SUBROUTINE PRODUCES A HEX DUMP OF EACH RECORD WHICH CONATINS
*    BAD DATA.
***********************************************************************
*
DUMP     ...                       DO REQUIRED DUMP
         BR    R7                  RETURN TO CALLING RTN
         ...
*
*
***********************************************************************
*    DC/DS STATEMENTS
***********************************************************************
*
SAVR14   DS    F
ONE      DC    P'1'
ERR1CTR  DC    PL3'0'              ERROR CTR FOR S0C7 ABENDS
ERR2CTR  DC    PL3'0'              ERROR CTR FOR S0CA/S0CB ABENDS
         ...
*
*
***********************************************************************
*    THE DSECTS
***********************************************************************
*
         IHAPIE                    GENERATES PIE DSECT
*
*
***********************************************************************
*    END OF PROGRAM
***********************************************************************
*
         END
