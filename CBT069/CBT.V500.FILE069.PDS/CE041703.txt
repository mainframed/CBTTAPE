*
* CODING EXAMPLE 4.17.3
*
*
         MACRO
&LABEL   SNAPA &BGADR,&ENDADR,&DATA=O,&SNAP=O
         GBLA  &SNAPA
         GBLC  &SNAPA1
         LCLA  &SUB,&OPT,&JLPA,&CB,&SPLS,&NOADRSW
         LCLC  &LBL
&SNAPA   SETA  &SNAPA+1
&LBL     SETC  '$'.'SNP'.'&SYSNDX'(2,3)
         AIF   (&SNAPA GT 1).B7
&SNAPA1  SETC  '&LBL'
.B7      ANOP
&LABEL   STM   14,1,&SNAPA1
         AIF   (&SNAPA GT 1).B1
         B     &LBL.A
SNAPDCB  DCB   DSORG=PS,RECFM=VBA,MACRF=W,BLKSIZE=882,LRECL=125,       -
               DDNAME=SNAPDUMP
&SNAPA1  DS    4F
&LBL.A   EQU   *
         OPEN  (SNAPDCB,(OUTPUT))
.B1      AIF   ('&SNAP' EQ 'NO').ENDX
         AIF   (T'&BGADR EQ 'O').NOADR
         AIF   (T'&ENDADR EQ 'O').NOADR
         AIF   ('&BGADR'(1,1) EQ '(').BGREG
         LA    14,&BGADR
.B6      AIF   ('&ENDADR'(1,1) EQ '(').ENDREG
         LA    15,&ENDADR
         AGO   .B2
.NOADR   ANOP
&NOADRSW SETA  1
.B2      ANOP
         AIF   ('&DATA' EQ 'O').SNAP0
&SUB     SETA  N'&DATA+1
.B3      ANOP
&SUB     SETA  &SUB-1
         AIF   (&SUB EQ 0).NOMORE
         AIF   ('&DATA(&SUB)' EQ 'ALL').SNAP1
         AIF   ('&DATA(&SUB)' EQ 'JLPA').JLPA
         AIF   ('&DATA(&SUB)' EQ 'CB').CB
         AIF   ('&DATA(&SUB)' EQ 'SPLS').SPLS
         MNOTE 'INVALID CODE SPECIFIED---CODE IGNORED'
         AGO   .B3
.JLPA    ANOP
&JLPA    SETA  1
         AGO   .B3
.CB      ANOP
&CB      SETA  1
         AGO   .B3
.SPLS    ANOP
&SPLS    SETA  1
         AGO   .B3
.BGREG   AIF   ('&BGADR' EQ '(14)').B6
         LR    14,&BGADR(1)
         AGO   .B6
.ENDREG  AIF   ('&ENDADR' EQ '(15)').B2
         LR    15,&ENDADR(1)
         AGO   .B2
.NOMORE  ANOP
         AIF   (&JLPA EQ 1).MKJPA
.B4      AIF   (&CB EQ 1).MKCB
.B5      AIF   (&SPLS EQ 1).MKSPLS
         AGO   .CHKOPT
.MKJPA   ANOP
&OPT     SETA  &OPT+100
         AGO   .B4
.MKCB    ANOP
&OPT     SETA  &OPT+10
         AGO   .B5
.MKSPLS  ANOP
&OPT     SETA  &OPT+1
.CHKOPT  AIF   (&OPT EQ 111).SNAP1
         AIF   (&OPT EQ 110).SNAP2
         AIF   (&OPT EQ 101).SNAP3
         AIF   (&OPT EQ 100).SNAP4
         AIF   (&OPT EQ 11).SNAP5
         AIF   (&OPT EQ 10).SNAP6
         AIF   (&OPT EQ 1).SNAP7
.SNAP0   ANOP
         AIF   (&NOADRSW EQ 1).SNAP0A
         SNAP  DCB=SNAPDCB,STORAGE=((14),(15)),PDATA=REGS,ID=&SNAPA
         AGO   .END
.SNAP0A  ANOP
         SNAP  DCB=SNAPDCB,PDATA=REGS,ID=&SNAPA
         AGO   .END
.SNAP1   ANOP
         AIF   (&NOADRSW EQ 1).SNAP1A
         SNAP  DCB=SNAPDCB,STORAGE=((14),(15)),SDATA=(CB,Q),           -
               PDATA=(REGS,ALLPA,PSW,SA,SPLS),ID=&SNAPA
         AGO   .END
.SNAP1A  ANOP
         SNAP  DCB=SNAPDCB,SDATA=(CB,Q),                               -
               PDATA=(REGS,ALLPA,PSW,SA,SPLS),ID=&SNAPA
         AGO   .END
.SNAP2   ANOP
         AIF   (&NOADRSW EQ 1).SNAP2A
         SNAP  DCB=SNAPDCB,STORAGE=((14),(15)),SDATA=(CB,Q),           -
               PDATA=(REGS,ALLPA,PSW,SA),ID=&SNAPA
         AGO   .END
.SNAP2A  ANOP
         SNAP  DCB=SNAPDCB,SDATA=(CB,Q),                               -
               PDATA=(REGS,ALLPA,PSW,SA),ID=&SNAPA
         AGO   .END
.SNAP3   ANOP
         AIF   (&NOADRSW EQ 1).SNAP3A
         SNAP  DCB=SNAPDCB,STORAGE=((14),(15)),                        -
               PDATA=(REGS,ALLPA,SPLS),ID=&SNAP
         AGO   .END
.SNAP3A  ANOP
         SNAP  DCB=SNAPDCB,PDATA=(REGS,ALLPA,SPLS),ID=&SNAPA
         AGO   .END
.SNAP4   ANOP
         AIF   (&NOADRSW EQ 1).SNAP4A
         SNAP  DCB=SNAPDCB,STORAGE=((14),(15)),PDATA=(REGS,ALLPA),     -
               ID=&SNAPA
         AGO   .END
.SNAP4A  ANOP
         SNAP  DCB=SNAPDCB,PDATA=(REGS,ALLPA),ID=&SNAPA
         AGO   .END
.SNAP5   ANOP
         AIF   (&NOADRSW EQ 1).SNAP5A
         SNAP  DCB=SNAPDCB,STORAGE=((14),(15)),SDATA=(CB,Q),           -
               PDATA=(REGS,PSW,SA,SPLS),ID=&SNAPA
         AGO   .END
.SNAP5A  ANOP
         SNAP  DCB=SNAPDCB,SDATA=(CB,Q),                               -
               PDATA=(REGS,PSW,SA,SPLS),ID=&SNAPA
         AGO   .END
.SNAP6   ANOP
         AIF   (&NOADRSW EQ 1).SNAP6A
         SNAP  DCB=SNAPDCB,STORAGE=((14),(15)),SDATA=(CB,Q),           -
               PDATA=(REGS,PSW,SA),ID=&SNAPA
         AGO   .END
.SNAP6A  ANOP
         SNAP  DCB=SNAPDCB,SDATA=(CB,Q),                               -
               PDATA=(REGS,PSW,SA),ID=&SNAPA
         AGO   .END
.SNAP7   ANOP
         AIF   (&NOADRSW EQ 1).SNAP7A
         SNAP  DCB=SNAPDCB,STORAGE=((14),(15)),PDATA=(REGS,SPLS),      -
               ID=&SNAPA
         AGO   .END
.SNAP7A  ANOP
         SNAP  DCB=SNAPDCB,PDATA=(REGS,SPLS),ID=&SNAPA
.END     ANOP
*
         MNOTE *,'ID # FOR THIS SNAP IS &SNAPA'
*
.ENDX    LM    14,1,&SNAPA1
         MEND
