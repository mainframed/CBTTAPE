*
* CODING EXAMPLE 3.6.2
*
*
         MACRO
&LABEL   RCNTL &RC=,&SA=
         LCLC  &SVAR
         AIF   (K'&LABEL LE 8).LABOK
         MNOTE 0,'LABEL CONTAINS MORE THAN 8 CHARACTERS---MACRO NOT GEN-
               ERATED'
         MEXIT
.LABOK   ANOP
         AIF   (T'&SA EQ 'O').NOSA
         AIF   ('&SA'(1,1) EQ '(').SAREG
&SVAR    SETC  '&SA'(1,8)
&LABEL   LA    13,&SVAR+4
         AGO   .CHKRC
.SAREG   ANOP
&LABEL   L     13,4(0,&SA(1))
         AGO   .CHKRC
.NOSA    ANOP
&LABEL   L     13,4(0,13)
.CHKRC   AIF   (T'&RC EQ 'O').NORC
         AIF   ('&RC'(1,1) EQ '(').RCREG
         AIF   (T'&RC NE 'N').RCINVAL
         AIF   (&RC GT 4095).RCINVAL
         LA    15,&RC
         AGO   .RCOK
.RCREG   AIF   (&RC(1) EQ 15).RCOK
         MNOTE 0,'INVALID REGISTER SPECIFIED FOR RETURN CODE---RC IGNOR-
               ED'
         AGO   .NORC
.RCINVAL MNOTE 0,'RETURN CODE SPECIFIED IS INVALID---RC IGNORED'
         AGO   .NORC
.RCOK    L     14,12(0,13)
         LM    0,12,20(13)
         BR    14
         MEXIT
.NORC    LM    14,12,12(13)
         BR    14
         MEND
