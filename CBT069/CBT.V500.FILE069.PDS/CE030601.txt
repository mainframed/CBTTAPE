*
* CODING EXAMPLE 3.6.1
*
*
         MACRO
&LABEL   INITL &EQU=,&ID=,&SA=
         LCLA  &POS,&TOTOPD,&DISP,&STOPSW,&REG13SW
         LCLC  &SVAR,&LBL,&PRFX
         LCLC  &REGS,&REG1,&REG2
         AIF   (T'&LABEL NE 'O').B1
         MNOTE *,'NO LABEL SPECIFIED'
         AGO   .CHKEQU
.B1      AIF   (K'&LABEL LE 8).LABOK
         MNOTE 'LABEL EXCEEDS 8 CHARACTERS---LEFT MOST 8 USED'
.LABOK   ANOP
&LBL     SETC  '&LABEL'(1,8)
.CHKEQU  AIF   (T'&EQU EQ 'O').B8
         AIF   (K'&EQU LE 6).EQUOK
         MNOTE 'EQU PREFIX EXCEEDS 6 CHARACTERS---LEFT MOST 6 USED'
.EQUOK   ANOP
&PRFX    SETC  '&EQU'(1,6)
.B8      ANOP
&SVAR    SETC  'SVAR'.'&SYSNDX'
&TOTOPD  SETA  N'&SYSLIST
         AIF   (&TOTOPD EQ 0).NOREG
         AIF   (&TOTOPD LE 15).PROCEED
         MNOTE 0,'MORE THAN 15 REGISTERS HAVE BEEN SPECIFIED---SURPLUS -
               ONES HAVE BEEN IGNORED'
&TOTOPD  SETA  15
.PROCEED ANOP
         AIF   (T'&EQU NE 'O').EQUTBL
.B2      ANOP
         AIF   (T'&ID NE 'O').SETID
.B3      ANOP
.ULOOP   ANOP
&POS     SETA  &POS+1
         AIF   (&POS GT &TOTOPD).USING
         AIF   ('&SYSLIST(&POS)' EQ '13').REG13
         AIF   ('&SYSLIST(&POS)' EQ '0').INVREG
.B4      ANOP
&REGS    SETC  '&REGS'.','.'&PRFX'.'&SYSLIST(&POS)'
         AGO   .ULOOP
.USING   ANOP
         AIF   (&REG13SW EQ 1).USING13
&LBL     STM   &PRFX.14,&PRFX.12,12(&PRFX.13)
         BALR  &PRFX.&SYSLIST(1),0
         USING *&REGS
.B5      ANOP
&POS     SETA  1
         AIF   (&TOTOPD EQ 1).REST
.NXLOAD  ANOP
&POS     SETA  &POS+1
         AIF   (&POS GT &TOTOPD).REST
&REG1    SETC  '&SYSLIST(&POS-1)'
&REG2    SETC  '&SYSLIST(&POS)'
         LA    &PRFX.&REG2,2048
         LA    &PRFX.&REG2,2048(&PRFX.&REG1,&PRFX.&REG2)
         AGO   .NXLOAD
.REG13   AIF   (&POS GT 1).INVREG
&REG13SW SETA  1
         AGO   .B4
.USING13 ANOP
         AIF   (T'&SA EQ 'O').B6
&SVAR    SETC  '&SA'(1,8)
.B6      ANOP
&LBL     STM   &PRFX.14,&PRFX.12,12(&PRFX.13)
         LR    &PRFX.2,&PRFX.13
         BALR  &PRFX.12,0
         BAL   &PRFX.13,76(0,&PRFX.12)
&SVAR    DC    18F'0'
         USING &SVAR&REGS
         AGO   .B5
.REST    AIF   (&REG13SW EQ 1).B7
         GETMAIN R,LV=72,SP=1
         ST    &PRFX.13,4(0,&PRFX.1)
         ST    &PRFX.1,8(0,&PRFX.13)
         LR    &PRFX.13,&PRFX.1
         L     &PRFX.1,4(0,&PRFX.13)
         L     &PRFX.1,24(0,&PRFX.1)
         MEXIT
.B7      ST    &PRFX.2,4(0,&PRFX.13)
         ST    &PRFX.13,8(0,&PRFX.2)
         MEXIT
.EQUTBL  ANOP
&PRFX.0  EQU   0
&PRFX.1  EQU   1
&PRFX.2  EQU   2
&PRFX.3  EQU   3
&PRFX.4  EQU   4
&PRFX.5  EQU   5
&PRFX.6  EQU   6
&PRFX.7  EQU   7
&PRFX.8  EQU   8
&PRFX.9  EQU   9
&PRFX.10 EQU   10
&PRFX.11 EQU   11
&PRFX.12 EQU   12
&PRFX.13 EQU   13
&PRFX.14 EQU   14
&PRFX.15 EQU   15
         AIF   (&REG13SW EQ 1).B5
         AGO   .B2
.SETID   B     12(0,&PRFX.15)
         AIF   ('&ID'(1,1) EQ '*').CSECTNM
         DC    CL8'&ID'
         AGO   .B3
.CSECTNM DC    CL8'&SYSECT'
         AGO   .B3
.NOREG   MNOTE 12,'NO REGISTERS SPECIFIED---MACRO NOT GENERATED'
         MEXIT
.INVREG  MNOTE 12,'INVALID REGISTER SPECIFIED---MACRO GENERATION TERMIN-
               ATED'
         MEND
