*
* CODING EXAMPLE 15.4.12
*
*
USER02   CSECT
*
*
***********************************************************************
*    INITIALIZATION
***********************************************************************
*
         INITL 3,EQU=R             INITIALIZE PROGRAM
*
*
***********************************************************************
*    MAINSTREAM OF PROGRAM
***********************************************************************
*
         BAL   R6,SETUP            SET UP AUTH AND LINKAGE FOR SP-AS
         BAL   R6,SRCHTBL          SEARCH TBL FOR SPECIFIC DATA ENTRY
         BAL   R6,CHKDATA          VERIFY DATA BY DUMPING IT
         BAL   R6,PROCESS          PROCESS DATA FROM TBL
         B     RETURN              RETURN TO MVS OR CALLING PROG
*
*
***********************************************************************
*    THIS ROUTINE INVOKES THE APPROPRIATE SVC ROUTINE AND REQUESTS THAT
*    IT SET UP THE REQUIRED AUTHORITY AND LINKAGE FOR ACCESS TO/FROM
*    THE SP-AS.
***********************************************************************
*
SETUP    LA    R0,0                 REQUEST AUTH/LINKAGE FOR SP-AS
         SVC   255                  INVOKE SVC TO SET UP AUTH/LINKAGE
         BR    R6                   RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS ROUTINE INVOKES THE APPROPRIATE SVC RTN TO CALL A SPECIFIC
*    PC ROUTINE TO SEARCH A TABLE LOCATED IN THAT ADDRESS SPACE AND
*    RETURN THE SPECIFIC DATA ENTRY WHICH CORRESPONDS TO THE SPECIFIED
*    KEY.
***********************************************************************
*
SRCHTBL  LA    R0,3                SPEC SERV-ID
         LA    R1,KEY              LD ADR OF KEY FOR SVC
         LA    R15,DATA            LD ADR OF RECV DATA AREA FOR SVC
         SVC   255                 INVOKE SVC TO GET DATA
         LTR   R15,R15             TEST FOR GOOD RC
         BZR   R6                  IF OK, RET TO CALLING ROUTINE
         WTO   '*** INVALID ID SPECIFIED ***',ROUTCDE=11
         LA    R15,4               IND INVALID ID SPECIFIED
         BR    R6                  RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS ROUTINE IS USED FOR TESTING ONLY. IT VERIFIES THAT THE
*    CORRECT DATA FROM THE TABLE WAS RECEIVED FOR THE SPECIFIED KEY
*    FROM THE CALLED PC ROUTINE.
***********************************************************************
*
CHKDATA  L     R8,DATA             LOAD 1ST FOUR BYTES OF RECV DATA
         L     R9,DATA+4           LOAD 2ND FOUR BYTES OF RECV DATA
         ABEND 900,DUMP            DUMP REGS
*
*
***********************************************************************
*    THIS ROUTINE PERFORMS THE MAIN PROCESSING OF THE PROGRAM.
***********************************************************************
*
PROCESS  LTR   R15,R15             CHK IF DATA RECEIVED FROM PC ROUTINE
         BNZ   R6                  IF NO, RET TO CALLING ROUTINE
*        ...                       IF YES, PROCESS DATA
         BR    R6                  RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS ROUTINE RESTORES THE REGISTERS AND RETURNS CONTROL TO MVS OR
*    THE CALLING PROGRAM.
***********************************************************************
*
RETURN   RCNTL RC=0                RETURN TO MVS OR CALLING PROG
*
*
***********************************************************************
*    THE DC/DS STATEMENTS
***********************************************************************
*
         DS    0F
KEY      DC    C'CODE02'           SPECIFIED ARGUMENT KEY
DATA     DS    CL20                RECEIVING FIELD FOR DATA FROM SP-AS
*
*
***********************************************************************
*    END OF PROGRAM
***********************************************************************
*
         END
