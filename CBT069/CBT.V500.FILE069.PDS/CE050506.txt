*
* CODING EXAMPLE 5.5.6
*
*
         LA    R0,PDNOLEN          LD LENGTH OF FIELD TO VALIDATE
         LA    R10,PDNUMB          LD ADR OF FIELD TO VALIDATE
         BAL   R7,VALDTPD          VALIDATE FIELD FOR PACKED DEC DATA
         LTR   R15,R15             CHK IF FIELD CONTAIN VALID DATA
         BZ    DATAOK              IF O, FIELD CONTAINS VALID PD NUMB
         B     DATAINV             IF NOT, FIELD CONTAINS INV PD NUMB
*
*
***********************************************************************
*   THIS SUBROUTINE CHECKS IF A FIELD OF DATA CONTAINS PACKED DECIMAL
*   NUMBERS. THIS SUBROUTINE PERFORMS THE VALIDATION BY USING TWO
*   TRANSLATION TABLES. ONE TABLE TO VALIDATE ALL THE BYTES EXCEPT THE
*   LAST ONE AND THE OTHER TABLE TO VALIDATE THE LAST BYTE (THE ONE
*   WHICH CONTAINS THE SIGN). AN RC IS SET IN REGISTER 15 FOR THE
*   CALLING ROUTINE; X'00' INDICATES VALID DATA, X'04' INDICATES
*   INVALID DATA.
***********************************************************************
*
VALDTPD  C     R0,BINONE           CHK IF FIELD CONTAINS ONLY 1 BYTE
         BE    CHKSIGN             IF YES, DO ONLY LAST BYTE TEST
         C     R0,BIN16            CHK IF LEN OF FIELD EXCEEDS 16 BYTES
         BH    PDATAINV            IF YES, PD FIELD TOO LONG
         LR    R14,R0              LD FD LEN INTO R14 FOR S + EX INSTR
         LR    R15,R10             LD ADR OF BG OF FIELD INTO R15 (SAVE
*                                  R10)
         S     R14,BINTWO          DECR LEN BY 2, 1 FOR SIGN, 1 FOR EX
         EX    R14,TRT1            CHK IF ALL BYTES (EXCEPT SIGN) VALID
         BC    8,CHKSIGN           IF ALL BYTES OK, CHK SIGN BYTE
         LA    R15,4               IF NOT, IND IT
         BR    R7                  RETURN TO CALLING RTN
TRT1     TRT   0(0,R10),TABLE1     TRT INSTR FOR EX
CHKSIGN  LR    R14,R0              LD FD LEN IN R14 FOR LA INSTR
         LA    R15,0(R14,R10)      LD ADR OF BYTE PAST FIELD
         BCTR  R15,0               SET ADR TO POINT TO SIGN (LAST) BYTE
         UNPK  0(1,R15),0(1,R15)   REVERSE SIGN AND DIGIT FOR TABLE2
         TRT   0(1,R15),TABLE2     CHK IF LAST BYTE VALID
         UNPK  0(1,R15),0(1,R15)   RESTORE LAST BYTE
         BC    8,PDATAOK           CHK IF LAST BYTE OK
PDATAINV LA    R15,4               IF NOT, IND IT
         BR    R7                  RETURN TO CALLING RTN
PDATAOK  LA    R15,0               DATA OK, IND IT
         BR    R7                  RETURN TO CALLING RTN
*
BINONE   DC    F'1'
BINTWO   DC    F'2'
BIN16    DC    F'16'
PDNOLEN  EQU   L'PDNUMB           CONTAINS THE LEN OF PDNUMB (IN BYTES)
PDNUMB   DC    ...                THE FIELD WHICH IS TO BE VALIDATED
*                                 FOR PACKED DECIMAL DATA
