*
* CODING EXAMPLE 15.3.2
*
*
AS1PROG  CSECT
*
*
***********************************************************************
*    INITIALIZATION
***********************************************************************
*
         INITL 3,EQU=R             INITIALIZE PROGRAM
         USING CVT,R10             DEFINE REG FOR CVT DSECT
         USING WORKAREA,R12        DEFINE REG FOR WORKAREA DSECT
*
*
***********************************************************************
*    MAINSTREAM OF PROGRAM
***********************************************************************
*
GETDATA  BAL   R6,CHKDATA          CHECK IF ANY A.S. HAS DATA TO SEND
         BAL   R6,SCHLDSRB         IF YES, SCHEDULE AN SRB
         BAL   R6,PROCESS          PROCESS DATA FROM SRB + NORM PROC
         BAL   R6,FREEMAIN         RELEASE WORKAREA ALLO BY SVC
         B     GETDATA             GET MORE DATA FROM ANY A.S.
*
*
***********************************************************************
*    THIS ROUTINE CHECKS THE CVTUSER FIELD TO DETERMINE IF ANY ADDRESS
*    SPACES HAVE DATA TO SEND TO THIS ADDRESS SPACE. IF THERE ARE NONE,
*    THEN THIS ROUTINE PERFORMS A CYCLE OF NORMAL PROCESSING AND THEN
*    CHECKS THE CVTUSER FIELD AGAIN. WHEN THE CVTUSER INDICATES THAT AN
*    ADDRESS SPACE HAS DATA TO SEND, THIS ROUTINE TURNS ON DATASW AND
*    RETURNS CONTROL TO THE CALLING ROUTINE.
***********************************************************************
*
CHKDATA  BAL   R7,ENQ              SERIALIZE ACCESS TO CVTUSER
         L     R10,16              LOAD ADR OF CVT
         CLC   CVTUSER,BINZEROS    CHECK IF CVTUSER HAS ADR OF A.S.
         BE    NODATA              IF NO, NO DATA TO BE RECEIVED
         MVI   DATASW,C'1'         IF YES, TURN ON DATASW
         BAL   R7,DEQ              RELEASE CVTUSER ENQ
         BR    R6                  RETURN TO CALLING ROUTINE
NODATA   MVI   DATASW,C'0'         IND NO DATA TO BE RECEIVED
         BAL   R7,DEQ              RELEASE CVTUSER ENQ
         WTO   '*** NO DATA ***'   *** FOR INITIAL TESTING ONLY ***
         ABEND 901                 *** FOR INITIAL TESTING ONLY ***
         BAL   R7,PROCESS1         DO A CYCLE OF NORM PROC
         B     CHKDATA             CHK AGAIN IF ANY A.S. HAVE DATA
*
*
***********************************************************************
*    THIS SUBROUTINE ISSUES AN EXCLUSIVE ENQ TO SERIALIZE THE ACCESS
*    TO CVTUSER.
***********************************************************************
*
ENQ      ENQ   (QNAME,RNAME,E,,SYSTEM)     DO ENQ
         BR    R7                          RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS SUBROUTINE ISSUES A DEQ TO RELEASE EXCLUSIVE ACCESS TO
*    CVTUSER.
***********************************************************************
*
DEQ      DEQ   (QNAME,RNAME,,SYSTEM)       DO DEQ
         BR    R7                          RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS ROUTINE CHECKS DATASW. IF THE SWITCH IS ON, THEN THE
*    APPROPRIATE SVC ROUTINE IS INVOKED TO SCHEDULE AN SRB IN THE
*    ADDRESS SPACE WHOSE ASCB ADDRESS IS STORED IN CVTUSER.
***********************************************************************
*
SCHLDSRB SVC   253                 IF YES, ISSUE SVC TO SCHEDULE SRB
         LR    R12,R1              SET ADBLTY FOR WORKAREA ALLO BY SVC
         BR    R6                  RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS ROUTINE PERIODICALLY CHECKS IF ANY OUTSTANDING SRBS HAVE
*    COMPLETED MOVING THE DATA FROM ANOTHER ADDRESS SPACE TO THIS ONE.
*    NORMAL PROGRAM PROCESSING IS PERFORMED BETWEEN CHECKS.
***********************************************************************
*
PROCESS  BAL   R7,CHKSRB           CHECK IF DATA MOVEMENT COMPLETE
         BAL   R7,PROCESS1         DO A CYCLE OF NORM PROC
         CLI   DATASW,C'1'         CHECK IF SRB OUTSTANDING
         BE    PROCESS             IF YES, CHECK AGAIN FOR COMPLETION
         BR    R6                  IF NO, RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS SUBROUTINE CHECKS DATASW. IF THE SWITCH IS ON, THEN THE ECB
*    IS CHECKED TO DETERMINE IF THE DATA MOVEMENT FROM THE TARGET
*    ADDRESS SPACE TO THIS ADDRESS SPACE HAS BEEN COMPLETED.
***********************************************************************
*
CHKSRB   TM    WECB,X'40'          IF YES, CHK ECB FOR COMPLETION
         BO    PROCDATA            IF COMPLETE, PROCESS DATA FROM
*                                  OTHER A.S.
         BR    R7                  IF NOT, RET TO CALLING ROUTINE
PROCDATA MVI   DATASW,C'0'         TURN OFF OUTSTANDING SRB SWITCH
         BAL   R8,PROCESS2         PROC DATA RECEIVED FROM OTHER A.S.
         BR    R7                  RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS SUBROUTINE SIMULATES PROGRAM PROCESSING BY GOING INTO A
*    FINITE I/O LOOP.
***********************************************************************
*
PROCESS1 LA    R10,10
OPEN     OPEN  (DCB,(OUTPUT))
         PUT   DCB,RECORD
         CLOSE DCB
         BCT   R10,OPEN
         BR    R7
*
*
***********************************************************************
*    THIS SUBROUTINE PROCESSES THE DATA RECEIVED FROM THE OTHER ADDRESS
*    SPACE. DURING TESTING, THIS ROUTINE DISPLAYS THE JOBNAME OF THE
*    OTHER ADDRESS SPACE AND THE FIRST 8 BYTES OF THE DATA RECEIVED.
***********************************************************************
*
PROCESS2 MVC   WTOJBNM,WJOBNAME    MOVE JOBNAME INTO WTO-LIST
         MVC   WTODATA,WDATA       MOVE FIRST 8 BYTES OF DATA RECEIVED
         LA    R1,WTOLST1          LD ADR OF WTO-LIST
         WTO   MF=(E,(R1))         ISSUE EXEC-FORM OF WTO
         BR    R8                  RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS ROUTINE RELEASES THE WORKAREA ALLOCATED BY THE SVC ROUTINE.
***********************************************************************
*
FREEMAIN LA    R10,WGMADR          LOAD ADR OF GETMAIN AREA POINTER
         FREEMAIN EU,LV=WKARLEN,A=(R10)
         ABEND 902                 *** FOR INITIAL TESTING ONLY ***
         BR    R6                  RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS ROUTINE RESTORES THE REGISTERS AND RETURNS CONTROL TO MVS OR
*    THE CALLING PROGRAM
***********************************************************************
*
RETURN   RCNTL RC=0                RETURN TO MVS OR CALLING PROG
*
*
***********************************************************************
*    THE WTO-LIST FOR THE EXECUTE FORM OF THE WTO MACRO
***********************************************************************
*
         DS    0F
WTOLST1  DC    AL2(WTOLST1X-WTOLST1)
         DC    H'0'
         DC    C'*** JOBNAME: '
WTOJBNM  DS    CL8
         DC    C'  DATA: '
WTODATA  DS    CL8
WTOLST1X EQU   *
*
*
***********************************************************************
*    DC/DS STATEMENTS
***********************************************************************
*
BINZEROS DC    F'0'
DATASW   DC    C'0'
QNAME    DC    CL8'USER'
RNAME    DC    C'CVTUSER'
*
*
***********************************************************************
*    DATA RECORD AND DCB USED TO SIMULATE PROCESSING
***********************************************************************
*
RECORD   DC    80CL1'A'
DCB      DCB   DSORG=PS,MACRF=PM,BLKSIZE=80,LRECL=80,RECFM=F,          -
               DDNAME=WORK
*
*
***********************************************************************
*    THE DSECTS
***********************************************************************
*
WORKAREA DSECT
WGMADR   DS    F
WECB     DS    F
WJOBNAME DS    CL8
WDATA    DS    CL100
WKARLEN  EQU   *-WORKAREA
*
         PRINT NOGEN
         CVT   DSECT=YES           GENERATES THE CVT DSECT
*
*
***********************************************************************
*    END OF PROGRAM
***********************************************************************
*
         END
