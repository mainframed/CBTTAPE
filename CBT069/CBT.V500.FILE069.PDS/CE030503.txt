*
* CODING EXAMPLE 3.5.3
*
*
         MACRO
&LABEL   BLDTBL
         LCLA  &POS,&TOTPOS
         LCLB  &OKBIT
         LCLC  &LAB,&MM,&DD,&DATE
&POS     SETA  1                   INITIALIZE RUNNING POS PARM COUNT
&TOTPOS  SET   N'&SYSLIST          TOTAL POS PARMS SPECIFIED
         AIF   (&TOTPOS EQ 0).NOPARMS    CHECK IF ANY POS PARMS SPEC
&MM      SETC  '&SYSDATE'(1,2)           EXTRACT MONTH
&DD      SETC  '&SYSDATE'(4,2)           EXTRACT DAY
         AIF   ('&DD' LT '04' OR '&DD' GT '25').INVDATE  CHK VALID DATE
&DATE    SETC  '&MM'.'&DD'
&LAB     SETC  '&LABEL'
         AIF   (K'&LAB LE 8).LABOK       CHECK LABEL LENGTH
&LAB     SETC  '&LABEL'(1,8)             SELECT FIRST 8 CHARACTERS
.LABOK   ANOP
&LAB     DS    0H
.LOOP    ANOP
.*
.* SET UP VALIDATION LOGIC FOR POS PARM IN SETB INSTRUCTION
.*
&OKBIT   SETB  ((K'&SYSLIST(&POS,1) EQ 4)  AND                         -
               (K'&SYSLIST(&POS,2) LE 8)   AND                         -
               (T'&SYSLIST(&POS,1) NE 'O') AND                         -
               (T'&SYSLIST(&POS,2) NE 'O'))
         AIF   (&OKBIT EQ 0).INVPARM     CHK IF POS PARM OK
.*
.* DEFINE TABLE ENTRY
.*
         DC    C'&SYSLIST(&POS,1)',C'&DATE',CL8'&SYSLIST(&POS,2)'
.INCRPOS ANOP
&POS     SETA  &POS+1                    INCR POS PARM COUNT
         AIF   (&POS GT &TOTPOS).TBLEND  CHECK FOR POS PARM END
         AGO   .LOOP
.TBLEND  DC    16XL1'FF'                 INDICATE TABLE END
         MEXIT
.*
.* MNOTES FOR ERROR CONDITIONS
.*
.NOPARMS MNOTE 8,'NO TABLE ENTRIES SPECIFIED---MACRO NOT GENERATED'
         MEXIT
.INVDATE MNOTE 4,'MACRO NOT GENERATED BECAUSE OF INVALID DATE'
         MEXIT
.INVPARM MNOTE 4,'POSITIONAL PARAMETER NUMBER &POS: &SYSLIST(&POS) IS I-
         AGO   .INCRPOS
         MEND
