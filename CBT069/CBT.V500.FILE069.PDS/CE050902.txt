*
* CODING EXAMPLE 5.9.2
*
*
***********************************************************************
*    THIS ROUTINE OBTAINS THE PRICE FOR PRODUCT NUMBER 0123.
***********************************************************************
*
GETP0123 LA    R10,P0123TBL              LD ADR OF ML-TBL FOR PROD-0123
         BAL   R8,GETPRICE               GET PRICE OF PROD-0123
         BR    R7                        RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS SUBROUTINE OBTAINS THE PRICE OF THE PRODUCT SPECIFIED BY THE
*    CALLING ROUTINE. WHEN THIS SUBROUTINE RECEIVES CONTROL, IT EXPECTS
*    THE ADDRESS OF THE MULTI-LEVEL PRICE TABLE FOR THE PRODUCT WHOSE
*    PRICE IS DESIRED, TO BE LOADED INTO REGISTER 10.
***********************************************************************
*
         USING DSIZETBL,R10              DEF REG FOR SIZE-TABLE DSECT
GETPRICE DS    0H
NEXTSIZE CLI   $SIZE,TBLENDID            CHECK FOR END OF TABLE
         BE    INVSIZE                   IF TBL END, INV SIZE SPEC
         CLC   $SIZE,ARGSIZE             SEARCH FOR REQUIRED SIZE
         BE    SETQTBL                   IF FOUND, LOAD QUAL TABLE
         LA    R10,$SENTLEN(0,R10)       INCR TO NEXT SIZE ENTRY
         B     NEXTSIZE                  SEARCH NEXT SIZE ENTRY
INVSIZE  LA    R15,4                     INDICATE INVALID SIZE
         BR    R8                        RETURN TO CALLING RTN
*
SETQTBL  L     R10,$QTBLADR              LOAD ADR OF CORRESPD QUAL TBL
         DROP  R10                       MAKE R10 AVAILABLE FOR DSECT
         USING DQUALTBL,R10              DEF REG FOR QUAL-TABLE DSECT
NEXTQUAL CLI   $QUAL,TBLENDID            CHECK FOR END OF TABLE
         BE    INVQUAL                   IF TBL END, INV QUAL SPEC
         CLC   $QUAL,ARGQUAL             SEARCH FOR REQUIRED QUAL
         BE    SETLTBL                   IF FOUND, LOAD LOC TABLE
         LA    R10,$QENTLEN(0,R10)       INCR TO NEXT QUAL ENTRY
         B     NEXTQUAL                  SEARCH NEXT QUAL ENTRY
INVQUAL  LA    R15,8                     INDICATE INVALID QUAL
         BR    R8                        RETURN TO CALLING RTN
*
SETLTBL  L     R10,$LTBLADR              LOAD ADR OF CORRESPD LOC TBL
         DROP  R10                       MAKE R10 AVAIL FOR DSECT
         USING DLOCTBL,R10               DEF REG FOR LOC-TBL DSECT
NEXTLOC  CLC   $LOC,ARGLOC               SEARCH FOR REQUIRED LOC
         BE    SETPRICE                  IF FOUND, GET PRICE
         CLI   $LOC,$TBLENDID            CHECK FOR END OF TABLE
         BE    INVLOC                    IF TBL END, INV LOC SPEC
         LA    R10,$LENTLEN(0,R10)       INCR TO NEXT LOC ENTRY
         B     NEXTLOC                   SEARCH NEXT LOC ENTRY
INVLOC   LA    R15,12                    INDICATE INVALID LOC
         BR    R8                        RETURN TO CALLING RTN
*
SETPRICE ZAP   PRICE,$PRICE              LOAD PRICE FOR CALLER
         LA    R15,0                     INDICATE PRICE SEARCH OK
         BR    R8                        RETURN TO CALLING RTN
*
*
***********************************************************************
*    DC/DS/EQU STATEMENTS
***********************************************************************
*
TBLENDID EQU   X'FF'
ARGSIZE  DS    CL1
ARGQUAL  DS    CL1
ARGLOC   DS    CL1
PRICE    DS    PL3
