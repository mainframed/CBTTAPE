*
CODING EXAMPLE B.8.3
*
*
//-------- JOB  ----
//ASMLINK  EXEC ASMACLG
//C.SYSLIB DD   DSN=SYS1.MACLIB,DISP=SHR
//         DD   DSN=USER.MACLIB,DISP=SHR
//C.SYSIN  DD   *
*
*
DYNASMLK CSECT
*
*
***********************************************************************
*    INITIALIZATION
***********************************************************************
*
         INITL 3,EQU=R         SET UP BASE REGS AND SAVEAREA
         USING S99RBP,R10      DEFINE REG FOR SVC99 REQ-BLK-PTR DSECT
         USING S99RB,R11       DEFINE REG FOR SVC99 REQ-BLOCK DSECT
*
*
***********************************************************************
*    MAINSTREAM OF PROGRAM
***********************************************************************
*
NXTSRCNM BAL   R6,GETSRCNM         GET PGM NAMES FROM SRC LIB DIR
         LTR   R15,R15             CHECK IF END-OF-DIR
         BNZ   RETURN              IF YES, TERMINATE PROGRAM
         BAL   R6,DYNALL01         IF NO, DYN ALLOC SRC LIB AND PGM NM
         BAL   R6,DYNALL02         DYN ALLOC OBJ LIB AND OBJMOD NAME
         BAL   R6,INVKASM          INVOKE THE ASSEMBLER
         BAL   R6,INVKLKED         INVOKE THE LINKAGE EDITOR
         BAL   R6,UNALL01          DYN UNALLOC SRC LIB AND PGM NAME
         BAL   R6,UNALL02          DYN UNALLOC OBJ LIB AND OBJMOD NAME
         B     NXTSRCNM            GET NEXT PROGRAM NAME
*
*
***********************************************************************
*    THIS ROUTINE OBTAINS ALL THE PROGRAM NAMES FROM THE SOURCE
*    LIBRARY DIRECTORY ONE AT A TIME. ALIASES ARE IGNORED.
***********************************************************************
*
GETSRCNM READDIR SRCDIR            GET PGM NAMES FROM LIB DIR
         C     R15,BIN4            CHECK FOR ALIAS NAME
         BE    GETSRCNM            IF YES, IGNORE
         MVC   SRCNAME,$MEMBER     PUT PGM NAME INTO DA-MEMB TEXT UNIT
         MVC   OBJNAME,$MEMBER     PUT PGM NAME INTO DA-MEMB TEXT UNIT
         MVC   PGMNAME,$MEMBER     PUT PGM NAME INTO LKED PARM LIST
         BR    R6                  RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS ROUTINE DYNAMICALLY ALLOCATES THE SOURCE LIBRARY AND THE
*    MEMBER (PROGRAM SOURCE NAME).
***********************************************************************
*
DYNALL01 BAL   R7,SETRBA01         SETUP RB FOR ALLOC
         BAL   R7,SETRBPTR         SETUP RB PTR
         BAL   R7,DOSVC99          ISSUE SVC 99
         BR    R6                  RETURN TO MVS OR CALLING PROG
*
*
***********************************************************************
*    THIS SUBROUTINE SETS UP THE SVC99 REQUEST BLOCK FOR ALLOCATION
*    OF THE SOURCE LIBRARY AND THE MEMBER NAME.
***********************************************************************
*
SETRBA01 LA    R11,REQBLK          SET ADRBLTY FOR RB DSECT
         XC    REQBLK,REQBLK       CLEAR RB
         MVI   S99RBLN,REQBLKLN    SET RB LEN
         MVI   S99VERB,S99VRBAL    SET ALLOCATION VERB
         LA    R10,TUPTRA01        LD ADR OF BG OF TEXT-UNIT-PTR LIST
         ST    R10,S99TXTPP        STORE THAT ADR INTO RB
         BR    R7                  RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS SUBROUTINE SETS UP THE SVC99 REQUEST-BLOCK-POINTER.
***********************************************************************
*
SETRBPTR LA    R10,RBPTR           SET ADRBLTY FOR RBPTR DSECT
         LA    R11,REQBLK          LD ADR OF RB
         ST    R11,S99RBPTR        STORE RB ADR INTO RB PTR
         OI    S99RBPTR,S99RBPND   TURN ON HIGH ORDER BIT
         BR    R7                  RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS SUBROUTINE ISSUES SVC 99 VIA THE DYNALLOC MACRO.
***********************************************************************
*
DOSVC99  LA    R1,RBPTR            LD ADR OF RB PTR INTO R1 FOR SVC 99
         DYNALLOC                  ISSUE SVC 99
         LTR   R15,R15             CHK RC
         BNZ   CHKERR              IF NOT ZERO, CHK ERROR/INFO FIELDS
         BR    R7                  IF OK, RETURN TO CALLING ROUTINE
CHKERR   LH    R8,S99ERROR
         LH    R9,S99INFO
         ABEND 900,DUMP
*
*
***********************************************************************
*    THIS ROUTINE DYNAMICALLY ALLOCATES THE OBJECT LIBRARY AND THE
*    MEMBER (OBJECT MODULE NAME).
***********************************************************************
*
DYNALL02 BAL   R7,SETRBA02         SETUP RB FOR ALLOC OBJECT DDNAME
         BAL   R7,SETRBPTR         SETUP RB PTR
         BAL   R7,DOSVC99          ISSUE SVC 99
         BR    R6                  RETURN TO MVS OR CALLING PROG
*
*
***********************************************************************
*    THIS SUBROUTINE SETS UP THE SVC99 REQUEST BLOCK FOR ALLOCATION
*    OF THE OBJECT LIBRARY AND THE MEMBER NAME.
***********************************************************************
*
SETRBA02 LA    R11,REQBLK          SET ADRBLTY FOR RB DSECT
         XC    REQBLK,REQBLK       CLEAR RB
         MVI   S99RBLN,REQBLKLN    SET RB LEN
         MVI   S99VERB,S99VRBAL    SET ALLOCATION VERB
         LA    R10,TUPTRA02        LD ADR OF BG OF TEXT-UNIT-PTR LIST
         ST    R10,S99TXTPP        STORE THAT ADR INTO RB
         BR    R7                  RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS ROUTINE INVOKES THE ASSEMBLER.
***********************************************************************
*
INVKASM  LINK  EP=ASMA90,PARAM=(AOPTLIST,ADDNLIST),VL=1
         BR    R6
*
*
***********************************************************************
*    THIS ROUTINE INVOKES THE LINKAGE EDITOR.
***********************************************************************
*
INVKLKED LINK  EP=HEWL,PARAM=(LOPTLIST,LDDNLIST),VL=1
         BR    R6                  RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS ROUTINE UNALLOCATES THE DDNAME ALLOCATED IN THE DYNALL01
*    ROUTINE.
***********************************************************************
*
UNALL01  BAL   R7,SETRBU01         SETUP RB FOR UNALLO OF SRC LIB
         BAL   R7,SETRBPTR         SETUP RB PTR
         BAL   R7,DOSVC99          ISSUE SVC 99
         BR    R6                  RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS SUBROUTINE SETS UP THE SVC99 REQUEST BLOCK FOR THE
*    UNALLOCATION OF THE SOURCE LIBRARY.
***********************************************************************
*
SETRBU01 LA    R11,REQBLK          SET ADRBLTY FOR RB DSECT
         XC    REQBLK,REQBLK       CLEAR RB
         MVI   S99RBLN,REQBLKLN    SET RB LEN
         MVI   S99VERB,S99VRBUN    SET ALLOCATION VERB
         LA    R10,TUPTRU01        LD ADR OF BG OF TEXT-UNIT-PTR LIST
         ST    R10,S99TXTPP        STORE THAT ADR INTO RB
         BR    R7                  RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS ROUTINE UNALLOCATES THE DDNAME ALLOCATED IN THE DYNALL02
*    ROUTINE.
***********************************************************************
*
UNALL02  BAL   R7,SETRBU02         SETUP RB FOR UNALLO OF OBJ LIB
         BAL   R7,SETRBPTR         SETUP RB PTR
         BAL   R7,DOSVC99          ISSUE SVC 99
         BR    R6                  RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS SUBROUTINE SETS UP THE SVC99 REQUEST BLOCK FOR THE
*    UNALLOCATION OF THE OBJECT LIBRARY.
***********************************************************************
*
SETRBU02 LA    R11,REQBLK          SET ADRBLTY FOR RB DSECT
         XC    REQBLK,REQBLK       CLEAR RB
         MVI   S99RBLN,REQBLKLN    SET RB LEN
         MVI   S99VERB,S99VRBUN    SET ALLOCATION VERB
         LA    R10,TUPTRU02        LD ADR OF BG OF TEXT-UNIT-PTR LIST
         ST    R10,S99TXTPP        STORE THAT ADR INTO RB
         BR    R7                  RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS ROUTINE RESTORES THE REGISTERS AND RETURNS
***********************************************************************
*
RETURN   RCNTL RC=0                RETURN TO MVS OR CALLING PROG
*
*
*
*
***********************************************************************
*     CONSTANTS
***********************************************************************
*
BIN4     DC    F'4'
*
*
***********************************************************************
*     PARAMETER-LISTS FOR DYNAMIC ALLOCATION OF SOURCE LIBRARY AND
*     MEMBER (PROGRAM SOURCE) NAME
***********************************************************************
*
*
***********************************************************************
*    SVC99 REQ-BLOCK-POINTER AND REQUEST BLOCK
***********************************************************************
*
RBPTR    DS    F
REQBLK   DS    CL(S99RBEND-S99RB)
REQBLKLN EQU   L'REQBLK
*
*
***********************************************************************
*    TEXT-UNITS-POINTERS LISTS FOR SVC99
***********************************************************************
*
TUPTRA01 DS    0F
         DC    A(TUDDN01)          ADR OF TU FOR DDNAME OF SRC LIB
         DC    A(TUDSN01)          ADR OF TU FOR DSNAME OF SRC LIB
         DC    A(TUMEMB01)         ADR OF TU FOR MEMBER NAME OF SRC
         DC    A(TUDISP1)          ADR OF TU FOR DS STATUS
         DC    X'80'               INDICATES LAST TU ADDRESS
         DC    AL3(TUDISP2)        ADR OF TU FOR DS NORM TERM DISP
*
TUPTRA02 DS    0F
         DC    A(TUDDN02)          ADR OF TU FOR DDNAME OF OBJ LIB
         DC    A(TUDSN02)          ADR OF TU FOR DSNAME OF OBJ LIB
         DC    A(TUMEMB02)         ADR OF TU FOR MEMBER NAME OF OBJ
         DC    A(TUDISP1)          ADR OF TU FOR DS STATUS
         DC    X'80'               INDICATES LAST TU ADDRESS
         DC    AL3(TUDISP2)        ADR OF TU FOR DS NORM TERM DISP
*
TUPTRU01 DS    0F
         DC    X'80'               INDICATES LAST TU ADDRESS
         DC    AL3(TUDDN01)        ADR OF TU FOR DDNAME OR SRC LIB
*
TUPTRU02 DS    0F
         DC    X'80'               INDICATES LAST TU ADDRESS
         DC    AL3(TUDDN02)        ADR OF TU FOR DDNAME OR OBJ LIB
*
*
***********************************************************************
*    TEXT UNITS FOR SVC99
***********************************************************************
*
TUDDN01  DC    AL2(DALDDNAM)       TU KEY FOR DDNAME
         DC    AL2(1)              NUMBER OF ENTRIES
         DC    AL2(8)              LENGTH OF DDNAME
         DC    CL8'SOURCE'         DDNAME OF SOURCE LIBRARY
*
TUDDN02  DC    AL2(DALDDNAM)       TU KEY FOR DDNAME
         DC    AL2(1)              NUMBER OF ENTRIES
         DC    AL2(8)              LENGTH OF DDNAME
         DC    CL8'OBJECT'         DDNAME OF OBJECT LIBRARY
*
TUDSN01  DC    AL2(DALDSNAM),AL2(1),AL2(44)
SRCLIB   DC    CL44'USER.TEST.SRCLIB'     DATASET NAME OF SRCLIB
*
TUDSN02  DC    AL2(DALDSNAM),AL2(1),AL2(44)
OBJLIB   DC    CL44'USER.TEST.OBJLIB'     DATASET NAME OF OBJLIB
*
TUMEMB01 DC    AL2(DALMEMBR),AL2(1),AL2(8)
SRCNAME  DS    CL8                           MEMBER NAME OF SOURCE
*
TUMEMB02 DC    AL2(DALMEMBR),AL2(1),AL2(8)
OBJNAME  DS    CL8                           MEMBER NAME OF OBJECT
*
TUDISP1  DC    AL2(DALSTATS),AL2(1),AL2(1),X'01'    OLD
*
TUDISP2  DC    AL2(DALNDISP),AL2(1),AL2(1),X'08'    KEEP
*
*
***********************************************************************
*     END OF PARAMETER-LISTS FOR DYNAMIC ALLOCATION
***********************************************************************
*
*
***********************************************************************
*     PARAMETER-LISTS FOR DYNAMIC INVOCATION OF THE ASSEMBLER
***********************************************************************
*
         DS    0H
AOPTLIST DC    AL2(AOPTLEN)
AOPTIONS DC    C'XREF(SHORT),OBJECT'
AOPTLEN  EQU   *-AOPTIONS
*
*
         DS    0H
ADDNLIST DC    AL2(ADDLSTLN)
ADDNAMES DC    CL8'OBJECT'     SYSLIN
         DC    XL8'00'         N/A
         DC    XL8'00'         N/A
         DC    XL8'00'         SYSLIB
         DC    CL8'SOURCE'     SYSIN
         DC    CL8'LISTING'    SYSPRINT
ADDLSTLN EQU   *-ADDNAMES
*
*
***********************************************************************
*     PARAMETER-LISTS FOR DYNAMIC INVOCATION OF THE LINKAGE EDITOR
***********************************************************************
*
         DS    0H
LOPTLIST DC    AL2(LOPTLEN)
LOPTIONS DC    C'XREF,NCAL,LIST'
LOPTLEN  EQU   *-LOPTIONS
*
*
         DS    0H
LDDNLIST DC    AL2(LDDLSTLN)
LDDNAMES DC    CL8'OBJECT'     SYSLIN
PGMNAME  DS    CL8             LOD MOD NAME
         DC    XL8'00'         SYSLMOD
         DC    XL8'00'         SYSLIB
         DC    XL8'00'         N/A
         DC    CL8'LISTING'    SYSPRINT
LDDLSTLN EQU   *-LDDNAMES
*
*
***********************************************************************
*     END OF PARAMETER-LISTS FOR THE LINKAGE EDITOR
***********************************************************************
*
*
***********************************************************************
*     DSECTS FOR DYNAMIC ALLOCATION
***********************************************************************
*
         IEFZB4D0        GENS DSECT FOR REQ BLOCK, TEXT UNIT, ETC.
         IEFZB4D2        GENS TABLE OF EQUATES FOR TU KEYS
*
*
***********************************************************************
*    END OF PROGRAM
***********************************************************************
*
*
         END
//*
//*
//G.SYSUT1   DD UNIT=VIO,SPACE=(CYL,(1,1)),DCB=BUFNO=10
//G.SYSLIB   DD DSN=SYS1.MACLIB,DISP=SHR
//           DD DSN=USER.MACLIB,DISP=SHR
//G.LISTING  DD SYSOUT=*
//G.SRCDIR   DD DSN=USER.TEST.SRCLIB,DISP=SHR
//G.SYSLMOD  DD DSN=USER.TEST.LOADLIB,DISP=SHR
//
