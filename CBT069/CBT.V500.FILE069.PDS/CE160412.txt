*
* CODING EXAMPLE 16.4.12
*
*
DSACCESS CSECT
*
*
**********************************************************************
* INITIALIZATION
**********************************************************************
*
         INITL 3,4,5,EQU=R     SET UP BASE REGS SAVEAREA
         USING DSRECORD,R12    DEFINE REG FOR DATA SPACE DSECT
*
*
**********************************************************************
* MAINSTREAM OF PROGRAM
**********************************************************************
*
         BAL   R6,GETDSINF     GET INFO FOR COMMON DATA SPACE
         BAL   R6,SETARMOD     SET UP AR-MODE
         BAL   R6,PUTDS        WRITE RECS INTO DATA SPACE
         BAL   R6,UPDATEDS     UPDATE RECS IN DATA SPACE
         BAL   R6,GETDS        READ RECS FROM DATA SPACE
         BAL   R6,DUMPDATA     DUMP ADDRESS SPACE AND DATA SPACE
         B     RETURN          RETURN TO MVS OR CALLING PROGRAM
*
*
**********************************************************************
* THIS ROUTINE LOADS THE LOAD MODULE (TABLE) CREATED BY THE PROGRAM
* THAT CREATED THE SCOPE=COMMON DATA SPACE. THE LOAD MODULE IS IN THE
* LPA AND CONTAINS THE FOLLOWING INFORMATION ABOUT THE DATA SPACE:
*    +0 (4) - ALET
*    +4 (4) - BEGINNING ADDRESS OF DATA SPACE
*    +8 (8) - STOKEN
* ALSO SETS UP ADDRESSABILITY TO DATA SPACE BY LOADING ALET INTO
* AR AND LOADING DSECT GPR WITH BEG ADDR OF THE DATA SPACE.
**********************************************************************
*
GETDSINF LOAD  EP=DSCNTL99        \
         LR    R10,R0              \  RETRIEVE DATA SPACE INFO
         MVC   DS01ALET,0(R10)      | FROM PROGRAM THAT CREATED THE
         MVC   DSBEGADR,4(R10)     /  SCOPE=COMMON DATA SPACE
         MVC   DS01TOK,8(R10)     /
         LAM   R12,R12,DS01ALET   LOAD ALET OF D.S. INTO AR 12
         L     R12,DSBEGADR       LOAD BG ADDR OF D.S. INTO GPR 12
         BR    R6                 RETURN TO CALLING ROUTINE
*
*
**********************************************************************
* THIS ROUTINE PUTS THE CPU IN AR-MODE WHICH IS REQUIRED TO ACCESS
* A DATA SPACE.
**********************************************************************
*
SETARMOD SAC   512
         SYSSTATE ASCENV=AR       INDICATE AR-MODE TO FOLLOWING MACROS
*
*
**********************************************************************
* THIS ROUTINE COPIES DATA INTO THE DATA SPACE.
**********************************************************************
*
PUTDS    MVC   DSFLD01,DATA01     \   COPY INITIAL TEST DATA INTO
         MVC   DSFLD02,DATA02      |  DATA SPACE
         MVC   DSFLD03,DATA03     /
         BR    R6                 RETURN TO CALLING ROUTINE
*
*
**********************************************************************
* THIS ROUTINE UPDATES DATA FIELDS IN THE DATA SPACE.
**********************************************************************
*
UPDATEDS MVC   DSFLD01,DATA11     \
         MVC   DSFLD02A,DATA12A    \  UPDATE FIELDS IN DATA SPACE
         MVC   DSFLD02C,DATA12C    /
         MVC   DSFLD04,DATA14     /
         BR    R6                 RETURN TO CALLING ROUTINE
*
*
**********************************************************************
* THIS ROUTINE RETRIEVES DATA FROM THE DATA SPACE.
**********************************************************************
*
GETDS    MVC   DATAX1,DSFLD01     \
         MVC   DATAX2,DSFLD02      \ COPY DATA FIELDS FROM DATA SPACE
         MVC   DATAX3,DSFLD03      / INTO FIELDS IN ADDRESS SPACE
         MVC   DATAX4,DSFLD04     /
         BR    R6                 RETURN TO CALLING ROUTINE
*
*
**********************************************************************
* THIS ROUTINE DUMPS THE ADDRESS SPACE AND FIRST 4K OF THE DATA SPACE
* WITH USERCODE OF 999. REG 8 POINTS TO THE BEGINNING OF THE RECORDS
* READ FROM THE DATA SPACE. *** FOR TESTING ONLY ***
**********************************************************************
*
DUMPDATA LA    R8,NEWDATA         POINT TO RECS READ FROM DATA SPACE
         LA    R10,0              \  SET BG ADDR OF DATA SPACE TO
         ST    R10,DMBGADR        /  DUMP
         LA    R10,4095           \  SET END ADDR OF DATA SPACE TO
         ST    R10,DMENDADR       /  DUMP
         OI    DMENDADR,X'80'     INDICATE END OF ADDRESS LIST
         MVC   DMSTOKEN,DS01TOK   PUT DATA SPACE TOKEN INTO ADDR LIST
         ABEND 999,DUMP,DUMPOPX=DUMPLST
*
*
**********************************************************************
* THIS ROUTINE ABENDS THE PROGRAM USING USERCODE IN REGISTER 10.
**********************************************************************
*
ABEND    ABEND (R10),DUMP
*
*
**********************************************************************
* THIS ROUTINE RESTORES THE REGISTERS AND RETURNS CONTROL.
**********************************************************************
*
RETURN   RCNTL RC=0
*
*
**********************************************************************
* WORKING STORAGE AND CONSTANTS
**********************************************************************
*
DS01TOK  DS    D
DS01ALET DS    F
DS01NAME DC    C'D01$$$$$'
DS01GNAM DS    CL8
MAXBYTES EQU   1000000
INITBYTS EQU   250000
MAXBLKS  DC    A((MAXBYTES+4095)/4096)
INITBLKS DC    A((INITBYTS+4095)/4096)
CURRBLKS DS    CL4
DSBEGADR DS    F
*
*
**********************************************************************
* INITIAL TEST DATA COPIED INTO THE DATA SPACE
**********************************************************************
*
DATA01   DC    10CL1'A'
DATA02   DC    50CL1'B'
DATA03   DC    25CL1'C'
DATA04   DC    20CL1'D'
*
*
**********************************************************************
* TEST DATA USED TO UPDATE FIELDS IN THE DATA SPACE
**********************************************************************
*
DATA11   DC    10CL1'1'
DATA12A  DC    10CL1'0'
DATA12B  DC    20CL1'X'
DATA12C  DC    5CL1'Y'
DATA12D  DC    15CL1'Z'
DATA13   DC    25CL1'3'
DATA14   DC    20CL1'4'
*
*
**********************************************************************
* FIELDS IN ADDRESS SPACE IN WHICH DATA IS READ FROM THE DATA SPACE
**********************************************************************
*
NEWDATA  DS    0CL105
DATAX1   DS    CL10
DATAX2   DS    CL50
DATAX3   DS    CL25
DATAX4   DS    CL20
*
*
**********************************************************************
* ADDRESS LIST FOR ABEND MACRO FOR DUMPING DATA FROM THE DATA SPACE
**********************************************************************
*
DUMPLST0 DS    0D
DMBGADR  DS    F
DMENDADR DS    F
DMSTOKEN DS    D
*
DUMPLST  SNAPX SDATA=SUM,DSPSTOR=DUMPLST0,MF=L
*
*
**********************************************************************
* MAPPING DSECT FOR THE FIELDS IN THE DATA SPACE
**********************************************************************
*
DSRECORD DSECT
DSFLD01  DS    CL10
DSFLD02  DS    0CL50
DSFLD02A DS    CL10
DSFLD02B DS    CL20
DSFLD02C DS    CL5
DSFLD02D DS    CL15
DSFLD03  DS    CL25
DSFLD04  DS    CL20
*
*
**********************************************************************
* END OF PROGRAM
**********************************************************************
*
         END
//*
//*
//G.SYSUDUMP DD SYSOUT=*
//
