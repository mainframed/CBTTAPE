*
* CODING EXAMPLE 12.6.5
*
*
IGC0025E CSECT
*
*
***********************************************************************
*    INITIALIZATION
***********************************************************************
*
         REGEQU EQU=R                  GENERATE EQU'S FOR REGISTERS
         USING *,R6                    SETUP ADDRESSABILITY
*
*
***********************************************************************
*    MAINSTREAM OF SVC ROUTINE
***********************************************************************
*
         BAL   R8,SAVEREGS             SAVE REGISTERS
         BAL   R8,DOREQ                DO PAGE FIX/FREE
         B     RETURN                  RETURN TO CALLING PROG VIA FLIH
*
*
***********************************************************************
*    THIS ROUTINE SAVES THE CONTENTS OF ALL REGISTERS WHICH CONTAIN
*    CONTROL INFORMATION.
***********************************************************************
*
SAVEREGS LR    R12,R14                 SAVE RETURN ADR
         LR    R10,R0                  SAVE PG FIX/FREE INDICATOR
         LR    R11,R1                  SAVE PG FIX/FREE ADR LIST
         WTO   '*** SVC 255 ENTERED ***',ROUTCDE=11    * TESTING ONLY *
         BR    R8                      RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE PERFORMS THE PAGE-FIX/FREE AS REQUESTED BY THE CALLER
*    OF THIS SVC ROUTINE. THE REQUESTED ACTION IS SPECIFIED IN REGISTER
*    0. THE SAVEREGS ROUTINE SAVES REGISTER 0 IN REGISTER 10. X'00'
*    INDICATES A PAGE-FIX, X'04' INDICATES A PAGE FREE. IF THE CALLING
*    PROGRAM SPECIFIES AN INVALID INDICATOR, THEN AN RC OF X'04' IS
*    SET AND CONTROL IS RETURNED TO THE CALLER WITHOUT THE REQUESTED
*    SERVICE BEING PERFORMED. THE BEGINNING AND ENDING ADDRESSES OF THE
*    PAGE-FIX/FREE ARE SPECIFIED AS TWO CONSECUTIVE FULLWORDS POINTED
*    TO BY REGISTER 1. THE SAVEREGS ROUTINE SAVES REGISTER 1 IN
*    REGISTER 11.
***********************************************************************
*
DOREQ    L     R2,0(0,R11)             LOAD BG ADR TO FIX/FREE
         L     R9,4(0,R11)             LOAD END ADR TO FIX/FREE
         C     R10,BIN0                CHK FIX/FREE IND
         BE    DOPGFIX                 IF 0, DO PAGE FIX
         C     R10,BIN4                CHK FIX/FREE IND
         BE    DOPGFREE                IF 4, DO PAGE FREE
         LA    R15,4                   IF NOT 0 OR 4, SET INV IND
         BR    R8                      RETURN TO CALLING RTN
DOPGFIX  PGFIX R,A=(R2),EA=(R9),ECB=0
         BR    R8                      RETURN TO CALLING RTN
DOPGFREE PGFREE R,A=(R2),EA=(R9)
         BR    R8                      RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE RESTORES THE RETURN ADDRESS INTO REGISTER 14 AND
*    RETURNS CONTROL TO THE FLIH.
***********************************************************************
*
RETURN   LR    R14,R12                 RESTORE RETURN ADR
         BR    R14                     RETURN TO CALLING PROG VIA FLIH
*
*
***********************************************************************
*    CONSTANTS
***********************************************************************
*
BIN0     DC    F'0'                    PAGE FIX INDICATOR
BIN4     DC    F'4'                    PAGE FREE INDICATOR
*
*
***********************************************************************
*    END OF SVC ROUTINE
***********************************************************************
*
         END
*
*
*
*
***********************************************************************
*    CODE TO INVOKE THE SVC IN CODING EXAMPLE 12.6.4.
***********************************************************************
         ...
         LA    R0,0                    SET PAGE-FIX REQUEST
         ST    R10,BGADDR              STOR BG ADR
         ST    R11,ENDADDR             STOR END ADR
         LA    R1,ADDRSPTR             LOAD ADR OF BG/END ADR-LIST
         SVC   255                     ISSUE SVC TO PAGE FIX
         LTR   R15,R15                 CHECK RC
         BNZ   NOPGFIX                 IF NON-ZERO, BAD PGFIX
         ...                           PROCESS
ADDRSPTR DS    0F
BGADDR   DS    F
ENDADDR  DS    F

