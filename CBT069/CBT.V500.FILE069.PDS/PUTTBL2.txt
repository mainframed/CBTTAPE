*
* CODING EXAMPLE 3.3.4
*
*
         MACRO
&LABEL   PUTTBL &TABLE,&ENTRY,&ENTLEN,&KEYLEN
&LABEL   LA    14,&TABLE              LOAD ADR OF BG OF TBL INTO REG 14
$SCANTBL CLI   0(14),X'00'            CHK FOR VACANT SPOT IN TBL
         BE    $INSTBL                IF YES, BRANCH TO INSERT RTN
         CLC   0(&KEYLEN,14),&ENTRY   CHK FOR DUPL ENTRY
         BE    $UPDTBL                IF YES, BRANCH TO UPDATE RTN
         CLI   0(14),X'FF'            CHK FOR END OF TBL
         BE    $TBLEND                IF YES, BRANCH TO TBLEND RTN
         LA    14,&ENTLEN.(0,14)      INCR TO NEXT TABLE SLOT
         B     $SCANTBL               BRANCH BACK TO BG OF LOOP
$INSTBL  MVC   0(&ENTLEN,14),&ENTRY   MOVE NEW ENTRY INTO TBL
         LA    15,0                   NEW ENTRY, SET RC = 0
         B     $PTBLEND               BRANCH OUT OF MACRO
$UPDTBL  MVC   0(&ENTLEN,14),&ENTRY   REPLACE DUPL ENTRY
         LA    15,4                   REPLACE ENTRY, SET RC = 4
         B     $PTBLEND               BRANCH OUT OF MACRO
$TBLEND  LA    15,8                   END OF TBL, SET RC = 8
$PTBLEND DS    0H
         MEND
