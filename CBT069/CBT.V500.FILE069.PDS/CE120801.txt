*
* CODING EXAMPLE 12.8.1
*
*
LOADSVC  CSECT
*
*
***********************************************************************
*    INITIALIZATION
***********************************************************************
*
         INITL 3,EQU=R
*
*
***********************************************************************
*    MAINSTREAM OF PROGRAM
***********************************************************************
*
         BAL   R6,GETPARM          EXTRACT PARM DATA
         BAL   R6,VERSVCNO         VERIFY SVC NUMBER
         BAL   R6,CNVSVCNO         CONVERT SVC NUMBER TO BINARY
         BAL   R6,SETMODE          SET PROGRAM TO SUPV STATE, KEY ZERO
         BAL   R6,LOADNSVC         LOAD NEW SVC INTO CSA
         BAL   R6,SETNWSVC         STORE NEW SVC EPA INTO SVC TBL
         B     RETURN              RETURN TO MVS OR CALLING PROGRAM
*
*
***********************************************************************
*    THIS ROUTINE OBTAINS THE SPECIFIED SVC NUMBER AND SVC ROUTINE NAME
*    FROM THE PARM PARAMETER OF THE JCL EXEC STATEMENT.
***********************************************************************
*
GETPARM  L     R1,0(0,R1)          LOAD ADR OF JCL PARM AREA
         LH    R15,0(0,R1)         LOAD LEN OF SPECIFIED PARM
         LTR   R15,R15             CHK IF PARM SPECIFIED
         BZ    NOPARM              IF LEN=0, NO PARM SPECIFIED
         BCTR  R15,0               DECR PARM LEN FOR EX INSTR
         EX    R15,MOVE            MOVE PARM INTO PROC AREA
         BR    R6                  RETURN TO CALLING RTN
MOVE     MVC   SVCINFO(0),2(R1)    MOVE INSTR FOR EX
NOPARM   WTO   '*** SVC NUMBER AND NAME NOT SPECIFIED---PROGRAM TERMINA-
               TED ***',ROUTCDE=11
         ABEND 901
*
*
***********************************************************************
*    THIS ROUTINE VERIFIES THAT THE SPECIFIED SVC NUMBER IS A DECIMAL
*    NUMBER FROM 200 THROUGH 255.
***********************************************************************
*
VERSVCNO LA    R0,3                SET NUMB OF DIGITS IN SVC NUMB
         LA    R10,DECSVCNO        LD ADR OF AREA WITH SVC NUMB
NXTSVCDG CLI   0(R10),C'0'         CHK IF A DIGIT IS LESS THAN 0
         BL    INVSVCNO            IF YES, INVALID SVC NUMB
         CLI   0(R10),C'9'         CHK IF A DIGIT IS MORE THAN 9
         BH    INVSVCNO            IF YES, INVALID SVC NUMB
         LA    R10,1(0,R10)        INCR TO NEXT SVC NUMB DIGIT
         BCT   R0,NXTSVCDG         CHK NEXT DIGIT UNTIL ALL 3 CHECKED
         CLC   DECSVCNO,=C'200'    CHK IF SVC NUMB IS LESS THAN 200
         BL    INVSVCNO            IF YES, INVALID SVC NUMB
         CLC   DECSVCNO,=C'255'    CHK IF SVC NUMB IS GREATER THAN 255
         BH    INVSVCNO            IF YES, INVALID SVC NUMB
         BR    R6                  RETURN TO CALLING RTN
INVSVCNO WTO   '*** INVALID SVC NUBER SPECIFIED---PROGRAM TERMINATED **-
               *',ROUTCDE=11
         ABEND 902
*
*
***********************************************************************
*    THIS ROUTINE CONVERTS THE SPECIFIED DECIMAL SVC NUMBER INTO
*    BINARY.
***********************************************************************
*
CNVSVCNO PACK  PKSVCNO,DECSVCNO    PACK SPECIFIED ZONED DEC SVC NUMB
         CVB   R10,PKSVCNO         CONV SVC NUMB INTO BINARY
         ST    R10,SVCNUM          SAVE BIN SVC NUMB
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE PUTS THE TASK INTO SUPERVISOR STATE AND PSW
*    PROTECTION KEY ZERO VIA THE MODESET MACRO. THIS IS REQUIRED TO
*    ISSUE THE SVCUPDTE MACRO.
***********************************************************************
*
SETMODE  MODESET MODE=SUP,KEY=ZERO
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE LOADS THE NEW SVC ROUTINE INTO CSA. THE LOAD MACRO
*    SPECIFIES THAT ONLY THE LIBRARY POINTED TO BY THE DDNAME SPECIFIED
*    IN THE SVCLIB DCB IS TO BE SEARCHED FOR THE NEW SVC ROUTINE.
***********************************************************************
*
LOADNSVC BAL   R7,OPENLIB          OPEN LOADLIB FOR LOAD MACRO
         LOAD  EPLOC=SVCNAME,GLOBAL=(YES,P),DCB=SVCLIB,LSEARCH=YES
         ST    R0,SVCEPA           SAVE EP ADR OF NEW SVC
         BAL   R7,CLOSELIB         CLOSE LOADLIB
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE OPENS THE LIBRARY WHICH CONTAINS THE NEW SVC ROUTINE.
***********************************************************************
*
OPENLIB  OPEN  SVCLIB              OPEN PRIV LOADLIB THAT HAS SVC RTN
         BR    R7                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE CLOSES THE LIBRARY WHICH CONTAINS THE NEW SVC
*    ROUTINE.
***********************************************************************
*
CLOSELIB CLOSE SVCLIB              CLOSE PRIV LOADLIB
         BR    R7                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE ISSUES THE SVCUPDTE MACRO TO PUT THE ENTRY POINT
*    ADDRESS OF THE NEW SVC ROUTINE INTO THE PROPER ENTRY IN THE SVC
*    TABLE AND TO INDICATE THAT THE SVC ROUTINE IS A TYPE-3 SVC.
***********************************************************************
*
SETNWSVC L     R10,SVCEPA          LOAD EP ADR OF SVC RTN
         L     R11,SVCNUM          LOAD SVC NUMBER
         ENQ   (QNAME,RNAME,E,,SYSTEM)
         SVCUPDTE (R11),REPLACE,TYPE=3,EP=(R10)
         DEQ   (QNAME,RNAME,,SYSTEM)
         LTR   R15,R15             CHK IF SVC TBL UPDATE OK
         BZR   R6                  IF YES, RET TO CALLING RTN
         WTO   '*** ERROR UPDATING SVC TABLE---PROGRAM TERMINATED ***'
         ABEND 903,DUMP
*
*
***********************************************************************
*    THIS ROUTINE RESTORES THE REGISTERS AND RETURNS CONTROL.
***********************************************************************
*
RETURN   RCNTL RC=0                RETURN TO MVS OR CALLING PROG
*
*
***********************************************************************
*    DC/DS STATEMENTS
***********************************************************************
*
PKSVCNO  DS    D
SVCNUM   DS    F
SVCEPA   DS    F
SVCINFO  DS    0CL12
DECSVCNO DS    CL3
         DS    CL1
SVCNAME  DC    CL8' '
*
*
***********************************************************************
*    QNAME/RNAME FOR ENQ/DEQ FOR SVCUPDTE MACRO
***********************************************************************
*
QNAME    DC    CL8'SYSZSVC'
RNAME    DC    C'TABLE'
*
*
***********************************************************************
*    THE DCB FOR THE SVC ROUTINE LIBRARY USED BY THE LOAD MACRO
***********************************************************************
*
SVCLIB   DCB   DSORG=PO,MACRF=R,DDNAME=SVCLIB
*
*
***********************************************************************
*    THE DSECTS
***********************************************************************
*
         CVT   DSECT=YES           GENERATES CVT DSECT REQUIRED BY
*                                  SVCUPDTE MACRO INSTRUCTION
*
*
***********************************************************************
*    END OF PROGRAM
***********************************************************************
*
         END
