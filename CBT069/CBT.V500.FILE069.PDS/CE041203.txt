*
* CODING EXAMPLE 4.12.3
*
*
WTOR03   XC    ECB03,ECB03         CLEAR ECB FOR WTOR
         WTO   '*** ERRORS DETECTED IN PARAMETERS FOR JOB jjjjjjjj ***'
         WTO   '*** SHOULD PROCESSING CONTINUE WITH DEFAULTS ... ***'
         WTOR  '*** REPLY "Y" FOR YES, OR "T" TO TERMINATE JOB ***',   -
               REPLY03,1,ECB03
SETWAIT  STIMER WAIT,DINTVL=WR3WTTME
         TM    ECB03,X'40'         CHECK IF ECB POSTED
         BO    CHKREPLY            IF YES, CHECK REPLY
         WTO   '*** OPERATOR, PLEASE REPLY TO OUTSTANDING MESSAGE FOR J-
               OB: xxxxxxxx ***',DESC=11
         B     SETWAIT             REISSUE WAIT
CHKREPLY CLI   REPLY03,C'Y'        CHK IF DEFAULT REQ
         BE    SETDFLT             IF YES, IND IT
         CLI   REPLY03,C'T'        CHK IF TERMINATION REQ
         BE    SETTERM             IF YES, IND IT
         WTO   '*** INVALID REPLY ***'
         B     WTOR03              REISSUE WTOR
SETDFLT  MVI   DFLTSW,C'1'         IND DEFAULT USAGE
         BR    R6                  RETURN TO CALLING RTN
SETTERM  MVI   TERMSW,C'1'         IND TERMINATION
         BR    R6                  RETURN TO CALLING RTN
         ...
ECB03    DS    F
REPLY03  DS    CL1
DFLTSW   DC    C'0'
TERMSW   DC    C'0'
         DS    0D
WR3WTTME DC    C'00001500'         15 SECONDS WAIT TIME
