*
* CODING EXAMPLE 4.12.2
*
*
         BAL   R6,WTOR02           ISSUE OUTSTANDING WTOR
CONTIN   BAL   R6,PROCESS          PROCESS A UNIT OR WORK
         BAL   R6,TESTWR02         CHECK IF WTOR HAS REPLY
         B     CONTIN              CONTINUE PROCESSING
*
WTOR02   XC    ECB02,ECB02         CLEAR ECB FOR WTOR
         L     R10,=A(L'REPLY02)   LOAD REPLY LEN IN R10 FOR WTOR
         WTOR  '*** REPLY "S" FOR ORDERLY SHUTDOWN ***',REPLY02,(R10), -
               ECB02
         BR    R6                  RETURN TO CALLING RTN
*
PROCESS  CLI   SDSW,C'1'           CHECK IF SHUTDOWN REQUESTED
         BE    SHUTDOWN            IF YES, GO TO SHUTDOWN RTN
         ...                       IF NO, PERFORM NORMAL PROCESSING
         BR    R6                  RETURN TO CALLING RTN
*
SHUTDOWN DS    0H
         ...                       PERFORM SHUTDOWN PROCESSING
*
TESTWR02 TM    ECB02,X'40'         CHECK IF WTOR ECB POSTED
         BO    CHKREPLY            IF YES, CHECK REPLY
         BR    R6                  IF NO, RETURN TO CALLING RTN
CHKREPLY CLI   REPLY02,C'S'        CHK IF SHUTDOWN REQ
         BE    SETSD               IF YES, IND IT
         CLI   REPLY02,X'A2'       CHECK FOR LOWER CASE "S'
         BE    SETSD               IF YES, IND IT
         WTO   '*** INVALID REPLY ***'
         ST    R6,SAVR6            SAVE RETURN ADDRESS
         BAL   R6,WTOR02           REISSUE WTOR
         L     R6,SAVR6            RESTORE RETURN ADDRESS
         BR    R6                  RETURN TO CALLING RTN
SETSD    MVI   SDSW,C'1'           INDICATE SHUTDOWN REQUESTED
         BR    R6                  RETURN TO CALLING RTN
*
ECB02    DS    F
SAVR6    DS    F
REPLY02  DS    CL1
SDSW     DC    C'0'
