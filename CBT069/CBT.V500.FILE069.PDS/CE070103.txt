*
* CODING EXAMPLE 7.1.3
*
*
CHKBLKSZ CSECT
*
*
***********************************************************************
*    INITIALIZATION
***********************************************************************
*
         INITL 3,EQU=R             INITIALIZE PROGRAM
         USING INFMJFCB,R10        DEFINE REG FOR JFCB DSECT
         USING IHADCB,R11          DEFINE REG FOR DCB DESCT
*
*
***********************************************************************
*    MAINSTREAM OF PROGRAM
***********************************************************************
*
         BAL   R6,RDJFCB           READ JFCB INTO PROG AREA
         BAL   R6,VALBLKSZ         VALIDATE BLKSZ SPEC ON JCL STMT
         BAL   R6,MDFYJFCB         SET DEFAULT BLKSZ
         BAL   R6,OPEN             OPEN DCB, CHK IF JFCB MDFY REQR
         BAL   R6,PROCESS          PROCESS DATASET
         B     RETURN              RETURN TO MVS OR CALLING PROG
*
*
***********************************************************************
*    THIS ROUTINE READS THE JFCB AND COPIES IT FROM THE SWA INTO THE
*    PROGRAMMER-SUPPLIED AREA.
***********************************************************************
*
RDJFCB   RDJFCB FILEDCB
         LTR   R15,R15             CHK IF DD STMT IN JCL
         BNZ   NODD                IF RC NOT 0, DD STMT MISSING
         BR    R6                  RETURN TO CALLING RTN
NODD     WTO   '*** FILEDD NOT SPECIFIED IN JCL ***',ROUTCDE=11
         ABEND 901
*
*
***********************************************************************
*    THIS ROUTINE EXAMINES THE BLKSIZE SPECIFIED IN THE REFERENCED JCL
*    STATEMENT ONLY FOR DISP=NEW DATASETS. IF ONE OF THE FOLLOWING
*    CONDITIONS IS TRUE:
*        * THE BLKSIZE IS OMITTED;
*        * THE BLKSIZE IS NOT A MULTIPLE OF THE LRECL SPECIFIED IN THE
*          DCB MACRO;
*        * THE BLKSIZE IS LESS A PREDEFINED MINIMUM;
*     THEN REGISTER 15 IS SET WITH A NON-ZERO RC TO INDICATE THAT THE
*     SPECIFIED BLKSIZE IS TO BE REPLACED WITH THE DEFAULT BLKSIZE.
***********************************************************************
*
VALBLKSZ LA    R10,JFCB            SET ADRBLTY FOR JFCB DSECT
         LA    R11,FILEDCB         SET ADRBLTY FOR DCB DSECT
         TM    JFCBIND2,JFCNEW     CHK IF DISP=NEW ON JCL STMT
         BO    NEWDS               IF YES, CHK BLKSZ
         LA    R15,0               IF NO, INDICATE BLKSZ OK
         BR    R6                  RETURN TO CALLING RTN
NEWDS    CLC   JFCBLKSI,BINZEROS   CHK IF BLKSZ IN JCL
         BNE   CHKBSZ1             IF YES, CHK IT
         LA    R15,4               IF NO, IND BLKSZ MOD REQR
         BR    R6                  RETURN TO CALLING RTN
CHKBSZ1  LH    R15,JFCBLKSI        LOAD R15 WITH BLKSZ FROM JCL
         LA    R14,0               SET R14 TO ZERO FOR DR INSTR
         LH    R12,DCBLRECL        LOAD R12 WITH LRECL FROM PROG
         DR    R14,R12             DIVIDE LRECL INTO BLKSZ
         LTR   R14,R14             CHK IF BLKSZ MULT OF LRECL (REMDR=0)
         BZ    CHKBSZ2             IF YES, CHK MIN BLKSZ
         LA    R15,4               IF NO, IND BLKSZ MOD REQR
         BR    R6                  RETURN TO CALLING RTN
CHKBSZ2  CLC   JFCBLKSI,MINBLKSZ   COMP JCL BLKSZ TO MIN BLKSZ
         BNL   BLKSZOK             OK, IF JCL BLKSZ EQ OR GT MIN BLKSZ
         LA    R15,4               IF LT MIN, IND BLKSZ MOD REQR
         BR    R6                  RETURN TO CALLING RTN
BLKSZOK  LA    R15,0               IND BLKSZ OK
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE MODIFIES THE JFCB IF REQUIRED (RC IN REGISTER 15 IS
*    NON-ZERO). IF REQUIRED, THE DEFAULT BLKSIZE IS PUT INTO THE JFCB
*    AND OVERRIDES THE ONE SPECIFIED IN THE JCL (IF ANY).
***********************************************************************
*
MDFYJFCB LTR   R15,R15             CHK IF BLKSZ MOD REQUIRED
         BZR   R6                  IF NO, RET TO CALLING RTN
         MVC   JFCBLKSI,DFLTBSZ    IF YES, USE DEFAULT BLKSZ
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE OPENS THE DCB. IF THE RC IN REGISTER 15 IS NON-ZERO,
*    THEN THE PROGRAMMER-SUPPLIED JFCB IS USED WITH THE NEW BLKSIZE.
***********************************************************************
*
OPEN     LTR   R15,R15             CHK IF BLKSZ MOD REQUIRED
         BNZ   OPENJ               IF YES, USE MOD JFCB FOR OPEN
         OPEN  (FILEDCB,(OUTPUT))
         B     CHKOPEN
OPENJ    OPEN  (FILEDCB,(OUTPUT)),TYPE=J
         B     CHKOPEN
CHKOPEN  TM    DCBOFLGS,DCBOFOPN   CHK FOR GOOD OPEN
         BNO   OPENBAD             IF NOT 1, OPEN UNSUCCESSFUL
         BR    R6                  RETURN TO CALLING RTN
OPENBAD  WTO   '*** OPEN ERROR FOR FILEDD ***',ROUTCDE=11
         ABEND 902
*
*
***********************************************************************
*    THIS ROUTINE DOES THE ACTUAL PROCESSING.
***********************************************************************
*
PROCESS  ...
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE RESTORES THE REGISTERS AND RETURNS CONTROL.
***********************************************************************
*
RETURN   RCNTL RC=0                RETURN TO MVS OR CALLING PROG
*
*
***********************************************************************
*    CONSTANTS
***********************************************************************
*
BINZEROS DC    H'0'
MINBLKSZ DC    H'4080'             MINIMUM BLKSIZE
DFLTBSZ  DC    H'4080'             DEFAULT BLKSIZE
*
*
***********************************************************************
*    THE JFCB-LIST AND THE DCB
***********************************************************************
*
JFCB     DS    44F
JFCBPTR  DC    X'87'
         DC    AL3(JFCB)
*
FILEDCB  DCB   DSORG=PS,MACRF=PM,EXLST=JFCBPTR,DDNAME=FILEDD,          -
               LRECL=80,RECFM=FB
*
*
***********************************************************************
*    THE DSECTS
***********************************************************************
*
         DSECT
         IEFJFCBN              GENERATES JFCB DSECT
*
         DCBD  DSORG=PS        GENERATES PS-DCB DSECT
*
*
**************************************************************************
*    END OF PROGRAM
**************************************************************************
*
         END
