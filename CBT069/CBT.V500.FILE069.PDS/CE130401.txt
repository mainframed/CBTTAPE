*
* CODING EXAMPLE 13.4.1
*
*
ALLOCSA1 CSECT
*
*
***********************************************************************
*    INITIALIZATION
***********************************************************************
*
         INITL 3,EQU=R             INITIALIZE PROGRAM
         USING R10,CVT             DEFINE REG FOR CVT DSECT
*
*
***********************************************************************
*    MAINSTREAM OF PROGRAM
***********************************************************************
*
         BAL   R6,GETCSA           ALLOC STORAGE FROM CSA
         BAL   R6,SETMODE          SET PROG INTO SUPV STATE, KEY ZERO
         BAL   R6,SETCVT           PUT CSA STRG ADR INTO CVT USER FLD
         B     RETURN              RETURN TO MVS OR CALLING PROG
*
*
***********************************************************************
*    THIS ROUTINE ALLOCATES NON-FETCH-PROTECTED VIRTUAL STORAGE FROM
*    THE CSA AND ASSIGNS IT THE SAME PSW KEY AS THE ISSUING TASK.
***********************************************************************
*
GETCSA   GETMAIN EC,LV=2048,SP=241,A=CSAADR
         LTR   R15,R15             CHECK FOR SUCCESSFUL GETMAIN
         BNZ   NOCSA               IF NON-ZERO, CSA STRG NOT AVAIL
         BR    R6                  RETURN TO CALLING RTN
NOCSA    WTO   '*** CSA STORAGE NOT AVAILABLE ***',ROUTCDE=11
         LA    R15,4               SET UNSUCCESSFUL RC INDICATION
         B     RETURNX             RETURN TO MVS WITH BAD RC
*
*
***********************************************************************
*    THIS ROUTINE PUTS THE TASK INTO SUPERVISOR STATE AND PSW
*    PROTECTION KEY ZERO VIA THE MODESET MACRO. THIS IS REQUIRED TO
*    MOVE THE ADDRESS OF THE ALLOCATED CSA INTO THE CVT USER-FIELD.
***********************************************************************
*
SETMODE  MODESET MODE=SUP,KEY=ZERO
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE PLACES THE ADDRESS OF THE ALLOCATED CSA INTO THE
*    CVT USER-FIELD.
***********************************************************************
*
SETCVT   L     R10,16              LOAD REG 10 WITH CVT ADR
         MVC   CVTUSER,CSAADR      MOVE CSA STRG ADR INTO CVT USER FLD
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE RESTORES THE REGISTERS AND RETURNS CONTROL.
***********************************************************************
*
RETURN   LA    R15,0               SET RC=0
RETURNX  L     R13,4(0,R13)        RESTORE PREV SA ADR
         L     R14,12(0,R13)       RESTORE REG 14
         LM    R0,R12,20(R13)      RESTORE REGS 0-12, R15 HAS RC
         BR    R14                 RETURN TO MVS OR CALLING PROG
*
CSAADR   DS    F
*
*
***********************************************************************
*    THE DSECTS
***********************************************************************
*
         PRINT NOGEN
         CVT   DSECT=YES           GENERATES CVT DSECT
*
*
***********************************************************************
*    END OF PROGRAM
***********************************************************************
*
         END
