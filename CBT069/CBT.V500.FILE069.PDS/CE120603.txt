*
* CODING EXAMPLE 12.6.3
*
*
IGC253   CSECT
*
*
***********************************************************************
*    INITIALIZATION
***********************************************************************
*
         REGEQU EQU=R                  DEFINE EQU'S FOR REGISTERS
         USING *,R6                    SETUP ADDRESSABILITY
         USING TCB,R4                  DEFINE REG FOR TCB DSECT
*
*
***********************************************************************
*    MAINSTREAM OF SVC ROUTINE
***********************************************************************
*
         BAL   R8,SAVEREGS             SAVE REGISTERS
         BAL   R8,GETCSA               GET REQUESTED CSA STORAGE
         B     RETURN                  RETURN TO CALLING PROG VIA FLIH
*
*
***********************************************************************
*    THIS ROUTINE SAVES THE CONTENTS OF ALL REGISTERS WHICH CONTAIN
*    CONTROL INFORMATION.
***********************************************************************
*
SAVEREGS LR    R12,R14                SAVE RETURN ADDRESS
         LR    R10,R0                 SAVE REQUESTED LENGTH OF CSA ALLO
         BR    R8                     RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE ALLOCATES FETCH-PROTECTED VIRTUAL STORAGE FROM THE
*    CSA AND ASSIGNES IT THE PSW STORAGE PROTECTION KEY OF THE CALLER
*    OF THE SVC ROUTINE. THE LENGTH OF THE REQUESTED CSA STORAGE IS
*    SPECIFIED VIA REGISTER 0 BY THE CALLER OF THE SVC. THE SAVEREGS
*    ROUTINE SAVES REGISTER 0 IN REGISTER 10.
***********************************************************************
*
GETCSA   IC    R2,TCBPKF               GET PSW PROT KEY OF CALLER
         GETMAIN RC,LV=(R10),SP=231,BRANCH=YES,KEY=(R2)
         LR    R11,R1                  SAVE ADDRESS OF CSA STORAGE
         LR    R9,R15                  SAVE RC FROM GETMAIN
         BR    R8                      RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE LOADS THE PARAMETER REGISTERS, RESTORES THE RETURN
*    ADDRESS INTO REGISTER 14 AND RETURNS CONTROL TO THE FLIH.
***********************************************************************
*
RETURN   LR    R1,R11                  LOAD ALLO CSA ADR IN A PARM REG
         LR    R15,R9                  LOAD RC IN A PARM REG
         LR    R14,R12                 RESTORE RETURN ADDRESS
         BR    R14                     RETURN TO CALLING PROG VIA FLIH
*
*
***********************************************************************
*    THE DSECTS
***********************************************************************
*
         IKJTCB                        GENERATES TCB DSECT
*                                      REQR BY BRANCH-ENTRY GETMAIN
*
***********************************************************************
*    END OF SVC ROUTINE
***********************************************************************
*
         END
