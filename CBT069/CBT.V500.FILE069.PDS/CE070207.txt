*
* CODING EXAMPLE 7.2.7
*
*
SVC99INR CSECT
*
*
***********************************************************************
*    INITIALIZATION
***********************************************************************
*
         INITL 3,EQU=R             INITIALIZE PROGRAM
         USING S99RBP,R10          DEFINE REG FOR REG BLK PTR DSECT
         USING S99RB,R11           DEFINE REG FOR REQ BLOCK DSECT
*
*
***********************************************************************
*    MAINSTREAM OF PROGRAM
***********************************************************************
*
         BAL   R6,SETRB007         SETUP RB FOR DYNM INFO RETV
         BAL   R6,SETRBPTR         SETUP RB PTR
NEXTINFO BAL   R6,SETRELNO         INCR REL NO AND STORE INTO TU
         BAL   R6,DOSVC99          ISSUE SVC 99
         BAL   R6,RETVINFO         OBTAIN DDN/DSN INFO
         BAL   R6,PROCESS          PROCESS DDN/DSN INFO
         B     NEXTINFO            GET NEXT DDN/DSN INFO
INFOEND  B     RETURN              RETURN TO MVS OR CALLING PROG
*
*
***********************************************************************
*    THIS ROUTINE SETS UP THE SVC99 REQUEST BLOCK.
***********************************************************************
*
SETRB007 LA    R11,REQBLK          SET ADRBLTY FOR RB DSECT
         XC    REQBLK,REQBLK       CLEAR RB
         MVI   S99RBLN,REQBLKLN    SET RB LEN
         MVI   S99VERB,S99VRBIN    SET DYNM INFO RETV VERB
         LA    R10,TUPTR007        LD ADR OF BG OF TEXT-UNIT-PTR LIST
         ST    R10,S99TXTPP        STORE THAT ADR INTO RB
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE SETS UP THE SVC99 REQUEST-BLOCK-POINTER.
***********************************************************************
*
SETRBPTR LA    R10,RBPTR           SET ADRBLTY FOR RBPTR DSECT
         LA    R11,REQBLK          LD ADR OF RB
         ST    R11,S99RBPTR        STORE RB ADR INTO RB PTR
         OI    S99RBPTR,S99RBPND   TURN HIGH ORDER BIT
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE INCREMENTS THE RELATIVE NUMBER OF THE ALLOCATION AND
*    STORES THE NEW RELATIVE NUMBER INTO THE RELATIVE-NUMBER TEXT UNIT.
***********************************************************************
*
SETRELNO L     R12,RELNO           LD LAST REL NUMB
         LA    R12,1(0,R12)        INCR REL NUMB
         ST    R12,RELNO           SAVE CUR REL NUMB
         ST    R12,I2RELNO         STORE CUR REL NUMB INTO TU
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE ISSUES SVC 99 VIA THE DYNALLOC MACRO.
***********************************************************************
*
DOSVC99  LA    R1,RBPTR            LD ADR OF RB PTR INTO R1 FOR SVC 99
         DYNALLOC                  ISSUE SVC 99
         LTR   R15,R15             CHK RC
         BNZ   CHKERR              IF NOT ZERO, CHK ERROR/INFO FIELDS
         BR    R6                  IF OK, RETURN TO CALLING RTN
CHKERR   ...
*
*
***********************************************************************
*    THIS ROUTINE EXTRACTS THE DDNAME AND DATASET NAME OF THE
*    ALLOCATION ASSOCIATED WITH THE SPECIFIED RELATIVE NUMBER.
***********************************************************************
*
RETVINFO MVC   DDNAME,DDNAME-1     CLEAR AREA FOR DDNAME
         MVC   DSNAME,DSNAME-1     CLEAR AREA FOR DSNAME
         LH    R12,I2DDNLEN        LD LEN OF RETURNED DDN
         BCTR  R12,0               DECR LEN FOR EX INSTR
         EX    R12,MOVEDDN         GET DDNAME
         LH    R12,I2DSNLEN        LD LEN OF RETURNED DSN
         BCTR  R12,0               DECR LEN FOR EX INSTR
         EX    R12,MOVEDSN         GET DSNAME
         CLI   I2LESW,X'80'        CHK IND FOR LAST DD STMT
         BE    INFOEND             IF YES, GO TO END-DD-STMT RTN
         BR    R6                  IF NO, RETURN TO CALLING RTN
MOVEDDN  MVC   DDNAME(0),I2DDNAME  MVC INTR FOR EX
MOVEDSN  MVC   DSNAME(0),I2DSNAME  MVC INTR FOR EX
*
*
***********************************************************************
*    THE ROUTINE PROCESSES THE DDN/DSN INFORMATION.
***********************************************************************
*
PROCESS  ...                       PROCESS INFORMATION
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE RESTORES THE REGISTERS AND RETURNS.
***********************************************************************
*
RETURN   RCNTL RC=0                RETURN TO MVS OR CALLING PROG
*
*
***********************************************************************
*    SVC99 REQ-BLOCK-POINTER AND REQUEST BLOCK
***********************************************************************
*
RBPTR    DS    F                   PTR TO SVC 99 REQ BLOCK
REQBLK   DS    CL(S99RBEND-S99RB)  STORAGE FOR REQ BLOCK
REQBLKLN EQU   L'REQBLK            LENGTH OF REQ BLOCK
*
*
***********************************************************************
*    TEXT-UNITS-POINTERS LIST
***********************************************************************
*
TUPTR007 DS    0F
         DC    A(TURLNOI2)         ADR OF TU FOR REL NUMB
         DC    A(TURDDNI2)         ADR OF TU FOR RET-DDN
         DC    A(TURDSNI2)         ADR OF TU FOR RET-DSN
         DC    X'80'               INDICATES LAST TU ADDRESS
         DC    AL3(TULESWI2)       ADR OF TU FOR LAST-DD-STMT IND
*
*
***********************************************************************
*    TEXT UNITS
***********************************************************************
*
TURLNOI2 DC    AL2(DINRELNO),AL2(1),AL2(2)
I2RELNO  DS    CL2                 RELATIVE NUMBER OF ALLOCATION
*
TURDDNI2 DC    AL2(DINRTDDN),AL2(1)
I2DDNLEN DS    CL2                 RETURNED LENGTH OF DDNAME
I2DDNAME DS    CL8                 RETURNED DDNAME
*
TURDSNI2 DC    AL2(DINRTDSN),AL2(1)
I2DSNLEN DS    CL2                 RETURNED LENGTH OF DSNAME
I2DSNAME DS    CL44                RETURNED DSNAME
*
TULESWI2 DC    AL2(DINRTLST),AL2(1),AL2(1)
I2LESW   DS    CL1                 LAST DD-STMT INDICATOR
*
*
***********************************************************************
*    DC/DS STATEMENTS
***********************************************************************
*
RELNO    DC    F'0'                REL NUM FOR DYNAM INFO RETRV
         DC    C' '                USED TO BLANK DDNAME
DDNAME   DS    CL8                 AREA TO SAVE DDN FROM INFO RETV
         DC    C' '                USED TO BLANK DSNAME
DSNAME   DS    CL44                AREA TO SAVE DSN FROM INFO RETV
*
*
***********************************************************************
*    THE DSECTS
***********************************************************************
*
         IEFZB4D0        GENS DSECT FOR REQ BLOCK, TEXT UNIT, ETC.
         IEFZB4D2        GENS TABLE OF EQUATES FOR TU KEYS
*
*
***********************************************************************
*    END OF PROGRAM
***********************************************************************
*
         END
