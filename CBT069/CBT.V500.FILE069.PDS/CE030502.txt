*
* CODING EXAMPLE 3.5.2
*
*
         MACRO
&LABEL   PUTTBL &TABLE,&ENTRY,&ENTLEN,&KEYLEN
         LCLC   &LBL
&LBL     SETC  '$'.'PTB'.'&SYSNDX'(2,3)    DEFINE LABEL BASE
.*
.* VERIFY THAT BOTH &ENTLEN AND &KEYLEN ARE SELF-DEFINING TERMS
.*
         AIF   (T'&ENTLEN EQ 'N' AND T'&KEYLEN EQ 'N').VRFY255
         MNOTE 4,'SPECIFIED ENTRY-LENGTH OR SPECIFIED KEY-LENGTH NOT VA-
               LID---MACRO NOT GENERATED'
         MEXIT
.*
.* VERIFY THAT BOTH &ENTLEN AND &KEYLEN ARE LESS THAN 256
.*
.VRFY255 AIF   (&ENTLEN LE 255 AND &KEYLEN LE 255).CHKREL
         MNOTE 4,'SPECIFIED ENTRY-LENGTH OR SPECIFIED KEY-LENGTH EXCEED-
               S 255---MACRO NOT GENERATED'
         MEXIT
.*
.* VERIFY THAT &ENTLEN IS GREATER THAN OR EQUAL TO &KEYLEN
.*
.CHKREL  AIF   (&ENTLEN GE &KEYLEN).CHKREGT
         MNOTE 4,'SPECIFIED KEY-LENGTH IS GREATER THAN SPECIFIED ENTRY--
               LENGTH---MACRO NOT GENERATED'
         MEXIT
.*
.* CHECK IF &TABLE IS SPECIFIED AS A REGISTER
.*
.CHKREGT AIF   ('&TABLE'(1,1) EQ '(').REGTBL
         LA    14,&TABLE                LD ADR OF BG OF TBL INTO REG 14
         AGO   .B1
.REGTBL  LR    14,&TABLE(1)             LD ADR OF BG OF TBL INTO REG 14
.B1      ANOP
&LBL.A   CLI   0(14),X'00'              CHK FOR VACANT SPOT IN TBL
         BE    &LBL.B                   IF YES, BRANCH TO INSERT RTN
.*
.* CHECK IF &ENTRY IS SPECIFIED AS A REGISTER
.*
.CHKREGE AIF   ('&ENTRY'(1,1) EQ '(').REGENT1
         CLC   0(&KEYLEN,14),&ENTRY     CHK FOR DUPL ENTRY
         AGO   .B2

.REGENT1 CLC   0(&KEYLEN,14),0(&ENTRY(1)) CHK FOR DUPL ENTRY
.B2      BE    &LBL.C                   IF YES, BRANCH TO UPDATE RTN
         CLI   0(14),X'FF'              CHK FOR END OF TBL
         BE    &LBL.D                   IF YES, BRANCH TO TBLEND RTN
         LA    14,&ENTLEN.(0,14)        INCR TO NEXT TABLE SLOT
         B     &LBL.A                   BRANCH BACK TO BG OF LOOP
         AIF   ('&ENTRY'(1,1) EQ '(').REGENT2
&LBL.B   MVC   0(&ENTLEN,14),&ENTRY     MOVE NEW ENTRY INTO TBL
         AGO   .B3
.REGENT2 ANOP
&LBL.B   MVC   0(&ENTLEN,14),0(&ENTRY(1)) MOVE NEW ENTRY INTO TBL
.B3      LA    15,0                     NEW ENTRY, SET RC = 0
         B     &LBL.E                   BRANCH OUT OF MACRO
         AIF   ('&ENTRY'(1,1) EQ '(').REGENT3
&LBL.C   MVC   0(&ENTLEN,14),&ENTRY     REPLACE DUPL ENTRY
         AGO   .B4
.REGENT3 ANOP
&LBL.C   MVC   0(&ENTLEN,14),0(&ENTRY(1)) REPLACE DUPL ENTRY
.B4      LA    15,4                     REPLACE ENTRY, SET RC = 4
         B     &LBL.E                   BRANCH OUT OF MACRO
&LBL.D   LA    15,8                     END OF TBL, SET RC = 8
&LBL.E   DS    0H
         MEND
