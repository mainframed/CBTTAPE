*
* CODING EXAMPLE 5.13.1
*
*
         CSECT
*
*
***********************************************************************
*    INITIALIZATION
***********************************************************************
*
         INITL 3,EQU=R                INITIALIZE PROGRAM
*
*
***********************************************************************
*    MAINSTREAM OF PROGRAM
***********************************************************************
*
         BAL   R6,OPEN                OPEN JES2 INTERNAL READER
         BAL   R6,ISSUCMDS            ISSUE COMMANDS
         BAL   R6,CLOSE               CLOSE INTERNAL READER
         B     RETURN                 RETURN TO MVS OR CALLING PROG
*
*
***********************************************************************
*    THIS ROUTINE OPENS THE JES2 INTERNAL READER.
***********************************************************************
*
OPEN     OPEN  (JES2IRDR,(OUTPUT))    OPEN IRDR
         ...                          CHK FOR GOOD OPEN
         BR    R6                     RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE ISSUES ALL THE SPECIFIED COMMANDS IN THE COMMAND
*    TABLE. IN ORDER TO ACCOMPLISH THIS TASK, THIS ROUTINE CALLS THE
*    SUBROUTINES WITH THE FOLLOWING FUNCTIONS:
*        * EXTRACT A COMMAND FROM THE COMMAND TABLE;
*        * WRITE THE COMMAND TO THE JES2 INTERNAL READER.
***********************************************************************
*
ISSUCMDS LA    R10,COMMTBL            LD ADR OF BG OF CMD TBL
NEXTCOMM BAL   R7,GETCOMM             GET A COMMAND FROM TBL
         CLI   0(R10),X'00'           CHK FOR END-OF-TABLE
         BER   R6                     IF END, RET TO CALLING RTN
         BAL   R7,PUTCOMM             IF NOT END, ISSUE COMMAND
         B     NEXTCOMM               GET NEXT CMD FROM TBL
*
*
***********************************************************************
*    THIS SUBROUTINE EXTRACTS THE CURRENT COMMAND ENTRY FROM THE
*    COMMAND TABLE AND MOVES THE COMMAND INTO THE I/O AREA USED BY THE
*    JES2 INTERNAL READER AND THEN INCREMENTS THE POINTER TO THE NEXT
*    COMMAND ENTRY. THE CURRENT COMMAND ENTRY IS POINTED TO BY REGISTER
*    10.
***********************************************************************
*
GETCOMM  MVC   COMMAND,0(R10)         MOVE CMD FROM TBL TO INTRDR BUF
         LA    R10,80(0,R10)          INCR TO NEXT CMD
         BR    R7                     RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS SUBROUTINE WRITES THE COMMAND TO THE JES2 INTERNAL READER.
***********************************************************************
*
PUTCOMM  PUT   JES2IRDR,COMMAND       WRITE CMD TO INTRDR
         BR    R7                     RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE CLOSES THE JES2 INTERNAL READER.
***********************************************************************
*
CLOSE    CLOSE JES2IRDR               CLOSE IRDR
         BR    R6                     RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE RESTORES THE REGISTERS AND RETURNS CONTROL.
***********************************************************************
*
RETURN   RCNTL RC=0                   RETURN TO MVS OR CALLING PROG
*
*
***********************************************************************
*    THE COMMAND TABLE
***********************************************************************
*
COMMTBL  DS    0H
         DC    CL80'/*$DA'            JES2 $DA COMMAND
         DC    CL80'/*$VS,'D A,L'     MVS D A COMMAND
         DC    CL80'/*$VS,'S xxxx'    MVS START COMMAND
         DC    CL80'/*EOF'            REQD ONLY IF INTRDR IS NOT CLOSED
         DC    X'00'                  END-OF-TBL INDICATOR
*
*
***********************************************************************
*    THE I/O AREA AND QSAM DCB FOR THE JES2 INTERNAL READER
***********************************************************************
*
COMMAND  DS    CL80
JES2IRDR DCB   DSORG=PS,MACRF=PM,RECFM=FB,LRECL=80,DDNAME=JESIRDR
*
*
***********************************************************************
*    END OF PROGRAM
***********************************************************************
*
         END
