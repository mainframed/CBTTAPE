*
* CODING EXAMPLE 10.5.1
*
*
           USING JFCB,R10             DEFINE REG FOR JFCB DSECT
           ...
*
*
***********************************************************************
*    MAINSTREAM OF PROGRAM
***********************************************************************
*
           ...
           BAL   R6,RDJFCB            READ JFCB INTO PROG AREA
           BAL   R6,GETDSNM           GET SPECIFIED DATASET NAME
           BAL   R6,GETTIOT           GET TIOT ADR
           BAL   R6,GETUCB            GET UCB ADR FOR RESERVE
           BAL   R6,RESERVE           ISSUE RESERVE
           ...
*
*
***********************************************************************
*    THIS ROUTINE READS THE JFCB AND COPIES IT FROM THE SWA INTO THE
*    PROGRAMMER-SUPPLIED AREA.
***********************************************************************
*
RDJFCB     RDJFCB (DCB01,(OUTPUT))
           LTR   R15,R15              CHK IF DD STMT IN JCL
           BNZ   NODDNM               IF NO, ERROR
           BR    R6                   IF YES, RET TO CALLING RTN
NODDNM1    WTO   '*** DDNAME "LIBDD" NOT IN SPECIFIED IN JCL ***',     -
                 ROUTCDE=11
           ABEND 901
*
*
***********************************************************************
*    THIS ROUTINE GETS THE SPECIFIED DATASET NAME FROM THE JFCB. THE
*    DATASET IS USED AS THE RNAME FOR THE RESERVE MACRO.
***********************************************************************
*
GETDSNM    LA    R10,JFCB             SET ADRBLTY FOR JFCB DSECT
           MVC   DSNAME,JFCBDSNM      EXTRACT SPEC DSNAME FROM JFCB
           BR    R6                   RETURN TO CALLIN RTN
*
*
***********************************************************************
*    THIS ROUTINE GETS THE TIOT ADDRESS WHICH IS USED TO OBTAIN THE
*    UCB ADDRESS.
***********************************************************************
*
GETTIOT    EXTRACT TIOTADR,FIELDS=TIOT    GET TIOT ADR
           BR    R6                       RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE GETS THE UCB ADDRESS BY SCANNING THE TIOT FOR THE
*    DDNAME LIBDD. THE TIOT ENTRY FOR THAT DDNAME CONTAINS THE REQUIRED
*    UCB ADDRESS.
***********************************************************************
*
GETUCB     L     R10,TIOTADR          LOAD ADR OF TIOT
           LA    R10,24(0,R10)        INCR PASS JOBNAME/STEPNAMES
*                                     TO FIRST TIOT DDNAME ENTRY
           LA    R11,0                CLEAR REG 11 FOR IC INSTR
NEXTDDNM   CLC   0(4,R10),BINZEROS    CHECK FOR END OF TIOT
           BE    NODDNM               IF END, DDNAME NOT FOUND
           CLC   4(8,R10),DDNAME      CHK FOR DDNAME
           BE    MVUCBADR             IF FOUND, GET UCB ADDRESS
           IC    R11,0(0,R10)         STORE TIOT DDNAME ENTRY LEN
           LA    R10,0(R11,R10)       INCR TO NEXT TIOT DDNAME ENTRY
           B     NEXTDDNM             CHK NEXT DDN IN TIOT
MVUCBADR   MVC   UCBADR,16(R10)       GET UCB ADR FROM TIOT DDN ENTRY
           BR    R6                   RETURN TO CALLING RTN
NODDNM2    WTO   '*** DDNAME "LIBDD" NOT IN SPECIFIED IN JCL ***',     -
                 ROUTCDE=11
           ABEND 902
*
*
***********************************************************************
*    THIS ROUTINE ISSUES THE RESERVE MACRO FOR THE UCB ALLOCATED BY THE
*    JCL DD STATEMENT WITH THE DDNAME LIBDD AND USES THE SPECIFIED
*    DATASET NAME AS THE RNAME.
***********************************************************************
*
RESERVE    MVC   RNAME,DSNAME         USE DSNAME AS RNAME FOR RESERVE
           RESERVE (QNAME,RNAME,E,,SYSTEMS),UCB=UCBADR
           LTR   R15,R15              CHK IF RESERVE OK
           BNZ   RSRVBAD              IF RC NOT 0, ERROR
           BR    R6                   RETURN TO CALLING RTN
RSRVBAD    WTO   '*** RESERVE ERROR ***',ROUTCDE=11
           ABEND 903
*
*
***********************************************************************
*    DC/DS STATEMENTS
***********************************************************************
*
BINZEROS   DC    F'0'
DSNAME     DS    CL44
DDNAME     DC    CL8'LIBDD'
TIOTADR    DS    F
UCBADR     DS    F
QNAME      DC    CL8'USER'
RNAME      DS    CL44
*
*
***********************************************************************
*    THE JFCB-LIST AND THE DCBS
***********************************************************************
*
JFCB       DS    44F
JFCBPTR    DC    X'87'
           DC    AL3(JFCB)
*
DCB01      DCB   DSORG=PS,MACRF=PM,EXLST=JFCBPTR,DDNAME=LIBDD
*
*
           ...
