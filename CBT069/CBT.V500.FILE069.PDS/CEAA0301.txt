*
* CODING EXAMPLE A.3.1
*
*
//-------- JOB  ----
//CALLASM  EXEC ASMACLG
//C.SYSLIB DD   DSN=SYS1.MACLIB,DISP=SHR
//         DD   DSN=USER.MACLIB,DISP=SHR
//C.SYSIN  DD   *
*
*
***********************************************************************
*    INITIALIZATION
***********************************************************************
*
INVKASM1 CSECT
         INITL 3,EQU=R
*
*
***********************************************************************
*    MAINSTREAM OF PROGRAM
***********************************************************************
*
NXTSRCDD BAL   R6,GETSRCDD         GET A REPLACEMENT DDNAME FOR SYSIN
         LTR   R15,R15             CHECK IF END OF DDNAMES
         BNZ   RETURN              IF YES, TERMINATE PROGRAM
         BAL   R6,INVKASM          IF NO, INVOKE ASSEMBLER
         B     NXTSRCDD            GET NEXT REPLACEMENT DDNAME
*
*
***********************************************************************
*    THIS ROUTINE OBTAINS THE REPLACEMENT DDNAMES OF SYSIN FROM A
*    TABLE ONE AT A TIME.
***********************************************************************
*
GETSRCDD L     R10,SRCDDPTR        LOAD PTR FOR NEXT DDNAME
         CLI   0(R10),SRCDDEND     CHK IF END-OF-TBL
         BE    SETDDEND            IF YES, INDICATE IT
         MVC   SRCDDNM,0(R10)      IF NO, MOVE NEXT DDN INTO PARM-LIST
         LA    R10,8(0,R10)        INCR TO NEXT DDNAME IN TBL
         ST    R10,SRCDDPTR        STORE THE PTR FOR THE NEXT DDNAME
         LA    R15,0               INDICATE NEXT DDN IN ASM PARM-LIST
         BR    R6                  RETURN TO CALLING ROUTINE
SETDDEND LA    R15,4               INDICATE END-OF-TABLE
         BR    R6                  RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS ROUTINE INVOKES THE ASSEMBLER.
***********************************************************************
*
INVKASM  LINK  EP=ASMA90,PARAM=(OPTLIST,DDNLIST),VL=1
         BR    R6                  RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS ROUTINE RESTORES THE REGISTERS AND RETURNS TO MVS OR TO THE
*    CALLING PROGRAM.
***********************************************************************
*
RETURN   RCNTL RC=0
*
*
*
*
***********************************************************************
*     PARAMETER-LISTS FOR DYNAMIC INVOCATION OF THE ASSEMBLER
***********************************************************************
*
         DS    0H
OPTLIST  DC    AL2(OPTLEN)
OPTIONS  DC    C'XREF(SHORT),NOOBJECT'
OPTLEN   EQU   *-OPTIONS
*
*
         DS    0H
DDNLIST  DC    AL2(DDNLEN)
DDNAMES  DC    XL8'00'         SYSLIN
         DC    XL8'00'         N/A
         DC    XL8'00'         N/A
         DC    XL8'00'         SYSLIB
SRCDDNM  DC    CL8' '          SYSIN
         DC    CL8'LISTING'    SYSPRINT
DDNLEN   EQU   *-DDNAMES
*
*
***********************************************************************
*     TABLE OF REPLACEMENT DDNAMES FOR SYSIN
***********************************************************************
*
SRCDDPTR DC    A(SRCDDTBL)
*
*
SRCDDTBL EQU   *
         DC    CL8'SOURCE01'
         DC    CL8'SOURCE02'
         DC    CL8'SOURCE03'
         DC    AL1(SRCDDEND)
*
SRCDDEND EQU   X'FF'
*
*
***********************************************************************
*    END OF PROGRAM
***********************************************************************
*
         END
//*
//*
//G.SYSUT1   DD UNIT=VIO,SPACE=(CYL,(1,1)),DCB=BUFNO=2
//G.SYSLIB   DD DSN=SYS1.MACLIB,DISP=SHR
//           DD DSN=USER.MACLIB,DISP=SHR
//G.LISTING  DD SYSOUT=*
//G.SOURCE01 DD DSN=USER.TEST.SRCLIB(PRGM01),DISP=SHR
//G.SOURCE02 DD DSN=USER.TEST.SRCLIB(PRGM02),DISP=SHR
//G.SOURCE03 DD DSN=USER.TEST.SRCLIB(PRGM03),DISP=SHR
//
