*
* CODING EXAMPLE 4.5.3
*
*
LOAD03   LA    R7,CHKERR3          LOAD ADR OF RECVRY CHK RTN
         LOAD  EP=PROG03,ERRET=LOADERR3
         BR    R6                  RETURN TO CALLING RTN
CHKERR3  C     R15,BIN4            CHK IF RECOVERY POSSIBLE
         BE    LOAD03              IF YES, TRY LOAD AGAIN
         ABEND ...                 IF NO, ABEND
         ...
LOADERR3 C     R1,ERR106           CHK FOR S106 ABEND
         BE    CHKER106            IF S106, CHK REASON CODE
         C     R1,ERR806           CHK FOR S806 ABEND
         BE    NOPROG              IF YES, IND PGM NOT FOUND
         LA    R15,8               INDICATE NON-RECVRY ERROR
         BR    R7                  RETURN TO LOAD RTN
CHKER106 C     R15,BIN12           CHK REASON CODE FOR NO VIR STOR
         BE    NOVSTOR             IF YES, ATTEMPT RECVRY
         LA    R15,8               INDICATE NON-RECRY ERROR
         BR    R7                  RETURN TO LOAD RTN
NOVSTOR  CLI   RCVRYSW,C'1'        CHK IF RECRY ALREADY ATTEMPTED
         BE    NOVSRCRY            IF YES, NON-RECVRY ERROR
         DELETE ...                DELETE NOT NEEDED PRGMS
         FREEMAIN ...              FREE NOT NEEDED VIRT STORAGE
         MVI   RCVRYSW,C'1'        INDICATE RECVRY ATTEMPTED
         LA    R15,4               INDICATE TRY LOAD AGAIN
         BR    R7                  RETURN TO LOAD RTN
NOVSRCRY WTO   '*** INSUFFICIENT VIRTUAL STORAGE FOR PROG03 ***'
         LA    R15,8               IND NON-RECRY ERROR
         BR    R7                  RETURN TO LOAD RTN
NOPROG   WTO   '*** PROGRAM PROG03 NOT FOUND ***'
         LA    R15,8               IND NON-RECRY ERROR
         BR    R7                  RETURN TO LOAD RTN
         ...
RCVRYSW  DC    C'0'
BIN4     DC    F'4'
BIN12    DC    F'12'
ERR106   DC    X'00000106'
ERR806   DC    X'00000806'
