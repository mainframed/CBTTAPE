*
* CODING EXAMPLE 14.6.3
*
*
         TITLE 'EXAMPLE OF A LINKAGE ASSIST PROGRAM'
*
*
***********************************************************************
*    THIS PROGRAM IS A LINKAGE ASSIST PROGRAM. IT RECEIVES CONTROL IN
*    THE AMODE OF THE CALLING PROGRAM. IT CHECKS THE AMODE OF THE
*    CALLING PROGRAM. IF THE AMODE IS 24-BIT, THEN THE HIGH-ORDER BYTE
*    OF REGISTER 1 (THE PARAMETER REGISTER) IS SET TO BINARY ZEROS
*    TO CREATE A VALID 31-BIT ADDRESS FOR THE CALLED PROGRAM. THIS
*    PROGRAM THEN LOADS THE CALLED PROGRAM, CHANGES THE AMODE TO THAT
*    REQUIRED BY THE CALLED PROGRAM AND BRANCHES TO IT. WHEN CONTROL
*    IS RETURNED, THIS PROGRAM RESTORES THE REGISTERS AND AMODE TO
*    THAT OF THE CALLING PROGRAM AND RETURNS CONTROL.
***********************************************************************
*
*
PROG02   CSECT
PROG02   AMODE ANY                 RECV CNTL IN AMODE OF CALLING PROG
PROG02   RMODE 24                  RESIDE BELOW 16 MB LINE TO
*                                  ACCOMMODATE AMODE OF 24 OR 31
*
*
***********************************************************************
*    INITIALIZATION
***********************************************************************
*
         INITL 3,EQU=R             INITIALIZE PROGRAM
*
*
***********************************************************************
*    MAIN STREAM OF PROGRAM
***********************************************************************
*
         BAL   R6,VALIDATE         CHK IF REG 1 NEEDS ADJUSTMENT
         BAL   R6,LOAD             LOAD CALLED PROG
         BAL   R6,BRANCH           BRANCH TO CALLED PROG
         B     RETURN              RETURN TO CALLING PROG
*
*
***********************************************************************
*    THIS ROUTINE SAVES THE AMODE OF THE CALLING PROGRAM AND PASSES
*    CONTROL TO THE SUBROUTINE WHICH DETERMINES IF REGISTER 1 NEEDS
*    TO BE ADJUSTED.
***********************************************************************
*
VALIDATE LA    R7,CHKAMODE         LD ADR OF CALLED RTN
         BASSM R8,R7               SAVE AMODE OF CALLING PROG,
*                                  BRANCH AND LINK TO CALLED RTN
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS SUBROUTINE CHECKS THE AMODE OF THE CALLING PROGRAM. IF THE
*    AMODE IS 24, THEN THE HIGH-ORDER BYTE OF REGISTER 1 IS SET T0
*    ZEROS.
***********************************************************************
*
CHKAMODE ST    R8,AMODE            STORE AMODE FOR TM INSTR
         TM    AMODE,X'80'         CHK AMODE OF CALLING PROG
         BZ    FIXREG1             IF AMODE=24 (BIT0=0), FIX REG 1
         ST    R1,PARMADR          STORE PARM ADR
         BR    R8                  AMODE=31, RET TO CALLING RTN
FIXREG1  ICM   R1,B'1000',=X'00'   SET BITS 0-7 OF PARM ADR TO ZEROS
         ST    R1,PARMADR          STORE FIXED PARM ADR
         BR    R8                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE LOADS THE CALLED PROGRAM AND SAVES THE POINTER-
*    DEFINED VALUE RETURNED FROM THE LOAD MACRO.
***********************************************************************
*
LOAD     LOAD  EP=PROG02X          LOAD CALLED PROG
         ST    R0,EPADR            SAVE POINTER-DEFINED VALUE
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE CHANGES THE AMODE TO THAT OF THE CALLED PROGRAM
*    AND BRANCHES AND LINKS TO THE CALLED PROGRAM.
***********************************************************************
*
BRANCH   L     R1,PARMADR          LOAD PARM ADR FROM CALLING PROG
         L     R15,EPADR           LOAD EP ADR AND AMODE OF CALLED PROG
         BASSM R14,R15           SET AMODE AND BR AND LK TO CALLED PROG
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE RESTORES THE REGISTERS AND THE AMODE TO THAT OF THE
*    CALLING PROGRAM AND RETURNS CONTROL.
***********************************************************************
*
RETURN   L     R13,4(0,R13)        RESTORE SA ADR OF CALLING PROG
         L     R14,12(0,R13)       RESTORE REG 14 OF CALLING PROG
         LM    R0,R12,20(R13)      RESTORE REGS 0-12 OF CALLING PROG
         BSM   0,R14               RESTORE AMODE OF CALLING PROG AND
*                                  RETURN
*
*
***********************************************************************
*    THE STORAGE AREAS USED BY THIS PROGRAM.
***********************************************************************
*
AMODE    DS    F
PARMADR  DS    F
EPADR    DS    F
*
*
***********************************************************************
*    END OF PROGRAM.
***********************************************************************
*
*
         END
