*
* CODING EXAMPLE 11.4.2
*
*
WRTMTV   CSECT
*
*
***********************************************************************
*    INITIALIZATION
***********************************************************************
*
         INITL 3,EQU=R             INITIALIZE PROGRAM
         USING IHADCB,R11          DEFINE REG FOR DCB DSECT
         USING IOBSTDRD,R12        DEFINE REG FOR IOB DSECT
*
*
***********************************************************************
*    MAINSTREAM OF PROGRAM
***********************************************************************
*
         BAL   R6,SETDSCTS         LOAD DSECT REGISTERS
         BAL   R6,SETIOB           SET REQUIRED IOB FIELDS
         BAL   R6,OPEN             OPEN DCBS
NEXTREC  BAL   R6,GETREC           READ A REC FOR THE TAPE DATASET
         BAL   R6,WRTREC           WRITE A PHYSICAL REC TO TAPE
         B     NEXTREC             PROCESS NEXT REC
ENDDATA  BAL   R6,WRTEND           EOD, INDICATE EOF1/EOF2 REQUIRED
         BAL   R6,CLOSE            CLOSE DCBS
         B     RETURN              RETURN TO MVS OR CALLING PROG
*
*
***********************************************************************
*    THIS ROUTINE LOADS THE REGISTERS FOR THE DCB AND THE IOB DSECTS
***********************************************************************
*
SETDSCTS LA    R11,WRTDCB           LOAD DCB ADR FOR DSECT ADRBLTY
         LA    R12,WRTIOB           LOAD IOB ADR FOR DSECT ADRBLTY
         BR    R6                   RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE SETS THE REQUIRED FIELDS OF THE IOB
***********************************************************************
*
SETIOB   MVI   IOBFLAG1,X'42'      IND CCW-CHAINING, NOT RELATED
         MVC   IOBSTART,CCWADR     LOAD BG ADR OF CCW-LIST
         MVC   IOBDCBPT,DCBADR     LOAD DCB ADR
         MVC   IOBECBPT,ECBADR     LOAD ECB ADR
         MVC   IOBINCAM,BINONE     WRITE FORWARD ONE BLOCK AT A TIME
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE OPENS ALL THE DCBS.
***********************************************************************
*
OPEN     OPEN  (INRECS,,WRTDCB(OUTPUT))
         ...                       CHECK FOR GOOD OPEN
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE READS THE RECORDS USED TO CREATE THE TAPE DATASET.
***********************************************************************
*
GETREC   GET   INRECS,RECORD       READ A RECORD
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE WRITES THE NEXT PHYSICAL RECORD TO THE TAPE DATASET.
*    IN ORDER TO PERFORM THAT TASK, THIS ROUTINE BRANCHES AND LINKS TO
*    THE SUBROUTINE WHICH PERFORMS THE FOLLOWING FUNCTION:
*      * ISSUE THE EXCP MACRO TO WRITE THE NEXT PHYSICAL RECORD OF THE
*        TAPE DATASET.
***********************************************************************
*
WRTREC   BAL   R7,DOEXCP           ISSUE EXCP MACRO TO WRITE REC
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS SUBROUTINE ISSUES THE EXCP MACRO TO WRITE A PHYSICAL RECORD
*    TO THE TAPE DATASET, CHECKS THE ECB COMPLETION CODE AND PERFORMS
*    THE APPROPRIATE ACTION INDICATED BY THE COMPLETION CODE.
***********************************************************************
*
DOEXCP   XC    WRTECB,WRTECB       CLEAR ECB FOR EXCP MACRO
         EXCP  WRTIOB              ISSUE EXCP MACRO
         WAIT  1,ECB=WRTECB        WAIT FOR I/O COMPLETION
         TM    WRTECB,X'7F'        CHECK FOR GOOD I/O
         BNO   CHKECB              IF NO, EXAMINE ECB
         BR    R7                  RETURN TO CALLING RTN
*
CHKECB   TM    WRTECB,X'41'        CHECK FOR EXCEPTION COND
         BO    CHKERR41            IF YES, CHK FOR END OF TAPE
         B     EXCPERR             IF NO, I/O ERROR, DUMP DIAG INFO
*
CHKERR41 TM    IOBUSTAT,X'01'      CHECK FOR END-OF-TAPE
         BO    DOEOV               IF YES, CAUSE ANOTHER TAPE MOUNT
         B     EXCPERR             IF NO, I/O ERROR, DUMP DIAG INFO
*
DOEOV    OI    DCBOFLGS,DCBOFLWR   IND EOV1/EOV2 LABELS, ETC. REQRD
         EOV   WRTDCB              DO END-OF-VOLUME PROCESSING
         BR    R7                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE RECEIVES CONTROL IF THE ATTEMPTED I/O IS
*    UNSUCCESSFUL. THIS ROUTINE LOAD VARIOUS DIAGNOSTIC INFORMATION
*    INTO REGISTERS 8 AND 9 AND THEN CAUSES AN ABEND WITH A DUMP.
***********************************************************************
*
EXCPERR  L     R8,WRTECB           LOAD CONTENTS OF ECB INTO A REG
         MVC   ERRINFO+1(1),IOSUSTAT    UNIT STATUS FIELD OF CSW
         MVC   ERRINFO+2(1),IOBSENS0    BYTE-1 OF HARDWARE SENSE CODE
         MVC   ERRINFO+3(1),IOBSENS1    BYTE-2 OF HARDWARE SENSE CODE
         L     R9,ERRINFO          LOAD ERROR INFO WORD INTO A REG
         L     R10,ERRID           LOAD ID TO FIND REGS IN DUMP
         ABEND 900,DUMP            ISSUE ABEND MACRO AND REQ DUMP
*
*
***********************************************************************
*    THIS ROUTINE SETS THE DCBOFLGS FIELD TO INDICATE TO DATA MGMT TO
*    DO THE FOLLOWING AFTER THE DATASET IS CLOSED:
*        * WRITE A TAPEMARK AFTER THE LAST RECORD OF THE DATASET;
*        * WRITE THE EOF1/EOF2 LABELS IF THE JCL SPECIFIED STANDARD
*          LABELS FOR THE DATASET;
*        * WRITE A DOUBLE TAPE MARK AFTER THE EOF1/EOF2 LABELS (FOR A
*          STANDARD LABEL DATASET) OR AFTER THE LAST RECORD OF THE
*          DATASET (FOR A NON-LABELED DATASET) TO INDICATE THAT THE
*          DATASET IS THE LAST ONE ON THE TAPE VOLUME.
***********************************************************************
*
WRTEND   LA    R11,WRTDCB          SET ADRBLTY FOR DCB DSECT
         OI    DCBOFLGS,DCBOFLWR   IND TO DATAMGMT TO DO EOD PROC
         BR    R6                  RET TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE CLOSES ALL THE DCBS.
***********************************************************************
*
CLOSE    CLOSE (INRECS,,WRTDCB)
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE RESTORES THE REGISTERS AND RETURNS CONTROL.
***********************************************************************
*
RETURN   RCNTL RC=0                RETURN TO MVS OR THE CALLING PROG
*
*
***********************************************************************
*    CCW, IOB AND ECB
***********************************************************************
*
WRTCCW   CCW   X'01',RECORD,X'20',80    WRITE CCW
*
WRTIOB   DC    10F'0'                   AREA FOR IOB
WRTECB   DC    F'0'                     FULLWORD FOR ECB
*
*
***********************************************************************
*    OTHER DC/DS STATEMENTS
***********************************************************************
*
BINONE   DC    H'1'
RECORD   DS    CL80
*
CCWADR   DC    A(WRTCCW)
DCBADR   DC    A(WRTDCB)
ECBADR   DC    A(WRTECB)
*
ERRINFO  DC    F'0'
ID       DC    4XL1'FF'
*
*
***********************************************************************
*    THE DCBS
***********************************************************************
*
INRECS   DCB   DSORG=PS,MACRF=GM,LRECL=80,RECFM=FB,EODAD=ENDDATA,      -
               DDNAME=INRECS
WRTDCB   DCB   MACRF=E,DDNAME=TESTFILE
*
*
***********************************************************************
*    THE DSECTS
***********************************************************************
*
         PRINT NOGEN
         DCBD  DSORG=XE        GENERATES EXCP-DCB DSECT
         IEZIOB                GENERATES IOB DSECT
         ORG   IOBCSTAT+1      POS TO RESIDUAL COUNT FIELD OF CSW
IOBCSWCT DS    CL2             PROVIDE LABEL FOR RES CT FIELD
*
*
***********************************************************************
*    END OF PROGRAM
***********************************************************************
*
         END
