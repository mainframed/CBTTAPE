*
* CODING EXAMPLE 7.2.1
*
*
SVC99ALO CSECT
*
*
***********************************************************************
*    INITIALIZATION
***********************************************************************
*
         INITL 3,EQU=R             INITIALIZE PROGRAM
         USING S99RBP,R10          DEFINE REG FOR REQ BLK PTR DSECT
         USING S99RB,R11           DEFINE REG FOR REQ BLOCK DSECT
*
*
***********************************************************************
*    MAINSTREAM OF PROGRAM
***********************************************************************
*
         BAL   R6,SETRB001         SETUP RB FOR ALLOC
         BAL   R6,SETRBPTR         SETUP RB PTR
         BAL   R6,DOSVC99          ISSUE SVC 99
         B     RETURN              RETURN TO MVS OR CALLING PROG
*
*
***********************************************************************
*    THIS ROUTINE SETS UP THE SVC99 REQUEST BLOCK.
***********************************************************************
*
SETRB001 LA    R11,REQBLK          SET ADRBLTY FOR RB DSECT
         XC    REQBLK,REQBLK       CLEAR RB
         MVI   S99RBLN,REQBLKLN    SET RB LEN
         MVI   S99VERB,S99VRBAL    SET ALLOCATION VERB
         LA    R10,TUPTR001        LD ADR OF BG OF TEXT-UNIT-PTR LIST
         ST    R10,S99TXTPP        STORE THAT ADR INTO RB
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE SETS UP THE SVC99 REQUEST-BLOCK-POINTER.
***********************************************************************
*
SETRBPTR LA    R10,RBPTR           SET ADRBLTY FOR RBPTR DSECT
         LA    R11,REQBLK          LD ADR OF RB
         ST    R11,S99RBPTR        STORE RB ADR INTO RB PTR
         OI    S99RBPTR,S99RBPND   TURN ON HIGH ORDER BIT
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE ISSUES SVC 99 VIA THE DYNALLOC MACRO.
***********************************************************************
*
DOSVC99  LA    R1,RBPTR            LD ADR OF RB PTR INTO R1 FOR SVC 99
         DYNALLOC                  ISSUE SVC 99
         LTR   R15,R15             CHK RC
         BNZ   CHKERR              IF NOT ZERO, CHK ERROR/INFO FIELDS
         BR    R6                  IF OK, RETURN TO CALLING RTN
CHKERR   ...
*
*
***********************************************************************
*    THIS ROUTINE RESTORES THE REGISTERS AND RETURNS
***********************************************************************
*
RETURN   RCNTL RC=0                RETURN TO MVS OR CALLING PROG
*
*
***********************************************************************
*    SVC99 REQ-BLOCK-POINTER AND REQUEST BLOCK
***********************************************************************
*
RBPTR    DS    F
REQBLK   DS    CL(S99RBEND-S99RB)
REQBLKLN EQU   L'REQBLK
*
*
***********************************************************************
*    TEXT-UNITS-POINTERS LIST
***********************************************************************
*
TUPTR001 DS    0F
         DC    A(TUDDNA1)          ADR OF TU FOR DDNAME
         DC    A(TUDSNA1)          ADR OF TU FOR DSNAME
         DC    A(TUDSSA1)          ADR OF TU FOR DS STATUS
         DC    A(TUDNDA1)          ADR OF TU FOR DS NORM TERM DISP
         DC    A(TUUNTA1)          ADR OF TU FOR UNIT
         DC    A(TUVOLA1)          ADR OF TU FOR VOLSER
         DC    A(TUBLKA1)          ADR OF TU FOR DCB BLKSZ
         DC    A(TULRLA1)          ADR OF TU FOR DCB LRECL
         DC    A(TURFMA1)          ADR OF TU FOR DCB RECFM
         DC    A(TUCYLA1)          ADR OF TU FOR SPACE IN CYL
         DC    A(TUPRMA1)          ADR OF TU FOR PRIM SPACE
         DC    A(TUSECA1)          ADR OF TU FOR SEC SPACE
         DC    X'80'               INDICATES LAST TU ADDRESS
         DC    AL3(TURLSA1)        ADR OF TU FOR SPACE RLSE
*
*
***********************************************************************
*    TEXT UNITS
***********************************************************************
*
TUDDNA1  DC    AL2(DALDDNAM)       TU KEY FOR DDNAME
         DC    AL2(1)              NUMBER OF ENTRIES
         DC    AL2(8)              LENGTH OF DDNAME
         DC    CL8'DD1'            DDNAME
*
TUDSNA1  DC    AL2(DALDSNAM),AL2(1),AL2(44),CL44'USER.NEWDATA'
TUDSSA1  DC    AL2(DALSTATS),AL2(1),AL2(1),X'04'
TUDNDA1  DC    AL2(DALNDISP),AL2(1),AL2(1),X'02'
TUUNTA1  DC    AL2(DALUNIT),AL2(1),AL2(5),C'SYSDA'
TUVOLA1  DC    AL2(DALVLSER),AL2(1),AL2(6),C'123456'
TUBLKA1  DC    AL2(DALBLKSZ),AL2(1),AL2(2),AL2(4080)
TULRLA1  DC    AL2(DALLRECL),AL2(1),AL2(2),AL2(80)
TURFMA1  DC    AL2(DALRECFM),AL2(1),AL2(1),X'90'
TUCYLA1  DC    AL2(DALCYL),AL2(0)
TUPRMA1  DC    AL2(DALPRIME),AL2(1),AL2(3),AL3(10)
TUSECA1  DC    AL2(DALSECND),AL2(1),AL2(3),AL3(1)
TURLSA1  DC    AL2(DALRLSE),AL2(0)
*
*
***********************************************************************
*    THE DSECTS
***********************************************************************
*
         IEFZB4D0        GENS DSECT FOR REQ BLOCK, TEXT UNIT, ETC.
         IEFZB4D2        GENS TABLE OF EQUATES FOR TU KEYS
*
*
***********************************************************************
*    END OF PROGRAM
***********************************************************************
*
         END
