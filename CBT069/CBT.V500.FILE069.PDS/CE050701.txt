*
* CODING EXAMPLE 5.7.1
*
*
         TITLE 'REENTRANT I/O PROGARM EXAMPLE'
*
*
***********************************************************************
*
*        EXAMPLE OF REENTRANT I/O PROGRAM
*
*        FILE-1 IS READ AND SELECTED FIELDS FROM EACH RECORD
*        ARE USED TO CREATE THE RECORDS OF FILE-2.
*
*        ALSO A RECORD COUNTER IS MAINTAINED IN THE PROGRAM AND
*        ITS CONTENTS IS USED AS ONE OF THE FIELDS OF THE RECORDS
*        OF FILE-2.
*
*        REGISTER 9 IS USED TO HOLD THE ADDRESS OF THE FIRST
*        GETMAINED AREA DURING THE ENTIRE EXECUTION OF THE PROGRAM.
*
***********************************************************************
*
*
RENTIO   CSECT
*
*
***********************************************************************
*    INITIALIZATION
***********************************************************************
*
         INITL 3,EQU=R             INITIALIZE PROGRAM
         USING WORKAREA,R9         DEFINE REG FOR WA DSECT
         USING RECORD1,R11         DEFINE REG FOR REC1 DSECT
         USING RECORD2,R12         DEFINE REG FOR REC2 DSECT
*
*
***********************************************************************
*    MAINSTREAM OF PROGRAM
***********************************************************************
*
         BAL   R6,GETMAIN1         GET STOR FOR WORKAREA
         BAL   R6,GETMAIN2         GET STOR FOR MACRO EXPNS, I/O AREAS
         BAL   R6,BLDCNTLS         MOVE MACRO EXPNS INTO GM AREA
         BAL   R6,OPENFLS          OPEN FILES
NEXTREC  BAL   R6,GETREC           READ A RECORD
         BAL   R6,BLDREC           BUILD OUTPUT RECORD
         BAL   R6,PUTREC           WRITE A RECORD
         B     NEXTREC             PROCESS NEXT RECORD
RECEND   BAL   R6,CLOSEFLS         CLOSE FILES
         B     RETURN              RETURN TO MVS OR TO CALLING PROG
*
*
***********************************************************************
*    THIS ROUTINE OBTAINS STORAGE FOR THE PROGRAM WORKAREA AND SAVES
*    THAT ADDRESS IN REGISTER 9.
***********************************************************************
*
GETMAIN1 GETMAIN RU,LV=WKARLEN
         LR    R9,R1               SET ADRBLTY FOR WA DSECT
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE OBTAINS STORAGE FOR THE I/O MACRO EXPANSIONS AND
*    THE I/O AREAS AND SAVES THE ADRRESSES OF THOSE STORAGE AREAS
*    IN FIELDS OF THE PREVIOUS GETMAINED AREA.
***********************************************************************
*
GETMAIN2 GETMAIN RU,LV=OPENLEN
         ST    R1,OPENADR
         GETMAIN RU,LV=CLOSELEN
         ST    R1,CLOSEADR
         GETMAIN RU,LV=DCB1LEN
         ST    R1,DCB1ADR
         GETMAIN RU,LV=DCB2LEN
         ST    R1,DCB2ADR
         GETMAIN RU,LV=REC2LEN
         ST    R1,REC2ADR
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE COPIES THE I/O MACRO EXPANSIONS INTO THE
*    GETMAINED STORAGE AREAS.
***********************************************************************
*
BLDCNTLS L     R10,OPENADR
         MVC   0(OPENLEN,R10),OPEN
         L     R10,CLOSEADR
         MVC   0(CLOSELEN,R10),CLOSE
         L     R10,DCB1ADR
         MVC   0(DCB1LEN,R10),DCB1
         L     R10,DCB2ADR
         MVC   0(DCB2LEN,R10),DCB2
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE OPENS THE DCBS FROM THE GETMAINED AREAS
*    AND ZEROS THE RECORD COUNTER FOR FILE-2
***********************************************************************
*
OPENFLS  L     R1,OPENADR          LOAD ADR OF OPEN LIST FROM GM AREA
         L     R10,DCB1ADR         LOAD ADR OF DCB1 FROM GM AREA
         L     R11,DCB2ADR         LOAD ADR OF DCB2 FROM GM AREA
         OPEN  ((R10),,(R11)),MF=(E,(R1))   DO REENTRANT OPEN
         ZAP   REC2CTR,ZERO        CLEAR RECORD COUNTER
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE READS A RECORD FORM FILE-1.
***********************************************************************
*
GETREC   L     R10,DCB1ADR         LOAD ADR OF DCB1 FROM GM AREA
         GET   (R10)               DO REENTRANT GET
         LR    R11,R1              SET ADRBLTY FOR REC1 DSECT
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE CREATES THE RECORDS FOR FILE-2.
***********************************************************************
*
BLDREC   L     R12,REC2ADR         SET ADRBLTY FOR REC2 DSECT
         AP    REC2CTR,ONE         INCR RECORD COUNTER
         MVC   ORECNUM,REC2CTR
         MVC   OFIELD1,IFIELD2
         MVC   OFIELD2,IFIELD4
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS SUBROUTINE WRITES THE RECORDS INTO FILE-2.
***********************************************************************
*
PUTREC   L     R10,DCB2ADR         LOAD ADR OF DCB2 FROM GM AREA
         L     R12,REC2ADR         LD ADR OF REC2 I/O AREA FROM GM AREA
         PUT   (R10),(R12)         DO REENTRANT PUT
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE CLOSES THE DCBS FROM THE GETMAINED AREAS
***********************************************************************
*
CLOSEFLS L     R1,CLOSEADR         LOAD ADR OF CLOSE LIST FROM GM AREA
         L     R10,DCB1ADR         LOAD ADR OF DCB1 FROM GM AREA
         L     R11,DCB2ADR         LOAD ADR OF DCB2 FROM GM AREA
         CLOSE ((R10),,(R11)),MF=(E,(R1))   DO REENTRANT CLOSE
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE RESTORES THE REGISTERS AND RETURNS CONTROL.
***********************************************************************
*
RETURN   RCNTL RC=0                RETURN TO MVS OR CALLING PROG
*
***********************************************************************
*    CONSTANTS USED BY PROGRAM
***********************************************************************
*
ZERO     DC    P'0'
ONE      DC    P'1'
*
*
***********************************************************************
*    LIST FORM OF I/O MACRO INSTRUCTIONS
***********************************************************************
*
OPEN     OPEN  (DCB1,(INPUT),DCB2,(OUTPUT)),MF=L
OPENLEN  EQU   (*-OPEN)
*
CLOSE    CLOSE (DCB1,,DCB2),MF=L
CLOSELEN EQU   (*-CLOSE)
*
*
***********************************************************************
*    DCB MACRO INSTRUCTIONS
***********************************************************************
*
FILE1DCB DCB   DSORG=PS,MACRF=GL,RECFM=FB,LRECL=80,EODAD=RECEND,       -
               DDNAME=INDATADD
DCB1LEN  EQU   (*-FILE1DCB)
*
FILE2DCB DCB   DSORG=PS,MACRF=PM,RECFM=FB,LRECL=50,DDNAME=OUTDATDD     -
DCB2LEN  EQU   (*-FILE2DCB)
*
*
***********************************************************************
*    DSECTS USED BY PROGRAM
***********************************************************************
*
WORKAREA DSECT
OPENADR  DS    F                   ADDR OF OPEN LIST IN GM AREA
CLOSEADR DS    F                   ADDR OF CLOSE LIST IN GM AREA
DCB1ADR  DS    F                   ADDR OF DCB1 IN GM AREA
DCB2ADR  DS    F                   ADDR OF DCB2 IN GM AREA
REC2ADR  DS    F                   ADDR OF OUT REC I/O AREA IN GM AREA
REC2CTR  DS    PL5                 ADDR OF RECORD COUNTER
WKARLEN  EQU   (*-WORKAREA)
*
RECORD1  DSECT
IFIELD1  DS    CL15
IFIELD2  DS    CL20
IFIELD3  DS    CL10
IFIELD4  DS    CL25
IFIELD5  DS    CL10
*
RECORD2  DSECT
ORECNUM  DS    CL5
OFIELD1  DS    CL20
OFIELD2  DS    CL25
REC2LEN  EQU   (*-RECORD2)
*
*
***********************************************************************
*    END OF PROGRAM
***********************************************************************
*
         END
*
