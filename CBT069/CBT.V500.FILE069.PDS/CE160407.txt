*
* CODING EXAMPLE 16.4.7
*
*
DSPCTEST CSECT
*
*
**********************************************************************
* INITIALIZATION
**********************************************************************
*
DSPCTEST AMODE 31                  \ INDICATE 31-BIT ADDRESSING
DSPCTEST RMODE ANY                 /
         INITL 3,4,5,EQU=R         SET UP BASE REGS AND SAVEAREA
         USING DSRECORD,R12        DEFINE REG FOR DSPC DSECT
*
*
**********************************************************************
* MAINSTREAM OF PROGRAM
**********************************************************************
*
         BAL   R6,MAKEDS       CREATE DATA SPACE
         BAL   R6,SETDSADR     SET UP ADDRESSABLITY TO DATASPACE
         BAL   R6,SETARMOD     SET UP AR-MODE
         BAL   R6,PUTDS        WRITE RECS INTO DATA SPACE
         BAL   R6,UPDATEDS     UPDATE RECS IN DATA SPACE
         BAL   R6,GETDS        READ RECS FROM DATA SPACE
         BAL   R6,DUMPDATA     DUMP ADDRESS SPACE AND DATA SPACE
         B     RETURN          RETURN TO MVS OR CALLING PROGRAM
*
*
**********************************************************************
* THIS ROUTINE CREATES THE DATA SPACE.
**********************************************************************
*
MAKEDS   DSPSERV CREATE,TYPE=BASIC,STOKEN=DS01TOK,SCOPE=SINGLE,        -
               BLOCKS=(MAXBLKS,INITBLKS),NUMBLKS=CURRBLKS,             -
               NAME=DS01NAME,GENNAME=YES,OUTNAME=DS01GNAM,             -
               ORIGIN=DSBEGADR
         LTR   R15,R15            CHECK IF DSPSERV OK
         BZR   R6                 IF OK, RETURN TO CALLING ROUTINE
         LA    R10,901            IF NOT, SET USERCODE
         B     ABEND              ABEND PROGRAM
*
*
**********************************************************************
* THIS ROUTINE SETS UP ADDRESSABILITY TO THE DATA SPACE.
**********************************************************************
*
SETDSADR ALESERV ADD,STOKEN=DS01TOK,ALET=DS01ALET,AL=WORKUNIT
         LA    R10,902            PRIME USERCODE IN REG 10
         LTR   R15,R15            CHECK IF ALESERV OK
         BNZ   ABEND              IF NOT, ABEND PROGRAM
         LAM   R12,R12,DS01ALET   LOAD ALET OF D.S. INTO AR 12
         L     R12,DSBEGADR       LOAD BG ADDR OF D.S. INTO GPR 12
         BR    R6                 RETURN TO CALLING ROUTINE
*
*
**********************************************************************
* THIS ROUTINE PUTS THE CPU IN AR-MODE WHICH IS REQUIRED TO ACCESS
* A DATA SPACE AND INDICATES AR-MODE TO THE FOLLOWING MACROS.
**********************************************************************
*
SETARMOD SAC   512                SET AR-MODE
         SYSSTATE ASCENV=AR       INDICATE AR-MODE TO FOLLOWING MACROS
         BR    R6                 RETURN TO CALLING ROUTINE
*
*
**********************************************************************
* THIS ROUTINE COPIES DATA INTO THE DATA SPACE.
**********************************************************************
*
PUTDS    MVC   DSFLD01,DATA01     \   COPY INITIAL TEST DATA INTO
         MVC   DSFLD02,DATA02      |  DATA SPACE
         MVC   DSFLD03,DATA03     /
         BR    R6                 RETURN TO CALLING ROUTINE
*
*
**********************************************************************
* THIS ROUTINE UPDATES DATA FIELDS IN THE DATA SPACE.
**********************************************************************
*
UPDATEDS MVC   DSFLD01,DATA11     \
         MVC   DSFLD02A,DATA12A    \  UPDATE FIELDS IN DATA SPACE
         MVC   DSFLD02C,DATA12C    /
         MVC   DSFLD04,DATA14     /
         BR    R6                 RETURN TO CALLING ROUTINE
*
*
**********************************************************************
* THIS ROUTINE RETRIEVES DATA FROM THE DATA SPACE.
**********************************************************************
*
GETDS    MVC   DATAX1,DSFLD01     \
         MVC   DATAX2,DSFLD02      \ COPY DATA FIELDS FROM DATA SPACE
         MVC   DATAX3,DSFLD03      / INTO FIELDS IN ADDRESS SPACE
         MVC   DATAX4,DSFLD04     /
         BR    R6                 RETURN TO CALLING ROUTINE
*
*
**********************************************************************
* THIS ROUTINE DUMPS THE ADDRESS SPACE AND FIRST 4K OF THE DATA SPACE
* WITH USERCODE OF 999 AND REG 8 POINTS TO THE BEGINNING OF THE
* RECORDS READ FROM THE DATA SPACE.
**********************************************************************
*
DUMPDATA LA    R8,NEWDATA         POINT TO RECS READ FROM DATA SPACE
         LA    R10,0              \  SET BG ADDR OF DATA SPACE TO
         ST    R10,DMBGADR        /  DUMP
         LA    R10,4095           \  SET END ADDR OF DATA SPACE TO
         ST    R10,DMENDADR       /  DUMP
         OI    DMENDADR,X'80'     INDICATE END OF ADDRESS LIST
         MVC   DMSTOKEN,DS01TOK   PUT DATA SPACE TOKEN INTO ADDR LIST
         ABEND 999,DUMP,DUMPOPX=DUMPLST
*
*
**********************************************************************
* THIS ROUTINE ABENDS PROGRAM USING USERCODE IN REGISTER 10.
**********************************************************************
*
ABEND    ABEND (R10),DUMP
*
*
**********************************************************************
* THIS ROUTINE RESTORES THE REGISTERS AND RETURNS CONTROL.
**********************************************************************
*
RETURN   RCNTL RC=0
*
*
**********************************************************************
* WORKING STORAGE AND CONSTANTS
**********************************************************************
*
DS01TOK  DS    D
DS01ALET DS    F
DS01NAME DC    C'D01$$$$$'
DS01GNAM DS    CL8
MAXBYTES EQU   1000000
INITBYTS EQU   250000
MAXBLKS  DC    A((MAXBYTES+4095)/4096)
INITBLKS DC    A((INITBYTS+4095)/4096)
CURRBLKS DS    CL4
DSBEGADR DS    F
*
*
**********************************************************************
* INITIAL TEST DATA COPIED INTO THE DATA SPACE
**********************************************************************
*
DATA01   DC    10CL1'A'
DATA02   DC    50CL1'B'
DATA03   DC    25CL1'C'
DATA04   DC    20CL1'D'
*
*
**********************************************************************
* TEST DATA USED TO UPDATE FIELDS IN THE DATA SPACE
**********************************************************************
*
DATA11   DC    10CL1'1'
DATA12A  DC    10CL1'0'
DATA12B  DC    20CL1'X'
DATA12C  DC    5CL1'Y'
DATA12D  DC    15CL1'Z'
DATA13   DC    25CL1'3'
DATA14   DC    20CL1'4'
*
*
**********************************************************************
* FIELDS IN ADDRESS SPACE IN WHICH DATA IS READ FROM THE DATA SPACE
**********************************************************************
*
NEWDATA  DS    0CL105
DATAX1   DS    CL10
DATAX2   DS    CL50
DATAX3   DS    CL25
DATAX4   DS    CL20
*
*
**********************************************************************
* ADDRESS LIST FOR ABEND MACRO FOR DUMPING DATA FROM THE DATA SPACE
**********************************************************************
*
DUMPLST0 DS    0D
DMBGADR  DS    F
DMENDADR DS    F
DMSTOKEN DS    D
*
DUMPLST  SNAPX SDATA=SUM,DSPSTOR=DUMPLST0,MF=L
*
*
**********************************************************************
* MAPPING DSECT FOR THE FIELDS IN THE DATA SPACE
**********************************************************************
*
DSRECORD DSECT
DSFLD01  DS    CL10
DSFLD02  DS    0CL50
DSFLD02A DS    CL10
DSFLD02B DS    CL20
DSFLD02C DS    CL5
DSFLD02D DS    CL15
DSFLD03  DS    CL25
DSFLD04  DS    CL20
*
*
**********************************************************************
* END OF PROGRAM
**********************************************************************
*
         END
