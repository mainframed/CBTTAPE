*
* CODING EXAMPLE 7.1.2
*
*
READDISK CSECT
*
*
***********************************************************************
*    INITIALIZATION
***********************************************************************
*
         INITL 3,EQU=R             INITIALIZE PROGRAM
         USING INFMJFCB,R10        DEFINE REG FOR JFCB DSECT
         USING IHADCB,R11          DEFINE REG FOR DCB DSECT
         USING DSTBLMAP,R12        DEFINE REG FOR USER DSN-TBL DSECT
*
*
***********************************************************************
*    MAINSTREAM OF PROGRAM
***********************************************************************
*
         BAL   R6,RDJFCB           READ JFCB INTO PROG AREA
NEXTFILE BAL   R6,MDFYJFCB         MODIFY JFCB WITH NEXT DATASET NAME
         BAL   R6,ENQ              ENQ ON DATASET NAME
         BAL   R6,OPEN             OPEN NEXT DATASET
         BAL   R6,PROCESS          PROCESS DATASET
DATAEND  BAL   R6,CLOSE            CLOSE DATASET
         BAL   R6,DEQ              DEQ ON DATASET NAME
         B     NEXTFILE            ALLOCATE NEXT DATASET
VOLEND   B     RETURN              ALL DATASETS ON VOL PROC, RETURN
*
*
***********************************************************************
*    THIS ROUTINE READS THE JFCB AND COPIES IT FROM THE SWA INTO THE
*    PROGRAMMER-SUPPLIED AREA.
***********************************************************************
*
RDJFCB   RDJFCB FILEDCB
         LTR   R15,R15             CHK IF DD STMT IN JCL
         BNZ   NODD                IF RC NOT 0, DD STMT MISSING
         BR    R6                  RETURN TO CALLING RTN
NODD     WTO   '*** FILEDD NOT SPECIFIED IN JCL ***',ROUTCDE=11
         ABEND 901
*
*
***********************************************************************
*    THIS ROUTINE MODIFIES THE JFCB. THE CURRENT DASD DATASET NAME FROM
*    THE TABLE IS PUT INTO THE DATASET-NAME FIELD OF THE JFCB.
***********************************************************************
*
MDFYJFCB LA    R10,JFCB            SET ADRBLTY FOR JFCB DSECT
         L     R12,DSTBLPTR        LD CUR DSN ENTRY ADR, SET DSECT
*                                  ADRBLTY
         CLI   DSNAME,X'00'        CHK FOR LAST DSN ENTRY
         BE    VOLEND              IF YES, ALL DSNMS PROCESSED
         MVC   JFCBDSNM,DSNAME     PUT CUR DSN INTO JFCB
         LA    R14,TBLENLEN(0,R12) INCR TO NEXT DSN ENTRY
         ST    R14,DSTBLPTR        SAVE ADR OF NEXT DSN ENTRY
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE ENQS THE DATASET WHICH WILL BE ALLOCATED VIA OPEN.
***********************************************************************
*
ENQ      MVC   RNAME,DSNAME        MOVE DSNAME FOR ENQ
         L     R2,DSNMLEN          LOAD LEN OF RNAME
         ENQ   (QNAME,RNAME,S,(R2),SYSTEMS)
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE OPENS THE DCB AND ALLOCATES THE DSNAME SPECIFIED IN
*    THE PROGRAMMER-SUPPLIED JFCB.
***********************************************************************
*
OPEN     LA    R11,FILEDCB         SET ADRBLTY FOR DCB DSECT
         OPEN  FILEDCB,TYPE=J      OPEN, USE JFCB IN PROG
         ...                       CHK FOR GOOD OPEN
         BR    R6                  IF YES, RET TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE READS A RECORD FROM THE DATASET AND PROCESSES IT.
***********************************************************************
*
PROCESS  GET   FILEDCB             READ A RECORD FROM DATASET
         ...                       PROCESS A RECORD
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE CLOSES THE DCB.
***********************************************************************
*
CLOSE    CLOSE FILEDCB             CLOSE DATASET
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE DEQS THE DATASET.
***********************************************************************
*
DEQ      L     R2,DSNMLEN
         DEQ   (QNAME,RNAME,(R2),SYSTEMS)
         BR    R6
*
*
***********************************************************************
*    THIS ROUTINE RESTORES THE REGISTERS AND RETURNS CONTROL.
***********************************************************************
*
RETURN   RCNTL RC=0                RETURN TO MVS OR CALLING PROG
*
*
***********************************************************************
*    DC/DS STATEMENTS FOR DASD DATASET NAME TABLE
***********************************************************************
*
DSTBLPTR DC    A(DSNTBL)
*
DSNTBL   DS    0F
TBLENTBG EQU   *
         DC    A(L'DS01)
DS01     DC    C'USER.FILE01'
         DC    CL(45-L'DS01)' '
TBLENTX  EQU   *
         DC    A(L'DS02)
DS02     DC    C'USER.FILE02'
         DC    CL(45-L'DS02)' '
         DC    A(L'DS03)
DS03     DC    C'USER.FILE03'
         DC    CL(45-L'DS03)' '
         DC    F'0'                INDICATE END-OF-DSN TABLE
TBLENLEN EQU   TBLENTX-TBLENTBG
*
*
***********************************************************************
*    DC/DS STATEMENTS FOR ENQ/DEQ MACROS
***********************************************************************
*
QNAME    DC    CL8'SYSDSN'
RNAME    DS    CL44
*
*
***********************************************************************
*    THE JFCB-LIST AND THE DCB
***********************************************************************
*
JFCB     DS    44F
JFCBPTR  DC    X'87'
         DC    AL3(JFCB)
*
FILEDCB  DCB   DSORG=PS,MACRF=GL,EXLST=JFCBPTR,EODAD=DATAEND,          -
               DDNAME=FILEDD
*
*
***********************************************************************
*    THE DSECTS
***********************************************************************
*
         DSECT
         IEFJFCBN              GENERATES JFCB DSECT
*
DSTBLMAP DSECT
DSNMLEN  DS    CL4
DSNAME   DS    CL44
         DS    CL1             REQD TO PREVENT ASM ERROR
*
*
***********************************************************************
*    END OF PROGRAM
***********************************************************************
*
         END
