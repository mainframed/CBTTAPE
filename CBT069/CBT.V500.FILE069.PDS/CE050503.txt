*
* CODING EXAMPLE 5.5.3
*
*
         MACRO
&LABEL   VALNUMB &ADR,&LEN
         LCLC  &LBL
&LBL     SETC  '$'.'VAL'.'&SYSNDX'(2,3)
         AIF   (T'&ADR EQ 'O').NOADR
         AIF   ('&ADR'(1,1) EQ '(').REGA
         AIF   (T'&LEN EQ 'O').ADRONLY
         AIF   (T'&LEN NE 'N').INVLEN
&LABEL   TRT   &ADR.(&LEN),&LBL.A
         AGO   .BC
.ADRONLY ANOP
&LABEL   TRT   &ADR,&LBL.A
.BC      BC    8,&LBL.B
         B     &LBL.C
         AGO   .REST
.REGA    AIF   (T'&LEN EQ 'O').NOLEN
         AIF   ('&LEN'(1,1) EQ '(').REGAL
&LABEL   TRT   0(&LEN,&ADR(1)),&LBL.A
         BC    8,&LBL.B
         B     &LBL.C
         AGO   .REST
.REGAL   ANOP
&LABEL   BCTR  &LEN(1),0
         EX    &LEN(1),&LBL.E
         LA    &LEN(1),1(0,&LEN(1))
         BC    8,&LBL.B
         B     &LBL.C
&LBL.E   TRT   0(0,&ADR(1)),&LBL.A
.REST    ANOP
&LBL.A   DC    256XL1'01'
         ORG   &LBL.A+C'0'
         DC    10XL1'00'
         ORG   &LBL.A+256
&LBL.B   LA    R15,0
         B     &LBL.D
&LBL.C   LA    R15,4
         B     &LBL.D
&LBL.D   DS    0H
         MEXIT
.NOADR   MNOTE 4,'DATA FIELD ADDRESS NOT SPECIFIED---MACRO NOT GENERATE-
               D'
         MEXIT
.NOLEN   MNOTE 4,'LENGTH NOT SPECIFIED---MACRO NOT GENERATED'
         MEXIT
.INVLEN  MNOTE 4,'INVALID LENGTH SPECIFIED---MACRO NOT GENERATED'
         MEXIT
         MEND
