*
* CODING EXAMPLE 8.6.2
*
*
PDSUPDTB CSECT
*
*
***********************************************************************
*    INITIALIZATION
***********************************************************************
*
         INITL 3,EQU=R             INITIALIZE PROGRAM
*
*
***********************************************************************
*    MAINSTREAM OF PROGRAM
***********************************************************************
*
         BAL   R6,OPEN             OPEN DATASETS
NEXTMEMB BAL   R6,LOCMEMB          LOC SPECIFIED MEMB IN PDS
NEXTREC  BAL   R6,READMEMB         READ A REC OF MEMB
         BAL   R6,UPDTMEMB         UPDATE REC
         BAL   R6,WRTMEMB          REWRITE REC OF MEMB
         B     NEXTREC             READ NEXT REC OF MEMB
MEMBEND  BAL   R6,STOW             MEMB-END, UPDATE DIR
         B     NEXTMEMB            GET NEXT MEMB
MEMBENDX BAL   R6,CLOSE            END-OF-MEMBS, CLOSE DATASETS
         B     RETURN              RETURN TO MVS OR CALLING PROG
*
*
***********************************************************************
*    THIS ROUTINE OPENS ALL THE DCBS.
***********************************************************************
*
OPEN     OPEN  (SYSIN,,PDSBDCBI,,PDSBDCBO,(OUTPUT))
         ...                       CHECK FOR GOOD OPEN
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE LOCATES THE NEXT MEMBER WHICH IS TO BE PROCESSED. IN
*    ORDER TO PERFORM THAT TASK, THIS ROUTINE BRANCHES AND LINKS TO THE
*    SUBROUTINES WHICH PERFORM THE FOLLOWING FUNCTIONS:
*        * READ THE NEXT MEMBER NAME FROM THE SYSIN DATASET;
*        * SET THE DASD LOCATION IN THE PDS DCB TO THE BEGINNING OF THE
*          MEMBER WHICH IS TO PROCESSED.
***********************************************************************
*
LOCMEMB  BAL   R7,GETNAME          GET A SPEC MEMB NAME FROM SEQ DS
         BAL   R7,FIND             SET MEMB ADR IN PDS DCB
         LTR   R15,R15             CHK IF SPEC MEMB IN PDS
         BNZ   LOCMEMB             IF NO, GET NEXT SPEC MEMB
         BR    R6                  IF YES, RET TO CALLING RTN
*
*
***********************************************************************
*    THIS SUBROUTINE READS THE NAME OF THE NEXT MEMBER, WHICH IS TO BE
*    PROCESSED, FROM THE SYSIN DATASET.
***********************************************************************
*
GETNAME  GET   SYSIN,MEMBNAME      GET A SPEC MEMB NAME
         BR    R7                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS SUBROUTINE CAUSES THE LOCATION OF THE BEGINNING OF THE
*    SPECIFIED MEMBER TO BE PLACED INTO THE PDS DCB.
***********************************************************************
*
FIND     FIND  PDSBDCBI,MEMBNAME,D
         LTR   R15,R15             CHK IF MEMB IN PDS
         BNZ   NOMEMB              IF NO, PROC CONDITION
         BR    R7                  RETURN TO CALLING RTN
NOMEMB   ...                       ISSUE WARN MSG, ETC.
         BR    R7                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE READS A RECORD OF THE SPECIFIED MEMBER FROM THE PDS.
***********************************************************************
*
READMEMB READ  RDECB,SF,PDSBDCBI,BLOCK
         CHECK RDECB               CHECK IF READ OK
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE UPDATES THE RECORDS OF THE MEMBERS.
***********************************************************************
*
UPDTMEMB ...                       UPDATE RECORD
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE REWRITES THE RECORDS OF THE MEMBERS TO THE PDS.
***********************************************************************
*
WRTMEMB  WRITE WDECB,SF,PDSDCB2O,BLOCK
         CHECK WDECB               CHECK IF WRITE OK
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE ISSUES THE STOW MACRO TO UPDATE THE TTR OF THE
*    DIRECTORY ENTRY OF THE PDS TO POINT TO THE UPDATED MEMBER.
***********************************************************************
*
STOW     MVC   STOWNAME,MEMBNAME   PUT MEMB NAME INTO STOW-LIST
         STOW  PDSBDCBO,STOWLST,R      UPDATE DIR
         LTR   R15,R15             CHK IF REPLACE OK
         BZR   R6                  IF YES, RET TO CALLING RTN
         C     R15,BIN12           CHK FOR NO DIR SPC
         BE    NODIRSPC            IF YES, DSPLY ERROR MSG
         ...                       CHECK OTHER RETURN CODES
NODIRSPC WTO   '*** NO MORE DIRECTORY SPACE FOR NEW MEMBER ***',       -
               ROUTCDE=11
         ABEND 901
*
*
***********************************************************************
*    THIS ROUTINE CLOSES ALL THE DCBS.
***********************************************************************
*
CLOSE    CLOSE (SYSIN,,PDSBDCBI,,PDSBDCBO)
         BR    R6
*
*
***********************************************************************
*    THIS ROUTINE RESTORES THE REGISTERS AND RETURNS CONTROL.
***********************************************************************
*
RETURN   RCNTL RC=0                RETURN TO MVS OR CALLING PROG
*
*
***********************************************************************
*    DC/DS STATEMENTS
***********************************************************************
*
BIN12    DC    F'12'
BLOCK    DS    CL1000
MEMBNAME DS    CL8
         DS    CL72
*
*
***********************************************************************
*    THE STOW-LIST
***********************************************************************
*
STOWLST  DS    0CL12
STOWNAME DS    CL8
STOWTTR  DS    CL3
STOWC    DC    X'00'
*
*
***********************************************************************
*    THE DCBS
***********************************************************************
*
SYSIN    DCB   DSORG=PS,MACRF=GM,LRECL=80,RECFM=FB,EODAD=MEMBENDX,     -
               DDNAME=SYSIN
PDSBDCBI DCB   DSORG=PO,MACRF=R,EODAD=MEMBEND,DDNAME=PDSDD
PDSBDCBO DCB   DSORG=PO,MACRF=W,DDNAME=PDSDD
*
*
***********************************************************************
*    END OF PROGRAM
***********************************************************************
*
         END
