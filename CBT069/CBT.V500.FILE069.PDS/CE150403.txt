*
* CODING EXAMPLE 15.4.3
*
*
BLDETBL  BAL   R8,SETET01          SET UP ENTRY TABLE PARAMETER-LIST
         ETCRE ENTRIES=ET01PGMS    CREATE ENTRY TABLE
         ST    R0,ET01TKEN         SAVE RETURNED TOKEN
         BR    R7                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS SUBROUTINE CREATES THE EX=0 AND THE EX=1 ENTRIES IN THE ENTRY
*    TABLE PARAMETER-LIST WHICH DEFINES THE PROGRAMS SERV01 AND SERV02.
***********************************************************************
*
         USING ETD,R10             DEFINE REG FOR ENT-TBL HEADER DSECT
SETET01  LA    R10,ETDHDR          POINT TO AREA FOR ENT-TBL HDR
         MVC   ETDNUM,=H'2'        IND 2 PGMS ARE DEFINED IN PARM-LIST
*
         DROP  R10                 RELEASE REG 10 FOR REUSE
         USING ETDELE,R10          DEFINE REG FOR E.T. ELEMENT DSECT
*
*  DEFINITION FOR PC ROUTINE SERV01
*
         LA    R10,ET01S01         POINT TO PARM AREA FOR SERV-PROG-1
         MVI   ETDEX,X'00'         INDICATE EX=0
         OI    ETDFLG,ETDSUP+ETDXM   IND SUPV MODE AND ADR SPACE SWITCH
         MVC   ETDAKM,=X'FFFF'     IND CALLING PGM CAN HAVE ANY KEY
         MVC   ETDEKM,=X'8000'     IND PC RTN CAN ACCESS STORAGE WHICH
*                                  HAS KEY 0 AND CALLING PROG KEYS
         MVC   ETDPAR,=C'OPT2'     SPECIFY PARM TO BE PASSED TO SERV01
         XC    ETDPRO1,ETDPRO1     ZERO FIELD TO IND EPA SPECIFIED
         MVC   ETDPRO2,EPAPC01     LOAD EPA OF SERV-PGM-1
*
*  DEFINITION FOR PC ROUTINE SERV02
*
         LA    R10,ETD01S02        POINT TO PARM AREA FOR SERV-PROG-2
         MVI   ETDEX,X'01'         INDICATE EX=1
         OI    ETDFLG,ETDSUP+ETDXM   IND SUPV MODE AND ADR SPACE SWITCH
         MVC   ETDAKM,=X'8080'     IND CALLING PGM MUST HAVE KEY 0 OR 8
         MVC   ETDEKM,=X'FFFF'     IND PC RTN CAN ACCESS STORAGE WHICH
*                                  HAS ANY KEY
         MVC   ETDPAR,=C'SW=5'     SPECIFY PARM TO BE PASSED TO SERV02
         MVC   ETDPRO1(8),=CL8'SERV02'     MOVE NAME OF SERV-PGM-2
*
         BR    R8                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THE AREA FOR THE ENTRY TABLE AND THE SAVE AREA FOR THE RETURNED
*    TOKEN FROM THE ETCRE MACRO
***********************************************************************
*
ET01PGMS DS    0F
ETD01HDR DS    CL(ETDLEN)          ENTRY TABLE HEADER
ETD01S01 DS    CL(ETDELEN)         EX=0 ENTRY FOR PGM SERV01
ETD01S02 DS    CL(ETDELEN)         EX=1 ENTRY FOR PGM SERV02
*
ET01TKEN DS    F
*
*
***********************************************************************
*    THE DSECTS FOR THE ENTRY TABLE
***********************************************************************
*
         IHAETD                    GENERATES DSECTS FOR ENTRY TABLE
