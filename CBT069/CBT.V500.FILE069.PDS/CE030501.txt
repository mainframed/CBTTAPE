*
* CODING EXAMPLE 3.5.1
*
*
         MACRO                   HEADER STATEMENT
&LABEL   ADD   &NUMB1,&NUMB2     PROTOTYPE STATEMENT
         LCLC  &LAB              DECLARE SETC SYMBOL FOR EDITED LABEL
         LCLC  &ERRSW            DECLARE SETC SYMB FOR PARM VALIDATION
&ERRSW   SETC  '0'               INIT ERRSW TO INDICATE NO ERRORS
&LAB     SETC  '&LABEL'
         AIF   (K'&LAB LE 8).LABOK    CHECK LABEL LENGTH
&LAB     SETC  '&LABEL'(1,8)     SELECT FIRST 8 CHARACTERS
         MNOTE *,'LABEL EXCEEDS 8 CHARACTERS---LEFT MOST 8 USED'
.LABOK   ANOP
.*
.* THE FOLLOWING AIF INSTRUCTIONS VALIDATE THE VALUE SPECIFIED FOR
.* THE SYMBOLIC PARAMETER &NUMB1
.*
         AIF   (T'&NUMB1 EQ 'O').NON1
         AIF   (T'&NUMB1 NE 'N').INVN1
         AIF   (&NUMB1 GT 4095).BIGN1
.*
.* THE FOLLOWING AIF INSTRUCTIONS VALIDATE THE VALUE SPECIFIED FOR
.* THE SYMBOLIC PARAMETER &NUMB2
.*
.V2      AIF   (T'&NUMB2 EQ 'O').NON2
         AIF   (T'&NUMB2 NE 'N').INVN2
         AIF   (&NUMB2 GT 4095).BIGN2
         AIF   ('ERRSW' EQ '0').GENCODE
         MEXIT
.GENCODE ANOP
.*
.* THE FOLLOWING MODEL STATEMENTS ARE GENERATED IF THE VALUES FOR
.* &NUMB1 AND &NUMB2 ARE SPECIFIED CORRECTLY
.*
&LAB     LA    R14,&NUMB1
         LA    R15,&NUMB2
         AR    R14,R15
         MEXIT
.*
.* MNOTES FOR THE VALIDATION OF &NUMB1 AND &NUMB2
.*
.NON1    MNOTE 8,'FIRST NUMBER OMITTED---MACRO NOT GENERATED'
&ERRSW   SETC  '1'                 TURN ON ERRSW TO INDICATE PARM ERROR
         AGO   .V2
.INVN1   MNOTE 8,'FIRST NUMBER IS INVALID---MACRO NOT GENERATED'
&ERRSW   SETC  '1'                 TURN ON ERRSW TO INDICATE PARM ERROR
         AGO   .V2
.BIGN1   MNOTE 8,'FIRST NUMBER EXCEEDS 4095---MACRO NOT GENERATED'
&ERRSW   SETC  '1'                 TURN ON ERRSW TO INDICATE PARM ERROR
         AGO   .V2
.NON2    MNOTE 8,'SECOND NUMBER OMITTED---MACRO NOT GENERATED'
         MEXIT
.INVN2   MNOTE 8,'SECOND NUMBER IS INVALID---MACRO NOT GENERATED'
         MEXIT
.BIGN2   MNOTE 8,'SECOND NUMBER EXCEEDS 4095---MACRO NOT GENERATED'
.*
         MEND                    TRAILER STATEMENT
