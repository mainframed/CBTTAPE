*
* CODING EXAMPLE 12.6.2
*
*
IGC0025C CSECT
*
*
***********************************************************************
*    INITIALIZATION
***********************************************************************
*
         REGEQU EQU=R                  DEFINE EQU'S FOR REGISTERS
         USING *,R6                    SETUP ADDRESSABILITY
*
*
***********************************************************************
*    MAINSTREAM OF SVC ROUTINE
***********************************************************************
*
         BAL   R8,SAVEREGS             SAVE REGISTERS
         BAL   R8,SETKEY               SET PSW KEY TO THAT OF CALLER
         BAL   R8,GETCSA               GET REQUESTED CSA STORAGE
         BAL   R8,RSTORKEY             RESTORE PSW KEY TO ZERO
         B     RETURN                  RETURN TO CALLING PROG VIA FLIH
*
*
***********************************************************************
*    THIS ROUTINE SAVES THE CONTENTS OF ALL REGISTERS WHICH CONTAIN
*    CONTROL INFORMATION.
***********************************************************************
*
SAVEREGS LR    R12,R14                 SAVE RETURN ADDRESS
         LR    R10,R0                 SAVE REQUESTED LENGTH OF CSA ALLO
         WTO   '*** SVC 253 ENTERED ***',ROUTCDE=11    * TESTING ONLY *
         BR    R8                      RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE CHANGES THE PSW STORAGE PROTECTION KEY TO THAT OF
*    THE CALLING PROGRAM. THIS IS DONE SO THAT THE ALLOCATED CSA
*    STORAGE IS SET TO THE PROTECTION KEY OF THAT OF THE CALLING
*    PROGRAM INSTEAD OF THE PROTECTION KEY OF THE SVC ROUTINE WHICH IS
*    KEY ZERO.
***********************************************************************
*
SETKEY   MODESET KEY=NZERO             SET KEY TO THAT OF CALLER
         BR    R8                      RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE ALLOCATES FETCH-PROTECTED VIRTUAL STORAGE FROM THE
*    CSA. THE LENGTH OF THE REQUESTED CSA STORAGE IS SPECIFIED VIA
*    REGISTER 0 BY THE CALLER OF THE SVC. THE SAVEREGS ROUTINE SAVES
*    REGISTER 0 IN REGISTER 10.
***********************************************************************
*
GETCSA   GETMAIN RC,LV=(R10),SP=231    ALLO CSA STORAGE
         LR    R11,R1                  SAVE ADDRESS OF CSA STORAGE
         LR    R9,R15                  SAVE RC FROM GETMAIN
         BR    R8                      RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE RESTORES THE PSW STORAGE PROTECTION KEY BACK TO KEY
*    ZERO. THIS IS REQUIRED OR THE SVC ROUTINE WILL ABEND WHEN IT
*    RETURNES CONTROL.
***********************************************************************
*
RSTORKEY MODESET KEY=ZERO              RESTORE KEY VALUE OF SVC
         BR    R8                      RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE LOADS THE PARAMETER REGISTERS, RESTORES THE RETURN
*    ADDRESS INTO REGISTER 14 AND RETURNS CONTROL TO THE FLIH.
***********************************************************************
*
RETURN   LR    R1,R11                  LOAD ALLO CSA ADR IN A PARM REG
         LR    R15,R9                  LOAD RC IN A PARM REG
         LR    R14,R12                 RESTORE RETURN ADDRESS
         BR    R14                     RETURN TO CALLING PROG VIA FLIH
*
*
***********************************************************************
*    END OF SVC ROUTINE
***********************************************************************
*
         END
*
*
*
*
***********************************************************************
*    CODE TO INVOKE SVC IN CODING EXAMPLE 12.6.2.
***********************************************************************
         ...
         LA    R0,100                  SET LENGTH OF REQ CSA
         SVC   253                     ISSUE SVC TO ALLO CSA
         LTR   R15,R15                 CHECK RC
         BNZ   NOCSA                   IF BAD, NO CSA AVAIL
         MVC   0(100,R1),CSADATA       IF OK, MOVE DATA INTO CSA
         ...
NOCSA    ...
