*
CODING EXAMPLE A.3.2
*
*
//-------- JOB  ----
//CALLASM  EXEC ASMACLG
//C.SYSLIB DD   DSN=SYS1.MACLIB,DISP=SHR
//         DD   DSN=USER.MACLIB,DISP=SHR
//C.SYSIN  DD *
*
*
INVKASM2 CSECT
*
*
***********************************************************************
*    INITIALIZATION
***********************************************************************
*
         INITL 3,EQU=R
         USING S99RBP,R10      DEFINE REG FOR SVC99 REQ-BLK-PTR DSECT
         USING S99RB,R11       DEFINE REG FOR SVC99 REQ-BLOCK DSECT
*
*
***********************************************************************
*    MAINSTREAM OF PROGRAM
***********************************************************************
*
NXTSRCNM BAL   R6,GETSRCNM         GET PGM NAMES FROM SRC LIB DIR
         LTR   R15,R15             CHECK IF END-OF-DIR
         BNZ   RETURN              IF YES, TERMINATE PROGRAM
         BAL   R6,DYNALLOC         IF NO, DYN ALLOC SRC LIB AND PGM
         BAL   R6,LINKASM          LINK TO ASSEMBLER
         BAL   R6,UNALLOC          DYN UNALLOC SRC LIB AND PGM
         B     NXTSRCNM            GET NEXT PROGRAM NAME
*
*
***********************************************************************
*    THIS ROUTINE OBTAINS ALL THE PROGRAM NAMES FROM THE SOURCE
*    LIBRARY DIRECTORY ONE AT A TIME. ALIASES ARE IGNORED.
***********************************************************************
*
GETSRCNM READDIR SRCDIR            GET PGM NAMES FROM LIB DIR
         C     R15,BIN4            CHECK FOR ALIAS NAME
         BE    GETSRCNM            IF YES, IGNORE
         MVC   PGMNAME,$MEMBER     PUT PGM NAME INTO DA-MEMB TEXT UNIT
         BR    R6                  RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS ROUTINE DYNAMICALLY ALLOCATES THE SOURCE LIBRARY AND THE
*    MEMBER (PROGRAM SOURCE NAME).
***********************************************************************
*
DYNALLOC BAL   R7,SETRBA01         SETUP RB FOR ALLOC
         BAL   R7,SETRBPTR         SETUP RB PTR
         BAL   R7,DOSVC99          ISSUE SVC 99
         BR    R6                  RETURN TO MVS OR CALLING PROG
*
*
***********************************************************************
*    THIS SUBROUTINE SETS UP THE SVC99 REQUEST BLOCK FOR ALLOCATION.
***********************************************************************
*
SETRBA01 LA    R11,REQBLK          SET ADRBLTY FOR RB DSECT
         XC    REQBLK,REQBLK       CLEAR RB
         MVI   S99RBLN,REQBLKLN    SET RB LEN
         MVI   S99VERB,S99VRBAL    SET ALLOCATION VERB
         LA    R10,TUPTRA01        LD ADR OF BG OF TEXT-UNIT-PTR LIST
         ST    R10,S99TXTPP        STORE THAT ADR INTO RB
         BR    R7                  RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS SUBROUTINE SETS UP THE SVC99 REQUEST-BLOCK-POINTER.
***********************************************************************
*
SETRBPTR LA    R10,RBPTR           SET ADRBLTY FOR RBPTR DSECT
         LA    R11,REQBLK          LD ADR OF RB
         ST    R11,S99RBPTR        STORE RB ADR INTO RB PTR
         OI    S99RBPTR,S99RBPND   TURN ON HIGH ORDER BIT
         BR    R7                  RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS SUBROUTINE ISSUES SVC 99 VIA THE DYNALLOC MACRO.
***********************************************************************
*
DOSVC99  LA    R1,RBPTR            LD ADR OF RB PTR INTO R1 FOR SVC 99
         DYNALLOC                  ISSUE SVC 99
         LTR   R15,R15             CHK RC
         BNZ   CHKERR              IF NOT ZERO, CHK ERROR/INFO FIELDS
         BR    R7                  IF OK, RETURN TO CALLING ROUTINE
CHKERR   LH    R8,S99ERROR
         LH    R9,S99INFO
         ABEND 900,DUMP
*
*
***********************************************************************
*    THIS ROUTINE LINKS TO THE ASSEMBLER.
***********************************************************************
*
LINKASM  LINK  EP=ASMA90,PARAM=(OPTLIST,DDNLIST),VL=1
         BR    R6                  RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS ROUTINE UNALLOCATES THE DDNAME ALLOCATED IN THE DYNALLOC
*    ROUTINE.
***********************************************************************
*
UNALLOC  BAL   R7,SETRBU01         SETUP RB FOR UNALLO
         BAL   R7,SETRBPTR         SETUP RB PTR
         BAL   R7,DOSVC99          ISSUE SVC 99
         BR    R6                  RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS SUBROUTINE SETS UP THE SVC99 REQUEST BLOCK FOR UNALLOC.
***********************************************************************
*
SETRBU01 LA    R11,REQBLK          SET ADRBLTY FOR RB DSECT
         XC    REQBLK,REQBLK       CLEAR RB
         MVI   S99RBLN,REQBLKLN    SET RB LEN
         MVI   S99VERB,S99VRBUN    SET ALLOCATION VERB
         LA    R10,TUPTRU01        LD ADR OF BG OF TEXT-UNIT-PTR LIST
         ST    R10,S99TXTPP        STORE THAT ADR INTO RB
         BR    R7                  RETURN TO CALLING ROUTINE
*
*
***********************************************************************
*    THIS ROUTINE RESTORES THE REGISTERS AND RETURNS
***********************************************************************
*
RETURN   RCNTL RC=0                RETURN TO MVS OR CALLING PROG
*
*
*
*
***********************************************************************
*     CONSTANTS
***********************************************************************
*
BIN4     DC    F'4'
*
*
***********************************************************************
*     PARAMETER-LISTS FOR DYNAMIC ALLOCATION OF SOURCE LIBRARY AND
*     MEMBER (PROGRAM SOURCE) NAME
***********************************************************************
*
*
***********************************************************************
*    SVC99 REQ-BLOCK-POINTER AND REQUEST BLOCK
***********************************************************************
*
RBPTR    DS    F
REQBLK   DS    CL(S99RBEND-S99RB)
REQBLKLN EQU   L'REQBLK
*
*
***********************************************************************
*    TEXT-UNITS-POINTERS LISTS
***********************************************************************
*
TUPTRA01 DS    0F
         DC    A(TUDDN)            ADR OF TU FOR DDNAME
         DC    A(TUDSN)            ADR OF TU FOR DSNAME
         DC    A(TUMEMB)           ADR OF TU FOR MEMBER NAME
         DC    A(TUDISP1)          ADR OF TU FOR DS STATUS
         DC    X'80'               INDICATES LAST TU ADDRESS
         DC    AL3(TUDISP2)        ADR OF TU FOR DS NORM TERM DISP
*
TUPTRU01 DS    0F
         DC    X'80'               INDICATES LAST TU ADDRESS
         DC    AL3(TUDDN)          ADR OF TU FOR DDNAME
*
*
***********************************************************************
*    TEXT UNITS
***********************************************************************
*
TUDDN    DC    AL2(DALDDNAM)       TU KEY FOR DDNAME
         DC    AL2(1)              NUMBER OF ENTRIES
         DC    AL2(8)              LENGTH OF DDNAME
         DC    CL8'SOURCE'         DDNAME
*
TUDSN    DC    AL2(DALDSNAM),AL2(1),AL2(44)
LIBNAME  DC    CL44'USER.TEST.SRCLIB'        DATASET NAME
*
TUMEMB   DC    AL2(DALMEMBR),AL2(1),AL2(8)
PGMNAME  DS    CL8                           MEMBER NAME
*
TUDISP1  DC    AL2(DALSTATS),AL2(1),AL2(1),X'01'    OLD
*
TUDISP2  DC    AL2(DALNDISP),AL2(1),AL2(1),X'08'    KEEP
*
*
***********************************************************************
*     END OF PARAMETER-LISTS FOR DYNAMIC ALLOCATION
***********************************************************************
*
*
***********************************************************************
*     PARAMETER-LISTS FOR DYNAMIC INVOCATION OF ASSEMBLER
***********************************************************************
*
         DS    0H
OPTLIST  DC    AL2(OPTLEN)
OPTIONS  DC    C'XREF(SHORT),OBJECT'
OPTLEN   EQU   *-OPTIONS
*
*
         DS    0H
DDNLIST  DC    AL2(DDNLEN)
DDNAMES  DC    CL8'OBJECT'     SYSLIN
         DC    XL8'00'         N/A
         DC    XL8'00'         N/A
         DC    XL8'00'         SYSLIB
         DC    CL8'SOURCE'     SYSIN
         DC    CL8'LISTING'    SYSPRINT
DDNLEN   EQU   *-DDNAMES
*
*
***********************************************************************
*     END OF PARAMETER-LISTS FOR ASSEMBLER
***********************************************************************
*
*
***********************************************************************
*     DSECTS FOR DYNAMIC ALLOCATION
***********************************************************************
*
         IEFZB4D0        GENS DSECT FOR REQ BLOCK, TEXT UNIT, ETC.
         IEFZB4D2        GENS TABLE OF EQUATES FOR TU KEYS
*
*
***********************************************************************
*    END OF PROGRAM
***********************************************************************
*
*
         END
//*
//*
//G.SYSUT1   DD UNIT=VIO,SPACE=(CYL,(1,1)),DCB=BUFNO=10
//G.SYSLIB   DD DSN=SYS1.MACLIB,DISP=SHR
//           DD DSN=USER.MACLIB,DISP=SHR
//G.LISTING  DD SYSOUT=*
//G.OBJECT   DD SYSOUT=*
//G.SRCDIR   DD DSN=USER.TEST.SRCLIB,DISP=SHR
//
