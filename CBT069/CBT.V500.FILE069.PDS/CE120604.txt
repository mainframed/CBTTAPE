*
* CODING EXAMPLE 12.6.4
*
*
IGC0025D CSECT
*
*
***********************************************************************
*    INITIALIZATION
***********************************************************************
*
         REGEQU EQU=R                  GENERATE EQU'S FOR REGISTERS
         USING *,R6                    SETUP ADDRESSABILITY
         USING TCB,R4                  DEFINE DESCT REG FOR TCB
         USING IEZJSCB,R5              DEFINE DSECT REG FOR JSCB
*
*
***********************************************************************
*    MAINSTREAM OF SVC ROUTINE
***********************************************************************
*
         BAL   R8,SAVEREGS             SAVE REGISTERS
         BAL   R8,GETJSCB              GET JSCB ADR
         BAL   R8,VERPW                VERIFY PASSWORD FROM CALLER
         BAL   R8,SETAPF               SET REQ APF INDICATOR
         B     RETURN                  RETURN TO CALLING PROG VIA FLIH
*
*
***********************************************************************
*    THIS ROUTINE SAVES THE CONTENTS OF ALL REGISTERS WHICH CONTAIN
*    CONTROL INFORMATION.
***********************************************************************
*
SAVEREGS LR    R12,R14                 SAVE RETURN ADR
         LR    R10,R0                  SAVE APF INDICATOR
         LR    R11,R1                  SAVE USER SPECIFIED PW
         WTO   '*** SVC 254 ENTERED ***',ROUTCDE=11    FOR TESTING ONLY
         BR    R8                      RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE OBTAINS THE ADDRESS OF THE JSCB FROM THE TCB.
***********************************************************************
*
GETJSCB  L     R5,TCBJSCB              LOAD JSCB ADR FROM TCB
         BR    R8                      RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE VERIFIES THE PASSWORD RECEIVED FROM THE CALLER OF
*    THIS SVC ROUTINE. THE PASSWORD IS SPECIFIED IN REGISTER 1. THE
*    SAVEREGS ROUTINE SAVES REGISTER 1 IN REGISTER 11. IF THE PASSWORD
*    IS OK, THEN THE REQUESTED SERVICE IS PERFORMED. IF THE PASSWORD
*    IS INVALID, THEN AN RC OF X'08' IS SET AND CONTROL IS RETURNED TO
*    THE CALLER WITHOUT THE REQUESTED SERVICE BEING PERFORMED.
***********************************************************************
*
VERPW    C     R11,PASSWORD            CHECK CALLER'S PASSWORD
         BER   R8                      IF OK, RET TO CALLING RTN
         LA    R15,8                   IF BAD, INDICATE IT
         B     RETURN                  RETURN TO CALLING PROG VIA FLIH
*
*
***********************************************************************
*    THIS ROUTINE SETS THE APF BIT AS REQUESTED BY THE CALLER OF THIS
*    SVC ROUTINE. THE REQUESTED ACTION IS SPECIFIED IN REGISTER 0.
*    THE SAVEREGS ROUTINE SAVES REGISTER 0 IN REGISTER 10. X'00'
*    INDICATES APF ON, X'04' INDICATES APF OFF. IF THE CALLING
*    PROGRAM SPECIFIES AN INVALID INDICATOR, THEN AN RC OF X'04' IS
*    SET AND CONTROL IS RETURNED TO THE CALLER WITHOUT THE REQUESTED
*    SERVICE BEING PERFORMED.
***********************************************************************
*
SETAPF   C     R10,BIN0                CHK IF APF REQ IS FOR "ON"
         BE    SETAON                  IF YES, DO IT
         C     R10,BIN4                CHK IF APF REQ IS FOR "OFF"
         BE    SETAOFF                 IF YES, DO IT
         LA    R15,4                   INDICATE BAD REQ
         BR    R8                      RETURN TO CALLING RTN
SETAON   OI    JSCBOPTS,X'01'          SET APF ON
         WTO   '*** AUTH ON ***',ROUTCDE=11      * FOR TESTING ONLY *
         LA    R15,0                   SET GOOD RC
         BR    R8                      RETURN TO CALLING RTN
SETAOFF  NI    JSCBOPTS,X'FE'          SET APF OFF
         WTO   '*** AUTH OFF ***',ROUTCDE=11     * FOR TESTING ONLY *
         LA    R15,0                   SET GOOD RC CODE
         BR    R8                      RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE RESTORES THE RETURN ADDRESS INTO REGISTER 14 AND
*    RETURNS CONTROL TO THE FLIH.
***********************************************************************
*
RETURN   LR    R14,R12                 RESTORE RETURN ADR
         BR    R14                     RETURN TO CALLING PROG VIA FLIH
*
*
***********************************************************************
*    CONSTANTS
***********************************************************************
*
BIN0     DC    F'0'                    SET APF-ON INDICATOR
BIN4     DC    F'4'                    SET APF-OFF INDICATOR
PASSWORD DC    C'....'                 PASSWORD TO USE THIS SVC
*
*
***********************************************************************
*    THE DSECTS
***********************************************************************
*
         PRINT NOGEN
         IKJTCB                        GENERATES TCB DSECT
         IEZJSCB                       GENERATES JSCB DSECT
*
*
***********************************************************************
*    END OF SVC ROUTINE
***********************************************************************
*
         END
*
*
*
*
***********************************************************************
*    CODE TO INVOKE SVC IN CODING EXAMPLE 12.6.3.
***********************************************************************
         ...
         LA    R0,0                    SET AUTH-ON REQUEST
         L     R1,PASSWORD             LOAD PW FOR SVC
         SVC   254                     ISSUE SVC TO SET AUTH
         C     R15,BIN0                CHECK RC
         BE    AUTHON                  IF 0, AUTH ON
         C     R15,BIN4                CHECK RC
         BE    INVAUTH                 IF 4, BAD AUTH IND
         B     INVPSWD                 IF 8, BAD PW
AUTHON   ...                           PERFORM AUTH FUNCTION

