*
* CODING EXAMPLE 12.6.1
*
*
IGC0025B CSECT
*
*
***********************************************************************
*    INITIALIZATION
***********************************************************************
*
         REGEQU EQU=R                  DEFINE EQU'S FOR REGISTERS
         USING *,R6                    SETUP ADDRESSABILITY
*
*
***********************************************************************
*    MAINSTREAM OF SVC ROUTINE
***********************************************************************
*
         BAL   R8,SAVEREGS             SAVE REGISTERS
         BAL   R8,PUTADDRS             PUT TCB/ASCB ADRS INTO PARM AREA
         B     RETURN                  RETURN TO CALLING PROG VIA FLIH
*
*
***********************************************************************
*    THIS ROUTINE SAVES THE CONTENTS OF ALL REGISTERS WHICH CONTAIN
*    CONTROL INFORMATION.
***********************************************************************
*
SAVEREGS LR    R12,R14                 SAVE RETURN ADDRESS
         LR    R11,R1                  SAVE CALLERS PARM AREA ADDRESS
         BR    R8                      RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE RETURNS THE ADDRESSES OF THE CALLER'S TCB AND ASCB
*    IN THE PARM AREA PROVIDED BY THE CALLER. THE CALLER PASSES THE
*    ADDRESS OF ITS PARM-LIST IN REGISTER 1. THE PARM AREA CONTAINS
*    TWO FULLWORDS. THE FIRST FULLWORD CONTAINS THE ADDRESS OF THE AREA
*    WHICH IS TO RECEIVE THE ADDRESS OF THE TCB AND THE SECOND FULLWORD
*    CONTAINS THE ADDRESS OF THE AREA WHICH IS TO RECEIVE THE ADDRESS
*    OF THE ASCB. THE SAVEREGS ROUTINE SAVES REGISTER 1 IN REGISTER 11.
***********************************************************************
*
PUTADDRS L     R10,0(0,R11)           LOAD ADR OF 1ST WORD OF PARM
         ST    R4,0(0,R10)            PUT TCB ADR INTO PROV AREA
         L     R10,4(0,R11)           LOAD ADR OF 2ND WORD OF PARM
         ST    R7,0(0,R10)            PUT ASCB ADR INTO PROV AREA
         BR    R8                     RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE LOADS THE PARAMETER REGISTERS, RESTORES THE RETURN
*    ADDRESS INTO REGISTER 14 AND RETURNS CONTROL TO THE FLIH.
***********************************************************************
*
RETURN   LR    R1,R11                  RESTORE PARM AREA ADR
         LA    R15,0                   LOAD RC IN A PARM REG
         LR    R14,R12                 RESTORE RETURN ADDRESS
         BR    R14                     RETURN TO CALLING PROG VIA FLIH
*
*
***********************************************************************
*    END OF SVC ROUTINE
***********************************************************************
*
         END
*
*
*
*
***********************************************************************
*    CODE TO INVOKE SVC IN CODING EXAMPLE 12.6.1.
***********************************************************************
         ...
         LA    R1,PARMAREA             LOAD ADR OF PARM AREA
         SVC   252                     ISSUE SVC TO RET TCB/ASCB ADRS
         LTR   R15,R15                 CHECK RC
         BNZ   NOADDRS                 IF NOT 0, ADRS NOT RETURNED
         ...                           USE TCB/ASCB ADRS
PARMAREA DS    0F
TCBADR   DS    F
ASCBADR  DS    F

