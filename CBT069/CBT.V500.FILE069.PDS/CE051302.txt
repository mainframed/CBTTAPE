*
* CODING EXAMPLE 5.13.2
*
*
PUTIRDRV CSECT
*
*
***********************************************************************
*    INITIALIZATION
***********************************************************************
*
         INITL 3,EQU=R             INITIALIZE PROGRAM
         USING IFGRPL,R10          DEFINE REG FOR RPL DSECT
*
*
***********************************************************************
*    MAINSTREAM OF PROGRAM
***********************************************************************
*
         BAL   R6,OPENRDR          OPEN INTERNAL RDR
         BAL   R6,SENDJCL          WRITE JCL TO INTRDR
         BAL   R6,GETJOBNO         GET JOB NUMB OF SUBM JOB
         BAL   R6,CLOSERDR         CLOSE INTERNAL RDR
         B     RETURN              RETURN TO MVS OR CALLING PROG
*
*
***********************************************************************
*    THIS ROUTINE OPENS THE JES2 INTERNAL READER.
***********************************************************************
*
OPENRDR  OPEN  (IRDRACB,(OUTPUT))
         ...                       CHECK FOR GOOD OPEN
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE SUBMITS THE JOB BY WRITTING ALL OF THE JCL STATEMENTS
*    TO THE INTERNAL READER.
***********************************************************************
*
SENDJCL  LA    R10,JCL             LOAD ADR OF BG OF JCL STREAM
NEXTJCL  CLI   0(R10),X'00'        CHK FOR END-OF-JCL
         BER   R6                  IF END, RET TO CALLING RTN
         MVC   JCLSTMT,0(R10)      MOVE JCL STMT INTO VSAM WKAR
         LA    R10,80(0,R10)       INCR TO NEXT JCL STMT
         PUT   RPL=IRDRRPL         WRITE JCL STMT TO INTRDR
         B     NEXTJCL             GET NEXT JCL STMT
*
*
***********************************************************************
*    THIS ROUTINE OBTAINS THE JOB NUMBER OF THE SUBMITTED JOB.
***********************************************************************
*
GETJOBNO ENDREQ RPL=IRDRRPL        INDICATE END-OF-JCL
         LA    R10,IRDRRPL         SET ADRBLTY FOR RPL DSECT
         MVC   JOBID,RPLRBAR       GET JOB NUMBER FROM RPL
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE CLOSES THE JES2 INTERNAL READER.
***********************************************************************
*
CLOSERDR CLOSE IRDRACB
         BR    R6                  RETURN TO CALLING RTN
*
*
***********************************************************************
*    THIS ROUTINE RESTORES THE REGISTERS AND RETURNS CONTROL.
***********************************************************************
*
RETURN   RCNTL RC=0                RETURN TO MVS OR CALLING PROG
*
*
***********************************************************************
*    THE JCL STATEMENTS TO BE SUBMITTED
***********************************************************************
*
JCL      DC    CL80'//DEMOJOB JOB  ACCTNO,NAME,CLASS=A'
         DC    CL80'//STEP    EXEC PGM=IEFBR14'
         DC    CL80'//'
         DC    X'00'          END-OF-JCL INDICATOR
*
*
***********************************************************************
*    THE DC/DS STATEMENTS
***********************************************************************
*
JOBID    DS    CL8
JCLSTMT  DS    CL80
*
*
***********************************************************************
*    THE VSAM ACB AND RPL
***********************************************************************
*
IRDRACB  ACB   AM=VSAM,MACRF=(ADR,SEQ,OUT),DDNAME=INTRDR
IRDRRPL  RPL   AM=VSAM,ACB=IRDRACB,OPTCD=(ADR,SEQ,SYN,NUP,MVE),        -
               AREA=JCLSTMT,AREALEN=80,RECLEN=80
*
*
***********************************************************************
*    THE DSECTS
***********************************************************************
*
         PRINT NOGEN
         IFGRPL                    GENERATES THE RPL DSECT
*
*
***********************************************************************
*    END OF PROGRAM
***********************************************************************
*
         END
