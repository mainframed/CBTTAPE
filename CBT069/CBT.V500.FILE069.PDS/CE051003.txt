*
* CODING EXAMPLE 5.10.3
*
*
         USING ENTRYMAP,R11        DEFINE REG FOR DESCT
*
PUTENTRY MVI   IN1SW,C'1'          (3) INIT NEW FIRST ENTRY SWITCH
         L     R10,LLENTONE        LOAD ADR OF FIRST LOG ENTRY OF LL
         L     R12,LLENTNXT        LOAD ADR OF NEXT AVAIL SLOT OF LL
         LA    R11,NEWENTRY        LOAD ADR OF NEW ENTRY
NXTENTRY CLC   0(4,R10),BINONES    (2) CHECK FOR LOG END OF LL
         BE    INSERTX             (2) IF END, INSERT NEW ENTRY AT END
         CLC   4(ENTKYLEN,R10),ENTRYKEY   COMPARE KEY OF A LL ENTRY
*                                         TO KEY OF NEW ENTRY
         BH    INSERT              IF HIGH, INSERT NEW ENTRY
         BE    DUPENTRY            IF EQUAL, ENTRY ALREADY EXISTS
         MVI   IN1SW,C'0'          (3) TURN OFF NEW FIRST ENTRY SW
         ST    R10,PRVENTRY        STORE ADR OF CURRENT ENTRY
         L     R10,0(0,R10)        LOAD ADR OF NEXT ENTRY IN SEQ
         B     NXTENTRY            COMPARE NEXT ENTRY KEY TO NEW KEY
*
INSERT   CLI   IN1SW,C'1'          (3) CHECK IF NEW FIRST ENTRY SW SET
         BE    INSERT1             (3) IF YES, INSERT NEW FIRST ENTRY
         L     R10,PRVENTRY        LOAD ADR OF ENTRY BEFORE NEW ENTRY
         MVC   0(4,R12),0(R10)     MOVE NEXT ENTRY PTR TO NEW ENTRY
*                                  LINK FIELD
         MVC   4(ENTRYLEN,R12),ENTRY      MOVE NEW ENTRY INTO NEXT
*                                         SLOT+4 (PASSED LINK FIELD)
         ST    R12,0(0,R10)        MOVE ADR OF NEW ENTRY INTO LINK
*                                  FIELD OF PREV ENTRY
         LA    R12,LLENTLEN(0,R12) CALC ADR OF NEW NEXT AVAIL SLOT
         ST    R12,LLENTNXT        SAVE ADR OF NEXT AVAIL SLOT
         BR    R6                  RETURN TO CALLING RTN
*
INSERT1  MVC   4(4,R12),0(R11)     (3) MOVE NEW ENTRY INTO NEXT SLOT+4
*                                  (PASSED LINK FIELD)
         ST    R10,0(0,R12)        (3) MOVE ADR OF OLD FIRST ENTRY INTO
*                                  LINK FIELD OF NEW FIRST ENTRY
         ST    R12,LLENTONE        (3) SAVE ADR OF NEW FIRST ENTRY
         LA    R12,LLENTLEN(0,R12)      (3) CALC ADR OF NEXT AVAIL SLOT
         ST    R12,LLENTNXT        (3) SAVE ADR OF NEXT AVAIL SLOT
         BR    R6                  (3) RETURN TO CALLING RTN
*
INSERTX  MVC   4(ENTRYLEN,R12),ENTRY    (2) MOVE NEW ENTRY INTO NEXT
*                                       SLOT+4 (PASSED LINK FIELD)
         ST    R12,0(0,R10)        (2) MOVE ADR OF NEW ENTRY INTO LINK
*                                  FIELD OF CURRENT LAST ENTRY
         MVC   0(4,R12),BINONES    (2) MOVE END-OF-LL INDICATOR INTO
*                                  LINK FIELD OF NEW LAST ENTRY
         LA    R12,LLENTLEN(0,R12)      (2) CALC ADR OF NEXT AVAIL
*                                       SLOT OF LL
         ST    R12,LLENTNXT        (2) SAVE ADR OF NEXT AVAIL SLOT
         BR    R6                  (2) RETURN TO CALLING RTN
*
IN1SW    DS    CL1
BINONES  DC    4XL1'FF'
LLENTONE DS    F
LLENTNXT DS    F
PRVENTRY DS    F
NEWENTRY DS    CL6
*
ENTRYMAP DSECT
ENTRY    EQU   *
ENTKEY   DS    CL2
ENTKYLEN EQU   *-ENTKEY
ENTFLD01 DS    CL4
ENTRYLEN EQU   *-ENTRYMAP
LLENTLEN EQU   ENTRYLEN+4
