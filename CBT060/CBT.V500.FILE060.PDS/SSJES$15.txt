       TITLE 'JES2 USER EXIT 15 - OUTPUT DATA SET / COPY SELECT'
*************************COMMENT BLOCK********************************
*                                                                    *
*              OUTPUT DATA SET / COPY SELECT   SP 2.1.5              *
*                                                                    *
*   PURPOSE:                                                         *
*                                                                    *
*      1. TO CHANGE TRANSLATE TABLE FOR REMOTE PRINTERS              *
*                                                                    *
*      2. TO SEND DJDE TO XEROX PRINTER                              *
*                                                                    *
*   ENTRY POINT = SSJES$15                                           *
*                                                                    *
*   (REGISTERS)  ENTRY AND RETURN:                                   *
*                                                                    *
*      R0    =0 DATA SET SELECT                                      *
*            =4 COPY OF A DATA SET                                   *
*      R1    ADDRESS OF PARAMETER LIST                               *
*            WORD 1   (+0) NUMBER OF COPIES OF DATA SET TO PRINT     *
*            WORD 2   (+4) ADDR OF THE WORK JOE                      *
*            WORD 3   (+8) ADDR OF JCT                               *
*            WORD 4   (+12) ADDR OF PDDB                             *
*            WORD 5   (+16) ADDR OF DCT                              *
*            WORD 6   (+20) NUMBER OF COPIES CURRENTLY PRINTED       *
*            WORD 7   (+24) PRINT TRANSLATE TABLE ADDRESS            *
*            WORD 8   (+28) CCW TRANSLATE TABLE ADDRESS              *
*            WORD 9   (+32) COPY GROUP ADDRESS                       *
*            WORD 10  (+36) CURRENT COPY GROUP COUNT                 *
*      R2-R10 SET AFTER ENTRY AND SHOW PROGRAM USE OF REGISTERS      *
*      R2    N/A                                 UNCHANGED           *
*      R3    ADDRESS FROM $GETBUF                                    *
*      R4    DCT ADDRESS                                             *
*      R5    WORK REGISTER                                           *
*      R6    WORK REGISTER                                           *
*      R7    WORK REGISTER                                           *
*      R8    N/A                                                     *
*      R9    BUFSTART ADDRESS                                        *
*      R10   N/A                                                     *
*      R11   HCT ADDRESS                         UNCHANGED           *
*      R12   BASE REGISTER FOR PROGRAM           UNCHANGED           *
*      R13   PCE ADDRESS                         UNCHANGED           *
*      R14   RETURN ADDRESS                      UNCHANGED           *
*      R15   ENTRY ADDRESS                       RETURN CODE         *
*                                                                    *
*      VALID RETURN CODES:                                           *
*                                                                    *
*            0  CONTINUE NORMAL PROCESSING                           *
*            4  SAME AS 0                                            *
*            8  DO NOT SELECT THIS PDDB (R0 = 0 ONLY)                *
*                                                                    *
*   JES2 MACROS USED:                                                *
*                                                                    *
*      $HASPGBL, $MODULE, $BUFFER, $JCT, $JQE, $PCE, $DCT,           *
*      $CAT, $HCT, $MIT, $ENTRY, $SAVE, $RETURN, $MODEND             *
*      $TRP, $TRACE
*                                                                    *
**********************************************************************
         COPY   $HASPGBL          COPY HASPGBL PARAMETERS
         PRINT  ON,GEN
SSJES$15 $MODULE $BUFFER,                                              X
               RPL,                     REQED BY $BUFFER               X
               $JCT,                    REQED BY CAT                   X
               $HASPEQU,                REQED TO DEFINE REGISTERS      X
               $JQE,                    REQED BY HCT                   X
               $PCE,                    REQED BY HCT                   X
               $CAT,                    REQED BY HCT                   X
               $HCT,                    REQED BY $SAVE, $RETURN, ETC.  X
               $MIT,                    REQED BY $MODEND               X
               $TRP,                    REQED BY $TRACE                X
               $DCT,                    REQED BY PROGRAM               X
               SYSP=(GEN,GEN,DATA,NOGEN,NOGEN)  DOCUMENTATION
       TITLE 'JES2 USER EXIT 15 - OUTPUT DATA SET / COPY SELECT'
         PRINT ON,GEN,DATA
USRXIT15 $ENTRY BASE=R12
         $SAVE
         LR    R12,R15            LOAD BASE REGISTER
         SLR   R15,R15                 PRESET RETURN CODE
         C     R0,=F'4'                IS THIS COPY OF A DATASET
         BNE   TRANSET                 NO, FORGET IT
         L     R4,16(R1)               GET DCT ADDRESS
         USING DCT,R4
         SPACE 1
*        SEE IF PRINTER IS PRINTING DATA SET SEPERATORS
*
         TM    DCTPPSW2,DCTSDSSW        SEPDS SET TO
         BNO   RETURN                   NO THEN EXIT
         SPACE 1
***************************************************************
* GET WORK AREA
***************************************************************
GETWORK $GETBUF TYPE=HASP,WAIT=YES,FIX=YES    GET WORK AREA
         LR    R3,R1                  SAVE BUFFER ADDR
         USING IOBDSECT,R3
         LA    R9,BUFSTART
         $SEPPDIR (R3)                SEND A PDIR IN CASE SNA
         SPACE 1
X97INIT  DS     0H       XEROX 9700 PROCESSING
         SPACE 1
*        BUILD THE DJDE BY SEARCH THRU THE FORM TABLE
         LA    R5,FORMS            POINT TO BEGINNING OF TABLE
         B     FRMCHK
FRMLOOP  CLI   X97XRTBL(R5),X'00'  AT END OF TABLE?
         BE    PRPUT               GO PLANT THE DEFAULT DJDE
         LA    R5,X97XRTBL(R5)     POINT TO NEXT TABLE ENTRY
FRMCHK   CLC   DCTFORMS,0(R5)      IS THIS THE FORM ?
         BE    PRPUT               YES, GO USE IT THEN
         BNE   FRMLOOP             NO, GO CHECK SOME MORE
         SPACE 1
         L     R5,8(R5)            POINT TO THE DJDES
PRPUT    SLR   R6,R6 CLEAR R6
         IC    R6,12(R5)           PUT PAPER TYPE IN R6
         CLC   DCTUSER0(1),12(R5)  PAPER SAME AS LAST TIME
         BE    SAMEPAPR            YES -  SKIP PAPER CHANGE
         MVC   DCTUSER0(1),12(R5)  SAVE CURRENT PAPER IN USER FIELD
         MH    R6,=H'80'           MULTIPLY BY ENTRY LENGTH
         LA    R7,PAPER            ADD BASE
         AR    R7,R6                    ADDRESS
         MVC   1(80,R9),0(R7)      MOVE PAPER CHANGE DJDE TO BUFFER
         BAL   R10,PUTDJDE         OUTPUT IT
SAMEPAPR L     R5,8(R5)            POINT TO THE DJDES
DJDELOOP CLI   0(R5),X'00'         IS THIS END OF DJDE FOR ENTRY?
         BE    FREEBUF             YES
         MVC   1(80,R9),0(R5)      COPY THIS DJDE INTO OUTPUT BUFFER
         BAL   R10,PUTDJDE
         LA    R5,80(R5)           POINT TO NEXT DJDE ENTRY LINE
         B     DJDELOOP
PUTDJDE  MVI   0(R9),X'01'         WRITE SUPRESS SPACING
         LR    R1,R9               POINT TO BUFFER
         LA    R0,81          ***  SET DATA LENGTH
         $PRPUT DATA=(R1),LEN=(R0),COUNT=1,WAIT=YES,CC=M
         BR    R10
         SPACE 1
TRANSET  LA    R15,0              SET RETURN CODE IN REGISTER 15
         L     R4,16(,R1)         LOAD ADDRESS OF DCT
         USING DCT,R4
         TM    DCTDEVTP,DCTRPR    IS IT A REMOTE
         BNO   RETURN             NO GET OUT
         LA    R5,TRANSTBL        PUT TABLE ADDRESS IN R5
         ST    R5,24(,R1)         PUT NEW TRANSLATE TABLE
*                                    ADDRESS INTO PARM LIST
         B     RETURN
         SPACE 1
FREEBUF  DS     0H
         $FREEBUF (R3)
         SLR   R15,R15
         SPACE 1
RETURN   EQU   *
         $RETURN RC=(R15)         TELL HASPRDR TO CONTINUE
         LTORG
         DROP  R4
         EJECT
         DC    0F'0'
*
TRANSTBL DC    256X'40'
         PRINT DATA
         ORG   TRANSTBL+C'¢'
         DC    C'¢.<(+|&&'
         ORG   TRANSTBL+C'!'
         DC    C'!$*);¬-/'
         ORG   TRANSTBL+C'¦'
         DC    C'¦,%_>?'
         ORG   TRANSTBL+C'`'
         DC    C'`:#@''=" '
         DC    C'abcdefghi'
         ORG   TRANSTBL+C'j'
         DC    C'jklmnopqr'
         ORG   TRANSTBL+C'~'
         DC    C'~stuvwxyz'
         ORG   TRANSTBL+C'{'
         DC    C'{ABCDEFGHI'
         ORG   TRANSTBL+C'}'
         DC    C'}JKLMNOPQR'
         ORG   TRANSTBL+C'\'
         DC    C'\ STUVWXYZ'
         ORG   TRANSTBL+C'0'
         DC    C'0123456789'
         ORG
*
PAPER    EQU   *
  DC CL80'$$$$DJDE OTEXT=(''INVALID PAPER REQUESTED'',WAIT),; '
  DC CL80'$$$$DJDE OTEXT=(''LOAD WHITE NOHOLE PAPER'',WAIT),; '
  DC CL80'$$$$DJDE OTEXT=(''LOAD WHITE PAPER 3 HOLES OUT'',WAIT),; '
  DC CL80'$$$$DJDE OTEXT=(''LOAD WHITE PAPER 3 HOLES IN'',WAIT),; '
  DC CL80'$$$$DJDE OTEXT=(''LOAD PERF 358 WITH PERF TO LEFT'',WAIT),; '
  DC CL80'$$$$DJDE OTEXT=(''LOAD BLS3023CA PAPER FACE DOWN'',WAIT),; '
  DC CL80'$$$$DJDE OTEXT=(''LOAD BLS3023V PAPER FACE DOWN'',WAIT),; '
         SPACE 3
*                    CLASS FORM/DJDE XREF TABLE
* LAST ENTRY IS THE DEFAULT, SO ADD NEW ENTRIES BEFORE THE LAST ONE
*
*              CL8'********' ----> FORM NAME FORM JCL
*                            A(NNNNN) ----> ADDRESS OF DJDE IMAGES
*                                     ,AL1(N) ----> PAPER OFFSET
*                                          1  ---  NOHOLE
*                                          2  ---  3 HOLE DUPLEX
*                                          3  ---  3 HOLE SIMPLEX
*                                          4  ---  PERF 358
*                                          5  ---  BLS 3023CA
*                                          6  ---  BLS 3023V
*                                            ,CL3'   ' ----> FILLER
*
         DC    0F'0'              FORCE ALIGNMENT OF FORM NAMES
FORMS    EQU   *
         DC    CL8'PQGW    ',A(FRMPQG),AL1(1),CL3'   '
X97XRTBL EQU   *-FORMS      LENGTH OF ONE ENTRY
         DC    CL8'LSGW    ',A(FRMLSG),AL1(1),CL3'   '
         DC    CL8'LDGW    ',A(FRMLDG),AL1(1),CL3'   '
         DC    CL8'PSNW    ',A(FRMPSN),AL1(1),CL3'   '
         DC    CL8'PDNW    ',A(FRMPDN),AL1(1),CL3'   '
         DC    CL8'PQNW    ',A(FRMPQN),AL1(1),CL3'   '
         DC    CL8'LSNW    ',A(FRMLSN),AL1(1),CL3'   '
         DC    CL8'LDNW    ',A(FRMLDN),AL1(1),CL3'   '
         DC    CL8'1061    ',A(FRMNDG),AL1(1),CL3'   '
         DC    CL8'NDGW    ',A(FRMNDG),AL1(1),CL3'   '
         DC    CL8'NSGW    ',A(FRMNSG),AL1(1),CL3'   '
         DC    CL8'NONE    ',A(FRMNONE),AL1(1),CL3'   '
         DC    CL8'NONES3  ',A(FRMNONE),AL1(3),CL3'   '
         DC    CL8'NONED3  ',A(FRMNONE),AL1(2),CL3'   '
         DC    CL8'S358    ',A(FRMNONE),AL1(4),CL3'   '
         DC    CL8'SIRP    ',A(FRMSIRP),AL1(1),CL3'   '
         DC    CL8'SIRT    ',A(FRMSIRT),AL1(1),CL3'   '
         DC    CL8'SL10    ',A(FRMLET1),AL1(1),CL3'   '
         DC    CL8'SLETTER1',A(FRMLET1),AL1(1),CL3'   '
         DC    CL8'SLETTER2',A(FRMLET2),AL1(1),CL3'   '
         DC    CL8'SLETTER3',A(FRMLET3),AL1(1),CL3'   '
         DC    CL8'SLETTER4',A(FRMLET4),AL1(1),CL3'   '
         DC    CL8'SLETTER5',A(FRMLET5),AL1(1),CL3'   '
         DC    CL8'SLETTERA',A(FRMLETA),AL1(1),CL3'   '
         DC    CL8'SD2W    ',A(FRMSD23),AL1(1),CL3'   '
         DC    CL8'SS2W    ',A(FRMSS23),AL1(1),CL3'   '
         DC    CL8'PQG3    ',A(FRMPQG3),AL1(2),CL3'   '
         DC    CL8'LSG3    ',A(FRMLSG3),AL1(3),CL3'   '
         DC    CL8'LDG3    ',A(FRMLDG3),AL1(2),CL3'   '
         DC    CL8'SSN3    ',A(FRMPSN),AL1(3),CL3'   '
         DC    CL8'PSN3    ',A(FRMPSN3),AL1(3),CL3'   '
         DC    CL8'SDN3    ',A(FRMSDN3),AL1(2),CL3'   '
         DC    CL8'SD23    ',A(FRMSD23),AL1(2),CL3'   '
         DC    CL8'SS23    ',A(FRMSS23),AL1(3),CL3'   '
         DC    CL8'PDN3    ',A(FRMPDN3),AL1(2),CL3'   '
         DC    CL8'PQN3    ',A(FRMPQN3),AL1(2),CL3'   '
         DC    CL8'LSN3    ',A(FRMLSN3),AL1(3),CL3'   '
         DC    CL8'LDN3    ',A(FRMLDN3),AL1(2),CL3'   '
         DC    CL8'NDG3    ',A(FRMNDG3),AL1(2),CL3'   '
         DC    CL8'NSG3    ',A(FRMNSG3),AL1(3),CL3'   '
         DC    CL8'BLS3023C',A(FRM3023),AL1(5),CL3'   '
         DC    CL8'BLS3023V',A(FRM3023),AL1(6),CL3'   '
         DC    CL8'BLS3023T',A(FRM3023),AL1(1),CL3'   '
         DC    CL8'1461    ',A(FRMLDG3),AL1(2),CL3'   '  DEFAULT FORM
         DC    X'00'  END OF TABLE
*
FRMNONE  EQU   *
         DC    X'00'  END OF TABLE
FRMPQG   EQU   *
  DC CL80'$$$$DJDE FORMS=QD1461,FORMAT=PDEPQ,BOF=132,END; '
         DC    X'00'  END OF TABLE
FRMLSG   EQU   *
  DC CL80'$$$$DJDE DUPLEX=NO,END; '
         DC    X'00'  END OF TABLE
FRMLDG   EQU   *
  DC CL80'$$$$DJDE SIDE=NUFRONT,END; '
         DC    X'00'  END OF TABLE
FRMPSN   EQU   *
  DC CL80'$$$$DJDE FORMS=NONE,FORMAT=PDEPS,DUPLEX=NO,END; '
         DC    X'00'  END OF TABLE
FRMPDN   EQU   *
  DC CL80'$$$$DJDE FORMS=NONE,FORMAT=PDEPS,SIDE=NUFRONT,END; '
         DC    X'00'  END OF TABLE
FRMSD23  EQU   *
  DC CL80'$$$$DJDE FORMS=NONE,FORMAT=PDEPS,SIDE=NUFRONT, '
  DC CL80'$$$$DJDE SHIFT=YES,FONTS=(P07TDC),BEGIN=(.5,.5),END; '
         DC    X'00'  END OF TABLE
FRMSS23  EQU   *
  DC CL80'$$$$DJDE FORMS=NONE,FORMAT=PDEPS,DUPLEX=NO, '
  DC CL80'$$$$DJDE FONTS=(P07TDC),BEGIN=(.5,.5),END; '
         DC    X'00'  END OF TABLE
FRMPQN   EQU   *
  DC CL80'$$$$DJDE FORMS=NONE,FORMAT=PDEPQ,BOF=132,END; '
         DC    X'00'  END OF TABLE
FRMLSN   EQU   *
  DC CL80'$$$$DJDE FORMS=NONE,DUPLEX=NO,END; '
         DC    X'00'  END OF TABLE
FRMLDN   EQU   *
  DC CL80'$$$$DJDE FORMS=NONE,SIDE=NUFRONT,END; '
         DC    X'00'  END OF TABLE
FRMNSG   EQU   *
  DC CL80'$$$$DJDE FORMS=GB1061,FORMAT=PDENS,DUPLEX=NO,END; '
         DC    X'00'  END OF TABLE
FRMNDG   EQU   *
  DC CL80'$$$$DJDE FORMS=GB1061,FORMAT=PDENS,SIDE=NUFRONT,END; '
         DC    X'00'  END OF TABLE
FRMLET1  EQU   *
  DC CL80'$$$$DJDE FORMS=LETTER,FORMAT=PDEPS,BEGIN=(1.5,.3),;  '
  DC CL80'$$$$DJDE DUPLEX=NO,FONTS=(P08TAC),END;  '
         DC    X'00'  END OF TABLE
FRMLET2  EQU   *
  DC CL80'$$$$DJDE FORMS=LETTER,FORMAT=PDEPS,BEGIN=(1.5,.3),;  '
  DC CL80'$$$$DJDE FONTINDEX=(0,ONE),DATA=(1,132),;  '
  DC CL80'$$$$DJDE DUPLEX=NO,FONTS=(P08TAC,P08TBC),END;  '
         DC    X'00'  END OF TABLE
FRMLET3  EQU   *
  DC CL80'$$$$DJDE FORMS=LETTES,FORMAT=PDEPS,BEGIN=(1.5,.3),;  '
  DC CL80'$$$$DJDE FONTINDEX=(0,ONE),DATA=(1,132),;  '
  DC CL80'$$$$DJDE DUPLEX=NO,FONTS=(P08TAC,P08TBC),END;  '
         DC    X'00'  END OF TABLE
FRMLET4  EQU   *
  DC CL80'$$$$DJDE FORMS=LETTEN,FORMAT=PDEPS,BEGIN=(1.5,.3),;  '
  DC CL80'$$$$DJDE FONTINDEX=(0,ONE),DATA=(1,132),;  '
  DC CL80'$$$$DJDE DUPLEX=NO,FONTS=(P08TAC,P08TBC),END;  '
         DC    X'00'  END OF TABLE
FRMLET5  EQU   *
  DC CL80'$$$$DJDE FORMS=LETTES,FORMAT=PDEPS,BEGIN=(1.5,.3),;  '
  DC CL80'$$$$DJDE FONTINDEX=(0,ONE),DATA=(1,132),;  '
  DC CL80'$$$$DJDE DUPLEX=NO,FONTS=(P07TDC,P07ITA,P07TCC),END;  '
         DC    X'00'  END OF TABLE
FRMLETA  EQU   *
  DC CL80'$$$$DJDE FORMS=LETTEA,FORMAT=PDEPS,BEGIN=(1.5,.3),;  '
  DC CL80'$$$$DJDE FONTINDEX=(0,ONE),DATA=(1,132),;  '
  DC CL80'$$$$DJDE DUPLEX=NO,FONTS=(P08TAC,P08TBC),END;  '
         DC    X'00'  END OF TABLE
FRMSIRP  EQU   *
  DC CL80'$$$$DJDE FORMS=SIRP,FORMAT=PDEPS,BEGIN=(.4,.5),;  '
  DC CL80'$$$$DJDE DUPLEX=NO,FONTS=(P08TAB),END;  '
         DC    X'00'  END OF TABLE
FRMSIRT  EQU   *
  DC CL80'$$$$DJDE FORMS=SIRT,FORMAT=PDEPS,BEGIN=(.4,.5),;  '
  DC CL80'$$$$DJDE DUPLEX=NO,FONTS=(P08TAB,P08TBB),MODIFY=CMCERT,END;'
         DC    X'00'  END OF TABLE
FRMPQG3  EQU   *
  DC CL80'$$$$DJDE FORMS=QD1461,FORMAT=PDEPQ,BOF=132,SHIFT=YES,END; '
         DC    X'00'  END OF TABLE
FRMLSG3  EQU   *
  DC CL80'$$$$DJDE DUPLEX=NO,SHIFT=YES,END; '
         DC    X'00'  END OF TABLE
FRMLDG3  EQU   *
  DC CL80'$$$$DJDE SIDE=NUFRONT,SHIFT=YES,END; '
         DC    X'00'  END OF TABLE
FRMPSN3  EQU   *
  DC CL80'$$$$DJDE FORMS=NONE,FORMAT=PDEPS,DUPLEX=NO,SHIFT=YES,END; '
         DC    X'00'  END OF TABLE
FRMPDN3  EQU   *
  DC CL80'$$$$DJDE FORMS=NONE,FORMAT=PDEPS,SIDE=NUFRONT,SHIFT=YES,END;'
         DC    X'00'  END OF TABLE
FRMSDN3  EQU   *
  DC CL80'$$$$DJDE FORMS=NONE,FORMAT=PDEPS,SIDE=NUFRONT,SHIFT=YES,;'
  DC CL80'$$$$DJDE BEGIN=(.5,.05),END;'
         DC    X'00'  END OF TABLE
FRMPQN3  EQU   *
  DC CL80'$$$$DJDE FORMS=NONE,FORMAT=PDEPQ,BOF=132,SHIFT=YES,END; '
         DC    X'00'  END OF TABLE
FRMLSN3  EQU   *
  DC CL80'$$$$DJDE FORMS=NONE,DUPLEX=NO,SHIFT=YES,END; '
         DC    X'00'  END OF TABLE
FRMLDN3  EQU   *
  DC CL80'$$$$DJDE FORMS=NONE,SIDE=NUFRONT,SHIFT=YES,END; '
         DC    X'00'  END OF TABLE
FRMNSG3  EQU   *
  DC CL80'$$$$DJDE FORMS=GB1061,FORMAT=PDENS,DUPLEX=NO,SHIFT=YES,END; '
         DC    X'00'  END OF TABLE
FRMNDG3  EQU   *
  DC CL80'$$$$DJDE FORMS=GB1061,FORMAT=PDENS,SIDE=NUFRONT,;'
  DC CL80'$$$$DJDE SHIFT=YES,END; '
         DC    X'00'  END OF TABLE
FRM3023  EQU   *
  DC CL80'$$$$DJDE FORMS=NONE,FORMAT=PDEPS,BEGIN=(0,.26),;  '
  DC CL80'$$$$DJDE FONTINDEX=(0,ONE),DATA=(1,132),;  '
  DC CL80'$$$$DJDE DUPLEX=NO,FONTS=(P08TAC,BCD40P),END;  '
         DC    X'00'  END OF TABLE
*
         LTORG
         $MODEND
         END
