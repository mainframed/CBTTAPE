         TITLE 'EXIT5: JES2 EXIT 5 JES2 COMMAND PREPROCESSOR'
***********************************************************************
* +-----------------------------------------------------------------+ *
* |                          E X I T 5                              | *
* +-----------------------------------------------------------------+ *
***********************************************************************
*                                                                     *
*        JES2 EXIT 5 JES2 COMMAND PREPROCESSOR                        *
*                                                                     *
*        ENVIRONMENT:  JES2/SP 1.3.4  LEVEL 8406                      *
*                      MVS/SP 1.3.3                                   *
*                                                                     *
*        THIS JES2 EXIT PROGRAM IS DESIGNED WORK IN CONJUCTION WITH   *
*        OUR SPECIAL JES2 EXIT 15 FOR XEROX FORMS CONTROL.  WHENEVER  *
*        THE OPERATOR ISSUES A STOP OR HALT COMMAND TO A XEROX        *
*        8700/9700 PRINTER THIS EXIT RESETS THE DCTUSER0 FIELD TO     *
*        INDICATE UNKNOWN FORMS SO THAT THE NEXT JOB PRINTED CAUSES   *
*        AN OPERATOR SETUP MESSAGE TO APPEAR ON THE XEROX PRINTER     *
*        CONSOLE (THUS FORCING THE OPERATOR TO VERIFY THE FORMS       *
*        SETUP).                                                      *
*                                                                     *
*        THIS EXIT IS COPIED INTO EXIT1MIT AT ASSEMBLY TIME.          *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        SYMBOLIC REGISTER DEFINITIONS                                *
*                                                                     *
***********************************************************************
*
**IC      EQU   R2                  INPUT CODE (FROM R0)
**IP      EQU   R3                  INPUT PARAMETERS LIST ADDRESS
**JOE     EQU   R4                  JOE ADDRESSABILITY REGISTER
**PDDB    EQU   R5                  PDDB ADDRESSABILITY REGISTER
**DCTREG  EQU   R6                  DCT ADDRESSABILITY REGISTER
**BUFREG  EQU   R7                  BUFFER ADDRESSABILITY REGISTER
*JCT      EQU   R10                 JCT ADDRESSABILITY REGISTER
*BASE1    EQU   R11                 HCT ADDRESSABILITY REGISTER
*BASE2    EQU   R12                 PROCESSOR ADDRESSABILITY REGISTER
*SAVE     EQU   R13                 PCE ADDRESSABILITY REGISTER
*LINK     EQU   R14                 LINK REGISTER
         SPACE 2
***********************************************************************
         COPY   $HASPGBL          COPY HASPGBL PARAMETERS
         PRINT  ON,GEN
SSJES$05 $MODULE $BUFFER,                                              X
               RPL,                     REQED BY $BUFFER               X
               $JCT,                    REQED BY CAT                   X
               $HASPEQU,                REQED TO DEFINE REGISTERS      X
               $JQE,                    REQED BY HCT                   X
               $PCE,                    REQED BY HCT                   X
               $CAT,                    REQED BY HCT                   X
               $HCT,                    REQED BY $SAVE, $RETURN, ETC.  X
               $MIT,                    REQED BY $MODEND               X
               $COMWORK,                REQED BY PROGRAM               X
               $JOE,                    REQED BY $COMWORK              X
               $TRP,                    REQED BY $TRACE                X
               $DCT,                    REQED BY PROGRAM               X
               SYSP=(GEN,GEN,DATA,NOGEN,NOGEN)  DOCUMENTATION
         PRINT ON,GEN,DATA
***********************************************************************
*                                                                     *
*        EXIT 5 ENTRY POINT                                           *
*                                                                     *
***********************************************************************
*
EXIT5    $ENTRY BASE=R12,CSECT=YES
         SPACE 1
         $SAVE                     SAVE CALLER'S REGISTERS
         LR    R12,R15             LOAD BASE REGISTER
*
***********************************************************************
*                                                                     *
*        TEST TO SEE IF OPERATOR ISSUED STOP OR HALT COMMAND          *
*                                                                     *
***********************************************************************
*
         CLI   COMVERB,C'P'        STOP COMMAND?
         BE    EXT5OPRN            YES - GO CHECK OPERANDS
         CLI   COMVERB,C'Z'        HALT COMMAND?
         BE    EXT5OPRN            YES - GO CHECK OPERANDS
         B     EXT5RET0            ELSE RETURN TO JES2
*
***********************************************************************
*                                                                     *
*        TEST OPERANDS TO SEE ANY APPLY TO A XEROX PRINTER            *
*                                                                     *
***********************************************************************
*
EXT5OPRN C     R5,=F'0'            VERIFY THERE IS AN OPERAND POINTER
         BE    EXT5RET0            NO OPERAND POINTER - RETURN TO JES2
         MVC   $USER1(8),=CL8'PRTX'
         L     R8,0(R5)            LOAD OPERAND POINTER
         C     R8,=F'0'            VERIFY THERE IS AN OPERAND
         BE    EXT5RET0            NO OPERAND - RETURN TO JES2
         CLC   0(3,R8),=CL3'PRT'   IF NOT PRT (SHORT FORM)
         BNE   EXT5LONG            THEN TRY LONG FORM OF NAME
         LA    R8,3(,R8)           ELSE MOVE R8 TO PRINTER NUMBER
         B     EXT5CHKN              AND GO CHECK PRINTER NUMBER
EXT5LONG CLC   0(7,R8),=CL7'PRINTER' IF NOT PRINTER (LONG FORM)
         BNE   EXT5OPRC            THEN TRY NEXT OPERAND
*
         LA    R8,7(,R8)           ELSE MOVE R8 TO PRINTER NUMBER
EXT5CHKN MVC   $USER1+3(1),0(R8)
         USING DCT,R9
         LA    R9,$PRTDCT-(DCTCHAIN-DCT)   ELSE GET START OF POOL2
*
DDYNLOOP ICM   R9,B'1111',DCTCHAIN         GET NEXT DCT
         BZ    EXT5RET0
         CLC   DCTDEVN,$USER1              IS THIS THE DCT...
         BNE   DDYNLOOP
*
         TM    DCTPPSW2,DCTSDSSW           IF SEPDS=Y
         BNO   EXT5OPRC                    NOT A XEROX PRINTER
         MVI   DCTUSER0,X'00'
*
EXT5OPRC CR    R5,R7               WAS THAT THE LAST OPERAND?
         BE    EXT5RET0            YES - RETURN TO JES2
         AR    R5,R6               INCREMENT R5 BY R6 (VALUE OF 4)
         B     EXT5OPRN               AND GO CHECK NEXT OPERAND
*
***********************************************************************
*                                                                     *
*        RETURN TO JES2                                               *
*                                                                     *
***********************************************************************
*
EXT5RET0 LA    R15,0              SET RETURN CODE TO 0 - NEXT EXIT 5
         $RETURN RC=(R15)         RETURN TO JES2
*
         EJECT
***********************************************************************
*                                                                     *
*        EXIT 5 DATA AREAS                                            *
*                                                                     *
***********************************************************************
*
         LTORG
         $MODEND
         END
*
************************ END OF MODULE EXIT5 **************************
