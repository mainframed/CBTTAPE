//SS58A JOB (150),CHAPMAN,CLASS=P,NOTIFY=SS58,COND=(0,NE)
//ASM       EXEC ASMFCL,MAC='SS.MACLIB',MAC1='SYS1.MACLIB',
//          PARM.ASM='NODECK,OBJ,XREF(FULL)',
//          PARM.LKED=(XREF,LET,LIST)
//ASM.SYSPRINT DD SYSOUT=X
//ASM.SYSIN    DD *
         TITLE 'SSR011PR'
SSR011PR REG SSR011PR-&SYSDATE-&SYSTIME,BASE1,BASE2
         TIME  DEC
         ST    R1,CURRDTE
         UNPK  YEAR1J(5),CURRDTE(4)
         MVI   JULF1,C' '
         MVI   OPTION,C'2'
         CALL  DATEMVS,(DATECONV)
         MVC   RPTMONTH(9),MNTHNAME
         MVC   RPTDAY(2),DAYOUTG
         MVC   RPTYEAR(2),YEAROUTG
         OPEN  (INPTFILE,,PRTFILE,OUTPUT)
         LA    R4,PRODNAM2+32
GETREC   GET   INPTFILE,SORTREC       READ INPUT RECORD
         CLC   SORTDATE(6),=X'000000000000' TYPE ZERO RECORD?
         BE    TYPEZERO                       YES
         CLC   SORTDATE(6),=X'000000000001' TYPE ONE RECORD?
         BE    TYPEONE                        YES
         B     TYPDTAIL
TYPEZERO EQU   *                GOT A TYPE ZERO RECORD
         CLC   SORTCCN,HOLDCCN           IS IT A NEW CONTROL CARD?
         BE    SAMECCN                     NO
         MVC   HOLDCCN(2),SORTCCN        NEW MAJOR ARGUMENT - HOLD CCN
         MVC   HOLDFORM(2),SORTFORM        AND FORM
SAMECCN  CLI   HOLDFORM,C'V'             FORM = 'V...'?
         BE    CKMJVEND                    YES
         CLI   SORTCAT,X'40'             CATEGORY BLANK?
         BE    SAMECAT1                    YES
         CLC   HEADER1(32),SORTCAT       CATEGORY BREAK?
         BE    SAMECAT1                    NO
         MVC   DASHLINE+7(33),DASHLINE+40  YES..
         BAL   R10,CLEANUP
         MVC   HEADER1(32),SORTCAT           PRINT NEW CAT
         BAL   R10,PUTMAJHD
         MVI   HEADER2,X'40'                 CLEAR HEADER2
         MVC   HEADER2+1(31),HEADER2
SAMECAT1 CLC   HEADER2(32),SORTVEND      VENDER BREAK?
         BE    SAMEVND2                    NO
         MVC   HEADER2(32),SORTVEND        YES - HOLD NEW VENDOR
         CLI   HOLDFORM+1,C' '           SECOND ARG = VENDOR?
         BE    VEND3ARG                    NO
         MVI   HED2FLAG,C'1'               YES - FLAG IT FOR PRINTLINE
         MVC   DASHLINE+7(33),DASHLINE+40  YES..
         B     GETREC
SAMEVND2 EQU   *
         CLI   HOLDFORM+1,C'V'           SECOND ARGUMENT = VENDOR?
         BE    GETREC                      YES
VEND3ARG MVI   HED2FLAG,C'2'               NO - FLAG IT FOR PRINTLINE
         B     GETREC
CKMJVEND EQU   *                         MAJOR ARGUMENT = VENDER
         CLI   SORTVEND,X'40'            CATEGORY BLANK?
         BE    SAMEVND1
         CLC   HEADER1(32),SORTVEND      VENDER BREAK?
         BE    SAMEVND1                    NO
         MVC   DASHLINE+7(33),DASHLINE+40  YES..
         BAL   R10,CLEANUP
         MVC   HEADER1(32),SORTVEND        YES - PRINT NEW VEND
         BAL   R10,PUTMAJHD
         MVI   HEADER2,X'40'             CLEAR HEADER2
         MVC   HEADER2+1(31),HEADER2
SAMEVND1 CLC   HEADER2(32),SORTCAT       CATEGORY BREAK?
         BE    SAMECAT2                    NO
         MVC   HEADER2(32),SORTCAT         YES - HOLD NEW CATEGORY
         CLI   HOLDFORM+1,C' '           MINOR ARG = CATEGORY?
         BE    CAT3ARG
         MVI   HED2FLAG,C'1'               YES - FLAG IT FOR PRINTLINE
         MVC   DASHLINE+7(33),DASHLINE+40  YES..
         B     GETREC
SAMECAT2 EQU   *
         CLI   HOLDFORM+1,C'C'           MINOR ARGUMENT = CATEGORY?
         BE    GETREC                      YES
CAT3ARG  MVI   HED2FLAG,C'2'               NO - FLAG IT FOR PRINTLINE
         B     GETREC
PUTMAJHD EQU   *
         CLC   LINCOUNT(2),=H'46'        ENOUGH ROOM REMAINING?
         BL    PUTMAJH2                    YES...
         BAL   R9,TOPOFORM               DO TOP OF FORM
PUTMAJH2 LA    R5,PRTLINE                R5 ==> PRTLINE
         MVC   PRTLINE+1(7),VENDRL       MAJOR HEADING = 'VENDOR:'
         LA    R8,THEDUMP                ROUTE VEND HEADING => NOWHERE
         LA    R7,HEDLINE+7                "   CTGRY HEADING => 2ND
         LA    R6,HEDLINE+41               "   PROD HEADING => 3RD
         CLI   HOLDFORM,C'V'             IS MAJOR ARGUMENT = VENDOR?
         BE    PRNTMJHD
         MVC   PRTLINE+1(9),CTGRYL       NO-MAJOR HEADING = 'CATEGORY:'
         LA    R8,HEDLINE+7              ROUTE VEND HEADING => 2ND
         LA    R7,THEDUMP                  "   CTGRY HEADING => NOWHERE
PRNTMJHD MVC   PRTLINE+12(32),HEADER1    LOAD HEADER1 TO PRINTLINE
         MVI   PRTLINE,C'0'              SKIP 2 LINES
         BAL   R9,PUTLINE2               PRINT MAJOR HEADING LINE
         MVI   PRTLINE,C' '              SKIP 1 LINE
         MVC   PRTLINE+1(7),DASHLINE+40  DASHES FOR VENDOR HEADING
         CLI   HOLDFORM,C'V'
         BE    PRNTMJH2
         MVC   PRTLINE+1(9),DASHLINE+40  DASHES FOR CATEGORY HEADING
PRNTMJH2 BAL   R9,PUTLINE2               PRINT MAJ HEADING DASHES
         CLI   HOLDFORM+1,C' '           IS 2ND ARGUMENT = PROD NAME?
         BNE   PRNTMNHD
         LA    R6,HEDLINE+7          YES: ROUTE PROD HEADING => 2ND
         LA    R8,HEDLINE+41             ROUTE VENDOR HEADING => 3RD
         CLI   HOLDFORM,C'C'             IS MAJOR ARGUMENT = VENDOR?
         BE    PRNTMNHD
         LA    R7,HEDLINE+41         YES: ROUTE CATEGORY HEADING => 3RD
         LA    R8,THEDUMP               ROUTE VENDOR HEADING => NOWHERE
PRNTMNHD MVI   HEDLINE,X'40'
         MVC   HEDLINE+1(72),HEDLINE     BLANK OUT HEADING LINE 1-72
         MVC   0(20,R6),PRDNAML          MOVE VENDOR, CATEGORY &
         MVC   0(8,R7),CTGRYL              PROD NAME HEADINGS TO WHERE
         MVC   0(6,R8),VENDRL                THEY'VE BEEN ROUTED
         LA    R5,HEDLINE                PRINT HEADING LINE
         BAL   R9,PUTLINE2
         CLI   SORTDATE,X'00'
         BE    0(R10)
         LA    R5,DASHLINE               PRINT UNDERSCORES
         BAL   R9,PUTLINE2
         MVI   DASHLINE+7,C' '           KILL DASHES
         MVC   DASHLINE+8(32),DASHLINE+7
         BR    R10
TOPOFORM MVC   LINCOUNT(2),=H'00'        ZERO OUT LINE COUNT
         MVI   PRTLINE+1,X'40'           BLANK OUT PRINTLINE
         MVC   PRTLINE+2(131),PRTLINE+1
         PUT   PRTFILE,TITLINE
         MVI   PAGEFLAG,C'0'             RESET PAGE FLAG
         BR    R9
PUTLINE  EQU   *
         CLI   PAGEFLAG,C'1'             NEED NEW PAGE?
         BNE   PUTLINE2                    NAH
         PUT   PRTFILE,DASHLINE          PRINT DASHES
         ST    R5,SAVER5                 STORE R5
         STM   R9,R10,SAVER9               R9 AND R10
         MVC   HOLDLINE(133),PRTLINE     STOW PRINTLINE
         BAL   R9,TOPOFORM               DO TOP OF FORM
         BAL   R10,PUTMAJHD              PRINT MAJ HEADINGS
         L     R5,SAVER5                 RESTORE R5
         LM    R9,R10,SAVER9               R9 AND R10
         MVC   PRTLINE(133),HOLDLINE     RESTORE PRINTLINE
PUTLINE2 PUT   PRTFILE,0(R5)             PRINT A LINE
         MVI   PRTLINE,X'40'
         MVC   PRTLINE+1(132),PRTLINE
         LH    R3,LINCOUNT               INCREMENT LINE COUNT
         AH    R3,=H'1'
         STH   R3,LINCOUNT
         BR    R9
TYPEONE  EQU   *                             GOT A TYPE 1 RECORD
         CLI   HOLDFORM+1,C' '
         BNE   TYPEONE2
         MVC   DASHLINE+7(33),DASHLINE+40    DASHES FOR END OF PRODUCT
TYPEONE2 BAL   R10,CLEANUP
         MVI   PRODACRN,X'40'
         MVC   PRODACRN+1(96),PRODACRN
         MVC   HOLDORDR(12),PRODACRN
         MVC   HOLDFMID(7),PRODACRN
         MVC   HOLDRELS(12),PRODACRN
         MVC   PRODACRN(16),SORTDATA
         MVC   PRODNAM1(64),SORTDATA+16
         BAL   R9,ADDRNAME
         B     GETREC
ADDRNAME LA    R4,PRODACRN
         CLI   PRODACRN,X'40'
         BE    ADDRPRD1
         CLC   PRODACRN(16),PRODNAM1
         BNE   0(R9)
ADDRPRD1 LA    R4,PRODNAM1
         B     0(R9)
TYPDTAIL EQU   *                             GOT A DETAIL TYPE RECORD
         CLC   LINCOUNT(2),=H'52'        GOING TO OVERFLOW PAGE?
         BL    FMTDTAIL                    NO...
         CLI   0(R4),X'40'
         BNE   FMTDTAIL                    NO...
         MVI   PAGEFLAG,C'1'             FLAG FOR TOP OF FORM
         MVC   DASHLINE+7(33),DASHLINE+40
         OI    HED2FLAG,X'F0'            FLAG TO PRINT 2ND ARGUMENT
         BAL   R9,ADDRNAME               SET R4 ==> FIRST NAME TO PRINT
         MVI   HOLDORDR,X'40'            BLANK OUT HOLDORDR, HOLDFMID
         MVC   HOLDORDR+1(30),HOLDORDR     AND HOLDRELS
FMTDTAIL CLI   HED2FLAG,C'1'            CATEGORY/VENDOR NEEDS PRINTING?
         BL    NEWORDR                        NO...
         CLI   HED2FLAG,C'1'                GOES IN 1ST SLOT?
         BNE   HED2FLG2
         MVC   PRTLINE+7(32),HEADER2          YES
         B     NEWORDR
HED2FLG2 MVC   PRTLINE+41(32),HEADER2         NO
NEWORDR  NI    HED2FLAG,X'0F'               RESET FLAG
         CLC   HOLDORDR(12),SORTDATA+21     NEW ORDER#?
         BE    NEWFMID                        NO...
         MVC   HOLDORDR(12),SORTDATA+21     HOLD NEW ORDER#
         MVC   PRTLINE+74(12),SORTDATA+21   LOAD ORDER# TO PRINTLINE
         MVC   HOLDFMID(19),PRTLINE+86      BLANK OUT HOLDFMID HOLDRELS
NEWFMID  CLC   HOLDFMID(7),SORTDATA+12      NEW FMID?
         BE    NEWRELS                        NO...
         MVC   HOLDFMID(7),SORTDATA+12      HOLD NEW FMID
         MVC   PRTLINE+87(7),SORTDATA+12    LOAD FMID TO PRINTLINE
NEWRELS  CLC   HOLDRELS(12),SORTDATA+33     NEW RELEASE?
         BE    LOADREST                       NO...
         MVC   HOLDRELS(12),SORTDATA+33     HOLD NEW RELEASE
         MVC   PRTLINE+95(12),SORTDATA+33   LOAD RELEASE TO PRINTLINE
LOADREST MVC   PRTLINE+108(10),SORTDATA     LOAD PTF/MOD TO PRINTLINE
         MVC   PRTLINE+122(2),SORTDATE+2    LOAD DATE TO PRINTLINE
         MVC   PRTLINE+125(2),SORTDATE+4
         MVC   PRTLINE+128(2),SORTDATE
         MVI   PRTLINE+124,C'/'
         MVI   PRTLINE+127,C'/'
         BAL   R11,PRTDTAIL
         B     GETREC
PRTDTAIL MVI   PRTLINE+6,C'|'
         MVI   PRTLINE+40,C'|'
         MVI   PRTLINE+73,C'|'
         MVI   PRTLINE+86,C'|'
         MVI   PRTLINE+94,C'|'
         MVI   PRTLINE+107,C'|'
         MVI   PRTLINE+121,C'|'
         MVI   PRTLINE+131,C'|'
         CLI   0(R4),X'40'                   PROD NAME LINES REMAINING?
         BE    PRNTDTL                         NO
         MVC   266(32,R6),0(R4)                YES - LOAD NEXT ONE
PRNTDTL  LA    R5,PRTLINE                    PRINT THAT SUCKER
         BAL   R9,PUTLINE
         CLI   0(R4),X'40'                   PROD NAME LINES REMAINING?
         BE    0(R11)                          NO
         LA    R4,32(R4)                       YES- BUMP R4 TO NEXT ONE
         BR    R11
CLEANUP  EQU   *   THIS ROUTINE ENSURES ALL PROD NAME LINES GET PRINTED
         CLI   0(R4),X'40'               PROD NAME LINES REMAINING?
         BE    CLEANEND                    NO
         BAL   R11,PRTDTAIL                YES - PRINT 'EM
         B     CLEANUP
CLEANEND CLC   LINCOUNT(2),=H'52'        GOING TO OVERFLOW PAGE?
         BL    PUTDASH                     NO...
         MVI   PAGEFLAG,C'1'               YES - SET PAGE FLAG
         MVC   DASHLINE+7(33),DASHLINE+40      - FULL DASH LINE
         OI    HED2FLAG,X'F0'              -FLAG TO PRINT 2ND ARGUMENT
         BR    R10
PUTDASH  CLI   FIRSTX,X'FF'             1ST PRINT?
         BNE   PUTDASH2
         MVI   FIRSTX,X'00'
         BR    R10
PUTDASH2 LA    R5,DASHLINE
         BAL   R9,PUTLINE                PRINT DASHES AT END OF PRODUCT
         MVI   DASHLINE+7,C' '           RESET DASHES
         MVC   DASHLINE+8(32),DASHLINE+7
         BR    R10
CLOSEALL EQU   *
         MVC   DASHLINE+7(33),DASHLINE+40
         LA    R5,DASHLINE
         BAL   R9,PUTLINE
         CLOSE (INPTFILE,,PRTFILE,)
         L     R13,4(R13)                    BRUSH YOUR TEETH
         RETURN (14,12),RC=0                     AND GO TO BED
         EJECT
INPTFILE DCB   DDNAME=INFILE,                                          X
               DSORG=PS,                                               X
               MACRF=GM,                                               X
               LRECL=94,                                               X
               EODAD=CLOSEALL,                                         X
               RECFM=FB
PRTFILE  DCB   DDNAME=PRTFILE,                                         X
               DSORG=PS,                                               X
               MACRF=PM,                                               X
               LRECL=133,                                              X
               RECFM=FBA
CURRDTE  DS    F
SAVER5   DS    F
SAVER9   DS    2F
LINCOUNT DC    H'50'    THIS INITIAL VALUE FORCES FIRST TOP OF FORM
CTGRYL   DC    CL9'CATEGORY:'
VENDRL   DC    CL7'VENDOR:'
PRDNAML  DC    CL20'ACRONYM/PRODUCT NAME'
HED2FLAG DC    CL1'0'
PAGEFLAG DC    CL1'0'
FIRSTX   DC    X'FF'     SUPPRESSES PRINT OF DASHLINE 1ST TIME
HEADER1  DS    CL32
HEADER2  DS    CL32
HOLDFORM DS    CL2
HOLDCCN  DC    H'32767'
HOLDPROD DS    0CL97
PRODACRN DS    CL32
PRODNAM1 DS    CL32
PRODNAM2 DS    CL32
         DC    CL1' '
HOLDORDR DS    CL12
HOLDFMID DS    CL7
HOLDRELS DS    CL12
SORTREC  DS    0CL94
SORTCCN  DS    H
SORTINX1 DS    H
SORTINX2 DS    H
SORTPRDN DS    H
SORTDATE DS    CL6
SORTDATA DS    CL80
         ORG   SORTDATA
SORTFORM DS    H
SORTVEND DS    CL32
SORTCAT  DS    CL32
         ORG   SORTDATA
SORTACRN DS    CL16
SORTPROD DS    CL64
**********************************
         ORG   SORTREC
DATECONV EQU   *
OPTION   DS    CL1
YEAR1J   DS    CL5
JULF1    DS    CL21
DAYOUTG  DS    CL2
YEAROUTG DS    CL8
MNTHNAME DS    CL27
         DS    CL30
**********************************
TITLINE  DS    0CL133
         DC    CL1'1'                TOP OF FORM
         DC    CL44'      ******* SOFTWARE STATUS REPORT *******'
         DC    CL66' '
RPTMONTH DC    CL10' '
RPTDAY   DC    CL6'  , 19'
RPTYEAR  DC    CL2'  '
         DC    CL4' '
HEDLINE  DS    0CL133
         DC    CL74' '
         DC    CL5'ORDER'
         DC    CL8' '
         DC    CL8'FMID    '
         DC    CL7'RELEASE'
         DC    CL6' '
         DC    CL7'PTF/MOD'
         DC    CL9' '
         DC    CL9'DATE     '
DASHLINE DS    0CL133
         DC    CL6' '
         DC    CL1'|'
         DC    CL33' '
         DC    CL31'-------------------------------'
         DC    CL31'-------------------------------'
         DC    CL31'-----------------------------| '
PRTLINE  DS    CL133
THEDUMP  DS    CL20
HOLDLINE DS    CL133
         LTORG
*
         END
//LKED.SYSLMOD  DD DSN=SSU.TESTLIB,DISP=SHR
//LKED.SYSPRINT DD SYSOUT=X
//SYSLIB DD DSN=SSU.LINKLIB,DISP=SHR
//LKED.SYSIN    DD *
    NAME SSR011PR(R)
//
