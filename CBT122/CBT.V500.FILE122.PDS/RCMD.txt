//RTPLBASM JOB (05DPTECH,TECH),'P BANKS',
//             MSGCLASS=R,CLASS=A,
// USER=UUUUUUUU,PASSWORD=PPPPPPPP
//RCMD     EXEC PGM=IFOX00,PARM='DECK,NOLOAD'
//*CMD  EXEC PGM=IEV90,PARM='DECK,NOLOAD'
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR
//         DD  DSN=SYS1.AMODGEN,DISP=SHR
//         DD  DSN=SYS2.MACLIB,DISP=SHR
//SYSUT1   DD  UNIT=VIO,SPACE=(TRK,(100,30))
//SYSUT2   DD  UNIT=VIO,SPACE=(TRK,(100,30))
//SYSUT3   DD  UNIT=VIO,SPACE=(TRK,(100,30))
//SYSPUNCH DD  DSN=BANKS.SOURCE.OBJ(RCMD),DISP=SHR
//SYSPRINT DD  SYSOUT=*
//SYSIN    DD *
         TITLE 'RCMD - ASK RMSG TO ISSUE SELECTED COMMANDS'
*********************************************************************
*                                                                   *
* THIS PROGRAM ISSUES A REQUEST FOR RMSG TO ISSUE SELECTED COMMANDS.*
* 2 SOURCES OF COMMANDS ARE AVAILABLE                               *
* 1) FOR SINGLE COMMANDS, USE THE PARM PARAMETER                    *
* 2) FOR MULTIPLE COMMANDS, USE SYSIN                               *
*                                                                   *
*********************************************************************
RCMD     COMEIN
         SR    R2,R2                  CLEAR WORK REG
         L     R1,0(R1)               POINT TO PARM FIELD
         CLC   0(2,R1),=H'0'          Q. ANY PARM
         BE    TRYSYSIN               A. NO - LOOK AT SYSIN
         SPACE 1
         LH    R2,0(R1)               GET PARM LENGTH
         BCTR  R2,0                   MINUS 1 FOR EXECUTE
         EX    R2,SETCMD              PUT COMMAND IN INPUT AREA
         BAL   R12,DOCMD              GO TO COMMON ROUTINE
         B     EXIT                   NOW LEAVE
SETCMD   MVC   INAREA(0),2(R1)        MOVE COMMAND TO INPUT AREA
         SPACE 1
TRYSYSIN OPEN  (SYSIN,INPUT)          OPEN SYSIN FOR COMMANDS
         LA    R1,SYSIN               POINT TO THE DCB FOR SYSIN
         USING IHADCB,R1
         TM    DCBOFLGS,X'10'         Q. DID SYSIN OPEN OK
         BZ    DOCMDI                 A. NO - NO COMMANDS FOUND
GETCMD   GET   SYSIN,INAREA           READ A COMMAND
         BAL   R12,DOCMD              PROCESS THE COMMAND
         B     GETCMD                 LOOP FOR NEXT COMMAND
         SPACE 1
DOCMD    EQU   *
         CLC   =C'SUPRA',INAREA       Q. START SUPRA INIT
         BNE   DOCMD1                 A. NO
         MVI   WTOTI+12,X'08'
         MVI   WTOSI+12,X'08'
WTOTI    WTO   'RCMD $TI10,V'
WTOSI    WTO   'RCMD $SI10'
         B     DOCMDX
DOCMD1   CLC   =C'RLSE ',INAREA       Q. RELEASE A JOB
         BNE   DOCMD2                 A. NO - INVALID REQUEST
         MVI   WTORLSE+12,X'08'
         MVC   JOBNAME,INAREA+5
WTORLSE  WTO   'RCMD $A''JJJJJJJJ''' SEND RELEASE JOB CMD
JOBNAME  EQU   WTORLSE+16,8
         B     DOCMDX
DOCMD2   CLC   =C'REFRESH ',INAREA    Q. REFRESH THE LLA
         BNE   DOCMDI                 A. NO - INVALID REQUEST
         MVI   WTOLLA+12,X'08'
WTOLLA   WTO   'RCMD Â¬ALL F LLA,REFRESH' REFRESH THE LLA ON ALL SYS
         B     DOCMDX
DOCMDI   CLI   INAREA,C'|'
         BE    SPECIAL
         SPACE 1
         WTO   'RCMD-INVALID OR MISSING COMMAND REQUEST',ROUTCDE=11
         B     DOCMDX
         SPACE 1
SPECIAL  MVI   INAREA,C' '            CLEAR THE DELIMITER
         MVI   INAREA-1,X'08'
         MVC   WTOCMD,INAREA-1        PUT IT IN A WTO
RMSGWTO  WTO   'RCMD .........=.........=.........=',ROUTCDE=11
WTOCMD   EQU   RMSGWTO+13,30
DOCMDX   EQU   *
         BR    R12                    RETURN TO CALLER
         SPACE 1
EODAD    CLOSE SYSIN
EXIT     GETOUT
FLAG     DC    X'07'                  SPECIAL FLAG FOR RMSG
INAREA   DC    CL80' '                INPUT AREA
         DS    0H                     FORCE ALIGNMENT
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=GM,EODAD=EODAD
         LTORG
         DCBD
         END
//LKED      EXEC  PGM=IEWL,PARM='XREF,LIST,LET'
//SYSPRINT  DD SYSOUT=*
//SYSLIN    DD DDNAME=SYSIN
//OBJLIB    DD DSN=BANKS.SOURCE.OBJ,DISP=SHR
//SYSLIB    DD DSN=BANKS.SOURCE.LOAD,DISP=SHR
//SYSLMOD   DD DSN=SYS6.LINKLIB,DISP=SHR
//SYSUT1    DD SPACE=(1024,(50,20)),UNIT=SYSDA
//SYSIN DD *
 INCLUDE OBJLIB(RCMD)
 NAME RCMD(R)
