CMDPGM   TITLE 'INITIAL DEFINITIONS'
*************************************************************
*                                                           *
* MODULE NAME = CMDPGM.                                     *
*                                                           *
* ENTRY NAMES = COMMAND (ALIASES: CMD, APFCMD)              *
*             = PROGRAM (ALIASES: PGM, APFPGM)              *
*                                                           *
* DESCRIPTIVE NAME = INTERFACE FOR THE EXECUTION OF         *
*                    ARBITRARY PROGRAMS OR TSO COMMANDS.    *
*                                                           *
* STATUS = RELEASE 84A.                                     *
*                                                           *
* FUNCTION = THIS ROUTINE PROVIDES A FRIENDLY INTERFACE FOR *
*            EXECUTING ARBITRARY PROGRAMS OR TSO COMMANDS   *
*            FROM ARBITRARY LOAD LIBRARIES. THE ORIGINATING *
*            LOAD LIBRARY MAY BE EITHER A LINK-LIBRARY, A   *
*            JOB-LIBRARY, A STEP-LIBRARY, OR A              *
*            TASK-LIBRARY.                                  *
*                                                           *
*            IF A "TASKLIB" DDNAME EXISTS, OR IF A          *
*            TASK-LIBRARY DDNAME IS GIVEN (SEE THE HELP     *
*            FILES), THEN THAT LIBRARY IS ESTABLISHED AS A  *
*            TASK-LIBRARY FOR THE DURATION OF THE EXECUTION *
*            OF THE DESIRED PROGRAM OR COMMAND.             *
*                                                           *
*            IF THIS PROGRAM IS ENTERED AUTHORIZED, THEN    *
*            (AFTER APPROPRIATE PERMISSION CHECKS) THE      *
*            PROGRAM OR COMMAND TO BE EXECUTED WILL BE      *
*            EXECUTED AUTHORIZED REGUARDLESS OF WHETHER OR  *
*            NOT IT COMES FROM AN AUTHORIZED LIBRARY.       *
*                                                           *
* PROCESSER = OS/VS ASSEMBLER.                              *
*                                                           *
* TYPE = TSO COMMAND PROCESSOR OR BATCH PROGRAM.            *
*                                                           *
* ATTRIBUTES = REENTRANT, REFRESHABLE, REUSABLE.            *
*                                                           *
* AUTHOR = DAVID B. COLE.                                   *
*                                                           *
* CHANGE LOG:                                               *
*                                                           *
*   2006/01/12 - ADJUSTED RMODE TO 31, AMODE TO 24.         *
*                GOT RID OF PSCBATR2 FLAGS AND JUST         *
*                TESTED PSCBATR1 FOR OPER (X'80')           *
*                ONLY.  GOOD ENOUGH FOR DISTRIBUTION.       *
*                YOU CHANGE YOUR OWN PERMIT MECHANISM       *
*                ACCORDING TO TASTE.                        *
*                                                           *
*************************************************************
*                                                           *
* ENTRY POINT = PROGRAM.                                    *
*                                                           *
* ALIAS NAME = PGM, APFPGM.                                 *
*                                                           *
* COMMAND FORMAT = PROGRAM <PGM NAME>:<DDNAME> <OPERANDS>   *
*                                                           *
* FUNCTION = THE INDICATED PROGRAM IS GIVEN CONTROL WITH R1 *
*            POINTING TO A POINTER TO A STANDARD OS PARM    *
*            FIELD CONSTRUCTED FROM THE GIVEN OPERANDS.     *
*                                                           *
*            :<DDNAME> IS OPTIONAL. IF GIVEN, THEN THAT     *
*            LIBRARY IS ESTABLISHED AS A TASK-LIBRARY FOR   *
*            THE PROGRAM. IF OMITTED, THEN IF A "TASKLIB"   *
*            DDNAME EXISTS, THEN THAT IS USED FOR THE       *
*            TASK-LIBRARY. IF A COLON IS GIVEN WITHOUT A    *
*            DDNAME, THEN NO TASK-LIBRARY IS ESTABLISHED    *
*            REGARDLESS OF WHETHER OR NOT A "TASKLIB"       *
*            DDNAME EXISTS.                                 *
*                                                           *
* ATTRIBUTES = THE APFPGM ALIAS SHOULD BE LINKED            *
*              AUTHORIZED.                                  *
*                                                           *
*************************************************************
*                                                           *
* ENTRY POINT = COMMAND.                                    *
*                                                           *
* ALIAS NAME = CMD, APFCMD.                                 *
*                                                           *
* COMMAND FORMAT = COMMAND <CMD NAME>:<DDNAME> <OPERANDS>   *
*                                                           *
* FUNCTION = THE INDICATED TSO COMMAND PROCESSOR IS GIVEN   *
*            CONTROL WITH R1 POINTING TO A CPPL.            *
*                                                           *
*            :<DDNAME> IS OPTIONAL. IF GIVEN, THEN THAT     *
*            LIBRARY IS ESTABLISHED AS A TASK-LIBRARY FOR   *
*            THE COMMAND. IF OMITTED, THEN IF A "TASKLIB"   *
*            DDNAME EXISTS, THEN THAT IS USED FOR THE       *
*            TASK-LIBRARY. IF A COLON IS GIVEN WITHOUT A    *
*            DDNAME, THEN NO TASK-LIBRARY IS ESTABLISHED    *
*            REGARDLESS OF WHETHER OR NOT A "TASKLIB"       *
*            DDNAME EXISTS.                                 *
*                                                           *
* ATTRIBUTES = THE APFCMD ALIAS SHOULD BE LINKED            *
*              AUTHORIZED.                                  *
*                                                           *
*************************************************************
         TITLE 'IBM CONTROL BLOCKS'
CMDPGM   CSECT ,
CMDPGM   AMODE 31
CMDPGM   RMODE 24
         PRINT GEN
         SPACE 3
*************************************************************
*        ASCB = ADDRESS SPACE CONTROL BLOCK                 *
*        ASCB = DSECT AND BASE                              *
*************************************************************
         SPACE 1
         IHAASCB ,
         SPACE 3
*************************************************************
*        CDE = CONTENTS DIRECTORY ENTRY                     *
*        CDENTRY = DSECT AND BASE                           *
*************************************************************
         SPACE 1
         IHACDE ,
         SPACE 3
*************************************************************
*        CPPL = COMMAND PROCESSOR PARAMETER LIST (TSO       *
*               COMMANDS)                                   *
*        CPPL = DSECT AND BASE                              *
*************************************************************
         SPACE 1
         IKJCPPL ,
         SPACE 3
*************************************************************
*        DCB = DATA CONTROL BLOCK                           *
*        IHADCB = DSECT AND BASE                            *
*************************************************************
         SPACE 1
         DCBD  DSORG=(PS,PO)
         SPACE 3
*************************************************************
*        DEB = DATA EXTENT BLOCK (IOS)                      *
*        AVT = APPENDAGE VECTOR TABLE (IOS)                 *
*        DEB = AVT AND DEB BASIC SECTION DSECT              *
*        DEB = AVT BASE                                     *
*        DEBBASIC = BASIC SECTION BASE                      *
*        DEBDASD = DIRECT ACCESS SECTION DSECT AND BASE     *
*        DEBACSMD = ACCESS METHOD SECTIONS DSECT AND BASE   *
*        DEBSUBNM = SUBROUTINE NAMES SECTION DSECT AND BASE *
*        DEBXTN = EXTENSION SECTION DSECT AND BASE          *
*************************************************************
         SPACE 1
         IEZDEB LIST=YES
         SPACE 3
*************************************************************
*        ECT = ENVIRONMENT CONTROL TABLE (TSO)              *
*        ECT = DSECT AND BASE                               *
*************************************************************
         SPACE 1
         IKJECT ,
         SPACE 3
*************************************************************
*        JSCB = JOB STEP CONTROL BLOCK                      *
*        IEZJSCB = DSECT AND BASE                           *
*************************************************************
         SPACE 1
         IEZJSCB ,
         SPACE 3
*************************************************************
*        PSA = PREFIXED STORAGE AREA                        *
*        PSA = DSECT AND BASE                               *
*************************************************************
         SPACE 1
         IHAPSA ,
         SPACE 3
*************************************************************
*        PSCB = PROTECTED STEP CONTROL BLOCK (TSO)          *
*        PSCB = DSECT AND BASE                              *
*************************************************************
         SPACE 1
         IKJPSCB ,
         SPACE 3
*************************************************************
*        RB = REQUEST BLOCK                                 *
*        RBPRFX = PREFIX AND BASIC SECTIONS DESCT           *
*        RBPRFX = PREFIX SECTION BASE                       *
*        RBBASIC = BASIC SECTION BASE                       *
*************************************************************
         SPACE 1
         IHARB ,
         SPACE 3
*************************************************************
*        S99 = SVC 99 PARAMETERS (DYNAMIC ALLOCATION)       *
*        S99RBP = SVC 99 INPUT REQUEST BLOCK POINTER DSECT  *
*                 AND BASE                                  *
*        S99RB = SVC 99 INPUT REQUEST BLOCK DSECT AND BASE  *
*        S99TUPL = SVC 99 TEXT UNITS POINTER LIST DSECT AND *
*                  BASE                                     *
*        S99TUNIT = SVC 99 TEXT UNIT DSECT AND BASE         *
*        S99TUFLD = SVC 99 TEXT UNIT FIELDS DSECT AND BASE  *
*************************************************************
         SPACE 1
         IEFZB4D0 ,
         SPACE 3
*************************************************************
*        TEXT KEY NAMES FOR SVC 99 (DYNAMIC ALLOCATION)     *
*        SVC99KYS = DSECT                                   *
*************************************************************
         SPACE 1
         IEFZB4D2 ,
         SPACE 3
*************************************************************
*        TCB = TASK CONTROL BLOCK                           *
*        TCBFIX = PREFIX AND BASIC SECTIONS DSECT           *
*        TCBFIX = PREFIX SECTION BASE                       *
*        TCB = BASIC SECTION BASE                           *
*        TCBXTNT2 = COMMON EXTENSION DSECT AND BASE         *
*************************************************************
         SPACE 1
         IKJTCB LIST=YES
         SPACE 3
*************************************************************
*        TIOT = TASK INPUT/OUTPUT TABLE                     *
*        DSECT CARD NOT GENERATED                           *
*        TIOCNJOB = BASIC SECTION BASE                      *
*        TIOENTRY = DD ENTRY BASE                           *
*        TIOESTTB = DEVICE ENTRY BASE                       *
*************************************************************
         SPACE 1
TIOT     DSECT ,
         IEFTIOT1 ,
         SPACE 3
*************************************************************
*        UPT = USER PROFILE TABLE (TSO)                     *
*        UPT = DSECT AND BASE                               *
*************************************************************
         SPACE 1
         IKJUPT ,
         SPACE 3
*************************************************************
*        STANDARD REGISTER NAMES                            *
*************************************************************
         SPACE 1
         #REGS R
         SPACE 3
         PRINT ON,GEN,NODATA
         TITLE 'DYNAMIC STORAGE AREA'
*************************************************************
*        DSAT -- LOCAL DYNAMIC STORAGE AREA                 *
*************************************************************
         SPACE 1
DSAT     #DSA  ,                   STANDARD REGISTER SAVE AREA
         SPACE 3
DSATDATA DS    0X                  START OF DATA AREA
         SPACE 1
*************************************************************
*        VARIOUS STANDARD REGISTER SAVE AREAS               *
*************************************************************
         SPACE 1
DSASAVE2 DS    18A                 USED BY DBCLOADR
         SPACE 3
*************************************************************
*        MISCELLANEOUS LOCAL SUBROUTINE SAVE AREAS          *
*************************************************************
         SPACE 1
SAVEPUM1 DS    4A                  PUTMSG1
SAVEPUM2 DS    4A                  PUTMSG2
         SPACE 3
*************************************************************
*        DYNAMIC ALLOCATION PARAMETERS                      *
*************************************************************
         SPACE 1
DALPLIST DS    0F
         DC    AL1(S99RBPND),AL3(DALRB)
         SPACE 1
DALRB    DS    0F
         DC    AL1(DALRBLEN,S99VRBIN,S99NOMNT,0)
         DC    2X'0000'
         DC    A(DALTEXTP)
         DC    XL4'00'
         DC    4B'00000000'
DALRBLEN EQU   *-DALRB
         SPACE 1
DALTEXTP DS    0F
         DC    AL1(S99TUPLN),AL3(LIBDDNAM)
DALTEXT  DS    0H
LIBDDNAM DC    Y(DINDDNAM,1,8)
DALTEXTL EQU   *-DALTEXT
LIBDDN   DS    CL8
         SPACE 1
         ORG   ,
         SPACE 3
*************************************************************
*        DCB AND OPEN PLIST FOR TASKLIB                     *
*************************************************************
         SPACE 1
TSKSAM   DS    0F
         PRINT NOGEN
TSKDCB   DCB   DDNAME=*-*,DSORG=PO,MACRF=R
         PRINT ON,GEN,NODATA
         SPACE 1
TSKOPEN  OPEN  TSKDCB,MF=L
TSKSAML  EQU   *-TSKSAM
         SPACE 3
*************************************************************
*        ATTACH PARAMETER LIST                              *
*************************************************************
         SPACE 1
DSATTACH ATTACH EPLOC=TSOCMDN,ECB=TERMECB,SHSPV=78,SZERO=NO,           *
               TASKLIB=*-*,TERM=NO,SF=L
         SPACE 3
*************************************************************
*        WTO BUFFER AND MESSAGE CONSTRUCTION AREA.          *
*************************************************************
         SPACE 1
WTO      DS    0F
WTOHEAD  DS    XL4
WTOTEXT  DS    CL256
WTOTAIL  DS    XL4
         SPACE 3
*************************************************************
*        MISCELLANEOUS                                      *
*************************************************************
         SPACE 1
CMDNAME  DS    CL8                 NAME OF COMMAND OR PROGRAM TO BE
*                                  DEBUGGED
TLIBDDN  DS    CL8                 ALTERNATE DDNAME FOR TASKLIB
         SPACE 1
DSAPARMA DS    A                   A(PARM FIELD OR CPPL)
PARMDATA DS    A                   --> PAST COMMAND NAME TO REAL
*                                  PARM DATA (BATCH ONLY)
DSABASER DS    A                   COMMAND'S BASE ADDRESS
DSACMD   DS    A                   A(COMMAND PROCESSOR)
TERMECB  DS    A                   TERMINATION ECB
DSATCB   DS    A                   A(SUBTASK TCB)
PARAMETR DS    A                   R1 VALUE TO PASS TO PGM/CMD
         SPACE 1
DSACC    DS    H                   SUB COMMAND COMPLETION CODE
PARMDATL DS    H                   LENGTH OF REAL PARM DATA (BATCH
*                                  ONLY)
         SPACE 1
TSOCMDN  DS    CL8                 NAME USED BY ATTACH
         SPACE 3
*************************************************************
*        FLAG BYTE                                          *
*************************************************************
         SPACE 1
DSATFLAG DS    B                   FLAG BYTE
CALLED   EQU   B'10000000'         PROGRAM (NOT COMMAND)
BATCH    EQU   B'01000000'         RUNNING IN THE BATCH
NOMSGID  EQU   B'00100000'         DELETE MSGID'S FROM MESSAGES
IAMAUTHD EQU   B'00010000'         I AM RUNNING AUTHORIZED
TLIBREQ  EQU   B'00001000'         A TASKLIB DDNAME IS REQUIRED
         SPACE 3
*************************************************************
*        END OF DYNAMIC STORAGE AREA                        *
*************************************************************
         SPACE 1
DSAZ     DS    0H
DSALEN   EQU   DSAZ-DSAT
         TITLE 'REGISTER AND OTHER EQUATES'
*************************************************************
*        ADDITIONAL REGISTER EQUATES                        *
*************************************************************
         SPACE 1
         #REGS (BASEREG,R12),      COMMAND & CMDLOADR BASE REG         *
               (DSATREG,R11)       DSATDATA PTR FOR COMMAND & LOADR
         TITLE 'COMMAND PROCESSOR ATTACHER'
*************************************************************
*        ENTRY POINTS. SET A SIGNAL DISTINGUISHING THEM.    *
*************************************************************
         SPACE 1
CMDPGM   CSECT ,
         SPACE 1
         ENTRY COMMAND,CMD,APFCMD  ALIAS NAMES
COMMAND  DS    0H                  ALIAS
CMD      DS    0H                  ALIAS
APFCMD   SR    R0,R0               SIGNAL 'COMMAND'
         B     COMMON-COMMAND(,R15) GO TO COMMON CODE
         SPACE 1
         ENTRY PROGRAM,PGM,APFPGM  ALIAS NAMES
PROGRAM  DS    0H                  ALIAS
PGM      DS    0H                  ALIAS
APFPGM   LA    R0,1                SIGNAL 'PROGRAM'
         SPACE 3
*************************************************************
*        REENTRANT ENTRY LINKAGE                            *
*************************************************************
         SPACE 1
COMMON   BALR  R15,0               LOAD TEMP BASE
BASE     #ENTER COMMAND/PROGRAM,ESDTYPE=NONE,                          *
               SAVTYPE=(RENT,(DSALEN,1)),BASES=(BASEREG)
         LA    DSATREG,DSATDATA-DSAT(,R13) --> DATA AREA
         USING DSATDATA,DSATREG    DCL DATA AREA BASE
         SPACE 3
*************************************************************
*        INITIALIZE THE DSA                                 *
*************************************************************
         SPACE 1
         ST    R1,DSAPARMA         SAVE A(PARM FIELD OR CPPL)
         ST    BASEREG,DSABASER    SAVE COMMAND'S BASE ADDRESS
         SPACE 1
         LTR   R0,R0               PROGRAM?
         BZ    CMDED               NO, COMMAND
         OI    DSATFLAG,CALLED     YES, REMEMBER
CMDED    DS    0H
         SPACE 3
*************************************************************
*        IF I AM RUNNING AUTHORIZED, VERIFY THAT MY CALLER  *
*        IS PERMITTED TO CALL ME.                           *
*************************************************************
         SPACE 1
         TESTAUTH FCTN=1,STATE=YES,KEY=YES,RBLEVEL=1 AM I SPECIAL?
         LTR   R15,R15             AM I SPECIAL?
         BNZ   PERMIT              NO, PERMIT MY USE
         OI    DSATFLAG,IAMAUTHD   YES, REMEMBER
         EJECT ,
*************************************************************
*        THE FOLLOWING IS COMPUTER CENTER DEPENDANT         *
*        AUTHORITY CHECKING CODE.                           *
         MNOTE 0,'  IMPORTANT -- REPLACE THE FOLLOWING CODE WITH       *
               *'
         MNOTE 0,'  AUTHORITY CHECKING CODE APPROPRIATE FOR YOUR       *
               *'
         MNOTE 0,'  COMPUTER CENTER.                                   *
               *'
*************************************************************
         SPACE 1
*        ALWFLAGS ,               PSCBATR2 FLAGS SET AT COLE'S OLD SITE
         SPACE 1
         L     R1,PSAAOLD-PSA      --> ASCB
         USING ASCB,R1             DCL ASCB BASE
         ICM   R15,15,ASCBJBNI     INITIATED (BATCH) JOB?
         BNZ   INBATCH             YES, GO TEST JOBNAME
         ICM   R0,15,ASCBTSB       NO, TSO JOB?
         BZ    PERMIT              NO, STC; PERMIT ACCESS
         DROP  R1                  YES, RELEASE ASCB BASE
         SPACE 1
INTSO    L     R1,PSATOLD-PSA      --> MY TCB
         L     R1,TCBJSCB-TCB(,R1) --> ITS JSCB
         L     R1,JSCBACT-IEZJSCB(,R1) --> ACTIVE JSCB
         ICM   R1,15,JSCBPSCB-IEZJSCB(R1) --> PSCB; EXIST?
         BZ    PROHIBIT            NO (STRANGE); DENY PERMISSION
         TM    PSCBATR1-PSCB(R1),X'80'      AT LEAST OPER PRIVILEGE?
         BNZ   PERMIT              YES, PERMIT MY USE
         B     PROHIBIT            NO, DENY PERMISSION
         SPACE 1
INBATCH  CLC   =C'J0PD',0(R15)     PRODUCT DEVELOPMENT?
         BE    PERMIT              YES, PERMIT MY USE
         CLC   =C'J0PS',0(R15)     NO, PRODUCT SUPPORT?
         BE    PERMIT              YES, PERMIT MY USE
         SPACE 1
*************************************************************
*        END OF COMPUTER CENTER DEPENDANT AUTHORITY         *
*        CHECKING CODE.                                     *
*************************************************************
         EJECT ,
*************************************************************
*        AUTHORIZED ACCESS HAS BEEN DENIED. BUILD A         *
*        MISSLEADING ERROR MESSAGE, AND EXIT.               *
*************************************************************
         SPACE 1
PROHIBIT MVC   WTOTEXT(18),=C'IKJ56500I COMMAND ' TEXT
         LA    R1,WTOTEXT+16       SCANNER
         L     R15,PSATOLD-PSA     --> MY TCB
         L     R15,TCBRBP-TCB(,R15) --> MY PRB (ASSUMED)
         ICM   R15,7,RBCDE1-RBBASIC(R15) --> MY CDE; DEFINED?
         BZ    NOPGMNAM            NO, SKIP
         MVC   2(L'CDNAME,R1),CDNAME-CDENTRY(R15) GET MY NAME
         LA    R1,2+L'CDNAME(,R1)  ADVANCE SCANNER
         BALR  R14,0               LOOPER
         BCTR  R1,0                BACK SCAN
         TM    0(R1),255-C' '      TRAILING BLANK?
         BZR   R14                 YES, CONTINUE BACKSCANNING
NOPGMNAM MVC   1(10,R1),=C' NOT FOUND' NO, APPEND TEXT
         #TEST SIZE=(L'WTOTEXT,GE,18+L'CDNAME+10)
         LA    R0,WTOTEXT-11       --> SO-MSG
         SR    R1,R0               GET L'MSG
         STC   R1,WTOTEXT-1        STORE
         LA    R2,WTOTEXT-1        --> MSG
         B     ERROR               GO DISPLAY AND EXIT
         SPACE 3
*************************************************************
*        ACCESS (AUTHORIZED OR NON-AUTHORIZED) HAS BEEN     *
*        GRANTED.                                           *
*************************************************************
         SPACE 1
PERMIT   DS    0H
         SPACE 3
*************************************************************
*        DETERMINE WHETHER WE ARE RUNNING IN TSO OR IN THE  *
*        BATCH WITHOUT THE TMP. IN THE LATTER CASE,         *
*        DISALLOW "COMMAND".                                *
*************************************************************
         SPACE 1
         L     R1,PSATOLD-PSA      --> MY TCB
         L     R1,TCBJSCB-TCB(,R1) --> JSCB
         L     R1,JSCBACT-IEZJSCB(,R1) --> ACTIVE JSCB
         ICM   R1,15,JSCBPSCB-IEZJSCB(R1) --> PSCB; EXIST?
         BNZ   INTSO2              YES, RUNNING UNDER A TMP
*                                  EITHER IN TSO OR IN BATCH
         OI    DSATFLAG,BATCH      NO, RUNNING IN BATCH
*                                  WITHOUT A TMP
         SPACE 1
         LA    R2,CANTCMD-1        --> POSSIBLE ERR MSG
         TM    DSATFLAG,CALLED     PROGRAM?
         BZ    ERROR               NO, COMMAND; TERMINATE
         SPACE 3
*************************************************************
*        RUNNING IN THE BATCH WITHOUT A TMP. ISSOLATE DATA  *
*        FROM THE PARM FIELD.                               *
*************************************************************
         SPACE 1
         LA    R2,BADPARM-1        --> POSSIBLE ERROR MSG
         ICM   R1,15,DSAPARMA      --> PARM PLIST; EXIST?
         BZ    ERROR               NO, TERMINATE
         L     R1,0(,R1)           --> PARM
         N     R1,=X'7FFFFFFF'     PURIFY; EXIST?
         BZ    ERROR               NO, TERMINATE
         LH    R0,0(,R1)           YES, GET ITS LENGTH
         LTR   R0,R0               PARM FIELD GIVEN?
         BNP   ERROR               NO, TERMINATE
         LA    R1,2(,R1)           YES, --> DATA
         B     DATASCAN            GO SCAN IT
         SPACE 3
*************************************************************
*        RUNNING UNDER A TMP (EITHER IN TSO OR IN THE       *
*        BATCH). SET THE MSGID CONTROL FLAG APPROPRIATELY.  *
*************************************************************
         SPACE 1
INTSO2   L     R1,DSAPARMA         --> CPPL
         USING CPPL,R1             DCL CPPL BASE
         L     R15,CPPLUPT         --> UPT
         TM    UPTSWS-UPT(R15),UPTMID DISPLAY MSGID'S?
         BNZ   GOTMIDSW            YES, PROCEED
         OI    DSATFLAG,NOMSGID    NO, REMEMBER
GOTMIDSW DS    0H
         SPACE 3
*************************************************************
*        ISSOLATE DATA FROM THE COMMAND BUFFER.             *
*************************************************************
         SPACE 1
         L     R1,CPPLCBUF         --> COMMAND BUFFER
         DROP  R1                  RELEASE CPPL BASE
         LH    R0,0(,R1)           GET L'CBUF
         AR    R0,R1               --> PAST CBUF
         SPACE 1
         LA    R14,4(,R1)          --> CLEARANCE SINK
         LH    R15,2(,R1)          GET CLEARANCE LENGTH
         L     R1,=AL1(C' ',0,0,0) GET CLEARANCE CONTROL
         MVCL  R14,R0              CLEAR ORIGINAL COMMAND NAME
         LR    R1,R14              --> TARGET COMMAND/PROGRAM NAME
         SPACE 1
         LA    R2,BADCBUF-1        --> POSSIBLE ERROR MSG
         SR    R0,R1               GET L'OPERANDS; NULL?
         BNP   ERROR               YES, ERROR
         SPACE 1
         LA    R2,IECNS-1          NO, --> POSSIBLE ERROR MSG
         CLI   0(R1),C'%'          IMPLICIT EXEC REQ?
         BE    ERROR               YES, ERROR
         SPACE 1
         LA    R2,NIA-1            NO, --> POSSIBLE INFO MSG
         CLI   0(R1),C'?'          HELP REQUESTED?
         BE    ERROR               YES, ERROR
         SPACE 3
*************************************************************
*        NOW SCAN THE DATA TO EXTRACT THE DESIRED PROGRAM   *
*        OR COMMAND NAME.                                   *
*************************************************************
         SPACE 1
DATASCAN TM    0(R1),C'0'          YES, START WITH A DIGIT?
         BO    BPGMERR             YES, INVALID
         LR    R14,R1              NO, SAVE SO-PARMS
         SPACE 1
BPGMSCAN SR    R2,R2               CLEAR FOR INSERT
         TRT   0(1,R1),VALID       TEST NEXT CHARACTER
         B     *+4(R2)             IS IT VALID?
         B     BPGMERR             +0 NO, INVALID PGM NAME
         B     BPGMGOTN            +4 DELIMITER; EO-NAME
         LA    R1,1(,R1)           +8 YES, ADVANCE THE SCANR
         BCT   R0,BPGMSCAN         LOOP FOR NEXT CHARACTER
         SPACE 1
BPGMGOTN LR    R15,R1              SAVE EO-CMD
         SR    R1,R14              GET L'CMD; NULL?
         BNP   BPGMERR             YES, INVALID
         CH    R1,=Y(L'CMDNAME)    NO, TOO LONG?
         BNH   BPGMOK              NO, PROCEED
         SPACE 1
BPGMERR  LA    R2,CNSE-1           YES, --> ERROR MSG
         B     ERROR               GO SEND IT
         SPACE 1
BPGMOK   MVC   CMDNAME,=CL(L'CMDNAME)' ' CLEAR THE NAME BUFFER
         BCTR  R1,0                ADJ L'NAME FOR 'EX'
         OC    0(*-*,R14),CMDNAME  (EXECUTED)
         EX    R1,*-6              UPCASE THE NAME
         MVC   CMDNAME(*-*),0(R14) (EXECUTED)
         EX    R1,*-6              COPY IT TO LOCAL BUFFER
         SPACE 3
*************************************************************
*        CONTINUE SCANNING TO EXTRACT A TASKLIB DDNAME, IF  *
*        ANY.                                               *
*************************************************************
         SPACE 1
         MVC   TLIBDDN,=CL(L'TLIBDDN)'TASKLIB' DEFAULT DDNAME
         LTR   R0,R0               ANY MORE DATA?
         BNP   NODDN               NO, SKIP
         CLI   0(R15),C':'         YES, IS A TASKLIB DDNAME GIVEN?
         BNE   NODDN               NO, SKIP
         MVI   0(R15),C' '         YES, CLEAR
         MVC   TLIBDDN,=CL(L'TLIBDDN)' ' CLEAR THE NAME BUFFER
         LA    R15,1(,R15)         ADVANCE SCANNER
         BCT   R0,BDDNMORE         DECR RESIDUE & PROCEED
         B     NODDN               NO MORE; DONE HERE
         SPACE 1
BDDNMORE TM    0(R15),C'0'         START WITH A DIGIT?
         BO    BDDNERR             YES, INVALID
         LR    R1,R15              NO, COPY SO-PARMS
         SPACE 1
BDDNSCAN SR    R2,R2               CLEAR FOR INSERT
         TRT   0(1,R1),VALID       TEST NEXT CHARACTER
         B     *+4(R2)             IS IT VALID?
         B     BDDNERR             +0 NO, INVALID PGM NAME
         B     BDDNGOTN            +4 DELIMITER; EO-NAME
         LA    R1,1(,R1)           +8 YES, ADVANCE THE SCANR
         BCT   R0,BDDNSCAN         LOOP FOR NEXT CHARACTER
         B     BDDNGOT2            PROCEED
         SPACE 1
BDDNGOTN CLI   0(R1),C':'          EO-DDN; IS DELIM OK?
         BE    BDDNERR             NO, ERROR
BDDNGOT2 LR    R14,R1              YES, SAVE EO-DDN
         SR    R1,R15              GET L'DDN; NULL?
         BNP   NULLDDN             YES, DONE
         CH    R1,=Y(L'TLIBDDN)    NO, TOO LONG?
         BNH   BDDNOK              NO, PROCEED
         SPACE 1
BDDNERR  LA    R2,BADDDN-1         YES, --> ERROR MSG
         B     ERROR               GO SEND IT
         SPACE 1
BDDNOK   OI    DSATFLAG,TLIBREQ    "TLIB IS REQUIRED"
         BCTR  R1,0                ADJ L'NAME FOR 'EX'
         OC    TLIBDDN(*-*),0(R15) (EXECUTED)
         EX    R1,*-6              COPY AND UPCASE THE NAME TO BUFFER
         SPACE 1
NULLDDN  BCTR  R15,0               BACK UP ONE
         MVC   1(*-*,R15),0(R15)   (EXECUTED)
         EX    R1,*-6              BLANK OUT THE DDNAME
         LR    R15,R14             ADVANCE SCANNER
NODDN    DS    0H
         SPACE 3
*************************************************************
*        NOW LOCATE THE START OF, AND DETERMINE THE LENGTH  *
*        OF THE OPERANDS/PARMS TO BE PASSED TO THE TARGET   *
*        COMMAND/PROGRAM.                                   *
*************************************************************
         SPACE 1
         LTR   R0,R0               ANY MORE DATA?
         BNP   SOOPNDS             NO, SKIP
         MVI   0(R15),C' '         YES, CHANGE POSSIBLE C',' TO C' '
         BALR  R14,0               LOOPER
         TM    0(R15),255-C' '     LEADING DELIMITER?
         BNZ   SOOPNDS             NO, GOT OPERANDS
         LA    R15,1(,R15)         YES, ADVANCE
         BCTR  R0,R14              AND LOOP
         SPACE 1
SOOPNDS  ST    R15,PARMDATA        YES, SAVE DATA POINTER
         STH   R0,PARMDATL         AND SAVE L'DATA
         SPACE 3
*************************************************************
*        CHECK TO SEE IF THE USE OF A TASKLIB IS TO BE      *
*        BYPASSED.                                          *
*************************************************************
         SPACE 1
         CLI   TLIBDDN,C' '        WAS A BLANK TLIB DDNAME GIVEN?
         BE    TSKLGOTN            YES, FORGET IT
         SPACE 3
*************************************************************
*        DETERMINE IF THE DDNAME TASKLIB HAS BEEN ASSIGNED  *
*        AS A TASK LIBRARY TO EITHER THE CURRENT TASK OR    *
*        TO ANY ORIGINATING TASK OF THE CURRENT TASK.       *
*************************************************************
         SPACE 1
         L     R1,PSATOLD-PSA      --> OUR TCB
         USING TCB,R1              DCL TCB BASE
TSKLSRCH ICM   R15,7,TCBJLB+1      JOB-, STEP-, | TASK-LIBRARY EXIST?
         BZ    TSKLCLMB            NO, GO GET MOMMY
         USING IHADCB,R15          YES, DCB DCB BASE
         TM    DCBOFLGS,DCBOFOPN   IS THE LIBRARY DCB OPEN?
         BZ    TSKLCLMB            NO (VERY STRANGE), GO GET MOMMY
         LH    R15,DCBTIOT         YES, GET TIOT DD ENTRY OFFSET
         DROP  R15                 RELEASE DCB BASE
         A     R15,TCBTIO          + A(TIOT)
         USING TIOENTRY,R15        = A(LIB'S DD ENTRY IN THE TIOT)
         CLC   TIOEDDNM,TLIBDDN    DDNAME = 'TASKLIB'?
         BE    TSKLGOTN            YES, NO NEED TO RESPECIFY
         DROP  R15                 NO, RELEASE TIOT DD ENTRY BASE
TSKLCLMB ICM   R1,7,TCBOTC+1       --> MOMMY; EXIST?
         BNZ   TSKLSRCH            YES, GO CHECK IT OUT
         DROP  R1                  NO, I MUST BE GOD
         SPACE 3
*************************************************************
*        SEE IF A TASKLIB DDNAME EXISTS.                    *
*************************************************************
         SPACE 1
         LA    R1,DALRB
         ST    R1,DALPLIST
         MVI   DALPLIST,S99RBPND   PLIST TERMINATER
         MVI   S99RBLN-S99RB+DALRB,DALRBLEN L'RB
         MVI   S99VERB-S99RB+DALRB,S99VRBIN
         MVI   S99FLAG1-S99RB+DALRB,S99NOMNT
         LA    R1,DALTEXTP         TEXT KEYS VECTOR
         ST    R1,S99TXTPP-S99RB+DALRB
         LA    R14,LIBDDNAM
         ST    R14,DALTEXTP        TEXT KEY VECTOR
         MVI   DALTEXTP,S99TUPLN   VECTOR TERMINATER
         MVC   DALTEXT(DALTEXTL),MDLTEXT TEXT KEYS
         MVC   LIBDDN,TLIBDDN      INSERT THE DDNAME
         LA    R1,DALPLIST         --> PLIST
         DYNALLOC ,                ATTEMPT DYNAMIC ALLOCATION
         LTR   R15,R15             AOK?
         BZ    TLIBOPEN            YES, PROCEED
         SPACE 1
TLIBFAIL TM    DSATFLAG,TLIBREQ    NO, IS A TASKLIB REQUIRED?
         BZ    TSKLGOTN            NO, FORGET TASKLIB
         LA    R2,NOTLIB-1         YES, --> ERROR MSG
         B     ERROR               EXIT
         SPACE 3
*************************************************************
*        THE DDNAME TASKLIB EXISTS. OPEN THE LIBRARY DATA   *
*        AND SET IT UP TO BE A TASK LIBRARY.                *
*************************************************************
         SPACE 1
TLIBOPEN MVC   TSKSAM(TSKSAML),MDLSAM DCB AND OPEN PLIST
         #TEST SIZE=(TSKSAML,EQ,MDLSAML)
         MVC   DCBDDNAM-IHADCB+TSKDCB,LIBDDN DDNAME
         OPEN  (TSKDCB,INPUT),MF=(E,TSKOPEN) OPEN THE TASK LIBRARY
         SPACE 1
         TM    DCBOFLGS-IHADCB+TSKDCB,DCBOFOPN OPEN OK?
         BNZ   TSKOPNED            YES, PROCEED
         XC    TSKOPEN,TSKOPEN     NO, CLEAR THE POINTER
         B     TLIBFAIL            GO CHECK FOR REQUIRED
TSKOPNED DS    0H
         SPACE 3
*************************************************************
*        IF I AM SPECIAL, THEN INSURE THAT I CAN LOAD       *
*        PROGRAMS FROM THE TASK-LIBRARY.                    *
*************************************************************
         SPACE 1
         TM    DSATFLAG,IAMAUTHD   AM I SPECIAL?
         BZ    TSKLGOTN            NO, SKIP
         L     R2,DCBDEBAD-IHADCB+TSKDCB YES, --> TSKLIB'S DEB
         USING DEBBASIC,R2         DCL DEB BASE
         TM    DEBFLGS1,DEBAPFIN   AUTHORIZED LIBRARY?
         BNZ   TSKLGOTN            YES, AOK
         SPACE 1
         MODESET KEY=ZERO          NO, LOAD KEY-0
         OI    DEBFLGS1,DEBAPFIN   MAKE IT AUTHORIZED
         MODESET KEY=NZERO         RESUME TCB-KEY
         DROP  R2                  RELEASE DEB BASE
TSKLGOTN DS    0H
         SPACE 3
*************************************************************
*        IDENTIFY A LOCAL ENTRY POINT. GO TO GREAT LENGTHS  *
*        TO FIND A UNIQUE NAME THAT IS STILL REMINISCENT OF *
*        THE NAME OF THE COMMAND/PROGRAM BEING CALLED.      *
*************************************************************
         SPACE 1
         MVC   TSOCMDN,CMDNAME     COPY PROGRAM NAME
         LA    R3,TSOCMDN+L'TSOCMDN --> PAST IT
TNBLP    BCTR  R3,0                BACK UP
         CLI   0(R3),C' '          TRAILING BLANK?
         BE    TNBLP               YES, KEEP BACKING
         CLI   TSOCMDN+L'TSOCMDN-1,C' ' NO, 8-CHAR NAME?
         BNE   GOTEON              YES, WILL KLOTZ LAST CHAR
         LA    R3,1(,R3)           NO, --> PAST NAME
GOTEON   DS    0H
         SPACE 1
         LH    R2,=H'-1'           LOAD SUFFIX GENERATER
IDENLOOP LA    R2,1(,R2)           --> NEXT SUFFIX
         CH    R2,=H'256'          ALL POSSIBILITIES EXHAUSTED?
         #DIE  NL                  YES, GIVE UP
         STC   R2,0(,R3)           NO, INSERT SUFFIX
         SPACE 1
         CLC   TSOCMDN,CMDNAME     SAME NAME AS TARGET PGM?
         BE    IDENLOOP            YES, TRY ANOTHER NAME
         SPACE 1
         L     R1,=A(CMDLOADR)     NO, --> INTERCEPT ROUTINE
         IDENTIFY EPLOC=TSOCMDN,ENTRY=(1)
         LTR   R15,R15             AOK?
         BNZ   IDENLOOP            NO, TRY NEXT NAME SUFFIX
         SPACE 3
*************************************************************
*        ATTACH A LOCAL ROUTINE WHICH WILL EVENTUALLY LOAD  *
*        AND PASS CONTROL TO THE PROGRAM TO BE EXECUTED.    *
*************************************************************
         SPACE 1
         MVI   TERMECB,0           CLEAR THE ECB
         L     R3,TSKOPEN          LOAD A(TASKLIB) OR 0
         LA    R3,0(,R3)           PURIFY
         LR    R1,DSATREG          A(DSATDATA)
         SPACE 1
         ATTACH EPLOC=TSOCMDN,ECB=TERMECB,SHSPV=78,SZERO=NO,           *
               TASKLIB=(R3),TERM=NO,SF=(E,DSATTACH)
         ORG   *-2                 DON'T SVC YET
         LTR   R3,R3               DOES A TASKLIB EXIST?
         BNZ   SVC42               YES, PROCEED WITH THE ATTACH
         NI    8(R15),B'11111101'  NO, CLEAR 'TASKLIB PRESENT' SIGNAL
SVC42    SVC   42                  ATTACH MY LOADER
         SPACE 1
         LTR   R15,R15             AOK?
         #DIE  NZ                  NO, LOGIC ERROR
         ST    R1,DSATCB           YES, SAVE A(SUBTASK TCB)
         SPACE 1
         WAIT  ECB=TERMECB         JUST WAIT FOR TERMINATION
         SPACE 3
*************************************************************
*        THE SUB-COMMAND/PROGRAM HAS ENDED. DETACH THE      *
*        SUBTASK.                                           *
*************************************************************
         SPACE 1
         MVC   DSACC,TERMECB+2     SAVE TERMINATION CC
         DETACH DSATCB             DETACH THE SUBTASK
         LTR   R15,R15             AOK?
         #DIE  NZ                  NO, LOGIC ERROR
         SPACE 3
*************************************************************
*        CLOSE TASKLIB FILE                                 *
*************************************************************
         SPACE 1
         ICM   R0,7,TSKOPEN+1      TASKLIB EXIST?
         BZ    NTASKLIB            NO, SKIP
         CLOSE MF=(E,TSKOPEN)      YES, CLOSE IT
NTASKLIB DS    0H
         SPACE 3
*************************************************************
*        RETURN TO THE CALLER.                              *
*************************************************************
         SPACE 1
RETURN   LH    R15,DSACC           LOAD THE COMPLETION CODE
         #EXIT ((R14,R12)),RC=(R15) RETURN TO CALLER
         SPACE 3
*************************************************************
*        ERROR HANDLER. DISPLAY A MESSAGE, THEN EXIT.       *
*************************************************************
         SPACE 1
ERROR    LR    R1,R2               --> ERROR MESSAGE
         BAL   R14,PUTMSG1         DISPLAY IT
         MVI   DSACC+1,16          SET COMPLETION CODE
         B     RETURN              GO EXIT
         TITLE 'PUTMSG1 -- MESSAGE DISPLAY ROUTINE'
*************************************************************
*                                                           *
*        PUTMSG1 -- THIS ROUTINE DISPLAYS A MESSAGE EITHER  *
*        TO THE OPERATOR (IF BATCH) OR TO TSO. IF           *
*        APPRIPRIATE, THEN MESSAGE ID'S ARE STRIPPED OFF.   *
*                                                           *
*        INPUTS:                                            *
*              - R1 POINTS TO A MESSAGE BLOCK.              *
*              - R14 POINTS TO A RETURN ADDRESS.            *
*                                                           *
*        RETURN 0(,R14):                                    *
*              - THE MESSAGE IS SENT.                       *
*              - ALL REGISTERS ARE RESTORED.                *
*                                                           *
*************************************************************
         SPACE 1
PUTMSG1  STM   R14,R1,SAVEPUM1     SAVE REGISTERS
         SPACE 1
         SR    R15,R15             CLEAR FOR INSERT
         ICM   R15,1,0(R1)         GET L'MSG; NULL?
         BZ    PUTM1RET            YES, RETURN DIRECTLY TO CALLER
         SPACE 1
         TM    DSATFLAG,BATCH      NO, IN THE BATCH?
         BZ    PUTM1TPU            NO, GO ISSUE TPUT
         SPACE 3
*************************************************************
*        THIS IS THE BATCH. ISSUE A WTO.                    *
*************************************************************
         SPACE 1
         MVC   WTOTEXT(*-*),1(R1)  (EXECUTED)
         EX    R15,*-6             COPY TO WTO BUFFER. (1 GARBAGE
*                                  BYTE IS INCLUDED).
         MVC   WTOHEAD,MWTOHEAD    SET WTO HEADER FLAGS
         LA    R15,L'WTOHEAD(,R15) GET WTO MSG BLOCK LENGTH
         STH   R15,WTOHEAD         STORE
         LA    R15,WTOHEAD(R15)    --> MSG TEXT
         MVC   0(L'WTOTAIL,R15),MWTOTAIL COPY WTO TAIL STUFF
         SPACE 1
         WTO   MF=(E,WTO)          SEND THE MESSAGE
         B     PUTM1RET            CO RETURN TO CALLER
         SPACE 3
*************************************************************
*        THIS IS TSO. ISSUE A TPUT. FIRST, IF REQUESTED,    *
*        THEN STRIP OFF THE MESSAGE ID.                     *
*************************************************************
         SPACE 1
PUTM1TPU LA    R1,1(,R1)           --> MSG
         SPACE 1
         TM    DSATFLAG,NOMSGID    SUPPRESS MESSAGE ID?
         BZ    PUTM1MID            NO, SKIP
PUTM1MIL TM    0(R1),255-C' '      YES, BLANK REACHED YET?
         BZ    PUTM1MID            YES, MSGID STRIPPED
         LA    R1,1(,R1)           NO, ADVANCE SCANNER
         BCT   R15,PUTM1MIL        LOOP FOR NEXT CHARACTER
         L     R1,SAVEPUM1+12      NO BLANKS; RESTORE MSG PTR
         IC    R15,0(,R1)          RESTORE L'MSG
         LA    R1,1(,R1)           --> TEXT
PUTM1MID TM    0(R1),255-C' '      BLANK HERE?
         BNZ   PUTM1PUT            NO, GO ISSUE TPUT
         LA    R1,1(,R1)           YES, ADVANCE PAST IT
         BCT   R15,PUTM1PUT        DECR L'MSG
         B     PUTM1RET            NULL MSG; FORGET IT
         SPACE 3
*************************************************************
*        TPUT THE MESSAGE TO THE USER.                      *
*************************************************************
         SPACE 1
PUTM1PUT TPUT  (1),(15),R          ISSUE TPUT
         SPACE 3
*************************************************************
*        RETURN TO CALLER                                   *
*************************************************************
         SPACE 1
PUTM1RET LM    R14,R1,SAVEPUM1     RESTORE REGISTERS
         BR    R14                 RETURN TO CALLER
         TITLE 'DATA'
*************************************************************
*        DATA AREA                                          *
*************************************************************
         SPACE 1
         DROP  ,                   RELEASE ALL BASES
         SPACE 3
MDLSAM   DS    0F
         PRINT NOGEN
         DCB   DDNAME=*-*,DSORG=PO,MACRF=R
         PRINT ON,GEN,NODATA
         SPACE 1
         OPEN  *-*,MF=L
MDLSAML  EQU   *-MDLSAM
         #TEST SIZE=(MDLSAML,EQ,TSKSAML)
         SPACE 3
MDLTEXT  DS    0H
MDLDDNAM DC    Y(DINDDNAM,1,8)
MDLTEXTL EQU   *-MDLTEXT
         #TEST SIZE=(MDLTEXTL,EQ,DALTEXTL)
         SPACE 3
MDLWTO   WTO   '*',ROUTCDE=11,DESC=7,MF=L
MWTOHEAD EQU   MDLWTO,L'WTOHEAD
MWTOTEXT EQU   MWTOHEAD+L'MWTOHEAD,1
MWTOTAIL EQU   MWTOTEXT+L'MWTOTEXT,L'WTOTAIL
         SPACE 3
         LTORG ,
         SPACE 3
         DC    AL1(L'CNSE)
CNSE     DC    C' INVALID SYNTAX FOR THE NAME OF THE PROGRAM OR COMMAND*
                TO BE EXECUTED'
         SPACE 1
         DC    AL1(L'BADDDN)
BADDDN   DC    C' INVALID SYNTAX FOR THE TASKLIB DDNAME'
         SPACE 1
         DC    AL1(L'NOTLIB)
NOTLIB   DC    C' REQUESTED TASK-LIBRARY DDNAME NOT FOUND'
         SPACE 1
         DC    AL1(L'IECNS)
IECNS    DC    C' IMPLICIT EXEC COMMAND NOT SUPPORTED'
         SPACE 1
         DC    AL1(L'NIA)
NIA      DC    C'IKJ56760I NO INFORMATION AVAILABLE'
         SPACE 1
         DC    AL1(L'CANTCMD)
CANTCMD  DC    C' "COMMAND" NOT SUPPORTED IN THE BATCH - USE "PROGRAM"'
         SPACE 1
         DC    AL1(L'BADPARM)
BADPARM  DC    C' REQUIRED PARM FIELD IS OMITTED OR INVALID'
         SPACE 1
         DC    AL1(L'BADCBUF)
BADCBUF  DC    C' REQUIRED OPERANDS OMITTED OR INVALID'
         SPACE 3
VALID    DC    256AL1(0)           INVALID CHARACTERS
         SPACE 1
         ORG   VALID+X'00'
         DC    AL1(4)              DELIMITER
         SPACE 1
         ORG   VALID+C' '
         DC    AL1(4)              DELIMITER
         SPACE 1
         ORG   VALID+C'$'
         DC    AL1(8)              VALID
         SPACE 1
         ORG   VALID+C','
         DC    AL1(4)              DELIMITER
         SPACE 1
         ORG   VALID+C':'
         DC    AL1(4)              DELIMITER
         SPACE 1
         ORG   VALID+C'#'
         DC    AL1(8)              VALID
         SPACE 1
         ORG   VALID+C'@'
         DC    AL1(8)              VALID
         SPACE 1
         ORG   VALID+C'a'
         DC    (C'i'-C'a'+1)AL1(8) VALID
         SPACE 1
         ORG   VALID+C'j'
         DC    (C'r'-C'j'+1)AL1(8) VALID
         SPACE 1
         ORG   VALID+C's'
         DC    (C'z'-C's'+1)AL1(8) VALID
         SPACE 1
         ORG   VALID+C'A'
         DC    (C'I'-C'A'+1)AL1(8) VALID
         SPACE 1
         ORG   VALID+C'J'
         DC    (C'R'-C'J'+1)AL1(8) VALID
         SPACE 1
         ORG   VALID+C'S'
         DC    (C'Z'-C'S'+1)AL1(8) VALID
         SPACE 1
         ORG   VALID+C'0'
         DC    (C'9'-C'0'+1)AL1(8) VALID
         SPACE 1
         ORG   ,
         TITLE 'COMMAND PROCESSOR LOADER'
*************************************************************
*        ENTRY LINKAGE                                      *
*************************************************************
         SPACE 1
         USING DSATDATA,R1         DCL TEMP DSATDATA BASE
CMDLOADR #ENTER SAVTYPE=(REMOTE,DSASAVE2) ENTRY LINKAGE
         LR    DSATREG,R1          COPY DSATDATA POINTER
         DROP  R1                  RELEASE TEMP BASE
         USING DSATDATA,DSATREG    DCL LOCAL DSATDATA BASE
         SPACE 3
*************************************************************
*        ATTEMPT TO LOAD THE INDICATED COMMAND PROCESSOR OR *
*        PROGRAM.                                           *
*************************************************************
         SPACE 1
         LOAD  EPLOC=CMDNAME,ERRET=CMDNFOUN LOAD THE COMMAND
         ST    R0,DSACMD           SAVE ITS ADDRESS
         SPACE 3
*************************************************************
*        ADJUST THE ECT                                     *
*************************************************************
         SPACE 1
         TM    DSATFLAG,BATCH      RUNNING IN THE BATCH?
         BNZ   YBCH3               YES, SKIP
         L     R1,DSAPARMA         NO, TSO; --> ORIGINAL CPPL
         L     R1,CPPLECT-CPPL(,R1) --> ECT
         USING ECT,R1              DCL ECT BASE
         MVC   ECTPCMD,CMDNAME     INSERT TRUE COMMAND NAME
         NI    ECTSWS,255-ECTNOPD ASSUME GOT OPERANDS
         ICM   R0,3,PARMDATL       RIGHT?
         BNZ   GOTOPNDS            YES, PROCEED
         OI    ECTSWS,ECTNOPD      NO, SIGNAL NO OPERANDS
GOTOPNDS DS    0H
         DROP  R1                  RELEASE ECT BASE
YBCH3    DS    0H
         SPACE 3
*************************************************************
*        IF THIS IS A COMMAND, THEN ADJUST THE CBUF TO      *
*        POINT TO THAT PART OF THE OPERANDS INTENDED FOR    *
*        THE COMMAND.                                       *
*************************************************************
         SPACE 1
         TM    DSATFLAG,CALLED     PROGRAM?
         BNZ   PARMF               YES, GO BUILD A PARM FIELD
         L     R2,DSAPARMA         NO, COMMAND; --> CPPL
         ST    R2,PARAMETR         STORE FOR LATER
         L     R2,CPPLCBUF-CPPL(,R2) --> CBUF
         L     R1,PARMDATA         --> OPERANDS IN THE CBUF
         LA    R0,4(,R2)           GET "BASE"
         SR    R1,R0               GET OPERANDS OFFSET
         STH   R1,2(,R2)           ADJUST THE CBUF
         B     NOPARMF             DONE HERE
         SPACE 3
*************************************************************
*        THIS IS A PROGRAM. CONSTRUCT A PARM FIELD.         *
*************************************************************
         SPACE 1
PARMF    L     R2,PARMDATA         --> PARM DATA
         LH    R3,PARMDATL         GET L'PARM DATA
         LA    R0,6(,R3)           GET L'PARM BLOCK TO BE
         CH    R0,=H'106'          UP TO MINIMUM SIZE?
         BNL   PARMSZOK            YES, PROCEED
         LA    R0,106              NO, LOAD MINIMUM SIZE
PARMSZOK ICM   R0,8,=AL1(1)        SET SUBPOOL=1
         GETMAIN R,LV=(0)          GET MEMORY
         ST    R1,PARAMETR         SAVE
         LA    R0,4(,R1)           --> PARM FIELD
         ST    R0,0(,R1)           BUILD PLIST
         OI    0(R1),B'10000000'   SET EOL FLAG
         STH   R3,4(,R1)           SET L'PARMS
         XC    6(100,R1),6(R1)     INSURE POSSIBLE EXCESS AREA CLEARED
         LTR   R3,R3               NULL PARMS?
         BNP   NOPARMF             YES, DONE HERE
         LA    R0,1                SET ' SWITCH
COPYLP   MVC   6(1,R1),0(R2)       COPY CHARACTER
         LTR   R0,R0               UPCASE IT?
         BM    NOUPCASE            NO, SKIP
         OI    6(R1),C' '          YES, DO SO
NOUPCASE CLI   6(R1),C''''         '?
         BNE   NOQUOTE             NO, SKIP
         LCR   R0,R0               YES, ALTER THE SWITCH
NOQUOTE  LA    R1,1(,R1)           ADVANCE SINK SCANNER
         LA    R2,1(,R2)           ADVANCE SOURCE SCANNER
         BCT   R3,COPYLP           CONTINUE
NOPARMF  DS    0H
         SPACE 3
*************************************************************
*        RESTORE THE ENTRY ENVIRONMENT, THEN BRANCH TO THE  *
*        COMMAND OR PROGRAM.                                *
*************************************************************
         SPACE 1
         #EXIT ((R14,R12))         RESTORE REGISTERS
         ORG   *-2                 DON'T BR R14
         PUSH  USING               SAVE USING STATUS
         DROP  ,                   RELEASE ALL BASES
         USING DSATDATA,R1         DCL TEMP DSATDATA BASE
         L     R15,DSACMD          --> PROGRAM'S ENTRY POINT
         L     R1,PARAMETR         --> PARM FIELD OR CPPL
         DROP  R1                  RELEASE DSATDATA BASE
         BR    R15                 GO TO CMD/PGM; NO RETURN
         POP   USING               RESTORE USING STATUS
         SPACE 3
*************************************************************
*        COMMAND NOT FOUND                                  *
*************************************************************
         SPACE 1
CMDNFOUN MVC   WTOTEXT(18),=C'IKJ56500I COMMAND ' MSG TEXT
         MVC   WTOTEXT+18(L'CMDNAME),CMDNAME COMMAND NAME
         LA    R1,WTOTEXT+L'CMDNAME+18 PREPARE TO BACKSCAN BLANKS
BLP1     BCTR  R1,0                BACK SCAN
         TM    0(R1),255-C' '      STILL BLANK?
         BZ    BLP1                YES, KEEP LOOPING
         MVC   1(10,R1),=C' NOT FOUND' MORE TEXT
         LA    R0,WTOTEXT-11       --> SO-MSG
         SR    R1,R0               GET L'MSG
         STC   R1,WTOTEXT-1        STORE
         LA    R1,WTOTEXT-1        --> MSG
         BAL   R14,PUTMSG2         DISPLAY IT
CERREXIT #EXIT ((R14,R12)),RC=16   RETURN WITH ERROR SIGNAL
         TITLE 'PUTMSG2 -- MESSAGE DISPLAY ROUTINE'
*************************************************************
*                                                           *
*        PUTMSG2 -- THIS ROUTINE DISPLAYS A MESSAGE EITHER  *
*        TO THE OPERATOR (IF BATCH) OR TO TSO. IF           *
*        APPRIPRIATE, THEN MESSAGE ID'S ARE STRIPPED OFF.   *
*                                                           *
*        INPUTS:                                            *
*              - R1 POINTS TO A MESSAGE BLOCK.              *
*              - R14 POINTS TO A RETURN ADDRESS.            *
*                                                           *
*        RETURN 0(,R14):                                    *
*              - THE MESSAGE IS SENT.                       *
*              - ALL REGISTERS ARE RESTORED.                *
*                                                           *
*************************************************************
         SPACE 1
PUTMSG2  STM   R14,R1,SAVEPUM2     SAVE REGISTERS
         SR    R15,R15             CLEAR FOR INSERT
         ICM   R15,1,0(R1)         GET L'MSG; NULL?
         BZ    PUTM2RET            YES, RETURN DIRECTLY TO CALLER
         SPACE 1
         TM    DSATFLAG,BATCH      NO, IN THE BATCH?
         BZ    PUTM2TPU            NO, GO ISSUE TPUT
         SPACE 3
*************************************************************
*        THIS IS THE BATCH. ISSUE A WTO.                    *
*************************************************************
         SPACE 1
         MVC   WTOTEXT(*-*),1(R1)  (EXECUTED)
         EX    R15,*-6             COPY TO WTO BUFFER. (1 GARBAGE
*                                  BYTE IS INCLUDED).
         L     R14,=A(MDLWTO)      --> MODEL WTO
         USING MDLWTO,R14          DCL A BASE FOR IT
         MVC   WTOHEAD,MWTOHEAD    SET WTO HEADER FLAGS
         LA    R15,L'WTOHEAD(,R15) GET WTO MSG BLOCK LENGTH
         STH   R15,WTOHEAD         STORE
         LA    R15,WTOHEAD(R15)    --> MSG TEXT
         MVC   0(L'WTOTAIL,R15),MWTOTAIL COPY WTO TAIL STUFF
         DROP  R14                 RELEASE MODEL WTO BASE
         SPACE 1
         WTO   MF=(E,WTO)          SEND THE MESSAGE
         B     PUTM2RET            CO RETURN TO CALLER
         SPACE 3
*************************************************************
*        THIS IS TSO. ISSUE A TPUT. FIRST, IF REQUESTED,    *
*        THEN STRIP OFF THE MESSAGE ID.                     *
*************************************************************
         SPACE 1
PUTM2TPU LA    R1,1(,R1)           --> MSG
         SPACE 1
         TM    DSATFLAG,NOMSGID    SUPPRESS MESSAGE ID?
         BZ    PUTM2MID            NO, SKIP
PUTM2MIL TM    0(R1),255-C' '      YES, BLANK REACHED YET?
         BZ    PUTM2MID            YES, MSGID STRIPPED
         LA    R1,1(,R1)           NO, ADVANCE SCANNER
         BCT   R15,PUTM2MIL        LOOP FOR NEXT CHARACTER
         L     R1,SAVEPUM2+12      NO BLANKS; RESTORE MSG PTR
         IC    R15,0(,R1)          RESTORE L'MSG
         LA    R1,1(,R1)           --> TEXT
PUTM2MID TM    0(R1),255-C' '      BLANK HERE?
         BNZ   PUTM2PUT            NO, GO ISSUE TPUT
         LA    R1,1(,R1)           YES, ADVANCE PAST IT
         BCT   R15,PUTM2PUT        DECR L'MSG
         B     PUTM2RET            NULL MSG; FORGET IT
         SPACE 3
*************************************************************
*        TPUT THE MESSAGE TO THE USER.                      *
*************************************************************
         SPACE 1
PUTM2PUT TPUT  (1),(15),R          ISSUE TPUT
         SPACE 3
*************************************************************
*        RETURN TO CALLER                                   *
*************************************************************
         SPACE 1
PUTM2RET LM    R14,R1,SAVEPUM2     RESTORE REGISTERS
         BR    R14                 RETURN TO CALLER
         TITLE 'DATA'
         DROP  ,                   CLEAR USINGS
         SPACE 3
         LTORG ,
         SPACE 3
         END   ,
