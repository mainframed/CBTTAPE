//TSGMXZ1G JOB (AD00),'#04D42 ZELDEN',
//             NOTIFY=&SYSUID,
//             CLASS=M,MSGCLASS=H,MSGLEVEL=(1,1)
//*
//ASMHCLG PROC
//C       EXEC PGM=IEV90,
//             PARM=(NODECK,OBJECT)
//SYSLIB   DD  DSN=SYS1.MACLIB,
//             DISP=SHR
//SYSUT1   DD  DSN=&SYSUT1,SPACE=(1024,(120,120),,,ROUND),UNIT=VIO,
//             DCB=BUFNO=1
//SYSPUNCH DD  SYSOUT=B
//SYSPRINT DD  SYSOUT=*
//SYSLIN   DD  DSN=&OBJ,SPACE=(3040,(40,40),,,ROUND),UNIT=VIO,
//             DISP=(MOD,PASS),
//             DCB=(BLKSIZE=3040,LRECL=80,RECFM=FBS,BUFNO=1)
//L       EXEC PGM=IEWL,COND=(5,LT,C),
//             PARM='LIST,LET,XREF,MAP'
//SYSLIN   DD  DSN=&OBJ,
//             DISP=(OLD,DELETE)
//         DD  DDNAME=SYSIN
//SYSLMOD  DD  SPACE=(CYL,(1,1,1)),DSN=&LOD(X),
//             DISP=(,PASS),
//             UNIT=SYSDA,DCB=BUFNO=1
//SYSUT1   DD  DSN=&SYSUT1,SPACE=(1024,(120,120),,,ROUND),UNIT=VIO,
//             DCB=BUFNO=1
//SYSPRINT DD  SYSOUT=*
//G       EXEC PGM=*.L.SYSLMOD,COND=((5,LT,L),(5,LT,C))
//        PEND
//*
//ASMHCLG  EXEC ASMHCLG,PARM.L='XREF,LIST',PARM.G=58
//C.SYSLIB   DD DSN=SYS1.MACLIB,DISP=SHR
//           DD DSN=SYS1.MODGEN,DISP=SHR
//C.SYSIN    DD *
         TITLE 'ASIDLIST - LIST ASIDS IN THE SYSTEM'
         PRINT NOGEN
***********************************************************************
* THIS PROGRAM WILL WRITE OUT THE NAMES OF ALL ADDRESS SPACE          *
* IDS IN THE SYSTEM, THEIR ASCB ADDRESS, THEIR ASID NUMBER IN         *
* HEX AND IN DECIMAL AS SHOWN IN THE SAMPLE BELOW:                    *
*                                                                     *
*   ASCB FOUND AT 00FD4100: *MASTER* - ASID X'0001' (   1 IN DECIMAL) *
*   ASCB FOUND AT 00F4BE80: PCAUTH   - ASID X'0002' (   2 IN DECIMAL) *
*   ASCB FOUND AT 00F4D700: RASP     - ASID X'0003' (   3 IN DECIMAL) *
*   ASCB FOUND AT *NONREUS: *NONREUS - ASID X'000E' (  14 IN DECIMAL) *
*   ASCB FOUND AT *AVALABL: *AVALABL - ASID X'0037' (  55 IN DECIMAL) *
*   ASCB FOUND AT *AVALABL: *AVALABL - ASID X'NNNN' (NNNN IN DECIMAL) *
*                                                                     *
* THIS PROGRAM ALSO DISPLAYS TOTALS AS SHOWN BELOW:                   *
*                                                                     *
*       TOTAL ADDRESS SPACES IN THE SYSTEM:               NNNN        *
*       TOTAL ACTIVE ADDRESS SPACES IN THE SYSTEM:        NNNN        *
*       TOTAL AVAILABLE ADDRESS SPACES IN THE SYSTEM:     NNNN        *
*       TOTAL NON-REUSABLE ADDRESS SPACES IN THE SYSTEM:  NNNN        *
*                                                                     *
*                                                                     *
*               ASID USAGE FROM ASVT                                  *
*                                                                     *
*       MAXUSER FROM IEASYSXX:  NNNN                                  *
*                IN USE ASIDS:  NNNN                                  *
*             AVAILABLE ASIDS:  NNNN                                  *
*                                                                     *
*       RSVSTRT FROM IEASYSXX:  NNNN                                  *
*              RSVSTRT IN USE:  NNNN                                  *
*           RSVSTRT AVAILABLE:  NNNN                                  *
*                                                                     *
*       RSVNONR FROM IEASYSXX:  NNNN                                  *
*              RSVNONR IN USE:  NNNN                                  *
*           RSVNONR AVAILABLE:  NNNN                                  *
*                                                                     *
*       NON-REUSABLE ASIDS   :  NNNN                                  *
*                                                                     *
*                                                                     *
* THE DEFAULT OUTPUT IS 55 LINES PER PAGE. AN OPTIONAL 2 DIGIT INPUT  *
* PARAMETER MAY BE SPECIFIED TO CHANGE THE DEFAULT LINES PER PAGE.    *
***********************************************************************
*** SAMPLE JCL:
***
*** //ASIDLIST JOB (ACCT),'COUNT JOBS',CLASS=M
*** //STEP1 EXEC PGM=ASIDLIST,PARM=58
*** //SYSPRINT  DD  SYSOUT=*
*** //
***********************************************************************
* UPDATES:
* 04/04/2016 MSZ  ADDED AMODE 31 / RMODE 24 ASSEMBLER DIRECTIVES TO
*                 TO SUPPORT ASVT IN 31-BIT STORAGE IN Z/OS 2.1
*                 AND ABOVE VIA DIAGXX CBLOC
***********************************************************************
***
*
*  REGISTER EQUATES AND USAGE
*
R00      EQU   0                  LINKAGE REGISTER
R01      EQU   1                  INITIAL POINTER TO INPUT PARM
R02      EQU   2                  WORK REG
R03      EQU   3                  POINTS TO PARM / MAX # ASVT ENTRIES
R04      EQU   4                  WORK - POINTS TO CURRENT ADDR IN ASVT
R05      EQU   5                  WORK REG
R06      EQU   6                  WORK REG - USED FOR BAL TO PRINT RTNS
R07      EQU   7                  POINTS TO START OF ASVTENTY
R08      EQU   8                  ASID COUNTER
R09      EQU   9                  BASE REG FOR ASVT
R10      EQU   10                 BASE REG FOR ASCB
R11      EQU   11                 2ND BASE REG
R12      EQU   12                 BASE REGISTER
R14      EQU   14                 LINKAGE REGISTER (RETURN ADDRESS)
R15      EQU   15                 LINKAGE REGISTER (ENTRY POINT)
*
ASIDLIST CSECT
ASIDLIST AMODE 31                 ADDED FOR IHAASVT IN 31-BIT MODE
ASIDLIST RMODE 24                 ADDED FOR IHAASVT IN 31-BIT MODE
         B     START-ASIDLIST(R15)
         DC    AL1(START-*)
         DC    C'ASIDLIST &SYSDATE &SYSTIME '
         DC    C'*** AUTHOR: MARK ZELDEN ***'
START    BAKR  R14,R00            BRANCH AND STACK (LINKAGE STACK)
         LR    R12,R15            SET UP ADDRESSABILITY
         LA    R11,2048(R12)      SET UP ADDRESSABILITY TO 2ND
         LA    R11,2048(R11)        BASE REGISTER
         USING ASIDLIST,R12,R11   SET UP BASE REGISTERS
* ====================================================================
*******************************************************************
* PROCESS INPUT PARM (IF THERE IS ONE)
*******************************************************************
         L     R03,0(R01)         POINT TO INPUT PARM
         CLC   0(2,R03),=H'0'     IS LENGTH=0 (NO PARM) ?
         BNE   USEPARM            NO, BRANCH AND USE PARM VALUE
         MVC   MAXLINES,=PL2'55'  YES, MOVE DEFAULT MAX LINES PER PAGE
         B     OPEN               BRANCH
USEPARM  CLC   0(2,R03),=H'2'     IS LENGTH=2  ?
         BNE   BADPARM            NO, BRANCH
         MVC   PARMLINE,2(R03)    MOVE PARM VALUE
         PACK  MAXLINES,PARMLINE  PACK IT
*******************************************************************
* OPEN SYSPRINT AND INITIALIZE COUNTERS
*******************************************************************
OPEN     OPEN  (SYSPRINT,(OUTPUT)) OPEN SYSPRINT FILE
         ZAP   TOTASIDS,=P'0'     ZERO OUT TOTAL ASID COUNTER
         ZAP   TOTACTIV,=P'0'     ZERO OUT TOTAL ACTIVE ASID COUNTER
         ZAP   TOTAVAL,=P'0'      ZERO OUT TOTAL AVAILABLE ASID COUNTER
         ZAP   TOTNONR,=P'0'      ZERO OUT TOTAL NON-REUSABLE COUNTER
         ZAP   LINCOUNT,=P'0'     ZERO CURRENT LINE # ON PAGE COUNTER
         ZAP   PAGCOUNT,=P'1'     INITIALIZE PAGE COUNTER
         LA    R08,1              INITIALIZE ASID COUNTER TO 1
         BAL   R06,PUTTITLE       BRANCH TO PRINT TITLE SUB-ROUTINE
*******************************************************************
* POINT TO ASVT
*******************************************************************
         L     R09,CVTPTR         POINT TO CVT  - X'10'
         USING CVT,R09            MAP CVT
         L     R09,CVTASVT        POINT TO ASVT
         DROP  R09                TELL ASMBLR TO STOP USING R09 FOR CVT
         USING ASVT,R09           MAP ASVT
         LA    R04,ASVTENTY       POINT TO FIRST ENTRY IN TABLE
         LR    R07,R04            SAVE ADDRESS IN R7 FOR LATER
         L     R03,ASVTMAXU       LOAD MAX NUMBER OF ENTRIES
ASVTLOOP DS    0H
*******************************************************************
* IS A NEW TOP OF FORM IS NEEDED ?
*******************************************************************
TITLECHK DS    0H
         CP    LINCOUNT,MAXLINES  DO WE NEED A NEW PAGE?
         BL    SAMEPAGE           NO, BRANCH
         BAL   R06,PUTTITLE       BRANCH TO PRINT TITLE SUB-ROUTINE
SAMEPAGE DS    0H
*******************************************************************
* THIS ROUTINE CHECKS EACH ASVT ENTRY.
* IF THE HIGH ORDER BIT IS ON, THE ENTRY IS THE ADDRESS OF THE
* NEXT AVAILABLE ASID (OR THE LAST ENTRY IF ZEROS).
* IF THE HIGH ORDER BIT IS NOT ON, THE ENTRY IS THE ADDRESS
* OF THE ASCB FOR THAT ENTRY. IT THE HIGH ORDER BIT IS ON AND
* THE ENTRY CONTAINS THE ADDRESS OF MASTER'S ASCB, THEN THE ASID
* IS NON-REUSABLE.
*******************************************************************
         TM    0(R04),ASVTAVAL    IS THIS AN ASSIGNED  ASCB
         BNO   CHKASCB            YES, BRANCH
         L     R05,0(R04)         SAVE ADDRESS
         SL    R05,=X'80000000'    ZERO OUT HIGH ORDER BIT
         CR    R05,R07            IS THIS A NON-REUSABLE ASID?
         BNE   AVALAS             MUST BE AVAILABLE, BRANCH
         MVC   JOBNAME,=C'*NONREUS' MOVE 'NONREUS' INTO JOBNAME
         AP    TOTNONR,=P'1'      ADD 1 TO TOTAL NON-REUSABLE COUNTER
         BAL   R06,PUTPRTLN       BRANCH TO PRINT SUB-ROUTINE
         LA    R04,4(,R04)        NO, POINT TO NEXT ENTRY IN ASVT
         BCT   R03,ASVTLOOP       GO CHECK NEXT ASVT ENTRY
         B     TOTALS             NO MORE ENTRIES - BRANCH
AVALAS   DS    0H
         MVC   JOBNAME,=C'*AVALABL' MOVE 'AVAILABLE' INTO JOBNAME
         AP    TOTAVAL,=P'1'      ADD 1 TO TOTAL AVAILABLE COUNTER
         BAL   R06,PUTPRTLN       BRANCH TO PRINT SUB-ROUTINE
         LA    R04,4(,R04)        NO, POINT TO NEXT ENTRY IN ASVT
         BCT   R03,ASVTLOOP       GO CHECK NEXT ASVT ENTRY
         B     TOTALS             NO MORE ENTRIES - BRANCH
*******************************************************************
* CHECK ASCB FOR JOB OR START/LOGON/MOUNT
*******************************************************************
CHKASCB  L     R10,0(R04)         POINT TO ASCB
         USING ASCB,R10           MAP IT
         L     R05,ASCBJBNI       POINT TO JOBNAME
         C     R05,=F'0'          WAS THIS A START/MOUNT/LOGON ?
         BE    NOTAJOB            YES, BRANCH
         MVC   JOBNAME,0(R05)     MOVE JOBNAME INTO MSG
         AP    TOTACTIV,=P'1'     ADD 1 TO TOTAL ACTIVE COUNTER
         BAL   R06,PUTPRTLN       BRANCH TO PRINT SUB-ROUTINE
         LA    R04,4(,R04)        POINT TO NEXT ENTRY IN ASVT
         BCT   R03,ASVTLOOP       GO CHECK NEXT ASVT ENTRY
         B     TOTALS             NO MORE ENTRIES - BRANCH
NOTAJOB  DS    0H
         L     R05,ASCBJBNS       POINT TO START/MOUNT/LOGON NAME
         C     R05,=F'0'          NAME PRESENT ?
         BNE   MOVESNAM           YES, BRANCH
         MVC   JOBNAME,=C'*STRTING' MOVE 'STRTING' INTO JOBNAME
         B     SKIPSNAM
MOVESNAM MVC   JOBNAME,0(R05)     MOVE JOBNAME INTO MSG
SKIPSNAM AP    TOTACTIV,=P'1'     ADD 1 TO TOTAL ACTIVE COUNTER
         BAL   R06,PUTPRTLN       BRANCH TO PRINT SUB-ROUTINE
         LA    R04,4(,R04)        POINT TO NEXT ENTRY IN ASVT
         BCT   R03,ASVTLOOP       GO CHECK NEXT ASVT ENTRY
         B     TOTALS             NO MORE ENTRIES - BRANCH
*******************************************************************
* SUB ROUTINE TO PRINT TITLE LINES ON TOP OF PAGE
*******************************************************************
PUTTITLE MVC   EDPAGNUM,EDMASK    MOVE EDIT WORD TO OUTPUT
         ED    EDPAGNUM,PAGCOUNT  MAKE PAGE NUMBER COUNT PRINTABLE
         MVC   PAGENUM(2),EDPAGNUM+2
         PUT   SYSPRINT,TITLELN1  WRITE
         PUT   SYSPRINT,TITLELN2    TITLE
         PUT   SYSPRINT,BLANKLIN  WRITE BLANK LINE
         AP    PAGCOUNT,=P'1'     ADD 1 TO PAGE NUMBER COUNTER
         ZAP   LINCOUNT,=P'2'     INITITALIZE LINE NUMBER COUNTER TO 2
         BR    R06                RETURN
*******************************************************************
* SUB ROUTINE TO WRITE A PRINT LINE
*******************************************************************
PUTPRTLN LR    R05,R08            LOAD ASID NUMBER
         CVD   R05,CVDWORK        CONVERT TO DECIMAL
         MVC   EDASID,EDMASK2     MOVE EDIT MASK
         ED    EDASID,CVDWORK+5   UNPACK AND EDIT
         MVI   ASIDHEX+6,C'('     MOVE "(" TO PRINTOUT
         STCM  R08,B'0011',WORK3  STORE "HEX" ASID
         UNPK  WORK5,WORK3        ADD ZONES
         TR    WORK5(4),HEXTAB-C'0'   TRANSLATE TO CHARACTERS
         MVC   ASIDHEX(4),WORK5   MOVE HEX ASID
*====================================================
         CLC   JOBNAME,=C'*NONREUS' IS IT A NON-REUSABLE ASID?
         BNE   CHKAVAL
         MVC   ASCBADDR,=C'*NONREUS'
         B     SKIPCVRT
CHKAVAL  CLC   JOBNAME,=C'*AVALABL' IS IT AN AVAILABLE ASID
         BNE   CVRTADDR
         MVC   ASCBADDR,=C'*AVALABL'
         B     SKIPCVRT
CVRTADDR ST    R10,WORK4          STORE ASCB ADDR X'ABCDEFAB'
         MVC   WORK5(4),WORK4     ABCDEFAB00
         UNPK  WORK9,WORK5        FAFBFCFDFEFFFAFB00
         TR    WORK9(8),HEXTAB-C'0'   C1C2C3C4C5C6C1C200
         MVC   ASCBADDR(8),WORK9  C'ABCDEFAB'
SKIPCVRT PUT   SYSPRINT,ACTMSG    WRITE JOB ACTIVE MSG
         MVC   JOBNAME,=CL8' '    CLEAR JOBNAME
         AP    TOTASIDS,=P'1'     ADD 1 TO TOTAL COUNTER
         AP    LINCOUNT,=P'1'     ADD 1 TO LINE NUMBER COUNTER
         AH    R08,=X'0001'       ADD 1 TO ASID NUMBER
         BR    R06                RETURN
*******************************************************************
* PUT OUT TOTAL MESSAGES
*******************************************************************
TOTALS   DS    0H
         L     R05,ASVTMAXI       MAXUSERS FROM ASVT
         CVD   R05,CVDWORK        CONVERT TO DECIMAL
         MVC   EDTOTMX,EDMASK2    MOVE EDIT MASK
         ED    EDTOTMX,CVDWORK+5  UNPACK AND EDIT
         L     R06,ASVTAAV        AVAILABLE FROM ASVT
         CVD   R06,CVDWORK        CONVERT TO DECIMAL
         MVC   EDTOTAVA,EDMASK2    MOVE EDIT MASK
         ED    EDTOTAVA,CVDWORK+5  UNPACK AND EDIT
         SLR   R05,R06            CALCULATED IN USE ASIDS
         CVD   R05,CVDWORK        CONVERT TO DECIMAL
         MVC   EDTOTAVI,EDMASK2    MOVE EDIT MASK
         ED    EDTOTAVI,CVDWORK+5  UNPACK AND EDIT
         L     R05,ASVTSTRT       START/SASI FROM ASVT (RSVSTRT)
         CVD   R05,CVDWORK        CONVERT TO DECIMAL
         MVC   EDTOTST,EDMASK2    MOVE EDIT MASK
         ED    EDTOTST,CVDWORK+5  UNPACK AND EDIT
         L     R06,ASVTAST        AVAILABLE START/SASI FROM ASVT
         CVD   R06,CVDWORK        CONVERT TO DECIMAL
         MVC   EDTOTSTA,EDMASK2   MOVE EDIT MASK
         ED    EDTOTSTA,CVDWORK+5 UNPACK AND EDIT
         SLR   R05,R06            CALCULATED IN USE SASI
         CVD   R05,CVDWORK        CONVERT TO DECIMAL
         MVC   EDTOTSTI,EDMASK2   MOVE EDIT MASK
         ED    EDTOTSTI,CVDWORK+5 UNPACK AND EDIT
         L     R05,ASVTNONR       NON-RESUSABLE FROM ASVT (RSVNONR)
         CVD   R05,CVDWORK        CONVERT TO DECIMAL
         MVC   EDTOTNR,EDMASK2    MOVE EDIT MASK
         ED    EDTOTNR,CVDWORK+5  UNPACK AND EDIT
         L     R06,ASVTANR        AVAILABLE NON-REUSABLE FROM ASVT
         CVD   R06,CVDWORK        CONVERT TO DECIMAL
         MVC   EDTOTNRA,EDMASK2   MOVE EDIT MASK
         ED    EDTOTNRA,CVDWORK+5 UNPACK AND EDIT
         SLR   R05,R06            CALCULATED IN USE NON-REUSABLE
         CVD   R05,CVDWORK        CONVERT TO DECIMAL
         MVC   EDTOTNRI,EDMASK2   MOVE EDIT MASK
         ED    EDTOTNRI,CVDWORK+5 UNPACK AND EDIT
*
         BAL   R06,PUTTITLE       BRANCH TO PRINT TITLE SUB-ROUTINE
         MVC   EDTOTAS,EDMASK2    MOVE EDIT WORD TO OUTPUT
         ED    EDTOTAS,TOTASIDS   MAKE TOTAL COUNT PRINTABLE
         MVC   EDTOTACT,EDMASK2   MOVE EDIT WORD TO OUTPUT
         ED    EDTOTACT,TOTACTIV  MAKE TOTAL COUNT PRINTABLE
         MVC   EDTOTAV,EDMASK2    MOVE EDIT WORD TO OUTPUT
         ED    EDTOTAV,TOTAVAL    MAKE TOTAL COUNT PRINTABLE
         MVC   EDTOTNOR,EDMASK2   MOVE EDIT WORD TO OUTPUT
         ED    EDTOTNOR,TOTNONR   MAKE TOTAL COUNT PRINTABLE
*
         MVC   TOT2NOR,EDTOTNOR   MOVE EDITED NUMBER OR NON-REUSE
*
         PUT   SYSPRINT,TOTALLN1  WRITE TOTAL1 HEADER LINE
         PUT   SYSPRINT,BLANKLIN  WRITE BLANK LINE
         PUT   SYSPRINT,TOTASMG   WRITE TOTAL ASID MSG
         PUT   SYSPRINT,TOTACTMG  WRITE TOTAL ACTIVE ASID MSG
         PUT   SYSPRINT,TOTAVAMG  WRITE TOTAL AVAILABLE ASID MSG
         PUT   SYSPRINT,TOTNORMG  WRITE TOTAL NON-REUSABLE ASID MSG
         PUT   SYSPRINT,BLANKLIN  WRITE BLANK LINE
         PUT   SYSPRINT,BLANKLIN  WRITE BLANK LINE
         PUT   SYSPRINT,TOTALLN2  WRITE TOTAL2 HEADER LINE
         PUT   SYSPRINT,BLANKLIN  WRITE BLANK LINE
         PUT   SYSPRINT,TOTMAXU   WRITE ASVT MAXUSER LINE
         PUT   SYSPRINT,TOTAVI    WRITE ASVT TOTAL IN USE LINE
         PUT   SYSPRINT,TOTAVA    WRITE ASVT TOTAL AVAILABLE LINE
         PUT   SYSPRINT,BLANKLIN  WRITE BLANK LINE
         PUT   SYSPRINT,TOTSASI   WRITE ASVT RSVSTRT LINE
         PUT   SYSPRINT,TOTSASII  WRITE ASVT RSVSTRT IN USE LINE
         PUT   SYSPRINT,TOTSASIA  WRITE ASVT RSVSTRT AVAILABLE LINE
         PUT   SYSPRINT,BLANKLIN  WRITE BLANK LINE
         PUT   SYSPRINT,TOTNR     WRITE ASVT RSVNONR LINE
         PUT   SYSPRINT,TOTNRI    WRITE ASVT RSVNONR IN USE LINE
         PUT   SYSPRINT,TOTNRA    WRITE ASVT RSVNONR AVAILABLE LINE
         PUT   SYSPRINT,BLANKLIN  WRITE BLANK LINE
         PUT   SYSPRINT,TOTNONRU  WRITE ASVT NON-REUSABLE ASID LINE
         B     RETURN             GO END
BADPARM  WTO   'ASIDLIST - NUMBER OF LINES PER PAGE IN PARM MUST BE 2 DX
               IGITS - JOB CANCELLED',ROUTCDE=11
         ABEND 01,REASON=0        U0001 ABEND  - NO DUMP
RETURN   CLOSE (SYSPRINT)         CLOSE FILES
         LA   R15,0               SET RETURN CODE TO ZERO
         PR                       PROGRAM RETURN (LINKAGE STACK)
         EJECT
* ====================================================================
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,RECFM=FBA,            X
               LRECL=133,BLKSIZE=3990
PARMLINE DS    ZL2                PARM VALUE
MAXLINES DS    PL2                MAXIMUM LINES PER PAGE
CVDWORK  DS    D                  WORK AREA FOR CVD
WORK3    DS    CL3                WORK AREA FOR HEX TO CHAR
WORK4    DS    F                  WORK AREA FOR HEX TO CHAR
WORK5    DS    CL5                WORK AREA FOR HEX TO CHAR
WORK9    DS    CL9                WORK AREA FOR HEX TO CHAR
HEXTAB   DC    C'0123456789ABCDEF' TRANSLATION TABLE FOR HEX TO CHAR
TOTASIDS DS    PL3                TOTAL # OF ASIDS
TOTACTIV DS    PL3                TOTAL # OF ACTIVE ASIDS
TOTAVAL  DS    PL3                TOTAL # OF AVAILABLE ASIDS
TOTNONR  DS    PL3                TOTAL # OF NON-REUSABLE ASIDS
LINCOUNT DS    PL2                CURRENT LINE COUNT ON PAGE
PAGCOUNT DS    PL2                CURRENT PAGE NUMBER COUNT
EDPAGNUM DS    CL4                EDITED PAGE NUMBER
BLANKLIN DC    CL133' '
TITLELN1 DC    CL50'1      A D D R E S S    S P A C E    I D    L I S '
         DC    CL20'T            PAGE - '
PAGENUM  DS    CL2                PAGE NUMBER
         DC    CL61' '            FILLER
TITLELN2 DC    CL50'+      ___________________________________________'
         DC    CL50'_                                                 '
         DC    CL33' '            FILLER
TOTALLN1 DC    CL50'                 PROGRAM TOTALS                   '
         DC    CL50'                                                  '
         DC    CL33' '            FILLER
ACTMSG   DC    CL15' ASCB FOUND AT '
ASCBADDR DS    CL8                EDITED ASCB ADDRESS
         DC    CL2': '
JOBNAME  DC    CL8' '
         DC    CL10' - ASID X'''
ASIDHEX  DS    CL4                ASID IN HEX CHAR
         DC    CL1''''
EDASID   DS    CL6                EDITED ASID IN DECIMAL
         DC    CL12' IN DECIMAL)'
         DC    CL67'        '     FILLER
EDMASK   DC    X'40202120'
EDMASK2  DC    X'402020202120'
TOTASMG  DC    CL49' TOTAL ADDRESS SPACES IN THE SYSTEM:             '
EDTOTAS  DS    CL6
         DC    CL78' '            FILLER
TOTACTMG DC    CL49' TOTAL ACTIVE ADDRESS SPACES IN THE SYSTEM:      '
EDTOTACT DS    CL6
         DC    CL78' '            FILLER
TOTAVAMG DC    CL49' TOTAL AVAILABLE ADDRESS SPACES IN THE SYSTEM:   '
EDTOTAV  DS    CL6
         DC    CL78' '            FILLER
TOTNORMG DC    CL49' TOTAL NON-REUSABLE ADDRESS SPACES IN THE SYSTEM:'
EDTOTNOR DS    CL6
         DC    CL78' '            FILLER
TOTALLN2 DC    CL49'              ASID USAGE FROM ASVT               '
         DC    CL49'                                                 '
         DC    CL35' '            FILLER
TOTMAXU  DC    CL23' MAXUSER FROM IEASYSXX:'
EDTOTMX  DS    CL6
         DC    CL104' '            FILLER
TOTAVI   DC    CL23'          IN USE ASIDS:'
EDTOTAVI DS    CL6
         DC    CL104' '            FILLER
TOTAVA   DC    CL23'       AVAILABLE ASIDS:'
EDTOTAVA DS    CL6
         DC    CL104' '            FILLER
TOTSASI  DC    CL23' RSVSTRT FROM IEASYSXX:'
EDTOTST  DS    CL6
         DC    CL104' '            FILLER
TOTSASII DC    CL23'        RSVSTRT IN USE:'
EDTOTSTI DS    CL6
         DC    CL104' '            FILLER
TOTSASIA DC    CL23'     RSVSTRT AVAILABLE:'
EDTOTSTA DS    CL6
         DC    CL104' '            FILLER
TOTNR    DC    CL23' RSVNONR FROM IEASYSXX:'
EDTOTNR  DS    CL6
         DC    CL104' '            FILLER
TOTNRI   DC    CL23'        RSVNONR IN USE:'
EDTOTNRI DS    CL6
         DC    CL104' '            FILLER
TOTNRA   DC    CL23'     RSVNONR AVAILABLE:'
EDTOTNRA DS    CL6
         DC    CL104' '            FILLER
TOTNONRU DC    CL23' NON-REUSABLE ASIDS   :'
TOT2NOR  DS    CL6
         DC    CL104' '            FILLER
         LTORG
         CVT DSECT=YES
         IHAASVT
         IHAASCB
         END
//G.SYSPRINT DD  SYSOUT=*
//G.SYSUDUMP DD  SYSOUT=*
