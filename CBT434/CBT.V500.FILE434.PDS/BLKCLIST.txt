CONTROL NOMSG NOLIST NOCONLIST
/*                           */
/* AUTHOR: MARK ZELDEN       */
/*                           */
LOOP: +
ISPEXEC DISPLAY PANEL(BLKPANEL)
/*  IF "END" WAS ENTERED, EXIT */
IF &LASTCC = 8 THEN DO
  FREE FI(BLKPRINT)
  EXIT CODE(0)
END

/* ALLOCATE FILE FOR BROWSING COMMAND OUTPUT */
ALLOC FILE(BLKPRINT) UNIT(SYSALLDA) SP(1,1) TRACK REUSE +
      RECFM(F B) LRECL(132) BLKSIZE(3960)
OPENFILE BLKPRINT OUTPUT            /* OPEN FILE TO BE BROWSED      */
SET &SYSOUTTRAP=500                 /* TRAP COMMAND OUTPUT          */

/* ISSUE APPROPRITE BLKDISK COMMAND */
IF &RECORDS = &STR() AND &KEY = &STR() AND +
    &BLK = &STR() THEN +
BLK&DEVT &LRECL
ELSE +
IF &KEY = &STR() AND &BLK=&STR() THEN  +
BLK&DEVT &LRECL RECORDS(&RECORDS)
ELSE +
IF &RECORDS = &STR() AND &BLK=&STR() THEN  +
BLK&DEVT &LRECL KEY(&KEY)
ELSE +
IF &RECORDS = &STR() AND &KEY=&STR() THEN  +
BLK&DEVT &LRECL BLKSIZE(&BLK)
ELSE +
IF &RECORDS = &STR() THEN +
BLK&DEVT &LRECL BLKSIZE(&BLK) KEY(&KEY)
ELSE +
IF &BLK = &STR() THEN +
BLK&DEVT &LRECL RECORDS(&RECORDS) KEY(&KEY)
ELSE +
IF &KEY = &STR() THEN +
BLK&DEVT &LRECL RECORDS(&RECORDS) BLKSIZE(&BLK)
ELSE +
BLK&DEVT &LRECL RECORDS(&RECORDS) BLKSIZE(&BLK) KEY(&KEY)

SET &SYSOUTTRAP=0                   /* RESET SO WE DON'T TRAP MORE  */
SET I = 1                           /* INITIALITE COUNTER           */
DO WHILE &I <= &SYSOUTLINE
  SET LINE = &STR(&&SYSOUTLINE&I)   /* GET A &SYSOUTLINENN VARIABLE */
  SET &BLKPRINT = &STR(&LINE)       /* SET PUTFILE VARIABLE         */
  SET I = &I + 1                    /* INCREASE THE LINE COUNTER    */
  PUTFILE BLKPRINT                  /* WRITE TO OUTPUT FILE         */
END
CLOSFILE BLKPRINT

ISPEXEC LMINIT DATAID(BROWSE) DDNAME(BLKPRINT)
ISPEXEC BROWSE DATAID(&BROWSE)
ISPEXEC LMFREE DATAID(&BROWSE)

GOTO LOOP
