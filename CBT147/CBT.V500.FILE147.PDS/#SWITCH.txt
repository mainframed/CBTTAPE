         MACRO
&LBL     #SWITCH &PARSE=,&BIT=,&MSGVAL=,&KWG=
         LCLC  &A,&L1,&L2,&L3
         SPACE 2
&A       SETC  '&LBL'
         AIF   ('&A' EQ '').NOLBL
&LBL     DS    0H
.NOLBL   ANOP
         #SET  &KWG,OFF
&L1      SETC  '#SW&SYSNDX.A'
&L2      SETC  '#SW&SYSNDX.B'
&L3      SETC  '#SW&SYSNDX.C'
         LA    R2,&PARSE           POINT TO PARSE ITEM
         USING PRSKWKW,R2
         LH    R1,PRSKWLN          LOAD THE LENGTH VALUE
         LTR   R1,R1               ANYTHING SUPPLIED ??
         BZ    &L3                 NOPE ---
         L     R1,PRSKWLOC         POINT TO THE VALUE
         CLC   0(3,R1),=C'YES'     'YES' GIVEN ??
         BE    &L2                 YES; ASSUME ITS THE DEFAULT
         CLI   0(R1),C'Y'          SHORT FORM ??
         BE    &L2                 YES; STILL DEFAULT
         CLI   0(R1),C'N'          NEGATIVE VALUE ??
         BE    &L1                 YES ---
         CLC   0(2,R1),=C'NO'      LONG FORM ??
         BE    &L1                 YES ---
         #PUT  =CL133'  ARCHPARS:  INVALID VALUE FOR &MSGVAL KEYWORD.'
         #PUT  IGNORED             SHOW KEYWORD WAS IGNORED
         OI    WKRC,8              SET RETURN CODE (8 = SYNTAX ERROR)
         B     &L3                 AND SKAT ??
&L1      DS    0H
         #SET  &BIT,OFF
         #SET  &KWG,ON
         B     &L3
&L2      DS    0H
         #SET  &BIT,ON
         #SET  &KWG,ON
&L3      DS    0H
         DROP  R2
         SPACE 2
         MEND
