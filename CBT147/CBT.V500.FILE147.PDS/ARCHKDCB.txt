CDCB     TITLE 'ARCHKDCB --- ARCHIVER FUNCTION SUPPORT SUBROUTINE'
*---------------------------------------------------------------------*
*---     FUNCTION: USING THE PREVIOUSLY-OPENED OUTPUT DCB AND      ---*
*---           THE INPUT MEMBER HEADER RECORD, CHECK FOR THE       ---*
*---           COMPATABILITY OF THE DCB PARAMETERS.  CERTAIN       ---*
*---           TYPES MAY BE CONVERTED BY THE ARCHIVER, WHILE       ---*
*---           OTHERS ARE NOT CONVERTIBLE.  FOR THOSE THAT MAY     ---*
*---           BE VALIDLY CONVERTED, SET A RETURN CODE OF 0,       ---*
*---           ELSE SET A RETURN CODE OF 4, ISSUE A MESSAGE,       ---*
*---           AND RETURN TO THE CALLER.  THE FOLLOWING TABLE      ---*
*---           DEFINES ACCEPTABLE CONVERSIONS.  THOSE THAT ARE     ---*
*---           ACCEPTABLE ARE MARKED WITH '*'; ALL OTHERS ARE      ---*
*---           REFUSED.                                            ---*
*---                                                               ---*
*---                        INPUT FORMAT FROM ARCHIVE              ---*
*---                -------------------------------------------    ---*
*---                |  F   |   FB   |   V   |   VB   |    U   |    ---*
*---           ------------------------------------------------    ---*
*---      O     F   |  *   |   *    |       |        |        |    ---*
*---      U    ------------------------------------------------    ---*
*---      T     FB  |  *   |   *    |       |        |        |    ---*
*---      P    ------------------------------------------------    ---*
*---      U     V   |  *   |    *   |   *   |   *    |   *    |    ---*
*---      T    ------------------------------------------------    ---*
*---            VB  |  *   |    *   |   *   |   *    |   *    |    ---*
*---      D    ------------------------------------------------    ---*
*---      C     U   |  *   |    *   |   *   |   *    |   *    |    ---*
*---      B    ------------------------------------------------    ---*
*---                                                               ---*
*---     NOTE: CONVERSION FROM F/FB TO V/VB REQUIRES THAT          ---*
*---           FOUR BYTES BE ADDED TO LRECL FOR RDW AND FOUR BYTES ---*
*---           ADDED TO BLKSIZE FOR BDW, WHERE APPROPRIATE.        ---*
*---                                                               ---*
*---     NOTE: CONVERSION FROM U TO V/VB REQUIRES THAT             ---*
*---           FOUR BYTES BE ADDED TO LRECL FOR RDW AND FOUR BYTES ---*
*---           ADDED TO BLKSIZE FOR BDW, WHERE APPROPRIATE.        ---*
*---                                                               ---*
*---     V/VB LRECLS MAY BE EXTENDED AS NEEDED.                    ---*
*---                                                               ---*
*---     U-FORMAT REQUIRES THAT THE OUTPUT BLKSIZE BE EQUAL OR     ---*
*---           LARGER THAN INPUT BLKSIZE.                          ---*
*---                                                               ---*
*---     PARAMETERS:  REGISTER 1 POINTS TO THE ARCHIVE ITEM        ---*
*---     HEADER RECORD. THE DCB TO BE CHECKED IS ADDRESS BY        ---*
*---     THE MAINWK DSECT.                                         ---*
*---------------------------------------------------------------------*
         SPACE 3
ARCHKDCB CSECT
R0       EQU   0                   R
R1       EQU   1                    E
R2       EQU   2                     G
R3       EQU   3                      I
R4       EQU   4                       S
R5       EQU   5                        T
R6       EQU   6                         E
R7       EQU   7                          R
R8       EQU   8
R9       EQU   9                   E
R10      EQU   10                   Q
R11      EQU   11                    U
R12      EQU   12                     A
R13      EQU   13                      T
R14      EQU   14                       E
R15      EQU   15                        S
         USING ARCHKDCB,R15
         #STAMP
         STM   R14,R12,12(R13)     STORE ENTRY REGISTERS
         LR    R11,R15             LOAD ROUTINE BASE REGISTER
         DROP  R15                 DROP THE OLD BASE
         USING ARCHKDCB,R11        DEFINE THE NEW BASE REGISTER
         LA    R14,72(,R13)        POINT DOWN THE SAVE AREA STACK
         ST    R14,8(,R13)         STORE FORWARD POINTER
         ST    R13,4(,R14)         AND BACK POINTER
         LR    R13,R14             LOAD NEW SAVE AREA POINTER
         L     R14,4(,R13)         RELOAD BACK POINTER
         L     R14,12(,R14)        RELOAD ORIG. REG 14
         USING MAINWK,R12          BASE THE MAIN WORK AREA
         LR    R3,R1               COPY THE RECORD ADDRESS
         USING REC,R3              AND HEADER DSECT
         L     R2,NDCBA            AND LOAD DCB ADDRESS
         USING IHADCB,R2           BASE DCB DSECT
         LA    R4,0                CLEAR WORK REGISTER
         IC    R4,RSRREC           INSERT THE RECORD FORMAT
         N     R4,=A(DCBRECU+DCBRECBR) CLOBBER EXTRANEOUS BITS
         IC    R5,DCBRECFM         OUTPUT RECFM
         N     R5,=A(DCBRECU+DCBRECBR) CLOBBER EXTRANEOUS BITS
         C     R5,=A(DCBRECF)      F-FORMAT OUTPUT ??
         BE    DCBC1               YES ---
         C     R5,=A(DCBRECF+DCBRECBR) FB-FORMAT RECORDS
         BE    DCBC4               YES ---
         C     R5,=A(DCBRECV)      V-FORMAT RECORDS ??
         BE    DCBC7               YES ---
         C     R5,=A(DCBRECV+DCBRECBR) VB-FORMAT RECORDS ??
         BE    DCBC13              YES ---
         B     DCBC19              U-FORMAT RECORDS
*---------------------------------------------------------------------*
*---     FIXED, UNBLOCKED RECORDS FOR OUTPUT                       ---*
*---                                                               ---*
*---     Q. INPUT RECFM = F                                        ---*
*---            BLKSIZEIN = BLKSIZE OUT ??                         ---*
*---                                                               ---*
*---     Q. INPUT RECFM = FB                                       ---*
*---            LRECLIN = BLKSIZE OUT ??                           ---*
*---------------------------------------------------------------------*
DCBC1    DS    0H
         C     R4,=A(DCBRECF+DCBRECBR) FIXED BLOCKED RECORDS ??
         BE    DCBC2               YES ---
         C     R4,=A(DCBRECF)      FIXED UNBLOCKED RECORDS ??
         BE    DCBC3               YES ---
         B     DCBC25              INVALID FORMAT CHANGE
DCBC2    DS    0H
         CLC   DCBBLKSI,RSRLRE     INPUT LRECL MATCH OUTPUT BLKSIZE ??
         BNE   DCBC25              NOPE; INVALID
         B     DCBC26              YES; ALL IS OK
DCBC3    DS    0H
         CLC   DCBBLKSI,RSRBLK     INPUT BLKL MATCH OUTPUT BLKSIZE ??
         BNE   DCBC25              NOPE; INVALID
         B     DCBC26              YES; ALL IS OK
*---------------------------------------------------------------------*
*---     FIXED, BLOCKED RECORDS FOR OUTPUT                         ---*
*---                                                               ---*
*---     Q. INPUT RECFM = F                                        ---*
*---            BLKSIZEIN = LRECL OUT ??                           ---*
*---                                                               ---*
*---     Q. INPUT RECFM = FB                                       ---*
*---            LRECLIN = LRECL OUT ??                             ---*
*---------------------------------------------------------------------*
DCBC4    DS    0H
         C     R4,=A(DCBRECF+DCBRECBR) FIXED-BLOCKED RECORDS ??
         BE    DCBC5               YES ---
         C     R4,=A(DCBRECF)      FIXED UNBLOCKED RECORDS ??
         BE    DCBC6               YES ---
         B     DCBC25              INVALID FORMAT CHANGE
DCBC5    DS    0H
         CLC   RSRLRE,DCBLRECL     LRECLS MATCH ??
         BNE   DCBC25              NOPE; INVALID CHANGE
         B     DCBC26              YES; ALL IS OK
DCBC6    DS    0H
         CLC   RSRBLK,DCBLRECL     IN BLKSIZE MATCH OUTPUT LRECL ??
         BNE   DCBC25              NOPE; INVALID CHANGE
         B     DCBC26              YES; ALL IS OK
*---------------------------------------------------------------------*
*---     VARIABLE, UNBLOCKED RECORDS FOR OUTPUT                    ---*
*---                                                               ---*
*---     Q. INPUT RECFM = V                                        ---*
*---            BLKSIZEIN <= BLKSIZE OUT ??                        ---*
*---                                                               ---*
*---     Q. INPUT RECFM = U                                        ---*
*---            BLKSIZEIN <= BLKSIZE OUT - 4 ??                    ---*
*---                                                               ---*
*---     Q. INPUT RECFM = F                                        ---*
*---            BLKSIZEIN <= BLKSIZE OUT - 4 ??                    ---*
*---                                                               ---*
*---     Q. INPUT RECFM = VB                                       ---*
*---            LRECLIN <= BLKSIZE OUT ??                          ---*
*---                                                               ---*
*---     Q. INPUT RECFM = FB                                       ---*
*---            LRECLIN <= BLKSIZE OUT-4 ??                        ---*
*---------------------------------------------------------------------*
DCBC7    DS    0H
         C     R4,=A(DCBRECV)      V-FORMAT INPUT ??
         BE    DCBC8               YES ---
         C     R4,=A(DCBRECU)      U-FORMAT INPUT ??
         BE    DCBC9               YES ---
         C     R4,=A(DCBRECF)      F-FORMAT INPUT ??
         BE    DCBC10
         C     R4,=A(DCBRECV+DCBRECBR) VB-FORMAT INPUT ??
         BE    DCBC11              YES ---
         C     R4,=A(DCBRECF+DCBRECBR) FB-FORMAT INPUT ??
         BE    DCBC12              YES ---
         ABEND 998,DUMP            INVALID INPUT RECORD FORMAT
DCBC8    DS    0H
         CLC   RSRBLK,DCBBLKSI     INPUT BLKL .LE. OUTPUT BLKL ??
         BNH   DCBC26              YES; ALL IS OK
         B     DCBC25              NOPE; FAIL THE VALIDATION
DCBC9    DS    0H
         LH    R4,RSRBLK           LOAD INPUT BLKSIZE
         LA    R4,4(,R4)           ADD 4
         CH    R4,DCBBLKSI         WILL BLOCKS STILL FIT ??
         BNH   DCBC26              YES; ALL IS OK
         B     DCBC25              NOPE; FAIL THE VALIDATION
DCBC10   DS    0H
         LH    R4,RSRBLK           LOAD INPUT BLKSIZE
         LA    R4,4(,R4)           ADD 4
         CH    R4,DCBBLKSI         WILL BLOCKS STILL FIT ??
         BNH   DCBC26              YES; ALL IS OK
         B     DCBC25              NOPE; FAIL THE VALIDATION
DCBC11   DS    0H
         CLC   RSRLRE,DCBBLKSI     WILL LRECL FIT INTO BLOCK ??
         BNH   DCBC26              YES; ALL IS OK
         B     DCBC25              NOPE; FAIL THE VALIDATION
DCBC12   DS    0H
         LH    R4,RSRLRE           LOAD INPUT LRECL
         LA    R4,4(,R4)           ADD 4
         CH    R4,DCBBLKSI         WILL IT STILL FIT ??
         BNH   DCBC26              YES; ALL IS OK
         B     DCBC25              NOPE; FAIL THE VALIDATION
*---------------------------------------------------------------------*
*---     VARIABLE, BLOCKED RECORDS FOR OUTPUT                      ---*
*---                                                               ---*
*---     Q. INPUT RECFM = V                                        ---*
*---            BLKSIZEIN <= LRECL OUT ??                          ---*
*---                                                               ---*
*---     Q. INPUT RECFM = U                                        ---*
*---            BLKSIZEIN <= LRECL OUT - 4 ??                      ---*
*---                                                               ---*
*---     Q. INPUT RECFM = F                                        ---*
*---            BLKSIZEIN <= LRECL OUT - 4 ??                      ---*
*---                                                               ---*
*---     Q. INPUT RECFM = VB                                       ---*
*---            LRECLIN <= LRECL OUT ??                            ---*
*---                                                               ---*
*---     Q. INPUT RECFM = FB                                       ---*
*---            LRECLIN <= LRECL OUT - 4 ??                        ---*
*---------------------------------------------------------------------*
DCBC13   DS    0H
         C     R4,=A(DCBRECV)      V-FORMAT INPUT ??
         BE    DCBC14              YES ---
         C     R4,=A(DCBRECU)      U-FORMAT INPUT ??
         BE    DCBC15              YES ---
         C     R4,=A(DCBRECF)      F-FORMAT INPUT ??
         BE    DCBC16
         C     R4,=A(DCBRECV+DCBRECBR) VB-FORMAT INPUT ??
         BE    DCBC17              YES ---
         C     R4,=A(DCBRECF+DCBRECBR) FB-FORMAT INPUT ??
         BE    DCBC18              YES ---
         ABEND 998,DUMP            INVALID INPUT RECORD FORMAT
DCBC14   DS    0H
         CLC   RSRBLK,DCBLRECL     INPUT BLKL .LE. OUTPUT BLKL ??
         BNH   DCBC26              YES; ALL IS OK
         B     DCBC25              NOPE; FAIL THE VALIDATION
DCBC15   DS    0H
         LH    R4,RSRBLK           LOAD INPUT BLKSIZE
         LA    R4,4(,R4)           ADD 4
         CH    R4,DCBLRECL         WILL BLOCKS STILL FIT ??
         BNH   DCBC26              YES; ALL IS OK
         B     DCBC25              NOPE; FAIL THE VALIDATION
DCBC16   DS    0H
         LH    R4,RSRBLK           LOAD INPUT BLKSIZE
         LA    R4,4(,R4)           ADD 4
         CH    R4,DCBLRECL         WILL BLOCKS STILL FIT ??
         BNH   DCBC26              YES; ALL IS OK
         B     DCBC25              NOPE; FAIL THE VALIDATION
DCBC17   DS    0H
         CLC   RSRLRE,DCBLRECL     WILL LRECL FIT INTO BLOCK ??
         BNH   DCBC26              YES; ALL IS OK
         B     DCBC25              NOPE; FAIL THE VALIDATION
DCBC18   DS    0H
         LH    R4,RSRLRE           LOAD INPUT LRECL
         LA    R4,4(,R4)           ADD 4
         CH    R4,DCBLRECL         WILL IT STILL FIT ??
         BNH   DCBC26              YES; ALL IS OK
         B     DCBC25              NOPE; FAIL THE VALIDATION
*---------------------------------------------------------------------*
*---     UNDEFINED-FORMAT RECORDS FOR OUTPUT                       ---*
*---                                                               ---*
*---     Q. INPUT RECFM = U ??                                     ---*
*---            BLKSIZEIN <= BLKSIZE OUT ??                        ---*
*---                                                               ---*
*---     Q. INPUT RECFM = V ??                                     ---*
*---            BLKSIZEIN <= BLKSIZE OUT+4 ??                      ---*
*---                                                               ---*
*---     Q. INPUT RECFM = F ??                                     ---*
*---            BLKSIZEIN <= BLKSIZE OUT ??                        ---*
*---                                                               ---*
*---     Q. INPUT RECFM = FB ??                                    ---*
*---            LRECLIN <= BLKSIZE OUT ??                          ---*
*---                                                               ---*
*---     Q. INPUT RECFM = VB ??                                    ---*
*---            LRECLIN <= BLKSIZE OUT+4 ??                        ---*
*---                                                               ---*
*---------------------------------------------------------------------*
DCBC19   DS    0H
         C     R4,=A(DCBRECU)      U-FORMAT INPUT ??
         BE    DCBC20              YES ---
         C     R4,=A(DCBRECF)      F-FORMAT INPUT ??
         BE    DCBC21              YES ---
         C     R4,=A(DCBRECV)      F-FORMAT INPUT ??
         BE    DCBC22              YES ---
         C     R4,=A(DCBRECF+DCBRECBR) FB-FORMAT INPUT ??
         BE    DCBC23              YES ---
         C     R4,=A(DCBRECV+DCBRECBR) VB-FORMAT INPUT ??
         BE    DCBC24              YES ---
DCBC20   DS    0H
         CLC   RSRBLK,DCBBLKSI     INPUT BKLK .LE. OUTPUT BLKL ??
         BNH   DCBC26              YES ---
         B     DCBC25              NOPE; INVALID REBLOCKING
DCBC21   DS    0H
         CLC   RSRBLK,DCBBLKSI     INPUT BKLK .LE. OUTPUT BLKL ??
         BNH   DCBC26              YES ---
         B     DCBC25              NOPE; INVALID REBLOCKING
DCBC22   DS    0H
         LH    R4,RSRBLK           LOAD INPUT BLKSIZE
         LA    R0,4
         SR    R4,R0               ACCOUNT FOR BDW
         CH    R4,DCBBLKSI         WILL IT FIT ??
         BNH   DCBC26              YES ---
         B     DCBC25              NOPE; INVALID REBLOCKING
DCBC23   DS    0H
         CLC   RSRLRE,DCBBLKSI     INPUT BLKSIZE .LE. OUTPUT BLKSIZE ??
         BNH   DCBC26              YES ---
         B     DCBC25              NOPE; INVALID REBLOCKING
DCBC24   DS    0H
         LH    R4,RSRLRE           LOAD INPUT LRECL
         LA    R0,4
         SR    R4,R0               ACCOUNT FOR BDW
         CH    R4,DCBBLKSI         WILL IT FIT ??
         BNH   DCBC26              YES ---
DCBC25   DS    0H
         #FORMAT RECTT,,DCBM1
         #PUT  LINE
         LA    R15,4
         L     R13,4(,R13)         LOAD BACK POINTER
         L     R14,12(,R13)        LOAD RETURN ADDRESS
         LM    R0,R12,20(R13)      RESTORE REGS
         BR    R14                 RETURN TO CALLER
DCBC26   DS    0H
         LA    R15,0
         L     R13,4(,R13)         LOAD BACK POINTER
         L     R14,12(,R13)        LOAD RETURN ADDRESS
         LM    R0,R12,20(R13)      RESTORE REGS
         BR    R14                 RETURN TO CALLER
         LTORG
DCBM1    DC    AL1(30),CL30' DCB ATTRIBUTES INCOMPATABLE.'
         LTORG
         DROP  R2,R3,R11,R12
         COPY  #RECS
         COPY  ##ARCHWK
         DCBD  DSORG=PS,DEVD=DA
         END
