COPY     TITLE 'ARCHIVER --- COPY FUNCTION'
*---------------------------------------------------------------------*
*---     THIS IS THE ARCHIVER CLUSTER-TO-CLUSTER COPY FUNCTION.    ---*
*---     ALL COPIES PROCEED FROM THE VSAM2 ARCHIVE TO THE VSAM1    ---*
*---     ARCHIVE.  ITEM SELECT IS PROVIDED AND ALL GENERICS ARE    ---*
*---     HONORED.                                                  ---*
*---------------------------------------------------------------------*
ARCHCOPY CSECT
R0       EQU   0                   R
R1       EQU   1                    E
R2       EQU   2                     G
R3       EQU   3                      I
R4       EQU   4                       S
R5       EQU   5                        T
R6       EQU   6                         E
R7       EQU   7                          R
R8       EQU   8
R9       EQU   9                   E
R10      EQU   10                   Q
R11      EQU   11                    U
R12      EQU   12                     A
R13      EQU   13                      T
R14      EQU   14                       E
R15      EQU   15                        S
         COPY #ARCBITS
         USING ARCHCOPY,R15
         #STAMP
         STM   R14,R12,12(R13)     STORE ENTRY REGISTERS
         LR    R11,R15             COPY ROUTINE BASE REGISTER
         DROP  R15                 DROP THE OLD BASE
         USING ARCHCOPY,R11        DEFINE THE NEW BASE REGISTER
         LA    R14,72(,R13)        POINT DOWN THE SAVE AREA STACK
         ST    R14,8(,R13)         STORE FORWARD POINTER
         ST    R13,4(,R14)         AND BACK POINTER
         LR    R13,R14             COPY NEW SAVE AREA POINTER
         L     R14,4(,R13)         RECOPY BACK POINTER
         L     R14,12(,R14)        RECOPY ORIG. REG 14
         LA    R10,0(,R11)         COPY ORIGINAL BASE REGISTER
         AH    R10,PASSIT-2        BUMP BY A BASE'S WORTH
         USING ARCHCOPY+4096,R10   AND DECLARE IT
         USING MAINWK,R12          BASE THE MAIN WORK AREA
         XC    FCOUNT,FCOUNT       CLEAR FUNCTION RECORD COUNT
         XC    MCNT,MCNT           AND MEMBER COUNT
         XC    ACNT,ACNT           CLEAR A COUNTER, FOR LATER
         MVC   VS1MODE,=CL8'OUTPUT' SET THE I/O MODE
         LA    R1,VSAM1DD          LOAD THE PARM POINTER
         LA    R0,0                AND THE 'OPEN' FLAG
         CALL  ARCHVSOP            CALL THE 'OPEN' FUNCTION
         LTR   R15,R15             OPEN OK ??
         BNZ   ENDITALL            NOPE; CAN'T WORK
         MVC   VS2MODE,=CL8'INPUT' SET THE I/O MODE
         LA    R1,VSAM2DD          LOAD THE PARM POINTER
         LA    R0,0                AND THE 'OPEN' FLAG
         CALL  ARCHVSOP            CALL THE 'OPEN' FUNCTION
         LTR   R15,R15             OPEN OK ??
         BNZ   ENDITALL            NOPE; CAN'T WORK
         XC    MCNT,MCNT           CLEAR THE COUNTERS
         XC    TOKEN,TOKEN
         XC    CHAIN,CHAIN
         EJECT
*---------------------------------------------------------------------*
*---     HERE'S THE MEAT OF THE PROCESSING. THE BASIC PROCESS IS   ---*
*---           THIS:                                               ---*
*---                                                               ---*
*---     1. USING THE CONTROL INFORMATION WE WERE GIVEN AND THE    ---*
*---        INFORMATION WE'VE DEVELOPED, LOCATE THE HEADER RECORD  ---*
*---        FOR THE FIRST MEMBER. WE'LL USE THE VS2BUF1 AREA FOR   ---*
*---        HEADER/KEY CONSTRUCTION.                               ---*
*---                                                               ---*
*---     2. LOOP THROUGH THE MEMBER, READING THE RECORDS AND       ---*
*---        COPYING THEM INTO THE OUTPUT VSAM BUFFER, WRITING AS   ---*
*---        EACH BUFFER GETS FULL.                                 ---*
*---                                                               ---*
*---     3. AT END-OF-FILE FOR THE MEMBER, UPDATE THE VERSION      ---*
*---        NUMBER, IF NOT SUPPLIED, AND LOOK FOR THE NEXT ITEM    ---*
*---        TO COPY.                                               ---*
*---                                                               ---*
*---     VSAM BUFFER USAGE:                                        ---*
*---           VS1BUF1: OUTPUT HEADER RECORD (MULTIPLE UPDATES)    ---*
*---           VS1BUF2: OUTPUT DATA/NOTE RECORD                    ---*
*---           VS1BUF3: USED BY ARCHERAS AS NEEDED                 ---*
*---           VS2BUF1: INPUT HEADER RECORD (OR ALIAS RECORD)      ---*
*---           VS2BUF2: INPUT 'REAL' HEADER RECORD (ALWAYS)        ---*
*---           VS2BUF3: INPUT DATA/NOTE RECORD                     ---*
*---                                                               ---*
*---     NOTE THAT THESE ASSIGNMENTS ARE SUBJECT TO CHANGE         ---*
*---     WITHOUT WARNING.                                          ---*
*---                                                               ---*
*---     IN ORDER TO AVOID COPYING THE SAME ITEM MULTIPLE TIMES,   ---*
*---     DUE TO ALIASES, THE CHAIN/CHKCHAIN ROUTINES ARE COPIED    ---*
*---     FROM ARCHLOAD AND USED FREELY.                            ---*
*---------------------------------------------------------------------*
         L     R6,VS2BUF1          POINT TO THE VSAM BUFFER
         USING REC,R6              BASE THE RECORD DSECT
         XC    0(RECKLEN,R6),0(R6)    START AT THE BEGINNING !!
         MODCB RPL=(*,RPL4),                                           X
               OPTCD=(SEQ,FKS,KGE,SYN,NUP),                            X
               ACB=(*,ACB2),                                           X
               ARG=(6),                                                X
               AREA=(6),                                               X
               AREALEN=(*,VS2LIM),                                     X
               MF=(G,GENWK)        POSITION TO CLUSTER START
         L     R1,RPL4             COPY THE RPL ADDRESS
         POINT RPL=(1)             GET TO IT
NEXTHDR  DS    0H
         L     R6,VS2BUF1          POINT TO THE RECORD
*        MVI   RECTT,#RTHDR        INITIAL RECORD TYPE
*        ICM   R15,15,RECVER       INSERT THE VERSION NUMBER
*        AL    R15,=F'1'           INCREMENT BY ONE
*        STCM  R15,15,RECVER       SAVE UPDATED VALUE
GETHDR   DS    0H
         L     R1,RPL4             COPY RPL ADDRESS
         GET   RPL=(1)             AND READ A HEADER RECORD
         LTR   R15,R15             ANYTHING LEFT ??
         BNZ   ENDITALL            NOPE; ALL DONE
         SHOWCB RPL=(*,RPL4),                                          X
               FIELDS=RECLEN,                                          X
               AREA=(S,VS2LEN1),                                       X
               LENGTH=4,                                               X
               MF=(G,GENWK)        FETCH THE RECORD LENGTH
         CLI   RECTT,#RTHDR        HEADER RECORD ??
         BNE   ENDITALL            NOPE; ALL DONE ---
         #IF   RSRALIAS,OFF,CHKA   'REAL' HEADER; CHECK IT
         #IF   LDARCHA,OFF,NEXTHDR SKIPPING ALIASES ??
CHKA     DS    0H
         LA    R1,RECN             POINT TO THE HEADER
         LA    R0,0                USE DEFAULT QUALIFIER DATA
         CALL  ARCHKNAM            GO CHECK: DO WE PROCESS THIS ITEM ??
         LTR   R15,R15             PASS THE CHECK ??
         BNZ   NEXTHDR             NOPE ---
SELECTED DS    0H
         LA    R1,RECN             CHECK FOR A PREVIOUS COPY OF IT
         #IF   RSRALIAS,OFF,SELA   THIS AIN'T AN ALIAS
         LA    R1,RECAN            CHECK FOR A PREVIOUS COPY OF IT
SELA     DS    0H
         BAL   R14,CHKCHN          CALL CHAIN-CHECKER
         LTR   R15,R15             WAS IT PRESENT ??
         BNZ   NEXTHDR             YES ---
*---------------------------------------------------------------------*
*---     THIS ITEM WAS SELECTED. CONSTRUCT THE 'TRUE' KEY IN       ---*
*---     VS2BUF2 AND GO GET THE TRUE HEADER RECORD.  ALSO DO THE   ---*
*---     TRUENAME CHAIN ADD.  IF THE TRUE HEADER CAN'T BE FOUND,   ---*
*---     JUST IGNORE THIS ITEM COMPLETELY.                         ---*
*---------------------------------------------------------------------*
         LA    R1,RECN             POINT TO ITEM NAME
         #IF   RSRALIAS,OFF,SELB   THIS AIN'T AN ALIAS
         LA    R1,RECAN            NOW THE TRUE NAME !!
SELB     DS    0H
         BAL   R14,CHAINER         CALL CHAIN-BUILDER
         L     R2,VS2BUF2          LOAD THE BUFFER POINTER
         MVC   0(RECKLEN,R2),RECTT COPY THE RECORD KEY
         #IF   RSRALIAS,OFF,SEL1   NOT AN ALIAS RECORD
         MVC   1(44,R2),RECAN      COPY THE 'TRUE' NAME VALUES
SEL1     DS    0H
         LR    R6,R2               BASE DSECT ON VS2BUF2
         MVC   BLDLAREA(RECKLEN),0(R6) SAVE KEY FOR ALIAS COPIER
         MODCB RPL=(*,RPL5),                                           X
               OPTCD=(DIR,FKS,KGE,SYN,NUP),                            X
               ACB=(*,ACB2),                                           X
               ARG=(6),                                                X
               AREA=(6),                                               X
               AREALEN=(*,VS2LIM),                                     X
               MF=(G,GENWK)        POSITION TO CLUSTER START
         L     R1,RPL5             POINT TO THE RPL
         GET   RPL=(1)             AND READ THE HEADER
         LTR   R15,R15             DID IT READ OK ??
         BNZ   NEXTHDR             NOPE; SKIP THIS ITEM
         SHOWCB RPL=(*,RPL5),                                          X
               FIELDS=RECLEN,                                          X
               AREA=(S,VS2LEN2),                                       X
               LENGTH=4,                                               X
               MF=(G,GENWK)        FETCH THE RECORD LENGTH
*---------------------------------------------------------------------*
*---     AT THIS POINT, WE'VE DETERMINED THAT THIS ITEM NEEDS TO   ---*
*---     BE COPIED. NOW WE CHECK THE VERSION NUMBER.  IF WE'RE     ---*
*---     NOT REPLACING A SIMILAR VERSION, WE WILL TRY TO WRITE     ---*
*---     THIS HEADER.  EACH TIME WE GET A DUPLICATE RECORD         ---*
*---     INDICATION FROM VSAM, WE'LL INCREMENT THE VERSION NUMBER  ---*
*---     BY ONE AND TRY AGAIN.  THIS WILL DETERMINE THE MAJORITY   ---*
*---     OF THE KEY FOR ALL SUBSEQUENT RECORDS IN THIS ITEM AS     ---*
*---     WELL.  IF, ON THE OTHER HAND, WE'RE REPLACING ANY         ---*
*---     DUPLICATE DATA, WE'LL SIMPLY CALL ARCHERAS TO REMOVE ANY  ---*
*---     PRE-EXISTING ITEM OF A MATCHING KEY.                      ---*
*---------------------------------------------------------------------*
         L     R6,VS2BUF2          LOAD THE INPUT RECORD POINTER
         L     R7,VS2LEN2          LOAD THE RECORD LENGTH
         L     R2,VS1BUF2          LOAD THE OUTPUT BUFFER ADDRESS
         LR    R3,R7               AND SET A LENGTH
         ST    R3,VS1LEN2          SAVE FOR THE 'PUT'TER
         MVCL  R2,R6               COPY THE RECORD
         L     R6,VS1BUF2          RELOAD THE BUFFER POINTER
         XC    RSRNOT#,RSRNOT#     NO NOTES, YET ---
PUTHDR   DS    0H
         LR    R1,R6               COPY THE RECORD POINTER
         L     R0,VS2LEN2          AND LENGTH
         BAL   R14,VSPUT           GO WRITE THE RECORD (MAYBE)
         LTR   R15,R15             DUPLICATE RECORD ??
         BZ    DONOTES             NOPE; ALL IS OK ---
         #IF   REPL,ON,DELEIT      DELETE FOR REPLACEMENT ??
         ICM   R15,15,RECVER       INSERT THE VERSION NUMBER
         BCTR  R15,R0              UP BY ONE
         STCM  R15,15,RECVER
         B     PUTHDR              GO RETRY THE PUT
DELEIT   DS    0H
         LR    R1,R6               LOAD THE BUFFER POINTER
         CALL  ARCHERAS            DELETE THE DUPLICATE ITEM
         B     PUTHDR              AND GO RETRY THE 'PUT'
DONOTES  DS    0H
         L     R7,VS2BUF2          LOAD HEADER-RCD ADDRESS
         L     R6,VS2BUF3          POINT TO THE OTHER BUFFER
         MVC   0(RECKLEN,R6),0(R7) COPY THAT KEY
         MVI   RECTT,#RTNOTE       RESET TO NOTE TYPE
         MODCB RPL=(*,RPL6),                                           X
               OPTCD=(SEQ,FKS,KGE,SYN,NUP),                            X
               ARG=(6),                                                X
               ACB=(*,ACB2),                                           X
               AREA=(*,VS2BUF3),                                       X
               AREALEN=(*,VS2LIM),                                     X
               MF=(G,GENWK)        POSITION TO ITEM NOTE START
         L     R1,RPL6             COPY THE RPL ADDRESS
         POINT RPL=(1)             GET TO IT
         L     R4,VS1BUF2          LOAD OUTPUT BUFFER POINTER
         MVI   0(R4),#RTNOTE       SET TYPE FOR NOTES
         LA    R0,1                LOAD INITIAL SEQ# VALUE
         STCM  R0,15,RECSEQ#-REC(R4) SAVE THAT NUMBER
         LA    R0,RECKLEN          LOAD KEY LENGTH
         ST    R0,VS1LEN2          SAVE AS INITIAL RECORD LENGTH
NT1      DS    0H
         L     R1,RPL6             POINT TO THE RPL
         GET   RPL=(1)             GO READ A MEMBER NOTE RECORD
         LTR   R15,R15             DID IT COMPLETE OK ??
         BNZ   NTDONE              NOPE; ASSUME WE'RE DONE WITH NOTES
         SHOWCB RPL=(*,RPL6),                                          X
               FIELDS=RECLEN,                                          X
               AREA=(S,VS2LEN3),                                       X
               LENGTH=4,                                               X
               MF=(G,GENWK)        FETCH THE RECORD LENGTH
         CLI   RECTT,#RTNOTE       DATA RECORD ??
         BNE   NTDONE
         CLC   1(RECKLEN-5,R6),1(R7) STILL IN THE RIGHT ITEM ??
         BNE   NTDONE              NOPE; GO DO THE DATA RECORDS
         L     R6,VS2BUF3          LOAD THE BUFFER POINTER
         LA    R3,RDATDATA         POINT TO NOTE DATA
         L     R4,VS2LEN3          LOAD RECORD LENGTH
         LA    R4,0(R4,R6)         POINT TO
         BCTR  R4,R0                 LAST DATA BYTE
NT2      DS    0H
         #ADDBLK BUFFER=2,                                             X
               RECORD=3(,R3),                                          X
               LENGTH=0(,R3),                                          X
               RTN=VSPUT,                                              X
               ERR=TOOLONG
         LA    R15,0               CLEAR A WORK REGISTER
         ICM   R15,7,0(R3)         INSERT SEGMENT LENGTH
         LA    R3,3(R15,R3)        POINT TO NEXT INPUT SEGMENT
         CR    R3,R4               PAST THE END LET ??
         BL    NT2                 NOPE ---
         B     NT1                 YES; GET ANOTHER RECORD
NTDONE   DS    0H
         #ADDBLK BUFFER=2,                                             X
               RTN=VSPUT,                                              X
               LAST=YES
NT5      DS    0H
*---------------------------------------------------------------------*
*---     NOTE DATA IS COPIED. NOW UPDATE THE NOTE DATA LAST        ---*
*---     SEQUENCE NUMBER FIELD IN THE HEADER RECORD. WE'LL USE     ---*
*---     THE VS1BUF2 BUFFER AREA FOR THIS UPDATE.                  ---*
*---------------------------------------------------------------------*
         L     R2,VS1BUF2          POINT TO THE LAST OUTPUT BUFFER
         ICM   R4,15,RECSEQ#-REC(R2) LOAD THE SEQUENCE NUMBER
         BCTR  R4,R0               BACK UP TO LAST TRUE VALUE
         XC    RECSEQ#-REC(4,R2),RECSEQ#-REC(R2) CLEAR THAT VALUE
         MVI   0(R2),#RTHDR        SET 'HEADER' TYPE
         MODCB RPL=(*,RPL1),                                           X
               OPTCD=(DIR,FKS,KEQ,SYN,UPD),                            X
               ACB=(*,ACB1),                                           X
               ARG=(*,VS1BUF2),                                        X
               AREA=(*,VS1BUF2),                                       X
               AREALEN=(*,VS1LIM),                                     X
               MF=(G,GENWK)        VSAM SETUP FOR UPDATE
         L     R1,RPL1             POINT TO THE RPL
         GET   RPL=(1)             READ THE HEADER RECORD
         LTR   R15,R15             DID WE GET IT OK ??
         BZ    NT6                 YES ---
         L     R4,RPL1             LOAD THE RPL ADDRESS
         ABEND 100,DUMP            AND DIE HOFFIBLY
NT6      DS    0H
         L     R2,VS1BUF2          LOAD THE BUFFER ADDRESS
         STCM  R4,15,RSRNOT#-REC(R2) UPDATE NOTE-RECORD COUNT
         L     R1,RPL1             LOAD THE RPL ADDRESS
         PUT   RPL=(1)             AND UPDATE THE RECORD
         LTR   R15,R15             DID WE UPDATE IT OK ??
         BZ    NTEND               YES ---
         L     R4,RPL1             LOAD THE RPL ADDRESS
         ABEND 100,DUMP            AND DIE HORRIBLY
NTEND    DS    0H
         L     R7,VS2BUF2          LOAD HEADER-RCD ADDRESS
         L     R6,VS2BUF3          POINT TO THE OTHER BUFFER
         MVC   0(RECKLEN,R6),0(R7) COPY THAT KEY
         MVI   RECTT,#RTDATA       RESET TO DATA TYPE
         MODCB RPL=(*,RPL6),                                           X
               OPTCD=(SEQ,FKS,KGE,SYN,NUP),                            X
               ARG=(6),                                                X
               ACB=(*,ACB2),                                           X
               AREA=(*,VS2BUF3),                                       X
               AREALEN=(*,VS2LIM),                                     X
               MF=(G,GENWK)        POSITION TO ITEM DATA START
         L     R1,RPL6             COPY THE RPL ADDRESS
         POINT RPL=(1)             GET TO IT
         L     R4,VS1BUF2          LOAD OUTPUT BUFFER POINTER
         LA    R0,1                LOAD INITIAL SEQ# VALUE
         STCM  R0,15,RECSEQ#-REC(R4) SAVE THAT NUMBER
         MVI   0(R4),#RTDATA       SET TYPE FOR DATA
         LA    R0,RECKLEN          LOAD KEY LENGTH
         ST    R0,VS1LEN2          SAVE AS INITIAL RECORD LENGTH
DT1      DS    0H
         L     R1,RPL6             POINT TO THE RPL
         GET   RPL=(1)             GO READ A MEMBER DATA RECORD
         LTR   R15,R15             DID IT COMPLETE OK ??
         BNZ   DTDONE              NOPE; ASSUME WE'RE DONE WITH DATA
         SHOWCB RPL=(*,RPL6),                                          X
               FIELDS=RECLEN,                                          X
               AREA=(S,VS2LEN3),                                       X
               LENGTH=4,                                               X
               MF=(G,GENWK)        FETCH THE RECORD LENGTH
         CLI   RECTT,#RTDATA       DATA RECORD ??
         BNE   DTDONE
         CLC   1(RECKLEN-5,R6),1(R7) STILL IN THE RIGHT ITEM ??
         BNE   DTDONE              NOPE; GO DO THE DATA RECORDS
         L     R6,VS2BUF3          LOAD INPUT BUFFER POINTER
         LA    R3,RDATDATA         POINT TO DATA DATA
         L     R4,VS2LEN3          LOAD RECORD LENGTH
         LA    R4,0(R4,R6)         POINT TO
         BCTR  R4,R0                 LAST DATA BYTE
DT2      DS    0H
         #ADDBLK BUFFER=2,                                             X
               RECORD=3(,R3),                                          X
               LENGTH=0(,R3),                                          X
               RTN=VSPUT,                                              X
               ERR=TOOLONG
         LA    R15,0               CLEAR A WORK REGISTER
         ICM   R15,7,0(R3)         INSERT SEGMENT LENGTH
         LA    R3,3(R15,R3)        POINT TO NEXT INPUT SEGMENT
         CR    R3,R4               PAST THE END YET ??
         BL    DT2                 NOPE ---
         B     DT1                 YES; GET ANOTHER RECORD
DTDONE   DS    0H
         #ADDBLK BUFFER=2,                                             X
               RTN=VSPUT,                                              X
               LAST=YES
         L     R1,MCNT             LOAD MEMBER COUNT
         LA    R1,1(,R1)           INCREMENT BY ONE
         ST    R1,MCNT             SAVE UPDATED COUNT
         L     R6,VS2BUF2          LOAD THE INPUT HEADER ADDRESS
         MVC   LINE,SEP            MOVE IN A SEPARATOR LINE
         #PUT  LINE                AND PRINT IT
         #FORMAT RECTT,,INMSG
         #PUT  LINE
         L     R6,VS1BUF2          LOAD OUTPUT HEADER VALUES
         #FORMAT RECTT,,OUTMSG
         #PUT  LINE
         #IF   RETA,OFF,NEXTHDR    DON'T BOTHER WITH ALIASES
*---------------------------------------------------------------------*
*---     COPY ALIASES, IF ALIASES=(Y|YES) WAS SPECIFIED.          ----*
*---------------------------------------------------------------------*
         L     R2,VS1BUF2          POINT TO OUTPUT BUFFER
         XC    RECSEQ#-REC(100,R2),RECSEQ#-REC
         MVC   RECAN-REC(44,R2),1(R2) COPY 'REAL' NAME FIELDS
         DROP  R6
         USING REC,R2
         #SET  RSRALIAS,ON
         DROP  R2
         USING REC,R6
         L     R6,VS2BUF2          POINT TO THE BUFFER
         XC    0(RECKLEN,R6),0(R6) START AT THE BEGINNING !!
         MODCB RPL=(*,RPL5),                                           X
               OPTCD=(SEQ,FKS,KGE,SYN,UPD),                            X
               ARG=(R6),                                               X
               AREA=(R6),                                              X
               AREALEN=(*,VS2LIM),                                     X
               MF=(G,GENWK)        POSITION TO CLUSTER START
         L     R1,RPL5
         POINT RPL=(1)             GET TO IT
AC       DS    0H
         L     R1,RPL5
         GET   RPL=(1)             AND READ A HEADER RECORD
         LTR   R15,R15             ANYTHING LEFT ??
         BNZ   NEXTHDR             NOPE; ALL DONE
         CLI   RECTT,#RTHDR        HEADER RECORD ??
         BNE   NEXTHDR             NOPE; ALL DONE ---
         #IF   RSRALIAS,OFF,AC     NOT AN ALIAS RECORD
         CLC   RECAN(44),BLDLAREA+1 IS THIS ONE WE COPY ??
         BNE   AC                  NOPE ---
         SHOWCB RPL=(*,RPL5),                                          X
               FIELDS=RECLEN,                                          X
               AREA=(S,VS2LEN2),                                       X
               LENGTH=4,                                               X
               MF=(G,GENWK)        FETCH THE RECORD LENGTH
         MVC   0(RECKLEN,R2),0(R6) COPY THE ALIAS VALUES
         MODCB RPL=(*,RPL2),       RPL ADDRESS                         X
               OPTCD=(DIR,FKS,NUP,MVE,KEQ), OPTION CODES ---           X
               ARG=(2),            KEY ADDRESS                         X
               AREA=(2),           BUFFER ADDRESS                      X
               RECLEN=RECAVER+4-REC, RECORD LENGTH                     X
               MF=(G,GENWK)
AC2      DS    0H
         L     R1,RPL2
         PUT   RPL=(1)             WRITE THE UPDATED RECORD
         LTR   R15,R15             ANYTHING ??
         BZ    AC3                 NOPE; ALL OK SO FAR
         L     R0,RECVER-REC(R2)   LOAD VERSION NUMBER
         BCTR  R0,R0               INCREMENT BY ONE
         ST    R0,RECVER-REC(R2)   SAVE UPDATED VALUE
         B     AC2                 AND TRY AGAIN
AC3      DS    0H
         L     R15,ACNT            LOAD THE COUNTER
         LA    R15,1(,R15)         INCREMENT BY ONE
         ST    R15,ACNT            AND SAVE UPDATED COUNT
         #FORMAT 0(,R2),,AC4       OUTPUT THE 'ALIAS IS COPIED.' MSG
         #PUT  LINE
         B     AC                  LOOP FOR MORE ---
AC4      DC    AL1(AC6-AC5)
AC5      DC    C'ALIAS IS COPIED. (VERSION MAY BE ALTERED.)'
AC6      EQU   *
INMSG    DC    AL1(INEND-INMSG1)
INMSG1   DC    C' WAS COPIED TO ---   '
INEND    EQU   *
OUTMSG   DC    AL1(OUTEND-OUTMSG1)
OUTMSG1  DC    C' ON OUTPUT VSAM CLUSTER. '
OUTEND   EQU   *
         SPACE 3
*---------------------------------------------------------------------*
*---     IF WE REACH THIS LABEL, WE'VE GOT AT LEAST ONE RECORD     ---*
*---     THAT CANNOT BE MADE TO FIT IN THE OUTPUT ARCHIVE          ---*
*---     CLUSTER. ISSUE AN ERROR MESSAGE AND ERASE THE             ---*
*---     PARTIALLY-COPIED ITEM.                                    ---*
*---------------------------------------------------------------------*
TOOLONG  DS    0H
         L     R1,VS2BUF2          LOAD THE HEADER ADDRESS
         #FORMAT 0(,R1),,LNGREC
         #PUT  LINE                PRINT THE ERROR
         L     R1,VS1BUF1          POINT TO THE RECORD KEY
         CALL  ARCHERAS            DELETE PARTIAL ITEM
         B     NEXTHDR             GO FOR ANOTHER ITEM
LNGREC   DC    AL1(LNGRECE-LNGRECS)
LNGRECS  DC    C' CANNOT BE COPIED. AT LEAST ONE RECORD TOO LONG.'
LNGRECE  EQU   *
ENDITALL DS    0H
         #PUT  ERR12
         L     R0,MCNT             COPY MEMBER COUNT
         LTR   R0,R0               ZERO ??
         BZ    COEND               YES ---
         CVD   R0,DWORK            DECIMALIZE IT
         MVC   LINE,MCMSG          COPY IN THE MESSAGE SKELETON
         ED    LINE+2(20),DWORK    ADD IN THE ACTUALS
         #PUT  LINE                AND OUTPUT THE RESULTS
         L     R0,ACNT             COPY MEMBER COUNT
         LTR   R0,R0               ZERO ??
         BZ    COEND               YES ---
         CVD   R0,DWORK            DECIMALIZE IT
         MVC   LINE,ACMSG          COPY IN THE MESSAGE SKELETON
         ED    LINE+2(20),DWORK    ADD IN THE ACTUALS
         #PUT  LINE                AND OUTPUT THE RESULTS
COEND    DS    0H
         L     R0,TOKEN            LOAD CPPB POINTER
         LTR   R0,R0               WAS THERE ANY ??
         BZ    COEND1              NOPE ---
         CALL  CELLDEL
         XC    TOKEN,TOKEN         CLEAR THAT POINTER
COEND1   DS    0H
         LA    R1,VSAM1DD          POINT TO THE DDNAME
         LA    R0,4                SET 'CLOSE' REQUEST
         CALL  ARCHVSOP            CALL THE VSAM 'CLOSE' ROUTINE
         LA    R1,VSAM2DD          POINT TO THE DDNAME
         LA    R0,4                SET 'CLOSE' REQUEST
         CALL  ARCHVSOP            CALL THE VSAM 'CLOSE' ROUTINE
         L     R13,4(,R13)         COPY BACK S.A. POINTER
         LM    R14,R12,12(R13)     RESTORE REGISTERS
         LA    R15,0               ALWAYS RC = 0
         MVI   12(R13),255
         BR    R14                 AND RETURN TO CALLER
         TITLE 'VSAM I/O OUTPUT '
*---------------------------------------------------------------------*
*---     THIS LITTLE LOCAL SUBROUTINE DOES ALL THE VSAM WRITING    ---*
*---     FOR THE COPY FUNCTION, WITH THE EXCEPTION OF THE ERASURE  ---*
*---     OF A DUPLICATE ITEM.  THE BASIC ASSUMPTIONS ARE THAT THE  ---*
*---     VS1BUF1 AREA IS THE RECORD TO BE WRITTEN AND ITS LENGTH   ---*
*---     IS ACCURATELY REPORTED IN THE VS1LEN1 FIELD.  THE         ---*
*---     INITIAL-LOAD CONDITION IS PROPERLY HANDLED HERE, IF       ---*
*---     NECESSARY.                                                ---*
*---------------------------------------------------------------------*
VSPUT    DS    0H
         STM   R14,R12,IOSAVE      SAVE ENTRY REGISTERS
         L     R4,RPL1             LOAD RPL ADDRESS
         LR    R5,R1               LOAD BUFFER ADDRESS
         LR    R6,R0               LOAD RECORD LENGTH
         MODCB RPL=(4),            RPL ADDRESS                         X
               OPTCD=(DIR,FKS,NUP,MVE,KEQ), OPTION CODES ---           X
               ARG=(5),            KEY ADDRESS                         X
               AREA=(5),           BUFFER ADDRESS                      X
               RECLEN=(6),         RECORD LENGTH                       X
               MF=(G,GENWK)
         PUT   RPL=(4)             OUTPUT THE VSAM RECORD
         LR    R3,R15              SAVE THAT RETURN CODE
         SHOWCB RPL=(4),                                               X
               FIELDS=FDBK,        FETCH THE RPL FEEDBACK FIELD        X
               AREA=(S,DWORK),                                         X
               LENGTH=4,                                               X
               MF=(G,GENWK)        GET RECORD LENGTH
         B     *+4(R3)             CHECK THE 'PUT' RETURN CODE
         B     VSPUT5              ALL IS OK
         B     VSPUT1              RPL SHOWS SOMETHING HAPPENING
         B     VSPUT3              LOGICAL ERROR
         B     VSPUTDIE            PHYSICAL ERROR
VSPUT1   DS    0H
         LA    R8,0                CLEAR A COUNTER REGISTER
VSPUT2   DS    0H
         CH    R8,=H'3'            PAST THE LIMIT YET ??
         BH    VSPUTDIE            YES ---
         LA    R8,1(,R8)           NOPE; INCREMENT COUNTER
         PUT   RPL=(4)             OUTPUT THE VSAM RECORD
         LR    R3,R15              SAVE THAT RETURN CODE
         SHOWCB RPL=(4),                                               X
               FIELDS=FDBK,        FETCH THE RPL FEEDBACK FIELD        X
               AREA=(S,DWORK),                                         X
               LENGTH=4,                                               X
               MF=(G,GENWK)        GET RECORD LENGTH
         B     *+4(R3)             CHECK THE 'PUT' RETURN CODE
         B     VSPUT5              ALL IS OK
         B     VSPUT2              RPL SHOWS SOMETHING HAPPENING
         B     VSPUT3              LOGICAL ERROR
         B     VSPUTDIE            PHYSICAL ERROR
VSPUT3   DS    0H                  LOGICAL ERROR HAS OCCURED
         CLI   DWORK+3,8           DUPLICATE KEY ??
         BNE   VSPUT4              NOPE ---
         LA    R15,4               LOAD THE RETURN CODE
         B     VSPUT5              AND TERMINATE
VSPUT4   DS    0H
         CLI   DWORK+3,116         INITIAL LOAD IN PROGRESS ??
         BNE   VSPUTDIE            NOPE; SOME OTHER ERROR
         MODCB RPL=(4),            RPL ADDRESS                         X
               OPTCD=(SEQ,NUP,MVE,KEQ), OPTION CODES ---               X
               MF=(G,GENWK)
         LR    R7,R15
         PUT   RPL=(4)             RE-DO THE PUT
         LTR   R15,R15             OK NOW ??
         BNZ   VSPUTDIE            NOPE; STILL SHAFTED
         CLOSE ,MF=(E,ACB1)        CLOSE THE CLUSTER
         OPEN  ,MF=(E,ACB1)        AND RE-OPEN THE CLUSTER
VSPUT5   DS    0H
         L     R14,IOSAVE          RELOAD R14
         LM    R0,R12,IOSAVE+8     RESTORE OTHER REGS
         BR    R14                 AND RETURN TO CALLER
VSPUTDIE DS    0H
         ABEND 100,DUMP
         LTORG
*---------------------------------------------------------------------*
*---     THIS ROUTINE WILL ADD THE 'REAL' NAME WE JUST LOADED TO   ---*
*---     A CHAIN OF REAL NAMES IN STORAGE.  THE CHAIN IS 'ROOTED'  ---*
*---     IN THE 'COUNT' FIELD OF THE ARCHIVER'S MAIN WORK AREA.    ---*
*---     THE PURPOSE OF THIS CHAIN IS TO PREVENT LOADING THE SAME  ---*
*---     ITEM MULTIPLE TIMES IF IT HAS MULTIPLE ALIASES AND WE'RE  ---*
*---     LOADING BASED ON ALIAS CHECKS AS WELL AS REAL NAME        ---*
*---     CHECKS.                                                   ---*
*---                                                               ---*
*---     MY USE OF CELL POOL MANAGEMENT HERE PRECLUDES USING THIS  ---*
*---     ROUTINE IN ANY MVS/370 SYSTEM.                            ---*
*---                                                               ---*
*---     REGISTER 1 IS ASSUMED TO POINT TO THE 44-BYTE QUALIFIER   ---*
*---     DATA TO BE ADDED TO THE CHAIN.                            ---*
*---------------------------------------------------------------------*
CHAINER  DS    0H
         STM   R14,R12,IOSAVE      SAVE THE ENTRY REGISTERS
         LR    R2,R1               COPY THE DATA POINTER
         NC    TOKEN,TOKEN         IS CELL POOL INIT'D ??
         BNZ   CHAIN1              YES ---
         CNOP  0,4
         BAL   R1,BLDPOOL          BRANCH AROUND PARM LIST
         DC    F'100'
         DC    F'100'
         DC    F'48'
         DC    AL1(14)
         DC    AL1(20)
         DC    CL20'REAL NAMES COPIED'
BLDPOOL  DS    0H
         CALL  CELLBLD
         ST    R0,TOKEN            SAVE THE 'TOKEN' VALUE
CHAIN1   DS    0H
         L     R0,TOKEN            LOAD THE 'TOKEN' VALUE
         CALL  CELLGET             GO GRAB A Q'CELL
         XC    0(48,R1),0(R1)      CLEAR IT OUT
         MVC   0(4,R1),CHAIN       INSERT INTO THE
         ST    R1,CHAIN              CHAIN
         MVC   4(44,R1),0(R2)      COPY DATA INTO THE CHAIN
         LM    R14,R12,IOSAVE      RESTORE THE REGISTERS
         LA    R15,0               ZERO RETURN CODE
         BR    R14                 AND RETURN TO MY CALLER
         EJECT
*---------------------------------------------------------------------*
*---     THIS ROUTINE CHECKS THROUGH THE CHAIN BUILT BY 'CHAIN'    ---*
*---     TO SEE IF WE'VE ALREADY PROCESSED THE ITEM WHOSE          ---*
*---     QUALIFIERS ARE POINTED TO BY REGISTER 1.  IF A MATCH IS   ---*
*---     FOUND, A RETURN CODE OF 4 IS SET IN REGISTER 15, ELSE     ---*
*---     THE RETURN CODE IS ZERO.                                  ---*
*---------------------------------------------------------------------*
CHKCHN   DS    0H
         STM   R14,R2,IOSAVE       SAVE THE ENTRY REGISTERS
         MVC   IOSAVE+4(4),=F'4'   SET INITIAL RETURN CODE
         L     R2,CHAIN            LOAD THE FIRST LINK POINTER
         B     CHKCHN2             AND GO CHECK FOR ANY LINK
CHKCHN1  DS    0H
         CLC   4(44,R2),0(R1)      DOES IT MATCH ??
         BE    CHKCHN3             YES; ERROR RETURN CODE
         L     R2,0(,R2)           POINT TO NEXT LINK
CHKCHN2  DS    0H
         LTR   R2,R2               ANOTHER LINK ??
         BNZ   CHKCHN1             YES ---
         XC    IOSAVE+4(4),IOSAVE+4  NOPE; SET ZERO RETURN CODE
CHKCHN3  DS    0H
         LM    R14,R2,IOSAVE       RESTORE REGISTERS
         BR    R14                 AND RETURN TO CALLER
ERR12    DC    CL133'0             COPY PROCESSING COMPLETED.  TOTALS FX
               OLLOW: '
MCMSG    DC    CL133' '
         ORG   MCMSG+2
         DC    X'40202020'
         DC    X'6B202020'
         DC    X'6B202020'
         DC    X'6B202020'
         DC    X'6B202120'
         DC    C' TOTAL ITEM(S) PROCESSED.'
         ORG
ACMSG    DC    CL133' '
         ORG   ACMSG+2
         DC    X'40202020'
         DC    X'6B202020'
         DC    X'6B202020'
         DC    X'6B202020'
         DC    X'6B202120'
         DC    C' TOTAL ALIAS(ES) PROCESSED.'
         ORG
SEP      DC    C' '
         DC    132C'-'
         COPY  #RECS
         COPY  ##ARCHWK
         END
