         MACRO
&LBL     #SETFLAG &BIT=,&MSGVAL=,&KWG=
         LCLC  &A,&L1,&L2,&L3,&L4
         SPACE 2
&A       SETC  '&LBL'
         AIF   ('&A' EQ '').NOLBL
&LBL     DS    0H
.NOLBL   ANOP
         AIF   ('&KWG' EQ '').NOG0
         #SET  &KWG,OFF
.NOG0    ANOP
&L1      SETC  '#SW&SYSNDX.A'
&L2      SETC  '#SW&SYSNDX.B'
&L3      SETC  '#SW&SYSNDX.C'
&L4      SETC  '#SW&SYSNDX.D'
         LH    R1,PRSKWLN          LOAD THE LENGTH VALUE
         LTR   R1,R1               ANYTHING SUPPLIED ??
         BZ    &L3                 NOPE ---
         L     R1,PRSKWLOC         POINT TO THE VALUE
         CLI   0(R1),C'Y'          SHORT FORM ??
         BE    &L2                 YES; STILL DEFAULT
         CLI   0(R1),C'N'          NEGATIVE VALUE ??
         BE    &L1                 YES ---
         #PUT  &L4
         #PUT  IGNORED             SHOW KEYWORD WAS IGNORED
         OI    WKRC,8              SET RETURN CODE              @RP2008
         B     &L3                 AND SKAT ??
&L4      DC    CL133'  ARCHPARS:  INVALID VALUE FOR &MSGVAL KEYWORD.'
&L1      DS    0H
         #SET  &BIT,OFF
         AIF   ('&KWG' EQ '').NOG1
         #SET  &KWG,ON
.NOG1    ANOP
         B     &L3
&L2      DS    0H
         #SET  &BIT,ON
         AIF   ('&KWG' EQ '').NOG2
         #SET  &KWG,ON
.NOG2    ANOP
&L3      DS    0H
         SPACE 2
         MEND
