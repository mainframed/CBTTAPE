/**rexx**/
/*  Program-id           SVCR                                      */
/*  Remarks              This REXX EXEC will display the entries   */
/*                       in the SVC update recording table.        */
/*                       The following information is displayed    */
/*                       for each updated SVC:                     */
/*                       1. SVC Number.                            */
/*                       2. The old SVC Table Entry.               */
/*                       3. The new SVC EP address.                */
/*                       4. Date the SVC table was updated.        */
/*                       5. The number of updates for the SVC.     */
/*                       6. Suffix of the IEASVCxx parmlib member, */
/*                          if the SVC table entry was replaced    */
/*                          using this method.                     */
/*                       When the table is displayed, the entries  */
/*                       can be selected to display the first 128  */
/*                       bytes of the SVC module storage.          */
/*trace i*/                             /* trace option            */
call house_keeping                      /* let's initialize        */
do forever
   call get_the_svcr_address            /* svc upd recording table */
   call display_info_panel              /* display info panel      */
   call build_the_svcr_entries          /* build the svcr entries  */
   call display_the_svcr_entries        /* display the svcr entries*/
   exit(0)                              /* let's get out           */
end
get_the_svcr_address:
cvt=         c2x(storage(10,4))         /* cvt address             */
cvtabend=    d2x((x2d(cvt))+200)        /* cvt + 200 = cvtabend    */
scvt=        c2x(storage(cvtabend,4))   /* scvt @                  */
scvtsvcr=    d2x(x2d(scvt)+136)         /* scvt + 136 = scvtsvcr   */
svcrtaddr=   c2x(storage(scvtsvcr,4))   /* svct table @            */
svcr@=       svcrtaddr                  /* store for panel output  */
return                                  /* return to the caller    */
build_the_svcr_entries:
do i = 0 to 255                         /* process all svcr entries*/
   svcr.i= storage(svcrtaddr,24)        /* pick up the svcr entry  */
   svcrtaddr= d2x(x2d(svcrtaddr)+24)    /* next svcr table entry   */
end
ADDRESS "ISPEXEC" "TBCREATE SVCRTAB NOWRITE REPLACE"
if (rc >  4) then do                    /* call okay?              */
   say 'TBCREATE error    rc = 'rc''    /* no- inform the user     */
   call dealloc                         /* remove allocation       */
   exit(0)                              /* let's quit              */
end
ssel=       '_'                         /* init                    */
svcrtadr=  svcr@                        /* svcr table address      */
ztdmark= '**************** End Of SVC Update Recording Table *****************'
ADDRESS "ISPEXEC" "VPUT (ztdmark) SHARED"
tabrows=  256                           /* max table rows          */
svcnos=   0                             /* count the no of svcs    */
do i= 0 to 255                          /* add the esr svcs to an  */
   if substr(svcr.i,1,16)= '00000000000000000000000000000000'x then
      iterate
   svcnos= (svcnos + 1)                 /* no of updated svcs      */
   svcnum= i                            /* svc number              */
   svcurold= c2x(substr(svcr.i,1,8))    /* old svc table entry     */
   svcurret= c2x(substr(svcr.i,9,4))    /* ret @ in caller        */
   svcurnew= c2x(substr(svcr.i,13,4))   /* new svc ep @           */
   svcurdat= c2x(substr(svcr.i,17,4))   /* cvtdate for update     */
   svcurcnt= c2x(substr(svcr.i,21,2))   /* count of updates for svc*/
   svcursx=  substr(svcr.i,23,2)        /* suffix of ieasvcxx     */
   ADDRESS "ISPEXEC" "TBADD SVCRTAB     /* add the entries        */
            SAVE(svcnum,svcurold,svcurret,svcurnew,svcurdat,svcurcnt,
            svcursx)
            MULT("TABROWS")"
   if (rc \= 0) then do                 /* call okay?             */
      say 'TBADD error    rc = 'rc''    /* no- inform the user    */
      call dealloc                      /* remove allocation      */
      exit(0)                           /* let's quit             */
   end
end
ADDRESS "ISPEXEC" "TBTOP SVCRTAB"       /* position to top of tab */
if (rc \= 0) then do                    /* call okay?             */
   say 'TBTOP error    rc = 'rc''       /* no- inform the user    */
   call dealloc                         /* remove allocation      */
   exit(0)                              /* let's quit             */
end
return
display_the_svcr_entries:
ADDRESS "ISPEXEC" "ADDPOP POPLOC(ZCMD)" /* pop-up position        */
ADDRESS "ISPEXEC" "TBDISPL SVCRTAB PANEL(SVCRPAN1)"
if (rc > 8) then do                     /* error?                 */
   say 'TBDISPL error    rc = 'rc''     /* yes- output message    */
   call dealloc                         /* remove allocation      */
   exit(0)                              /* and quit               */
end
if (rc = 8) then do                     /* end?                   */
   ADDRESS "ISPEXEC" "REMPOP"           /* remove pop-up          */
   call dealloc                         /* remove allocation      */
   return                               /* and quit               */
end
do while (ztdsels > 0)                  /* svc selection loop     */
   address "ISPEXEC" "CONTROL DISPLAY SAVE" /* Save The Environment */
   if (rc \= 0) then do                 /* save okay?             */
      say 'CONTROL SAVE error  rc = 'rc''
      call dealloc                      /* Dealloc                */
      exit(0)                           /* and quit               */
   end
   if (ssel = '/') then do              /* user selected a svc?   */
      call display_svc_storage          /* display the svc storage*/
   end
   address "ISPEXEC" "CONTROL DISPLAY RESTORE" /* Restore the envirn*/
   if (rc \= 0) then do                 /* save okay?             */
      say 'CONTROL RESTORE error  rc = 'rc''
      call dealloc                      /* Dealloc                */
      exit(0)                           /* and quit               */
   end
   ssel= '_'                             /* reset the sel ind      */
ztdmark= '**************** End Of SVC Update Recording Table *****************'
   ADDRESS "ISPEXEC" "TBDISPL SVCRTAB"
   if (rc = 8) then do                   /* pf3 or return?         */
      ADDRESS "ISPEXEC" "REMPOP"         /* remove pop-up          */
      return                             /* and quit               */
   end
   if (rc > 8) then do
      say 'TBDISPL error  rc = 'rc''
      call dealloc                       /* Dealloc                */
      exit(0)                            /* and quit               */
   end
end
ADDRESS "ISPEXEC" "REMPOP"               /* remove menu pop-up     */
return
display_svc_storage:
ADDRESS "ISPEXEC" "TBCREATE SVCSTOR NOWRITE REPLACE"
if (rc >  4) then do                     /* call okay?             */
   say 'TBCREATE error    rc = 'rc''     /* no- inform the user    */
   call dealloc                          /* remove allocation      */
   exit(0)                               /* let's quit             */
end
ztdmark= ''                              /* set to null            */
ADDRESS "ISPEXEC" "VPUT (ztdmark) SHARED"
tabrows=  20                             /* max table rows         */
svc_storage= storage(svcurnew,128)       /* 128 bytes of storage   */
x= 1                                     /* starting position      */
do i= 1 to 8                             /* the number of lines    */
   modhex=  c2x(substr(svc_storage,x,4)) /* first four bytes       */
   modhex=  modhex' '                    /* blank                  */
   modhex=  modhex''c2x(substr(svc_storage,x+4,4))
   modhex=  modhex' '                    /* blank                  */
   modhex=  modhex''c2x(substr(svc_storage,x+8,4))
   modhex=  modhex' '                    /* blank                  */
   modhex=  modhex''c2x(substr(svc_storage,x+12,4))
   modchar= substr(svc_storage,x,16)     /* 16 bytes char          */
   ADDRESS "ISPEXEC" "TBADD SVCSTOR      /* add the entries        */
            SAVE(modhex,modchar)
            MULT("TABROWS")"
   if (rc \= 0) then do                  /* call okay?             */
      say 'TBADD error    rc = 'rc''     /* no- inform the user    */
      call dealloc                       /* remove allocation      */
      exit(0)                            /* let's quit             */
   end
   x= (x + 16)                           /* next entry             */
end
ADDRESS "ISPEXEC" "TBTOP SVCSTOR"        /* position to top of tab */
if (rc \= 0) then do                     /* call okay?             */
   say 'TBTOP error    rc = 'rc''        /* no- inform the user    */
   call dealloc                          /* remove allocation      */
   exit(0)                               /* let's quit             */
end
ADDRESS "ISPEXEC" "ADDPOP POPLOC(ZCMD)" /* pop-up position         */
ADDRESS "ISPEXEC" "TBDISPL SVCSTOR PANEL(SVCRPAN2)"
if (rc > 8) then do                     /* error?                  */
   say 'TBDISPL error    rc = 'rc''     /* yes- output message     */
   call dealloc                         /* remove allocation       */
   exit(0)                              /* and quit                */
end
ADDRESS "ISPEXEC" "REMPOP"              /* remove pop-up           */
return
display_info_panel:
ADDRESS "ISPEXEC" "CONTROL DISPLAY LOCK" /* lock the terminal      */
if (rc \= 0) then do                     /* error?                 */
   say 'SUBSYS Lock error    rc = 'rc''  /* yes- output message    */
   call dealloc                          /* dealloc                */
   exit(0)                               /* and quit               */
end
svcinfo= 'Your Request Is Being Processed Please Wait'
ADDRESS "ISPEXEC" "ADDPOP ROW(1) COLUMN(1)" /* pop-up position     */
ADDRESS "ISPEXEC" "DISPLAY PANEL(svcrpan3)" /* position the cursor */
if (rc \= 0) then do                    /* error?                  */
   ADDRESS "ISPEXEC" "REMPOP"           /* remove pop-up           */
   say 'Display error    rc = 'rc''
   call dealloc                         /* Dealloc                 */
   exit(0)                              /* and quit                */
end
ADDRESS "ISPEXEC" "REMPOP"              /* remove pop-up           */
return
house_keeping:
/*                                                                 */
/* Set the error mode                                              */
/*                                                                 */
address "ISPEXEC" "CONTROL ERRORS CANCEL"
/*                                                                 */
/* dynamically allocate the panel libraries                        */
/*                                                                 */
/* --------------------------------------------------------------- */
/* Change the next line to point to your panel dataset in ISPPLIB. */
/* --------------------------------------------------------------- */
address "ISPEXEC" "LIBDEF ISPPLIB DATASET ID('SYS1.U.PANELS')"
return
dealloc:
address "ISPEXEC" "LIBDEF ISPPLIB"
return
