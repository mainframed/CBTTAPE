PRTPDS   TITLE '**** PDS PRINT UTILITY ****'
***********************************************************************
*    NAME - PRTPDS                                                    *
*                                                                     *
*    AUTHOR - TED BESTANI                                             *
*                                                                     *
*    PURPOSE - THIS PROGRAM PRINTS CARD IMAGE PDS'S.                  *
*                                                                     *
*    ATTRIBUTES - NON-REUSABLE                                        *
*                                                                     *
*    DDNAMES - PDS      - INPUT PDS TO BE PRINTED                     *
*              DIRECT   - OUTPUT LISTING OF THE DIRECTORY             *
*              REPORT   - OUTPUT LISTING OF THE PDS                   *
*                                                                     *
*    PARMS - RUN TIME PARAMETERS ARE OF TWO TYPES, THOSE THAT         *
*            ACTIVATE BIT SWITCHES ONLY, AND THOSE THAT HAVE VALUES   *
*            ASSOCIATED WITH THEM.  THE PARAMETERS ARE:               *
*                                                                     *
*            1) COL(XX) - A ONE OR TWO DIGIT NUMBER WHICH INDICATES   *
*               THE COLUMN TO BEGIN PRINTING IN.  THE DEFAULT IS 27.  *
*            2) PRTASM - IF THIS PARM IS SPECIFIED, ASSEMBLER EJECT   *
*               AND SPACE PRINT COMMANDS WILL BE EXPANDED.  THE       *
*               DEFAULT IS NO.                                        *
*            3) DIRONLY - IF THIS PARM IS SPECIFIED, ONLY THE PDS     *
*               DIRECTORY WILL BE PRINTED, AND NO ATTEMPT WILL BE     *
*               MADE TO OPEN THE REPORT OUTPUT DATASET.  THE DEFAULT  *
*               IS NO, WHICH MEANS PRINT THE FULL REPORT.             *
*            4) RANGE(A,B) - IF THIS IS SPECIFIED, PRTPDS WILL PRINT  *
*               ONLY THE MEMBERS THAT EXIST IN THE RANGE BETWEEN "A"  *
*               AND "B", INCLUSIVE.  BOTH "A" AND "B" MUST EXIST, OR  *
*               PRTPDS WILL TURN THE "DIRONLY" OPTION ON.  THE        *
*               DEFAULT FOR THIS OPTION IS "ALL".  "A" AND "B" CAN    *
*               BE VARIABLE LENGTH, FROM 1 TO 8 CHARACTERS, AND MUST  *
*               BE SEPARATED BY A COMMA.                              *
*            5) DWIDTH(X) - A ONE DIGIT NUMBER WHICH INDICATES THE    *
*               NUMBER OF DIRECTORY ELEMENTS TO PRINT PER LINE ON     *
*               THE DIRECTORY LISTING.  THE DEFAULT (ALSO MAX) IS 6.  *
*               THE MINIMUM IS 1.  USE "DWIDTH(3)" IF YOU LIKE TO     *
*               CUT YOUR LISTINGS DOWN TO BINDER SIZE.                *
*            6) LINES(XX) - A TWO DIGIT NUMBER WHICH INDICATES THE    *
*               NUMBER OF LINES YOU WISH PRINTED PER PAGE.  THE       *
*               DEFAULT IS 59, AND THE MINIMUM IS 20.                 *
*            7) FIND(X) - UP TO TWENTY CHARACTER SEARCH STRING        *
*            8) FINDSAVE - SAVE FOUND INFO IN DATASET FINDSAVE        *
*                                                                     *
*    EXAMPLES:                                                        *
*                                                                     *
*    //LIST1 EXEC PGM=PRTPDS,PARM='COL(1),PRTASM,DWIDTH(3)'           *
*    //LIST2 EXEC PGM=PRTPDS,PARM='COL(1),RANGE(IEAIPS00,LNKLST00)'   *
*    //LIST3 EXEC PGM=PRTPDS,PARM='DIRONLY'                           *
*                                                                     *
***********************************************************************
         EJECT
         PRINT ON,NOGEN,NODATA
PRTPDS   CSECT
         STM   R14,R12,12(R13)         SAVE CALLERS REGISTERS
         BALR  R9,0                    WHERE ARE WE?
         USING *,R9                    TEMPORARY BASE REGISTER
         ST    R13,SAVEAREA+4          SAVE BACKWARD SA PTR
         LA    R8,SAVEAREA             GET SA ADDR
         ST    R8,8(R13)               SAVE FORWARD SA PTR
         LR    R13,R8                  COPY SA ADDR
         LA    R12,4095(R13)           BASE2 = BASE1 + 4095 +
         LA    R12,1(R12)                                     1
         LA    R11,4095(R12)           BASE3 = BASE2 + 4095 +
         LA    R11,1(R11)                                     1
         LA    R10,4095(R11)           BASE4 = BASE3 + 4095 +
         LA    R10,1(R10)                                     1
         USING SAVEAREA,R13,R12,R11,R10    PERMANENT BASE REGISTERS
         DROP  R9                      DROP TEMPORARY BASE REGISTER
         SPACE 1
         BAL   R9,DRIVER               CALL MAIN DRIVER ROUTINE
         SPACE 1
         L     R13,SAVEAREA+4          GET BACKWARD SA PTR
         LM    R14,R12,12(R13)         RESTORE REGISTERS
         LA    R15,0                   SET RC=0
         BR    R14                     GO HOME......
         SPACE 1
SAVEAREA DS    18F    PROGRAM MAIN SAVE AREA
         SPACE 1
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
***********************************************************************
*     THIS IS THE HIGHEST LEVEL WORK ROUTINE IN THE PROGRAM.  IT      *
*   DIRECTS THE FLOW OF CONTROL TO THE WORK ROUTINES.                 *
*     THE PROGRAM IS WRITTEN USING BASIC STRUCTURED PROGRAMMING. FLOW *
*   BETWEEN ROUTINES IS ACCOMPLISHED VIA REGISTER 9, WHICH IS ALWAYS  *
*   SAVED UPON ENTRY AND RESTORED BEFORE EXIT.                        *
***********************************************************************
         SPACE 2
DRIVER   DS    0H
         ST    R9,SAVE10
         B     BSAVE10
SAVE10   DC    F'-1'
         DC    CL8'DRIVER  '
BSAVE10  EQU   *
         SPACE 1
         BAL   R9,OPTIONS              CHECK PARMS FOR VALID OPTIONS
         BAL   R9,GETDATE              GET AND FORMAT DATE AND TIME
         BAL   R9,READDIR              READ DIRECTORY AND BUILD BLDL
         TM    OPTBYTE,FINDSVSW        DID USER REQUEST A FIND SAVE?
         BNO   BYPJCLGN                NO, BYPASS JCL GENERATION
         BAL   R9,CHGROUP              INVOKE FIRST TIME TO GEN JCL
BYPJCLGN EQU   *
         TM    OPTBYTE,RANGESW         DID USER REQUEST A RANGE?
         BNO   BYPRNGER                NO, BYPASS RANGER ROUTINE
         BAL   R9,RANGER               PARSE RANGE PARM AND BUILD PTRS
BYPRNGER EQU   *
         TM    OPTBYTE,DIRONLSW        DID USER WANT DIRECTORY ONLY?
         BO    BYPRPRT                 YES, BYPASS REPORT
         BAL   R9,PRTLIST              PRINT MEMBERS
BYPRPRT  EQU   *
         BAL   R9,PRTDIR               PRINT DIRECTORY
         SPACE 1
EXIT10   EQU   *
         L     R9,SAVE10
         BR    R9
         EJECT
***********************************************************************
*     THIS ROUTINE SCANS THE INPUT PARMS FOR VALID OPTIONS.           *
***********************************************************************
         SPACE 2
OPTIONS  DS    0H
         ST    R9,SAVE15
         B     BSAVE15
SAVE15   DC    F'-1'
         DC    CL8'OPTIONS '
BSAVE15  EQU   *
         SPACE 1
*------> INITIALIZE FOR OPTION SCAN
         L     R1,0(R1)                ADDR OF PARM LIST
         LH    R2,0(R1)                LENGTH OF PARM
         CH    R2,=H'6'                IS IT THE MINIMUM LENGTH?
         BL    EXIT15                  NO, EXIT ROUTINE
         LA    R3,2(R1)                BUMP TO START OF PARM
         LR    R4,R3                   COPY START ADDR
         AR    R4,R2                   ADD LENGTH
*------> CHECK FOR PRESENCE OF OPTIONS IN PARAMETER STRING
CHKOPT   EQU   *
         CR    R3,R4                   SCAN OVER?
         BNL   EXIT15                  YES, EXIT ROUTINE
         CLC   0(4,R3),=CL4'COL('      USER WANTS HER OWN COLUMNS?
         BE    CHKCOL                  YES, GO HANDLE COLUMNS
         CLC   0(6,R3),=CL6'PRTASM'    USER WANTS EJECTS EXPANDED?
         BE    CHKEJECT                YES, GO HANDLE PRTASM
         CLC   0(7,R3),=CL7'DIRONLY'   USER WANTS DIRECTORY ONLY?
         BE    CHKDIRON                YES, GO HANDLE DIRONLY
         CLC   0(6,R3),=CL6'RANGE('    USER WANTS TO SPECIFY RANGE
         BE    CHKRANGE                YES, GO HANDLE RANGE
         CLC   0(7,R3),=CL7'DWIDTH('   USER WANTS TO SPECIFY DIR WIDTH
         BE    CHKWIDTH                YES, GO HANDLE DIR WIDTH
         CLC   0(6,R3),=CL6'LINES('    USER WANTS TO SPECIFY PAGE SIZE
         BE    CHKLINES                YES, GO HANDLE PAGE SIZE
         CLC   0(5,R3),=CL6'FIND('     USER WANTS TO FIND STRING
         BE    CHKFINDR                YES, GO HANDLE FIND STRING
         CLC   0(8,R3),=CL8'FINDSAVE'  USER WANTS TO SAVE FIND STRINGS
         BE    CHKFINDS                YES, GO HANDLE FIND STRING SAVE
CHKOPT2  EQU   *
         LA    R3,1(R3)                BUMP INDEX
         B     CHKOPT                  CHECK NEXT POSITION
*------> PROCESS COL(XX) OPTION
CHKCOL   EQU   *
         TRT   4(2,R3),TRTNUM          IS IT NUMERIC?
         BC    7,CHKCOL2               NO, CHECK FOR 1 DIGIT ONLY
         PACK  WORKDBLW,4(2,R3)        PACK THE NUMBER
         B     CHKCOL3                 GO AROUND
CHKCOL2  EQU   *
         TRT   4(1,R3),TRTNUM          IS IT ONLY ONE DIGIT?
         BC    7,COLBAD1               NO, ISSUE ERROR MESSAGE
         PACK  WORKDBLW,4(1,R3)        PACK 1 DIGIT ONLY
CHKCOL3  EQU   *
         CVB   R2,WORKDBLW             CONVERT TO HEX
         CH    R2,=H'51'               IS IT BEYOND LIMITS?
         BH    COLBAD2                 YES, ISSUE ERROR MESSAGE
         STH   R2,OFFSET               PUT IT IN THE OFFSET COUNTER
         B     CHKOPT2                 GO CHECK FOR NEXT OPTION
COLBAD1  EQU   *
         WTO   'COLUMN NOT NUMERIC - USING DEFAULT'
         B     EXIT15
COLBAD2  EQU   *
         WTO   'COLUMN NOT WITHIN RANGE - USING DEFAULT'
         B     EXIT15
*------> PROCESS PRTASM OPTION
CHKEJECT EQU   *
         OI    OPTBYTE,PRTASMSW        TURN THE PRTASM OPTION ON
         B     CHKOPT2                 GO CHECK FOR NEXT OPTION
*------> PROCESS DIRONLY OPTION
CHKDIRON EQU   *
         OI    OPTBYTE,DIRONLSW        TURN THE DIRONLY OPTION ON
         B     CHKOPT2                 GO CHECK FOR NEXT OPTION
*------> PROCESS FINDSAVE OPTION
CHKFINDS EQU   *
         OI    OPTBYTE,FINDSVSW        TURN THE FINDSAVE OPTION ON
         B     CHKOPT2                 GO CHECK FOR NEXT OPTION
*------> PROCESS RANGE OPTION
CHKRANGE EQU   *
         OI    OPTBYTE,RANGESW         TURN THE RANGE OPTION ON
         ST    R3,RANGEPTR             SAVE PTR TO BEGINNING OF STRING
         B     CHKOPT2                 GO CHECK FOR NEXT OPTION
*------> PROCESS DWIDTH(X) OPTION
CHKWIDTH EQU   *
         TRT   7(1,R3),TRTNUM          IS IT NUMERIC?
         BC    7,WIDTHBAD              NO, ISSUE ERROR MESSAGE
         PACK  WORKDBLW,7(1,R3)        PACK THE NUMBER
         CVB   R2,WORKDBLW             CONVERT TO HEX
         CH    R2,=H'6'                IS IT BEYOND LIMITS?
         BH    WIDTHBAD                YES, ISSUE ERROR MESSAGE
         CH    R2,=H'1'                IS IT BEYOND LIMITS?
         BL    WIDTHBAD                YES, ISSUE ERROR MESSAGE
         STH   R2,DWIDTH               SAVE IT FOR LATER USE
         B     CHKOPT2                 GO CHECK FOR NEXT OPTION
WIDTHBAD EQU   *
         WTO   'PRTPDS - DIR WIDTH ERROR - USING DEFAULT'
         B     EXIT15
*------> PROCESS LINES(XX) OPTION
CHKLINES EQU   *
         TRT   6(2,R3),TRTNUM          IS IT NUMERIC?
         BC    7,LINESBAD              NO, ISSUE ERROR MESSAGE
         PACK  WORKDBLW,6(2,R3)        PACK THE NUMBER
         CP    WORKDBLW,=PL2'20'       IS IT BELOW LIMITS?
         BL    LINESBAD                YES, ISSUE ERROR MESSAGE
         ZAP   PAGESIZE,WORKDBLW       SAVE IT FOR LATER USE
         SP    PAGESIZE,=PL1'3'        SUBTRACT 3 FOR HEADERS
         B     CHKOPT2                 GO CHECK FOR NEXT OPTION
LINESBAD EQU   *
         WTO   'PRTPDS - ERROR IN LINES PARAMETER - USING DEFAULT'
         B     EXIT15
*------> PROCESS FIND(XX) OPTION
CHKFINDR EQU   *
         OI    OPTBYTE,FINDSW          TURN ON THE FIND STRING SWITCH
         LA    R4,20                   MAXIMUM 20 CHARACTER STRING
         LR    R5,R3                   COPY STARTING POINT
         LA    R5,5(R5)                POINT PAST STRING
CHKFIND2 EQU   *
         CLI   0(R5),C')'              FIND THE RIGHT PAREN
         BE    CHKFIND3                GOT IT, GO MOVE IT
         LA    R5,1(R5)                BUMP
         BCT   R4,CHKFIND2             ITERATE
         WTO   'PRTPDS - ERROR IN FIND PARAMETER'
         B     CHKOPT2                 GO CHECK NEXT PARAMETER
CHKFIND3 EQU   *
         S     R4,=F'20'               SUBTRACT MAX LENGTH
         MH    R4,=H'-1'               CHANGE SIGN
         BCTR  R4,0                    DECREMENT FOR MOVE
         STH   R4,FINDLNTH             SAVE LENGTH
         EX    R4,FNDEXMVC             MOVE THE STRING
         OC    FINDSTR,=CL20' '        CONVERT TO UPPER CASE
         B     CHKOPT2                 GO CHECK NEXT PARAMETER
FNDEXMVC MVC   FINDSTR(1),5(R3)        MOVE THE FINDER STRING
         SPACE 1
EXIT15   EQU   *
         L     R9,SAVE15
         BR    R9
         EJECT
***********************************************************************
*   THIS ROUTINE OBTAINS AND FORMATS THE CURRENT DATE AND TIME.       *
***********************************************************************
         SPACE 1
GETDATE  DS    0H
         ST    R9,SAVE17
         B     BSAVE17
SAVE17   DC    F'-1'
         DC    CL8'GETDATE '
BSAVE17  EQU   *
         SPACE 1
         TIME  DEC                     GET THE TIME IN DECIMAL
*--> FORMAT THE CURRENT TIME
         STM   R0,R1,WORKDBLW          STORE THE RESULTS
         UNPK  WORKTIME,WORKDBLW(4)    UNPACK THE TIME
         OI    WORKTIME+7,X'F0'        CLEAR THE SIGN NIBBLE
         MVC   TIMEHH,WORKTIME+1       MOVE THE HH
         MVC   TIMEMM,WORKTIME+3                MM
         MVC   TIMESS,WORKTIME+5                SS
*--> FORMAT THE CURRENT DATE
         MVC   DIVFIELD+0(2),=XL2'0019'    MOVE ZEROES + 19
         MVC   DIVFIELD+2(1),WORKDBLW+5    MOVE YEAR
         MVI   DIVFIELD+3,X'0F'            MOVE SIGN NIBBLE
         DP    DIVFIELD,=PL2'40'       DIVIDE BY 4 X 10
         CP    DIVFIELD+2(2),=PL1'0'   IS IT A LEAP YEAR?
         BNE   NOTLEAP                 NO, DON'T CHANGE TABLE
         ZAP   MONTHTBL+2(2),=PL2'29'  FEBRUARY
NOTLEAP  EQU   *
         LA    R4,MONTHTBL             GET ADDRESS OF MONTHTBL
         LA    R5,12                   NUMBER OF MONTHS FOR BCT
         ZAP   MONTH,=PL1'1'           SOMEPLACE TO START
MONTH1   EQU   *
         CP    WORKDBLW+6(2),0(2,R4)   NUMBER OF DAYS IN MONTH
         BNH   MONTHEND                LESS THAN MONTH, GO FORMAT
         SP    WORKDBLW+6(2),0(2,R4)   MORE THAN A MONTH
         AP    MONTH,=PL1'1'           ADD 1 TO MONTH
         LA    R4,2(R4)                BUMP MONTH POINTER
         BCT   R5,MONTH1               GO TRY NEXT MONTH
MONTHEND EQU   *
         LA    R3,DATE                 ADDRESS OF DATE FIELD
         UNPK  DATEMM,MONTH            UNPACK MONTH
         OI    1(R3),X'F0'             CLEAR SIGN NIBBLE
         UNPK  DATEDD,WORKDBLW+6(2)    MOVE THE DAY
         OI    4(R3),X'F0'             CLEAR SIGN NIBBLE
         UNPK  WORKTIME+0(3),WORKDBLW+5(2) UNPACK THE YEAR
         OI    WORKTIME+2,X'F0'        CLEAR SIGN NIBBLE
         MVC   DATEYY,WORKTIME         MOVE THE YEAR
         SPACE 1
EXIT17   EQU   *
         L     R9,SAVE17
         BR    R9
         EJECT
***********************************************************************
*    THE PURPOSE OF THIS ROUTINE IS TO READ THE DIRECTORY OF THE      *
*  NAMED PDS INTO MEMORY, IN THE PROCESS BUILDING A BLDL LIST WHICH   *
*  WHICH WILL BE USED TO PRINT THE INDIVIDUAL MEMBERS.                *
*    THE ROUTINE ALSO ACQUIRES THE DSNAME OF THE PDS VIA A RDJFCB.    *
***********************************************************************
         SPACE 2
READDIR  DS    0H
         ST    R9,SAVE20
         B     BSAVE20
SAVE20   DC    F'-1'
         DC    CL8'READDIR '
BSAVE20  EQU   *
         SPACE 1
         OPEN  (INPUT,INPUT)
         RDJFCB INPUT                  PICK UP THE JFCB
         MVC   DSNAME,JFCBAREA         MOVE THE DSNAME
         LA    R6,BLDLLIST+4           POINT TO START OF BLDL LIST
         XR    R7,R7                   INIT MEMBER COUNT
READER   EQU   *
         GET   INPUT,WORKREC           READ DIRECTORY RECORD
         CLC   WORKREC+2(4),=F'-1'     NULL RECORD?
         BNE   BYPLASTR                NO, BYPASS CODE
         BCTR  R7,0                    NORMALIZE ELEMENT COUNTER
         B     ENDIRECT                GO TO END OF DIRECTORY
BYPLASTR EQU   *
         LA    R2,WORKREC+2            PICK UP FIRST PAST COUNT
         LA    R3,WORKREC              PICK UP FIRST
         LH    R4,WORKREC              PICK UP COUNT
         AR    R3,R4                   POINT TO END OF LOGICAL RECORD
SCANREC  EQU   *
         CLC   0(4,R2),=F'-1'          LAST MEMBER IN DIRECTORY?
         BE    ENDIRECT                SAME AS END-OF-FILE
         CR    R2,R3                   END OF LOGICAL RECORD?
         BNL   READER                  GO GET NEXT RECORD
         MVC   0(08,R6),0(R2)          MOVE MEMBER NAME TO BLDL LIST
         LA    R7,1(R7)                INCREMENT MEMBER COUNT
         XR    R5,R5                   CLEAR REGISTER
         IC    R5,11(R2)               INSERT USER COUNT FIELD
         L     R1,=X'0000001F'         INSERT CLEAR MASK
         NR    R5,R1                   CLEAR ALL BUT USER COUNT
         SLL   R5,1                    MULTIPLY BY 2
         AR    R2,R5                   ADD USER COUNT
         LA    R2,12(R2)               ADD PDS CONSTANT BASE
         LA    R6,12(R6)               BUMP PTR TO BLDL LIST
         B     SCANREC                 GO SCAN FOR NEXT ELEMENT
ENDIRECT EQU   *
         STH   R7,BLDLLIST             STORE # MEMBERS
         BLDL  INPUT,BLDLLIST          FILL IN TTR'S AND "C" FIELDS
         CLOSE (INPUT)                 CLOSE THE INPUT FILE
         SPACE 1
EXIT20   EQU   *
         L     R9,SAVE20
         BR    R9
         EJECT
***********************************************************************
*    THIS ROUTINE PARSES THE RANGE EXECUTION PARAMETER AND BUILDS THE *
*  NECESSARY POINTERS AND STORAGE AREAS TO BE USED LATER.  IF THERE   *
*  IS AN ERROR, THE "DIRONLY" OPTION IS TURNED ON AND A WTO OPERATOR  *
*  MESSAGE IS GENERATED.                                              *
***********************************************************************
         SPACE 2
RANGER   DS    0H
         ST    R9,SAVE25
         B     BSAVE25
SAVE25   DC    F'-1'
         DC    CL8'RANGER  '
BSAVE25  EQU   *
         SPACE 1
*------> FIND THE COMMA AND THE CLOSING PARENTHESES
         L     R2,RANGEPTR             GET ADDR OF RANGE STRING
         LA    R2,6(R2)                BUMP TO FIRST MEMBER NAME
         LA    R3,18(R2)               BUMP TO ENDING ADDRESS
RNGSCAN  EQU   *
         CR    R2,R3                   END OF STATEMENT?
         BE    RNGERROR                ERROR - GO HANDLE
         CLI   0(R2),C','              IS THIS THE COMMA?
         BE    RNGCOMMA                YES, GO STORE ADDR OF COMMA
         CLI   0(R2),C')'              IS THIS THE CLOSING PAREN?
         BE    RNGPAREN                YES, GO STORE ADDR OF PAREN
         LA    R2,1(R2)                BUMP INDEX
         B     RNGSCAN                 TRY NEXT BYTE
RNGCOMMA EQU   *
         ST    R2,RANGECOM             STORE ADDR OF COMMA
         LA    R2,1(R2)                BUMP INDEX
         B     RNGSCAN                 TRY NEXT BYTE
RNGPAREN EQU   *
         ST    R2,RANGEPAR             STORE ADDR OF PAREN
         B     RNGLENTH                GO CHECK LENGTHS
*------> IF AN ERROR OCCURS, COME HERE
RNGERROR EQU   *
         WTO   'PRTPDS - ERROR IN RANGE STATEMENT'
         OI    OPTBYTE,DIRONLSW        TURN ON DIRONLY SWITCH
         B     EXIT25
*------> CHECK LENGTHS AND CHECK FOR SPACES IN THE RANGE
RNGLENTH EQU   *
         L     R2,RANGEPTR             PTR TO BEGINNING OF STRING
         LA    R2,6(R2)                PTR TO FIRST MEMBER NAME
         L     R3,RANGECOM             PTR TO IMBEDDED COMMA
         SR    R3,R2                   GET LENGTH OF FIRST MEMBER
         CH    R3,=H'1'                IS IT LESS THAN 1?
         BL    RNGERROR                YES, THAT'S NO GOOD
         CH    R3,=H'8'                IS IT GREATER THAN 8?
         BH    RNGERROR                YES, THAT'S NO GOOD
         BCTR  R3,0                    SUBTRACT ONE FOR EX CLC
         EX    R3,RNGEXCL1             EXECUTE THE FIRST CLC
         L     R2,RANGECOM             PTR TO BEGINNING OF 2ND NAME
         LA    R2,1(R2)                BUMP PAST COMMA
         L     R3,RANGEPAR             PTR TO RIGHT PAREN
         SR    R3,R2                   GET LENGTH OF 2ND MEMBER
         CH    R3,=H'1'                IS IT LESS THAN 1?
         BL    RNGERROR                YES, THAT'S NO GOOD
         CH    R3,=H'8'                IS IT GREATER THAN 8?
         BH    RNGERROR                YES, THAT'S NO GOOD
         BCTR  R3,0                    SUBTRACT ONE FOR EX CLC
         EX    R3,RNGEXCL2             EXECUTE THE SECOND CLC
         B     RNGCHKNM                GO CHECK IF MEMBERS PRESENT
RNGEXCL1 MVC   RANGESNM(1),0(R2)       MOVE FIRST MEMBER NAME
RNGEXCL2 MVC   RANGEENM(1),0(R2)       MOVE SECOND MEMBER NAME
*------> CHECK LISTS TO SEE IF NAMED MEMBERS ARE PRESENT
RNGCHKNM EQU   *
         LH    R2,BLDLLIST             GET NUMBER OF MEMBERS
         LA    R3,BLDLLIST+4           GET ADDR OF 1ST MEMBER
RNGCNM1  EQU   *
         CLC   0(8,R3),RANGESNM        IS IT THE FIRST NAMED MEMBER?
         BE    RNGCNM2                 YES, CHECK SECOND
         LA    R3,12(R3)               POINT TO NEXT MEMBER
         BCT   R2,RNGCNM1              NO, CHECK NEXT
         B     RNGERR02                FELL THROUGH - ERROR
         LH    R2,BLDLLIST             GET NUMBER OF MEMBERS
         LA    R3,BLDLLIST+4           GET ADDR OF 1ST MEMBER
RNGCNM2  EQU   *
         CLC   0(8,R3),RANGEENM        IS IT THE SECOND NAMED MEMBER?
         BE    RNGCHKSQ                YES, CHECK SEQUENCE
         LA    R3,12(R3)               POINT TO NEXT MEMBER
         BCT   R2,RNGCNM2              NO, CHECK NEXT
         B     RNGERR02                FELL THROUGH - ERROR
RNGCHKSQ EQU   *
         CLC   RANGESNM,RANGEENM       IS FIRST BEFORE SECOND?
         BL    EXIT25                  YES, EXIT RTN
         WTO   'PRTPDS - 1ST RANGE MBR NOT GREATER THAN 2ND'
         OI    OPTBYTE,DIRONLSW        TURN ON DIRONLY SWITCH
         B     EXIT25
RNGERR02 EQU   *
         WTO   'PRTPDS - EITHER OR BOTH RANGE MBRS NOT FOUND'
         OI    OPTBYTE,DIRONLSW        TURN ON DIRONLY SWITCH
         SPACE 1
EXIT25   EQU   *
         L     R9,SAVE25
         BR    R9
         EJECT
***********************************************************************
*    THIS ROUTINE SCANS THE BLDL LIST AND PRINTS THE MEMBERS.         *
***********************************************************************
         SPACE 2
PRTLIST  DS    0H
         ST    R9,SAVE30
         B     BSAVE30
SAVE30   DC    F'-1'
         DC    CL8'PRTLIST '
BSAVE30  EQU   *
         SPACE 1
         OPEN  (PDS,INPUT,REPORT,OUTPUT)
         LH    R2,BLDLLIST             GET NUMBER OF MEMBERS
         LA    R3,BLDLLIST+4           GET FIRST MEMBER
         L     R7,=A(RECNTLST)         GET LIST OF RECCNT TABLE
         TM    OPTBYTE,RANGESW         WAS THE RANGE OPTION SELECTED?
         BNO   PRTLOOP1                NO, BYPASS
PRTCHK01 EQU   *
         CLC   0(8,R3),RANGESNM        IS THIS THE FIRST NAME YET?
         BE    PRTLOOP1                YES, GO START PROCESSING
         ZAP   8(4,R3),=PL4'9999999'   SET TO SPECIAL NUMBER
         LA    R3,12(R3)               BUMP TO NEXT MEMBER
         BCT   R2,PRTCHK01             ITERATE
         ISK   0,0                     INTENTIONAL BOMB
PRTLOOP1 EQU   *
         FIND  PDS,8(R3),C             POINT TO MEMBER IN QUESTION
         ZAP   RECHOLD,=PL1'0'         SET RECORD COUNT HOLD TO ZERO
         ZAP   PAGEHOLD,PAGECNT        HOLD CURRENT PAGE NUMBER
PRTLOOP2 EQU   *
         BAL   R9,NEWPAGE              FLIP TO NEW PAGE
PRTLOOP3 EQU   *
         READ  PDSDECB,SF,PDS,DEBLOCK  READ RECORD
         CHECK PDSDECB,DSORG=ALL       CHECK FOR COMPLETION
         L     R4,=A(DEBLOCK)          ADDRESS OF DEBLOCKING AREA
         L     R6,PDS+68               PTR TO IOB PREFIX
         LH    R6,22(R6)               PICK UP RESIDUAL COUNT
         LR    R5,R4                   ADDRESS OF DEBLOCKING AREA
         AH    R5,PDS+62               ADD BLKSIZE
         SR    R5,R6                   SUBTRACT RESIDUAL COUNT
PRTLOOP4 EQU   *
         LA    R6,PRTREC               GET BASE ADDR OF PRINT RECORD
         AH    R6,OFFSET               ADD DEFAULT OR PARM OFFSET
         MVC   0(80,R6),0(R4)          MOVE DATA TO THE PRINT RECORD
         AP    RECHOLD,=PL1'1'         BUMP THE RECORD COUNTER
         TM    OPTBYTE,FINDSW          IS THERE A FIND REQUEST?
         BNO   PRTLOOP5                NO, BRANCH AROUND CODE
         LA    R0,80                   LOAD ITERATION REGISTER
         SH    R0,FINDLNTH             SUBTRACT LENGTH OF (ARG-1)
         LH    R1,FINDLNTH             LOAD LENGTH
         LR    R8,R6                   COPY STARTING POINT
STRLOOP1 EQU   *
         EX    R1,EXSTRMVC             EXECUTE THE MOVE
         EX    R1,EXSTROC              EXECUTE THE OR
         EX    R1,EXSTRCLC             EXECUTE THE COMPARE
         BE    STRLOOP2                FOUND, GO ISSUE THE WTO
         LA    R8,1(R8)                BUMP
         BCT   R0,STRLOOP1             ITERATE
         B     PRTLOOP5                NOT FOUND, BYPASS WTO
EXSTRMVC MVC   CMPRSTR(0),0(R8)        MOVE STRING TO AREA
EXSTROC  OC    CMPRSTR(0),=CL20' '     'OR' BLANKS TO REMOVE CASE
EXSTRCLC CLC   CMPRSTR(0),FINDSTR      EXECUTED CLC FOR STRING
STRLOOP2 EQU   *
         MVC   STRWTO+42(8),0(R3)      MOVE NAME OF MEMBER TO WTO
         TM    OPTBYTE,FINDSVSW        USER REQUEST FINDSAVE?
         BO    STRWTOB                 YES, BYPASS WTO
         PRINT ON,GEN,DATA
STRWTO   WTO   'PRTPDS - STRING FOUND IN MEMBER--------       ',       X
               DESC=6,ROUTCDE=11
         PRINT ON,NOGEN,NODATA
STRWTOB  EQU   *
         TM    OPTBYTE,FINDSVSW        IS THERE A FINDSAVE REQUEST?
         BNO   PRTLOOP5                NO, BRANCH AROUND CODE
         BAL   R9,CHGROUP              CALL RTN TO GEN EDIT STATEMENTS
PRTLOOP5 EQU   *
         BAL   R9,PRTLINE              GO PRINT THE LINE
         LA    R4,80(R4)               BUMP PTR
         CR    R4,R5                   END OF BLOCK?
         BNL   PRTLOOP3                YES, GO GET NEXT BLOCK
         B     PRTLOOP4                NO, CONTINUE DEBLOCKING
EOFINPUT EQU   *
         MVI   PRTREC,C' '             CLEAR FIRST BYTE
         MVC   PRTREC+1(132),PRTREC    CLEAR REST OF LINE
FILLPAGE EQU   *
         CP    LINECNT,PAGESIZE        HAS IT BEEN RESET YET?
         BNL   ENDMEMBR                YES, FILLING PROCESS COMPLETE
         PUT   REPORT,PRTREC           GO PRINT THE LINE
         AP    LINECNT,=PL1'1'         BUMP THE LINE COUNTER
         B     FILLPAGE                CONTINUE FILLING PAGE
ENDMEMBR EQU   *
         AP    PAGEHOLD,=PL1'1'        IT'S REALLY THE NEXT PAGE
         ZAP   8(4,R3),PAGEHOLD        STUFF IT INTO OLD BLDL TTR
         ZAP   0(3,R7),RECHOLD         SAVE THE RECORD COUNTER
RANGENDI EQU   *
         LA    R3,12(R3)               BUMP TO NEXT MEMBER
         LA    R7,3(R7)                BUMP TO NEXT RECORD COUNTER
         TM    OPTBYTE,RANGESW         WAS RANGE OPTION SELECTED
         BNO   BRANGEND                NO, BYPASS THIS CODE
         CLC   0(8,R3),RANGEENM        IS IT HIGHER THAN THE LAST MBR?
         BNH   BRANGEND                NO, BYPASS THIS CODE
RANGENDS EQU   *
         ZAP   8(4,R3),=PL4'9999999'   SET TO SPECIAL NUMBER
         BCT   R2,RANGENDI             ITERATE THROUGH SPECIAL POINT
         B     BYPBCT01                BYPASS THE NORMAL BCT
BRANGEND EQU   *
         BCT   R2,PRTLOOP1             ITERATE THRU MEMBERS
BYPBCT01 EQU   *
         MVI   PRTREC,C'-'             CLEAR FIRST BYTE
         MVC   PRTREC+1(132),PRTREC    CLEAR REST OF LINE
         MVI   PRTREC,C'1'             INDICATE TOP OF FORM
         PUT   REPORT,PRTREC           FLIP TO NEW PAGE
         ZAP   LINECNT,=PL1'0'         CLEAR LINE COUNTER
         MVI   PRTREC,C' '             INDICATE SINGLE SPACE
ENDPDS   EQU   *
         PUT   REPORT,PRTREC           PRINT THE LINE
         AP    LINECNT,=PL1'1'         BUMP THE LINE COUNTER
         CP    LINECNT,PAGESIZE        END OF PAGE YET?
         BL    ENDPDS                  NO, LOOP UNTIL IT DOES
         PUT   REPORT,PRTREC           PRINT IT
         CLOSE (PDS,,REPORT)
         TM    OPTBYTE,FINDSVSW        USER REQUEST FINDSAVE?
         BNO   PRTBFSCL                NO, BYPASS CLOSE
         CLOSE (FINDSAVE)              CLOSE SAVE FILE
PRTBFSCL EQU   *
         SPACE 1
EXIT30   EQU   *
         L     R9,SAVE30
         BR    R9
         EJECT
***********************************************************************
*    THIS ROUTINE GENERATES JCL TO UPDATE A CARD-IMAGE PDS USING TSO  *
*  EDIT UNDER THE BACKGROUND TMP (IKJEFT01).  UPON ENTRY 0(R3) MUST   *
*  POINT TO A VALID MEMBERNAME FOR WHICH CHANGE STATEMENTS ARE TO BE  *
*  BUILT.  THE FIRST TIME THE ROUTINE IS ENTERED IT GEN'S THE JCL.    *
***********************************************************************
         SPACE 2
CHGROUP  DS    0H
         ST    R9,SAVE35
         B     BSAVE35
SAVE35   DC    F'-1'
         DC    CL8'CHGROUP '
BSAVE35  EQU   *
         SPACE 1
         CLI   CHGFIRST,C'1'           FIRST TIME THROUGH?
         BNE   CHGNFRST                NO, BYPASS JCL GEN
         MVI   CHGFIRST,C'0'           RESET FIRST TIME SWITCH
         OPEN  (FINDSAVE,OUTPUT)       OPEN SAVE FILE
         MVI   FINDREC,C' '
         MVC   FINDREC+1(L'FINDREC-1),FINDREC
         MVC   FINDREC(25),=CL25'//BGTMP EXEC PGM=IKJEFT01'
         PUT   FINDSAVE,FINDREC        WRITE OUTPUT RECORD
         MVI   FINDREC,C' '
         MVC   FINDREC+1(L'FINDREC-1),FINDREC
         MVC   FINDREC(22),=CL22'//SYSPRINT DD SYSOUT=*'
         PUT   FINDSAVE,FINDREC        WRITE OUTPUT RECORD
         MVI   FINDREC,C' '
         MVC   FINDREC+1(L'FINDREC-1),FINDREC
         MVC   FINDREC(22),=CL22'//SYSTSPRT DD SYSOUT=*'
         PUT   FINDSAVE,FINDREC        WRITE OUTPUT RECORD
         MVI   FINDREC,C' '
         MVC   FINDREC+1(L'FINDREC-1),FINDREC
         MVC   FINDREC(15),=CL15'//SYSTSIN  DD *'
         PUT   FINDSAVE,FINDREC        WRITE OUTPUT RECORD
         B     EXIT35
CHGNFRST EQU   *
         MVI   FINDREC,C' '
         MVC   FINDREC+1(L'FINDREC-1),FINDREC
         MVC   FINDREC+0(4),=CL4'EDIT'
         MVI   FINDREC+5,X'7D'         INSERT AN APOSTROPHE
         MVC   FINDREC+6(44),DSNAME    MOVE DSNAME
         LA    R1,FINDREC+7            POINT TO 1ST CHAR OF DSN
         LA    R9,43                   NUMBER OF CHARS IN DSN
CHGFNDBL EQU   *
         CLI   0(R1),C' '              BLANK?
         BE    CHGFNDB2                YUP, RIGHT HERE
         LA    R1,1(R1)                BUMP
         BCT   R9,CHGFNDBL             ITERATE
CHGFNDB2 EQU   *
         MVI   0(R1),C'('              INSERT LEFT PAREN
         MVC   1(8,R1),0(R3)           MOVE MEMBER NAME
         LA    R1,2(R1)                POINT TO 1ST CHAR OF MBRNAME
         LA    R9,8                    NUMBER OF CHARS IN MBRNAME
CHGFNDB3 EQU   *
         CLI   0(R1),C' '              BLANK?
         BE    CHGFNDB4                YUP, RIGHT HERE
         LA    R1,1(R1)                BUMP
         BCT   R9,CHGFNDB3             ITERATE
CHGFNDB4 EQU   *
         MVI   0(R1),C')'              INSERT RIGHT PAREN
         MVI   1(R1),X'7D'             INSERT RIGHT APOST
         MVC   3(14,R1),=CL14'CNTL NONUM OLD'
         CLC   0(8,R3),MBRLAST         SAME AS LAST?
         BE    EXIT35                  YES, BYPASS WRITE
         PUT   FINDSAVE,FINDREC        WRITE OUTPUT RECORD
         MVC   MBRLAST,0(R3)           SAVE OLD MEMBER
         MVI   FINDREC,C' '
         MVC   FINDREC+1(L'FINDREC-1),FINDREC
         MVC   FINDREC+0(9),=CL9'VERIFY ON'
         PUT   FINDSAVE,FINDREC        WRITE OUTPUT RECORD
         MVI   FINDREC,C' '
         MVC   FINDREC+1(L'FINDREC-1),FINDREC
         MVC   FINDREC+0(14),=CL14'C * 99999999 /'
         LH    R9,FINDLNTH             GET LENGTH
         EX    R9,CHGEXMV1             MOVE THE FOUND STRING
CHGEXMV1 MVC   FINDREC+14(00),FINDSTR  MOVE THE FOUND STRING
         LA    R1,FINDREC+14           GET ORIGIN ADDRESS
         AH    R1,FINDLNTH             ADD LENGTH OF FOUND STRING
         LA    R1,1(R1)                BUMP LENGTH BY 1
         MVI   0(R1),C'/'              INSERT NEXT SLASH
         LA    R1,1(R1)                BUMP LENGTH BY 1
         LH    R9,FINDLNTH             GET LENGTH
         EX    R9,CHGEXMV2             MOVE THE FOUND STRING
CHGEXMV2 MVC   0(00,R1),FINDSTR        MOVE THE FOUND STRING AGAIN
         AH    R1,FINDLNTH             ADD LENGTH AGAIN
         LA    R1,1(R1)                BUMP LENGTH BY 1
         MVI   0(R1),C'/'              LAST SLASH
         MVC   2(3,R1),=CL3'ALL'       ALL RECORDS
         PUT   FINDSAVE,FINDREC        WRITE OUTPUT RECORD
         MVI   FINDREC,C' '
         MVC   FINDREC+1(L'FINDREC-1),FINDREC
         MVC   FINDREC+0(15),=CL15'RENUM 10000 10000'
         PUT   FINDSAVE,FINDREC        WRITE OUTPUT RECORD
         MVI   FINDREC,C' '
         MVC   FINDREC+1(L'FINDREC-1),FINDREC
         MVC   FINDREC+0(8),=CL8'END SAVE'
         PUT   FINDSAVE,FINDREC        WRITE OUTPUT RECORD
         SPACE 1
EXIT35   EQU   *
         L     R9,SAVE35
         BR    R9
         EJECT
***********************************************************************
*    THIS ROUTINE PRINTS LINES AND EJECTS ON A LINE COUNTER.  IT ALSO *
*  CHECKS TO SEE IF OPTION PRTASM IS ON, AND IF SO, EXPANDS EJECTS.   *
***********************************************************************
         SPACE 2
PRTLINE  DS    0H
         ST    R9,SAVE40
         B     BSAVE40
SAVE40   DC    F'-1'
         DC    CL8'PRTLINE '
BSAVE40  EQU   *
         SPACE 1
         TM    OPTBYTE,PRTASMSW        DID USER REQUEST PRTASM OPTION?
         BNO   NOPRTASM                NO, BYPASS THAT CODE
         CLC   9(5,R4),=CL5'EJECT'     IS IT AN EJECT?
         BE    PLEJECT                 YES, GO HANDLE EJECT
         CLC   9(5,R4),=CL5'SPACE'     IS IT A SPACE?
         BE    PLSPACE                 YES, GO HANDLE SPACE
         BNE   NOPRTASM                NONE OF THOSE, HANDLE NORMALLY
PLEJECT  EQU   *
         ZAP   WORKDBLW,LINECNT        COPY LINE COUNTER
         AP    WORKDBLW,=PL1'1'        BUMP BY 1
         CP    WORKDBLW,PAGESIZE       IS IT => PAGESIZE?
         BNL   FLIPNEW                 YES, DON'T BOTHER SPACING
         MVI   PRTREC,C' '             CLEAR FIRST BYTE
         MVC   PRTREC+1(132),PRTREC    PROPOGATE
PLEJECT2 EQU   *
         PUT   REPORT,PRTREC           PRINT A LINE
         AP    LINECNT,=PL1'1'         BUMP LINE COUNTER
         CP    LINECNT,PAGESIZE        END-OF-PAGE
         BL    PLEJECT2                NO, KEEP SPACING
FLIPNEW  EQU   *
         BAL   R9,NEWPAGE              YES, FLIP TO NEW PAGE
         B     EXIT40                  GO TO EXIT
PLSPACE  EQU   *
         TRT   15(1,R4),TRTNUM         IS COLUMN 16 NUMERIC?
         BC    7,NOPRTASM              NO, HANDLE NORMALLY
         PACK  WORKDBLW,15(1,R4)       NUMERIC - PICK IT UP
         AP    WORKDBLW,LINECNT        ADD IT TO WHAT'S ALREADY THERE
         CP    WORKDBLW,PAGESIZE       DOES IT GO PAST PAGE?
         BNL   PLEJECT                 YES, MIGHT AS WELL BE AN EJECT
         PACK  WORKDBLW,15(1,R4)       NUMERIC - PICK IT BACK UP
         CVB   R8,WORKDBLW             CONVERT IT TO HEX
         LTR   R8,R8                   IS IT REALISTIC?
         BNP   NOPRTASM                NO, JUST PRINT THE RECORD
         MVI   PRTREC,C' '             CLEAR FIRST BYTE
         MVC   PRTREC+1(132),PRTREC    PROPOGATE
PLSPACE2 EQU   *
         PUT   REPORT,PRTREC           PRINT A LINE
         BCT   R8,PLSPACE2             ITERATE
         AP    LINECNT,WORKDBLW        ADD SPACE COUNT TO COUNTER
         B     EXIT40                  GET OUT OF HERE
NOPRTASM EQU   *
         PUT   REPORT,PRTREC           PRINT IT
         AP    LINECNT,=PL1'1'         BUMP LINE COUNTER
         CP    LINECNT,PAGESIZE        END-OF-PAGE
         BL    EXIT40                  NO, EXIT
         BAL   R9,NEWPAGE              YES, FLIP TO NEW PAGE
         SPACE 1
EXIT40   EQU   *
         L     R9,SAVE40
         BR    R9
         EJECT
***********************************************************************
*    THIS ROUTINE FLIPS TO A NEW PAGE AND RESETS THE LINE COUNTER     *
***********************************************************************
         SPACE 2
NEWPAGE  DS    0H
         ST    R9,SAVE50
         B     BSAVE50
SAVE50   DC    F'-1'
         DC    CL8'PRTLINE '
BSAVE50  EQU   *
         SPACE 1
         AP    PAGECNT,=PL1'1'         BUMP THE PAGE COUNTER
         MVI   PRTREC,C' '             CLEAR THE FIRST BYTE
         MVC   PRTREC+1(132),PRTREC    PROPOGATE
         MVC   PRTREC+95(8),TIME       PRINT THE TIME IN HEADER
         MVC   PRTREC+85(8),DATE       PRINT THE DATE IN HEADER
         MVC   PRTREC+70(6),=XL6'402020202120'
         ED    PRTREC+70(6),PAGECNT+1  PRINT PAGE NUMBER IN HEADER
         MVC   PRTREC+66(4),=CL4'PAGE'
         MVC   PRTREC+50(7),=CL7'MEMBER='
         MVC   PRTREC+57(8),0(R3)      PRINT MEMBER NAME IN HEADER
         MVC   PRTREC+1(4),=CL4'DSN='
         MVC   PRTREC+5(44),DSNAME     PRINT DSNAME IN HEADER
         MVI   PRTREC,C'1'             INDICATE TOP OF FORM
         PUT   REPORT,PRTREC           PRINT IT
         MVI   PRTREC,C'-'             SET FIRST BYTE TO DASH
         MVC   PRTREC+1(132),PRTREC    PROPOGATE
         MVI   PRTREC,C' '             INDICATE SINGLE SPACE
         PUT   REPORT,PRTREC           PRINT IT
         MVI   PRTREC,C' '             CLEAR THE FIRST BYTE
         MVC   PRTREC+1(132),PRTREC    PROPOGATE
         MVI   PRTREC,C' '             INDICATE SINGLE SPACE
         PUT   REPORT,PRTREC           PRINT IT
         MVI   PRTREC,C' '             INDICATE SINGLE SPACES
         ZAP   LINECNT,=PL1'0'         RE-INIT LINE COUNTER
         SPACE 1
EXIT50   EQU   *
         L     R9,SAVE50
         BR    R9
         EJECT
***********************************************************************
*    THIS ROUTINE PRINTS THE DIRECTORY USING THE OLD BLDL ENTRIES     *
***********************************************************************
         SPACE 2
PRTDIR   DS    0H
         ST    R9,SAVE60
         B     BSAVE60
SAVE60   DC    F'-1'
         DC    CL8'PRTDIR  '
BSAVE60  EQU   *
         SPACE 1
         OPEN  (DIRECT,OUTPUT)         OPEN THE DIRECTORY PRINT FILE
         LH    R2,BLDLLIST             GET THE NUMBER OF MEMBERS
         LA    R3,BLDLLIST+4           GET THE ADDR OF THE 1ST MEMBER
         L     R7,=A(RECNTLST)         ADDR OF THE RECORD COUNTER TABLE
         MVI   PRTREC,C' '             CLEAR THE FIRST BYTE
         MVC   PRTREC+1(132),PRTREC    PROPOGATE
         MVI   PRTREC,C'1'             INDICATE TOP OF FORM
         MVC   PRTREC+1(26),=CL26'DIRECTORY LISTING FOR DSN='
         MVC   PRTREC+27(44),DSNAME    MOVE THE DSNAME
         MVC   PRTREC+72(08),DATE      MOVE THE DATE
         MVC   PRTREC+85(08),TIME      MOVE THE TIME
         PUT   DIRECT,PRTREC           PRINT THE RECORD
         MVI   PRTREC,C'-'             SET LINE TO DASHES
         MVC   PRTREC+1(132),PRTREC    PROPOGATE
         MVI   PRTREC,C' '             INDICATE SINGLE SPACE
         PUT   DIRECT,PRTREC           PRINT THE RECORD
         MVI   PRTREC,C' '             SET LINE TO SPACES
         MVC   PRTREC+1(132),PRTREC    PROPOGATE
         MVC   PRTREC+1(30),=CL30'OPTIONS==> COL(  ),PRTASM(OFF)'
         MVC   PRTREC+31(13),=CL13',DIRONLY(OFF)'
         MVC   PRTREC+44(25),=CL25',RANGE(-ALL-ALL,ALL-ALL-)'
         MVC   PRTREC+69(10),=CL10',DWIDTH(6)'
         MVC   PRTREC+79(10),=CL10',LINES(59)'
         MVC   PRTREC+89(06),=CL06',FIND('
         MVC   PRTREC+95(20),FINDSTR   MOVE SEARCH STRING
         MVI   PRTREC+115,C')'         MOVE CLOSING PAREN
         MVC   PRTREC+116(14),=CL14',FINDSAVE(OFF)'
         TM    OPTBYTE,RANGESW         WAS A RANGE SELECTED?
         BNO   BMVRANGE                NO, BYPASS
         MVC   PRTREC+51(8),RANGESNM   MOVE STARTING NAME
         MVC   PRTREC+60(8),RANGEENM   MOVE ENDING NAME
BMVRANGE EQU   *
         TM    OPTBYTE,FINDSW          WAS A STRING SEARCH SELECTED?
         BO    BMVFIND                 YES, BYPASS
         MVC   PRTREC+95(4),=CL4'NONE' INDICATE NO SEARCH REQUESTED
BMVFIND  EQU   *
         LH    R4,OFFSET               PICK UP OFFSET
         CVD   R4,WORKDBLW             CONVERT TO DECIMAL
         UNPK  PRTREC+16(2),WORKDBLW   MAKE READABLE
         OI    PRTREC+17,X'F0'         CLEAR SIGN NIBBLE
         TM    OPTBYTE,PRTASMSW        IS PRTASM OPTION ON?
         BNO   BYPRTASM                NO, BYPASS THE LITERAL
         MVC   PRTREC+27(3),=CL3'ON '  YES, CHANGE LITERAL
BYPRTASM EQU   *
         TM    OPTBYTE,DIRONLSW        IS DIRONLY OPTION ON?
         BNO   BDIRONLY                NO, BYPASS THE LITERAL
         MVC   PRTREC+40(3),=CL3'ON '  YES, CHANGE LITERAL
BDIRONLY EQU   *
         TM    OPTBYTE,FINDSVSW        IS FINDSAVE OPTION ON?
         BNO   BFINDSV                 NO, BYPASS THE LITERAL
         MVC   PRTREC+126(3),=CL3'ON ' YES, CHANGE LITERAL
BFINDSV  EQU   *
         LH    R4,DWIDTH               GET DIRECTORY WIDTH
         CVD   R4,WORKDBLW             CHANGE RADIX
         UNPK  PRTREC+77(1),WORKDBLW   UNPACK DIR WIDTH
         OI    PRTREC+77,X'F0'         CLEAR SIGN NIBBLE
         AP    PAGESIZE,=PL1'3'        ADD 3 FOR THE HEADERS
         UNPK  PRTREC+86(2),PAGESIZE   UNPACK LINES (PAGESIZE)
         OI    PRTREC+87,X'F0'         CLEAR SIGN NIBBLE
         PUT   DIRECT,PRTREC           PRINT THE RECORD
         MVI   PRTREC,C'-'             SET LINE TO DASHES
         MVC   PRTREC+1(132),PRTREC    PROPOGATE
         MVI   PRTREC,C' '             INDICATE SINGLE SPACE
         PUT   DIRECT,PRTREC           PRINT THE RECORD
         MVI   PRTREC,C' '             CLEAR THE FIRST BYTE
         MVC   PRTREC+1(132),PRTREC    PROPOGATE
         MVC   PRTREC+1(22),=CL22'MEMBER   PGNUM/RECCNT '
         MVC   PRTREC+23(5*22),PRTREC+1 PROPOGATE
         PUT   DIRECT,PRTREC           PRINT THE RECORD
         MVI   PRTREC,C'-'             SET TO DASHES
         MVC   PRTREC+1(132),PRTREC    PROPOGATE
         MVI   PRTREC,C' '             CLEAR THE FIRST BYTE
         PUT   DIRECT,PRTREC           PRINT THE RECORD
         MVI   PRTREC,C' '             SET TO SPACES
         MVC   PRTREC+1(132),PRTREC    PROPOGATE
         MVI   PRTREC,C' '             CLEAR THE FIRST BYTE
         PUT   DIRECT,PRTREC           PRINT THE RECORD
*------> PRINT THE DIRECTORY
DIRLOOP  EQU   *
         LA    R4,PRTREC+1             GET ADDRESS OF FIRST PRINT BYTE
         LH    R5,DWIDTH               # OF MEMBERS PER PRINT LINE
         MVI   PRTREC,C' '             CLEAR THE FIRST BYTE
         MVC   PRTREC+1(132),PRTREC    PROPOGATE
DIRLOOP1 EQU   *
         MVC   0(8,R4),0(R3)           MOVE MEMBER NAME
         TM    OPTBYTE,DIRONLSW        WAS A PAGE NUMBER GENERATED?
         BO    BYPAGES                 NO, DIRONLY OPTION SELECTED
         MVC   9(12,R4),=CL12'NOT-IN-RANGE' SET UP DEFAULT
         CP    8(4,R3),=PL4'9999999'   WAS IT THE SPECIAL NUMBER?
         BE    BYPAGES                 YES, BYPASS PRINTING THE PAGE #
         MVC   8(6,R4),=XL6'402020202120'
         ED    8(6,R4),9(R3)           EDIT THE PAGE NUMBER
         MVC   14(7,R4),=XL7'4020206B202120'
         ED    14(7,R4),0(R7)          EDIT THE PAGE NUMBER
         MVI   14(R4),C'/'             INSERT THE SEPARATOR
         LA    R7,3(R7)                BUMP TO NEXT ENTRY
BYPAGES  EQU   *
         LA    R3,12(R3)               BUMP TO NEXT ENTRY
         LA    R4,22(R4)               BUMP THE INDEX
         BCTR  R2,0                    DECREMENT THE # OF MEMBERS
         LTR   R2,R2                   IS IT ZERO YET?
         BZ    CLOSEDIR                YES, PRINT THE LAST LINE
         BCT   R5,DIRLOOP1             ITERATE THRU MEMBERS
         PUT   DIRECT,PRTREC           PRINT A LINE
         B     DIRLOOP                 ITERATE THRU PRINT LINES
CLOSEDIR EQU   *
         PUT   DIRECT,PRTREC           PRINT THE LAST LINE
         CLOSE (DIRECT)                CLOSE THE PRINT FILE
         SPACE 1
EXIT60   EQU   *
         L     R9,SAVE60
         BR    R9
         EJECT
***********************************************************************
*              W O R K I N G   S T O R A G E   S E C T I O N          *
***********************************************************************
         SPACE 3
INPUT    DCB   DSORG=PS,               DCB FOR READING DIRECTORY       X
               RECFM=F,                                                X
               MACRF=GM,                                               X
               EODAD=ENDIRECT,                                         X
               DDNAME=PDS,                                             X
               EXLST=JFCBLIST,                                         X
               BLKSIZE=256,                                            X
               LRECL=256
PDS      DCB   DSORG=PO,               DCB FOR READING PDS             X
               RECFM=FB,                                               X
               MACRF=R,                                                X
               EODAD=EOFINPUT,                                         X
               DDNAME=PDS
DIRECT   DCB   DSORG=PS,               DCB FOR PRINTING DIRECTORY      X
               RECFM=FBA,                                              X
               MACRF=PM,                                               X
               DDNAME=DIRECT,                                          X
               BLKSIZE=0,                                              X
               LRECL=133
REPORT   DCB   DSORG=PS,               DCB FOR PRINTING PDS            X
               RECFM=FBA,                                              X
               MACRF=PM,                                               X
               DDNAME=REPORT,                                          X
               BLKSIZE=0,                                              X
               LRECL=133
FINDSAVE DCB   DSORG=PS,               DCB FOR SAVING FOUND IMAGES     X
               RECFM=FB,                                               X
               MACRF=PM,                                               X
               DDNAME=FINDSAVE,                                        X
               BLKSIZE=0,                                              X
               LRECL=80
*------> THIS AREA IS USED FOR MISCELLANEOUS STUFF
WORKREC  DS    CL256                   READ AREA FOR DIRECTORY
PRTREC   DS    CL133                   CURRENT PRINT RECORD
FINDREC  DC    CL80' '                 WORK RECORD FOR FINDSAVE FILE
DSNAME   DC    CL44' '                 DSN OF PDS DATASET FROM JFCB
MBRLAST  DC    CL8' '                  KEEP LAST MBR TO AVOID DUPS
LINECNT  DC    PL2'0'                  NUMBER OF LINES THIS PAGE
PAGECNT  DC    PL4'0'                  PAGE NUMBER
PAGEHOLD DC    PL4'0'                  HOLD PAGE # PRIOR TO DIR PRINT
RECHOLD  DS    PL3                     HOLD RECCNT PRIOR TO DIR PRINT
PAGESIZE DC    PL2'56'                 NUMBER OF LINES PER PAGE
FIRSTSW  DC    C'1'                    FIRST TIME THRU NEWPAGE RTN
CHGFIRST DC    C'1'                    FIRST TIME THRU CHGROUP RTN
*------> THIS AREA IS USED BY THE RANGE ROUTINE
RANGEPTR DC    A(0)                    PTR TO START OF RANGE STRING
RANGECOM DC    A(0)                    PTR TO IMBEDDED COMMA
RANGEPAR DC    A(0)                    PTR TO TRAILING RIGHT PAREN
RANGESNM DC    CL8' '                  NAME OF STARTING MEMBER
RANGEENM DC    CL8' '                  NAME OF ENDING MEMBER
*------> THIS AREA IS USED BY THE OPTIONS ROUTINE
OPTBYTE  DC    X'00'                   OPTIONS
PRTASMSW EQU   X'80'                   PRTASM OPTION SELECTED
DIRONLSW EQU   X'40'                   DIRONLY OPTION SELECTED
RANGESW  EQU   X'20'                   RANGE OPTION SELECTED
FINDSW   EQU   X'10'                   FIND  OPTION SELECTED
FINDSVSW EQU   X'08'                   FINDSAVE OPTION SELECTED
OFFSET   DC    H'27'                   DEFAULT OFFSET
DWIDTH   DC    H'6'                    DEFAULT DIRECTORY WIDTH
FINDSTR  DC    CL20' '                 AREA TO HOLD FINDER STRING
FINDLNTH DC    H'0'                    LENGTH OF FOUND STRING
CMPRSTR  DC    CL20' '                 AREA TO CHANGE CASE
*------> THIS AREA IS USED TO CHECK FOR NUMERICS
TRTNUM   DC    256XL1'FF'             |TRT TABLE TO CHECK NUMERICS
         ORG   TRTNUM+C'0'            |
         DC    XL10'00'               |
         ORG
*------> THE FOLLOWING IS USED BY THE RDJFCB MACRO TO PICK UP THE DSN
JFCBLIST DS    0F                      FOR ALIGNMENT
         DC    X'87'                   OPTION CODE + END LIST BIT
         DC    AL3(JFCBAREA)           ADDRESS OF JFCB AREA
JFCBAREA DS    CL176                   AREA TO HOLD THE JFCB
*------> THE FOLLOWING IS USED BY THE DATE ROUTINE
WORKDBLW DS    D                       WORKAREA FOR CVB CONVERSIONS
WORKTIME DS    CL8
MONTHTBL DC    PL2'31',PL2'28',PL2'31',PL2'30',PL2'31',PL2'30'
         DC    PL2'31',PL2'31',PL2'30',PL2'31',PL2'30',PL2'31'
DIVFIELD DS    PL4                     MEMORY FOR DIVIDE PACKED
MONTH    DS    PL2                     PACKED MONTH
TIME     DS    0CL8                    CURRENT TIME
TIMEHH   DS    CL2
         DC    CL1':'
TIMEMM   DS    CL2
         DC    CL1':'
TIMESS   DS    CL2
DATE     DS    0CL8                    CURRENT DATE
DATEMM   DS    CL2
         DC    CL1'/'
DATEDD   DS    CL2
         DC    CL1'/'
DATEYY   DS    CL2
*------>
         LTORG
*------>
BLDLLIST DC    F'12'                   # MEMBERS & LENGTH
         DC    10000XL12'00'           BLDL LIST - INITIAL
RECNTLST DC    10000PL3'0'             AREA TO SAVE RECORD COUNTS
DEBLOCK  DS    CL32768                 DEBLOCKING AREA
         SPACE 3
         END   PRTPDS
