*          DATA SET PUT1B033   AT LEVEL 001 AS OF 02/04/91
*          DATA SET PUT1B033   AT LEVEL 003 AS OF 01/22/91              00001
*          DATA SET PUT1B033   AT LEVEL 002 AS OF 01/22/91              00002
*          DATA SET PUT1B033CM AT LEVEL 024 AS OF 10/25/90              00003
*          DATA SET PUT1B033CM AT LEVEL 023 AS OF 10/25/90              00004
*********************************************************************** 00005
*                           *              *                            00006
*                           *   PUT1B033   *                            00007
*                           *              *                            00008
*                           ****************                            00009
*                                                                       00010
*   SUMMARY:                                                            00011
*      DATA SET UTILITY TO ADD, DELETE, & RENAME MEMBER & ALIAS NAMES.  00012
*                                                                       00013
*********************************************************************** 00014
*                                                                       00015
*   ABBREVIATIONS:                                                      00016
*      @         ADDRESS.                                               00017
*      CALC      CALCULATE.                                             00018
*      CHAR      CHARACTER.                                             00019
*      CC        CONDITION CODE; CARRIAGE CONTROL.                      00020
*      HSA       HIGHER SAVE AREA.                                      00021
*      INSTR     INSTRUCTION.                                           00022
*      PACK DEC  PACKED DECIMAL.                                        00023
*      REG       REGISTER(S).                                           00024
*      SA        SAVE AREA.                                             00025
*                                                                       00026
*   CONVENTIONS:                                                        00027
*                                                                       00028
*   DESCRIPTION:                                                        00029
*                                                                       00030
*      PUT1B033 IS A PARTITIONED DATA SET UTILITY PROGRAM TO RENAME     00031
*      MEMBERS, ADD ALIAS NAMES TO MEMBERS, AND DELETE MEMBERS.         00032
*                                                                       00033
*      CONTROL STATEMENTS ARE FUNRISHED AS CARD IMAGES ON SYSIN.        00034
*      AT PRESENT 3 STATEMENTS ARE RECOGNIZED:                          00035
*         RENAME MEMBER=_,NEWNAME=_                                     00036
*         ALIAS MEMBER=_,NEWNAME=_                                      00037
*         DELETE MEMBER=_                                               00038
*      (UNDERLINES REPRESENT USER-SUPPLIED MEMBER NAMES.)               00039
*      THE RENAME, ALIAS, AND DELETE STATEMENTS RENAME, ADD AN ALIAS    00040
*      TO, AND DELETE A MEMBER, RESPECTIVELY.                           00041
*      MEMBER SPECIFIES THE NAME OF THE EXISTING MEMBER; NEWNAME        00042
*      SPECIFIES THE NEW NAME OF THE RENAMED MEMBER OR THE ALIAS TO     00043
*      BE ADDED.                                                        00044
*                                                                       00045
*      STATEMENTS MUST BE IN COLUMNS 1-71 AND CANNOT BE CONTINUED.      00046
*      OPERATION AND OPERAND FIELDS MUST BE WRITTEN WITH NO EMBEDDED    00047
*      BLANKS AND MUST BE SEPARATED FROM EACH OTHER BY AT LEAST ONE     00048
*      BLANK.  THE STATEMENT IS TERMINATED BY THE FIRST BLANK AFTER     00049
*      THE START OF THE OPERAND FIELD OR COLUMN 72, WHICHEVER COMES     00050
*      FIRST.                                                           00051
*                                                                       00052
*      THE CONTROL CARD AND MESSAGES CONCERNING PERFORMANCE ARE         00053
*      WRITTEN ON SYSPRINT, WHICH IS RECFM=FBA, LRECL=121 AND HAS       00054
*      A DEFAULT OF BLKSIZE=1210.                                       00055
*                                                                       00056
*      THE PARTITIONED DATA SET TO BE PROCESSED IS FURNISHED BY SYSUT1. 00057
*      THE RETURN CODE IS SET TO INDICATE PROGRAM PERFORMANCE AND THE   00058
*      VALUE FOR THE STEP IS REPORTED ON SYSPRINT.  (SEE MESSAGES.)     00059
*                                                                       00060
*   EXAMPLES:                                                           00061
*      1.  THE FOLLOWING JOB STEP WOULD LEAVE THE LIBRARY ON SYSUT1     00062
*      AS IT WAS WHEN THE STEP BEGAN IF THERE ALREADY WAS A MEMBER      00063
*      NAMED A AND THERE WERE NO MEMBERS NAMED B OR C.  OTHERWISE       00064
*      ONE OR MORE OF THE OPERATIONS WOULD FAIL AND GIVE APPROPRIATE    00065
*      ERROR MESSAGES.                                                  00066
*         // EXEC PGM=PUT1B033,REGION=30K                               00067
*         //SYSPRINT DD SYSOUT=A                                        00068
*         //SYSUT1 DD DSN=USERID.LIB.LOAD,DISP=OLD                      00069
*         //SYSIN DD *                                                  00070
*          ALIAS MEMBER=A,NEWNAME=B                                     00071
*          RENAME MEMBER=B,NEWNAME=C                                    00072
*          DELETE MEMBER=C                                              00073
*                                                                       00074
*   INPUT:                                                              00075
*      SYSIN     CONTROL STATEMENTS:                                    00076
*                CARD 1-N:                                              00077
*                   COL 1-71:                                           00078
*                      0-N BLANKS.                                      00079
*                      OPERATION:                                       00080
*                         RENAME -- RENAME A MEMBER.                    00081
*                         ALIAS -- ADD AN ALIAS TO A MEMBER.            00082
*                         DELETE -- DELETE A MEMBER.                    00083
*                      1-N BLANKS.                                      00084
*                      OPERAND:  COMMA SEPARATES; BLANK ENDS.           00085
*                         (_ IS A MEMBER NAME.)                         00086
*                         MEMBER=_ -- EXISTING MEMBER NAME.             00087
*                         NEWNAME=_ -- NEW MEMBER NAME.                 00088
*                         (BOTH ARE REQUIRED FOR RENAME & ALIAS;        00089
*                         ONLY MEMBER=_ IS REQUIRED FOR DELETE.)        00090
*   INPUT/OUTPUT:                                                       00091
*      SYSUT1    PARTITIONED DATA SET TO BE MODIFIED.                   00092
*   OUTPUT:                                                             00093
*      SYSPRINT  PRINTED PROGRAM EXECUTION LOG:                         00094
*                A PROGRAM AND RUN IDENTIFICATION IS PRINTED, FOLLOWED  00095
*                BY ALTERNATING CONTROL STATEMENTS AND ERROR MESSAGES,  00096
*                FOLLOWED BY THE END MESSAGE REPORTING THE RETURN CODE. 00097
*                                                                       00098
*   MESSAGES:                                                           00099
*                                                                       00100
*      FOLLOWING IS INFORMATION ABOUT SYSPRINT MESSAGES.  THIS CONSISTS 00101
*      OF THE RETURN CODE, THE MESSAGE ITSELF (IN COLLATING SEQUENCE),  00102
*      AND AN EXPLANATION.                                              00103
*      0.   ACTION COMPLETED.  THE REQUESTED ACTION WAS SUCCESSFULLY    00104
*              FINISHED.                                                00105
*      16.  DIRECTORY FULL.  THERE IS NO MORE SPACE IN THE PARTITIONED  00106
*              DATA SET DIRECTORY BLOCKS.                               00107
*      12.  DUPLICATE NAME.  A MEMBER ALREADY EXISTS WITH THE NAME      00108
*              GIVEN FOR NEWNAME=_.                                     00109
*      8.   GETMAIN FAILED.  STOW DID NOT FIND ENOUGH SPACE AVAILABLE   00110
*              FOR ITS GETMAIN.                                         00111
*      20.  I/O ERROR.  AN I/O ERROR WAS ENCOUNTERED WHEN ATTEMPTING TO 00112
*              PROCESS THE PARTITIONED DATA SET DIRECTORY.              00113
*      4.   INVALID FORMAT.  THE CONTROL CARD WAS IMPROPERLY WRITTEN.   00114
*      12.  MEMBER NOT FOUND.  THE MEMBER NAME GIVEN FOR MEMBER=_ DOES  00115
*              NOT EXIST.                                               00116
*      8.   SYSUT1 NOT OPEN.  STOW DETECTED THAT THE SYSUT1 DCB WAS NOT 00117
*              CORRECTLY OPENED.                                        00118
*                                                                       00119
*   ERRORS:                                                             00120
*      USER ABEND 1 - 'SYSIN' DIDN'T OPEN                               00121
*      USER ABEND 2 - 'SYSPRINT' DIDN'T OPEN                            00122
*      USER ABEND 3 - 'SYSUT1' DIDN'T OPEN                              00123
*                                                                       00124
*   TERMINATION:                                                        00125
*      ERROR RETURNS:                                                   00126
*      RETURN CODE:                                                     00127
*         THE RETURN CODE INDICATES THE MOST SERIOUS ERROR CONDITION    00128
*         ENCOUNTERED AS FOLLOWS:                                       00129
*          0  ALL REQUESTS PERFORMED SUCCESSFULLY.                      00130
*          4  INVALID CONTROL STATEMENT.                                00131
*          8  ENVIRONMENT PRECLUDES OPERATION.                          00132
*         12  LOGICAL INCONSISTENCY IN REQUESTED ACTION.                00133
*         16  DATA SET STATUS PREVENTS OPERATION.                       00134
*         20  I/O ERROR ON DATA SET.                                    00135
*         FOR SPECIFICS SEE MESSAGES.                                   00136
*         THE CODE IS REPORTED ON SYSPRINT AT THE END OF THE PROGRAM.   00137
*                                                                       00138
*   GLOSSARY:                                                           00139
*      BLDLC     C BYTE OF TTRC IN BLDL LIST FOR PDS DIRECTORY ENTRY.   00140
*      BLDLCON   BYTE FOR CONCATENATION NUMBER IN BLDL LIST.            00141
*      EDIT      NUMBER EDITING WORK AREA.                              00142
*      ENDMSG    PROGRAM TERMINATION MESSAGE.                           00143
*      LINPRINT  PRINT LINE AREA FOR SYSPRINT.                          00144
*      LIST      SHARED AREA FOR BLDL AND STOW LIST.                    00145
*      MEMBER    OLD MEMBER NAME AREA.                                  00146
*      MSGNTR    # BYTES IN LONGEST ERROR MESSAGE.                      00147
*      MSGTBL    TABLE OF ERROR MESSAGES & RETURN CODES.                00148
*      MVC1      PATTERN MVC INSTR.                                     00149
*      NAME      MEMBER NAME IN BLDL & STOW LIST.                       00150
*      NEWNAME   NEW MEMBER NAME AREA.                                  00151
*      PACKED    DOUBLE WORK AREA FOR PACKED DECIMAL.                   00152
*      PAT1      EDIT PATTERN FOR RETURN CODE.                          00153
*      PAT2      EDIT PATTERN FOR RUN TIME.                             00154
*      RTRNCODE  FULLWORD RETURN CODE.                                  00155
*      STOWC     C BYTE OF TTRC IN STOW LIST FOR PDS DIRECTORY ENTRY.   00156
*      SW1       BIT SWITCHES FOR CONTROL OPERANDS:                     00157
*                1. MEMBER= FOUND.                                      00158
*                2. NEWNAME= FOUND.                                     00159
*      TIME      2 FULLWORD WORK AREA FOR RUN TIME.                     00160
*      XLSPRINT  SYSPRINT DCB EXIT LIST.                                00161
*                                                                       00162
*   TECHNIQUE:                                                          00163
*      1.  THE SAME AREA IS USED FOR THE BLDL LIST AND THE STOW LIST.   00164
*      THE LATTER PART OF THE BLDL LIST IS MOVED FORWARD TO FORM THE    00165
*      STOW LIST.                                                       00166
*                                                                       00167
*   HISTORY:                                                            00168
*      IEBFGR                                                           00169
*      -- --- --:  ORIGINATED BY FRANK G. ROSS,                         00170
*         GODDARD SPACE FLIGHT CENTER (GSFC),                           00171
*         GREENBELT, MARYLAND, 20771.                                   00172
*      14 JUN 71:  MODIFIED BY CARL WRANDLE BARTH,                      00173
*         SPACE AND EARTH SCIENCES COMPUTING CENTER (SESCC),            00174
*         GSFC.                                                         00175
*      PUT1B033                                                         00176
*      13 OCT 73:  REWRITTEN TO CORRECT, UPDATE, AND MODIFY BY          00177
*         DANIEL A. KIRK, SESCC.                                        00178
*         1.  RENAMED CSECT TO ALIAS FROM IEBFGR.                       00179
*         2.  ADDED SYSPRINT EXLST FOR DEFAULT BLKSIZE=1210.            00180
*         3.  REPLACE CNSRV & GOBCK MACROS FOR RENT LINKAGE TO SIMPLE   00181
*         SAVE AND RETURN LINKAGE.                                      00182
*         4.  ADDED RETURN CODES AND CHANGED MESSAGE HANDLING.          00183
*         5.  ADDED RETURN CODE TO SYSPRINT END MESSAGE AND SHORTENED   00184
*         MESSAGES.                                                     00185
*         6.  CHANGED SYSPRINT HEADER LINE INCLUDING DYNAMIC PGM ID     00186
*         AND RUN TIME.                                                 00187
*         7.  REMOVED UNIMPLEMENTED ADD AND REPL COMMANDS AND RELATED   00188
*         ERROR MESSAGE.                                                00189
*         8.  CORRECTED CONTROL CARD SCAN.                              00190
*         9.  EXTENDED ERROR HANDLING FOR RELEASE 21 STOW.              00191
*         10.  REMOVED CHANGE COMMAND (ALIAS FOR RENAME).               00192
*         11.  CHANGED NEWMEM KEYWORD TO NEWNAME.                       00193
*       3 FEB 77:  MODIFIED BY DANIEL A. KIRK OF THE RENAMED            00194
*         ADVANCED SOFTWARE SYSTEMS DEVELOPMENT SECTION,                00195
*         SCIENCE AND APPLICATIONS COMPUTING CENTER,                    00196
*         SCIENCES DIRECTORATE,                                         00197
*         GSFC.                                                         00198
*         1.  CHANGED SYSPRINT FROM PUT LOCATE MODE TO PUT MOVE MODE    00199
*         SO THAT UNDER TSO THE PRINT LINE RELATING TO THE SYSIN CARD   00200
*         ENTERED WILL COME AFTER THAT ENTRY RATHER THAN AFTER THE      00201
*         THE NEXT ENTRY.                                               00202
*      02 MAR 77: FRANK PAJERSKI,CODE 531                               00203
*         1. UPDATE MACROS, BLKSIZE, ETC. FOR THE M&DO 360'S.           00204
*         2. CHECK FOR 'GOOD' OPEN.                                     00205
*                                                                       00206
*      02 JAN 90 - 22 JAN 91: CLARK MORRIS, PHILIPS LIGHTING.           00207
*         1. CHANGE NAME TO PUT1B033, ALLOW DELETE BY PARM ( 1 MEMBER)  00208
*         2. CHANGE RETURN CODE FOR NOT FOUND CONDITION FROM 12 TO 4    00209
*         3. ADD SPFEDIT AND SYSIEWLP ENQUEUES BEFORE LIBRARY OPEN.     00210
*                                                                       00211
*********************************************************************** 00212
*                                                                       00213
PUT1B033 CSECT                                                          00214
*                                                                       00215
*********************************************************************** 00216
*                                                                       00217
*   IDENTIFICATION & LINKAGE:                                           00218
*                                                                       00219
         SAVE  (14,12),,PUT1B033                                        00220
         LR    R12,R15                                                  00221
         USING PUT1B033,R12                                             00222
         LR    R15,R13                                                  00223
         LA    R13,SAVEAREA                                             00224
         ST    R15,SAVEAREA+4                                           00225
         ST    R13,8(,R15)                                              00226
*                                                                       00227
*********************************************************************** 00228
*                                                                       00229
         COPY WEJREGEQ                                                  00230
*                                                                       00231
*********************************************************************** 00232
*   BEGIN.                                                              00233
*                                                                       00234
         L     R2,0(,R1)         GET ADDRESS OF PARM AREA               00235
         OPEN  (SYSPRINT,(OUTPUT))   OPEN PRINT FILE                    00236
         TM    SYSPRINT+48,X'10' IF BAD OPEN GO TO ABEND                00237
         BNO   BADOPEN2                                                 00238
         LH    R3,0(,R2)         GET LENGTH OF PARM                     00239
         LTR   R3,R3             IF ZERO, INPUT IS FROM CARDS           00240
         BZ    OPEN1                                                    00241
         CH    R3,=H'8'          IF PARM SIZE GREATER THAN 8, BAD NAME  00242
         BH    BADPARM             SO WRITE MESSAGE AND ABEND           00243
         BCTR  R3,R0             ELSE ADJUST IT FOR AN MVC AND MOVE     00244
         EX    R3,MVPARM           THE NAME TO THE NAME PARM AREA       00245
         B     OPEN2               AND BYPASS OPEN OF SYSIN             00246
MVPARM   MVC   MEMBER(0),2(R2)                                          00247
OPEN1    DS    0H                                                       00248
         OPEN (SYSIN,(INPUT))                                           00249
         TM    SYSIN+48,X'10'    IF BAD OPEN OF SYSIN GO TO ABEND       00250
         BNO   BADOPEN1                                                 00251
OPEN2    DS    0H                                                       00252
         CALL  PUT1S001,(SYSUT1,DSNAREA,WORKADDR),VL                    00253
         LTR   R2,R15                                                   00254
         BNZ   BADENQ                                                   00255
         OPEN  (SYSUT1,(OUTPUT))                                        00256
         TM    SYSUT1+48,X'10'                                          00257
         BNO   BADOPEN3                                                 00258
*   PRINT HEADER.                                                       00259
         MVC   TTLPGMNM,5(R12)  PUT PGM-ID IN PRINT HEADER LINE         00260
         TIME  DEC * GET CURRENT TIME.                                  00261
         SRL   R1,4 * REMOVE PLUS SIGN.                                 00262
         ST    R1,TIME * SAVE YEAR & DAY.                               00263
         ST    R0,TIME+4 * SAVE HOURS, MINUTES, SECONDS, ETC.           00264
         MVC   TTLDTTME,PAT2 * PUT EDIT PATTERN IN                      00265
*                                       * PRINT LINE.                   00266
         ED    TTLDTTME,TIME+1 * PUT YEAR DAY, HOUR,                    00267
*                 * MINUTE & SECOND IN PRINT LINE.                      00268
         MVC   TTLDSNAM,DSNAME                                          00269
         CLC     MEMBER,=CL8' '    IS MEMBER NAME BLANK                 00270
         BE      READ                                                   00271
         MVC     PRTCOMM,=C'PARM DELETE OF MEMBER='                     00272
         MVC     PRTMEMB,MEMBER                                         00273
         ZAP     LINEADV,=P'2'                                          00274
         MVI     PRTCTLCH,C'0'                                          00275
         BAL     R14,PUTPRINT                                           00276
         BAL     R14,DELMEMBR      PERFORM DELETE MEMBER ROUTINE        00277
         B       CLOSEPRT                                               00278
*********************************************************************** 00279
*   READ CONTROL CARDS.                                                 00280
*                                                                       00281
READ     DC       0H'0'                                                 00282
         GET      SYSIN,CARD * GET NEXT CONTROL CARD.                   00283
         MVI      SW1,B'00000000' * RESET ALL SWITCHES.                 00284
*   PRINT CONTROL CARD.                                                 00285
         MVI      PRTCTLCH,C'0'   SET TO DOUBLE SPACE                   00286
         ZAP      LINEADV,=P'2'                                         00287
         MVC      PRTCARD,CARD * COPY CONTROL CARD TO                   00288
*                                     * PRINT LINE.                     00289
         BAL      R14,PUTPRINT * PRINT LINE.                            00290
         CLI      CARD,C'*' * IS THIS A COMMENT CARD                    00291
         BE       READ      * YES, GET NEXT CARD                        00292
         LA       R3,CARD   * LOAD @ BYTE AT BEGINNING OF CARD          00293
         LA       R6,71     * LOAD @ CMD LAST BYTE.                     00294
*********************************************************************** 00295
*   DETERMINE FUNCTION REQUESTED.                                       00296
*                                                                       00297
FINDFUNC DC       0H'0'                                                 00298
         EX       R6,TRTNOTSP       SCAN FOR NOT SPACE                  00299
         BNZ      CHKOPER  * GO TO CHECK OPERATOR                       00300
         LA       R15,36   * SET CODE FOR ENTIRE CARD BLANK             00301
         B        CARDERR                                               00302
CHKOPER  DS       0H                                                    00303
         CLC      =C'DELETE ',0(R1) * TEST DELETE & CONTROL STRING.     00304
         BE       DELETE * DELETE FUNCTION REQUESTED.                   00305
         CLC      =C'RENAME ',0(R1) * TEST RENAME & CONTROL STRING.     00306
         BE       CHANGE * RENAME FUNCTION REQUESTED.                   00307
         CLC      =C'ALIAS ',0(R1) * TEST ALIAS & CONTROL STRING.       00308
         BE       ALIAS1 * ALIAS FUNCTION REQUESTED.                    00309
*********************************************************************** 00310
*   SIGNAL INVALID CONTROL CARD.                                        00311
*                                                                       00312
CARDBAD  DC       0H'0' * INVALID CONTROL CARD.                         00313
         LA       R15,40 * LOAD PERFORMANCE FOR BAD CONTROL CODE        00314
         B        CARDERR                                               00315
CARDERR  DS       0H                                                    00316
         BAL      R14,QUALITY * RECORD PERFORMANCE.                     00317
         B        READ                                                  00318
*********************************************************************** 00319
*   ADD AN ALIAS.                                                       00320
*                                                                       00321
ALIAS1   DC       0H'0' * DO ALIAS FUNCTION.                            00322
         LA       R6,CARD+70                                            00323
         LA       R3,5(,R1) * INCR CARD CURRENT BYTE PTR.               00324
         SR       R6,R3       GET REMAINDER SIZE                        00325
         BAL      R14,OPDSCN * SCAN CONTROL CARD FOR OPERANDS.          00326
         TM       SW1,B'11000000' * TEST MEMBERE& OLDRMEMBERTNAME BITS. 00327
         BO       MOVENAME *  ALL REQUIRED OPERANDS NOT FURNISHED.      00328
         LA       R15,28  * SET PERFORMANCE CODE FOR MISSING OPERAND    00329
         B        CARDERR                                               00330
MOVENAME DS       0H                                                    00331
         MVC      NAME,MEMBER * PUT MEMBER NAME IN BLDL LIST.           00332
         BLDL     SYSUT1,LIST * SEARCH FOR MEMBER NAME.                 00333
         LTR      R15,R15 * TEST BLDL RETURN CODE.                      00334
         BZ       ALIAS2 * BLDL SUCCESSFUL.                             00335
         LA       R15,8 * LOAD PERFORMANCE CODE.                        00336
         BAL      R14,QUALITY * RECORD PERFORMANCE.                     00337
         B        READ                                                  00338
ALIAS2   DC       0H'0'                                                 00339
         MVC      NAME,NEWNAME * PUT ALIAS NAME IN STOW LIST.           00340
         MVC      STOWC(63),BLDLC * CVT BLDL LIST TO STOW LIST.         00341
         OI       STOWC,B'10000000' * SET ALIAS BIT.                    00342
         STOW     SYSUT1,NAME,A * STOW ALIAS NAME.                      00343
         BAL      R14,QUALITY * RECORD PERFORMANCE.                     00344
         B        READ                                                  00345
*********************************************************************** 00346
*   RENAME A MEMBER.                                                    00347
*                                                                       00348
CHANGE   DC       0H'0' * DO CHANGE FUNCTION.                           00349
         LA       R3,6(,R1) * INCR CARD CURRENT BYTE PTR.               00350
         BAL      R14,OPDSCN * SCAN CONTROL CARD FOR OPERANDS.          00351
         TM       SW1,B'11000000' * TEST MEMBERE& OLDRMEMBERTNAME BITS. 00352
         BO       CHNGNAME * ALL OPERANDS PRESENT                       00353
         LA       R15,28  * SET PERFORMANCE CODE FOR MISSING OPERAND    00354
         B        CARDERR                                               00355
CHNGNAME DS       0H                                                    00356
         STOW     SYSUT1,MEMBER,C * ISSUE CHANGE STOW.                  00357
         MVC      PRTMEMB,MEMBER                                        00358
         MVC      PRTNEWN,NEWNAME                                       00359
         BAL      R14,QUALITY * RECORD PERFORMANCE.                     00360
         B        READ                                                  00361
*********************************************************************** 00362
*   DELETE A MEMBER.                                                    00363
*                                                                       00364
DELETE   DC       0H'0' * DO DELETE FUNCTION.                           00365
         LA       R3,6(,R1) * INCR CARD CURRENT BYTE PTR.               00366
         BAL      R14,OPDSCN * SCAN CONTROL CARD FOR OPERANDS.          00367
         TM       SW1,B'10000000' * TEST MEMBER NAME BIT.               00368
         BO       PERFDEL * ALL REQUIRED OPERANDS  FURNISHED.           00369
         LA       R15,28  * SET PERFORMANCE CODE FOR MISSING OPERAND    00370
         B        CARDERR                                               00371
PERFDEL  DS       0H                                                    00372
         BAL      R14,DELMEMBR     PERFORM ACTUAL DELETE ROUTINE        00373
         B        READ                                                  00374
*********************************************************************** 00375
*   DO THE ACTUAL DELETE                                                00376
DELMEMBR DS       0H                                                    00377
         ST       R14,DELMEMEX                                          00378
         STOW     SYSUT1,MEMBER,D * ISSUE DELETE STOW.                  00379
         MVC      PRTMEMB,MEMBER                                        00380
         BAL      R14,QUALITY * RECORD PERFORMANCE.                     00381
         L        R14,DELMEMEX                                          00382
         BR       R14   EXIT FROM ACTUAL DELETE ROUTINE                 00383
DELMEMEX DC       F'0',C'DELMEMEX'                                      00384
*********************************************************************** 00385
*   END OF PROCESSING.                                                  00386
*                                                                       00387
END      DC       0H'0'                                                 00388
         CLOSE    SYSIN                                                 00389
CLOSEPRT DS       0H                                                    00390
*   PRINT RETURN CODE.                                                  00391
         MVC      LINPRINT+1(L'ENDMSG),ENDMSG * PUT END MSG IN         *00392
                                              * PRINT LINE.             00393
         L        R15,RTRNCODE * LOAD RETURN CODE.                      00394
         CVD      R15,PACKED * CALC PACK DEC RETURN CODE.               00395
         MVC      EDIT,PAT1 * PUT PATTERN IN EDIT FIELD.                00396
         ED       EDIT,PACKED+6 * EDIT RETURN CODE.                     00397
         CLI      EDIT+2,C' ' * TEST RETCOD 10S DIGIT & BLANK.          00398
         BNE      RETCOD10 * RETCOD >= 10.                              00399
         MVC      LINPRINT+L'ENDMSG+1(1),EDIT+3 * PUT RETCOD IN        *00400
                                                * PRINT LINE.           00401
         B        RETPRT                                                00402
RETCOD10 DC       0H'0'                                                 00403
         MVC      LINPRINT+L'ENDMSG+1(2),EDIT+2 * PUT RETCOD IN        *00404
                                                * PRINT LINE.           00405
RETPRT   DC       0H'0' * CLOSE ALL DATA SETS.                          00406
         BAL      R14,PUTPRINT * PRINT LINE.                            00407
         CLOSE    (SYSPRINT,,SYSUT1) * CLOSE  PRINT & PDS DATA SETS.    00408
         CALL  PUT1S001,(SYSUT1,DSNAREA,WORKADDR),VL                    00409
         L        R15,RTRNCODE * LOAD RETURN CODE.                      00410
         L     R13,SAVEAREA+4      A(HSA)                               00411
         RETURN   (14,12),T,RC=(15) * LOAD REG, KEEP RETCOD, & RETURN.  00412
*********************************************************************** 00413
*   BAL ROUTINES.                                                       00414
*********************************************************************** 00415
*   SCAN CONTROL CARD OPERAND FIELD.                                    00416
*                                                                       00417
OPDSCN   DC       0H'0'                                                 00418
         ST       R14,OPDSCNEX                                          00419
         EX       R6,TRTNOTSP                                           00420
         BNZ      OPDSCN2                                               00421
         LA       R15,28  * SET PERFORMANCE CODE FOR MISSING OPERAND    00422
         B        CARDERR                                               00423
OPDSCN2  DC       0H'0'                                                 00424
         CLC      =C'MEMBER=',0(R1) * TEST MEMBER & OPERAND STRING.     00425
         BE       MEMBEROP * MEMBER KEYWORD FOUND.                      00426
         CLC      =C'NEWNAME=',0(R1) * TEST NEWNAME & OPERAND STRING.   00427
         BE       NEWNAMOP * NEWNAME KEYWORD FOUND.                     00428
         LA       R15,28  * SET PERFORMANCE CODE FOR MISSING OPERAND    00429
         B        CARDERR                                               00430
MEMBEROP DC       0H'0'                                                 00431
         OI       SW1,B'10000000' * SET MEMBER FOUND BIT.               00432
         LA       R3,7(,R1) * INCR CARD CURRENT BYTE PTR FOR KEYWORD.   00433
         LA       R4,MEMBER * LOAD @ MEMBER NAME AREA.                  00434
         B        MEMBNAME                                              00435
NEWNAMOP DC       0H'0'                                                 00436
         OI       SW1,B'01000000' * SET NEWNAME FOUND BIT.              00437
         LA       R3,8(,R1) * INCR CARD CURRENT BYTE PTR FOR KEYWORD.   00438
         LA       R4,NEWNAME * LOAD @ NEW MEMBER NAME AREA.             00439
MEMBNAME DC       0H'0'                                                 00440
         MVC   0(8,R4),=CL8' '    BLANK NAME AREA                       00441
MEMBCHAR DC       0H'0'                                                 00442
         LA       R6,CARD+71            POINT TO END OF CARD            00443
         SR       R6,R3 * TEST @ CARD CURRENT BYTE & @ CMD LAST BYTE.   00444
         CH       R6,=H'1'      IS THERE ROOM FOR A 1 BYTE NAME         00445
         BNH      BADNAME1                                              00446
         EX       R6,TRTDELIM                                           00447
         BZ       BADNAME2                                              00448
         LR       R7,R1         SAVE POINTER TO DELIMITER               00449
         SR       R1,R3                                                 00450
         BNP      BADNAME1      IF FIRST CHAR A DELIMITER, BAD CARD     00451
         CH       R1,=H'8'      IS NAME GREATER THAN 8 CHARACTERS       00452
         BH       BADNAME2                                              00453
         BCTR     R1,R0    * CALC MVC INSTR # BYTES.                    00454
         EX       R1,MVC1 * PUT NAME IN PROPER NAME AREA.               00455
         CLI      0(R7),C' ' IS THIS THE END OF THE OPERAND             00456
         BE       OPDSCNLD    YES, EXIT                                 00457
         LA       R1,1(,R7)  NO, POINT R1 TO NEXT OPERAND               00458
         B        OPDSCN2                                               00459
OPDSCNLD DS       0H                                                    00460
         L        R14,OPDSCNEX                                          00461
         BR       R14                                                   00462
OPDSCNEX DC       F'0',C'OPDSCNEX'                                      00463
BADNAME1 DS       0H                                                    00464
         LA       R15,44    SET PERFORMANCE CODE TO NAME MISSING        00465
         B        CARDERR                                               00466
BADNAME2 DS       0H                                                    00467
         LA       R15,32    SET PERFORMANCE CODE TO NAME NOT 1-8 CHAR   00468
         B        CARDERR                                               00469
*********************************************************************** 00470
*   PRINT A LINE.                                                       00471
*                                                                       00472
PUTPRINT DC       0H'0' * WRITE PRINT LINE & INIT LINE AREA.            00473
         ST       R14,PUTPRTEX                                          00474
         SP       LINELEFT,LINEADV   SUBTRACT LINE ADVANCE COUNT        00475
         BP       PUTDETL         GO TO PUT DETAIL                      00476
         AP       PGCNT,=P'1'                                           00477
         UNPK     TTLPGCNT,PGCNT                                        00478
         OI       TTLPGCNT+L'TTLPGCNT-1,C'0' FORCE F ZONE ON LSC        00479
         LA       R0,TTLLINE                                            00480
         BAL      R14,PUTLINE                                           00481
         MVI      PRTCTLCH,C'0'                                         00482
         ZAP      LINELEFT,=P'58'                                       00483
PUTDETL  DS       0H                                                    00484
         LA       R0,LINPRINT                                           00485
         BAL      R14,PUTLINE                                           00486
         ZAP      LINEADV,=P'1'                                         00487
         MVI      PRTCTLCH,C' ' * BLANK 1ST  CHAR.                      00488
         MVC      LINPRINT+1(L'LINPRINT-1),LINPRINT  BLNK RST OF LINE.  00489
         L        R14,PUTPRTEX                                          00490
         BR       R14                                                   00491
PUTPRTEX DC       F'0',C'PUTPRTEX'                                      00492
PUTLINE  DC       0H'0' * WRITE PRINT LINE                              00493
         ST       R14,PUTLINEX                                          00494
         PUT      SYSPRINT,(R0)     * WRITE PRINT LINE.                 00495
         L        R14,PUTLINEX                                          00496
         BR       R14                                                   00497
PUTLINEX DC       F'0',C'PUTLINEX'                                      00498
*********************************************************************** 00499
*   PRINT MESSAGES AND RECORD HIGHEST RETURN CODE.                      00500
*                                                                       00501
QUALITY  DC       0H'0'                                                 00502
         ST       R14,QUALITEX                                          00503
         SRL      R15,2 * DIVIDE CODE BY 4 TO GET ENTRY # (ORIGIN 0).   00504
         MH       R15,=AL2(MSGNTR+1) * CALC MSG TABLE ENTRY OFFSET.     00505
         LA       R11,MSGTBL * LOAD @ MSG TABLE.                        00506
         AR       R11,R15 * CALC @ MSG TABLE ENTRY.                     00507
*   PRINT ERROR MESSAGE.                                                00508
         MVC      PRTCOMM(MSGNTR),1(R11) * PUT MSG IN PRINT LINE.       00509
         BAL      R14,PUTPRINT * PRINT LINE.                            00510
         SR       R0,R0 * LOAD 0.                                       00511
         IC       R0,0(R11) * LOAD CURRENT RETURN CODE.                 00512
         C        R0,RTRNCODE * TEST CURRENT AND MAX RETURN CODES.      00513
         BNH      QUALITLD * CURRENT Â¬> MAX RETURN CODE.                00514
         ST       R0,RTRNCODE * SET NEW MAX RETURN CODE.                00515
QUALITLD DS       0H                                                    00516
         L        R14,QUALITEX                                          00517
         BR       R14                                                   00518
QUALITEX DC       F'0',C'QUALITEX'                                      00519
*********************************************************************** 00520
*   EXIT ROUTINES.                                                      00521
*********************************************************************** 00522
*********************************************************************** 00523
*   DCB STORAGE.                                                        00524
*********************************************************************** 00525
SYSIN    DCB      DDNAME=SYSIN,DSORG=PS,MACRF=GM,                      *00526
               RECFM=FB,LRECL=80,                                      *00527
               EODAD=END * CONTROL DATA SET.                            00528
*********************************************************************** 00529
SYSPRINT DCB      DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,                   *00530
               RECFM=FBA,LRECL=121                                      00531
*********************************************************************** 00532
SYSUT1   DCB      DDNAME=SYSUT1,DSORG=PO,MACRF=W * PDS DATA SET.        00533
*********************************************************************** 00534
*   LITERAL STORAGE.                                                    00535
*********************************************************************** 00536
         LTORG                                                          00537
*********************************************************************** 00538
*   STRUCTURED STORAGE.                                                 00539
*********************************************************************** 00540
*   LIST FOR BLDL AND STOW MACROS.                                      00541
*                                                                       00542
LIST     DC       H'1' * # LIST ENTRIES.                                00543
         DC       H'76' * # ENTRY BYTES.                                00544
NAME     DC       CL8' ' * MEMBER NAME.                                 00545
         DC       X'000000' * TTR.                                      00546
BLDLCON  DC       X'00' * CONCATENATION #.                              00547
         DC       X'00' * LIBRARY FOUND CODE.                           00548
BLDLC    DC       X'00' * ALIAS, TTRN, AND # USER DATA BYTES.           00549
         DC       CL62' ' * USER DATA.                                  00550
         ORG      BLDLCON * REDEFINE LIST FOR STOW.                     00551
STOWC    DS       X'00' * ALIAS, TTRN, AND # USER DATA BYTES.           00552
         DS       CL62' ' * USER DATA.                                  00553
         ORG                                                            00554
*********************************************************************** 00555
*   OLD AND NEW MEMBER NAMES FOR STOW RENAME AND GENERAL USE.           00556
*                                                                       00557
MEMBER   DC       CL8' ' * OLD MEMBER NAME AREA.                        00558
NEWNAME  DC       D'0'   * NEW MEMBER NAME AREA.                        00559
*********************************************************************** 00560
*   MESSAGE TABLE WITH RETURN CODE AND MESSAGE TEXT.                    00561
*                                                                       00562
MSGNTR   EQU      16 * MAX # BYTES IN A MESSAGE.                        00563
MSGTBL   DC       0X'00' * ENTIRE MESSAGE TABLE.                        00564
M000     DC       AL1(00),CL(MSGNTR)'ACTION COMPLETED'                  00565
M004     DC       AL1(12),CL(MSGNTR)'DUPLICATE NAME'                    00566
M008     DC       AL1(04),CL(MSGNTR)'MEMBER NOT FOUND'                  00567
M012     DC       AL1(16),CL(MSGNTR)'DIRECTORY FULL'                    00568
M016     DC       AL1(20),CL(MSGNTR)'I/O ERROR'                         00569
M020     DC       AL1(16),CL(MSGNTR)'SYSUT1 NOT OPEN'                   00570
M024     DC       AL1(16),CL(MSGNTR)'GETMAIN FAILED'                    00571
M028     DC       AL1(12),CL(MSGNTR)'MISSING OPERAND'                   00572
M032     DC       AL1(12),CL(MSGNTR)'NAME NOT 1-8 CH'                   00573
M036     DC       AL1(04),CL(MSGNTR)'CARD BLANK    '                    00574
M040     DC       AL1(12),CL(MSGNTR)'BAD OPERATION '                    00575
M044     DC       AL1(12),CL(MSGNTR)'NAME MISSING  '                    00576
*********************************************************************** 00577
*   ELEMENT STORAGE.                                                    00578
*********************************************************************** 00579
CARD     DC       CL80' ' * CONTROL CARD AREA.                          00580
TTLLINE  DS       0CL121                                                00581
TTLCHR   DC       C'1'                                          000-000 00582
TTLPGMNM DC       CL8' '                                        001-008 00583
         DC       CL08' RUN AT '                                009-016 00584
TTLDTTME DC       CL16' '                                       017-032 00585
         DC       CL15' FOR LIBRARY - '                         033-047 00586
TTLDSNAM DC       CL44' '                                       048-091 00587
         DC       CL21' '                                       092-112 00588
         DC       CL5'PAGE '                                    113-117 00589
TTLPGCNT DC       CL3'000'                                      118-120 00590
LINELEFT DC       P'000'                                                00591
LINEADV  DC       P'1'                                                  00592
PGCNT    DC       P'0'                                                  00593
DSNAREA  DS       0CL52      DATA SET NAME AND MEMBER FROM ENQ SUB      00594
DSNAME   DC       CL44' '      PUT1S001                                 00595
DSMEMBER DC       CL8' '                                                00596
EDIT     DC       CL4' ' * EDIT AREA.                                   00597
ENDMSG   DC       C'PUT1B033.  END.  RETURN CODE=' * END PGM MSG.       00598
LINPRINT DS       0CL121  * SYSPRINT PRINT LINE.                        00599
PRTCTLCH DC       C' '                                                  00600
PRTCARD  DS       0CL80                                                 00601
PRTCOMM  DC       CL22'                      '                          00602
PRTMEMB  DC       CL8' '                                                00603
         DC       C' '                                                  00604
PRTNEWN  DC       CL8' '                                                00605
         DC       CL41' '                                               00606
         DC       CL40' '                                               00607
MVC1     MVC      0(0,R4),0(R3) * KEYWORD OPERAND SAVING INSTR.         00608
TRTNOTSP TRT      0(0,R3),NOTSPTBL * CHECK FOR NOT SPACE                00609
TRTDELIM TRT      0(0,R3),DELIMTBL * CHECK FOR SPACE OR COMMA           00610
NOTSPTBL DC       256X'01'                                              00611
         ORG      NOTSPTBL+X'40'                                        00612
         DC       X'00'                                                 00613
         ORG                                                            00614
DELIMTBL DC       XL256'00'                                             00615
         ORG      DELIMTBL+X'40'                                        00616
         DC       X'01'                                                 00617
         ORG      DELIMTBL+C','                                         00618
         DC       X'01'                                                 00619
         ORG                                                            00620
PACKED   DC       D'0' * PACKED DECIMAL WORK AREA.                      00621
PAT1     DC       X'40202120' * RETURN CODE PATTERN.                    00622
PAT2     DC       X'402021204B2020204B202020204B2020' * TIME PATTERN.   00623
RTRNCODE DC       F'0' * PGM RETURN CODE.                               00624
WORKADDR DC       F'0' * USED BY PUT1S001, DON'T CHANGE                 00625
SW1      DC       B'00000000' * CONTROL OPERAND BIT SWITCHES.           00626
TIME     DC       2F'0' * CURRENT TIME (000YYDDDHHMMSS--).              00627
SAVEAREA DC    18F'0'                                                   00628
         SPACE 2                                                        00629
BADOPEN1 DS    0H                                                       00630
         WTO   'PUT1B033-001 UNABLE TO OPEN SYSIN'                      00631
         ABEND 1                                                        00632
BADOPEN2 DS    0H                                                       00633
         WTO   'PUT1B033-002 UNABLE TO OPEN SYSPRINT'                   00634
         ABEND 2                                                        00635
BADOPEN3 DS    0H                                                       00636
         WTO   'PUT1B033-003 UNABLE TO OPEN SYSUT1'                     00637
         ABEND 3                                                        00638
BADPARM  DS    0H                                                       00639
         WTO   'PUT1B033-004 PARM (MEMBER-NAME TO BE DELETED) GREATER T+00640
               HAN 8 BYTES'                                             00641
         ABEND 4                                                        00642
BADENQ   DS    0H                                                       00643
         WTO   'PUT1B033-004 UNABLE TO ENQ FILE RELATED TO SYSUT1 OR DC+00644
               B INFORMATION IN JCL'                                    00645
         ABEND (R2)                                                     00646
         LUT1A001 STAMP='035PUT1B033  02/04/91 13.59.27  '              00647
         END                                                            00648
