         TITLE 'IGC0022F,SVC 226, WRITE USER SMF RECORD'
*-------
*------- TITLE: IGC0022F,SVC 226, WRITE USER SMF RECORD.
*-------
*------- AUTHOR: DICK SZIEDE CSC 1972
*-------
*------- PURPOSE: WRITE SMF RECORD CONTAINING ACCOUNTING DATA
*-------       FROM SUCH PACKAGES AS TELAGRAF, THAT REQUIRE THEIR
*-------       OWN ACCOUNTING DATA.
*-------
*------- METHOD:THE CALLER MUST PROVIDE A POINTER TO THE SMF RECORD IN
*-------       REGISTER ONE.  SVC226 WILL VALIDATE THE ARGUMENTS,
*-------       FILL IN THE STANDARD SMF HEADER AND DISPATCH
*-------       THE RECORD WITH A SMFWTM MACRO.
*-------
*-------       THE RECORD PROVIDED MUST CONTAIN 26-BYTES PREFIX FOR
*-------       THE HEADER.
*-------
*------- ARGUMENTS: THE SMF HEADER MUST CONTAIN "240" FOR SMF
*-------       RECORD TYPE.  THE USER SECTION LENGTH INDICATOR
*-------       MUST BE VALID: GREATER THAN ZERO AND, JUST TO BE A
*-------       SNOT, I'LL LIMIT IT TO LESS THAN 4096.
*-------       THE USER RECORD TYPE MUST BELONG TO A KNOWN LIST.
*-------       ASSIGNMENTS ARE:
*-------           1-USPTO SYSTEM MANAGEMENT SUBSYSTEM
*-------           2-VTAM SESSION ACCOUNTING
*-------           3-USPTO MESSENGER ACCOUNTING
*-------           4-
*-------           5-
*-------
*-------
*------- OUTPUT: RETURN CODES ARE PROVIDED IN REGISTERS 15, AND ZERO,
*-------       AS FOLLOWS.
*-------       R15 - 0  SMF RECORD WRITTEN
*-------             4
*-------             8  UNKNOWN USER RECORD TYPE.
*-------            12  BAD RETURN FROM SMFWTM. R0 HAS SMFWTM RC.
*-------            16  INVALID CALLING PARAMETERS.
*-------       R0  - 0  WILL NOT HAPPEN.
*-------            Â¬0  WILL BE NON-ZERO RC FROM SMFWTM. R15 = 12
*-------
*------- NOTE: THIS CODE CONTAINS  NO PROTECTION AGAINST RECURSION.
*-------       A LOOPING USER COULD FILL SMF.  AN 0C4 IS POSSIBLE
*-------       IF R1 IS NON-ZERO,  BUT POINTS TO A PROTECTED AREA.
*-------
*------- CHANGES: CHANGE SMF230 MACRO TO SMF240.  SMF230 WAS ALREADY
*-------       TAKEN HERE AT THE USPTO PROJECT, AND SET THE XA BIT
*-------       IN THE RECORD HEADER.  CHECK TO BE SURE THE SMF RECORD
*-------       IS BELOW 16MEG LINE.
*-------       DICK SZIEDE, PRC, 1985.
*-------       ALLOW  RECORD SUBTYPES 1-5. PTW 06/24/86.
*-------
IGC0022F CSECT
IGC0022F AMODE 31
IGC0022F RMODE ANY
         REGS
         USING *,R6                ADDRESSABILITY
         USING TCB,R4              ADDRESSABILITY
         USING ASCB,R7             ADDRESSABILITY
         USING CVT,R3              ADDRESSABILITY
*-------
*-------       VALIDATION ROUTINES
*-------
         LTR   R11,R1              CHECK FOR VALID RECORD POINTER
         BZ    DUWHUT              IT MUST BE NON-ZERO, AND LESS THAN
         N     R11,=F'2130706432'  16-MEG.
         BNZ   DUWHUT              NO SMF RECORD ==> RC 16.
         LR    R11,R1
         USING SM240BLK,R11        ELSE MAKE IT ADDRESSABLE
         LR    R11,R1
         CLI   SM240RTY,240        CSC USER RECORD?
         BNE   DUWHUT              NO ==> RC 16
         LH    R2,SM240ULN         IS THE LENGTH IF THE USER SECTION
         CH    R2,=H'4096'            VALID?
         BH    DUWHUT              NO ==> RC 16
         CLC   SM240TYP,=H'1'      IZZIT A KNOWN USER RECORD TYPE?
         BL    RET8                NO ==> RC 8.
         CLC   SM240TYP,=H'5'      IZZIT A KNOWN USER RECORD TYPE?
         BH    RET8                NO ==> RC 8.
*-------
*-------       FILL IN SMF HEADER
*-------
SMFHEAD  AH    R2,=H'26'           ADD IN LENGTH OF SMF HEADER
         SLL   R2,16               AND ZERO SEGMENT DESCRIPTOR
         ST    R2,SM240LEN
         SPACE
         L     R2,CVTSMCA          FIND THE SMF CONTROL AREA
         MVC   SM240SID,X'10'(R2)  AND GIT THE SID
         OI    SM240FLG,X'06'      SAY WE'RE MVS/XA  9/20/85--RRS
         SPACE
         ICM   R2,15,TCBTCT        GET THE READER TIME OUT OF JMR
         BZ    NOJMR
         USING SMFTCT,R2
         ICM   R2,15,TCTJMR
         BZ    NOJMR
         DROP  R2
         USING JMR,R2
         MVC   SM240RST(8),JMRDRSTP
         DROP  R2
         SPACE
NOJMR    TIME  BIN                 GET BUFFER MOVE TIME INTO RECORD
         STCM  R0,15,SM240TME
         STCM  R1,15,SM240DTE
         SPACE
*-------
*-------       DO THE DEED
*-------
         SMFWTM (R11)
         LTR   R15,R15             DID IT WORK?
         BZR   R14
         SPACE
RETC     LR    R0,R15              NO ==> SAVE SMFWTM RC
         LA    R15,12              AND RETURN 12 OURSELVES
         BR    R14
         SPACE
RET8     LA    R15,8
         BR    R14
DUWHUT   LA    R15,16
         BR    R14
         LTORG
SRCLEVEL DC    C'IGC0022F V01.M05 85/09/26 17:57 PSYRRS  '
*                MODULE   VER/MOD   DATE   TIME  USER NAME
COPYRIGT DC    C' COPYRIGHT (C) 1985 PLANNING RESEARCH CORPORATION '
ASMSTAMP DC    C'ASM &SYSDATE &SYSTIME'
         PRINT ON,NOGEN
         IKJTCB
         IHAASCB
         IEFJMR
         IEFTCT
         CVT   DSECT=YES
         PRINT ON,GEN
         SMF240 DSECT=YES
         END
