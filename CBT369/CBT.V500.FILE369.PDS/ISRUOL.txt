/*********************************************************************
/*
/* CHANGE LOG -
/*  02/05/86 PTW IMPLEMENTED
/*  03/17/86 CJP ADDED TBSORT PRIOR TO FIRST TBDISPL TO PUT JOBS IN
/*               DESCENDING JES NUMBER ORDER
/*  05/08/86 PTW FIX BUGS, ADD FEATURES, ETC.
/*
/*********************************************************************
/*********************************************************************
/*
/* IBM CODE BEGINS HERE.  WE HAVE INCLUDED ISRUOL INTACT.  PRC CHANGES
/*   ARE MARKED "PRC".
/*
/*********************************************************************
         PROC 0
           CONTROL NOFLUSH                     /* OUTLIST UTILITY(3.8)/
           SET &MSGID =
         LOOP: +
           ISPEXEC DISPLAY PANEL(ISRUOL01) MSG(&MSGID)
           IF &LASTCC = 8 THEN +
             EXIT CODE(0)                      /* END WAS REQUESTED  */
           ELSE +
             DO                                /* END NOT REQUESTED  */
  IF &ZCMD ¬= L THEN ISPEXEC CONTROL DISPLAY LINE START(20) /* PRC   */
               SET &MSGID =
               IF &ZC ¬=  THEN                                +
                 SET &CLASS = CLASS(&ZC.)    /* SET UP CLASS         */
               ELSE +
                 SET &CLASS =
               IF &TJID ¬=  THEN +
                 SET &JOBID = &STR((&TJID.&STR())) /* SET UP JOBID   */
               ELSE +
                 SET &JOBID =
               IF (&ZCMD = P OR &ZCMD =  )  THEN +
                 DO                            /* ALLOC PRINT DATASET*/
                   SET &ZOLID = 100            /*                    */
                   ISPEXEC VGET (ZOLID) PROFILE
                   IF (&LASTCC = 8 OR &ZOLID = 999) THEN +
                     SET &ZOLID = 100          /* GENERATE 3 DIGIT # */
                   ELSE                        /* TO MAKE DSN UNIQUE */+
                     SET &ZOLID = &ZOLID + 1
                   ISPEXEC VPUT (ZOLID) PROFILE
                   /* OZ88258 REMOVED COMMENTS BEYOND COL 72 BELOW  */
                   ISPEXEC VGET (ZAPPLID) ASIS
                     /* GET APPLICATION ID  AND */
                   ISPEXEC TBSAVE &ZAPPLID.PROF LIBRARY(ISPPROF)
                     /* SAVE IT IN CASE OF ABNORMAL TERMINATION    */
                     /* OZ80534 */
                   IF (&STR(&SYSUID) = &STR(&SYSPREF) OR +
                     &LENGTH(&SYSPREF)=0) THEN +
                     SET &DSN = &SYSUID..SPF&ZOLID..OUTLIST
                     /* OZ83124 */
                   /* OZ88258 REMOVED COMMENTS BEYOND COL 72 ABOVE */
                   ELSE  +
                     SET &DSN = &SYSPREF..&SYSUID..SPF&ZOLID..OUTLIST
                   CONTROL NOMSG
                   FREE FI(SPFUOL1)
                   IF (&ZCMD = ) THEN         /*            OZ83443 */+
                     DO                       /*            OZ83443 */
                   ATTR SPFUOL1 BLKSIZE(3059) LRECL(133)  /*OZ83443*/ +
                     DSORG(PS) RECFM(F B)     /*            OZ83443 */
                     END                      /*             OZ83443 */
                   ELSE                       /*             OZ83443*/+
                     DO                       /*            OZ83443 */
                   ATTR SPFUOL1 BLKSIZE(3059) LRECL(133) +
                     DSORG(PS) RECFM(F B &ZR)
                     END                      /*             OZ83443 */
                   CONTROL MSG
                   ERROR +
                     DO                        /* EXIT USED FOR      */
                       SET &MSGID = ISRU220    /* ALLOCATION ERROR   */
                       RETURN
                     END
                   ALLOC DA('&DSN') TRACKS     /* ATTRIBUTES OF THE  */+
                     USING(SPFUOL1) RELEASE    /* PRINT DATASET MAY  */+
                     SPACE(200 100) CATALOG    /* BE CHANGED TO FIT  */
                   ERROR OFF                   /* LOCAL ENVIRONMENT. */
                 END                           /* TAKE CARE          */
               IF &ZCMD = P AND &MSGID =   THEN +
                 DO                            /* PRINT OPTION       */
                   OUTPUT &TJNM.&JOBID &CLASS PRINT('&DSN') BEGIN
                   ISPEXEC LMINIT DATAID(PRINTDSN) DATASET('&DSN')
                                               /* INIT DATAID OZ84629*/
                   ISPEXEC LMOPEN DATAID(&PRINTDSN)
                                               /* OPEN DATAID OZ84629*/
                   ISPEXEC LMGET  DATAID(&PRINTDSN) MODE(LOCATE) +
                           DATALOC(PRINTBUF) DATALEN(PRINTLEN)   +
                           MAXLEN(32768)       /* READ REC    OZ84629*/
                   SET &RC = &LASTCC
                   ISPEXEC LMFREE DATAID(&PRINTDSN)
                                               /* FREE DATAID OZ84629*/
                   IF &RC ¬= 0 THEN            /* CHECK FOR          */+
                     DO                        /* EMPTY DATASET      */
                       SET &MSGID = ISRU222
                       CONTROL NOMSG
                       DELETE '&DSN'
                       CONTROL MSG
                     END
                   ELSE +
                     DO                        /* FOREGROUND PRINT   */
                       FREE DA('&DSN')
                       ISPEXEC CONTROL ERRORS CANCEL
                       SET &ZOCLA =              /* INIT VARIABLES   */
                       SET &ZOPID =              /* IN CASE THEY ARE */
                       SET &ZOJB1 =              /* NOT IN THE       */
                       SET &ZOJB2 =              /* PROFILE POOL     */
                       SET &ZOJB3 =
                       SET &ZOJB4 =
                       ISPEXEC VGET (ZOCLA,ZOPID, +
                         ZOJB1,ZOJB2,ZOJB3,ZOJB4) PROFILE
                       SET &ZCLA = &STR(&ZOCLA)  /* THESE VARIABLES  */
                       SET &ZPID = &STR(&ZOPID)  /* ARE NEEDED BY THE*/
                       SET &ZJB1 = &STR(&ZOJB1)  /* ISRFPT PROGRAM   */
                       SET &ZJB2 = &STR(&ZOJB2)  /* IN THE SHARED    */
                       SET &ZJB3 = &STR(&ZOJB3)  /* VARIABLE POOL    */
                       SET &ZJB4 = &STR(&ZOJB4)
                       SET &ZFPRINT = ISRFPPRT
                       SET &ZFLID = &DSN
                       ISPEXEC VPUT (ZFPRINT,ZFLID,  +
                         ZCLA,ZPID,ZJB1,ZJB2,ZJB3,ZJB4)
                       ISPEXEC SELECT PGM(ISRFPT)
                       ISPEXEC VGET (ZCLA,ZPID,ZJB1,ZJB2,ZJB3,ZJB4)
                       SET &ZOCLA = &STR(&ZCLA)  /* SAVE VARIABLES   */
                       SET &ZOPID = &STR(&ZPID)  /* FROM THE PRINT   */
                       SET &ZOJB1 = &STR(&ZJB1)  /* PANEL IN THE     */
                       SET &ZOJB2 = &STR(&ZJB2)  /* PROFILE POOL     */
                       SET &ZOJB3 = &STR(&ZJB3)
                       SET &ZOJB4 = &STR(&ZJB4)
                       SET &MSGID = ISRU227
                       ISPEXEC VPUT (ZOCLA,ZOPID, +
                         ZOJB1,ZOJB2,ZOJB3,ZOJB4) PROFILE
                     END
                 END
               ELSE +
                 IF &ZCMD =  AND &MSGID =   THEN +
                   DO                          /* BROWSE OPTION      */
                     OUTPUT &TJNM.&JOBID &CLASS PRINT('&DSN') +
                       BEGIN KEEP HOLD
                     ISPEXEC CONTROL ERRORS RETURN
                     ISPEXEC BROWSE DATASET('&DSN')
                     IF &LASTCC ¬= 0 THEN      /* CHECK FOR          */+
                       SET &MSGID = ISRU221    /* EMPTY DATASET      */
                     ISPEXEC CONTROL ERRORS CANCEL
                     CONTROL NOMSG
                     DELETE '&DSN'
                     CONTROL MSG
                   END
                 ELSE +
                   IF &ZCMD = L THEN +
                     DO                        /* LIST OPTION        */
                       GOTO PRCUOL             /* PRC                */
                       SET &L = &LENGTH(&SYSUID)
                       IF &LENGTH(&TJNM) = &EVAL(&L + 1) THEN +
                         IF (&SUBSTR(1:&L,&TJNM) = &SYSUID) THEN +
                           STATUS
                         ELSE +
                           STATUS &TJNM
                       ELSE +
                         STATUS &TJNM
                     END
                   ELSE +
                     IF &ZCMD = R THEN +
                       DO                     /* REQUEUE OPTION      */
                         OUTPUT &TJNM.&JOBID &CLASS NEWCLASS(&ZN)
                         ISPEXEC LOG MSG(ISRU223)
                         SET &MSGID = ISRU225  /* REQUEUE COMPLETED  */
                       END
                     ELSE +
                       IF &ZCMD = D THEN +
                         DO                   /* DELETE OPTION       */
                           OUTPUT &TJNM.&JOBID &CLASS DELETE
                           ISPEXEC LOG MSG(ISRU224)
                           SET &MSGID = ISRU226 /* DELETE COMPLETED  */
                         END
             END
           GOTO LOOP                           /* LOOP UNTIL END     */
         END
/*********************************************************************
/*
/* IBM CODE ENDS HERE.  PRCUOL ROUTINE FOLLOWS.
/*
/*********************************************************************
PRCUOL: +
CONTROL NOFLUSH
SET &LAST =
SET &M =
SET &MSGID =
SET &ERRCMD =
SET &ERRMSG1 =
ISPEXEC VGET (ZSCREEN)
SET &FILE = PRCUOL&ZSCREEN
ISPEXEC TBCREATE MEMTABLE REPLACE NOWRITE +
        KEYS(JOB) NAMES(STATUS LAST)
/* BE SURE THAT MSGID IS ON IN PROFILE...WE DEPEND ON THIS.
SET &SETPROF =
SET &SYSOUTTRAP = 2
PROFILE LIST
SET &SYSOUTTRAP = 0
SET &SYSDVAL = &STR(&SYSOUTLINE1)
READDVAL &A &B &C &D &E &F &G &H
IF &F = &STR(NOMSGID) OR &G = &STR(NOMSGID) THEN +
  DO
    SET &SETPROF = YES
    PROFILE MSGID
  END
SET &SYSOUTTRAP = 60
SET &L = &LENGTH(&SYSUID)
ISPEXEC VGET (TJNM TJID CLASS ZN ZR) PROFILE
/* HERE WE DO OUR STATUS COMMAND AND TRAP OUTPUT.
SET &SYSOUTTRAP = 60
IF &LENGTH(&TJNM) = &EVAL(&L + 1) THEN +
  IF (&SUBSTR(1:&L,&TJNM) = &SYSUID) THEN +
    STATUS
  ELSE +
    STATUS &TJNM
ELSE +
  STATUS &TJNM
SET &SYSOUTTRAP = 0
SET &S = 1
SET &SL = &SYSOUTLINE
DO WHILE &S < &SL+1
  SET &T = &&SYSOUTLINE&S
  SET &SYSDVAL = &STR(&T)
  READDVAL  &A &B &C &D &E &F &G &H &I &J
/* IKJ56216I = NO JOBS FOUND
  IF &A = &STR(IKJ56216I) THEN +
    DO
      SET &MSGID = ISRP222
      GOTO ENDPRC
    END
/* IKJ56202I = JOB JOBNAME NOT FOUND
  IF &A = &STR(IKJ56202I) THEN +
    DO
      SET &MSGID = ISRP223
      GOTO ENDPRC
    END
/* IKJ56192I = JOB ON OUTPUT QUEUE
/* IKJ56211I = JOB EXECUTING
/* IKJ56197I = JOB WAITING FOR EXECUTION
/* IKJ56190I = JOB AWAITING NETWORK SERVICES
  IF &A = &STR(IKJ56192I) OR &A = &STR(IKJ56211I) OR   +
     &A = &STR(IKJ56197I) OR &A = &STR(IKJ56190I) THEN +
    DO
      SET J&S = &C
      SET ST&S = &STR(&D &E &F &G &H &I)
    END
  ELSE +
    DO
      SET &MSGID = ISRP224
      GOTO ENDPRC
    END
  SET &S = &S+1
END
/* ST TSO IS TREATED SPECIAL BY IKJEFF53.
IF &TJNM = &STR(TSO) THEN GOTO ENDPRC

/* BUILD OUR TABLE.
SET &S = 1
DO WHILE &S < &SL+1
  SET &K = &&J&S
  SET JOB = &K
  SET &L = &&ST&S
  SET STATUS = &STR(&L)
  ISPEXEC TBADD MEMTABLE
  SET &S = &S + 1
END

/* OUR PROCESSING LOOP.
ISPEXEC TBSORT  MEMTABLE FIELDS(JOB,C,D)
ISPEXEC TBTOP   MEMTABLE
ISPEXEC TBDISPL MEMTABLE PANEL(ISRUOLP ) POSITION(ROW)
SET &TBDRC = &LASTCC
MAINLOOP: DO WHILE &TBDRC < 8
  DO WHILE &ZTDSELS > 0
    IF &DOZCMD ¬= YES THEN ISPEXEC CONTROL DISPLAY SAVE
    SET &SYSOUTLINE1 =
    SET &SYSOUTLINE2 =
    SET &LASTJOB =
    SET &TBDRC = 0
/* BROWSE.
    IF &M = B THEN +
      DO
        SET &SYSOUTTRAP = 2
        FREE  F(&FILE)
        ALLOC F(&FILE) TRACKS RELEASE SPACE(20 50) UNIT(VIO)NEW +
          BLKSIZE(3059) LRECL(133) DSORG(PS) RECFM(F B &ZR)
        SET &RC = &LASTCC
        SET &SYSOUTTRAP = 0
        IF &RC = 0 THEN +
          DO
            ISPEXEC SELECT PGM(PRC38DSN) PARM(&FILE)
            ISPEXEC VGET (OUTDSN OUTVOL)
            SET &SYSOUTTRAP = 2
            CONTROL NOLIST NOCONLIST
            OUTPUT &JOB &CLASS PRINT('&OUTDSN') BEGIN KEEP HOLD
            SET &SYSOUTTRAP = 0
            IF &SYSOUTLINE = 0 THEN +
              DO
                ISPEXEC LMINIT DATAID(PRCDATA) DDNAME(&FILE)
                ISPEXEC LMOPEN DATAID(&PRCDATA)
                ISPEXEC LMGET  DATAID(&PRCDATA) MODE(LOCATE) +
                  DATALOC(PRINTBUF) DATALEN(PRINTLEN) MAXLEN(32768)
                SET &RC = &LASTCC
                IF  &RC = 0 THEN +
                  DO
                    ISPEXEC BROWSE DATAID(&PRCDATA)
                    SET &LAST = BROWSED
                    SET &LASTJOB = &JOB
                  END
                ELSE +
                  DO
                    SET &MSGID = ISRU221
                  END
                ISPEXEC LMFREE DATAID(&PRCDATA)
                SET &SYSOUTTRAP = 2
                FREE F(&FILE)
                SET &SYSOUTTRAP = 0
              END
            ELSE +
              DO
                SET &MSGID = ISRP220
                SET &ERRCMD = BROWSE
                SET &ERRMSG1 = &STR(&SYSOUTLINE1)
                SET &SYSOUTTRAP = 2
                FREE F(&FILE)
                SET &SYSOUTTRAP = 0
              END
          END
        ELSE +
          DO
            SET &MSGID = ISRU220
            SET &SYSOUTTRAP = 2
            FREE F(&FILE)
            SET &SYSOUTTRAP = 0
          END
      END
/* CANCEL.
    IF &M = C THEN +
      DO
        SET &SYSOUTTRAP = 2
        CANCEL &JOB
        SET &RC = &LASTCC
        SET &SYSOUTTRAP = 0
        IF &RC = 0 THEN +
          DO
            SET &LAST = CANCELLED
            SET &LASTJOB = &JOB
          END
        ELSE +
          DO
            SET &MSGID = ISRP220
            SET &ERRCMD = CANCEL
            SET &ERRMSG1= &STR(&SYSOUTLINE1)
          END
      END
/* CANCEL/DELETE.
    IF &M = CD THEN +
      DO
        SET &SYSOUTTRAP = 2
        CANCEL &JOB PURGE
        SET &RC = &LASTCC
        SET &SYSOUTTRAP = 0
        IF &RC = 0 THEN +
          DO
            SET &LAST = &STR(CANCEL/DELETED)
          END
        ELSE +
          DO
            SET &MSGID = ISRP220
            SET &ERRCMD = &STR(CAN/DEL)
            SET &ERRMSG1= &STR(&SYSOUTLINE1)
          END
      END
/* DELETE.
    IF &M = D THEN +
      DO
        SET &SYSOUTTRAP = 2
        CONTROL NOLIST NOCONLIST
        OUTPUT &JOB &CLASS DELETE
        SET &RC = &LASTCC
        SET &SYSOUTTRAP = 0
        IF &SYSOUTLINE = 0 THEN +
          DO
            SET &LAST = DELETED
          END
        ELSE +
          DO
            SET &MSGID = ISRP220
            SET &ERRCMD = DELETE
            SET &ERRMSG1= &STR(&SYSOUTLINE1)
          END
      END
/* KEEP.
    IF &M = K THEN +
      DO
        SET &Q1 = &LENGTH(&JOB)-10
        SET &QUAL1 = &SUBSTR(1:&Q1,&JOB)
        SET &Q1 = &LENGTH(&JOB)-7
        SET &Q2 = &LENGTH(&JOB)
        SET &Q3 = &STR(&Q1:&Q2,)
        SET &QUAL2 = &SUBSTR(&Q3,&JOB)
      /*WRITE QUAL1= &QUAL1, QUAL2= &QUAL2..
        IF (&STR(&SYSUID) = &STR(&SYSPREF) OR +
          &LENGTH(&SYSPREF) = 0) THEN +
          SET &OUTDSN = &SYSUID..&QUAL1..&QUAL2..OUTLIST
        ELSE +
          SET &OUTDSN = &SYSPREF..&SYSUID..&QUAL1..&QUAL2..OUTLIST
        SET &SYSOUTTRAP = 2
        ALLOC DA('&OUTDSN') TRACKS RELEASE SPACE(200 100) CATALOG  +
          BLKSIZE(3059) LRECL(133) DSORG(PS) RECFM(F B &ZR)
        SET &RC = &LASTCC
        SET &SYSOUTTRAP = 0
        IF &RC = 0 THEN +
          DO
            SET &SYSOUTTRAP = 2
            CONTROL NOLIST NOCONLIST
            OUTPUT &JOB &CLASS PRINT('&OUTDSN') BEGIN
            SET &SYSOUTTRAP = 0
            IF &SYSOUTLINE = 0 THEN +
              DO
                ISPEXEC LMINIT DATAID(PRCDATA) DATASET('&OUTDSN')
                ISPEXEC LMOPEN DATAID(&PRCDATA)
                ISPEXEC LMGET  DATAID(&PRCDATA) MODE(LOCATE) +
                      DATALOC(PRINTBUF) DATALEN(PRINTLEN) MAXLEN(32768)
                SET &RC = &LASTCC
                IF  &RC = 0 THEN +
                  DO
                    SET &LAST = KEPT
                  END
                ELSE +
                  DO
                    SET &MSGID = ISRU222
                    SET &SYSOUTTRAP = 2
                    DELETE '&OUTDSN'
                    SET &SYSOUTTRAP = 0
                  END
                ISPEXEC LMFREE DATAID(&PRCDATA)
                SET &SYSOUTTRAP = 2
                FREE DA('&OUTDSN')
                SET &SYSOUTTRAP = 0
              END
            ELSE +
              DO
                SET &MSGID = ISRP220
                SET &ERRCMD = KEEP
                SET &ERRMSG1 = &STR(&SYSOUTLINE1)
                SET &SYSOUTTRAP = 2
                DELETE '&OUTDSN'
                SET &SYSOUTTRAP = 0
              END
          END
        ELSE +
          DO
            SET &MSGID = ISRU220
            SET &ERRCMD = KEEP
            SET &ERRMSG1 = &STR(&SYSOUTLINE1)
            SET &SYSOUTTRAP = 2
            DELETE '&OUTDSN'
            SET &SYSOUTTRAP = 0
          END
      END
/* PRINT.
    IF &M = P THEN +
      DO
        SET &SYSOUTTRAP = 2
        FREE  F(&FILE)
        ALLOC F(&FILE) TRACKS RELEASE SPACE(20 50) UNIT(VIO)NEW +
          BLKSIZE(3059) LRECL(133) DSORG(PS) RECFM(F B &ZR)
        SET &RC = &LASTCC
        SET &SYSOUTTRAP = 0
        IF &RC = 0 THEN +
          DO
            ISPEXEC SELECT PGM(PRCGTDSN) PARM(&FILE)
            ISPEXEC VGET (OUTDSN OUTVOL)
            SET &SYSOUTTRAP = 2
            CONTROL NOLIST NOCONLIST
            OUTPUT &JOB &CLASS PRINT('&OUTDSN') BEGIN KEEP HOLD
            SET &SYSOUTTRAP = 0
            IF &SYSOUTLINE = 0 THEN +
              DO
                ISPEXEC LMINIT DATAID(PRCDATA) DDNAME(&FILE)
                ISPEXEC LMOPEN DATAID(&PRCDATA)
                ISPEXEC LMGET  DATAID(&PRCDATA) MODE(LOCATE) +
                      DATALOC(PRINTBUF) DATALEN(PRINTLEN) MAXLEN(32768)
                SET &RC = &LASTCC
                IF  &RC = 0 THEN +
                  DO
                    SET &SYSOUTTRAP = 15
                    CONTROL NOLIST NOCONLIST
                    PRINTO '&OUTDSN' NOHEADING
                    SET &SYSOUTTRAP = 0
                    SET &SYSDVAL = &STR(&SYSOUTLINE1)
                    READDVAL &A &B
                    IF &A = &STR(PROCESSING) THEN +
                      DO
                        SET &LAST = PRINTED
                        SET &LASTJOB = &JOB
                      END
                    ELSE +
                      DO
                        SET &MSGID  = ISRP220
                        SET &ERRCMD = PRINT
                        SET &ERRMSG1= &STR(&SYSOUTLINE1)
                      END
                  END
                ELSE +
                  DO
                    SET &MSGID = ISRU222
                  END
                ISPEXEC LMFREE DATAID(&PRCDATA)
                SET &SYSOUTTRAP = 2
                FREE F(&FILE)
                SET &SYSOUTTRAP = 0
              END
            ELSE +
              DO
                SET &MSGID = ISRP220
                SET &ERRCMD = PRINT
                SET &ERRMSG1 = &STR(&SYSOUTLINE1)
                SET &SYSOUTTRAP = 2
                FREE F(&FILE)
                SET &SYSOUTTRAP = 0
              END
          END
        ELSE +
          DO
            SET &MSGID = ISRU220
            SET &SYSOUTTRAP = 2
            FREE F(&FILE)
            SET &SYSOUTTRAP = 0
          END
      END
/* REQUE.
    IF &M = R THEN +
      DO
        SET &SYSOUTTRAP = 2
        OUTPUT &JOB &CLASS NEWCLASS(&ZN)
        SET &RC = &LASTCC
        SET &SYSOUTTRAP = 0
        IF &SYSOUTLINE = 0 THEN +
          DO
            SET &LAST = REQUED
            SET &LASTJOB = &JOB
          END
        ELSE +
          DO
            SET &MSGID = ISRP220
            SET &ERRCMD = REQUE
            SET &ERRMSG1= &STR(&SYSOUTLINE1)
          END
      END
/* UPDATE ENTRY FOR LAST ACTION.
    ISPEXEC TBPUT MEMTABLE
/* HANDLE MULTIPLE LINE SELECTIONS.
    IF &DOZCMD ¬= YES THEN +
      DO
        ISPEXEC CONTROL DISPLAY RESTORE
        IF &MSGID ¬= THEN SET &ZTDSELS = 0
        IF &ZTDSELS > 1 THEN ISPEXEC TBDISPL MEMTABLE POSITION(ROW)
        ELSE SET &ZTDSELS = 0
      END
    ELSE +
      DO
        SET &ZCMD =
        SET &ZTDSELS = 0
      END
  END
  /*WRITE ZCMD= &ZCMD, ROW= &ROW, LASTJOB = &LASTJOB..
/* REFRESH ON COMMAND LINE.
  IF &ZCMD = REF OR &ZCMD = REFRESH THEN +
    DO
      GOTO PRCUOL
    END
/* HANDLE LINE COMMANDS ON COMMAND LINE.
  IF &ZCMD ¬= THEN +
    IF &LASTJOB ¬=  THEN +
      DO
        SET &ZTDSELS = 1
        SET &M = &ZCMD
        SET &DOZCMD = YES
        SET &JOB = &LASTJOB
        ISPEXEC TBGET MEMTABLE
        GOTO MAINLOOP
      END
    ELSE +
      DO
        SET &MSGID = ISRP226
        SET &LASTJOB =
        SET &ZTDSELS = 0
      END
/* DO A SCREEN REDISPLAY.
  IF &MSGID = THEN +
    DO
      IF &DOZCMD ¬= YES THEN +
        DO
          IF &M = B OR &M = P THEN +
            DO
/* KEEP CURSOR ON SAME LINE FOR BROWSE OR PRINT.
              SET &M =
              ISPEXEC TBDISPL MEMTABLE CSRROW(&ROW)AUTOSEL(NO) +
                POSITION(ROW)
              SET &TBDRC = &LASTCC
            END
          ELSE +
            DO
/* PUT CURSOR ON COMMAND LINE FOR ANYTHING ELSE.
              SET &M =
              ISPEXEC TBDISPL MEMTABLE POSITION(ROW)
              SET &TBDRC = &LASTCC
            END
        END
      IF &DOZCMD = YES THEN +
        DO
          SET &M =
          SET &DOZCMD =
          DO
/* MUST REDISPLAY PANEL IF A COMMAND LINE COMMAND PROCESSED.
            ISPEXEC TBDISPL MEMTABLE PANEL(ISRUOLP)
             SET &TBDRC = &LASTCC
          END
        END
    END
  ELSE +
    DO
      ISPEXEC TBDISPL MEMTABLE +
        MSG(&MSGID) CSRROW(&ROW) AUTOSEL(YES) POSITION(ROW)
      SET &TBDRC = &LASTCC
    END
  SET &MSGID =
  SET &ERRCMD =
  SET &ERRMSG1 =
END
ISPEXEC TBEND MEMTABLE
ENDPRC: +
IF &SETPROF = &STR(YES) THEN PROFILE NOMSGID
GOTO LOOP
