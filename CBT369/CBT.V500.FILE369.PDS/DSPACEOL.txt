PRC      TITLE 'DSPACE - DISPLAY DASD SPACE INFORMATION.'
********************************************************************
*                                                                  *
* NAME - DSPACE.                                                   *
*                                                                  *
* DESCRIPTION - TSO COMMAND TO DISPLAY AVAILABLE SPACE ON DIRECT   *
*               ACCESS VOLUME(S).                                  *
*                                                                  *
* TO USE - ENTER: DSPACE VVVVVV   ,VVVVVV = A VOLSER (1-6 CHARS)   *
*                 DSPACE UUU      ,UUU = A UCB ADDRESS (3 CHARS)   *
*                 DSPACE NNNNNNNN ,NNNNNNNN = A UNITNAME (1-8 CHARS*
*                 DSPACE STORAGE  ,FOR ALL STORAGE VOLUMES         *
*                 DSPACE PUBLIC   ,FOR ALL PUBLIC VOLUMES          *
*                 DSPACE PRIVATE  ,FOR ALL PRIVATE VOLUMES         *
*                 DSPACE ONLINE   ,FOR ALL ONLINE VOLUMES (DEFAULT)*
*                                                                  *
*   NOTE: DSPACE ACCEPTS A MASK: DSPACE RES* OR DSPACE 3C*         *
*         DSPACE ALSO ACCEPTS A LIST: DSPACE (VVVVVV,UUU,...)      *
*                                                                  *
* LOG -                                                            *
*   12/17/86 L01 PTW IMPLEMENTED                                   *L01
*                                                                  *
********************************************************************
         LCLC  &CSECT
&CSECT   SETC  'DSPACE'
&CSECT   CSECT
********************************************************************
*                                                                  *
*        I N I T I A L I Z E                                       *
*                                                                  *
********************************************************************
         PRINT NOGEN
         SAVE  (14,12),,&CSECT-&SYSDATE-&SYSTIME
         LR    R12,R15            ESTABLISH BASE.
         USING &CSECT,R12         TELL ASSEMBLER.
         GETMAIN RU,LV=DYNLEN     DYNAMIC AREA.
         LR    R2,R13             SAVE AREA
         LR    R13,R1               SWITCH.
         USING DYNAMIC,R13        TELL ASSEMBLER.
         LR    R0,R1
         LA    R1,DYNLEN
         LA    R14,R0
         SLR   R15,R15
         MVCL  R0,R14             ZERO STORAGE.
         ST    R2,4(,R13)         BACK CHAIN.
         ST    R13,8(,R2)         FORWARD CHAIN.
         XC    RC,RC
         L     R2,4(,R13)
         L     R2,24(,R2)         R2 = A(PARMLIST).
         USING CPPL,R2            CONSTRUCT TSO CONTROL BLOCKS.
         MVC   MYIOPB(IOPBLEN),XIOPB INITIALIZE DYNAMIC IOPB.
         LA    R3,MYPPL           BUILD PPL.
         USING PPL,R3
         MVC   PPLUPT,CPPLUPT
         MVC   PPLECT,CPPLECT
         LA    R4,ECB
         ST    R4,PPLECB
         MVC   PPLPCL,=A(MYPCL)
         LA    R4,MYANS
         ST    R4,PPLANS
         MVC   PPLCBUF,CPPLCBUF
         XC    PPLUWA,PPLUWA
         DROP  R3
         LA    R3,MYIOPL          BUILD IOPL.
         USING IOPL,R3
         MVC   IOPLUPT,CPPLUPT
         MVC   IOPLECT,CPPLECT
         LA    R4,ECB
         ST    R4,IOPLECB
         LA    R4,MYIOPB
         ST    R4,IOPLIOPB
         DROP  R2,R3
         LA    R2,UCBAREA         INITIALIZE UCB SCAN LIST.
         ST    R2,UCBAAREA
         MVI   UCBDEVC,UCB3DACC
         LA    R2,UCBDEVC
         ST    R2,UCBADEVC
         LA    R2,UCBUCB
         ST    R2,UCBAUCB
         MVI   UCBAUCB,X'80'
         MVC   OUTAREA(4),=AL2(OUTAREAL,0) INITIALIZE PUTLINE MSG.
         CALLTSSR EP=IKJPARS,MF=(E,MYPPL) CALL PARSE.
         LTR   R15,R15
         BNZ   BADPARS
         L     R2,MYANS           R2 = A(PDL FROM PARSE)
         USING MYPDL,R2
         LA    R2,VOLUMES         R2 = A(VOLUME POSITIONAL PDE).
         DROP  R2
         XC    COUNT,COUNT
********************************************************************
*                                                                  *
*        G E T   1 S T   T H I N G   I N   L I S T                 *
*                                                                  *
********************************************************************
LOOPPDE  DS    0H
         MVI   TYPE,0
         MVI   TRY,0
         MVI   ATTR,0
         ST    R2,PDEADDR         SAVE A(PDE).
         XC    UCBAREA,UCBAREA
         MVI   VALUE,C' '
         MVC   VALUE+1(L'VALUE-1),VALUE
         L     R3,0(,R2)          R3 = A(PDE).
         LH    R4,4(,R2)          R5 = L'VALUE.
         STH   R4,VALUELEN
         BCTR  R4,R0
         EX    R4,MVCVALUE
         CLC   =C'STORAGE ',VALUE CHECK FOR STORAGE.
         BNE   NOTSTOR
         OI    TYPE,STORAGE
         MVI   ATTR,UCBBSTR
         B     DOSCAN
NOTSTOR  DS    0H
         CLC   =C'PUBLIC ',VALUE CHECK FOR PUBLIC.
         BNE   NOTPUB
         OI    TYPE,PUBLIC
         MVI   ATTR,UCBBPUB
         B     DOSCAN
NOTPUB   DS    0H
         CLC   =C'PRIVATE ',VALUE CHECK FOR PRIVATE.
         BNE   NOTPRIV
         OI    TYPE,PRIVATE
         MVI   ATTR,UCBBPRV
         B     DOSCAN
NOTPRIV  DS    0H
         CLC   =C'ONLINE ',VALUE CHECK FOR ONLINE.
         BNE   ENDONL
         OI    TYPE,ONLINE
         B     DOSCAN
ENDONL   DS    0H
         CLC   VALUELEN,=H'6'     IF L'VALUE > 6, CAN ONLY BE UNITNAME.
         BH    TRYUNIT
         CLC   VALUELEN,=H'2'     IF L'VALUE < 2, TRY UNITNAME.
         BL    TRYUNIT
         OI    TRY,VOLUME         SPECIFY TRY FOR VOLUME.
         LH    R4,VALUELEN
         BCTR  R4,0
         LA    R5,VALUE(R4)
         CLI   0(R5),C'*'
         BNE   NOTMASK
         OI    TRY,MASK
         STH   R4,VALUELEN
NOTMASK  DS    0H
         CLC   VALUELEN,=H'3'     IF L'VALUE > 3, CAN'T BE UCB.
         BH    DOSCAN
         OI    TRY,UCB
         B     DOSCAN
********************************************************************
*                                                                  *
*        G E T   N E X T   T H I N G   I N   L I S T               *
*                                                                  *
********************************************************************
NEXTPDE  DS    0H
         L     R2,PDEADDR         GET NEXT ENTRY IS PARSE LIST.
         L     R2,8(,R2)
         CL    R2,=X'FF000000'    IF NO MORE, RETURN.
         BNE   LOOPPDE
********************************************************************
*                                                                  *
*        R E T U R N                                               *
*                                                                  *
********************************************************************
RETURN   DS    0H
         IKJRLSA MYANS
         L     R2,RC                  R2 = RC.
         LR    R1,R13              PREPARE
         L     R13,4(,R13)           FOR FREEMAIN.
         FREEMAIN RU,LV=DYNLEN,A=(1)
         LR    R15,R2              AND RETURN
         RETURN (14,12),RC=(15)      WITH R15 = RC.
********************************************************************
*                                                                  *
*        U C B   S C A N                                           *
*                                                                  *
********************************************************************
DOSCAN   DS    0H
         LA    R1,UCBPARML
         L     R15,FLCCVT-PSA
         L     R15,CVTUCBSC-CVT(,R15) R15 = A(UCB SCAN ROUTINE).
         BALR  R14,R15
         LTR   R15,R15            IF BAD RC, ASSUME NO MORE TO SCAN.
         BNZ   ENDSCAN
         L     R3,UCBUCB          R3 = A(UCB).
         USING UCBCMSEG,R3
         TM    TRY,VOLUME         IF WE ARE TO TRY FOR VOLUME,
         BZ    NOTVOL
         LA    R4,L'UCBVOLI-1     ASSUME FULL LENGTH COMPARE...
         TM    TRY,MASK             UNLESS A MASK WAS SPECIFIED.
         BZ    *+10
         LH    R4,VALUELEN
         BCTR  R4,0
         EX    R4,CLCVOLI
         BNE   NOTVOL
         OI    TYPE,VOLUME
         B     SCANSPAC
NOTVOL   DS    0H
         TM    TRY,UCB            IF WE ARE TO TRY FOR UCB,
         BZ    NOTUCB
         LA    R4,L'UCBNAME-1     ASSUME FULL LENGTH COMPARE...
         TM    TRY,MASK             UNLESS A MASK WAS SPECIFIED.
         BZ    *+10
         LH    R4,VALUELEN
         BCTR  R4,0
         EX    R4,CLCNAME
         BNE   NOTUCB
         OI    TYPE,UCB
         B     SCANSPAC
NOTUCB   DS    0H
         TM    TYPE,STORAGE+PUBLIC+PRIVATE
         BZ    NOTATTR            IF ATTRIBUTE COMPARE, DO IT.
         SLR   R4,R4
         IC    R4,ATTR
         EX    R4,TMATTR
         BO    SCANSPAC
         B     DOSCAN
NOTATTR  DS    0H
         TM    TYPE,ONLINE        IF NO ONLINE REQUEST,
         BZ    DOSCAN               GET NEXT UCB.
SCANSPAC DS    0H
         BAL   R10,DOLSPACE       GO DO LSPACE.
         TM    TYPE,VOLUME+UCB    IF WE HIT ON SINGLE REQUEST,
         BZ    DOSCAN               ALL DONE.
         TM    TRY,MASK           UNLESS IT WAS A MASK REQUEST.
         BO    DOSCAN
ENDSCAN  DS    0H
         CLI   TYPE,0             IF WE HAVE A HIT,
         BNE   NEXTPDE              GO DO NEXT.
********************************************************************
*                                                                  *
*        U N I T   R E Q U E S T                                   *
*                                                                  *
********************************************************************
TRYUNIT  DS    0H
         MVC   UNITNAME,VALUE     INIT LISTS FO IEFEB4UV.
         XC    UNITLIST,UNITLIST
         XC    UNITATTR,UNITATTR
         MVI   UNITFLG,X'10'
         LINK  EP=IEFEB4UV,PARAM=(UNITTAB,UNITFLG),MF=(E,UNITPARM)
         LTR   R15,R15            IF RC Â¬= 0,
         BNZ   ENDUNIT              ASSUME NOT A UNITNAME.
         OI    TYPE,UNIT
         L     R2,UNITLIST
         L     R4,4(,R2)          R4 = # UCBS IN RETURNED LIST.
         L     R3,8(,R2)          R3 = A(UCB).
         LA    R2,8(,R2)          R2 = A(THIS UCB ENTRY IN TABLE).
UNITLOOP DS    0H
         BAL   R10,DOLSPACE
         LA    R2,4(,R2)          R2 = A(NEXT UCB ENTRY IN TABLE).
         L     R3,0(,R2)          R3 = A(NEXT UCB).
         BCT   R4,UNITLOOP        LOOP.
         L     R2,UNITLIST
         L     R0,0(,R2)
         FREEMAIN R,LV=(0),A=(R2) FREEMAIN IEFEB4UV STORAGE.
         B     NEXTPDE
ENDUNIT  DS    0H
         B     BADREQ             CAN'T IDENTIFY REQUEST.
********************************************************************
*                                                                  *
*        D O   L S P A C E   S V C ( 7 8 )                         *
*                                                                  *
********************************************************************
DOLSPACE DS    0H
         LH    R0,COUNT           PUT OUT HEADER FOR 1ST REQ ONLY.
         AH    R0,=H'1'
         STH   R0,COUNT
         CH    R0,=H'1'
         BNE   NOHDR
         PUTLINE OUTPUT=(OUTHDR,SINGLE,DATA),MF=(E,MYIOPL)
NOHDR    DS    0H
         MVI   OUTAREA+4,C' '     FORMAT OUTPUT LINE.
         MVC   OUTAREA+5(OUTAREAL-5),OUTAREA+4
         MVC   OUTUCB,UCBNAME
         TM    UCBSTAT,UCBONLI    IF UCB NOT ONLINE,
         BZ    BADONL               GO TO ERROR ROUTINE.
         TM    UCBSTAT,UCBCHGS+UCBUNLD NO FUNNY STATUS.
         BNZ   BADUCB
         MVC   OUTVOL,UCBVOLI
         LR    R0,R3              R0 = A(UCB).
         LA    R1,SVCAREA         R1 = A(MSG AREA).
         SVC   78                 DO LSPACE.
         LTR   R15,R15            IF BAD RC,
         BNZ   BADSVC78             GO TO ERROR ROUTINE.
         MVC   OUTSPACE(L'SVCAREA),SVCAREA
         CVAFTST UCB=(R3)         CHECK FOR INDEXED VTOC.
         CH    R15,=H'8'          IF RC = 8, WE'RE INDEXED.
         BNE   *+8
         MVI   OUTINDEX,C'I'
         PUTLINE OUTPUT=(OUTAREA,SINGLE,DATA),MF=(E,MYIOPL)
         BR    R10
********************************************************************
*                                                                  *
*        E R R O R S                                               *
*                                                                  *
********************************************************************
BADPARS  DS    0H                 BAD PARSE.
         MVC   RC,=F'12'
         LA    R15,MSGPARS
         LA    R10,RETURN
         B     DOMSG
BADREQ   DS    0H                 CAN'T IDENTIFY REQUEST.
         MVC   RC,=F'8'
         LA    R15,MSGREQ
         LA    R10,NEXTPDE
         B     DOMSG
BADONL   DS    0H                 UCB NOT ONLINE.
         TM    TYPE,STORAGE+PUBLIC+PRIVATE+ONLINE
         BNZ   RETMSG             FOR ABOVE REQUESTS, IGNORE ERROR.
         MVC   OUTSPACE(L'MSGONL),MSGONL
         LA    R15,OUTAREA
         B     DOMSG
BADUCB   DS    0H                 FUNNY STATUS.
         TM    TYPE,STORAGE+PUBLIC+PRIVATE+ONLINE
         BNZ   RETMSG             FOR ABOVE REQUESTS, IGNORE ERROR.
         MVC   OUTSPACE(L'MSGUCB),MSGUCB
         LA    R15,OUTAREA
         B     DOMSG
BADSVC78 DS    0H                 BAD LSAPCE RC.
         TM    TYPE,STORAGE+PUBLIC+PRIVATE+ONLINE
         BNZ   RETMSG             FOR ABOVE REQUESTS, IGNORE ERROR.
         MVC   OUTSPACE(L'MSGSVC78),MSGSVC78
         LA    R10,NEXTPDE
         B     DOMSG
DOMSG    DS    0H
         PUTLINE OUTPUT=((R15),SINGLE,DATA),MF=(E,MYIOPL)
RETMSG   DS    0H
         BR    R10
********************************************************************
*                                                                  *
*        S T A T I C   A R E A                                     *
*                                                                  *
********************************************************************
MVCVALUE MVC   VALUE(*-*),0(R3)
CLCVOLI  CLC   UCBVOLI(*-*),VALUE        COMPARE VOLSER.
CLCNAME  CLC   UCBNAME(*-*),VALUE        COMPARE EBCDIC UCB NAME.
TMATTR   TM    UCBSTAB,*-*
MSGONL   DC    C'OFFLINE'
MSGUCB   DC    C'SPACE COULD NOT BE DETERMINED.'
MSGSVC78 DC    C'LSPACE SVC ERROR, IGNORED.'
MSGPARS  DC    AL2(MSGPARSL,0)
         DC    C'TSO PARSE ERROR, PROGRAM TERMINATED.'
MSGPARSL EQU   *-MSGPARS
MSGREQ   DC    AL2(MSGREQL,0)
         DC    C'VOLSER, UCB, OR UNIT NOT FOUND, IGNORED.'
MSGREQL  EQU   *-MSGREQ
XIOPB    PUTLINE MF=L
IOPBLEN  EQU   *-XIOPB
MYPCL    IKJPARM DSECT=MYPDL
VOLUMES  IKJIDENT  VOLUME,LIST,FIRST=ALPHANUM,OTHER=ALPHANUM,CHAR,     X
               MAXLNTH=8,DEFAULT='ONLINE'
         IKJENDP
OUTHDR   DC    AL2(OUTHDRL,0)
         DC    C'VOL    '
         DC    C'  '
         DC    C'UNIT'
         DC    C'     '
         DC    C'#CYL '
         DC    C'#TRK '
         DC    C'#EXT '
         DC    C'CYL-C '
         DC    C'TRK-C '
OUTHDRL  EQU   *-OUTHDR
         DROP  R3
         LTORG *
SRCLEVEL DC    C'                                        '
********************************************************************
*                                                                  *
*        D Y N A M I C   A R E A                                   *
*                                                                  *
********************************************************************
         IKJCPPL
CPPLLEN  EQU   *-CPPL
         IKJPPL
PPLLEN   EQU   *-PPL
         IKJIOPL
IOPLLEN  EQU   *-IOPL
         IHAPSA
         CVT   DSECT=YES
         IEFUCBOB
DYNAMIC  DSECT
SAVE     DS    18F .
RC       DS    F
ECB      DS    F
MYANS    DS    A
PDEADDR  DS    A
MYCPPL   DS    0F,CL(CPPLLEN)
MYPPL    DS    0F,CL(PPLLEN)
MYIOPL   DS    0F,CL(IOPLLEN)
MYIOPB   DS    0F,CL(IOPBLEN)
UCBPARML DS    0F
UCBAAREA DS    F
UCBADEVC DS    F
UCBAUCB  DS    F
UCBUCB   DS    F
UCBAREA  DS    XL100
UCBDEVC  DS    CL1
COUNT    DS    H
VALUELEN DS    H
VALUE    DS    CL8
SVCAREA  DS    CL50
OUTAREA  DS    0F,AL2,AL2
OUTVOL   DS    CL6,CL1
OUTINDEX DS    CL1,CL1
OUTUCB   DS    CL3,CL1
OUTSPACE DS    CL50
OUTAREAL EQU   *-OUTAREA
* IEFEB4UV PARM LIST.
UNITPARM DS    2A                     IEFEB4UV PARM LIST
UNITTAB  DS    0F                     UNIT TABLE
UNITNAME DS    CL8                    UNITNAME (INPUT TO IEFEB4UV)
UNITLIST DS    F                      UNITNAME LIST ADDR (OUTPUT)
UNITATTR DS    F                      DON'T PROVIDE ATTRIBUTE LIST
UNITFLG  DS    X                      IEFEB4UV PARM LIST
* END IEFEB4UV PARM LIST.
TYPE     DS    X
TRY      DS    X
PRIVATE  EQU   X'01'
STORAGE  EQU   X'02'
PUBLIC   EQU   X'04'
VOLUME   EQU   X'08'
UCB      EQU   X'10'
UNIT     EQU   X'20'
ONLINE   EQU   X'40'
MASK     EQU   X'80'
ATTR     DS    X
DYNLEN   EQU   *-DYNAMIC
         PRINT OFF
R0       EQU   00
R1       EQU   01
R2       EQU   02
R3       EQU   03
R4       EQU   04
R5       EQU   05
R6       EQU   06
R7       EQU   07
R8       EQU   08
R9       EQU   09
R10      EQU   10
R11      EQU   11
R12      EQU   12                 PROGRAM BASE.
R13      EQU   13                 A(SAVE AREA).
R14      EQU   14
R15      EQU   15
         PRINT ON
         END   &CSECT
