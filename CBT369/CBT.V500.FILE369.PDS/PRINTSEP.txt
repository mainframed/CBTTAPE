         TITLE 'APS JES2 EXITS: CUSTOM PRINTER BANNER SEPARATOR PAGE'
***********************************************************************
*  NAME                                                               *
*        PRINTSEP                                                     *
*                                                                     *
*  FUNCTION                                                           *
*        CUSTOM PRINT SEPARATOR EXIT ROUTINES                         *
*                                                                     *
*  ENTRY POINTS                                                       *
*        SEPEX1                                                       *
*                                                                     *
*  MVS MACROS                                                         *
*        FREEMAIN                                                     *
*        GETMAIN                                                      *
*        TIME                                                         *
*                                                                     *
*  JES2 MACROS                                                        *
*        $ENTRY                                                       *
*        $FREEBUF                                                     *
*        $GETBUF                                                      *
*        $HASPGBL                                                     *
*        $MODEND                                                      *
*        $MODULE                                                      *
*        $PBLOCK                                                      *
*        $PRPUT                                                       *
*        $SEPPDIR                                                     *
*        $RETURN                                                      *
*        $SAVE                                                        *
*        $WTO                                                         *
*                                                                     *
*  JES2 CONTROL BLOCKS                                                *
*        BUFFER                                                       *
*        DCT                                                          *
*        JCT                                                          *
*        JOE                                                          *
*        PCE                                                          *
*        RDT                                                          *
*        SVT                                                          *
*                                                                     *
*  NOTES                                                              *
*        1.  ENTRY SEPEX1 IS TO BE INVOKED AT STANDARD JES2           *
*            EXIT 1.                                                  *
*        2.  THE ACF2 LOGON ID IS PASSED IN JCTUSEID AS LONG AS ACF2  *
*            OPTION STAMPSMF IS IN EFFECT. WATCH OUT -- SOME OTHER    *
*            PRODUCT MAY STOMP ON THIS.                               *
*        3.  THE LOGOS ARE IN LOWER CASE. TAKE CARE WITH THE          *
*            EDITOR IF YOU MAKE CHANGES.                              *
*                                                                     *
*  AUTHOR                                                             *
*        CHARLIE PITTS                                                *
*                                                                     *
*  MAINTENANCE                                                        *
*    V1.0  5/23/86  CJP  ADDED PROVISIONS FOR TWO DIFFERENT LOGOS     *
*                        DEPENDING ON PRINTER NODE.                   *
*    V2.0  6/ 5/86  CJP  CORRECT BUG WITH LENGTH OF SITE NAME         *
*                        TO TRIGGER ALTERNATE LOGO.                   *
*    V3.0  4/27/87  CJP  ADDED SUPPORT FOR 'BLDG' INSTEAD OF 'ROOM'   *
*                        BASED ON PRINTER NODE. ALSO ADDED 'DELIVER   *
*                        TO REMOTE BLDG' BLOCK IN HEADER, PER LEN     *
*                        BEECH.                                       *
*    V3.1  5/20/87  CJP  CHANGED FORMAT OF 'DELIVER TO...' BLOCK,     *
*                        PER LEN BEECH.                               *
*    V3.2 12/22/87  CJP  ADDED DAY-OF-THE-WEEK TO HEADER.             *
*    V4.0 12/23/87  CJP  UPDATED FOR JES2 R2.1.5. STILL AT ACF2 R4.0. *
*                        NOTE ACF2 R4.0 CIRCUMVENTION JUST BEFORE     *
*                        #ACFJCB INVOCATION. REMOVE FOR ACF2 R5.0.    *
*    V4.1  1/19/88  CJP  REMOVED BUFDSECT ACF R4 CIRCUMVENTION.       *
*                        REMOVED SEPEX222 ENTRY POINT AND LOGIC       *
*                        BECAUSE ACF2 R5 ELIMINATED THE EXIT. WE'RE   *
*                        GETTING THE USER'S LID FROM JCTUSEID AS      *
*                        A RESULT OF ENABLING THE STAMPSMF GLOBAL     *
*                        ACF2 OPTION. THERE IS NOW NO ACF2 CODE IN    *
*                        THIS MODULE.                                 *
*    V4.2  6/ 9/88  CJP  CORRECTED LOGIC FOR PRC LOGO FOR CPK2. PRC   *
*                        LOGO IS USED ONLY AT APS1.                   *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
*        COPY  $HASPGBL
         PRINT OFF
         COPY  $HASPGBL
         PRINT ON,NOGEN
PRINTSEP $MODULE SYSP=(NOGEN,GEN,NODATA,NOGEN,NOGEN),                  X
               $BUFFER,            I/O BUFFER DSECT                    X
               $CAT,               CLASS ATTRIBUTE TABLE               X
               $DCT,               DEVICE CONTROL TABLE                X
               $HASPEQU,           REGISTER EQUATES, ETC               X
               $HCT,               HASP COMMUNICATIONS TABLE           X
               $JCT,               JOB CONTROL TABLE                   X
               $JOE,               JOB OUTPUT ELEMENT                  X
               $JQE,               JOB QUEUE ELEMENT                   X
               $MIT,               MODULE INFORMATION TABLE            X
               $PCE,               PROCESSOR CONTROL ELEMENT           X
               $PDDB,              PERIPHERAL DATA DEFINITION BLOCK    X
               $PPPWORK,           PRT/PUN PROCESSOR PCE WORKAREA      X
               $RDT,               REMOTE DESTINATION TABLE            X
               $SCAT,              SYSOUT CLASS ATTRIBUTE TABLE        X
               $SVT,               JES2 SUBSYSTEM VECTOR TABLE         X
               $XECB,              EXTENDED ECB                        X
               RPL                 REQUEST PARAMETER LIST
         SPACE 2
         USING JCT,R10
         SPACE
         PRINT GEN
         TITLE 'APS JES2 EXIT 1: CUSTOM PRINTER BANNER SEPARATOR PAGE'
***********************************************************************
*  FUNCTION                                                           *
*        CUSTOM PRINT SEPARATOR EXIT ROUTINE                          *
*                                                                     *
*  ENTRY POINT                                                        *
*        SEPEX1                                                       *
*                                                                     *
*  CALLED FROM                                                        *
*        HASPPRPU VIA $EXIT                                           *
*                                                                     *
*  NOTES                                                              *
*        1.  THERE IS SOME UNUSUAL CODE IN THE DETAIL                 *
*            SUBROUTINE TO GET AROUND THE RESTRICTIVE EXPANSION OF    *
*            THE $PRPUT MACRO. IF THAT MACRO CHANGES, SOMETHING       *
*            MAY HAVE TO BE ADJUSTED.                                 *
***********************************************************************
*  REGISTERS AT ENTRY                                                 *
*        R0    ENTRY CODE                                             *
*                0  START OF JOB                                      *
*                4  CONTINUATION OF JOB                               *
*                8  END OF JOB                                        *
*        R1    PRINTER/PUNCH DCT                                      *
*        R10   JCT                                                    *
*        R11   HCT                                                    *
*        R13   PCE                                                    *
*        R14   RETURN ADDRESS                                         *
*        R15   ENTRY ADDRESS                                          *
*                                                                     *
*  REGISTERS USE DURING PROCESSING                                    *
*        R4    WORKAREA                                               *
*        R5    SUBROUTINE LINKAGE                                     *
*        R7    BUFFER POINTER                                         *
*        R8    SUBROUTINE LINKAGE                                     *
*        R9    DCT                                                    *
*        R10   JCT                                                    *
*        R11   HCT                                                    *
*        R12   PROGRAM BASE                                           *
*        R13   PCE                                                    *
*                                                                     *
*  REGISTERS AT EXIT                                                  *
*        R15   RETURN CODE                                            *
*                0  CALL NEXT EXIT; PRODUCE STANDARD SEPARATOR        *
*                     IF 'S=Y' IS IN EFFECT.                          *
*                4  NO MORE EXITS; PRODUCE STANDARD SEPARATOR         *
*                     IF 'S=Y' IS IN EFFECT.                          *
*                8  CALL NEXT EXIT; SUPPRESS STANDARD SEPARATOR.      *
*                C  CALL NEXT EXIT; PRODUCE STANDARD SEPARATOR.       *
***********************************************************************
         EJECT
***********************************************************************
*        JES ENTRY POINT                                              *
***********************************************************************
         SPACE 2
SEPEX1   $ENTRY BASE=R12,CSECT=YES
         SPACE
         $SAVE
         EJECT
***********************************************************************
* SEP0000:                                                            *
*        SET BASE REGISTER;                                           *
*        SAVE SEPARATOR TYPE ENTRY PARAMETER;                         *
*        SET ADDRESSABILITY FOR DCT;                                  *
*        CLEAR BUFFER REGISTER POINTER;                               *
*        IF THIS IS NOT PRINT              - OR -                     *
*                SEPARATORS ARE SUPPRESSED                            *
*          THEN GO EXIT WITH RC=0;                                    *
*        CALL INITWORK SUBROUTINE;                                    *
*        CALL EXTRACT SUBROUTINE;                                     *
*        ISSUE $GETBUF TO ACQUIRE FIXED HASP BUFFER;                  *
*        ISSUE $SEPPDIR IN CASE DEVICE IS A REMOTE;                   *
***********************************************************************
         SPACE
SEP0000  DS    0H
         LR    R12,R15             SET BASE REGISTER
         LR    R2,R0               SAVE SEPARATOR TYPE
         LR    R9,R1               SAVE DCT ADDRESS
         USING DCT,R9
         SPACE
         XR    R4,R4               CLEAR WORKAREA POINTER REGISTER
         XR    R7,R7               CLEAR BUFFER POINTER REGISTER
         SPACE
         TM    PCEID,PCEPUSID      IS THIS A PUNCH?
         BO    SEP9000             YEP...GO EXIT
         TM    DCTPPSW,DCTPPSWS    SEPARATORS SUPRESSED?
         BO    SEP9000             YEP...EXIT
         SPACE
         BAL   R8,INITWORK         INITIALIZE WORKAREA
         USING WORKAREA,R4
         BAL   R8,EXTRACT          GET JOB DATA FROM JES CONTROL BLOCKS
         SPACE
         $GETBUF TYPE=HASP,FIX=YES,WAIT=YES
         LR    R7,R1               BUFFER ADDRESS
         USING BFPDSECT,R7
         SPACE
         $SEPPDIR (R7)
         EJECT
***********************************************************************
*                 /*  MAIN LINE LOGIC  */                             *
*        DO COUNT=PAGES BY -1 UNTIL 0;                                *
*          INITIALIZE LINE COUNTER;                                   *
*          CALL HEADER SUBROUTINE;                                    *
*          CALL BLOCK SUBROUTINE;                                     *
*          CALL DETAIL SUBROUTINE;                                    *
*          CALL ENDPAGE SUBROUTINE;                                   *
*        END;                                                         *
*        RETURN WITH RC=8;                                            *
***********************************************************************
         SPACE
         LH    R0,PAGECNT          FOR INITIAL PASS THRU LOOP
SEP4000  DS    0H
         STH   R0,PAGECNT          SAVE COUNT
         MVC   LINECNT,SEPLINES    INITIALIZE COUNTER
         SPACE
         BAL   R8,HEADER           FORMAT HEADER
         BAL   R8,BLOCK            FORMAT BLOCK LINES
         BAL   R8,DETAIL           FORMAT/PRINT DETAIL LINES
         BAL   R8,ENDPAGE          FORMAT/PRINT END-OF-PAGE LINE
         SPACE
         LH    R0,PAGECNT          PAGES REMAINING
         BCT   R0,SEP4000          LOOP IF MORE
         SPACE
         B     SEP9200             RETURN WITH RC=8
         EJECT
***********************************************************************
* SEP9000:                                  /*  RETURN WITH RC=0  */  *
*        SET RETURN CODE TO 0;                                        *
*        ONWARD AT SEP9900;                                           *
* SEP9200:                                  /*  RETURN WITH RC=8  */  *
*        SET RETURN CODE TO 8;                                        *
* SEP9900:                                                            *
*        IF A WORKAREA WAS OBTAINED                                   *
*          THEN ISSUE FREEMAIN;                                       *
*        IF A BUFFER WAS OBTAINED                                     *
*          THEN ISSUE $FREEBUF;                                       *
*        $RETURN TO JES;                                              *
***********************************************************************
         SPACE
SEP9000  DS    0H
         LA    R2,X'00'            SET RETURN CODE 00
         B     SEP9900             ONWARD
SEP9200  DS    0H
         LA    R2,X'08'            SET RETURN CODE 08
         SPACE
SEP9900  DS    0H
         LTR   R1,R4               WAS A WORKAREA OBTAINED?
         BZ    SEP9920             NOPE...ONWARD
         LA    R0,WORKLEN          SPECIFY WORKAREA LENGTH & SP0
         FREEMAIN R,LV=(0),A=(1)
SEP9920  DS    0H
         SPACE
         LTR   R1,R7               WAS A BUFFER OBTAINED?
         BZ    SEP9940             NOPE...ONWARD
         $FREEBUF (1)
SEP9940  DS    0H
         SPACE
         LR    R15,R2              RETURN CODE
         $RETURN RC=(R15)
         EJECT
***********************************************************************
* INITWORK:                              /*  INITIALIZE WORKAREA  */  *
*        IF THIS IS A LOCAL DEVICE                                    *
*          THEN USE $PRIDCT  FOR SEPARATOR LINE COUNT AND             *
*               USE LCLPAGES FOR SEPARATOR PAGE COUNT;                *
*          ELSE USE $TPIDCT  FOR SEPARATOR LINE COUNT AND             *
*               USE REMPAGES FOR SEPARATOR PAGE COUNT;                *
*        IF SEPARATOR LINE COUNT IS ZERO                              *
*          THEN GO EXIT WITH RC=0;                                    *
*        ISSUE GETMAIN TO ACQUIRE WORKAREA;                           *
***********************************************************************
         SPACE
INITWORK DS    0H
         SPACE
         XR    R5,R5               CLEAR WORK REGISTER
         IC    R5,$PRIDCT          LOCAL SEPARATOR LINE COUNT
         L     R3,LCLPAGES         LOCAL PAGE COUNT
         TM    DCTDEVID,DCTRMTID   IS IT A REMOTE?
         BNO   IWRK0200            NOPE...ONWARD
         L     R3,REMPAGES         REMOTE PAGE COUNT
         IC    R5,$TPIDCT          REMOTE SEPARATOR LINE COUNT
IWRK0200 DS    0H
         SPACE
         LTR   R5,R5               COUNT = 0?
         BZ    SEP9000             YEP...EXIT
         SPACE
         LA    R0,WORKLEN          SPECIFY WORKAREA LENGTH & SP0
         GETMAIN  R,LV=(0)
         LR    R4,R1               SAVE WORKAREA POINTER
         USING WORKAREA,R4
         EJECT
***********************************************************************
*        SAVE ENTRY PARM, LINE COUNT AND PAGE COUNT IN WORKAREA;      *
*        IF 'START' OR 'CONTINUE'                                     *
*          THEN PAGES = HDRPAGES;                                     *
*          ELSE PAGES = TLRPAGES;                                     *
***********************************************************************
         SPACE
         STC   R2,SEPTYPE          SAVE SEPARATOR TYPE
         ST    R3,SEPPAGES         SAVE HEADER/TRAILER PAGE COUNTS
         STH   R5,SEPLINES         SAVE LINE COUNT
         SPACE
         LH    R0,TPAGES           END JOB PAGE COUNT
         CLI   SEPTYPE,ENDJOB      TRAILER?
         BE    IWRK1600            YEP...ONWARD
         LH    R0,HPAGES           START/CONTINUE JOB PAGE COUNT
IWRK1600 DS    0H
         STH   R0,PAGECNT          PAGES REQUIRED
         EJECT
***********************************************************************
*        ISSUE OS TIME SVC;                                           *
*        FORMAT DATE AS 'DDMMMYY';                                    *
***********************************************************************
         SPACE
         TIME  DEC
         SPACE
         LR    R5,R0               SAVE TIME - PACKED 'HHMMSSTH'
         SPACE
         STCM  R1,B'1111',PDATE    SAVE PACKED FORM
         UNPK  $DOUBLE,PDATE       CONVERT DATE TO ZONED
         MVC   DATEYR,$DOUBLE+3    THAT'S THE YEAR
         ZAP   $DOUBLE,PDAY        EXTRACT AND EXPAND DAY OF YEAR
         CVB   R0,$DOUBLE          CONVERT DAY PORTION TO BINARY
         TM    DATEYR+1,X'03'      LEAP YEAR?
         BZ    IWRK2200            YEP...ONWARD
         CH    R0,LEAPYEAR         IS DATE > 28FEB?
         BNH   IWRK2200            NOPE...ONWARD
         AH    R0,=H'1'            MAKE IT LIKE A LEAP YEAR
IWRK2200 DS    0H
         SPACE
         XR    R14,R14             LOOP INDEX
         LA    R15,12              LOOP COUNTER
         SPACE
IWRK2400 DS    0H
         CH    R0,MONTHLEN(R14)    IS THIS THE MONTH?
         BNH   IWRK1800            YEP...ONWARD
         SH    R0,MONTHLEN(R14)    DECREMENT DAY
         LA    R14,L'MONTHLEN(,R14) BUMP
         BCT   R15,IWRK2400         LOOP
         SPACE
IWRK1800 DS    0H
         LA    R14,12              MAX MONTHS
         SR    R14,R15             LESS LOOP INDEX
         MH    R14,=Y(L'MONTHNAM)  GENERATE OFFSET
         LA    R14,MONTHNAM(R14)   AND ADDRESS
         MVC   DATEMON,0(R14)      MOVE MONTH NAME
         CVD   R0,$DOUBLE                 DAY OF MONTH TO PACKED
         MVC   $DOUBLE(L'EDIT3),EDIT3     EDIT PATTERN
         ED    $DOUBLE(L'EDIT3),$DOUBLE+6 FORMAT DAY OF MONTH
         MVC   DATEDAY,$DOUBLE+2   FINIALLY
         EJECT
***********************************************************************
*        DETERMINE THE DAY-OF-THE-WEEK;                               *
*        FORMAT TIME AS 'HH:MM:SS';                                   *
*        FORMAT TIME AS 'HH:MM:SSAM' ALSO;                            *
*        RETURN TO CALLER VIA R8;                                     *
***********************************************************************
         SPACE
         MVI   PDAYS+L'PDAYS-1,X'0C' SET IN SIGN FOR YEAR
         MVO   PDAYS,PYEAR         MAKE IT PROPER PACKED W/SIGN
         MP    PDAYS,=P'36525'     100*(DAYS/YEAR)
         AP    PDAYS,=P'99'        FORCE ROUNDING UP
         SRP   PDAYS,X'1000'-2,0   CEIL(PDAYS/100)
         AP    PDAYS,PDAY          FINIALLY - DAY NUMBER SINCE 1/1/1900
         DP    PDAYS,=P'7'         DIVIDE BY DAYS/WEEK
         SPACE
         ZAP   $DOUBLE,DAYNO       EXPAND TO 8 BYTES
         CVB   R15,$DOUBLE         CONVERT TO BINARY
         MH    R15,=Y(L'DAYS)      INDEX TO DAYS TABLE
         LA    R15,DAYS(R15)       ADDRESS OF PROPER DAY NAME
         MVC   WEEKDAY,0(R15)      MOVE ANSWER
         SPACE
         ST    R5,$DOUBLE          PICK UP PACKED TIME
         SPACE
         MVC   TIME24-1(L'EDITTIME),EDITTIME   EDIT PATTERN
         ED    TIME24-1(L'EDITTIME),$DOUBLE    FORMAT 24 HOUR TIME
         MVC   TIME12+8(3),=C'AM ' PRIME 12 HOUR TIME
         CLI   $DOUBLE,X'12'       SEE IF IT'S REALLY PM
         BL    IWRK3000            NOPE...ONWARD
         MVI   TIME12+8,C'P'       SET TO 'PM'
         IC    R0,$DOUBLE          GET HOURS BYTE
         SH    R0,=Y(X'12')        REDUCE HOURS
         STC   R0,$DOUBLE          REPLACE IT
         TM    $DOUBLE,X'08'       BINARY SUBTRACTION ERROR?
         BNO   IWRK3000            NOPE
         NI    $DOUBLE,X'09'       A LITTLE MAGIC TO FIX IT
IWRK3000 DS    0H
         SPACE
         CLI   $DOUBLE,X'00'       IS IT NOON OR MIDNIGHT?
         BNE   IWRK3200            NOPE...ONWARD
         MVI   $DOUBLE,X'12'       SET TO 12
IWRK3200 DS    0H
         SPACE
         MVC   TIME12-1(L'EDITTIME),EDITTIME   EDIT PATTERN
         ED    TIME12-1(L'EDITTIME),$DOUBLE    FORMAT 12 HOUR TIME
         SPACE
         BR    R8                  RETURN TO CALLER
         EJECT
***********************************************************************
* EXTRACT:                         /*  FORMAT DATA INTO WORKAREA  */  *
*        SET ADDRESSABILITY FOR CURRENT JOE;                          *
*        MOVE VALUES FROM JES CB'S TO WORKAREA;                       *
***********************************************************************
         SPACE
EXTRACT  DS    0H
         L     R6,PPPWKJOE         CURRENT JOE WORK JOE
         USING JOE,R6
         MVC   JOBNAME,JCTJNAME    JOB NAME
         MVC   JESJOBID,JCTJOBID   JES JOBLOG ID
         MVC   SYSOUTCL,JOECURCL   SYSOUT CLASS
         MVC   PDEVICE,DCTDEVN     PRINTER DEVICE NAME
         MVC   PSYSTEM,$SID        SYSTEM ID
         MVC   ONODE,JCTNONDE      ORIGINAL NODE NAME
         MVC   GROUP,JOENAME       JOE GROUP NAME
         MVC   PGMRNAME,JCTPNAME   PROGRAMMER'S NAME
         MVC   FORM,DCTFORMS       FORMS NAME
         MVC   FCB,DCTFCB          FORMS CONTROL BUFFER
         MVC   ROOM,JCTROOMN       ROOM NUMBER
         SPACE
         XR    R0,R0               CLEAR PARAMETER REGISTER
         ICM   R0,B'1100',$OWNNODE CURRENT NODE NUMBER
         LA    R1,PNODE            PRINTER NODE NAME
         BAL   R5,ROUTEMAP         CONVERT NODE NUMBER
         SPACE
         MVC   ACF2NAME,$BLANKS    PRIME ACF2 USER ID NAME, IN CASE
         CLI   JCTUSEID,C' '       IS IT PRESENT?
         BNH   EXTR0040            NOPE...BLANKS
         MVC   ACF2NAME,JCTUSEID   GET ACF2 USER ID
EXTR0040 DS    0H
         SPACE
         L     R0,JOERECCT         TOTAL LINES
         CLI   SEPTYPE,ENDJOB      TRAILER?
         BNE   EXTR0070            NOPE...ONWARD
         S     R0,PPPRECNM         CURRENT RECORD NUMBER
EXTR0050 DS    0H
         S     R0,JOEWRECN         ALREADY DONE
EXTR0070 DS    0H
         CVD   R0,$DOUBLE          TO PACKED FORMAT
         MVC   LINESREM,EDIT7      EDIT PATTERN
         ED    LINESREM,$DOUBLE+4  EDIT 7 SIGNIFICANT DIGITS
         EJECT
***********************************************************************
*        CALL ROUTEMAP TO FORMAT JOE ROUTE/DESTINATION;               *
*        FORMAT JOE NAME 1 & 2;                                       *
*        IF FCB NAME IMPLIES 8 LINES PER INCH                         *
*          THEN MULTIPLY LINE COUNT BY 1.3333;                        *
*        IF PRINTING SITE REQUIRES 'BLDG' DESIGNATOR                  *
*          THEN SET ROOMBLDG TO 'BLDG';                               *
*          ELSE SET ROOMBLDG TO 'ROOM';                               *
*        RETURN TO CALLER VIA R8;                                     *
***********************************************************************
         SPACE
         L     R0,JOERECCT         TOTAL LINES
         CVD   R0,$DOUBLE          TO PACKED FORMAT
         MVC   LINESTOT,EDIT7      EDIT PATTERN
         ED    LINESTOT,$DOUBLE+4  EDIT 7 SIGNIFICANT DIGITS
         SPACE
         L     R0,JOEROUT          WHOLE ROUTE WORD
         LA    R1,PROUTE           PRINTER ROUTE CODE
         BAL   R5,ROUTEMAP         FORMAT INTO 8 BYTES
         SPACE
         LH    R0,JOEID1           FIRST ID
         MH    R0,=H'10000'        SHIFT LEFT 4 DECIMAL DIGITS
         AH    R0,JOEID2           SECOND ID
         CVD   R0,$DOUBLE          CONVERT TO PACKED
         UNPK  JOEIDF,$DOUBLE+4(4) TO ZONED
         OI    JOEIDF+6,X'F0'      FIX THE SIGN BYTE
         MVI   JOEIDF+3,C'.'       INSERT PERIOD
         SPACE
         CLI   FCB,C'8'            FIRST CHAR=8 => 8LPI
         BNE   EXTR1400            LET IT BE AT 6LPI
         SPACE
         LH    R1,SEPLINES         ---
         BCTR  R1,0                -
         MH    R1,=HS14'1.33333'   -  COMPUTE
         AH    R1,=HS14'0.5'       -  SEPLINES =
         SRA   R1,14               -    ROUNDED((SEPLINES-1)*1.3333)+1)
         LA    R1,1(,R1)           -
         STH   R1,SEPLINES         ---
EXTR1400 DS    0H
         SPACE
         MVC   ROOMBLDG,=C'ROOM'   INITIALIZE
         CLC   PNODE,PRCSITE       THIS SITE REQUIRE 'BLDG'?
         BE    EXTR1800            NOPE...ONWARD
         MVC   ROOMBLDG,=C'BLDG'   INITIALIZE
EXTR1800 DS    0H
         SPACE
         BR    R8                  RETURN TO CALLER
         SPACE
         DROP  R6,R9               JOE,DCT
         EJECT
***********************************************************************
* HEADER:                   /*  FORMAT & PRINT LOGO AND INFO      */  *
*        IF THERE ARE NOT ENOUGH REMAINING LINES FOR LOGO             *
*          THEN RETURN VIA R8;                                        *
*        IF SITE REQUIRES APS LOGOS                                   *
*          THEN DO;                                                   *
*                 SET UP FOR APS LOGO AND TITLE LINE;                 *
*                 IF ROOM INDICATES DELIVERY TO REMOTE BUILDING       *
*                   THEN SET UP FOR 'DELIVER' BLOCK;                  *
*               END;                                                  *
*          ELSE SET UP FOR PRC LOGO AND TITLE LINE;                   *
*        DECREMENT LINECNT FOR LOGO;                                  *
*        DO FOR EACH DATA LINE;                                       *
*          FORMAT TEXT AND VALUE FIELDS;                              *
*          INSERT LOGO;                                               *
*          CALL PRINTBUF SUBROUTINE;                                  *
*        END:                                                         *
*        CALL PRINTBLK FOR A BLANK LINE;                              *
*        RETURN VIA R8;                                               *
***********************************************************************
         SPACE
HEADER   DS    0H
         LH    R0,LINECNT          REMAINING LINES
         SH    R0,=H'17'           DECREMENT
         BMR   R8                  NOT ENOUGH? GO BACK
         STH   R0,LINECNT          UPDATE COUNT
         SPACE
         LA    R2,BUFSTART         DATA AREA IN BUFFER
         USING INFOHEAD,R2
         MVI   INFOHEAD,C' '       FILL CHARACTER
         MVC   INFOHEAD+1(LINELEN),INFOHEAD
         SPACE
         LA    R6,DELIVERX         DEFAULT - NO REMOTE BLDG BLOCK
         CLC   PNODE,PRCSITE       THIS SITE REQUIRE PRC LOGO?
         BNE   HEAD1200            NOPE...ONWARD
         SPACE
         L     R3,=A(PRCLOGO)      SET LOGO ADDRESS
         LA    R14,TH1PRC          SET TITLE HEADER LINE 1 ADDRESS
         LA    R15,L'TH1PRC-1      SET LENGTH FOR EXECUTE
         B     HEAD1900            ONWARD
         SPACE
HEAD1200 DS    0H
         L     R3,=A(APSLOGO)      SET LOGO ADDRESS
         LA    R14,TH1APS          SET TITLE HEADER LINE 1 ADDRESS
         LA    R15,L'TH1APS-1      SET LENGTH FOR EXECUTE
         CLI   ROOM,ROOMREM        DESIGNATED FOR REMOTE BLDG?
         BNE   HEAD1900            NOPE...ONWARD
         LA    R6,DELIVER          REMOTE BLDG BLOCK FIRST LINE
         SPACE
HEAD1900 DS    0H
         SPACE
*                                  LINE 1
         SPACE
         LA    R1,LINELEN-LOGOLEN-1          LENGTH FOR TITLE HEAD 1
         SR    R1,R15                        SUBTRACT TEXT LENGTH
         SRL   R1,1                          DIVIDE BLANK SPACE BY 2
         LA    R1,INFOHEAD(R1)               CENTER HEAD1
         EX    R15,HEAD9900                  FIRST HEAD LINE
         MVC   INFOLOGO,0(R3)                LOGO LINE
         BAL   R5,PRINTBUF                   PRINT IT
         SPACE
*                                  LINE 2
         SPACE
         LA    R3,LOGOLEN(,R3)               NEXT LINE OF LOGO
         MVC   INFOLOGO,0(R3)                LOGO LINE
         BAL   R5,PRINTBUF                   PRINT IT
         SPACE
*                                  LINE 3
         SPACE
         LA    R1,(LINELEN-LOGOLEN-L'TH2)/2
         LA    R1,INFOHEAD(R1)               CENTER HEAD2
         MVC   0(L'TH2,R1),TH2               SECOND HEAD LINE
         LA    R3,LOGOLEN(,R3)               NEXT LINE OF LOGO
         MVC   INFOLOGO,0(R3)                LOGO LINE
         BAL   R5,PRINTBUF                   PRINT IT
         SPACE
*                                  LINE 4
         SPACE
         LA    R3,LOGOLEN(,R3)               NEXT LINE OF LOGO
         MVC   INFOLOGO,0(R3)                LOGO LINE
         BAL   R5,PRINTBUF                   PRINT IT
         SPACE
*                                  LINE 5
         SPACE
         MVC   INFOTXT1(L'TJN),TJN           JOBNAME
         MVC   INFOVAL1(L'JOBNAME),JOBNAME   VALUE
         MVC   INFOTXT2(L'TLN),TLN           LINES
         CLI   SEPTYPE,ENDJOB                IS THIS A TRAILER?
         BNE   HEAD2000                      NO...ONWARD
         MVC   INFOTXT2(L'TLL),TLL           LINES LEFT
HEAD2000 DS    0H
         MVC   INFOVAL2(L'LINESREM),LINESREM REMAINING LINES
         LA    R3,LOGOLEN(,R3)               NEXT LINE OF LOGO
         MVC   INFOLOGO,0(R3)                LOGO LINE
         BAL   R5,PRINTBUF                   PRINT IT
         SPACE
*                                  LINE 6
         SPACE
         MVC   INFOTXT1(L'TJI),TJI           JES JOB ID
         MVC   INFOVAL1(L'JESJOBID),JESJOBID VALUE
         MVC   INFOTXT2(L'TOF),TOF           OF
         MVC   INFOVAL2(L'LINESTOT),LINESTOT TOTAL LINES
         LA    R3,LOGOLEN(,R3)               NEXT LINE OF LOGO
         MVC   INFOLOGO,0(R3)                LOGO LINE
         BAL   R5,PRINTBUF                   PRINT IT
         SPACE
*                                  LINE 7
         SPACE
         LA    R3,LOGOLEN(,R3)               NEXT LINE OF LOGO
         MVC   INFOLOGO,0(R3)                LOGO LINE
         BAL   R5,PRINTBUF                   PRINT IT
         SPACE
*                                  LINE 8
         SPACE
         MVC   INFOTXT1(L'TON),TON           ORIGIN NODE
         MVC   INFOVAL1(L'ONODE),ONODE       VALUE
         MVC   INFOTXT2(L'TPO),TPO           PRINTED ON
         MVC   INFOVAL2(L'PDEVICE),PDEVICE   VALUE
         LA    R3,LOGOLEN(,R3)               NEXT LINE OF LOGO
         MVC   INFOLOGO,0(R3)                LOGO LINE
         BAL   R5,PRINTBUF                   PRINT IT
         SPACE
*                                  LINE 9
         SPACE
         MVC   INFOTXT2(L'TSY),TSY           SYSTEM
         MVC   INFOVAL2(L'PSYSTEM),PSYSTEM   VALUE
         LA    R3,LOGOLEN(,R3)               NEXT LINE OF LOGO
         MVC   INFOLOGO,0(R3)                LOGO LINE
         BAL   R5,PRINTBUF                   PRINT IT
         SPACE
*                                  LINE 10
         SPACE
         MVC   INFOTXT1(L'TGP),TGP           JOE GROUP NAME
         MVC   INFOVAL1(L'GROUP),GROUP       VALUE
         MVC   INFOTXT2(L'TPN),TPN           PRINTER NODE
         MVC   INFOVAL2(L'PNODE),PNODE       VALUE
         LA    R3,LOGOLEN(,R3)               NEXT LINE OF LOGO
         MVC   INFOLOGO,0(R3)                LOGO LINE
         BAL   R5,PRINTBUF                   PRINT IT
         SPACE
*                                  LINE 11
         SPACE
         MVC   INFOTXT1(L'TID),TID           JOE ID 1/2
         MVC   INFOVAL1(L'JOEIDF),JOEIDF     VALUE
         MVC   INFOTXT2(L'TRC),TRC           ROUTE CODE
         MVC   INFOVAL2(L'PROUTE),PROUTE     VALUE
         LA    R3,LOGOLEN(,R3)               NEXT LINE OF LOGO
         MVC   INFOLOGO,0(R3)                LOGO LINE
         BAL   R5,PRINTBUF                   PRINT IT
         SPACE
*                                  LINE 12
         SPACE
         LA    R3,LOGOLEN(,R3)               NEXT LINE OF LOGO
         MVC   INFOLOGO,0(R3)                LOGO LINE
         BAL   R5,PRINTBUF                   PRINT IT
         SPACE
*                                  LINE 13
         SPACE
         MVC   INFOTXT1(L'TTI),TTI           TIME
         MVC   INFOVAL1(L'TIME24),TIME24     VALUE
         MVC   INFOTXT2(L'TSC),TSC           SYSOUT CLASS
         MVC   INFOVAL2(L'SYSOUTCL),SYSOUTCL VALUE
         MVC   INFOVAL3(L'DELIVER),0(R6)     'DELIVER' BLOCK
         LA    R3,LOGOLEN(,R3)               NEXT LINE OF LOGO
         MVC   INFOLOGO,0(R3)                LOGO LINE
         BAL   R5,PRINTBUF                   PRINT IT
         SPACE
*                                  LINE 14
         SPACE
         MVC   INFOTXT1(L'TDA),TDA           DATE
         MVC   INFOVAL1+1(L'DATE),DATE       VALUE
         MVC   INFOVAL1-L'WEEKDAY(L'WEEKDAY),WEEKDAY DAY-OF-WEEK
         MVC   INFOTXT2(L'TFO),TFO           FORMS NAME
         MVC   INFOVAL2(L'FORM),FORM         VALUE
         LA    R6,L'DELIVER(,R6)             NEXT LINE OF BLOCK
         MVC   INFOVAL3(L'DELIVER),0(R6)     'DELIVER' BLOCK
         LA    R3,LOGOLEN(,R3)               NEXT LINE OF LOGO
         MVC   INFOLOGO,0(R3)                LOGO LINE
         BAL   R5,PRINTBUF                   PRINT IT
         SPACE
*                                  LINE 15
         SPACE
         MVC   INFOTXT2(L'TFC),TFC           FCB NAME
         MVC   INFOVAL2(L'FCB),FCB           VALUE
         LA    R6,L'DELIVER(,R6)             NEXT LINE OF BLOCK
         MVC   INFOVAL3(L'DELIVER),0(R6)     'DELIVER' BLOCK
         LA    R3,LOGOLEN(,R3)               NEXT LINE OF LOGO
         MVC   INFOLOGO,0(R3)                LOGO LINE
         BAL   R5,PRINTBUF                   PRINT IT
         SPACE
*                                  LINE 16
         SPACE
         MVC   INFOTXT1(L'TAL),TAL           ACF LOGON
         MVC   INFOVAL1(L'ACF2NAME),ACF2NAME VALUE
         LA    R6,L'DELIVER(,R6)             NEXT LINE OF BLOCK
         MVC   INFOVAL3(L'DELIVER),0(R6)     'DELIVER' BLOCK
         LA    R3,LOGOLEN(,R3)               NEXT LINE OF LOGO
         MVC   INFOLOGO,0(R3)                LOGO LINE
         BAL   R5,PRINTBUF                   PRINT IT
         SPACE
*                                  LINE 17
         SPACE
         MVC   INFOTXT1(L'TPG),TPG           PROGRAMMER
         MVC   INFOVAL1(L'PGMRNAME),PGMRNAME VALUE
         MVC   INFOVAL2-L'ROOMBLDG-1(L'ROOMBLDG),ROOMBLDG ROOM/BLDG
         MVC   INFOVAL2(L'ROOM),ROOM         VALUE
         LA    R6,L'DELIVER(,R6)             NEXT LINE OF BLOCK
         MVC   INFOVAL3(L'DELIVER),0(R6)     'DELIVER' BLOCK
         BAL   R5,PRINTBUF                   PRINT IT
         SPACE
         BAL   R5,PRINTBLK         PRINT BLANK LINE
         SPACE
         DROP  R2                  HEADER
         BR    R8                  RETURN TO CALLER
         SPACE
HEAD9900 MVC   0(*-*,R1),0(R14)    EXECUTED -- MOVE TITLE HEADER LINE 1
         EJECT
***********************************************************************
* BLOCK:                   /*  FORMAT & PRINT BLOCK LETTER LINES  */  *
*        DO FOR ACF2NAME, JOBNAME, JOBID;                             *
*          IF THERE ARE NOT ENOUGH REMAINING LINES FOR BLOCK LETTERS  *
*              THEN RETURN VIA R5;                                    *
*          DECREMENT LINE COUNT;                                      *
*          ISSUE $PRBLOCK TO FORMAT AND PRINT;                        *
*          CALL PRINTBLK SUBROUTINE FOR A BLANK LINE;                 *
*        END;                                                         *
*        RETURN VIA R8;                                               *
***********************************************************************
         SPACE
BLOCK    DS    0H
         LH    R0,LINECNT          REMAINING LINES
         SH    R0,=Y(BLOCKLNS)     DECREMENT
         BMR   R8                  NOT ENOUGH ROOM...RETURN
         STH   R0,LINECNT          UPDATE COUNT
         LA    R0,ACF2NAME         ACF2 LOGON ID
         $PBLOCK DATA=(R0),BUFFER=(R7),SLANT=YES,CENTER=YES
         BAL   R5,PRINTBLK         BLANK LINE
         SPACE
         LH    R0,LINECNT          REMAINING LINES
         SH    R0,=Y(BLOCKLNS)     DECREMENT
         BMR   R8                  NOT ENOUGH ROOM...RETURN
         STH   R0,LINECNT          UPDATE COUNT
         LA    R0,JOBNAME          JOBNAME
         $PBLOCK DATA=(R0),BUFFER=(R7),SLANT=NO,CENTER=YES
         BAL   R5,PRINTBLK         BLANK LINE
         SPACE
         LH    R0,LINECNT          REMAINING LINES
         SH    R0,=Y(BLOCKLNS)     DECREMENT
         BMR   R8                  NOT ENOUGH ROOM...RETURN
         STH   R0,LINECNT          UPDATE COUNT
         LA    R0,JESJOBID         JES JOBLOG ID
         $PBLOCK DATA=(R0),BUFFER=(R7),SLANT=NO,CENTER=YES
         BAL   R5,PRINTBLK         BLANK LINE
         SPACE
         BR    R8                  RETURN TO CALLER
         EJECT
***********************************************************************
* DETAIL:       /*  FORMAT & PRINT DETAIL LINES TO EXHAUST COUNT  */  *
*        IF NO MORE LINES TO PRINT                                    *
*          THEN RETURN VIA R8;                                        *
*        ADJUST LINECNT TO 0<LINECNT<X'100';                          *
*        CLEAR PRINT AREA TO SPACES;                                  *
*        SAVE LINECNT IN $PRPUT PARAMETER;                            *
***********************************************************************
         SPACE
DETAIL   DS    0H
         XR    R0,R0               CLEAR WORK REGISTER
         ICM   R0,B'0011',LINECNT  GET LINECOUNT
         BZR   R8                  NONE LEFT...RETURN
         CH    R8,=Y(FF)           GREATER THAN X'FF'?
         BNH   DET1000             NOPE...ONWARD
         LH    R8,=Y(FF)           SET TO MAXIMUM
DET1000  DS    0H
         STC   R0,PRPUTPRM+1       SAVE IN $PRPUT PARM LIST
         MVI   PRPUTPRM,PRPUTOPT   OTHER BYTE OF PARAMETER (OPTIONS)
         SPACE
         LA    R1,BUFSTART         AREA IN BUFFER FOR DATA
         USING DETLINE,R1
         SPACE
         MVI   DETLINE,C' '        FILL CHARACTER
         MVC   DETLINE+1(LINELEN),DETLINE
         EJECT
***********************************************************************
*        FORMAT DETAIL LINE;                                          *
***********************************************************************
         SPACE
         CLI   SEPTYPE,STARTJOB    START OF JOB?
         BE    DET2100             YEP...GO SET
         CLI   SEPTYPE,CONTJOB     CONTINUE?
         BE    DET2200             YEP...GO SET
         MVC   DETSTYPE,=C' END '  BEGINNING OF LINE
         MVC   DETETYPE,=C' END '  END OF LINE
         B     DET2400             ONWARD
DET2100  DS    0H
         MVC   DETSTYPE,=C'START'  BEGINNING OF LINE
         MVC   DETETYPE,=C'START'  END OF LINE
         B     DET2400             ONWARD
DET2200  DS    0H
         MVC   DETSTYPE,=C' CONT'  BEGINNING OF LINE
         MVC   DETETYPE,=C'CONT '  END OF LINE
DET2400  DS    0H
         SPACE
         MVI   DETSDLM,C'*'        FRAME DELIMETER CHARACTER
         MVC   DETJNUM(1),JESJOBID     J/S/T CHARACTER ONLY
         MVC   DETJNUM+1(4),JESJOBID+4 NUMBER PART
         MVC   DETJNAME,JOBNAME    JOBNAME
         MVC   DETONODE,ONODE      ORININATING DEVICE/NODE
         MVC   DETACF2N,ACF2NAME   ACF2 USER ID
         MVC   DETPNAME,PGMRNAME   PROGRAMMER NAME
         MVC   DETRBTXT,ROOMBLDG   'ROOM' OR 'BLDG'
         MVC   DETROOM,ROOM        ROOM
         MVC   DETTIME,TIME12      TIME WITH AM/PM
         MVC   DETDATE,DATE        DATE
         MVC   DETPNODE,PNODE      PRINTER NODE
         MVC   DETROUTE,PROUTE     JOE GROUP NAME
         MVC   DETPSYS,PSYSTEM     SYSTEM ID
         MVC   DETPDEV,PDEVICE     PRINTER DEVICE
         MVC   DETCLASS,SYSOUTCL   SYSOUT CLASS
         MVI   DETEDLM,C'*'        FRAME DELIMETER CHARACTER
         EJECT
***********************************************************************
*        ISSUE $PRPUT WITH SPECIAL COUNT;                             *
*        RETURN VIA R8;                                               *
***********************************************************************
         SPACE
         LA    R0,LINELEN          LINE SIZE
         $PRPUT DATA=(R1),LEN=(R0),WAIT=YES,COUNT=1
         SPACE
*              THE FOLLOWING CIRCUMVENTS A RESTRICTION
*              PROHIBITING A VARIABLE NUMBER OF LINES
*              TO BE PRINTED BY $PRPUT
         SPACE
DETFIX   ORG   *-X'E'              RESET TO WITHIN $PRPUT EXPANSION
PRPUTOPT EQU   128                 FIRST BYTE OF $PRPUT PARM (OPTIONS)
         ICM   R0,B'1100',PRPUTPRM NEW,IMPROVED PARM -- CORRECT COUNT
         ORG   DETFIX              BACK TO NORMAL
         SPACE
*              END OF CIRCUMVENTION
         SPACE
         DROP  R1                  DETAIL LINE
         BR    R8                  RETURN TO CALLER
         EJECT
***********************************************************************
* ROUTEMAP:                            /*  DERIVED FROM HASPPRPU  */  *
*        LOCATE FIRST AND LAST RDT;                                   *
*        DO FOR EACH RDT;                                             *
*          IF ROUTECODE = RDT ROUTE                                   *
*            THEN IF THIS IS THE FIRST MATCH                          *
*                   THEN SAVE DESTINATION;                            *
*                   ELSE INDICATE MULTIPLE MATCHES FOUND;             *
*        END;                                                         *
***********************************************************************
         SPACE
ROUTEMAP DS    0H
         L     R15,$SVT            GET THE SSVT ADDRESS AND
         USING SVT,R15
         LM    R14,R15,SVTRDT      THEN FIRST/LAST DESTID
         DROP  R15
         USING RDT,R14
         MVI   0(R1),X'00'         INDICATE NO DESTID YET
         SPACE
RMAP1000 DS    0H
         CR    R14,R15             PAST THE END OF THE TABLE...
         BH    RMAP4000            YES - BRANCH TO CONVERSION RTN
         L     R2,RDTROUTE         LOAD COMPLETE ROUTE CODE
         TM    RDTFLAG,RDTFLAGR    TEST FOR REMOTE
         BNO   RMAP2000            BRANCH IF NOT REMOTE
         TM    RDTFLAG,RDTFLAGN    IS NODE SPECIFIED
         BO    RMAP2000            YES THEN BYPASS
         ICM   R2,B'1100',$OWNNODE PROVIDE DEFAULT NODE
RMAP2000 DS    0H
         SPACE
         CLR   R2,R0               A DESTID FOR THIS ROUTE?
         BNE   RMAP3000            NO - GO CHECK THE NEXT ONE
         CLI   0(R1),X'00'         HAVE DESTID FOR JOE YET ...
         BNE   RMAP5000            YES, GO FORM JOE DEST
         MVC   0(L'RDTNAME,R1),RDTNAME  NO, MOVE DESTID FROM RDT
RMAP3000 DS    0H
         LA    R14,RDTSIZ(,R14)    GET ADDR OF NEXT RDT
         B     RMAP1000            BRANCH TO CONTINUE CHECKING
         DROP  R14                 RDT
         EJECT
***********************************************************************
*        IF NO MATCH WAS FOUND OR MULTIPLE MATCHES WERE FOUND         *     *
*          THEN CONVERT ROUTECODE TO RNNNN|UNNNN|'LOCAL'              *
*          ELSE USE DESTINATION FROM RDT;                             *
*        RETURN VIA R5;                                               *
***********************************************************************
         SPACE
RMAP4000 DS    0H
         CLI   0(R1),X'00'         SINGLE DESTID FOR JOE ...
         BNE   RMAP9000            YES, SKIP DEST FORMULATION
         SPACE
RMAP5000 DS    0H
         CLM   R0,B'0011',$ZEROS   IS THIS A LOCAL JOE...
         BE    RMAP7000            YES - BR TO LOCAL CONVERSION
         SPACE
         L     R15,=X'0000FFFF'    EXTRACTION MASK FOR REMOTE
         NR    R15,R0              PLACE ROUTE INTO REGISTER
         CVD   R15,0(R1)           CONVERT ROUTING TO DECIMAL
         MVC   $DOUBLE(L'EDIT5),EDIT5   MOVE IN PATTERN
         ED    $DOUBLE(L'EDIT5),5(R1)   AND EDIT ROUTING
         MVC   0(5,R1),$DOUBLE+1   VALUE INTO
         MVC   5(3,R1),$BLANKS     DESTINATION
         CLM   R0,B'1100',$ZEROS   CHECK FOR UNIT SPECIFICATION
         BE    RMAP6000            BR TO MOVE IN 'U' LABEL
         MVI   0(R1),C'R'          INDICATE ROUTED TO REMOTE
         B     RMAP9000            MOVE ROUTE INTO HDRLINE
RMAP6000 DS    0H
         MVI   0(R1),C'U'          INDICATE SPECIAL LOCAL ROUT
         B     RMAP9000            MOVE ROUTE INTO HDRLINE
         SPACE
RMAP7000 DS    0H
         MVC   0(8,R1),=CL8'LOCAL' MOVE LOCAL TO WORK AREA
         SPACE
RMAP9000 DS    0H
         BR    R5                  RETURN TO CALLER
         EJECT
***********************************************************************
* ENDPAGE:                 /*  FORMAT & PRINT END-OF-PAGE MARKER  */  *
*        IF THIS IS NOT A TRAILER       - AND -                       *
*           THIS IS THE LAST PAGE                                     *
*          THEN RETURN VIA R8;                                        *
*        FORMAT SKIP TO END-OF-PAGE;                                  *
*        ISSUE $PRPUT TO SKIP TO END-OF-PAGE;                         *
*        FORMAT ROW OF '*'S;                                          *
*        ISSUE $PRPUT TO PRINT LINE;                                  *
*        IF THIS IS THE LAST PAGE                                     *
*          THEN RETURN VIA R8;                                        *
*        ISSUE $PRPUT TO SKIP TO TOP-OF-PAGE;                         *
*        RETURN VIA R8;                                               *
***********************************************************************
         SPACE
ENDPAGE  DS    0H
         CLI   SEPTYPE,ENDJOB      TRAILER?
         BE    ENDP1000            YEP...ALWAYS DO PAGE BREAK
         CLC   PAGECNT,=H'1'       LAST PAGE?
         BER   R8                  YEP...SKIP PAGE BREAK
ENDP1000 DS    0H
         SPACE
         LA    R1,BUFSTART         DATA AREA IN BUFFER
         MVI   0(R1),SKIPCH9       SKIP TO CHANNEL 9 IMMEDIATE
         LA    R0,1                DATA LENGTH
         SPACE
         $PRPUT DATA=(R1),LEN=(R0),WAIT=YES,COUNT=1,CC=M
         SPACE
         LA    R1,BUFSTART         DATA AREA IN BUFFER
         MVI   0(R1),C'*'          BREAK CHARATCER
         MVC   1(LINELEN-1,R1),0(R1)
         LA    R0,LINELEN          DATA LENGTH
         SPACE
         $PRPUT DATA=(R1),LEN=(R0),WAIT=YES,COUNT=1
         SPACE
         CLC   PAGECNT,=H'1'       LAST PAGE?
         BER   R8                  YEP...SKIP PAGE BREAK
         LA    R1,BUFSTART         DATA AREA IN BUFFER
         MVI   0(R1),SKIPCH1       SKIP TO CHANNEL 1 IMMEDIATE
         LA    R0,1                DATA LENGTH
         SPACE
         $PRPUT DATA=(R1),LEN=(R0),WAIT=YES,COUNT=1,CC=M
         SPACE
         BR    R8                  RETURN TO CALLER
         EJECT
***********************************************************************
* PRINTBUF:                    /*  PRINT A FORMATTED BUFFER LINE  */  *
*        ISSUE $PRPUT FOR BUFFER;                                     *
*        CLEAR BUFFER TO SPACES;                                      *
*        RETURN VIA R5;                                               *
***********************************************************************
         SPACE
PRINTBUF DS    0H
         SPACE
         LA    R1,BUFSTART         ADDRESS OF DATA PORTION OF BUFFER
         LA    R0,LINELEN          MAX LINE LENGTH
         $PRPUT LEN=(R0),DATA=(R1),WAIT=YES
         SPACE
         MVI   BUFSTART,C' '       FILL CHARACTER
         MVC   BUFSTART+1(LINELEN-1),BUFSTART
         SPACE
         BR    R5                  RETURN
         SPACE 2
***********************************************************************
* PRINTBLK:                                /*  PRINT BLANK LINES  */  *
*        IF THERE ARE NOT ENOUGH REMAINING LINES                      *
*          THEN RETURN VIA R5;                                        *
*        DECREMENT LINE COUNTER;                                      *
*        ISSUE $PRPUT FOR BUFFER;                                     *
*        RETURN VIA R5;                                               *
***********************************************************************
         SPACE
PRINTBLK DS    0H
         LH    R0,LINECNT          REMAINING LINES
         SH    R0,=H'1'            DECREMENT
         BMR   R5                  NOT ENOUGH? GO BACK
         STH   R0,LINECNT          UPDATE COUNT
         SPACE
         MVI   BUFSTART,C' '       BLANK
         LA    R0,1                MAX LINE LENGTH
         LA    R1,BUFSTART         ADDRESS OF DATA PORTION OF BUFFER
         MVI   0(R1),SKIPLINE      SKIP 1 LINE IMMEDIATE
         $PRPUT LEN=(R0),DATA=(R1),WAIT=YES,COUNT=1,CC=M
         SPACE
         BR    R5                  RETURN
         SPACE
         DROP  R4                  WORKAREA
         DROP  R7                  BUFFER
         EJECT
***********************************************************************
*        CONSTANTS AND LITERALS                                       *
***********************************************************************
         SPACE
STARTJOB EQU   0                   ---
CONTJOB  EQU   4                   - SEPARATOR TYPE ENTRY PARM VALUES
ENDJOB   EQU   8                   ---
         SPACE
LCLHPAGE EQU   2                   NUMBER OF HEADER PAGES  --  LOCAL
LCLTPAGE EQU   1                   NUMBER OF TRAILER PAGES --  LOCAL
REMHPAGE EQU   1                   NUMBER OF HEADER PAGES  --  REMOTE
REMTPAGE EQU   1                   NUMBER OF TRAILER PAGES --  REMOTE
         SPACE
BLOCKLNS EQU   12                  NUMBER OF LINES PRODUCED BY $PRBLOCK
LINELEN  EQU   132                 PRINT LINE LENGTH
         SPACE
         DS    0F
LCLPAGES DC    Y(LCLHPAGE,LCLTPAGE)
REMPAGES DC    Y(REMHPAGE,REMTPAGE)
         SPACE
SKIPLINE EQU   X'09'               SKIP 1 LINE IMMEDIATE
SKIPCH1  EQU   X'8B'               SKIP TO CH 1 IMMEDIATE -- TOP PAGE
SKIPCH9  EQU   X'CB'               SKIP TO CH 9 IMMEDIATE -- END PAGE
         SPACE
LOGOLEN  EQU   64                  ALL LOGO LINES ARE 64 BYTES LONG
LOGOLNES EQU   16                  ALL LOGOS HAVE 16 LINES
         SPACE
PRCSITE  DC    CL8'APS1'           SITE REQUIRING PRC LOGO
ROOMREM  EQU   C'C'                THIS FIRST CHAR OF ROOM => REM BLDG
         SPACE
TH1PRC DC C'P L A N N I N G  R E S E A R C H  C O R P O R A T I O N'
TH1APS DC C'U. S.  P A T E N T  A N D  T R A D E M A R K   O F F I C E'
TH2      DC    C'A U T O M A T E D  P A T E N T  S Y S T E M'
TJN      DC    C'JOB NAME'
TJI      DC    C'JES2 JOB ID'
TON      DC    C'ORIGIN NODE'
TGP      DC    C'GROUP NAME'
TID      DC    C'JOE ID 1&&2'
TTI      DC    C'TIME'
TDA      DC    C'DATE'
TAL      DC    C'ACF2 USER ID'
TPG      DC    C'PROGRAMMER'
TLN      DC    C'LINES'
TLL      DC    C'LINES LEFT'
TOF      DC    C'  OF'
TPO      DC    C'PRINTED ON'
TSY      DC    C'SYSTEM'
TPN      DC    C'NODE'
TRC      DC    C'ROUTE CODE'
TSC      DC    C'SYSOUT CLASS'
TFC      DC    C'FCB NAME'
TFO      DC    C'FORMS'
         SPACE
DELIVER  DC    C'  ***********  '
         DC    C' ************* '
         DC    C'*** DELIVER ***'
         DC    C' ************* '
         DC    C'  ***********  '
DELIVERL EQU   (*-DELIVER)/L'DELIVER
DELIVERX DC    (DELIVERL)CL(L'DELIVER)' '
         SPACE
EDIT3    DC    X'40202120'           EDIT PATTERN -- BLANK + 3 DIGITS
EDIT5    DC    X'402020202120'       EDIT PATTERN -- BLANK + 5 DIGITS
EDIT7    DC    X'4020202020202120'   EDIT PATTERN -- BLANK + 7 DIGITS
*                  H H : M M : S S
EDITTIME DC    X'4021207A20207A2020' EDIT PATTERN -- TIME OF DAY
         SPACE
*                J  F  M  A  M  J  J  A  S  O  N  D
MONTHLEN DC    H'31,29,31,30,31,30,31,31,30,31,30,31'
LEAPYEAR DC    Y(31+28)            ADJUST DAY IF GREATER THAN THIS
MONTHNAM DS    0CL3
         DC    C'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC???'
         SPACE
DAYS     DS    (0*7)CL9
         DC    CL9' SATURDAY'
         DC    CL9'   SUNDAY'      1/1/1900 (DAY 1) WAS SUNDAY
         DC    CL9'   MONDAY'
         DC    CL9'  TUESDAY'
         DC    CL9'WEDNESDAY'
         DC    CL9' THURSDAY'
         DC    CL9'   FRIDAY'
         SPACE 2
         LTORG
         EJECT
***********************************************************************
*        LOGO DEFINITIONS                                             *
***********************************************************************
         SPACE
*              EACH LOGO DEFINITION IS 1K LONG. PUTTING THEM EARLIER
*              IN THE CSECT CAUSES THE SINGLE BASE REGISTER
*              ADDRESSING TO BE EXCEEDED. BY PLACING THEM LAST
*              THE CSECT AND ADDRESSING VIA ACONS, LIFE GOES ON.
*              NOTE THAT THE LOGOS ARE IN LOWER CASE.
         SPACE
PRCLOGO  EQU   *,LOGOLEN,C'C'
 DC C'      pppppppp              rrrrrrrr              cccccccc      '
 DC C'   pppppppppppppp        rrrrrrrrrrrrrr        cccccccccccccc   '
 DC C'  pppppppppppppppp      rrrrrrrrrrrrrrrr      cccccccccccccccc  '
 DC C' pppppp      pppppp    rrrrrr     rrrrrrr    cccccc      cccccc '
 DC C'pppppp        pppppp  rrrrrr       rrrrrrr  cccccc        cccccc'
 DC C'ppppp          ppppp  rrrrr         rrrrrr  ccccc               '
 DC C'ppppp          ppppp  rrrrr                 ccccc               '
 DC C'ppppp         pppppp  rrrrr                 cccccc        cccccc'
 DC C'ppppp p     ppppppp   rrrrr                  cccccc      cccccc '
 DC C'ppppp pppppppppppp    rrrrr                   cccccccccccccccc  '
 DC C'ppppp ppppppppppp     rrrrr                    cccccccccccccc   '
 DC C'ppppp  ppppppp        rrrrr                       cccccccc      '
 DC C'ppppp                                                           '
 DC C'ppppp                                                           '
 DC C'ppppp                                                           '
 DC C'ppppp                                                           '
PRCLOGN  EQU   (*-PRCLOGO)/L'PRCLOGO  NUMBER OF LOGO LINES
         SPACE
APSLOGO  EQU   *,LOGOLEN,C'C'
 DC C'      aaaaaaaa              pppppppp              ssssssss      '
 DC C'   aaaaaaaaaaaaa         pppppppppppppp        ssssssssssssss   '
 DC C'  aaaaaaaaaaaaaaaa      pppppppppppppppp      ssssssssssssssss  '
 DC C' aaaaaa      aaaaaa    pppppp      pppppp    ssssss      ssssss '
 DC C'aaaaaa        aaaaaa  pppppp        pppppp  sssssssssss   ssssss'
 DC C'aaaaa          aaaaa  ppppp          ppppp     sssssssssss      '
 DC C'aaaaa          aaaaa  ppppp          ppppp        sssssssssss   '
 DC C'aaaaaa        aaaaaa  ppppp         pppppp  ssssss   sssssssssss'
 DC C' aaaaaa      aaaaaaa  ppppp p     ppppppp    ssssss      ssssss '
 DC C'  aaaaaaaaaaaaaaaaaa  ppppp pppppppppppp      ssssssssssssssss  '
 DC C'   aaaaaaaaaaaaaaaaa  ppppp ppppppppppp        ssssssssssssss   '
 DC C'      aaaaaaaa aaaaa  ppppp  ppppppp              ssssssss      '
 DC C'                      ppppp                                     '
 DC C'                      ppppp                                     '
 DC C'                      ppppp                                     '
 DC C'                      ppppp                                     '
APSLOGN  EQU   (*-APSLOGO)/L'APSLOGO  NUMBER OF LOGO LINES
         SPACE
         EJECT
***********************************************************************
*        DSECTS                                                       *
***********************************************************************
         SPACE
WORKAREA DSECT
         SPACE
JOBNAME  DS    CL8                 JOBNAME
ACF2NAME DS    CL8                 ACF2 LOGON ID
JESJOBID DS    CL8                 JES JOBLOG ID
PNODE    DS    CL8                 FORMATTED ROUTE/DESTINATION NODE
PDEVICE  DS    CL8                 PRINTER DEVICE
PROUTE   DS    CL8                 JOE ROUTE CODE
GROUP    DS    CL8                 JOE GROUP NAME
ONODE    DS    CL8                 JOB ORIGIN DEVICE/NODE
LINESTOT DS    CL8                 TOTAL LINES FOR JOE
LINESREM DS    CL8                 REMAINING LINES FOR JOE
PAGESTOT DS    CL8                 TOTAL PAGES FOR JOE
PAGESREM DS    CL8                 REMAINING PAGES FOR JOE
ROOMBLDG DS    CL4                 'ROOM' OR 'BLDG'
FORM     DS    CL8                 FORM NAME
ROOM     DS    CL4                 PROGRAMMER'S ROOM
PSYSTEM  DS    CL4                 PRINTER SYSTEM ID
FCB      DS    CL4                 FCB NAME
PGMRNAME DS    CL20                PROGRAMMER'S NAME
SYSOUTCL DS    CL1                 SYSOUT CLASS
         DS    CL2                 SPACE FOR EXTRA EDIT BYTES
JOEIDF   DS    CL7                 FORMATTED JOE ID1/ID2
WEEKDAY  DS    CL(L'DAYS)          DAY OF THE WEEK
DATE     DS    0CL7                ---
DATEDAY  DS    CL2                 - FORMATTED DATE
DATEMON  DS    CL3                 - LIKE DDMMMYY
DATEYR   DS    CL2                 ---
         DS    C                   SPACE FOR EDIT FILLER BYTE
TIME24   DS    CL8                 FORMATTED TIME - 24 HOUR CLOCK
         DS    C                   SPACE FOR EDIT FILLER BYTE
TIME12   DS    CL10                FORMATTED TIME - 12 HOUR CLOCK
PDATE    DS    0PL4                ---
         DS    X                   - PACKED DATE
PYEAR    DS    X                   - FROM TIME SVC
PDAY     DS    PL2                 ---
PDAYS    DS    0PL6                ACCUMULATOR
WEEKNO   DS    PL5                 WEEK NUMBER SINCE 1/1/1900
DAYNO    DS    PL1                 DAY-OF-THE-WEEK
SEPPAGES DS    0F                  HEADER/TRAILER PAGE COUNTS
HPAGES   DS    H                   HEADER PAGES
TPAGES   DS    H                   TRAILER PAGES
SEPLINES DS    H                   LINES PER SEPARATOR
LINECNT  DS    H                   LINES OF HEADER REMAINING
PAGECNT  DS    H                   PAGES OF HEADER REMAINING
PRPUTPRM DS    XL2                 SPECIAL PARAMETER FOR $PRPUT
SEPTYPE  DS    X                   START/CONT/END
         SPACE
WORKLEN  EQU   *-WORKAREA
         EJECT
INFOHEAD DSECT
         SPACE
INFOTXT1 DS    CL14                FIRST TEXT FIELD
INFOVAL1 DS    CL13                FIRST VALUE & SPACES
INFOTXT2 DS    CL14                SECOND TEXT
INFOVAL2 DS    CL27                SECOND VALUE & SPACES
INFOVAL3 EQU   INFOVAL2+6,L'DELIVER,C'C'  'REMOTE BLDG' BLOCK
INFOLOGO DS    CL(LOGOLEN)         LOGO AREA
         SPACE 3
DETLINE  DSECT
         SPACE
DETSDLM  DS    CL1,C               FRAME DELIMETER, BLANK
DETSTYPE DS    CL5,C               FORM TYPE START/CONT/END, BLANK
DETJNUM  DS    CL5,C               JOB NUMBER(SHORT FORM), BLANK
DETJNAME DS    CL8,C               JOB NAME, BLANK
DETONODE DS    CL8,C               JOB ORIGIN, BLANK
DETACF2N DS    CL8,C               ACF2 USER ID NAME, BLANK
DETPNAME DS    CL20,C              PROGRAMMER, BLANK
DETRBTXT DS    CL4,C               'ROOM', BLANK
DETROOM  DS    CL4,C               ROOM NUMBER, BLANK
DETTIME  DS    CL10,C              TIME AS HH:MM:SSAM, BLANK
DETDATE  DS    CL7,C               DATE AS DDMMMYY
DETPNODE DS    CL8,C               PRINTER NODE, BLANK
DETROUTE DS    CL8,C               ROUTE CODE, BLANK
DETPSYS  DS    CL4,C               PRINTER SYSTEM ID, BLANK
DETPDEV  DS    CL8,C               DEVICE, BLANK
DETCLASS DS    CL1,C               SYSOUT CLASS, BLANK
DETETYPE DS    CL5,C               FORM TYPE START/CONT/END,BLANK
DETEDLM  DS    CL1                 FRAME DELIMETER
         SPACE
DETLEN   EQU   *-DETLINE
         SPACE 2
***********************************************************************
*        JES EPILOGUE                                                 *
***********************************************************************
         SPACE 2
PRINTSEP CSECT
         PRINT NOGEN
         $MODEND
         SPACE 2
         END
