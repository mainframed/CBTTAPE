PRC      TITLE 'SID - THIS PGM RETURNS A SYSTEM ID INDICATOR IN R15'
********************************************************************
*                                                                  *
* NAME - SID.                                                      *
*                                                                  *
* DESCRIPTION - THIS UTILITY PROGRAM RETURNS A CODE IN R15 THAT    *
*   DENOTES THE SYSTEM IT IS RUNNING ON. THIS CAN BE USEFUL        *
*   FOR SITE OR CPU-DEPENDENT JCL OR CLISTS.                       *
*                                                                  *
* TO USE -                                                         *
*   // EXEC PGM=SID                                                *
*                                                                  *
*   FOR TESTING PURPOSES, THE SID CAN BE PASSED THRU THE PARM      *
*   FIELD TO OVERRIDE THE LIVE SID:                                *
*   // EXEC PGM=SID,PARM=APS2                                      *
*                                                                  *
* RETURN CODES -                                                   *
*   R15 = 1-? - INDICATES THE SYSTEM ID AS DIRECTED IN TABLE       *
*   R15 = 16  - SYSTEM ID NOT FOUND IN TABLE                       *
*   R15 = 20  - PARM FIELD INVALID (NOT 4 BYTES LONG)              *
*                                                                  *
* NOTES -                                                          *
*   THE SID TABLE (AT LABEL TABLE) IS INSTALLATION DEPENDENT.      *
*   WE NEED MACLIB AND AMODGEN TO ASSEMBLE PROPERLY.               *
*                                                                  *
* LOG -                                                            *
*   05/00/85 L01 PTW IMPLEMENTED                                   *L01
*   09/21/88 L02 PTW REDONE, FIXED S0C4 W/PLI NO-PARM CALL.        *L02
*                                                                  *
********************************************************************
         LCLC  &CSECT
&CSECT   SETC  'SID'
&CSECT   CSECT
********************************************************************
*                                                                  *
*        I N I T I A L I Z E                                       *
*                                                                  *
********************************************************************
         PRINT NOGEN
         SAVE  (14,12),,&CSECT-&SYSDATE-&SYSTIME
         LR    R12,R15            ESTABLISH BASE.
         USING &CSECT,R12         TELL ASSEMBLER.
         GETMAIN RU,LV=DYNLEN     GET DYNAMIC AREA.
         LR    R2,R13             SAVE AREA
         LR    R13,R1             SAVE AREA
         USING DYNAMIC,R13        TELL ASSEMBLER.
         LA    R0,DYNAMIC         ZERO
         L     R1,=A(DYNLEN)        DYNAMIC
         LR    R14,R0                 STORAGE.
         SLR   R15,R15
         MVCL  R0,R14             DO IT.
         ST    R2,4(,R13)         BACK CHAIN.
         ST    R13,8(,R2)         FORWARD CHAIN.
         MVC   RC,=F'16'          ASSUME BAD RC.
********************************************************************
*                                                                  *
*        P A R M   P R O C E S S I N G                             *
*                                                                  *
********************************************************************
         MVI   GOTPARM,NO         DEFAULT TO NO PARM.
         L     R2,4(,R13)         DIG FOR PARM.
         L     R2,24(,R2)         R2 = A(PARMLIST).
         LTR   R2,R2              WATCH OUT FOR NO PARM FROM
         BZ    ENDPARM              HI-LEVEL LANGUAGE.
         L     R2,0(,R2)          R2 = A(PARMLIST).
         LH    R3,0(,R2)          R3 = L'PARMLIST.
         LTR   R3,R3              IF PARM NOT THERE,
         BZ    ENDPARM              OK, TAKE DEFAULT.
         CH    R3,=H'4'          IF LENGTH Â¬= 4,
         BNE   BADPARM             BAD.
         MVC   OURSID,2(R2)      SAVE SID.
         MVI   GOTPARM,YES       INDICATE PARM PRESENT.
ENDPARM  DS    0H
********************************************************************
*                                                                  *
*        D O   T A B L E   L O O K U P                             *
*                                                                  *
********************************************************************
         CLI   GOTPARM,YES        IF PARM PRESENT,
         BE    USEPARM              USE IT. BYPASS NEXT.
         L     R2,CVTPTR          R2 = A(CVT).
         L     R2,CVTSMCA-CVT(,R2) DIG INTO SMCA FOR SID.
         MVC   OURSID,SMCASID-SMCABASE(R2)
USEPARM  DS    0H
         LA    R2,TABLE           DO TABLE LOOKUP.
         USING SIDTAB,R2
LOOP     DS    0H
         CLI   SIDNAME,X'FF'      IF WE HAVE STOPPER,
         BE    RETURN               THEN SID NOT FOUND.
         CLC   OURSID,SIDNAME     IF WE HAVE A MATCH,
         BE    GOTSID               THEN USE IT.
         LA    R2,SIDLEN(,R2)     ELSE BUMP TO NEXT ENTRY.
         B     LOOP
GOTSID   DS    0H
         MVC   RC,SIDRC           USE THE RETURN CODE IN TABLE.
         DROP  R2
********************************************************************
*                                                                  *
*        R E T U R N                                               *
*                                                                  *
********************************************************************
RETURN   DS    0H
         L     R2,RC              R2 = RC.
         LR    R1,R13             PREPARE
         L     R13,4(,R13)          FOR FREEMAIN.
         FREEMAIN RU,LV=DYNLEN,A=(1)
         LR    R15,R2               AND RETURN
         RETURN (14,12),RC=(15)      WITH R15 = RC.
********************************************************************
*                                                                  *
*        E R R O R S                                               *
*                                                                  *
********************************************************************
BADPARM  DS    0H                 BAD PARM VALUE PASSED TO US.
         MVC   RC,=F'20'
         B     RETURN
********************************************************************
*                                                                  *
*        S T A T I C   A R E A S.                                  *
*                                                                  *
********************************************************************
*
*   ===> INSTALLATION DEPENDENT TABLE <===
*
TABLE    DC    CL4'APS1',F'1'    MAPPED BY SIDTAB DSECT
         DC    CL4'APS2',F'2'
         DC    CL4'APS3',F'3'
         DC    CL4'APS4',F'4'
         DC    CL4'    ',F'0'
         DC    CL4'    ',F'0',X'FF'
*
SRCLEVEL DC    C'                                        '
         LTORG *
********************************************************************
*                                                                  *
*        D Y N A M I C   A R E A S.                                *
*                                                                  *
********************************************************************
DYNAMIC  DSECT
SAVE     DS    18F                SAVE AREA.
RC       DS    F                  RETURN CODE.
OURSID   DS    CL4                OUR SYSTEM ID.
GOTPARM  DS    X                  PARM PRESENT?
YES      EQU   1
NO       EQU   2
DYNLEN   EQU   *-DYNAMIC
*
SIDTAB   DSECT                    DESCRIBES TABLE.
SIDNAME  DS    CL4
SIDRC    DS    F
SIDLEN   EQU   *-SIDTAB
*
         CVT   DSECT=YES
         IEESMCA
*
         PRINT OFF
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         PRINT ON,NOGEN
         END   &CSECT
