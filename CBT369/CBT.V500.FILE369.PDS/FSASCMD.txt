PROC 0  LOADLIB('APS1.SAS.LIBRARY')
  CONTROL NOLIST NOCONLIST MSG NOFLUSH NOPROMPT END(END)
  ISPEXEC CONTROL ERRORS RETURN
  ISPEXEC VGET (TYPE,QUAL,ERRSM,ERRLM,ERRMSG) SHARED
  ISPEXEC VGET (SASPGM,SASOPT,SASPDEST,+
                SASDD1,SASDD2,SASDD3,+
                SASDSN1,SASDSN2,SASDSN3,+
                SASDISP1,SASDISP2,SASDISP3,+
                SASP1,SASP2,SASP3) PROFILE
  SET &ZERRSM  = &STR()
  SET &ZERRLM  = &STR()
  ISPEXEC VPUT (ZERRSM,ZERRLM) SHARED
  SET &ERRMSG = &STR()
  SET &ERRSM  = &STR()
  SET &ERRLM  = &STR()
/* PROCESS A CHANGE PROGRAM.
  IF &TYPE = C OR &TYPE = E THEN +
    DO
      SET &SASDSN =
      SET &SASMEM =
      ISPEXEC VPUT (SASDSN,SASMEM) SHARED
      ISPEXEC EDIT DATASET(&SASPGM) MACRO(FSASEMAC)
      SET &DSNAME = &SASPGM
      ISPEXEC VPUT (DSNAME) SHARED
      ISPEXEC VGET (SASDSN,SASMEM) SHARED
      SET &MEMBER = &SASMEM
      SET &ERRSM  = &ZERRSM
      SET &ERRLM  = &ZERRLM
      SET &A = &LENGTH(&DSNAME)
      SET &B = &SUBSTR(&A,&DSNAME)
      IF &B = ' THEN SET &B = &SUBSTR(&A-1,&DSNAME)
      IF &MEMBER ¬= AND &B ¬= ) THEN +
        DO
          SET &A = &LENGTH(&DSNAME)
          SET &B = &SUBSTR(&A,&DSNAME)
          IF  &B ¬= ' THEN SET &SASPGM = &STR(&DSNAME(&MEMBER)
          ELSE +
            DO
              SET &C =
              SET &C = &SUBSTR(2:&A-1,&DSNAME)
              SET &SASPGM = &STR('&C(&MEMBER)')
            END
        END
      ISPEXEC VPUT (SASPGM) PROFILE
    END
/* PROCESS A RUN PROGRAM.
  IF &TYPE = R THEN +
    DO
       SET &PURGE = NO
       SET &DSNAME = &SASPGM
       ISPEXEC VPUT (DSNAME) SHARED
       SET &SYSOUTTRAP = 10
       CONTROL NOLIST NOCONLIST
       LISTD &DSNAME
       SET &RC = &LASTCC
     /*CONTROL   LIST   CONLIST
       SET &SYSOUTTRAP = 0
       IF &SYSOUTLINE = 5 AND &RC = 0 THEN +
         DO
           SET &SYSDVAL = &SYSOUTLINE3
           READDVAL  &A &B &C &D
           IF &D = PO THEN +
             DO
               SET &PURGE = YES
               SELMEMBR
               SET &RC = &LASTCC
               IF &RC = 0 THEN +
                 DO
                   ISPEXEC VGET (MEMBER) SHARED
                   SET &MEMBER = &MEMBER
                   SET &A = &LENGTH(&DSNAME)
                   SET &B = &SUBSTR(&A,&DSNAME)
                   IF  &B ¬= ' THEN SET &SASPGM = &STR(&DSNAME(&MEMBER)
                   ELSE +
                     DO
                       SET &C =
                       SET &C = &SUBSTR(2:&A-1,&DSNAME)
                       SET &SASPGM = &STR('&C(&MEMBER)')
                     END
                   ISPEXEC VPUT (SASPGM) PROFILE
                   SET &PURGE = NO
                 END
             END
         END
      IF &PURGE = NO THEN +
        DO
          CONTROL NOMSG
          FREE F(SASPGM,FT11F001,FT12F001)
          CONTROL   MSG
          SET &ALLOC = &SYSDSN(&SASPGM)
          IF  &ALLOC = &STR(OK) THEN +
            DO
              CONTROL NOMSG
              ALLOC DA(&QUAL..PRINTTO,&SASPGM) F(SASPGM) SHR
              SET &RC = &LASTCC
              CONTROL   MSG
              IF &RC = 0 THEN +
                DO
                  ISPEXEC CONTROL DISPLAY LINE START(1)
                  ALLOC F(FT11F001) DA(&QUAL..LOG) SHR
                  ALLOC F(FT12F001) DA(&QUAL..LIST) USING(SASP) SHR
                  WRITE ***********************************
                  WRITE * SAS PROGRAM BEGINNING EXECUTION
                  WRITE ***********************************
                  TIME
    CALL '&LOADLIB(SAS)' +
      '&SASOPT,S=72,SYSIN=SASPGM,LOG=FT11F001'
                  SET &RC = &LASTCC
                  TIME
                  WRITE *****************************************
                  WRITE * SAS PROGRAM COMPLETED, RC = &RC..
                  WRITE * HIT "ENTER" TO BROWSE THE LOG OUTPUT.
                  WRITE *****************************************
                  SET &ERRMSG = &STR(FSAS000)
                  ISPEXEC BROWSE DATASET(&QUAL..LOG)
                  SET &ERRSM  = &ZERRSM
                  SET &ERRLM  = &ZERRLM
                END
              ELSE +
                SET &ERRMSG = &STR(FSAS001)
            END
          ELSE +
            DO
              IF &ALLOC = &STR(DATASET NOT FOUND) THEN +
                SET &ERRMSG = &STR(FSAS002)
              ELSE +
                IF &ALLOC = &STR(MEMBER NOT FOUND) THEN +
                  SET &ERRMSG = &STR(FSAS003)
                ELSE +
                  SET &ERRMSG = &STR(FSAS004)
            END
        END
    END
/* PROCESS A BROWSE LOG.
  IF &TYPE = 1 THEN +
    DO
      ISPEXEC BROWSE DATASET(&QUAL..LOG)
      SET &ERRSM  = &ZERRSM
      SET &ERRLM  = &ZERRLM
    END
/* PROCESS A BROWSE LIST.
  IF &TYPE = 2 THEN +
    DO
      ISPEXEC BROWSE DATASET(&QUAL..LIST)
      SET &ERRSM  = &ZERRSM
      SET &ERRLM  = &ZERRLM
    END
/* PROCESS A CALL SAS INTERACTIVE.
  IF &TYPE = I THEN +
    DO
      ISPEXEC CONTROL DISPLAY LINE START(1)
      CONTROL NOMSG
      FREE F(SYSIN,FT11F001,FT12F001)
      CONTROL   MSG
      ALLOC F(SYSIN) DA(*)
      ALLOC F(FT11F001) DA(*)
      ALLOC F(FT12F001) DA(*)
      CALL '&LOADLIB(SAS)' '&SASOPT'
      SET &RC = &LASTCC
      SET &ERRMSG = &STR(FSAS000)
    END
/* PROCESS A PRINT.
  IF &TYPE = P THEN +
    DO
      SET &SYSOUTTRAP = 20
      SET &RC1 = &STR()
      SET &RC2 = &STR()
      SET &RC3 = &STR()
      IF &SASP1 NE &STR() THEN +
        DO
          PRINTOFF &QUAL..LOG DEST(&SASPDEST)
          SET &RC1 = &LASTCC
          IF &RC1 ¬= 0 THEN SET &ERRLM = &STR(&SYSOUTLINE1)
          ELSE SET &ERRLM = &STR(YOUR PRINT OUTPUT HAS BEEN SCHEDULED)
        END
      IF &SASP2 NE &STR() THEN +
        DO
          PRINTOFF &QUAL..LIST DEST(&SASPDEST)
          SET &RC2 = &LASTCC
          IF &RC2 ¬= 0 THEN SET &ERRLM = &STR(&SYSOUTLINE1)
          ELSE SET &ERRLM = &STR(YOUR PRINT OUTPUT HAS BEEN SCHEDULED)
        END
      IF &SASP3 NE &STR() THEN +
        DO
          PRINTOFF &SASPGM DEST(&SASPDEST)
          SET &RC3 = &LASTCC
          IF &RC3 ¬= 0 THEN SET &ERRLM = &STR(&SYSOUTLINE1)
          ELSE SET &ERRLM = &STR(YOUR PRINT OUTPUT HAS BEEN SCHEDULED)
        END
      SET &RC = &RC1
      IF &RC2 > &RC THEN SET &RC = &RC2
      IF &RC3 > &RC THEN SET &RC = &RC3
      SET &ERRMSG = &STR(FSAS000)
      SET &SYSOUTTRAP = 0
    END
/* PROCESS AN ALLOCATE.
  IF &TYPE = A THEN +
    DO
      SET &SYSOUTTRAP = 20
      SET &RC1 = &STR()
      SET &RC2 = &STR()
      SET &RC3 = &STR()
      IF &SASDD1 NE &STR() THEN +
        DO
          SET &SYSOUTTRAP = 10
          ALLOC F(&SASDD1) DA(&SASDSN1) &SASDISP1
          SET &RC1 = &LASTCC
          IF &RC1 ¬= 0 THEN SET &ERRLM = &STR(&SYSOUTLINE1)
          ELSE SET &ERRLM = &STR(YOUR FILE WAS ALLOCATED OK)
        END
      IF &SASDD2 NE &STR() THEN +
        DO
          SET &SYSOUTTRAP = 10
          ALLOC F(&SASDD2) DA(&SASDSN2) &SASDISP2
          SET &RC2 = &LASTCC
          IF &RC2 ¬= 0 THEN SET &ERRLM = &STR(&SYSOUTLINE1)
          ELSE SET &ERRLM = &STR(YOUR FILE WAS ALLOCATED OK)
        END
      IF &SASDD3 NE &STR() THEN +
        DO
          SET &SYSOUTTRAP = 10
          ALLOC F(&SASDD3) DA(&SASDSN3) &SASDISP3
          SET &RC3 = &LASTCC
          IF &RC3 ¬= 0 THEN SET &ERRLM = &STR(&SYSOUTLINE1)
          ELSE SET &ERRLM = &STR(YOUR FILE WAS ALLOCATED OK)
        END
      SET &RC = &RC1
      IF &RC2 > &RC THEN SET &RC = &RC2
      IF &RC3 > &RC THEN SET &RC = &RC3
      SET &ERRMSG = &STR(FSAS000)
      SET &SYSOUTTRAP = 20
    END
/* PROCESS A FREE.
  IF &TYPE = F THEN +
    DO
      SET &SYSOUTTRAP = 5
      SET &RC1 = &STR()
      SET &RC2 = &STR()
      SET &RC3 = &STR()
      IF &SASDD1 NE &STR() THEN +
        DO
          FREE F(&SASDD1)
          SET &RC1 = &LASTCC
          IF &RC1 ¬= 0 THEN SET &ERRLM = &STR(&SYSOUTLINE1)
          ELSE SET &ERRLM = &STR(YOUR FILE WAS FREED SUCCESSFULLY)
        END
      IF &SASDD2 NE &STR() THEN +
        DO
          FREE F(&SASDD2)
          SET &RC2 = &LASTCC
          IF &RC2 ¬= 0 THEN SET &ERRLM = &STR(&SYSOUTLINE1)
          ELSE SET &ERRLM = &STR(YOUR FILE WAS FREED SUCCESSFULLY)
        END
      IF &SASDD3 NE &STR() THEN +
        DO
          FREE F(&SASDD3)
          SET &RC3 = &LASTCC
          IF &RC3 ¬= 0 THEN SET &ERRLM = &STR(&SYSOUTLINE1)
          ELSE SET &ERRLM = &STR(YOUR FILE WAS FREED SUCCESSFULLY)
        END
      SET &RC = &RC1
      IF &RC2 > &RC THEN SET &RC = &RC2
      IF &RC3 > &RC THEN SET &RC = &RC3
      SET &ERRMSG = &STR(FSAS000)
    END
  ISPEXEC VPUT (ERRMSG,ERRSM,ERRLM,RC) SHARED
  EXIT CODE(0)
END
