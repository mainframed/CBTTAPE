   PROC 0 LIST
 /********************************************************************/
 /*                                                                  */
 /* SOURCE LEVEL SELMEMBR V01.M16 85/11/27 07:51 PDVHEM              */
 /*                                                                  */
 /* NAME         SELMEMBR                                            */
 /*                                                                  */
 /* COMPONENT    UTILITY                                             */
 /*                                                                  */
 /* FUNCTION     DISPLAY A MEMBER LIST USING TABLE SERVICES          */
 /*              AND PERMIT A USER TO SELECT A MEMBER.               */
 /*                                                                  */
 /* ENTRY        VIA ISPEXEC SELECT OR CLIST INVOCATION.             */
 /*                                                                  */
 /* INPUT        DSNAME OF A PDS IN SHARED POOL VARIABLE DSNAME.     */
 /*                                                                  */
 /* OUTPUT       MEMBER NAME IN SHARED POOL VARIABLE MEMBER.         */
 /*              RETURN CODE.                                        */
 /*                                                                  */
 /* PARAMETERS   LIST - ENABLE DEBUGGING VIA CONTROL LIST.           */
 /*                                                                  */
 /* EXT REF      NONE.                                               */
 /*                                                                  */
 /* ISPF SERVICE CONTROL                                             */
 /*              LMCLOSE                                             */
 /*              LMFREE                                              */
 /*              LMINIT                                              */
 /*              LMMLIST                                             */
 /*              LMOPEN                                              */
 /*              TBADD                                               */
 /*              TBCREATE                                            */
 /*              TBDISPL                                             */
 /*              TBEND                                               */
 /*              TBTOP                                               */
 /*              VGET                                                */
 /*              VPUT                                                */
 /*                                                                  */
 /* MESSAGES     ISRM007                                             */
 /*                                                                  */
 /* PANELS       SELMEMP1 - TABLE DISPLAY                            */
 /*              SELMEMH0 - HELP                                     */
 /*              SELMEMH1 - HELP                                     */
 /*              SELMEMH2 - HELP                                     */
 /*                                                                  */
 /* PROGRAMS     NONE                                                */
 /*                                                                  */
 /* CLISTS       NONE                                                */
 /*                                                                  */
 /* OPERATION    THE PDS IS PREPARED BY ISSUING LMINIT AND LMOPEN.   */
 /*              THE TABLE 'MEMTABLE' IS INITIALIZED WITH TBCREATE.  */
 /*              LMMLIST AND TBADD ARE ISSUED TO ADD ONE ROW TO      */
 /*              THE TABLE FOR EACH MEMBER. THE PDS IS THEN CLOSED   */
 /*              WITH LMCLOSE AND LMFREE. TBDISPL IS USED TO SHOW    */
 /*              THE TABLE AND ACCEPT A SELECTED MEMBER. IF          */
 /*              END IS SIGNALED, THE CLIST RETURNS WITH NO MEMBER   */
 /*              AND RETURN CODE 8.                                  */
 /*                                                                  */
 /* RETURN CODE  0 MEMBER NAME RETURNED                              */
 /*              8 END COMMAND                                       */
 /*                                                                  */
 /* RESTRICTIONS NONE.                                               */
 /*                                                                  */
 /* NOTES        NONE.                                               */
 /*                                                                  */
 /* PROGRAMMER   C. J. PITTS                                         */
 /*                                                                  */
 /* SOURCE LEVEL |                                                   */
 /*                                                                  */
 /* MAINTENANCE  WHO  DATE   DESCRIPTION                             */
 /*                                                                  */
 /********************************************************************/

  IF &LIST=LIST     +
    THEN CONTROL LIST   MSG
    ELSE CONTROL NOLIST MSG

  ISPEXEC CONTROL ERRORS CANCEL

  ISPEXEC VGET (DSNAME) SHARED

  ISPEXEC LMINIT DATAID(DSID) DATASET(&DSNAME)
  ISPEXEC LMOPEN DATAID(&DSID)
  ISPEXEC TBCREATE MEMTABLE REPLACE NOWRITE KEYS(MEMBER) NAMES(STATS)

  SET &LMLRC = 0
  DO WHILE &LMLRC = 0
    ISPEXEC LMMLIST DATAID(&DSID) MEMBER(MEMBER) +
                    OPTION(LIST) STATS(YES)
    SET &LMLRC = &LASTCC
    IF &LMLRC = 0   +
      THEN DO
             SET &STATS = &STR(&ZLVERS..&ZLMOD  &ZLCDATE +
                               &ZLMDATE &ZLMTIME &ZLCNORC +
                               &ZLINORC &ZLMNORC &ZLUSER)
             ISPEXEC TBADD MEMTABLE
           END
  END

  ISPEXEC LMMLIST DATAID(&DSID) OPTION(FREE)
  ISPEXEC LMCLOSE DATAID(&DSID)
  ISPEXEC LMFREE  DATAID(&DSID)

  ISPEXEC TBTOP  MEMTABLE
  ISPEXEC TBDISPL MEMTABLE PANEL(SELMEMP1) POSITION(ROW)
  SET &TBDRC = &LASTCC

  SET &DONE = NO
  DO WHILE &DONE = NO
    IF      &TBDRC>4       +
        THEN SET &DONE = YES
      ELSE IF &S = S       +
        THEN DO
               SET &TBDRC = 0
               SET &DONE  = YES
             END
      ELSE IF &S = &STR( )   +
        THEN DO
               ISPEXEC TBDISPL MEMTABLE POSITION(ROW)
               SET &TBDRC = &LASTCC
             END
      ELSE DO
             ISPEXEC TBDISPL MEMTABLE MSG(ISRM007) POSITION(ROW)  +
                                      CSRROW(&ROW)
             SET &TBDRC = &LASTCC
           END
  END

  ISPEXEC TBEND  MEMTABLE
  ISPEXEC VPUT (MEMBER) SHARED
  EXIT CODE(&TBDRC)
