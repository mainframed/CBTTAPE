         TITLE 'SITEID - SET RETURN CODE BASED ON JES2 SPOOL NODE NAME'
***********************************************************************
*                                                                     *
* NAME         SITEID                                                 *
*                                                                     *
* COMPONENT    SYSTEM UTILITY                                         *
*                                                                     *
* FUNCTION     SET RETURN CODE TO INDICATE EXEC SITE                  *
*                                                                     *
* ENTRY        STANDARD LINKAGE                                       *
*                                                                     *
* PARAMETERS   NONE                                                   *
*                                                                     *
* INPUT        NONE                                                   *
*                                                                     *
* OUTPUT       NONE                                                   *
*                                                                     *
* RETURN CODES 1 TO 15   SPECIFIC SITE IDENTIFIED (SEE LABEL SITETBL) *
*              16        UNABLE TO IDENTIFY SITE                      *
*              20        UNABLE TO LOCATE JCT                         *
*                                                                     *
* FILES        NONE                                                   *
*                                                                     *
* EXT REF      NONE                                                   *
*                                                                     *
* EXITS        NONE                                                   *
*                                                                     *
* MESSAGES     NONE                                                   *
*                                                                     *
* MACROS       RETURN                                                 *
*              SAVE                                                   *
*                                                                     *
* TABLES       JCT                                                    *
*              JSCB                                                   *
*              PSA                                                    *
*              SJB                                                    *
*              SSIB                                                   *
*              TCB                                                    *
*                                                                     *
* OPERATION    |                                                      *
*                                                                     *
* ATTRIBUTES   REENTRANT                                              *
*              REUSABLE                                               *
*              PROBLEM PROGRAM STATE                                  *
*              PROBLEM PROGRAM KEY                                    *
*              NOT AUTHORIZED                                         *
*              AMODE ANY                                              *
*              RMODE ANY                                              *
*                                                                     *
* RESTRICTIONS |                                                      *
*                                                                     *
* NOTES        NO CALLS OR SVC'S ARE USED, SO THIS MODULE PROVIDES    *
*              NO SAVE AREA. NO WORKAREA IS NEEDED SO IT IS ALSO      *
*              REENTRANT ANAD RESUABLE.                               *
*                                                                     *
*              THE PROCEDURE FOR LOCATING THE JCT IS DOCUMENTED       *
*              IN WSC FLASH 8118 AND UPDATED BY INFO/MVS UNIT         *
*              Q364737.                                               *
*                                                                     *
*              THE TABLE OF SITE ID'S AT LABEL SITETBL HAS A FEW      *
*              UNUSED ENTRIES FOR ZAPS OR OTHER FAST-AND-DIRTY        *
*              MAINTENANCE.                                           *
*                                                                     *
*              A LOT OF EXTRA JES2 DSECTS ARE NEEDED TO ALLOW THE     *
*              JCT TO ASSEMBLE WITHOUT ERRORS.                        *
*                                                                     *
* AUTHOR       C. J. PITTS                                            *
*                                                                     *
* MAINTENANCE  WHO    DATE     DESCRIPTION                            *
*                                                                     *
*                                                                     *
**********************************************************************/
*                                                                     *
* REGISTERS AT ENTRY                                                  *
*              R13   SAVE AREA                                        *
*              R14   RETURN ADDRESS                                   *
*              R15   ENTRY POINT                                      *
*                                                                     *
* REGISTER USE IN PROGRAM                                             *
*              R9    JCT BASE REGISTER                                *
*              R12   BASE REGISTER                                    *
*              R15   RETURN CODE                                      *
*                                                                     *
* REGISTERS AT EXIT                                                   *
*              R15   RETURN CODE                                      *
*                                                                     *
**********************************************************************/
         SPACE 3
SITEID   CSECT
         SPACE
SITEID   AMODE ANY
SITEID   RMODE ANY
         SPACE
         USING SITEID,R15          TEMPORARY BASE
         B     BEGIN               SKIP OVER DOCUMENTATION
         DC    AL1(BEGIN-*)        LENGTH OF TEXT
SRCLEVEL DC    C'SITEID   VNN.MNN YY/MM/DD HH:MM UUUUUUUU'
*                MODULE   VER/MOD   DATE   TIME  USER NAME
COPYRIGT DC    C' COPYRIGHT (C) 1988 PLANNING RESEARCH CORPORATION '
ASMSTAMP DC    C'ASM &SYSDATE &SYSTIME'
         DROP  R15                 DONE WITH TEMPORARY BASE
BEGIN    DS    0H
         EJECT
***********************************************************************
*                                                                     *
* SITE1000:                                                           *
*        SAVE CALLER'S REGISTERS;                                     *
*        SET BASE REGISTER;                                           *
*        SET DEFAULT RETURN CODE FOR JCT NOT LOCATED;                 *
*        LOCATE SSIB: PSA->TCB->JSCB->SSIB;                           *
*        IF SSIB INDICATES MASTER SCHEDULER                           *
*          THEN RETURN WITH BAD RETURN CODE;                          *
*        LOCATE FIRST AND SECOND SJB: SSIB->SJB->SJB;                 *
*        IF SECOND SSIB DOES NOT EXIST (EG, FOR TSU)                  *
*          THEN USE THE FIRST SJB;                                    *
*        LOCATE JCT: SJB->JCT;                                        *
*        IF JCT EYECATCHER IS NOT VALID                               *
*          THEN RETURN WITH BAD RETURN CODE;                          *
*                                                                     *
***********************************************************************
         SPACE
SITE1000 DS    0H
         SAVE  (14,12)
         SPACE
         LR    R12,R15             BASE REGISTER
         USING SITEID,R12
         SPACE
         LA    R15,NOGOTJCT        SET DEFAULT RETURN CODE
         USING PSA,0
         L     R1,PSATOLD          CURRENT (OLD) TCB
         USING TCB,R1
         XR    R2,R2               CLEAR HIGH BYTE
         ICM   R2,B'0111',TCBJSCBB JSCB (24 BIT)
         USING IEZJSCB,R2
         L     R1,JSCBSSIB         SSIB
         USING SSIB,R1
         SPACE
         CLC   SSIBSSNM,=CL4'MSTR' RUNNING UNDER MASTER SCHEDULER?
         BE    SITE9000            YEP...GO EXIT
         SPACE
         L     R1,SSIBSUSE         INIT, STC OR TSU SJB POINTER
         USING SJB,R1
         ICM   R0,B'1111',SJBSJB   NEXT SJB (JOB UNDER INIT)
         BZ    SITE1900            NOT THERE...USE THE FIRST ONE
         LR    R1,R0               USE THE SECOND ONE
SITE1900 DS    0H
         SPACE
         L     R9,SJBJCT           JCT (FINIALLY)
         USING JCT,R9
         CLC   JCTID,=CL4'JCT'     EYECATCHER MATCH?
         BNE   SITE9000            NOPE...GO EXIT
         SPACE
         DROP  R1,R2
         EJECT
***********************************************************************
*                                                                     *
* SITE3000:                            /* LOOK UP EXECUTION SITE  */  *
*        SET DEFAULT RETURN CODE - UNKNOWN SITE;                      *
*        LOOK UP EXECUTION SITE NAME IN TABLE;                        *
*        IF FOUND                                                     *
*          THEN SET RETURN CODE TO TABLE INDEX VALUE;                 *
*                                                                     *
***********************************************************************
         SPACE
SITE3000 DS    0H
         LA    R15,NOGOTSIT        SET RETURN CODE - UNKNOWN SITE
         SPACE
         LA    R1,MAXSITES         LOOP COUNTER
         LA    R2,SITETBL          FIRST ENTRY
         SPACE
SITE3200 DS    0H
         CLC   =CL8' ',0(R2)       TABLE ENTRY USED?
         BE    SITE3600            NOPE...NEXT ENTRY
         CLC   JCTNXNDE,0(R2)      EXECUTION NODE TABLE ENTRY?
         BE    SITE3800            YEP...GO SET RETURN CODE
         SPACE
SITE3600 DS    0H
         LA    R2,L'SITETBL(,R2)   NEXT ENTRY
         BCT   R1,SITE3200         LOOP
         B     SITE3900            NOT FOUND IN TABLE...ONWARD
         SPACE
SITE3800 DS    0H
         LA    R15,MAXSITES        LAST TABLE INDEX
         SR    R15,R1              COMPUTE RETURN CODE
         SPACE
SITE3900 DS    0H
         SPACE 2
***********************************************************************
*                                                                     *
* SITE9000:                      /*  RETURN WITH R15 RETURN CODE  */  *
*        RETURN TO CALLER WITH RETURN CODE;                           *
*                                                                     *
***********************************************************************
         SPACE
SITE9000 DS    0H
         RETURN (14,12),RC=(15)
         EJECT
***********************************************************************
*        CONSTANTS AND LITERALS                                       *
***********************************************************************
         SPACE
         DS    0D
SITETBL  DS    0CL8                LIST OF SITES
         DC    CL8' '                0  -  UNUSED
         DC    CL8'APS1'             1  -  DULLES SDL
         DC    CL8' '                2  -  UNUSED
         DC    CL8'CPK2'             3  -  CRYSTAL PARK 2  PTO
         DC    CL8' '                4  -  UNUSED
         DC    CL8' '                5  -  UNUSED
         SPACE
MAXSITES EQU   (*-SITETBL)/L'SITETBL
         SPACE
NOGOTSIT EQU   16                  RETURN CODE - UNIDENTIFIED SITE
NOGOTJCT EQU   20                  RETURN CODE - UNABLE TO LOCATE JCT
         SPACE
         LTORG
         SPACE 2
***********************************************************************
*        DSECTS                                                       *
***********************************************************************
         SPACE
         PRINT NOGEN
         $BUFFER
         $HASPEQU
         $JCT
         $SJB
         $TQE
         IEFJSSIB
         IEZJSCB
         IFGRPL
         IHAPSA
         IKJTCB
         SPACE 3
         END   SITEID
