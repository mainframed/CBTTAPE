        /* DATA SET ASMTOZAP   AT LEVEL 001 AS OF 10/11/78    */
 ASZAP: PROC OPTIONS(MAIN) REORDER;                                     00001
  /* THIS IS A UTILITY WHICH CONVERTS AN ASSEMBLER LISTING              00002
     DATASET INTO A FORMATTED IMASPZAP INPUT DECK OR SMP PTF. IT        00003
     IS USEFUL IN PREPARING, MAINTAINING, AND DOCUMENTING THOSE         00004
     INSTALLATION MODIFICATIONS WHICH CANNOT BE INSTALLED EXECPT        00005
     BY MODIFING IBM CODE. ONE CAN MAKE FREE USE OF MACROS,             00006
     LITERALS, AND OTHER CONVENIENCE FEATURES IN PREPARING THE          00007
     DECK. CONTROL CARDS IN SPECIAL ASSEMBLER COMMENT FORM              00008
     PROVIDE FLEXABILITY IN MODIFICATION DESIGN AND CONTROL             00009
     OVER OUTPUT.                                                       00010
        ALL CONTROL CARDS ARE INCLUDED IN THE ASSEMBLER DECK            00011
     BEGINNING WITH THE CHARACTERS '*ZAP'. THE PURPOSE OF THE           00012
     CONTROL CARDS IS TO INTERRUPT AND RESUME GENERATION OF             00013
     ZAP OUTPUT. STATEMENTS WHICH DO NOT GENERATE OBJECT CODE           00014
     (COMMENTS,ORG,DS,EQU,ETC.) ARE TRANSFERRED TO THE OUTPUT AS        00015
     ZAP COMMENT CARDS, UNLESS OUTPUT GENERATION IS CURRENTLY           00016
     PAUSED. OTHER STATEMENTS (INSTRUCTIONS,DC,CCW,ETC.) ARE            00017
     TRANSFERRED TO THE OUTPUT AS 'VER' OR 'REP' CARDS IF               00018
     OUTPUT GENERATION HAS BEEN (RE)STARTED IN ONE OF THESE             00019
     TWO MODES. THE CARDS GOVERNING THIS ARE                            00020
          *ZAP START REP                                                00021
          *ZAP START VER                                                00022
          *ZAP PAUSE                                                    00023
     ZAP GENERATION IS INITIALLY PAUSED. A '*ZAP START' CARD IS         00024
     VALID AND (RE)STARTS GENERATION IN REP OR VER MODE, WHICHEVER      00025
     WAS LAST ACTIVE, BUT IF IT IS THE FIRST START CARD IN THE          00026
     DECK, IT CANNOT BE FOLLOWED BY OBJECT GENERATING STATEMENTS        00027
     UNTIL MODE IS SPECIFIED ON A SUBSEQUENT '*ZAP START (VER/REP)'     00028
     CARD.                                                              00029
        WHEN THE ZAP IS DONE, A '*ZAP END' CARD WILL TERMINATE          00030
     A SCAN OF THE LISTING AND CLOSE THE OUTPUT FILE. THIS NOT          00031
     ONLY SAVES A FEW EXTRA MICROSECONDS OF CPU WHICH WOULD BE          00032
     SPENT SCANNING THE MAPPING MACROS AND CROSS REFRENCE, BUT          00033
     HELPS THE HUMAN READING THE ASSEMBLER TO KNOW WHERE THE            00034
     ESSENTIAL CODE HAS STOPPED AND THE SUPPORT DEFINITIONS             00035
     HAVE BEGUN. IT TAKES A LOT OF ZAP COMMENT CARDS TO DEFINE          00036
     THE CVT DSECT.                                                     00037
        THE '*ZAP CARD' CONTROL CARD ALLOWS ARBITRARY TEXT TO           00038
     BE REPRODUCED INT THE OUTPUT. THE CHARACTERS UP TO THE             00039
     SPACE AFTER 'CARD' ARE STRIPPED OFF AND THE REMAINDER OF           00040
     THE CARD TO COLUMN 72 IS REPRODUCED IN THE OUTPUT DECK.            00041
     THIS IS PRIMARILY FOR NAME,DUMP, AND SMP CONTROL CARDS.            00042
          *ZAP CARD ++PTF(YU00001) /* TYPICAL PTF DECK * /.             00043
          *ZAP CARD ++ZAP(IEXAMPLE).                                    00044
          *ZAP CARD   EXPAND IEXAMPLE(50)                               00045
          *ZAP CARD   NAME IEXAMPLE                                     00046
          *ZAP START VER                                                00047
     RESTRICTIONS:                                                      00048
          YOU MUST SPECIFY 'PRINT DATA' IN THE ASSEMBLY IF YOU          00049
             GENERATE CONSTANTS OR LITERAL IN EXCESS OF 8 BYTES         00050
             LENGTH.                                                    00051
          REMEMBER TO PUT THE 'LTORG' STATEMENT BEFORE THE              00052
            '*ZAP END' CARD IF LITERALS ARE GENERATED.                  00053
          NO RELOCATABLE ADDRESS CONSTANTS CAN BE CODED IN THE          00054
            ZAP CARD.                                                   00055
     IT IS NOT POSSIBLE AT THIS TIME TO DETECT VIOLATIONS OF THE        00056
      RESTRICTIONS LISTED ABOVE. BE CAREFUL.                            00057
     GOOD TECHNIQUE:                                                    00058
        PUT ALL SENSITIVE ADDRESSES AT THE BEGINNING OF THE DECK AS     00059
           EQU INSTRUCTIONS. THEY CAN BE EASILY MODIFIED WHEN HIT BY    00060
           PTFS OR WHEN THE NEXT RELEASE IS DISTRIBUTED. CANDIDATES     00061
           INCLUDE THE ZAPPED OVER INSTRUCTION, THE PATCH AREA, AND     00062
           TARGETS OF CONDITIONAL BRANCH INSTRUCTIONS OTHER THAN THE    00063
           NATURAL RETURN.                                              00064
        IT IS SOMETIMES POSSIBLE TO IMBED THE ZAP INSTRUCTIONS IN       00065
           ENOUGH SUPPORT CODE TO MAKE THE OBJECT DECK OUTPUT OF        00066
           THE ASSEMBLY AN INDEPENDENTLY EXECUTABLE PROGRAM WHICH       00067
           YOU CAN TSO TEST BEFORE INCORPORATING INTO THE SYSTEM.       00068
           THIS SAVES A LITTLE SYSTEM TEST TIME TRACKING DOWN DATA      00069
           EXCEPTIONS AND THE LIKE.                                     00070
                                                                        00071
     COMMENTS AND SUGGESTIONS SHOULD BE ADDRESSED TO                    00072
          HOWARD GILBERT                                                00073
          YALE COMPUTER CENTER                                          00074
          175 WHITNEY AVE                                               00075
          NEW HAVEN, CONN. 06520                                        00076
                                                                        00077
     DDNAMES USED: IN,OUT,SYSPRINT                                      00078
  */                                                                    00079
  DCL IN INPUT FILE RECORD /* DS CONTAINING SYSPRINT FROM ASSEMBLY */;  00080
  DCL OUT OUTPUT FILE STREAM ENV(FB RECSIZE(80) BLKSIZE(800));          00081
     /* RECEIVES ZAP CARDS */                                           00082
  DCL LINE CHAR(133) VAR; /* ONE LINE OF ASSEMBLER */                   00083
  DCL LINE2 CHAR(133);                                                  00084
  DCL ADDR CHAR(4) DEF LINE2 POS(4); /* OFFSET OR ADDRESS PART */       00085
  DCL DATA CHAR(16) DEF LINE2 POS(9); /* HEX INSTRUCTION CODE OR DATA */00086
          /* NOTE THAT SUBSTR(DATA,1,1) OVERLAPS LAST DIGIT OF          00087
             HEX REPRESENTATION OF ADDRESS GENERATED BY EQU             00088
             STATEMENT, SO TEST IS ON SUBSTR(DATA,2,1) FOR              00089
             PRESENCE OF ANY REAL DATA */                               00090
  DCL CARD CHAR(72) DEF LINE2 POS(42); /* ASM SOURCE PART */            00091
  DCL (DATE,TIME,INDEX,VERIFY,SUBSTR) BUILTIN;                          00092
  DCL WHAT CHAR(3) INIT(' '); /* BECOMES 'VER' OR 'REP' */              00093
  DCL SKP BIT(1) INIT('1'B);                                            00094
  ON ERROR BEGIN;                                                       00095
     ON ERROR SYSTEM;                                                   00096
     PUT EDIT(' ERROR PROCESSING:',LINE)(SKIP,A);                       00097
     END;                                                               00098
  %PAGE;                                                                00099
  /* LOGIC:                                                             00100
     EACH LINE IS HANDLED SEPARATELY. THE ONLY INFORMATION WHICH        00101
       EXTENDS ACROSS LINES IS THE MODE OF GENERATION IN 'WHAT'         00102
       AND THE GENERATION (0) OR PAUSE (1) STATUS IN 'SKP'.             00103
       FIRST ONE DETECTS AND PROCESSES CONTROL CARDS.                   00104
       THEN ONE CHECKS FOR PAGE HEADER LINES AND BLANK LINES.           00105
       ASSEMBLER COMMENT CARDS ARE REPRODUCED.                          00106
       A COMMENT CARD IS PRODUCED FOR LABELS OR NON-OBJECT              00107
         GENERATING STATEMENTS.                                         00108
       A VER OR REP CARD IS CREATED FOR OBJECT GENERATING STATEMENTS.   00109
    */                                                                  00110
  GETLINE:                                                              00111
     READ FILE(IN) INTO(LINE);                                          00112
     LINE2=LINE;                                                        00113
                                                                        00114
                                                                        00115
                                                                        00116
     IF SUBSTR(CARD,1,4)='*ZAP' THEN                                    00117
          DO /* ASMTOZAP CONTROL CARD PROCESSING */;                    00118
          I=VERIFY(SUBSTR(CARD,5),' ') +4;                              00119
          IF SUBSTR(CARD,I,5)='START' THEN                              00120
               DO;                                                      00121
               SKP='0'B;                                                00122
               IF INDEX(CARD,'VER')>0 THEN WHAT='VER';                  00123
               IF INDEX(CARD,'REP')>0 THEN WHAT='REP';                  00124
               GO TO GETLINE;                                           00125
               END;                                                     00126
          IF SUBSTR(CARD,I,5)='PAUSE' THEN                              00127
               DO;                                                      00128
               SKP='1'B;                                                00129
               GO TO GETLINE;                                           00130
               END;                                                     00131
          IF SUBSTR(CARD,I,4)='STOP'|SUBSTR(CARD,I,4)='END'             00132
             THEN GO TO FINN;                                           00133
          IF SUBSTR(CARD,I,4)='CARD' THEN                               00134
               DO;                                                      00135
               PUT FILE(OUT) EDIT(SUBSTR(CARD,I+5))(COL(1),A(72));      00136
               GO TO GETLINE;                                           00137
               END;                                                     00138
          PUT EDIT ('***** UNRECOGNIZED CONTROL CARD ****',CARD)        00139
               (SKIP(2),2 A);                                           00140
          CALL PLIRETC(16);                                             00141
          GO TO GETLINE;                                                00142
          END /* OF CONTROL CARD PROCESSING */;                         00143
     IF SKP THEN GO TO GETLINE;                                         00144
  /* NO PROCESSING IS DONE IF GENERATION IS PAUSED */                   00145
     IF VERIFY(SUBSTR(LINE2,2,23),' 0123456789ABCDEF')>0                00146
        THEN GO TO GETLINE;                                             00147
   /* IF THERE ARE NON HEX CHARACTERS IN THE ADDRESS AND DATA FIELDS    00148
      THEN WE ARE PROBABLY LOOKING AT A PAGE TITLE LINE */              00149
     IF CARD=' ' & DATA=' ' THEN GO TO GETLINE;                         00150
          /* IF THESE FIELDS ARE BLANK, THEN WE ARE LOOKING AT BLANK    00151
             LINES PRODUCED BY SPACE OR UNDER PAGE HEADER. */           00152
     IF SUBSTR(CARD,1,1)='*' THEN                                       00153
          DO /* REPRODUCE ASSEMBLER COMMENT CARD */;                    00154
          PUT FILE(OUT) EDIT(CARD)(COL(1),A(72));                       00155
          GO TO GETLINE;                                                00156
          END;                                                          00157
     IF SUBSTR(CARD,1,1)¬=' ' & SUBSTR(DATA,2,1)¬=' ' THEN              00158
          DO /* WE HAVE A LABELLED OBJECT GENERATING STATEMENT.         00159
            BREAK IT UP INTO TWO LINE, A COMMENTED 'LABEL EQU *' AND    00160
            A VER OR REP LINE THIS ALLOWS LESS TRUNCATION OF THE        00161
            COMMENT FIELD OF THE ASSEMBLER SOURCE, SINCE COLS 1-9 ARE   00162
            NEVER REPRODUCED AS BLANKS. */;                             00163
          PUT FILE(OUT) EDIT('*',SUBSTR(CARD,1,8),'EQU   *')            00164
               (COL(1),A(1),COL(21),A(8),COL(30), A);                   00165
          END;                                                          00166
     IF SUBSTR(DATA,2,1)=' ' THEN                                       00167
          DO /* NON-OBJECT-GENERATING STATEMENT */;                     00168
          PUT FILE(OUT) EDIT('*',CARD)                                  00169
               (COL(1),A(1),COL(21),A(60));                             00170
          GO TO GETLINE;                                                00171
          END /* IT IS PASSED AS A COMMENT */;                          00172
     IF WHAT=' ' THEN                                                   00173
          DO /* OBJECT GENERATOR HIT AFTER ONLY '*ZAP START' CARD */;   00174
          PUT EDIT('**** NEITHER VER NOR REP HAS BEEN SPECIFIED ****',  00175
               CARD)(SKIP, A);                                          00176
          CALL PLIRETC(16);                                             00177
          END;                                                          00178
     /* ZAP IS WRITTEN IN TWO PIECES. FIRST VER OR REP AND OFFSET,      00179
        NEXT DATA AND COLUMNS 10- ON ASSEMBLER SOURCE. */               00180
     PUT FILE(OUT) EDIT(WHAT,ADDR,' ')(COL(1),A(3),X(1),A(4),A(1));     00181
     I=1;                                                               00182
               /* DATA GENERATING STATEMENTS ARE OF TWO KINDS:          00183
                  MACHINE OPS GENERATE '0000 0000 0000' WITH            00184
                  THE SECOND AND THIRD GROUP OPTIONAL.                  00185
                  DC GENERATES A VARIABLE LENGTH                        00186
                  '0000000000000000' WITH AN EVEN # DIGITS<17 */        00187
          DO J=1 TO 4 WHILE(I<14);                                      00188
          PUT FILE(OUT) EDIT(SUBSTR(DATA,I,4))(A(4));                   00189
          IF SUBSTR(DATA,I+4,1)=' ' THEN I=I+1;                         00190
          IF SUBSTR(DATA,I+4,1)¬=' ' & I<10 THEN                        00191
               PUT FILE(OUT) EDIT(',')(A(1));                           00192
          I=I+4;                                                        00193
          END;                                                          00194
     PUT FILE(OUT) EDIT(SUBSTR(CARD,10))(COL(30),A(51));                00195
     GO TO GETLINE;                                                     00196
  FINN:                                                                 00197
     PUT FILE(OUT) EDIT('* DECK PRODUCED BY THE ASMTOZAP UTILITY'       00198
          ,DATE,TIME)(COL(1),A,X(1),P'99/99/99',X(1),A(6));             00199
     END;                                                               00200
