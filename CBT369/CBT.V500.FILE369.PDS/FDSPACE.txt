/*********************************************************************
/*
/* CHANGE LOG -
/*  12/05/86 PTW IMPLEMENTED
/*
/*********************************************************************
ISPEXEC VGET (NAME SORT ZDEL) PROFILE
CONTROL NOFLUSH

SET OLDNAME = &STR(&NAME)
SET OLDSORT = &STR(&SORT)

IF &STR(&NAME) = &STR( ) THEN +
  DO
    SET NAME    = ONLINE
    SET OLDNAME = ONLINE
  END

SET MODLINE  = &STR( )
ISPEXEC DISPLAY  PANEL(FDSPACEP) MSG(FDS003)

IF &LASTCC ¬= 8 THEN +
  DO
    REFRESH: +
    CONTROL NOCONLIST NOLIST
    ISPEXEC CONTROL  NONDISPL
    SET MODLINE  = &STR( )
    ISPEXEC DISPLAY  PANEL(FDSPACEP)
    SET &M =
    SET &MSGID =

    ISPEXEC TBCREATE MEMTABLE REPLACE NOWRITE                  +
                              KEYS(VOLUME UCB)                 +
                              NAMES(VTOC CYL TRK EXT CYLC TRKC +
                                    SORTUCB SORTFREE SORTCONT)

    SET &SYSOUTTRAP = 256
    DSPACE &NAME
    SET &SYSOUTTRAP = 0

    SET &LINE = 1
    SET &SL   = &SYSOUTLINE
    SET &LNES = &EVAL(&SYSOUTLINE - 1)

    DO WHILE &LINE < &SL+1
      SET &T = &&SYSOUTLINE&LINE
      SET &SYSDVAL = &STR(&T)

      READDVAL  &A &B &C &D &E &F &G

      IF &A = &STR(VOLSER) THEN +
        DO
          SET STEXT = &STR("&NAME" NOT FOUND)
          SET LTEXT = &STR(+
           VOLSER, UCB, OR UNIT "&NAME" NOT FOUND, REQUEST IGNORED)
          SET MSGNUM = &STR(FDS000)
          ISPEXEC SETMSG  MSG(&MSGNUM)
        END
      ELSE +
        DO
          IF &SUBSTR(1:3,&A) ¬= &STR(VOL)  THEN +
            DO
              SET VOLUME   = &A
              IF &B = &STR(I) THEN +
                DO
                  SET VTOC     = &B
                  SET UCB      = &C
                  SET CYL      = &SUBSTR(7:10,&D)
                  SET TRK      = &STR(&E)
                  SET EXT      = &SUBSTR(1:4,&STR(&F))
                  SET CYLC     = &SUBSTR(6:9,&STR(&F))
                  SET TRKC     = &STR(&G)
                END
              IF &B ¬= &STR(I)       AND  +
                 &B ¬= &STR(OFFLINE) THEN +
                DO
                  SET VTOC     = &STR( )
                  SET UCB      = &B
                  SET CYL      = &SUBSTR(7:10,&C)
                  SET TRK      = &STR(&D)
                  SET EXT      = &SUBSTR(1:4,&STR(&E))
                  SET CYLC     = &SUBSTR(6:9,&STR(&E))
                  SET TRKC     = &STR(&F)
                END
              IF &STR(&B) = &STR(OFFLINE) THEN +
                DO
                  SET VOLUME   = &STR( )
                  SET VTOC     = &B
                  SET UCB      = &A
                  SET CYL      = &STR( )
                  SET TRK      = &STR( )
                  SET EXT      = &STR( )
                  SET CYLC     = &STR( )
                  SET TRKC     = &STR( )
                END
              SET CNTR = 1
              DO WHILE &CNTR <= 3
                SET W&CNTR = &SUBSTR(&CNTR,&UCB)
                SET DIGIT = &SUBSTR(&CNTR,&UCB)
                IF &DIGIT = &STR(A) THEN SET W&CNTR = 10
                IF &DIGIT = &STR(B) THEN SET W&CNTR = 11
                IF &DIGIT = &STR(C) THEN SET W&CNTR = 12
                IF &DIGIT = &STR(D) THEN SET W&CNTR = 13
                IF &DIGIT = &STR(E) THEN SET W&CNTR = 14
                IF &DIGIT = &STR(F) THEN SET W&CNTR = 15
                SET CNTR = &CNTR + 1
              END
              SET SORTUCB  = &EVAL(&W1*256 + &W2*16 + &W3)
              SET SORTFREE = &STR(&CYL || &TRK)
              SET SORTCONT = &STR(&CYLC || &TRKC)
              SET LNEC     = &LINE - 1
              IF &EVAL(&LNEC//50) = 0 THEN +
                DO
                  ISPEXEC SETMSG   MSG(FDS004)
                  ISPEXEC CONTROL  DISPLAY LOCK
                  ISPEXEC DISPLAY  MSG(FDS004)
                END
              ISPEXEC TBADD    MEMTABLE
              ISPEXEC TBVCLEAR MEMTABLE
            END
        END
      SET &LINE = &LINE+1
    END

/*CONTROL LIST CONLIST

    RESORT: +
    IF &SORT =   THEN ISPEXEC TBSORT MEMTABLE FIELDS(VOLUME   C A  +
                                                     SORTUCB  N A)
    IF &SORT = 1 THEN ISPEXEC TBSORT MEMTABLE FIELDS(VOLUME   C A  +
                                                     SORTUCB  N A)
    IF &SORT = 2 THEN ISPEXEC TBSORT MEMTABLE FIELDS(SORTUCB  N A)
    IF &SORT = 3 THEN ISPEXEC TBSORT MEMTABLE FIELDS(SORTFREE B D)
    IF &SORT = 4 THEN ISPEXEC TBSORT MEMTABLE FIELDS(SORTCONT B D)
    ISPEXEC TBTOP   MEMTABLE
    SET MODLINE = &STR(           +
         _S#VOLUME  @UCB  @VTOC   @CYL  @TRK    @EXT   @CYLC @TRKC)
    ISPEXEC TBDISPL MEMTABLE PANEL(FDSPACEP) +
                             POSITION(ROW)   +
                             AUTOSEL(NO)     +
                             MSG(&MSGNUM)
    SET &TBDRC  = &LASTCC

    DO WHILE &TBDRC < 8
      IF &STR(&MSGNUM) ¬= &STR( ) THEN +
        DO
          SET OLDNAME = &STR(&NAME)
          SET &MSGNUM = &STR( )
          GOTO REFRESH
        END
      ELSE +
        SET &MSGNUM = &STR( )

      IF &STR(&NAME) ¬= &STR(&OLDNAME) THEN +
        DO
          SET OLDNAME = &STR(&NAME)
          GOTO REFRESH
        END

      IF &STR(&SORT) ¬= &STR(&OLDSORT) THEN +
        DO
          SET OLDSORT = &STR(&SORT)
          GOTO RESORT
        END

      IF &ZTDSELS > 0 THEN +
        DO
          IF &STR(&VTOC) = &STR(OFFLINE) THEN +
            DO
              ISPEXEC SETMSG  MSG(FDS001)
              ISPEXEC TBDISPL MEMTABLE POSITION(ROW) +
                                       AUTOSEL(NO)   +
                                       CSRROW(&ROW)  +
                                       MSG(FDS001)
              SET &TBDRC = &LASTCC
            END
          ELSE +
            DO
              SET PRCMOD  = &STR(FDSPACE)
              ISPEXEC VPUT (PRCMOD VOLUME) SHARED

              ISPEXEC CONTROL DISPLAY SAVE
              ISPEXEC CONTROL NONDISPL ENTER
              ISPEXEC SELECT PGM(ISRUDL)
              ISPEXEC CONTROL DISPLAY RESTORE

              SET PRCMOD = &STR( )
              SET VOLUME = &STR( )
              ISPEXEC VPUT (PRCMOD VOLUME) SHARED

              ISPEXEC TBDISPL MEMTABLE POSITION(ROW) +
                                       AUTOSEL(NO)   +
                                       CSRROW(&ROW)
              SET &TBDRC = &LASTCC
            END
        END
      ELSE +
        DO
          ISPEXEC TBDISPL MEMTABLE POSITION(ROW)
          SET &TBDRC = &LASTCC
        END
      CONTROL NOLIST NOCONLIST
    END
    ISPEXEC TBEND MEMTABLE
    ISPEXEC VPUT (NAME SORT) PROFILE
  END

