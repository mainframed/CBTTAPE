         MACRO ,
&NAME    TIOTSCAN &REG,&WKREG=R0,&NEXT=,&LAST=,&DDN=,&OK=,&NOK=
         LCLC  &R1,&R2
         LCLC  &LBL1,&LBL2,&LBL3,&LBL4
&LBL1    SETC  'TIO&SYSNDX.A'
&LBL2    SETC  'TIO&SYSNDX.B'
&LBL3    SETC  'TIO&SYSNDX.C'
&LBL4    SETC  'TIO&SYSNDX.D'
&R1      SETC  'R1'                     DEFAULT TO REGISTER 1
         AIF   ('&REG' EQ '').START     CHECK IF REG= SPECIFIED
&R1      SETC  '&REG'                   SET TO SPECIFIED REGISTER
.START   ANOP  ,
.*--------------------------------------------------------------------*
.*       LOCATE FIRST TIOT ENTRY                                      *
.*--------------------------------------------------------------------*
         PUSH  ACONTROL
         ACONTROL FLAG(NOPAGE0)
&NAME    L     &R1,16                   POINT TO CVT
         POP   ACONTROL
         L     &R1,0(0,&R1)             POINT TO TCB WORDS
         L     &R1,4(0,&R1)             POINT TO OLD TCB
         L     &R1,12(0,&R1)            POINT TO TIOT
         LA    &R1,24(0,&R1)            PT TO FIRST TIOT ENTRY
         AIF   ('&DDN' NE '').DDN
.*--------------------------------------------------------------------*
.*       SCAN THROUGH THE TIOT SERIALLY                               *
.*--------------------------------------------------------------------*
         AIF   ('&NEXT' EQ '').E1       MUST SPECIFY NEXT=
         AIF   ('LAST' EQ '').E2        MUST SPECIFY LAST=
         B     &LBL1                    PROCESS THE FIRST ENTRY
&NEXT    SR    &WKREG,&WKREG
         IC    &WKREG,0(0,&R1)          LOAD TIOT ENTRY LENGTH
         AR    &R1,&WKREG               PT TO NEXT TIOT ENTRY
&LBL1    CLI   0(&R1),0                 CHECK FOR END OF TIOT
         AIF   ('&LAST'(1,1) EQ '(').LASTR
         BE    &LAST                    YES, TIOT SCAN EXHAUSTED
         AGO   .MEXIT
.LASTR   ANOP  ,
&R2      SETC  '&LAST'(2,K'&LAST-2)
         BER   &R2                      YES, TIOT SCAN EXHAUSTED
         AGO   .MEXIT
.*--------------------------------------------------------------------*
.*       SCAN THROUGH THE TIOT SEARCHING FOR A SPECIFIC DDNAME        *
.*--------------------------------------------------------------------*
.DDN     ANOP  ,
         AIF   ('&OK&NOK' EQ '').E3     CHECK IF OK=/NOK= SPECIFIED
         AIF   ('&OK' NE '').OK
.*--------------------------------------------------------------------*
.*       SPECIFIC SEARCH AND NOK= WAS SPECIFIED AND OK= NOT SPECIFIED *
.*--------------------------------------------------------------------*
.NOK     ANOP  ,
         AIF   ('&DDN'(1,1) EQ '(').NOK1
&LBL2    CLC   =CL8'&DDN',4(&R1)        CHECK FOR MATCHING DDNAME
         AGO   .NOK2
.NOK1    ANOP  ,
&R2      SETC  '&DDN'(2,K'&DDN-2)
&LBL2    CLC   0(8,&R2),4(&R1)          CHECK FOR MATCHING DDNAME
.NOK2    ANOP  ,
         BE    &LBL3
         SR    &WKREG,&WKREG
         IC    &WKREG,0(0,&R1)          GET CURRENT ENTRY LENGTH
         AR    &R1,&WKREG               PT TO NEXT TIOT ENTRY
         CLI   0(&R1),0                 CHECK FOR END OF TIOT
         BNE   &LBL2                    IF NOT - KEEP SEARCHING
         AIF   ('&NOK'(1,1) EQ '(').NOK3
         B     &NOK                     OTHERWISE - ENTRY NOT FOUND
         AGO   .NOK4
.NOK3    ANOP  ,
&R2      SETC  '&NOK'(2,K'&NOK-2)
         BR    &R2                      OTHERWISE - ENTRY NOT FOUND
.NOK4    ANOP  ,
&LBL3    DS    0H
         AGO   .MEXIT
.*--------------------------------------------------------------------*
.*       SPECIFIC SEARCH AND OK= WAS SPECIFIED                        *
.*--------------------------------------------------------------------*
.OK      ANOP  ,
         AIF   ('&DDN'(1,1) EQ '(').OK1
&LBL4    CLC   =CL8'&DDN',4(&R1)        CHECK FOR MATCHING DDNAME
         AGO   .OK2
.OK1     ANOP  ,
&R2      SETC  '&DDN'(2,K'&DDN-2)
&LBL4    CLC   0(8,&R2),4(&R1)          CHECK FOR MATCHING DDNAME
.OK2     ANOP  ,
         AIF   ('&OK'(1,1) EQ '(').OK3
         BE    &OK                      IF SO  - SCAN IS COMPLETE
         AGO   .OK4
.OK3     ANOP  ,
&R2      SETC  '&OK'(2,K'&OK-2)
         BER   &R2                      IF SO - SCAN IS COMPLETE
.OK4     ANOP  ,
         SR    &WKREG,&WKREG
         IC    &WKREG,0(0,&R1)          GET CURRENT ENTRY LENGTH
         AR    &R1,&WKREG               PT TO NEXT TIOT ENTRY
         CLI   0(&R1),0                 CHECK FOR END OF TIOT
         BNE   &LBL4                    IF NOT - KEEP SEARCHING
         AIF   ('&NOK' EQ '').MEXIT
         AIF   ('&NOK'(1,1) EQ '(').OK5
         B     &NOK                     OTHERWISE - ENTRY NOT FOUND
         AGO   .MEXIT
.OK5     ANOP  ,
&R2      SETC  '&NOK'(2,K'&NOK-2)
         BR    &R2                      OTHERWISE - ENTRY NOT FOUND
         AGO   .MEXIT
.E1      MNOTE 8,'NEXT= Option Not Specified'
         AGO   .MEXIT
.E2      MNOTE 8,'LAST= Option Not Specified'
         AGO   .MEXIT
.E3      MNOTE 8,'OK= Or NOK= Keywords Not Specified'
.MEXIT   MEND  ,
