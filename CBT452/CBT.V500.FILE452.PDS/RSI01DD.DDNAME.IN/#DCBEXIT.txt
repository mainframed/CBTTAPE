         MACRO ,
&NAME    #DCBEXIT &ASCII=NO,            RECFM=D SUPPORT                X
               &BLOCK=YES,              BLOCKING BY DEFAULT            X
               &CSECT=NO,               GENERATE NEW CSECT             X
               &EXTEND=,                EXTENDED DCB LOGIC             X
               &LRECL=80,               DEFAULT LRECL                  X
               &BLKSIZE=,               DEFAULT BLKSIZE                X
               &PPC=NO                  UNLIKE CONCAT SUPPORT
         LCLC  &L,&LABEL,&P
***********************************************************************
*
*        DCB EXIT TO SET DEFAULT LRECL & BLKSIZE
*
.*       NOTE-  THIS CODE WILL FUNCTION ON ANY LEVEL OF OS/VS
.*
&P       SETC  'DCB&SYSNDX'
&L       SETC  'OPENBLK'
&LABEL   SETC  'OPENBLK'
         AIF   ('&NAME' EQ '').NONAME
&LABEL   SETC  '&NAME'
&L       SETC  '&NAME'
.NONAME  AIF   ('&CSECT' NE 'YES').BOTH
.CSECT   ANOP  ,
&L       CSECT ,
&L       SETC  ' '
.BOTH    ANOP  ,
         PUSH  USING
         DROP  ,
         USING IHADCB,R1
         USING &LABEL,R15
         SPACE 1
&L       LH    R0,DCBLRECL              GET ORIGINAL LRECL
         LH    R3,DCBBLKSI                     AND   BLKSIZE
         SPACE 1
         LTR   R0,R0                    IS LRECL NON-ZERO ?
         BNZ   &P.A                     YES, USE IT AS-IS
         LA    R0,&LRECL                OTHERWISE USE DEFAULT
         SPACE 1
&P.A     MVC   DCBBLKSI(1),DCBRECFM     ISOLATE F/V/U REC TYPE
         NI    DCBBLKSI,DCBRECL
         AIF   ('&ASCII'(1,1) EQ 'N').NOASCII
         SPACE 1
         TM    DCBRECFM,DCBRECD         FORMAT-D RECORDS ?
         BZ    *+8                      NO, SKIP
         MVI   DCBBLKSI,DCBRECV         YES, TREAT LIKE FORMAT-V
         SPACE 1
.NOASCII LTR   R3,R3                    BLKSIZE STILL ZERO ?
         BNZ   &P.D                     NO, LEAVE ALONE
         LR    R3,R0                    YES, SET BLKSIZE = LRECL
         SPACE 1
         TM    DCBBLKSI,DCBRECU         IS THIS TYPE-U
         BNM   &P.E                     YES, BRANCH
         AIF   ('&BLOCK'(1,1) NE 'Y').BASIC
         SPACE 1
         PUSH  ACONTROL
         ACONTROL FLAG(NOPAGE0)
         L     R2,16                    POINT TO CVT
         POP   ACONTROL
         L     R2,0(0,R2)               POINT TO TCB WORDS
         L     R2,4(0,R2)               POINT TO OLD TCB
         L     R2,12(0,R2)              POINT TO TIOT
         AH    R2,DCBTIOT               POINT TO TIOT ENTRY
         ICM   R2,7,17(R2)              GET UCB ADDRESS
         BZ    &P.C                     IF NO UCB, NO BLOCKING
         TM    18(R2),160               TAPE OR DASD ?
         BZ    &P.C                     NEITHER, DO NOT BLOCK
         LA    R4,8                     ASSUME DASD FACTOR OF 256
         TM    18(R2),128               TAPE ?
         BZ    &P.B                     NO, SKIP
         LA    R4,10                    TAKE TAPE FACTOR OF 1024
&P.B     LA    R3,24                    BLOCKSIZE IS FACTOR*24 AND
         SLL   R3,0(R4)                 IS ROUNDED DOWN FOR F-TYPE
         B     &P.D                     SKIP BDW ALLOWANCE
         SPACE 1
.BASIC   ANOP  ,
&P.C     CLI   DCBBLKSI,DCBRECV         IS THIS V-TYPE ?
         BNE   &P.D                     NO, NO OFFSET
         LA    R3,4(0,R3)               YES, ALLOW 4 BYTES FOR BDW
         SPACE 1
&P.D     CLI   DCBBLKSI,DCBRECF         IS THIS F-TYPE ?
         BNE   &P.F                     NO, ALL DONE
         SR    R2,R2
         DR    R2,R0                    YES, MAKE BLKSIZE EVEN
         MR    R2,R0                         MULTIPLE OF LRECL
         CR    R3,R0                    ENSURE RESULT NOT ZERO
         BH    &P.F                     OKAY, ALL DONE
         LR    R3,R0                    SET BLKSIZE TO LRECL
         NI    DCBRECFM,255-DCBRECBR    NO BLOCKING
         B     &P.F                     ALL DONE
         SPACE 1
&P.E     OI    DCBRECFM,DCBRECU         MAKE RECFM=U EXPLICIT
         NI    DCBRECFM,255-(DCBRECBR+DCBRECSB)   NEITHER B NOR S
         SR    R0,R0                    SET LRECL TO ZERO
         SPACE 1
&P.F     STH   R0,DCBLRECL              SET ACTUAL LRECL
         AIF   ('&BLKSIZE' EQ '').NOBLKSI
         LTR   R3,R3                    IS BLKSIZE STILL ZERO?
         BNZ   &P.H                     B. IF NOT
         L     R3,&P.G                  OTHERWISE USE DEFAULT
         B     &P.H                     BYPASS CONSTANT
&P.G     DC    A(&BLKSIZE)              USER DEFINED DEFAULT BLKSIZE
.NOBLKSI ANOP  ,
&P.H     STH   R3,DCBBLKSI              SET ACTUAL BLKSIZE
         AIF   ('&PPC'(1,1) EQ 'N').NOPPC
         SPACE 1
         TM    DCBOFLGS,DCBOFPPC        UNLIKE CONCATENATION ?
         BZR   R14                      NO, RETURN TO OPEN
         MVI   IHADCB,255               SET FLAG
.NOPPC   ANOP  ,
         BR    R14                      RETURN TO OPEN
         SPACE 1
         AIF   ('&EXTEND' EQ '').SKIP2
         SPACE 1
.*
.*  DCB  PREFIX FORMAT -
.*
.*  -8   DS    X  ID BYTE=X'ED'
.*  -7   DS    B  RECFM
.*  -6   DS    H  LRECL
.*  -4   DS    H  BLKSIZE
.*  -2   DS    B  OPTCD BITS
.*  -1   DS    X  BUFNO
.*
         USING &EXTEND,R15
&EXTEND  LA    R0,8                     DCB PREFIX LENGTH
         SR    R1,R0                    BACK UP TO PREFIX
         USING IHADCB-8,R1
         SPACE 1
         CLI   IHADCB-8,237             EXTENDED DCB ?
         BNE   &P.I                     NO, SKIP EXTENSIONS
         SPACE 1
         CLI   DCBRECFM,0
         BNZ   *+10
         MVC   DCBRECFM,IHADCB-7        SET DEFAULT RECFM
         SPACE 1
         ICM   R0,3,DCBLRECL
         BNZ   *+10
         MVC   DCBLRECL,IHADCB-6        SET DEFAULT LRECL
         SPACE 1
         ICM   R0,3,DCBBLKSI
         BNZ   *+10
         MVC   DCBBLKSI,IHADCB-4        SET DEFAULT BLKSIZE
         SPACE 1
         OC    DCBOPTCD,IHADCB-2        SET FORCED OPTCD BITS
         SPACE 1
         CLI   DCBBUFNO,0
         BNZ   *+10
         MVC   DCBBUFNO,IHADCB-1        SET DEFAULT BUFNO
         SPACE 1
&P.I     LA    R1,IHADCB                POINT TO BASIC DCB
         LA    R0,&EXTEND-&LABEL
         SR    R15,R0
         BR    R15                      BRANCH TO MAIN EXIT
         SPACE 1
.SKIP2   ANOP  ,
         POP   USING
         MEND  ,
