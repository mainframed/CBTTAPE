         MACRO ,
&LABEL   #COPY &FUNC,&BR=NO,&SIZE=,&WHO=,&YEAR=
.**********************************************************************
.*                                                                    *
.*     THIS MACRO IS USED TO INSERT THE NECESSARY COPYRIGHT           *
.*     NOTICE INTO BOTH THE LISTING AND THE OBJECT CODE.  NOTE        *
.*     THAT MNOTES PRINT REGARDLESS OF USE OF PRINT NOGEN.            *
.*                                                                    *
.*     THE MACRO HAS TWO FUNCTIONS, 'SET' AND 'GEN'.  'SET' IS        *
.*     USED TO ESTABLISH THE NAME OF THE COPYRIGHT HOLDER AND/OR      *
.*     SET THE GLOBAL &#COPYSZ FOR USE IN AN OUTER MACRO.  'GEN'      *
.*     IS CODED TO ACTUALLY GENERATE THE COPYRIGHT.  IF NEITHER       *
.*     IS CODED, 'GEN' IS ASSUMED.                                    *
.*                                                                    *
.**********************************************************************
         GBLA  &#COPY
         GBLA  &#COPYSZ                 TOTAL SIZE OF STRING IN BYTES
         GBLC  &#COPYYR                 YEAR OF COPYRIGHT
         GBLC  &#COPYWH                 NAME OF COPYRIGHT HOLDER
         LCLA  &N
         LCLC  &X,&Y
.*
&X       SETC  '&SYSDATC'(1,4)
         AIF   ('&YEAR' EQ '').NOYEAR
&#COPYYR SETC  '&YEAR'
.NOYEAR  ANOP  ,
.*
         AIF   ('&WHO' NE '').EXPLW     NAME CODED ?
         AIF   ('&#COPYWH' NE '').SIZE  FIRST TIME THRU ?
&#COPYWH SETC  'zOS.JES2@GMail.com'     SET DEFAULT NAME
         AGO   .SIZE                    GO SET SIZE
.*
.EXPLW   ANOP  ,                        EXPLICIT NAME CODED
&#COPYSZ SETA  0                        MUST RESET SIZE
         AIF   ('&WHO'(1,1) NE '''').NOQUOTE
&N       SETA  K'&WHO-2                 STRIP APOSTROPHES
&#COPYWH SETC  '&WHO'(2,&N)
         AGO   .SIZE
.NOQUOTE ANOP  ,
&#COPYWH SETC  '&WHO'                   UNQUOTED EXPLICIT NAME
.*
.SIZE    AIF   ('&SIZE' NE '').EXPL     IS SIZE CODED ?
         AIF   (&#COPYSZ NE 0).FUNC
&#COPYSZ SETA  ((42+K'&#COPYWH+1)/2)*2  EVEN DEFAULT SIZE
         AGO   .FUNC
.EXPL    ANOP  ,
&#COPYSZ SETA  ((&SIZE+1)/2)*2          ROUNDED EXPLICIT SIZE
.*
.FUNC    ANOP  ,
         AIF   (&#COPY NE 0).FUNC1
&#COPY   SETA  1
         AIF   ('&#COPYYR' NE '').YEAROK
&#COPYYR SETC  '1985, &X'
&#COPYSZ SETA  &#COPYSZ+6
         AGO   .FUNC1
.YEAROK  ANOP  ,
         AIF   ('&#COPYYR' EQ '&X').FUNC1
&#COPYYR SETC  '&#COPYYR., &X'
&#COPYSZ SETA  &#COPYSZ+6
.FUNC1   ANOP  ,
         AIF   ('&FUNC' EQ 'SET').MEND
         AIF   ('&FUNC' EQ '' OR '&FUNC' EQ 'GEN').GEN
         MNOTE 4,'Invalid Function &FUNC, GEN Assumed'
.*
.GEN     ANOP  ,                        GENERATE THE COPYRIGHT
         AIF   ('&BR' EQ 'NO').NOBR     BRANCH NEEDED ?
&N       SETA  &#COPYSZ+4
         B     *+&N                     BR AROUND COPYRIGHT NOTICE
.NOBR    ANOP  ,
*
         MNOTE '*        (C) Copyright &#COPYYR &#COPYWH.'
         MNOTE '*        Program Property Of &#COPYWH.'
         MNOTE '*        All Rights Reserved.'
 MNOTE '*        No part of this program may be reproduced, stored'
 MNOTE '*        in a retrieval system or transmitted in any form'
 MNOTE '*        or by any means, including photocopying, recording,'
 MNOTE '*        electronic, mechanical or otherwise without the'
 MNOTE '*        written consent of the copyright holder.'
*
&N       SETA  &#COPYSZ-22
&LABEL   DC    CL&N.' (C) Copyright &#COPYYR &#COPYWH  '
         DC    CL22' All Rights Reserved.'
.MEND    MEND  ,
