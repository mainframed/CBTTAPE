         MACRO
&L       #ENVIRON &TSO=,&BATCH=,&STC=,&SCR=15
         LCLC  &X                   SYSNDX
&X       SETC  '&SYSNDX'
         PUSH  ACONTROL
         ACONTROL FLAG(NOPAGE0)
&L       L     &SCR,548             GET @ ASCB
         POP   ACONTROL
         AIF   ('&BATCH' NE '' OR '&STC' NE '').ENV0100   TSO
         CLC   60(4,&SCR),=F'0'     TSB POINTER ZERO?
         BNE   &TSO                 NO, TAKE TSO EXIT
         MEXIT
.ENV0100 AIF   ('&TSO' NE '' OR '&STC' NE '').ENV0200   BATCH
         CLC   172(4,&SCR),=F'0'    JOBNAME POINTER ZERO?
         BNE   &BATCH               NO, TAKE BATCH EXIT
         MEXIT
.ENV0200 AIF   ('&TSO' NE '' OR '&BATCH' NE '').ENV0300   STC
         CLC   60(4,&SCR),=F'0'     TSB POINTER ZERO?
         BNE   ENV&X.1              YES, GIVE UP
         CLC   172(4,&SCR),=F'0'    NO, JOBNAME POINTER ZERO?
         BE    &STC                 YES, TAKE STC EXIT
ENV&X.1  DS    0H
         MEXIT
.ENV0300 AIF   ('&STC' NE '').ENV0400   TSO AND BATCH
         CLC   60(4,&SCR),=F'0'     TSB POINTER ZERO?
         BNE   &TSO                 NO, TAKE TSO EXIT
         CLC   172(4,&SCR),=F'0'    YES, JOBNAME POINTER ZERO?
         BNE   &BATCH               NO, TAKE BATCH EXIT
         MEXIT
.ENV0400 AIF   ('&BATCH' NE '').ENV0500   TSO AND STC
         CLC   60(4,&SCR),=F'0'     TSB POINTER ZERO?
         BNE   &TSO                 NO, TAKE TSO EXIT
         CLC   172(4,&SCR),=F'0'    YES, JOBNAME POINTER ZERO?
         BE    &STC                 YES, TAKE STC EXIT
         MEXIT
.ENV0500 AIF   ('&TSO' NE '').ENV0600   BATCH AND STC
         CLC   172(4,&SCR),=F'0'    JOBNAME POINTER ZERO?
         BNE   &BATCH               YES, TAKE BATCH EXIT
         CLC   60(4,&SCR),=F'0'     NO, TSB POINTER ZERO?
         BE    &STC                 YES, TAKE STC EXIT
         MEXIT
.ENV0600 ANOP                       TSO, BATCH AND STC
         CLC   60(4,&SCR),=F'0'     TSB POINTER ZERO ?
         BNE   &TSO                 NO, TAKE TSO EXIT
         CLC   172(4,&SCR),=F'0'    YES, JOBNAME POINTER ZERO?
         BNE   &BATCH               NO, TAKE BATCH EXIT
         B     &STC                 YES, TAKE STC EXIT
         MEND
