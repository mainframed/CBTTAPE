*
*        PDSDOC *VSAMANAL UPDATED FOR 3380 (AND 3390?)................*
*
***********************************************************************
*                                                                     *
* THIS PROGRAM IS A TSO COMMAND PROCESSOR CALLED 'VSAMANAL'           *
*                                                                     *
* THE FUNCTION OF THIS COMMAND PROCESSOR IS TO DISPLAY TO THE USER    *
* THE AVAILABLE CONTROL INTERVAL SIZES FOR A VSAM DATASET HAVING THE  *
* CHARACTERISTICS DESCRIBED BY THE USER THROUGH THE FILLING IN OF     *
* A MENU SELECTION SCREEN.                                            *
* ALONG WITH THE AVAILABLE CONTROL INTERVAL SIZES OTHER               *
* CHARACTERISTICS PERTINENT TO A VSAM DATASET WILL BE DISPLAYED       *
* INCLUDING ESTIMATED SPACE REQUIREMENTS FOR THE DESCRIBED DATASET    *
*                                                                     *
* ALL COMMUNICATION WITH THE TSO USER IS THROUGH THE USE OF TPUT'S    *
* AND TGET'S.  THE FULLSCR OPTION IS USED FOR ALL TPUT'S AND THE      *
* BUFFERS USED ARE CREATED THROUGH THE CODING OF SPECIALIZED MACROS   *
* WHICH GENERATE ALL THE 3270 BUFFER CONTROL CHARACTERS....           *
*                                                                     *
* AUTHOR: ARTHUR J. CARTIER                                           *
* DATE:   10/10/78                                                    *
* OWNER:  STATE OF CONNECTICUT                                        *
*         CENTRAL DATA PROCESSING SERVICES                            *
*         TECHNICAL SUPPORT SECTION                                   *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
* THE FOLLOWING SPECIAL MACROS ARE USED BY THIS MODULE:               *
*     BUF3270                                                         *
*     FIELD (NESTED MACROS= SBA,SF)                                   *
*                                                                     *
***********************************************************************
*        PRINT NOGEN
         PRINT ON,GEN              ASSEMBLER OPTIONS
         EJECT
         OSINIT  VSAMANAL,(3,10,11,12),NO
GETSTRG  GETMAIN R,                                                    X
               LV=WORKLEN+(DEVTABLN*DEVICES)+MENUL+HEADRL+(DATAL*4)
         USING WORKAREA,WAREAREG        ADDRESSABILITY  TO OUR WORKAREA
*INITIALIZE WORKAREA DSECT.......................................
         LR    R6,R1             SET FOR CLEAR TO LOW VALUES(X'00')
         LA    R7,WORKLEN        LENGTH OF WORKAREA FOR CLEAR
         XR    R8,R8                   ZERO FOR CLEAR
         XR    R9,R9                   ZERO FOR CLEAR
         MVCL  R6,R8           CLEAR WORKAREA TO BINARY ZEROS(X'00'S)
         ST    R1,8(R13)                SET FORWORD SAVEAREA CHAIN
         ST    R13,4(R1)                SET BACKWORD SAVEAREA CHAIN
         LR    WAREAREG,R1              WORKAREA ADDRESS
*INITIALIZE MENU SCREEN (3270 FULLSCR OUTPUT BUFFER).............
         ST    R6,AMENUD               SET WORKAREA MENU SCREEN POINTER
         LA    R7,MENUL                 LENGTH OF MVCL OPERAND 1
         ST    R7,LMENUD                SAVE LENGTH FOR OTHER MODS
         LR    R9,R7                    SAVE LENGTH FOR OPERAND 2
         LA    R8,MENU                  ADDR OF OPERAND 2 FOR MVCL
         MVCL  R6,R8                    MOVE MENU SCREEN TO WORKAREA
*INITIALIZE HEADR SCREEN (3270 FULLSCR OUTPUT BUFFER).............
         ST    R6,AHEADRD             SET WORKAREA HEADR SCREEN POINTER
         LA    R7,HEADRL                LENGTH OF MVCL OPERAND 1
         ST    R7,LHEADRD               SAVE LENGTH FOR OTHER MODS
         LR    R9,R7                    SAVE LENGTH FOR OPERAND 2
         LA    R8,HEADR                 ADDR OF OPERAND 2 FOR MVCL
         MVCL  R6,R8                    MOVE MENU SCREEN TO WORKAREA
INITPGE1 DS    0H
*INITIALIZE(PAGE1) DATA SCREEN (3270 FULLSCR OUTPUT BUFFER)...........
         ST    R6,DATA1PGE             SET WORKAREA DATA SCREEN(PAGE 1)
         LA    R7,DATAL                 LENGTH OF MVCL OPERAND 1
         ST    R7,LDATAD                SAVE LENGTH FOR OTHER MODS
         LR    R9,R7                    SAVE LENGTH FOR OPERAND 2
         LA    R8,DATA                  ADDR OF OPERAND 2 FOR MVCL
         MVCL  R6,R8                    MOVE MENU SCREEN TO WORKAREA
*INITIALIZE(PAGE2) DATA SCREEN (3270 FULLSCR OUTPUT BUFFER)...........
         ST    R6,DATA2PGE             SET WORKAREA DATA SCREEN(PAGE 1
         LA    R7,DATAL                 LENGTH OF MVCL OPERAND 1
         LR    R9,R7                    SAVE LENGTH FOR OPERAND 2
         LA    R8,DATA                  ADDR OF OPERAND 2 FOR MVCL
         MVCL  R6,R8                    MOVE MENU SCREEN TO WORKAREA
*INITIALIZE(PAGE3) DATA SCREEN (3270 FULLSCR OUTPUT BUFFER)...........
         ST    R6,DATA3PGE             SET WORKAREA DATA SCREEN(PAGE 1
         LA    R7,DATAL                 LENGTH OF MVCL OPERAND 1
         LR    R9,R7                    SAVE LENGTH FOR OPERAND 2
         LA    R8,DATA                  ADDR OF OPERAND 2 FOR MVCL
         MVCL  R6,R8                    MOVE MENU SCREEN TO WORKAREA
*INITIALIZE(PAGE4) DATA SCREEN (3270 FULLSCR OUTPUT BUFFER)...........
         ST    R6,DATA4PGE             SET WORKAREA DATA SCREEN(PAGE 4
         LA    R7,DATAL                 LENGTH OF MVCL OPERAND 1
         LR    R9,R7                    SAVE LENGTH FOR OPERAND 2
         LA    R8,DATA                  ADDR OF OPERAND 2 FOR MVCL
         MVCL  R6,R8                    MOVE MENU SCREEN TO WORKAREA
         EJECT
INITDEVC DS    0H
*INITIALIZE ALL DEVICE TABLES ...................................
*  EACH DEVICE TYPE SUPPORTED BY THIS PROGRAM HAS ITS OWN TABLE OF
*  CI SIZES AND OTHER DEVICE DEPENDENT DATA.
*  FOR RE-ENTRENCY PURPOSES THE DEVICE TABLES, WHICH ARE USED
*  FOR CALCULATIONS, WILL BE COPIES OF THE REAL DEVICE TABLES.
*  THE STORAGE FOR THESE TABLES(COPIES) WAS JUST GETMAINED SO NOW
*  THE WORKING DEVICE TABLES MUST BE INITIALIZED.
*  THIS IS ACCOMPLISHED THROUGH THE USE OF STATIC DEVICE LIST WHICH
*  CONTAINS THE NO. OF DEVICES TYPES SUPPORTED (DEVICE TABLES),
*  AND AND ENTRY FOR EACH DEVICE TYPE SUPPORTED,  WHICH CONSISTS
*  OF THE DEVICE TYPE AND THE ADDRESS OF THE REAL DEVICE TABLE.
*
*  THERE ALSO IS A WORK DEVICE LIST IN OUR COMMON WORKAREA THAT
*  CORRESPONDS TO THE STATIC DEVICE LIST.            .
*  THIS WORK LIST WILL BE INITIALIZED WITH THE ADDRESSES OF THE
*  WORKING DEVICE TABLES.
*
*  AFTER INITIALIZATION THE WORK DEVICE TABLES, THE WORK DEVICE
*  LIST POINTING TO THESE DEVICE TABLES WILL BE USED IN ALL
*  PROCESSING.
*
         MVC   DEVLSTWK,DEVLSTST    MAKE BOTH DEVICE LISTS THE SAME
         LA    R1,DEVLSTST    START OF STATIC DEVICE LIST
         LA    R2,DEVLSTWK    START OF WORKING DEVICE LIST
         LH    R15,0(R1)      NUMBER OF DEVICES IN DEVICE LIST
         LA    R1,2(R1)       FIRST DEVICE ENTRY IN STATIC LIST
         LA    R2,2(R2)       FIRST DEVICE ENTRY IN WORKING LIST
DEVTBLP  DS    0H             ENTRY POINT FOR DEVICE LIST LOOP
         ST    R6,4(R2)       SET WORKING LIST DEVICE TABLE ADDRESS
         LA    R7,DEVTABLN    LENGTH OF DEVICE TABLE FOR MVCL
         LR    R9,R7          SAVE LENGTH FOR OPERAND 2
         L     R8,4(R1)       ADDR OF STATIC LIST DEVICE ENTRY
         MVCL  R6,R8          MOVE DEVICE TABLE TO WORK COPY
         LA    R1,8(R1)       NEXT STATIC LIST ENTRY
         LA    R2,8(R2)       NEXT WORKING LIST ENTRY
         BCT   R15,DEVTBLP
         EJECT
PUTMENU  DS    0H
         L     SCRNREG,AMENUD  ESTABLISH ADDRESSABILITY FOR SCREEN
         USING MENUD,SCRNREG            ADDRESSABILITY FOR DSECT
         TPUT CLEARBUF,CLEARLEN,FULLSCR
         LR    R1,SCRNREG               LOAD FOR TPUT REGISTER FORMAT
         L     R0,LMENUD                LOAD FOR TPUT REGISTER FORMAT
         ICM   R1,8,FULLSCR             SET FOR FULLSCR OPTION
         TPUT ((1)),((0)),R
         LA    R1,MENUD                 ADDR OF MENU SCREEN IN WORKAREA
         L     R0,LMENUD                LENGTH OF MENU SCREEN
         LR    R2,WAREAREG              ADDR OF COMMON WORKAREA
         MVC   HLPTYPE,=CL3'ALL'        HELP TYPE REQUESTED
         LINK  EP=VSAMAGET              GO AND READ FROM TUBE
*                                       AND PROCESS INPUT
         OC    PROCERR,PROCERR          ANY PROCESSING ERRORS?
         BZ    CKMENUKY                 NO GO EDIT INPUT DATA
         MVC   ERROR,PROCERR            SET PROCESSING ERROR IN MENU
         B     PUTMENU
CKMENUKY DS    0H
         CLI   KEYHIT,CLEAR
         BE    RETURN
         CLI   KEYHIT,PF4               WAS PF4-HIT??
         BE    RETURN                   YES-GET OUT OF THIS PROGRAM
         CLI   KEYHIT,PF3               WAS PF3-HIT??
         BE    REINIT                   NO-GO CHECK PARMAMETERS
         CLI   KEYHIT,PF1               WAS HELP REQUESTED?
         BNE   PARM1
         LR    R0,WAREAREG              WORKAREA ADDRESS
         LINK  EP=VSAMAHLP
         CLI   KEYHIT,PF4               SPF PRIMARY OPTION MENU
         BE    RETURN
         B     PUTMENU
REINIT   DS    0H
         LR    R1,WAREAREG              SET FOR FREEMAIN
         L     R13,SAVE+4               RESET SAVE AREA POINTER
FREESTRG FREEMAIN R,A=(1),                                             X
               LV=WORKLEN+(DEVTABLN*DEVICES)+MENUL+HEADRL+(DATAL*3)
         B     GETSTRG                  GO GET ANOTHER PROGRAM WORKAREA
         EJECT
PARM1    DS    0H
         LA    R15,RECA
         LH    R14,=Y(RECAL)          LENGTH OF FIELD
         BAL   LINKREG,NUMERIC         GO SEE IF DATA IS NUMERIC
         B     BADRECSA                NOT NUMERIC
         CLC   RECA,BLANKS             WAS DATA KEYED
         BE    NORECA                  NO-ERROR PARAMETER NOT OPTIONAL
         BAL   LINKREG,CVB             YES-CONVERT DATA TO BINARY
         OR    R1,R1              WAS RECSIZE-AVG ZEROS????
         BZ    RECAZERO           YES-ERROR
         ST    R1,RECSZ                 STORE DATA
         C     R1,=F'32761'             IS IT GREATER THAN MAX ALLOWED
         BH    RECALARG                 YES - ERROR
         SPACE 3
PARM2    DS    0H
         LA    R15,RECM
         LH    R14,=Y(RECML)           LENGTH OF DATA 730
         BAL   LINKREG,NUMERIC         GO SEE IF DATA IS NUMERIC
         B     BADRECSM                NOT NUMERIC
         CLC   RECM,BLANKS             WAS DATA KEYED
         BNE   PARM2C                  YES-GO CONVERT IT TO BINARY
SAVAVG   DS    0H
         MVC   MAXRECSZ,RECSZ          NO-MAKE IT SAVE AS AVERAGE
         B     PARM3                   GO PROCESS NEXT PARAMETER
PARM2C   DS    0H
         BAL   LINKREG,CVB              CONVERT DATA TO BINARY
         ST    R1,MAXRECSZ              STORE DATA
         C     R1,=F'32761'             IS MAXIMUM GREATER THAN ALLOWED
         BH    RECMLARG                 YES-ERROR IN RECSIZE
         C     R1,RECSZ                 IS MAX GREATER THAN AVG
         BL    LORECM                   NO-MAX RECSZ ERROR(LOW)
         OR    R1,R1                    WAS ZEROS KEYED???
         BZ    SAVAVG                   YES-USE AVG-RECSIZE
         SPACE 3
PARM3    DS    0H
         CLC   RECLOAD,BLANKS          WAS DATA KEYED
         BE    NORECLD                 NO-ERROR PARAMETER NOT OPTIONAL
         LA    R15,RECLOAD
         LH    R14,=Y(RECLOADL)        LENGTH OF DATA FIELD910
         BAL   LINKREG,NUMERIC         GO SEE IF DATA IS NUMERIC
         B     BADRECLD                NOT NUMERIC
         BAL   LINKREG,CVB              CONVERT DATA TO BINARY
         OR    R1,R1              WAS RECLOAD ZEROS???????
         BZ    RECLZERO           YES-ERROR
         ST    R1,#RECLD                STORE DATA
         SPACE 3
PARM4    DS    0H
         CLC   RECADD,BLANKS            WAS PARAMETER KEYED
         BNE   CONTP4                   YES-GO PROCESS THIS PARAMETER
         XC    #RECADD,#RECADD          ZERO OUT PARAMETER
         B     PARM5
CONTP4   DS    0H
         LA    R15,RECADD
         LH    R14,=Y(RECADDL)          LENGTH OF DATA FIELD
         BAL   LINKREG,NUMERIC          GO SEE IF DATA IS NUMERIC
         B     BADRECAD                 NOT NUMERIC
         BAL   LINKREG,CVB              YES-CONVERT DATA TO BINARY
         ST    R1,#RECADD               STORE DATA
PARM5    DS    0H
         LA    R15,KEYL
         LH    R14,=Y(KEYLL)           LENGTH OF DATA FIELD
         BAL   LINKREG,NUMERIC          GO SEE IT DATA IS NUMERIC
         B     BADKLEN                  NOT NUMERIC
         CLC   KEYL,BLANKS              WAS PARAMETER KEYED
         BE    CHKRADD                  NO- GO SEE IF RADD KEYED
         BAL   LINKREG,CVB              YES- CONVERT DATA TO BINARY
         STH   R1,KEYLEN                STORE DATA
         CH    R1,=H'255'               IS KEYLEN GREATER THAN MAX
         BH    HIKLEN                   YES- ERROR IN KEY
         C     R1,MAXRECSZ              IS MAXRECSZ LESS THAN KEYLEN
         BH    BADMAXRC                 YES-ERROR IN KEYLEN OR MAXRSZ
         B     PARM6                    GO PROCESS NEXT PARAMETER
CHKRADD  DS    0H
         XC    KEYLEN,KEYLEN            ZERO OUT PARAMETER
         OC    #RECADD,#RECADD          WAS RADD PARAMETER KEYED
         BZ    PARM6                    NO-GO PROCESS NEXT PARAMETER
         L     R1,#RECADD
         A     R1,#RECLD               ADD RADD TO RLOAD
         ST    R1,#RECLD               SET NEW RECLOAD
         XC    #RECADD,#RECADD          CLEAR BECAUSE NOT KSDS
         SPACE 3
PARM6    DS    0H
         OC    #RECADD,#RECADD   #RECADD NOT ENTERED OR ESDS/RRDS?
         BZ    NOCICAPR          YES-GO SET CICA RATIOS TO ZERO?
         CLC   CIPER,BLANKS   WAS CIPERCENTAGE KEYED??
         BE    SETCIPD        NO-GO SET CI/CA RATIO DEFAULTS...
         LA    R15,CIPER      ADDRESS OF DATA
         LH    R14,=Y(CIPERL) LENGTH OF DATA
         BAL   LINKREG,NUMERIC IS DATA NUMERIC????
         B     BADCIPER       NOT NUMERIC
         BAL   LINKREG,CVB    NUMERIC-CONVERT IT TO BINARY
         C     R1,=F'100'     IS PERCENTAGE GREATER THAN 100
         BH    CIPERHI        YES-ERROR
         ST    R1,CIRATIO       SET CI FREESPACE RATIO(INSRT RATE)
         B     PARM6A
SETCIPD  DS    0H
         MVC   CIRATIO,=F'0'    CIRATIO  DEFAULT
         B     PARM6A
NOCICAPR DS    0H
         XC    CIRATIO,CIRATIO  ZERO
         XC    CARATIO,CARATIO  ZERO
         XC    XTRARECS,XTRARECS ZERO
         B     PARM7
         SPACE 3
PARM6A   DS    0H
         CLC   CAPER,BLANKS   WAS CAPERCENTAGE KEYED??
         BE    SETCAPD        NO-GO SET CA RATIO DEFAULTS...
         LA    R15,CAPER      ADDRESS OF DATA
         LH    R14,=Y(CAPERL) LENGTH OF DATA
         BAL   LINKREG,NUMERIC IS DATA NUMERIC????
         B     BADCAPER       NOT NUMERIC
         BAL   LINKREG,CVB    NUMERIC-CONVERT IT TO BINARY
         C     R1,=F'100'     IS PERCENTAGE GREATER THAN 100
         BH    CAPERHI        YES-ERROR
         A     R1,CIRATIO     ADD CI RATIO TO CA RATIO
         C     R1,=F'100'     ARE THEY COLLECTIVELY GREATER THAN 100
         BH    CICAPRHI       YES-ERROR
         S     R1,CIRATIO     NO- SUB CI RATIO
         ST    R1,CARATIO       SET CA FREESPACE RATIO(INSRT RATE)
         B     XTRAINSR       GO CI CA RATIOS ARE COLLECTIVELY LESS
*                             THAN 100 IF SO THEN GIVE USER FREE RECS
*                             AT THE END OF THE DATASET.....
SETCAPD  DS    0H
         MVC   CARATIO,=F'0'   CARATIO  DEFAULT
         B     XTRAINSR
         SPACE 3
XTRAINSR DS    0H
         OC    CIRATIO,CIRATIO    WAS CI% KEYED?
         BNZ   CONTXTRA           YES-CONT PROCESSING
         OC    CARATIO,CARATIO    WAS CA% KEYED?
         BZ    NULLRECA           NO-BOTH CI% AND CA% NOT KEYED.
CONTXTRA DS    0H
         XC    XTRARECS,XTRARECS  ZERO
         L     R14,CIRATIO
         A     R14,CARATIO
         LA    WORKREG,100
         SR    WORKREG,R14
         OR    WORKREG,WORKREG    DO CI CA RATIO EQUAL 100
         BZ    PARM7              YES-NO XTRA RECS AT END OF DATASET
* THE (ADD RECORD PERCENTAGES FOR CI AND CA FREESPACE CALCULATIONS DO
* NOT ADD UP TO 100, THEREFORE THE NUMBER OF ADD RECORDS NOT ACCOUNTED
* FOR IN THE TWO PERCENTAGES WILL BE CALCULATED TO FIT AT THE END OF
* THE DATASET AND NOT WITHIN CI-CA FREESPACE.
*
         XR    R14,R14            CLEAR
         L     R15,#RECADD        SET FOR MULTIPLY
         MR    R14,WORKREG        #RECADD X (100 - (CIRATIO + CARATIO))
         D     R14,=F'100'        FIND HOW MANY XTRA RECS AT END DSET
         ST    R15,XTRARECS       STORE XTRARECS VALUE FOR FUTURE CALC
* NOW THAT THE EXTRA RECORDS SLOTS TO BE LEFT AT THE END OF THE DATASET
* HAS BEEN CALCULATED THE CIRATIO AND CARATIO MUST BE RATIOED SO THEY
* ADD UP TO 100.
* THE CALCULATION FOR ADJUSTMENT IS:
*
*   CIRATIO = (CIRATIO * 100)  /  (CIRATIO + CARATIO)   (DON'T ROUND)
*
*   CARATIO = (CARATIO * 100)  /  (CIRATIO + CARATIO)   (ROUNDED UP)
*
*
         L     WORKREG,CIRATIO     SET UP DIVISOR
         A     WORKREG,CARATIO     WORKREG = (CIRATIO + CARATIO)
         XR    R14,R14             CLEAR FOR MULTIPLY
         L     R15,CIRATIO         SET UP FOR MULTIPLY
         MH    R15,=H'100'         MULTIPLY BY 100 FOR PERCENTAGE
         DR    R14,WORKREG         DIVIDE GIVING ADJUSTED PERCENTAGE
         ST    R15,CIRATIO         SAVE NEW PERCENTAGE
         XR    R14,R14             CLEAR FOR MULTIPLY
         L     R15,CARATIO         SET UP FOR MULTIPLY
         MH    R15,=H'100'         MULTIPLY BY 100 FOR PERCENTAGE
         DR    R14,WORKREG         DIVIDE GIVING ADJUSTED PERCENTAGE
         OR    R14,R14             CHECK IF ROUNDING NEEDED
         BZ    BYRATRND            NO - BYPASS ROUNDING UP
         LA    R15,1(R15)          ROUND UP
BYRATRND DS    0H
         ST    R15,CARATIO         SAVE NEW PERCENTAGE
         B     PARM7
         SPACE 3
NULLRECA DS    0H
         L     WORKREG,#RECLD     CI AND CA ADD REC %'S WERE NOT KEYED
         A     WORKREG,#RECADD    SO RECORDS T/B ADDED WILL BE ADDED TO
         ST    WORKREG,#RECLD     RECORDS T/B LOADED. AND THEN MADE 0
         XC    #RECADD,#RECADD
         B     PARM7
         SPACE 3
PARM7    DS    0H
         LA    R1,DEVLSTWK              START OF WORK DEVICE LIST
         XR    R14,R14
         ICM   R14,3,0(R1)              NO OF DEVICES IN LIST
         LA    R1,2(R1)                 FIRST ENTRY IN DEVICE LIST
DEVLOOP  DS    0H
         CLC   DEVTYPE,0(R1)            IS DEVICE IN LIST
         BE    SETDEVIC                 YES GO SETUP AS ACTIVE
         LA    R1,8(R1)                 NEXT ENTRY IN LIST
         BCT   R14,DEVLOOP              GO CHECK IT OUT
         B     BADDEV                   DEVICE TYPE NOT IN LIST
SETDEVIC DS    0H
         MVC   DEVICE,DEVTYPE           SET DEVICE AS KEYED
         MVC   DEVREGSV,4(R1)           SAVE WORK DEVICE TABLE ADDR
         B     PARM8
PARM8    DS    0H
         OC    IMB,BLANKS          MAKE UPPERCASE
         CLC   IMB,BLANKS          WAS IMBED KEYED
         BE    SETNOIMB            NO-SET TO NOIMBED
         CLC   IMB,=C'NO '         WAS NO KEYED
         BE    SETNOIMB            YES-GO SET
         CLC   IMB,=C'YES'         WAS YES KEYED
         BNE   BADIMB              NO-ERROR IS SPECIFICATION
         MVC   IMBFCTR,=H'1'       YES -SET IMBED FACTOR
         B     PARM9
SETNOIMB DS    0H
         XC    IMBFCTR,IMBFCTR     SET IMBED FACTOR TO ZERO
         B     PARM9
*
PARM9    DS    0H
         OC   REPL,BLANKS          MAKE UPPERCASE
         CLC  REPL,BLANKS          WAS REPLICATE KEYED
         BE    SETNOREP            NO-SET TO NOREPL
         CLC  REPL,=C'NO '         WAS NO KEYED
         BE    SETNOREP            YES-GO SET
         CLC  REPL,=C'YES'         WAS YES KEYED
         BNE   BADREPL             NO-ERROR IS SPECIFICATION
         MVC   REPLFCTR,=H'1'      YES -SET REPLICATE FACTOR
         B     RESTMENU       GO RESET MENU FIELDS
SETNOREP DS    0H
         XC    REPLFCTR,REPLFCTR   SET REPLICATE FACTOR TO ZERO
         B     RESTMENU
* CONVERT A PARAMETER TO BINARY
CVB      DS    0H
         XR    R1,R1               CLEAR
         LA    WORKREG,0(R14,R15)  END OF DATA + 1
         BCTR  WORKREG,0           END OF DATA
CVBCOMP  DS    0H
         CLI   0(WORKREG),C' '     IS THERE A BLANK
         BNE   CVBCONT             NO-CONTINUE
         BCTR  R14,0               YES- SUB 1 FROM DATALENGTH
         BCTR  WORKREG,0           BACKUP 1 BYTE
         CR    WORKREG,R15         ARE WE AT THE BEGINNING OF DATA
         BLR   LINKREG             YES-EXIT DATA IS ALL BLANK
         B     CVBCOMP        NO-GO FIND NON-BLANK
CVBCONT  DS    0H
         IC    R1,PACKPARM+1       SET TO INSTRUCTION LENGHTS
         AR    R1,R14              ADD LENGTH OF KEYED DATA (OPERAND 2)
         BCTR  R1,0                SUB 1 FOR EXECUTE  INSTRUCTION
         EX    R1,PACKPARM         DO VARIABLE PACK
         CVB   R1,DOUBLE           CONVERT PARM VALUE TO BINARY
         BR    LINKREG
PACKPARM PACK  DOUBLE,0(0,R15)     VARIABLE LENGTH PACK
         SPACE 3
NUMERIC  DS    0H
         BCTR  R14,0               SUB 1 FOR EXECUTE
         EX    R14,NUMTRT          GO CHECK FOR ALL NUMERICS OR BLANKS
         LA    R14,1(R14)          RESET TO DATALENGTH
         BNZR  LINKREG             GO TO ERROR BRANCH(NOT NUMERIC)
         B     4(LINKREG)          NUMERIC BRANCH
NUMTRT   TRT   0(0,R15),NUMTABLE
NUMTABLE DS    0CL256
         DC    CL64' '
         DC    X'00'      BLANK
         DC    CL175' '
         DC    10X'00'    0 THRU 9
         DC    CL6' '
RESTMENU DS    0H
* THIS ROUTINE WILL RESET ALL KEYABLE FIELDS IN THE MENU SCREEN
* TO ASKIP,MDTOFF BECAUSE NO FIELDS WERE IN ERROR.........
*
         MVC   RECAA,ASKIPOFF     RESET ATTRIBUTE
         MVC   RECMA,ASKIPOFF     RESET ATTRIBUTE
         MVC   RECLOADA,ASKIPOFF  RESET ATTRIBUTE
         MVC   RECADDA,ASKIPOFF   RESET ATTRIBUTE
         MVC   DEVTYPEA,ASKIPOFF  RESET ATTRIBUTE
         MVC   KEYLA,ASKIPOFF     RESET ATTRIBUTE
         MVC   CIPERA,ASKIPOFF    RESET ATTRIBUTE
         MVC   CAPERA,ASKIPOFF    RESET ATTRIBUTE
         MVC   IMBA,ASKIPOFF      RESET ATTRIBUTE
         MVC   REPLA,ASKIPOFF     RESET ATTRIBUTE
         MVC   ERRORA,ASKIPOFF    RESET ATTRIBUTE
         MVC   ERROR,MENU+ERROR-MENUD
         LR    R1,SCRNREG         MENU SCREEN ADDRESS
         L     R0,LMENUD          MENU SCREEN LENGTH
         ICM   R1,8,FULLSCR       TPUT,FULLSCR OPTION BYTE
         TPUT  ((R1)),((R0)),R
         B     CKDEVTYP           KEEP ON T
         SF    (ASKIP,MDTOFF,HI)
         ORG   *-1                LOCATION TO ATTRIBUTE
ASKIPOFF DS    0X                 SYMBOLIC REFERENCE TO ATTRIBUTE BYTE
         ORG
         EJECT
* INPUT ERROR ROUTINES...DESCRIPTIVE MESSAGE OF ERROR
* CONDITION WILL BE DISPLAYED ON TERMINAL........
*
*
BADKLEN  DS    0H
         MVC   ERROR,KLENMSG
         MVC   ADDRESS,KEYLS+1    3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
BADCIPER DS    0H
         MVC   ERROR,CIPMSG
         MVC   ADDRESS,CIPERS+1   3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
CIPERHI  DS    0H
         MVC   ERROR,CIPHIMSG
         MVC   ADDRESS,CIPERS+1   3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
BADCAPER DS    0H
         MVC   ERROR,CAPMSG
         MVC   ADDRESS,CAPERS+1   3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
CAPERHI  DS    0H
         MVC   ERROR,CAPHIMSG
         MVC   ADDRESS,CAPERS+1   3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
CICAPRHI DS    0H
         MVC   ERROR,CICAHMSG
         MVC   ADDRESS,CIPERS+1   3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
HIKLEN   DS    0H
         MVC   ERROR,HIKEYMSG
         MVC   ADDRESS,KEYLS+1    3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
BADMAXRC DS    0H
         MVC   ERROR,BADKYMSG
         MVC   ADDRESS,KEYLS+1    3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
BADDEV   DS    0H
         MVC   ERROR,DEVMSG
         MVC   ADDRESS,DEVTYPES+1 3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
BADRECSA DS    0H
         MVC   ERROR,RECAMSG
         MVC   ADDRESS,RECAS+1    3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
RECAZERO DS    0H
         MVC   ERROR,RECAZMSG
         MVC   ADDRESS,RECAS+1    3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
BADRECSM DS    0H
         MVC   ERROR,RECMMSG
         MVC   ADDRESS,RECMS+1    3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
RECMLARG DS    0H
         MVC   ERROR,RECMLMSG
         MVC   ADDRESS,RECMS+1    3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
RECALARG DS    0H
         MVC   ERROR,RECALMSG
         MVC   ADDRESS,RECAS+1    3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
BADIMB   DS    0H
         MVC   ERROR,IMBMSG
         MVC   ADDRESS,IMBS+1     3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
BADREPL  DS    0H
         MVC   ERROR,REPLMSG
         MVC   ADDRESS,REPLS+1    3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
BADRECLD DS    0H
         MVC   ERROR,RECLDMSG
         MVC   ADDRESS,RECLOADS+1 3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
BADRECAD DS    0H
         MVC   ERROR,RECADMSG
         MVC   ADDRESS,RECADDS+1  3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
NORECLD  DS    0H
         MVC   ERROR,NRECLMSG
         MVC   ADDRESS,RECLOADS+1 3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
RECLZERO DS    0H
         MVC   ERROR,RECLZMSG
         MVC   ADDRESS,RECLOADS+1 3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
NORECA   DS    0H
         MVC   ERROR,NRECAMSG
         MVC   ADDRESS,RECAS+1    3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
LORECM   DS    0H
         MVC   ERROR,LORECMSG
         MVC   ADDRESS,RECMS+1    3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         EJECT
SBAMINUS DS    0H                 THIS ROUTINE SUBS 1 FROM 3270 ADDR
         ST    R15,SAVEREG        SAVE
         XR    R15,R15            CLEAR
         XR    R14,R14            CLEAR
         IC    R14,ADDRESS        FIRST 3270 ADDRESS BYTE
         N     R14,=X'0000003F'   TURN OFF UNUSED BITS 0-1
         IC    R15,ADDRESS+1      SECOND 3270 ADDRESS BYTE
         N     R15,=X'0000003F'   TURN OFF UNUSED BITS 0-1
         SLL   R14,6
         AR    R14,R15            RELATIVE BUFFER POSITION
         BCTR  R14,0              SUB 1 FROM RELATIVE POSITION
         SPACE 3
         XR    R15,R15            CLEAR
         SLL   R14,20
         SRDL  R14,26
         SRL   R15,26
         STC   R14,ADDRESS
         STC   R15,ADDRESS+1
         TR    ADDRESS,BUFTABLE
         L     R15,SAVEREG        RESTORE BEFORE EXITING
         BR    LINKREG
         EJECT
SBAPLUS  DS    0H                 THIS ROUTINE ADDS 1 TO 3270 ADDR
         XR    R15,R15            CLEAR
         XR    R14,R14            CLEAR
         IC    R14,ADDRESS        FIRST 3270 ADDRESS BYTE
         N     R14,=X'0000003F'   TURN OFF UNUSED BITS 0-1
         IC    R15,ADDRESS+1      SECOND 3270 ADDRESS BYTE
         N     R15,=X'0000003F'   TURN OFF UNUSED BITS 0-1
         SLL   R14,6
         AR    R14,R15            RELATIVE BUFFER POSITION
         LA    R14,1(R14)         ADD 1 TO RELATIVE POSITION
         SPACE 3
         XR    R15,R15            CLEAR
         SLL   R14,20
         SRDL  R14,26
         SRL   R15,26
         STC   R14,ADDRESS
         STC   R15,ADDRESS+1
         TR    ADDRESS,BUFTABLE
         BR    LINKREG
         EJECT
* THIS ROUTINE WILL SET UP THE CORRECT DEVICE TABLE POINTERS ACCORDING
* TO THE DEVICE TYPE SPECIFIED BY INPUT OR THE DEFAULT DEVICE TYPE..
*
*
CKDEVTYP DS    0H
         USING DEVENTRY,DEVREG     ADDRESSABILITY FOR DEVICE DSECT
         L     DEVREG,DEVREGSV     POINT TO BEGINNING OF DEVICE TABLE
         LA    DEVREG,DEVTABLN-4(DEVREG) BLOCK OVERHEAD VALUE
         MVC   BLKOVRHD,0(DEVREG)  BLOCK OVERHEAD IN BYTES
         SH    DEVREG,=H'4'        TRACK CAPACITY VALUE
         MVC   TRKCAP,0(DEVREG) MOVE IT
         SH    DEVREG,=H'2'   TRACKS PER CYLINDER VALUE
         LH    WORKREG,0(DEVREG)   GET TRACKS PER CYLINDER
         ST    WORKREG,TRKCYL      SAVE IT
         SH    DEVREG,=H'2'        DEVICE TYPE CODE
         MVC   DEVCODE,0(DEVREG)   MOVE IT
         B     NULLCISZ            GO NULLIFY DCISZ TABLE ENTRIES
         EJECT
* THIS ROUTINE WILL SCAN THE DATA CI SIZE FIELD IN EACH ENTRY OF THE
* DEVICE TABLE TO SEE IF THE DATA CI SIZE IS TO SMALL FOR THE
* MAXRECSZ+7.. IF SO THEN THAT DATA CI SIZE ENTRY WILL NULLED(ZEROS)
* SO THAT IT WILL NOT BE CONSIDERED WHEN FINDING THE BEST DATA CI SIZES
*
*
NULLCISZ DS    0H
         L     DEVREG,DEVREGSV         FIRST TABLE ENTRY
         L     R15,MAXRECSZ
         LA    R15,7(R15)              ADD 7 TO MAXRECSZ
NULLLOOP DS    0H
         CLC   DCISZ,=F'-1'            ARE WE AT THE END OF THE TABLE
         BE    PROCCISZ                YES- GO PROCESS DCISZ'S IN TABLE
         C     R15,DCISZ               IS MAXRECSZ+7 GREATER THAN DCISZ
         BNH   PROCCISZ                NO-GO PROCESS DCISZ'S IN TABLE
         XC    DCISZ,DCISZ             YES-NULLIFY THIS TABLE ENTRY
         LA    DEVREG,DEVENLEN(DEVREG) POINT TO THE NEXT TABLE ENTRY
         B     NULLLOOP                GO LOOK AGAIN
         EJECT
* THIS ROUTINE WILL PROCESS EACH DATA CI SIZE ENTRY IN THE DEVICE TABLE
* IN ORDER TO DETERMINE WHICH DATA CI SIZES ARE THE BEST CHOICES FOR
* THE INDICATED RECORDSIZES........
*
*
PROCCISZ DS    0H
         L     DEVREG,DEVREGSV     SET TO TOP OF DEVICE TABLE
DCISZP2  OC    DCISZ,DCISZ         DOES THIS DCISZ QUALIFY
         BZ    NXTDCISZ            NO -  GO BUMP TO NEXT ENTRY
         CLC   DCISZ,=F'-1'        IS IT THE END OF THE DEVICE TABLE
         BE    PUTHEADR            YES - GO DISPLAY THE INFO
         MVC   DCARATIO,CARATIO    MOVE PERCENTAGE OF ADD RECORDS
         MVC   DCIRATIO,CIRATIO    NEEDED IN CI-CA FREESPACE FOR EACH
*                                  DCISZ TABLE ENTRY
         BAL   LINKREG,CALC#0D     GO CALCULATE #TRKCA,#CICA,#RCINFSP,
*                                  #RCANFSP
         B     DCALC#4
DCALC#1  DS    0H
         XC    #CICA(DEVCLLEN),#CICA CLEAR FOR RE-CALCULATION
         BAL   LINKREG,CALC#1D     GO CALCULATE #CICA
         BAL   LINKREG,CALC#2D     GO CALCULATE #RCINFSP
         BAL   LINKREG,CALC#3D     GO CALCULATE #RCANFSP
DCALC#4  DS    0H
         BAL   LINKREG,CALC#4D     GO CALCULATE #TRKNFSP
         OC    #RECADD,#RECADD     ARE RECORDS GOING TO BE ADDED
         BZ    NOFREESP            NO- GO PROCESS NO FREESPACE LOGIC
* RECORDS TO BE ADDED WAS SPECIFIED(RECADD INPUT KEYWORD)
* SO WE MUST CALCULATE HOW MUCH FREESPACE WILL BE NEEDED TO ADD THE
* RECORDS SPECIFIED TO THE PRIMARY DATASET ALLOCATION
         BAL   LINKREG,CALC#5D     GO CALCULATE PERRADD
         BAL   LINKREG,CALC#6D     GO CALCULATE #FRCA
         BAL   LINKREG,CALC#7D     GO CALCULATE PERFSPCA
         BAL   LINKREG,CALC#8D     GO CALCULATE PERFSPC2
         BAL   LINKREG,CALC#9D     GO CALCULATE #RCILOAD
         BAL   LINKREG,CALC#10D    GO CALCULATE #RCALOAD
         BAL   LINKREG,CALC#11D    GO CALCULATE #TRKXTRA
         BAL   LINKREG,CALC#12D    GO CALCULATE #TRKDATA
         EJECT
CALCINDX DS    0H
         OC    KEYLEN,KEYLEN       IS THIS A KSDS/AIX?
         BZ    NXTDCISZ            NO-GO PROCESS NEXT DCISZ
* SET UP PARAMETER LIST TO LINK TO PL/1 INDEX CALCULATION ROUTINE.
* ALL PARAMETERS PASSED ARE FIXED BIN(15,0) OR (31,0).
         LA    WORKREG,KEYLEN
         ST    WORKREG,PLIPLIST       PARM. 1  FIXED BIN(15,0)
         LA    WORKREG,IMBFCTR
         ST    WORKREG,PLIPLIST+4*1   PARM. 2  FIXED BIN(15,0)
         LA    WORKREG,REPLFCTR
         ST    WORKREG,PLIPLIST+4*2   PARM. 3  FIXED BIN(15,0)
         LA    WORKREG,#CICA
         ST    WORKREG,PLIPLIST+4*3   PARM. 4  FIXED BIN(31,0)
         LA    WORKREG,#TRKDATA
         ST    WORKREG,PLIPLIST+4*4   PARM. 5  FIXED BIN(31,0)
         LA    WORKREG,#TRKCA
         ST    WORKREG,PLIPLIST+4*5   PARM. 6  FIXED BIN(31,0)
         LA    WORKREG,#TRKINDX
         ST    WORKREG,PLIPLIST+4*6   PARM. 7  FIXED BIN(31,0)
         LA    WORKREG,TRKCAP
         ST    WORKREG,PLIPLIST+4*7   PARM. 8  FIXED BIN(31,0)
         LA    WORKREG,BLKOVRHD
         ST    WORKREG,PLIPLIST+4*8   PARM. 9  FIXED BIN(31,0)
         LA    WORKREG,#INDXLV
         ST    WORKREG,PLIPLIST+4*9   PARM. 10 FIXED BIN(31,0)
         LA    WORKREG,ICISZ
         ST    WORKREG,PLIPLIST+4*10  PARM. 11 FIXED BIN(31,0)
         LA    WORKREG,#CIHIGHL
         ST    WORKREG,PLIPLIST+4*11  PARM. 12 FIXED BIN(31,0)
         MVI   PLIPLIST+4*11,X'80'    FLAG LAST PARM IN LIST
*         L     R15,=V(IHESAPB)        PLI ENTRY POINT(VSAMANDX)
         L     R15,=V(PLICALLA)       PLI ENTRY POINT(VSAMANDX)
         LA    R1,PLIPLIST            PARAMETER LIST
         BALR  R14,R15                GO DO INDEX CALCULATIONS
         OC    #INDXLV,#INDXLV        WAS INDEX CISZ TO LARGE
         BNZ   INDXSTAR               NO GO PROCESS NEXT DCISZ
* INDEX CISZ WAS TO COMPUTED AND FOUND TO BE LARGER THAN THE MAXIMUM
* ALLOWED (4096). SO NOW WE DECRESE THE SIZE OF THE DATA CONTROL
* AREA BY 1 TRK AND TRY OUR  CALCULATIONS AGAIN.
         L     WORKREG,#TRKCA         CONTROL AREA SIZE(IN TRACKS)
         SH    WORKREG,=H'1'          SUB 1 TRK
         BP    NEWTRKCA               IF NOT NEGATIVE OR ZERO GO
*                                     RETRY CALCULATIONS, OTHERWISE
         XC    DCISZ,DCISZ            NULLIFY THIS DCISZ(NOT ACCEPTED)
INDXSTAR DS    0H                     CHECK NUMBER OF INDEX TRACKS
         OC    BLKOVRHD,BLKOVRHD      DO WE HAVE A BLOCK OVERHEAD?
         BNZ   NXTDCISZ               NO, #TRKINDX IS VALID
         OC    #TRKINDX,#TRKINDX   INDEX TRACKS CALCULATED?
         BNZ   NXTDCISZ            YES, CARRY ON
         TRKCALC FUNCTN=TRKCAP,    FIND TRACK CAPACITY                 +
               TYPE=DEVCODE,       FOR THIS DEVICE                     +
               R=1,K=0,DD=ICISZ+2, WITH THIS INDEX                     +
               REGSAVE=YES,        SAVE THE REGISTERS                  +
               MF=(E,TRKCALST)     USING A REMOTE LIST
         LTR   R15,R15             GET AN ANSWER?
         BNZ   NXTDCISZ            NO, FORGET IT
         SR    EVENODD,EVENODD     CLEAR EVEN REGISTER
         LR    DIVISOR,R0          GET RECORDS PER TRACK
         L     DIVIDEND,#CIHIGHL   GET TOTAL NUMBER OF RECORDS
         DR    EVENODD,DIVISOR     CALCULATE TRACKS REQUIRED
         LTR   REMAINDR,REMAINDR   ANY REMAINDER?
         BZ    *+4+4               NO, NO ADJUSTMENT NEEDED
         LA    QUOTIENT,1(QUOTIENT) YES, CANNOT HAVE PART OF A TRACK
         ST    QUOTIENT,#TRKINDX   SAVE NUMBER OF INDEX TRACKS
         EJECT
NXTDCISZ DS    0H
         LA    DEVREG,DEVENLEN(DEVREG)  POINT TO NEXT TABLE ENTRY
         B     DCISZP2
NEWTRKCA DS    0H
         ST    WORKREG,#TRKCA         NEW TRK/CA VALUE
         B     DCALC#1                GO RECALCULATE
         SPACE 3
NOFREESP DS    0H
         MVC   #RCILOAD,#RCINFSP        SAME AS NO FREESPACE
         MVC   #RCALOAD,#RCANFSP        SAME AS NO FREESPACE
         MVC   #TRKDATA,#TRKNFSP        SAME AS NO FREESPACE
         B     CALCINDX                 GO CALC INDEX IF NECESSARY
         EJECT
CALC#0D  DS    0H
*
* THIS ROUTINE WILL CALCULATE THE NUMBER OF TRACKS PER CONTROL AREA
*
* THE VALUE IS DETERMINED BY THE SMALLER OF:
*
*        NUMBER OF TRACKS PER CYLINDER.
*
*                    OR
*
*        NUMBER OF TRACKS NEEDED TO CONTAIN (#RECLD + #RECADD).
*
*
* THIS CALCULATION WILL FORCE A CA SIZE OF 1 CYL IF (#RECLD + #RECADD)
* WILL NEED 1 CYLINDER OF MORE OF SPACE
*
*
*
* NOTE: IF (#RECLD+#RECADD) IS LESS THAN OR EQUAL TO THE #RCINFSP VALUE
* (NUMBER OF RECORDS THAT FIT IN A CI WITH NO FREESPACE)
* FOR THE CURRENT DCISZ TABLE ENTRY THEN THIS ENTRY(IF NOT THE FIRST)
* AND ALL SUBSEQUENT DCISZ TABLE
* ENTRIES WILL BE NULLED(ZEROS) AND THE PROCESSING OF THE DCISZ TABLE
* WILL BE STOPPED AND ALL VALID DCISZ'S WILL BE DISPLAYED TO THE USER
* FOR SELECTION.
*
         ST    LINKREG,DOUBLE   SAVE OUR LINK REGISTER
         BAL   LINKREG,CALC#1D      GO CALC #CICA
         BAL   LINKREG,CALC#2D      GO CALC #RCINFSP
         BAL   LINKREG,CALC#3D      GO CALC #RCANFSP
         L     WORKREG,#RECLD
         A     WORKREG,#RECADD
         C     WORKREG,#RCINFSP     WILL 1 CI HANDLE ALL RECORDS
         BNH   NULL#0D              YES - GO NULL THE REMAINING DCISZ'S
BYNULLCK DS    0H
         C     WORKREG,#RCANFSP     IS CASIZE(TRACKS) TO LARGE FOR
*                                   TOTAL RECORD SPACE NEEDED
         BNL   EXITC#0              NO - WERE ALL THRU HERE
         OC    DCIRATIO,DCIRATIO    IF TRKS NEEDED FOR DATASET IS LESS
         BNZ   EXITC#0              THAN TRKS PER CYL AND FREESPACE
         OC    DCARATIO,DCARATIO    WAS REQUEST (CI OR CA) THEN USE
         BNZ   EXITC#0              TRKS PER CYL.
         LH    R1,IMBFCTR           IMBED FACTOR (IF SPECIFIED)
*                                   REGISTER 1 NOW CONTAINS THE MINIMUM
*                                   ALLOWABLE CASIZE - 1
RE#TRKCA DS    0H                   OTHERWISE,
         L     R8,#TRKCA            WE MUST DECREASE THE CASIZE BY
         SH    R8,=H'1'             1 TRACK UNTIL WE GET A GOOD FIT.
         CR    R8,R1                ARE BELOW THE MIMIMUM CASIZE
         BE    MINTRKCA             IF IF SO GO MAKE IT THE MINIMUM
         ST    R8,#TRKCA            OTHERWISE SAVE NEW #TRKCA
         ST    WORKREG,DOUBLE+4     SAVE WORKREG
         BAL   LINKREG,CALC#1D      GO RE-CALC #CICA
         BAL   LINKREG,CALC#2D      GO RE-CALC #RCINFSP
         BAL   LINKREG,CALC#3D      GO RE-CALC #RCANFSP
         L     WORKREG,DOUBLE+4     RESTORE WORKREG
         C     WORKREG,#RCANFSP     IS CASIZE(TRACKS) STILL TO LARGE
         BL    RE#TRKCA             YES - GO MAKE IT SMALLER
         BE    EXITC#0              NO - ITS JUST WHERE WE WANT IT
*                                   NO - NOW ITS TO SMALL SO
         L     R8,#TRKCA            PUT IT BACK IN REGISTER
         LA    R8,1(R8)             INCREASE IT BY 1 TRACK
         ST    R8,#TRKCA            STORE FINAL VALUE AND DO
*                                   LAST RE-CALCULATION FOR:
         BAL   LINKREG,CALC#1D      #CICA
         BAL   LINKREG,CALC#2D      #RCINFSP
         BAL   LINKREG,CALC#3D      #RCANFSP
         B     EXITC#0              AND EXIT THIS ROUTINE
MINTRKCA DS    0H                   BECAUSE ITS NOW TO SMALL
         LA    R8,1(R8)             SO LETS INCREASE IT BY 1 TRACK
         ST    R8,#TRKCA            AND CALL IT A DAY
EXITC#0  DS    0H                   EXIT CALCULATION
         L     LINKREG,DOUBLE       RESTORE LINK REGISTER
         BR    LINKREG              AND GO BACK TO WHEREVER
NULL#0D  DS    0H
         ST    DEVREG,DOUBLE+4      SAVE CURRENT DCISZ ENTRY PTR
NULL#0DL DS    0H
         LA    DEVREG,DEVENLEN(DEVREG) POINT TO NEXT DCISZ ENTRY
         CLC   DCISZ,=F'-1'       IS IT THE END OF THE TABLE
         BE    NULL#0DE           YES - GO RESET DCISZ PTR
         XC    DCISZ,DCISZ        NULL THIS DCISZ ENTRY (TO LARGE)
         B     NULL#0DL           AND GO CHECK OUT THE NEXT ENTRY...
NULL#0DE DS    0H
         L     DEVREG,DOUBLE+4    RESTORE PTR TO LAST GOOD DCISZ ENTRY
         B     BYNULLCK           GO FINISH UP WITH THIS DCISZ
         EJECT
CALC#1D  DS    0H       ROUNDED DOWN
* #CICA= #TRKCA*#BLKSTRK/(DCISZ/PHYBLKSZ)
* THIS ROUTINE WILL CALCULATE THE NUMBER OF CT'S PER CA FOR THE CONTROL
* INTERVAL SIZE DEVICE TABLE ENTRY
         L     DIVIDEND,#TRKCA     NUMBER OF TRACKS PER CONTROL AREA
         SH    DIVIDEND,IMBFCTR    SUB IMBED FACTOR (IF ANY)
         M     EVENODD,#BLKSTRK    MULTIPLY BY THE NUMBER OF PHYSICAL
         LR    WORKREG,QUOTIENT    BLOCKS PER TRACK
         L     DIVIDEND,DCISZ      LOAD FOR DIVIDE
         XR    EVENODD,EVENODD     CLEAR
         L     DIVISOR,PHYBLKSZ    LOAD FOR DIVIDE
         DR    EVENODD,DIVISOR     DIVIDE DCISZ BY PHYSBLK SIZE
         LR    DIVISOR,QUOTIENT    USE RESULT FOR NEXT DIVIDE
         XR    EVENODD,EVENODD     CLEAR
         LR    DIVIDEND,WORKREG    NUMBER OF PHYSICAL BLOCKS PER CA
         DR    EVENODD,DIVISOR     DIVIDE PHYSBLKS/CA BY PHYSBLKS/CI
         ST    QUOTIENT,#CICA      GIVING #CICA(ROUNDED DOWN)
         BR    LINKREG             GO BACK
         SPACE 3
CALC#2D  DS    0H  ROUNDED DOWN                 ROUNDED DOWN
* #RCINFSP = (DCISZ-10)/(RECSZ) FIXED OR (DCISZ-4)/(RECSZ+3) VARIABL
* THIS ROUTINE WILL CALCULATE THE NUMBER OF RECORDS THAT CAN FIT IN A
* CONTROL INTERVAL WITH NO FREESPACE SPECIFIED
         XR    EVENODD,EVENODD     CLEAR
         L     DIVIDEND,DCISZ      DATA CONTROL INTERVAL SIZE
         L     DIVISOR,RECSZ       AVERAGE RECORD SIZE
         CLC   RECSZ,MAXRECSZ      IS IT FIXED LENGTH RECORDS
         BNE   VAR#2D              NO - GO DO VARIABLE LENGTH LOGIC
* FIXED LENGTH RECORDS
         SH    DIVIDEND,=H'07'     SUB LENGTH OF CIDF+RDF FIELDS
         B     DOCALC#2
* VARIBLE LENGTH RECORDS
VAR#2D   DS    0H
         SH    DIVIDEND,=H'4'      SUB LENGTH OF CIDF FIELD
         LA    DIVISOR,3(DIVISOR)  ADD LENGTH OF RDF  FIELD
DOCALC#2 DS    0H
         DR    EVENODD,DIVISOR     DIVIDE DCISZ BY RECSZ
         ST    QUOTIENT,#RCINFSP   GIVING #RCINFSP(ROUNDED DOWN)
         BR    LINKREG
         EJECT
CALC#3D  DS    0H
*  #RCANFSP =  #RCINFSP * #CICA
* THIS ROUTINE WILL CALCULATE THE NUMBER OF RECORDS PER CONTROL AREA
* WITH NO FREESPACE
*
         L     DIVIDEND,#RCINFSP
         M     EVENODD,#CICA
         ST    QUOTIENT,#RCANFSP
         BR    LINKREG
         SPACE 3
CALC#4D  DS    0H ROUNDED UP (PUTS #TRKNFSP ON CA BOUNDARY)
*  #TRKNFSP =  #RECLD/#RCANFSP*#TRKCA
* THIS ROUTINE WILL CALCULATE THE NUMBER OF TRACKS NEEDED FOR THE DATA
* COMPONENT (NO FREESPACE)
         L     DIVISOR,#RCANFSP
         XR    EVENODD,EVENODD     CLEAR
         L     DIVIDEND,#RECLD
         DR    EVENODD,DIVISOR
         OR    REMAINDR,REMAINDR   IS THIS THERE A REMINDER
         BZ    MULT#4D             NO-GO MULTIPLY
         LA    QUOTIENT,1(QUOTIENT) YES ADD 1 TO QUOTIENT
MULT#4D  M     EVENODD,#TRKCA      MULT BY TRACK PER CA
         ST    QUOTIENT,#TRKNFSP   GIVING #TRKNFSP
         BR    LINKREG
         SPACE 3
CALC#5D  DS    0H        ROUNDED UP
*  PERRADD =  (#RECADD-XTRARECS)/(#RECLD+#RECADD-XTRARECS)
* THIS ROUTINE CALCULATES THE INSERTION RATE USING THE RECADD FIELD
* OBTAINED FROM INPUT......
*
*
         XR    EVENODD,EVENODD
         L     DIVISOR,#RECLD
         L    DIVIDEND,#RECADD
         S    DIVIDEND,XTRARECS
         AR    DIVISOR,DIVIDEND ADD RECADD TO RECLOADED FOR INSRT RATE
         MH   DIVIDEND,=H'100'          MULT DIVIDEND BY 100
         DR    EVENODD,DIVISOR          YES- FIND PERCENTAGE
         OR    REMAINDR,REMAINDR        IS THERE A REMAINDER
         BZ    SETPERCT                 NO-GO SET PERCENTAGE
         LA    QUOTIENT,1(QUOTIENT)     YES-ROUND UP
SETPERCT DS    0H
         ST    QUOTIENT,PERRADD         SET PERCENTAGE
         BR    LINKREG
         EJECT
CALC#6D  DS    0H     ROUNDED DOWN
*  #FRCA =  #RCANFSP*PERRADD
*  THIS ROUTINE WILL DETERMINE THE NUMBER OF FREE RECORDS NEEDED PER
*  CONTROL AREA
*
         XR    EVENODD,EVENODD     CLEAR
         L     DIVIDEND,#RCANFSP   NUMBER OF RECORDS PER CA
         M     EVENODD,PERRADD     MULT BY PERCENT OF RECADD TO RECLOAD
         LH    DIVISOR,=H'100'     SET FOR TRUE PERCENTAGE CALC
         DR    EVENODD,DIVISOR     DIVIDE BY 100
         OC    DCIRATIO,DCIRATIO   ANY CI FREESPACE NEEDED??
         BNZ   BYRND#6             YES - THAT WILL TAKE CARE OF ANY
*                                  RECORD FRACTIONS.
         OR    REMAINDR,REMAINDR   NO  -  CI FREESPACE WILL NOT BE
*                                  CALCULATED SO IF THERE IS A REMAINDR
*                                  (RECORD FRACTION) WE HAVE TO ROUND
*                                  UP FOR THIS CALCULATION.
         BZ    BYRND#6             NO FRACTION (DONT ROUND)
         LA    QUOTIENT,1(QUOTIENT) RECORD FRACTION (ROUND UP)
BYRND#6  DS    0H
         LTR   QUOTIENT,QUOTIENT   IS IT LESS THAN 1
         BZ    SETFRCA1            YES SET #FRCA TO 1
         C     QUOTIENT,#RCANFSP   ARE WE ASKING FOR 100% FREESPACE
         BL    NOSUB#6 NO THEN DONT SUB #CICA
         S     QUOTIENT,#CICA  SUB SO 1 REC/CI IS LOADED
NOSUB#6  DS    0H
         ST    QUOTIENT,#FRCA      SET #FRCA
         M     EVENODD,DCARATIO  NO. FREE RECORDS NEEDED FOR FREE CI'S
         D     EVENODD,=F'100'          TRUE VALUE
         ST    QUOTIENT,FRCAFSP
         L     EVENODD,#FRCA    FREE RECORDS NEEDED PER CA
         SR    EVENODD,QUOTIENT SUB FREE RECORDS NEEDED IN CAFREESPACE
         ST    EVENODD,FRCIFSP  SET NUMBER OF RECORDS NEED IN CIFREESP
         BR    LINKREG
SETFRCA1 DS    0H
         MVC   #FRCA,=F'1'         SET TO | (MINIMUM)
         BR    LINKREG
         EJECT
CALC#7D  DS   0H
* PERFSPCA  = (FRCAFSP/#RCINFSP)/#CICA
* THIS CALCULATION DETERMINES THE CA FREESPACE PERCENTAGE BY
* DETERMINING THE NUMBER OF FREE CI'S NEEDED TO CONTAIN ENOUGH
* RECORDS TO SATISFY THE CARATIO OF THE INSERTION RATE..
*
*
         OC    FRCAFSP,FRCAFSP          DO WE NEED CA FREESPACE
         BZR   LINKREG                  NO GET OUT
         XR    EVENODD,EVENODD          CLEAR
         L     DIVIDEND,FRCAFSP         RECS NEEDED IN CA FREESPACE
         D     EVENODD,#RCINFSP         NO OF FREE CI'S NEEDED
         OR    REMAINDR,REMAINDR        IS THERE A REMAINDER
         BZ    BYRND#71                 NO DONT ROUND
         LA    QUOTIENT,1(QUOTIENT)     YES ROUND UP
BYRND#71 DS    0H
         M     EVENODD,=F'100' FOR TRUE PERCENTAGE
         D     EVENODD,#CICA
         OR    REMAINDR,REMAINDR        IS THERE A REMAINDER
         BZ    BYRND#72                 NO DONT ROUND
         LA    QUOTIENT,1(QUOTIENT)     YES ROUND UP
BYRND#72 DS 0H
         ST    QUOTIENT,PERFSPCA        SET CA FREESPACE PERCENTAGE
         L     DIVIDEND,#CICA           NO OF CI'S PER CA
         M     EVENODD,PERFSPCA         FOR PERCENTAGE
         D     EVENODD,=F'100'          FIND FREE CI'S PER CA
         OR    QUOTIENT,QUOTIENT        IS RESULT ZERO
         BNZ   BYRND#73                 NO DONT ROUND
         LA    QUOTIENT,1(QUOTIENT)     ROUND UP
BYRND#73 DS    0H
         C     QUOTIENT,#CICA        ARE FREE CI'S EQUAL TO MAX CI/CA
         BNE   STORE#7
         BCTR  QUOTIENT,0             SUB 1(CANT BE SAME AS #CICA)
STORE#7  DS    0H
         ST    QUOTIENT,#FCICA          STORE IT
         M     EVENODD,#RCINFSP HOW MANY REC FIT IN FREE CI'S
         ST    QUOTIENT,#RCAFREE SAVE RECS IN FREE CI'S
         S     QUOTIENT,FRCAFSP  SUB FREE RECORDS NEEDED IF FREE CI'S
         BMR   LINKREG           LEAVE ZERO IF NEGATIVE
         ST    QUOTIENT,XRCCAFSP SAVE EXCESS FREE RECORDS IN FREE CI'S
         BR    LINKREG                  GET OUT
         EJECT
CALC#8D  DS    0H
         SPACE 3
********************************************************************
*                                                                  *
*              IF  FRCIFSP = 0 OR,                                 *
*                                                                  *
*              IF  FREE RECS NEEDED PER NON FREE CI IS GREATER OR  *
*                  EQUAL TO #RCINFSP                               *
*                                                                  *
* PERFSPCI = 0.                                                    *
*                                                                  *
*              IF  XRCCAFSP (IS GREATER THAN OR EQUAL TO) FRCIFSP, *
*                                                                  *
* PERFSPCI = FRCIFSP/B*(RECSZ+X)/DCISZ                             *
*                                                                  *
*              IF XRCCAFSP (IS LESS THAN) FRCIFSP,                 *
*                                                                  *
* PERFSPCI = ((FRCIFSP-A)/B))*(RECSZ+C)/DCISZ  ROUNDED UP          *
*                                                                  *
*                                                                  *
********************************************************************
*                                                                  *
*  WHERE:A=(XRCCAFSP/B) DONT ROUND---THE REMAINDER IS ZRCCAFSP     *
*                                                                  *
*        B=(#CICA-#FCICA)                                          *
*                                                                  *
*        C=0 FOR FIXED LENGTH RECORDS                              *
*                                                                  *
*        C=3 FOR VARIABLE LENGTH RECORDS                           *
*                                                                  *
*  AND  : ((FRCIFSP-A)/B)) IS ROUNDED UP IF                        *
*                          REMAINDER IS GREATER THAN ZRCCAFSP      *
*                                                                  *
*                                                                  *
*                                                                  *
*                                                                  *
*                                                                  *
********************************************************************
         OC    FRCIFSP,FRCIFSP          DO WE NEED ANY CI FREESPACE
         BZR   LINKREG                  NO-GET OUT
         CLC   XRCCAFSP,FRCIFSP         EXCESS RECS LESS THAN FRCIFSP
         BL    USEXCESS                 YES-USE EXCESS RECS IN CALC
         XC    XRCCAFSP,XRCCAFSP        ZERO OUT EXCESS FREE RECS
         B     CALC8P2                  BY PASS USING EXCESS FREE RECS
USEXCESS DS    0H
         XR    EVENODD,EVENODD          CLEAR FOR DIVIDE
         L     DIVIDEND,XRCCAFSP        WHAT WE ARE DIVIDING INTO
         L     DIVISOR,#CICA
         S     DIVISOR,#FCICA  DIVISOR=B  WHAT WE ARE DIVIDING BY;
*                                       NON-FREE CI'S IN A CA
         DR    EVENODD,DIVISOR          DIVIDE GIVING A
         ST    REMAINDR,ZRCCAFSP        SAVE NEW EXCESS FREE RECS
         ST    QUOTIENT,XRCCAFSP QUOTIENT=A -NO. OF EXCESS FREE RECS
*                                       PER NON-FREE CI IN A CA
CALC8P2  DS    0H
         L     DIVIDEND,FRCIFSP
         XR    EVENODD,EVENODD          CLEAR
         S     QUOTIENT,XRCCAFSP        SUB EXCESS
         DR    EVENODD,DIVISOR          FREE RECS NEEDED NON-FREE CI'S
         C     REMAINDR,ZRCCAFSP        IS REMAINDER GREATER THAN
*                                       THE NEW EXCESS VALUE
         BNH   BYRND#81                 NO-THEN DONT ROUND UP
         LA    QUOTIENT,1(QUOTIENT)     ROUND UP
BYRND#81 DS    0H
         C     QUOTIENT,#RCINFSP        ARE FREE RECS NEEDED GREATER
*                                       OR EQUAL TO MAX RECS-CI
         BNL   NOCIPR                   YES - GO NULL OUT CI FREESPACE
         L     DIVISOR,RECSZ  DIVISOR=C RECORD SIZE
         CLC   RECSZ,MAXRECSZ           CHECK FOR FIXED/VARIABLE
         BE    FIX#8                    FIXED LENGTH
         LA    DIVISOR,3(DIVISOR)       ADD 3 FOR VAR RECS
FIX#8    DS    0H
         MR    EVENODD,DIVISOR          BYTES NEEDED FOR FREE RECS/CI
         M     EVENODD,=F'100'          FOR PERCENTAGE
         L     DIVISOR,DCISZ            DATA CI SIZE
         DR    EVENODD,DIVISOR          PERCENT OF CI
         OR    REMAINDR,REMAINDR        DO WE HAVE A REMAINDER
         BZ    BYRND#82                 NO DONT ROUND
         LA    QUOTIENT,1(QUOTIENT)     YES ROUND UP
BYRND#82 DS    0H
         ST    QUOTIENT,PERFSPCI        SET CI FREESPACE PERCENTAGE
* NOW CALCULATE FREE RECS PER CI USING THE CI FREESPACE PERCENTAGE
         XR    EVENODD,EVENODD     CLEAR
         L     DIVIDEND,DCISZ      SET FOR MULTIPLY
         M     EVENODD,PERFSPCI    NUMBER OF FREE BYTES PER CI * 100
         D     EVENODD,=F'100'     NUMBER OF FREE BYTES PER CI
         L     DIVISOR,RECSZ       SET UP FOR FINAL DIVIDE
         CLC   RECSZ,MAXRECSZ      FIXED OR VARIABLE LENGTH RECS
         BE    FIX#8B              FIXED
         LA    DIVISOR,3(DIVISOR)  VARIABLE
FIX#8B   DS    0H
         XR    EVENODD,EVENODD     CLEAR
         DR    EVENODD,DIVISOR     DIVIDE GIVING NO OF FREE RECS PER CI
         ST    QUOTIENT,#RCIFREE        SAVE FREE RECS-NON FREE CI'S
         BR    LINKREG
NOCIPR   DS    0H
         L     WORKREG,DCARATIO
         A     WORKREG,DCIRATIO    MAKE ALL RECS TO BE ADDED
         ST    WORKREG,DCARATIO    GO INTO CA FREESPACE
         XC    DCIRATIO,DCIRATIO   CLEAR OUT CIRATIO
         B     DCALC#1             GO DO IT ALL OVER FOR THIS DCISZ
         EJECT
CALC#9D  DS    0H
*
* THIS CALCULATION WILL COMPUTE THE NUMBER OF RECORDS THAT CAN FIT IN
* A CI AT LOAD TIME AND THE MAXIMUM NUMBER OF RECORDS THAT CAN FIT IN
* IN A CI TAKING INTO ACCOUNT THE POSSIBLE RECORD FRACTION LOST
* BECAUSE CI FREESPACE.
*
* #RCILOAD = (DCISZ*(1-PERFSPCI)-10)/(RECSZ+0)  FOR FIXED LENGTH RECS
*          = (DCISZ*(1-PERFSPCI)-4)/(RECSZ+3)   FOR VARIABLE LENGTH REC
*
*
* #RCIMAX = (DCISZ - (DCISZ * PERFSPCI / 100)) / RECSZ + #RCILOAD
*    OR
* #RCIMAX = (DCISZ - (DCISZ * PERFSPCI / 100)) / (RECSZ + 3) + #RCILOAD
*
*
         L     DIVIDEND,DCISZ
         LH    WORKREG,=H'100'     SET FOR (1-PERFSPCI)
         S     WORKREG,PERFSPCI    SUB CI FREESPACE PERCENTAGE
         BZ    RCIDFLT1            IF ZERO DEFAULT TO 1 REC-CI-LOAD
         MR    EVENODD,WORKREG     GET NUMBER OF CI BYTES*100 AVAIL LD
         XR    EVENODD,EVENODD     CLEAR
         D     EVENODD,=F'100'     GET NUMBER OF CI'S BYTES AVAIL AT LD
         SH    QUOTIENT,=H'07'     SUB CIDF AND RDF FIELDS
         BNP   CIPER100            IF NOT PLUS MAKE CI FREESPACE 100
         XR    EVENODD,EVENODD     CLEAR
         L     DIVISOR,RECSZ
         CLC   RECSZ,MAXRECSZ      IS IT FIXED LENGTH RECORDS
         BE    FIXCALC9            YES - DONT AD 3 BYTE RDF
         LA    DIVISOR,3(DIVISOR)  NO- VARIABLE SO ADD 3 BYTE RDF
         LA    QUOTIENT,3(QUOTIENT) NEGATE PREVIOUS RDF SUBTRACTION
FIXCALC9 DS    0H
         DR    EVENODD,DIVISOR     PERFORM FINAL CALC
         OR    QUOTIENT,QUOTIENT  IS IT LESS THAN ONE RECORD
         BNZ   STORE                    NO GO STORE IT
CIPER100 DS    0H
         MVC   PERFSPCI,=F'100'  CHANGE CI FREESPACE TO 100 PERCENT
RCIDFLT1 DS    0H
         MVC   #RCILOAD,=F'1'           SET REC PER CI TO MINIMUM
         BR    LINKREG                  GET THE HELL OUT
STORE    DS    0H
         ST    QUOTIENT,#RCILOAD
         BR    LINKREG
         EJECT
CALC#10D DS    0H
* #RCALOAD = #RCILOAD*(#CICA-#FCICA)
* THIS CALCULATION WILL COMPUTE   THE NUMBER OF RECORDS THAT CAN FIT
* IN A CONTROL AREA AT LOAD TIME
*
         XR    EVENODD,EVENODD     CLEAR
         L     WORKREG,#CICA       CI'S PER CA
         S     WORKREG,#FCICA      SUB FREE CI'S PER CA
         L     QUOTIENT,#RCILOAD
         MR    EVENODD,WORKREG     DETERMINE NUMBER OF REC/CA AT LOAD
         ST    QUOTIENT,#RCALOAD
         BR    LINKREG
         SPACE 3
CALC#11D DS    0H
* #TRKXTRA = XTRARECS/#RCANFSP*#TRKCA
* THIS CALCULATION WILL COMPUTE THE NUMBER OF TRACKS NEEDED FOR THE
* XTRA FREE RECS NEEDED AT END OF DATA COMPONENT
* (THAT IS THOSE FREE RECORDS NOT INCLUDED IN CI AND CA FREESPACE
* CALCULATIONS).
*
         OC    XTRARECS,XTRARECS
         BZR   LINKREG
         XR    EVENODD,EVENODD     CLEAR
         L     DIVIDEND,XTRARECS
         L     DIVISOR,#RCANFSP
         DR    EVENODD,DIVISOR
         OR    REMAINDR,REMAINDR   IS THIS THERE A REMAINDER
         BZ    BYRND#11            NO - DONT ROUND
         LA    QUOTIENT,1(QUOTIENT) ROUND UP
BYRND#11 DS    0H
         L     WORKREG,#TRKCA
         MR    EVENODD,WORKREG
         ST    QUOTIENT,#TRKXTRA
         BR    LINKREG
CALC#12D DS    0H
*
* #TRKDATA = THE GREATER OF:
*
*       #RECLD /  #RCALOAD * #TRKCA + #TRKXTRA
*
*     OR
*
*                (#RECADD-XTRARECS)
*       -----------------------------------------   * #TRKCA
*       (#RCAFREE + (#RCIFREE * (#CICA - #FCICA)))
*
*
*
* THIS CALCULATION WILL COMPUTE THE NUMBER OF TRACKS NEEDED FOR THE
* DATA COMPONENT:
*
*
* START 1ST CALCULATION:
*
*       #RECLD /  #RCALOAD * #TRKCA + #TRKXTRA
*
         XR    EVENODD,EVENODD     CLEAR
         L     DIVIDEND,#RECLD
         L     DIVISOR,#RCALOAD
         DR    EVENODD,DIVISOR
         OR    REMAINDR,REMAINDR   IS THIS THERE A REMAINDER
         BZ    BYRND#12            NO - DONT ROUND
         LA    QUOTIENT,1(QUOTIENT) ROUND UP
BYRND#12 DS    0H
         L     WORKREG,#TRKCA
         MR    EVENODD,WORKREG
         A     QUOTIENT,#TRKXTRA
         ST    QUOTIENT,#TRKDATA
*
* START 2ND CALCULATION:
*
*                (#RECADD-XTRARECS)
*       -----------------------------------------   * #TRKCA
*       (#RCAFREE + (#RCIFREE * (#CICA - #FCICA)))
*
         XR    EVENODD,EVENODD     CLEAR
         L     WORKREG,#CICA
         S     WORKREG,#FCICA      NO. OF NON-FREE CI'S PER CA
         L     DIVIDEND,#RCIFREE   LOAD FOR MULTIPLY
         MR    EVENODD,WORKREG     NO. OF FREE RECS IN NON-FREE CI'S
*                                  IN A CA
         LR    DIVISOR,DIVIDEND    SET UP FOR DIVIDE
         A     DIVISOR,#RCAFREE    ADD FREE RECS IN FREE CI'S IN A CA
         L     DIVIDEND,#RECADD    SET UP FOR DIVIDE
         S     DIVIDEND,XTRARECS   SUB XTRARECS AT END OF DATASET
         DR    EVENODD,DIVISOR     FREE RECS NEEDED IN A CA
         OR    REMAINDR,REMAINDR   ROUND UP
         BZ    BYRND12B
         LA    QUOTIENT,1(QUOTIENT)
BYRND12B DS    0H
         XR    EVENODD,EVENODD     CLEAR
         M     EVENODD,#TRKCA      MULTIPLY BY TRACK PER CA
         C     QUOTIENT,#TRKDATA   CHECK IF GREATER THAN PREVIOUS CALC
         BNHR  LINKREG             IF LOW OR EQUAL USE PREVIOUS RESULT
         ST    QUOTIENT,#TRKDATA   IF HIGH USE 2ND CALC RESULT
         BR    LINKREG             GET OUT
         EJECT
PUTHEADR DS    0H
*
* THIS ROUTINE WILL DISPLAY THE HEADR SCREEN FOR THE DATACISZ
* SELECTION SCREEN...
*
*
*
         L     SCRNREG,AHEADRD     ADDRESS OF HEADR SCREEN
         USING HEADRD,SCRNREG      ADDRESSABILITY FOR SCREEN DSECT
         OC    KEYLEN,KEYLEN       IT THIS A KSDS??
         BZ    ESDSRRDS            NO-MUST BE AN ESDS OR RRDS
         MVC   FILETYP,=CL11'(KSDS)' YES-SET HEADR FILETYP
         B     TPUTHEAD            GO DISPLAY HEADR ON TUBE
ESDSRRDS DS    0H
         MVC   FILETYP,=CL11'(ESDS/RRDS)' SET HEADR FILETYP
TPUTHEAD DS    0H
         LR    R1,SCRNREG          BUFFER(SCREEN) ADDRESS
         L     R0,LHEADRD          BUFFER(SCREEN) LENGTH
         ICM   R1,8,FULLSCR        SET OPTION BYTE
         TPUT  ((R1)),((R0)),R
         L     R2,DEVREGSV         START OF DEVICE TABLE
         USING DEVENTRY,R2         ADDRESSABILITY FOR DEVENTRY DSECT
         LA    WORKREG,DATA1PGE    ADDR OF PAGE 1 SCREEN POINTER
         ST    WORKREG,FRSTPAGE    SAVE AS FIRST PAGE POINTER
         ST    WORKREG,CURPAGE     SAVE AS CURRENT PAGE POINTER
         L     SCRNREG,DATA1PGE    PAGE 1 SCREEN ADDR
         LA    PAGEREG,DATA1PGE    ADDR OF PAGE 1 SCREEN ADDR
         BAL   LINKREG,DPAGEBLD    GO BUILD 1ST DATACISZ PAGE
         L     SCRNREG,DATA2PGE    PAGE 2 SCREEN ADDR
         LA    PAGEREG,DATA2PGE    ADDR OF PAGE 1 SCREEN ADDR
         BAL   LINKREG,DPAGEBLD    GO BUILD 2ND DATACISZ PAGE
         L     SCRNREG,DATA3PGE    PAGE 3 SCREEN ADDR
         LA    PAGEREG,DATA3PGE    ADDR OF PAGE 3 SCREEN ADDR
         BAL   LINKREG,DPAGEBLD    GO BUILD 3RD DATACISZ PAGE
         L     SCRNREG,DATA4PGE    PAGE 4 SCREEN ADDR
         LA    PAGEREG,DATA4PGE    ADDR OF PAGE 4 SCREEN ADDR
         BAL   LINKREG,DPAGEBLD    GO BUILD 4TH DATACISZ PAGE
         B     PUTDPAGE            GO DISPLAY 1ST PAGE
         EJECT
DPAGEBLD DS    0H
         USING DATAD,SCRNREG       ADDRSABLTY TO DATA SCREEN DSECT
         LA    LINE,INFO1          1ST INFO LINE ON SCREEN
         LA    LINECNT,9           NUMBER OF INFO LINES ON SCREEN
DETLOOP  DS    0H
*                                  WAIT TO MOVE PERIOD SO WE CAN FIT
*                                  THE EDIT MASK FOR DCISZ
* CONVERT CONTROL INTERVAL SIZE
*
         OC    DCISZ,DCISZ        IS THIS DCISZ NULLED
         BZ    NXTD               YES-GO LOOK AT THE NEXT DCISZ
         L     WORKREG,DCISZ            CONTROL INTERVAL SIZE
         CVD   WORKREG,DOUBLE
         MVC   1(6,LINE),MASK6
         ED    1(6,LINE),DOUBLE+5
*
*
* CONVERT NUMBER OF TRACKS PER CONTROL AREA
*
         L    WORKREG,#TRKCA
         CVD   WORKREG,DOUBLE
         MVC   9(4,LINE),MASK4
         ED    9(4,LINE),DOUBLE+6
*
*
* CONVERT NUMBER OF CONTROL INTERVALS PER CONTROL AREA
*
         L    WORKREG,#CICA
         CVD   WORKREG,DOUBLE
         MVC  18(4,LINE),MASK4
         ED   18(4,LINE),DOUBLE+6
*
*
* CONVERT RECORDS PER CONTROL AREA (TO BE LOADED)
*
         L    WORKREG,#RCALOAD
         CVD   WORKREG,DOUBLE
         MVC  26(6,LINE),MASK6
         ED   26(6,LINE),DOUBLE+5
*
*
* CONVERT RECORDS PER CONTROL INTERVAL (TO BE LOADED)
*
         L    WORKREG,#RCILOAD
         CVD   WORKREG,DOUBLE
         MVC  35(6,LINE),MASK6
         ED   35(6,LINE),DOUBLE+5
*
*
* CONVERT                  FREESPACE PERCENTAGE
*
         L    WORKREG,PERFSPCI
         CVD   WORKREG,DOUBLE
         MVC  42(4,LINE),MASK4
         ED   42(4,LINE),DOUBLE+6
         L    WORKREG,PERFSPCA
         CVD   WORKREG,DOUBLE
         MVC  46(4,LINE),MASK4
         ED   46(4,LINE),DOUBLE+6
         MVI  46(LINE),C'-'
*
*
* CONVERT RECORDS PER CONTROL AREA WITH NO FREESPACE
*
         L     WORKREG,#RCANFSP
         CVD   WORKREG,DOUBLE
         MVC   52(6,LINE),MASK6
         ED    52(6,LINE),DOUBLE+5
*
*
* CONVERT RECORDS PER CONTROL AREA WITH NO FREESPACE
*
         L     WORKREG,#RCINFSP
         CVD   WORKREG,DOUBLE
         MVC   61(6,LINE),MASK6
         ED    61(6,LINE),DOUBLE+5
*
* CONVERT TOTAL TRACKS NEEDED TO CYLS IF #TRKCA IS EQUAL TO TRKCYL
*
*
         CLC   #TRKCA,TRKCYL    IS #TRKCA EQUAL TO TRKCYL
         BNE   USET             NO- SPECIFIFY SPACE IN TRACKS
         XR    R14,R14          YES-GET SET TO CALC NO. OF CYLS
         L     R15,#TRKDATA
         L     R1,TRKCYL
         DR    R14,R1
         LR    WORKREG,R15
         MVI   76(LINE),C'C'
         B     CONVIT
USET     DS    0H
         L     WORKREG,#TRKDATA
         MVI   76(LINE),C'T'
CONVIT   DS    0H
         CVD   WORKREG,DOUBLE
         MVC   67(8,LINE),MASK8
         ED    67(8,LINE),DOUBLE+4
NXTDETAL DS    0H
         LA    LINE,INFO2-INFO1(LINE)  POINT TO NEXT INFO LINE
         LA    R2,DEVENLEN(R2)    NEXT DCISZ
         CLC   DCISZ,=F'-1'       IS IT THE END OF TABLE
         BE    SETLASTP           YES-GO SET LAST PAGE POINTER
         BCT   LINECNT,DETLOOP    GO PROCESS NEXT LINE
         BR    LINKREG            GO BACK TO CALLER
NXTD     DS    0H
         LA    R2,DEVENLEN(R2)    NEXT DCISZ
         CLC   DCISZ,=F'-1'       IS IT THE END OF TABLE
         BE    SETLASTP           YES-GO SET LAST PAGE POINTER
         B     DETLOOP            GO LOOK AT NEXT DCISZ
SETLASTP DS    0H
         ST    PAGEREG,LASTPAGE   SET THIS PAGE AS LAST PAGE POINTER
         B     PUTDPAGE
         EJECT
PUTDPAGE DS    0H
         L     WORKREG,CURPAGE    CURRENT PAGE POINTER
         L     R1,0(WORKREG)      PAGE(SCREEN) ADDRESS
         LA    R0,DATAL           LENGTH OF PAGE(SCREEN)
         ICM   R1,8,FULLSCR       OPTION BYTE
         TPUT  ((R1)),((R0)),R
         LA    R0,DATAL           SCREEN LENGTH
         L     R1,0(WORKREG)      SCREEN ADDRESS
         LR    R2,WAREAREG        COMMON WORKAREA ADDRESS
         LINK  EP=VSAMAGET        LINK TO MODULE FOR TGET/ASIS
*                                 AND INPUT BUFFER EDITING...
         OC    PROCERR,PROCERR    ANY ERRORS IN SCREEN PROCESSING..
         BNZ   PUTDPAGE           YES-GO TRY IT AGAIN.....
*
         CLI   KEYHIT,CLEAR       WAS CLEAR KEY PRESSED?
         BE    RETURN             YES-GET OUT THIS PROGRAM
         CLI   KEYHIT,PF3         WAS PF3 (LAST MENU) PRESSED?
         BE    RSETATTR           YES-RESET KEYABLE FIELDS(UP,MDTOFF)
         CLI   KEYHIT,PF4         WAS PF4-SPF PRIMARY OPTION MENU?
         BE    RETURN             YES-GET OUT OF THIS..
         CLI   KEYHIT,PF7         WAS PF7 (PAGE BACKWARD) HIT?
         BE    SELLOGIC           YES-GO SEE IF ANY DCISZ'S SELCTD
         CLI   KEYHIT,PF8         WAS PF8 (PAGE FORWARD) HIT?
         BE    SELLOGIC           YES-GO SEE IF ANY DCISZ'S SELCTD
         CLI   KEYHIT,ENTER       WAS ENTER(DETAIL INFO) HIT?
         BE    SELLOGIC           YES-GO SEE IF ANY DCISZ'S SELCTD
         CLI   KEYHIT,PF1         WAS HELP REQUESTED?
         BNE   PUTDPAGE           NO-GO REDISPLAY CURRENT PAGE.
         LR    R0,WAREAREG        WORKAREA ADDRESS
         MVC   HLPTYPE,=CL3'CIS'  INDICATE CI SELECTION HELP RQSTD
         LINK  EP=VSAMAHLP
         CLI   KEYHIT,PF4         WAS PF4-SPF PRIMARY OPTION MENU?
         BE    RETURN             YES-GET OUT OF THIS...
         TPUT  CLEARBUF,CLEARLEN,FULLSCR
         L     R1,AMENUD          MENU SCREEN ADDRESS
         L     R0,LMENUD          MENU SCREEN LENGTH
         ICM   R1,8,FULLSCR       FULLSCR OPTION
         TPUT  ((R1)),((R0)),R    REDISPLAY MENU SCREEN
         L     R1,AHEADRD         HEADER SCREEN ADDRESS
         L     R0,LHEADRD         HEADER SCREEN LENGTH
         ICM   R1,8,FULLSCR       FULLSCR OPTION
         TPUT  ((R1)),((R0)),R
         B     PUTDPAGE           GO REDISPLAY CURRENT DATA PAGE..
SELLOGIC DS    0H
*
* THIS NEXT ROUTINE WILL LOOK IF ANY DCISZ'S WERE SELECTED FOR
* ADDITIONAL INFORMATION AND SET THE SELECT INDICATOR FOR THOSE
* SELECTED.
*
         L     SCRNREG,0(WORKREG) SET UP SCREEN POINTER
         USING DATAD,SCRNREG      ADDRESSABILITY TO SCREEN DSECT
         LA    WORKREG,9          LOOP FACTOR(MAX 9 DCISZ'S PER PAGE)
         LA    R7,DATAS1          FIRST SELECT FIELD IN SCREEN
         SPACE 2
CHKSEL   DS    0H
         CLC   INFO1-DATAS1+1(6,R7),BLANKS  IS THIS SCREEN LINE BLANKS
         BE    CHKPFKEY                     YES-CONT PFKEY CHECKING
         LA    R15,INFO1-DATAS1+1(R7)  ADDR OF DCISZ(CHARACTER)
         LA    R14,6              LENGTH OF DCISZ
         ST    WORKREG,SAVEREG    TEMP SAVE
         BAL   LINKREG,CVB        GO CONVERT IT TO BINARY
         L     WORKREG,SAVEREG    RESTORE AFTER CVB ROUTINE
         USING DEVENTRY,DEVREG    ADDRESSABILITY TO DEVICE ENTRY DSECT
         L     DEVREG,DEVREGSV    START OF DEVICE TABLE
CHKSEL2  DS    0H
         CLM   R1,7,DCISZ+1       IS THE RIGHT DEVICE TABLE ENTRY
         BNE   SELP2              NO-GO CHECK NEXT DEVICE TABLE ENTRY
         CLI   0(R7),C'S'         YES-WAS THIS DCISZ SELECTED???
         BNE   DSELOFF            NO-TURN OFF SELECTION INDICATOR..
         MVI   DSELECT,X'01'      YES-INDICATE IT WAS SELECTED...
         B     SELP3              GO-PROCESS NEXT SCREEN ENTRY
DSELOFF  DS    0H
         MVI   DSELECT,X'00'      TURN OFF SELECTION INDICATOR
         B     SELP3              GO PROCESS NEXT SCREEN ENTRY
SELP2    DS    0H
         LA    DEVREG,DEVENLEN(DEVREG) NEXT DEVICE TABLE ENTRY
         CLC   DCISZ,=F'-1'       END OF DEVICE TABLE
         BE    CHKPFKEY           YES-CONT. PFKEY PROCESSING
         B     CHKSEL2
SELP3    DS    0H
         LA    R7,DATAS2-DATAS1(R7) NEXT SCREEN ENTRY
         BCT   WORKREG,CHKSEL     DO LOOP
* END OF DETAIL SELECTION LOGIC...............
CHKPFKEY DS    0H
         CLI   KEYHIT,PF7         WAS PF7 (PAGE BACKWARD) PRESSED?
         BE    DPAGBACK           YES-GO AND PAGE BACKWARD
         CLI   KEYHIT,PF8         WAS PF8 (PAGE FORWARD) PRESSED?
         BE    DPAGFRWD           YES-GO AND PAGE FORWARD
         CLI   KEYHIT,ENTER       WAS ENTER (DETAIL PHASE) PRESSED?
         BNE   PUTDPAGE           NO-GO DISPLAY CURRENT PAGE AGAIN..
         LR    R0,WAREAREG        COMMON WORKAREA ADDRESS
         LINK  EP=VSAMADTL        GO TO DETAIL PHASE PROCESSING
         CLI   KEYHIT,CLEAR       WAS CLEAR KEY PRESSED?
         BE    RETURN             YES-GET OUT OF THIS PROGRAM
         CLI   KEYHIT,PF3         WAS-PF3 (LAST DATA) PRESSED?
         BE    DPAGFRST           YES-GO REDISPLAY FIRST DATA SCREEN
         CLI   KEYHIT,PF4         WAS-PF4 (LAST MENU) PRESSED?
         BE    RETURN             YES-GO BACK TO SPF PRIMARY OPTION
         B     PUTDPAGE           NO-GO REDISPLAY CURRENT PAGE....
RSETATTR DS    0H
         L     SCRNREG,AMENUD     ADDR OF MENU SCREEN
         USING MENUD,SCRNREG      ADDRESSABILITY TO SCREEN DSECT
         MVC   RECAA,UPOFF        RESET ATTRIBUTE
         MVC   RECMA,UPOFF        RESET ATTRIBUTE
         MVC   RECLOADA,UPOFF     RESET ATTRIBUTE
         MVC   RECADDA,UPOFF      RESET ATTRIBUTE
         MVC   DEVTYPEA,UPOFF     RESET ATTRIBUTE
         MVC   KEYLA,UPOFF        RESET ATTRIBUTE
         MVC   CIPERA,UPOFF       RESET ATTRIBUTE
         MVC   CAPERA,UPOFF       RESET ATTRIBUTE
         MVC   IMBA,UPOFF         RESET ATTRIBUTE
         MVC   REPLA,UPOFF        RESET ATTRIBUTE
         MVC   MENUX,MENU+MENUX-MENUD  RESET CURSOR TO FIRST KEYABLE
         L     R6,DATA1PGE        SET FOR REINITIALIZATION OF PAGES
         B     INITPGE1           RENINIT(PAGES-DEV TABLES) + REDISPLAY
         SF    (UP,MDTOFF,HI)
         ORG   *-1
UPOFF    DS    0X
         ORG
         EJECT
DPAGBACK DS    0H
         L     PAGEREG,CURPAGE    ADDR OF CURRENT PAGE POINTER
         L     WORKREG,FRSTPAGE   ADDR OF FIRST PAGE POINTER
         CR    PAGEREG,WORKREG    CAN WE PAGE BACKWARDS
         BNH   PUTDPAGE           NO-THEN REDISPLAY CURRENT PAGE
         SH    PAGEREG,=H'4'      YES-BACK UP 1 PAGE POINTER
         ST    PAGEREG,CURPAGE    SET AS CURRENT PAGE
         B     PUTDPAGE           GO DISPLAY NEW PAGE ON TUBE
DPAGFRST DS    0H
         L     WORKREG,FRSTPAGE   ADDR OF FIRST PAGE POINTER
         ST    WORKREG,CURPAGE    SET AS CURRENT PAGE
         L     R1,AHEADRD         ADDRESS OF HEADER SCREEN
         LA    R0,HEADRL          LENGTH OF HEADER SCREEN
         ICM   R1,8,FULLSCR       SET OPTION BYTE
         TPUT  ((R1)),((R0)),R
         B     PUTDPAGE           GO DISPLAY NEW PAGE ON TUBE
         SPACE 3
DPAGFRWD DS    0H
         L     PAGEREG,CURPAGE    ADDR OF CURRENT PAGE POINTER
         L     WORKREG,LASTPAGE   ADDR OF FIRST PAGE POINTER
         CR    PAGEREG,WORKREG    CAN WE PAGE FORWARD??
         BNL   PUTDPAGE           NO-THEN REDISPLAY CURRENT PAGE
         LA    PAGEREG,4(PAGEREG) YES-FORWARD 1 PAGE POINTER
         ST    PAGEREG,CURPAGE    SET AS CURRENT PAGE
         B     PUTDPAGE           GO DISPLAY NEW PAGE ON TUBE
         EJECT
RETURN   DS    0H
         TPUT CLEARBUF,CLEARLEN,FULLSCR
         L     R13,SAVE+4
         RETURN (14,12),RC=0
         TITLE 'VSAM ANALYZER STATIC DEVICE TABLE LIST'
         CNOP  2,4       ALIGN LIST ON PROPER BOUNDARY
DEVLSTST DS    0H
         DC    Y(DEVICES)   NUMBER OF DEVICE TABLES IN LIST
         DC    CL4'3330',A(DEV3330)
         DC    CL4'3350',A(DEV3350)
         DC    CL4'3380',A(DEV3380)
         DC    CL4'3390',A(DEV3390)
*
*  THIS NEXT VALUE MUST BE CHANGED WHEN ANY DEVICE TABLES ARE ADDED
*  OR DELETED FROM THIS PROGRAM. IT INDICATES THE NUMBER OF DEVICES
*  SUPPORTED BY THIS PROGRAM.
*
*  WHEN ADDING ANY NEW DEVICES TO THIS PROGRAM A DEVICE TABLE MUST
*  BE CODED WITH THE DEVICE DEPENDENT DATA, AN ENTRY FOR IT PLACED IN
*  THE DEVICE LIST (DEVLSTST), AND THE NUMBER OF DEVICES AS INDICATED
*  BY THE SYMBOL (DEVICES) MUST BE INCREMENTED ACCORDINGLY.
*
*  ALSO THE VSAMAWRK MODULE LABEL=DEVLSTWK MUST ALSO BE INCREMENTED
*  TO REFLECT THE SIZE OF THE DEVICE LIST (DEVLSTST). A CHANGE TO
*  THIS MODULE WILL NECESSITATE ALL OTHER VSAMANAL SUB-MODULES TO
*  BE RE-ASSEMBLED.
*
DEVICES  EQU   4           NUMBER OF DEVICES TABLES
         EJECT
*
*   P R O G R A M   C O N S T A N T S
*
MASK8    DC    XL8'4020202020202120'
MASK6    DC    XL6'402020202120'
MASK4    DC    XL4'40202120'
*
*
*
KLENMSG  DC CL40'KEYLENGTH(NONNUMERIC)'
*
CIPMSG   DC CL40'%-ADD RECS CI-FREESPACE(NONNUMERIC)'
*
CAPMSG   DC CL40'%-ADD RECS CA-FREESPACE(NONNUMERIC)'
*
DEVMSG   DC CL40'DEVICE TYPE NOT ''3390''/''3380'''
*
RECAMSG  DC CL40'AVG-RECSIZE(NONNUMERIC)'
RECMMSG  DC CL40'MAX-RECSIZE(NONNUMERIC)'
*
CIPHIMSG DC CL40'%-ADD RECS CI-FREESPACE OVER 100'
*
CAPHIMSG DC CL40'%-ADD RECS CA-FREESPACE OVER 100'
*
CICAHMSG DC CL40'%-ADD RECS CI-CA COLLECTIVELY OVER 100'
*
IMBMSG   DC CL40'IMBED NOT ''NO''/''YES'''
*
REPLMSG  DC CL40'REPLICATE NOT ''NO''/''YES'''
*
RECLDMSG DC CL40'RECORDS T/B LOADED(NONNUMERIC)'
*
RECADMSG DC CL40'RECORDS T/B ADDED(NONNUMERIC)'
*
HIKEYMSG DC CL40'KEYLENGTH GREATER THAN 255'
BADKYMSG DC CL40'KEYLENGTH GREATER THAN MAX-RECSIZE'
*
NRECLMSG DC CL40'RECORDS T/B LOADED REQUIRED'
*
NRECAMSG DC CL40'AVG-RECSIZE REQUIRED'
*
LORECMSG DC CL40'MAX-RECSIZE LESS THAN AVG-RECSIZE'
RECAZMSG DC CL40'AVG-RECSIZE CANNOT BE 0'
*
RECLZMSG DC CL40'RECORDS T/B LOADED CANNOT BE 0'
*
RECMLMSG DC CL40'MAX-RECSIZE GREATER THAN 32761'
*
RECALMSG DC CL40'AVG-RECSIZE GREATER THAN 32761'
*
*
         SPACE 3
FULLSCR  DC    X'03'                    FULLSCRN OPTION BYTE
TGETASIS DC    X'81'                    ASIS OPTION BYTE
         SPACE 3
BUFTABLE DC    X'40C1C2C3C4C5C6C7C8C94A4B4C4D4E4F' 64 BYTE TRANSLATE
         DC    X'50D1D2D3D4D5D6D7D8D95A5B5C5D5E5F' TABLE FOR CONVERT
         DC    X'6061E2E3E4E5E6E7E8E96A6B6C6D6E6F' TO 3270 LINE CODE
         DC    X'F0F1F2F3F4F5F6F7F8F97A7B7C7D7E7F'
         SPACE 3
SBATABLE DC    17X'00'  256 BYTE TRANSLATE TABLE TO FIND SBA(X'11)
         DC    X'11'    IN THE TGET INPUT BUFFER AND CORRESPONDING 3270
         DC    238X'00' SCREEN......
         SPACE 3
BLANKS   DC    CL200' '
CLEARBUF DC X'C2115D7F1140403C5D7F0011404013'  *POSITIONAL*
CLEARLEN EQU *-CLEARBUF                        *POSITIONAL*
         LTORG
         COPY  VSAMAEQU    SYMBOLIC EQUATES
         COPY  MENUSCRN    MENU SCREEN(3270 OUTPUT BUFFER)
         COPY  HEADSCRN    HEADER SCREEN(3270 OUTPUT BUFFER)
         COPY  DATASCRN    DATA SCREEN(3270 OUTPUT BUFFER)
         TITLE 'VSAM ANALYZER DEVICE TABLE FOR 3330S MOD 1 AND 2'
DEV3330  DS    0F
         DC    F'512',F'512',F'20',F'77',F'19',24F'0'
         DC    F'1024',F'1024',F'11',F'85',F'19',24F'0'
         DC    F'1536',F'512',F'20',F'77',F'19',24F'0'
         DC    F'2048',F'2048',F'6',F'93',F'19',24F'0'
         DC    F'2560',F'512',F'20',F'77',F'19',24F'0'
         DC    F'3072',F'1024',F'11',F'85',F'19',24F'0'
         DC    F'3584',F'512',F'20',F'77',F'19',24F'0'
         DC    F'4096',F'4096',F'3',F'93',F'19',24F'0'
         DC    F'4608',F'512',F'20',F'77',F'19',24F'0'
         DC    F'5120',F'1024',F'11',F'85',F'19',24F'0'
         DC    F'5632',F'512',F'20',F'77',F'19',24F'0'
         DC    F'6144',F'2048',F'6',F'93',F'19',24F'0'
         DC    F'6656',F'512',F'20',F'77',F'19',24F'0'
         DC    F'7168',F'1024',F'11',F'85',F'19',24F'0'
         DC    F'7680',F'1536',F'7',F'82',F'19',24F'0'
         DC    F'8192',F'4096',F'3',F'93',F'19',24F'0'
         DC    F'10240',F'2048',F'6',F'93',F'19',24F'0'
         DC    F'12288',F'4096',F'3',F'93',F'19',24F'0'
         DC    F'14336',F'2048',F'6',F'93',F'19',24F'0'
         DC    F'16384',F'4096',F'3',F'93',F'19',24F'0'
         DC    F'18432',F'2048',F'6',F'93',F'19',24F'0'
         DC    F'20480',F'4096',F'3',F'93',F'19',24F'0'
         DC    F'22528',F'2048',F'6',F'93',F'19',24F'0'
         DC    F'24576',F'4096',F'3',F'93',F'19',24F'0'
         DC    F'26624',F'2048',F'6',F'93',F'19',24F'0'
         DC    F'28672',F'4096',F'3',F'93',F'19',24F'0'
         DC    F'30720',F'2048',F'6',F'93',F'19',24F'0'
         DC    F'32768',F'4096',F'3',F'93',F'19',24F'0'
         DC    F'-1'                              END OF TABLE INDICATR
         DC    X'00'       DEVICE TYPE
         DC    X'00'       RESERVED
         DC    H'19'         CYLINDERS PER TRACK
         DC    F'13165'      TRACK CAPACITY IN BYTES
         DC    F'135'        BLOCK OVERHEAD(INTER BLOCK GAP)
DEVTABLN EQU   *-DEV3330
         TITLE 'VSAM ANALYZER DEVICE TABLE FOR 3350 DEVICE'
DEV3350  DS    0F
         DC    F'512',F'512',F'27',F'71',F'30',24F'0'
         DC    F'1024',F'1024',F'15',F'79',F'30',24F'0'
         DC    F'1536',F'512',F'27',F'71',F'30',24F'0'
         DC    F'2048',F'2048',F'8',F'85',F'30',24F'0'
         DC    F'2560',F'512',F'27',F'71',F'30',24F'0'
         DC    F'3072',F'1024',F'15',F'79',F'30',24F'0'
         DC    F'3584',F'512',F'27',F'71',F'30',24F'0'
         DC    F'4096',F'4096',F'4',F'85',F'30',24F'0'
         DC    F'4608',F'512',F'27',F'71',F'30',24F'0'
         DC    F'5120',F'1024',F'15',F'79',F'30',24F'0'
         DC    F'5632',F'512',F'27',F'71',F'30',24F'0'
         DC    F'6144',F'2048',F'8',F'85',F'30',24F'0'
         DC    F'6656',F'512',F'27',F'71',F'30',24F'0'
         DC    F'7168',F'3584',F'5',F'93',F'30',24F'0'
         DC    F'7680',F'2560',F'7',F'93',F'30',24F'0'
         DC    F'8192',F'4096',F'4',F'85',F'30',24F'0'
         DC    F'10240',F'2048',F'8',F'85',F'30',24F'0'
         DC    F'12288',F'4096',F'4',F'85',F'30',24F'0'
         DC    F'14336',F'2048',F'8',F'85',F'30',24F'0'
         DC    F'16384',F'4096',F'4',F'85',F'30',24F'0'
         DC    F'18432',F'2048',F'8',F'85',F'30',24F'0'
         DC    F'20480',F'4096',F'4',F'85',F'30',24F'0'
         DC    F'22528',F'2048',F'8',F'85',F'30',24F'0'
         DC    F'24576',F'4096',F'4',F'85',F'30',24F'0'
         DC    F'26624',F'2048',F'8',F'85',F'30',24F'0'
         DC    F'28672',F'4096',F'4',F'85',F'30',24F'0'
         DC    F'30720',F'2048',F'8',F'85',F'30',24F'0'
         DC    F'32768',F'4096',F'4',F'85',F'30',24F'0'
         DC    F'-1'                              END OF TABLE INDICATR
         DC    X'00'       DEVICE TYPE
         DC    X'00'       RESERVED
         DC    H'30'       CYLINDERS PER TRACK
         DC    F'19254'    TRACK CAPACITY IN BYTES
         DC    F'185'      BLOCK OVERHEAD(INTER BLOCK GAP)
         TITLE 'VSAM ANALYZER DEVICE TABLE FOR 3390 DEVICE'
DEV3390  DS    0F
         DC     F'512',F'512',F'49',F'45',F'15',24F'0'
         DC     F'1024',F'1024',F'33',F'63',F'15',24F'0'
         DC     F'1536',F'1536',F'26',F'71',F'15',24F'0'
         DC     F'2048',F'2048',F'20',F'78',F'15',24F'0'
         DC     F'2560',F'2560',F'17',F'81',F'15',24F'0'
         DC     F'3072',F'3072',F'15',F'84',F'15',24F'0'
         DC     F'3584',F'3584',F'13',F'87',F'15',24F'0'
         DC     F'4096',F'4096',F'12',F'88',F'15',24F'0'
         DC     F'4608',F'4608',F'10',F'89',F'15',24F'0'
         DC     F'5120',F'5120',F'9',F'91',F'15',24F'0'
         DC     F'5632',F'5632',F'9',F'91',F'15',24F'0'
         DC     F'6144',F'6144',F'8',F'92',F'15',24F'0'
         DC     F'6656',F'6656',F'7',F'93',F'15',24F'0'
         DC     F'7168',F'7168',F'7',F'93',F'15',24F'0'
         DC     F'7680',F'7680',F'6',F'94',F'15',24F'0'
         DC     F'8192',F'8192',F'6',F'94',F'15',24F'0'
         DC     F'10240',F'10240',F'5',F'95',F'15',24F'0'
         DC     F'12288',F'12288',F'4',F'97',F'15',24F'0'
         DC     F'14336',F'7168',F'7',F'93',F'15',24F'0'
         DC     F'16384',F'16384',F'3',F'98',F'15',24F'0'
         DC     F'18432',F'18432',F'3',F'98',F'15',24F'0'
         DC     F'20480',F'10240',F'5',F'95',F'15',24F'0'
         DC     F'22528',F'5632',F'9',F'91',F'15',24F'0'
         DC     F'24576',F'24576',F'2',F'99',F'15',24F'0'
         DC     F'26624',F'26624',F'2',F'99',F'15',24F'0'
         DC     F'28672',F'7168',F'7',F'93',F'15',24F'0'
         DC     F'30720',F'7680',F'6',F'94',F'15',24F'0'
         DC     F'32768',F'16384',F'3',F'98',F'15',24F'0'
         DC    F'-1'                              END OF TABLE INDICATR
         DC    X'0F'       DEVICE TYPE
         DC    X'00'       RESERVED
         DC    H'15'       CYLINDERS PER TRACK
         DC    F'56664'    TRACK CAPACITY IN BYTES
         DC    F'0'        BLOCK OVERHEAD(INTER BLOCK GAP)
         TITLE 'VSAM ANALYZER DEVICE TABLE FOR 3380 DEVICE'
DEV3380  DS    0F
         DC     F'512',F'512',F'46',F'49',F'15',24F'0'
         DC     F'1024',F'1024',F'31',F'66',F'15',24F'0'
         DC     F'1536',F'1536',F'23',F'74',F'15',24F'0'
         DC     F'2048',F'2048',F'18',F'77',F'15',24F'0'
         DC     F'2560',F'2560',F'15',F'80',F'15',24F'0'
         DC     F'3072',F'3072',F'13',F'83',F'15',24F'0'
         DC     F'3584',F'3584',F'11',F'82',F'15',24F'0'
         DC     F'4096',F'4096',F'10',F'85',F'15',24F'0'
         DC     F'4608',F'4608',F'9',F'86',F'15',24F'0'
         DC     F'5120',F'5120',F'8',F'85',F'15',24F'0'
         DC     F'5632',F'5632',F'7',F'82',F'15',24F'0'
         DC     F'6144',F'6144',F'7',F'90',F'15',24F'0'
         DC     F'6656',F'6656',F'6',F'83',F'15',24F'0'
         DC     F'7168',F'7168',F'6',F'90',F'15',24F'0'
         DC     F'7680',F'7680',F'5',F'80',F'15',24F'0'
         DC     F'8192',F'8192',F'5',F'85',F'15',24F'0'
         DC     F'10240',F'10240',F'4',F'85',F'15',24F'0'
         DC     F'12288',F'6144',F'7',F'90',F'15',24F'0'
         DC     F'14336',F'14336',F'3',F'90',F'15',24F'0'
         DC     F'16384',F'8192',F'5',F'85',F'15',24F'0'
         DC     F'18432',F'6144',F'7',F'90',F'15',24F'0'
         DC     F'20480',F'20480',F'2',F'85',F'15',24F'0'
         DC     F'22528',F'22528',F'2',F'94',F'15',24F'0'
         DC     F'24576',F'6144',F'7',F'90',F'15',24F'0'
         DC     F'26624',F'6656',F'6',F'83',F'15',24F'0'
         DC     F'28672',F'14336',F'3',F'90',F'15',24F'0'
         DC     F'30720',F'6144',F'7',F'90',F'15',24F'0'
         DC     F'32768',F'8192',F'5',F'85',F'15',24F'0'
         DC    F'-1'                              END OF TABLE INDICATR
         DC    X'0E'       DEVICE TYPE
         DC    X'00'       RESERVED
         DC    H'15'       CYLINDERS PER TRACK
         DC    F'47968'    TRACK CAPACITY IN BYTES
         DC    F'0'        BLOCK OVERHEAD(INTER BLOCK GAP)
         COPY  VSAMAWRK    COMMON WORKAREA DSECT
         COPY  VSAMADEV    DEVICE TABLE ENTRY DSECT
         END
