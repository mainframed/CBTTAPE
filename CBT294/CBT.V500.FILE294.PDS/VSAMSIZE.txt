* THIS TSO COMMAND PROCESSOR IS AN EARLY VERSION OF VSAMANAL TO BE
* USED FOR TSO NON-3270 TERMINALS (HARDCOPY). INPUT IS THRU
* THE TSO PARSE ROUTINES.....
* NOTE:  THIS VERSION IS AT A LOWER LEVEL THAN THE 3270 VERSION
*        AND DOES NOT PROVIDE THE USER WITH DATA CISZ SELECTION
*        CAPABILITIES.
         OSINIT  VSAMSIZE,(11,12),NO
         LR    R2,R1                    SAVE COMMAND PARM LIST
         GETMAIN R,LV=DEV3330L+DEV3350L+WORKLEN
         LR    R6,R1                    SETE 3330 TABLE ADDR FOR MVCL
         LA    R7,DEV3330L    LENGTH OF MVCL OPERAND 1
         LR    R9,R7          SAVE LENGTH FOR OPERAND 2
         LA    R8,DEV3330     ADDR OF OPERAND 2 FOR MVCL
         MVCL  R6,R8          MOVE DEV3330 TABLE TO WORKAREA
         LR    R3,R1                    3330 TABLE ADDRESS
         LA    R1,DEV3330L(R1)          POINT TO 3350 TABLE LOCATION
         LR    R6,R1                    SETE 3350 TABLE ADDR FOR MVCL
         LA    R7,DEV3350L    LENGTH OF MVCL OPERAND 1
         LR    R9,R7          SAVE LENGTH FOR OPERAND 2
         LA    R8,DEV3350     ADDR OF OPERAND 2 FOR MVCL
         MVCL  R6,R8          MOVE DEV3350 TABLE TO WORKAREA
         LR    R4,R1                    3350 TABLEADDRESS
         LA    WAREAREG,DEV3350L(R1)    WORKAREA ADDRESS
         USING WORKAREA,WAREAREG        ADDRESSABILITY  TO OUR WORKAREA
         LR    R6,WAREAREG       SET FOR CLEAR TO LOW VALUES(X'00')
         LA    R7,WORKLEN        LENGTH OF WORKAREA FOR CLEAR
         XR    R8,R8                   ZERO FOR CLEAR
         XR    R9,R9                   ZERO FOR CLEAR
         MVCL  R6,R8           CLEAR WORKAREA TO BINARY ZEROS(X'00'S)
         ST    R3,A3330TAB              SET WORKAREA 3330 TABLE POINTER
         ST    R4,A3350TAB              SET WORKAREA 3350 TABLE POINTER
         MVC   CIRATIO,=F'75'           SET CIRATIO DEFAULT...
         MVC   CARATIO,=F'25' SET CARATIO DEFAULT...
         LA    R3,SAVE                  OUR SAVE AREA
         ST    R3,8(R13)                SET FORWORD SAVEAREA CHAIN
         ST    R13,4(R3)                SET BACKWORD SAVEAREA CHAIN
         LR    R13,R3                   POINT R13 TO OUR SAVE AREA
         EJECT
* THIS ROUTINE WILL INITIALIZE THE PARSE PARAMETER LIST IN
* PREPARATION FOR OUR LINKING TO THE PARSE PROCESSING ROUTINE(IKJPARS)
*
* THE PARSE ROUTINE WILL PROCESS ALL OF THE KEYWORD INPUT FOR THIS
* COMMAND..........
         ST    R2,CPPLADDR              SAVE COMMAND PROCESSOR PARM LST
         L     CPPLREG,CPPLADDR         LOAD INTO CPPL ADDRESS REGISTER
         USING CPPL,CPPLREG             ESTABLISH ADDRESSABILTY TO CPPL
         LA    PPLREG,PPLAREA           LOAD PPL ADDR INTO ADDRESS REG.
         USING PPL,PPLREG               ESTABLISH ADDRESSABILITY TO PPL
         MVC   PPLUPT,CPPLUPT               USER'S PROFILE TABLE ADDR
         MVC   PPLECT,CPPLECT               ENVIRONMENT CNTRL TABL ADDR
         XC    CMDECB,CMDECB                CLEAR ECB TO ZEROS
         LA    WORKREG,CMDECB
         ST    WORKREG,PPLECB               ECB ADDR
         LA    WORKREG,PDLADDR
         ST    WORKREG,PPLANS               ADDR OF PDL ADDR WORD
         MVC   PPLCBUF,CPPLCBUF             COMMAND BUFFER ADDR
         L     WORKREG,=A(PCL)
         ST    WORKREG,PPLPCL               PCL ADDR
         LR    R1,PPLREG                LOAD PPL ADDR FOR LINK TO PARSE
         LINK  EP=IKJPARS               GO TO PARSE PROCESSING ROUTINE
         LTR   R15,R15                  IS PARSE RETURN CODE OKAY
         BNZ   BADPARSE                 NO WE HAVEN A PARSE ERROR
         L     PDLREG,PDLADDR           LOAD PDL ADDR FROM PARSE
         USING IKJPARMD,PDLREG          ESTABLISH ADDRESSABILITY TO PDL
         EJECT
* THIS ROUTINE WILL PERFORM A FINAL EDIT ON THE COMMAND INPUT RETURNED
* FROM PARSE...AND MOVE ALL INPUT VALUES TO THEIR CORRESPONDING
* FIELDS IN OUR WORKAREA...AND THEN THE PARSE PDL AREA WILL BE RELEASED
*
*
PARM1    DS    0H
         LA    PDE,RECSIZEA             ADDR OF PDE (AVERAGE RECSIZE)
         BAL   LINKREG,CVB              CONVERT DATA TO BINARY
         ST    R1,RECSZ                 STORE DATA
         SPACE 3
PARM2    DS    0H
         LA    PDE,RECSIZEM             ADDR OF PDE (MAXIMUM RECSIZE)
         BAL   LINKREG,CVB              CONVERT DATA TO BINARY
         ST    R1,MAXRECSZ              STORE DATA
         C     R1,=F'32761'             IS MAXIMUM GREATER THAN ALLOWED
         BH    BADRECSZ                 YES-ERROR IN RECSIZE
         SPACE 3
PARM3    DS    0H
         LA    PDE,RECLOAD              ADDR OF PDE(RECORDS TO LOAD)
         BAL   LINKREG,CVB              CONVERT DATA TO BINARY
         ST    R1,#RECLD                STORE DATA
         SPACE 3
PARM4    DS    0H
         LA    PDE,RECADD               ADDR OF PDE (RECORDS ADDED)
         TM    DATASW(PDE),KEYED        WAS PARAMETER KEYED
         BNO   PARM5                    NO-GO PROCESS NEXT PARAMETER
         BAL   LINKREG,CVB              YES-CONVERT DATA TO BINARY
         ST    R1,#RECADD               STORE DATA
         LA    PDE,CIPER  ADDR OF PDE(PERCNT OF INSRT RATE/CI FREESPC)
         TM    DATASW(PDE),KEYED        WAS PARAMETE KEYED
         BNO   PARM5                    NO-GO PROCESS NEXT PARAMETER
         BAL   LINKREG,CVB              YES-CONVERT DATA TO BINARY
         C     R1,=F'100'               IS RATIO GREATER THAN 100
         BH    BADCIPER
         ST    R1,CIRATIO       SET CI FREESPACE RATIO(INSRT RATE)
         LA    WORKREG,100              SET FOR DETERM CA RATIO
         SR    WORKREG,R1               SUB CI RATIO FOR CA RATIO
         ST    WORKREG,CARATIO  SET CA FREESPACE RATIO(INSRT RATE)
PARM5    DS    0H
         LA    PDE,KEYLENTH             ADDR OF PDE(KEY LENGTH)
         TM    DATASW(PDE),KEYED        WAS PARAMETER KEYED
         BNO   CHKRADD                  NO- GO SEE IF RADD KEYED
         BAL   LINKREG,CVB              YES- CONVERT DATA TO BINARY
         STH   R1,KEYLEN                STORE DATA
         CH    R1,=H'255'               IS KEYLEN GREATER THAN MAX
         BH    BADKLEN                  YES- ERROR IN KEY
         B     PARM6                    GO PROCESS NEXT PARAMETER
CHKRADD  DS    0H
         OC    #RECADD,#RECADD          WAS RADD PARAMETER KEYED
         BZ    PARM6                    NO-GO PROCESS NEXT PARAMETER
         A     R1,RECLOAD              ADD RADD TO RLOAD
         ST    R1,RECLOAD              SET NEW RECLOAD
         XC    #RECADD,#RECADD          CLEAR BECAUSE NOT KSDS
         SPACE 3
PARM6    DS    0H
         L     R15,KEYCOMP              ADDR OF KEYED DATA
         LA    PDE,KEYCOMP              ADDR OF PDE(KEY COMPRESSION)
         TM    DATASW(PDE),KEYED        WAS PARAMETER KEYED
         BNO   SETFAIR                  NO- GO SET TO DEFAULT
         CLC   DATALEN(2,PDE),=H'3'     IS DATA LENGTH LESS THAN 3
         BL    BADKCOMP                 YES-ERROR IN SPECIFICATION
         BE    COMPBAD                  EQUAL TO 3 CAN ONLY EQUAL 'BAD'
         CLC   0(4,R15),=C'FAIR'        IS IT 'FAIR'
         BE    SETFAIR                  YES- GO SET
         CLC   0(4,R15),=C'GOOD'        IS IT 'GOOD'
         BNE   BADKCOMP                 NO- ERROR IN SPECIFICATION
         MVC   KEYCMP,=C'GOOD'          YES- SET AND GO PROCESS NEXT
         B     PARM7                    PARAMETER
COMPBAD  DS    0H
         CLC   0(3,R15),=C'BAD'         IS IT 'BAD'
         BNE   BADKCOMP                 NO - ERROR IN SPECIFICATION
         MVC   KEYCOMP,=C'BAD '         YES - SET AND GO PROCESS NEXT
         B     PARM7                    PARAMETER
SETFAIR  DS    0H
         MVC   KEYCMP,=C'FAIR'          SET
         SPACE 3
PARM7    DS    0H
         L     R15,DEVTYPE              ADDR OF KEYED DATA
         LA    PDE,DEVTYPE              ADDR OF PDE(DEVICE TYPE)
         TM    DATASW(PDE),KEYED        WAS PARAMETER KEYED
         BNO   SET3350                  NO- GO SET TO DEFAULT
         CLC   DATALEN(2,PDE),=H'4'     WAS 4 NUMERICS KEYED
         BL    BADDEV                   NO- ERROR IN DEVICE TYPE
         CLC   0(4,R15),=C'3350'        WAS 3350 DEVICE TYPE KEYED
         BE    SET3350                  YES GO SET
         CLC   0(4,R15),=C'3330'        WAS 3330 DEVICE TYPE KEYED
         BNE   BADDEV                   NO - ERROR IN DEVICE TYPE
         MVC   DEVICE,=C'3330'          SET FOR 3330
         B     PARM8910
SET3350  DS    0H
         MVC   DEVICE,=C'3350'          SET FOR 3350
PARM8910 DS    0H
         MVC   IMBFCTR,IMB
         MVC   REPLFCTR,REPL
         MVC   ALLFCTR,ALL
* RELEASE PDL STORAGE
*
         USING PPL,PPLREG
         LA    PPLREG,PPLAREA
         L     R1,PPLANS
         IKJRLSA (1)               RELEASE PARSE STORAGE (PDL)
*
         B     CKDEVTYP            GO AND PROCESS DEVICE TYPE TABLE
* CONVERT A PARAMETER TO BINARY
CVB      DS    0H
         L     R15,0(PDE)          ADDR OF KEYED DATA
         XR    R1,R1               CLEAR FOR PROCESSING
         IC    R1,PACKPARM+1       SET TO INSTRUCTION LENGHTS
         AH    R1,DATALEN(PDE)     ADD LENGTH OF KEYED DATA (OPERAND 2)
         BCTR  R1,0                SUB 1 FOR EXECUTE  INSTRUCTION
         EX    R1,PACKPARM         DO VARIABLE PACK
         CVB   R1,DOUBLE           CONVERT PARM VALUE TO BINARY
         BR    LINKREG
PACKPARM PACK  DOUBLE,0(0,R15)     VARIABLE LENGTH PACK
         EJECT
* INPUT AND PARSE ERROR ROUTINES...DESCRIPTIVE MESSAGE OF ERROR
* CONDITION WILL BE DISPLAYED ON TERMINAL........
*
*
BADPARSE DS    0H
         CVD   R15,DOUBLE
         OI    DOUBLE+7,X'0F'
         UNPK  PARSRTNC,DOUBLE+5(2)
         LA    R2,PARSMSG
         BAL   LINKREG,TPUT
         B     RETURN
         SPACE 3
BADKLEN  DS    0H
         LA    R2,KLENMSG
         BAL   LINKREG,TPUT
         B     RETURN
         SPACE 3
BADKCOMP DS    0H
         LA    R2,KCMPMSG
         BAL   LINKREG,TPUT
         B     RETURN
         SPACE 3
BADDEV   DS    0H
         LA    R2,DEVMSG
         BAL   LINKREG,TPUT
         B     RETURN
         SPACE 3
BADRECSZ DS    0H
         LA    R2,RECSMSG
         BAL   LINKREG,TPUT
         B     RETURN
         SPACE 3
BADCIPER DS    0H
         LA    R2,CIPRMSG
         BAL   LINKREG,TPUT
         B     RETURN
         SPACE 3
         EJECT
* THIS ROUTINE WILL SET UP THE CORRECT DEVICE TABLE POINTERS ACCORDING
* TO THE DEVICE TYPE SPECIFIED BY INPUT OR THE DEFAULT DEVICE TYPE..
*
*
CKDEVTYP DS    0H
         USING DEVENTRY,DEVREG          ADDRESSABILITY FOR DSECT
         CLC   DEVICE,=C'3350'
         BE    USE3350
*3330 DEVICE TYPE IS BEING USED
*
         L     DEVREG,A3330TAB     POINT TO BEGINNING OF 3330 TABLE
         ST    DEVREG,DEVREGSV     SAVE REGISTER
         B     NULLCISZ            GO NULLIFY DCISZ TABLE ENTRIES
         SPACE 3
USE3350  DS    0H
         L     DEVREG,A3350TAB     POINT TO BEGINNING OF 3350 TABLE
         ST    DEVREG,DEVREGSV     SAVE REGISTER
         B     NULLCISZ            GO NULLIFY DCISZ TABLE ENTRIES
         EJECT
* THIS ROUTINE WILL SCAN THE DATA CI SIZE FIELD IN EACH ENTRY OF THE
* DEVICE TABLE TO SEE IF THE DATA CI SIZE IS TO SMALL FOR THE
* MAXRECSZ+7.. IF SO THEN THAT DATA CI SIZE ENTRY WILL NULLED(ZEROS)
* SO THAT IT WILL NOT BE CONSIDERED WHEN FINDING THE BEST DATA CI SIZES
*
*
NULLCISZ DS    0H
         L     DEVREG,DEVREGSV         FIRST TABLE ENTRY
         L     R15,MAXRECSZ
         LA    R15,7(R15)              ADD 7 TO MAXRECSZ
NULLLOOP DS    0H
         CLC   DCISZ,=F'-1'            ARE WE AT THE END OF THE TABLE
         BE    PRODCISZ                YES- GO PROCESS DCISZ'S IN TABLE
         C     R15,DCISZ               IS MAXRECSZ+7 GREATER THAN DCISZ
         BNH   PRODCISZ                NO-GO PROCESS DCISZ'S IN TABLE
         XC    DCISZ,DCISZ             YES-NULLIFY THIS TABLE ENTRY
         LA    DEVREG,DEVENLEN(DEVREG) POINT TO THE NEXT TABLE ENTRY
         B     NULLLOOP                GO LOOK AGAIN
         EJECT
* THIS ROUTINE WILL PROCESS EACH DATA CI SIZE ENTRY IN THE DEVICE TABLE
* IN ORDER TO DETERMINE WHICH DATA CI SIZES ARE THE BEST CHOICES FOR
* THE INDICATED RECORDSIZES........
*
*
PRODCISZ DS    0H
         L     DEVREG,DEVREGSV     SET TO TOP OF DEVICE TABLE
DCISZP2  OC    DCISZ,DCISZ         DOES THIS DCISZ QUALIFY
         BZ    NXTDCISZ            NO -  GO BUMP TO NEXT ENTRY
         CLC   DCISZ,=F'-1'        IS IT THE END OF THE DEVICE TABLE
         BE    DETBESTD            YES - GO FIND THE BEST DCISZ'S
         BAL   LINKREG,CALC#0D     GO CALCULATE #TRKCA
         BAL   LINKREG,CALC#1D     GO CALCULATE #CICA
         BAL   LINKREG,CALC#2D     GO CALCULATE #RCINFSP
         BAL   LINKREG,CALC#3D     GO CALCULATE #RCANFSP
         BAL   LINKREG,CALC#4D     GO CALCULATE #TRKNFSP
         OC    #RECADD,#RECADD     ARE RECORDS GOING TO BE ADDED
         BZ    NOFREESP            NO- GO PROCESS NO FREESPACE LOGIC
* RECORDS TO BE ADDED WAS SPECIFIED(RECADD INPUT KEYWORD)
* SO WE MUST CALCULATE HOW MUCH FREESPACE WILL BE NEEDED TO ADD THE
* RECORDS SPECIFIED TO THE PRIMARY DATASET ALLOCATION
         OC    PERRADD,PERRADD WAS THIS VALUE ALREADY CALCULATED
         BNZ   DO#6           YES DONT DO IT AGAIN
         BAL   LINKREG,CALC#5D     GO CALCULATE PERRADD
DO#6     DS    0H
         BAL   LINKREG,CALC#6D     GO CALCULATE #FRCA
         BAL   LINKREG,CALC#7D     GO CALCULATE PERFSPCA
         BAL   LINKREG,CALC#8D     GO CALCULATE PERFSPC2
         BAL   LINKREG,CALC#9D     GO CALCULATE #RCILOAD
         BAL   LINKREG,CALC#10D    GO CALCULATE #RCALOAD
         BAL   LINKREG,CALC#12D    GO CALCULATE #TRKDATA
NXTDCISZ DS    0H
         LA    DEVREG,DEVENLEN(DEVREG)  POINT TO NEXT TABLE ENTRY
         B     DCISZP2
         SPACE 3
NOFREESP DS    0H
         MVC   #RCILOAD,#RCINFSP        SAME AS NO FREESPACE
         MVC   #RCALOAD,#RCANFSP        SAME AS NO FREESPACE
         MVC   #TRKDATA,#TRKNFSP        SAME AS NO FREESPACE
         B     NXTDCISZ                 GO DO THE NEXT DCISZ
         EJECT
CALC#0D  DS    0H
*
* THIS ROUTINE WILL CALCULATE THE NUMBER OF TRACKS PER CONTROL AREA
*
* #TRKCA = ((RECSZ+3)*(#RECLD+#RECADD))/(PHYBLKSZ*#BLKSTRK)
*        OR
* #TRKCA = # OF TRACKS PER CYLINDER (19 OR 30)
*        WHICHEVER IS SMALLER
*
* NOTE: IF ((RECSZ+3)*(#RECLD+#RECADD) IS  <   CURRENT DCISZ TABLE
* ENTRY THEN THIS AND ALL SUBSEQUENT DCISZ TABLE ENTRIES WILL BE
* NULLED(ZEROS) AND THE PROCESSING OF THE DCISZ TABLE WILL BE STOPPED
* AND CONTROL WILL BE PASSED TO THE BESTFIT ROUTINE.....
*
         L     DIVIDEND,RECSZ
         LA    DIVIDEND,3(DIVIDEND)
         L     WORKREG,#RECLD
         A     WORKREG,#RECADD
         MR    EVENODD,WORKREG
         C     DIVIDEND,DCISZ  IS THE DATASET SMALLER THAN THIS DCISZ
         BL    NULL#0D    YES-GO NULLIFY THIS AND REMAINING DCISZ'S
         L     DIVISOR,PHYBLKSZ
         M     DIVISOR-1,#BLKSTRK
         DR    EVENODD,DIVISOR
         OR    REMAINDR,REMAINDR
         BZ    CHKTRKS
         LA    QUOTIENT,1(QUOTIENT)
CHKTRKS  DS    0H
         AH    QUOTIENT,IMBFCTR
         C     QUOTIENT,#TRKCA
         BNLR  LINKREG
         ST    QUOTIENT,#TRKCA
         BR    LINKREG
NULL#0D  DS    0H
         XC    DCISZ,DCISZ  NULL THIS DCISZ ENTRY(GREATER THAN DATASET)
         LA    DEVREG,DEVENLEN(DEVREG) POINT TO NEXT DCISZ ENTRY
         CLC   DCISZ,=F'-1'       IS IT THE END OF THE TABLE
         BE    DETBESTD           YES-GO PROCESS BEST FITS...
         B     NULL#0D            NO-GO NULLIFY THIS ENTRY ALSO...
         EJECT
CALC#1D  DS    0H       ROUNDED DOWN
* #CICA= #TRKCA*#BLKSTRK/(DCISZ/PHYBLKSZ)
* THIS ROUTINE WILL CALCULATE THE NUMBER OF CT'S PER CA FOR THE CONTROL
* INTERVAL SIZE DEVICE TABLE ENTRY
         L     DIVIDEND,#TRKCA     NUMBER OF TRACKS PER CONTROL AREA
         SH    DIVIDEND,IMBFCTR    SUB IMBED FACTOR (IF ANY)
         M     EVENODD,#BLKSTRK    MULTIPLY BY THE NUMBER OF PHYSICAL
         LR    WORKREG,QUOTIENT    BLOCKS PER TRACK
         L     DIVIDEND,DCISZ      LOAD FOR DIVIDE
         XR    EVENODD,EVENODD     CLEAR
         L     DIVISOR,PHYBLKSZ    LOAD FOR DIVIDE
         DR    EVENODD,DIVISOR     DIVIDE DCISZ BY PHYSBLK SIZE
         LR    DIVISOR,QUOTIENT    USE RESULT FOR NEXT DIVIDE
         XR    EVENODD,EVENODD     CLEAR
         LR    DIVIDEND,WORKREG    NUMBER OF PHYSICAL BLOCKS PER CA
         DR    EVENODD,DIVISOR     DIVIDE PHYSBLKS/CA BY PHYSBLKS/CI
         ST    QUOTIENT,#CICA      GIVING #CICA(ROUNDED DOWN)
         BR    LINKREG             GO BACK
         SPACE 3
CALC#2D  DS    0H  ROUNDED DOWN                 ROUNDED DOWN
* #RCINFSP = (DCISZ-7)/(RECSZ) FIXED OR (DCISZ-4)/(RECSZ+3) VARIABL
* THIS ROUTINE WILL CALCULATE THE NUMBER OF RECORDS THAT CAN FIT IN A
* CONTROL INTERVAL WITH NO FREESPACE SPECIFIED
         XR    EVENODD,EVENODD     CLEAR
         L     DIVIDEND,DCISZ      DATA CONTROL INTERVAL SIZE
         L     DIVISOR,RECSZ       AVERAGE RECORD SIZE
         CLC   RECSZ,MAXRECSZ      IS IT FIXED LENGTH RECORDS
         BNE   VAR#2D              NO - GO DO VARIABLE LENGTH LOGIC
* FIXED LENGTH RECORDS
         SH    DIVIDEND,=H'7'      SUB LENGTH OF CIDF+RDF FIELDS
         B     DOCALC#2
* VARIBLE LENGTH RECORDS
VAR#2D   DS    0H
         SH    DIVIDEND,=H'4'      SUB LENGTH OF CIDF FIELD
         LA    DIVISOR,3(DIVISOR)  ADD LENGTH OF RDF  FIELD
DOCALC#2 DS    0H
         DR    EVENODD,DIVISOR     DIVIDE DCISZ BY RECSZ
         ST    QUOTIENT,#RCINFSP   GIVING #RCINFSP(ROUNDED DOWN)
         BR    LINKREG
         EJECT
CALC#3D  DS    0H
*  #RCANFSP =  #RCINFSP * #CICA
* THIS ROUTINE WILL CALCULATE THE NUMBER OF RECORDS PER CONTROL AREA
* WITH NO FREESPACE
*
         L     DIVIDEND,#RCINFSP
         M     EVENODD,#CICA
         ST    QUOTIENT,#RCANFSP
         BR    LINKREG
         SPACE 3
CALC#4D  DS    0H ROUNDED UP (PUTS #TRKNFSP ON CA BOUNDARY)
*  #TRKNFSP =  #RECLD/#RCANFSP*#TRKCA
* THIS ROUTINE WILL CALCULATE THE NUMBER OF TRACKS NEEDED FOR THE DATA
* COMPONENT (NO FREESPACE)
         L     DIVISOR,#RCANFSP
         XR    EVENODD,EVENODD     CLEAR
         L     DIVIDEND,#RECLD
         DR    EVENODD,DIVISOR
         OR    REMAINDR,REMAINDR   IS THIS THERE A REMINDER
         BZ    MULT#4D             NO-GO MULTIPLY
         LA    QUOTIENT,1(QUOTIENT) YES ADD 1 TO QUOTIENT
MULT#4D  M     EVENODD,#TRKCA      MULT BY TRACK PER CA
         ST    QUOTIENT,#TRKNFSP   GIVING #TRKNFSP
         BR    LINKREG
         SPACE 3
CALC#5D  DS    0H        ROUNDED UP
*  PERRADD =  #RECADD/(#RECLD+#RECADD)
* THIS ROUTINE CALCULATES THE INSERTION RATE USING THE RECADD FIELD
* OBTAINED FROM INPUT......
*
*
         XR    EVENODD,EVENODD
         L     DIVISOR,#RECLD
         L    DIVIDEND,#RECADD
         AR    DIVISOR,DIVIDEND ADD RECADD TO RECLOADED FOR INSRT RATE
         MH   DIVIDEND,=H'100'          MULT DIVIDEND BY 100
         DR    EVENODD,DIVISOR          YES- FIND PERCENTAGE
         OR    REMAINDR,REMAINDR        IS THERE A REMAINDER
         BZ    SETPERCT                 NO-GO SET PERCENTAGE
         LA    QUOTIENT,1(QUOTIENT)     YES-ROUND UP
SETPERCT DS    0H
         ST    QUOTIENT,PERRADD         SET PERCENTAGE
         BR    LINKREG
         EJECT
CALC#6D  DS    0H     ROUNDED DOWN
*  #FRCA =  #RCANFSP*PERRADD
*  THIS ROUTINE WILL DETERMINE THE NUMBER OF FREE RECORDS NEEDED PER
*  CONTROL AREA
*
         XR    EVENODD,EVENODD     CLEAR
         L     DIVIDEND,#RCANFSP   NUMBER OF RECORDS PER CA
         M     EVENODD,PERRADD     MULT BY PERCENT OF RECADD TO RECLOAD
         LH    DIVISOR,=H'100'     SET FOR TRUE PERCENTAGE CALC
         DR    EVENODD,DIVISOR     DIVIDE BY 100
         LTR   QUOTIENT,QUOTIENT   IS IT LESS THAN 1
         BZ    SETFRCA1            YES SET #FRCA TO 1
         C     QUOTIENT,#RCANFSP   ARE WE ASKING FOR 100% FREESPACE
         BL    NOSUB#6 NO THEN DONT SUB #CICA
         S     QUOTIENT,#CICA  SUB SO 1 REC/CI IS LOADED
NOSUB#6  DS    0H
         ST    QUOTIENT,#FRCA      SET #FRCA
         L     QUOTIENT,#FRCA
         M     EVENODD,CARATIO NO OF FREE RECORDS NEEDED FOR FREE CI'S
         D     EVENODD,=F'100'          TRUE VALUE
         ST    QUOTIENT,FRCAFSP
         BR    LINKREG
SETFRCA1 DS    0H
         MVC   #FRCA,=F'1'         SET TO | (MINIMUM)
         BR    LINKREG
         EJECT
CALC#7D  DS   0H
* PERFSPCA  = (FRCAFSP/#RCINFSP)/#CICA
* THIS CALCULATION DETERMINES THE CA FREESPACE PERCENTAGE BY
* DETERMINING THE NUMBER OF FREE CI'S NEEDED TO CONTAIN ENOUGH
* RECORDS TO SATISFY THE CARATIO OF THE INSERTION RATE..
*
*
         OC    FRCAFSP,FRCAFSP          DO WE NEED CA FREESPACE
         BZR   LINKREG                  NO GET OUT
         XR    EVENODD,EVENODD          CLEAR
         L     DIVIDEND,FRCAFSP         RECS NEEDED IN CA FREESPACE
         D     EVENODD,#RCINFSP         NO OF FREE CI'S NEEDED
         OR    REMAINDR,REMAINDR        IS THERE A REMAINDER
         BZ    BYRND#71                 NO DONT ROUND
         LA    QUOTIENT,1(QUOTIENT)     YES ROUND UP
BYRND#71 DS    0H
         M     EVENODD,=F'100' FOR TRUE PERCENTAGE
         D     EVENODD,#CICA
         OR    REMAINDR,REMAINDR        DO WE HAVE A REMAINDER
         BZ    BYRND#72                 NO DONT ROUND
         LA    QUOTIENT,1(QUOTIENT)     YES ROUND UP
BYRND#72 DS    0H
         ST    QUOTIENT,PERFSPCA        SET CA FREESPACE PERCENTAGE
         L     DIVIDEND,#CICA           NO OF CI'S PER CA
         M     EVENODD,PERFSPCA         FOR PERCENTAGE
         D     EVENODD,=F'100'          FIND FREE CI'S PER CA
         OR    REMAINDR,REMAINDR        CHK FOR REMAINDER
         BZ    BYRND#73                 NO DONT ROUND
         LA    QUOTIENT,1(QUOTIENT)     ROUND UP
BYRND#73 DS    0H
         C     QUOTIENT,#CICA        ARE FREE CI'S EQUAL TO MAX CI/CA
         BNE   STORE#7
         BCTR  QUOTIENT,0               SUB 1(CANT BE SAME AS #CICA)
STORE#7  DS    0H
         ST    QUOTIENT,#FCICA          STORE IT
         M     EVENODD,#RCINFSP HOW MANY REC FIT IN FREE CI'S
         L     WORKREG,#FRCA TOTAL NO. OF FREE RECS NEEDED/CA
         SR    WORKREG,QUOTIENT   NO. OF RECS NEEDED IN CIFREESPACE
         BMR   LINKREG  MAKE CI FREESPACE ZERO
         ST    WORKREG,FRCIFSP  STORE IN TABLE
         BR    LINKREG                  GET OUT
         EJECT
CALC#8D  DS    0H
*
*PERFSPCI  =   (FRCIFSP/(#CICA-(#FCICA))*RECSZ+X)/DCISZ
*      WHERE  X =  0 FOR FIXED LENGTH RECORDS,
*                  3 FOR VARIABLE LENGTH RECORDS
*
*
         L     DIVISOR,#CICA
         S     DIVISOR,#FCICA           SUB NUMBER OF FREE CI'S IN A CA
         OC    FRCIFSP,FRCIFSP          DO WE NEED AND CI FREESPACE
         BZR   LINKREG                  GO GET OUT
         L     DIVIDEND,FRCIFSP
         XR    EVENODD,EVENODD          CLEAR
         DR    EVENODD,DIVISOR
         OR    REMAINDR,REMAINDR        DO WE HAVE A REMAINDER
         BZ    BYRND#81                 NO DONT ROUND
         LA    QUOTIENT,1(QUOTIENT)     YES ROUND UP
BYRND#81 DS    0H
         L     DIVISOR,RECSZ            RECORD SIZE
         CLC   RECSZ,MAXRECSZ           CHECK FOR FIXED/VARIABLE
         BE    FIX#8                    FIXED LENGTH
         LA    DIVISOR,3(DIVISOR)       ADD 3 FOR VAR RECS
FIX#8    DS    0H
         MR    EVENODD,DIVISOR          BYTES NEEDED FOR FREE RECS/CI
         M     EVENODD,=F'100'          FOR PERCENTAGE
         L     DIVISOR,DCISZ            DATA CI SIZE
         DR    EVENODD,DIVISOR          PERCENT OF CI
         OR    REMAINDR,REMAINDR        DO WE HAVE A REMAINDER
         BZ    BYRND#82                 NO DONT ROUND
         LA    QUOTIENT,1(QUOTIENT)     YES ROUND UP
BYRND#82 DS    0H
         ST    QUOTIENT,PERFSPCI        SET CI FREESPACE PERCENTAGE
         BR    LINKREG
         EJECT
CALC#9D  DS    0H
*  RCILOAD = (DCISZ*(1-PERFSPCI)-7)/(RECSZ+0)   FOR FIXED LENGTH RECS
*          = (DCISZ*(1-PERFSPCI)-7)/(RECSZ+3)   FOR VARIABLE LENGTH REC
* THIS CALCULATION WILL COMPUTE THE NUMBER OF RECORDS THAT CAN FIT IN
* A CI AT LOAD TIME.
*
         L     DIVIDEND,DCISZ
         LH    WORKREG,=H'100'     SET FOR (1-PERFSPCI)
         S     WORKREG,PERFSPCI    SUB CI FREESPACE PERCENTAGE
         MR    EVENODD,WORKREG     GET NUMBER OF CI BYTES*100 AVAIL LD
         XR    EVENODD,EVENODD     CLEAR
         D     EVENODD,=F'100'     GET NUMBER OF CI'S BYTES AVAIL AT LD
         SH    QUOTIENT,=H'7'      SUB CIDF AND RDF FIELDS
         XR    EVENODD,EVENODD     CLEAR
         L     DIVISOR,RECSZ
         CLC   RECSZ,MAXRECSZ      IS IT FIXED LENGTH RECORDS
         BE    FIXCALC9            YES - DONT AD 3 BYTE RDF
         LA    DIVISOR,3(DIVISOR)  NO- VARIABLE SO ADD 3 BYTE RDF
FIXCALC9 DS    0H
         DR    EVENODD,DIVISOR     PERFORM FINAL CALC
         OR    QUOTIENT,QUOTIENT  IS IT LESS THAN ONE RECORD
         BNZ   STORE                    NO GO STORE IT
         MVC   PERFSPCI,=F'100'  CHANGE CI FREESPACE TO 100 PERCENT
         MVC   #RCILOAD,=F'1'           SET REC PER CI TO MINIMUM
         BR    LINKREG                  GET THE HELL OUT
STORE    DS    0H
         ST    QUOTIENT,#RCILOAD
         BR    LINKREG
         EJECT
CALC#10D DS    0H
* #RCALOAD = #RCILOAD*(#CICA*(1-PERFSPCA))
* THIS CALCULATION WILL COMPUTE   THE NUMBER OF RECORDS THAT CAN FIT
* IN A CONTROL AREA AT LOAD TIME
*
         XR    EVENODD,EVENODD     CLEAR
         LH    WORKREG,=H'100'
         S     WORKREG,PERFSPCA    GET PERCENT OF CI/CA USED FOR LOAD
         L     DIVIDEND,#CICA
         MR    EVENODD,WORKREG
         D     EVENODD,=F'100'     GET NUMBER OF NONFREE CI/CA
         OR    QUOTIENT,QUOTIENT        IS LOAD CI'S/CA ZERO
         BNZ   LOAD#10                  NO EVERYTHINGS COOL
         LA    QUOTIENT,1(QUOTIENT)     YES-ADD 1(CANT BE ZERO)
LOAD#10  DS    0H
         LR    WORKREG,QUOTIENT
         L     QUOTIENT,#RCILOAD
         MR    EVENODD,WORKREG     DETERMINE NUMBER OF REC/CA AT LOAD
         ST    QUOTIENT,#RCALOAD
         BR    LINKREG
         SPACE 3
CALC#12D DS    0H
* #TRKDATA = #RECLD/#RCALOAD*#TRKCA
* THIS CALCULATION WILL COMPUTE THE NUMBER OF TRACKS NEEDED FOR THE
* DATA COMPONENT
*
*
         XR    EVENODD,EVENODD     CLEAR
         L     DIVIDEND,#RECLD
*        A     DIVIDEND,#RECADD
         L     DIVISOR,#RCALOAD
         DR    EVENODD,DIVISOR
         OR    REMAINDR,REMAINDR   IS THIS THERE A REMAINDER
         BZ    BYRND#11            NO - DONT ROUND
         LA    QUOTIENT,1(QUOTIENT) ROUND UP
BYRND#11 DS    0H
         L     WORKREG,#TRKCA
         MR    EVENODD,WORKREG
         ST    QUOTIENT,#TRKDATA
         BR    LINKREG
         EJECT
DETBESTD DS    0H
*
* THIS ROUTINE WILL WILL SCAN THE DEVICE TABLE FOR THE FOUR BEST DATA
* CONTROL INTERVAL SIZES. THE CRITERIA FOR CHOOSING THESE CI SIZES
* WILL BE DISK UTILIZATION WHICH IS THE SMALLEST OF #TRKDATA FIELDS IN
* THE TABLE
*
         OC    ALLFCTR,ALLFCTR    WAS DISPLAY ALL DCISZ'S SPECIFIED
         BNZ   SCREEN             YES THEN DONT DETERMINE BEST FIT.....
*                                 GO DISPLAY ALL THE DCISZ'S
         L     DEVREG,DEVREGSV     POINT TO TOP OF DEVICE TABLE
BEGNBFIT DS    0H
         LA    R15,4               NUMBER OF BEST FITS
         LA    WORKREG,BESTFIT1    POINT TO FIRST BEST FIT
         CLC   DCISZ,=F'-1'        ARE WE AT THE END OF DEVICE TABLE
         BE    DISPLAYD            YES- GO DISPLAY DATA INFO
         OC    DCISZ,DCISZ         IS THIS DATA CISIZE NULLED
         BZ    NEXTDCZ             YES-GO PROCESS NEXT TABLE ENTRY
BFITLOOP L     R14,0(WORKREG)      LOAD CURRENT BEST FIT
         LTR   R14,R14             IS THERE ONE
         BZ    MAKECURB            NO MAKE THIS ONE THE CURRENT BESTFIT
         CLC   #TRKDATA,#TRKDATA-DEVENTRY(R14) IS THIS DCISZ BETTER
         BNL   NEXTBFIT  NO GO PROCESS NEXT BEST FIT
         MVC   12(4,WORKREG),8(WORKREG) YES-MOVE CURRENT BESTFIT DOWN
         MVC   8(4,WORKREG),4(WORKREG)
         MVC   4(4,WORKREG),0(WORKREG)
MAKECURB DS    0H
         ST    DEVREG,0(WORKREG)   SAVE AS BEST FIT IN CURRENT POSITION
         B     NEXTDCZ             GO PROCESS NEXT DCISZ
NEXTBFIT DS    0H
         LA    WORKREG,4(WORKREG)      BUMP TO NEXT BEST FIT
         BCT   R15,BFITLOOP        DID WE LOOK AT THE FOUR BEST
         B     NEXTDCZ             YES- GO LOOK AT NEXT DCISZ
NEXTDCZ  DS    0H
         LA    DEVREG,DEVENLEN(DEVREG)  BUMP TO NEXT TABLE ENTRY
         B     BEGNBFIT            GO LOOK AT THIS ONE
DISPLAYD DS    0H
*
* THIS ROUTINE WILL SORT THE FOUR BEST FITS INTO ASCENDING ORDER
* BY DCISIZE
*
         LA    R15,DATAFIT1        RECEIVING FIELD FOR TABLE ADDRESSES
         LA    R4,4                LOOPING FACTOR
         MVC   WORKFIT(16),BESTFIT1 SAVE BEST FITS IN #TRKDATA ORDER
BESTLOOP DS    0H
         LA    R8,3                LOOPING FACTOR
         LA    WORKREG,WORKFIT     POINT TO THE FIRST OF THE FIRST FITS
         LA    R9,WORKFIT+4        POINT TO NEXT ENTRY FOR COMPARE
*
* NOTE :  THE TABLE ENTRY WITH THE LOWEST ADDRESS HAS THE SMALLEST
*      DCISIZE BECAUSE OF THE ORDER OF THE TABLE
*
LOWLOOP  DS    0H
         CLC   0(4,WORKREG),0(R9)   DOES WORKREG POINT TO THE ENTRY
*                                  WITH THE LOWEST ADDRESS?
         BL    BUMPR9              YES-GO SET R9 FOR NEXT COMPARE
         LR    WORKREG,R9          NO-THEN MAKE SURE IT DOES
BUMPR9   DS    0H
         LA    R9,4(R9)            SET FOR NEXT COMPARE
         BCT   R8,LOWLOOP          CONTINUE SEARCH FOR LOWEST ADDRESS
         MVC   0(4,R15),0(WORKREG) SAVE IT IN RECEIVING FIELD
         MVC   0(4,WORKREG),=F'-1' NULL THAT ENTRY SO WE CAN FIND
*                                  THE NEXT LOWEST ENTRY
         LA    R15,12(R15)         BUMP RECEIVING FIELD POINTER
         BCT   R4,BESTLOOP         GO FIND THE NEXT ONE
* INITIALIZE DATAFIT FIELDS WITH THEIR APPROPRIATE DESCRIPTORS..
         MVC   DATAFIT1+4(8),=C'DIR     '
         MVC   DATAFIT2+4(8),=C'DIR-SEQ '
         MVC   DATAFIT3+4(8),=C'SEQ-DIR '
         MVC   DATAFIT4+4(8),=C'SEQ     '
         EJECT
SCREEN   DS    0H
*
* THE FOLLOWING ROUTINE WILL FORMAT AND PRINT ALL THE HEADING AND
* DETAIL LINES
         MVC   PLINE,=CL79' '
         MVC   PLINE(40),=C'          V S A M   A N A L Y Z E R   ( '
         OC    KEYLEN,KEYLEN       IS THIS FOR A KSDS?
         BZ    ESDSRRDS            NO-THEN SET UP HEADER ACCORDINGLY
         MVC   PLINE+40(10),=C'K S D S   '
         LA    WORKREG,PLINE+50
         B     FINHEAD
ESDSRRDS DS    0H
         MVC   PLINE+40(20),=C'E S D S / R R D S   '
         LA    WORKREG,PLINE+60
FINHEAD  DS    0H
         CLC   DEVICE,=C'3330'     IS DEVINE TYPE 3330?
         BNE   FIFTY               NO-THEN ITS A 3350
         MVC   0(9,WORKREG),=C'3 3 3 0 )'
         B     PUTH1
FIFTY    DS    0H
         MVC   0(9,WORKREG),=C'3 3 5 0 )'
PUTH1    DS    0H
         BAL   LINKREG,TPUTPLIN
         MVC   PLINE,=CL79' '    CLEAR
         BAL   LINKREG,TPUTPLIN
         MVC   PLINE,=CL79' '  CLEAR
         MVC   PLINE(15),=C'DATA COMPONENT:'
         BAL   LINKREG,TPUTPLIN
         MVC   PLINE,=CL79' ' CLEAR
         MVC   PLINE(78),=C'                             LOAD     LOAD *
                  CI-CA     MAX      MAX     SPACE'
         BAL   LINKREG,TPUTPLIN
         MVC   PLINE,=CL79' '
         MVC   PLINE,=CL79'   DCISZ   TRK/CA   CI/CA   REC/CA   REC/CI *
                 FRSPC   REC/CA   REC/CI   CYL-TRK'
         BAL   LINKREG,TPUTPLIN
         OC    ALLFCTR,ALLFCTR    WAS DISPLY ALL DCISZ'S SPECIFIED
         BZ    BESTDETL           NO-GO DISPLY 4 BEST
         L     DEVREG,DEVREGSV    YES-POINT TO TOP OF DEVICE TABLE
         B     ALL1        GO-PROCESS 1ST DETAIL LINE FOR ALL DCISZ'S
BESTDETL DS    0H
         LA    R2,BESTFIT1
         LA    R3,4
ALL1     DS    0H
         ZAP   COUNT,=P'0'
DETLOOP  DS    0H
         MVC   PLINE,=CL79' '      CLEAR PRINT LINE
*                                  WAIT TO MOVE PERIOD SO WE CAN FIT
*                                  THE EDIT MASK FOR DCISZ
* CONVERT CONTROL INTERVAL SIZE
*
         OC    ALLFCTR,ALLFCTR    WAS DISPLY ALL DCISZ'S SPECFD
         BNZ   DONTLOAD           YES-DONT LOAD DEVREG
         L     DEVREG,0(R2)        ADDRESSABILITY TO TABLE ENTRY
DONTLOAD DS    0H
         OC    DCISZ,DCISZ        IS THIS DCISZ NULLED
         BZ    NXTDETAL           YES-GO LOOK AT THE NEXT DCISZ
         L     WORKREG,DCISZ            CONTROL INTERVAL SIZE
         CVD   WORKREG,DOUBLE
         MVC   PLINE+1(8),MASK               SUPPRESS LEADING ZEROS
         ED    PLINE+1(8),DOUBLE+4
         OI    PLINE+8,X'F0'       GET RID OF SIGN
         AP    COUNT,=P'1'         DETAIL LINE COUNTER
         UNPK  PLINE(2),COUNT
         OI    PLINE+1,X'F0'         GET RID OF SIGN
         MVI   PLINE+2,C'.'
*
*
* CONVERT NUMBER OF TRACKS PER CONTROL AREA
*
         L    WORKREG,#TRKCA
         CVD   WORKREG,DOUBLE
         MVC   PLINE+11(4),MASK
         ED    PLINE+11(4),DOUBLE+6
*
*
* CONVERT NUMBER OF CONTROL INTERVALS PER CONTROL AREA
*
         L    WORKREG,#CICA
         CVD   WORKREG,DOUBLE
         MVC   PLINE+20(4),MASK
         ED    PLINE+20(4),DOUBLE+6
*
*
* CONVERT RECORDS PER CONTROL AREA (TO BE LOADED)
*
         L    WORKREG,#RCALOAD
         CVD   WORKREG,DOUBLE
         MVC   PLINE+27(6),MASK
         ED    PLINE+27(6),DOUBLE+5
*
*
* CONVERT RECORDS PER CONTROL INTERVAL (TO BE LOADED)
*
         L    WORKREG,#RCILOAD
         CVD   WORKREG,DOUBLE
         MVC   PLINE+36(6),MASK
         ED    PLINE+36(6),DOUBLE+5
         OI    PLINE+41,X'F0'
*
*
* CONVERT                  FREESPACE PERCENTAGE
*
         L    WORKREG,PERFSPCA
         CVD   WORKREG,DOUBLE
         MVC   PLINE+46(4),MASK
         ED    PLINE+46(4),DOUBLE+6
         OI    PLINE+49,X'F0'
         L    WORKREG,PERFSPCI
         CVD   WORKREG,DOUBLE
         MVC   PLINE+43(4),MASK
         ED    PLINE+43(4),DOUBLE+6
         OI    PLINE+46,X'F0'
*
*
* CONVERT RECORDS PER CONTROL AREA WITH NO FREESPACE
*
         L     WORKREG,#RCANFSP
         CVD   WORKREG,DOUBLE
         MVC   PLINE+53(6),MASK
         ED    PLINE+53(6),DOUBLE+5
*
*
* CONVERT RECORDS PER CONTROL AREA WITH NO FREESPACE
*
         L     WORKREG,#RCINFSP
         CVD   WORKREG,DOUBLE
         MVC   PLINE+62(6),MASK
         ED    PLINE+62(6),DOUBLE+5
*
*
* CONVERT TOTAL TRACKS NEEDED
*
         XR    EVENODD,EVENODD
         L     DIVIDEND,#TRKDATA
         CLC   DEVICE,=C'3330'
         BNE   USE30
         LA    DIVISOR,19
         B     DIVIDIT
USE30    DS    0H
         LA    DIVISOR,30
DIVIDIT  DS    0H
         DR    EVENODD,DIVISOR
         OR    QUOTIENT,QUOTIENT
         BZ    USET
         OR    REMAINDR,REMAINDR  CYLINDER BOUNDARY?????
         BNZ   USET    SPECIFY SPACE IN TRACKS
         LR    WORKREG,QUOTIENT
         MVI   PLINE+77,C'C'
         B     CONVIT
USET     DS    0H
         L     WORKREG,#TRKDATA
         MVI   PLINE+77,C'T'
CONVIT   DS    0H
         CVD   WORKREG,DOUBLE
         MVC   PLINE+70(6),MASK
         ED    PLINE+70(6),DOUBLE+5
         BAL   LINKREG,TPUTPLIN
NXTDETAL DS    0H
         OC    ALLFCTR,ALLFCTR    WAS DISPLY ALL DCISZ'S SPECFD
         BZ    BESTBMP            NO-GO BUMP THROUGH BEST FIT
         LA    DEVREG,DEVENLEN(DEVREG) NEXT DCISZ
         CLC   DCISZ,=F'-1'       IS IT THE END OF TABLE
         BE    RETURN             YES-GET THE HELL OUT
         B     DETLOOP            NO- GO DO THE NEXT DETAIL LINE
BESTBMP  DS    0H
*
         LA    R2,4(R2)            POINT TO NEXT BEST
         BCT   R3,DETLOOP          CONTINUE
         MVC   PLINE,=CL79' '
         BAL   LINKREG,TPUTPLIN
         B     BOTTOM
BOTTOM   MVC   PLINE,TRAILER
         LA    R4,PLINE+8-18
         LA    R6,4
         LA    R8,BESTFIT1
CLOOP    DS    0H
         LA    R5,4
         LA    R9,DATAFIT1
LINELOOP DS    0H
         CLC   0(4,R8),0(R9)
         BE    GETWORDS
         LA    R9,12(R9)
         BCT   R5,LINELOOP
         LA    R4,18(R4)
         MVC   0(8,R4),=CL8'*ERROR*'
         B     ECONT
GETWORDS DS    0H
         LA    R4,18(R4)
         MVC   0(8,R4),4(R9)
ECONT    LA    R8,4(R8)
         BCT   R6,CLOOP
         BAL   LINKREG,TPUTPLIN
         B     RETURN
         SPACE 3
TPUTPLIN DS    0H
         LA    R1,PLINE
         LA    R0,79
         TPUT ((1)),((0)),R
         BR    LINKREG
TPUT     DS    0H
         XR    R9,R9
         ICM   R9,3,0(R2)
         LA    R8,2(R2)
         TPUT  ((8)),((9)),R
         BR    LINKREG
         SPACE 3
RETURN   DS    0H
         L     R13,SAVE+4
         RETURN (14,12)
         TITLE 'VSAM ANALYZER DEVICE TABLE FOR 3330S MOD 1 AND 2'
DEV3330  DS    0F
         DC    F'512',F'512',F'20',F'19',13F'0'
         DC    F'1024',F'1024',F'11',F'19',13F'0'
         DC    F'1536',F'512',F'20',F'19',13F'0'
         DC    F'2048',F'2048',F'6',F'19',13F'0'
         DC    F'2560',F'512',F'20',F'19',13F'0'
         DC    F'3072',F'1024',F'11',F'19',13F'0'
         DC    F'3584',F'512',F'20',F'19',13F'0'
         DC    F'4096',F'4096',F'3',F'19',13F'0'
         DC    F'4608',F'512',F'20',F'19',13F'0'
         DC    F'5120',F'1024',F'11',F'19',13F'0'
         DC    F'5632',F'512',F'20',F'19',13F'0'
         DC    F'6144',F'2048',F'6',F'19',13F'0'
         DC    F'6656',F'512',F'20',F'19',13F'0'
         DC    F'7168',F'1024',F'11',F'19',13F'0'
         DC    F'8192',F'4096',F'3',F'19',13F'0'
         DC    F'10240',F'2048',F'6',F'19',13F'0'
         DC    F'12288',F'4096',F'3',F'19',13F'0'
         DC    F'14336',F'2048',F'6',F'19',13F'0'
         DC    F'16384',F'4096',F'3',F'19',13F'0'
         DC    F'18432',F'2048',F'6',F'19',13F'0'
         DC    F'20480',F'4096',F'3',F'19',13F'0'
         DC    F'22528',F'2048',F'6',F'19',13F'0'
         DC    F'24576',F'4096',F'3',F'19',13F'0'
         DC    F'26624',F'2048',F'6',F'19',13F'0'
         DC    F'28672',F'4096',F'3',F'19',13F'0'
         DC    F'30720',F'2048',F'6',F'19',13F'0'
         DC    F'32768',F'4096',F'3',F'19',13F'0'
         DC    F'-1'                              END OF TABLE INDICATR
DEV3330L EQU   *-DEV3330
         EJECT
DEV3350  DS    0F
         DC    F'512',F'512',F'27',F'30',13F'0'
         DC    F'1024',F'1024',F'15',F'30',13F'0'
         DC    F'1536',F'512',F'27',F'30',13F'0'
         DC    F'2048',F'2048',F'8',F'30',13F'0'
         DC    F'2560',F'512',F'27',F'30',13F'0'
         DC    F'3072',F'1024',F'15',F'30',13F'0'
         DC    F'3584',F'512',F'27',F'30',13F'0'
         DC    F'4096',F'4096',F'4',F'30',13F'0'
         DC    F'4608',F'512',F'27',F'30',13F'0'
         DC    F'5120',F'1024',F'15',F'30',13F'0'
         DC    F'5632',F'512',F'27',F'30',13F'0'
         DC    F'6144',F'2048',F'8',F'30',13F'0'
         DC    F'6656',F'512',F'27',F'30',13F'0'
         DC    F'7168',F'1024',F'15',F'30',13F'0'
         DC    F'8192',F'4096',F'4',F'30',13F'0'
         DC    F'10240',F'2048',F'8',F'30',13F'0'
         DC    F'12288',F'4096',F'4',F'30',13F'0'
         DC    F'14336',F'2048',F'8',F'30',13F'0'
         DC    F'16384',F'4096',F'4',F'30',13F'0'
         DC    F'18432',F'2048',F'8',F'30',13F'0'
         DC    F'20480',F'4096',F'4',F'30',13F'0'
         DC    F'22528',F'2048',F'8',F'30',13F'0'
         DC    F'24576',F'4096',F'4',F'30',13F'0'
         DC    F'26624',F'2048',F'8',F'30',13F'0'
         DC    F'28672',F'4096',F'4',F'30',13F'0'
         DC    F'30720',F'2048',F'8',F'30',13F'0'
         DC    F'32768',F'4096',F'4',F'30',13F'0'
         DC    F'-1'                              END OF TABLE INDICATR
DEV3350L EQU   *-DEV3350
         EJECT
*
*   P R O G R A M   C O N S T A N T S
*
TRAILER  DC    CL79' 1-BEST            2-BEST            3-BEST        *
                   4-BEST'
MASK     DC    XL8'4020202020202020'
*
*
PARSMSG  DC    YL2(PARSMSGE-*-2)
         DC    C'PARSE ERROR--RETURN CODE='
PARSRTNC DC    C'  '
PARSMSGE EQU   *
*
KLENMSG  DC    YL2(KLENMSGE-*-2)
         DC    C'KEYL GREATER THAN 255'
KLENMSGE EQU   *
*
KCMPMSG  DC    YL2(KCMPMSGE-*-2)
         DC    C'KCOMP IS NOT ''BAD'' ''GOOD'' OR ''FAIR'''
KCMPMSGE EQU   *
*
DEVMSG   DC    YL2(DEVMSGE-*-2)
         DC    C'DEV IS NOT ''3330'' OR ''3350'''
DEVMSGE  EQU   *
*
RECSMSG  DC    YL2(RECSMSGE-*-2)
         DC    C'MAXIMUM RECORD SIZE IS GREATER THAN ''32761'''
RECSMSGE EQU   *
*
CIPRMSG  DC    YL2(CIPRMSGE-*-2)
         DC    C'CI PERCENTAGE IS GREATER THAN ''100'''
CIPRMSGE EQU   *
         SPACE 3
         LTORG
         EJECT
*
*
*  R E G I S T E R    E Q U A T E S
*
*
*
CPPLREG  EQU   3
PPLREG   EQU   4
WORKREG  EQU   5
PDLREG   EQU   4
BASEREG1 EQU   11
BASEREG2 EQU   12
R1       EQU   1
PDE      EQU   5
DATALEN  EQU   4
DATASW   EQU   6
KEYED    EQU   X'80'
NOTKEYED EQU   X'00'
LINKREG  EQU   6
DEVREG   EQU   4
R15      EQU   15
EVENODD  EQU   8
DIVIDEND EQU   9
REMAINDR EQU   8
QUOTIENT EQU   9
DIVISOR  EQU   15
R8       EQU   8
R5       EQU   5
R4       EQU   4
WAREAREG EQU   10
R2       EQU   2
R3       EQU   3
R6       EQU   6
R7       EQU   7
R9       EQU   9
R13      EQU   13
R14      EQU   14
R0       EQU   0
         EJECT
PCL      IKJPARM
RSIZE    IKJKEYWD
         IKJNAME 'RSIZE',SUBFLD=RSIZESUB
RLOAD    IKJKEYWD
         IKJNAME 'RLOAD',SUBFLD=RLOADSUB
RADD     IKJKEYWD
         IKJNAME 'RADD',SUBFLD=RADDSUB
KLEN     IKJKEYWD
         IKJNAME 'KLEN',SUBFLD=KLENSUB
KCOMP    IKJKEYWD
         IKJNAME 'KCOMP',SUBFLD=KCOMPSUB
DEV      IKJKEYWD
         IKJNAME 'DEV',SUBFLD=DEVSUB
IMB      IKJKEYWD
         IKJNAME 'IMB'
REPL     IKJKEYWD
         IKJNAME 'REPL'
ALL      IKJKEYWD
         IKJNAME 'ALL'
RSIZESUB IKJSUBF
RECSIZEA IKJIDENT 'NUMBER',MAXLNTH=6,FIRST=NUMERIC,OTHER=NUMERIC,      *
               PROMPT='RECORDSIZE-AVG MAX'
RECSIZEM IKJIDENT 'NUMBER',MAXLNTH=6,FIRST=NUMERIC,OTHER=NUMERIC,      *
               PROMPT='RECORDSIZE-MAX'
RLOADSUB IKJSUBF
RECLOAD  IKJIDENT 'NUMBER',MAXLNTH=7,FIRST=NUMERIC,OTHER=NUMERIC,      *
               PROMPT='RECORDS TO BE LOADED'
RADDSUB  IKJSUBF
RECADD   IKJIDENT 'NUMBER',MAXLNTH=6,FIRST=NUMERIC,OTHER=NUMERIC
CIPER    IKJIDENT 'NUMBER',MAXLNTH=3,FIRST=NUMERIC,OTHER=NUMERIC
KLENSUB  IKJSUBF
KEYLENTH IKJIDENT 'NUMBER',MAXLNTH=3,FIRST=NUMERIC,OTHER=NUMERIC
KCOMPSUB IKJSUBF
KEYCOMP  IKJIDENT 'BAD/FAIR/GOOD',MAXLNTH=4,
DEVSUB   IKJSUBF
DEVTYPE  IKJIDENT '3330/3350',MAXLNTH=4,FIRST=NUMERIC,OTHER=NUMERIC
         IKJENDP
         EJECT
WORKAREA DSECT
CPPLADDR DS    F                   ADDRESS OF CPPL
PPLAREA  DS    7F                  PPL AREA USED TO COMMUNICATE W/PARSE
CMDECB   DS    F                   COMMAND ECB USED BY PARSE
PDLADDR  DS    F                   ANSWER AREA USED BY PARSE
         SPACE 3
* THE FOLLOWING VALUES ARE ENTERED AS INPUT TO THIS COMMAND
*
RECSZ    DC    F'0'                INPUT(PDL=RECSIZEA)
MAXRECSZ DC    F'0'                INPUT(PDL=RECSIZEM)
#RECLD   DC    F'0'                INPUT(PDL=RECLOAD)
#RECADD  DC    F'0'                INPUT(PDL=RECADD)
KEYLEN   DC    H'0'                INPUT(PDL=KEYLENGTH)
PERRADD  DC    F'0'                COMPUTE: #RECADD / #RECLD
CIRATIO  DC    F'0'
CARATIO  DC    F'0'
DEVICE   DC    CL4' '              INPUT(PDL=DEVTYPE)
IMBFCTR  DC    H'0'                INPUT(PDL=IMB) 0=IF NOT ENTERED
*                                                 1=IF ENTERED
KEYCMP   DC    CL4' '              INPUT(PDL=KEYCOMB)
DOUBLE   DS    D
REPLFCTR DC    H'0'
ALLFCTR   DC   H'0'                INPUT(PDL=ALL) 0=IF NOT ENTERED
*                                                 1=IF ENTERED
A3330TAB DS    F                   ADDRESS OF 3330 TABLE
A3350TAB DS    F                   ADDRESS OF 3350 TABLE
DATAFIT1 DS    3F                  BEST DATA CISZ FIT FROM TABLE+ADDR
DATAFIT2 DS    3F                  BEST DATA CISZ FIT FROM TABLE+ADDR
DATAFIT3 DS    3F                  BEST DATA CISZ FIT FROM TABLE+ADDR
DATAFIT4 DS    3F                  BEST DATA CISZ FIT FROM TABLE+ADDR
DEVREGSV DS    F                   SAVE AREA FOR BEGINNING OF DEVTABLE
BESTFIT1 DS    F               1ST BEST  #TRKDATA  FIT FROM TABLE
BESTFIT2 DS    F               2ND BEST  #TRKDATA  FIT FROM TABLE
BESTFIT3 DS    F               3RD BEST  #TRKDATA  FIT FROM TABLE
BESTFIT4 DS    F               4TH BEST  #TRKDATA  FIT FROM TABLE
         DS    3F                  FILLER FOR BEST FIT ROUTINE
WORKFIT  DS    4F
SAVE     DS    9D
PLINE    DS    CL79
COUNT    DS    PL2
         DS    D                   ALIGN FOR GETMAIN
WORKLEN  EQU   *-CPPLADDR
         EJECT
* THIS DSECT IS FOR A DEVICE TABLE ENTRY.......
*
*
DEVENTRY DSECT
DCISZ    DS    F    DATA CONTROL INTERVAL SIZE
PHYBLKSZ DS    F    PHYSICAL BLOCKSIZE
#BLKSTRK DS    F    NUMBER OF PHYSICAL BLOCKS PER TRACK
#TRKCA   DS    F    NUMBER OF TRACK PER CONTROL AREA (DEFAULT = 1 CYL)
#CICA    DS    F    NUMBER OF CONTROL INTERVALS PER CONTROL AREA
#RCINFSP DS    F    NUMBER OF RECORDS PER CONTROL INTERVAL-NO FREESPACE
#RCANFSP DS    F    NUMBER OF RECORDS PER CONTROL AREA -   NO FREESPACE
#FRCA    DS    F    NUMBER OF FREE RECORDS NEED PER CONTROL AREA
FRCAFSP  DS    F    NUMBER OF FREE RECORDS NEED/CA FREESPACE
FRCIFSP  DS    F    NUMBER OF FREE RECORDS NEED/CI FREESPACE
PERFSPCI DS    F    PERCENTAGE OF FREESPACE PER CONTROL INTERVAL
PERFSPCA DS    F    PERCENTAGE OF FREESPACE PER CONTROL AREA
#FCICA   DS    F    NUMBER OF FREE CONTROL INTERVALS PER CONTROL AREA
#RCILOAD DS    F    NUMBER OF RECORDS IN A CONTROL INTERVAL AT LOADTIME
#RCALOAD DS    F    NUMBER OF RECORDS IN A CONTROL AREA AT LOADTIME
#TRKDATA DS    F    NUMBER OF TRACKS FOR DATA COMPONENT WITH FREESPACE
#TRKNFSP DS    F    NUMBER OF TRACKS FOR DATA COMPONENT WITHOUT FREESPC
DEVENLEN EQU   *-DCISZ
         IKJCPPL DSECT
         EJECT
         IKJPPL DSECT
         EJECT
         END
