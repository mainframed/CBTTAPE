VPP      TITLE 'IEFVPP - AMDAHL MVS DYNAMIC PROCLIB SUPPORT'
***********************************************************************
*                                                                     *
*        IEFVPP                                                       *
*        ******                                                       *
*                                                                     *
*        VERSION 3, RELEASE 2, MODIFICATION 4                         *
*        ******* ** ******* ** ************ *                         *
*                                                                     *
* THIS CSECT CONTAINS THE DYNAMIC PROCEDURE LIBRARY SUPPORT ROUTINES. *
*                                                                     *
* THIS PROGRAM IS A REWRITE OF THE USER PROCLIB SUPPORT PROGRAM       *
* UPROCRDR, WHICH HAD BEEN IN USE AT THE MUTUAL LIFE OF CANADA ON     *
* OS/MVT SINCE MARCH OF 1972.                                         *
*                                                                     *
* THIS CODE HAS BEEN SUBSTANTIALLY MODIFIED BY THE AMDAHL CORPORATE   *
* COMPUTER CENTER TO SIMPLIFY THE MODIFICATIONS NECESSARY TO          *
* IBM CODE IN ORDER TO IMPLEMENT AND INSTALL THIS CODE.               *
* ADDITIONAL FEATURES ADDED ARE CLEANUP RECOVERY, MULTIPLE            *
* CONVERTER SUPPORT, MVS/SE RELEASE 2 COMPATIBILITY, MVS/SP           *
* VERSION 1, RELEASE 3.2 SUPPORT, AND MVS/SP 2.2.0 COMPATIBILITY.     *
*                                                                     *
*                                                                     *
***********************************************************************
         TITLE '**** DYNAMIC PROCLIB LINKAGE REQUIREMENTS ****'
***********************************************************************
*                                                                     *
* ENTRIES:                                                            *
*        IEFVPP0 - (FROM IEFVH1)  - INITIALIZATION PROCESSING         *
*        IEFVPP1 - (FROM IEFVFA)  - //JOBPROC VALIDATE & ALLOCATE AND *
*                                   JOBPROC CONCATENATE & OPEN        *
*        IEFVHF  - (FROM MANY)    - BRANCHES TO IEFVPP3               *
*        IEFVPP3 - (FROM IEFVHF)  - CLEANUP AT JOB PROCESSING END     *
*                                                                     *
* ATTRIBUTES:                                                         *
*        REENTRANT, REFRESHABLE.                                      *
*        RMODE 24, AMODE 24 (CONVERTER REQUIREMENT).                  *
*                                                                     *
* EXTERNALS:                                                          *
*        NONE.                                                        *
*                                                                     *
* EXITS:                                                              *
*        IEFVPP0 BRANCHES TO IEFVHA AND DOES NOT RETURN TO CALLER.    *
*        IEFVPP1 RETURNS TO CALLER AFTER CALLING CONVERTER POST       *
*                SCAN EXIT (IF ANY) AND IEFUJV INTERNAL TEXT TEXT     *
*                (IF OPTION ASSEMBLED).                               *
*        IEFVPP3 BRANCHES TO IEFVHFX AND DOES NOT RETURN TO CALLER.   *
*                                                                     *
*        SEE COMMENTS AT EACH INDIVIDUAL ENTRY POINT FOR MORE         *
*        INFORMATION.                                                 *
*                                                                     *
***********************************************************************
         TITLE '**** DYNAMIC PROCLIB INSTALLATION NOTES ****'
***********************************************************************
*                                                                     *
*     1) ALL ENTRIES (EXCEPT IEFVPP0) TEST THE "PPW0INIT"             *
*           SWITCH IN THE DYNAMIC PROCLIB WORK AREA BEFORE            *
*           PERFORMING ANY PROCESSING.                                *
*                                                                     *
*           THE USER PROCLIB SUPPORT CODE CAN THEREFORE BE DISABLED   *
*           BY SIMPLY ALTERING IEFVPP0 NOT TO TURN ON "PPW0INIT".     *
*           IEFVPP0 MUST ALWAYS BE ENTERED AS IT BUILDS THE DYNAMIC   *
*           PROCLIB WORK AREA AND ANCHORS IT IN THE CONVERTER         *
*           WORK AREA (CWA) IN A RESERVED FIELD.                      *
*                                                                     *
*     2) ALL ROUTINES EXPECT R12 TO PERMANENTLY ADDRESS THE CWA.      *
*                                                                     *
*     3) A RESERVED FULL WORD MUST BE AVAILABLE IN THE CWA TO         *
*           ANCHOR THE ADDRESS OF THE DYNAMIC PROCLIB WORK AREA.      *
*           THE NAME OF THIS FIELD IS DEFINED BY THE &ANCHOR SET      *
*           SYMBOL IN THIS PROGRAM.  THE LOCATION OF THIS FIELD       *
*           IS NOT NEEDED TO BE KNOWN OUTSIDE OF THIS MODULE.         *
*                                                                     *
*     4) ANY ERROR NOTED DURING CLEANUP OPERATIONS WILL CAUSE THE     *
*           CONVERTER TO ABORT.                                       *
*                                                                     *
*     5) SEE THE SECTION OF COMMENTS ON CONVERTER MODIFICATION FOR    *
*           PARTICULARS ON THE CHANGES TO THE CONVERTER/INTERPRETER.  *
*                                                                     *
*     6) TO PROPERLY ASSEMBLE THIS MODULE, THE CONVERTER/INTERPRETER  *
*           WORK AREA MACRO, IEFCVRWA, MUST BE AVAILABLE.  IBM        *
*           DISTRIBUTES THIS MACRO IN APVTMACS.                       *
*                                                                     *
*     7) THE MESSAGE NUMBER PREFIX IS SET BY THE SET SYMBOL           *
*           &MSGPFX.  ITS DEFINITION FOLLOWS THE LOCAL MACRO          *
*           DEFINITIONS.                                              *
*                                                                     *
*     8) THE INTERNAL TEXT ENTRY INTO IEFUJV IS ENABLED OR DISABLED   *
*           BY THE USE OF THE &INTEXTX SET SYMBOL IN THIS PROGRAM.    *
*                                                                     *
*     9) THE DEFAULT OF SYSPROC=YES CAN BE CHANGED BY ALTERING THE    *
*           VALUE OF THE &SYSPROC SET SYMBOL IN THIS PROGRAM.         *
*                                                                     *
*    10) THE DEFAULT DDNAME OF "JOBPROC" CAN BE ALTERED BY CHANGING   *
*           THE VALUE OF THE &DDNAME SET SYMBOL IN THIS PROGRAM.      *
*           NOTE, HOWEVER, THAT WHATEVER DDNAME IS CHOSEN BECOMES     *
*           RESERVED AND CANNOT BE USED ANYWHERE ELSE IN JCL.         *
*                                                                     *
*    11) DISABLING OF THIS CODE CAN BE DONE BY ZAPPING THE            *
*           INSTRUCTION AT "PP0CANCL".  THE TRACING CODE CAN BE       *
*           ENABLED BY ZAPPING THE INSTRUCTION AT "PP0SETTR" OR       *
*           BY SPECIFYING SYSPARM(TRACE) DURING ASSEMBLY OF THIS      *
*           MODULE.                                                   *
*                                                                     *
*    12) THIS MODULE MUST BE ASSEMBLED WITH THE "SYS1.MACLIB"         *
*           AND "SYS1.AMODGEN" FROM AN MVS/SP VERSION 1 (OR LATER)    *
*           SYSTEM.  ASSEMBLER H, VERSION 2, IS REQUIRED.             *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
* COPYRIGHT:                                                          *
*        AMDAHL CORPORATION, 1978, 1981, 1982, 1988.                  *
*                                                                     *
*        THIS COPYRIGHT IS NOT INTENDED TO LIMIT THE USE OF THIS      *
*        CODE BY THE RECEIVING INSTALLATION, BUT TO PREVENT ITS       *
*        REDISTRIBUTION FOR PROFIT.                                   *
*                                                                     *
*        THERE IS AT LEAST ONE KNOWN DYNAMIC PROCLIB PRODUCT THAT     *
*        HAS TAKEN THIS CODE AND HAS MADE IT A SUBSTANTIAL PART OF    *
*        THEIR PRODUCT.  NAUGHTY, NAUGHTY.                            *
*                                                                     *
***********************************************************************
         TITLE '**** DYNAMIC PROCLIB SUMMARY OF AMENDMENTS ****'
***********************************************************************
*                                                                     *
*        VERSION 3, RELEASE 2, MODIFICATION 4                         *
*                                                                     *
*        THE INTERNAL TEXT KEY FOR THE SYSPROC= KEYWORD PARAMETER     *
*        HAS BEEN CHANGED FROM X'01' TO THE SAME VALUE AS THE HOLD=   *
*        KEYWORD PARAMETER (X'23').  THIS PREVENTS AN 0C1 ABEND IN    *
*        THE INTERPRETER IF THE SYSPROC= PARAMETER IS CODED ON ANY    *
*        DD STATEMENT OTHER THAN THE FIRST JOBPROC DD STATEMENT.      *
*        EQUALLY, THIS MEANS THAT HOLD=YES OR HOLD=NO COULD BE USED   *
*        ON THE JOBPROC DD STATEMENT TO SPECIFY INCLUSION OR          *
*        EXCLUSION OF THE SYSTEM PROCLIBS.                            *
*                                                                     *
*        NOTE THAT IN ORDER FOR THIS CHANGE TO BE EFFECTIVE AND FOR   *
*        SYSPROC= TO CONTINUE WORKING, THE SUPERZAP TO THE CON-       *
*        VERTER KEYWORD TABLE IN IEFVFA MUST BE UPDATED AT THE SAME   *
*        TIME AS THIS LEVEL OF IEFVPP IS INSTALLED.                   *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        VERSION 3, RELEASE 2, MODIFICATION 3                         *
*                                                                     *
*        THE INTERNAL ROUTINE THAT DUPLICATED THE PROCESSING OF       *
*        IEFVGM HAS BEEN ELIMINATED, AS IEFVGM PROVIDES AN INTERFACE  *
*        THAT ALLOWS IT TO BE USED WITHOUT HAVING TO ALTER ANY        *
*        CONVERTER MESSAGE TABLES.  THIS PROVIDES AN ADDITIONAL       *
*        LEVEL OF CONVERTER MAINTENANCE INDEPENDENCE.                 *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        VERSION 3, RELEASE 2, MODIFICATION 2                         *
*                                                                     *
*        A SET SYMBOL (&SWA220) HAS BEEN ADDED TO RESOLVE THE PROBLEM *
*        OF RE-ASSEMBLY WITH A NON MVS/SP 2.2.0 SYSTEM.               *
*                                                                     *
*        A SET OF MODIFICATIONS FOR MVS/SP 3.1.0 (MVS/ESA(TM)) IS     *
*        SUPPLIED.  THE ONLY CHANGES ARE THE SUPERZAPS AND THE        *
*        SYSGEN MACRO UPDATE.                                         *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        VERSION 3, RELEASE 2, MODIFICATION 1                         *
*                                                                     *
*        SET SYMBOLS HAVE BEEN ADDED TO ALLOW ADDITIONAL INSTALLATION *
*        CUSTOMIZATION WITHOUT ALTERING THE SOURCE.                   *
*                                                                     *
*        THE &SYSPROC SETB SYMBOL ESTABLISHES THE DEFAULT FOR THE     *
*        SYSPROC=YES|NO JCL PARAMETER.  THE SUPPLIED DEFAULT IS 1,    *
*        WHICH MEANS SYSPROC=YES.                                     *
*                                                                     *
*        THE &DDNAME SETC SYMBOL SPECIFIES THE DDNAME TO BE RESERVED  *
*        TO IDENTIFY THE DYNAMIC PROCLIB DD STATEMENT.  THE SUPPLIED  *
*        DEFAULT IS "JOBPROC".  ALL MESSAGES ALSO REFLECT THIS        *
*        SPECIFIED DDNAME.                                            *
*                                                                     *
*        WHEN A JOBPROC DD STATEMENT WAS SPECIFIED AFTER THE FIRST    *
*        EXEC STATEMENT AND NO JOBPROC STATEMENT HAD BEEN SPECIFIED   *
*        BEFORE THE FIRST EXEC STATEMENT, NO JCL ERROR WAS DETECTED   *
*        AND THE JOBPROC STATEMENT WAS ESSENTIALLY IGNORED.  THIS     *
*        HAS BEEN CORRECTED AND A "MISPLACED JOBPROC STATEMENT"       *
*        MESSAGE IS ISSUED AND A JCL ERROR IS INDICATED.              *
*                                                                     *
*        THE ERROR RECOVERY CODE THAT CHECKS FOR AN S213-04 ABEND     *
*        DURING OPEN HAS BEEN DELETED.  IT IS NO LONGER NEEDED, AS    *
*        THE "RETURN DSORG" FUNCTION OF DYNAMIC ALLOCATION (ADDED     *
*        TO THIS CODE IN 3.2.0) INSURES THAT THE DSCB DOES IN FACT    *
*        EXIST.  IF THE USER CAN DELETE THE DATA SET BETWEEN THE      *
*        DYNAMIC ALLOCATION AND THE OPEN, MORE POWER TO HIM (OR HER)  *
*        AND THE JOB WILL FAIL.                                       *
*                                                                     *
*        DDNAMES FOR ALLOCATED PROCLIBS ARE NOW SPECIFICALLY          *
*        ASSIGNED TO PREVENT GIGANTIC TYPE 30 SMF RECORDS.  SINCE     *
*        MULTIPLE CONVERTERS CAN BE RUNNING SIMULTANEOUSLY, THE       *
*        DDNAMES ARE COMPOSED TO THE LETTER "P", THE SIX HEXA-        *
*        DECIMAL DIGIT TCB ADDRESS, AND A SUFFIX OF 0-9, A-F.         *
*        ALSO, SINCE THE DDNAMES ARE "FIXED" FOR A GIVEN CONVERTER    *
*        SUBTASK, IT IS POSSIBLE TO CLEAN THEM UP (I.E., DEALLOCATE   *
*        THEM) ON A SUBSEQUENT CONVERTER CALL SHOULD A PREVIOUS       *
*        CONVERTER INVOCATION FAIL TO DEALLOCATE THEM.                *
*                                                                     *
*        IN SCANNING THE TIOT FOR THE SYSTEM PROCLIBS ASSOCIATED      *
*        WITH THE CURRENT CONVERTER INVOCATION, SWAREQ IS NOW USED    *
*        TO LOCATE THE JFCB WHEN SWA IS LOCATED ABOVE THE 16 MEG      *
*        LINE.                                                        *
*                                                                     *
*        RATHER THAN ISSUING AND CANCELLING AND ESTAE AT EACH AND     *
*        EVERY ENTRY TO THE DYNAMIC PROCLIB CODE (ESSENTAILLY ON      *
*        EVERY SINGLE JCL STATEMENT), A SINGLE ESTAE IS ISSUED IN     *
*        IEFVPP0.  IEFVPP3, DURING CLEANUP, WILL CANCEL THIS ESTAE.   *
*        BECAUSE THIS ESTAE IS "LOWER" THAT THE CONVERTER'S ESTAE,    *
*        IT WILL RECEIVE CONTROL ON ANY CONVERTER ABEND.  IF THE      *
*        ABEND OCCURS IN THE DYNAMIC PROCLIB CODE, THE SAME RECOVERY  *
*        THAT WAS PREVIOUSLY TAKEN IS PERFORMED.  IF THE ABEND        *
*        OCCURS ELSEWHERE IN THE CONVERTER, NO RECOVERY IS ATTEMPTED  *
*        HERE AND THE CONVERTER'S ESTAE EXIT WILL BE PERCOLATED TO.   *
*        ALSO, IF AN ERROR OCCURS IN THE DYNAMIC PROCLIB INITIALI-    *
*        ZATION ROUTINE (IEFVPP0), NO JCL ERROR WILL BE INDICATED.    *
*        USE OF A JOBPROC DD STATEMENT WILL, OF COURSE, RESULT IN     *
*        A JCL ERROR, BUT NON JOBPROC JOBS COULD STILL BE RUN,        *
*        ALLOWING FOR POSSIBLE REPAIR OF THE PROBLEM.                 *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        VERSION 3, RELEASE 2, MODIFICATION 0                         *
*                                                                     *
*        SUPPORT FOR SYSTEMS BEFORE MVS/SP VERSION 1 HAS BEEN         *
*        DROPPED.  THE CODE MAY IN FACT STILL WORK, BUT THERE ARE     *
*        NO GUARANTEES.                                               *
*                                                                     *
*        THE SPECIAL DDS FOR EACH CONVERTER (IEF0PDSI, IEF1PDSI,      *
*        IEF2PDSI, ETC.) ARE NO LONGER NEEDED.  THE "SYSPROC"         *
*        DATA SET NAMES ARE DETERMINED FROM THE CURRENTLY OPEN        *
*        CONVERTER PROCLIB DCB FOR WHATEVER "PROCNN" (IN JES2 TERMS)  *
*        DD IS BEING USED.                                            *
*                                                                     *
*        ALL DDNAMES USED TO ALLOCATED DYNAMIC PROCLIBS ARE SYSTEM    *
*        GENERATED NAMES (SYS*****).                                  *
*                                                                     *
*        THE USE OF THE RESERVED BIT IN THE STRINDCS FIELD OF THE     *
*        INTERNAL TEXT HEADER HAS BEEN ELIMINATED.                    *
*                                                                     *
*        THE SUPERZAP MODIFICATION TO IEFVHE IS NOT LONGER NEEDED     *
*        AND CAN BE REMOVED.  A NEW SUPERZAP TO INTERPRETER MODULE    *
*        IEFVDA IS REQUIRED.                                          *
*                                                                     *
*        THE SUPERZAP TO IEFVHF IS NO LONGER REQUIRED AND MUST BE     *
*        REMOVED IF IT IS CURRENTLY INSTALLED.  AN ADDITIONAL CHANGE  *
*        STATEMENT MUST BE ADDED TO THE CONVERTER SYSGEN JCL (AND     *
*        SMP JCLIN).                                                  *
*                                                                     *
*        THE SUPERZAP TO IEFVFA TO CALL THIS MODULE HAS BEEN ELIMIN-  *
*        ATED AND MUST BE REMOVED IF PREVIOUSLY INSTALLED.  THE       *
*        SUPERZAP TO DEFINE THE INTERNAL TEXT FOR SYSPROC IS NOW      *
*        OPTIONAL AND IS NEEDED ONLY IF THE SYSPROC KEYWORD IS TO     *
*        BE SUPPORTED.                                                *
*                                                                     *
***********************************************************************
         TITLE '**** DYNAMIC PROCLIB USER EXTERNALS ****'
***********************************************************************
*                                                                     *
*        THE USER WISHING TO SUPPLY A DYNAMIC PROCLIB NEED ONLY       *
*        CODE ONE OR MORE DD STATEMENTS (FOLLOWING THE NORMAL         *
*        RULES OF CONCATENATION) WITH THE DD NAME OF "JOBPROC".       *
*        THE JOBPROC DD MUST APPEAR BEFORE THE FIRST EXEC STATE-      *
*        MENT OF A JOB.  THE ONLY JCL KEYWORD PARAMETERS SUPPORTED    *
*        ARE DSN, DISP, UNIT, VOL=SER, VOL=REF, AND SYSPROC.  USE     *
*        OF OTHER JCL PARAMETERS MAY CAUSE ERRORS OR MAY BE IGNORED.  *
*        DSN IS MANDATORY.  DISP CAN ONLY BE "SHR".  UNIT AND VOL     *
*        ARE NECESSARY ONLY IF THE DATA SET IS NOT CATALOGUED (VIA    *
*        MASTER AND ALIASES - JOBCAT IS NOT USED).  SYSPROC INDICATES *
*        THE CONCATENATION (SYSPROC=YES) OR NON-CONCATENATION         *
*        (SYSPROC=NO) OF THE SYSTEM PROCLIBS WITH THE USER SUPPLIED   *
*        PROCLIBS.  THE DEFAULT IS SYSPROC=YES.                       *
*                                                                     *
*        THE SYSPROC KEYWORD IS AN OPTIONAL FEATURE OF THIS PROGRAM   *
*        AND IS ENABLED VIA A MODIFICATION TO IEFVFA.  IF THE         *
*        SYSPROC MODIFICATION HAS NOT BEEN MADE, SYSPROC=YES WILL     *
*        ALWAYS BE ASSUMED.                                           *
*                                                                     *
***********************************************************************
         TITLE '**** CONVERTER/INTERPRETER MODIFICATIONS ****'
***********************************************************************
*                                                                     *
*        CONVERTER/INTERPRETER MODIFICATIONS                          *
*        ********************* *************                          *
*                                                                     *
*        THERE ARE THREE AREAS OF CHANGES TO THE CONVERTER/           *
*        INTERPRETER NECESSARY TO PROPERLY IMPLEMENT DYNAMIC PROCLIB  *
*        SUPPORT IN MVS.  THE THREE AREAS ARE: SYSGEN MACROS (WHICH   *
*        INCLUDES LINK EDIT CHANGES TO THE CONVERTER); A NEW MODULE   *
*        (IEFVPP); AND SUPERZAPS TO CONVERTER/INTERPRETER MODULES.    *
*        IN ADDITION, IF THESE MODIFICATIONS ARE INSTALLED ONTO       *
*        AN EXISTING SYSTEM, A UCLIN AND JCLIN WILL BE NECESSARY      *
*        TO SMP AND A SPECIAL, ONE TIME, MANUAL RE-LINKEDIT OF THE    *
*        CONVERTER (IEFVH1) WILL BE REQUIRED TO PROPERLY ALTER THE    *
*        APPROPRIATE EXTERNAL REFERENCES.                             *
*                                                                     *
*        THE CHANGES TO SYSGEN ARE ALL CONTAINED IN MACRO SGIEF441    *
*        FOR MVS/370 AND MACRO SGIEF409 FOR MVS/XA.                   *
*        THESE MACROS CONTAIN THE LINK EDIT CONTROL STATEMENTS FOR    *
*        THE CONVERTER.  THE CHANGES ARE TWO CHANGE STATEMENTS TO     *
*        ALTER THE EXTERNAL REFERENCE TO IEFVHA IN IEFVH1 TO REFER    *
*        IEFVPP0 IN IEFVPP AND TO CHANGES THE CSECT NAME OF IEFVHF    *
*        TO IEFVHFX, AND AN INCLUDE OF THE NEW MODULE IEFVPP.         *
*                                                                     *
*        THE CHANGE STATEMENT:                                        *
*        PUNCH ' CHANGE IEFVHA(IEFVPP0) '                             *
*        IS INSERTED IMMEDIATELY BEFORE THE PUNCH INCLUDE OF IEFVH1.  *
*                                                                     *
*        THE CHANGE STATEMENT:                                        *
*        PUNCH ' CHANGE IEFVHF(IEFVHFX) '                             *
*        IS INSERTED IMMEDIATELY BEFORE THE PUNCH INCLUDE OF IEFVHF.  *
*                                                                     *
*        THE INCLUDE STATEMENT:                                       *
*        PUNCH ' INCLUDE AOSB3(IEFVPP) '                              *
*        IS INSERTED IMMEDIATELY BEFORE THE PUNCH ENTRY IEFVH1.       *
*                                                                     *
*        THE NEW MODULE, IEFVPP IS LINK EDITTED AS PART OF IEFVH1,    *
*        THE JCL CONVERTER.  THE LINK EDIT CHANGE STATEMENTS CAUSE    *
*        IT TO BE INVOKED TO INITIALIZE THE EXPANDED CONVERTER        *
*        WORK AREA AND TO RECEIVE CONTROL INSTEAD OF IEFVHF.  IT      *
*        ALSO RECEIVES CONTROL BY ALTERING THE CONVERTER POST SCAN    *
*        EXIT ADDRESS LIST.                                           *
*                                                                     *
*        THE SUPERZAP IS TO MODULE IEFVDA IN LOAD MODULE IEFNB903.    *
*        THE ZAP TO IEFVDA (IN THE INTERPRETER) MUST BE INSTALLED     *
*        TO PREVENT A JOBPROC STATEMENT FROM CAUSING A MISPLACED      *
*        DD STATEMENT JCL ERROR IN THE INTERPRETER.                   *
*                                                                     *
*        THE ZAP FOR IEFVDA IS TO HAVE THE INTERPRETER BYPASS         *
*        INTERNAL TEXT DD STATEMENTS THAT PRECEED THE FIRST EXEC      *
*        STATEMENT THAT ARE DD DYNAM.  THIS IS SET BY IEFVPP ON       *
*        ALL JOBPROC DD STATEMENTS.  THE INTERPRETER ALREADY HAS      *
*        LOGIC TO NOT BUILD SWA BLOCKS FOR DD DYNAM STATEMENTS.       *
*        THIS CHANGE FROM THE PREVIOUS BYPASS LOGIC IS TO ACCOM-      *
*        ODATE MVS/SP 2.2.0, WHICH SETS A LAST STATEMENT FLAG IN      *
*        THE LAST DD STATEMENT OF A STEP (INCLUDING STEP "0") AND     *
*        WILL ABEND (0B0) IN THE INTERPRETER IF THE JOBPROC STATE-    *
*        MENT IS THE LAST STATEMENT BEFORE THE EXEC STATEMENT AND     *
*        IS PRECEEDED BY A JOBLIB OR JOBCAT STATEMENT.                *
*                                                                     *
*        THE ZAP TO IEFVFA IS TO PROVIDE THE SYSPROC PARAMETER BY     *
*        ALTERING A SUITABLY UNUSED JCL KEYWORD (HOW'S "SUBALLOC"     *
*        GRAB YOU?) TO BECOME THE "SYSPROC" KEYWORD.                  *
*                                                                     *
*        SINCE THE ACTUAL TEXT OF THESE ZAPS WILL DEPEND ON THE       *
*        CURRENT MAINTENANCE LEVELS OF THE VARIOUS MODULES, THEY ARE  *
*        NOT ENUMERATED HERE.  THE PTF TO APPLY THESE ZAPS SHOULD     *
*        BE EXAMINED, AS WELL AS THE SOURCE MICROFICHE OR OPTIONAL    *
*        MATERIALS.                                                   *
*                                                                     *
***********************************************************************
         TITLE '**** STATEMENT OF SUPPORT ****'
***********************************************************************
*                                                                     *
*        STATEMENT OF SUPPORT                                         *
*        ********* ** *******                                         *
*                                                                     *
*        THIS PROGRAM IS RUN IN THE CORPORATE COMPUTER CENTER         *
*        OF THE AMDAHL CORPORATION AND IT'S USERS ARE TOTALLY         *
*        DEPENDANT UPON IT.  HOWEVER, THAT DOES NOT IMPLY THAT        *
*        THE AMDAHL CORPORATION FORMALLY SUPPORTS THIS CODE           *
*        AND MAINTAINS RESPONSIBILITY FOR IT.  AS WITH ALL            *
*        MODIFICATIONS, THE USING INSTALLATION MUST DECIDE            *
*        WHETER OR NOT TO INSTALL IT BASED ON THEIR ABILITY TO        *
*        MAINTAIN IT IN THE FUTURE.  NOTE THAT SUPPORT IS PRI-        *
*        MARILY A MATTER OF TIMING (I.E., WE MAY NOT BE RUNNING A     *
*        NEW PTF OR RELEASE LEVEL AS SONN AS OTHER INSTALLATIONS).    *
*                                                                     *
*        SPECIAL NOTE: THE AMDAHL CORPORATE COMPUTER CENTER IS NOW    *
*        RUNNING ONLY MVS/XA.  WHILE IT HAS BEEN ATTEMPTED TO MAIN-   *
*        TAIN COMPATIBILITY WITH MVS/370, THIS HAS NOT BEEN TESTED    *
*        AND SUCH INSTALLATIONS ARE ADVISED TO TEST THIS CODE VERY    *
*        CAREFULLY.                                                   *
*                                                                     *
*        ADDITIONAL INFORMATION CONCERNING THESE MODIFICATIONS        *
*        CAN BE OBTAINED BY CONTACTING:                               *
*                                                                     *
*        KEITH E. MOE                                                 *
*        AMDAHL CORPORATION                                           *
*        CORPORATE COMPUTER CENTER                                    *
*        MAIL STOP 201                                                *
*        POST OFFICE BOX 3470                                         *
*        1250 EAST ARQUES AVENUE                                      *
*        SUNNYVALE, CALIFORNIA  94088-3470                            *
*        (408) 746-6386                                               *
*                                                                     *
***********************************************************************
         TITLE '**** DYNAMIC MACRO INSTRUCTIONS ****'
         MACRO
         IEFVPPMS &MNO,&TEXT
.*
         LCLA  &LTH
         LCLC  &STR
         GBLC  &MSGPFX
.*
         AIF   (T'&TEXT EQ 'O').VGMER01
         AIF   (T'&MNO EQ 'O').VGMER02
.*
&LTH     SETA  K'&TEXT-2           GET LENGTH OF MESSAGE TEXT
&STR     SETC  '&TEXT'(2,&LTH)     EXTRACT MESSAGE TEXT
&LTH     SETA  &LTH+8              CALCULATE TOTAL MESSAGE LENGTH
.*
PPMS&MNO DC    AL1(0)              RESERVED
         DC    AL1(&LTH)           LENGTH OF MESSAGE TEXT
         DC    CL8'&MSGPFX&MNO'    EXTERNAL MESSAGE IDENTIFICATION
         DC    C'&STR'
         SPACE 2
         MEXIT ,
.*
.VGMER01 MNOTE 12,'*** MESSAGE TEXT REQUIRED BUT NOT SPECIFIED ***'
         MEXIT ,
.*
.VGMER02 MNOTE 12,'*** MESSAGE NUMBER REQUIRED BUT NOT SPECIFIED ***'
         MEND
*
         MACRO
&NAME    PPTRACE &WHERE
&NAME    BAL   R14,PPTRACER        GO TO DEBUGGING TRACER
         DC    CL4'&WHERE'         INDICATE WHERE WE ARE
         MEND
*
         TITLE '**** SYMBOLIC PARAMETER DEFINITIONS ****'
***********************************************************************
*                                                                     *
*        THE SYMBOLIC PARAMETERS THAT CAN BE USED TO ALTER THE        *
*        CHARACTERISTICS OF THIS MODULE ARE DEFINED HERE.             *
*                                                                     *
***********************************************************************
*
         GBLA  &MAXPROC            MAXIMUM NUMBER OF PROCLIBS
*
         GBLB  &INTEXTX            IEFUJV INTERNAL TEST EXIT
*
         GBLB  &SWA220             SWA ABOVE THE LINE SUPPORT FOR XA
*
         GBLB  &SYSPROC            SYSPROC= YES|NO DEFAULT
*
         GBLC  &ANCHOR             CONVERTER WORK AREA ARCHOR WORD
*
         GBLC  &DDNAME             JOBPROC DD NAME
*
         GBLC  &MSGPFX             MESSAGE ID PREFIX
*
***********************************************************************
*                                                                     *
*        MAXIMUM NUMBER OF PROCLIBS                                   *
*                                                                     *
*        THIS PARAMETER REPRESENTS THE MAXIMUM NUMBER OF PROCLIBS     *
*        (USER PLUS SYSTEM) THAT CAN BE ALLOCATED FOR USE IN DYNAMIC  *
*        PROCLIB PROCESSING.                                          *
*                                                                     *
***********************************************************************
*
&MAXPROC SETA  16                  A FULL 16 PROCLIBS
*
***********************************************************************
*                                                                     *
*        INTERNAL TEXT EXIT                                           *
*                                                                     *
*        THIS PARAMETER SHOULD BE SET TO EITHER 1 (ONE) OR 0 (ZERO)   *
*        TO GENERATE OR NOT GENERATE SUPPORT FOR THE INTERNAL         *
*        TEXT EXIT TO IEFUJV.                                         *
*                                                                     *
***********************************************************************
*
&INTEXTX SETB  0                   WE DON'T WANT THE EXIT
*
***********************************************************************
*                                                                     *
*        SWA ABOVE THE LINE                                           *
*                                                                     *
*        THIS PARAMETER SHOULD BE SET TO EITHER 1 (ONE) OR 0 (ZERO)   *
*        TO GENERATE OR NOT GENERATE SUPPORT FOR SWA ABOVE THE LINE   *
*        IN MVS/SP 2.2.0 OR LATER.  WHEN SET TO 1 (ONE), THE MVS/SP   *
*        2.2.0 MACLIBS (WITH PTF UY16002) ARE REQUIRED.  THE GENER-   *
*        ATED CODE IS FULLY COMPATIBLE WITH ALL RELEASES.  WHEN SET   *
*        TO 0 (ZERO), THE GENERATED CODE WILL STILL EXECUTE PROPERLY  *
*        ON AN MVS/SP 2.2.0 SYSTEM, BUT THE CONVERTER'S SWA (I.E.,    *
*        JES2'S) MUST BE BELOW THE LINE.                              *
*                                                                     *
***********************************************************************
*
&SWA220  SETB  1                   WE WANT THE SWA ABOVE THE LINE CODE
*
***********************************************************************
*                                                                     *
*        SYSPROC = YES|NO DEFAULT                                     *
*                                                                     *
*        THIS PARAMETER SHOULD BE SET TO 1 (ONE) TO MAKE SYSPROC=YES  *
*        THE DEFAULT OR 0 (ZERO) TO MAKE SYSPROC=NO THE DEFAULT.      *
*                                                                     *
***********************************************************************
*
&SYSPROC SETB  1                   SET SYSPROC=YES
*
***********************************************************************
*                                                                     *
*        DYNAMIC PROCLIB WORK AREA ANCHOR                             *
*                                                                     *
*        THIS PARAMETER SHOULD BE SET TO THE NAME OF THE FIELD        *
*        IN THE COMMON OR CONVERTER WORK AREA THAT WILL BE USED       *
*        TO CONTAIN THE ADDRESS OF THE DYNAMIC PROCLIB WORK AREA.     *
*        THIS FIELD MUST BE A FULL WORD THAT IS UNUSED BY THE         *
*        CONVERTER.  THE SUPERZAPS TO THE CONVERTER NOT LONGER        *
*        NEED TO REFER TO THIS FIELD.                                 *
*                                                                     *
***********************************************************************
*
&ANCHOR  SETC  'RFULLE'            AT THE END OF THE CONVERTER WORKAREA
*
***********************************************************************
*                                                                     *
*        JOBPROC DD NAME                                              *
*                                                                     *
*        THIS PARAMETER SHOULD BE SET TO THE DDNAME THAT IS DESIRED   *
*        TO BE USED FOR THE DYNAMIC PROCLIB DD STATEMENT.  THIS DD    *
*        NAME CANNOT BE USED FOR ANY OTHER PURPOSE IN JCL AND WILL    *
*        CAUSE A JCL ERROR (MISPLACED DD STATEMENT) IF IT IS.         *
*                                                                     *
***********************************************************************
*
&DDNAME  SETC  'JOBPROC'           SET DDNAME=JOBPROC
*
***********************************************************************
*                                                                     *
*        MESSAGE PREFIX                                               *
*                                                                     *
*        THIS PARAMETER SHOULD BE SET TO THE THREE (3) CHARACTER      *
*        WHICH IS DESIRED ON ALL MESSAGES ISSUED BY THE MODULE.       *
*        THE MESSAGE NUMBERS WILL BE XXX800 THROUGH XXX899, WHERE     *
*        XXX IS THE SELECTED PREFIX.                                  *
*                                                                     *
***********************************************************************
*
&MSGPFX  SETC  'CCC'               CORPORATE COMPUTER CENTER
*                                  -         -        -
         TITLE '**** CONTROL BLOCK MAPPINGS ****'
IEFVPP   CSECT ,                   CONTROL SECTION NAME
IEFVPP   AMODE 24
IEFVPP   RMODE 24
*
         SPLEVEL SET=1             MVS/370 AND MVS/XA COMPATIBILITY
*
         PUSH  PRINT
         PRINT NOGEN
*
         CVT   DSECT=YES
*
NEL      DSECT
         IEFNEL SUBCOM=C
*
         IEFJMR ,
*
         IEESMCA ,
*
         IFGRPL ,
*
         IEFZB4D0  ,
*
         IEFZB4D2  ,
*
         IKJEFFDF  DFDSECT=YES
*
         DCBD  DSORG=PO,DEVD=DA
*
         IHASDWA DSECT=YES
*
         IHAPSA
*
         IHAASCB
*
         IKJTCB
*
TIODSECT DSECT
         IEFTIOT1
*
JFCBDSCT DSECT
         IEFJFCBN
*
UCBDSECT DSECT
         IEFUCBOB
*
         IEFJESCT
*
         AIF   (NOT &SWA220).NOSWA0
         IEFZB505 LOCEPAX=YES
.NOSWA0  ANOP
*
         IEFTXTFT ,
*
         IEFVKEYS ,
*
***********************************************************************
*                                                                     *
*        THE FOLLOWING SYMBOL IS USED AS THE INTERNAL TEXT KEY VALUE  *
*        FOR THE "SYSPROC=" KEYWORD.  ITS VALUE MUST BE USED IN       *
*        CHANGING THE JCL KEYWORD TABLE IN IEFVFA.  THIS SYMBOL IS    *
*        DEFINED HERE TO AVOID HAVING TO CHANGE THE IEFVKEYS MACRO.   *
*                                                                     *
***********************************************************************
*
SYSPROCK EQU   HOLDK   *     DD      SYSPROC=  JOBPROC DD STATEMENTS
*
         IEFCOMWA ,                COMMON WORK AREA
*
         IEFCVRWA ,                CONVERTER WORK AREA
*
         POP  PRINT
*
         TITLE '**** DYNAMIC PROCLIB WORK AREA ****'
***********************************************************************
*                                                                     *
* PPWDSECT                                                            *
* ********                                                            *
*                                                                     *
* THE FOLLOWING IS THE DYNAMIC PROCEDURE LIBRARY SUPPORT WORK AREA.   *
* IT IS POINTED TO BY THE "&ANCHOR" FIELD IN THE CONVERTER WORK       *
* AREA.                                                               *
*                                                                     *
***********************************************************************
*
PPANCHOR EQU   &ANCHOR
*
         USING PPWDSECT,R13
PPWDSECT DSECT                     USER PROCLIB WORK AREA
*
PPWSAVE  DS    9D                  SAVE AREA FOR ALL ENTRY POINTS
PPWESAVE DS    9D                  SAVE AREA FOR ESTAE EXIT
*
PPWBASE1 DS    A                   FIRST BASE ADDRESS
PPWBASE2 DS    A                   SECOND BASE ADDRESS
*
PPWSTAT0 DS    X'00'               STATUS FLAGS 0 (PER INVOCATION)
PPW0VPP0 EQU   X'80'                    IEFVPP0 IS EXECUTING
PPW0VPP1 EQU   X'40'                    IEFVPP1 IS EXECUTING
PPW0VPP2 EQU   X'20'                    IEFVPP2 IS EXECUTING
PPW0VPP3 EQU   X'10'                    IEFVPP3 IS EXECUTING
PPW0EREC EQU   X'08'                    ERROR RECOVERY IN PROGRESS
PPW0VGM  EQU   X'04'                    CONTROL PASSED TO IEFVGM
PPW0DALC EQU   X'02'                    CONTROL PASSED TO SVC 99
PPW0INIT EQU   X'01'                    INITIALIZATION IS COMPLETE
*
PPWSTAT1 DS    X'00'               STATUS FLAGS 1 (PER JOB):
PPW1JPER EQU   X'80'                    ERROR NOTED IN JOBPROC STMT
PPW1HVJP EQU   X'40'                    JOBPROC DD CARD IS PRESENT
PPW1JPBZ EQU   X'20'                    POSSIBLE JOBPROC CONCAT.
PPW1CNJP EQU   X'10'                    JOBPROC IS A CONCATENATION
PPW1SYSP EQU   X'08'                    SYSTEM PROCLIB IS REQUIRED
PPW1BUFR EQU   X'04'                    NEW PROCLIB BUFFER OBTAINED
PPW1OPEN EQU   X'02'                    USER PROCLIB DCB IS OPEN
PPW1JPCN EQU   X'01'                    JOBPROC IS NOW CONCATENATED
*
PPWSTAT2 DS    X'00'               STATUS FLAGS 2 (PER STATEMENT):
PPW2DSN  EQU   X'80'                      DSNAME PROVIDED
PPW2VSER EQU   X'40'                      VOLSER PROVIDED
PPW2VREF EQU   X'20'                      VOL=REF=DSNAME SPECIFIED
PPW2UNIT EQU   X'10'                      UNIT=" " SPECIFIED
*
*        THE FOLLOWING TRACE FLAGS ARE USED FOR ERROR RECOVERY
*
PPWTRPP0 DS    X'00'               TRACE FLAGS FOR IEFVPP0
*
PPWTRPP1 DS    X'00'               TRACE FLAGS FOR IEFVPP1
*
PPWTRPP2 DS    X'00'               TRACE FLAGS FOR IEFVPP2
PPWT2PP2 EQU   X'80'                  PP2 CALLED FOR CURRENT JOB
PPWT2CON EQU   X'40'                  DYNAMIC CONCAT IN PROGRESS
PPWT2OPN EQU   X'20'                  PROCLIB OPEN IN PROGRESS
*
PPWTRPP3 DS    X'00'               TRACE FLAGS FOR IEFVPP3
PPWT3DCN EQU   X'80'                  DYNAMIC DE-CON IN PROGRESS
PPWT3DAL EQU   X'40'                  DYNAMIC DE-ALOC IN PROGRESS
*
PPWMISC  DS    X'00'               MISCELLANEOUS FLAGS
PPWMTRCE EQU   X'80'                  ALL SORTS OF TRACING CODE
PPWMESTA EQU   X'40'                  ESTAE HAS BEEN ISSUED
PPWMNOSY EQU   X'20'                  SYSPROC UNAVAILABLE
PPWMVSXA EQU   X'10'                  MVS/XA OR MVS/ESA SYSTEM
*
PPWTIOTA DS    A                   ADDRESS OF TIOT
PPWTCBAD DS    A                   CONVERTER TCB ADDRESS
*
PPWJPCNT DS    H                   NO. OF JOBPROC DD STATEMENTS
PPWJPSYS DS    H                   NO. OF SYSTEM PROCLIBS
PPWJPMAX DS    H                   MAXIMUM USER PROCLIBS ALLOWED
         DS    H                   RESERVED FOR FUTURE USE
*
PPWSYSDS DS    16A                 ADDRESS OF SYSPROC JFCBS
PPWDDBSE DS    CL8                 BASE DDNAME FOR ALL ALLOCATIONS
PPWDDSUF DS    CL16                DDNAME SUFFIX CHARACTERS
*
         AIF   (NOT &SWA220).NOSWA1
PPWSWAPL DS    2A                  SWAREQ PARAMETER POINTERS
PPWSWAEA DS    A                   SWAREQ PARAMETER LIST
PPWSWAEP DS    CL(L'SWAEPAX)       SWAREQ EPA
.NOSWA1  ANOP
*
PPWTXTEX DS    A                   -> ORIGINAL EXIT LIST ADDRESS
PPWSDCBA DS    A                   -> ORIGINAL IEFPDSI DCB
PPWSBUFA DS    A                   -> ORIG. PDSI BUF(IF REPLACED)
PPWBUFAD DS    A                   -> NEW PROCLIB BUF(IF REPLACED)
PPWSYSDD DS    CL8                 NAME OF SYSTEM PROCLIB DD
*
PPWDCB   DCB   DDNAME=PTTTTTT0,DSORG=PO,MACRF=(R)
PPWDCBDD EQU   (DCBDDNAM-IHADCB)+PPWDCB DYNAMIC PROCLIB DDNAME
PPWDCBL  EQU   *-PPWDCB            LENGTH OF DCB AREA
*
PPWDARGL DS    A                   DYN ALLOC ARG LIST
PPWDARB  DS    0XL20'0',5F'0'      DYN ALLOC REQUEST BLOCK
PPWDATXT DS    10A                 DYN ALLOC TEXT POINTERS
*
PPWDDNMK DS    H                   DDNAME KEY FOR DYN ALLOC/DE-ALLOC
PPWDDNM# DS    H                         NO. OF LENGTH-PARM ITEMS:
PPWDDNML DS    H                         DDNAME LENGTH
PPWDDNME DS    CL8                       USER PROCLIB DDNAME
*
PPWRTDSK DS    H                   RETURN DSORG KEY FOR DYN ALLOC
PPWRTDS# DS    H                         NO. OF LENGTH-PARM ITEMS:
PPWRTDSL DS    H                         DSORG LENGTH
PPWRTDSE DS    XL2                       DSORG RETURNED
*
PPWDSNMK DS    H                   DSNAME KEY FOR DYN ALLOC
PPWDSNM# DS    H                         NO. OF LENGTH-PARM ITEMS:
PPWDSNML DS    H                         DSNAME LENGTH
PPWDSNME DS    CL44                      USER PROCLIB DSNAME
*
PPWVOLSK DS    H                   VOLSER KEY FOR DYN ALLOC
PPWVOLS# DS    H                         NO. OF LENGTH-PARM ITEMS:
PPWVOLSL DS    H                         VOLSER LENGTH
PPWVOLSR DS    CL8                       USER PROCLIB VOLSER
*
PPWUNITK DS    H                   UNIT KEY FOR DYN ALLOC
PPWUNIT# DS    H                         NO. OF LENGTH-PARM ITEMS
PPWUNITL DS    H                         UNIT LENGTH
PPWUNIT  DS    CL8                       USER PROCLIB UNIT
*
PPWDISPK DS    H                   STATUS DISPOSITION KEY
PPWDISP# DS    H                         NO. OF LENGTH-PARM ITEMS
PPWDISPL DS    H                         DISPOSITION CODE LENGTH
PPWDISP  DS    X                         DISP CODE
*
PPWCLOSK DS    H                   FREE=CLOSE KEY FOR DYN DEALLOC
PPWCLOS# DS    H                         NO. OF LENGTH-PARM ITEMS:
*
PPWVLRFK DS    H                   VOLUME REFERENCE TO DSNAME KEY
PPWVLRF# DS    H                         NO. OF LENGTH-PARM ITEMS
PPWVLRFL DS    H                         REFERENCE DSNAME LENGTH
PPWVLREF DS    CL44                      VOLUME REFERENCE DSNAME
*
PPWCONCK DS    H                   DDNAME KEY FOR CONCATENATIONS
PPWCONC# DS    H                         NUMBER OF LENGTH-PARM ITEMS:
PPWCONCM EQU   &MAXPROC                  MAX NUMBER OF CONCATENATIONS
PPWCONCD DS    (PPWCONCM)XL10            DDNAME LENGTH AND TEXT
*
PPWDARET DS    A                   SAVE AREA FOR DYN ERROR ANALYSIS RTN
PPWDFPRM DS    5F                  DAIRFAIL PARM LIST
PPWDF02A DS    A                   DAIRFAIL - IKJEFF02 WORD
PPWDFRCD DS    F                   DAIRFAIL RETURN CODE
PPWDFIDN DS    H                   DAIRFAIL CALLER IDENT.
*
PPWMSRET DS    A                   SAVE AREA FOR MESSAGE ROUTINE RETURN
*
PPWESTAE ESTAE *-*,TERM=YES,MF=L
PPWESTAL EQU   *-PPWESTAE          AREA LENGTH
*
PPWOCLST OPEN  (PPWDCB,(INPUT)),MF=L
*
PPWLOCPL CAMLST NAME,*-*,,*-*      PARM LIST FOR LOCATE
*
         DS    0D
PPWLOCWK DS    CL268               WORK AREA FOR LOCATE
*
PPWTOWRK DS    CL144               WTO MESSAGE AREA
*
         DS    0D                  ALIGN TO A DOUBLE WORD
PPWORKLN EQU   *-PPWDSECT          WORK AREA LENGTH
*
PPALL    EQU   X'FF'               ALL BITS MASK
PPVLBIT  EQU   X'80'               PARMLIST VL
PPABEND  EQU   X'BAD'              INTERNAL ABEND CODE
*
PPASHR   EQU   X'08'               "DISP=SHR" PARM CODE
*
         TITLE '**** DYNAMIC PROCLIB MODULE PREFIX ****'
IEFVPP   CSECT ,                   CONTROL SECTION NAME
         ENTRY IEFVPP0             INITIALIZATION CALL
         ENTRY IEFVPP3             CLEAN UP AND TERMINATION
*
R10      EQU   10                  SECONDARY BASE REGISTER
R11      EQU   11                  PRIMARY BASE REGISTER
R12      EQU   12                  CONVERTER WORK AREA
R13      EQU   13                  REGISTER SAVE AREA
R14      EQU   14                  SCRATCH AND LINK
R15      EQU   15                  SCRATCH AND LINK
*
         USING COMWA,R12           SET STANDARD BASE
         USING IEFVPP,R11,R10      INFORM ASSEMBLER OF NORMAL BASE REG
*
         DC    AL1(VPPIDL)         LTH OF IDENTIFICATION PREFIX
VPPID    DC    CL8'IEFVPP'         USER PROCLIB SUPPORT MODULE ID
VPPVERSN DC    CL9'V3.R2.M4 '      CURRENT AMDAHL RELEASE NO.
VPPSDATE DC    CL8'&SYSDATE'       LAST ASSEMBLY DATE
VPPSTIME DC    CL8' &SYSTIME'      TIME OF LAST ASSEMBLY
VPPCPYRT DC    C'COPYRIGHT 1978, 1981, 1982, 1988  BY AMDAHL CORP.'
VPPIDL   EQU   *-VPPID             LENGTH OF IDENTIFICATION HEADER
*
         TITLE '**** IEFVPP0  - INITIALIZATION PROCESSING ****'
***********************************************************************
*                                                                     *
* IEFVPP0                                                             *
* *******                                                             *
*                                                                     *
* THIS ROUTINE IS CALLED TO PROCESS ANY INITIALIZATION FUNCTIONS.     *
* IF THIS ROUTINE IS NOT INVOKED, THE CONVERTER WILL FAIL IN THE MOST *
* TERRIBLE WAY, SINCE THE ADDRESS OF THE DYNAMIC PROCLIB WORK AREA    *
* WILL NOT HAVE BEEN PLACED IN THE CONVERTER WORK AREA RESERVED WORD. *
*                                                                     *
*    1  GETMAIN THE DYNAMIC PROCLIB WORK AREA AND ANCHOR IT IN        *
*       THE CONVERTER WORK AREA.                                      *
*    2  INITIALIZE SWITCHES, COUNTERS, AND TRACE FLAGS.               *
*    3  INITIALIZE DYNAMIC ALLOCATION PARAMETER BLOCK.                *
*    4  DETERMINE NUMBER AND NAMES OF SYSTEM PROCLIBS.                *
*    5  BRANCH TO IEFVHA.                                             *
*                                                                     *
* INPUT:                                                              *
*        CONVERTER WORK AREA (REGISTER 12).                           *
*                                                                     *
* OUTPUT:                                                             *
*        INITIALIZE DYNAMIC PROCLIB ANCHOR IN THE CWA AND THE         *
*        DYNAMIC PROCLIB WORK AREA ITSELF.                            *
*                                                                     *
* ENTERNALS:                                                          *
*        NONE                                                         *
*                                                                     *
* EXIT:                                                               *
*        BRANCH TO IEFVHA.                                            *
*                                                                     *
* SVC:                                                                *
*        GETMAIN  (SVC 120)                                           *
*        ESTAE    (SVC 60)                                            *
*        WTO      (SVC 35)                                            *
*                                                                     *
***********************************************************************
         EJECT
IEFVPP0  DS    0H                  ENTRY POINT ADDRESS
         USING *,R15               TEMPORARY BASE
*
         STM   R14,R12,12(R13)     SAVE CALLERS REGS
         L     R11,PP0BASE         SET UP MODULE BASE
         LA    R10,2048            SET UP SECOND
         LA    R10,2048(R10,R11)    BASE REGISTER
         DROP  R15                 DROP TEMPORARY BASE
*
         LA    R0,PPWORKLN         LENGTH OF PROCLIB WORK AREA
         GETMAIN RU,LV=(0),LOC=(BELOW,ANY) GET THE PROCLIB WORK AREA
         ST    R1,PPANCHOR         STORE IN CONVERTER WORK AREA
*
         LR    R0,R1               SET FOR MVCL
         LA    R1,PPWORKLN         LENGTH OF MVCL
         SLR   R15,R15             ZERO FROM LENGTH
         MVCL  R0,R14              CLEAR THE WORK AREA
*
         L     R1,PPANCHOR         RELOAD DYNAMIC PROCLIB WORK AREA
         ST    R1,8(,R13)          STORE FORWARD POINTER
         ST    R13,4(,R1)          STORE BACKWARD POINTER
         LR    R13,R1              SET NEW SAVE AREA ADDRESS
*
         MVI   PPWSTAT0,PPW0VPP0   INDICATE IEFVPP0 IS EXECUTING
*
         ST    R11,PPWBASE1        SET MODULE BASE IN WORK AREA
         ST    R10,PPWBASE2        SET MODULE BASE IN WORK AREA
*
         L     R15,PP0TRCV         LOAD CONVERTER TRACE ROUTINE ADDRESS
         CNOP  2,4                 ALIGN TRACE PARM LIST
         BALR  R14,R15             ENTER MODULE ID IN TRACE REC.
PP0TRCV  DC    V(TRACE)            TRACE ROUTINE ID (IN IEFVH1)
         DC    CL4'VPP0'           MODULE ID FOR TRACE RECORD
*
         MVC   PPWESTAE(PPWESTAL),PP0ESTAE MOVE IN MODEL PLIST
*
         ESTAE PPESTAEX,CT,        ESTABLISH DYNAMIC PROCLIB           X
               TERM=YES,PARAM=(12), ERROR RECOVERY                     X
               MF=(E,PPWESTAE)       ENVIRONMMENT
*
         OI    PPWMISC,PPWMESTA    INDICATE ESTAE IN EFFECT
*
         EJECT
***********************************************************************
*                                                                     *
*        THE INSTRUCTION BELOW CAN BE CHANGED OR ZAPPED TO ENABLE     *
*        EXTERNAL DEBUGGING TRACE CODE (WTO MESSAGES).                *
*                                                                     *
***********************************************************************
*
PP0SETTR DS    0AL4(PP0SETTR)      PROVIDE XREF REFERENCE
         OI    PPWMISC,PPWMTRCE    THIS IS HOW DEBUGGING IS TURNED ON
*
         AIF   ('&SYSPARM' EQ 'TRACE').PPTRACE
         ORG   PP0SETTR+1          ORG BACK TO IMMEDIATE FIELD
         DC    X'00'               PATCH TO TURN ON DEBUGGING CODE
         ORG   ,                   BACK TO NORMAL LOCATION COUNTER
.PPTRACE ANOP
*
         EJECT
***********************************************************************
*                                                                     *
*        INITIALIZE THE DYNAMIC PROCLIB WORK AREA                     *
*                                                                     *
***********************************************************************
*
         MVI   PPWSTAT1,0          INITIALIZE STATUS FLAG 1
         MVI   PPWSTAT2,0          INITIALIZE STATUS FLAG 2
*
         MVI   PPWTRPP0,0          INITIALIZE
         MVI   PPWTRPP1,0           TRACE
         MVI   PPWTRPP2,0            FLAGS
         MVI   PPWTRPP3,0
*
         L     R1,CVTPTR           LOAD THE CVT ADDRESS
         USING CVT,R1
         TM    CVTDCB,CVTMVSE      TEST FOR MVS/XA
         BZ    *+8                 BRANCH IF NOT
         OI    PPWMISC,PPWMVSXA    INDICATE MVS/XA
         DROP  R1
*
         MVC   PPWDCB(PP0DCBML),PP0DCBMD INIT DCB AREA
*
         LA    R9,PPWDARB          -> DYNAMIC ALLOC REQUEST BLOCK
         USING S99RB,R9            GET ADDRESSABILITY TO DARB
         ST    R9,PPWDARGL         SET PTR TO DARB IN PARM LIST->
         OI    PPWDARGL,S99RBPND   SET END OF PARM LIST INDICATOR
         XC    PPWDARB(L'PPWDARB),PPWDARB CLEAR ALLOC REQUEST BLOCK
         MVI   S99RBLN,L'PPWDARB   SET LENGTH OF REQUEST BLOCK
         MVI   S99FLG11,S99ONCNV+S99NOCNV+S99NOMNT SET FLAGS 1
*              NO CONVERTIBLE, NO EXISTING ALLOC, NO VOLUME MOUNT
         MVI   S99FLG21,S99NORES   SET NO DATA SET ENQUEUE FLAGS 2
         LA    R0,PPWDATXT         -> DYNAMIC ALLOCATION TEXT
         ST    R0,S99TXTPP         SET TEXT LIST PTR IN DARB
*
         LA    R1,1                GET A NICE CONSTANT
*
         LA    R0,DALDDNAM         GET DDNAME KEY
         STH   R0,PPWDDNMK         SET DDNAME KEY
         STH   R1,PPWDDNM#         SET NUMBER OF OPERANDS
         LA    R0,8                GET MAX DDNAME LENGTH
         STH   R0,PPWDDNML         SET DDNAME LENGTH
*
         LA    R0,DALRTORG         GET RETURN DSORG KEY
         STH   R0,PPWRTDSK         SET RETURN DSORG KEY
         STH   R1,PPWRTDS#         SET NUMBER OF OPERANDS
         LA    R0,2                GET DSORG LENGTH
         STH   R0,PPWRTDSL         SET DSORG LENGTH
*
         LA    R0,DALDSNAM         GET DSNAME TEXT KEY
         STH   R0,PPWDSNMK         SET DSNAME TEXT KEY
         STH   R1,PPWDSNM#         SET NUMBER OF OPERANDS
*
         LA    R0,DALSTATS         GET STATUS DISP KEY
         STH   R0,PPWDISPK         SET STATUS DISP KEY
         STH   R1,PPWDISP#         SET NUMBER OF OPERANDS
         STH   R1,PPWDISPL         SET LENGTH OF DISP CODE
*
         LA    R0,DALCLOSE         GET FREE=CLOSE KEY
         STH   R0,PPWCLOSK         SET FREE=CLOSE KEY
         XC    PPWCLOS#,PPWCLOS#   SET NUMBER OF OPERANDS
*
         LA    R0,DALVLSER         GET VOL=SER TEXT KET
         STH   R0,PPWVOLSK         SET VOL=SER TEXT KEY
         STH   R1,PPWVOLS#         SET NUMBER OF OPERANDS
*
         LA    R0,DALVLRDS         GET VOL=REF TEXT KEY
         STH   R0,PPWVLRFK         SET VOL=REF TEXT KEY
         STH   R1,PPWVLRF#         SET NUMBER OF OPERANDS
*
         LA    R0,DALUNIT          GET UNIT TEXT KEY
         STH   R0,PPWUNITK         SET UNIT TEXT KEY
         STH   R1,PPWUNIT#         SET NUMBER OF OPERANDS
*
         LA    R0,DCCDDNAM         GET CONCATENATE DDNAME KEY
         STH   R0,PPWCONCK         SET CONCATENATE DDNAME KEY
*
         LA    R15,PPWCONCD        POINT AT FIRST CONCAT DDNAME
         LA    R14,PPWCONCM        GET MAXIMUM NUMBER OF DDNAMES
         LA    R0,8                GET THE DDNAME LENGTH
*
PP0CONCL STH   R0,0(,R15)          STORE THE LENGTH
         LA    R15,L'PPWCONCD(,R15)     INCREMENT TO NEXT DDNAME
         BCT   R14,PP0CONCL        LOOP THROUGH THE CONCATENATION LIST
*
         LA    R9,PPWDFPRM         -> DAIRFAIL PARM LIST
         USING DFDSECTD,R9         MAKE IT ADDRESSABLE
*
         LA    R1,PPWDARB          -> DYNAMIC ALLOCATE RB
         ST    R1,DFS99RBP         SET IN DFPL
         LA    R1,PPWDFRCD         -> RETURN CODE HOLDING AREA
         ST    R1,DFRCP            SET IN DFPL
         LA    R1,PPWDF02A         -> IKJEFF02 ADDRESS AREA
         ST    R1,DFJEFF02         STORE PTR IN DAIRFAIL PLIST
         XC    PPWDF02A,PPWDF02A   SHOW IKJEFF02 ADDRESS UNKNOWN
         LA    R1,PPWDFIDN         -> DAIRFAIL IDENTIFICATION NO.
         ST    R1,DFIDP            SET IN DFPL
         OI    DFIDP,X'80'         FLAG END OF PARM LIST
*
         XC    DFCPPLP,DFCPPLP     CLEAR CPPL POINTER
         LA    R1,50               INDICATE SVC 99 ERROR
         STH   R1,PPWDFIDN          IN DAIRFAIL PARM LIST
         OI    PPWDFIDN,DFWTP      INDICATE WTP REQUEST
*
         MVC   PPWLOCPL(PP0CAMLL),PP0CAMLS MOVE IN LOCATE CAMLST
         LA    R1,PPWDSNME         -> DSNAME HOLD AREA
         ST    R1,PPWLOCPL+4       SET DSNAME PTR IN CAMLST
         LA    R1,PPWLOCWK         -> LOCATE WORK AREA
         ST    R1,PPWLOCPL+12      SET WORK AREA PTR IN CAMLST
*
         AIF   (NOT &SWA220).NOSWA2
         MVC   PPWSWAPL(8),PP0SWARQ     INITIALIZE SWAREQ PARM LIST
         LA    R1,PPWSWAEA         POINT AT EPA POINTER
         ST    R1,PPWSWAPL         STORE IN PARAMETER LIST
         XC    PPWSWAEP,PPWSWAEP   CLEAR THE SWA EPA
         LA    R1,PPWSWAEP         POINT AT SWA EPA
         ST    R1,PPWSWAEA         STORE IN POINTER
.NOSWA2  ANOP
*
         L     R1,PSATOLD-PSA(,R0) LOAD TCB ADDRESS
         ST    R1,PPWTCBAD         SAVE THE TCB ADDRESS
*
         L     R1,TCBTIO-TCB(,R1)  LOAD TIOT ADDRESS
         ST    R1,PPWTIOTA         SAVE THE ADDRESS
*
         UNPK  PPWLOCWK(9),PPWTCBAD(5)  UNPACK THE TCB ADDRESS
         TR    PPWLOCWK(8),PP0HEXTR-240 CONVERT TO PRINTABLE HEX
         MVI   PPWDDBSE,C'P'       SET DDNAME PREFIX CHARACTER
         MVC   PPWDDBSE+1(6),PPWLOCWK+2 MOVE THE TCB ADDRESS
         MVI   PPWDDBSE+7,C' '     BLANK THE LAST CHARACTER
         MVC   PPWDDSUF,PP0DDPAT   SET THE DDNAME SUFFIXES
*
         EJECT
***********************************************************************
*                                                                     *
*        LOCATE THE SYSTEM PROCLIBS FROM THE OPEN CONVERTER DCB       *
*                                                                     *
***********************************************************************
*
         L     R1,PPWTIOTA         LOAD THE TIOT ADDRESS
         USING TIODSECT,R1         SET UP ADDRESSIBILITY
*
         SLR   R15,R15             ZERO FOR INSERTS
         SLR   R2,R2               ZERO FOR COUNTING
*
         L     R9,PDCBP            LOAD CURRENT PROCLIB DCB ADDRESS
         USING IHADCB,R9
         TM    DCBOFLGS,DCBOFOPN   IS THIS DCB OPEN
         BZ    PP0SYSMS            IF NOT, SKIP COUNTING DATA SETS
*
         SLR   R8,R8               CLEAR FOR INSERT
         ICM   R8,3,DCBTIOT        LOAD THE DCB TIOT OFFSET
         AR    R8,R1               GET THE ACTUAL TIOT ENTRY ADDRESS
         DROP  R9
         USING TIOENTRY,R8
*
         MVC   PPWSYSDD,TIOEDDNM   SAVE THIS DDNAME FOR POSTERITY
*
PP0SYCNT DS    0H
         SLR   R1,R1               CLEAR FOR THE INSERT
         ICM   R1,7,TIOEJFCB       LOAD THE ADDRESS
         TM    TIOEJFCB+2,X'01'    IS THE JFCB ADDRESS REALLY A TOKEN
         BO    PP0SYS31            BRANCH IF SO
         LA    R1,16(,R1)          POINT AT THE ACTUAL JFCB
         B     PP0SYSTO            AND GO STORE THIS ADDRESS
*
PP0SYS31 DS    0H
         AIF   (NOT &SWA220).NOSWA3
         STCM  R1,7,SWVA-ZB505+PPWSWAEP STORE THE SVA
         SWAREQ MF=(E,PPWSWAPL),UNAUTH=YES
         LTR   R15,R15             TEST IF SUCCESSFUL
         BZ    *+6                 BRANCH IF GOOD
.NOSWA3  ANOP
         DC    H'0'                IF THIS HAPPENS, WE ARE IN TROUBLE
*
         AIF   (NOT &SWA220).NOSWA4
         L     R1,SWBLKPTR-ZB505+PPWSWAEP LOAD THE ACTUAL SWA BLK ADDR
.NOSWA4  ANOP
*
PP0SYSTO DS    0H
         LR    R14,R2              GET PROCLIB COUNT (-1)
         SLL   R14,2               MULTIPLY BY 4
         ST    R1,PPWSYSDS(R14)    STORE THE JFCB ADDRESS
*
         LA    R2,1(,R2)           BUMP SYSTEM PROCLIB COUNT
         IC    R15,TIOELNGH        LOAD THE LENGTH
         AR    R8,R15              INCREMENT THE ADDRESS
         CLI   TIOELNGH,0          CHECK FOR LAST ENTRY
         BE    *+12                SKIP CONCATENATION LOOP IF SO
         CLI   TIOEDDNM,C' '       CHECK FOR CONCATENATION
         BE    PP0SYCNT            LOOP IF IT IS
         DROP  R8
*
         LA    R0,PPWCONCM         SET MAXIMUM PROCLIBS
         SR    R0,R2               DEDUCT FOR SYSTEM PROCLIBS
         STH   R2,PPWJPSYS         SET THE SYSTEM PROCLIB COUNT
         STH   R0,PPWJPMAX         SET THE MAX USER PROCLIB COUNT
         B     PP0SYSOK            AND FINISH INITIALIZATION
*
PP0SYSMS L     R1,PPWTIOTA         LOAD THE TIOT ADDRESS
         CLC   TIOCNJOB(8),=CL8'MSTRJCL' TEST IF MASTER'S TIOT
         BE    PP0SYMST            SKIP MESSAGE IF SO
         CLC   TIOCNJOB(6),=C'MSTJCL'   TEST IF MASTER'S TIOT (1.3.2)
         BE    PP0SYMST            SKIP MESSAGE IF SO
         DROP  R1                  DONE WITH THE TIOT
*
         L     R1,PSAAOLD-PSA(,0)  LOAD ASCB ADDRESS
         LH    R1,ASCBASID-ASCB(,R1)    LOAD THE ASID
         CH    R1,=H'1'            CHECK FOR THE MASTER ADDRESS SPACE
         BE    PP0SYMST            BRANCH IF SO
*
         WTO   MF=(E,PPMSYSMS)     INDICATE ERROR
*
PP0SYMST OI    PPWMISC,PPWMNOSY    INDICATE SYSPROC UNAVAILABLE
         LA    R0,PPWCONCM         SET MAXIMUM PROCLIBS
         STH   R0,PPWJPMAX         STASH THE MAX
*
PP0SYSOK DS    0H
         SR    R0,R0               CLEAR R0
         STH   R0,PPWJPCNT         INITIALIZE CONCATENATION COUNT
*
         EJECT
***********************************************************************
*                                                                     *
*        CLEAN UP AFTER A PREVIOUS CONVERTER FAILURE                  *
*                                                                     *
***********************************************************************
*
         L     R1,PPWTIOTA         GET THE TIOT ADDRESS
         USING TIODSECT,R1         SET UP ADDRESSIBILITY
*
         SLR   R15,R15             ZERO FOR INSERTS
*
PP0CLNL1 CLI   TIOELNGH,0          CHECK FOR END OF TIOT
         BE    PP0CLNDN            BRANCH IF NAME NOT FOUND
         CLC   TIOEDDNM(7),PPWDDBSE     CHECK FOR LEFT OVER DDNAME
         BE    PP0CLNUP            IF FOUND, WE HAVE TO CLEAN UP
*
         IC    R15,TIOELNGH        LOAD THE LENGTH
         AR    R1,R15              INCREMENT ADDRESS
         B     PP0CLNL1            CONTINUE TIOT SCAN
*
         DROP  R1
*
PP0CLNUP DS    0H                  NEED TO CLEAN UP LEFTOVERS
         LA    R9,PPWDARB          -> DYNAMIC ALLOC REQUEST BLOCK
         USING S99RB,R9            GET DARB ADDRESSABILITY
         MVI   S99VERB,S99VRBDC    INDICATE DE-CONCAT REQUEST
         LA    R9,PPWDATXT         -> TEXT UNIT PTR SLOTS
*
         USING S99TUPL,R9          GET TEXT PTR ADDRESSABILITY
         USING S99TUNIT,R8         GET TEXT UNIT ADDRESSABILITY
*
         LA    R8,PPWCONCK         -> DE-CONCATENATE TEXT UNIT
         LA    R0,DDCDDNAM         GET DE-CONCATENATE KEY
         STH   R0,S99TUKEY         SET DE-CONCATENATE KEY
         LA    R1,1                GET THE NUMBER OF DDNAMES
         STH   R1,S99TUNUM         SET NO. OF DDNAMES
         MVC   S99TUPAR(8),PPWDDBSE     SET BASE DDNAME
         MVC   S99TUPAR+7(1),PPWDDSUF   AND FIRST DDNAME SUFFIX
         ST    R8,S99TUPTR         SET PTR TO DE-CONC TEXT UNIT
         OI    S99TUPTR,S99TUPLN   FLAG END OF TEXT UNIT PTR'S
*
         LA    R1,PPWDARGL         -> DYNAMIC ALLOC PARM PTR
         OI    PPWSTAT0,PPW0DALC   INDICATE WE ARE IN ALLOC. CODE
         DYNALLOC ,                DE-CONCATENATE PROCLIB'S
         NI    PPWSTAT0,PPALL-PPW0DALC  INDICATE WE ARE BACK
*
         DROP  R8,R9
*
         PPTRACE DCN0              TRACE DECONCATENATION IN PP0
*
         LA    R0,DCCDDNAM         GET CONCATENATE DDNAME KEY
         STH   R0,PPWCONCK         REPAIR CONCATENATE DDNAME KEY
*
         LA    R2,PPWDDSUF         POINT AT DDNAME SUFFIX STRING
         LA    R3,PPWCONCM         GET MAXIMUM CONCATENATIONS
*
         LA    R9,PPWDARB          -> DYNAMIC ALLOC REQUEST BLOCK
         USING S99RB,R9            GET DARB ADDRESSABILITY
         MVI   S99VERB,S99VRBUN    INDICATE UNALLOC REQUEST
         LA    R9,PPWDATXT         -> TEXT UNIT PTR SLOTS
*
         USING S99TUPL,R9          GET TEXT PTR ADDRESSABILITY
         USING S99TUNIT,R8         GET TEXT UNIT ADDRESSABILITY
*
         LA    R8,PPWDDNMK         -> DDNAME TEXT UNIT
         LA    R0,DUNDDNAM         SET DDNAME TEXT KEY
         STH   R0,PPWDDNMK         SET DDNAME TEXT KEY
*
         ST    R8,S99TUPTR         SET PTR TO DDNAME TEXT UNIT
         OI    S99TUPTR,S99TUPLN   FLAG END OF TEXT UNIT PTR'S
*
         MVC   S99TUPAR(8),PPWDDBSE     MOVE THE DDNAME BASE
*
PP0CLNDL DS    0H
         MVC   S99TUPAR+7(1),0(R2) MOVE THE DDNAME SUFFIX
*
         LA    R1,PPWDARGL         -> DYNAMIC ALLOC PARM PTR
         OI    PPWSTAT0,PPW0DALC   INDICATE WE ARE IN ALLOC. CODE
         DYNALLOC ,                FREE A PROCLIB DATA SET
         NI    PPWSTAT0,PPALL-PPW0DALC  INDICATE WE ARE BACK
*
         LA    R2,1(,R2)           INCREMENT THE SUFFIX POINTER
         BCT   R3,PP0CLNDL         LOOP THROUGH DEALLOCATION
*
         DROP  R8,R9
*
         PPTRACE DAL0              TRACE DEALLOCATION IN PP0
*
         LA    R0,DALDDNAM         GET DDNAME KEY
         STH   R0,PPWDDNMK         REPAIR DDNAME KEY
*
PP0CLNDN DS    0H
*
         EJECT
***********************************************************************
*                                                                     *
*        PLANT THE CONVERTER POST SCAN EXIT INTERCEPT                 *
*                                                                     *
***********************************************************************
*
         MVC   PPWTXTEX,CWATXTEX   SAVE THE CONVERTER'S POST SCAN EXIT
         LA    R0,PP0EXITL         POINT AT OUR EXIT LIST
         ST    R0,CWATXTEX         AND OVERLAY THE STANDARD EXIT ADDR
*
         PPTRACE END0              INDICATE END OF PP0
*
         EJECT
***********************************************************************
*                                                                     *
*        THE FOLLOWING MVI INSTRUCTION SHOULD HAVE ITS IMMEDIATE      *
*        FIELD MODIFIED FROM PPW0INIT TO ZERO TO CRIPPLE THE DYNAMIC  *
*        PROCLIB SUPPORT.  THIS IS THE ONLY WAY, SHORT OF REMOVING    *
*        ALL CONVERTER MODIFICATIONS (ZAPS AND ALL).                  *
*                                                                     *
***********************************************************************
*
PP0CANCL DS    0AL4(PP0CANCL)      PROVIDE XREF REFERENCE
         MVI   PPWSTAT0,PPW0INIT   INDICATE VPP0 HAS COMPLETED
*
PP0RETRN DS    0H
         L     R13,4(,R13)         UNCHAIN SAVE AREAS
         L     R15,PP0VHA          LOAD THE IEFVHA ADDRESS
         L     R14,12(,R13)        RELOAD
         LM    R0,R12,20(R13)       REGISTERS
         BR    R15                 AND LET IT DO IT'S THING
*
         EJECT
***********************************************************************
*                                                                     *
*        CONSTANTS AND DATA AREAS USED BY IEFVPP0                     *
*                                                                     *
***********************************************************************
*
PP0BASE  DC    A(IEFVPP)           BASE ADDRESS FOR ALL ROUTINES
*
PP0VHA   DC    V(IEFVHA)           EXIT FROM IEFVPP0
*
PP0EXITL DS    0F                  OUR INTERNAL TEXT EXIT LIST
         DC    AL1(NELEXADD)       ENTRY POINT IS 3-BYTE ADDRESS
         DC    AL1(NELTXTEX)       INDICATE POST SCAN EXIT ID
         DC    AL2(0)              FILLER
         DC    A(IEFVPP1)          ADDRESS OF OUR POST SCAN EXIT
*
PP0DCBMD DCB   DDNAME=********,    PATTERN DCB                         X
               DSORG=PO,                                               X
               MACRF=(R),                                              X
               BUFNO=0,                                                X
               BUFCB=0,                                                X
               RECFM=FB,                                               X
               LRECL=80
PP0DCBML EQU   *-PP0DCBMD          PATTERN DCB LENGTH
*
PP0CAMLS CAMLST NAME,*-*,,*-*      BUILD LOCATE PARM LIST
*
PP0CAMLL EQU   *-PP0CAMLS          PARM LIST LENGTH
*
PP0ESTAE ESTAE *-*,CT,             ESTABLISH DYNAMIC PROCLIB           X
               TERM=YES,            ERROR                              X
               PARAM=*-*,            RECOVERY                          X
               MF=L                   ENVIRONMMENT
*
         AIF   (NOT &SWA220).NOSWA5
PP0SWARQ SWAREQ FCODE=LA,EPA=*-*,UNAUTH=YES,MF=L
.NOSWA5  ANOP
*
PP0HEXTR DC    C'0123456789ABCDEF' CHARACTER STRING FOR HEX TRANSLATE
PP0DDPAT EQU   PP0HEXTR            LET'S REUSE THIS STRING
*
PP0LITRL LTORG ,                   FLUSH LITERALS
*
         TITLE '**** IEFVPP1  - //JOBPROC DD PROCESSOR ****'
***********************************************************************
*                                                                     *
* IEFVPP1                                                             *
* *******                                                             *
*                                                                     *
*    THIS ROUTINE RECEIVES CONTROL FROM IEFVFA AS IF IT WERE THE      *
*    CONVERTER POST SCAN EXIT.  IT IS CALLED FOR EACH CONVERTED       *
*    JCL STATEMENT IN INTERNAL TEXT FORM.  IT DETERMINES IF IT IS     *
*    BEING CALLED FOR A DD STATEMENT OR AN EXEC STATEMENT AND WILL    *
*    BRANCH TO IEFVPP2 IF THE FIRST EXEC STATEMENT IS ENCOUNTERED.    *
*    THE IEFVPP1 PORTION WILL PERFORM THE FOLLOWING:                  *
*                                                                     *
*    1) DETERMINES WHETHER THE DD STATEMENT IS A JOBPROC. IF NOT,     *
*       CONTROL IS PASSED TO PP12RETN.                                *
*                                                                     *
*    2) THE INTERNAL TEXT STRING IS FLAGGED AS IF DD DYNAM WAS        *
*       SPECIFIED ON THE JOBPROC DD CARD. THIS METHOD IS USED TO      *
*       CAUSE THE INTERPRETER TO IGNORE THE DD STATEMENT AT THE       *
*       INTERPRETER "DD" MODULE (IEFVDA) DURING INTERPRETATION.       *
*       THIS IS NECESSARY IN ORDER TO PREVENT A MISPLACED DD          *
*       STATEMENT ERROR MESSAGE.                                      *
*                                                                     *
*    3) DETERMINES IF THE FIRST EXEC STATEMENT OF THE STEP HAS        *
*       BEEN RECEIVED. IF IT HAS, AN ERROR MESSAGE INDICATING THAT    *
*       THE JOBPROC DD STATEMENT IS "MISPLACED" IS ISSUED AND         *
*       CONTROL IS PASSED TO PP12RETN.                                *
*                                                                     *
*    4) DETERMINES IF THIS DD STATEMENT IS CONCATENATED TO A PREVIOUS *
*       JOBPROC DD STATEMENT. IF NOT (IE. A DDNAME HAS BEEN CODED), A *
*       MESSAGE IS ISSUED INDICATING THE THIS IS A DUPLICATE JOBPROC  *
*       DD STATEMENT.                                                 *
*                                                                     *
*                                                                     *
*    5) ALL INFORMATION NECESSARY TO ALLOCATE THE JOBPROC DATA SET    *
*       DEFINED ON THE DD STATEMENT IS OBTAINED BY SCANNING THE       *
*       INTERNAL TEXT BUFFER. CURRENTLY ONLY THE FOLLOWING DD CARD    *
*       KEYWORDS ARE PROCESSED:                                       *
*                                                                     *
*           KEYWORD       RESTRICTIONS                                *
*          ---------     --------------                               *
*           DSNAME           NONE                                     *
*           UNIT             FIRST POSITIONAL ONLY (IE. UNIT NAME)    *
*           VOLUME           SER="VOLUME SERIAL NUMBER"               *
*                            REF="DSNAME"                             *
*           SYSPROC          YES -> INSTALLATION PROCLIBS AS DEFINED  *
*                                   BY THE IEFDPDSI DD CARD IN THE    *
*                                   JES PROC SHOULD BE CONCATENATED   *
*                                   FOLLOWING THE USERS' PROCLIBS.    *
*                            NO  -> ONLY THE USERS' PROCLIBS SHOULD   *
*                                   BE SEARCHED.                      *
*                                                                     *
*    6) THE USER PROCLIB IS ALLOCATED TO "JES" VIA THE DYNAMIC        *
*       ALLOCATION FACILITIES OF OS/VS.  THE MAXIMUM NO. OF DD'S      *
*       ALLOWED IS SET DURING INITIALIZATION (IEFVPP0) AS "16" MINUS  *
*       THE NUMBER OF PROCLIBS PROVIDED IN THE SYSTEM PROCLIB THAT    *
*       WAS OPEN DURING DYNAMIC PROCLIB INITIALIZATION.               *
*       IF SYSPROC=NO IS SPECIFIED, THE NUMBR OF USER SUPPLIED        *
*       PROCLIBS IS AUTOMATICALLY SET TO THE MAXIMUM (16).            *
*                                                                     *
*    7) CONTROL IS PASSED TO PP12RETN TO INVOKE THE REAL POST SCAN    *
*       EXIT AND RETURN TO IEFVFA.                                    *
*                                                                     *
* INPUT:                                                              *
*        IWA(SWY)      - INDICATES IF IEFVFA HAS DETECTED A SYNTAX    *
*                        ERROR IN THE CURRENT STATEMENT               *
*        IWA(TEXTBUFP) - CONTAINS A PTR TO THE INTERNAL TEXT BUFFER   *
*        IEFVKEYS      - JCL KEY EQUATES USED FOR SCANNING THE TEXT   *
*                        BUFFER                                       *
*                                                                     *
* OUTPUT:                                                             *
*        INTERNAL TEXT STRING IS FLAGGED AS REPRESENTING A JOBPROC    *
*        AND USER PROCLIB IS ALLOCATED.                               *
*                                                                     *
* SUBROUTINES:                                                        *
*        CALL TO PPMESSGE WHEN ERROR CONDITION IS DETECTED.           *
*                                                                     *
* EXIT:                                                               *
*        BRANCH TO RR12RETN.                                          *
*                                                                     *
* SVC:                                                                *
*        DYNAMIC ALLOCATION (SVC 99)                                  *
*                                                                     *
* NOTES:                                                              *
*     1) THE TEXT SCANNING IS SOMEWHAT SIMPLIFIED SINCE ALL REQUIRED  *
*        OPERANDS ARE THE FIRST FOR THE RELATED KEYWORDS.             *
*                                                                     *
*     2) ALL ERROR SITUATIONS CAUSE A CALL TO THE PPMESSGE SUBROUTINE *
*        WHICH CAUSES THE JOB TO BE FLUSHED ON A JCL ERROR. ERROR     *
*        MESSAGES ARE WRITTEN TO THE USER'S JCL MESSAGES DATA SET.    *
*                                                                     *
***********************************************************************
         EJECT
IEFVPP1  DS    0H                  ENTRY POINT ADDRESS
         STM   R14,R12,12(R13)     SAVE CALLERS REGS
         L     R1,PPANCHOR         LOAD PP WORK AREA ADDRESS
         ST    R13,4(,R1)          STORE BACKWARD POINTER
         ST    R1,8(,R13)          STORE FORWARD POINTER
         LR    R13,R1              SET NEW SAVE AREA ADDRESS
*
         L     R11,PPWBASE1        SET UP MODULE 1ST BASE
         L     R10,PPWBASE2        SET UP MODULE 2ND BASE
*
         TM    PPWSTAT0,PPW0INIT   IS JOBPROC SUPPORT ENABLED?
         BZ    PP12RETN            NO, SIMPLY RETURN
*
         OI    PPWSTAT0,PPW0VPP1   INDICATE IEFVPP1 IS EXECUTING
*
         L     R15,PP1TRCV         LOAD CONVERTER TRACE ROUTINE ADDRESS
         CNOP  2,4                 ALIGN TRACE PARM LIST
         BALR  R14,R15             ENTER MODULE ID IN TRACE REC.
PP1TRCV  DC    V(TRACE)            TRACE ROUTINE ID (IN IEFVH1)
         DC    CL4'VPP1'           MODULE ID FOR TRACE RECORD
*
         EJECT
***********************************************************************
*                                                                     *
*        DETERMINE THE ENVIRONMENT                                    *
*                                                                     *
***********************************************************************
*
         TM    SWZ,DDSW            IS THIS A DD STATEMENT
         BO    PP1DDCRD            BRANCH IF SO
*
         TM    SWA,JHS             HAD FIRST EXEC STATEMENT
         BO    PP12RETN            RETURN IF SO
         TM    SWZ,EXECSW          IS THIS AN EXEC STATEMENT
         BZ    PP12RETN            BRANCH IF NOT
*
         TM    PSTMT+4,X'08'       IS THIS A CONVERTED PROC
         BO    PP12RETN            RETURN IF SO
         B     PP2ENTRY            BRANCH TO IEFVPP2
*
***********************************************************************
*                                                                     *
*        DETERMINE IF WE ARE INTERESTED IN THIS DD CARD               *
*                                                                     *
***********************************************************************
*
PP1DDCRD DS    0H
         TM    SWY,X'01'           ANY SYNTAX ERROR IN STATEMENT ?
         BO    PP12RETN            YES, IGNORE THE STATEMENT
*
         L     R1,TEXTBUFP         -> INTERNAL TEXT BUFFER
         LA    R1,STRDKEY-TEXT(,R1)  -> START KEY
         CLI   1(R1),1             SINGLE POSITIONAL (= DDNAME) ?
         BNE   PP12RETN            NO, WILL GET JCL ERROR
*
         TM    PPWSTAT1,PPW1JPBZ   BUSY ON JOBPROC CONCAT ?
         BO    PP1TCONC            YES, SKIP
*
         CLC   2(8,R1),PP1JBPRC    IS START OF JOBPROC CONCAT ?
         BNE   PP12RETN            NO, EXIT
*
         TM    PPWSTAT1,PPW1HVJP   BEEN HERE BEFORE ?
         BO    PP1ER836            YES -> MISPLACED JOBPROC ERROR
*
         OI    PPWSTAT1,PPW1HVJP+PPW1JPBZ BUSY, HAVE JOBPROC
         OI    PPWSTAT1,PPW1SYSP*&SYSPROC SET DEFAULT SYSPROC=YES|NO
         TM    SWA,JHS             FIRST EXEC STATEMENT REACHED YET?
         BZ    PP1COMM             CONTINUE INITIALIZATION IF NOT
         B     PP1ER836            ERROR IF SO
*
PP1TCONC DS    0H
         CLI   2(R1),0             MISSING DDNAME = CONCATENATION
         BE    PP1SETCO            YES, GO SAVE STATUS
*
         NI    PPWSTAT1,PPALL-PPW1JPBZ   CLEAR JOBPROC BUSY STATUS
         CLC   2(8,R1),PP1JBPRC    A SECOND "JOBPROC" DD CARD ?
         BE    PP1ER836            YES, GIVE "MISPLACED JOBPROC"
         B     PP12RETN            AND EXIT
*
PP1SETCO DS    0H
         TM    SWA,JHS             FIRST EXEC STATEMENT REACHED YET?
         BO    PP1ER836            ERROR IF SO
*
         OI    PPWSTAT1,PPW1CNJP   SHOW CONCATENATED USER FILES
*
PP1COMM  DS    0H
         MVI   PPWDSNME,C' '       CLEAR DSNAME AREA
         MVC   PPWDSNME+1(43),PPWDSNME    TO BLANKS
         MVC   PPWVLREF,PPWDSNME   CLEAR VOL=REF=DSNAME
         MVC   PPWVOLSR,PPWDSNME   CLEAR VOLSER AREA
         MVC   PPWUNIT,PPWDSNME    CLEAR THE UNIT NAME AREA
         MVI   PPWDISP,PPASHR      SET DEFAULT DISP TO "SHR" SO THAT
*                                   DISP PARAMETER MAY BE IGNORED
*
         PPTRACE ENT1              TRACE PP1 WITH JOBPROC CARD
*
         L     R2,TEXTBUFP         R2 -> INTERNAL TEXT BUFFER
         OI    STRDINDC-TEXT(R2),DTXDYNAM FLAG DD CARD AS A DD DYNAM
*
         TM    PPWSTAT1,PPW1JPER   PREVIOUS ERROR NOTED ?
         BO    PP12RETN            YES, JUST EXIT
*
         MVI   PPWSTAT2,0          CLEAR STATEMENT STATUS FLAGS
*
         EJECT
***********************************************************************
*                                                                     *
*        SCAN THE INTERNAL TEXT LOOKING FOR OUR PARAMETERS            *
*                                                                     *
***********************************************************************
*
         SR    R0,R0               CLEAR NUMBER/COUNT STACK (MAX = 4)
         SR    R14,R14             USED FOR NUMBER/COUNT
         SR    R15,R15             USED FOR LENGTH
*
PP1ITXT1 DS    0H
         LR    R2,R1               SAVE KEY ADDR
         CLI   0(R2),ENDK          END OF TEXT ?
         BE    PP1TSTOP            BRANCH IF YES..GO TEST OPTIONS
         IC    R14,1(,R1)          LOAD NUMBER
         LA    R1,2(,R1)           -> LENGTH/COUNT/KEY
         LTR   R14,R14             HAVE OPERAND ?
         BZ    PP1ITXT1            NULL OPERAND, LOOP
*
PP1ITXT2 DS    0H
         TM    0(R1),PPVLBIT       IS THIS A COUNT ?
         BZ    PP1ITXT3            NO, SKIP
         SLA   R0,8                PUSH NUMBER/COUNT STACK
         BO    PP1ER800            ERROR IF OVERFLOW
         OR    R0,R14              SAVE CURRENT NUMBER/COUNT
         IC    R14,0(,R1)          LOAD NEW COUNT
         LA    R1,1(,R1)           -> LENGTH/COUNT
         N     R14,PP1MSKCL        CLEAR TOP FLAG AND TEST ZERO
         BNZ   PP1ITXT2            HAVE COUNT, GO INSPECT NEXT
         B     PP1ITXT5            NULL COUNT, GO POP STACK
*
PP1ITXT3 DS    0H
         IC    R15,0(,R1)          LOAD LENGTH
         LTR   R2,R2               HAVE A KEY TO INSPECT ?
         BZ    PP1ITXT4            NO, SKIP
         LTR   R3,R15              IS LENGTH NULL ?
         BZ    PP1ITXT4            IGNORE IF SO
*
         CLI   0(R2),DSNAMEK       DSNAME ?
         BE    PP1SETDS                 GO SAVE IT
         CLI   0(R2),SERMK         VOLUME SERIAL ?
         BE    PP1SETSR                 GO SAVE IT
         CLI   0(R2),REFMK         VOLUME REF BY DSNAME ?
         BE    PP1SETRF                 GO SAVE IT
         CLI   0(R2),SYSPROCK      SYSPROC OPTION ?
         BE    PP1SETSY                 GO SAVE IT
         CLI   0(R2),UNITK         UNIT SPECIFICATION ?
         BE    PP1SETUN                 GO SAVE IT
*
PP1ITXT4 DS    0H
         SR    R2,R2               KILL KEY INSPECTION TILL NEXT KEY
         LA    R1,1(R15,R1)        -> LENGTH/COUNT/KEY
         BCT   R14,PP1ITXT2        LOOP FOR REST OF NUMBER/COUNT
*
PP1ITXT5 DS    0H
         SRA   R0,8                OPERAND END, POP THE NUMBER/COUNT
         BZ    PP1ITXT1            STACK EMPTY, GO DO NEXT KEY
         LR    R14,R0              SET LOOP COUNTER
         N     R14,PP1MSKCL        ONLY ONE BYTE VALUE
         B     PP1ITXT2            GO TEST FOR COUNT/LENGTH NEXT
*
         EJECT
***********************************************************************
*                                                                     *
*        EXTRACT THE INTERNAL TEXT PARAMETER DATA                     *
*                                                                     *
***********************************************************************
*
PP1SETSY DS    0H
         TM    PPWSTAT1,PPW1CNJP   PRIMARY JOBPROC STATEMENT ?
         BO    PP1ER801            NO -> ERROR, SYSPROC NOT VALID
         CLC   PP1SPYES(4),0(R1)   SYSPROC=YES ?
         BE    PP1ITXT4                 IS DEFAULT, RETURN TO SCAN
         CLC   PP1SPNO(3),0(R1)    SYSPROC=NO ?
         BNE   PP1ER843            ELSE INVALID OPERAND
*
         NI    PPWSTAT1,PPALL-PPW1SYSP  SAVE NO SYSTEM PROC WANTED
         LA    R4,PPWCONCM         SET MAXIMUM NUMBER OF PROCLIBS
         STH   R4,PPWJPMAX         SET IT FOR LATER USE
         B     PP1ITXT4            CONTINUE SCAN
*
PP1SETDS DS    0H
         OI    PPWSTAT2,PPW2DSN    SHOW DSNAME GIVEN
         STH   R3,PPWDSNML         SAVE STRING LENGTH
         LA    R4,PP1DSNTB         -> TABLE ENTRY FOR PROCESSING
         B     PP1TSTMV            MERGE
*
PP1SETSR DS    0H
         OI    PPWSTAT2,PPW2VSER   SHOW VOLSER GIVEN
         STH   R3,PPWVOLSL         SAVE STRING LENGTH
         LA    R4,PP1SERTB         -> TABLE ENTRY FOR PROCESSING
         B     PP1TSTMV            MERGE
*
PP1SETUN DS    0H
         OI    PPWSTAT2,PPW2UNIT   SHOW UNIT GIVEN
         STH   R3,PPWUNITL         SAVE STRING LENGTH
         LA    R4,PP1UNITB         -> TABLE ENTRY FOR PROCESSING
         B     PP1TSTMV            MERGE
*
PP1SETRF DS    0H
         OI    PPWSTAT2,PPW2VREF   SHOW VOLUME REF BY DSNAME
         STH   R3,PPWVLRFL         SAVE DSNAME LENGTH FOR ALLOC
         LA    R4,PP1REFTB         -> TABLE ENTRY FOR PROCESSING
*
PP1TSTMV DS    0H
         CH    R3,0(,R4)           TEST STRING LENGTH FOR MAX
         BH    PP1ER842            "EXCESSIVE PARAMETER LENGTH"
         BCTR  R3,0                GET MC LENGTH FOR MOVE
         EX    R3,2(,R4)           SET STRING ACCORDING TO MOVE
         B     PP1ITXT4            CONTINUE SCAN
*
         EJECT
***********************************************************************
*                                                                     *
*        MAXIMUM LENGTH OF DATA FOLLOWED BY EXECUTED INSTRUCTION      *
*        THAT WILL MOVE THE USER SPECIFIED PARAMETER                  *
*                                                                     *
***********************************************************************
*
PP1DSNTB DC    Y(44)                       MAX STRING LENGTH
         MVC   PPWDSNME(*-*),1(R1)         SHIFT STRING TO WORK AREA
*
PP1SERTB DC    Y(6)
         MVC   PPWVOLSR(*-*),1(R1)
*
PP1REFTB DC    Y(44)
         MVC   PPWVLREF(*-*),1(R1)
*
PP1UNITB DC    Y(8)
         MVC   PPWUNIT(*-*),1(R1)
*
         EJECT
***********************************************************************
*                                                                     *
*        CHECK FOR REQUIRED PARAMETERS                                *
*                                                                     *
***********************************************************************
*
PP1TSTOP DS    0H
         TM    PPWSTAT2,PPW2DSN    IS DSNAME PROVIDED ?
         BZ    PP1ER802            ERROR IF MISSING
*
         TM    PPWSTAT2,PPW2UNIT   UNIT PARAMETER SPECIFIED ?
         BZ    PP1TNMSS            BRANCH IF NOT
         CLC   PPWUNIT,=CL8'3330V' WAS MSS SPECIFIED AS UNIT?
         BE    PP1ER811            ERROR IF SO
         CLC   PPWUNIT,=CL8'SDG00' CHECK LOWER BOUND OF SDGXX NAME
         BL    PP1TNMSS            OK IF LOWER
         CLC   PPWUNIT,=CL8'SDG99' CHECK UPPER BOUND OF SDGXX NAME
         BNH   PP1ER811            ERROR IF NOT HIGHER
*
PP1TNMSS DS    0H
*
***********************************************************************
*                                                                     *
*        IF IT IS DESIRED TO PROHIBIT ACCESS TO ANY DATA SETS VIA     *
*        JOBPROC, THE CHECK CAN BE MADE HERE APPROPRIATE MESSAGE      *
*        ROUTINE (PP1ER803) CAN BE BRANCHED TO.                       *
*                                                                     *
***********************************************************************
*
         CLC   PPWJPCNT,PPWJPMAX   ARE WE ALREADY AT THE MAXIMUM ?
         BNL   PP1ER804            YES, GIVE UP
*
         EJECT
***********************************************************************
*                                                                     *
*        ALLOCATE THE USER'S PROCLIB                                  *
*                                                                     *
***********************************************************************
*
PP1GOALC LA    R9,PPWDARB          -> DYNAMIC ALLOC REQUEST BLOCK
         USING S99RB,R9            GET ADDRESSABILITY TO DARB
         MVI   S99VERB,S99VRBAL    INDICATE "ALLOC DSNAME" REQUEST
         LA    R9,PPWDATXT         -> ALLOCATION TEXT POINTERS
*
         USING S99TUPL,R9          MAKE TEXT PTRS ADDRESSABLE
         USING S99TUNIT,R8         MAKE TEXT UNITS ADDRESSABLE
*
         LH    R2,PPWJPCNT         GET CURRENT CONCATENATION NO.
         LA    R2,1(,R2)           BUMP BY ONE
*
         LA    R8,PPWDDNMK         -> DDNAME RETURN TEXT UNIT SLOT
         MVC   S99TUPAR(8),PPWDDBSE     SET THE BASE DDNAME
         LA    R1,PPWDDSUF-1(R2)   POINT AT SUFFIX CHARACTER
         MVC   S99TUPAR+7(1),0(R1) MOVE THE SUFFIX CHARACTER
         ST    R8,S99TUPTR         SET PTR TO RETURN DDNAME TEXT UNIT
*
         LA    R8,PPWRTDSK         -> DSORG RETURN TEXT UNIT SLOT
         LA    R9,L'S99TUPTR(,R9)  -> NEXT TEXT UNIT PTR
         XC    S99TUPAR(2),S99TUPAR     CLEAR THE DSORG
         ST    R8,S99TUPTR         SET PTR TO RETURN DSORG TEXT UNIT
*
         LA    R8,PPWDSNMK         -> DSNAME TEXT UNIT SLOT
         LA    R9,L'S99TUPTR(,R9)  -> NEXT TEXT UNIT PTR
         ST    R8,S99TUPTR         SET PTR TO "DSNAME" TEXT UNIT
*
         LA    R8,PPWDISPK         -> STATUS DISPOSITION TEXT
         LA    R9,L'S99TUPTR(,R9)  -> NEXT TEXT PTR SLOT
         ST    R8,S99TUPTR         SET PTR TO "STATUS" DISP TEXT
*
         LA    R8,PPWCLOSK         -> FREE=CLOSE TEXT
         LA    R9,L'S99TUPTR(,R9)  -> NEXT TEXT PTR SLOT
         ST    R8,S99TUPTR         SET PTR TO "STATUS" DISP TEXT
*
         TM    PPWSTAT2,PPW2VSER   VOLUME SERIAL NO. SPECIFIED ?
         BZ    PP1AVREF            BRANCH IF NOT
         LA    R8,PPWVOLSK         -> VOLUME SERIAL TEXT UNIT
         LA    R9,L'S99TUPTR(,R9)  -> NEXT TEXT PTR SLOT
         ST    R8,S99TUPTR         SET PTR TO "VOL=SER=" TEXT
*
PP1AVREF TM    PPWSTAT2,PPW2VREF   "VOL=REF=DSN" SPECIFIED ?
         BZ    PP1AUNIT            BRANCH IF NOT
         LA    R8,PPWVLRFK         -> VOLUME REFERENCE TEXT UNIT
         LA    R9,L'S99TUPTR(,R9)  -> NEXT TEXT PTR SLOT
         ST    R8,S99TUPTR         SET PTR TO "VOL=SER=" TEXT
*
PP1AUNIT TM    PPWSTAT2,PPW2UNIT   UNIT PARAMETER SPECIFIED ?
         BZ    PP1AENDT            BRANCH IF NOT
         LA    R8,PPWUNITK         -> "UNIT" TEXT UNIT
         LA    R9,L'S99TUPTR(,R9)  -> NEXT TEXT PTR SLOT
         ST    R8,S99TUPTR         SET PTR TO "UNIT" TEXT
*
PP1AENDT OI    S99TUPTR,S99TUPLN   SET END OF TEXT UNITS INDICATOR
         LA    R1,PPWDARGL         R1 -> DYNAMIC ALLOC PARM LIST
         OI    PPWSTAT0,PPW0DALC   INDICATE WE ARE IN ALLOC. CODE
         DYNALLOC ,                ALLOCATE TO USER PROCLIB
         NI    PPWSTAT0,PPALL-PPW0DALC INDICATE WE ARE BACK
         LTR   R15,R15             ALLOCATION SUCCESSFUL ?
         BZ    PP1DYNOK            BRANCH IF YES
*
         BAL   R14,PPDAERRA        DO ERROR ANALYSIS
         B     PP12RETN            RETURN TO CALLER
*
PP1DYNOK STH   R2,PPWJPCNT         UPDATE CONCATENATION NO.
         BCTR  R2,0                SUBTRACT ONE
         MH    R2,=Y(L'S99TULEN+8) MULTIPLE BY 10
         LA    R2,PPWCONCD(R2)     CALCULATE DDNAME POSITION
         MVC   2(8,R2),PPWDDNME    COPY THE ALLOCATED DDNAME
*
         PPTRACE ALC1              TRACE END OF ALLOCATION
*
         TM    PPWRTDSE,X'02'      TEST FOR DSORG=PO
         BZ    PP1ER803            DATA SET NOT VALID IF WRONG DSORG
*
         L     R1,PPWTIOTA         LOAD THE TIOT ADDRESS
         USING TIODSECT,R1         SET UP ADDRESSIBILITY
         SLR   R15,R15             ZERO FOR INSERTS
*
PP1TIOLP CLI   TIOELNGH,0          CHECK FOR END OF TIOT
         BE    PP1ER811            BRANCH IF NAME NOT FOUND (ERROR)
         CLC   TIOEDDNM,PPWDDNME   CHECK FOR THE ALLOCATED DD ENTRY
         BE    PP1TIOTF            BRANCH IF FOUND
         IC    R15,TIOELNGH        LOAD THE LENGTH
         AR    R1,R15              INCREMENT ADDRESS
         B     PP1TIOLP            CONTINUE TIOT SCAN
*
PP1TIOTF ICM   R1,7,TIOEFSRT       LOAD THE UCB ADDRESS
         BZ    PP1ER811            IF NONE, THIS IS VERY BAD
         USING UCBDSECT,R1         TELL THE ASSEMBLER ABOUT THE UCB
*
         TM    UCBTBYT3,UCB3DACC   TEST FOR DIRECT ACCESS
         BZ    PP1ER811            IF NOT, THIS CANNOT BE ALLOWED
         TM    UCBTBYT2,UCBRVDEV   TEST FOR VIRTUAL DEVICE
         BO    PP1ER811            THIS IS ALSO NOT ALLOWED
*
         DROP  R1
*
         PPTRACE END1              TRACE END OF PP1
*
         B     PP12RETN            RETURN TO CONVERTER
*
         DROP  R8,R9
*
         EJECT ,
***********************************************************************
*                                                                     *
*        THE FOLLOWING ARE THE ERROR MESSAGE INTERFACES TO PPMESSGE   *
*        FOR THE JOBPROC DD STATEMENT PROCESSOR (IEFVPP1).            *
*                                                                     *
***********************************************************************
*
PP1ER800 DS    0H
         LA    R2,PPMS800I         SYNTAX ERROR IN JOBPROC STMT
         B     PP1ERRNS            GO PRODUCE ERROR MESSAGE
*
PP1ER801 DS    0H
         LA    R2,PPMS801I         ILLEGAL USE OF "SYSPROC" KEY
         B     PP1ERRNS            GO PRODUCE ERROR MESSAGE
*
PP1ER802 DS    0H
         LA    R2,PPMS802I         JOBPROC DSNAME MISSING
         B     PP1ERRNS
*
PP1ER803 DS    0H
         LA    R2,PPMS803I         DATA SET NOT VALID FOR JOBPROC
         B     PP1ERRNS
*
PP1ER804 DS    0H
         LA    R2,PPMS804I         CONCATENATION LIMIT EXCEEDED
         B     PP1ERRNS
*
PP1ER811 DS    0H
         LA    R2,PPMS811I         JOBPROC VOLUME NOT ALLOWED
         B     PP1ERRNS
*
PP1ER836 DS    0H
         LA    R2,PPMS836I         MISPLACED OR DUPLICATE JOBPROC
         B     PP1ERRNS
*
PP1ER842 DS    0H
         LA    R2,PPMS842I         EXCESSIVE PARAMETER LENGTH (SYSPROC)
         B     PP1ERRNS
*
PP1ER843 DS    0H
         LA    R2,PPMS843I         UNIDENTIFIED PARAMETER IN SYSPROC
         B     PP1ERRNS
*
PP1ERRNS DS    0H
         BAL   R14,PPMESSGE        PRODUCE ERROR MESSAGE
         PPTRACE ERR1              TRACE ERROR IN PP1
*
         B     PP12RETN            RETURN TO CALLER
*
         EJECT
***********************************************************************
*                                                                     *
*        CONSTANTS FOR IEFVPP1 PROCESSING                             *
*                                                                     *
***********************************************************************
*
PP1MSKCL DC    A(X'7F')            TO CLEAR FLAG BIT IN INT TEXT COUNT
PP1JBPRC DC    AL1(7),C'&DDNAME'   INT. TEXT DEF FOR DDNAME
*
PP1SPYES DC    AL1(3),C'YES'       INT TEXT FORM FOR SYSPROC=YES
PP1SPNO  DC    AL1(2),C'NO'        INT TEXT FORM FOR SYSPROC=NO
*
         TITLE '**** IEFVPP2 - SET USER PROCLIB ENVIRONMENT ****'
***********************************************************************
*                                                                     *
* IEFVPP2                                                             *
* *******                                                             *
*                                                                     *
* THIS ROUTINE IS PASSED CONTROL FROM IEFVPP1 WHEN THE FIRST EXEC     *
* STATEMENT HAS BEEN DETECTED.                                        *
*                                                                     *
*      1) A CHECK IS MADE TO SEE IF A JOBPROC DD CARD HAS BEEN        *
*         DETECTED BY IEFVPP1. IF NOT, CONTROL IS IMMEDIATELY PASSED  *
*         TO PP12RETN.                                                *
*                                                                     *
*      2) IF THE JOBPROC DEFINES A CONCATENATION OR THE "SYSPROC=YES" *
*         OPTION HAS BEEN CHOSEN, THE OS/VS DYNAMIC ALLOCATION        *
*         ROUTINES ARE CALLED TO CONCATENATE THE DATA SETS ALLOCATED  *
*         BY IEFVPP1.                                                 *
*                                                                     *
*      3) THE USER PROCLIB DCB IS OPENED.                             *
*                                                                     *
*      4) IF THE PROCLIB BUFFER OBTAINED BY CONVERTER INITIALIZATION  *
*         IS SMALLER THAN THE USER'S PROCLIB BLKSIZE, A NEW BUFFER IS *
*         OBTAINED AND ITS ADDRESS SET IN THE CONVERTER WORK AREA.    *
*                                                                     *
*      5) ALL ERRORS ENCOUNTERED DURING IEFVPP2 CAUSE AN APPROPRIATE  *
*         ERROR MESSAGE TO BE ISSUED AND THE JOB BEING PROCESSED TO   *
*         FAIL WITH A JCL ERROR.                                      *
*                                                                     *
*      6) CONTROL IS PASSED BACK TO IEFVFA VIA PP12RETN.              *
*                                                                     *
* INPUT:                                                              *
*        IWA(PDCBP)    -> ORIGINAL PROCLIB DCB                        *
*        IWA(IWAINTS4) -> ORIGINAL PROCLIB BUFFER                     *
*                                                                     *
* OUTPUT:                                                             *
*        REGISTERS RESTORED                                           *
*        IWA(PDCBP)    -> JOBPROC DCB                                 *
*        IWA(IWAINTS4) -> TEMPORARY PROCLIB BUFFER (IF NEEDED)        *
*        IWA(PSTMT) -> TEMPORARY PROCLIB BUFFER (IF NEEDED COPY)      *
*                                                                     *
* EXTERNALS:                                                          *
*        PPMESSGE FOR ERROR CONDITION                                 *
*                                                                     *
* EXIT:                                                               *
*        RETURN TO IEFVFA VIA PP12RETN.                               *
*                                                                     *
* SVC:                                                                *
*        DYNAMIC ALLOCATION (SVC 99)                                  *
*        OPEN (SVC 19)                                                *
*        GETMAIN (SVC 120)                                            *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        INDICATE WHERE WE ARE NOW                                    *
*                                                                     *
***********************************************************************
*
PP2ENTRY DS    0H
         XI    PPWSTAT0,PPW0VPP1+PPW0VPP2 INDICATE IEFVPP2 ENTRY
*
***********************************************************************
*                                                                     *
*        STATUS ROUTING                                               *
*                                                                     *
***********************************************************************
*
         TM    PPWSTAT1,PPW1HVJP   HAVE JOBPROC TO PROCESS ?
         BZ    PP12RETN            NO, RETURN
*
         PPTRACE ENT2              TRACE ENTRY INTO PP2
*
         TM    PPWSTAT1,PPW1JPER   ERROR NOTED IN JOBPROC ?
         BO    PP12RETN            SO JUST RETURN
*
         LH    R0,PPWJPCNT         LOAD DD CARD COUNT
         LTR   R0,R0               TEST IF ANY
         BZ    PP12RETN            FORGET THE WHOLE THING
*
         TM    PPWTRPP2,PPWT2PP2   HAVE WE BEEN HERE BEFORE ?
         BO    PP12RETN            YES, MUST HAVE BEEN A FAILURE
*                                   DURING PROCESSING OF FIRST
*                                    EXEC STATEMENT WHICH TURNED
*                                     OUT TO BE AN "EXEC PROC="
         OI    PPWTRPP2,PPWT2PP2   INDICATE PROCLIB OPEN HAS BEEN
*                                   TRIED FOR CURRENT JOB IN CASE
*                                    CONVERTER RE-ENTERS IEFVPP2
*                                     FOR THE SAME JOB
         EJECT
***********************************************************************
*                                                                     *
*        ALLOCATE THE SYSTEM PROCLIB DATA SETS IF NECESSARY           *
*                                                                     *
***********************************************************************
*
         TM    PPWSTAT1,PPW1SYSP   TEST IF SYSTEM PROCLIB(S) WANTED
         BZ    PP2ANSYS            NO, SKIP
         TM    PPWMISC,PPWMNOSY    TEST IF SYSPROC UNAVAILABLE
         BO    PP2ANSYS            YES, SKIP CONCATENATION
         ICM   R3,3,PPWJPSYS       GET COUNT OF SYSTEM PROCLIBS
         BZ    PP2ANSYS            BRANCH IF NOT
*
         OI    PPWSTAT1,PPW1CNJP   INDICATE CONCATENATION REQUIRED
         LA    R3,1                START SYSTEM PROCLIB COUNT AT 1
*
PP2ASYSL LA    R9,PPWDARB          -> DYNAMIC ALLOC REQUEST BLOCK
         USING S99RB,R9            GET ADDRESSABILITY TO DARB
         MVI   S99VERB,S99VRBAL    INDICATE "ALLOC DSNAME" REQUEST
         LA    R9,PPWDATXT         -> ALLOCATION TEXT POINTERS
*
         USING S99TUPL,R9          MAKE TEXT PTRS ADDRESSABLE
         USING S99TUNIT,R8         MAKE TEXT UNITS ADDRESSABLE
*
         LH    R2,PPWJPCNT         GET CURRENT CONCATENATION NO.
         LA    R2,1(,R2)           BUMP BY ONE
*
         LR    R15,R3              GET SYSTEM PROCLIB NUMBER
         BCTR  R15,0               SUBTRACT ONE
         SLL   R15,2               MULTIPLY BY 4
         L     R15,PPWSYSDS(R15)   LOAD PROCLIB JFCB ADDRESS
         USING JFCBDSCT,R15
*
         TM    PPWMISC,PPWMVSXA    ARE WE MVS/XA
         BZ    PP2ASYX1            BRANCH IF NOT
         LA    R14,PP2ASYX1        GET THE ADDRESS
         O     R14,=X'80000000'    INDICATE 31-BIT MODE
         BSM   0,R14               GET INTO 31-BIT MODE(JFCB ABOVE 16M)
PP2ASYX1 DS    0H
*
         LA    R8,PPWDDNMK         -> DDNAME RETURN TEXT UNIT SLOT
         MVC   S99TUPAR(8),PPWDDBSE     SET THE BASE DDNAME
         LA    R1,PPWDDSUF-1(R2)   GET THE DDNAME SUFFIX
         MVC   S99TUPAR+7(1),0(R1) SET THE DDNAME SUFFIX
         ST    R8,S99TUPTR         SET PTR TO RETURN DDNAME TEXT UNIT
*
         LA    R8,PPWDSNMK         -> DSNAME TEXT UNIT SLOT
         LA    R9,L'S99TUPTR(,R9)  -> NEXT TEXT UNIT PTR
         MVC   S99TULNG,=H'44'     SET DATA SET NAME LENGTH
         MVC   S99TUPAR(44),JFCBDSNM    MOVE THE DATA SET NAME
         ST    R8,S99TUPTR         SET PTR TO "DSNAME" TEXT UNIT
*
         LA    R8,PPWDISPK         -> STATUS DISPOSITION TEXT
         LA    R9,L'S99TUPTR(,R9)  -> NEXT TEXT PTR SLOT
         MVI   PPWDISP,PPASHR      SET DEFAULT DISP TO "SHR" SO THAT
         ST    R8,S99TUPTR         SET PTR TO "STATUS" DISP TEXT
*
         LA    R8,PPWVOLSK         -> VOLUME SERIAL TEXT UNIT
         LA    R9,L'S99TUPTR(,R9)  -> NEXT TEXT PTR SLOT
         MVC   S99TULNG,=H'6'      SET VOL SER LENGTH
         MVC   S99TUPAR(6),JFCBVOLS     MOVE THE VOLUME SERIAL NUMBER
         ST    R8,S99TUPTR         SET PTR TO "VOL=SER=" TEXT
*
         LA    R8,PPWUNITK         -> "UNIT" TEXT UNIT
         LA    R9,L'S99TUPTR(,R9)  -> NEXT TEXT PTR SLOT
         LA    R0,8                GET UNIT NAME LENGTH
         STH   R0,S99TULNG         SET THE UNIT NAME LENGTH
         MVC   S99TUPAR(8),=C'SYSALLDA' SET THE UNIT NAME
         ST    R8,S99TUPTR         SET PTR TO "UNIT" TEXT
*
         TM    PPWMISC,PPWMVSXA    ARE WE MVS/XA
         BZ    PP2ASYX2            BRANCH IF NOT
         LA    R14,PP2ASYX2        GET THE ADDRESS
         BSM   0,R14               GET BACK INTO 24-BIT MODE
PP2ASYX2 DS    0H
*
         OI    S99TUPTR,S99TUPLN   SET END OF TEXT UNITS INDICATOR
         LA    R1,PPWDARGL         R1 -> DYNAMIC ALLOC PARM LIST
         OI    PPWSTAT0,PPW0DALC   INDICATE WE ARE IN ALLOC. CODE
         DYNALLOC ,                ALLOCATE TO USER PROCLIB
         NI    PPWSTAT0,PPALL-PPW0DALC INDICATE WE ARE BACK
         LTR   R15,R15             ALLOCATION SUCCESSFUL ?
         BZ    PP2SYSOK            BRANCH IF YES
*
         BAL   R14,PPDAERRA        DO ERROR ANALYSIS
         B     PP12RETN            RETURN TO CALLER
*
PP2SYSOK STH   R2,PPWJPCNT         UPDATE CONCATENATION NO.
         BCTR  R2,0                SUBTRACT ONE
         MH    R2,=Y(L'S99TULEN+8) MULTIPLE BY 10
         LA    R2,PPWCONCD(R2)     CALCULATE DDNAME POSITION
         MVC   2(8,R2),PPWDDNME    COPY THE ALLOCATED DDNAME
*
         LA    R3,1(,R3)           INCREMENT SYSTEM PROCLIB COUNT
         CH    R3,PPWJPSYS         ARE WE DONE?
         BNH   PP2ASYSL            LOOP IF WE AREN'T
*
         DROP  R8,R9
*
         PPTRACE ALC2              TRACE ALLOCATION IN PP2
*
         EJECT
***********************************************************************
*                                                                     *
*        CONCATENATE ALL USER AND SYSTEM PROCLIB DATA SETS TOGETHER   *
*                                                                     *
***********************************************************************
*
PP2ANSYS TM    PPWSTAT1,PPW1CNJP   REQUIRE CONCATENATION ?
         BO    PP2CONCT            YES, PERFORM CONCATENATION
         TM    PPWSTAT1,PPW1SYSP   TEST IF SYSTEM PROCLIB(S) WANTED
         BZ    PP2OPEN             NO, SKIP
         TM    PPWMISC,PPWMNOSY    TEST IF SYSPROC UNAVAILABLE
         BO    PP2OPEN             YES, SKIP CONCATENATION
*
PP2CONCT LA    R9,PPWDARB          -> DYNAMIC ALLOC REQUEST BLOCK
         USING S99RB,R9            GET DARB ADDRESSABILITY
         MVI   S99VERB,S99VRBCC    INDICATE CONCATENATION REQUEST
         LA    R9,PPWDATXT         -> ALLOCATION TEXT PTR SLOTS
*
         USING S99TUPL,R9          GET TEXT LIST ADDRESSABILITY
         USING S99TUNIT,R8         MAKE TEXT UNITS ADDRESSABLE
*
         LA    R8,PPWCONCK         -> CONCATENATION TXT UNIT SLOT
         ST    R8,S99TUPTR         SET PTR TO CONCATENATE TEXT
         LH    R14,PPWJPCNT        LOAD COUNT OF DDNAMES
         STH   R14,S99TUNUM        SET NO. OF DDNAMES TO CONC.
         OI    S99TUPTR,S99TUPLN   FLAG END OF TEXT UNIT PTR'S
*
         LA    R1,PPWDARGL         -> ALLOC REQUEST PARM PTR
         OI    PPWSTAT0,PPW0DALC   INDICATE WE ARE IN ALLOC. CODE
         DYNALLOC ,                CONCATENATE THE PROCLIB'S
         NI    PPWSTAT0,PPALL-PPW0DALC INDICATE WE ARE BACK
         LTR   R15,R15             WAS CONCATENATION OK ?
         BZ    PPCSETCI            BRANCH IF YES
*
         BAL   R14,PPDAERRA        DO ERROR ANALYSIS
         B     PP12RETN            RETURN TO CALLER
*
         DROP  R8,R9
*
PPCSETCI OI    PPWSTAT1,PPW1JPCN   SHOW FILES CONCATENATED
*
         PPTRACE CON2              TRACE CONCATENATION IN PP2
*
         EJECT
***********************************************************************
*                                                                     *
*        OPEN THE USER PROCLIB(S)                                     *
*                                                                     *
***********************************************************************
*
PP2OPEN  DS    0H
         OI    PPWTRPP2,PPWT2OPN   SET "OPEN" TRACE EVENT
         OI    PPWOCLST,PPVLBIT    SET LISTEND
*
         MVC   PPWDCBDD,PPWCONCD+2 SET THE FIRST DDNAME
*
         OPEN  (PPWDCB),MF=(E,PPWOCLST) OPEN THE FILE
*
PP2TOPEN DS    0H
         NI    PPWTRPP2,PPALL-PPWT2OPN  RESET "OPEN" TRACE EVENT ?
         NI    PPWSTAT0,PPALL-PPW0EREC  RESET IN CASE WE HAD ERROR
*
         TM    DCBOFLGS-IHADCB+PPWDCB,DCBOFOPN DID FILE OPEN OK ?
         BZ    PP2ER812            NO, SKIP
         OI    PPWSTAT1,PPW1OPEN   ELSE SAVE STATUS
         PPTRACE OPN2              TRACE OPEN IN PP2
*
         EJECT
***********************************************************************
*                                                                     *
*        DETERMINE IF A NEW BUFFER IS NEEDED                          *
*                                                                     *
***********************************************************************
*
         LH    R2,DCBBLKSI-IHADCB+PPWDCB LOAD REQUIRED BUFFER LEN
         L     R3,PDCBP            -> STANDARD PROCLIB DCB
         USING IHADCB,R3
*
         CH    R2,DCBBLKSI         COMPARE BLOCK SIZES
         BNL   *+10                BRANCH IF NEW NOT SMALLER
         MVC   DCBBLKSI-IHADCB+PPWDCB,DCBBLKSI REPLACE SMALLER BLKSIZE
         BNH   PP2STDCB            NO NEED FOR NEW
*
         GETMAIN RC,LV=(R2),LOC=(BELOW,ANY) NOW GETMAIN BUFFER
         LTR   R15,R15             ALL OK ?
         BNZ   PP2ER810            NOGO
*
         ST    R1,PPWBUFAD         SAVE PTR TO OUR BUFFER
         OI    PPWSTAT1,PPW1BUFR   SHOW BUFFER GOTTEN
         MVC   PPWSBUFA,IWAINTS4   SAVE ORIG BUFFER PTR
         MVC   IWAINTS4+1(3),PPWBUFAD+1  SET NEW
         MVC   PSTMT+1(3),PPWBUFAD+1     SET NEW (COPY)
*
PP2STDCB DS    0H
         MVC   DCBREAD+1-IHADCB+PPWDCB(3),DCBREAD+1 SHIFT FIELDS THAT
         MVC   DCBPOINT+1-IHADCB+PPWDCB(3),DCBPOINT+1  CONVERTER
         MVC   DCBEODAD+1-IHADCB+PPWDCB(3),DCBEODAD+1  LIKES TO
         MVC   DCBSYNAD+1-IHADCB+PPWDCB(3),DCBSYNAD+1  PLAY WITH
         MVC   PPWSDCBA(4),PDCBP   SAVE ORIG DCB ADDR
         MVC   PDCBP+1(3),PPWOCLST+1   AND SET TO NEW
*
         PPTRACE END2              TRACE END OF PP2
*
         B     PP12RETN            RETURN
*
         DROP  R3
*
         EJECT
***********************************************************************
*                                                                     *
*        ERROR ROUTINES                                               *
*                                                                     *
***********************************************************************
*
PP2ER812 DS    0H
         LA    R2,PPMS812I         USER PROCLIB OPEN FAILED
         B     PP2ERROR
*
PP2ER810 DS    0H
         LA    R2,PPMS810I         INSUFFICIENT BUFFER STORAGE
*
PP2ERROR DS    0H
         BAL   R14,PPMESSGE        GIVE USER HIS ERROR
         PPTRACE ERR2              TRACE ERROR IN PP2
*
         B     PP12RETN            RETURN TO CALLER
*
         TITLE '**** EXIT ROUTINE FOR IEFVPP1/2 ****'
***********************************************************************
*                                                                     *
* PP12RETN                                                            *
* ********                                                            *
*                                                                     *
*        THIS PASSES CONTROL BACK TO IEFVFA, WHICH CALLED IEFVPP1     *
*        AS THE CONVERTER POSTSCAN EXIT ROUTINE.  BEFORE RETURNING    *
*        TO IEFVFA, THE "REAL" POST SCAN EXIT (IF ANY) IS CALLED      *
*        AND (IF ASSEMBLED) THE INTERNAL TEXT IEFUJV EXIT CALL IS     *
*        MADE.                                                        *
*                                                                     *
*                                                                     *
* INPUT:                                                              *
*        R13 -> DYNAMIC PROCLIB WORK AREA (PPWDSECT).                 *
*                                                                     *
* OUTPUT:                                                             *
*        CURRENT ERROR RECOVERY ENVIRONMENT, IF ANY, IS CANCELLED,    *
*        TRACE FLAGS ARE RESET, AND CONTROL IS PASSED BACK TO         *
*        THE CALLER (IEFVFA) VIA A REGISTER 14.                       *
*                                                                     *
* EXTERNALS:                                                          *
*        SMFEXIT TO INVOKE IEFUJV FOR INTERNAL TEXT EXIT.             *
*                                                                     *
* EXIT:                                                               *
*        RETURN TO IEFVFA VIA REGISTER 14.                            *
*                                                                     *
* SVC:                                                                *
*        LINK  (SVC 6)                                                *
*                                                                     *
***********************************************************************
         EJECT
PP12RETN DS    0H
         ICM   R15,15,PPWTXTEX     SEE IF WE HAVE A POST SCAN EXIT
         BZ    PP12NPST            BRANCH IF NOT
*
         LA    R1,TEXTBUFP         POINT AT INTERNAL EXIT
         TM    0(R15),NELEXADD     TEST FOR EXIT ADDRESS
         BO    PP12PBAL            BRANCH IF SO
*
         LA    R2,CWAPSENM         POINT AT NAME
         L     R15,CWALINK         POINT AT LINK PARM LIST
         LINK  EPLOC=(R2),MF=(E,(1)),SF=(E,(15)) LINK TO POST SCAN EXIT
         B     PP12NPST            AND CONTINUE ON
*
PP12PBAL L     R15,4(,R15)         LOAD THE ACTUAL EXIT ADDRESS
         BALR  R14,R15             CALL THE POST SCAN EXIT
*
PP12NPST DS    0H
*
         AIF   (NOT &INTEXTX).UJVDONE
***********************************************************************
*                                                                     *
* NOTES:                                                              *
*        THIS ROUTINE IS NOT PART OF THE DYNAMIC PROCLIB SUPPORT      *
*        PER SE, BUT IT DOES USE THE CONVERTER WORK AREA INTERFACE.   *
*        THE PRIMARY PURPOSE OF THIS EXIT WAS THE IMPLEMENTATION      *
*        OF A JOB RESOURCE MANAGEMENT SUBSYSTEM.                      *
*                                                                     *
***********************************************************************
*
         L     R15,WANELPTR          LOAD THE NEL ADDRESS
         TM    NELOPSWT-NEL(R15),NELSMF TEST FOR SMF OPTION
         BNO   PPRNOUJV              SKIP IF NO SMF
         L     R15,CWAJMRPT          LOAD JMR ADDRESS
         USING JMR,R15
*
         LA    R1,JMRJOB             POINT AT JOB LOG
         ST    R1,JMRJOBP            SET IN PARAMETER LIST
         MVC   JMRJCLP,TEXTBUFP      SET POINTER TO INTERNAL TEXT
         LA    R1,JMRJCLCD           SET ADDRESS OF ENTRY CODE BYTE
         ST    R1,JMRJCLCP           STORE IN PARAMETER LIST
         MVI   JMRJCLCD,64           INTERNAL TEXT EXIT TYPE
         LA    R1,JMRPTRS            POINT AT PARAMETER LIST
*
         DROP  R15                   DONE WITH THE JMR
*
         SMFEXIT IEFUJV              INVOKE UJV EXIT
*
.UJVDONE ANOP
*
         EJECT
***********************************************************************
*                                                                     *
*        CLEAR EXECUTION TRACE INDICATORS                             *
*                                                                     *
***********************************************************************
*
         NI    PPWSTAT0,PPALL-PPW0VPP1-PPW0VPP2-PPW0EREC
*
         L     R13,4(,R13)         UNCHAIN SAVE AREAS
         LM    R14,R12,12(R13)     RELOAD SAVED REGISTERS
         BR    R14                 RETURN TO CALLER
*
PP12LITS LTORG ,                   FLUSH LITERALS
*
         TITLE '**** IEFVPP3 - JOB END CLEANUP ****'
***********************************************************************
*                                                                     *
* IEFVPP3                                                             *
* *******                                                             *
*                                                                     *
* THIS ROUTINE IS BRANCHED TO INSTEAD OF IEFVHF.  IF THIS IS NOT      *
* A CONVERTER TERMINATION CALL, CONTROL IS SIMPLY PASSED TO THE       *
* REAL IEFVHF (EXTERNAL REFERENCE IEFVHFX).  IF IT IS A TERMINATION   *
* CALL, THE FOLLOWING IS DONE BEFORE BRANCHING TO THE REAL IEFVHF.    *
*                                                                     *
*    1)  RESTORE ORIGINAL PROCLIB DCB ADDRESS.                        *
*    2)  CLOSE THE DYNAMIC PROCLIB DCB.                               *
*    3)  DE-CONCATENATE THE DYNAMIC AND SYSTEM PROCLIB DATASETS.      *
*    4)  DE-ALLOCATE THE DYNAMIC PROCLIB DATASETS.                    *
*    5)  FREEMAIN THE DYNAMIC PROCLIB WORK AREA.                      *
*    6)  RETURN TO CALLER OR ABORT IF ERROR.                          *
*                                                                     *
* INPUT:                                                              *
*        IWA(PDCBP)    -> JOBPROC DCB                                 *
*        IWA(IWAINTS4) -> TEMPORARY PROCLIB BUFFER                    *
*                                                                     *
* OUTPUT:                                                             *
*        REGISTERS RESTORED                                           *
*        IWA(PDCBP)    RESTORED FOR ORIG PROCLIB DCB                  *
*        IWA(IWAINTS4) RESTORED FOR ORIG PROCLIB BUFFER               *
*        IWA(PSTMT) RESTORED FOR ORIG PROCLIB BUFFER - COPY           *
*                                                                     *
* EXTERNALS:                                                          *
*        BRANCH TO IEFVHFX (ORIGINAL IEFVHF).                         *
*                                                                     *
* EXITS:                                                              *
*        NORMAL    - RETURN TO CALLER                                 *
*        ERROR     - ABORT                                            *
*                                                                     *
* SVC:                                                                *
*        ESTAE (SVC 60)                                               *
*        FREEMAIN (SVC 10)                                            *
*        CLOSE (SVC 20)                                               *
*        DYNALLOC (SVC 99)                                            *
*        ABEND (SVC 13)                                               *
*                                                                     *
***********************************************************************
         EJECT
IEFVPP3  DS    0H                  ENTRY POINT ADDRESS
         STM   R14,R12,12(R13)     SAVE CALLERS REGS
         L     R1,PPANCHOR         LOAD PP WORK AREA ADDRESS
*
         ST    R13,4(,R1)          STORE BACKWARD POINTER
         ST    R1,8(,R13)          STORE FORWARD POINTER
         LR    R13,R1              SET NEW SAVE AREA ADDRESS
*
         L     R11,PPWBASE1        SET UP MODULE 1ST BASE
         L     R10,PPWBASE2        SET UP MODULE 2ND BASE
*
         TM    PPWSTAT0,PPW0INIT   IS JOBPROC SUPPORT ENABLED?
         BZ    PP3LEAVE            IF NOT, SKIP CLEANUP
*
         OI    PPWSTAT0,PPW0VPP3   INDICATE IEFVPP3 IS EXECUTING
*
         L     R15,PP3TRCV         LOAD CONVERTER TRACE ROUTINE ADDRESS
         CNOP  2,4                 ALIGN TRACE PARM LIST
         BALR  R14,R15             ENTER MODULE ID IN TRACE REC.
PP3TRCV  DC    V(TRACE)            TRACE ROUTINE ID (IN IEFVH1)
         DC    CL4'VPP3'           MODULE ID FOR TRACE RECORD
*
         PPTRACE ENT3              TRACE ENTRY TO PP3
*
         NI    PPWTRPP2,PPALL-PPWT2PP2  RESET "IEFVPP2 ENTERED" FLAG
*
         EJECT
***********************************************************************
*                                                                     *
*        CLOSE THE DYNAMIC PROCLIB DATA SET AND FREEMAIN THE BUFFER   *
*                                                                     *
***********************************************************************
*
         TM    PPWSTAT1,PPW1OPEN   WAS DYNAMIC PROCLIB DCB OPENED ?
         BZ    PP3DCONC            NO, SKIP
         TM    PPWSTAT1,PPW1BUFR   WAS BUFFER GOTTEN ?
         BZ    PP3CLOSE            NO, SKIP
*
         L     R1,IWAINTS4         -> GOTTEN BUFFER
         LH    R0,DCBBLKSI-IHADCB+PPWDCB LOAD BUFFER LENGTH
         FREEMAIN RU,LV=(0),A=(1)  RELEASE IT
         NI    PPWSTAT1,PPALL-PPW1BUFR  CLEAR STATUS
         MVC   IWAINTS4+1(3),PPWSBUFA+1 RESET ORIG BUFFER ADDR
         MVC   PSTMT+1(3),PPWSBUFA+1    RESET ORIG BUFFER ADDR
*
PP3CLOSE DS    0H
         L     R3,PPWSDCBA         -> ORIGINAL PROCLIB DCB ADDRESS
         USING IHADCB,R3
         MVC   DCBEODAD+1(3),DCBEODAD+1-IHADCB+PPWDCB PASS ON ANY
         MVC   DCBSYNAD+1(3),DCBSYNAD+1-IHADCB+PPWDCB  CHANGES
         MVC   DCBREAD+1(3),DCBREAD+1-IHADCB+PPWDCB
         MVC   DCBPOINT+1(3),DCBPOINT+1-IHADCB+PPWDCB
         DROP  R3
*
         MVC   PDCBP+1(3),PPWSDCBA+1   RESTORE ORIGINAL DCB ADDR
         OI    PPWOCLST,PPVLBIT    SET VL FOR CLOSE PARMLIST
         CLOSE (PPWDCB),MF=(E,PPWOCLST) CLOSE THE FILE
         NI    PPWSTAT1,PPALL-PPW1OPEN  CLEAR OPEN STATUS
*
         PPTRACE CLS3              TRACE CLOSE IN PP3
*
         EJECT
***********************************************************************
*                                                                     *
*        DE-CONCATENATE THE PROCLIB DATA SETS IF MORE THAN ONE        *
*                                                                     *
***********************************************************************
*
PP3DCONC DS    0H
         TM    PPWSTAT1,PPW1JPCN   ANY CONCATENATIONS DONE ?
         BZ    PP3DALOC            NO, SKIP
*
         LA    R9,PPWDARB          -> DYNAMIC ALLOC REQUEST BLOCK
         USING S99RB,R9            GET DARB ADDRESSABILITY
         MVI   S99VERB,S99VRBDC    INDICATE DE-CONCAT REQUEST
         LA    R9,PPWDATXT         -> TEXT UNIT PTR SLOTS
*
         USING S99TUPL,R9          GET TEXT PTR ADDRESSABILITY
         USING S99TUNIT,R8         GET TEXT UNIT ADDRESSABILITY
*
         LA    R8,PPWCONCK         -> DE-CONCATENATE TEXT UNIT
         LA    R0,DDCDDNAM         GET DE-CONCATENATE KEY
         STH   R0,S99TUKEY         SET DE-CONCATENATE KEY
         LA    R1,1                GET THE NUMBER OF DDNAMES
         STH   R1,S99TUNUM         SET NO. OF DDNAMES
         ST    R8,S99TUPTR         SET PTR TO DE-CONC TEXT UNIT
         OI    S99TUPTR,S99TUPLN   FLAG END OF TEXT UNIT PTR'S
*
         LA    R1,PPWDARGL         -> DYNAMIC ALLOC PARM PTR
         OI    PPWSTAT0,PPW0DALC   INDICATE WE ARE IN ALLOC. CODE
         DYNALLOC ,                DE-CONCATENATE PROCLIB'S
         NI    PPWSTAT0,PPALL-PPW0DALC  INDICATE WE ARE BACK
         LTR   R15,R15             WAS DE-CONCATENATION OK ?
         BZ    *+8                 RETURN IF YES
         BAL   R14,PPDAERRA        DO ERROR ANALYSIS
*                                   AND MAYBE RETURN
         NI    PPWSTAT1,PPALL-PPW1JPCN  CLEAR CONCATENATION STATUS
*                                   AND MAYBE RETURN
         DROP  R8,R9
*
         PPTRACE DCN3              TRACE DECONCATENATION IN PP3
*
         EJECT
***********************************************************************
*                                                                     *
*        DE-ALLOCATE THE USER AND SYSTEM PROCLIB DATA SETS            *
*                                                                     *
***********************************************************************
*
PP3DALOC DS    0H
         LH    R0,PPWJPCNT         GET COUNT OF CONCATENATIONS
         LTR   R0,R0               DO WE HAVE ANYTHING TO FREE ?
         BZ    PP3NDALC            NO, GO RESET STATUS FLAGS
*
         LH    R2,PPWJPCNT         SET MAXIMUM TO DEALLOCATE
         LA    R9,PPWDARB          -> DYNAMIC ALLOC REQUEST BLOCK
         USING S99RB,R9            GET DARB ADDRESSABILITY
         MVI   S99VERB,S99VRBUN    INDICATE UNALLOC REQUEST
         LA    R9,PPWDATXT         -> TEXT UNIT PTR SLOTS
*
         USING S99TUPL,R9          GET TEXT PTR ADDRESSABILITY
         USING S99TUNIT,R8         GET TEXT UNIT ADDRESSABILITY
*
         LA    R8,PPWDDNMK         -> DDNAME TEXT UNIT
         LA    R0,DUNDDNAM         SET DDNAME TEXT KEY
         STH   R0,PPWDDNMK         SET DDNAME TEXT KEY
*
         ST    R8,S99TUPTR         SET PTR TO DDNAME TEXT UNIT
         OI    S99TUPTR,S99TUPLN   FLAG END OF TEXT UNIT PTR'S
*
PP3DEDDN DS    0H
         LR    R15,R2              GET THE CURRENT COUNTER
         BCTR  R15,0               SUBTRACT ONE
         MH    R15,=Y(L'S99TULEN+8)     GET OFFSET IN DDNAME LIST
         LA    R15,PPWCONCD(R15)   POINT TO ACTUAL NAME
         MVC   S99TUPAR(8),2(R15)  MOVE THE DDNAME
*
         LA    R1,PPWDARGL         -> DYNAMIC ALLOC PARM PTR
         OI    PPWSTAT0,PPW0DALC   INDICATE WE ARE IN ALLOC. CODE
         DYNALLOC ,                FREE A PROCLIB DATA SET
         NI    PPWSTAT0,PPALL-PPW0DALC  INDICATE WE ARE BACK
*
         LTR   R15,R15             DID DE-ALLOCATE GO OK ?
         BZ    PP3DENXT            YES, CONTINUE ON
         BAL   R14,PPDAERRA        NO, GO BOMB THE MOTHER
*
PP3DENXT BCT   R2,PP3DEDDN         DE-ALLOCATE ALL PROCLIB'S
*
         XC    PPWJPCNT,PPWJPCNT   RESET CONCATENATION COUNTER
         XC    PPWSTAT1(2),PPWSTAT1     CLEAR REMAINING STATUS FLAGS
*
         DROP  R8,R9
*
         PPTRACE DAL3              END OF DEALLOCATION
*
PP3NDALC DS    0H
*
         EJECT
***********************************************************************
*                                                                     *
*        PASS CONTROL TO THE REAL IEFVHF                              *
*                                                                     *
***********************************************************************
*
PP3LEAVE DS    0H
         TM    PPWMISC,PPWMESTA    TEST IF ESTAE ESTABLISHED
         BZ    PP3NSTAE            BRANCH IF NOT
         ESTAE 0                   CANCEL ERROR RECOVERY
         NI    PPWMISC,PPALL-PPWMESTA   TURN OFF ESTAE EXISTS FLAG
*
PP3NSTAE MVC   CWATXTEX,PPWTXTEX   RESTORE EXIT LIST ADDRESS
         XC    PPANCHOR,PPANCHOR   CLEAR ANCHOR WORD
*
         LR    R1,R13              LOAD THE WORK AREA ADDRESS
         L     R13,4(,R13)         UNCHAIN SAVE AREAS
*
         LA    R0,PPWORKLN         LOAD LENGTH OF WORK AREA
         FREEMAIN RU,A=(1),LV=(0)  FREE THE WORK AREA
*
         L     R15,PP3VHFX         LOAD THE REAL ADDRESS OF IEFVHF
         L     R14,12(,R13)        RELOAD SAVED REG 14
         LM    R0,R12,20(R13)      RELOAD REMAINING SAVED REGISTERS
         BR    R15                 AND BRANCH TO REAL IEFVHF
*
PP3VHFX  DC    V(IEFVHFX)          REFERENCE TO REAL IEFVHF ENTRY POINT
*
PP3LITRL LTORG ,                   FLUSH LITERALS
*
         TITLE '**** DYNAMIC ALLOCATION ERROR ANALYSIS ****'
***********************************************************************
*                                                                     *
* PPDAERRA                                                            *
* ********                                                            *
*                                                                     *
*     THIS ROUTINE ANALYZES ERRORS DETECTED BY THE OS/VS DYNAMIC      *
*  ALLOCATION ROUTINES. COMMON ERRORS WILL CAUSE A SPECIFIC ERROR     *
*  MESSAGE TO BE PRODUCED WHILE ALL OTHERS WILL CAUSE A WTP MESSAGE   *
*  GIVING A LESS PRECISE ERROR DESCRIPTION. IN THIS LATTER CASE       *
*  THE USER WILL ALSO RECEIVE MESSAGE XXX808I INFORMING HIM THAT      *
*  HE SHOULD EXAMINE THE WTP MESSAGE FOR ERROR DIAGNOSIS.             *
*                                                                     *
* INPUT:                                                              *
*        R15 = DYNAMIC ALLOCATION RETURN CODE (NOT = 0).              *
*                                                                     *
* OUTPUT:                                                             *
*        ERROR MESSAGE IS PRODUCED VIA PPMESSGE AND CONTROL IS PASSED *
*        TO THE CALLER VIA R14.                                       *
*                                                                     *
*        PPWDARB   - OUR DYNAMIC ALLOCATE PARAMETER BLOCK.            *
*        PPWDFRCD  - RETURN CODE HOLD AREA FOR DAIRFAIL(IKJEFF18)     *
*                                                                     *
* EXTERNALS:                                                          *
*        IKJEFF18 (DAIRFAIL SERVICE ROUTINE) - ISSUES WTP MESSAGE.    *
*                                                                     *
* EXIT:                                                               *
*        RETURN TO CALLER VIA R14.                                    *
*        ABEND THE CONVERTER BECAUSE OF UNRECOVERABLE ERROR.          *
*                                                                     *
* SVC:                                                                *
*        LINK (SVC 6)                                                 *
*        LOCATE (SVC 26)                                              *
*        WTO (SVC 35)                                                 *
*        ABEND (SVC 13)                                               *
*                                                                     *
***********************************************************************
         EJECT
PPDAERRA LA    R9,PPWDARB          -> DYNAMIC ALLOC REQUEST BLOCK
         USING S99RB,R9            MAKE DARB ADDRESSABLE
         ST    R15,PPWDFRCD        SET RETURN CODE FOR DAIRFAIL
         ST    R14,PPWDARET        SAVE RETURN ADDRESS
*
***********************************************************************
*                                                                     *
*        ANALYZE ALLOCATION TIME ERRORS                               *
*                                                                     *
***********************************************************************
*
         CLI   S99VERB,S99VRBAL    IS THIS AN ALLOCATION FAILURE
         BNE   PPDAERRC            NO, TRY CONCATENATION
         CLI   S99ERROR,LOCATE     IS THIS A CATALOG LOCATE ERROR
         BE    PPDER805            GO GIVE ERROR MESSAGE
         CLI   S99ERROR,OBTAIN     IS THIS AN OBTAIN ERROR ?
         BE    PPDER807            GO GIVE ERROR MESSAGE
         CLI   S99ERROR,DALRNOTA   IS THIS A "RESOURCE" ERROR ?
         BNE   PPDER808            NO, GIVE ERROR CODE IN MESSAGE
         CLI   S99ERROR+1,DALDSNEX EXCLUSIVE "DSN" REQUEST ?
         BE    PPDER813            YES, GIVE ERROR MESSAGE
         CLI   S99ERROR+1,DALDSNNA DATA SET CURRENTLY "IN USE" ?
         BE    PPDER813            YES, GIVE ERROR MESSAGE
         CLI   S99ERROR+1,DALUNITE UNIT NOT AVAILABLE ?
         BE    PPDER814            YES, GIVE ERROR MESSAGE
         CLI   S99ERROR+1,DALUNITI INVALID UNIT NAME ?
         BE    PPDER814            YES, GIVE ERROR MESSAGE
         CLI   S99ERROR+1,DALVOLNM VOLUME NOT MOUNTED ?
         BE    PPDER806            YES, GIVE ERROR MESSAGE
         CLI   S99ERROR+1,DALVOLNA VOLUME NOT AVAILABLE ?
         BE    PPDER806            YES, GIVE ERROR MESSAGE
         CLI   S99ERROR+1,DALCVOLE CVOL NOT MOUNTED ?
         BE    PPDER815            YES, GIVE ERROR MESSAGE
         B     PPDER808            GIVE GENERAL FAILURE MESSAGE
*
         EJECT
***********************************************************************
*                                                                     *
*        ANALYZE CONCATENATION ERRORS                                 *
*                                                                     *
***********************************************************************
*
PPDAERRC CLI   S99VERB,S99VRBCC    CONCATENATION FAILURE ?
         BE    PPDER809            GIVE ERROR MESSAGE
*
***********************************************************************
*                                                                     *
*        ANALYZE DE-CONCATENATION ERRORS                              *
*                                                                     *
***********************************************************************
*
PPDAERRD CLI   S99VERB,S99VRBDC    DE-CONCATENATION FAILURE ?
         BNE   PPDAERRR            NO, TRY DE-ALLOCATION
         CLC   S99ERROR,=X'0438'   DDNAME NOT FOUND ?
         BNE   PPDER808            NO, GIVE MESSAGE AND ABEND
         BR    R14                 RETURN -> IGNORE ERROR
*
***********************************************************************
*                                                                     *
*        ANALYZE DE-ALLOCATION ERRORS                                 *
*                                                                     *
***********************************************************************
*
PPDAERRR CLI   S99VERB,S99VRBUN    DE-ALLOCATION FAILURE ?
         BNER  R14                 IMPOSSIBLE ERROR -> RETURN
         CLC   S99ERROR,=X'0438'   DDNAME NOT FOUND ?
         BNE   PPDER808            NO, GIVE MESSAGE AND ABEND
         BR    R14                 RETURN TO CALLER
*
         EJECT
***********************************************************************
*                                                                     *
*        DYNAMIC ALLOCATION ERROR MESSAGE ROUTINES                    *
*                                                                     *
***********************************************************************
*
PPDER805 DS    0H
         LA    R2,PPMS805I         CATALOG SEARCH FAILED
         B     PPDAERRM
*
PPDER806 DS    0H
         LA    R2,PPMS806I         REQUIRED VOLUME NOT MOUNTED
         TM    PPWSTAT2,PPW2VSER+PPW2UNIT VOLSER AND UNIT HARD CODED ?
         BO    PPDVNMSG            BRANCH IF YES, NO NEED FOR LOCATE
*
         LOCATE PPWLOCPL           LOCATE REQUIRED VOLSER FROM CATLG
         LTR   R15,R15             LOCATE SUCCESSFUL ?
         TM    PPWSTAT2,PPW2VSER   WAS VOLSER CODED?
         BNZ   PPDVNMSG            BRANCH IF NOT
*
         LA    R15,PPWLOCWK+6      SET LOCATED VOLUME
         CLC   PPWLOCWK(2),=H'1'   MAKE SURE THERE IS ONLY ONE VOLUME
         BNE   PPDVNALW            BRANCH IF IT ISN'T
         TM    PPWLOCWK+2+2,UCB3DACC    TEST FOR DIRECT ACCESS DEVICE
         BZ    PPDVNALW            BRANCH IF NOT DASD
*
         TM    PPWLOCWK+2+1,UCBRVDEV    TEST FOR MSS DEVICE
         BZ    PPDVNMSG            BRANCH IF NOT MSS
*
PPDVNALW LA    R2,PPMS811I         VOLUME NOT ALLOW FOR JOBPROC
*
PPDVNMSG DS    0H
         B     PPDAERRM
*
PPDER807 DS    0H
         LA    R2,PPMS807I         JOBPROC DATA SET NOT ON VOLUME
         B     PPDAERRM
*
PPDER808 DS    0H
         LA    R1,PPWDFPRM         -> DAIRFAIL PARM LIST
         LINK  EP=IKJEFF18         ISSUE WTP FOR FAILURE
*
         L     R14,PPWDARET        RESTORE RETURN ADDR JUST IN CASE
*
         CLI   S99VERB,S99VRBDC    FAILURE DURING DE-CONCAT. ?
         BE    PPDER816            YES, ABEND THE CONVERTER
         CLI   S99VERB,S99VRBUN    FAILURE DURING DE-ALLOCATION ?
         BE    PPDER817            YES, ABEND THE CONVERTER
         LA    R2,PPMS808I         PROCLIB ALLOCATION FAILED
         B     PPDAERRM            ISSUE ERROR MESSAGE
*
PPDER809 DS    0H
         LA    R1,PPWDFPRM         -> DAIRFAIL PARM LIST
         LINK  EP=IKJEFF18         ISSUE WTP FOR FAILURE
*
         L     R14,PPWDARET        RESTORE RETURN ADDR JUST IN CASE
*
         LA    R2,PPMS809I         USER PROCLIB CONCATENATION ERR
         B     PPDAERRM
*
PPDER813 DS    0H
         LA    R2,PPMS813I         DATA SET NOT AVALIABLE EXCL'VE
         B     PPDAERRM
*
PPDER814 DS    0H
         LA    R2,PPMS814I         UNIT NOT AVAILABLE (INVALID ?)
         B     PPDAERRM
*
PPDER815 DS    0H
         LA    R2,PPMS815I         CATALOG VOLUME NOT MOUNTED
         B     PPDAERRM
*
PPDER816 WTO   MF=(E,PPMSDCNM)     DECONCATENATION FAILURE
         LA    R2,PPMS816I         GIVE USER ERROR MESSAGE TOO
         B     PPDAERRM            GO ISSUE ERROR MESSAGE
*
***********************************************************************
*                                                                     *
*        DEALLOCATION FAILURE                                         *
*                                                                     *
***********************************************************************
*
PPDER817 WTO   MF=(E,PPMSDALM)     DEALLOCATION FAILURE
*
***********************************************************************
*                                                                     *
*        ABEND THE CONVERTER                                          *
*                                                                     *
***********************************************************************
*
PPDABEND ABEND PPABEND,DUMP,,SYSTEM LET JES CLEANUP CONVERTER
*
         EJECT
***********************************************************************
*                                                                     *
*        DISPLAY ERROR MESSAGE                                        *
*                                                                     *
***********************************************************************
*
PPDAERRM BAL   R14,PPMESSGE        GIVE USER ERROR MESSAGE
*
         L     R14,PPWDARET        RESTORE RETURN ADDRESS
*
         BR    R14                 RETURN TO CALLER
*
LOCATE   EQU   X'17'               CATALOG LOCATE FAILED
OBTAIN   EQU   X'67'               DSCB OBTAIN FAILED
DALRNOTA EQU   X'02'               RESOURCE NOT AVAILABLE
DALDSNEX EQU   X'0C'                  UNABLE TO GET DATA SET EXCLUSIVE
DALDSNNA EQU   X'10'                  DATA SET IN USE
DALUNITE EQU   X'14'                  UNAVAILABLE UNIT SPECIFIED
DALVOLNM EQU   X'18'                  REQUIRED VOLUME NOT MOUNTED
DALUNITI EQU   X'1C'                  INVALID UNIT SPECIFIED
DALVOLNA EQU   X'20'                  VOLUME NOT AVAILABLE
DALCVOLE EQU   X'3C'                  CVOL NOT MOUNTED
*
         DROP  R9
*
PPDLITRL LTORG ,                   FLUSH LITERALS
*
         TITLE '**** ERROR MESSAGE ROUTINE ****'
***********************************************************************
*                                                                     *
* PPMESSGE                                                            *
* ********                                                            *
*                                                                     *
* THIS ROUTINE SETS THE JOB ERROR FLAG FOR THE CONVERTER AND PRINTS   *
* AN ERROR MESSAGE FOR THE USER.                                      *
*                                                                     *
* INPUT:                                                              *
*        R2 =  ADDRESS OF MESSAGE.                                    *
*                                                                     *
* OUTPUT:                                                             *
*        IWA(WAJOBPFX) SET TO INDICATE JCL ERROR.                     *
*        MESSAGE TO THE JCL MESSAGE DATA SET.                         *
*                                                                     *
* EXTERNALS:                                                          *
*        CONVERTER MESSAGE ROUTINE IEFVGM.                            *
*                                                                     *
* EXIT:                                                               *
*        RETURN TO CALLER OF THIS ROUTINE.                            *
*                                                                     *
* SVC:                                                                *
*        NONE                                                         *
*                                                                     *
***********************************************************************
         EJECT
PPMESSGE DS    0H
         ST    R14,PPWMSRET        SAVE CALLER'S RETURN ADDR
         OI    STRJINDC-TEXT+WAJOBPFX,JTXJOBFL SET JOB ERROR FLAG
         OI    PPWSTAT1,PPW1JPER   BYPASS ALL FURTHER PROCESSING
         OI    PPWSTAT0,PPW0VGM    INDICATE WE ARE IN PPMESSGE CODE
*
         L     R1,WAMSGBUF         LOAD ADDRESS OF MESSAGE BUFFER
         MVI   0(R1),C' '          BLANK THE BUFFER OUT
         MVC   1(159,R1),0(R1)
*
         SR    R14,R14             CLEAR FOR INSERT
         IC    R14,1(,R2)          GET LENGTH OF MESSAGE
         STH   R14,8(,R1)          STORE MESSAGE LENGTH
         BCTR  R14,0               DECREMENT FOR MVC
         EX    R14,PPMMVMSG        MOVE THE MESSAGE TEXT
*
         EJECT
***********************************************************************
*                                                                     *
*        CALL THE CONVERTER'S MESSAGE ROUTINE TO ISSUE THE MESSAGE    *
*                                                                     *
***********************************************************************
*
         SLR   R2,R2               ZERO MESSAGE NUMBER FOR IEFVGM
         OI    AOSW4,AOMSGTXT      INDICATE MESSAGE TEXT IN MSG AREA
*
         L     R15,PPMVGM          LOAD MESSAGE ROUTINE ADDRESS
         BALR  R14,R15             GO HAVE CONVERTER ISSUE MESSAGE
         NI    AOSW4,255-AOMSGTXT  CLEAR THIS MESSAGE FLAG
*
         NI    PPWSTAT0,PPALL-PPW0VGM   INDICATE COMPLETION OF PPMESSGE
         L     R14,PPWMSRET        RESTORE RETURN ADDRESS
         BR    R14                 RETURN TO CALLER
*
PPMMVMSG MVC   10(0,R1),2(R2)      MOVE MESSAGE TEXT - EXECUTE ONLY
*
         EJECT
PPMVGM   DC    V(IEFVGM)           CONVERTER MESSAGE ROUTINE
*
PPMLITRL LTORG ,                   FLUSH LITERALS
*
         TITLE '**** DEBUGGING TRACER OF LAST RESORT ****'
***********************************************************************
*                                                                     *
* PPTRACER                                                            *
* ********                                                            *
*                                                                     *
* THIS ROUTINE WILL PRODUCE A CONSOLE MESSAGE FOR TRACING IF THE      *
* "PPWMTRCE" FLAG IS ON.                                              *
*                                                                     *
* INPUT:                                                              *
*        R14 = ADDRESS OF FOUR BYTE TRACE ID.                         *
*                                                                     *
* OUTPUT:                                                             *
*        MESSAGE ***869I XXXX HAS BEEN ENCOUNTERED                    *
*        XXXX IS THE FOUR BYTE TRACE ID.                              *
*                                                                     *
* EXTERNALS:                                                          *
*        NONE.                                                        *
*                                                                     *
* EXIT:                                                               *
*        RETURN TO CALLER OF THIS ROUTINE TO FOUR AFTER REGISTER 14.  *
*                                                                     *
* SVC:                                                                *
*        WTO (SVC 35)                                                 *
*                                                                     *
***********************************************************************
*
PPTRACER DS    0H
         TM    PPWMISC,PPWMTRCE    SEE IF TRACING TURNED ON
         BNO   4(,R14)             RETURN PAST GOODIE
*
         STM   R14,R12,12(R13)     SAVE THE REGISTERS
         MVC   PPWTOWRK(PPMSTRML),PPMSTRMS SET THE WTO
         MVC   PPWTOWRK+12(4),0(R14)   SET THE TRACE CODE
         WTO   MF=(E,PPWTOWRK)     SO THE TERRIBLE WTO
         LM    R14,R12,12(R13)     RESTORE THE REGISTERS
         B     4(,R14)             RETURN TO CALLER
*
         TITLE '**** SYSTEM ERROR RECOVERY EXIT (ESTAE) ****'
***********************************************************************
*                                                                     *
* PPESTAEX                                                            *
* ********                                                            *
*                                                                     *
* THIS ROUTINE SETS THE JOB ERROR FLAG FOR THE CONVERTER AND PRINTS   *
* AN ERROR MESSAGE FOR THE USER.                                      *
*                                                                     *
* INPUT:                                                              *
*        R1 -> SYSTEM DIAGNOSTIC WORK AREA (SDWA).                    *
*                                                                     *
* OUTPUT:                                                             *
*        UPDATED SDWA FOR THE APPROPRIATE ERROR RECOVERY ROUTINE.     *
*        IWA(WAJOBPFX) SET TO INDICATE JCL ERROR.                     *
*                                                                     *
* EXTERNALS:                                                          *
*        NONE.                                                        *
*                                                                     *
* EXIT:                                                               *
*        NORMAL:  RETURN TO R/TM2 VIA SETRP MACRO.                    *
*        NO SDWA: RETURN TO R/TM2 WITH R15 = 0.                       *
*                                                                     *
* SVC:                                                                *
*        SDUMP (SVC 51) WHEN IN DEBUGGING MODE.                       *
*                                                                     *
* NOTES:                                                              *
*        IT IS HOPED THAT SDWAPARM CONTAINS A PTR TO THE CWA.         *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        PREPARE THE SDWA AND CHECK FOR UNRECOVERABLE ABENDS          *
*                                                                     *
***********************************************************************
*
         DROP  R11,R10             DROP THESE REGISTERS AROUND ESTAE
*
         USING PPESTAEX,R15        TEMPORARY BASE
PPESTAEX DS    0H
         C     R0,=F'12'           CHECK FOR NO SDWA
         BE    PPESTAEZ            GIVE UP IF NONE
         LTR   R1,R1               CHECK FOR NO SDWA
         BE    PPESTAEZ            GIVE UP IF NONE
*
         STM   R14,R12,12(R13)     SAVE R/TM REGISTERS
         LR    R11,R15             COPY THE BASE REGISTER
         USING PPESTAEX,R11
         DROP  R15
*
         LR    R3,R1               SET PTR TO SDWA
         USING SDWA,R3             ESTABLISH SDWA ADDRESSABILITY
*
         L     R12,SDWAPARM        ENSURE CWA ADDRESSABILITY
         CLC   CWAID,=X'11111111'  ENSURE THIS IS IN FACT THE CWA
         BNE   PPESFATL            NO, DO NOT RECOVER FROM ABEND
         ICM   R1,15,PPANCHOR      LOAD DYNAMIC PROCLIB WORK AREA
         BZ    PPESFATL            THIS IS DEADLY IF WE DON'T
*
         LA    R1,PPWESAVE-PPWDSECT(,R1) POINT AT ESTAE SAVE AREA
         ST    R13,4(,R1)          SAVE THE CURRENT SAVE AREA
         ST    R1,8(,R13)          CHAIN THIS TOO
         LR    R13,R1              ESTABLISH NEW SAVE AREA
         PUSH  USING               SAVE THE CURRENT ENVIRONMENT
         DROP  R13                 DROP THE NORMAL ADDRESSABILITY
         USING PPWESAVE,R13        ESTABLISH SPECIAL ADDRESSABILITY
*
         TM    PPWMISC,PPWMTRCE    TEST IF DEBUGGING IN FORCE
         BZ    PPESBYP             BRANCH IF NO DEBUGGING
         SDUMP MF=(E,PPESDUMP)     INVOKE A BIG DUMP
PPESBYP  DS    0H
*
         MVC   SDWARECP,PPESRECP   MOVE IN RECORDING PARMS
         MVI   SDWADPVA,SDWAHEX    DUMP USER DATA IN HEX
         MVI   SDWAURAL,PPWDCB-PPWSTAT0 MOVE IN LENGTH OF USER DATA
         MVC   SDWAVRA(PPWDCB-PPWSTAT0),PPWSTAT0 TRACE INFO FOR LOGREC
*
         OI    STRJINDC-TEXT+WAJOBPFX,JTXJOBFL FLUSH THE BAD JOB
         OI    PPWSTAT1,PPW1JPER   ALSO FLAG ERROR FOR US
*
         SR    R1,R1               CLEAR R1
         ICM   R1,3,SDWACMPC       R1 = ABEND CODE (XXX0)
         SRL   R1,4                DROP INSIGNIFICANT BITS
         CLM   R1,3,=Y(PPABEND)    IS THIS A SELF INFLICTED ABEND
         BE    PPESNREC            BRANCH IF YES
         TM    PPWSTAT0,PPW0EREC   IS THIS A RECURSIVE ABEND ?
         BO    PPESNREC            BRANCH IF SO
*
         EJECT
***********************************************************************
*                                                                     *
*        DETERMINE THE LOCATION OF THE ERROR                          *
*                                                                     *
***********************************************************************
*
PPESENVR OI    PPWSTAT0,PPW0EREC   INDICATE ERROR EXIT IN CONTROL
*
         L     R11,PPWBASE1        SET UP MODULE 1ST BASE
         L     R10,PPWBASE2        SET UP MODULE 2ND BASE
         USING IEFVPP,R11,R10      INFORM ASSEMBLER ABOUT IT
*
         TM    PPWSTAT0,PPW0VPP0   FAILURE DURING INITIALIZATION?
         BNO   PPESPP1             NO, CONTINUE
         MVI   SDWARECP+14,C'0'    INDICATE IEFVPP0 FAILED
         OI    SDWAACF2,SDWARCRD   INDICATE LOGREC RECORD REQ'D
         MVI   PPWSTAT0,0          DISABLE USER PROCLIB SUPPORT
*
         NI    STRJINDC-TEXT+WAJOBPFX,255-JTXJOBFL LET THE JOB GO
         LA    R4,PP0RETRN         SET RETRY FOR GRACEFUL EXIT
         B     PPESRTRN            RETURN TO R/TM
*
PPESPP1  TM    PPWSTAT0,PPW0VPP1   FAILURE IN JOBPROC PROCESSOR?
         BNO   PPESPP2             BRANCH IF NOT
         MVI   SDWARECP+14,C'1'    INDICATE IEFVPP1 FAILED
         OI    SDWAACF2,SDWARCRD   INDICATE LOGREC RECORD REQ'D
*
         LA    R4,PP12RETN         SET RETRY ADDRESS
         B     PPESRTRN            RETURN TO R/TM
*
PPESPP2  TM    PPWSTAT0,PPW0VPP2   FAILURE IN PROCLIB PREPARE?
         BNO   PPESPP3             BRANCH IF NOT
         MVI   SDWARECP+14,C'2'    INDICATE IEFVPP2 FAILED
*
         LA    R4,PP12RETN         GRACEFUL EXIT IF NOT OPEN ERR
         OI    SDWAACF2,SDWARCRD   INDICATE LOGREC RECORD REQ'D
*
         TM    PPWTRPP2,PPWT2OPN   IS PROCLIB OPEN IN PROGRESS?
         BNO   PPESRTRN            RETURN TO R/TM
*
         LA    R4,PP2TOPEN         SET RECOVERY PTR FOR OPEN ERR
         NI    SDWAACF2,PPALL-SDWARCRD NO RECORDING FOR USER ERR
         B     PPESRTRN            RETURN TO R/TM
*
PPESPP3  TM    PPWSTAT0,PPW0VPP3   FAILURE IN CLEANUP PROCESSING?
         BNO   PPESNREC            NO -> THINGS ARE IN BAD SHAPE
         MVI   SDWARECP+14,C'3'    INDICATE IEFVPP3 FAILED
*
         B     PPESNREC            DO NOT RECOVER FROM PP3 ERROR
*
         EJECT
***********************************************************************
*                                                                     *
*        RETURN TO RTM                                                *
*                                                                     *
***********************************************************************
*
PPESRTRN L     R13,4(,R13)         UNCHAIN THE SAVE AREA
*
         SETRP RC=4,RETADDR=(R4),FRESDWA=YES,WKAREA=(R3),              X
               REGS=(14,12)        RETURN TO R/TM
*
PPESNREC L     R13,4(,R13)         UNCHAIN THE SAVE AREA
*
PPESFATL DS    0H
         SETRP REGS=(14,12),RC=0,RECORD=YES, * CONTINUE ABEND *        X
               WKAREA=(R3)
*
PPESTAEZ DS    0H                  NO SDWA ADDRESS PROVIDED
         SLR   R15,R15             INDICATE NO RETRY
         BR    R14                  AND RETURN
*
         POP   USING               RESTORE PREVIOUS ADDRESSABILITY
*
         EJECT
PPESRECP DC    CL8'IEFVH1',CL8'IEFVPP?',CL8'PPESTAEX'
PPESHDR  DC    AL1(32),CL32'DYNAMIC PROCLIB ESTAE DUMP'
*
PPESDUMP SDUMP HDRAD=PPESHDR,      SVC DUMP PARAMETER LIST             X
               SDATA=(LSQA,PSA,RGN,TRT),                               X
               MF=L
*
PPELITRL LTORG ,                   FLUSH LITERALS
*
         TITLE '**** ALL USER AND OPERATOR ERROR MESSAGES ****'
      IEFVPPMS 800I,                                                   X
               'SYNTAX ERROR IN &DDNAME DD STATEMENT'
*
      IEFVPPMS 801I,                                                   X
               'ILLEGAL USE OF "SYSPROC" OPTION'
*
      IEFVPPMS 802I,                                                   X
               '&DDNAME DATA SET NAME NOT SPECIFIED'
*
      IEFVPPMS 803I,                                                   X
               'DATA SET NAME NOT SPECIFIABLE FOR &DDNAME'
*
      IEFVPPMS 804I,                                                   X
               '&DDNAME CONCATENATION LIMIT EXCEEDED'
*
      IEFVPPMS 805I,                                                   X
               '&DDNAME DATA SET NOT FOUND IN CATALOG'
*
      IEFVPPMS 806I,                                                   X
               'VOLUME CONTAINING &DDNAME DATA SET NOT MOUNTED'
*
      IEFVPPMS 807I,                                                   X
               '&DDNAME DATA SET NOT ON VOLUME'
*
      IEFVPPMS 808I,                                                   X
               '&DDNAME ALLOCATION FAILED'
*
      IEFVPPMS 809I,                                                   X
               '&DDNAME CONCATENATION FAILED'
*
      IEFVPPMS 810I,                                                   X
               'INSUFFICIENT STORAGE AVAILABLE TO ALLOCATE &DDNAME BUFFX
               ER'
*
      IEFVPPMS 811I,                                                   X
               'VOLUME OR DEVICE TYPE NOT ALLOWED FOR &DDNAME USAGE'
*
      IEFVPPMS 812I,                                                   X
               '&DDNAME OPEN FAILED'
*
      IEFVPPMS 813I,                                                   X
               '&DDNAME DATA SET NOT AVAILABLE UNDER REQUESTED DISPOSITX
               ION'
*
      IEFVPPMS 814I,                                                   X
               '&DDNAME DATA SET HAS INCORRECT UNIT SPECIFICATION'
*
      IEFVPPMS 815I,                                                   X
               'CATALOG VOLUME REQUIRED FOR &DDNAME PROCESSING IS NOT MX
               OUNTED'
*
      IEFVPPMS 816I,                                                   X
               '&DDNAME DE-CONCATENATION FAILED'
*
PPMSDCNM WTO   '&MSGPFX.816I &DDNAME DE-CONCATENATION FAILED',         X
               ROUTCDE=1,DESC=1,MF=L
PPMSDCNL EQU   *-PPMSDCNM
*
      IEFVPPMS 817I,                                                   X
               '&DDNAME DE-ALLOCATION FAILED'
*
PPMSDALM WTO   '&MSGPFX.817I &DDNAME DE-ALLOCATION FAILED',            X
               ROUTCDE=1,DESC=1,MF=L
PPMSDALL EQU   *-PPMSDALM
*
      IEFVPPMS 836I,                                                   X
               'MISPLACED &DDNAME STATEMENT'
*
      IEFVPPMS 842I,                                                   X
               'EXCESSIVE PARAMETER LENGTH IN THE SYSPROC FIELD'
*
      IEFVPPMS 843I,                                                   X
               'UNIDENTIFIED POSITIONAL PARAMETER IN THE SYSPROC FIELD'
*
PPMSYSMS WTO   '&MSGPFX.852I SYSPROC LIBRARIES UNAVAILABLE',           X
               ROUTCDE=11,DESC=6,MF=L
PPMSYSML EQU   *-PPMSYSMS
*
PPMSTRMS WTO   '&MSGPFX.869I **** HAS BEEN ENCOUNTERED',               X
               ROUTCDE=11,DESC=6,MF=L
PPMSTRML EQU   *-PPMSTRMS
*
         TITLE '**** IEFVHF - CONVERTER TERMINATION FRONT-END'
***********************************************************************
*                                                                     *
* IEFVHF                                                              *
* ******                                                              *
*                                                                     *
*        THIS ROUTINE RECEIVES CONTROL FROM ANY BRANCH TO IEFVHF.     *
*        IT SIMPLY BRANCHES TO IEFVPP3.  IT IS PACKAGED AS A          *
*        SEPARATE CSECT IN ORDER TO PERFORM THE PROPER CSECT          *
*        REPLACEMENT OF IEFVHF WHEN INSTALLED INTO LOAD MODULE        *
*        IEFVH1.                                                      *
*                                                                     *
***********************************************************************
*
IEFVHF   CSECT
IEFVHF   AMODE 24
IEFVHF   RMODE 24
*
         BALR  R11,0               ESTABLISH BASE - THERE MAY NOT BE
         USING *,R11               A USABLE ONE UPON ENTRY
*
         TM    AOSW4,CWATERM       IS THIS A TERMINATION CALL
         BZ    VHFLEAVE            BRANCH IF IT ISN'T
*
         ICM   R15,15,PPANCHOR     LOAD PP WORK AREA ADDRESS
         BZ    VHFLEAVE            BYPASS IF NEVER BUILT
*
         L     R15,VHFVPP3         LOAD IEFVPP3 ADDRESS
         BR    R15                 BRANCH TO IEFVPP3
*
VHFLEAVE L     R15,VHFVHFX         LOAD THE REAL IEFVHF ADDRESS
         BR    R15                 AND LET IT DO IT'S THING
*
VHFVPP3  DC    V(IEFVPP3)          DYNAMIC PROCLIB CLEANUP ROUTINE ADDR
VHFVHFX  DC    V(IEFVHFX)          ADDRESS OF REAL IEFVHF
*
         DROP  ,
*
         END   ,(IEFVPP,0324,91233)
