 /********************* WPASTE   ISPF EDIT MACRO *********************/
 /* WPASTE IS AN ISPF EDIT MACRO  TO  PASTE THE DATA CUT BY THE WCUT */
 /* EDIT MACRO.  THE WPASTE  MACRO  PASTES  OVER THE DATA WITHIN THE */
 /* WINDOW.  IT DOES NOT  INSERT  LINES.   TYPE  WCUT ON THE COMMAND */
 /* LINE AND PLACE THE CURSOR AT  THE BEGINNING OF THE AREA YOU WISH */
 /* THE BEGINNING IF THE CUT WINDOW  TO BE PASTED.  AS LITTLE AS ONE */
 /* CHARACTER CAN BE CUT OR PASTED.                                  */
 /*                                                                  */
 /*                                                                  */
 /* SYNTAX: WPASTE    {PLACE CURSOR WHERE PASTE IS TO BEGIN}         */
 /*                                                                  */
 /********************************************************************/

  ISREDIT MACRO (HELP)
       IF &STR(&HELP) = HELP THEN +
           DO
           ISPEXEC DISPLAY PANEL(WPASTE)
           EXIT
           END

       IF &STR(&HELP) = ? THEN +
           DO
           ISPEXEC DISPLAY PANEL(WPASTE)
           EXIT
           END

  ISPEXEC CONTROL ERRORS RETURN
  CONTROL ASIS
    SET &PPART1 = &STR()
    SET &PPART3 = &STR()
  SET &SPACES = &STR(``````````````````````````````````````````````````+
  `````````````````````)
  CONTROL MSG
  SET &WSVAR = WSVAR
  ISREDIT (LB,RB) = BOUNDS
  ISREDIT (ROW,COL) = CURSOR
  SET &WCPASR = &ROW
  SET &WCPASC = &COL
  SET &WCPASC = &WCPASC -1
  ISPEXEC VGET (WCCOLS WCROWS) PROFILE
  ISPEXEC VGET (WCCOLE WCROWE) PROFILE
  ISPEXEC VGET (WSLEN) PROFILE
  SET COPYLEN = &WSLEN
     SET &COPYLEN = &COPYLEN + 1
  SET &WSLEN = &EVAL(&WSLEN + &COL)
  SET &WSLEN = &WSLEN + 1
  SET &WS = &EVAL(&WCROWS)
  SET &LR = &EVAL(&WCROWE - &WCROWS)
  SET &WE =   &LR + &WS
  SET &PS = &EVAL(&WCPASR)
  ISREDIT LABEL &PS = .A
  SET &LC = &EVAL(&WCROWE - &WCROWS)
  SET &LC = &LC + &PS
  ISREDIT LABEL &LC = .B
  ISREDIT C X'50'  '@'   .ZFIRST .ZLAST ALL
PASLOOP: +
   ISREDIT (CURLINE) = LINE &PS
    IF &WCPASC = &LB | &WCPASC > &LB THEN +
    SET &PPART1 =  &SUBSTR(&LB:&WCPASC,&NRSTR(&CURLINE))
    ISPEXEC VGET (WSVAR&WS) PROFILE
     SET &PPART2 = &STR(&&WSVAR&WS)
     SET &PPART2 = &STR(&NRSTR(&PPART2&SPACES))
     SET &PPART2 = &SUBSTR(1:&COPYLEN,&STR(&PPART2))
     IF &RB > &WSLEN | &RB = &WSLEN THEN +
      SET &PPART3 = &SUBSTR(&WSLEN:&RB,&NRSTR(&CURLINE))
   ISREDIT LINE &PS = "&PPART1&PPART2&PPART3"
    SET &WS = &WS + 1
    SET &PS = &PS + 1
    IF &WS > &WE THEN GOTO FINISH
    GOTO PASLOOP
FINISH: +
  ISREDIT C '@'  '50'X  .ZFIRST .ZLAST ALL
  ISREDIT C X'79' X'40'  .A .B ALL
ISPEXEC VGET WCZPF12 PROFILE
SET &ZPF12 = &WCZPF12
ISPEXEC VPUT ZPF12 PROFILE
ISREDIT RESET
ISREDIT CURSOR = &ROW &COL
EXIT
