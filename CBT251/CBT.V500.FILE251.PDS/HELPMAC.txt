 /********************* HELPMAC  ISPF EDIT MACRO *********************/
 /* HELPMAC IS AN EDIT  MACRO  TO  BUILD  A  HELP ENTRY FOR AND EDIT */
 /* MACRO BASED ON  USING  THE  MACPEEK  MACRO.   HELPMAC CHECKS FOR */
 /* INPUT VARIABLES ON THE  ISREDIT  MACRO  STATEMENT.  IF FOUND THE */
 /* FIRST ONE IS USED TO  CHECK  FOR  REQUESTS FOR HELP.  IF NONE IS */
 /* FOUND, HELPMAC CREATES  AN  INPUT  VARIABLE  NAMED HELP.  IF THE */
 /* FIRST VARIABLE OF THE MACRO IS  HELP OR ?  THEN HELPMAC WILL USE */
 /* THE MACPEEK MACRO TO FIND  THE  FIRST  OCCURANCE OF THE MACRO IN */
 /* THE  SYSPROC  CONCATENATION.    IF   THE   AUTHOR  HAS  INCLUDED */
 /* DOCUMENTATION WITHIN THE MACRO  IT  IS  AVAILABLE TO VIEW.  THIS */
 /* ALSO ASSURES  THAT  THE  MACRO  BEING  VIEWED  IS  THE ONE BEING */
 /* EXECUTED.                                                        */
 /*                                                                  */
 /* SYNTAX: HELPMAC                                                  */
 /*                                                                  */
 /********************************************************************/

ISREDIT MACRO   (HELP)
    IF &HELP = HELP  THEN DO
     ISREDIT MACPEEK HELPMAC
    EXIT
    END
    IF &HELP = ?  THEN DO
     ISREDIT MACPEEK HELPMAC
    EXIT
    END
ISREDIT (MEM) = MEMBER
ISREDIT LOCATE 1
ISREDIT F 'ISREDIT MACRO'
ISREDIT (ROW,COL) =  CURSOR
ISREDIT (DATALINE) = LINE &ROW
ISREDIT LABEL &ROW = .X
ISREDIT FIND .X .X '('
ISREDIT (ROW,COL1) = CURSOR
ISREDIT FIND .X .X ','
SET &COMMA = &LASTCC
ISREDIT (ROW,COL2) = CURSOR
IF &COMMA ¬= 0 THEN DO
ISREDIT FIND .X .X ')'
SET &BRACCC = &LASTCC
IF &BRACCC ¬= 0 THEN GOTO NOVAR
ISREDIT (ROW,COL2) = CURSOR
END
GOTO PROCESS
NOVAR: +
ISREDIT F 'MACRO' .X .X
ISREDIT (ROW,COL) = CURSOR
SET &PART1 = &SUBSTR(1:&EVAL(&COL + 5),&DATALINE)
ISREDIT LINE &ROW = "&STR(&PART1 (HELP)"
SET &CMD = HELP
GOTO CMDBLD
PROCESS: +
SET &CMD = &SUBSTR(&EVAL(&COL1+1):&EVAL(&COL2-1),&NRSTR(&DATALINE))
CMDBLD: +
SET &CMD = &&&CMD
SET &L1 = &STR(    IF &STR(&&CMD) = HELP  THEN DO )
ISREDIT LINE_AFTER &ROW = "&NRSTR(&L1)"
SET &ROW = &ROW + 1
SET &L2 = &STR(     ISREDIT MACPEEK &MEM)
ISREDIT LINE_AFTER &ROW = "&NRSTR(&L2)"
SET &ROW = &ROW + 1
SET &L3 = &STR(    EXIT)
ISREDIT LINE_AFTER &ROW = "&NRSTR(&L3)"
SET &ROW = &ROW + 1
SET &L4 = &STR(    END)
ISREDIT LINE_AFTER &ROW = "&NRSTR(&L4)"
SET &ROW = &ROW + 1
SET &L5 = &STR(    IF &STR(&&CMD) = ?  THEN DO )
ISREDIT LINE_AFTER &ROW = "&NRSTR(&L5)"
SET &ROW = &ROW + 1
SET &L6 = &STR(     ISREDIT MACPEEK &MEM)
ISREDIT LINE_AFTER &ROW = "&NRSTR(&L6)"
SET &ROW = &ROW + 1
SET &L7 = &STR(    EXIT)
ISREDIT LINE_AFTER &ROW = "&NRSTR(&L7)"
SET &ROW = &ROW + 1
SET &L8 = &STR(    END)
ISREDIT LINE_AFTER &ROW = "&NRSTR(&L8)"
EXIT
