 /********************* MEMLSTX  ISPF EDIT MACRO *********************/
 /* MEMLSTX IS A SPECIAL PURPOSE MACRO CALL BY THE MEMDEL MACRO.  IT */
 /* PRODUCES A MEMBER LIST FOR  THE  IDENTIFICATION OF MEMBERS TO BE */
 /* DELETED.                                                         */
 /*                                                                  */
 /* THE MEMLSTX ISPF EDIT  MACRO  WILL  BUILD  A  MEMBER LIST OF THE */
 /* CURRENT PDS IN EDIT.   THE  RANGE  OF  MEMBERS  TO LISTED MAY BE */
 /* LIMITED BY INCLUDING  LIMITING  STRINGS  WHEN ISSUING THE MEMDEL */
 /* MACRO.  THE DEFAULT IS ALL MEMBERS.                              */
 /*                                                                  */
 /*                                                                  */
 /* SYNTAX: NONE                                                     */
 /*                                                                  */
 /********************************************************************/

 ISREDIT MACRO (FROM,TO) NOPROCESS
 ISPEXEC VGET (FROM,TO) SHARED
 ISREDIT (WORKDSN) = DATASET
 SET &Z9 = Z9999998
 SET &ZX = Z9999999
 IF &FROM = ? THEN DO
 ISPEXEC DISPLAY PANEL(MEMLIST)
 EXIT
 END
 IF &FROM ¬= THEN SET &RT = &STR(RANGE &FROM TO &TO)
 IF &TO = THEN SET &TO = &Z9
 SET &TARGET1 = &FROM
 SET T1 = &LENGTH(&TARGET1)
 IF &FROM =  THEN SET &T1 = 8
 SET &TARGET2 = &TO
 SET T2 = &LENGTH(&TARGET2)
SET  PDSDSN = &WORKDSN
SET &LPTR = 1
ISREDIT LINE_AFTER 0 =  +
" MEMBER(S) OF &WORKDSN REPLACE '_' WITH 'X' BEFORE MEMBER(S) TO DELETE"
/* INITIALIZE DATASET */
ISPEXEC  LMINIT DATAID(PDSID) DATASET('&PDSDSN') +
                ENQ(SHRW)                        +
                ORG(PDSORG)
SET LMRC = &LASTCC
IF &LMRC ¬= 0 THEN +
   DO
      WRITE LMINIT FAILED - RETURN CODE &LMRC
      WRITE DATA SET IS NOT ALLOCATED OR NOT PARTITIONED
      EXIT CODE(8)
   END

/* OPEN DATA SET */
ISPEXEC LMOPEN DATAID(&PDSID) OPTION(INPUT)
SET LMRC = &LASTCC

SET &MCTR = 1
DO WHILE (&LMRC = 0)
SET &MCTR = &MCTR + 1
  /* BUILD MEMBER LIST AND RETURN NEXT MEMBER */
  ISPEXEC LMMLIST DATAID(&PDSID) -
        OPTION(LIST) MEMBER(MEMBER) -
        STATS(NO)
  SET LMRC = &LASTCC
  SET &LM1 = &LMRC
IF &LMRC ¬= 0 THEN GOTO SETIT
     IF &SUBSTR(1:&T1,&MEMBER) GT &TARGET2 THEN SET &LM1 = X
  ISPEXEC LMMLIST DATAID(&PDSID) -
        OPTION(LIST) MEMBER(MEMBER2) -
        STATS(NO)
  SET LMRC = &LASTCC
  SET &LM2 = &LMRC
IF &LMRC ¬= 0 THEN GOTO SETIT
     IF &SUBSTR(1:&T1,&MEMBER2) GT &TARGET2 THEN SET &LM2 = X
  ISPEXEC LMMLIST DATAID(&PDSID) -
        OPTION(LIST) MEMBER(MEMBER3) -
        STATS(NO)
  SET LMRC = &LASTCC
  SET &LM3 = &LMRC
     IF &SUBSTR(1:&T1,&MEMBER3) GT &TARGET2 THEN SET &LM3 = X
IF &LMRC ¬= 0 THEN GOTO SETIT
  ISPEXEC LMMLIST DATAID(&PDSID) -
        OPTION(LIST) MEMBER(MEMBER4) -
        STATS(NO)
  SET LMRC = &LASTCC
  SET &LM4 = &LMRC
     IF &SUBSTR(1:&T1,&MEMBER4) GT &TARGET2 THEN SET &LM4 = X
IF &LMRC ¬= 0 THEN GOTO SETIT
  ISPEXEC LMMLIST DATAID(&PDSID) -
        OPTION(LIST) MEMBER(MEMBER5) -
        STATS(NO)
  SET LMRC = &LASTCC
  SET &LM5 = &LMRC
     IF &SUBSTR(1:&T1,&MEMBER5) GT &TARGET2 THEN SET &LM5 = X
IF &LMRC ¬= 0 THEN GOTO SETIT
  IF &LMRC = 0 THEN /* NOT END OF MEMBER LIST */ +
     DO
     IF &SUBSTR(1:&T1,&MEMBER) GE &TARGET1 AND +
      &SUBSTR(1:&T2,&MEMBER) LE &TARGET2  THEN DO
SETIT: +
IF &LM1 = 0 THEN +
SET &MEMLINE = +
&STR( _&MEMBER)
IF &LM2 = 0 THEN +
SET &MEMLINE = +
&STR( _&MEMBER    _&MEMBER2)
IF &LM3 = 0 THEN +
SET &MEMLINE = +
&STR( _&MEMBER    _&MEMBER2    _&MEMBER3)
IF &LM4 = 0 THEN +
SET &MEMLINE = +
&STR( _&MEMBER    _&MEMBER2    _&MEMBER3    _&MEMBER4)
IF &LM5 = 0 THEN +
SET &MEMLINE = +
&STR( _&MEMBER    _&MEMBER2    _&MEMBER3    _&MEMBER4    _&MEMBER5)
ISREDIT LINE_AFTER  &LPTR =  "&MEMLINE"
   SET &LPTR = &LPTR + 1
SET &MEMBER  = &ZX
SET &MEMBER2 = &ZX
SET &MEMBER3 = &ZX
SET &MEMBER4 = &ZX
SET &MEMBER5 = &ZX
SET &LM1 =
SET &LM2 =
SET &LM3 =
SET &LM4 =
SET &LM5 =
       END
     END
 END
/* FREE MEMBER LIST */
ISPEXEC LMMLIST DATAID(&PDSID) OPTION(FREE)
/* CLOSE DATA SET */
ISPEXEC LMCLOSE DATAID(&PDSID)
/* FREE DATA ID */
ISPEXEC LMFREE  DATAID(&PDSID)

EXIT CODE(0)
