 /********************* FPROCMAC ISPF EDIT MACRO *********************/
 /* FPROCMAC IS AN ISPF EDIT MACRO CALLED BY CLIST FPROCBLD TO EDIT  */
 /* INPUT OF 'SYS1.PROCLIB(JES2)' AND BUILD A LIST OF PROCS FOR THE  */
 /* FINDPROC CLIST. THIS EDIT MACRO ASSUMES SEVERAL DATA LOCATIONS,  */
 /* AND FOR BEST RESULTS CHECK 'ISREDIT F' STATEMENTS AND &SUBSTR    */
 /* STATEMENTS FOR BEST RESULTS.                                     */
 /*                                                                  */
 /*                                                                  */
 /* SYNTAX: NONE        {NOT FUNCTIONAL STANDALONE}                  */
 /*                                                                  */
 /********************************************************************/

ISREDIT MACRO
SET &PROC = PROC
SET &HINUM = HINUM
SET &AMPER = &STR(¦)
SET &PART1 = &STR(SET &AMPER&PROC&AMPER&HINUM =)
ISREDIT X ALL
ISREDIT F 'PROC' 12 ALL
ISREDIT DEL X ALL
ISREDIT X ALL
ISREDIT F '=' 18 ALL
ISREDIT DEL X ALL
ISREDIT C ''' '' ALL
ISREDIT C '/' ' ' ALL
ISREDIT C P'=======' '       ' 12 ALL
ISREDIT C ',' ' ' ALL
ISREDIT RESET
ISREDIT LOCATE .ZF
LOOP: +
ISREDIT (ROW,COL) = CURSOR
ISREDIT (WRKLINE) = LINENUM  &ROW
ISREDIT (DATALINE) = LINE &WRKLINE
SET &PART2 = &SUBSTR(18:45,&STR(&DATALINE))
ISREDIT LINE &WRKLINE = '&PART1&PART2'
ISREDIT LINE_AFTER &WRKLINE = 'SET &AMPER&HINUM = &AMPER&HINUM + 1'
ISREDIT F ' ' NEXT
ISREDIT F '.' NEXT
IF &LASTCC NE 0 THEN GOTO FINISH
GOTO LOOP
FINISH: +
ISREDIT DEL .ZL
ISREDIT C '¦' X'50' ALL
ISREDIT COPY FPROCTOP BEFORE .ZF
ISREDIT COPY FPROCBOT AFTER .ZL
ISREDIT REP .ZF .ZL FINDPROC
ISREDIT CANCEL
