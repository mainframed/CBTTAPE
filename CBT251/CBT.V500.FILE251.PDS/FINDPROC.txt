 /********************* FINDPROC TSO/E CLIST     *********************/
 /* FINDPROC IS A CLIST THAT WILL SEARCH THE JES PROCLIB             */
 /* CONCATENATION FINDING A TARGET MEMBER. SEVERAL OPTIONS ARE       */
 /* AVAILABLE:                                                       */
 /*                                                                  */
 /*  WRITE  - SHOW LIBRARY NAME AS IT IS SEARCHED                    */
 /*                                                                  */
 /*  QUIT   - QUIT AFTER FIRST OCCURANCE OF TARGET                   */
 /*                                                                  */
 /*  EDIT   - EDIT THE TARGET MEMBER WHEN FOUND                      */
 /*                                                                  */
 /*  BROWSE - BROWSE THE TARGET MEMBER WHEN FOUND                    */
 /*                                                                  */
 /*  THIS CLIST MUST BE UPDATED WITH THE PROCLIBS IN THE ORDER JES   */
 /*  SEARCHES THEM. THIS IS USUALLY IN THE JES2 PROC OR THE JES3     */
 /*  INISH DECK.                                                     */
 /*                                                                  */
 /*                                                                  */
 /* SYNTAX: FINDPROC PROCNAME {WRITE QUIT EDIT BROWSE}               */
 /*                                                                  */
 /********************************************************************/

PROC 1 MBR WRITE  QUIT EDIT BROWSE
     SET &PROC = PROC
     CLRSCRN
SET &HINUM = &HINUM + 1
SET &PROC&HINUM = SYS1.PROCLIB
SET &HINUM = &HINUM + 1
SET &PROC&HINUM = SYS2.PROCLIB
SET &HINUM = &HINUM + 1
SET &PROC&HINUM = SYS3.PROCLIB
SET &HINUM = &HINUM + 1
SET &PROC&HINUM = S99.H6.PROCLIB
SET &HINUM = &HINUM + 1
SET &PROC&HINUM = S99.COMMON.PROCLIB
SET &HINUM = &HINUM + 1
SET &PROC&HINUM = S99.SYSTEST.PROCLIB
WRITE SEARCHING SYSTEM PROCLIBS FOR PROC: &MBR
LOOP: +
SET &CTR = &CTR + 1
IF &CTR > &HINUM THEN GOTO OUT
SET &LIB = &&PROC&CTR
IF &WRITE =  WRITE THEN DO
WRITE  SEARCHING '&LIB'
END
IF &SYSDSN('&LIB(&MBR)') = OK THEN DO
IF &WRITE = EDIT |  +
   &QUIT  = EDIT |  +
   &BROWSE = EDIT |  +
   &EDIT  = EDIT THEN  DO
ISPEXEC EDIT DATASET('&LIB(&MBR)')
GOTO CHECKXIT
END
IF &WRITE = BROWSE | +
   &QUIT  = BROWSE | +
   &BROWSE = BROWSE | +
   &EDIT  = BROWSE THEN DO
ISPEXEC BROWSE DATASET('&LIB(&MBR)')
GOTO CHECKXIT
END
WRITE                           &MBR FOUND IN &LIB
CHECKXIT: +
IF &BROWSE = QUIT THEN EXIT
IF &WRITE = QUIT THEN EXIT
IF &QUIT = QUIT THEN EXIT
IF &EDIT = QUIT THEN EXIT
SET &FOUND = YES
END
GOTO LOOP
OUT: +
WRITE &HINUM SYSTEM PROCLIBS SEARCHED
IF &FOUND Â¬= YES THEN WRITE PROC &MBR NOT FOUND IN ANY JES2 PROCLIB
