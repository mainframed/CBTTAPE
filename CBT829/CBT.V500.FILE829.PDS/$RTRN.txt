         MACRO
&NAME    $RTRN &RC=0,&RENT=,&SP=1,&SPM=YES,&REST01=YES
         MNOTE *,'       $RTRN     VERSION 002 05/07/75  04/29/76  GPW'
         PRINT OFF,NOGEN
.**********************************************************************
.*                                                                    *
.* $RTRN                                                              *
.*                                                                    *
.* FUNCTION       GENERATE CODE REQUIRED TO RETURN CONTROL TO THE     *
.*                CALLING PROGRAM.                                    *
.*                                                                    *
.* DESCRIPTION    STANDARD IBM LINKAGE CODE IS GENERATED BY THE MACRO *
.*                TO RETURN CONTROL TO THE CALLING PROGRAM.  THE USER *
.*                MAY SPECIFY THE RETURN CODE TO BE RETURNED.         *
.*                RE-ENTRANT CODE IS SUPPORTED.                       *
.*                                                                    *
.*                REGISTERS 0 THROUGH 14 (OR OPTIONALLY, REGISTERS    *
.*                2 THROUGH 14) WILL BE RESTORED TO THEIR STATUS      *
.*                UPON ENTRY TO THE ROUTINE.  REGISTER 15 WILL BE     *
.*                USED FOR A DEFAULT OR USER SPECIFIED RETURN CODE.   *
.*                AN X'FF' WILL BE PLACED IN THE HIGH ORDER BYTE OF   *
.*                WORD 4 OF THE OLD SAVE AREA TO INDICATE THAT        *
.*                CONTROL HAS BEEN RETURNED TO THE CALLING PROGRAM    *
.*                                                                    *
.*                CODE IS ALSO GENERATED TO RESTORE THE PROGRAM MASK  *
.*                TO ITS STATUS UPON ENTRY TO THE ROUTINE.  THE       *
.*                VALUE OF THE MASK IS OBTAINED FROM THE HIGH ORDER   *
.*                BYTE OF REGISTER 14.  THE ASSUMPTION IS MADE THAT   *
.*                STANDARD LINKAGE WAS USED TO INVOKE THE ROUTINE,    *
.*                THAT IS, THAT ENTRY WAS MADE BY A  BALR  R14,R15.   *
.*                IF THIS IS NOT TRUE, THE PROGRAM MASK WILL BE       *
.*                RESTORED INCORRECTLY.  IF SPM=NO IS SPECIFIED, THE  *
.*                PROGRAM MASK WILL NOT BE ALTERED.                   *
.*                                                                    *
.*                IF RE-ENTRANT CODE IS BEING GENERATED, A DROP R2    *
.*                STATEMENT IS GENERATED, SO THE USER SHOULD NOT USE  *
.*                REGISTER 2 AS A BASE REGISTER.                      *
.*                                                                    *
.* SYNTAX         NAME     $RTRN     RC=NUM1                          *
.*                                      (R15)                         *
.*                                   RENT=LEN                         *
.*                                   SP=NUM2                          *
.*                                   SPM=NO                           *
.*                                   REST01=NO                        *
.*                                                                    *
.*                NAME   - A SYMBOLIC TAG ASSIGNED TO THE FIRST       *
.*                         INSTRUCTION GENERATED.                     *
.*                                                                    *
.*                RC     - SPECIFIES THE RETURN CODE TO BE PLACED IN  *
.*                         REGISTER 15.  A NUMBER MAY BE SPECIFIED,   *
.*                         OR (R15) INDICATES THAT THE RETURN CODE    *
.*                         HAS ALREADY BEEN PLACED IN REGISTER 15 BY  *
.*                         THE USER.  THE DEFAULT RETURN CODE IS 0.   *
.*                                                                    *
.*                RENT   - INDICATES THAT THE WORK AREA OBTAINED BY   *
.*                         $ENTER IS TO BE FREED.  LEN SPECIFIES THE  *
.*                         LENGTH OF THE AREA.  THE ADDRESS IS        *
.*                         OBTAINED FROM REGISTER 13.                 *
.*                                                                    *
.*                SP     - SPECIFIES THE SUBPOOL OF THE WORK AREA.    *
.*                         IT MUST AGREE WITH THE SUBPOOL SPECIFIED   *
.*                         IN $ENTER.  THE DEFAULT IS 1.              *
.*                                                                    *
.*                SPM    - SPM=NO SPECIFIES THAT THE PROGRAM MASK     *
.*                         IS NOT TO BE ALTERED.                      *
.*                                                                    *
.*                REST01 - REST01=NO SPECIFIES THAT THE CONTENTS      *
.*                         OF REGISTERS 0 AND 1 ARE NOT TO BE         *
.*                         RESTORED.  THIS OPTION SHOULD BE           *
.*                         SPECIFIED WHEN DATA IS TO BE PASSED        *
.*                         BACK TO THE CALLING PROGRAM IN             *
.*                         REGISTERS 0 AND 1.                         *
.*                                                                    *
.* ERRORS         NO ERROR MESSAGES ARE GENERATED                     *
.*                                                                    *
.* EXAMPLE        EX1      $RTRN                                      *
.*                                                                    *
.*                EX2      $RTRN  RC=4                                *
.*                                                                    *
.*                EX3      $RTRN  RC=(R15),RENT=DSECTLEN              *
.*                                                                    *
.* GLOBAL SYMBOLS                                                     *
.*                                                                    *
.*                NONE                                                *
.*                                                                    *
.* MACROS CALLED                                                      *
.*                                                                    *
.*                FREEMAIN                                            *
.*                                                                    *
.* UPDATE SUMMARY                                                     *
.*                                                                    *
.*      VERSION   DATE     CHANGE                                     *
.*                                                                    *
.*        002   04/29/76   REST01 KEYWORD ADDED                       *
.*                                                                    *
.**********************************************************************
.*
         AIF   ('&RENT' EQ '').REGULAR
&NAME    LR    R1,R13                   LOAD STORAGE ADDRESS
         BALR  R2,0                     ESTABLISH BASE REG
         USING *,R2                     DEFINE BASE REGISTER
         L     R3,4(R13)                SAVE OLD SAVEAREA ADDR
         LR    R4,R15                   SAVE RETURN CODE
.FREE    MNOTE *,' FREEMAIN R,LV=&RENT,A=(1),SP=&SP'
         FREEMAIN RU,LV=&RENT,A=(1),SP=&SP FREE STORAGE
* END FREEMAIN - $RTRN
         AIF   ('&RC' NE '(R15)').RESSAV
         LR    R15,R4                   RESTORE RETURN CODE
.RESSAV  LR    R13,R3                   RESTORE OLD SAVEAREA
         DROP  R2
         AGO   .COMMON
.REGULAR ANOP
&NAME    L     R13,4(R13)               RESTORE OLD SAVEAREA
.COMMON  AIF   ('&RC' EQ '(R15)').RES14
         LA    R15,&RC                  SET RETURN CODE
.RES14   L     R14,12(R13)              RESTORE RETURN ADDRESS
         AIF   ('&REST01' EQ 'NO').REST2
         LM    R0,R12,20(R13)           RESTORE REGS 0 - 12
         AGO   .SETFLAG
.REST2   LM    R2,R12,28(R13)           RESTORE REGS 2 - 12
.SETFLAG MVI   12(R13),X'FF'            SET RETURNED FLAG
.*-------TEST FOR SPM RESET
         AIF   ('&SPM' EQ 'NO').NOSPM
         SPM   R14                      RESTORE PROGRAM MASK
.NOSPM   BSM   0,R14                    RETURN  EHV FOR XA
         PRINT ON,GEN
         MEND
