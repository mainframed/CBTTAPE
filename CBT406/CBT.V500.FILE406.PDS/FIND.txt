FIND     CSECT
*
* WROTE BY: ED MOLNAR
*           COMPUTER DATA SYSTEMS, INC.
*           BETHESDA MD.
*
* UPDATED JUNE 18 2016 FOR UCB LOGIC AND 3380/3390, 4-DIGIT UNIT  .MOD.
* UPDATED CODE MARKED WITH .MOD. OR +.MOD. IF JUST COMMENT ADDED. .MOD.
*
*       THIS IS A DATA SET SEARCH ROUTINE.  THIS USES CP REQUESTS
*       THE DATA SET NAME AND DOES SOME STANDARD CKECKING.  IT
*       THE SEARCHES THE CATALOG TO FIND THE ENTRY.  AFTER THE
*       CATALOG IS SEARCHED IT THEN SEARCHES THE UCB ENTRIES AND
*       COMPARES THEM WITH OUR DASD TYPES.  THIS LIST IS INCORPRATED
*       WITHIN THE CODE AND MAY BE ADDED TO EASILY.  IT OBTAINS THE
*       VOLUME NAME AND CKECKS THE VTOC FOR THE FORMAT 1 DSCB. ALSO,
*       IF THERE IS A ALTERNATE PATH THERE IS A SECOND ADDRESS IN THE
*       TABEL POINTING TO THE DEVICE, THERFORE A SECOND SEARCH IS DONE
*       AND IF A 'HIT' IS MADE A SECOND MESSAGE IS PRODUCED.
*       A SECOND CHARATER TABLE IS PUT IN FOR THE CONVIENCE OF OTHERS.
*
         SAVE      (14,12),,FIND-&SYSTIME-&SYSDATE
         LR        12,15
         USING     FIND,12
         LA        11,SAVEAREA
         ST        11,8(13)
         ST        13,SAVEAREA+4
         LR        13,11
*----------------------- SOURCE CODE -------------------------------
         L         R8,4(R1)         GET UPT.
         L         R9,12(R1)        GET ECT.
***********************************************************************
*
*        CKECK THE COMMAND BUFFER FOR PARMS.
*
***********************************************************************
         L         R2,0(R1)         GET COMMAND BUFFER.
         LH        R3,0(R2)         GET LENGTH
         LH        R4,2(R2)         GET OFFSET
         LR        R5,R4            SAVE LENGTH OF COMMAND.
         BCTR      R5,0             SET UP FOR EXECUTE.
         EX        R5,CMD           SAVE COMMAND.
         SH        R3,=H'4'         SUBTRACT BYTES FOR FIRST WORD.
         SR        R3,R4            TAKE OFF COMMAND LENGTH.
         LA        R2,4(R2)         ADD 4 TO POINT PAST FIRST WORD.
         AR        R2,R4            POINT TO PARM FIELD.
         CH        R3,=H'0'         IS IT IN THE COMMAND BUFFER?
         BH        BLANKCK          YES,GO CKECK FOR BLANKS.
***********************************************************************
*
*        GET INFORMATION FROM PUTLINE.
*
***********************************************************************
NEEDDSN  DS        0H
         XC        MSG1,MSG1
         MVC       MSG1(19),=C'ENTER DATASET NAME:'
         BAL       R5,PUTMSG        BRANCH TO PUTLINE.
         BAL       R5,GETMSG        BRANCH TO GETLINE.
         LA        R2,GETBLOCK
         L         R2,4(R2)
         LH        R3,0(R2)         GET LENGTH
         LH        R4,2(R2)         GET OFFSET
         CH        R3,=H'4'         CHECK LENGTH.
         BE        NEEDDSN          FORGOT TO ENTER.
PARMLEN  SH        R3,=H'4'         TRUE LENGTH.
         LA        R4,4(R4)         TRUE OFFSET TO PARM.
         AR        R2,R4            ADD REG TO POINT TO PARM.
***********************************************************************
*
*        CKECK FOR PRECEEDING BLANKS.
*
***********************************************************************
BLANKCK  DS        0H
         CLI       0(R2),X'40'      BLANK?
         BNE       BLANKEND         NO,SKIP.
         LA        R2,1(R2)         ADD 1 TO PTR.
         BCT       R3,BLANKCK       CONTINUE ONWARD.
BLANKEND DS        0H
***********************************************************************
*
*        CKECK FOR ENDING BLANKS.
*
***********************************************************************
         LR        R4,R2            GET STARTING ADDRESS.
         LR        R5,R3            GET NUMBER OF BYTES.
         AR        R4,R3
         BCTR      R4,0             TAKE AWAY 1.
ENDCK    DS        0H
         CLI       0(R4),X'40'      IS LAST BYTE BLANK?
         BNE       ENDCKEND         NOPE.
         BCTR      R3,0             YEP,TAKE AWAY ONE FROM LENGTH.
         BCTR      R4,0             YEP,TAKE AWAY ONE FOR NEXT CHECK.
         BCT       R5,ENDCK         CKECK NEXT BYTE.
ENDCKEND DS        0H               END OF LOOP.
***********************************************************************
*
*        CKECK FOR VALID LENGTH.
*
***********************************************************************
LENGTH   DS        0H
         CH        R3,=H'1'         CKECK LENGTH.
         BL        NEEDDSN          TO SHORT,GO GET NAME.
         CH        R3,=H'44'        CHECK THE LENGTH AGAIN.
         BH        BADNAME          TO LONG,SEND MSG.
*                                   R3 NOW HAS THE LENGTH.
*                                   R2 NOW HAS ADDRESS OF REAL START.
***********************************************************************
*
*                  MAKE CAPS AND CKECK FOR VALID CHARACTERS.
*
***********************************************************************
         LR        R4,R2            GET STARTING ADDRESS.
         LR        R5,R3            GET NUMBER OF BYTES.
CHARCK0  DS        0H
         LA        R6,CHARE         GET NUMBER OF BYTES IN TABEL.
         LA        R7,CHARB         GET START OF TABEL.
         OI        0(R4),X'40'      MAKE CAPS.
CHARCK1  DS        0H
         CLC       0(1,R4),0(R7)    GOOD BYTE.
         BE        CHARCKE0         YES,NEXT BYTE.
         LA        R7,1(R7)         MOVE TO NEXT BYTE.
         BCT       R6,CHARCK1       NO,COMPARE NEXT ENTRY.
         B         BADNAME          NOT HERE,GO TELL THEM.
CHARCKE0 DS        0H
         LA        R4,1(R4)         ADD 1 FOR NEXT CHAR.
         BCT       R5,CHARCK0       GO SEARCH THE LIST.
***********************************************************************
*
*                  CHECK FIRST BYTE FOR ALPHA.
*
***********************************************************************
         LR        R4,R2            GET STARTING ADDRESS.
         LR        R5,R3            GET NUMBER OF BYTES.
ALPHA0   DS        0H
         LA        R6,CHARA         GET NUMBER OF ALPHA BYTES.
         LA        R7,CHARB         GET START OF TABEL.
ALPHA1   DS        0H
         CLC       0(1,R4),0(R7)    GOOD BYTE.
         BE        ALPHA2           MOVE ALONG.
         LA        R7,1(R7)         NO,MOVE TO NEXT BYTE.
         BCT       R6,ALPHA1        COMPARE NEXT ENTRY.
         B         BADNAME          END,GO GIVE MSG.
ALPHA2   DS        0H
         LA        R4,1(R4)         ADD 1 FOR NEXT CHAR.
         BCTR      R5,0             SUBTRACT ONE.
         CH        R5,=H'0'         LAST BYTE?
         BE        ALPHAE           YES,LEAVE.
         CLI       0(R4),X'4B'      IS IT A PERIOD?
         BNE       ALPHA2           NO,NEXT BYTE.
         BCTR      R5,0             SUBTRACT ONE.
         CH        R5,=H'0'         LAST BYTE?
         BE        BADNAME          OH NO CANT END IN PERIOD.
         LA        R4,1(R4)         OK, POINT TO NEXT CHAR.
         B         ALPHA0           GO CKECK FOR VALID CHAR.
ALPHAE   DS        0H
*
         BCTR      R3,0             REDUCE BY 1 FOR EXECUTE.
         B         MOVENAME         SKIP AROUND BADNAME ROUTINE.
***********************************************************************
*
*                  BAD NAME.
*
***********************************************************************
BADNAME  DS        0H
         XC        MSG1,MSG1        CLEAR MSG.
         MVC       MSG1(22),=C'INVALID DATA SET NAME:'
         BAL       R5,PUTMSG        TELL THE STORY.
         B         NEEDDSN          GO GET ANOTHER NAME.
***********************************************************************
*
*                  MOVE NAME IN ALL THE RIGHT PALCES.
*
***********************************************************************
MOVENAME DS        0H
         EX        R3,NAME          MOVE IN FOR LENGTH OF NAME.
         XC        MSG1,MSG1        CLEAR MSG.
         EX        R3,NAME2         MOVE NAME INTO MSG.
         BAL       R5,PUTMSG        SHOW NAME.
*
*
***********************************************************************
*
*                  SET UP AND SEARCH CATALOG.
*
***********************************************************************
         MVC       DDDSN(4),=XL4'44000000'
         MVC       DDDSN+8(4),=XL4'00000000'
*
*
         LOCATE    DDDSN
         LTR       R15,R15
         BNZ       NOTCAT
***********************************************************************
*
*                  GIVE THE RESULTS.
*
***********************************************************************
         XC        MSG1,MSG1
         MVC       MSG1(25),=C'DATASET CATALOGED ON VOL:'
         MVC       MSG1+26(6),VOLNAME
         BAL       R5,PUTMSG
         B         SERCHUCB
NOTCAT   DS        0H
         MVC       MSG1(22),=C'DATASET NOT CATALOGED:'
         BAL       R5,PUTMSG
***********************************************************************
*
*                  SEARCH ALL DASD VOLUMES FOR DATA SET.
*
*                  FIND UCB.
***********************************************************************
SERCHUCB DS      0H
         L       R5,16                     ADDRESS OF CVT
         TM      116(R5),X'80'             CVTMAP,CVTMVSE (XA)    .MOD.
         BZ      UCBNOTXA                                         .MOD.
         SR      R4,R4                     INDICATE XA            .MOD.
         XC      DEVWORK(100),DEVWORK                             .MOD.
NEXTXA   DS      0H                                               .MOD.
         UCBSCAN COPY,WORKAREA=DEVWORK,DEVCLASS=DASD,RANGE=ALL,   .MOD.X
               DEVNCHAR=DEVNCHAR,UCBAREA=DEVAREA,DYNAMIC=YES      .MOD.
         LTR     15,15                                            .MOD.
         BNZ     HOMEWARD                                         .MOD.
         LA      R6,DEVAREA                                       .MOD.
         TM      3(R6),X'80'               UCBSTAT,UCBONLI        .MOD.
         BZ      NEXTXA                    BRANCH IF NOT ONLINE   .MOD.
         B       CKDASD                                           .MOD.
UCBNOTXA L       R4,40(R5)                 CVTILK2                .MOD.
NEXTUCB  EQU     *
         CLC     0(2,R4),=X'FFFF'          LAST ENTRY?
         BE      HOMEWARD                  THATS ALL FOKES.
         ICM     R6,B'0011',0(R4)          LOAD UCB.
         CLI     2(R6),X'FF'               VALID ENTRY?
         BNE     ADDUCB                    NOPE,NEXT.
***********************************************************************
*
*                  CHECK FOR DEVTYPE.
*
***********************************************************************
CKDASD   DS      0H
         CLI     18(R6),X'20'              UCBTBYT3,UCB3DACC     +.MOD.
         BNE     ADDUCB
         CLI     28(R6),X'00'              UCBVOLI               +.MOD.
         BE      ADDUCB
         CLI     19(R6),X'0F'                                     .MOD.
         BNE     *+14                                             .MOD.
         MVC     DASD(6),=C'3390  '                               .MOD.
         B       GETNAME                                          .MOD.
         CLI     19(R6),X'0E'                                     .MOD.
         BNE     *+14                                             .MOD.
         MVC     DASD(6),=C'3380  '                               .MOD.
         B       GETNAME                                          .MOD.
         CLI     19(R6),X'06'
         BNE     *+14
         MVC     DASD(6),=C'2305  '
         B       GETNAME
         CLI     19(R6),X'07'
         BNE     *+14
         MVC     DASD(6),=C'2305  '
         B       GETNAME
         CLI     19(R6),X'09'
         BNE     *+14
         MVC     DASD(6),=C'3330  '
         B       GETNAME
         CLI     19(R6),X'0A'
         BNE     *+14
         MVC     DASD(6),=C'3340  '
         B       GETNAME
         CLI     19(R6),X'0B'
         BNE     *+14
         MVC     DASD(6),=C'3350  '
         B       GETNAME
         CLI     19(R6),X'0D'
         BNE     *+14
         MVC     DASD(6),=C'3330-1'
         B       GETNAME
         B       ADDUCB                    NEXT,NONE OF OUR DEVICES.
***********************************************************************
*
*                  CHECK NUMBER OF PATHS.
*
***********************************************************************
GETNAME  DS      0H
         MVC     VOLNAME,28(R6)             MOVE VOL IN.
         OBTAIN  DSCBNAME                   GO CHECK IT.
         LTR     R15,R15                    FOUND?
         BNZ     ADDUCB                     NOT HERE,NEXT.
***********************************************************************
*
*                  SET UP THE MESSAGE AND GIVE IT.
*
***********************************************************************
         MVC     STATUS,=CL30' '
         LTR     R4,R4                     TEST FOR XA            .MOD.
         BZ      STATUSX1                  SKIP OLD PRE-XA UCBCHM .MOD.
         MVC     STATUS,STATUS2             REBUILD STATUS LINE.
         TM      8(R6),B'10000000' PRIMARY CPU 0 NOT AVAILABLE
         BNO     *+10
         MVC     STATUS(6),=C'0 P NA'
         TM      8(R6),B'01000000' SECONDARY CPU 0 NOT AVAILABLE
         BNO     *+10
         MVC     STATUS+7(6),=C'0 S NA'
         TM      8(R6),B'00100000' PRIMARY CPU 1 NOT AVAILABLE
         BNO     *+10
         MVC     STATUS+14(6),=C'1 P NA'
         TM      8(R6),B'00010000' SECONDARY CPU 1 NOT AVAILABLE
         BNO     *+10
         MVC     STATUS+21(6),=C'1 S NA'
STATUSX1 DS      0H                                               .MOD.
         XC      MSG1,MSG1
         MVC     MSG1(12),=C'DEV ADDRESS:'
         MVC     MSG1+13(3),13(R6)         UCBNAME
         LTR     R4,R4                     TEST FOR XA            .MOD.
         BNZ     OKUNIT3                   BRANCH IF NOT XA       .MOD.
         MVC     MSG1+12(4),DEVNCHAR       COPY 4-CHAR UNIT ADDR  .MOD.
         CLI     MSG1+12,C'0'              REPLACE LEADING ZERO   .MOD.
         BNE     *+8                       WITH                   .MOD.
         MVI     MSG1+12,C' '               SPACE                 .MOD.
OKUNIT3  DS      0H                                               .MOD.
         MVC     MSG1+17(4),=C'VOL:'
         MVC     MSG1+22(6),28(R6)         UCBVOLI               +.MOD.
         MVC     MSG1+29(5),=C'TYPE:'
         MVC     MSG1+35(6),DASD
         LTR     R4,R4                     TEST FOR XA            .MOD.
         BZ      STATUSX2                  BRANCH IF XA           .MOD.
         MVC     MSG1+42(5),=C'PATH:'
         MVC     MSG1+48(30),STATUS
STATUSX2 DS      0H                                               .MOD.
         BAL     R5,PUTMSG
***********************************************************************
*
*                  SET UP FOR NEXT ENTRY.
*
***********************************************************************
ADDUCB   DS      0H
         LTR     R4,R4                     TEST FOR XA            .MOD.
         BZ      NEXTXA                                           .MOD.
         LA      R4,2(R4)
         B       NEXTUCB
***********************************************************************
*
*                  CLEAN UP AND HOMEWARD BOUND.
*
***********************************************************************
HOMEWARD DS        0H
*-------------------------------------------------------------------
         L         13,SAVEAREA+4
         ST        15,16(13)
         RETURN    (14,12),T
*
*
GETMSG   DS        0H
         GETLINE   PARM=GETBLOCK,UPT=(R8),ECT=(R9),ECB=ECBADS,         *
               MF=(E,IOPLADS)
         BR        R5
*
*
PUTMSG   DS        0H
         PUTLINE   PARM=PUTBLOCK,UPT=(R8),ECT=(R9),                    *
               ECB=ECBADS,OUTPUT=(PROMPT,TERM,SINGLE,DATA),            *
               MF=(E,IOPLADS)
         BR        R5
*
*
*------------------------ VARIALBELS -------------------------------
CMD      MVC     COMMAND(1),4(R2)
NAME     MVC     CAMNAME(1),0(R2)
NAME2    MVC     MSG1(1),0(R2)
DDDSN    CAMLST  NAME,CAMNAME,,VCB
DSCBNAME CAMLST  SEARCH,CAMNAME,VOLNAME,CAMFILL
CAMNAME  DC      CL44' '
VCB      DS      0D
VOLNUM   DS      CL2
DEVTYP   DS      CL4
VOLNAME  DS      CL6
DATASEQ# DS      CL2
CAMFILL  DS      CL251
COMMAND  DC      CL8' '
DASD     DS      CL6
STATUS   DS      CL30
STATUS2  DC      CL30'0 P AV 0 S AV 1 P AV 1 S AV'
DEVAREA  DS      12F                                              .MOD.
DEVWORK  DS      25F                                              .MOD.
DEVNCHAR DS      CL4                                              .MOD.
ECBADS   DC      F'0'
IOPLADS  DC      4F'0'
PROMPT   DS      0F
         DC      H'84',H'0'
MSG1     DC      CL80' '
PUTBLOCK PUTLINE MF=L
GETBLOCK GETLINE INPUT=(TERM,PHYSICAL),TERMGET=(EDIT,WAIT),MF=L
*-------------------------------------------------------------------
SAVEAREA DS      18F
BADCHARB DS      0H
         DC      X'4A'           ¢ CENT.
         DC      X'4C'           < LESS THAN.
         DC      X'4D'           ( LEFT PARREN.
         DC      X'4F'           | BAR.
         DC      X'50'           & ANPERSAN.
         DC      X'5A'           ! EXLAMATION.
         DC      X'5C'           * ASTERISK.
         DC      X'5D'           ) RIGHT PERREN.
         DC      X'5E'           ; SEMI COLON.
         DC      X'5F'           ¬ NOT SIGN.
         DC      X'60'           - DASH.
         DC      X'61'           / SLASH.
         DC      X'6A'           ¦ SPLIT BAR.
         DC      X'6B'           , CAMMA.
         DC      X'6C'           % PERCENT SIGN.
         DC      X'6D'           _ UNDER SCORE.
         DC      X'6E'           > GREATER THAN.
         DC      X'6F'           ? QUESTION MARK.
         DC      X'79'           ` APOSTRAPHY.
         DC      X'7A'           : COLON.
         DC      X'7D'           ' QUOTE.
         DC      X'7E'           = EQUAL.
         DC      X'7F'           " QUOTE.
         DC      X'A1'           ~ NOR.
         DC      X'C0'           { BRACKET.
         DC      X'D0'           } BRACKET.
         DC      X'E0'           \ BACK SLASH
         DC      X'FA'           | BAR.
BADCHARE EQU     *-BADCHARB
CHARB    DS      0H
         DC      X'4B'           . PERIOD.
         DC      X'5B'           $ DOLLAR SIGN.
         DC      X'7C'           @ AT SIGN.
         DC      X'7B'           # POUND SIGN.
         DC      X'C1'           A
         DC      X'C2'           B
         DC      X'C3'           C
         DC      X'C4'           D
         DC      X'C5'           E
         DC      X'C6'           F
         DC      X'C7'           G
         DC      X'C8'           H
         DC      X'C9'           I
         DC      X'D1'           J
         DC      X'D2'           K
         DC      X'D3'           L
         DC      X'D4'           M
         DC      X'D5'           N
         DC      X'D6'           O
         DC      X'D7'           P
         DC      X'D8'           Q
         DC      X'D9'           R
         DC      X'E2'           S
         DC      X'E3'           T
         DC      X'E4'           U
         DC      X'E5'           V
         DC      X'E6'           W
         DC      X'E7'           X
         DC      X'E8'           Y
         DC      X'E9'           Z
CHARA    EQU     *-CHARB
         DC      X'F0'           1
         DC      X'F1'           2
         DC      X'F2'           3
         DC      X'F3'           4
         DC      X'F4'           5
         DC      X'F5'           6
         DC      X'F6'           7
         DC      X'F7'           8
         DC      X'F8'           9
         DC      X'F9'           0
CHARE    EQU     *-CHARB
R0       EQU       0
R1       EQU       1
R2       EQU       2
R3       EQU       3
R4       EQU       4
R5       EQU       5
R6       EQU       6
R7       EQU       7
R8       EQU       8
R9       EQU       9
R10      EQU       10
R11      EQU       11
R12      EQU       12
R13      EQU       13
R14      EQU       14
R15      EQU       15
         LTORG
         END
