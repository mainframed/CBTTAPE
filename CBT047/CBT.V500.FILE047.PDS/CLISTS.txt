./       ADD   NAME=ASM
PROC 0
ISREDIT MACRO
ISREDIT (CHG) = DATA_CHANGED
IF &STR(&CHG) = &STR(YES) THEN ISREDIT SAVE
ISREDIT (CURDSN) = DATASET
ISREDIT (CURMEM) = MEMBER
SET &DSN = &CURDSN.(&CURMEM.)
ISPEXEC VPUT (DSN) SHARED
ISPEXEC DISPLAY PANEL(ISRFP01) CURSOR(FAL1)
SET &LIBCONC = &Z
IF &STR(&FAL1) ¬= &Z THEN -
SET &LIBCONC = &STR(,&FAL1)
IF &STR(&FAL2) ¬= &Z THEN -
SET &LIBCONC = &STR(&LIBCONC,&FAL2)
ASMH   '&DSN'    +
      PRINT('&PRJ1..&CURMEM..LIST')        +
      &FASM +
       LIB( 'SYS1.AMODGEN' 'SYS1.MACLIB' '&CURDSN' &LIBCONC )
%GENPRINT DSN(&CURMEM..LIST) NODEL DEOPT(K)
SET &DSN = &STR(TEMP)
ISPEXEC DISPLAY PANEL(ISRFP07) CURSOR(FSL1)
SET &LIBCONC = &Z
IF &STR(&FSL1) ¬= &Z THEN -
SET &LIBCONC = &STR(,&FSL1)
IF &STR(&FSL2) ¬= &Z THEN -
SET &LIBCONC = &STR(&LIBCONC,&FSL2)
LINK  TEMP                                +
      LOAD('TECTSG.ISPFJCL.LOAD(&CURMEM)')    +
      LIB('TECTSG.ISPFJCL.LOAD'              +
          &LIBCONC)                       +
      PRINT('&PRJ1..&CURMEM..LINKLIST')        +
           &FLNK
%GENPRINT DSN(&CURMEM..LINKLIST) NODEL DEOPT(K)
END
./       ADD   NAME=BOOK
PROC 0
ISREDIT MACRO (STYLE)
SET &FONT = &STR(X0G055AI)
IF &STR(&STYLE) = &STR() THEN SET &STYLE = &STR(FINAL)
ISREDIT (CHG) = DATA_CHANGED
IF &STR(&CHG) = &STR(YES) THEN ISREDIT SAVE
ISREDIT (CURDSN) = DATASET
ISREDIT (CURMEM) = MEMBER
IF &SYSDSN(&CURMEM..LIST3820) ¬= &STR(OK) +
THEN ALLOC DA(&CURMEM..LIST3820) NEW CATALOG +
             UNIT(DEVL) +
             LRECL(8205) BLKSIZE(8209) +
             RECFM(V B M)  DSORG(PS) +
             SPACE(1 15) TRACK
IF &CURMEM ¬= &Z THEN SET &DSN = &CURDSN.(&CURMEM.)
ELSE SET &DSN = &CURDSN
IF &STR(&STYLE) = &STR(OFFSET) THEN DO
SCRIPT '&DSN'                       +
        MESSAGE(ID)    INDEX +
        SYSVAR(U CENTURY  S OFFSET X NO) +
        DEVICE(3820A)              +
        FILE('&SYSUID..&CURMEM..LIST3820') +
        FO('SYS1.FONTLIBB') +
        CHARS(&FONT) +
        SEG('SYS1.PSEGLIB') +
        PROF('SCRIPT.R40.MACLIB(EDFPRF30)') +
        L('SCRIPT.R40.MACLIB')         +
        CONTINUE                    +
        BIND(10 10)               +
        FP(2)
END
ELSE DO
SCRIPT '&DSN'                       +
        MESSAGE(ID)    INDEX +
        SYSVAR(U CENTURY  S 1 X NO) +
        DEVICE(3820A)               +
        FILE('&SYSUID..&CURMEM..LIST3820') +
        FO('SYS1.FONTLIBB') +
        CHARS(&FONT) +
        SEG('SYS1.PSEGLIB') +
        PROF('SCRIPT.R40.MACLIB(EDFPRF30)') +
        L('SCRIPT.R40.MACLIB')         +
        CONTINUE                    +
        BIND(10 10)               +
        FP(2)
END
%GENPRINT DSN(&CURMEM..LIST3820) NODEL DEOPT(K)
END
./       ADD   NAME=BRIF
PROC 1 DSVSAM
SET TIME = &STR(&SUBSTR(1:2,&SYSTIME)&SUBSTR(4:5,&SYSTIME)+
           &SUBSTR(7:8,&SYSTIME))
SET DDVSAM = DD&TIME
ALLOC DD(&DDVSAM) DA(&DSVSAM) REU SHR
IF &LASTCC = 0 THEN DO
   ISPEXEC VPUT (DSVSAM,DDVSAM) SHARED
   ISPEXEC SELECT PGM(BRIFASM)
   FREE DD(&DDVSAM)
END
ELSE ISPEXEC SETMSG MSG(VSAM002)
SET &TABLE = E&SYSUID
ISPEXEC CONTROL ERRORS RETURN
ISPEXEC TBOPEN &TABLE WRITE
IF &LASTCC > 0 THEN EXIT
SET &EDBRDSNM = &DSVSAM
SET &LPR = &LENGTH(&EDBRDSNM)
SET &EDBRDSNM = &SUBSTR(2:&LPR-1,&EDBRDSNM)
IF &SYSDSN('&EDBRDSNM') = &STR(OK) +
THEN DO
   ISPEXEC   TBSCAN &TABLE ARGLIST(EDBRDSNM)
   IF &LASTCC = 8   THEN DO
      SET &EDBRLSTA = &STR(** BROWSE     )
      SET &EDBRLSTU = &STR(&SYSSDATE)
      LISTDSI '&EDBRDSNM' NORECALL
      SET &EDBRVOLS = &SYSVOLUME
      ISPEXEC  TBADD &TABLE ORDER
   END
END
ISPEXEC TBCLOSE &TABLE
END
./       ADD   NAME=CNAEBROW
PROC 1   DSN VOLUME()
SET FUNC = BROWSE
IF &STR(&DSN) = &STR(*) THEN DO
   ISPEXEC VGET (BRDSN BRMEM) PROFILE
   IF &BRMEM = THEN SET DSN = &STR('&BRDSN')
   ELSE SET DSN = &STR('&BRDSN(&BRMEM)')
END
SET &APOS = &STR(')
ISPEXEC VGET (ZPREFIX)
SET &LPR = &LENGTH(&ZPREFIX)
IF &LENGTH(&DSN) < &LPR THEN SET &LPR = &LENGTH(&DSN)
IF &SUBSTR(1,&DSN) ¬= &APOS THEN DO
   IF &SUBSTR(1:&LPR,&DSN) ¬= &ZPREFIX THEN +
   SET &DSN = &ZPREFIX..&DSN
   SET &DSN = &STR(&APOS&DSN&APOS)
END
LOOP:+
ISPEXEC CONTROL ERRORS RETURN
ISPEXEC BROWSE DATASET(&DSN) VOLUME(&VOLUME)
SET LCC = &LASTCC
IF &LCC ¬= 0 THEN DO
   ISPEXEC DISPLAY PANEL(BRDDSN) MSG(&ZERRMSG) CURSOR(DSN)
   IF &LASTCC = 0 THEN GOTO LOOP
END
SET &TABLE = E&SYSUID
ISPEXEC TBOPEN &TABLE WRITE
SET &EDBRDSNM = &DSN
SET &LPR = &LENGTH(&EDBRDSNM)
SET &EDBRDSNM = &SUBSTR(2:&LPR-1,&EDBRDSNM)
IF &SYSDSN('&EDBRDSNM') = &STR(OK) +
THEN DO
   ISPEXEC   TBSCAN &TABLE ARGLIST(EDBRDSNM)
   IF &LASTCC = 8   THEN DO
      SET &EDBRLSTA = &STR(** BROWSE     )
      SET &EDBRLSTU = &STR(&SYSSDATE)
      LISTDSI '&EDBRDSNM' NORECALL
      SET &EDBRVOLS = &SYSVOLUME
      ISPEXEC  TBADD &TABLE ORDER
   END
END
ISPEXEC TBCLOSE &TABLE
EXIT CODE(&LCC)
./       ADD   NAME=CNAEEDIT
PROC 1 DSN VOL() PASSWORD() MACRO() PROFILE()
SET FUNC = EDIT
IF &STR(&DSN) = &STR(*) THEN DO
   ISPEXEC VGET (BRDSN BRMEM) PROFILE
   IF &BRMEM = THEN SET DSN = &STR('&BRDSN')
   ELSE SET DSN = &STR('&BRDSN(&BRMEM)')
END
SET &APOS = &STR(')
ISPEXEC VGET (ZPREFIX)
SET &LPR = &LENGTH(&ZPREFIX)
IF &LENGTH(&DSN) < &LPR THEN SET &LPR = &LENGTH(&DSN)
IF &SUBSTR(1,&DSN) ¬= &APOS THEN DO
   IF &SUBSTR(1:&LPR,&DSN) ¬= &ZPREFIX THEN +
   SET &DSN = &ZPREFIX..&DSN
   SET &DSN = &STR(&APOS&DSN&APOS)
END
ISPEXEC SELECT PGM(EDREC)
IF &LASTCC = 20 THEN EXIT
LOOP: +
ISPEXEC CONTROL ERRORS RETURN
ISPEXEC EDIT DATASET(&DSN) +
        VOLUME(&VOL) +
        PASSWORD(&PASSWORD) +
        MACRO(&MACRO) +
        PROFILE(&PROFILE)
SET LCC = &LASTCC
IF &LCC > 4 THEN DO
   ISPEXEC DISPLAY PANEL(EDDDSN) MSG(&ZERRMSG) +
   CURSOR(DSN)
   IF &LASTCC = 0 THEN GOTO LOOP
END
SET &TABLE = E&SYSUID
ISPEXEC TBOPEN &TABLE WRITE
SET &EDBRDSNM = &DSN
SET &LPR = &LENGTH(&EDBRDSNM)
SET &EDBRDSNM = &SUBSTR(2:&LPR-1,&EDBRDSNM)
IF &SYSDSN('&EDBRDSNM') = &STR(OK) THEN DO
ISPEXEC TBSCAN &TABLE ARGLIST(EDBRDSNM)
   IF &LASTCC = 8   THEN DO
      SET &EDBRLSTA = &STR(** EDIT       )
      SET &EDBRLSTU = &STR(&SYSSDATE)
      LISTDSI '&EDBRDSNM' NORECALL
      SET &EDBRVOLS = &SYSVOLUME
      ISPEXEC  TBADD &TABLE ORDER
   END
END
ISPEXEC TBCLOSE &TABLE
EXIT CODE(&LCC)
./       ADD   NAME=DCF
PROC 0
ISREDIT MACRO (STYLE)
SET &FONT = &STR(X0G055AI)
IF &STR(&STYLE) = &STR() THEN SET &STYLE = &STR(FINAL)
ISPEXEC  VGET  (DCFHEAD DCFOFF DCFFP DCFINDEX DCFLDEV) PROFILE
IF &DCFHEAD =  THEN SET &DCFHEAD = &STR(N)
IF &DCFOFF =  THEN SET &DCFOFF = &STR(N)
IF &DCFFP =  THEN SET &DCFFP = &STR(1)
IF &DCFINDEX =  THEN SET &DCFINDEX = &STR(N)
IF &DCFLDEV =  THEN SET &DCFLDEV = &STR(3820A)
IF &STR(&STYLE) = &STR(SET) THEN DO
   ISPEXEC DISPLAY PANEL(DCFSET)
   ISPEXEC  VPUT  (DCFHEAD DCFOFF DCFFP DCFINDEX DCFLDEV) PROFILE
   GOTO EXIT
END
IF &DCFHEAD = Y THEN SET &SYSVARH = &STR(H YES )
ELSE SET &SYSVARH = &STR(H NO )
IF &DCFOFF = Y THEN SET &SYSVARS = &STR(S OFFSET )
ELSE SET &SYSVARS = &STR(S NO )
IF &DCFINDEX = Y THEN SET &IX = &STR(INDEX)
ELSE SET &IX = &STR()
ISREDIT (CHG) = DATA_CHANGED
IF &STR(&CHG) = &STR(YES) THEN ISREDIT SAVE
ISREDIT (CURDSN) = DATASET
ISREDIT (CURMEM) = MEMBER
IF &CURMEM ¬= &Z THEN SET &DSN = &CURDSN.(&CURMEM.)
ELSE SET &DSN = &CURDSN
IF &STR(&STYLE) = &STR(FINAL) THEN DO
SCRIPT '&DSN'                       +
        MESSAGE(ID)    &IX   +
        SYSVAR(&SYSVARH &SYSVARS  X NO) +
        DEVICE(&DCFLDEV)           +
        FILE('&SYSUID..&CURMEM..LIST3820') +
        FO('SYS1.FONTLIBB') +
        CHARS(&FONT) +
        SEG('SYS1.PSEGLIB') +
        PROF('SCRIPT.R40.MACLIB(DSMPROF4)') +
        L('SCRIPT.R40.MACLIB')         +
        CONTINUE                    +
        BIND(10 10)               +
        FP(&DCFFP)
   %GENPRINT DSN(&CURMEM..LIST3820) NODEL DEOPT(K)
END
ELSE IF &STR(&STYLE) = &STR(DRAFT) THEN DO
SCRIPT '&DSN'                       +
        MESSAGE(ID)    &IX   +
        SYSVAR(&SYSVARH &SYSVARS  X NO) +
        DEVICE(1403N6)              +
        FILE('&SYSUID..&CURMEM..LIST') +
        PROF('SCRIPT.R40.MACLIB(DSMPROF4)') +
        L('SCRIPT.R40.MACLIB')         +
        CONTINUE                    +
        BIND(10 10)               +
        FP(&DCFFP)
   %GENPRINT DSN(&CURMEM..LIST) NODEL DEOPT(K)
END
EXIT:+
END
./       ADD   NAME=DSNTBL
ISREDIT MACRO (D)
CONTROL NOFLUSH NOMSG
IF &D = D THEN DO
   CONTROL MSG L C S
   SET &ERR = CANCEL
END
ELSE SET &ERR = RETURN
ISPEXEC CONTROL ERRORS &ERR
ISREDIT (USTATE) = USER_STATE
SET &TABLE = DSN
SET &STABLE = STEP
SET &XFIELD = STEP
ISPEXEC TBCREATE &STABLE NOWRITE SHARE KEYS(XROW) NAMES(XSTEP)
ISPEXEC TBSORT   &STABLE FIELDS(XROW,N,D)
SET &RC = 0
DO WHILE (&RC = 0)
   ISREDIT FIND 'EXEC'
   SET &RC = &LASTCC
   ISREDIT (XROW) = CURSOR
   ISREDIT (DATA) = LINE &XROW
   SET &XSTEP = &SUBSTR(3:10,&DATA)
   IF &RC = 0 THEN ISPEXEC TBADD &STABLE ORDER
END
ISPEXEC TBCREATE &TABLE NOWRITE KEYS(DDNAME DSN STEP ROW COL) SHARE +
          NAMES(VOLUME DSORG)
ISPEXEC TBSORT   &TABLE FIELDS(STEP,C,A)
SET &RC = 0
ISREDIT CURSOR = 1 1
DO WHILE (&RC = 0)
   ISREDIT FIND 'DSN='
   SET &RC = &LASTCC
   IF &RC = 0 THEN DO
      SET &FOUND = 1
      ISREDIT (ROW,COL) = CURSOR
      ISPEXEC TBTOP &STABLE
      ISPEXEC TBVCLEAR &STABLE
      ISREDIT (XROW) = CURSOR
      ISPEXEC TBSARG &STABLE NAMECOND(XROW,LT)
      ISPEXEC TBSCAN &STABLE
      IF &LASTCC = 0 THEN SET &STEP = &NRSTR(&XSTEP)
      ISREDIT (L) = LINE &ROW
      SET &DD = &SUBSTR(3:&SYSINDEX(&STR( ),&STR(&L),3),&STR(&L))
      IF &STR(&DD) ¬= &STR() THEN SET &DDNAME = &STR(&DD)
      SET &CNT = &EVAL(&SYSINDEX(&STR(,),&STR(&NRSTR(&L)),&COL)-1)
      IF &CNT < 0 THEN +
       SET &CNT = &EVAL(&SYSINDEX(&STR( ),&STR(&NRSTR(&L)),&COL)-1)
      SET &DSN = &SUBSTR(&EVAL(&COL+4):&CNT,&STR(&L))
      IF &SUBSTR(1:1,&NRSTR(&DSN)) = &STR(') THEN +
      SET &DSN=&SUBSTR(2:&LENGTH(&DSN)-1,&NRSTR(&DSN))
      IF &SYSINDEX(&STR(,),&STR(&DSN)) = 1 THEN +
       SET &DSN = &STR(** CANNOT RESOLVE DATA SET NAME **)
       SET &VOLUME = &STR()
      IF &SUBSTR(1:1,&NRSTR(&DSN)) = &STR(&&)  THEN +
      SET &VOLUME = &STR()
      ELSE DO
          LISTDSI '&DSN' NORECALL
          IF &LASTCC = 16 THEN DO
             IF &SYSREASON = 0009 THEN SET &VOLUME = MIGRAT
             IF &SYSREASON = 0005 THEN SET &VOLUME = &STR(??????)
             IF &SYSREASON = 0001 THEN SET &VOLUME = &STR(??????)
          END
          ELSE DO
             SET &VOLUME = &SYSVOLUME
             SET &DSORG = &SYSDSORG
          END
      END
      ISPEXEC TBADD &TABLE ORDER
      IF &LASTCC > 8 THEN DO
         ISPEXEC SETMSG MSG(&ZERRMSG)
         EXIT CODE(12)
      END
   END
END
IF &FOUND = 1 THEN DO
   ISPEXEC TBTOP &TABLE
   ISPEXEC CONTROL DISPLAY SAVE
   SET &DRC = 0
   SET &LINE1 = &STR(B - BROWSE             L - LIST ATTR    )
   SET &LINE2 = &STR(E - EDIT              LC - LISTCAT      )
   SET &LINE3 = &STR(G - GOTO DSN IN FILE  HR - HRECALL      )
   DO WHILE (&DRC < 8)
      ISPEXEC TBDISPL &TABLE PANEL(&TABLE)
      SET &DRC = &LASTCC
      IF &DRC < 8 AND &ZCMD ¬= &STR() THEN DO
         IF &SYSINDEX(&STR(SORT),&ZCMD) = 1 THEN DO
            SET &PARM =+
       &SUBSTR(&SYSINDEX(&STR( ),&STR(&ZCMD))+1:&LENGTH(&ZCMD),&ZCMD)
         IF &PARM = STEP | &PARM = DDNAME | &PARM = DSN THEN DO
            ISPEXEC TBSORT &TABLE FIELDS(&PARM,C,A)
            SET &XFIELD = &PARM
         END
         ELSE DO
            SET &ZEDSMSG = &STR(INVALID SORT FIELD)
            SET &ZEDLMSG = &STR(INVALID SORT FIELD)
            ISPEXEC SETMSG MSG(ISRZ001)
         END
      END
      ELSE IF &SUBSTR(1:1,&ZCMD) = L THEN DO
         SET &PARM =+
         &SUBSTR(&SYSINDEX(&STR( ),&STR(&ZCMD))+1:&LENGTH(&ZCMD),&ZCMD)
         ISPEXEC TBVCLEAR &TABLE
         SET &&XFIELD = &STR(&PARM*)
         ISPEXEC TBSARG &TABLE NAMECOND(&XFIELD,GE)
         ISPEXEC TBSCAN &TABLE
      END
      ELSE DO
         SET &ZEDSMSG = &STR(INVALID COMMAND)
         SET &ZEDLMSG = &STR(INVALID COMMAND)
         ISPEXEC SETMSG MSG(ISRZ001)
      END
   END
   ELSE IF &DRC < 8 THEN DO
      SELECT (&S)
         WHEN (B) DO
            IF &DSORG = &STR(VS) THEN DO
               ISPEXEC SELECT CMD(%BRIF '&DSN')
            END
            ELSE DO
               ISPEXEC BROWSE DATASET('&DSN')
               IF &LASTCC > 8 THEN
               ISPEXEC SETMSG(&ZERRMSG)
            END
         END
         WHEN (E) DO
            IF &DSORG = &STR(VS) THEN DO
               ISPEXEC SELECT CMD(%EDIF '&DSN')
            END
            ELSE DO
               ISPEXEC EDIT DATASET('&DSN')
               IF &LASTCC > 8 THEN
               ISPEXEC SETMSG(&ZERRMSG)
            END
         END
         WHEN (L)  ISPEXEC SELECT CMD(LISTD '&DSN')
         WHEN (HR) ISPEXEC SELECT CMD(HRECALL '&DSN')
         WHEN (LC) ISPEXEC SELECT CMD(LISTC ENT('&DSN') ALL)
         WHEN (G) DO
            ISREDIT CURSOR = &ROW,&COL
            SET &DRC = 8
         END
         OTHERWISE DO
            SET &ZEDSMSG = &STR(INVALID COMMAND)
            SET &ZEDLMSG = &STR(INVALID COMMAND)
            ISPEXEC SETMSG MSG(ISRZ001)
         END
      END
   END
   END
   ISPEXEC CONTROL DISPLAY RESTORE
   ISPEXEC TBCLOSE &TABLE
   ISPEXEC TBCLOSE &STABLE
   IF &S ¬= G THEN ISREDIT USER_STATE = (USTATE)
END
ELSE DO
   SET &ZEDSMSG = &STR(NO DATASETS FOUND)
   SET &ZEDLMSG = &STR(NO DATASETS FOUND)
   ISPEXEC SETMSG MSG(ISRZ001)
END
./       ADD   NAME=EDIF
PROC 1 DSVSAM
SET TIME = &STR(&SUBSTR(1:2,&SYSTIME)&SUBSTR(4:5,&SYSTIME)+
           &SUBSTR(7:8,&SYSTIME))
SET DDVSAM = DD&TIME
ALLOC DD(&DDVSAM) DA(&DSVSAM) REU SHR
IF &LASTCC = 0 THEN DO
   ISPEXEC VPUT (DSVSAM,DDVSAM) SHARED
   ISPEXEC SELECT PGM(EDIFASM)
   FREE DD(&DDVSAM)
END
ELSE ISPEXEC SETMSG MSG(VSAM002)
END
./       ADD   NAME=EDX
PROC 0 DEBUG
ISPEXEC CONTROL ERRORS RETURN
CONTROL NOMSG NOFLUSH
IF &SF = THEN SET &SF = &STR(DSNAME)
IF &SD = THEN SET &SD = &STR(A)
SET &TABLE = E&SYSUID
ISPEXEC VPUT (SF SD) PROFILE
IF &DEBUG = DEBUG THEN CONTROL L C S MSG
  ISPEXEC  TBOPEN &TABLE            WRITE
  IF &LASTCC = 8 THEN DO
  ISPEXEC  TBCREATE  &TABLE    KEYS(EDBRDSNM) +
                     NAMES(EDBRVOLS EDBRLSTU EDBRLSTA) +
                     WRITE
  END
DISPLAY:+
  ISPEXEC TBDISPL &TABLE PANEL(EDBRPANL)
  SET &LCC = &LASTCC
RESTART:+
  IF &LCC >= 8 THEN GOTO EXIT
  ISPEXEC TBQUERY &TABLE POSITION(CRP)
  IF &OPT = &STR(ADD) +
  THEN DO
     ISPEXEC  TBVCLEAR &TABLE
     SET &EDBRLSTA = &STR(** ADD    )
     SET &EDBRLSTU = &STR(&SYSSDATE)
     ISPEXEC DISPLAY PANEL(EDBRADD)
     IF &LASTCC = 8 THEN GOTO DISPLAY
     IF &EDBRDSNM = &STR() +
     THEN DO
        SET &SYSOUTTRAP = 5000
        LISTC LEV(&EDBRLV)
        SET &SYSOUTTRAP = 0
        SET COUNT = 1
        SET &LIMIT = &SYSOUTLINE
        DO WHILE &COUNT <= &LIMIT
           SET &LINE = &&SYSOUTLINE&COUNT
           IF (&SUBSTR(1:7,&LINE) = NONVSAM) OR +
           (&SUBSTR(1:7,&LINE) = CLUSTER) +
           THEN DO
              SET EDBRDSNM = &SUBSTR(17:&LENGTH(&STR(&LINE)),&LINE)
              LISTDSI '&EDBRDSNM' NORECALL
              SET &LCC = &LASTCC
              IF &LCC = 16 && &SYSREASON = 0008 +
              THEN
              ELSE DO
                 IF &LCC = 16 && &SYSREASON = 0009 +
                 THEN SET &EDBRVOLS = MIGRAT
                 ELSE SET &EDBRVOLS = &SYSVOLUME
                 ISPEXEC  TBADD &TABLE ORDER
                 IF &LASTCC = 8 THEN  ISPEXEC  TBPUT &TABLE ORDER
              END
           END
           SET &COUNT = &COUNT + 1
        END
     END
     ELSE DO
        LISTDSI '&EDBRDSNM' NORECALL
        IF &LASTCC = 16 && &SYSREASON = 0009 +
        THEN SET &EDBRVOLS = MIGRAT
        ELSE DO
           IF &SYSDSN('&EDBRDSNM') ¬= &STR(OK) +
           THEN DO
              ISPEXEC SETMSG MSG(EDX004)
              GOTO DISPLAY
           END
           ELSE SET &EDBRVOLS = &SYSVOLUME
        END
        ISPEXEC  TBADD &TABLE                   ORDER
        IF &LASTCC = 8 THEN DO
           ISPEXEC  TBPUT &TABLE ORDER
        END
     END
  END
  ELSE IF &OPT = &STR(DROP)   +
  THEN DO
     ISPEXEC DISPLAY PANEL(EDBRDROP)
     IF &LASTCC = 8 THEN GOTO DISPLAY
     SET &DL = &LENGTH(&EDBRDLEV)
     ISPEXEC TBTOP &TABLE
     SET &DCC = 0
     SET &DCNT = 1
     DO WHILE (&DCC = 0)
        IF &SUBSTR(1:&DL,&STR(&EDBRDSNM)&STR(                    )) =+
        &STR(&EDBRDLEV) +
        THEN DO
           SET &DCNT = &DCNT + 1
           ISPEXEC  TBDELETE &TABLE
        END
        ISPEXEC  TBSKIP &TABLE
        SET &DCC = &LASTCC
     END
     ISPEXEC SETMSG MSG(EDX017)
  END
  ELSE IF &OPT = &STR(VERIFY) +
  THEN DO
     ISPEXEC TBTOP &TABLE
     SET &VCC = 0
     DO WHILE (&VCC = 0)
        LISTDSI '&EDBRDSNM' NORECALL
        SET &LDCC = &LASTCC
        IF &LDCC = 16 && &SYSREASON = 0009 THEN DO
           SET &EDBRVOLS = MIGRAT
           SET &EDBRLSTA = &STR(** VERIFY )
           ISPEXEC  TBPUT &TABLE ORDER
        END
        ELSE IF &LDCC = 16 && &SYSREASON = 0005 THEN DO
           ISPEXEC  TBDELETE &TABLE
        END
        ELSE DO
           SET &EDBRVOLS = &SYSVOLUME
           SET &EDBRLSTA = &STR(** VERIFY )
           SET &EDBRLSTU = &STR(&SYSSDATE)
           ISPEXEC  TBPUT &TABLE ORDER
        END
        ISPEXEC  TBSKIP &TABLE
        SET &VCC = &LASTCC
     END
     ISPEXEC SETMSG MSG(EDX017)
  END
  ELSE IF &LENGTH(&OPT) > 3 +
  THEN DO
     IF &SUBSTR(1:4,&STR(&OPT)) = &STR(SORT) +
     THEN DO
        SET &S = &SUBSTR(6:&LENGTH(&OPT),&STR(&OPT))
        IF &SUBSTR(1:6,&STR(&S)) = &STR(DSNAME) +
        THEN DO
           SET &SF = &STR(EDBRDSNM)
           SET &SD = &SUBSTR(&LENGTH(&S):&LENGTH(&S),&STR(&S))
        END
        ELSE IF &SUBSTR(1:6,&STR(&S)) = &STR(VOLSER) +
        THEN DO
           SET &SF = &STR(EDBRVOLS)
           SET &SD = &SUBSTR(&LENGTH(&S):&LENGTH(&S),&STR(&S))
        END
        ELSE IF &SUBSTR(1:5,&STR(&S)) = &STR(LSTUS) +
        THEN DO
           SET &SF = &STR(EDBRLSTU)
           SET &SD = &SUBSTR(&LENGTH(&S):&LENGTH(&S),&STR(&S))
        END
        ISPEXEC  TBSORT   &TABLE FIELDS(&SF C,&SD)
     END
  END
  IF &SELECT = &STR(B) +
  THEN DO
     LISTDSI '&EDBRDSNM' NORECALL
     IF &LASTCC = 16 THEN DO
        IF &SYSREASON = 0009 THEN DO
           SET &EDBRVOLS = MIGRAT
           ISPEXEC  TBPUT &TABLE ORDER
           ISPEXEC SETMSG MSG(EDX001)
        END
        ELSE IF &SYSREASON = 0005 THEN DO
           ISPEXEC SETMSG MSG(EDX013)
        END
     END
     ELSE DO
        SET &EDBRVOLS = &SYSVOLUME
        SET &EDBRLSTA = &STR(** BROWSE )
        SET &EDBRLSTU = &STR(&SYSSDATE)
        ISPEXEC  TBPUT &TABLE ORDER
        ISPEXEC CONTROL DISPLAY SAVE
        IF &SYSDSORG ¬= &STR(VS) THEN +
        ISPEXEC BROWSE DATASET('&EDBRDSNM')
        ELSE ISPEXEC SELECT CMD(%BRIF '&EDBRDSNM')
        ISPEXEC CONTROL DISPLAY RESTORE
     END
     GOTO ENDSEL
  END
  ELSE IF &SELECT = &STR(E) +
  THEN DO
     LISTDSI '&EDBRDSNM' NORECALL
     IF &LASTCC = 16 THEN DO
        IF &SYSREASON = 0009 THEN DO
           SET &EDBRVOLS = MIGRAT
           ISPEXEC  TBPUT &TABLE ORDER
           ISPEXEC SETMSG MSG(EDX001)
        END
        ELSE IF &SYSREASON = 0005 THEN DO
           ISPEXEC SETMSG MSG(EDX014)
        END
     END
     ELSE DO
        SET &EDBRVOLS = &SYSVOLUME
        SET &EDBRLSTA = &STR(** EDIT )
        SET &EDBRLSTU = &STR(&SYSSDATE)
        ISPEXEC  TBPUT &TABLE ORDER
        ISPEXEC CONTROL DISPLAY SAVE
        IF &SYSDSORG ¬= &STR(VS) THEN +
        ISPEXEC EDIT DATASET('&EDBRDSNM')
        ELSE ISPEXEC SELECT CMD(%EDIF '&EDBRDSNM')
        ISPEXEC CONTROL DISPLAY RESTORE
     END
     GOTO ENDSEL
  END
  ELSE IF &SELECT = &STR(P) +
  THEN DO
     LISTDSI '&EDBRDSNM' NORECALL
     IF &LASTCC = 16 && &SYSREASON = 0009 THEN DO
        SET &EDBRVOLS = MIGRAT
        ISPEXEC  TBPUT &TABLE ORDER
        ISPEXEC SETMSG MSG(EDX001)
     END
     ELSE DO
        SET &EDBRVOLS = &SYSVOLUME
        SET &EDBRLSTA = &STR(** PDS  )
        SET &EDBRLSTU = &STR(&SYSSDATE)
        ISPEXEC  TBPUT &TABLE ORDER
        ISPEXEC CONTROL DISPLAY SAVE
        PDS84 '&EDBRDSNM'
        ISPEXEC CONTROL DISPLAY RESTORE
     END
     GOTO ENDSEL
  END
  ELSE IF &SELECT = &STR(HL) +
  THEN DO
     ALLOC DA('&SYSUID..HL') NEW CATALOG +
             UNIT(DEVL) LRECL(121) BLKSIZE(1210) RECFM(F B A) +
             DSORG(PS) SPACE(1 1) TRACK
     HLIST DA('&EDBRDSNM') BOTH OUTDATASET('&SYSUID..HL')
     ISPEXEC BROWSE DATASET('&SYSUID..HL')
     DELETE '&SYSUID..HL'
     SET &EDBRLSTA = &STR(** HLIST )
     SET &EDBRLSTU = &STR(&SYSSDATE)
     ISPEXEC  TBPUT &TABLE ORDER
     GOTO ENDSEL
  END
  ELSE IF &SELECT = &STR(PR) +
  THEN DO
     LISTDSI '&EDBRDSNM' NORECALL
     IF &LASTCC = 16 && &SYSREASON = 0009 THEN DO
        SET &EDBRVOLS = MIGRAT
        ISPEXEC  TBPUT &TABLE ORDER
        ISPEXEC SETMSG MSG(EDX001)
     END
     ELSE DO
        ISPEXEC  SELECT CMD(%PRT '&EDBRDSNM')
        SET &EDBRVOLS = &SYSVOLUME
        SET &EDBRLSTA = &STR(** PRINT )
        SET &EDBRLSTU = &STR(&SYSSDATE)
        ISPEXEC  TBPUT &TABLE ORDER
     END
     GOTO ENDSEL
  END
  ELSE IF &SELECT = &STR(V) +
  THEN DO
     LISTDSI '&EDBRDSNM' NORECALL
     SET &LDCC = &LASTCC
     IF &LDCC = 16 && &SYSREASON = 0009 THEN DO
        SET &EDBRVOLS = MIGRAT
        ISPEXEC  TBPUT &TABLE ORDER
        ISPEXEC SETMSG MSG(EDX001)
     END
     ELSE IF &LDCC = 16 && &SYSREASON = 0005 THEN DO
        SET &EDBRVOLS = &STR(??????)
        SET &EDBRLSTA = &STR(** INFO )
        SET &EDBRLSTU = &STR(&SYSSDATE)
        ISPEXEC  TBPUT &TABLE ORDER
        ISPEXEC SETMSG MSG(EDX016)
     END
     ELSE DO
        SET &EDBRVOLS = &SYSVOLUME
        SET &EDBRLSTA = &STR(** VOLSER )
        SET &EDBRLSTU = &STR(&SYSSDATE)
        ISPEXEC  TBPUT &TABLE ORDER
     END
     GOTO ENDSEL
  END
  ELSE IF &SELECT = &STR(D) +
  THEN DO
     ISPEXEC  DISPLAY  PANEL(EDBRDELT)
     IF &LASTCC = 8 THEN GOTO ENDSEL
     DELETE '&EDBRDSNM'
     IF &LASTCC = 0 +
     THEN DO
        ISPEXEC  TBDELETE &TABLE
        ISPEXEC SETMSG MSG(EDX005)
     END
     ELSE ISPEXEC SETMSG MSG(EDX006)
     GOTO ENDSEL
  END
  ELSE IF &SELECT = &STR(A) +
  THEN DO
     LISTDSI '&EDBRDSNM' NORECALL DIRECTORY
     IF &LASTCC ¬= 0 THEN GOTO ENDSEL
     SET &EDBRBLK  = &SYSBLKSIZE
     SET &EDBRPRIM = &SYSPRIMARY
     SET &EDBRSECQ = &SYSSECONDS
     SET &EDBRDIRB = &SYSADIRBLK
     SET &EDBRVOLS = &SYSVOLUME
     SET &EDBRMDSN = &EDBRDSNM
     SET &EDBRDSNM = &STR(')&EDBRDSNM&STR(')
     ISPEXEC  DISPLAY  PANEL(EDBRMODL)
     IF &LASTCC = 8 THEN GOTO ENDSEL
     IF &SYSRECFM = &STR(FB) +
     THEN SET &SYSRECFM = &STR(F B)
     ELSE IF &SYSRECFM = &STR(VB) +
     THEN SET &SYSRECFM = &STR(V B)
     ELSE IF &SYSRECFM = &STR(FBA) +
     THEN SET &SYSRECFM = &STR(F B A)
     ELSE IF &SYSRECFM = &STR(VBA) +
     THEN SET &SYSRECFM = &STR(V B A)
     IF &SYSUNITS = &STR(BLOCK) +
     THEN SET &SYSUNITS = &STR(BLOCK(&EDBRBLK))
     IF &SYSDSORG = &STR(PO) THEN +
     ALLOC DA('&EDBRDSNM') NEW CATALOG +
         UNIT(&SYSUNIT) VOL(&EDBRVOLS) +
         LRECL(&SYSLRECL) BLKSIZE(&EDBRBLK) +
         RECFM(&SYSRECFM) DIR(&EDBRDIRB) DSORG(&SYSDSORG) +
         SPACE(&SYSPRIMARY &SYSSECONDS) &SYSUNITS
     ELSE +
     ALLOC DA('&EDBRDSNM') NEW CATALOG +
         UNIT(&SYSUNIT) VOL(&EDBRVOLS) +
         LRECL(&SYSLRECL) BLKSIZE(&EDBRBLK) +
         RECFM(&SYSRECFM)  DSORG(&SYSDSORG) +
         SPACE(&SYSPRIMARY &SYSSECONDS) &SYSUNITS
     IF &LASTCC = 0 +
     THEN ISPEXEC SETMSG MSG(EDX007)
     ELSE DO
        ISPEXEC SETMSG MSG(EDX008)
        GOTO ENDSEL
     END
     SET &EDBRLSTA = &STR(** ALLOC  )
     SET &EDBRLSTU = &STR(&SYSSDATE)
     ISPEXEC  TBADD &TABLE                   ORDER
     GOTO ENDSEL
  END
  ELSE IF &SELECT = &STR(X) +
  THEN DO
     ISPEXEC  TBDELETE &TABLE
     GOTO ENDSEL
  END
  ELSE IF &SELECT = &STR(LB) +
  THEN DO
     ISPEXEC SELECT CMD(%LB '&EDBRDSNM')
     SET &EDBRLSTA = &STR(** LBCK )
     SET &EDBRLSTU = &STR(&SYSSDATE)
     ISPEXEC  TBPUT &TABLE                   ORDER
     GOTO ENDSEL
  END
  ELSE IF &SELECT = &STR(G) +
  THEN DO
     ISPEXEC SELECT CMD(%G '&EDBRDSNM')
     SET &EDBRLSTA = &STR(** INFO-G )
     SET &EDBRLSTU = &STR(&SYSSDATE)
     ISPEXEC  TBPUT &TABLE                   ORDER
     GOTO ENDSEL
  END
  ELSE IF &SELECT = &STR(HM) +
  THEN DO
     HMIGRATE '&EDBRDSNM' NOWAIT
     ISPEXEC SETMSG MSG(EDX002)
     SET &EDBRVOLS = MIGRAT
     SET &EDBRLSTA = &STR(** MIGRATE )
     SET &EDBRLSTU = &STR(&SYSSDATE)
     ISPEXEC  TBPUT &TABLE ORDER
     GOTO ENDSEL
  END
  ELSE IF &SELECT = &STR(HR) +
  THEN DO
     HRECALL  '&EDBRDSNM' NOWAIT
     ISPEXEC SETMSG MSG(EDX003)
     SET &EDBRVOLS = ONLINE
     SET &EDBRLSTA = &STR(** RECALL )
     SET &EDBRLSTU = &STR(&SYSSDATE)
     ISPEXEC  TBPUT &TABLE ORDER
  END
  ELSE IF &SELECT = &STR(?) +
  THEN DO
     LISTDSI '&EDBRDSNM' NORECALL
     IF &LASTCC = 16 THEN DO
        IF &SYSREASON = 0009 THEN DO
           SET &EDBRVOLS = MIGRAT
           ISPEXEC  TBPUT &TABLE ORDER
           ISPEXEC SETMSG MSG(EDX001)
        END
        ELSE IF &SYSREASON = 0005 THEN DO
           ISPEXEC SETMSG MSG(EDX015)
        END
     END
     ELSE DO
        SET &EDBRBLK  = &SYSBLKSIZE
        SET &EDBRPRIM = &SYSPRIMARY
        SET &EDBRSECQ = &SYSSECONDS
        SET &EDBREXT  = &SYSEXTENTS
        SET &EDBRVOLS = &SYSVOLUME
        SET &EDBRCDTE = &STR(&SYSCREATE)
        SET &EDBREFDT = &STR(&SYSREFDATE)
        SET &EDBRUPDT = &SYSUPDATED
        ISPEXEC  DISPLAY  PANEL(EDBRINFO)
        SET &EDBRLSTA = &STR(** INFO )
        SET &EDBRLSTU = &STR(&SYSSDATE)
        ISPEXEC  TBPUT &TABLE ORDER
     END
  END
 ENDSEL:+
 ISPEXEC TBTOP  &TABLE
 ISPEXEC TBSKIP &TABLE NUMBER(&ZTDTOP)
 ISPEXEC VGET   (ZVERB ZSCROLLN)
 SELECT
  WHEN (&ZVERB = 'UP') THEN ISPEXEC TBSKIP &TABLE NUMBER(-&ZSCROLLN)
  WHEN (&ZVERB = 'DOWN') THEN ISPEXEC TBSKIP &TABLE NUMBER(&ZSCROLLN)
  OTHERWISE
 END
  IF &LCC = 4 THEN DO
     ISPEXEC TBDISPL &TABLE  PANEL(EDBRPANL)
     SET &LCC = &LASTCC
     GOTO RESTART
  END
  ELSE GOTO DISPLAY
EXIT:+
ISPEXEC TBCLOSE &TABLE
END
./       ADD   NAME=FONTHELP
ISREDIT MACRO
%FONTTABL  EDIT
./       ADD   NAME=FONTMAPS
PROC 0 DEBUG NOSUB

/**********************************************************************/
/* THIS CLIST IS DESIGNED TO READ THE ADVANCED FUNCTION PRINTING FONT */
/* TABLE AND PERFORM THE FOLLOWING FUNCTION:                          */
/*                                                                    */
/*  1)  CREATE A TABLE OF CONTENTS LIST OF ALL FONTS LESS THAN 37     */
/*      POINTS IN SIZE.                                               */
/*  2)  GENERATE AND SUBMIT THE NECESSARY JCL AND OGL STATEMENTS      */
/*      TO PRODUCE A MAP OF EVERY FONT SELECTED IN (1)                */
/*                                                                    */
/*  OPTIONS:  DEBUG  -  TURN ON CLIST TRACING FOR DIAGNOSTIC PURPOSES */
/*            NOSUB  -  CREATE TABLE OF CONTENTS BUT DO NOT SUBMIT    */
/*                      THE JOBS TO MAP THE FONTS                     */
/*                                                                    */
/*  NOTE:  THE SUBMITTED JOBS ARE STRUCTURED TO CONTAIN 50 OGL        */
/*         STEPS                                                      */
/**********************************************************************/

IF &DEBUG = DEBUG +
  THEN CONTROL LIST CONLIST SYMLIST NOFLUSH PROMPT MSG
  ELSE CONTROL NOLIST NOCONLIST NOSYMLIST FLUSH NOPROMPT MSG

ISPEXEC LIBDEF FTABLE DATASET ID('TECTSG.ISPFJCL.ISPTLIB')

DEL FONTMAPS.CNTL
DEL FONTMAP.CONTENTS

ALLOC F(CONTENTS) DS(FONTMAP.CONTENTS) NEW  REUSE +
      SPACE(10,10) TRACK RECFM(F B) LRECL(80) BLKSIZE(23440)

ALLOC F(ISPFILE) DS(FONTMAPS.CNTL) NEW  REUSE +
      SPACE(30) CYL RECFM(F B) LRECL(80) BLKSIZE(23440)

ISPEXEC TBOPEN AFPFONTS NOWRITE LIBRARY(FTABLE) SHARE
ISPEXEC TBSORT AFPFONTS FIELDS(TYPEFACE,C,A POINTSIZ,N,A)
ISPEXEC TBSKIP AFPFONTS

SET STEP = 0
SET JOB  = NO
SET PAGE = 0
SET TOC = 0
SET MAXTOC = 46  /* MAX FONTS PER PAGE */

OPENFILE CONTENTS OUTPUT

SET CONTENTS = &STR(.DF FONT TYPE(SERIF  8   ) CODEPAGE T1D0BASE )
PUTFILE CONTENTS
SET CONTENTS = &STR(.BF FONT)
PUTFILE CONTENTS
SET CONTENTS = &STR(.PN PREF AFP-TOC-)
PUTFILE CONTENTS
SET CONTENTS = &STR(.RH ON)
PUTFILE CONTENTS
SET CONTENTS = &STR(AFP FONT MAPS - TABLE OF CONTENTS)
PUTFILE CONTENTS
SET CONTENTS = &STR(.RH OFF)
PUTFILE CONTENTS

SET HEAD1 = &STR(:RDEF ID=FMTOC CWIDTHS='2.6I .5I 1.2I 1.2I .7I' +
                  HP='2 0' ALIGN='L R'.)
SET HEAD2 = &STR(:TABLE REFID=FMTOC.)
SET LINE1 = &STR(:C.FONT NAME:C.SIZE:C.WIDTH:C.WEIGHT:C.PAGE #)

SET CONTENTS = &STR(&HEAD1)
PUTFILE CONTENTS
SET CONTENTS = &STR(&HEAD2)
PUTFILE CONTENTS
SET CONTENTS = &STR(:ROW.)
PUTFILE CONTENTS
SET CONTENTS = &STR(&LINE1)
PUTFILE CONTENTS

FIXUP: +
    IF &POINTSIZ < 37 THEN DO
              SET STEP = &STEP + 1
              IF &STEP = 1  THEN SET JOB = YES
              SET TFONT = &SUBSTR(3:&LENGTH(&CODEFONT),&CODEFONT  )
              SET PAGE = &PAGE + 1

   IF &TOC = &MAXTOC THEN DO
                          SET TOC = 0
                          SET CONTENTS = &STR(:ETABLE.)
                          PUTFILE CONTENTS
                          SET CONTENTS = &STR(.PA)
                          PUTFILE CONTENTS
                          SET CONTENTS = &STR(&HEAD2)
                          PUTFILE CONTENTS
                          SET CONTENTS = &STR(:ROW.)
                          PUTFILE CONTENTS
                          SET CONTENTS = &STR(&LINE1)
                          PUTFILE CONTENTS
                          END

   SET CONTENTS = &STR(:ROW.)
   PUTFILE CONTENTS

   SET OFF = &SYSINDEX(NORMAL,&STR(&WIDTH),1)
       IF &OFF > 0 THEN SET &WIDTH =

   SET OFF = &SYSINDEX(MEDIUM,&STR(&WEIGHT),1)
       IF &OFF > 0 THEN SET &WEIGHT =

SET CONTENTS = &STR(:C.&TYPEFACE:C.&POINTSIZ:C.&WIDTH:C.&WEIGHT:C.&PAGE)
PUTFILE CONTENTS

   SET TOC = &TOC + 1

              IF &NOSUB = THEN DO
              ISPEXEC FTOPEN
              ISPEXEC FTINCL FONTMJCL
              IF &STEP = 50 THEN DO
                                 ISPEXEC FTCLOSE
                                 SUB FONTMAPS.CNTL
                                 WRITE JOB SUBMITTED UP TO PAGE &PAGE
                                 SET STEP = 0
                                 END
                           SET JOB = NO
                           END
                           END
                    ISPEXEC TBSKIP AFPFONTS
                    IF &LASTCC = 0 THEN GOTO FIXUP
EXIT: +
      SET CONTENTS = &STR(:ETABLE.)
      PUTFILE CONTENTS
      CLOSFILE CONTENTS

      ISPEXEC TBEND AFPFONTS
      ISPEXEC LIBDEF FTABLE
      FREE FILE(CONTENTS ISPFILE)
      DEL FONTMAPS.CNTL
./       ADD   NAME=FONTSUMM
PROC 0 DEBUG ALL

/**********************************************************************/
/* THIS CLIST IS DESIGNED TO SCAN THE TABLE OF ADVANCED FUNCTION      */
/* PRINTING FONTS AND GENERATE A SUMMARY THAT CAN BE USED BY THE USER */
/* IN SELECTING A FONT TO BE USED IN SCRIPTING A DOCUMENT.            */
/*                                                                    */
/* THE CLIST IS INVOKED BY ENTERING %FONTSUMM                         */
/*                               OR %FONTSUMM ALL                     */
/*                                                                    */
/*      BY SPECIFYING THE OPTION 'ALL' A SUMMARY WILL BE              */
/*      GENERATED OF ALL AVAILABLE FONTS.  THE DEFAULT IS TO          */
/*      EXCLUDE THE FOREIGN (NON-U.S.) FONTS.                         */
/*                                                                    */
/*  THE OUTPUT PRODUCED BY THIS CLIST SHOULD THEN BE PROCESSED BY     */
/*  SCRIPT TO GENERATE THE SUMMARY DOCUMENT                           */
/**********************************************************************/
IF &DEBUG = DEBUG +
  THEN CONTROL LIST CONLIST SYMLIST NOFLUSH PROMPT MSG
  ELSE CONTROL NOLIST NOCONLIST NOSYMLIST FLUSH NOPROMPT NOMSG


/*  ISPEXEC LIBDEF FTABLE DATASET ID('TSGJCL.SPF.ISPTLIB')

DEL FONTSUMM.TEXT

ALLOC F(CONTENTS) DS(FONTSUMM.TEXT) NEW +
      SPACE(10,10) TRACK RECFM(F B) LRECL(80) BLKSIZE(23440)
FREE  F(CONTENTS)
ALLOC F(CONTENTS) DS(FONTSUMM.TEXT) MOD +
      SPACE(10,10) TRACK RECFM(F B) LRECL(80) BLKSIZE(23440)

ISPEXEC TBOPEN AFPFONTS NOWRITE                 SHARE
WRITE &LASTCC
ISPEXEC TBSORT AFPFONTS FIELDS(TYPEFACE,C,A POINTSIZ,N,A)
ISPEXEC TBSKIP AFPFONTS

SET STEP = 0
SET JOB  = NO
SET PAGE = 0
SET TOC = 0
SET SAMPLE = 0
SET XFONT =
SET FONT = FONT
SET CP = CP
SET MAXTOC = 23  /* MAX FONTS PER PAGE */

OPENFILE CONTENTS OUTPUT

SET CONTENTS = &STR(.DF FONT TYPE('HELVETICA LATIN1' 8   ) +
                     CODEPAGE T1V10500 )
PUTFILE CONTENTS
SET CONTENTS = &STR(.BF FONT)
PUTFILE CONTENTS
SET CONTENTS = &STR(.PN PREF FONTS-)
PUTFILE CONTENTS
SET CONTENTS = &STR(.RH ON)
PUTFILE CONTENTS
SET CONTENTS = &STR(AFP FONT SUMMARY)
PUTFILE CONTENTS
SET CONTENTS = &STR(.RH OFF)
PUTFILE CONTENTS

SET HEAD1 = +
&NRSTR(:RDEF ID=FMTOC CWIDTHS='2.6I .5I 1.3I 1.1I .8I' HP='2'  +
                  ALIGN='L R L L R'.)
SET HEAD2 = &NRSTR(:RDEF ID=ROW2 CWIDTHS='1I 2.1I 3.2I' HP='1 1' +
                  ALIGN='L R R'.)
SET HEAD3 = &NRSTR(:RDEF ID=HEAD CWIDTHS='2.6I .5I 1.3I 1.1I .8I' +
                  HP='2' ALIGN='L R L L R'.)
SET HEAD4 = &NRSTR(:TABLE REFID=FMTOC.)
SET LINE1 = &NRSTR(:C.FONT NAME:C.SIZE:C.WIDTH:C.WEIGHT:C.CODEPAGE)

SET CONTENTS = &STR(&HEAD1)
PUTFILE CONTENTS
SET CONTENTS = &STR(&HEAD2)
PUTFILE CONTENTS
SET CONTENTS = &STR(&HEAD3)
PUTFILE CONTENTS
SET CONTENTS = &STR(&HEAD4)
PUTFILE CONTENTS
SET CONTENTS = &STR(:ROW REFID=HEAD.)
PUTFILE CONTENTS
SET CONTENTS = &STR(&LINE1)
PUTFILE CONTENTS

FIXUP: +
 IF &ALL ¬= ALL THEN DO
   IF &SYSINDEX(AUSTRAL,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
   IF &SYSINDEX(AUSTRIA,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
   IF &SYSINDEX(BELGIUM,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
   IF &SYSINDEX(BRAZIL,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
   IF &SYSINDEX(DENMARK,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
   IF &SYSINDEX(FINLAND,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
   IF &SYSINDEX(FRANCE,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
   IF &SYSINDEX(GERMANY,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
   IF &SYSINDEX(FRENCH,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
   IF &SYSINDEX(ICELAND,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
   IF &SYSINDEX(KATAKANA,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
   IF &SYSINDEX(ITALY,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
   IF &SYSINDEX(JAPAN,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
   IF &SYSINDEX(KINGDOM,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
   IF &SYSINDEX(LUXEMBOURG,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
   IF &SYSINDEX(LATIN,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
   IF &SYSINDEX(U.K.&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
   IF &SYSINDEX(H.K.,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
   IF &SYSINDEX(NETHER,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
   IF &SYSINDEX(NORWAY,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
   IF &SYSINDEX(PORTU,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
   IF &SYSINDEX(SPAIN,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
   IF &SYSINDEX(SWEEDEN,&NRSTR(&CODEPD),1) > 0 THEN GOTO SKIP
      END

   IF &TOC = &MAXTOC THEN DO
                          SET TOC = 0
                          SET CONTENTS = &STR(:ETABLE.)
                          PUTFILE CONTENTS
                          SET CONTENTS = &STR(.PA)
                          PUTFILE CONTENTS
                          SET CONTENTS = &STR(&HEAD4)
                          PUTFILE CONTENTS
                          SET CONTENTS = &STR(:ROW REFID=HEAD.)
                          PUTFILE CONTENTS
                          SET CONTENTS = &STR(&LINE1)
                          PUTFILE CONTENTS
                          END

   SET CONTENTS = &STR(:ROW REFID=FMTOC.)
   PUTFILE CONTENTS

   SET OFF = &SYSINDEX(NORMAL,&STR(&WIDTH),1)
       IF &OFF > 0 THEN SET &WIDTH =

   SET OFF = &SYSINDEX(MEDIUM,&NRSTR(&WEIGHT),1)
       IF &OFF > 0 THEN SET &WEIGHT =


   SET CONTENTS = &STR(:C.&TYPEFACE:C.&POINTSIZ:C.&WIDTH+
                       :C.&NRSTR(&WEIGHT):C.&CODEPAGE)
   PUTFILE CONTENTS

   SET CONTENTS = &STR(:ROW REFID=ROW2.)
   PUTFILE CONTENTS
   SET CONTENTS = &STR(:C.&CODEFONT:C.&STYLE:C.&NRSTR(&CODEPD))
   PUTFILE CONTENTS

   IF &STR(&XFONT) ¬= &STR(&TYPEFACE) THEN DO
                            IF &STR(&WIDTH) ¬= THEN GOTO SETTOC
                            IF &NRSTR(&WEIGHT) ¬= THEN GOTO SETTOC
                            IF &POINTSIZ ¬= 10 THEN GOTO SETTOC
                            IF &STR(&STYLE) ¬= THEN GOTO SETTOC
                            SET SAMPLE = &SAMPLE + 1
                            SET XFONT = &STR(&TYPEFACE)
                            SET &&FONT&SAMPLE = &STR(&TYPEFACE)
                            SET &&CP&SAMPLE = &CODEPAGE
                            END
SETTOC: +
   SET TOC = &TOC + 1

SKIP: +
   ISPEXEC TBSKIP AFPFONTS
      IF &LASTCC = 0 THEN GOTO FIXUP

EXIT: +
      SET CONTENTS = &STR(:ETABLE.)
      PUTFILE CONTENTS

IF &SAMPLE = 0 THEN GOTO EXIT2

SET COUNT = 1

   SET CONTENTS = &STR(.DS;.FO OFF)
   PUTFILE CONTENTS
   CLOSFILE CONTENTS

SAMPLES: +
      OPENFILE CONTENTS OUTPUT
      SET CONTENTS = &STR(.PA)
      PUTFILE CONTENTS

      CONTROL NOCAPS
      SET FONT = &&FONT&COUNT
      SET CP = &&CP&COUNT
      SET CONTENTS = &STR(.DF F&COUNT TYPE('&FONT' 10) CODEPAGE &CP)
      PUTFILE CONTENTS
      SET CONTENTS = &STR(.BF FONT)
      PUTFILE CONTENTS
      SET CONTENTS = &STR(FONT &FONT SIZE:10 )
      PUTFILE CONTENTS
      SET CONTENTS = &STR(.BF F&COUNT)
      PUTFILE CONTENTS
      CLOSFILE CONTENTS

      ISPEXEC SELECT PGM(FONTSAMP)


      SET COUNT = &COUNT + 1
      IF &COUNT <= &SAMPLE THEN GOTO SAMPLES

EXIT2: +
      ISPEXEC TBEND AFPFONTS
/*    ISPEXEC LIBDEF FTABLE
      FREE FILE(CONTENTS)
      ISPEXEC BROWSE DATASET(FONTSUMM.TEXT)
./       ADD   NAME=FONTTABL
PROC 0 DEBUG NEW SYS EDIT
IF &DEBUG = DEBUG +
   THEN CONTROL LIST CONLIST SYMLIST NOFLUSH PROMPT MSG
   ELSE CONTROL NOLIST NOCONLIST NOSYMLIST FLUSH NOPROMPT NOMSG
SET &SYS = SYS
IF &SYS  = SYS  | &NEW = NEW THEN SET PANEL = AFPFONTX
IF &EDIT = EDIT THEN SET PANEL = AFPFONTE
IF &PANEL =     THEN SET PANEL = AFPFONTX
ISPEXEC LIBDEF FTABLE DATASET ID('TECTSG.ISPFJCL.ISPTLIB')
IF &NEW ¬= NEW THEN DO
                    ISPEXEC TBOPEN AFPFONTS NOWRITE +
                            LIBRARY(FTABLE) SHARE
                    SET LCC = &LASTCC
                    IF &LCC = 0 THEN GOTO DOTOP
                    SET ZEDSMSG = ERROR
                    SET ZEDLMSG = &STR(SEVERE ERROR CODE=&LCC +
                        OPENING FONT TABLE: INFORM SUPPORT PROGRAMMER)
                    ISPEXEC SETMSG MSG(ISRZ000)
                    ISPEXEC LIBDEF FTABLE
                    EXIT
                    END
SET C =
CREATE: +
SET SYS = SYS
ISPEXEC TBCREATE AFPFONTS KEYS(CODEFONT) +
        NAMES(CHARSET CODEPAGE TYPEFACE WEIGHT WIDTH +
              POINTSIZ STYLE CODEPD CODEPTS CHARFP) +
        WRITE REPLACE LIBRARY(FTABLE)
SET CC = &LASTCC
SELECT
WHEN (&CC = 0) GOTO ALLOC
WHEN (&CC = 4)  DO
IF &C = C THEN DO
               SET ZEDSMSG = ERROR
               SET ZEDLMSG = &STR(TABLE CREATE ERROR TWICE)
               ISPEXEC SETMSG MSG(ISRZ000)
               ISPEXEC LIBDEF FTABLE
               EXIT
               END
ISPEXEC TBEND AFPFONTS
ISPEXEC TBERASE AFPFONTS LIBRARY(FTABLE)
SET C = C
GOTO CREATE
END
WHEN (&CC > 4)  DO
                SET ZEDSMSG = ERROR
                SET ZEDLMSG = &STR(TABLE CREATE ERROR RETURN CODE = &CC)
                ISPEXEC SETMSG MSG(ISRZ000)
                ISPEXEC LIBDEF FTABLE
                EXIT
                END
         END
ALLOC: +
       ALLOC F(TBIN) DS(FONT3820.DATA) SHR REUSE
OPENFILE TBIN
SET CL =
CONTROL NOLIST NOCONLIST NOSYMLIST FLUSH NOPROMPT NOMSG
READ: +
IF &CL = CLOSED THEN GOTO SAVEIT
   GETFILE TBIN
/*  ERROR CHECK  */
ERROR DO
      SET CL = CLOSED
      CLOSFILE TBIN
      FREE FILE(TBIN)
      ERROR OFF
      END
CONTROL NOCAPS
SET CODEFONT = &SUBSTR(1:8,&TBIN)
SET CHARSET =  &SUBSTR(9:16,&TBIN)
SET CODEPAGE = &SUBSTR(17:24,&TBIN)
SET TF       = &STR(&SUBSTR(25:56,&TBIN))
      SET OFF = &SYSINDEX(  ,&TF,1)
      SET TYPEFACE = &SUBSTR(1:&OFF,&STR(&TF))
SET WT       = &STR(&SUBSTR(57:71,&TBIN))
      SET OFF = &SYSINDEX(  ,&WT,1)
      IF &OFF = 0 THEN SET WEIGHT = &WTR(&WT)
                  ELSE SET WEIGHT  = &SUBSTR(1:&OFF,&WT)
SET POINTSIZ = &SUBSTR(72:75,&TBIN)
    SET POINTSIZ = &POINTSIZ
SET ST       = &STR(&SUBSTR(76:107,&TBIN))
      SET OFF = &SYSINDEX(  ,&ST,1)
      SET STYLE  = &SUBSTR(1:&OFF,&ST)
SET WD       = &STR(&SUBSTR(108:122,&TBIN))
      SET OFF = &SYSINDEX(  ,&WD,1)
      IF &OFF = 0 THEN SET WIDTH = &STR(&WD)
                  ELSE SET WIDTH  = &STR(&SUBSTR(1:&OFF,&WD))
SET CPD      = &STR(&SUBSTR(123:154,&TBIN))
      SET OFF = &SYSINDEX(  ,&NRSTR(&CPD),1)
      IF &OFF = 0 THEN SET CODEPD = &NRSTR(&CPD)
                  ELSE SET CODEPD = &STR(&SUBSTR(1:&OFF,&NRSTR(&CPD)))
SET CODEPTS  = &SUBSTR(155:158,&TBIN)
CONTROL NOCAPS
SET CHARFP   = &SUBSTR(159:159,&TBIN)
IF &CHARFP = F THEN SET CHARFP = Fixed
               ELSE SET CHARFP = Prop
ISPEXEC TBADD AFPFONTS
GOTO READ
SAVEIT: ISPEXEC TBSAVE AFPFONTS LIBRARY(FTABLE)
DOTOP: +
IF &DEBUG = DEBUG +
   THEN CONTROL LIST CONLIST SYMLIST NOFLUSH PROMPT MSG
   ELSE CONTROL NOLIST NOCONLIST NOSYMLIST FLUSH NOPROMPT NOMSG
      ISPEXEC TBSORT AFPFONTS FIELDS(TYPEFACE,C,A POINTSIZ,N,A)
      ISPEXEC TBTOP AFPFONTS
      ISPEXEC TBVCLEAR AFPFONTS
      ISPEXEC TBSCAN AFPFONTS ARGLIST(CODEFONT)
      SET CODEFONT = &STR(X*)
      CONTROL NOCAPS
      ISPEXEC TBSARG AFPFONTS
      SET LCC = 0
DISPLAY: +
 IF &LCC = 4 THEN ISPEXEC TBDISPL AFPFONTS
             ELSE ISPEXEC TBDISPL AFPFONTS PANEL(&PANEL)
 SET LCC = &LASTCC
 IF &LCC = 8 THEN GOTO EXIT
 IF &TSEL = && &STR(&ZCMD) = THEN GOTO DISPLAY
 IF &TSEL = S && &EDIT = EDIT THEN DO
  /*******************************************************/
  /*   DETERMINE IF THERE ARE ANY LINES IN THE DATASET   */
  /*   TO BEGIN WITH.                                    */
  /*******************************************************/
  ISREDIT (FIRST,LAST) = DISPLAY_LINES
   ISREDIT RESET
   ISREDIT (LINE) = CURSOR
   SET OLINE = &LINE
   IF &LINE > 1 THEN SET LINE = &LINE+1
   IF &LINE ¬= 0 THEN ISREDIT CURSOR = &LINE
SET STYLE = &STYLE
  SET OFF = &SYSINDEX(MEDIUM,&WEIGHT,1)
  IF &OFF ¬= 0 THEN SET WEIGHT =
  SET OFF = &SYSINDEX(NORMAL,&WIDTH,1)
  IF &OFF ¬= 0 THEN SET WIDTH =
SET DEFINE =&STR(.DF F?? TYPE('&TYPEFACE' &POINTSIZ +
    &STYLE &WEIGHT &WIDTH) CODEPAGE &CODEPAGE)
ISREDIT LINE_AFTER  .ZCSR=(DEFINE)
GOTO EXIT
                            END
IF &TSEL = M && &SYS = SYS THEN DO   /*  MAP THE CHARACTER */
   SET TFONT = &SUBSTR(3:&LENGTH(&CODEFONT),&CODEFONT  )
   ISPEXEC FTOPEN TEMP
   ISPEXEC FTINCL FONTMAP
   ISPEXEC FTCLOSE
   ISPEXEC VGET ZTEMPF
   FREE F(SYSPRINT SAMPLE OVRLIB FONTDD SYMBOLIC SEGDD SYSIN)
   ALLOC F(SYSPRINT) DUMMY                            REUSE
   OUTDES SAMPLE FORMDEF(A10110)
   ALLOC F(SAMPLE)   SYSOUT(F) OUTDES(SAMPLE)         REUSE
   ALLOC F(SYSIN)    DS('&ZTEMPF')     SHR            REUSE
   ALLOC F(SYMBOLIC) DUMMY                            REUSE
   ALLOC F(FONTDD)   DS('SYS1.FONTLIBB')      SHR     REUSE
   ALLOC F(OVRLIB)   DS('SYS1.OVERLIB')          SHR  REUSE
   ALLOC F(SEGDD)    DUMMY                            REUSE
   CALL 'SYS1.LINKLIB(DZIOVRLY)' 'NOSEQ,ENG'
   FREE F(SAMPLE)
   FREE OUTDES(SAMPLE)
   FREE F(ISPFILE SYSPRINT OVRLIB FONTDD SYMBOLIC SEGDD SYSIN)
   END
SET TSEL =
IF &STR(&ZCMD) = THEN GOTO DISPLAY
SET SYSDVAL = &STR(&ZCMD)
READDVAL V1 V2 V3
ISPEXEC TBVCLEAR AFPFONTS
IF &V2 = THEN GOTO DISPLAY
IF &SUBSTR(1:1,&STR(&V1)) = S THEN SET V1 = SORT
IF &SUBSTR(1:1,&STR(&V1)) = L THEN SET V1 = LOCATE
IF &SUBSTR(1:1,&STR(&V1)) = O THEN SET V1 = ONLY
IF &STR(&V1) = ONLY | &STR(&V1) = LOCATE +
   THEN DO
    IF &STR(&V2) = CODEFONT | +
        &STR(&V2) = CHARSET | +
        &STR(&V2) = CODEPAGE | +
        &STR(&V2) = NAME |        /* REALLY TYPEFACE */ +
        &STR(&V2) = WEIGHT | +
        &STR(&V2) = STYLE  | +
        &STR(&V2) = WIDTH | +
        &STR(&V2) = SIZE         /* REALLY POINTSIZ */ +
        THEN DO
           IF &STR(&V2) = CODEFONT THEN SET CODEFONT = &STR(&V3)&STR(*)
           IF &STR(&V2) = CHARSET THEN SET CHARSET = &STR(&V3)&STR(*)
           IF &STR(&V2) = CODEPAGE THEN SET CODEPAGE = &STR(&V3)&STR(*)
           IF &STR(&V2) = NAME THEN SET TYPEFACE = &STR(&V3)&STR(*)
           IF &STR(&V2) = WEIGHT THEN SET WEIGHT = &STR(&V3)&STR(*)
           IF &STR(&V2) = STYLE THEN SET STYLE  = &STR(&V3)&STR(*)
           IF &STR(&V2) = WIDTH THEN SET WIDTH = &STR(&V3)&STR(*)
           IF &STR(&V2) = SIZE THEN SET POINTSIZ = &STR(&V3)&STR(*)
           IF &STR(&V2) = NAME THEN SET &V2 = TYPEFACE
           IF &STR(&V2) = SIZE THEN SET &V2 = POINTSIZ
           IF &STR(&V1) = ONLY THEN ISPEXEC TBSARG AFPFONTS
                         ELSE +
           IF &STR(&V1) = LOCATE THEN DO
                                ISPEXEC TBTOP AFPFONTS
                                ISPEXEC TBSCAN AFPFONTS ARGLIST(&V2)
                                END
           END
GOTO DISPLAY
END
IF &STR(&V1) = SORT THEN DO
                   IF &STR(&V2) = NAME THEN SET &V2 = TYPEFACE
                   IF &STR(&V2) = SIZE THEN SET &V2 = POINTSIZ
                   IF &V2 = POINTSIZ THEN SET SORTTYPE = N
                                     ELSE SET SORTTYPE = C
                   ISPEXEC TBSORT AFPFONTS FIELDS(&V2.,&SORTTYPE.,A)
                   GOTO DISPLAY
                   END
GOTO DISPLAY
EXIT: +
      ISPEXEC TBEND AFPFONTS
      ISPEXEC LIBDEF FTABLE
./       ADD   NAME=GENAIX
 ISREDIT MACRO (OBJNAME) NOPROCESS
 ISPEXEC CONTROL ERRORS RETURN
/*-------------------------------------------------------------*/
/* MAKE SURE A DATASET NAME WAS SPECIFIED                      */
/*-------------------------------------------------------------*/
 IF &STR(&PARMS) ¬= THEN DO
    SET ZEDSMSG = &STR(INVALID PARAMETERS)
    SET ZEDLMSG = &STR(SPECIFY A DATASET NAME IN VALID TSO +
    SYNTAX ONLY)
    ISPEXEC SETMSG MSG(ISRZ001)
    EXIT CODE(12)
 END
 ISREDIT PROCESS DEST
 SET DSNINFO = &STR(&OBJNAME)
/*-------------------------------------------------------------*/
/* MAKE SURE THE MODEL DATASET EXISTS                          */
/*-------------------------------------------------------------*/
 IF &SYSDSN(&DSNINFO)= &STR(DATASET NOT FOUND) THEN DO
    SET ZEDSMSG = &STR(DATASET NOT FOUND)
    SET ZEDLMSG = &STR(SPECIFY A DATASET NAME IN VALID TSO SYNTAX)
    ISPEXEC SETMSG MSG(ISRZ001)
    EXIT CODE(12)
 END
/*-------------------------------------------------------------*/
/* ALLOC "GENAIX" AS AN OUTPUT FILE FOR LISTCAT                */
/*-------------------------------------------------------------*/
ALLOC F(GENAIX) REUSE TRACKS SPACE(1 15) UNIT(WORK) -
 LRECL(125) BLOCK(23375) RECFM(V B A) DSORG(PS)
/*-------------------------------------------------------------*/
/* LIST THE CATALOGUE ENTRY FOR THE MODEL DATASET              */
/*-------------------------------------------------------------*/
LISTCAT ENT(&OBJNAME) OUTFILE(GENAIX) ALL
/*-------------------------------------------------------------*/
/* HANDLE END OF FILE ON THE CATALOGUE LISTING                 */
/*-------------------------------------------------------------*/
ERROR                              /* CLIST ERROR ROUTINE       */ +
  DO
    SET RCODE = &LASTCC            /* SAVE RETURN CODE          */
    IF &RCODE = 400 THEN           /* END OF FILE REACHED?      */ +
      DO                           /* RC 400 (EOF) ROUTINE      */
         CLOSFILE GENAIX           /* CLOSE THE FILE            */
         SET &EOF = ON             /* SAVE EOF STATUS           */
         RETURN                    /* RETURN AFTER GETFILE STMT */
      END                          /* END RC 400 ROUTINE        */
    ELSE                           /* HARD ERROR, IT'S NOT EOF  */ +
      DO
        WRITE ERROR IN CLIST PROCESSING, ERROR CODE = &RCODE
        IF &DATATYPE(&RCODE) EQ NUM THEN /* CLIST ERROR         */ +
          DO
            ERROR OFF              /* CANCEL ERROR ACTION       */
          END
        EXIT CODE(16)              /* CLIST UNSUCCESSFUL        */
      END
  END                              /* END ERROR ROUTINE         */
/*-------------------------------------------------------------*/
/* LOOP THROUGH THE CATALOGUE LISTING PROCESSING SELECTED      */
/* LINES OF INTEREST.                                          */
/*-------------------------------------------------------------*/
OPENFILE GENAIX INPUT
SET &EOF = OFF
SET &XTSW = OFF
SET &RCI = 0
SET &ISW = NO
SET &DSW = NO
DO WHILE &EOF ¬= ON
  GETFILE GENAIX
  IF &EOF = OFF THEN DO
    SET &GENAIX = &GENAIX&STR(                                       )
    SET &GENAIX = &GENAIX&STR(                                       )
    SET &L = &LENGTH(&GENAIX)
    SET &RCI = &RCI + 1
    SET &UD  = &STR(&&GENAIX)
/*-------------------------------------------------------------*/
/* PICK UP DATA COMPONENT NAME                                 */
/*-------------------------------------------------------------*/
    IF &SUBSTR(9:14,&UD) = &STR(DATA--) && &DSW = NO THEN DO
      SET &COMP=&STR(DATA)
      SET &DN = &SUBSTR(18:62,&GENAIX)
      SET &DSW = YES
    END
    ELSE IF &SUBSTR(9:17,&UD) = &STR(CLUSTER--) THEN DO
      SET &BN = &SUBSTR(18:62,&GENAIX)
    END
/*-------------------------------------------------------------*/
/* PICK UP INDEX COMPONENT NAME                                */
/*-------------------------------------------------------------*/
    ELSE IF &SUBSTR(5:11,&UD) = &STR(INDEX -) && &ISW = NO THEN DO
      SET &COMP=&STR(INDEX)
      SET &IN = &SUBSTR(18:62,&GENAIX)
      SET &ISW = YES
    END
/*-------------------------------------------------------------*/
/* PICK UP AIX NAME                                            */
/*-------------------------------------------------------------*/
    ELSE IF &SUBSTR(2:10,&UD) = &STR(AIX -----) THEN DO
      SET &CN = &SUBSTR(18:&L,&GENAIX)
    END
/*-------------------------------------------------------------*/
/* PICK UP KEYLENGTH, AVERAGE RECORD LENGTH, BUFFERSPACE       */
/* AND DATA CONTROLINTERVALSZIE                                */
/*-------------------------------------------------------------*/
    ELSE IF &SUBSTR(9:15,&UD) = &STR(KEYLEN-) THEN DO
      IF &COMP = &STR(DATA) THEN DO
        SET &KL = &SUBSTR(28:32,&GENAIX)
        SYSCALL BLANK KL
        SET &ARL = &SUBSTR(55:61,&GENAIX)
        SYSCALL BLANK ARL
        SET &BFS = &SUBSTR(85:90,&GENAIX)
        SYSCALL BLANK BFS
        SET &DCI = &SUBSTR(115:119,&GENAIX)
        SYSCALL BLANK DCI
      END
/*-------------------------------------------------------------*/
/* PICK UP INDEX CONTROLINTERVALSIZE                           */
/*-------------------------------------------------------------*/
      ELSE DO
        SET &ICI = &SUBSTR(115:119,&GENAIX)
        SYSCALL BLANK ICI
      END
    END
/*-------------------------------------------------------------*/
/* PICK UP NUMBER OF EXTENTS                                   */
/*-------------------------------------------------------------*/
    ELSE IF &SUBSTR(67:74,&GENAIX) = &STR(EXTENTS-) && &XTSW = OFF +
    THEN DO
       SET &XTSW = ON
       IF &SUBSTR(89:89,&GENAIX) = &STR(-) THEN +
          SET &XT = &SUBSTR(90:90,&GENAIX)
       ELSE SET &XT = &SUBSTR(89:90,&GENAIX)
    END
/*-------------------------------------------------------------*/
/* PICK UP RELATIVE KEY POSITION AND MAXIMUM RECORD LENGTH     */
/*-------------------------------------------------------------*/
    ELSE IF &SUBSTR(9:12,&UD) = &STR(RKP-) THEN DO
      IF &COMP = &STR(DATA) THEN DO
        SET &MRL = &SUBSTR(55:61,&GENAIX)
        SYSCALL BLANK MRL
      END
    END
    ELSE IF &SUBSTR(9:14,&UD) = &STR(AXRKP-) THEN DO
      SET &RKP = &SUBSTR(28:32,&GENAIX)
      SYSCALL BLANK RKP
    END
    ELSE IF &SUBSTR(9:16,&UD) = &STR(UPGRADE ) THEN DO
      SET &UPG = &STR(UPGRADE)
    END
    ELSE IF &SUBSTR(9:18,&UD) = &STR(NOUPGRADE ) THEN DO
      SET &UPG = &STR(NOUPGRADE)
    END
/*-------------------------------------------------------------*/
/* PICK UP SHROPTIONS, SPEED/RECOVERY, AIX TYPE,               */
/* IMBED AND REPLICATE OPTIONS.                                */
/*-------------------------------------------------------------*/
    ELSE IF &SUBSTR(9:16,&UD) = &STR(SHROPTNS) THEN DO
      IF &COMP = &STR(DATA) THEN DO
        SET &SHR = &SUBSTR(9:21,&GENAIX)
        SET &SHR = &STR(SHAREOPTIONS)&SUBSTR(9:13,&SHR)
        SET &SPD = &SUBSTR(25:32,&GENAIX)
        SET &IDX = &SUBSTR(67:76,&GENAIX)
        IF &IDX = &STR(INDEXED) THEN SET &TYPE = K
        IF &IDX = &STR(NONINDEXED) THEN SET &TYPE = E
        IF &IDX = &STR(NUMBERED) THEN SET &TYPE = R
        SET &IMB = &SUBSTR(96:102,&GENAIX)
        SET &RPL = &SUBSTR(110:119,&GENAIX)
        IF &RPL = &STR(NOREPLICAT) +
        THEN SET &RPL = &RPL&STR(E)
      END
    END
/*-------------------------------------------------------------*/
/* PICK UP REUSE AND SPANNED OPTIONS                           */
/*-------------------------------------------------------------*/
    ELSE IF &SUBSTR(9:17,&UD) = &STR(UNORDERED) THEN DO
      IF &COMP = &STR(DATA) THEN DO
        IF &SYSINDEX(NOREUSE,&GENAIX,1) > 0 +
        THEN SET &REU = &STR(NOREUSE)
        ELSE SET &REU = &STR(REUSE)
        SET &SPA = &SUBSTR(55:64,&GENAIX)
        IF &SYSINDEX(NONUNIQKEY,&GENAIX,1) > 0 +
        THEN SET &UNK = &STR(NONUNIQUEKEY)
        ELSE SET &UNK = &STR(UNIQUEKEY)
      END
    END
/*-------------------------------------------------------------*/
/* PICK UP CI FREESPACE PERCENT                                */
/*-------------------------------------------------------------*/
    ELSE IF &SUBSTR(9:18,&UD) = &STR(REC-INSERT) THEN DO
      IF &COMP = &STR(DATA) THEN DO
        SET &FCI = &SUBSTR(59:61,&GENAIX)
        SYSCALL BLANK FCI
      END
    END
/*-------------------------------------------------------------*/
/* PICK UP CA FREESPACE PERCENT                                */
/*-------------------------------------------------------------*/
    ELSE IF &SUBSTR(9:18,&UD) = &STR(REC-UPDATE) THEN DO
      IF &COMP = &STR(DATA) THEN DO
        SET &FCA = &SUBSTR(59:61,&GENAIX)
        SYSCALL BLANK FCA
      END
    END
/*-------------------------------------------------------------*/
/* PICK UP SPACE ALLOCATION TYPE                               */
/*-------------------------------------------------------------*/
    ELSE IF &SUBSTR(9:18,&UD) = &STR(SPACE-TYPE) THEN DO
      IF &COMP = &STR(DATA) THEN DO
        SET &SPT = &SUBSTR(25:32,&GENAIX)
        SYSCALL BLANK SPT
        IF &SPT = CYLINDER THEN SET &SPT = CYLINDERS
        IF &SPT = TRACK    THEN SET &SPT = TRACKS
      END
    END
/*-------------------------------------------------------------*/
/* PICK UP PRIMARY SPACE QUANTITY                              */
/*-------------------------------------------------------------*/
    ELSE IF &SUBSTR(9:17,&UD) = &STR(SPACE-PRI) THEN DO
      IF &COMP = &STR(DATA) THEN DO
        SET &SPP = &SUBSTR(28:32,&GENAIX)
        SYSCALL BLANK SPP
      END
    END
/*-------------------------------------------------------------*/
/* PICK UP SECONDARY SPACE QUANTITY                            */
/*-------------------------------------------------------------*/
    ELSE IF &SUBSTR(9:17,&UD) = &STR(SPACE-SEC) THEN DO
      IF &COMP = &STR(DATA) THEN DO
        SET &SPS = &SUBSTR(28:32,&GENAIX)
        SYSCALL BLANK SPS
      END
    END
/*-------------------------------------------------------------*/
/* PICK UP DATA AND INDEX COMPONENT VOLSERS                    */
/*-------------------------------------------------------------*/
    ELSE IF &SUBSTR(9:15,&UD) = &STR(VOLSER-) THEN DO
      IF &COMP = &STR(DATA) THEN DO
        SET &VD = &SUBSTR(27:32,&GENAIX)
      END
      ELSE DO
        SET &VI = &SUBSTR(27:32,&GENAIX)
      END
    END
  END
  ELSE DO
  END
END
ERROR OFF
/*-------------------------------------------------------------*/
/* CALCULATE TOTAL SPACE REQUIRED FOR AIX                      */
/*-------------------------------------------------------------*/
SET &SPP = &SPP + (&SPS * &XT)
FREE  F(GENAIX)
 ISREDIT (DEST) = LINENUM .ZDEST
 SET OB = &STR((
 SET CB = )
 SET CT = &NRSTR(-)
/*-------------------------------------------------------------*/
/* GENERATE "DEFINE AIX( -"                                    */
/*-------------------------------------------------------------*/
 SET LINE = &NRSTR(&STR(  DEFINE AIX)&OB&STR( )&CT )
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
 SET DEST = &DEST + 1
/*-------------------------------------------------------------*/
/* GENERATE "NAME(AIX-NAME) - "                                */
/*-------------------------------------------------------------*/
 SET &CCB = )
 SET &CCT = &NRSTR(-)
 SET &COB = &STR((
 SET &LINE = &STR(NAME)
 SET &LINE = &LINE&STR(&COB)
 SET &LINE = &LINE&CN
 SET &LINE = &LINE&CCB
 SET &LINE = &LINE&STR( )
 SET &LINE = &LINE&STR(&CCT)
 SET &LINE = &NRSTR(&STR(        )&LINE)
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
 SET DEST = &DEST + 1
 SET LINE = &STR(        RELATE(&BN) - )
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
/*-------------------------------------------------------------*/
/* GENERATE "        RECORDSIZE(XX XX) - "                     */
/*-------------------------------------------------------------*/
 SET DEST = &DEST + 1
 SET LINE = &STR(        RECORDSIZE(&ARL &MRL) - )
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
/*-------------------------------------------------------------*/
/* GENERATE "        SPACE-TYPE(PRI SEC) - "                   */
/*-------------------------------------------------------------*/
 SET DEST = &DEST + 1
 SET LINE = &STR(        &SPT(&SPP &SPS)       - )
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
/*-------------------------------------------------------------*/
/* GENERATE "        (NON)INDEXED SHR(N N) (REUSE) - "         */
/*-------------------------------------------------------------*/
 SET DEST = &DEST + 1
 SET LINE = &STR(        &SHR &REU &UNK - )
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
/*-------------------------------------------------------------*/
/* GENERATE "        (NON)SPANNED (NO)IMBED                    */
/*                   (NO)REPLICATE UNIQUE - "                  */
/*-------------------------------------------------------------*/
 SET DEST = &DEST + 1
 SET LINE = &STR(        &IMB &RPL UNIQUE  - )
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
/*-------------------------------------------------------------*/
/* GENERATE "        FREESPACE(N N) - "                        */
/*-------------------------------------------------------------*/
 SET DEST = &DEST + 1
 SET LINE = &STR(        FREESPACE(&FCI &FCA)   - )
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
 IF &TYPE = K THEN DO
/*-------------------------------------------------------------*/
/* GENERATE "        KEYS(N N) SPEED/RECOVERY - "              */
/*-------------------------------------------------------------*/
 SET DEST = &DEST + 1
 SET LINE = &STR(        KEYS(&KL &RKP) &SPD   - )
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
 END
 ELSE DO
/*-------------------------------------------------------------*/
/* GENERATE "        SPEED/RECOVERY - "                        */
/*-------------------------------------------------------------*/
 SET DEST = &DEST + 1
 SET LINE = &STR(        &SPD                  - )
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
 END
/*-------------------------------------------------------------*/
/* GENERATE "        BUFFERSPACE(NNNN) - "                     */
/*-------------------------------------------------------------*/
 SET DEST = &DEST + 1
 SET LINE = &STR(        BUFFERSPACE(&BFS)     - )
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
/*-------------------------------------------------------------*/
/* GENERATE "        ) -                 "                     */
/*-------------------------------------------------------------*/
 SET DEST = &DEST + 1
 SET LINE = &NRSTR(&STR(         )&CB&STR( )&CT)
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
/*-------------------------------------------------------------*/
/* GENERATE "        DATA(NAME(DATACOMPONENTNAME) - "          */
/*-------------------------------------------------------------*/
 SET DEST = &DEST + 1
 SET LINE = &NRSTR(&STR(   DATA)&OB&STR(NAME)&OB&DN&CB&STR( )&CT)
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
/*-------------------------------------------------------------*/
/* GENERATE "        CISZ(NNNN) - "                            */
/*-------------------------------------------------------------*/
 SET DEST = &DEST + 1
 SET LINE = &STR(        CISZ(&DCI)        - )
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
/*-------------------------------------------------------------*/
/* GENERATE "        VOLUME(VOLSER) - "                        */
/*-------------------------------------------------------------*/
 SET DEST = &DEST + 1
 SET LINE = &NRSTR(        VOLUME(&VD) - )
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
 IF &TYPE ¬= K THEN GOTO SKIPIDX
/*-------------------------------------------------------------*/
/* GENERATE "        ) -                 "                     */
/*-------------------------------------------------------------*/
 SET DEST = &DEST + 1
 SET LINE = &NRSTR(&STR(         )&CB&STR( )&CT)
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
/*-------------------------------------------------------------*/
/* GENERATE "        INDEX(NAME(INDEXCOMPONENTNAME) - "        */
/*-------------------------------------------------------------*/
 SET DEST = &DEST + 1
 SET LINE = &NRSTR(&STR(  INDEX)&OB&STR(NAME)&OB&IN&CB&STR( )&CT)
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
/*-------------------------------------------------------------*/
/* GENERATE "        CISZ(NNNN) - "                            */
/*-------------------------------------------------------------*/
 SET DEST = &DEST + 1
 SET LINE = &STR(        CISZ(&ICI)        - )
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
/*-------------------------------------------------------------*/
/* GENERATE "        VOLUME(VOLSER) - "                        */
/*-------------------------------------------------------------*/
 SET DEST = &DEST + 1
 SET LINE = &NRSTR(        VOLUME(&VI) - )
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
/*-------------------------------------------------------------*/
/* GENERATE "        )                   "                     */
/*-------------------------------------------------------------*/
 SKIPIDX:+
 SET DEST = &DEST + 1
 SET LINE = &NRSTR(&STR(         )&CB&STR( ))
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
EXIT CODE(0)
/*-------------------------------------------------------------*/
/* SUBPROCEDURE TO TRANSLATE DASHES TO BLANKS                  */
/*-------------------------------------------------------------*/
BLANK: PROC 1 &OLDSTRG
SYSREF &OLDSTRG
SET &NEWSTRG =
SET &LSTRG = &LENGTH(&STR(&OLDSTRG))
SET &CURR = 0
DO UNTIL &CURR = &LSTRG
  SET &CURR = &CURR + 1
  IF &CURR > &LSTRG THEN GOTO CHGCHARX
  SET &NEW = &SUBSTR(&CURR:&CURR,&OLDSTRG)
  IF &SUBSTR(&CURR:&CURR,&STR(&OLDSTRG)) ¬= &STR(-) +
  THEN GOTO CHGCHARC
  SET &NEW = &STR( )
CHGCHARC: SET &NEWSTRG = &STR(&NEWSTRG&NEW)
END
CHGCHARX: SET &OLDSTRG = &NEWSTRG
END
./       ADD   NAME=GENDEF
 ISREDIT MACRO (OBJNAME) NOPROCESS
 ISPEXEC CONTROL ERRORS RETURN
/*-------------------------------------------------------------*/
/* MAKE SURE A DATASET NAME WAS SPECIFIED                      */
/*-------------------------------------------------------------*/
 IF &STR(&PARMS) ¬= THEN DO
    SET ZEDSMSG = &STR(INVALID PARAMETERS)
    SET ZEDLMSG = &STR(SPECIFY A DATASET NAME IN VALID TSO +
    SYNTAX ONLY)
    ISPEXEC SETMSG MSG(ISRZ001)
    EXIT CODE(12)
 END
 ISREDIT PROCESS DEST
 SET DSNINFO = &STR(&OBJNAME)
/*-------------------------------------------------------------*/
/* MAKE SURE THE MODEL DATASET EXISTS                          */
/*-------------------------------------------------------------*/
 IF &SYSDSN(&DSNINFO)= &STR(DATASET NOT FOUND) THEN DO
    SET ZEDSMSG = &STR(DATASET NOT FOUND)
    SET ZEDLMSG = &STR(SPECIFY A DATASET NAME IN VALID TSO SYNTAX)
    ISPEXEC SETMSG MSG(ISRZ001)
    EXIT CODE(12)
 END
/*-------------------------------------------------------------*/
/* ALLOC "GENDEF" AS AN OUTPUT FILE FOR LISTCAT                */
/*-------------------------------------------------------------*/
ALLOC F(GENDEF) REUSE TRACKS SPACE(1 15) UNIT(WORK) -
 LRECL(125) BLOCK(23375) RECFM(V B A) DSORG(PS)
/*-------------------------------------------------------------*/
/* LIST THE CATALOGUE ENTRY FOR THE MODEL DATASET              */
/*-------------------------------------------------------------*/
LISTCAT ENT(&OBJNAME) OUTFILE(GENDEF) ALL
/*-------------------------------------------------------------*/
/* HANDLE END OF FILE ON THE CATALOGUE LISTING                 */
/*-------------------------------------------------------------*/
ERROR                              /* CLIST ERROR ROUTINE       */ +
  DO
    SET RCODE = &LASTCC            /* SAVE RETURN CODE          */
    IF &RCODE = 400 THEN           /* END OF FILE REACHED?      */ +
      DO                           /* RC 400 (EOF) ROUTINE      */
         CLOSFILE GENDEF           /* CLOSE THE FILE            */
         SET &EOF = ON             /* SAVE EOF STATUS           */
         RETURN                    /* RETURN AFTER GETFILE STMT */
      END                          /* END RC 400 ROUTINE        */
    ELSE                           /* HARD ERROR, IT'S NOT EOF  */ +
      DO
        WRITE ERROR IN CLIST PROCESSING, ERROR CODE = &RCODE
        IF &DATATYPE(&RCODE) EQ NUM THEN /* CLIST ERROR         */ +
          DO
            ERROR OFF              /* CANCEL ERROR ACTION       */
          END
        EXIT CODE(16)              /* CLIST UNSUCCESSFUL        */
      END
  END                              /* END ERROR ROUTINE         */
/*-------------------------------------------------------------*/
/* LOOP THROUGH THE CATALOGUE LISTING PROCESSING SELECTED      */
/* LINES OF INTEREST.                                          */
/*-------------------------------------------------------------*/
OPENFILE GENDEF INPUT
SET &EOF = OFF
SET &XTSW = OFF
SET &RCI = 0
DO WHILE &EOF ¬= ON
  GETFILE GENDEF
  IF &EOF = OFF THEN DO
    SET &GENDEF = &GENDEF&STR(                                       )
    SET &GENDEF = &GENDEF&STR(                                       )
    SET &L = &LENGTH(&GENDEF)
    SET &RCI = &RCI + 1
    SET &UD  = &STR(&&GENDEF)
/*-------------------------------------------------------------*/
/* PICK UP DATA COMPONENT NAME                                 */
/*-------------------------------------------------------------*/
    IF &SUBSTR(9:14,&UD) = &STR(DATA--) THEN DO
      SET &COMP=&STR(DATA)
      SET &DN = &SUBSTR(18:62,&GENDEF)
    END
/*-------------------------------------------------------------*/
/* PICK UP INDEX COMPONENT NAME                                */
/*-------------------------------------------------------------*/
    ELSE IF &SUBSTR(5:11,&UD) = &STR(INDEX -) THEN DO
      SET &COMP=&STR(INDEX)
      SET &IN = &SUBSTR(18:62,&GENDEF)
    END
/*-------------------------------------------------------------*/
/* PICK UP CLUSTER NAME                                        */
/*-------------------------------------------------------------*/
    ELSE IF &SUBSTR(2:10,&UD) = &STR(CLUSTER -) THEN DO
      SET &CN = &SUBSTR(18:&L,&GENDEF)
    END
/*-------------------------------------------------------------*/
/* PICK UP KEYLENGTH, AVERAGE RECORD LENGTH, BUFFERSPACE       */
/* AND DATA CONTROLINTERVALSZIE                                */
/*-------------------------------------------------------------*/
    ELSE IF &SUBSTR(9:15,&UD) = &STR(KEYLEN-) THEN DO
      IF &COMP = &STR(DATA) THEN DO
        SET &KL = &SUBSTR(28:32,&GENDEF)
        SYSCALL BLANK KL
        SET &ARL = &SUBSTR(55:61,&GENDEF)
        SYSCALL BLANK ARL
        SET &BFS = &SUBSTR(85:90,&GENDEF)
        SYSCALL BLANK BFS
        SET &DCI = &SUBSTR(115:119,&GENDEF)
        SYSCALL BLANK DCI
      END
/*-------------------------------------------------------------*/
/* PICK UP INDEX CONTROLINTERVALSIZE                           */
/*-------------------------------------------------------------*/
      ELSE DO
        SET &ICI = &SUBSTR(115:119,&GENDEF)
        SYSCALL BLANK ICI
      END
    END
/*-------------------------------------------------------------*/
/* PICK UP NUMBER OF EXTENTS                                   */
/*-------------------------------------------------------------*/
    ELSE IF &SUBSTR(67:74,&GENDEF) = &STR(EXTENTS-) && &XTSW = OFF +
    THEN DO
       SET &XTSW = ON
       IF &SUBSTR(88:88,&GENDEF) ¬= &STR(-) THEN +
          SET &XT = &SUBSTR(88:90,&GENDEF)
       ELSE IF &SUBSTR(89:89,&GENDEF) = &STR(-) THEN +
          SET &XT = &SUBSTR(90:90,&GENDEF)
       ELSE SET &XT = &SUBSTR(89:90,&GENDEF)
    END
/*-------------------------------------------------------------*/
/* PICK UP RELATIVE KEY POSITION AND MAXIMUM RECORD LENGTH     */
/*-------------------------------------------------------------*/
    ELSE IF &SUBSTR(9:12,&UD) = &STR(RKP-) THEN DO
      IF &COMP = &STR(DATA) THEN DO
        SET &RKP = &SUBSTR(28:32,&GENDEF)
        SYSCALL BLANK RKP
        SET &MRL = &SUBSTR(55:61,&GENDEF)
        SYSCALL BLANK MRL
      END
    END
/*-------------------------------------------------------------*/
/* PICK UP SHROPTIONS, SPEED/RECOVERY, CLUSTER TYPE,           */
/* IMBED AND REPLICATE OPTIONS.                                */
/*-------------------------------------------------------------*/
    ELSE IF &SUBSTR(9:16,&UD) = &STR(SHROPTNS) THEN DO
      IF &COMP = &STR(DATA) THEN DO
        SET &SHR = &SUBSTR(9:21,&GENDEF)
        SET &SHR = &STR(SHAREOPTIONS)&SUBSTR(9:13,&SHR)
        SET &SPD = &SUBSTR(25:32,&GENDEF)
        SET &IDX = &SUBSTR(67:76,&GENDEF)
        IF &IDX = &STR(INDEXED) THEN SET &TYPE = K
        IF &IDX = &STR(NONINDEXED) THEN SET &TYPE = E
        IF &IDX = &STR(NUMBERED) THEN SET &TYPE = R
        SET &IMB = &SUBSTR(96:102,&GENDEF)
        SET &RPL = &SUBSTR(110:119,&GENDEF)
        IF &RPL = &STR(NOREPLICAT) +
        THEN SET &RPL = &RPL&STR(E)
      END
    END
/*-------------------------------------------------------------*/
/* PICK UP REUSE AND SPANNED OPTIONS                           */
/*-------------------------------------------------------------*/
    ELSE IF &SUBSTR(9:17,&UD) = &STR(UNORDERED) THEN DO
      IF &COMP = &STR(DATA) THEN DO
        IF &SYSINDEX(NOREUSE,&GENDEF,1) > 0 +
        THEN SET &REU = &STR(NOREUSE)
        ELSE SET &REU = &STR(REUSE)
        IF &SYSINDEX(NONSPANNED,&GENDEF,1) > 0 +
        THEN SET &REU = &STR(NONSPANNED)
        ELSE SET &REU = &STR(SPANNED)
      END
    END
/*-------------------------------------------------------------*/
/* PICK UP CI FREESPACE PERCENT                                */
/*-------------------------------------------------------------*/
    ELSE IF &SUBSTR(9:18,&UD) = &STR(REC-INSERT) THEN DO
      IF &COMP = &STR(DATA) THEN DO
        SET &FCI = &SUBSTR(59:61,&GENDEF)
        SYSCALL BLANK FCI
      END
    END
/*-------------------------------------------------------------*/
/* PICK UP CA FREESPACE PERCENT                                */
/*-------------------------------------------------------------*/
    ELSE IF &SUBSTR(9:18,&UD) = &STR(REC-UPDATE) THEN DO
      IF &COMP = &STR(DATA) THEN DO
        SET &FCA = &SUBSTR(59:61,&GENDEF)
        SYSCALL BLANK FCA
      END
    END
/*-------------------------------------------------------------*/
/* PICK UP SPACE ALLOCATION TYPE                               */
/*-------------------------------------------------------------*/
    ELSE IF &SUBSTR(9:18,&UD) = &STR(SPACE-TYPE) THEN DO
      IF &COMP = &STR(DATA) THEN DO
        SET &SPT = &SUBSTR(25:32,&GENDEF)
        SYSCALL BLANK SPT
        IF &SPT = CYLINDER THEN SET &SPT = CYLINDERS
        IF &SPT = TRACK    THEN SET &SPT = TRACKS
      END
    END
/*-------------------------------------------------------------*/
/* PICK UP PRIMARY SPACE QUANTITY                              */
/*-------------------------------------------------------------*/
    ELSE IF &SUBSTR(9:17,&UD) = &STR(SPACE-PRI) THEN DO
      IF &COMP = &STR(DATA) THEN DO
        SET &SPP = &SUBSTR(28:32,&GENDEF)
        SYSCALL BLANK SPP
      END
    END
/*-------------------------------------------------------------*/
/* PICK UP SECONDARY SPACE QUANTITY                            */
/*-------------------------------------------------------------*/
    ELSE IF &SUBSTR(9:17,&UD) = &STR(SPACE-SEC) THEN DO
      IF &COMP = &STR(DATA) THEN DO
        SET &SPS = &SUBSTR(28:32,&GENDEF)
        SYSCALL BLANK SPS
      END
    END
/*-------------------------------------------------------------*/
/* PICK UP DATA AND INDEX COMPONENT VOLSERS                    */
/*-------------------------------------------------------------*/
    ELSE IF &SUBSTR(9:15,&UD) = &STR(VOLSER-) THEN DO
      IF &COMP = &STR(DATA) THEN DO
        SET &VD = &SUBSTR(27:32,&GENDEF)
      END
      ELSE DO
        SET &VI = &SUBSTR(27:32,&GENDEF)
      END
    END
  END
  ELSE DO
  END
END
ERROR OFF
/*-------------------------------------------------------------*/
/* CALCULATE TOTAL SPACE REQUIRED FOR CLUSTER                  */
/*-------------------------------------------------------------*/
SET &SPP = &SPP + (&SPS * &XT)
FREE  F(GENDEF)
 ISREDIT (DEST) = LINENUM .ZDEST
 SET OB = &STR((
 SET CB = )
 SET CT = &NRSTR(-)
/*-------------------------------------------------------------*/
/* GENERATE "DEFINE CLUSTER( -"                                */
/*-------------------------------------------------------------*/
 SET LINE = &NRSTR(&STR(  DEFINE CLUSTER)&OB&STR( )&CT )
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
 SET DEST = &DEST + 1
/*-------------------------------------------------------------*/
/* GENERATE "NAME(CLUSTER-NAME) - "                            */
/*-------------------------------------------------------------*/
 SET &CCB = )
 SET &CCT = &NRSTR(-)
 SET &COB = &STR((
 SET &LINE = &STR(NAME)
 SET &LINE = &LINE&STR(&COB)
 SET &LINE = &LINE&CN
 SET &LINE = &LINE&CCB
 SET &LINE = &LINE&STR( )
 SET &LINE = &LINE&STR(&CCT)
 SET &LINE = &NRSTR(&STR(        )&LINE)
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
/*-------------------------------------------------------------*/
/* GENERATE "        RECORDSIZE(XX XX) - "                     */
/*-------------------------------------------------------------*/
 SET DEST = &DEST + 1
 SET LINE = &STR(        RECORDSIZE(&ARL &MRL) - )
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
/*-------------------------------------------------------------*/
/* GENERATE "        SPACE-TYPE(PRI SEC) - "                   */
/*-------------------------------------------------------------*/
 SET DEST = &DEST + 1
 SET LINE = &STR(        &SPT(&SPP &SPS)       - )
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
/*-------------------------------------------------------------*/
/* GENERATE "        (NON)INDEXED SHR(N N) (REUSE) - "         */
/*-------------------------------------------------------------*/
 SET DEST = &DEST + 1
 SET LINE = &STR(        &IDX &SHR &REU   - )
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
/*-------------------------------------------------------------*/
/* GENERATE "        (NON)SPANNED (NO)IMBED                    */
/*                   (NO)REPLICATE UNIQUE - "                  */
/*-------------------------------------------------------------*/
 SET DEST = &DEST + 1
 SET LINE = &STR(        &SPA &IMB &RPL UNIQUE  - )
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
/*-------------------------------------------------------------*/
/* GENERATE "        FREESPACE(N N) - "                        */
/*-------------------------------------------------------------*/
 SET DEST = &DEST + 1
 SET LINE = &STR(        FREESPACE(&FCI &FCA)   - )
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
 IF &TYPE = K THEN DO
/*-------------------------------------------------------------*/
/* GENERATE "        KEYS(N N) SPEED/RECOVERY - "              */
/*-------------------------------------------------------------*/
 SET DEST = &DEST + 1
 SET LINE = &STR(        KEYS(&KL &RKP) &SPD   - )
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
 END
 ELSE DO
/*-------------------------------------------------------------*/
/* GENERATE "        SPEED/RECOVERY - "                        */
/*-------------------------------------------------------------*/
 SET DEST = &DEST + 1
 SET LINE = &STR(        &SPD                  - )
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
 END
/*-------------------------------------------------------------*/
/* GENERATE "        BUFFERSPACE(NNNN) - "                     */
/*-------------------------------------------------------------*/
 SET DEST = &DEST + 1
 SET LINE = &STR(        BUFFERSPACE(&BFS)     - )
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
/*-------------------------------------------------------------*/
/* GENERATE "        ) -                 "                     */
/*-------------------------------------------------------------*/
 SET DEST = &DEST + 1
 SET LINE = &NRSTR(&STR(         )&CB&STR( )&CT)
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
/*-------------------------------------------------------------*/
/* GENERATE "        DATA(NAME(DATACOMPONENTNAME) - "          */
/*-------------------------------------------------------------*/
 SET DEST = &DEST + 1
 SET LINE = &NRSTR(&STR(   DATA)&OB&STR(NAME)&OB&DN&CB&STR( )&CT)
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
/*-------------------------------------------------------------*/
/* GENERATE "        CISZ(NNNN) - "                            */
/*-------------------------------------------------------------*/
 SET DEST = &DEST + 1
 SET LINE = &STR(        CISZ(&DCI)        - )
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
/*-------------------------------------------------------------*/
/* GENERATE "        VOLUME(VOLSER) - "                        */
/*-------------------------------------------------------------*/
 SET DEST = &DEST + 1
 SET LINE = &NRSTR(        VOLUME(&VD) - )
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
 IF &TYPE ¬= K THEN GOTO SKIPIDX
/*-------------------------------------------------------------*/
/* GENERATE "        ) -                 "                     */
/*-------------------------------------------------------------*/
 SET DEST = &DEST + 1
 SET LINE = &NRSTR(&STR(         )&CB&STR( )&CT)
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
/*-------------------------------------------------------------*/
/* GENERATE "        INDEX(NAME(INDEXCOMPONENTNAME) - "        */
/*-------------------------------------------------------------*/
 SET DEST = &DEST + 1
 SET LINE = &NRSTR(&STR(  INDEX)&OB&STR(NAME)&OB&IN&CB&STR( )&CT)
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
/*-------------------------------------------------------------*/
/* GENERATE "        CISZ(NNNN) - "                            */
/*-------------------------------------------------------------*/
 SET DEST = &DEST + 1
 SET LINE = &STR(        CISZ(&ICI)        - )
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
/*-------------------------------------------------------------*/
/* GENERATE "        VOLUME(VOLSER) - "                        */
/*-------------------------------------------------------------*/
 SET DEST = &DEST + 1
 SET LINE = &NRSTR(        VOLUME(&VI) - )
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
/*-------------------------------------------------------------*/
/* GENERATE "        )                   "                     */
/*-------------------------------------------------------------*/
 SKIPIDX:+
 SET DEST = &DEST + 1
 SET LINE = &NRSTR(&STR(         )&CB&STR( ))
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
EXIT CODE(0)
/*-------------------------------------------------------------*/
/* SUBPROCEDURE TO TRANSLATE DASHES TO BLANKS                  */
/*-------------------------------------------------------------*/
BLANK: PROC 1 &OLDSTRG
SYSREF &OLDSTRG
SET &NEWSTRG =
SET &LSTRG = &LENGTH(&STR(&OLDSTRG))
SET &CURR = 0
DO UNTIL &CURR = &LSTRG
  SET &CURR = &CURR + 1
  IF &CURR > &LSTRG THEN GOTO CHGCHARX
  SET &NEW = &SUBSTR(&CURR:&CURR,&OLDSTRG)
  IF &SUBSTR(&CURR:&CURR,&STR(&OLDSTRG)) ¬= &STR(-) +
  THEN GOTO CHGCHARC
  SET &NEW = &STR( )
CHGCHARC: SET &NEWSTRG = &STR(&NEWSTRG&NEW)
END
CHGCHARX: SET &OLDSTRG = &NEWSTRG
END
./       ADD   NAME=GENJCL
 ISREDIT MACRO (DSN TSO PARMS) NOPROCESS
 ISPEXEC CONTROL ERRORS RETURN
 IF &STR(&PARMS) ¬= THEN DO
    SET ZEDSMSG = &STR(INVALID PARAMETERS)
    SET ZEDLMSG = &STR(SPECIFY A DATASET NAME IN VALID TSO +
    SYNTAX ONLY)
    ISPEXEC SETMSG MSG(ISRZ001)
    EXIT CODE(12)
 END
 IF &STR(&TSO) = OR &STR(&TSO) = TSO THEN
 ELSE DO
    SET ZEDSMSG = &STR(INVALID PARAMETER)
    SET ZEDLMSG = &STR(THE ONLY VALID PARAMETER FOR THIS +
    COMMAND IS "TSO")
    ISPEXEC SETMSG MSG(ISRZ001)
    EXIT CODE(12)
 END
 ISREDIT PROCESS DEST
 IF &STR(&DSN) = THEN DO
    SET ZEDSMSG = &STR(SPECIFY A DATASET)
    SET ZEDLMSG = &STR(SPECIFY A DATASET NAME IN VALID TSO SYNTAX)
    ISPEXEC SETMSG MSG(ISRZ001)
    EXIT CODE(12)
 END
 SET DSNINFO = &STR(&DSN)
 IF &SYSDSN(&DSNINFO) = &STR(DATASET NOT FOUND) THEN DO
    WRITE &SYSDSN(&DSNINFO)
    SET ZEDSMSG = &STR(DATASET NOT FOUND)
    SET ZEDLMSG = &STR(SPECIFY A DATASET NAME IN VALID TSO SYNTAX)
    ISPEXEC SETMSG MSG(ISRZ001)
    EXIT CODE(12)
 END
 IF &SUBSTR(1:1,&DSN) ¬= &STR(') THEN DO
    SET DSN = &STR(&SYSPREF..&DSN)
 END
 ELSE DO
    SET &DSN = &SUBSTR(2:&LENGTH(&DSN)-1,&DSN)
 END
 LISTDSI &DSNINFO  NORECALL DIRECTORY
 IF &SYSUNITS = &STR(TRACK) THEN SET &ALCTYPE = &STR(TRK)
 ELSE IF &SYSUNITS = &STR(CYLINDER) THEN SET &ALCTYPE = &STR(CYL)
 ELSE SET &ALCTYPE = &SYSBLKSIZE
 IF &STR(&TSO) = TSO THEN GOTO FLOWTSO
FLOWIT: +
 IF &SUBSTR(1:1,&SYSVOLUME) = &STR(D) +
 THEN SET &SYSUNIT = &STR(DEVL)
 IF &SUBSTR(1:3,&SYSVOLUME) = &STR(WOR) +
 THEN SET &SYSUNIT = &STR(WORK)
 IF &SUBSTR(1:1,&SYSVOLUME) = &STR(P) +
 THEN SET &SYSUNIT = &STR(PROD)
 IF &STR(&SYSADIRBLK) ¬= THEN SET DIR = &STR(,&SYSADIRBLK)
 ELSE SET DIR =
 ISREDIT (DEST) = LINENUM .ZDEST
 SET LINE = &STR(//GENJCL  DD  DSN=&DSN,)
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
 SET DEST = &DEST + 1
 SET LINE = &STR(//            DISP=(NEW,CATLG,DELETE),)
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
 SET DEST = &DEST + 1
 SET LINE = &STR(//            UNIT=&SYSUNIT,)
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
 SET DEST = &DEST + 1
 SET LINE = &STR(//            DCB=(LRECL=&SYSLRECL,+
 BLKSIZE=&SYSBLKSIZE,RECFM=&SYSRECFM,DSORG=&SYSDSORG),)
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
 SET DEST = &DEST + 1
 IF &ALCTYPE = &STR(CYL) THEN DO
    SET &SYSPRIMARY = &SYSPRIMARY * 15
    SET &SYSSECONDS = &SYSSECONDS * 15
    SET &ALCTYPE    = &STR(TRK)
 END
 SET LINE = &STR(//            SPACE=(&ALCTYPE,(&SYSPRIMARY,+
 &SYSSECONDS.&DIR),RLSE))
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
 EXIT CODE(0)
FLOWTSO: +
 IF &LENGTH(&SYSRECFM) = 1 THEN SET &RFM = &SYSRECFM
 ELSE IF &LENGTH(&SYSRECFM) = 2 THEN DO
    SET &RFM = &SUBSTR(1:1,&SYSRECFM)&STR( )&SUBSTR(2:2,&SYSRECFM)
 END
 ELSE IF &LENGTH(&SYSRECFM) = 3 THEN DO
    SET &RFM = &SUBSTR(1:1,&SYSRECFM)&STR( )&SUBSTR(2:2,&SYSRECFM)
    SET &RFM = &RFM&STR( )&SUBSTR(3:3,&SYSRECFM)
 END
 IF &SYSADIRBLK = THEN SET DIR =
 ELSE SET DIR = &STR(DIR(&SYSADIRBLK))
 ISREDIT (DEST) = LINENUM .ZDEST
 SET LINE = &STR(ALLOC DA('&DSN') NEW CATALOG +)
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
 SET DEST = &DEST + 1
 SET LINE = &STR(        UNIT(&SYSUNIT) LRECL(&SYSLRECL) +)
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
 SET DEST = &DEST + 1
 SET LINE = &STR(        BLKSIZE(&SYSBLKSIZE) +)
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
 SET DEST = &DEST + 1
 SET LINE = &STR(        RECFM(&RFM) &DIR DSORG(&SYSDSORG) +)
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
 SET DEST = &DEST + 1
 SET LINE = &STR(        SPACE(&SYSPRIMARY &SYSSECONDS) TRACK)
 ISREDIT LINE_AFTER &DEST = DATALINE '&LINE'
 EXIT CODE(0)
./       ADD   NAME=GENPRINT
PROC 0 DSN() GPCLASS(A) GPDEST() NOBRO GPCOPIES GPFOLD DEOPT(P) NOTELL -
NODEL DEBUG EDIT GPVOL() CC() NOED
 IF &DEBUG = DEBUG THEN CONTROL LIST CONLIST SYMLIST PROMPT NOFLUSH
    CONTROL NOMSG
    IF &GPVOL = THEN ALLOC F(DD1) DS(&DSN) SHR REUSE
                ELSE ALLOC F(DD1) DS(&DSN) SHR VOL(&GPVOL) REUSE
    SET &GNPDSN = &DSN
    LISTDSI &DSN
    IF &SYSDSORG = PO THEN GOTO FREEIT
    OPENFILE DD1
ERROR DO
    IF &NOTELL = NOTELL THEN GOTO EXIT4
    WRITE &DSN IS EMPTY
    GOTO EXIT4
    END
    GETFILE DD1
    CLOSFILE DD1
ERROR OFF
FREEIT:  +
    FREE F(DD1)
    CONTROL MSG
    IF &CC = X THEN SET &CC =
    IF &EDIT = EDIT THEN GOTO EDITIT
    IF &NOBRO = NOBRO THEN GOTO DISP
     IF &GPVOL = THEN ISPEXEC BROWSE DATASET(&DSN)
                 ELSE ISPEXEC BROWSE DATASET(&DSN) VOLUME(&GPVOL)
     SET &GNPOPT = &DEOPT
DISP: ISPEXEC DISPLAY PANEL(GENPRINT)
     IF  &LASTCC  = 0 +
     THEN DO
           IF &GNPOPT = P THEN GOTO PRINT
           IF &GNPOPT = B THEN GOTO BROWSE
           IF &GNPOPT = D THEN GOTO DELETE
           IF &GNPOPT = K THEN GOTO EXIT
           IF &GNPOPT = E THEN GOTO EDITIT
           IF &GNPOPT = R THEN GOTO RENAMEIT
           GOTO DISP
     END
     ELSE GOTO EXIT
     GOTO DISP
PRINT: +
     IF &GPCLASS ¬= THEN SET &POSOC = &GPCLASS
     IF &GPDEST  ¬= THEN DO
                         SET &PODEST = &GPDEST
                         ISPEXEC VPUT PODEST
                         END
     IF &GPCOPIES ¬= THEN SET &POCOPY = &GPCOPIES
     IF &GPFOLD ¬= THEN SET &FOLD = &GPFOLD
     SET &HEAD = NO
     SET &PODSN = &DSN
     SET &POCC = &CC
     ISPEXEC VPUT (DSN)
     ISPEXEC VPUT (PODSN) PROFILE
     ISPEXEC VGET (POSOC POOUTD) PROFILE
     ISPEXEC SELECT PANEL(PRINTOFF)
     ISPEXEC VPUT (HEAD PODSN POSOC POCOPY FOLD POCC POOUTD) PROFILE
     GOTO DISP
BROWSE: +
     IF &GPVOL = THEN ISPEXEC BROWSE DATASET(&DSN)
                 ELSE ISPEXEC BROWSE DATASET(&DSN)  VOLUME(&GPVOL)
     GOTO DISP
RENAMEIT: +
     IF &NODEL = NODEL THEN GOTO DISP
     RENAME &DSN &ZGPRDSN
     ISPEXEC VPUT ZGPRDSN PROFILE  /* SAVE THE DSN FOR FUTURE USE */
     SET &SHORT = RENAMED
     SET &LONG = &STR(DATASET &DSN RENAMED TO &ZGPRDSN)
     SET &DSN = &ZGPRDSN
     ISPEXEC VPUT (DSN SHORT LONG) PROFILE
     ISPEXEC  SETMSG MSG(AOST010)
     GOTO DISP
EDITIT: +
     IF &NOED = NOED THEN GOTO DISP
     IF &GPVOL = THEN ISPEXEC EDIT DATASET(&DSN)
                 ELSE ISPEXEC EDIT DATASET(&DSN) VOLUME(&GPVOL)
     GOTO DISP
DELETE: +
     IF &NODEL = NODEL THEN GOTO DISP
     CONTROL NOMSG
     DELETE &DSN
EXIT: +
      CONTROL NOMSG
      FREE F(DD1)
      EXIT
EXIT4: +
      ERROR OFF
      CONTROL NOMSG
      FREE F(DD1)
      EXIT CODE(4)
BADTYPE: +
    WRITE *** DATE DSORG INVALID FOR PRINT
    GOTO EXIT4
ERROR: ERROR OFF
       CLOSFILE DD1
       FREE F(DD1)
./       ADD   NAME=GENSUB
PROC 1 DSN

/* ================================================================= */
/* THIS CLIST IS A GENERALIZED SUBMIT CLIST WHICH DISPLAYS ISPF      */
/* PANEL GENSUB AND ALLOWS THE USER TO CANCEL THE SUBMIT; DO THE     */
/* SUBMIT; EDIT THE JOB PRIOR TO SUBMIT; OR BROWSE THE JOB PRIOR     */
/* TO SUBMIT.                                                        */
/* ================================================================= */


SET &DSNX = &DSN
ISPEXEC VPUT DSNX
ISPEXEC SELECT PGM(FIXUPDSN) PARM(DSNX)
ISPEXEC VGET DSNX

ALLOC F(GENSUB) REUSE DA('&DSNX') SHR
ISPEXEC SELECT PGM(RETDSNVL) PARM(GENSUB)
ISPEXEC VGET (DSN VOL)
FREE F(GENSUB)

DISPLAY: +
ISPEXEC DISPLAY PANEL(GENSUB)

     IF &LASTCC NE 0 THEN GOTO EXIT

IF &SOPT = C THEN GOTO EXIT

IF &SOPT = S THEN DO
                    SUBMIT '&DSNX'
                    GOTO EXIT
                    END

IF &SOPT = E THEN ISPEXEC EDIT DATASET('&DSNX') VOLUME(&VOL)

IF &SOPT = B THEN ISPEXEC BROWSE DATASET('&DSNX') VOLUME(&VOL)

GOTO DISPLAY    /* ASK THE QUESTION AGAIN */

EXIT: +
      END
./       ADD   NAME=INDEX
/* JIMLANE 09/02/87 11:28:57 JIMLANE.CLIST(INDEX)
ISREDIT MACRO (MODE,DEBUG)
CONTROL NOFLUSH
SET CAPS = NO
IF &DEBUG ¬= THEN +
 IF &SUBSTR(1,&DEBUG) = D THEN CONTROL L C S FLUSH
IF &MODE ¬= THEN DO
 IF &SUBSTR(1,&MODE) = C THEN SET CAPS = YES
 IF &SUBSTR(1,&MODE) = R THEN DO
  SET &CS =
  SET &LS =
  ISPEXEC VPUT (LS,CS)
  EXIT CODE(0)
 END
END
ISPEXEC VGET (LS,CS)
IF &CS =  THEN DO
 ISREDIT (LS,CS) = CURSOR
 IF &CS = 0 THEN DO
  ISREDIT LINE_BEFORE .ZCSR = MSGLINE +
     'CURSOR POSITION OUTSIDE OF DATA'
  EXIT CODE(4)
 END
 ISREDIT FIND ' ' PREV
 ISREDIT FIND P'@'
 ISREDIT (LS,CS) = CURSOR
 ISPEXEC VPUT (LS,CS)
END
ELSE DO
 ISREDIT FIND ' '
 ISREDIT FIND P'¬' PREV
 ISREDIT (LE,CE) = CURSOR
 IF &LE ¬= &LS THEN DO
  ISREDIT LINE_BEFORE .ZCSR = MSGLINE +
     'INDEX ITEM MUST BE ON ONE LINE'
  EXIT CODE(4)
 END
 IF &CE < &CS THEN DO
  ISREDIT LINE_BEFORE .ZCSR = MSGLINE +
     'INDEX ITEM START MUST BE MARKED BEFORE END'
  SET CS =
  ISPEXEC VPUT (CS)
 END
 ELSE DO
  ISREDIT (ILIN) = LINE .ZCSR
  SET &ZTR = &SUBSTR(&CS:&CE,&NRSTR(&ILIN))
  ISREDIT FIND ':' PREV 1
  SET RC = &LASTCC
  ISREDIT (LC) = LINENUM .ZCSR
  SET &LC = &LC + 1
  ISREDIT LINE_AFTER .ZCSR = ':I1.&ZTR'
  ISREDIT LABEL (LC) = .IPTR
  IF &CAPS = NO THEN +
   ISREDIT CHANGE P'>' P'<' ALL .IPTR .IPTR
  ISREDIT LINE_AFTER .ZCSR = NOTELINE 'INDEX MARK &ZTR'
  SET DONE = NO
  DO WHILE &DONE = NO
   ISREDIT FIND ':P' NEXT
   IF &LASTCC ¬= 0 THEN SET &DONE = YES
   IF &DONE = NO THEN DO
    ISREDIT FIND '&ZTR' NEXT
    SET &RC = &LASTCC
    IF &RC = 0 THEN DO
     ISREDIT FIND ':P' PREV
     ISREDIT (LPAR) = CURSOR
     ISREDIT LINE_BEFORE .ZCSR = ':I1.&ZTR'
     ISREDIT LABEL (LPAR) = .IPTR
     ISREDIT LINE_BEFORE .ZCSR = NOTELINE 'INDEX MARK &ZTR'
     IF &CAPS = NO THEN +
      ISREDIT CHANGE P'>' P'<' ALL .IPTR .IPTR
     SET &LPAR = &LPAR + 2
     ISREDIT CURSOR = (LPAR)
    END
    ELSE SET &DONE = YES
   END
  END
  ISREDIT RESET CHG
  ISREDIT CURSOR = (LE,CE)
  SET CS =
  ISPEXEC VPUT (CS)
 END
END
EXIT CODE(0)
./       ADD   NAME=JOBCARD
ISREDIT MACRO
ISREDIT (DSN)  = DATASET
ISREDIT (MBR)  = MEMBER
IF &MBR ¬= &Z THEN SET DSN = &STR(&DSN.(&MBR))
SET &CLASS = &STR(A)
IF &SYSUID = &STR(LANEJ)     THEN SET &CLASS = &STR(Z)
IF &SYSUID = &STR(COPPW)     THEN SET &CLASS = &STR(Z)
IF &SYSUID = &STR(DORDEAN)   THEN SET &CLASS = &STR(Z)
IF &SYSUID = &STR(TITKAIS)   THEN SET &CLASS = &STR(Z)
IF &SYSUID = &STR(CHOWA)     THEN SET &CLASS = &STR(Z)
IF &SYSUID = &STR(DB2STAT)   THEN SET &CLASS = &STR(Z)
IF &SYSUID = &STR(DB2STAT)   THEN +
ISREDIT LINE_AFTER 0      = +
'//PROCLIB DD DSN=INT.DIST.PROCLIB,DISP=SHR                       '
ISREDIT LINE_AFTER 0      = +
'//*-------------------------------------------------------------*'
ISREDIT LINE_AFTER 0      = +
'//*   THIS JCL IS IN:        &DSN                               *'
ISREDIT LINE_AFTER 0      = +
'//*-------------------------------------------------------------*'
ISREDIT LINE_AFTER 0        = +
'//*FOURUP   OUTPUT PAGEDEF=U46D,FORMDEF=U46D DEFAULT=YES  '
ISREDIT LINE_AFTER 0        = +
'//*TWOUP    OUTPUT PAGEDEF=A26D,FORMDEF=A26D DEFAULT=YES  '
ISREDIT LINE_AFTER 0        = +
'//*PORTRAIT OUTPUT PAGEDEF=A16D,FORMDEF=A16D DEFAULT=YES  '
IF &SYSUID = LANEJ THEN +
ISREDIT LINE_AFTER 0      = +
'//*JOBLIB DD DSN=TECTSG.ISPFJCL.LOAD,DISP=SHR '
ISREDIT LINE_AFTER 0 =+
'//         MSGLEVEL=(1,1),MSGCLASS=X,CLASS=&CLASS  '
IF &SYSUID = LANEJ THEN +
ISREDIT LINE_AFTER 0 =+
'//*        USER=IBMUSER,                  '
ISREDIT LINE_AFTER 0 =+
'//&SYSUID     JOB &SYSUID,&MBR,NOTIFY=&SYSUID.,REGION=6M,  '
EXIT
EXIT
./       ADD   NAME=LC
PROC 1 &OBJNAME
ALLOC F(CATOUT) REUSE TRACKS SPACE(1 15) UNIT(WORK) -
 LRECL(125) BLOCK(23375) RECFM(V B A) DSORG(PS)
ISPEXEC LMINIT DATAID(CDSN) DDNAME(CATOUT)
IF &SUBSTR(2:5,&OBJNAME) = &STR(UCAT) THEN +
LISTCAT ENT(&OBJNAME) CAT(&OBJNAME) OUTFILE(CATOUT) ALL
ELSE LISTCAT ENT(&OBJNAME) OUTFILE(CATOUT) ALL
ISPEXEC BROWSE DATAID(&CDSN) PANEL(CATBRO)
END
./       ADD   NAME=MI
ISREDIT MACRO
CONTROL NOCAPS
ISPEXEC DISPLAY PANEL(MIPANEL)
IF &LASTCC = 0 THEN DO
   SET &SCC = 0
   DO WHILE (&SCC = 0)
      ISREDIT SEEK '&MIID'   NEXT
      SET &SCC = &LASTCC
      IF &SCC = 0 THEN DO
         IF &MIDIR = A | &MIDIR = AFTER THEN +
         ISREDIT LINE_AFTER .ZCSR  = '&MILINE'
         ELSE ISREDIT LINE_BEFORE .ZCSR  = '&MILINE'
      END
   END
END
ISREDIT TOP
END
./       ADD   NAME=NUMS
ISREDIT MACRO (START,COUNT,LEFT,BY,DIGITS) NOPROCESS
ISPEXEC CONTROL ERRORS RETURN
CONTROL ASIS
ISREDIT PROCESS DEST
ISREDIT (FR) = LINENUM .ZDEST
IF &BY = &Z THEN SET &BY = 1
SET &NUMBER = &START + (&COUNT * &BY) - 1
IF &DIGITS = &Z THEN SET &DIGITS = &LENGTH(&NUMBER)
AGAIN:+
SET &L = &LENGTH(&NUMBER)
IF &L < &DIGITS THEN DO
   SET &P = &DIGITS - &L
   IF &P = 1 THEN SET &N = &STR(0&NUMBER)
   IF &P = 2 THEN SET &N = &STR(00&NUMBER)
   IF &P = 3 THEN SET &N = &STR(000&NUMBER)
END
ELSE SET &N = &STR(&NUMBER)
ISREDIT LINE_AFTER .ZDEST = <&LEFT &N>
SET &NUMBER = &NUMBER - &BY
SET &COUNT = &COUNT - 1
IF &COUNT > 0 THEN GOTO AGAIN
EXIT CODE(0)
./       ADD   NAME=OGL
ISREDIT MACRO
ISREDIT (CHG) = DATA_CHANGED
IF &STR(&CHG) = &STR(YES) THEN ISREDIT SAVE
ISREDIT (CURDSN) = DATASET
ISREDIT (CURMEM) = MEMBER
IF &CURMEM ¬= &Z THEN SET &DSN = &CURDSN.(&CURMEM.)
ELSE SET &DSN = &CURDSN
ALLOC F(SYSIN) DA('&DSN') SHR REUSE
IF &SYSDSN(&CURMEM..LIST) ¬= &STR(OK) +
THEN ALLOC DA(&CURMEM..LIST) NEW CATALOG +
             UNIT(DEVL) LRECL(133) +
             BLKSIZE(0)    +
             F(SYSPRINT) REUSE +
             RECFM(F B A)  DSORG(PS) +
             SPACE(1 15) TRACK
ELSE ALLOC DA(&CURMEM..LIST) FI(SYSPRINT) SHR REUSE
ALLOC F(SYMBOLIC) DUMMY REUSE
ALLOC F(SEGDD)    DA('SYS1.PSEGLIB') SHR REU
ALLOC F(FONTDD)   DA('SYS1.FONTLIBB') SHR REU
ALLOC F(OVRLIB)   DA('SYS1.OVERLIB') SHR REU
OUTDES OGL FORMDEF(OGL) REU
ALLOC F(SAMPLE) SYS(F) OUTDES(OGL) REU
CALL 'SYS1.LINKLIB(DZIOVRLY)'
FREE F(SAMPLE)
%GENPRINT DSN(&CURMEM..LIST) NODEL DEOPT(K)
FREE F(SYSIN)
END
./       ADD   NAME=PPFA
ISREDIT MACRO
ISREDIT (CHG) = DATA_CHANGED
IF &STR(&CHG) = &STR(YES) THEN ISREDIT SAVE
ISREDIT (CURDSN) = DATASET
ISREDIT (CURMEM) = MEMBER
IF &CURMEM ¬= &Z THEN SET &DSN = &CURDSN.(&CURMEM.)
ELSE SET &DSN = &CURDSN
ALLOC F(SYSIN) DA('&DSN') SHR REUSE
IF &SYSDSN(&CURMEM..LIST) ¬= &STR(OK) +
THEN ALLOC DA(&CURMEM..LIST) NEW CATALOG +
             UNIT(DEVL) LRECL(133) +
             BLKSIZE(0)    +
             F(SYSPRINT) REUSE +
             RECFM(F B A)  DSORG(PS) +
             SPACE(1 15) TRACK
ELSE ALLOC DA(&CURMEM..LIST) FI(SYSPRINT) SHR REUSE
ALLOC F(FORMLIB) DA('SYS1.FDEFLIB') SHR REUSE
ALLOC F(PAGELIB) DA('SYS1.PDEFLIB') SHR REUSE
CALL 'SYS1.LINKLIB(AKQPPFA)'
%GENPRINT DSN(&CURMEM..LIST) NODEL DEOPT(K)
FREE F(SYSIN)
END
./       ADD   NAME=PRINTOFF
PROC 0 A() B() C() D()

CONTROL NOMSG PROMPT

         ISPEXEC  VGET (PODSN POSOC PODEST POOUTD)
         ISPEXEC  VPUT (POSOC PODEST) PROFILE

         SET &A = &STR(PRINTDS)
         SET &B = &STR(DA(&PODSN))
         SET &C = CLASS(&POSOC)
         SET &D = OUTDES(&POOUTD)
         IF &PODEST ¬= THEN SET &E = DEST(&PODEST)

/* ================================================================= */
/* EXECUTE THE PRINTDS  TSO COMMAND........                          */
/* ================================================================= */

         &A &B &C &D &E

         SET &SHORT = &STR(PRINTED)
         SET &LONG  = &STR(&B HAS BEEN PRINTED)
         ISPEXEC VPUT (SHORT LONG)
         ISPEXEC SETMSG MSG(AOST010)
./       ADD   NAME=SUM
ISREDIT MACRO (START,END,DEBUG)
/* SUM MACRO. GIVE THE NUM OF NUMBERS IN SPECIFIED COLUMNS
/*  FROM TECHNICAL SUPPORT MAGAZINE MARCH/89
IF &DEBUG = DEBUG THEN CONTROL L C S
IF &START = &STR() | &END = &STR() | &START > &END THEN DO
   SET ZEDSMSG = INVALID PARAMETERS
   SET ZEDLMSG = BOTH START AND END COLUMNS MUST BE SPECIFIED
   ISPEXEC SETMSG MSG(ISRZ001)
   EXIT
END
CONTROL NOFLUSH
ISREDIT (NUMLINES) = LINENUM .ZLAST
SET &TOTL = 0
SET &NUMBERS = NO
DO WHILE &NUMLINES > 0
   ISREDIT (LINE) = LINE &NUMLINES
   SET NUM = &SUBSTR(&START:&END,&STR(&LINE))
   IF &DATATYPE(&NUM) = NUM THEN DO
      SET TOTL = &TOTL + &NUM
      SET NUMBERS = YES
   END
   SET NUMLINES = &NUMLINES - 1
END
IF &NUMBERS = YES THEN DO
   SET ZEDSMSG = &TOTL
   SET ZEDLMSG = TOTAL OF COLUMNS &START:&END IS &TOTL
   ISPEXEC SETMSG MSG(ISRZ000)
END
ELSE DO
   SET ZEDSMSG = NO NUMBERS
   SET ZEDLMSG = COLUMNS &START:&END CONTAIN NO NUMERIC VALUES
   ISPEXEC SETMSG MSG(ISRZ001)
END
EXIT
