//ASQCCCDF  JOB (X0041,QCC,ASQCC),'X-BRIAN COOK',
//   MSGCLASS=X,
//   MSGLEVEL=(1,1),CLASS=X,NOTIFY=ASQCC
/*JOBPARM L=99,SYSAFF=MVSB
//*
//*   TECH.XA.SOURCE       (CDF2)
//*
//JOBLIB   DD  DSN=SYS1.LINKLIB,DISP=SHR,
//         UNIT=SYSDA,VOL=SER=MVSTGT
//*
//ASM1    EXEC PGM=IEV90,REGION=2048K,
//        PARM='OBJECT,NODECK,TERM,XREF(SHORT)'
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR,
//         UNIT=SYSDA,VOL=SER=MVSTGT
//         DD  DSN=SYS1.AMODGEN,DISP=SHR,
//         UNIT=SYSDA,VOL=SER=MVSDLB
//         DD  DSN=SYS2.MACLIB,DISP=SHR   <== JDFMACS, SPFMACS
//SYSUT1   DD  SPACE=(CYL,(5,5)),
//             UNIT=SYSDA
//SYSTERM  DD  SYSOUT=*
//SYSPRINT DD  SYSOUT=*
//SYSPUNCH DD  DUMMY
//SYSLIN   DD   DSN=&&OBJSET,UNIT=SYSDA,SPACE=(CYL,(5,5)),
//             DISP=(MOD,PASS)
//SYSIN    DD  *
         PRINT OFF
         COPY SPFMACS
         PRINT ON
         PRINT NOGEN
CDF      TITLE 'CHANGE LOG'
***********************************************************************
*                                                                     *
* CHANGE LOG:                                                         *
*                                                                     *
*  8/05/88  BRIAN COOK     REVISED SAVE/LAST COMMAND LOGIC SO IT WORKS*
*                                                                     *
*  8/05/88  BRIAN COOK     REVISED SCREEN DISPLAY LOGIC TO ALLOW FOR  *
*                          LARGE-SCREEN CONSOLES AND SMALL-SCREEN     *
*                          TSO TUBES. (FIND 'BIGCONS' FOR DETAILS)    *
*                                                                     *
*  5/04/88  BRIAN COOK     FIXED ABEND0C4, DUE TO IEECRDCM MOVED TO   *
*                          CONSOLE PRIVATE AREA BY 2.2.0/DFP 2.3.0    *
*                                                                     *
*  4/27/88  BRIAN COOK     ALLOW SCROLL (FOR LARGE-SCREEN CONSOLES)   *
*                          (NOT COMPLETED)                            *
*                                                                     *
*  1/21/87  BRIAN COOK     COPIED FROM CBT FILE 6, CHANGES:           *
*                          1. ADDED SPFMACS                           *
*                          2. USE DYNAMIC PANEL INSTEAD OF TABLE      *
*                          3. REVISE AUTH CHECKING (F AUTH TO FIND)   *
*                          4. ADDED COMMAND SAVE/RESTORE              *
*                          5. ADDED BASE REGS FOR PGM AND DSECT       *
*                                                                     *
*                                                                     *
*  4/11/86 DAVID W.MEYER                                           DWM*
*    MADE PROGRAM REENTRANT                                        DWM*
*    LOAD ISPLINK AT EXECUTION TIME TO GET MOST CURRENT VERSION    DWM*
*    SEARCH FOR "CONSOLE" ASID AND DETERMINE ASID NUMBER           DWM*
*      (USED BY CROSS MEMORY SERVICES)                             DWM*
*    CODE SKIPPED LAST UCME - CORRECTED                            DWM*
*    CHANGED MASTER CONSOLE SEARCH TO USE MORE DIRECT APPROACH     DWM*
*    ADDED INACTIVE CONSOLE CHECK - IF SO, SKIP BUFFER MOVE AND    DWM*
*      TABLE BUILD                                                 DWM*
*    ADDED BUFFER LENGTH CHECK                                     DWM*
*    USED CONTROL BLOCK FIELDS TO CALCULATE LINE OFFSETS AND       DWM*
*      BUFFER LENGTH                                               DWM*
*    ADDED "CON" COMMAND TO SELECT OTHER CONSOLES                  DWM*
*                                                                     *
***********************************************************************
         EJECT
*                                                                     *
***********************************************************************
         TITLE 'CONSOLE DISPLAY FACILITY (CDF)'
***********************************************************************
*                                                                     *
*                                                                     *
*                 CONSOLE DISPLAY FACILITY (CDF)                      *
*                                                                     *
*                                                                     *
* ABSTRACT: DISPLAY MVS MASTER CONSOLE ACTIVITY AND PROCESS MVS       *
*           AND JES COMMANDS.                                         *
*                                                                     *
*                                                                     *
* FUNCTION: CDF IS DESIGNED TO OPERATE UNDER ISPF USING DISPLAY       *
*           MANAGEMENT SERVICES. IT PROVIDES A CONSOLE DISPLAY        *
*           SIMILAR TO JESOPER, DIDOCS,ETC.,        BUT SINCE IT      *
*           RUNS UNDER ISPF, IT CAN BE INVOKED WITH SPLIT-SCREEN.     *
*                                                                     *
*           CDF LOOKS AT VARIOUS MVS XA 2.1.3 CONTROL BLOCKS.      DWM*
*                                                                     *
*                                                                     *
* EXTERNAL REFERENCE: ISPLINK                                         *
*                                                                     *
*                                                                     *
* MACROS USED:                                                     DWM*
*              ABEND                                               DWM*
*              FREEMAIN                                            DWM*
*              GETMAIN                                             DWM*
*              LOAD                                                DWM*
*              DELETE                                              DWM*
*              SAVE                                                DWM*
*              RETURN                                              DWM*
*                                                                  DWM*
* DATA AREA                                                        DWM*
* MACROS USED:                                                     DWM*
*              CVT                                                 DWM*
*              IEECDCM                                             DWM*
*              IEECUCM                                             DWM*
*              IEFUCBOB                                            DWM*
*              IHAASVT                                             DWM*
*              IHAASCB                                             DWM*
*                                                                     *
* ATTRIBUTES:                                                         *
*              RENT,REUS                                           DWM*
*                                                                     *
***********************************************************************
         EJECT
CDF      CSECT
         SAVE  (14,12),,CDF_&SYSDATE                               DWM
         LR    R12,R15        SET UP BASE REGISTER 1               BC
         LA    R10,2048                   REGISTER 2               BC
         LA    R10,2048(R10,R12)          REGISTER 2               BC
         LR    R8,R13         SAVE HIGHER SAVE AREA ADDRESS        BC
         USING CDF,R12,R10                                         BC
         L     R0,GETMAINA    GET STORAGE LENGTH                   BC
         GETMAIN R,LV=(0)     GET STORAGE FOR CDFWORK              BC
         LR    R13,R1         SETUP CDFWORK REGISTER 1             BC
         LA    R11,2048                     REGISTER 2             BC
         LA    R11,2048(R11,R13)            REGISTER 2             BC
         USING CDFWORK,13,11                                       BC
         LR    R0,R1          AREA TO BE CLEARED                   DWM
         L     R1,GETMAINA    GET LENGTH TO BE CLEARED             BC
         XR    R15,R15        ZERO PAD AND FROM LENGTH             DWM
         MVCL  R0,R14         ZERO CDFWORK                         DWM
         MVC   CDFWT,CDFWORKT MOVE IN CDFWORK EYECATCHER           DWM
         MVC   BUFS,BUFST     MOVE IN BUFFER START EYECATCHER      DWM
         MVC   BUFE,BUFET     MOVE IN BUFFER END EYECATCHER        DWM
         ST    R8,4(,R13)     SAVE PREVIOUS SAVEAREA @             DWM
         ST    R13,8(,R8)     LINK SAVE AREAS                      BC
*                                                                  BC
         B     SHOWINIT                                            BC
*                                                                  BC
GETMAINA DC    A(WORKLEN)                                          BC
*                                                                  BC
*
*
*---------------------------------------------------------------------*
*                                                                     *
*                       PROGRAM INITIALIZATION                        *
*                                                                     *
*---------------------------------------------------------------------*
*
SHOWINIT DS    0H
*
         MVC   CMDLINE,BLANKS   CLEAR CMDLINES                     DWM
         LOAD  EP=ISPLINK       LOAD CURRENT ISPLINK MODULE        DWM
         ST    R0,ISPLINKA      SAVE ISPLINK ADDRESS               DWM
*
         MVC   F8(4),=F'8'
         L     R15,ISPLINKA
         CALL  (15),                                                   X
               (VCOPY,ZUSER,F8,USERID,SPFMOVE),                    DWM X
               VL,MF=(E,ISPARMS)
*                                                                  BC
*
         VDEFINE CDFDATA,3280
         XC    CDF#COFF(4),CDF#COFF
         VDEFINE CDF#COFF,4,FMT=FIXED
         MVC   CDF#CSRN(8),ZEROS
         VDEFINE CDF#CSRN,8
*        XC    CDF#CSRN(4),CDF#CSRN
*        VDEFINE CDF#CSRN,4,FMT=FIXED
         XC    CDF#DDEP(4),CDF#DDEP
         VDEFINE CDF#DDEP,4,FMT=FIXED
*
         VDEFINE CMDLINE,51
         VDEFINE LASTCMD1,51,OPT=COPY
         VDEFINE LASTCMD2,51,OPT=COPY
         VDEFINE LASTCMD3,51,OPT=COPY
         VDEFINE LASTCMD4,51,OPT=COPY
         VDEFINE LASTCMD5,51,OPT=COPY
         VDEFINE LASTCMD6,51,OPT=COPY
         VDEFINE LASTCMD7,51,OPT=COPY
         VDEFINE LASTCMD8,51,OPT=COPY
         VDEFINE LASTCMD9,51,OPT=COPY
*
*
*
* THESE FIELDS DEFINED BY ISPF FOR DYNAMIC SCREEN AREAS
*
*
         VDEFINE AREATYP,8
         VDEFINE COLS,4,FMT=FIXED
         VDEFINE ROWS,4,FMT=FIXED
         VDEFINE TLROW,4,FMT=FIXED
         VDEFINE TLCOL,4,FMT=FIXED
         VDEFINE ZVERB,8
         VDEFINE ZSCROLLA,4
         XC    ZSCROLLN(4),ZSCROLLN
         VDEFINE ZSCROLLN,4,FMT=FIXED,OPT=COPY
*
**  TITLE LINE VARIABLES
*
         VDEFINE CDFMST,6
         VDEFINE CDFS,3
         VDEFINE CDFI,3
         VDEFINE CDFC,4
         VDEFINE CDFN,2
         VDEFINE CDFU,3
*
** AUTH CHECKING DONE HERE                                         BC
*                                                                  BC
*                                                                  BC
** REMOVE THE ASTERISK FROM THE AUTH CHECK DESIRED                 BC
*                                                                  BC
*        B     ADOIT            SKIP SECURITY                      BC
*                                                                  BC
*        B     SPSAUTH          SOUTHWESTERN PUBLIC SERVICE CO     BC
*                                                                  BC
         B     MNPAUTH          MORTON                             BC
*                                                                  BC
SPSAUTH  DS    0H                                                  BC
         LA    R2,ATABNUM                                          DWM
         LA    R3,ATABLE                                           DWM
ALOOP    DS    0H                         QUICK                    DWM
         MVC   AWORK,0(R3)                AND                      DWM
         XC    AWORK,AKEY                 DIRTY                    DWM
         CLC   USERID,AWORK               SECURITY                 DWM
         BE    ADOIT                                               DWM
         LA    R3,8(0,R3)                                          DWM
         BCT   R2,ALOOP                                            DWM
         L     R15,ISPLINKA                                        DWM
         CALL  (15),                                               DWM X
               (SETMSG,                                            DWM X
               CDF001),                                            DWM X
               VL,MF=(E,ISPARMS)                                   DWM
         B     SHOWRTRN                                            DWM
*                                                                  BC
MNPAUTH  DS    0H                                                  BC
*                                                                  BC
         MVI   MNPFLAG,C'D'        ALLOW ONLY DISPLAY COMMANDS     BC
*                                                                  BC
         CLC   USERID(2),=C'AS'    SYSTEMS PROGRAMMERS             BC
         BE    MNPALL                                              BC
         CLC   USERID(2),=C'AP'    PRODUCTION SERVICES DEPT        BC
         BE    MNPALL                                              BC
         CLC   USERID(2),=C'AO'    OPERATORS                       BC
         BE    MNPALL                                              BC
         CLC   USERID(2),=C'BP'    BPSCAN                          BC
         BE    MNPALL                                              BC
         CLC   USERID(2),=C'MV'    MVSOPER                         BC
         BE    MNPALL                                              BC
         CLC   USERID(2),=C'AA'    DATA CENTER MANAGEMENT          BC
         BE    MNPALL                                              BC
*                                                                  BC
         B     ADOIT                                               BC
*                                                                  BC
MNPALL   DS    0H                                                  BC
*                                                                  BC
         MVI   MNPFLAG,C'A'        ALLOW ANY/ALL COMMANDS          BC
*                                                                  BC
         B     ADOIT                                               BC
*                                                                  BC
*                                                                  DWM
*  LOCATE ASID NUMBER FOR 'CONSOLE' ADDRESS SPACE                  DWM
*                                                                  DWM
ADOIT    DS    0H                                                  DWM
         L     R3,CVTPTR        GET CVT ADDRESS                    DWM
         USING CVTMAP,R3                                           DWM
         L     R3,CVTASVT       GET ASVT ADDRESS                   DWM
         DROP  R3                                                  DWM
         USING ASVT,R3                                             DWM
         L     R9,ASVTMAXU      GET MAX NUMBER OF ADDRESS SPACES   DWM
         LA    R6,4             START INDEX                        DWM
ASCBLP   DS    0H                                                  DWM
         LA    R7,ASVTFRST(R6)  GET ASCB POINTER                   DWM
         TM    0(R7),ASVTAVAL   IS ASCB AVAILABLE?                 DWM
         BO    ASCBNXT          YES..DO NEXT ASCB                  DWM
         L     R4,0(R7)         GET ASCB ADDRESS                   DWM
         USING ASCB,R4                                             DWM
         L     R5,ASCBJBNS      GET JOBNAME POINTER                DWM
         LTR   R5,R5            IS THE POINTER ZERO?               DWM
         BZ    ASCBNXT          YES..DO NEXT ASCB                  DWM
         CLC   CONSNAME,0(R5)   IS IT 'CONSOLE' ?                  DWM
         BE    CONSFND          FINALLY FOUND IT                   DWM
         DROP  R4                                                  DWM
ASCBNXT  DS    0H                                                  DWM
         LA    R6,4(,R6)        BUMP TO NEXT ASVT ENTRY            BC
         BCT   R9,ASCBLP        KEEP SEARCHING                     BC
         DROP  R3                                                  DWM
         L     R15,ISPLINKA                                        DWM
         CALL  (15),                                               DWM X
               (SETMSG,                                            DWM X
               CDF002),                                            DWM X
               VL,MF=(E,ISPARMS)                                   DWM
         B     SHOWRTRN         NEVER FOUND 'CONSOLE'.. QUIT NOW   DWM
CONSFND  DS    0H                                                  DWM
         SRL   R6,2             DIVIDE INDEX BY 4 TO GET ASID NUMBERWM
         ST    R6,CONSASID      SAVE 'CONSOLE' ASID NUMBER         DWM
*
*  NOW THAT WE HAVE FOUND 'CONSOLE', WE MAY BEGIN
*
         B     SHOWCONS       DISPLAY THE MASTER CONSOLE
*
SHOWDONE DS    0H
*
         VDELETE CDFDATA
         VDELETE CDF#COFF
         VDELETE CDF#CSRN
         VDELETE CDF#DDEP
*
         VDELETE CMDLINE
         VDELETE LASTCMD1
         VDELETE LASTCMD2
         VDELETE LASTCMD3
         VDELETE LASTCMD4
         VDELETE LASTCMD5
         VDELETE LASTCMD6
         VDELETE LASTCMD7
         VDELETE LASTCMD8
         VDELETE LASTCMD9
*
*
*
* THESE FIELDS DEFINED BY ISPF FOR DYNAMIC SCREEN AREAS
*
*
         VDELETE AREATYP
         VDELETE COLS
         VDELETE ROWS
         VDELETE TLROW
         VDELETE TLCOL
         VDELETE ZVERB
         VDELETE ZSCROLLA
         VDELETE ZSCROLLN
*
*
**  TITLE LINE VARIABLES
*
         VDELETE CDFMST
         VDELETE CDFS
         VDELETE CDFN
         VDELETE CDFI
         VDELETE CDFC
         VDELETE CDFU
*
SHOWRTRN DS    0H
         DELETE EP=ISPLINK    DELETE ISPLINK MODULE                DWM
         LR    R1,R13         SETUP FOR FREEMAIN                   BC
         L     R0,GETMAINA    SETUP FOR FREEMAIN                   BC
         L     R13,4(R13)     RESTORE THE CALLER'S R13             DWM
         FREEMAIN R,A=(1),LV=(0)       FREE CDFWORK STORAGE        BC
         RETURN (14,12),RC=0  RETURN TO OUR CALLER                 DWM
*
*
         TITLE 'CDF   START OF SHOW CONSOLE LOOP'
SHOWCONS DS    0H
*
*        ST    R7,CDF7HOLD SAVE THE RETURN ADDRESS                 DWM
*
         TITLE 'CDF   BUILD UCM TABLE           '
TAB1LOOP DS    0H
*
*---------------------------------------------------------------------*
*                                                                     *
*          BUILD A TABLE OF UCM ADDRESSES (ONE PER CONSOLE)           *
*                                                                     *
*---------------------------------------------------------------------*
BLDUCMS  L     R4,16              R4 = ADDR OF CVT
         USING CVT,R4
         L     R4,CVTCUCB         R4 = ADDR OF 'CUCB' (UCM BASE)
         DROP  R4
         USING UCM,R4
         L     R5,UCMVEA          R5 = ADDR OF FIRST UCM ENTRY
         L     R6,UCMVEZ          R6 = LENGTH OF EACH UCM ENTRY
         L     R7,UCMVEL          R7 = ADDR OF LAST UCM ENTRY
         LA    R1,UCMTAB+4        R1 = ADDR OF UCMTAB
         LA    R9,UCMTABE         R9 = ADDR OF END OF UCMTAB
         XR    R2,R2              R2  = 0 (NUMBER OF VALID UCMS)
UCMLOOP  ST    R5,0(0,R1)         SAVE UCM ADDRESS IN UCMTAB
         LA    R2,1(0,R2)         R2  = R2  + 1  (ONE MORE UCM)
         LA    R1,4(0,R1)         R1 = ADDR OF NEXT UCMTAB ENTRY
         CR    R1,R9              DOES R1 POINT PAST END OF UCMTAB?
         BNL   UCMDONE            YES; LEAVE LOOP
         AR    R5,R6              R5 = ADDR OF NEXT UCM ENTRY
         CR    R5,R7              DOES R5 POINT PAST UCM ENTRIES?
         BNH   UCMLOOP            NOPE; KEEP GOING                 DWM
UCMDONE  STH   R2,NUMUCMS         SAVE NUMBER OF UCMS FOUND
*---------------------------------------------------------------------*
*        SCAN THE UCM FOR THE MASTER CONSOLE AS THE DEFAULT        GMM*
*        CONSOLE WHEN ENTERING THE CONSOLE DISPLAY FACILITY        DWM*
*---------------------------------------------------------------------*
         L     R8,CONSOLE         GET CURRENT CONSOLE              DWM
         LTR   R8,R8              IS IT ZERO (FIRST TIME THROUGH) ?DWM
         BNZ   NEXTPAGE           NO...DONT NEED THIS              DWM
         LR    R8,R4              SETUP UCM POINTER                DWM
         S     R8,=F'4'           BACKUP POINTER TO MCS PREFIX POINTER
         L     R8,0(R8)           GET MCS PREFIX ADDRESS           DWM
         USING UCMPRFX,R8                                          DWM
         L     R1,UCMMCENT        GET ADDRESS OF MASTER CONSOLE UCMDWM
         S     R1,UCMVEA          FIND OFFSET FROM BEGINNING       DWM
         DROP  R4,R8              DONT NEED UCM ADDRESSABILITY NOW DWM
         SR    R0,R0                                               BC
         M     R0,=F'1'           SETUP SIGN FOR DIVIDE            DWM
         DR    R0,R6              DIVIDE BY UCM ENTRY LENGTH       DWM
         LA    R1,1(0,R1)         POSITIVE INTEGERS ONLY           DWM
         ST    R1,CONSOLE         SET VALID CONSOLE NUMBER (MASTER)DWM
         TITLE 'CDF   PICK A CONSOLE            '
*---------------------------------------------------------------------*
*                                                                     *
*                            TOP OF LOOP                              *
*                                                                     *
*---------------------------------------------------------------------*
NEXTPAGE DS    0H
         LA    R5,UCMTAB          R5 = ADDR OF UCMTAB
         L     R4,CONSOLE         R4 = CONSOLE TO BE DISPLAYED     DWM
         ST    R4,OLDCONS         R4 = LAST CONSOLE DISPLAYED      DWM
         BZ    WRTERR4            WE HAVE NO CONSOLE ZERO (MASTER) GMM
         CH    R4,NUMUCMS         IS NUMBER TOO HIGH?
         BNH   GETUCM             NO, CONTINUE
*        MVI   CLEAR,X'C5'        TURN ON BELL BIT IN WCC          DWM
RESETCN  L     R4,OLDCONS         RESET TO OLD CONSOLE
         ST    R4,CONSOLE         AND SAVE IT
GETUCM   SLL   R4,2               MULTIPLY BY 4
         LA    R5,0(R5,R4)        R5 = ADDR OF ADDR OF UCM
         L     R5,0(0,R5)         R5 = ADDR OF UCM
         USING UCMLIST,R5
         L     R6,UCMXB           R6 = ADDR OF RDCM
*        USING IEERDCM,R6                                          GMM
         LTR   R6,R6              IS THIS A GRAPHICS CONSOLE?
         BP    GRAPHICS           YES
         LA    R5,UCMTAB          R5 = ADDR OF UCMTAB
WRTERR4  DS    0H
*        MVI   CLEAR,X'C5'        TURN ON BELL BIT IN WCC          DWM
         B     RESETCN            RESET THE CONSOLE NUMBER
         SPACE
GRAPHICS EQU   *
         MVC   TITLE(TITLEL),BLANKS BLANK OUT CONSOLE TITLE        DWM
         MVC   TITLEC1,=C' CONSOLE='                               DWM
         MVC   TITLEC2,=C' AUTH='                                  DWM
         MVC   TITLEC3,=C' ADDR='                                  DWM
         TM    UCMDISP1,UCMDISPA  IS THIS A MASTER CONSOLE?
         BNO   AUTH0              NO
         MVC   CDFMST,=CL6'MASTER' YES                             DWM
AUTH0    TM    UCMAUTHA,UCMAUTH1  IS THIS CONSOLE SYSTEM AUTHORIZED?
         BNO   AUTH1              NO
         MVC   CDFS,=CL3'SYS'     YES                              DWM
AUTH1    TM    UCMAUTHA,UCMAUTH2  IS IT I/O AUTHOZRIZED?
         BNO   AUTH2              NO
         MVC   CDFI,=CL3'I/O' YES                                  DWM
AUTH2    TM    UCMAUTHA,UCMAUTH3  IS IT CONS AUTHORIZED?
         BNO   AUTHDONE           NO
         MVC   CDFC,=CL4'CONS' YES                                 DWM
AUTHDONE EQU   *
         L     R7,UCMUCB          R7 = ADDR OF UCB
         USING UCBCMSEG,R7                                         DWM
         MVC   CDFU(3),UCBNAME MOVE UNIT ADDR INTO LINE            DWM
         DROP  R7                                                  DWM
         L     R3,CONSOLE         LOAD THE CONSOLE NUMBER
         CVD   R3,WORK            CONVERT TO DECIMAL IN WORK
         MVC   SCRATCH(4),PATTERN MOVE IN EDIT PATTERN
         ED    SCRATCH(4),WORK+6  EDIT IN CONSOLE NUMBER
         MVC   CDFN(2),SCRATCH+2 MOVE CONSOLE NUMBER INTO PLACE
         NI    FLAG1,255-CONOTACT TURN OFF NOT ACTIVE FLAG         DWM
         TM    UCMATR,UCMUF       IS CONSOLE ACTIVE?               DWM
         BO    ACTIVE1            YES..IS ACTIVE                   DWM
         OI    FLAG1,CONOTACT     INDICATE CONSOLE NOT ACTIVE      DWM
         B     BUILD1             SKIP BUFFER MOVE                 BC
ACTIVE1  DS    0H                                                  DWM
         NI    FLAG1,255-BUFSHORT TURN OFF BUFFER TOO SHORT FLAG   DWM
         LA    R8,BUFLEN          LOAD BUFFER LENGTH FOR COMPARE   DWM
         SPACE 2
         TITLE 'CDF   CROSS MEMORY SERVICES     '
*---------------------------------------------------------------------*
*                                                                     *
*                 FILLIN TRAILER AT BOTTOM OF SCREEN                  *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
         LA    R0,1                    REQUEST AUTHORIZATION ON    JL1
         SVC   235                     SET AUTHORIZATION ON        JL1
         MODESET MF=(E,SUPRMOD)   GET INTO SUPV STATE AND KEY ZERO JPB
*
*                                      SET UP FOR CROSS MEMORY MOVE
*
         AXRES AXLIST=AXLIST           RESERVE AN AX               JPB
         SPACE 2
         L     R1,CONSASID             'CONSOLE' ASID              DWM
         AXEXT ASID=(1)                EXTRACT AX OF 'CONSOLE'IN R0JPB
         SPACE 2
         AXSET AX=(0)                  SET OUT AX TO THAT OF CONSOLEPB
         SPACE 2
         L     R1,CONSASID             'CONSOLE' ASID              DWM
         SSAR  R1                      SET 'CONSOLE' AS SECONDARY  JPB
         SPACE 2
         PRINT ON,GEN
*
*                                      PICK UP START ADDRESSES
*  IEECRDCM MOVED TO CONSOLE PRIVATE AREA BY 2.2.0/DFP 2.3.0
         LA    R1,4               SET TRUE LENGTH                  JPB
         SLR   R0,R0              SET STORAGE KEY                  JPB
         MVCP  TDCMADDR(R1),0(R6),R0                PT TO TDCM     BC
         L     R7,TDCMADDR        GET ADDRESS OF TDCM              BC
         USING STRTDCM,7
*
         LA    R1,4               SET TRUE LENGTH                  JPB
         SLR   R0,R0              SET STORAGE KEY                  JPB
         MVCP  SIBPTR(R1),DCMASCRN-STRTDCM(R7),R0 PT TO SCRN ADRJPB
         LA    R1,2               SET UP FOR HALF WORD MOVES       DWM
         MVCP  OUTLEN(R1),DCMLGNTH-STRTDCM(R7),R0                  DWM
         MVCP  OUTCLEN(R1),DCMCORLN-STRTDCM(R7),R0                 DWM
         MVCP  OUTNUM(R1),DCMMSGAL-STRTDCM(R7),R0                  DWM
*
*                                      SET UP FOR XMEM MOVE LOOP
*
         L     R1,SIBPTR          R1 = ADDR OF SCREEN IMAGE
         LA    R4,BUF             R4 = ADDR OF OUTPUT BUFFER
         LH    R5,OUTCLEN         GET INCORE LINE LENGTH           DWM
         MH    R5,OUTNUM          * NUMBER OF LINES = BUFFER LENGTHDWM
         AH    R5,OUTCLEN         ADD 1 FOR LUCK                   BC
         CR    R5,R8              IS OUR BUFFER LONG ENOUGH        DWM
         BNH   MOVEBUFF           YES..GO DO IT                    DWM
         OI    FLAG1,BUFSHORT     INDICATE BUFFER TOO SHORT        DWM
         B     RESETSAR           DONT MOVE BUFFER                 DWM
MOVEBUFF DS    0H
         LH    R2,OUTCLEN         R2 = INCORE LINE LENGTH          DWM
         SH    R2,OUTLEN        - DATA LENGTH = # OF CONTROL CHARS DWM
         SR    R1,R2           BACKUP POINTER TO BEGINNING OF DATA DWM
         SLR   R0,R0              RESET THE KEY
*
         $AMODE 31,R=R15,PUSHR=R14
         ST    R14,CDFAMODE
*
MVCPLOOP DS    0H
         MVCP  0(R5,R4),0(R1),R0  LOOP THRU THE CONSOLE BUFFER
*
         BZ    RESETSAR
         AL    R1,=F'256'         MAX LEN ALLOWED
         AL    R4,=F'256'         MAX LEN ALLOWED
         SL    R5,=F'256'         MAX LEN ALLOWED
         B     MVCPLOOP
*
RESETSAR DS    0H
*
         L     R14,CDFAMODE
         $AMODE R=R15,POPR=R14
*
*---------------------------------------------------------------------*
*                                                                     *
*        IN ORDER TO ISSUE SVCS THE PRIMARY AND                       *
*        SECONDARY ADDRSPC MUST BE THE SAME                           *
*                                                                     *
*---------------------------------------------------------------------*
*
         EPAR  R1                 EXTRACT PRIMARY ASN
         SSAR  R1
         MODESET MF=(E,PROBMOD)   RETURN TO PROBLEM STATE          DWM
         SR    R0,R0                   REQUEST AUTHORIZATION OFF   JL1
         SVC   235                     SET AUTHORIZATION OFF       JL1
*
         DROP  R5,R7                                               GMM
*
         TITLE 'CDF   BUILD ISPF TABLE'                            DWM
*                                                                  DWM
*  CHECK FOR ERROR CONDITIONS AND ISSUE MESSAGES IF APPROPRIATE    DWM
*                                                                  DWM
         TM    FLAG1,CONOTACT     IS CONSOLE NOT ACTIVE?           DWM
         BZ    CHKSHORT         NO...BUILD LINES FOR ACTIVE CONSOLEDWM
         L     R15,ISPLINKA       YES..GIVE THEM THE BAD NEWS      DWM
         CALL  (15),                                               DWM X
               (SETMSG,                                            DWM X
               CDF003),                                            DWM X
               VL,MF=(E,ISPARMS)                                   DWM
         B     SHOWSCRN                                            DWM
CHKSHORT DS    0H                                                  DWM
         TM    FLAG1,BUFSHORT     WAS THE BUFFER TOO SHORT         DWM
         BZ    BUILD1             NO...BUILD LINES FOR CONSOLE     DWM
         L     R15,ISPLINKA       YES..GIVE THEM THE BAD NEWS      DWM
         CALL  (15),                                               DWM X
               (SETMSG,                                            DWM X
               CDF004),                                            DWM X
               VL,MF=(E,ISPARMS)                                   DWM
         B     SHOWSCRN                                            DWM
*                                                                  DWM
*  BUILD CONSOLE LINES                                             DWM
*                                                                  DWM
BUILD1   DS    0H                                                  DWM
*                                                                  BC
         PQUERY CDF3P1,CDFDATA                                     BC
         LA    R1,CDFDATA         FIRST LINE LOCATION              BC
*                                                                  BC
         LH    R15,OUTNUM         GET NUMBER OF LINES              DWM
*        LA    R15,1(,R15)        ADD 1 FOR LUCK                   BC
         LA    R3,BUF             SET POINTER TO FIRST LINE        DWM
*                                                                  BC
         LH    R5,OUTCLEN         GET INCORE LINE LENGTH           DWM
         LH    R6,OUTLEN          GET DATA LINE LENGTH             DWM
         LR    R7,R5              SET UP FOR OFFSET CALCULATION    DWM
         SR    R7,R6              CALCULATE OFFSET TO DATA         DWM
         LA    R9,80              GET LINE LENGTH                  BC
         CR    R6,R9              DATA LENGTH > SCREEN  LENGTH ?   BC
         BNH   DOLINE             NO...USE DATA LENGTH             DWM
         LR    R6,R9              YES..USE SCREEN  LENGTH          BC
DOLINE   DS    0H                                                  DWM
         BCTR  R6,0               LENGTH - 1 FOR EXECUTE           DWM
*                                                                  DWM
         C     R15,ROWS           CONSOLE LINES VS ISPF SCREEN SZ  BC
         BNH   NEXTL              ALL THE LINES WILL FIT           BC
*                                                                  BC
BIGCONS  DS    0H                                                  DWM
*                                                                  BC
         LR    R14,R15            GET THE NUMBER OF EXCESS         BC
         L     R15,ROWS                                            BC
         SR    R14,R15            ROWS INTO R14                    BC
*                                                                  BC
DROPROWS DS    0H                                                  BC
         LA    R4,0(R7,R3)        SET UP POINTER TO DATA           BC
         CLI   3(R4),C'*'         HIGHLIGHT?                       BC
         BE    HILITE1            YES                              BC
         CLI   3(R4),C'@'         HIGHLIGHT?                       BC
         BNE   NEXTROWS           NO                               BC
HILITE1  DS    0H
         MVI   0(R1),C' '         INITIALIZE DYNAMIC AREA          BC
         MVC   1(79,R1),0(R1)     INITIALIZE DYNAMIC AREA          BC
         MVC   0(1,R1),0(R4)      MOVE IT TO DYNAMIC AREA          BC
         EX    R6,*-6             MOVE IT TO SPF AREA              BC
         MVI   0(R1),3            HIGH-LIGHT                       BC
         LA    R1,80(,R1)         POINT TO NEXT LINE               BC
         LA    R14,1(,R14)        INCREMENT                        BC
         BCTR  R15,0                                               BC
*                                                                  BC
NEXTROWS DS    0H                                                  BC
         LA    R3,0(R5,R3)        BUMP DOWN IN CONSOLE BUFFER      BC
         BCT   R14,DROPROWS       DROPPING THE FIRST "N" LINES     BC
*                                                                  BC
SIZEOK   DS    0H                                                  BC
*                                                                  BC
NEXTL    DS    0H                                                  DWM
         LA    R4,0(R7,R3)        SET UP POINTER TO DATA           DWM
         MVI   0(R1),C' '         INITIALIZE DYNAMIC AREA          BC
         MVC   1(79,R1),0(R1)     INITIALIZE DYNAMIC AREA          BC
         MVC   0(1,R1),0(R4)      MOVE IT TO DYNAMIC AREA          BC
         EX    R6,*-6             MOVE IT TO SPF AREA              BC
*                                                                  BC
** CHECK FOR HIGH-LIGHTING                                         BC
*                                                                  BC
         MVI   0(R1),1            LOW INTENS IS DEFAULT            BC
         CLI   3(R1),C'*'         HIGHLIGHT?                       BC
         BE    HIGHLITE           YES                              BC
         CLI   3(R1),C'@'         HIGHLIGHT?                       BC
         BE    HIGHLITE           YES                              BC
*                                                                  BC
         B     BUFFBUMP           LEAVE IT LOW INTENS              BC
*                                                                  BC
HIGHLITE DS    0H                                                  BC
*                                                                  BC
         MVI   0(R1),3            HIGH-LIGHT                       BC
*                                                                  BC
BUFFBUMP DS    0H                                                  BC
*                                                                  BC
         LA    R3,0(R5,R3)        POINT TO NEXT LINE               DWM
         LA    R1,80(,R1)         POINT TO NEXT LINE               BC
*                                                                  DWM
         BCT   R15,NEXTL          PRINT NEXT LINE                  DWM
*                                                                  DWM
         TITLE 'CDF  DISPLAY SPF TABLE          '
*
***********************************************************************
*
*   NOW SHOW THE ISPPLIB FORMAT AND THE TABLE ON THE SCREEN
*
***********************************************************************
*
SHOWSCRN DS    0H
         DISPLAY CDF3P1,ENDKEY=SHOWDONE
*
***********************************************************************
*
*   DETERMINE WHETHER ANYTHING WAS ENTERED ON THE SCREEN
*
***********************************************************************
*
GOTSTUFF DS    0H
         TR    CMDLINE,CAPSONLY  UPPER-CASE ALL INPUT
*
*  NOTE THE TR TABLE IS SET UP TO DELETE SEMI-COLONS, IN ORDER TO
*       PREVENT COMMAND CONCATENATION.
*
         CLI   CMDLINE,C' '   IF COMMAND AREA IS SPACES
         BE    SHOWCONS       THEN A COMMAND WAS NOT ENTERED       DWM
*                                                                  DWM
         CLC   CMDLINE(L'CONCMD),CONCMD    NEW CONSOLE REQUEST ?   DWM
         BE    CONCMDX                     YES, PROCESS            BC
*                                                                  BC
** CHECK FOR COMMAND STACK COMMANDS                                BC
*                                                                  BC
*
         CLC   CMDLINE(4),=C'LAST'     IS IT RESHOW LAST PREVIOUS CMD
         BE    RPTLAST                   THEN GO REPEAT IT
*
         CLC   CMDLINE(4),=C'SAVE'     IS IT SAVE LAST PREVIOUS CMD
         BE    RPTSAVE                   THEN GO DO IT
*
         MVC   LASTCMD(51),CMDLINE
         MVC   CMDLINE(51),BLANKS
*                                                                  BC
*        B     GOTCMD                      BYPASS MNP AUTH CHECK   BC
*                                                                  BC
         CLI   MNPFLAG,C'A'            ALL COMMANDS ALLOWED?
         BE    GOTCMD                    THEN GO DO IT
*
         CLC   LASTCMD(2),=C'D '       IS IT A DISPLAY COMMAND
         BE    GOTCMD                    THEN GO DO IT
*
         CLC   LASTCMD(2),=C'$D'       IS IT A DISPLAY COMMAND
         BE    GOTCMD                    THEN GO DO IT
*
         CLC   LASTCMD(2),=C'$L'       IS IT A DISPLAY COMMAND
         BE    GOTCMD                    THEN GO DO IT
*
         SETMSG CDF001                                             BC
         B     SHOWRTRN                                            BC
*                                                                  BC
CONCMDX  DS    0H                                                  BC
*                                                                  BC
         XC    WORK,WORK         CLEAR DOUBLEWORD                  DWM
         PACK  WORK+7(1),LASTCMD+L'CONCMD(1) PACK NEW CONSOLE NUMBERWM
         CVB   R1,WORK           CONVERT TO BINARY                 DWM
         ST    R1,CONSOLE        STORE NEW CONSOLE NUMBER          DWM
*                                                                  DWM
         B     NEXTPAGE                                            BC
*                                                                  DWM
*                                                                  DWM
GOTCMD   DS    0H                                                  DWM
*                                                                  DWM
         LA    R1,CMDLEN        LENGTH OF REPLY COMMAND            DWM
         STH   R1,CMDPARM       STICK IT IN THERE                  DWM
         SR    R0,R0            CLEAR REGISTER                     DWM
         STH   R0,CMDRESV       CLEAR RESERVED HALFWORD            DWM
*                                                                  DWM
         LA    R0,1                    REQUEST AUTHORIZATION ON    DWM
         SVC   235                     SET AUTHORIZATION ON        DWM
         MODESET MF=(E,SUPRMOD)   GET INTO SUPV STATE AND KEY ZERO DWM
*                                                                  DWM
         L     R0,CONSOLE              INSERT CONSOLE NUMBER       DWM
         MGCR  CMDPARM                 ISSUE COMMAND VIA SVC 34    DWM
*                                                                  DWM
         MODESET MF=(E,PROBMOD)        RETURN TO PROBLEM STATE     DWM
         SR    R0,R0                   REQUEST AUTHORIZATION OFF   DWM
         SVC   235                     SET AUTHORIZATION OFF       DWM
*                                                                  DWM
CMDFINSH DS    0H                                                  DWM
         B     SHOWCONS                                            DWM
*                                                                  DWM
         EJECT
*
RPTLAST  DS    0H
*
         CLI   CMDLINE+4,C'1'          SAVED COMMAND
         BE    RPTLAST1
         CLI   CMDLINE+4,C'2'          SAVED COMMAND
         BE    RPTLAST2
         CLI   CMDLINE+4,C'3'          SAVED COMMAND
         BE    RPTLAST3
         CLI   CMDLINE+4,C'4'          SAVED COMMAND
         BE    RPTLAST4
         CLI   CMDLINE+4,C'5'          SAVED COMMAND
         BE    RPTLAST5
         CLI   CMDLINE+4,C'6'          SAVED COMMAND
         BE    RPTLAST6
         CLI   CMDLINE+4,C'7'          SAVED COMMAND
         BE    RPTLAST7
         CLI   CMDLINE+4,C'8'          SAVED COMMAND
         BE    RPTLAST8
         CLI   CMDLINE+4,C'9'          SAVED COMMAND
         BE    RPTLAST9
*
         MVC   CMDLINE(51),LASTCMD
         B     SHOWCONS
*
RPTLAST1 MVC   CMDLINE(51),LASTCMD1
         B     SHOWCONS
RPTLAST2 MVC   CMDLINE(51),LASTCMD2
         B     SHOWCONS
RPTLAST3 MVC   CMDLINE(51),LASTCMD3
         B     SHOWCONS
RPTLAST4 MVC   CMDLINE(51),LASTCMD4
         B     SHOWCONS
RPTLAST5 MVC   CMDLINE(51),LASTCMD5
         B     SHOWCONS
RPTLAST6 MVC   CMDLINE(51),LASTCMD6
         B     SHOWCONS
RPTLAST7 MVC   CMDLINE(51),LASTCMD7
         B     SHOWCONS
RPTLAST8 MVC   CMDLINE(51),LASTCMD8
         B     SHOWCONS
RPTLAST9 MVC   CMDLINE(51),LASTCMD9
         B     SHOWCONS
*
         EJECT
*
RPTSAVE  DS    0H
*
         CLI   CMDLINE+4,C'1'          SAVED COMMAND
         BE    RPTSAVE1
         CLI   CMDLINE+4,C'2'          SAVED COMMAND
         BE    RPTSAVE2
         CLI   CMDLINE+4,C'3'          SAVED COMMAND
         BE    RPTSAVE3
         CLI   CMDLINE+4,C'4'          SAVED COMMAND
         BE    RPTSAVE4
         CLI   CMDLINE+4,C'5'          SAVED COMMAND
         BE    RPTSAVE5
         CLI   CMDLINE+4,C'6'          SAVED COMMAND
         BE    RPTSAVE6
         CLI   CMDLINE+4,C'7'          SAVED COMMAND
         BE    RPTSAVE7
         CLI   CMDLINE+4,C'8'          SAVED COMMAND
         BE    RPTSAVE8
         CLI   CMDLINE+4,C'9'          SAVED COMMAND
         BE    RPTSAVE9
*
         B     SHOWCONS
*
RPTSAVE1 MVC   LASTCMD1(51),LASTCMD
         MVC   CMDLINE(51),SPACES      CLEAR IT
         VPUT  LASTCMD1
         B     SHOWCONS
RPTSAVE2 MVC   LASTCMD2(51),LASTCMD
         VPUT  LASTCMD2
         B     SHOWCONS
RPTSAVE3 MVC   LASTCMD3(51),LASTCMD
         VPUT  LASTCMD3
         B     SHOWCONS
RPTSAVE4 MVC   LASTCMD4(51),LASTCMD
         VPUT  LASTCMD4
         B     SHOWCONS
RPTSAVE5 MVC   LASTCMD5(51),LASTCMD
         VPUT  LASTCMD5
         B     SHOWCONS
RPTSAVE6 MVC   LASTCMD6(51),LASTCMD
         VPUT  LASTCMD6
         B     SHOWCONS
RPTSAVE7 MVC   LASTCMD7(51),LASTCMD
         VPUT  LASTCMD7
         B     SHOWCONS
RPTSAVE8 MVC   LASTCMD8(51),LASTCMD
         VPUT  LASTCMD8
         B     SHOWCONS
RPTSAVE9 MVC   LASTCMD9(51),LASTCMD
         VPUT  LASTCMD9
         B     SHOWCONS
*
         EJECT
*
         TITLE 'MISCELLANEOUS CONSTANTS'
         LTORG
*
         DS    0F
*
*
*
FZEROS   DC    F'0'
L79      DC    F'79'
L51      DC    F'51'                                               DWM
*                                                                  DWM
TBADD    DC    C'TBADD    '
TBCREATE DC    C'TBCREATE '
TBDISPL  DC    C'TBDISPL  '
TBEND    DC    C'TBEND    '
TBTOP    DC    C'TBTOP    '
TBVCLEAR DC    C'TBVCLEAR '
VDEFINE  DC    C'VDEFINE  '
VDELETE  DC    C'VDELETE  '
VPUT     DC    C'VPUT     '
AKEY     DC    X'9459277829C12B52'                                 DWM
*
NOWRITE  DC    C'NOWRITE  '
*
NULLENT  DC    C'()'
*
DANAMLST DC    C'('             CDF NAME LIST
         DC    C'CONLINE )'     JOB INFO
CONLINEP DC    C'(CONLINE )'    JOB INFO
JCMD     DC    C'(JCMD    )'    SELECTION CODE
*
*
CHAR     DC    C'CHAR    '
*
CDFDATAB DC    C'CDFDATAB'
CDFTABLE DC    C'CDFTABLE'
CDFWORKT DC    CL4'CDFW'                                           DWM
BUFST    DC    CL4'BUFS'                                           DWM
BUFET    DC    CL4'BUFE'                                           DWM
*
*
*---------------------------------------------------------------------*
*                                                                     *
*                         C O N S T A N T S                           *
*                                                                     *
*---------------------------------------------------------------------*
PATTERN  DC    X'40202020'         EDIT PATTERN FIELD
SPACES   EQU   *
BLANKS   DC    CL80' '
*---------------------------------------------------------------------*
*                                                                     *
*        FOLLOWING IS IN SUPPORT OF C M S                             *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
AXLIST   DC    H'1',H'0'           AXRES PARMLIST
         SPACE 2
         SPACE 5
         TITLE 'EQUATES'
***********************************************************************
*                                                                     *
*        REGISTER EQUATES                                             *
*                                                                     *
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         DS    0F
SUPRMOD  MODESET KEY=ZERO,MODE=SUP,MF=L                            GMM
PROBMOD  MODESET KEY=NZERO,MODE=PROB,MF=L                          GMM
*                |...|...|...|...|...|...|...|...|...|...|...
CAPSONLY DC    CL64' '
         DC    CL10' '
         DC    C'¢.<(+|&&'
         DC    CL9' '
         DC    C'!$*)'
         DC    C' '             NOTE SEMICOLON CHANGED TO BLANK
         DC    C'¬-/'
         DC    CL9' ',C',%_>?',CL10' ',C':#@''="'
         DC    CL16' ABCDEFGHI      '
         DC    CL16' JKLMNOPQR      '
         DC    CL16'  STUVWXYZ      '
         DC    CL16'                '
         DC    CL16' ABCDEFGHI      '
         DC    CL16' JKLMNOPQR      '
         DC    CL16'  STUVWXYZ      '
         DC    CL16'0123456789      '
         SPACE 2
*
ATABLE   DC    X'70BBE2A1E0056B12'                                 DWM
ATABLEN  EQU   *-ATABLE                                            DWM
ATABNUM  EQU   ATABLEN/8                                           DWM
*                                                                  DWM
CDF001   DC    CL8'CDF001 '                                        DWM
CDF002   DC    CL8'CDF002 '                                        DWM
CDF003   DC    CL8'CDF003 '                                        DWM
CDF004   DC    CL8'CDF004 '                                        DWM
*                                                                  DWM
SETMSG   DC    CL8'SETMSG  '                                       DWM
*                                                                  DWM
*
VCOPY    DC    CL8'VCOPY '
ZUSER    DC    CL8'ZUSER '
SPFMOVE  DC    CL8'MOVE   '
CONSNAME DC    CL8'CONSOLE'                                        DWM
CONCMD   DC    C'CON '                                             DWM
ZEROS    DC    CL8'00000000'                                       BC
         TITLE 'WORKING STORAGE AREAS'
*                                                                  DWM
*   WORKING STORAGE DSECT                                          DWM
*                                                                  DWM
CDFWORK  DSECT                                                     DWM
CDFWT    DS    CL4                                                 DWM
SAVEAREA DS    18F                                                 DWM
*                                                                  DWM
CDFAMODE DS    F                                                   DWM
F8       DS    F                                                   BC
WORK     DS    D                                                   DWM
SCRATCH  DS    D                   SCRATCH AREA FOR CHAR. MANIP    DWM
ISPARMS  DS    10F            PARM LIST FOR ISPLINK                DWM
ISPLINKA DS    F              ISPLINK ADDRESS                      DWM
UCMTAB   DS    36F            UCM TABLE                            DWM
UCMTABE  EQU   *              UCM TABLE END                        DWM
OLDCONS  DS    F                   PREVIOUS CONSOLE NUMBER         DWM
CONSOLE  DS    F                   CONSOLE TO BE LOOKED AT         DWM
SIBPTR   DS    F                   PRT TO SCREEN IMAGE BUFFER      DWM
TDCMADDR DS    F                   ADDRESS OF "IEECDCM"            BC
CONSASID DS    F                   'CONSOLE' ASID FOR CMS          DWM
NUMUCMS  DS    H              NUMBER OF UCMS                       DWM
OUTLEN   DS    H                  CONSOLE LINE IMAGE LENGTH        DWM
OUTCLEN  DS    H                  INCORE LINE LENGTH               DWM
OUTNUM   DS    H                  NUMBER OF LINES FOR CONSOLE      DWM
MNPFLAG  DS    CL1            MORTON SECURITY FLAG                 BC
TITLE    DS    0CL1               CONSOLE TITLE                    DWM
TITLEC1  DS    CL9                ' CONSOLE='                      DWM
CDFN     DS    CL2                CONSOLE NUMBER                   DWM
TITLEC2  DS    CL6                ' AUTH='                         DWM
CDFS     DS    CL3                SYS  AUTHORIZATION               DWM
         DS    CL1                                                 DWM
CDFI     DS    CL3                I/O  AUTHORIZATION               DWM
         DS    CL1                                                 DWM
CDFC     DS    CL4                CONS AUTHORIZATION               DWM
         DS    CL1                                                 DWM
CDFMST   DS    CL6                MASTER CONSOLE                   DWM
TITLEC3  DS    CL6                ' ADDR='                         DWM
CDFU     DS    CL3                UNIT ADDR OF CONSOLE             DWM
         DS    CL1                                                 DWM
TITLEL   EQU   *-TITLE                                             DWM
*                                                                  DWM
*     THE ISP TABLE FLAGS INDICATE NO ACTIVE TABLE (0), OR ONE EXISTSM
ATABFL   DS    XL1     DO I HAVE A CURRENT "DISPLAY ACTIVE" TABLE  DWM
FLAG1    DS    XL1     GENERAL PURPOSE FLAG                        DWM
CONOTACT EQU   X'80'   CONSOLE NOT ACTIVE FLAG                     DWM
BUFSHORT EQU   X'40'   BUFFER TOO SHORT CONDITION                  DWM
*                                                                  DWM
CMDPARM  DS    H                                                   DWM
CMDRESV  DS    H                                                   DWM
LASTCMD  DS    CL51
CMDLEN   EQU   *-CMDPARM                                           DWM
USERID   DS    CL8                                                 DWM
AWORK    DS    CL8                                                 DWM
*
CMDLINE  DS    CL51        COMMAND SVC 34 INPUT                    DWM
*
** COMMAND STACK VARIABLES
*
LASTCMD1 DS    CL51
LASTCMD2 DS    CL51
LASTCMD3 DS    CL51
LASTCMD4 DS    CL51
LASTCMD5 DS    CL51
LASTCMD6 DS    CL51
LASTCMD7 DS    CL51
LASTCMD8 DS    CL51
LASTCMD9 DS    CL51
*
** ISPF PANEL VARIABLES
*
AREATYP  DS    CL8
COLS     DS    F
ROWS     DS    F
TLROW    DS    F
TLCOL    DS    F
ZVERB    DS    CL8
ZSCROLLA DS    CL4
ZSCROLLN DS    F
CDF#COFF DS    F
CDF#CSRN DS    CL8
CDF#DDEP DS    F
CDFDATA  DS    XL3440
*
*---------------------------------------------------------------------*
*                                                                  DWM*
*               DISPLAY SCREEN - IMAGE BUFFER SECTION              DWM*
*                                                                  DWM*
*---------------------------------------------------------------------*
BUFS     DS    CL4                                                 DWM
BUF      DS    35CL80             OPERATORS SCREEN BUFFER          DWM
BUFLEN   EQU   *-BUF              SCREEN BUFFER LENGTH             DWM
BUFE     DS    CL4                                                 DWM
*                                                                  DWM
*                                                                  DWM
*                                                                  DWM
WORKLEN  EQU   *-CDFWORK                                           DWM
         EJECT
         PRINT NOGEN
         TITLE '*** COMMUNICATION VECTOR TABLE ***'                GMM
         CVT   DSECT=YES                                           GMM
         TITLE '*** ADDRESS SPACE VECTOR TABLE ***'                DWM
         IHAASVT DSECT=YES                                         DWM
         TITLE '*** ADDRESS SPACE CONTROL BLOCK ***'               JL1
         IHAASCB DSECT=YES                                         JL1
*        TITLE '*** RDCM DSECT ***'                                GMM
*IEERDCM  DSECT ,                                                  GMM
*        IEECRDCM                                                  GMM
         TITLE '*** TDCM DSECT ***'                                GMM
IEETDCM  DSECT ,                                                   GMM
         IEECDCM                                                   GMM
         TITLE '*** MULTIPLE CONSOLE SUPPORT (MCS) UCM PREFIX ***' GMM
         IEECUCM FORMAT=NEW
         TITLE '*** UNIT CONTROL BLOCK ***'                        DWM
         IEFUCBOB                                                  DWM
         END
/*
//LKED1    EXEC PGM=HEWLH096,PARM=(XREF,LET,LIST,TERM),
//            COND=(4,LT,ASM1),REGION=1024K
//SYSLIN   DD   DSN=&&OBJSET,DISP=(OLD,DELETE)
//         DD   DDNAME=SYSIN
//SYSLIB   DD   DISP=SHR,DSN=ISP.V2R2M0.ISPLOAD
//SYSLMOD  DD   DISP=SHR,DSN=ASQCC.TEST.STEPLIB
//*YSLMOD  DD   DISP=SHR,DSN=SYS2.CMDLIB
//SYSUT1   DD   DSN=&&SYSUT1,UNIT=SYSDA,SPACE=(1024,(50,20))
//SYSTERM  DD   SYSOUT=*
//SYSPRINT DD   SYSOUT=*
//SYSIN    DD   *
  NAME CDF(R)
/*
//*EFRESH  EXEC PGM=MVSCMD,PARM='F LLA,REFRESH'
