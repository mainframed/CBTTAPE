***********************************************************************
*
*  CHANGE LOG
*
*  9/12/86  BRIAN COOK    INCREASED AVAIL FRAME COUNT TO 5 POSITIONS
*
*  7/18/86  BRIAN COOK    REVISED FOR XA
*
*
***********************************************************************
         PRINT OFF
         COPY SPFMACS
         PRINT ON
         PRINT NOGEN
         EJECT
***********************************************************************
*                                                                     *
*                                                                     *
*               EXECUTION DISPLAY FACILITY (XDF)                      *
*                                                                     *
*                                                                     *
* ABSTRACT: DISPLAY STATUS OF JES2 JOBS, LOGONS, & STARTED TASKS.     *
*                                                                     *
*                                                                     *
* FUNCTION: XDF IS DESIGNED TO OPERATE UNDER ISPF USING DISPLAY       *
*           MANAGEMENT SERVICES. IT PROVIDES A DISPLAY OF THE         *
*           ACTIVITY IN THE SYSTEM, SIMILAR TO RMF, BUT SINCE IT      *
*           RUNS UNDER ISPF, IT CAN BE INVOKED WITH SPLIT-SCREEN.     *
*                                                                     *
*           XDF LOOKS AT VARIOUS MVS SP 1.3.0 CONTROL BLOCKS.         *
*                                                                     *
*                                                                     *
* EXTERNAL REFERENCE: ISPLINK                                         *
*                                                                     *
*                                                                     *
* MACROS USED: SAVE                                                   *
*              ABEND                                                  *
*              RETURN                                                 *
*              GETMAIN                                                *
*              STAX                                                   *
*              ENQ                                                    *
*              DEQ                                                    *
*              TPUT                                                   *
*              FREEMAIN                                               *
*              $JQE                                                   *
*              $JOE                                                   *
*              CVT                                                    *
*              CVTUSERS (USER)                                        *
*              TCBWORK  (USER)                                        *
*                                                                     *
*                                                                     *
* CUSTOMIZATION:                                                      *
*                                                                     *
* THE FOLLOWING EQUATES MAY NEED TO BE CHANGED.                       *
*                                                                     *
*
HOTPG    EQU   244          C'4'         TOP PERFORMANCE GROUP
JDFSVC   EQU   237                       JDF SVC
*
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*
*  GLOSSARY OF TERMS FOR OUTPUT DISPLAY:
*
*    JOB       JOB NAME OF MEMORY
*    STEP      FOR BATCH JOBS WILL ALWAYS BE INIT.
*              FOR TSO JOBS AND STARTED TASKS WILL BE ACTUAL STEP NAME.
*    P         REASON CODE FOR SWAP OUT FROM OUCBPFL.
*              THE FOLLOWING ARE THE REASON CODES DISPLAYED:
*                U  0A MEMORY WAS UNILATERALLY SWAPPED
*                X  09 MEMORY WAS EXCHANGE SWAPPED
*                E  08 AN ENQHOLD HAS BEEN RECEIVED FOR THIS MEMORY
*                S  07 MEMORY REQUESTED SWAP OUT
*                D  06 INVOLUNTARY SWAPOUT DUE TO DETECTED WAIT
*                R  05 SWAPOUT CAUSED BY REAL STORAGE SHORTAGE
*                A  04 SWAPOUT CAUSED BY AUX. STORAGE SHORTAGE
*                L  03 MEMORY SWAPPED OUT BY MS6 LONG WAIT DETECTION
*                I  02 MEMORY IS IN INPUT TERMINAL WAIT
*                T  01 MEMORY IS IN OUTPUT TERMINAL WAIT
*                ?     UNABLE TO DETERMINE SWAPOUT REASON.
*    MEMORY    CURRENT AMOUNT OF MEMORY ALLOCATED TO THIS MEMORY.
*    WSS       SRM'S VIEW OF THE WORKING SET SIZE FOR THIS MEMORY.
*    LOC       CURRENT LOCATION OF THIS MEMORY:
*                IN  SWAPPED IN AND ELIGIBLE TO RUN.
*                OUT SWAPPED OUT BUT READY TO RUN.
*                WT  SWAPPED OUT AND NOT READY TO RUN.
*                N/S SWAPPED IN AND V=R OR NON-SWAPPABLE STATUS.
*                <-> TRANSITIONING BETWEEN STATES.
*    DP        DISPATCHING PRIORITY OF MEMORY (IN HEX).
*    PG        PERFORMANCE GROUP.
*    SC        NUMBER OF TIMES THE MEMORY HAS BEEN SWAPPED IN THE
*              CURRENT TRANSACTION.
*    WMR       CURRENT SRM WORKLOAD MANAGER RECOMMENDATION VALUE.
*              (ACTUALLY THE WORKLOAD LEVEL AT WHICH THIS MEMORY IS
*               OPERATING).
*    CPU       CPU TIME IN SECONDS USED BY THE MEMORY (SRB PLUS TASK).
*
*    SIO'S     CURRENT SIO COUNT
*
*
*
*
***********************************************************************
         EJECT
XDF      CSECT
         USING *,13,12,11,10
         B     72(,15)
         DC    17F'0'
         SAVE  (14,12)
         LA    R2,0(,R15)     A(XDFST)
         ST    R2,8(R13)      LINK SAVE AREAS
         ST    R13,4(R2)      SAVE THE CALLER'S R13
         LR    R13,R2         SET SAVE AREA AND BASE 1
         LR    R12,R2         SET
         A     R12,L4096          BASE 2
         LR    R11,R12        SET
         A     R11,L4096          BASE 3
         LR    R10,R11        SET
         A     R10,L4096          BASE 4
*
         USING PSA,0
*
         MVI   ATABFL,C'0'    NO ACTIVE TABLE
         MVI   XDFLINE,C' '   CLEAR TAB ENTRY AREA
         MVC   XDFLINE+1(79),XDFLINE
         XC    ISPARMS(40),ISPARMS
         MVC   XDFCMD(3),=C'ALL'    INITIAL COMMAND - SHOW EVERYTHING
         MVI   DELTA,0              NOT LIMITED TO CHANGED
         MVC   SELCMD(3),=C'ALL'    INITIAL COMMAND - SHOW EVERYTHING
*
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VCOPY,ZUSER,U8,USERID,SPFMOVE),                        X
               VL,MF=(E,ISPARMS)
*
SHOWAGIN DS    0H
         BAL   R7,SHOWRMF     DISPLAY THE RESOURCE STUFF
*
SHOWDONE DS    0H
*
         VDELETE XDFSEL
         VDELETE XDFLINE
         VDELETE XDFCMD
         VDELETE XCPU
         VDELETE XAFC
         VDELETE XPFR
         VDELETE XASQ
         VDELETE XUIC
         VDELETE XDIO
         VDELETE XDMN1
         VDELETE XDMN2
         VDELETE XDMN3
*
         TBEND XDFTABLE
         MVI   ATABFL,C'0'        NO ACTIVE TABLE
*
         L     R13,4(R13)     RESTORE THE CALLER'S R13
         RETURN (14,12),RC=0  RETURN TO OUR CALLER
*
L4096    DC    F'4096'
*
         EJECT
SHOWRMF  DS    0H
         ST    R7,XDF7HOLD SAVE THE RETURN ADDRESS
*
         MVC   XDFSEL(3),SPACES   SET SELECTION CODE TO SPACES
*
         CLI   ATABFL,C'0'        DO I HAVE AN ACTIVE TABLE?
         BE    ANOTAB             NOPE
*
         VDELETE XDFLINE
         VDELETE XDFSEL
         VDELETE XDFCMD
         VDELETE XCPU
         VDELETE XAFC
         VDELETE XPFR
         VDELETE XASQ
         VDELETE XUIC
         VDELETE XDIO
         VDELETE XDMN1
         VDELETE XDMN2
         VDELETE XDMN3
*
         TBEND XDFTABLE
         MVI   ATABFL,C'0'        NO ACTIVE TABLE
ANOTAB   DS    0H
*
         VDEFINE XDFLINE,78
         VDEFINE HILITE,4
         MVC   HILITE(4),=CL4'LOW'
         VPUT  HILITE
         VGET  HILITE
*
         MVC   XDFCMD(3),SELCMD      SET COMMAND
         VDEFINE XDFCMD,40
         VDEFINE XDFSEL,1
         VDEFINE XCPU,4
         VDEFINE XAFC,5
         VDEFINE XPFR,4
         VDEFINE XASQ,4
         VDEFINE XUIC,4
         VDEFINE XDIO,4
         VDEFINE XDMN1,5
         VDEFINE XDMN2,5
         VDEFINE XDMN3,5
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBCREATE,                                              X
               XDFTABLE,                                               X
               NULLENT,                                                X
               DANAMLST,                                               X
               NOWRITE),                                               X
               VL,MF=(E,ISPARMS)
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBVCLEAR,                                              X
               XDFTABLE),                                              X
               VL,MF=(E,ISPARMS)
         MVI   ATABFL,C'1'        AN ACTIVE TABLE
*
*
FINDCVT  DS    0H
*
**  LOOK THRU SRM CONTROL BLOCKS TO BUILD HEADER INFO
*
*
BUILDHDR DS    0H
         L     R4,FLCCVT       ---> CVT...
         USING CVT,4
***********************************************************************
*
*   COPY SRM CONTROL BLOCKS
*
*   RMCT
*   CCT
*   RCT
*   DMDT
*
***********************************************************************
*
         L     R4,CVTOPCTP     ---> RMCT... 1ST MODULE OF IRARMCNS
         DROP  4
*
         LA    R6,RMCT
         PRINT GEN
         $AMODE 31
         PRINT NOGEN
         MVC   0(RMCTLEN,R6),0(R4)
*
         L     R6,RMCTRCT      ---> RCT
         LA    R1,RCT
         MVC   0(RCTLEN,R1),0(R6)
*
         L     R6,RMCTCCT      ---> CCT
         LA    R1,CCT
         MVC   0(CCTLEN,R1),0(R6)
*
         L     R6,RMCTDMDT      ---> DMDT
         LA    R5,DMDTLEN
         AR    R6,R5
         LA    R1,DMDT
         MVC   0(DMDTLEN,R1),0(R6)
         AR    R6,R5
         AR    R1,R5
         MVC   0(DMDTLEN,R1),0(R6)
         AR    R6,R5
         AR    R1,R5
         MVC   0(DMDTLEN,R1),0(R6)
         $AMODE 24
*
*
***********************************************************************
*
*   FILL IN SYSTEM CPU UTILIZATION
*
***********************************************************************
*
         LH    R6,CCVUTILP        CPU UTILIZATION
         CVD   R6,WORK             CONVERT TO DECIMAL...
         OI    WORK+7,X'0F'        FIX THE SIGN
         UNPK  XCPU,WORK+6(2)       ...AND EDIT
         MVI   XCPU+3,C'%'
*
         CLC   XCPU(3),=C'101'
         BNE   *+10
         MVC   XCPU(3),=C'100'
*
         SPACE
***********************************************************************
*
*   FILL IN AVERAGE AVAILABLE FRAME COUNT
*
***********************************************************************
         LH    R6,CCVLGFMC        AVERAGE AVAILABLE FRAME COUNT
         CVD   R6,WORK             CONVERT TO DECIMAL...
         OI    WORK+7,X'0F'        FIX THE SIGN
         UNPK  WRK5,WORK+5(3)       ...AND EDIT
         MVC   XAFC(5),WRK5
         SPACE
***********************************************************************
*
*   FILL IN PAGE FAULT RATE
*
***********************************************************************
         LH    R6,RCVDPR          PAGE FAULT RATE
         CVD   R6,WORK             CONVERT TO DECIMAL...
         OI    WORK+7,X'0F'        FIX THE SIGN
         UNPK  XPFR,WORK+6(2)       ...AND EDIT
         SPACE
         LH    R6,RCVASMQA        AUX. STORAGE MANAGER QUE LENGTH
         CVD   R6,WORK             CONVERT TO DECIMAL...
         OI    WORK+7,X'0F'        FIX THE SIGN
         UNPK  XASQ,WORK+6(2)       ...AND EDIT
         SPACE
         LH    R6,RCVUICA         UNREFERENCED INTERVAL COUNT
         CVD   R6,WORK             CONVERT TO DECIMAL...
         OI    WORK+7,X'0F'        FIX THE SIGN
         UNPK  XUIC,WORK+6(2)       ...AND EDIT
         SPACE
         LH    R6,RCVAVQC         DEFERRED IO REQUESTS
         CVD   R6,WORK             CONVERT TO DECIMAL...
         OI    WORK+7,X'0F'        FIX THE SIGN
         UNPK  XDIO,WORK+6(2)       ...AND EDIT
         L     R4,R4SAV        RESTORE R4
         SPACE
         SR    R15,R15         CLEAR A REG
         LA    R5,3            NUMBER OF DOMAIN ENTRIES TO PROCESS
         LA    R14,HDRDMN      POINT TO FIRST DOMAIN ENTRY
         LA    R7,DMDT           SET DMDT POINTER
         USING DMDT,R7
DODMNS   DS    0H
*
         LH    R15,DMDTMPLT    GET MULTI-PROGRAMMING LEVEL TARGET
         CVD   R15,WORK        CHANGE IT TO DECIMAL
         OI    WORK+7,X'0F'    FIX THE SIGN
         UNPK  1(2,R14),WORK+6(2) MOVE IT TO THE OUTPUT AREA
*        LH    R15,DMDTRUA     GET NUMBER OF READY USERS
         LH    R15,DMDTCMPL    GET CURRENT MPL
         CVD   R15,WORK        CHANGE IT TO DECIMAL
         OI    WORK+7,X'0F'    FIX THE SIGN
         UNPK  4(2,R14),WORK+6(2) MOVE IT TO THE OUTPUT AREA
         LA    R14,6(R14)     POINT TO NEXT DOMAIN ENTRY
         LA    R7,DMDTLEN(R7)  BUMP PAST THIS DOMAIN
         BCT   R5,DODMNS       GO PROCESS ANOTHER
*
         MVI   WHICH,C'S'       DO STARTED TASKS FIRST
*
TOPASCB  DS    0H
*
         L     R2,FLCCVT        LOAD PTR TO CVT.
         USING CVT,2
         L     R2,CVTASVT       TO ASVT
         DROP  2
         USING ASVT,2
         L     R3,ASVTMAXU      MAXIMUM NUMBER OF ENTRIES
         LA    R4,ASVTFRST      FIRST ENTRY
         B     ASCB1ST
         SPACE
ASCBLOOP DS    0H
*
         LA    R4,4(,R4)        BUMP BY FOUR.
ASCB1ST  DS    0H
***********************************************************************
*
*   SET R5 = ASCB
*
         TM    0(R4),128        ASCB POINTER
         BO    NOGOOD           BRANCH IF BAD PTR.
         ICM   R5,B'1111',0(R4) LOAD THE ASCB POINTER
         USING ASCB,5
*
***********************************************************************
*
         LH    R1,ASCBASID      PICK UP ASID
         CVD   R1,DOUBLEWD
         MVC   WORK6(6),EDIT6
         ED    WORK6(6),DOUBLEWD+5
         MVC   ASID(4),WORK6+2  SET ASID
*
***********************************************************************
*
*   SET R6 = OUCB
*
         PRINT GEN
         LA    R6,OUCBDATA       SET OUCB POINTER
         L     R15,ASCBOUCB      POINT TO IT
         $AMODE 31
         MVC   0(OUCBDLEN,R6),0(R15)
         $AMODE 24
         PRINT NOGEN
*
         USING OUCB,6
*
***********************************************************************
*
         TM    OUCBYFL,OUCBLOG  CHECK FOR TSO.
         BO    ITISATSO
*
         TM    OUCBYFL,OUCBMNT  CHECK FOR MOUNT
         BO    ITISASTC
         TM    OUCBYFL,OUCBSTT  CHECK FOR STARTED TASK
         BZ    ITISABAT
*
** THE OUCB ALWAYS INDICATES STARTED TASK FOR BATCH INITIATORS
*
         L     R1,ASCBJBNI         POINT TO BATCH JOB NAME
         LTR   R1,R1               NO POINTER,
         BZ    ITISASTC            SO IT HAS TO BE A STARTED TASK
*
         L     R1,ASCBJBNS         POINT TO STARTED TASK NAME
         LTR   R1,R1               NO POINTER,
         BZ    ITISABAT            SO IT HAS TO BE BATCH
*
         CLC   0(5,R1),=C'INIT '   INITIATOR STARTED TASK?
         BE    ITISABAT            THEN IT'S REALLY BATCH
*
         TM    OUCBYFL,OUCBSTT  CHECK FOR STARTED TASK
         BO    ITISASTC
*
ITISABAT DS    0H
         MVC   ASID(4),WORK6+2  SET ASID
         CLI   WHICH,C'B'       AM I DOING BATCH
         BNE   NOGOOD           NO, SO SKIP THIS.
         CLC   SELCMD(3),=C'ALL'     DISPLAY EVERYTHING
         BE    DISPALL          YES
         CLC   SELCMD(3),=C'BAT'     DISPLAY BATCH
         BE    DISPALL          YES
         B     NOGOOD           NO, SO SKIP THIS.
ITISATSO DS    0H
         MVC   PSTEP(8),=CL8'TSU'   SET DEFAULT TSO PROCSTEPNAME
         CLI   WHICH,C'T'       AM I DOING TSO
         BNE   NOGOOD           NO, SO SKIP THIS.
         CLC   SELCMD(3),=C'ALL'     DISPLAY EVERYTHING
         BE    DISPTSO          YES
         CLC   SELCMD(3),=C'TSU'     DISPLAY TSO
         BE    DISPTSO          YES
         B     NOGOOD           NO, SO SKIP THIS.
ITISASTC DS    0H
*
         CLI   WHICH,C'S'       AM I DOING STARTED TASKS
         BNE   NOGOOD           NO, SKIP THIS
         MVC   PSTEP(8),=CL8'STC'   SET DEFAULT STC PROCSTEPNAME
         MVC   STEP(8),SPACES
         CLC   SELCMD(3),=C'ALL'     DISPLAY EVERYTHING
         BE    DISPDATA         YES
         CLC   SELCMD(3),=C'STC'     DISPLAY STARTED TASKS
         BE    DISPDATA         YES
         B     NOGOOD           NO, SO SKIP THIS.
DISPALL  DS    0H
         MVC   PSTEP(8),SPACES
DISPTSO  DS    0H
         MVC   STEP(8),SPACES
DISPDATA DS    0H
         SPACE
         MVC   JOB(8),=CL8'STARTING' AVOID JUNK
         L     R7,ASCBCSCB           LOAD PTR TO CMD SCH CTL BLK
         LTR   R7,R7
         BZ    MMASTER
         USING CHAIN,7
*
         CLI   WHICH,C'T'          AM I DOING TSO
         BE    TSOCHAIN
*
         CLI   WHICH,C'S'          AM I DOING STARTED TASKS
         BE    STCCHAIN
*
         L     R7,ASCBJBNI         POINT TO JOBNAME (IN CSCB?)
         LA    R1,CHKEY-CHAIN      BACK UP
         SR    R7,R1               BACK UP
         MVC   JOB(8),CHKEY        MOVE IN JOBNAME.
         MVC   STEP(8),CHSTEP      MOVE IN STEP NAME (NO PSTEP)
         MVC   PSTEP(8),CHPROCSN   MOVE IN PROC STEP NAME
*
         B     MNEXT               CONTINUE
*
STCCHAIN DS    0H
*
         L     R7,ASCBJBNS         POINT TO STARTED TASK NAME
         LTR   R7,R7               NO POINTER,
         BZ    MMASTER             SO SKIP THIS
         LA    R1,CHCLS-CHAIN      BACK UP
         SR    R7,R1               BACK UP
*
         MVC   JOB,CHCLS           MOVE IN JOBNAME.
         MVC   STEP(8),CHKEY       MOVE IN PROC NAME
*
         B     MMASTER             CONTINUE
*
TSOCHAIN DS    0H
         MVC   JOB,CHKEY           MOVE IN JOBNAME.
         MVC   STEP(8),CHCLS       MOVE IN PROC NAME
*
         B     MMASTER             CONTINUE
*
MMASTER  DS    0H
*
         CLC   ASID(4),=C'   1' ASID 1, MASTER SCHEDULER
         BNE   MNEXT            NO - CHECK NEXT
         MVC   JOB(8),=C'*MASTER*' MOVE IN JOBNAME.
*
MNEXT    DS    0H
*
         CLC   JOB(8),=CL8'INIT' IS IT A 'DRY' INIT??           KMT
         BE    NOGOOD           YEP...KISS OFF                  KMT
*
***********************************************************************
*
*   FILL IN ALLOCATED FRAME COUNT
*
***********************************************************************
*
         LH    R6,ASCBFMCT       LOAD ALLOC FRAME COUNT.
         SLL   R6,2             MULTIPLY BY FOUR.
         CVD   R6,WORK          CONVERT TO DECIMAL FORMAT.
         MVC   WORK7(7),EDIT7   GET READY FOR EDIT.
         ED    WORK7(6),WORK+5  EDIT THE RESULT.
         MVC   SIZE(5),WORK7+2
*
***********************************************************************
*
*   FILL IN WORKING SET SIZE
*
***********************************************************************
*
         LA    R6,OUCBDATA      PTR TO OUCB.
         LH    R7,OUCBWSS        LOAD OUCBWSS
*        SLL   R7,2             TIMES FOUR FOR K.
         CVD   R7,WORK          CONVERT TO DECIMAL.
         MVC   WORK7(7),EDIT7B  MOVE IN MASK
         ED    WORK7(6),WORK+5  EDIT THE RESULT.
         MVC   LINEWSS(5),WORK7+1
         SPACE
         TM    OUCBYFL,OUCBLOG  TEST FOR TSO MEMORY
         BZ    NOTTSO           BRANCH IF NOT TSO.
         MVI   TYPE,C'T'        MOVE IN TSO MEMORY INDICATION.
         B     TYPEOK           BYPASS REST OF TYPE PROCESSING.
NOTTSO   MVI   TYPE,C'S'        MOVE IN S/M/L INDICATION.
TYPEOK   DS    0H
         SPACE
         MVC   WHERE,=CL3'IN'   DEFAULT MEMORY POSITION IS IN.
         TM    OUCBSFL,OUCBNSW  TEST FOR NON-SWAP
         BZ    TRYWAIT          BRANCH IF NOT NON-SWAP.
         MVC   WHERE,=CL3'N/S'  MOVE IN NON-SWAP INDICATION.
         B     WHEREOK          BY-PASS REST OF POSITION PROCESSING.
TRYWAIT  DS    0H
         TM    OUCBQFL,OUCBOFF  CHECK FOR WAIT
         BZ    TRYOUT           BRANCH IF NOT.
         MVC   WHERE,=CL3'WT'   MOVE IN WAIT POSITION INDICATION.
         B     WHEREOK          BY-PASS REST OF POSITION PROCESSING.
TRYOUT   TM    OUCBQFL,OUCBOUT  TRY OUT QUEUE
         BZ    TRYTRIV          IF NOT, GO TRY TRIVIAL QUEUE STATUS.
         MVC   WHERE,=CL3'OUT'  MOVE IN OUT QUEUE POSITION.
         B     WHEREOK          BY-PASS REST OF POSITION PROCESSING.
TRYTRIV  TM    OUCBQFL,OUCBLSW  CHECK FOR TRIVIAL QUEUE POSITION.
         BZ    WHEREOK          IF NOT, EXIT POSITION PROCESSING.
         MVC   WHERE,=CL3'TRV'  MOVE IN TRIVIAL QUEUE POSITION.
WHEREOK  DS    0H
         TM    OUCBQFL,X'E0'    CHECK FOR TRANSITIONING STATUS
         BZ    NOTTRANS         BRANCH IF MEMORY NOT TRANSITIONING.
         MVC   WHERE(3),=CL3'<->' INDICATE TRANSITIONING.
NOTTRANS DS    0H
         SPACE
         SR    R7,R7            PREPARE FOR THE INSERT CHARACTERS.
         IC    R7,ASCBDP        LOAD ASCBDP
         SRL   R7,4             SHIFT OUT LOW ORDER NIBBLE.
         IC    R8,HEX(R7)       PICK THE EBCDIC CHARACTER.
         STC   R8,DP            STORE INTO OUTPUT FIELD.
         IC    R7,ASCBDP        LOAD THE DISPATCHING PRIORITY AGN.
         N     R7,=F'15'        TURN OFF HIGH ORDER NIBBLE.
         IC    R8,HEX(R7)       LOAD THE EBCDIC CHARACTER.
         STC   R8,DP+1          STORE INTO OUTPUT LINE.
         SPACE
***********************************************************************
*
*   FILL IN PERFORMANCE GROUP NUMBER
*
***********************************************************************
         SR    R7,R7            PREPARE FOR THE IC.
         IC    R7,OUCBSPG+1     NEW PGN  ???
         CVD   R7,WORK          CONVERT IT TO DECIMAL.
         MVC   WORK4(4),EDIT4              GET READY FOR EDIT.
         ED    WORK4(4),WORK+6 EDIT THE RESULT.
         MVC   LINEPGN(3),WORK4+1
*
***********************************************************************
*
*   FILL IN SWAP REASON CODE
*
***********************************************************************
         TM    OUCBQFL,OUCBLSW  SEE IF OUT SOMEHOW
         BZ    PFLDONE          MUST BE IN.
CHKSRC   DS    0H
         CLI   OUCBSRC,X'0A'    OUCBSRC (SWAP REASON CODE)
         BH    UNKNOWN          TOO BIG REASON UNKNOWN
         SR    R7,R7            CLEAR OUT WORK REGISTER        KMT
         IC    R7,OUCBSRC       GET OUCBSRC (SWAP REASON CODE) KMT
         SLL   R7,2             MULTIPLY BY 4 TO GET INDEX     KMT
         LA    R7,REASONS(R7)   GET ADDRESS OF REASON CODE     KMT
         MVC   STATUS,0(R7)     MOVE IT INTO THE MESSAGE       KMT
         B     PFLDONE                                         KMT
UNKNOWN  MVC   STATUS,=CL4'????'       SORRY..DONT KNOW        KMT
PFLDONE  DS    0H
*
***********************************************************************
*
*   FILL IN CPU TIME
*
***********************************************************************
*
         LM    R8,R9,ASCBEJST   LOAD UP EJST (CPU TIME - TASK TYPE)
         SRDL  R8,12            TO GET MICROSEC
         D     R8,=F'10000'     TO GET HUNDREDTHS AND MICROS
         LM    10,R11,ASCBSRBT  LOAD UP SRB TIME.
         SRDL  R10,12           TO GET MICROSEC
         D     R10,=F'10000'    TO GET HUNDREDTHS AND MICROS
         AR    R9,R11           ADD TOGETHER.
         CVD   R9,WORK             CONVERT IT TO DECIMAL.
         MVC   CPUTIME(8),EDIT8P   MOVE IN THE EDIT MASK.
         ED    CPUTIME-1(9),WORK+4 EDIT THE RESULT.
*
OUXBTEST DS    0H
*
** FOR JOBS THAT ARE SWAPPED IN, THE OUXB IS ADDRESSIBLE
*
         CLC   WHERE,=CL3'IN'   DEFAULT MEMORY POSITION IS IN.
         BE    OUXBSETP
         CLC   WHERE,=CL3'N/S'  MOVE IN NON-SWAP INDICATION.
         BE    OUXBSETP
*
         B     SIOLOOP
*
OUXBSETP DS    0H
*
         L     R15,ASCBOUXB         LOAD OUXB POINTER
         LTR   R15,R15
         BZ    SIOLOOP
*
         DROP  6
         LA    R6,OUXBDATA
*
         $AMODE 31
         MVC   0(OUXBDLEN,R6),0(R15)
         $AMODE 24
*
         USING OUXBNAME,6
*
*
***********************************************************************
*
*   FILL IN SWAP COUNT
*
***********************************************************************
         L     R7,OUXBSWCT      LOAD OUXB SWAP COUNT
         CVD   R7,WORK          CONVERT IT TO DECIMAL.
         MVC   LINESWC(4),EDIT4  MOVE IN THE EDIT MASK
         ED    LINESWC(4),WORK+6 EDIT THE RESULT.
*
***********************************************************************
*
*   FILL IN SERVICE UNITS
*   (NOT PRESENTLY DISPLAYED)
*
***********************************************************************
*
*        L     R7,OUXBJBS       OUXB SERVICE UNITS THIS SESSION
*        CVD   R7,WORK          CONVERT TO DECIMAL.
*        MVC   LINEWMS(7),=X'20202020202120' MOVE IN THE EDIT MASK.
*        ED    LINEWMS-1(8),WORK+4 EDIT THE RESULT.
*
SIOS1    DS    0H
*        LH    R1,ASCBXCNT
*        A     R1,OUXBIOSM
         L     R1,OUXBIOSM
         CVD   R1,WORK          CONVERT TO DECIMAL FORMAT.
         MVC   WORK8(8),EDIT8   MOVE MASK
         ED    WORK8(8),WORK+4  EDIT THE RESULT.
         MVC   SIOS(7),WORK8+1
*
SIOLOOP  DS    0H
*
         MVI   CPUFLAG,C' '     DEFAULT
         MVI   SIOFLAG,C' '     DEFAULT
*
*
*  NOW LOOP, CHECKING FOR THE JOBS WHOSE EXCP COUNT HAS INCREASED SINCE
*            THE LAST XDF DISPLAY
*
         LA    R7,CPUTAB        START OF TABLE
SIOLOOP1 DS    0H
         CLI   0(R7),X'FF'      END OF TABLE
         BE    JUSTOUT1         YES, IGNORE
*
         CLC   0(38,R7),SPACES  UNUSED ENTRY?
         BE    SIOADD1          YES, ADD THIS JOB TO TABLE
*
         CLC   0(31,R7),ASID    MATCH ON ASID, JOBNAME, STEPNAME, PSTEP
         BE    SIOMATCH         YES, COMPARE CURRENT SIO TIME VS LAST
*
         LA    R7,44(,7)        BUMP TO NEXT ENTRY
         B     SIOLOOP1         KEEP LOOPING
*
SIOMATCH DS    0H
*
         CLC   SIOS(5),SPACES   DO I HAVE ANYTHING?
         BNE   *+10             YES
         MVC   SIOS(7),33(R7)   NO, COPY ACCUM TO DISPLAY
*
         CLC   33(5,R7),SIOS            STORE 100'S OF SIO'S
         BE    CPUMATCH         IF THEY ARE THE SAME, DON'T SET FLAG
         MVC   33(7,R7),SIOS            STORE 100'S OF SIO'S
         MVI   SIOFLAG,C'*'     INDICATE SIO TIME HAS INCREASED
*
CPUMATCH DS    0H
         CLC   31(2,R7),CPUTIME+3       CHECK CPU WHOLE SECONDS
         BE    SWAPACUM         IF THEY ARE THE SAME, DON'T SET FLAG
         MVC   31(2,R7),CPUTIME+3       STORE CPU WHOLE SECONDS
         MVI   CPUFLAG,C'*'     INDICATE CPU TIME HAS INCREASED
         B     SWAPACUM         DISPLAY THE LINE
*
SIOADD1  DS    0H
         MVC   0(31,R7),ASID    MOVE IN ASID, JOBNAME, STEPNAME, PSTEP
         MVC   31(2,R7),CPUTIME+3       STORE CPU WHOLE SECONDS
         MVC   33(7,R7),SIOS            STORE EXCP COUNT
*
SWAPACUM DS    0H
*
         CLC   LINESWC(4),SPACES   DO I HAVE ANYTHING?
         BNE   *+10                YES
         MVC   LINESWC(4),40(R7)   NO, COPY ACCUM TO DISPLAY
         MVC   40(4,R7),LINESWC    YES, COPY DISPLAY TO ACCUM
*
*
JUSTOUT1 DS    0H
         CLI   DELTA,0          CHANGED ONLY
         BE    JUSTOUT2         NO, SHOW THEM ALL
*
         CLI   CPUFLAG,C'*'     IF CPU TIME HAS INCREASED
         BE    JUSTOUT2         SHOW IT
         CLI   SIOFLAG,C'*'     IF SIO COUNT HAS INCREASED
         BE    JUSTOUT2         SHOW IT
         B     NOGOOD           ELSE SKIP IT
*
JUSTOUT2 DS    0H
*
         BAL   R7,OUTMSG        ADD THE LINE TO THE SPF TABLE.
*
NOGOOD   BCT   R3,ASCBLOOP      BRANCH BACK TO LOOK AT NEXT MEMORY.
*
         CLI   WHICH,C'T'       FINISHED TSO?
         BE    GOTSOMEJ         YES, GO DISPLAY THE TABLE
*
         CLI   WHICH,C'S'       FINISHED STARTED TASKS
         MVI   WHICH,C'B'       SET DO BATCH NEXT
         BE    TOPASCB          YES, GO RE-LOOP THROUGH THE ASCB'S
*
         MVI   WHICH,C'T'       IF I'M NOT DOING STARTED TASKS OR TSO,
         B     TOPASCB          GO RE-LOOP THROUGH THE ASCB'S
*
         EJECT
EODAD    DS    0H
BYEBY    DS    0H
*
GOTSOMEJ DS    0H
*        MODESET KEY=NZERO,MODE=PROB
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBTOP,                                                 X
               XDFTABLE),                                              X
               VL,MF=(E,ISPARMS)
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBDISPL,                                               X
               XDFTABLE,                                               X
               XDFDATAB),                                              X
               VL,MF=(E,ISPARMS)
         MVC   TABWORK(80),XDFLINE   SAVE REPLY FIELDS
         C     R15,FZEROS     COMMAND ENTERED/LINE MODIFIED??
         BE    GOTSTUFF       YUP, GO PROCESS IT
         B     DUNWITIT       ALL DONE
GOTSTUFF DS    0H
         MVC   SELSAVE(3),XDFSEL     SAVE SEL CODE
         OC    SELSAVE(3),SPACES   UPPER-CASE IT
         MVC   XDFSEL(3),SPACES   SET SELECTION CODE TO SPACES
         MVC   JOBASID+1(3),TABWORK+1  SAVE ASID
         MVC   JOBNAMS(8),TABWORK+5  SAVE JOB NAME
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBGET,                                                 X
               XDFTABLE),                                              X
               VL,MF=(E,ISPARMS)
         MVC   TABGOTN(80),XDFLINE   SAVE REPLY FIELDS
         C     R15,F8         RC=8 MEANS CRP IS AT TOP
         BE    GOTCMD         THEN A COMMAND WAS ENTERED
*
* IF THE CRP IS NOT ZERO, THEN A LINE SELECTION CODE MUST HAVE BEEN
* ENTERED. I HAVE THE CURRENT LINE, SO EDIT THE SELECTION CODE.
*
         CLI   SELSAVE,C'C'   IS IT A JOB CANCEL
         BE    JOBCAN         GO DO IT
*
         CLI   SELSAVE,C'F'   IS IT A JOB CANCEL-FORCE
         BE    JOBFOR         GO DO IT
*
         CLI   SELSAVE,C'E'   IS IT RESET PERFORMANCE GROUP
         BE    JOBRESET       GO DO IT
*
         B     SHOWAGIN       GO REPEAT THE DISPLAY
*
         EJECT
JOBCAN   DS    0H
*
         CLC   USERID(5),JOBNAMS
         BE    JOBCANIT
         CLC   USERID(2),=C'AS'
         BE    JOBCANIT
         CLC   USERID(2),=C'AP'
         BE    JOBCANIT
         CLC   USERID(2),=C'BP'
         BE    JOBCANIT
         CLC   USERID(3),=C'MVS'
         BE    JOBCANIT
*
         B     SHOWAGIN       GO REPEAT THE DISPLAY
*
JOBCANIT DS    0H
         MVI   CMDLINE,C' '
         MVC   CMDLINE+1(71),CMDLINE
         MVC   CMDLINE(7),=C'CANCEL '
         MVC   CMDLINE+8(8),JOBNAMS    JOB NAME
*
         SLR   R0,0                    CLEAR REG 0
         LA    R1,CMDRESET            GET ADDRESS OF COMMAND
         LA    R15,1                   SET OPTION
         SVC   237                     ISSUE COMMAND VIA SVC 34
*
         B     SHOWAGIN       GO REPEAT THE SCREEN
         EJECT
JOBFOR   DS    0H  CANCEL JOB WITH CC=333
         CLC   USERID(2),=C'AS'
         BE    JOBFORCE
         CLC   USERID(2),=C'AP'
         BE    JOBFORCE
         CLC   USERID(2),=C'BP'
         BE    JOBFORCE
         CLC   USERID(3),=C'MVS'
         BE    JOBFORCE
*
         B     SHOWAGIN       GO REPEAT THE DISPLAY
*
JOBFORCE DS    0H  CANCEL JOB WITH CC=333
*
         PACK  DOUBLEWD(8),JOBASID(4)
         CVB   R2,DOUBLEWD
*
         STAX  DEFER=YES
*
         LA    R0,1                    SET AUTH ON
         SVC   235
         MODESET KEY=ZERO,MODE=SUP
*
         LR    R7,R13
         LA    R13,RTMWORK
       CALLRTM TYPE=MEMTERM,COMPCOD=819,ASID=(R2)
         LR    R13,R7
*
         MODESET KEY=NZERO,MODE=PROB
         SR    R0,R0                   SET AUTH OFF
         SVC   235
*
         STAX  DEFER=NO
*
         B     SHOWAGIN       GO REPEAT THE SCREEN
RTMWORK  DS    18F
         EJECT
*
JOBRESET DS    0H
*
         CLC   USERID(2),=C'AS'
         BE    ELEVATE
         CLC   USERID(2),=C'AP'
         BE    ELEVATE
         CLC   USERID(2),=C'BP'
         BE    ELEVATE
         CLC   USERID(3),=C'MVS'
         BE    ELEVATE
*
         B     SHOWAGIN       GO REPEAT THE DISPLAY
*
ELEVATE  DS    0H  CHANGE THE PERFORMANCE GROUP TO 3
*
         MVI   CMDLINE,C' '
         MVC   CMDLINE+1(71),CMDLINE
         MVC   CMDLINE(2),=C'E '
         MVC   CMDLINE+3(8),JOBNAMS    JES NUMBER
*
         LA    R15,CMDLINE+4           FIND A BLANK
*
         CLI   0(R15),C' '             GOT A BLANK?
         BE    *+12                    YUP
         LA    R15,1(,15)              BUMP POINTER
         B     *-12                    LOOP
*
         MVC   0(9,R15),=C',PERFORM='
         MVI   9(R15),HOTPG             TOP PERFORMANCE
*
         SLR   R0,0                    CLEAR REG 0
         LA    R15,1                   SET FUNCTION FLAG
         LA    R1,CMDRESET            GET ADDRESS OF COMMAND
         SVC   JDFSVC                  ISSUE COMMAND VIA SVC 34
*
         B     SHOWAGIN       GO REPEAT THE SCREEN
*
CMDRESET DC    Y(76),Y(00)
CMDLINE  DC    CL72'E JOB' COMMAND SVC 34 INPUT
*
*
         EJECT
*
DUNWITIT DS    0H
         L     R7,XDF7HOLD     RESTORE  RETURN ADDRESS
         BR    R7             RETURN TO OUR CALLER
         EJECT
*
*    ONLY COMMANDS BELOW ARE ALLOWED
*
GOTCMD   DS    0H
         CLC   XDFCMD(5),=C'DELTA'
         BE    CMDDELTA
         CLC   XDFCMD(3),=C'ALL'
         BE    GOODCMD
         CLC   XDFCMD(3),=C'TSU'
         BE    GOODCMD
         CLC   XDFCMD(3),=C'BAT'
         BE    GOODCMD
         CLC   XDFCMD(3),=C'STC'
         BE    GOODCMD
         B     SHOWAGIN
*
CMDDELTA DS    0H
         MVI   DELTA,0              LIMITED TO CHANGED
         CLC   XDFCMD(8),=C'DELTAOFF'
         MVC   XDFCMD(8),SPACES
         MVC   XDFCMD(3),SELCMD
         BE    GOODCMD
         MVI   DELTA,1              LIMITED TO CHANGED
GOODCMD  DS    0H
         MVC   SELCMD(3),XDFCMD
         MVI   XDFLINE,C' '   CLEAR TAB ENTRY AREA
         MVC   XDFLINE+1(79),XDFLINE
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBEND,                                                 X
               XDFTABLE),                                              X
               VL,MF=(E,ISPARMS)
         MVI   ATABFL,C'0'        NO ACTIVE TABLE
*
         XC    ISPARMS(40),ISPARMS
         B     SHOWAGIN       GO REPEAT THE SCREEN
         EJECT
INITMSG  MVI   XDFLINE,C' '   BLANK OUT THE
         MVC   XDFLINE+1(79),XDFLINE MESSAGE BUFFER
         BR    R7             RETURN TO OUR CALLER
         SPACE 5
         EJECT
OUTMSG   DS    0H
         ST    R7,OUT7HOLD    RETURN ADDRESS
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VPUT,                                                  X
               DANAMLST),                                              X
               VL,MF=(E,ISPARMS)
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBADD,                                                 X
               XDFTABLE),                                              X
               VL,MF=(E,ISPARMS)
         MVC   HILITE(8),=CL8'LOW'
         VPUT  HILITE
         VGET  HILITE
         MVI   XDFLINE,C' '
         MVC   XDFLINE+1(79),XDFLINE
         L     R7,OUT7HOLD    RETURN ADDRESS
         BR    R7             RETURN TO OUR CALLER
         TITLE 'EXECUTED INSTRUCTIONS'
MVNUMBER MVC   0(0,R15),0(R1) TO BE EXECUTED
         TITLE 'MISCELLANEOUS CONSTANTS'
         PRINT NOGEN
         LTORG
*
         DS    0F
ZEROFF   DC    X'000000FF'
P1       DC    PL1'+1'
P3ZERO   DC    PL3'+0'
XDF7HOLD DS    F
OUT7HOLD DS    F
*
DIGITS3  DC    X'202020'
THREEPT  DC    X'2020204B'
EDIT4    DC    X'40202120'
EDIT6    DC    X'402020202121'
EDIT7    DC    X'402020202120D2'
EDIT7B   DC    X'40202020212040'
EDIT8    DC    X'4020202020202020'
EDIT8P   DC    X'20202021204B2020'
*
JOBN     DC    C'JOB'
TSU      DC    C'TSU'
STC      DC    C'STC'
CTAB     DS    0H
ISPLINK  DC    V(ISPLINK)
*
L78      DC    F'78'
L40      DC    F'40'
FZEROS   DC    F'0'
F8       DC    F'8'
L1       DC    F'1'
L20      DC    F'20'
*
H1       DC    H'1'
H2       DC    H'2'
H8       DC    H'8'
*
VCOPY    DC    CL8'VCOPY '
ZUSER    DC    CL8'ZUSER '
USERID   DC    CL8' '
SPFMOVE  DC    CL8'MOVE   '
U8       DC    F'8'
*
TBADD    DC    C'TBADD    '
TBCREATE DC    C'TBCREATE '
TBDISPL  DC    C'TBDISPL  '
TBEND    DC    C'TBEND    '
TBGET    DC    C'TBGET    '
TBQUERY  DC    C'TBQUERY  '
TBSKIP   DC    C'TBSKIP   '
TBTOP    DC    C'TBTOP    '
TBVCLEAR DC    C'TBVCLEAR '
VDEFINE  DC    C'VDEFINE  '
VDELETE  DC    C'VDELETE  '
VGET     DC    C'VGET     '
VPUT     DC    C'VPUT     '
VREPLACE DC    C'VREPLACE '
*
NOWRITE  DC    C'NOWRITE  '
*
NULLENT  DC    C'()'
*
DANAMLST DC    C'('             DISPLAY ACTIVE NAME LIST
         DC    C'XDFSEL   '     SELECTION CODE
         DC    C'XDFLINE )'     JOB INFO
*
XDFCMD   DC    CL40' '
SELCMD   DC    CL40' '
*
SPACES   DC    CL80' '
CZEROS   DC    CL8'00000000'
*
CHAR     DC    C'CHAR    '
*
XDFDATAB DC    C'XDFDATAB'
XDFTABLE DC    C'XDFTABLE'
*
*
         TITLE 'REGISTER EQUATES'
***********************************************************************
*                                                                     *
*        REGISTER EQUATES                                             *
*                                                                     *
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         TITLE 'WORKING STORAGE AREAS'
DOUBLEWD DS    D              PACKED DECIMAL WORK AREA
TABADDRH DS    A              ADDRESS OF TABLE TO USE FOR OUTMSG
DNMADDRH DS    A              ADDRESS OF NAMES TO USE FOR OUTMSG
WHICH    DS    CL1
*
WORK4    DS    XL4
WORK6    DS    XL6
WORK7    DS    XL7
DELTA    DS    XL1
WORK8    DS    XL8
*
ISPARMS  DS    10F            PARM LIST FOR ISPLINK
*
*     THE ISP TABLE FLAGS INDICATE NO ACTIVE TABLE (0), OR ONE EXISTS
ATABFL   DS    XL1     DO I HAVE A CURRENT "DISPLAY ACTIVE" TABLE
*
*
         DS    0F
         DC    C'TABWORK '
TABWORK  DS    CL80
         DS    0F
         DC    C'TABGOTN '
TABGOTN  DS    CL80
         DS    0F
*
LINEWMS  DC    CL7' '
*
XDFSEL   DS    CL3            LINE SELECTION CODE
SELSAVE  DS    CL3            LINE SELECTION CODE SAVE AREA
*
*
         DS    0F
         DC    C'SELECTED ASID '
JOBASID  DC    CL4' '
*
         DS    0F
         DC    C'SELECTED JOBNAME'
*
JOBNAMS  DS    CL8            JOBNAME SAVE AREA
TYPE     DS    CL1            JOBTYPE SAVE AREA
*
WORK     DS    D
HEX      DC    C'0123456789ABCDEF'
REASONS  DC    C' ?? TPUTTGETLONGAUX<RS< WAITREQSENQ EXCHSWAP'
*                |...|...|...|...|...|...|...|...|...|...|...
         SPACE 2
         LTORG
         EJECT
SYSHDR   DS    0CL77
         DC    CL4'CPU='         CPU UTILIZATION
XCPU     DC    CL3'XXX',C'%'
         DC    CL5' AFC='        AVERAGE AVAILABLE FRAME COUNT
XAFC     DC    CL5'XXXXX'
         DC    CL5' PFR='        PAGE FAULT RATE
XPFR     DC    CL4'XXXX'
         DC    CL5' ASQ='        AUX STORAGE MANAGER QUE LENGTH
XASQ     DC    CL4'XXXX'
         DC    CL5' UIC='        UNIREFERENCED INTERVAL COUNT
XUIC     DC    CL4'XXXX'
         DC    CL5' DIO='        DEFERRED IO REQUESTS
XDIO     DC    CL4'XXXX'
         DC    CL5' DMN='       # OF DOMAINS IN ENTRIES
HDRDMN   DC    C' '             MULTI-PROGRAMMING LEVEL TARGET
XDMN1    DC    CL6'MT:RU '      MULTI-PROGRAMMING LEVEL TARGET
XDMN2    DC    CL6'MT:RU '      MULTI-PROGRAMMING LEVEL TARGET
XDMN3    DC    CL5'MT:RU'       MULTI-PROGRAMMING LEVEL TARGET
         DC    CL8' '
*
WRK5     DS    CL5
*
HILITE   DS    CL4
*
         DC    C'XDFLINE '
XDFLINE  DS    0CL78
ASID     DC    CL4' '
         DC    C' '
JOB      DC    CL8' '
         DC    C' '
PSTEP    DC    CL8' '
         DC    C' '
STEP     DC    CL8' '
         DC    C' '
WHERE    DC    CL3' '
STATUS   DC    CL4' '
SIZE     DC    CL5' '
LINEWSS  DC    CL5' '
         DC    C' '
DP       DC    CL2' '
LINEPGN  DC    CL3' '
LINESWC  DC    CL4' '
         DC    C' '
CPUTIME  DC    CL8' '
CPUFLAG  DC    C' '
SIOS     DC    CL7' '
SIOFLAG  DC    C' '
*
OUCBDLEN EQU   OUCBEND-OUCBNAME
*
OUCBDATA DS    XL(OUCBDLEN)
*
OUXBDLEN EQU   OUXBEND-OUXBNAME
*
OUXBDATA DS    XL(OUXBDLEN)
*
*.....................................................................*
*        MAPPING  FOR DMDT (DOMAIN DESCRIPTOR TABLE)
*.....................................................................*
         SPACE
DMDT     DS    0F
DMDTNO   DS    XL1             DOMAIN NUMBER
         DS    XL1             RESERVED
         DS    XL1             RESERVED
DMDTWT   DS    XL1             WEIGHTING FACTOR
DMDTMPLT DS    XL2             CURRENT MPL TARGET
DMDTRUA  DS    XL2             AVE # OF READY USERS
DMDTGOOU DS    XL2             CUR NUMBER OF USERS GOING OUT
DMDTCMPL DS    XL2             CURRENT MPL
DMDTOUTU DS    XL2             CURRENT # USERS ON OUT Q
DMDTINCU DS    XL2             CURRENT # SWAPPABLE INCORE USERS
DMDTRUC  DS    XL4             ACCUM FOR READY USER AVERAGE
DMDTWMS  DS    XL4             INTERVAL DOMAIN SERVICE ACCUM
DMDTTWSR DS    XL4             WEIGHTED INTERVAL DOMAIN SERVICE
DMDTAOBJ DS    XL1             USER PERFORMANCE OBJECTIVE NUMBER
DMDTDOBJ DS    XL1             DOMAIN PERFORMANCE OBJECTIVE NUMBER
DMDTFWKL DS    XL2             DOMAIN FIXED WORKLOAD LEVEL
DMDTCIDX DS    XL2             CONTENTION INDEX
DMDTNSW  DS    XL2             CURRENT NUMBER OF NONSWAPPABLE IN USERS
DMDTRUMX DS    XL2             MAX NUMBER OF READY USERS IN INTERVAL
DMDTFLGS DS    XL1             FLAG BYTE
DMDTRTO  EQU   128             RTO PERIOD IN THIS DOMAIN
         DS    XL1             RESERVED
DMDTTRNC DS    XL4             TRANSACTION COUNT FOR RTO
DMDTTRNT DS    XL4             ELAPSED TIME ACCUM FOR RTO
DMDTTWET DS    XL4             ELAPSED TIME AVERAGE FOR RTO
*
DMDTLO   DS    XL1             MIN  MPL LEVEL
DMDTHI   DS    XL1             MAX  MPL LEVEL
*
         DS    XL4             RESERVED
         DS    XL4             RESERVED
DMDTEND  DS    0X              END OF DMT
DMDTLEN  EQU   *-DMDT
DMDT2    DS    XL(DMDTLEN)
DMDT3    DS    XL(DMDTLEN)
R4SAV    DS    F                 REG. 4 SAVE AREA
CPUTAB   DC    100CL44' '        CPUTABLE SAVE AREA
         DC    X'FF'             END OF CPUTABLE
         ORG   CPUTAB
CLC26    DS    CL31              ASID, JOB, PROCSTEP, STEP
CLCPU    DS    CL2               CPU SECONDS
         CNOP  0,8
***
*  THE FOLLOWING WAS ADDED TO PROVIDE ADDRESSABILITY TO MVS/SP 2.1.3
*  SRM VALUES. THESE DSECTS MAY BE AVAILABLE IN "PVTMACS".
***
RMCT     DS    0F
RMCTNAME DS    CL4
RMCTCCT  DS    F                   POINTER TO CCT
         DS    XL172
RMCTDMDT DS    F                   POINTER TO DOMAIN TABLE
         DS    XL44
RMCTRCT  DS    F                   POINTER TO RCT
RMCTLEN  EQU   *-RMCT
*
CCT      DS    0F                  SRM RES CNTL TABLE               MNP
CCTCCT   DS    CL4                 'CCT'                            MNP
         DS    XL96                                                 MNP
         DS    XL2                                                  MNP
CCVUTILP DS    XL2                 SYSTEM CPU UTILIZATION           MNP
         DS    XL2                                                  MNP
CCVLGFMC DS    XL2                 AVERAGE AVAILABLE FRAME COUNT    MNP
CCTLEN   EQU   *-CCT
*
RCT      DS    0F                  SRM RES CNTL TABLE               MNP
RCTRCT   DS    CL4                 'RCT'                          NERDC
         DS    XL48
*
RCVCTMC  DS    H                   SAMPLE INTERVALS COUNT         NERDC
RCVUICA  DS    H                   UIC AVERAGE                    NERDC
RCVCPUA  DS    H                   CPU USAGE AVERAGE              NERDC
RCVAVQC  DS    H                   AVQ LOWW COUNT                 NERDC
RCVAFQA  DS    H                   AVG AVAIL FRAMES               NERDC
RCVASMQA DS    H                   ASM Q LENGTH AVERAGE           NERDC
RCVPTR   DS    H                   PAGING RATE                    NERDC
RCVDPR   DS    H                   DEMAND PAGING RATE             NERDC
RCVPAGRT DS    H                   TOTAL PAGING RATE              NERDC
RCVMSPP  DS    H                   PAGE DELAY TIME (MS)           NERDC
RCTLEN   EQU   *-RCT
***
*
***
         EJECT
*.....................................................................*
*        MAPPING DSECT FOR ASCB
*.....................................................................*
         IHAASCB
*.....................................................................*
*        MAPPING DSECT FOR SRM USER CONTROL BLOCK
*.....................................................................*
         IRAOUCB
         IHAOUXB
         SPACE 2
*
* EQUATES....
*
         SPACE 2
         IHAPSA
         IHAASVT
         IHAPVT
         IEECHAIN
         CVT   DSECT=YES
         END
