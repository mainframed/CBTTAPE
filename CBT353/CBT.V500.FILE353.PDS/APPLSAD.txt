//ASQCCSAD  JOB (X0002,QCC,ASQCC),'X-BRIAN COOK',
//   MSGCLASS=X,
//   MSGLEVEL=(1,1),CLASS=X,NOTIFY=ASQCC
/*JOBPARM L=999
//ASM     EXEC PGM=IEV90,REGION=1024K,
//             PARM='XREF(SHORT),OBJECT,NODECK,TERM'
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR
//         DD  DSN=SYS1.AMODGEN,DISP=SHR
//         DD  DISP=SHR,DSN=TECH.SERV.MACLIB   <=== TCBUVTAM
//SYSUT1   DD  SPACE=(CYL,(5,5)),
//             UNIT=SYSDA
//SYSUT2   DD  SPACE=(CYL,(5,5)),
//             UNIT=SYSDA
//SYSUT3   DD  SPACE=(CYL,(5,5)),
//             UNIT=SYSDA
//SYSTERM  DD  SYSOUT=*
//SYSPRINT DD  SYSOUT=*
//SYSPUNCH DD  DUMMY
//SYSLIN   DD   DSN=&&OBJSET,UNIT=SYSDA,SPACE=(CYL,(1,1)),
//             DISP=(MOD,PASS)
//SYSIN    DD  *
*          DATA SET TSO045     AT LEVEL 002 AS OF 01/23/80
*          DATA SET TSO045     AT LEVEL 001 AS OF 01/02/80
APPLMON  TITLE 'APPLSAD - SYSTEM ACTIVITY DISPLAY FOR APPLMON'
APPLSAD  CSECT
***********************************************************************
*
* THIS COMMAND HAS SEVERAL DIFFERENT MODES OF DISPLAY:
*
*     I   - THIS MODE WILL DISPLAY ONLY THOSE MEMORIES WHICH ARE EITHER
*           SWAPPED IN AND RUNNING OR SWAPPED OUT BUT READY TO RUN.
*     O   - THIS MODE WILL DISPLAY ALL ACTIVE MEMORIES IN THE SYSTEM,
*           REGARDLESS OF LOCATION.
*
*   TO SWITCH BETWEEN MODES, TYPE EITHER I OR O.
*
*
*      YOU MAY ALSO ENTER OPTIONS TO RESTRICT THE OUTPUT TO ONLY
*      TSO MEMORIES (IN EITHER IN/OUT OR WAITING STATUS ACCORDING
*      TO YOUR USE OF THE I AND O OPTIONS ABOVE) OR ONLY STARTED
*      TASK/BATCH JOB MEMORIES:
*
*     T  - RESTRICTS THE OUTPUT TO ONLY TSO MEMORIES
*
*     B  - RESTRICTS THE OUTPUT TO STARTED TASK/BATCH MEMORIES
*
*     A  - RESETS THE TSO/STARTED TASK/BATCH JOB SELECTION.
*
*   TO EXIT FROM THIS COMMAND, SIMPLY TYPE S
*
*  GLOSSARY OF TERMS FOR OUTPUT DISPLAY:
*
*    JOB       JOB NAME OF MEMORY
*    STEP      FOR BATCH JOBS WILL ALWAYS BE INIT.
*              FOR TSO JOBS AND STARTED TASKS WILL BE ACTUAL STEP NAME.
*    P         REASON CODE FOR SWAP OUT FROM OUCBPFL.
*              THE FOLLOWING ARE THE REASON CODES DISPLAYED:
*                U  0A MEMORY WAS UNILATERALLY SWAPPED
*                X  09 MEMORY WAS EXCHANGE SWAPPED
*                E  08 AN ENQHOLD HAS BEEN RECEIVED FOR THIS MEMORY
*                S  07 MEMORY REQUESTED SWAP OUT
*                D  06 INVOLUNTARY SWAPOUT DUE TO DETECTED WAIT
*                R  05 SWAPOUT CAUSED BY REAL STORAGE SHORTAGE
*                A  04 SWAPOUT CAUSED BY AUX. STORAGE SHORTAGE
*                L  03 MEMORY SWAPPED OUT BY MS6 LONG WAIT DETECTION
*                I  02 MEMORY IS IN INPUT TERMINAL WAIT
*                T  01 MEMORY IS IN OUTPUT TERMINAL WAIT
*                ?     UNABLE TO DETERMINE SWAPOUT REASON.
*    MEMORY    CURRENT AMOUNT OF MEMORY ALLOCATED TO THIS MEMORY.
*    WSS       SRM'S VIEW OF THE WORKING SET SIZE FOR THIS MEMORY.
*    T         TYPE OF MEMORY (S=STC OR BATCH, T=TSO USER)
*    LOC       CURRENT LOCATION OF THIS MEMORY:
*                IN  SWAPPED IN AND ELIGIBLE TO RUN.
*                OUT SWAPPED OUT BUT READY TO RUN.
*                WT  SWAPPED OUT AND NOT READY TO RUN.
*                N/S SWAPPED IN AND V=R OR NON-SWAPPABLE STATUS.
*                <-> TRANSITIONING BETWEEN STATES.
*    DP        DISPATCHING PRIORITY OF MEMORY (IN HEX).
*    PG        PERFORMANCE GROUP.
*    SC        NUMBER OF TIMES THE MEMORY HAS BEEN SWAPPED IN THE
*              CURRENT TRANSACTION.
*    WMR       CURRENT SRM WORKLOAD MANAGER RECOMMENDATION VALUE.
*              (ACTUALLY THE WORKLOAD LEVEL AT WHICH THIS MEMORY IS
*               OPERATING).
*    SRV       SERVICE ACCUMULATED IN THE LAST SWAPPED IN PERIOD.
*    CPU       CPU TIME IN SECONDS USED BY THE MEMORY (SRB PLUS TASK).
*
*
* MODS:  A) 11/26/79  K. TRUE
*          - NO 'DRY INITS' DISPLAY, DEFAULT OF 'BATCH,OUT' FOR DISPLAY
*          - MISC FIXES ETC TO ORIG CBT APPLSAD COMMAND.
*          - ADDED DYNAMIC DOMAIN DISPLAY AND SHUFFLED PGM TO FIT..
*
*        B) 1/2/80  M. FARMER
*          - CORRECT 0C4 IN SWITCH FROM DOMAIN TO JOBS DISPLAY
*          - NAMING CONVENTION TO TSO... - LATEST NEW STANDARD
*
*        C) 4/22/82 BRIAN COOK
*          - REVISED SRM LOGIC FOR MVS/SP1.3
*
*        D) 9/17/84 BRIAN COOK
*          - REVISED TO RUN UNDER APPLMON
*
*
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
         USING *,12
         STM   R14,R12,12(R13)  SAVE THE CALLERS REGISTERS
         LR    R12,R15          ESTABLISH
*
         USING PSA,0
         L     R1,PSATOLD       PICK UP TCB
*
         USING TCB,1
         L     R1,TCBUSER       PICK UP TCB USER AREA
*
         USING TCBUVTAM,1
         LR    R11,R13          PRESERVE CALLING PGM R13
         LA    R13,TCBUSAV2     MY SAVE AREA
         USING TCBUSAV2,13
         DROP  1
*
         ST    R11,4(,R13)        SAVE
         ST    R13,8(,R11)           AREA CHAINING
*
FINDCVT  DS    0H
*
         SR    R15,R15
         IC    R15,TCBUROWS     MAX ROWS
         BCTR  R15,0
         BCTR  R15,0
         BCTR  R15,0
*
         L     R2,FLCCVT        LOAD PTR TO CVT.
         L     R2,556(,R2)      TO ASVT
         L     R3,516(,R2)      MAXIMUM NUMBER OF ENTRIES
         LA    R4,524(,R2)      FIRST ENTRY MINUS 4.
*
         CR    R3,R15           COMPARE MAX VERSUS SCREEN SIZE
         BL    SETBUFFR         IF OK
         LR    R3,R15           SET TO MAXIMUM DISPLAY
*
SETBUFFR DS    0H
         LA    R1,ROW784        LOAD ADDRESS OF LINE4 BUFFER ADDRESS
         ST    R1,THISROWA      LOAD ADDRESS OF LINE4 BUFFER ADDRESS
         MVC   THISROW(2),0(R1) MOVE BUFFER ADDRESS FOR LINE4
         LA    R1,BUFFER-CLEAR
         STH   R1,SCRSIZE
         LA    R1,BUFFER        LOAD ADDRESS OF OUTPUT BUFFER.
         MVC   HEADING,HEAD1    MOVE IN SCREEN HEADING           KMT
         SPACE
ASCBLOOP LA    R4,4(,R4)        BUMP BY FOUR.
         ICM   R5,B'1111',0(R4) LOAD THE ASCB POINTER
***********************************************************************
*
*   SET R5 = ASCB
*
         USING ASCB,5
***********************************************************************
         BM    NOGOOD           BRANCH IF BAD PTR.
         TM    102(R5),X'04'    TEST FOR SWAPPED OUT.
INOROUT  BC    0,NOGOOD         BRANCH IF OUT AND NOT READY TO RUN.
         L     R6,ASCBOUCB       LOAD PTR TO OUCB FOR QUICK TSO CHK
***********************************************************************
*
*   SET R6 = OUCB
*
         USING OUCB,6
***********************************************************************
         TM    OUCBYFL,OUCBLOG  CHECK FOR TSO.
TSOORNO  BC    1,NOGOOD         TO BE FILLED IN IF BATCH ONLY.
BATCHORN BC    0,NOGOOD         TO BE FILLED IN LATER.
         TM    OUCBSFL,OUCBNSW  TEST FOR NON-SWAP
*        BO    NOGOOD           BRANCH IF NON-SWAP.
SWAPABLE BC    0,NOGOOD         TO BE FILLED IN LATER.
         SPACE
         MVI   LINE+2,C' '      CLEAR OUTPUT LINE TO BLANKS.
         MVC   LINE+3(77),LINE+2 DITTO.
         MVC   JOB(8),=CL8'STARTING' TO BYPASS GARBAGE
         ICM   R6,B'1111',172(R5) LOAD PTR TO JOBNAME.
         BZ    NOTJOB           BRANCH IF ZERO POINTER.
         MVC   JOB,0(R6)        OTHERWISE, MOVE IN JOBNAME.
         CLC   =CL8' ',X'18'(R6)  IS THERE A PROC STEP NAME?    KMT
         BE    NOTSTEP                                          KMT
         MVI   PSTEP,C'-'                                       KMT
         MVC   STEP(8),X'18'(R6)  MOVE IN PROC STEP NAME        KMT
         B     STEPOK                                           KMT
NOTSTEP  MVI   PSTEP,C' '                                       KMT
         MVC   STEP(8),X'38'(R6)  MOVE IN STEP NAME (NO PSTEP)  KMT
         B     STEPOK                                           KMT
NOTJOB   ICM   R6,B'1111',176(R5) PTR IF S/M/L.
         BZ    JOBOK            BYPASS THIS MOVE IF POINTER IS ZERO.
         MVC   JOB,0(R6)        MOVE IN JOBNAME FOR S/M/L MEMORIES.
JOBOK    DS    0H
         SPACE
         ICM   R7,B'1111',ASCBCSCB    POINTER TO THE TOP CSCB.
         BZ    STEPOK           BRANCH IF CSCB POINTER IS ZERO.
         CLI   4(R7),X'04'      CHECK TO SEE IF BATCH JOB OR S/M/L
         BE    STORMT           JOB IS START OR MOUNT
         CLI   4(R7),X'0C'      CHECK TO SEE IF BATCH JOB OR S/M/L
         BE    STORMT           JOB IS START OR MOUNT
         MVC   STEP(8),16(R7)   PICK UP CHCLS.
         B     STEPOK           BYPASS FURTHER CSCB PROCESSING.
STORMT   MVC   STEP(8),8(R7)    PICK UP CHKEY.
STEPOK   DS    0H
         CLC   JOB(8),=CL8'INIT' IS IT A 'DRY' INIT??           KMT
         BE    NOGOOD           YEP...KISS OFF                  KMT
         SPACE
         LH    R6,ASCBFMCT       LOAD ALLOC FRAME COUNT.
         SLL   R6,2             MULTIPLY BY FOUR.
         CVD   R6,WORK          CONVERT TO DECIMAL FORMAT.
         MVC   SIZE(6),=X'2020202120D2' GET READY FOR EDIT.
         ED    SIZE-1(6),WORK+5 EDIT THE RESULT.
         SPACE
***********************************************************************
*
*   SET R6 = OUCB, AGAIN
*
***********************************************************************
         SPACE
***********************************************************************
*
*   FILL IN WORKING SET SIZE
*
***********************************************************************
         L     R6,ASCBOUCB      PTR TO OUCB.
         LH    R7,OUCBWSS        LOAD OUCBWSS
*        SLL   R7,2             TIMES FOUR FOR K.
         CVD   R7,WORK          CONVERT TO DECIMAL.
         MVC   LINEWSS(6),=X'202020212040' MOVE IN THE EDIT MASK.
         ED    LINEWSS-1(6),WORK+5 EDIT THE RESULT.
         SPACE
         TM    18(R6),X'20'     TEST FOR TSO MEMORY
         BZ    NOTTSO           BRANCH IF NOT TSO.
         MVI   TYPE,C'T'        MOVE IN TSO MEMORY INDICATION.
         B     TYPEOK           BYPASS REST OF TYPE PROCESSING.
NOTTSO   MVI   TYPE,C'S'        MOVE IN S/M/L INDICATION.
TYPEOK   DS    0H
         SPACE
         MVC   WHERE,=CL3'IN'   DEFAULT MEMORY POSITION IS IN.
         MVI   LINE+1,X'E8'     DEFAULT TO HI-INTENSITY PROTECTED.
         TM    17(R6),X'80'     TEST FOR NON-SWAP
         BZ    TRYWAIT          BRANCH IF NOT NON-SWAP.
         MVC   WHERE,=CL3'N/S'  MOVE IN NON-SWAP INDICATION.
         B     WHEREOK          BY-PASS REST OF POSITION PROCESSING.
TRYWAIT  DS    0H
         TM    16(R6),X'08'     CHECK FOR WAIT
         BZ    TRYOUT           BRANCH IF NOT.
         MVC   WHERE,=CL3'WT'   MOVE IN WAIT POSITION INDICATION.
         MVI   LINE+1,X'60'     MOVE IN 3270 LOW-INTENSITY ATTR BYTE.
         B     WHEREOK          BY-PASS REST OF POSITION PROCESSING.
TRYOUT   TM    16(R6),X'04'     TRY OUT QUEUE
         BZ    TRYTRIV          IF NOT, GO TRY TRIVIAL QUEUE STATUS.
         MVC   WHERE,=CL3'OUT'  MOVE IN OUT QUEUE POSITION.
         MVI   LINE+1,X'60'     MOVE IN 3270 LOW-INTENSITY ATTR BYTE.
         B     WHEREOK          BY-PASS REST OF POSITION PROCESSING.
TRYTRIV  TM    16(R6),X'02'     CHECK FOR TRIVIAL QUEUE POSITION.
         BZ    WHEREOK          IF NOT, EXIT POSITION PROCESSING.
         MVC   WHERE,=CL3'TRV'  MOVE IN TRIVIAL QUEUE POSITION.
WHEREOK  DS    0H
         TM    16(R6),X'E0'     CHECK FOR TRANSITIONING STATUS
         BZ    NOTTRANS         BRANCH IF MEMORY NOT TRANSITIONING.
         MVC   WHERE(3),=CL3'<->' INDICATE TRANSITIONING.
NOTTRANS DS    0H
         SPACE
         SR    R7,R7            PREPARE FOR THE INSERT CHARACTERS.
         IC    R7,43(,R5)       LOAD ASCBDP
         SRL   R7,4             SHIFT OUT LOW ORDER NIBBLE.
         IC    R8,HEX(R7)       PICK THE EBCDIC CHARACTER.
         STC   R8,DP            STORE INTO OUTPUT FIELD.
         IC    R7,43(,R5)       LOAD THE DISPATCHING PRIORITY AGN.
         N     R7,=F'15'        TURN OFF HIGH ORDER NIBBLE.
         IC    R8,HEX(R7)       LOAD THE EBCDIC CHARACTER.
         STC   R8,DP+1          STORE INTO OUTPUT LINE.
         SPACE
***********************************************************************
*
*   FILL IN PERFORMANCE GROUP NUMBER
*
***********************************************************************
         SR    R7,R7            PREPARE FOR THE IC.
         IC    R7,OUCBSPG+1     NEW PGN  ???
         CVD   R7,WORK          CONVERT IT TO DECIMAL.
         MVC   LINEPGN(3),=X'202120' MOVE IN THE EDIT MASK.
         ED    LINEPGN-1(4),WORK+6 EDIT THE OUTPUT FIELD.
         SPACE
***********************************************************************
*
*   FILL IN SWAP COUNT
*
***********************************************************************
         LH    R7,OUCBSWC       LOAD OUCBSWC (SWAP COUNT)
         CVD   R7,WORK          CONVERT IT TO DECIMAL.
         MVC   LINESWC(3),=X'202120' MOVE IN THE EDIT MASK.
         ED    LINESWC-1(4),WORK+6 EDIT THE OUTPUT FIELD.
         SPACE
***********************************************************************
*
*   FILL IN WORK LOAD MANAGER RECOMMENDATION
*
***********************************************************************
         L     R7,OUCBWMR       LOAD OUCBWMR
         LTR   R7,R7            SEE IF HI-ORDER BIT IS ON
         BNM   WMROK             INDICATING WMR INVALID.
         MVC   LINEWMR(5),=C'*N/A*' IF SO, MOVE IN NOT AVAIL INDIC.
         MVI   LINEWMS+6,C'-'   AND BLANK OUT THE SERVICE COUNTER.
         B     NOWMS            BY-PASS THE SERVICE PROCESSING.
WMROK    DS    0H
         SRL   R7,8             DIVIDE BY 256
         CVD   R7,WORK          CONVERT IT TO DECIMAL.
         MVC   LINEWMR(5),=X'2020202120' MOVE IN EDIT MASK.
         ED    LINEWMR-1(6),WORK+5 EDIT THE RESULT.
WMROKA   DS    0H
         SPACE
***********************************************************************
*
*   FILL IN SWAP REASON CODE
*
***********************************************************************
         TM    OUCBQFL,OUCBLSW  SEE IF OUT SOMEHOW
         BZ    PFLDONE          MUST BE IN.
CHKSRC   DS    0H
         CLI   OUCBSRC,X'0A'    OUCBSRC (SWAP REASON CODE)
         BH    UNKNOWN          TOO BIG REASON UNKNOWN
         SR    R7,R7            CLEAR OUT WORK REGISTER        KMT
         IC    R7,OUCBSRC       GET OUCBSRC (SWAP REASON CODE) KMT
         SLL   R7,2             MULTIPLY BY 4 TO GET INDEX     KMT
         LA    R7,REASONS(R7)   GET ADDRESS OF REASON CODE     KMT
         MVC   STATUS,0(R7)     MOVE IT INTO THE MESSAGE       KMT
         B     PFLDONE                                         KMT
UNKNOWN  MVC   STATUS,=CL4'????'       SORRY..DONT KNOW        KMT
PFLDONE  DS    0H
         SPACE
***********************************************************************
*
*   FILL IN SERVICE UNITS
*
***********************************************************************
         L     R7,OUCBWMS       OUCBWMS LOAD SERVICE UNITS THIS XACTION
         CVD   R7,WORK          CONVERT TO DECIMAL.
         MVC   LINEWMS(7),=X'20202020202120' MOVE IN THE EDIT MASK.
         ED    LINEWMS-1(8),WORK+4 EDIT THE RESULT.
         SPACE
***********************************************************************
*
*   FILL IN CPU TIME
*
***********************************************************************
NOWMS    LM    R8,R9,ASCBEJST   LOAD UP EJST (CPU TIME - TASK TYPE)
         SRDL  R8,12            TO GET MICROSEC
         D     R8,=F'10000'     TO GET HUNDREDTHS AND MICROS
         LM    10,R11,200(R5)   LOAD UP SRB TIME.
         SRDL  R10,12           TO GET MICROSEC
         D     R10,=F'10000'    TO GET HUNDREDTHS AND MICROS
         LTR   R9,R9            TEST JST CPU TIME
         BP    TESTSRB          IF ITS POISTIVE GO ON
         NOPR  R9               DUMMY INSTR FOR TSO TEST TO STOP ON
TESTSRB  LTR   R11,R11          TEST SRB CPU TIME
         BP    ADSRBJST         IF ITS OK GO ADD SRB AND JST CPU TIME
         NOPR  R9               ANOTHER DUMMY INSTR.
ADSRBJST AR    R9,R11           ADD TOGETHER.
         CVD   R9,WORK          CONVERT IT TO DECIMAL.
         MVC   CPUTIME(8),=X'20202021204B2020' MOVE IN THE EDIT MASK.
         ED    CPUTIME-1(9),WORK+4 EDIT THE RESULT.
         SPACE
         MVC   3(80,R1),LINE    MOVE THE LINE TO THE 3270 BUFFER.
         MVI   0(R1),X'11'      SBA CODE
         MVC   1(2,R1),THISROW  BUFFER ADDRESS
         L     R7,THISROWA      ADDRESS OF THIS LINE'S BUFFER ADDRESS
         LA    R7,BALENGTH(,R7) BUMP RO NEXT
         ST    R7,THISROWA      ADDRESS OF NEXT LINE'S BUFFER ADDRESS
         MVC   THISROW(2),0(R7)
         LH    R7,SCRSIZE       BUMP BUFFER COUNTER.
         LA    R7,83(,R7)
         STH   R7,SCRSIZE
         LA    R1,83(,R1)       SEE IF THE BUFFER IS FULL YET.
         C     R1,ADBUF
         BE    PUTIT            IF YES, THE GO PUT THIS SCREEN.
NOGOOD   BCT   R3,ASCBLOOP      BRANCH BACK TO LOOK AT NEXT MEMORY.
         LA    R4,BUFFER        LOAD ADDRESS OF OUTPUT BUFFER.
         CR    R4,R1            ARE WE STILL AT THE BEGINNING.
         BE    FINDCVT          IF SO, GO START AT THE TOP AGAIN.
         B     BUILDHDR
*
THISROW  DS    H
THISROWA DS    F
*
**  LOOK THRU SRM CONTROL BLOCKS TO BUILD HEADER INFO
*
BUILDHDR DS    0H
         L     R4,16           ---> CVT...
         L     R7,356(R4)      ---> PVT...
***********************************************************************
*
*   FILL IN SYSTEM CPU UTILIZATION
*
***********************************************************************
         L     R4,604(R4)      ---> RMCT... 1ST MODULE OF IRARMCNS
         L     R5,4(R4)        ---> IRACCT
         LH    R6,102(R5)         CPU UTILIZATION
         CVD   R6,WORK             CONVERT TO DECIMAL...
         OI    WORK+7,X'0F'        FIX THE SIGN
         UNPK  HDRCPU,WORK+6(2)     ...AND EDIT
         SPACE
***********************************************************************
*
*   FILL IN AVERAGE AVAILABLE FRAME COUNT
*
***********************************************************************
         LH    R6,2(R7)           AVERAGE AVAILABLE FRAME COUNT
         CVD   R6,WORK             CONVERT TO DECIMAL...
         OI    WORK+7,X'0F'        FIX THE SIGN
         UNPK  HDRAFC,WORK+6(2)     ...AND EDIT
         SPACE
***********************************************************************
*
*   FILL IN PAGE FAULT RATE
*
***********************************************************************
         LH    R6,X'226'(R4)      PAGE FAULT RATE
         CVD   R6,WORK             CONVERT TO DECIMAL...
         OI    WORK+7,X'0F'        FIX THE SIGN
         UNPK  HDRAPR,WORK+6(2)     ...AND EDIT
         SPACE
         LH    R6,X'224'(R4)      AUX. STORAGE MANAGER QUE LENGTH
         CVD   R6,WORK             CONVERT TO DECIMAL...
         OI    WORK+7,X'0F'        FIX THE SIGN
         UNPK  HDRASQ,WORK+6(2)     ...AND EDIT
         SPACE
         LH    R6,X'21E'(R4)      UNREFERENCED INTERVAL COUNT
         CVD   R6,WORK             CONVERT TO DECIMAL...
         OI    WORK+7,X'0F'        FIX THE SIGN
         UNPK  HDRUIC,WORK+6(2)     ...AND EDIT
         SPACE
         LH    R6,X'240'(R4)      DEFERRED IO REQUESTS
         CVD   R6,WORK             CONVERT TO DECIMAL...
         OI    WORK+7,X'0F'        FIX THE SIGN
         UNPK  HDRDIO,WORK+6(2)     ...AND EDIT
         SPACE
         L     R7,180(R4)      ---> DMDT..     R4--> RMCT
         SR    R15,R15         CLEAR A REG
         LA    R5,3            NUMBER OF DOMAIN ENTRIES TO PROCESS
         LA    R14,HDRDMN      POINT TO FIRST DOMAIN ENTRY
         LA    R7,20(R7)       BUMP PAST FIRST DOMAIN
DODMNS   DS    0H
*        IC    R15,0(R7)       GET DOMAIN NUMBER
*        CVD   R15,WORK        CHANGE IT TO DECIMAL
*        OI    WORK+7,X'0F'    FIX THE SIGN
*        UNPK  1(2,R14),WORK+6(2) MOVE IT TO THE OUTPUT AREA
         LH    R15,4(R7)       GET MULTI-PROGRAMMING LEVEL TARGET
         CVD   R15,WORK        CHANGE IT TO DECIMAL
         OI    WORK+7,X'0F'    FIX THE SIGN
         UNPK  1(2,R14),WORK+6(2) MOVE IT TO THE OUTPUT AREA
         LH    R15,6(R7)       GET NUMBER OF READY USERS
         CVD   R15,WORK        CHANGE IT TO DECIMAL
         OI    WORK+7,X'0F'    FIX THE SIGN
         UNPK  4(2,R14),WORK+6(2) MOVE IT TO THE OUTPUT AREA
         LA    R14,6(R14)     POINT TO NEXT DOMAIN ENTRY
         LA    R7,20(R7)       BUMP PAST THIS DOMAIN
         BCT   R5,DODMNS       GO PROCESS ANOTHER
         SPACE
*        STCK  TIME            STORE TIME OF DAY CLOCK
PUTIT    LA    R1,CLEAR         PREPARE
         LH    R0,SCRSIZE       BUMP BUFFER COUNTER.
         L     R15,TCBUVPUT
         BALR  R14,R15
         LTR   R15,R15            OK?
         BZ    VPUTOK             YES
         LR    R4,R15              ELSE SET RC
         ABEND 998,DUMP,STEP       AND ABEND - CANT GO ON IF CANT SEND.
VPUTOK   DS    0H
*
         LA    R1,BUFFER
         LA    R0,80
         L     R15,TCBUVGET
         BALR  R14,R15
         LTR   R4,R15            OK?
         BZ    VGETOK             YES
         ABEND 999,DUMP,STEP      ELSE ABEND - CANT GO ON IF CANT RCV..
*
VGETOK   DS    0H
         CLI   BUFFER,X'7D'     CHECK IF TERMINATION REQUESTED.
         BNE   EODAD            BYE BYE BABY...
*
         OI    BUFFER+6,X'40'   GET TO UPPER CASE BEFORE COMPARE.
         CLI   BUFFER+6,C' '    IS ANYONE HOME?
         BNE   CHKITOUT
         TM    MODESW,DOMAIN    IS DOMAIN MODE IN PROCESS       KMT
         BO    DISPDMN          YES....GO DO IT                 KMT
CHKITOUT CLI   BUFFER+6,C'S'    CHECK IF TERMINATION REQUESTED.
         BE    EODAD            BYE BYE BABY...
ISDOMAIN CLI   BUFFER+6,C'D'    CHECK FOR DOMAIN MODE
         BNE   NOTDMN
         OI    MODESW,DOMAIN+MORSTUF    ON WITH THE SHOW....    MF1-80
         B     GOODGRIF         FINITO..
NOTDMN   NI    MODESW,255-DOMAIN SOMETHING ELSE..TURN OFF DOMAINKMT
         CLI   BUFFER+6,C'T'    CHECK FOR TSO ONLY.
         BNE   BATCHCHK         IF NOT, TRY FOR BATCH ONLY MODE.
         MVI   BATCHORN+1,X'80' RESET THE BRANCHES TO GET
         MVI   TSOORNO+1,X'00'     ONLY TSO MEMORIES.
         MVC   OPTMODE,=C'TSO'  INDICATE MODE OF OPERATION      KMT
         B     GOODGRIF         BY-PASS FURTHER INPUT CHECKING.
BATCHCHK CLI   BUFFER+6,C'B'    CHECK FOR BATCH ONLY
         BNE   SWAPCHK          BRANCH TO CHECK OTHER OPTIONS.
         MVI   TSOORNO+1,X'10'  RESET THE BRANCHES TO GET
         MVI   BATCHORN+1,X'00'    ONLY BATCH (STC) MEMORIES.
         MVC   OPTMODE,=C'BAT'  INDICATE MODE OF OPERATION      KMT
         B     GOODGRIF         BY-PASS FURTHER INPUT CHECKING.
SWAPCHK  CLI   BUFFER+6,C'N'    CHECK FOR SWAPPABLE ONLY
         BNE   OTHERCHK         BRANCH TO CHECK OTHER OPTIONS.
         MVI   SWAPABLE+1,X'10' ONLY SWAPABLE MEMORIES.
         MVC   OPTMODE,=C'SWP'  INDICATE MODE OF OPERATION      KMT
         B     GOODGRIF         BY-PASS FURTHER INPUT CHECKING.
OTHERCHK CLI   BUFFER+6,C'A'    CHECK FOR EVERYTHING
         BNE   XYZ              IF NOT, TRY FOR IN OR OUT MODES.
         MVI   TSOORNO+1,X'00'  IF EVERYTHING, NOP ALL THE
         MVI   BATCHORN+1,X'00'    SELECTION BRANCHES.
         MVI   SWAPABLE+1,X'00' ONLY SWAPABLE MEMORIES.
         MVC   OPTMODE,=C'ALL'  INDICATE MODE OF OPERATION      KMT
XYZ      DS    0H
         CLI   BUFFER+6,C'O'    CHECK FOR OUT MODE
         BNE   WHATHECK
         MVI   INOROUT+1,X'00'  NOP THE BRANCH
         OI    MODESW,MORSTUF   INDICATE CHANGE OF MODES.
         MVC   OPTINO,=C'ANY'   INDICATE IN/OUT SELECTION       KMT
         B     GOODGRIF
WHATHECK CLI   BUFFER+6,C'I'    CHECK FOR IN OR READY MODE
         BNE   GOODGRIF
         MVI   INOROUT+1,X'10'  TAKE THE BRANCH
         OI    MODESW,MORSTUF   INDICATE MODE SWITCH.
         MVC   OPTINO,=C'IN '   INDICATE IN/OUT SELECTION       KMT
         B     GOODGRIF
         SPACE
GOODGRIF DS    0H
         MVC   SCRSIZE,=H'0'    RESET SCREEN SIZE.
         LA    R1,BUFFER        PREPARE
         LA    R6,BUFFER           FOR
         ICM   R6,8,C' '              THE
         LA    R7,BUFLEN                 MOVE
         LA    R8,BUFFER                    CHARACTER
         SR    R9,R9                           LONG
         MVCL  R6,R8                              TO CLEAR BUFFER.
         TM    MODESW,DOMAIN    IS DOMAIN THE NAME?            KMT
         BO    DISPDMN          GO DOIT SAM                    KMT
         TM    MODESW,MORSTUF   CHECK FOR CHANGE OF MODES.
         BNO   MODEOK           BRANCH IF SAME MODE.
         NI    MODESW,255-MORSTUF RESET MODE CHANGE.
         B     FINDCVT          START OVER FROM BEGINNING.
         SPACE
MODEOK   DS    0H
         LTR   R3,R3            CHECK TO SEE IF MORE MEMORIES TO PROCES
         BZ    FINDCVT          NO, TAKE IT FROM THE TOP
         BCT   R3,ASCBLOOP      WAS THERE ONLY ONE LEFT AT BUFFER FULL
         B     FINDCVT          YES, START OVER FROM THE TOP.
         EJECT
*.....................................................................*
*     FCI MOD TO DISPLAY DOMAINS ON THE SCREEN                        *
*.....................................................................*
         SPACE
DISPDMN  L     R5,16               (R5)=ADDR OF CVT                FCI1
         L     R5,CVTOPCTP(R5)     (R5)=ADDR OF RMCT               FCI1
         L     R9,RMCTDMDT(R5)     (R9)=ADDR OF DMDT (FIRST ENTRY) FCI1
         XR    R8,R8               CLEAR REG 4                     FCI1
         ICM   R8,B'0011',RMCTDMNC(R5)   (R4)=NUMBER OF DOMAINS    FCI1
         LTR   R8,R8               ANYBODY HOME?                   FCI1
         BNP   BUILDHDR             NOPE-> EXIT STAGE LEFT         FCI1
         USING DMDT,R9             ESTABLISH ADDR TO DOMAIN DESC. TABLE
         LA    R1,BUFFER                                           KMT
         MVC   SCRSIZE,=H'0'       INITIALIZE POINTERS AND BUFFER  KMT
         MVC   HEADING,FCIHEAD     INIT DOMAIN DISPLAY HEADING     KMT
         SPACE
DMNLOOP  MVC   FCIBUF(2),=X'1DE8' MOVE IN HIGH INTENSITY FIELD.    FCI1
         MVI   FCIBUF+2,X'40'     CLEAR OUTPUT LINE                FCI1
         MVC   FCIBUF+3(78),FCIBUF+2 TO BLANKS.                    FCI1
         SPACE 2
         LA    R4,DMDTNO                                           FCI1
         LA    R5,MNO                                              FCI1
         BAL   R6,EDIT             GO EDIT DOMAIN NUMBER INTO MSG  FCI1
         CLI   MNO+2,C'-'          DOMAIN=0?                       FCI1
         BNE   DMNCONT              NO-> SKIP FIXUP                FCI1
         MVI   MNO+2,C'0'                                          FCI1
DMNCONT  DS    0H                                                  FCI1
         SPACE
         LA    R4,DMDTLO                                           FCI1
         LA    R5,MLO                                              FCI1
         BAL   R6,EDIT             GO EDIT MIN MPL INTO MSG        FCI1
         SPACE
         LA    R4,DMDTHI                                           FCI1
         LA    R5,MHI                                              FCI1
         BAL   R6,EDIT             GO EDIT MAX MPL INTO MSG        FCI1
         SPACE
         LA    R4,DMDTWT                                           FCI1
         LA    R5,MWT                                              FCI1
         BAL   R6,EDIT             GO EDIT WGHT FACTOR INTO MSG    FCI1
         SPACE
         LA    R4,DMDTMPLT                                         FCI1
         LA    R5,MMPLT                                            FCI1
         BAL   R6,EDITH            GO EDIT CURRENT MPL TGT INTO MSGFCI1
         SPACE
         LA    R4,DMDTRUA                                          FCI1
         LA    R5,MRUA                                             FCI1
         BAL   R6,EDITH            GO EDIT AVE # OF READY USRS     FCI1
         SPACE
         LA    R4,DMDTGOOU                                         FCI1
         LA    R5,MGOOU                                            FCI1
         BAL   R6,EDITH            GO EDIT CURRENT # USRS GOING OUTFCI1
         SPACE
         LA    R4,DMDTCMPL                                         FCI1
         LA    R5,MCMPL                                            FCI1
         BAL   R6,EDITH            GO EDIT CURRENT MPL INTO MSG    FCI1
         SPACE
         LA    R4,DMDTOUTU                                         FCI1
         LA    R5,MOUTU                                            FCI1
         BAL   R6,EDITH            GO EDIT CURNT # USRS ON OUT Q   FCI1
         SPACE
         LA    R4,DMDTINCU                                         FCI1
         LA    R5,MINCU                                            FCI1
         BAL   R6,EDITH            GO EDIT CURNT # SWAPBL IN C USR FCI1
         SPACE
         CLC   2(79,R1),FCIBUF+2    ANY CHANGE FROM LAST TIME      FCI1
         BNE   MVLIN                                               FCI1
         MVI   FCIBUF+1,X'60'      INDICATE NO CHANGE (LOW INTENS) FCI1
MVLIN    MVC   0(81,R1),FCIBUF     MOVE BUFFER TO LINE             FCI1
         SPACE 2
         LA    R9,DMDTLEN(R9)      BUMP POINTER                    FCI1
         LH    R7,SCRSIZE         SAVE ADDRESS OF LAST ENTRY       FCI1
         LA    R7,81(R7)           AND BUMP                        FCI1
         STH   R7,SCRSIZE           SIZE FOR TPUT TO USE           FCI1
         LA    R1,81(R1)           BUMP BUFFER POINTER             FCI1
         BCT   R8,DMNLOOP          LOOP THROUGH ALL DOMAINS        FCI1
         B     BUILDHDR                                            FCI1
         EJECT
*.....................................................................*
*     LOCAL SUBROUTINES   EDIT,EDITH                                  *
*.....................................................................*
         SPACE
EDIT     ICM   R4,B'0001',0(R4)    GET BYTE TO CONVERT             FCI1
         N     R4,=X'000000FF'                                     FCI1
         B     DOEDIT                                              FCI1
EDITH    ICM   R4,B'0011',0(R4)                                    FCI1
         N     R4,=X'0000FFFF'                                     FCI1
DOEDIT   C     R4,=F'999'          TO LARGE?                       FCI1
         BNH   EDIT1                                               FCI1
         MVC   0(3,R5),=C'***'     INDICATE TOO LARGE              FCI1
         BR    R6                    AND RETURN                    FCI1
EDIT1    CVD   R4,WORK             CONVERT TO DECIMAL              FCI1
         MVC   0(3,R5),=X'202020'  MOVE IN MASK                    FCI1
         BCTR  R5,0                DECREMENT POSITION FOR SPACE    FCI1
         ED    0(4,R5),WORK+6        BEFORE NUMBER AND EDIT        FCI1
         CLC   1(3,R5),=C'   '     ANYTHING THERE                  FCI1
         BE    EDIT2                                               FCI1
         BR    R6                                                  FCI1
EDIT2    MVI   3(R5),C'-'          INDICATE PLACE HOLDER           FCI1
         BR    R6                  RETURN                          FCI1
         EJECT
EODAD    DS    0H
*
         L     R13,4(,R13)           AREA CHAINING
         LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
*
         EJECT
MODESW   DC    X'00'  MODE CHANGE.
MORSTUF  EQU   X'80'  MORE JUNK TO DO.....
DOMAIN   EQU   X'08'  PLEASE DO DOMAIN STUFF                       FCI1
WORK     DS    D
HEX      DC    C'0123456789ABCDEF'
SAVE     DS    18F
REASONS  DC    C' ?? TPUTTGETLONGAUX<RS< WAITREQSENQ EXCHSWAP'
*                |...|...|...|...|...|...|...|...|...|...|...
SBACODE  DC    X'114040'
X        DC    CL80' '
         ORG   X
LINE     DC    X'1D60'
JOB      DC    CL8' '
         DC    C' '
PSTEP    DC    C' '
STEP     DC    CL8' '
         DC    C' '
WHERE    DC    CL3' '
         DC    C' '
STATUS   DC    CL4' '
         DC    C' '
SIZE     DC    CL5' '
         DC    CL2' '
LINEWSS  DC    CL5' '
         DC    CL2' '
TYPE     DC    C' '
         DC    C' '
DP       DC    CL2' '
         DC    C' '
LINEPGN  DC    CL3' '
         DC    C' '
LINESWC  DC    CL3' '
         DC    C' '
         DC    C' '
         DC    C' '
LINEWMR  DC    CL5' '
         DC    C' '
LINEWMS  DC    CL7' '
         DC    C' '
CPUTIME  DC    CL8' '
         DC    C' '
         ORG X+90
         DS    0H
SCRSIZE  DC    AL2(BUFFER-CLEAR)
TPUTFLG  DC    X'03'
ADBUF    DC    A(Z)
CLR      DC    X'C7115D7E1140403C40400011404013'
         SPACE 2
*   C7= WCC FOR CLEAR  11= SET BUFFER ADDRESS 4040 = BUFADD POS 0000
*   3C= REPEAT TO ADDRESS 4040(0000) CHAR(00) 11 = SET BUFFER ADDRESS
*   4040 = POSITION 0000(R=1 C=1)  13 = INSERT CURSOR
         SPACE 2
*             XXXXXXXX -XXXXXXXX XXX XXXX XXXXX  XXXXX  X XX XXX XXX
HEAD1   DC X'11C260'      LINE 3
        DC CL77'  JOB     STEP   .STATUS. MEMORY   WSS  T DP  PG SWAPS X
                 WMR    SRV    CPU'
*              XXXXX XXXXXXX XXXXXXXX
        DC X'11C150'      LINE 2
FCIHEAD  DC   CL77'         DOMAIN MIN MAX WGT CUR TGT RDY  IN >OT OUT'
*                   456789.123456789.123456789.123456789.123456789.012
*                            XXX   XXX XXX XXX XXX XXX XXX XXX XXX XXX
FCIBUF   DS    CL81                                                FCI1
FCIOFST  EQU   9                 OFFSET INTO FCIBUFFER             FCI1
         ORG   FCIBUF+FCIOFST+4                                    FCI1
MSG      DS    CL43                                                FCI1
         ORG   FCIBUF+FCIOFST+5                                    FCI1
MNO      DS    CL3                                                 FCI1
         ORG   FCIBUF+FCIOFST+11                                   FCI1
MLO      DS    CL3                                                 FCI1
         ORG   FCIBUF+FCIOFST+15                                   FCI1
MHI      DS    CL3                                                 FCI1
         ORG   FCIBUF+FCIOFST+19                                   FCI1
MWT      DS    CL3                                                 FCI1
         ORG   FCIBUF+FCIOFST+23                                   FCI1
MCMPL    DS    CL3                                                 FCI1
         ORG   FCIBUF+FCIOFST+27                                   FCI1
MMPLT    DS    CL3                                                 FCI1
         ORG   FCIBUF+FCIOFST+31                                   FCI1
MRUA     DS    CL3                                                 FCI1
         ORG   FCIBUF+FCIOFST+35                                   FCI1
MINCU    DS    CL3                                                 FCI1
         ORG   FCIBUF+FCIOFST+39                                   FCI1
MGOOU    DS    CL3                                                 FCI1
         ORG   FCIBUF+FCIOFST+43                                   FCI1
MOUTU    DS    CL3                                                 FCI1
         ORG
         SPACE 2
         LTORG
         TITLE 'SBA  - BUFFER ADDRESSES'
*
* THE SET BUFFER ADDRESS (SBA) ORDER CODE IS FOLLOWED BY A 2-BYTE
* BUFFER ADDRESS THAT CORRESPONDS (BUT NOT IN ANY RATIONAL WAY) TO
* THE ROWS AND COLUMNS ON THE DISPLAY TUBE. THIS TABLE IS PROVIDED
* TO SUPPORT DYNAMICALLY BUILDING THE BUFFER ADDRESS FIELDS WHEN THE
* ROW AND COLUMN IS KNOWN.
*
* EACH BUFFER ADDRESS ENTRY CORRESPONDS TO A ROW ON THE DISPLAY
* THE FIRST BYTE IS THE SAME FOR ALL ROWS, THE SECOND, THIRD AND
* FOURTH BYTES DEFINE COLUMNS 1, 7, AND 8, RESPECTIVELY.
*
* TO PICK UP THE BUFFER ADDRESS YOU WANT, MULTIPLY THE ROW NUMBER
* BY THE LENGTH OF EACH BUFFER ADDRESS ENTRY AND ADD THE START ADDRESS
* OF THE TABLE.
*
* FOR EXAMPLE, THE ROW3 ENTRY IS =>  X'C1',X'50',X'D6',X'D7'
* THE BUFFER ADDRESS CODES ARE:
*
* ROW 3, COL 1 = X'C150'
* ROW 3, COL 7 = X'C1D6'
* ROW 3, COL 8 = X'C1D7'
*
SBACODES DS    0H
ROW781   DC    X'40',X'40',X'C6',X'C7'
BALENGTH EQU   *-ROW781
ROW782   DC    X'C1',X'50',X'D6',X'D7'
ROW783   DC    X'C2',X'60',X'E6',X'E7'
ROW784   DC    X'C3',X'F0',X'F6',X'F7'
ROW785   DC    X'C5',X'40',X'C6',X'C7'
ROW786   DC    X'C6',X'50',X'D6',X'D7'
ROW787   DC    X'C7',X'60',X'E6',X'E7'
ROW788   DC    X'C8',X'F0',X'F6',X'F7'
ROW789   DC    X'4A',X'40',X'C6',X'C7'
ROW7810  DC    X'4B',X'50',X'D6',X'D7'
ROW7811  DC    X'4C',X'60',X'E6',X'E7'
ROW7812  DC    X'4D',X'F0',X'F6',X'F7'
ROW7813  DC    X'4F',X'40',X'C6',X'C7'
ROW7814  DC    X'50',X'50',X'D6',X'D7'
ROW7815  DC    X'D1',X'60',X'E6',X'E7'
ROW7816  DC    X'D2',X'F0',X'F6',X'F7'
ROW7817  DC    X'D4',X'40',X'C6',X'C7'
ROW7818  DC    X'D5',X'50',X'D6',X'D7'
ROW7819  DC    X'D6',X'60',X'E6',X'E7'
ROW7820  DC    X'D7',X'F0',X'F6',X'F7'
ROW7821  DC    X'D9',X'40',X'C6',X'C7'
ROW7822  DC    X'5A',X'50',X'D6',X'D7'
ROW7823  DC    X'5B',X'60',X'E6',X'E7'
ROW7824  DC    X'5C',X'F0',X'F6',X'F7'
ROW7825  DC    X'5E',X'40',X'C6',X'C7'
ROW7826  DC    X'5F',X'50',X'D6',X'D7'
ROW7827  DC    X'60',X'60',X'E6',X'E7'
ROW7828  DC    X'61',X'F0',X'F6',X'F7'
ROW7829  DC    X'E3',X'40',X'C6',X'C7'
ROW7830  DC    X'E4',X'50',X'D6',X'D7'
ROW7831  DC    X'E5',X'60',X'E6',X'E7'
ROW7832  DC    X'E6',X'F0',X'F6',X'F7'
ROW7833  DC    X'E8',X'40',X'C6',X'C7'
ROW7834  DC    X'E9',X'50',X'D6',X'D7'
ROW7835  DC    X'6A',X'60',X'E6',X'E7'
ROW7836  DC    X'6B',X'F0',X'F6',X'F7'
ROW7837  DC    X'6D',X'40',X'C6',X'C7'
ROW7838  DC    X'6E',X'50',X'D6',X'D7'
ROW7839  DC    X'6F',X'60',X'E6',X'E7'
ROW7840  DC    X'F0',X'F0',X'F6',X'F7'
ROW7841  DC    X'F2',X'40',X'C6',X'C7'
ROW7842  DC    X'F3',X'50',X'D6',X'D7'
ROW7843  DC    X'F4',X'60',X'E6',X'E7'
BTABLEN4 EQU   *-ROW781
         EJECT
**********************************************************************
* NOTE: JUNK FROM THIS POINT UNTIL 'Z' ARE ALL PART OF THE           *
*   'BIG SCREEN'  LEAVE THIS STUFF TOGETHER AND PUT CLEAR AT THE     *
*   END OF THE PROGRAM IF YOU DONT WANT TO OVERFLOW MY ONE CHEAP     *
*   BASE REGISTER..THAT DC 21CL81' ' WILL CHEW UP A LOT OF SPACE.... *
**********************************************************************
         SPACE 1
CLEAR    DC    X'7EC71140401D4013401DE8'
*LEAR    DC    X'C7115D7F1140403C4040001140401D4013401DE8'
         SPACE 2
*   C7= WCC FOR CLEAR  11= SET BUFFER ADDRESS 4040 = BUFADD POS 0000
*   3C= REPEAT TO ADDRESS 4040(0000) CHAR(00) 11 = SET BUFFER ADDRESS
*   4040 = POSITION 0000(R=1 C=1)  1D = START FIELD 40= UNPROTECTED
*   13 = INSERT CURSOR 40 = BLANK 1D = START FIELD E8 = PROTECT HILITE
         SPACE 2
SYSHDR   DS    0CL77
         DC    CL4'CPU='         CPU UTILIZATION
HDRCPU   DC    CL3'XXX',C'%'
         DC    CL5' AFC='        AVERAGE AVAILABLE FRAME COUNT
HDRAFC   DC    CL4'XXXX'
         DC    CL5' PFR='        PAGE FAULT RATE
HDRAPR   DC    CL4'XXXX'
         DC    CL5' ASQ='        AUX STORAGE MANAGER QUE LENGTH
HDRASQ   DC    CL4'XXXX'
         DC    CL5' UIC='        UNIREFERENCED INTERVAL COUNT
HDRUIC   DC    CL4'XXXX'
         DC    CL5' DIO='        DEFERRED IO REQUESTS
HDRDIO   DC    CL4'XXXX'
         DC    CL6' DMNS='       # OF DOMAINS IN ENTRIES
HDRDMN   DC    3CL6' MT:RU'      MULTI-PROGRAMMING LEVEL TARGET
         DC    X'11C150'         LINE 2
OPTDEF   DC    79C'-'            # OF READY USERS
         ORG   OPTDEF+3
         DC    C'OPTIONS='
OPTMODE  DC    C'BAT',C','
OPTINO   DC    C'ANY'
         ORG
HEADING  DC    CL77' '
BUFFER   DC    43CL83' '
LENGTH   EQU   BUFFER-CLEAR
Z        DS    X
BUFLEN   EQU   Z-BUFFER
         EJECT
*.....................................................................*
*        MAPPING DSECT FOR ASCB
*.....................................................................*
         IHAASCB
*.....................................................................*
*        MAPPING DSECT FOR SRM USER CONTROL BLOCK
*.....................................................................*
         IRAOUCB
*.....................................................................*
*        MAPPING DSECT FOR DMDT (DOMAIN DESCRIPTOR TABLE)             *
*.....................................................................*
         SPACE
DMDT     DSECT
DMDTNO   DS    XL1             DOMAIN NUMBER
DMDTLO   DS    XL1             MIN  MPL LEVEL
DMDTHI   DS    XL1             MAX  MPL LEVEL
DMDTWT   DS    XL1             WEIGHTING FACTOR
DMDTMPLT DS    XL2             CURRENT MPL TARGET
DMDTRUA  DS    XL2             AVE # OF READY USERS
DMDTGOOU DS    XL2             CURRENT # USERS GOING OUT
DMDTCMPL DS    XL2             CURRENT MPL
DMDTOUTU DS    XL2             CURRENT # USERS ON OUT Q
DMDTINCU DS    XL2             CURRENT # SWAPPABLE INCORE USERS
DMDTRUC  DS    XL4             ACCUM FOR READY USER AVERAGE
DMDTEND  DS    0X              END OF DMT
DMDTLEN  EQU   *-DMDT
         SPACE 2
*
* EQUATES....
*
CVTOPCTP EQU   X'25C'          POINTER TO RMCT
RMCTDMDT EQU   X'B4'           POINTER TO DOMAIN TABLE
RMCTDMNC EQU   X'BC'           COUNT OF NUMBER OF DOMAINS
RMCTWMST EQU   X'18'           ADDRESS OF WLM SPEC TABLE
         SPACE 2
         PRINT NOGEN
         IKJTCB
         IHAPSA
TCBUVTAM DSECT
         TCBUVTAM PFX=TCBU
         END
/*
//LKED     EXEC PGM=HEWLF064,PARM=(XREF,LET,LIST,NCAL,TERM),
//             COND=(4,LT,ASM),REGION=1024K
//SYSLIN   DD   DSN=&&OBJSET,DISP=(OLD,DELETE)
//         DD   DDNAME=SYSIN
//SYSLIB   DD   DSN=SYS2.LINKLIB,DISP=SHR
//SYSLMOD  DD   DISP=SHR,DSN=TECH.SERV.LOAD
//SYSUT1   DD   DSN=&&SYSUT1,UNIT=VIO,SPACE=(1024,(50,20))
//SYSTERM  DD   SYSOUT=*
//SYSPRINT DD   SYSOUT=*
//SYSIN    DD   *
  SETCODE AC(1)
     NAME APPLSAD(R)
/*
