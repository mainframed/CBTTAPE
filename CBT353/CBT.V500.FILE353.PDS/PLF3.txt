//ASQCCPLF  JOB (X0002,QCC,ASQCC),'X-COOK BRIAN',
//   MSGCLASS=X,
//   MSGLEVEL=(1,1),CLASS=X,NOTIFY=ASQCC
/*JOBPARM L=99
//* *****************************************************************
//* *****************************************************************
//*                                                               ***
//* TECH.ASQCC.SOURCE(PLF3)                                       ***
//*                                                               ***
//* *****************************************************************
//* *****************************************************************
//ASM1    EXEC PGM=IEV90,REGION=2048K,
//        PARM='OBJECT,NODECK,TERM,XREF(FULL)'
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR
//         DD  DISP=SHR,DSN=SYS1.AMODGEN
//         DD  DSN=SYS2.MACLIB,DISP=SHR <== SPFMACS
//SYSUT1   DD  SPACE=(CYL,(5,5)),
//             UNIT=SYSDA
//SYSUT2   DD  SPACE=(CYL,(5,5)),
//             UNIT=SYSDA
//SYSUT3   DD  SPACE=(CYL,(5,5)),
//             UNIT=SYSDA
//SYSTERM  DD  SYSOUT=*
//SYSPRINT DD  SYSOUT=*
//SYSPUNCH DD  DUMMY
//SYSLIN   DD   DSN=&&OBJSET,UNIT=SYSDA,SPACE=(CYL,(5,5)),
//             DISP=(MOD,PASS)
//SYSIN    DD  *
         PRINT OFF
         COPY  SPFMACS
         PRINT ON
         PRINT NOGEN
PLF3     TITLE 'PLF - PAN LIBRARY FACILITY CHANGE LOG'
***********************************************************************
*                                                                     *
*   CHANGE LOG                                                        *
*                                                                     *
*  10/13/86  BRIAN COOK    CHANGED "VGET" MACRO'S TO USE "POOL=".     *
*                                                                     *
*   9/18/86  BRIAN COOK    ADDED RETURN CODE TEST FOR EDIT-CANCEL.    *
*                                                                     *
*   8/19/86  BRIAN COOK    CONTINUE SR#994, ADD PROMPT FOR "MOVE" OR  *
*                          "COPY", TRANSFER LIB, CHANGE STATUS TO     *
*                          "PROD", ETC.                               *
*                                                                     *
*   8/15/86  BRIAN COOK    STARTED SR#994, MOVE OPTION FOR CHANGE     *
*                          CONTROL. CHANGED SOURCE NAME TO PLF3.      *
*                                                                     *
*   8/04/86  CHANGED ALL REQUEST FOR DYNAMIC DATASET ALLOCATIONS      *
*            FROM PAN.DATA, PAN.LIST TO @TEMP.PANDATA, @TEMP.PANLIST  *
*                                                          MARK SPAIN *
*                                                                     *
*  12/03/85  PER SR#877, DO NOT ALLOW TRANSFER IF "FROM"   BRIAN COOK *
*            LIBRARY MEMBER DOES NOT HAVE A COMMENT.                  *
*                                                                     *
*  11/15/85  ADDED POPEN/PCLOSE AFTER COMM/NOCOMM. THERE   BRIAN COOK *
*            MAY BE A PROBLEM WITH "PSERCH" IGNORING THE              *
*            "NAME1" FIELD.                                           *
*                                                                     *
*  10/15/85  ADDED "DALRLSE" TO ALL DYNAMIC ALLOCATIONS.   BRIAN COOK *
*                                                                     *
*  10/04/85  ADDED 5TH BASE REGISTER.                  BRIAN COOK     *
*                                                                     *
*  10/03/85  REVISED "XFER" JCL DUE TO BUG IN PAN#2.   BRIAN COOK     *
*                                                                     *
*   9/01/85  CONVERTED TO ISPF V2R1M2 DYNAMIC PANEL.   BRIAN COOK     *
*                                                                     *
*   7/10/85  ADDED "LIST" COMMAND TO PRINT PAN.LIST.   BRIAN COOK     *
*            ADDED "X" LINE COMMAND AND "XFER" COMMAND                *
*            TO SUPPORT TRANSFER FROM ONE PAN LIBRARY                 *
*            TO ANOTHER.                                              *
*                                                                     *
*   7/09/85  ADDED ACCESS CODE FOR DISABLE.            BRIAN COOK     *
*            ADDED "SHOW" COMMAND TO DISPLAY ACCESS                   *
*            CODES.                                                   *
*                                                                     *
*   6/27/85  CHANGED PAN OPEN/CLOSE TO ONCE PER LIB    BRIAN COOK     *
*                                                                     *
*   6/26/85  ADD ACCESS CODE LINE COMMANDS:            BRIAN COOK     *
*           "AA" ADD ACCESS CODE (IN NEWNAME COLUMN)                  *
*           "AC" CHANGE (OLD,NEW  IN NEWNAME COLUMN)                  *
*           "AD" DELETE (OLD ACCESS CODE IN NEWNAME COLUMN)           *
*                                                                     *
*           ALSO ADDED "COMM" AND "NOCOMM" COMMANDS                   *
*           TO CONTROL DISPLAY OF COMMENTS                            *
*                                                                     *
*   6/25/85  1. REMOVE OLD VARIABLES.                  BRIAN COOK     *
*            2. ADD SCROLL AMOUNT IN COMMAND LINE.                    *
*            3. ACCESS CODE CHECKING. (IN NEWNAME COLUMN)             *
*                                                                     *
*   6/11/85  CALL ISPF DISPLAY MODULE DIRECTLY,        BRIAN COOK     *
*            REMOVE DIAG MGMT SRVCS CALLS.                            *
*                                                                     *
*   5/29/85  MAJOR PERFORMANCE CHANGES:                BRIAN COOK     *
*            1. CHANGED THE DIRECTORY DISPLAY LOGIC                   *
*               SO THAT AN IN-CORE TABLE IS BUILT ON                  *
*               THE FLY.                                              *
*            2. CHANGED THE TABLE DISPLAY SO THAT ONLY                *
*               20 MEMBERS ARE SHOWN ON THE SCREEN. THE               *
*               PF KEYS CANNOT BE USED FOR SCROLLING THE              *
*               DIRECTORY.                                            *
*                                                                     *
*   5/31/85  CHANGED TO USE "SPFMACS".                 BRIAN COOK     *
*                                                                     *
*   7/30/84  CHANGED PRINT FILE "FREE" TO DYN ALLOC    BRIAN COOK     *
*                                                                     *
*   6/05/84  ADDED "PRINT MEMBER FUNCTION"             BRIAN COOK     *
*                                                                     *
*   6/08/84  ADDED "PSPOOL" COMMAND TO IDENTIFY        BRIAN COOK     *
*            LIBRARIES WITH PANSPOOL FORMAT.                          *
*                                                                     *
*                                                                     *
*                                                                     *
***********************************************************************
         TITLE 'PLF - PAN LIBRARY FACILITY GENERAL DESCRIPTION'
***********************************************************************
*                                                                     *
*                                                                     *
*               PAN LIBRARY FACILITY (PLF)                            *
*               (FOR ISPF V2R1M2 5665-319)                            *
*                                                                     *
*                                                                     *
* ABSTRACT: EDIT, BROWSE AND UTILITY FUNCTIONS FOR PAN LIBRARIES.     *
*                                                                     *
*                                                                     *
* FUNCTION: PLF IS DESIGNED TO OPERATE UNDER ISPF USING THE VERSION 2 *
*           DYNAMIC PANEL DISPLAY FOR THE MEMBER SELECTION PANEL.     *
*           IT  WILL NOT WORK WITH ANY PRIOR VERSION OF SPF.          *
*                                                                     *
*           IT DYNAMICALLY INVOKES THE SPF EDIT AND BROWSE            *
*           FUNCTIONS USING THE FOLLOWING DATASETS:                   *
*                                                                     *
*                         &USERID.@TEMP.PANCARD                       *
*                         &USERID.@TEMP.PANDATA                       *
*                         &USERID.@TEMP.PANLIST.                      *
*                                                                     *
*           THE FOLLOWING EQUATE SHOULD BE SET TO A VALUE THAT IS     *
*           COMFORTABLY GREATER THAN THE LARGEST PAN LIBRARY THAT     *
*           PLF WILL EVER HANDLE:                                     *
*                                                                     *
DIRMAX   EQU 10000       LARGEST PAN LIB HAS LESS THAN 10,000 MEMBERS
*                                                                     *
*                                                                     *
* EXTERNAL REFERENCES: ISPLINK, PAM                                   *
*                                                                     *
*                                                                     *
* MACROS USED: SAVE                                                   *
*              RETURN                                                 *
*              GETMAIN                                                *
*              STAX                                                   *
*              ENQ                                                    *
*              DEQ                                                    *
*              TPUT                                                   *
*              FREEMAIN                                               *
*              $JQE                                                   *
*              $JOE                                                   *
*              CVT                                                    *
*              CVTUSERS (USER)                                        *
*              TCBWORK  (USER)                                        *
*                                                                     *
*                                                                     *
* ISPF PANEL VARIABLES:                                               *
*                                                                     *
*      PLFZ     MENU 1 OPTION.                                        *
*      PANLIB   MENU 1 DSN OF PAN LIBRARY.                            *
*      PANCONTR MENU 1 CONTROL WORD. IF CONTROL-WORD PROTECTED.       *
*                                                                     *
*      XFERTO   MENU 3 DSN OF PAN LIBRARY, TARGET FOR TRANSFER.       *
*      XFERCON  MENU 3 CONTROL WORD. IF CONTROL-WORD PROTECTED.       *
*      XFERSTA  MENU 3 PROMPT FOR CHANGING STSTUS TO "PROD'           *
*      XFERMOD  MENU 3 PROMPT FOR "MOVE" OR "COPY".                   *
*      PJC1     MENU 3 JOB CARD FOR TRANSFER JCL.                     *
*      PJC2     MENU 3 JOB CARD FOR TRANSFER JCL.                     *
*                                                                     *
***********************************************************************
         TITLE 'PLF - PROGRAM INITIALIZATION'
***********************************************************************
*
* NOTE THE FOLLOWING ENTRY POINT LOGIC IS NON-STANDARD. IN PARTICULAR,
*      IT WILL NOT WORK WITH CALLED PL/I PROGRAMS.
*
***********************************************************************
PLF3     CSECT
         PRINT GEN
         USING *,13,12,11,10,9
         B     100(,R15)
         DC    17F'0'
         DC    CL28'PLF3_&SYSDATE_&SYSTIME'
         PRINT NOGEN
         STM   14,12,12(13)   SAVE CALLING PGM'S REGISTERS
         LA    R2,0(,R15)     LOAD EPA IN R2 FOR WORK REGISTER
         ST    R13,4(R2)      SAVE THE CALLER'S R13
         ST    R2,8(R13)      LINK SAVE AREAS
         LR    R13,R2         SET SAVE AREA AND BASE 1
*
SETBASES DS    0H
         L     R2,L4096       SET INCREMENT
         LA    R12,0(R2,R13)  SET BASE 2
         LA    R11,0(R2,R12)  SET BASE 3
         LA    R10,0(R2,R11)  SET BASE 4
         LA    R9,0(R2,R10)   SET BASE 5
*
         XC    0(4,R13),0(13) CLEAR WORD 1 OF SAVE AREA
*
         MVI   PLFTABFL,C'0'  NO ACTIVE TABLE
         MVI   PLFPOPEN,C'0'  PAN NOT OPEN
*
         BAL   R7,INITFLDS    INITIALIZATION
*
         TITLE 'PLF - MAIN PROCESSING LOOP'
SHOWAGIN DS    0H
*
         MVI   DONEFLAG,C'0'  NOT ALL DONE
         BAL   R7,SHOWMENU    PROMPT FOR INPUT
         CLI   DONEFLAG,C'1'  AM I ALL DONE
         BE    PLFEOJ         YUP
*
         BAL   R7,PLFAPAN     PROCESS THE LIBRARY
         B     SHOWAGIN       RE-SHOW THE MENU
*
PLFEOJ   DS    0H
*
         BAL   R7,PLFDONE
*
         L     R13,4(R13)     RESTORE THE CALLER'S R13
         LM    14,12,12(13)   RETURN TO OUR CALLER
         SR    15,15          SET RC=0
         BR    14             AND RETURN
*
DONEFLAG DC    C'0'
*
L4096    DC    F'4096'
*
         TITLE 'TERMINATION LOGIC'
DONE7    DS    F
PLFDONE DS     0H
*
         ST    R7,DONE7
*
         CLI   PLFTABFL,C'0'      NO ACTIVE TABLE
         BE    PLFTOPEN        CHECK PAN OPEN STATUS
*
PLFTOPEN DS 0H
         CLI   PLFPOPEN,C'0'  PAN NOT OPEN
         BE    EOJ
*
         XC    ACTION,ACTION       CLEAR ACTION WORD
         L     R15,=V(PCLOSE)
         LA    R1,@PCLOSE
         BALR  R14,R15             GO CLOSE OUT PANVALET FILE
         MVI   PLFPOPEN,C'0'  PAN NOT OPEN
*
EOJ      DS    0H
         VDELETE PANCONTR
         VDELETE PANLIB
*
*
*  FREE THE FILES
*
         LA    R1,FREEIN     SYSIN
         DYNALLOC
*
         LA    R1,FREEPR     SYSPRINT
         DYNALLOC
*
         LA    R1,FREEDD2    PANDD2
         DYNALLOC
*
         LA    R1,FREEDATA   PANDATA
         DYNALLOC
*
         LA    R1,FREEDD1    PANDD1
         DYNALLOC
*
         LA    R1,DECONCAT   PANDATA AND SYSIN
         DYNALLOC
*
CLOSE1   DS    0H
*
         TM    PANLIST+48,X'10'   DCB OPEN?
         BZ    CLOSE2             NOPE
*
         CLOSE PANLIST
*
CLOSE2   DS    0H
*
         TM    SYSIN+48,X'10'     DCB OPEN?
         BZ    CLOSE3             NOPE
*
         CLOSE SYSIN
*
CLOSE3   DS    0H
*
         TM    PANDATA+48,X'10'   DCB OPEN?
         BZ    CLOSE4             NOPE
*
         CLOSE PANDATA
*
CLOSE4   DS    0H
*
         TM    SYSUT1+48,X'10'    DCB OPEN?
         BZ    CLOSE5             NOPE
*
         CLOSE SYSUT1
*
CLOSE5   DS    0H
*
         TM    SYSUT2+48,X'10'    DCB OPEN?
         BZ    CLOSED             NOPE
*
         CLOSE SYSUT2
*
CLOSED   DS    0H
*
         L     R7,DIRGMAX
         L     R2,TBLFIRST
      FREEMAIN R,LV=(7),A=(2)
*
DONEDONE DS    0H
         L     R7,DONE7
         BR    R7             RETURN
*
         TITLE 'PLF - INITIALIZATION'
INIT7    DC    F'0'
INITFLDS DS    0H
         ST    R7,INIT7
*
         VCOPY ZUSER,U8,USERID,MOVE
*
         LTR   R15,15          IF RETURN CODE IS ZERO,
         BZ    GOTZUSER        OK
*
         WTO   'PLF001 - UNABLE TO RETRIEVE ZUSER',ROUTCDE=11
ABEND1   ABEND 1,DUMP,STEP
GOTZUSER DS    0H
*
         MVC   PLFZUSER(8),USERID
*
* SET UP THE DSNAME FIELDS FOR DYNAMIC ALLOCATION.
*
         SR    R14,R14
         MVC   DSNDATA(8),USERID
         LA    R1,DSNDATA
         CLI   0(R1),C' '
         BE    *+16
         LA    R1,1(,R1)
         LA    R14,1(,R14)
         B     *-16
         MVC   0(14,R1),=C'.@TEMP.PANDATA'
         LA    R14,14(,R14)
         STH   R14,LENDATA
*
         SR    R14,R14
         MVC   DSNLIST(8),USERID
         LA    R1,DSNLIST
         CLI   0(R1),C' '
         BE    *+16
         LA    R1,1(,R1)
         LA    R14,1(,R14)
         B     *-16
         MVC   0(14,R1),=C'.@TEMP.PANLIST'
         LA    R14,14(,R14)
         STH   R14,LENLIST
*
         SR    R14,R14
         MVC   DSNCARD(8),USERID
         LA    R1,DSNCARD
         CLI   0(R1),C' '
         BE    *+16
         LA    R1,1(,R1)
         LA    R14,1(,R14)
         B     *-16
         MVC   0(14,R1),=C'.@TEMP.PANCARD'
         LA    R14,14(,R14)
         STH   R14,LENCARD
*
* THESE FIELDS DISPLAYED ON THE PLF MENU
*
         VDEFINE PANLIB,44
         VDEFINE XFERTO,44
         VDEFINE PANCONTR,4
         VDEFINE XFERCON,4
         VDEFINE PJC1,80
         VDEFINE PJC2,80
         VDEFINE XFERMOD,4
         VDEFINE XFERSTA,4
         VDEFINE AREATYP,8
         VDEFINE COLS,4,FMT=FIXED
         VDEFINE ROWS,4,FMT=FIXED
         VDEFINE TLROW,4,FMT=FIXED
         VDEFINE TLCOL,4,FMT=FIXED
         VDEFINE PCMD,40
         VDEFINE PLFDATA,3200
         VDEFINE ZVERB,8
         VDEFINE ZSCROLLA,4
         VDEFINE ZSCROLLN,4,FMT=FIXED
*
* THESE FIELDS ARE USED IN THE DIRECTORY MEMBER DISPLAY
*
         MVI   PANS,C' '          SET SELECTION CODE TO SPACES
*
*
*  FREE THE FILES
*
         LA    R1,FREEIN     SYSIN
         DYNALLOC
*
         LA    R1,FREEPR     SYSPRINT
         DYNALLOC
*
         LA    R1,FREEDD2    PANDD2
         DYNALLOC
*
         LA    R1,FREEDATA   PANDATA
         DYNALLOC
*
         LA    R1,DECONCAT   PANDATA AND SYSIN
         DYNALLOC
*
*    ALLOCATE PANDD2
*
*
DD2OLD   DS    0H
         LA    R1,ALLODD2    PANDD2--DUMMY
         DYNALLOC
*
         LTR   R15,R15
         BZ    DATAOLD
*
         WTO   'PLF  UNABLE TO ALLOCATE PANDD2 DUMMY   ',ROUTCDE=11
         DC    H'0'          ABEND S0C1
*
*    ALLOCATE PANDATA  @TEMP.PANDATA
*
*
DATAOLD  DS    0H
*
         LA    R1,OLDDATA    PANDATA--@TEMP.PANDATA
         DYNALLOC
         LTR   R15,R15
         BZ    SYSINOLD
*
         LA    R1,NEWDATA    PANDATA--@TEMP.PANDATA
         DYNALLOC
         LTR   R15,R15
         BZ    SYSINOLD
*
         WTO   'PLF  UNABLE TO ALLOCATE @TEMP.PAN.DATA',ROUTCDE=11
         DC    H'0'          ABEND S0C1
*
*
*    ALLOCATE SYSIN
*
*
*    ALLOCATE @TEMP.PANCARD
*
SYSINOLD DS    0H
         LA    R1,OLDIN      SYSIN
         DYNALLOC
*
         LTR   R15,R15
         BZ    PRINTOLD
*
         LA    R1,NEWIN      SYSIN
         DYNALLOC
*
         LTR   R15,R15
         BZ    PRINTOLD
*
    WTO   'PLF  UNABLE TO ALLOCATE SYSIN FILE @TEMP.PANCARD',ROUTCDE=11
         DC    H'0'          ABEND S0C1
*
*
*    ALLOCATE SYSPRINT
*
*
*    FIRST TRY TO ALLOCATE IT OLD
*
PRINTOLD DS    0H
         LA    R1,OLDPR      SYSPRINT
         DYNALLOC
*
         LTR   R15,R15
         BZ    DIRGMAIN
*
         LA    R1,NEWPR      SYSPRINT
         DYNALLOC
*
         LTR   R15,R15
         BZ    DIRGMAIN
*
    WTO   'PLF  UNABLE TO ALLOCATE SYSPRINT @TEMP.PAN.LIST',ROUTCDE=11
         DC    H'0'          ABEND S0C1
*
DIRGMAIN DS    0H
*
         L     R7,DIRGMAX
         LA    R7,4095(,R7)      ROUND UP TO 4K BOUNDARY
         SRL   R7,12
         LA    R7,1(,R7)         ADD A PAGE
         SLL   R7,12
         ST    R7,DIRGMAX
*
       GETMAIN R,LV=(7)
*
         ST    R1,TBLFIRST
         ST    R1,TBLNEXT
         A     R1,L4096
         ST    R1,DIRGADD
*
         MVI   0(R1),0         ZERO OUT THE AREA
         LR    R0,R1           SET ADDRESS
         LA    R14,1(,R1)
         LR    R15,R7
         BCTR  R15,0
         LR    R1,R15
         MVCL  R14,R0
*
INITDONE DS    0H
         L     R7,INIT7
         BR    R7
*
DIRGMAX  DC    A(16*DIRMAX)
DIRGADD  DS    F
DIRFIRST DS    F
DIRCURR  DS    F
DIRLAST  DS    F
*
TBLFIRST DS    F
TBLNEXT  DS    F
*
         TITLE 'PLF - DISPLAY THE MENU'
MENU7    DS    F
PLFPF3   DS    0H
         MVI   DONEFLAG,C'1'  I AM ALL DONE
MENUDONE DS    0H
         L     R7,MENU7
         BR    R7
SHOWMENU DS    0H
         ST    R7,MENU7
DISPLAY1 DS    0H
*
         VGET  PANLIB
*
*
         DISPLAY PLF3P1,ENDKEY=PLFPF3
*
*
         C     R15,FZEROS         IF ENTER KEY PRESSED
         BNE   PLFBADRC
*
         L     R1,DIRGADD
         MVI   0(R1),0         ZERO OUT THE AREA
         LR    R0,R1           SET ADDRESS
         LA    R14,1(,R1)
         LR    R15,R7
         BCTR  R15,0
         LR    R1,R15
         MVCL  R14,R0
         MVC   NAME1(22),TOPNAME         RESET DIR SEARCH
         MVC   DIRFIRST(4),DIRGADD
         L     R1,DIRFIRST
         MVC   0(10,R1),TOPNAME
         MVC   DIRCURR(4),DIRGADD
         MVC   DIRLAST(4),DIRGADD
*
         TR    PANLIB(44),CAPSONLY       UPPER-CASE IT
         MVC   XFERFROM(44),PANLIB       MAKE A COPY
         VPUT  PANLIB
*
         TITLE 'PLF - SET DSNAME FOR PAN LIBRARY'
PLFSETDS DS    0H
*
*  PICK UP DSNAME
*
         SR    R14,R14
         LA    R1,PANLIB
*
         CLI   0(R1),C' '
         BE    *+16
         LA    R1,1(,R1)
         LA    R14,1(,R14)
         B     *-16
*
         STH   R14,LENLIB
*
*  ALLOCATE THE PANLIB
*
*
         CLI   PLFPOPEN,C'1'  PAN OPEN
         BNE   OKFREE1        NO, JUST FREE
*
         XC    ACTION,ACTION       CLEAR ACTION WORD
         L     R15,=V(PCLOSE)
         LA    R1,@PCLOSE
         BALR  R14,R15             GO CLOSE OUT PANVALET FILE
         MVI   PLFPOPEN,C'0'  PAN CLOSED
*
OKFREE1  DS    0H
         LA    R1,FREEDD1              PANLIB
         DYNALLOC
*
         LA    R1,OLDDD1               PANLIB
         DYNALLOC
*
         LTR   R15,R15
         BZ    CHKPSPL
*
         WTO   'PLF  UNABLE TO ALLOCATE PANLIB',ROUTCDE=11
         DC    H'0'          ABEND S0C1
*
CHKPSPL  DS    0H
*
         XC    ACTION,ACTION
         L     R15,=V(POPEN)
         LA    R1,@POPEN
         BALR  R14,R15               INVOKE OPEN PANVALET FILE
         SPACE 1
         CLC   ACTION,=F'0'
         BNE   PLFRRPOP              NO GOOD
         MVI   PLFPOPEN,C'1'  PAN OPEN
*
         MVI   PSPOOL,C'0'                NOT PSPOOL
         CLC   PANLIB(7),=C'P.SPOOL'     IS IT P.SPOOL
         BNE   *+8                        NOPE
         MVI   PSPOOL,C'1'                SET TO PSPOOL
*
         B     MENUDONE                   GO TO WORK
*
         TITLE 'PLF - PAN LIBRARY PROCESSOR '
APAN7    DS    F
PLFEPAN  DS    0H
         L     R7,APAN7
         BR    R7
*
PLFAPAN  DS    0H
         ST    R7,APAN7
*
         MVI   POSITION,C' '
*
         MVI   DIRTOPN,C' '
         MVC   DIRTOPN+1(21),DIRTOPN
*
         TITLE 'PLF - PAN LIBRARY PROCESSOR - READ DIRECTORY LOOP'
LD#LOOP1 DS    0H
*
*
         L     R1,TBLFIRST
         ST    R1,TBLNEXT
         MVI   0(R1),X'FF'   END OF TABLE FLAG
         MVI   TOPSEL,C' '        FIRST MEMBER SELECTED ON SCREEN
         MVI   DIRTOPN,C' '
*
         SR    R15,15
         ST    R15,TABCOUNT   NO LINES ADDED
*
         PQUERY PLF3P2,PLFDATA
*
         L     R14,COLS               NUMBER OF COLUMNS
         MH    R14,ROWS+2             TIMES NUMBER OF ROWS
         ST    R14,ISPFSIZE           GIVES SCREEN AREA
         L     R15,ROWS               LOOP CONTROL
         L     R1,COLS                NUMBER OF COLUMNS
         BCTR  R1,0                   EXECUTED MOVE
*
         LA    R14,PLFDATA
         ST    R14,ISPFWORK
*
         EX    R1,MASKMOVE
         LA    R14,1(R1,R14)
         BCT   R15,*-8
*
         XC    ISPFNUMB(4),ISPFNUMB INITIALIZE COUNTER
*
LD#LOOP2 DS    0H
*
         BAL   R7,DIRLOOP1         RETRIEVE A LIBRARY DIRECTORY ENTRY
*
         CLC   DIRENTRY(2),=C'$*'  TEST FOR NO MORE DIR ENTRIES
         BE    LD#EOF              THATS ALL..EXIT
*
*
         L     R15,ISPFWORK
         MVC   0(80,R15),TAB1
         MVC   DIRBOTM(10),NAMEPAN FOR SPLIT-SCREEN, LAST MEM
         LA    R15,80(,R15)
         ST    R15,ISPFWORK
         L     R15,ISPFNUMB
         LA    R15,1(,R15)
         ST    R15,ISPFNUMB
         C     R15,ROWS
         BNL   LD#EOF
*
LD#COMT1 DS    0H
*
         CLI   COMMFLAG,C'0'       SHOW COMMENTS?
         BE    LD#LOOP2            NO, JUST GET THE NEXT MEMBER
*
*
         L     R15,ISPFWORK
         MVC   0(80,R15),COMMLINE
         LA    R15,80(,R15)
         ST    R15,ISPFWORK
         L     R15,ISPFNUMB
         LA    R15,1(,R15)
         ST    R15,ISPFNUMB
         C     R15,ROWS
         BL    LD#LOOP2
*
LD#EOF   DS    0H
*
         B     SHOWTBLS
*
SHOWTBDS DS    0H
*
         CLI   TOPSEL,C' '        FIRST MEMBER SELECTED ON SCREEN
         BE    SHOWTBD1           NONE SELECTED, SO RESHOW
*
         MVC   NAME1(10),TOPSEL
         B     SHOWTBD2           GO DO IT
*
SHOWTBD1 DS    0H
*
         MVC   NAME1(10),DIRTOPN
*
SHOWTBD2 DS    0H
*
         B     LD#LOOP1
*
         TITLE 'INVOKE ISPF DISPLAY MODULE'
SHOWTBLS DS    0H
*
         VREPLACE PLFDATA,ISPFSIZE,PLFDATA
*
SHOWTBL2 DS    0H
         XC    ZSCROLLN(4),ZSCROLLN
         MVC   ZSCROLLA(4),SPACES
         MVC   ZVERB(8),SPACES
*
         DISPLAY PLF3P2,ENDKEY=PLFEPAN
*
         TR    PCMD(40),CAPSONLY    UPPER-CASE ALL INPUT FIELDS
*
DISPLINE DS    0H
         LA    R1,PLFDATA          POINT TO FIRST LINE OF MEMBERS
         L     R14,TBLFIRST        POINT TO FIRST LINE ADDRESS
         MVI   0(R14),X'FF'
         LA    R15,40              LOOP CONTROL
*
MEMLOOP  DS    0H
         CLI   27(R1),X'20' COMMENT CHANGED?
         BE    COMMMOVE
         CLI   0(R1),X'20'  MEMBER SELECTED?
         BNE   MEMNSEL
MEMSELD  DS    0H
*
         MVC   0(80,R14),0(R1)
         LA    R14,80(,R14)
         MVI   0(R14),X'FF'
*
* IF I AM DOING A TRANSFER, MAKE SURE THE MEMBER HAS A COMMENT
*
         OI    1(R1),C' '         MAKE SEL CODE UPPER-CASE
         CLI   1(R1),C'X'         TRANSFER
         BNE   MEMNSEL
         CLI   COMMFLAG,C'0'       SHOW COMMENTS?
         BE    MESSQ               NO, DISPLAY THE MESSAGE
*
*        LA    R0,108(,R1)
         CLI   108(R1),C'*'        COMMENT LINE?
         BE    MEMCOMM             GO MOVE IT IN
MEMNCOMM DS    0H
*        DC    H'0'
         SETMSG PLF001P
         B     SHOWTBL2
*
MESSQ    DS    0H
         SETMSG PLF001Q
         B     SHOWTBL2
*
MEMCOMM  DS    0H
         LA    R1,80(,R1)
*        LA    R0,29(,R1)
         CLC   29(10,R1),SPACES    IF THE COMMENT IS NOT SPACES
         BNE   COMMMOVE            OK TO TRANSFER
         B     MEMNCOMM            SET MESSAGE AND FAIL
COMMMOVE DS    0H
         MVC   0(80,R14),0(R1)
         LA    R14,80(,R14)
         MVI   0(R14),X'FF'
MEMNSEL  DS    0H
         LA    R1,80(,R1)
         BCT   R15,MEMLOOP
*
         VGET  ZVERB,POOL=ASIS
         VGET  ZSCROLLA,POOL=ASIS
         VGET  ZSCROLLN,POOL=ASIS
*
         CLC   ZVERB(2),=C'UP'     PF7
         BE    DIRPF7              BRANCH IF YES
         CLC   ZVERB(4),=C'DOWN'   PF8
         BE    DIRPF8              BRANCH IF YES
         B     GOTSTUFF            ASSUME ENTER
*
ENDK     DS    0H
         B     PLFEPAN             ALL DONE
*
         TITLE 'SCROLL UP'
DIRPF7   DS    0H
*
         L     R1,DIRFIRST         POINT TO FIRST MEMBER
*
         CLI   PCMD,C'M'           GO TO TOP?
         BNE   DIRPF71             NO, JUST SCROLL
*
DIRTOP   DS    0H
*
         L     R1,DIRFIRST         POINT TO FIRST MEMBER
         MVC   NAME1(10),0(R1)     START DIRECTORY DISPLAY AT TOP
         B     SHOWTBD2
*
DIRPF71  DS    0H
*
         C     R1,DIRLAST          HAVE I GONE PAST END?
         BNL   DIRTOP              YES, JUST GO TO TOP
*
         CLC   DIRTOP2(10),0(R1)   TOP OF SCREEN?
         BE    DIRPF72             YES, I FOUND IT
*
         LA    R1,10(,R1)          BUMP TO NEXT
         B     DIRPF71             KEEP LOOPING
*
DIRPF72  DS    0H
*
         LA    R0,10               EACH ENTRY IS 10 BYTES
         MH    R0,ZSCROLLN+2       BACK UP SCREEN SIZE
         SR    R1,R0
*
         C     R1,DIRFIRST         HAVE I GONE PAST TOP?
         BL    DIRTOP              YES, JUST GO TO TOP
*
         MVC   NAME1(10),0(R1)     POINT TO ENTRY
         B     SHOWTBD2
*
         TITLE 'SCROLL DOWN'
DIRPF8   DS    0H
*
         CLI   PCMD,C'M'           GO TO BOTTOM?
         BE    DIRBOT              YES
*
         L     R3,DIRFIRST
*
DIRPF81  DS    0H
*
         LA    R3,10(,R3)
         CLC   DIRTOP2(10),0(R3)
         BNH   DIRPF82
*
         B     DIRPF81
*
DIRPF82  DS    0H
*
         LA    R0,10               EACH ENTRY IS 10 BYTES
         MH    R0,ZSCROLLN+2       BUMP DOWN SCREEN SIZE
         AR    R3,R0
*
         C     R3,DIRLAST          HAVE I GONE PAST BOTTOM?
         BH    DIRBUMP
         C     R3,DIRFIRST         HAVE I GONE PAST TOP?
         BL    DIRTOP              YES, JUST GO TO TOP
*
         LR    R1,R3
         B     DIRPF8E
*
DIRBUMP  DS    0H
*
         L     R2,ZSCROLLN
*
DIRBUMP1 DS    0H
*
         L     R1,DIRLAST          POINT TO BOTTOM
         MVC   NAME1(10),0(R1)     START DIRECTORY DISPLAY
*
DIRBUMP2 DS    0H
*
         BAL   R7,DIRLOOP1         RETRIEVE A LIBRARY DIRECTORY ENTRY
*
         CLC   DIRENTRY(2),=C'$*'  TEST FOR NO MORE DIR ENTRIES
         BE    DIRBUMP3            JUST BOTTOM
*
         BCT   R2,DIRBUMP2         KEEP LOOPING
*
DIRBUMP3 DS    0H
*
         L     R1,DIRLAST          POINT TO BOTTOM
         B     DIRPF8E             ALL DONE
*
DIRBOT   DS    0H
*
         LA    R2,4095             SET LOOP CONTROL
         B     DIRBUMP1            GO LOOP
*
DIRPF8E  DS    0H
*
         MVC   NAME1(10),0(R1)     START DIRECTORY DISPLAY
*
         B     SHOWTBD2
*
         TITLE 'PROCESS LINE COMMANDS'
*
***********************************************************************
*
*   DETERMINE WHETHER ANYTHING WAS ENTERED ON THE SCREEN
*
***********************************************************************
*
GOTSTUFF DS    0H
*
         L     R1,TBLFIRST
         ST    R1,LINEPTR
*
         CLI   PCMD,C' '           COMMAND ENTERED?
         BNE   GOTCMD              YES, IGNORE LINE COMMANDS
*
NEXTLINE DS    0H
*
         L     R3,LINEPTR         POINT TO SCREEN LINE
         CLI   0(R3),X'FF'        IF ALL DONE
         BE    SHOWTBDS             RESHOW THE SCREEN
         LA    R1,80(,R3)          POINT TO NEXT  MODIFIED LINE
         ST    R1,LINEPTR          STORE FOR NEXT TIME THROUGH
         CLI   0(R3),X'05'        IF COMMENT LINE
         BE    NEXTLINE            RESHOW THE SCREEN
*
         MVC   PANS(79),1(R3)   COPY STUFF
         TR    PANS(80),CAPSONLY    UPPER-CASE ALL INPUT FIELDS
         MVC   SELSAVE(3),PANS
*
         CLI   TOPSEL,C' '        FIRST MEMBER SELECTED ON SCREEN
         BNE   *+10               NO
         MVC   TOPSEL(10),NAMEPAN YES, SET IT
*
         CLI   SELSAVE,C' '       IF A SELECTION CODE WAS NOT ENTERED
         BE    NEXTLINE           CHECK THE NEXT LINE
*
         CLI   SELSAVE,C'B'   IS IT A BROWSE
         BE    PBROWSE        GO DO IT
*
         CLI   SELSAVE,C'S'   IS IT "SELECT"
         BNE   CMDCHKS        NOPE
         MVI   SELSAVE,C'B'   SET IT TO BROWSE
         CLI   PSPOOL,C'1'    IS IT PANSPOOL
         BE    PBROWSE        GO DO IT
         MVI   SELSAVE,C'E'   IT IS EDIT
         B     PEDIT          GO DO IT
*
CMDCHKS  DS    0H
         CLI   SELSAVE,C'E'   IS IT EDIT
         BE    PEDIT          GO DO IT
*
         CLI   SELSAVE,C'D'   IS IT DISABLE
         BE    PDELETE        GO DO IT
*
         CLI   SELSAVE,C'K'   IS IT DELETE
         BE    PDELETE        GO DO IT
*
         CLI   SELSAVE,C'N'   IS IT STATUS ENABLE
         BE    PENABLE        GO DO IT
*
         CLI   SELSAVE,C'P'   IS IT PRINT MEM
         BE    PPRINT         GO DO IT
*
         CLI   SELSAVE,C'R'   IS IT RENAME
         BE    PRENAME        GO DO IT
*
         CLC   SELSAVE(3),=C'COM'   COMMENT?
         BE    PCOMMNT        GO DO IT
*
         CLI   SELSAVE,C'C'   IS IT COPY
         BE    PCOPY          GO DO IT
*
         CLI   SELSAVE,C'X'   IS IT XFER
         BNE   CMDAC          NO, CHECK FOR ACCESS CODE COMMANDS
*
         BE    PXFER          GO DO IT
*
CMDAC    DS    0H
         CLI   SELSAVE,C'U'   IS IT CHANGE USER ID
         BE    PNEWUSER       GO DO IT
         CLI   SELSAVE,C'A'   IS IT ACCESS CODE MAINT
         BE    PACMAINT       GO DO IT
*
* ADD TESTS FOR OTHER LINE SELECTION CODES HERE
*
         B     NEXTLINE       GO REPEAT THE DISPLAY
         TITLE 'PLF - PAN LIBRARY FACILITY - ACCESS CODE MAINTENANCE'
PACMAINT DS    0H
*
         MVI   ACLINE,C' '
         MVC   ACLINE+1(79),ACLINE
*
         MVC   ACLINE(6),=C'++USER'
         MVC   ACLINE+7(10),NAMEPAN
         LA    R15,ACLINE+8
*
         CLI   0(R15),C' '  GOT A BLANK?
         BE    *+12         YUP
         LA    R15,1(,15)   NO, BUMP 1
         B     *-12
*
         MVI   0(R15),C','  SET THE COMMA
         LA    R15,1(,15)       BUMP 1
*
         CLI   SELSAVE+1,C'A'   IS IT ACCESS CODE ADD
         BE    PACMAINA
         CLI   SELSAVE+1,C'C'   IS IT ACCESS CODE CHANGE
         BE    PACMAINC
         CLI   SELSAVE+1,C'D'   IS IT ACCESS CODE DELETE
         BE    PACMAIND
*
         B     NEXTLINE       GO REPEAT THE DISPLAY
*
PACMAINA DS    0H             ADD ACCESS CODE
*
         CLI   PACCESS,C' '   IF NON-BLANK ACCESS CODE,
         BNE   NEXTLINE       GO REPEAT THE DISPLAY
*
         MVC   0(4,R15),PUSER EXISTING USER CODE
         MVI   4(R15),C','
         MVC   5(4,R15),NEWNAME  NEW ACCESS CODE
         MVC   9(2,R15),=C',1'
*
         B     PACLINK        GO CALL PAN#1
*
PACMAINC DS    0H             CHANGE ACCESS CODE
*
         CLI   PACCESS,C' '   IF BLANK ACCESS CODE
         BE    NEXTLINE       GO REPEAT THE DISPLAY
*
         MVC   ALINE+9(4),NEWNAME  OLD ACCESS CODE
         MVC   0(9,R15),NEWNAME  "OLD,NEW" ACCESS CODE
*
         B     PACLINK        GO CALL PAN#1
*
PACMAIND DS    0H             DELETE ACCESS CODE
*
         CLI   PACCESS,C' '   IF BLANK ACCESS CODE
         BE    NEXTLINE       GO REPEAT THE DISPLAY
*
         MVC   ALINE+9(4),NEWNAME  OLD ACCESS CODE
         MVC   0(4,R15),NEWNAME  EXISTING ACCESS CODE
         MVC   4(7,R15),=C',0000,0'  DELETE ACCESS CODE
*
         B     PACLINK        GO CALL PAN#1
*
*
PACLINK  DS    0H
*
         OPEN  (SYSIN,(OUTPUT))
*
         CLI   PANCONTR,C' '      CONTROL WORD ENTERED
         BE    PUTACLIN
         PUT   SYSIN,CLINE
PUTACLIN DS    0H
*
         CLI   SELSAVE+1,C'C'   IS IT ACCESS CODE CHANGE
         BE    PUTALIN2
         CLI   SELSAVE+1,C'D'   IS IT ACCESS CODE DELETE
         BE    PUTALIN2
*
         B     PUTACLN2
*
PUTALIN2 DS    0H
         PUT   SYSIN,ALINE
*
PUTACLN2 DS    0H
         PUT   SYSIN,ACLINE
         CLOSE SYSIN
*
         LA    R1,PANPARMS
         LINK  EP=PAN#1
         LTR   R15,R15
         BZ    NEXTLINE
*
         MVC   DSNOTLST(44),LISTISPF   SET DSN FOR BROWSE
      PDFBROWS DSNOTLST
*
         B     NEXTLINE
*
         TITLE 'PLF - PAN LIBRARY PROCESSOR - CHANGE USER ID'
PNEWUSER DS    0H
*
         MVC   ULINE+7(10),NAMEPAN
         LA    R15,ULINE+8
*
         CLI   0(R15),C' '  GOT A BLANK?
         BE    *+12         YUP
         LA    R15,1(,15)   NO, BUMP 1
         B     *-12
*
         MVI   0(R15),C','  SET THE COMMA
*
         OC    PUSER(4),=C'0000'  SET BLANKS TO ZEROS
         MVC   1(4,R15),PUSER     MOVE IT IN
         MVI   5(R15),C','  SET THE COMMA
*
         OC    NEWNAME(4),=C'0000'  SET BLANKS TO ZEROS
         MVC   6(4,R15),NEWNAME   MOVE IT IN
*
         OPEN  (SYSIN,(OUTPUT))
*
         CLI   PANCONTR,C' '      CONTROL WORD ENTERED
         BE    PUTULINE
         PUT   SYSIN,CLINE
PUTULINE DS    0H
         PUT   SYSIN,ULINE
         CLOSE SYSIN
*
         LA    R1,PANPARMS
         LINK  EP=PAN#1
         LTR   R15,R15
         BZ    NEXTLINE
*
         MVC   DSNOTLST(44),LISTISPF   SET DSN FOR BROWSE
      PDFBROWS DSNOTLST
*
         B     NEXTLINE
*
         TITLE 'PLF - PAN LIBRARY PROCESSOR - ADD/CHANGE COMMENT'
PCOMMNT  DS    0H
*
         MVI   OLINE,C' '
         MVC   OLINE+1(79),OLINE
         MVC   OLINE(10),=C'++COMMENT '
         MVC   OLINE+11(10),NAMEPAN
         LA    R15,OLINE+12
*
         CLI   0(R15),C' '  GOT A BLANK?
         BE    *+12         YUP
         LA    R15,1(,15)   NO, BUMP 1
         B     *-12
*
         MVI   0(R15),C','  SET THE COMMA
*
         L     R1,LINEPTR
         MVC   1(51,R15),28(R1)   COPY COMMENT
*
         OPEN  (SYSIN,(OUTPUT))
*
         CLI   PANCONTR,C' '      CONTROL WORD ENTERED
         BE    CHKCOMAC
         PUT   SYSIN,CLINE
CHKCOMAC DS    0H
*
         CLI   PACCESS,C' '   IF BLANK ACCESS CODE
         BE    PUTOLINE       JUST CHANGE THE COMMENT
*
         MVC   ALINE+9(4),NEWNAME  OLD ACCESS CODE
         PUT   SYSIN,ALINE
*
PUTOLINE DS    0H
*
         PUT   SYSIN,OLINE
         CLOSE SYSIN
*
         LA    R1,PANPARMS
         LINK  EP=PAN#1
         LTR   R15,R15
         BZ    NEXTLINE
*
         MVC   DSNOTLST(44),LISTISPF   SET DSN FOR BROWSE
      PDFBROWS DSNOTLST
*
         B     NEXTLINE
*
LISTISPF DC    CL44'@TEMP.PANLIST'
DATAISPF DC    CL44'@TEMP.PANDATA'
         TITLE 'PLF - PAN LIBRARY PROCESSOR - EDIT A PAN MEMBER'
PEDIT    DS    0H
*
*        DC    H'0'
*
         BAL   R7,READPAN
*
         MVC   DSNOTLST(44),DATAISPF   SET DSN FOR EDIT
       PDFEDIT DSNOTLST
*
         LTR   R15,R15
         BNZ   NEXTLINE
*
         OPEN  (SYSIN,(OUTPUT))
*
TSTACC   DS    0H
*
         CLI   PACCESS,C'A'         ACCESS CONTROL FOR THIS MEMBER?
         BNE   TSTCCOD
         MVC   ALINE+10(4),NEWNAME  SET ACCESS CODE
         PUT   SYSIN,ALINE
*
TSTCCOD  DS    0H
*
         CLI   PANCONTR,C' '      CONTROL WORD ENTERED
         BE    SETELINE
         PUT   SYSIN,CLINE
*
SETELINE DS    0H
*
         PUT   SYSIN,ELINE
         CLOSE SYSIN
*
*
         LA    R1,CONCATIN
         DYNALLOC
         LTR   R15,R15
         BZ    *+6
         DC    H'0'
*
*
         LA    R1,PANPARMS
         LINK  EP=PAN#1
         LTR   R15,R15
         BZ    EDECONCT
*
         MVC   DSNOTLST(44),LISTISPF
      PDFBROWS DSNOTLST
*
EDECONCT DS    0H
*
         LA    R1,DECONCAT
         DYNALLOC
*
*
         B     NEXTLINE
*
         TITLE 'PLF - PAN LIBRARY PROCESSOR - RENAME A PAN MEMBER'
PRENAME  DS    0H
*
         MVC   RLINE(9),=CL9'++RENAME'
PRBLANKS DS    0H
         MVC   POSITION(10),NAMEPAN
         MVC   RLINE+9(10),NAMEPAN
         LA    R15,RLINE+10
*
         CLI   0(R15),C' '  GOT A BLANK?
         BE    *+12
         LA    R15,1(,15)   NO, BUMP 1
         B     *-12
*
         MVI   0(R15),C','  SET THE COMMA
         MVC   1(10,R15),NEWNAME  MOVE IT IN
*
         OPEN  (SYSIN,(OUTPUT))
*
         CLI   PANCONTR,C' '   CONTROL WORD ENTERED
         BE    PUTRLINE
         PUT   SYSIN,CLINE
*
PUTRLINE DS    0H
*
         PUT   SYSIN,RLINE
         CLOSE SYSIN
*
*
         LA    R1,PANPARMS
         LINK  EP=PAN#1
         LTR   R15,R15
         BZ    NEXTLINE
*
         MVC   DSNOTLST(44),LISTISPF
      PDFBROWS DSNOTLST
*
         B     NEXTLINE
*
GOODRNAM DS    0H
         MVC   POSITION(10),NEWNAME    SET POSITION
         B     NEXTLINE
         TITLE 'PLF - PAN LIBRARY PROCESSOR - COPY A PAN MEMBER'
PCOPY    DS    0H
*
         MVC   RLINE(9),=CL9'++COPY  '
*
         B     PRBLANKS       GO REPEAT THE DISPLAY
         TITLE 'PLF - PAN LIBRARY PROCESSOR - TRANSFER A PAN MEMBER'
PXFER    DS    0H
*
         CLI   XFERFLAG,C'0'         ALREADY INITIALIZED?
         BNE   PXFER2                YES
         MVI   XFERFLAG,C'1'         ALREADY INITIALIZED
         XC    XFERNUMB(4),XFERNUMB  ALREADY INITIALIZED
         MVC   XFERLAST(4),XFERST    ALREADY INITIALIZED
*
PXFER2   DS    0H
*
         L     R1,XFERLAST
         CLI   0(R1),X'EE'    END OF LIST
         BE    NEXTLINE       GO REPEAT THE DISPLAY
*
         MVC   0(10,R1),NAMEPAN SAVE THIS NAME
         LA    R1,12(,R1)
         ST    R1,XFERLAST
         MVI   0(R1),X'FF'
         MVC   1(11,R1),0(R1)
         L     R1,XFERNUMB
         LA    R1,1(,R1)
         ST    R1,XFERNUMB
*
         B     NEXTLINE       GO REPEAT THE DISPLAY
*
*
         TITLE 'PLF - PAN LIBRARY PROCESSOR - DELETE A PAN MEMBER'
MINUS1   DC    F'-1'
PDELETE  DS    0H
*
         OPEN  (SYSIN,(OUTPUT))
*
         MVC   KLINE+14(10),NAMEPAN SAVE THIS NAME
*
         MVC   DLINE+9(70),DLINE+8
         MVC   DLINE+9(10),NAMEPAN  SAVE THIS NAME
         LA    R15,DLINE+10
         CLI   0(R15),C' '
         BE    *+12
         LA    R15,1(,15)
         B     *-12
         MVC   0(8,R15),=C',DISABLE'
*
         CLI   PANCONTR,C' '      CONTROL WORD ENTERED
         BE    PUTDLINE
         PUT   SYSIN,CLINE
*
PUTDLINE DS    0H
*
         CLI   SELSAVE,C'K'   IS IT DELETE
         BE    PUTKLINE       YES
*
         CLI   PACCESS,C'A'         ACCESS CONTROL FOR THIS MEMBER?
         BNE   TSTDOPT
         MVC   ALINE+10(4),NEWNAME  SET ACCESS CODE
         PUT   SYSIN,ALINE
*
TSTDOPT  DS    0H
*
         PUT   SYSIN,DLINE
*
         CLOSE SYSIN
*
         LA    R1,PANPARMS
         LINK  EP=PAN#1
*
         LTR   R15,15         IF RC=0
         BZ    GOODNAM        GO RESET THE DIR LIST
         B     NOTDLTED       GO DISPLAY THE SYSPRINT FILE MSGS
*
PUTKLINE DS    0H
*
         PUT   SYSIN,KLINE
*
         CLOSE SYSIN
*
         LA    R1,PANPARMS
         LINK  EP=PAN#2
         LTR   R15,R15
         BZ    NEXTLINE
*
NOTDLTED DS    0H
         MVC   DSNOTLST(44),LISTISPF
      PDFBROWS DSNOTLST
*
         B     NEXTLINE
*
GOODNAM  DS    0H
         B     NEXTLINE
*
         TITLE 'PLF - PAN LIBRARY PROCESSOR - ENABLE A PAN MEMBER'
PENABLE  DS    0H
*
         OPEN  (SYSIN,(OUTPUT))
*
         MVC   DLINE+9(70),DLINE+8
         MVC   DLINE+9(10),NAMEPAN  SAVE THIS NAME
         LA    R15,DLINE+10
         CLI   0(R15),C' '
         BE    *+12
         LA    R15,1(,15)
         B     *-12
         MVC   0(8,R15),=C',ENABLE '
*
         CLI   PANCONTR,C' '      CONTROL WORD ENTERED
         BE    PUTNLINE
         PUT   SYSIN,CLINE
*
PUTNLINE DS    0H
*
         PUT   SYSIN,DLINE
*
         CLOSE SYSIN
*
         LA    R1,PANPARMS
         LINK  EP=PAN#1
         LTR   R15,R15
         BZ    NEXTLINE
*
         MVC   DSNOTLST(44),LISTISPF
      PDFBROWS DSNOTLST
*
         B     NEXTLINE
*
         TITLE 'PLF - PAN LIBRARY PROCESSOR - BROWSE A PAN MEMBER'
PBROWSE  DS    0H
*
*        FIRST CHECK FOR ACCESS CODE REQUIRED
*
         CLI   PACCESS,C'A'
         BNE   BROWSEOK
*
         CLC   NEWNAME(4),PUSER             MATCH
         BE    BROWSEOK                     YES, ALL DONE.
*
         B     NEXTLINE
*
BROWSEOK DS    0H
*
*
         BAL   R7,READPAN
*
         MVC   DSNOTLST(44),DATAISPF   SET DSN FOR PAN MEMB BROWSE
         CLI   PSPOOL,C'0'         NOT PANSPOOL FILE
         BE    *+10                JUST DO THE BROWSE
         MVC   DSNOTLST(44),LISTISPF   SET DSN FOR PANSPOOL BROWSE
      PDFBROWS DSNOTLST
*
         B     NEXTLINE       GO REPEAT THE DISPLAY
         TITLE 'PLF - PAN LIBRARY PROCESSOR - PRINT  A PAN MEMBER'
PPRINT   DS    0H
*
         BAL   R7,READPAN
*
         MVC   PRTLRECL(2),=H'80'   IF NOT PANSPOOL
         MVC   PRTBLOCK(2),=H'80'   IF NOT PANSPOOL
         MVI   SOUTFBA,X'90'           SET RECFM=FB
         LA    R1,NAMDATA              SET DSN FOR PAN MEMB PRINT
         CLI   PSPOOL,C'0'         NOT PANSPOOL FILE
         BE    PPRINTDO            JUST DO THE PRINT
PRNTLIST DS    0H
         MVC   PRTLRECL(2),=H'133'  IF
         MVC   PRTBLOCK(2),=H'133'         PANSPOOL
         MVI   SOUTFBA,X'94'           SET RECFM=FBA
         LA    R1,NAMLIST              SET DSN FOR PAN MEMB PRINT
*
PPRINTDO DS    0H
         ST    R1,UT1DSN               SET DSN FOR PAN MEMB PRINT
         LA    R1,FREE1                FREE SYSUT1
         DYNALLOC
         LA    R1,FREE2                FREE SYSUT2
         DYNALLOC
*
*
         LA    R1,SOUTPARM
         DYNALLOC
         LTR   R15,15
         BNZ   BADUT2
*
         LA    R1,ALLO1
         DYNALLOC
         LTR   R15,15
         BZ    *+6
         DC    H'0'
*
         OPEN  (SYSUT1,(INPUT),SYSUT2,(OUTPUT))
*
PPRTLOOP DS    0H
*
         GET   SYSUT1,PRTLINE
         LA    R2,PRTLINE
*
*  IF RECFM=FBA, MAKE SURE COL 1 HAS VALID CARRIAGE CONTROL
*
         CLI   SOUTFBA,X'94'            IS RECFM=FBA
         BNE   PPRTPUT                  NO, JUST PRINT IT
*
         CLI   0(R2),C'1'               SKIP TO CHAN 1
         BE    PPRTPUT                  OK
*
         CLI   0(R2),C'0'               DOUBLE SPACE
         BE    PPRTPUT                  OK
*
         CLI   0(R2),C'-'               TRIPLE SPACE
         BE    PPRTPUT                  OK
*
         LA    R2,PRTBUFF               POINT TO A BLANK BEFORE LINE
*
PPRTPUT  DS    0H
         PUT   SYSUT2,(2)
*
         B     PPRTLOOP
*
PPRTEND  DS    0H
*
         CLOSE (SYSUT1,,SYSUT2)
        SETMSG PLF001O
*
         B     NEXTLINE       GO REPEAT THE DISPLAY
*
BADUT1   DS    0H
*
         WTO   'PLF DYNALLOC OF SYSUT1 FILE FAILED',ROUTCDE=11
*
         B     NEXTLINE       GO REPEAT THE DISPLAY
*
BADUT2   DS    0H
*
         WTO   'PLF DYNALLOC OF SYSUT2 FILE FAILED',ROUTCDE=11
*
         B     NEXTLINE       GO REPEAT THE DISPLAY
*
PRTBUFF  DC    C' '
PRTLINE  DC    CL133' '
*
         TITLE 'PLF - PAN LIBRARY PROCESSOR - PROCESS A COMMAND'
*
*    THE COMMAND ENTERED IS IN "PCMD"
*
GOTCMD   DS    0H
*
         CLI   PCMD,C' ' ANY DATA IN COMMAND LINE
         BE    SHOWTBDS     NOPE
*
         CLC   PCMD(6),=C'PSPOOL'     IS THIS A PANSPOOL LIBRARY
         BE    ITISPSPL     YUP
         CLC   PCMD(4),=C'LIST'       LIST @TEMP.PANLIST?
         BE    LISTLIST               YUP
         CLC   PCMD(4),=C'XFER'       TRANSFER JCL?
         BE    XFEREDIT               YUP
         CLC   PCMD(4),=C'SHOW'       SHOW ACCESS CODES?
         BE    SHOWACCC               YUP
         CLC   PCMD(6),=C'NOSHOW'     DON'T SHOW ACCESS CODES?
         BE    NOSHOWAC               YUP
         CLC   PCMD(3),=C'END'        END
         BE    PLFEPAN      YUP
         CLC   PCMD(5),=C'ABEND'      ABEND
         BNE   *+6
         DC    H'0'                   S0C1
         CLI   PCMD,C'L' LOCATE
         BE    PLOCATE      YUP
         CLI   PCMD,C'F' LOCATE
         BE    PLOCATE      YUP
         CLI   PCMD,C'S' LOCATE
         BE    PLOCATE      YUP
         CLC   PCMD(4),=C'COMM'    SET COMMENTS ON?
         BE    PCOMMON             YUP
         CLC   PCMD(6),=C'NOCOMM'  SET COMMENTS OFF?
         BE    PCOMMOFF            YUP
         CLI   PCMD,C'C' CONTROL
         BE    PCONTROL     YUP
*
*     PUT ADDITIONAL COMMAND TESTS HERE
*
         B     PLFBADCM
         TITLE 'PLF - PAN LIBRARY PROCESSOR - SET PANSPOOL FORMAT'
ITISPSPL DS    0H
*
         MVI   PSPOOL,C'1'         INDICATE PANSPOOL FORMAT
         B     SHOWTBD1       GO REPEAT THE DISPLAY
*
         TITLE 'PLF - PAN LIBRARY PROCESSOR - LIST @TEMP.PANLIST'
LISTLIST DS    0H
*
         MVI   PCMD,C' '             COMMAND LINE
         B     PRNTLIST
*
         TITLE 'PLF - PAN LIBRARY PROCESSOR - EDIT @TEMP.PANDATA'
XFEREDIT DS    0H
*
* THE TRANSFER COMMAND CREATES A JOB-STREAM THAT WILL:
*
*     1. ARCHIVE TO TAPE THE MEMBERS IN THE "TO" LIBRARY
*     2. BACKUP TO TAPE THE MEMBERS IN THE "FROM" LIBRARY
*     3. RESTORE THE TAPE TO THE "TO" LIBRARY
*     4. DELETE THE MEMBERS ON THE "FROM" LIBRARY
*
*
* THE TERMINAL OPERATOR IS PLACED IN "EDIT" ON THE JOB-STREAM, AND
* CAN MODIFY, SUBMIT, OR CANCEL THE XFER.
*
*
         VGET  XFERTO
         VGET  PJC1
         VGET  PJC2
*
*
         DISPLAY PLF3P3,ENDKEY=NEXTLINE
*
*
*
         TR    XFERTO(44),CAPSONLY       UPPER-CASE IT
         MVC   ARCHFROM(44),XFERTO       MAKE A COPY
         VPUT  XFERTO
         TR    PJC1(80),CAPSONLY         UPPER-CASE IT
         VPUT  PJC1
         TR    PJC2(80),CAPSONLY         UPPER-CASE IT
         VPUT  PJC2
         TR    XFERMOD(4),CAPSONLY       UPPER-CASE IT
         TR    XFERSTA(4),CAPSONLY       UPPER-CASE IT
*
*
         MVI   XFERFLAG,C'0'         RESET
*
         L     R5,XFERNUMB               NUMBER OF MEMBERS SELECTED
         LTR   R5,R5
         BZ    SHOWTBD1       GO REPEAT THE DISPLAY
*
* SORT PANVALET MEMBERS FOR TRANSFER
*
         LA    R1,SORTPARM
         CALL  CORESORT
*
* SET UP THE TAPE GDG NAME FOR THE ARCHIVE STEP
*
         OPEN  (PANDATA,(OUTPUT))
*
         MVC   ARCHTAP1(44),XFERTO       MAKE A COPY
         MVC   ARCHTAP2(44),XFERTO       MAKE A COPY
*
         LA    R1,ARCHTAP1
*
         CLI   0(R1),C' '
         BE    *+12
         LA    R1,1(,R1)
         B     *-12
*
         MVC   0(5,R1),=C'(+1),'
*
         LA    R1,ARCHTAP2
*
         CLI   0(R1),C' '
         BE    *+12
         LA    R1,1(,R1)
         B     *-12
*
         MVC   0(3,R1),=C'(0)'
*
         PUT   PANDATA,PJC1
         PUT   PANDATA,PJC2
*
         MVC   WORK80(80),SPACES
         MVC   WORK80(L'ARCHJCL1),ARCHJCL1
         PUT   PANDATA,WORK80
*
         PUT   PANDATA,ARCHJCL3
         PUT   PANDATA,ARCHJCL4
*
         MVC   WORK80(80),SPACES
         MVC   WORK80(L'ARCHJCL5),ARCHJCL5
         PUT   PANDATA,WORK80
*
         PUT   PANDATA,ARCHJCL6
*
         MVC   WORK80(80),SPACES
         MVC   WORK80(L'SYSZCARD),SYSZCARD
         PUT   PANDATA,WORK80
*
         MVC   WORK80(80),SPACES
         MVC   WORK80(L'SYSNCARD),SYSNCARD
         PUT   PANDATA,WORK80
*
         CLI   XFERCON,C' '      CONTROL WORD ENTERED
         BE    XFELOOP1
         MVC   WORK80(80),SPACES
         MVC   WORK80(70),XLINE
         PUT   PANDATA,WORK80
*
XFELOOP1 DS    0H
         MVI   WORK80,C' '
         MVC   WORK80+1(79),WORK80
         MVC   WORK80(L'ARCHJCL7),ARCHJCL7
         PUT   PANDATA,WORK80
*
         L     R6,XFERST
*
XFELOOP2 DS    0H
*
         MVC   KLINE+14(10),0(R6)
         LA    R6,12(,R6)
*
         PUT   PANDATA,KLINE
*
         BCT   R5,XFELOOP2
*
XFECLOS1 DS    0H
*
         MVC   WORK80(80),SPACES
         MVC   WORK80(L'XFEREXEC),XFEREXEC
         PUT   PANDATA,WORK80
*
         MVC   WORK80(80),SPACES
         MVC   WORK80(L'SYSPCARD),SYSPCARD
         PUT   PANDATA,WORK80
*
         PUT   PANDATA,XFERDD1
         PUT   PANDATA,XFERDD2
*
         MVC   WORK80(80),SPACES
         MVC   WORK80(L'DD3DUMM),DD3DUMM
         PUT   PANDATA,WORK80
*
         MVC   WORK80(80),SPACES
         MVC   WORK80(L'SYSNCARD),SYSNCARD
         PUT   PANDATA,WORK80
*
         CLI   PANCONTR,C' '     CONTROL WORD ENTERED
         BE    XFELOOP4
         PUT   PANDATA,CLINE
*
XFELOOP4 DS    0H
*
*
         L     R5,XFERNUMB               NUMBER OF MEMBERS SELECTED
         L     R6,XFERST
*
XFELOOP5 DS    0H
*
         MVC   XFERNAME(10),0(R6)
         LA    R6,12(,R6)
*
         PUT   PANDATA,XFERXFER
*
         BCT   R5,XFELOOP5
*
         MVC   WORK80(80),SPACES
         MVC   WORK80(L'RSTREXEC),RSTREXEC
         PUT   PANDATA,WORK80
*
         MVC   WORK80(80),SPACES
         MVC   WORK80(L'SYSZCARD),SYSZCARD
         PUT   PANDATA,WORK80
*
         PUT   PANDATA,RSTRDD1
*
         PUT   PANDATA,RSTRDD3
*
         MVC   WORK80(80),SPACES
         MVC   WORK80(L'DD2DUMM),DD2DUMM
         PUT   PANDATA,WORK80
*
         MVC   WORK80(80),SPACES
         MVC   WORK80(L'SYSNCARD),SYSNCARD
         PUT   PANDATA,WORK80
*
XFELOOP3 DS    0H
*
         CLI   XFERCON,C' '      CONTROL WORD ENTERED
         BE    XFECLOS2
         PUT   PANDATA,XLINE
*
XFECLOS2 DS    0H
*
         MVC   WORK80(80),SPACES
         MVC   WORK80(L'RSTRRSTR),RSTRRSTR
         PUT   PANDATA,WORK80
*
         CLC   XFERMOD(4),=C'MOVE'
         BNE   XFELOOP8                  NO, LEAVE IT COPY MODE
*
         MVC   WORK80(80),SPACES
         MVC   WORK80(L'DELTJCL1),DELTJCL1
         PUT   PANDATA,WORK80
*
         MVI   WORK80,C' '
         MVC   WORK80+1(79),WORK80
         MVC   WORK80(L'SYSZCARD),SYSZCARD
         PUT   PANDATA,WORK80
*
         PUT   PANDATA,XFERDD1
*
         MVC   WORK80(80),SPACES
         MVC   WORK80(L'DD2DUMM),DD2DUMM
         PUT   PANDATA,WORK80
*
         MVC   WORK80(80),SPACES
         MVC   WORK80(L'DD3DUMM),DD3DUMM
         PUT   PANDATA,WORK80
*
         MVC   WORK80(80),SPACES
         MVC   WORK80(L'SYSNCARD),SYSNCARD
         PUT   PANDATA,WORK80
*
         CLI   PANCONTR,C' '     CONTROL WORD ENTERED
         BE    XFELOOP6
         PUT   PANDATA,CLINE
*
XFELOOP6 DS    0H
*
*
         L     R5,XFERNUMB               NUMBER OF MEMBERS SELECTED
         L     R6,XFERST
*
XFELOOP7 DS    0H
*
         MVC   KLINE+14(10),0(R6)
*
         PUT   PANDATA,KLINE
         LA    R6,12(,R6)
*
         BCT   R5,XFELOOP7
*
XFELOOP8 DS    0H
*
         CLC   XFERSTA(4),=C'PROD'
         BNE   XFELOOPB                  NO, LEAVE IT
*
         MVC   WORK80(80),SPACES
         MVC   WORK80(L'STATJCL1),STATJCL1
         PUT   PANDATA,WORK80
*
         MVC   WORK80(80),SPACES
         MVC   WORK80(L'SYSPCARD),SYSPCARD
         PUT   PANDATA,WORK80
*
         PUT   PANDATA,RSTRDD1
*
         MVC   WORK80(80),SPACES
         MVC   WORK80(L'DD2DUMM),DD2DUMM
         PUT   PANDATA,WORK80
*
         MVC   WORK80(80),SPACES
         MVC   WORK80(L'SYSNCARD),SYSNCARD
         PUT   PANDATA,WORK80
*
         MVC   WORK80(80),SPACES
         MVC   WORK80(9),=CL9'++STATUS '
*
         CLI   XFERCON,C' '      CONTROL WORD ENTERED
         BE    XFELOOP9
         PUT   PANDATA,XLINE
*
XFELOOP9 DS    0H
*
         L     R5,XFERNUMB               NUMBER OF MEMBERS SELECTED
         L     R6,XFERST
*
XFELOOPA DS    0H
*
         MVC   WORK80+9(10),0(R6)
         MVC   WORK80+19(61),SPACES
*
         LA    R15,11
         LA    R14,WORK80+9
*
         CLI   0(R14),C' '
         BE    *+12
         LA    R14,1(,R14)
         BCT   R15,*-12
*
         MVC   0(5,R14),=C',PROD'
*
         PUT   PANDATA,WORK80
         LA    R6,12(,R6)
*
         BCT   R5,XFELOOPA
*
XFELOOPB DS    0H
*
         CLOSE (PANDATA)
*
XFEDIT   DS    0H
*
         MVC   DSNOTLST(44),DATAISPF
       PDFEDIT DSNOTLST
         B     NEXTLINE       GO REPEAT THE DISPLAY
*
         TITLE 'PLF - PAN LIBRARY PROCESSOR - SHOW ACCESS CODES'
SHOWACCC DS    0H
*
         CLC   USERID(2),=C'AS'    AUTHORIZED USER?
         BNE   SHOWTBD1       GO REPEAT THE DISPLAY
*
         MVI   SHOWFLAG,C'1'  SHOW ACCESS CODES
         B     SHOWTBD1       GO REPEAT THE DISPLAY
*
NOSHOWAC DS    0H
*
         MVI   SHOWFLAG,C'0'  DON'T SHOW ACCESS CODES
         B     SHOWTBD1       GO REPEAT THE DISPLAY
*
         TITLE 'PLF - PAN LIBRARY PROCESSOR - SET COMMENT FLAG'
PCOMMON  DS    0H
*
         MVI   COMMFLAG,C'1'  SET COMMENT ON
         XC    ACTION,ACTION       CLEAR ACTION WORD
         L     R15,=V(PCLOSE)
         LA    R1,@PCLOSE
         BALR  R14,R15             GO CLOSE OUT PANVALET FILE
         XC    ACTION,ACTION
         L     R15,=V(POPEN)
         LA    R1,@POPEN
         BALR  R14,R15               INVOKE OPEN PANVALET FILE
         B     SHOWTBD1       GO REPEAT THE DISPLAY
*
PCOMMOFF DS    0H
*
         MVI   COMMFLAG,C'0'  SET COMMENT ON
         XC    ACTION,ACTION       CLEAR ACTION WORD
         L     R15,=V(PCLOSE)
         LA    R1,@PCLOSE
         BALR  R14,R15             GO CLOSE OUT PANVALET FILE
         XC    ACTION,ACTION
         L     R15,=V(POPEN)
         LA    R1,@POPEN
         BALR  R14,R15               INVOKE OPEN PANVALET FILE
         B     SHOWTBD1       GO REPEAT THE DISPLAY
*
         TITLE 'PLF - PAN LIBRARY PROCESSOR - LOCATE A PAN MEMBER'
PLOCATE  DS    0H
*
         MVC   POSITION(10),PCMD+2
*
PLOCATE1 DS    0H
*
         LA    R6,POSITION
         LA    R14,9
*
         CLI   0(R6),C' '     SEARCH FOR FIRST BLANK
         BE    *+12           SET AN ASTERISK
         LA    R6,1(,R6)      BUMP POINTER
         BCT   R14,*-12       LOOP
*
         LA    R14,POSITION
         SR    R6,R14         SET R6=LENGTH
*
         L     R1,DIRLAST     POINT TO HIGHEST MEMBER READ SO FAR
         EX    R6,LOCNAMCL    COMPARE LASTMEM VS SEARCH MEM
*
         BH    PLOCATE3       LOCATE A MEMBER WE HAVE ALREADY RETRIEVED
*
         MVC   NAME1(10),0(R1)     START OF SEARCH
*
PLOCATE2 DS    0H
*
*        MVI   DIRL0C1,0      MAKE   NO-OP AN 0C1
         BAL   R7,DIRLOOP1    READ A DIRECTORY ENTRY
         L     R1,DIRLAST     POINT TO LAST MEMBER READ
         CLC   DIRENTRY(2),=C'$*'  TEST FOR NO MORE DIR ENTRIES
         BE    PLOCATE9
*
         LA    R14,POSITION
         EX    R6,LOCNAMCL    COMPARE LISTMEM VS SEARCH MEM
         BE    PLOCATED
         BH    PLOCATED
*
         B     PLOCATE2
*
PLOCATE3 DS    0H
*
         L     R1,DIRFIRST    SEARCH
*
PLOCATE4 DS    0H
*
         C     R1,DIRLAST     LAST OF DIRECTORY LIST
         BNL   PLOCATED       I GIVE UP
*
         LA    R14,POSITION
         EX    R6,LOCNAMCL    COMPARE LISTMEM VS SEARCH MEM
         BE    PLOCATED
         BH    PLOCATED
*
         LA    R1,10(,R1)
         B     PLOCATE4
*
PLOCATED DS    0H
*
         MVC   NAME1(10),0(R1)
         B     SHOWTBD2
*
PLOCATE9 DS    0H
*
         MVC   NAME1(10),0(R1)
         B     SHOWTBD2
*
LOCNAMCL CLC   0(1,R1),0(R14)
         TITLE 'PLF - PAN LIBRARY PROCESSOR - PROCESS A CONTROL WORD'
PCONTROL DS    0H
         MVC   PANCONTR(4),PCMD+1
*
         VPUT  PANCONTR
*
         B     SHOWTBD1       GO REPEAT THE DISPLAY
         TITLE 'PLF - PAN LIBRARY PROCESSOR - READ A PAN MEMBER'
*
*  READ THE PAN MEMBER
*
         SPACE
READPAN  DS    0H
*
         ST    R7,KEEP7PAN
*
         MVC   NAME(10),NAMEPAN    MOVE SEARCHED NAME TO READ NAME
*
         CLI   SELSAVE,C'E'        IS IT EDIT
         BNE   RDBROWSE           NOPE, NO SPECIAL HANDLING
*
         XC    ACTION,ACTION
         LA    R7,PANDATA         DCB ADDRESS
         ST    R7,ADCBADD
*
         OPEN  ((7),(OUTPUT))
         TM    48(R7),X'10'       IS IT OPEN?
         BNO   PLFEPAN             NOPE, FORGET IT
*
         MVC   ELINE+9(40),SPACES  CLEAR IT
         MVC   ELINE+9(10),NAMEPAN MOVE SEARCHED NAME TO UPDATE CARD
         LA    R14,ELINE+10        SET START OF LOOP
         LA    R15,9               SET LOOP CONTROL
*
UPDLOOP  DS    0H
         CLI   0(R14),C' '  GOT A BLANK
         BE    UPDMOVE      MOVE IN THE STUFF
         LA    R14,1(,14)   BUMP
         BCT   R15,UPDLOOP  LOOP
*
UPDMOVE  DS    0H
*
         MVI   0(R14),C','
         MVC   1(3,R14),PLEVEL       MOVE IN OLD LEVEL NUMBER
         MVC   4(4,R14),=C',ALL'
*
*
         B     READAGN
*
RDBROWSE DS    0H
*
         XC    ACTION,ACTION
         LA    R7,PANDATA         DCB ADDRESS
         CLI   PSPOOL,C'0'         NOT PANSPOOL FILE
         BE    OPENOUT             JUST OPEN IT
         LA    R7,PANLIST         DCB ADDRESS
OPENOUT  DS    0H
         ST    R7,ADCBADD
         OPEN  ((7),(OUTPUT))
         TM    48(R7),X'10'       IS IT OPEN?
         BO    READAGN            YUP
*
        SETMSG PLF008
*
         B     PLFEOJ
*
         EJECT
READAGN  DS    0H
*
READAPAN DS    0H
*
*        FIRST CHECK FOR ACCESS CODE REQUIRED
*
         CLI   PACCESS,C'A'
         BNE   READACOK
*
PROMPTAC DS    0H
*
         CLC   NEWNAME(4),PUSER             MATCH
         BE    READACOK                     YES, ALL DONE.
*
         B     NEXTLINE
*
READACOK DS    0H
*
         XC    ACTION,ACTION                SET TO BINARY ZEROS
         MVC   INCLUDES,NOENTRY             DO NOT PROCESS INCLUDES
         MVC   COMMENT,NOENTRY              DO NOT PROCESS COMMENTS
         L     R15,=V(PREAD)
         LA    R1,@PREAD
         BALR  R14,R15             GO READ A RECORD VIA PAM
         SPACE 1
         CLC   ACTION,=F'0'
         BE    GOODREAD            RETURN CODE SHOULD BE ZERO
         CLC   ACTION,=F'4'        4 IS PROBABLY "INCLUDE NOT FOUND"
         BE    GOODREAD
*
        SETMSG PLF009
         B     PLFEOJ
*
GOODREAD DS    0H
         CLC   RECORD(2),=C'$*'    TEST FOR END OF DATA
         BE    ENDREAD
*
*PROCESS A RECORD
*
         CLI   PSPOOL,C'1'         PANSPOOL FILE
         BE    PUTSPL              READ AGAIN
         L     R7,ADCBADD
         PUT   (7),RECORD
         B     READAGN
PUTSPL   DS    0H
         XC    ACTION,ACTION                SET TO BINARY ZEROS
         MVC   PSPREC(80),RECORD
         MVC   INCLUDES,NOENTRY             DO NOT PROCESS INCLUDES
         MVC   COMMENT,NOENTRY              DO NOT PROCESS COMMENTS
         L     R15,=V(PREAD)
         LA    R1,@PREAD
         BALR  R14,R15             GO READ A RECORD VIA PAM
         SPACE 1
         CLC   ACTION,=F'0'
         BE    GOTPSPL             RETURN CODE SHOULD BE ZERO
*
        SETMSG PLF009
         B     PLFEOJ
*
GOTPSPL  DS    0H
         MVC   PSPREC+80(53),RECORD+27
         L     R7,ADCBADD
         PUT   (7),PSPREC
         B     READAGN
ENDREAD  DS    0H
         L     R7,ADCBADD
         CLOSE ((7))
*
ENDREAD2 DS    0H
*
         L     R7,KEEP7PAN
         BR    R7
         TITLE 'PLF - PAN LIBRARY PROCESSOR - GET NEXT DIRECTORY ENTRY'
DIRLOOP1 DS    0H
*
*        WTO   'PLF--DIRLOOP1 ENTERED',ROUTCDE=11
*
         MVI   PANS,C' '
         MVC   PANS+1(79),PANS
*                                RE-INITIALIZE ATTRIBUTE BYTES
         MVI   TAB1,X'05' BEFORE LINE SELECTION FIELD
         MVI   TAB2,X'01' BEFORE MEMBER NAME
         MVI   TAB3,X'05' BEFORE NEWNAME/AC COLUMN
         MVI   TAB4,X'01' BEFORE LEVEL NUMBER
         MVI   TAB5,X'02' BEFORE SECURITY FLAG
         MVI   TAB6,X'01' BEFORE USER CODE
         MVI   TAB7,X'01' AFTER  USER CODE
*
         XC    ACTION,ACTION
*        MVC   COMMENT,NOENTRY               WE WANT NO COMMENTS
         MVC   COMMENT,SPACES                WE WANT COMMENTS
         MVC   SUBSET,NOENTRY                BUT NO SUBSET JUNK
*  SEARCH
         L     R15,=V(PSRCH)
         LA    R1,@PSRCH
         BALR  R14,R15               INVOKE DIR SEARCH FUNCTION
DIRL0C1  DC    X'0700'   NO-OP
         SPACE 1
         CLC   ACTION,=F'0'        RETURN CODE SHOULD BE ZERO
         BNE   DIRERR7
*
         CLC   DIRENTRY(2),=C'$*'  TEST FOR NO MORE DIR ENTRIES
         BE    DIRLEND             THATS ALL..EXIT
*
*  MOVE/FORMAT DIRECTORY FIELDS FOR / DIR DISPLAY                     *
*
         MVC   NAMEPAN(10),DNAME
*
         CLI   DIRTOPN,C' '        TOP OF SCREEN
         BNE   *+16
         MVC   DIRTOPN(10),DNAME
         MVC   DIRTOP2(10),DNAME
*
         L     R1,DIRLAST
         CLC   DNAME(10),0(R1)
         BNH   *+18
         LA    R1,10(,R1)
         MVC   00(10,R1),DNAME
         ST    R1,DIRLAST
*
         CLI   DSECURE,C'0'       ACCESS CODE?
         BE    NOSECDIR           NO
         CLI   DSECURE,C' '       ACCESS CODE?
         BE    NOSECDIR           NO
*
         MVI   PACCESS,C'A'       INDICATE ACCESS CODE REQUIRED
*
         CLI   SHOWFLAG,C'0'      SHOW USER CODES
         BNE   *+8                YES
         MVI   TAB6,X'04' BEFORE USER CODE, OUTPUT NON-DISPLAY
*
NOSECDIR DS    0H
         MVC   PUSER(4),DUSER
*
         MVC   PLEVEL(3),DLEVEL
         MVC   PTYPE(5),DTYPE
         MVC   PSTATUS(3),DSTATUS
         MVC   PDATEM(8),DDATEM
         MVC   PDATEA(8),DDATEA
         PACK  PACKWK(5),DSTMTS(8)
         MVC   PSTMTS(8),EDIT8
         ED    PSTMTS(8),PACKWK+1
         MVC   PLASTACT(4),DLASTACT
*
         B     DIRLEND
*
DIRERR7  DS    0H
         L     R0,ACTION
         DC    H'0'
*
DIRLEND  DS    0H
         L     R0,ACTION
         BR    R7
*
         TITLE 'PERFORMED ROUTINES'
MOVENAME DS    0H
*
         CLI   0(R15),C' '                END OF THIS NAME
         BE    MOVEDONE                   YUP
*
         MVC   0(1,R1),0(R15)             END OF THIS NAME
         LA    R1,1(,1)                   BUMP " TO " FIELD ADDRESS
         LA    R15,1(,15)                 BUMP "FROM" FIELD ADDRESS
         B     MOVENAME                   LOOP
*
MOVEDONE DS    0H
*
         BR    R14                        YUP
*
         TITLE 'PLF - ERROR MESSAGE ROUTINES  '
*
PLFRRPOP DS    0H
        SETMSG PLF006
         B     PLFEOJ
*
PLFRRPRC DS    0H
        SETMSG PLF001A
         B     PLFEOJ
*
PLFBADRC DS    0H
        SETMSG PLF001L
         B     PLFEOJ
*
PLFRCOMP DS    0H
        SETMSG PLF001N
         B     PLFEOJ
*
PLFBADCM DS    0H
        SETMSG PLF001N
         B     PLFEOJ
*
         LTORG
         EJECT
         LTORG
         TITLE 'REGISTER EQUATES'
***********************************************************************
*                                                                     *
*        REGISTER EQUATES                                             *
*                                                                     *
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         TITLE 'WORKING STORAGE AREAS'
*
         DS    0F
TABCOUNT DC    F'0'
ZEROFF   DC    X'000000FF'
P1       DC    PL1'+1'
P3ZERO   DC    PL3'+0'
OUT7HOLD DS    F
L1       DC    F'1'
L10      DC    F'10'
L6       DC    F'6'
L8       DC    F'8'
L44      DC    F'44'
L50      DC    F'50'
*
DIGITS3  DC    X'202020'
THREEPT  DC    X'2020204B'
EDIT4    DC    X'40202120'
EDIT6    DC    X'402020202121'
EDIT8    DC    X'4020202020202120'
*
PSPOOL   DC    C'0'
VCOPY    DC    CL8'VCOPY '
ZUSER    DC    CL8'ZUSER '
USERID   DC    CL8' '
SPFMOVE  DC    CL8'MOVE   '
U8       DC    F'8'
*
DSNOTLST DC    CL44' '
KEEP7PAN DS    F
ADCBADD  DS    F
PACKWK   DS    PL5
PLFPOPEN DC    C'0'
PSPREC   DC    CL133' '
*
PLFZUSER DC    CL8' '
*
*
ULINE    DC    CL80'++USER '
ALINE    DC    CL80'++ACCESS '
ACLINE   DS    CL80
OLINE    DS    CL80
*
ENAME    DC    CL10' '
*
         DS    0F
PANPARMS DC    X'80',AL3(H0)
H0       DC    H'0'
*
*
DLINE    DC    CL80'++STATUS      '
*
KLINE    DC    CL80'++DELETE NAME='
*
ELINE    DC    CL80'++UPDATE '
*
RLINE    DC    CL80'++RENAME '
*
NULLPARM DC    X'80',AL3(H0)
*
CTAB     DS    0H
PLFOPTP  DC    A(SELECTR)
         DC    X'80',AL3(PLFOPT)
SELECTR  DC    CL8'SELECT  '
PLFOPT   DC    CL8'PLFOPT  '
*
L78      DC    F'78'
FZEROS   DC    F'0'
F8       DC    F'8'
F4       DC    F'4'
L3       DC    F'3'
L77      DC    F'77'
L20      DC    F'20'
L40      DC    F'40'
L4       DC    F'4'
*
DISPLRC  DS    F          RETURN CODE FROM TBDISPL
H1       DC    H'1'
H2       DC    H'2'
H8       DC    H'8'
*
         TITLE 'DYNAMIC ALLOCATION PARAMETER LISTS'
         DS    0F
FREEIN   DC    AL1(128),AL3(RBFIN)     SYSIN
OLDIN    DC    AL1(128),AL3(RBOIN)     SYSIN
NEWIN    DC    AL1(128),AL3(RBNIN)     SYSIN
*
FREEPR   DC    AL1(128),AL3(RBFPR)     SYSPRINT
OLDPR    DC    AL1(128),AL3(RBOPR)     SYSPRINT
NEWPR    DC    AL1(128),AL3(RBNPR)     SYSPRINT
*
FREEDD2  DC    AL1(128),AL3(RBFDD2)    PANDD2
ALLODD2  DC    AL1(128),AL3(RBDDD2)    PANDD2
*
FREEDD1  DC    AL1(128),AL3(RBFDD1)    PANDD1
OLDDD1   DC    AL1(128),AL3(RBODD1)    PANDD1
*
FREEDATA DC    AL1(128),AL3(RBFDATA)   PANDATA
OLDDATA  DC    AL1(128),AL3(RBODATA)   PANDATA
NEWDATA  DC    AL1(128),AL3(RBNDATA)   PANDATA
*
CONCATIN DC    AL1(128),AL3(CONCAT)    SYSIN+PANDATA
DECONCAT DC    AL1(128),AL3(DECON)     SYSIN+PANDATA
*
FREE1    DC    X'80',AL3(FREEUT1)         REQUEST BLOCK
FREE2    DC    X'80',AL3(FREEUT2)         REQUEST BLOCK
ALLO1    DC    X'80',AL3(ALLOUT1)         REQUEST BLOCK
SOUTPARM DC    X'80',AL3(ALLOUT2)         REQUEST BLOCK
         TITLE 'SYSIN (@TEMP.PANDATA) REQUEST BLOCKS'
         DS    0F
RBFIN    DC    X'14'           LENGTH = 20
         DC    X'02'           FREE REQUEST
         DC    X'00'           FLAGS1 = ALLOW OFFLINE UNITS
         DC    X'00'           REST OF FLAGS1
         DC    F'0'            RETURN CODES
         DC    A(*+12)         POINTER TO FIRST TEXT UNIT
         DC    F'0'            RESERVED
         DC    F'0'            FLAGS2
*
         DC    A(UNCOND)       UNCONDITIONAL FREE
         DC    X'80',AL3(DDIN) POINTER TO DDNAME TEXT UNIT
*
*
         DS    0F
RBOIN    DC    X'14'           LENGTH = 20
         DC    X'01'           ALLOCATE REQUEST
         DC    X'00'           FLAGS1 = ALLOW OFFLINE UNITS
         DC    X'00'           REST OF FLAGS1
         DC    F'0'            RETURN CODES
         DC    A(*+12)         POINTER TO FIRST TEXT UNIT
         DC    F'0'            RESERVED
         DC    F'0'            FLAGS2
*
         DC    A(DDIN)             DDNAME
         DC    A(NAMCARD)          DSNAME--&USERID.@TEMP.PANCARD
         DC    A(RLSE)             RELEASE SPACE AT CLOSE
         DC    X'80',AL3(DISPSHR)  POINTER TO DISP=SHR
*
         DS    0F
RBNIN    DC    X'14'           LENGTH = 20
         DC    X'01'           ALLOCATE REQUEST
         DC    X'00'           FLAGS1 = ALLOW OFFLINE UNITS
         DC    X'00'           REST OF FLAGS1
         DC    F'0'            RETURN CODES
         DC    A(*+12)         POINTER TO FIRST TEXT UNIT
         DC    F'0'            RESERVED
         DC    F'0'            FLAGS2
*
         DC    A(DDIN)             DDNAME
         DC    A(NAMCARD)          DSNAME
         DC    A(LR80)             LRECL=80
         DC    A(BLK3120)          BLKSIZE=3120
         DC    A(RECFMFB)          RECFM=FB
         DC    A(PSPACE)           PRIMARY SPACE = 1
         DC    A(SSPACE)           SECONDARY SPACE = 1
         DC    A(SPTRK)            TRACKS
         DC    A(DISPNEW)          DISP=NEW
         DC    A(RLSE)             RELEASE SPACE AT CLOSE
         DC    X'80',AL3(DISPCAT)          ,CATLG
*
*
         TITLE 'SYSPRINT (@TEMP.PANLIST) REQUEST BLOCKS'
*
         DS    0F
RBFPR    DC    X'14'           LENGTH = 20
         DC    X'02'           FREE REQUEST
         DC    X'00'           FLAGS1 = ALLOW OFFLINE UNITS
         DC    X'00'           REST OF FLAGS1
         DC    F'0'            RETURN CODES
         DC    A(*+12)         POINTER TO FIRST TEXT UNIT
         DC    F'0'            RESERVED
         DC    F'0'            FLAGS2
*
         DC    A(UNCOND)       UNCONDITIONAL FREE
         DC    X'80',AL3(DDPR) POINTER TO DDNAME TEXT UNIT
*
*
         DS    0F
RBOPR    DC    X'14'           LENGTH = 20
         DC    X'01'           ALLOCATE REQUEST
         DC    X'00'           FLAGS1 = ALLOW OFFLINE UNITS
         DC    X'00'           REST OF FLAGS1
         DC    F'0'            RETURN CODES
         DC    A(*+12)         POINTER TO FIRST TEXT UNIT
         DC    F'0'            RESERVED
         DC    F'0'            FLAGS2
*
         DC    A(DDPR)             DDNAME
         DC    A(NAMLIST)          DSNAME
         DC    A(RLSE)             RELEASE SPACE AT CLOSE
         DC    X'80',AL3(DISPSHR)  POINTER TO DISP=SHR
*
         DS    0F
RBNPR    DC    X'14'           LENGTH = 20
         DC    X'01'           ALLOCATE REQUEST
         DC    X'00'           FLAGS1 = ALLOW OFFLINE UNITS
         DC    X'00'           REST OF FLAGS1
         DC    F'0'            RETURN CODES
         DC    A(*+12)         POINTER TO FIRST TEXT UNIT
         DC    F'0'            RESERVED
         DC    F'0'            FLAGS2
*
         DC    A(DDPR)             DDNAME
         DC    A(NAMLIST)          DSNAME
         DC    A(PSPACE)           PRIMARY SPACE = 1
         DC    A(SSPACE)           SECONDARY SPACE = 1
         DC    A(SPTRK)            CYLINDERS
         DC    A(DISPNEW)          DISP=NEW
         DC    A(RLSE)             RELEASE SPACE AT CLOSE
         DC    X'80',AL3(DISPCAT)          ,CATLG
*
         TITLE 'PANDD2   (DUMMY   ) REQUEST BLOCKS'
*
*
*
         DS    0F
RBFDD2   DC    X'14'           LENGTH = 20
         DC    X'02'           FREE REQUEST
         DC    X'00'           FLAGS1 = ALLOW OFFLINE UNITS
         DC    X'00'           REST OF FLAGS1
         DC    F'0'            RETURN CODES
         DC    A(*+12)         POINTER TO FIRST TEXT UNIT
         DC    F'0'            RESERVED
         DC    F'0'            FLAGS2
*
         DC    A(UNCOND)       UNCONDITIONAL FREE
         DC    X'80',AL3(DDDD2) POINTER TO DDNAME TEXT UNIT
*
*
         DS    0F
RBDDD2   DC    X'14'           LENGTH = 20
         DC    X'01'           ALLOCATE REQUEST
         DC    X'00'           FLAGS1 = ALLOW OFFLINE UNITS
         DC    X'00'           REST OF FLAGS1
         DC    F'0'            RETURN CODES
         DC    A(*+12)         POINTER TO FIRST TEXT UNIT
         DC    F'0'            RESERVED
         DC    F'0'            FLAGS2
*
         DC    A(DUMMY)        DUMMY DATASET
         DC    X'80',AL3(DDDD2) POINTER TO DDNAME TEXT UNIT
*
         TITLE 'PANDD1   (PANLIB  ) REQUEST BLOCKS'
*
         DS    0F
RBFDD1   DC    X'14'           LENGTH = 20
         DC    X'02'           FREE REQUEST
         DC    X'00'           FLAGS1 = ALLOW OFFLINE UNITS
         DC    X'00'           REST OF FLAGS1
         DC    F'0'            RETURN CODES
         DC    A(*+12)         POINTER TO FIRST TEXT UNIT
         DC    F'0'            RESERVED
         DC    F'0'            FLAGS2
*
         DC    A(UNCOND)       UNCONDITIONAL FREE
         DC    X'80',AL3(DDDD1) POINTER TO DDNAME TEXT UNIT
*
         DS    0F
RBODD1   DC    X'14'           LENGTH = 20
         DC    X'01'           ALLOCATE REQUEST
         DC    X'00'           FLAGS1 = ALLOW OFFLINE UNITS
         DC    X'00'           REST OF FLAGS1
         DC    F'0'            RETURN CODES
         DC    A(*+12)         POINTER TO FIRST TEXT UNIT
         DC    F'0'            RESERVED
         DC    F'0'            FLAGS2
*
         DC    A(DDDD1)            DDNAME
         DC    A(NAMLIB)           DSNAME
         DC    X'80',AL3(DISPSHR)  POINTER TO DISP=SHR
*
         TITLE 'PANDATA  (@TEMP.PANDATA) REQUEST BLOCKS'
*
*
         DS    0F
RBFDATA  DC    X'14'           LENGTH = 20
         DC    X'02'           FREE REQUEST
         DC    X'00'           FLAGS1 = ALLOW OFFLINE UNITS
         DC    X'00'           REST OF FLAGS1
         DC    F'0'            RETURN CODES
         DC    A(*+12)         POINTER TO FIRST TEXT UNIT
         DC    F'0'            RESERVED
         DC    F'0'            FLAGS2
*
         DC    A(UNCOND)       UNCONDITIONAL FREE
         DC    X'80',AL3(DDDATA) POINTER TO DDNAME
*
         DS    0F
RBODATA  DC    X'14'           LENGTH = 20
         DC    X'01'           ALLOCATE REQUEST
         DC    X'00'           FLAGS1 = ALLOW OFFLINE UNITS
         DC    X'00'           REST OF FLAGS1
         DC    F'0'            RETURN CODES
         DC    A(*+12)         POINTER TO FIRST TEXT UNIT
         DC    F'0'            RESERVED
         DC    F'0'            FLAGS2
*
         DC    A(DDDATA)           DDNAME
         DC    A(NAMDATA)          DSNAME
         DC    A(RLSE)             RELEASE SPACE AT CLOSE
         DC    X'80',AL3(DISPSHR)  POINTER TO DISP=SHR
*
         DS    0F
RBNDATA  DC    X'14'           LENGTH = 20
         DC    X'01'           ALLOCATE REQUEST
         DC    X'00'           FLAGS1 = ALLOW OFFLINE UNITS
         DC    X'00'           REST OF FLAGS1
         DC    F'0'            RETURN CODES
         DC    A(*+12)         POINTER TO FIRST TEXT UNIT
         DC    F'0'            RESERVED
         DC    F'0'            FLAGS2
*
         DC    A(DDDATA)           DDNAME
         DC    A(NAMDATA)          DSNAME
         DC    A(LR80)             LRECL=80
         DC    A(BLK3120)          BLKSIZE=3120
         DC    A(RECFMFB)          RECFM=FB
         DC    A(PSPACE)           PRIMARY SPACE = 1
         DC    A(SSPACE)           SECONDARY SPACE = 1
         DC    A(SPTRK)            CYLINDERS
         DC    A(DISPNEW)          DISP=NEW
         DC    A(RLSE)             RELEASE SPACE AT CLOSE
         DC    X'80',AL3(DISPCAT)          ,CATLG
*
         TITLE 'CONCATENATION/DECONCATENATION REQUEST BLOCKS'
         DS    0F
CONCAT   DC    X'14'           LENGTH = 20
         DC    X'03'           CONCATENATE
         DC    X'00'           FLAGS1 = ALLOW OFFLINE UNITS
         DC    X'00'           REST OF FLAGS1
         DC    F'0'            RETURN CODES
         DC    A(*+12)         POINTER TO FIRST TEXT UNIT
         DC    F'0'            RESERVED
         DC    F'0'            FLAGS2
*
         DC    X'80',AL3(DDCON)           DDNAME TEXT UNIT
*
         DS    0F
DECON    DC    X'14'           LENGTH = 20
         DC    X'04'           DE-CONCATENATE
         DC    X'00'           FLAGS1 = ALLOW OFFLINE UNITS
         DC    X'00'           REST OF FLAGS1
         DC    F'0'            RETURN CODES
         DC    A(*+12)         POINTER TO FIRST TEXT UNIT
         DC    F'0'            RESERVED
         DC    F'0'            FLAGS2
*
         DC    X'80',AL3(DDIN)            DDNAME TEXT UNIT
*
         TITLE 'SYSUT1/SYSUT2 FOREGROUND PRINT REQUEST BLOCKS'
         DS    0F
*
FREEUT1  DC    X'14'           LENGTH = 20
         DC    X'02'           FREE REQUEST
         DC    X'00'           FLAGS1 = ALLOW OFFLINE UNITS
         DC    X'00'           REST OF FLAGS1
         DC    F'0'            RETURN CODES
         DC    A(*+12)         POINTER TO DYNTEXT1
         DC    F'0'            RESERVED
         DC    F'0'            FLAGS2
*
         DC    A(UNCOND)       UNCONDITIONAL FREE
         DC    X'80',AL3(UT1)
*
ALLOUT1  DC    X'14'           LENGTH = 20
         DC    X'01'           ALLOCATE REQUEST
         DC    X'00'           FLAGS1 = ALLOW OFFLINE UNITS
         DC    X'00'           REST OF FLAGS1
         DC    F'0'            RETURN CODES
         DC    A(*+12)         POINTER TO DYNTEXT1
         DC    F'0'            RESERVED
         DC    F'0'            FLAGS2
*
         DC    A(DISPSHR)
UT1DSN   DS    F
         DC    X'80',AL3(UT1)
*
         DS    0F
*
FREEUT2  DC    X'14'           LENGTH = 20
         DC    X'02'           FREE REQUEST
         DC    X'00'           FLAGS1 = ALLOW OFFLINE UNITS
         DC    X'00'           REST OF FLAGS1
         DC    F'0'            RETURN CODES
         DC    A(*+12)         POINTER TO DYNTEXT1
         DC    F'0'            RESERVED
         DC    F'0'            FLAGS2
*
         DC    A(UNCOND)       UNCONDITIONAL FREE
         DC    X'80',AL3(UT2)
*
         DS    0F
*
ALLOUT2  DC    X'14'           LENGTH = 20
         DC    X'01'           DSNAME ALLOCATION
         DC    X'00'           FLAGS1 = ALLOW OFFLINE UNITS
         DC    X'00'           REST OF FLAGS1
         DC    F'0'            RETURN CODES
         DC    A(*+12)         POINTER TO DYNTEXT1
         DC    F'0'            RESERVED
         DC    F'0'            FLAGS2
*
         DC    A(UT2)          POINTER TO DYNDDNAM
         DC    A(SOUTLREC)                LRECL
         DC    A(SOUTBLOK)                BLKSIZE
         DC    A(SOUTRECF)                RECFM
         DC    A(SOUTFREE)                FREE=CLOSE
         DC    X'80',AL3(SOUTCLAS)        SYSOUT CLASS
*
         TITLE 'DYANLLOC TEXT UNITS'
UT1      DC    H'0001'         DDNAME=  PARAMETER
         DC    H'0001'         NUMBER OF TEXT UNITS
         DC    H'0006'         LENGTH OF PARM
         DC    CL6'SYSUT1'     DDNAME
*
UT2      DC    H'0001'         DDNAME=  PARAMETER
         DC    H'0001'         NUMBER OF TEXT UNITS
         DC    H'0006'         LENGTH OF PARM
         DC    CL6'SYSUT2'     DDNAME
*
SOUTLREC DC    X'0042'         LRECL    PARAMETER
         DC    H'0001'         NUMBER OF TEXT UNITS
         DC    H'0002'         LENGTH OF PARM
PRTLRECL DC    X'0050'         LRECL
*
SOUTFREE DC    X'001C'         FREE=CLOSE
         DC    H'0000'         NUMBER OF TEXT UNITS
*
SOUTBLOK DC    X'0030'         BLKSIZE  PARAMETER
         DC    H'0001'         NUMBER OF TEXT UNITS
         DC    H'0002'         LENGTH OF PARM
PRTBLOCK DC    X'0050'         MATCHES LRECL
*
SOUTRECF DC    X'0049'         RECFM    PARAMETER
         DC    H'0001'         NUMBER OF TEXT UNITS
         DC    H'0001'         LENGTH OF PARM
SOUTFBA  DC    X'94'           FBA
*
SOUTCLAS DC    H'0024'         SYSOUT   PARAMETER
         DC    H'0001'         NUMBER OF TEXT UNITS
         DC    H'0001'         LENGTH OF PARM
         DC    C'A'            CLASS
DDCON    DC    H'0001'         DDNAME
         DC    H'0002'         NUMBER=2
         DC    H'0005'         LEN 1
         DC    C'SYSIN'        DDN 1
         DC    X'0007'         LEN 2
         DC    C'PANDATA'      DDN 2
*
DDIN     DC    H'0001'         DDNAME=  PARAMETER
         DC    H'0001'         NUMBER OF TEXT UNITS
         DC    H'0005'         LENGTH OF PARM
         DC    CL6'SYSIN'      DDNAME
*
DDPR     DC    H'0001'         DDNAME=  PARAMETER
         DC    H'0001'         NUMBER OF TEXT UNITS
         DC    H'0008'         LENGTH OF PARM
         DC    CL8'SYSPRINT'   DDNAME
*
DDDD2    DC    H'0001'         DDNAME=  PARAMETER
         DC    H'0001'         NUMBER OF TEXT UNITS
         DC    H'0006'         LENGTH OF PARM
         DC    CL6'PANDD2'     DDNAME
*
DDDATA   DC    H'0001'         DDNAME=  PARAMETER
         DC    H'0001'         NUMBER OF TEXT UNITS
         DC    H'0007'         LENGTH OF PARM
         DC    CL8'PANDATA'    DDNAME USED IN CONCATENATIONS
*
DDDD1    DC    H'0001'         DDNAME=  PARAMETER
         DC    H'0001'         NUMBER OF TEXT UNITS
         DC    H'0006'         LENGTH OF PARM
         DC    CL6'PANDD1'     DDNAME
*
NAMCARD  DC    H'0002'         DSNAME=  PARAMETER
         DC    H'0001'         NUMBER OF TEXT UNITS
LENCARD  DC    H'0044'         LENGTH OF PARM
DSNCARD  DC    CL44' '         DSNAME
*
NAMDATA  DC    H'0002'         DSNAME=  PARAMETER
         DC    H'0001'         NUMBER OF TEXT UNITS
LENDATA  DC    H'0044'         LENGTH OF PARM
DSNDATA  DC    CL44' '         DSNAME
*
NAMLIST  DC    H'0002'         DSNAME=  PARAMETER
         DC    H'0001'         NUMBER OF TEXT UNITS
LENLIST  DC    H'0044'         LENGTH OF PARM
DSNLIST  DC    CL44' '         DSNAME
*
NAMLIB   DC    H'0002'         DSNAME=  PARAMETER
         DC    H'0001'         NUMBER OF TEXT UNITS
LENLIB   DC    H'0044'         LENGTH OF PARM
PANLIB   DC    CL44' '         DSNAME
*
UNCOND   DC    X'0007'         FREE
         DC    H'0000'
*
DISPSHR  DC    X'0004'         STATUS
         DC    H'0001'
         DC    H'0001'
         DC    X'0800'         08=SHR
*
DISPNEW  DC    X'0004'         STATUS
         DC    H'0001'
         DC    H'0001'
         DC    X'0400'         04=NEW
*
DISPCAT  DC    X'0005'         DISPOSITION
         DC    H'0001'
         DC    H'0001'
         DC    X'0200'         02=CATLG
*
LR80     DC    X'0042'         LRECL    PARAMETER
         DC    H'0001'         NUMBER OF TEXT UNITS
         DC    H'0002'         LENGTH OF PARM
         DC    X'0050'         LRECL
*
BLK3120  DC    X'0030'         BLKSIZE  PARAMETER
         DC    H'0001'         NUMBER OF TEXT UNITS
         DC    H'0002'         LENGTH OF PARM
         DC    H'3120'         MATCHES LRECL
*
RECFMFB  DC    X'0049'         RECFM    PARAMETER
         DC    H'0001'         NUMBER OF TEXT UNITS
         DC    H'0001'         LENGTH OF PARM
         DC    X'9000'         FB
*
PSPACE   DC    H'0010'         PRIMARY SPACE
         DC    H'0001'         NUMBER OF TEXT UNITS
         DC    H'0003'         LENGTH OF PARM
         DC    AL3(30)         30
*
SSPACE   DC    H'0011'         SECONDARY SPACE
         DC    H'0001'         NUMBER OF TEXT UNITS
         DC    H'0003'         LENGTH OF PARM
         DC    AL3(30)         30
*
SPTRK    DC    H'0007'         TRACK ALLOCATION
         DC    H'0000'         NUMBER OF TEXT UNITS
*
DUMMY    DC    X'0024'         TRACK ALLOCATION
         DC    H'0000'         NUMBER OF TEXT UNITS
RLSE     DC    X'000D'         RELEASE SPACE AT CLOSE
         DC    H'0000'         NUMBER OF TEXT UNITS
*
*
*
*              V D E F I N E    STORAGE AREAS
*
*
*  VARIABLES FOR PLFP1--PLF PRIMARY OPTION PANEL
*
PLFACC   DC    CL04' ' ACCESS CODE. IF SECURITY-LEVEL PRESENT.
*
*  VARIABLES FOR PLFMENU2--DIRECTORY DISPLAY
*
PCMD     DC    CL40' ' COMMAND LINE.
*
TAB1     DC    X'13'
PANS     DC    CL03' ' LINE SELECTION CODE.
TAB2     DC    X'05'
NAMEPAN  DC    CL10' ' LIBRARY MEMBER NAME.
TAB3     DC    X'13'
NEWNAME  DC    CL10' ' NEWNAME FOR RENAME OR COPY.
TAB4     DC    X'05'
PANLINE  DS    0CL54   DETAIL DIRECTORY DATA.
         DC    C' '
PLEVEL   DC    CL3' '
TAB5     DC    C' ' BEFORE SECURITY FLAG
PACCESS  DC    C' '
TAB6     DC    C' ' BEFORE USER CODE
PUSER    DC    CL4' '
TAB7     DC    C' ' AFTER USER CODE
PTYPE    DC    CL5' '
         DC    C' '
PSTATUS  DC    CL3' '
         DC    C' '
PDATEM   DC    CL8' '
         DC    C' '
PDATEA   DC    CL8' '
         DC    C' '
PSTMTS   DC    CL8' '
         DC    C' '
PLASTACT DC    CL4' '
         DC    CL1' '         PAD TO 80 BYTES
*
CLINE    DC    CL10'++CONTROL '
PANCONTR DC    CL70'          '
*
XLINE    DC    CL10'++CONTROL '
XFERCON  DC    CL70'          '
*
NUMBPAN  DC    CL4'0000' CURRENT USER CODE FOR ++USER COMMAND
NUMBNEW  DC    CL4'0000' NEW USER CODE FOR ++USER COMMAND
*
*
*
DDLIB    DC    CL8'PANDD1'
*
DOUBLEWD DS    D              PACKED DECIMAL WORK AREA
MASKMOVE MVC   0(1,R14),PLFMASK
PLFMASK  DC    X'05'               INPUT DISPLAY, CAPS
SPACES   DC    CL80' '
CZEROS   DC    CL8'00000000'
*
POSITION DC    CL11' '
*
L7       DC    F'7'           ADDRESS OF TABLE TO USE FOR OUTMSG
TABADDRH DS    A              ADDRESS OF TABLE TO USE FOR OUTMSG
DNMADDRH DS    A              ADDRESS OF NAMES TO USE FOR OUTMSG
*
ISPARMS  DS    10F            PARM LIST FOR ISPLINK
*
*     THE ISP TABLE FLAGS INDICATE NO ACTIVE TABLE (0), OR ONE EXISTS
PLFTABFL DS    XL1     DO I HAVE A CURRENT "DISPLAY ACTIVE" TABLE
*
*
*
*
SELSAVE  DS    CL3            LINE SELECTION CODE SAVE AREA
*
*
WORK     DS    D
HEX      DC    C'0123456789ABCDEF'
*.....................................................................*
*  LIST FORM OF MACROS                                                *
*.....................................................................*
         SPACE
@POPEN CALL ,(ACTION,DDLIB,BACKUP),VL,MF=L
*
*     POPEN PARAMETERS
*
BACKUP   DC    CL8'NO-ENTRY'     MUST BE CL8'BACKUP' IF PROTECTION FIL
         SPACE
@PSRCH   CALL ,(ACTION,DIRENTRY,NAME1,NAME2,COMMENT,SUBSET),VL,MF=L
         SPACE
@PREAD   CALL  ,(ACTION,RECORD,NAME,INCLUDES,COMMENT),VL,MF=L
*
*  SHARED PARAMETER
         DS    0D
         DC    CL8'ACTION'
ACTION   DC    F'0'         RETURN CODE FROM PAN, SHOULD BE SET TO ZERO
         DS    0D
         DC    CL8'RECORD'
RECORD   DS    CL80                STATMENT WILL BE RETURNED HERE
         DS    0D
         DC    CL8'NAME  '
NAME     DC    CL22'A          '   PREAD NAME
         DS    0D
         DC    CL8'INCLUDES'
INCLUDES DS    CL8
         DS    0D
         DC    CL8'COMMENT '
COMMLINE DC    X'01'               OUTPUT DISPLAY
         DC    CL26' '
COMMTAB  DC    X'07'               INPUT DISPLAY, CAPS
COMMENT  DC    CL52'NO-ENTRY'      NO COMMENTS WANTED, ELSE='COMMENT'
COMMFLAG DC    C'0'       0 = DON'T SHOW COMMENTS, 1 = SHOW COMMENTS
SHOWFLAG DC    C'0'       0 = DON'T SHOW ACCESS CODES, 1 = SHOW THEM
XFERFLAG DC    C'0'       0 = JCL NOT COPIED FOR XFER, 1 = COPIED
XFERNUMB DS    F          NUMBER OF MEMBERS SELECTED FOR TRANSFER
KEYLEN1  DC    F'10'
KEYPOS1  DC    F'1'
LRECL1   DC    F'12'
SORTPARM DS    0F
         DC    A(KEYLEN1)
         DC    A(KEYPOS1)
         DC    A(LRECL1)
         DC    A(XFERNUMB)
         DC    A(XFERMEMS)
XFERST   DC    A(XFERMEMS)
XFERLAST DS    F
*
PJC1     DC    CL80' '
PJC2     DC    CL80' '
*
XFEREXEC DC    C'//XFER EXEC PGM=PAN#2'
XFERDD1  DC    CL26'//PANDD1  DD DISP=SHR,DSN='
XFERFROM DC    CL54' '
XFERDD2  DC    CL50'//PANDD2 DD DISP=(,PASS),UNIT=SYSDA,SPACE=(CYL,(20'
         DC    CL10',20)),DSN='
         DC    X'50',CL19'PAN                '
XFERXFER DC    CL11'++TRANSFER '
XFERNAME DC    CL69'           '
*
RSTREXEC DC    C'//RSTR EXEC PGM=PAN#2'
RSTRDD1  DC    CL26'//PANDD1  DD DISP=SHR,DSN='
XFERTO   DC    CL54' '
RSTRDD3  DC    CL35'//PANDD3  DD DISP=(OLD,DELETE),DSN='
         DC    X'50',CL44'PAN'
RSTRRSTR DC    C'++RESTORE  '
DD2DUMM  DC    C'//PANDD2   DD DUMMY'
DD3DUMM  DC    C'//PANDD3   DD DUMMY'
*
ARCHJCL1 DC    C'//ARCH EXEC PGM=PAN#2'
ARCHJCL3 DC    CL26'//PANDD1  DD DISP=SHR,DSN='
ARCHFROM DC    CL54' '
ARCHJCL4 DC    CL43'//PANDD2  DD DISP=(NEW,CATLG),DSN=PSC.SUPD.'
ARCHTAP1 DC    CL54' '
ARCHJCL5 DC    C'// UNIT=TAPE,DCB=S.MODEL,LABEL=EXPDT=99000'
ARCHJCL6 DC    CL35'//PANDD3  DD DISP=OLD,DSN=PSC.SUPD.'
ARCHTAP2 DC    CL54' '
ARCHJCL7 DC    C'++OPTION INPUT'
*
DELTJCL1 DC    C'//DELT EXEC PGM=PAN#2'
*
STATJCL1 DC    C'//STAT EXEC PGM=PAN#1'
*
SYSZCARD DC    C'//SYSPRINT DD SYSOUT=Z'
SYSPCARD DC    C'//SYSPRINT DD SYSOUT=A,COPIES=2'
SYSNCARD DC    C'//SYSIN    DD *'
*
XFERMOD  DC    CL4' '
XFERSTA  DC    CL4' '
WORK80   DC    CL80' '
*
@PCLOSE  CALL  ,(ACTION),VL,MF=L
         EJECT
*.....................................................................*
*  CONSTRUCTS FOR PANVALET                                            *
*.....................................................................*
*  PSRCH PARAMETERS
DIRENTRY DS    0CL80               DIR ENTRY RETURNED IN 0-UP FORMAT
DNAME    DS    CL10                NAME LEFT JUSTIFIED
DLEVEL   DS    CL3                 LEVEL NUMBER
DUSER    DS    CL4                 USER CODE
DSECURE  DS    CL1                 SECURITY CODE
DTYPE    DS    CL5                 LANGUAGE TYPE
DSTATUS  DS    0CL3                3 CHAR STATUS
DPRODT   DS    CL1                 P-PROD  T-TEST
DEORDD   DS    CL1                 E-ENABLE  D-DISABLE
DAORDI   DS    CL1                 A-ACTIVE  I-INACTIVE
DDATEM   DS    CL8                 DATE OF LAST MAINTENANCE MM/DD/YY
DDATEA   DS    CL8                 DATE OF LAST ACCESS  MM/DD/YY
DBLOCKS  DS    CL5                 NO. OF BLOCKS
DSTMTS   DS    CL8                 NO. OF STATEMENTS
DLASTACT DS    CL4                 LAST ACTION, 1ST POS. IS * IF PROD
DBYTES   DS    CL2                 NO. BYTES PER STATMENT
DSUBSET  DS    CL4                 NO. OF SUBSETS(SUPERSETS ONLY)
DNAME2   DS    CL10                NAME RIGHT JUSTIFIED
         DS    CL1                 NOT USED
         DS    CL1                 N=NOFORMAT, T=TSO, ELSE BLANK
DINC     DS    CL1                 I=INCLUDE BEING EXPND (EXIT ONLY)
DVERSION DS    XL2                 VER. #(FOR PROTECTION FILES ONLY)
         EJECT
TOPSEL   DC    CL10'           '   SEARCH NAME1
DIRBOTM  DC    CL10'           '   SEARCH NAME1
DIRTOPN DC     CL22'A          '   SEARCH NAME1
DIRTOP2 DC     CL10' '
NAME1    DC    CL22'A          '   SEARCH NAME1
NAME2    DC    CL11'Z999999999 '   SEARCH NAME2
TOPNAME DC     CL22'A          '   SEARCH NAME1
**
* NOTE  COMMENTS WILL BE PROCEDED AND ENDED WITH AN *
**
SUBSET   DC    CL27'NO-ENTRY'      NO SUBSETS WANTED, ELSE='SUBSET'
**
*  NOTE  IF SUBSETS ARE REQUESTED THE FOLLOWING WILL BE A DEFINITION OF
*        THE SUBSET DIRECTORY ENTRY
**
SUBENTRY DS    0CL27
SUBNAME  DS    CL11                FORMAT .XXXXXXXXXX X'S = SUBSET NME
         DS    CL1                 BLANK
SUBDATE  DS    CL8                 DATE ATTACHED IN FORM MM/DD/YY
         DS    CL2                 BLANKS
SUBSTMT  DS    CL5                 # OF STMTS IN SUBSET
         SPACE 2
**
* SPECIAL RETURN CODES FROM EACH CALL TO PAM WILL BE RETNED IN ACTION
*   2    NAME NOT ALPHANUMERIC (SAME AS PV002)
*   4    ++INCLUDE NOT FOUND (PREAD ONLY)
*   7    PARAMETER TO LONG (SAME AS PV007)
*   23   NAME NOT FOUND (SAME AS PV023)
*   245  PARAMETER LIST ERROR IN CALL TO PAM
**
NOENTRY  DC    CL92'NO-ENTRY'            NO-ENTRY FOR PANVALET
BLANKS   EQU   NOENTRY+8,80              BLANKS FOR US
         EJECT
*.....................................................................*
*  OVERLAY MAPPING OF DIRECTORY LINE FORMAT ON SCREEN                 *
*.....................................................................*
         SPACE
MDIR     DS    0CL80     MAP FOR NICE FORMATTED DIR ENTRY
MDNAME   DS    CL10,CL1
SCANSTRT EQU   *
MDLEVEL  DS    CL3,CL1
MDTYPE   DS    CL5,CL1
MDUSER   DS    CL4,CL1
MDSECURE DS    CL1,CL1
MDSTATUS DS    CL3,CL1
MDMAINT  DS    CL8,CL1
MDACCES  DS    CL8,CL1
MDSTMTS  DS    CL8,CL2
MDBLOCKS DS    CL5,CL1
SCANLEN  EQU   *-SCANSTRT
MDLSTPRD DS    CL1
MDLSTACT DS    CL8
CAPSONLY DC    CL64' '
         DC    CL10' ',C'.<(+|&&',CL9' ',C'!$*);-/'
         DC    CL9' ',C',%_>?',CL10' ',C':#@''="'
         DC    CL16' ABCDEFGHI      '
         DC    CL16' JKLMNOPQR      '
         DC    CL16'  STUVWXYZ      '
         DC    CL16'                '
         DC    CL16' ABCDEFGHI      '
         DC    CL16' JKLMNOPQR      '
         DC    CL16'  STUVWXYZ      '
         DC    CL16'0123456789      '
         TITLE 'PLF - DCB S'
         PRINT NOGEN
PANLIST  DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(GM,PM),                 X
               LRECL=133,BLKSIZE=13300,RECFM=FBA
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=(GM,PM)
PANDATA  DCB   DDNAME=PANDATA,DSORG=PS,MACRF=(GM,PM)
SYSUT1   DCB   DDNAME=SYSUT1,MACRF=GM,DSORG=PS,EODAD=PPRTEND
SYSUT2   DCB   DDNAME=SYSUT2,MACRF=PM,DSORG=PS
*
         TITLE 'PLF - DISPLAY FIELDS'
***********************************************************************
*                                                                     *
*        ISPF DISPLAY MODULE CONSTANTS AND WORK AREAS                 *
*                                                                     *
***********************************************************************
*
ISPFWORK DS    F
ISPFSIZE DS    F
ISPFNUMB DS    F
*
ZSCROLLN DS    F
ZSCROLLA DS    CL4
ZVERB    DS    CL8
*
AREATYP  DS    CL8
COLS     DS    F
ROWS     DS    F
TLROW    DS    F
TLCOL    DS    F
*
FWORD    DS    F
DWORD    DS    D
*
COMMTST  DC    CL30'* '
*
*
LINEPTR  DS    F
         LTORG
PLFDATA  DS    XL3200
XFERMEMS DS    XL2400
         DC    X'EEEEEEEE'
         TITLE 'PLF - IBM DSECTS'
         IEFZB4D0
         EJECT
         IEFZB4D2
         PRINT GEN
         TITLE 'CALLABLE BUBBLE SORT                           '
***********************************************************************
*                                                                     *
*             INPUT = REG1 POINTS TO A LIST OF FOUR-BYTE ADRESSES AS  *
*                     FOLLOWS:                                        *
*                     1. KEY LENGTH                                   *
*                     2. KEY POSITION WITHIN THE RECORD, REL 0        *
*                     3. RECORD LENGTH - MAXIMUM IS 256               *
*                     4. NUMBER OF RECORDS IN THE PASSED LIST         *
*                     5. ADDRESS OF THE START OF THE LIST             *
*                                                                     *
***********************************************************************
CORESORT CSECT
         USING *,15
         STM   R14,R12,12(R13)
*
* NOTE THAT NO SAVE_AREA WAS GETMAINED SINCE THIS ROUTINE
*      CALLS NO OTHER ROUTINE OR ISSUES ANY SVC'S
*
         LM    R3,R7,0(R1)          LOAD INPUT PARM LIST ADDRESSES
         L     R3,0(,3)       SET R3 = KEY LENGTH
         LA    R3,0(,3)                MAKE SURE IT'S POSITIVE
         L     R4,0(,4)       SET R4 = KEY POSITION
         LA    R4,0(,4)                MAKE SURE IT'S POSITIVE
         BCTR  R4,0                    KEY POSITION RELATIVE ZERO
         L     R5,0(,5)       SET R5 = RECORD LENGTH
         LA    R5,0(,5)                MAKE SURE IT'S POSITIVE
         BCTR  R5,0                    RECORD LENGTH RELATIVE ZERO
         L     R6,0(,6)       SET R6 = NUMBER OF RECORDS
         LA    R6,0(,6)                MAKE SURE IT'S POSITIVE
*
*                     R7 = ADDRESS OF THE START OF THE LIST OF RECORDS
*
*   FIRST DO SOME EDITING
         C     R5,F255                 IS IT MORE THAN 256
         BH    BADLRECL                YES, MUST BE NO GOOD
         LA    R8,0(R3,R4)  ADD KEY LENGTH + KEY POSITION
         CR    R8,R5       IS TOTAL GREATER THAN LRECL
         BH    BADKEY                  YES, MUST BE NO GOOD
         BCTR  R3,0                    KEY LENGTH RELATIVE ZERO
         C     R6,F2       DO I HAVE ANY RECORDS??????
         BL    ALLDONE     NOPE
         B     STARTUP
F255     DC    F'255'
F2       DC    F'2'
F1       DC    F'2'
KEYCLC   CLC   0(1,R1),0(R2) COMPARE
OR1      XC    0(1,R1),0(R2)     OR 1
OR2      XC    0(1,R2),0(R1)     OR 2
OR3      XC    0(1,R1),0(R2)     OR 3
*
STARTUP  DS    0H
         LR    R8,R6   SET NUMBER OF ENTRIES FOR BCT LOOP
         SR    R9,9    CLEAR "GOT ONE" SWITCH
         LA    R10,1(R7,R5) SECOND RECORD IN LIST (NOTE R5 IS REL 0)
         LA    R1,0(R7,R4) SET R1 = KEY POSITION IN REC 1 (R4 IS REL 0)
         LA    R2,0(R10,R4)  SET R2 = KEY POSITION IN REC 2
CLCLOOP  DS  0H
         EX    R3,KEYCLC     COMPARE
         BNH   CLCBUMP
DOMOVE   DS  0H
         SR    R1,R4  SET R1 = START OF REC 1
         SR    R2,R4  SET R2 = START OF REC 2
         EX    R5,OR1
         EX    R5,OR2
         EX    R5,OR3
         LA    R1,0(1,R4)  SET R1 = START OF REC1 KEY
         LA    R2,0(2,R4)  SET R2 = START OF REC2 KEY
         LA    R9,1(,9)   ADD 1 TO INDICATE A MOVE WAS DONE
CLCBUMP  DS  0H
         LA    R1,1(R1,R5)  SET R1 = KEY POSITION IN REC 1
         LA    R2,1(R2,R5)  SET R2 = KEY POSITION IN REC 2
*        DC    H'0'
         BCT   R8,CLCLOOP
*
         C     R9,F2       IF NUMBER OF MOVES IS LESS THAN 2
         BL    ALLDONE      I MUST BE DONE
         B     STARTUP     ELSE GO LOOP AGAIN
*
ALLDONE  DS  0H
         LM    R14,R12,12(R13) RESTORE REGS
         SR    15,15  ALL OK
         BR    R14
*
BADLRECL DS    0H
         LM    R14,R12,12(R13) RESTORE REGS
         LA    15,4          LRECL > 256
         BR    R14
BADKEY   DS    0H
         LM    R14,R12,12(R13) RESTORE REGS
         LA    15,8      KEY LENGTH + KEY POSITION > LRECL
         BR    R14
*
         END
/*
//LKED1    EXEC PGM=IEWL,PARM=(XREF,LET,LIST,TERM,REUS),
//            COND=(2,LT,ASM1),REGION=1024K
//SYSLIN   DD   DSN=&&OBJSET,DISP=(OLD,DELETE)
//         DD   DDNAME=SYSIN
//SYSLIB   DD   DSN=SYS2.LINKLIB,DISP=SHR        <== PAM
//         DD   DISP=SHR,DSN=ISP.V2R2M0.ISPLOAD  <== ISPLINK
//*YSLMOD  DD   DISP=SHR,DSN=SYS2.LINKLIB        <== TSO CMDLIB
//SYSLMOD  DD   DISP=SHR,DSN=ASQCC.TEST.STEPLIB  <== TSO CMDLIB
//SYSUT1   DD   DSN=&&SYSUT1,UNIT=SYSDA,SPACE=(1024,(50,20))
//SYSTERM  DD   SYSOUT=*
//SYSPRINT DD   SYSOUT=*
//SYSIN    DD   *
  INCLUDE SYSLIB(ISPLINK)
  INCLUDE SYSLIB(PAM)
  NAME PLF3(R)
/*
