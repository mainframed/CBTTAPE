//ASQCCVDF  JOB (X0041,QCC,ASQCC),'X-BRIAN COOK',
//   MSGCLASS=X,
//   MSGLEVEL=(1,1),CLASS=X,NOTIFY=ASQCC
/*JOBPARM L=99
//* *****************************************************************
//* *****************************************************************
//*                                                               ***
//* TECH.SERV.SOURCE(VDF)                                         ***
//*                                                               ***
//* *****************************************************************
//* *****************************************************************
//*
//JOBLIB   DD  DSN=SYS1.LINKLIB,DISP=SHR,
//         UNIT=SYSDA,VOL=SER=MVSTGT
//*
//ASM1    EXEC PGM=IEV90,REGION=2048K,
//        PARM='OBJECT,NODECK,TERM,XREF(SHORT)'
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR,
//         UNIT=SYSDA,VOL=SER=MVSTGT
//         DD  DSN=SYS1.AMODGEN,DISP=SHR,
//         UNIT=SYSDA,VOL=SER=MVSDLB
//         DD  DSN=SYS2.MACLIB,DISP=SHR
//SYSUT1   DD  SPACE=(CYL,(5,5)),
//             UNIT=SYSDA
//SYSTERM  DD  SYSOUT=*
//SYSPRINT DD  SYSOUT=*
//SYSPUNCH DD  DUMMY
//SYSLIN   DD   DSN=&&OBJSET,UNIT=SYSDA,SPACE=(CYL,(5,5)),
//             DISP=(MOD,PASS)
//SYSIN    DD  *
VDF       TITLE 'CHANGE LOG'
***********************************************************************
*                                                                     *
* CHANGE LOG                                                          *
*                                                                     *
*  9/05/86 BRIAN COOK    IF RESERVED, INDICATE WHY IN DETAIL LINE.    *
*                                                                     *
*  4/21/86 BRIAN COOK    "S" OPTION FOR VTOC GIVES 3.4 ISPF SCREEN    *
*                                                                     *
*  7/16/85 BRIAN COOK    ADDED SIO COUNT DISPLAY                      *
*                                                                     *
*                                                                     *
***********************************************************************
          TITLE 'VTOC DISPLAY FACILITY'
          PRINT OFF
          COPY  SPFMACS
          PRINT ON
          PRINT NOGEN
***********************************************************************
*                                                                     *
*                                                                     *
*                    VTOC DISPLAY FACILITY (VDF)                      *
*                                                                     *
*                                                                     *
* ABSTRACT: DISPLAY ONLINE DISK VTOC'S AND DATASETS.                  *
*                                                                     *
*                                                                     *
* FUNCTION: VDF IS DESIGNED TO OPERATE UNDER ISPF USING DISPLAY       *
*           MANAGEMENT SERVICES. IT PROVIDES A DISPLAY OF THE         *
*           ACTIVITY IN THE SYSTEM, SIMILAR TO RMF, BUT SINCE IT      *
*           RUNS UNDER ISPF, IT CAN BE INVOKED WITH SPLIT-SCREEN.     *
*                                                                     *
*           VDF LOOKS AT VARIOUS MVS SP 1.3.0 CONTROL BLOCKS.         *
*                                                                     *
*                                                                     *
* EXTERNAL REFERENCE: ISPLINK                                         *
*                                                                     *
*                                                                     *
* MACROS USED: SAVE                                                   *
*              ABEND                                                  *
*              RETURN                                                 *
*              GETMAIN                                                *
*              STAX                                                   *
*              ENQ                                                    *
*              DEQ                                                    *
*              TPUT                                                   *
*              FREEMAIN                                               *
*              $JQE                                                   *
*              $JOE                                                   *
*              CVT                                                    *
*              CVTUSERS (USER)                                        *
*              TCBWORK  (USER)                                        *
*                                                                     *
*                                                                     *
* ATTRIBUTES:                                                         *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*
* NOTE THE FOLLOWING ENTRY POINT LOGIC IS NON-STANDARD. IN PARTICULAR,
*      IT WILL NOT WORK WITH CALLED PL/I PROGRAMS.
*
***********************************************************************
VDF      CSECT
         USING *,13,12,11,10
         B     72(,15)
         DC    17F'0'
         STM   14,12,12(13)   SAVE CALLING PGM'S REGISTERS
         LA    R2,0(,R15)     LOAD EPA IN R2 FOR WORK REGISTER
         ST    R2,8(R13)      LINK SAVE AREAS
         ST    R13,4(R2)      SAVE THE CALLER'S R13
*
SETBASES DS    0H
         LR    R13,R2         SET SAVE AREA AND BASE 1
         LR    R12,R2         SET
         A     R12,L4096          BASE 2
         LR    R11,R12        SET
         A     R11,L4096          BASE 3
         LR    R10,R11        SET
         A     R10,L4096          BASE 4
*
INITFLDS DS    0H
         MVI   ATABFL,C'0'    NO ACTIVE TABLE
         MVI   TABAREA,C' '   CLEAR TAB ENTRY AREA
         MVC   TABAREA+1(79),TABAREA
         XC    ISPARMS(40),ISPARMS
         MVC   VOLPARM(3),=C'DEF'
*
SHOWAGIN DS    0H
         B     SHOWDASD    DISPLAY THE VTOC'S
*
SHOWDONE DS    0H
*                                  DELETE THE TABLE VARIABLES
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               VTOLINEP),                                              X
               VL,MF=(E,ISPARMS)
*
*                                  DELETE THE TABLE VARIABLES
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               VSLP),                                                  X
               VL,MF=(E,ISPARMS)
*
*                                  DELETE THE TABLE ITSELF
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBEND,                                                 X
               VDFTABLE),                                              X
               VL,MF=(E,ISPARMS)
         MVI   ATABFL,C'0'        NO ACTIVE TABLE
*
         L     R13,4(R13)     RESTORE THE CALLER'S R13
         LM    14,12,12(13)   RETURN TO OUR CALLER
         SR    15,15          SET RC=0
         BR    14             AND RETURN
*
L4096    DC    F'4096'
*
         EJECT
SHOWDASD DS    0H
*
         MVC   SELCODE(3),SPACES  SET SELECTION CODE TO SPACES
*
         CLI   ATABFL,C'0'        DO I HAVE AN ACTIVE TABLE?
         BE    ANOTAB             NOPE
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               VTOLINEP),                                              X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               VCMD),                                                  X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               VSLP),                                                  X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBEND,                                                 X
               VDFTABLE),                                              X
               VL,MF=(E,ISPARMS)
         MVI   ATABFL,C'0'        NO ACTIVE TABLE
ANOTAB   DS    0H
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               VTOLINEP,TABAREA,CHAR,L77),                             X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               VSLP,SELCODE,CHAR,L3),                                  X
               VL,MF=(E,ISPARMS)
*
         VDEFINE ZDLVOL,6
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               VCMD,CMDDATA,CHAR,L40),                                 X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBCREATE,                                              X
               VDFTABLE,                                               X
               NULLENT,                                                X
               DANAMLST,                                               X
               NOWRITE),                                               X
               VL,MF=(E,ISPARMS)
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBVCLEAR,                                              X
               VDFTABLE),                                              X
               VL,MF=(E,ISPARMS)
         MVI   ATABFL,C'1'        AN ACTIVE TABLE
*
*
CHECKUCB DS    0H
*
*    GET INFORMATION FROM UCB.
*                                      SETUP FOR UCBSCAN SERVICE
         USING PSA,0
*
         XC    WORKAREA,WORKAREA       CLEAR REQUIRED WORK AREA
         MVI   DEVCLASS,UCB3DACC       INDICATE DASD UCB SEARCH ONLY
*
         OI    PARMUCB,X'80'           INDICATE END OF PARMLIST
*
SEARCH   DS    0H
*
*                              INTERFACE:
*                                 R1 = ADDRESS OF PARMLIST
*                                 R13= ADDRESS OF AN 18-WORD SAVE AREA
*                                 R14= RETURN ADDRESS
*                                 R15= SCAN SERVICE ENTRY POINT ADDRESS
*
         L     R1,FLCCVT               GET CVTADDRESS
         USING CVTMAP,1                SETUP ADDRESSABILITY FOR CVT
*
         L     R15,CVTUCBSC           GET SCAN SERVICE ADDRESS-IOSVSUCB
         DROP  1
         LA    R1,PARMLIST             PUT PARMLIST IN REGISTER 1
         BALR  R14,R15                GO TO SCAN SERVICE ROUTINE
*
*
         LTR   R15,R15                 HAS A UCB BEEN RETURNED?
         BNZ   SHOWVTAB                NO, END OF DEVICE CLASS SEARCH
*
         B     GOTUCB                  YES, GO PROCESS
*
PARMLIST DS    0F                   PARMLIST MAPPING FOR SCAN SERVICE
*
PARMWA   DC    A(WORKAREA)          ADDRESS OF 100 BYTE WORKAREA
PARMDEVT DC    A(DEVCLASS)          ADDRESS OF BYTE CONTAINING DEVICE
PARMUCB  DC    A(ADDRUCB)           ADDRESS OF WORD CONTAINING UCB ADDR
*                                   TYPE TO BE SEARCHED.
*
DEVCLASS DS    CL1                  BYTE USED FOR DEVICE CLASS TO BE
*                                   SEARCHED
ADDRUCB  DS    F                    WORD IN WHICH UCB SCAN WILL PLACE
*                                   THE ADDR OF UCBS.
         DS    0D                   THE WORKAREA FOR SCAN SERVICE MUST
*                                   BE ON A DOUBLE WORD BOUNDARY
WORKAREA DS    CL100
*
ENDUCB   DS    0H
*
*        IHAPSA
*        IEFUCBOB  LIST=NO
*        CVT    DSECT=YES,LIST=NO
*
         EJECT
*
         TITLE 'EXTRACT UCB INFORMATION'
*
GOTUCB   DS    0H
*
** FOLLOWING UCB ADDRESABILITY LOGIC IS USED FOR SP1
*
*        L     R8,ADDRUCB              GET UCB ADDRESS THAT SCAN
*        USING UCBCMSEG,8              SETUP ADDRESSABILITY TO  UCB
*
** FOLLOWING UCB ADDRESABILITY LOGIC IS USED FOR XA
*
         L     R8,ADDRUCB              GET UCB ADDRESS THAT SCAN
         LA    R1,UCBPRFX
         SR    R8,R1
         USING UCBBGN,8                SETUP ADDRESSABILITY TO  UCB
*
         CLC   VOLPARM(3),=C'OFF'           INCLUDE OFFLINE?
         BE    USEUCB
*
         TM    UCBSTAT,UCBONLI              ONLINE?
         BNO   SEARCH                       NO, IGNORE DEVICE
         CLC   UCBTBYT3(2),=X'2005'         DATA CELL?
         BE    SEARCH                       IF SO SKIP IT
         TM    UCBVOLI,X'FF'                ANY VOLUME MOUNTED?
         BZ    SEARCH                       NO, IGNORE IT
*
         CLC   VOLPARM(3),=C'DEF'           DEFAULT?
         BE    DEFTST
*
         CLC   VOLPARM(3),=C'ALL'           SHOW ALL?
         BE    USEUCB
*
         CLC   VOLPARM(3),UCBVOLI           THIS ONE SELECTED
         BE    USEUCB
*
         CLC   VOLPARM(3),=C'SYS'           SYSTEM PACK
         BNE   SEARCH
*
         CLC   UCBVOLI(3),=C'TST'           SYS  PACK
         BE    USEUCB
         CLC   UCBVOLI(3),=C'MVS'           SYS  PACK
         BE    USEUCB
         CLC   UCBVOLI(3),=C'SPO'           SYS  PACK
         BE    USEUCB
*
         B     SEARCH
*
DEFTST   DS    0H
         CLC   UCBVOLI(2),=C'VM'            VM PACK?
         BE    SEARCH
*
         CLC   UCBVOLI(5),=C'SPARE'         SPARE PACK?
         BE    SEARCH
*
USEUCB   DS    0H
*
*        ICM   R1,15,UCBEXTPT               POINT TO UCB EXTENSION
*        USING UCBCMEXT,1
*        MVC   SIOCOUNT+2(2),UCBMFCNT       COPY SIO COUNT
*        DROP  1
*
         MVC   ZDLVOL,UCBVOLI               PRINT VOLUME NAME
         MVC   UNITADDR,UCBNAME             GET UNIT ADDRESS
*
         MVC   UNITTYPE(4),=C'3350'
         CLI   UCBUNTYP,X'0B'     IS IT 3350
         BE    TSTACT             YUP
*
         MVC   UNITTYPE(4),=C'3380'
         CLI   UCBUNTYP,X'0E'     IS IT 3380
         BE    TSTACT             YUP
*
         MVC   UNITTYPE(4),=C'3330'
         CLI   UCBUNTYP,X'09'     IS IT 3330
         BE    TSTACT             YUP
*
         MVC   UNITTYPE(4),=C'3331'
         CLI   UCBUNTYP,X'0D'     IS IT 3330 MOD 11
         BE    TSTACT             YUP
*
         MVC   UNITTYPE(4),=C'????'
TSTACT   DS    0H
*
         MVC   USTATUS(4),=C'OFFL'
         TM    UCBVOLI,X'FF'                ANY VOLUME MOUNTED?
         BZ    ADD1LINE                     NO  JUST DISPLAY IT
*
         MVC   USTATUS(4),=C'ACT '
*
         TM    UCBSTAT,UCBALOC    IS IT ALLOCATED
         BO    TSTTYPE            YUP
*
         MVC   USTATUS(4),=C'RESV'
*
         TM    UCBSTAT,UCBRESV    IS IT RESERVED
         BO    TSTTYPE            YUP
*
         MVC   USTATUS(4),=C'OFFP'
*
         TM    UCBSTAT,UCBUNLD    IS IT OFFLINE-PENDING
         BO    TSTTYPE            YUP
*
         MVC   USTATUS(4),=C'ONLN'
*
TSTTYPE  DS    0H
         MVC   USAGE(4),=C'PRIV'
*
         TM    UCBSTAB,UCBBPRV    IS IT A PRIVATE VOLUME
         BO    SETUSERS           YUP
*
         MVC   USAGE(4),=C'STOR'
*
         TM    UCBSTAB,UCBBSTR    IS IT A STORAGE VOLUME
         BO    SETUSERS           YUP
*
         MVC   USAGE(4),=C'PUB '
*
SETUSERS DS    0H
         SR    R1,R1                        CLEAR A REG
         IC    R1,UCBUSER                   GET USE COUNT
         CVD   R1,DBLW                      CONVERT TO PRINT
         UNPK  USECNT,DBLW+6(2)             PLACE IT ON THE LINE
         OI    USECNT+2,X'F0'               FIX SIGN BITS
*
** FOLLOWING FLAGS ARE USED FOR SP1
*
*        TM    UCBFLA,UCBSAP      IS IT IN USE BY STANDALONE PROCESS
*        BO    SETRSVD            YES, SET MSG  AND SKIP LSPACE
*        TM    UCBFLB,UCBRESVH    IS IT RESERVED
*        BO    SETRSVD            YES, SET MSG  AND SKIP LSPACE
*
** FOLLOWING FLAGS ARE USED FOR XA
*
         TM    UCBIOSF1,UCBRESVH  IS IT RESERVED
         BO    SETRSVD            YES, SET MSG  AND SKIP LSPACE
         TM    UCBIOSF1,UCBRESVP  IS A RESERVE PENDING
         BO    SETRSVD            YES, SET MSG  AND SKIP LSPACE
         TM    UCBIOSF1,UCBRRP    IS A RESERVE/RELEASE PENDING
         BO    SETRSVD            YES, SET MSG  AND SKIP LSPACE
*        TM    UCBIOSF1,UCBDPVAL  IS PATH VALIDATION REQUIRED
*        BO    SETRSVD            YES, SET MSG  AND SKIP LSPACE
         TM    UCBFL5,UCBNALOC    IS IT IN USE BY ICKDSF
         BZ    DOLSPACE           NO, GO DO LSPACE
*
SETRSVD  DS    0H
         LA    R1,TOTCYL
         MVI   0(R1),3            HIGH-LIGHT
         LA    R1,1(,R1)
*
** FOLLOWING FLAGS ARE DISPLAYED FOR SP1
*
*        TM    UCBFLA,UCBSAP      IS IT IN USE BY STANDALONE PROCESS
*        BZ    *+14                NO
*        MVC   0(3,R1),=C'SAP'    SET MSG
*        LA    R1,4(,R1)
*        TM    UCBFLB,UCBRESVH    IS IT RESERVED
*        BZ    ADD1LINE           NO
*        MVC   0(5,R1),=C'RESVH'  SET MSG
*        LA    R1,6(,R1)
*        B     ADD1LINE           NO
*
** FOLLOWING FLAGS ARE DISPLAYED FOR XA
*
         TM    UCBIOSF1,UCBRESVH  IS IT RESERVED
         BZ    *+14               NO
         MVC   0(5,R1),=C'RESVH'  SET MSG
         LA    R1,6(,R1)
         TM    UCBIOSF1,UCBRESVP  IS A RESERVE PENDING
         BZ    *+14               NO
         MVC   0(5,R1),=C'RESVP'  SET MSG
         LA    R1,6(,R1)
         TM    UCBIOSF1,UCBRRP    IS A RESERVE/RELEASE PENDING
         BZ    *+14               NO
         MVC   0(3,R1),=C'RRP'    SET MSG
         LA    R1,4(,R1)
         TM    UCBIOSF1,UCBDPVAL  IS PATH VALIDATION REQUIRED
         BZ    *+14               NO
         MVC   0(5,R1),=C'DPVAL'  SET MSG
         LA    R1,6(,R1)
         TM    UCBFL5,UCBNALOC    IS IT IN USE BY ICKDSF
         BZ    ADD1LINE           NO
         MVC   0(5,R1),=C'NALOC'  SET MSG
         B     ADD1LINE
*
         TITLE 'EXTRACT SPACE INFORMATION USING LSPACE SVC'
DOLSPACE DS    0H
         LR    R0,R8                        POINT TO UCB
         LA    R1,LSPACE                    MONITOR SPACE MESSAGE AREA
*  R1 -> MONITOR SPACE MESSAGE AREA
*  R0 -> UCB, HIGH ORDER BYTE INDICATES NO SMF RECORD (TYPE 19)
         SVC   78                           GET SPACE INFO
         LTR   R15,R15                      DID IT WORK?
         BZ    SPCOK                        YES
         B     SEARCH                       NOW GO ON
*
*  LSPACE RETURNS ITS INFORMATION IN THE FORM:
*        SPACE=CCCC,TTTT,AAAA/EEEE,KKKK
*  WHERE
*        CCCC = TOTAL FREE CYLINDERS
*        TTTT = TOTAL FREE TRACKS
*        AAAA = TOTAL FREE AREAS
*        EEEE = NUMBER OF CYLINDERS IN LARGEST FREE EXTENT
*        KKKK = NUMBER OF TRACKS IN LARGEST FREE EXTENT
*
SPCOK    DS    0H
         MVC   TOTCYL,CYLTOTAL+1            CYL (TOTAL)
         MVC   TOTTRK,TRKTOTAL+1            TRK (TOTAL)
         MVC   TOTAREA,AREATOTL+1           NUM (OF AREAS)
         MVC   LARGECYL,CYLEXT+1            CYL (LARGEST)
         MVC   LARGETRK,TRKEXT+1            TRK (LARGEST)
*
*        L     R15,SIOCOUNT                 UCB SIO COUNT
*        CVD   R15,DBLW
*        MVC   SIOS(6),EDIT6
*        ED    SIOS,DBLW+5
*
*
* PRTLINE IS FORMATTED WITH THE VOL DISPLAY LINE
*
ADD1LINE DS    0H
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VPUT,                                                  X
               DANAMLST),                                              X
               VL,MF=(E,ISPARMS)
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBADD,                                                 X
               VDFTABLE),                                              X
               VL,MF=(E,ISPARMS)
         MVI   TABAREA,C' '
         MVC   TABAREA+1(79),TABAREA
*
         B     SEARCH                       NOW GO ON
         TITLE 'VDF-SHOW VTOC TABLE'
*
***********************************************************************
*
*   NOW SHOW THE ISPPLIB FORMAT AND THE TABLE ON THE SCREEN
*
***********************************************************************
*
*
SHOWVTAB DS    0H
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBTOP,                                                 X
               VDFTABLE),                                              X
               VL,MF=(E,ISPARMS)
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBDISPL,                                               X
               VDFTABLE,                                               X
               VDFDATAB),                                              X
               VL,MF=(E,ISPARMS)
*
***********************************************************************
*
*   DETERMINE WHETHER ANYTHING WAS ENTERED ON THE SCREEN
*
***********************************************************************
*
         C     R15,FZEROS     COMMAND ENTERED/LINE MODIFIED??
         BNE   SHOWDONE       NOPE
GOTSTUFF DS    0H
         MVC   SELSAVE(3),SELCODE    SAVE SEL CODE
         OC    SELSAVE(3),SPACES   UPPER-CASE IT
         MVC   SELCODE(3),SPACES  SET SELECTION CODE TO SPACES
*
         CLI   CMDDATA,C' '   IF NOT SPACES
         BNE   GOTCMD         THEN A COMMAND WAS ENTERED
*
* IF THE CRP IS NOT ZERO, THEN A LINE SELECTION CODE MUST HAVE BEEN
* ENTERED. I HAVE THE CURRENT LINE, SO EDIT THE SELECTION CODE.
*
         CLI   SELSAVE,C'S'   IS IT A "SHOW VTOC" REQUEST
         BE    SHOWVTOC       GO DO IT
*
* ADD TESTS FOR OTHER LINE SELECTION CODES HERE
*
         B     SHOWAGIN       GO REPEAT THE DISPLAY
         EJECT
SHOWVTOC DS    0H
*
         VPUT  ZDLVOL
         CONTROL DISPLAY,SAVE
*
         PRINT GEN
         SELECT PGM(ISRUDL)
         PRINT NOGEN
*
*        LA    R1,VTOCPARM
*        LINK  EP=VDFVTOC
*
         CONTROL DISPLAY,RESTORE
         CONTROL DISPLAY,REFRESH
*
         B     SHOWAGIN       GO REPEAT THE DISPLAY
*
AH0      DC    X'80',AL3(H0)
H0       DC    H'0'
         EJECT
*
*    ONLY COMMAND SUPPORTED IS "REPEAT"
*
GOTCMD   DS    0H
         TR    CMDDATA,CAPSONLY  UPPER-CASE ALL INPUT
*
*  NOTE THE TR TABLE IS SET UP TO DELETE SEMI-COLONS, IN ORDER TO
*       PREVENT COMMAND CONCATENATION.
*
         CLI   CMDDATA,C' '   IF COMMAND AREA IS SPACES
         BE    SHOWAGIN       THEN A COMMAND WAS NOT ENTERED
*
         CLC   CMDDATA(2),=C'V='  IS IT VOLUME SELECTION?
         BE    SETNVOL            RESET DEFAULT
*
         B     SHOWAGIN       GO REPEAT THE SCREEN
SETNVOL  DS    0H
         MVC   VOLPARM(3),CMDDATA+2
*
         B     SHOWAGIN       GO REPEAT THE SCREEN
         EJECT
         TITLE 'MISCELLANEOUS CONSTANTS'
         PRINT NOGEN
         LTORG
*
         DS    0F
EPNAME   DC    CL8' '
ZEROFF   DC    X'000000FF'
P1       DC    PL1'+1'
P3ZERO   DC    PL3'+0'
VDF7HOLD DS    F
OUT7HOLD DS    F
*
VOLPARM  DC    CL3'   '
*
DIGITS3  DC    X'202020'
THREEPT  DC    X'2020204B'
EDIT4    DC    X'40202120'
EDIT6    DC    X'402020202121'
EDIT8    DC    X'4020202020202120'
*
JOBN     DC    C'JOB'
TSU      DC    C'TSU'
STC      DC    C'STC'
CTAB     DS    0H
ISPLINK  DC    V(ISPLINK)
VDFOPTP  DC    A(SELECTR)
         DC    X'80',AL3(VDFOPT)
SELECTR  DC    CL8'SELECT  '
VDFOPT   DC    CL8'VDFOPT  '
CMDDATA  DC    CL40'       '
*
L78      DC    F'78'
FZEROS   DC    F'0'
F8       DC    F'8'
L3       DC    F'3'
L77      DC    F'77'
L40      DC    F'40'
*
SELCODE  DC    CL3' '
SELSAVE  DC    CL3' '
*
H1       DC    H'1'
H2       DC    H'2'
H8       DC    H'8'
*
VTOCPARM DC    A(ZDLVOL)
*
TBADD    DC    C'TBADD    '
TBCREATE DC    C'TBCREATE '
TBDISPL  DC    C'TBDISPL  '
TBEND    DC    C'TBEND    '
TBGET    DC    C'TBGET    '
TBQUERY  DC    C'TBQUERY  '
TBSKIP   DC    C'TBSKIP   '
TBTOP    DC    C'TBTOP    '
TBVCLEAR DC    C'TBVCLEAR '
VDEFINE  DC    C'VDEFINE  '
VDELETE  DC    C'VDELETE  '
VGET     DC    C'VGET     '
VPUT     DC    C'VPUT     '
VREPLACE DC    C'VREPLACE '
*
NOWRITE  DC    C'NOWRITE  '
*
NULLENT  DC    C'()'
*
DANAMLST DC    C'('             DISPLAY ACTIVE NAME LIST
SEL      DC    C'VSLA     '     SELECTION CODE
VTOLINE  DC    C'VTOLINE )'     JOB INFO
VTOLINE2 DC    C'VTOLINE  '     JOB INFO
VTOLINEP DC    C'(VTOLINE )'    JOB INFO
VSLP     DC    C'(VSLA    )'    SELECTION CODE
VCMD     DC    C'(VCMD    )'    SELECTION CODE
*
SPACES   DC    C'        '
CZEROS   DC    CL8'00000000'
*
CHAR     DC    C'CHAR    '
*
VDFDATAB DC    C'VDFDATAB'
VDFTABLE DC    C'VDFTABLE'
*
*
         TITLE 'REGISTER EQUATES'
***********************************************************************
*                                                                     *
*        REGISTER EQUATES                                             *
*                                                                     *
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         TITLE 'WORKING STORAGE AREAS'
DOUBLEWD DS    D              PACKED DECIMAL WORK AREA
TABADDRH DS    A              ADDRESS OF TABLE TO USE FOR OUTMSG
DNMADDRH DS    A              ADDRESS OF NAMES TO USE FOR OUTMSG
*
ISPARMS  DS    10F            PARM LIST FOR ISPLINK
*
*     THE ISP TABLE FLAGS INDICATE NO ACTIVE TABLE (0), OR ONE EXISTS
ATABFL   DS    XL1     DO I HAVE A CURRENT "DISPLAY ACTIVE" TABLE
*
*
         TITLE 'VDF-WORK AREAS      '
         LTORG
*
DBLW     DS    D                           CVD WORK AREA
SIOCOUNT DC    F'0'                        UCB SIO COUNT
*
CAPSONLY DC    CL64' '
         DC    CL10' '
         DC    C'¢.<(+|&&'
         DC    CL9' '
         DC    C'!$*)'
         DC    C' '             NOTE SEMICOLON CHANGED TO BLANK
         DC    C'¬-/'
         DC    CL9' ',C',%_>?',CL10' ',C':#@''="'
         DC    CL16' ABCDEFGHI      '
         DC    CL16' JKLMNOPQR      '
         DC    CL16'  STUVWXYZ      '
         DC    CL16'                '
         DC    CL16' ABCDEFGHI      '
         DC    CL16' JKLMNOPQR      '
         DC    CL16'  STUVWXYZ      '
         DC    CL16'0123456789      '
         TITLE 'VDF-LSPACE WORK AREA'
*
LSPACE   DS    0CL1
SPACEMSG DS    CL6                          'SPACE='
CYLTOTAL DS    CL4                          TOTAL FREE CYLINDERS
COMMA1   DS    CL1
TRKTOTAL DS    CL4                          TOTAL FREE TRACKS
COMMA2   DS    CL1
AREATOTL DS    CL4                          TOTAL FREE AREAS
SLASH    DS    CL1
CYLEXT   DS    CL4                          CYLS IN LARGEST FREE EXTENT
COMMA3   DS    CL1
TRKEXT   DS    CL4                          TRKS IN LARGEST FREE EXTENT
         DC   CL40' '
         TITLE 'VDF-VTOC LINE FORMAT'
VDFHEAD  DC    CL40' NAME  ADR CNT CYL TRK NUM CYL/TRK'
TABAREA  DS    0CL77
PRTLINE  DS   0CL1
ZDLVOL   DS   CL6                           VOLUME NAME
DIRFFLG  DS   C                             DIRF FLAG
UNITADDR DS   CL3                           UNIT ADDRESS
         ORG  PRTLINE+11
         DS   CL1
UNITTYPE DS   CL4
         DS   CL2
USTATUS  DS   CL4
         DS   CL2
USAGE    DS   CL4
         DS   CL3
USECNT   DS   CL3                           # OF CURRENT ALLOCATIONS
         DS   CL3
TOTCYL   DS   CL3                           TOTAL FREE CYLINDERS
         DS   CL3
TOTTRK   DS   CL3                           TOTAL FREE TRACKS
         DS   CL3
TOTAREA  DS   CL3                           TOTAL FREE AREAS
         DS   CL4
LARGECYL DS   CL3                           # CYLS IN LARGEST FREE EXT
         DS   CL2
LARGETRK DS   CL3                           # TRKS IN LARGEST FREE EXT
SIOS     DS   CL6                           SIO COUNT
         DS   XL400
         EJECT
*.....................................................................*
*        MAPPING DSECT FOR ASCB
*.....................................................................*
         IHAASCB
*.....................................................................*
*        MAPPING DSECT FOR SRM USER CONTROL BLOCK
*.....................................................................*
         IRAOUCB
*.....................................................................*
*        MAPPING DSECT FOR DMDT (DOMAIN DESCRIPTOR TABLE)             *
*.....................................................................*
         SPACE
DMDT     DSECT
DMDTNO   DS    XL1             DOMAIN NUMBER
DMDTLO   DS    XL1             MIN  MPL LEVEL
DMDTHI   DS    XL1             MAX  MPL LEVEL
DMDTWT   DS    XL1             WEIGHTING FACTOR
DMDTMPLT DS    XL2             CURRENT MPL TARGET
DMDTRUA  DS    XL2             AVE # OF READY USERS
DMDTGOOU DS    XL2             CURRENT # USERS GOING OUT
DMDTCMPL DS    XL2             CURRENT MPL
DMDTOUTU DS    XL2             CURRENT # USERS ON OUT Q
DMDTINCU DS    XL2             CURRENT # SWAPPABLE INCORE USERS
DMDTRUC  DS    XL4             ACCUM FOR READY USER AVERAGE
DMDTEND  DS    0X              END OF DMT
DMDTLEN  EQU   *-DMDT
         SPACE 2
*
* EQUATES....
*
RMCTDMDT EQU   X'B4'           POINTER TO DOMAIN TABLE
RMCTDMNC EQU   X'BC'           COUNT OF NUMBER OF DOMAINS
RMCTWMST EQU   X'18'           ADDRESS OF WLM SPEC TABLE
         SPACE 2
         PRINT GEN
         TITLE 'COMMUNICATION VECTOR TABLE'
         CVT   DSECT=YES,PREFIX=NO
         TITLE 'UNIT CONTROL BLOCK'
         IEFUCBOB LIST=YES,PREFIX=YES
         TITLE 'DATA EXTENT BLOCK'
         IEZDEB
         TITLE 'FMT4 DSCB'
DSCB4    DSECT
         ORG   DSCB4+44                     GET PAST KEY (44X'04')
         IECSDSL1 (4)
         IHAPSA
         END
/*
//LKED1    EXEC PGM=IEWL,PARM=(XREF,LET,LIST,TERM),
//            COND=(2,LT,ASM1),REGION=1024K
//SYSLIN   DD   DSN=&&OBJSET,DISP=(OLD,DELETE)
//         DD   DDNAME=SYSIN
//SYSLIB   DD   DISP=SHR,DSN=ISP.V2R2M0.ISPLOAD
//SYSLMOD  DD   DISP=SHR,DSN=SYS2.CMDLIB
//*YSLMOD  DD   DISP=SHR,DSN=ASQCC.TEST.STEPLIB
//SYSUT1   DD   DSN=&&SYSUT1,UNIT=SYSDA,SPACE=(1024,(50,20))
//SYSTERM  DD   SYSOUT=*
//SYSPRINT DD   SYSOUT=*
//SYSIN    DD   *
  NAME VDF(R)
/*
