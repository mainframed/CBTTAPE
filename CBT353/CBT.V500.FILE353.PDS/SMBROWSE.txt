         PRINT OFF
         COPY  SPFMACS
         PRINT ON
         PRINT NOGEN
SMBROWSE TITLE 'CHANGE LOG'
***********************************************************************
*                                                                     *
*   CHANGE LOG                                                        *
*                                                                     *
*  11/03/86  BRIAN COOK   ADDED "FUP-" COMMAND, TO SCROLL UP "-" LINES*
*                         AFTER SUCCESSFUL "FIND".                    *
*                                                                     *
*  10/30/86  BRIAN COOK   UPDATE SCROLL AMOUNT BASED ON DIRECTION     *
*                                                                     *
*  10/16/86  BRIAN COOK   UPDATE SCROLL AMOUNT WHEN SCREEN SIZE       *
*                         CHANGES.                                    *
*                                                                     *
*  10/13/86  BRIAN COOK   CHANGED LENGTH OF FIND STRING FROM 49 TO 47.*
*                         ADDED "VL1" AND "HL1" PARAMETERS FOR SCROLL.*
*                                                                     *
*   9/03/86  BRIAN COOK   BUG IN SEARCH FOR QUOTED STRING             *
*                         'MEMBER '                                   *
*                                                                     *
*   9/02/86  BRIAN COOK   FIXED HIGH-LIGHTING "FOUND" STRING          *
*                         ADDED "NEXT" AND "PREV" COMMANDS FOR FIND   *
*                         SEARCH DIRECTION.                           *
*                                                                     *
*   8/29/86  BRIAN COOK   CHANGED TO RE-ENTRANT                       *
*                                                                     *
*   8/25/86  BRIAN COOK   INITIAL START DATE                          *
*                                                                     *
*                                                                     *
***********************************************************************
         TITLE 'SPF MEMORY BROWSE GENERAL DESCRIPTION'
***********************************************************************
*                                                                     *
*                                                                     *
*                      SMBROWSE                                       *
*               (FOR ISPF V2R1M2 5665-319)                            *
*                                                                     *
*                                                                     *
* ABSTRACT: IN-CORE BROWSE UTILITY FOR ISPF APPLICATIONS.             *
*                                                                     *
*                                                                     *
* FUNCTION: DESIGNED TO OPERATE UNDER ISPF USING THE VERSION 2        *
*           DYNAMIC PANEL DISPLAY.                                    *
*                                                                     *
*           IT  WILL NOT WORK WITH ANY PRIOR VERSION OF SPF.          *
*                                                                     *
*           IT IS CALLED WITH A PARAMETER LIST POINTING TO AN AREA    *
*           IN MEMORY THAT CONTAINS RECORDS TO BE DISPLAYED ON THE    *
*           TERMINAL. NO DISK I/O IS PERFORMED.                       *
*                                                                     *
*           ADDITIONAL PARAMETERS SPECIFY THE RECORD LENGTH AND       *
*           NUMBER OF RECORDS.                                        *
*                                                                     *
*                                                                     *
* THE FOLLOWING DIALOG VARIABLES MUST BE DEFINED BY THE CALLING       *
* PROGRAM AND INITIALIZED PRIOR TO INVOKING SMBROWSE:                 *
*                                                                     *
*    1. SMBSTART =  STARTING ADDRESS OF RECORDS                       *
*    2. SMBNUMB  =  NUMBER OF RECORDS                                 *
*    3. SMBLRECL =  RECORD LENGTH                                     *
*    4. SMBTITLE =  TITLE FOR THE DISPLAY (USUALLY DSNAME)            *
*                                                                     *
*                                                                     *
*                                                                     *
* ASSEMBLY PARMS:                                                     *
*                                                                     *
*    'OBJECT,NODECK,TERM,XREF(SHORT),RENT'                            *
*                                                                     *
*                                                                     *
* LINK-EDIT PARMS:                                                    *
*                                                                     *
*    (XREF,LET,LIST,TERM,REUS,RENT)                                   *
*                                                                     *
*                                                                     *
* LINK-EDIT EXTERNAL REFERENCES:                                      *
*                                                                     *
*    ISP.V2R2M0.ISPLOAD, MEMBER ISPLINK                               *
*                                                                     *
*                                                                     *
*                                                                     *
***********************************************************************
***********************************************************************
         TITLE 'INSTALLATION NOTES'
***********************************************************************
*
* 1.  SMB REQUIRES "SYS1.MACLIB" AS A SYSLIB IN THE ASSEMBLY STEP,
*     FOR THE FOLLOWING MACRO'S:
*
*          SAVE/RETURN
*          GETMAIN/FREEMAIN
*          CALL
*
*
* 2.  SMB ALSO REQUIRES MEMBER "SPFMACS" FROM SYS2.MACLIB (OR THE CBT
*     TAPE FILE 353) FOR THE ASSEMBLY.
*
*
* 3.  SMB IS RE-ENTRANT, AND CAN BE PLACED IN LPALIB. IT IS LESS THAN
*     6K, SO THAT'S PROBABLY A GOOD SPOT FOR IT.
*
*
* 4.  BELOW ARE SOME SAMPLE STATEMENTS THAT ARE REQUIRED TO INVOKE
*     SMB (EXAMPLE TAKEN FROM JDF, USING "SPFMACS"):
*
*     A. DEFINITIONS:
*
*        VDEFINE SMBSTART,4,FMT=FIXED
*        VDEFINE SMBNUMB,4,FMT=FIXED
*        VDEFINE SMBLRECL,4,FMT=FIXED
*        VDEFINE SMBTITLE,49
*
*                SMBSTART DS    F     ADDRESS OF 1ST RECORD
*                SMBNUMB  DS    F     NUMBER OF RECORDS
*                SMBLRECL DS    F     LRECL OF RECORDS
*                SMBTITLE DS    CL49  TITLE INFO
*
*
*     B. UPDATE SHARED POOL:
*
*        VPUT  SMBSTART,SMBNUMB,SMBLRECL,SMBTITLE,POOL=SHARED
*
*
*     C. INVOKE SMBROWSE:
*
*        SELECT PGM(SMBROWSE)
*
*
*
* 5.  IF YOU DON'T HAVE ASEMBLER H VERSION 2, COMMENT OUT THE AMODE AND
*     RMODE STATEMENTS. THESE ARE THE ONLY XA DEPENDENCIES.
*
*
* 6.  SMB USES AN ISPF PANEL WITH A DYNAMIC AREA, SO REQUIRES ISPF V2.
*     THE PANELS ARE IN ISPPLIB (OR CBT TAPE FILE 353) AND ARE:
*
*     SMBP1  --  THE DISPLAY PANEL
*     SMBH1  --  THE HELP PANEL
*
*
* 7.  SMB HAS ONE MESSAGE MEMBER, SMB00, WHICH IS IN ISPMLIB (OR CBT
*     TAPE FILE 353).
*
*
***********************************************************************
         TITLE 'PROGRAM INITIALIZATION'
***********************************************************************
*
*
***********************************************************************
SMBROWSE CSECT
*
*
         USING *,12,11,10,9
*
SMBROWSE AMODE 31
SMBROWSE RMODE ANY
*
         SAVE  (14,12),,SMBROWSE_08/25/86_BRIAN COOK_&SYSTIME_&SYSDATE
*
         LR    R12,R15            SET BASE REGISTER
         LA    R9,4095
         LA    R11,1(R9,R12)     SET BASE 2
         LA    R10,1(R9,R11)     SET BASE 3
         LA    R9,1(R9,R10)      SET BASE 4
*
         L     R2,GETMLENG
         GETMAIN R,LV=(2)
*
         ST    R13,4(R1)          STORE BACKWARDS POINTER
         ST    R1,8(R13)          STORE FORWARD POINTER
         LR    R13,R1             DSECT BASE
         USING WORKAREA,13
*
*
*
         TITLE 'MAIN PROCESSING LOOP'
*
         BAL   R7,INITFLDS    INITIALIZATION
*
         BAL   R7,DOBROWSE    BROWSE THE DATA
*
BROWSEOJ DS    0H
*
         BAL   R7,BROWDONE
*
*
*
         LR    R3,R13             START ADDRESS OF GETMAINED AREA
         L     R2,GETMLENG        LENGTH OF GETMAINED AREA
         L     R13,4(,R13)        PICK UP CALLING SAVE AREA
         FREEMAIN R,LV=(2),A=(3)
         LM    R14,R12,12(R13)
         SR    R15,15
         BR    R14
*
*
*
         TITLE 'TERMINATION LOGIC'
BROWDONE DS    0H
*
         ST    R7,DONE7
*
         L     R2,ENT1
         LTR   R2,R2
         BZ    NOPFK1
*
         MVI   PFKWORK,C' '
         MVC   PFKWORK+1(254),PFKWORK
         MVC   PFKWORK(5),=C'RFIND'
*
*
         CALL  ISPLINK,(=CL8'VPUT',                                    X
               (2)),                                                   X
               VL,MF=(E,ISPARMS)
*
NOPFK1   DS    0H
*
*
         L     R2,ENT2
         LTR   R2,R2
         BZ    NOPFK2
*
         MVI   PFKWORK,C' '
         MVC   PFKWORK+1(254),PFKWORK
         MVC   PFKWORK(5),=C'RFIND'
*
*
         CALL  ISPLINK,(=CL8'VPUT',                                    X
               (2)),                                                   X
               VL,MF=(E,ISPARMS)
*
NOPFK2   DS    0H
*
DONEDONE DS    0H
         L     R7,DONE7
         BR    R7             RETURN
*
         TITLE 'INITIALIZATION'
INITFLDS DS    0H
         ST    R7,INIT7
*
*
* INITIAL DISPLAY PROFILE FLAGS
*
*
         MVI   COLSFLAG,0          FLAG OFF
         MVI   HEXFLAG,0           FLAG OFF
         MVI   NEXTFLAG,0          FIND IS NEXT, NOT PREV
*
*
* THESE FIELDS DISPLAYED ON THE BROWSE SCREEN
*
*
         MVC   SMBCMD(40),SPACES
         VDEFINE SMBCMD,40
         MVC   SMBFIND(80),SPACES
         VDEFINE SMBFIND,47
         VDEFINE SMBDATA,3280
         VDEFINE SMBTITLE,49,OPT=COPY
         MVC   SMBLN(5),=C'00001'
         VDEFINE SMBLN,5
         MVC   SMBCL(5),=C'00001'
         VDEFINE SMBCL,5
         MVC   FM(2),=C'CH'
         VDEFINE FM,2
         MVC   FC1(3),=C'001'
         VDEFINE FC1,3
         MVC   FC2(3),=C'999'
         VDEFINE FC2,3
*
*
* THESE FIELDS DEFINED BY ISPF FOR DYNAMIC SCREEN AREAS
*
*
         VDEFINE AREATYP,8
         VDEFINE COLS,4,FMT=FIXED
         VDEFINE ROWS,4,FMT=FIXED
         VDEFINE TLROW,4,FMT=FIXED
         VDEFINE TLCOL,4,FMT=FIXED
         VDEFINE ZVERB,8
         VDEFINE ZSCROLLA,4
         XC    ZSCROLLN(4),ZSCROLLN
         VDEFINE ZSCROLLN,4,FMT=FIXED,OPT=COPY
*
** SET DEFAULT VERTICAL AND HORIZONTAL SCROLL AMOUNTS
*
         PQUERY SMBP1,SMBDATA
*
         MVC   ZSCROLLN(4),ROWS       ELSE SET IT TO TOTAL ROWS
         MVC   OLDROWS(4),ZSCROLLN    SAVE FOR LATER
         L     R14,ZSCROLLN           NUMBER OF ROWS FOR SCROLLING
         LTR   R14,R14                IF SCROLL AMOUNT IS NON-ZERO
         BNZ   *+8                    THEN USE IT
         L     R14,ROWS               ELSE USE TOTAL ROWS
         CVD   R14,DWORD
         OI    DWORD+7,X'0F'
         UNPK  VL1(3),DWORD+6(2)
         VDEFINE VL1,3
         L     R14,COLS               NUMBER OF COLUMNS
         CVD   R14,DWORD              NUMBER OF COLUMNS
         OI    DWORD+7,X'0F'
         UNPK  HL1(3),DWORD+6(2)
         VDEFINE HL1,3
*
* THESE FIELDS DEFINED BY CALLING PROGRAM TO SPECIFY THE BROWSE DATA
*
*
         VDEFINE SMBSTART,4,FMT=FIXED,OPT=COPY
         VDEFINE SMBNUMB,4,FMT=FIXED,OPT=COPY
         VDEFINE SMBLRECL,4,FMT=FIXED,OPT=COPY
*
         NI    SMBSTART,X'7F'
         NI    SMBNUMB,X'7F'
         NI    SMBLRECL,X'7F'
*
         ICM   R15,15,SMBSTART       CHECK FOR PRESENCE OF VARIABLE
         BZ    BADVARS
         ICM   R15,15,SMBNUMB        CHECK FOR PRESENCE OF VARIABLE
         BZ    BADVARS
         ICM   R15,15,SMBLRECL       CHECK FOR PRESENCE OF VARIABLE
         BZ    BADVARS
*
         B     VARSOK
*
BADVARS  DS    0H
*
         SETMSG SMB004
*
         B     BROWSEOJ
*
*        L     R0,SMBSTART
*        L     R1,SMBNUMB
*        L     R2,SMBLRECL
*        LA    R3,SMBTITLE
*        DC    H'0'
*
VARSOK   DS    0H
*
** SET DEFAULT "FIND-AND-SCROLL-UP" AMOUNT
*
         MVC   FUPAMT(4),=F'3'     DEFAULT IS SCROLL UP 3 AFTER "FIND"
         L     R14,SMBLRECL
         MH    R14,FUPAMT+2
         STH   R14,FUPAMT+2
*
         L     R4,SMBNUMB       NUMBER OF LINES
         LA    R4,1(,R4)        BUMP 1 TO GO PAST LAST LINE
         MH    R4,SMBLRECL+2    TIMES LENGTH
         A     R4,SMBSTART      POINT TO LAST LINE
         ST    R4,BRLAST        LAST LINE
*
         CONTROL ERRORS,RETURN
*
         XC    ENT1(8),ENT1     CLEAR PFK ADDRESS POINTERS
         LA    R2,PFTABLE       POINT TO FIRST PF KEY
*
SCANPFK1 DS    0H
*
         CLI   0(R2),X'FF'      END OF TABLE?
         BE    INITDONE         YES
*
         CALL  ISPLINK,(=CL8'VDEFINE',                                 X
               (2),PFKWORK,=CL8'CHAR',=F'255',                         X
               =CL10'(COPY)'),                                         X
               VL,MF=(E,ISPARMS)
*
         OC    PFKWORK(5),SPACES         UPPER-CASE
         CLC   PFKWORK(5),=C'RFIND'
         BE    PFGOT1
         LA    R2,8(,R2)
         B     SCANPFK1
*
PFGOT1   DS    0H
*
         ST    R2,ENT1
         MVI   PFKWORK,C' '
         MVC   PFKWORK+1(254),PFKWORK
         MVC   PFKWORK(8),=C'>RFIND  '
*
*
         CALL  ISPLINK,(=CL8'VPUT',                                    X
               (2)),                                                   X
               VL,MF=(E,ISPARMS)
*
*
SCANPFK2 DS    0H
*
         CLI   0(R2),X'FF'      END OF TABLE?
         BE    INITDONE         YES
*
         CALL  ISPLINK,(=CL8'VDEFINE',                                 X
               (2),PFKWORK,=CL8'CHAR',=F'255',                         X
               =CL10'(COPY)'),                                         X
               VL,MF=(E,ISPARMS)
*
         OC    PFKWORK(5),SPACES
         CLC   PFKWORK(5),=C'RFIND'
         BE    PFGOT2
         LA    R2,8(,R2)
         B     SCANPFK2
*
PFGOT2   DS    0H
*
         ST    R2,ENT2
         MVI   PFKWORK,C' '
         MVC   PFKWORK+1(254),PFKWORK
         MVC   PFKWORK(8),=C'>RFIND  '
*
*
         CALL  ISPLINK,(=CL8'VPUT',                                    X
               (2)),                                                   X
               VL,MF=(E,ISPARMS)
*
*
INITDONE DS    0H
         L     R7,INIT7
         BR    R7
*
PFTABLE  DC    CL8'(ZPF01)'
         DC    CL8'(ZPF02)'
         DC    CL8'(ZPF03)'
         DC    CL8'(ZPF04)'
         DC    CL8'(ZPF05)'
         DC    CL8'(ZPF06)'
         DC    CL8'(ZPF07)'
         DC    CL8'(ZPF08)'
         DC    CL8'(ZPF09)'
         DC    CL8'(ZPF10)'
         DC    CL8'(ZPF11)'
         DC    CL8'(ZPF12)'
         DC    CL8'(ZPF13)'
         DC    CL8'(ZPF14)'
         DC    CL8'(ZPF15)'
         DC    CL8'(ZPF16)'
         DC    CL8'(ZPF17)'
         DC    CL8'(ZPF18)'
         DC    CL8'(ZPF19)'
         DC    CL8'(ZPF20)'
         DC    CL8'(ZPF21)'
         DC    CL8'(ZPF22)'
         DC    CL8'(ZPF23)'
         DC    CL8'(ZPF24)'
         DC    X'FF'
*
         TITLE 'BROWSE THE RECORDS'
MOVELINE MVC   0(1,R15),0(R1)
TEXTTRT  TR    0(1,R15),ASISTRT   CONVERT OUT NON-TEXT
LITETRT  TR    0(1,R1),LOWTRT     CONVERT OUT INTENSITY
ENDBROWS DS    0H
         L     R7,BROWSE7
         BR    R7
*
DOBROWSE DS    0H
*
         ST    R7,BROWSE7
*
         MVC   BRADDR(12),PARMS SET THE PARMS
         MVC   TOPPTR(4),BRADDR SET TOP LINE POINTER
         MVC   SMBFIND(80),SPACES
         XC    OFFSET(4),OFFSET
*
RESHOW   DS    0H
*
         PQUERY SMBP1,SMBDATA
*
*        L     R14,ROWS               ELSE USE TOTAL ROWS
*        CVD   R14,DWORD
*        OI    DWORD+7,X'0F'
*        UNPK  VL1(3),DWORD+6(2)
*        L     R14,COLS               ELSE USE TOTAL COLS
*        CVD   R14,DWORD
*        OI    DWORD+7,X'0F'
*        UNPK  HL1(3),DWORD+6(2)
*
         L     R14,COLS               NUMBER OF COLUMNS
         MH    R14,ROWS+2             TIMES NUMBER OF ROWS
         ST    R14,ISPFSIZE           GIVES SCREEN AREA
         L     R15,ROWS               LOOP CONTROL
         L     R1,COLS                NUMBER OF COLUMNS
         BCTR  R1,0                   EXECUTED MOVE
*
         LA    R14,SMBDATA
         ST    R14,ISPFWORK
*
         LA    R15,132
         C     R15,COLS
         BE    INIT132
*
         LA    R15,40
*
         MVC   0(80,R14),INITLINE
         LA    R14,80(,R14)
         BCT   R15,*-10
*
         B     RESHOW1
*
INIT132  DS    0H
*
         LA    R15,24
*
         MVC   0(132,R14),INITLINE
         LA    R14,132(,R14)
         BCT   R15,*-10
*
RESHOW1  DS    0H
*
         XC    ISPFNUMB(4),ISPFNUMB INITIALIZE COUNTER
*
         NI    TOPPTR,X'7F'     CLEAR SIGN BIT
         L     R15,TOPPTR       ADDRESS OF FIRST LINE
         C     R15,SMBSTART     AM I TRYING TO START BEFORE TOP
         BH    *+12             NO, JUST CONTINUE
         L     R15,SMBSTART     SET TOP TO START
         ST    R15,TOPPTR       SET TOP TO START
         S     R15,SMBSTART     SUBTRACT START
         LTR   R15,R15
         BZ    RLINE0
         SR    R14,R14
         L     R3,SMBLRECL
         DR    R14,R3
*
RLINE0   DS    0H
*
         LA    R15,1(,R15)      MAKE RELATIVE 1
         CVD   R15,DWORD
         OI    DWORD+7,X'0F'
         UNPK  SMBLN(5),DWORD+5(3)
*
RLEN0    DS    0H
*
         L     R5,SMBLRECL     IF LRECL
         C     R5,COLS            IS LESS THAN OR EQUAL TO COLS
         BNH   RLEN7              THEN USE LRECL FOR LINE LENGTH
*
*                              IF LRECL
         C     R5,OFFSET          IS LESS THAN OFFSET
         BH    *+10
         XC    OFFSET(4),OFFSET   SET OFFSET TO ZERO
*
         L     R5,SMBLRECL     IF LRECL
         S     R5,OFFSET          MINUS OFFSET
         C     R5,COLS            IS GREATER THAN OR EQUAL TO COLS
         BNL   RLEN8              THEN JUST USE COLS FOR LINE LENGTH
*
         L     R5,SMBLRECL     ELSE TAKE LRECL
         S     R5,COLS              MINUS COLS
         ST    R5,OFFSET            AND USE THAT FOR OFFSET
         B     RLEN8                AND USE COLS FOR LINE LENGTH
*
RLEN7    DS    0H
*
         XC    OFFSET(4),OFFSET
         L     R5,SMBLRECL
         B     RLEN9
*
*
RLEN8    DS    0H
*
         L     R5,COLS         R5=LINE LENGTH
RLEN9    DS    0H
         BCTR  R5,0
*
RLENSET  DS    0H
*
         L     R15,OFFSET       COLUMN 1 OF DISPLAT
         LA    R15,1(,R15)      MAKE RELATIVE 1
         CVD   R15,DWORD
         OI    DWORD+7,X'0F'
         UNPK  SMBCL(5),DWORD+5(3)
*
         L     R3,TOPPTR       R3=ADDRESS OF FIRST LINE
*
         L     R4,BRLAST       R4=NUMBER OF LINES
         L     R15,ISPFWORK
         SR    R14,R14
*
         CLI   COLSFLAG,0      COLS?
         BE    RESHOW2         NO
*
         MVC   0(80,R15),COLSDATA
         A     R15,COLS
         LA    R14,1(,R14)
*
RESHOW2  DS    0H
*
         CR    R3,R4          NEXT LINE VS LAST LINE OF DATA
         BNL   SHOWTBLS       IF NEXT LINE IS NOT LOW
*
         L     R1,OFFSET
         AR    R1,R3
         ST    R1,SAVE1            SAVE IT
*
         EX    R5,MOVELINE
*
         EX    R5,TEXTTRT
*
         C     R3,FINDPTR      IS THIS A LINE I HAVE FOUND
         BNE   NOTFNDLN        NO
*
         XC    FINDPTR(4),FINDPTR     ONE-TIME-THROUGH
*
         L     R1,FINDOFF
         LTR   R1,R1                  COL 0
         BZ    NOTFNDLN               YES
         BCTR  R1,0                   BACK UP ONE POSITION
         C     R1,OFFSET              LESS THAN OFFSET
         BNH   NOTFNDLN               YES
*
         S     R1,OFFSET
         AR    R1,R15
         MVI   0(R1),2                HIGH-INTENSITY
         L     R2,BROFLENG            FIND STRING
         CLI   FM,C'H'                HEX FORMAT?
         BNE   *+8                    NO
         SRL   R2,1                   YES, DIVIDE LENGTH BY 2
         AR    R1,R2                  PAST FIND STRING
         MVI   2(R1),1                LOW-INTENSITY
*
NOTFNDLN DS    0H
*
         CLI   HEXFLAG,0           FLAG OFF
         BE    RESHOW3             YES
*
         LA    R14,1(,R14)         ADD 1
         LA    R0,3(,R14)          ROOM FOR 3 MORE LINES
         C     R0,ROWS
         BH    SHOWTBLS            NOPE
*
         ST    R5,SAVE5            SAVE IT
         L     R5,COLS             BUMP TO ACTUAL LENGTH
*
         AR    R15,R5              POINT TO NEXT SCREEN LINE
         ST    R15,SAVE15          SAVE IT
*
         L     R1,SAVE1
*
HEXLOOP1 DS    0H
*
         PACK  0(1,R15),0(1,R1)
         NI    0(R15),X'0F'
*
         LA    R15,1(,R15)
         LA    R1,1(,R1)
         BCT   R5,HEXLOOP1
*
         L     R15,SAVE15
         L     R5,COLS             BUMP TO ACTUAL LENGTH
         BCTR  R5,0
*
         EX    R5,HEXTR
*
         LA    R5,1(,R5)           BUMP TO ACTUAL LENGTH
*
         LA    R14,1(,R14)         ADD 1
         A     R15,COLS            POINT TO NEXT SCREEN LINE
         ST    R15,SAVE15          SAVE IT
*
         L     R1,SAVE1
*
HEXLOOP2 DS    0H
*
         MVC   0(1,R15),0(R1)
         NI    0(R15),X'0F'
*
         LA    R15,1(,R15)
         LA    R1,1(,R1)
         BCT   R5,HEXLOOP2
*
         L     R15,SAVE15
         L     R1,SAVE1
         L     R5,COLS             BUMP TO ACTUAL LENGTH
         BCTR  R5,0
*
         EX    R5,HEXTR
*
         LA    R14,1(,R14)         ADD 1
         A     R15,COLS            POINT TO NEXT SCREEN LINE
*
         L     R5,SAVE5
         B     RESHOW3             CONTINUE
*
HEXTR    TR    0(1,R15),HEX16
HEX16    DC    C'0123456789ABCDEF'
*
RESHOW3  DS    0H
*
         A     R15,COLS
*
         A     R3,BRLINELN
*
         LA    R14,1(,R14)
*
RESHOW9  DS    0H
*
         C     R14,ROWS
         BL    RESHOW2
*
*        DC    H'0'
*
         TITLE 'INVOKE ISPF DISPLAY MODULE'
SHOWTBLS DS    0H
*
         VREPLACE SMBDATA,ISPFSIZE,SMBDATA
         LTR   R15,R15
         BZ    *+6
         DC    H'0'
*
         ST    R3,BOTPTR              LAST LINE ON SCREEN
*
SHOWTBL2 DS    0H
         XC    ZSCROLLN(4),ZSCROLLN
         MVC   ZSCROLLA(4),SPACES
         MVC   ZVERB(8),SPACES
*
         DISPLAY SMBP1,ENDKEY=ENDBROWS
         LTR   R15,R15
         BZ    *+6
         DC    H'0'
*
         TR    SMBCMD(40),CAPSONLY    UPPER-CASE ALL INPUT FIELDS
         MVI   BROFCAPS,C' '
         MVC   BROFCAPS+1(59),BROFCAPS
         CLI   SMBFIND,C''''          QUOTED STRING?
         BNE   *+14                   NO
         MVC   BROFCAPS(45),SMBFIND+1 COPY QUOTED FIND STRING
         B     *+10                   TRANSLATE
         MVC   BROFCAPS(47),SMBFIND   COPY THE FIND STRING
         TR    BROFCAPS(60),CAPSONLY  UPPER-CASE THE FIND STRING
*
         OI    FM,C' '                UPPER-CASE
         CLI   FM,C'H'                HEX FORMAT?
         BNE   SHOWTBL3               NO
*
         MVC   BROFWORK(46),SMBFIND   COPY THE FIND STRING
         TR    BROFWORK(46),HEXTRT    CONVERT TO HEX
*
         LA    R3,BROFWORK         STRING CONVERTED
         LA    R4,BROFHEX          STRING TO HEX
         LA    R5,23              BYTES
*
GETFHEX1 DS    0H
*
         PACK  0(1,R4),0(1,R3)        LEFT HALF-BYTE
         OC    0(1,R4),1(R3)          RIGHT HALF-BYTE
*
         LA    R3,2(,R3)              BUMP INPUT
         LA    R4,1(,R4)              BUMP OUTPUT
*
         BCT   R5,GETFHEX1            LOOP
*
*
SHOWTBL3 DS    0H
*
         VGET  ZVERB,POOL=ASIS
         VGET  ZSCROLLA,POOL=ASIS
         VGET  ZSCROLLN,POOL=ASIS
*
         CLC   ZVERB(5),=C'RIGHT'  PF11
         BE    HSCROLL                YES, LEAVE VIRTICAL SCROLL ALONE
         CLC   SMBCMD(5),=C'RIGHT' PF11
         BE    HSCROLL                YES, LEAVE VIRTICAL SCROLL ALONE
         CLC   ZVERB(4),=C'LEFT'   PF10
         BE    HSCROLL                YES, LEAVE VIRTICAL SCROLL ALONE
         CLC   SMBCMD(4),=C'LEFT'  PF10
         BE    HSCROLL                YES, LEAVE VIRTICAL SCROLL ALONE
*
         NI    ZSCROLLN,X'7F'         TURN OFF SIGN BIT
         LTR   R15,R15                IF RETURN CODE IS ZERO,
         BZ    *+10                   USE ZSCROLLN AMOUNT
         MVC   ZSCROLLN(4),ROWS       ELSE SET IT TO TOTAL ROWS
*
         CLC   OLDROWS(4),ZSCROLLN    MATCH
         BE    TSTVERB                YES, LEAVE VERTICAL SCROLL ALONE
*
         MVC   OLDROWS(4),ZSCROLLN    UPDATE THIS
         L     R14,ZSCROLLN           NUMBER OF ROWS FOR SCROLLING
         LTR   R14,R14                IF SCROLL AMOUNT IS NON-ZERO
         BNZ   *+8                    THEN USE IT
         L     R14,ROWS               ELSE USE TOTAL ROWS
         CVD   R14,DWORD
         OI    DWORD+7,X'0F'
         UNPK  VL1(3),DWORD+6(2)
         B     TSTVERB                DONE SETTING VIRTICAL SCROLL
*
HSCROLL  DS    0H
*
         NI    ZSCROLLN,X'7F'         TURN OFF SIGN BIT
         LTR   R15,R15                IF RETURN CODE IS ZERO,
         BZ    *+10                   USE ZSCROLLN AMOUNT
         MVC   ZSCROLLN(4),COLS       ELSE SET IT TO TOTAL COLS
*
         CLC   OLDCOLS(4),ZSCROLLN    MATCH
         BE    TSTVERB                YES, LEAVE VERTICAL SCROLL ALONE
*
         MVC   OLDCOLS(4),ZSCROLLN    UPDATE THIS
         L     R14,ZSCROLLN           NUMBER OF COLS FOR SCROLLING
         LTR   R14,R14                IF SCROLL AMOUNT IS NON-ZERO
         BNZ   *+8                    THEN USE IT
         L     R14,COLS               ELSE USE TOTAL COLS
         CVD   R14,DWORD
         OI    DWORD+7,X'0F'
         UNPK  HL1(3),DWORD+6(2)
         B     TSTVERB                DONE SETTING VIRTICAL SCROLL
*
TSTVERB  DS    0H
*
         CLC   ZVERB(8),=C'SMBRFIND'
         BE    BROPF5              BRANCH IF YES
         CLC   ZVERB(5),=C'RFIND'
         BE    BROPF5              BRANCH IF YES
         CLI   ZVERB,C'F'
         BE    BROPF5              BRANCH IF YES
         CLC   SMBCMD(8),=C'SMBRFIND'
         BE    BROPF5              BRANCH IF YES
         CLC   SMBCMD(5),=C'RFIND'
         BE    BROPF5              BRANCH IF YES
         CLC   SMBCMD(3),=C'FUP'
         BE    NEWFUP              BRANCH IF YES
         CLI   SMBCMD,C'F'
         BE    BROPF5              BRANCH IF YES
         XC    BROFPTR(4),BROFPTR  CLEAR "FIND" FIRST LINE
*
         CLC   ZVERB(2),=C'UP'     PF7
         BE    BROPF7              BRANCH IF YES
         CLC   SMBCMD(2),=C'UP'    PF7
         BE    BROPF7              BRANCH IF YES
         CLC   ZVERB(4),=C'DOWN'   PF8
         BE    BROPF8              BRANCH IF YES
         CLC   SMBCMD(4),=C'DOWN' PF8
         BE    BROPF8              BRANCH IF YES
         CLC   ZVERB(4),=C'LEFT'   PF10
         BE    BROPF10             BRANCH IF YES
         CLC   SMBCMD(4),=C'LEFT' PF10
         BE    BROPF10             BRANCH IF YES
         CLC   ZVERB(5),=C'RIGHT'  PF11
         BE    BROPF11             BRANCH IF YES
         CLC   SMBCMD(5),=C'RIGHT' PF11
         BE    BROPF11             BRANCH IF YES
         B     GOTSTUFF            ASSUME ENTER
*
ENDK     DS    0H
         B     ENDBROWS            ALL DONE
*
         TITLE 'FIND A CHARACTER STRING'
BROPF5   DS    0H
*
* CALCULATE THE LENGTH OF THE "FIND" STRING
*
         LA    R1,SMBFIND+79       STRING
         LA    R14,79              LENGTH
*
BROLLOOP DS    0H
*
         CLI   0(R1),C' '          DO I HAVE A BLANK?
         BNE   BROLGOT             NO, MUST BE LAST CHARACTER OF STRING
*
BROLBUMP DS    0H
*
         BCT   R14,BROLNEXT        BACK UP TO NEXT CHARACTER
*
         B     RESHOW              NO STRING SPECIFIED
*
BROLNEXT DS    0H
*
         LA    R1,SMBFIND(R14)     PREVIOUS CHARACTER
         B     BROLLOOP            LOOP
*
BROLGOT  DS    0H
*
         L     R1,BROFPTR          FIRST LINE
         L     R2,BROFADDR         OFFSET WITHIN LINE
         LA    R2,1(,R2)           BUMP 1 CHARACTER
         L     R15,BROFOFFS        LENGTH REMAINING ON LINE
         BCTR  R15,0               MINUS 1
*
         CLI   SMBFIND,C''''       QUOTED STRING?
         BNE   *+8                 NO
         BCTR  R14,0               MINUS 1 FOR LEADING QUOTE
         BCTR  R14,0               MINUS 1 FOR TRAILING QUOTE
*
         LTR   R1,R1               RESUME FIND?
         BZ    BRONFIND            NO, START FROM SCRATCH
         C     R14,BROFLENG        RESUME FIND?
         BNE   BRONFIND            NO, START FROM SCRATCH
*
         B     BROPF51             RESUME
*
BRONFIND DS    0H
*
         L     R1,TOPPTR           FIRST LINE
         ST    R14,BROFLENG        SAVE LENGTH
*
BROFLOOP DS    0H
*
         CLI   NEXTFLAG,0          FIND-NEXT?
         BE    BROFLP02            YES
*
         C     R1,BRADDR           FIRST LINE OF DATA
         BL    BRONOTF
*
         B     BROFLP03            CONTINUE SEARCH
*
BROFLP02 DS    0H
*
         C     R1,BRLAST           LAST OF DATA?
         BH    BRONOTF
*
BROFLP03 DS    0H
*
         L     R15,SMBLRECL        LOOP CONTROL PER LINE
         CLC   FC2(3),=C'999'      LIMIT MAX?
         BE    BROFLIM1            YES
*
         PACK  DWORD(8),FC2(3)
         CVB   R2,DWORD            LOOP CONTROL
*
         CR    R15,R2              LRECL VS SCREEN MAX
         BL    BROFLIM1            SCREEN MAX TOO HIGH FOR LRECL
         LR    R15,R2              LRECL = SCREEN MAX
*
BROFLIM1 DS    0H
*
         CLC   FC1(3),=C'001'      LIMIT COL1?
         BE    BROFLIM9            YES
*
         PACK  DWORD(8),FC1(3)
         CVB   R2,DWORD            LOOP CONTROL
*
         C     R2,SMBLRECL         SCREEN COL1 VERSUS LRECL
         BL    BROFLIM2            SCREEN COL1 LESS THAN LRECL, SO OK
         SR    R2,R2               SCREEN COL1 TOO HIGH FOR LRECL
*
BROFLIM2 DS    0H
*
         SR    R15,R2              LRECL - SCREEN COL1
         SR    R15,R14             LRECL - FIND STRING LENGTH
         LA    R2,0(R1,R2)
*
         B     BROPF51
*
BROFLIM9 DS    0H
*
         SR    R15,R14
*
         LR    R2,R1
*
BROPF51  DS    0H
*
         CLI   FM,C'H'             HEX FORMAT?
         BE    BROPF513            YES
*
         CLI   SMBFIND,C''''       QUOTED STRING?
         BE    BROPF511            YES
*
         EX    R14,BROFCLC1        MATCH STRING AS ENTERED?
         BE    BROGOTF             YES, PUT THIS LINE AT TOP
         CLI   FM,C'T'             TEXT FORMAT?
         BE    BROPF512            YES, MUST MATCH EXACTLY
*
BROPF510 DS    0H
*
         EX    R14,BROFMOV2        MOVE DATA TO BROFWORK
         EX    R14,BROFTRT2        MAKE IT ALL CAPS
         EX    R14,BROFCLC3        MATCH STRING ALL CAPS?
         BE    BROGOTF             YES, PUT THIS LINE AT TOP
*
         B     BROPF512            NO, JUST BUMP
*
BROPF511 DS    0H
*
         EX    R14,BRFQCLC1        MATCH STRING AS ENTERED?
         BE    BROGOTF             YES, PUT THIS LINE AT TOP
         CLI   FM,C'C'             CHAR FORMAT?
         BE    BROPF510            YES, SEE IF ALL-CAPS MATCHES
*
BROPF512 DS    0H
*
         LA    R2,1(,R2)           BUMP TO NEXT CHARACTER
         BCT   R15,BROPF51             KEEP LOOPING
*
*
         CLI   NEXTFLAG,0          FIND-NEXT?
         BNE   BROP5122            NO
*
         A     R1,SMBLRECL         BUMP TO NEXT LINE
         B     BROFLOOP            AND LOOP
*
BROP5122 DS    0H
*
         S     R1,SMBLRECL         BUMP TO PREV LINE
         B     BROFLOOP            AND LOOP
*
BROPF513 DS    0H
*
         L     R14,BROFLENG        GET LENGTH
         SRL   R14,1               DIVIDE BY 2
         EX    R14,BROFCLC4        MATCH STRING?
         BE    BROGOTF             YES, PUT THIS LINE AT TOP
*
         B     BROPF512            CONTINUE LOOP
*
BROGOTF  DS    0H
*
         ST    R1,BROFPTR          LINE I WAS ON
         ST    R1,FINDPTR          LINE I WAS ON
*
**  THIS BACKS UP THE DISPLAY WHEN A CHARACTER STRING IS FOUND,
*   WHICH IS OFTEN MORE CONVENIENT.
*
         S     R1,FUPAMT           SEE IF I CAN BACK UP
         ST    R1,TOPPTR           ASSUME SO
         A     R1,FUPAMT           ADJUST FOR SUBTRACTION
*
         ST    R2,BROFADDR         OFFSET WITHIN LINE
         ST    R15,BROFOFFS        LENGTH REMAINING ON LINE
         SR    R2,R1               OFFSET WITHIN LINE
         ST    R2,FINDOFF          OFFSET WITHIN LINE
*
         LTR   R1,R2
         BZ    LEFT00
         BCTR  R1,0
         C     R1,OFFSET           COMPARE TO DISPLAY OFFSET
         BH    LEFTOK              RIGHT OF OFFSET
*
LEFT00   DS    0H
*
         ST    R1,OFFSET           MAKE OFFSET = START OF FOUND STRING
         B     RIGHTOK             FIND STRING LENGTH IS ALWAYS LESS
*                                  THAN COLS
*
LEFTOK   DS    0H
*
         LR    R1,R2
         A     R1,BROFLENG         POINT TO
         LA    R1,3(,R1)                    END OF STRING
         L     R14,COLS            NUMBER OF COLS
         A     R14,OFFSET          PLUS OFFSET = RIGHT MARGIN
         C     R14,SMBLRECL        HIGHER THAN LRECL?
         BNH   *+8
         L     R14,SMBLRECL        MAKE IT LRECL
         CR    R1,R14              COMPARE TO DISPLAY OFFSET
         BNH   RIGHTOK             RIGHT OF OFFSET
*
         LTR   R2,R2
         BZ    *+6
         BCTR  R2,0
         ST    R2,OFFSET           COMPARE TO DISPLAY OFFSET
*
RIGHTOK  DS    0H
*
         SETMSG SMB002
*
         B     RESHOW
*
BRONOTF  DS    0H
*
*        L     R0,BROFLENG         FIND STRING LENGTH
*        DC    H'0'                S0C1
*
         SETMSG SMB001
*
         B     RESHOW
*
BROFCLC1 CLC   SMBFIND(1),0(R2)
BRFQCLC1 CLC   SMBFIND+1(1),0(R2)
BROFCLC2 CLC   BROFCAPS(1),0(R2)
BROFCLC3 CLC   BROFWORK(1),BROFCAPS
BROFCLC4 CLC   BROFHEX(1),0(R2)
BROFMOV2 MVC   BROFWORK(1),0(R2)
BROFTRT2 TR    BROFWORK(1),CAPSONLY
         TITLE 'SCROLL UP'
BROPF7   DS    0H
*
         CLC   ZSCROLLA(3),=C'MAX' GO TO TOP?
         BE    BROTOP              YES
         CLI   SMBCMD,C'M'            GO TO TOP?
         BE    BROTOP              YES
*
         TR    VL1(3),NUMONLY         ALLOW ONLY NUMERICS
         PACK  DWORD(8),VL1(3)
         CVB   R1,DWORD
         STH   R1,PFSCRL
*        MVC   PFSCRL(2),ZSCROLLN+2       SCREEN SCROLL AMOUNT
         CLI   HEXFLAG,0                  HEX DISPLAY?
         BE    BROPF72                    NO
*
         LH    R1,PFSCRL           SCROLL AMOUNT
         SRL   R1,2                DIVIDE BY 4
         STH   R1,PFSCRL           SCROLL AMOUNT
*
BROPF72  DS    0H
*
         L     R1,TOPPTR           GO TO TOP?
*
         L     R0,BRLINELN         INPUT LINE LENGTH
         MH    R0,PFSCRL           BACK UP SCREEN SIZE
         SR    R1,R0
         ST    R1,TOPPTR           START DISPLAY
*
         C     R1,BRADDR           HAVE I GONE PAST TOP?
         BNL   RESHOW              NO, START THE DISPLAY
*
BROTOP   DS    0H
*
         L     R1,BRADDR           POINT TO FIRST LINE
         ST    R1,TOPPTR           START DISPLAY AT TOP
         B     RESHOW
*
         TITLE 'SCROLL DOWN'
BROPF8   DS    0H
*
         CLC   ZSCROLLA(3),=C'MAX' GO TO BOTTOM?
         BE    BROBOT              YES
         CLI   SMBCMD,C'M'         GO TO BOTTOM?
         BE    BROBOT              YES
*
         TR    VL1(3),NUMONLY         ALLOW ONLY NUMERICS
         PACK  DWORD(8),VL1(3)
         CVB   R1,DWORD
         STH   R1,PFSCRL
*        MVC   PFSCRL(2),ZSCROLLN+2       SCREEN SCROLL AMOUNT
         CLI   HEXFLAG,0                  HEX DISPLAY?
         BE    BROPF82                    NO
*
         LH    R1,PFSCRL           SCROLL AMOUNT
         SRL   R1,2                DIVIDE BY 4
         STH   R1,PFSCRL           SCROLL AMOUNT
*
BROPF82  DS    0H
*
         L     R1,TOPPTR
*
         L     R0,BRLINELN         LINE LENGTH
         MH    R0,PFSCRL           BUMP DOWN SCREEN SIZE
         AR    R1,R0
*
         C     R1,BRLAST           HAVE I GONE PAST BOTTOM?
         BH    RESHOW              YES, JUST RESHOW THE SCREEN
*
         ST    R1,TOPPTR
*
         B     RESHOW
*
BROBOT   DS    0H
*
         L     R1,BRLAST           POINT TO BOTTOM
*
         L     R0,BRLINELN         LINE LENGTH
         MH    R0,ZSCROLLN+2       BUMP UP SCREEN SIZE
         SR    R1,R0
         ST    R1,TOPPTR
*
         C     R1,BRADDR           HAVE I GONE PAST TOP?
         BNL   RESHOW
*
         L     R1,BRADDR           POINT TO TOP
         ST    R1,TOPPTR
         B     RESHOW
*
         TITLE 'SCROLL LEFT'
BROPF10  DS    0H
         MVC   ZVERB(4),=C'LEFT'   PF10
         MVC   SMBCMD(4),=C'LEFT' PF10
*
         L     R1,OFFSET
         LTR   R1,R1
         BZ    RESHOW
*
         TR    HL1(3),NUMONLY         ALLOW ONLY NUMERICS
         PACK  DWORD(8),HL1(3)
         CVB   R14,DWORD
         CR    R1,R14
         BNH   LEFT0
*
         SR    R1,R14
         ST    R1,OFFSET
         B     RESHOW
*
LEFT0    DS    0H
*
         XC    OFFSET(4),OFFSET
         B     RESHOW
*
         TITLE 'SCROLL RIGHT'
BROPF11  DS    0H
         MVC   ZVERB(5),=C'RIGHT'  PF11
         MVC   SMBCMD(5),=C'RIGHT' PF11
*
         TR    HL1(3),NUMONLY         ALLOW ONLY NUMERICS
         PACK  DWORD(8),HL1(3)
         CVB   R14,DWORD
         ST    R14,HL1COLS
*
         L     R1,SMBLRECL
         L     R14,HL1COLS
         A     R14,OFFSET
         CR    R1,R14
         BNH   RIGHT9
*
         L     R1,HL1COLS
         A     R1,OFFSET
         ST    R1,OFFSET
         B     RESHOW
*
RIGHT9   DS    0H
*
         L     R1,SMBLRECL
         S     R1,HL1COLS
         ST    R1,OFFSET
         B     RESHOW
*
*
         B     RESHOW
*
         TITLE 'PROCESS  COMMANDS'
*
***********************************************************************
*
*   DETERMINE WHETHER ANYTHING WAS ENTERED ON THE SCREEN
*
***********************************************************************
*
GOTSTUFF DS    0H
*
         CLI   SMBCMD,C' '         COMMAND ENTERED?
         BE    RESHOW
*
*    THE COMMAND ENTERED IS IN "SMBCMD"
*
GOTCMD   DS    0H
*
         CLI   SMBCMD,C'C'         COLS?
         BE    TOGGLEC             YES
*
         CLI   SMBCMD,C'H'         HEX?
         BE    TOGGLEH             YES
*
         CLI   SMBCMD,C'N'         "FIND" DIRECTION IS FIND-NEXT
         BE    TOGGLEN             YES
*
         CLI   SMBCMD,C'P'         "FIND" DIRECTION IS FIND-PREVIOUS
         BE    TOGGLEP             YES
*
*
*     PUT ADDITIONAL COMMAND TESTS HERE
*
         B     RESHOW
*
         TITLE 'PROCESS  COLS  COMMAND'
*
***********************************************************************
*
*   IF COLS IS ON, SET IT OFF; IF OFF, SET IT ON
*
***********************************************************************
*
NEWFUP   DS    0H
*
         TR    SMBCMD(4),NUMONLY
         PACK  DWORD(8),SMBCMD(4)
         CVB   R1,DWORD
         MH    R1,SMBLRECL+2       ADD 1
         ST    R1,FUPAMT
         B     RESHOW
*
         TITLE 'PROCESS  COLS  COMMAND'
*
***********************************************************************
*
*   IF COLS IS ON, SET IT OFF; IF OFF, SET IT ON
*
***********************************************************************
*
TOGGLEC  DS    0H
*
         CLI   COLSFLAG,0          FLAG OFF?
         BE    COLSFON
*
         MVI   COLSFLAG,0          ELSE TURN IT OFF
         PACK  DWORD(8),VL1(3)
         CVB   R1,DWORD
         LA    R1,1(,1)            ADD 1
         CVD   R1,DWORD
         OI    DWORD+7,X'0F'  MAKE PRINTABLE
         UNPK  VL1(3),DWORD+6(2)
         B     RESHOW
*
COLSFON  DS    0H
*
         MVI   COLSFLAG,1          SET FLAG ON
         PACK  DWORD(8),VL1(3)
         CVB   R1,DWORD
         BCTR  R1,0
         CVD   R1,DWORD
         OI    DWORD+7,X'0F'  MAKE PRINTABLE
         UNPK  VL1(3),DWORD+6(2)
         B     RESHOW
*
         TITLE 'PROCESS  FIND-NEXT SPECIFICATION'
*
TOGGLEN  DS    0H
*
         MVI   NEXTFLAG,0          SET IT SO
         B     RESHOW
*
         TITLE 'PROCESS  FIND-PREVIOUS SPECIFICATION'
*
TOGGLEP  DS    0H
*
         MVI   NEXTFLAG,1          SET IT SO
         B     RESHOW
*
         TITLE 'PROCESS  HEX  COMMAND'
*
***********************************************************************
*
*   IF HEX IS ON, SET IT OFF; IF OFF, SET IT ON
*
***********************************************************************
*
TOGGLEH  DS    0H
*
         CLI   HEXFLAG,0          FLAG OFF?
         MVI   HEXFLAG,1          ASSUME SO
         BE    RESHOW
*
         MVI   HEXFLAG,0          ELSE TURN IT OFF
         B     RESHOW
*
         TITLE 'SMB - CONSTANTS'
***********************************************************************
*                                                                     *
*        ISPF DISPLAY MODULE CONSTANTS                                *
*                                                                     *
***********************************************************************
*
COLSDATA EQU   *
         DC    C'----+----1'
         DC    C'----+----2'
         DC    C'----+----3'
         DC    C'----+----4'
         DC    C'----+----5'
         DC    C'----+----6'
         DC    C'----+----7'
         DC    C'----+----',X'01'
*
INITLINE DC    X'01'
SPACES   DC    CL132'  '
*
CAPSONLY DC    CL64' '
         DC    CL10' ',C'¢.<(+|&&',CL9' ',C'!$*);¬-/'
         DC    CL9' ',C',%_>?',CL10' ',C':#@''="'
         DC    CL16' ABCDEFGHI      '
         DC    CL16' JKLMNOPQR      '
         DC    CL16'  STUVWXYZ      '
         DC    CL16'                '
         DC    CL16' ABCDEFGHI      '
         DC    CL16' JKLMNOPQR      '
         DC    CL16'  STUVWXYZ      '
         DC    CL16'0123456789      '
*
ASISTRT  DS    0H
         DC    CL64' '
         DC    CL10' ',C'¢.<(+|&&',CL9' ',C'!$*);¬-/'
         DC    CL9' ',C',%_>?',CL10' ',C':#@''="'
         DC    XL10'40818283848586878889',CL6' {ð(+ '
         DC    XL10'40919293949596979899',CL6' }°)Æ€'
         DC    XL10'A0A1A2A3A4A5A6A7A8A9',CL6' ¿ÐÝÞ®'
         DC    XL10'B0B1B2B3B4B5B6B7B8B9',CL6' ]¯¨´µ'
         DC    CL16' ABCDEFGHI      '
         DC    CL16' JKLMNOPQR      '
         DC    CL16'  STUVWXYZ      '
         DC    CL16'0123456789      '
*
LOWTRT   DS    0H
         DC    CL16' '          DELETE INTENS LOW (01) AND HIGH (02)
         DC    CL48' '
         DC    CL10' ',C'¢.<(+|&&',CL9' ',C'!$*);¬-/'
         DC    CL9' ',C',%_>?',CL10' ',C':#@''="'
         DC    XL10'40818283848586878889',CL6' {ð(+ '
         DC    XL10'40919293949596979899',CL6' }°)Æ€'
         DC    XL10'A0A1A2A3A4A5A6A7A8A9',CL6' ¿ÐÝÞ®'
         DC    XL10'B0B1B2B3B4B5B6B7B8B9',CL6' ]¯¨´µ'
         DC    CL16' ABCDEFGHI      '
         DC    CL16' JKLMNOPQR      '
         DC    CL16'  STUVWXYZ      '
         DC    CL16'0123456789      '
*
HEXTRT   DS    0H
         DC    XL16'00000000000000000000000000000000'
         DC    XL16'00000000000000000000000000000000'
         DC    XL16'00000000000000000000000000000000'
         DC    XL16'00000000000000000000000000000000'
         DC    XL16'00000000000000000000000000000000'
         DC    XL16'00000000000000000000000000000000'
         DC    XL16'00000000000000000000000000000000'
         DC    XL16'00000000000000000000000000000000'
         DC    XL16'000A0B0C0D0E0F000000000000000000'
         DC    XL16'00000000000000000000000000000000'
         DC    XL16'00000000000000000000000000000000'
         DC    XL16'00000000000000000000000000000000'
         DC    XL16'000A0B0C0D0E0F000000000000000000'
         DC    XL16'00000000000000000000000000000000'
         DC    XL16'00000000000000000000000000000000'
         DC    XL16'00010203040506070809000000000000'
*
NUMONLY  DS    0H
         DC    240C'0'
         DC    CL16'0123456789000000'
*
GETMLENG DC    A(WORKLEN)
*
         LTORG
WORKAREA DSECT
         DS    18F
ISPARMS  DS    10F
AREATYP  DS    CL8
COLS     DS    F
HL1COLS  DS    F
ROWS     DS    F
TLROW    DS    F
TLCOL    DS    F
SMBCMD   DS    CL40
ZVERB    DS    CL8
ZSCROLLA DS    CL4
ZSCROLLN DS    F
OLDROWS  DS    F
OLDCOLS  DS    F
*
PFSCRL   DS    H
SAVE1    DS    F
SAVE15   DS    F
SAVE5    DS    F
BROFPTR  DS    F
FINDPTR  DS    F
FINDOFF  DS    F
BROFADDR DS    F
BROFOFFS DS    F
BROFLENG DS    F
*
ISPFWORK DS    F
ISPFSIZE DS    F
ISPFNUMB DS    F
*
FUPAMT   DS    F
*
DWORD    DS    D
OFFSET   DS    F
*
COLSFLAG DS    X
HEXFLAG  DS    X
NEXTFLAG DS    X
*
*
*                   PARAMETERS FROM CALLING PROGRAM
*
PARMS    DS    0F
SMBSTART DS    F
SMBNUMB  DS    F
SMBLRECL DS    F
BRADDR   DS    F
BRNUMLN  DS    F
BRLINELN DS    F
BRLAST   DS    F
U4       DC    F'4'
*
*                   SAVE AREAS FOR LINK REGISTER 7
*
DONE7    DS    F
INIT7    DS    F
BROWSE7  DS    F
*
*                   POINTERS FOR SCROLLING
*
TOPPTR   DS    F
BOTPTR   DS    F
SMBFIND  DS    CL80
BROFCAPS DS    CL80
BROFWORK DS    CL80
BROFHEX  DS    CL24
*
PFKWORK  DS    CL255
SMBTITLE DS    CL49
SMBLN    DS    CL5
SMBCL    DS    CL5
FM       DS    CL2
FC1      DS    CL3
FC2      DS    CL3
VL1      DS    CL3
HL1      DS    CL3
*
ENT1     DS    F
ENT2     DS    F
*
SMBDATA  DS    XL3280
         DS    XL8
WORKLEN  EQU   *-WORKAREA
*
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12                  1ST BASE REG
R13      EQU   13                  WORK AREA
R14      EQU   14
R15      EQU   15
*
         END
