//ASQCCETP JOB (X0041,QCC,ASQCC),'X-COOK, BRIAN',
//   MSGCLASS=X,CLASS=X,NOTIFY=ASQCC,MSGLEVEL=(1,1)
/*JOBPARM L=99,SYSAFF=MVSA
//*
//* TECH.ARCHIVE.SOURCE  (ETPS)
//*
//ASM     EXEC PGM=IEV90,REGION=1024K,
//             PARM='TERM,NODECK,OBJECT,XREF(SHORT),RENT'
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR
//         DD  DSN=SYS1.AMODGEN,DISP=SHR
//         DD  DISP=SHR,DSN=SYS2.MACLIB  <== TCBUVTAM, ETPSMACS
//SYSUT1   DD  SPACE=(CYL,(5,1)),
//             UNIT=SYSDA
//SYSTERM  DD  SYSOUT=*
//SYSPRINT DD  SYSOUT=*
//SYSPUNCH DD  DUMMY
//SYSLIN   DD   DSN=&&OBJSET,UNIT=SYSDA,SPACE=(80,(200,50)),
//             DISP=(MOD,PASS)
//SYSIN    DD  *
ETPS24   TITLE 'ETPS - WARNING     '
***********************************************************************
**                                                                   **
**   W A R N I N G ! ! ! ! ! !                                       **
**                                                                   **
**   W A R N I N G ! ! ! ! ! !                                       **
**                                                                   **
**   W A R N I N G ! ! ! ! ! !                                       **
**                                                                   **
**                                                                   **
**   THIS PROGRAM SHOULD BE TESTED DURING STANDALONE TIME ONLY.      **
**                                                                   **
**   THIS PROGRAM IS AUTHORIZED AND MODIFIES KEY 0 MEMORY IN THE     **
**   NUCLEUS.                                                        **
**                                                                   **
**   YOU SHOULD NOT ATTEMPT TO EDIT ANY LIBRARY UNLESS YOU HAVE      **
**   ADEQUATE BACKUP.                                                **
**                                                                   **
**   USE AT YOUR OWN RISK. NO WARRANTY IS EXPRESSED OR IMPLIED.      **
**                                                                   **
**                                                                   **
**   W A R N I N G ! ! ! ! ! !                                       **
**                                                                   **
**   W A R N I N G ! ! ! ! ! !                                       **
**                                                                   **
**   W A R N I N G ! ! ! ! ! !                                       **
**                                                                   **
**                                                                   **
***********************************************************************
         TITLE 'ETPS - CHANGE LOG  '
***********************************************************************
**                                                                   **
**  4/02/87  BRIAN COOK   ADDED DSUNIT FOR ALLOCATION OF MEMBER,     **
**                        PER CARL GELBART, PACIFIC MUTUAL.     PML  **
**                                                                   **
** 11/21/86  CHANGED DEFAULT UNIT TO "SYSALLDA" BRIAN COOK      MNP  **
**                                                                   **
** 11/10/86  VERSION 2.4. ENHANCEMENTS TO THE   BRIAN COOK      MNP  **
**           IDCAMS INTERFACE:                                       **
**                                                                   **
**           1. INCREASED NUMBER OF INPUT COMMAND DATA               **
**              LINES FROM 1 TO 5 TO ALLOW FOR MORE                  **
**              VERBOSE COMMANDS, SUCH AS "DEFINE PAGESPACE".        **
**           2. ALLOW SCROLLING UP AND DOWN IN THE                   **
**              OUTPUT USING PF7/PF8.                                **
**                                                                   **
**           ALSO ADDED SOME HELP SCREENS.                           **
**                                                                   **
** 11/06/86  OPEN PROBLEM NOTED.                BRIAN COOK      MNP  **
**           ON THE EDIT AND BROWSE SCREENS, WHEN IN SPLIT-SCREEN    **
**           MODE ON THE BOTTOM SCREEN, THE CURSOR IS PLACED 1       **
**           CHARACTER TO THE LEFT OF THE "DSNAME" INPUT FIELD.      **
**           THE PDS, IDCAMS, AND PRIMARY OPTION MENU WORK OK!!!!!   **
**                                                                   **
** 11/03/86  LIB  ROUTINE IGNORING VOLSER,      BRIAN COOK      MNP  **
**           RESULTING IN EDIT-SAVE INTO A CATALOGED                 **
**           PDS INSTEAD OF AN UNCATALOGED ONE SELECTED              **
**           BY SPECIFIC VOLSER. MINOR OVERSIGHT,                    **
**           THANKS, OKLAHOMA STATE.                                 **
**                                                                   **
**   7/7/86  ADDED BACK I/O ATTENTION EXIT,     W.A.M.         VCCS  **
**           FOR TUBE I/O.                                           **
**                                                                   **
**                                                                   **
**           PERSONALLY, I CAN'T GET THIS TO WORK AT ALL,  BC  MNP   **
**           SO I HAVE SET UP "EXCPFLG1" AND "EXCPFLG2"              **
**           THAT CAN BE SET TO NON-ZERO IF ANYONE                   **
**           WANTS TO TRY TO DEBUG THIS. THE ERROR I GET             **
**           IS AN S0C4 IN IECVPST. IT APPEARS THAT THE              **
**           ATTENTION EXIT GETS CONTROL IN AMODE 24, BUT            **
**           THE RETURN ADDRESS IS A 31-BIT ADDRESS.                 **
**                                                                   **
**                                                                   **
**                                                                   **
**  6/25/86  VERSION 2.3. ENHANCEMENTS TO THE   BRIAN COOK      MNP  **
**           PDS INTERFACE:                                          **
**                                                                   **
**           1. COMPRESS OPTION                                      **
**           2. MEMBER SELECTION "HELP" PANEL                        **
**           3. MEMBER SELECTION OPTIONS FOR                         **
**              BROWSE, EDIT, RENAME, DELETE,                        **
**              AND COPY                                             **
**                                                                   **
**           ALSO FIXED BUGS IN THE SBA CALC,                        **
**           AND REMOVED THE MENU HIERARCHY LOGIC.                   **
**                                                                   **
**  1/21/86  3279-2C WORKS SAME AS 3278-2A      WAYNE MITCHELL  VCCS **
**                                                                   **
** 12/17/85  REMOVED ALMOST ALL OF THE EXCP     BRIAN COOK      MNP  **
**           LOGIC FOR TERMINAL I/O, DUE TO                          **
**           ABEND'S AFTER INSTALLING 1.3.3.                         **
**                                                                   **
**           WITHOUT THE ATTENTION EXIT, ETPS JUST                   **
**           LOOPS AROUND A 2-SECOND "STIMER", SO                    **
**           THE TERMINAL RESPONSE TIME IS VERY                      **
**           SLUGGISH.                                               **
**                                                                   **
**                                                                   **
**  4/24/85  ADDED OPTION 4.1, FOREGROUND       BRIAN COOK      MNP  **
**           INVOKATION OF IDCAMS. MODULE IS                         **
**           "ETPSIDC2".                                             **
**                                                                   **
**  4/09/85  INCORPORATED CHANGES FOR 3278-2A   BRIAN COOK      MNP  **
**           SUPPORT, REDUCED LOGON SCREEN TO                        **
**           20 LINES, ALLOW "CAN" AS ABBREVIATION                   **
**           FOR EDIT "CANCEL".                                      **
**                                                                   **
**           CHANGES PROVIDED BY WAYNE MITCHELL,      W.A.M.   VCCS  **
**           VIRGINIA COMMUNITY COLLEGE SYSTEM.                      **
**                                                                   **
**  4/04/85  REVISED DSECTS, MADE INTO MACRO    BRIAN COOK      MNP  **
**           CALLS. ALSO SPLIT OUT EDITOR                            **
**           MODULE, ETPSEDT3, AND BROWSE,                           **
**           ETPSBRO3.                                               **
**                                                                   **
**  9/27/84  VERSION 2.1 -- ADDED TSO MODE      BRIAN COOK      MNP  **
**           AND DSECT FOR NATIVE VTAM. SEE                          **
**           PROGRAM "ETPSTSO" FOR DOC ON                            **
**           RUNNING UNDER TSO. VTAM CONTROL                         **
**           PROGRAM WILL BE DEVELOPED ONE OF                        **
**           THESE DAYS.                                             **
**                                                                   **
**  6/20/84  ADDED BROWSE PROCESSING, VERSION   BRIAN COOK      MNP  **
**           1.1 FLAG ON LOGON/PRIMARY MENUS. THE                    **
**           BROWSE MODULE IS BASED ON "REVIEW",                     **
**           BY BILL GODFREY. IT HAS BEEN MUCH                       **
**           MODIFIED TO RUN UNDER ETPS, AND                         **
**           HAS ALSO BEEN ENHANCED TO SUPPORT                       **
**           BROWSING DSID'S DIRECTLY ON THE                         **
**           JES2 SPOOL. (USES MODIFIED UAL SVC.)                    **
**                                                                   **
**           (THE JES2 BROWSE IS FOR A POSSIBLE                      **
**           FUTURE ENHANCEMENT.)                                    **
**                                                                   **
**  6/11/84  ADDED RETURN CODE TEST AFTER       BRIAN COOK      MNP  **
**           GETMAIN FOR ATTENTION EXIT.                             **
**                                                                   **
**  5/16/84  ADDED SPF STATS SUPPORT.           BRIAN COOK      MNP  **
**                                                                   **
**  4/19/83  ADDED BYPASS OF LOGON PANEL FOR EMERGENCY USE. IF ETPS  **
**           IS INVOKED WITH A PARM BEGINNING WITH THE LETTERS "OP", **
**           A LOGON IS NOT REQUIRED.           BRIAN COOK       MNP **
**                                                                   **
**  6/10/83  CHANGED TUBE IO TO EXCP.           BRIAN COOK      MNP  **
**                                                                   **
**  6/11/83  ADDED UPDATE-IN-PLACE LOGIC FOR    BRIAN COOK      MNP  **
**           PDS MEMBERS THAT ARE EDITED AND                         **
**           SAVED. IF THE NUMBER OF CARDS IS                        **
**           THE SAME BEFORE AND AFTER THE                           **
**           EDIT, AN UPDATE-IN-PLACE IS DONE.                       **
**           THIS SHOULD REDUCE THE POSSIBILITY                      **
**           OF X37 ABENDS.                                          **
**                                                                   **
**  6/23/83  ADDED "COPY-AFTER"   TO EDIT       BRIAN COOK      MNP  **
**                                                                   **
**  8/01/83  ADDED SPLIT-SCREEN LOGIC           BRIAN COOK      MNP  **
**                                                                   **
** 12/05/83  RENAMED TO ETPS, ADDED "ET" LOGO.  BRIAN COOK      MNP  **
**                                                                   **
***********************************************************************
         TITLE 'ETPS - EMERGENCY TELE-PROCESSING SYSTEM'
***********************************************************************
**                                                                   **
**             E  T  P  S                                            **
**                                                                   **
***********************************************************************
**                                                                   **
** FUNCTION-                                                         **
**                                                                   **
** THIS PROGRAM IS INTENDED TO PROVIDE EMERGENCY TELE-PROCESSING     **
** SERVICES WHEN THE NORMAL SUBSYSTEMS SUCH AS JES2, VTAM, TCAM, ETC **
** ARE NOT AVAILABLE. IT IS INTENDED TO BE INVOKED AS A STARTED TASK **
** OR A BATCH JOB. IT CAN ALSO BE INVOKED AS A TSO COMMAND PROCESSOR **
** USING A FRONT-END MODULE, "ETPSTSO".                              **
**                                                                   **
** IF YOU WANT TO INVOKE IT AS A SUB-SYSTEM WHEN JES2 IS NOT UP, YOU **
** MUST ADD A SUB-SYSTEM DEFINITION.                                 **
**                                                                   **
**                                                                   **
** PARM -                                                            **
**                                                                   **
** THIS PROGRAM MUST BE INVOKED WITH A STANDARD EXECUTE-CARD FORMAT  **
** PARM. THE LENGTH MUST BE 8 BYTES, AS FOLLOWS:                     **
**                                                                   **
**    1. THE FIRST 4 CHARACTERS MUST BE "ETPS" OR "OPER".            **
**                                                                   **
**    2. THE 5TH BYTE IS TERMINAL TYPE. VALID TERMINAL TYPES ARE:    **
**                                                                   **
**            A = 3278-2A OR 3279-2C (20-LINE TERMINAL)              **
**            2 = 3278 MODEL 2                                       **
**            3 = 3279 MODEL 3B                                      **
**            4 = 3278 MODEL 4                                       **
**                                                                   **
**    3. THE LAST 3 BYTES MUST BE THE UNIT ADDRESS OF THE TERMINAL.  **
**                                                                   **
** AS AN EXAMPLE, //STEP1  EXEC  PGM=ETPS,PARM='ETPS49C4' STARTS     **
** TIME-SHARING ON A LOCAL 3278  MOD 4 AT ADDR 9C4.                  **
**                                                                   **
**                                                                   **
** TO INVOKE AS A TSO COMMAND PROCESSOR, THE PARAMETER MUST BE:      **
**                                                                   **
**        DC    H'8',CL8'TSO'                                        **
**                                                                   **
**                                                                   **
** DOC-                                                              **
**                                                                   **
** REFER TO ETPS$ FOR FURTHER DOCUMENTATION.                         **
**                                                                   **
***********************************************************************
         TITLE 'ETPS - EMERGENCY TELE-PROCESSING SYSTEM'
         PRINT OFF
         COPY ETPSMACS
         PRINT ON
ETPS     CSECT
*
         PRINT NOGEN
*
         USING *,12,11,9
         USING MYSAVE,13,10
*
         STM   R14,R12,12(R13)    SAVE INPUT REGS
         LR    R12,R15            SET PROGRAM BASE REGISTER 1
         L     R2,0(,R1)          SAVE PARM POINTER
         B     PROLOG
SYSNAME  DC    C'ETPS     -  BRIAN COOK &SYSDATE &SYSTIME'
PROLOG   DS    0H
         L     R9,GETSIZE        GET SAVE AREA LENGTH
         SRL   R9,12             ROUND UP TO 4K BOUNDARY
         LA    R9,1(,9)          ROUND UP TO 4K BOUNDARY
         SLL   R9,12             ROUND UP TO 4K BOUNDARY
         GETMAIN R,LV=(9)        GET MY SAVE AREA
         LR    R8,R1             SAVE GETMAIN POINTER
         ST    R8,8(R13)         STORE FORWARD POINTER
         LR    R5,R13            SAVE BACKWARDS POINTER
         LR    R13,R8            SET DSECT BASE 1
*
         LR    R4,R9             SAVE THE LENGTH FOR FREEMAIN
*
         MVI   0(R8),X'00'       SET BINARY ZEROS
         LA    R14,1(,R8)
         LR    R15,R9            LENGTH FOR MVCL
         BCTR  R15,0             RECEIVING FIELD IS 1 LESS
         MVCL  R14,R8        CLEAR BUFFER
*
         ST    R5,4(,R13)        STORE BACKWARDS POINTER
         ST    R13,SAVETOP       SAVE THE START ADDRESS
         LA    R3,1
         LA    R11,4095(R3,R12)   SET PROGRAM BASE REGISTER 2
         LA    R9,4095(R3,R11)    SET PROGRAM BASE REGISTER 3
         LA    R10,4095(R3,R13)   SET DSECT BASE 2
         ST    R4,FREESIZE       SAVE THE LENGTH FOR FREEMAIN
*
RE       EQU   14
RF       EQU   15
         TITLE 'ETPS - MAINLINE PROCESSING LOOP'
*
MAIN     DS    0H
         BAL   R14,INIT
ALOGON   DS    0H
         BAL   R14,LOGON
*
         CLC   3(3,R3),=CL3'EOJ'     ALL DONE?
         BE    CLOSETB               GO CLOSE DOWN
*
         BAL   R14,PRIOPT
*
         CLC   TUBEID(2),=C'OP'  IS IT OPERATOR
         BNE   ALOGON            NO, NO AUTOMATIC LOGOFF
*
*
CLOSETB  DS    0H
*
         CLC   TUBEDDNM(4),=C'VTAM'    VTAM     TERMINAL
         BE    RETRNEND          YES
*
         CLC   TUBEDDNM(3),=C'TSO'     VTAM     TERMINAL
         BE    RETRNEND          YES
*
         LA    R2,TUBE
         MVI   PARMOP,128
         CLOSE ((2)),MF=(E,PARMOP)
* DE-ALLOCATE TUBE
FREETUBE DS    0H
         MVI   VERBCODE,2       SET TO INDICATE UNALLOCATE
         OI    DYNTEXT1,X'80'   DDNAME ONLY
         LA    R1,DYNPARM
         DYNALLOC
*
         LTR   R15,15
         BNZ   WTOMS4
*
RETRNEND DS    0H
         LR    R11,R13
         L     R9,FREESIZE
         L     R13,4(,R13)  PICK UP CALLING SAVE AREA
         FREEMAIN R,LV=(9),A=(11)
         LM    R14,R12,12(R13)
         SR    R15,15
         BR    R14
         TITLE 'ETPS - INITIALIZATION'
*
INIT     DS    0H
         ST    R14,INIT14
*
* PLUG COMM ROUTINE ADDRESSES INTO DSECT
*
         MVC   COMMA3(4),=A(COMM3)
         MVC   COMMA4(4),=A(COMM4)
*
* INITIALIZE SAVE AREAS FOR SPLIT-SCREEN
*
         MVC   SPLIT1(72),0(R13)
         MVC   SPLIT2(72),0(R13)
*
* DETERMINE OPERATING ENVIRONMENT
*
         MVC   TUBEDDNM(8),2(R2) MOVE IN ENVIRONMENT INDICATOR
*
         CLC   TUBEDDNM(2),=C'OP'      OPERATOR TERMINAL
         BE    INITEXCP          YES
*
         CLC   TUBEDDNM(4),=C'ETPS'    USER     TERMINAL
         BE    INITEXCP          YES
*
         CLC   TUBEDDNM(4),=C'VTAM'    VTAM     TERMINAL
         BE    INITVTAM          YES
*
         CLC   TUBEDDNM(3),=C'TSO'     VTAM     TERMINAL
         BE    INITVTAM          YES
*
         WTO   'INVALID PARM',ROUTCDE=2,DESC=(2)
         DC    H'0'
*
*
INITEXCP DS    0H
*
* ALLOCATE TUBE
*
         MVC   DYNRB(DYNLENG),CONRB
*
         LA    R1,DYNDDNAM
         ST    R1,DYNTEXT1
*
         LA    R1,DYNDISP
         ST    R1,DYNTEXT2
*
         LA    R1,DYNUNIT
         ST    R1,DYNTEXT3
         OI    DYNTEXT3,X'80'
*
         LA    R1,DYNTEXT1
         ST    R1,TEXTPTRS
*
         LA    R1,DYNRB
         ST    R1,DYNPARM
         OI    DYNPARM,X'80'
*
* PICK UP PARM = DDNAME
*
         LA    R1,8
         CH    R1,0(,R2) IF PARM LENGTH IS NOT = 8
         BNE   WTOMS1       SHUT IT DOWN
*
         MVC   TUBEDDNM(8),2(R2) MOVE IN DDNAME FOR DYNALLOC
         MVC   TUBEID(8),2(R2)   ALSO SAVE IT
         MVC   UNITADDR(3),7(R2) MOVE IN UNIT ADDRESS
*
         MODESET KEY=ZERO,MODE=SUP
         LA    R1,DYNPARM
         DYNALLOC
         LR    R5,R15
         MODESET KEY=NZERO,MODE=PROB
*
         LTR   R15,R5
         BNZ   WTOMS2
*
         CLC   2(2,R2),=C'OP'    IS IT OPERATOR TERMINAL
         BE    *+14              YES, LET IT GO
         CLC   2(4,R2),SYSNAME   SYSTEM NAME
         BNE   WTOMS5       SHUT IT DOWN
*
         MVI   PERMERR,0                FIRST TIME THROUGH
*
         MVI   PFKFLAG,0                NOT A PFK/ATTN
*
*
INITVTAM DS    0H
*
         MVC   SUPRMOD(SMODLEN),SMOD
         MVC   PROBMOD(PMODLEN),PMOD
*
         MVI   USUALWCC,X'C3'           NO ALARM
         MVI   ALARMWCC,X'C7'           ALARM
*
         L     R1,=A(BUFF3278)          3278 SBA CODES
         ST    R1,SBACODES              SET DEFAULT
         MVI   SBACODE,X'11'            DEFAULT SBA
*
         CLC   TUBEDDNM(4),=C'VTAM'    VTAM     TERMINAL
         BE    SETSCVTM          YES
*
         CLC   TUBEDDNM(3),=C'TSO'     TSO      TERMINAL
         BE    SETSCTSO          YES
*
         MVI   WRITECC,X'0D'     SET ERASE/WRITE ALTERNATE
         CLI   TUBEID+4,C'3'     IS IT A MOD3
         BE    ITSMOD3              GO SET IT
         CLI   TUBEID+4,C'4'     IS IT A MOD4
         BE    ITSMOD4              GO SET IT
*
         MVI   WRITECC,5         SET DEFAULT COMMAND CODE  W.A.M.
*
         CLI   TUBEID+4,C'A'     IS IT A 3278-2A           W.A.M.
         BE    ITSMOD2A             SET 20 LINES           W.A.M.
*                                ASSUME 3278-2
         LA    R1,1920           DEFAULT BUFF SIZE
         ST    R1,SCREENSZ       SET IT
         LA    R15,24            DEFAULT NUMBER OF ROWS
         ST    R15,SCROWS        SET IT
*
         B     SETSCRSZ             USE DEFAULT
*
SMOD     MODESET KEY=ZERO,MODE=SUP,MF=L
SMODLEN  EQU   *-SMOD
PMOD     MODESET KEY=NZERO,MODE=PROB,MF=L GET BACK IN PROB STATE
PMODLEN  EQU   *-PMOD
ITSMOD3  DS    0H
         LA    R1,2240           MOD 3 BUFF SIZE
         ST    R1,SCREENSZ       SET IT
         LA    R15,28            DEFAULT NUMBER OF ROWS
         ST    R15,SCROWS        SET IT
         B     SETSCRSZ          ALL SET
*
ITSMOD4  DS    0H
         LA    R1,3440           MOD 4 BUFF SIZE
         ST    R1,SCREENSZ       SET IT
         LA    R15,43            DEFAULT NUMBER OF ROWS
         ST    R15,SCROWS        SET IT
         B     SETSCRSZ          ALL SET
*
ITSMOD2A DS    0H                                         W.A.M.
         LA    R1,1600           MOD 2A BUFF SIZE         W.A.M.
         ST    R1,SCREENSZ       SET IT                   W.A.M.
         LA    R15,20            DEFAULT NUMBER OF ROWS   W.A.M.
         ST    R15,SCROWS        SET IT                   W.A.M.
         B     SETSCRSZ          ALL SET                  W.A.M.
*
SETSCVTM DS    0H
*
         USING PSA,0
         L     R1,PSATOLD        PICK UP TCB
         USING TCB,1
         L     R1,TCBUSER        TCBUSER FIELD
         USING TCBUVTAM,1
*
         SR    R15,R15
         IC    R15,TCBUROWS
         ST    R15,SCROWS        SET IT
         ST    R15,SCROWS1       SET IT
         IC    R15,TCBUCOLS
         MH    R15,SCROWS+2
         ST    R15,SCREENSZ      SET IT
*
         XC    SCROWS2(4),SCROWS2
         MVC   ROWFILL(8),DUMMYROW
         B     INITBUF
*
SETSCTSO DS    0H
*
         GTSIZE
*
         LTR   R15,R15
         BNZ   TSOBAD1
*
         ST    R0,SCROWS        SET IT
         ST    R0,SCROWS1       SET IT
         MH    R1,SCROWS+2
         ST    R1,SCREENSZ      SET IT
*
         XC    SCROWS2(4),SCROWS2
         MVC   ROWFILL(8),DUMMYROW
*
 TPUT ERASE,14,FULLSCR
         B     INITBUF
*
ERASE DC X'C1115D7E1140403C4040001DC813'
TSOBAD1  DS    0H
*
         WTO   'GTSIZE FAILED',ROUTCDE=2,DESC=(2)
         DC    H'0'
*
SETSCRSZ DS    0H
         MVC   SCROWS1(4),SCROWS
         XC    SCROWS2(4),SCROWS2
*
         MVC   ROWFILL(8),DUMMYROW
*
*
OPENTUBE DS    0H
*
* OPEN TUBE
         MVC   TUBE(56),TINIT
         MVC   TUBE+40(8),TUBEDDNM MOVE IN DDNAME
* SET IOB ADDRESS
         LA    R2,TUBEIOB        SET IOB ADDRESS
         STCM  R2,7,TUBE+29                       IN THE DCB
         LA    R2,TUBE           GET DCB ADDRESS
*
         MVI   PARMOP,128
         OPEN  ((2)),MF=(E,PARMOP)
*
         TM    48(R2),X'10'      GOOD OPEN
         BZ    WTOMS7            NOPE
*
** ALL THIS STUFF IS SETTING UP THE ETPS ACCESS METHOD FOR TUBE I/O
*
         CLI   EXCPFLG1,0            ETPS ACCESS METHOD
         BE    ETPSEXCP              NO
*
         L     R3,44(,R2) GET DEB ADDRESS
         LA    R3,0(,3)  CLEAR LEFT BYTE
         MODESET MF=(E,SUPRMOD)
*                                         BECOME NON-SWAPPABLE
         LRA   R1,0(,R3)  GET REAL ADDRESS OF DEB
         ST    R1,DEBADDR  SAVE IT
         MVI   DEBADDR,X'01'  DEVICE INDEX
         L     R6,X'20'(,R3)      LOAD THE UCB ADDRESS FOR THE TUBE
         ST    R6,UCBADDR         SAVE THE UCB ADDRESS FOR TUBE-READ
         USING UCBCMSEG,6
         MVC   UCBCTLNK(4),DEBADDR    MOVE IN DEB ADDRESS (REAL)
         MVI   UCBATNCT,0             CLEAR ATTENTION COUNT
         MVI   UCBATNCT+1,X'06'       TURN ON SKIP FLAG, RI PENDING
         MVI   UCBIRBA+2,X'01'        SET IRB ADDRESS TO NON-ZERO
         L     R4,UCBEXTPT            UCB EXTENSION
         USING UCBCMEXT,4
*
* SET ATTENTION EXIT. X'08' IS DESIGNATED FOR PROGRAM PRODUCTS.
*
         USING PSA,0
         L     1,FLCCVT          CVT POINTER
         USING CVT,1
         L     1,CVTIXAVL        IOS COMM ADDRESS
         USING IOCOM,1
         L     1,IOCATTBL        ATTENTION TABLE
         LA    R1,ATBLEN(,1)     BUMP TO X'08' SECOND ENTRY
         LA    R2,ATBLEN(,1)     BUMP TO X'10' THIRD ENTRY, OURS
         USING ATB,2
*
         CLC   ATBRTN(4),=X'00000000'  NO ROUTINE
         BE    GOTATB                  YUP, USE THIS ONE
*
         LA    15,ATXTLEN     PICK UP SIZE OF CSECT
         BCTR  15,0           SUBTRACT ONE FOR EX INSTRUCTION    W.A.M.
         L     14,=A(ETPSATEN)
         L     R1,ATBRTN
         EX    15,ATXTCLC
         BE    UCBATION
*        DC    H'0'                    JUST ABEND, CAN'T FIND ATB
*
GOTATB   DS    0H
*
         LA    R0,ATXTLEN+80  PICK UP SIZE OF CSECT
         GETMAIN RU,LV=(0),SP=228
         ST    R1,ATBRTN
         LA    15,ATXTLEN     PICK UP SIZE OF CSECT
         BCTR  15,0           SUBTRACT ONE FOR EX INSTRUCTION    W.A.M.
         L     14,=A(ETPSATEN)
         EX    15,ATXTMVC
         B     UCBATION
*
ATXTMVC  MVC   0(1,R1),0(R14)
ATXTCLC  CLC   0(1,R1),0(R14)
EXCPFLG1 DC    X'00'     0=STRAIGHT EXCP, 1=ETPS ACCESS METHOD
UCBATION DS    0H
         MVI   UCBATI,X'08'          ETPSATEN
         XC    UCBRDYQ(4),UCBRDYQ    CLEAR ECB
         DROP  6
         DROP  4
         MODESET MF=(E,PROBMOD)
*
ETPSEXCP DS    0H
*
** END OF ETPS ACCESS METHOD SETUP
*
         XC    RECB(4),RECB   CLEAR ECB
         OI    RECB,X'40'        INIT RECB TO POSTED
* INITIALIZE IOB
         MVC   TUBEIOB(40),IOBLANK
         LA    R2,TUBE           SET DCB ADDRESS
         ST    R2,TUBEIOB+20                      IN THE IOB
INITBUF  DS    0H
* SET SCREEN BUFFER ADDRESSES
         L     R1,SCRNOFFA
         AR    R1,R13
*        LA    R1,SCREENIO
         ST    R1,BUFF
         LA    R1,4095(,1)
         ST    R1,REPLY
         LA    R1,4095(,1)
         ST    R1,TERMINPT
         LA    R1,4095(,1)
         ST    R1,LINES
*
         LA    R1,4095(,1)
         SRL   R1,2                    FULL-WORD ALIGN
         SLL   R1,2                    FULL-WORD ALIGN
         ST    R1,DYNWORK1
         LA    R1,DYNRCODE
         ST    R1,DYNWORK2
         OI    DYNWORK2,X'80'
*
         MVI   SPLIT,0                  NOT IN SPLIT SCREEN
*
         LA    R14,WCCFLAG              "WCC" IS USED TO SET THE ALARM
         ST    R14,TERMOUT              NOW BUILD INADDRESSES
*
         LA    R14,TERMOUT+4            NOW BUILD INADDRESSES
         LA    R1,43                    LOOP CONTROL
         L     R15,LINES                FIRST LINE ADDRESS
*
         ST    R15,0(,R14)              STORE THE ADDRESS
         LA    R15,93(,15)              BUMP 93 BYTES
         LA    R14,4(,14)               BUMP TO NEXT INADDR
         BCT   R1,*-12                  LOOP 43 TIMES
*
         MVI   WCCFLAG,C'A'             ALARM REQUESTED
*
         LA    R14,TERMINPT+4           NOW BUILD INADDRESSES
         LA    R1,43                    LOOP CONTROL
         L     R15,TERMINPT             FIRST LINE ADDRESS
         LA    R15,93(,15)              BUMP 93 BYTES
         MVI   0(R15),X'00'             SET FIRST BYTE TO ZEROS
*
         ST    R15,0(,R14)              STORE THE ADDRESS
         LA    R15,93(,15)              BUMP 93 BYTES
         MVI   0(R15),X'00'             SET FIRST BYTE TO ZEROS
         LA    R14,4(,14)               BUMP TO NEXT INADDR
         BCT   R1,*-16                  LOOP 43 TIMES
*
         LA    R0,LIBDCB1
         LA    R1,MEMDCB1
         LA    R2,PRTDCB1
         LA    R3,COPDCB1
         LA    R4,LIBDCB2
         LA    R5,MEMDCB2
         LA    R6,PRTDCB2
         LA    R7,COPDCB2
*
         MVC   LIBDCB1(88),DIRDCB
         MVC   LIBDCB2(88),DIRDCB
         MVC   LIBDCB1+40(8),=CL8'LIBDCB1'
         MVC   LIBDCB2+40(8),=CL8'LIBDCB2'
         MVC   MEMDCB1(88),SEQDCB
         MVC   MEMDCB2(88),SEQDCB
         MVC   MEMDCB1+40(8),=CL8'MEMDCB1'
         MVC   MEMDCB2+40(8),=CL8'MEMDCB2'
         MVC   PRTDCB1(88),OUTDCB
         MVC   PRTDCB2(88),OUTDCB
         MVC   PRTDCB1+40(8),=CL8'PRTDCB1'
         MVC   PRTDCB2+40(8),=CL8'PRTDCB2'
         MVC   COPDCB1(88),SEQDCB
         MVC   COPDCB2(88),SEQDCB
         MVC   COPDCB1+40(8),=CL8'COPDCB1'
         MVC   COPDCB2+40(8),=CL8'COPDCB2'
*
         DROP  R10
         LA    R10,SPLITWRK       SET DSECT BASE FOR SPLIT-SCREEN
         USING SPLTAREA,10
*
         ST    R0,LIBDCBA
         ST    R1,MEMDCBA
         ST    R2,PRTDCBA
         ST    R3,COPDCBA
*
         LR    R14,R13                 SAVE THE SAVE AREA ADDRESS
         L     R1,SPLIT2AD
         AR    R13,R1
*
         LA    R10,SPLITWRK
         ST    R4,LIBDCBA
         ST    R5,MEMDCBA
         ST    R6,PRTDCBA
         ST    R7,COPDCBA
         LR    R13,R14                RESET THE SAVE AREA ADDRESS
         LA    R10,SPLITWRK
         MVI   PRIMEFLG,0               NOT IN PRIMARY OPTION MENU
*
         MVC   SUBRB(SUBLENG),SUBCONRB
*
         LA    R1,SUBRB
         ST    R1,SUBSUBP
         OI    SUBSUBP,X'80'
*
INITDONE DS    0H
         L     R14,INIT14
         BR    R14
*
         TITLE 'ETPS - SUBMIT DYNAMIC ALLOCATION PARAMETERS, ETC'
*
GETSIZE  DC    A(GMSIZE)
SCRNOFFA DC    A(SCRNOFFS)
SPLIT1AD DC    A(SPLIT100)    OFFSET OF SPLIT2 AREA
SPLIT2AD DC    A(SPLIT200)    OFFSET OF SPLIT2 AREA
*
         DS    0F
SUBCONRB DC    X'14'           LENGTH = 20
         DC    X'01'           DSNAME ALLOCATION
         DC    X'00'           FLAGS1 = ALLOW OFFLINE UNITS
         DC    X'00'           REST OF FLAGS1
         DC    F'0'            RETURN CODES
         DC    A(SUBTEXT1)     POINTER TO SUBTEXT1
         DC    F'0'            RESERVED
         DC    F'0'            FLAGS2
*
SUBLENG  EQU   *-SUBCONRB
*
SUBTEXT1 DC    A(SUBDDNAM)     POINTER TO DDNAME
SUBTEXT2 DC    A(SUBCLS)       POINTER TO SYSOUT CLASS
SUBTEXT3 DC    A(SUBFRE)       POINTER TO FREE=CLOSE
SUBTEXT4 DC    X'80',AL3(SUBPGM) POINTER TO INTRDR
*
SUBTEXT5 DC    X'80',AL3(SUBDDNAM)          DDNAME FOR FREE
*
         DS    0H
SUBDDNAM DC    X'0001'         DDNAME=  PARAMETER
         DC    X'0001'         NUMBER OF TEXT UNITS
         DC    X'0006'         LENGTH OF PARM
         DC    CL8'SUBJOB'     DDNAME
*
         DS    0H
SUBCLS   DC    X'0018'         SYSOUT= PARAMETER
         DC    X'0001'         NUMBER OF TEXT UNITS
         DC    X'0001'         LENGTH OF PARM
         DC    C'A'            SYSOUT CLASS
*
         DS    0H
SUBPGM   DC    X'0019'         INTRDR PARAMETER
         DC    X'0001'         NUMBER OF TEXT UNITS
         DC    X'0006'         LENGTH OF PARM
         DC    C'INTRDR'       PGM NAME
*
         DS    0H
SUBFRE   DC    X'001C'         FREE=CLOSE
         DC    X'0000'         NUMBER OF TEXT UNITS
*
IOBLANK  DC    X'0200'
         DC    X'0000'
         DC    X'00000000'              ECB
         DC    X'0000000000000000'
         DC    X'00000000'              CH PGM
         DC    X'00000000'              DCB ADD
         DC    X'00000000'
         DC    X'00000000'
         DC    X'0000000000000000'      UCB INDEX
*
DUMMYROW DC    AL1(6),X'04',CL5'     '
*
         ENTRY SEQDCB
SEQDCB   DCB   DSORG=PS,MACRF=(GM,PM),DDNAME=DUMMY,EODAD=DUMMYROW
OUTDCB   DCB   DSORG=PS,MACRF=(PM),DDNAME=DUMMY
DIRDCB   DCB      DSORG=PS,MACRF=(RP,WP),DDNAME=PPROCLIB,RECFM=F,      C
               LRECL=256,BLKSIZE=256,KEYLEN=8,EODAD=DIRENDOF
         TITLE 'ETPS - LOGON PROCESSING'
LOGON    DS    0H
         ST    R14,LOG14
*
*              PROMPT FOR LOGON AND PASSWORD
*
RELOGON  DS    0H
*
         MVI   SPLIT,0                  NOT IN SPLIT SCREEN
         MVI   PRIMEFLG,0               NOT IN PRIMARY OPTION MENU
*
         CLC   TUBEDDNM(2),=C'OP'      OPERATOR TERMINAL
         BE    OPRLOGON          YES, NO LOGON
*
         CLC   TUBEDDNM(4),=C'VTAM'    VTAM     TERMINAL
         BE    VTMLOGON          YES, NO LOGON
*
         CLC   TUBEDDNM(3),=C'TSO'     VTAM     TERMINAL
         BE    TSOLOGON          YES, NO LOGON
*
         CLC   TUBEID(4),=C'VTAM'     VTAM?
         BE    VTMLOGON          YES, NO LOGON
*
         L     R15,=A(LOGHELP)     DEFAULT HELP SCREEN
         ST    R15,HELPADD
*
         XC    MSGADD(4),MSGADD    NO MESSAGE AT UPPER-RIGHT
*
         MVI   WCCFLAG,C'A'             ALARM REQUESTED
*
         L     R6,=A(LOGSCREE)
         BAL   R14,SCRNBLD
*
         MVI   AIDROW,16           CURSOR ROW
         MVI   AIDCOL,32           CURSOR COLUMN
         MVC   HOMEAID(2),AIDADDR  COPY
*
         L     R15,=A(COMM2)       GO COMMUNICATE
         BALR  R14,R15
*
         MVI   WCCFLAG,C'N'          NO ALARM REQUESTED
         L     R2,TERMINPT           PICK UP RESPONSES
         LM    R4,R6,TERMINPT+68     PICK UP RESPONSES     W.A.M.
*
         LTR   R15,15                   IS IT A BAD RETURN CODE
         BNZ   CLOSETB                  YUP, GO EOJ
*
         CLI   PFKFLAG,0                IS IT A PF KEY/ATTN
         BE    CHECKEOJ              NOPE, SEE IF IT IS EOJ
*
         CLI   0(R2),X'6E'           IS IT PA2
         BE    RELOGON               YUP, RESHOW THE SCREEN
         B     CLOSETB              NOPE- GO CLOSE THE TUBE AND EOJ
*
CHECKEOJ DS    0H
         CLC   33(3,R4),=CL3'EOJ'    ALL DONE?
         BE    CLOSETB               YUP- GO CLOSE THE TUBE AND EOJ
         CLC   33(3,R4),=CL4'DUMP'   TAKE A DUMP??
         BNE   CHECKUSR              NO, GO CHECK THE USER-ID
         DC    H'0'                  YUP- TAKE AN S0C1
CHECKUSR DS    0H
         LA    R1,USERTABL           VALID USERS
USERTOP  DS    0H
         CLC   0(5,R1),SPACES        END OF TABLE
         BE    RELOGON                NO- GO REPROMPT
         CLC   33(5,R4),0(R1)           AUTHORIZED USERID
         BE    GOODUSER               YUP
         LA    R1,5(,R1)             BUMP TO NEXT USER IN TABLE
         B     USERTOP               LOOP
USERTABL DS    0H
         DC    CL5'ASQCC'
SPACES   DC    CL8' '
MASTPASS DC    CL8'XXXXXXXX'
GOODUSER DS    0H
         MVC   USERID(5),33(R4)       SAVE USERID
         MVC   USERPASS(8),33(R5)    SAVE PASSWORD
         MVC   RACGROUP(8),33(R6)    SAVE RACF GROUP ID
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*   NOTE ADD PASSWORD CHECKING HERE
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
ENDLOGON DS    0H
         L     R14,LOG14
         BR    R14
OPRLOGON DS    0H
         MVC   USERID(5),USERTABL     SET USERID=FIRST IN TABLE
         MVC   USERPASS(8),MASTPASS      PASSWORD IS MASTER PASSWORD
         B     ENDLOGON
*
VTMLOGON DS    0H
         L     R1,PSATOLD        PICK UP TCB
         USING TCB,1
         L     R1,TCBUSER        TCBUSER FIELD
         USING TCBUVTAM,1
*
         MVC   USERID(8),TCBUSRID     SET USERID
         MVC   USERPASS(8),TCBUPASS  AND PASSWORD
         B     ENDLOGON
*
TSOLOGON DS    0H
         MVI   USERPASS,C' '
         MVC   USERPASS+1(7),USERPASS    CLEAR PASSWORD
         B     ENDLOGON
         L     R1,PSATOLD        PICK UP TCB
         USING TCB,1
         L     R1,TCBTIO         TCB TIOT
         USING TIOT,1
         MVC   USERID(8),TIOCNJOB     SET USERID=JOBNAME
         L     R1,PSAAOLD
         USING ASCB,1
         L     R1,ASCBTSB
         USING TSB,1
         MODESET MF=(E,SUPRMOD)
         MVC   USERPASS(8),TSBPSWD   SET PASSWORD=TSO LOGON PASSWORD
         MODESET MF=(E,PROBMOD)
         B     ENDLOGON
*
         TITLE 'ETPS - DISPLAY PRIMARY OPTION MENU'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*
PRIOPT   DS    0H
*
         CLI   SPLIT,0                AM I IN SPLIT-SCREEN?
         BNE   REPRIOPT               YUP
         STM   R2,R13,PRISPLIT        SAVE REGISTERS
*
REPRIOPT DS    0H
         ENTRY REPRIOPT
*
REPRIMSG DS    0H
         L     R15,=A(POPHELP)     DEFAULT HELP SCREEN
         ST    R15,HELPADD
*
         MVI   PRIMEFLG,1          ON SCREEN1
*
PRIBLD   DS    0H
*
         L     R6,=A(PRIMOPT)
         BAL   R14,SCRNBLD
*
         MVI   AIDROW,01           CURSOR ROW
         MVI   AIDCOL,21           CURSOR COLUMN
         MVC   HOMEAID(2),AIDADDR  COPY
*
         L     R15,=A(COMM2)       GO COMMUNICATE
         BALR  R14,R15
*
         LTR   R15,15              NON-ZERO
         BNZ   PRIEND              YUP  - GET OUT
*
         MVI   PRIMEFLG,0          ON SCREEN1
*
         LM    R2,R5,TERMINPT        PICK UP RESPONSES
*
         CLI   SPLIT,2                  AM I ON SCREEN 2
         BNE   PRIBLD2                  NOPE
*
         L     R3,SCROWS1          PICK UP SIZE OF SCREEN1
         SLL   R3,2                MULTIPLY BY 4
         LA    R3,TERMINPT(R3)     PICK UP LOCATION OF RESPONSES
         LM    R3,R5,4(R3)         PICK UP RESPONSES
*
PRIBLD2  DS    0H
         CLI   PFKFLAG,0                IS IT A PF KEY/ATTN
         BNE   PRIPFK                   YUP
*
         CLI   22(R4),C'X'         END
         BE    PRIEND              YUP  - GO RESHOW THE LOGON MENU
*
         B     PARSEOPT            NOPE - GO SEE WHAT OPTION WAS INPT
*
PRIPFK   DS    0H
*
*
         CLI   0(R2),X'F3'         IS IT PF3
         BE    PRIEND              YUP  - GO RESHOW THE LOGON MENU
         CLI   0(R2),X'C3'         IS IT PF15
         BE    PRIEND              YUP  - GO RESHOW THE LOGON MENU
*
         CLI   0(R2),X'6C'         IS IT PA1
         BE    PRIEND              YUP  - GO RESHOW THE LOGON MENU
*
         CLI   0(R2),X'6E'         IS IT PA2
         BE    REPRIOPT            YUP  - GO RESHOW THE PRIMOPT
*
         CLI   0(R2),X'6D'         IS IT "CLEAR"
         BE    REPRIOPT            YUP  - GO RESHOW THE PRIMOPT
*
PRIEND   DS    0H
         CLC   TUBEDDNM(4),=C'VTAM'    VTAM     TERMINAL
         BE    RETRNEND          YES
*
         CLC   TUBEDDNM(3),=C'TSO'     VTAM     TERMINAL
         BE    RETRNEND          YES
*
         B     RELOGON
*
         TITLE 'ETPS - DETERMINE PRIMARY OPTION PROCESSOR'
PARSEOPT DS    0H
*
         CLI   22(R4),C'1'         BROWSE?
         BE    BROPT               GO DO IT
         CLI   22(R4),C'2'         EDIT?
         BE    EDOPT               GO DO IT
         CLI   22(R4),C'3'         UTILITIES?
         BE    UTLOPT              GO DO IT
         CLI   22(R4),C'4'         IDCAMS INTERFACE?
         BE    FOOPT               GO DO IT
*
         LA    R1,INVOPT
         ST    R1,MSGADD
         B     REPRIMSG   GO RESHOW THE PRIMARY OPTION MENU
*
INVOPT   DC    CL20' INVALID OPTION    '
         TITLE 'ETPS - BROWSE PROCESSING'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*
BROPT    DS    0H
*
         MVI   LIBFUNC,C'B'       INDICATE BROWSE
REBROPT  DS    0H
*
         L     R15,=A(BROHELP)      DEFAULT HELP SCREEN
         ST    R15,HELPADD
*
         L     R6,=A(BROWSCRN)
         BAL   R14,SCRNBLD
*
         MVI   AIDROW,02           CURSOR ROW
         MVI   AIDCOL,29           CURSOR COLUMN
         MVC   HOMEAID(2),AIDADDR  COPY
*
         L     R15,=A(COMM2)       GO COMMUNICATE
         BALR  R14,R15
*
         LTR   R15,15              BAD RC
         BNZ   BREND               YUP
*
         CLI   PFKFLAG,0           PFK/ATTN ENTERED?
         BE    BRPARSE             NOPE, GO BROWSE THE DATASET
*
         L     R2,TERMINPT           PICK UP RESPONSES
         CLI   0(R2),X'6E'           IS IT PA2
         BE    REBROPT               YUP, RESHOW THE SCREEN
         B     BREND               ELSE I'M DONE
*
BRPARSE  DS    0H
         B     EDCKINPT
*
BREND    DS    0H
         B     REPRIOPT
*
         TITLE 'ETPS - EDIT PROCESSING'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*
EDOPT    DS    0H
*
         MVI   LIBFUNC,C'E'       INDICATE FUNCTION
*
REEDOPT  DS    0H
*
         L     R15,=A(EDTHELP)     DEFAULT HELP SCREEN
         ST    R15,HELPADD
*
         L     R6,=A(EDITOPT)
         BAL   R14,SCRNBLD
*
         MVI   AIDROW,02           CURSOR ROW
         MVI   AIDCOL,29           CURSOR COLUMN
         MVC   HOMEAID(2),AIDADDR  COPY
*
         L     R15,=A(COMM2)
         BALR  R14,R15
*
         LTR   R15,15              BAD RC
         BNZ   EDTEND              YUP
*
EDCKINPT DS    0H
         LM    R2,R7,TERMINPT      PICK UP FOUR REPLY FIELDS
*
         CLI   SPLIT,2                  AM I ON SCREEN 2
         BNE   EDCKINP2                 NOPE
*
         L     R3,SCROWS1          PICK UP SIZE OF SCREEN1
         SLL   R3,2                MULTIPLY BY 4
         LA    R3,TERMINPT(R3)     PICK UP LOCATION OF RESPONSES
         LM    R3,R7,4(R3)         PICK UP RESPONSES
*
EDCKINP2 DS    0H
         CLI   PFKFLAG,0           PFK/ATTN ENTERED?
         BE    EDPARSE             NOPE, GO EDIT THE DATASET
*
         CLI   0(R2),X'6E'         IS IT PA2?
         BE    EDTPA2              YUP
         B     EDTEND              ELSE I'M DONE
*
EDTPA2   DS    0H
*
         CLI   LIBFUNC,C'B'       INDICATE FUNCTION
         BE    REBROPT             YES, RESHOW THE SCREEN
*
         CLI   LIBFUNC,C'U'       INDICATE FUNCTION
         BE    REUTOPT             YES, RESHOW THE SCREEN
*
         B     REEDOPT             ASSUME EDIT, RESHOW THE SCREEN
*
EDPARSE  DS    0H
*
         CLC   30(4,R5),=C'END '  END OF EDIT
         BE    EDTEND
*
         MVC   EDTDSN1(54),30(R5)
*
         MVC   DIRPASS(8),USERPASS SET DSN PASSWORD TO LOGON PASSWORD
         MVC   DIRVOL(6),SPACES
*
         CLI   0(R6),X'00'         IF NO VOLSER ENTERED LOOK FOR
         BE    *+16                PASSWORD
*
         MVC   DIRVOL(6),30(R6)  MOVE IN THE VOLSER
         MVC   EDTVOL1(6),30(R6)          THE VOLSER
*
         CLI   0(R7),X'00'         IF NO PASSWORD ENTERED,
         BE    *+10                PASSWORD
*
         MVC   DIRPASS(8),30(R7)   MOVE IN THE PASSWORD
*
NOVOLPAS DS    0H
*
*
         MVC   EDTPAS1(8),DIRPASS SET PASSWORD
*
         MVI   EDTDSN,C' '
         MVC   EDTDSN+1(43),EDTDSN
         LA    R14,EDTDSN1        START ADDRESS FOR DSN LENGTH CALC
         SR    R15,15             SET COUNTER
*
EDDSNLOP DS    0H
         CLI   0(R14),C'('        LEFT PAREN INDICATES MEMBER
         BE    EDDSNMEM           GOT ONE
         CLI   0(R14),C' '        BLANK IS END OF DSN
         BE    EDDSNEND           GOT END
*
         LA    R14,1(,14)         BUMP ADDRESS
         LA    R15,1(,15)         BUMP COUNTER
         B     EDDSNLOP           TEST NEXT BYTE
*
EDMOVDSN MVC   EDTDSN(1),EDTDSN1
EDDSNMEM DS    0H
         STC   R15,EDTDSN1L       SAVE LENGTH
*
         BCTR  R15,0              SUBTRACT1
         EX    R15,EDMOVDSN       SUBTRACT1
*
         LA    R14,1(,14)         BUMP ADDRESS
         LA    R15,EDTMEM1        SET ADDRESS OF MEMBER FIELD
         MVC   EDTMEM1(8),SPACES  INIT MEM FIELD
EDMEMLOP DS    0H
         CLI   0(R14),C')'       RIGHT PAREN INDICATES MEMBER END
         BE    EDTSTPAN           ALL DONE
         CLI   0(R14),C' '        BLANK IS END OF DSN
         BE    EDTSTPAN           ALL DONE
*
         MVC   0(1,R15),0(R14)    MOVE 1 CHARACTER
         LA    R14,1(,14)         BUMP ADDRESS - INPUT
         LA    R15,1(,15)         BUMP ADDRESS - OUTPUT
         B     EDMEMLOP           TEST NEXT BYTE
*
*
EDDSNEND DS    0H
         STC   R15,EDTDSN1L       SAVE LENGTH
*
         BCTR  R15,0              SUBTRACT1
         EX    R15,EDMOVDSN       SUBTRACT1
*
EDTSTPAN DS    0H
         L     R15,=A(PANAMES)    FIRST SEE IF IT'S A PAN LIBRARY
*
EDORGLP1 DS    0H
         CLI   0(R15),X'FF'       END OF TABLE
         BE    EDORGNPN           CAN'T BE A PAN LIBRARY
*
         CLC   1(44,R15),EDTDSN1  DSN MATCH
         BE    EDORGPAN           MUST BE A PAN LIBRARY
*
         LA    R15,45(,15)        BUMP TO NEXT PANAMES ENTRY
         B     EDORGLP1          TEST FOR A PAN LIBRARY
*
EDORGPAN DS    0H
         MVC   EDTORG1(3),=C'PAN' SET IT PAN
*
EDORGNPN DS    0H
*
EDSETVOL DS    0H
         OI    DIRVOL,C' '
         CLI   DIRVOL,C' '        WAS VOLSER PROVIDED?
         BNE   EDSETORG           YUP
*
         XC    CAT(16),CAT        CLEAR CAMLST AREA
         MVI   CAT,X'44'          SET FLAGS
         LA    R14,EDTDSN         SET DSN
         ST    R14,CAT+4
         LA    R14,CATINFO        SET OUTPUT AREA
         ST    R14,CAT+12
*
         LOCATE CAT
*
         LTR   R15,15             TEST RETURN CODE
         BNZ   EDBADLOC           IF NO GOOD, SET MESSAGE
*
         MVC   DIRVOL(6),CATINFO+6   SET THE VOLSER
         MVC   EDTVOL1(6),CATINFO+6   SET THE VOLSER
*
EDSETORG DS    0H
*
         XC    VTOC(16),VTOC      CLEAR CAMLST AREA
         MVI   VTOC,X'C1'         SET FLAGS
         LA    R14,EDTDSN         SET DSN
         ST    R14,VTOC+4
         LA    R14,DIRVOL         SET VOLSER
         ST    R14,VTOC+8
         LA    R14,VTOCINFO       SET OUTPUT AREA
         ST    R14,VTOC+12
*
         OBTAIN VTOC
*
         LTR   R15,15             CHECK RETURN CODE
         BNZ   EDBADOBT           IF NO GOOD, SET MESSAGE
*
         LA    R14,VTOCINFO       PICK UP VTOCINFO
*
         CLC   EDTORG1(3),=C'PAN' IS PAN ALREADY SET
         BE    EDFMTDIR               YUP
*
         MVC   EDTORG1(3),=C'SEQ' SET DEFAULT DSORG
         CLC   38(2,R14),=X'4000'     IS IT SEQUENTIAL
         BE    EDTSTLRC               YUP
*
         MVC   EDTORG1(3),=C'PDS' SET DEFAULT DSORG
*
         CLC   38(2,R14),=X'0200'     IS IT PO
         BE    EDTSTLRC               YUP
*
         B     EDBADORG           IF NO GOOD, SET MESSAGE
*
EDTSTLRC DS    0H
*
         CLI   LIBFUNC,C'E'       DOING EDIT?
         BNE   EDFMTDIR           NO, SKIP LRECL CHECK
*
         CLC   44(2,R14),=X'0050'   IS LRECL=80
         BNE   EDBAD80            IF NO GOOD, SET MESSAGE
*
EDFMTDIR DS    0H
         L     R15,=A(ETPSLIB2)   GO DO EDIT
*
         CALL  (15),(EDTDSN1,EDTDSN1L,DIRVOL,EDTORG1,DIRDD,            X
               USERPASS,EDTMEM1),VL,MF=(E,PARM43)
*
         MVI   EDTMEM1,C' '       RESET MEMBER NAME
         MVC   EDTMEM1+1(7),EDTMEM1
*
         B     REEDSCN            RETRY THE SCREEN
*
EDBADLOC DS    0H
         LA    R1,BADEDLOC
         ST    R1,MSGADD
         B     REEDSCN            RETRY THE SCREEN
BADEDLOC DC    CL20'EDIT - LOCATE FAILED'
*
EDBADOBT DS    0H
         LA    R1,BADEDOBT
         ST    R1,MSGADD
         B     REEDSCN            RETRY THE SCREEN
BADEDOBT DC    CL20'EDIT - OBTAIN FAILED'
*
EDBADORG DS    0H
         LA    R1,BADEDORG
         ST    R1,MSGADD
         B     REEDSCN            RETRY THE SCREEN
BADEDORG DC    CL20'EDIT - BAD DSORG    '
*
EDBAD80  DS    0H
         LA    R1,BADED80
         ST    R1,MSGADD
         B     REEDSCN            RETRY THE SCREEN
BADED80  DC    CL20'EDIT - LRECL NOT 80 '
*
REEDSCN  DS    0H
         CLI   LIBFUNC,C'B'       INDICATE FUNCTION
         BE    REBROPT             YES, RESHOW THE SCREEN
*
         CLI   LIBFUNC,C'U'       INDICATE FUNCTION
         BE    REUTOPT             YES, RESHOW THE SCREEN
*
         B     REEDOPT             ASSUME EDIT, RESHOW THE SCREEN
*
EDTEND   DS    0H
*
         CLI   LIBFUNC,C'B'       INDICATE FUNCTION
         BE    REBROPT             YES, RESHOW THE SCREEN
*
         CLI   LIBFUNC,C'U'       INDICATE FUNCTION
         BE    REUTOPT             YES, RESHOW THE SCREEN
*
         B     REPRIOPT
*
*
         TITLE 'ETPS - PDS UTILITY       PROCESSING'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*
UTLOPT   DS    0H
*
         MVI   LIBFUNC,C'U'       INDICATE FUNCTION
*
REUTOPT  DS    0H
*
         L     R15,=A(UT1HELP)     DEFAULT HELP SCREEN
         ST    R15,HELPADD
*
         L     R6,=A(UT1MENU)
         BAL   R14,SCRNBLD
*
         MVI   AIDROW,01           CURSOR ROW
         MVI   AIDCOL,21           CURSOR COLUMN
         MVC   HOMEAID(2),AIDADDR  COPY
*
         L     R15,=A(COMM2)
         BALR  R14,R15
*
         LTR   R15,15                   BAD RC
         BNZ   UTEND                    OUT
*
         L     R2,TERMINPT         PICK UP AID ADDRESS
         L     R3,TERMINPT+8               COMMAND INPUT LINE
         L     R5,TERMINPT+36              DSNAME FIELD
         L     R6,TERMINPT+40              VOLSER
         L     R7,TERMINPT+48              PASSWORD
*
         CLI   SPLIT,2                  AM I ON SCREEN 2
         BNE   UT1SEL2                  NOPE
*
         L     R3,SCROWS1          PICK UP SIZE OF SCREEN1
         SLL   R3,2                MULTIPLY BY 4
         L     R5,TERMINPT+36(R3)  PICK UP RESPONSES
         L     R6,TERMINPT+40(R3)  PICK UP RESPONSES
         L     R7,TERMINPT+48(R3)  PICK UP RESPONSES
         L     R3,TERMINPT+8(R3)   PICK UP COMMAND INPUT LINE
*
UT1SEL2  DS    0H
         CLI   PFKFLAG,0           PFK/ATTN ENTERED?
         BNE   REPRIOPT            YUP
*
         CLC   22(4,R3),=C'END '  END OF THIS SCREEN
         BE    REPRIOPT
*
         CLI   0(R3),X'00'        IF NO COMMAND ENTERED
         BE    UT1LIST            GO LIST THE LIBRARY
*
         CLI   22(R3),C'C'        IS IT A COMPRESS
         BE    UT1CPRS            YUP
*
*        LA    R0,30(,R5)         POINT TO DSN
*        LA    R1,30(,R6)         POINT TO VOLSER
*        LA    R2,30(,R7)         POINT TO PASSWORD
*        LA    R3,22(,R3)         POINT TO OPTION
*        DC    H'0'
*
         LA    R1,UT1BADOP
         ST    R1,MSGADD
         B     REUTOPT
*
UT1CPRS  DS    0H
*
         L     R1,DYNWORK1
         LA    R15,30(,R5)         POINT TO DSN
         ST    R15,0(,R1)
         LA    R15,30(,R6)         POINT TO VOLSER
         ST    R15,4(,R1)
         LA    R15,30(,R7)         POINT TO PASSWORD
         ST    R15,8(,R1)
*
         LINK  EP=ETPSCOMP
         LA    R1,UT1OKCP
         ST    R1,MSGADD
         LTR   R15,R15
         BZ    REUTOPT
*
         LA    R1,UT1BADCP
         ST    R1,MSGADD
         B     REUTOPT
*
UT1DLIST DS    0H
*
UT1DLBAD DS    0H
         LA    R1,UT1BADDL
         ST    R1,MSGADD
         B     REUTOPT
*
UT1FLIST DS    0H
*
UT1FLBAD DS    0H
         LA    R1,UT1BADLS
         ST    R1,MSGADD
         B     REUTOPT
*
UT1LIST  DS    0H
         MVC   EDTDSN1(54),30(R5)
*
         MVC   DIRPASS(8),USERPASS SET DSN PASSWORD TO LOGON PASSWORD
         MVC   DIRVOL(6),SPACES
*
         CLI   0(R6),X'00'         IF NO VOLSER ENTERED LOOK FOR
         BE    *+10                PASSWORD
*
         MVC   DIRVOL(6),30(R6)  MOVE IN THE VOLSER
*
         CLI   0(R7),X'00'         IF NO PASSWORD ENTERED,
         BE    *+10                PASSWORD
*
         MVC   DIRPASS(8),30(R7)   MOVE IN THE PASSWORD
*
         MVC   EDTPAS1(8),DIRPASS SET PASSWORD
*
         LA    R14,EDTDSN1        START ADDRESS FOR DSN LENGTH CALC
         SR    R15,15             SET COUNTER
*
U1DSNLOP DS    0H
         CLI   0(R14),C' '        BLANK IS END OF DSN
         BE    U1DSNEND           GOT END
*
         LA    R14,1(,14)         BUMP ADDRESS
         LA    R15,1(,15)         BUMP COUNTER
         B     U1DSNLOP           TEST NEXT BYTE
*
U1DSNEND DS    0H
         STC   R15,EDTDSN1L       SAVE LENGTH
         MVC   EDTMEM1(8),SPACES  INIT MEM FIELD
         MVC   EDTORG1(3),=C'PDS' SET IT PDS, BY DEFAULT
*
U1TSTPAN DS    0H
         L     R15,=A(PANAMES)    FIRST SEE IF IT'S A PAN LIBRARY
*
U1ORGLP1 DS    0H
         CLI   0(R15),X'FF'       END OF TABLE
         BE    U1ORGNPN           CAN'T BE A PAN LIBRARY
*
         CLC   1(44,R15),EDTDSN1  DSN MATCH
         BE    U1ORGPAN           MUST BE A PAN LIBRARY
*
         LA    R15,45(,15)        BUMP TO NEXT PANAMES ENTRY
         B     U1ORGLP1          TEST FOR A PAN LIBRARY
*
U1ORGPAN DS    0H
         MVC   EDTORG1(3),=C'PAN' SET IT PAN
*
U1ORGNPN DS    0H
*
U1SETVOL DS    0H
         CLI   DIRVOL,C' '        WAS VOLSER PROVIDED?
         BNE   U1SETORG           YUP
*
         XC    CAT(16),CAT        CLEAR CAMLST AREA
         MVI   CAT,X'44'          SET FLAGS
         LA    R14,EDTDSN1        SET DSN
         ST    R14,CAT+4
         LA    R14,CATINFO        SET OUTPUT AREA
         ST    R14,CAT+12
*
         LOCATE CAT
*
         LTR   R15,15             TEST RETURN CODE
         BNZ   UT1LBAD            IF NO GOOD, RETRY THE SCREEN
*
         MVC   DIRVOL(6),CATINFO+6   SET THE VOLSER
         B     U1SETORG
*
UT1LBAD  DS    0H
*
         LA    R1,U1BADLOC
         ST    R1,MSGADD
         B     REUTOPT
*
UT1OBAD  DS    0H
*
         LA    R1,U1BADOBT
         ST    R1,MSGADD
         B     REUTOPT
*
UT1ORBAD DS    0H
*
         LA    R1,U1BADORG
         ST    R1,MSGADD
         B     REUTOPT
*
UT1LRBAD DS    0H
*
         LA    R1,U1BADLRE
         ST    R1,MSGADD
         B     REUTOPT
*
U1SETORG DS    0H
*
         XC    VTOC(16),VTOC      CLEAR CAMLST AREA
         MVI   VTOC,X'C1'         SET FLAGS
         LA    R14,EDTDSN1        SET DSN
         ST    R14,VTOC+4
         LA    R14,DIRVOL         SET VOLSER
         ST    R14,VTOC+8
         LA    R14,VTOCINFO       SET OUTPUT AREA
         ST    R14,VTOC+12
*
         OBTAIN VTOC
*
         LTR   R15,15             CHECK RETURN CODE
         BNZ   UT1OBAD            IF NO GOOD, RETRY THE SCREEN
*
         LA    R14,VTOCINFO       PICK UP VTOCINFO
*
         CLC   EDTORG1(3),=C'PAN' IS PAN ALREADY SET
         BE    U1FMTDIR               YUP
*
         CLC   38(2,R14),=X'0200'     IS IT PO
         BNE   UT1ORBAD               NOPE
*
         CLC   44(2,R14),=X'0050'   IS LRECL=80
         BNE   UT1LRBAD           IF NO GOOD, RETRY THE SCREEN
*
U1FMTDIR DS    0H
         L     R15,=A(ETPSLIB2)   GO DO PDS UTILITY
*
         CALL  (15),(EDTDSN1,EDTDSN1L,DIRVOL,EDTORG1,DIRDD,            X
               USERPASS,EDTMEM1),VL,MF=(E,PARM43)
*
         MVI   EDTMEM1,C' '       RESET MEMBER NAME
         MVC   EDTMEM1+1(7),EDTMEM1
*
         B     REUTOPT            RESHOW THE SCREEN
*
U1BADLOC DC    CL20'DATASET NOT CATALOGD'
U1BADOBT DC    CL20'VTOC OBTAIN FAILED  '
U1BADORG DC    CL20'INVALID DSORG       '
U1BADLRE DC    CL20'INVALID LRECL-NOT 80'
UT1BADCP DC    CL20'COMPRESS FAILED     '
UT1OKCP  DC    CL20'COMPRESS SUCCESSFUL '
UT1BADDL DC    CL20'DIRECTORY LIST FAILD'
UT1BADLS DC    CL20'DATASET LIST FAILED '
UT1BADOP DC    CL20'INVALID OPTION      '
*
UTEND    DS    0H
         B     REPRIOPT
*
         TITLE 'ETPS - FOREGROUND    PROCESSING'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*
FOOPT    DS    0H
*
*
         LOAD  EP=ETPSIDC2
         LR    R15,R0
         BALR  R14,R15
       DELETE  EP=ETPSIDC2
*
         B     REPRIOPT
*
         TITLE 'ETPS - BACKGROUND    PROCESSING'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*
BAOPT    DS    0H
         L     R15,=A(NOHELP)      DEFAULT HELP SCREEN
         ST    R15,HELPADD
*
         L     R6,=A(NOTGOT)
         BAL   R14,SCRNBLD
*
         MVI   AIDROW,02           CURSOR ROW
         MVI   AIDCOL,31           CURSOR COLUMN
         MVC   HOMEAID(2),AIDADDR  COPY
*
         L     R15,=A(COMM2)
         BALR  R14,R15
*
BAEND    DS    0H
         B     REPRIOPT
*
         TITLE 'ETPS - COMMAND       PROCESSING'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*
CDOPT    DS    0H
         L     R15,=A(NOHELP)      DEFAULT HELP SCREEN
         ST    R15,HELPADD
*
         L     R6,=A(NOTGOT)
         BAL   R14,SCRNBLD
*
         MVI   AIDROW,02           CURSOR ROW
         MVI   AIDCOL,31           CURSOR COLUMN
         MVC   HOMEAID(2),AIDADDR  COPY
*
         L     R15,=A(COMM2)
         BALR  R14,R15
*
CDEND    DS    0H
         B     REPRIOPT
*
*
*
         TITLE 'ETPS - USER WTOS'
         PRINT NOGEN
WTOMS1   WTO   'ETPS1001 INVALID PARM LENGTH',ROUTCDE=2,DESC=(2)
         B     RETRNEND
WTOMS2   WTO   'ETPS1002 COULD NOT ALLOCATE TUBE          ',           X
               ROUTCDE=2,DESC=(2)
         L     R1,DYNPARM
         DC    H'0'
         B     RETRNEND
WTOMS4   WTO   'ETPS1004 COULD NOT FREE TUBE              ',           X
               ROUTCDE=2,DESC=(2)
         B     RETRNEND
WTOMS5   WTO   'ETPS1005 INVALID PARM',ROUTCDE=2,DESC=(2)
         B     RETRNEND
WTOMS6   WTO   'ETPS1006 UCB LOOKUP FAILED',ROUTCDE=2,DESC=(2)
         B     RETRNEND
WTOMS7   WTO   'ETPS1007 COULD NOT OPEN TUBE              ',           X
               ROUTCDE=2,DESC=(2)
         DC    H'0'
         B     FREETUBE
WTOMS21  WTO   'ETPS1021 TUBE I/O ERROR                   ',           X
               ROUTCDE=2,DESC=(2)
         B     CLOSETB
WTOMS22  WTO   'ETPS1041 TUBE I/O ERROR                   ',           X
               ROUTCDE=2,DESC=(2)
         B     CLOSETB
         TITLE 'ETPS - DATA AREAS AND CONSTANTS'
******** SCREEN DCB
         DS    0F
TINIT    DCB   DSORG=PS,MACRF=E,DDNAME=SCREEN,BUFL=4096,RECFM=U
         DS    0F
UPCASE   DC    CL80' '
*
ASTERS   DC    80C'*'
*
SBA3278  DC    A(BUFF3278)
*
         DS    0F
CONRB    DC    X'14'           LENGTH = 20
         DC    X'01'           DSNAME ALLOCATION
         DC    B'00000000'     FLAGS1 = ALLOW OFFLINE UNITS
         DC    X'00'           REST OF FLAGS1
         DC    F'0'            RETURN CODES
         DC    F'0'            POINTER TO TEXT POINTERS
         DC    F'0'            RESERVED
         DC    X'08000000'     FLAGS2, CONSIDER OFFLINE DEVICES
*
         DS    0H
CONDDNAM DC    X'0001'         DDNAME=  PARAMETER
         DC    X'0001'         NUMBER OF TEXT UNITS
         DC    X'0008'         LENGTH OF PARM
         DC    CL8'TUBE'       DDNAME
*
DISPOLD  DS    0H
CONDISP  DC    X'0004'         DISP=    PARAMETER
         DC    X'0001'         NUMBER OF TEXT UNITS
         DC    X'0001'         LENGTH OF PARM
         DC    X'01'           OLD
*
         DS    0H
CONUNIT  DC    X'0015'         UNIT= PARAMETER
         DC    X'0001'         NUMBER OF TEXT UNITS
         DC    X'0003'         LENGTH OF PARM
         DC    C'45B'          UNIT ADDRESS
*
DISPSHR  DC    X'0004'         DISP=    PARAMETER
         DC    X'0001'         NUMBER OF TEXT UNITS
         DC    X'0001'         LENGTH OF PARM
         DC    X'08'           SHR
         LTORG
         TITLE 'ETPS GENERAL SCREEN BUILD SUB ROUTINE'
SCRNBLD  DS    0H
*
* AT ENTRY, R6 POINTS TO THE ADDRESS OF THE SCREEN
*
         L     R4,SCROWS1               SCREEN 1
         LA    R3,TERMOUT+4             SECOND TERMOUT, FIRST LINE
         CLI   SPLIT,2                  AM I ON SCREEN 2
         BNE   SCRNBLD1                 NOPE, I'M ALL SET
*
         SLL   R4,2                     MULTIPLY OFFSET BY 4
         LA    R3,TERMOUT+4(R4)         POINT TO ROW 1 OF SCREEN 2
         L     R4,SCROWS2               NUMBER OF ROWS IN SCREEN 2
*
SCRNBLD1 DS    0H
         L     R2,0(,R6)                ADDRESS OF FROM FIELD
         L     R7,0(,R3)                ADDRESS OF "TO" FIELD
         IC    R1,0(,R2)                PICK UP LENGTH
         EX    R1,SCRNBLDM              MOVE IT
*
         CLI   PRIMEFLG,1               PRIMARY OPTION MENU
         BNE   SCRNBL11                 NOPE, SKIP THIS
         CLC   UOFF(11,R7),=C'USERID   - '     USERID
         BE    SCRNBL1U                 YES, MOVE USERID
         CLC   TIMEOFF(11,R7),=C'TIME     - '     TIME
         BE    SCRNBL1T                 YES, MOVE TIME
         CLC   TERMOFF(11,R7),=C'TERMINAL - '     TERMINAL
         BNE   SCRNBL11                 NOPE, SKIP THIS
*
         MVC   TERMOFF+11(1,R7),TERMTYPE
         MVC   TERMOFF+13(3,R7),TERMADDR
         B     SCRNBL11                 DO THE REST
*
SCRNBL1U DS    0H
         MVC   UOFF+11(7,R7),USERID     MOVE IT
         B     SCRNBL11                 DO THE REST
*
SCRNBL1T DS    0H
         TIME  DEC
         SRL   R0,12
         ST    R0,PACK8+4
         OI    PACK8+7,X'0F'
         UNPK  TIMEOFF+11(5,R7),PACK8+5(3)
         MVC   TIMEOFF+11(2,R7),TIMEOFF+12(R7)
         MVI   TIMEOFF+13(R7),C':'
         B     SCRNBL11                 DO THE REST
*
SCRNBL11 DS    0H
         TM    0(R6),X'80'              END OF PARMS?
         BO    SCRNBLD2                 YUP
         LA    R6,4(,6)                 BUMP TO NEXT PARM
         LA    R3,4(,3)                 BUMP TO NEXT PARM
         NI    0(R3),X'7F'              CLEAR THE "VL" BIT
         BCT   R4,SCRNBLD1              KEEP LOOPING
*
SCRNBLD2 DS   0H
         LTR   R4,4                     END OF SCREEN ROWS
         BZ    SCRNBLD4                 YES
         BCTR  R4,0                     SUBTRACT 1 FOR LUCK
         LTR   R4,4                     END OF SCREEN ROWS
         BZ    SCRNBLD4                 YES
         LA    R3,4(,3)                 BUMP TO NEXT PARM
         L     R7,0(,R3)                ADDRESS OF "TO" FIELD
*
SCRNBLD3 DS   0H
         MVC   0(8,R7),DUMMYROW         MOVE IN A DUMMY ROW
         NI    0(R3),X'7F'              CLEAR THE "VL" BIT
         LA    R3,4(,3)                 BUMP TO NEXT PARM
         L     R7,0(,R3)                ADDRESS OF "TO" FIELD
         BCT   R4,SCRNBLD3              KEEP LOOPING
         CLI   SPLIT,1                  AM I IN SPLIT-SCREEN, SCR 1
         BE    SCRNBLD4                 YUP, DON'T MOVE THIS IN
         MVC   0(8,R7),DUMMYROW         MOVE IN A DUMMY ROW
*
SCRNBLD4 DS   0H
*
         CLI   SPLIT,0                  AM I IN SPLIT-SCREEN
         BE    SCRNBLDZ                 NOPE, I'M ALL SET
*
         CLI   SPLIT,2                  AM I ON SCREEN 2
         BE    SCRNBLDZ                 YUP, GO OR THE PARM LIST
         BR    R14
*
SCRNBLDZ DS   0H
         OI    0(R3),X'80'              END OF TERMOUT PARMS
         BR    R14
*
SCRNBLDM MVC   0(1,R7),0(R2)
         LTORG
         TITLE 'ETPS - SCREEN FORMATS'
*
*  THE BUFFER ALWAYS STARTS WITH A WRITE CONTROL CHARACTER (WCC):
*
*    X'C7'  =  SOUND ALARM, RESET KEYBOARD, RESET MODIF DATA TAG BITS
*    X'C3'  =  RESET KEYBOARD, RESET MDT BITS
*
*
*  FOLLOWING THE WCC ARE ORDER CODE SEQUENCES AND DATA:
*
*    X'11'  =  SET BUFFER ADDRESS (SBA), FOLLOWED BY 2-BYTE BUFF ADDR
*    X'13'  =  INSERT CURSOR (IC)
*    X'1D'  =  START FIELD (SF), FOLLOWED BY 1-BYTE ATTRIBUTE CHARACTER
*
         TITLE 'ETPS - LOGON LOGO'
*
LOGSCREE CSECT
         DC    A(LOGSCR01)
         DC    A(LOGSCR02)
         DC    A(LOGSCR03)
         DC    A(LOGSCR04)
         DC    A(LOGSCR05)
         DC    A(LOGSCR06)
         DC    A(LOGSCR07)
         DC    A(LOGSCR08)
         DC    A(LOGSCR09)
         DC    A(LOGSCR10)
         DC    A(LOGSCR11)
         DC    A(LOGSCR12)
         DC    A(LOGSCR13)
         DC    A(LOGSCR14)
         DC    A(LOGSCR15)
         DC    A(LOGSCR16)
         DC    A(LOGSCR17)
         DC    A(LOGSCR18)
         DC    A(LOGSCR19)
         DC    X'80',AL3(LOGSCR20)
LOGSCR01 DS    0H
         DC    AL1(76),X'04'         , SBA = 0
         DC    40C'* '
LOGSCR02 DC    AL1(17),X'04',CL16'** EEEE        '
LOGSCR03 DC    AL1(76),X'04',CL16'** EE          '
 DC C'                    (((-----)))                             '
LOGSCR04 DC    AL1(76),X'04',CL16'** EEE         '
 DC C'                  ** (       )**                            '
LOGSCR05 DC    AL1(76),X'04',CL16'** EE          '
 DC C'                 *( (        ) )*                           '
LOGSCR06 DC    AL1(76),X'04',CL16'** EEEE MERGENCY'
 DC C'               ((                )                          '
LOGSCR07 DC    AL1(72),X'04',CL16'*   '
*DC C'              (  < (O) > < (O) >   )                        '
 DC C'              (  <',X'05'
 DC C'(O)',X'04'
 DC C'> <',X'05'
 DC C'(O)',X'04'
 DC C'>  )                        '
LOGSCR08 DC    AL1(76),X'04',CL16'** TTTTT       '
 DC C'               ((       (       )))                         '
LOGSCR09 DC    AL1(76),X'04',CL16'**   T         '
 DC C'               ((               )))                         '
LOGSCR10 DC    AL1(76),X'04',CL16'**   T         '
 DC C'                ((((    ___   ))))                          '
LOGSCR11 DC    AL1(76),X'04',CL16'**   T  ELE -  '
 DC C'                 ((((        ))))                           '
LOGSCR12 DC    AL1(76),X'04',CL16'*   '
 DC C'                    ((      ))                              '
LOGSCR13 DC    AL1(76),X'04',CL16'** PPPP        '
 DC C'                      )    /                                '
LOGSCR14 DC    AL1(76),X'04',CL16'*   '
 DC C'                      |( (|                                 '
LOGSCR15 DC    AL1(76),X'04',CL16'**  SS         '
 DC C'                      |) )|                                 '
LOGSCR16 DC    AL1(17),X'04',CL16'** S           '
LOGSCR17 DC    AL1(75),X'04',CL16'**  SS         '
         DC    CL14'ENTER USER ID:'
         DC    X'01'                 SF = UNPROT
         DC    CL43'              '
LOGSCR18 DC    AL1(75),X'04',CL16'**    S        '
         DC    CL14' AND PASSWORD:'
         DC    X'03'                 SF = UNPROT, NO DISPLAY
         DC    CL47'              '
LOGSCR19 DC    AL1(74),X'04',CL16'** SSS  YSTEM  '
         DC    CL14'   RACF GROUP:'
         DC    X'03'                 SF = UNPROT, NO DISPLAY
         DC    CL28'              '
         DC    X'05',CL17'VERSION 2.4      '
LOGSCR20 DC    AL1(80),X'04',79C'*'
LOGSCRSZ EQU   *-LOGSCREE
         TITLE 'ETPS - PRIMARY OPTION MENU'
PRIMOPT  CSECT
         DC    A(PRIMEL1)
         DC    A(PRIMEL2)
         DC    A(PRIMEL3)
         DC    A(PRIMEL4)
         DC    A(PRIMEL5)
         DC    A(PRIMEL6)
         DC    A(PRIMEL7)
         DC    A(PRIMEL8)
         DC    A(PRIMEL9)
         DC    A(PRIMEL10)
         DC    A(PRIMEL11)
         DC    X'80',AL3(PRIMEL12)
PRIMEL1  DS    0H
         DC    AL1(80)
         DC    X'05'                 SF PROTECTED, HIGH-LIGHT    ATTR
         DC    19C'-'
         DC    CL40'       ETPS  -  PRIMARY OPTION MENU     '
         DC    21C'-'
PRIMEL2  DC    AL1(80)
         DC    X'05'                 SF PROTECTED, HIGH-LIGHT    ATTR
         DC    CL19'SELECT OPTION ===> '
         DC    X'01'                 SF UNPROTECTED              ATTR
PRIMHOME EQU   *-PRIMOPT
         DC    CL60' '
PRIMEL3  DC    AL1(80)
         DC    X'04'                 SF   PROTECTED              ATTR
         DC    CL61' '
UOFF     EQU   *-PRIMEL3
         DC    CL11'USERID   - '
         DC    CL7'  '
PRIMEL4  DC    AL1(80)
         DC    X'05'                 SF PROTECTED, HIGH-LIGHT    ATTR
         DC    CL6'   1  '
         DC    X'04'                 SF   PROTECTED              ATTR
 DC CL54'BROWSE DATA                                           '
TIMEOFF  EQU   *-PRIMEL4
         DC    CL11'TIME     - '
         DC    CL8'  '
*
PRIMEL5  DC    AL1(80)
         DC    X'05'                 SF PROTECTED, HIGH-LIGHT    ATTR
         DC    CL6'   2  '
         DC    X'04'                 SF   PROTECTED              ATTR
 DC CL54'EDIT SOURCE DATA                                      '
TERMOFF  EQU   *-PRIMEL5
         DC    CL11'TERMINAL - '
         DC    CL8'  '
*
PRIMEL6  DC    AL1(80)
         DC    X'05'                 SF PROTECTED, HIGH-LIGHT    ATTR
         DC    CL6'   3  '
         DC    X'04'                 SF   PROTECTED              ATTR
 DC CL54'PDS UTILITY                                           '
         DC    CL19' '
*
PRIMEL7  DC    AL1(80)
         DC    X'05'                 SF PROTECTED, HIGH-LIGHT    ATTR
         DC    CL6'   4  '
         DC    X'04'                 SF   PROTECTED              ATTR
 DC CL54'IDCAMS (VSAM) INTERFACE                               '
         DC    CL19' '
*
PRIMEL8  DC    AL1(80)
         DC    X'05'                 SF PROTECTED, HIGH-LIGHT    ATTR
         DC    CL6'      '
         DC    X'04'                 SF   PROTECTED              ATTR
 DC CL54'                                                      '
         DC    CL19' '
*
PRIMEL9  DC    AL1(80)
         DC    X'05'                 SF PROTECTED, HIGH-LIGHT    ATTR
         DC    CL6'      '
         DC    X'04'                 SF   PROTECTED              ATTR
 DC CL54'                                                      '
         DC    CL19' '
*
PRIMEL10 DC    AL1(80)
         DC    X'05'                 SF PROTECTED, HIGH-LIGHT    ATTR
         DC    CL6'   X  '
         DC    X'04'                 SF   PROTECTED              ATTR
 DC CL54'EXIT       - TERMINATE ETPS AND RETURN TO LOGON MENU  '
         DC    CL19' '
*
PRIMEL11 DC    AL1(80)
         DC    X'04'                 SF PROTECTED                ATTR
         DC    CL79' '
*
PRIMEL12 DC    AL1(80)
         DC    X'04'                 SF   PROTECTED              ATTR
         DC    CL6'PRESS '
         DC    X'05'                 SF PROTECTED, HIGH-LIGHT    ATTR
         DC    CL7'END KEY'
         DC    X'04'                 SF   PROTECTED              ATTR
         DC    CL52' TO TERMINATE ETPS'
         DC    CL11'VERSION 2.4'
*
PRIMOPSZ EQU   *-PRIMOPT
         TITLE 'ETPS - BROWSE MENU'
BROWSCRN CSECT
*
* THIS IS THE BROWSE MENU
*
         DC    A(BROROW1)
         DC    A(BROROW2)
         DC    A(BROROW3)
         DC    A(BROROW4)
         DC    A(BROROW5)
         DC    X'80',AL3(BROROW6)
BROROW1  DC    AL1(80)                  ROW 01, COL 01
         DC    X'05'                    PROTECT, HIGH-LIGHT
         DC    24C'-'
         DC    CL21'BROWSE- ENTRY PANEL  '
         DC    34C'-'
BROROW2  DC    AL1(80)                  ROW 02, COL 01
         DC    X'05'
         DC    CL30'ENTER/VERIFY PARAMETERS BELOW:'
         DC    X'01'
         DC    CL49' '
BROROW3  DC    AL1(80)                  ROW 03, COL 01
         DC    X'01'
         DC    CL22'FULLY-QUALIFIED DSNAME'
         DC    X'05'
         DC    CL4'==>'
         DC    X'02'
         DC    CL53' '
BROROW4  DC    AL1(80)                  ROW 04, COL 01
         DC    X'04'
         DC    CL22'         VOLUME SERIAL'
         DC    X'05'
         DC    CL4'==>'
         DC    X'02'
         DC    CL6' '
         DC    X'04'
         DC    CL47'          (IF NOT CATALOGED)'
BROROW5  DC    AL1(80)                  ROW 05, COL 01
         DC    X'04'
         DC    CL79' '
BROROW6  DC    AL1(80)                  ROW 06, COL 01
         DC    X'04'
         DC    CL22'      DATASET PASSWORD'
         DC    X'05'
         DC    CL4'==>'
         DC    X'03'              NO DISPLAY
         DC    CL8' '
         DC    X'04'
         DC    CL45'        (IF PASSWORD PROTECTED)'
         TITLE 'ETPS - EDIT OPTIONS MENU'
*
* THIS IS THE EDIT OPTIONS MENU
*
EDITOPT  CSECT
         DC    A(EDTROW1)
         DC    A(EDTROW2)
         DC    A(EDTROW3)
         DC    A(EDTROW4)
         DC    A(EDTROW5)
         DC    X'80',AL3(EDTROW6)
EDTROW1  DC    AL1(80)                  ROW 01, COL 01
         DC    X'05'                    PROTECT, HIGH-LIGHT
         DC    24C'-'
         DC    CL21' EDIT - ENTRY PANEL  '
         DC    34C'-'
EDTROW2  DC    AL1(80)                  ROW 02, COL 01
         DC    X'05'
         DC    CL30'ENTER/VERIFY PARAMETERS BELOW:'
         DC    X'01'
         DC    CL49' '
EDTROW3  DC    AL1(80)                  ROW 03, COL 01
         DC    X'01'
         DC    CL22'FULLY-QUALIFIED DSNAME'
         DC    X'05'
         DC    CL4'==>'
         DC    X'02'
EDTDSNAM EQU   *-EDITOPT
         DC    CL53' '
EDTROW4  DC    AL1(80)                  ROW 04, COL 01
         DC    X'04'
         DC    CL22'         VOLUME SERIAL'
         DC    X'05'
         DC    CL4'==>'
         DC    X'02'
EDTVOLSR EQU   *-EDITOPT
         DC    CL6' '
         DC    X'04'
         DC    CL47'          (IF NOT CATALOGED)'
EDTROW5  DC    AL1(80)                  ROW 05, COL 01
         DC    X'04'
         DC    CL79' '
EDTROW6  DC    AL1(80)                  ROW 06, COL 01
         DC    X'04'
         DC    CL22'      DATASET PASSWORD'
         DC    X'05'
         DC    CL4'==>'
         DC    X'03'              NO DISPLAY
         DC    CL8' '
         DC    X'04'
         DC    CL45'        (IF PASSWORD PROTECTED)'
EDITOPSZ EQU   *-EDITOPT
*
         TITLE 'ETPS - LIBRARY UTILITY MENU'
*
* THIS IS THE LIBRARY UTILITY MENU
*
UT1MENU  CSECT
         DC    A(UT1ROW1)
         DC    A(UT1ROW2)
         DC    A(UT1ROW3)
         DC    A(UT1ROW4)
         DC    A(UT1ROW5)
         DC    A(UT1ROW6)
         DC    A(UT1ROW7)
         DC    A(UT1ROW9)
         DC    A(UT1ROW10)
         DC    A(UT1ROW11)
         DC    A(UT1ROW12)
         DC    X'80',AL3(UT1ROW13)
UT1ROW1  DC    AL1(80)                  ROW 01, COL 01
         DC    X'05'                    PROTECT, HIGH-LIGHT
         DC    24C'-'
         DC    CL21'UTILITY- ENTRY PANEL '
         DC    34C'-'
UT1ROW2  DC    AL1(32)
         DC    X'05'                 SF PROTECTED, HIGH-LIGHT    ATTR
         DC    CL19'SELECT OPTION ===> '
         DC    X'01'                 SF UNPROTECTED              ATTR
         DC    CL12' '
UT1ROW3  DC    AL1(11)                  ROW 02, COL 01
         DC    X'05'
         DC    CL12' '
UT1ROW4  DC    AL1(68)                  ROW 02, COL 01
         DC    X'04',C'   '
         DC    X'05',C'BLANK'
         DC    X'04'
         DC    CL25' - DISPLAY MEMBER LIST   '
         DC    X'05',C'    C'
         DC    X'04'
         DC    CL26' - COMPRESS DATASET      '
UT1ROW5  DC    AL1(68)                  ROW 02, COL 01
         DC    X'04',C'   '
         DC    X'05',C'     '
         DC    X'04'
         DC    CL25'                         '
         DC    X'05',C'     '
         DC    X'04'
         DC    CL26'                         '
UT1ROW6  DC    AL1(68)                  ROW 02, COL 01
         DC    X'04',C'   '
         DC    X'05',C'     '
         DC    X'04'
         DC    CL25'                         '
         DC    X'05',C'     '
         DC    X'04'
         DC    CL26'                         '
UT1ROW7  DC    AL1(68)                  ROW 02, COL 01
         DC    X'04',C'   '
         DC    X'05',C'     '
         DC    X'04'
         DC    CL25'                         '
         DC    X'05',C'     '
         DC    X'04'
         DC    CL26'                         '
UT1ROW8  DC    AL1(11)                  ROW 02, COL 01
         DC    X'05'
         DC    CL10' '
UT1ROW9  DC    AL1(80)                  ROW 02, COL 01
         DC    X'05'
         DC    CL30'ENTER/VERIFY PARAMETERS BELOW:'
         DC    X'04'
         DC    CL50' '
UT1ROW10 DC    AL1(80)                  ROW 03, COL 01
         DC    X'04'
         DC    CL22'FULLY-QUALIFIED DSNAME'
         DC    X'05'
         DC    CL4'==>'
         DC    X'02'
         DC    CL54' '
UT1ROW11 DC    AL1(80)                  ROW 04, COL 01
         DC    X'04'
         DC    CL22'         VOLUME SERIAL'
         DC    X'05'
         DC    CL4'==>'
         DC    X'02'
         DC    CL6' '
         DC    X'04'
         DC    CL48'          (IF NOT CATALOGED)'
UT1ROW12 DC    AL1(80)                  ROW 05, COL 01
         DC    X'04'
         DC    CL80' '
UT1ROW13 DC    AL1(80)                  ROW 06, COL 01
         DC    X'04'
         DC    CL22'      DATASET PASSWORD'
         DC    X'05'
         DC    CL4'==>'
         DC    X'03'              NO DISPLAY
         DC    CL8' '
         DC    X'04'
         DC    CL46'        (IF PASSWORD PROTECTED)'
         TITLE 'ETPS - FUNCTION NOT AVAILABLE DISPLAY'
*
* THIS IS DISPLAYED WHEN AN OPERATOR ATTEMPTS TO INVOKE A ETPS
* FUNCTION THAT HAS NOT YET BEEN IMPLEMENTED.
*
NOTGOT   CSECT
         DC    A(NOTGOT1)
         DC    A(NOTGOT2)
         DC    X'80',AL3(NOTGOT3)
NOTGOT1  DC    AL1(80)                  ROW 01, COL 01
         DC    X'05'                    PROTECT, HIGH-LIGHT
         DC    24C'-'
         DC    CL21' NOT AVAILABLE       '
         DC    34C'-'
NOTGOT2  DC    AL1(31)                  ROW 02, COL 01
         DC    X'05'
         DC    CL30'THE OPTION YOU HAVE SELECTED  '
NOTGOT3  DC    AL1(34)                  ROW 03, COL 01
         DC    X'05'
         DC    CL29'HAS NOT YET BEEN IMPLEMENTED.'
         DC    X'02'
         DC    CL2' '
         TITLE 'HELP FOR ETPS PDS LIBRARY MEMBER DISPLAY'
*
* THIS IS THE HELP SCREEN FOR THE PDS OPTION
*
LIBHELP  CSECT
         DC    A(LIBHELP1)
         DC    A(LIBHELP2)
         DC    A(LIBHELP3)
         DC    A(LIBHELP4)
         DC    A(LIBHELP5)
         DC    A(LIBHELP6)
         DC    A(LIBHELP7)
         DC    X'80',AL3(LIBHELPZ)
LIBHELP1 DC    AL1(80)                  ROW 01, COL 01
         DC    X'05'                    PROTECT, HIGH-LIGHT
         DC    24C'-'
         DC    CL21' PDS MEMBER OPTIONS  '
         DC    34C'-'
LIBHELP2 DC    AL1(10),X'05',CL12' '
LIBHELP3 DC    AL1(51)
         DC    X'05'
         DC    CL50'S OR E ---  EDIT MEMBER                           '
LIBHELP4 DC    AL1(51)
         DC    X'05'
         DC    CL50'     B ---  BROWSE MEMBER                         '
LIBHELP5 DC    AL1(51)
         DC    X'05'
         DC    CL50'     D ---  DELETE MEMBER                         '
LIBHELP6 DC    AL1(51)
         DC    X'05'
         DC    CL50'     R ---  RENAME MEMBER  (REQUIRES "NEWNAME")   '
LIBHELP7 DC    AL1(51)
         DC    X'05'
         DC    CL50'     C ---  COPY   MEMBER  (REQUIRES "NEWNAME")   '
LIBHELPZ DC    AL1(10),X'05',CL12' '
         TITLE 'ETPS - HELP NOT AVAILABLE DISPLAY'
*
* THIS IS DISPLAYED WHEN AN OPERATOR ATTEMPTS TO INVOKE A ETPS
* HELP SCREEN THAT HAS NOT YET BEEN IMPLEMENTED.
*
NOHELP   CSECT
         DC    A(NOHELP1)
         DC    A(NOHELP2)
         DC    X'80',AL3(NOHELP3)
NOHELP1  DC    AL1(80)                  ROW 01, COL 01
         DC    X'05'                    PROTECT, HIGH-LIGHT
         DC    24C'-'
         DC    CL21' NOT AVAILABLE       '
         DC    34C'-'
NOHELP2  DC    AL1(33)                  ROW 02, COL 01
         DC    X'05'
         DC    CL32'NO HELP INFORMATION IS AVAILABLE'
NOHELP3  DC    AL1(33)                  ROW 03, COL 01
         DC    X'05'
         DC    CL29'FOR THIS SCREEN.             '
         DC    X'02'
         DC    CL2' '
         TITLE 'ETPS - LIBRARY MANAGEMENT'
ETPSLIB2 CSECT
*
         DROP  13
         DROP  12
         DROP  11
         DROP  10
         USING *,12,11
         USING SAVE2,13
*
         STM   R14,R12,12(R13)    SAVE INPUT REGS
         LR    R12,R15            SET PROGRAM BASE REGISTER 1
         L     R2,0(,R1)          SAVE PARM POINTER
         LA    R11,72(,R13)       LEVEL 2 MODULE, SO BUMP TO SAVE2
         ST    R13,4(,R11)       STORE BACKWARDS POINTER
         ST    R11,8(R13)         STORE FORWARD POINTER
         LR    R13,R11            SET DSECT BASE 1
         LA    R3,1
         LA    R11,4095(R3,R12)   SET PROGRAM BASE REGISTER 2
         LA    R10,SPLITWRK       SET DSECT FOR SPLIT-SCREEN
         USING SPLTAREA,10
*
         LM    R2,R8,0(R1)         PICK UP 7 PARMS
*
* R2=DSNAME
* R3=DSN LENGTH
* R4=VOLSER
* R5=DSORG
* R6=DDNAME
* R7=PASSWORD, IF ANY
* R8=MEMBER NAME, IF ANY
*
*                                ALLOCATE THE FILE
*
*
         USING PARMLIST,6
         L     R6,DYNWORK1   INITIALIZE DYNAMIC ALLOCATION WORK
         MVI   0(R6),C' '
         MVC   001(256,R6),000(R6)
         MVC   255(255,R6),255(R6)
*
         MVC   LIBORG(3),0(R5)    DSORG
*
         SR    R1,1
         IC    R1,0(,R3)          PICK UP LENGTH
         BCTR  R1,0
         EX    R1,EXMV1           DSNAME
*
         L     R9,LIBDCBA
         MVC   DDNAME(8),40(R9) DDNAME
         MVC   DSVOLSER(6),0(R4)  VOLSER
         MVC   PASSWORD(8),0(R7)  PASSWORD
         MVI   DSSTATUS,C'S'         SET DISP=SHR
*
         MVI   EDSELMEM,C' '      CLEAR MEMBER NAME FIELD
         MVC   EDSELMEM+1(7),EDSELMEM
         MVI   LOCNAME,C' '        NOT DOING LOCATE
*
         CLI   0(R8),X'41'         WAS A MEMBER NAME PROVIDED
         BL    *+10                NOPE
*
         MVC   DSMEMBER(8),0(R8)  MEMBER NAME
*
         MVC   DSUNIT(8),=C'SYSALLDA'
*
         CLC   LIBORG,=C'SEQ'      IS THIS A SEQUENTIAL FILE
         BE    LIBDYN3             YUP
*
         CLC   LIBORG,=C'PAN'      IS THIS A PAN LIBRARY
         BE    LIBDYN4             YUP
*
         CLI   DSMEMBER,C' '       MEMBER SPECIFIED
         BNE   LIBDYN2             YUP
         B     LIBDYN1             NOPE, DISPLAY MEMBER SELECTION
*
EXMV1    MVC   DSNAME(1),0(R2)
         EJECT
LIBDYN1  DS    0H
*
         LA    R1,DYNWORK1
         L     R15,=A(DYNAM)
         BALR  R14,R15
*
         LTR   R15,15
         BNZ   ERRMS23
*
         L     R2,LIBDCBA         PICK UP LIBRARY ADDRESS
         XC    DIRPOINT(4),DIRPOINT    INITIALIZE
         XC    DIRNOTE(4),DIRNOTE
         XC    DIRTOP(4),DIRTOP
         MVI   FIRSTMEM,C' '
APDSDCB  DS    0H
*
         TM    48(R2),16      IS IT OPEN ALREADY
         BO    APDSOPND       YUP
*
*    OPEN FILE.
*
         MVI   PARMOP,X'80'   SET VL BIT
         OPEN  ((2),(UPDAT)),MF=(E,PARMOP)
*
         TM    48(R2),16      TEST FOR GOOD OPEN
         BZ    ERRMS25
*
APDSOPND DS    0H
         MVC   DECB(20),DIRECB
*
         MVI   DIREOFLG,0         INDICATE NOT AT EOF
*
*    PROCESS PDS.
*
READNXT  DS    0H
*
         LA    R4,TERMOUT+4      PICK UP ADDR OF FIRST OUTPUT LINE
         L     R8,SCROWS          PICK UP NUMBER OF ROWS
         CLI   SPLIT,0           AM I DOING SPLIT-SCREEN?
         BE    SETDIRNX          NOPE
*
         CLI   SPLIT,2           AM I ON SCREEN 2?
         BE    SETDIRN2          YUP
*
         L     R8,SCROWS1        PICK UP NUMBER OF ROWS ON SCREEN 1
         B     SETDIRNX          GO DO IT
*
SETDIRN2 DS    0H
*
         L     R8,SCROWS1        PICK UP NUMBER OF ROWS ON SCREEN 1
         SLL   R8,2              MULTIPLY BY 4
         LA    R4,TERMOUT+4(R8)  SET ADDRESS OF FIRST OUTPUT LINE
         L     R8,SCROWS2        PICK UP NUMBER OF ROWS ON SCREEN 2
*
SETDIRNX DS    0H
*
         NI    0(R4),X'7F'        NOT END OF PARM LIST
         L     R14,0(,R4)         PICK UP ADDR OF FIRST OUTPUT LINE
         LA    R15,DIRROW1        PICK UP FIRST LINE OF INPUT
         SR    R1,1
         IC    R1,0(,R15)         PICK UP LENGTH
         EX    R1,MVEDR                 PICK UP BUFFER ADDRESS
         MVC   16(44,R14),EDTDSN1
*
         CLI   LIBFUNC,C'E'       INDICATE FUNCTION
         BE    LIBROW2
*
         MVC   4(7,R14),=C'BROWSE '
         CLI   LIBFUNC,C'B'       INDICATE FUNCTION
         BE    LIBROW2
*
         MVC   4(8,R14),=C'UTILITY '
*
         LA    R14,DIRDSNAM(,14)   MOVE IN DSNAME
         MVC   DIRDSN1(44),EDTDSN1
*
LIBROW2  DS    0H
         L     R14,4(,R4)         BUMP
         NI    4(R4),X'7F'        NOT END OF PARM LIST
         LA    R15,DIRROW2        PICK UP NEXT LINE OF INPUT
         IC    R1,0(,R15)         PICK UP LENGTH
         EX    R1,MVEDR                 PICK UP BUFFER ADDRESS
         L     R14,8(,R4)         BUMP
         NI    8(R4),X'7F'        NOT END OF PARM LIST
         LA    R15,DIRROW3        PICK UP NEXT LINE OF INPUT
         IC    R1,0(,R15)         PICK UP LENGTH
         EX    R1,MVEDR                 PICK UP BUFFER ADDRESS
*
         LA    R4,12(,R4)         SET R4 TO START OF BUILD AREA
         LA    R1,4               SUBTRACT FIRST 3 ROWS
         SR    R8,R1   MAX DIRECTORY MEMBERS FOR ONE SCREEN
*
READ     EQU        *
         L     R2,LIBDCBA
         LA    R3,PDSKEY
         READ  DECB,SF,(2),(3),'S',MF=E
         CHECK DECB
         NOTE  (R2)
         ST    R1,DIRNOTE
*
         SR    R15,15
         C     R15,DIRTOP      AT TOP OF MEMBER LIST
         BNE   *+8             NO
         ST    R1,DIRTOP       YES, RESET POINTER TO TOP
*
         LA    R9,10(,R3)          LOAD ADDRESS OF FIRST MEMBER
NEXT     EQU        *
         CLI   0(R9),X'FF'         TEST FOR END OF DIRECTORY
         BE    DIRENDOF            BRANCH ON LAST USED BLOCK
*
         CLI   DIREOFLG,1          HAVE I  HIT EOF
         BE    DIRENDOF            YUP
*
         USING DIRMASK,1           BRANCH ON LAST USED BLOCK
         NI    0(R4),X'7F'        NOT END OF PARM LIST
         L     R1,0(,R4)           BRANCH ON LAST USED BLOCK
         MVI   0(R1),77           LENGTH OF DIR ENTRY
         MVC   1(77,R1),DIRLIST4  MOVE IT IN
*
*
         USING PDS2,9
*
*    AM I DOING LOCATE FUNCTION?
*
         CLI   LOCNAME,C' '        LOCATE?
         BE    NOTDLOCF            NOPE
*
*
         CLC   LOCNAME(1),PDS2NAME MATCH?
         BL    GOTDLOCF            GONE PAST IT
         BH    DIRBUMP1            IF HIGH, KEEP READING DIRECTORY
*
         CLI   LOCNAME+1,C' '      END OF STRING
         BE    GOTDLOCF            YES, MATCHED UP TO HERE, SO OK
         CLC   LOCNAME+1(1),PDS2NAME+1     MATCH?
         BL    GOTDLOCF            GONE PAST IT
         BH    DIRBUMP1            IF HIGH, KEEP READING DIRECTORY
*
         CLI   LOCNAME+2,C' '      END OF STRING
         BE    GOTDLOCF            YES, MATCHED UP TO HERE, SO OK
         CLC   LOCNAME+2(1),PDS2NAME+2     MATCH?
         BL    GOTDLOCF            GONE PAST IT
         BH    DIRBUMP1            IF HIGH, KEEP READING DIRECTORY
*
         CLI   LOCNAME+3,C' '      END OF STRING
         BE    GOTDLOCF            YES, MATCHED UP TO HERE, SO OK
         CLC   LOCNAME+3(1),PDS2NAME+3     MATCH?
         BL    GOTDLOCF            GONE PAST IT
         BH    DIRBUMP1            IF HIGH, KEEP READING DIRECTORY
*
         CLI   LOCNAME+4,C' '      END OF STRING
         BE    GOTDLOCF            YES, MATCHED UP TO HERE, SO OK
         CLC   LOCNAME+4(1),PDS2NAME+4     MATCH?
         BL    GOTDLOCF            GONE PAST IT
         BH    DIRBUMP1            IF HIGH, KEEP READING DIRECTORY
*
         CLI   LOCNAME+5,C' '      END OF STRING
         BE    GOTDLOCF            YES, MATCHED UP TO HERE, SO OK
         CLC   LOCNAME+5(1),PDS2NAME+5     MATCH?
         BL    GOTDLOCF            GONE PAST IT
         BH    DIRBUMP1            IF HIGH, KEEP READING DIRECTORY
*
         CLI   LOCNAME+6,C' '      END OF STRING
         BE    GOTDLOCF            YES, MATCHED UP TO HERE, SO OK
         CLC   LOCNAME+6(1),PDS2NAME+6     MATCH?
         BL    GOTDLOCF            GONE PAST IT
         BH    DIRBUMP1            IF HIGH, KEEP READING DIRECTORY
*
         CLI   LOCNAME+7,C' '      END OF STRING
         BE    GOTDLOCF            YES, MATCHED UP TO HERE, SO OK
         CLC   LOCNAME+7(1),PDS2NAME+7     MATCH?
         BL    GOTDLOCF            GONE PAST IT
         BH    DIRBUMP1            NOPE
*
GOTDLOCF DS    0H
         MVI   LOCNAME,C' '        LOCATED MEMBER, SO TURN THIS OFF
*
NOTDLOCF DS    0H
*
*    FORMAT THE PDS MEMBER
*
         MVC   DIRNAME(8),PDS2NAME MOVE MEMBER NAME TO PRINT
*
         NI    PDS2INDC,X'1F'
         CLI   PDS2INDC,X'0F'
         BNE   NOTSPF
*
         LA    R14,PDS2USRD       START  OF USER AREA
         USING SPFMT,14
*
*
         SR    R15,R15
         IC    R15,SPFVER
         CVD   R15,PACK8
         OI    PACK8+7,X'0F'
         UNPK  DIRLEVEL(3),PACK8+6(2)
         MVI   DIRLEVEL,C' '
         IC    R15,SPFMOD
         CVD   R15,PACK8
         OI    PACK8+7,X'0F'
         UNPK  DIRLEVEL+3(3),PACK8+6(2)
         MVI   DIRLEVEL+3,C'.'
         MVC   DIRLEVEL(5),DIRLEVEL+1
         MVI   DIRLEVEL+5,C' '
*
         UNPK  DIRCREAT(5),SPFCRDT(3)     CREATION DATE
         UNPK  DIRMOD(5),SPFMODT(3)     CREATION DATE
*
         SR    R15,15
         ICM   R15,3,SPFSIZE
         CVD   R15,PACK8
         OI    PACK8+7,X'0F'
         UNPK  DIRLINES(5),PACK8+5(3)
*
         MVC   DIRUSER(7),SPFUID
*
NOTSPF   DS    0H
*
         CLI   FIRSTMEM,C' '
         BNE   *+10
         MVC   FIRSTMEM(8),0(R9)    SET TOP OF LIST
*
         BCT   R8,DIRTNEXT         KEEP GOING
*
         B     DIRFMT
*
DIRTNEXT DS    0H
         LA    R4,4(,4)   LOAD ADDRESS OF NEXT MEMBER
DIRBUMP1 DS    0H
         MVC   LASTMEM(8),0(R9)    SET IT
         CLC   PDSKEY,0(R9)        TEST FOR LAST MEMBER IN BLOCK
         BNH   READ
         SR    R14,14
         NI    11(R9),X'1F'       CLEAR ALIAS AND TTR FLAGS
         IC    R14,11(,R9)       PICK UP USER DATA LENGTH IN HWORDS
         SLL   R14,1             MULTIPLY BY 2
         LA    R9,12(R14,R9)      LOAD ADDRESS OF NEXT MEMBER
         B     NEXT
DIRENDOF DS    0H
         MVI   DIREOFLG,1         INDICATE I HAVE HIT EOF
*
         NI    0(R4),X'7F'        NOT END OF PARM LIST
         L     R1,0(,R4)           BRANCH ON LAST USED BLOCK
         MVI   0(R1),77           LENGTH OF DIR ENTRY
         MVC   1(77,R1),DIRLIST4  MOVE IT IN
*
         MVC   DIRNAME(9),=C'** EOF **'
*
*
         BCT   R8,DIREOFUP         KEEP GOING
*
         B     DIRFMT
*
DIREOFUP DS    0H
         LA    R4,4(,4)   LOAD ADDRESS OF NEXT MEMBER
         B     DIRENDOF+4
         DROP  1
DIRFMT   DS    0H
*
*
         CLI   SPLIT,1            AM I ON TOP SCREEN
         BE    *+8                YUP
         OI    0(R4),X'80'        END OF PARM LIST
*
         L     R15,=A(LIBHELP)     DEFAULT HELP SCREEN
         ST    R15,HELPADD
*
         MVI   AIDROW,01           CURSOR ROW
         MVI   AIDCOL,21           CURSOR COLUMN
         MVC   HOMEAID(2),AIDADDR  COPY
*
         L     R15,=A(COMM3)       GO COMMUNICATE
         BALR  R14,R15
*
         LTR   R15,15                   BAD RC
         BNZ   FREELIB                  OUT
*
*                   CHECK TO SEE WHETHER AN EDIT OPTION WAS ENTERED
*
         LM    R2,R5,TERMINPT      PICK UP FIRST REPLY FIELD
*
         CLI   SPLIT,2             AM I ON SECOND SCREEN
         BNE   DIRFMTPF            NO, GO TEST PFKEY
*
         L     R3,SCROWS1        PICK UP NUMBER OF ROWS ON SCREEN 1
         SLL   R3,2              MULTIPLY BY 4
         LA    R3,TERMINPT+4(R3) SET ADDRESS OF FIRST INPUT LINE
         LM    R3,R5,0(R3)       PICK UP FIRST REPLY FIELD
*
DIRFMTPF DS    0H
         CLI   0(R2),X'6E'         PA 2
         BNE   DIRCHKPF            NOPE
*
         MVC   LOCNAME(8),FIRSTMEM RE-LOCATE THE TOP OF DISPLAY
         MVI   FIRSTMEM,C' '
         B     EDDIRLC2            NOPE
*
DIRCHKPF DS    0H
         MVI   FIRSTMEM,C' '
         CLI   PFKFLAG,0           PFK/ATTN ENTERED?
         BNE   EDDIRPFK            YUP
*
         CLI   0(R4),X'00'      COMMAND ENTERED?
         BE    EDDIRSEL         NOPE
*
         CLC   22(2,R4),=C'PF'    END OF EDIT
         BE    EDDIRPFK
         CLC   22(3,R4),=C'END'   END OF EDIT
         BE    EDDSNDUN
*
         CLC   22(3,R4),=C'ABE'   ABEND
         BE    S0C1
*
         CLI   22(R4),C'L'        LOCATE?
         BE    EDDIRLOC           YUP, GO LOCATE
         CLI   22(R4),C'F'        ALLOW "F" FOR FIND AS WELL
         BE    EDDIRLOC           GO LOCATE
*
* PUT OTHER TESTS FOR COMMAND LINE INPUT HERE
*
         LA    R1,ELERR01
         ST    R1,MSGADD
*
         B     AMEMEOFG
*
ELERR01  DC    CL20'INVALID COMMAND'
*
S0C1     DC    H'0'
*
MVEDR    MVC   0(1,R14),0(R15)    MOVE INPUT INTO INADDR WORK AREA
EDDIRSEL DS    0H
         MVI   LOCNAME,C' '      CLEAR TOP OF DIRECTORY
         LA    R3,TERMINPT+16     POINT TO LINE 4
         L     R4,SCROWS1        PICK UP NUMBER OF ROWS ON SCREEN 1
         CLI   SPLIT,2             AM I ON SECOND SCREEN
         BNE   EDIRSEL1            NO, GO ROLL
*
         L     R3,SCROWS1        PICK UP NUMBER OF ROWS ON SCREEN 1
         SLL   R3,2              MULTIPLY BY 4
         LA    R3,TERMINPT+16(R3) SET ADDRESS OF FIRST INPUT LINE
         L     R4,SCROWS2        PICK UP NUMBER OF ROWS ON SCREEN 2
*
EDIRSEL1 DS    0H
         LA    R1,3               SUBTRACT FIRST 3 ROWS
         SR    R4,R1   MAX DIRECTORY MEMBERS FOR ONE SCREEN
*
EDIRSEL2 DS    0H
         L     R5,0(,R3)          POINT TO EACH LINE IN TURN
         CLI   0(R5),X'00'        ANY INPUT?
         BNE   EDIRSEL4           YUP
EDIRSEL3 DS    0H
         LA    R3,4(,R3)          POINT TO NEXT LINE
         BCT   R4,EDIRSEL2
         CLI   LOCNAME,C' '      LOCNAME ALREADY SET?
         BNE   AMEMEOFG          YES
         MVC   LOCNAME(8),LASTMEM NO, SO SET IT
         B     AMEMEOFG           GO SHOW MORE
EDIRSEL4 DS    0H
*
         CLI   LOCNAME,C' '      LOCNAME ALREADY SET?
         BNE   *+10              YES
         MVC   LOCNAME(8),6(R5)  NO, SO SET IT
*
         MVC   SELFUNC(1),2(R5)  SET SELECT CODE
         MVI   2(R5),C' '        CLEAR IT
*
         CLI   SELFUNC,C'S'      SELECT CODE?
         BE    DIRMEMBR          YES PROCESS IT
*
         CLI   SELFUNC,C'B'      BROWSE
         BE    DIRMEMBR          GO  PROCESS IT
*
         CLI   SELFUNC,C'E'      EDIT
         BE    DIRMEMBR          GO  PROCESS IT
*
         CLI   SELFUNC,C'D'      DELETE
         BE    MEMDEL            YES
         CLI   SELFUNC,C'R'      RENAME
         BE    MEMREN            YES
         CLI   SELFUNC,C'C'      COPY
         BE    MEMCOPY           YES
*
* PUT TESTS FOR OTHER SELECTION CODES HERE
*
         B     EDIRSEL3           TEST NEXT LINE
*
DIRMEMBR DS    0H
*
         L     R1,DYNWORK1   INITIALIZE DYNAMIC ALLOCATION WORK
         MVI   0(R1),C' '
         MVC   001(256,R1),000(R1)
         MVC   255(255,R1),255(R1)
*
         MVC   EDSELMEM(8),6(R5)    MEMBER SELECTED
         MVC   DSMEMBER(8),EDSELMEM
         L     R4,MEMDCBA
         MVC   DDNAME(8),40(R4)     SET DDNAME TO SHOW MEMBER
         L     R1,=A(SEQDCB)
         MVC   0(96,R4),0(R1)
         MVC   40(8,R4),DDNAME      SET DDNAME TO SHOW MEMBER
         MVC   DSNAME(44),EDTDSN1   SET DSNAME OF LIBRARY
         MVI   DSSTATUS,C'S'            STATUS, DISP=SHR
         MVC   DSVOLSER(6),EDTVOL1
         MVC   PASSWORD(8),EDTPAS1
         MVC   DSUNIT(8),=C'SYSALLDA'
*
         LA    R1,DYNWORK1
         L     R15,=A(DYNAM)
         BALR  R14,R15
*
         LTR   R15,15
         BNZ   ERRMS32
*
         CLI   SELFUNC,C'B'         IS BROWSE REQUESTED
         BE    MEMBRO               YES
*
         CLI   LIBFUNC,C'B'         IS BROWSE REQUESTED
         BE    MEMBRO               YES
*
         LA    R1,AMEMEOF1
         STCM  R1,7,33(R4)
         SR    R3,3           CLEAR RECORD COUNTER
*
         MVI   PARMOP,X'80'   SET VL BIT
         OPEN  ((4),(INPUT)),MF=(E,PARMOP)
*
         TM    48(R4),X'10'   GOOD OPEN
         BZ    ERRMS28        NOPE
*
AMEMCOUN DS    0H
*
         GET   (4),EDAFTSV1
         LA    R3,1(,3)       BUMP COUNTER
         B     AMEMCOUN
*
ERRMS28  WTO   'ETPS1028 OPEN FAILED FOR LIB(MEM)         ',ROUTCDE=11
         LA    R1,ERRMS28+13
         ST    R1,MSGADD
         B     AMEMEOFF           SKIP IT
ERRMS26  WTO   'ETPS1026 PREMATURE EOF                    ',ROUTCDE=11
         LA    R1,ERRMS26+13
         ST    R1,MSGADD
         B     AMEMEOFF           SKIP IT
*
MEMBRO   DS    0H
         LA    R2,DSNAME
         LA    R8,DSMEMBER
         LA    R5,LIBORG
*
         LOAD  EP=ETPSBRO3
         LR    R15,R0
* PARM LIST FOR BROWSE IS : DCB, DSN, MEMBER NAME, DSORG
         CALL  (15),((4),(2),(8),(5)),                                 X
               VL,MF=(E,PARM43)
        DELETE EP=ETPSBRO3
         B     AMEMEOFR
*
AMEMEOF1 DS    0H
*
         MVI   PARMOP,128
         CLOSE ((4)),MF=(E,PARMOP)
*
AMEMEOFF DS    0H
         ST    R3,SEQRNUM
         LA    R3,SEQRNUM
         LA    R2,DSNAME
         LA    R8,DSMEMBER
         LA    R5,LIBORG
*
         LOAD  EP=ETPSEDT3
         LR    R15,R0
* PARM LIST FOR EDITOR IS : DCB, DSN, MEMBER NAME, DSORG, NUM RECS
         CALL  (15),((4),(2),(8),(5),(3)),                             X
               VL,MF=(E,PARM43)
        DELETE EP=ETPSEDT3
*
AMEMEOFR DS    0H
         L     R1,DYNWORK1   INITIALIZE DYNAMIC ALLOCATION WORK
         MVI   0(R1),C' '
         MVC   001(256,R1),000(R1)
         MVC   255(255,R1),255(R1)
*
         MVI   DSNAME,X'00'       INDICATE FREE REQUEST
         MVC   DSNAME+1(43),DSNAME
         MVC   DDNAME(8),40(R4)     SET DDNAME TO SHOW MEMBER
         LA    R1,DYNWORK1
         L     R15,=A(DYNAM)
         BALR  R14,R15
*
         LTR   R15,R15
         BZ    AMEMEOFG           OK, CONTINUE
        WTO   'ETPSLIB2 - DYN FREE FOR LIB(MEM) FAILED   ',ROUTCDE=11
*
AMEMEOFG DS    0H
*
         MVI   DSMEMBER,C' '      CLEAR MEMBER NAME FIELD
         MVC   DSMEMBER+1(7),DSMEMBER
         MVI   EDSELMEM,C' '      CLEAR MEMBER NAME FIELD
         MVC   EDSELMEM+1(7),EDSELMEM
         B     EDDIRLC2
*
EDDIRLOC DS    0H
*                   LOCATE MEMBER IN LIBRARY
         MVC   LOCNAME(8),27(R4)   MOVE IN "LOCATE" NAME
EDDIRLC2 DS    0H
         L     R2,LIBDCBA          POINT TO START OF LIBRARY DIR
         POINT (2),POINT0
         B     APDSDCB
*
POINT0   DC    F'256'
ERRMS32  WTO   'ETPS1032 ALLOC FAILED FOR LIB(MEM)        ',ROUTCDE=11
         LA    R1,ERRMS32+13
         ST    R1,MSGADD
         L     R1,DYNWORK1         PICK UP PARAMETER BASE
         LM    R2,R3,DYNRCODE      PICK UP ERROR CODES
         DC    H'0'
         B     AMEMEOFG           SKIP IT
*
         TITLE 'ETPS LIBRARY MANAGEMENT -- DELETE MEMBER'
MEMDEL   DS    0H
*
         L     R1,LIBDCBA         POINT TO LIBRARY
         LA    R0,6(,R5)          POINT TO MEMBER NAME
         STOW  (1),(0),D
*
         B     EDIRSEL3           CHECK FOR ANOTHER MEMBER SELECTED
*
         TITLE 'ETPS LIBRARY MANAGEMENT -- RENAME MEMBER'
MEMREN   DS    0H
*
         L     R1,DYNWORK1        POINT TO MEMBER NAME
         MVC   0(8,R1),6(R5)      MOVE CURRENT NAME
         MVC   8(8,R1),17(R5)     MOVE NEW     NAME
*
         LR    R0,R1              POINT TO MEMBER NAME
         L     R1,LIBDCBA         POINT TO LIBRARY
         STOW  (1),(0),C
*
         B     EDIRSEL3           CHECK FOR ANOTHER MEMBER SELECTED
*
         TITLE 'ETPS LIBRARY MANAGEMENT -- COPY MEMBER'
MEMCOPY  DS    0H
*
         OI    EDTVOL1,C' '
         CLI   EDTVOL1,C' '
         BNE   *+10
         MVC   EDTVOL1(6),DIRVOL
*
         L     R1,DYNWORK1         USE THIS AS WORK AREA
         LA    R15,EDTDSN1         POINT TO LIBRARY DSNAME
         ST    R15,00(,R1)
         LA    R15,6(,R5)          CURRENT MEMBER
         ST    R15,04(,R1)
         LA    R15,EDTVOL1         VOLSER
         ST    R15,08(,R1)
         LA    R15,EDTPAS1         PASSWORD, IF ANY
         ST    R15,12(,R1)
*
         LA    R15,EDTDSN1         POINT TO LIBRARY DSNAME
         ST    R15,16(,R1)
         LA    R15,17(,R5)         NEW MEMBER
         ST    R15,20(,R1)
         LA    R15,EDTVOL1         VOLSER
         ST    R15,24(,R1)
         LA    R15,EDTPAS1         PASSWORD, IF ANY
         ST    R15,28(,R1)
         OI    28(R1),X'80'
         LINK  EP=ETPSMEMC
*
         B     EDIRSEL3           CHECK FOR ANOTHER MEMBER SELECTED
*
         TITLE 'ETPS LIBRARY MANAGEMENT -- TEST PF KEY'
EDDIRPFK DS    0H
*                   PF KEY WAS ENTERED, SEE WHAT IT WAS
*
         CLI   0(R2),X'7D'         IS IT ENTER
         BE    EDDIRSEL            YUP  - GO SEE WHAT WAS ENTERED
*
         CLI   0(R2),X'F3'         IS IT PF3
         BE    EDDSNDUN            YUP  - ALL DONE
         CLI   0(R2),X'C3'         IS IT PF15
         BE    EDDSNDUN            YUP  - ALL DONE
*
         CLI   0(R2),X'F8'         IS IT PF8
         BE    EDMORDIR            YUP  - SHOW NEXT SCREEN OF DIR
         CLI   0(R2),X'C8'         IS IT PF20
         BE    EDMORDIR            YUP  - SHOW NEXT SCREEN OF DIR
*
         CLI   0(R2),X'F7'         IS IT PF7
         BE    EDPREDIR            YUP  - SHOW PREV SCREEN OF DIR
         CLI   0(R2),X'C7'         IS IT PF19
         BE    EDPREDIR            YUP  - SHOW PREV SCREEN OF DIR
*
         CLI   0(R2),X'6C'         IS IT PA1
         BE    EDDSNDUN            YUP  - ALL DONE
*
         CLI   0(R2),X'6E'         IS IT PA2
         BE    DIRFMT              YUP  - GO RESHOW THE SCREEN
*
         CLI   0(R2),X'6D'         IS IT "CLEAR"
         BE    DIRFMT              YUP  - GO RESHOW THE SCREEN
*
         B     EDDSNDUN            ALL OTHER PFK'S
*
EDMORDIR DS    0H
*                   SHOW MORE DIRECTORY INFO
*
*
         LA    R4,TERMOUT+4      PICK UP ADDR OF FIRST OUTPUT LINE
         L     R8,SCROWS1         PICK UP NUMBER OF ROWS
         CLI   SPLIT,2           AM I DOING SPLIT-SCREEN?
         BNE   SETMORNX          NOPE
*
SETMORN2 DS    0H
*
         SLL   R8,2              MULTIPLY BY 4
         LA    R4,TERMOUT+4(R8)  SET ADDRESS OF FIRST OUTPUT LINE
         L     R8,SCROWS2        PICK UP NUMBER OF ROWS ON SCREEN 2
*
SETMORNX DS    0H
*
         NI    0(R4),X'7F'        NOT END OF PARM LIST
         L     R14,0(,R4)         PICK UP ADDR OF FIRST OUTPUT LINE
*
         LA    R15,DIRROW1        PICK UP FIRST LINE OF INPUT
         SR    R1,1
         IC    R1,0(,R15)         PICK UP LENGTH
         EX    R1,MVEDR                 PICK UP BUFFER ADDRESS
         MVC   16(44,R14),EDTDSN1
*
         CLI   LIBFUNC,C'E'       INDICATE FUNCTION
         BE    LIBROW22
*
         MVC   4(7,R14),=C'BROWSE '
         CLI   LIBFUNC,C'B'       INDICATE FUNCTION
         BE    LIBROW22
*
         MVC   4(8,R14),=C'UTILITY '
*
         LA    R14,DIRDSNAM(,14)   MOVE IN DSNAME
         MVC   DIRDSN1(44),EDTDSN1
*
LIBROW22 DS    0H
         L     R14,4(,R4)         BUMP
         NI    4(R4),X'7F'        NOT END OF PARM LIST
         LA    R15,DIRROW2        PICK UP NEXT LINE OF INPUT
         IC    R1,0(,R15)         PICK UP LENGTH
         EX    R1,MVEDR                 PICK UP BUFFER ADDRESS
         L     R14,8(,R4)         BUMP
         NI    8(R4),X'7F'        NOT END OF PARM LIST
         LA    R15,DIRROW3        PICK UP NEXT LINE OF INPUT
         IC    R1,0(,R15)         PICK UP LENGTH
         EX    R1,MVEDR                 PICK UP BUFFER ADDRESS
*
         LA    R4,8(,4)           SET R4 TO START OF BUILD AREA, -1 ROW
         LA    R1,3               SUBTRACT FIRST 3 ROWS
         SR    R8,R1   MAX DIRECTORY MEMBERS FOR ONE SCREEN
*
         L     R2,LIBDCBA
         XC    DIRTOP(4),DIRTOP
         CLI   LOCNAME,C' '        "LOCATE" NAME PRESENT
         BE    DIRTNEXT            NOPE
         LA    R4,4(,4)           SET R4 TO START OF BUILD AREA
         B     READ
*
EDPREDIR DS    0H
*
*                   SHOW PREV DIRECTORY INFO
*
         MVC   DIRPOINT(4),DIRTOP   SET LAST BLOCK READ
         CLI   DIRPOINT+2,1         AM I ON BLOCK 1
         BE    EDPRETRK             YES, JUST SUBTRACT FROM TRACK
*
         SR    R2,2
         IC    R2,DIRPOINT+2        PICK UP TRACK NUMBER
         BCTR  R2,0                 SUBTRACT 1
         STC   R2,DIRPOINT+2
         CLI   DIRPOINT+2,1         AM I ON BLOCK 1
         BE    EDPRETRK             YES, JUST SUBTRACT FROM TRACK
         BCTR  R2,0                 SUBTRACT 2
         STC   R2,DIRPOINT+2
         CLI   DIRPOINT+2,1         AM I ON BLOCK 1
         BE    EDPRETRK             YES, JUST SUBTRACT FROM TRACK
         BCTR  R2,0                 SUBTRACT 3
         STC   R2,DIRPOINT+2
*
         B     EDPREPOI             GO POINT TO IT
*
EDPRETRK DS    0H
         SR    R2,2
         CH    R2,DIRPOINT          AM I ON TRACK 0
         BE    EDPREPOI             GO POINT TO IT
         LH    R2,DIRPOINT          AM I ON TRACK 0
         BCTR  R2,0
         STH   R2,DIRPOINT          AM I ON TRACK 0
         MVI   DIRPOINT+2,X'10'     POINT TO BLOCK 16
EDPREPOI DS    0H
         L     R2,LIBDCBA
         POINT (2),DIRPOINT
         XC    DIRTOP(4),DIRTOP
         MVI   DSMEMBER,C' '      CLEAR MEMBER NAME FIELD
         MVC   DSMEMBER+1(7),DSMEMBER
         MVI   LOCNAME,C' ' NOTLOOKING FOR PARTICULAR MEMBER
         B     APDSDCB
*
         EJECT
LIBDYN2  DS    0H
*
         LA    R1,DYNWORK1
         L     R15,=A(DYNAM)
         BALR  R14,R15
*
         LTR   R15,15
         BNZ   ERRMS23
*
         L     R4,MEMDCBA         PICK UP LIBRARY ADDRESS
         MVC   40(8,R4),DDNAME    SET DDNAME
*
         CLI   LIBFUNC,C'E'       EDIT SEQUENTIAL
         BE    MEMEDIT            YES
         CLI   LIBFUNC,C'B'       BROWSE SEQ
         BE    MEMBROWS           YES
         B     MEMFREE
*
MEMEDIT  DS    0H
*
*
         LA    R1,AMEMEOF2
         STCM  R1,7,33(R4)
         SR    R3,3           CLEAR RECORD COUNTER
*
         MVI   PARMOP,X'80'   SET VL BIT
         OPEN  ((4),(INPUT)),MF=(E,PARMOP)
*
         TM    48(R4),X'10'   GOOD OPEN
         BZ    ERRMS28M       NOPE
*
BMEMCOUN DS    0H
*
         GET   (4),EDAFTSV1
         LA    R3,1(,3)       BUMP COUNTER
         B     BMEMCOUN
*
ERRMS28M WTO   'ETPS1028 OPEN FAILED FOR LIB(MEM)         ',ROUTCDE=11
         LA    R1,ERRMS28+13
         ST    R1,MSGADD
         B     MEMFREE            SKIP IT
*
AMEMEOF2 DS    0H
*
         MVI   PARMOP,128
         CLOSE ((4)),MF=(E,PARMOP)
*
         ST    R3,SEQRNUM
         LA    R3,SEQRNUM
         LA    R2,DSNAME
         LA    R8,DSMEMBER
         LA    R5,LIBORG
*
         LOAD  EP=ETPSEDT3
         LR    R15,R0
* PARM LIST FOR EDITOR IS : DCB, DSN, MEMBER NAME, DSORG, NUM RECS
         CALL  (15),((4),(2),(8),(5),(3)),                             X
               VL,MF=(E,PARM43)
        DELETE EP=ETPSEDT3
*
         B     MEMFREE
*
MEMBROWS DS    0H
         LA    R2,DSNAME
         LA    R8,DSMEMBER
         LA    R5,LIBORG
*
         LOAD  EP=ETPSBRO3
         LR    R15,R0
* PARM LIST FOR BROWSE IS : DCB, DSN, MEMBER NAME, DSORG
         CALL  (15),((4),(2),(8),(5)),                                 X
               VL,MF=(E,PARM43)
        DELETE EP=ETPSBRO3
*
MEMFREE  DS    0H
         L     R1,DYNWORK1   INITIALIZE DYNAMIC ALLOCATION WORK
         MVI   0(R1),C' '
         MVC   001(256,R1),000(R1)
         MVC   255(255,R1),255(R1)
*
         MVI   DSNAME,X'00'       INDICATE FREE REQUEST
         MVC   DSNAME+1(43),DSNAME
         MVC   DDNAME(8),40(R4)     SET DDNAME TO SHOW MEMBER
         LA    R1,DYNWORK1
         L     R15,=A(DYNAM)
         BALR  R14,R15
*
*
         B     DIRRETRN
*
         EJECT
LIBDYN3  DS    0H
*
*
         LA    R1,DYNWORK1
         L     R15,=A(DYNAM)
         BALR  R14,R15
*
         LTR   R15,15
         BNZ   ERRMS23
*
         L     R4,COPDCBA         PICK UP LIBRARY ADDRESS
         MVC   40(8,R4),DDNAME    SET DDNAME
*
         CLI   LIBFUNC,C'E'       EDIT SEQUENTIAL
         BE    SEQEDIT            YES
         CLI   LIBFUNC,C'B'       BROWSE SEQ
         BE    SEQBRO             YES
         B     SEQFREE
*
SEQEDIT  DS    0H
*
         B     MEMEDIT
*
SEQBRO   DS    0H
*
         B     MEMBROWS
*
SEQFREE  DS    0H
*
         B     MEMFREE
*
*
         EJECT
LIBDYN4  DS    0H
*
         B     FREELIB             AND I'M DONE
*
         EJECT
EDDSNDUN DS    0H
*                   DONE EDITING THIS FILE, SO FREE IT
*
FREELIB  DS    0H
         LA    R4,LIBDCBA
         LA    R3,4
BUMPLIB1 DS    0H
         L     R2,0(,R4)
         TM    48(R2),X'10'
         BZ    BUMPLIB2
         MVI   PARMOP,128
         CLOSE ((2)),MF=(E,PARMOP)
BUMPLIB2 DS    0H
*
         L     R1,DYNWORK1   INITIALIZE DYNAMIC ALLOCATION WORK
         MVI   0(R1),C' '
         MVC   001(256,R1),000(R1)
         MVC   255(255,R1),255(R1)
*
         MVI   DSNAME,X'00'
         MVC   DSNAME+1(43),DSNAME
         MVC   DDNAME(8),40(R2)     SET DDNAME FOR FREE
         LA    R1,DYNWORK1
         L     R15,=A(DYNAM)
         BALR  R14,R15
         LA    R4,4(,4)
         BCT   R3,BUMPLIB1        SEQ ALLOCATED
EOJ      DS    0H
DIRRETRN DS    0H
         L     R13,4(,13)               RESTORE CALLERS REGS
         LM    R14,R12,12(R13)          RESTORE CALLERS REGS
         BR    R14                      NORMAL RETURN
ERRMS23  DS    0H
         CLC   DYNRCODE(2),ALLO0210
         BE    ERRINUSE
         WTO   'ETPS1023 ALLOC FAILED FOR LIB             ',ROUTCDE=11
         LA    R1,ERRMS23+17
         ST    R1,MSGADD
         L     R1,DYNWORK1         PICK UP PARAMETER BASE
         B     EDDSNDUN            YUP  - ALL DONE
ERRINUSE DS    0H
         LA    R1,ALLO0210+2
         ST    R1,MSGADD
         B     EDDSNDUN            YUP  - ALL DONE
ALLO0210 DC    X'0210',CL20'0210-DATASET IN USE '
ERRMS25  WTO   'ETPS1025 OPEN FAILED FOR LIB             ',ROUTCDE=11
         LA    R1,ERRMS25+13
         ST    R1,MSGADD
         B     EDDSNDUN            YUP  - ALL DONE
         LTORG
*
PDSDIR   DCB      DSORG=PS,MACRF=(RP,WP),DDNAME=PPROCLIB,RECFM=F,      C
               LRECL=256,BLKSIZE=256,KEYLEN=8,EODAD=DIRENDOF
         READ  DIRECB,SF,PDSDIR,PDSDIR,'S',MF=L
         TITLE 'ETPS - DIRECTORY LIST FORMAT'
*
* THIS IS THE FORMAT FOR A PDS OR PAN LIBRARY DIRECTORY LISTING.
*
DIRTITLE DS    0F
DIRROW1  DC    AL1(80)
         DC    X'05'
         DC    CL8'   EDIT '
         DC    C'- '
DIRDSNAM EQU   *-DIRTITLE
         DC    69C'-'
DIRROW2  DC    AL1(80)
         DC    X'05'
         DC    CL19'COMMAND INPUT ===> '
         DC    X'01'
DIRHOME  EQU   *-DIRTITLE
         DC    CL40' '
         DC    X'05'
         DC    CL13' SCROLL ===> '
         DC    X'01'
         DC    CL4'PAGE'
DIRROW3  DC    AL1(70)
         DC    X'04'
         DC    CL23'   NAME       NEWNAME '
         DC    CL7'    LVL'
         DC    CL9' CREATED '
         DC    CL16' LAST MODIFIED  '
         DC    CL4'    '
         DC    CL6'SIZE  '
         DC    CL4' ID '
DIRTLENG EQU   *-DIRTITLE
*
DIRLIST4 DS    0XL1
       DC X'01',C' ',X'04',CL12' ',X'01',CL10' ',X'04',CL46' '
DIRELEN  EQU   *-DIRLIST4
*
         LTORG
         TITLE 'ETPS - TERMINAL COMMUNICATIONS SUBROUTINE'
*
*     CONSOLE I/O SUBROUTINE.
*
         SPACE 5
******** COMMUNICATIONS SUBROUTINE - EXCP INTERFACE
COMM2    CSECT
         DROP  13
         DROP  12
         DROP  11
         DROP  10
         USING *,12,11
         USING SAVE2,13,10
*
         STM   R14,R12,12(R13)    SAVE INPUT REGS
         LR    R12,R15            SET PROGRAM BASE REGISTER 1
         LA    R11,72(,R13)       LEVEL 2 MODULE, SO BUMP TO SAVE2
         ST    R13,4(,R11)       STORE BACKWARDS POINTER
         ST    R11,8(R13)         STORE FORWARD POINTER
         LR    R13,R11            SET DSECT BASE 1
         L     R15,=A(COMM3)
         BALR  R14,R15
         L     R13,4(,13)               RESTORE CALLERS REGS
         LM    R0,R12,20(R13)          RESTORE CALLERS REGS
         L     R14,12(R13)              PRESERVE R15
         BR    R14                      NORMAL RETURN
         LTORG
COMM3    CSECT
         DROP  13
         DROP  12
         DROP  11
         DROP  10
         USING *,12,11
         USING SAVE3,13,10
*
         STM   R14,R12,12(R13)    SAVE INPUT REGS
         LR    R12,R15            SET PROGRAM BASE REGISTER 1
         LA    R11,72(,R13)       LEVEL 3 MODULE, SO BUMP TO SAVE3
         ST    R13,4(,R11)       STORE BACKWARDS POINTER
         ST    R11,8(R13)         STORE FORWARD POINTER
         LR    R13,R11            SET DSECT BASE 1
         L     R15,=A(COMM4)
         BALR  R14,R15
         L     R13,4(,13)               RESTORE CALLERS REGS
         LM    R0,R12,20(R13)          RESTORE CALLERS REGS
         L     R14,12(R13)              PRESERVE R15
         BR    R14                      NORMAL RETURN
         LTORG
COMM4    CSECT
         DROP  13
         DROP  12
         DROP  11
         DROP  10
         USING *,12,11
         USING SAVE4,13
*
         STM   R14,R12,12(R13)    SAVE INPUT REGS
         LR    R12,R15            SET PROGRAM BASE REGISTER 1
         LA    R11,72(,R13)       LEVEL 4 MODULE, SO BUMP TO SAVE4
         ST    R13,4(,R11)       STORE BACKWARDS POINTER
         ST    R11,8(R13)         STORE FORWARD POINTER
         LR    R13,R11            SET DSECT BASE 1
         LA    R3,1
         LA    R11,4095(R3,R12)   SET PROGRAM BASE REGISTER 2
         LA    R10,4095(R3,R13)   SET DSECT BASE 2
         LA    R10,SPLITWRK       SET DSECT FOR SPLIT-SCREEN
         USING SPLTAREA,10
*
RESCREEN DS    0H
*
         MVI   HELPFLAG,0         INDICATE "NOT DOING HELP"
         L     R9,SCROWS          PICK UP TERMINAL SCREEN SIZE
*
*
BLDBUFF  DS    0H
*
         SR    R5,5                     LENGTH COUNTER
*
         L     R7,BUFF                  ACTUAL WRITE BUFFER
         MVI   0(R7),X'7E'              ERASE-WRITE-ALTERNATE
         MVC   1(1,R7),USUALWCC         NO ALARM
         LA    R7,2(,7)                 BUMP ADDRESS
         LA    R5,2(,5)                 BUMP COUNTER
*
         L     R8,SBACODES              PICK UP LINE 1
*
         LA    R6,TERMOUT+4             PICK UP LINE 1
*
         CLI   HELPFLAG,0               AM I DOING HELP
         BE    BLDBUFFS                 NOPE
         L     R6,HELPADD               YUP
*
BLDBUFFS DS    0H
*
         MVC   0(1,R7),SBACODE          ADDRESS OF FROM FIELD
         MVC   1(2,R7),0(R8)            ADDRESS OF FROM FIELD
         LA    R7,3(,7)                 BUMP ADDRESS
         LA    R5,3(,5)                 BUMP COUNTER
*
         L     R2,0(,R6)                ADDRESS OF FROM FIELD
*
         CLI   0(R2),0                  IS LENGTH ZERO?
         BNE   *+8                      NOPE
         MVI   0(R2),81                 SET IT TO 81 AS A DEFAULT
*
         SR    R1,1                     CLEAR REGISTER
         IC    R1,0(,R2)                PICK UP LENGTH
         LA    R2,1(,R2)                POINT TO FIRST INPUT BYTE
*
BUFFMOV0 DS    0H
*
         CLI   0(R2),0                  NULL
         BE    BUFFMOV1                 MOVE IT
         CLI   0(R2),7                  SBA CODE
         BL    BUFFSBA1                 MOVE IT
*
BUFFMOV1 DS    0H
*
         MVC   0(1,R7),0(R2)    *  MOVE BYTE OF DATA
*                               *
         LA    R7,1(,R7)        *  BUMP BUFFER POINTER
         LA    R5,1(,R5)        *  BUMP BUFFER COUNTER
         LA    R2,1(,R2)        *  BUMP INPUT  POINTER
*                               *
         B     BUFFMOV9         *  CONTINUE
*                               *
BUFFSBA1 DS    0H               *
*                               *
         SR    R14,R14          *  CLEAR WORK REGISTER
         IC    R14,0(,R2)       *  PICK UP ATTRIBUTE BYTE
         SLL   R14,1            *  MULTIPLY BY 2
         LA    R15,ATTRIBS(R14) *  POINT TO ATTRIBUTE
         MVC   0(2,R7),0(R15)   *  MOVE ATTRIBUTE
*                               *
         LA    R7,2(,R7)        *  BUMP BUFFER POINTER
         LA    R5,2(,R5)        *  BUMP BUFFER COUNTER
         LA    R2,1(,R2)        *  BUMP INPUT  POINTER
*                               *
BUFFMOV9 DS    0H               *
*                               *
         BCT   R1,BUFFMOV0      *  MOVE EVERY BYTE
*
NOCURSR  DS    0H
         SR    R14,14
         C     R14,MSGADD               IS THERE A MESSAGE
         BE    BLDBUMP2                 NO, GO BUMP
*
         CLI   SPLIT,2                  AM I IN SPLIT-SCREEN 2
         BE    CHKMSG2                  YES, SKIP THIS
         LA    R14,TERMOUT+4            SET LINE 1 ADDRESS
         CR    R6,R14                   AM I ON LINE1
         BE    BLDBUMP1                 YES I AM
         B     BLDBUMP2                 NO, GO BUMP
*
CHKMSG2  DS    0H
         L     R14,SCROWS1
         SLL   R14,2
         LA    R14,TERMOUT+4(14)        SET ADDR OF LINE1, SCRN2
         CR    R6,R14                   AM I ON LINE1
         BNE   BLDBUMP2                 NO, GO BUMP
*
BLDBUMP1 DS    0H
*
         LR    R15,R7
         LA    R14,20
         SR    R15,R14
         LA    R15,4(R1,15)
         L     R14,MSGADD               MESSAGE ADDRESS
         MVC   0(20,R15),0(R14)
         XC    MSGADD(4),MSGADD         CLEAR THE MESSAGE
         L     R14,BUFF                 BUFFER ADDRESS
         MVC   1(1,R14),ALARMWCC        SOUND ALARM
*
BLDBUMP2 DS    0H
         LA    R8,4(,8)                 BUMP TO NEXT SBACODES
*
         TM    0(R6),X'80'              END OF LINES
         BO    SETCURSR                 LET 'ER RIP
*
         LA    R6,4(,6)                 BUMP TO NEXT PARM
         BCT   R9,BLDBUFFS              GO DO IT
*                               *
SETCURSR DS    0F
         MVI   0(R7),X'11'
         SR    R0,R0
         SR    R1,R1
         IC    R0,AIDROW        *  ROW
         CLI   SPLIT,2
         BNE   *+8
         A     R0,SCROWS1       *  ROW ON SPLIT SCREEN
         IC    R1,AIDCOL        *  COL
         MH    R0,=H'80'
         AR    R1,R0
         SR    R0,R0
         D     R0,=F'64'
         STC   R1,1(,R7)
         STC   R0,2(,R7)
         TR    1(2,R7),BINTOEBC
         MVI   3(R7),X'13'
         LA    R5,4(,R5)
         ST    R5,BUFFL1                MAKE SURE THE LENGTH IS SET
*                               *
*
         B     WRTBUFF                  LET 'ER RIP
*
BINTOEBC DS    0F
*                0 1 2 3 4 5 6 7 8 9 A B C D E F
         DC    X'40C1C2C3C4C5C6C7C8C94A4B4C4C4E4F'  0
         DC    X'50D1D2D3D4D5D6D7D8D95A5B5C5D5E5F'  1
         DC    X'6061E2E3E4E5E6E7E8E96A6B6C6D6E6F'  2
         DC    X'F0F1F2F3F4F5F6F7F8F97A7B7C7D7E7F'  3
*                0 1 2 3 4 5 6 7 8 9 A B C D E F
*                               *
ATTRIBS  DS    0H               *   FIELD ATTRIBUTE BYTES
*                               *
         DC    X'1D40'          *   OFFSET ZERO
ATTRIB01 DC    X'1D40'          *   UNPROTECTED, DISPLAY
ATTRIB02 DC    X'1DC8'          *   UNPROTECTED, DISPLAY, INTENSIFIED
ATTRIB03 DC    X'1D4C'          *   UNPROTECTED, NONDISPLAY
ATTRIB04 DC    X'1D60'          *   PROTECTED, DISPLAY
ATTRIB05 DC    X'1DE8'          *   PROTECTED, DISPLAY, INTENSIFIED
ATTRIB06 DC    X'1D6C'          *   PROTECTED, NONDISPLAY
*                               *
MOVINADD MVC   0(1,R7),0(R2)
MOVINBUF MVC   3(1,R7),1(R2)
         EJECT
WRTBUFF  DS    0H
*
**  LOG BUFFER BEFORE WRITE TO TRACK PROG402, ETC
*
*        L     R4,BUFF                  ACTUAL WRITE BUFFER
*        LA    R1,8
*        SR    R4,R1
*        MVC   0(2,R4),BUFFL1+2
*        MVC   2(2,R4),AIDADDR
*        MVC   4(2,R4),SCROWS1+2
*        OPEN  (BUFFLOG,(EXTEND))
*
*        TM    BUFFLOG+48,X'10'
*        BZ    BUFFLOG9
*
*        PUT   BUFFLOG,(4)
*        CLOSE BUFFLOG
*        B     BUFFLOG9
*
*BUFFLOG  DCB   DDNAME=BUFFLOG,DSORG=PS,MACRF=PM
*
*BUFFLOG9 DS    0H
*
         L     R4,BUFF                  ACTUAL WRITE BUFFER
         L     R5,BUFFL1                PICK UP THE LENGTH
         MVI   RECB,X'80'               RESET ECB
WRITUBE1 DS    0H
         CLC   TUBEDDNM(4),=C'VTAM'    VTAM     TERMINAL
         BE    WRITUBEV          YES
*
         CLC   TUBEDDNM(3),=C'TSO'     TSO      TERMINAL
         BE    WRITUBET          YES
*
         MVC   CCW1(8),SELECT0B
         MVC   CCW2(1),WRITECC          COMMAND CODE SET BASED ON
         MVC   CCW2+1(7),WRITECCW+1     TERMINAL TYPE
*
         STCM  R5,3,CCW2+6              SET THE LENGTH
         STCM  R4,7,CCW2+1              SET THE DATA ADDRESS
*
         LA    R1,CCW1
         STCM  R1,7,TUBEIOB+17
         LA    R2,RECB           SET ECB ADDRESS
         ST    R2,TUBEIOB+4                       IN THE IOB
         EXCP  TUBEIOB
*
         WAIT  ECB=RECB
*
         CLI   RECB,X'44'               CHECK FOR INTERCEPT
         BE    RESCREEN                 IF YES,RE-WRITE SCREEN
         CLI   RECB,X'41'               CHECK FOR PERMANENT I/O ERR
         BE    PERMIOER                 TRY TO RECOVER
         CLI   RECB,X'7F'               CHECK FOR OTHERWISE OKAY
         BNE   WTOMS12                  IF NOT,DO ERROR RECOVERY
         MVI   PERMERR,0                FIRST TIME THROUGH
*
TUBEREAD DS    0H
*
**  START OF ETPS ACCESS METHOD READ ROUTINE
*
         CLI   EXCPFLG2,0               ETPS ACCESS METHOD
         BE    STDEXCP                  NO
*
         L     R6,UCBADDR
         USING UCBCMSEG,6
         MODESET MF=(E,SUPRMOD)
         XC    UCBRDYQ(4),UCBRDYQ    CLEAR ECB
         WAIT ECB=UCBRDYQ
         MODESET MF=(E,PROBMOD)
*
         L     R5,SCREENSZ              PICK UP BUFFER LENGTH
*
         L     R4,REPLY                 PICK UP BUFFER ADDRESS
         MVI   RECB,X'80'               RESET ECB
*
         MVC   CCW1(8),SELECT0B
         MVC   CCW2(8),READCCW
         STCM  R5,3,CCW2+6              SET THE LENGTH
         STCM  R4,7,CCW2+1              SET THE DATA ADDRESS
*
         LA    R1,CCW1
         STCM  R1,7,TUBEIOB+17
         LA    R2,RECB           SET ECB ADDRESS
         ST    R2,TUBEIOB+4                       IN THE IOB
*
         EXCP  TUBEIOB
*
         WAIT  ECB=RECB
*
         B     TESTREAD                 GO CHECK READ
*
EXCPFLG2 DC    X'00'   0=STRAIGHT EXCP, 1=ETPS ACCESS METHOD
*
**  END OF ETPS ACCESS METHOD READ ROUTINE
*
STDEXCP  DS    0H
*
         L     R5,SCREENSZ              PICK UP BUFFER LENGTH
*
         L     R4,REPLY                 PICK UP BUFFER ADDRESS
         MVI   RECB,X'80'               RESET ECB
*
         MVC   CCW1(8),SELECT0B
         MVC   CCW2(8),READCCW
         STCM  R5,3,CCW2+6              SET THE LENGTH
         STCM  R4,7,CCW2+1              SET THE DATA ADDRESS
*
         LA    R1,CCW1
         STCM  R1,7,TUBEIOB+17
         LA    R2,RECB           SET ECB ADDRESS
         ST    R2,TUBEIOB+4                       IN THE IOB
*
TUREREAD DS    0H
         EXCP  TUBEIOB
*
         WAIT  ECB=RECB
*
         CLI   0(R4),X'60' GOT ANY DATA??
         BNE   TESTREAD                 GO CHECK READ
*
         STIMER WAIT,BINTVL=TWOSEC
         B     TUREREAD                 GO CHECK READ
*
TWOSEC   DC    F'120'
*
*
WRITUBEV DS    0H
*
         L     R1,PSATOLD        PICK UP TCB
         USING TCB,1
         L     R1,TCBUSER        TCBUSER FIELD
         USING TCBUVTAM,1
         L     R15,TCBUVPUT
*
         L     R1,BUFF                  ACTUAL WRITE BUFFER
         L     R0,BUFFL1                PICK UP THE LENGTH
         AH    R0,VTAMADD1              ADD 1
         BALR  R14,R15
*
         LTR   R15,R15
         BNZ   WBADVTAM
*
         L     R1,PSATOLD        PICK UP TCB
         USING TCB,1
         L     R1,TCBUSER        TCBUSER FIELD
         USING TCBUVTAM,1
         L     R15,TCBUVGET
         L     R0,SCREENSZ              PICK UP BUFFER LENGTH
         L     R1,REPLY                 PICK UP BUFFER ADDRESS
*
         BALR  R14,R15
*
         LTR   R15,R15
         BNZ   RBADVTAM
*
         L     R1,PSATOLD        PICK UP TCB
         USING TCB,1
         L     R1,TCBUSER        TCBUSER FIELD
         USING TCBUVTAM,1
         LH    R1,TCBUGETL              PICK UP ACTUAL READ LENGTH
         L     R0,SCREENSZ              PICK UP BUFFER LENGTH
         SR    R0,R1                    SET R0 = RESIDUAL LENGTH
         STH   R0,TUBEIOB+14            TUCK IT AWAY IN IOB
         MVI   RECB,X'7F'               SET READ OK
         B     TESTREAD
*
VTAMADD1 DC    H'1'
*
WBADVTAM DS    0H
*
         LR    R2,R15                    SAVE RETURN CODE
         WTO   'BAD WRITE RETURN CODE FROM VTAM',ROUTCDE=2,DESC=(2)
         DC    H'0'
*
RBADVTAM DS    0H
*
         LR    R2,R15                    SAVE RETURN CODE
         WTO   'BAD READ  RETURN CODE FROM VTAM',ROUTCDE=2,DESC=(2)
         DC    H'0'
*
WRITUBET DS    0H
*
         PRINT GEN
*
         L     R1,BUFF                  ACTUAL WRITE BUFFER
         BCTR  R1,0                     BACK UP 1 BYTE
         MVI   0(R1),X'27'              SET TSO WRITE SEQUENCE
         L     R0,BUFFL1                PICK UP THE LENGTH
         AH    R0,TSOADD1               ADD 1 BYTE
*
         TPUT  (1),(0),FULLSCR
*
         LTR   R15,R15
         BNZ   WBADTSO
*
         L     R14,REPLY
         MVI   0(R14),0
         LA    R14,1(,R14)
         L     R15,SCREENSZ
         BCTR  R15,0
         L     R0,REPLY
         LR    R1,R15
         MVCL  R14,R0
*
         L     R0,SCREENSZ              PICK UP BUFFER LENGTH
         L     R1,REPLY                 PICK UP BUFFER ADDRESS
*
         TGET  (1),(0),ASIS
*
         LTR   R15,R15
         BNZ   RBADTSO
*
         L     R0,SCREENSZ              PICK UP BUFFER LENGTH
         SR    R0,R1                    SET R0 = RESIDUAL LENGTH
         STH   R0,TUBEIOB+14            TUCK IT AWAY IN IOB
         MVI   RECB,X'7F'               SET READ OK
         B     TESTREAD
*
         PRINT NOGEN
TSOADD1  DC    H'1'
*
WBADTSO  DS    0H
*
         LR    R2,R15                    SAVE RETURN CODE
         WTO   'BAD WRITE RETURN CODE FROM TPUT',ROUTCDE=2,DESC=(2)
         DC    H'0'
*
RBADTSO  DS    0H
*
         LR    R2,R15                    SAVE RETURN CODE
         WTO   'BAD READ  RETURN CODE FROM TGET',ROUTCDE=2,DESC=(2)
         DC    H'0'
*
*
SEC10    DC    F'1000' 10 SECOND WAIT
WRITECCW DC    X'0D',AL3(0),X'2000',AL2(0)
NOOP     DC    X'04',AL3(0),X'2000',AL2(1)
SELECT0B DC    X'0B',AL3(0),X'6000',AL2(1)
READCCW  DC    X'06',AL3(0),X'2000',AL2(0)
PERMIOER DS    0H
         CLI   PERMERR,0                FIRST TIME THROUGH?
         BNE   WTOMS41                  NOPE
         MVI   PERMERR,1                NOT FIRST TIME THROUGH.
         B     RESCREEN                 TRY TO RESHOW THE SCREEN
COPYMSG  MVC   0(1,R3),2(R2)            MOVE MSG INTO BUF-VAR INSTR
INFO     EQU   X'01'                    DO NO DO READ TI,NO REPLY
FIRST    EQU   X'80'                    FIRST READ/WRITE
*        EXAMINE READ RESULT AND SET POINTERS TO INPUT FIELDS
TESTREAD DS    0H
*
*
**  LOG BUFFER AFTER READ
*
*        L     R4,REPLY                 READ-IN BUFFER
*        LA    R1,4
*        SR    R4,R1
*        XC    0(4,R4),0(R4)
*        MVC   0(2,R4),TUBEIOB+14
*        OPEN  (BUFFLOG,(EXTEND))
*
*        TM    BUFFLOG+48,X'10'
*        BZ    BUFFLOGZ
*
*        PUT   BUFFLOG,(4)
*        CLOSE BUFFLOG
*        B     BUFFLOGZ
*
*BUFFLOG  DCB   DDNAME=BUFFLOG,DSORG=PS,MACRF=PM
*
*BUFFLOGZ DS    0H
*
*
         LA    R14,TERMINPT+4           NOW BUILD INADDRESSES
         L     R1,SCROWS1          PICK UP SIZE OF SCREEN1
*
         CLI   SPLIT,2                  AM I ON SCREEN 2
         BNE   *+20                     NOPE
*
         L     R3,SCROWS1          PICK UP SIZE OF SCREEN1
         SLL   R3,2                MULTIPLY BY 4
         LA    R14,TERMINPT+4(R3)  PICK UP RESPONSES
         L     R1,SCROWS2          PICK UP SIZE OF SCREEN2
*
         L     R15,0(,R14)              STORE THE ADDRESS
         MVI   0(R15),X'00'             SET FIRST BYTE TO ZEROS
         LA    R14,4(,14)               BUMP TO NEXT INADDR
         BCT   R1,*-12                  LOOP 43 TIMES
*
         LA    R4,TERMINPT              GET A(BUFFER)
         L     R3,REPLY                 GET A(BUFFER)
         CLI   RECB,X'44'               CHECK IF READ INTERCEPTED
         BE    RESCREEN                 IF YES,ASSUME CLEAR WAS HIT
         CLI   RECB,X'41'               PERMANENT IO ERROR
         BE    PERMIOER                 -RECOVER IF NOT
         CLI   RECB,X'7F'               CHECK FOR OTHERWISE OKAY
         BNE   WTOMS14                  -RECOVER IF NOT
         MVI   PERMERR,0                FIRST TIME THROUGH
*
         CLI   HELPFLAG,0               AM I DOING HELP ALREADY
         MVI   HELPFLAG,0               SHUT IT OFF
         BNE   AFTRHELP                 GO REDISPLAY THE SCREEN
*
         MVI   PFKFLAG,0                NOT A PFK/ATTN
         CLI   0(R3),X'7D'              IS IT ENTER
         BE    *+8                      YES
         MVI   PFKFLAG,1                IF NOT ENTER, MUST BE PFK
*
         L     R5,SCREENSZ              START EXAMINING BUFFER FOR DATA
         SH    R5,TUBEIOB+14            (RESIDUAL COUNT)
         LA    R9,1(R5,R3)              LAST BYTE OF INPUT READ
         CLC   =X'016C6102',0(R3)       CHECK FOR TEST-REQ
         BE    RESCREEN                 RESET SCREEN IF YES
SREAD    DS    0H
         L     R14,0(,R4)               PICK UP ADDR FOR AID
         MVC   0(1,R14),0(R3)           MOVE IN AID
         MVC   SBAWORK(2),1(R3)         MOVE IN AID ADDRESS
*
         BAL   R14,SBAXLATE             TRANSLATE TO ROW AND COLUMN
         MVC   AIDADDR(2),SBAWORK       SAVE CURSOR POSITION
*
         CLI   SPLIT,2
         BNE   SREAD2
*
         SR    R0,R0
         IC    R0,AIDROW
         S     R0,SCROWS1
         STC   R0,AIDROW
*
SREAD2   DS    0H
         L     R14,0(,R4)               PICK UP ADDR FOR AID
         MVC   1(2,R14),SBAWORK         MOVE CURSOR POSITION
*
TFORHELP DS    0H
*
         CLI   0(R3),X'F1'              IS IT HELP PFKEY
         BE    *+12                     YUP
         CLI   0(R3),X'C1'              IS IT HELP PFKEY
         BNE   TSPLIT                   NO, SEE IF IT'S SPLIT-SCREEN
*
         MVI   HELPFLAG,1               TURN IT ON
         B     BLDBUFF                  DISPLAY HELP
*
TSPLIT   DS    0H
*
         CLI   0(R3),X'F2'              IS IT SPLIT PFKEY
         BE    *+12                     YUP
         CLI   0(R3),X'C2'              IS IT SPLIT PFKEY
         BNE   TSWITCH                  OTHERWISE, TEST FOR SWITCH
*
         CLI   SPLIT,0                  SPLIT-SCREEN ALREADY?
         BE    *+16                     NO, SPLIT IT
         L     R2,TERMINPT              PICK UP RESPONSE ADDRESS
         MVI   0(R2),X'6E'              RESET IT TO PA2
         B     TRETURN                  JUST EXECUTE PA2
*
         L     R1,SCROWS                SET IT
         SRL   R1,1                     DIVIDE BY 2
         ST    R1,SCROWS1               STORE IT
         L     R0,SCROWS                PICK IT UP
         SR    R0,R1                    SUBTRACT
         ST    R0,SCROWS2               STORE THE REMAINDER
*        TM    SCROWS+3,1               EVEN NUMBER OF ROWS?
*        BZ    *+12                     YES
*        A     R0,=F'1'                 BUMP 1
*        ST    R0,SCROWS2               STORE IT
*
*
         MVI   SPLIT,2                  INDICATE SCREEN 2
         L     R14,SAVETOP              PICK UP ADDRESS OF SAVE1
         L     R15,SPLTMOVL             LENGTH OF SPLIT AREA
         LR    R1,R15
         L     R0,SPLIT14A
         AR    R0,R13
*
         MVCL  R0,R14
*
         L     R14,SAVETOP              PICK UP ADDRESS OF SAVE1
         L     R15,SPLTMOVL             LENGTH OF SPLIT AREA
         LR    R1,R15
         L     R0,SPLIT24A
         AR    R0,R13
         MVCL  R14,R0
*
         L     R14,R14ADDR              PICK UP RETURN ADDRESS
         LM    R2,R13,PRISPLIT
         BR    R14                      SHOW PRIMARY OPTION MENU
*
R14ADDR  DC    A(REPRIOPT)
SPLTMOVL DC    A(AREALEN)
SPLIT14A DC    A(SPLIT140)    OFFSET OF SPLIT1 AREA
SPLIT24A DC    A(SPLIT240)    OFFSET OF SPLIT1 AREA
*                              OF BUFFERS
TSWITCH  DS    0H
*
         CLI   0(R3),X'F9'              IS IT SWITCH SCREEN
         BE    *+12                     YUP
         CLI   0(R3),X'C9'              IS IT SWITCH SCREEN
         BNE   TENDPFK                  OTHERWISE, TEST FOR END PFK
*
         L     R2,TERMINPT              PICK UP RESPONSE ADDRESS
         MVI   0(R2),X'6E'              RESET IT TO PA2
*
         CLI   SPLIT,0                  IN SPLIT-SCREEN MODE?
         BE    TRETURN                  NO, JUST EXECUTE PA2
*
         CLI   SPLIT,1                  AM I ON TOP SCREEN
         BNE   SWBOTTOP                 NOPE, GO FROM BOTTOM TO TOP
*
         L     R14,SAVETOP              PICK UP ADDRESS OF SAVE1
         L     R15,SPLTMOVL             LENGTH OF SPLIT AREA
         LR    R1,R15
         L     R0,SPLIT14A
         AR    R0,R13
         MVCL  R0,R14
*
         MVI   SPLIT,2                  INDICATE SCREEN 2
         L     R14,SAVETOP              PICK UP ADDRESS OF SAVE1
         L     R15,SPLTMOVL             LENGTH OF SPLIT AREA
         LR    R1,R15
         L     R0,SPLIT24A
         AR    R0,R13
         MVCL  R14,R0
*
         B     SETASWCH                 SET UP FOR SWITCH
*
SWBOTTOP DS    0H
*
         L     R14,SAVETOP              PICK UP ADDRESS OF SAVE1
         L     R15,SPLTMOVL             LENGTH OF SPLIT AREA
         LR    R1,R15
         L     R0,SPLIT24A
         AR    R0,R13
         MVCL  R0,R14
*
         L     R14,SAVETOP              PICK UP ADDRESS OF SAVE1
         L     R15,SPLTMOVL             LENGTH OF SPLIT AREA
         LR    R1,R15
         L     R0,SPLIT14A
         AR    R0,R13
         MVCL  R14,R0
*
         MVI   SPLIT,1                  TURN IT ON
*
SETASWCH DS    0H
         B     RESCREEN
*
TENDPFK  DS    0H
*
         CLI   0(R3),X'F3'              IS IT END PFK
         BE    *+12                     YUP
         CLI   0(R3),X'C3'              IS IT END PFK
         BNE   LREAD                    OTHERWISE, GO PROCESS
*
         CLI   SPLIT,0                  SPLIT-SCREEN?
         BE    TRETURN                  NOPE, JUST GO PROCESS
*
         CLI   PRIMEFLG,0          PRIMARY OPTION MENU ON SCREEN 1?
         BE    TRETURN             NO, JUST RETURN
*
         CLI   SPLIT,2                  AM I ON BOTTOM SCREEN
         BE    SWBOTEOF                 YES, GO END THE BOTTOM SCREEN
*
         L     R2,TERMINPT              PICK UP RESPONSE ADDRESS
         MVI   0(R2),X'6E'              RESET IT TO PA2
*
         L     R14,SAVETOP              PICK UP ADDRESS OF SAVE1
         L     R15,SPLTMOVL             LENGTH OF SPLIT AREA
         LR    R1,R15
         L     R0,SPLIT24A
         AR    R0,R13
         MVCL  R14,R0
*
         MVC   SCROWS1(4),SCROWS        RESET NUMBER OF ROWS
         XC    SCROWS2(4),SCROWS2       CLEAR THIS
*
         MVI   SPLIT,0                  INDICATE NOT DOING SPLIT-SCREEN
         B     TRETURN                  OTHERWISE, GO PROCESS
*
SWBOTEOF DS    0H
*
         L     R2,TERMINPT              PICK UP RESPONSE ADDRESS
         MVI   0(R2),X'6E'              RESET IT TO PA2
*
         L     R14,SAVETOP              PICK UP ADDRESS OF SAVE1
         L     R15,SPLTMOVL             LENGTH OF SPLIT AREA
         LR    R1,R15
         L     R0,SPLIT14A
         AR    R0,R13
         MVCL  R14,R0
*
         MVC   SCROWS1(4),SCROWS        RESET NUMBER OF ROWS
         XC    SCROWS2(4),SCROWS2       CLEAR THIS
*
         MVI   SPLIT,0                  INDICATE NOT IN SPLIT-SCREEN
         B     TRETURN                  GO PROCESS PA2
*
SETAEOF  DS    0H
         B     RESCREEN
*
AFTRHELP DS    0H
         B     RESCREEN
         EJECT
LREAD    DS    0H
         C     R5,=F'3'                RETURN IF ENTER WITH NO DATA
         BL    TRETURN
*
         LA    R4,4(,R4)                BUMP TO NEXT ADDRESS
         LA    R3,3(,R3)                POINT PAST AID AND CURSOR
         CLI   0(R3),X'11'              LOOK FOR SBA
         BNE   WTOMS15                  IF NOT, ITS AN ERROR
         LR    R8,R3                    START OF DATA
         SR    R6,6                     LENGTH OF DATA
         B     GOTSBA2                  AFTER START OF SBA LOOP
GOTSBA1  DS    0H
         BCTR  R6,0
         EX    R6,REPLYCAP              UPPER-CASE THE INPUT
         EX    R6,REPLYMVC              MOVE IT INTO THE LINE
         SR    R6,6                     LENGTH OF DATA
         LR    R8,R3                    START OF DATA
GOTSBA2  DS    0H
*
* NOW I HAVE TO PICK UP THE SBA AND LOCATE THE CORRESPONDING
* OUTPUT LINE.
*
* FIRST TRANSLATE SBA CODES TO ROW AND COLUMN
*
         CLI   0(R8),X'11'              DO I HAVE AN SBA
         BNE   TRETURN
         MVC   SBAWORK(2),1(R8)         SET ADDR OF SBA
         BAL   R14,SBAXLATE
*
* NOW MOVE THE IMAGE OF THE ROW THAT WAS TRANSMITTED TO THE TERMINAL
* INTO MY INPUT BUFFER SO THAT I CAN SIMULATE A FULL-LINE READ.
*
         LA    R14,TERMOUT+4            LINE 1 THAT WAS DISPLAYED
         SR    R15,15                   CLEAR REGISTER
         IC    R15,SBAROW               PICK UP LINE NUMBER (REL 0)
         SLL   R15,2                    MULTIPLY BY 4
         L     R2,0(R15,R14)            POINT TO ACTUAL OUTPUT LINE
         LA    R14,TERMINPT+4           POINT TO INPUT LINE SLOT
         L     R7,0(R15,R14)            POINT TO ACTUAL INPUT LINE
         LA    R4,0(R15,R14)            SET ADDRESS POINTER
*
         CLI   0(R7),X'00'              IS THIS A BLANK LINE
         BNE   *+14                     NO, SECOND FIELD ON THIS LINE
*
         SR    R14,14                   CLEAR REGISTER
         IC    R14,0(,R2)               PICK UP LENGTH
         EX    R14,MOVINADD             MOVE LINE IMAGE TO INPT BUFF
*
         SR    R14,14                   CLEAR REG
         IC    R14,SBACOL               PICK UP COLUMN (REL 0)
         LA    R7,1(R7,R14)             POINT TO ACTUAL INPUT LOCATION
*
* NOW I HAVE TO FIND THE LENGTH OF INPUT THAT WAS ENTERED AT THE
* TERMINAL SO I CAN MOVE IT INTO THE LINE IMAGE
*
         LA    R3,1(,3)                 BUMP ADDRESS
SBABUMP1 DS    0H
         CLI   0(R3),X'11'              LOOK FOR NEXT SBA
         BE    GOTSBA1                  GOT IT
         LA    R3,1(,3)                 BUMP ADDRESS
         LA    R6,1(,6)                 BUMP LENGTH
         CR    R3,R9                    COMPARE VS LAST BYTE READ IN
         BL    SBABUMP1                 KEEP LOOPING
GOTSBAS  DS    0H
         BCTR  R6,0
         EX    R6,REPLYCAP              UPPER-CASE THE INPUT
         EX    R6,REPLYMVC              MOVE IT INTO THE LINE
TRETURN  DS    0H
         SR    R15,15                   SET RC=0
TRETURN2 DS    0H
         OI    0(R4),X'80'              FLAG AS LAST ENTRY
         L     R13,4(,13)               RESTORE CALLERS REGS
         LM    R0,R12,20(R13)          RESTORE CALLERS REGS
         L     R14,12(R13)              PRESERVE R15
         BR    R14                      NORMAL RETURN
         TITLE 'SBA TRANSLATE TO ROW AND COLUMN'
SBAXLATE DS    0H
         STM   R14,R1,12(R13)           SAVE SBAS WORK REGISTERS
*                               *
         LA    R15,SBAWORK
         TR    0(2,R15),EBCTOBIN
         NC    0(2,R15),=X'3F3F'
         SR    0,0
         SR    1,1
         IC    R0,0(,R15)
         IC    R1,1(,R15)
         SLL   R0,6
         AR    R1,R0
         SR    0,0
         D     R0,=F'80'
         STC   R1,SBAROW        0-42
         STC   R0,SBACOL        1-80
*
         LM    R14,R1,12(R13)         RESTOR SBAS WORK REGISTERS
         BR    R14
*
*
EBCTOBIN DC    256AL1(*-EBCTOBIN)
         ORG   EBCTOBIN+X'40'
         DC    X'C0'
         ORG   EBCTOBIN+X'4A'
         DC    X'CACBCCCDCECF'
         ORG   EBCTOBIN+X'50'
         DC    X'D0'
         ORG   EBCTOBIN+X'5A'
         DC    X'DADBDCDDDEDF'
         ORG   EBCTOBIN+X'60'
         DC    X'E0E1'
         ORG   EBCTOBIN+X'6A'
         DC    X'EAEBECEDEEEF'
         ORG   EBCTOBIN+X'7A'
         DC    X'FAFBFCFDFEFF'
         ORG
*
         TITLE 'ETPS - CAPS ONLY TRT TABLE'
CAPSONLY DC    CL16' '
         DC    X'1111'         SBA
         DC    CL46' '
         DC    CL10' '
         DC    C'.<(+|&&'
         DC    CL9' '
         DC    C'!$*)'
         DC    C' '             NOTE SEMICOLON CHANGED TO BLANK
         DC    C'-/'
         DC    CL9' ',C',%_>?',CL10' ',C':#@''="'
         DC    CL16' ABCDEFGHI      '
         DC    CL16' JKLMNOPQR      '
         DC    CL16'  STUVWXYZ      '
         DC    CL16'                '
         DC    CL16' ABCDEFGHI      '
         DC    CL16' JKLMNOPQR      '
         DC    CL16'  STUVWXYZ      '
         DC    CL16'0123456789      '
REPLYCAP TR    3(1,R8),CAPSONLY
REPLYMVC MVC   0(1,R7),3(R8)
WTOMS11  WTO   'ETPS1011 COMM   BAD WRITE TO TUBE      ',ROUTCDE=11
         LA    R15,11
         B     TRETURN2
WTOMS12  WTO   'ETPS1012 COMM   BAD ECB CODE ON WRITE  ',ROUTCDE=11
         LA    R15,12
         B     TRETURN2
WTOMS13  WTO   'ETPS1013 COMM   BAD READ FROM TUBE     ',ROUTCDE=11
         LA    R15,13
         B     TRETURN2
WTOMS14  WTO   'ETPS1014 COMM   BAD ECB CODE ON READ   ',ROUTCDE=11
         LA    R15,14
         B     TRETURN2
WTOMS15  WTO   'ETPS1015 COMM   INVALID SBA CODE       ',ROUTCDE=11
         DC    H'0'
         LA    R15,15
         B     TRETURN2
WTOMS41  WTO   'ETPS1041 COMM   PERMANENT I/O ERROR    ',ROUTCDE=11
         DC    H'0'
         B     TRETURN2
         SPACE 2
         LTORG
         TITLE 'ETPS - PAN LIBRARY NAMES'
*
*
PANAMES  CSECT
         DC    CL45'NETPS1.PANLIB'
         DC    CL45'CPROD.JCL'
         DC    CL45'CP.SOURCE'
         DC    CL45'CCICS.SOURCE'
         DC    CL45'NT.SOURCE'
         DC    CL45'NP.SPOOL'
         DC    45X'FF'
         TITLE 'ETPS - BUFFER ADDRESSES'
*
* THE SET BUFFER ADDRESS (SBA) ORDER CODE IS FOLLOWED BY A 2-BYTE
* BUFFER ADDRESS THAT CORRESPONDS (BUT NOT IN ANY RATIONAL WAY) TO
* THE ROWS AND COLUMNS ON THE DISPLAY TUBE. THIS TABLE IS PROVIDED
* TO SUPPORT DYNAMICALLY BUILDING THE BUFFER ADDRESS FIELDS WHEN THE
* ROW AND COLUMN IS KNOWN.
*
* EACH BUFFER ADDRESS ENTRY CORRESPONDS TO A ROW ON THE DISPLAY
* THE FIRST BYTE IS THE SAME FOR ALL ROWS, THE SECOND, THIRD AND
* FOURTH BYTES DEFINE COLUMNS 1, 7, AND 8, RESPECTIVELY.
*
* TO PICK UP THE BUFFER ADDRESS YOU WANT, MULTIPLY THE ROW NUMBER
* BY THE LENGTH OF EACH BUFFER ADDRESS ENTRY AND ADD THE START ADDRESS
* OF THE TABLE.
*
* FOR EXAMPLE, THE ROW3 ENTRY IS =>  X'C1',X'50',X'D6',X'D7'
* THE BUFFER ADDRESS CODES ARE:
*
* ROW 3, COL 1 = X'C150'
* ROW 3, COL 7 = X'C1D6'
* ROW 3, COL 8 = X'C1D7'
*
BUFF3278 CSECT
ROW781   DC    X'40',X'40',X'C6',X'C7'
BALENGTH EQU   *-ROW781
ROW782   DC    X'C1',X'50',X'D6',X'D7'
ROW783   DC    X'C2',X'60',X'E6',X'E7'
ROW784   DC    X'C3',X'F0',X'F6',X'F7'
ROW785   DC    X'C5',X'40',X'C6',X'C7'
ROW786   DC    X'C6',X'50',X'D6',X'D7'
ROW787   DC    X'C7',X'60',X'E6',X'E7'
ROW788   DC    X'C8',X'F0',X'F6',X'F7'
ROW789   DC    X'4A',X'40',X'C6',X'C7'
ROW7810  DC    X'4B',X'50',X'D6',X'D7'
ROW7811  DC    X'4C',X'60',X'E6',X'E7'
ROW7812  DC    X'4D',X'F0',X'F6',X'F7'
ROW7813  DC    X'4F',X'40',X'C6',X'C7'
ROW7814  DC    X'50',X'50',X'D6',X'D7'
ROW7815  DC    X'D1',X'60',X'E6',X'E7'
ROW7816  DC    X'D2',X'F0',X'F6',X'F7'
ROW7817  DC    X'D4',X'40',X'C6',X'C7'
ROW7818  DC    X'D5',X'50',X'D6',X'D7'
ROW7819  DC    X'D6',X'60',X'E6',X'E7'
ROW7820  DC    X'D7',X'F0',X'F6',X'F7'
ROW7821  DC    X'D9',X'40',X'C6',X'C7'
ROW7822  DC    X'5A',X'50',X'D6',X'D7'
ROW7823  DC    X'5B',X'60',X'E6',X'E7'
ROW7824  DC    X'5C',X'F0',X'F6',X'F7'
ROW7825  DC    X'5E',X'40',X'C6',X'C7'
ROW7826  DC    X'5F',X'50',X'D6',X'D7'
ROW7827  DC    X'60',X'60',X'E6',X'E7'
ROW7828  DC    X'61',X'F0',X'F6',X'F7'
ROW7829  DC    X'E3',X'40',X'C6',X'C7'
ROW7830  DC    X'E4',X'50',X'D6',X'D7'
ROW7831  DC    X'E5',X'60',X'E6',X'E7'
ROW7832  DC    X'E6',X'F0',X'F6',X'F7'
ROW7833  DC    X'E8',X'40',X'C6',X'C7'
ROW7834  DC    X'E9',X'50',X'D6',X'D7'
ROW7835  DC    X'6A',X'60',X'E6',X'E7'
ROW7836  DC    X'6B',X'F0',X'F6',X'F7'
ROW7837  DC    X'6D',X'40',X'C6',X'C7'
ROW7838  DC    X'6E',X'50',X'D6',X'D7'
ROW7839  DC    X'6F',X'60',X'E6',X'E7'
ROW7840  DC    X'F0',X'F0',X'F6',X'F7'
ROW7841  DC    X'F2',X'40',X'C6',X'C7'
ROW7842  DC    X'F3',X'50',X'D6',X'D7'
ROW7843  DC    X'F4',X'60',X'E6',X'E7'
BTABLEN4 EQU   *-ROW781
         TITLE 'DYNAM - DYNAMIC ALLOCATION INTERFACE'
DYNAM    CSECT
         USING *,R12
         SAVE  (14,12),,*
         LR    R12,R15
         L     R11,0(0,R1)         PICK UP PARAMETER LIST ADDRESS.
         USING PARMLIST,R11
         L     R0,WORKLEN
         LA    R1,SAVEAREA
         LR    R2,R13
         LR    R13,1
         USING SAVEAREA,R13
         LR    R14,R1              SET UP FOR CLEAR OF WORK AREA.
         L     R15,WORKLEN
         SR    R0,R0
         SR    R1,R1
         MVCL  R14,R0              CLEAR WORK AREA TO ALL X'00'
         ST    R2,SAVEAREA+4
         ST    R13,8(0,R2)
         LA    R10,DYNWORK
         USING S99RB,R10
         ST    R10,REQBKPTR        POINT TO REQUEST BLOCK.
         OI    REQBKPTR,X'80'
         LA    R1,S99RBEND-S99RB   LENGTH OF REQUEST BLOCK.
         STC   R1,S99RBLN
         CLC   DSNAME,NULLS        CHECK FOR UNALLOCATION REQUEST.
         BNE   *+12
         MVI   S99VERB,S99VRBUN    REQUEST UNALLOCATION.
         B     *+12
         MVI   S99VERB,S99VRBAL    REQUEST ALLOCATION.
         OI    S99FLG11,S99NOCNV+S99NOMNT
         LA    R8,S99RBEND         PICK UP ADDRESS FOR TEXT POINTERS
         ST    R8,S99TXTPP
         L     R9,TXPTRSPC         PICK UP SPACE RESERVED FOR TEXT PTRS
         ALR   R9,R8               POINT TO FIRST TEXT UNIT.
         USING S99TUNIT,R9
         SPACE 1
* DDNAME **************************
         SPACE 1
         LA    R7,DDNAME-PARMLIST  INIT 'OFFSET' REGISTER.
         CLI   S99VERB,S99VRBUN    HAS UNALLOCATION BEEN REQUESTED?
         BNE   *+12
         LA    R1,DUNDDNAM
         B     *+8
         LA    R1,DALDDNAM
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         LA    R14,DDNAME          FIND OUT ACTUAL LENGTH OF DDNAME.
         LA    R15,L'DDNAME
         BAL   R1,PP000
         LTR   R15,R15
         BNP   QQ000
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM         MOVE DDNAME.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1(R15)  NEXT TEXT UNIT.
         CLI   S99VERB,S99VRBUN    HAS UNALLOCATION BEEN REQUESTED?
         BE    MM000               GO DO REQUEST.
         SPACE 1
* DSNAME **************************
         SPACE 1
         LA    R7,DSNAME-PARMLIST  OFFSET
         CLC   DSNAME,=CL44'NULLFILE ' CHECK FOR DUMMY ALLOCATION.
         BNE   AA100
         LA    R1,DALDUMMY
         STH   R1,S99TUKEY
*                                  S99TUNUM SET TO X'0000' BY WORKAREA
*                                      CLEAR.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUENT         NEXT TEXT UNIT.
         B     AA190
AA100    EQU   *
         LA    R14,DSNAME
         LA    R15,L'DSNAME
         BAL   R1,PP000            GET DSNAME LENGTH.
         LTR   R15,R15
         BNP   AA110
         LA    R1,DALDSNAM
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM         MOVE DSNAME.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1(R15)  NEXT TEXT UNIT.
         SPACE 1
* DSMEMBER ************************
         SPACE 1
AA110    EQU   *
         LA    R7,DSMEMBER-PARMLIST
         LA    R14,DSMEMBER
         LA    R15,L'DSMEMBER
         BAL   R1,PP000            GET DSMEMBER LENGTH.
         LTR   R15,R15
         BNP   AA120
         LA    R1,DALMEMBR
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM         MOVE DSMEMBER.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1(R15)  NEXT TEXT UNIT.
         SPACE 1
* PASSWORD ************************
         SPACE 1
AA120    EQU   *
         LA    R7,PASSWORD-PARMLIST
         LA    R14,PASSWORD
         LA    R15,L'PASSWORD
         BAL   R1,PP000            GET PASSWORD LENGTH
         LTR   R15,R15
         BNP   AA130
         LA    R1,DALPASSW
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM         MOVE PASSWORD.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1(R15)  NEXT TEXT UNIT.
         SPACE 1
* DSSTATUS ************************
         SPACE 1
AA130    EQU   *
         LA    R7,DSSTATUS-PARMLIST
         LA    R14,DSSTATUS
         LA    R15,L'DSSTATUS
         BAL   R1,PP000            GET DSSTATUS LENGTH
         LTR   R15,R15
         BNP   AA140
         LA    R1,DALSTATS
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         CLI   DSSTATUS,C'O'         OLD
         BNE   *+12
         MVI   S99TUPAR,X'01'
         B     AA135
         CLI   DSSTATUS,C'M'         MOD
         BNE   *+12
         MVI   S99TUPAR,X'02'
         B     AA135
         CLI   DSSTATUS,C'N'         NEW
         BNE   *+12
         MVI   S99TUPAR,X'04'
         B     AA135
*        CLC   DSSTATUS,=CL8'SHR '
*        BNE   QQ000
*                                  ASSUME DISP=SHR
         MVI   S99TUPAR,X'08'
AA135    EQU   *
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER
         LA    R9,S99TUPAR+1       NEXT TEXT UNIT.
         SPACE 1
* DSNDISP *************************
         SPACE 1
AA140    EQU   *
         LA    R7,DSNDISP-PARMLIST
         LA    R14,DSNDISP
         LA    R15,L'DSNDISP
         BAL   R1,PP000            GET DSNDISP LENGTH
         LTR   R15,R15
         BNP   AA150
         LA    R1,DALNDISP
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         CLI   DSNDISP,C'U'            UNCATLG
         BNE   *+12
         MVI   S99TUPAR,X'01'
         B     AA145
         CLI   DSNDISP,C'C'            CATLG
         BNE   *+12
         MVI   S99TUPAR,X'02'
         B     AA145
         CLI   DSNDISP,C'D'            DELETE
         BNE   *+12
         MVI   S99TUPAR,X'04'
         B     AA145
         MVI   S99TUPAR,X'08'           ASSUME KEEP
AA145    EQU   *
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1       NEXT TEXT UNIT.
         SPACE 1
* DSADISP *************************
         SPACE 1
AA150    EQU   *
         LA    R7,DSADISP-PARMLIST
         LA    R14,DSADISP
         LA    R15,L'DSADISP
         BAL   R1,PP000            GET DSADISP LENGTH
         LTR   R15,R15
         BNP   AA160
         LA    R1,DALCDISP
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         CLI   DSADISP,C'U'             UNCATLG
         BNE   *+12
         MVI   S99TUPAR,X'01'
         B     AA155
         CLI   DSADISP,C'C'             CATLG
         BNE   *+12
         MVI   S99TUPAR,X'02'
         B     AA155
         CLI   DSADISP,C'D'             DELETE
         BNE   *+12
         MVI   S99TUPAR,X'04'
         B     AA155
         MVI   S99TUPAR,X'08'            ASSUME KEEP
AA155    EQU   *
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1       NEXT TEXT UNIT.
         SPACE 1
* DSUNIT **************************
         SPACE 1
AA160    EQU   *
         LA    R7,DSUNIT-PARMLIST
         LA    R14,DSUNIT
         LA    R15,L'DSUNIT
         BAL   R1,PP000            GET DSUNIT LENGTH
         LTR   R15,R15
         BNP   AA170
         LA    R1,DALUNIT
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM         MOVE DSUNIT.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1(R15)  NEXT TEXT UNIT.
         SPACE 1
* $RESERV1 ************************
         SPACE 1
AA170    EQU   *
* DSVOLSER ************************
         SPACE 1
         LA    R7,DSVOLSER-PARMLIST
         CLC   DSVOLSER,NULLS
         BNE   AA172
         LA    R1,DALRTVOL         SET UP TO HAVE VOLSER RETURNED TO US
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,6
         MVC   S99TUPAR(6),BLANKS
         LA    R1,S99TUPAR
         ST    R1,WKRTNVOL         SAVE ADDRESS OF PARAMETER FIELD.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+6       NEXT TEXT UNIT.
         B     AA180
AA172    EQU   *
         LA    R14,DSVOLSER
         LA    R15,L'DSVOLSER
         BAL   R1,PP000            GET DSVOLSER LENGTH.
         LTR   R15,R15
         BNP   AA180
         LA    R1,DALVLSER
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM         MOVE DSVOLSER.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1(R15)  NEXT TEXT UNIT.
         SPACE 1
* $RESERV2 ************************
         SPACE 1
AA180    EQU   *
         SPACE 1
* DSVOLREF ************************
         SPACE 1
         LA    R7,DSVOLREF-PARMLIST
         LA    R14,DSVOLREF
         LA    R15,L'DSVOLREF
         BAL   R1,PP000            GET DSVOLREF LENGTH.
         LTR   R15,R15
         BNP   AA190
         CLC   DSVOLSER,BLANKS     CHECK TO SEE THAT DSVOLREF DOES NOT
         BE    AA184                   CONFLICT WITH DSVOLSER.
         CLC   DSVOLSER,NULLS
         BNE   QQ000
AA184    EQU   *
         LA    R1,DALVLRDS
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM         MOVE DSVOLREF.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1(R15)  NEXT TEXT UNIT.
         SPACE 1
* DSFREE **************************
         SPACE 1
AA190    EQU   *
         LA    R7,DSFREE-PARMLIST
         LA    R14,DSFREE
         LA    R15,L'DSFREE
         BAL   R1,PP000            GET DSFREE LENGTH.
         LTR   R15,R15
         BNP   AA198
         CLC   DSFREE,=CL8'END   '
         BE    AA198
         CLC   DSFREE,=CL8'CLOSE '
         BNE   QQ000
         LA    R1,DALCLOSE
         STH   R1,S99TUKEY
*                                  S99TUNUM SET TO X'0000' BY WORKAREA
*                                      CLEAR.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUENT         NEXT TEXT UNIT.
AA198    EQU   *
         CLC   DSNAME,=CL44'NULLFILE ' CHECK FOR DUMMY ALLOCATION.
         BE    CC100               GO DO DCB PARAMETERS.
         SPACE 1
* DSLABEL *************************
         SPACE 1
AA200    EQU   *
         LA    R7,DSLABEL-PARMLIST
         LA    R14,DSLABEL
         LA    R15,L'DSLABEL
         BAL   R1,PP000            GET DSLABEL LENGTH.
         LTR   R15,R15
         BNP   AA210
         LA    R1,DALLABEL
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         CLC   DSLABEL,=CL4'SL  '
         BNE   *+12
         MVI   S99TUPAR,X'02'
         B     AA205
         CLC   DSLABEL,=CL4'SUL '
         BNE   QQ000
         MVI   S99TUPAR,X'08'
AA205    EQU   *
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1       NEXT TEXT UNIT.
         SPACE 1
* DSINOUT *************************
         SPACE 1
AA210    EQU   *
         LA    R7,DSINOUT-PARMLIST
         LA    R14,DSINOUT
         LA    R15,L'DSINOUT
         BAL   R1,PP000            GET DSINOUT LENGTH
         LTR   R15,R15
         BNP   AA220
         LA    R1,DALINOUT
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         CLI   DSINOUT,C'I'
         BNE   *+12
         MVI   S99TUPAR,X'80'
         B     AA215
         CLI   DSINOUT,C'O'
         BNE   QQ000
         MVI   S99TUPAR,X'40'
AA215    EQU   *
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1       NEXT TEXT UNIT.
         SPACE 1
* $RESERV3 ************************
         SPACE 1
AA220    EQU   *
         LA    R7,$RESERV3-PARMLIST
         CLC   $RESERV3,BLANKS
         BNE   QQ000
         SPACE 3
         CLC   DSSTATUS,BLANKS     CHECK FOR NEW ALLOCATION.
         BE    BB100
         CLC   DSSTATUS,=CL8'NEW '
         BNE   CC100               GO DO DCB PARAMETERS.
         EJECT
* DSPWDLBL ************************
         SPACE 1
BB100    EQU   *
         LA    R7,DSPWDLBL-PARMLIST
         LA    R14,DSPWDLBL
         LA    R15,L'DSPWDLBL
         BAL   R1,PP000            GET DSPWDLBL LENGTH.
         LTR   R15,R15
         BNP   BB110
         LA    R1,DALPASPR
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         CLC   DSPWDLBL,=CL8'PASSWORD'
         BNE   *+12
         MVI   S99TUPAR,X'10'
         B     BB105
         CLC   DSPWDLBL,=CL8'NOPWREAD'
         BNE   QQ000
         MVI   S99TUPAR,X'30'
BB105    EQU   *
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1       NEXT TEXT UNIT.
         SPACE 1
* DSDATE **************************
         SPACE 1
BB110    EQU   *
         LA    R7,DSDATE-PARMLIST
         LA    R14,DSDATE
         LA    R15,L'DSDATE
         BAL   R1,PP000            GET DSDATE LENGTH.
         LTR   R15,R15
         BNP   BB120
         CLC   DSDATE(6),=C'EXPDT=' CHECK FOR DATE TYPE.
         BNE   BB114
         SH    R15,=H'6'
         CH    R15,=H'5'           CHECK FOR VALID LENGTH.
         BNE   QQ000
         LA    R14,6(0,R14)        BUMP POINTER FOR MVCPARM
         LA    R1,DALEXPDT
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM         MOVE EXPDT DATE.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1(R15)  NEXT TEXT UNIT.
         B     BB120
         SPACE 1
BB114    EQU   *
         CLC   DSDATE(6),=C'RETPD='
         BNE   QQ000               ERROR
         SH    R15,=H'6'
         BNP   QQ000
         CH    R15,=H'4'
         BH    QQ000
         LA    R14,6(0,R14)        BUMP POINTER TO START OF NUMBER.
         BAL   R1,PP100            EDIT AND CONVERT RETPD.
         LA    R1,DALRETPD
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,2
         STH   R15,S99TUPAR        PLACE RETPD VALUE IN TEXT UNIT.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+2       NEXT TEXT UNIT.
         SPACE 1
* DSALLOC *************************
         SPACE 1
BB120    EQU   *
         LA    R7,DSALLOC-PARMLIST
         LA    R14,DSALLOC
         LA    R15,L'DSALLOC
         BAL   R1,PP000            GET DSALLOC LENGTH.
         LTR   R15,R15
         BNP   QQ000               ERROR
         CLC   DSALLOC,=CL5'TRK '  CHECK FOR TRACK ALLOCATION.
         BNE   BB122
         LA    R1,DALTRK
         STH   R1,S99TUKEY
*                                  S99TUNUM SET TO X'0000' BY WORKAREA
*                                      CLEAR.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUENT         NEXT TEXT UNIT.
         B     BB130
         SPACE 1
BB122    EQU   *
         CLC   DSALLOC,=CL5'CYL '  CHECK FOR CYLINDER ALLOCATION.
         BNE   BB124
         LA    R1,DALCYL
         STH   R1,S99TUKEY
*                                  S99TUNUM SET TO X'0000' BY WORKAREA
*                                      CLEAR.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUENT         NEXT TEXT UNIT.
         B     BB130
         SPACE 1
BB124    EQU   *
         BAL   R1,PP100            CHECK FOR AVERAGE BLOCK ALLOCATION.
         LA    R1,DALBLKLN
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,3
         STCM  R15,B'0111',S99TUPAR
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+3       NEXT TEXT UNIT.
         SPACE 1
* DSPRI ***************************
         SPACE 1
BB130    EQU   *
         LA    R7,DSPRI-PARMLIST
         LA    R14,DSPRI
         LA    R15,L'DSPRI
         BAL   R1,PP000            GET DSPRI LENGTH.
         LTR   R15,R15
         BNP   QQ000
         BAL   R1,PP100            CHECK FOR PRIMARY QUANTITY.
         LA    R1,DALPRIME
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,3
         STCM  R15,B'0111',S99TUPAR
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+3       NEXT TEXT UNIT.
         SPACE 1
* DSSEC ***************************
         SPACE 1
BB140    EQU   *
         LA    R7,DSSEC-PARMLIST
         LA    R14,DSSEC
         LA    R15,L'DSSEC
         BAL   R1,PP000            GET DSSEC LENGTH.
         LTR   R15,R15
         BNP   BB150
         BAL   R1,PP100            CHECK FOR SECONDARY QUANTITY.
         LA    R1,DALSECND
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,3
         STCM  R15,B'0111',S99TUPAR
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+3       NEXT TEXT UNIT.
         SPACE 1
* DSDIR ***************************
         SPACE 1
BB150    EQU   *
         LA    R7,DSDIR-PARMLIST
         LA    R14,DSDIR
         LA    R15,L'DSDIR
         BAL   R1,PP000            GET DSDIR LENGTH.
         LTR   R15,R15
         BNP   BB160
         BAL   R1,PP100            CHECK FOR DIRECTORY QUANTITY.
         LA    R1,DALDIR
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,3
         STCM  R15,B'0111',S99TUPAR
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+3       NEXT TEXT UNIT.
         SPACE 1
* DSRLSE **************************
         SPACE 1
BB160    EQU   *
         LA    R7,DSRLSE-PARMLIST
         LA    R14,DSRLSE
         LA    R15,L'DSRLSE
         BAL   R1,PP000            GET DSRLSE LENGTH.
         LTR   R15,R15
         BNP   BB170
         CLC   DSRLSE,=CL8'RLSE '
         BNE   QQ000
         LA    R1,DALRLSE
         STH   R1,S99TUKEY
*                                  S99TUNUM SET TO X'0000' BY WORKAREA
*                                      CLEAR.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUENT         NEXT TEXT UNIT.
         SPACE 1
* DSCONTIG ************************
         SPACE 1
BB170    EQU   *
         LA    R7,DSCONTIG-PARMLIST
         LA    R14,DSCONTIG
         LA    R15,L'DSCONTIG
         BAL   R1,PP000            GET DSCONTIG LENGTH.
         LTR   R15,R15
         BNP   BB180
         LA    R1,DALSPFRM
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         CLC   DSCONTIG,=CL8'CONTIG '
         BNE   QQ000
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1       NEXT TEXT UNIT.
         SPACE 1
* DSROUND *************************
         SPACE 1
BB180    EQU   *
         LA    R7,DSROUND-PARMLIST
         LA    R14,DSROUND
         LA    R15,L'DSROUND
         BAL   R1,PP000            GET DSROUND LENGTH.
         LTR   R15,R15
         BNP   BB190
         CLC   DSROUND,=CL8'ROUND '
         BNE   QQ000
         LA    R1,DALROUND
         STH   R1,S99TUKEY
*                                  S99TUNUM SET TO X'0000' BY WORKAREA
*                                      CLEAR.
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUENT         NEXT TEXT UNIT.
         SPACE 1
* $RESERV4 ************************
         SPACE 1
BB190    EQU   *
         LA    R7,$RESERV4-PARMLIST
         CLC   $RESERV4,BLANKS
         BNE   QQ000
         EJECT
* DSBLKSI *************************
*
CC100    EQU   *
*
         CLC   DSSTATUS(3),=C'NEW' IF ALLOCATION IS NOT NEW
         BNE   MM000
*
         LA    R7,DSBLKSI-PARMLIST
         LA    R14,DSBLKSI
         LA    R15,L'DSBLKSI
         BAL   R1,PP000            GET DSBLKSI LENGTH.
         LTR   R15,R15
         BNP   CC110
         BAL   R1,PP100            CHECK FOR BLKSIZE VALUE.
         CH    R15,=H'32767'       CHECK FOR MAXIMUM BLOCKSIZE.
         BH    QQ000               ERROR.
         LA    R1,DALBLKSZ
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,2
         STH   R15,S99TUPAR
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+2       NEXT TEXT UNIT.
         SPACE 1
* DSORG ***************************
         SPACE 1
CC110    EQU   *
         LA    R7,DSORG-PARMLIST
         CLC   DSORG,NULLS
         BNE   CC112
         LA    R1,DALRTORG         SET UP TO HAVE DSORG RETURNED TO US
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,2
         MVC   S99TUPAR(2),NULLS
         LA    R1,S99TUPAR
         ST    R1,WKRTDSRG         SAVE ADDRESS OF PARAMETER FIELD
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+2       NEXT TEXT UNIT.
         B     CC120
CC112    EQU   *
         LA    R14,DSORG
         LA    R15,L'DSORG
         BAL   R1,PP000            GET DSORG LENGTH
         LTR   R15,R15
         BNP   CC120
         LA    R1,DALDSORG
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,2
         CLC   DSORG,=CL8'VSAM '
         BNE   *+14
         MVC   S99TUPAR(2),=XL2'0008'
         B     CC115
         CLC   DSORG,=CL8'PO   '
         BNE   *+14
         MVC   S99TUPAR(2),=XL2'0200'
         B     CC115
         CLC   DSORG,=CL8'POU  '
         BNE   *+14
         MVC   S99TUPAR(2),=XL2'0300'
         B     CC115
         CLC   DSORG,=CL8'DA   '
         BNE   *+14
         MVC   S99TUPAR(2),=XL2'2000'
         B     CC115
         CLC   DSORG,=CL8'DAU  '
         BNE   *+14
         MVC   S99TUPAR(2),=XL2'2100'
         B     CC115
         CLC   DSORG,=CL8'PS   '
         BNE   *+14
         MVC   S99TUPAR(2),=XL2'4000'
         B     CC115
         CLC   DSORG,=CL8'PSU  '
         BNE   QQ000               ERROR
         MVC   S99TUPAR(2),=XL2'4100'
CC115    EQU   *
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+2       NEXT TEXT UNIT.
         SPACE 1
* DSKEYLEN ************************
         SPACE 1
CC120    EQU   *
         LA    R7,DSKEYLEN-PARMLIST
         LA    R14,DSKEYLEN
         LA    R15,L'DSKEYLEN
         BAL   R1,PP000            GET DSKEYLEN LENGTH.
         LTR   R15,R15
         BNP   CC130
         BAL   R1,PP100            CHECK FOR KEYLENGTH QUANTITY.
         CH    R15,=H'255'         CHECK FOR MAX KEYLENGTH.
         BH    QQ000               ERROR
         LA    R1,DALKYLEN
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         STC   R15,S99TUPAR
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1       NEXT TEXT UNIT.
         SPACE 1
* DSLRECL *************************
         SPACE 1
CC130    EQU   *
         LA    R7,DSLRECL-PARMLIST
         LA    R14,DSLRECL
         LA    R15,L'DSLRECL
         BAL   R1,PP000            GET DSLRECL LENGTH.
         LTR   R15,R15
         BNP   CC140
         LA    R1,DALLRECL
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,2
         CLC   DSLRECL,=CL5'X '    CHECK FOR SPANNED RECORDS.
         BNE   *+14
         MVC   S99TUPAR(2),=XL2'8000'
         B     CC134
         BAL   R1,PP100            CHECK FOR LRECL QUANTITY.
         CH    R15,=H'32767'
         BH    QQ000
         STH   R15,S99TUPAR
CC134    EQU   *
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+2       NEXT TEXT UNIT.
         SPACE 1
* DSRECFM1 THRU DSRECFM8 **********
         SPACE 1
CC140    EQU   *
         LA    R7,DSRECFM1-PARMLIST
         CLC   DSRECFM,BLANKS      CHECK FOR NO INFORMATION
         BE    CC180
         LA    R1,DALRECFM
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         CLI   DSRECFM1,C' '
         BE    CC144
         CLI   DSRECFM1,C'V'
         BNE   *+12
         OI    S99TUPAR,X'40'
         B     CC144
         CLI   DSRECFM1,C'F'
         BNE   *+12
         OI    S99TUPAR,X'80'
         B     CC144
         CLI   DSRECFM1,C'U'
         BNE   QQ000               ERROR
         OI    S99TUPAR,X'C0'
         SPACE 1
CC144    EQU   *
         LA    R7,DSRECFM2-PARMLIST
         CLI   DSRECFM2,C' '
         BE    CC148
         CLI   DSRECFM2,C'B'
         BNE   QQ000               ERROR
         OI    S99TUPAR,X'10'
         SPACE 1
CC148    EQU   *
         LA    R7,DSRECFM3-PARMLIST
         CLI   DSRECFM3,C' '
         BE    CC152
         CLI   DSRECFM3,C'S'
         BNE   QQ000               ERROR
         OI    S99TUPAR,X'08'
         SPACE 1
CC152    EQU   *
         LA    R7,DSRECFM4-PARMLIST
         CLI   DSRECFM4,C' '
         BE    CC156
         CLI   DSRECFM4,C'T'
         BNE   QQ000               ERROR
         OI    S99TUPAR,X'20'
         SPACE 1
CC156    EQU   *
         LA    R7,DSRECFM5-PARMLIST
         CLI   DSRECFM5,C' '
         BE    CC160
         CLI   DSRECFM5,C'M'
         BNE   *+12
         OI    S99TUPAR,X'02'
         B     CC160
         CLI   DSRECFM5,C'A'
         BNE   QQ000               ERROR
         OI    S99TUPAR,X'04'
         SPACE 1
CC160    EQU   *
         LA    R7,DSRECFM6-PARMLIST
         CLI   DSRECFM6,C' '
         BNE   QQ000               ERROR
         SPACE 1
CC164    EQU   *
         LA    R7,DSRECFM7-PARMLIST
         CLI   DSRECFM7,C' '
         BNE   QQ000               ERROR
         SPACE 1
CC168    EQU   *
         LA    R7,DSRECFM8-PARMLIST
         CLI   DSRECFM8,C' '
         BNE   QQ000               ERROR
         SPACE 1
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1       NEXT TEXT UNIT.
         SPACE 1
* DSDCBDS *************************
         SPACE 1
CC180    EQU   *
         LA    R7,DSDCBDS-PARMLIST
         LA    R14,DSDCBDS
         LA    R15,L'DSDCBDS
         BAL   R1,PP000            GET DSDCBDS LENGTH.
         LTR   R15,R15
         BNP   CC190
         LA    R1,DALDCBDS
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM
         ST    R9,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R9,S99TUPAR+1(R15)  NEXT TEXT UNIT.
         SPACE 1
* $RESERV5 ************************
         SPACE 1
CC190    EQU   *
         LA    R7,$RESERV5-PARMLIST
         CLC   $RESERV5,BLANKS
         BNE   QQ000
         B     MM000               GO DO REQUEST
         SPACE 3
MVCPARM  MVC   S99TUPAR(0),0(R14)
         EJECT
MM000    EQU   *
         SH    R8,=H'4'            BACK UP TO LAST USED TEXT POINTER.
         OI    0(R8),X'80'         INDICATE END OF LIST.
         LA    R1,REQBKPTR         GET THE BEGINNING OF THIS MESS.
*        DC    H'0'
         DYNALLOC
         L     R0,S99RSC           PICK UP REASON CODES.
         LTR   R15,R15             CHECK FOR ERRORS.
         BNZ   ZZ000
         L     R1,WKRTNVOL         CHECK FOR VOLSER RETURN REQUEST.
         LTR   R1,R1
         BZ    MM010
         MVC   DSVOLSER,0(R1)      GIVE CALLER INFO.
MM010    EQU   *
         L     R1,WKRTDSRG         CHECK FOR DSORG RETURN REQUEST.
         LTR   R1,R1
         BZ    MM020
         CLC   0(2,R1),=XL2'0008'
         BNE   *+14
         MVC   DSORG,=CL8'VSAM '
         B     MM020
         CLC   0(2,R1),=XL2'0200'
         BNE   *+14
         MVC   DSORG,=CL8'PO   '
         B     MM020
         CLC   0(2,R1),=XL2'0300'
         BNE   *+14
         MVC   DSORG,=CL8'POU  '
         B     MM020
         CLC   0(2,R1),=XL2'2000'
         BNE   *+14
         MVC   DSORG,=CL8'DA   '
         B     MM020
         CLC   0(2,R1),=XL2'2100'
         BNE   *+14
         MVC   DSORG,=CL8'DAU  '
         B     MM020
         CLC   0(2,R1),=XL2'4000'
         BNE   *+14
         MVC   DSORG,=CL8'PS   '
         B     MM020
         CLC   0(2,R1),=XL2'4100'
         BNE   *+14
         MVC   DSORG,=CL8'PSU  '
         B     MM020
         CLC   0(2,R1),=XL2'8000'
         BNE   *+14
         MVC   DSORG,=CL8'IS   '
         B     MM020
         CLC   0(2,R1),=XL2'8100'
         BNE   MM020
         MVC   DSORG,=CL8'ISU  '
MM020    EQU   *
         B     ZZ000
         EJECT
PP000    EQU   *                   ROUTINE TO DETERMINE LENGTH OF
         LTR   R15,R15                 NON-BLANK CHARACTERS IN FIELD.
         BNP   PP030                   AT ENTRY R1 CONTAINS THE RETURN
         ALR   R15,R14                 ADDRESS.  R14 CONTAINS FIELD
PP010    EQU   *                       ADDRESS.  R15 CONTAINS LENGTH
         BCTR  R15,0                   OF FIELD.  AT EXIT R15 WILL BE
         CLI   0(R15),C' '             ALTERED TO CONTAIN LENGTH OF
         BNE   PP020                   NON-BLANK CHARACTERS IN FIELD.
         CLR   R15,R14
         BH    PP010
         SR    R15,R15
         B     PP030
PP020    EQU   *
         SLR   R15,R14
         LA    R15,1(0,R15)
PP030    EQU   *
         BR    R1
         SPACE 3
PP100    EQU   *                   ROUTINE TO EDIT EBCDIC NUMERIC
         LTR   R15,R15                 DATA AND, IF VALID, TO CONVERT
         BNP   PP120                   IT TO BINARY.
         ST    R15,DBLWORD             AT ENTRY R1 CONTAINS THE RETURN
         ALR   R15,R14                 ADDRESS.  R14 CONTAINS DATA
PP110    EQU   *                       ADDRESS.  R15 CONTAINS LENGTH
         BCTR  R15,0                   OF DATA.  AT NORMAL EXIT, R15
         CLI   0(R15),C'0'             WILL BE ALTERED TO CONTAIN THE
         BL    QQ000                   BINARY EQUIVALENT OF THE NUMBER.
         CLI   0(R15),C'9'
         BH    QQ000                   AN ABNORMAL EXIT WILL BE TAKEN
         CLR   R15,R14                 TO THE COMMON ERROR ROUTINE IF
         BH    PP110                   DATA IS NOT NUMERIC.
         L     R15,DBLWORD
         BCTR  R15,0
         EX    R15,PACKNUM
         CVB   R15,DBLWORD
PP120    EQU   *
         BR    R1
         SPACE 1
PACKNUM  PACK  DBLWORD,0(0,R14)
         EJECT
QQ000    EQU   *
         LR    R0,R7               SET UP RETURN CODES
         LA    R15,15
         B     ZZ000
         SPACE 3
ZZ000    EQU   *
         LR    R1,R13              PREPARE FOR FREEMAIN.
         L     R13,SAVEAREA+4
         LTR   R11,R11             CHECK TO SEE IF PARAMETER LIST IS
         BNP   ZZ010                   ONLY ENTRY IN CALLER'S ADDRESS
         L     R2,24(0,R13)            LIST.
         L     R2,4(0,R2)          GET SECOND ADDRESS IN LIST.
         ST    R0,0(0,R2)          PUT REGISTER INFORMATION IN LIST.
         ST    R15,4(0,R2)
ZZ010    EQU   *
         LR    R2,R0               SAVE CONTENTS OF REGISTERS.
         LR    R3,R15
FREE1    DS    0H
         LR    R0,R2
         LR    R15,R3
         L     R14,12(0,R13)
         RETURN (1,12),RC=(15)
WORKLEN  DC    A(DYNWRKSZ)
TXPTRSPC DC    A(4*40)             RESERVE SPACE FOR 40 TEXT POINTERS.
BLANKS   DC    CL44' '
NULLS    DC    XL44'00'
         LTORG
         CNOP  0,8
         EJECT
         PRINT NOGEN
         PARMLIST
         IHAPSA
         IHAASCB
         IKJTCB
         IEFUCBOB
         IEFZB4D0
         IEFZB4D2
         CVT   DSECT=YES
         IECDIOCM
         IECDATB
ATBLEN   EQU   *-ATB
         TCBUVTAM PFX=TCBU
TIOT     DSECT
         IEFTIOT1
         IKJTSB
         IHAPDS PDSBLDL=NO
*
*
*
*
ACBSIZ   EQU   56
MODSIZ   EQU   36
RPLSIZ   EQU   72
DYNDECBL EQU   20
GMVUL    EQU   10
*
         PRINT GEN
         SPLTAREA
         MYSAVE
         DIRMASK
         SPFMT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12                  1ST BASE REG
R13      EQU   13                  WORK AREA
R14      EQU   14
R15      EQU   15
*
         CNOP  0,8
ETPSATEN CSECT
*                       R1  ON ENTRY IS THE IOSB ADDRESS
*                       R13 ON ENTRY IS THE SAVE AREA ADDRESS
*                       R14 ON ENTRY IS THE RETURN ADDRESS
*                              R14 + 0 = IOS NOP
*                              R14 + 4 = IOS GO POST ANOTHER ASID'S ECB
*                       R15 ON ENTRY IS THE ENTRY ADDRESS
ATXTBGN  EQU   *
         USING *,R8
         STM   R0,R15,0(R13)       SAVE REGS
         LR    R8,R15              SET BASE
         USING IOSB,1
         L     R3,IOSSRB           LOAD ADDRESS OF SRB FROM IOSB
         USING SRBSECT,3           LOAD ADDRESS OF SRB FROM IOSB
         L     R5,SRBASCB          LOAD ADDRESS OF ASCB FROM SRB
         USING ASCB,5
         L     R7,IOSUCB           LOAD ADDRESS OF UCB BEGIN FROM IOSB
         USING UCBCMSEG,7
         L     R2,UCBEXTPT         LOAD ADDRESS OF UCB EXTENSION
         USING UCBCMEXT,2
         OC    UCBASID,UCBASID     SEE IF ASID IS ZERO           W.A.M.
         BZ    BADEXIT             IF ZERO, EXIT WITH RETURN CODE ZERO
         CLC   ASCBASID,UCBASID    COMPARE IF IT'S OUR ADDRESS SPACE ID
         BE    TFORECB             IF OURS, GO POST
         MVC   IOSASID,UCBASID     PUT THE ASID TO BE SCHEDULED IN IOSB
         LM    R0,R14,0(R13)               RESTORE REGS
         LA    R15,4               SET RETURN CODE TO 4
         B     4(,R14)             RETURN TO CALLER, REQ. IOS POST IOSB
TFORECB  DS    0H
         TM    UCBRDYQ,X'80'       DID ETPS ISSUE A WAIT?
         BZ    BADEXIT             IF NOT, AN UNSOLICITED INTERRUPT
         LR    R9,R13                      THIS REGISTER IS PRESERVED
         LA    R11,UCBRDYQ                 ECB ADDRESS
         LR    R13,R5                      SET ASCB
*
*  NOTE BRANCH ENTRY POST WILL DESTORY REGISTERS 10 THRU 15
*
         PRINT GEN
         POST  (11),ASCB=(13),ERRET=AFTPOST,BRANCH=YES,ECBKEY=0
         DROP  ,                   DROP ALL REGISTERS
AFTPOST  DS    0H
         LR    R13,R9              RE-LOAD SAVE AREA REGISTER
BADEXIT  DS    0H
         LM    R0,R14,0(R13)               RESTORE REGS
         SLR   R15,R15                     SET RETURN CODE TO ZERO
         BR    R14                 RETURN TO CALLER, NO WORK FOR IOS
         CNOP  0,8
ATXTLEN  EQU   *-ATXTBGN
         IECDIOSB
         IHASRB
         TITLE 'ETPS - LOGON HELP SCREEN'
LOGHELP  CSECT
         DC    A(LOGHELP1)
         DC    A(LOGBLANK)
         DC    A(LOGHELP2)
         DC    A(LOGHELP3)
         DC    A(LOGHELP4)
         DC    A(LOGHELP5)
         DC    A(LOGBLANK)
         DC    A(LOGHELP6+X'80000000')
LOGHELP1 DC    AL1(80)                  ROW 01, COL 01
         DC    X'05'                  PROTECT, HIGH-LIGHT
         DC    24C'-'
         DC    CL21' LOGON PANEL         '
         DC    34C'-'
*        123456789012345678901234567890123456789012345678901234567890
LOGHELP2 DC    AL1(61),X'05'
 DC CL62'THE ETPS LOGON PANEL IS PROVIDED TO ALLOW A USERID TO BE    '
LOGHELP3 DC    AL1(61),X'05'
 DC CL62'    ENTERED AND VERIFIED TO LIMIT THE POSSIBILITY OF        '
LOGHELP4 DC    AL1(61),X'05'
 DC CL62'    UNAUTHORIZED USE. THE PASSWORD FIELD IS NOT VERIFIED BY '
LOGHELP5 DC    AL1(61),X'05'
 DC CL62'    ETPS, AS SHIPPED.                                       '
LOGHELP6 DC    AL1(61),X'05'
 DC CL62'PF3, OR ENTERING "EOJ" IN THE USERID FIELD LOGS YOU OFF.    '
LOGBLANK DC    AL1(10),X'05',CL12' '
         CNOP  0,8
         EJECT
         TITLE 'ETPS - PRIMARY OPTION HELP SCREEN'
POPHELP  CSECT
         DC    A(POPHELP1)
         DC    A(POPBLANK)
         DC    A(POPHELP2)
         DC    A(POPHELP3)
         DC    A(POPHELP4)
         DC    A(POPBLANK)
         DC    A(POPHELP5)
         DC    A(POPHELP6+X'80000000')
POPHELP1 DC    AL1(80)                  ROW 01, COL 01
         DC    X'05'                  PROTECT, HIGH-LIGHT
         DC    24C'-'
         DC    CL22' PRIMARY OPTION PANEL'
         DC    34C'-'
*        123456789012345678901234567890123456789012345678901234567890
POPHELP2 DC    AL1(61),X'05'
 DC CL62'THE PRIMARY OPTION PANEL SHOWS WHAT ETPS FUNCTIONS ARE      '
POPHELP3 DC    AL1(61),X'05'
 DC CL62'    AVAILABLE. THE OPTIONS ARE INVOKED BY ENTERING THE      '
POPHELP4 DC    AL1(61),X'05'
 DC CL62'    OPTION NUMBER IN THE COMMAND LINE.                      '
POPHELP5 DC    AL1(61),X'05'
 DC CL62'PF3, OR ENTERING "X" IN THE COMMAND LINE RETURNS TO THE     '
POPHELP6 DC    AL1(61),X'05'
 DC CL62'    LOGON PANEL.                                            '
POPBLANK DC    AL1(10),X'05',CL12' '
         CNOP  0,8
         EJECT
         TITLE 'ETPS - PDS BROWSE HELP SCREEN'
BROHELP  CSECT
         DC    A(BROHELP1)
         DC    A(BROBLANK)
         DC    A(BROHELP2)
         DC    A(BROHELP3)
         DC    A(BROHELP4)
         DC    A(BROBLANK)
         DC    A(BROHELP5+X'80000000')
BROHELP1 DC    AL1(80)                  ROW 01, COL 01
         DC    X'05'                  PROTECT, HIGH-LIGHT
         DC    24C'-'
         DC    CL22' PDS BROWSE  PANEL'
         DC    34C'-'
*        123456789012345678901234567890123456789012345678901234567890
BROHELP2 DC    AL1(61),X'05'
 DC CL62'THE PDS BROWSE PANEL PROVIDES FOR ENTERING THE PDS DSNAME,  '
BROHELP3 DC    AL1(61),X'05'
 DC CL62'    WHICH MUST BE A FULLY-QUALIFIED DSNAME, NOT IN QUOTES.  '
BROHELP4 DC    AL1(61),X'05'
 DC CL62'    A SPECIFIC VOLSER MAY ALSO BE ENTERED, AND A PASSWORD.  '
BROHELP5 DC    AL1(61),X'05'
 DC CL62'PF3 RETURNS TO THE PRIMARY OPTION PANEL.                    '
BROBLANK DC    AL1(10),X'05',CL12' '
         CNOP  0,8
         EJECT
         TITLE 'ETPS - PDS EDIT   HELP SCREEN'
EDTHELP  CSECT
         DC    A(EDTHELP1)
         DC    A(EDTBLANK)
         DC    A(EDTHELP2)
         DC    A(EDTHELP3)
         DC    A(EDTHELP4)
         DC    A(EDTBLANK)
         DC    A(EDTHELP5+X'80000000')
EDTHELP1 DC    AL1(80)                  ROW 01, COL 01
         DC    X'05'                  PROTECT, HIGH-LIGHT
         DC    24C'-'
         DC    CL22' PDS EDIT    PANEL'
         DC    34C'-'
*        123456789012345678901234567890123456789012345678901234567890
EDTHELP2 DC    AL1(61),X'05'
 DC CL62'THE PDS EDIT   PANEL PROVIDES FOR ENTERING THE PDS DSNAME,  '
EDTHELP3 DC    AL1(61),X'05'
 DC CL62'    WHICH MUST BE A FULLY-QUALIFIED DSNAME, NOT IN QUOTES.  '
EDTHELP4 DC    AL1(61),X'05'
 DC CL62'    A SPECIFIC VOLSER MAY ALSO BE ENTERED, AND A PASSWORD.  '
EDTHELP5 DC    AL1(61),X'05'
 DC CL62'PF3 RETURNS TO THE PRIMARY OPTION PANEL.                    '
EDTBLANK DC    AL1(10),X'05',CL12' '
         CNOP  0,8
         EJECT
         TITLE 'ETPS - PDS UTILITY HELP SCREEN'
UT1HELP  CSECT
         DC    A(UT1HELP1)
         DC    A(UT1BLANK)
         DC    A(UT1HELP2)
         DC    A(UT1HELP3)
         DC    A(UT1HELP4)
         DC    A(UT1BLANK)
         DC    A(UT1HELP5)
         DC    A(UT1BLANK)
         DC    A(UT1HELP6+X'80000000')
UT1HELP1 DC    AL1(80)                  ROW 01, COL 01
         DC    X'05'                  PROTECT, HIGH-LIGHT
         DC    24C'-'
         DC    CL22' PDS UTILITY PANEL'
         DC    34C'-'
*        123456789012345678901234567890123456789012345678901234567890
UT1HELP2 DC    AL1(61),X'05'
 DC CL62'THE PDS UTILITY PANEL PROVIDES FOR ENTERING THE PDS DSNAME, '
UT1HELP3 DC    AL1(61),X'05'
 DC CL62'    WHICH MUST BE A FULLY-QUALIFIED DSNAME, NOT IN QUOTES.  '
UT1HELP4 DC    AL1(61),X'05'
 DC CL62'    A SPECIFIC VOLSER MAY ALSO BE ENTERED, AND A PASSWORD.  '
UT1HELP5 DC    AL1(61),X'05'
 DC CL62'THE ONLY OPTION IS "C" FOR COMPRESS.                        '
UT1HELP6 DC    AL1(61),X'05'
 DC CL62'PF3 RETURNS TO THE PRIMARY OPTION PANEL.                    '
UT1BLANK DC    AL1(10),X'05',CL12' '
         CNOP  0,8
         EJECT
         END
/*
//LKED1    EXEC PGM=HEWL,PARM='XREF,LET,LIST,RENT,AC=1',
//             COND=(2,LT,ASM),REGION=1024K
//SYSLIN   DD   DSN=&&OBJSET,DISP=(OLD,PASS)
//         DD   DDNAME=SYSIN
//SYSLIB   DD   DSN=SYS1.LINKLIB,DISP=SHR
//SYSLMOD  DD   DISP=SHR,DSN=SYS1.LINKLIB,UNIT=SYSDA,VOL=SER=MVSTGT
//SYSUT1   DD   DSN=&&SYSUT1,UNIT=SYSDA,SPACE=(1024,(50,20))
//SYSPRINT DD   SYSOUT=*
//SYSIN    DD   *
     NAME ETPS(R)
/*
//LKED2    EXEC PGM=HEWL,PARM='XREF,LET,LIST,RENT,AC=1',
//             COND=(2,LT,ASM),REGION=1024K
//SYSLIN   DD   DSN=&&OBJSET,DISP=(OLD,PASS)
//         DD   DDNAME=SYSIN
//SYSLIB   DD   DSN=SYS1.LINKLIB,DISP=SHR
//SYSLMOD  DD   DISP=SHR,DSN=SYS1.LINKLIB,UNIT=SYSDA,VOL=SER=MVSYSA
//SYSUT1   DD   DSN=&&SYSUT1,UNIT=SYSDA,SPACE=(1024,(50,20))
//SYSPRINT DD   SYSOUT=*
//SYSIN    DD   *
     NAME ETPS(R)
/*
//*LA  EXEC PGM=MVSCMD,PARM='F LLA,REFRESH'
