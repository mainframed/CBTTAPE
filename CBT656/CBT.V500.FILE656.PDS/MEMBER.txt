/* Rexx  find which data sets in a dslist contain a member           */
/*       Syntax: Tso findmem pattern                                 */
/*       where pattern is a name or pattern with % And * Characters  */
/*       requires ISPF 4.5+              Doug nadel 6/2001           */
/*       ** Uses some undocumented or unsupported methods ***        */
/*       ** Renamed to Member to reflect z/os command                */
Parse Upper Arg pattern .
If pattern="" Then                     /* Insure parm was passed     */
  Do
    Say "No member pattern specified"
    Exit
  End
dta = ptr(76+ptr(ptr(24+ptr(112+ptr(132+ptr(540))))))
tname=storage(d2x(dta+196),8)          /* Get dslist table name      */
If substr(tname,1,3)="DSL" & "NUM"=datatype(delstr(tname,1,3)) Then
  Do
    say tname
    Address ispexec
    "CONTROL ERRORS RETURN"            /* Trap errors                */
    "TBTOP  "tname                     /* Move to top of table       */
    "TBSKIP "tname                     /* Go to 1st row              */
    Do While rc=0                      /* Loop through rows          */
      zudxstat="Y"                     /* Exclude the line           */
      If zudvol \= "MIGRAT" & substr(zudvol,1,1)\= "*" Then
        Do                             /* If online real dataset     */
          "LMINIT DATAID(MS) DATASET('&ZUDSNS') ENQ(SHR) ORG(DSO)"
          If rc=0 Then                 /* Allocated?                 */
            Do
              If dso="PO" Then         /* Known pds or pdse          */
                Do
                  check=""             /* Init for LMMLIST           */
                  "LMOPEN DATAID(&MS) OPTION(INPUT)" /* Open the ds  */
                  If rc=0 Then         /* Open OK?                   */
                    If pos("*",pattern)>0 | pos("%",pattern)>0 Then
                      "LMMLIST DATAID(&MS) MEMBER(CHECK) ",
                        "PATTERN(&PATTERN) OPTION(LIST)"
                    Else
                      "LMMFIND DATAID(&MS) MEMBER(&PATTERN) "
                  If rc=0 Then         /* If found                   */
                    Do
                      zudxstat="N"     /* Unexclude list             */
                      zulmsg="07"x||"FOUND: "pattern /* Set message  */
                    End
                End
              "LMFREE DATAID(&MS)"     /* Free the allocation        */
            End
        End
      "TBPUT  "tname                   /* Update the table           */
      "TBSKIP "tname                   /* Move to next row           */
    End
    "CONTROL NONDISPL ENTER"           /* Force refresh              */
  End
Else
  Say " The data set list table could not be found " tname
Return

ptr:Return c2d(storage(d2x(Arg(1)),4))

