         TITLE 'IPO-SUPPLIED PRINT COMMAND (WITH MANY MODIFICATIONS)   C
               WSRCC VERSION 14'
         PRINT ON,GEN
***********************************************************************
*                                                                     *
*             MODULE NAME = PRINTOFF                                  *
*                                                                     *
*             DESCRIPTIVE NAME =  IPO SUPPLIED PRINT COMMAND FOR      *
*                TSO FOREGROUND EXECUTION - HEAVILY MODIFIED!         *
*                                                                     *
*             COPYRIGHT = NONE                                        *
*                                                                     *
*             STATUS = WSRCC VERSION 14.                              *
*                                                                     *
*             FUNCTION =                                              *
*                PRINTS A DATASET(S) WHICH IS SEQUENTIAL OR PARTIONED *
*                ALLOWING THE USER TO SPECIFY OUTPUT CLASS, COPIES,   *
*                DESTINATION AND HOLD/NOHOLD.  ALSO ALLOWS THE USER   *
*                TO SPECIFY LIST/NOLIST, PRINT/NOPRINT, VOLUME,       *
*                OR FOLD/NOFOLD.                                      *
*                LIST IF PDS MEMBER NAMES ARE ONLY TO BE LISTED.      *
*                PRINT IF PDS MEMBERS ARE ONLY TO BE PRINTED.         *
*                                                                     *
*                OPERATION =                                          *
*                   BUILD PARS, DAIR, AND PUTLINE PARAMETER LISTS.    *
*                   CALL PARS TO PARSE COMMAND BUFFER.                *
*                   ALLOCATE DATASET AND DETERMINE IF PDS OR          *
*                      SEQUENTIAL.                                    *
*                   DETERMINE WHAT FUNCTIONS WERE TO BE PERFORMED     *
*                      FOR THIS DATASET IF PDS.                       *
*                   ALLOCATE SYSOUT DATASETS WHEN SOMETHING IS        *
*                      FOUND TO BE PRINTED.                           *
*                   PROCESS NEXT DATASET IN LIST.                     *
*                   AT END OF LIST CLOSE DATASETS AND FREE ANY        *
*                      SYSOUT DATASETS.                               *
*                   DATSETS WILL BE PRINTED AS EITHER VBA OR VBM.     *
*                                                                     *
*              NOTES =                                                *
*                                                                     *
*                 DEPENDENCIES = CHARACTER SET IS EBCDIC.  REASSEMBLE *
*                    IF A DIFFERENT CHARACTER SET IS NEEDED.          *
*                                                                     *
*                 RESTRICTIONS = DATASETS MUST HAVE LRECL LESS THAN   *
*                    255 AND MUST BE EITHER FIXED OR VARIABLE FORMAT. *
*                                                                     *
*                 REGISTER CONVENTIONS = STANDARD CONVENTIONS.        *
*                    REGISTERS 0 TO 9  = WORK REGISTERS               *
*                    REGISTERS 10 - 12 = ADDRESSABILITY TO PRINTOFF   *
*                    REGISTER  13      = SAVE AREA REGISTER AND       *
*                                        ADDRESSABILITY TO GETMAINED  *
*                                        WORK AREA                    *
*                    REGISTERS 14,15   = WORK REGISTERS               *
*                                                                     *
*                PATCH LABEL = PATCH (UNUSED AND INTIALIZED TO        *
*                   'ZAP*'S)                                          *
*                                                                     *
*                TSO COMMAND SYNTAX = PRINTOFF ('DSLIST')             *
*                            CLASS('CLASS') DEST('REMOTE')            *
*                            COPIES('NNN') HOLD/NOHOLD LIST/NOLIST    *
*                            PRINT/NOPRINT FORMS('FORM') UCS('IMAGE') *
*                            PROG('NAME') FCB('FCBNAME')              *
*                            HEADDING/NOHEADDING                      *
*                            CCTL/CCTL('CODE')/NOCCTL CAPS/ASIS       *
*                            VOLUME('VOLSER') TEXT KEEP/DELETE PDS    *
*                            NOLINES/LINES('LINES')                   *
*                            EJECT/NOEJECT('LINES') ARR(###)          *
*                            START('NNNNNN') STOP('NNNNNN')           *
*                            COUNT('NNNN') TITLE(' TITLE ') DUPLEX    *
*                            NUMBER EXPRESS DOUBLE TRIPLE             *
*                            CHARS=(SET1,SET2,SET3,SET4) TRC(Y/N)     *
*                            MODIFY=(GRAPHMOD-ID) MODTRC=(TRC-ID)     *
*                            FLASH=(FLASH-NAME) FCOUNT=(FLASH-COUNT)  *
*                            BURST(Y/N)                               *
*                                                                     *
*             MODULE TYPE = PROCEDURE                                 *
*                                                                     *
*                PROCESSOR = ASM                                      *
*                                                                     *
*                MODULE SIZE = ???K BYTES                             *
*                                                                     *
*                ATTRIBUTES = SCHEDULER KEY 8, REENTRANT,             *
*                   PROBLEM PROGRAM STATE                             *
*                                                                     *
*             ENTRY POINTS = PRINTOFF (ONLY ENTRY POINT)              *
*                                                                     *
*                LINKAGE =                                            *
*                   FROM TERMINAL MONITOR PROGRAM AS A COMMAND        *
*                                                                     *
*             INPUT = REGISTER 1 POINTS TO COMMAND PROCESSOR          *
*                PARAMETER LIST MAPPED BY IKJCPPL MACRO               *
*                                                                     *
*             OUTPUT = NONE                                           *
*                                                                     *
*             EXIT - NORMAL = AT PROGRAM END VIA BRANCH REGISTER 14   *
*                                                                     *
*                OUTPUT = NONE                                        *
*                                                                     *
*                RETURN CODE = ZERO                                   *
*                                                                     *
*             EXIT - ERROR = NONE - PROGRAM WILL DISPLAY DECIMAL      *
*                RETURN CODE AND RETURN VIA BRANCH REGISTER 14        *
*                                                                     *
*                OUTPUT = NONE                                        *
*                                                                     *
*                RETURN CODE = ZERO                                   *
*                                                                     *
*             EXTERNAL REFERENCES =                                   *
*                                                                     *
*                ROUTINES = IKJPARS, DAIRFAIL, GNRLFAIL, PUTLINE      *
*                                                                     *
*                DATA AREAS = NONE                                    *
*                                                                     *
*                CONTROL BLOCKS = CPPL, PPL, DAPL, DCB, DAPB, CVT,    *
*                   S99RBP, S99RB, S99TUPL, S99TUNIT, S99TUFLD,       *
*                   GFPARMS, DFPARMS, IOPL, IOPB                      *
*                                                                     *
*             TABLES = DATA AREA TO BE GETMAINED.  MAPPED BY DSECT    *
*                BEGINNING AT LABEL RDATD.                            *
*                                                                     *
*             MACROS = SAVE, GETMAIN, CALLTSSR, LINK, TPUT, FREEMAIN, *
*                   OPEN, PUT, GET, CLOSE, RDJFCB, IKJRLSA, DYNALLOC, *
*                   DCB, IKJPARM, IKJPOSIT, IKJKEYWD, IKJNAME,        *
*                   IKJSUBF, IKJIDENT, IKJENDP, IKJDAP08, IKJDAP18,   *
*                   IKJDAPL, IKJCPPL, IKJPPL, CVT, IEFZB4D0, IKJIOPL, *
*                   IEFZB4D2, DCBD, IKJEFFGF, IKJEFFDF, PUTLINE       *
*             WSRCC MACROS = BEGIN, FINISH                            *
*                                                                     *
*  ********** CHANGE ACTIVITY:                        **************  *
*         1.  JOE MARTIN - 04/19/79 - ID.JM#1                         *
*             ADDITION OF DATE AND TIME TO HEADING LINES.             *
*                                                                     *
*         2.  TOM FOTH   - 02/21/81 - ID.TF#1                         *
*             ADDED KEYWORDS NOCCTL AND CCTL TO OVERRIDE CARRIAGE     *
*             CONTROL OF INPUT DATASET                                *
*                                                                     *
*         3.  TOM FOTH   - 02/21/81 - ID.TF#2/JM#1                    *
*             CORRECTED MOD 1.                                        *
*                                                                     *
*         4.  TOM JARVIS - 07/07/82 - ID.TJ#1                         *
*             ADDITION OF:                                            *
*               A) DELETE/KEEP KEYWORD - KEEP OR DELETE DATASET AFTER *
*                                        PRINTING.                    *
*               B) PDS KEYWORD   - WILL ONLY PRINT PDS IF THE         *
*                                        PDS KEYWORD IS PRESENT.      *
*               C) EJECT/NOEJECT KEYWORD                              *
*                   EJECT   - EJECTS AT BEGINNING OF EVERY DATASET.   *
*                             FIRST EJECT IS SKIPPED IF ARR SPECIFIED.*
*                   NOEJECT(#) - NO EJECT ON FIRST DATASET AND '#'    *
*                                BLANK LINES BETWEEN DATASETS.        *
*                                (NOEJECT(4) IS THE DEFAULT).         *
*               D) ADDED CODE TO CHANGE CARRAIGE CONTROL CODE TO      *
*                  TRY AND PREVENT BLANK PAGES FROM THE 6670.         *
*                                                                     *
*         5.  TOM JARVIS - 07/15/82 - ID.TJ#2                         *
*             ADDITION OF:                                            *
*                A) ARR(###) PARAMETERS - USED TO SPECIFY             *
*                            A 6670 ARRANGEMENT TO BE USE BY DATASET. *
*                B) NOLINES/LINES(##) - USED TO SPECIFY THE NUMBER    *
*                            OF LINES ON A PAGE OF OUTPUT, NOLINES    *
*                            SPECIFIES THAT THERE SHOULD NOT BE A PAGE*
*                            BREAK, AND LINES(##) SPECIFIES HOW MANY  *
*                            LINES TO PRINT ON A PAGE. NOLINES IS THE *
*                            DEFAULT . LINES MUST BE GREATER THAN 3   *
*                            AND ARE IGNORED IF OUTPUT USES CC.       *
*                                                                     *
*         6.  TOM JARVIS - 07/19/82 - ID.TJ#3                         *
*             ADDITION OF:                                            *
*                A) CORRECTION TO UPDATES ABOVE.                      *
*                B) TITLE(' TITLE ') PARM  - USED TO SPECIFY A TITLE  *
*                     TO REPLACE HEADING, IGNORED IF NOHEAD IS CODED. *
*                                                                     *
*         7.  TOM JARVIS - 08/20/82 - ID.TJ#4.                        *
*             ADDITION OF:                                            *
*                A) COUNT(##) PARM - PRINT ONLY ## RECORDS            *
*                B) START(##) PARM - START PRINT AFTER ## RECORDS     *
*                C) STOP(##) PARM  - STOP PRINTING ON RECORD ##       *
*                D) DUPLEX PARM    - ONLY IF ARR SPECIFIED, ADD       *
*                                    'DUP 1' TO ARRANGEMENT           *
*                E) IF ARR IS SPECIFIED THE COPIES IS PUT INTO THE    *
*                   ARRANGEMENT.                                      *
*                                                                     *
*         8.  TOM JARVIS - 08/31/82 - ID.TJ#5                         *
*             ADDITION OF:                                            *
*                A) NUMBER - USED ONLY IF ARR IS SPECIFIED,           *
*                            ARRANGEMENT LINE ADDED TO SPECIFY PAGE   *
*                            NUMBERING.                               *
*                B) EXPRESS - USED ONLY IF ARR IS SPECIFIED,          *
*                            ARRANGEMENT LINE ADDED TO SPECIFY SENDING*
*                            OUTPUT TO 6670 IN EXPRESS MODE.          *
*                                                                     *
*         9.  TOM JARVIS - 09/14/82 - ID.TJ#6                         *
*             ADDITION OF:                                            *
*                A) CORRECTION OF PROBLEM WITH TJ#3                   *
*                B) DOUBLE - INDICATES THAT THE OUTPUT SHOULD BE      *
*                            DOUBLE SPACED (CC IS IGNORED).           *
*                C) TRIPLE - INDICATES THAT THE OUTPUT SHOULD BE      *
*                            TRIPLE SPACED (CC IS IGNORED).           *
*                                                                     *
*         10. TOM JARVIS - 09/28/82 - ID.TJ#7                         *
*             ADDITION OF:                                            *
*               A) CORRECTION OF PROBLEM WITH BLANK LINES AND         *
*                  VARIABLE LENGTH FILES WHEN PRINTING WITH FORCED CC.*
*                                                                     *
*         11. TOM JARVIS - 10/25/82 - ID.TJ#8                         *
*             ADDITION OF:                                            *
*               A) CORRECTION OF PROBLEM WITH USING 6670 ARRANGEMENT  *
*                  AND PRINTING MEMBERS OF A PDS.                     *
*               B) CORRECTION OF PROBLEM WITH USING 6670 ARRANGEMENT  *
*                  AND EJECT.                                         *
*               C) ADDED PUTLINE MESSAGE WITH PRINTED LINE TOTAL.     *
*                                                                     *
*         12. TOM JARVIS - 08/24/83 - ID.TJ#9                         *
*             I COMMENTED OUT ALL CODE DEALING WITH ARRANGEMENTS AND  *
*             QUANTITY (COPIES) DUE TO PROBLEMS WITH BILLING, WE      *
*             CANNOT CAPTURE QUANTITIES IN SMF RECORDS.               *
*             LOCAL GEN OPTION ONLY                              JM#14*
*                                                                     *
*                                                                     *
*         13. TOM JARVIS - 11/22/83 - ID.TJ#10                        *
*             INCREMENTED START AND STOP SIZES.                       *
*                                                                     *
*                                                                     *
*         14. JOE MARTIN - 09/25/84 - ID.JM#14                        *
*             ADDING SUPPORT FOR 3800 PRINTERS:                       *
*                CHARS=(SET1,SET2,SET3,SET4)       USING SETPRT       *
*                MODIFY=(GRAPHMOD-ID)              USING SETPRT       *
*                MODTRC=(TRC-ID)                   USING SETPRT       *
*                FLASH=(FLASH-NAME)                USING SETPRT       *
*                FCOUNT=(FLASH-COUNT)              USING SETPRT       *
*                BURST(Y|N)                        USING SETPRT       *
*                TRC(Y|N) - IMPLIES DCB=OPTCD=J    USING SETPRT       *
*                CGROUP=(GROUP1,GROUP2,GROUP3,...) USING ALLOCATION   *
*             ADDED LOCAL CONDITIONAL ASSEMBLEY FOR SOME FUNCTIONS    *
*             AS THOSE FUNCTIONS ARE EITHER CURRENTLY UNUSED OR       *
*             RESTRICTED FOR GENERAL USAGE. (SEE #12 ABOVE).          *
*             ADDED "BEGIN" AND "FINISH" MACROS.                      *
*                                                                     *
*                                                                     *
*                                                                     *
*  **********  END CHANGE ACTIVITY LOG                **************  *
*                                                                     *
*             MESSAGES =                                              *
*                                                                     *
*                BOTH THE DARIFAIL AND GNRLFAIL SERVICE ROUTINES ARE  *
*                USED TO ISSUE MESSAGES.                              *
*                THOSE ISSUED BY THE COMMAND ARE FOUND FOLLOWING THE  *
*                LABEL GNRLERR AND ARE ISSUED USING PUTLINE.          *
*                                                                     *
*             ABEND CODES = NONE                                      *
*                                                                     *
***********************************************************************
         EJECT 1                                                 JM#14
         LCLA  &LGENOPT                FLAG FOR LOCAL GEN OPTNS. JM#14
&LGENOPT SETA  1                                                 JM#14
*LGENOPT SETA  1                       1 - INDICATES FULL GEN.   JM#14
*LGENOPT SETA  0                       0 - INDICATES PARTIAL     JM#14
         SPACE 1                                                 JM#14
PRINTOFF BEGIN SAVE=(WORKAREA,LDYNAMIC,0),BASE=(R12,R11,R10),    JM#14 X
               AUTHOR=IPO,VER=014,GEN=GEN,                       JM#14 X
               TITLE='IPO-SUPPLIED PRINT COMMAND (WITH MANY MODIFICATIOX
               NS) - WSRCC VERSION 14'                           JM#14
         LR    R8,R1                   LOAD REGISTER 8 TO PRESERVE
         SPACE 1                                                 JM#14
         L     R2,16                   LOAD ADDRESS OF CVT         TJ#3
         L     R2,0(R2)                LOAD ADDRESS OF TCB'S       TJ#3
         L     R2,4(R2)                LOAD ADDRESS OF CURRENT TCB TJ#3
         L     R2,12(R2)               LOAD ADDRESS OF TIOT        TJ#3
         MVC   DPID(8),0(R2)           GET JOB NAME                TJ#3
         MVC   DPIDOUT(19),BLANKS      BLANK OUT DPID AREA         TJ#3
*
***********************************************************************
*                                                                     *
*        BUILD PARSE AND DAIR PARAMETER LISTS USING COMMAND           *
*           PROCESSOR PARAMETER LIST                                  *
*        INITIALIZE OUTPUT DATA CONTROL BLOCKS IN WORKAREA            *
*                                                                     *
***********************************************************************
*
         USING CPPL,R8                 ESTABLISH ADDRESSABILITY TO CPPL
         LA    R4,PPLSECT              LOAD ADDRESS OF PARSE PARM LIST
         USING PPL,R4                  ESTABLISH ADDRESSABILITY TO PPL
         LA    R6,DAPLSECT             LOAD ADDRESS OF DAIR PARM LIST
         USING DAPL,R6                 ESTABLISH ADDRESSABILITY TO DAPL
         LA    R5,IOPLSECT             LOAD ADDRESS OF PUTLINE PARM LST
         USING IOPL,R5                 ESTABLISH ADDRESSABILITY TO IOPL
         L     R1,CPPLUPT              LOAD POINTER TO UPT FROM CPPL
         L     R2,CPPLECT              LOAD POINTER TO ECT FROM CPPL
         SLR   R3,R3                   ZERO REGISTER 3
         ST    R3,ECB                  STORE REGISTER AS ECB FOR PPL
         LA    R3,ECB                  LOAD ADDRESS OF ECB
         STM   R1,R3,DFPLSECT          STORE UPT,ECT,ECB IN DFPL  GTE
         STM   R1,R3,PPLSECT           STORE ADDRESS OF UPT, ECT, AND
*                                      ECB IN PPL
         STM   R1,R3,DAPLSECT          STORE ADDRESS OF UPT, ECT, AND
*                                      ECB IN DAPL
         STM   R1,R3,IOPLSECT          STORE ADDRESS OF UPT, ECT, AND
*                                      ECB IN IOPL
         LA    R3,IOPB                 LOAD ADDRESS OF PUTLINE PARM BLK
         ST    R3,IOPLIOPB             STORE IOPB ADDRESS IN IOPL
         LA    R3,DFPBSECT             GET ADDRESS OF DFPB     GTE
         ST    R3,DFPLSECT+12          PUT IT IN THE CONTROL BLOCK GTE
         USING  DFPB,R3                                      GTE
         LA     R2,DFPBDSL                                   GTE
         ST     R2,DFPBDSN                                   GTE
         LA     R2,DFPBQUA                                   GTE
         ST     R2,DFPBQUAL                                  GTE
         MVI    DFPBCODE,DFPB04                              GTE
         MVC    DFPBPSCB,CPPLPSCB                            GTE
         MVI    DFPBCNTL,DFPBRET                             GTE
         XC     DFPBCAT(8),DFPBCAT                           GTE
         DROP    R3                                          GTE
         MVC   PPLPCL,PCLADDR          MOVE POINTER TO PARAMETER
*                                      CONTROL LIST INTO PPL
         LA    R2,PARSBACK             LOAD ADDRESS OF FULLWORD TO
*                                      CONTAIN ADDRESS OF PDE FROM PARS
         ST    R2,PPLANS               STORE POINTER TO RETURN ADDRESS
         MVC   PPLCBUF,CPPLCBUF        MOVE POINTER TO COMMAND BUFFER
*                                      INTO PPL
         LA    R2,PPLUWA               LOAD ADDRESS OF USER WORK AREA
         ST    R2,PPLUWA               STORE ADDRESS IN PPL
         MVC   DAPLPSCB,CPPLPSCB       MOVE POINTER TO PSCB INTO DAPL
         DROP  R4,R5,R6,R8             DROP ADDRESSABILITY TO CPPL,
*                                      IOPL, DAPL, AND PPL
         MVC   OUTPUTA(OUTPUTAL),OUTPUTAC  MOVE ASA DCB CONSTANT INTO
*                                      ASA DCB IN WORKAREA
         MVC   OUTPUTM(OUTPUTML),OUTPUTMC  MOVE MACHINE DCB CONSTANT
*                                      INTO MACHINE DCB IN WORKAREA
*
***********************************************************************
*                                                                     *
*        CALLTSSR TO PARSE (ENTRY NAME IKJPARS)                       *
*        ON RETURN PARSBACK -> PDL                                    *
*        HANDLE ALL ERROR CODES RETURNED BY PARS                      *
*        IF NECESSARY CALL GENERAL FAIL SERVICE ROUTINE               *
*                                                                     *
***********************************************************************
*
         CALLTSSR  EP=IKJPARS,MF=(E,PPLSECT)
*                                      CALL PARS TO PARSE COMMAND
         LTR   R15,R15                 SEE IF RETURN CODE IS ZERO
         BZ    GOODPARS                YES....GO TO GOOD PARS ROUTINE
         C     R15,FULL4               SEE IF RETURN CODE IS FOUR
         BE    RETURN                  YES....PARS ISSUED ERROR
*                                      MESSAGE.  GO CLEANUP AND RETURN
         C     R15,FULL20              SEE IF RETURN CODE IS TWENTY
         BE    RETURN                  YES....VALIDITY CHECKING
*                                      ROUTINE ISSUED ERROR MESSAGE.
*                                      GO CLEANUP AND RETURN
         XC    GFPARMS(GFLENGF),GFPARMS  INITIALIZE PARMLIST FOR
*                                      GENERAL FAIL TO ZEROS
         LA    R1,GFPARMS              LOAD ADDRESS OF PARMLIST
         ST    R1,GFPARMP              STORE ADDRESS AS POINTER TO
*                                      PARMLIST
         ST    R15,GFRCODE             STORE RETURN CODE IN PARMLIST
         LA    R1,GFPARSE              LOAD CALLER ID FOR PARS
         STH   R1,GFCALLID             STORE CALLER ID IN PARMLIST
         L     R1,SAVEAREA+4           LOAD ADDRESS OF PEVIOUS SAVEAREA
         L     R1,24(R1)               LOAD POINTER TO CPPL
         ST    R1,GFCPPLP              STORE POINTER TO CPPL IN PRMLIST
         LA    R1,ECB                  LOAD ADDRESS OF OPTIONAL ECB
         ST    R1,GFECBP               STORE POINTER TO ECB IN PARMLIST
         SLR   R1,R1                   ZERO REGISTER 1
         ST    R1,ECB                  SET OPTIONAL ECB TO ZERO
         LINK  EP=IKJEFF19,MF=(E,GFPARMP)  LINK TO GENERAL FAIL SERVICE
*                                      ROUTINE TO HANDLE RETURN CODE
         LTR   R15,R15                 SEE IF RETURN CODE IS ZERO
         BZ    RETURN                  YES....GO CLEANUP AND RETURN
         LR    R8,R15                  LOAD REGISTER 8 WITH RETURN
         BAL   R14,SETCODE             GO CONVERT CODE TO DECIMAL
         PUTLINE OUTPUT=(GNRLERR,SINGLE,DATA),MF=(E,IOPLSECT)  PUT OUT
*                                      GENERAL FAIL ERROR MESSAGE
         PUTLINE OUTPUT=(RETNCD,SINGLE,DATA),MF=(E,IOPLSECT)  PUT OUT
*                                      DECIMAL RETURN CODE
         B     RETURN                  GO CLEANUP AND RETURN
*
***********************************************************************
*                                                                     *
*        SET UP ADDRESSABILITY TO PDL                                 *
*           AND LOAD REGISTER 6 WITH ADDRESS OF DSNAME PDE            *
*                                                                     *
***********************************************************************
*
GOODPARS DS    0H
         L     R9,PARSBACK             LOAD POINTER TO PARS ANSWER
         USING IKJPARMD,R9             ESTABLISH ADDRESSABILITY TO PDL
         MVC   CCTLKEY,PCCTL           SAVE THE CCTL KEYWORD NUM   TF#1
         MVC   CTYPKEY,CCTLTYP         SAVE THE CC TYPE KEYWORD    TF#1
         LA    R6,DSNAMES              LOAD POINTER TO DSNAME LIST
         MVI   VOLBIT,BLANK            BLANK OUT THE VOLUME WORK AREA
         MVC   VOLUME,VOLBIT           CONTINUE BLANKING THE VOLUME
         MVC   LINMSGH(LINMSGL),LINMSG SET UP TOTAL LINES MESSAGE  TJ#8
         MVI   CCFLAG,C'Y'             SET CC FLAG FOR CC          TJ#2
         MVI   FIRSTFL,C'F'            SET FLAG FOR FIRST PASS     TJ#1
         MVI   REPCC,C'N'           SET FLAG FOR NO REPLACEMENT CC TJ#6
         CLC   PEJECT(2),HALF1         CHECK FOR    EJECT          TJ#1
         BNE   CHCKEJCT               GO CHECK WHICH NO EJECT TYPE TJ#1
         MVI   EJECTFL,C'E'            SET EJECT FLAG FOR EJECT    TJ#1
         B     CHCKLNS                 GO TO CHECK VOLUME          TJ#1
PACKEJCT PACK  DEC(8),0(0,R7)                                      TJ#1
CHCKEJCT DS    0H                                                  TJ#1
         MVI   EJECTFL,C'F'            SET EJECT FLAG FOR FIRST    TJ#1
         L     R7,NOEJKEY              LOAD POINTER TO EJECT FIELD TJ#1
         LH    R4,NOEJKEY+4            LOAD LENGTH OF EJECT FIELD  TJ#1
         BCTR  R4,0                    DECREMENT R4 FOR EX OF PACK TJ#1
         EX    R4,PACKEJCT             EX PACK TO CONVERT EJECT    TJ#1
*                                      TO PACKED DECIMAL           TJ#1
         CVB   R4,DEC                  CONVERT PACKED TO BINARY    TJ#1
         ST    R4,EJECTNM              STORE NUMBER INTO EJECTNM   TJ#1
CHCKLNS  DS    0H                                                  TJ#2
         ZAP   NLINES(3),=P'0'         ZERO NLINES                 TJ#2
         ZAP   HLINES(9),=P'0'         ZERO HLINES                 TJ#2
         ZAP   TLINES(9),=P'0'         ZERO TOTAL LINES PRINTED    TJ#4
         ZAP   GTLINES(9),=P'0'        ZERO GRAND TOTAL OF LINES   TJ#8
         ZAP   TRECDS(9),=P'0'         ZERO TOTAL RECORDS READ     TJ#4
         ZAP   STARTP(6),=P'0'         ZERO START POINT            TJ#4
         ZAP   STOPP(6),=P'0'          ZERO STOP POINT             TJ#4
         ZAP   COUNTP(4),=P'0'         ZERO RECORD TO PRINT COUNT  TJ#4
         CLC   PLINES(2),HALF1         CHECK FOR NOLINES           TJ#2
         BE    CHKSTART           IF NOLINES THEN GOTO CHK START   TJ#2
         L     R7,LINNUMB              LOAD POINTER TO LINES FIELD TJ#2
         LH    R4,LINNUMB+4            LOAD LENGTH OF LINES FIELD  TJ#2
         BCTR  R4,0                    DECREMENT R4 FOR EX OF PACK TJ#2
         EX    R4,PACKLINS             EX PACK TO CONVERT LINES    TJ#2
         CP    NLINES,=P'4'            MAKE SURE AT LEAST 4 LINES  TJ#2
         BL    BADLINS                 ITS BAD, BRANCH TO PRT MSG  TJ#2
         B     CHKSTART                ITS OK, BRANCH TO CHK START TJ#2
PACKLINS PACK  NLINES(3),0(0,R7)                                   TJ#2
CHKSTART DS    0H                                                  TJ#4
         CLC   PSTART(2),HALF1         CHECK FOR START             TJ#4
         BNE   CHKSTOPP           IF NO STOP  THEN CHECK STOP      TJ#4
         L     R7,STARTNUM             LOAD POINTER TO START FIELD TJ#4
         LH    R4,STARTNUM+4           LOAD LENGTH OF START FIELD  TJ#4
         BCTR  R4,0                    DECREMENT R4 FOR EX OF PACK TJ#4
         EX    R4,PACKSTRT             EX PACK TO CONVERT START    TJ#4
         B     CHKSTOPP                        BRANCH TO CHK STOP  TJ#4
PACKSTRT PACK  STARTP(6),0(0,R7)                                   TJ#4
CHKSTOPP DS    0H                                                  TJ#4
         CLC   PSTOP(2),HALF1         CHECK FOR STOP               TJ#4
         BNE   CHKCNTP            IF NO START THEN CHECK COUNT     TJ#4
         L     R7,STOPNUM              LOAD POINTER TO STOP  FIELD TJ#4
         LH    R4,STOPNUM+4            LOAD LENGTH OF STOP  FIELD  TJ#4
         BCTR  R4,0                    DECREMENT R4 FOR EX OF PACK TJ#4
         EX    R4,PACKSTOP             EX PACK TO CONVERT STOP     TJ#4
         B     CHKCNTP                         BRANCH TO CHK COUNT TJ#4
PACKSTOP PACK  STOPP(6),0(0,R7)                                    TJ#4
CHKCNTP  DS    0H                                                  TJ#4
         CLC   PCOUNT(2),HALF1        CHECK FOR COUNT              TJ#4
         BNE   CHCKDUB            IF NO COUNT THEN CHECK DOUBLE    TJ#6
         L     R7,COUNTNUM             LOAD POINTER TO COUNT FIELD TJ#4
         LH    R4,COUNTNUM+4           LOAD LENGTH OF COUNT FIELD  TJ#4
         BCTR  R4,0                    DECREMENT R4 FOR EX OF PACK TJ#4
         EX    R4,PACKCNTP             EX PACK TO CONVERT COUNT    TJ#4
         B     CHCKDUB                        BRANCH TO CHK DOUBLE TJ#6
PACKCNTP PACK  COUNTP(4),0(0,R7)                                   TJ#4
CHCKDUB  DS    0H                                                  TJ#6
         CLC   PDOUBLE(2),HALF1        SEE IF DOUBLE SPECIFIED     TJ#6
         BNE   CHKTRIP                 NO, SEE IF TRIPLE SPECIFIED TJ#6
         MVI   REPCC,C'D'              SET FLAG FOR DOUBLE CC      TJ#6
CHKTRIP  DS    0H                                                  TJ#6
         CLC   PTRIPLE(2),HALF1        SEE IF TRIPLE SPECIFIED     TJ#6
         BNE   CHCKVOL                 NO, SEE IF VOLUME SPECIFIED TJ#6
         MVI   REPCC,C'T'              SET FLAG FOR TRIPLE CC      TJ#6
CHCKVOL  DS    0H                                                  TJ#4
         CLC   PVOL(2),HALF1           SEE IF VOLUME SPECIFIED
         BNE   CHKFOLD                 NO.....GO SEE IF FOLD IS DESIRED
         L     R7,SVOL                 LOAD POINTER TO VOLUME
         LH    R8,SVOL+4               LOAD LENGTH OF VOLUME
         BCTR  R8,0                    DECREMENT R8 FOR EX OF MOVE
         EX    R8,MOVEVOL              MOVE VOLUME INTO WORK AREA
         OI    VOLBIT,HIGH             INDICATE VOLUME PRESENT
CHKFOLD  DS    0H
         CLC   PFOLD(2),HALF1          SEE IF FOLD SPECIFIED
         BNE   CHKHEAD                 NO.....GO LOOP THROUGH DSNAMES
         OI    VOLBIT,LOW              INDICATE FOLD PRESENT
CHKHEAD  DS    0H
         MVI   NODELTF,C'N'          INITIALIZE DELETE FLAG TO NO  TJ#1
         CLC   PHEAD(2),HALF1          SEE IF HEAD SPECIFIED
         BNE   NEXT                    NO.....GO LOOP THROUGH DSNAMES
         OI    VOLBIT,X'08'              INDICATE HEAD PRESENT
         DROP  R9                      DROP ADDRESSABILITY TO PDL
*
***********************************************************************
*                                                                     *
*        LOOP THROUGH DSNAME LIST RETURNED BY PARS                    *
*                                                                     *
***********************************************************************
*
NEXT     DS    0H
         LA    R1,PRINTI               LOAD POINTER TO DDNAME TO BE
*                                      FREED
         BAL   R2,FREEDD               BRANCH TO FREE ROUTINE
         LTR   R8,R8                   SEE IF RETURN CODE FROM FREE IS
*                                      ZERO
         BNZ   CLOSDCBS                NO.....GO CLOSE DCBS AND RETURN
         MVI   NODELTF,C'Y'          INITIALIZE DELETE FLAG TO YES TJ#1
         L     R5,0(R6)                LOAD POINTER TO DSNAME
         LH    R4,4(R6)                LOAD LENGTH OF DSNAME
         STH   R4,DFPBDSL                                    GTE
         TM    6(R6),X'40'                                   GTE
         BO    GTENOQ                                        GTE
         BCTR  R4,0                                          GTE
         MVC   DFPBNAME,BLANKS                               GTE
         EX    R4,DFPBMVE                                    GTE
         LA    R1,DFPLSECT                                   GTE
         MVC   DFPBQUA,=CL8'   '                             GTE
         CALLTSSR  EP=IKJEHDEF                               GTE
GTEDEF   LTR    R15,R15                                      GTE
GTEOK    LA     R5,DFPBNAME                                  GTE
         LH     R4,DFPBDSL                                   GTE
GTENOQ   DS    0H                                            GTE
         STCM  R4,3,DSNLEN             STORE LENGTH OF DSNAME
         BCTR  R4,0                    DECREMENT R4 FOR EX OF MOVE
         LA    R8,DAP08                LOAD ADDRESS OF DAPB FOR 08
         USING DAPB08,R8               ESTABLISH ADDRESSABILITY TO DAPB
         MVC   DAPB08(DAPB08L),DAPB08C MOVE CONSTANTS INTO DAPB
         LA    R1,DSNLEN               LOAD POINTER TO DSN BUFFER
         ST    R1,DA08PDSN             STORE POINTER TO DSN BUFFER IN
*                                      DAPB
         MVI   DSNBUF,BLANK            BLANK OUT THE DSNAME BUFFER
         MVC   DSNBUF+1(43),DSNBUF     FINISH BLANKING OUT THE BUFFER
         EX    R4,MOVEDSN              MOVE IN THE DSNAME
         TM    14(R6),HIGH             SEE IF MEMBER NAME IS PRESENT
         BNO   CHKPASS                 NO.....GO CHECK IF THERE IS A
*                                      PASSWORD
         MVI   NODELTF,C'N'            SET DELETE FLAG TO NO       TJ#1
         L     R5,8(R6)                LOAD POINTER TO THE MEMBER NAME
         LH    R4,12(R6)               LOAD LENGTH OF MEMBER NAME
         BCTR  R4,0                    DECREMENT R4 FOR EX OF MOVE
         EX    R4,MOVEMEM              MOVE IN THE MEMBER NAME
CHKPASS  DS    0H
         MVC   MEMNAME,DA08MNM         MOVE MEMBER NAME INTO HEADER 2
         TM    22(R6),HIGH             SEE IF PASSWORD IS PRESENT
         BNO   CHKVOL                  NO.....GO SEE IF VOLUME PRESENT
         L     R5,16(R6)               LOAD POINTER TO THE PASSWORD
         LH    R4,20(R6)               LOAD LENGTH OF PASSWORD
         BCTR  R4,0                    DECREMENT R4 FOR EX OF MOVE
         EX    R4,MOVEPASS             MOVE IN THE PASSWORD
CHKVOL   DS    0H
         TM    VOLBIT,HIGH             SEE IF VOLUME IS PRESENT
         BNO   ALLOCDD                 NO.....GO ALLOCATE DATA SET
         MVC   DA08SER,VOLUME          MOVE IN THE VOLUME SERIAL
ALLOCDD  DS    0H
         L     R9,PARSBACK       LOAD ADDRESS PARS ANSWER          TJ#1
         USING IKJPARMD,R9       ESTABLISH ADDRESSABILITY TO PDL   TJ#1
         MVI   DA08DSP1,X'08'          MOVE IN  SHR DISPOSITION    TJ#1
         CLC   PDELETE(2),HALF1        WAS DELETE SPECIFIED?       TJ#1
         BNE   SKIPDELT                IF NOT SKIP MOVING IN DEL   TJ#1
         CLI   NODELTF,C'N'            CHECK DELETE FLAG           TJ#1
         BE    SKIPDELT                IF NO, SKIP MOVING IN DEL   TJ#1
         MVI   DA08DSP1,X'01'          MOVE IN  OLD DISPOSITION    TJ#1
         DROP  R9                                                  TJ#1
SKIPDELT DS    0H                                                  TJ#1
         SLR   R4,R4                   ZERO REGISTER 4
         ST    R4,ECB                  STORE REGISTER 4 AS ECB
         LA    R1,DAPLSECT             LOAD ADDRESS OF DAPL
         USING DAPL,R1                 ESTABLISH ADDRESSABILITY TO DAPL
         ST    R8,DAPLDAPB             STORE ADDRESS OF DAPB IN DAPL
         DROP  R1                      DROP ADDRESSABILITY TO DAPL
         CALLTSSR EP=IKJDAIR           CALL DAIR TO ALLOCATE DATASET
         LTR   R15,R15                 SEE IF RETURN CODE IS ZERO
         BZ    OUTPUT                  YES....GO PRINT DATASET
         MVI   DFID+1,DFDAIR           SET DAIRFAIL ID TO INDICATE DAIR
         BAL   R2,DAIRFAIL             GO TO DAIRFAIL ROUTINE TO HAVE
*                                      ERROR MESSAGE PUT OUT
NOTPMESS DS    0H
         PUTLINE OUTPUT=(DSNNOTP,SINGLE,DATA),MF=(E,IOPLSECT)  PUT OUT
*                                      DATASET NOT PRINTED MESSAGE
         B     EXITMSG                 GO PUT OUT DATASET AND MEMBER
*                                      NAMES
*
***********************************************************************
*                                                                     *
*        DETERMINE DATASET TYPE AND CALL PRINT ROUTINE                *
*                                                                     *
***********************************************************************
*
OUTPUT   DS    0H
         TIME  DEC                     TIME AND DATE FOR HEAD TF#2/JM#1
         SRL   R0,8                    GET RID OF T&H OF SEC  TF#2/JM#1
         SLL   R1,8                    GET RID OF X'00'IN DATETF#2/JM#1
         ST    R0,TIMESAVE             SAVE THE TIME          TF#2/JM#1
         ST    R1,DATESAVE              AND THE DATE          TF#2/JM#1
         MVC   DATETIME(19),DATEEDIT   MOVE THE EDIT PATTERN  TF#2/JM#1
         ED    DATETIME(19),TIMESAVE+1 EDIT DATE&TIME      TF#2/JM#1
         TM    DA08DSO,DSOPS           X'40' PHYSICAL SEQUENTIAL
         BO    PRINTIT                 YES....GO PRINT DATASET
         CLC   DA08MNM,BLANKS          SEE IF MEMBER NAME IS BLANK
         BNE   PRINTIT                 NO.....GO TREAT AS SEQUENTIAL
         TM    DA08DSO,DSOPO           X'02' PARTITIONED
         BO    SCROLLIT                YES....GO SCROLL DIRECTORY
         DROP  R8                      DROP ADDRESSABILITY TO DAPB
         PUTLINE OUTPUT=(NOTPSPO,SINGLE,DATA),MF=(E,IOPLSECT)  PUT OUT
*                                      NOT PARTITIONED OR SEQUENTIAL
*                                      MESSAGE
         B     NOTPMESS                GO PUT NOT PRINTED MESSAGE
*
***********************************************************************
*                                                                     *
*        READS RECORDS USING GET MOVE                                 *
*           AND ATTRIBUTES OF GIVEN DATASET                           *
*        WRITES THESE SAME RECORDS USING PUT MOVE                     *
*           AND THE INPUT DATASET ATTRIBUTES                          *
*                                                                     *
***********************************************************************
*
PRINTIT  DS    0H
         LA    R8,INPUT                LOAD POINTER TO INPUT DCB
         USING IHADCB,R8               ESTABLISH ADDRESSABILITY TO DCB
         MVC   INPUT(INPUTL),INPUTC    MOVE INPUT DCB CONSTANT INTO
*                                      INPUT DCB IN WORKAREA
         MVC   OPENLST,OPENCLOS        MOVE OPEN(CLOSE) LIST CONSTANT
*                                      INTO OPEN LIST IN WORKAREA
         OPEN  ((R8),INPUT),MF=(E,OPENLST)  OPEN INPUT DCB
PRINTS   DS    0H                      BRANCH ADDRESS FOR PRINTING
*                                      MEMBERS
         TM    DCBOFLGS,DCBOFOPN       SEE IF DCB OPENED SUCCESSFULLY
         BNO   NOTPMESS                NO.....GO PUT OUT NOT PRINTED
*                                      MESSAGE
         TM    DCBRECFM,DCBRECU        SEE IF RECFM IS UNDEFINED
         BO    ERRORU                  NO.....GO PUT OUT ERROR MESSAGE
         BZ    ERRORU                  ZERO...NOT FIXED OR VARIABLE SO
*                                      GO PUT OUT ERROR MESSAGE
         CLC   DCBLRECL,HALF256        SEE IF LRECL IS GREATER THAN 256
         BH    ERRORL                  YES....GO PUT OUT RECORD LENGTH
*                                      MESSAGE
         LA    R9,LINE                 LOAD ADDRESS FOR INPUT
         SLR   R4,R4                   ZERO OUT REGISTER 4
         ST    R4,LENGTH               STORE ZERO IN LENGTH (PLUS 2
*                                      RESERVED BYTES) FIELD
         TM    DCBRECFM,DCBRECV        SEE IF RECFM IS VARIABLE
         BO    CHKCNTRL                YES....GO PUT OUT DSNAME
*                                      NO.....RECFM MUST BE FIXED
*                                      SO CONTINUE
         A     R9,FULL4                SET TO HAVE FIXED LENGTH
         LH    R4,DCBLRECL             LOAD LOGICAL RECORD LENGTH
         A     R4,FULL4                ADD 4 FOR LENGTH FIELD PREFIX
         STH   R4,LENGTH               STORE IN LENGTH FIELD
CHKCNTRL DS    0H
         MVC   HEADER,HEADERC          MOVE HEADER INTO DATA AREA
         MVC   DPIDOUT+4(8),DPID        MOVE IN DPID               TJ#3
         L     R7,PARSBACK             LOAD ADDRESS TO PARMS       TJ#3
         USING IKJPARMD,R7        ESTABLISH ADDRESSABILITY TO PDL  TJ#3
         CLC   PTITLE(2),HALF1    DID THE USER SPECIFY A TITLE     TJ#3
         BNE   CHKCCTLK      NO...                                 TJ#3
         MVC   HDRCNTL+1(40),BLANKS   SPACE OUT HEADER             TJ#3
         L     R3,TITLEFLD            LOAD POINTER TO TITLE        TJ#3
         LH    R4,TITLEFLD+4          LOAD LENGTH OF TITLE         TJ#3
         BCTR  R4,R0                  DECREMENT FOR EXECUTE        TJ#3
         EX    R4,MOVETTL1            MOVE IN USER TITLE           TJ#3
         B     CHKCCTLK               BRANCH TO CHECK CCTLKEY      TJ#3
MOVETTL1 MVC   HDRCNTL+1(0),0(R3)     MOVE IN USER TITLE           TJ#3
         DROP  R7                                                  TJ#3
CHKCCTLK CLC   CCTLKEY,HALF0   DID USER SPECIFY CCTL OVERRIDE      TF#1
         BE    CHKCNTL1      NO...                                 TF#1
         CLC   CCTLKEY,HALF1   DID USER SPECIFY NOCCTL             TF#1
         BE    FORCNOCC      YES                                   TF#1
         CLC   CTYPKEY,HALF1 MUST HAVE SPECIFIED CCTL-WAS IT ASA?  TF#1
         BE    FORCEASA      YES                                   TF#1
         B     FORCEMCH      IT HAD TO BE MCH                      TF#1
CHKCNTL1 DS    0H                                                  TF#1
         TM    DCBRECFM,DCBRECCM       SEE IF CONTROL IS MACHINE
         BNO   USEA                    NO.....GO TO USE ASA CONTROL
FORCEMCH DS    0H                      BRANCH HERE TO FORCE MCH    TF#1
         MVI   CCTYPE,C'M'             CC TYPE IS MACHINE          TJ#1
         MVC   HBLNK(5),BLNKMCH        SAVE MCH BLANK LINE         TJ#7
         LA    R5,OUTPUTM              LOAD ADDRESS OF OUTPUT DCB FOR
*                                      MACHINE CODE
         LA    R4,EJECTM               LOAD ADDRESS OF EJECT DATA
         MVI   HDRCNTL,SKIPM           MOVE INTO HEADER MACHINE CODE
*                                      FOR SPACE
         CLI   REPCC,C'N'              CHECK FOR REPLACEMENT CC    TJ#6
         BE    SKPMCHCC                NO, GO TO CHECK DCB         TJ#6
         CLI   REPCC,C'D'              CHECK FOR DOUBLE CC         TJ#6
         BNE   CHKMCHCT                NO, GO TO CHECK DCB         TJ#6
         MVI   REPCC,X'11'             SET UP DOUBLE MCH CC        TJ#6
         B     SKPMCHCC                GO TO CONTINUE              TJ#6
CHKMCHCT CLI   REPCC,C'T'              CHECK FOR TRIPLE CC         TJ#6
         BNE   SKPMCHCC                NO, BAD REPCC GOTO SKPMCHCC TJ#6
         MVI   REPCC,X'19'             SET UP TRIPLE MCH CC        TJ#6
SKPMCHCC DS    0H                                                  TJ#6
         TM    48(R5),DCBOFOPN         SEE IF DCB IS ALREADY OPEN
         BO    PUTDSN                  YES....GO PUT OUT HEADERS
         LA    R3,PRINTM               LOAD POINTER TO PRINTM
         LA    R14,PUTDSN              LOAD ADDRESS FROM RETURN FROM
*                                      SYSOUT ALLOCATION
         B     ALLOCOUT                GO TO ALLOCATE SYSOUT
USEA     DS    0H
         TM    DCBRECFM,DCBRECCA       SEE IF CONTROL IS ASA
         BO    GOODLENA                YES....GO SEE IF DCB IS OPEN
FORCNOCC A     R9,FULL1                SET GET AREA TO ALLOW FOR NO
*                                      CONTROL
         MVI   CCFLAG,C'N'             SET CC FLAG FOR NO CC       TJ#2
         LH    R4,LENGTH               GET THE LENGTH OF OUTPUT    TJ#6
         A     R4,FULL1                ADD ONE FOR A CONTROL BYTE
         STH   R4,LENGTH               STORE LENGTH IN PUT AREA
GOODLENA DS    0H
FORCEASA DS    0H                      BRANCH HERE TO FORCE ASA    TF#1
         MVI   CCTYPE,C'A'             CC TYPE IS ASA              TJ#1
         MVC   HBLNK(5),BLNKASA        SAVE ASA BLANK LINE         TJ#7
         LA    R5,OUTPUTA              LOAD ADDRESS OF OUTPUT DCB FOR
*                                      ASA CODE
         LA    R4,EJECTA               LOAD ADDRESS OF EJECT DATA
         CLI   REPCC,C'N'              CHECK FOR REPLACEMENT CC    TJ#6
         BE    SKPASACC                NO, GO TO CHECK DCB         TJ#6
         CLI   REPCC,C'D'              CHECK FOR DOUBLE CC         TJ#6
         BNE   CHKASACT                NO, GO TO CHECK DCB         TJ#6
         MVI   REPCC,C'0'              SET UP DOUBLE ASA CC        TJ#6
         B     SKPASACC                GO TO CONTINUE              TJ#6
CHKASACT CLI   REPCC,C'T'              CHECK FOR TRIPLE ASA CC     TJ#6
         BNE   SKPASACC                NO, BAD REPCC GOTO SKPASACC TJ#6
         MVI   REPCC,C'-'              SET UP TRIPLE ASA CC        TJ#6
SKPASACC DS    0H                                                  TJ#6
         TM    48(R5),DCBOFOPN         SEE IF DCB IS ALREADY OPEN
         BO    PUTDSN                  YES....GO PUT OUT HEADERS
         LA    R3,PRINTA               LOAD POINTER TO PRINTA
         BAL   R14,ALLOCOUT            LOAD ADDRESS FOR RETURN AND GO
*                                      DO SYSOUT ALLOCATION
PUTDSN   DS    0H
         CLI   EJECTFL,C'E'            CHECK FOR EJECT             TJ#1
         BE    EJECT1                  IF SET BRANCH TO EJECT      TJ#1
         CLI   EJECTFL,C'F'            CHECK FOR FIRST EJECT       TJ#1
         BNE   CHKEJCT1                IF NOT FIRST THEN BRANCH    TJ#1
         MVI   EJECTFL,C'X'            CLEAR FIRST EJECT FLAG      TJ#1
         B     SKPEJCT1                ALWAYS SKIP FIRST EJECT     TJ#1
CHKEJCT1 DS    0H                                                  TJ#1
         L     R7,EJECTNM              LOAD NUMBER OF SEP LINES    TJ#1
         LTR   R7,R7                   IS THE NUMBER ZERO?         TJ#1
         BZ    SKPEJCT1                BRANCH IF NO SEP LINES      TJ#1
         LA    R4,BLNKASA              ASSUME ASA                  TJ#1
         CLI   CCTYPE,C'M'             IS TYPE MCH?                TJ#1
         BNE   SEPLN1                  NO, BRANCH TO PUT SEP LINES TJ#1
         LA    R4,BLNKMCH              SET UP BLANK MCH LINE       TJ#1
SEPLN1   DS    0H                                                  TJ#1
         PUT   (R5),(R4)               PUT OUT BLANK LINE          TJ#1
         AP    GTLINES(9),=P'1'        ADD 1 TO GRAND TOTAL        TJ#8
         AP    HLINES(9),=P'1'         ADD 1 TO NUMBER LINES PRINT TJ#2
         BCT   R7,SEPLN1               BRANCH IF NOT ZERO          TJ#1
         MVI   FIRSTFL,C'X'            CLEAR FIRST PRINT FLAG      TJ#1
         B     SKPEJCT1                       BRANCH AROUND EJECT  TJ#1
EJECT1   DS    0H                                                  TJ#1
         CLI   FIRSTFL,C'F'            CHECK FOR FIRST PRINT       TJ#8
         BNE   EJECTIT1                BRANCH TO EJECT IF NOT      TJ#8
         L     R7,PARSBACK             LOAD ADDRESS TO PARMS       TJ#8
         USING IKJPARMD,R7        ESTABLISH ADDRESSABILITY TO PDL  TJ#8
         CLC   PARR(2),HALF1           CHECK FOR   ARR             TJ#8
         BE    SKPEJCT1               ARR, BRANCH TO SKIP EJECT    TJ#8
EJECTIT1 DS    0H                                                  TJ#8
         PUT   (R5),(R4)               PUT OUT PAGE EJECT
         AP    GTLINES(9),=P'1'        ADD 1 TO GRAND TOTAL        TJ#8
         ZAP   HLINES(9),=P'0'         ZERO HLINES                 TJ#2
         MVI   FIRSTFL,C'X'            CLEAR FIRST PRINT FLAG      TJ#1
SKPEJCT1 DS    0H                                                  TJ#1
         CLI   FIRSTFL,C'F'            CHECK FOR FIRST PRINT       TJ#2
         BNE   CHCKHEAD                BRANCH TO CHECK FOR HEADER  TJ#2
         L     R7,PARSBACK             LOAD ADDRESS TO PARMS       TJ#2
         USING IKJPARMD,R7        ESTABLISH ADDRESSABILITY TO PDL  TJ#2
         CLC   PARR(2),HALF1           CHECK FOR   ARR             TJ#2
         BNE   CHCKHEAD            NO ARR, BRANCH TO CHECK HEADER  TJ#2
         MVI   FIRSTFL,C'X'            CLEAR FIRST PRINT FLAG      TJ#2
         MVC   ARRPRTLN(ARRPL),ARRPRTR                             TJ#2
         L     R15,ARRNUM              LOAD ADDRESS OF TYPE        TJ#2
         MVC   ARRPRTN(3),0(R15)       MOVE IN ARRANGMENT NUMBER   TJ#2
         MVC   ARRCC,CCASATOP          ASSUME ASA CNTL             TJ#2
         CLI   CCTYPE,C'M'             SHOULD IT BE MCH TYPE?      TJ#2
         BNE   MVPRINT                 NO, BRANCH TO CONTINUE      TJ#2
         MVC   ARRCC,CCMCHTOP          MOVE IN MCH CNTL            TJ#2
MVPRINT  DS    0H                                                  TJ#2
         MVC   ARRPRT,=C',PRINT  '     MOVE IN ,PRINT              TJ#2
         PUT   (R5),ARRPRTLN           PUT OUT ARRANGEMENT LINE    TJ#2
         AP    GTLINES(9),=P'1'        ADD 1 TO GRAND TOTAL        TJ#8
CHCKDPLX DS    0H                                                  TJ#5
         CLC   PDUPLEX(2),HALF1        CHECK FOR DUPLEX            TJ#4
         BNE   CKARCOPY                NO DUPLEX, CHECK COPIES     TJ#4
         MVC   ARRPRT(80),ARRDUP       MOVE IN DUPLEX              TJ#4
         PUT   (R5),ARRPRTLN           PUT OUT DUPLEX LINE         TJ#4
         AP    GTLINES(9),=P'1'        ADD 1 TO GRAND TOTAL        TJ#8
CKARCOPY DS    0H                                                  TJ#4
         AIF   (&LGENOPT EQ 0).CKANUM  IF PARTIAL GEN OPT        JM#14
         CLC   PCOPIES(2),HALF1        SEE IF COPY SPECIFIED       TJ#4
         BNE   CHCKANUM                NO.....GO CHECK NUMBER      TJ#4
         MVC   ARRPRT,=C'QUA     '     MOVE IN  QUA                TJ#4
         MVC   ARRPRTN(3),=C'   '      BLANK OUT COPIES AREA       TJ#4
         L     R15,SCOPIES             LOAD ADDRESS OF COPIES      TJ#4
         LH    R1,SCOPIES+4            LOAD LENGTH OF COPY FIELD   TJ#4
         BCTR  R1,0                    DECREMENT R1 FOR EX OF MOVE TJ#4
         EX    R1,MOVECOPY             EXECUTE MOVE TO MOVE COPIES TJ#4
         PUT   (R5),ARRPRTLN           PUT OUT QUANITY LINE        TJ#4
         AP    GTLINES(9),=P'1'        ADD 1 TO GRAND TOTAL        TJ#8
         B     CHCKANUM                                            TJ#4
MOVECOPY MVC   ARRPRTN(0),0(R15)       MOVE IN COPIES              TJ#4
.CKANUM  ANOP                                                    JM#14
CHCKANUM DS    0H                                                  TJ#5
         CLC   PNUMBER(2),HALF1        CHECK FOR NUMBER            TJ#5
         BNE   SETUPEND                NO NUMBER, GO SET UP END    TJ#5
         MVC   ARRPRT(80),ARRNUM1      PUT OUT ,MARGIN TEXT        TJ#5
         PUT   (R5),ARRPRTLN           PUT OUT NUMBER LINE         TJ#5
         AP    GTLINES(9),=P'1'        ADD 1 TO GRAND TOTAL        TJ#8
         MVC   ARRPRT(80),ARRNUM2      PUT OUT ,MARGIN TEXT        TJ#5
         PUT   (R5),ARRPRTLN           PUT OUT NUMBER LINE         TJ#5
         AP    GTLINES(9),=P'1'        ADD 1 TO GRAND TOTAL        TJ#8
SETUPEND MVC   ARRPRT,=C',END    '     MOVE IN ,END                TJ#2
         MVC   ARRPRTN(3),=C'   '      SPACE OUT ARR NUM AREA      TJ#2
         MVC   ARRCC,HDRCNTL           MOVE IN CNTL SAVED IN HEAD  TJ#2
         PUT   (R5),ARRPRTLN           PUT OUT ARRANGEMENT LINE    TJ#2
         AP    GTLINES(9),=P'1'        ADD 1 TO GRAND TOTAL        TJ#8
CHCKEXPR DS    0H                                                  TJ#5
         CLC   PEXPRES(2),HALF1        CHECK FOR EXPRESS           TJ#5
         BNE   CHCKHEAD                NO EXPRESS, GO CHECK HEAD   TJ#5
         MVC   ARRPRT(80),EXPRESS      MOVE IN EXPRESS LINE        TJ#5
         PUT   (R5),ARRPRTLN           PUT OUT EXPRESS LINE        TJ#5
         AP    GTLINES(9),=P'1'        ADD 1 TO GRAND TOTAL        TJ#8
         DROP  R7                                                  TJ#2
CHCKHEAD TM    VOLBIT,X'08'            TEST FOR  HEAD
         BO    GTEHEAD       YES
         PUT   (R5),HEADER             PUT OUT PAGE 1 HEADER
         AP    GTLINES(9),=P'1'        ADD 1 TO GRAND TOTAL        TJ#8
         MVI   FIRSTFL,C'X'            CLEAR FIRST PRINT FLAG      TJ#2
         AP    HLINES(9),=P'1'         ADD 1 TO NUMBER LINES PRINT TJ#2
         MVC   HEADER2,LDSNAME         MOVE LENGTH AND 'DSNAME=' INTO
*                                      INTO SECOND HEADER
         MVC   HEADER2+4(1),HDRCNTL    MOVE IN CONTROL BYTE
         CLI   MEMNAME,BLANK           SEE IF MEMBER NAME IS BLANK
         BE    NOMEM                   YES....NO NEED TO PUT IN PARENS
         MVI   OPENPRN,C'('            MOVE OPEN PAREN INTO DSNAME
         MVI   CLOSEPRN,C')'           MOVE CLOSE PAREN INTO DSNAME
         B     PUTHDR2                 GO PUT OUT HEADER 2
NOMEM    DS    0H
         MVI   OPENPRN,BLANK           MOVE BLANK INSTEAD OF OPEN AND
         MVI   CLOSEPRN,BLANK          CLOSE PARENS
PUTHDR2  DS    0H
         PUT   (R5),HEADER2            PUT OUT PAGE 1 HEADER 2
         AP    GTLINES(9),=P'1'        ADD 1 TO GRAND TOTAL        TJ#8
         AP    HLINES(9),=P'1'         ADD 1 TO NUMBER LINES PRINT TJ#2
         MVC   HEADER2(4),SPACE        CHANGE HEADER 2 FOR PUT OF BLANK
*                                      LINE
         PUT   (R5),HEADER2            PUT OUT BLANK LINE
         AP    GTLINES(9),=P'1'        ADD 1 TO GRAND TOTAL        TJ#8
         AP    HLINES(9),=P'1'         ADD 1 TO NUMBER LINES PRINT TJ#2
GTEHEAD  MVC   CNTLBYTE,HDRCNTL        MOVE IN SPACE AS CONTROL
*                                      CHARACTER
         TM    VOLBIT,LOW              SEE IF FOLD IS REQUESTED
         BO    FOLDIT                  YES....GO FOLD OUTPUT
         TM    DCBRECFM,DCBRECV        SEE IF RECFM IS VARIABLE
         BNO   COPYF                   NO.....GO HANDLE FIXED INPUT
         CLC   CCTLKEY,HALF2           WAS CC SPECIFIED           TJ#7
         BE    COPYV                   CC SPECIFIED, GO TO COPYV  TJ#7
         TM    DCBRECFM,DCBRECCC       SEE IF THERE IS A CONTROL BYTE
         BZ    COPYVNO                 NO.....GO TO ADD CONTROL FOR
*                                      VARIABLE
COPYV    DS    0H
         GET   INPUT,(R9)              GET INPUT RECORD
         AP    TRECDS(9),=P'1'         ADD 1 TO TOTAL RECORDS READ TJ#4
         LH    R15,LENGTH              LOAD LENGTH OF LINE         TJ#7
         C     R15,=F'4'               TEST FOR LENGTH OF 4        TJ#7
         BH    CPYVP                   IF GREATER THAN 4 BRANCH    TJ#7
         MVC   0(5,R9),HBLNK           MOVE IN BLANK LINE          TJ#7
CPYVP    BAL   R3,PTLINE               BAL TO PRINT OUT THE LINE   TJ#2
         B     COPYV                   GO GET NEXT RECORD
*
COPYVNO  DS    0H
         GET   INPUT,(R9)              GET INPUT RECORD
         AP    TRECDS(9),=P'1'         ADD 1 TO TOTAL RECORDS READ TJ#4
         SLR   R15,R15                 ZERO REGISTER 15 FOR INSERT
         ICM   R15,7,LENGTH+1          INSERT LENGTH AND RESERVED BYTE
         A     R15,FULL256             ADD 1 TO LENGTH
         STCM  R15,7,LENGTH            STORE LENGTH AND RESERVED BYTE
         MVC   CNTLBYTE,HDRCNTL        MOVE IN CONTROL SAVED IN HEADER
         BAL   R3,PTLINE               BAL TO PRINT OUT THE LINE   TJ#2
         B     COPYVNO                 GO GET NEXT INPUT RECORD
*
COPYF    DS    0H
         GET   INPUT,(R9)              GET INPUT RECORD
         AP    TRECDS(9),=P'1'         ADD 1 TO TOTAL RECORDS READ TJ#4
         BAL   R3,PTLINE               BAL TO PRINT OUT THE LINE   TJ#2
         B     COPYF                   GO GET NEXT RECORD
*
FOLDIT   DS    0H
         TM    DCBRECFM,DCBRECV        SEE IF RECFM IS VARIABLE
         BNO   FOLDF                   NO.....GO HANDLE FIXED INPUT
         CLC   CCTLKEY,HALF2           WAS CC SPECIFIED           TJ#7
         BE    FOLDV                   CC SPECIFIED, GO TO FOLDV  TJ#7
         TM    DCBRECFM,DCBRECCC       SEE IF THERE IS A CONTROL BYTE
         BZ    FOLDVNO                 NO.....GO TO ADD CONTROL FOR
*                                      VARIABLE
FOLDV    DS    0H
         GET   INPUT,(R9)              GET INPUT RECORD
         AP    TRECDS(9),=P'1'         ADD 1 TO TOTAL RECORDS READ TJ#4
         LH    R15,LENGTH              LOAD LENGTH OF LINE
         SH    R15,HALF6               DECREMENT FOR EXECUTE
         EX    R15,FOLDLINE            EXECUTE FOLD OF LINE
         LH    R15,LENGTH              LOAD LENGTH OF LINE         TJ#7
         C     R15,=F'4'               TEST FOR LENGTH OF 4        TJ#7
         BH    FLDVP                   IF GREATER THAN 4 BRANCH    TJ#7
         MVC   0(5,R9),HBLNK           MOVE IN BLANK LINE          TJ#7
FLDVP    BAL   R3,PTLINE               BAL TO PRINT OUT THE LINE   TJ#2
         B     FOLDV                   GO GET NEXT RECORD
*
FOLDVNO  DS    0H
         GET   INPUT,(R9)              GET INPUT RECORD
         AP    TRECDS(9),=P'1'         ADD 1 TO TOTAL RECORDS READ TJ#4
         SLR   R15,R15                 ZERO REGISTER 15 FOR INSERT
         ICM   R15,7,LENGTH+1          INSERT LENGTH AND RESERVED BYTE
         A     R15,FULL256             ADD 1 TO LENGTH
         STCM  R15,7,LENGTH            STORE LENGTH AND RESERVED BYTE
         MVC   CNTLBYTE,HDRCNTL        MOVE IN CONTROL SAVED IN HEADER
         LH    R15,LENGTH              LOAD LENGTH OF LINE
         SH    R15,HALF6               DECREMENT FOR EXECUTE
         EX    R15,FOLDLINE            EXECUTE FOLD OF LINE
         BAL   R3,PTLINE               BAL TO PRINT OUT THE LINE   TJ#2
         B     FOLDVNO                 GO GET NEXT INPUT RECORD
*
FOLDF    DS    0H
         LH    R4,LENGTH               LOAD LENGTH INTO REGISTER 4
         SH    R4,HALF6                SUBTRACT TO SET UP FOR EXECUTE
FOLDFLP  DS    0H
         GET   INPUT,(R9)              GET INPUT RECORD
         AP    TRECDS(9),=P'1'         ADD 1 TO TOTAL RECORDS READ TJ#4
         EX    R4,FOLDLINE             FOLD DATA
         BAL   R3,PTLINE               BAL TO PRINT OUT THE LINE   TJ#2
         B     FOLDFLP                 GO GET NEXT RECORD
FOLDLINE OC    DATA(0),BLANKS          OC INSTRUCTION TO BE EXECUTED
*
PTLINE   DS    0H           SUBROUTINE TO PUT LINE                 TJ#2
         CP    STARTP,=P'0'  IS THERE A START POINT?               TJ#4
         BE    CHKSTOP       NO, CHECK FOR STOP                    TJ#4
         CP    TRECDS,STARTP START RECORD PASSED?                  TJ#4
         BL    PTLINEND      NO, DON'T PRINT LINE                  TJ#4
CHKSTOP  CP    STOPP,=P'0'   IS THERE A STOP POINT?                TJ#4
         BE    CHKCOUNT      NO, CHECK FOR COUNT                   TJ#4
         CP    TRECDS,STOPP  STOP RECORD PASSED?                   TJ#4
         BH    PTLINEND      YES, DON'T PRINT LINE                 TJ#4
CHKCOUNT CP    COUNTP,=P'0'  IS THERE A RECORD COUNT?              TJ#4
         BE    ENDCHCK       NO, SKIP COUNT CHECK                  TJ#4
         CP    TLINES,COUNTP HAVE ALL LINES PRINTED?               TJ#4
         BNL   PTLINEND      YES, DON'T PRINT ANY MORE             TJ#4
ENDCHCK  DS    0H                                                  TJ#4
         CLI   CCFLAG,C'Y'             CHECK FOR DATASET WITH CC   TJ#2
         BE    SKPLNCK                 THERE IS CC, BRANCH TO PUT  TJ#2
         CLI   FIRSTFL,C'F'            CHECK FOR FIRST PRINT       TJ#2
         BNE   MVCNTL1                 IF NOT FIRST, MOVE IN CNTL  TJ#2
         MVI   FIRSTFL,C'X'            CLEAR FIRST PRINT FLAG      TJ#2
         MVC   CNTLBYTE,CCASATOP       ASSUME ASA CNTL             TJ#2
         CLI   CCTYPE,C'M'             SHOULD IT BE MCH TYPE?      TJ#2
         BNE   SKCNTL1                 NO, BRANCH TO CONTINUE      TJ#2
         MVC   CNTLBYTE,CCMCHTOP       MOVE IN MCH CNTL            TJ#2
         B     SKCNTL1                 BRANCH TO CONTINUE          TJ#2
MVCNTL1  DS    0H                                                  TJ#2
         MVC   CNTLBYTE,HDRCNTL        MOVE IN CNTL SAVED IN HEAD  TJ#2
SKCNTL1  DS    0H                                                  TJ#2
*                                                                  TJ#2
         CLI   CCFLAG,C'Y'             CHECK FOR DATASET WITH CC   TJ#2
         BE    SKPLNCK                 THERE IS CC, SKIP LINE CHCK TJ#2
         CP    NLINES,=P'0'            ARE LINES TO BE CHECKED?    TJ#2
         BE    SKPLNCK                 NO, SKIP LINE CHCK          TJ#2
         CP    HLINES,NLINES           COMPARE LINE COUNT TO LINES TJ#2
         BL    SKPLNCK                 LIMIT NOT EXCEEDED, CONT    TJ#2
         ZAP   HLINES(9),=P'0'         ZERO LINE COUNT             TJ#2
         MVC   CNTLBYTE,CCASATOP       ASSUME ASA CNTL             TJ#2
         CLI   CCTYPE,C'M'             SHOULD IT BE MCH TYPE?      TJ#2
         BNE   SKPLNCK                 NO, BRANCH TO CONTINUE      TJ#2
         MVC   CNTLBYTE,CCMCHTOP       MOVE IN MCH CNTL            TJ#2
SKPLNCK  DS    0H                                                  TJ#2
         CLI   REPCC,C'N'              CHECK FOR REPLACEMENT CC    TJ#6
         BE    LINEPRNT                BRACH TO PRINT THE LINE     TJ#6
         MVC   CNTLBYTE(1),REPCC       MOVE IN REPLACEMENT CC      TJ#6
LINEPRNT DS    0H                                                  TJ#6
         PUT   (R5),LINE    PUT OUT THE OUTPUT LINE                TJ#2
         AP    HLINES(9),=P'1'         ADD 1 TO NUMBER LINES PRINT TJ#2
         AP    TLINES(9),=P'1'         ADD 1 TO TOTAL  LINES PRINT TJ#4
PTLINEND BR    R3           LINE PRINTED, RETURN                   TJ#2
*                                                                  TJ#2
EXIT     DS    0H
         AP    GTLINES(9),TLINES       ADD UP FOR GRAND TOTAL      TJ#8
         ZAP   TLINES(9),=P'0'         ZERO TOTAL LINES PRINTED    TJ#8
         TM    DCBOFLGS,DCBOFOPN       SEE IF INPUT DCB IS OPEN
         BNO   EXITMSG                 NO....GO PUT OUT DATASET AND
*                                      MEMBER NAMES
         MVC   CLOSLST,OPENCLOS        MOVE OPEN(CLOSE) LIST CONSTANT
*                                      INTO CLOSE LIST IN WORKAREA
         CLOSE ((R8),),MF=(E,CLOSLST)  CLOSE INPUT DCB
EXITMSG  DS    0H
         PUTLINE OUTPUT=(DSNMSG,SINGLE,DATA),MF=(E,IOPLSECT)  PUT OUT
*                                      DATA SET NAME MESSAGE
         LH    R4,DFPBDSL              LOAD LENGTH OF DSNAME   GTE
         A     R4,FULL4                INCREMENT LENGTH FOR PUTLINE
         SLL   R4,16                   SHIFT LENGTH TO HIGH ORDER BYTES
         STCM  R4,15,DSNLENP           STORE LENGTH OF PUTLINE
         PUTLINE OUTPUT=(DSNLENP,SINGLE,DATA),MF=(E,IOPLSECT)  PUT OUT
*                                      DATA SET NAME MESSAGE
         TM    14(R6),HIGH             SEE IF MEMBER NAME IS PRESENT
         BNO   CONTINUE                NO.....GO CHECK FOR ANOTHER
*                                      DATASET NAME
         PUTLINE OUTPUT=(MEMMSG,SINGLE,DATA),MF=(E,IOPLSECT)  PUT OUT
*                                      DATA SET NAME MESSAGE
         LH    R4,12(R6)               LOAD LENGTH OF MEMBER NAME
         A     R4,FULL4                INCREMENT LENGTH FOR PUTLINE
         SLL   R4,16                   SHIFT LENGTH TO HIGH ORDER BYTES
         STCM  R4,15,MEMNAMEL          STORE LENGTH OF PUTLINE
         PUTLINE OUTPUT=(MEMNAMEL,SINGLE,DATA),MF=(E,IOPLSECT)  PUT OUT
*                                      MEMBER NAME
         B     CONTINUE                GO CONTINUE WITH NEXT DATASET
         DROP  R8                      DROP ADDRESSABILITY TO INPUT DCB
ERRORL   DS    0H
         PUTLINE OUTPUT=(DSNISL,SINGLE,DATA),MF=(E,IOPLSECT)  PUT OUT
*                                      DATASET NOT PRINTED DUE TO
*                                      RECORD LENGTH TOO LONG MESSAGE
         B     EXIT                    GO SEE IF INPUT DCB IS OPEN
ERRORU   DS    0H
         PUTLINE OUTPUT=(DSNISU,SINGLE,DATA),MF=(E,IOPLSECT)  PUT OUT
*                                      DATASET NOT PRINTED DUE TO
*                                      UNDEFINED RECORD FORMAT MESSAGE
         B     EXIT                    GO SEE IF INPUT DCB IS OPEN
ERRI     DS    0H
         PUTLINE OUTPUT=(SYNADI,SINGLE,DATA),MF=(E,IOPLSECT)  PUT OUT
*                                      DATASET NOT PRINTED DUE TO SYNAD
*                                      ERROR ON INPUT DATASET MESSAGE
         B     EXIT                    GO SEE IF INPUT DCB IS OPEN
ERRO     DS    0H
         PUTLINE OUTPUT=(SYNADO,SINGLE,DATA),MF=(E,IOPLSECT)  PUT OUT
*                                      DATASET NOT PRINTED DUE TO
*                                      UNDEFINED RECORD FORMAT MESSAGE
         B     CLOSDCBS                GO CLOSE OPEN DCBS AND RETURN
*                                                                     *
PDSKWERR DS    0H                                                  TJ#1
         PUTLINE OUTPUT=(PDSKWE,SINGLE,DATA),MF=(E,IOPLSECT)       TJ#1
*                     PUT OUT DATASET NOT PRINTED DUE TO           TJ#1
*                             PO FORMAT AND PDS NOT SPECIFIED      TJ#1
         B     CLOSDCBS       GO CLOSE OPEN DCBS AND RETURN        TJ#1
*                                                                     *
BADLINS  DS    0H                                                  TJ#2
         PUTLINE OUTPUT=(BADLNE,SINGLE,DATA),MF=(E,IOPLSECT)       TJ#2
*                     PUT OUT DATASET NOT PRINTED DUE TO           TJ#2
*                             DAB NUMBER OF LINES SPECIFIED        TJ#2
         B     EXIT           GO TO SEE IF INPUT DCB OPEN          TJ#2
*                                                                     *
***********************************************************************
*                                                                     *
*        READ THE DIRECTORY OF A PDS                                  *
*           THEN EITHER LIST MEMBER NAMES,                            *
*           PRINT MEMBERS,                                            *
*           OR LIST MEMBER NAMES AND PRINT MEMBERS                    *
***********************************************************************
*
SCROLLIT DS    0H
         MVI   NODELTF,C'N'            SET DELETE FLAG TO NO       TJ#1
         ST    R6,SAVER6               SAVE REGISTER 6
         L     R9,PARSBACK             LOAD ADDRESS PARS ANSWER
         USING IKJPARMD,R9             ESTABLISH ADDRESSABILITY TO PDL
         CLC   PPRINT(2),HALF2           WAS  NOPRINT SPECIFIED? TJ#3
         BE    SKPPDS                   YES, SKIP PPDS CHECK       TJ#3
         CLC   PPDS(2),HALF1           WAS  PDS   SPECIFIED?       TJ#1
         BNE   PDSKWERR                IF PDS NOT SPECIFIED ERROR  TJ#1
SKPPDS   CLC   PLIST,HALF2             SEE IF NOLIST SPECIFIED
         BL    CONTSCR                 LOW....CONTINUE WITH SCROLL
         CLC   PPRINT,HALF2            SEE IF NOPRINT SPECIFIED
         BE    CONTINUE                YES....SKIP SCROLL
         DROP  R9                      DROP ADDRESSABILITY TO PDL
CONTSCR  DS    0H
         LA    R8,DIRECT               LOAD POINTER TO DIRECTORY DCB
         USING IHADCB,R8               ESTABLISH ADDRESSABILITY TO DCB
         MVC   DIRECT(DIRECTL),DIRECTC MOVE DIRECTORY DCB CONSTANT INTO
*                                      DIRECTORY DCB IN WORKAREA
         MVC   OPENLST,OPENCLOS        MOVE OPEN(CLOSE) LIST CONSTANT
*                                      INTO OPEN LIST IN WORKAREA
         OPEN  ((R8),INPUT),MF=(E,OPENLST)  OPEN DIRECTORY DCB
         TM    DCBOFLGS,DCBOFOPN       SEE IF DCB OPENED SUCCESSFULLY
         BNO   NOTPMESS                NO.....GO PUT OUT NOT PRINTED
*                                      MESSAGE
         DROP  R8                      DROP ADDRESSABILITY TO DCB
*                                                                     *
***********************************************************************
*                                                                     *
*        GETMAIN MEMBER TABLE FOR STORAGE OF DIRECTORY NAMES, ETC.    *
*           INITIALIZE TABLE                                          *
*                                                                     *
***********************************************************************
*
         L     R0,MEMDATL              LOAD LENGTH OF GETMAIN FOR
*                                      MEMBER TABLE
         LR    R2,R0                   SAVE IN REGISTER 2
         GETMAIN R,LV=(0)              GETMAIN MEMBER TABLE STORAGE
         ST    R1,MEMTABLE             SAVE ADDRESS OF MEMBER TABLE
         ST    R1,PRESENT              ALSO SAVE ADDRESS AS TABLE
*                                      IN USE
         LA    R3,0(R2,R1)             LOAD ADDRESS OF END OF TABLE
         ST    R3,ENDTABLE             STORE ADDRESS OF TABLE END
         SLR   R4,R4                   ZERO REGISTER 4
         ST    R4,0(R1)                STORE AS FIRST WORD OF MEMBER
*                                      TABLE
         LA    R4,8(R1)                LOAD REGISTER 4 AS POINTER TO
*                                      THE FIRST ENTRY
         USING MEMDSECT,R4             ESTABLISH ADDRESSABILITY TO
*                                      MEMBER TABLE DSECT
         SLR   R5,R5                   INITIALIZE MEMBER COUNT TO ZERO
*                                                                     *
***********************************************************************
*                                                                     *
*        READ DIRECTORY AND BUILD MEMBER TABLE                        *
*                                                                     *
***********************************************************************
*
GETDIR   EQU   *
         GET   (R8),DATA               GET A DIRECTORY BLOCK
         CLC   DATA,HALF12             SEE IF BLOCK IS UNUSED
         BL    FREEBLK                 YES....GO HANDLE DIRECTORY END
         LA    R9,DATA+2               LOAD POINTER TO FIRST DIRECTORY
*                                      ENTRY
         SLR   R7,R7                   ZERO REGISTER 7 FOR INSERT
         ICM   R7,3,DATA               INSERT LENGTH USED OF THIS BLOCK
         LA    R7,DATA-1(R7)           LOAD LAST USED BYTE OF BLOCK
UNBLOCK  DS    0H
         MVC   MEMNTTR,0(R9)           PUT NAME AND TTR IN TABLE
         MVC   CFIELD,11(R9)           PUT C FIELD IN TABLE
         CLI   MEMBER,HEXFF            SEE IF LAST MEMBER
         BE    FREEBLK                 YES....GO HANDLE DIRECTORY END
         IC    R6,CFIELD               INSERT CFIELD INTO REGISTER 6
         SLL   R6,27                   SHIFT OUT HIGH ORDER BITS
         SRL   R6,26                   SHIFT RIGHT TO GET LENGTH OF
*                                      USER DATA
         LA    R6,12(R6)               ADD STANDARD ENTRY LENGTH
         LA    R4,MEMSECTN             LOAD PINTER TO NEXT TABLE ENTRY
         LA    R5,1(R5)                INCREMENT NUMBER OF MEMBERS IN
*                                      TABLE
         C     R4,ENDTABLE             SEE IF END OF TABLE
         BL    NEXTNTRY                LOW.....GO PROCESS NEXT ENTRY
         LR    R0,R2                   LOAD LENGTH OF TABLE FOR GETMAIN
         GETMAIN R,LV=(0)              GET NEXT TABLE
         L     R14,PRESENT             LOAD REGISTER 14 WITH ADDRESS OF
*                                      LAST TABLE
         ST    R1,0(R14)               CHAIN MEMBER TABLES TOGETHER
         ST    R1,PRESENT              ALSO SAVE ADDRESS AS TABLE
*                                      IN USE
         LA    R3,0(R2,R1)             LOAD ADDRESS OF TABLE END
         ST    R3,ENDTABLE             STORE ADDRESS OF TABLE END
         SLR   R4,R4                   ZERO REGISTER 4
         ST    R4,0(R1)                STORE AS FIRST WORD OF MEMBER
*                                      TABLE
         LA    R4,8(R1)                LOAD REGISTER 4 AS POINTER TO
*                                      THE FIRST ENTRY
         STH   R5,4(R14)               STORE MEMBER COUNT IN OLD TABLE
         SLR   R5,R5                   INITIALIZE MEMBER COUNT TO ZERO
NEXTNTRY DS    0H
         BXLE  R9,R6,UNBLOCK           INCREMENT REGISTER 9 WITH
*                                      REGISTER 6 AND BRANCH
*                                      LESS THAN OR EQUAL TO
*                                      REGISTER 7 TO UNBLOCK
         B     GETDIR                  NO.....GO GET NEXT DIRECTORY
*                                      BLOCK
         DROP  R4                      DROP ADDRESSABILITY TO TABLE
*
***********************************************************************
*                                                                     *
*        LIST MEMBERS IF:                                             *
*           LIST SPECIFIED OR                                         *
*           BOTH PRINT AND NOLIST WERE NOT SPECIFIED                  *
*                                                                     *
***********************************************************************
*
FREEBLK  DS    0H
         MVC   OPENLST,OPENCLOS        MOVE CONSTANT INTO CLOSE LIST
         CLOSE ((R8),),MF=(E,OPENLST)  CLOSE DIRECTORY DCB
         L     R2,PRESENT              LOAD POINTER TO LAST TABLE
         STH   R5,4(R2)                STORE COUNT OF MEMBERS
         L     R4,MEMTABLE             LOAD POINTER TO FIRST TABLE
         CLC   4(2,R4),HALF0           SEE IF THERE WERE ANY MEMBERS
         BE    ENDMEMB                 ZERO...GO FREE MEMBER TABLE
         LA    R8,INPUT                LOAD POINTER TO INPUT DCB
         USING IHADCB,R8               ESTABLISH ADDRESSABILITY TO DCB
         MVC   INPUT(INPUTL),INPUTC    MOVE INPUT DCB CONSTANT INTO
*                                      INPUT DCB IN WORKAREA
         MVC   OPENLST,OPENCLOS        MOVE OPEN(CLOSE) LIST CONSTANT
*                                      INTO OPEN LIST IN WORKAREA
         LA    R1,XLIST                LOAD ADDRESS OF EXIT LIST
         ST    R1,DCBEXLST             STORE ADDRESS OF EXIT LIST IN
*                                      DCB
         LA    R1,SEXIT                LOAD ADDRESS OF EODAD EXIT
         ST    R1,DCBEODAD             STORE ADDRESS EODAD IN DCB
         LA    R2,JFCBAREA             LOAD ADDRESS OF JFCBAREA
         ST    R2,XLIST                STORE ADDRESS IN EXIT LIST
         MVI   XLIST,X'87'             SET END OF LIST AND JFCB BYTE
         MVC   RDJL,OPENCLOS           MOVE OPEN(CLOSE) LIST CONSTANT
*                                      INTO READJFCB LIST IN
*                                      WORKAREA
         RDJFCB (INPUT,),MF=(E,RDJL)   READ JFCB INTO JFCBAREA
         MVC   JFCBELNM,8(R4)          MOVE MEMBER NAME INTO JFCB
         OI    JFCBIND1,JFCPDS         SET JFCB TO INDICATE PDS MEMBER
         MVI   JFCDSRG1,HEX00          SET JFCB TO REMOVE OTHER DSORG
*                                      INDICATORS
         OPEN  ((R8),INPUT),TYPE=J,MF=(E,OPENLST)  OPEN INPUT DCB
         L     R9,PARSBACK             LOAD ADDRESS PARS ANSWER
         USING IKJPARMD,R9             ESTABLISH ADDRESSABILITY TO PDL
         CLC   PLIST,HALF2             SEE IF NOLIST SPECIFIED
         BE    NOLIST                  YES....SKIP LISTING
         CLC   PLIST,HALF1             SEE IF LIST SPECIFIED
         BE    LISTIT                  YES....GO LISTIT
         CLC   PPRINT,HALF1            SEE IF PRINT SPECIFIED
         BE    NOLIST                  YES....SKIP LISTING
LISTIT   DS    0H
         MVC   HEADER,HEADERM          MOVE HEADER INTO DATA AREA
         MVC   DPIDOUT+4(8),DPID        MOVE IN DPID               TJ#3
         CLC   PTITLE(2),HALF1    DID THE USER SPECIFY A TITLE     TJ#3
         BNE   CHKCCTL2      NO...                                 TJ#3
         MVC   HDRCNTL+1(40),BLANKS   SPACE OUT HEADER             TJ#3
         L     R3,TITLEFLD            LOAD POINTER TO TITLE        TJ#3
         LH    R4,TITLEFLD+4          LOAD LENGTH OF TITLE         TJ#3
         BCTR  R4,R0                  DECREMENT FOR EXECUTE        TJ#3
         EX    R4,MOVETTL2            MOVE IN USER TITLE           TJ#3
         B     CHKCCTL2               BRANCH TO CHECK CCTLKEY      TJ#3
MOVETTL2 MVC   HDRCNTL+1(0),0(R3)     MOVE IN USER TITLE           TJ#3
CHKCCTL2 CLC   CCTLKEY,HALF0   DID USER SPECIFY CCTL OVERRIDE      TF#1
         BE    LISTIT1       NO...                                 TF#1
         CLC   CCTLKEY,HALF1   DID USER SPECIFY NOCCTL             TF#1
         BE    SFORCNOC      YES                                   TF#1
         CLC   CTYPKEY,HALF1 MUST HAVE SPECIFIED CCTL-WAS IT ASA?  TF#1
         BE    SFORCASA      YES                                   TF#1
         B     SFORCMCH      IT HAD TO BE MCH                      TF#1
LISTIT1  DS    0H                                                  TF#1
         TM    DCBRECFM,DCBRECCM       SEE IF CONTROL IS MACHINE
         DROP  R8                      DROP ADDRESSABILITY TO DCB
         BNO   SUSEA                   NO.....GO TO USE ASA CONTROL
SFORCMCH DS    0H                      BRANCH HERE TO FORCE MCH    TF#2
         LA    R5,OUTPUTM              LOAD ADDRESS OF OUTPUT DCB FOR
*                                      MACHINE CODE
         LA    R4,EJECTM               LOAD ADDRESS OF EJECT DATA
         MVI   HDRCNTL,SKIPM           MOVE INTO HEADER MACHINE CODE
*                                      FOR SPACE
         TM    48(R5),DCBOFOPN         SEE IF DCB IS ALREADY OPEN
         BO    SPUTDSN                 YES....GO PUT OUT HEADERS
         LA    R3,PRINTM               LOAD POINTER TO PRINTM
         LA    R14,SPUTDSN             LOAD ADDRESS FOR RETURN FROM
*                                      SYSOUT ALLOCATION
         B     ALLOCOUT                GO TO ALLOCATE SYSOUT
SUSEA    DS    0H
SFORCASA DS    0H                      BRANCH HERE TO FORCE ASA    TF#2
SFORCNOC DS    0H                      BRANCH HERE TO FORCE NO CC  TF#2
         LA    R5,OUTPUTA              LOAD ADDRESS OF OUTPUT DCB FOR
*                                      ASA CODE
         LA    R4,EJECTA               LOAD ADDRESS OF EJECT DATA
         TM    48(R5),DCBOFOPN         SEE IF DCB IS ALREADY OPEN
         BO    SPUTDSN                 YES....GO PUT OUT HEADERS
         LA    R3,PRINTA               LOAD POINTER TO PRINTA
         BAL   R14,ALLOCOUT            LOAD ADDRESS FOR RETURN AND GO
*                                      DO SYSOUT ALLOCATION
SPUTDSN  DS    0H
         CLI   EJECTFL,C'E'            CHECK FOR EJECT             TJ#1
         BE    EJECT2                  IF SET BRANCH TO EJECT      TJ#1
         CLI   EJECTFL,C'F'            CHECK FOR FIRST EJECT       TJ#1
         BNE   CHKEJCT2                IF NOT FIRST THEN BRANCH    TJ#1
         MVI   EJECTFL,C'X'            CLEAR FIRST EJECT FLAG      TJ#1
         B     SKPEJCT2                ALWAYS SKIP FIRST EJECT     TJ#1
CHKEJCT2 DS    0H                                                  TJ#1
         L     R7,EJECTNM              LOAD NUMBER OF SEP LINES    TJ#1
         LTR   R7,R7                   IS THE NUMBER ZERO?         TJ#1
         BZ    SKPEJCT2                BRANCH IF NO SEP LINES      TJ#1
         LA    R4,BLNKASA              ASSUME ASA                  TJ#1
         CLI   CCTYPE,C'M'             IS TYPE MCH?                TJ#1
         BNE   SEPLN2                  NO, BRANCH TO PUT SEP LINES TJ#1
         LA    R4,BLNKMCH              SET UP BLANK MCH LINE       TJ#1
SEPLN2   DS    0H                                                  TJ#1
         PUT   (R5),(R4)               PUT OUT BLANK LINE          TJ#1
         AP    GTLINES(9),=P'1'        ADD 1 TO GRAND TOTAL        TJ#8
         AP    HLINES(9),=P'1'         ADD 1 TO NUMBER LINES PRINT TJ#2
         BCT   R7,SEPLN2               BRANCH IF NOT ZERO          TJ#1
         MVI   FIRSTFL,C'X'            CLEAR FIRST PRINT FLAG      TJ#1
         B     SKPEJCT2                       BRANCH AROUND EJECT  TJ#1
EJECT2   DS    0H                                                  TJ#1
         CLI   FIRSTFL,C'F'            CHECK FOR FIRST PRINT       TJ#8
         BNE   EJECTIT2                BRANCH TO EJECT IF NOT      TJ#8
         L     R7,PARSBACK             LOAD ADDRESS TO PARMS       TJ#8
         USING IKJPARMD,R7        ESTABLISH ADDRESSABILITY TO PDL  TJ#8
         CLC   PARR(2),HALF1           CHECK FOR   ARR             TJ#8
         BE    SKPEJCT2               ARR, BRANCH TO SKIP EJECT    TJ#8
         DROP  R7                                                  TJ#8
EJECTIT2 DS    0H                                                  TJ#8
         PUT   (R5),(R4)               PUT OUT PAGE EJECT
         AP    GTLINES(9),=P'1'        ADD 1 TO GRAND TOTAL        TJ#8
         ZAP   HLINES(9),=P'0'         ZERO LINES COUNT            TJ#2
         MVI   FIRSTFL,C'X'            CLEAR FIRST PRINT FLAG      TJ#1
SKPEJCT2 DS    0H                                                  TJ#1
         CLI   FIRSTFL,C'F'            CHECK FOR FIRST PRINT       TJ#8
         BNE   CHCKHEA2                BRANCH TO CHECK FOR HEADER  TJ#8
         CLC   PARR(2),HALF1           CHECK FOR   ARR             TJ#8
         BNE   CHCKHEA2            NO ARR, BRANCH TO CHECK HEADER  TJ#8
         MVI   FIRSTFL,C'X'            CLEAR FIRST PRINT FLAG      TJ#8
         MVC   ARRPRTLN(ARRPL),ARRPRTR                             TJ#8
         L     R15,ARRNUM              LOAD ADDRESS OF TYPE        TJ#8
         MVC   ARRPRTN(3),0(R15)       MOVE IN ARRANGMENT NUMBER   TJ#8
         MVC   ARRCC,CCASATOP          ASSUME ASA CNTL             TJ#8
         CLI   CCTYPE,C'M'             SHOULD IT BE MCH TYPE?      TJ#8
         BNE   MVPRINT2                NO, BRANCH TO CONTINUE      TJ#8
         MVC   ARRCC,CCMCHTOP          MOVE IN MCH CNTL            TJ#8
MVPRINT2 DS    0H                                                  TJ#8
         MVC   ARRPRT,=C',PRINT  '     MOVE IN ,PRINT              TJ#8
         PUT   (R5),ARRPRTLN           PUT OUT ARRANGEMENT LINE    TJ#8
         AP    GTLINES(9),=P'1'        ADD 1 TO GRAND TOTAL        TJ#8
CHCKDPL2 DS    0H                                                  TJ#8
         CLC   PDUPLEX(2),HALF1        CHECK FOR DUPLEX            TJ#8
         BNE   CKARCOP2                NO DUPLEX, CHECK COPIES     TJ#8
         MVC   ARRPRT(80),ARRDUP       MOVE IN DUPLEX              TJ#8
         PUT   (R5),ARRPRTLN           PUT OUT DUPLEX LINE         TJ#8
         AP    GTLINES(9),=P'1'        ADD 1 TO GRAND TOTAL        TJ#8
CKARCOP2 DS    0H                                                  TJ#8
         AIF   (&LGENOPT EQ 0).CKANM2  IF PARTIAL GEN OPT        JM#14
         CLC   PCOPIES(2),HALF1        SEE IF COPY SPECIFIED       TJ#8
         BNE   CHCKANU2                NO.....GO CHECK NUMBER      TJ#8
         MVC   ARRPRT,=C'QUA     '     MOVE IN  QUA                TJ#8
         MVC   ARRPRTN(3),=C'   '      BLANK OUT COPIES AREA       TJ#8
         L     R15,SCOPIES             LOAD ADDRESS OF COPIES      TJ#8
         LH    R1,SCOPIES+4            LOAD LENGTH OF COPY FIELD   TJ#8
         BCTR  R1,0                    DECREMENT REGISTER 1 FOR EX TJ#8
*                                      OF MOVE INSTRUCTION         TJ#8
         EX    R1,MOVECOP2             EXECUTE MOVE TO MOVE COPIES TJ#8
         PUT   (R5),ARRPRTLN           PUT OUT QUANITY LINE        TJ#8
         AP    GTLINES(9),=P'1'        ADD 1 TO GRAND TOTAL        TJ#8
         B     CHCKANU2                                            TJ#8
MOVECOP2 MVC   ARRPRTN(0),0(R15)       MOVE IN COPIES              TJ#8
.CKANM2  ANOP                                                    JM#14
CHCKANU2 DS    0H                                                  TJ#8
         CLC   PNUMBER(2),HALF1        CHECK FOR NUMBER            TJ#8
         BNE   SETUPEN2                NO NUMBER, GO SET UP END    TJ#8
         MVC   ARRPRT(80),ARRNUM1      PUT OUT ,MARGIN TEXT        TJ#8
         PUT   (R5),ARRPRTLN           PUT OUT NUMBER LINE         TJ#8
         AP    GTLINES(9),=P'1'        ADD 1 TO GRAND TOTAL        TJ#8
         MVC   ARRPRT(80),ARRNUM2      PUT OUT ,MARGIN TEXT        TJ#8
         PUT   (R5),ARRPRTLN           PUT OUT NUMBER LINE         TJ#8
         AP    GTLINES(9),=P'1'        ADD 1 TO GRAND TOTAL        TJ#8
SETUPEN2 MVC   ARRPRT,=C',END    '     MOVE IN ,END                TJ#8
         MVC   ARRPRTN(3),=C'   '      SPACE OUT ARR NUM AREA      TJ#8
         MVC   ARRCC,HDRCNTL           MOVE IN CNTL SAVED IN HEAD  TJ#8
         PUT   (R5),ARRPRTLN           PUT OUT ARRANGEMENT LINE    TJ#8
         AP    GTLINES(9),=P'1'        ADD 1 TO GRAND TOTAL        TJ#8
CHCKEXP2 DS    0H                                                  TJ#8
         CLC   PEXPRES(2),HALF1        CHECK FOR EXPRESS           TJ#8
         BNE   CHCKHEA2                NO EXPRESS, GO CHECK HEAD   TJ#8
         MVC   ARRPRT(80),EXPRESS      MOVE IN EXPRESS LINE        TJ#8
         PUT   (R5),ARRPRTLN           PUT OUT EXPRESS LINE        TJ#8
         AP    GTLINES(9),=P'1'        ADD 1 TO GRAND TOTAL        TJ#8
         DROP  R9                      DROP ADDRESSABILITY TO PDL
CHCKHEA2 TM    VOLBIT,X'08'            TEST FOR  HEAD              TJ#8
         BO    SKPHEAD2      YES                                   TJ#8
         PUT   (R5),HEADER             PUT OUT PAGE 1 HEADER
         AP    GTLINES(9),=P'1'        ADD 1 TO GRAND TOTAL        TJ#8
         AP    HLINES(9),=P'1'         ADD 1 TO NUMBER LINES PRINT TJ#2
         MVC   HEADER2,LDSNAME         MOVE LENGTH AND 'DSNAME=' INTO
*                                      INTO SECOND HEADER
         MVC   HEADER2+4(1),HDRCNTL    MOVE IN CONTROL BYTE
         MVI   OPENPRN,BLANK           BLANK OUT MEMBER NAME
         MVC   MEMNAME(9),OPENPRN      FINISH BLANKING OUT MEMBER NAME
         PUT   (R5),HEADER2            PUT OUT PAGE 1 HEADER 2
         AP    GTLINES(9),=P'1'        ADD 1 TO GRAND TOTAL        TJ#8
         AP    HLINES(9),=P'1'         ADD 1 TO NUMBER LINES PRINT TJ#2
SKPHEAD2 DS    0H                                                  TJ#8
         L     R2,MEMTABLE             LOAD ADDRESS OF FIRST TABLE
         MVC   LENGTH,HALF85           SET LENGTH TO 85 BYTES
         MVC   LENGTH+2,HALF0          ZERO RESERVED BYTES
         MVC   CNTLBYTE,HDRCNTL        MOVE IN HEADER CONTROL BYTE
*
***********************************************************************
*                                                                     *
*        LIST MEMBER NAMES                                            *
*                                                                     *
***********************************************************************
*
SCROLLER DS    0H
         LA    R9,50                  LOAD LINE COUNT
         ST    R2,PRESENT              SAVE ADDRESS OF CURRENT TABLE
         LH    R7,4(R2)                LOAD NUMBER OF MEMBERS IN TABLE
         LTR   R7,R7                   SEE IF THERE ARE ENTRIES
         BZ    NOLIST                  NO.....GO PRINT MEMBERS
         LA    R2,8(R2)                LOAD ADDRESS OF FIRST MEMBER
         USING MEMDSECT,R2             ESTABLISH ADDRESSABILITY TO
*                                      MEMBER TABLE ENTRY
LOOP1    DS    0H
         LA    R6,DATA                 LOAD ADDRESS OF OUTPUT BUFFER
         MVI   DATA,BLANK              BLANK OUT OUTPUT BUFFER
         MVC   DATA+1(79),DATA         FINISH BLANKING BUFFER
         TM    CFIELD,HIGH             SEE IF THIS IS AN ALIAS
         BZ    NOTALIAS                NO.....CONTINUE WITH MAIN NAME
         BAL   R14,LOOKLOOP            GO LOOK FOR MAIN NAME
NOTALIAS DS    0H
         MVC   2(8,R6),MEMBER          PUT MEMBER NAME IN OUTPUT BUFFER
         ST    R2,SAVER2               SAVE REGISTER 2
         LA    R3,2                    LOAD NUMBER OF COLUMNS LEFT
         LA    R8,1                    LOAD ENTRY NUMBER
LOOP2    LA    R8,50(R8)               LOAD POINTER TO NEXT COLUMN
*                                      ENTRY
         CR    R8,R7                   SEE IF VALID ENTRY
         BH    END2                    NO.....GO PRINT LINE
         LA    R2,600(R2)              LOAD POINTER TO MEMBER NAME
         LA    R6,30(R6)               LOAD POINTER TO NEXT OUTPUT
*                                      BUFFER LOCATION
         TM    CFIELD,HIGH             SEE IF THIS IS AN ALIAS
         BZ    NOTAL2                  NO.....CONTINUE WITH MAIN NAME
         BAL   R14,LOOKLOOP            GO LOOK FOR MAIN NAME
NOTAL2   DS    0H
         MVC   2(8,R6),MEMBER          PUT MEMBER NAME IN OUTPUT BUFFER
         BCT   R3,LOOP2                DECREMENT COLUMN COUNT
END2     DS    0H
         PUT   (R5),LINE               PUT OUT OUTPUT LINE
         AP    TLINES(9),=P'1'         ADD 1 TO TOTAL  LINES PRINT TJ#8
         AP    HLINES(9),=P'1'         ADD 1 TO NUMBER LINES PRINT TJ#2
         L     R2,SAVER2               RELOAD REGISTER 2
         LA    R2,MEMSECTN             LOAD POINTER TO NEXT MEMBER NAME
         BCT   R9,MOREROW              DECREMENT ROW COUNT
         CLI   EJECTFL,C'E'            CHECK FOR EJECT             TJ#1
         BE    EJECT3                  IF SET BRANCH TO EJECT      TJ#1
         CLI   EJECTFL,C'F'            CHECK FOR FIRST EJECT       TJ#1
         BNE   CHKEJCT3                IF NOT FIRST THEN BRANCH    TJ#1
         MVI   EJECTFL,C'X'            CLEAR FIRST EJECT FLAG      TJ#1
         B     SKPEJCT3                ALWAYS SKIP FIRST EJECT     TJ#1
CHKEJCT3 DS    0H                                                  TJ#1
         L     R7,EJECTNM              LOAD NUMBER OF SEP LINES    TJ#1
         LTR   R7,R7                   IS THE NUMBER ZERO?         TJ#1
         BZ    SKPEJCT3                BRANCH IF NO SEP LINES      TJ#1
         LA    R4,BLNKASA              ASSUME ASA                  TJ#1
         CLI   CCTYPE,C'M'             IS TYPE MCH?                TJ#1
         BNE   SEPLN3                  NO, BRANCH TO PUT SEP LINES TJ#1
         LA    R4,BLNKMCH              SET UP BLANK MCH LINE       TJ#1
SEPLN3   DS    0H                                                  TJ#1
         PUT   (R5),(R4)               PUT OUT BLANK LINE          TJ#1
         AP    GTLINES(9),=P'1'        ADD 1 TO GRAND TOTAL        TJ#8
         AP    HLINES(9),=P'1'         ADD 1 TO NUMBER LINES PRINT TJ#2
         BCT   R7,SEPLN3               BRANCH IF NOT ZERO          TJ#1
         MVI   FIRSTFL,C'X'            CLEAR FIRST PRINT FLAG      TJ#1
         B     SKPEJCT3                       BRANCH AROUND EJECT  TJ#1
EJECT3   DS    0H                                                  TJ#1
         PUT   (R5),(R4)               PUT OUT PAGE EJECT WHEN ZERO
         AP    GTLINES(9),=P'1'        ADD 1 TO GRAND TOTAL        TJ#8
         ZAP   HLINES(9),=P'0'         ZERO HLINES                 TJ#2
         MVI   FIRSTFL,C'X'            CLEAR FIRST PRINT FLAG      TJ#1
SKPEJCT3 DS    0H                                                  TJ#1
         B     LOOP3                   RESET COUNTERS
MOREROW  DS    0H
         BCT   R7,LOOP1                DECREMENT MEMBER COUNT FOR
*                                      CURRENT TABLE
LOOP3    L     R2,PRESENT              LOAD POINTER TO CURRENT TABLE
         L     R2,0(R2)                LOAD POINTER TO NEXT TABLE
         LTR   R2,R2                   SEE IF IT WAS THE LAST TABLE
         BNZ   SCROLLER                NO.....GO LIST OUT NEXT TABLE
*
***********************************************************************
*                                                                     *
*        PRINT MEMBERS IF:                                            *
*           PRINT SPECIFIED OR                                        *
*           BOTH LIST AND NOPRINT WERE NOT SPECIFIED                  *
*                                                                     *
***********************************************************************
*
NOLIST   DS    0H
         AP    GTLINES(9),TLINES       ADD UP FOR GRAND TOTAL      TJ#8
         ZAP   TLINES(9),=P'0'         ZERO TOTAL LINES PRINTED    TJ#8
         L     R9,PARSBACK             LOAD ADDRESS PARS ANSWER
         USING IKJPARMD,R9             ESTABLISH ADDRESSABILITY TO PDL
         CLC   PPRINT,HALF2            SEE IF NOPRINT SPECIFIED
         BE    ENDMEMB                 YES....GO SKIP PRINT
         CLC   PPRINT,HALF1            SEE IF PRINT SPECIFIED
         BE    CONTPRT                 YES....GO PRINT MEMBERS
         CLC   PLIST,HALF1             SEE IF LIST SPECIFIED
         BE    ENDMEMB                 YES....GO SKIP PRINT
         DROP  R9                      DROP ADDRESSABILITY TO PDL
CONTPRT  DS    0H
         L     R6,SAVER6               RESTORE REGISTER 6
         LA    R8,INPUT                LOAD ADDRESS OF INPUT DCB
         USING IHADCB,R8               ESTABLISH ADDRESSABILITY TO DCB
         TM    DCBRECFM,DCBRECU        SEE IF RECFM IS UNDEFINED
         BO    ERRORU                  NO.....GO PUT OUT ERROR MESSAGE
         BZ    ERRORU                  ZERO...NOT FIXED OR VARIABLE SO
*                                      GO PUT OUT ERROR MESSAGE
         MVC   CLOSLST,OPENCLOS        MOVE OPEN(CLOSE) LIST CONSTANT
*                                      INTO CLOSE LIST IN WORKAREA
         CLOSE ((R8),),MF=(E,CLOSLST)  CLOSE INPUT DCB
         DROP  R8                      DROP ADDRESSABILITY TO INPUT DCB
         PUTLINE OUTPUT=(PRTMSG,SINGLE,DATA),MF=(E,IOPLSECT)  PUT OUT
*                                      DATASET PRINT MESSAGE
         LH    R4,4(R6)                LOAD LENGTH OF DSNAME
         A     R4,FULL4                INCREMENT LENGTH FOR PUTLINE
         SLL   R4,16                   SHIFT LENGTH TO HIGH ORDER BYTES
         STCM  R4,15,DSNLENP           STORE LENGTH OF PUTLINE
         PUTLINE OUTPUT=(DSNLENP,SINGLE,DATA),MF=(E,IOPLSECT)  PUT OUT
*                                      DATA SET NAME MESSAGE
         L     R2,MEMTABLE             LOAD ADDRESS OF FIRST MEMBER
*                                      TABLE
         LH    R3,4(R2)                LOAD NUMBER OF MEMBERS IN TABLE
         LTR   R3,R3                   SEE IF THERE ARE ANY MEMBERS
         BZ    ENDMEMB                 NO.....GO TO END OF SCROLLER
         ST    R2,PRESENT              SAVE ADDRESS OF CURRENT TABLE
         LA    R2,8(R2)                LOAD POINTER TO MEMBER NAME
NEXTMEMB DS    0H
         TM    CFIELD,HIGH             SEE IF THIS IS AN ALIAS ENTRY
         BO    AGAIN                   YES....DO NOT PRINT IT
PRINTMEM DS    0H
         MVC   MEMNAME,MEMBER          MOVE MEMBER NAME INTO HEADER
         MVC   JFCBELNM,MEMBER         MOVE MEMBER NAME INTO JFCBAREA
         MVC   OPENLST,OPENCLOS        MOVE CONSTANT INTO OPEN LIST
         OPEN  ((R8),INPUT),TYPE=J,MF=(E,OPENLST)
*                                      OPEN INPUT DCB
         STM   R2,R3,SAVER2            SAVE REGISTERS 2 AND 3
         B     PRINTS                  GO PRINT MEMBER
SEXIT    DS    0H
         AP    GTLINES(9),TLINES       ADD UP FOR GRAND TOTAL      TJ#8
         ZAP   TLINES(9),=P'0'         ZERO TOTAL LINES PRINTED    TJ#8
         LM    R2,R3,SAVER2            RESTORE REGISTERS 2 AND 3
         MVC   CLOSLST,OPENCLOS        MOVE OPEN(CLOSE) LIST CONSTANT
*                                      INTO CLOSE LIST IN WORKAREA
         CLOSE ((R8),),MF=(E,CLOSLST)  CLOSE INPUT DCB
AGAIN    DS    0H
         LA    R2,MEMSECTN             LOAD ADDRESS OF NEXT MEMBER
         BCT   R3,NEXTMEMB             BRANCH TO HANDLE NEXT MEMBER
         L     R14,PRESENT             ZERO...LOAD CURRENT TABLE
*                                      ADDRESS
         L     R2,0(R14)               LOAD ADDRESS OF NEXT TABLE
         LTR   R2,R2                   SEE IF ZERO, LAST TABLE
         BZ    ENDMEMB                 NO.....GO HANDLE END OF MEMBERS
         ST    R2,PRESENT              SAVE ADDRESS OF CURRENT TABLE
         LH    R3,4(R2)                LOAD NUMBER OF MEMBERS IN NEXT
*                                      TABLE
         LTR   R3,R3                   SEE IF ANY MEMBERS IN TABLE
         BZ    ENDMEMB                 NO.....GO HANDLE END OF MEMBERS
         LA    R2,8(R2)                LOAD POINTER TO MEMBER NAME
         B     NEXTMEMB                GO GET NEXT MEMBER
ENDMEMB  DS    0H
         L     R2,MEMTABLE             LOAD POINTER TO FIRST TABLE
FREETABS DS    0H
         LR    R1,R2                   LOAD AREA TO BE FREED
         L     R2,0(R2)                LOAD POINTER TO NEXT TABLE
         L     R0,MEMDATL              LOAD SIZE OF AREA TO BE FREED
         FREEMAIN R,LV=(0),A=(1)       FREE STORAGE GETMAINED FOR TABLE
         LTR   R2,R2                   SEE IF THERE WAS ANOTHER TABLE
         BNZ   FREETABS                YES....GO FREE NEXT TABLE
         L     R6,SAVER6               RESTORE REGISTER 6
         LA    R8,INPUT                LOAD POINTER TO INPUT DCB
         USING IHADCB,R8               ESTABLISH ADDRESSABILITY TO DCB
         TM    DCBOFLGS,DCBOFOPN       SEE IF INPUT DCB IS STILL OPEN
         BNO   EXITMSG                 NO.....GO HANDLE NEXT DATASET
         MVC   CLOSLST,OPENCLOS        MOVE OPEN(CLOSE) LIST CONSTANT
*                                      INTO CLOSE LIST IN WORKAREA
         CLOSE ((R8),),MF=(E,CLOSLST)  CLOSE INPUT DCB
         DROP  R8                      DROP ADDRESSABILITY TO DCB
         B     EXITMSG                 GO HANDLE NEXT DATASET
*                                                                     *
***********************************************************************
*                                                                     *
*        PUT OUT SYNAD ERROR MESSAGE,                                 *
*           GO FREE MEMBER TABLES, AND                                *
*           CONTINUE WITH NEXT DATASET                                *
*                                                                     *
***********************************************************************
*
SERRI    DS    0H
         PUTLINE OUTPUT=(SYNADI,SINGLE,DATA),MF=(E,IOPLSECT)  PUT OUT
*                                      DATASET NOT PRINTED DUE TO SYNAD
*                                      ERROR ON INPUT DATASET MESSAGE
         B     ENDMEMB                 GO FREE MEMBER TABLES
*                                                                     *
***********************************************************************
*                                                                     *
*        SEARCH FOR MAIN NAME,                                        *
*           SAME TTR AND NOT AN ALIAS                                 *
*                                                                     *
***********************************************************************
*
LOOKLOOP DS    0H
         STM   R3,R4,SAVER3            SAVE REGISTERS 3 AND 4
         L     R3,MEMTABLE             LOAD ADDRESS OF FIRST TABLE
         USING MEMDSECT,R3             ESTABLISH ADDRESSABILITY TO
*                                      MEMBER ENTRY
LOOP4    DS    0H
         ST    R3,CURRENT              SAVE ADDRESS OF PRESENT TABLE
         LH    R4,4(R3)                LOAD NUMBER OF ELEMENTS IN TABLE
         LTR   R4,R4                   SEE IF THERE ARE ZERO ELEMENTS
         BZ    NOMAIN                  YES....GO HANDLE MAIN NOT FOUND
         LA    R3,8(R3)                LOAD ADDRESS OF FIRST MEMBER
LOOP5    DS    0H
         CLC   MEMTTR,8(R2)            SEE IF TTRS ARE THE SAME
         BE    SAMETTR                 YES....GO SEE IF IT IS AN ALIAS
ENDLP5   DS    0H
         LA    R3,MEMSECTN             LOAD ADDRESS OF NEXT MEMBER
         BCT   R4,LOOP5                DECREMENT MEMBER COUNT
         L     R3,CURRENT              LOAD ADDRESS OF THIS TABLE
         L     R3,0(R3)                LOAD ADDRESS OF NEXT TABLE
         LTR   R3,R3                   SEE IF IT WAS THE LAST TABLE
         BNZ   LOOP4                   NO.....GO CHECK NEXT TABLE
         B     NOMAIN                  GO HANDLE MAIN NOT FOUND
SAMETTR  DS    0H
         TM    CFIELD,HIGH             SEE IF THIS IS ALSO AN ALIAS
         BO    ENDLP5                  YES....GO LOOK FOR ANOTHER TTR
         MVC   11(8,R6),MEMBER         MOVE MEMBER NAME INTO OUTPUT
         B     ENDLOOK                 GO TO RETURN TO CALLER
NOMAIN   DS    0H
         MVC   11(8,R6),ALIAS          INDICATE THIS IS AN ALIAS
ENDLOOK  DS    0H
         LM    R3,R4,SAVER3            RESTORE REGISTERS 3 AND 4
         BR    R14                     RETURN TO CALLER
         DROP  R3                      DROP ADDRESSABILITY TO TABLE
*
CONTINUE DS    0H
         L     R6,24(R6)               LOAD POINTER TO NEXT DSNAME ON
*                                      CHAIN
         C     R6,ENDCHAIN             SEE IF END OF CHAIN IS REACHED
         BNE   NEXT                    NO.....GO DO NEXT DATASET NAME
CLOSDCBS DS    0H
         TM    INPUT+48,DCBOFOPN       SEE IF INPUT DCB IS STILL OPEN
         BNO   CHKOUTA                 NO.....GO CHECK DCB FOR ASA CODE
         MVC   CLOSLST,OPENCLOS        MOVE OPEN(CLOSE) LIST CONSTANT
*                                      INTO CLOSE LIST WORKAREA
         LA    R5,INPUT                LOAD POINTER TO INPUT DCB
         CLOSE ((R5),),MF=(E,CLOSLST)  CLOSE INPUT DCB
CHKOUTA  DS    0H
         TM    OUTPUTA+48,DCBOFOPN     SEE IF OUTPUT DCB FOR ASA WAS
*                                      OPENED
         BNO   CHKOUTM                 NO.....GO CHECK DCB FOR MACHINE
*                                      CODE
         MVC   CLOSLST,OPENCLOS        MOVE OPEN(CLOSE) LIST CONSTANT
*                                      INTO CLOSE LIST WORKAREA
         LA    R5,OUTPUTA              LOAD POINTER TO ASA CODE DCB
         CLOSE ((R5),),MF=(E,CLOSLST)  CLOSE ASA CODE OUTPUT DCB
         LA    R1,PRINTA               LOAD ADDRESS OF PRINT DDNAME
         BAL   R2,FREEDD               GO FREE ASA CODE OUTPUT DDNAME
CHKOUTM  DS    0H
         TM    OUTPUTM+48,DCBOFOPN     SEE IF OUTPUT DCB FOR MACHINE
*                                      CODE WAS OPENED
         BNO   RETURN                  NO.....GO FREE GETMAINED AREAS
*                                      AND RETURN
         MVC   CLOSLST,OPENCLOS        MOVE OPEN(CLOSE) LIST CONSTANT
*                                      INTO CLOSE LIST WORKAREA
         LA    R5,OUTPUTM              LOAD POINTER TO MACHINE CODE DCB
         CLOSE ((R5),),MF=(E,CLOSLST)  CLOSE MACHINE CODE OUTPUT DCB
         LA    R1,PRINTM               LOAD ADDRESS OF PRINT DDNAME
         BAL   R2,FREEDD               GO FREE MACHINE CODE OUTPUT
*                                      DDNAME
*
***********************************************************************
*                                                                     *
*        CLEANUP AREAS GETMAINED BY PARS,                             *
*           SET RETURN CODE, RESTORE REGISTERS AND                    *
*           RETURN TO CALLER                                          *
*                                                                     *
***********************************************************************
*
RETURN   DS    0H
         AP    GTLINES,TLINES          ADD LEFT OVER LINES TO TOT  TJ#8
         MVC   GRANTOT(10),=X'40202020202020202120'     ED PATTERN TJ#8
         ED    GRANTOT,GTLINES+4         MOVE IN TOTAL LINES       TJ#8
         PUTLINE OUTPUT=(LINMSGH,SINGLE,DATA),MF=(E,IOPLSECT)      TJ#8
*                              PUT OUT TOTAL LINES PRINTED MESSAGE TJ#8
         LA    R1,=CL8'PRINTI'         SET UP DDNAME FOR FREEDD    TJ#1
         BAL   R2,FREEDD               BRANCH TO FREE PRINTI       TJ#1
         IKJRLSA PARSBACK              RELEASE AREAS GETMAINED BY PARS
         SPACE
FINISH   DS    0H                                                JM#14
         FINISH RC=0,GEN=GEN           EXIT THIS PLACE           JM#14
*
***********************************************************************
*                                                                     *
*        ROUTINE TO CONVERT NONZERO RETURN CODES TO DECIMAL           *
*           FOR DISPLAY                                               *
*                                                                     *
***********************************************************************
*
SETCODE  DS    0H
         CVD   R8,DEC                  CONVERT RETURN CODE TO DECIMAL
         UNPK  CONV(8),DEC(8)          UNPACK RETURN CODE
         MVZ   CONV+1(7),CONV          MOVE ZONES IN CONVERTED RETURN
*                                      CODE
         MVC   RETNCD(2),HALF12        MOVE LENGTH OF DATA FOR PUTLINE
         MVC   RETNCD+2(2),HALF0       MOVE IN ZEROES FOR PUTLINE
         BR    R14                     RETURN
*
***********************************************************************
*                                                                     *
*        FREE AND REALOCATE PRINTX FILE AS SYSOUT                     *
*                                                                     *
***********************************************************************
*
ALLOCOUT DS    0H
         STM   14,9,ALCSAVE            SAVE WORK REGISTERS
         ST    R5,ODCBADDR             SAVE OUTPUT DCB POINTER   JM#14
         LR    R1,R3                   LOAD POINTER TO DDNAME
         BAL   R2,FREEDD               BRANCH TO FREE ROUTINE
         LTR   R8,R8                   SEE IF RETURN CODE FROM FREE IS
*                                      ZERO
         BNZ   OPENFAIL                NO.....GO PUT OUT OPEN FAILED
         LA    R8,REQBLK               LOAD POINTER TO REQUEST BLOCK
         ST    R8,RBPTR                INITIALIZE REQUEST BLOCK POINTER
         MVI   RBPTR,S99RBPND          INDICATE END OF PARAMETER LIST
         USING S99RB,R8                ESTABLISH ADDRESSABILITY TO
*                                      REQUEST BLOCK
         MVC   S99RBLN(RBLEN),REQBLKC  INITALIZE RB
         LA    R1,TEXTPTRS             LOAD POINTER TO TEXT POINTERS
         ST    R1,S99TXTPP             STORE POINTER IN RB
         DROP  R8                      DROP ADDRESSABILITY TO RB
         USING S99TUPL,R1              ESTABLISH ADDRESSABILITY TO
*                                      TEXT UNIT POINTER LIST
         LA    R2,TEXTA                LOAD POINTER TO FIRST TEXT UNIT
         ST    R2,S99TUPTR             STORE POINTER IN TUPL
         USING S99TUNIT,R2             ESTABLISH ADDRESSABILITY TO
*                                      TEXT UNIT
         MVC   S99TUKEY(TEXTALEN),TEXTAC  INITIALIZE TEXT UNIT A
         MVC   S99TUPAR(8),0(R3)       MOVE IN PARAMETER (DDNAME)
         LA    R1,4(R1)                LOAD POINTER TO NEXT TEXT UNIT
*                                      POINTER
         LA    R2,TEXTB                LOAD POINTER TO TEXT UNIT B
         ST    R2,S99TUPTR             STORE POINTER IN TUPL
         MVC   S99TUKEY(TEXTBLEN),TEXTBC  INITIALIZE TEXT UNIT B
*
***********************************************************************
*                                                                     *
*        SET UP ADDRESSABILITY TO PDL                                 *
*           AND SEE IF SYSOUT CLASS/DEST ARE PRESENT                  *
*           ALSO FORMS/TRAIN/FCB/PROG.                          GTEL  *
*           ALSO CGROUP                                          JM#14*
***********************************************************************
*
         L     R9,PARSBACK             LOAD ADDRESS PARS ANSWER
         USING IKJPARMD,R9             ESTABLISH ADDRESSABILITY TO PDL
         CLC   PCLASS(2),HALF1         SEE IF CLASS SPECIFIED
         BNE   NOCLASS                 NO.....GO CHECK FOR DESTINATION
         L     R7,SCLASS               LOAD POINTER TO SYSOUT CLASS
         MVC   S99TUPAR,0(R7)          MOVE CLASS INTO TEXT UNIT B
NOCLASS  DS    0H
         CLC   PDEST(2),HALF1          SEE IF DEST SPECIFIED
         BNE   NODEST                  NO.....GO CHECK FOR HOLD
         LA    R1,4(R1)                LOAD POINTER TO NEXT TEXT UNIT
*                                      POINTER
         LA    R2,TEXTC                LOAD POINTER TO TEXT UNIT C
         ST    R2,S99TUPTR             STORE POINTER IN TUPL
         MVC   S99TUKEY(TEXTCLEN),TEXTCC  INITIALIZE TEXT UNIT C
         L     R7,SDEST                LOAD POINTER TO DESTINATION
         LH    R6,SDEST+4              LOAD LENGTH OF DESTINATION ID
         STH   R6,S99TULNG             STORE LENGTH OF DEST IN TU
         BCTR  R6,0                    DECREMENT R6 FOR EX OF MOVE
         EX    R6,MOVEDEST             MOVE DEST INTO TU
MOVEDEST MVC   S99TUPAR,0(R7)          MOVE TO BE EXECUTED
*
NODEST   DS    0H
         CLC   PHOLD(2),HALF1          SEE IF HOLD SPECIFIED
         BNE   NOHOLD                  NO.....GO CHECK FOR COPIES
         LA    R1,4(R1)                LOAD POINTER TO NEXT TEXT UNIT
*                                      POINTER
         LA    R2,TEXTDC               LOAD POINTER TO TEXT UNIT C
         ST    R2,S99TUPTR             STORE POINTER IN TUPL
NOHOLD   DS    0H
         CLC   PCOPIES(2),HALF1        SEE IF COPY SPECIFIED
         BNE   NOCOPY                  NO.....GO ZERO ECB
         AIF   (&LGENOPT EQ 0).SKPCK01 IF PARTIAL GEN OPT        JM#14
         CLC   PARR(2),HALF1           CHECK FOR   ARR             TJ#4
         BE    NOCOPY              LET ARR HANDLE  COPIES          TJ#4
.SKPCK01 ANOP                                                    JM#14
         LA    R1,4(R1)                LOAD PNTR TO NEXT TEXT UNIT PTR
         LA    R2,TEXTE                LOAD POINTER TO TEXT UNIT E
         ST    R2,S99TUPTR             STORE POINTER IN TUPL
         MVC   S99TUKEY(TEXTELEN),TEXTEC  INITIALIZE TEXT UNIT E
         L     R7,SCOPIES              LOAD POINTER TO COPY FIELD
         LH    R6,SCOPIES+4            LOAD LENGTH OF COPY FIELD
         BCTR  R6,0                    DECREMENT R6 FOR EX OF PACK
         EX    R6,PACKCOPY             EXECUTE PACK TO CONVERT COPIES
*                                      TO ZONED DECIMAL
         CVB   R6,DEC                  CONVERT ZONED TO BINARY
         STCM  R6,1,S99TUPAR           STORE NUMBER OF COPIES IN TU
NOCOPY   DS    0H
*********** START OF GTE MOD
         CLC   PFORMS(2),HALF1         SEE IF FORMS SPECIFIED
         BNE   NOFORMS                 NO.....GO CHECK TRAIN
         LA    R1,4(R1)                LOAD POINTER TO NEXT TEXT UNIT
*                                      POINTER
         LA    R2,TEXTI                LOAD POINTER TO TEXT UNIT I
         ST    R2,S99TUPTR             STORE POINTER IN TUPL
         MVC   S99TUKEY(TEXTILEN),TEXTIC  INITIALIZE TEXT UNIT I
         L     R7,RFORMS               LOAD POINTER TO FORMS FIELD
         LH    R6,RFORMS+4             LOAD LENGTH OF FORMS FIELD
         STH   R6,S99TULNG             STORE LENGTH IN TUE
         BCTR  R6,0                    DECREMENT LENGTH
         EX    R6,MOVEDEST             MOVE FORMS INTO FIELD
NOFORMS  DS    0H
         CLC   PTRAIN(2),HALF1         SEE IF TRAIN SPECIFIED
         BNE   NOTRAIN                 NO.....GO CHECK FCB
         LA    R1,4(R1)                LOAD POINTER TO NEXT TEXT UNIT
*                                      POINTER
         LA    R2,TEXTH                LOAD POINTER TO TEXT UNIT G
         ST    R2,S99TUPTR             STORE POINTER IN TUPL
         MVC   S99TUKEY(TEXTHLEN),TEXTHC  INITIALIZE TEXT UNIT H
         L     R7,RTRAIN               LOAD POINTER TO TRAIN FIELD
         LH    R6,RTRAIN+4             LOAD LENGTH OF TRAIN FIELD
         STH   R6,S99TULNG             SAVE LENGTH OF FCBE
         BCTR  R6,0                    DECREMENT FOR EX
         EX    R6,MOVEDEST             MOVE FCB INTO TU
NOTRAIN  DS    0H
         CLC   PFCB(2),HALF1           SEE IF FCB  SPECIFIED
         BNE   NOFCB                   NO.....GO ZERO ECB
         LA    R1,4(R1)                LOAD POINTER TO NEXT TEXT UNIT
*                                      POINTER
         LA    R2,TEXTG                LOAD POINTER TO TEXT UNIT G
         ST    R2,S99TUPTR             STORE POINTER IN TUPL
         MVC   S99TUKEY(TEXTGLEN),TEXTGC  INITIALIZE TEXT UNIT G
         L     R7,RFCB                 LOAD POINTER TO FCB FIELD
         LH    R6,RFCB+4               LOAD LENGTH OF FCB FIELD
         STH   R6,S99TULNG             SAVE LENGTH OF FCB IN TUE
         BCTR  R6,0                    DECREMENT
         EX    R6,MOVEDEST             MOVE FCB TO TU
NOFCB    DS    0H
         CLC   PPROG(2),HALF1          SEE IF PROG SPECIFIED
         BNE   NOPROG                  NO.....GO CHECK CGROUP
         LA    R1,4(R1)                LOAD POINTER TO NEXT TEXT UNIT
*                                      POINTER
         LA    R2,TEXTJ                LOAD POINTER TO TEXT UNIT J
         ST    R2,S99TUPTR             STORE POINTER IN TUPL
         MVC   S99TUKEY(TEXTJLEN),TEXTJC  INITIALIZE TEXT UNIT J
         L     R7,RPROG                LOAD POINTER TO PROG FIELD
         LH    R6,RPROG+4              LOAD LENGTH OF PROG FIELD
         STH   R6,S99TULNG             SAVE LENGTH OF PROG IN TUE
         BCTR  R6,0                    DECREMENT FOR MOVE
         EX    R6,MOVEDEST             MOVE PROG TO TU
NOPROG   DS    0H
********* END OF GTE MOD
         CLC   PCCGROUP(2),HALF1       SEE IF CGROUP SPECIFIED   JM#14
         BNE   NOCGROUP                NO.....GO ZERO ECB        JM#14
         LA    R1,4(R1)                LOAD PTR TO NEXT TU PTR   JM#14
         LA    R2,TEXTK                LOAD PTR TO TEXT UNIT K   JM#14
         ST    R2,S99TUPTR             STORE POINTER IN TUPL     JM#14
         MVC   S99TUKEY(TEXTKLEN),TEXTKC INIT TEXT UNIT K        JM#14
         SR    R8,R8                   ZERO COUNTER              JM#14
         LA    R3,4(R2)                POINT TO FIRST SUBFIELD   JM#14
         USING S99TUFLD,R3             ADDRESS THE TU FIELDS     JM#14
         LA    R5,CGRPVAL              POINT TO PDE FOR CGROUP   JM#14
CGRP01   DS    0H                      LOOP THROUGH CGROUPS      JM#14
         MVC   S99TULEN(2),=X'0001'    ASSUME COUNT =< 255 (FF)  JM#14
         L     R7,0(R5)                LOAD PTR TO THE CGROUP    JM#14
         LH    R6,4(R5)                LOAD LENGTH OF FIELD      JM#14
         BCTR  R6,0                    REDUCE FOR PACK           JM#14
         EX    R6,PACKCGRP             PACK THE COPY GROUP VALUE JM#14
         CVB   R15,CGRPVALD            CONVERT TO BINARY         JM#14
         STCM  R15,B'0001',S99TUPRM    STORE THE CGROUP VALUE    JM#14
         LA    R8,1(R8)                COUNT THIS CGROUP         JM#14
         C     R8,FULL10               HAVE WE DONE 10 CGROUPS   JM#14
         BNL   CGRP02                  YES - FINISH UP TUE'S     JM#14
         LA    R7,8(R5)                POINT AT CHAIN WORD       JM#14
         TM    0(R7),X'FF'             IS THIS THE LAST VALUE    JM#14
         BO    CGRP02                  YES - FINISH UP TUE'S     JM#14
         L     R5,8(R5)                POINT AT NEXT PDE         JM#14
         LA    R3,3(R3)                POINT TO NEXT TUE AREA    JM#14
         B     CGRP01                  GO DO NEXT CGROUP VALUE   JM#14
         DROP  R3                                                JM#14
CGRP02   DS    0H                                                JM#14
         STH   R8,S99TUNUM             SAVE THE CGROUP COUNT     JM#14
NOCGROUP DS    0H                                                JM#14
         LA    R1,4(R1)                LOAD POINTER TO NEXT TEXT UNIT
*                                      POINTER
         LA    R2,TEXTFC               LOAD POINTER TO TEXT UNIT F
*                                      PERMANENTLY ALLOCATED ATTRIBUTE
         ST    R2,S99TUPTR             STORE POINTER IN TUPL
         MVI   S99TUPTR,S99TUPLN       INDICATE END OF TEXT UNIT
*                                      POINTER LIST
         DROP  R1,R2                   DROP ADDRESSABILITY       JM#14
         LA    R1,RBPTR                LOAD ADDRESS OF REQUEST BLOCK
*                                      POINTER
         DYNALLOC                      ISSUE DYNAMIC ALLOCATION SVC
         LTR   R15,R15                 SEE IF RETURN CODE IS ZERO
         BNZ   ALCFAIL                 NO.....GO AND CALL DAIR FAIL
         MVC   OPENLST,OPENCLOS        MOVE OPEN(CLOSE) LIST CONSTANT
*                                      INTO WORKAREA
*                                                                JM#14
*        CHECK FOR THE PRESENCE OF 3800 KEYWORDS.                JM#14
*        IF ANY ARE PRESENT MOVE THEM TO WORKAREA                JM#14
*        AND SET FLAGS THAT INDICATE THEIR PRESENCE.             JM#14
*                                                                JM#14
         MVI   P3800FLG,X'00'          INITIALIZE FLAG BYTE      JM#14
         AIF   (&LGENOPT EQ 0).CKCHRS  IF PARTIAL GEN SELECTED   JM#14
CHKMODFY CLC   PCMODIFY,HALF1          IF MODIFY NOT SPECIFIED   JM#14
         BNE   CKFLASH                    THEN CHECK FOR FLASH   JM#14
         MVC   MODIFYID(4),BLANKS      INITIALIZE THE FIELD      JM#14
         LA    R15,MODIFYID            POINT AT THE FIELD        JM#14
         L     R14,MDFYVAL             POINT AT THE PARM         JM#14
         LH    R1,MDFYVAL+4            GET THE LENGTH            JM#14
         BCTR  R1,0                    REDUCE R1 FOR MOVE        JM#14
         EX    R1,MOVE3800             MOVE FIELD TO WORKAREA    JM#14
         OI    P3800FLG,P38MODFY       SET FLAG BIT              JM#14
         CLC   PCMODTRC,HALF1          IF MODTRC NOT SPECIFIED   JM#14
         BNE   CKFLASH                    THEN CHECK FOR FLASH   JM#14
         MVI   MODTRCID,X'00'          INITIALIZE THE FIELD      JM#14
         LA    R15,MODTRCID            POINT AT THE FIELD        JM#14
         L     R14,MTRCVAL             POINT AT THE PARM         JM#14
         EX    0,MOVE3800              MOVE FIELD TO WORKAREA    JM#14
         OI    P3800FLG,P38MDTRC       SET FLAG BIT              JM#14
CKFLASH  CLC   PCFLASH,HALF1           IF FLASH NOT SPECIFIED    JM#14
         BNE   CHKCHARS                   THEN CHECK FOR CHARS   JM#14
         MVC   FLASHID(4),BLANKS       INITIALIZE THE FIELD      JM#14
         LA    R15,FLASHID             POINT AT THE FIELD        JM#14
         L     R14,FLSHVAL             POINT AT THE PARM         JM#14
         LH    R1,FLSHVAL+4            GET THE LENGTH            JM#14
         BCTR  R1,0                    REDUCE R1 FOR MOVE        JM#14
         EX    R1,MOVE3800             MOVE FIELD TO WORKAREA    JM#14
         OI    P3800FLG,P38FLASH       SET FLAG BIT              JM#14
         CLC   PCFCOUNT,HALF1          IF FCOUNT NOT SPECIFIED   JM#14
         BNE   CHKCHARS                   THEN CHECK FOR CHARS   JM#14
         MVC   FLASHCNC(4),BLANKS      INITIALIZE THE FIELD      JM#14
         LA    R15,FLASHCNC            POINT AT THE FIELD        JM#14
         L     R14,FCNTVAL             POINT AT THE PARM         JM#14
         LH    R1,FCNTVAL+4            GET THE LENGTH            JM#14
         BCTR  R1,0                    REDUCE R1 FOR MOVE        JM#14
         EX    R1,MOVE3800             MOVE FIELD TO WORKAREA    JM#14
         EX    R1,PACKFCNT             PACK THE FLASH COUNT      JM#14
         CVB   R15,FLASHCND            CONVERT TO BINARY         JM#14
         STCM  R15,B'0001',FLASHCNT    STORE THE LENGTH          JM#14
         OI    P3800FLG,P38FLCNT       SET FLAG BIT              JM#14
.CKCHRS  ANOP                                                    JM#14
CHKCHARS DS    0H                                                JM#14
         CLC   PCCHARS,HALF1           IF CHARS NOT SPECIFIED    JM#14
         BNE   CHKBURST                   THEN CHECK FOR TRC     JM#14
         MVC   CHARSETS(16),BLANKS     INITIALIZE THE FIELD      JM#14
         LA    R15,CHARSETS            POINT AT THE FIELD        JM#14
         LA    R3,CHARVAL              POINT AT THE PCE          JM#14
         L     R14,0(R3)               POINT AT THE PARM         JM#14
         LH    R1,4(R3)                GET THE LENGTH            JM#14
         BCTR  R1,0                    REDUCE R1 FOR MOVE        JM#14
         EX    R1,MOVE3800             MOVE FIELD TO WORKAREA    JM#14
         OI    P3800FLG,P38CHARS       SET FLAG BITS             JM#14
         LA    R2,1                    SET CHARS COUNTER         JM#14
         CLI   8(R3),X'FF'             IF AT END OF PCES         JM#14
         BE    CHKBURST                   THEN CHECK FOR BURST   JM#14
CKCHRNX  LA    R15,4(R15)              POINT AT THE FIELD        JM#14
         L     R3,8(R3)                POINT AT THE PCE          JM#14
         L     R14,0(R3)               POINT AT THE PARM         JM#14
         LH    R1,4(R3)                GET THE LENGTH            JM#14
         BCTR  R1,0                    REDUCE R1 FOR MOVE        JM#14
         EX    R1,MOVE3800             MOVE FIELD TO WORKAREA    JM#14
         LA    R2,1(R2)                BUMP UP CHARS COUNTER     JM#14
         C     R2,=F'4'                IF WE ARE AT 4 (OR MORE)  JM#14
         BE    CHKBURST                   THEN WE ARE DONE       JM#14
         CLI   8(R3),X'FF'             IF NOT AT END OF PCES     JM#14
         BNE   CKCHRNX                    THEN CHECK NEXT CHARS  JM#14
CHKBURST DS    0H                                                JM#14
         AIF   (&LGENOPT EQ 0).CKTRC1  IF PARTIAL GEN SELECTED   JM#14
         CLC   PCBURST,HALF0   DID USER SPECIFY BURST OVERRIDE   JM#14
         BE    CHKTRC01      NO...                               JM#14
         CLC   PCBURST,HALF1   DID USER SPECIFY NOBURST          JM#14
         BE    CHKTRC01      YES...                              JM#14
         CLC   BRSTVAL,HALF1   TRC SPECIFIED - WAS IT YES??      JM#14
         BNE   CHKTRC01      NO....                              JM#14
         OI    P3800FLG+1,P38BURST     INDICATE BURST=Y          JM#14
*                                                                JM#14
*        CHECK FOR THE PRESENCE OF "TRC" OR "TRC(YES)"           JM#14
*        AND ADD OPTCD=J TO DCB BEFORE OPEN IF THERE.            JM#14
*                                                                JM#14
.CKTRC1  ANOP                                                    JM#14
CHKTRC01 DS    0H                                                JM#14
         L     R5,ODCBADDR             LOAD ADDRESS OF DCB       JM#14
         USING IHADCB,R5               ESTABLISH DCB ADDRESSBLTY JM#14
         CLC   PCTRC,HALF0     DID USER SPECIFY TRC OVERRIDE     JM#14
         BE    OPEN5OUT      NO...                               JM#14
         CLC   PCTRC,HALF1     DID USER SPECIFY NOTRC            JM#14
         BE    OPEN5OUT      YES...                              JM#14
         CLC   TRCVAL,HALF1    TRC SPECIFIED - WAS IT YES??      JM#14
         BNE   OPEN5OUT      NO....                              JM#14
         OI    DCBOPTCD,DCBOPTJ        SETUP OPTCD=J             JM#14
OPEN5OUT DS    0H                                                JM#14
         OPEN  ((R5),(OUTPUT)),MF=(E,OPENLST)
         DROP  R5                      DROP DCB ADDRESSABILITY   JM#14
         TM    48(R5),DCBOFOPN         SEE IF DCB OPENED SUCCESSFULLY
         BNO   OPENFAIL                NO.....GO PUT OUT OUTPUT DCB
*                                      NOT OPENED MESSAGE
*                                                                JM#14
*        CHECK TO SEE IF ANY 3800 RELATED PARAMETER HAS BEEN     JM#14
*        SPECIFIED - IF SO THEN ISSUE THE SETPRT MACRO FOR       JM#14
*        THE APPROPRIATE FIELDS.                                 JM#14
*                                                                JM#14
CHK3800F DS    0H                                                JM#14
         TM    P3800FLG,X'FF'          IF NO 3800 ITEMS SPECD.   JM#14
         BZ    ALLOCEND                   THEN GO RETURN         JM#14
         LA    R4,SETPRTD              POINT AT SETPRT           JM#14
         USING SETPRT,R4               GET ADDRESSABILITY        JM#14
         MVC   SETPRTD(SPLEN),SETPRTC  MOVE SETPRT TO WORKAREA   JM#14
         LA    R15,MSGAREA             LOAD THE MESSAGE ADDRESS  JM#14
         ST    R15,@SPMSGAD            STORE MESSAGE ADDRESS     JM#14
         ST    R5,@SPDCBAD             STORE DCB ADDRESS         JM#14
         OC    @SPFLAG2(1),P3800FLG+1  OR IN THE FLAGS (BURST)   JM#14
         TM    P3800FLG,P38CHAR1+P38CHAR2+P38CHAR3+P38CHAR4      JM#14
         BZ    CHKMODID                IF NO CHARS - CHK MODIFY  JM#14
         MVC   @SPCHAR1(16),CHARSETS   MOVE NAMES TO SETPRT      JM#14
CHKMODID DS    0H                                                JM#14
         AIF   (&LGENOPT EQ 0).DOSPRT  IF PARTIAL GEN SELECTED   JM#14
         TM    P3800FLG,P38MODFY       IF NO MODIFY SPECD.       JM#14
         BZ    CHKFLASH                   THEN CHECK FOR FLASH   JM#14
         MVC   @SPMODID(4),MODIFYID    MOVE IN MODIFY NAME       JM#14
         TM    P3800FLG,P38MDTRC       IF NO MODIFY TRC SPECD.   JM#14
         BZ    CHKFLASH                   THEN CHECK FOR FLASH   JM#14
         MVC   @SPMDTRC(1),MODTRCID    MOVE IN TRC IDENTIFICTN.  JM#14
CHKFLASH DS    0H                                                JM#14
         TM    P3800FLG,P38FLASH       IF NO FLASH SPECIFIED     JM#14
         BZ    DOSETPRT                   THEN FINISH SETPRT     JM#14
         MVC   @SPFLASH(4),FLASHID     MOVE IN THE FLASH-ID      JM#14
         TM    P3800FLG,P38FLCNT       IF NO FLASH COUNT SPECD.  JM#14
         BZ    DOSETPRT                   THEN FINISH SETPRT     JM#14
         MVC   @SPFLCNT,FLASHCNT       MOVE IN THE FLASH COUNT   JM#14
.DOSPRT  ANOP                                                    JM#14
DOSETPRT DS    0H                                                JM#14
         SETPRT MF=(E,(R4))            DO THE SETPRT             JM#14
         LTR   R15,R14                 IF RETURN CODE IS X'00'   JM#14
         BZ    ALLOCEND                   THEN END ROUTINE       JM#14
         PUTLINE OUTPUT=(MSGAREA,SINGLE,DATA),MF=(E,IOPLSECT)    JM#14
         DROP  R4,R9                   DROP ADDRESSABILITY       JM#14
ALLOCEND DS    0H                                                JM#14
         LM    14,9,ALCSAVE            RESTORE REGISTERS
         BR    R14                     RETURN TO CALLER
ALCFAIL  DS    0H
         LA    R1,REQBLK               LOAD ADDRESS OF REQUEST BLOCK
         MVI   DFID+1,DFSVC99          SET DAIR FAIL FOR SVC 99 ERROR
         BAL   R2,DAIRFAIL             LOAD ADDRESS OF OPEN FAIL AND
*                                      BRANCH TO DAIRFAIL ROUTINE
OPENFAIL DS    0H
         PUTLINE OUTPUT=(OPENMSG,SINGLE,DATA),MF=(E,IOPLSECT)  PUT OUT
*                                      OUTPUT DCB NOT OPEND MESSAGE
         S     R3,FULL4                GET POINTER TO PUTLINE DDNAME
         PUTLINE OUTPUT=((R3),SINGLE,DATA),MF=(E,IOPLSECT)  PUT OUT
*                                      DDNAME
         B     CLOSDCBS                GO CLOSE DCBS AND RETURN
*
***********************************************************************
*                                                                     *
*        INVOKE DAIR TO FREE FILENAME VIA CALLTSSR                    *
*           AND CHECK FOR ZERO RETURN CODE                            *
*                                                                     *
***********************************************************************
*
FREEDD   DS    0H
         MVC   DAP18(DAPB18L),DAPB18C  INITIALIZE DAIR PARAMETER BLOCK
*                                      18 (FREE BY DATASET)
         LA    R4,DAP18                LOAD ADDRESS OF DAPB
         USING DAPB18,R4               ESTABLISH ADDRESSABILITY TO
         CLC   0(8,R1),=CL8'PRINTI'    IS THIS THE INPUT DATASET?  TJ#1
         BNE   SKPDELCK                IF NOT THEN CONTINUE        TJ#1
         L     R8,PARSBACK       LOAD ADDRESS PARS ANSWER          TJ#1
         USING IKJPARMD,R8       ESTABLISH ADDRESSABILITY TO PDL   TJ#1
         CLC   PDELETE(2),HALF1        WAS DELETE SPECIFIED?       TJ#1
         BNE   SKPDELCK                IF NOT SKIP MOVING IN DEL   TJ#1
         CLI   NODELTF,C'N'            CHECK DELETE FLAG           TJ#1
         BE    SKPDELCK                IF NO, SKIP MOVING IN DEL   TJ#1
         MVI   DA18DPS2,X'04'          MOVE IN DELETE DISPOSITION  TJ#1
         DROP  R8                                                  TJ#1
SKPDELCK DS    0H                                                  TJ#1
*                                      DAPB
         SLR   R8,R8                   INITIALIZE REGISTER 8 TO ZERO
*                                      RETURN CODE
         MVC   DA18DDN,0(R1)           MOVE DDNAME INTO DAPB
         SLR   R7,R7                   ZERO REGISTER 7
         ST    R7,ECB                  STORE REGISTER 7 AS DUMMY ECB
         LA    R1,DAPLSECT             LOAD ADDRESS OF DAPL
         USING DAPL,R1                 ESTABLISH ADDRESSABILITY TO DAPL
         ST    R4,DAPLDAPB             STORE ADDRESS OF DAPB IN DAPL
         DROP  R1,R4                   DROP ADDRESSABILITY TO DAPL AND
*                                      DAPB
         CALLTSSR EP=IKJDAIR           CALL DAIR TO FREE DDNAME
         C     R15,FULL28              SEE IF RETURN CODE IS 28
*                                      (DDNAME NOT ALLOCATED)
         BER   R2                      YES....GO CONTINUE PROCESSING
         LTR   R15,R15                 SEE IF RETURN CODE IS ZERO
         BZR   R2                      YES....GO CONTINUE PROCESSING
         MVI   DFID+1,DFDAIR           SET DAIRFAIL ID TO INDICATE DAIR
*
***********************************************************************
*                                                                     *
*        INVOKE DAIRFAIL TO DISPLAY AN ERROR MESSAGE                  *
*                                                                     *
***********************************************************************
*
DAIRFAIL XC    DFPARMS(DFLEN),DFPARMS  INITIALIZE PARMLIST FOR DAIR
*                                      FAIL TO ZEROS
         ST    R1,DFDAPLP              STORE POINTER TO DAPL/RB IN
*                                      PARAMETER LIST
         ST    R15,RETCODE             STORE RETURN CODE
         LA    R1,RETCODE              LOAD ADDRESS OF RETURN CODE
         ST    R1,DFRCP                STORE POINTER TO RETURN CODE IN
*                                      PARAMETER LIST
         LA    R1,FULL0                LOAD ADDRESS OF FULLWORD OF
*                                      ZEROS
         ST    R1,DFJEFF02             STORE DUMMY POINTER TO IKJEFF02
*                                      IN PARMLIST
         LA    R1,DFID                 LOAD ADDRESS OF DAIR FAIL ID
*                                      FIELD
         ST    R1,DFIDP                STORE POINTER TO DFID IN
*                                      PARMLIST
         MVI   DFID,NOWTP              SET FOR NO WRITE TO PROGRAMMER
         L     R1,SAVEAREA+4           LOAD ADDRESS OF PEVIOUS
*                                      SAVEAREA
         L     R1,24(R1)               LOAD POINTER TO CPPL
         ST    R1,DFCPPLP              STORE POINTER TO CPPL IN
*                                      PARMLIST
         LINK  EP=IKJEFF18,MF=(E,DFPARMS)  LINK TO DAIR FAIL SERVICE
*                                      ROUTINE TO HANDLE RETURN
*                                      CODE
         LTR   R15,R15                 SEE IF RETURN CODE IS ZERO
         BZR   R2                      YES....GO RETURN TO CALLER
         LR    R8,R15                  LOAD REGISTER 8 WITH RETURN CODE
         BAL   R14,SETCODE             GO CONVERT CODE TO DECIMAL
         PUTLINE OUTPUT=(DRFLERR,SINGLE,DATA),MF=(E,IOPLSECT)  PUT OUT
*                                      DAIR FAIL ERROR MESSAGE
         PUTLINE OUTPUT=(RETNCD,SINGLE,DATA),MF=(E,IOPLSECT)  PUT OUT
*                                      DECIMAL RETURN CODE
         BR    R2                      GO RETURN TO CALLER
*
******************** INSTRUCTIONS TO BE EXECUTED **********************
*
PACKFCNT PACK  FLASHCND(8),FLASHCNC(0) 3800 PACK THE FLASH COUNT JM#14
PACKCGRP PACK  CGRPVALD(8),0(0,R7)     3800 PACK THE COPY GROUPS JM#14
MOVE3800 MVC   0(0,R15),0(R14)         3800 EXECUTED MOVE        JM#14
MOVEDSN  MVC   DSNBUF(0),0(R5)
MOVEVOL  MVC   VOLUME(0),0(R7)
PACKCOPY PACK  DEC(8),0(0,R7)
         USING DAPB08,R8
MOVEMEM  MVC   DA08MNM(0),0(R5)
MOVEPASS MVC   DA08PSWD(0),0(R5)
         DROP  R8
DFPBMVE  MVC   DFPBNAME(0),0(R5)                    GTE
*
***************************** CONSTANTS *******************************
*
         LTORG
******************** DAIR CONTROL BLOCK CONSTANTS *********************
DAPB08C  DS    0F
         DC    X'0008'                 DA08CD
         DC    H'0'                    DA08FLG
         DC    H'0'                    DA08DARC
         DC    H'0'                    DA08CTRC
         DC    A(0)                    DA08PDSN
         DC    CL8'PRINTI  '           DA08DDN
         DC    CL8' '                  DA08UNIT
         DC    CL8' '                  DA08SER
         DC    F'0'                    DA08BLK
         DC    F'0'                    DA08PQTY
         DC    F'0'                    DA08SQTY
         DC    F'0'                    DA08DQTY
         DC    CL8' '                  DA08MNM
         DC    CL8' '                  DA08PSWD
         DC    X'08'                   DA08DSP1      SHR
         DC    X'08'                   DA08DPS2      KEEP
         DC    X'08'                   DA08DSP3      KEEP
         DC    X'08'                   DA08CTL       PERM
         DC    F'0'                    DA08DSO
         DC    CL8' '                  DA08ALN
DAPB08L  EQU   *-DAPB08C               LENGTH
         SPACE 2
DAPB18C  DS    0F
         DC    X'0018'                 DA18CD
         DC    X'0000'                 DA18FLG
         DC    H'0'                    DA18DARC
         DC    H'0'                    DA18CTRC
         DC    F'0'                    DA18PDSN
         DC    CL8'PRINTI'             DA18DDN
         DC    CL8' '                  DA18MNM
         DC    CL2' '                  DA18SCLS
         DC    X'08'                   DA18DPS2
         DC    X'10'                   DA18CTL
         DC    CL8' '                  DA18JBNM
DAPB18L  EQU   *-DAPB18C               LENGTH
         SPACE 2
INPUTC   DCB   DSORG=PS,MACRF=(GM),EODAD=EXIT,SYNAD=ERRI,DDNAME=PRINTI
INPUTL   EQU   *-INPUTC
*
OUTPUTAC DCB   DSORG=PS,MACRF=(PM),SYNAD=ERRO,DDNAME=PRINTA,RECFM=VBA, X
               LRECL=256,BLKSIZE=3120
OUTPUTAL EQU   *-OUTPUTAC
*
OUTPUTMC DCB   DSORG=PS,MACRF=(PM),SYNAD=ERRO,DDNAME=PRINTM,RECFM=VBM, X
               LRECL=256,BLKSIZE=3120
OUTPUTML EQU   *-OUTPUTMC
*
DIRECTC  DCB   DDNAME=PRINTI,DSORG=PS,MACRF=(GM),RECFM=U,BLKSIZE=256,  X
               SYNAD=SERRI,EODAD=FREEBLK
DIRECTL  EQU   *-DIRECTC
*
OPENCLOS OPEN  (,),MF=L
*                                                                JM#14
SETPRTC  SETPRT MF=L                   LIST FORM FOR PROGRAM     JM#14
SPLEN    EQU   *-SETPRTC               LENGTH OF THE SETPRT      JM#14
         SPACE 2
HEADERC  DS    0F
         DC    H'84'
         DC    H'0'
HDRCNTLC DC    C' '
         DC    CL43'**** TSO FOREGROUND HARDCOPY ****'
         DC    CL36' '
HEADERM  DS    0F
         DC    H'84'
         DC    H'0'
         DC    C' '
         DC    CL43'*** TSO FOREGROUND MEMBER LIST **'
         DC    CL36' '
LDSNAME  DS    0CL12
         ORG   LDSNAME
         DC    H'66'
         DC    H'0'
         DC    CL8' DSNAME='
BLNKASA  DC    H'5',H'0',C' '          BLANK ASA LINE              TJ#1
BLNKMCH  DC    H'5',H'0',X'09'         BLANK MCH LINE              TJ#1
CCASATOP DC    C'1'              CARRIAGE CONTROL FOR ASA          TJ#1
CCMCHTOP DC    X'09'             CARRIAGE CONTROL FOR MCH        JM#14
*CMCHTOP DC    X'8B' ----WAS-----CARRIAGE CONTROL FOR MCH          TJ#1
ARRPRTR  DC    H'85',H'0',C' '   ARRANGEMENT PRINT LINE SETUP      TJ#2
         DC    CL80' '           ARRANGEMENT TYPE LINE SETUP       TJ#2
ARRPL    EQU   *-ARRPRTR         ARRANGEMENT PRINT LINE SETUP      TJ#2
ARRDUP   DC    CL80'DUP 1 '      ARRANGEMENT DUPLEX LINE           TJ#4
ARRNUM1  DC    CL80',MARGIN TEXT 2 1 '    MARGIN TEXT LINE         TJ#5
ARRNUM2  DC    CL38' ',CL42'- // // - '    MARGIN TEXT LINE        TJ#5
EXPRESS  DC    CL80',EXPRESS '             EXPRESS ARR LINE        TJ#5
LINMSG   DC    AL2(LINMSGL),AL2(0)                                 TJ#8
         DC    C'TOTAL NUMBER OF LINES PRINTED OUT:'               TJ#8
         DC    C'            '                                     TJ#8
LINMSGL  EQU   *-LINMSG                                            TJ#8
EJECTM   DS    0F
         DC    H'5'
         DC    H'0'
         DC    X'8B'
EJECTA   DS    0F
         DC    H'5'
         DC    H'0'
         DC    C'1'
SPACE    EQU   EJECTA
         SPACE 2
FULL0    DC    F'0'
FULL1    DC    F'1'
FULL4    DC    F'4'
FULL20   DC    F'20'
FULL28   DC    F'28'
FULL256  DC    F'256'
PRINTILN DC    H'12',H'0'
PRINTI   DC    CL8'PRINTI'
PRINTALN DC    H'12',H'0'
PRINTA   DC    CL8'PRINTA'
PRINTMLN DC    H'12',H'0'
PRINTM   DC    CL8'PRINTM'
BLANKS   DC    256CL1' '
ALIAS    DC    CL8' *ALIAS*'
ENDCHAIN DS    0F
         DC    X'FF000000'
MEMDATL  DC    X'00'                   SUBPOOL NUMBER
         DC    AL3(1808)
HALF0    DC    H'0'
HALF1    DC    H'1'
HALF2    DC    H'2'
HALF6    DC    H'6'
HALF12   DC    H'12'
HALF85   DC    H'85'
HALF256  DC    H'256'
FULL10   DC    F'10'                   FULLWORD WITH VALUE OF 10 JM#14
PATCH    DC    8CL4'ZAP*'
HEXFF    EQU   X'FF'
HIGH     EQU   X'80'
BLANK    EQU   X'40'
LOW      EQU   X'01'
JFCPDS   EQU   X'01'
HEX00    EQU   X'00'
SCLSA    EQU   C'A'
SKIPM    EQU   X'09'
DSOPS    EQU   X'40'
DSOPO    EQU   X'02'
NOWTP    EQU   X'00'
DATEEDIT DS    0CL19         EDIT PATTERN FOR  DATE AND TIME       JM#1
         DC    X'21207A20207A2020'       TIME                      TJ#3
         DC    C'     ',X'20204B202020'  DATE                      TJ#3
**************************************************************JM#1*
*DATEEDIT DS    0CL36         EDIT PATTERN FOR  DATE AND TIME JM#1*
*        DC    C'    PRODUCED  AT ',X'21207A20207A2020'       JM#1*
*        DC    C'  ON ',X'20204B202020'                       JM#1*
*        *---  PRODUCED  AT HH:MM:SS  ON YY.DDD  ---*         JM#1*
**************************************************************JM#1*
         EJECT
****************** POINTER TO PARS PARAMETER CSECT ********************
PCLADDR  DC    A(PARMTAB)
         SPACE 2
************************ PARS PARAMETER LIST **************************
PARMTAB  IKJPARM  DSECT=IKJPARMD
DSNAMES  IKJPOSIT  DSNAME,LIST,USID,                                   X
               PROMPT='DSNAME',                                        X
               HELP='DSNAME TO BE PRINTED'
PCCTL    IKJKEYWD ,
         IKJNAME 'NOCCTL'                                          TF#1
         IKJNAME 'CCTL',SUBFLD=CCTLSUB                             TF#1
PCTRC    IKJKEYWD ,                    3800 SUPPORT FOR TRC      JM#14
         IKJNAME 'NOTRC'                                         JM#14
         IKJNAME 'TRC',SUBFLD=TRCSUB                             JM#14
         AIF   (&LGENOPT EQ 0).PARS01  IF PARTIAL GEN OPT        JM#14
PCBURST  IKJKEYWD ,                    3800 SUPPORT FOR BURST    JM#14
         IKJNAME 'NOBURST'                                       JM#14
         IKJNAME 'BURST',SUBFLD=BRSTSUB                          JM#14
PCMODTRC IKJKEYWD ,                    3800 SUPPORT FOR MODIFY   JM#14
         IKJNAME 'MODTRC',SUBFLD=MTRCSUB,ALIAS=('MTRC')          JM#14
PCMODIFY IKJKEYWD ,                    3800 SUPPORT FOR MODIFY   JM#14
         IKJNAME 'MODIFY',SUBFLD=MDFYSUB                         JM#14
PCFLASH  IKJKEYWD ,                    3800 SUPPORT FOR FLASH    JM#14
         IKJNAME 'FLASH',SUBFLD=FLSHSUB                          JM#14
PCFCOUNT IKJKEYWD ,                    3800 SUPPORT FOR FLASH    JM#14
         IKJNAME 'FCOUNT',SUBFLD=FCNTSUB,ALIAS=('FCNT')          JM#14
.PARS01  ANOP                                                    JM#14
PCCHARS  IKJKEYWD ,                    3800 SUPPORT FOR CHARS    JM#14
         IKJNAME 'CHARS',SUBFLD=CHARSUB                          JM#14
PCCGROUP IKJKEYWD ,                    3800 SUPPORT COPY GROUPS  JM#14
         IKJNAME 'CGROUP',SUBFLD=CGRPSUB                         JM#14
PCLASS   IKJKEYWD ,
         IKJNAME 'CLASS',SUBFLD=CLASSUB
PDEST    IKJKEYWD ,
         IKJNAME 'DEST',SUBFLD=DESTSUB
PDELETE  IKJKEYWD ,                                                TJ#1
         IKJNAME 'DELETE'                                          TJ#1
         IKJNAME 'KEEP'                                            TJ#1
PPDS     IKJKEYWD ,                                                TJ#1
         IKJNAME 'PDS'                                             TJ#1
PEJECT   IKJKEYWD DEFAULT='NOEJECT(4)'                             TJ#1
         IKJNAME 'EJECT'                                           TJ#1
         IKJNAME 'NOEJECT',SUBFLD=NOEJSUB                          TJ#1
PARR     IKJKEYWD ,                                                TJ#2
         IKJNAME 'ARR',SUBFLD=ARRSUB                               TJ#2
PLINES   IKJKEYWD DEFAULT='NOLINES'                                TJ#2
         IKJNAME 'NOLINES'                                         TJ#2
         IKJNAME 'LINES',SUBFLD=LINSUB                             TJ#2
PTITLE   IKJKEYWD ,                                                TJ#3
         IKJNAME 'TITLE',SUBFLD=TITLESUB                           TJ#3
PNUMBER  IKJKEYWD ,                                                TJ#5
         IKJNAME 'NUMBER'                                          TJ#5
PEXPRES  IKJKEYWD ,                                                TJ#5
         IKJNAME 'EXPRESS'                                         TJ#5
PDOUBLE  IKJKEYWD ,                                                TJ#6
         IKJNAME 'DOUBLE'                                          TJ#6
PTRIPLE  IKJKEYWD ,                                                TJ#6
         IKJNAME 'TRIPLE'                                          TJ#6
*********** #4. ************************                           TJ#4
PCOUNT   IKJKEYWD ,
         IKJNAME 'COUNT',SUBFLD=SUBCOUNT                           TJ#4
PSTART   IKJKEYWD ,
         IKJNAME 'START',SUBFLD=SUBSTART                           TJ#4
PSTOP    IKJKEYWD ,
         IKJNAME 'STOP',SUBFLD=SUBSTOP                             TJ#4
PDUPLEX  IKJKEYWD ,
         IKJNAME 'DUPLEX'                                          TJ#4
*********** #4. ************************                           TJ#4
PHOLD    IKJKEYWD ,
         IKJNAME 'HOLD'
         IKJNAME 'NOHOLD'
PCOPIES  IKJKEYWD ,
         IKJNAME 'COPIES',SUBFLD=COPYSUB
PPRINT   IKJKEYWD ,
         IKJNAME 'PRINT'
         IKJNAME 'NOPRINT'
PLIST    IKJKEYWD ,
         IKJNAME 'LIST'
         IKJNAME 'NOLIST'
PHEAD    IKJKEYWD ,
         IKJNAME 'NOHEADING'                       GTE
         IKJNAME 'HEADING'                         GTE
PVOL     IKJKEYWD ,
         IKJNAME 'VOLUME',SUBFLD=VOLSUB
PFOLD    IKJKEYWD ,
         IKJNAME 'FOLD',ALIAS=('CAPS')
         IKJNAME 'NOFOLD',ALIAS=('ASIS')
PFORMS   IKJKEYWD ,
         IKJNAME  'FORMS',SUBFLD=SFORMS                    GTEL
PTRAIN   IKJKEYWD ,
         IKJNAME  'TRAIN',SUBFLD=STRAIN,ALIAS=('UCS')      GTEL
PFCB     IKJKEYWD ,
         IKJNAME    'FCB',SUBFLD=SFCB                      GTEL
PPROG    IKJKEYWD ,
         IKJNAME    'PROG',SUBFLD=SPROG                    GTEL
PTEXT    IKJKEYWD ,
         IKJNAME    'TEXT',INSERT='UCS(TN) ASIS'           GTEL
*        B E G I N   S U B F I E L D S
CCTLSUB  IKJSUBF
CCTLTYP  IKJKEYWD DEFAULT='ASA'                                    TF#1
         IKJNAME  'ASA'                                            TF#1
         IKJNAME  'MCH'                                            TF#1
******** IKJSUBF****************                                   TJ#1
NOEJSUB  IKJSUBF
NOEJKEY  IKJIDENT 'OUTPUT SEPARATION',MAXLNTH=2,                   TJ#1X
               FIRST=NUMERIC,OTHER=NUMERIC,                        TJ#1X
               PROMPT='1-2 DIGITS - NUMBER OF SEPARATION LINES',   TJ#1X
               HELP=('NUMBER OF BLANK LINES SEPARATING OUTPUT')    TJ#1
ARRSUB   IKJSUBF
ARRNUM   IKJIDENT '6670 ARRANGEMENT NUMBER',MAXLNTH=3,             TJ#2X
               FIRST=NUMERIC,OTHER=NUMERIC,                        TJ#2X
               PROMPT='1-3 DIGITS - 6670 ARRANGEMENT DESIRED',     TJ#2X
               HELP=('NUMBER OF DESIRED 6670 ARRANGEMENT')         TJ#2
LINSUB   IKJSUBF
LINNUMB  IKJIDENT 'LINES PER PAGE',MAXLNTH=2,                      TJ#2X
               FIRST=NUMERIC,OTHER=NUMERIC,                        TJ#2X
               PROMPT='1-2 DIGITS - NUMBER OF LINES PER PAGE',     TJ#2X
               HELP=('NUMBER OF LINE PER PAGE ON OUTPUT')          TJ#2
TITLESUB IKJSUBF
TITLEFLD IKJIDENT 'TITLE FOR OUTPUT',MAXLNTH=40,                   TJ#3X
               FIRST=ANY,OTHER=ANY,CHAR,                           TJ#3X
               PROMPT='TITLE FOR THE PRINTOFF OUTPUT',             TJ#3X
               HELP=('ENTER TITLE FOR OUTPUT DATASET')             TJ#3
SUBCOUNT IKJSUBF
COUNTNUM IKJIDENT 'COUNT OF RECORDS TO PRINT',MAXLNTH=4,           TJ#4X
               FIRST=NUMERIC,OTHER=NUMERIC,                        TJ#4X
               PROMPT='0-9999 - COUNT OF RECORDS TO PRINT',        TJ#4X
               HELP=('ENTER NUMBER OF RECORDS TO PRINT')           TJ#4
SUBSTART IKJSUBF
STARTNUM IKJIDENT 'RECORD TO START PRINT ON ',MAXLNTH=6,           TJ#4X
               FIRST=NUMERIC,OTHER=NUMERIC,                        TJ#4X
               PROMPT='0-999999 - START COUNT FOR PRINTTING',     TJ#10X
               HELP=('ENTER START RECORD NUMBER')                  TJ#4
SUBSTOP  IKJSUBF
STOPNUM  IKJIDENT 'RECORD TO STOP PRINTTING ON',MAXLNTH=6,         TJ#4X
               FIRST=NUMERIC,OTHER=NUMERIC,                        TJ#4X
               PROMPT='0-999999 - STOP COUNT TO STOP PRINT',      TJ#10X
               HELP=('ENTER STOP RECORD NUMBER')                   TJ#4
CLASSUB  IKJSUBF
SCLASS   IKJIDENT 'CLASSNAME',LIST,FIRST=NONATNUM,MAXLNTH=1,           X
               PROMPT='CLASS NAME'
DESTSUB  IKJSUBF
SDEST    IKJPOSIT USERID,                                              X
               PROMPT='REMOTE STATION ID FOR THE DEST KEYWORD'
COPYSUB  IKJSUBF
SCOPIES  IKJIDENT 'COPIES',MAXLNTH=3,                                  X
               FIRST=NUMERIC,OTHER=NUMERIC,                            X
               PROMPT='1-3 DIGITS - NUMBER OF COPIES OF OUTPUT',       X
               HELP=('NUMBER OF COPIES OF PRINTOUT DESIRED')
VOLSUB   IKJSUBF
SVOL     IKJIDENT 'VOLUME',MAXLNTH=8,                                  X
               FIRST=ALPHANUM,OTHER=ALPHANUM,                          X
               PROMPT='VOLUME SERIAL FOR DATA SETS TO BE PRINTED',     X
               HELP=('VOLUME SERIAL WILL BE ASSUMED FOR ALL DATA SETS')
SFORMS   IKJSUBF
RFORMS   IKJIDENT 'FORMS',MAXLNTH=4,FIRST=ALPHANUM,               GTEL X
               OTHER=ALPHANUM,                                    GTEL X
               PROMPT='FORMS DESIGNATION FOR PRINTED OUTPUT',     GTEL X
               HELP=('AN ALPANUMERIC STRING TO SPECIFY THE FORM') GTEL
STRAIN   IKJSUBF
RTRAIN   IKJIDENT   'UCS',MAXLNTH=4,FIRST=ALPHANUM,               GTEL X
               OTHER=ALPHANUM,                                    GTEL X
               PROMPT='UCS   DESIGNATION FOR PRINTED OUTPUT',     GTEL X
               HELP=('AN ALPANUMERIC STRING TO SPECIFY THE TRAIN') GTEL
SFCB     IKJSUBF
RFCB     IKJIDENT 'FCB',MAXLNTH=4,FIRST=ALPHANUM,                 GTEL X
               OTHER=ALPHANUM,                                    GTEL X
               PROMPT='FCB DESIGNATION FOR PRINTED OUTPUT',       GTEL X
               HELP=('AN ALPANUMERIC STRING TO SPECIFY THE FCB')   GTEL
SPROG    IKJSUBF
RPROG    IKJIDENT 'PROG',MAXLNTH=8,FIRST=ALPHA,                   GTEL X
               OTHER=ALPHANUM,                                    GTEL X
               PROMPT='PROG DESIGNATION FOR PRINTED OUTPUT',      GTEL X
               HELP=('AN ALPANUMERIC STRING TO SPECIFY THE PROG')  GTEL
TRCSUB   IKJSUBF ,                     3800 SUPPORT FOR TRC      JM#14
TRCVAL   IKJKEYWD DEFAULT='YES'                                  JM#14
         IKJNAME  'YES'                                          JM#14
         IKJNAME  'NO'                                           JM#14
         AIF   (&LGENOPT EQ 0).PARS02  IF PARTIAL GEN OPT        JM#14
BRSTSUB  IKJSUBF ,                     3800 SUPPORT FOR BURST    JM#14
BRSTVAL  IKJKEYWD DEFAULT='YES'                                  JM#14
         IKJNAME  'YES'                                          JM#14
         IKJNAME  'NO'                                           JM#14
MDFYSUB  IKJSUBF ,                     3800 SUPPORT FOR MODIFY   JM#14
MDFYVAL  IKJIDENT 'COPY MODIFICATION MODULE NAME',MAXLNTH=4,     JM#14 X
               FIRST=ANY,OTHER=ANY,CHAR,                         JM#14 X
               PROMPT='COPY MODIFICATION MODULE NAME',           JM#14 X
               HELP=('ENTER 3800 COPY MODIFICATION MODULE NAME') JM#14
MTRCSUB  IKJSUBF ,                     3800 SUPPORT FOR MODIFY   JM#14
MTRCVAL  IKJIDENT 'MODIFICATION MODULE TRC',MAXLNTH=1,           JM#14 X
               FIRST=NUMERIC,                                    JM#14 X
               PROMPT='MODIFICATION MODULE TRC',                 JM#14 X
               HELP=('ENTER 3800 COPY MODIFICATION MODULE TRC')  JM#14
FCNTSUB  IKJSUBF ,                     3800 SUPPORT FOR FLASH    JM#14
FCNTVAL  IKJIDENT 'FORMS FLASH COUNT',MAXLNTH=3,                 JM#14 X
               FIRST=NUMERIC,OTHER=NUMERIC,                      JM#14 X
               PROMPT='FORMS FLASH COUNT',                       JM#14 X
               HELP=('ENTER 3800 FORMS FLASH COUNT')             JM#14
FLSHSUB  IKJSUBF ,                     3800 SUPPORT FOR FLASH    JM#14
FLSHVAL  IKJIDENT 'FORMS FLASH NAME',MAXLNTH=4,                  JM#14 X
               FIRST=ANY,OTHER=ANY,CHAR,                         JM#14 X
               PROMPT='FORMS FLASH NAME',                        JM#14 X
               HELP=('ENTER 3800 FORMS FLASH NAME')              JM#14
.PARS02  ANOP                                                    JM#14
CHARSUB  IKJSUBF ,                     3800 SUPPORT FOR CHARS    JM#14
CHARVAL  IKJIDENT 'CHARS VALUES',LIST,                           JM#14 X
               MAXLNTH=4,                                        JM#14 X
               FIRST=ANY,OTHER=ANY,CHAR,                         JM#14 X
               PROMPT='CHARS VALUES',                            JM#14 X
               HELP=('ENTER 3800 CHARACTER ARRANGEMENT NAMES')   JM#14
CGRPSUB  IKJSUBF ,                     3800 SUPPORT COPY GROUPS  JM#14
CGRPVAL  IKJIDENT 'COPY GROUP VALUES',LIST,                      JM#14 X
               MAXLNTH=2,                                        JM#14 X
               FIRST=NUMERIC,OTHER=NUMERIC,                      JM#14 X
               PROMPT='COPY GROUP VALUES',                       JM#14 X
               HELP=('ENTER COPY GROUP VALUES FOR 3800')         JM#14
         IKJENDP
         SPACE 2
****************************** MESSAGES *******************************
GNRLERR  DC    AL2(GNRLERRL),AL2(0)
         DC    C'AN ERROR WAS ENCOUNTERED IN THE GENERAL FAIL SERVICE RX
               OUTINE.  THE RETURN CODE FROM GNRLFAIL IS: '
GNRLERRL EQU   *-GNRLERR
*
DRFLERR  DC    AL2(DRFLERRL),AL2(0)
         DC    C'AN ERROR WAS ENCOUNTERED IN THE DAIR FAIL SERVICE ROUTX
               INE.  THE RETURN CODE FROM DAIR FAIL IS: '
DRFLERRL EQU   *-DRFLERR
*
OPENMSG  DC    AL2(OPENMSGL),AL2(0)
         DC    C'OUPUT PROCESSING IS BEING TERMINATED.  AN ERROR WAS ENX
               COUNTERED ATTEMPTING TO OPEN THE OUTPUT DATASET ASSOCIATX
               ED WITH DDNAME: '
OPENMSGL EQU   *-OPENMSG
*
DSNNOTP  DC    AL2(DSNNOTPL),AL2(0)
         DC    C'AN ERROR OCCURRED AND YOUR DATASET WAS NOT PRINTED'
DSNNOTPL EQU   *-DSNNOTP
*
PDSKWE   DC    AL2(PDSKWEL),AL2(0)                                 TJ#1
         DC    C'DATASET IS PDS AND ''PDS'' KEYWORD NOT SPECIFIED' TJ#1
PDSKWEL  EQU   *-PDSKWE                                            TJ#1
*
BADLNE   DC    AL2(BADLNEL),AL2(0)                                 TJ#1
         DC    C'NUMBER OF LINES SPECIFIED MUST BE GREATER THAN 4' TJ#1
BADLNEL  EQU   *-BADLNE                                            TJ#1
*
*
DSNMSG   DC    AL2(DSNMSGL),AL2(0)
         DC    C'PROCESSING HAS BEEN COMPLETED FOR DATASET: '
DSNMSGL  EQU   *-DSNMSG
*
MEMMSG   DC    AL2(MEMMSGL),AL2(0)
         DC    C' MEMBER: '
MEMMSGL  EQU   *-MEMMSG
*
PRTMSG   DC    AL2(PRTMSGL),AL2(0)
         DC    C'MEMBERS WILL NOW BE PRINTED FOR DATASET: '
PRTMSGL  EQU   *-PRTMSG
*
NOTPSPO  DC    AL2(NOTPSPOL),AL2(0)
         DC    C'DATASET IS NOT SEQUENTIAL OR PARTITIONED AND WAS NOT PX
               RINTED.'
NOTPSPOL EQU   *-NOTPSPO
*
SYNADI   DC    AL2(SYNADIL),AL2(0)
         DC    C'A SYNAD ERROR OCURRED READING YOU INPUT DATASET AND ONX
               LY PART OF YOUR DATASET MAY HAVE BEEN PRINTED.'
SYNADIL  EQU   *-SYNADI
*
SYNADO   DC    AL2(SYNADOL),AL2(0)
         DC    C'A SYNAD ERROR OCURRED WRITING THE OUTPUT DATASET AND NX
               O MORE DATASETS WILL BE PRINTED.'
SYNADOL  EQU   *-SYNADO
*
DSNISU   DC    AL2(DSNISUL),AL2(0)
         DC    C'DATASET HAS UNDEFINED RECORD LENGTH AND WILL NOT BE PRX
               INTED.'
DSNISUL  EQU   *-DSNISU
*
DSNISL   DC    AL2(DSNISLL),AL2(0)
         DC    C'DATASET HAS RECORD LENGTH GREATER THAN 256.'
DSNISLL  EQU   *-DSNISL
         SPACE 2
************************ DSECTS (MAPPING MACROS) **********************
MEMDSECT DSECT
MEMNTTR  DS    CL11
         ORG   MEMNTTR
MEMBER   DS    CL8
MEMTTR   DS    CL3
CFIELD   DS    CL1
MEMSECTN EQU   *
         IKJDAP08
DAP08LEN EQU   *-DAPB08
         IKJDAP18
DAP18LEN EQU   *-DAPB18
         IKJDAPL
DAPLLEN  EQU   *-DAPL
         IKJCPPL
CPPLLEN  EQU   *-CPPL
         IKJPPL
PPLLEN   EQU   *-PPL
         IKJIOPL
IOPLLEN  EQU   *-IOPL
         IHADSAB
TIOEDDNM EQU   4           X'1C'(OFFSET) - X'18'(JOB STEP)
         CVT
         IEFZB4D0
         IEFZB4D2
         IKJDFPL                                          GTE
         IKJDFPB                                          GTE
RBLEN    EQU   S99RBEND-S99RB
         DCBD  DSORG=(PS,PO)
         EJECT
******************** CONSTANTS FOR DYNAMIC ALLOCATION *****************
PRINTOFF CSECT
REQBLKC  DC    AL1(S99RBEND-S99RB),AL1(S99VRBAL),18AL1(0)
TEXTAC   DC    AL2(DALDDNAM),AL2(1),AL2(8),CL8'PRINT'  DDNAME
TEXTALEN EQU   *-TEXTAC
TEXTBC   DC    AL2(DALSYSOU),AL2(1),AL2(1),C'A'        SYSOUT CLASS
TEXTBLEN EQU   *-TEXTBC
TEXTCC   DC    AL2(DALSUSER),AL2(1),AL2(8),CL8'LOCAL'  DESTINATION
TEXTCLEN EQU   *-TEXTCC
TEXTDC   DC    AL2(DALSHOLD),AL2(0)              SYSOUT HOLD
TEXTDLEN EQU   *-TEXTDC
TEXTEC   DC    AL2(DALCOPYS),AL2(1),AL2(1),AL2(1)      SYSOUT COPIES
TEXTELEN EQU   *-TEXTEC
TEXTFC   DC    AL2(DALPERMA),AL2(0)              PERM ALLOCATION
TEXTFLEN EQU   *-TEXTFC
TEXTGC   DC    AL2(DALFCBIM),AL2(1),AL2(4),CL4'STD.'  FCB       GTEL
TEXTGLEN EQU   *-TEXTGC                                         GTEL
TEXTHC   DC    AL2(DALUCS),AL2(1),AL2(2),CL4'PN'      UCS       GTEL
TEXTHLEN EQU   *-TEXTHC                                         GTEL
TEXTIC   DC    AL2(DALSFMNO),AL2(1),AL2(2),CL4'STD.'  FORMS     GTEL
TEXTILEN EQU   *-TEXTIC                                         GTEL
TEXTJC   DC    AL2(DALSPGNM),AL2(1),AL2(8),CL8'PROG'  PROG      GTEL
TEXTJLEN EQU   *-TEXTJC                                         GTEL
TEXTKC   DC    AL2(DALCOPYG),AL2(0),AL2(0),AL1(0)    COPY GROUPS JM#14
         DC                         AL2(0),AL1(0)    UP          JM#14
         DC                         AL2(0),AL1(0)     TO         JM#14
         DC                         AL2(0),AL1(0)      10        JM#14
         DC                         AL2(0),AL1(0)       MAY      JM#14
         DC                         AL2(0),AL1(0)        BE      JM#14
         DC                         AL2(0),AL1(0)         USED   JM#14
         DC                         AL2(0),AL1(0)          AT    JM#14
         DC                         AL2(0),AL1(0)           ANY  JM#14
         DC                         AL2(0),AL1(0)           TIME JM#14
TEXTKLEN EQU   *-TEXTKC                                        . JM#14
*********************** DSECT FOR DYNAMIC WORKAREA ********************
WORKAREA DSECT
SAVEAREA DS    18F
ALCSAVE  DS    12F
         DS    0D
DEC      DS    2F
RETNCD   DS    F
CONV     DS    2F
IOPB     PUTLINE MF=L
DAP08    DS    0F
         ORG   DAP08+DAP08LEN
DAP18    DS    0F
         ORG   DAP18+DAP18LEN
DAPLSECT DS    0F
         ORG   DAPLSECT+DAPLLEN
PPLSECT  DS    0F
         ORG   PPLSECT+PPLLEN
IOPLSECT DS    0F
         ORG   IOPLSECT+IOPLLEN
DFPLSECT DS    4F                   SPACE FOR DFPL     GTE
DFPBSECT DS    5F                   SPACE FOR DFPB     GTE
         DS    H                    TO GET ALLIGNMENT  GTE
DFPBDSL  DS    H                    DSNAME LENGTH      GTE
DFPBNAME DS    CL44                 DSNAME             GTE
DFPBQUA  DS    CL8
ECB      DS    F
PARSBACK DS    F
LINE     DS    0H
LENGTH   DS    H
         DS    H
CNTLBYTE DS    CL1
DATA     DS    CL256
HEADER2  DS    CL12
DSNAME   DS    CL44
OPENPRN  DS    CL1
MEMNAME  DS    CL8
CLOSEPRN DS    CL1
         ORG   DSNAME-4
DSNLENP  DS    CL2
DSNLEN   DS    CL2
DSNBUF   DS    CL44
         ORG   MEMNAME-4
MEMNAMEL DS    CL2
         ORG
TIMESAVE DS    F             SAVE AREA FOR EXECUTION DATE          JM#1
DATESAVE DS    F             SAVE AREA FOR EXECUTION TIME          JM#1
HEADER   DS    CL46
DATETIME DS    CL19          EDIT PATTERN FOR  DATE AND TIME       JM#1
DPIDOUT  DS    CL19          JOBNAME OF INVOKER                    TJ#3
         ORG   HEADER+4
HDRCNTL  DS    CL1
DFID     DS    CL2
         ORG
VOLBIT   DS    CL1
VOLUME   DS    CL8
MEMTABLE DS    F
ENDTABLE DS    F
CURRENT  DS    F
PRESENT  DS    F
COLUMN   DS    F
XLIST    DS    F
CCTLKEY  DS    H                                                   TF#1
CTYPKEY  DS    H                                                   TF#1
P3800FLG DS    H                       3800 FLAG BYTE(S)         JM#14
P38CHARS EQU   X'0F'                   3800 - CHARS FIELDS FLAG  JM#14
P38CHAR1 EQU   X'01'                   3800 - CHARS 1ST FLD      JM#14
P38CHAR2 EQU   X'02'                   3800 - CHARS 2ND FLD      JM#14
P38CHAR3 EQU   X'04'                   3800 - CHARS 3RD FLD      JM#14
P38CHAR4 EQU   X'08'                   3800 - CHARS 4TH FLD      JM#14
P38MODFY EQU   X'10'                   3800 - COPY MODIFICATION  JM#14
P38MDTRC EQU   X'20'                   3800 - MODIFICATION TRC   JM#14
P38FLASH EQU   X'40'                   3800 - FORMS FLASH        JM#14
P38FLCNT EQU   X'80'                   3800 - FLASH COUNT        JM#14
P38BURST EQU   X'80'                   3800 - BURST=Y FLAG       JM#14
CHARSETS DS    4CL4                    3800 CHARACTER SET IDS    JM#14
MODIFYID DS    CL4                     3800 COPY MODIFICATION ID JM#14
MODTRCID DS    XL1                     3800 COPY MOD TRC ID      JM#14
FLASHID  DS    CL4                     3800 FORMS FLASH ID       JM#14
FLASHCNT DS    XL1                     3800 FORMS FLASH COUNT    JM#14
FLASHCNC DS    CL4                     3800 FLASH COUNT EBCDIC   JM#14
FLASHCND DS    D                       3800 FLASH COUNT PACKED   JM#14
CGRPVALD DS    D                       3800 COPY GROUP PACKED    JM#14
ODCBADDR DS    F                       ADDRESS OF OUTPUT DCB     JM#14
MSGAREA  DS    0CL128                  SETPRT MSG AREA           JM#14
MSGTLEN  DS    H                       SETPRT TOTAL MSG LENGTH   JM#14
MSGRSV1  DS    F                       SETPRT RESERVED AREA      JM#14
MSGLEN   DS    H                       SETPRT MSG TEXT LENGTH    JM#14
MSGRSV2  DS    H                       SETPRT RESERVED AREA      JM#14
MSGTEXT  DS    CL118                   SETPRT MESSAGE TEXT       JM#14
*  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *   TJ#1
CCFLAG   DC    C'X'              CARRIAGE CONTROL YES OR NO FLAG   TJ#2
CCTYPE   DC    C'X'              CARRIAGE CONTROL TYPE FLAG        TJ#1
*     C'A' - ASA CC TYPE,   C'M' - MCH TYPE                        TJ#1
FIRSTFL  DC    C'X'              FIRST TIME THRU FLAG              TJ#1
NODELTF  DC    C'Y'              DELETE FLAG                       TJ#1
EJECTFL  DC    C'X'              EJECT  FLAG                       TJ#1
* C'E' - EJECT EVERY TIME, C'F' - FIRST PASS, C'X' - PRINT EJECTNM TJ#1
*                                                    BLANK LINES   TJ#1
EJECTNM  DC    F'0'            NUMBER OF LINES TO SEPARATE OUTPUT  TJ#1
ARRPRTLN DS    0H              THE ARRANGEMENT LINE AREA           TJ#2
         DS    H,H                                                 TJ#2
ARRCC    DS    C               CC FOR ARR LINE                     TJ#2
ARRPRT   DS    CL8 ',PRINT  ' OR ' ,PRINT '  : ',END   ' OR ' ,END'TJ#2
ARRPRTN  DS    CL3             ARRANGEMENT NUMBER                  TJ#2
         DS    CL90                                                TJ#2
NLINES   DS    PL3             NUMBER OF LINES PER PAGE            TJ#2
HLINES   DS    PL9             NUMBER OF LINES COUNTER             TJ#2
TLINES   DS    PL9      TOTAL  NUMBER OF LINES COUNTER             TJ#4
GTLINES  DS    PL9            GRAND TOTAL NUMBER OF LINES          TJ#8
TRECDS   DS    PL9      TOTAL  NUMBER OF RECORDS COUNTER           TJ#4
DPID     DS    CL8             USER'S DPID FOR HEADING             TJ#3
STARTP   DS    PL6             STARTING POINT FOR PRINTING         TJ#4
STOPP    DS    PL6             STOPPING POINT FOR PRINTING         TJ#4
COUNTP   DS    PL4             COUNT OF RECORDS TO PRINT           TJ#4
REPCC    DS    X               REPLACEMENT CC FOR DOUBLE OR TRIPLE TJ#6
HBLNK    DS    CL5             HOLD BLANK LINE OF ASA OR MCH       TJ#7
LINMSGH  DS    AL2(0),AL2(0)                                       TJ#8
         DC    C'TOTAL NUMBER OF LINES PRINTED OUT:'               TJ#8
GRANTOT  DC    C'               '                                  TJ#8
*  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *   TJ#1
RDJL     RDJFCB (,),MF=L
JFCBAREA DS    44F
         ORG   JFCBAREA+44
JFCBELNM DS    CL8
         ORG   JFCBAREA+86
JFCBIND1 DS    CL1
         ORG   JFCBAREA+98
JFCDSRG1 DS    CL1
         ORG
SAVER6   DS    F
SAVER2   DS    2F
SAVER3   DS    2F
*
*
INPUT    DCB   DSORG=PS,MACRF=(GM),EODAD=EXIT,SYNAD=ERRI,DDNAME=PRINTI
*
OUTPUTA  DCB   DSORG=PS,MACRF=(PM),SYNAD=ERRO,DDNAME=PRINTA,RECFM=VBA, X
               LRECL=256,BLKSIZE=3120
*
OUTPUTM  DCB   DSORG=PS,MACRF=(PM),SYNAD=ERRO,DDNAME=PRINTM,RECFM=VBM, X
               LRECL=256,BLKSIZE=3120
*
DIRECT   DCB   DDNAME=PRINTI,DSORG=PS,MACRF=(GM),RECFM=U,BLKSIZE=256,  X
               SYNAD=SERRI,EODAD=FREEBLK
*
OPENLST  OPEN  (,),MF=L
*
CLOSLST  CLOSE (,),MF=L
*
SETPRTD  SETPRT MF=L                   LIST FORM FOR WORKAREA    JM#14
*
RETCODE  DS    F
GFPARMP  DS    F
         IKJEFFGF
         IKJEFFDF
RBPTR    DS    F
REQBLK   DS    10F                                          GTEL
TEXTPTRS DS    10F                                          GTEL
TEXTA    DS    0F                                   DDNAME
         ORG   TEXTA+TEXTALEN
TEXTB    DS    0F                                   SYSOUT CLASS
         ORG   TEXTB+TEXTBLEN
TEXTC    DS    0F                                   DESTINATION
         ORG   TEXTC+TEXTCLEN
TEXTE    DS    0F                                   SYSOUT COPIES
         ORG   TEXTE+TEXTELEN
TEXTG    DS    0F                                   FCB        GTEL
         ORG   TEXTG+TEXTGLEN                                  GTEL
TEXTH    DS    0F                                   UCB        GTEL
         ORG   TEXTH+TEXTHLEN                                  GTEL
TEXTI    DS    0F                                   FORMS      GTEL
         ORG   TEXTI+TEXTILEN                                  GTEL
TEXTJ    DS    0F                                   PROG       GTEL
         ORG   TEXTJ+TEXTJLEN                                  GTEL
TEXTK    DS    0F                         MAX OF 10 COPY GROUPS  JM#14
         ORG   TEXTK+TEXTKLEN                                    JM#14
LDYNAMIC EQU   *-WORKAREA
         EJECT 1                                                 JM#14
SETPRT   DSECT ,                       DSECT FOR SETPRT MACRO    JM#14
@SPDCBAD DS    AL4                     DCB ADDRESS               JM#14
@SPUCSID DS    AL4                     UCS ID                    JM#14
@SPUCSFL DS    AL1                     UCS FOLD MODE             JM#14
@SPUCSVF DS    AL1                     UCS VERIFY                JM#14
@SPFLAG1 DS    AL1                     SETPRT FLAGS (1)          JM#14
@SPFCBID DS    AL4                     FCB ID                    JM#14
@SPFCBOP DS    AL1                     FCB OPTIONS               JM#14
@SPFLAG2 DS    AL1                     SETPRT FLAGS (2)          JM#14
@SPFLAG3 DS    AL1                     SETPRT FLAGS (3)          JM#14
@SPCPYCT DS    AL1                     NUMBER OF COPIES          JM#14
@SPCPYNR DS    AL1                     NUMBER TO REQUEUE         JM#14
@SPPRMLN DS    AL2                     LENGTH OF PARAMETERS      JM#14
@SPFLCNT DS    AL1                     FORM FLASH COUNT          JM#14
@SPMDTRC DS    AL1                     MODIFICATION TRC          JM#14
@SPMODID DS    AL4                     COPY MOD ID               JM#14
@SPFLASH DS    AL4                     FORMS FLASH ID            JM#14
@SPCHAR1 DS    AL4                     CHARACTER ARR. TAB. 1     JM#14
@SPCHAR2 DS    AL4                     CHARACTER ARR. TAB. 2     JM#14
@SPCHAR3 DS    AL4                     CHARACTER ARR. TAB. 3     JM#14
@SPCHAR4 DS    AL4                     CHARACTER ARR. TAB. 4     JM#14
@SPMSGAD DS    AL4                     ADDRESS OF MESSAGE AREA   JM#14
@SPLIBAD DS    AL4                     LIBRARY DCB ADDRESS       JM#14
         SPACE 2                                                 JM#14
         END
