SWAP     TITLE 'S W A P - System Workload Analysis Pgm - Version 4.5'
***********************************************************************
*                                                                     *
*                              S W A P                                *
*                                                                     *
*                 System  Workload Analysis Program                   *
*                                                                     *
*     SWAP enables a TSO user to get a brief look at what the         *
*     system is doing. Various system and SRM values are displayed    *
*     for each ASID currently executing.                              *
*                                                                     *
*     SWAP may be executed as a CALLed program or as a CP. Various    *
*     subcommands are available to specify what information should    *
*     be displayed.                                                   *
*                                                                     *
*     If SWAP is to be used in conjunction with SPY via the 'L'       *
*     subcommand then SWAP must be APF-authorized and its name        *
*     should go in the IKJEFTE2 and IKJEFTE8 lists. If SWAP is        *
*     merely being used by itself APF authorization is unnecessary.   *
*                                                                     *
*                                                                     *
*  Command         Description                                        *
*                                                                     *
*     A     Display started tasks,batch jobs and TSO                  *
*     B     Display batch and started task jobs only                  *
*     E     Exit from SWAP                                            *
*     Fxxx  Display only those jobs whose name begins with 'xxx'.     *
*           SWAP will continue in this mode until 'F' is entered.     *
*     G     This mode will display general address space information. *
*           (As opposed to 'S' mode.)                                 *
*     I     This mode will display only those memories which are      *
*           either swapped in and runngin or swapped out but ready    *
*           to run.                                                   *
*     L     XCTL to 'SPY' operator console program.                   *
*     O     This mode will display all active memories in the system, *
*           regardless of location.                                   *
*     S     This mode will display SRM information. (As opposed to    *
*           'G' mode.)                                                *
*     T     Display TSO users only                                    *
*     Dxx   set 'wait mode' delay time to 'xx' tenths of a second.    *
*     Wxx   Enter automatic refresh mode. SWAP will automatically     *
*           refresh and update the screen every so often for 'xx'     *
*           seconds. If 'xx' is omitted, a default of 30 seconds is   *
*           Assumed. If 'xx' = 0, the timer will count UP instead of  *
*           down until 'PA1' is pressed.                              *
*     ?     Display this list of help for SWAP commands               *
*                                                                     *
*  Glossary of terms for output display:                              *
*                                                                     *
*    HH:MM:SS  Transaction time                                       *
*    R         Reason code for swap out (from OUCBEFL).               *
*              The following are the reason codes displayed:          *
*                    not swapped - in core                            *
*                O   Swap code 1 : Terminal output wait               *
*                I   Swap code 2 : Terminal input wait                *
*                W   Swap code 3 : Long wait                          *
*                A   Swap code 4 : Auxiliary storage shortage         *
*                R   Swap code 5 : Real storage shortage              *
*                V   Swap code 6 : MS0 detected wait                  *
*                S   SWap code 7 : REQSWAP SYSEVENT issued            *
*                E   SWap code 8 : ENQHOLD exchg by swap analysis     *
*                X   Swap code 9 : Exchg recommended by swap analysis *
*                $   Swap code a : Unilateral swapout                 *
*                ?               : probable error or SRM modification *
*    L         Current location of this memory:                       *
*                I  Swapped in and eligible to run.                   *
*                O  Swapped out but ready to run.                     *
*                W  Swapped out and not ready to run.                 *
*                $  Swapped in and V=R or non-swappable status.       *
*                ?  Transitioning between states.                     *
*                L  Logically swapped.                                *
*    Mem       Current amount of memory allocated to this memory.     *
*    WSS       SRM's view of the working set size for this memory.    *
*    DP        Dispatching priority of memory (in hex).               *
*    DQ        Relative position of memory on dispatching queue.      *
*    PG        Performance group                                      *
*    p         Performance group period                               *
*    Dm        Domain                                                 *
*    RM        Resource manager recommendation                        *
*    WR        Work load manager recommendation                       *
*    SU/S      Service rate (S.U.'s per second)                       *
*    S.U.      Service units (in interval if mode=S )                 *
*    I/O       EXCP's in interval (max=65K)                           *
*    ASID      Address space ID (hex)                                 *
*    CPU       Task CPU time                                          *
*    TCPU      The total CPU time used by this memory (task+SRB).     *
*    TRS       Service accumulated in the transaction.                *
*    SC        Number of times the memory has been swapped            *
*    MSL       MOUNT/START/LOGON initiated task                       *
*    TCB       Number of active TCBs                                  *
*    Procname  Procstepname for this step                             *
*    Stepname  Stepname                                               *
*    TSLS      Time since last swap action in seconds                 *
*                                                                     *
*    SWAP requires SYS1.AMODGEN as a MACLIB                           *
*                                                                     *
*    Registers used:                                                  *
*        R1 - buffer address         R6 - ptr to OUCB                 *
*        R2 - ptr to ASVT entry      R7 - R11  working regs           *
*        R3 - count of ASCBs         R12 - Base register              *
*        R4 - ptr to OUXB                                             *
*        R5 - ptr to ASCB                                             *
*                                                                     *
*                                                                     *
*    Authors:                                                         *
*      SWAP is a heavily reworked version of the MIPS program.        *
*      Rewriting at SCE done by:                                      *
*                                                                     *
*         Marty Sprinzen and Steve Langley                            *
*         Southern California Edison                                  *
*         Rosemead, Calif. 91770                                      *
*         1-213-572-3435                                              *
*                                                                     *
         EJECT
*                                                                     *
*       The above version has been additionally modified at WDA       *
*       to use BEGIN and FINISH macros.  Other changes will be        *
*       marked with WDA#xx where xx is the change number.             *
*       Questions and comments may be sent to                         *
*                                                                     *
*          Joseph A Martin                                            *
*          State of Wisconsin    (WDA)                                *
*          DH&SS - OIS - WSRCC                                        *
*          P.O. Box 7850                                              *
*          Madison, Wisc. 53707-7850                                  *
*          1-608-266-9525                                             *
*                                                                     *
***********************************************************************
         EJECT
         PRINT ON,NOGEN
SWAP     BEGIN AUTHOR=CBT,BASE=(R12)                            WDA#01
         BAL   R14,INITSCAN       initialize IKJSCAN parmlist
         LA    3,STAXLIST
         STAX  ATTNEXIT,MF=(E,(3)) attn exit trap
         GTSIZE ,                 get terminal size
         LTR   R0,R0              is it a hardcopy?
         BZ    HARDCOPY           yes; branch
         LR    R2,R0              R2 = number of lines per screen
         SH    R2,=H'2'           R2 = R2 - 2
         MH    R2,=H'81'          R2 = number of bytes in screen bufr
         LA    R3,BUFFER          R3 = addr of start of buffer
         LA    R2,0(R2,R3)        R2 = addr of end of buffer
         ST    R2,ADBUF           save R2
         STFSMODE ON,INITIAL=YES  turn on full screen mode for VTAM
         B     BOTHTYPE
HARDCOPY STSIZE SIZE=80           set line size to 80
         MVI   CRTFLAG,X'00'      we are using a hardcopy
         MVC   CLR(26),BLANKS     zap out cntrl chars
         MVC   CMDCTRL1(2),BLANKS
BOTHTYPE MVC   HEADINGA(80),HEADING2   load general heading initially.
***********************************************************************
*                                                                     *
*                        Locate CVT and ASVT pointers                 *
*                                                                     *
***********************************************************************
FINDCVT  L     R2,16              CVT pointer
         USING CVT,R2
         L     R2,CVTASVT         ASVT pointer
         DROP  R2
         USING ASVT,R2
         L     R3,ASVTMAXU        maximum number of entries
         LA    R2,ASVTFRST        first entry minus 4
         ST    R2,FRSTASVT        save it for later
         LA    R1,BUFFER          load address of output buffer.
         ST    R2,ASCBADDR        save ASCB address
         CLC   JOBMASK(8),BLANKS  is the jobmask blank?
         BNE   ASCBLOOP           no, so don't set page to 0
         MVI   PAGE,C'0'          reset page to page 0
         EJECT
***********************************************************************
*                                                                     *
*            Major Loop (through all ASCB's)                          *
*                                                                     *
***********************************************************************
ASCBLOOP L     R2,ASCBADDR        R2 = addr of last ASCB entry
         LA    R2,4(R2)           increment to next ASCB entry
         ST    R2,ASCBADDR        save ASCB addr
         ICM   R5,B'1111',0(R2)   ASCB pointer
         USING ASCB,R5
         BM    NOGOOD             branch if bad ptr
         TM    102(R5),X'04'      test for swapped out
INOROUT  BC    0,NOGOOD           branch if out and not ready to run
         L     R6,ASCBOUCB        OUCB pointer
         USING OUCB,R6
         L     R4,ASCBOUXB        OUXB pointer
         USING OUXB,R4
         TM    OUCBYFL,OUCBLOG    logon created user?
TSOORNO  BC    1,NOGOOD           branch if batch only
BATCHORN BC    0,NOGOOD           branch if TSO only
         MVI   LINE+2,C' '        clear output line to blanks.
         MVC   LINE+3(77),LINE+2
***********************************************************************
*                                                                     *
*                              Jobname                                *
*                                                                     *
***********************************************************************
         MVC   JOB(8),START       initialize 'starting' jobname
         ICM   R7,B'1111',ASCBJBNI jobname pointer
         BZ    NOTJOB             jobname not available
         MVC   JOB,0(R7)          move in jobname
         B     CONT1
NOTJOB   ICM   R7,B'1111',ASCBJBNS S/M/L pointer
         BZ    CONT1              S/M/L not available
         MVC   JOB,0(R7)          move in jobname for S/M/L
CONT1    CLC   JOBMASK(8),BLANKS  is the job mask all blanks?
         BE    CONT1A             yes, so go on
         LA    R8,JOBMASK         R8 = addr of jobmask
         LA    R7,JOBMASK+7       R7 = addr of last char in mask
MASKLOOP CLI   0(R8),C' '         is the char a blank?
         BE    GOTLEN             yes
         CR    R8,R7              is R8 = R7?
         BE    GOTLEN             yes - end of mask
         LA    R8,1(R8)           point to next char
         B     MASKLOOP           check next char
GOTLEN   LA    R7,JOBMASK+1       R7 = addr of first char in mask + 1
         SR    R8,R7              R8 = length of mask - 1
         EX    R8,VARCLC1         is the job mask = jobname?
         BNE   NOGOOD             no, so flush this ASCB
***********************************************************************
*                                                                     *
*                             Swap count                              *
*                                                                     *
***********************************************************************
CONT1A   LH    R7,OUCBSWC         R7 = swap count
         CVD   R7,WORK            convert it to decimal.
         MVO   WORK,WORK
         MVI   WORK+7,X'0C'
         MVC   $UCBSWC(2),PTRN1+5
         ED    $UCBSWC-1(3),WORK+6
***********************************************************************
*                                                                     *
*                          Swap Reason code - OUCBSRC                 *
*                                                                     *
***********************************************************************
SWAPCODE TM    OUCBQFL,OUCBOUT    are we swapped out?
         BZ    FORGET             no, swap code means nothing
         CLI   OUCBSRC,X'0A'      check for valid swap reason
         BH    BADCODE            no.
         CLI   OUCBSRC,X'00'
         BE    BADCODE            no.
         SR    R8,R8
         IC    R8,OUCBSRC         use code number as offset into table
         SH    R8,=H'1'           decrease by 1
         LA    R7,CODETABL
         LA    R7,0(R8,R7)        load addr of correct alpha code
         MVC   STATUS(1),0(R7)    move code to output
         B     FORGET
BADCODE  MVI   STATUS,C'?'        error in code or program logic
***********************************************************************
*                                                                     *
*                    Address Space Queue Location                     *
*                                                                     *
***********************************************************************
FORGET   MVI   LOC,C'I'           assume address space on 'in' queue
         MVI   LINE+1,X'E8'       attr byte - protected, hi intensity
         TM    OUCBSFL,OUCBNSW    address space - non-swap
         BZ    LOGSQ              no
         MVI   LOC,C'$'           move in non-swap indication.
         B     DONE
LOGSQ    TM    OUCBQFL,OUCBLSW    'logical swap' queue
         BZ    WAITQ              no
         MVI   LOC,C'L'           'logical swap' queue indication
         MVI   LINE+1,X'60'       attr byte - low intensity
         B     DONE
WAITQ    TM    OUCBQFL,OUCBOFF    'wait' queue
         BZ    OUTQ               no
         MVI   LOC,C'W'           'wait' queue indication
         MVI   LINE+1,X'60'       attr byte - low intensity
         B     DONE
OUTQ     TM    OUCBQFL,OUCBOUT    'out' queue
         BZ    TRANS              no
         MVI   LOC,C'O'           'out' queue indication
         MVI   LINE+1,X'60'       attr byte - low intensity
         B     DONE
TRANS    TM    OUCBQFL,OUCBGOO+OUCBGOI+OUCBGOB    transitioning' status
         BZ    DONE               no
         MVI   LOC,C'?'           'transitioning' indication
***********************************************************************
*                                                                     *
*                    Total CPU Time (task + SRB)                      *
*                                                                     *
***********************************************************************
DONE     LM    R8,R9,ASCBEJST     EJST (CPU time - task type)
         SRDL  R8,12              convert to microsec
         LM    R10,R11,ASCBSRBT   srb time
         SRDL  R10,12             convert to microsec
         AR    R9,R11             total (task + SRB) CPU time
         BNO   NOOVFLO            branch if no overflow.
         A     R8,=F'1'           otherwise add one to high order.
NOOVFLO  AR    R8,R10             high order.
         D     R8,=F'100000'      for divide.
         CVD   R9,WORK            convert to decimal.
         MVC   TCPU(6),PTRN2
         ED    TCPU-1(7),WORK+5
***********************************************************************
*                                                                     *
*                 Frames Allocated to address space                   *
*                                                                     *
***********************************************************************
         LH    R7,ASCBFMCT        allocated page frame count
         CVD   R7,WORK
         MVC   MEM(3),PTRN1+4
         ED    MEM-1(4),WORK+6
         EJECT
***********************************************************************
*                                                                     *
*     'General' or 'S.R.M.' Mode?                                     *
*                                                                     *
***********************************************************************
         CLI   SRMSW,X'00'
         BE    GENERAL
***********************************************************************
*  SRM Mode:                                                          *
***********************************************************************
*                                                                     *
*                     Working Set size at swapin                      *
*                                                                     *
***********************************************************************
         LH    R7,OUCBWSS         swapin frame count
         CVD   R7,WORK
         MVC   $UCBWSS(3),PTRN1+4
         ED    $UCBWSS-1(4),WORK+6
***********************************************************************
*                                                                     *
*                        Dispatching priority                         *
*                                                                     *
***********************************************************************
         XR    R7,R7              R7 = 0
         IC    R7,ASCBDP          R7 = dispatching priority
         SRL   R7,4               convert first hex character
         IC    R8,HEX(R7)
         STC   R8,DP
         IC    R7,ASCBDP          R7 = dispatching priority
         N     R7,=F'15'          convert second hex character
         IC    R8,HEX(R7)
         STC   R8,DP+1
***********************************************************************
*                                                                     *
*                   Position on Dispatching queue                     *
*                                                                     *
***********************************************************************
         CLI   LOC,C'O'           memory swapped out ?
         BE    NOPOS              if yes, forget about position.
         CLI   LOC,C'W'           wait status ?
         BE    NOPOS              if yes, forget about position.
         LH    R7,ASCBSEQN        load dp queue position.
         CVD   R7,WORK            prepare for conversion to EBCDIC.
         UNPK  DQ,WORK+6(2)       convert to decimal.
         OI    DQ+1,X'F0'
***********************************************************************
*                                                                     *
*                  Resource Manager Recommendation                    *
*                                                                     *
***********************************************************************
NOPOS    L     R7,OUCBCMRV        composite WLM recommendation value
         LTR   R7,R7              hi-order bit is on ?
         BNM   RMROK              yes, WMR invalid
         MVC   $UCBWMR,=C'**'     'not avail' indication
         MVI   $UCBWMS+4,C'-'     blank out the service counter.
         B     CONT3
RMROK    SRL   R7,8               divide by 256
         CVD   R7,WORK            convert to decimal .
         UNPK  $UCBRMR,WORK+6(2)
         OI    $UCBRMR+1,X'F0'
         CP    WORK+6(2),=PL2'0'  RMR greater than 99 ?
         BNE   CONT3              yes, double zero field.
         MVI   $UCBRMR,C' '       WMR less than 1.
***********************************************************************
*                                                                     *
*                         Performance Group                           *
*                                                                     *
***********************************************************************
CONT3    XR    R7,R7              R7 = 0
         ICM   R7,3,OUCBNPG       R7 = performance group
         CVD   R7,WORK            convert to decimal.
         UNPK  $UCBPGN,WORK+6(2)
         OI    $UCBPGN+1,X'F0'
***********************************************************************
*                                                                     *
*                      Performance Group Period                       *
*                                                                     *
***********************************************************************
         XR    R7,R7              R7 = 0
         IC    R7,OUCBPGP         performance group period offset
         SH    R7,=H'12'          - 12
         SRL   R7,4               / 16
         LA    R7,1(R7)
         STC   R7,$UCBPGP
         OI    $UCBPGP,X'F0'
***********************************************************************
*                                                                     *
*                         Domain                                      *
*                                                                     *
***********************************************************************
         XR    R7,R7              R7 = 0
         IC    R7,OUCBDMN         R7 = domain
         CVD   R7,WORK            convert to decimal.
         UNPK  $UCBDOM,WORK+6(2)
         OI    $UCBDOM+1,X'F0'
***********************************************************************
*                                                                     *
*                      WLM Recommendation value                       *
*                                                                     *
***********************************************************************
         L     R7,OUCBWMR         work load manager rec. value
         LTR   R7,R7              hi-order bit is on ?
         BNM   WMROK              yes, WMR invalid
         MVC   $UCBWMR,=C'NA'     'not avail' indication
         MVI   $UCBWMS+4,C'-'     blank out the service counter.
         B     NORECOM
WMROK    SRL   R7,8               divide by 256
         CVD   R7,WORK            convert to decimal .
         UNPK  $UCBWMR,WORK+6(2)
         OI    $UCBWMR+1,X'F0'
         CP    WORK+6(2),=PL2'0'  WMR greater than 99 ?
         BNE   NORECOM            yes, double zero field.
         MVI   $UCBWMR,C' '       WMR less than 1.
***********************************************************************
*                                                                     *
*                      In-Storage Service Rate                        *
*                                                                     *
***********************************************************************
NORECOM  L     R10,16             R10 = addr of CVT
         USING CVT,R10
         L     R10,CVTOPCTP       R10 = addr of RMCT
         DROP  R10
         L     R10,X'7C'(R10)     R10 = current TOD
         S     R10,OUCBTMW        WLM interval start time
         BP    PLUSRATE
         MVC   SERATE(3),BLANKS
         B     NOSRV
PLUSRATE L     R8,OUCBWMS         interval service units
         SRDL  R8,32              calculate sevice rate
         M     R8,=F'976'
         DR    R8,R10
         CVD   R9,WORK            convert to decimal
         MVC   SERATE(3),PTRN1+4
         ED    SERATE-1(4),WORK+6
***********************************************************************
*                                                                     *
*                   Interval Service Accumulation                     *
*                                                                     *
***********************************************************************
NOSRV    L     R7,OUCBWMS         service units - transaction
         CVD   R7,WORK            convert to decimal.
         MVC   $UCBWMS(6),PTRN1+1
         MVO   WORK,WORK
         MVI   WORK+7,X'0C'
         ED    $UCBWMS-1(7),WORK+4
***********************************************************************
*                                                                     *
*              I/O Count used for Recommendation value                *
*                                                                     *
***********************************************************************
         LH    R9,ASCBIOSM        R9 =  I/O service measure
         LH    R10,OUXBIOS        R10 = WLM base I/O measurement
         SLR   R9,10              I/O count for this in core interval
         N     R9,=X'0000FFFF'    clear high order bytes
         CVD   R9,WORK
         MVC   IOC(5),PTRN1+2
         ED    IOC-1(6),WORK+5
***********************************************************************
*                                                                     *
*              TSLS - TIME SINCE Last swap (seconds)                  *
*                                                                     *
***********************************************************************
         L     R8,16              R8 = addr of CVT
         USING CVT,R8
         L     R8,CVTOPCTP        R8 = addr of RMCT
         DROP  R8
         L     R8,X'7C'(R8)       R8 = time-of-day
         S     R8,OUCBTMS         subtract time of last swap action
         SRDL  R8,32              convert to HH.MM.SS
         D     R8,=F'1024'        convert to seconds
         CVD   R9,WORK
         MVC   TSLS(5),PTRN1+2
         ED    TSLS-1(6),WORK+5
*
*
         B     LINEDONE
         EJECT
***********************************************************************
*                                                                     *
*                      Elapsed Transaction time                       *
*                                                                     *
***********************************************************************
GENERAL  L     R8,16              R8 = addr of CVT
         USING CVT,R8
         L     R8,CVTOPCTP        R8 = addr of RMCT
         DROP  R8
         L     R8,X'7C'(R8)       R8 = current TOD
         S     R8,OUCBTMO         subtract transaction start time
         SRDL  R8,32              convert to HH.MM.SS
         D     R8,=F'1024'        convert to seconds
         ST    R9,TRANSSEC        store seconds for later
         SR    R8,R8              get hours
         D     R8,=F'3600'
         CVD   R9,PACK
         UNPK  HH,PACK+6(R2)
         OI    HH+1,X'F0'
         MVI   HH+2,C':'
         SRDL  R8,32              get minutes
         D     R8,=F'60'
         CVD   R9,PACK
         UNPK  MM(2),PACK+6(2)
         OI    MM+1,X'F0'
         MVI   MM+2,C':'          get seconds
         CVD   R8,PACK
         UNPK  SS(2),PACK+6(2)
         OI    SS+1,X'F0'
***********************************************************************
*                                                                     *
*                          Address Space ID                           *
*                                                                     *
***********************************************************************
         L     R9,ASCBADDR        R9 = addr of entry in ASVT table
         S     R9,FRSTASVT        minus addr of first entry
         SRL   R9,2               divide by 4 = ASID
         LR    R7,R9              R7 = ASID
         SRL   R7,4               convert first hex character
         IC    R8,HEX(R7)
         STC   R8,ASID
         LR    R7,R9              R7 = ASID
         N     R7,=F'15'          convert second hex character
         IC    R8,HEX(R7)
         STC   R8,ASID+1
***********************************************************************
*                                                                     *
*                           Task CPU time                             *
*                                                                     *
***********************************************************************
         LM    R8,R9,ASCBEJST     EJST (CPU time - task type)
         SRDL  R8,12              convert to microsec
         D     R8,=F'100000'      convert to seconds x 10-3
         CVD   R9,WORK            convert to decimal.
         MVC   CPU(6),PTRN2
         ED    CPU-1(7),WORK+5
***********************************************************************
*                                                                     *
*                            Stepname                                 *
*                                                                     *
***********************************************************************
         ICM   R7,B'1111',ASCBJBNI jobname pointer
         BZ    SML                jobname not available
         MVC   PROCSTEP(8),24(R7) move in procstepname
         MVC   STEPNAME,56(R7)    move in stepname
         B     CONT
SML      ICM   R7,B'1111',ASCBJBNS S/M/L pointer
         BZ    CONT               S/M/L not available
         CLI   JOB,C'*'           is this *MASTER* ?
         BE    TSO                yes, treat it like a userid
         TM    OUCBYFL,OUCBSTT    is this a started task?
         BNO   TSO                no, so use TSO stepname
         B     CONT
TSO      MVC   STEPNAME,8(R7)     move in stepname for LOGON
CONT     EQU    *
***********************************************************************
*                                                                     *
*                    M/S/L                                            *
*                                                                     *
***********************************************************************
         TM    OUCBYFL,OUCBSTT    START created task?
         BZ    TEST2              no
         MVI   MSL,C'S'
         B     CONT2
TEST2    TM    OUCBYFL,OUCBLOG    LOGON created task?
         BZ    TEST3              no
         MVI   MSL,C'L'
         B     CONT2
TEST3    TM    OUCBYFL,OUCBMNT    MOUNT created task?
         BZ    CONT2              no
         MVI   MSL,C'M'
***********************************************************************
*                                                                     *
*                  # TCBs Active                                      *
*                                                                     *
***********************************************************************
CONT2    L     R7,ASCBTCBS         R7 = no. of active TCBs
         CVD   R7,WORK
         UNPK  TCBA,WORK+7(1)
         OI    TCBA,X'F0'
***********************************************************************
*                                                                     *
*                  Transaction Service Accumulation                   *
*                                                                     *
***********************************************************************
         TM    OUCBQFL,OUCBOUT    address space swapped in
         BO    OUTRANS            yes
         L     R7,OUXBTRS         accumulated service
         CVD   R7,WORK            convert to decimal.
         MVC   $UXBTRS(7),PTRN1
         ED    $UXBTRS-1(8),WORK+4
         B     LINEDONE
OUTRANS  MVC   $UXBTRS(7),BLANKS  OUXB not available
*
*        Move Line to buffer
*
LINEDONE MVC   0(81,R1),LINE      move the line to the 3270 buffer.
         LH    R7,SCRSIZE         increment 'TPUT' size
         LA    R7,81(R7)          add 81 to current buffer length
         STH   R7,SCRSIZE         store away again
         LA    R1,81(R1)          R1 = addr of bottom of buffer
         C     R1,ADBUF           have we reached the bottom?
         BE    PUTIT              yes, go put this screen.
NOGOOD   BCT   R3,ASCBLOOP        process next address space
         DROP  R4,R5,R6
         EJECT
***********************************************************************
*                                                                     *
*              Put the SWAP buffer to the 3270 screen                 *
*                                                                     *
***********************************************************************
PUTIT    CLC   JOBMASK(8),BLANKS  is the jobmask blank?
         BNE   CHKTYPE            no, so skip
         IC    R8,PAGE            load current page character
         LA    R8,1(R8)           add one to it
         STC   R8,PAGE            store it back
         CLC   SCRSIZE(2),=H'0'   is the screen empty?
         BE    FINDCVT            yes - go do the screen over
CHKTYPE  CLI   CRTFLAG,X'FF'      is this a CRT?
         BE    CRT1               yes
         LA    R1,ERROR           R1 = addr of first line
         LA    R0,79              R0 = length of heading
         ICM   R1,8,EDITFLG       edit mode
         TPUT  (1),(0),R          write line
         LA    R1,HEADINGA+1      R1 = addr of first line
         LA    R0,79              R0 = length of heading
         ICM   R1,8,EDITFLG       edit mode
         TPUT  (1),(0),R          write line
         LH    R7,SCRSIZE         R7 = total screensize
         LA    R8,81              R8 = one line
         LA    R1,BUFFER+2        set pointer to first line
         ICM   R1,8,EDITFLG       edit mode
         LA    R0,79              R0 length of output line
NEXTL    LR    R2,R1              save R1 since TPUT zaps it
         TPUT  (1),(0),R          print one line
         LA    R8,81(R8)          add 81 bytes
         CR    R7,R8              have we printed last line?
         BL    CONTA              yes, continue
         LA    R1,81(R2)          nope, point to next line
         LA    R0,79              load length
         ICM   R1,8,EDITFLG       edit mode
         B     NEXTL              print next line
CRT1     LA    R1,CLEAR           R1 = addr of buffer
         LA    R0,LENGTH          R0 = length of buffer
         AH    R0,SCRSIZE         R0 = full length of buffer
         ICM   R1,8,FULLSCR       full screen mode
         TPUT  (1),(0),R          put screen
CONTA    MVC   ERROR(27),BLANKS   blank out error field
         CLI   WAITSW,X'FF'       are we in wait mode?
         BNE   GETCHAR            no, read from terminal
***********************************************************************
*                                                                     *
*                           'Wait' mode                               *
*                                                                     *
***********************************************************************
         STIMER WAIT,BINTVL=DELAY yes, wait 1 second
         L     R10,COUNTER        load counter
         BCTR  R10,0              counter = counter - 1
         ST    R10,COUNTER        store back
         CVD   R10,WORK           convert to decimal.
         MVC   SCRATCH(4),PATTERN move in edit pattern
         ED    SCRATCH(4),WORK+6  edit in time
         MVC   TIME(3),SCRATCH+1  move in time
         LTR   R10,R10            counter = 0 ?
         BNZ   CONTINUE           refresh if still counting
         XI    WAITSW,X'FF'       counter up, do reads again
         MVC   TIME(3),BLANKS     clear counter field
         B     CONTINUE           refresh one last time
***********************************************************************
*                                                                     *
*                       Read Command from user                        *
*                                                                     *
***********************************************************************
GETCHAR  MVC   REPLY(33),BLANKS   blank out reply field
         TGET  REPLY,33           get 79 characters from terminal
         CLI   REPLY,X'6E'        reshow code?
         BE    GETCHAR            yes; ignore it
         OC    REPLY(33),BLANKS   convert chars to upper case
         XC    CBUF+2(2),CBUF+2   clear offset
         MVC   CSPLBLOK(24),CSPLSAVE  copy in CSPL blok
         XC    CSOABLOK(8),CSOABLOK
         L     R2,SAVEJSCB        R2 = addr of JSCB
         USING IEZJSCB,R2
***********************************************************************
*                                                                     *
*     IKJSCAN is being called merely to allow the 'X' feature to      *
*     function if desired. If SWAP is being used in conjunction       *
*     with 'SPY', then SWAP must be APF authorized. Since 'X' will    *
*     not work with authorized programs, we flip off the auth bit     *
*     in the JSCB to fool it.                                         *
*                                                                     *
***********************************************************************
*        NI    JSCBOPTS,X'FF'-JSCBAUTH turn off auth to fool PCFSCAN
         LA    R1,CSPLBLOK        R1 = addr of cmd scan parm list
         L     R15,ADDRSCAN       R15 = addr of IKJSCAN
         BALR  R14,R15            invoke IKJSCAN
*        OI    JSCBOPTS,JSCBAUTH  turn APF bits back on
         DROP  R2
         LA    R1,CSOABLOK        R1 = addr of cmd scan output area
         USING CSOA,R1
         TM    CSOAFLG,CSOANOC    is the buffer empty?
         BO    CONTINUE           yes; just go refresh
         DROP  R1
***********************************************************************
*                                                                     *
*                       W  --  Enter wait mode                        *
*                                                                     *
***********************************************************************
WAITCHK  CLI   REPLY,C'W'         go into wait mode?
         BNE   CDELAY             no
         XI    WAITSW,X'FF'       yes, toggle wait switch
         LA    R2,30              set default value = 30
         LA    R15,CONVBIN        branch to conversion rtn
         BALR  R14,R15            EBCDIC to binary
         MVC   SCRATCH(4),PATTERN move in edit pattern
         ED    SCRATCH(4),WORK+6  edit in time left
         MVC   TIME(3),SCRATCH+1  move time left into place
         ST    R2,COUNTER         store starting timer value
         B     CONTINUE           all set - go display next page
***********************************************************************
*                                                                     *
*            D  --  Set timer delay in tenths of a second             *
*                                                                     *
***********************************************************************
CDELAY   CLI   REPLY,C'D'         are we changing the time delay?
         BNE   ENDCHK             no, so continue
         LA    R2,10              set default value = 10 tenths second
         LA    R15,CONVBIN        branch to conversion rtn
         BALR  R14,R15            EBCDIC to binary
         MVC   SCRATCH(5),DPATTRN move in edit pattern
         ED    SCRATCH(5),WORK+6  edit in delay time
         MVC   PAUSE(3),SCRATCH+2 move time left into place
         MH    R2,=H'10'          convert to 100ths of a second
         ST    R2,DELAY           store wait delay value
         B     CONTINUE           all set - go display next page
***********************************************************************
*                                                                     *
*                          E  --  End SWAP                            *
*                                                                     *
***********************************************************************
ENDCHK   CLI   REPLY,C'E'         termination requested ?
         BE    STOP               all done
***********************************************************************
*                                                                     *
*                   T  --  TSO Address spaces only                    *
*                                                                     *
***********************************************************************
TSOCHK   CLI   REPLY,C'T'         TSO only ?
         BNE   BATCHCHK           no
         MVI   MODESW,X'FF'       indicate change of modes.
         MVI   BATCHORN+1,X'80'   reset the branches
         MVI   TSOORNO+1,X'00'    only TSO memories.
         MVI   MODETBA,C'T'       set mode indicator
         B     CONTINUE
***********************************************************************
*                                                                     *
*                  B  --  Batch Address spaces only                   *
*                                                                     *
***********************************************************************
BATCHCHK CLI   REPLY,C'B'         batch only ?
         BNE   BOTHCHK            no
         MVI   MODESW,X'FF'       indicate change of modes.
         MVI   TSOORNO+1,X'10'    reset the branches
         MVI   BATCHORN+1,X'00'   only batch (STC) memories.
         MVI   MODETBA,C'B'       set mode indicator
         B     CONTINUE
***********************************************************************
*                                                                     *
*             A  --  All Address Spaces (Batch and TSO)               *
*                                                                     *
***********************************************************************
BOTHCHK  CLI   REPLY,C'A'         batch and TSO?
         BNE   OUTCHK             no
         MVI   MODESW,X'FF'       indicate change of modes.
         MVI   TSOORNO+1,X'00'    yes, so NOP all the
         MVI   BATCHORN+1,X'00'   selection branches.
         MVI   MODETBA,C'A'       set mode indicator
         B     CONTINUE
***********************************************************************
*                                                                     *
*        O  --  Address Spaces which are 'out' and not ready          *
*                                                                     *
***********************************************************************
OUTCHK   CLI   REPLY,C'O'         out mode ?
         BNE   INCHK              no
         MVI   MODESW,X'FF'       indicate change of modes.
         MVI   INOROUT+1,X'00'    nop the branch
         MVI   MODEIO,C'O'        set mode indicator
         B     CONTINUE
***********************************************************************
*                                                                     *
*             I  --  Address Spaces which are swapped in              *
*                                                                     *
***********************************************************************
INCHK    CLI   REPLY,C'I'         in or ready mode ?
         BNE   FINDCHK            no
         MVI   INOROUT+1,X'10'    branch
         MVI   MODESW,X'FF'       indicate mode switch.
         MVI   MODEIO,C'I'        set mode indicator
         B     CONTINUE
***********************************************************************
*                                                                     *
*                F  --  Display only certain jobnames                 *
*                                                                     *
***********************************************************************
FINDCHK  CLI   REPLY,C'F'         should we set the jobmask?
         BNE   SRMCHK             no
         MVI   MODESW,X'FF'       indicate mode switch.
         MVC   JOBMASK(8),REPLY+1 move mask into place
         MVC   MASKNAME(8),JOBMASK move mask indicator into place
         CLC   JOBMASK(8),BLANKS  is jobmask blank?
         BNE   CONTINUE           no, go on
         MVC   MASKNAME(8),=C'  Page 1'
         B     CONTINUE
***********************************************************************
*                                                                     *
*                      S  --  SRM display mode                        *
*                                                                     *
***********************************************************************
SRMCHK   CLI   REPLY,C'S'         SRM mode ?
         BNE   GENCHK             no
         MVI   SRMSW,X'FF'        indicate SRM mode
         MVC   HEADINGA(80),HEADING1 move in SRM heading
         MVC   MODEGS(7),=C'S.R.M. ' set mode indicator
         B     CONTINUE
***********************************************************************
*                                                                     *
*                    G  --  General display mode                      *
*                                                                     *
***********************************************************************
GENCHK   CLI   REPLY,C'G'         general mode ?
         BNE   LINKSPY            no
         MVI   SRMSW,X'00'        indicate general mode
         MVC   HEADINGA(80),HEADING2 move in general heading
         MVC   MODEGS(7),=C'General' set mode indicator
         B     CONTINUE
***********************************************************************
*                                                                     *
*                         L  --  Link to SPY                          *
*                                                                     *
***********************************************************************
LINKSPY  CLI   REPLY,C'L'         XCTL to SPY?
         BNE   NEEDHELP           no
         L     R13,4(R13)         callers save area pointer.    WDA#01
         XCTL  (2,12),EP=SPY      XCTL to SPY
***********************************************************************
*                                                                     *
*               ?  --  Display help for SWAP commands                 *
*                                                                     *
***********************************************************************
NEEDHELP CLI   REPLY,C'?'         user needs help page displayed?
         BNE   BADCMD
         L     R1,HLP1ADDR        R1 = addr of help
         LA    R0,HELPLEN1        R0 = length of help
         ICM   R1,8,FULLSCR       full screen mode
         TPUT  (1),(0),R          write help to screen
         TGET  REPLY,33           go get a command
         L     R1,HLP2ADDR        R1 = addr of help
         LA    R0,HELPLEN2        R0 = length of help
         ICM   R1,8,FULLSCR       full screen mode
         TPUT  (1),(0),R          write help to screen
         B     GETCHAR            go get a command
***********************************************************************
*                                                                     *
*                     Blank or Invalid commands                       *
*                                                                     *
***********************************************************************
BADCMD   CLI   REPLY,C' '         was a blank just entered?
         BE    CONTINUE
         MVC   ERROR(27),ERRMSG2  move in bad command msg
CONTINUE MVC   SCRSIZE,=H'0'      reset screen size.
         LA    R1,BUFFER          ---+
         LA    R6,BUFFER             |
         ICM   R6,8,C' '             | move character long to fill
         LA    R7,22*81              | screen buffer with blanks
         LA    R8,BUFFER             |
         SR    R9,R9                 |
         MVCL  R6,R8              ---+
         CLI   MODESW,X'FF'       change of modes ?
         BNE   MODEOK             branch if same mode.
         MVI   MODESW,X'00'       reset mode change.
         B     FINDCVT            start over from beginning.
MODEOK   LTR   R3,R3              more memories to process ?
         BNZ   NOGOOD             yes.
         CLI   ATTNFLG,X'00'      was attn hit?
         BE    FINDCVT            no
         MVI   ATTNFLG,X'00'      yes, reset flag
         MVC   TIME(3),BLANKS     blank out timer field
         MVI   WAITSW,X'00'       turn off wait flag
         LA    R3,0               R3 = 0
         ST    R3,COUNTER         set timer to 0
         B     FINDCVT            otherwise, start over from the top.
STOP     CLI   CRTFLAG,X'00'      is this a CRT?
         BE    STOP1              no; don't clear
         LA    R1,CLR             R1 = addr of clear string
         LA    R0,CLRLEN          R0 = length of clear string
         ICM   R1,8,FULLSCR       full screen mode
         TPUT  (1),(0),R          clear the screen before we leave
         STFSMODE OFF             turn off fullscreen mode
STOP1    L     R13,4(R13)                                       WDA#01
         RETURN (14,12),RC=0
VARCLC1  CLC   JOBMASK(0),JOB
         EJECT
***********************************************************************
*                                                                     *
*            Convert EBCDIC numbers from user into binary             *
*                                                                     *
***********************************************************************
CONVBIN  CVD   R2,WORK            convert to decimal.
         CLI   REPLY+1,C' '       did he enter a number?
         BE    RTRN               no, use the default
         CLI   REPLY+1,C'0'       is the hex code < 'F0' ?
         BL    BADCHAR            yes, error
         CLI   REPLY+1,C'9'       is the hex code > 'F9' ?
         BH    BADCHAR            yes, error
         PACK  WORK(8),REPLY+1(1) pack EBCDIC (assume 1 digit)
         CLI   REPLY+2,C' '       did he enter 2 digits?
         BE    CVB                no, dont do the 2 digit pack
         CLI   REPLY+2,C'0'       is the hex code < 'F0' ?
         BL    BADCHAR            yes, error
         CLI   REPLY+2,C'9'       is the hex code > 'F9' ?
         BH    BADCHAR            yes, error
         PACK  WORK(8),REPLY+1(2) pack again, with 2 digits this time
CVB      CVB   R2,WORK            get binary
RTRN     BR    R14                return to mainline
BADCHAR  MVC   ERROR(27),ERRMSG1  numeric error
         B     RTRN
         EJECT
***********************************************************************
*                                                                     *
*          Setup and Initialize Parameter list for IKJSCAN            *
*                                                                     *
***********************************************************************
INITSCAN STM   R15,R4,SAVE154     save regs
         L     R3,CVTPTR          R3 = addr of CVT
         L     R3,0(R3)           R3 = addr of addr of TCB
         L     R3,4(R3)           R3 = addr of TCB
         USING TCB,R3
         ICM   R3,7,TCBJSCBB      R3 = addr of JSCB
         ST    R3,SAVEJSCB        save addr of JSCB
         DROP  R3
         USING IEZJSCB,R3
         L     R4,JSCBPSCB        R4 = addr of PSCB
         DROP  R3
         USING PSCB,R4
         L     R3,PSCBUPT         R3 = addr of UPT
         ST    R3,SAVEUPT         save addr of UPT
         LA    R2,CSPLSAVE        R2 = addr of cmd scan parm list
         USING CSPL,R2
         ST    R3,CSPLUPT         save UPT addr
         L     R3,PSCBRLGB        R3 = addr of relogon buffer
         L     R3,256(R3)         R3 = addr of ECT
         ST    R3,CSPLECT         save ECT addr
         LA    R3,CPECB           R3 = addr of fake ECB for this CP
         ST    R3,CSPLECB         save ECB addr
         LA    R3,FLAGWORD        R3 = addr of CSPL flag word
         ST    R3,CSPLFLG         save flag addr
         LA    R3,CSOABLOK        R3 = addr of cmd scan output area
         ST    R3,CSPLOA          save output area addr
         LA    R3,CBUF            R3 = addr of 'cmd buffer'
         ST    R3,CSPLCBUF        save cbuf addr
         DROP  R2,R4
         LOAD  EP=IKJSCAN
         ST    R0,ADDRSCAN
         LM    R15,R4,SAVE154     restore regs 15 - 4
         BR    R14
         DS    0F
SAVE154  DS    6F
SAVEUPT  DS    F                   save addr of UPT
SAVEJSCB DS    F                   save addr of JSCB
CSPLBLOK DS    6F                  cmd scan parameter list
CSPLSAVE DS    6F
CPECB    DC    F'0'                fake ECB for this CP
FLAGWORD DC    F'0'
CSOABLOK DS    2F
CBUF     DC    AL2(37),AL2(0)
REPLY    DC    CL33' '             user's command input buffer
ADDRSCAN DS    A
         DROP  R12
         EJECT
***********************************************************************
*                                                                     *
*                          A T T N E X I T                            *
*                                                                     *
*         Trap userd Attention Interrupts and flag for reset          *
*                                                                     *
***********************************************************************
ATTNEXIT SAVE  (14,12)            save the callers registers
         LR    R7,R15             establish
         USING ATTNEXIT,R7        addressability.
         LA    R11,SAVEA          set-up
         ST    R13,SAVEA+4         save
         ST    R11,8(,R13)          area
         LR    R13,R11               chaining
         MVI   ATTNFLG,X'FF'      set attn flag
         L     R13,SAVEA+4        callers save area pointer.
         RETURN (14,12),RC=0      and away we go...
SAVEA    DS    18F
         DROP  R7
         EJECT
***********************************************************************
*                                                                     *
*                             Constants                               *
*                                                                     *
***********************************************************************
STAXLIST STAX  ATTNEXIT,MF=L
PACK     DS    D
WORK     DS    D
SCRATCH  DS    D                  scratch area
ASCBADDR DS    F                  ASCB addr save area
FRSTASVT DS    F                  first ASVT entry save area
COUNTER  DC    F'0'               wait counter
TRANSSEC DS    F                  transaction time in seconds
DELAY    DC    F'100'             2 second delay for refresh
HLP1ADDR DC    A(HELP1)
HLP2ADDR DC    A(HELP2)
ADBUF    DC    A(0)
SCRSIZE  DC    H'0'
ATTNFLG  DC    X'00'              attention flag
WAITSW   DC    X'00'              wait mode
MODESW   DC    X'00'              mode change.
SRMSW    DC    X'00'              SRM mode.
PTRN1    DC    X'20202020202120'  edit pattern
PTRN2    DC    X'202021204B202020'
PATTERN  DC    X'40202020'        edit pattern
DPATTRN  DC    X'4021204B20'      edit pattern
BLANKS   DC    CL40' '
CODETABL DC    C'O'  Swap Code 1 : Terminal output wait
         DC    C'I'  Swap Code 2 : Terminal input wait
         DC    C'W'  Swap Code 3 : Long Wait
         DC    C'A'  Swap Code 4 : Auxiliary Storage Shortage
         DC    C'R'  Swap Code 5 : Real Storage Shortage
         DC    C'V'  Swap Code 6 : MS0 Detected Wait
         DC    C'S'  Swap Code 7 : REQSWAP SYSEVENT Issued
         DC    C'E'  Swap Code 8 : ENQHOLD Exchg by swap analysis
         DC    C'X'  Swap Code 9 : EXCHG Recommended by swap analysis
         DC    C'$'  Swap Code A : Unilateral Swapout
HEX      DC    C'0123456789ABCDEF'
START    DC    C'Starting'
         EJECT
***********************************************************************
*                                                                     *
*                        Format Display Line                          *
*                                                                     *
***********************************************************************
X        DC    CL80' '
         ORG   X                  Byte
LINE     DC    X'1D60'            0     Protected, low intensity
JOB      DC    CL8' '             2
         DC    C' '               10
         ORG   X+11               For General Listing
PROCSTEP DC    CL8' '             11
         DC    C' '               19
STEPNAME DC    CL8' '             20
         DC    CL3' '             28
ASID     DC    CL2' '             31
         DC    CL2' '             33
CPU      DC    CL6' '             35
         DC    C' '               41
         ORG   X+11               For SRM Listing
$UCBPGN  DC    CL2' '             11
         DC    C' '               13
$UCBPGP  DC    C' '               14
         DC    C' '               15
$UCBDOM  DC    CL2' '             16
         DC    C' '               18
DP       DC    CL2' '             19
         DC    C' '               21
DQ       DC    CL2' '             22
         DC    C' '               24
$UCBRMR  DC    CL2' '             25
         DC    C' '               27
$UCBWMR  DC    CL2' '             28
         DC    C' '               30
SERATE   DC    CL3' '             31
         DC    C' '               34
$UCBWMS  DC    CL6' '             35
         DC    C' '               41
         ORG   X+42               For General and SRM Listings
TCPU     DC    CL6' '             42
         DC    C' '               48
$UCBSWC  DC    CL2' '             49
         DC    C' '               51
STATUS   DC    C' '               52
         DC    C' '               53
LOC      DC    C' '               54
         DC    C' '               55
MEM      DC    CL3' '             56
         DC    C' '               59
         ORG   X+60               For SRM listing
$UCBWSS  DC    CL3' '             60
         DC    C' '               63
IOC      DC    CL5' '             64
         DC    C' '               69
TSLS     DC    CL5' '             70
         DC    C' '               75
         ORG   X+60               For General Listing
HH       DC    CL2' '             60
         DC    C':'               62
MM       DC    CL2' '             63
         DC    C':'               65
SS       DC    CL2' '             66
         DC    C' '               68
MSL      DC    C' '               69
         DC    C' '               70
$UXBTRS  DC    CL7' '             71
         DC    C' '               78
TCBA     DC    CL1' '             79
         DC    C' '               80
         ORG X+90
FULLSCR  DC    X'03'
EDITFLG  DC    X'00'
CRTFLAG  DC    X'FF'
JOBMASK  DC    CL8'        '
ERRMSG1  DC    CL27' ERROR - Non-numeric value '
ERRMSG2  DC    CL27' ERROR - Invalid command   '
         LTORG
HEADING1 DC CL80' Jobname  PG P Dm DP DQ RM WR SU/S  S.U.  TCPU  SC R LX
                Mem WSS   I/O  TSLS       '
HEADING2 DC CL80' Jobname  Procstep Stepname  ASID   CPU   TCPU  SC R LX
                Mem HH:MM:SS MSL S.U. TCB '
CLR      DC    X'C1'               WCC - Clear screen at end of pgm
         DC    X'115D7E'           SBA to row 24, col 79 (FSE 5.0)
         DC    X'114040'           SBA to row 1, col 1
         DC    X'3C404000'         Fill screen with nulls
         DC    X'114040'           SBA to row 1, col 1
         DC    X'13'               Insert cursor
CLRLEN   EQU   *-CLR
CLEAR    DC    X'C1'               WCC
         DC    X'115D7F'           SBA to row 24, col 80 (FSE 5.0)
         DC    X'114040'           SBA to row 1, col 1
         DC    X'3C404000'         Fill screen with nulls
PHEADING DC    X'114040'           SBA to row 1, COL 1
         DC    X'1D40'             ATTR Byte - unprotected, low intens.
         DC    X'13'               Insert cursor
         DC    C'   '
ERROR    DC    CL30' '
CMDCTRL1 DC    X'1DE8'             ATTR Byte - protected, hi intens.
         DC    CL7'Timer: '
TIME     DC    CL3'   '
SLASH    DC    CL1'/'
PAUSE    DC    CL3'1.0'
         DC    CL5'     '
         DC    CL6'Mode: '
MODETBA  DC    CL1'B'
MODEIO   DC    CL2'O '
MODEGS   DC    CL9'General  '
MASKNAME DC    CL7'  Page '
PAGE     DC    CL1'1'
HEADINGA DS    CL80' '
BUFFER   DC    41CL81' '
LENGTH   EQU   BUFFER-CLEAR
         EJECT
HELP1    DC    X'C1'               WCC
         DC    X'115D7F'           SBA to row 24, col 80 (FSE 5.0)
         DC    X'114040'           SBA to row 1, col 1
         DC    X'3C404000'         Fill screen with nulls
         DC    X'114040',X'1DE8',C'Command      Description'
         DC    X'11C150'
         DC    X'11C260',C'   A         Display Started tasks, batch '
         DC    C'and TSO'
         DC    X'11C3F0',C'   B         Display Started tasks and '
         DC    C'batch only'
         DC    X'11C540',C'   DXX       Set delay to XX tenths seconds'
         DC    X'11C650',C'   E         End SWAP'
         DC    X'11C760',C'   F         Display all Jobnames'
         DC    X'11C8F0',C'   FXXXXXXXX Display only Job ''XXXXXXXX'''
         DC    X'114A40',C'   G         General Information Will be '
         DC    C'displayed'
         DC    X'114B50',C'   I         Display only swapped-in '
         DC    C'memories'
         DC    X'114C60',C'   O         Display both in and out '
         DC    C'memories'
         DC    X'114DF0',C'   L         LINK to program SPY '
         DC    C'(if available)'
         DC    X'114F40',C'   S         SRM information displayed'
         DC    X'115050',C'   T         Display TSO jobs only'
         DC    X'11D160',C'   W         Start timer mode for 30 '
         DC    C'seconds'
         DC    X'11D2F0',C'   WXX       Start timer mode for XX '
         DC    C'seconds'
         DC    X'11D440',C'   W0        Start timer mode until '
         DC    C'interrUPT'
         DC    X'11D550',C'   ?         Display this page'
         DC    X'11D660'
         DC    X'11D7F0',C'Hitting INTERRUPT will stop the wait timer'
         DC    X'11D940'
         DC    X'115A50',C'*WARNING*  INTERRUPT may be ignored if '
         DC    C'timer is set too fast (< .4 sec)'
         DC    X'115B60'
         DC    X'115CF0',C'Hit ENTER to continue'
         DC    X'115DC61D40134040401DE8'
HELPMRK1 EQU   *
HELPLEN1 EQU   HELPMRK1-HELP1
         EJECT
HELP2    DC    X'C1'               WCC
         DC    X'115D7F'           SBA to row 24, col 80 (FSE 5.0)
         DC    X'114040'           SBA to row 1, col 1
         DC    X'3C404000'         Fill screen with nulls
         DC    X'114040',X'1DE8',C'Swap Code    Description'
         DC    X'11C150'
         DC    X'11C260',C'   A       Auxilary storage shortage'
         DC    X'11C260',C'   E       ENQHOLD exchange'
         DC    X'11C3F0',C'   I       Terminal input wait'
         DC    X'11C540',C'   O       Terminal output wait'
         DC    X'11C650',C'   R       Real storage shortage'
         DC    X'11C760',C'   S       REQSWAP SYSEVENT issued'
         DC    X'11C8F0',C'   V       MS0 detected wait'
         DC    X'114A40',C'   W       Long wait'
         DC    X'114B50',C'   X       Exchange swap'
         DC    X'114C60',C'   $       Unilateral swapout'
         DC    X'114DF0',C'   ?       Unknown'
         DC    X'114F40'
         DC    X'115050',C'Loc. Code   Description'
         DC    X'11D160'
         DC    X'11D2F0',C'    I      Swapped in and eligible to run'
         DC    X'11D440',C'    O      Swapped out but ready to run'
         DC    X'11D550',C'    W      Swapped out and not ready to run'
         DC    X'11D660',C'    $      Swapped in: V=R or non-swappable'
         DC    X'11D7F0',C'    ?      In transition between states'
         DC    X'11D940',C'    L      Logically Swapped'
         DC    X'115A50'
         DC    X'115B60',C'Version 4.5'
         DC    X'115CF0',C'Hit ENTER to continue'
         DC    X'115DC61D40134040401DE8'
HELPMRK2 EQU   *
HELPLEN2 EQU   HELPMRK2-HELP2
         EJECT
***********************************************************************
*                                                                     *
*                System Control Block Mapping DSECTs                  *
*                                                                     *
*     All macros can be found in either 'SYS1.MACLIB' or in           *
*     'SYS1.AMODGEN'.                                                 *
*                                                                     *
***********************************************************************
         PRINT NOGEN
         CVT   DSECT=YES          Communications Vector Table
         IEZJSCB ,                Job Step Control Block
         SPACE 2
         IHAASCB ,                Address Space Control Block
         SPACE 2
         IHAASVT ,                Address Space Vector Table
         SPACE 2
         IHAOUXB ,                SRM User Extension Block
         SPACE 2
         IKJCSOA ,                Command Scan OuTPUT Area
         SPACE 2
         IKJCSPL ,                Command Scan Parameter List
         SPACE 2
         IKJPSCB ,                Protected Step Control Block
         SPACE 2
         IKJTCB  ,                Task Control Block
         SPACE 2
         IRAOUCB ,                SRM User Control Block
         SPACE 2
         END   SWAP
