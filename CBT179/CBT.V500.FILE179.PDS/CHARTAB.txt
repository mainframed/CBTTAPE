         MACRO
&NAME    CHARTAB &FILL=' ',&LC=YES,&TYPE=STANDARD
.*.
.*$MACRO=CHARTAB    DATE=08/23/79       SOURCE=LDW
.*  UPDATES:
.*    07/12/82  LDW  SUPPORT TYPE=FROM-ASCII AND LC=FOLD
.*
.*  THIS MACRO GENERATES A TRANSLATE TABLE TO TRANSLATE UNPRINTABLES
.*  TO A FILL CHARACTER.
.*.
&NAME    DC    256C&FILL
         AIF   ('&TYPE' EQ 'STANDARD').STD
         AIF   ('&TYPE' EQ 'FROM-ASCII').FASCII
         MNOTE 8,'TYPE=&TYPE UNKNOWN, TYPE=STANDARD ASSUMED'
.STD     AIF   ('&LC'(1,1) EQ 'N').NOLC1
         AIF   ('&LC'(1,1) EQ 'F').FOLD1
         ORG   &NAME+C'A'-X'40'
         DC    X'818283848586878889'
         ORG   &NAME+C'J'-X'40'
         DC    X'919293949596979899'
         ORG   &NAME+C'S'-X'40'
         DC    X'A2A3A4A5A6A7A8A9'
         AGO   .NOLC1
.FOLD1   ORG   &NAME+C'A'-X'40'
         DC    C'ABCDEFGHI'
         ORG   &NAME+C'J'-X'40'
         DC    C'JKLMNOPQR'
         ORG   &NAME+C'S'-X'40'
         DC    C'STUVWXYZ'
.NOLC1   ORG   &NAME+C'A'
         DC    C'ABCDEFGHI'
         ORG   &NAME+C'J'
         DC    C'JKLMNOPQR'
         ORG   &NAME+C'S'
         DC    C'STUVWXYZ'
         ORG   &NAME+C'0'
         DC    C'0123456789'
         ORG   &NAME+C' '
         DC    C' '
         ORG   &NAME+C'.'
         DC    C'.<(+|&&'
         ORG   &NAME+C'$'
         DC    C'$*);¬-/'
         ORG   &NAME+C','
         DC    C',%_>?'
         ORG   &NAME+C':'
         DC    C':#@''="'
         ORG   &NAME+256
         MEXIT
.FASCII  ORG   &NAME+X'20'
         DC    C' !"#$%&&''()*+,-./'
         DC    C'0123456789:;<=>?'
         DC    C'@ABCDEFGHIJKLMNO'
         DC    C'PQRSTUVWXYZ'
         ORG   &NAME+X'5C'
         DC    C'/'
         ORG   &NAME+X'5F'
         DC    C'_`'
         AIF   ('&LC'(1,1) EQ 'N').NOLC2
         AIF   ('&LC'(1,1) EQ 'F').FOLD2
         DC    X'818283848586878889'
         DC    X'919293949596979899'
         DC    X'A2A3A4A5A6A7A8A9'
         AGO   .NOLC2
.FOLD2   DC    C'ABCDEFGHI'
         DC    C'JKLMNOPQR'
         DC    C'STUVWXYZ'
.NOLC2   DC    C'{¦}~'
         ORG   &NAME+256
         MEND
