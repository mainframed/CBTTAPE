         MACRO
         REGEQU  &F=,&FR=,&PREFIX=R,&STARS=YES,&DUPMSG=YES
.*.
.*$MACRO=REGEQU     DATE=11/26/78       SOURCE=LDW
.*
.*  THIS MACRO WILL GENERATE EQU'S FOR SYMBOLIC REGISTERS IF THEY HAVE
.*       NOT BEEN PREVIOUSLY GENERATED BY THIS MACRO.
.*
.* PREFIX=R  SPECIFIES PREFIX FOR GENERAL REGISTER EQUATES.
.* F= (FR=)  SPECIFIES PREFIX FOR FLOATING POINT REG EQUATES.
.*            (DEFAULT IS NO EQUATES FOR F.P. REGS)
.* STARS=YES GENERATE LINE OF ASTERISKS BEFORE AND AFTER EQUATES.
.*
.* MULTIPLE REGEQU'S WITH THE SAME PREFIX WILL BE IGNORED.
.*.
         GBLC  &$REGS$(15),&R,&FF
         LCLA  &X
&R       SETC  '&PREFIX'
.RLOOP   ANOP
&X       SETA  &X+1
         AIF   (&X GT 15).GEN
         AIF   ('&$REGS$(&X)' EQ '').GEN
         AIF   ('&R' EQ '&$REGS$(&X)').NOGEN
         AGO   .RLOOP
.GEN     ANOP
&$REGS$(&X) SETC '&R'
         AIF   ('&STARS' NE 'YES').NOSTAR1
*=====================================================================*
.*
.NOSTAR1 ANOP
&R.0     EQU   0
&R.1     EQU   1
&R.2     EQU   2
&R.3     EQU   3
&R.4     EQU   4
&R.5     EQU   5                        +===========+
&R.6     EQU   6                        |  DEFINE   |
&R.7     EQU   7                        | SYMBOLIC  |
&R.8     EQU   8                        | REGISTERS |
&R.9     EQU   9                        +===========+
&R.10    EQU   10
&R.11    EQU   11
&R.12    EQU   12
&R.13    EQU   13
&R.14    EQU   14
&R.15    EQU   15
.*
         AGO   .CONT11
.NOGEN   ANOP
         AIF   ('&DUPMSG' NE 'YES').CONT11
******** REGEQU ALREADY CODED FOR THIS PREFIX, IGNORED ****************
.CONT11  AIF   ('&F&FR' EQ '').NOF
&X       SETA  0                        RE-INIT
&FF      SETC  '&F&FR'
.FLOOP   ANOP
&X       SETA  &X+1
         AIF   (&X GT 15).FGEN
         AIF   ('&$REGS$(&X)' EQ '').FGEN
         AIF   ('&FF' EQ '&$REGS$(&X)').NOGENF
         AGO   .FLOOP
.FGEN    ANOP
&$REGS$(&X) SETC '&FF'
         SPACE 1
&FF.0    EQU   0
&FF.2    EQU   2
&FF.4    EQU   4
&FF.6    EQU   6
.NOF     AIF   ('&STARS' NE 'YES').NOSTAR2
*=====================================================================*
.NOSTAR2 SPACE
         AGO   .MEND
.NOGENF  ANOP
         AIF   ('&DUPMSG' NE 'YES').MEND
******** REGEQU F= ALREADY CODED FOR THIS PREFIX, IGNORED *************
.MEND    MEND
