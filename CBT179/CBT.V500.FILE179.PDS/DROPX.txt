         MACRO
         DROPX &PARMS
.*.
.*$MACRO=DROPX      DATE=05/10/80       SOURCE=UCLA(JCJ)
.*  THIS IS AN COPY OF OACDROP, UNDER A DIFFERENT NAME
.*  UPDATES:
.*    10/18/84 LDW  CHANGE COMMENT COLUMN
.*
.*  THIS MACRO REMOVES AN ENTRY FROM THE USING TABLE BUILT BY 'USNGX'
.*       (DISPLAYED BY 'INUSE').  IT THEN DOES A REGULAR 'DROP'.
.*.
         GBLC  &USELAB(16)        SAVED PROGRAM LABELS
         GBLC  &USEREG(16)        SAVED REGISTER TABLE
         LCLA  &UNDX,&II          USE INDICES,TEMP USAGE
         LCLA  &PNDX,&DNDX        INPUT PARM,DROP STRING INDICES
         LCLB  &GOTREG,&DROPALL   HAVE REGS TO DROP,DROP ALL TO DO
         LCLC  &R(4),&B(4)        REGS TO DROP,PREVIOUS BASES FOR REGS
         LCLC  &BLANKS            BLANKS TO PAD DROP LINE TO COMMENTS
&DROPALL SETB  ((N'&SYSLIST EQ 0) OR ('&PARMS' EQ '')) SET DROPALL FLAG
.LP#SYS  AIF   (&DROPALL).LP#SAV  BIF DROP ALL IN PROGRESS
         AIF   (&PNDX GE N'&SYSLIST).CK#DROP LOOP TIL DONE WITH REGS
&PNDX    SETA  &PNDX+1            UP THE COMMA/REG INDEX
&UNDX    SETA  0                  RE-INITIALIZE USE ARRAY INDEX
.LP#SAV  ANOP                     VALID DROP REG CHECK LOOP
&UNDX    SETA  &UNDX+1            UP THE USE ARRAY COUNT
         AIF   (&DROPALL).CK#ALL  BIF DROP ALL IN PROGRESS
         AIF   ('&USEREG(&UNDX)' EQ '&SYSLIST(&PNDX)').DROP#R BIF MATCH
         AIF   (&UNDX LT 16).LP#SAV LOOP FOR ALL ENTRIES IF NEEDED
         MNOTE 4,'REGISTER &SYSLIST(&PNDX) WAS NOT A USNGX OPERAND'
         AGO   .LP#SYS            CONTINUE
.CK#ALL  AIF   (&UNDX GT 16).CK#DROP BIF END OF SAVE TABLE/CHECK DROPS
         AIF   ('&USEREG(&UNDX)'  EQ '').LP#SAV BIF EMPTY SLOT
.DROP#R  AIF   (&DNDX EQ 0).DROP$R BIF NO PREVIOUS REGISTER/BASE
&R(&DNDX) SETC '&R(&DNDX)'.','    APPEND COMMA AFTER PREVIOUS REG
&B(&DNDX) SETC '&B(&DNDX)'.','    APPEND COMMA AFTER PREVIOUS BASE
.DROP$R  ANOP
&DNDX    SETA  &DNDX+1            UP THE DROP STRING INDEX
&R(&DNDX) SETC '&USEREG(&UNDX)'   GET PREVIOUS REGISTER
&B(&DNDX) SETC '&USELAB(&UNDX)'   GET PREVIOUS BASE
&USEREG(&UNDX) SETC ''            BLANK THE SAVE TABLE REG FIELD
&USELAB(&UNDX) SETC ''            BLANK THE SAVE TABLE BASE FIELD
         AIF   (&DNDX GE 4).DO#DR BIF DROP LINE FULL
&GOTREG  SETB  1                  SAY HAVE SOMETHING TO DROP
         AGO   .LP#SYS            GO CHECK FOR &SYSLIST/TABLE END
.CK#DROP AIF   (NOT &GOTREG).EXIT SKIP THE DROP IF NOTHING TO DO
.* NOTE THAT THIS MACRO ASSUMES OPERAND/COMMENT COLUMNS 16 AND 35.
.DO#DR   ANOP
&BLANKS  SETC  ' '                SET ONE BLANK IN CASE TOO LONG
&II      SETA  (41-16)-K'&R(1)-K'&R(2)-K'&R(3)-K'&R(4) BLANK COUNT
         AIF   (&II LE 1).DO$DR   BIF ONE BLANK CORRECT
&BLANKS  SETC  '                  '(1,&II) ELSE SET CORRECT NUMBER
.DO$DR   DROP  &R(1)&R(2)&R(3)&R(4)&BLANKS&B(1)&B(2)&B(3)&B(4)
&GOTREG  SETB  0                  NOTHING TO DROP
.DROP#CL AIF   (&DNDX EQ 0).LP#SYS BIF CLEARING DONE
&R(&DNDX) SETC ''                 BLANK REG FIELD
&B(&DNDX) SETC ''                 BLANK BASE FIELD
&DNDX    SETA  &DNDX-1            DECREMENT INDEX
         AGO   .DROP#CL           CONTINUE
.EXIT    MEND
