./ ADD NAME=JCI      0122-89275-94012-1359-00110-00072-00000-TRIDJK
ISREDIT MACRO (JCCLASS)
/**********************************************************************/
/*    JOB CARD INITIAL MACRO:                                         */
/*                                                                    */
/*    USE AS AN INITIAL MACRO TO BUILD A JOB CARD AFTER FILE          */
/*    TAILORING HAS CREATED ALL THE OTHER JCL.                        */
/*                                                                    */
/*    THE PANEL WILL BE DISPLAYED AND THE JOB CARD WILL BE BUILT      */
/*    WITH THE PROFILE VARIABLES THAT HAVE BEEN SAVED.                */
/*                                                                    */
/**********************************************************************/
CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST
IF &STR(&JCCLASS) ¬= &Z THEN -
  DO
    ISPEXEC CONTROL NONDISPL ENTER
    ISPEXEC DISPLAY PANEL(JC)
    IF &LASTCC > 0 THEN -
      EXIT
  END
ELSE -
  DO
    ISPEXEC DISPLAY PANEL(JC)
    IF &LASTCC > 0 THEN -
      EXIT
  END
IF &STR(&JCPROC) ¬= &Z -
  THEN -
    DO
      IF &SUBSTR(1,&STR(&JCPROC)) = ' THEN -
        SET JCPROC = &SUBSTR(2:&LENGTH(&STR(&JCPROC))-1,&STR(&JCPROC))
      ELSE -
        SET JCPROC = &STR(&SYSUID..&STR(&JCPROC))
    END
IF &STR(&JCOTHER1) ¬= &Z THEN -
  DO
    IF &SUBSTR(&LENGTH(&STR(&JCOTHER1)),&STR(&JCOTHER1)) ¬= &STR(,) -
      THEN -
        SET JCOTHER1 = &STR(&JCOTHER1)&STR(,)
  END
IF &STR(&JCOTHER2) ¬= &Z THEN -
  DO
    IF &SUBSTR(&LENGTH(&STR(&JCOTHER2)),&STR(&JCOTHER2)) ¬= &STR(,) -
      THEN -
        SET JCOTHER2 = &STR(&JCOTHER2)&STR(,)
  END
IF &STR(&JCOTHER3) ¬= &Z  THEN -
  DO
    IF &SUBSTR(&LENGTH(&STR(&JCOTHER3)),&STR(&JCOTHER3)) ¬= &STR(,) -
      THEN -
        SET JCOTHER3 = &STR(&JCOTHER3)&STR(,)
  END
SET JOB1  = &STR(//&JCPRE.&JCSU JOB (&JCACC),'&JCNAME',)
SET JOBO1 = &STR(//             &JCOTHER1)
SET JOBO2 = &STR(//             &JCOTHER2)
SET JOBO3 = &STR(//             &JCOTHER3)
SET JOB2  = &STR(//             CLASS=&JCCLASS,)
SET JOB3  = &STR(//             TIME=(&JCTIME),)
SET JOB4  = &STR(//             NOTIFY=&JCNOTIFY,)
SET JOB5  = &STR(//             MSGCLASS=&JCMC)
SET JOB6  = &STR(&STR(/*)JOBPARM R=&JCRM,T=9999,L=9999)
SET JOB7  = &STR(&STR(/*)ROUTE XEQ   &JCJXEQ)
SET JOB8  = &STR(&STR(/*)ROUTE PRINT &JCJPRT)
SET JOB9  = &STR(//JCL      JCLLIB  ORDER=&JCPROC)
SET JOB10 = &STR(//JOB      OUTPUT  NAME='&JCNAME',)
SET JOB11 = &STR(//             TITLE='&JCTITLE',)
SET JOB12 = &STR(//             DEPT='&JCDEPT',)
SET JOB13 = &STR(//             BUILDING='&JCBLDG',)
SET JOB14 = &STR(//             ADDRESS=('&JCADDR1',
SET JOB15 = &STR(//             '&JCADDR2',)
SET JOB16 = &STR(//             '&JCADDR3',)
SET JOB17 = &STR(//             '&JCADDR4'))&STR(,)
SET JOB18 = &STR(//             JESDS=ALL,)
SET JOB19 = &STR(//             DEFAULT=YES)
ISREDIT LINE_BEFORE .ZFIRST = "&JOB19"
ISREDIT LINE_BEFORE .ZFIRST = "&JOB18"
ISREDIT LINE_BEFORE .ZFIRST = "&JOB17"
ISREDIT LINE_BEFORE .ZFIRST = "&JOB16"
ISREDIT LINE_BEFORE .ZFIRST = "&JOB15"
ISREDIT LINE_BEFORE .ZFIRST = "&JOB14"
ISREDIT LINE_BEFORE .ZFIRST = "&JOB13"
ISREDIT LINE_BEFORE .ZFIRST = "&JOB12"
ISREDIT LINE_BEFORE .ZFIRST = "&JOB11"
ISREDIT LINE_BEFORE .ZFIRST = "&JOB10"
/* CHECK FOR EXISTING JCLLIB IN SKELETON JCL */
ISREDIT FIND FIRST " JCLLIB "
IF &LASTCC ¬= 0 && &STR(&JCPROC) ¬= &Z THEN -
  ISREDIT LINE_BEFORE .ZFIRST = "&JOB9"
ISREDIT CURSOR = 1 1
IF &STR(&JCJPRT) ¬= &Z THEN -
  ISREDIT LINE_BEFORE .ZFIRST = "&JOB8"
IF &STR(&JCJXEQ) ¬= &Z THEN -
  ISREDIT LINE_BEFORE .ZFIRST = "&JOB7"
ISREDIT LINE_BEFORE .ZFIRST = "&JOB6"
ISREDIT LINE_BEFORE .ZFIRST = "&JOB5"
IF &STR(&JCNOTIFY) ¬= &Z THEN -
  ISREDIT LINE_BEFORE .ZFIRST = "&JOB4"
IF &STR(&JCTIME) ¬= &Z THEN -
  ISREDIT LINE_BEFORE .ZFIRST = "&JOB3"
ISREDIT LINE_BEFORE .ZFIRST = "&JOB2"
IF &STR(&JCOTHER3) ¬= &Z THEN -
  ISREDIT LINE_BEFORE .ZFIRST = "&JOBO3"
IF &STR(&JCOTHER2) ¬= &Z THEN -
  ISREDIT LINE_BEFORE .ZFIRST = "&JOBO2"
IF &STR(&JCOTHER1) ¬= &Z THEN -
  ISREDIT LINE_BEFORE .ZFIRST = "&JOBO1"
ISREDIT LINE_BEFORE .ZFIRST = "&JOB1"
/* RETURN CURSOR TO COMMAND LINE */
EXIT CODE(1)
/* J. KALINICH, X4521 */
/* INITIAL EDIT MACRO TO BUILD JOBCARD SEQUENCE FOR JCL SKELETONS */
./ ADD NAME=M204B    0157-88362-96204-1253-00018-00014-00000-TRIDJK
PROC 0 DEBUG
IF &DEBUG = &STR(DEBUG) THEN -
  CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT
ELSE -
  CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST ASIS
ISPEXEC DISPLAY PANEL(M204B)
IF &LASTCC = 8 THEN EXIT
FREE  F(CCAIN CCAOUT)
ALLOC F(CCAIN)  DA(*) SHR REUSE
ALLOC F(CCAOUT) DA(*) SHR REUSE
WRITE MODEL 204 BATCH2 - READY FOR LOGON     (Enter /* to end session)
%M204LIBS     /* VERIFY LOAD LIBRARY NAME */
ISPEXEC VGET (M204LOAD) PROFILE
SET &LOAD = &SUBSTR(2:&LENGTH(&STR(&M204LOAD))-1,&STR(&M204LOAD))
ISPEXEC CONTROL DISPLAY LINE START(2)
CALL '&LOAD(BATCH2)' '&M204CHAN,OMC=32'
/* J.KALINICH, X4521 */
/* CLIST TO INVOKE MODEL 204 - MULTI-USER MODE            */
./ ADD NAME=M204BROW 0151-90047-98107-0810-00438-00300-00000-TRIDJK
PROC 0 DEBUG
/* -------------------------------------------------------------- */
/*    MODEL 204 PROCEDURE LIST USING ISPF VIEW - VERSION 3.0      */
/* -------------------------------------------------------------- */
/*    1. TABLE POSITIONING "MORE SPF-LIKE"                        */
/*    2. RESET UDDCCC 74                                          */
/*    3. DEACTIVATE ERROR PROCESSING AFTER OUTXXXXN EOF           */
/*    4. TABLE LOCATE "EVEN MORE SPF-LIKE"                        */
/*    5. USE PROFILE VARIABLE FOR LOAD LIBRARY NAME               */
/*    6. ALLOW PATTERN MATCHING CHARACTERS (*,+,#,@) IN THE       */
/*       PROCEDURE NAME FIELD FOR A MEMBER LIST SUBSET            */
/*    7. DISPLAY FILE NAME IN GROUP ON MEMBER LIST                */
/*    8. VERSION 3.2 SUPPORT FOR NEW DISPLAY PROC LIST FORMAT     */
/*    9. ALLOW 100 CHARACTER PROC NAMES (3.2 ONLY)                */
/*   10. VIEW/BROWSE DETERMINED BY ZVIEW PROFILE VARIABLE         */
/*   11. YEAR 2000 SUPPORT FOR DATE SORTING                       */
/* -------------------------------------------------------------- */
IF &DEBUG = &STR(DEBUG) THEN -
  CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT
ELSE -
  CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST
ISPEXEC VGET (ZVIEW) PROFILE
IF &ZVIEW = &STR(YES) THEN -
  SET &VIEWM = &STR(VIEW)
ELSE -
  SET &VIEWM = &STR(BROWSE)
%M204LIBS     /* VERIFY LOAD AND CRAM LIBRARY NAMES */
ISPEXEC VGET (M204CRAM) PROFILE
SET &CRAM = &SUBSTR(2:&LENGTH(&STR(&M204CRAM))-1,&STR(&M204CRAM))
ISPEXEC VGET (TERMID) SHARED
IF &TERMID = &STR() THEN -
  DO
    CALL '&CRAM(TERMID)'
    ISPEXEC VPUT (TERMID) SHARED
  END
ISPEXEC VGET (M204LOAD) PROFILE
SET &LOAD = &SUBSTR(2:&LENGTH(&STR(&M204LOAD))-1,&STR(&M204LOAD))
ISPEXEC VGET (ZJDATE ZSCREEN)
SET &TID = &SUBSTR(&LENGTH(&TERMID)-3:&LENGTH(&TERMID),&TERMID)+
           &STR(&ZSCREEN)
SET &UID = &SYSUID
DELETE ('&UID..CCAIN' '&UID..OUT&TID')
FREE F(CCAIN OUT&TID)
FREE ATTR(DCB)
ATTR DCB RECFM(F B) LRECL(132) BLKSIZE(6336)
ALLOC F(CCAIN) DA('&UID..CCAIN') NEW CATALOG SP(1 1) -
  TRACK RELEASE USING(DCB) REUSE
PANEL: +
SET &M204MODE = &STR(&VIEWM)       /* SCREEN SHARE WITH EDIT   */
ISPEXEC DISPLAY PANEL(M204EDIT)
IF &LASTCC = 8 THEN EXIT
FREE F(CCAOUT)
IF &STR(&M204AUDT) = &STR( ) THEN -
  DO
    ALLOC F(CCAOUT) DUMMY
  END
ELSE -
  DO
    IF &STR(&M204AUDT) = &STR(*) THEN -
      DO
        ALLOC F(CCAOUT) DA(*) SHR REUSE
      END
    ELSE -
      DO
        ALLOC F(CCAOUT) SYSOUT(&M204AUDT) &M204Q
      END
  END
IF &STR(&M204PROC) = &STR() THEN -
  SET &M204PROC = &STR(LIST)
IF &SYSINDEX(&STR(*),&STR(&M204PROC),1) > 0 OR -
   &SYSINDEX(&STR(+),&STR(&M204PROC),1) > 0 OR -
   &SYSINDEX(&STR(#),&STR(&M204PROC),1) > 0 OR -
   &SYSINDEX(&STR(@),&STR(&M204PROC),1) > 0 THEN -
  SET &M204PROC = &STR(LIST LIKE )&STR(&M204PROC)
PROC: +
IF &SUBSTR(1:4,&M204PROC) = &STR(LIST) THEN -
  DO
    OPENFILE CCAIN OUTPUT
    SET CCAIN = &STR( )
    PUTFILE CCAIN
    SET CCAIN = &STR(LOGIN &M204LOGO)
    PUTFILE CCAIN
    SET CCAIN = &STR(&M204PSWD)
    PUTFILE CCAIN
    SET CCAIN = &STR(OPEN &M204FILE)
    PUTFILE CCAIN
    SET CCAIN = &STR(&M204FPWD)
    PUTFILE CCAIN
    SET CCAIN = &STR(RESET HDRCTL 1)
    PUTFILE CCAIN
    SET CCAIN = &STR(FREE DATASET OUT&TID)
    PUTFILE CCAIN
    SET CCAIN = &STR(ALLOCATE DATASET OUT&TID WITH SEQUENTIAL - )
    PUTFILE CCAIN
    SET CCAIN = &STR(  DSN=&UID..OUT&TID COND SHARE - )
    PUTFILE CCAIN
    SET CCAIN = &STR(  LRECL=132 BLKSIZE=6336 RECFM=FB - )
    PUTFILE CCAIN
    SET CCAIN = &STR(  PRIMARY=5 SECONDARY =15 TRACK UNIT=DISK)
    PUTFILE CCAIN
    SET CCAIN = &STR(USE OUT&TID)
    PUTFILE CCAIN
    SET CCAIN = &STR(DISPLAY PROC &M204PROC)
    PUTFILE CCAIN
    SET CCAIN = &STR(FREE DATASET OUT&TID)
    PUTFILE CCAIN
    SET CCAIN = &STR(LOGOFF)
    PUTFILE CCAIN
    CLOSFILE CCAIN
    CALL '&LOAD(BATCH2)' '&M204CHAN'
    SET &M204RC = &LASTCC
    IF &M204RC > 0 THEN -
    DO
      ISPEXEC SETMSG MSG(MOD000)
      GOTO PANEL
    END
    /*                           */
    /* TURN OFF DURING DEBUGGING */
    /*                           */
    CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST
    ISPEXEC TBCREATE M204PDS KEYS(M204MBR) -
      NAMES(M204CEN M204DATE M204LEN M204UID M204GRP) -
      NOWRITE REPLACE
    IF &LASTCC > 4 THEN -
      DO
        WRITE TBCREATE FAILED
        EXIT
      END
    ALLOC F(OUT&TID) DA('&UID..OUT&TID') SHR REUSE
    OPENFILE OUT&TID INPUT
    SET &EOF = N
    DO WHILE &EOF = N
      ERROR -
        DO
          IF &LASTCC = 400 -
            THEN -
              SET &EOF = Y
            ELSE -
              SET &NULL = &STR()
          RETURN
        END
      GETFILE OUT&TID
      IF &EOF = Y THEN GOTO DONE
      SET &OUT      = &STR(OUT)
      SET &OUTPROC  = &&OUT&TID
      /* VERSION 3.2 DISPLAY PROC LIST FORMAT ? */
      /* TAKE ANY LOAD - 09/09/97               */
      IF &SYSINDEX(&STR(LOAD),&M204LOAD,1) ¬= 0 THEN -
        DO
          IF &SUBSTR(30:30,&OUTPROC) = &STR( ) THEN -
            DO
              IF &STR(&M204DATE) = &STR(+) THEN -
                SET &M204MBR  = &STR(&M204LONG)
              ELSE -
                SET &M204MBR  = &SUBSTR(1:29,&OUTPROC)
              SET &M204YY   = &SUBSTR(37:38,&OUTPROC)
              /* 66 IS START OF 19XX DATE WINDOW */
              IF &STR(&M204YY) < 66 THEN -
                SET M204CEN = &STR(1)
              ELSE -
                SET M204CEN = &STR(0)
              SET &M204DATE = &SUBSTR(37:38,&OUTPROC)+
                              &SUBSTR(36:36,&OUTPROC)+
                              &SUBSTR(31:35,&OUTPROC)+
                              &SUBSTR(39:47,&OUTPROC)
              SET &M204LEN  = &SUBSTR(49:54,&OUTPROC)
              SET &M204UID  = &SUBSTR(56:65,&OUTPROC)
              SET &M204GRP  = &SUBSTR(71:78,&OUTPROC)
            END
          ELSE -
            DO
              SET &M204MBR  = &STR( )
              SET &M204LONG = &SUBSTR(1:100,&OUTPROC)
              SET &M204CEN  = &STR( )
              SET &M204DATE = &STR(+)
              SET &M204LEN  = &STR( )
              SET &M204UID  = &STR( )
              SET &M204GRP  = &STR( )
            END
        END
      /* VERSION 3.1 AND EARLIER */
      ELSE -
        DO
          SET &M204MBR  = &SUBSTR(1:40,&OUTPROC)
          SET &M204YY   = &SUBSTR(48:49,&OUTPROC)
          /* 66 IS START OF 19XX DATE WINDOW */
          IF &STR(&M204YY) < 66 THEN -
            SET M204CEN = &STR(1)
          ELSE -
            SET M204CEN = &STR(0)
          SET &M204DATE = &SUBSTR(48:49,&OUTPROC)+
                          &SUBSTR(47:47,&OUTPROC)+
                          &SUBSTR(42:46,&OUTPROC)+
                          &SUBSTR(50:58,&OUTPROC)
          SET &M204LEN  = &SUBSTR(60:65,&OUTPROC)
          SET &M204GRP  = &SUBSTR(71:78,&OUTPROC)
        END
      IF &SUBSTR(1:1,&M204MBR) = &STR( ) THEN -
        DO
        END
      ELSE -
        DO
          ISPEXEC TBADD M204PDS
          IF &LASTCC > 0 THEN -
            DO
              WRITE TBADD FAILED
              EXIT
            END
        END
    END
    DONE: +
    ERROR OFF
    CLOSFILE OUT&TID
    FREE F(OUT&TID)
    /*           */
    /* DEBUGGING */
    /*           */
    IF &DEBUG = &STR(DEBUG) THEN -
      CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT
    ELSE -
      CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST
    DISPLAY: +
    SET CSSR = 1
    SET RC   = 0
    ISPEXEC TBSORT M204PDS FIELDS(M204MBR,C,A)
    ISPEXEC TBTOP  M204PDS
    DO WHILE &RC < 8
      ISPEXEC TBDISPL M204PDS PANEL(M204PDS) AUTOSEL(NO)
      SET RC = &LASTCC
      IF &LENGTH(&STR(&ZCMD)) = 0 AND -
         &ZTDSELS = 0 THEN -
        DO
          /* ENTER WITH NO COMMAND OR SELECTION */
          ISPEXEC TBTOP M204PDS
          ISPEXEC TBSKIP M204PDS NUMBER(&ZTDTOP)
        END
      IF &LENGTH(&STR(&ZCMD)) > 2 THEN -
        DO
          IF &SUBSTR(1:2,&ZCMD) = &STR(L ) THEN -
            DO
              ISPEXEC TBVCLEAR M204PDS
              SET &M204MBR = &SUBSTR(3:&LENGTH(&STR(&ZCMD)),&ZCMD)
              ISPEXEC TBTOP  M204PDS
              ISPEXEC TBSARG M204PDS NAMECOND(M204MBR,EQ)
              ISPEXEC TBSCAN M204PDS
              IF &LASTCC = 8 THEN -
                DO
                  ISPEXEC TBTOP  M204PDS
                  ISPEXEC TBSARG M204PDS NAMECOND(M204MBR,GT)
                  ISPEXEC TBSCAN M204PDS
                  IF &LASTCC = 8 THEN -
                    ISPEXEC TBBOTTOM M204PDS
                  ELSE -
                    ISPEXEC TBSKIP M204PDS NUMBER(-1)
                END
              ISPEXEC TBQUERY M204PDS POSITION(CSSR)
            END
          IF &SUBSTR(1:2,&ZCMD) = &STR(S ) THEN -
            DO
              SET &M204MBR  = &SUBSTR(3:&LENGTH(&STR(&ZCMD)),&ZCMD)
              SET &ZTDSELS = 1
            END
        END
      IF &LENGTH(&STR(&ZCMD)) = 4 THEN -
        DO
          IF &SUBSTR(1:4,&ZCMD) = &STR(SORT) THEN -
            DO
              SET &SKEY = &STR(M204MBR,C,A)
              ISPEXEC TBSORT M204PDS FIELDS(&SKEY)
              ISPEXEC TBTOP  M204PDS
            END
        END
      IF &LENGTH(&STR(&ZCMD)) > 5 THEN -
        DO
          IF &SUBSTR(1:5,&ZCMD) = &STR(SORT ) THEN -
            DO
              SET &SKEY = &STR(M204MBR,C,A)
              SET &MBR  = &STR(M204MBR,C,A)
              SET &DAT  = &STR(M204CEN,C,D,M204DATE,C,D)
              SET &LEN  = &STR(M204LEN,C,D)
              SET &USER = &STR(M204UID,C,A)
              SET &GRP  = &STR(M204GRP,C,A)
              IF &LENGTH(&STR(&ZCMD)) > 5  && -
                 &LENGTH(&STR(&ZCMD)) < 12 THEN -
                DO
                  IF &SYSINDEX(&STR(NAME),&ZCMD,6) ¬= 0 -
                    THEN -
                      SET &SKEY = &STR(&MBR)
                  IF &SYSINDEX(&STR(DATE),&ZCMD,6) ¬= 0 -
                    THEN -
                      SET &SKEY = &STR(&DAT)
                  IF &SYSINDEX(&STR(TIME),&ZCMD,6) ¬= 0 -
                    THEN -
                      SET &SKEY = &STR(&DAT)
                  IF &SYSINDEX(&STR(LENGTH),&ZCMD,6) ¬= 0 -
                    THEN -
                      SET &SKEY = &STR(&LEN)
                  IF &SYSINDEX(&STR(USERID),&ZCMD,6) ¬= 0 -
                    THEN -
                      SET &SKEY = &STR(&USER)
                  IF &SYSINDEX(&STR(USER),&ZCMD,6) ¬= 0 -
                    THEN -
                      SET &SKEY = &STR(&USER)
                  IF &SYSINDEX(&STR(ID),&ZCMD,6) ¬= 0 -
                    THEN -
                      SET &SKEY = &STR(&USER)
                  IF &SYSINDEX(&STR(FILE),&ZCMD,6) ¬= 0 -
                    THEN -
                      SET &SKEY = &STR(&GRP)
                END
              ISPEXEC TBSORT M204PDS FIELDS(&SKEY)
              ISPEXEC TBTOP  M204PDS
            END
        END
      IF &LENGTH(&STR(&ZCMD)) > 7 THEN -
        DO
          IF &SUBSTR(1:7,&ZCMD) = &STR(LOCATE ) THEN -
            DO
              ISPEXEC TBVCLEAR M204PDS
              SET &M204MBR = &SUBSTR(8:&LENGTH(&STR(&ZCMD)),&ZCMD)
              ISPEXEC TBTOP  M204PDS
              ISPEXEC TBSARG M204PDS NAMECOND(M204MBR,EQ)
              ISPEXEC TBSCAN M204PDS
              IF &LASTCC = 8 THEN -
                DO
                  ISPEXEC TBTOP  M204PDS
                  ISPEXEC TBSARG M204PDS NAMECOND(M204MBR,GT)
                  ISPEXEC TBSCAN M204PDS
                  IF &LASTCC = 8 THEN -
                    ISPEXEC TBBOTTOM M204PDS
                  ELSE -
                    ISPEXEC TBSKIP M204PDS NUMBER(-1)
                END
              ISPEXEC TBQUERY M204PDS POSITION(CSSR)
            END
          IF &SUBSTR(1:7,&ZCMD) = &STR(SELECT ) THEN -
            DO
              SET &M204MBR  = &SUBSTR(8:&LENGTH(&STR(&ZCMD)),&ZCMD)
              SET &ZTDSELS = 1
            END
        END
      DO WHILE &ZTDSELS > 0
        SET &M204PROC = &STR(&M204MBR)
        ISPEXEC VPUT (M204PROC) PROFILE
        OPENFILE CCAIN OUTPUT
        SET CCAIN = &STR( )
        PUTFILE CCAIN
        SET CCAIN = &STR(LOGIN &M204LOGO)
        PUTFILE CCAIN
        SET CCAIN = &STR(&M204PSWD)
        PUTFILE CCAIN
        SET CCAIN = &STR(OPEN &M204FILE)
        PUTFILE CCAIN
        SET CCAIN = &STR(&M204FPWD)
        PUTFILE CCAIN
        SET CCAIN = &STR(RESET HDRCTL 1)
        PUTFILE CCAIN
        SET CCAIN = &STR(RESET UDDCCC 74)
        PUTFILE CCAIN
        SET CCAIN = &STR(FREE DATASET OUT&TID)
        PUTFILE CCAIN
        SET CCAIN = &STR(ALLOCATE DATASET OUT&TID WITH OLD SHARE - )
        PUTFILE CCAIN
        SET CCAIN = &STR(  DSN=&UID..OUT&TID)
        PUTFILE CCAIN
        SET CCAIN = &STR(USE OUT&TID)
        PUTFILE CCAIN
        SET CCAIN = &STR(DISPLAY PROC (LABEL) &M204PROC)
        PUTFILE CCAIN
        SET CCAIN = &STR(FREE DATASET OUT&TID)
        PUTFILE CCAIN
        SET CCAIN = &STR(LOGOFF)
        PUTFILE CCAIN
        CLOSFILE CCAIN
        CALL '&LOAD(BATCH2)' '&M204CHAN'
        ISPEXEC CONTROL DISPLAY SAVE
        ISPEXEC &VIEWM DATASET('&UID..OUT&TID')
        ISPEXEC CONTROL DISPLAY RESTORE
        IF &ZTDSELS > 1 THEN -
          ISPEXEC TBDISPL M204PDS POSITION(CRP)
        ELSE -
          SET ZTDSELS = 0
        /* RE-POSITION TABLE AT LAST MEMBER BROWSED */
        ISPEXEC TBTOP  M204PDS
        ISPEXEC TBSARG M204PDS
        ISPEXEC TBSCAN M204PDS
        ISPEXEC TBQUERY M204PDS POSITION(CSSR)
      END
    END
  END
ELSE -
  DO
    OPENFILE CCAIN OUTPUT
    SET CCAIN = &STR( )
    PUTFILE CCAIN
    SET CCAIN = &STR(LOGIN &M204LOGO)
    PUTFILE CCAIN
    SET CCAIN = &STR(&M204PSWD)
    PUTFILE CCAIN
    SET CCAIN = &STR(OPEN &M204FILE)
    PUTFILE CCAIN
    SET CCAIN = &STR(&M204FPWD)
    PUTFILE CCAIN
    SET CCAIN = &STR(RESET HDRCTL 1)
    PUTFILE CCAIN
    SET CCAIN = &STR(RESET UDDCCC 74)
    PUTFILE CCAIN
    SET CCAIN = &STR(FREE DATASET OUT&TID)
    PUTFILE CCAIN
    SET CCAIN = &STR(ALLOCATE DATASET OUT&TID WITH SEQUENTIAL - )
    PUTFILE CCAIN
    SET CCAIN = &STR(  DSN=&UID..OUT&TID COND SHARE - )
    PUTFILE CCAIN
    SET CCAIN = &STR(  LRECL=132 BLKSIZE=6336 RECFM=FB - )
    PUTFILE CCAIN
    SET CCAIN = &STR(  PRIMARY=5 SECONDARY =15 TRACK UNIT=DISK)
    PUTFILE CCAIN
    SET CCAIN = &STR(USE OUT&TID)
    PUTFILE CCAIN
    SET CCAIN = &STR(DISPLAY PROC (LABEL) &M204PROC)
    PUTFILE CCAIN
    SET CCAIN = &STR(FREE DATASET OUT&TID)
    PUTFILE CCAIN
    SET CCAIN = &STR(LOGOFF)
    PUTFILE CCAIN
    CLOSFILE CCAIN
    CALL '&LOAD(BATCH2)' '&M204CHAN'
    SET &M204RC = &LASTCC
    IF &M204RC > 0 THEN -
    DO
      ISPEXEC SETMSG MSG(MOD000)
      GOTO PANEL
    END
    ISPEXEC &VIEWM DATASET('&UID..OUT&TID')
  END
GOTO PANEL
/* J.KALINICH, X4521 */
/* CLIST TO INVOKE MODEL 204 AND VIEW/BROWSE PROCEDURES WITH ISPF */
./ ADD NAME=M204DM1  0117-89166-92363-1329-00021-00015-00000-TRIDJK
PROC 0 DEBUG
IF &DEBUG = &STR(DEBUG) THEN -
  CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT
ELSE -
  CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST
ISPEXEC DISPLAY PANEL(M204DM1)
IF &LASTCC = 8 THEN EXIT
SET &DM204FIL = &STR(M204-)&M204FIL
SET &M204FILE = &STR(SIMAPROC)
%M204LIBS     /* VERIFY LOAD AND CRAM LIBRARY NAMES */
ISPEXEC VGET (M204LOAD M204CRAM) PROFILE
SET &LOAD = &SUBSTR(2:&LENGTH(&STR(&M204LOAD))-1,&STR(&M204LOAD))
SET &CRAM = &SUBSTR(2:&LENGTH(&STR(&M204CRAM))-1,&STR(&M204CRAM))
ISPEXEC FTOPEN
ISPEXEC FTINCL M204DM1
ISPEXEC FTCLOSE NAME(M204DM1)
/* INVOKE INITIAL JOBCARD MACRO AND PUT USER IN EDIT */
ISPEXEC EDIT DATASET(GEN.CNTL(M204DM1)) MACRO(JCI)
END
/* J.KALINICH, X4521 */
/* CLIST TO EXPORT DATAMANAGER M204 FILES TO DICTIONARY/204           */
./ ADD NAME=M204DM2  0116-89166-92363-1330-00019-00015-00000-TRIDJK
PROC 0 DEBUG
IF &DEBUG = &STR(DEBUG) THEN -
  CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT
ELSE -
  CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST
ISPEXEC DISPLAY PANEL(M204DM2)
IF &LASTCC = 8 THEN EXIT
%M204LIBS     /* VERIFY LOAD AND CRAM LIBRARY NAMES */
ISPEXEC VGET (M204LOAD M204CRAM) PROFILE
SET &LOAD = &SUBSTR(2:&LENGTH(&STR(&M204LOAD))-1,&STR(&M204LOAD))
SET &CRAM = &SUBSTR(2:&LENGTH(&STR(&M204CRAM))-1,&STR(&M204CRAM))
ISPEXEC FTOPEN
ISPEXEC FTINCL M204DM2
ISPEXEC FTCLOSE NAME(M204DM2)
/* INVOKE INITIAL JOBCARD MACRO AND PUT USER IN EDIT */
ISPEXEC EDIT DATASET(GEN.CNTL(M204DM2)) MACRO(JCI)
END
/* J.KALINICH, X4521 */
/* CLIST TO CHECK M204 FIELD NAMES IN DATAMANAGER DICTIONARY        */
./ ADD NAME=M204DM3  0115-89166-94012-0937-00019-00015-00000-TRIDJK
PROC 0 DEBUG
IF &DEBUG = &STR(DEBUG) THEN -
  CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT
ELSE -
  CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST
ISPEXEC DISPLAY PANEL(M204DM3)
IF &LASTCC = 8 THEN EXIT
%M204LIBS     /* VERIFY LOAD AND CRAM LIBRARY NAMES */
ISPEXEC VGET (M204LOAD M204CRAM) PROFILE
SET &LOAD = &SUBSTR(2:&LENGTH(&STR(&M204LOAD))-1,&STR(&M204LOAD))
SET &CRAM = &SUBSTR(2:&LENGTH(&STR(&M204CRAM))-1,&STR(&M204CRAM))
ISPEXEC FTOPEN
ISPEXEC FTINCL M204DM3
ISPEXEC FTCLOSE NAME(M204DM3)
/* INVOKE INITIAL JOBCARD MACRO AND PUT USER IN EDIT */
ISPEXEC EDIT DATASET(GEN.CNTL(M204DM3)) MACRO(JCI)
END
/* M.MCNEILL, X5043 */
/* CLIST TO PRODUCE FILE RECORD LAYOUTS FROM DICTIONARY/204         */
./ ADD NAME=M204DM5  0120-89166-92363-1330-00018-00015-00000-TRIDJK
PROC 0 DEBUG
IF &DEBUG = &STR(DEBUG) THEN -
  CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT
ELSE -
  CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST
ISPEXEC DISPLAY PANEL(M204DM5)
IF &LASTCC = 8 THEN EXIT
%M204LIBS     /* VERIFY LOAD AND CRAM LIBRARY NAMES */
ISPEXEC VGET (M204LOAD M204CRAM) PROFILE
SET &LOAD = &SUBSTR(2:&LENGTH(&STR(&M204LOAD))-1,&STR(&M204LOAD))
SET &CRAM = &SUBSTR(2:&LENGTH(&STR(&M204CRAM))-1,&STR(&M204CRAM))
ISPEXEC FTOPEN
ISPEXEC FTINCL M204DM5
ISPEXEC FTCLOSE NAME(M204DM5)
/* INVOKE INITIAL JOBCARD MACRO AND PUT USER IN EDIT */
ISPEXEC EDIT DATASET(GEN.CNTL(M204DM5)) MACRO(JCI)
/* J.KALINICH, X4521 */
/* CLIST TO IMPORT DICTIONARY/204 M204 FILES TO DATAMANAGER           */
./ ADD NAME=M204DM6  0114-89166-92363-1330-00016-00015-00000-TRIDJK
PROC 0 DEBUG
IF &DEBUG = &STR(DEBUG) THEN -
  CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT
ELSE -
  CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST
ISPEXEC DISPLAY PANEL(M204DM6)
IF &LASTCC = 8 THEN EXIT
SET &DM204FIL = &STR(M204-)&M204FIL
ISPEXEC FTOPEN
ISPEXEC FTINCL M204DM6
ISPEXEC FTCLOSE NAME(M204DM6)
/* INVOKE INITIAL JOBCARD MACRO AND PUT USER IN EDIT */
ISPEXEC EDIT DATASET(GEN.CNTL(M204DM6)) MACRO(JCI)
END
/* J.KALINICH, X4521 */
/* CLIST TO VALIDATE CONTENTS OF M204 FILES IN DATAMANAGER            */
./ ADD NAME=M204EDIT 0150-91099-97282-1411-00553-00421-00000-TRIDJK
PROC 0 DEBUG
/* -------------------------------------------------------------- */
/*    MODEL 204 PROCEDURE UPDATE USING ISPF EDIT - VERSION 3.0    */
/* -------------------------------------------------------------- */
/*    1. TABLE POSITIONING "MORE SPF-LIKE"                        */
/*    2. RESET UDDCCC 74                                          */
/*    3. M204I EDIT MACRO DELETES '- ' CONTINUATIONS IN CC 1-2    */
/*    4. DEACTIVATE ERROR PROCESSING AFTER OUTXXXXN EOF           */
/*    5. TABLE LOCATE "EVEN MORE SPF-LIKE"                        */
/*    6. USE PROFILE VARIABLE FOR LOAD LIBRARY NAME               */
/*    7. ALLOW PATTERN MATCHING CHARACTERS (*,+,#,@) IN THE       */
/*       PROCEDURE NAME FIELD FOR A MEMBER LIST SUBSET            */
/*    8. DISPLAY FILE NAME IN GROUP ON MEMBER LIST                */
/*    9. ISPF EDIT RECOVERY ON OUTXXXXN DATA SETS                 */
/*   10. VERSION 3.2 SUPPORT FOR NEW DISPLAY PROC LIST FORMAT     */
/*   11. ALLOW 100 CHARACTER PROC NAMES (3.2 ONLY)                */
/*   12. YEAR 2000 SUPPORT FOR DATE SORTING                       */
/* -------------------------------------------------------------- */
IF &DEBUG = &STR(DEBUG) THEN -
  CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT
ELSE -
  CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST
%M204LIBS     /* VERIFY LOAD AND CRAM LIBRARY NAMES */
ISPEXEC VGET (M204CRAM) PROFILE
SET &CRAM = &SUBSTR(2:&LENGTH(&STR(&M204CRAM))-1,&STR(&M204CRAM))
ISPEXEC VGET (TERMID) SHARED
IF &TERMID = &STR() THEN -
  DO
    CALL '&CRAM(TERMID)'
    ISPEXEC VPUT (TERMID) SHARED
  END
ISPEXEC VGET (M204LOAD) PROFILE
SET &LOAD = &SUBSTR(2:&LENGTH(&STR(&M204LOAD))-1,&STR(&M204LOAD))
ISPEXEC VGET (ZJDATE ZSCREEN)
SET &TID = &SUBSTR(&LENGTH(&TERMID)-3:&LENGTH(&TERMID),&TERMID)+
           &STR(&ZSCREEN)
SET &UID = &SYSUID
DELETE ('&UID..CCAIN')
FREE F(CCAIN OUT&TID)
FREE ATTR(DCB)
ATTR DCB RECFM(F B) LRECL(132) BLKSIZE(6336)
ALLOC F(CCAIN) DA('&UID..CCAIN') NEW CATALOG SP(1 1) -
  TRACK RELEASE USING(DCB) REUSE
EDREC: +
SET &RECOVER = &STR(STARTED)
DO WHILE &RECOVER = &STR(STARTED)
  ISPEXEC EDREC QUERY
  IF &LASTCC = 4 THEN -
    DO
      IF &EVAL(&LENGTH(&STR(&UID))+3) < &LENGTH(&ZEDTDSN) THEN -
        IF &SUBSTR(1:&EVAL(&LENGTH(&STR(&UID))+4),&ZEDTDSN) = +
           &STR(&UID)&STR(.OUT) THEN -
          DO
            ISPEXEC DISPLAY PANEL(M204EREC)
            IF &LASTCC = 8 THEN -
              ISPEXEC EDREC CANCEL
            ELSE -
              DO
                IF &SYSDSN('&UID..OUT&TID') = &STR(OK) THEN -
                  ISPEXEC EDREC PROCESS
                ELSE -
                  ISPEXEC EDREC CANCEL
              END
          END
        ELSE -
          ISPEXEC EDREC DEFER
    END
  ELSE -
    SET &RECOVER = &STR(DONE)
END
DELETE ('&UID..OUT&TID')
PANEL: +
SET &M204MODE = &STR(EDIT)         /* SCREEN SHARE WITH BROWSE */
ISPEXEC DISPLAY PANEL(M204EDIT)
IF &LASTCC = 8 THEN EXIT
FREE F(CCAOUT)
IF &STR(&M204AUDT) = &STR( ) THEN -
  DO
    ALLOC F(CCAOUT) DUMMY
  END
ELSE -
  DO
    IF &STR(&M204AUDT) = &STR(*) THEN -
      DO
        ALLOC F(CCAOUT) DA(*) SHR REUSE
      END
    ELSE -
      DO
        ALLOC F(CCAOUT) SYSOUT(&M204AUDT) &M204Q
      END
  END
IF &STR(&M204PROC) = &STR() THEN -
  SET &M204PROC = &STR(LIST)
IF &SYSINDEX(&STR(*),&STR(&M204PROC),1) > 0 OR -
   &SYSINDEX(&STR(+),&STR(&M204PROC),1) > 0 OR -
   &SYSINDEX(&STR(#),&STR(&M204PROC),1) > 0 OR -
   &SYSINDEX(&STR(@),&STR(&M204PROC),1) > 0 THEN -
  SET &M204PROC = &STR(LIST LIKE )&STR(&M204PROC)
PROC: +
IF &SUBSTR(1:4,&M204PROC) = &STR(LIST) THEN -
  DO
    OPENFILE CCAIN OUTPUT
    SET CCAIN = &STR( )
    PUTFILE CCAIN
    SET CCAIN = &STR(LOGIN &M204LOGO)
    PUTFILE CCAIN
    SET CCAIN = &STR(&M204PSWD)
    PUTFILE CCAIN
    SET CCAIN = &STR(OPEN &M204FILE)
    PUTFILE CCAIN
    SET CCAIN = &STR(&M204FPWD)
    PUTFILE CCAIN
    SET CCAIN = &STR(RESET HDRCTL 1)
    PUTFILE CCAIN
    SET CCAIN = &STR(FREE DATASET OUT&TID)
    PUTFILE CCAIN
    SET CCAIN = &STR(ALLOCATE DATASET OUT&TID WITH SEQUENTIAL - )
    PUTFILE CCAIN
    SET CCAIN = &STR(  DSN=&UID..OUT&TID COND SHARE - )
    PUTFILE CCAIN
    SET CCAIN = &STR(  LRECL=132 BLKSIZE=6336 RECFM=FB - )
    PUTFILE CCAIN
    SET CCAIN = &STR(  PRIMARY=5 SECONDARY =15 TRACK UNIT=DISK)
    PUTFILE CCAIN
    SET CCAIN = &STR(USE OUT&TID)
    PUTFILE CCAIN
    SET CCAIN = &STR(DISPLAY PROC &M204PROC)
    PUTFILE CCAIN
    SET CCAIN = &STR(FREE DATASET OUT&TID)
    PUTFILE CCAIN
    SET CCAIN = &STR(LOGOFF)
    PUTFILE CCAIN
    CLOSFILE CCAIN
    CALL '&LOAD(BATCH2)' '&M204CHAN'
    SET &M204RC = &LASTCC
    IF &M204RC > 0 THEN -
      DO
        ISPEXEC SETMSG MSG(MOD000)
        GOTO PANEL
      END
    /* TURN OFF DURING DEBUGGING */
    CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST
    ISPEXEC TBCREATE M204PDS KEYS(M204MBR) -
      NAMES(M204CEN M204DATE M204LEN M204UID M204GRP) -
      NOWRITE REPLACE
    IF &LASTCC > 4 THEN -
      DO
        WRITE TBCREATE FAILED
        EXIT
      END
    ALLOC F(OUT&TID) DA('&UID..OUT&TID') SHR REUSE
    OPENFILE OUT&TID INPUT
    SET &EOF = N
    DO WHILE &EOF = N
      ERROR -
        DO
          IF &LASTCC = 400 -
            THEN -
              SET &EOF = Y
            ELSE -
              SET &NULL = &STR()
          RETURN
        END
      GETFILE OUT&TID
      IF &EOF = Y THEN GOTO DONE
      SET &OUT      = &STR(OUT)
      SET &OUTPROC  = &&OUT&TID
      /* VERSION 3.2 DISPLAY PROC LIST FORMAT ? */
      /* TAKE ANY LOAD - 09/09/97               */
      IF &SYSINDEX(&STR(LOAD),&M204LOAD,1) ¬= 0 THEN -
        DO
          IF &SUBSTR(30:30,&OUTPROC) = &STR( ) THEN -
            DO
              IF &STR(&M204DATE) = &STR(+) THEN -
                SET &M204MBR  = &STR(&M204LONG)
              ELSE -
                SET &M204MBR  = &SUBSTR(1:29,&OUTPROC)
              SET &M204YY   = &SUBSTR(37:38,&OUTPROC)
              /* 66 IS START OF 19XX DATE WINDOW */
              IF &STR(&M204YY) < 66 THEN -
                SET M204CEN = &STR(1)
              ELSE -
                SET M204CEN = &STR(0)
              SET &M204DATE = &SUBSTR(37:38,&OUTPROC)+
                              &SUBSTR(36:36,&OUTPROC)+
                              &SUBSTR(31:35,&OUTPROC)+
                              &SUBSTR(39:47,&OUTPROC)
              SET &M204LEN  = &SUBSTR(49:54,&OUTPROC)
              SET &M204UID  = &SUBSTR(56:65,&OUTPROC)
              SET &M204GRP  = &SUBSTR(71:78,&OUTPROC)
            END
          ELSE -
            DO
              SET &M204MBR  = &STR( )
              SET &M204LONG = &SUBSTR(1:100,&OUTPROC)
              SET &M204CEN  = &STR( )
              SET &M204DATE = &STR(+)
              SET &M204LEN  = &STR( )
              SET &M204UID  = &STR( )
              SET &M204GRP  = &STR( )
            END
        END
      /* VERSION 3.1 AND EARLIER */
      ELSE -
        DO
          SET &M204MBR  = &SUBSTR(1:40,&OUTPROC)
          SET &M204YY   = &SUBSTR(48:49,&OUTPROC)
          /* 66 IS START OF 19XX DATE WINDOW */
          IF &STR(&M204YY) < 66 THEN -
            SET M204CEN = &STR(1)
          ELSE -
            SET M204CEN = &STR(0)
          SET &M204DATE = &SUBSTR(48:49,&OUTPROC)+
                          &SUBSTR(47:47,&OUTPROC)+
                          &SUBSTR(42:46,&OUTPROC)+
                          &SUBSTR(50:58,&OUTPROC)
          SET &M204LEN  = &SUBSTR(60:65,&OUTPROC)
          SET &M204GRP  = &SUBSTR(71:78,&OUTPROC)
        END
      IF &SUBSTR(1:1,&M204MBR) = &STR( ) THEN -
        DO
        END
      ELSE -
        DO
          ISPEXEC TBADD M204PDS
          IF &LASTCC > 0 THEN -
            DO
              WRITE TBADD FAILED
              EXIT
            END
        END
    END
    DONE: +
    ERROR OFF
    CLOSFILE OUT&TID
    FREE F(OUT&TID)
    /* DEBUGGING */
    IF &DEBUG = &STR(DEBUG) THEN -
      CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT
    ELSE -
      CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST
    DISPLAY: +
    SET CSSR = 1
    SET RC   = 0
    ISPEXEC TBSORT M204PDS FIELDS(M204MBR,C,A)
    ISPEXEC TBTOP  M204PDS
    DO WHILE &RC < 8
      ISPEXEC TBDISPL M204PDS PANEL(M204PDS) AUTOSEL(NO)
      SET RC = &LASTCC
      IF &LENGTH(&STR(&ZCMD)) = 0 AND -
         &ZTDSELS = 0 THEN -
        DO
          /* ENTER WITH NO COMMAND OR SELECTION */
          ISPEXEC TBTOP M204PDS
          ISPEXEC TBSKIP M204PDS NUMBER(&ZTDTOP)
        END
      IF &LENGTH(&STR(&ZCMD)) > 2 THEN -
        DO
          IF &SUBSTR(1:2,&ZCMD) = &STR(L ) THEN -
            DO
              ISPEXEC TBVCLEAR M204PDS
              SET &M204MBR = &SUBSTR(3:&LENGTH(&STR(&ZCMD)),&ZCMD)
              ISPEXEC TBTOP  M204PDS
              ISPEXEC TBSARG M204PDS NAMECOND(M204MBR,EQ)
              ISPEXEC TBSCAN M204PDS
              IF &LASTCC = 8 THEN -
                DO
                  ISPEXEC TBTOP  M204PDS
                  ISPEXEC TBSARG M204PDS NAMECOND(M204MBR,GT)
                  ISPEXEC TBSCAN M204PDS
                  IF &LASTCC = 8 THEN -
                    ISPEXEC TBBOTTOM M204PDS
                  ELSE -
                    ISPEXEC TBSKIP M204PDS NUMBER(-1)
                END
              ISPEXEC TBQUERY M204PDS POSITION(CSSR)
            END
          IF &SUBSTR(1:2,&ZCMD) = &STR(S ) THEN -
            DO
              ISPEXEC TBVCLEAR M204PDS
              SET &M204MBR  = &SUBSTR(3:&LENGTH(&STR(&ZCMD)),&ZCMD)
              IF &SUBSTR(1:1,&SYSTIME) = &STR(0) THEN -
                SET &M204TIME = &STR( )&SUBSTR(2:8,&SYSTIME)
              ELSE -
                SET &M204TIME = &STR(&SYSTIME)
              SET &M204DATE = &SUBSTR(7:8,&SYSDATE)+
                              &SUBSTR(6:6,&SYSDATE)+
                              &SUBSTR(1:5,&SYSDATE)+
                              &STR( )&STR(&M204TIME)
              SET &M204LEN  = &STR( *NEW*)
              ISPEXEC TBADD M204PDS ORDER
              SET &ZTDSELS = 1
            END
        END
      IF &LENGTH(&STR(&ZCMD)) = 4 THEN -
        DO
          IF &SUBSTR(1:4,&ZCMD) = &STR(SORT) THEN -
            DO
              SET &SKEY = &STR(M204MBR,C,A)
              ISPEXEC TBSORT M204PDS FIELDS(&SKEY)
              ISPEXEC TBTOP  M204PDS
            END
        END
      IF &LENGTH(&STR(&ZCMD)) > 5 THEN -
        DO
          IF &SUBSTR(1:5,&ZCMD) = &STR(SORT ) THEN -
            DO
              SET &SKEY = &STR(M204MBR,C,A)
              SET &MBR  = &STR(M204MBR,C,A)
              SET &DAT  = &STR(M204CEN,C,D,M204DATE,C,D)
              SET &LEN  = &STR(M204LEN,C,D)
              SET &USER = &STR(M204UID,C,A)
              SET &GRP  = &STR(M204GRP,C,A)
              IF &LENGTH(&STR(&ZCMD)) > 5  && -
                 &LENGTH(&STR(&ZCMD)) < 12 THEN -
                DO
                  IF &SYSINDEX(&STR(NAME),&ZCMD,6) ¬= 0 -
                    THEN -
                      SET &SKEY = &STR(&MBR)
                  IF &SYSINDEX(&STR(DATE),&ZCMD,6) ¬= 0 -
                    THEN -
                      SET &SKEY = &STR(&DAT)
                  IF &SYSINDEX(&STR(TIME),&ZCMD,6) ¬= 0 -
                    THEN -
                      SET &SKEY = &STR(&DAT)
                  IF &SYSINDEX(&STR(LENGTH),&ZCMD,6) ¬= 0 -
                    THEN -
                      SET &SKEY = &STR(&LEN)
                  IF &SYSINDEX(&STR(USERID),&ZCMD,6) ¬= 0 -
                    THEN -
                      SET &SKEY = &STR(&USER)
                  IF &SYSINDEX(&STR(USER),&ZCMD,6) ¬= 0 -
                    THEN -
                      SET &SKEY = &STR(&USER)
                  IF &SYSINDEX(&STR(ID),&ZCMD,6) ¬= 0 -
                    THEN -
                      SET &SKEY = &STR(&USER)
                  IF &SYSINDEX(&STR(FILE),&ZCMD,6) ¬= 0 -
                    THEN -
                      SET &SKEY = &STR(&GRP)
                END
              ISPEXEC TBSORT M204PDS FIELDS(&SKEY,M204MBR,C,A)
              ISPEXEC TBTOP  M204PDS
            END
        END
      IF &LENGTH(&STR(&ZCMD)) > 7 THEN -
        DO
          IF &SUBSTR(1:7,&ZCMD) = &STR(LOCATE ) THEN -
            DO
              ISPEXEC TBVCLEAR M204PDS
              SET &M204MBR = &SUBSTR(8:&LENGTH(&STR(&ZCMD)),&ZCMD)
              ISPEXEC TBTOP  M204PDS
              ISPEXEC TBSARG M204PDS NAMECOND(M204MBR,EQ)
              ISPEXEC TBSCAN M204PDS
              IF &LASTCC = 8 THEN -
                DO
                  ISPEXEC TBTOP  M204PDS
                  ISPEXEC TBSARG M204PDS NAMECOND(M204MBR,GT)
                  ISPEXEC TBSCAN M204PDS
                  IF &LASTCC = 8 THEN -
                    ISPEXEC TBBOTTOM M204PDS
                  ELSE -
                    ISPEXEC TBSKIP M204PDS NUMBER(-1)
                END
              ISPEXEC TBQUERY M204PDS POSITION(CSSR)
            END
          IF &SUBSTR(1:7,&ZCMD) = &STR(SELECT ) THEN -
            DO
              ISPEXEC TBVCLEAR M204PDS
              SET &M204MBR  = &SUBSTR(8:&LENGTH(&STR(&ZCMD)),&ZCMD)
              IF &SUBSTR(1:1,&SYSTIME) = &STR(0) THEN -
                SET &M204TIME = &STR( )&SUBSTR(2:8,&SYSTIME)
              ELSE -
                SET &M204TIME = &STR(&SYSTIME)
              SET &M204DATE = &SUBSTR(7:8,&SYSDATE)+
                              &SUBSTR(6:6,&SYSDATE)+
                              &SUBSTR(1:5,&SYSDATE)+
                              &STR( )&STR(&M204TIME)
              SET &M204LEN  = &STR( *NEW*)
              ISPEXEC TBADD M204PDS ORDER
              SET &ZTDSELS = 1
            END
        END
      DO WHILE &ZTDSELS > 0
        SET &M204PROC = &STR(&M204MBR)
        ISPEXEC VPUT (M204PROC) PROFILE
        OPENFILE CCAIN OUTPUT
        SET CCAIN = &STR( )
        PUTFILE CCAIN
        SET CCAIN = &STR(LOGIN &M204LOGO)
        PUTFILE CCAIN
        SET CCAIN = &STR(&M204PSWD)
        PUTFILE CCAIN
        SET CCAIN = &STR(OPEN &M204FILE)
        PUTFILE CCAIN
        SET CCAIN = &STR(&M204FPWD)
        PUTFILE CCAIN
        SET CCAIN = &STR(RESET HDRCTL 1)
        PUTFILE CCAIN
        SET CCAIN = &STR(RESET UDDCCC 74)
        PUTFILE CCAIN
        SET CCAIN = &STR(FREE DATASET OUT&TID)
        PUTFILE CCAIN
        SET CCAIN = &STR(ALLOCATE DATASET OUT&TID WITH OLD SHARE - )
        PUTFILE CCAIN
        SET CCAIN = &STR(  DSN=&UID..OUT&TID)
        PUTFILE CCAIN
        SET CCAIN = &STR(USE OUT&TID)
        PUTFILE CCAIN
        SET CCAIN = &STR(DISPLAY PROC (LABEL) &M204PROC)
        PUTFILE CCAIN
        SET CCAIN = &STR(FREE DATASET OUT&TID)
        PUTFILE CCAIN
        SET CCAIN = &STR(LOGOFF)
        PUTFILE CCAIN
        CLOSFILE CCAIN
        CALL '&LOAD(BATCH2)' '&M204CHAN'
        ISPEXEC CONTROL DISPLAY SAVE
        ISPEXEC EDIT DATASET('&UID..OUT&TID') MACRO(M204I)
        SET &EDITCC = &LASTCC
        ISPEXEC CONTROL DISPLAY RESTORE
        IF &EDITCC = 0 THEN -
          DO
            SET &M204SAVE = &STR(M)+
                            &SUBSTR(4:6,&ZJDATE)+
                            &SUBSTR(1:2,&SYSTIME)+
                            &SUBSTR(4:5,&SYSTIME)
            REPRO IDS('&UID..OUT&TID') -
                  ODS('M204.M204EDIT.REPRO(&M204SAVE)')
            /* "LSSC-ONLY" MOD TO ADD ISPF STATS TO BACK-UP PDS */
            PDS 'M204.M204EDIT.REPRO' ATTRIB &M204SAVE ADDSTATS
            /*                            */
            /* UPDATE M204 PROCEDURE FILE */
            /*                            */
            OPENFILE CCAIN OUTPUT
            SET CCAIN = &STR( )
            PUTFILE CCAIN
            SET CCAIN = &STR(LOGIN &M204LOGO)
            PUTFILE CCAIN
            SET CCAIN = &STR(&M204PSWD)
            PUTFILE CCAIN
            SET CCAIN = &STR(OPEN &M204FILE)
            PUTFILE CCAIN
            SET CCAIN = &STR(&M204FPWD)
            PUTFILE CCAIN
            SET CCAIN = &STR(RESET PROMPT 16)
            PUTFILE CCAIN
            SET CCAIN = &STR(RESET INCCC 0)
            PUTFILE CCAIN
            CLOSFILE CCAIN
            ALLOC F(CCAIN) DA('&UID..CCAIN' +
                              '&UID..OUT&TID') SHR REUSE
            CALL '&LOAD(BATCH2)' '&M204CHAN'
            SET &M204RC = &LASTCC
            ISPEXEC SETMSG MSG(MOD000)
            ALLOC F(CCAIN) DA('&UID..CCAIN') SHR REUSE
          END
        IF &ZTDSELS > 1 THEN -
          ISPEXEC TBDISPL M204PDS POSITION(CRP)
        ELSE -
          SET ZTDSELS = 0
        /* RE-POSITION TABLE AT LAST MEMBER EDITED */
        ISPEXEC TBTOP  M204PDS
        ISPEXEC TBSARG M204PDS
        ISPEXEC TBSCAN M204PDS
        ISPEXEC TBQUERY M204PDS POSITION(CSSR)
      END
    END
  END
ELSE -
  DO
    OPENFILE CCAIN OUTPUT
    SET CCAIN = &STR( )
    PUTFILE CCAIN
    SET CCAIN = &STR(LOGIN &M204LOGO)
    PUTFILE CCAIN
    SET CCAIN = &STR(&M204PSWD)
    PUTFILE CCAIN
    SET CCAIN = &STR(OPEN &M204FILE)
    PUTFILE CCAIN
    SET CCAIN = &STR(&M204FPWD)
    PUTFILE CCAIN
    SET CCAIN = &STR(RESET HDRCTL 1)
    PUTFILE CCAIN
    SET CCAIN = &STR(RESET UDDCCC 74)
    PUTFILE CCAIN
    SET CCAIN = &STR(FREE DATASET OUT&TID)
    PUTFILE CCAIN
    SET CCAIN = &STR(ALLOCATE DATASET OUT&TID WITH SEQUENTIAL - )
    PUTFILE CCAIN
    SET CCAIN = &STR(  DSN=&UID..OUT&TID COND SHARE - )
    PUTFILE CCAIN
    SET CCAIN = &STR(  LRECL=132 BLKSIZE=6336 RECFM=FB - )
    PUTFILE CCAIN
    SET CCAIN = &STR(  PRIMARY=5 SECONDARY =15 TRACK UNIT=DISK)
    PUTFILE CCAIN
    SET CCAIN = &STR(USE OUT&TID)
    PUTFILE CCAIN
    SET CCAIN = &STR(DISPLAY PROC (LABEL) &M204PROC)
    PUTFILE CCAIN
    SET CCAIN = &STR(FREE DATASET OUT&TID)
    PUTFILE CCAIN
    SET CCAIN = &STR(LOGOFF)
    PUTFILE CCAIN
    CLOSFILE CCAIN
    CALL '&LOAD(BATCH2)' '&M204CHAN'
    SET &M204RC = &LASTCC
    IF &M204RC > 0 THEN -
      DO
        ISPEXEC SETMSG MSG(MOD000)
        GOTO PANEL
      END
    ISPEXEC EDIT DATASET('&UID..OUT&TID') MACRO(M204I)
    IF &LASTCC = 0 THEN -
      DO
        SET &M204SAVE = &STR(M)+
                        &SUBSTR(4:6,&ZJDATE)+
                        &SUBSTR(1:2,&SYSTIME)+
                        &SUBSTR(4:5,&SYSTIME)
        REPRO IDS('&UID..OUT&TID') -
              ODS('M204.M204EDIT.REPRO(&M204SAVE)')
        /* "LSSC-ONLY" MOD TO ADD ISPF STATS TO BACK-UP PDS */
        PDS 'M204.M204EDIT.REPRO' ATTRIB &M204SAVE ADDSTATS
        /*                            */
        /* UPDATE M204 PROCEDURE FILE */
        /*                            */
        OPENFILE CCAIN OUTPUT
        SET CCAIN = &STR( )
        PUTFILE CCAIN
        SET CCAIN = &STR(LOGIN &M204LOGO)
        PUTFILE CCAIN
        SET CCAIN = &STR(&M204PSWD)
        PUTFILE CCAIN
        SET CCAIN = &STR(OPEN &M204FILE)
        PUTFILE CCAIN
        SET CCAIN = &STR(&M204FPWD)
        PUTFILE CCAIN
        SET CCAIN = &STR(RESET PROMPT 16)
        PUTFILE CCAIN
        SET CCAIN = &STR(RESET INCCC 0)
        PUTFILE CCAIN
        CLOSFILE CCAIN
        ALLOC F(CCAIN) DA('&UID..CCAIN' +
                          '&UID..OUT&TID') SHR REUSE
        CALL '&LOAD(BATCH2)' '&M204CHAN'
        SET &M204RC = &LASTCC
        ISPEXEC SETMSG MSG(MOD000)
        ALLOC F(CCAIN) DA('&UID..CCAIN') SHR REUSE
      END
  END
GOTO PANEL
/* J.KALINICH, X4521 */
/* CLIST TO INVOKE MODEL 204 AND EDIT USER PROCEDURES WITH ISPF EDIT */
./ ADD NAME=M204F    0141-89055-97290-0814-00319-00123-00000-TRIDJK
PROC 0 DEBUG
/*                                                                */
/*    MODEL 204 FILE LIST FROM METADATA                           */
/*                                                                */
IF &DEBUG = &STR(DEBUG) THEN -
  CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT
ELSE -
  CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST
%M204LIBS     /* VERIFY LOAD AND CRAM LIBRARY NAMES */
ISPEXEC VGET (M204CRAM) PROFILE
SET &CRAM = &SUBSTR(2:&LENGTH(&STR(&M204CRAM))-1,&STR(&M204CRAM))
ISPEXEC VGET (TERMID) SHARED
IF &TERMID = &STR() THEN -
  DO
    CALL '&CRAM(TERMID)'
    ISPEXEC VPUT (TERMID) SHARED
  END
ISPEXEC VGET (M204LOAD) PROFILE
SET &LOAD = &SUBSTR(2:&LENGTH(&STR(&M204LOAD))-1,&STR(&M204LOAD))
ISPEXEC VGET (ZJDATE ZSCREEN)
SET &TID = &SUBSTR(&LENGTH(&TERMID)-3:&LENGTH(&TERMID),&TERMID)+
           &STR(&ZSCREEN)
SET &UID = &SYSUID
DELETE ('&UID..CCAIN' '&UID..OUT&TID')
FREE F(CCAIN OUT&TID)
FREE ATTR(DCB)
ATTR DCB RECFM(F B) LRECL(132) BLKSIZE(6336)
ALLOC F(CCAIN) DA('&UID..CCAIN') NEW CATALOG SP(1 1) -
  TRACK RELEASE USING(DCB) REUSE
PANEL: +
ISPEXEC DISPLAY PANEL(M204F)
IF &LASTCC = 8 THEN EXIT
FREE F(CCAOUT)
IF &STR(&M204AUDT) = &STR( ) THEN -
  DO
    ALLOC F(CCAOUT) DUMMY
  END
ELSE -
  DO
    IF &STR(&M204AUDT) = &STR(*) THEN -
      DO
        ALLOC F(CCAOUT) DA(*) SHR REUSE
      END
    ELSE -
      DO
        ALLOC F(CCAOUT) SYSOUT(&M204AUDT) &M204Q
      END
  END
FILE: +
OPENFILE CCAIN OUTPUT
SET CCAIN = &STR( )
PUTFILE CCAIN
SET CCAIN = &STR(LOGIN &M204LOGO)
PUTFILE CCAIN
SET CCAIN = &STR(&M204PSWD)
PUTFILE CCAIN
SET CCAIN = &STR(OPEN METADATA)
PUTFILE CCAIN
SET CCAIN = &STR( )
PUTFILE CCAIN
SET CCAIN = &STR(FREE DATASET OUT&TID)
PUTFILE CCAIN
SET CCAIN = &STR(ALLOCATE DATASET OUT&TID WITH SEQUENTIAL - )
PUTFILE CCAIN
SET CCAIN = &STR(  DSN=&UID..OUT&TID COND SHARE - )
PUTFILE CCAIN
SET CCAIN = &STR(  LRECL=132 BLKSIZE=6336 RECFM=FB - )
PUTFILE CCAIN
SET CCAIN = &STR(  PRIMARY=5 SECONDARY =15 TRACK UNIT=DISK)
PUTFILE CCAIN
SET CCAIN = &STR(RESET HDRCTL 1)
PUTFILE CCAIN
SET CCAIN = &STR(USE OUT&TID)
PUTFILE CCAIN
SET CCAIN = &STR(BEGIN)
PUTFILE CCAIN
SET CCAIN = &STR(FIND1:  IN METADATA FD REC=D)
PUTFILE CCAIN
SET CCAIN = &STR(                       ENTITY=('FILE' OR 'STAGED FILE'))
PUTFILE CCAIN
SET CCAIN = &STR(        END FIND FIND1)
PUTFILE CCAIN
SET CCAIN = &STR(SORT1:  SORT RECORDS IN FIND1 BY NAME)
PUTFILE CCAIN
SET CCAIN = &STR(FOR1:   FR SORT1)
PUTFILE CCAIN
SET CCAIN = &STR(        PRINT NAME          AT 1 -)
PUTFILE CCAIN
SET CCAIN = &STR(              ENTITY        AT 10 -)
PUTFILE CCAIN
SET CCAIN = &STR(              UPDATED-BY    AT 22 -)
PUTFILE CCAIN
SET CCAIN = &STR($DATECNV('DD MON YY','YY/MM/DD',CREATE DATE) AT 35 -)
PUTFILE CCAIN
SET CCAIN = &STR($DATECNV('DD MON YY','YY/MM/DD',LAST UPDATED) AT 49)
PUTFILE CCAIN
SET CCAIN = &STR(        END FOR FOR1)
PUTFILE CCAIN
SET CCAIN = &STR(END)
PUTFILE CCAIN
SET CCAIN = &STR(CLOSE ALL)
PUTFILE CCAIN
SET CCAIN = &STR(FREE DATASET OUT&TID)
PUTFILE CCAIN
SET CCAIN = &STR(LOGOFF)
PUTFILE CCAIN
CLOSFILE CCAIN
CALL '&LOAD(BATCH2)' '&M204CHAN'
SET &M204RC = &LASTCC
IF &M204RC > 0 THEN -
DO
  ISPEXEC SETMSG MSG(MOD000)
  GOTO PANEL
END
/*                           */
/* TURN OFF DURING DEBUGGING */
/*                           */
CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST
ISPEXEC TBCREATE M204F -
  KEYS(M204NAME M204ENT M204UPBY M204CDAT M204UDAT -
       M204CCEN M204UCEN) NOWRITE REPLACE
IF &LASTCC > 4 THEN -
  DO
    WRITE TBCREATE FAILED
    EXIT
  END
ALLOC F(OUT&TID) DA('&UID..OUT&TID') SHR REUSE
OPENFILE OUT&TID INPUT
SET &EOF = N
DO WHILE &EOF = N
  ERROR -
    DO
      IF &LASTCC = 400 -
        THEN -
          SET &EOF = Y
        ELSE -
          SET &NULL = &STR()
      RETURN
    END
  GETFILE OUT&TID
  IF &EOF = Y THEN GOTO DONE
  SET &OUT      = &STR(OUT)
  SET &OUTPROC  = &&OUT&TID
  SET &M204NAME = &SUBSTR(1:8,&OUTPROC)
  SET &M204ENT  = &SUBSTR(10:20,&OUTPROC)
  SET &M204UPBY = &SUBSTR(22:31,&OUTPROC)
  SET &M204CDAT = &SUBSTR(35:42,&OUTPROC)
  SET &M204YY   = &SUBSTR(35:36,&OUTPROC)
  /* 66 IS START OF 19XX DATE WINDOW */
  IF &STR(&M204YY) < 66 THEN -
    SET M204CCEN = &STR(1)
  ELSE -
    SET M204CCEN = &STR(0)
  SET &M204UDAT = &SUBSTR(49:56,&OUTPROC)
  SET &M204YY   = &SUBSTR(49:50,&OUTPROC)
  /* 66 IS START OF 19XX DATE WINDOW */
  IF &STR(&M204YY) < 66 THEN -
    SET M204UCEN = &STR(1)
  ELSE -
    SET M204UCEN = &STR(0)
  IF &SUBSTR(1:1,&M204NAME) = &STR( ) THEN -
    DO
    END
  ELSE -
    DO
      ISPEXEC TBADD M204F
      IF &LASTCC > 0 THEN -
        DO
          WRITE TBADD FAILED
          EXIT
        END
    END
END
DONE: +
ERROR OFF
CLOSFILE OUT&TID
FREE F(OUT&TID)
/*           */
/* DEBUGGING */
/*           */
IF &DEBUG = &STR(DEBUG) THEN -
  CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT
ELSE -
  CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST
DISPLAY: +
SET CSSR = 1
SET RC   = 0
ISPEXEC TBTOP  M204F
DO WHILE &RC < 8
  ISPEXEC TBDISPL M204F PANEL(M204FT) CURSOR(ZCMD) CSRROW(&CSSR)
  SET RC = &LASTCC
  IF &LENGTH(&STR(&ZCMD)) > 5 THEN -
    IF &SUBSTR(1:5,&ZCMD) = &STR(SORT ) THEN -
      DO
        SET &SORTKEY = &STR(M204NAME,C,A)
        IF &SUBSTR(6:6,&ZCMD) = &STR(F) THEN -
          SET &SORTKEY = &STR(M204NAME,C,A)
        IF &SUBSTR(6:6,&ZCMD) = &STR(N) THEN -
          SET &SORTKEY = &STR(M204NAME,C,A)
        IF &SUBSTR(6:6,&ZCMD) = &STR(E) THEN -
          SET &SORTKEY = &STR(M204ENT,C,A)
        IF &SUBSTR(6:6,&ZCMD) = &STR(U) THEN -
          SET &SORTKEY = &STR(M204UPBY,C,A)
        IF &SUBSTR(6:6,&ZCMD) = &STR(C) THEN -
          SET &SORTKEY = &STR(M204CCEN,C,D,M204CDAT,C,D)
        IF &SUBSTR(6:6,&ZCMD) = &STR(L) THEN -
          SET &SORTKEY = &STR(M204UCEN,C,D,M204UDAT,C,D)
        ISPEXEC TBSORT M204F FIELDS(&SORTKEY,M204NAME,C,A)
        ISPEXEC TBTOP  M204F
      END
  IF &LENGTH(&STR(&ZCMD)) > 6 THEN -
    IF &SUBSTR(1:6,&ZCMD) = &STR(SORTA ) THEN -
      DO
        SET &SORTKEY = &STR(M204NAME,C,A)
        IF &SUBSTR(7:7,&ZCMD) = &STR(F) THEN -
          SET &SORTKEY = &STR(M204NAME,C,A)
        IF &SUBSTR(7:7,&ZCMD) = &STR(N) THEN -
          SET &SORTKEY = &STR(M204NAME,C,A)
        IF &SUBSTR(7:7,&ZCMD) = &STR(E) THEN -
          SET &SORTKEY = &STR(M204ENT,C,A)
        IF &SUBSTR(7:7,&ZCMD) = &STR(U) THEN -
          SET &SORTKEY = &STR(M204UPBY,C,A)
        IF &SUBSTR(7:7,&ZCMD) = &STR(C) THEN -
          SET &SORTKEY = &STR(M204CCEN,C,A,M204CDAT,C,A)
        IF &SUBSTR(7:7,&ZCMD) = &STR(L) THEN -
          SET &SORTKEY = &STR(M204UCEN,C,A,M204UDAT,C,A)
        ISPEXEC TBSORT M204F FIELDS(&SORTKEY,M204NAME,C,A)
        ISPEXEC TBTOP  M204F
      END
  IF &LENGTH(&STR(&ZCMD)) > 6 THEN -
    IF &SUBSTR(1:6,&ZCMD) = &STR(SORTD ) THEN -
      DO
        SET &SORTKEY = &STR(M204NAME,C,D)
        IF &SUBSTR(7:7,&ZCMD) = &STR(F) THEN -
          SET &SORTKEY = &STR(M204NAME,C,D)
        IF &SUBSTR(7:7,&ZCMD) = &STR(N) THEN -
          SET &SORTKEY = &STR(M204NAME,C,D)
        IF &SUBSTR(7:7,&ZCMD) = &STR(E) THEN -
          SET &SORTKEY = &STR(M204ENT,C,D)
        IF &SUBSTR(7:7,&ZCMD) = &STR(U) THEN -
          SET &SORTKEY = &STR(M204UPBY,C,D)
        IF &SUBSTR(7:7,&ZCMD) = &STR(C) THEN -
          SET &SORTKEY = &STR(M204CCEN,C,D,M204CDAT,C,D)
        IF &SUBSTR(7:7,&ZCMD) = &STR(L) THEN -
          SET &SORTKEY = &STR(M204UCEN,C,D,M204UDAT,C,D)
        ISPEXEC TBSORT M204F FIELDS(&SORTKEY,M204NAME,C,A)
        ISPEXEC TBTOP  M204F
      END
  IF &LENGTH(&STR(&ZCMD)) > 2 THEN -
    DO
      IF &SUBSTR(1:2,&ZCMD) = &STR(L ) OR -
         &SUBSTR(1:2,&ZCMD) = &STR(F ) THEN -
        DO
          ISPEXEC TBVCLEAR M204F
          SET &M204NAME = &SUBSTR(3:&LENGTH(&STR(&ZCMD)),&ZCMD)
          ISPEXEC TBTOP  M204F
          ISPEXEC TBSARG M204F NAMECOND(M204NAME,EQ)
          ISPEXEC TBSCAN M204F
          IF &LASTCC = 8 THEN -
            DO
              ISPEXEC TBTOP  M204F
              ISPEXEC TBSARG M204F NAMECOND(M204NAME,GT)
              ISPEXEC TBSCAN M204F
              IF &LASTCC = 8 THEN -
                ISPEXEC TBBOTTOM M204F
              ELSE -
                ISPEXEC TBSKIP M204F NUMBER(-1)
            END
          ISPEXEC TBQUERY M204F POSITION(CSSR)
        END
    END
  IF &LENGTH(&STR(&ZCMD)) > 7 THEN -
    DO
      IF &SUBSTR(1:7,&ZCMD) = &STR(LOCATE ) THEN -
        DO
          ISPEXEC TBVCLEAR M204F
          SET &M204NAME = &SUBSTR(8:&LENGTH(&STR(&ZCMD)),&ZCMD)
          ISPEXEC TBTOP  M204F
          ISPEXEC TBSARG M204F NAMECOND(M204NAME,EQ)
          ISPEXEC TBSCAN M204F
          IF &LASTCC = 8 THEN -
            DO
              ISPEXEC TBTOP  M204F
              ISPEXEC TBSARG M204F NAMECOND(M204NAME,GT)
              ISPEXEC TBSCAN M204F
              IF &LASTCC = 8 THEN -
                ISPEXEC TBBOTTOM M204F
              ELSE -
                ISPEXEC TBSKIP M204F NUMBER(-1)
            END
          ISPEXEC TBQUERY M204F POSITION(CSSR)
        END
    END
  IF &LENGTH(&STR(&ZCMD)) > 5 THEN -
    DO
      IF &SUBSTR(1:5,&ZCMD) = &STR(FIND ) THEN -
        DO
          ISPEXEC TBVCLEAR M204F
          SET &M204NAME = &SUBSTR(6:&LENGTH(&STR(&ZCMD)),&ZCMD)
          ISPEXEC TBTOP  M204F
          ISPEXEC TBSARG M204F NAMECOND(M204NAME,EQ)
          ISPEXEC TBSCAN M204F
          IF &LASTCC = 8 THEN -
            DO
              ISPEXEC TBTOP  M204F
              ISPEXEC TBSARG M204F NAMECOND(M204NAME,GT)
              ISPEXEC TBSCAN M204F
              IF &LASTCC = 8 THEN -
                ISPEXEC TBBOTTOM M204F
              ELSE -
                ISPEXEC TBSKIP M204F NUMBER(-1)
            END
          ISPEXEC TBQUERY M204F POSITION(CSSR)
        END
    END
END
GOTO PANEL
/* J.KALINICH, X4521 */
/* CLIST TO INVOKE MODEL 204 AND LIST FILES ON METADATA              */
./ ADD NAME=M204I    0159-88355-94105-0729-00059-00010-00000-TRIDJK
ISREDIT MACRO
/**********************************************************************/
/*    M204 EDIT PROCEDURE INITIAL MACRO:                              */
/*                                                                    */
/* 1. TURN RECOVERY ON FOR OUTXXXXN TEMPORARY DATASET                 */
/* 2. DELETE FIRST BLANK LINE IN MODEL 204 PROCEDURE DISPLAY          */
/* 3. DELETE ALL '- ' CONTINUATION RECORDS IN COLUMN 1                */
/* 4. ADD PROCEDURE HEADER/TRAILER LINES FOR NEW PROCEDURES           */
/* 5. ADD ==MSG> AND =NOTE= LINES REGARDING DELIMITERS                */
/* 6. TURN NULLS ON FOR EDIT SESSION                                  */
/*                                                                    */
/**********************************************************************/
CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST
ISREDIT NULLS = ON STD
ISREDIT RECOVERY = ON
ISREDIT DELETE .ZFIRST
ISREDIT EXCLUDE '- ' ALL 1
ISREDIT DELETE ALL X
ISPEXEC VGET (M204PROC) PROFILE
SET PROC1 = &STR(PROCEDURE &M204PROC)
SET PROC2 = &STR(END PROCEDURE)
SET L1 = &STR(.-------------------------------------------------------.)
SET L2 = &STR(| M204EDIT NOTES:                                       |)
SET L3 = &STR(|   THE "PROCEDURE NAME" AND "END PROCEDURE" DELIMITERS |)
SET L4 = &STR(|   ARE REQUIRED FOR A SUCCESSFUL PROCEDURE DEFINITION  |)
SET L5 = &STR(|   (SAVE) ONTO THE MODEL 204 PROCEDURE FILE.  DO NOT   |)
SET L6 = &STR(|   IMBED EXTRA DELIMITERS FROM CUT AND COPY COMMANDS.  |)
SET L7 = &STR('-------------------------------------------------------')
ISREDIT LOCATE 1
SET LOCATECC = &LASTCC
IF &LOCATECC > 0 -
  THEN -
    DO
      ISREDIT LINE_AFTER .ZFIRST = "&PROC1"
      ISREDIT LINE_AFTER .ZFIRST = "&PROC2"
      ISREDIT INSERT 1
      ISREDIT LINE_AFTER 0 = NOTELINE '&L7'
      ISREDIT LINE_AFTER 0 = NOTELINE '&L6'
      ISREDIT LINE_AFTER 0 = NOTELINE '&L5'
      ISREDIT LINE_AFTER 0 = NOTELINE '&L4'
      ISREDIT LINE_AFTER 0 = NOTELINE '&L3'
      ISREDIT LINE_AFTER 0 = MSGLINE  '&L2'
      ISREDIT LINE_AFTER 0 = NOTELINE '&L1'
    END
  ELSE -
    DO
      ISREDIT LINE_AFTER 0 = NOTELINE '&L7'
      ISREDIT LINE_AFTER 0 = NOTELINE '&L6'
      ISREDIT LINE_AFTER 0 = NOTELINE '&L5'
      ISREDIT LINE_AFTER 0 = NOTELINE '&L4'
      ISREDIT LINE_AFTER 0 = NOTELINE '&L3'
      ISREDIT LINE_AFTER 0 = MSGLINE  '&L2'
      ISREDIT LINE_AFTER 0 = NOTELINE '&L1'
      ISREDIT LOCATE 0
      /* RETURN CURSOR TO COMMAND LINE */
      EXIT CODE(1)
    END
/* J. KALINICH, X4521 */
/* INITIAL EDIT MACRO TO PREPARE M204 USER PROCEDURES FOR EDIT */
./ ADD NAME=M204LIBS 0114-91205-92363-1330-00017-00015-00000-TRIDJK
PROC 0 DEBUG
IF &DEBUG = &STR(DEBUG) THEN -
  CONTROL MAIN MSG NOFLUSH LIST CONLIST SYMLIST PROMPT ASIS
ELSE -
  CONTROL MAIN NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST ASIS
ISPEXEC CONTROL NONDISPL END
ISPEXEC DISPLAY PANEL(M204PROF)
CHECK: +
ISPEXEC VGET (M204LOAD M204CRAM) PROFILE
IF &SYSDSN(&M204LOAD) = &STR(OK) AND -
   &SYSDSN(&M204CRAM) = &STR(OK) THEN -
  EXIT
ELSE -
  %M204PROF
GOTO CHECK
/* J. KALINICH, X4521 */
/* CLIST TO VERIFY M204 PROFILE VARIABLES FOR LOAD AND CRAM */
./ ADD NAME=M204LOG  0122-89166-92363-1330-00023-00015-00000-TRIDJK
PROC 0 DEBUG LOG($LOG)
IF &DEBUG = &STR(DEBUG) THEN -
  CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT
ELSE -
  CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST
/* IF AMXSI-TRI THEN EDIT, ELSE BROWSE FOR OTHERS */
IF &SUBSTR(1:3,&SYSUID) = &STR(TRI) OR -
   &SUBSTR(1:3,&SYSUID) = &STR(S2K) THEN -
  DO
    SET &PDF = &STR(EDIT)
    SET &IMACRO = &STR(MACRO(M204NOTE))
  END
ELSE -
  DO
    SET &PDF = &STR(BROWSE)
    SET &IMACRO = &STR()
  END
ISPEXEC CONTROL ERRORS RETURN
ISPEXEC &PDF DATASET('M204.PARMS(&LOG)') &IMACRO
ISPEXEC CONTROL ERRORS CANCEL
EXIT
/* J.KALINICH, X4521 */
/* CLIST TO EDIT/BROWSE MODEL 204 SYSTEM CHANGE LOGS                  */
./ ADD NAME=M204NEW  0147-89055-94336-0813-00049-00123-00000-TRIDJK
PROC 0 DEBUG
IF &DEBUG = &STR(DEBUG) THEN -
  CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT ASIS
ELSE -
  CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST ASIS
ALLOC F(M204NEW) DA('AD.M204ISPF.SOURCE($204NEW)') SHR REUSE
SET EOF = N
OPENFILE M204NEW INPUT
ISPEXEC TBCREATE M204NEW +
  NAMES(M204$NEW) NOWRITE REPLACE
IF &LASTCC > 4 THEN -
  DO
    WRITE TBCREATE FAILED
    EXIT
  END
DO WHILE &EOF = N
  ERROR -
    DO
      IF &LASTCC = 400 -
        THEN -
          SET EOF = Y
        ELSE -
          SET NULL = &STR()
      RETURN
    END
  GETFILE M204NEW
  IF &EOF = Y THEN GOTO CLOSE
  SET &M204$NEW = &SUBSTR(2:&LENGTH(&STR(&M204NEW)),&STR(&M204NEW))
  ISPEXEC TBADD M204NEW
  IF &LASTCC NE 0 THEN -
    DO
      WRITE TBADD FAILED
    END
  ERROR OFF
END
CLOSE: +
ERROR OFF
CLOSFILE M204NEW
ISPEXEC TBTOP M204NEW
SET CSSR = 1
DO WHILE &RC < 8
ISPEXEC TBDISPL M204NEW PANEL(M204NEW) CURSOR(ZCMD) CSRROW(&CSSR)
SET RC = &LASTCC
END
ISPEXEC TBCLOSE M204NEW
EXIT: +
END
/* J. KALINICH, X4521 */
/* CLIST TO DISPLAY WHAT'S NEW IN MODEL 204 ISPF DIALOG DEVELOPMENT */
./ ADD NAME=M204NOTE 0133-89305-93242-1214-00022-00116-00000-TRIDJK
ISREDIT MACRO
CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST
SET L1 = &STR(.-------------------------------------------------------.)
SET L2 = &STR(| M204LOG NOTES:                                        |)
SET L3 = &STR(|  THE AMXSI-TRI ISPF "DATE" COMMAND CAN BE ISSUED AT   |)
SET L4 = &STR(|  THE COMMAND ===>_ LINE.  AN INPUT DATE FIELD ALLOWS  |)
SET L5 = &STR(|  FOR SPECIFYING A GIVEN DATE.  THE SCROLL PF KEYS CAN |)
SET L6 = &STR(|  BE USED TO SCROLL TO THE PREVIOUS/NEXT MONTH (UP/DN) |)
SET L7 = &STR(|  OR TO THE PREVIOUS/NEXT YEAR (LEFT/RIGHT).           |)
SET L8 = &STR('-------------------------------------------------------')
ISREDIT LINE_AFTER 0 = NOTELINE '&L8'
ISREDIT LINE_AFTER 0 = NOTELINE '&L7'
ISREDIT LINE_AFTER 0 = NOTELINE '&L6'
ISREDIT LINE_AFTER 0 = NOTELINE '&L5'
ISREDIT LINE_AFTER 0 = NOTELINE '&L4'
ISREDIT LINE_AFTER 0 = NOTELINE '&L3'
ISREDIT LINE_AFTER 0 = MSGLINE  '&L2'
ISREDIT LINE_AFTER 0 = NOTELINE '&L1'
/* RETURN CURSOR TO COMMAND LINE */
EXIT CODE(1)
/* J. KALINICH, X4521 */
/* INITIAL EDIT MACRO TO NOTIFY M204LOG USERS OF DATE COMMAND  */
./ ADD NAME=M204PROF 0108-91205-92363-1331-00026-00015-00000-TRIDJK
PROC 0 DEBUG
IF &DEBUG = &STR(DEBUG) THEN -
  CONTROL MAIN MSG NOFLUSH LIST CONLIST SYMLIST PROMPT ASIS
ELSE -
  CONTROL MAIN NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST ASIS
PANEL: +
ISPEXEC DISPLAY PANEL(M204PROF)
SET &PROFCC = &LASTCC
IF &SYSDSN(&M204LOAD) ¬= &STR(OK) THEN -
  DO
    SET ZEDSMSG = &STR(&SYSDSN(&M204LOAD))
    SET ZEDLMSG = &STR(&M204LOAD - &SYSDSN(&M204LOAD))
    ISPEXEC SETMSG MSG(ISRZ001)
  END
IF &SYSDSN(&M204CRAM) ¬= &STR(OK) THEN -
  DO
    SET ZEDSMSG = &STR(&SYSDSN(&M204CRAM))
    SET ZEDLMSG = &STR(&M204CRAM - &SYSDSN(&M204CRAM))
    ISPEXEC SETMSG MSG(ISRZ001)
  END
IF &PROFCC = 8 THEN -
  EXIT
ELSE -
  GOTO PANEL
/* J. KALINICH, X4521 */
/* CLIST TO DISPLAY SET M204 PROFILE VARIABLES FOR LOAD AND CRAM */
./ ADD NAME=M204UL   0120-90310-91275-0658-00057-00008-00000-TRIDJK
ISREDIT MACRO
/**********************************************************************/
/*  EDIT MACRO TO FIND ALL CONTROL LOGIC FLOW FOR MODEL 204           */
/*  USER LANGUAGE PROGRAMS.                                           */
/*                                                                    */
/*  TO RUN:                                                           */
/*    ENTER M204UL ON THE COMMAND LINE                                */
/**********************************************************************/
CONTROL NOMSG MAIN NOLIST NOCONLIST NOSYMLIST
ISREDIT (USTATE) = USER_STATE
ISREDIT EXCLUDE ALL
ISREDIT FIND ALL ': ' 1 20           /* LABELS */
ISREDIT FIND ALL 'BEGIN '
ISREDIT FIND ALL 'CALL '
ISREDIT FIND ALL 'ELSE '
ISREDIT FIND ALL 'ELSEIF '
ISREDIT FIND ALL 'END '
ISREDIT FIND ALL 'FD '
ISREDIT FIND ALL 'FDR '
ISREDIT FIND ALL 'FDV '
ISREDIT FIND ALL 'FEO '
ISREDIT FIND ALL 'FIND '
ISREDIT FIND ALL 'FOR '
ISREDIT FIND ALL 'FPC '
ISREDIT FIND ALL 'FR '
ISREDIT FIND ALL 'FRN '
ISREDIT FIND ALL 'FRV '
ISREDIT FIND ALL 'IF '
ISREDIT FIND ALL 'INCLUDE '
ISREDIT FIND ALL 'JUMP '
ISREDIT FIND ALL 'MORE '
ISREDIT FIND ALL 'PROCEDURE '
ISREDIT FIND ALL 'REPEAT '
ISREDIT FIND ALL 'RETURN '
ISREDIT FIND ALL 'THEN '
ISREDIT EXCLUDE ALL '*' 1 20         /* COMMENTS */
ISREDIT EXCLUDE ALL '%' 1            /* VARIABLES */
ISREDIT EXCLUDE ALL 'END ARRAY'
ISREDIT EXCLUDE ALL 'END IMAGE'
ISREDIT EXCLUDE ALL 'END MENU'
ISREDIT EXCLUDE ALL 'END SCREEN'
ISREDIT EXCLUDE ALL 'END STORE'
ISREDIT EXCLUDE ALL 'END USE'
ISREDIT EXCLUDE ALL 'FOR ALL'
ISREDIT EXCLUDE ALL 'FOR INOUT'
ISREDIT EXCLUDE ALL 'FOR INPUT'
ISREDIT EXCLUDE ALL 'FOR OUTPUT'
ISREDIT EXCLUDE ALL 'FOR PRINT'
ISREDIT EXCLUDE ALL 'FOR READ'
ISREDIT EXCLUDE ALL 'FOR REREAD'
ISREDIT EXCLUDE ALL 'FOR TAG'
ISREDIT EXCLUDE ALL 'PRINT '
ISREDIT EXCLUDE ALL 'PROMPT '
ISREDIT EXCLUDE ALL 'TITLE '
ISREDIT USER_STATE = (USTATE)
/* J. KALINICH, X4521 */
/* EDIT MACRO TO SHOW FLOW OF CONTROL WORDS FOR M204 USER LANGUAGE */
./ ADD NAME=M204UT1  0121-89166-92363-1331-00079-00015-00000-TRIDJK
PROC 0 DEBUG
IF &DEBUG = &STR(DEBUG) THEN -
  CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT
ELSE -
  CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST
ISPEXEC DISPLAY PANEL(M204UT1)
IF &LASTCC = 8 THEN EXIT
%M204LIBS     /* VERIFY LOAD AND CRAM LIBRARY NAMES */
ISPEXEC VGET (M204LOAD M204CRAM) PROFILE
SET &LOAD = &SUBSTR(2:&LENGTH(&STR(&M204LOAD))-1,&STR(&M204LOAD))
SET &CRAM = &SUBSTR(2:&LENGTH(&STR(&M204CRAM))-1,&STR(&M204CRAM))
IF &STR(&M204SPGM) = &STR(PDS) THEN -
  GOTO PDS
/* STRIP OFF WORD/SUFFIX/PREFIX/C FOR SEARCH-FOR OPTION */
SET I = 1
DO WHILE &I <= 5
SET &SRCH&I = &STR()
SET &SFS = &&M204STR&I
IF &STR(&SFS) ¬= &STR() THEN -
  DO
    IF &SUBSTR(1:1,&STR(&SFS)) ¬= &STR(') THEN -
      DO
        IF &LENGTH(&STR(&SFS)) > 5 THEN -
          DO
            IF &SUBSTR(&LENGTH(&STR(&SFS))-4:+
               &LENGTH(&STR(&SFS)),&STR(&SFS)) = &STR( WORD) THEN -
              SET &SFS = &SUBSTR(1:&LENGTH(&STR(&SFS))-3,&STR(&SFS))
          END
        IF &LENGTH(&STR(&SFS)) > 7 THEN -
          DO
            IF &SUBSTR(&LENGTH(&STR(&SFS))-6:+
               &LENGTH(&STR(&SFS)),&STR(&SFS)) = &STR( PREFIX) THEN -
              SET &SFS = &SUBSTR(1:&LENGTH(&STR(&SFS))-5,&STR(&SFS))
          END
        IF &LENGTH(&STR(&SFS)) > 7 THEN -
          DO
            IF &SUBSTR(&LENGTH(&STR(&SFS))-6:+
               &LENGTH(&STR(&SFS)),&STR(&SFS)) = &STR( SUFFIX) THEN -
              SET &SFS = &SUBSTR(1:&LENGTH(&STR(&SFS))-5,&STR(&SFS))
          END
        IF &SUBSTR(&LENGTH(&STR(&SFS))-1:+
                   &LENGTH(&STR(&SFS)),&STR(&SFS)) = &STR( C) OR -
           &SUBSTR(&LENGTH(&STR(&SFS))-1:+
                   &LENGTH(&STR(&SFS)),&STR(&SFS)) = &STR( P) OR -
           &SUBSTR(&LENGTH(&STR(&SFS))-1:+
                   &LENGTH(&STR(&SFS)),&STR(&SFS)) = &STR( S) OR -
           &SUBSTR(&LENGTH(&STR(&SFS))-1:+
                   &LENGTH(&STR(&SFS)),&STR(&SFS)) = &STR( W) THEN -
          DO
            SET &SFS = &STR(')+
                       &SUBSTR(1:&LENGTH(&STR(&SFS))-2,&STR(&SFS))+
                       &STR(',)+
                       &SUBSTR(&LENGTH(&STR(&SFS)),&STR(&SFS))
          END
        ELSE -
          SET &SFS = &STR('&STR(&SFS)')
      END
    IF &SUBSTR(&LENGTH(&STR(&SFS)),&STR(&SFS)) = &STR(C) THEN -
      DO
        SET &SFS = &SUBSTR(1:&LENGTH(&STR(&SFS))-2,&STR(&SFS))
        SET &SRCH&I = &STR(SRCHFORC)
      END
    ELSE -
      DO
        SET &SRCH&I = &STR(SRCHFOR)
      END
    SET &SFS&I = &STR(&SFS)
  END
SET I = &I + 1
END
PDS: +
ISPEXEC FTOPEN
ISPEXEC FTINCL M204UT1
ISPEXEC FTCLOSE NAME(M204UT1)
/* INVOKE INITIAL JOBCARD MACRO AND PUT USER IN EDIT */
ISPEXEC EDIT DATASET(GEN.CNTL(M204UT1)) MACRO(JCI)
END
/* J.KALINICH, X4521 */
/* CLIST TO BUILD/SEARCH M204 PROCEDURE PDS                         */
./ ADD NAME=M204UT2  0139-89055-92363-1331-00122-00123-00000-TRIDJK
PROC 0 DEBUG
IF &DEBUG = &STR(DEBUG) THEN -
  CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT
ELSE -
  CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST
%M204LIBS     /* VERIFY LOAD LIBRARY NAME */
ISPEXEC VGET (M204LOAD) PROFILE
SET &LOAD = &SUBSTR(2:&LENGTH(&STR(&M204LOAD))-1,&STR(&M204LOAD))
SET &UID = &SYSUID
DELETE ('&UID..CCAIN' '&UID..CCAOUT')
FREE F(CCAIN CCAOUT)
FREE ATTR(DCB)
ATTR DCB RECFM(F B) LRECL(132) BLKSIZE(6336)
ALLOC F(CCAIN) DA('&UID..CCAIN') NEW CATALOG SP(1 1) -
  TRACK RELEASE USING(DCB) REUSE
ALLOC F(CCAOUT) DA('&UID..CCAOUT') NEW CATALOG SP(1 1) -
  TRACK RELEASE USING(DCB) REUSE
PANEL: +
ISPEXEC DISPLAY PANEL(M204UT2)
IF &LASTCC = 8 THEN EXIT
OPENFILE CCAIN OUTPUT
SET CCAIN = &STR( )
PUTFILE CCAIN
SET CCAIN = &STR(LOGIN &M204LOGO)
PUTFILE CCAIN
SET CCAIN = &STR(&M204PSWD)
PUTFILE CCAIN
IF &LPDLST ¬= &STR( ) THEN -
  DO
    SET CCAIN = &STR(UTABLE LPDLST &LPDLST)
    PUTFILE CCAIN
  END
IF &LGTBL ¬= &STR( ) THEN -
  DO
    SET CCAIN = &STR(UTABLE LGTBL &LGTBL)
    PUTFILE CCAIN
  END
IF &LQTBL ¬= &STR( ) THEN -
  DO
    SET CCAIN = &STR(UTABLE LQTBL &LQTBL)
    PUTFILE CCAIN
  END
IF &LFTBL ¬= &STR( ) THEN -
  DO
    SET CCAIN = &STR(UTABLE LFTBL &LFTBL)
    PUTFILE CCAIN
  END
IF &LNTBL ¬= &STR( ) THEN -
  DO
    SET CCAIN = &STR(UTABLE LNTBL &LNTBL)
    PUTFILE CCAIN
  END
IF &LXTBL ¬= &STR( ) THEN -
  DO
    SET CCAIN = &STR(UTABLE LXTBL &LXTBL)
    PUTFILE CCAIN
  END
IF &LSTBL ¬= &STR( ) THEN -
  DO
    SET CCAIN = &STR(UTABLE LSTBL &LSTBL)
    PUTFILE CCAIN
  END
IF &LITBL ¬= &STR( ) THEN -
  DO
    SET CCAIN = &STR(UTABLE LITBL &LITBL)
    PUTFILE CCAIN
  END
IF &LTTBL ¬= &STR( ) THEN -
  DO
    SET CCAIN = &STR(UTABLE LTTBL &LTTBL)
    PUTFILE CCAIN
  END
IF &LFSCB ¬= &STR( ) THEN -
  DO
    SET CCAIN = &STR(UTABLE LFSCB &LFSCB)
    PUTFILE CCAIN
  END
IF &LVTBL ¬= &STR( ) THEN -
  DO
    SET CCAIN = &STR(UTABLE LVTBL &LVTBL)
    PUTFILE CCAIN
  END
SET CCAIN = &STR(VIEW LSERVER)
PUTFILE CCAIN
SET CCAIN = &STR(LOGOFF)
PUTFILE CCAIN
CLOSFILE CCAIN
CALL '&LOAD(BATCH2)' '&M204CHAN'
SET &M204RC = &LASTCC
IF &M204RC > 0 THEN -
  DO
    ISPEXEC SETMSG MSG(MOD000)
    GOTO PANEL
  END
OPENFILE CCAOUT INPUT
SET &EOF = N
DO WHILE &EOF = N
  ERROR -
    DO
      IF &LASTCC = 400 -
        THEN -
          SET &EOF = Y
        ELSE -
          SET &NULL = &STR()
      RETURN
    END
  GETFILE CCAOUT
  IF &EOF = Y THEN GOTO DONE
  IF &SUBSTR(1:7,&CCAOUT) = &STR(LSERVER) THEN -
    DO
      SET &LSERVER = &SUBSTR(11:19,&CCAOUT)
      SET &SHORT = &STR(RINNN...G)
      SET &LONG  = &STR(SERVER SIZE CALCULATED FROM UTABLE SETTINGS)
      ISPEXEC SETMSG MSG(MOD004)
    END
END
DONE: +
ERROR OFF
CLOSFILE CCAOUT
GOTO PANEL
/* J.KALINICH, X4521 */
/* CLIST TO INVOKE MODEL 204 TO CALCULATE SERVER SIZE                */
