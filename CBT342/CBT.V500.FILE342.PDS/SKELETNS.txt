./ ADD NAME=M204DM1  0127-89166-93251-1216-00077-00059-00000-TRIDSJ
)CM
)CM *****     JOB CARD GENERATED ON THE FLY BY JCI MACRO  *****
)CM
//*
//*   JCL FOR DOWNLOAD FROM DATAMANAGER TO DICTIONARY 204
//*
)CM
)CM *****     IDCAMS RESTART STEP                         *****
)CM
//DELETE   EXEC  PGM=IDCAMS
//SYSPRINT DD SYSOUT=*
 DELETE (&ZUSER..M204.DOWNLOAD)
)CM
)CM *****     DATAMANAGER TRANSLATE/TRANSFER TO M204      *****
)CM
//DATAMNGR EXEC  PGM=CM00,REGION=6144K,TIME=1439
//STEPLIB  DD  DSN=AD.DMG.LOADLIB,DISP=SHR
//MPAID    DD  DSN=AD.DMG.MPAID,DISP=SHR
//MPOUT    DD  SYSOUT=*
//MPIN     DD  DDNAME=SYSIN
//SYSUDUMP DD  SYSOUT=*
//SOLID    DD  DSN=AD.SOLID.INDEX,DISP=SHR
//SOLIDD   DD  DSN=AD.SOLID.DATA.ENTRIES,DISP=SHR
//SOLIDS   DD  DSN=AD.SOLID.SOURCE,DISP=SHR
//SOLIDE   DD  DSN=AD.SOLID.RECOVER,DISP=SHR
//DOWNLOAD DD  DSN=&ZUSER..M204.DOWNLOAD,DISP=(,CATLG,DELETE),
//             UNIT=DISK,SPACE=(CYL,(5,5))
  LOGON USER PASSWORD USER;
  DI SOLID;
  AU 'PUBLIC';
  M204P '&DM204FIL';
  LOGOFF;
)CM
)CM *****     DICTIONARY/204 CREATE METADATA & DATALINK   *****
)CM
//BATCH2   EXEC  PGM=BATCH2,
//             REGION=2048K,
//             COND=(0,NE,DATAMNGR),
//             PARM='&M204CHAN'
//STEPLIB  DD  DSN=&LOAD,DISP=SHR
//         DD  DSN=&CRAM,DISP=SHR
//CCAOUT   DD  SYSOUT=*
//CCAIN    DD  *
LOGIN &M204LOGO
&M204PSWD
RESET MCPU   1000000
RESET MBSCAN 1000000
RESET MCNCT  100000
RESET MDKRD  1000000
RESET MDKWR  1000000
FREE DATASET OUTRMETA
ALLOCATE DATASET OUTRMETA WITH SCOPE=SYSTEM -
  DSNAME=&ZUSER..M204.DOWNLOAD COND SHARE
UTABLE LNTBL 1000
UTABLE LQTBL 3500
UTABLE LSTBL 8000
UTABLE LVTBL 2047
B
%DEPT = '&M204LOGO'
%DEV  = '&M204DEV'
%X = $SETG('DEPT',%DEPT)
%X = $SETG('DEV',%DEV)
END
O &M204FILE
PASSWORD
O METADATA
PASSWORD
O DATALINK
PASSWORD
O DTSFDMMT
O DTSFDMDT
IN &M204FILE I DMGRLOAD
IN &M204FILE I DMGRDATA
IN &M204FILE I DMGRMETA
CLOSE ALL
FREE DATASET OUTRMETA
LOGOUT
./ ADD NAME=M204DM2  0159-89166-93251-1216-00133-00059-00000-TRIDSJ
)CM
)CM *****     JOB CARD GENERATED ON THE FLY BY JCI MACRO  *****
)CM
//*
//*   JCL FOR M204 FIELD NAME CHECK IN DATAMANAGER
//*
)CM
)CM *****     IDCAMS RESTART STEP                         *****
)CM
//DELETE   EXEC  PGM=IDCAMS
//SYSPRINT DD  SYSOUT=*
 DELETE (&SYSUID..M204.FIELDCHK)
 DELETE (&SYSUID..M204.FIELDCHK.RENAMED)
 DELETE (&SYSUID..M204.POST)
)CM
)CM *****     IEFBR14 CATALOG STEP FOR M204              *****
)CM
//IEFBR14  EXEC  PGM=IEFBR14
//CATALOG  DD  DSN=&SYSUID..M204.FIELDCHK,DISP=(,CATLG,DELETE),
//             UNIT=DISK,SPACE=(TRK,(1,5)),
//             DCB=(RECFM=FB,LRECL=80,BLKSIZE=6320)
)CM
)CM *****     DICTIONARY/204 EXTRACT FIELD NAMES          *****
)CM
//BATCH2   EXEC  PGM=BATCH2,
//             REGION=2048K,
//             PARM='&M204CHAN'
//STEPLIB  DD  DSN=&LOAD,DISP=SHR
//         DD  DSN=&CRAM,DISP=SHR
//CCAOUT   DD  SYSOUT=*
//CCAIN    DD  *
)CM
)CM  FILE TAILORING NOTE:
)CM
)CM  THE DEFAULT CHARACTERS IN FILE TAILORING ARE  )&?!<|>
)CM  SO, YOU MUST CODE TWO IN SUCCESSION TO GET ONE OUT
)CM  IN YOUR USER LANGUAGE CODE.
)CM
LOGIN &M204LOGO
&M204PSWD
RESET MBSCAN -1

FREE DATASET OUTDMGR
ALLOCATE DATASET OUTDMGR WITH SCOPE=SYSTEM -
  DSNAME=&SYSUID..M204.FIELDCHK COND SHARE

O DATALINK
PASSWORD
O METADATA
PASSWORD

RESET HDRCTL 1
USE OUTDMGR

BEGIN

%FILE    IS STRING LEN 70
%RECORD  IS STRING LEN 70
%FIELD   IS STRING LEN 70
%DMGR    IS STRING LEN 70

%FILE = '&M204FIL'

        PRINT 'POST TEXT "MODEL 204 FILE(S) &M204FIL FIELD NAMES".'

FIND1:  IN DATALINK FD NAME1 IS LIKE %FILE
                       ENT1='FILE'
                       ENT2='RECORD'
        END FIND FIND1

SORT1:  SORT RECORDS IN FIND1 BY NAME2

        NEW PAGE
        PRINT 'DI SOLID.'
        PRINT 'AU PUBLIC.'

FOR1:   FR SORT1
        %RECORD = NAME2
        PRINT 'POST TEXT " RECORD ' %RECORD WITH '".'

        FIND2:  IN DATALINK FD NAME1=%RECORD
                               ENT2='FIELD'
                               REFTYPE='~'
                END FIND FIND2

        SORT2:  SORT RECORDS IN FIND2 BY NAME2

        FOR2:  FR SORT2

               %DMGR = $SUBSTR(NAME2,$INDEX(NAME2,'.')+1,70)
               %DMGR = $TRANS(%DMGR,'_','-')
               PRINT 'POST WHAT IS ' %DMGR WITH '.'
               END FOR FOR2

        END FOR FOR1

END

CLOSE ALL
FREE DATASET OUTDMGR
LOGOUT
)CM
)CM *****     RENAME M204 OUTPUT TO AVOID ENQ CONFLICT    *****
)CM
//RENAME   EXEC  PGM=IDCAMS
//SYSPRINT DD SYSOUT=*
 ALTER     &SYSUID..M204.FIELDCHK -
   NEWNAME(&SYSUID..M204.FIELDCHK.RENAMED)
)CM
)CM *****     DATAMANAGER 'WHAT IS' FIELD-NAME QUERY      *****
)CM
//DATAMNGR EXEC  PGM=CM00,REGION=6144K,TIME=1439
//STEPLIB  DD  DSN=AD.DMG.LOADLIB,DISP=SHR
//MPAID    DD  DSN=AD.DMG.MPAID,DISP=SHR
//MPIN     DD  DSN=&SYSUID..M204.FIELDCHK.RENAMED,DISP=SHR
//MPOUT    DD  DUMMY
//MPRPOST  DD  DSN=&SYSUID..M204.POST,DISP=(,CATLG,DELETE),
//             UNIT=DISK,SPACE=(TRK,(5,5),RLSE)
//SYSUDUMP DD  SYSOUT=*
//SOLID    DD  DSN=AD.SOLID.INDEX,DISP=SHR
//SOLIDD   DD  DSN=AD.SOLID.DATA.ENTRIES,DISP=SHR
//SOLIDS   DD  DSN=AD.SOLID.SOURCE,DISP=SHR
//SOLIDE   DD  DSN=AD.SOLID.RECOVER,DISP=SHR
)CM
)CM *****     PDS COMMAND TO FIND 'NOT ON DICTIONARY'       *****
)CM
//FIND     EXEC  PGM=IKJEFT01,DYNAMNBR=30
//STEPLIB  DD DSN=AD.CMDLIB,DISP=SHR
//SYSTSPRT DD SYSOUT=*
//SYSTSIN  DD *
 PDS '&SYSUID..M204.POST'
 FIND 'NOT ON THE DICTIONARY'
 LIST
./ ADD NAME=M204DM3  0162-89166-93055-1328-00110-00059-00000-TRIDJK
)CM
)CM *****     JOB CARD GENERATED ON THE FLY BY JCI MACRO  *****
)CM
//*
//*   JCL FOR M204 RECORD LAYOUT FROM MODEL 204 DICTIONARY
//*
)CM
)CM *****     DICTIONARY/204 RECORD LAYOUT                *****
)CM
//BATCH2   EXEC  PGM=BATCH2,
//             REGION=2048K,
//             PARM='&M204CHAN'
//STEPLIB  DD  DSN=&LOAD,DISP=SHR
//         DD  DSN=&CRAM,DISP=SHR
//CCAOUT   DD  SYSOUT=*
//CCAIN    DD  *
)CM
)CM  FILE TAILORING NOTE:
)CM
)CM  THE DEFAULT CHARACTERS IN FILE TAILORING ARE  )&?!<|>
)CM  SO, YOU MUST CODE TWO IN SUCCESSION TO GET ONE OUT
)CM  IN YOUR USER LANGUAGE CODE.
)CM
LOGIN &M204LOGO
&M204PSWD
RESET MBSCAN -1

*DEVICE 132,55
O DATALINK
PASSWORD
O METADATA
PASSWORD

BEGIN
%NAME1 IS STRING LEN 70
%NAME2 IS STRING LEN 70
%FILE IS STRING LEN 20
%FLD IS STRING LEN 200
%A IS STRING LEN 20
%B IS STRING LEN 1
%C IS STRING LEN 3
%KEY = 'KEY Y/N'
%ORD = 'ORDERED Y/N'

%NAME1 = '&M204FIL'

FIND1:  IN DATALINK FD ENT1='FILE' AND NAME1 IS LIKE %NAME1 -
             AND ENT2='RECORD'
        END FIND
        SET HEADER 2 '        REPORT OF       ' AT 30
        SET HEADER 3 'FILE AND RECORD DESCRIPTIONS' AT 30
        SET HEADER 4 'FOR SYSTEM' AT 30 AND %NAME1 AT 45
        SET HEADER 5
        NEW PAGE
SORT1:    SORT RECORDS IN FIND1 BY NAME1
FOR1:     FR SORT1
           %NAME1 = NAME1
           %NAME2 = NAME2
           %DOTF = $INDEX(%NAME1,'.')
           %FILE = $SUBSTR(%NAME1,%DOTF-1)
           %DOT = $INDEX(%NAME2,'.')
           %REC = $SUBSTR(%NAME2,%DOT+1)
           SET HEADER 2
           SET HEADER 3 'RECORD DESCRIPTIONS' AT 30
           SET HEADER 4 'FOR FILE' AT 30 AND %FILE AT 40
           SET HEADER 5
           NEW PAGE
           SKIP 1 LINE
           PRINT 'RECORD = ' AT 5 AND %REC AT 20
FIND2:     IN DATALINK FD ENT1='RECORD' AND NAME1=%NAME2 -
                AND ENT2='FIELD' AND REFTYPE='~'
           END FIND
SORT2:       SORT RECORDS IN FIND2 BY NAME2
             FR SORT2
                %NAME2 = NAME2
FIND3:          IN METADATA FD NAME=%NAME2 AND ENTITY='FIELD'
                END FIND
                  FR FIND3
                    %A = ''
                    %B = 'N'
                    %C = '1'
                    %D = 'N'
                    %A = PHYSICAL PICTURE
                    IF %%KEY = 'Y' THEN
                       %B = 'Y'
                    END IF
                    IF MAX OCCURRENCES >> 1 THEN
                       %C = MAX OCCURRENCES
                    END IF
                    IF %%ORD = 'Y' THEN
                       %D = 'Y'
                    END IF
                  END FOR
                %DOT = $INDEX(%NAME2,'.')
                %FLD = $SUBSTR(%NAME2,%DOT+1)
                PRINT 'FIELD = ' AT 8 AND %FLD AT 20 -
                  AND 'PIC ' AT 50 AND %A AT 55 -
                  AND 'KEY ' AT 75 AND %B AT 80 -
                  AND 'MAX OCC ' AT 87 AND %C AT 96 -
                  AND 'ORD FLD ' AT 102 AND %D AT 111
              END FOR
        END FOR
        SET HEADER 2
        SET HEADER 3
        SET HEADER 4
        SET HEADER 5
        NEW PAGE
END
CLOSE ALL
LOGOUT
./ ADD NAME=M204DM5  0166-89166-93055-1328-00160-00059-00000-TRIDJK
)CM
)CM *****     JOB CARD GENERATED ON THE FLY BY JCI MACRO  *****
)CM
//*
//*   JCL FOR DICTIONARY/204 IMPORT TO DATAMANAGER
//*
)CM
)CM *****     IDCAMS RESTART STEP                         *****
)CM
//DELETE   EXEC  PGM=IDCAMS
//SYSPRINT DD  SYSOUT=*
 DELETE (&SYSUID..M204.IMPORT)
)CM
)CM *****     IEFBR14 CATALOG STEP FOR M204              *****
)CM
//IEFBR14  EXEC  PGM=IEFBR14
//CATALOG  DD  DSN=&SYSUID..M204.IMPORT,DISP=(,CATLG,DELETE),
//             UNIT=DISK,SPACE=(TRK,(5,5)),
//             DCB=(RECFM=FB,LRECL=80,BLKSIZE=6320)
)CM
)CM *****     DICTIONARY/204 EXTRACT FILE/RECORD/FIELDS   *****
)CM
//BATCH2   EXEC  PGM=BATCH2,
//             REGION=2048K,
//             PARM='&M204CHAN'
//STEPLIB  DD  DSN=&LOAD,DISP=SHR
//         DD  DSN=&CRAM,DISP=SHR
//CCAOUT   DD  SYSOUT=*
//CCAIN    DD  *
)CM
)CM  FILE TAILORING NOTE:
)CM
)CM  THE DEFAULT CHARACTERS IN FILE TAILORING ARE  )&?!<|>
)CM  SO, YOU MUST CODE TWO IN SUCCESSION TO GET ONE OUT
)CM  IN YOUR USER LANGUAGE CODE.
)CM
LOGIN &M204LOGO
&M204PSWD
RESET MBSCAN -1

FREE DATASET OUTDMGR
ALLOCATE DATASET OUTDMGR WITH SCOPE=SYSTEM -
  DSNAME=&SYSUID..M204.IMPORT COND SHARE

O DATALINK
PASSWORD
O METADATA
PASSWORD

RESET HDRCTL 1
USE OUTDMGR

BEGIN

%FILE    IS STRING LEN 70
%RECORD  IS STRING LEN 70
%FIELD   IS STRING LEN 70
%DMGR    IS STRING LEN 70
%LASTUPD = 'LAST UPDATED'
%CREATED = 'CREATE DATE'
%UPDBY   = 'UPDATED-BY'
%FILES = '&M204FIL'

        PRINT 'TEXT "IMPORT OF MODEL 204 FILE(S):  &M204FIL".'
*
* FIRST PASS TO BUILD DATAMANAGER FILE MEMBERS
*
DATA1:  -
        IN DATALINK FD NAME1 IS LIKE %FILES
                       ENT1='FILE'
                       ENT2='RECORD'
        END FIND
        %FILE = ' '
SORT1:  -
        SORT RECORDS IN DATA1 BY NAME2
        NEW PAGE
FORD1:  -
        FR SORT1
        IF %FILE = NAME1 THEN
          %CONT = ','
        ELSE
          %CONT = ' '
          %FILE = NAME1
META:     -
          IN METADATA FD ENTITY=FILE AND NAME=%FILE
          END FIND
          FOR 1 RECORD IN META
          %NOTE1 = %%LASTUPD
          %NOTE2 = %%CREATED
          %NOTE3 = %%UPDBY
          END FOR
          PRINT '.'
          PRINT 'REPLACE M204-' WITH %FILE WITH '.'
          PRINT 'FILE'
          PRINT 'NOTE'
          PRINT '"DICTIONARY/204 INFORMATION:"'
          PRINT '"  LAST UPDATED ' WITH %NOTE1 WITH '"'
          PRINT '"  CREATE DATE  ' WITH %NOTE2 WITH '"'
          PRINT '"  UPDATED-BY   ' WITH %NOTE3 WITH '"'
          PRINT 'CONTAINS'
        END IF
        %RECORD = NAME2
        PRINT %CONT WITH '"M204-' WITH -
              $TRANS(%RECORD,'_','-') WITH '"'
        END FOR
        PRINT '.'
*
* SECOND PASS TO BUILD DATAMANAGER RECORD MEMBERS
*
DATA2:  -
        IN DATALINK FD NAME1 IS LIKE %FILES
                       ENT1='FILE'
                       ENT2='RECORD'
        END FIND
SORT2:  -
        SORT RECORDS IN DATA2 BY NAME2
        NEW PAGE
FORD2:  -
        FR SORT2
        %RECORD = NAME2
        DATA3:  -
                IN DATALINK FD NAME1=%RECORD
                               ENT2='FIELD'
                               REFTYPE='~'
                END FIND
        %RECORD = ' '
        SORT3:  -
                SORT RECORDS IN DATA3 BY NAME2
        FORD3:  -
                FR SORT3
                IF %RECORD = NAME1 THEN
                  %CONT = ','
                ELSE
                  %CONT = ' '
                  %RECORD = NAME1
                  PRINT '.'
                  PRINT 'REPLACE "M204-' WITH -
                        $TRANS(%RECORD,'_','-') WITH '".'
                  PRINT 'RECORD'
                  PRINT 'CONTAINS'
                END IF
                %DMGR = $SUBSTR(NAME2,$INDEX(NAME2,'.')+1,70)
                %DMGR = $TRANS(%DMGR,'_','-')
                PRINT %CONT WITH '"' WITH %DMGR WITH '"'
                END FOR
        END FOR
        PRINT '.'
END
CLOSE ALL
FREE DATASET OUTDMGR
LOGOUT
)CM
)CM *****     PDS COMMAND TO LIST IMPORTED DEFINITIONS      *****
)CM
//LIST     EXEC  PGM=IKJEFT01,DYNAMNBR=30
//STEPLIB  DD DSN=AD.CMDLIB,DISP=SHR
//SYSTSPRT DD SYSOUT=*
//SYSTSIN  DD *
 PDS '&SYSUID..M204.IMPORT'
 LIST
./ ADD NAME=M204DM6  0123-91101-93251-1216-00054-00053-00000-TRIDSJ
)CM
)CM *****     JOB CARD GENERATED ON THE FLY BY JCI MACRO  *****
)CM
//*
//*   JCL FOR VALIDATION OF DATAMANAGER M204 FILE DEFINITIONS
//*
)CM
)CM *****     IDCAMS RESTART STEP                         *****
)CM
//DELETE   EXEC  PGM=IDCAMS
//SYSPRINT DD SYSOUT=*
 DELETE (&SYSUID..M204VAL)
)CM
)CM *****     DATAMANAGER M204 FILE VALIDATION            *****
)CM
//DATAMNGR EXEC  PGM=CM00,REGION=6144K,TIME=1439
//STEPLIB  DD  DSN=AD.DMG.LOADLIB,DISP=SHR
//MPAID    DD  DSN=AD.DMG.MPAID,DISP=SHR
//MPOUT    DD  SYSOUT=*
//MPIN     DD  DDNAME=SYSIN
//SYSUDUMP DD  SYSOUT=*
//SOLID    DD  DSN=AD.SOLID.INDEX,DISP=SHR
//SOLIDD   DD  DSN=AD.SOLID.DATA.ENTRIES,DISP=SHR
//SOLIDS   DD  DSN=AD.SOLID.SOURCE,DISP=SHR
//SOLIDE   DD  DSN=AD.SOLID.RECOVER,DISP=SHR
//MPRPOST  DD  DSN=&SYSUID..M204VAL,DISP=(,CATLG,DELETE),UNIT=DISK,
//             SPACE=(TRK,(5,5)),DCB=(RECFM=FB,LRECL=80,BLKSIZE=6160)
DI SOLID.
AU 'PUBLIC'.
.
POST NORMAL ONTO MPRPOST.
POST TEXT ' -------------------------------------------------------- '.
POST TEXT '          M204 FILE/RECORD/FIELD CONTENTS:                '.
POST TEXT ' -------------------------------------------------------- '.
POST WHAT CONS &DM204FIL..
.
KEEP WHAT CONS &DM204FIL..
POST TEXT ' -------------------------------------------------------- '.
POST TEXT '                OBSOLETE DEFINITIONS:                     '.
POST TEXT ' -------------------------------------------------------- '.
POST WHICH KEPT ITEMS FORM 'OBSOLETE-DEFINITION'.
POST TEXT ' -------------------------------------------------------- '.
POST TEXT '                MISSING DEFINITIONS:                      '.
POST TEXT ' -------------------------------------------------------- '.
POST LIST KEPT DUMMIES.
POST TEXT ' -------------------------------------------------------- '.
POST TEXT '                INCOMPLETE DEFINITIONS:                   '.
POST TEXT ' -------------------------------------------------------- '.
POST WHICH KEPT BELONG 'NONDED'.
//PRINT    EXEC  PGM=IEBGENER
//SYSPRINT DD  DUMMY
//SYSUT1   DD  DSN=&SYSUID..M204VAL,DISP=SHR
//SYSUT2   DD  SYSOUT=*
//SYSIN    DD  DUMMY
./ ADD NAME=M204UT1  0165-89166-96116-0628-00129-00059-00000-TRIDJK
)CM
)CM *****     JOB CARD GENERATED ON THE FLY BY JCI MACRO  *****
)CM
//*
//*   JCL FOR BUILD/SEARCH OF M204 PROCEDURE PDS
//*
)CM
)CM *****     IDCAMS RESTART STEP                         *****
)CM
//DELETE   EXEC  PGM=IDCAMS
//SYSPRINT DD  SYSOUT=*
 DELETE (&SYSUID..M204.&M204FILE)
 DELETE (&SYSUID..&M204FILE)
 DELETE (&SYSUID..PROCALL)
 DELETE (&SYSUID..PROCIX)
 DELETE (&SYSUID..PROCSEQ)
//BATCH2   EXEC  PGM=BATCH2,
//             REGION=2048K,
//             PARM='&M204CHAN'
//STEPLIB  DD  DSN=&LOAD,DISP=SHR
//         DD  DSN=&CRAM,DISP=SHR
//CCAOUT   DD  SYSOUT=*
//CCAIN    DD  *
)CM
)CM  FILE TAILORING NOTE:
)CM
)CM  THE DEFAULT CHARACTERS IN FILE TAILORING ARE  )&?!<|>
)CM  SO, YOU MUST CODE TWO IN SUCCESSION TO GET ONE OUT
)CM  IN YOUR USER LANGUAGE CODE.
)CM
LOGIN &M204LOGO
&M204PSWD

FREE DATASET OUTPROCS
ALLOCATE DATASET OUTPROCS WITH SEQUENTIAL -
  DSN=&SYSUID..M204.&M204FILE COND SHARE -
  LRECL=80 BLKSIZE=6320 RECFM=FB -
  PRIMARY=75 SECONDARY =75 TRACK UNIT=DISK

OPEN &M204FILE
&M204FPWD
)SEL &M204FPWD = &Z
)BLANK 1
)ENDSEL
RESET HDRCTL 1
RESET UDDCCC 72
USE OUTPROCS
DISPLAY PROC (LABEL) ALL
CLOSE ALL
FREE DATASET OUTPROCS
LOGOUT

)CM
)CM *****     RENAME M204 OUTPUT TO AVOID ENQ CONFLICT    *****
)CM
//RENAME   EXEC  PGM=IDCAMS
//SYSPRINT DD SYSOUT=*
 ALTER     &SYSUID..M204.&M204FILE -
   NEWNAME(&SYSUID..PROCALL)
)CM
)CM *****     BUILD IEBUPDTE INPUT FROM DISPLAY PROC ALL  *****
)CM
//USERPLIB EXEC  PGM=USERPLIB
//STEPLIB  DD  DSN=LOAD.AD,DISP=SHR
//PROCALL  DD  DSN=&SYSUID..PROCALL,DISP=SHR
//PROCIX   DD  DSN=&SYSUID..PROCIX,DISP=(,CATLG,DELETE),
//             SPACE=(TRK,(1,10),RLSE),UNIT=DISK
//PROCSEQ  DD  DSN=&SYSUID..PROCSEQ,DISP=(,CATLG,DELETE),
//             SPACE=(CYL,(10,10),RLSE),UNIT=DISK
)CM
)CM *****     CREATE USER LANGUAGE PDS                    *****
)CM
//IEBUPDTE EXEC  PGM=IEBUPDTE,PARM=NEW
//SYSPRINT DD  DUMMY
//SYSUT2   DD  DSN=&SYSUID..&M204FILE,DISP=(,CATLG,DELETE),
//             SPACE=(CYL,(10,10,200),RLSE),UNIT=DISK,
//             DCB=(RECFM=FB,LRECL=80,BLKSIZE=6320)
//SYSIN    DD  DSN=&SYSUID..PROCIX,DISP=SHR
//         DD  DSN=&SYSUID..PROCSEQ,DISP=SHR
)SEL &M204SPGM = PDS
)CM
)CM *****     PDS COMMAND TO FIND SEARCH STRINGS            *****
)CM
//FIND     EXEC  PGM=IKJEFT01,DYNAMNBR=30
//STEPLIB  DD DSN=AD.CMDLIB,DISP=SHR
//SYSTSPRT DD SYSOUT=*
//SYSTSIN  DD *
 PDS '&SYSUID..&M204FILE'
 LIST $INDEX
)SEL &M204STR1 ¬= &Z
 FIND : &M204STR1
)ENDSEL
)SEL &M204STR2 ¬= &Z
 FIND : &M204STR2
)ENDSEL
)SEL &M204STR3 ¬= &Z
 FIND : &M204STR3
)ENDSEL
)SEL &M204STR4 ¬= &Z
 FIND : &M204STR4
)ENDSEL
)SEL &M204STR5 ¬= &Z
 FIND : &M204STR5
)ENDSEL
)ENDSEL
)SEL &M204SPGM = SEARCH-FOR
)CM
)CM *****     IBM SUPERC WITH SEARCH-FOR OPTION (3.14)      *****
)CM
//SUPERC   EXEC  PGM=ISRSUPC,PARM=(SRCHCMP,ANYC)
//NEWDD    DD  DSN=&SYSUID..&M204FILE,DISP=SHR
//OUTDD    DD  SYSOUT=*
SRCHFOR 'PROCEDURE #',PREFIX
)SEL &SFS1 ¬= &Z
&SRCH1 &SFS1
)ENDSEL
)SEL &SFS2 ¬= &Z
&SRCH2 &SFS2
)ENDSEL
)SEL &SFS3 ¬= &Z
&SRCH3 &SFS3
)ENDSEL
)SEL &SFS4 ¬= &Z
&SRCH4 &SFS4
)ENDSEL
)SEL &SFS5 ¬= &Z
&SRCH5 &SFS5
)ENDSEL
)ENDSEL
