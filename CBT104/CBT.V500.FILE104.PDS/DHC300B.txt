***********************************************************************
* THIS CODE READS THE MEMBER OF JPRHEAD SPECIFIED IN THE HEADER       *
* DATA AND OUTPUTS IT TO THE PRINTER.                                 *
***********************************************************************
         SPACE 1
         TM    J1DFLAG3,J1DF3H     IS A HEADER REQUIRED?
         BNO   DHC3HDEX            NO, BYPASS THIS CODE
         MVC   P3DMEMB,J1DHMEM     YES, COPY MEMBER NAME
         SPACE 2
         LA    R15,P3DHEAD         POINT TO DCB
         OPEN  ((R15),INPUT),MF=(E,J1DOPENX) OPEN PDS
         EJECT ,
DHC300BA DS    0H                  PROCESS HEADER DATA
         LA    R2,P3DHEDSW         POINT AT MDSCB
         BAL   R4,PDSFIND          GET THE MEMBER                       D THE ME
         LTR   R1,R1               DID WE?
         BZ    DHC300BB            YES, CONTINUE
*        PLACE ANY ERROR MESSAGING HERE
         B     DHC3HDEX            BYPASS THE REST OF THIS CODE
         SPACE 1
DHC300BB DS    0H                  PROCESS HEADER DATA MEMBER
         LA    R2,P3DHEDSW         POINT AT MDSCB
         BAL   R4,PDSGET           READ DATA FROM PDS
         LTR   R1,R1               TEST FOR DATA
         BZ    DHC300BX            NONE, FINISHED
         CLI   4(R1),C'*'          IS IT A COMMENT?
         BE    DHC300BB            YES, IGNORE IT
         LH    R0,0(,R1)           GET LENGTH OF DATA
         SH    R0,=H'5'            CORRECT LENGTH FOR RDW + CC
         MVC   J1DCONCH,4(R1)      COPY CARRIAGE CONTROL
         LA    R1,5(,R1)           SKIP RDW + CC
         BAS   R14,P3WLOCS         PRINT HEADER
         B     DHC300BB            GO ROUND FOR MORE
         SPACE 2
DHC300BX DS    0H                  HEADER PROCESSING COMPLETED
         NI    P3DSW1,0            RESET FLAG                           LEM00457
         LA    R2,P3DHEDSW         POINT AT MDSCB
         USING MDCB,R2             ESTABLISH ADDRESSABILITY
         BAL   R4,CLOSE            GO OPEN THE PDS
         DROP  R2                  END DD LIST ADDRESSABILITY
DHC3HDEX DS    0H                  HEADER PROCESSING COMPLETED
         EJECT ,
