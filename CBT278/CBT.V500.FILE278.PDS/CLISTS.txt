./       ADD   NAME=SLSS
PROC 0 DEBUG
  IF &DEBUG = &STR(DEBUG) THEN DO
      CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT ASIS
  END DO
  ELSE DO
      CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST ASIS
  END DO
  ISPEXEC VPUT (DEBUG) SHARED
/*====================================================================*/
/*  AUTHOR:  Dave Weilert                                             */
/*                                                                    */
/*  ADDRESS: Wilson Foods Corporation                                 */
/*           4545 North Lincoln Boulevard                             */
/*           Oklahoma City, Oklahoma 73105                            */
/*                                                                    */
/*  PHONE:   (405) 525-4896                                           */
/*                                                                    */
/*  DOC:     SLSS MAINLINE DRIVER                                     */
/*                                                                    */
/*  NOTE:    Undocumented options on Primary Selection Panel          */
/*                                                                    */
/*           Once the Primary seleciton panel is displayed tracing    */
/*           of this applicaiton is possible by entering a question   */
/*           mark (?) and shutting off the trace with a at sign (@)'. */
/*           Tracing will occur in all CLISTs and can only be shut    */
/*           off on the Primary Selection panel.                      */
/*                                                                    */
/*====================================================================*/
/*                                                                    */
                                       /* Advertise a little          */
                                       /* This section may be com-    */
                                       /* mended out if you do not    */
                                       /* want the SLSS top panel.    */
ADVERTIS: +
  ISPEXEC DISPLAY PANEL(SLSS@TOP)
  IF &LASTCC ¬= 0 THEN EXIT
                                       /* Technical contact for error */
                                       /* messages.  Modify to have   */
                                       /* desired department or       */
                                       /* individuals name.           */
SETTECH: +
  SET &TECHCONT = &STR(Tech Services)
DEFLIBS: +
  ISPEXEC CONTROL SPLIT ENABLE
  SET &UID1    = &STR(    )
  SET &BID1    = &STR(    )
  SET &SLSS    = &STR(    )
  SET &CHGFLG  = &STR(N)
  SET &LIBEDIT = &STR(    )
  SET &SCANMOD = &STR(N)
  SET &ERRCNT  = 0
  ISPEXEC VPUT (TECHCONT) SHARED
OPENADM: +
  ISPEXEC CONTROL ERRORS RETURN
  ISPEXEC TBEND  SLSSADMN
  ISPEXEC TBOPEN SLSSADMN
  SET &RC = &LASTCC
  IF &RC ¬= 0 THEN DO
    SET &SLSSERR1 = Unable to open Administrator file SLSSADMN.
    SET &SLSSERR2 = Return Code for 'ISPEXEC TBOPEN' was &RC..
    SET &SLSSERR3 = Contact &TECHCONT for further information
    SET &SLSSERR4 = regarding this error.
    SET &SLSSERR5 =
    SET &SLSSERRX = Press ENTER to continue
    ISPEXEC DISPLAY PANEL(SLSS@ERR)
    ISPEXEC TBEND SLSSSL@$
    ISPEXEC TBEND SLSSADMN
    FREE F(ISPTABL)
    EXIT
  END DO
  ISPEXEC TBSKIP SLSSADMN NUMBER(+1)
  SET &RC = &LASTCC
  IF &RC ¬= 0 THEN DO
    SET &SLSSERR1 = Unable to obtain Administrator ID.
    SET &SLSSERR2 = Return Code for 'ISPEXEC TBSKIP' was &RC..
    SET &SLSSERR3 = Contact &TECHCONT for further information
    SET &SLSSERR4 = regarding this error.
    SET &SLSSERR5 =
    SET &SLSSERRX = Press ENTER to continue
    ISPEXEC DISPLAY PANEL(SLSS@ERR)
    ISPEXEC TBEND SLSSSL@$
    ISPEXEC TBEND SLSSADMN
    EXIT
  END DO
  SET &LIBADMIN = &SLSSADMN
  ISPEXEC TBEND SLSSADMN
  SET &RC = &LASTCC
  IF &RC ¬= 0 THEN DO
    SET &SLSSCAU1 = Unable to close Administrator file.
    SET &SLSSCAU2 = Return Code for 'ISPEXEC TBEND' was &RC..
    SET &SLSSCAU3 = Processing will continue.
    SET &SLSSCAU4 =
    SET &SLSSCAU5 =
    SET &SLSSCAUX = Press ENTER to continue
    ISPEXEC DISPLAY PANEL(SLSS@CAU)
  END DO
  ISPEXEC CONTROL ERRORS CANCEL
                                       /* DDNAME ISPTABL is freed and */
                                       /* re-allocated.  This section */
                                       /* may be changed to allocate  */
                                       /* the desired data sets along */
                                       /* with the SLSS output table. */
BEGIN: +
  FREE  F(ISPTABL)
  ALLOC F(ISPTABL) DA('&SLSSTABL') SHR
  SET &RC = &LASTCC
  IF &LASTCC ¬= 0 THEN DO
    SET &SLSSERR1 = Unable to allocate output library for DD 'ISPTABL'
    SET &SLSSERR2 = data set &TABL.
    SET &SLSSERR3 = This data set is required for SLSS.
    SET &SLSSERR4 = Return Code for 'ALLOC FILE' was &RC.
    SET &SLSSERR5 =
    SET &SLSSERRX = Press ENTER to continue
    ISPEXEC DISPLAY PANEL(SLSS@ERR)
    FREE F(ISPTABL)
    EXIT
  END DO
  ISPEXEC CONTROL ERRORS RETURN
BLDCNTL: +
  ISPEXEC TBEND    SLSSSL@$
  ISPEXEC TBCREATE SLSSSL@$ NOWRITE -
  KEYS(LIBN) -
  NAMES(LIBCDATE LIBMDATE LIBSTABL LIBSDATE LIBNUMBR LIBNAME -
        LIBSRTKY LIBOWNER LIBOWNAM LIBEXTRA LIBUPDTE LIBJDATE LIBTIME -
        LIBLOC1  LIBLOC2  LIBLOC3  LIBLOC4  LIBLOC5  -
        UID1  UID2  UID3  UID4  UID5  UID6  UID7  UID8  UID9  UID10 -
        UID11 UID12 UID13 UID14 UID15 UID16 UID17 UID18 UID19 UID20 -
        UID21 UID22 UID23 UID24 UID25 UID26 UID27 UID28 UID29 UID30 -
        UID31 UID32 UID33 UID34 UID35 UID36 UID37 UID38 UID39 UID40 -
        UID41 UID42 UID43 UID44 UID45 UID46 UID47 UID48 UID49 UID50 -
        UID51 UID52 UID53 UID54 UID55 UID56 UID57 UID58 UID59 UID60 -
        BID1  BID2  BID3  BID4  BID5  BID6  BID7  BID8  BID9  BID10 -
        BID11 BID12 BID13 BID14 BID15 BID16 BID17 BID18 BID19 BID20 -
        BID21 BID22 BID23 BID24 BID25 BID26 BID27 BID28 BID29 BID30 -
        BID31 BID32 BID33 BID34 BID35 BID36 BID37 BID38 BID39 BID40 -
        BID41 BID42 BID43 BID44 BID45 BID46 BID47 BID48 BID49 BID50 -
        BID51 BID52 BID53 BID54 BID55 BID56 BID57 BID58 BID59 BID60)
  SET &RC = &LASTCC
  IF &RC ¬= 0 THEN DO
    SET &SLSSERR1 = Unable to create temporary file SLSSSL@$.
    SET &SLSSERR2 = Return Code for 'ISPEXEC TBCREATE' was &RC.
    SET &SLSSERR3 = Contact &TECHCONT for further information
    SET &SLSSERR4 = regarding this error.
    SET &SLSSERR5 =
    SET &SLSSERRX = Press ENTER to continue.
    ISPEXEC DISPLAY PANEL(SLSS@ERR)
    ISPEXEC TBEND SLSSSL@$
    FREE  F(ISPTABL)
    EXIT
  END DO
OPENCNTL: +
  ISPEXEC TBEND  SLSSCNTL
  ISPEXEC TBOPEN SLSSCNTL
  SET &RC = &LASTCC
CHKCNTL: +
  IF &RC = 0 THEN GOTO LOOPCNTL
    GOTO LOOPCNTL
  END DO
  SET &ERRLOP = 0
  SET &ERRCNT = &ERRCNT + 1
  IF &ERRCNT = 4 THEN DO
    GOTO GOODBYE
  END DO
  DO WHILE &ERRLOP < 100
    SET &ERRLOP = &ERRLOP + 1
  END DO
  GOTO OPENCNTL
LOOPCNTL: +
  ISPEXEC TBSKIP SLSSCNTL NUMBER(+1)
  SET &RC = &LASTCC
  IF &RC = 8 THEN DO
    ISPEXEC TBEND SLSSCNTL
    GOTO DISPLAY
  END DO
  IF &RC ¬= 0 THEN DO
    SET &SLSSERR1 = Unable to load temporary file SLSSSL@$.
    SET &SLSSERR2 = Return Code for 'ISPEXEC TBSKIP' was &RC..
    SET &SLSSERR3 = Contact &TECHCONT for further information
    SET &SLSSERR4 = regarding this error.
    SET &SLSSERR5 =
    SET &SLSSERRX = Press ENTER to continue.
    ISPEXEC DISPLAY PANEL(SLSS@ERR)
    ISPEXEC TBEND SLSSSL@$
    ISPEXEC TBEND SLSSCNTL
    GOTO LEAVE
  END DO
  IF &DEBUG = DEBUG THEN WRITE &STR(LIBRARY &CIBN BEING PROCESSED)
IFOWNER: +
  IF &SYSUID = &CIBOWNER OR &SYSUID = &LIBADMIN THEN GOTO OWNER
LOOPEDIT: +
  SET &CI = &STR(&CID1)
  IF &STR(&CI) = &STR(ALLUSERS) OR &STR(&CI) = &STR(/) THEN DO
    SET &UID1 = &SYSUID
    GOTO BLDOUT
  END DO
  SET &CHKWHO = &STR(N)
  SET &CI = &STR(&SYSUID)
  SET &CNT = 1
  DO WHILE &CNT < 60
    SET &CI  = &STR(&SYSUID)
    SET &PF  = &STR(&&CID&CNT)
    IF &STR(&PF) = &STR( ) THEN GOTO LOOPBRWS
    SET &HL = &LENGTH(&STR(&PF))
    SET &UL = &LENGTH(&STR(&CI))
    SET &LC = &SUBSTR(&HL:&HL,&STR(&PF))
    IF &STR(&LC) = &STR(/) THEN DO
      SET &LS = &HL - 1
      SET &PF = &SUBSTR(1:&LS,&STR(&PF))
      SET &LS = &UL - 1
      SET &CI = &SUBSTR(1:&LS,&STR(&CI))
    END DO
    IF &CI = &PF THEN DO
      SET &UID1 = &SYSUID
      GOTO BLDOUT
    END DO
    SET &CNT = &CNT + 1
  END DO
LOOPBRWS: +
  SET &CI = &STR(&LID1)
  IF &STR(&CI) = &STR(ALLUSERS) OR &STR(&CI) = &STR(/) THEN DO
    SET &BID1 = &SYSUID
    GOTO BLDOUT
  END DO
  SET &CNT = 1
  SET &CI = &STR(&SYSUID)
  DO WHILE &CNT < 60
    SET &PF  = &STR(&&LID&CNT)
    IF &STR(&PF) = &STR( ) THEN GOTO CHKIDS
    SET &HL = &LENGTH(&STR(&PF))
    SET &UL = &LENGTH(&STR(&CI))
    SET &LC = &SUBSTR(&HL:&HL,&STR(&PF))
    IF &STR(&LC) = &STR(/) THEN DO
      SET &LS = &HL - 1
      SET &PF = &SUBSTR(1:&LS,&STR(&PF))
      SET &LS = &UL - 1
      SET &CI = &SUBSTR(1:&LS,&STR(&CI))
    END DO
    IF &CI = &PF THEN DO
      SET &BID1 = &SYSUID
      GOTO BLDOUT
    END DO
    SET &CNT = &CNT + 1
  END DO
  GOTO LOOPCNTL
CHKIDS: +
  IF &UID1 = &STR( ) THEN DO
    IF &BID1 = &STR( ) THEN GOTO LOOPCNTL
  END DO
OWNER: +
  SET &CNT = 0
  DO WHILE &CNT < 60
    SET &CNT = &CNT + 1
    SET &PF  = &STR(&&CID&CNT)
    SET &UID&CNT = &STR(&PF)
  END DO
  SET &CNT = 0
  DO WHILE &CNT < 60
    SET &CNT = &CNT + 1
    SET &PF  = &STR(&&LID&CNT)
    SET &BID&CNT = &STR(&PF)
  END DO
BLDOUT: +
  SET &LIBN     = &STR(&CIBN)
  SET &LIBCDATE = &STR(&CIBCDATE)
  SET &LIBMDATE = &STR(&CIBMDATE)
  SET &LIBSTABL = &STR(&CIBSTABL)
  SET &LIBSDATE = &STR(&CIBSDATE)
  SET &LIBNUMBR = &STR(&CIBNUMBR)
  SET &LIBNAME  = &STR(&CIBNAME)
  SET &LIBSRTKY = &STR(&CIBSRTKY)
  SET &LIBOWNER = &STR(&CIBOWNER)
  SET &LIBOWNAM = &STR(&CIBOWNAM)
  SET &LIBEXTRA = &STR(&CIBEXTRA)
  SET &LIBLOC1  = &STR(&CIBLOC1)
  SET &LIBLOC2  = &STR(&CIBLOC2)
  SET &LIBLOC3  = &STR(&CIBLOC3)
  SET &LIBLOC4  = &STR(&CIBLOC4)
  SET &LIBLOC5  = &STR(&CIBLOC5)
  SET &LIBUPDTE = &STR(N)
  SET &LIBJDATE = &STR( )
  SET &LIBTIME  = &STR( )
  ISPEXEC TBADD    SLSSSL@$
  ISPEXEC TBVCLEAR SLSSSL@$
  IF &DEBUG = &STR(DEBUG) THEN WRITE &STR(LIBRARY &CIBN LOADED)
  GOTO LOOPCNTL
GOODBYE: +
  SET &SLSSERR1 = Unable to open SLSS control file SLSSCNTL.
  SET &SLSSERR2 = Return Code for 'ISPEXEC TBOPEN' was &RC..
  SET &SLSSERR3 = Contact &TECHCONT for further information
  SET &SLSSERR4 = regarding this error.
  SET &SLSSERR5 =
  SET &SLSSERRX = Press ENTER to continue
  ISPEXEC DISPLAY PANEL(SLSS@ERR)
  ISPEXEC TBEND SLSSCNTL
  ISPEXEC TBEND SLSSSL@$
  FREE F(ISPTABL)
  EXIT
DISPLAY: +
  SET &SEL = &STR( )
/*ISPEXEC CONTROL DISPLAY REFRESH */
  ISPEXEC DISPLAY PANEL(SLSSPRIM)
  IF &LASTCC = 8 OR &SEL = &STR(X) THEN DO
    GOTO EXITCNTL
  END DO
  SET &SEL = &SYSCAPS(&SEL)
  SET &SLSS = &SYSCAPS(&SLSS)
  IF &SEL = &STR(?) OR &SEL = &STR(@) THEN GOTO OPTON
  ISPEXEC VPUT (SLSS) SHARED
  ISPEXEC TBTOP SLSSSL@$
  IF &SLSS = &STR(    ) THEN DO
    GOTO SHOWSLSS
  END DO
  ISPEXEC TBVCLEAR SLSSSL@$
  SET &LIBN = &STR(&SLSS)
  ISPEXEC TBSARG SLSSSL@$
  ISPEXEC TBSCAN SLSSSL@$
  SET &RC = &LASTCC
  IF &RC = 0 THEN GOTO PROCEED
  IF &RC = 8 THEN GOTO NEWTABL
  GOTO DISPLAY
SHOWSLSS: +
  SET &SL = &STR( )
  ISPEXEC TBDISPL SLSSSL@$ PANEL(SLSSMSTR)
  SET &RC = &LASTCC
  IF &RC = 8 THEN DO
    GOTO DISPLAY
  END DO
  IF &RC > 8 THEN DO
    SET &SLSSERR1 = You are not authorized to view any library in the
    SET &SLSSERR2 = SLSS system.  Contact the Library Administrator
    SET &SLSSERR3 = &LIBADMIN for further information regarding this
    SET &SLSSERR4 = error.
    SET &SLSSERR5 =
    SET &SLSSERRX = Press ENTER to continue
    ISPEXEC DISPLAY PANEL(SLSS@ERR)
    GOTO DISPLAY
  END DO
  SET &SLSS = &LIBN
  ISPEXEC VPUT (SLSS) SHARED
  IF &LASTCC = 0 THEN GOTO PROCEED
  GOTO DISPLAY
NEWTABL: +
  IF &SLSS = &STR(    ) THEN GOTO DISPLAY
  ISPEXEC DISPLAY PANEL(SLSSNEWT)
  IF &LASTCC = 8 THEN DO
    GOTO DISPLAY
  END DO
  ISPEXEC DISPLAY PANEL(SLSSUPDT)
  ISPEXEC DISPLAY PANEL(SLSSBRWS)
  SET &LIBN = &STR(&SLSS)
  SET &LIBCDATE = &STR(&SYSDATE)
  SET &LIBMDATE = &STR( )
  SET &LIBSTABL = &STR(N)
  SET &LIBSDATE = &STR( )
  SET &LIBEXTRA = &STR( )
  SET &LIBUPDTE = &STR(Y)
  SET &CHGFLG = &STR(Y)
  ISPEXEC VGET (ZJDATE)
  SET &LIBJDATE = &STR(&ZJDATE)
  SET &LIBTIME  = &STR(&SYSTIME)
  ISPEXEC TBADD SLSSSL@$
  SET &RC = &LASTCC
  IF &RC ¬= 0 THEN DO
    SET &SLSSERR1 = Unable to add &SLSS to control file SLSSCNTL.
    SET &SLSSERR2 = Return Code for 'ISPEXEC TBADD' was &RC.
    SET &SLSSERR3 = Contact &TECHCONT for further information
    SET &SLSSERR4 = regarding this error.
    SET &SLSSERR5 =
    SET &SLSSERRX = Press ENTER to continue
    ISPEXEC DISPLAY PANEL(SLSS@ERR)
  END DO
  IF &RC = 0 THEN DO
    ISPEXEC VPUT (LIBSRTKY) SHARED
    GOTO PROCEED
  END DO
  GOTO DISPLAY
PROCEED: +
  ISPEXEC VPUT (DEBUG SLSS LIBSRTKY LIBNUMBR LIBADMIN LIBOWNER) SHARED
OPT1: +
  IF &SEL = &STR(1) THEN DO
    IF &SYSUID = &STR(&LIBADMIN) THEN GOTO INVKOPT1
    IF &SYSUID = &STR(&LIBOWNER) THEN GOTO INVKOPT1
    IF &UID1 = &SYSUID THEN GOTO INVKOPT1
    IF &BID1 = &SYSUID THEN GOTO INVKOPT1
    SET &CNT = 0
    DO WHILE &CNT < 60
      SET &CNT = &CNT + 1
      SET &PF  = &STR(&&UID&CNT)
      IF &SYSUID = &PF THEN DO
        GOTO INVKOPT1
      END DO
    END DO
    SET &CNT = 0
    DO WHILE &CNT < 60
      SET &CNT = &CNT + 1
      SET &PF  = &STR(&&BID&CNT)
      IF &SYSUID = &PF THEN DO
        GOTO INVKOPT1
      END DO
    END DO
    SET &SLSSWRN1 = User '&SYSUID' is unauthorized to BROWSE the
    SET &SLSSWRN2 = selected library '&SLSS'.  Please contact library
    SET &SLSSWRN3 = owner '&LIBOWNER' for further assistance.
    SET &SLSSWRN4 =
    SET &SLSSWRN5 =
    SET &SLSSWRNX = Press ENTER to continue
    ISPEXEC DISPLAY PANEL(SLSS@WRN)
    GOTO DISPLAY
  END DO
  GOTO OPT2
INVKOPT1: +
  %SLSSOPT1
  GOTO DISPLAY
OPT2: +
  IF &SEL = &STR(2) THEN DO
    IF &SYSUID = &STR(&LIBADMIN) THEN GOTO INVKOPT2
    IF &SYSUID = &STR(&LIBOWNER) THEN GOTO INVKOPT2
    IF &UID1 = &SYSUID THEN GOTO INVKOPT2
    SET &CNT = 0
    DO WHILE &CNT < 60
      SET &CNT = &CNT + 1
      SET &PF  = &STR(&&UID&CNT)
      IF &SYSUID = &PF THEN DO
        GOTO INVKOPT2
      END DO
    END DO
    SET &SLSSWRN1 = User '&SYSUID' is unauthorized to maintain the
    SET &SLSSWRN2 = selected library '&SLSS'.  Please contact library
    SET &SLSSWRN3 = owner '&LIBOWNER' for further assistance.
    SET &SLSSWRN4 =
    SET &SLSSWRN5 =
    SET &SLSSWRNX = Press ENTER to continue
    ISPEXEC DISPLAY PANEL(SLSS@WRN)
    GOTO DISPLAY
  END DO
  GOTO OPT3
INVKOPT2: +
  %SLSSOPT2
  ISPEXEC VGET (MOD)  SHARED
  IF &MOD = &STR(Y) THEN DO
    SET &LIBMDATE = &STR(&SYSDATE)
    ISPEXEC VGET (ZJDATE ZTIME)
    SET &LIBJDATE = &STR(&ZJDATE)
    SET &LIBTIME  = &STR(&ZTIME)
    SET &LIBUPDTE = &STR(Y)
    SET &CHGFLG = &STR(Y)
    ISPEXEC TBMOD SLSSSL@$
  END DO
  GOTO DISPLAY
OPT3: +
  IF &SEL = &STR(3) THEN DO
    IF &SYSUID = &STR(&LIBADMIN) THEN GOTO INVKOPT3
    IF &SYSUID = &STR(&LIBOWNER) THEN GOTO INVKOPT3
    IF &UID1 = &SYSUID THEN GOTO INVKOPT3
    SET &CNT = 0
    DO WHILE &CNT < 60
      SET &CNT = &CNT + 1
      SET &PF  = &STR(&&UID&CNT)
      IF &SYSUID = &PF THEN DO
        GOTO INVKOPT3
      END DO
    END DO
    SET &SLSSWRN1 = User '&SYSUID' is unauthorized for UTILITIES option
    SET &SLSSWRN2 = for selected library '&SLSS'.  Please contact
    SET &SLSSWRN3 = library owner '&LIBOWNER' for further assistance.
    SET &SLSSWRN4 =
    SET &SLSSWRN5 =
    SET &SLSSWRNX = Press ENTER to continue
    ISPEXEC DISPLAY PANEL(SLSS@WRN)
    GOTO DISPLAY
  END DO
  GOTO OPTS
INVKOPT3: +
  ISPEXEC DISPLAY PANEL(SLSSUTIL)
  SET &RC = &LASTCC
  IF &RC = 8 THEN GOTO DISPLAY
OPT31: +
  IF &SLSSOPT = 1 THEN DO
    %SLSSDELT
    SET &LIBUPDTE = &STR(N)
    GOTO CHKUPDT
  END DO
OPT32: +
  IF &SLSSOPT = 2 THEN DO
    %SLSSKEYW
    ISPEXEC VGET (LIBSRTKY SCANMOD LIBUPDTE) SHARED
    IF &SCANMOD = &STR(Y) THEN DO
      SET &LIBSDATE = &STR(&SYSDATE)
      SET &LIBSTABL = &STR(Y)
      SET &LIBUPDTE = &STR(Y)
      SET &CHGFLG = &STR(Y)
    END DO
    ELSE DO
      SET &LIBUPDTE = &STR(N)
      GOTO CHKUPDT
    END DO
  END DO
OPT33: +
  IF &SLSSOPT = 3 THEN DO
    %SLSSPRNT
    SET &LIBUPDTE = &STR(N)
    GOTO CHKUPDT
  END DO
OPT34: +
  IF &SLSSOPT = 4 THEN DO
    SET &CHKSRTKY = &LIBSRTKY
    %SLSSSORT
    ISPEXEC VGET (LIBSRTKY)
    IF &CHKSRTKY ¬= &LIBSRTKY THEN DO
      SET &LIBUPDTE = &STR(Y)
      SET &CHGFLG = &STR(Y)
    END DO
    ELSE DO
      SET &LIBUPDTE = &STR(N)
      GOTO CHKUPDT
    END DO
  END DO
OPT35: +
  IF &SLSSOPT = 5 THEN DO
    IF &SYSUID = &LIBOWNER OR &SYSUID = &LIBADMIN THEN DO
      ISPEXEC DISPLAY PANEL(SLSSBRWS)
      IF &LASTCC = 8 THEN DO
        SET &LIBUPDTE = &STR(N)
        GOTO CHKUPDT
      END DO
      SET &LIBUPDTE = &STR(Y)
      SET &CHGFLG = &STR(Y)
      GOTO CHKUPDT
    END DO
    ELSE DO
      SET &SLSSWRN1 = User '&SYSUID' is unauthorized to select option.
      SET &SLSSWRN2 = Please contact library owner '&LIBOWNER' for
      SET &SLSSWRN3 = further assistance.
      SET &SLSSWRN4 =
      SET &SLSSWRN5 =
      SET &SLSSWRNX = Press ENTER to continue
      ISPEXEC DISPLAY PANEL(SLSS@WRN)
      GOTO INVKOPT3
    END DO
  END DO
OPT36: +
  IF &SLSSOPT = 6 THEN DO
    IF &SYSUID = &LIBOWNER OR &SYSUID = &LIBADMIN THEN DO
      ISPEXEC DISPLAY PANEL(SLSSUPDT)
      IF &LASTCC = 8 THEN DO
        SET &LIBUPDTE = &STR(N)
        GOTO CHKUPDT
      END DO
      SET &LIBUPDTE = &STR(Y)
      SET &CHGFLG = &STR(Y)
      GOTO CHKUPDT
    END DO
    ELSE DO
      SET &SLSSWRN1 = User '&SYSUID' is unauthorized to select option.
      SET &SLSSWRN2 = Please contact library owner '&LIBOWNER' for
      SET &SLSSWRN3 = further assistance.
      SET &SLSSWRN4 =
      SET &SLSSWRN5 =
      SET &SLSSWRNX = Press ENTER to continue
      ISPEXEC DISPLAY PANEL(SLSS@WRN)
      GOTO INVKOPT3
    END DO
  END DO
OPT37: +
  IF &SLSSOPT = 7 THEN DO
    IF &SYSUID = &LIBOWNER OR &SYSUID = &LIBADMIN THEN DO
      ISPEXEC DISPLAY PANEL(SLSSMODL)
      IF &LASTCC = 8 THEN DO
        SET &LIBUPDTE = &STR(N)
        GOTO CHKUPDT
      END DO
      SET &LIBUPDTE = &STR(Y)
      SET &CHGFLG = &STR(Y)
      GOTO CHKUPDT
    END DO
    ELSE DO
      SET &SLSSWRN1 = User '&SYSUID' is unauthorized to select option.
      SET &SLSSWRN2 = Please contact library owner '&LIBOWNER' for
      SET &SLSSWRN3 = further assistance.
      SET &SLSSWRN4 =
      SET &SLSSWRN5 =
      SET &SLSSWRNX = Press ENTER to continue
      ISPEXEC DISPLAY PANEL(SLSS@WRN)
      GOTO INVKOPT3
    END DO
  END DO
OPT38: +
  IF &SLSSOPT = 8 THEN DO
    SET &LIBUPDTE = &STR(N)
    %SLSSRCOV
    GOTO CHKUPDT
  END DO
CHKUPDT: +
  IF &LIBUPDTE = &STR(Y) THEN DO
    SET &CHGFLG = &STR(Y)
    ISPEXEC VGET (ZJDATE ZTIME)
    SET &LIBJDATE = &STR(&ZJDATE)
    SET &LIBTIME  = &STR(&ZTIME)
    ISPEXEC TBMOD SLSSSL@$
    SET &RC = &LASTCC
    IF &RC ¬= 0 THEN DO
      SET &SLSSERR1 = Unable to modify library control file.
      SET &SLSSERR2 = Return Code for 'ISPEXEC TBMOD' was &RC..
      SET &SLSSERR3 =
      SET &SLSSERR4 =
      SET &SLSSERR5 =
      SET &SLSSERRX = Press ENTER to continue
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
    END DO
  END DO
  GOTO INVKOPT3
OPTS: +
  IF &SEL = &STR(S) THEN DO
    IF &SYSUID = &STR(&LIBADMIN) THEN GOTO INVKOPTS
    IF &SYSUID = &STR(&LIBOWNER) THEN GOTO INVKOPTS
    IF &BID1 = &STR(ALLUSERS) THEN GOTO INVKOPTS
    IF &UID1 = &STR(ALLUSERS) THEN GOTO INVKOPTS
    SET &CNT = 0
    DO WHILE &CNT < 60
      SET &CNT = &CNT + 1
      SET &PF  = &STR(&&UID&CNT)
      IF &SYSUID = &PF THEN DO
        GOTO INVKOPTS
      END DO
    END DO
    SET &CNT = 0
    DO WHILE &CNT < 60
      SET &CNT = &CNT + 1
      SET &PF  = &STR(&&BID&CNT)
      IF &SYSUID = &PF THEN DO
        GOTO INVKOPTS
      END DO
    END DO
    SET &SLSSWRN1 = User '&SYSUID' is unauthorized to SCAN the
    SET &SLSSWRN2 = selected library '&SLSS'.  Please contact library
    SET &SLSSWRN3 = owner '&LIBOWNER' for further assistance.
    SET &SLSSWRN4 =
    SET &SLSSWRN5 =
    SET &SLSSWRNX = Press ENTER to continue
    ISPEXEC DISPLAY PANEL(SLSS@WRN)
    GOTO DISPLAY
  END DO
  GOTO OPTON
INVKOPTS: +
  %SLSSOPTS
  GOTO DISPLAY
OPTON: +
  IF &SEL = &STR(?) THEN DO
    CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT
    SET &DEBUG = &STR(DEBUG)
    ISPEXEC VPUT (DEBUG) SHARED
    GOTO DISPLAY
  END
OPTOFF: +
  IF &SEL = &STR(@) THEN DO
    CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST
    SET &DEBUG = &STR(     )
    ISPEXEC VPUT (DEBUG) SHARED
    GOTO DISPLAY
  END
EXITCNTL: +
  IF &CHGFLG = &STR(N) THEN DO
    ISPEXEC TBEND   SLSSSL@$
    EXIT
  END DO
  ISPEXEC TBOPEN SLSSCNTL
  SET &RC = &LASTCC
EXTCNTL: +
  IF &RC = 0 THEN DO
    ISPEXEC TBTOP SLSSSL@$
    GOTO LOOPEXIT
  END DO
  SET &ERRLOP = 0
  SET &ERRCNT = &ERRCNT + 1
  IF &ERRCNT = 4 THEN DO
    GOTO EXITBYE
  END DO
  DO WHILE &ERRLOP < 100
    SET &ERRLOP = &ERRLOP + 1
  END DO
  GOTO EXITCNTL
LOOPEXIT: +
  ISPEXEC TBVCLEAR SLSSSL@$
  ISPEXEC TBSKIP SLSSSL@$ NUMBER(+1)
  SET &RC = &LASTCC
  IF &RC = 8 THEN DO
    ISPEXEC TBEND   SLSSSL@$
    ISPEXEC TBCLOSE SLSSCNTL
    EXIT
  END DO
  IF &RC ¬= 0 THEN DO
    SET &SLSSWRN1 = Unable to update control file information for
    SET &SLSSWRN2 = one of the libraries updated.  Processing will
    SET &SLSSWRN3 = continue for remaining libraries.
    SET &SLSSWRN4 =
    SET &SLSSWRN5 = Return Code for 'ISPEXEC TBSKIP' was &RC.
    SET &SLSSWRNX = Press ENTER to continue.
    ISPEXEC DISPLAY PANEL(SLSS@WRN)
    GOTO LOOPEXIT
  END DO
  IF &LIBUPDTE ¬= &STR(Y) THEN GOTO LOOPEXIT
  ISPEXEC TBVCLEAR SLSSCNTL
  SET &CIBN = &STR(&LIBN)
  SET &NEWTBL = &STR(N)
  ISPEXEC TBGET SLSSCNTL
  SET &RC = &LASTCC
  IF &RC = 0 THEN GOTO SETDATA
  IF &RC = 8 THEN DO
    ISPEXEC TBBOTTOM SLSSCNTL
    ISPEXEC TBCLEAR  SLSSCNTL
    SET &NEWTBL = &STR(Y)
    SET &CIBN = &LIBN
    GOTO SWAPDATA
  END DO
ERRFND: +
  SET &SLSSWRN1 = Unable to obtain control file entry for library
  SET &SLSSWRN2 = &CIBN..
  SET &SLSSWRN3 = Processing will continue.
  SET &SLSSWRN4 =
  SET &SLSSWRN5 =
  SET &SLSSWRNX = Press ENTER to continue.
  ISPEXEC DISPLAY PANEL(SLSS@WRN)
  GOTO LOOPEXIT
SETDATA: +
  SET &LASTUPD = &STR(&CIBJDATE.&CIBTIME)
  SET &NOWUPD  = &STR(&LIBJDATE.&LIBTIME)
  IF &STR(&LASTUPD) > &STR(&NOWUPD) THEN GOTO LOOPEXIT
SWAPDATA: +
  SET &CIBCDATE = &STR(&LIBCDATE)
  SET &CIBMDATE = &STR(&LIBMDATE)
  SET &CIBSTABL = &STR(&LIBSTABL)
  SET &CIBSDATE = &STR(&LIBSDATE)
  SET &CIBNUMBR = &STR(&LIBNUMBR)
  SET &CIBNAME  = &STR(&LIBNAME)
  SET &CIBSRTKY = &STR(&LIBSRTKY)
  SET &CIBOWNER = &STR(&LIBOWNER)
  SET &CIBOWNAM = &STR(&LIBOWNAM)
  SET &CIBEXTRA = &STR(&LIBEXTRA)
  SET &CIBUPDTE = &STR(N)
  SET &CIBJDATE = &STR(&LIBJDATE)
  SET &CIBTIME  = &STR(&LIBTIME)
  SET &CIBLOC1  = &STR(&LIBLOC1)
  SET &CIBLOC2  = &STR(&LIBLOC2)
  SET &CIBLOC3  = &STR(&LIBLOC3)
  SET &CIBLOC4  = &STR(&LIBLOC4)
  SET &CIBLOC5  = &STR(&LIBLOC5)
  SET &CNT = 0
CHKIFOK: +
  IF &SYSUID = &CIBOWNER OR +
     &SYSUID = &LIBADMIN OR +
     &NEWTBL = &STR(Y)   THEN GOTO UPDTBANC
  GOTO UPDATIT
UPDTBANC: +
  DO WHILE &CNT < 60
    SET &CNT = &CNT + 1
    SET &PF  = &STR(&&UID&CNT)
    SET &CID&CNT = &STR(&PF)
  END DO
  SET &CNT = 0
  DO WHILE &CNT < 60
    SET &CNT = &CNT + 1
    SET &PF  = &STR(&&BID&CNT)
    SET &LID&CNT = &STR(&PF)
  END DO
UPDATIT: +
  ISPEXEC TBMOD    SLSSCNTL
  ISPEXEC TBVCLEAR SLSSCNTL
  GOTO LOOPEXIT
EXITBYE: +
  SET &SLSSERR1 = Unable to open SLSS control file SLSSCNTL.
  SET &SLSSERR2 = Return Code for 'ISPEXEC TBOPEN' was &RC..
  SET &SLSSERR3 = Contact &TECHCONT for further information
  SET &SLSSERR4 = regarding this error.
  SET &SLSSERR5 =
  SET &SLSSERRX = Press ENTER to continue
  ISPEXEC DISPLAY PANEL(SLSS@ERR)
  ISPEXEC TBEND SLSSCNTL
  ISPEXEC TBEND SLSSSL@$
  FREE F(ISPTABL)
  EXIT
./       ADD   NAME=SLSSCHKV
PROC 0
    ISPEXEC VGET (DEBUG SLSS LIBSRTKY TECHCONT) SHARED
    IF &DEBUG = DEBUG THEN DO
        CONTROL MSG LIST CONLIST SYMLIST ASIS
    END DO
    ELSE DO
        CONTROL NOMSG NOLIST NOCONLIST NOSYMLIST ASIS
    END DO
/*====================================================================*/
/*  AUTHOR:  Dave Weilert                                             */
/*                                                                    */
/*  ADDRESS: Wilson Foods Corporation                                 */
/*           4545 North Lincoln Boulevard                             */
/*           Oklahoma City, Oklahoma 73105                            */
/*                                                                    */
/*  PHONE:   (405) 525-4896                                           */
/*                                                                    */
/*  DOC:     SLSS conversion routine from release 3 or eariler to     */
/*           release 4.                                               */
/*                                                                    */
/*====================================================================*/
SETSORT: +
    SET &FIELDS   = &STR( )
    IF &LIBSRTKY =   THEN DO
      ISPEXEC DISPLAY PANEL(SLSSSORT)
    END DO
    IF &LIBSRTKY = T THEN SET &FIELDS = &STR(NAME1,C,A,SLSID,C,A)
    IF &LIBSRTKY = L THEN SET &FIELDS = &STR(BOOK,C,A,SLSID,C,A)
    IF &LIBSRTKY = 1 OR &LIBSRTKY = 2 OR &LIBSRTKY = 3 +
    OR &LIBSRTKY = 4 THEN SET &FIELDS = &STR(SRTKY,C,A,SLSID,C,A)
BLDSAVE: +
    ISPEXEC TBCREATE &SLSS.SAVE -
        KEYS(XLSID) -
        NAMES(XLSSVER XAME1 XAME2 XAME3 -
        XRDSTS XOOK XPD XATEORD XATERECV -
        XSTUWHO XSTUDAT XSTUTIM XRETWHO XRETDAT XRETTIM -
        XGMID1 XGMID2 XGMID3 XGMID4 XRTKY XEYWORDS -
        XPD0 XPD1 XPD2 XPD3 XPD4 XPD5 XPD6 XPD7 -
        XPD8 XPD9 XPDA XPDB XPDC XPDD XPDE XPDF)
    SET &RC = &LASTCC
    IF &RC ¬= 0 THEN DO
      SET &SLSSERR1 = Unable to create work file to convert &SLSS.
      SET &SLSSERR2 = Return Code from 'ISPEXEC TBCREATE' was &RC.
      SET &SLSSERR3 = Contact &TECHCONT for further information
      SET &SLSSERR4 = regarding this error.
      SET &SLSSERR5 =
      SET &SLSSERRX = Press ENTER to continue
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      ISPEXEC TBEND &SLSS.SAVE
      SET &VERSION = (NOGO)
      ISPEXEC VPUT (VERSION) SHARED
      EXIT
    END DO
                                       /* Set SORT                */
SETTOP: +
    ISPEXEC TBTOP &SLSS
    SET &LPFLG = Y
SETOUT: +
    DO WHILE &LPFLG = Y
      ISPEXEC TBSKIP &SLSS NUMBER(+1)
      SET &RC = &LASTCC
      IF &RC = 8 THEN DO
        SET &LPFLG = N
        GOTO SAVEWORK
      END DO
      IF &RC ¬= 0 THEN DO
        SET &SLSSERR1 = Unable to convert library to new format.
        SET &SLSSERR2 = Return Code from 'ISPEXEC TBSKIP' was &RC..
        SET &SLSSERR3 = Contact &TECHCONT for further information
        SET &SLSSERR4 = regarding this error.
        SET &SLSSERR5 =
        SET &SLSSERRX = Press ENTER to continue
        ISPEXEC DISPLAY PANEL(SLSS@ERR)
        ISPEXEC TBEND &SLSS.SAVE
        EXIT
      END DO
      IF &LPFLG = Y THEN DO
        ISPEXEC TBVCLEAR &SLSS.SAVE
        SET &XLSID    = &STR(&SLSID)
        SET &XLSSVER  = &STR(&SLSSVER)
        SET &XAME1    = &STR(&NAME1)
        SET &XAME2    = &STR(&NAME2)
        SET &XAME3    = &STR(&NAME3)
        SET &XRDSTS   = &STR(&ORDSTS)
        SET &XOOK     = &STR(&BOOK)
        SET &XPD      = &STR(&UPD)
        SET &XATEORD  = &STR(&DATEORD)
        SET &XATERECV = &STR(&DATERECV)
        SET &XRETWHO  = &STR(&CRETWHO)
        SET &XRETDAT  = &STR(&CRETDAT)
        SET &XRETTIM  = &STR(&CRETTIM)
        SET &XSTUWHO  = &STR(&LSTUWHO)
        SET &XSTUDAT  = &STR(&LSTUDAT)
        SET &XSTUTIM  = &STR(&LSTUTIM)
        SET &XPD0     = &STR(&UPD0)
        SET &XPD1     = &STR(&UPD1)
        SET &XPD2     = &STR(&UPD2)
        SET &XPD3     = &STR(&UPD3)
        SET &XPD4     = &STR(&UPD4)
        SET &XPD5     = &STR(&UPD5)
        SET &XPD6     = &STR(&UPD6)
        SET &XPD7     = &STR(&UPD7)
        SET &XPD8     = &STR(&UPD8)
        SET &XPD9     = &STR(&UPD9)
        SET &XPDA     = &STR(&UPDA)
        SET &XPDB     = &STR(&UPDB)
        SET &XPDC     = &STR(&UPDC)
        SET &XPDD     = &STR(&UPDD)
        SET &XPDE     = &STR(&UPDE)
        SET &XPDF     = &STR(&UPDF)
        ISPEXEC TBADD    &SLSS.SAVE
        SET &RC = &LASTCC
        IF &RC ¬= 0 THEN DO
          SET &SLSSERR1 = Unable to convert library to new format.
          SET &SLSSERR2 = Return Code from 'ISPEXEC TBADD' was &RC..
          SET &SLSSERR3 = Contact &TECHCONT for further information
          SET &SLSSERR4 = regarding this error.
          SET &SLSSERR5 =
          SET &SLSSERRX = Press ENTER to continue
          ISPEXEC DISPLAY PANEL(SLSS@ERR)
          ISPEXEC TBEND &SLSS.SAVE
          SET &VERSION = &STR(NOGO)
          ISPEXEC VPUT (VERSION) SHARED
          EXIT
        END DO
        ELSE DO
          ISPEXEC CONTROL DISPLAY LOCK
          SET &SLSSPRC1 = MANUAL &STR(&XLSID) unloaded from old format.
          SET &SLSSPRC2 =
          SET &SLSSPRC3 =          Converting to new format.
          SET &SLSSPRC4 =
          SET &SLSSPRC5 =
          SET &SLSSPRCX =
          ISPEXEC DISPLAY PANEL(SLSS@PRC)
        END DO
      END DO
    END DO
SAVEWORK: +
    ISPEXEC TBSAVE &SLSS.SAVE
    SET &RC = &LASTCC
    IF &RC ¬= 0 THEN DO
      SET &SLSSERR1 = Unable to save work file for conversion.
      SET &SLSSERR2 = Return Code from 'ISPEXEC TBSAVE' was &RC.
      SET &SLSSERR3 = Contact &TECHCONT for further information
      SET &SLSSERR4 = regarding this error.
      SET &SLSSERR5 =
      SET &SLSSERRX = Press ENTER to continue
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      ISPEXEC TBEND &SLSS.SAVE
      SET &VERSION = &STR(NOGO)
      ISPEXEC VPUT (VERSION) SHARED
      EXIT
    END DO
DELTSLSS: +
    ISPEXEC TBEND   &SLSS
    SET &RC = &LASTCC
    IF &RC ¬= 0 THEN DO
      SET &SLSSERR1 = Unable to close old file for deleting.
      SET &SLSSERR2 = Return Code from 'ISPEXEC TBEND' was &RC.
      SET &SLSSERR3 = Contact &TECHCONT for further information
      SET &SLSSERR4 = regarding this error.
      SET &SLSSERR5 =
      SET &SLSSERRX = Press ENTER to continue
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      ISPEXEC TBEND &SLSS.SAVE
      SET &VERSION = &STR(NOGO)
      ISPEXEC VPUT (VERSION) SHARED
      EXIT
    END DO
    ISPEXEC TBERASE &SLSS
    SET &RC = &LASTCC
    IF &RC ¬= 0 THEN DO
      SET &SLSSERR1 = Unable to delete old file for conversion.
      SET &SLSSERR2 = Return Code from 'ISPEXEC TBERASE' was &RC.
      SET &SLSSERR3 = Contact &TECHCONT for further information
      SET &SLSSERR4 = regarding this error.
      SET &SLSSERR5 =
      SET &SLSSERRX = Press ENTER to continue
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      ISPEXEC TBEND &SLSS.SAVE
      SET &VERSION = &STR(NOGO)
      ISPEXEC VPUT (VERSION) SHARED
      EXIT
    END DO
NEWSLSS: +
    ISPEXEC TBCREATE &SLSS -
        KEYS(SLSID) -
        NAMES(SLSSVER NAME1 NAME2 NAME3 -
        ORDSTS BOOK UPD DATEORD DATERECV -
        LSTUWHO LSTUDAT LSTUTIM CRETWHO CRETDAT CRETTIM -
        PGMID1 PGMID2 PGMID3 PGMID4 SRTKY KEYWORDS -
        UPD0 UPD1 UPD2 UPD3 UPD4 UPD5 UPD6 UPD7 -
        UPD8 UPD9 UPDA UPDB UPDC UPDD UPDE UPDF)
    SET &RC = &LASTCC
    IF &RC ¬= 0 THEN DO
      SET &SLSSERR1 = Unable to create the requested library &SLSS..
      SET &SLSSERR2 = Return Code from 'ISPEXEC TBCREATE' was &RC..
      SET &SLSSERR3 = Contact &TECHCONT for further information
      SET &SLSSERR4 = regarding this error.
      SET &SLSSERR5 =
      SET &SLSSERRX = Press ENTER to continue
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      ISPEXEC TBEND &SLSS
      SET &VERSION = &STR(NOGO)
      ISPEXEC VPUT (VERSION) SHARED
      EXIT
    END DO
                                       /* Set SORT                */

    ISPEXEC TBSORT &SLSS FIELDS(&FIELDS)
    SET &RC = &LASTCC
    IF &RC ¬= 0 THEN DO
      SET &SLSSWRN1 = Library cannot be kept in sequence if manuals
      SET &SLSSWRN2 = are added or deleted.  Return Code for
      SET &SLSSWRN3 = 'ISPEXEC TBSORT' was &RC.  Process will
      SET &SLSSWRN4 = continue but file will require sorting after
      SET &SLSSWRN5 = conversion is complete.
      SET &SLSSWRNX = Press ENTER to continue
      ISPEXEC DISPLAY PANEL(SLSS@WRN)
    END DO


    ISPEXEC TBCREATE &SLSS.SCAN -
    KEYS(GWORD) -
    NAMES(NUM BKIDS)
    SET &RC = &LASTCC
    IF &RC = 8 THEN GOTO UNLOAD
    IF &RC ¬= 0 THEN DO
      SET &SLSSWRN1 = Unable to create Scan file &SLSS.SCAN
      SET &SLSSWRN2 = Return Code for 'ISPEXEC TBCREATE' was &RC..
      SET &SLSSWRN3 = Processing will continue but keyword build
      SET &SLSSWRN4 = utility will need to be ran after conversion
      SET &SLSSWRN5 = is complete.
      SET &SLSSWRNX = Press ENTER to continue.
      ISPEXEC DISPLAY PANEL(SLSS@WRN)
    END DO
    ISPEXEC TBCLOSE &SLSS.SCAN
UNLOAD: +
    SET &LPFLG = Y
    SET &UNLD =  Y
    ISPEXEC TBTOP  &SLSS.SAVE
    DO WHILE &LPFLG = Y
      ISPEXEC TBSKIP &SLSS.SAVE NUMBER(+1)
      SET &RC = &LASTCC
      IF &RC = 8 THEN DO
        SET &LPFLG = N
        GOTO SAVENEW
      END DO
      IF &RC ¬= 0 THEN DO
        SET &SLSSWRN1 = Unable to complete library to new format.
        SET &SLSSWRN2 = Return Code from 'ISPEXEC TBSKIP' was &RC..
        SET &SLSSWRN3 = Manual &STR(&XLSID) was not unloaded.
        SET &SLSSWRN4 =
        SET &SLSSWRN5 =
        SET &SLSSWRNX = Press ENTER to continue
        SET &UNLD = &STR(N)
        ISPEXEC DISPLAY PANEL(SLSS@WRN)
      END DO
      IF &LPFLG = Y AND &UNLD = Y THEN DO
        ISPEXEC TBVCLEAR &SLSS
        SET &SLSID    = &STR(&XLSID)
        SET &SLSSVER  = &STR(&XLSSVER)
        SET &NAME1    = &STR(&XAME1)
        SET &NAME2    = &STR(&XAME2)
        SET &NAME3    = &STR(&XAME3)
        SET &ORDSTS   = &STR(&XRDSTS)
        SET &BOOK     = &STR(&XOOK)
        SET &UPD      = &STR(&XPD)
        SET &DATEORD  = &STR(&XATEORD)
        SET &DATERECV = &STR(&XATERECV)
        SET &CRETWHO  = &STR(&XRETWHO)
        SET &CRETDAT  = &STR(&XRETDAT)
        SET &CRETTIM  = &STR(&XRETTIM)
        SET &LSTUWHO  = &STR(&XSTUWHO)
        SET &LSTUDAT  = &STR(&XSTUDAT)
        SET &LSTUTIM  = &STR(&XSTUTIM)
        SET &UPD0     = &STR(&XPD0)
        SET &UPD1     = &STR(&XPD1)
        SET &UPD2     = &STR(&XPD2)
        SET &UPD3     = &STR(&XPD3)
        SET &UPD4     = &STR(&XPD4)
        SET &UPD5     = &STR(&XPD5)
        SET &UPD6     = &STR(&XPD6)
        SET &UPD7     = &STR(&XPD7)
        SET &UPD8     = &STR(&XPD8)
        SET &UPD9     = &STR(&XPD9)
        SET &UPDA     = &STR(&XPDA)
        SET &UPDB     = &STR(&XPDB)
        SET &UPDC     = &STR(&XPDC)
        SET &UPDD     = &STR(&XPDD)
        SET &UPDE     = &STR(&XPDE)
        SET &UPDF     = &STR(&XPDF)
        SET &PGMID1   = &STR( )
        SET &PGMID2   = &STR( )
        SET &PGMID3   = &STR( )
        SET &PGMID4   = &STR( )
        SET &KEYWORDS = &STR( )
        IF &FIELDS = &STR(SRTKY,C,A,SLSID,C,A) THEN DO
          SET &SRTKY = &SUBSTR(&LIBSRTKY:9,&STR(&XLSID         ))
        END DO
        ELSE DO
          SET &SRTKY =
        END DO
        ISPEXEC TBADD    &SLSS ORDER
        SET &RC = &LASTCC
        IF &RC ¬= 0 THEN DO
          SET &SLSSWRN1 = Unable to add &STR(&SLSID) to new library.
          SET &SLSSWRN2 = Return Code from 'ISPEXEC TBADD' was &RC..
          SET &SLSSWRN3 = Processing will continue.  Manual will need
          SET &SLSSWRN4 = to be reentered.
          SET &SLSSWRN5 =
          SET &SLSSWRNX = Press ENTER to continue
          ISPEXEC DISPLAY PANEL(SLSS@WRN)
        END DO
        ELSE DO
          ISPEXEC CONTROL DISPLAY LOCK
          SET &SLSSPRC1 =
          SET &SLSSPRC2 =
          SET &SLSSPRC3 = MANUAL &STR(&XLSID) reloaded in new format.
          SET &SLSSPRC4 =
          SET &SLSSPRC5 =
          SET &SLSSPRCX =
          ISPEXEC DISPLAY PANEL(SLSS@PRC)
        END DO
      END DO
      SET &UNLD = &STR(Y)
    END DO
SAVENEW: +
    ISPEXEC TBSAVE &SLSS
    SET &RC = &LASTCC
    IF &RC ¬= 0 THEN DO
      SET &SLSSERR1 = Unable to save new file in converted format.
      SET &SLSSERR2 = Return Code from 'ISPEXEC TBSAVE' was &RC.
      SET &SLSSERR3 = Contact &TECHCONT for further information
      SET &SLSSERR4 = regarding this error.
      SET &SLSSERR5 =
      SET &SLSSERRX = Press ENTER to continue
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      ISPEXEC TBEND &SLSS.SCAN
      SET &VERSION = &STR(NOGO)
      ISPEXEC VPUT (VERSION) SHARED
      EXIT
    END DO
    ISPEXEC TBEND   &SLSS.SAVE
    ISPEXEC TBERASE &SLSS.SAVE
    SET &RC = &LASTCC
    IF &RC ¬= 0 THEN DO
      SET &SLSSERR1 = Unable to delete old file for conversion.
      SET &SLSSERR2 = Return Code from 'ISPEXEC TBERASE' was &RC.
      SET &SLSSERR3 = Contact &TECHCONT for further information
      SET &SLSSERR4 = regarding this error.
      SET &SLSSERR5 =
      SET &SLSSERRX = Press ENTER to continue
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      ISPEXEC TBEND &SLSS.SAVE
      SET &VERSION = &STR(NOGO)
      ISPEXEC VPUT (VERSION) SHARED
      EXIT
    END DO
    SET &VERSION = &STR(CURRENT)
    ISPEXEC VPUT (VERSION) SHARED
    SET &SLSSPRC1 = Library &STR(&SLSS) converted to new format
    SET &SLSSPRC2 =
    SET &SLSSPRC3 =            Conversion completed.
    SET &SLSSPRC4 =
    SET &SLSSPRC5 =
    SET &SLSSPRCX = Press ENTER to continue.
    ISPEXEC DISPLAY PANEL(SLSS@PRC)
    EXIT
./       ADD   NAME=SLSSDELT
PROC 0
  ISPEXEC VGET (DEBUG SLSS LIBOWNER LIBADMIN) SHARED
  IF &DEBUG = &STR(DEBUG) THEN DO
      CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT ASIS
  END DO
  ELSE DO
      CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST ASIS
  END DO
/*====================================================================*/
/*  AUTHOR:  Dave Weilert                                             */
/*                                                                    */
/*  ADDRESS: Wilson Foods Corporation                                 */
/*           4545 North Lincoln Boulevard                             */
/*           Oklahoma City, Oklahoma 73105                            */
/*                                                                    */
/*  PHONE:   (405) 525-4896                                           */
/*                                                                    */
/*  DOC:     SLSS library delete routine.  Deletes all tables and     */
/*           control entries for a library.                           */
/*                                                                    */
/*====================================================================*/
CHKWHO: +
  IF &LIBOWNER = &SYSUID OR &LIBADMIN = &SYSUID THEN GOTO PROCESS
NOGO: +
  SET &SLSSWRN1 = USER '&SYSUID' UNAUTHORIZED TO DELETE LIBRARY.
  SET &SLSSWRN2 = CONTACT OWNER '&LIBOWNER' FOR FURTHER ASSISTANCE
  SET &SLSSWRN3 = WITH SELECTED OPTION.
  SET &SLSSWRN4 =
  SET &SLSSWRN5 =
  SET &SLSSWRNX = PRESS ENTER TO CONTINUE
  ISPEXEC DISPLAY PANEL(SLSS@WRN)
  EXIT
PROCESS: +
  ISPEXEC DISPLAY PANEL(SLSSDELT)
  SET &RC = &LASTCC
  IF &RC = 8 THEN EXIT
CHKCNT: +
  SET &CHKROW = 0
  ISPEXEC TBQUERY SLSSSL@$ ROWNUM(CHKROW)
  IF &CHKROW = 1 THEN DO
    SET &SLSSERR1 = ONE LIBRARY IS REQUIRED IN THE CONTROL FILE.
    SET &SLSSERR2 = IF DELETION IS OF FILE IS ALLOWED SLSS SYSTEM
    SET &SLSSERR3 = WILL NO LONGER WORK.
    SET &SLSSERR4 =
    SET &SLSSERR5 =
    SET &SLSSERRX = PRESS ENTER TO CONTINUE
    ISPEXEC DISPLAY PANEL(SLSS@ERR)
    EXIT
  END DO
  ISPEXEC CONTROL ERRORS RETURN
  ISPEXEC CONTROL DISPLAY REFRESH
OPENCNTL: +
  ISPEXEC TBEND  SLSSCNTL
  ISPEXEC TBOPEN SLSSCNTL
  SET &RC = &LASTCC
CHKCNTL: +
  IF &RC = 0 THEN GOTO DELTCNTL
  SET &ERRLOP = 0
  SET &ERRCNT = &ERRCNT + 1
  IF &ERRCNT = 4 THEN DO
    GOTO GOODBYE
  END DO
  DO WHILE &ERRLOP < 100
    SET &ERRLOP = &ERRLOP + 1
  END DO
  GOTO OPENCNTL
GOODBYE: +
  SET &SLSSERR1 = UNABLE TO OPEN SLSS CONTROL FILE SLSSCNTL.
  SET &SLSSERR2 = RETURN CODE FOR 'ISPEXEC TBOPEN' WAS &RC..
  SET &SLSSERR3 = UNABLE TO DELETE LIBRARY WITHOUT OBTAINING
  SET &SLSSERR4 = CONTROL FILE.
  SET &SLSSERR5 =
  SET &SLSSERRX = PRESS ENTER TO CONTINUE
  ISPEXEC DISPLAY PANEL(SLSS@ERR)
  EXIT
DELTCNTL: +
  SET &CIBN = &SLSS
  ISPEXEC TBGET    SLSSCNTL
  SET &RC = &LASTCC
  IF &RC ¬= 0 THEN DO
    SET &SLSSWRN1 = UNABLE TO LOCATE LIBRARY IN CONTROL FILE.
    SET &SLSSWRN2 = RETURN CODE FOR 'ISPEXEC TBGET' WAS &RC..
    SET &SLSSWRN3 = PRESS ENTER TO CONTINUE PROCESSING TO DELETE
    SET &SLSSWRN4 = OTHER FILES, ELSE PRESS END TO TERMINATE
    SET &SLSSWRN5 = PROCESS.
    SET &SLSSWRNX = PRESS ENTER TO CONTINUE, END KEY TO TERMINATE
    ISPEXEC DISPLAY PANEL(SLSS@WRN)
    SET &RC = &LASTCC
    IF &RC = 8 THEN DO
      ISPEXEC TBEND SLSSCNTL
      EXIT
    END DO
    ISPEXEC TBEND SLSSCNTL
    SET &LIB4RC = 8
    GOTO DELTTEMP
  END DO
CONTDELT: +
  ISPEXEC TBDELETE SLSSCNTL
  SET &LIB4RC = &LASTCC
  IF &LIB4RC = 0 THEN DO
    ISPEXEC TBCLOSE SLSSCNTL
    GOTO DELTTEMP
  END DO
  ISPEXEC TBEND   SLSSCNTL
  SET &SLSSERR1 = UNABLE TO DELETE LIBRARY IN CONTROL FILE.
  SET &SLSSERR2 = RETURN CODE FOR 'ISPEXEC TBDELETE' WAS &LIB4RC..
  SET &SLSSERR3 =
  SET &SLSSERR4 =
  SET &SLSSERR5 =
  SET &SLSSERRX = PRESS ENTER TO CONTINUE
  ISPEXEC CONTROL DISPLAY REFRESH
  ISPEXEC DISPLAY PANEL(SLSS@ERR)
  EXIT
DELTTEMP: +
  SET &LIBN = &SLSS
  ISPEXEC TBDELETE SLSSSL@$
  SET &LIB3RC = &LASTCC
DELTLIB: +
  ISPEXEC TBERASE &SLSS
  SET &LIB1RC = &LASTCC
DELTSCAN: +
  ISPEXEC TBERASE &SLSS.SCAN
  SET &LIB5RC = &LASTCC
DELTENQ: +
  ISPEXEC TBERASE &SLSS.ENQ
  SET &LIB2RC = &LASTCC
DELTLOCS: +
  ISPEXEC TBERASE &SLSS.LOCS
  SET &LIB6RC = &LASTCC
SHOWRCS: +
  ISPEXEC CONTROL DISPLAY REFRESH
  ISPEXEC DISPLAY PANEL(SLSSDELS)
  EXIT
./       ADD   NAME=SLSSINIT
PROC 0 DEBUG
    IF &DEBUG = &STR(DEBUG) THEN DO
        CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT ASIS
    END DO
    ELSE DO
        CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST ASIS
    END DO
/*====================================================================*/
/*  AUTHOR:  Dave Weilert                                             */
/*                                                                    */
/*  ADDRESS: Wilson Foods Corporation                                 */
/*           4545 North Lincoln Boulevard                             */
/*           Oklahoma City, Oklahoma 73105                            */
/*                                                                    */
/*  PHONE:   (405) 525-4896                                           */
/*                                                                    */
/*  DOC:     SLSS Initialization routine.  This CLIST is used once    */
/*           to create the SLSSCNTL and SLSSADMN tables and is not    */
/*           used again.                                              */
/*                                                                    */
/*====================================================================*/
SETCNTL: +
    SET &SLSSCNTL = &STR(SLSSCNTL)
    SET &SLSSADMT = &STR(SLSSADMN)
BEGIN: +
    ISPEXEC DISPLAY PANEL(SLSS1TIM)
    SET &RC = &LASTCC
    IF &RC = 8 THEN EXIT
    ISPEXEC DISPLAY PANEL(SLSS2TIM)
    SET &RC = &LASTCC
    IF &RC = 8 THEN EXIT
INIT: +
    FREE  F(ISPTABL)
    ALLOC F(ISPTABL) DA('&SLSSDSN') SHR
    SET &RC = &LASTCC
    IF &LASTCC ¬= 0 THEN DO
      SET &SLSSERR1 = UNABLE TO ALLOCATE OUTPUT LIBRARY FOR DD 'ISPTABL'
      SET &SLSSERR2 = REVIEW THE DATA SET NAME AND RETRY PROCESS.
      SET &SLSSERR3 =
      SET &SLSSERR4 =
      SET &SLSSERR5 =
      SET &SLSSERRX = PRESS 'ENTER' TO CONTINUE, 'END KEY' TO TERMINATE
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      GOTO BEGIN
    END DO
    ISPEXEC CONTROL ERRORS RETURN
BLDCNTL: +
    ISPEXEC TBCREATE &SLSSCNTL -
    KEYS(CIBN) -
    NAMES(CIBCDATE CIBMDATE CIBSTABL CIBSDATE CIBNUMBR CIBNAME  -
          CIBSRTKY CIBOWNER CIBOWNAM CIBEXTRA CIBJDATE CIBTIME  -
          CIBLOC1  CIBLOC2  CIBLOC3  CIBLOC4  CIBLOC5  -
          CID1  CID2  CID3  CID4  CID5  CID6  CID7  CID8  CID9  CID10 -
          CID11 CID12 CID13 CID14 CID15 CID16 CID17 CID18 CID19 CID20 -
          CID21 CID22 CID23 CID24 CID25 CID26 CID27 CID28 CID29 CID30 -
          CID31 CID32 CID33 CID34 CID35 CID36 CID37 CID38 CID39 CID40 -
          CID41 CID42 CID43 CID44 CID45 CID46 CID47 CID48 CID49 CID50 -
          CID51 CID52 CID53 CID54 CID55 CID56 CID57 CID58 CID59 CID60 -
          LID1  LID2  LID3  LID4  LID5  LID6  LID7  LID8  LID9  LID10 -
          LID11 LID12 LID13 LID14 LID15 LID16 LID17 LID18 LID19 LID20 -
          LID21 LID22 LID23 LID24 LID25 LID26 LID27 LID28 LID29 LID30 -
          LID31 LID32 LID33 LID34 LID35 LID36 LID37 LID38 LID39 LID40 -
          LID41 LID42 LID43 LID44 LID45 LID46 LID47 LID48 LID49 LID50 -
          LID51 LID52 LID53 LID54 LID55 LID56 LID57 LID58 LID59 LID60) -
    REPLACE
    SET &RC = &LASTCC
    IF &RC > 4 THEN DO
      SET &SLSSERR1 = UNABLE TO CREATE CONTROL FILE &SLSSCNTL.
      SET &SLSSERR2 = RETURN CODE FOR 'ISPEXEC TBCREATE' IS &RC..
      SET &SLSSERR3 = CONTACT TECHNICAL SERVICES FOR FURTHER INFORMATION
      SET &SLSSERR4 = REGARDING THIS ERROR.
      SET &SLSSERR5 =
      SET &SLSSERRX = PRESS ENTER TO CONTINUE.
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      ISPEXEC TBEND &SLSSCNTL
      FREE  F(ISPTABL)
      EXIT
    END DO
BLDADMN: +
    ISPEXEC TBCREATE &SLSSADMT -
    NAMES(SLSSADMN SLSSAWHO SLSSLOC1 SLSSLOC2 SLSSLOC3 SLSSTABL -
          AID1  AID2  AID3  AID4  AID5  AID6  AID7  AID8  AID9  AID10 -
          AID11 AID12 AID13 AID14 AID15 AID16 AID17 AID18 AID19 AID20 -
          AID21 AID22 AID23 AID24 AID25 AID26 AID27 AID28 AID29 AID30 -
          AID31 AID32 AID33 AID34 AID35 AID36 AID37 AID38 AID39 AID40 -
          AID41 AID42 AID43 AID44 AID45 AID46 AID47 AID48 AID49 AID50 -
          AID51 AID52 AID53 AID54 AID55 AID56 AID57 AID58 AID59 AID60) -
    REPLACE
    SET &RC = &LASTCC
    IF &RC > 4 THEN DO
      SET &SLSSERR1 = UNABLE TO CREATE LIBRARY ADMINISTOR FILE.
      SET &SLSSERR2 = RETURN CODE FOR 'ISPEXEC TBCREATE' IS &RC..
      SET &SLSSERR3 = CONTACT TECHNICAL SERVICES FOR FURTHER INFORMATION
      SET &SLSSERR4 = REGARDING THIS ERROR.
      SET &SLSSERR5 =
      SET &SLSSERRX = PRESS ENTER TO CONTINUE.
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      ISPEXEC TBEND &SLSSCNTL
      ISPEXEC TBEND &SLSSADMT
      FREE  F(ISPTABL)
      EXIT
    END DO
BLDSAMP: +
    SET &CIBN = &STR(SAMP)
    SET &CIBCDATE = &STR(&SYSDATE)
    SET &CIBOWNER = &SYSUID
    SET &CIBOWNAM = &STR(LIBRARY NAME GOES HERE)
    SET &CIBLOC1  = &STR( )
    SET &CIBLOC2  = &STR( )
    SET &CIBLOC3  = &STR( )
    SET &CIBLOC4  = &STR( )
    SET &CIBLOC5  = &STR( )
    SET &CIBNUMBR = &STR(00000000)
    SET &CIBNAME  = &STR(SAMPLE LIBRARY)
    SET &CIBSTABL = &STR(N)
    ISPEXEC VGET (ZJDATE ZTIME)
    SET &CIBJDATE = &STR(&ZJDATE)
    SET &CIBTIME  = &STR(&ZTIME)
    SET &CID1     = &STR(ALLUSERS)
    SET &LID1     = &STR(ALLUSERS)
    ISPEXEC TBADD  &SLSSCNTL
    SET &RC = &LASTCC
    IF &RC ¬= 0 THEN DO
      SET &SLSSERR1 = UNABLE TO CREATE SAMPLE ENTRY IN CONTROL FILE.
      SET &SLSSERR2 = RETURN CODE FOR 'ISPEXEC TBADD' IS &RC..
      SET &SLSSERR3 = PLEASE REVIEW THE ABOVE RETURN CODE AND RETRY
      SET &SLSSERR4 = PROCESS.
      SET &SLSSERR5 =
      SET &SLSSERRX = PRESS ENTER TO CONTINUE.
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      ISPEXEC TBEND &SLSSCNTL
      FREE  F(ISPTABL)
      EXIT
    END DO
BLDADM: +
    SET &SLSSTABL = &STR(&SLSSDSN)
    ISPEXEC TBADD  &SLSSADMT
    SET &RC = &LASTCC
    IF &RC ¬= 0 THEN DO
      SET &SLSSERR1 = UNABLE TO ENTRY IN ADMINISTRATOR CONTROL FILE.
      SET &SLSSERR2 = RETURN CODE FOR 'ISPEXEC TBADD' IS &RC..
      SET &SLSSERR3 = PLEASE REVIEW THE ABOVE RETURN CODE AND RETRY
      SET &SLSSERR4 = PROCESS.
      SET &SLSSERR5 =
      SET &SLSSERRX = PRESS ENTER TO CONTINUE.
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      ISPEXEC TBEND &SLSSCNTL
      ISPEXEC TBEND &SLSSADMT
      FREE  F(ISPTABL)
      EXIT
    END DO
CLSCNTL: +
    ISPEXEC TBCLOSE &SLSSCNTL
    SET &RC = &LASTCC
    IF &RC ¬= 0 THEN DO
      SET &SLSSERR1 = UNABLE TO CLOSE CONTROL FILE TO OUTPUT DATA SET.
      SET &SLSSERR2 = RETURN CODE FOR 'ISPEXEC TBCLOSE' IS &RC..
      SET &SLSSERR3 = CONTACT TECHNICAL SERVICES FOR FURTHER INFORMATION
      SET &SLSSERR4 = REGARDING THIS ERROR.
      SET &SLSSERR5 =
      SET &SLSSERRX = PRESS ENTER TO CONTINUE.
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      ISPEXEC TBEND &SLSSCNTL
      FREE  F(ISPTABL)
      EXIT
    END DO
CLSADMN: +
    ISPEXEC TBCLOSE &SLSSADMT
    SET &RC = &LASTCC
    IF &RC ¬= 0 THEN DO
      SET &SLSSERR1 = UNABLE TO CLOSE ADMINISTRATOR FILE.
      SET &SLSSERR2 = RETURN CODE FOR 'ISPEXEC TBCLOSE' IS &RC..
      SET &SLSSERR3 = CONTACT TECHNICAL SERVICES FOR FURTHER INFORMATION
      SET &SLSSERR4 = REGARDING THIS ERROR.
      SET &SLSSERR5 =
      SET &SLSSERRX = PRESS ENTER TO CONTINUE.
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      ISPEXEC TBEND &SLSSADMT
      ISPEXEC TBERASE &SLSSCNTL
      FREE  F(ISPTABL)
      EXIT
    END DO
DISPLAY: +
    FREE  F(ISPTABL)
    SET &SLSSPRC1 = Control file and administrator file sucessfully
    SET &SLSSPRC2 = created.  A sample library entry has been placed
    SET &SLSSPRC3 = in the control file.  You must copy the sample
    SET &SLSSPRC4 = library from the install tape to the output library
    SET &SLSSPRC5 = before you can select the library.
    SET &SLSSPRCX = Press ENTER to continue.
    ISPEXEC DISPLAY PANEL(SLSS@PRC)
    EXIT
./       ADD   NAME=SLSSKEYU
PROC 0
    ISPEXEC VGET (DEBUG SLSS SLSID NAME1 NAME2 NAME3 KEYWORDS KEYOPT)
    IF &DEBUG = &STR(DEBUG) THEN DO
        CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT ASIS
    END DO
    ELSE DO
        CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST ASIS
    END DO
/*====================================================================*/
/*  AUTHOR:  Dave Weilert                                             */
/*                                                                    */
/*  ADDRESS: Wilson Foods Corporation                                 */
/*           4545 North Lincoln Boulevard                             */
/*           Oklahoma City, Oklahoma 73105                            */
/*                                                                    */
/*  PHONE:   (405) 525-4896                                           */
/*                                                                    */
/*  DOC:     SLSS Keyword Scan file maintenance routine.  Will add    */
/*           or delete keywords from scan file.                       */
/*                                                                    */
/*====================================================================*/
CHKIN: +
    IF &KEYOPT = U OR &KEYOPT = D THEN DO
      ISPEXEC VGET (OLDNAME1 OLDNAME2 OLDNAME3 OLDKWRDS)
    END DO
BEGIN: +
    SET &SCANTABL = &STR(&SLSS.SCAN)
    ISPEXEC CONTROL ERRORS RETURN
    ISPEXEC TBOPEN  &SCANTABL
    SET &RC = &LASTCC
    IF &RC = 0 THEN GOTO SORTIT
    SET &SLSSERR1 = Unable to open scan table to update the keyword
    SET &SLSSERR2 = scan file.  Processing will continue.  Notify the
    SET &SLSSERR3 = Library Owner or Administrator or ERROR.  UTILTIY
    SET &SLSSERR4 = Keyword Build may have to be run on library to
    SET &SLSSERR5 = correct problem.
    SET &SLSSERRX = Press ENTER
    ISPEXEC DISPLAY PANEL(SLSS@ERR)
    ISPEXEC TBCLOSE &SCANTABL
    EXIT
SORTIT: +
    ISPEXEC TBSORT &SCANTABL FIELDS(GWORD)
    SET &RC = &LASTCC
    IF &RC = 0 THEN GOTO CHECKOPT
    SET &SLSSERR1 = Unable to sort scan table for updating.  Processing
    SET &SLSSERR2 = canceled.
    SET &SLSSERR3 =
    SET &SLSSERR4 =
    SET &SLSSERR5 =
    SET &SLSSERRX = Press ENTER
    ISPEXEC DISPLAY PANEL(SLSS@ERR)
    ISPEXEC TBCLOSE &SCANTABL
    EXIT
CHECKOPT: +
    IF &KEYOPT = &STR(A) THEN GOTO PARSENEW
    IF &KEYOPT = &STR(D) THEN GOTO PARSEOLD
    IF &STR(&KEYWORDS) = &STR(&OLDKWRDS) THEN DO
      SET &OLDKWRDS =
    END DO
    IF &STR(&NAME3) = &STR(&OLDNAME3) THEN DO
      SET &OLDNAME3 =
    END DO
    IF &STR(&NAME2) = &STR(&OLDNAME2) THEN DO
      SET &OLDNAME2 =
    END DO
    IF &STR(&NAME1) = &STR(&OLDNAME1) THEN DO
      SET &OLDNAME1 =
    END DO
/*                                               PARSE OLDNAME FLDS */
PARSEOLD: +
    SET &OTEXT = &STR(&OLDNAME1 &OLDNAME2 &OLDNAME3 &OLDKWRDS)
    SET &OI = 1
    SET &OWRD  = &STR(&&OWRD)
    SET &OWRD&OI =
    SET &LTEXT = &LENGTH(&STR(&OTEXT))
    SET &CHARPOS = 1
BLDOWRD: +
    IF &CHARPOS GT &LTEXT THEN GOTO PARSENEW
    SET &CHAR = &STR(&SUBSTR(&CHARPOS,&OTEXT))
    IF &STR(&CHAR) =  &STR( ) THEN GOTO NEXTOWRD
    SET &OWRD&OI = &STR(&OWRD&OI.&STR(&CHAR))
    GOTO NEXTOCHR
NEXTOWRD: +
    IF &STR(&OWRD&OI) = &STR( ) THEN GOTO NEXTOCHR
    SET &OI = &OI + 1
    SET &OWRD&OI =
NEXTOCHR: +
    SET &CHARPOS = &CHARPOS + 1
    GOTO BLDOWRD
/*                                               PARSE NAME FLDS    */
PARSENEW: +
    IF &KEYOPT = D THEN GOTO DONE
    SET &TEXT = &STR(&NAME1 &NAME2 &NAME3 &KEYWORDS)
    SET &INDEX = 1
    SET &WORD  = &STR(&&WORD)
    SET &WORD&INDEX =
    SET &LTEXT = &LENGTH(&STR(&TEXT))
    SET &CHARPOS = 1
BLDWORD: +
    IF &CHARPOS GT &LTEXT THEN GOTO DONE
    SET &CHAR = &STR(&SUBSTR(&CHARPOS,&TEXT))
    IF &STR(&CHAR) =  &STR( ) THEN GOTO NEXTWORD
    SET &WORD&INDEX = &STR(&WORD&INDEX.&STR(&CHAR))
    GOTO NEXTCHAR
NEXTWORD: +
    IF &STR(&WORD&INDEX) = &STR( ) THEN GOTO NEXTCHAR
    SET &INDEX = &INDEX + 1
    SET &WORD&INDEX =
NEXTCHAR: +
    SET &CHARPOS = &CHARPOS + 1
    GOTO BLDWORD
DONE: +
    IF &KEYOPT = A THEN GOTO ADD
    IF &KEYOPT = U THEN GOTO UPDATE
    IF &KEYOPT = D THEN GOTO DELETE

                                 /* The following process removes   */
                                 /* duplicate keys befor processing */

UPDATE: +
    SET &OMAX = &OI
    SET &NMAX = &INDEX
    SET &OX = 1
    SET &IX = 1
    SET &SW = Y

    DO WHILE &SW = Y
      SET &OXWRD = &STR(&OWRD&OX)
      SET &NXWRD = &STR(&WORD&IX)
      IF &STR(&OXWRD) = &STR(&NXWRD) THEN DO
        SET &OWRD&OX =
        SET &WORD&IX =
      END DO
      ELSE DO
        SET &IX = &IX + 1
        DO WHILE &IX <= &NMAX
          SET &NXWRD = &STR(&WORD&IX)
          IF &STR(&OXWRD) = &STR(&NXWRD) THEN DO
            SET &OWRD&OX =
            SET &WORD&IX =
          END DO
          SET &IX = &IX + 1
        END DO
      END DO
      SET &OX = &OX + 1
      SET &IX = 1
      IF &OX > &OMAX THEN DO
        SET &SW = N
      END DO
    END DO
    GOTO ADD

                                 /* The following adds new manuals  */
                                 /* and the keywords for them.      */

ADD: +
    SET &N = 1
    DO WHILE &N LT &INDEX + 1
      ISPEXEC TBTOP &SCANTABL
      ISPEXEC TBVCLEAR &SCANTABL
      SET &WWORD = &STR(&WORD&N)
      SET &GWORD = &STR(&SYSCAPS(&WWORD))
      DO WHILE &STR(&GWORD) ¬=
        ISPEXEC TBSARG &SCANTABL
        ISPEXEC TBSCAN &SCANTABL
        IF &LASTCC = 0 THEN DO
          SET &BNT = 1
          DO WHILE &NUM >= &BNT
            SET &LP = &BNT * 9
            SET &FP = &LP - 8
            SET &CKF = &SUBSTR(&FP:&LP,&STR(&BKIDS))
            IF &STR(&CKF) = &STR(&SLSID) THEN DO
              SET &CKF = &STR( )
              SET &BNT = &NUM
            END DO
            SET &BNT = &BNT + 1
          END DO
          IF &STR(&CKF) ¬= &STR( ) THEN DO
            SET &NUM = &NUM + 1
            SET &SLSID = &SUBSTR(1:9,&STR(&SLSID         ))
            SET &BKIDS = &STR(&BKIDS&SLSID)
            ISPEXEC TBMOD &SCANTABL ORDER
            SET &GWORD =
          END DO
        END DO
        ELSE DO
          ISPEXEC TBVCLEAR &SCANTABL
          SET &SLSID = &SUBSTR(1:9,&STR(&SLSID         ))
          SET &BKIDS = &STR(&SLSID)
          SET &NUM = 1
          SET &WWORD = &STR(&WORD&N)
          SET &GWORD = &STR(&SYSCAPS(&WWORD))
          ISPEXEC TBADD &SCANTABL ORDER
          SET &GWORD =
        END DO
      END DO
      SET &N = &N + 1
    END DO
    IF &KEYOPT = U THEN GOTO DELETE
    GOTO CLOSTABL

                                 /* The following deletes keywords  */
                                 /* for manuals being deleted.      */

DELETE: +
    SET &N = 1
    DO WHILE &N <= &OI
      ISPEXEC TBTOP &SCANTABL
      ISPEXEC TBVCLEAR &SCANTABL
      SET &WWORD = &STR(&OWRD&N)
      SET &GWORD = &STR(&SYSCAPS(&WWORD))
      IF &STR(&GWORD) =    THEN SET &GWORD = &SYSTIME
      ISPEXEC TBSARG &SCANTABL
      ISPEXEC TBSCAN &SCANTABL
      IF &LASTCC = 0 THEN DO
        SET &BNT = 1
        DO WHILE &NUM >= &BNT
          SET &LP = &BNT * 9
          SET &FP = &LP - 8
          SET &CKF = &SUBSTR(&FP:&LP,&STR(&BKIDS))
          IF &STR(&CKF) = &STR(&SLSID) THEN DO
            IF &BNT = 1 THEN DO
              SET &HWL = &LENGTH(&STR(&BKIDS))
              IF &NUM > 1 THEN DO
                SET &BKIDS = &SUBSTR(10:&HWL,&STR(&BKIDS))
              END DO
              ELSE DO
                SET &BKIDS =
              END DO
            END DO
            ELSE DO
              SET &WLP = &FP - 1
              SET &PART1 = &SUBSTR(1:&WLP,&STR(&BKIDS))
              SET &HWL = &LENGTH(&STR(&BKIDS))
              SET &WFP = &LP + 1
              IF &WFP > &HWL THEN DO
                SET &PART2 =
              END DO
              ELSE DO
                SET &PART2 = &SUBSTR(&WFP:&HWL,&STR(&BKIDS))
              END DO
              SET &BKIDS = &STR(&PART1&PART2)
            END DO
            SET &NUM = &NUM - 1
            IF &NUM > 0 THEN DO
              ISPEXEC TBMOD &SCANTABL ORDER
              IF &LASTCC ¬= 0 THEN DO
                WRITE &STR(&GWORD not UPDATED with manual ID &SLSID)
              END DO
            END DO
            ELSE DO
              ISPEXEC TBDELETE &SCANTABL
              IF &LASTCC ¬= 0 THEN DO
                WRITE &STR(&GWORD not DELETED from &SCANTABL)
              END DO
            END DO
            SET &BNT = &NUM + 1
          END DO
          SET &BNT = &BNT + 1
        END DO
      END DO
      SET &N = &N + 1
    END DO
    GOTO CLOSTABL
CLOSTABL: +
    ISPEXEC TBCLOSE &SCANTABL
    EXIT
./       ADD   NAME=SLSSKEYW
PROC 0
    ISPEXEC VGET (DEBUG SLSS) SHARED
    IF &DEBUG = &STR(DEBUG) THEN DO
        CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT ASIS
    END DO
    ELSE DO
        CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST ASIS
    END DO
/*====================================================================*/
/*  AUTHOR:  Dave Weilert                                             */
/*                                                                    */
/*  ADDRESS: Wilson Foods Corporation                                 */
/*           4545 North Lincoln Boulevard                             */
/*           Oklahoma City, Oklahoma 73105                            */
/*                                                                    */
/*  PHONE:   (405) 525-4896                                           */
/*                                                                    */
/*  DOC:     SLSS Keyword build utility.                              */
/*                                                                    */
/*  NOTE:    Compress library before this is invoked.  Variable       */
/*           SAVCNT is used to control saving of the new keyword      */
/*           table as it is being created.  The small the number      */
/*           the more space it takes.                                 */
/*                                                                    */
/*====================================================================*/
SETDATA: +
    SET &SCANMOD = &STR(N)
    SET &ANDSAV =
    SET &SAVCNT = 25
ASK: +
    SET &SLSSWRN1 = Selected option will require several minutes
    SET &SLSSWRN2 = to complete.  Please insure that you want to
    SET &SLSSWRN3 = perform selected option.
    SET &SLSSWRN4 =
    SET &SLSSWRN5 =
    SET &SLSSWRNX = Press ENTER to CONTINUE, END key to TERMINATE
    ISPEXEC DISPLAY PANEL(SLSS@KEY)
    IF &LASTCC = 8 THEN DO
      EXIT
    END DO
BEGIN: +
    SET &SLSSMSG = &STR(  )
    SET &CNT = 0
    SET &CHKCNT = 10
    SET &FIRST = &STR(Y)
    SET &SCANTABL = &STR(&SLSS.SCAN)
    ISPEXEC CONTROL ERRORS RETURN
    ISPEXEC TBEND   &SCANTABL
    ISPEXEC TBERASE &SCANTABL
    SET &RC = &LASTCC
    IF &RC = 0 OR &RC = 8 THEN GOTO BLDTBL
    SET &SLSSERR1 = UNABLE TO DELETE OLD SCAN TABLE.
    SET &SLSSERR2 = RETURN CODE FOR 'ISPEXEC TBERASE' IS &RC..
    SET &SLSSERR3 = BEFORE CONTINUING PROCESS EXIT SLSS AND DELETE
    SET &SLSSERR4 = MEMBER &SCANTABL FROM YOUR ISPTLIB DATA SET, ONCE
    SET &SLSSERR5 = COMPLETED RETRY PROCESS.
    SET &SLSSERRX = PRESS ENTER TO CONTINUE.
    ISPEXEC DISPLAY PANEL(SLSS@ERR)
    EXIT
BLDTBL: +
    ISPEXEC TBCREATE &SCANTABL -
    KEYS(GWORD) -
    NAMES(NUM BKIDS)
    SET &RC = &LASTCC
    IF &RC ¬= 0 THEN DO
      SET &SLSSERR1 = UNABLE TO CREATE NEW SCAN TABLE &SCANTABL.
      SET &SLSSERR2 = RETURN CODE FOR 'ISPEXEC TBCREATE' IS &RC..
      SET &SLSSERR3 = CONTACT TECHNICAL SERVICES FOR FURTHER INFORMATION
      SET &SLSSERR4 = REGARDING THIS ERROR.
      SET &SLSSERR5 =
      SET &SLSSERRX = PRESS ENTER TO CONTINUE.
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      ISPEXEC TBEND &SCANTABL
      EXIT
    END DO
    ISPEXEC TBSORT &SCANTABL FIELDS(GWORD)
    SET &RC = &LASTCC
    IF &RC ¬= 0 THEN DO
      SET &SLSSERR1 = Unable to build table in sorted sequence.
      SET &SLSSERR2 =
      SET &SLSSERR3 = Cancelling process.
      SET &SLSSERR4 =
      SET &SLSSERR5 =
      SET &SLSSERRX = Press ENTER
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      ISPEXEC TBEND &SCANTABL
      EXIT
    END DO
PROCESS: +
    ISPEXEC TBOPEN &SLSS NOWRITE
    SET &RC = &LASTCC
    IF &RC ¬= 0 THEN DO
      SET &SLSSERR1 = UNABLE TO OPEN LIBRARY &SLSS..
      SET &SLSSERR2 = RETURN CODE FOR 'ISPEXEC TBOPEN' IS &RC..
      SET &SLSSERR3 = CONTACT TECHNICAL SERVICES FOR FURTHER INFORMATION
      SET &SLSSERR4 = REGARDING THIS ERROR.
      SET &SLSSERR5 =
      SET &SLSSERRX = PRESS ENTER TO CONTINUE.
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      ISPEXEC TBEND &SLSS
      ISPEXEC TBEND &SLSS.SCAN
      EXIT
    END DO
CHKRESRT: +
    IF &STR(&RESLSID) = &STR( ) THEN GOTO LOOPTABL
    SET &SLSID = &STR(&RESLSID)
    ISPEXEC TBGET &SLSS
    SET &RC = &LASTCC
    IF &RC ¬= 0 THEN DO
      SET &SLSSERR1 = UNABLE TO RESTART KEYWORD BUILD AT SELECTED ID.
      SET &SLSSERR2 =
      SET &SLSSERR3 = PRESS ENTER TO CONTINUE OR END TO TERMINATE.
      SET &SLSSERR4 =
      SET &SLSSERR5 =
      SET &SLSSERRX =
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      IF &LASTCC = 8 THEN DO
        ISPEXEC TBEND &SLSS
        ISPEXEC TBEND &SLSS.SCAN
        EXIT
      END DO
    END DO
LOOPTABL: +
    ISPEXEC TBSKIP &SLSS NUMBER(1)
    IF &LASTCC = 8 THEN DO
      SET &SLSSMSG = &STR(  )
      ISPEXEC TBEND &SLSS
      ISPEXEC TBCLOSE &SCANTABL
      SET &SLSSPRC1 = KEYWORD BUILD PROCESSING COMPLETED
      SET &SLSSPRC2 =
      SET &SLSSPRC3 = &CNT MANUALS PROCESSED.
      SET &SLSSPRC4 =
      SET &SLSSPRC5 =
      SET &SLSSPRCX = PRESS ENTER TO CONTINUE.
      ISPEXEC DISPLAY PANEL(SLSS@PRC)
      SET &SCANMOD = &STR(Y)
      ISPEXEC VPUT (SCANMOD)
      EXIT
    END DO
    ISPEXEC TBGET &SLSS
    IF &LASTCC ¬= 0 THEN GOTO LOOPTABL
    SET &CNT = &CNT + 1
    IF &CNT = &SAVCNT THEN DO
      SET &SAVCNT = &SAVCNT + 25
      ISPEXEC TBSAVE &SCANTABL
      SET &ANDSAV = &STR( AND SAVED)
    END DO
    IF &CNT = &CHKCNT THEN DO
      SET &CHKCNT = &CNT + 10
      ISPEXEC CONTROL DISPLAY LOCK
      SET &SLSSPRC1 = KEYWORD BUILD PROCESSING.
      SET &SLSSPRC2 =
      SET &SLSSPRC3 = &CNT MANUALS PROCESSED &ANDSAV
      SET &SLSSPRC4 =
      SET &SLSSPRC5 = &STR(&SLSID CURRENT MANUAL)
      SET &SLSSPRCX =
      ISPEXEC DISPLAY PANEL(SLSS@PRC)
      SET &ANDSAV =
    END DO
PARSENAM: +
    SET &TEXT = &STR(&NAME1 &NAME2 &NAME3 &KEYWORDS)
    SET &INDEX = 1
    SET &WORD  = &STR(&&WORD)
    SET &WORD&INDEX =
    SET &LTEXT = &LENGTH(&STR(&TEXT))
    SET &CHARPOS = 1
BLDWORD: +
    IF &CHARPOS GT &LTEXT THEN GOTO DONE
    SET &CHAR = &STR(&SUBSTR(&CHARPOS,&TEXT))
    IF &STR(&CHAR) =  &STR( ) THEN GOTO NEXTWORD
    SET &WORD&INDEX = &STR(&WORD&INDEX.&STR(&CHAR))
    GOTO NEXTCHAR
NEXTWORD: +
    IF &STR(&WORD&INDEX) = &STR( ) THEN GOTO NEXTCHAR
    SET &INDEX = &INDEX + 1
    SET &WORD&INDEX =
NEXTCHAR: +
    SET &CHARPOS = &CHARPOS + 1
    GOTO BLDWORD
DONE: +
    SET &N = 1
    IF &LENGTH(&STR(&WORD&N)) = 0 THEN GOTO LOOPTABL
    DO WHILE &N LT &INDEX + 1
      ISPEXEC TBTOP &SCANTABL
      ISPEXEC TBVCLEAR &SCANTABL
      SET &WWORD = &STR(&WORD&N)
      SET &GWORD = &STR(&SYSCAPS(&WWORD))
      ISPEXEC TBSARG &SCANTABL
      ISPEXEC TBSCAN &SCANTABL
      IF &LASTCC = 0 THEN DO
        SET &BNT = 1
        DO WHILE &NUM >= &BNT
          SET &LP = &NUM * 9
          SET &FP = &LP - 8
          SET &CKF = &SUBSTR(&FP:&LP,&STR(&BKIDS))
          IF &STR(&CKF) = &STR(&SLSID) THEN DO
            SET &CKF = &STR( )
            SET &BNT = &NUM
          END DO
          SET &BNT = &BNT + 1
        END DO
        IF &STR(&CKF) ¬= &STR( ) THEN DO
          SET &NUM = &NUM + 1
          SET &SLSID = &SUBSTR(1:9,&STR(&SLSID         ))
          SET &BKIDS = &STR(&BKIDS&SLSID)
          ISPEXEC TBMOD &SCANTABL ORDER
          IF &LASTCC ¬= 0 THEN DO
             WRITE &STR(&GWORD NOT UPDATED WITH MANUAL ID &SLSID)
          END DO
        END DO
      END DO
      ELSE DO
        ISPEXEC TBVCLEAR &SCANTABL
        SET &SLSID = &SUBSTR(1:9,&STR(&SLSID         ))
        SET &BKIDS = &STR(&SLSID)
        SET &NUM = 1
        SET &WWORD = &STR(&WORD&N)
        SET &GWORD = &STR(&SYSCAPS(&WWORD))
        ISPEXEC TBADD &SCANTABL ORDER
        IF &LASTCC ¬= 0 THEN DO
             WRITE &STR(&GWORD NOT CREATED WITH MANUAL ID &SLSID)
        END DO
      END DO
      SET &N = &N + 1
    END
    GOTO LOOPTABL
./       ADD   NAME=SLSSONOR
PROC 0
    ISPEXEC VGET (DEBUG SLSS LIBSRTKY LIBNUMBR)
    IF &DEBUG = DEBUG THEN DO
        CONTROL MSG LIST CONLIST SYMLIST
    END DO
    ELSE DO
        CONTROL NOMSG
    END DO
/*====================================================================*/
/*  AUTHOR:  Dave Weilert                                             */
/*                                                                    */
/*  ADDRESS: Wilson Foods Corporation                                 */
/*           4545 North Lincoln Boulevard                             */
/*           Oklahoma City, Oklahoma 73105                            */
/*                                                                    */
/*  PHONE:   (405) 525-4896                                           */
/*                                                                    */
/*  DOC:     SLSS Onorder report generator.                           */
/*                                                                    */
/*  NOTE:    This CLIST allocates a file that is used as input to     */
/*           an IEBGENER.  If you wish to save the output file        */
/*           modify the skelton JCL in the ISPSLIB.                   */
/*                                                                    */
/*====================================================================*/
SETDATA: +
    SET &FS = &STR(                                                  )
    SET &PRTRTN = &STR(N)
    SET &SAVELINE = &STR( )
INIT: +
    ISPEXEC VGET (SLSSJCL1 SLSSJCL2 SLSSJCL3 SLSSJCL4 SLSSOCLS +
                  SLSSFCB  SLSSCOPY SLSSCHAR SLSSBRST SLSSLPI  +
                  SLSSPAG  SLSSMARG SLSSLINE) PROFILE
    ISPEXEC TBOPEN &SLSS
OUTFILE: +
    SET &WRKTIME = &SYSTIME
    SET &W1 = &SUBSTR(1:2,&STR(&WRKTIME))
    SET &W2 = &SUBSTR(4:5,&STR(&WRKTIME))
    SET &W3 = &SUBSTR(7:8,&STR(&WRKTIME))
    SET &OUTLIST = &STR(&SYSUID..T&W1&W2&W3..OUTLIST)
    FREE F(SLSSOUT)
    FREE ATTR(ATTR2)
    ATTR ATTR2 LRECL(133) BLKSIZE(6118) RECFM(F B A)
    ALLOC F(SLSSOUT)  DA('&OUTLIST')   NEW SP(5 2) TR -
        RELEASE  USING(ATTR2)
    OPENFILE SLSSOUT OUTPUT
SETPAGE: +
    SET &PAGNUM = 0
    IF &SLSSLPI = 6 THEN DO
      IF &SLSSPAG = &STR(8.5) THEN DO
        SET &MAXLN = 44
        SET &CHKMXS = 41
        SET &CHKMXD = 38
      END DO
      ELSE DO
        SET &MAXLN = 59
        SET &CHKMXS = 56
        SET &CHKMXD = 53
      END DO
    END DO
    ELSE DO
      IF &SLSSPAG = &STR(8.5) THEN DO
        SET &MAXLN = 59
        SET &CHKMXS = 56
        SET &CHKMXD = 53
      END DO
      ELSE DO
        SET &MAXLN = 79
        SET &CHKMXS = 76
        SET &CHKMXD = 73
      END DO
    END DO
SETHEAD: +
    IF &SLSSLINE = &STR(S) THEN DO
      SET &CR = &STR( )
    END DO
    ELSE DO
      SET &CR = &STR(0)
    END DO
    IF &LIBSRTKY = &STR( ) THEN DO
      SET &LIBSRTKY = &STR(1)
    END DO
    SET &HDR0A = &STR(&SYSDATE                     SLSS LIBRA)
    SET &HDR0B = &STR(RY REPORT                     PAGE )
    SET &HDR1A = &STR(                    LIBRARY NAME: &SLSS)
    SET &HDR1B = &STR(    NUMBER: &LIBNUMBR)
    SET &HDR4A = &STR(---------------------------------------)
    SET &HDR4B = &STR(---------------------------------------)
    IF &SLSSRPT = 1 THEN DO
      IF &LIBSRTKY = L THEN DO
        SET &HDR2A = &STR(                          LOCATION SEQU)
        SET &HDR2B = &STR(ENCE LISTING)
        SET &HDR3A = &STR(LOCATION   MANUAL ID    U  S           )
        SET &HDR3B = &STR(            TITLE )
        GOTO PRTHDRS
      END DO
    END DO
    IF &SLSSRPT = 1 THEN DO
      IF &LIBSRTKY = T THEN DO
        SET &HDR2A = &STR(                            TITLE SEQUE)
        SET &HDR2B = &STR(NCE LISTING)
        SET &HDR3A = &STR(                      TITLE            )
        SET &HDR3B = &STR(             MANUAL ID    LOCATION  U  S)
        GOTO PRTHDRS
      END DO
    END DO
    IF &SLSSRPT = 1 THEN DO
      SET &HDR2A = &STR(                          MANUAL ID SEQ)
      SET &HDR2B = &STR(UENCE LISTING)
      SET &HDR3A = &STR( MANUAL ID    LOCATION  U  S           )
      SET &HDR3B = &STR(            TITLE                      )
      GOTO PRTHDRS
    END DO
    IF &SLSSRPT = 2 THEN DO
      SET &HDR2A = &STR(                           MANUALS ON O)
      SET &HDR2B = &STR(DER LISTING)
      SET &HDR3A = &STR( MANUAL ID    DATE ORD.                )
      SET &HDR3B = &STR(            TITLE                      )
      GOTO PRTHDRS
    END DO
PRTHDRS: +
    SET &MARGIN = &SUBSTR(1:&SLSSMARG,&STR(&FS))
    SET &PAGNUM = &PAGNUM + 1
    SET &SLSSOUT = &STR(1&MARGIN &HDR0A&HDR0B &PAGNUM)
    PUTFILE SLSSOUT
    SET &SLSSOUT = &STR( &MARGIN &HDR2A&HDR2B)
    PUTFILE SLSSOUT
    SET &SLSSOUT = &STR( &MARGIN &HDR1A&HDR1B)
    PUTFILE SLSSOUT
    SET &SLSSOUT = &STR(   )
    PUTFILE SLSSOUT
    SET &SLSSOUT = &STR( &MARGIN &HDR3A&HDR3B)
    PUTFILE SLSSOUT
    SET &SLSSOUT = &STR( &MARGIN &HDR4A&HDR4B)
    PUTFILE SLSSOUT
    SET &LNCNT = 6
    IF &PRTRTN = &STR(Y) THEN DO
      SET &SLSSOUT = &STR(&SAVELINE)
      SET &PRTRTN = &STR(N)
      GOTO PRTLINE
    END DO
LOOPTBL: +
    SET &SLSSOUT = &STR( )
    ISPEXEC TBSKIP &SLSS NUMBER(+1)
    SET &RC = &LASTCC
    IF &RC = 8 THEN DO
      GOTO ENDRPT
    END DO
    IF &RC ¬= 0 THEN DO
      SET &SLSSERR1 = UNABLE TO PROCESS LIBRARY &SLSS FOR REPORT.
      SET &SLSSERR2 = RETURN CODE FROM 'ISPEXEC TBSKIP' WAS &RC.
      SET &SLSSERR3 =
      SET &SLSSERR4 =
      SET &SLSSERR5 =
      SET &SLSSERRX = PRESS ENTER TO CONTINUE
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      GOTO ENDRPT
    END DO
    IF &SLSSVER = &STR( ) THEN DO
      SET &R1 = &SUBSTR(1:12,&STR(&SLSID            ))
    END DO
    ELSE DO
      SET &R1 = &SUBSTR(1:12,&STR(&SLSID.-&SLSSVER            ))
    END DO
    SET &R2 = &SUBSTR(1:8,&STR(&BOOK        ))
    SET &R3 = &SUBSTR(1:1,&STR(&STATUS ))
    SET &R4 = &SUBSTR(1:1,&STR(&UPD ))
    SET &R5 = &SUBSTR(1:50,&STR(&NAME&FS))
    SET &R6 = &SUBSTR(1:8,&STR(&DATEORD        ))
SETLINE: +
    IF &SLSSRPT = 1 THEN DO
      IF &LIBSRTKY = L THEN DO
        SET &SLSSOUT = &STR(&CR&MARGIN &R2  &R1  &R3  &R4  &R5)
        GOTO PRTLINE
      END DO
    END DO
    IF &SLSSRPT = 1 THEN DO
      IF &LIBSRTKY = T THEN DO
        SET &SLSSOUT = &STR(&CR&MARGIN &R5  &R1  &R2  &R3  &R4)
        GOTO PRTLINE
      END DO
    END DO
    IF &SLSSRPT = 1 THEN DO
      SET &SLSSOUT = &STR(&CR&MARGIN &R1  &R2  &R3  &R4  &R5)
      GOTO PRTLINE
    END DO
    IF &SLSSRPT = 2 THEN DO
      IF &STR(&R6) ¬= &STR( ) THEN DO
        SET &SLSSOUT = &STR(&CR&MARGIN &R1  &R6  &R5)
        GOTO PRTLINE
      END DO
      GOTO LOOPTBL
    END DO
PRTLINE: +
    IF &SLSSLINE = &STR(S) THEN DO
      IF &LNCNT > &CHKMXS THEN DO
        SET &SAVELINE = &STR(&SLSSOUT)
        SET &PRTRTN = &STR(Y)
        GOTO PRTHDRS
      END DO
    END DO
    ELSE DO
      IF &LNCNT > &CHKMXD THEN DO
        SET &SAVELINE = &STR(&SLSSOUT)
        SET &PRTRTN = &STR(Y)
        GOTO PRTHDRS
      END DO
    END DO
    PUTFILE SLSSOUT
    IF &CR = &STR( ) THEN DO
      SET &LNCNT = &LNCNT + 1
    END DO
    ELSE DO
      SET &LNCNT = &LNCNT + 2
    END DO
CHKNAME2: +
    IF &NAME2 = &STR( ) THEN GOTO CHKLNCNT
    SET &R1 = &STR(            )
    SET &R2 = &STR(        )
    SET &R3 = &STR( )
    SET &R4 = &STR( )
    SET &R5 = &SUBSTR(1:50,&STR(&NAME2&FS))
    SET &R6 = &STR(        )
    IF &SLSSRPT = 1 THEN DO
      IF &LIBSRTKY = L THEN DO
        SET &SLSSOUT = &STR(&CR&MARGIN &R2  &R1  &R3  &R4  &R5)
      END DO
    END DO
    IF &SLSSRPT = 1 THEN DO
      IF &LIBSRTKY = T THEN DO
        SET &SLSSOUT = &STR(&CR&MARGIN &R5  &R1  &R2  &R3  &R4)
      END DO
    END DO
    IF &SLSSRPT = 1 THEN DO
      SET &SLSSOUT = &STR(&CR&MARGIN &R1  &R2  &R3  &R4  &R5)
    END DO
    IF &SLSSRPT = 2 THEN DO
      SET &SLSSOUT = &STR(&CR&MARGIN &R1  &R6  &R5)
    END DO
    PUTFILE SLSSOUT
    IF &CR = &STR( ) THEN DO
      SET &LNCNT = &LNCNT + 1
    END DO
    ELSE DO
      SET &LNCNT = &LNCNT + 2
    END DO
CHKNAME3: +
    IF &NAME3 = &STR( ) THEN GOTO CHKLNCNT
    SET &NAME2 = &STR(&NAME3)
    SET &NAME3 = &STR( )
    GOTO CHKNAME2
CHKLNCNT: +
    IF &LNCNT < &MAXLN THEN DO
      GOTO LOOPTBL
    END DO
    ELSE DO
      SET &SLSSPRC1 =
      SET &SLSSPRC2 = &STR(     PROCESSING COMPLETED FOR PAGE &PAGNUM)
      SET &SLSSPRC3 =
      SET &SLSSPRC4 =
      SET &SLSSPRC5 =
      SET &SLSSPRCX =
      ISPEXEC CONTROL DISPLAY LOCK
      ISPEXEC DISPLAY PANEL(SLSS@PRC)
      GOTO PRTHDRS
    END DO
ENDRPT: +
    CLOSFILE SLSSOUT
    ISPEXEC BROWSE DATASET('&OUTLIST')
    FREE F(SLSSOUT)
    ISPEXEC TBEND &SLSS
    SET &SLSSPRC1 = &STR(          REPORT FOR LIBRARY &SLSS)
    SET &SLSSPRC2 = PRESS ENTER TO SUBMIT JOB TO PRINT REPORT.
    SET &SLSSPRC3 =
    SET &SLSSPRC4 = PRESS END KEY TO TERMINATE PROCESS AND NOT
    SET &SLSSPRC5 = SUBMIT JOB.
    SET &SLSSPRCX = PRESS ENTER TO SUBMIT JOB, END KEY TO TERMINATE.
    ISPEXEC DISPLAY PANEL(SLSS@PRC)
    IF &LASTCC ¬= 0 THEN DO
      DELETE '&OUTLIST'
      GOTO INIT
    END DO
    SET &SLSSP1 = &STR(//*        )
    SET &SLSSP2 = &STR(//*        )
    SET &SLSSP3 = &STR(//*        )
    IF &SLSSFCB ¬= ' ' THEN DO
      SET &SLSSP1 = &STR(//         FCB=&SLSSFCB.,)
    END DO
    IF &SLSSBRST ¬= ' ' THEN DO
      SET &SLSSP2 = &STR(//         BURST=&SLSSBRST.,)
    END DO
    IF &SLSSCHAR ¬= ' ' THEN DO
      SET &SLSSP3 = &STR(//         CHARS=&SLSSCHAR.,)
    END DO
    CONTROL MSG
    ISPEXEC FTOPEN TEMP
    ISPEXEC FTINCL SLSSRPT
    ISPEXEC FTCLOSE
    ISPEXEC VGET ZTEMPF
    IF &DEBUG ¬= &STR(  ) THEN DO
      ISPEXEC EDIT  DATASET('&ZTEMPF')
    END DO
    SUBMIT '&ZTEMPF'
    CONTROL NOMSG
DONE: +
    EXIT CODE(0)
./       ADD   NAME=SLSSOPTS
PROC 0
    ISPEXEC VGET (DEBUG SLSS TECHCONT LIBSRTKY)
    IF &DEBUG = &STR(DEBUG) THEN DO
        CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT ASIS
    END DO
    ELSE DO
        CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST ASIS
    END DO
/*====================================================================*/
/*  AUTHOR:  Dave Weilert                                             */
/*                                                                    */
/*  ADDRESS: Wilson Foods Corporation                                 */
/*           4545 North Lincoln Boulevard                             */
/*           Oklahoma City, Oklahoma 73105                            */
/*                                                                    */
/*  PHONE:   (405) 525-4896                                           */
/*                                                                    */
/*  DOC:     SLSS Scan keyword file (table) routine.                  */
/*                                                                    */
/*====================================================================*/
BEGIN: +
    SET &FILLS    = &STR(            )
    SET &SCANTABL = &SLSS.SCAN
    ISPEXEC CONTROL ERRORS RETURN
    ISPEXEC TBEND  &SCANTABL
    ISPEXEC TBOPEN &SCANTABL NOWRITE
    SET &RC = &LASTCC
    IF &RC = 8 THEN DO
      SET &SLSSERR1 = Keyword scan file does not exist.  Invoke KEYWORD
      SET &SLSSERR2 = BUILD option of UTILITIES and retry this option
      SET &SLSSERR3 = after completing utility.
      SET &SLSSERR4 =
      SET &SLSSERR5 =
      SET &SLSSERRX = Press ENTER to continue
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      ISPEXEC TBEND &SCANTABL
      EXIT
    END DO
    IF &RC ¬= 0 THEN DO
      SET &SLSSERR1 = Unable to open scan table &SCANTABL..
      SET &SLSSERR2 = Return Code for 'ISPEXEC TBOPEN' was &RC..
      SET &SLSSERR3 = Contact &TECHCONT for further information
      SET &SLSSERR4 = regarding this error.
      SET &SLSSERR5 =
      SET &SLSSERRX = Press ENTER to continue
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      ISPEXEC TBEND &SCANTABL
      EXIT
    END DO
    SET &VARCNT = 0
    ISPEXEC TBQUERY &SCANTABL NAMENUM(VARCNT)
    IF &VARCNT > 3 THEN DO
      SET &SLSSERR1 = The scan table &SCANTABL is in the old format.
      SET &SLSSERR2 =
      SET &SLSSERR3 = Contact the owner of the library to convert the
      SET &SLSSERR4 = scan table format by using UTILITY Keyword Build.
      SET &SLSSERR5 =
      SET &SLSSERRX = Press ENTER to continue
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      ISPEXEC TBEND &SCANTABL
      EXIT
    END DO
    ISPEXEC TBOPEN &SLSS
    SET &RC = &LASTCC
    IF &RC ¬= 0 THEN DO
      SET &SLSSERR1 = Unable to open library &SLSS..  This library
      SET &SLSSERR2 = is required when scanning keywords.
      SET &SLSSERR3 = Return Code for 'ISPEXEC TBOPEN' was &RC..
      SET &SLSSERR4 = Invoke ERROR RECOVERY by pressing ENTER.  This
      SET &SLSSERR5 = process will try to save and close &SLSS..
      SET &SLSSERRX = Press ENTER to continue, END to cancel recovery.
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      SET &RC = &LASTCC
      IF &RC = 0 THEN DO
        ISPEXEC TBSAVE &SLSS
        ISPEXEC TBEND  &SLSS
      END DO
      ISPEXEC TBEND &SCANTABL
      EXIT
    END DO
DISPLAY: +
    ISPEXEC TBTOP &SCANTABL
    ISPEXEC DISPLAY PANEL(SLSSCAN1)
    IF &LASTCC = 8 THEN DO
        ISPEXEC TBEND &SCANTABL
        ISPEXEC TBEND &SLSS
        EXIT
    END DO
    IF &STR(&GENERIC) = &STR(??) THEN DO
      ISPEXEC TBDISPL &SCANTABL PANEL(SLSSCAN5)
      GOTO DISPLAY
    END DO
    SET &GEND = &LENGTH(&STR(&GENERIC))
    SET &GCHK = &SUBSTR(&GEND:&GEND,&STR(&GENERIC))
RESCAN: +
    ISPEXEC TBVCLEAR &SCANTABL
    SET &GWORD = &STR(&GENERIC)
    ISPEXEC TBSARG &SCANTABL
    ISPEXEC TBSCAN &SCANTABL
    IF &LASTCC ¬= 0 THEN DO
        SET &SLSSMSG = &STR(NO ENTRIES FOUND)
        GOTO DISPLAY
    END DO
    SET &SLSSMSG = &STR(   )
    SET &FP = 0
    SET &LP = 0
    SET &CT = 0
    SET &WH = 1
    SET &BK = BK
    SET &SCANMSG = &STR( )
    DO WHILE &CT < 79
      SET &CT = &CT + 1
      SET &BK&CT = &STR( )
    END DO
    SET &CT = 0
    DO WHILE &WH <= &NUM
      SET &LP = &WH * 9
      SET &FP = &LP - 8
      SET &BID = &SUBSTR(&FP:&LP,&STR(&BKIDS))
      SET &CT = &CT + 1
      SET &BK&CT = &STR(&BID)
      SET &WH = &WH + 1
      IF &WH > 78 THEN DO
        SET &SCANMSG = &STR(ADDITIONAL NUMBERS NOT SHOWN)
        SET &WH = &NUM + 1
      END DO
    END DO
    ISPEXEC DISPLAY PANEL(SLSSCAN3)
    IF &LASTCC = 0 THEN GOTO SCANIT
    IF &STR(&GCHK) ¬= &STR(*) THEN GOTO DISPLAY
    ISPEXEC DISPLAY PANEL(SLSSCAN4)
    IF &LASTCC = 8 THEN GOTO DISPLAY
    IF &SLSSREP = &STR(Y) THEN GOTO RESCAN
    IF &SLSSREP = &STR(N) THEN GOTO DISPLAY
SCANIT: +
    ISPEXEC TBCREATE TEMP -
    KEYS(SCANNUM) -
    NAMES(SCANBOOK SCANNAM1 SCANNAM2 SCANNAM3) -
    NOWRITE
    IF &LASTCC ¬= 0 THEN DO
        SET &RC = 21
        ISPEXEC VPUT RC
        EXIT
    END DO
LOOPSCAN: +
    SET &WH = 1
    DO WHILE &WH <= &NUM
      SET &LP = &WH * 9
      SET &FP = &LP - 8
      SET &BID = &SUBSTR(&FP:&LP,&STR(&BKIDS))
      ISPEXEC TBVCLEAR &SLSS
      SET &SLSID = &STR(&BID)
      IF &STR(&SLSID) = &STR(  ) THEN GOTO SHOWTABL
      ISPEXEC TBSARG &SLSS
      ISPEXEC TBTOP &SLSS
      ISPEXEC TBSCAN &SLSS
      IF &LASTCC = 0 THEN DO
        SET &SCANNUM  = &STR(&SLSID)
        SET &SCANBOOK = &STR(&BOOK)
        SET &SCANNAM1 = &STR(&NAME1)
        SET &SCANNAM2 = &STR(&NAME2)
        SET &SCANNAM3 = &STR(&NAME3)
        ISPEXEC TBADD TEMP
        SET &RC = &LASTCC
        IF &RC ¬= 0 THEN DO
          WRITE &STR(&RC UNABLE TO ADD &SLSID &BOOK &NAME)
        END DO
        SET &WH = &WH + 1
      END DO
      ELSE DO
        SET &WH = &WH + 1
      END DO
    END DO
SHOWTABL: +
    ISPEXEC TBTOP TEMP
    ISPEXEC TBSKIP TEMP NUMBER(1)
    ISPEXEC TBDISPL TEMP PANEL(SLSSCAN2)
    ISPEXEC TBEND TEMP
    IF &STR(&GCHK) ¬= &STR(*) THEN GOTO DISPLAY
    ISPEXEC DISPLAY PANEL(SLSSCAN4)
    IF &LASTCC = 8 THEN GOTO DISPLAY
    IF &SLSSREP = &STR(Y) THEN GOTO RESCAN
    GOTO DISPLAY
./       ADD   NAME=SLSSOPT1
PROC 0
    ISPEXEC VGET (DEBUG SLSS TECHCONT)
    IF &DEBUG = DEBUG THEN DO
        CONTROL MSG LIST CONLIST SYMLIST ASIS
    END DO
    ELSE DO
        CONTROL NOMSG ASIS
    END DO
/*====================================================================*/
/*  AUTHOR:  Dave Weilert                                             */
/*                                                                    */
/*  ADDRESS: Wilson Foods Corporation                                 */
/*           4545 North Lincoln Boulevard                             */
/*           Oklahoma City, Oklahoma 73105                            */
/*                                                                    */
/*  PHONE:   (405) 525-4896                                           */
/*                                                                    */
/*  DOC:     SLSS Browse routine                                      */
/*                                                                    */
/*====================================================================*/
INIT: +
    SET &ENQU = &STR( )
    ISPEXEC TBOPEN &SLSS NOWRITE
    SET &RC = &LASTCC
    IF &RC = 12 THEN DO
      SET &SLSSWRN1 = Library is currently open for edit by another
      SET &SLSSWRN2 = user.  Please try process at a later time.
      SET &SLSSWRN3 =
      SET &SLSSWRN4 =
      SET &SLSSWRN5 =
      SET &SLSSWRNX = Press ENTER to continue
      ISPEXEC DISPLAY PANEL(SLSS@WRN)
      ISPEXEC TBEND &SLSS
      EXIT
    END DO
    IF &RC ¬= 0 THEN DO
      SET &SLSSERR1 = Unable to open library &SLSS.
      SET &SLSSERR2 = Return Code for 'ISPEXEC TBOPEN' was &RC..
      SET &SLSSERR3 = Contact &TECHCONT for further information
      SET &SLSSERR4 = regarding this error.
      SET &SLSSERR5 =
      SET &SLSSERRX = Press ENTER to continue
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      ISPEXEC TBEND &SLSS
      EXIT
    END DO
PROCESS: +
    ISPEXEC CONTROL DISPLAY REFRESH
    ISPEXEC TBDISPL &SLSS PANEL(SLSSBROW)
    SET &RC = &LASTCC
    IF &RC = 8 THEN DO
      ISPEXEC TBEND &SLSS
      EXIT
    END DO
    IF &STR(&OPT) ¬= &STR( ) THEN DO
      ISPEXEC TBTOP    &SLSS
      ISPEXEC TBVCLEAR &SLSS
      SET &SLSID = &STR(&OPT)
      ISPEXEC TBSARG &SLSS
      ISPEXEC TBSCAN &SLSS
      SET &LCC = &LASTCC
      IF &LCC = 0 THEN GOTO DISPLAY
      IF &LCC = 8 THEN DO
        SET &SLSSCAU1 = &STR(Entry '&SLSID' not found in library)
        SET &SLSSCAU2 =
        SET &SLSSCAU3 =
        SET &SLSSCAU4 =
        SET &SLSSCAU5 =
        SET &SLSSCAUX = Press ENTER to continue
        ISPEXEC DISPLAY PANEL(SLSS@CAU)
        GOTO PROCESS
      END DO
    END DO
    IF &RC = 0 THEN DO
      GOTO DISPLAY
    END DO
    IF &RC = 12 THEN DO
      GOTO DISPLAY
    END DO
    SET &SLSSERR1 = Unable to browse library &SLSS.
    SET &SLSSERR2 = Return Code from 'ISPEXEC TBDISPL' was &RC..
    SET &SLSSERR3 =
    SET &SLSSERR4 =
    SET &SLSSERR5 =
    SET &SLSSERRX = Press ENTER to continue, END key to terminate.
    ISPEXEC DISPLAY PANEL(SLSS@ERR)
    SET &RC = &LASTCC
    IF &RC = 8 THEN EXIT
DISPLAY: +
    ISPEXEC DISPLAY PANEL(SLSSBRW2)
    SET &RC = &LASTCC
    IF &RC = 8 THEN DO
      GOTO PROCESS
    END DO
    GOTO DISPLAY
./       ADD   NAME=SLSSOPT2
PROC 0
    ISPEXEC VGET (DEBUG SLSS LIBADMIN LIBSRTKY TECHCONT) SHARED
    IF &DEBUG = DEBUG THEN DO
        CONTROL MSG LIST CONLIST SYMLIST ASIS
    END DO
    ELSE DO
        CONTROL NOMSG NOLIST NOCONLIST NOSYMLIST ASIS
    END DO
/*====================================================================*/
/*  AUTHOR:  Dave Weilert                                             */
/*                                                                    */
/*  ADDRESS: Wilson Foods Corporation                                 */
/*           4545 North Lincoln Boulevard                             */
/*           Oklahoma City, Oklahoma 73105                            */
/*                                                                    */
/*  PHONE:   (405) 525-4896                                           */
/*                                                                    */
/*  DOC:     SLSS Update routine.  This routine will also delete      */
/*           manuals from a library.                                  */
/*                                                                    */
/*====================================================================*/
INIT: +
    SET &SELECT   = &STR( )
    SET &FUNC     = &STR( )
    SET &MOD      = &STR(N)
    SET &ENQU     = &STR(XXXX)
    SET &LOCSOPEN = &STR(N)
    SET &FIELDS   = &STR( )
    IF &LIBSRTKY =   THEN SET &FIELDS = &STR(SLSID)
    IF &LIBSRTKY = T THEN SET &FIELDS = &STR(NAME1,C,A,SLSID,C,A)
    IF &LIBSRTKY = L THEN SET &FIELDS = &STR(BOOK,C,A,SLSID,C,A)
    IF &LIBSRTKY = 1 OR &LIBSRTKY = 2 OR &LIBSRTKY = 3 +
    OR &LIBSRTKY = 4 THEN SET &FIELDS = &STR(SRTKY,C,A,SLSID,C,A)
PROCESS: +
    ISPEXEC TBOPEN &SLSS
    SET &RC = &LASTCC
    IF &RC = 12 THEN DO
      SET &SLSSERR1 = Library is currently open for EDIT by another
      SET &SLSSERR2 = user.  Please try process ar a later time.
      SET &SLSSERR3 =
      SET &SLSSERR4 =
      SET &SLSSERR5 =
      SET &SLSSERRX = Press ENTER to continue
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      ISPEXEC TBEND &SLSS
      ISPEXEC VPUT (MOD) SHARED
      EXIT
    END DO
    IF &RC = 8 THEN DO
      GOTO BLDTABL
    END DO
    IF &RC ¬= 0 THEN DO
      SET &SLSSERR1 = Error OPENING library &SLSS.
      SET &SLSSERR2 = Return Code for 'ISPEXEC TBOPEN' was &RC.
      SET &SLSSERR3 = Contact &TECHCONT for further information
      SET &SLSSERR4 = regarding this error.
      SET &SLSSERR5 =
      SET &SLSSERRX = Press ENTER to continue
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      ISPEXEC TBEND &SLSS
      ISPEXEC VPUT (MOD) SHARED
      EXIT
    END DO
                                       /* Check Version           */

    SET &VARNUM   =
    ISPEXEC TBQUERY &SLSS  NAMENUM(VARNUM)
    IF &VARNUM < 36 THEN DO
      SET &SLSSWRN1 = Unable to edit library &SLSS in current format.
      SET &SLSSWRN2 = The library may be browsed but must be converted
      SET &SLSSWRN3 = before it may be edited.  Press ENTER to convert
      SET &SLSSWRN4 = the library or press END key to terminate process.
      SET &SLSSWRN5 =
      SET &SLSSWRNX = Press ENTER to continue, END key to terminate
      ISPEXEC DISPLAY PANEL(SLSS@WRN)
      SET &RC = &LASTCC
      IF &RC ¬= 0 THEN DO
        ISPEXEC TBEND &SLSS
        SET &MOD =
        ISPEXEC VPUT (MOD) SHARED
        EXIT
      END DO
      ELSE DO
        SET &VERSION =
        %SLSSCHKV
        ISPEXEC VGET (VERSION) SHARED
        IF &VERSION ¬= &STR(CURRENT) THEN DO
          SET &SLSSERR1 = Library not converted to new format.
          SET &SLSSERR2 = Contact &TECHCONT for futher information
          SET &SLSSERR3 = regarding this error.
          SET &SLSSERR4 =
          SET &SLSSERR5 =
          SET &SLSSERRX = Press ENTER to continue
          ISPEXEC DISPLAY PANEL(SLSS@ERR)
          ISPEXEC TBEND &SLSS
          SET &MOD =
          ISPEXEC VPUT (MOD) SHARED
          EXIT
        END DO
      END DO
    END DO
                                       /* Set SORT                */

    ISPEXEC TBSORT &SLSS FIELDS(&FIELDS)
    SET &RC = &LASTCC
    IF &RC ¬= 0 THEN DO
      SET &SLSSERR1 = Library cannot be kept in sequence if manuals
      SET &SLSSERR2 = are added or deleted.  Return Code for
      SET &SLSSERR3 = 'ISPEXEC TBSORT' was &RC.  Process being
      SET &SLSSERR4 = canceled.
      SET &SLSSERR5 =
      SET &SLSSERRX = Press ENTER to continue
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      ISPEXEC TBEND &SLSS
      ISPEXEC VPUT (MOD) SHARED
      EXIT
    END DO
                                       /* Create ENQ file         */

    ISPEXEC TBOPEN &SLSS.ENQ
    SET &RC = &LASTCC
    IF &RC ¬= 0 THEN DO
      SET &SLSSERR1 = Unable to check enqueue on library &SLSS..
      SET &SLSSERR2 = Return Code from 'ISPEXEC TBOPEN' was &RC..
      SET &SLSSERR3 = Contact &TECHCONT for further information
      SET &SLSSERR4 = regarding this error.
      SET &SLSSERR5 =
      SET &SLSSERRX = Press ENTER to continue
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      ISPEXEC TBEND &SLSS
      ISPEXEC TBEND &SLSS.ENQ
      ISPEXEC VPUT (MOD) SHARED
      EXIT
    END DO
    ISPEXEC TBSKIP &SLSS.ENQ NUMBER(+1)
    IF &ENQU ¬= &STR(    ) THEN DO
      SET &SLSSERR1 = Selected library is currently being edited by
      SET &SLSSERR2 = USER '&ENQU'.  Please try process at a later
      SET &SLSSERR3 = time.
      SET &SLSSERR4 =
      SET &SLSSERR5 =
      SET &SLSSERRX = Press ENTER to continue
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      ISPEXEC TBEND &SLSS
      ISPEXEC TBEND &SLSS.ENQ
      ISPEXEC VPUT (MOD) SHARED
      EXIT
    END DO
    ELSE DO
      SET &ENQU = &SYSUID
      ISPEXEC TBPUT &SLSS.ENQ
      ISPEXEC TBSAVE &SLSS.ENQ
    END DO
    GOTO SHOWIT
BLDTABL: +
    SET &CREATE = Y
    ISPEXEC TBCREATE &SLSS -
        KEYS(SLSID) -
        NAMES(SLSSVER NAME1 NAME2 NAME3 -
        ORDSTS BOOK UPD DATEORD DATERECV -
        LSTUWHO LSTUDAT LSTUTIM CRETWHO CRETDAT CRETTIM -
        PGMID1 PGMID2 PGMID3 PGMID4 SRTKY KEYWORDS -
        UPD0 UPD1 UPD2 UPD3 UPD4 UPD5 UPD6 UPD7 -
        UPD8 UPD9 UPDA UPDB UPDC UPDD UPDE UPDF)
    SET &RC = &LASTCC
    IF &RC ¬= 0 THEN DO
      SET &SLSSERR1 = Unable to create the requested library &SLSS..
      SET &SLSSERR2 = Return Code from 'ISPEXEC TBCREATE' was &RC..
      SET &SLSSERR3 = Contact &TECHCONT for further information
      SET &SLSSERR4 = regarding this error.
      SET &SLSSERR5 =
      SET &SLSSERRX = Press ENTER to continue
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      ISPEXEC TBEND &SLSS
      ISPEXEC VPUT (MOD) SHARED
      EXIT
    END DO
                                       /* Set SORT                */

    IF &LIBSRTKY =   THEN SET &FIELDS = &STR(SLSID)
    IF &LIBSRTKY = T THEN SET &FIELDS = &STR(NAME1,C,A,SLSID,C,A)
    IF &LIBSRTKY = L THEN SET &FIELDS = &STR(BOOK,C,A,SLSID,C,A)
    IF &LIBSRTKY = 1 OR &LIBSRTKY = 2 OR &LIBSRTKY = 3 +
    OR &LIBSRTKY = 4 THEN SET &FIELDS = &STR(SRTKY,C,A,SLSID,C,A)

    ISPEXEC TBSORT &SLSS FIELDS(&FIELDS)
    SET &RC = &LASTCC
    IF &RC ¬= 0 THEN DO
      SET &SLSSERR1 = Library cannot be kept in sequence if manuals
      SET &SLSSERR2 = are added or deleted.  Return Code for
      SET &SLSSERR3 = 'ISPEXEC TBSORT' was &RC.  Process being
      SET &SLSSERR4 = canceled.
      SET &SLSSERR5 =
      SET &SLSSERRX = Press ENTER to continue
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      ISPEXEC TBEND &SLSS
      ISPEXEC VPUT (MOD) SHARED
      EXIT
    END DO

    ISPEXEC TBCREATE &SLSS.ENQ -
        NAMES(ENQU)
    SET &RC = &LASTCC
    IF &RC ¬= 0 THEN DO
      SET &SLSSERR1 = Unabel to create the ENQUEUE library for &SLSS..
      SET &SLSSERR2 = Return Code from 'ISPEXEC TBCREATE' was &RC..
      SET &SLSSERR3 = Contact &TECHCONT for further information
      SET &SLSSERR4 = regarding this error.
      SET &SLSSERR5 =
      SET &SLSSERRX = Press ENTER to continue
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      ISPEXEC TBEND &SLSS.ENQ
      ISPEXEC TBEND &SLSS
      ISPEXEC VPUT (MOD) SHARED
      EXIT
    END DO
    SET &ENQU = &SYSUID
    ISPEXEC TBADD &SLSS.ENQ
    SET &RC = &LASTCC
    IF &RC ¬= 0 THEN DO
      SET &SLSSERR1 = Unable to add USER '&SYSUID' to the ENQUEUE file.
      SET &SLSSERR2 = Return Code from 'ISPEXEC TBADD' was &RC..
      SET &SLSSERR3 = Ccontact &TECHCONT for further information
      SET &SLSSERR4 = regarding this error.
      SET &SLSSERR5 =
      SET &SLSSERRX = Press ENTER to continue
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      ISPEXEC TBEND &SLSS.ENQ
      ISPEXEC TBEND &SLSS
      ISPEXEC VPUT (MOD) SHARED
      EXIT
    END DO
    ISPEXEC TBSAVE &SLSS.ENQ
BLDTBL: +
    ISPEXEC TBCREATE &SLSS.SCAN -
    KEYS(GWORD) -
    NAMES(NUM BKIDS)
    SET &RC = &LASTCC
    IF &RC ¬= 0 THEN DO
      SET &SLSSERR1 = Unable to create Scan file &SLSS.SCAN
      SET &SLSSERR2 = Return Code for 'ISPEXEC TBCREATE' was &RC..
      SET &SLSSERR3 = Contact &TECHCONT for further information
      SET &SLSSERR4 = regarding this error.
      SET &SLSSERR5 =
      SET &SLSSERRX = Press ENTER to continue.
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      ISPEXEC TBEND &SLSS.SCAN
      ISPEXEC TBEND &SLSS.ENQ
      ISPEXEC TBEND &SLSS
      ISPEXEC VPUT (MOD) SHARED
      EXIT
    END DO
    ISPEXEC TBCLOSE &SLSS.SCAN
    SET &RC = &LASTCC
    IF &RC ¬= 0 THEN DO
      SET &SLSSERR1 = Unable to build Scan file.
      SET &SLSSERR2 =
      SET &SLSSERR3 = Cancelling process.
      SET &SLSSERR4 =
      SET &SLSSERR5 =
      SET &SLSSERRX = Press ENTER
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      ISPEXEC TBEND &SLSS.SCAN
      ISPEXEC TBEND &SLSS.ENQ
      ISPEXEC TBEND &SLSS
      ISPEXEC VPUT (MOD) SHARED
      EXIT
    END DO
    GOTO ADD
SHOWIT: +
    IF &FUNC = A THEN GOTO ADD
    SET &CREATE =
    ISPEXEC CONTROL DISPLAY REFRESH
    ISPEXEC TBDISPL &SLSS PANEL(SLSS)
    SET &LCC = &LASTCC
    SET &ADDMSG = &STR(  )
    IF &LCC = 0 THEN GOTO TESTOPT
    IF &LCC = 4 THEN GOTO UPDATE
    IF &LCC = 8 THEN GOTO CLOSE
    SET &SLSSERR1 = Unable to display requested library.
    SET &SLSSERR2 = Return Code for 'ISPEXEC TBDISPL' was &LCC.
    SET &SLSSERR3 = Contact &TECHCONT for further information
    SET &SLSSERR4 = regarding this error.
    SET &SLSSERR5 =
    SET &SLSSERRX = Press ENTER to continue
    ISPEXEC DISPLAY PANEL(SLSS@ERR)
    GOTO CLOSE
TESTOPT:  +
    IF &STR(&OPT) = ? THEN DO
        CONTROL MSG LIST CONLIST SYMLIST ASIS
        GOTO SHOWIT
    END DO
    IF &STR(&OPT) = @ THEN DO
        CONTROL NOMSG NOLIST NOCONLIST NOSYMLIST ASIS
        GOTO SHOWIT
    END DO
    IF &STR(&OPT) = A THEN GOTO ADDS
    IF &STR(&OPT) = SAVE THEN GOTO SAVEIT
    IF &SELECT = D THEN GOTO DELETE
    IF &SELECT = S THEN GOTO REFRESH
    IF &STR(&OPT) = THEN GOTO SHOWIT
    SET &SLSID = &STR(&OPT)
    ISPEXEC TBGET &SLSS
    SET &LCC = &LASTCC
    IF &LCC = 0 THEN GOTO SHOWIT
    IF &LCC = 8 THEN DO
      SET &SLSSWRN1 = &STR(Entry '&SLSID' not found in library.)
      SET &SLSSWRN2 =
      SET &SLSSWRN3 =
      SET &SLSSWRN4 =
      SET &SLSSWRN5 =
      SET &SLSSWRNX = Press ENTER to continue
      ISPEXEC DISPLAY PANEL(SLSS@WRN)
    END DO
    GOTO SHOWIT
SAVEIT:  +
    ISPEXEC TBSAVE &SLSS PAD(30)
    GOTO SHOWIT
ADDS:  +
    ISPEXEC TBVCLEAR &SLSS
ADD:     +
    ISPEXEC DISPLAY PANEL(SLSSADD)
    IF &LASTCC > 0 THEN GOTO NOADD
    IF &STR(&SLSID) = &STR( ) THEN GOTO ADD
    IF &FIELDS = &STR(SRTKY,C,A,SLSID,C,A) THEN DO
      SET &SRTKY = &SUBSTR(&LIBSRTKY:9,&STR(&SLSID         ))
    END DO
    SET &SLSSVER = &SUBSTR(1:2,&STR(&SLSSVER   ))
    SET &SLSID = &SUBSTR(1:9,&STR(&SLSID            ))
    IF &STR(&DATEORD) = &STR( ) THEN DO
        SET &DATEORD = &STR(        )
        SET &ORDSTS  = &STR( )
    END DO
    ELSE DO
        SET &ORDSTS  = &STR(*)
    END DO
    IF &STR(&DATERECV) = &STR( ) THEN DO
        SET &DATERECV = &STR(        )
    END DO
    SET &CRETWHO = &SYSUID
    SET &CRETDAT = &STR(&SYSDATE)
    SET &CRETTIM = &STR(&SYSTIME)
    SET &LSTUWHO = &STR(  )
    SET &LSTUDAT = &STR(  )
    SET &LSTUTIM = &STR(  )
    SET &CKCHAR = &SUBSTR(1:1,&STR(&NAME1))
    IF &CKCHAR < &STR(A) THEN DO
      SET &SLSSWRN1 = TITLE: &STR(&NAME1)
      SET &SLSSWRN2 = First character of the above manual title contains
      SET &SLSSWRN3 = a value less than an upper case A.  Ensure you
      SET &SLSSWRN4 = want the desired value, lower case letters sort
      SET &SLSSWRN5 = sort before upper case letters.
      SET &SLSSWRNX = Press ENTER to continue, END to modify
      ISPEXEC DISPLAY PANEL(SLSS@WRN)
      SET &RC = &LASTCC
      IF &RC ¬= 0 THEN GOTO ADD
    END DO
    ISPEXEC TBADD &SLSS ORDER
    SET &RC = &LASTCC
    IF &RC = 0 THEN DO
      SET &ADDMSG = &STR(ENTRY &SLSID ADDED)
      SET &KEYOPT = (A)
      ISPEXEC VPUT (DEBUG SLSS SLSID NAME1 NAME2 NAME3 KEYWORDS KEYOPT)
      %SLSSKEYU
    END DO
    IF &RC = 8 THEN DO
      SET &SLSSERR1 = &STR(Entry &SLSID alreaty exists.)
      SET &SLSSERR2 =
      SET &SLSSERR3 =
      SET &SLSSERR4 =
      SET &SLSSERR5 =
      SET &SLSSERRX = Press ENTER to continue
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      GOTO ADD
    END DO
    IF &RC ¬= 0 THEN DO
      SET &SLSSERR1 = Unable to complete requested function.
      SET &SLSSERR2 = Return Code for 'ISPEXEC TBADD' was &RC.
      SET &SLSSERR3 = Please try process again or contact
      SET &SLSSERR4 = &TEHCCONT for further information regarding
      SET &SLSSERR5 = this error.
      SET &SLSSERRX = Press ENTER to continue
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      GOTO ADD
    END DO
    IF &SELECT = R THEN GOTO SHOWIT
    IF &LOCS = Y THEN GOTO PROCLOCS
    GOTO ADD
NOADD:  +
    SET &FUNC = D
    GOTO SHOWIT
DELETE:  +
    SET &ADDMSG = &STR(    )
    SET &SLSSPRC1 = &STR(Confirm request to DELETE entry &SLSID.)
    SET &SLSSPRC2 =
    SET &SLSSPRC3 = Press ENTER to CONFIRM request.
    SET &SLSSPRC4 = Press END key to TERMINATE request.
    SET &SLSSPRC5 =
    SET &SLSSPRCX = Press ENTER to CONFIRM, END key to TERMINATE.
    ISPEXEC DISPLAY PANEL(SLSS@PRC)
    SET &RC = &LASTCC
    IF &RC = 8 THEN DO
      SET &ADDMSG = &STR(DELETE request for entry &SLSID terminated)
      GOTO SHOWIT
    END DO
    SET &OLDNAME1 = &STR(&NAME1)
    SET &OLDNAME2 = &STR(&NAME2)
    SET &OLDNAME3 = &STR(&NAME3)
    SET &OLDKWRDS = &STR(&KEYWORDS)
    ISPEXEC TBDELETE &SLSS
    SET &RC = &LASTCC
    IF &RC = 0 THEN DO
      SET &ADDMSG = &STR(ENTRY &SLSID DELETED )
      SET &KEYOPT = (D)
      ISPEXEC VPUT (DEBUG SLSS SLSID NAME1 NAME2 NAME3 KEYWORDS +
                    KEYOPT OLDNAME1 OLDNAME2 OLDNAME3 OLDKWRDS)
      %SLSSKEYU
    END DO
    ELSE DO
      SET &SLSSWRN1 = Unabel to complete request to delete entry.
      SET &SLSSWRN2 = Return Code from 'ISPEXEC TBDELETE' was &RC.
      SET &SLSSWRN3 =
      SET &SLSSWRN4 =
      SET &SLSSWRN5 =
      SET &SLSSWRNX = Press ENTER to continue.
      ISPEXEC DISPLAY PANEL(SLSS@WRN)
    END DO
    IF &LOCSOPEN = Y THEN DO
      SET &LOCID = &STR(&SLSS&SLSID)
      ISPEXEC TBDELETE &SLSS.LOCS
      GOTO SHOWIT
    END DO
    ISPEXEC TBOPEN &SLSS.LOCS
    SET &RC = &LASTCC
    IF &RC = 8 THEN DO
      GOTO SHOWIT
    END DO
    SET &LOCID = &STR(&SLSS&SLSID)
    ISPEXEC TBDELETE &SLSS.LOCS
    ISPEXEC TBCLOSE &SLSS.LOCS
    GOTO SHOWIT
UPDATE:  +
    IF &SELECT = D THEN GOTO DELETE
    SET &OLDNAME1 = &STR(&NAME1)
    SET &OLDNAME2 = &STR(&NAME2)
    SET &OLDNAME3 = &STR(&NAME3)
    SET &OLDKWRDS = &STR(&KEYWORDS)
    SET &ADDMSG = &STR(    )
    IF &STR(&DATEORD) = &STR( ) THEN DO
        SET &DATEORD = &STR(        )
        SET &ORDSTS = &STR( )
    END DO
    ELSE DO
        SET &ORDSTS = &STR(*)
    END DO
    IF &STR(&DATERECV) = &STR( ) THEN DO
        SET &DATERECV = &STR(        )
    END DO
    SET &LSTUWHO = &STR(&SYSUID)
    SET &LSTUDAT = &STR(&SYSDATE)
    SET &LSTUTIM = &STR(&SYSTIME)
    SET &CKCHAR = &SUBSTR(1:1,&STR(&NAME1))
    IF &CKCHAR < &STR(A) THEN DO
      SET &SLSSWRN1 = TITLE: &STR(&NAME1)
      SET &SLSSWRN2 = First character of the above manual title contains
      SET &SLSSWRN3 = a value less than an upper case A.  Ensure you
      SET &SLSSWRN4 = want the desired value, lower case letters sort
      SET &SLSSWRN5 = sort before upper case letters.
      SET &SLSSWRNX = Press ENTER to continue, END to modify
      ISPEXEC DISPLAY PANEL(SLSS@WRN)
      SET &RC = &LASTCC
      IF &RC ¬= 0 THEN GOTO UPDATE
    END DO
    ISPEXEC TBMOD &SLSS ORDER
    SET &RC = &LASTCC
    IF &RC = 0 THEN DO
      SET &ADDMSG = &STR(ENTRY &SLSID MODIFIED )
      SET &KEYOPT = (U)
      ISPEXEC VPUT (DEBUG SLSS SLSID NAME1 NAME2 NAME3 KEYWORDS +
                    KEYOPT OLDNAME1 OLDNAME2 OLDNAME3 OLDKWRDS)
      %SLSSKEYU
    END DO
    IF &RC = 8 THEN DO
      SET &SLSSWRN1 = &STR(Entry &SLSID did not exist, but was added.)
      SET &SLSSWRN2 = Return Code from 'ISPEXEC TBMOD' was &RC.
      SET &SLSSWRN3 =
      SET &SLSSWRN4 =
      SET &SLSSWRN5 =
      SET &SLSSWRNX = Press ENTER to continue.
      ISPEXEC DISPLAY PANEL(SLSS@WRN)
      IF &LOCS = Y THEN GOTO PROCLOCS
      GOTO SHOWIT
    END DO
    IF &RC ¬= 0 THEN DO
      SET &SLSSWRN1 = Unable to complete request to update entry.
      SET &SLSSWRN2 = Return Code from 'ISPEXEC TBMOD' was &RC.
      SET &SLSSWRN3 =
      SET &SLSSWRN4 =
      SET &SLSSWRN5 =
      SET &SLSSWRNX = Press ENTER to continue.
      ISPEXEC DISPLAY PANEL(SLSS@WRN)
    END DO
    IF &LOCS = Y THEN GOTO PROCLOCS
    GOTO SHOWIT
REFRESH:  +
    SET &OLDID = &STR(&SLSID)
    SET &ADDMSG = &STR(    )
    SET &OLDNAME1 = &STR(&NAME1)
    SET &OLDNAME2 = &STR(&NAME2)
    SET &OLDNAME3 = &STR(&NAME3)
    SET &OLDKWRDS = &STR(&KEYWORDS)
    ISPEXEC DISPLAY PANEL(SLSSADD)
    IF &LASTCC >0 THEN GOTO SHOWIT
    SET &SLSSVER = &SUBSTR(1:2,&STR(&SLSSVER  ))
    SET &SLSID = &SUBSTR(1:9,&STR(&SLSID           ))
    IF &STR(&DATEORD) = &STR( ) THEN DO
        SET &DATEORD = &STR(        )
        SET &ORDSTS = &STR( )
    END DO
    ELSE DO
        SET &ORDSTS = &STR(*)
    END DO
    IF &STR(&DATERECV) = &STR( ) THEN DO
        SET &DATERECV = &STR(        )
    END DO
    SET &LSTUWHO = &STR(&SYSUID)
    SET &LSTUDAT = &STR(&SYSDATE)
    SET &LSTUTIM = &STR(&SYSTIME)
    ISPEXEC TBMOD &SLSS ORDER
    SET &RC = &LASTCC
    IF &RC = 0 THEN DO
      SET &ADDMSG = &STR(Entry &SLSID modified )
      SET &KEYOPT = (U)
      ISPEXEC VPUT (DEBUG SLSS SLSID NAME1 NAME2 NAME3 KEYWORDS +
                    KEYOPT OLDNAME1 OLDNAME2 OLDNAME3 OLDKWRDS)
      %SLSSKEYU
    END DO
    IF &RC = 8 THEN DO
      ISPEXEC TBDELETE &SLSS
      SET &SLSSERR1 = Entry was not modified.  To add a new entry
      SET &SLSSERR2 = enter OPTION 'A' on MAINTENANCE SELECTION panel.
      SET &SLSSERR3 =
      SET &SLSSERR4 =
      SET &SLSSERR5 =
      SET &SLSSERRX = Press ENTER to continue.
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      SET &SLSID = &STR(&OLDID)
      ISPEXEC TBGET &SLSS
      GOTO REFRESH
    END DO
    IF &RC ¬= 0 THEN DO
      SET &SLSSERR1 = UNABLE TO COMPLETE REQUEST TO UPDATE ENTRY.
      SET &SLSSERR2 = RETURN CODE FROM 'ISPEXEC TBMOD' WAS &RC.
      SET &SLSSERR3 =
      SET &SLSSERR4 =
      SET &SLSSERR5 =
      SET &SLSSERRX = PRESS ENTER TO continue.
      ISPEXEC DISPLAY PANEL(SLSS@WRN)
      GOTO SHOWIT
    END DO
    IF &LOCS = Y THEN GOTO PROCLOCS
    GOTO SHOWIT
PROCLOCS: +
    SET &LOCS =
    IF &LOCSOPEN = Y THEN GOTO SHOWLOCS
    ISPEXEC TBOPEN &SLSS.LOCS
    SET &RC = &LASTCC
    IF &RC = 8 THEN DO
      GOTO BLDLOCS
    END DO
    IF &RC ¬= 0 THEN DO
      SET &SLSSERR1 = ERROR OPENING LIBRARY &SLSS.LOCS
      SET &SLSSERR2 = RETURN CODE FOR 'ISPEXEC TBOPEN' WAS &RC.
      SET &SLSSERR3 = CONTACT TECHNICAL SERVICE FOR FURTHER INFORMATION
      SET &SLSSERR4 = CONCERNING THIS ERROR.
      SET &SLSSERR5 =
      SET &SLSSERRX = PRESS ENTER TO continue
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      ISPEXEC TBEND &SLSS.LOCS
      GOTO SHOWIT
    END DO
    SET &LOCSOPEN = Y
    GOTO SHOWLOCS
BLDLOCS: +
    ISPEXEC TBCREATE &SLSS.LOCS -
    KEYS(LOCID) -
    NAMES(LOC1  LOC2  LOC3  LOC4  LOC5  LOC6  LOC7  LOC8  LOC9  LOC10 -
          LOC11 LOC12 LOC13 LOC14 LOC15 LOC16 LOC17 LOC18 LOC19 LOC20 -
          LOC21 LOC22 LOC23 LOC24 LOC25 LOC26 LOC27 LOC28 LOC29 LOC30 -
          LOC31 LOC32 LOC33 LOC34 LOC35 LOC36 LOC37 LOC38 LOC39 LOC40 -
          LOC41 LOC42 LOC43 LOC44 LOC45 LOC46 LOC47 LOC48 LOC49 LOC50 -
          LOC51 LOC52 LOC53 LOC54 LOC55 LOC56 LOC57 LOC58 LOC59 LOC60)
    SET &RC = &LASTCC
    IF &RC ¬= 0 THEN DO
      SET &SLSSERR1 = UNABLE TO CREATE THE REQUESTED LIBRARY &SLSS.LOCS
      SET &SLSSERR2 = RETURN CODE FROM 'ISPEXEC TBCREATE' WAS &RC..
      SET &SLSSERR3 = PLEASE CONTACT TECHNICAL SERVICES FOR FURTHER
      SET &SLSSERR4 = INFORMATION REGARDING THIS ERROR.
      SET &SLSSERR5 =
      SET &SLSSERRX = PRESS ENTER TO continue
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      ISPEXEC TBEND &SLSS.LOCS
      GOTO SHOWIT
    END DO
    SET &LOCSOPEN = Y
SHOWLOCS: +
    SET &LOCID = &STR(&SLSS&SLSID)
    ISPEXEC TBGET &SLSS.LOCS
    SET &RC = &LASTCC
    IF &RC = 8 THEN DO
      SET &SLSSPRC1 = The current manual does not have any additional
      SET &SLSSPRC2 = locations.  Enter locations on next panel.
      SET &SLSSPRC3 =
      SET &SLSSPRC4 =
      SET &SLSSPRC5 =
      SET &SLSSPRCX = PRESS ENTER TO continue
      ISPEXEC DISPLAY PANEL(SLSS@PRC)
      SET &LOCSAVE = &LOCID
      ISPEXEC TBVCLEAR &SLSS.LOCS
      SET &LOCID = &LOCSAVE
    END DO
    ISPEXEC DISPLAY PANEL(SLSSLOCS)
    IF &LASTCC = 8 THEN DO
      GOTO SHOWIT
    END DO
    ISPEXEC TBMOD &SLSS.LOCS
    SET &RC = &LASTCC
    IF &RC > 12 THEN DO
      SET &SLSSERR1 = UNABLE TO ADD/UPDATE ENTRY.
      SET &SLSSERR2 = RETURN CODE FOR 'ISPEXEC TBMOD' WAS &RC.
      SET &SLSSERR3 =
      SET &SLSSERR4 =
      SET &SLSSERR5 =
      SET &SLSSERRX = PRESS ENTER TO continue
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
    END DO
    SET &ADDMSG = &STR(ENTRY &SLSID AND LOCATIONS MODIFIED)
    GOTO SHOWIT
CLOSE: +
    SET &ENQU = &STR(    )
    SET &MOD = &STR(Y)
    ISPEXEC VPUT (MOD) SHARED
    ISPEXEC TBPUT &SLSS.ENQ
    ISPEXEC TBCLOSE &SLSS.ENQ
    SET &RC = &LASTCC
    IF &RC ¬= 0 THEN DO
      SET &SLSSERR1 = Unable to close and properly update the enqueue
      SET &SLSSERR2 = file.  You may not be able to edit library the
      SET &SLSSERR3 = next time selected.  You may be required to select
      SET &SLSSERR4 = the edit option several times to provide proper
      SET &SLSSERR5 = cleanup process. &STR(LASTCC = &RC)
      SET &SLSSERRX = Press ENTER to continue
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      ISPEXEC TBEND &SLSS
    END DO
    ISPEXEC TBCLOSE &SLSS PAD(30)
    SET &RC = &LASTCC
    IF &RC ¬= 0 THEN DO
      SET &SLSSERR1 = Unable to close and properly update the library.
      SET &SLSSERR2 = All updates may be lost.  &STR(LASTCC = &RC)
      SET &SLSSERR3 =
      SET &SLSSERR4 =
      SET &SLSSERR5 =
      SET &SLSSERRX = Press ENTER to continue
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      ISPEXEC TBEND &SLSS
    END DO
    IF &LOCSOPEN = Y THEN DO
      ISPEXEC TBCLOSE &SLSS.LOCS
    END DO
    EXIT
./       ADD   NAME=SLSSPRNT
PROC 0
    ISPEXEC VGET (DEBUG SLSS LIBSRTKY LIBNUMBR LIBOWNER) SHARED
    IF &DEBUG = DEBUG THEN DO
        CONTROL MSG LIST CONLIST SYMLIST ASIS
    END DO
    ELSE DO
        CONTROL NOMSG ASIS
    END DO
/*====================================================================*/
/*  AUTHOR:  Dave Weilert                                             */
/*                                                                    */
/*  ADDRESS: Wilson Foods Corporation                                 */
/*           4545 North Lincoln Boulevard                             */
/*           Oklahoma City, Oklahoma 73105                            */
/*                                                                    */
/*  PHONE:   (405) 525-4896                                           */
/*                                                                    */
/*  DOC:     SLSS report listing generator.                           */
/*                                                                    */
/*====================================================================*/
SETI: +
    SET &FS = &STR(                                                  )
    SET &PRTRTN = &STR(N)
    SET &SAVELINE = &STR( )
INIT: +
    ISPEXEC VGET (SLSSJCL1 SLSSJCL2 SLSSJCL3 SLSSJCL4 SLSSOCLS +
                  SLSSFCB  SLSSCOPY SLSSCHAR SLSSBRST SLSSLPI  +
                  SLSSPAG  SLSSMARG SLSSLINE) PROFILE
DISPLAY: +
    ISPEXEC DISPLAY PANEL(SLSSPRNT)
    IF &LASTCC ¬= 0 THEN DO
      EXIT
    END DO
    ISPEXEC VPUT (SLSSJCL1 SLSSJCL2 SLSSJCL3 SLSSJCL4 SLSSOCLS +
                  SLSSFCB  SLSSCOPY SLSSCHAR SLSSBRST SLSSLPI  +
                  SLSSPAG  SLSSMARG SLSSLINE) PROFILE
    IF &SLSSRPT = &STR(O) THEN DO
      %SLSLONOR
      GOTO DISPLAY
    END DO
    ISPEXEC TBOPEN &SLSS
OUTFILE: +
    SET &WRKTIME = &SYSTIME
    SET &W1 = &SUBSTR(1:2,&STR(&WRKTIME))
    SET &W2 = &SUBSTR(4:5,&STR(&WRKTIME))
    SET &W3 = &SUBSTR(7:8,&STR(&WRKTIME))
    SET &OUTLIST = &STR(&SYSUID..T&W1&W2&W3..OUTLIST)
    FREE F(SLSSOUT)
    FREE ATTR(ATTR2)
    ATTR ATTR2 LRECL(133) BLKSIZE(6118) RECFM(F B A)
    ALLOC F(SLSSOUT)  DA('&OUTLIST')   NEW SP(5 2) TR -
        RELEASE  USING(ATTR2)
    OPENFILE SLSSOUT OUTPUT
SETPAGE: +
    SET &PAGNUM = 0
    IF &SLSSLPI = 6 THEN DO
      IF &SLSSPAG = &STR(8.5) THEN DO
        SET &MAXLN = 44
        SET &CHKMXS = 41
        SET &CHKMXD = 38
      END DO
      ELSE DO
        SET &MAXLN = 59
        SET &CHKMXS = 56
        SET &CHKMXD = 53
      END DO
    END DO
    ELSE DO
      IF &SLSSPAG = &STR(8.5) THEN DO
        SET &MAXLN = 59
        SET &CHKMXS = 56
        SET &CHKMXD = 53
      END DO
      ELSE DO
        SET &MAXLN = 79
        SET &CHKMXS = 76
        SET &CHKMXD = 73
      END DO
    END DO
SETHEAD: +
    IF &SLSSLINE = &STR(S) THEN DO
      SET &CR = &STR( )
    END DO
    ELSE DO
      SET &CR = &STR(0)
    END DO
    IF &LIBSRTKY = &STR( ) THEN DO
      SET &LIBSRTKY = &STR(1)
    END DO
    SET &HDR0A = &STR(&SYSDATE                     SLSS LIBRA)
    SET &HDR0B = &STR(RY REPORT                     PAGE )
    SET &HDR1A = &STR(            LIBRARY ID: &SLSS  NUMBER: )
    SET &HDR1B = &STR(&LIBNUMBR  OWNER: &LIBOWNER            )
    SET &HDR4A = &STR(---------------------------------------)
    SET &HDR4B = &STR(---------------------------------------)
    IF &SLSSRPT = 1 THEN DO
      IF &LIBSRTKY = L THEN DO
        SET &HDR2A = &STR(                          LOCATION SEQU)
        SET &HDR2B = &STR(ENCE LISTING)
        SET &HDR3A = &STR(LOCATION   MANUAL ID    U  S           )
        SET &HDR3B = &STR(            TITLE )
        GOTO PRTHDRS
      END DO
    END DO
    IF &SLSSRPT = 1 THEN DO
      IF &LIBSRTKY = T THEN DO
        SET &HDR2A = &STR(                            TITLE SEQUE)
        SET &HDR2B = &STR(NCE LISTING)
        SET &HDR3A = &STR(                      TITLE            )
        SET &HDR3B = &STR(             MANUAL ID    LOCATION  U  S)
        GOTO PRTHDRS
      END DO
    END DO
    IF &SLSSRPT = 1 THEN DO
      SET &HDR2A = &STR(                          MANUAL ID SEQ)
      SET &HDR2B = &STR(UENCE LISTING)
      SET &HDR3A = &STR( MANUAL ID    LOCATION  U  S           )
      SET &HDR3B = &STR(            TITLE                      )
      GOTO PRTHDRS
    END DO
    IF &SLSSRPT = 2 THEN DO
      SET &HDR2A = &STR(                           MANUALS ON O)
      SET &HDR2B = &STR(RDER LISTING)
      SET &HDR3A = &STR( MANUAL ID    DATE ORD.                )
      SET &HDR3B = &STR(            TITLE                      )
      GOTO PRTHDRS
    END DO
PRTHDRS: +
    SET &MARGIN = &SUBSTR(1:&SLSSMARG,&STR(&FS))
    SET &PAGNUM = &PAGNUM + 1
    SET &SLSSOUT = &STR(1&MARGIN &HDR0A&HDR0B &PAGNUM)
    PUTFILE SLSSOUT
    SET &SLSSOUT = &STR( &MARGIN &HDR2A&HDR2B)
    PUTFILE SLSSOUT
    SET &SLSSOUT = &STR( &MARGIN &HDR1A&HDR1B)
    PUTFILE SLSSOUT
    SET &SLSSOUT = &STR(   )
    PUTFILE SLSSOUT
    SET &SLSSOUT = &STR( &MARGIN &HDR3A&HDR3B)
    PUTFILE SLSSOUT
    SET &SLSSOUT = &STR( &MARGIN &HDR4A&HDR4B)
    PUTFILE SLSSOUT
    SET &LNCNT = 6
    IF &PRTRTN = &STR(Y) THEN DO
      SET &SLSSOUT = &STR(&SAVELINE)
      SET &PRTRTN = &STR(N)
      GOTO PRTLINE
    END DO
LOOPTBL: +
    SET &BYPASS = &STR(N)
    SET &SLSSOUT = &STR( )
    ISPEXEC TBSKIP &SLSS NUMBER(+1)
    SET &RC = &LASTCC
    IF &RC = 8 THEN DO
      GOTO ENDRPT
    END DO
    IF &RC ¬= 0 THEN DO
      SET &SLSSERR1 = UNABLE TO PROCESS LIBRARY &SLSS FOR REPORT.
      SET &SLSSERR2 = RETURN CODE FROM 'ISPEXEC TBSKIP' WAS &RC.
      SET &SLSSERR3 =
      SET &SLSSERR4 =
      SET &SLSSERR5 =
      SET &SLSSERRX = PRESS ENTER TO CONTINUE
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      GOTO ENDRPT
    END DO
    IF &SLSSVER = &STR( ) THEN DO
      SET &R1 = &SUBSTR(1:12,&STR(&SLSID            ))
    END DO
    ELSE DO
      SET &R1 = &SUBSTR(1:12,&STR(&SLSID.-&SLSSVER            ))
    END DO
    SET &R2 = &SUBSTR(1:8,&STR(&BOOK        ))
    SET &R3 = &SUBSTR(1:1,&STR(&ORDSTS ))
    SET &R4 = &SUBSTR(1:1,&STR(&UPD ))
    SET &R5 = &SUBSTR(1:50,&STR(&NAME1&FS))
    SET &R6 = &SUBSTR(1:8,&STR(&DATEORD        ))
SETLINE: +
    IF &SLSSRPT = 1 THEN DO
      IF &LIBSRTKY = L THEN DO
        SET &SLSSOUT = &STR(&CR&MARGIN &R2  &R1  &R3  &R4  &R5)
        GOTO PRTLINE
      END DO
    END DO
    IF &SLSSRPT = 1 THEN DO
      IF &LIBSRTKY = T THEN DO
        SET &SLSSOUT = &STR(&CR&MARGIN &R5  &R1  &R2  &R3  &R4)
        GOTO PRTLINE
      END DO
    END DO
    IF &SLSSRPT = 1 THEN DO
      SET &SLSSOUT = &STR(&CR&MARGIN &R1  &R2  &R3  &R4  &R5)
      GOTO PRTLINE
    END DO
    IF &SLSSRPT = 2 THEN DO
      IF &STR(&R6) ¬= &STR( ) THEN DO
        SET &SLSSOUT = &STR(&CR&MARGIN &R1  &R6  &R5)
        GOTO PRTLINE
      END DO
      GOTO LOOPTBL
    END DO
PRTLINE: +
    IF &SLSSLINE = &STR(S) THEN DO
      IF &LNCNT > &CHKMXS THEN DO
        SET &SAVELINE = &STR(&SLSSOUT)
        SET &PRTRTN = &STR(Y)
        GOTO PRTHDRS
      END DO
    END DO
    ELSE DO
      IF &LNCNT > &CHKMXD THEN DO
        SET &SAVELINE = &STR(&SLSSOUT)
        SET &PRTRTN = &STR(Y)
        GOTO PRTHDRS
      END DO
    END DO
    PUTFILE SLSSOUT
    IF &CR = &STR( ) THEN DO
      SET &LNCNT = &LNCNT + 1
    END DO
    ELSE DO
      SET &LNCNT = &LNCNT + 2
    END DO
CHKNAME2: +
    IF &STR(&NAME2) = &STR( ) THEN GOTO CHKLNCNT
PRCNAME2: +
    SET &R1 = &STR(            )
    SET &R2 = &STR(        )
    SET &R3 = &STR( )
    SET &R4 = &STR( )
    SET &R5 = &SUBSTR(1:50,&STR(&NAME2&FS))
    SET &R6 = &STR(        )
    IF &SLSSRPT = 1 THEN DO
      IF &LIBSRTKY = L THEN DO
        SET &SLSSOUT = &STR(&CR&MARGIN &R2  &R1  &R3  &R4  &R5)
        GOTO PUTLN2
      END DO
    END DO
    IF &SLSSRPT = 1 THEN DO
      IF &LIBSRTKY = T THEN DO
        SET &SLSSOUT = &STR(&CR&MARGIN &R5  &R1  &R2  &R3  &R4)
        GOTO PUTLN2
      END DO
    END DO
    IF &SLSSRPT = 1 THEN DO
      SET &SLSSOUT = &STR(&CR&MARGIN &R1  &R2  &R3  &R4  &R5)
      GOTO PUTLN2
    END DO
    IF &SLSSRPT = 2 THEN DO
      SET &SLSSOUT = &STR(&CR&MARGIN &R1  &R6  &R5)
      GOTO PUTLN2
    END DO
PUTLN2: +
    PUTFILE SLSSOUT
    IF &CR = &STR( ) THEN DO
      SET &LNCNT = &LNCNT + 1
    END DO
    ELSE DO
      SET &LNCNT = &LNCNT + 2
    END DO
CHKNAME3: +
    IF &BYPASS = &STR(Y) THEN GOTO CHKLNCNT
    IF &STR(&NAME3) = &STR( ) THEN GOTO CHKLNCNT
    SET &BYPASS = &STR(Y)
    SET &NAME2 = &STR(&NAME3)
    SET &NAME3 = &STR( )
    GOTO CHKNAME2
CHKLNCNT: +
    IF &LNCNT < &MAXLN THEN DO
      GOTO LOOPTBL
    END DO
    ELSE DO
      SET &SLSSPRC1 =
      SET &SLSSPRC2 = &STR(     PROCESSING COMPLETED FOR PAGE &PAGNUM)
      SET &SLSSPRC3 =
      SET &SLSSPRC4 =
      SET &SLSSPRC5 =
      SET &SLSSPRCX =
      ISPEXEC CONTROL DISPLAY LOCK
      ISPEXEC DISPLAY PANEL(SLSS@PRC)
      GOTO PRTHDRS
    END DO
ENDRPT: +
    CLOSFILE SLSSOUT
    ISPEXEC BROWSE DATASET('&OUTLIST')
    FREE F(SLSSOUT)
    ISPEXEC TBEND &SLSS
    SET &SLSSPRC1 = &STR(          REPORT FOR LIBRARY &SLSS)
    SET &SLSSPRC2 = PRESS ENTER TO SUBMIT JOB TO PRINT REPORT.
    SET &SLSSPRC3 =
    SET &SLSSPRC4 = PRESS END KEY TO TERMINATE PROCESS AND NOT
    SET &SLSSPRC5 = SUBMIT JOB.
    SET &SLSSPRCX = PRESS ENTER TO SUBMIT JOB, END KEY TO TERMINATE.
    ISPEXEC DISPLAY PANEL(SLSS@PRC)
    IF &LASTCC ¬= 0 THEN DO
      DELETE '&OUTLIST'
      GOTO INIT
    END DO
    SET &SLSSP1 = &STR(//*        )
    SET &SLSSP2 = &STR(//*        )
    SET &SLSSP3 = &STR(//*        )
    IF &SLSSFCB ¬= ' ' THEN DO
      SET &SLSSP1 = &STR(//         FCB=&SLSSFCB.,)
    END DO
    IF &SLSSBRST ¬= ' ' THEN DO
      SET &SLSSP2 = &STR(//         BURST=&SLSSBRST.,)
    END DO
    IF &SLSSCHAR ¬= ' ' THEN DO
      SET &SLSSP3 = &STR(//         CHARS=&SLSSCHAR.,)
    END DO
    CONTROL MSG
    ISPEXEC FTOPEN TEMP
    ISPEXEC FTINCL SLSSRPT
    ISPEXEC FTCLOSE
    ISPEXEC VGET ZTEMPF
    IF &DEBUG ¬= &STR(  ) THEN DO
      ISPEXEC EDIT  DATASET('&ZTEMPF')
    END DO
    SUBMIT '&ZTEMPF'
    CONTROL NOMSG
DONE: +
    EXIT CODE(0)
./       ADD   NAME=SLSSRCOV
PROC 0
  ISPEXEC VGET (DEBUG SLSS LIBOWNER LIBADMIN) SHARED
  IF &DEBUG = &STR(DEBUG) THEN DO
      CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT
  END DO
  ELSE DO
      CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST
  END DO
/*====================================================================*/
/*  AUTHOR:  Dave Weilert                                             */
/*                                                                    */
/*  ADDRESS: Wilson Foods Corporation                                 */
/*           4545 North Lincoln Boulevard                             */
/*           Oklahoma City, Oklahoma 73105                            */
/*                                                                    */
/*  PHONE:   (405) 525-4896                                           */
/*                                                                    */
/*  DOC:     SLSS error recovery routine.                             */
/*                                                                    */
/*====================================================================*/
CHKWHO: +
  IF &SYSUID = &STR(&LIBADMIN) THEN GOTO INIT
  IF &SYSUID = &STR(&LIBOWNER) THEN GOTO INIT
  SET &SLSSWRN1 = USER '&SYSUID' IS UNAUTHORIZED FOR SELECTED OPTION.
  SET &SLSSWRN2 = PLEASE CONTACT OWNER '&LIBOWNER' OF LIBRARY FOR
  SET &SLSSWRN3 = FURTHER ASSISTANCE.
  SET &SLSSWRN4 =
  SET &SLSSWRN5 =
  SET &SLSSWRNX = PRESS ENTER TO CONTINUE
  ISPEXEC DISPLAY PANEL(SLSS@WRN)
  EXIT
INIT: +
  ISPEXEC DISPLAY PANEL(SLSSRCOV)
  IF &LASTCC = 8 THEN DO
    EXIT
  END DO
SLSS: +
  ISPEXEC TBEND &SLSS
SCAN: +
  ISPEXEC TBEND &SLSS.SCAN
LOCS: +
  ISPEXEC TBEND &SLSS.LOCS
ENQ: +
  ISPEXEC TBEND &SLSS.ENQ
OPENENQ: +
  ISPEXEC TBOPEN &SLSS.ENQ
  SET &RC = &LASTCC
  IF &RC ¬= 0 THEN DO
    SET &SLSSERR1 = UNABLE TO OPEN ENQUEUE FILE TO RESET.
    SET &SLSSERR2 = RETURN CODE FROM 'ISPEXEC TBOPEN' WAS &RC.
    SET &SLSSERR3 = PLEASE CONTACT TECHNICAL SERVICES FOR FURTHER
    SET &SLSSERR4 = ASSISTANCE REGARDING THIS ERROR.
    SET &SLSSERR5 =
    SET &SLSSERRX = PRESS ENTER TO CONTINUE
    ISPEXEC DISPLAY PANEL(SLSS@ERR)
    ISPEXEC TBEND &SLSS.ENQ
    EXIT
  END DO
GETENQ: +
  ISPEXEC TBSKIP &SLSS.ENQ NUMBER(+1)
  SET &RC = &LASTCC
  IF &RC ¬= 0 THEN DO
    SET &SLSSERR1 = UNABLE TO OBTAIN ENQUEUE RECORD.
    SET &SLSSERR2 = RETURN CODE FROM 'ISPEXEC TBSKIP' WAS &RC.
    SET &SLSSERR3 = PLEASE CONTACT TECHNICAL SERVICES FOR FURTHER
    SET &SLSSERR4 = ASSISTANCE REGARDING THIS ERROR.
    SET &SLSSERR5 =
    SET &SLSSERRX = PRESS ENTER TO CONTINUE
    ISPEXEC DISPLAY PANEL(SLSS@ERR)
    ISPEXEC TBEND &SLSS.ENQ
    EXIT
  END DO
  SET &ENQU = &STR(    )
PUTENQ: +
  ISPEXEC TBPUT &SLSS.ENQ
  SET &RC = &LASTCC
  IF &RC ¬= 0 THEN DO
    SET &SLSSERR1 = UNABLE TO RESET ENQUEUE RECORD.
    SET &SLSSERR2 = RETURN CODE FROM 'ISPEXEC TBPUT' WAS &RC.
    SET &SLSSERR3 = PLEASE CONTACT TECHNICAL SERVICES FOR FURTHER
    SET &SLSSERR4 = ASSISTANCE REGARDING THIS ERROR.
    SET &SLSSERR5 =
    SET &SLSSERRX = PRESS ENTER TO CONTINUE
    ISPEXEC DISPLAY PANEL(SLSS@ERR)
    ISPEXEC TBEND &SLSS.ENQ
    EXIT
  END DO
CLOSENQ: +
  ISPEXEC TBCLOSE &SLSS.ENQ
  SET &RC = &LASTCC
  IF &RC ¬= 0 THEN DO
    SET &SLSSERR1 = UNABLE TO CLOSE ENQUEUE FILE PROPERLY.
    SET &SLSSERR2 = RETURN CODE FROM 'ISPEXEC TBPUT' WAS &RC.
    SET &SLSSERR3 = PLEASE CONTACT TECHNICAL SERVICES FOR FURTHER
    SET &SLSSERR4 = ASSISTANCE REGARDING THIS ERROR.
    SET &SLSSERR5 =
    SET &SLSSERRX = PRESS ENTER TO CONTINUE
    ISPEXEC DISPLAY PANEL(SLSS@ERR)
    ISPEXEC TBEND &SLSS.ENQ
    EXIT
  END DO
EXITENQ: +
  EXIT
./       ADD   NAME=SLSSSORT
PROC 0
    ISPEXEC VGET (DEBUG SLSS LIBSRTKY LIBOWNER TECHCONT) SHARED
    IF &DEBUG = DEBUG THEN DO
        CONTROL MSG LIST CONLIST SYMLIST ASIS
    END DO
    ELSE DO
        CONTROL NOMSG NOLIST NOCONLIST NOSYMLIST ASIS
    END DO
/*====================================================================*/
/*  AUTHOR:  Dave Weilert                                             */
/*                                                                    */
/*  ADDRESS: Wilson Foods Corporation                                 */
/*           4545 North Lincoln Boulevard                             */
/*           Oklahoma City, Oklahoma 73105                            */
/*                                                                    */
/*  PHONE:   (405) 525-4896                                           */
/*                                                                    */
/*  DOC:     SLSS Sort Utility.  Unit equal VIO and SYSDA is used in  */
/*           this CLIST.                                              */
/*           The program name SORT is invoked.  If you are unable to  */
/*           invoke the program SORT you must change the program name */
/*           to what ever you are able to invoke.                     */
/*                                                                    */
/*====================================================================*/
DISPLAY: +
    ISPEXEC DISPLAY PANEL(SLSSSORT)
    IF &LASTCC > 4 THEN DO
        EXIT
    END DO
CHKSRT: +
    SET &FIELDS   = &STR( )
    IF &LIBSRTKY = T THEN SET &FIELDS = &STR(NAME1,C,A,SLSID,C,A)
    IF &LIBSRTKY = L THEN SET &FIELDS = &STR(BOOK,C,A,SLSID,C,A)
    IF &LIBSRTKY = 1 OR &LIBSRTKY = 2 OR LIBSRTKY = 3 +
    OR &LIBSRTKY = 4 THEN SET &FIELDS = &STR(SRTKY,C,A,SLSID,C,A)
INIT: +
    FREE F(SORTIN ATTR1)
    ATTR ATTR1 BLKSIZE(6000) LRECL(60) RECFM(F B)
    ALLOC F(SORTIN) UNIT(VIO) NEW SPA(10,10) TR USI(ATTR1)
    OPENFILE SORTIN OUTPUT
    ISPEXEC TBOPEN &SLSS
    SET &RC = &LASTCC
    IF &RC ¬= 0 THEN DO
      SET &SLSSERR1 = Unable to open library &SLSS.
      SET &SLSSERR2 = Return Code from 'ISPEXEC TBOPEN' was &RC.
      SET &SLSSERR3 = Contact &TECHCONT for further information
      SET &SLSSERR4 = regarding this error.
      SET &SLSSERR5 =
      SET &SLSSERRX = Press ENTER to continue
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      FREE F(SORTIN)
      FREE ATTR(ATTR1)
      ISPEXEC TBEND &SLSS
      EXIT
    END DO
SETSRT: +
    ISPEXEC TBSORT &SLSS FIELDS(&FIELDS)
    SET &RC = &LASTCC
    IF &RC ¬= 0 THEN DO
      SET &SLSSERR1 = Unable to set SORT for library.
      SET &SLSSERR2 = Return Code from 'ISPEXEC TBSORT' was &RC..
      SET &SLSSERR3 = Contact &TECHCONT for further information
      SET &SLSSERR4 = regarding this error.
      SET &SLSSERR5 =
      SET &SLSSERRX = Press ENTER to continue
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      FREE F(SORTIN)
      FREE ATTR(ATTR1)
      EXIT
    END DO
SETTOP: +
    ISPEXEC TBTOP &SLSS
SETOUT: +
    ISPEXEC TBSKIP &SLSS NUMBER(+1)
    SET &RC = &LASTCC
    IF &RC = 8 THEN DO
      CLOSFILE SORTIN
      GOTO SORTFILE
    END DO
    IF &RC ¬= 0 THEN DO
      SET &SLSSERR1 = Unable to process library for SORT.
      SET &SLSSERR2 = Return Code from 'ISPEXEC TBSKIP' was &RC..
      SET &SLSSERR3 = Contact &TECHCONT for further information
      SET &SLSSERR4 = regarding this error.
      SET &SLSSERR5 =
      SET &SLSSERRX = Press ENTER to continue
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      FREE F(SORTIN)
      FREE ATTR(ATTR1)
      EXIT
    END DO
    SET &SLSID = &STR(&SLSID          )
    IF &LIBSRTKY = &STR(T) THEN DO
      SET &SORTID = &SUBSTR(1:9,&STR(&SLSID        ))
      SET &SORTIN = &STR(&SORTID&NAME1)
      GOTO WRTREC
    END
    IF &LIBSRTKY = &STR(L) THEN DO
      SET &SORTID = &SUBSTR(1:9,&STR(&SLSID        ))
      SET &SORTIN = &STR(&SORTID&BOOK)
      GOTO WRTREC
    END
    SET &SORTID = &SUBSTR(1:9,&STR(&SLSID          ))
    SET &SORTIN = &STR(&SORTID&SRTKY)
WRTREC: +
    PUTFILE SORTIN
    GOTO SETOUT
SORTFILE: +
    FREE  F(SYSPRINT SYSIN SORTOUT SORTWK01 SORTWK02 SORTMSG SYSOUT)
    ALLOC F(SORTOUT)  +
            UNIT(VIO)   SPACE(5,1) TRACKS NEW USING(ATTR1)
    ALLOC F(SORTMSG)  +
            DUMMY
    ALLOC F(SORTWK01) +
            UNIT(SYSDA) SPACE(1,1) TRACKS
    ALLOC F(SORTWK02) +
            UNIT(SYSDA) SPACE(1,1) TRACKS
    ALLOC F(SYSPRINT) +
            DUMMY
    ALLOC F(SYSOUT)   +
            DUMMY
    ALLOC F(SYSIN)    +
            UNIT(VIO)   SPACE(1,1) TRACKS
SORTSEQ: +
    OPENFILE SYSIN OUTPUT
    SET &SYSIN = &STR( SORT FIELDS=(10,50,CH,A,1,9,CH,A)  )
    PUTFILE SYSIN
    SET &SYSIN = &STR( END )
    PUTFILE SYSIN
    CLOSFILE SYSIN
SORTINVK: +
    SORT                                  /* Invoke SORT)
    IF &LASTCC ¬= 0 THEN DO
      SET &SLSSERR1 = Unable to SORT library.
      SET &SLSSERR2 = Return Code from 'SORT' was &RC..
      SET &SLSSERR3 = Contact &TECHCONT for further information
      SET &SLSSERR4 = regarding this error.
      SET &SLSSERR5 =
      SET &SLSSERRX = Press ENTER to continue.
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      FREE F(SORTIN)
      FREE ATTR(ATTR1)
      EXIT
    END DO
    SET &SLSSPRC1 =
    SET &SLSSPRC2 = &STR(                  SORTING)
    SET &SLSSPRC3 = &STR( )
    SET &SLSSPRC4 = &STR(                LIBRARY &SLSS)
    SET &SLSSPRC5 =
    SET &SLSSPRCX =
    ISPEXEC CONTROL DISPLAY LOCK
    ISPEXEC DISPLAY PANEL(SLSS@PRC)
    FREE  F(SYSIN    SORTWK01 SORTWK02)
TOPEN:  +
    OPENFILE SORTOUT INPUT
TGET: +
    ERROR GOTO CKERR
    GETFILE SORTOUT
    SET &SLSID = &SUBSTR(1:9,&STR(&SORTOUT)
    ERROR OFF
    ISPEXEC TBGET &SLSS
SAVEDATA: +
    SET &SID     = &STR(&SLSID)
    SET &SDASH   = &STR(&SLSSVER)
    SET &SNAME1  = &STR(&NAME1)
    SET &SNAME2  = &STR(&NAME2)
    SET &SNAME3  = &STR(&NAME3)
    SET &SPGM1   = &STR(&PGMID1)
    SET &SPGM2   = &STR(&PGMID2)
    SET &SPGM3   = &STR(&PGMID3)
    SET &SPGM4   = &STR(&PGMID4)
    SET &SKWRDS  = &STR(&KEYWORDS)
    SET &SORDSTS = &STR(&ORDSTS)
    SET &SBOOK   = &STR(&BOOK)
    SET &SUPD    = &STR(&UPD)
    SET &SDORD   = &STR(&DATEORD)
    SET &SDRECV  = &STR(&DATERECV)
    SET &SCRETWH = &STR(&CRETWHO)
    SET &SCRETDA = &STR(&CRETDAT)
    SET &SCRETTI = &STR(&CRETTIM)
    SET &SLSTUWH = &STR(&LSTUWHO)
    SET &SLSTUDA = &STR(&LSTUDAT)
    SET &SLSTUTI = &STR(&LSTUTIM)
    SET &SUPD0   = &STR(&UPD0)
    SET &SUPD1   = &STR(&UPD1)
    SET &SUPD2   = &STR(&UPD2)
    SET &SUPD3   = &STR(&UPD3)
    SET &SUPD4   = &STR(&UPD4)
    SET &SUPD5   = &STR(&UPD5)
    SET &SUPD6   = &STR(&UPD6)
    SET &SUPD7   = &STR(&UPD7)
    SET &SUPD8   = &STR(&UPD8)
    SET &SUPD9   = &STR(&UPD9)
    SET &SUPDA   = &STR(&UPDA)
    SET &SUPDB   = &STR(&UPDB)
    SET &SUPDC   = &STR(&UPDC)
    SET &SUPDD   = &STR(&UPDD)
    SET &SUPDE   = &STR(&UPDE)
    SET &SUPDF   = &STR(&UPDF)
TDELETE: +
    ISPEXEC TBDELETE &SLSS
TBOTTOM: +
    ISPEXEC TBBOTTOM &SLSS
GETDATA: +
    SET &SLSID    = &STR(&SID)
    SET &SLSSVER  = &STR(&SDASH)
    SET &NAME1    = &STR(&SNAME1)
    SET &NAME2    = &STR(&SNAME2)
    SET &NAME3    = &STR(&SNAME3)
    SET &PGMID1   = &STR(&SPGM1)
    SET &PGMID2   = &STR(&SPGM2)
    SET &PGMID3   = &STR(&SPGM3)
    SET &PGMID4   = &STR(&SPGM4)
    SET &KEYWORDS = &STR(&SKWRDS)
    SET &ORDSTS   = &STR(&SORDSTS)
    SET &BOOK     = &STR(&SBOOK)
    SET &UPD      = &STR(&SUPD)
    SET &DATEORD  = &STR(&SDORD)
    SET &DATERECV = &STR(&SDRECV)
    SET &CRETWHO  = &STR(&SCRETWH)
    SET &CRETDAT  = &STR(&SCRETDA)
    SET &CRETTIM  = &STR(&SCRETTI)
    SET &LSTUWHO  = &STR(&SLSTUWH)
    SET &LSTUDAT  = &STR(&SLSTUDA)
    SET &LSTUTIM  = &STR(&SLSTUTI)
    SET &UPD0     = &STR(&SUPD0)
    SET &UPD1     = &STR(&SUPD1)
    SET &UPD2     = &STR(&SUPD2)
    SET &UPD3     = &STR(&SUPD3)
    SET &UPD4     = &STR(&SUPD4)
    SET &UPD5     = &STR(&SUPD5)
    SET &UPD6     = &STR(&SUPD6)
    SET &UPD7     = &STR(&SUPD7)
    SET &UPD8     = &STR(&SUPD8)
    SET &UPD9     = &STR(&SUPD9)
    SET &UPDA     = &STR(&SUPDA)
    SET &UPDB     = &STR(&SUPDB)
    SET &UPDC     = &STR(&SUPDC)
    SET &UPDD     = &STR(&SUPDD)
    SET &UPDE     = &STR(&SUPDE)
    SET &UPDF     = &STR(&SUPDF)
    SET &SRTKY    = &STR( )
    IF &FIELDS = &STR(SRTKY,C,A,SLSID,C,A) THEN DO
      SET &SRTKY = &SUBSTR(&LIBSRTKY:9,&STR(&SLSID         ))
    END DO
    ISPEXEC TBADD   &SLSS ORDER
    GOTO TGET
CKERR: +
    IF &LASTCC = &STR(400) OR &MAXCC = &STR(400) THEN DO
      ERROR OFF
      ISPEXEC TBCLOSE &SLSS
      CLOSFILE SORTOUT
      FREE F(SORTOUT )
      SET &LIBUPDTE = &STR(Y)
      ISPEXEC VPUT (LIBSRTKY LIBUPDTE)
      SET &SLSSPRC1 =
      SET &SLSSPRC2 = &STR(                SORTED COMPLETED)
      SET &SLSSPRC3 =
      SET &SLSSPRC4 =
      SET &SLSSPRC5 =
      SET &SLSSPRCX =PRESS ENTER TO CONTINUE.
      ISPEXEC DISPLAY PANEL(SLSS@PRC)
      EXIT
    END DO
    ELSE DO
      ERROR OFF
      SET &SLSSERR1 = Unable to sucessfully end SORT.
      SET &SLSSERR2 = Input library &SLSS may have been destroyed.
      SET &SLSSERR3 = Contact &TECHCONT for further information
      SET &SLSSERR4 = regarding this error.
      SET &SLSSERR5 =
      SET &SLSSERRX = Press ENTER to continue
      ISPEXEC DISPLAY PANEL(SLSS@ERR)
      ISPEXEC TBCLOSE &SLSS
    EXIT
    END DO
