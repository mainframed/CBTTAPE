* ----------------------------------------------------------------- *
*       CKIEBGEN   -   ORIGINAL PROGRAM WAS BY BALDOMERO CASTILLA   *
*                                                                   *
*                      "PRETTIFIED BY SAM GOLOB"                    *
*                      "IMPROVED BY WARREN WHITFORD"                *
* ----------------------------------------------------------------- *
         GBLC  &LLEV
&LLEV    SETC  '006'
*
CKIEBGEN CSECT
         YREGS
         USING *,R15
         B     EYECATCH
         DC    C'--- '            MAKE EYECATCHER VISIBLE
         DC    C'CKIEBGEN - GENERAL QSAM COPY PGM   - '
         DC    C'&SYSDATE',C'  ',C'&SYSTIME ',C'--- '
         DC    C'LEVEL &LLEV '
         DC    C'--- '            MAKE EYECATCHER VISIBLE
SAVEAREA DC    18F'0'
EYECATCH DS    0H             ENSURE ALIGNMENT
         SAVE  (14,12)
         LR    R5,R1          SAVE PARM POINTER
         LA    R1,SAVEAREA          NEW SAVE-AREA ADDR
         ST    R1,8(,R13)           FORWARD SAVE-AREA CHAIN PTR
         ST    R13,4(,R1)           BACKWARD SAVE-AREA CHAIN PTR
         LR    R13,R1               NEW SAVE AREA ADDRESS
         DROP  R15
* -----                       SET UP TWO BASE REGISTERS
         USING CKIEBGEN,R12,R11,R10
         LR    R12,R15
         LR    R11,R12
         LA    R11,4095(,R12)
         LA    R11,1(,R11)
         LA    R10,4095(,R11)
         LA    R10,1(,R10)
         LR    R1,R5          RESTORE PARM POINTER
         PRINT GEN
***********************************************************************
* GET SYSTEM DATE AND TIME                                            *
***********************************************************************
         STM   R14,R2,SAVE1402
         TIME  DEC,DATETIME,LINKAGE=SYSTEM,DATETYPE=MMDDYYYY
         OI    DATE+4,X'0F'
         UNPK  DATED,DATE
         OI    TIME+3,X'0F'
         UNPK  TIMED,TIME
*                          FILL HEADDATE AREA WITH INFO TO REUSE
         MVC   HEADDATE(2),DATED
         MVI   HEADDATE+2,C'/'
         MVC   HEADDATE+3(2),DATED+2
         MVI   HEADDATE+5,C'/'
         MVC   HEADDATE+6(4),DATED+4
         MVC   HEADDATE+12(2),TIMED
         MVI   HEADDATE+14,C':'
         MVC   HEADDATE+15(2),TIMED+2
         MVI   HEADDATE+17,C':'
         MVC   HEADDATE+18(2),TIMED+4
         MVC   HEADER+54(23),HEADDATE
         MVI   HEADER+38,C'-'
         MVC   HEADER+40(6),=C'LEVEL '
         MVC   HEADER+46(3),=C'&LLEV'
         LM    R14,R2,SAVE1402
***********************************************************************
         OPEN  (SYSPRINT,OUTPUT)
PRLMJFCB DS    0H                   PRELIMINARY READ JFCB TO DETERMINE
*                                   AVAILABLE DCB FIELD INFORMATION
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),HEADER
         PUT   SYSPRINT,OUTLINE
* -------
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),HEADERJ
         PUT   SYSPRINT,OUTLINE
* -------
         OPEN  (SYSUT1,(INPUT))
         OPEN  (SYSUT2,(OUTPUT))
         RDJFCB SYSUT1
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE,MSGDSN
         MVI   OUTLINE+11,C'1'            SYSUT1
         BAL   R7,PRTJFCB
         RDJFCB SYSUT2
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE,MSGDSN
         MVI   OUTLINE+11,C'2'            SYSUT2
         BAL   R7,PRTJFCB
         TM    FLAG1,X'07'
         BZ    GO
         B     ABSTART
* -------
GO       DS    0H
         ZAP   INRCDS,=P'0'
         ZAP   OUTRCDS,=P'0'
LEER     GET   SYSUT1,AREA                MAIN PROCESSING LOOP
         AP    INRCDS,=P'1'
         PUT   SYSUT2,AREA
         AP    OUTRCDS,=P'1'
         B     LEER
* -------
FIN      DS    0H
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),MSG01
         MVC   OUTLINE+19(12),EDPAT
         ED    OUTLINE+19(12),INRCDS+2
         PUT   SYSPRINT,OUTLINE
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),MSG02
         MVC   OUTLINE+19(12),EDPAT
         ED    OUTLINE+19(12),OUTRCDS+2
         PUT   SYSPRINT,OUTLINE
         CLOSE SYSUT1
         CLOSE SYSUT2
* -------
THEEND   DS    0H
         CLOSE SYSPRINT
         L     R13,4(,R13)
         RETURN (14,12),RC=0
         EJECT
ABSTART  DS    0H
         CLOSE SYSUT1
         CLOSE SYSUT2
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),ABENDMSG
         PUT   SYSPRINT,OUTLINE
         B     THEEND
* -- BELOW ---------------  SUBROUTINE TO PRINT JFCB INFORMATION ---- *
*
PRTJFCB  DS    0H
         MVC   OUTLINE+19(44),JFCBDSNM    DSNAME
         PUT   SYSPRINT,OUTLINE
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE,MSGMBR
         MVC   OUTLINE+19(8),JFCBELNM     MEMBER NAME
         PUT   SYSPRINT,OUTLINE
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE,MSGVOLSR           VOLSER
         MVC   OUTLINE+19(6),JFCBVOLS
         MVC   OUTLINE+26(6),JFCBVOLS+6
         MVC   OUTLINE+33(6),JFCBVOLS+12
         MVC   OUTLINE+40(6),JFCBVOLS+18
         MVC   OUTLINE+47(6),JFCBVOLS+24
         PUT   SYSPRINT,OUTLINE
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE,MSGDSORG          DSORG
         MVC   OUTLINE+19(3),=C'IS '
         CLI   JFCDSRG1,JFCORGIS
         BE    PUTDSRG
         MVC   OUTLINE+19(3),=C'ISU'
         CLI   JFCDSRG1,JFCORGIS+JFCORGU
         BE    PUTDSRG
         MVC   OUTLINE+19(3),=C'PS '
         CLI   JFCDSRG1,JFCORGPS
         BE    PUTDSRG
         MVC   OUTLINE+19(3),=C'PSU'
         CLI   JFCDSRG1,JFCORGPS+JFCORGU
         BE    PUTDSRG
         MVC   OUTLINE+19(3),=C'DA '
         CLI   JFCDSRG1,JFCORGDA
         BE    PUTDSRG
         MVC   OUTLINE+19(3),=C'DAU'
         CLI   JFCDSRG1,JFCORGDA+JFCORGU
         BE    PUTDSRG
         MVC   OUTLINE+19(3),=C'PO '
         CLI   JFCDSRG1,JFCORGPO
         BE    PUTDSRG
         MVC   OUTLINE+19(3),=C'POU'
         CLI   JFCDSRG1,JFCORGPO+JFCORGU
         BE    PUTDSRG
         OI    FLAG1,X'01'                FLAG DSORG MISSING
         MVC   OUTLINE+19(15),=C'MISSING OR VSAM'
PUTDSRG  PUT   SYSPRINT,OUTLINE
RECFM    DS    0H
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE,MSGRECFM           RECFM
         LA    R4,OUTLINE+19
         TM    JFCRECFM,JFCUND
         BNO   NOTUND
         MVI   0(R4),C'U'
         OI    FLAG2,X'01'                FLAG RECFM=U
         A     R4,ONE
         B     CHKB
NOTUND   TM    JFCRECFM,JFCFIX
         BNO   NOTFIX
         MVI   0(R4),C'F'
         A     R4,ONE
NOTFIX   TM    JFCRECFM,JFCVAR
         BNO   CHKB
         MVI   0(R4),C'V'
         A     R4,ONE
CHKB     TM    JFCRECFM,JFCRFB
         BNO   NOTBLK
         MVI   0(R4),C'B'
         A     R4,ONE
NOTBLK   TM    JFCRECFM,JFCRFS
         BNO   NOTSCN
         MVI   0(R4),C'S'
         A     R4,ONE
NOTSCN   TM    JFCRECFM,JFCASA
         BNO   NOTASA
         MVI   0(R4),C'A'
         A     R4,ONE
NOTASA   TM    JFCRECFM,JFCMAC
         BNO   PUTRCFM
         MVI   0(R4),C'M'
PUTRCFM  PUT   SYSPRINT,OUTLINE
LRECL    DS    0H
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE,MSGLRECL
         SR    R4,R4
         LH    R4,JFCLRECL
         C     R4,=F'0'
         BNE   LRECPRT
         TM    FLAG2,X'01'                IS RECFM=U?
         BO    LRECPRT                    YES-0 IS LEGITIMATE
         OI    FLAG1,X'04'
         MVC   OUTLINE+19(7),=C'MISSING'
         B     LRECPUT
LRECPRT  DS    0H
         CVD   R4,DECVALUE
         MVC   OUTLINE+19(12),EDPAT       LRECL
         ED    OUTLINE+19(12),DECVALUE+3
LRECPUT  PUT   SYSPRINT,OUTLINE
BLKSIZE  MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE,MSGBLKSZ
         SR    R4,R4
         LH    R4,JFCBLKSI
         C     R4,=F'0'
         BNE   BLKSIZEG                   BLOCK SIZE IS GOOD
         OI    FLAG1,X'02'                FLAG BLKSIZE MISSING
         MVC   OUTLINE+19(15),=C'ZERO OR MISSING'
         B     BLKSZPUT
BLKSIZEG CVD   R4,DECVALUE
         MVC   OUTLINE+19(12),EDPAT       BLKSIZE
         ED    OUTLINE+19(12),DECVALUE+3
BLKSZPUT PUT   SYSPRINT,OUTLINE
         BR    R7
* -- ABOVE ---------------  SUBROUTINE TO PRINT JFCB INFORMATION ---- *
*
* ----- WORK AREA ------ *
FLAG1    DC    X'00'
*              X'01'          DSORG IS MISSING
FLAG2    DC    X'00'
* ----- CONSTANTS ------ *
ONE      DC    F'1'
DECVALUE DS    CL8
EDPAT    DC    X'402020206B2020206B202120'
         DC    C' '
BLANKS   DC    CL133' '
OUTLINE  DS    CL133
HEADER   DC    CL133'1  CKIEBGEN - SEQUENTIAL COPY PROGRAM'
         DC    CL133' '
HEADERJ  DC    CL133'-   JFCB REPORT OF DATASET CHARACTERISTICS '
         DC    CL133' '
MSGDSN   DC    CL133'-     SYSUTX DSN ='
         DC    CL133' '
MSGVOLSR DC    CL133'-        VOLSER  ='
         DC    CL133' '
MSGMBR   DC    CL133'         MEMBER  ='
         DC    CL133' '
MSGDSORG DC    CL133'         DSORG   ='
         DC    CL133' '
MSGBLKSZ DC    CL133'         BLKSIZE ='
         DC    CL133' '
MSGLRECL DC    CL133'         LRECL   ='
         DC    CL133' '
MSGRECFM DC    CL133'         RECFM   ='
         DC    CL133' '
MSG01    DC    CL133'-    RECORDS  IN ='
         DC    CL133' '
MSG02    DC    CL133'     RECORDS OUT ='
         DC    CL133' '
ABENDMSG DC    CL133'-   JFCB IS MISSING VALUES - CORRECT YOUR JCL '
         DC    CL133' '
INRCDS   DC    PL7'0'
OUTRCDS  DC    PL7'0'
         LTORG
SYSUT1   DCB   DDNAME=SYSUT1,MACRF=GM,EODAD=FIN,DSORG=PS,EXLST=JFCB1
SYSUT2   DCB   DDNAME=SYSUT2,MACRF=PM,DSORG=PS,EXLST=JFCB1
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,LRECL=133,RECFM=FBA
         DC    CL8' '
SAVE1402 DS    6F                 ONE MORE FOR SAFETY
TIMED    DS    CL7
DATED    DS    CL9
DATETIME DS    4F
         ORG   DATETIME
TIME     DS    CL4
         DS    CL4
DATE     DS    CL5
         DS    CL3
HEADDATE DC    CL23' '
         DC    CL8' '
         DS    0F
JFCB1    DC    X'87'
         DC    AL3(INFMJFCB)
         IEFJFCBN LIST=YES
AREA     DS    CL32768
         END   CKIEBGEN
