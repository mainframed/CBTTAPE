* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
 * * * * * * *                         * * * * * * * * * * * * * * * *
* * * * * * *   LINKLIST MOD - SOURCE * * * * * * * * * * * * * * * *
 * * * * * * *                         * * * * * * * * * * * * * * * *
* * * * * * *   MVS XA 2.2.0          * * * * * * * * * * * * * * * *
 * * * * * * *                         * * * * * * * * * * * * * * * *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
//S40DA01S JOB (11613109,0402,2,2,,LONG,,,104),PURDY,CLASS=H,MSGCLASS=X
//ASM      EXEC  PGM=IEV90,REGION=2048K,
//           PARM='NODECK,NOOBJ,BUFSIZE(MAX),LINECNT=99'
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR
//SYSUT1   DD  DSN=&&SYSUT1,UNIT=VIO,SPACE=(1700,(600,100))
//SYSUT2   DD  DSN=&&SYSUT2,UNIT=VIO,SPACE=(1700,(300,50))
//SYSUT3   DD  DSN=&&SYSUT3,UNIT=VIO,SPACE=(1700,(300,50))
//SYSPRINT DD  DSN=&&SYSPRINT,UNIT=VIO,SPACE=(141,(500,100)),
//            DISP=(NEW,PASS)
//SYSPUNCH DD  DUMMY
//SYSIN    DD  *
IEAVNP03 CSECT
         SPACE 2
*  THIS IS A MODIFICATION TO MODULE IEAVNP03 TO RECOGNIZE A DATASET
*  IN THE LINKLIST THAT IS EITHER NOT CATALOGUED OR IS CATALOGUED
*  BUT NOT IN THE MVS MASTER CATALOG.
*  THIS MOD IS AT THE 8708 LEVEL OF MVS/SP2.2.0 (JBB2220)
*
*
*  USE:  PLACE THE VOLSER OF THE DATASET IN PARENTHESIS IMMEDIATELY
*        FOLLOWING THE DATA SET NAME
*
*   LINKLIST CARD EXAMPLE:
*
*      SYSTEMS.LINKLIB(RES012)
*
*  THIS CODE IS INTENDED TO BE ASSEMBLED AND THEN PASSED THROUGH
*  AN ASSEMBLER-TO-ZAP TRANSLATOR LIKE THE YALE ASMTOZAP PROGRAM.
*  ASMTOZAP IS FOUND IN FILE 172 AND 173 OF THE CBT TAPE.
*
         SPACE 2
@0       EQU   0    - WORK REGISTER
@4       EQU   4    - SCAN POINTER - ==> LCNAMPTR
@7       EQU   7    - POINTER TO VOLSER
@9       EQU   9    - BASE REGISTER 1
@10      EQU   10   - BASE REGISTER 2
@11      EQU   11   - BASE REGISTER 3
@12      EQU   12   - BASE REGISTER 4
@15      EQU   15   - WORK REGISTER
         SPACE
LNKVOLID EQU   X'2D'   DISPLACEMENT WHERE VOLSER IS STORED
         SPACE 2
*ZAP CARD //S40DA01A JOB (11613109,0402,10,10),JPURDY,
*ZAP CARD //  MSGCLASS=X,CLASS=H,NOTIFY=S40DA01
*ZAP CARD //S1       EXEC PGM=GIMSMP,REGION=4096K
*ZAP CARD //SYSPRINT DD SYSOUT=*
*ZAP CARD //SMPCSI   DD DSN=MVSSMP.GLOBAL.CSI,DISP=SHR
*ZAP CARD //SMPPTS   DD DSN=SYS1.SMPPTS,DISP=SHR
*ZAP CARD //SMPPTFIN DD DSN=SYS2.INSTLIB(MSX0500),DISP=SHR
*ZAP CARD //SMPLOG   DD DSN=SYS1.SMPGLOG,DISP=SHR
*ZAP CARD ++USERMOD(MSX0500).
*ZAP CARD ++VER(Z038) FMID(JBB2220)
*ZAP CARD ++ZAP(IEAVNP03).
*ZAP CARD   NAME         IEAVNP03 IEAVNP03
*ZAP CARD   IDRDATA      MSX0500
         SPACE 2
         ORG   IEAVNP03+X'2C'        ORG TO BASE REGISTER ADDRESS
@PSTART  EQU   *
         USING @PSTART,@9            SET BASE REGISTERS 9, 10, 11 & 12
         USING @PSTART+4095,@10
         USING @PSTART+8190,@11
         USING @PSTART+12285,@12
         SPACE
         ORG   IEAVNP03+X'2218'      SET ADDRESS OF SCANREG
SCANREG  EQU   *                     INTERNAL LIST OF LNKLST DATA SETS
         ORG   IEAVNP03+X'22E4'      SET ADDRESS OF LCNAMPTR
LCNAMPTR EQU   *                     POINTER TO NEXT NAME TO PROCESS
         SPACE
*ZAP START VER
         ORG   IEAVNP03+X'EFE'       SET ADDRESS OF HOOK
NP3LCLOC L     @4,LCNAMPTR           VERIFY CURRENT INSTRUCTION
         SPACE
         ORG   IEAVNP03+X'F2E'       SET ADDRESS OF RETURN BRANCH
NP3LCLXT LA    @0,53                 VERIFY CURRENT INSTRUCTION
         SPACE
*ZAP START REP
         ORG   IEAVNP03+X'EFE'       SET ADDRESS OF HOOK
         B     PATCHLOC              BRANCH TO MOD
         SPACE
         ORG   IEAVNP03+X'2F8A'      SET ADDRESS OF PATCH AREA
PATCHLOC LA    @15,53                LOAD # BYTES TO SCAN
         L     @4,SCANREG            GET ADDRESS OF NEXT PARM TO SCAN
         LA    @0,1                  LOAD CONSTANT 1 INTO REG 0
SCANLPAR EQU *
         CLI   0(@4),C'('            LOOK FOR OPEN PARENTHESIS
         BE    FOUND                 ...  FOUND
         ALR   @4,@0            ADD ONE TO ADDRESS - NEXT BYTE
         BCT   @15,SCANLPAR          BRANCH BACK TO LOOK AGAIN
         B     NOTFOUND              ...  NO PARENTHESIS - MUST BE
*                                         IN MASTER CATALOGUE
         SPACE
FOUND    CLI   7(@4),C')'            VERIFY CLOSING PARENTHESIS
         BE    MOVEVOL               ...  OK GO MOVE VOLSER
NOTFOUND L     @4,LCNAMPTR           REPLACED INSTRUCTION - SETUP
*                                    FOR MVS LOCATE IN MASTER CAT
         B     NP3LCLOC+4            GO DO LOCATE
         SPACE
MOVEVOL  EQU   *
         L     @7,SCANREG            LOAD POINTER - WHERE TO PUT VOLSER
         MVC   LNKVOLID(6,@7),1(@4)  MOVE VOLSER TO LNKVOLID
         MVI   0(@4),C' '            CLEAR OUT VOLSER AND PARENDS
         MVC   1(7,@4),0(@4)         ON LNKLST CARD
         SR    @15,@15               CLEAR REGISTER 15
         B     NP3LCLXT              GO SETUP FOR PROCESS OF NEXT CARD
         SPACE
*ZAP CARD //SMPCNTL DD *
*ZAP CARD SET BOUNDARY(GLOBAL).
*ZAP CARD RECIEVE S(MSX0500) SYSMODS.
*ZAP CARD SET BOUNDARY(MVSTZN).
*ZAP CARD APPLY S(MSX0500) CHECK.
*ZAP CARD /*
*ZAP CARD //
*ZAP END
         END
//PRINT    EXEC PGM=IEBGENER
//SYSIN    DD  DUMMY
//SYSPRINT DD  SYSOUT=*
//SYSUT1   DD  DSN=&&SYSPRINT,DISP=(OLD,PASS)
//SYSUT2   DD  SYSOUT=*
//TOZAP    EXEC PGM=ASMTOZAP
//STEPLIB  DD  DSN=SYSTEMS.LINKLIB,DISP=SHR
//SYSPRINT DD  SYSOUT=*
//IN       DD  DSN=&&SYSPRINT,DISP=(OLD,DELETE)
//OUT      DD  DSN=S40DA01.USER.DATA(LNKLSTZP),DISP=SHR
