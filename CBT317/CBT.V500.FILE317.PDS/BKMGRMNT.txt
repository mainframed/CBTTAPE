/******************************* REXX ********************************/
/*                                                                   */
/*   Maintain BookManager Data Sets                                  */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/* Program Name - BKMGRMNT                                           */
/*                                                                   */
/* Author       - Tim Henness                                        */
/*                                                                   */
/* Date Written - January 4, 1995                                    */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/* Function -                                                        */
/*                                                                   */
/*    This REXX EXEC will indicate the maintainance required for     */
/*    BookManager data sets.  The following listings are produced:   */
/*                                                                   */
/*       1.  List of bookshelves not in the bookshelf list.          */
/*                                                                   */
/*       2.  List of bookshelf indexes not used by any bookshelf.    */
/*                                                                   */
/*       3.  List of books not in any bookshelf.                     */
/*                                                                   */
/*       4.  List of books to be replaced in special bookshelves.    */
/*                                                                   */
/*    Also, a REXX EXEC can be written that, when executed, will     */
/*    delete the bookshelves and books indicated.                    */
/*                                                                   */
/* Parameters -                                                      */
/*                                                                   */
/*    This EXEC is called with the following parameter format:       */
/*                                                                   */
/*       BKMGRMNT  cntlfile                                          */
/*                                                                   */
/*    where:                                                         */
/*                                                                   */
/*       cntlfile - is the data set name of the control file.        */
/*                                                                   */
/* Control File Formats -                                            */
/*                                                                   */
/*    The control file consists of control records that define the   */
/*    data to be processed.                                          */
/*                                                                   */
/*    The bookshelf lists, bookshelves, bookshelf indexes, and       */
/*    books are accessed from the catalog.  The cataloged data set   */
/*    names are defined by the following control statements:         */
/*                                                                   */
/*       CATALOG BKLSHELF=mask                                       */
/*       CATALOG BKSHELF=mask                                        */
/*       CATALOG BKINDEX=mask                                        */
/*       CATALOG BOOK=mask                                           */
/*                                                                   */
/*    where:                                                         */
/*                                                                   */
/*       mask - is a mask defining the data set names.  A mask       */
/*              consists of a high-level qualifier, followed by one  */
/*              or more qualifiers.  A percent sign ('%') can be     */
/*              used to match any single character.  An asterisk     */
/*              ('*') can be used to match multiple characters in a  */
/*              single qualifier.  A double asterisk ('**') can be   */
/*              used to match zero or more qalifiers.                */
/*                                                                   */
/*    To exclude specific data sets, use the following control       */
/*    statements:                                                    */
/*                                                                   */
/*       EXCLUDE BKLSHELF=dsn                                        */
/*       EXCLUDE BKSHELF=dsn                                         */
/*       EXCLUDE BKINDEX=dsn                                         */
/*       EXCLUDE BOOK=dsn                                            */
/*                                                                   */
/*    where:                                                         */
/*                                                                   */
/*       dsn - is the data set name to be excluded.                  */
/*                                                                   */
/*    'Special' bookshelves are bookshelves that are created         */
/*    locally, using books from other bookshelves.  For example,     */
/*    the 'MESSAGES' bookshelf contains all of the books containing  */
/*    messages and codes from the other bookshelves.  The special    */
/*    bookshelves are defined by the following control statement:    */
/*                                                                   */
/*       SPECIAL BKSHELF=dsn                                         */
/*                                                                   */
/*    where:                                                         */
/*                                                                   */
/*       dsn - is the data set name of the special bookshelf.        */
/*                                                                   */
/*    This EXEC will write several reports.  The report data set     */
/*    is defined from the following control statements:              */
/*                                                                   */
/*       REPORT DSN=dsn                                              */
/*       REPORT SYSOUT=class                                         */
/*       REPORT TERM                                                 */
/*       REPORT ALLOC=parms                                          */
/*       REPORT OUTDES=outdes                                        */
/*       REPORT DEPTH=depth                                          */
/*                                                                   */
/*    where:                                                         */
/*                                                                   */
/*       dsn    - is the name of the data set to which the report    */
/*                is to be written.                                  */
/*                                                                   */
/*       class  - is the sysout class to which the report is to be   */
/*                written.                                           */
/*                                                                   */
/*       TERM   - specifies that the report is to be written to the  */
/*                TSO terminal.                                      */
/*                                                                   */
/*       parms  - specifies the parameters to be included on the     */
/*                TSO ALLOCATE for the report data set.              */
/*                                                                   */
/*       outdes - specifies the parameters to be used for the TSO    */
/*                OUTDES command for the report data set.  An        */
/*                'OUTDES(BKMGROUT)' parameter will be included in   */
/*                the TSO ALLOCATE command for the report data set.  */
/*                This parameter can be specified only if the        */
/*                'REPORT SYSOUT' parameter is specified.            */
/*                                                                   */
/*       depth  - is the number of lines to be printed on a page.    */
/*                If this parameter is omitted, the default is 60.   */
/*                                                                   */
/*    This EXEC will write another REXX EXEC that to delete the      */
/*    unreferenced bookshelves, bookshelf indexes, and books.        */
/*    The REXX EXEC is written to a data set defined by the          */
/*    following control statements:                                  */
/*                                                                   */
/*       EXEC DSN=dsn                                                */
/*       EXEC ALLOC=parms                                            */
/*                                                                   */
/*    where:                                                         */
/*                                                                   */
/*       dsn   - is the data set name of the REXX EXEC.  If this     */
/*               parameter is not specified, the EXEC will not be    */
/*               written.                                            */
/*                                                                   */
/*       parms - specifies the allocation parameters to be included  */
/*               on the TSO ALLOCATE for the REXX EXEC data set.     */
/*               If this parameter is not specified, the data set    */
/*               must already exist.                                 */
/*                                                                   */
/*********************************************************************/

   /* TRACE R */

   x = TIME("R")

   SAY "BKMGRMNT - Maintain BookManager Data Sets"

/*   Set host environment to TSO                                     */

   ADDRESS TSO

/*   Initialize variables                                            */

   cat_bslist_mask_num = 0             /* Data Set List mask for     */
   cat_bslist_mask. = ""               /* Bookshelf List data sets   */

   cat_shelf_mask_num = 0              /* Data Set List mask for     */
   cat_shelf_mask. = ""                /* Bookshelf data sets        */

   cat_index_mask_num = 0              /* Data Set List mask for     */
   cat_index_mask. = ""                /* Bookshelf Index data sets  */

   cat_book_mask_num = 0               /* Data Set List mask for     */
   cat_book_mask. = ""                 /* Book data sets             */

   excl_bslist_num = 0                 /* Bookshelf List Data Sets   */
   excl_bslist_dsn. = ""               /* to be excluded             */

   excl_shelf_num = 0                  /* Bookshelf Data Sets to be  */
   excl_shelf_dsn. = ""                /* excluded                   */

   excl_index_num = 0                  /* Bookshelf Index Data Sets  */
   excl_index_dsn. = ""                /* to be excluded             */

   excl_book_num = 0                   /* Book Data Sets to be       */
   excl_book_dsn. = ""                 /* excluded                   */

   special_shelf_num = 0               /* Special Bookshelves        */
   special_shelf_dsn. = ""

   report_dest = ""                    /* Report Output              */
   report_dsn = ""
   report_sysout = ""
   report_alloc = ""
   report_outdes = ""
   report_depth = 60
   report_pageno = 0
   report_lineno = 0
   report_title = ""
   report_head. = ""
   report_head.0 = 0
   report_num = 0
   report_line. = ""

   exec_dsn = ""                       /* REXX EXEC Output           */
   exec_alloc = ""
   exec_num = 0
   exec_line. = ""

   cat_bslist_num = 0                  /* Cataloged Bookshelf List   */
   cat_bslist_dsn. = ""                /* Data Sets                  */

   cat_shelf_num = 0                   /* Cataloged Bookshelf Data   */
   cat_shelf_dsn. = ""                 /* Sets                       */

   cat_index_num = 0                   /* Cataloged Bookshelf Index  */
   cat_index_dsn. = ""                 /* Data Sets                  */

   cat_book_num = 0                    /* Cataloged Book Data Sets   */
   cat_book_dsn. = ""

   shelf_num = 0                       /* Referenced Bookshelf Table */
   shelf_dsn. = ""
   shelf_name. = ""
   shelf_desc. = ""
   shelf_special. = 0
   shelf_index_dsn. = ""
   shelf_index_name. = ""
   shelf_book_num. = 0
   shelf_book_dsn. = ""
   shelf_book_name. = ""

   index_num = 0                       /* Referenced Bookshelf Index */
   index_dsn. = ""                     /* Table                      */
   index_name. = ""
   index_shelf_num. = 0
   index_shelf_dsn. = ""
   index_shelf_name. = ""

   book_num = 0                        /* Referenced Book Table      */
   book_dsn. = ""
   book_name. = ""
   book_title. = ""
   book_docnum. = ""
   book_shelf_num. = 0
   book_shelf_dsn. = ""
   book_shelf_name. = ""
   book_shelf_special. = 0

/*   Get arguments                                                   */

   ARG cntlfile extra

   IF extra ¬= "" THEN DO
      SAY "Extraneous parameters specified, execution terminated"
      EXIT 16
      END

/*   Get Control Table                                               */

   SAY ET() "- Get control table"

   CALL GETCNTL(cntlfile)

/*   Initialize REXX EXEC                                            */

   execline = "/* REXX */"
   CALL EXECLINE execline

/*   Get Cataloged Data Set Lists                                    */

   SAY ET() "- Get cataloged bookshelf lists"
   CALL CATBSLST

   SAY ET() "- Get cataloged bookshelves"
   CALL CATSHELF

   SAY ET() "- Get cataloged bookshelf indexes"
   CALL CATINDEX

   SAY ET() "- Get cataloged books"
   CALL CATBOOK

/*   Build Bookshelf Table                                           */

   SAY ET() "- Build bookshelf table"
   DO list# = 1 to cat_bslist_num
      CALL GETBKLST(cat_bslist_dsn.list#)
      END

/*   Build Index and Book Tables                                     */

   SAY ET() "- Build index and book tables"
   DO shelf# = 1 to shelf_num
      CALL GETSHELF(shelf#)
      END

/*   Create Unreferenced Bookshelf List                              */

   SAY ET() "- Create Unreferenced Bookshelf List"
   CALL REFSHELF

/*   Create Unreferenced Bookshelf Index List                        */

   SAY ET() "- Create Unreferenced Bookshelf Index List"
   CALL REFINDEX

/*   Create Unreferenced Book List                                   */

   SAY ET() "- Create Unreferenced Book List"
   CALL REFBOOK

/*   Create Special Bookshelf Updated Books List                     */

   SAY ET() "- Create Special Bookshelf Updated Book List"
   DO shelf# = 1 to special_shelf_num
      CALL UPDSPEC(special_shelf_dsn.shelf#)
      END

/*   Complete Processing                                             */

   SAY ET() "- Write Reports"
   CALL RPTEND

   SAY ET() "- Write REXX EXEC"
   CALL EXECEND

   SAY ET() "- Processing Complete"

   EXIT

/*********************************************************************/
/*                                                                   */
/*   Get Control Table                                               */
/*                                                                   */
/*********************************************************************/

GETCNTL: PROCEDURE EXPOSE cat_bslist_mask_num cat_bslist_mask. ,
                          cat_shelf_mask_num cat_shelf_mask. ,
                          cat_index_mask_num cat_index_mask. ,
                          cat_book_mask_num cat_book_mask. ,
                          excl_bslist_num excl_bslist_dsn. ,
                          excl_shelf_num excl_shelf_dsn. ,
                          excl_index_num excl_index_dsn. ,
                          excl_book_num excl_book_dsn. ,
                          special_shelf_num special_shelf_dsn. ,
                          report_dest report_dsn report_sysout ,
                          report_alloc report_outdes report_depth ,
                          exec_dsn exec_alloc

   ARG cntlfile

/*   Initialize stem variables                                       */

   bkmcntl. = ""

/*   Read control file                                               */

   "ALLOC FILE(BKMCNTL) REUSE SHR ",
         "DATASET('"cntlfile"')"
   IF rc ¬= 0 THEN DO
      SAY "ALLOC error for control file '"cntlfile"' - RC="rc
      EXIT 8
      END

   "EXECIO * DISKR BKMCNTL ( STEM bkmcntl. OPEN FINIS )"
   IF rc ¬= 0 THEN DO
      SAY "EXECIO error for control file '"cntlfile"' - RC="rc
      EXIT 8
      END

   "FREE FILE(BKMCNTL)"

/*   Parse control file, Build control table                         */

   cntlerr = 0

   DO cntl#=1 TO bkmcntl.0

      cntlline = bkmcntl.cntl#

      PARSE VAR cntlline stmt type "=" data
      data = STRIP(data)

      SELECT

         WHEN stmt = "CATALOG" THEN

            SELECT

               WHEN type = "BKLSHELF" THEN DO
                  cat_bslist_mask_num = cat_bslist_mask_num + 1
                  cat_bslist_mask.cat_bslist_mask_num = data
                  END

               WHEN type = "BKSHELF" THEN DO
                  cat_shelf_mask_num = cat_shelf_mask_num + 1
                  cat_shelf_mask.cat_shelf_mask_num = data
                  END

               WHEN type = "BKINDEX" THEN DO
                  cat_index_mask_num = cat_index_mask_num + 1
                  cat_index_mask.cat_index_mask_num = data
                  END

               WHEN type = "BOOK" THEN DO
                  cat_book_mask_num = cat_book_mask_num + 1
                  cat_book_mask.cat_book_mask_num = data
                  END

               OTHERWISE DO
                  SAY "Invalid parameter type in control data -" cntlline
                  cntlerr = 1
                  END

               END

         WHEN stmt = "EXCLUDE" THEN

            SELECT

               WHEN type = "BKLSHELF" THEN DO
                  excl_bslist_num = excl_bslist_num + 1
                  excl_bslist_dsn.excl_bslist_num = data
                  END

               WHEN type = "BKSHELF" THEN DO
                  excl_shelf_num = excl_shelf_num + 1
                  excl_shelf_dsn.excl_shelf_num = data
                  END

               WHEN type = "BKINDEX" THEN DO
                  excl_index_num = excl_index_num + 1
                  excl_index_dsn.excl_index_num = data
                  END

               WHEN type = "BOOK" THEN DO
                  excl_book_num = excl_book_num + 1
                  excl_book_dsn.excl_book_num = data
                  END

               OTHERWISE DO
                  SAY "Invalid parameter type in control data -" cntlline
                  cntlerr = 1
                  END

               END

         WHEN stmt = "SPECIAL" THEN

            SELECT

               WHEN type = "BKSHELF" THEN DO
                  special_shelf_num = special_shelf_num + 1
                  special_shelf_dsn.special_shelf_num = data
                  END

               OTHERWISE DO
                  SAY "Invalid parameter type in control data -" cntlline
                  cntlerr = 1
                  END

               END

         WHEN stmt = "REPORT" THEN

            SELECT

               WHEN type = "DSN" THEN DO
                  report_dest = "DATASET"
                  report_dsn = data
                  END

               WHEN type = "SYSOUT" THEN DO
                  report_dest = "SYSOUT"
                  report_sysout = data
                  END

               WHEN type = "TERM" THEN DO
                  report_dest = "TERMINAL"
                  IF data ¬= "" THEN DO
                     SAY "Extraneous control data -" cntlline
                     cntlerr = 1
                     END
                  END

               WHEN type = "ALLOC" THEN DO
                  If report_alloc = "" THEN
                     report_alloc = data
                  ELSE
                     report_alloc = report_alloc || " " || data
                  END

               WHEN type = "OUTDES" THEN DO
                  If report_outdes = "" THEN
                     report_outdes = data
                  ELSE
                     report_outdes = report_outdes || " " || data
                  END

               WHEN type = "DEPTH" THEN DO
                  report_depth = data
                  END

               OTHERWISE DO
                  SAY "Invalid parameter type in control data -" cntlline
                  cntlerr = 1
                  END

               END

         WHEN stmt = "EXEC" THEN

            SELECT

               WHEN type = "DSN" THEN DO
                  exec_dsn = data
                  END

               WHEN type = "ALLOC" THEN DO
                  If exec_alloc = "" THEN
                     exec_alloc = data
                  ELSE
                     exec_alloc = exec_alloc || " " || data
                  END

               OTHERWISE DO
                  SAY "Invalid parameter type in control data -" cntlline
                  cntlerr = 1
                  END

               END

         OTHERWISE DO
            SAY "Invalid statement in control data -" cntlline
            cntlerr = 1
            END

         END

      END

   IF cntlerr THEN DO
      SAY "Errors in control file, execution terminated"
      EXIT 16
      END

   RETURN

/*********************************************************************/
/*                                                                   */
/*   Get Cataloged Bookshelf List Data Set Names                     */
/*                                                                   */
/*********************************************************************/

CATBSLST: PROCEDURE EXPOSE cat_bslist_mask_num cat_bslist_mask. ,
                           cat_bslist_num cat_bslist_dsn. ,
                           excl_bslist_num excl_bslist_dsn.

/*   Process each mask                                               */

   DO mask# = 1 TO cat_bslist_mask_num

      cat_dsn. = ""
      cat_dsn.0 = 0

      CALL CATLIST(cat_bslist_mask.mask#)

/*   Process each data set                                           */

      DO dsn# = 1 TO cat_dsn.0

/*   Check for data set excluded                                     */

         DO i = 1 TO excl_bslist_num UNTIL excl_bslist_dsn.i = cat_dsn.dsn#
            END
         IF i <= excl_bslist_num THEN
            ITERATE

/*   Check for data set already in list                              */

         IF mask# > 1 THEN DO
            traceopt = TRACE("OFF")
            DO i = 1 TO cat_bslist_num UNTIL cat_bslist_dsn.i = cat_dsn.dsn#
               END
            x = TRACE(traceopt)
            IF i <= cat_bslist_num THEN
               ITERATE
            END

/*   Add data set to list                                            */

         cat_bslist_num = cat_bslist_num + 1
         cat_bslist_dsn.cat_bslist_num = cat_dsn.dsn#

         END

      END

   RETURN

/*********************************************************************/
/*                                                                   */
/*   Get Cataloged Bookshelf Data Set Names                          */
/*                                                                   */
/*********************************************************************/

CATSHELF: PROCEDURE EXPOSE cat_shelf_mask_num cat_shelf_mask. ,
                           cat_shelf_num cat_shelf_dsn. ,
                           excl_shelf_num excl_shelf_dsn.

/*   Process each mask                                               */

   DO mask# = 1 TO cat_shelf_mask_num

      cat_dsn. = ""
      cat_dsn.0 = 0

      CALL CATLIST(cat_shelf_mask.mask#)

/*   Process each data set                                           */

      DO dsn# = 1 TO cat_dsn.0

/*   Check for data set excluded                                     */

         DO i = 1 TO excl_shelf_num UNTIL excl_shelf_dsn.i = cat_dsn.dsn#
            END
         IF i <= excl_shelf_num THEN
            ITERATE

/*   Check for data set already in list                              */

         IF mask# > 1 THEN DO
            traceopt = TRACE("OFF")
            DO i = 1 TO cat_shelf_num UNTIL cat_shelf_dsn.i = cat_dsn.dsn#
               END
            x = TRACE(traceopt)
            IF i <= cat_shelf_num THEN
               ITERATE
            END

/*   Add data set to list                                            */

         cat_shelf_num = cat_shelf_num + 1
         cat_shelf_dsn.cat_shelf_num = cat_dsn.dsn#

         END

      END

   RETURN

/*********************************************************************/
/*                                                                   */
/*   Get Cataloged Bookshelf Index Data Set Names                    */
/*                                                                   */
/*********************************************************************/

CATINDEX: PROCEDURE EXPOSE cat_index_mask_num cat_index_mask. ,
                           cat_index_num cat_index_dsn. ,
                           excl_index_num excl_index_dsn.

/*   Process each mask                                               */

   DO mask# = 1 TO cat_index_mask_num

      cat_dsn. = ""
      cat_dsn.0 = 0

      CALL CATLIST(cat_index_mask.mask#)

/*   Process each data set                                           */

      DO dsn# = 1 TO cat_dsn.0

/*   Check for data set excluded                                     */

         DO i = 1 TO excl_index_num UNTIL excl_index_dsn.i = cat_dsn.dsn#
            END
         IF i <= excl_index_num THEN
            ITERATE

/*   Check for data set already in list                              */

         IF mask# > 1 THEN DO
            traceopt = TRACE("OFF")
            DO i = 1 TO cat_index_num UNTIL cat_index_dsn.i = cat_dsn.dsn#
               END
            x = TRACE(traceopt)
            IF i <= cat_index_num THEN
               ITERATE
            END

/*   Add data set to list                                            */

         cat_index_num = cat_index_num + 1
         cat_index_dsn.cat_index_num = cat_dsn.dsn#

         END

      END

   RETURN

/*********************************************************************/
/*                                                                   */
/*   Get Cataloged Book Data Set Names                               */
/*                                                                   */
/*********************************************************************/

CATBOOK: PROCEDURE EXPOSE cat_book_mask_num cat_book_mask. ,
                          cat_book_num cat_book_dsn. ,
                          excl_book_num excl_book_dsn.

/*   Process each mask                                               */

   DO mask# = 1 TO cat_book_mask_num

      cat_dsn. = ""
      cat_dsn.0 = 0

      CALL CATLIST(cat_book_mask.mask#)

/*   Process each data set                                           */

      DO dsn# = 1 TO cat_dsn.0

/*   Check for data set excluded                                     */

         DO i = 1 TO excl_book_num UNTIL excl_book_dsn.i = cat_dsn.dsn#
            END
         IF i <= excl_book_num THEN
            ITERATE

/*   Check for data set already in list                              */

         IF mask# > 1 THEN DO
            traceopt = TRACE("OFF")
            DO i = 1 TO cat_book_num UNTIL cat_book_dsn.i = cat_dsn.dsn#
               END
            x = TRACE(traceopt)
            IF i <= cat_book_num THEN
               ITERATE
            END

/*   Add data set to list                                            */

         cat_book_num = cat_book_num + 1
         cat_book_dsn.cat_book_num = cat_dsn.dsn#

         END

      END

   RETURN

/*********************************************************************/
/*                                                                   */
/*   Get List of Cataloged Data Set Names                            */
/*                                                                   */
/*********************************************************************/

CATLIST: PROCEDURE EXPOSE cat_dsn.

   ARG mask

/*   Set command environment to ISPEXEC                              */

   cmdenv = ADDRESS()

   "SUBCOM ISPEXEC"
   IF rc ¬= 0 THEN DO
      SAY "ISPF must be active to run this exec"
      EXIT 8
      END

   ADDRESS ISPEXEC

/*   Initialize data set list-ID                                     */

   "LMDINIT LISTID(listid) LEVEL("mask")"
   IF rc ¬= 0 THEN DO
      SAY "LMDINIT error for '"mask"' - RC =" rc
      EXIT 16
      END

   dsn = ""

/*   Add each data set to list                                       */

   DO WHILE rc = 0

      "LMDLIST LISTID("listid") OPTION(LIST) DATASET(dsn)"
      IF rc = 4 | rc = 8 THEN
         LEAVE
      IF rc ¬= 0 THEN DO
         SAY "LMDLIST error for '"mask"' - RC =" rc
         EXIT 16
         END

      i = cat_dsn.0 + 1
      cat_dsn.0 = i
      cat_dsn.i = dsn

      END

/*   Free data set list-ID                                           */

   "LMDFREE LISTID("listid")"
   IF rc ¬= 0 THEN DO
      SAY "LMDFREE error for '"mask"' - RC =" rc
      EXIT 16
      END

/*   Reset caller's command environment                              */

   ADDRESS VALUE cmdenv

   RETURN

/*********************************************************************/
/*                                                                   */
/*   Get Bookshelf List                                              */
/*                                                                   */
/*********************************************************************/

GETBKLST: PROCEDURE EXPOSE shelf_num shelf_dsn. ,
                           shelf_name. shelf_desc. shelf_special. ,
                           special_shelf_num special_shelf_dsn.

   ARG bklist_dsn

/*   Initialize stem variables                                       */

   bklist_rec. = ""

/*   Read bookshelf list file                                        */

   "ALLOC FILE(BKLSHELF) REUSE SHR ",
         "DATASET('"bklist_dsn"')"
   IF rc ¬= 0 THEN DO
      SAY "ALLOC error for bookshelf list data set '"bklist_dsn"' - RC="rc
      EXIT 8
      END

   "EXECIO * DISKR BKLSHELF ( STEM bklist_rec. OPEN FINIS )"
   IF rc ¬= 0 THEN DO
      SAY "EXECIO error for bookshelf list data set '"bklist_dsn"' - RC="rc
      EXIT 8
      END

   "FREE FILE(BKLSHELF)"

/*   Process each bookshelf list entry                               */

   DO rec#=1 TO bklist_rec.0

      record = bklist_rec.rec#

/*   Parse bookshelf list entry                                      */

      PARSE VAR record keyword name dsn desc

      IF keyword ¬= "SHELF" THEN DO
         SAY "Error in Bookshelf List '"bklist_dsn"'",
             "Invalid input record -" record
         ITERATE
         END

/*   Find existing Bookshelf Table entry                             */

      traceopt = TRACE("OFF")
      DO shelf# = 1 TO shelf_num UNTIL dsn = shelf_dsn.shelf#
         END
      x = TRACE(traceopt)

      IF shelf# > shelf_num THEN DO

/*   Create new Bookshelf Table entry                                */

         shelf_num = shelf_num + 1
         shelf_dsn.shelf_num = dsn
         shelf_name.shelf_num = name
         shelf_desc.shelf_num = desc

/*   Check for special bookshelf                                     */

         DO i = 1 TO special_shelf_num ,
               UNTIL special_shelf_dsn.i = shelf_dsn.shelf_num
            END
         IF i <= special_shelf_num THEN
            shelf_special.shelf_num = 1

         END

      END

   RETURN

/*********************************************************************/
/*                                                                   */
/*   Get Bookshelf                                                   */
/*                                                                   */
/*********************************************************************/
/*                                                                   */
/*   Warning - Although The parsing for this routine is somewhat     */
/*             dependant on the order of the Bookshelf records,      */
/*             no checking for this order is done.  In particular,   */
/*             the BKMDSN keyword for each book must follow all      */
/*             other keywords for which data is used for the book.   */
/*                                                                   */
/*********************************************************************/

GETSHELF: PROCEDURE EXPOSE shelf_num shelf_dsn. ,
                           shelf_name. shelf_desc. shelf_special. ,
                           shelf_index_dsn. shelf_index_name. ,
                           shelf_book_num. shelf_book_dsn. ,
                           shelf_book_name. ,
                           index_num index_dsn. ,
                           index_name. ,
                           index_shelf_num. index_shelf_dsn. ,
                           index_shelf_name. ,
                           book_num book_dsn. ,
                           book_name. book_title. book_docnum. ,
                           book_shelf_num. book_shelf_dsn. ,
                           book_shelf_name. book_shelf_special.

   ARG shelf#

/*   Initialize stem variables                                       */

   shelf_rec. = ""

   dsn = ""
   name = ""
   title = ""
   docnum = ""

/*   Read bookshelf file                                             */

   "ALLOC FILE(BKSHELF) REUSE SHR ",
         "DATASET('"shelf_dsn.shelf#"')"
   IF rc ¬= 0 THEN DO
      SAY "ALLOC error for bookshelf data set '"shelf_dsn.shelf#"' - RC="rc
      EXIT 8
      END

   "EXECIO * DISKR BKSHELF ( STEM shelf_rec. OPEN FINIS )"
   IF rc ¬= 0 THEN DO
      SAY "EXECIO error for bookshelf data set '"shelf_dsn.shelf#"' - RC="rc
      EXIT 8
      END

   "FREE FILE(BKSHELF)"

/*   Process each bookshelf entry                                    */

   DO rec#=1 TO shelf_rec.0

      record = shelf_rec.rec#

/*   Parse bookshelf entry                                           */

      keyword = SUBSTR(record,1,2)
      IF keyword = "SH" | keyword = "ST" THEN
         data = STRIP(SUBSTR(record,3))
      ELSE
         PARSE VAR record keyword "=" data

      SELECT
         WHEN keyword = "SH" THEN
            docnum = data

         WHEN keyword = "ST" THEN
            title = data

         WHEN keyword = "BKSHELF" THEN
            IF data ¬= shelf_name.shelf# THEN
               SAY "Bookshelf name does not match bookshelf list -" ,
                   shelf_dsn.shelf#

         WHEN keyword = "BKSMDSN" THEN
            IF data ¬= shelf_dsn.shelf# THEN
               SAY "Bookshelf data set name does not match bookshelf list -" ,
                   shelf_dsn.shelf#

         WHEN keyword = "BKSTITLE" THEN
            IF data ¬= shelf_desc.shelf# THEN
               SAY "Bookshelf title does not match bookshelf list -" ,
                   shelf_dsn.shelf#

         WHEN keyword = "BKSINDEX" THEN
            shelf_index_name.shelf# = data

         WHEN keyword = "BKSIMDSN" THEN
            shelf_index_dsn.shelf# = data

         WHEN keyword = "BKNAME" THEN
            name = data

         WHEN keyword = "BKMDSN" THEN
            dsn = data

         WHEN keyword = "BKSDATETIME" THEN NOP
         WHEN keyword = "BKIDATETIME" THEN NOP
         WHEN keyword = "BKSLEXIS" THEN NOP
         WHEN keyword = "BKIBOOKS" THEN NOP
         WHEN keyword = "DOSNAME" THEN NOP
         WHEN keyword = "BKDATETIME" THEN NOP
         WHEN keyword = "BKFLAG" THEN NOP

         OTHERWISE DO
            SAY "Error in Bookshelf '"shelf_dsn.shelf#"'",
                "Invalid input record -" record
            ITERATE
            END

         END

/*   Check for Book information complete                             */

      IF keyword = "BKMDSN" THEN DO

/*   Add book to bookshelf/book list                                 */

         shelf_book_num.shelf# = shelf_book_num.shelf# + 1
         i = shelf_book_num.shelf#
         shelf_book_dsn.shelf#.i = dsn
         shelf_book_name.shelf#.i = name

/*   Find existing Book Table entry                                  */

         traceopt = TRACE("OFF")
         DO book# = 1 TO book_num UNTIL dsn = book_dsn.book#
            END
         x = TRACE(traceopt)

/*   Create new Book Table entry                                     */

         IF book# > book_num THEN DO
            book_num = book_num + 1
            book_dsn.book_num = dsn
            book_name.book_num = name
            book_title.book_num = title
            book_docnum.book_num = docnum
            END

/*   Add bookshelf name to book/bookshelf list                       */

         book_shelf_num.book# = book_shelf_num.book# + 1
         i = book_shelf_num.book#
         book_shelf_dsn.book#.i = shelf_dsn.shelf#
         book_shelf_name.book#.i = shelf_name.shelf#
         book_shelf_special.book#.i = shelf_special.shelf#

/*   End of parse - Reset book information variables                 */

         dsn = ""
         name = ""
         title = ""
         docnum = ""

         END

      END

/*   Check for Bookshelf Index specified                             */

   IF shelf_index_dsn.shelf# ¬= "" THEN DO

/*   Find existing Bookshelf Index Table entry                       */

      traceopt = TRACE("OFF")
      DO index# = 1 TO index_num UNTIL index_dsn.index# = shelf_index_dsn.shelf#
         END
      x = TRACE(traceopt)

/*   Create new Bookshelf Index Table entry                          */

      IF index# > index_num THEN DO
         index_num = index_num + 1
         index_dsn.index_num = shelf_index_dsn.shelf#
         index_name.index_num = shelf_index_name.shelf#
         END

/*   Add bookshelf name to index/bookshelf list                      */

      index_shelf_num.index# = index_shelf_num.index# + 1
      i = index_shelf_num.index#
      index_shelf_dsn.index#.i = shelf_dsn.shelf#
      index_shelf_name.index#.i = shelf_name.shelf#

      END

   RETURN

/*********************************************************************/
/*                                                                   */
/*   Create Unreferenced Bookshelf List                              */
/*                                                                   */
/*********************************************************************/

REFSHELF: PROCEDURE EXPOSE cat_shelf_num cat_shelf_dsn. ,
                           shelf_num shelf_dsn. ,
                           report_depth report_pageno report_lineno ,
                           report_title report_num report_line. ,
                           exec_dsn exec_num exec_line.

/*   Process each cataloged bookshelf data set                       */

   CALL RPTHEAD "Unreferenced Bookshelves to be Deleted", ,
                "   Bookshelf Data Set Name"

   DO shelf#=1 TO cat_shelf_num

/*   Check for bookshelf referenced                                  */

      traceopt = TRACE("OFF")
      DO i = 1 TO shelf_num UNTIL shelf_dsn.i = cat_shelf_dsn.shelf#
         END
      x = TRACE(traceopt)

/*   Indicate unreferenced bookshelf to be deleted                   */

      IF i > shelf_num THEN DO
         rptline = "   " || cat_shelf_dsn.shelf#
         CALL RPTLINE rptline
         execline = """DELETE '" || cat_shelf_dsn.shelf# || "'"""
         CALL EXECLINE execline
         END

      END

   RETURN

/*********************************************************************/
/*                                                                   */
/*   Create Unreferenced Bookshelf Index List                        */
/*                                                                   */
/*********************************************************************/

REFINDEX: PROCEDURE EXPOSE cat_index_num cat_index_dsn. ,
                           index_num index_dsn. ,
                           report_depth report_pageno report_lineno ,
                           report_title report_num report_line. ,
                           exec_dsn exec_num exec_line.

/*   Process each cataloged bookshelf index data set                 */

   CALL RPTHEAD "Unreferenced Bookshelf Indexes to be Deleted", ,
                "   Bookshelf Index Data Set Name"

   DO index#=1 TO cat_index_num

/*   Check for bookshelf index referenced                            */

      traceopt = TRACE("OFF")
      DO i = 1 TO index_num UNTIL index_dsn.i = cat_index_dsn.index#
         END
      x = TRACE(traceopt)

/*   Indicate unreferenced bookshelf index to be deleted             */

      IF i > index_num THEN DO
         rptline = "   " || cat_index_dsn.index#
         CALL RPTLINE rptline
         execline = """DELETE '" || cat_index_dsn.index# || "'"""
         CALL EXECLINE execline
         END

      END

   RETURN

/*********************************************************************/
/*                                                                   */
/*   Create Unreferenced Book List                                   */
/*                                                                   */
/*********************************************************************/

REFBOOK: PROCEDURE EXPOSE cat_book_num cat_book_dsn. ,
                          book_num book_dsn. ,
                          report_depth report_pageno report_lineno ,
                          report_title report_num report_line. ,
                          exec_dsn exec_num exec_line.

/*   Process each cataloged book data set                            */

   CALL RPTHEAD "Unreferenced Books to be Deleted", ,
                "   Book Data Set Name"

   DO book#=1 TO cat_book_num

/*   Check for book referenced                                       */

      traceopt = TRACE("OFF")
      DO i = 1 TO book_num UNTIL book_dsn.i = cat_book_dsn.book#
         END
      x = TRACE(traceopt)

/*   Indicate unreferenced book to be deleted                        */

      IF i > book_num THEN DO
         rptline = "   " || cat_book_dsn.book#
         CALL RPTLINE rptline
         execline = """DELETE '" || cat_book_dsn.book# || "'"""
         CALL EXECLINE execline
         END

      END

   RETURN

/*********************************************************************/
/*                                                                   */
/*   Create Special Bookshelf Updated Books List                     */
/*                                                                   */
/*********************************************************************/

UPDSPEC: PROCEDURE EXPOSE shelf_num shelf_dsn. ,
                          shelf_name. shelf_desc. shelf_special. ,
                          shelf_index_dsn. shelf_index_name. ,
                          shelf_book_num. shelf_book_dsn. ,
                          shelf_book_name. ,
                          book_num book_dsn. ,
                          book_name. book_title. book_docnum. ,
                          book_shelf_num. book_shelf_dsn. ,
                          book_shelf_name. book_shelf_special. ,
                          report_depth report_pageno report_lineno ,
                          report_title report_num report_line.

   ARG special_shelf_dsn

/*   Find special bookshelf in bookshelf table                       */

   traceopt = TRACE("OFF")
   DO shelf# = 1 TO shelf_num UNTIL shelf_dsn.shelf# = special_shelf_dsn
      END
   x = TRACE(traceopt)

   IF shelf# > shelf_num THEN DO
      SAY "Special bookshelf" special_shelf_dsn "was not found"
      RETURN
      END

   title = "Updated Books for Special Bookshelf - " || special_shelf_dsn
   CALL RPTHEAD title, ,
                "   Book Name     Data Set Name"

/*   Check for each book in a non-special bookshelf                  */
/*   If book is not in any non-special bookshelf, then report it     */

   DO book# = 1 TO shelf_book_num.shelf#

      traceopt = TRACE("OFF")
      DO i = 1 TO book_num UNTIL book_dsn.i = shelf_book_dsn.shelf#.book#
         END
      x = TRACE(traceopt)
      IF i > book_num THEN DO
         SAY "BKMGRMNT EXEC error - Book in shelf is not found"
         SAY "   Shelf DSN = '"special_shelf_dsn"'"
         SAY "   Book DSN = '"shelf_book_dsn.shelf#.book#"'"
         EXIT
         END

      traceopt = TRACE("OFF")
      DO j = 1 TO book_shelf_num.i WHILE book_shelf_special.i.j
         END
      x = TRACE(traceopt)

      IF j > book_shelf_num.i THEN DO
         rptline = "   " || LEFT(shelf_book_name.shelf#.book#,8) || ,
                   "   " || shelf_book_dsn.shelf#.book#
         CALL RPTLINE rptline
         END

      END

   RETURN

/*********************************************************************/
/*                                                                   */
/*   Set Report Heading                                              */
/*                                                                   */
/*********************************************************************/

RPTHEAD: PROCEDURE EXPOSE report_depth report_pageno report_lineno ,
                          report_title report_head. ,
                          report_num report_line.

/*   Get title                                                       */

   report_title = ARG(1)

/*   Get headings                                                    */

   report_head. = ""
   report_head.0 = ARG() - 1
   DO head# = 1 TO report_head.0
      report_head.head# = ARG(report_head.0+1)
      END

/*   Print first heading                                             */

   report_pageno = 0
   report_lineno = 0

   RETURN

/*********************************************************************/
/*                                                                   */
/*   Add Line to Report                                              */
/*                                                                   */
/*********************************************************************/

RPTLINE: PROCEDURE EXPOSE report_depth report_pageno report_lineno ,
                          report_title report_head. ,
                          report_num report_line.

   rptline = ARG(1)

   skip = TRANSLATE(ARG(2))
   IF skip = "" THEN
      skip = "SINGLE"

/*   Add carriage control to report line                             */

   SELECT
      WHEN skip = "SINGLE" THEN DO
         cc = " "
         report_lineno = report_lineno + 1
         END
      WHEN skip = "DOUBLE" THEN DO
         cc = "0"
         report_lineno = report_lineno + 2
         END
      WHEN skip = "TRIPLE" THEN DO
         cc = "-"
         report_lineno = report_lineno + 3
         END
      WHEN skip = "NONE" THEN DO
         cc = "+"
         END
      WHEN skip = "PAGE" THEN DO
         CALL RPTEJECT
         cc = " "
         report_lineno = report_lineno + 1
         END
      OTHERWISE DO
         cc = " "
         report_lineno = report_lineno + 1
         END
      END

/*   Check for end of page                                           */

   IF report_pageno = 0 | report_lineno > report_depth THEN DO
      CALL RPTEJECT
      cc = " "
      report_lineno = report_lineno + 1
      END

/*   Add line to report array                                        */

   report_num = report_num + 1
   report_line.report_num = cc || rptline

   RETURN

/*********************************************************************/
/*                                                                   */
/*   Start New Report Page                                           */
/*                                                                   */
/*********************************************************************/

RPTEJECT: PROCEDURE EXPOSE report_depth report_pageno report_lineno ,
                           report_title report_head. ,
                           report_num report_line.

/*   Add title to report array                                       */

   report_pageno = report_pageno + 1

   report_num = report_num + 1
   report_line.report_num = "1" || CENTER(report_title,98) || ,
                            DATE() || "  " || TIME() || ,
                            "     PAGE " || report_pageno

   report_num = report_num + 1
   report_line.report_num = " "

   report_lineno = 2

/*   Add headings to report array                                    */

   IF report_head.0 > 0 THEN DO

      DO head# = 1 TO report_head.0
         report_num = report_num + 1
         report_line.report_num = " " || report_head.head#
         report_lineno = report_lineno + 1
         END

      report_num = report_num + 1
      report_line.report_num = " "
      report_lineno = report_lineno + 1

      END

   RETURN

/*********************************************************************/
/*                                                                   */
/*   Write Report                                                    */
/*                                                                   */
/*********************************************************************/

RPTEND: PROCEDURE EXPOSE report_dest report_dsn report_sysout ,
                         report_alloc report_outdes ,
                         report_num report_line.

   IF report_num = 0 THEN DO
      SAY "No reports produced"
      RETURN
      END

/*   Allocate report data set                                        */

   SELECT

      WHEN report_dest = "DSN" THEN DO
         "ALLOC FILE(BKMRPT) REUSE ",
               "DATASET('"report_dsn"') ",
               report_alloc
         IF rc ¬= 0 THEN DO
            SAY "ALLOC error for report data set '"report_dsn"' - RC="rc
            EXIT 8
            END
         END

      WHEN report_dest = "SYSOUT" THEN DO
         IF report_outdes ¬= "" THEN DO
            "OUTDES BKMGROUT REUSE "report_outdes
            IF rc ¬= 0 THEN DO
               SAY "OUTDES error for report data set - RC="rc
               EXIT 8
               END
            "ALLOC FILE(BKMRPT) REUSE SHR ",
                  "SYSOUT("report_sysout") ",
                  "OUTDES(BKMGROUT) ",
                  "RECFM(F B A) LRECL(133)"
            IF rc ¬= 0 THEN DO
               SAY "ALLOC error for report data set (SYSOUT) - RC="rc
               EXIT 8
               END
            END
         ELSE DO
            "ALLOC FILE(BKMRPT) REUSE SHR ",
                  "SYSOUT("report_sysout") ",
                  "RECFM(F B A) LRECL(133)"
            IF rc ¬= 0 THEN DO
               SAY "ALLOC error for report data set (SYSOUT) - RC="rc
               EXIT 8
               END
            END
         END

      WHEN report_dest = "TERMINAL" THEN DO
         "ALLOC FILE(BKMRPT) REUSE SHR ",
               "DATASET(*) ",
               "RECFM(F B A) LRECL(133)"
         IF rc ¬= 0 THEN DO
            SAY "ALLOC error for report data set (terminal) - RC="rc
            EXIT 8
            END
         END

      OTHERWISE RETURN

      END

/*   Write output report                                             */

   "EXECIO" report_num "DISKW BKMRPT ( STEM report_line. OPEN FINIS )"
   IF rc ¬= 0 THEN DO
      SAY "EXECIO error for report data set - RC="rc
      EXIT 8
      END

/*   Free report data set                                            */

   x = MSG("OFF")
   "FREE FILE(BKMRPT) OUTDES(BKMGROUT)"
   x = MSG("ON")

   RETURN

/*********************************************************************/
/*                                                                   */
/*   Add Line to REXX EXEC                                           */
/*                                                                   */
/*********************************************************************/

EXECLINE: PROCEDURE EXPOSE exec_dsn exec_num exec_line.

   execline = ARG(1)

/*   Add line to report array                                        */

   exec_num = exec_num + 1
   exec_line.exec_num = execline

   RETURN

/*********************************************************************/
/*                                                                   */
/*   Write REXX EXEC                                                 */
/*                                                                   */
/*********************************************************************/

EXECEND: PROCEDURE EXPOSE exec_dsn exec_alloc ,
                          exec_num exec_line.

   IF exec_dsn = "" THEN DO
      SAY "No REXX EXEC produced"
      RETURN
      END

/*   Allocate REXX EXEC data set                                     */

   "ALLOC FILE(BKMEXEC) REUSE ",
         "DATASET('"exec_dsn"') ",
         exec_alloc
   IF rc ¬= 0 THEN DO
      SAY "ALLOC error for REXX EXEC data set '"exec_dsn"' - RC="rc
      EXIT 8
      END

/*   Write output REXX EXEC                                          */

   "EXECIO" exec_num "DISKW BKMEXEC ( STEM exec_line. OPEN FINIS )"
   IF rc ¬= 0 THEN DO
      SAY "EXECIO error for REXX EXEC data set - RC="rc
      EXIT 8
      END

/*   Free REXX EXEC data set                                         */

   x = MSG("OFF")
   "FREE FILE(BKMEXEC)"
   x = MSG("ON")

   RETURN

/*********************************************************************/
/*                                                                   */
/*   Format Elapsed Time                                             */
/*                                                                   */
/*********************************************************************/

ET: PROCEDURE

   e_time = TIME("E")

   e_min = TRUNC(e_time/60)
   e_sec = e_time-(e_min*60)

   e_format = FORMAT(e_min,4) || ":" || RIGHT(FORMAT(e_sec,,2),5,"0")

   RETURN e_format

