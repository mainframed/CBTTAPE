/* REXX - MINE TOOL(FOR COBOL PROGRAMS)                             */
/*__________________________________________________________________*/
/* AUTHOR : C QUASAR. QUASAR.CHUNAWALA@BNYMELLON.COM                */
/*                                                                  */
/* DESCRIPTION :                                                    */
/* -----------                                                      */
/* MINE REFERS TO SOFTWARE MINING AND IS FOR ANALYSIS OF            */
/* COBOL PROGRAMS.                                                  */
/*                                                                  */
/* IT RUNS AS AN EDIT-MACRO. THE <PF2> KEY IS USED TO TRAVERSE      */
/* DOWN FROM THE CALLING TO THE CALLED ROUTINE. THE <PF4> KEY       */
/* IS USED BACK UP. <PF1> KEY HELPS TO GENERATE A STRCUTURE CHART.  */
/*                                                                  */
/*__________________________________________________________________*/


/* START OF MACRO */
ADDRESS ISREDIT
"MACRO (PARM) NOPROCESS"

ADDRESS ISPEXEC "CONTROL ERRORS RETURN"

/*__________________________________________________________________*/
  MAIN:
/*-----                                                             */
/* THE MAIN ROUTINE FIRST INITIALIZES THE VARIABLES TO THE DEFAULT  */
/* VALUES. NEXT, IT READS THE LONG VARIABLE PARA_STACK TO DETERMINE */
/* HOW DEEP WE ARE IN THE CALL-TREE. DEPENDING UPON THE PARAMETER   */
/* PASSED, 'IN' OR 'OUT' IT WILL PROCESSING IS PERFORMED. A <PF3>   */
/* KEY-PRESS IS INTERCEPTED AND THE PARA_STACK VARIABLE IS CLEARED. */
/*__________________________________________________________________*/

  CALL INITIALIZATION

  PARM = TRANSLATE(PARM)

  IF PARM = 'IN' THEN DO
     CALL MINE_IN
  END

  IF PARM = 'OUT' THEN DO
     CALL MINE_OUT
  END

  IF PARM = 'GRAPH' THEN DO
     CALL GEN_FLOW_GRAPH
  END

  EXIT

/*__________________________________________________________________*/
  INITIALIZATION:
/*--------------                                                    */
/* THE INITIALIZATION ROUTINE ASSIGNS TO DEFAULT VALUES TO VARIABLES*/
/* THAT ARE USED, EACH TIME THIS EXEC IS INVOKED.                   */
/*__________________________________________________________________*/

  LINENUM = 0
  LINEDATA = ''
  SCREEN_NO = 00
  RACFID = ''
  EDIT_DATASET = ''
  PARAGRAPH = ''
  LINE_NUMBER = 00
  PARA_NAME = ''
  MATCH_FOUND = 0
  ITEM_FOUND = ''
  SEARCH_KEY = ''
  MAX_SIZE = 0
  ITEM_LENGTH = 102
  SEARCH_KEY_LENGTH = 93
  STEP_SIZE = -102
  EXTRACTED_STR = ''
  I = 0
  J = 0
  PRECEDING_STR  = ''
  SUCCEEDING_STR = ''
  PARA_LIST.0 = 0
  PARA_LIST. = ''
  PARA_IDX = 0
  N = 0
  GRAPH. = 0
  GRAPH_PARA = ''
  CALLER_LIST.0 = 0
  CALLER_LIST.  = ''
  CALLED_LIST.0 = 0
  CALLED_LIST.  = ''
  SHAD_CALLER_LIST.0 = 0
  SHAD_CALLER_LIST.  = ''
  SHAD_CALLED_LIST.0 = 0
  SHAD_CALLED_LIST.  = ''
  CALLER_CONNECTOR.0 = 0
  CALLER_CONNECTOR.  =''
  CALLED_CONNECTOR.0 = 0
  CALLED_CONNECTOR.  =''

  Respect_cmd = 'Yes'
  Caller_list_has_more_items = 'Yes'
  Called_list_has_more_items = 'Yes'
  Caller_List_Size = 0
  Called_List_Size = 0
  Caller_List_Max_Pos = 0
  Called_List_Max_Pos = 0
  DYN_CALLER_LIST.0 = 0
  DYN_CALLER_LIST.  = ''
  DYN_CALLED_LIST.0 = 0
  DYN_CALLED_LIST.  = ''

/* List of Function Pool Variables
  Curr_marker_pos = 0
  Main_Para = ''
*/

  RETURN

/*__________________________________________________________________*/
  VGET_CALL_STACK:
/*--------------                                                    */
/* THIS ROUTINE VGETS THE CALLSTK VARIABLE FROM THE ISPF SHARED     */
/* POOL.                                                            */
/*__________________________________________________________________*/

  ADDRESS ISPEXEC "VGET (CALLSTK) SHARED"

  RETURN

/*__________________________________________________________________*/
  VPUT_CALL_STACK:
/*--------------                                                    */
/* THIS ROUTINE VPUTS THE CALLSTK VARIABLE TO   THE ISPF SHARED     */
/* POOL.                                                            */
/*__________________________________________________________________*/

  ADDRESS ISPEXEC
  "VPUT CALLSTK SHARED";

  RETURN

/*__________________________________________________________________*/
  MINE_IN:
/*-------                                                           */
/* THIS ROUTINE JUMPS FROM THE CALLING <PARA> TO THE CALLED <PARA>. */
/* IT FIRST ACQUIRES THE LINE-NO, THE PERFORM PARA-NAME, THE RACFID */
/* AND THE CURRENT DATASET NAME AND APPENDS THEM TO THE CALLSTK     */
/* VARIABLE IN THE BASE ISR SHARED POOL.                            */
/* NEXT, IT EXECUTES THE FIND ALL <PARA-NAME> 08 COMMAND TO JUMP    */
/* TO THE CALLED PARAGRAPH.                                         */
/*__________________________________________________________________*/


/* GET THE CURRENT LINE NUMBER, WHERE THE CURSOR IS POSITIONED      */
  ADDRESS ISREDIT
  "(LINENUM) = LINENUM .ZCSR"

/* GET THE CURRENT LINE DATA                                        */
  ADDRESS ISREDIT
  "(LINEDATA) = LINE .ZCSR"

/* PARSE THE CURRENT LINE-OF-CODE, TO SEE IF THIS IS A PERFORM      */
  PARSE VAR LINEDATA TAG 8,
                     AREA_A 12,
                     AREA_B 72,
                     COMMENT


/* IF AREA-B STATEMENT CONTAINS THE COBOL KEYWORD PERFORM           */
  IF INDEX(AREA_B,'PERFORM') > 0
  THEN DO

/* THE TSO-USER RACF-ID - 8 BYTES                                   */
     RACF_ID = USERID()
     RACF_ID = RACF_ID || COPIES(' ',08 - LENGTH(RACF_ID))

/* THE STATEMENT NUMBER OF PERFORM <PARA> - 08 BYTES                */
     LINE_NUMBER = LINENUM

/* THE COBOL PARAGRAPH NAME - 30 BYTES                              */
     PARSE VAR AREA_B VERB PARAGRAPH .

     IF VERB = 'PERFORM' THEN DO
        IF PARAGRAPH = 'VARYING' | PARAGRAPH = 'UNTIL',
        | PARAGRAPH = 'TEST' THEN DO
           WARNMSG = '  THIS IS AN INLINE PERFORM STATEMENT. POSITION',
                     'THE CURSOR ON PERFORM <PARA>'
           Call Prepare_popup
           ADDRESS ISPEXEC "ADDPOP ROW(19) COLUMN(01)"
           ADDRESS ISPEXEC "DISPLAY PANEL(MINEMESG)"
           ADDRESS ISPEXEC "REMPOP"
           Call Free_popup
        END
        ELSE DO
          PARAGRAPH = PARAGRAPH || COPIES(' ',30 - LENGTH(PARAGRAPH))

/* THE CURRENT DATASET OPEN IN EDIT MODE - 54 BYTES                 */
          ADDRESS ISREDIT "(CURDSN) = DATASET"
          ADDRESS ISREDIT "(MEMBER) = MEMBER"
          CURDSN = CURDSN || '(' || MEMBER || ')'
          CURDSN = CURDSN || COPIES(' ',54 - LENGTH(CURDSN))

/* THE CURRENT LOGICAL SCREEN                                       */
          ADDRESS ISPEXEC "VGET ZSCREEN SHARED"
          SCREEN_NO = ZSCREEN

/* EXECUTE THE FIND ALL <PARA-NAME> 08 COMMAND                      */
          ADDRESS ISREDIT "FIND ALL "PARAGRAPH" 08 "

/* IF THE FIND <PARA-NAME> COMMAND COMPLETES SUCCESSFULLY           */
          IF RC = 0 THEN DO

/* APPEND THE DATA TO END OF THE CALLSTK                            */
             CALL VGET_CALL_STACK

/* IF YOU'RE MINE'ING FOR THE VERY FIRST TIME IN THE TSO SESSION,   */
/* THE VGET FAILS WITH A RETURN-CODE 08. HENCE INITIALISE THE       */
/* CALLSTK VARIABLE TO SPACES.                                      */
             IF RC > 0  THEN DO
                CALLSTK = ''
             END

             CALLSTK = CALLSTK || RACF_ID || PARAGRAPH || CURDSN,
                       || SCREEN_NO || LINE_NUMBER || '|'

             CALL VPUT_CALL_STACK

          END
          ELSE DO
             WARNMSG = '   THE <PARA-NAME> ON THE PERFORM STATEMENT COULD',
                       'NOT BE FOUND'
             Call prepare_popup
             ADDRESS ISPEXEC "ADDPOP ROW(19) COLUMN(01)"
             ADDRESS ISPEXEC "DISPLAY PANEL(MINEMESG)"
             ADDRESS ISPEXEC "REMPOP"
             Call free_popup
          END
        END
     END
     ELSE DO
       WARNMSG = '   THIS IS NOT A PERFORM STATEMENT. POSITION THE',
                 'CURSOR ON PERFORM <PARA>'
       Call prepare_popup
       ADDRESS ISPEXEC "ADDPOP ROW(19) COLUMN(01)"
       ADDRESS ISPEXEC "DISPLAY PANEL(MINEMESG)"
       ADDRESS ISPEXEC "REMPOP"
       Call free_popup
     END
  END
  ELSE DO
     WARNMSG = '   THIS IS NOT A PERFORM STATEMENT. POSITION THE',
               'CURSOR ON PERFORM <PARA>'
     Call prepare_popup
     ADDRESS ISPEXEC "ADDPOP ROW(19) COLUMN(01)"
     ADDRESS ISPEXEC "DISPLAY PANEL(MINEMESG)"
     ADDRESS ISPEXEC "REMPOP"
     Call free_popup
  END

  RETURN

/*__________________________________________________________________*/
  MINE_OUT:
/*-------                                                           */
/* THIS ROUTINE JUMPS BACK FROM THE CALLED <PARA> TO THE CALLING    */
/* <PARA>.                                                          */
/*__________________________________________________________________*/

/* FIND THE CURRENT PARAGRAPH IN WHICH <Pf4> KEY WAS PRESSED        */
   ADDRESS ISREDIT "FIND PREV P' #' 07"
   ADDRESS ISREDIT "(LINEDATA) = LINE .ZCSR"

/* GET THE CURRENT PARAGRAPH NAME                                   */
   PARSE VAR LINEDATA TAG 08 PARAGRAPH '.' .
   PARAGRAPH = PARAGRAPH || COPIES(' ',30 - LENGTH(PARAGRAPH))

/* GET THE RACFID OF THE USER                                       */
   RACF_ID = USERID()
   RACF_ID = RACF_ID || COPIES(' ',08 - LENGTH(RACF_ID))

/* WHAT'S THE CURRENT DATASET NAME                                  */
   ADDRESS ISREDIT "(CURDSN) = DATASET"
   ADDRESS ISREDIT "(MEMBER) = MEMBER"
   CURDSN = CURDSN || '(' || MEMBER || ')'
   CURDSN = CURDSN || COPIES(' ',54 - LENGTH(CURDSN))

/* THE CURRENT LOGICAL SCREEN                                       */
   ADDRESS ISPEXEC "VGET ZSCREEN SHARED"
   SCREEN_NO = ZSCREEN

/* FORM THE SEARCH KEY                                              */
/* 93    BYTES  <---08----|----30-----|---54----|----01---->         */
   SEARCH_KEY =  RACF_ID || PARAGRAPH || CURDSN || SCREEN_NO

/* VGET THE CALL STACK FROM THE APPLICATION SHARED POOL             */
   CALL VGET_CALL_STACK


/* GET THE TOTAL LENGTH OF THE CALLSTK STRING                       */
   MAX_SIZE = LENGTH(CALLSTK)

/* SEARCH THE CALLSTK STRING BACKWARDS                              */
/* ITEM_LENGTH = 102 BYTES STEP_SIZE=102 BYTES                      */
   DO I = (MAX_SIZE - ITEM_LENGTH + 1) TO 1 BY STEP_SIZE
      EXTRACTED_STR = SUBSTR(CALLSTK,I,SEARCH_KEY_LENGTH)
      J = J + ITEM_LENGTH
      IF SEARCH_KEY = EXTRACTED_STR THEN DO
         MATCH_FOUND = 1
         LEAVE
      END
   END

   IF MATCH_FOUND = 1 THEN DO

      ITEM_FOUND = SUBSTR(CALLSTK,I,ITEM_LENGTH - 1)
      LINE_NUMBER = SUBSTR(ITEM_FOUND,94,08)
      ADDRESS ISREDIT "UP MAX"
      ADDRESS ISREDIT "CURSOR = "LINE_NUMBER""
      ADDRESS ISREDIT "FIND "PARAGRAPH""

      PRECEDING_STR = SUBSTR(CALLSTK,1,I - 1)
      SUCCEEDING_STR = SUBSTR(CALLSTK,I + ITEM_LENGTH,J)

      CALLSTK = PRECEDING_STR || SUCCEEDING_STR

      CALL VPUT_CALL_STACK

   END

   IF MATCH_FOUND = 0 THEN DO
      WARNMSG = '             THERE ARE NO MORE LEVELS TO MINE BA',
                || 'CK UP.                  '
      Call prepare_popup
      ADDRESS ISPEXEC "ADDPOP ROW(19) COLUMN(01)"
      ADDRESS ISPEXEC "DISPLAY PANEL(MINEMESG)"
      ADDRESS ISPEXEC "REMPOP"
      Call free_popup
   END

   RETURN

/*__________________________________________________________________*/
   Gen_Flow_Graph:
/* --------------                                                   */
/* This paragraph is used to prepare first finds the para, for which*/
/* the user requested the graph and then invoked Build_Graph and    */
/* and Build_Dyn_Area routines.                                     */
/*__________________________________________________________________*/

   Curr_marker_pos = 0

   Call Get_Para

   Display_rc = 0

   Do Until Display_rc = 08

      Call Initialization

      Graph_Para = Main_Para

      Call Build_Graph
      Call Build_Dyn_Area
      Call Display_Dyn_Panel

/*    Say 'zcmd : ' zcmd                                            */

      If zcmd = 'NEXT' then do
         Curr_marker_pos = Curr_marker_pos + 5
/*       Say' curr_marker_pos = ' curr_marker_pos                   */
      End

      If zcmd = 'PREV' then do
         Curr_marker_pos = Curr_marker_pos - 5
         If Curr_marker_pos < 0 then do
            Curr_marker_pos = 0
         End
/*       Say' curr_marker_pos = ' curr_marker_pos                   */
      End
   End

   Address isredit "LOCATE "CURRPOS""

   RETURN

/*__________________________________________________________________*/
   Get_Para:
/* --------------                                                   */
/* UNDER WHICH PARAGRAPH WAS THE PF1 KEY PRESSED                    */
/*__________________________________________________________________*/
  ADDRESS ISREDIT "(CURRPOS) = LINENUM .ZCSR"
  ADDRESS ISREDIT "FIND PREV P' #' 07"
  ADDRESS ISREDIT "(LINEDATA) = LINE .ZCSR"
  PARSE VAR LINEDATA TAG 08 MAIN_PARA  '.' .
  Say 'CURRPOS = ' currpos

  RETURN

/*__________________________________________________________________*/
   BUILD_GRAPH:
/* --------------                                                   */
/* THIS PARAGRAPH IS USED TO PREPARE AN MATRIX OF N X N, REPRESENTIN*/
/* ALL THE CALLING PARAGRAPHS AND THE CALLED PARAGRAPHS.            */
/* IT THEN DRAWS THE STRUCTURE CHART ON A DYNAMIC AREA.             */
/*__________________________________________________________________*/

/* COLLECT THE LIST OF ALL THE PARAGRAPHS IN AN ARRAY.              */
  LBL = 'PD'
  ADDRESS ISREDIT "X ALL 07 '*'"
  ADDRESS ISREDIT "F ALL 'PROCEDURE' 08"
  ADDRESS ISREDIT "X ALL .ZF .ZCSR"
  ADDRESS ISREDIT "UP MAX"
  DO FOREVER
     ADDRESS ISREDIT "F 08 P'¬' NX"
     IF RC > 0 THEN DO
        PARA_LIST.0 = PARA_IDX
        ADDRESS ISREDIT "RESET"
        LEAVE
     END
     ELSE DO
/* GET THE CURRENT LINE DATA                                        */
       ADDRESS ISREDIT
       "(LINEDATA) = LINE .ZCSR"
/* PARSE THE CURRENT LINE-OF-CODE, TO SEE IF THIS IS A PERFORM      */
       PARSE VAR LINEDATA TAG 8,
                          AREA_A '.'
       IF INDEX(AREA_A,'EXIT') = 0
       THEN DO
          PARA_IDX = PARA_IDX + 1
          PARA_LIST.PARA_IDX = AREA_A
       END
     END
  END


  N = PARA_IDX

/* DISPLAY THE LIST OF PARAGRAPHS */

/*DO I = 1 TO N
   SAY PARA_LIST.I
END */

/* BUILD THE FLOW GRAPH - IT WILL BE A N X N MATRIX                 */
/* FIRST WE SHALL INITIALIZE THE MATRIX                             */
  DO X = 1 TO N
     DO Y = 1 TO N
        GRAPH.X.Y = 0
     END
  END

  DO PARA_IDX = 1 TO N
/* POSITION THE CURSOR AT THE PARA                                  */
     ADDRESS ISREDIT "F ALL "PARA_LIST.PARA_IDX" 08"
     ADDRESS ISREDIT "(LINENUM) = LINENUM .ZCSR"
     ADDRESS ISREDIT "(LASTLIN) = LINENUM .ZLAST"
/*   SAY 'PARA:' PARA_LIST.PARA_IDX                              */
/* READ ALL THE LINES IN THIS PARAGRAPH TILL THE END-OF-PARA        */
     DO FOREVER
/* QUERY THE LINE-NUMBER                                            */
        LINENUM = LINENUM + 1
        ADDRESS ISREDIT  "(LINEDATA) = LINE "LINENUM""

/*      SAY 'LINEDATA : ' LINEDATA                                  */

        PARSE VAR LINEDATA TAG 8,
                           AREA_A 12,
                           AREA_B 72,
                           COMMENT

/* FIX TO IGNORE COMMENT LINES                                      */
        IF SUBSTR(TAG,7,1) = '*' THEN DO
           ITERATE
        END

        IF (AREA_A  > '' & (SUBSTR(TAG,7,1) <> '*')),
           | (LINENUM > LASTLIN) THEN DO
/*         SAY PARA_LIST.PARA_IDX ' ENDED AT ' LINENUM            */
           LEAVE
        END
/* IF A PERFORM STATEMENT IS FOUND ON THIS LINE                     */
        IF INDEX(AREA_B,'PERFORM') > 0   DO
        THEN DO
           PARSE VAR AREA_B VERB PARAGRAPH .
           IF PARAGRAPH = 'VARYING' | PARAGRAPH = 'UNTIL' THEN DO
              ITERATE
           END

           ELSE DO
              X = PARA_IDX
/* SEARCH THIS PARAGRAPH NAME IN THE PARA_LIST                      */
              DO Y = 1 TO N
                 IF PARA_LIST.Y = PARAGRAPH THEN DO
/* THERE SHOULD BE AN EDGE FROM X TO Y                              */
                    GRAPH.X.Y = 1
                 END
              END
           END
        END
     END
  END

/*DO X = 1 TO N
     DO Y = 1 TO N
        SAY 'GRAPH(' X ',' Y ')' GRAPH.X.Y
     END
  END */

/* FIND OUT ALL EDGES LEADING INTO THE GRAPH_PARA - CALLER_LIST     */

  DO Y = 1 TO N
     IF PARA_LIST.Y = GRAPH_PARA THEN DO
        LEAVE
     END
  END
  I = 0
  DO X = 1 TO N
     IF GRAPH.X.Y = 1 THEN DO
        I = I + 1
        CALLER_LIST.I = PARA_LIST.X
/*      SAY 'CALLER_LIST.' I ' = ' CALLER_LIST.I                    */
     END
  END

  X = Y
/* FIND OUT ALL EDGES GOING OUT OF THE GRAPH_PARA - CALLED_LIST     */
  I = 0
  DO Y = 1 TO N
     IF GRAPH.X.Y = 1 THEN DO
        I = I + 1
        CALLED_LIST.I = PARA_LIST.Y
/*      SAY 'CALLED_LIST.' I ' = ' CALLED_LIST.I                    */
     END
  END

  RETURN

/*__________________________________________________________________*/
   BUILD_DYN_AREA:
/* --------------                                                   */
/* This paragraph is used to prepare an matrix of n x n, representin*/
/* all the calling paragraphs and the called paragraphs.            */
/* It then draws the structure chart on a dynamic area.             */
/*__________________________________________________________________*/

/*
Layout of the Dynamic Area
__________________________

----+----1----+----2----+----3----+----4----+----5----+----6----+----7----+----8
   $0000-MAIN          ¢-----$2000-MAINLINE-PROCE¢-----$2500-DISPLAY-DATA  ¢
    <<----- 19 ------>>       <<------ 19 ----->>     <<------ 19 ----->>


How PF7 and PF8 Scrolling works ?
_______________________________
The curr_marker_pos keeps track of the current position. Each time
a scroll up, subtracts 5 to curr_marker_pos, a scroll down adds 5 to
curr_marker_pos.

We check if either the Caller_list or Called_list have no. of elements
greater than the value of curr_marker_pos. If so, we must display the
remaining flow on the screen.

*/

/* PF7 and PF8 Logic                                               */
   Curr_marker_pos = Curr_marker_pos + 1

   If Caller_list.curr_marker_pos > '' then do
      Respect_cmd = 'Yes'
      Caller_list_has_more_items = 'Yes'
   End
   Else do
      Respect_cmd = 'No'
      Caller_list_has_more_items = 'No'
   End

   If Called_list.curr_marker_pos > '' then do
      Respect_cmd = 'Yes'
      Called_list_has_more_items = 'Yes'
   End
   Else do
      Respect_cmd = 'No'
      Called_list_has_more_items = 'No'
   End

/* Say 'Caller_List_has_more_items = ' Caller_List_has_more_items  */
/* Say 'Called_List_has_more_items = ' Called_List_has_more_items  */
   Curr_marker_pos = Curr_marker_pos - 1

/* Find out the Caller_List_Size and Called_List_Size              */

   I = 1
   Do Forever
      If Caller_List.I = '' then do
         Caller_List_Size = I - 1
         Leave
      End
      Else do
         I = I + 1
      End
   End

   I = 1
   Do Forever
      If Called_List.I = '' then do
         Called_List_Size = I - 1
         Leave
      End
      Else do
         I = I + 1
      End
   End

/* Find Caller_List_Max_Pos and Called_List_Max_Pos                */
   Caller_List_Max_Pos = Caller_List_Size - (Caller_List_Size // 5)
   Called_List_Max_Pos = Called_List_Size - (Called_List_Size // 5)

/* Say 'Caller_List_Size    : ' Caller_List_Size
   Say 'Called_List_Size    : ' Called_List_Size
   Say 'Caller_List_Max_Pos : ' Caller_List_Max_Pos
   Say 'Called_List_Max_Pos : ' Called_List_Max_Pos                */

/* Build the Dynamic Area                                          */

   DO I = 1 TO 05
      If Caller_list_has_more_items = 'Yes' then do
         INDX = curr_marker_pos + I
/*       Say 'Copying CALLER_LIST.' INDX ' TO DYN_CALLER_LIST.' I   */
         DYN_CALLER_LIST.I = SUBSTR(CALLER_LIST.INDX,1,19)
      End
      Else do
         INDX = Caller_List_Max_Pos + I
/*       Say 'Copying CALLER_LIST.' INDX ' TO DYN_CALLER_LIST.' I   */
         DYN_CALLER_LIST.I = SUBSTR(CALLER_LIST.INDX,1,19)
      End

      If Called_list_has_more_items = 'Yes' then do
         INDX = curr_marker_pos + I
/*       Say 'Copying CALLED_LIST.' INDX ' TO DYN_CALLED_LIST.' I   */
         DYN_CALLED_LIST.I = SUBSTR(CALLED_LIST.INDX,1,19)
      End
      Else do
         INDX = Called_List_Max_Pos + I
/*       Say 'Copying CALLED_LIST.' INDX ' TO DYN_CALLED_LIST.' I   */
         DYN_CALLED_LIST.I = SUBSTR(CALLED_LIST.INDX,1,19)
      End

      IF DYN_CALLER_LIST.I > '' THEN DO
         DYN_CALLER_LIST.I = '$' || LEFT(DYN_CALLER_LIST.I,19) || '¢'
         SHAD_CALLER_LIST.I = ' ' || COPIES('Y',19) || ' '
/*       SAY 'CALLER_LIST(' I ') = ' DYN_CALLER_LIST.I              */
      END
      ELSE DO
         DYN_CALLER_LIST.I = COPIES(' ',21)
         SHAD_CALLER_LIST.I = COPIES(' ',21)
      END
      IF DYN_CALLED_LIST.I > '' THEN DO
         DYN_CALLED_LIST.I = '$' || LEFT(DYN_CALLED_LIST.I,19) || '¢'
         SHAD_CALLED_LIST.I = ' ' || COPIES('Y',19) || ' '
      END
      ELSE DO
         DYN_CALLED_LIST.I = COPIES(' ',21)
         SHAD_CALLED_LIST.I = COPIES(' ',21)
      END
   END

   CALL Build_connector_array

   GRAPH_PARA = SUBSTR(GRAPH_PARA,1,19)
   GRAPH_PARA = '$' || LEFT(GRAPH_PARA,19) || '¢'

   DYNVAR = ''
   SHADVAR = ''

   DYNVAR = DYNVAR ||,
           DYN_CALLER_LIST.1 || CALLER_CONNECTOR.1 || GRAPH_PARA        ||,
                                CALLED_CONNECTOR.1 || DYN_CALLED_LIST.1 ||,
                                COPIES(' ',02)     ||,
           COPIES(' ',21)    || CALLER_CONNECTOR.2 || COPIES(' ',21)    ||,
           CALLED_CONNECTOR.2|| COPIES(' ',23),
      ||   DYN_CALLER_LIST.2 || CALLER_CONNECTOR.3 || COPIES(' ',21)    ||,
                                CALLED_CONNECTOR.3 || DYN_CALLED_LIST.2 ||,
                                COPIES(' ',02)     || ,
           COPIES(' ',21)    || CALLER_CONNECTOR.4 || COPIES(' ',21)    ||,
           CALLED_CONNECTOR.4|| COPIES(' ',23),
      ||   DYN_CALLER_LIST.3 || CALLER_CONNECTOR.5 || COPIES(' ',21)    ||,
                                CALLED_CONNECTOR.5 || DYN_CALLED_LIST.3 ||,
                                COPIES(' ',02)     || ,
           COPIES(' ',21)    || CALLER_CONNECTOR.6 || COPIES(' ',21)    ||,
           CALLED_CONNECTOR.6|| COPIES(' ',23),
      ||   DYN_CALLER_LIST.4 || CALLER_CONNECTOR.7 || COPIES(' ',21)    ||,
                                CALLED_CONNECTOR.7 || DYN_CALLED_LIST.4 ||,
                                COPIES(' ',02)     || ,
           COPIES(' ',21)    || CALLER_CONNECTOR.8 || COPIES(' ',21)    ||,
           CALLED_CONNECTOR.8|| COPIES(' ',23),
      ||   DYN_CALLER_LIST.5 || CALLER_CONNECTOR.9 || COPIES(' ',21)    ||,
                                CALLED_CONNECTOR.9 || DYN_CALLED_LIST.5 ||,
                                COPIES(' ',02)

   SHADVAR = SHADVAR,
          ||SHAD_CALLER_LIST.1  || COPIES(' ',06) || COPIES('Y',19),
                                || COPIES(' ',06) || SHAD_CALLED_LIST.1,
                                || COPIES(' ',02) || COPIES(' ',75),
          ||SHAD_CALLER_LIST.2  || COPIES(' ',05) || COPIES(' ',21),
                                || COPIES(' ',05) || SHAD_CALLED_LIST.2,
                                || COPIES(' ',02) || COPIES(' ',75),
          ||SHAD_CALLER_LIST.3  || COPIES(' ',05) || COPIES(' ',21),
                                || COPIES(' ',05) || SHAD_CALLED_LIST.3,
                                || COPIES(' ',02) || COPIES(' ',75),
          ||SHAD_CALLER_LIST.4  || COPIES(' ',05) || COPIES(' ',21),
                                || COPIES(' ',05) || SHAD_CALLED_LIST.4,
                                || COPIES(' ',02) || COPIES(' ',75),
          ||SHAD_CALLER_LIST.5  || COPIES(' ',05) || COPIES(' ',21),
                                || COPIES(' ',05) || SHAD_CALLED_LIST.5,
                                || COPIES(' ',02)

   RETURN

/*__________________________________________________________________*/
   Display_Dyn_Panel:
/* -----------------                                                */
/* This routine builds an ISPF Panel on the fly and displays it.    */
/*__________________________________________________________________*/
   ZPF07 = 'PREV'
   ZPF08 = 'NEXT'
   ZCMD  = ''
   ROW   = 0
   COL   = 0

   Call load_dynlib
   Call put_object

   Address ispexec "libdef ispplib library id("dynlib") stack"
/* SAY 'LIBDEF RC = ' RC                                            */

   Address Ispexec "VPUT (ZPF07,ZPF08) PROFILE"
/* Say 'VPUT RC: ' RC                                               */
   Address Ispexec "ADDPOP ROW(05) COLUMN(01)"
   IF RC > 0 THEN DO
      Say 'ADDPOP RC = ' RC
      EXIT
   end

   Address Ispexec "DISPLAY PANEL(DYNPANE)"
   Display_rc = rc

   Address ISPEXEC "REMPOP"
   Address Ispexec "libdef ispplib"
   Address tso "free f("dynlib")"
   TrapON=OutTrap('ON')
   Address tso "delete '"tempds"'"
/* SAY 'DISPLAY_RC : ' DISPLAY_RC                                   */
   RETURN

/*__________________________________________________________________*/
   load_dynlib:
/* -----------------                                                */
/* This routine queues the panel body, and then stores it in a mem. */
/*__________________________________________________________________*/
   dynlib = 'dyn'||random(99999)
   tempds = USERID()|| '.ISPPLIB.TMP' ||random(9999)
   alloc_fi = "ALLOC FI ("dynlib") da('"tempds"')" ||,
           " lrecl(80) blksize(800)" ||,
           " new reu" ||,
           " space(5,5) dir(10) "

   ADDRESS TSO ""alloc_fi""


   if rc = 0 then do
      ispdyn = tempds
   end
   else do
      ispdyn = 'NOT FOUND'
      Say 'Failed to allocated Dynamic Library'
      Say '//' || dynlib || ' DD DSN=' || tempds
      Say rc
      exit
   end

   Address tso
   "newstack"

   member = 'DYNPANE'
   queue ')ATTR'
   queue '@ AREA(DYNAMIC) SCROLL(OFF)'
   queue 'Y TYPE(CHAR) COLOR(TURQUOISE) HILITE(REVERSE)'
   queue '~ TYPE(CHAR) COLOR(GREEN)'
   queue '$ TYPE(DATAIN) COLOR(TURQUOISE) HILITE(REVERSE)'
   queue '_ TYPE(NEF) PADC(_)'
   queue '| TYPE(TEXT) COLOR(YELLOW)'
   queue '¢ TYPE(DATAOUT) COLOR(BLUE)'
   queue '? TYPE(NT)'
   queue ')BODY WINDOW(76,12) CMD(ZCMD)'
   queue '|                        MINE - Structure Chart Generator' ||,
         '                  +'
   queue '?Command ==> _ZCMD                                       ' ||,
         '                  +'
   queue '?                                                        ' ||,
         '                  +'
   queue '+@DYNVAR,SHADVAR                                         ' ||,
         '                  @'
   queue '+@                                                       ' ||,
         '                  @'
   queue '+@                                                       ' ||,
         '                  @'
   queue '+@                                                       ' ||,
         '                  @'
   queue '+@                                                       ' ||,
         '                  @'
   queue '+@                                                       ' ||,
         '                  @'
   queue '+@                                                       ' ||,
         '                  @'
   queue '+@                                                       ' ||,
         '                  @'
   queue '+@                                                       ' ||,
         '                  @'
   queue ')PROC'
   queue '&PFKEY=.PFKEY'
   queue ')END'

   RETURN

/*__________________________________________________________________*/
   put_object:
/* ----------                                                       */
/* This routine stores the member into the Temporary ISPF Library   */
/* allocated previously.                                            */
/*__________________________________________________________________*/
   tfil = 'tfil' || random(9999)
   Address tso
   "alloc f("tfill") da('"ispdyn"("member")') shr reu"
   if rc > 0 then do
      say 'Failed to allocate member, RC = ' rc
   end
   Address tso
   "execio" queued() "diskw "tfill" (finis"
   "free f("tfill")"
   "delstack"

   RETURN

   Build_connector_array:

/* BUILD THE CONNECTOR ARRAY                                        */
  IF Dyn_CALLER_LIST.1 > '' THEN DO
     CALLER_CONNECTOR.1 = '-----'
  END
  ELSE DO
     CALLER_CONNECTOR.1 = COPIES(' ',05)
  END
  J = 1
  DO J = 02 TO 05
     I = (J - 1)*2
     IF Dyn_CALLER_LIST.J > '' THEN DO
        CALLER_CONNECTOR.I = '  |  '
        I = I + 1
        CALLER_CONNECTOR.I = '--+  '
     END
     ELSE DO
        CALLER_CONNECTOR.I = '     '
        I = I + 1
        CALLER_CONNECTOR.I = '     '
     END
  END

  IF Dyn_CALLED_LIST.1 > '' THEN DO
     CALLED_CONNECTOR.1 = '-----'
  END
  ELSE DO
     CALLED_CONNECTOR.1 = COPIES(' ',05)
  END
  J = 1
  DO J = 02 TO 05
     I = (J - 1)*2
     IF Dyn_CALLED_LIST.J > '' THEN DO
        CALLED_CONNECTOR.I = '  |  '
        I = I + 1
        CALLED_CONNECTOR.I = '  +--'
     END
     ELSE DO
        CALLED_CONNECTOR.I = COPIES(' ',05)
        I = I + 1
        CALLED_CONNECTOR.I = COPIES(' ',05)
     END
  END

  Return

/*__________________________________________________________________*/
   Prepare_popup:
/* -------------                                                    */
/* This routine builds the popup screen on-the-fly, does necessary  */
/* allocations and LIBDEF's it to the ISPPLIB.                      */
/*__________________________________________________________________*/
   dynlib = 'dyn'||random(99999)
   tempds = USERID()|| '.ISPPLIB.TMP' ||random(9999)
   alloc_fi = "ALLOC FI ("dynlib") da('"tempds"')" ||,
           " lrecl(80) blksize(800)" ||,
           " new reu" ||,
           " space(5,5) dir(10) "

   ADDRESS TSO ""alloc_fi""


   if rc = 0 then do
      ispdyn = tempds
   end
   else do
      ispdyn = 'NOT FOUND'
      Say 'Failed to allocated Dynamic Library'
      Say '//' || dynlib || ' DD DSN=' || tempds
      Say rc
      exit
   end

   Address tso
   "newstack"

   member = 'MINEMESG'
   queue   ')ATTR'
   queue   '  $ TYPE(CT)'
   queue   '  @ TYPE(PT)'
   queue   '  + TYPE(NT)'
   queue   '  # TYPE(OUTPUT) CAPS(ON) COLOR(YELLOW)'
   queue   ')BODY WINDOW(76,03)'
   queue   '@                               MINE ERROR'
   queue   '#WARNMSG'
   queue   ')END'

   tfil = 'tfil' || random(9999)
   Address tso
   "alloc f("tfill") da('"ispdyn"("member")') shr reu"
   if rc > 0 then do
      say 'Failed to allocate member, RC = ' rc
   end
   Address tso
   "execio" queued() "diskw "tfill" (finis"
   "free f("tfill")"
   "delstack"

   Address ispexec "libdef ispplib library id("dynlib") stack"
   return

/*__________________________________________________________________*/
   Free_popup:
/* -------------                                                    */
/* Frees the ISPF Tempoprary dataset and deletes it.                */
/*__________________________________________________________________*/
   Address Ispexec "libdef ispplib"
   Address tso "free f("dynlib")"
   TrapON=OutTrap('ON')
   Address tso "delete '"tempds"'"
   return
