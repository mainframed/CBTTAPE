TSTVSSEC TITLE 'Do all the security checking for TSTVS'
*---------------------------------------------------------------------*
*                                                                     *
*        MODULE:       TSTVSSEC                                       *
*        AUTHOR:       Rob Prins, Amsterdam.                          *
*                                                                     *
*        FUNCTION:     Ask for a valid userid+password from the       *
*                      operator. Validate the given user and          *
*                      check usage of the program TSTVS, by means     *
*                      of checking the resource '$TSTVS' in the       *
*                      FACILITY class.                                *
*                      All the checking is done via the SAF interface *
*                      so TSTVSSEC is suitable for RACF of ACF2       *
*                      systems.                                       *
*                      If ACF2 has been used, please map the          *
*                      FACILITY class in the GSO SAFMAPS              *
*                      If neither RACF or ACF2 aren't active, the     *
*                      TSTVS program password will be asked.          *
*                                                                     *
*        PARAMETER     Jobname                                        *
*                                                                     *
*        CALLED BY:    TSTVS                                          *
*                                                                     *
*        Return code:  0000 - Access to TSTVS is allowed              *
*                      /= 0000 - Access to TSTVS has been rejected    *
*                                                                     *
*        Modifications:                                               *
*        95/12/19: rpr incorrect length of resource $tstvs corrected  *
*                                                                     *
*------------------------------------------ (C)-1992 SKYBIRD SYSTEMS -*
         SPACE 2
TSTVSSEC CSECT
         SAVE  (14,12),,*
         USING TSTVSSEC,R15            GET LOCAL ADDRESSABILITY @911224
         LA    R11,SAVEA                                        @911224
         ST    R13,SAVEA+4                                      @911224
         ST    R11,8(0,R13)                                     @911224
         LR    R13,R11                                          @911224
         B     START                                            @911224
         DS    0F                                               @911224
SAVEA    DC    18F'-1'
         DC    CL8'&SYSDATE',C' ',CL8'&SYSTIME'
START    DS    0H
         DROP  R15                     KILL LOCAL ADDRESSABILITY@911224
         USING SAVEA,R13               Get program addres.      @921218
         L     R3,0(R1)                Parm from TSTVS (jobname)@921218
         MVI   SW,0                    clear flags
         MVC   PASSWRD,PW1                                      @911224
         OC    PASSWRD,PW2                                      @911224
         MVC   PASSWRD(2),0(R3)        1ST 2 POS OF JOBNAME.
         GETMAIN RU,LV=512             Obtain room for workarea @921218
         LR    R4,R1                   PICKUP GOTTEN ADDRESS    @911224
         LA    R6,2                    TRY MAX. 2 TIMES         @911224
         MVC   RC,=F'8'                Set default a bad code   @921218
ASKUID   DS    0H                                               @921218
         XC    ECB1,ECB1               CLEAR ECB                @911224
         MVC   USERID(8),BLANKS        Blank userid             @921218
         WTOR  'EDT301A Enter name of an authorized user',             *
               USERID,8,ECB1,ROUTCDE=(9)                        @921218
         WAIT  ECB=ECB1                WAIT FOR REPLY.          @911224
         CLI   USERID,C' '             Nothing replied ?        @921218
         BE    ASKUID                  YES: ASK AGAIN           @921218
         LA    R1,USERID+8             Point after userid       @921218
         TRT   USERID,TRTAB            compute the length       @921218
         LA    R2,USERID               start of field           @921218
         SLR   R1,R2                   Diffence is the length   @921218
         STC   R1,USER                 and save length          @921218
ASKPWD   DS    0H                                               @911224
         XC    ECB1,ECB1               CLEAR ECB                @911224
         MVC   PASSWORD(8),BLANKS      Blank password field     @921218
         WTOR  'EDT302A Enter password of this authorized user',       *
               PASSWORD,8,ECB1,ROUTCDE=(9)                      @921218
         WAIT  ECB=ECB1                WAIT FOR REPLY.          @911224
         CLI   PASSWORD,C' '           NOTHING REPLIED ?        @921218
         BE    ASKPWD                  YES: ASK AGAIN           @911224
         LA    R1,PASSWORD+8           Point after password     @921218
         TRT   PASSWORD,TRTAB          compute the length       @921218
         LA    R2,PASSWORD             start of field           @921218
         SLR   R1,R2                   Diffence is the length   @921218
         STC   R1,PWD                  and save length          @921218
         LA    R8,USER                 A(userid)                @921218
         LA    R7,PWD                  password address         @921218
         RACROUTE REQUEST=VERIFY,                                      *
               ENVIR=CREATE,           Create an ACEE                  *
               ACEE=ACEEADR,           and place address in fullword   *
               USERID=(8),                                             *
               RELEASE=1.9,                                            *
               PASSWRD=(7),                                            *
               TERMID=LUTSTVS,         user's terminal or appl. name   *
               WORKA=(4),                                              *
               MF=(E,VFYLIST)
         XC    PASSWORD,PASSWORD       clear password           @921218
         LTR   R15,R15                 verify ok ?              @921218
         BZ    CHKRSRC                 yes,  LOGON SO FAR VALID.@921218
         MVC   MSG98+4(32),=C'EDT305E RACINIT failed for user=' @921218
         MVC   MSG98+36(8),USERID      move userid in error     @921218
         MVC   MSG98+44(4),=C' rc='                             @921218
         ST    R15,SAFRC               save SAF return code     @921218
         CVD   R15,DWB                 convert rc               @921218
         UNPK  MSG98+48(4),DWB         make it zone and ...     @921218
         OI    MSG98+51,X'F0'          remove sign              @921218
         MVC   MSG98+52(10),=C', RACF rc='                      @921218
         LA    R2,VFYLIST              point to RACROUTE list   @921218
         L     R15,0(,R2)              RACF return code         @921218
         ST    R15,RACFRC              save 'RACF?' rc          @921218
         CVD   R15,DWB                 to decimal               @921218
         UNPK  MSG98+62(4),DWB         make it zone and ...     @921218
         OI    MSG98+65,X'F0'          remove sign              @921218
         CLC   SAFRC,=F'8'             RC > 8                   @921218
         BH    RACRC                   YES: It's a real error   @921218
         BE    CHKPWE                  RC=8 possible passw err. @921218
         CLC   RACFRC,=F'20'           RACF  not active ??      @921218
         BE    BYPRAC                  yes: bypass RACF check   @930429
         CLC   RACFRC,=F'0'            ACF2  not active ??      @930429
         BNE   RACRC                   no: other error          @921218
BYPRAC   DS    0H                                               @930429
         MVC   MSG98+4(66),MSG25       move bypass msg          @921218
         LA    R1,MSG98                                         @921218
         WTO   MF=(E,(1))              INFORM OPERATOR          @911224
         FREEMAIN RU,LV=512,A=(4)      freemain workarea        @921218
         B     TSTVSPW0                Ask for TSTVS password   @921218
CHKPWE   DS    0H                                               @921218
         CLC   RACFRC,=F'8'            password not auth.       @921218
         BE    PWNMATCH                yes: give suitable msg   @921218
         CLC   RACFRC,=F'12'           password expired         @921218
         BNE   RACRC                   no: other error          @921218
         MVC   MSG29+15(8),USERID      move userid in error     @921218
         MVC   MSG98+4(66),MSG29       move correct message     @921218
         B     RACRC                   branch                   @921218
PWNMATCH DS    0H                                               @921218
         MVC   MSG28+15(8),USERID      move userid in error     @921218
         MVC   MSG98+4(66),MSG28       move correct message     @921218
RACRC    DS    0H                                               @921218
         LA    R1,MSG98                                         @921218
         WTO   MF=(E,(1))              INFORM OPERATOR          @911224
         B     RACREOJ                 TERMINATE or try again   @921218
CHKRSRC  DS    0H                      Userid verified          @921218
         OI    SW,$CREATE              mark ACEE created        @921218
         L     R5,ACEEADR              Pickup ACEE address      @921218
         LA    R8,SAFENT               Entity field             @921218
         LA    R7,SAFCLASS             Class  field             @921218
         RACROUTE                                                      X
               REQUEST=AUTH,           check if resource authorized    X
               ACEE=(5),               current acee address            x
               WORKA=(4),                                              X
               ENTITYX=((8)),          resource name=$TSTVS            X
               ATTR=READ,                                              X
               RELEASE=1.9,                                            X
               CLASS=(7),              class name = FACILITY           X
               MF=(E,SAFLIST)
         LTR   R15,R15                 Auth SAF call ok ?       @940406
         BNZ   NOPRIV                  No: no access to $TSTVS  @940406
         LA    R1,MSG98                                         @921218
         MVC   MSG98+4(66),MSG27       MSG EDT309I              @921218
         WTO   MF=(E,(1))              userid accepted          @921218
         XC    RC,RC                   RC = 0000                @921218
         B     RACREOJ                 RELEASE storage and cont.@921218
NOPRIV   DS    0H                      No access to resource    @921218
         LA    R1,MSG98                                         @911224
         MVC   MSG98+4(66),MSG26       MSG EDT304E              @911224
         WTO   MF=(E,(1))              INFORM OPERATOR          @911224
         B     RACREOJ                 TRY AGAIN OR TERMINATE   @921218
RACREOJ  DS    0H                                               @921218
         TM    SW,$CREATE              ACEE has been created ?  @921218
         BNO   RACNDEL                 no: do not delete        @921218
         RACROUTE REQUEST=VERIFY,      delete ACEE                     *
               ENVIR=DELETE,                                           *
               ACEE=ACEEADR,           Delete this ACEE                *
               WORKA=(4)
         NI    SW,255-$CREATE          flag off                 @921218
RACNDEL  DS    0H                                               @921218
         ICM   R15,15,RC               return code              @921218
         BZ    TSTVSFR2                All zero: verified       @921218
         BCT   R6,ASKUID               error: ask again         @921218
         FREEMAIN RU,LV=512,A=(4)      RELEASE workarea         @921218
         B     TSTVSRET                TERMINATE PROCESSING     @911224
TSTVSFR2 DS    0H                                               @921218
         FREEMAIN RU,LV=512,A=(4)      RELEASE workarea         @921218
         B     TSTVSRET                user has been verified   @921218
         TITLE 'No security package active, ask program password'
TSTVSPW0 DS    0H
         XC    ECB1,ECB1               CLEAR ECB FOR WTOR.
         WTOR  'EDT310A Enter TSTVS password',REPLYPW,8,ECB1,          *
               ROUTCDE=9
         WAIT  ECB=ECB1                WAIT FOR REPLY.
         MVC   SYPSWRD,PW3                                      @911224
         OC    SYPSWRD,PW4                                      @911224
         CLC   PASSWRD(2),=C'SY'       SYITSTVS OR NOT.
         BE    TSTVSPW2                IF SY THEN BRANCH
         CLC   PASSWRD(8),REPLYPW      ELSE COMPARE
         BNE   TSTVSRET                WRONG PASSWORD, EXIT
         XC    RC,RC                   Clear return code        @921218
         B     TSTVSRET                Return with RC=0000      @921218
TSTVSPW2 DS    0H                                               @921218
         CLC   REPLYPW(8),SYPSWRD      COMPARE PASSWORD
         BNE   TSTVSRET                WRONG PASSWORD, EXIT
         XC    RC,RC                   Clear return code        @921218
         TITLE 'Return to TSTVS mainline'
TSTVSRET DS    0H                                               @921218
         XC    PASSWRD,PASSWRD         CLEAR PASSWORD FIELD     @911224
         XC    SYPSWRD,SYPSWRD         CLEAR PASSWORD FIELD     @911224
         XC    REPLYPW,REPLYPW         CLEAR REPLY    FIELD     @911224
         L     R15,RC                  Return code              @921218
         L     R13,4(,R13)             Pickup callers savearea  @921218
         RETURN (14,12),RC=(15)        Return to caller         @921218
         TITLE 'Variables, constants and literals'
DWB      DS    D                       For CVD instructions
ACEEADR  DS    F                       A(ACEE) - RACROUTE VFY   @921218
ECB1     DS    F                       ECB for replies          @921218
RC       DS    F                       Returncode RACROUTE's    @921218
SAFRC    DS    F                       SAF return code          @921218
RACFRC   DS    F                       RACF? return code        @921218
*
SW       DS    X                       Flags
$CREATE  EQU   128                     1... .... ACEE created   @921218
*                                      .xxx xxxx reserved
PW1      DC    XL8'0000E002E005E040'
PW2      DC    XL8'000003E003E00200'
PASSWRD  DC    CL8' '
PW3      DC    XL8'E009D007E009C001'
PW4      DC    XL8'02C004D006D004F0'
SYPSWRD  DC    CL8' '
REPLYPW  DC    CL8' '
BLANKS   DC    CL8' '
*
TRTAB    DC    256X'00'                xlate table to check delimiters
         ORG   TRTAB
         DC    X'01'                   00 is a delimiter
         ORG   TRTAB+64
         DC    X'01'                   40 (a blank) is a delimiter
         ORG   ,
*
R0       EQU    0
R1       EQU    1
R2       EQU    2
R3       EQU    3
R4       EQU    4
R5       EQU    5
R6       EQU    6
R7       EQU    7
R8       EQU    8
R9       EQU    9
R10      EQU    10
R11      EQU    11
R12      EQU    12
R13      EQU    13
R14      EQU    14
R15      EQU    15
*
         LTORG ,
         TITLE 'SAF control blocks'
SAFLIST  RACROUTE                                                      X
               REQUEST=AUTH,           check resource                  X
               WORKA=,                                                 X
               ENTITYX=,               resource name                   X
               RELEASE=1.9,                                            X
               CLASS=,                 class name                      X
               ATTR=READ,                                              X
               MF=L
VFYLIST  RACROUTE REQUEST=VERIFY,                                      *
               ENVIR=,                                                 *
               USERID=,                                                *
               RELEASE=1.9,                                            *
               PASSWRD=,                                               *
               WORKA=,                                                 *
               MF=L
SAFLISTL EQU   *-SAFLIST               LENGTH OF RACROUTE LIST FORM
SAFCLASS DC    AL1(8),CL8'FACILITY'    CLASSNAME RACROUTE (FAST)AUTH
*
SAFENT   DS    0CL10                   RACROUTE ENTITY NAME     @921218
ENTL1    DC    AL2(0)                  BUFFER LENGTH            @921218
ENTL2    DC    AL2(6)                  ENTITY LENGTH            @951219
ENTITY   DC    CL6'$TSTVS'             entity name              @921218
         DC    CL34' '                 34 blanks for ACF2       @930506
*
PWD      DC    AL1(8)                  password length          @921218
PASSWORD DS    CL8                     real password            @921218
*
USER     DC    AL1(8)                  userid length            @921218
USERID   DS    CL8                     real userid              @921218
*
LUTSTVS  DC    CL8'LUTSTVS'            input source/terminal    @921218
         TITLE 'Message block'
MSG25    DC    CL66'EDT303E No security system active, bypass userid ch*
               eck...'
MSG26    DC    CL66'EDT304E Userid has no access to the ''TSTVS'' progr*
               am'
MSG27    DC    CL66'EDT309I Userid has been verified and accepted'
MSG28    DC    CL66'EDT306W Userid=xxxxxxxx, unauthorized password'
MSG29    DC    CL66'EDT307W Userid=xxxxxxxx, password has been expired'
*
MSG98    WTO   '                                                       X
                          ',ROUTCDE=(9),MF=L
         END
