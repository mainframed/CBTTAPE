TSTVS    TITLE 'D Y N A M I C CONSOLE MONITOR'
*---------------------------------------------------------------------*
*                                                                     *
* PROGRAM:                  TSTVS                                     *
* FUNCTION:                 Line-editor from the operator console     *
* PURPOSE:                  Edit crucial PDS members if the           *
*                           SUBSYSTEMS aren't available               *
*                           and create and submit jobs if necessary.  *
*                           Update of members will be done if the     *
*                           MSG   'EDT200A enter command...' has been *
*                           replied with 'U member' or 'UN member'    *
*                           If replied with 'A member' or 'AN member' *
*                           a new member will be created.             *
*                           If 'UN' or 'AN' specified, TSTVS will     *
*                           prompt you for a linenumber (COLS 73-80)  *
*                           if a new record has been made.            *
* PROCESSOR.                ASSEMBER-H VERSION 2/HL-ASM               *
* REGISTER ASSIGNMENT.      R0-R1          RESERVED                   *
*                           R2             COUNT REG. NUMBER OF CRDS. *
*                           R3             SAVE REGISTER FOR PARM.    *
*                           R4             ADDRESS RECORD INPUT       *
*                           R5-R8          WORK-REGISTERS             *
*                           R9             THIRD BASE                 *
*                           R10            BASE-REGISTER              *
*                           R11            BASE-REGISTER              *
*                           R12            ADDRESS GETM. AREA         *
*                           R13            ADDRESS SAVE-AREA          *
*                           R14-R15        RESERVED                   *
* AUTHOR.                   Rob Prins - Jan 11, 1977                  *
*        PROGRAM STRUCTURE:                                           *
*                                                                     *
*        -------------        -------------        -------------      *
*        |  MAIN     |        | CHANGE    |        |  INSERT   |      *
*        |  ROUTINE  | <----> | ROUTINE   | <----> |  DEL ROUT.|      *
*        |           |        |           |        |           |      *
*        -------------        -------------        -------------      *
*             |                     |                                 *
*             V                     |                                 *
*        -------------              |                                 *
*        | DEL       |              |                                 *
*        | MEMBERS   |              |                                 *
*        | AND RDDIR |              |                                 *
*        -------------              |                                 *
*                                   |                                 *
*        _______________________________________________________      *
*             |                     |                    |            *
*        -------------        -------------        -------------      *
*        | PRINT     |        |  UPDIPL   |        |  SUBMIT   |      *
*        | ROUTINE   |        |  ROUTINE  |        |  ROUTINE  |      *
*        |           |        |           |        |           |      *
*        -------------        -------------        -------------      *
*        -------------        -------------        -------------      *
*        |  LIST     |        |  LINENUM  |        |   SAVE    |      *
*        |  ROUTINE  |        |   ROUTINE |        |  ROUTINE  |      *
*        |           |        |           |        |           |      *
*        -------------        -------------        -------------      *
*        -------------        -------------        -------------      *
*        |  FIND     |        |           |        |           |      *
*        |  ROUTINE  |        |           |        |           |      *
*        |           |        |           |        |           |      *
*        -------------        -------------        -------------      *
*                                                                     *
*        SUBROUTINES:                                                 *
*                    TSTVSALL:  ALLOCATE THE LIBRARY                  *
*                    TSTVSCOM:  PROCESS THE COMMANDS                  *
*                    TSTVSSEC:  check security                        *
*                                                                     *
*        Note: Assemble TSTVS with SYSPARM(NOSEC) to bypass           *
*              the security check of RACF or ACF2                     *
*                                                                     *
*                                                                     *
*------------------------------------------ (C)-2019 SKYBIRD SYSTEMS -*
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*        COPYRIGHT 77/01/11, Rob Prins                                *
*        LAST UPDATE 79/11/02  BY ROB PRINS                           *
*        ADD R SUBCOMMAND (REPEAT).                                   *
*        LAST UPDATE AT 79/12/04 BY ROB PRINS:                        *
*        CHANGE THE DESIGN OF TSTVS AND ADD ALLOC COMMAND             *
*                                                                     *
*        LAST UPDATE AT 80/04/22 BY ROB PRINS                         *
*        CHANGE START COMMAND FOR RDRTST FROM:                        *
*        "S RDRTST,M=MEMBERNAME" INTO                                 *
*        "S RDRTST,D=DATASETNAME,M=MEMBERNAME" FOR VS1 AND SVS SUBMIT.*
*        ERROR SOLVED IN UPDIPL ROUTINE        /* 80.08.26 */         *
*                                                                     *
*        LAST UPDATE AT 85/05/13 BY ROB PRINS                         *
*        ADD 'XLST' SUBCOMMAND. THIS COMMAND IS AN EXTENTION OF THE   *
*        LIST SUBCOMMAND. WITH 'XLST' THE WHOLE 80 COLUMS OF THE      *
*        RECORDS WILL BE DISPLAYED.                                   *
*                                                                     *
*        LAST UPDATE AT 86/02/12 BY ROB PRINS                         *
*        ADD 'L' SUBCOMMAND. THIS IS AN ALIAS OF THE 'LIST' SUBCOMMAND*
*        ADD 'R' COMMAND, RENAME OF MEMBERS SYNTAX: 'R MEMBER,NEWNAME'*
*        ADD 'F' SUBCOMMAND, SYNTAX: 'F $STRING$' INITIAL FIND, OR    *
*                'F' REPEAT FIND.                                     *
*        AFTER 'SAVE' SUBCOMMAND, LEAVE SUBCOMMAND MODE.              *
*                                                                     *
*        LAST UPDATE AT 87/04/23 BY ROB PRINS                         *
*        DISPLAY WHOLE LINE AFTER MATCH FOUND IN 'F' SUBCOMMAND       *
*        INSTEAD OF THE FIRST 55 POSITIONS                            *
*        REJECT 'SUBMIT' AND 'PRINT' SUBCOMMAND IF TSTVS HAS STARTED  *
*        VIA OPER.                                                    *
*                                                                     *
*        UPDATE BY ROB PRINS AT 91/12/18                              *
*        RETRY MAXIMUM 6 ABENDS TO PREVENT RECURSIVE ABENDS           *
*        ABEND90A SOLVED AFTER FIRST ABEND913 DURING OPEN             *
*        REJECT 'SUBMIT' AND 'PRINT' SUBCOMMAND IF JOBNAME=EDIT       *
*        SUBCOMMAND 'C ALL,...' ADDED                                 *
*                                                                     *
*        Update by Rob Prins at dec 24,1991 Christmas Supprise        *
*        ACF2 support added: TSTVS will ask for an authorized LOGONID *
*        and password. If the verification is successful, the LOGONID *
*        will be checked for the 'TSTVS' privilege (LIDI2F5)          *
*        If ACF2 is not active, the check will be bypassed and the    *
*        program password will be asked.                              *
*        MAX workspace size increased from 2999 to 3999               *
*                                                                     *
*        Update by Rob Prins at 1992/12/18, Christmas Supprise        *
*        All the ACF2 macro's are deleted and replaced by RACROUTE    *
*        The TSTVS privilege has been replaced by a resource called   *
*        '$TSTVS' in the FACILITY class. So TSTVS can be used in both *
*        an ACF2 or a RACF environment                                *
*        The complete security check is now done in module TSTVSSEC   *
*                                                                     *
*        Update by Rob Prins at Jul 26,1993                           *
*        The "Key" in   "PDSLIST,key" has now a variable length       *
*                                                                     *
*        Update by Rob Prins at Jul 29,1993                           *
*        Add the RENAME command to rename datasets                    *
*        Remove the VS1 support. TSTVS assumes, that the operating    *
*        system is always MVS (ESA)                                   *
*        MSGID's changed from 'JCL' into 'EDT'.                       *
*                                                                     *
*        Update by Rob Prins at Sept 04,2019                          *
*        Remove the LINK to program SYP120, This program is not       *
*        a part of TSTVS, so this LINK is changed into a SVC 34       *
*                                                                     *
*------------------------------------------ (C)-2019 SKYBIRD SYSTEMS -*
         SPACE 2
TSTVS    CSECT
         SAVE  (14,12),,*
         USING TSTVS,R15               GET LOCAL ADDRESSABILITY @911224
         LA    R11,SAVEA                                        @911224
         ST    R13,SAVEA+4                                      @911224
         ST    R11,8(0,R13)                                     @911224
         LR    R13,R11                                          @911224
         B     START                                            @911224
         DS    0F                                               @911224
SAVEA    DC    18F'-1'
         DC    CL8'&SYSDATE',C' ',CL8'&SYSTIME'
START    DS    0H
         DROP  R15                     KILL LOCAL ADDRESSABILITY@911224
         LA    R11,2048(,R13)          Setup second             @911224
         LA    R11,2048(,R11)                      base         @911224
         LA    R10,2048(,R11)          Setup third              @911224
         LA    R10,2048(,R10)                      base         @911224
         LA    R9,2048(,R10)           Setup fourth             @911224
         LA    R9,2048(,R9)                        base         @911224
         USING SAVEA,R13,R11,R10,R9    GET ADDRESSABILITY MODULE@911224
         L     R3,0(,R1)               SAVE PARM
         WTO   'EDT001I TSTVS Version 3.1 - &SYSDATE started',         *
               DESC=2,ROUTCDE=(2,3,7,8,11)                      @930729
         ST    R1,MSGID                save it's msgid          @930729
         CLC   0(2,R3),=H'0'           no parm in exec stmnt?
         BE    TSTVSRAC                BRANCH TO security mod   @921218
         LH    R6,0(,R3)               LOAD LENGTH
         LA    R3,2(,R3)               SKIP LENGTH
         LR    R7,R6
         CH    R6,=H'2'                LENGTH LONGER THAN 2
         BH    MESS                    YES: ERROR MSG AND TAKE DEF
LOOP     CLI   0(R3),C'0'              NUMERIC TEST
         BL    MESS                    TAKE DEFAULTS IF NOT NUMERIC
         LA    R3,1(,R3)
         BCT   R6,LOOP                 BRANCH FOR SECOND TEST
         SR    R3,R7                   RESTORE ADDRESS PARM
         BCTR  R7,0
         EX    R7,COMPPRM
         BNH   MESS                    TAKE DEFAULT ROUTCODES
         MVI   ZZZ-2,X'00'
         EX    R7,PACK1
         CVB   R6,DWB                  CONVERT PARM
         BCTR  R6,0                    NO SHIFT IS ROUTING CODE-1
         XR    R7,R7
         IC    R7,=X'80'               SET HIGH ORDER BIT BITS 24-31
         SLL   R7,8                    SHIFT 8 BITS TO THE LEFT
         SRL   R7,0(R6)                SHIFT TO CORRECT ROUTCDE
         STH   R7,ZZZ-2                STORE
         STH   R7,BBB-4                  ROUTING
         STH   R7,CCC-4                     CODES
         STH   R7,DDD-4                       IN
         STH   R7,FFF-4                         WTOR
         STH   R7,GGG-4                           EXPANSIONS
         STH   R7,HHH-4
         STH   R7,III-4
         STCM  R7,3,PARMROUT
         B     TSTVSRAC                BRANCH TO PASSWORD ROUT. @921218
MESS     DS    0H
         LA    R1,MSG99
         MVC   MSG99+4(60),MSG03       MSG EDT007E (DEFAULT ROUTCDE)
         WTO   MF=(E,(1))
         B     TSTVSRAC                                         @921218
COMPPRM  CLC   0(0,R3),=C'00'          EXECUTE ONLY
*---------------------------------------------------------------------*
* first call 'TSTVSSEC' to check the security
*---------------------------------------------------------------------*
TSTVSRAC DS    0H                                               @921218
         EXTRACT ADDRTIOT,FIELDS=TIOT  OBTAIN TIOT
         L     R3,ADDRTIOT             TIOT ADDRESS IN REG 3
         CLC   0(8,R3),=CL8'EDIT'      JOBNAME IS 'EDIT'        @911218
         BNE   TSTVSC                  NO: BRANCH               @911224
         OI    SW02,$OPER              MARK IT
TSTVSC   DS    0H                                               @921218
*---------------------------------------------------------------------*
*
*        If you want to bypass the security check with RACF or ACF2
*        please assemble TSTVS with SYSPARM(NOSEC)
*
*---------------------------------------------------------------------*
         AIF   ('&SYSPARM' EQ 'NOSEC').NOSEC
*        B     TSTVSOK                 #### BYPASS CHECK
         CALL  TSTVSSEC,((3))          Do first the sec. check  @921218
         LTR   R15,R15                 OK ??                    @921218
         BZ    TSTVSOK                 yes: use the facility    @921218
         L     R13,4(,R13)             pickup callers savearea  @921218
         RETURN (14,12),RC=8           terminate ....           @921218
.NOSEC   ANOP
TSTVSOK  DS    0H                                               @921218
         GETMAIN RU,LV=320000
         LR    R12,R1
         ST    R12,SAVEGETM            SAVE BEGINADDRESS
         STM   R9,R13,RECOVER          SAVE REGISTERS FOR ESTAE
         ESTAE STAEEXIT                ESTABLISH ESTAE ROUTINE
TSTVSST  DS    0H
         L     R4,SAVEGETM             ADDRESS OF WORKAREA
         L     R5,=A(320000)           LENGTH
         LR    R6,R4                   SAME ADDRESS IN R6
         L     R7,=X'FF000000'         PADDING CHAR X'FF'
         MVCL  R4,R6                   FILLUP AREA WITH X'FF'
         L     R12,SAVEGETM            RESTORE BEGINADDRESS
         CALL  TSTVSCOM,(PARM)         CALL COMMAND PROCESSOR
         B     BRLIST(R15)
BRLIST   B     TSTVSUPD                Update member
         B     TSTVSNEW                New member
         B     TSTVSDEL                Delete the member
         B     TSTVSUPD                Update the member
         B     TSTVSAL                 allocate library
         B     TSTVSEOJ                end of TSTVS
         B     TSTVSREN                Rename a dataset         @930729
TSTVSUPD DS    0H
         BAL   R14,RDDIR               TEST IF MEMBER PRESENT
         CLC   PARM33R(7),=C'PDSLIST'  LISTPDS ?
         BE    TSTVSST                 ASK FOR MEMBER-NAME.
         TM    SW01,SW2                MEMBER PRESENT IN DIRECTORY
         BNO   UPDERR                  GIVE ERROR MSG
         RDJFCB OUTDCB
         MVC   JFCB+44(8),PARM33M
         OI    JFCB+86,X'01'           MARK 'MEMBER IN PDS'
         OPEN  (OUTDCB),TYPE=J
         XR    R2,R2
         OI    SW02,$OPEN              MARK OPENED *RETRY ROUT* @911218
LOAD     DS    0H
         CH    R2,=H'3999'             MAXIMUM LINES REACHED ?
         BE    LOAD9                   YES: BRANCH
         GET   OUTDCB
         LR    R4,R1
         MVC   0(80,R12),0(R4)         STORE RECORD INTO WORKSPACE
         LA    R12,80(,R12)            Next entry in workspace
         LA    R2,1(,R2)               Count record
         B     LOAD
LOAD9    DS    0H
         CLOSE (OUTDCB)
         FREEPOOL OUTDCB               FREE THE BUFFERS
         NI    SW02,255-$OPEN          OUTDCB closed now        @911218
         ST    R2,REG2                 Save GR2
         B     CHANGE
UPDERR   MVC   MSG99+4(60),MSG05
         LA    R1,MSG99
         WTO   MF=(E,(1))              WRITE ERROR MSG EDT010E
         B     TSTVSST                 ASK FOR COMMAND
TSTVSNEW DS    0H
         L     R12,SAVEGETM
         XR    R2,R2                   ZERO IN COUNT REG
TSTVSINP DS    0H
         XC    ECB1,ECB1
         MVC   CARD(80),BLANKS
         WTOR  'EDT003A Enter data, ''SAVE'' OR ''SUBMIT''',           X
               REPLY,72,ECB1,ROUTCDE=(2,3)
BBB      WAIT  ECB=ECB1
         OC    REPLY(72),BLANKS        CHANGE IT TO CAPITALS
         CLC   REPLY(4),=C'SAVE'       SAVE WORKSPACE IN MEMBER ?
         BE    TSTVSCLS                YES: BRANCH
         CLC   REPLY(6),=C'SUBMIT'     SUBMIT ?
         BE    TSTVSCLS                YES: SAVE AND SUBMIT
         OI    SW01,$CHANGED           CHANGE SWITH ON
         TM    PARMSW,NBRS
         BNO   NOSEQ
TSTVSSEQ XC    ECB1,ECB1
         WTOR  'EDT004A Enter sequence number ',                       X
               REPLY2,8,ECB1,ROUTCDE=(2,3)
CCC      WAIT  ECB=ECB1
         LA    R7,8                    MAXIMUM REPLY LENGTH
         XR    R6,R6                   REG 6 ZERO
         LA    R8,REPLY2               LOAD REPLY ADDRESS
SEQ002   DS    0H
         CLI   0(R8),X'40'             Blank found ?
         BE    SEQ007
         CLI   0(R8),X'F0'             NUMERIC TEST
         BL    SEQ004
         CLI   0(R8),X'F9'             NUMERIC TEST
         BH    SEQ004
         LA    R8,1(,R8)               Load next character
         LA    R6,1(,R6)               Length = length+1
         BCT   R7,SEQ002
         B     SEQ007
SEQ004   DS    0H                      Error found in seq. nbr
         LA    R1,MSG99
         MVC   MSG99+4(60),MSG04
         WTO   MF=(E,(1))              WRITE ERROR MSG EDT008E
SEQ005   DS    0H
         MVC   REPLY2(8),BLANKS        CLEAR REPLY AREA
         B     TSTVSSEQ                ASK FOR NEW CARDNUMBER
SEQ007   DS    0H
         LTR   R6,R6                   TEST IF LENGTH ZERO
         BZ    SEQ005                  BRANCH TO WTOR
         BCTR  R6,0                    FOR EX INSTRUCTION
         EX    R6,PACK2                PACK CARDNUMBER
         UNPK  REPLY2(8),DWB
         OI    REPLY2+7,X'F0'          CLEAR SIGN
NOSEQ    TM    PARMSW,UPDT
         BNO   TSTVSI9
         MVC   0(80,R12),CARD
         B     CHANGE
TSTVSI9  DS    0H
         LA    R2,1(,R2)               Number of lines in workspace
         MVC   0(80,R12),CARD
         LA    R12,80(,R12)
         B     TSTVSINP
TSTVSCLS DS    0H
         BAL   R14,SAVE
         CLC   =C'SAVE ',REPLY         Save member requested ?  @930729
         BE    TSTVSST                 YES: end of TSTVS
         BAL   R14,SUB                 Else: submit first
         B     TSTVSST
TSTVSAL  DS    0H
         CALL  TSTVSALL,(PARM)         Allocate the dataset
         B     TSTVSST
TSTVSREN DS    0H                      Rename a dataset         @930729
         OI    PARMSW,$RENDS           Tell TSTVSALL, that a    @930729
*                                      rename should occur      @930729
         CALL  TSTVSALL,(PARM)         Rename the dataset       @930729
         NI    PARMSW,255-$RENDS       Reset flag               @930729
         B     TSTVSST                                          @930729
TSTVSDEL DS    0H                      DELETE MEMBER OR RENAME MEMBER
         B     DEL
TSTVSEOJ DS    0H                      Pickup workspace
         L     R12,SAVEGETM            LOAD ADDRESS WORKSPACE
         ESTAE 0                       KILL ESTAE ROUTINE
         OI    PARMSW,$UNALL           MARK UNALLOC ONLY BY TSTVSALL
         CALL  TSTVSALL,(PARM)
         FREEMAIN RU,LV=320000,A=(R12)
TSTVSEO2 DS    0H
         LA    R2,MSGID
         DOM   MSG=(2)                 delete the start msg     @930729
         L     R13,4(,R13)             Pickup callers save area
         RETURN (14,12),RC=0           BACK TO OS
PACK1    PACK  DWB,0(1,R3)             PACK ROUTING CODE ** EX ONLY **
PACK2    PACK  DWB,REPLY2(1)           PACK CARD NUMBER ** EX ONLY **
         TITLE 'U P D A T E   R O U T I N E'
CHANGE   DS    0H
         OI    SW02,$SUBMODE           MARK SUB COMMAND MODE
         NI    SW01,X'FF'-$LINEDEL     SW DEL. REQUESTED OFF
         XC    ECB1,ECB1
         MVC   REPLY4(60),BLANKS
         WTOR  'EDT005A Enter subcommand or ''HELP''',REPLY4,60,       X
               ECB1,ROUTCDE=(2,3)
DDD      WAIT  ECB=ECB1
         OC    REPLY4(60),BLANKS       TRANSLATE TO CAPITALS
         CLI   REPLY4,C' '
         BE    CHANGE                  NOTHING REPLIED.
         CLC   REPLY4(4),=C'END '      END OF SUBCOMMAND MODE
         BE    CHEND
         CLC   REPLY4(5),=C'SAVE '     SAVE CONTENTS ?
         BNE   CHEOJ                   NO TEST OTHER SUBCOMMANDS
         BAL   R14,SAVE                SAVE MEMBER
         B     CHEND3                  TERMINATE SUBCOMMAND MODE
CHEOJ    DS    0H
         CLC   REPLY4(3),=C'EOJ'       END OF JOB    ?
         BNE   CHSUB                   NO: TEST OTHER COMMANDS
         OI    SW01,$EOJ               SET FLAG
         B     CHEND                   BRANCH
CHSUB    DS    0H
         CLC   REPLY4(6),=C'SUBMIT'    SUBMIT THE WORKAREA ?
         BNE   CHADD                   NO TEST OTHER FONCTIONS
         OI    SW01,$WKSUBM            INDICATE WKAREA SUBMIT
         BAL   R14,SUB                 SUBMIT THE WORKAREA
         B     CHANGE                  NEW SUBCOMMAND
CHADD    DS    0H
         CLC   REPLY4(4),=C'ADD '      ADD JCL AFTER EOF WORKSPACE
         BNE   CHVFY                   NO: TEST OTHER FUNCTIONS
         LR    R12,R2                  # OF LINES IN WORKSPACE
         MH    R12,=H'80'              X RECORD LENGTH
         A     R12,SAVEGETM            + BEGIN ADRESS WORKSPACE
         NI    PARMSW,X'FF'-UPDT       SUMULATE NEW MEMBER
         B     TSTVSINP                ASK FOR INPUT STATEMENTS
CHVFY    CLI   REPLY4,C'V'             LIST LINE AFTER 'C' COMMAND
         BE    VFY
         CLC   REPLY4(4),=C'HELP'      HELP MSG EDT014A
         BNE   CHNG
         LA    R1,MSG99
         MVC   MSG99+4(60),MSG09
         WTO   MF=(E,(1))
         LA    R1,MSG99
         MVC   MSG99+4(60),MSG10
         WTO   MF=(E,(1))
         LA    R1,MSG99
         MVC   MSG99+4(60),MSG11
         WTO   MF=(E,(1))
         MVC   MSG99+4(60),MSG11A
         WTO   MF=(E,MSG99)
         MVC   MSG99+4(60),MSG11B
         WTO   MF=(E,MSG99)
         MVC   MSG99+4(60),MSG11C
         WTO   MF=(E,MSG99)
         MVC   MSG99+4(60),MSG11D
         WTO   MF=(E,MSG99)
         MVC   MSG99+4(60),MSG11E
         WTO   MF=(E,MSG99)
         MVC   MSG99+4(60),MSG11F
         WTO   MF=(E,MSG99)
         MVC   MSG99+4(60),MSG11G
         WTO   MF=(E,MSG99)
         MVC   MSG99+4(60),MSG11H
         WTO   MF=(E,MSG99)
         MVC   MSG99+4(60),MSG11I
         WTO   MF=(E,MSG99)
         MVC   MSG99+4(60),MSG11J
         WTO   MF=(E,MSG99)
         MVC   MSG99+4(60),MSG11K
         WTO   MF=(E,MSG99)
         MVC   MSG99+4(60),MSG11L
         WTO   MF=(E,MSG99)
         MVC   MSG99+4(60),MSG11M
         WTO   MF=(E,MSG99)
         MVC   MSG99+4(60),MSG11N
         WTO   MF=(E,MSG99)
         MVC   MSG99+4(60),MSG11O      LAST HELP LINE
         WTO   MF=(E,MSG99)
         B     CHANGE
CHNG     CLC   REPLY4(4),=C'LIST'      LIST WORKSPACE ? (FIRST 55 POS)
         BNE   CHNGL                   NO: TEST OTHER FUNCTIONS
         NI    SW01,255-$XLST          MARK NORMAL LIST
         BAL   R14,LIST                LIST ROUTINE
         B     CHANGE                  ASK FOR NEW SUBCOMMAND
CHNGL    CLC   REPLY4(2),=C'L '        LIST WORKSPACE ? (ALIAS OF LIST)
         BNE   CHNGXLST                NO: TEST OTHER FUNCTIONS
         NI    SW01,255-$XLST          MARK NORMAL LIST
         BAL   R14,LIST                LIST ROUTINE
         B     CHANGE                  ASK FOR NEW SUBCOMMAND
CHNGXLST CLC   REPLY4(4),=C'XLST'      LIST WORKSPACE ? (80 POS)
         BNE   CHNGNUM                 NO: TEST OTHER FUNCTIONS
         OI    SW01,$XLST              MARK EXTENDED LIST
         BAL   R14,LIST                LIST ROUTINE
         B     CHANGE                  ASK FOR NEW SUBCOMMAND
CHNGNUM  CLC   REPLY4(5),=C'RENUM'     CARDNUMBERS IN COLS 73-80 ?
         BE    RENUM                   YES: BRANCH
         CLC   REPLY4(5),=C'UNNUM'     REMOVE CARDNUMBERS ?
         BE    UNNUM                   YES: BRANCH
         CLI   REPLY4,C'C'             UPDATE INPLACE CHAR STRINGS ?
         BNE   CHINS                   NO: TEST OTHER FUNCTIONS
         CLC   =C'ALL',REPLY4+2        'CHANGE ALL' SUBCOMMAND
         BNE   CHSINGLE                NO: CHANGE SINGLE LINE
         LA    R4,1                    START WITH LINE=1
         L     R12,SAVEGETM            LOAD ADDR WORKSPACE
         OI    SW02,$CHALL             MAKE CHANGE ALL COMMAND  @911218
         OI    SW02,$NOMATCH           DEFAULT NO MATCH FOUND   @911218
*--------------------------------------------------------------------*
* FLAG '$NOMATCH' WILL BE RESET IF THE FIRST CHARACTER STRING IS
* REPLACED
*---------------------------------------------------------------------*
CHALL1   DS    0H                                               @911218
         CLC   0(2,R12),=X'FFFF'       E.O.F.?                  @911218
         BE    CHALL9                  YES: TEST RC             @911218
         CVD   R4,DWB                  CONVERT LINE TO DECIMAL  @911218
         UNPK  REPLY4+2(3),DWB         MAKE ZONED               @911218
         OI    REPLY4+4,X'F0'          REMOVE SIGN              @911218
         BAL   R14,UPDIPL              UPDATE INPLACE ROUTINE   @911218
         LA    R4,1(,R4)               Next line number         @911218
         LA    R12,80(,R12)            Next record in workspace @911218
         B     CHALL1                  LOOP UNTIL ALL LINES DONE@911218
         MVC   MSG99+4(60),MSG12                                @911218
CHALL9   DS    0H                                               @911218
         TM    SW02,$NOMATCH           NO LINE'S CHANGED ?      @911218
         BNO   CHANGE                  NO: BRANCH, YES: GIVE MSG@911218
         MVC   MSG99+4(60),MSG12       MOVE MSG 'TEXT NOT FOUND'@911218
         LA    R1,MSG99                                         @911218
         WTO   MF=(E,(1))              GIVE MSG 'TEXT NOT FOUND'@911218
         B     CHANGE                  ASK FOR NEXT SUBCOMMAND  @911218
CHSINGLE DS    0H
         NI    SW02,255-$CHALL         NO 'C ALL' SUBCOMMAND    @911218
         BAL   R14,UPDIPL              UPDATE INPLACE ROUTINE
         B     CHANGE
CHINS    DS    0H
         CLI   REPLY4,C'I'             LINE INSERTION ?
         BNE   CHREPEAT                NO: TEST OTHER FUNCTIONS
         B     INSERT                  INSERT/DELETE/REPEAT ROUTINE
CHREPEAT DS    0H
         CLC   REPLY4(2),=C'R '        REPEAT THE LINE BEHIND ITSSELF?
         BNE   CHDEL                   TEST OTHER SUBCOMMANDS
         B     INSERT                  INSERT THE REPEATED LINE
CHDEL    DS    0H
         CLI   REPLY4,C'D'             DELETE LINE OR RANGE ?
         BNE   CHPRT
         OI    SW01,$LINEDEL           SW DEL. REQUESTED
         B     INSERT
CHPRT    DS    0H
         CLC   REPLY4(5),=C'PRINT'     Print DATASET ?
         BNE   CHFIND
         BAL   R14,PRT                 BRANCH TO PRINT ROUTINE.
         B     CHANGE
CHFIND   DS    0H
         CLC   REPLY4(2),=C'F '        'F' (FIND) SUBCOMMAND ?
         BNE   NUM                     NO: THEN LINE NUMBER
         BAL   R14,FIND                EXEC FIND ROUTINE
         B     CHANGE
NUM      DS    0H
         LA    R1,REPLY4               OFFSET FOR LINENUM
         BAL   R14,LINENUM             GET LINENUMBER
         LTR   R15,R15                 ERROR IN LINENUM ?
         BNZ   CHANGE                  NEXT SUBCOMMAND
         L     R5,LINENR1              OBTAINED LINENBR
         BCTR  R5,0
         MH    R5,=H'80'
         L     R12,SAVEGETM
         AR    R12,R5
         MVC   MSG99+4(60),0(R12)
         LA    R1,MSG99
         WTO   MF=(E,(1))
         B     TSTVSINP
RENUM    DS    0H
         L     R12,SAVEGETM            LOAD GETM. AREA
         ZAP   DWB,=PL2'10'            FIRST LINE NUMBER
RENUM2   CLC   0(2,12),=X'FFFF'        END OF DATA
         BE    WTONUM                  GIVE MSG EDT013I
         UNPK  72(8,R12),DWB
         OI    79(R12),X'F0'
         AP    DWB,=PL2'10'            INCREMENT VALUE
         LA    R12,80(,R12)            Next record
         B     RENUM2
UNNUM    DS    0H
         L     R12,SAVEGETM            LOAD ADDR WORKSPACE
UNNUM2   DS    0H
         CLC   0(2,R12),=X'FFFF'       E.O.F.?
         BE    CHANGE
         MVC   72(8,R12),BLANKS        REMOVE LINE NUMBERS
         LA    R12,80(,R12)            Next record
         B     UNNUM2
WTONUM   DS    0H
         LA    R1,MSG99
         MVC   MSG99+4(60),MSG08
         WTO   MF=(E,(1))
         B     CHANGE                  ASK FOR NEW SUBCOMMAND
VFY      DS    0H                      LIST OR DONT LIST LINE AFTER
*                                      'C' SUBCOMMAND
         CLC   REPLY4+2(3),=C'ON ' LIST ?
         BE    VFY2
         MVC   REPLY4+2(3),=C'OFF'     MOVE DEFAULT
         NI    SW01,X'FF'-$VFY         SET LISTD ID OFF
         B     VFY3
VFY2     OI    SW01,$VFY               SET LIST ID ON
VFY3     DS    0H                      ISSUE MSG EDT022I
         MVC   ONOFF(3),REPLY4+2       MOVE VERIFY OPERAND
         MVC   MSG99+4(60),MSG18       MOVE IN MSG
         WTO   MF=(E,MSG99)            INFORM OPERATOR
         B     CHANGE
CHEND    DS    0H                      PROCESS 'END'
         TM    SW01,$CHANGED           NOT ALL DATA SAVED ?
         BNO   CHEND3                  BRANCH IF ALL SAVED
CHEND2   XC    ECB1,ECB1               CLEAR ECB
         MVC   REPLY8,BLANKS           BLANK REPLY AREA
         WTOR  'EDT025A *WARNING* nothing saved, Enter ''SAVE'' or ''ENX
               D''',REPLY8,4,ECB1,ROUTCDE=(2,3)
III      WAIT  ECB=ECB1                WAIT FOR EVENT
         CLC   REPLY8(4),=C'END '      NOTHING SAVE FORCED ?
         BE    CHEND3                  SAVE NOTHING IF YES
         CLC   REPLY8(4),=C'SAVE'      SAVE ?
         BNE   CHEND2                  WRONG REPLY
         BAL   R14,SAVE                SAVE THE MEMBER
CHEND3   DS    0H
         NI    SW01,255-$CHANGED       CHANGE SWITCH OFF
         NI    SW02,255-$SUBMODE       RESET SUBCOMMAND MODE FLAG
         TM    SW01,$EOJ               EOJ SUBCOMMAND ENTERED ?
         BO    TSTVSEOJ                YES: TERMINATE TSTVS
         B     TSTVSST                 ASK FOR NEW COMMAND
         TITLE 'L I S T        ROUTINE'
LIST     DS    0H
         STM   R14,R12,SAVER           SAVE REGISTERS
         L     R12,SAVEGETM            STORE ADDR WORKSPACE
         LA    R1,REPLY4+5             PARAMETER FOR LINENUM
         CLC   REPLY4(2),=C'L '        ALIAS OF 'LIST' ??
         BNE   LIST1                   NO 'LIST' OR 'XLST'
         CLI   REPLY4+2,X'40'          NO LINE NUMBER ?
         BE    LIST2                   LIST ENTIRE DATASET
         LA    R1,REPLY4+2             'L' COMMAND (ALIAS OF 'LIST')
         B     LIST1A                  BRANCH
LIST1    DS    0H
         CLI   REPLY4+5,C' '           NO LINE NUMBERS ??
         BE    LIST2                   LIST ENTIRE DATASET
LIST1A   DS    0H
         BAL   R14,LINENUM             OBTAIN LINENBRS OPERANDS
         LTR   R15,R15                 OK
         BNZ   LISTEND
         B     LIST4
LIST2    DS    0H
         ZAP   PACKFLD,=PL3'0'         *** LIST ENTIRE WORKSPACE ***
         ZAP   WTOCNT,=PL2'0'          COUNTER FOR EDT017A CONT MSG
         LH    R3,=H'3999'             MAX # OF LINES IN WORKSPACE
         MH    R3,=H'80'
         AR    R3,R12                  LAST LINE TO DISPLAY
LIST3    DS    0H
         CLC   0(2,R12),=X'FFFF'       EOF WORKSPACE
         BE    LISTEND
         CR    R12,R3                  OUT OF LINE RANGE ?
         BH    LISTEND                 END OF LIST ROUT
         AP    PACKFLD,=PL1'1'
         CP    WTOCNT,=P'16'           16 LINES DISPLAYED ??
         BL    LIST3#                  NO: DISPLAY NEXT LINE
         ZAP   WTOCNT,=PL2'0'          ZERO IN COUNTER
LISTCONT DS    0H
         MVC   REPLY6,BLANKS           CLEAR REPLY AREA
         XC    ECB1,ECB1               CLEAR ECB LIST
         WTOR  'EDT017A Should TSTVS continue this function? Reply Y OrX
                N ',REPLY6,3,ECB1,ROUTCDE=(2,3)
FFF      WAIT  ECB=ECB1
         OC    REPLY6,BLANKS           XLATE TO UPPERCASE
         CLC   REPLY6(2),=C'Y '        CONTINUE ?
         BE    LIST3#                  YES: LIST ANOTHER 16 LINES
         CLC   REPLY6(2),=C'N '        TERMINATE THE DISPLAY
         BNE   LISTCONT                NO: BRANCH TO WTOR
         B     LISTWTO2                YES: GIVE MSG EDT018I
LIST3#   DS    0H
         TM    SW01,$XLST              EXTENDED LIST ?
         BNZ   LIST3B                  YES: DO THAT
LIST3A   DS    0H
         UNPK  MSG99+4(4),PACKFLD      LINE NUMBER BEFORE LINE
         OI    MSG99+7,X'F0'           CLEAR SIGN
         MVI   MSG99+8,X'40'
         MVC   MSG99+9(55),0(R12)      1ST 55 POSITIONS ON CONSOLE
         LA    R1,MSG99
         WTO   MF=(E,(1))
         AP    WTOCNT,=PL1'1'
         LA    R12,80(,R12)            Next record
         B     LIST3
LIST3B   DS    0H
         MVC   MSG99+4(60),BLANKS      BLANK MSG FIELD
         UNPK  MSG99+4(4),PACKFLD      LINE NUMBER BEFORE LINE
         OI    MSG99+7,X'F0'           CLEAR SIGN
         MVC   MSG99+9(7),=C'01...40'  FIRST 40 COLUMNS
         MVC   MSG99+17(40),0(R12)     1ST 40 POSITIONS ON CONSOLE
         LA    R1,MSG99
         WTO   MF=(E,(1))
         AP    WTOCNT,=PL1'1'
         MVC   MSG99+4(4),=C'....'     START 2ND PART OF LINE
         MVC   MSG99+9(7),=C'41...80'  SECOND 40 COLUMNS
         MVC   MSG99+17(40),40(R12)    2ND 40 POSITIONS ON CONSOLE
         LA    R1,MSG99
         WTO   MF=(E,(1))
         AP    WTOCNT,=PL1'1'
         LA    R12,80(,R12)            Next record
         B     LIST3
LIST4    DS    0H                      *** LIST LINE RANGE *****
         L     R2,LINENR1              START LINE
         CVD   R2,DWB                  MAKE DECIMAL
         ZAP   PACKFLD(3),DWB
         SP    PACKFLD,=P'1'           MINUS 1
         BCTR  R2,0                    DECREASE WITH ONE
         MH    R2,=H'80'               X RECORD LENGTH
         AR    R12,R2
         L     R3,LINENR2              SECOND LINE NUMBER
         BCTR  R3,0                    DECREASE WITH ONE
         MH    R3,=H'80'               X RECORD LENGTH
         A     R3,SAVEGETM             LAST LINE TO DISPLAY
         ZAP   WTOCNT,=PL2'0'
         B     LIST3
LISTWTO2 DS    0H                      MSG EDT018I *****
         MVC   MSG99+4(60),MSG14
         LA    R1,MSG99
         WTO   MF=(E,(1))
LISTEND  DS    0H
         LM    R14,R12,SAVER           RESTORE THE REGISTERS
         BR    R14                     RETURN TO CALLER
         TITLE 'P R I N T        ROUTINE'
*---------------------------------------------------------------------*
*        ROUTINE TO PRINT THE WORKSPACE TO CHECK THE                  *
*        CHANGES MADE BY THIS PROGRAM                                 *
*        THE PRINT DATASET IS A SPIN-OFF SYSOUT DATASET               *
*        THIS ROUTINE IS ONLY SUPPORTED BY M.V.S.                     *
*------------------------------------------ (C)-1993 SKYBIRD SYSTEMS -*
         SPACE 2
PRT      DS    0H                      PRINT ROUTINE
         STM   R14,R12,SAVER           SAVE THE REGISTERS
         XC    SYSINT(4),SYSINT        CLEAR INTRDR SPECIFICATION
         TM    SW02,$OPER              ISSUED BY OPER ? (PRIM SUBSYS)
         BNZ   PRTWTO3                 YES: BRANCH
         TRT   REPLY4+6(1),CLSTAB      TEST IF SYSOUT IS VALID
         BNZ   PRT001                  TAKE DEFAULT IF INVALID
         MVC   SYSCLS(1),REPLY4+6      MOVE SYSOUT CLASS OF REPLY
PRT001   DS    0H
         LA    R1,BLKPTR               PREPARE DYNAMIC ALLOCATION
         SVC   99                      ALLOCATE
         MVC   MSG19CLS,SYSCLS         MOVE SYSOUTCLASS IN MESSAGE
         MVI   SYSCLS,C'C'             MOVE DEFAULT SYSOUT CLASS
         LTR   R15,R15                 TEST IF ALLOC O.K. ?
         BNZ   PRTWTO2                 INFORM OPERATOR IF NOT
         MVC   PRTDCB+DCBDDNAM-IHADCB(8),ALCDDNAM
*                                      MOVE GENERATED DDNAME
         OPEN  (PRTDCB,(OUTPUT))
         MVC   HEADMEM,JFCB+44         MOVE MEMBER INTO HEADING
         L     R12,SAVEGETM            LOAD GETM AREA ADDRESS
         ZAP   PACKFLD,=PL1'0'         INITIALIZE LINE COUNTER
         ZAP   LINECNT,=P'99'          FORCE HEADING
         ZAP   PAGECNT,=P'0'           PAGE COUNTER
PRT01    DS    0H                      **** PRINT DATASET *****
         CLC   0(2,R12),=X'FFFF'       END OF AREA ?
         BE    PRT02                   PRINT OK
         CP    LINECNT,=P'54'          BOTTOM OF PAGE REACHED ?
         BL    PRT01A                  PRINT LINE IF NOT
         AP    PAGECNT,=P'1'           PAGECNT = PAGECNT +1
         UNPK  HEADCNT,PAGECNT         UNPACK THE RESULT
         OI    HEADCNT+2,X'F0'         CLEAR SIGN
         PUT   PRTDCB,HEADLINE         WRITE LINE
         MVC   LINE,LINE-1             CLEAR LINE
         PUT   PRTDCB,LINE             WRITE BLANK LINE
         ZAP   LINECNT,=P'2'           INITIALIZE LINECOUNT
PRT01A   DS    0H
         AP    PACKFLD,=PL1'1'         LINE COUNTER
         MVC   LINE,LINE-1             BLANK LINE
         UNPK  LINE+1(4),PACKFLD       TSTVS SEQ NUMBER
         OI    LINE+4,X'F0'            CLEAR SIGN
         MVC   LINE+10(80),0(R12)      CARD IN PRINTLINE
         PUT   PRTDCB,LINE             WRITE LINE
         AP    LINECNT,=P'1'           COUNT LINES
         LA    R12,80(,R12)            Next record
         B     PRT01                   PRINT NEXT LINE
PRT02    DS    0H                      CLOSE DATASET
         CLOSE PRTDCB                  CLOSE AND UNALLOCATE
         FREEPOOL PRTDCB               AND FREE THE BUFFERS
         UNPK  MSG19#L,PACKFLD         # OF LINES
         OI    MSG19#L+3,X'F0'
         MVC   MSG99+4(60),MSG19       MOVE IN MSG
         WTO   MF=(E,MSG99)            INFORM OPERATOR/PROGRAMMER
         B     PRTEND            E N D   O F   R O U T I N E
PRTWTO3  DS    0H                      ISSUE MSG EDT028E
         LA    R1,MSG99
         MVC   MSG99+4(60),MSG24       IS NOT POSSIBLE IF SUBSYSTEM
         WTO   MF=(E,(1))              INFORM OPERATOR
         B     PRTEND
PRTWTO2  DS    0H                      MSG EDT019E
         SYCONVHX IN=ERROR,OUT=ERRORBC,L=2
         SYCONVHX IN=INFO,OUT=INFOBC,L=2
         LA    R1,MSG99                DYN ALLOC FAILED
         MVC   MSG99+4(60),MSG15
         WTO   MF=(E,(1))              INFORM
PRTEND   DS    0H
         LA    R2,INTKEY               REPAIRE INTRDR SPECIFICATION
         LA    R2,0(,R2)               CLEAR HI ORDER BYTE
         ST    R2,SYSINT
         LM    R14,R12,SAVER           RESTORE THE REGISTERS
         BR    R14 RETURN TO CALLER
         TITLE 'DELETE OR RENAME MEMBERS IN DIRECTORY'
DEL      DS    0H                      IF $RENAME ON THEN RENAME
         MVC   NOT,=C'   '             ) ELSE DELETE
         MVC   RENDEL,=CL8' DELETED'   DEFAULT DELETE
         MVC   RTRNTEXT,BLANKS
         OPEN  (PDSDCB,(UPDAT))
         TM    PARMSW,$RENAME          RENAME MEMBER REQUESTED ?
         BZ    DEL2                    NO: DELETE MEMBER
         STOW  PDSDCB,PARM33M,C        RENAME MEMBER
         MVC   RENDEL,=CL8' RENAMED'   CHANGE TEXT
         NI    PARMSW,255-$RENAME      FLAG OFF
         B     DEL4                    BRANCH
DEL2     DS    0H
         STOW  PDSDCB,PARM33M,D        DELETE MEMBER
DEL4     DS    0H
         LTR   R15,R15                 TEST COMPLETION
         BZ    DEL6                    YES: DELETE OK
         MVC   NOT,=C'NOT'             ELSE PRODUCE ERROR MSG
         MVC   RTRNTEXT(12),=C' RETURNCODE '
         CVD   R15,DWB
         UNPK  RTRNTEXT+12(4),DWB
         OI    RTRNTEXT+15,X'F0'
DEL6     DS    0H
         CLOSE (PDSDCB)
         MVC   WTOMEM,PARM33M
         LA    R1,MSG99
         MVC   MSG99+4(60),MSG06
         WTO   MF=(E,(1))              WRITE 'MEMBER DELETED' MSG
         B     TSTVSST                 ASK FOR NEW COMMAND
         TITLE 'READ DIRECTORIES FROM PARTITIONED DATASET'
RDDIR    DS    0H
         STM   R14,R12,SAVER           SAVE REGISTERS
         NI    SW01,X'FF'-SW2
         CLC   PARM33R(7),=C'PDSLIST'  LIST DIRECTORIES OF PDS ?
         BE    PDSL03                  CHECK PRESENCE OF MEMBER
         OPEN  (PDSDCB)
         MVC   BLDLM,PARM33M           Move membername
         BLDL  PDSDCB,BLDLLIST         BLDL  ENTRY SUPPLIED NAME
         LTR   R15,R15                 TEST COMPLETION OF BLDL
         BNZ   PDSL01                  MEMBER NOT PRESENT
         OI    SW01,SW2                MARK MEMBER PRESENT
PDSL01   DS    0H
         CLOSE PDSDCB
         B     PDSL99                  END OF BLDL ROUTINE
PDSL03   DS    0H
         MVC   MSG13(60),BLANKS
         RDJFCB OUTDCB                 PROVIDE JFCB INFORMATION
         OPEN  PDSDCB
         MVC   MSG13(8),=C'EDT016I '   MOVE MSG ID
         MVC   MSG13+8(44),JFCB        MOVE DATASET NAME
         LA    R1,MSG99
         MVC   MSG99+4(60),MSG13
         WTO   MF=(E,(1))              Write header
         LA    R7,16
PDSL05   DS    0H
         READ  DECB,SF,PDSDCB,MEMBAREA,256
         CHECK DECB
         XR    R3,R3
         LA    R4,MEMBAREA
         LH    R3,0(,R4)               # of bytes
         SH    R3,=H'2'                SUBTR. COUNT
         LA    R4,2(,R4)               Point to first member
PDSL07   DS    0H
         LTR   R3,R3                    TEST IF COUNT ZERO
         BZ    PDSL05                  LAST MEMBER IN DIR BLOCK
         CLI   0(R4),X'FF'             LAST MEMBER ?
         BE    PDSL15                  YES: CLOSE PDS
         XR    R1,R1                   clear register           @930726
         IC    R1,PARM33KL             Keylength                @930726
         LTR   R1,R1                   No length ?              @930726
         BZ    PDSL08                  Yes: Branch              @930726
         EX    R1,PDSLCMP              Test with key            @930726
         BL    PDSL13                  NEXT MEMBER IF NOT REACHED
PDSL08   DS    0H                                               @930726
         BCT   R7,PDSL11
         LA    R7,16
PDSL09   DS    0H                                               @930726
         MVC   REPLY6,BLANKS           CLEAR REPLY AREA
         XC    ECB1,ECB1               CLEAR ECB
         WTOR  'EDT017A Should TSTVS continue this function? Reply Y OrX
                N ',REPLY6,3,ECB1,ROUTCDE=(2,3)
GGG      WAIT  ECB=ECB1
         OC    REPLY6,BLANKS           XLATE TO UPPERCASE
         CLC   REPLY6(2),=C'Y '        CONTINUE ?
         BE    PDSL11                  YES: LIST NEXT 16 LINES
         CLC   REPLY6(2),=C'N '        TERMINATE PDSLIST ?
         BNE   PDSL09                  NO: REISSUE WTOR
         MVC   MSG99+4(60),MSG14       GIVE MSG EDT018I
         LA    R1,MSG99                GIVE MSG EDT018I
         WTO   MF=(E,(1))              GIVE MSG EDT018I
         B     PDSL15                  TERMINATE PDSLIST
PDSL11   DS    0H
         MVC   MSG13(60),BLANKS
         MVC   MSG13+8(8),0(R4)        MOVE MEMBERNAME IN MSG
         LA    R1,MSG99
         MVC   MSG99+4(60),MSG13
         WTO   MF=(E,(1))              GIVE MESSAGE TO OPER
PDSL13   DS    0H
         SR    R5,R5                   REG-5 ZERO
         SR    R6,R6                   REG-6 ZERO
         IC    R5,11(R4)               NUMBER OF ADDITIONAL HALFWORDS
         IC    R6,=X'1F'               MAXIMUM OF ADDITIONAL HALFWORDS
         NR    R5,R6                   TEST OF ADD HW NOT GREATER 31
         AR    R5,R5                    MULTIPLY BY 2 BECAUSE HALFWORDS
         LA    R4,12(R5,R4)             LOAD NEXT MEMBER ENTRY
         SR    R3,R5  SUBTRACT LENGTH OF ADD HALFWORDS FROM COUNT
         SH    R3,=H'12'                SUBTRACT OFFSET FROM COUNT
         B     PDSL07                   RETURN
PDSL15   DS    0H
         CLOSE PDSDCB
PDSL99   DS    0H
         LM    R14,R12,SAVER     RESTORE THE REGISTERS
         BR    R14
PDSLCMP  CLC   0(0,R4),PARM33KY        TEST WITH KEY ** exec only **
         TITLE 'I N S E R T  /  D E L E T E  / R E P E A T   LINES'
INSERT   DS    0H
         CLC   REPLY4+2(3),=C'   '       NO LINE NUMBER
         BNE   IN002                     IF NOT THEN NUMERIC TEST
         XC    LINENR1,LINENR1           INSERT BEFORE FIRST LINE
         B     INOK
IN002    DS    0H                        NUMERIC TEST
         LA    R1,REPLY4+2               LOAD PARM LINENUM
         BAL   R14,LINENUM             OBTAIN LINENUMBER
         LTR   R15,R15                 ERROR ?
         BNZ   CHANGE                  END OF ROUTINE
INOK     L     R5,LINENR1              CONVERT LINE-NO TO BIN
         TM    SW01,$LINEDEL           DELETE LINE ?
         BNO   IN001                   BRANCH IF NOT
         LTR   R5,R5                   LINE 0 COULD NOT DEL.
         BZ    INERR                   THEN MSG EDT006E
         CLC   LINENR1,LINENR2         NO SECOND LINE NUMBER ?
         BE    IN000A                  BRANCH IF YES
         ST    R3,DELR3                SAVE R3 FOR OTHER USE
         L     R3,LINENR2              OBTAIN 2ND LINENR
         SR    R3,R5                   2ND LINENR - 1ST LINENR
         LA    R3,1(,R3)               +/+ 1 FOR BCT LOOP
         B     DEL001                  GO TO DELETE ROUT
IN000A   DS    0H                      ONLY FIRST LINENR
         ST    R3,DELR3                SAVE R3
         LA    R3,1                    1 FOR BCT
         B     DEL001
IN001    DS    0H
         LR    R8,R2                   SAVE COUNT REG
         LA    R8,1(,R8)
         CH    R8,=H'3999'
         BH    INERR2                  LINE EXEEDS MAX LINES
         CLC   REPLY4(2),=C'R '        REPEAT FUNCTION ?
         BNE   IN001A                  ELSE INSERT
         OI    SW01,$CHANGED           CHANGE SWITCH ON
         LTR   R5,R5                   LINE NUMBER 0 SPEC. OR OMITTED
         BZ    IN001A                  THEN REPEAT LINE 1.
         BCTR  R5,0                    ELSE REPEAT SPECIFIED LINE
IN001A   DS    0H
         MH    R5,=H'80'               MULTIPLY WITH RECORD LENGTH
         LR    R8,R5
         L     R12,SAVEGETM            STORE ADDR WORKSPACE
         AR    R12,R5                  SHIFT LINES BEHIND
         MVC   HELP1(80),0(R12)        THE LINE TO BE INSERTED
IN003    DS    0H
         CLC   HELP1(2),=X'FFFF'       LAST LINE
         BE    INEND
         LA    R12,80(,R12)            Next record in workspace
         MVC   HELP2(80),0(R12)        SAVE LINE
         MVC   0(80,R12),HELP1
         MVC   HELP1,HELP2
         B     IN003
INERR    DS    0H                      LINE-NUMBER INVALID
         LA    R1,MSG99
         MVC   MSG99+4(60),MSG02       MSG EDT006E
         WTO   MF=(E,(1))
         B     CHANGE                  TRY AGAIN
INERR2   DS    0H
         LA    R1,MSG99                LINES EXCEEDS MAX.
         MVC   MSG99+4(60),MSG07       MSG EDT012E
         WTO   MF=(E,(1))
         B     CHANGE                  ADVICE SPLIT MEMBER
*                                      INTO TWO MEMBERS
INEND    DS    0H
         LA    R2,1(,R2)               COUNT = COUNT +1
         CLC   REPLY4(2),=C'R '        REPEAT FUNCTION ?
         BE    CHANGE                  ASK FOR NEW SUBCOMMAND
         L     R12,SAVEGETM            STORE GETM.AREA
         AR    R12,R8                  RIGHT POSITION
         B     TSTVSINP                ASK FOR JCL-STMNT
DEL001   DS    0H                      DEL LINE FROM GETM.-AREA
         BCTR  R5,0
         MH    R5,=H'80'               R5 FROM INSERT
         L     R12,SAVEGETM
         AR    R12,R5
         ST    R12,DELR12              SAVE FOR 2ND USE
DEL001A  DS    0H
         MVI   DEL003+1,X'00'          CHANGE BC 15 INTO BC 0
         L     R12,DELR12
DEL002   DS    0H
         CLC   0(2,R12),=X'FFFF'       END OF FILE
         BE    DEL005                  DEL LINE   COMPLETED
         OI    SW01,$CHANGED           CHANGE SWITH ON
DEL003   DS    0H
         BC    0,DEL004
         OI    DEL003+1,X'F0'          CHANGE BC 0 INTO BC 15
         BCTR  R2,0                    DECREASE COUNT WITH 1
DEL004   DS    0H
         MVC   0(80,R12),80(R12)
         LA    R12,80(,R12)            Next record
         B     DEL002
DEL005   DS    0H                      NEXT LINE
         BCT   R3,DEL001A              NEXT LINE TO DELETE
         L     R3,DELR3                RESTORE R3
         B     CHANGE
DELR3    DS    F                       FOR SAVE R3
DELR12   DS    F
         TITLE 'CHANGE OLD CHAR. STRING IN NEW CHAR. STRING'
UPDIPL   DS    0H
************* REGISTERS USED          ***/
*/* R2        REGISTER FOR WORKSPACE    */
*/* R3        WORKREGISTER              */
*/* R4        BCT  REGISTER             */
*/* R5        LENGTH CHAR. STRING       */
*/* R6        ADRESS CHAR STRING        */
*/* R7        WORKREGISTER.             */
*/* ALL REGISTERS ARE SAVED             */
         STM   R14,R12,SAVER        SAVE REGISTERS
         CLC   REPLY4+2(3),=X'F0F0F0'  TEST IF LINENUM NUMERIC
         BNH   UPDWTO1                  GIVE ERROR MSG EDT006E
         OI    SW01,SW7                 Mark 'C' Subcommand
         LA    R1,REPLY4+2              Parameter
         BAL   R14,LINENUM              Obtain line number
         NI    SW01,X'FF'-SW7
         LTR   R15,R15              ERROR ?
         BNZ   UPDBK                END OF ROUTINE
         L     R2,SAVEGETM           RESTORE WORKSPACE ADDRESS
         L     R3,LINENR1           OBTAIN LINENUM
         BCTR  R3,0                 DECREASE WITH 1
         MH    R3,=H'80'            RECORD LENGTH IS 80
         AR    R2,R3                RIGHT LINE FOUND
         ST    R2,SAVELST           STORE LINE TO CHANGE
         LA    R4,25                SEARCH MAX 25 TIMES
         XR    R5,R5                COUNT REG FOR LENGTH STRING
         L     R6,OFFSET            OFFSET IN REPLY 4 FROM LINENUM RT
         LR    R8,R6
UPD01A   DS    0H
         CLI   0(R6),C'$'           END OF CHAR STRING ?
         BE    UPD01B               BRANCH IF YES
         LA    R5,1(,R5)            # OF BYTES LENGTH
         LA    R6,1(,R6)            TEST NEXT POSITION
         BCT   R4,UPD01A            TRY AGAIN
         B     UPDWTO2              MSG EDT015E TEXT NOT FOUND
UPD01B   DS    0H
         MVC   UPD01A+6(2),=S(UPD01C) MOVE DIFFERENT BRANCH ADR
         LR    R7,R5                SAVE LENGTH OLD CHAR. STRING
         LA    R4,25                SEARCH MAX 25 TIMES.
         LA    R6,1(,R6)            NEW CHAR. STRING.
         ST    R6,SAVER6
         XR    R5,R5                CLEAR GR. 5
         B     UPD01A               TEST LENGTH NEW STRING
UPD01C   DS    0H
         ST    R7,LENGTH1           SAVE LENGTH OLD CHAR STRING
         ST    R5,LENGTH2           SAVE LENGTH NEW CHAR STRING
         LTR   R7,R7                LENGTH OLD CHAR STRING ZERO
         BZ    UPD02A               BRANCH IF YES.
         LA    R4,79                SEARCH MAX 79 TIMES
         BCTR  R7,0                 DECREASE WITH 1 FOR EX
         SPACE
*** SEARCH OLD CHAR STRING AND REPLACE INTO NEW CHAR STRING ***/
         SPACE
UPD2     DS    0H
         EX    R7,COMPARE           TEST IF STRING PRESENT
         BE    UPD02A               REPLACE INTO NEW STRING
         LA    R2,1(,R2)            NEXT CHARACTER
         BCT   R4,UPD2              TRY AGAIN
         B     UPDWTO2              MSG EDT015E TEXT NOT FOUND
COMPARE  CLC   0(0,R2),0(R8)        **** EXECUTE ONLY ****
UPD02A   DS    0H
         ST    R2,SAVER2            SAVE POSITION IN LINE
         L     R7,LENGTH1
         L     R5,LENGTH2
         SR    R7,R5                DIFFERENT LENGTH.
         C     R7,=F'0'             LENGTHS EQUAL
         BE    UPD3                 NO PROBLEMS REPLACE.
         BH    UPD02B               OLD STRING LONGER.
         L     R3,SAVER2            RESTORE POSITION.
         A     R3,LENGTH1           ADD LENGTH OF OLD STRING
         ST    R3,SAVER3            SAVE END OF OLD STRING IN LINE.
         LPR   R5,R7                MAKE RESULT POSITIVE
         L     R4,SAVELST           LOAD FIRST POS CHANG. LINE
         MVC   CARD(80),0(R4)       SAVE LINE.
         LA    R4,72(,R4)           LAST POSITION IN LINE.
         SR    R4,R3                LENGTH OF REMAINING DATA.
         SR    R4,R5
         AR    R3,R5
         L     R5,SAVELST           RESTORE FIRST POS OF LINE
         SR    R3,R5                OFFSET.
         LR    R5,R3                SAVE OFFSET.
         LA    R3,CARD
         AR    R3,R5
         L     R5,SAVER3
         BCTR  R4,0
         C     R4,=F'0'                NEGATIVE ?
         BL    UPD3                    IF SO NO SHIFT REM DATA
         EX    R4,MOVE2             SHIFT REMAINING DATA
         L     R4,SAVELST           RESTORE CHANGED LINE
         MVC   0(80,R4),CARD        RESTORE CHANGED LINE
         B     UPD3                 REPLACE.
UPD02B   DS    0H
         L     R3,SAVER2            RESTORE POSITTION.
         A     R3,LENGTH1           ADD L OF OLD STRING.
         L     R4,SAVELST           LOAD FIRST POS CH. LINE
         LA    R4,72(,R4)           LAST POSITION IN LINE.
         ST    R3,SAVER3            1ST CHAR AFTER 'OLD STRING'
         SR    R4,R3                LENGTH OF REMAINING DATA
         BCTR  R4,0                 FOR EX. INSTRUCTION.
         MVC   CARD,BLANKS          CLEAR WORK-AREA
         C     R4,=F'0'             LAST CHAR OF 1ST STRING IN C 72 ?
         BL    UPD02B1              YES: DO NOT MOVE REMAINING DATA
*                                   APAR TSTVS001
         EX    R4,MOVE3
UPD02B1  DS    0H
         SR    R3,R7
         AR    R4,R7                SUBST. BLANKS IN REM. DATA
         EX    R4,MOVE4             SHIFT REMAINING DATA TO LEFT
UPD3     DS    0H
****     REPLACE OLD STRING INTO NEW STRING     ***/
         NI    SW02,255-$NOMATCH    AT LEAST 1 ONE LINE CHANGED @911218
         L     R6,SAVER6            RESTORE R6.
         L     R5,LENGTH2
         LTR   R5,R5
         BZ    UPDEND               NEW STRING HAS LENGTH=0.
         BCTR  R5,0                 FOR EX.
         EX    R5,MOVE1             MOVE NEW CHAR STRING
         OI    SW01,$CHANGED
         B     UPDEND               RETURN
MOVE1    MVC   0(0,R2),0(R6)        **** EXECUTE ONLY ****
MOVE2    MVC   0(0,R3),0(R5)        **** EXECUTE ONLY ****
MOVE3    MVC   CARD(0),0(R3)        **** EXECUTE ONLY ****
MOVE4    MVC   0(0,R3),CARD         **** EXECUTE ONLY ****
UPDWTO1  DS    0H
****     MSG EDT006E, INVALID LINE NUMBER       ***/
         MVC   MSG99+4(60),MSG02
         LA    R1,MSG99
         WTO   MF=(E,(1))           GIVE MESSAGE
         B     UPDBK
UPDWTO2  DS    0H
         TM    SW02,$CHALL          'CHANGE ALL COMMAND'        @911218
         BO    UPDBK                NO: DO NOT ISSUE MSG        @911218
****     MSG EDT015E    TEXT NOT FOUND          ***/
         MVC   MSG99+4(60),MSG12
         LA    R1,MSG99
         WTO   MF=(E,(1))           GIVE MESSAGE
         B     UPDBK
UPDEND   DS    0H
         TM    SW01,$VFY            LIST THE LINE ?
         BNO   UPDBK                IF NOT RETURN
         L     R2,SAVELST           LOAD ADRESS CHANGED LINE
         MVC   MSG99+4(60),0(R2)    IN WTO
         LA    R1,MSG99
         WTO   MF=(E,(1))           INFORM OPERATOR
UPDBK    MVC   UPD01A+6(2),=S(UPD01B) RESTORE BRANCH ADR
         LM    R14,R12,SAVER        RESTORE THE REGISTERS
         BR    R14                  RETURN TO CALLER
LENGTH1  DS    F                    LENGTH OF OLD CHAR STRING
LENGTH2  DS    F                    LENGTH OF NEW CHAR STRING
SAVER2   DS    F                    SAVE REG 2.
SAVER3   DS    F                    SAVE REG 3.
SAVER6   DS    F                    SAVE REG 6.
         TITLE 'F I N D      ROUTINE'
*---------------------------------------------------------------------*
*        THIS ROUTINE EXECUTES THE 'F' SUBCOMMAND                     *
*        THE SYNTAX IS 1. 'F  $STRING$' OR 2. 'F'                     *
*        IF ONLY AN 'F' IS ENTERED, A REPEAT FIND OF THE PREVIOUS     *
*        CHARACTER STRING IS EXECUTED                                 *
*-------------------------------------------(C)-1993 SKYBIRD SYSTEMS--*
         SPACE 2
FIND     DS    0H
         STM   R14,R12,SAVER           SAVE THE REGISTERS
         CLI   REPLY4+2,C'$'           DELIMITER FOUND ?
         BNE   RFIND                   NO: ISSUE REPEAT FIND
         LA    R3,REPLY4+3             POINT TO FIRST BYTE OF STRING
         LA    R4,56                   MAX LENGTH OF STRING
         XR    R6,R6                   COUNT LENGTH
         MVC   RSTRING,BLANKS          CLEAR PREVIOUS STRING
         LA    R5,RSTRING
FIND001  DS    0H
         CLI   0(R3),C'$'              END DELIMITER FOUND ?
         BE    FIND007                 YES: BRANCH
         MVC   0(1,R5),0(R3)           MOVE 1 BYTE STRING
         LA    R3,1(,R3)               Next byte in string
         LA    R5,1(,R5)               Incr output address
         LA    R6,1(,R6)               Incr count
         BCT   R4,FIND001              REPEAT UNTIL END DLM FOUND
         MVC   MSG99+4(60),MSG21       MOVE MSG ID
         WTO   MF=(E,MSG99)            ISSUE MSG
         MVC   RSTRING,BLANKS          CLEAR PREVIOUS STRING
         B     FIND999
FIND007  DS    0H
         ST    R6,RLEN                 SAVE LENGTH
         L     R12,SAVEGETM            START ADDRESS
         ST    R12,RFINDAD             SAVE ADDRESS
         B     RFIND003                BRANCH
RFIND    DS    0H
         CLC   RSTRING,BLANKS          NO PREVIOUS STRING SPECIFIED?
         BNE   RFIND001                NO: CONTINUE
         MVC   MSG99+4(60),MSG22       MOVE MSG ID
         WTO   MF=(E,MSG99)            ISSUE MSG
         B     FIND999
RFIND001 L     R12,RFINDAD             START WITH LAST FOUND LINE
RFIND003 CLC   0(2,R12),=X'FFFF'       END OF WORKSPACE ?
         BE    RFINDERR                ERROR.
         LR    R3,R12                  COPY R12
         L     R6,RLEN                 LENGTH OF STRING
         BCTR  R6,0                    -/- FOR EX.
         LA    R4,80                   MAX BCT VALUE
         SR    R4,R6                   MINUS LENGTH OF STRING
         LA    R4,1(,R4)               + 1
RFIND005 DS    0H
         EX    R6,RFINDCMP             COMPARE WITH STRING
         BE    RFIND019                YES: FOUND, DISPLAY LINE
         LA    R3,1(,R3)               NEXT BYTE IN LINE
         BCT   R4,RFIND005             LOOP UNTIL ALL DONE OR FOUND
         LA    R12,80(,R12)            Next record
         B     RFIND003                BRANCH
RFIND019 DS    0H
         MVC   MSG99+9(55),0(R12)      MOVE CONTENTS OF FOUND LINE
         MVI   MSG99+8,C' '
         LR    R5,R12                  CURRENT ADDRESS
         XR    R4,R4                   CLEAR EVEN REG.
         S     R5,SAVEGETM             MINUS START ADDRESS
         D     R4,=F'80'               DIVIDE BY RECORD LENGTH
         LA    R5,1(,R5)               PLUS ONE
         CVD   R5,DWB                  MAKE IT PACKED
         UNPK  MSG99+4(4),DWB          MAKE IT ZONED
         OI    MSG99+7,X'F0'           AND CLEAR SIGN
         WTO   MF=(E,MSG99)            ISSUE MSG WITH FOUND LINE
         MVC   MSG99+9(25),55(R12)     2ND PART OF LINE
         MVC   MSG99+34(30),BLANKS
         WTO   MF=(E,MSG99)            ISSUE MSG WITH 2ND PART OF LINE
         LA    R12,80(,R12)            POINT AFTER FOUND LINE
         ST    R12,RFINDAD             SAVE ITS ADDRESS
         B     FIND999                 END OF ROUTINE
RFINDERR MVC   MSG99+4(60),MSG12       TEXT NOT FOUND
         WTO   MF=(E,MSG99)            TELL OPERATOR
FIND999  DS    0H
         LM    R14,R12,SAVER           RESTORE REGS
         BR    R14                     RETURN TO CALLER
RFINDAD  DC    F'0'
RLEN     DC    F'0'
RFINDCMP CLC   0(0,R3),RSTRING         *** EXECUTE ONLY ***
RSTRING  DC    CL56' '
         TITLE 'S U B M I T  ROUTINE'
*---------------------------------------------------------------------*
*                                                                     *
*        Routine to submit the job in the specified member in the     *
*        JOB execution queue.                                         *
*        An internal reader will be allocated to submit the job.      *
*        If the allocation fails, a S RDRTST command will be issued   *
*        to start a reader procedure to submit the job.            -  *
*                                                                     *
*------------------------------------------ (C)-2019 SKYBIRD SYSTEMS -*
         SPACE 2
SUB      DS    0H                      TO GET BOUNDARY.
         STM   R14,R12,SAVER           SAVE THE REGISTERS
         TM    SW02,$OPER              SUBMIT via SUB=MSTR?
         BNO   SUB000                  No: branch
         LA    R1,MSG99
         MVC   MSG99+4(60),MSG24       Is not possible is subsystem
         WTO   MF=(E,(1))              INFORM OPERATOR
         B     SUBEND2
SUB000   DS    0H
         LA    R1,BLKPTR               LOAD ALLOCATION CONTROL BLK
         SVC   99                      ****** ALLOCATE  *********
         LTR   R15,R15                 TEST IF ALLOC OK ?
         BNZ   SUB009                  ISSUE MSG AND LINK TO SYP120
         MVC   INTRDR+DCBDDNAM-IHADCB(8),ALCDDNAM
*                                      MOVE GENERATED DD-NAME
         OPEN  (INTRDR,(OUTPUT))       ALLOCATION O.K.
         L     R12,SAVEGETM            LOAD WORKSPACE
*        ** FILL ALLOCATED INTRDR DATASET ****
SUB001   DS    0H
         CLC   0(2,R12),=X'FFFF'       END OF DATA IN WORKSPACE ?
         BE    SUB003                  YES: CLOSE THE INTRDR
         PUT   INTRDR,(12)             WRITE
         LA    R12,80(,R12)            Next record in workspace
         B     SUB001                  Return
*        EOD WORKSPACE ****  JOB SUBMITTED M.V.S. *****
SUB003   DS    0H
         CLOSE INTRDR                  FREE =CLOSE
         FREEPOOL INTRDR               Free the buffers
         B     SUBEND                  GO TO END OF THIS ROUTINE
SUB009   DS    0H                      ALLOCATION FAILED.
         SYCONVHX IN=ERROR,OUT=ERRORBC,L=2
*        CONVERT DYN ALLOC ERRORCODE TO EBCDIC
         SYCONVHX IN=INFO,OUT=INFOBC,L=2
*        CONVERT DYN ALLOC INFOCODE TO EBCDIC   *****
         LA    R1,MSG99                PREPARE WTO
         MVC   MSG99+4(60),MSG15       PREPARE WTO
         WTO   MF=(E,(1))              GIVE MSG EDT019E
         SPACE
         MVI   PARMRST,C' '            CLEAR
         MVC   PARMRST+1(L'PARMRST-1),PARMRST  FIELD
         MVC   CMDL(2),=AL2(PARMRST-PARM10) initial command length
SUB010   DS    0H                      Start the rdr if alloc fails
         LA    R2,PARMRST              LOAD REST OF PARM
         LH    R3,CMDL                 LOAD LENGTH
         LA    R4,JFCB                 LOAD ADDRESS DSNAME
         LA    R5,44                   MAX. LENGTH OF DSNAME
SUB010A  DS    0H                      MOVE DSNAME IN PARM
         CLI   0(R4),C' '              DSNAME PASSED ?
         BE    SUB010B                 YES: END OF LOOP
         MVC   0(1,R2),0(R4)           MOVE POSITION
         LA    R2,1(,R2)               NEXT POSITION OUTPUT
         LA    R3,1(,R3)               Count in length
         LA    R4,1(,R4)               NEXT POSITION OUTPUT
         BCT   5,SUB010A
SUB010B  DS    0H                      DSNAME= IN PARM
         MVC   0(4,R2),=C''',M='       MOVE KEYWORD FOR MEMBER
         MVC   4(8,R2),JFCB+44         MOVE MEMBERNAME IN START CMD
         LA    R3,16(,R3)              COUNT LENGTH
         STH   R3,CMDL                 NEW LENGTH
*
*        Length is CMDL + actual length of DSNAME
*        + string ',M= + 8 bytes member name + 4 bytes prefix
*
         MODESET KEY=ZERO              Set key to zero for SVC 34
*
*        Issue SVC 34 to invoke the 'S RDRTST,D=...,M=...' command
*
         LA    R1,CMDL                 Parameter list
         XR    R0,R0
         SVC   34                      ISSUE THE COMMAND *
         MODESET KEY=NZERO             RESTORE PROGRAM STATUS
SUBEND   DS    0H                      END OF SUBMIT ROUTINE
         L     R12,SAVEGETM            BEGIN OF GETM AREA
         LA    R12,2(,R12)
         LA    R2,8                    MAX LENGTH OF JOBNAME
         MVC   JOBNAME(8),BLANKS       CLEAR JOBNAME
         LA    R3,JOBNAME
SUB012   DS    0H                      BUILD 'JOB SUBMITTED' MSG
         CLI   0(R12),C' '             END OF JOBNAME ?
         BE    SUB014
         MVC   0(1,R3),0(R12)          MOVE CHAR BY CHAR
         LA    R3,1(,R3)               Next position
         LA    R12,1(,R12)
         BCT   R2,SUB012               BRANCH
*             J O B          S U B M I T T E D ****
SUB014   DS    0H
         LA    R1,MSG99
         MVC   MSG99+4(60),MSG17       EDT021I JOB SUBMITTED
         WTO   MF=(E,(1))              INFORM OPERATOR
SUBEND2  DS    0H
         NI    SW01,X'FF'-$WKSUBM      SWITCH OFF
         LM    R14,R12,SAVER           RESTORE THE REGISTERS
         BR    R14                     RETURN
         TITLE 'S A V E    R O U T I N E'
SAVE     DS    0H
         STM   R14,R12,SAVER           SAVE REGISTERS
         L     R12,SAVEGETM
         NI    SW01,X'FF'-$CHANGED     CHANGE SWITH OFF
         RDJFCB OUTDCB
SAV      DS    0H
         MVC   REPLY7(8),BLANKS
         XC    ECB1,ECB1               CLEAR ECB AREA
         MVC   HHH-14(8),PARM33M       MEMBER NAME IN WTOR
         WTOR  'EDT026A Enter new name to save your workarea or reply 'X
               'U'' to save in member xxxxxxxx',                       X
               REPLY7,8,ECB1,ROUTCDE=(2,3)
HHH      WAIT  ECB=ECB1
         CLC   REPLY7(2),=C'U '
         BE    SAVE3                   SAVE UNDER NAME OF EDT009A
         CLI   REPLY7,C'$'             NATIONAL CHAR ALSO ALLOWED
         BE    SAV001                  BRANCH IF YES
         CLI   REPLY7,C'@'             ALSO NATIONAL CHARACTER
         BE    SAV001                  BRANCH
         CLI   REPLY7,C'#'             ALSO NATIONAL CHARACTER
         BE    SAV001                  BRANCH
         TM    REPLY7,X'C0'
         BNO   SAV
         TM    REPLY7,X'F0'   FIRST CHAR MUST BE ALPHABETIC
         BO    SAV
SAV001   DS    0H
         MVC   JFCB+44(8),REPLY7       NEWNAME
         B     SAVE5
SAVE3    DS    0H
         MVC   JFCB+44(8),PARM33M
SAVE5    DS    0H
         OI    JFCB+86,X'01'
         OPEN  (OUTDCB,(OUTPUT)),TYPE=J
SAVE6    DS    0H
         CLC   0(2,R12),=X'FFFF'
         BE    SAVE7
         MVC   CARD(80),0(R12)
         PUT   OUTDCB,CARD
         LA    R12,80(,R12)
         B     SAVE6
SAVE7    DS    0H
         L     R12,SAVEGETM
         CLOSE (OUTDCB)
         FREEPOOL OUTDCB
         MVC   MSG20M,JFCB+44          MOVE MEMBER NAME IN MSG
         MVC   MSG99+4(60),MSG20       MOVE IN MSG
         WTO   MF=(E,MSG99)
         LM    R14,R12,SAVER           RESTORE REGISTERS
         BR    R14                     RETURN
         TITLE 'REGULATE LINENUMBERS *** FREE FORMAT ***'
*---------------------------------------------------------------------*
*                                                                     *
*        SCAN THE LINENUMBERS, WHICH ARE OPERANDS IN THE SUBCOMMANDS  *
*        THE LINENUMBERS ARE MAX FOUR POSITIONS LONG.                 *
*        THE OFFSET IN REPLY4 (REPLY FOR SUBCOMMAND) IS PLACED        *
*        IN GENERAL REGISTER 1.                                       *
*        WHEN ONE OF THE LINE NUMBERS IS IN ERROR RC 4 IS GIVEN       *
*        IN REGISTER 15 AND THE MSG EDT006E IS GIVEN.                 *
*        WHEN THE OPERAND IS ONLY 1 LINENUMBER, LINENUMBER 2 IS THE   *
*        SAME AS LINENUMBER 1 (THESE ARE TWO BINARY FULLWORDS TO BE   *
*        GIVEN BACK).                                                 *
*                                                                     *
*------------------------------------------ (C)-2019-Skybird Systems -*
         SPACE 2
LINENUM  DS    0H                      Scan line numbers
         STM   R14,R12,SAVELIN         SAVE REGISTERS
         XC    LINERET,LINERET         Clear return code
         LR    R2,R1                   SAVE REGISTER 1
         ST    R2,OFFSRPL4             SAVE THE OFFSET
         XC    LINENR1,LINENR1         CLEAR FIELDS
         XC    LINENR2,LINENR2
         CLI   0(R2),C' '              NO LINENRS PROVIDED ?
         BE    LINEEND                 END OF ROUTINE IF YES
         LA    R4,4                    MAXIMUM LENGTH OF LINENUM
         XR    R5,R5                   CLEAR GR 5 FOR COUNT POS.
LINE001  DS    0H                      BCT LOOP
         CLI   0(R2),C' '              END OF LINENBR
         BE    LINE005                 ONLY 1 LINENBR PROVIDED
         CLI   0(R2),C'-'              LINENBR RANGE ?
         BE    LINE007                 2ND LINENBR TOO
         CLI   0(R2),C','              COMMA INSTEAD OF HYPHEN ?
         BE    LINE007                 2ND LINENBR TOO.
         CLI   0(R2),C'0'              NUMERIC TEST
         BL    LINEERR
         CLI   0(R2),C'9'              NUMERIC TEST
         BH    LINEERR                 ERROR ROUTINE
         LA    R5,1(,R5)               COUNT POSITIONS
         LA    R2,1(,R2)               NEXT POSITION
         BCT   R4,LINE001
         CLI   0(R2),C' '              ONLY 1 LINENBR
         BNE   LINE007                 BRANCH IF NOT
LINE005  DS    0H
         BAL   R6,LINEBIN              MAKE LINENBR BINARY
         ST    R7,LINENR1              AND STORE THE RESULT
         ST    R7,LINENR2              LINENR2 IS THE SAME AS LINENR1
         LA    R2,1(,R2)
         ST    R2,OFFSET               FOR 'C' SUBCOMMAND
         B     LINEEND
LINE007  DS    0H                      TWO LINENBRS
         TM    SW01,SW7                'C' SUBCOMMAND ?
         BO    LINE005                 THIS SUBCOMMAND HAS NEVER A
*                                      )SECOND LINENUMBER
         LA    R2,1(,R2)
         ST    R2,OFFSET               FOR 'C' SUBCOMMAND
         BAL   R6,LINEBIN              MAKE BINARY
         ST    R7,LINENR1              AND STORE THE RESULT
         ST    R2,OFFSRPL4             SAVE THE OFFSET
         LA    R4,4                    FOR BCT INSTRUCTION
         XR    R5,R5                   CLEAR COUNT REGISTER
LINE009  DS    0H
         CLI   0(R2),C' '              END OF LINENBR 2
         BE    LINE011                 BRANCH IF YES
         CLI   0(R2),C'0'              NUMERIC TEST
         BL    LINEERR                 ERROR
         CLI   0(R2),C'9'              NUMERIC TEST
         BH    LINEERR                 ERROR TOO
         LA    R5,1(,R5)               COUNT POSITION
         LA    R2,1(,R2)               NEXT POSITION
         BCT   R4,LINE009
LINE011  DS    0H
         BAL   R6,LINEBIN              MAKE BINARY
         ST    R7,LINENR2              STORE THE RESULT
         CLC   LINENR2,LINENR1         CHECK VALIDITY OF RANGE
         BNL   LINEEND                 IF LOW INVALID RANGE
LINEERR  DS    0H                      LINENBR ERROR
         MVC   MSG99+4(60),MSG02       MSG EDT006E
         WTO   MF=(E,MSG99)            INFORM OPERATOR
         LA    R2,4
         ST    R2,LINERET              RETURNCODE
LINEEND  DS    0H
         LM    R14,R12,SAVELIN         RESTORE REGISTERS
         L     R15,LINERET             RETURNCODE
         BR    R14                     BACK TO CALLER
*
LINEBIN  DS    0H                      MAKE LINENBRS BINARY
         L     R3,OFFSRPL4             LOAD STARTADDRESS OF LINENBR
         LTR   R5,R5                   LENGTH ZERO ?
         BZ    LINEERR                 NO LINENBR FOUND
         BCTR  R5,0                    FOR EX INSTRUCION
         EX    R5,LINEPK               PACK THE LINE
         CVB   R7,DWB                  MAKE BINARY
         LTR   R7,R7                   CHECK FOR ZERO
         BZ    LINEERR                 ERROR IF ZERO
         C     R7,=F'3999'             MAX NUMBER OF LINES ?
         BH    LINEERR                 ERROR
         BR    R6                      RETURN
LINEPK   PACK  DWB(8),0(0,R3)          **** EXECUTE ONLY ****
         TITLE 'ESTAE EXIT ROUTINE'
STAEEXIT DS    0H
         DROP
         USING *,R15                   GET TEMPORARY ADDRESSABILITY
         USING SDWA,R1                 GET ADDRESSABILITY OVER SDWA
         LM    R9,R13,RECOVER
         DROP  R15
         USING SAVEA,R13,R11,R10,R9    RESTORE ADDRESSABILITY   @911224
         XR    R2,R2                   CLEAR REGISTER 2
         ICM   R2,7,SDWACMPC           INSERT COMPLETION CODE
         SRL   R2,12                   SHIFT OUT USER COMPLETION CODE
         STH   R2,SYSTEMCC             SAVE SYSTEM COMPLETION CODE
         CLI   RETRYCNT,5              MORE THAN 5 RETRIES?     @911218
         BNH   SETRP4                  NO: RETRY                @911218
         SETRP RC=0,DUMP=YES           ELSE PERCOLATE           @911218
         BR    R14                     BRANCH TO RTM            @911218
         DROP  R1                      KILL ADDRESSABILITY
SETRP4   DS    0H                                               @911218
         SETRP RC=4,RETADDR=RET,RETREGS=NO,FRESDWA=YES,DUMP=NO
         BR    R14
SYSTEMCC DS    H
         TITLE 'ESTAE RETRY ROUTINE'
RET      DS    0H
         DROP
         USING *,R15
         LM    R9,R13,RECOVER          RESTORE REGISTERS
         L     R2,REG2                 SAVE COUNT REG
         DROP  R15
         USING SAVEA,R13,R11,R10,R9    RESTORE ADDRESSABILITY   @911224
         XR    R1,R1                   CLEAR REGISTER           @911218
         IC    R1,RETRYCNT             # ABEND RETRIES          @911218
         LA    R1,1(,R1)               Increase                 @911218
         STC   R1,RETRYCNT             AND SAVE                 @911218
         SYCONVHX IN=SYSTEMCC,OUT=SAVECC,L=2
         MVC   MSG23+13(3),SAVECC+1    MOVE COMPLETION CODE
         MVC   MSG99+4(60),MSG23       MSG EDT027D (ABENDXXX DETECTED)
         WTO   MF=(E,MSG99)
         CLOSE OUTDCB                  CLOSE
         TM    SW02,$OPEN              DCB OPENED ?             @911218
         BNO   RET2                    NO: BYPASS FREEPOOL      @911218
         FREEPOOL OUTDCB
         NI    SW02,255-$OPEN          OUTDCB CLOSED NOW        @911218
RET2     DS    0H                                               @911218
         MVC   OUTDCB(DCBLEN),COPYDCB  REFRESH  DCB
         CLOSE PDSDCB                        ALL
         CLOSE PRTDCB                            POSSIBLE DCBS
         NI    PARMSW,255-$RENDS       Reset rename flag        @930729
         TM    SW02,$SUBMODE           SUBCOMMAND MODE ?
         BO    CHANGE                  YES: BRANCH TO CHANGE ROUTINE
         B     TSTVSST                 ELSE ASK FOR COMMAND
REG2     DC    F'0'
RECOVER  DS    5F
SAVECC   DS    CL4
         TITLE 'A L L O C A T I O N     CONTROL BLOCKS'
         SPACE 2
         DS    0F
BLKPTR   DC    AL1(128),AL3(REQBLK)
*
REQBLK   DC    AL1(20)              LENGTH OF REQBLK
VERB     DC    X'01'                DSNAME ALLOC
FLAGS1   DC    X'2000'              FLGS
ERROR    DC    H'0'                 ERRORCODE
INFO     DC    H'0'                 INFOCODE
         DC    A(SYSOUT)            SYSOUT ALLOC
RESERVED DC    F'0'
FLAGS2   DC    F'0'                 FLGS
*
SYSOUT   DS    0F                   BOUNDARY
         DC    A(SYSKEY)            SYSOUT-SPEC
         DC    A(UNALLKEY)          FREE = CLOSE
SYSINT   DC    A(INTKEY)            INTERNAL READER
         DC    AL1(128),AL3(DDNAMKEY)
*
         DS    0H
SYSKEY   DC    X'0018'              SYSOUT SPECIF.
SYSNBR   DC    AL2(1)
SYSLEN   DC    AL2(1)               LENGTH
SYSCLS   DC    C'C'                 SYSOUT-CLASS
*
CLSTAB   DC    256X'FF'                TRANSLATE TABLE FOR SYSOUTCLS
         ORG   CLSTAB+C'A'
         DC    9X'00'
         ORG   CLSTAB+C'J'
         DC    9X'00'
         ORG   CLSTAB+C'S'
         DC    8X'00'
         ORG   CLSTAB+C'0'
         DC    10X'00'
         ORG
*
         DS    0H
UNALLKEY DC    X'001C'              UNALLOCATE AT CLOSE
UNALLNBR DC    AL2(0)
UNALLLEN DS    0H
UNALLPRM DS    0H
*
INTKEY   DC    X'0019'
INTNBR   DC    AL2(1)
INTLEN   DC    AL2(6)
INTPARM  DC    C'INTRDR'
*
         DS    0H
DDNAMKEY DC    X'0055'             DDNAME TU
         DC    AL2(1)
DDNAMLEN DC    AL2(8)              LENGTH
ALCDDNAM DC    CL8' '              DDNAME FROM SVC 99
*
LINECNT  DC    PL2'0'                  FOR PRINT ROUTINE
PAGECNT  DC    PL2'0'                  FOR PRINT ROUTINE
         DC    C' '
LINE     DS    CL121
HEADLINE DC    CL53'1(C) TSTVS SKYBIRD SYSTEMS: PRINT OF MODIFIED MEMBEX
               R '
HEADMEM  DS    CL8
         DC    CL40' '
         DC    C'PAGE '
HEADCNT  DS    CL3
         DC    CL12' '
         TITLE 'DATA CONTROL BLOCKS'
         PRINT  NOGEN
OUTDCB   DCB   LRECL=80,MACRF=(GL,PM),DDNAME=OUTP01,EXLST=EXLST,       X
               DSORG=PS,RECFM=FB,EODAD=LOAD9
DCBLEN   EQU   *-OUTDCB
COPYDCB  DCB   LRECL=80,MACRF=(GL,PM),DDNAME=OUTP01,EXLST=EXLST,       X
               DSORG=PS,RECFM=FB,EODAD=LOAD9
PDSDCB   DCB   DDNAME=INP01,MACRF=R,DSORG=PO,RECFM=F,BLKSIZE=256
INTRDR   DCB   DDNAME=INTRD,MACRF=PM,LRECL=80,RECFM=F,                 X
               BLKSIZE=80,DSORG=PS
PRTDCB   DCB   DDNAME=INTRD,MACRF=PM,LRECL=121,RECFM=FBA,              X
               BLKSIZE=1210,DSORG=PS
         PRINT GEN
         TITLE 'GENERAL EQUATES'
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         TITLE 'VARIABLES, CONSTANTS AND CONTROL BLOCKS'
ECB1     DC    F'0'
MSGID    DC    F'0'                    MSGID of start msg       @930729
CARD     DS    0CL80                   OUTPUT-LINE
REPLY    DC    CL72' '                 REPLY FOR JCLSTMNT
REPLY2   DC    CL8'0'                  REPLY FOR CARDNUMBER
BLANKS   DC    80CL1' '
SAVEGETM DC    F'0'                    START ADDRESS WORKSPACE
SAVELST  DC    F'0'                    SAVE FIELD FOR BLOCK UPDIPL
DWB      DC    D'0'
REPLY4   DC    CL60' '                 REPLY FOR LINE-NUMBER
REPLY6   DC    CL3'   '                REPLY FOR CONTINUATION
REPLY7   DS    CL8
REPLY8   DC    CL4' '                  REPLY FOR EDT025A
*
ADDRTIOT DS    A                       FOR PASSWORD CONTROL
*
SAVELIN  DC    18F'0'                  SAVEAREA FOR LINENUM
OFFSRPL4 DS    F
OFFSET   DS    F                       OFFSET IN REPLY4 FOR 'C' SUBC.
LINENR1  DS    F
LINENR2  DS    F
LINERET  DS    F                       RETURNCODE FOR LINENUM ROUT
*
BLDLLIST DS    0H                      LIST ENTRY FOR BLDL MACRO
N1       DC    AL2(1)                  NUMBER OF ENTRIES
L1       DC    AL2(14)                 LENGTH OF DIR. ENTRY
BLDLM    DS    CL8
         DS    CL6
*
PARM     EQU   *
PARMSW   DC    X'00'
ALLC     EQU   X'80'                   SET BY TSTVSALL
NBRS     EQU   X'20'                   SET BY TSTVSCOM
UPDT     EQU   X'10'                   SET BY TSTVSCOM
$UNALL   EQU   X'08'                   MARK UNALLOC ONLY BY TSTVSALL
$RENAME  EQU   X'04'                   IF ON: RENAME MEMBER     @TSTV02
$RENDS   EQU   X'02'                   IF ON: RENAME A dataset  @930729
PARMROUT DC    X'6000'                 DEFAULT ROUTCDE=(2,3)
*
PARM33   DS    0CL25                   Commarea passed by 'com' @930726
PARM33M  DS    CL8                     MEMBERNAME OF TSTVSCOM
PARM33R  DS    CL8                     Newname from tstvscom    @930726
PARM33KY DS    CL8                     Key 'PDSLIST,key'        @930726
PARM33KL DS    C                       Actual key length        @930726
*
RETRYCNT DC    XL1'00'                 NUMBER OF ABEND RETRIES
SW01     DC    XL1'00'
$EOJ     EQU   128          1 = EOJ SUBCOMMAND
SW7      EQU   64       INDICATE 'C' SUBCOMMAND FOR LINENUM ROUTINE
$WKSUBM  EQU   32                      INDICATE WKAREA SUBMIT
$CHANGED EQU   16           1 IS MODIFCATIONS MADE AND NOT YET SAVED
$VFY     EQU   8            1 = V ON,   0 = V OFF
$LINEDEL EQU   4            IF $LINEDEL=1 LINE DELETION REQUESTED
SW2      EQU   2            IF SW2=1 MEMBERNAME PRESENT
$XLST    EQU   1            IF ON DO AN EXTENDED LIST ELSE A NORMAL
SW02     DC    XL1'00'
$SUBMODE EQU   128          IF ON: SUBCOMMAND MODE ENTERED
$OPER    EQU   64           IF ON: DO NOT ALLOW SUBMIT AND PRINT CMD
*                                  BECAUSE TSTVS IS ATTACHED BY A
*                                  PRIMARY SUBSYSTEM
$OPEN    EQU   32           IF ON: OUTDCB OPENED               @911218
$CHALL   EQU   16           IF ON: BUSY WITH 'C ALL' SUBCMD    @911218
$NOMATCH EQU   8            IF ON: NOT ONLY 1 MATCH IN 'C ALL' @911218
PACKFLD  DC    PL3'0'
WTOCNT   DC    PL2'0'
SAVER    DC    15F'0'
MEMBAREA DC    256X'00'
         DS    0F
EXLST    DC    X'87',AL3(JFCB)
JFCB     DS    CL176
HELP1    DS    CL80
HELP2    DS    CL80
CMDL     DS    H                       Length of command including pfx
         DC    H'0'                    Reserved
PARM10   DC    C'START RDRTST,D='''
PARMRST  DC    CL60' '
         DS    0H
         TITLE 'M E S S A G E    B L O C K'
MSG02    DC    CL60'EDT006E Invalid line-number, reply again'
MSG03    DC    C'EDT007E Parameter error, defaults taken (ROUTCDE=2,3) X
                     '
MSG04    DC    CL60'EDT008E seq. number not numeric, reply again'
MSG05    DC    C'EDT010E Member-name to be updated not present. ReenterX
                     '
MSG06    DC    CL15'EDT011I Member '
WTOMEM   DC    CL8' '
         DC    CL1' '
NOT      DC    CL3'   '
RENDEL   DC    CL8'        '
RTRNTEXT DC    CL25' '
MSG07    DC    C'EDT012E Maximum lines in member, insertion impossible.X
                     '
MSG08    DC    CL60'EDT013I Dataset has been re-numbered'
MSG09    DC    C'EDT014I SUBCOMMANDS                                   X
                     '
MSG10    DC    C'        XXX;       replace the line, EDT003A follows. X
                     '
MSG11    DC    C'        I XXX;     insert a line , EDT003A follows.   X
                     '
MSG11A   DC    C'        D XXX OR D XXX,YYY delete a line or a range ofX
                Lines'
MSG11B   DC    C'        R XXX      repeat the specified line after curX
               rent  '
MSG11C   DC    C'        L(IST),L(IST) x or L(IST) x,y List entire or lX
               inenrs'
MSG11D   DC    C'        XLST       same as list, but 80 positions dispX
               layed '
MSG11E   DC    C'        RENUM;     renumber cols 73-80 of the member  X
                     '
MSG11F   DC    C'        UNNUM;     remove numbers of cols 73-80       X
                     '
MSG11G   DC    C'        V ON/OFF;  list or not list line after "C" comX
               mand  '
MSG11H   DC    C'        ADD;       add lines at the end of the member X
                     '
MSG11I   DC    C'        SUBMIT;    submit the contents of the workareaX
                     '
MSG11J   DC    C'        C XXX,string1$string2$ modify character stringX
               s     '
MSG11K   DC    C'        F $string$ find string or "F" repeat find of sX
               tring '
MSG11L   DC    C'        PRINT x;   print spinoff sysout of workarea   X
                     '
MSG11M   DC    C'        SAVE;      save workarea in the member        X
                     '
MSG11N   DC    C'        END;       end of subcommands and ask for nextX
                cmd. '
MSG11O   DC    C'        EOJ;       terminate TSTVS from subcommand modX
               e.    '
MSG12    DC    CL60'EDT015E string(s) not found'
MSG13    DC    CL8'EDT016I '
MSGNAME  DS    CL52
         DS    0F
MSG14    DC    C'EDT018I function terminated by operator               X
                     '
MSG15    DC    CL30'EDT019E Dyn. ALLOC. errorcode '
ERRORBC  DS    CL4
         DC    CL10' Infocode '
INFOBC   DS    CL4
         DC    12C' '
MSG17    DC    CL12'EDT021I job '
JOBNAME  DS    CL8
         DC    CL10' submitted'
         DC    30C' '
MSG18    DC    C'EDT022I  verification turned '   29
ONOFF    DS    CL3                                 3
         DC    CL28' '
MSG19    DC    C'EDT023I '             MSG AFTER PRINT COMMAND
MSG19#L  DS    CL4                     # OF LINES PRINTED
         DC    C' lines printed in SYSOUT='
MSG19CLS DS    C
         DC    CL22' '
MSG20    DC    CL15'EDT024I member '   MEMBER SUCCESSFULLY SAVED
MSG20M   DC    CL8' '                  SAVED MEMBER
         DC    CL19' successfully saved'
         DC    CL18' '
MSG21    DC    CL60'EDT025E invalid character string'
MSG22    DC    CL60'EDT026E invalid repeat find, no string found'
MSG23    DC    CL60'EDT027D ABENDXXX detected, retry successful' 13
MSG24    DC    CL60'EDT028E allocation subsystem dataset not allowed'
MSG99    WTO   '                                                       X
                    ',ROUTCDE=(2,3),MF=L
ZZZ      EQU   *
*
         TITLE 'L I T E R A L        P O O L'
         LTORG
         TITLE 'DSECTS'
         PRINT NOGEN
         DCBD  DSORG=PS,DEVD=DA
         IHASDWA
         END
