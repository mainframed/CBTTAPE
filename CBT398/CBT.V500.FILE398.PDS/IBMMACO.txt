.* ------------------------------------------------------------------ *
.*                                                                    *
.*      Simplify including most system control block DSECTs           *
.*                                                                    *
.*         ASCB=       (YES|NO, LIST|GEN|NOGEN)                       *
.*                                                                    *
.*         ASXB=       (YES|NO, LIST|GEN|NOGEN)                       *
.*                                                                    *
.*         CVT=        (YES|NO|PREFIX, LIST|GEN|NOGEN)                *
.*                                                                    *
.*         DCB=        (YES|NO, dsorg, devd)                          *
.*                                                                    *
.*         DEB=        (YES|NO, LIST|GEN|NOGEN)                       *
.*                                                                    *
.*         IOB=        (YES|NO, GEN|NOGEN)                            *
.*                                                                    *
.*         JFCB=       (YES|NO, LIST|GEN|NOGEN, dsectname)            *
.*                                                                    *
.*         OUCB=       (YES|NO, GEN|NOGEN)                            *
.*                                                                    *
.*         PSA=        (YES|NO, LIST|GEN|NOGEN)                       *
.*                                                                    *
.*         TCB=        (YES|NO, LIST|GEN|NOGEN)                       *
.*                                                                    *
.*         TIOT=       (YES|NO, GEN|NOGEN, dsectname)                 *
.*                                                                    *
.*         UCB=        (YES|NO|PREFIX, LIST|GEN|NOGEN)                *
.*                                                                    *
.*         VTOC=       n or (n,n,...n)   where n is 1, 2, 3, 4, or 5  *
.*                                                                    *
.* ------------------------------------------------------------------ *
         MACRO
         IBMMAC &ASCB=NO,         ASCB DSECT                           +
               &ASXB=NO,          ASXB DSECT                           +
               &CVT=NO,           CVT DSECT                            +
               &DCB=NO,           DCB DSECT                            +
               &DEB=NO,           DEB DSECT                            +
               &GEN=YES,          MACRO GENERATED STATEMENTS           +
               &IOB=NO,           IOB DSECT                            +
               &JFCB=NO,          JFCB DSECT                           +
               &OUCB=NO,          OUCB DSECT                           +
               &PRINT=ON,         SOURCE STATEMENTS                    +
               &PSA=NO,           PSA DSECT                            +
               &TCB=NO,           TCB DSECT                            +
               &TIOT=NO,          TIOT DSECT                           +
               &UCB=NO,           UCB DSECT                            +
               &VTOC=             VTOC RECORD DSECT
         LCLA  &SUB1,&SUB2
         LCLC  &DEVD,&DSORG,&MGEN
         LCLC  &PREFIX,&PRT,&LIST
         AIF   (T'&PRINT EQ 'O').INIT030
         AIF   ('&PRINT' EQ 'ON').INIT020
         AIF   ('&PRINT' EQ 'OFF').INIT020
         MNOTE 12,'INVALID PRINT OPTION'
         MEXIT
.INIT010 ANOP
&PRT     SETC  'OFF'
         AGO   .INIT030
.INIT020 ANOP
&PRT     SETC  '&PRINT'
.INIT030 ANOP
         AIF   (T'&GEN EQ 'O').INIT060
         AIF   ('&GEN' EQ 'YES').INIT050
         AIF   ('&GEN' EQ 'NO').INIT040
         MNOTE 12,'INVALID GEN OPTION'
         MEXIT
.INIT040 ANOP
&MGEN    SETC  'NOGEN'
         AGO   .INIT060
.INIT050 ANOP
&MGEN    SETC  'GEN'
.INIT060 ANOP
         PRINT  &PRT,&MGEN
.* ------------------------------------------------------------------ *
.*              ASCB                                                  *
.* ------------------------------------------------------------------ *
         AIF    (T'&ASCB EQ 'O').ASCB070
         AIF    ('&ASCB' EQ 'NO').ASCB070
         AIF    ('&ASCB(1)' EQ 'YES').ASCB010
         MNOTE  12,'INVALID ASCB OPTION'
         MEXIT
.ASCB010 ANOP
         AIF    (N'&ASCB EQ 1).ASCB050
         AIF    (N'&ASCB GT 2).ERR0010
         AIF    ('&ASCB(2)' EQ 'LIST').ASCB020
         AIF    ('&ASCB(2)' EQ 'GEN').ASCB030
         AIF    ('&ASCB(2)' EQ 'NOGEN').ASCB040
         MNOTE  12,'INVALID ASCB PRINT OPTION'
         MEXIT
.ASCB020 ANOP
&LIST    SETC   'YES'
         AGO    .ASCB060
.ASCB030 ANOP
&LIST    SETC   'YES'
         PRINT  GEN
         AGO    .ASCB060
.ASCB040 ANOP
         PRINT  NOGEN
         AGO    .ASCB060
.ASCB050 ANOP
&LIST    SETC   'NO'
.ASCB060 ANOP
* ------------------------------------------------------------------- *
*                                                                     *
*               ADDRESS SPACE CONTROL BLOCK                           *
*                                                                     *
* ------------------------------------------------------------------- *
         IHAASCB LIST=&LIST
         PRINT  &PRT,&MGEN
.ASCB070 ANOP
.* ------------------------------------------------------------------ *
.*              ASXB                                                  *
.* ------------------------------------------------------------------ *
         AIF    (T'&ASXB EQ 'O').ASXB070
         AIF    ('&ASXB' EQ 'NO').ASXB070
         AIF    ('&ASXB(1)' EQ 'YES').ASXB010
         MNOTE  12,'INVALID ASXB OPTION'
         MEXIT
.ASXB010 ANOP
         AIF    (N'&ASXB EQ 1).ASXB050
         AIF    (N'&ASXB GT 2).ERR0020
         AIF    ('&ASXB(2)' EQ 'LIST').ASXB020
         AIF    ('&ASXB(2)' EQ 'GEN').ASXB030
         AIF    ('&ASXB(2)' EQ 'NOGEN').ASXB040
         MNOTE  12,'INVALID ASXB PRINT OPTION'
         MEXIT
.ASXB020 ANOP
&LIST    SETC   'YES'
         AGO    .ASXB060
.ASXB030 ANOP
&LIST    SETC   'YES'
         PRINT  GEN
         AGO    .ASXB060
.ASXB040 ANOP
         PRINT  NOGEN
         AGO    .ASXB060
.ASXB050 ANOP
&LIST    SETC   'NO'
.ASXB060 ANOP
* ------------------------------------------------------------------- *
*                                                                     *
*               ADDRESS SPACE CONTROL BLOCK EXTENSION                 *
*                                                                     *
* ------------------------------------------------------------------- *
         IHAASXB LIST=&LIST
         PRINT  &PRT,&MGEN
.ASXB070 ANOP
.* ------------------------------------------------------------------ *
.*              CVT                                                   *
.* ------------------------------------------------------------------ *
&PREFIX  SETC   'NO'
         AIF    (T'&CVT EQ 'O').CVT0080
         AIF    ('&CVT' EQ 'NO').CVT0080
         AIF    ('&CVT(1)' EQ 'PREFIX').CVT0010
         AIF    ('&CVT(1)' EQ 'YES').CVT0020
         MNOTE  12,'INVALID CVT OPTION'
         MEXIT
.CVT0010 ANOP
&PREFIX  SETC   'YES'
.CVT0020 ANOP
         AIF    (N'&CVT EQ 1).CVT0060
         AIF    (N'&CVT GT 2).ERR0060
         AIF    ('&CVT(2)' EQ 'LIST').CVT0030
         AIF    ('&CVT(2)' EQ 'GEN').CVT0040
         AIF    ('&CVT(2)' EQ 'NOGEN').CVT0050
         MNOTE  12,'INVALID CVT PRINT OPTION'
         MEXIT
.CVT0030 ANOP
&LIST    SETC   'YES'
         AGO    .CVT0070
.CVT0040 ANOP
&LIST    SETC   'YES'
         PRINT  GEN
         AGO    .CVT0070
.CVT0050 ANOP
         PRINT  NOGEN
         AGO    .CVT0070
.CVT0060 ANOP
&LIST    SETC   'NO'
.CVT0070 ANOP
* ------------------------------------------------------------------- *
*                                                                     *
*               COMMUNICATION VECTOR TABLE                            *
*                                                                     *
* ------------------------------------------------------------------- *
         CVT    DSECT=YES,LIST=&LIST,PREFIX=&PREFIX
         PRINT  &PRT,&MGEN
.CVT0080 ANOP
.* ------------------------------------------------------------------ *
.*              DCB                                                   *
.* ------------------------------------------------------------------ *
         AIF    (T'&DCB EQ 'O').DCB0050
         AIF    ('&DCB' EQ 'NO').DCB0050
         AIF    ('&DCB(1)' EQ 'YES').DCB0010
         MNOTE  12,'INVALID DCB OPTION'
         MEXIT
.DCB0010 ANOP
         AIF    (N'&DCB EQ 1).DCB0020
         AIF    (N'&DCB GT 3).ERR0030
&DSORG   SETC   '&DCB(2)'
         AIF    (N'&DCB EQ 2).DCB0030
&DEVD    SETC   '&DCB(3)'
         AGO    .DCB0040
.DCB0020 ANOP
&DSORG   SETC   'PS'
.DCB0030 ANOP
&DEVD    SETC   ''
.DCB0040 ANOP
* ------------------------------------------------------------------- *
*                                                                     *
*               DATA CONTROL BLOCK                                    *
*                                                                     *
* ------------------------------------------------------------------- *
         DCBD DSORG=&DSORG,DEVD=&DEVD
.DCB0050 ANOP
.* ------------------------------------------------------------------ *
.*              DEB                                                   *
.* ------------------------------------------------------------------ *
         AIF    (T'&DEB EQ 'O').DEB0070
         AIF    ('&DEB' EQ 'NO').DEB0070
         AIF    ('&DEB(1)' EQ 'YES').DEB0010
         MNOTE  12,'INVALID DEB OPTION'
         MEXIT
.DEB0010 ANOP
         AIF    (N'&DEB EQ 1).DEB0050
         AIF    (N'&DEB GT 2).ERR0110
         AIF    ('&DEB(2)' EQ 'LIST').DEB0020
         AIF    ('&DEB(2)' EQ 'GEN').DEB0030
         AIF    ('&DEB(2)' EQ 'NOGEN').DEB0040
         MNOTE  12,'INVALID DEB PRINT OPTION'
         MEXIT
.DEB0020 ANOP
&LIST    SETC   'YES'
         AGO    .DEB0060
.DEB0030 ANOP
&LIST    SETC   'YES'
         PRINT  GEN
         AGO    .DEB0060
.DEB0040 ANOP
         PRINT  NOGEN
         AGO    .DEB0060
.DEB0050 ANOP
&LIST    SETC   'NO'
.DEB0060 ANOP
* ------------------------------------------------------------------- *
*                                                                     *
*               DATA EXTENT BLOCK                                     *
*                                                                     *
* ------------------------------------------------------------------- *
         IEZDEB  LIST=&LIST
         PRINT  &PRT,&MGEN
.DEB0070 ANOP
.* ------------------------------------------------------------------ *
.*              IOB                                                   *
.* ------------------------------------------------------------------ *
         AIF    (T'&IOB EQ 'O').IOB0050
         AIF    ('&IOB' EQ 'NO').IOB0050
         AIF    ('&IOB(1)' EQ 'YES').IOB0010
         MNOTE  12,'INVALID IOB OPTION'
         MEXIT
.IOB0010 ANOP
         AIF    (N'&IOB EQ 1).IOB0040
         AIF    (N'&IOB GT 2).ERR0030
         AIF    ('&IOB(2)' EQ 'GEN').IOB0020
         AIF    ('&IOB(2)' EQ 'NOGEN').IOB0030
         MNOTE  12,'INVALID IOB PRINT OPTION'
         MEXIT
.IOB0020 ANOP
         PRINT  GEN
         AGO    .IOB0040
.IOB0030 ANOP
         PRINT  NOGEN
.IOB0040 ANOP
* ------------------------------------------------------------------- *
*                                                                     *
*               INPUT/OUTPUT BLOCK                                    *
*                                                                     *
* ------------------------------------------------------------------- *
         IEZIOB
         PRINT  &PRT,&MGEN
.IOB0050 ANOP
.* ------------------------------------------------------------------ *
.*              JFCB                                                  *
.* ------------------------------------------------------------------ *
         AIF    (T'&JFCB EQ 'O').JFCB090
         AIF    ('&JFCB' EQ 'NO').JFCB090
         AIF    ('&JFCB(1)' EQ 'YES').JFCB010
         MNOTE  12,'INVALID JFCB OPTION'
         MEXIT
.JFCB010 ANOP
         AIF    (N'&JFCB EQ 1).JFCB060
         AIF    (N'&JFCB GT 3).ERR0040
         AIF    (T'&JFCB(2) EQ 'O').JFCB060
         AIF    ('&JFCB(2)' EQ 'LIST').JFCB020
         AIF    ('&JFCB(2)' EQ 'GEN').JFCB030
         AIF    ('&JFCB(2)' EQ 'NOGEN').JFCB040
         MNOTE  12,'INVALID JFCB PRINT OPTION'
         MEXIT
.JFCB020 ANOP
&LIST    SETC   'YES'
         AGO    .JFCB060
.JFCB030 ANOP
&LIST    SETC   'YES'
         PRINT  GEN
         AGO    .JFCB060
.JFCB040 ANOP
         PRINT  NOGEN
.JFCB060 ANOP
* ------------------------------------------------------------------- *
*                                                                     *
*               JOB FILE CONTROL BLOCK                                *
*                                                                     *
* ------------------------------------------------------------------- *
         AIF   (N'&JFCB LT 3).JFCB070
&JFCB(3) DSECT
         AGO   .JFCB080
.JFCB070 ANOP
JFCB     DSECT
.JFCB080 ANOP
         IEFJFCBN LIST=&LIST
         PRINT  &PRT,&MGEN
.JFCB090 ANOP
.* ------------------------------------------------------------------ *
.*              OUCB                                                  *
.* ------------------------------------------------------------------ *
         AIF    (T'&OUCB EQ 'O').OUCB050
         AIF    ('&OUCB' EQ 'NO').OUCB050
         AIF    ('&OUCB(1)' EQ 'YES').OUCB010
         MNOTE  12,'INVALID OUCB OPTION'
         MEXIT
.OUCB010 ANOP
         AIF    (N'&IOB EQ 1).OUCB040
         AIF    (N'&IOB GT 2).ERR0100
         AIF    ('&IOB(2)' EQ 'GEN').OUCB020
         AIF    ('&IOB(2)' EQ 'NOGEN').OUCB030
         MNOTE  12,'INVALID OUCB PRINT OPTION'
         MEXIT
.OUCB020 ANOP
         PRINT  GEN
         AGO    .OUCB040
.OUCB030 ANOP
         PRINT  NOGEN
.OUCB040 ANOP
* ------------------------------------------------------------------- *
*                                                                     *
*               RESOURCE MANAGER (RSM) USER CONTROL BLOCK             *
*                                                                     *
* ------------------------------------------------------------------- *
         IRAOUCB
         PRINT  &PRT,&MGEN
.OUCB050 ANOP
.* ------------------------------------------------------------------ *
.*              PSA                                                   *
.* ------------------------------------------------------------------ *
         AIF    (T'&PSA EQ 'O').PSA0070
         AIF    ('&PSA' EQ 'NO').PSA0070
         AIF    ('&PSA(1)' EQ 'YES').PSA0010
         MNOTE  12,'INVALID PSA OPTION'
         MEXIT
.PSA0010 ANOP
         AIF    (N'&PSA EQ 1).PSA0050
         AIF    (N'&PSA GT 2).ERR0050
         AIF    ('&PSA(2)' EQ 'LIST').PSA0020
         AIF    ('&PSA(2)' EQ 'GEN').PSA0030
         AIF    ('&PSA(2)' EQ 'NOGEN').PSA0040
         MNOTE  12,'INVALID PSA PRINT OPTION'
         MEXIT
.PSA0020 ANOP
&LIST    SETC   'YES'
         AGO    .PSA0060
.PSA0030 ANOP
&LIST    SETC   'YES'
         PRINT  GEN
         AGO    .PSA0060
.PSA0040 ANOP
         PRINT  NOGEN
         AGO    .PSA0060
.PSA0050 ANOP
&LIST    SETC   'NO'
.PSA0060 ANOP
* ------------------------------------------------------------------- *
*                                                                     *
*               PREFIXED SAVE AREA                                    *
*                                                                     *
* ------------------------------------------------------------------- *
         IHAPSA  LIST=&LIST
         PRINT  &PRT,&MGEN
.PSA0070 ANOP
.* ------------------------------------------------------------------ *
.*              TCB                                                   *
.* ------------------------------------------------------------------ *
         AIF    (T'&TCB EQ 'O').TCB0070
         AIF    ('&TCB' EQ 'NO').TCB0070
         AIF    ('&TCB(1)' EQ 'YES').TCB0010
         MNOTE  12,'INVALID TCB OPTION'
         MEXIT
.TCB0010 ANOP
         AIF    (N'&TCB EQ 1).TCB0050
         AIF    (N'&TCB GT 2).ERR0070
         AIF    ('&TCB(2)' EQ 'LIST').TCB0020
         AIF    ('&TCB(2)' EQ 'GEN').TCB0030
         AIF    ('&TCB(2)' EQ 'NOGEN').TCB0040
         MNOTE  12,'INVALID TCB PRINT OPTION'
         MEXIT
.TCB0020 ANOP
&LIST    SETC   'YES'
         AGO    .TCB0060
.TCB0030 ANOP
&LIST    SETC   'YES'
         PRINT  GEN
         AGO    .TCB0060
.TCB0040 ANOP
         PRINT  NOGEN
         AGO    .TCB0060
.TCB0050 ANOP
&LIST    SETC   'NO'
.TCB0060 ANOP
* ------------------------------------------------------------------- *
*                                                                     *
*               TASK CONTROL BLOCK                                    *
*                                                                     *
* ------------------------------------------------------------------- *
         IKJTCB  LIST=&LIST
         PRINT  &PRT,&MGEN
.TCB0070 ANOP
.* ------------------------------------------------------------------ *
.*              TIOT                                                  *
.* ------------------------------------------------------------------ *
         AIF    (T'&TIOT EQ 'O').TIOT060
         AIF    ('&TIOT' EQ 'NO').TIOT060
         AIF    ('&TIOT' EQ 'YES').TIOT010
         MNOTE  12,'INVALID TIOT OPTION'
         MEXIT
.TIOT010 ANOP
         AIF    (N'&TIOT EQ 1).TIOT050
         AIF    (N'&TIOT GT 3).ERR0080
         AIF    (T'&TIOT(2) EQ 'O').TIOT040
         AIF    ('&TIOT(2)' EQ 'GEN').TIOT020
         AIF    ('&TIOT(2)' EQ 'NOGEN').TIOT030
         MNOTE  12,'INVALID TIOT PRINT OPTION'
         MEXIT
.TIOT020 ANOP
         PRINT  GEN
         AGO    .TIOT040
.TIOT030 ANOP
         PRINT  NOGEN
.TIOT040 ANOP
         AIF    (T'&TIOT(3) EQ 'O').TIOT050
&TIOT(3) DSECT
.TIOT050 ANOP
* ------------------------------------------------------------------- *
*                                                                     *
*               TASK I/O TABLE                                        *
*                                                                     *
* ------------------------------------------------------------------- *
         IEFTIOT1
         PRINT  &PRT,&MGEN
.TIOT060 ANOP
.* ------------------------------------------------------------------ *
.*              UCB                                                   *
.* ------------------------------------------------------------------ *
&PREFIX  SETC   'NO'
         AIF    (T'&UCB EQ 'O').UCB0080
         AIF    ('&UCB' EQ 'NO').UCB0080
         AIF    ('&UCB(1)' EQ 'PREFIX').UCB0010
         AIF    ('&UCB(1)' EQ 'YES').UCB0020
         MNOTE  12,'INVALID UCB OPTION'
         MEXIT
.UCB0010 ANOP
&PREFIX  SETC   'YES'
.UCB0020 ANOP
         AIF    (N'&UCB EQ 1).UCB0060
         AIF    (N'&UCB GT 2).ERR0090
         AIF    ('&UCB(2)' EQ 'LIST').UCB0030
         AIF    ('&UCB(2)' EQ 'GEN').UCB0040
         AIF    ('&UCB(2)' EQ 'NOGEN').UCB0050
         MNOTE  12,'INVALID UCB PRINT OPTION'
         MEXIT
.UCB0030 ANOP
&LIST    SETC   'YES'
         AGO    .UCB0070
.UCB0040 ANOP
&LIST    SETC   'YES'
         PRINT  GEN
         AGO    .UCB0070
.UCB0050 ANOP
         PRINT  NOGEN
         AGO    .UCB0070
.UCB0060 ANOP
&LIST    SETC   'NO'
.UCB0070 ANOP
* ------------------------------------------------------------------- *
*                                                                     *
*               UNIT CONTROL BLOCK                                    *
*                                                                     *
* ------------------------------------------------------------------- *
         IEFUCBOB LIST=&LIST,PREFIX=&PREFIX
         PRINT  &PRT,&MGEN
.UCB0080 ANOP
.* ------------------------------------------------------------------ *
.*        VTOC records (format-1, -2, -3, -4, or -5)                  *
.* ------------------------------------------------------------------ *
         AIF  (N'&VTOC LT 1).VTOC020
&SUB1    SETA 1
.VTOC010 ANOP
* ------------------------------------------------------------------- *
*                                                                     *
*               VTOC RECORD                                           *
*                                                                     *
* ------------------------------------------------------------------- *
         IECSDSL1 &VTOC(&SUB1)
         AIF  (&SUB1 EQ N'&VTOC).VTOC020
&SUB1    SETA &SUB1+1
         AGO  .VTOC010
.VTOC020 ANOP
         MEXIT
.* ------------------------------------------------------------------ *
.*        ERRORS                                                      *
.* ------------------------------------------------------------------ *
.ERR0010 ANOP
         MNOTE  12,'TOO MANY PARAMETERS FOR ASCB'
         MEXIT
.ERR0020 ANOP
         MNOTE  12,'TOO MANY PARAMETERS FOR ASXB'
         MEXIT
.ERR0030 ANOP
         MNOTE  12,'TOO MANY PARAMETERS FOR DCB'
         MEXIT
.ERR0040 ANOP
         MNOTE  12,'TOO MANY PARAMETERS FOR JFCB'
         MEXIT
.ERR0050 ANOP
         MNOTE  12,'TOO MANY PARAMETERS FOR PSA'
         MEXIT
.ERR0060 ANOP
         MNOTE  12,'TOO MANY PARAMETERS FOR CVT'
         MEXIT
.ERR0070 ANOP
         MNOTE  12,'TOO MANY PARAMETERS FOR TCB'
         MEXIT
.ERR0080 ANOP
         MNOTE  12,'TOO MANY PARAMETERS FOR TIOT'
         MEXIT
.ERR0090 ANOP
         MNOTE  12,'TOO MANY PARAMETERS FOR UCB'
         MEXIT
.ERR0100 ANOP
         MNOTE  12,'TOO MANY PARAMETERS FOR OUCB'
         MEXIT
.ERR0110 ANOP
         MNOTE  12,'TOO MANY PARAMETERS FOR DEB'
         MEXIT
         MEND
