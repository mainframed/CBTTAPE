/* REXX */
/* trace ?i */

/*
     Author:   Seymour Metz

     This EXEC catalogs all of the PO and PS datasets on a volume
     using the extended indirect format. It assumes that all of
     the volumes of a system have a common 3-character prefix,
     that the last three characters are one of DLB, DL2, RES or
     RS2 and that the static system variables &SYSDL1, &SYSDL2,
     &SYSR1 and &SYSR2 will refer to those volumes.

     If you do not supply a catalog then the master catalog is
     implied

*/

arg vol cat recat .

if length(vol) Â¬= 6 then do
   say 'Volume serial' vol 'is not six characters'
   exit
   end

type=right(vol,3)
select
   when type='DLB' then
      volser='&SYSD1'
   when type='DL2' then
      volser='&SYSD2'
   when type='RES' then
      volser='******'
   when type='RS2' then
      volser='&SYSR2'
   otherwise do
      say 'Type' type 'is not DLB, DL2, RES or RS2'
      exit
      end
   end

select
   when cat='' then do
      catalog=""
      recat='RECAT'
      end
   when cat='.' then
      catalog=""
   otherwise
      catalog="CATALOG('"cat"')"
   end

address ISPEXEC 'LMDINIT LISTID(DSLIST) VOLUME('VOL')'
select
   when rc=0 then
      nop
   when rc=8 then do
      address ISPEXEC 'SETMSG  MSG( ISRZ002 )'
      address ISPEXEC 'LOG  MSG( ISRZ002 )'
      exit
      end
   otherwise do
      say 'Return code' rc 'from LMDINIT'
      exit
      end
   end
dsname=left('',44)
address TSO
do forever
   address ISPEXEC 'LMDLIST LISTID(&DSLIST) DATASET(dsname) STATS(YES)'
   select
      when rc=0 then do
         dsn="'"dsname"'"
         select
            when abbrev(dsn,"'FDRABR.V") then
               iterate
            when abbrev(dsn,"'SYS1.VTOCIX.V") then
               iterate
            when abbrev(dsn,"'SYS1.VVDS.V") then
               iterate
            when abbrev(ZDLDSORG,'PS') then
               nop
            when ZDLDSORG='PO' & ZDLDSNTP='PDS' then
               nop
            when ZDLDSORG='VS' then
               iterate
            otherwise do
               say dsn 'has ZDLDSORG='ZDLDSORG 'and' 'ZDLDSNTP='ZDLDSNTP
               iterate
               end
            end
         if recat='RECAT' then
            "DELETE" dsn "NONVSAM NOSCRATCH" catalog
         "DEFINE NONVSAM (NAME("DSN")"       ,
                         "VOLUMES("VOLSER")" ,
                         "DEVICETYPES(0000) )" catalog
         end
      when rc=4 then do
         say 'No datasets found on volume' vol
         exit
         end
      when rc=8 then
         leave
      otherwise do
         say 'Return code' rc 'from LMDLIST'
         exit
         end
      end
   end
address ISPEXEC 'LMDFREE LISTID(&DSLIST)'
select
   when rc=0 then
      nop
   when rc=8 then do
      'SETMSG  MSG( ISRZ002 )'
      'LOG  MSG( ISRZ002 )'
      exit
      end
   otherwise do
      say 'Return code' rc 'rom LMDFREE'
      exit
      end
   end
