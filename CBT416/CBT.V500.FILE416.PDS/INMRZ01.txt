         TITLE 'INMRZ01' - RECEIVE INITIALIZATION EXIT
***********************************************************************
*                                                                     *
*             MODULE NAME = INMRZ01                                   *
*                                                                     *
*             DESCRIPTIVE NAME = RECEIVE INITIALIZATION EXIT          *
*                                                                     *
*             FUNCTION =                                              *
*                EXIT ALLOWS AUTHORIZATION CHECKING FOR USER          *
*                IDS EXECUTING RECEIVE IN BACKGROUND.                 *
*                                                                     *
*                OPERATION =                                          *
*                   SAVE REGISTERS AND ESTABLISH ADDRESSABILITY       *
*                   GET ASCB ADDRESSABILITY                           *
*                   IF REQUEST IS FROM TSO USER,                      *
*                     EXIT WITH RC=0                                  *
*                   GET ASXB ADDRESSABILITY                           *
*                   DROP ASCB ADDRESSABILITY                          *
*                   GET ACEE ADDRESSABILITY                           *
*                   DROP ASXB ADDRESSABILITY                          *
*                   IF USER HAS SPECIAL ATTRIBUTE,                    *
*                     SET BYPASS AUTHORIZATION CHECKING FLAG          *
*                     EXIT WITH RC=0                                  *
*                   IF USERID MATCHES RECEIVE USERID,                 *
*                     EXIT WITH RC=0                                  *
*                   GET DYNAMIC WORK AREA FOR RACF MACRO              *
*                   TRY TO LOCATE USERID IN RACF FILE                 *
*                   IF USERID FOUND,                                  *
*                     EXIT WITH RC=4                                  *
*                   IF USERID NOT FOUND,                              *
*                     SET BYPASS AUTHORIZATION CHECKING FLAG          *
*                     EXIT WITH RC=0                                  *
*                                                                     *
*              NOTES =                                                *
*                                                                     *
*                 REGISTER CONVENTIONS = STANDARD CONVENTIONS.        *
*                    REGISTER  1       = PARAMETER LIST POINTER       *
*                    REGISTER  2       = ASCB AND LATER TCB           *
*                                        ADDRESSABILITY AND           *
*                                        WORK REGISTER                *
*                    REGISTER  3       = ASXB AND LATER TIOT          *
*                                        ADDRESSABILITY AND           *
*                                        WORK REGISTER                *
*                    REGISTERS 4 - 10  = WORK REGISTERS               *
*                    REGISTER  11      = ACEE ADDRESSABILITY          *
*                    REGISTER  12      = BASE REGISTER                *
*                    REGISTER  13      = SAVE AREA POINTER            *
*                    REGISTER  14      = RETURN ADDRESS               *
*                    REGISTER  15      = ENTRY POINT ADDRESS AND      *
*                                        RETURN CODE REGISTER         *
*                                                                     *
*             MACROS =                                                *
*                SAVE, RETURN, EQUATE, GETMAIN, FREEMAIN, ICHEINTY    *
*                                                                     *
*             MAPPING MACROS USED =                                   *
*                IHAACEE       ACEE MAP                               *
*                IHAASCB       ASCB MAP                               *
*                IHAASXB       ASXB MAP                               *
*                                                                     *
*                                                                     *
*             CHANGE ACTIVITY =                                       *
*                02/07/84  WRITTEN.      BC.                          *
*                                                                     *
*                                                                     *
*                                                                     *
***********************************************************************
*
INMRZ01 CSECT
         SAVE  (14,12),,INMRZ01_DELUXE_CHECK_&SYSDATE_&SYSTIME
         LR    R12,R15
         USING INMRZ01,R12             ESTABLISH ADDRESSABILITY
         LR    R7,R1                   PRESERVE PTR TO PARMS
*
*  CHECK TO SEE IF THIS IS A BATCH JOB.  IF IT ISN'T, EXIT.
*
BTCHCHK  EQU   *
         L     R2,16                   GET ADDRESS OF CVT
         L     R2,0(R2)                GET ADDR OF TCB ADDRESS WORD
         L     R2,12(R2)               GET ADDRESS OF CURRENT ASCB
         USING ASCB,R2                 TELL ASSEMBLER
         L     R3,ASCBTSB              GET ADDRESS OF TSB
         LTR   R3,R3                   IF NOT ZERO,
         BNZ   EXIT                      THIS IS A TSO USER
*
*  IF USER HAS SPECIAL ATTRIBUTE, EXIT WITHOUT FURTHER CHECKING
*
         L     R3,ASCBASXB             GET ADDRESS OF ASXB
         DROP  R2
         USING ASXB,R3                 TELL ASSEMBLER
         L     R11,ASXBSENV            GET ADDRESS OF ACEE
         DROP  R3                      DROP ASXB ADDRESSABILITY
         USING ACEE,R11                ESTABLISH ADDRESSABILITY TO ACEE
         TM    ACEEFLG1,ACEESPEC       USER HAVE SPECIAL ATTRIBUTE?
         BNO   CHKMTCH                 IF NOT, CHECK FURTHER
         L     R5,8(R7)                GET ADDRESS TO FLAGS
         OI    0(R5),X'08'             SET BYPASS AUTH CHECKING FLAG
         B     EXIT                    AND EXIT
*
*  IF USERID MATCHES RECEIVE USERID, EXIT WITH RC=0
*
CHKMTCH  EQU   *
         L     R5,24(R7)               GET ADDRESS OF USERID FIELD
         L     R6,28(R7)               GET ADDR OF RECEIVE USERID
         CLC   0(8,R5),0(R6)           ARE THEY THE SAME?
         BNE   LOCUSER                 IF NOT, CHECK FURTHER
         B     EXIT                    IF MATCH, EXIT
*
*  IF USERID IS DEFINED TO RACF, EXIT WITH RC=4.  OTHERWISE,
*    SET THE BYPASS AUTHORIZATION CHECKING FLAG AND EXIT WITH
*    RC=0.
*
LOCUSER  EQU   *
         GETMAIN R,LV=LAREA            GET DYNAMIC WORK AREA
         LR    R10,R1                  PRESERVE POINTER TO AREA
         USING WORKAREA,R10            ESTABLISH ADDRESSABILITY
         MVC   EXLOC(LDATA),DATA       MOVE DATA INTO WORK AREA
         LA    R3,8                    LOAD LENGTH OF USERID
         MVC   USERID+1(8),0(R6)       LOAD RECEIVE USERID
         LA    R4,USERID+8             POINT TO LAST POSSIBLE CHAR
LU10     EQU   *
         CLI   0(R4),X'40'             LOOK FOR LAST ACTUAL CHAR IN ID
         BNE   LU20                    WHEN FOUND, BRANCH
         BCTR  R4,0                    MOVE POINTER BACK
         BCT   R3,LU10                 CHECK PREVIOUS CHARACTER
LU20     EQU   *
         STCM  R3,B'0001',USERID
         LA    R3,LOCAEND-LOCAREA
         ST    R3,LOCAREA
         ICHEINTY LOCATE,TYPE='USR',ENTRY=USERID,                      X
               WKAREA=LOCAREA,MF=(E,EXLOC)
         LR    R3,R15                  SAVE RETURN CODE
         FREEMAIN R,LV=LAREA,A=(10)    FREE THE GETMAINED AREA
         LTR   R3,R3                   USER FOUND?
         BZ    LU30                    IF SO, BRANCH
         L     R5,8(R7)                GET ADDRESS TO FLAGS
         OI    0(R5),X'08'             SET BYPASS AUTH CHECKING FLAG
         B     EXIT                    AND EXIT
LU30     EQU   *
         LA    R15,4                   SET RC = 4
         B     EXITX                   AND EXIT
*
*  EXIT THE EXIT
*
EXIT     EQU   *
         SLR   R15,R15                 SET RETURN CODE TO ZERO
EXITX    EQU   *
         RETURN (14,12),RC=(15)        END THE EXIT
*
         LTORG
*
*  REGISTER EQUATES
*
         EQUATE
*
*  INITIAL DATA FOR WORK AREA
*
DATA    ICHEINTY LOCATE,TYPE='USR',MF=L
LDATA   EQU  *-DATA
*
*  WORK AREA DESECT
*
WORKAREA DSECT
*
EXLOC   ICHEINTY LOCATE,TYPE='USR',MF=L
USERID  DS   AL1
        DS   CL8
LOCAREA DS   F
        DS   CL24
        DS   F
LOCAEND EQU  *
LAREA   EQU  *-WORKAREA
*
*  DSECTS
*
         IHAACEE
         IHAASCB
         IHAASXB
*
         END   INMRZ01
