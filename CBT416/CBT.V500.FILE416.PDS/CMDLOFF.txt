         TITLE 'DELUXE COMMAND AUTHORIZATION INTERFACE'
*
*        COMMAND AUTHORIZATION INTERFACE
*
CMDLOFF CSECT
*********************************************************************** 00022
*                                                                     * 00023
*   COMMAND PROCESSOR FOR COMMAND 'CMDLOFF'                           * 00024
*   THIS COMMAND PROCESSOR ALLOWS ANOTHER COMMAND TO BE ISSUED AFTER  * 00025
*   SETTING THE ECTLOGF BIT TO 1 SO THAT AFTER THE COMMAND FINISHES   * 00026
*   THE TMP WILL LOG THE USER OFF.                                    * 00027
*                                                                     * 00031
*    -COMMAND PROCESSOR                                               * 00053
*       ENTER:                                                        * 00056
*       CMDLOFF 'COMMAND NAME' 'COMMAND OPERANDS'                     * 00057
*         -COMMAND NAME IS THE COMMAND DESIRED FOR EXECUTION.         * 00064
*       THE COMMAND IS ATTACHED .                                     * 00065
*       BEFORE ATTACHING, THE CPPL PASSED TO CMDLOFF IS REBUILT       * 00066
*       FOR THE COMMAND BEING ATTACHED.                               * 00067
*       THE COMMAND BUFFER AND THE ENVIRONMENT CONTROL TABLE ARE      * 00068
*       MODIFIED.                                                     * 00069
*                                                                     * 00070
*********************************************************************** 00071
         SAVE  (14,12),,CMDLOFF_DELUXE_CMD_FRTEND_&SYSDATE_&SYSTIME
         LR    R12,R15          EXTABLISH R12 AS BASE REG
         USING CMDLOFF,R12      TELL ASSEMBLER
         ST    R13,SAVEAREA+4   SAVE OS SAVE AREA ADDRESS
         LR    R6,R13           SAVE IT IN R6
         LA    R13,SAVEAREA     POINT R13 AT OUR SAVEAREA
         ST    R13,8(R6)        STORE OUR SAVEAREA ADDRESS IN OS
         EJECT
         LR    R2,R1                   R2 ---> CPPL
         L     R4,12(R2)               R4 ---> ECT
         USING ECT,R4
         OI    ECTSWS,ECTLOGF     TELL TMP THAT LOGOFF HAS BEEN ENTERED
         DROP  R4
         L     R4,0(R2)                R4 ---> COMMAND BUFFER
         SR    R5,R5
         LH    R5,0(R4)                R5 = LENGTH(CBUF)
         S     R5,F5                   R5 - 4(PREFIX) -1(FOR MOVE)
         EX    R5,UPCASE               XLATE TO UPPERCASE
         LH    R5,0(R4)                R5 = LENGTH(CBUF)
         S     R5,F4                   MINUS LENGTH OF PREFIX (4)
         SR    R6,R6
         LH    R6,2(R4)                R6 = OFFSET TO OPERANDS
         SR    R7,R7
         LA    R7,4(R4)                START OF TEXT
         AR    R7,R6                   R7 ---> OPERANDS
         SR    R5,R6                   R5 = LENGTH(OPERANDS)
         BCTR  R5,0                    MINUS ONE FOR MOVE
         EX    R5,MOVETEXT             CREATE NEW CBUF TEXT
         A     R5,F5                   R5 + $(PREFIX) +1(FROM MOVE)
         STH   R5,CBUF                 STORE NEW CBUF LENGTH
         BAL   R14,FINDOPND            FIND FIRST OPERAND
         STH   R8,OFFSET               AND STORE OFFSET IN CBUF
         BCTR  R8,0                    MINUS ONE FOR MOVE
         EX    R8,MOVECMND             MOVE COMMAND NAME FOR LINK
         LA    R8,CBUF                 ADDRESS OF NEW CBUF
         ST    R8,0(R2)                AND STORE IN CPPL
         LR    R1,R2                   GET CPPL ADDRESS
         SPACE
LINK     LINK  EPLOC=COMMAND           LINK TO COMMAND
         LA    R8,LOFFCMD              ADDRESS OF NEW CBUF
         ST    R8,0(R2)                AND STORE IN CPPL
         LR    R1,R2                   GET CPPL ADDRESS
         SPACE
LINK2    LINK  EPLOC=LOGOFF            LINK TO LOGOFF
         SPACE
         SLR   R15,R15                 LOAD RETURN CODE
         L     R13,SAVEAREA+4          LOAD OS SAVEAREA ADDR IN R13
         RETURN (14,12),RC=(15)        RETURN TO CALLER
         EJECT
FINDOPND LA    R7,TEXT                 R7 ---> COMMAND BUFFER TEXT
         SR    R8,R8                   COUNTER FOR OPERAND OFFSET
         LR    R9,R5                   COMMAND BUFFER LENGTH
         S     R9,F4                   MINUS PREFIX LENGTH
         SPACE
LOOP1    CLI   0(R7),C' '              FOUND BLANK
         BE    LOOP2                       YES..NEXT LOOP
         SPACE
         LA    R7,1(R7)                UPDATE POINTER
         LA    R8,1(R8)                UPDATE OFFSET
         BCT   R9,LOOP1                LOOP IF STILL IN BUFFER
         BR    R14                     AND EXIT
         SPACE
LOOP2    CLI   0(R7),C' '              FOUND BLANK
         BNE   ENDLOOP                     NO...END OF ROUTINE
         SPACE
         LA    R7,1(R7)                UPDATE POINTER
         LA    R8,1(R8)                UPDATE OFFSET
         BCT   R9,LOOP2                LOOP IF STILL IN BUFFER
         BCTR  R8,0                    IF NO OPERANDS, BACK UP ONE
         SPACE
ENDLOOP  BR    R14                     RETURN TO CALLER
         EJECT
         SPACE
         DS   0D
OLDLEN   DC    F'0'                    ORIGINAL CBUF LENGTH
F4       DC    F'4'
F5       DC    F'5'
COMMAND  DC    CL8' '
CBUF     DS   0D                       NEW COMMAND BUFFER
         DC    H'0'                    BUFFER LENGTH
OFFSET   DC    H'0'                    OFFSET TO OPERANDS
TEXT     DC    CL255' '                COMMAND TEXT
BLANKS   DC    CL255' '
UPCASE   OC    4(*-*,R4),BLANKS
MOVETEXT MVC   TEXT,0(R7)              MOVE COMMAND TEXT TO NEW CBUF
MOVECMND MVC   COMMAND(*-*),TEXT       MOVE COMMAND NAME TO WORK AREA
LOGOFF   DC    CL8'LOGOFF  '
LOFFCMD  DC    XL2'000A',XL2'0006',CL7'LOGOFF '
SAVEAREA DC    18F'0'
         EQUATE
         IKJECT
         SPACE
         END
