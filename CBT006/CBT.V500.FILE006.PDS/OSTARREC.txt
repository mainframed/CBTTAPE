/* * Rexx proc: OSTARREC ** Author: RMC20 ***** Date: 7 Mar 1997 *** */
/* ----------------------------------------------------------------- */
/* Purpose: Uncompress multiple datasets from a dataset created by   */
/* OSTARXMT for internet transmission.                               */
/* ----------------------------------------------------------------- */
/*                                                                   */
/*    This software is supplied by Amdahl Corp. for the transmission */
/* of materials between ObjectStar support and it's customers. Any   */
/* other use of this software is at the user's discretion and is not */
/* supported in any way by Amdahl Corp.                              */
/*                                                                   */
/*   The software is supplied as 'freeware' and may be used/modified */
/* by anyone provided they do not then sell it on for profit or      */
/* expect support from Amdahl Corp.                                  */
/*                                                                   */
/*   Limited support MAY in some cases be available from the author. */
/*                                                                   */
/* ----------------------------------------------------------------- */
/*  V 1.02 remove chars which won't ascii transmit.                  */
/*  V 1.03 Make work file same size as input file.                   */
/*  V 1.04 Improve performance by better blocking workfile.          */
/*  V 1.05 Add diagnostic info on error checking failure.            */
/*  V 1.06 Correct SYSUT2 allocation if inputfile in blocks.         */
/*  V 1.07 Correct record counts.                                    */
/*  V 1.08 Provide error message if input truncated.                 */
/*  V 1.09 Allow proc to continue after checksum error.              */
/*  V 1.0A Allow allow use of TRSMAIN PAK files.                     */
/*  V 1.0B Add support for XM1 & XM2 files.                          */
/*  V 1.0C Simplify detection of different input formats.            */
/*  V 1.0D Add autoname option and improve help.                     */
/*  V 1.0E remove | characters, x'4f', which don't translate well.   */
/*  V 1.0F Add fast edc assembler program OSTAREDC.                  */
/*  V 1.0G Add disclaimer.                                           */
/* ----------------------------------------------------------------- */
/* Usage: Add OSTARREC member to a dataset in sysexec/sysproc concat */
/*  then type OSTARREC against a .XM? format dataset in ISPF 3.4.    */
/*  User is prompted, via TSO RECEIVE, for output DSN(s).            */
/* ----------------------------------------------------------------- */
/* Parameter(s): InputDataset autoname                               */
/* ***************************************************************** */
TRSMAIN = "'CSUPP.SHARED.LOADLIB(TRSMAIN)'" /* MODIFY IF AVAILABLE */
OSTAREDC = "'CSUPP.SHARED.LOADLIB(OSTAREDC)'" /* MODIFY IF AVAILABLE */
ADDRESS "TSO"
ARG INDSN P1 .
IF INDSN = ""  THEN CALL HELP
IF INDSN = "?" THEN CALL HELP
IF ABBREV("AUTONAME",P1,4)
THEN AUTONAME = 1
ELSE AUTONAME = 0
RECS = 0
/* PHASE 1 - EVALUATE INPUT */
IF SYSDSN(INDSN) <> "OK"
THEN CALL ERROR "unable to find dataset" INDSN "Please respecify."
X = LASTPOS('.',INDSN)
SFX = SUBSTR(INDSN,X+1)
PFX = SUBSTR(INDSN,1,X-1)
IF SUBSTR(PFX,1,1) = "'"
THEN DO
     QUOTES = 1
     X = LENGTH(SFX)
     SFX = SUBSTR(SFX,1,X-1)
     PFX = SUBSTR(PFX,2)
     END
ELSE QUOTES = 0
X = PROMPT("ON")
X = LISTDSI(INDSN)
SELECT
  WHEN SFX = "XMT"
  THEN IF SYSLRECL <> 88
       THEN CALL ERROR "Invalid LRECL="SYSLRECL" for file with sfx="SFX"."
  WHEN SFX = "XM1"
  THEN IF SYSLRECL <> 1024
       THEN CALL ERROR "Invalid LRECL="SYSLRECL" for file with sfx="SFX"."
  WHEN SFX = "XM2"
  THEN IF SYSLRECL <> 1024
       THEN CALL ERROR "Invalid LRECL="SYSLRECL" for file with sfx="SFX"."
  OTHERWISE
  CALL ERROR "Dsname" INDSN "suffix" SFX "is not XMT, XM1, or XM2."
  END
IF SYSUNITS = "BLOCK" THEN SYSUNITS = ""
/* PHASE 2 - DECOMPRESS IF POSSIBLE */
IF SFX <> "XMT"
THEN DO
     IF SYSDSN(TRSMAIN) <> "OK"
     THEN CALL ERROR "TRSMAIN not available. Cannot decompress data."
     X = LASTPOS('.',INDSN)
     IF SFX = "XM2"
     THEN DO
          XM2 = 1
          ODSN = SUBSTR(INDSN,1,X-1)".XMX1"
          IF SUBSTR(ODSN,1,1) = "'" THEN ODSN = ODSN"'"
          END
     ELSE DO
          XM2 = 0
          ODSN = "OSTARREC.WORKFILE"
          END
     IF SYSDSN(ODSN) = "OK"
     THEN CALL ERROR "Dataset" ODSN "already exists. Please delete it."
     SAY "Uncompressing" INDSN "into" ODSN"."
     SELECT
       WHEN SYSUNIT = "3390" THEN WORK = "27984"
       OTHERWISE
       WORK = 0
       END
     "ALLOCATE FI(SYSPRINT) DA(*) SHR REUSE"
     "ALLOCATE FI(INFILE) DA("INDSN") SHR REUSE"
     IF RC <> 0 THEN CALL ERROR "Unable to allocate" INDSN"."
     "ALLOCATE FI(OUTFILE) DA("ODSN") NEW REUSE TRACKS",
           "SPACE("SYSALLOC*5","SYSALLOC*5")" SYSUNITS "CATALOG"
     IF RC <> 0 THEN CALL ERROR "Unable to allocate" ODSN"."
     "CALL" TRSMAIN "'UNPACK'"
     IF RC <> 0
     THEN CALL ERROR "Unable to uncompress" INDSN". RC="RC"."
     "FREE FI(INFILE,OUTFILE)"
     IF XM2
     THEN EXIT 0
     INDSN = ODSN
     END
/* PHASE 3 - UNPACKAGE XMT FILE */
"ALLOCATE FI(OSTARXMT) DA("INDSN") SHR REUSE"
IF RC = 0
THEN X = LISTDSI(INDSN)
ELSE CALL ERROR "Unable to allocate file" INDSN
SELECT
  WHEN SYSUNIT = "3390" THEN WORK = "27920"
  WHEN SYSUNIT = "3380" THEN WORK = "23440"
  OTHERWISE
  WORK = 0
  END
EXECIO "1 DISKR OSTARXMT ( STEM IN"
PARSE VAR IN1 HEADER VERS DSNS DATE TIME .
IF HEADER = COPIES("OSTARXMT10",4)
THEN NOP
ELSE CALL ERROR "Invalid input dsn" INDSN". Invalid header."
IF SUBSTR(VERS,1,4) = "V1.0"
THEN NOP
ELSE CALL ERROR "Input file is" VERS". You need a new version of 'OSTARREC'."
TOKEN = HEADER
RECS = 1
RCVC = 0
SAY "Now processing input file" INDSN"."
SAY "Created on" DATE "at" TIME", containing" DSNS "dataset(s)."
IF SYSDSN(OSTAREDC) = "OK"
THEN DO
     EXECIO "0 DISKR OSTARXMT ( FINIS"
     DO I = 1 TO DSNS
        DDN.I = "OSTAR"RIGHT(I,3,"0")
        "ALLOCATE FI("DDN.I") NEW DELETE REUSE",
               "SPACE("SYSALLOC","SYSALLOC%10")" SYSUNITS ,
               "LRECL(80) RECFM(F B) BLKSIZE("WORK")"
        END
     PARMS = "'"RIGHT(DSNS,3,"0")"UN"HEADER"'"
     "ALLOCATE FI(SYSOUT) DA(*) SHR"
     "CALL" OSTAREDC PARMS
     EDCRC = RC
     IF EDCRC = 0
     THEN NOP
     ELSE CALL ERROR "Record/file level error checking failed."
     DO I = 1 TO DSNS
        IF AUTONAME
        THEN DO
             IF QUOTES
             THEN PUSH "DA('"PFX".XMX"I"')"
             ELSE PUSH "DA("PFX".XMX"I")"
             END
       "RECEIVE INDD("DDN.I") NODISPLAY"
       "FREE FI("DDN.I")"
        END
     "FREE FI(OSTARXMT)"
     END
ELSE DO
     SAY "Fast error detection program, OSTAREDC, not available."
     "ALLOCATE FI(SYSUT2) NEW DELETE REUSE",
          "SPACE("SYSALLOC","SYSALLOC%10")" SYSUNITS ,
               "LRECL(80) RECFM(F B) BLKSIZE("WORK")"
     IF RC = 0
     THEN NOP
     ELSE CALL ERROR "Error allocating work file"
     DO FOREVER
        EXECIO "1 DISKR OSTARXMT (STEM IN"
        IF RC = 2 THEN LEAVE
        IF RC = 0
        THEN RECS = RECS + 1
        ELSE CALL ERROR "Read error on input file."
        IF SUBSTR(IN1,1,40) = HEADER
        THEN DO
             EXECIO "0 DISKW SYSUT2 (FINIS"
             IF TOKEN = SUBSTR(IN1,41,40)
             THEN TOKEN = HEADER
             ELSE DO
                  TOKEN = HEADER
                  CALL ERROR "File level error checking failed."
                  END
             RCVC = RCVC + 1
             IF AUTONAME
             THEN DO
                  IF QUOTES
                  THEN PUSH "DA('"PFX".XMX"RCVC"')"
                  ELSE PUSH "DA("PFX".XMX"RCVC")"
                  END
             "RECEIVE INDD(SYSUT2) NODISPLAY"
             ITERATE
             END
        ELSE DO
             TOKEN2 = SUBSTR(IN1,1,8)
             DO I = 9 TO 73 BY 8
                TOKEN2 = BITXOR(TOKEN2,SUBSTR(IN1,I,8))
                END
             IF TOKEN2 = SUBSTR(IN1,81,8)
             THEN OUT1 = SUBSTR(IN1,1,80)
             ELSE DO
                  OUT1 = SUBSTR(IN1,1,80)
                  CALL ERROR "Record level error checking failed."
                  END
             END
        TOKEN = BITXOR(TOKEN,SUBSTR(IN1,1,40))
        TOKEN = BITXOR(TOKEN,SUBSTR(IN1,41,40))
        EXECIO "1 DISKW SYSUT2 (STEM OUT"
        IF RC = 0
        THEN NOP
        ELSE CALL ERROR "Write error to temp file"
        END
     EXECIO "0 DISKR OSTARXMT (FINIS"
     EXECIO "0 DISKW SYSUT2 (FINIS"
     "FREE FI(OSTARXMT,SYSUT2)"
     IF SUBSTR(IN1,1,40) = HEADER
     THEN NOP
     ELSE CALL ERROR "Incomplete XMT file. End of file record not found."
     END
IF INDSN = "OSTARREC.WORKFILE" THEN "DELETE OSTARREC.WORKFILE"
EXIT 0
ERROR: PROCEDURE EXPOSE RC RECS
PARSE ARG MSG
SAY " "
SAY "====>" MSG "<===="
SAY " "
SAY "====> Error occurred processing record" RECS "<===="
SAY " "
IF WORD(MSG,2) = 'level'
THEN DO
     SAY "Do you wish to continue after this checksum error? (Y,N)"
     PULL CONT
     IF ABBREV("YES",CONT,1)
     THEN DO
          SAY
          SAY "********************************************************"
          SAY "*    WARNING: The file being rebuilt is corrupted.     *"
          SAY "*                                                      *"
          SAY "*    Do NOT use it for purposes other than diagnosis.  *"
          SAY "*    ================================================  *"
          SAY "********************************************************"
          RETURN
          END
     END
EXIT 16
Help: procedure
SAY "The OSTARREC REXX procedure is used to reconstruct datasets"
SAY "created by the OSTARXMT procedure."
SAY ""
SAY "OSTARXMT creates 3 possible formats of file."
SAY ""
SAY " An XMT file is a single MVS FB LRECL=88 file containing 1 or more"
SAY "MVS sequential or partitioned datasets with error detection data"
SAY "added."
SAY ""
SAY " An XM1 file is a compressed version of an XMT file to allow"
SAY "faster data transmission over the internet.  It has RECFM=FB and"
SAY "LRECL=1024."
SAY ""
SAY " An XM2 file is a HIGHLY compressed version of a single large"
SAY "seqential dataset, e.g. a dump. It is quick to create and send"
SAY "via the internet but has no error detection information.  It has"
SAY "RECFM=FB and LRECL=1024."
SAY ""
SAY "To restore files with OSTARREC do as follows :-"
SAY ""
SAY "1) Upload the file, using BINARY transmission, to a seqential"
SAY "  dataset with RECFM=FB."
SAY "  For XMT format the file should end in .XMT and have LRECL=88."
SAY "  For XM1 format the file should end in .XM1 and have LRECL=1024."
SAY "  For XM2 format the file should end in .XM2 and have LRECL=1024."
SAY ""
SAY "3) Find the uploaded dataset with ISPF 3.4 and type OSTARREC"
SAY "  against it to recreate the original datasets. For XMT and XM1"
SAY "  files you will be prompted to provide dataset names for each"
SAY "  input file in the dataset. Press enter to select the original"
SAY "  name with the high level qualifier changed to your userid as per"
SAY "  the TSO RECEIVE command."
SAY ""
SAY "  For any of the three formats the dataset may be large and you"
SAY " may want to run the job under batch TSO. You can then add the"
SAY " AUTONAME parameter, eg %OSTARREC 'my.dataset.XM1' AUTONAME, which"
SAY " will cause the datasets to be named 'my.dataset.XMXnnn' where nnn"
SAY " is the file number within the XMT or XM1 file."
SAY ""
SAY " For info on how to create OSTARXMT files from MVS files see the"
SAY "OSTARXMT help via the '%OSTARXMT ?' command."
SAY ""
SAY " If you have any problems, or encounter error messages you don't"
SAY "understand then contact the Support Centre."
SAY ""
exit 0
