//DSPACE   JOB (1),'ASM/LINK DSPACE TSO ',CLASS=A,MSGCLASS=A,
//      MSGLEVEL=(1,1),REGION=0M,NOTIFY=&SYSUID
/*ROUTE PRINT LOCAL
//*
//OUTPUT   OUTPUT BUILDING='H',NAME='William J. Smith - 766-2917',
//     MAILTO=('william.smith.dhtu@statefarm.com'),ROOM='2nd Floor'
//ASM      EXEC PGM=ASMA90,
//         PARM='OBJECT,NODECK,RENT'
//SYSPRINT  DD  SYSOUT=*
//SYSPUNCH  DD  DUMMY
//SYSLIB    DD  DISP=SHR,DSN=SYS1.MACLIB
//          DD  DISP=SHR,DSN=SYS1.MODGEN
//SYSUT1    DD  UNIT=DISK,SPACE=(TRK,(2,1))
//SYSLIN    DD  UNIT=DISK,SPACE=(TRK,(2,1)),DSN=&&OBJMOD,
//     DCB=(RECFM=FB,LRECL=80,BLKSIZE=0,DSORG=PS),
//     DISP=(,PASS,DELETE)
//SYSIN     DD  *
DSPACE   TITLE 'DSPACE - DISPLAY DASD SPACE INFORMATION'
********************************************************************
*                                                                  *
* NAME - DSPACE                                                    *
*                                                                  *
* DESCRIPTION - TSO COMMAND TO DISPLAY AVAILABLE SPACE ON ONLINE   *
*               DIRECT ACCESS STORAGE DEVICES                      *
*                                                                  *
* TO USE - ENTER: DSPACE VVVVVV   ,VVVVVV = A VOLSER (1-6 CHARS)   *
*                 DSPACE UUUU     ,UUUU = A UCB ADDRESS (4 CHARS)  *
*                 DSPACE NNNNNNNN ,NNNNNNNN = A UNITNAME (1-8 CHARS*
*                 DSPACE STORAGE  ,FOR ALL STORAGE VOLUMES         *
*                 DSPACE PUBLIC   ,FOR ALL PUBLIC VOLUMES          *
*                 DSPACE PRIVATE  ,FOR ALL PRIVATE VOLUMES         *
*                 DSPACE ONLINE   ,FOR ALL ONLINE VOLUMES (DEFAULT)*
*                                                                  *
*   NOTE: DSPACE ACCEPTS A MASK: DSPACE RES* OR DSPACE 3C*         *
*         DSPACE ALSO ACCEPTS A LIST: DSPACE (VVVVVV,UUUU,...)     *
*         -OR- A COMBINATION THEREOF: DSPACE (WORK*, SYSRES, 91*)  *
*                                                                  *
* LOG -                                                            *
*   12/17/86 L01 PTW IMPLEMENTED                                   *L01
*   05/16/95 DAV ADD 3390 TO DEVTABLE.                             *
*   01/14/99 DAV CHANGE TO WORK WITH NEW I/O GEN, USING UCBSCAN.   *
*                ALSO ADD 3390 DYNAMIC TO DEVTABLE.                *
*   01/27/99 DAV CHANGE TO WORK 4 DIGIT ADDRESS AND CORRECT IXVTOC *
*                NOT SHOWING.                                      *
*   02/22/99 DAV CHANGE TO LSPACE MACRO (FROM THE SVC DIRECT).     *
*                                                                  *
*   05/16/03 WJS Added 3380 as an "Emulated 2105 Shark Device" to  *
*                the devtable.                                     *
*                Major updates to the accompanying ISPF FDSPACE    *
*                dialog largely in support of 4-byte UCBs.         *
*                Tested on a T/REX using z/OS 1.3, TSO/E 3.3.0,    *
*                and ISPF 5.2.0.                                   *
*                                                                  *
*   10/07/14 WJS EAV Support on z/OS 2.1 JES2 HJE7790              *
*                This code remains UNTESTED on pre-z/OS 2.1 images *
*                with or without EAVs.                             *
*                                                                  *
*                Caveats:  This code supports the 95 byte extended *
*                expanded return area contents. The non-EAV DSPACE *
*                supported the 40 byte return area.  The output    *
*                header was expanded to display the free space     *
*                statistics from the track-managed space of the    *
*                volume.  Consequently, the output will WRAP on a  *
*                3270 in mod 2,3,4 flavors with an 80 column       *
*                screen width.  If this code is executed from TSO  *
*                READY mode under control of the Session Manager,  *
*                it SHOULD format correctly with no wrapping.  I   *
*                was unable to test this code at the time with TSO *
*                Session Manager.  If the output header is incor-  *
*                rectly aligned, change the code at label OUTHDR.  *
*                The ISPF support remains to be reworked: It will  *
*                not work as currently written (panel and CLIST)   *
*                due to changes in the offsets of the returned     *
*                results and the content being displayed.          *
*                                                                  *
*                What was changed? Very Little - just made it work *
*                  1. Changed the LIST and EXECUTE forms of the    *
*                     LSPACE macro invocations from EXPMSG to      *
*                     XEXPMSG and added PLISTVER=MAX.  That's it.  *
*                  2. Increased the value of the variable OUTSPACE *
*                     from 50 to 120.  It's used by PUTLINE after  *
*                     a MVC moves it from the return area.  If it  *
*                     is too small, the TMS free space statistics  *
*                     will be truncated upon display.  I over-     *
*                     allocated it by 25% just as the 16 bit code  *
*                     did.                                         *
*                  3. No changes were made to the HELP. This needs *
*                     to be done to document the meaning of the    *
*                     new headers.                                 *
*                  4. In the process of working on the code I      *
*                     discovered errors in the DFSMSdfp Advanced   *
*                     Service manual.  IBM was notified of the     *
*                     errors on page 33 that describe the XEXPMSG  *
*                     returned results.                            *
*                                                                  *
* REF - IBM z/OS Hot Topics Newsletter, Issue 20, March 2009,      *
*       "LSPACE: Above and Beyond", pp. 36-38, by Keith Cowden,    *
*       Jan Redding, and William Smith,                            *
*       http://publibz.boulder.ibm.com/epubs/pdf/e0z2n191.pdf      *
*                                                                  *
*     - z/OS V2R21.0 DFSMSdfp Advanced Sevices, SC23-6861-01       *
*                                                                  *
********************************************************************
         EJECT
********************************************************************
*                                                                  *
*        I N I T I A L I Z E                                       *
*                                                                  *
********************************************************************
         LCLC  &CSECT
&CSECT   SETC  'DSPACE'
&CSECT   CSECT
&CSECT   AMODE 31
&CSECT   RMODE ANY
R0       EQU   00
R1       EQU   01
R2       EQU   02                 WORK
R3       EQU   03                 WORK
R4       EQU   04                 WORK
R5       EQU   05                 WORK
R6       EQU   06                 WORK
R7       EQU   07                 UCB ADR
R8       EQU   08                 LSPACE PARM ADR
R9       EQU   09
R10      EQU   10
R11      EQU   11
R12      EQU   12                 PROGRAM BASE.
R13      EQU   13                 A(SAVE AREA).
R14      EQU   14
R15      EQU   15
*
DOSAVE   EQU   *
         SAVE  (14,12),,&CSECT--&SYSDATE--&SYSTIME
         LR    R12,R15            ESTABLISH BASE.
         USING &CSECT,R12         TELL ASSEMBLER.
         GETMAIN RU,LV=DYNLEN     DYNAMIC AREA.
         EJECT
DOPTRS   EQU   *
         LR    R2,R13             SAVE AREA
         LR    R13,R1               SWITCH.
         USING DYNAMIC,R13        TELL ASSEMBLER.
         LR    R0,R1
         LA    R1,DYNLEN
         LA    R14,R0
         SLR   R15,R15
         MVCL  R0,R14             ZERO STORAGE.
         ST    R2,4(,R13)         BACK CHAIN.
         ST    R13,8(,R2)         FORWARD CHAIN.
         XC    RC,RC
         L     R2,4(,R13)
         L     R2,24(,R2)         R2 = A(PARMLIST).
         USING CPPL,R2            CONSTRUCT TSO CONTROL BLOCKS.
         MVC   MYIOPB(IOPBLEN),XIOPB INITIALIZE DYNAMIC IOPB.
         LA    R3,MYPPL           BUILD PPL.
         USING PPL,R3
         MVC   PPLUPT,CPPLUPT
         MVC   PPLECT,CPPLECT
         LA    R4,ECB
         ST    R4,PPLECB
         MVC   PPLPCL,=A(MYPCL)
         LA    R4,MYANS
         ST    R4,PPLANS
         MVC   PPLCBUF,CPPLCBUF
         XC    PPLUWA,PPLUWA
         DROP  R3
         LA    R3,MYIOPL          BUILD IOPL.
         USING IOPL,R3
         MVC   IOPLUPT,CPPLUPT
         MVC   IOPLECT,CPPLECT
         LA    R4,ECB
         ST    R4,IOPLECB
         LA    R4,MYIOPB
         ST    R4,IOPLIOPB
         DROP  R2,R3
         MVC   OUTAREA(4),=AL2(OUTAREAL,0) INITIALIZE PUTLINE MSG.
         CALLTSSR EP=IKJPARS,MF=(E,MYPPL) CALL PARSE.
         LTR   R15,R15
         BNZ   BADPARS
         L     R2,MYANS           R2 = A(PDL FROM PARSE)
         USING MYPDL,R2
         LA    R2,VOLUMES         R2 = A(VOLUME POSITIONAL PDE).
         DROP  R2
         XC    COUNT,COUNT
         XC    SCANWORK,SCANWORK  CLEAR SCANWORK AREA
         XC    UCBCOPY,UCBCOPY    CLEAR UCBCOPY  AREA
         XC    UCBEXTN,UCBEXTN    CLEAR UCBEXTN  AREA
         MVI   DCETEXT,X'00'      START CLEAR OF DCE AREA
         MVC   DCETEXT+1(L'DCETEXT-1),DCETEXT
         LA    R8,LSPCPRMW        -> R8 TO LSPACE PARM WORKAREA
         USING LSPCPRM,R8         R8 = A(LSPACE PARM WORKAREA)
         LA    R0,LSPCMSG         GET ADR TO LSPCMSG
         ST    R0,LSPCMSGA        STORE IT IN LSPCMSGA
         EJECT
********************************************************************
*                                                                  *
*        G E T   1 S T   T H I N G   I N   L I S T                 *
*                                                                  *
********************************************************************
LOOPPDE  DS    0H
         MVI   TYPE,0
         MVI   TRY,0
         MVI   ATTR,0
         ST    R2,PDEADDR         SAVE A(PDE).
         MVI   VALUE,C' '
         MVC   VALUE+1(L'VALUE-1),VALUE
         L     R3,0(,R2)          R3 = A(PDE).
         LH    R4,4(,R2)          R5 = L'VALUE.
         STH   R4,VALUELEN
         BCTR  R4,R0
         EX    R4,MVCVALUE
         CLC   =C'STORAGE ',VALUE CHECK FOR STORAGE.
         BNE   NOTSTOR
         OI    TYPE,STORAGE
         MVI   ATTR,UCBBSTR
         B     DOSCAN
NOTSTOR  DS    0H
         CLC   =C'PUBLIC ',VALUE CHECK FOR PUBLIC.
         BNE   NOTPUB
         OI    TYPE,PUBLIC
         MVI   ATTR,UCBBPUB
         B     DOSCAN
NOTPUB   DS    0H
         CLC   =C'PRIVATE ',VALUE CHECK FOR PRIVATE.
         BNE   NOTPRIV
         OI    TYPE,PRIVATE
         MVI   ATTR,UCBBPRV
         B     DOSCAN
NOTPRIV  DS    0H
         CLC   =C'ONLINE ',VALUE CHECK FOR ONLINE.
         BNE   ENDONL
         OI    TYPE,ONLINE
         B     DOSCAN
ENDONL   DS    0H
         CLC   VALUELEN,=H'6'     IF L'VALUE > 6, CAN ONLY BE UNITNAME.
         BH    TRYUNIT
         CLC   VALUELEN,=H'2'     IF L'VALUE < 2, TRY UNITNAME.
         BL    TRYUNIT
         OI    TRY,VOLUME         SPECIFY TRY FOR VOLUME.
         LH    R4,VALUELEN
         BCTR  R4,0
         LA    R5,VALUE(R4)
         CLI   0(R5),C'*'
         BNE   NOTMASK
         OI    TRY,MASK
         STH   R4,VALUELEN
         EJECT
NOTMASK  DS    0H
         CLC   VALUELEN,=H'4'     IF L'VALUE > 4, CAN'T BE UCB.       |
         BH    DOSCAN
         OI    TRY,UCB
         B     DOSCAN
         SPACE 3
********************************************************************
*                                                                  *
*        G E T   N E X T   T H I N G   I N   L I S T               *
*                                                                  *
********************************************************************
NEXTPDE  DS    0H
         XC    SCANWORK,SCANWORK  CLEAR SCANWORK AREA
         XC    UCBCOPY,UCBCOPY    CLEAR UCBCOPY  AREA
         XC    UCBEXTN,UCBEXTN    CLEAR UCBEXTN  AREA
         MVI   DCETEXT,X'00'      START CLEAR OF DCE AREA
         MVC   DCETEXT+1(L'DCETEXT-1),DCETEXT
         L     R2,PDEADDR         GET NEXT ENTRY IS PARSE LIST.
         L     R2,8(,R2)
         CL    R2,=X'FF000000'    IF NO MORE, RETURN.
         BNE   LOOPPDE
         EJECT
********************************************************************
*                                                                  *
*        R E T U R N                                               *
*                                                                  *
********************************************************************
RETURN   DS    0H
         IKJRLSA MYANS
         L     R2,RC                  R2 = RC.
         LR    R1,R13              PREPARE
         L     R13,4(,R13)           FOR FREEMAIN.
         FREEMAIN RU,LV=DYNLEN,A=(1)
         LR    R15,R2              AND RETURN
         RETURN (14,12),RC=(15)      WITH R15 = RC.
         EJECT
********************************************************************
*                                                                  *
*        U C B   S C A N - Unauthorized - Must use COPY option     *
*                                                                  *
********************************************************************
DOSCAN   DS    0H
         UCBSCAN COPY,                                                 C
               WORKAREA=SCANWORK,                                      C
               UCBAREA=UCBCOPY,                                        C
               CMXTAREA=UCBEXTN,                                       C
               DCEAREA=DCETEXT,                                        C
               DCELEN=DCELENTH,                                        C
               DYNAMIC=YES,                                            C
               RANGE=ALL,                                              C
               DEVCLASS=DASD,                                          C
               DEVNCHAR=DEVNAME,                                       C
               RETCODE=RETCODE,                                        C
               RSNCODE=RSNCODE,                                        C
               MF=(E,UCBSCANL)
         LTR   R15,R15            CHECK THE RETURN CODE.
         BNZ   ENDSCAN            IF NOT 0, ASSUME NO MORE TO SCAN
DOSCAN1  DS    0H
         LA    R3,UCBCOPY         GET ADR OF UCB
         LA    R7,UCBCOPY         GET ADR OF UCB
         USING UCBDSECT,R7        TELL ASM
         TM    TRY,VOLUME         IF WE ARE TO TRY FOR VOLUME,
         BZ    NOTVOL
         LA    R4,L'UCBVOLI-1     ASSUME FULL LENGTH COMPARE...
         TM    TRY,MASK             UNLESS A MASK WAS SPECIFIED.
         BZ    *+10
         LH    R4,VALUELEN
         BCTR  R4,0
         EX    R4,CLCVOLI
         BNE   NOTVOL
         OI    TYPE,VOLUME
         B     SCANSPAC
NOTVOL   DS    0H
         TM    TRY,UCB            IF WE ARE TO TRY FOR UCB,
         BZ    NOTUCB
         LA    R4,L'DEVNAME-1     ASSUME FULL LENGTH COMPARE...       |
         TM    TRY,MASK             UNLESS A MASK WAS SPECIFIED.
         BZ    *+10
         LH    R4,VALUELEN
         BCTR  R4,0
         EX    R4,CLCNAME
         BNE   NOTUCB
         OI    TYPE,UCB
         B     SCANSPAC
NOTUCB   DS    0H
         TM    TYPE,STORAGE+PUBLIC+PRIVATE
         BZ    NOTATTR            IF ATTRIBUTE COMPARE, DO IT.
         SLR   R4,R4
         IC    R4,ATTR
         EX    R4,TMATTR
         BO    SCANSPAC
         B     DOSCAN
NOTATTR  DS    0H
         TM    TYPE,ONLINE        IF NO ONLINE REQUEST,
         BZ    DOSCAN               GET NEXT UCB.
SCANSPAC DS    0H
         BAL   R10,DOLSPACE       GO DO LSPACE.
         TM    TYPE,VOLUME+UCB    IF WE HIT ON SINGLE REQUEST,
         BZ    DOSCAN               ALL DONE.
         TM    TRY,MASK           UNLESS IT WAS A MASK REQUEST.
         BO    DOSCAN
ENDSCAN  DS    0H
         CLI   TYPE,0             IF WE HAVE A HIT,
         BNE   NEXTPDE              GO DO NEXT.
         EJECT
*******************************************************************
*                                                                 *
*        U N I T   R E Q U E S T                                  *
*                                                                 *
*******************************************************************
TRYUNIT  DS    0H
         MVC   UNITNAME,VALUE     INIT LISTS FO IEFEB4UV.
         XC    UNITLIST,UNITLIST
         XC    UNITATTR,UNITATTR
         MVI   UNITFLG,X'10'
         LINK  EP=IEFEB4UV,PARAM=(UNITTAB,UNITFLG),MF=(E,UNITPARM)
         LTR   R15,R15            IF RC ^= 0,
         BNZ   ENDUNIT              ASSUME NOT A UNITNAME.
         OI    TYPE,UNIT
         L     R2,UNITLIST
         L     R4,4(,R2)          R4 = # UCBS IN RETURNED LIST.
         L     R3,8(,R2)          R3 = A(UCB).
         LA    R2,8(,R2)          R2 = A(THIS UCB ENTRY IN TABLE).
UNITLOOP DS    0H
         BAL   R10,DOLSPACE
         LA    R2,4(,R2)          R2 = A(NEXT UCB ENTRY IN TABLE).
         L     R3,0(,R2)          R3 = A(NEXT UCB).
         BCT   R4,UNITLOOP        LOOP.
         L     R2,UNITLIST
         L     R0,0(,R2)
         FREEMAIN R,LV=(0),A=(R2) FREEMAIN IEFEB4UV STORAGE.
         B     NEXTPDE
ENDUNIT  DS    0H
         B     BADREQ             CAN'T IDENTIFY REQUEST.
         EJECT
********************************************************************
*                                                                  *
*        D O   L S P A C E   S V C ( 7 8 )                         *
*                                                                  *
********************************************************************
DOLSPACE DS    0H
         LH    R0,COUNT           PUT OUT HEADER FOR 1ST REQ ONLY.
         AH    R0,=H'1'
         STH   R0,COUNT
         CH    R0,=H'1'
         BNE   NOHDR
         PUTLINE OUTPUT=(OUTHDR,SINGLE,DATA),MF=(E,MYIOPL)
NOHDR    DS    0H
         MVI   OUTAREA+4,C' '     FORMAT OUTPUT LINE.
         MVC   OUTAREA+5(OUTAREAL-5),OUTAREA+4
         MVC   OUTUCB,DEVNAME                                         |
         TM    UCBSTAT,UCBONLI    IF UCB NOT ONLINE,
         BZ    BADONL               GO TO ERROR ROUTINE.
         TM    UCBSTAT,UCBCHGS+UCBUNLD NO FUNNY STATUS.
         BNZ   BADUCB
         MVC   OUTVOL,UCBVOLI
GETDEVT  EQU   *
         MVC   OUTDEVT(5),=C'???? ' MOVE IN SKELETON
         LA    R6,DEVTABLE         POINT TO OUR TABLE
DEVLOOP  EQU   *
         CLI   0(R6),X'FF'         ARE WE AT END OF TABLE?
         BE    ENDOTAB             IF SO, WE WILL KNOW
         CLC   0(4,R6),UCBTYP      DO WE MATCH?
         BE    GOTDEVT
         A     R6,=F'09'           MOVE ALONG IN TABLE
         B     DEVLOOP             LOOP BACK
GOTDEVT  EQU   *
         MVC   OUTDEVT(5),4(R6)    MOVE IN ITS NAME
ENDOTAB  EQU   *
         TM    UCBSTAB,UCBBPRV     CHK IF MOUNTED PRIV
         BO    PRTPRIV
         TM    UCBSTAB,UCBBPUB     CHK IF MOUNTED PUB
         BO    PRTPUB
         TM    UCBSTAB,UCBBSTR     CHK IF MOUNTED STRG
         BO    PRTSTRG
         MVC   OUTATTR,=C'????'    NOT MOUNTED NORMALLY
         B     CONTPRT
PRTPRIV  MVC   OUTATTR,=C'PRIV'    IS MOUNTED PRIVATE
         B     CONTPRT
PRTPUB   MVC   OUTATTR,=C'PUB '    IS MOUNTED PUBLIC
         B     CONTPRT
PRTSTRG  MVC   OUTATTR,=C'STRG'    IS MOUNTED STORAGE
*
CONTPRT  EQU   *
*        LR    R0,R7              R0 = A(UCB).
*        LA    R1,LSPCMSG         R1 = A(MSG AREA).
*        SVC   78                 DO LSPACE.
         MVI   LSPCPRMW,X'00'     START CLEAR OF LSPACE PARM WORKAREA |
         MVC   LSPCPRMW+1(L'LSPCPRMW-1),LSPCPRMW    FINISH CLEAR      |
         MVC   LSPCPRMW,LSPCPRMT  COPY PARM TEMPLATE TO WORKAREA      |
*
********************************************************************
*                                                                  *
*        EAV Parameter List for LSPACE - 28 bit execution          *
*                                                                  *
*        LSPACE returns the return code in register 15.            *
*        SMF requires authorization.                               *
*                                                                  *
********************************************************************
*
LSPCEXEC LSPACE XEXPMSG=LSPCMSG,                                       C
               MF=(E,LSPCPRM),                                         C
               UCB=(R7),                                               C
               SMF=NONE,                                               C
               F4DSCB=0
*
********************************************************************
*                                                                  *
*        Pre-EAV Parameter List for LSPACE - 16 bit execution      *
*                                                                  *
********************************************************************
*
*LSPCEXEC LSPACE EXPMSG=LSPCMSG,                                      C
*               F4DSCB=0,                                             C
*               SMF=NONE,                                             C
*               UCB=(R7),                                             C
*               MF=(E,LSPCPRM)
         LTR   R15,R15            IF BAD RC,
         BNZ   BADSVC78             GO TO ERROR ROUTINE.
         MVC   OUTSPACE(L'LSPCMSG),LSPCMSG
         CVAFTST UCB=(R7)         CHECK FOR INDEXED VTOC.
         CH    R15,=H'8'          IF RC = 8, WE'RE INDEXED.
         BNE   CONTPRT1
         MVI   OUTINDEX,C'I'
CONTPRT1 EQU   *
         PUTLINE OUTPUT=(OUTAREA,SINGLE,DATA),MF=(E,MYIOPL)
         BR    R10
         EJECT
********************************************************************
*        E R R O R S                                               *
********************************************************************
BADPARS  DS    0H                 BAD PARSE.
         MVC   RC,=F'12'
         LA    R15,MSGPARS
         LA    R10,RETURN
         B     DOMSG
BADREQ   DS    0H                 CAN'T IDENTIFY REQUEST.
         MVC   RC,=F'8'
         LA    R15,MSGREQ
         LA    R10,NEXTPDE
         B     DOMSG
BADONL   DS    0H                 UCB NOT ONLINE.
         TM    TYPE,STORAGE+PUBLIC+PRIVATE+ONLINE
         BNZ   RETMSG             FOR ABOVE REQUESTS, IGNORE ERROR.
         MVC   OUTSPACE(L'MSGONL),MSGONL
         LA    R15,OUTAREA
         B     DOMSG
BADUCB   DS    0H                 FUNNY STATUS.
         TM    TYPE,STORAGE+PUBLIC+PRIVATE+ONLINE
         BNZ   RETMSG             FOR ABOVE REQUESTS, IGNORE ERROR.
         MVC   OUTSPACE(L'MSGUCB),MSGUCB
         LA    R15,OUTAREA
         B     DOMSG
BADSVC78 DS    0H                 BAD LSAPCE RC.
         TM    TYPE,STORAGE+PUBLIC+PRIVATE+ONLINE
         BNZ   RETMSG             FOR ABOVE REQUESTS, IGNORE ERROR.
         MVC   OUTSPACE(L'MSGSVC78),MSGSVC78
         LA    R10,NEXTPDE
         B     DOMSG
DOMSG    DS    0H
         PUTLINE OUTPUT=((R15),SINGLE,DATA),MF=(E,MYIOPL)
RETMSG   DS    0H
         BR    R10
         EJECT
********************************************************************
*                                                                  *
*        S T A T I C   A R E A                                     *
*                                                                  *
********************************************************************
MVCVALUE MVC   VALUE(*-*),0(R3)
CLCVOLI  CLC   UCBVOLI(*-*),VALUE        COMPARE VOLSER.
CLCNAME  CLC   DEVNAME(*-*),VALUE        COMPARE EBCDIC UCB NAME.     |
TMATTR   TM    UCBSTAB,*-*
*
         DS    0F
DCELENTH DC    AL2(L'DCETEXT)
MSGONL   DC    C'OFFLINE'
MSGUCB   DC    C'SPACE COULD NOT BE DETERMINED.'
MSGSVC78 DC    C'LSPACE SVC ERROR, IGNORED.'
MSGPARS  DC    AL2(MSGPARSL,0)
         DC    C'TSO PARSE ERROR, PROGRAM TERMINATED.'
MSGPARSL EQU   *-MSGPARS
MSGREQ   DC    AL2(MSGREQL,0)
         DC    C'VOLSER, UCB, OR UNIT NOT FOUND, IGNORED.'
MSGREQL  EQU   *-MSGREQ
*
XIOPB    PUTLINE MF=L
IOPBLEN  EQU   *-XIOPB
MYPCL    IKJPARM DSECT=MYPDL
*
VOLUMES  IKJIDENT  VOLUME,LIST,FIRST=ALPHANUM,OTHER=ALPHANUM,CHAR,     X
               MAXLNTH=8,DEFAULT='ONLINE'
*
         IKJENDP
*
OUTHDR   DC    AL2(OUTHDRL,0)
         DC    C'VOL    '
         DC    C'  '
         DC    C'UNIT '
         DC    C'DEVT  '
         DC    C'ATTR '
         DC    C'      '
         DC    C'#FREE CYL '
         DC    C' #FREE TRK '
         DC    C' #FREE EXT'
         DC    C' #CYL LGXT'
         DC    C' TL'
         DC    C' TMS #CYLS'
         DC    C'  TMS #TRKS'
         DC    C'  TMS #EXTS'
         DC    C' #CYL LGXT'
         DC    C' TL'
OUTHDRL  EQU   *-OUTHDR
*
         LTORG *
*
SRCLEVEL DC    C'                                        '
*
* DEVICE TABLE
*
DEVTABLE EQU   *
         DC    X'3010200E',C'3380 '
         DC    X'3030200E',C'3380 '    EMULATED 3380 2105 SHARK DEV
         DC    X'3010200F',C'3390 '    3390 "NORMAL"
         DC    X'3030200F',C'3390 '    3390 DYNAMIC
         DC    X'FFFFFFFF'             END OF TABLE
********************************************************************
*                                                                  *
*        List form of the LSPACE macro with PLISTVER=MAX added to  *
*        support EAV and the return of 95 bytes of data to the     *
*        return area.                                              *
*                                                                  *
********************************************************************
LSPCPRMT LSPACE MF=L,PLISTVER=MAX  LSPACE PARM TEMPLATE
         EJECT
********************************************************************
*                                                                  *
*        D Y N A M I C   A R E A                                   *
*                                                                  *
********************************************************************
DYNAMIC  DSECT
SAVE     DS    18F
RC       DS    F
ECB      DS    F
MYANS    DS    A
PDEADDR  DS    A
MYCPPL   DS    0F,CL(CPPLLEN)
MYPPL    DS    0F,CL(PPLLEN)
MYIOPL   DS    0F,CL(IOPLLEN)
MYIOPB   DS    0F,CL(IOPBLEN)
COUNT    DS    H
VALUELEN DS    H
VALUE    DS    CL8
OUTAREA  DS    0F,AL2,AL2
OUTVOL   DS    CL6,CL1
OUTINDEX DS    CL1,CL1
OUTUCB   DS    CL4,CL1                                                |
OUTDEVT  DS    CL5,CL1
OUTATTR  DS    CL4,CL1
OUTSPACE DS    CL120                  EAV SUPPORT - Increased from 50
OUTAREAL EQU   *-OUTAREA
* IEFEB4UV PARM LIST.
UNITPARM DS    2A                     IEFEB4UV PARM LIST
UNITTAB  DS    0F                     UNIT TABLE
UNITNAME DS    CL8                    UNITNAME (INPUT TO IEFEB4UV)
UNITLIST DS    F                      UNITNAME LIST ADDR (OUTPUT)
UNITATTR DS    F                      DON'T PROVIDE ATTRIBUTE LIST
UNITFLG  DS    X                      IEFEB4UV PARM LIST
* END IEFEB4UV PARM LIST.
TYPE     DS    X
TRY      DS    X
PRIVATE  EQU   X'01'
STORAGE  EQU   X'02'
PUBLIC   EQU   X'04'
VOLUME   EQU   X'08'
UCB      EQU   X'10'
UNIT     EQU   X'20'
ONLINE   EQU   X'40'
MASK     EQU   X'80'
ATTR     DS    X
*
         DS    0D
         UCBSCAN MF=(L,UCBSCANL)
         DS    0D
SCANWORK DS    CL100    WORK AREA USED BY THE UCBSCAN SERVICES
         DS    0D
UCBCOPY  DS    48C      UCB COMN SEGMENT & DEVICE DEPENDENT SEGMENT
         DS    0D
UCBEXTN  DS    36C      UCB COMN EXTENTION SEGMENT
RETCODE  DS    F        RETURN CODE USED BY UCBSCAN SERVICES
RSNCODE  DS    F        REASON CODE USED BY UCBSCAN SERVICES
DEVNAME  DS    CL4      DEVICE NUMBER ASSOCIATED WITH UCB.
DCETEXT  DS    XL256    DCE AREA
*                                                                  *
********************************************************************
*                                                                  *
*                                                                  *
* LSPACE ITEMS (Work Areas)                                        *
*                                                                  *
*                                                                  *
********************************************************************
*                                                                  *
LSPCMSGA DS    A                                                      |
LSPCPRMW DS    XL(LSPAEND-LSPCPRM) LSPACE PARM WORKAREA               |
*                                                                  *
********************************************************************
*                                                                  *
* Invocation of the List-Form of LSPACE using the extended and     *
* expanded return area of 95 bytes for EAVs.                       *
*                                                                  *
********************************************************************
*                                                                  *
LSPCMSG  LSPACE MF=(L,XEXPMSG)                                        |
*
********************************************************************
*                                                                  *
* Old List-form for the expanded message return area (40 bytes)    *
*                                                                  *
********************************************************************
*                                                                  *
*LSPCMSG  LSPACE MF=(L,EXPMSG)                                        |
*
DYNLEN   EQU   *-DYNAMIC
         TITLE 'DSPACE - MACRO LSPACE PARM LIST'
*
********************************************************************
*                                                                  *
* Old DSECT-form without PLISTVER                                  *
*                                                                  *
********************************************************************
*                                                                  *
*LSPCPRM  LSPACE MF=D
*
********************************************************************
*                                                                  *
* New DSECT-form with PLISTVER=MAX                                 *
*                                                                  *
********************************************************************
*                                                                  *
LSPCPRM  LSPACE MF=D,PLISTVER=MAX
         TITLE 'DSPACE - MACRO IKJCPPL'
         IKJCPPL
CPPLLEN  EQU   *-CPPL
         TITLE 'DSPACE - MACRO IKJPPL'
         IKJPPL
PPLLEN   EQU   *-PPL
         TITLE 'DSPACE - MACRO IKJIOPL'
         IKJIOPL
IOPLLEN  EQU   *-IOPL
         SPACE 3
         TITLE 'DSPACE - MACRO IHAPSA'
         IHAPSA LIST=YES
         TITLE 'DSPACE - MACRO CVT'
         CVT   DSECT=YES,LIST=YES
         TITLE 'DSPACE - MACRO UCB'
UCBDSECT DSECT
         IEFUCBOB LIST=YES
         END   &CSECT
/*
//BIND     EXEC PGM=IEWBLINK,COND=(4,LT),
//         PARM='LIST,LET,XREF,RENT,REUS'
//SYSPRINT  DD  SYSOUT=*
//SYSUT1    DD  UNIT=DISK,SPACE=(TRK,(2,1))
//SYSLMOD   DD  DISP=SHR,DSN=DHTU.LOAD
//SYSLIN    DD  UNIT=DISK,DSN=&&OBJMOD,
//     DISP=(OLD,DELETE)
//          DD  DDNAME=SYSIN
//SYSIN     DD  *
 IDENTIFY DSPACE('UPDATED WITH EAV SUPPORT')
 NAME     DSPACEAV(R)
/*
//
