PROC 0
CONTROL NOMSG NOLIST NOCONLIST NOSYMLIST NOFLUSH NOPROMPT
/*
ISPEXEC LIBDEF ISPPLIB DATASET ID('USXXBCR.CBT.ALL')
/*
/*  TABLA DE LOS DIAS DE FIESTA EN FORMATO AAAAMMDD
/*
SET &FN1  = &STR(2000011)
SET &FN2  = &STR(2000016)
SET &FN3  = &STR(20000420)
SET &FN4  = &STR(20000421)
SET &FN5  = &STR(2000051)
SET &FN6  = &STR(2000052)
SET &FN7  = &STR(20000515)
SET &FN8  = &STR(20000815)
SET &FN9  = &STR(20001012)
SET &FN10 = &STR(2000111)
SET &FN11 = &STR(2000119)
SET &FN12 = &STR(2000126)
SET &FN13 = &STR(2000128)
SET &FN14 = &STR(20001225)
/*
/*   FIN DE LA TABLA
/*
SET &ZPF = &&ZPF
SET &ZZ = &STR(01)
DO WHILE &ZZ <= 24
 ISPEXEC VGET (ZPF&ZZ)
 SET &ZZ = &ZZ + 01
 IF &LENGTH(&ZZ) = 1 THEN SET &ZZ = &STR(0)&ZZ
END
/*CONTROL MSG LIST CONLIST SYMLIST FLUSH NOPROMPT
SET &ZZ = &STR(01)
DO WHILE &ZZ <= 24
 IF &STR(&ZPF&ZZ) = &STR(RETURN) THEN +
  IF (&ZZ ¬= 07) AND (&ZZ ¬= 08) AND (&ZZ ¬= 19) AND (&ZZ ¬= 20) THEN +
    DO
     SET &WPF&ZZ = &STR(&ZPF&ZZ)
     SET &ZPF&ZZ = &STR(END)
     ISPEXEC VPUT (ZPF&ZZ)
    END
 SET &ZZ = &ZZ + 01
 IF &LENGTH(&ZZ) = 1 THEN SET &ZZ = &STR(0)&ZZ
END
CONTROL NOMSG NOLIST NOCONLIST NOSYMLIST NOFLUSH NOPROMPT
SET &WPF07 = &STR(&ZPF07)
SET &WPF08 = &STR(&ZPF08)
SET &WPF19 = &STR(&ZPF19)
SET &WPF20 = &STR(&ZPF20)
SET &ZPF07 = &STR(END)
SET &ZPF08 = &STR(END)
SET &ZPF19 = &STR(END)
SET &ZPF20 = &STR(END)
ISPEXEC VPUT (ZPF07 ZPF08 ZPF19 ZPF20)
SET &WSYSJDATE = &STR(&SYS4JDATE)
SET &WSYSDATE  = &SUBSTR(1:6,&SYS4DATE)&SUBSTR(7:10,&SYS4DATE)
/*SET &WSYSJDATE = &STR(20)&STR(&SYSJDATE)
/*SET &WSYSDATE  = &SUBSTR(1:6,&SYSDATE)&STR(20)&SUBSTR(7:8,&SYSDATE)
DO WHILE (&PFK ¬= PF03) AND (&PFK ¬= PF15)
/*DO UNTIL (&PFK = PF03) OR (&PFK = PF15)
SET &JD   = &SUBSTR(6:8,&WSYSJDATE)
SET &DD   = &SUBSTR(4:5,&WSYSDATE)
SET &MM   = &SUBSTR(1:2,&WSYSDATE)
SET &AA   = &SUBSTR(7:10,&WSYSDATE)
SET &AAAA = &AA
SET &MES = 10*(&MM-1) + 1
SET &MESES = &STR(   ENERO    FEBRERO   MARZO      ABRIL     MAYO   +
JUNIO     JULIO     AGOSTO    SEPTIEMBRE  OCTUBRE NOVIEMBRE  DICIEMBRE)
SET &LMM = &SUBSTR(&MES:&MES+9,&STR(&MESES))
SET &LAA = &STR(  )&AAAA&STR(  )
SET &AAAA = &AAAA - 1
SET &BIS  = &AAAA/4
SET &SW  = &AAAA-4*&BIS
SET &TOTAL = (&AAAA*365) + &BIS + &JD - &DD + 1
SET &DISE = &TOTAL/7
SET &DISE = &TOTAL-&DISE*7
/*WRITE TOTAL=&TOTAL AA=&AA JD=&JD MM=&MM DD=&DD DISE=&DISE
IF &DISE = 1 THEN SET &DISE = 7
IF &DISE = 2 THEN SET &DISE = 1
IF &DISE = 3 THEN SET &DISE = 2
IF &DISE = 4 THEN SET &DISE = 3
IF &DISE = 5 THEN SET &DISE = 4
IF &DISE = 6 THEN SET &DISE = 5
IF &DISE = 0 THEN SET &DISE = 6
IF (&MM = 02) THEN +
  IF &SW = 3 THEN SET &NVECES = 29
  ELSE SET &NVECES = 28
IF (&MM = 04) OR +
   (&MM = 06) OR +
   (&MM = 09) OR +
   (&MM = 11) THEN SET &NVECES = 30
IF (&MM = 01) OR +
   (&MM = 03) OR +
   (&MM = 05) OR +
   (&MM = 07) OR +
   (&MM = 08) OR +
   (&MM = 10) OR +
   (&MM = 12) THEN SET &NVECES = 31
SET &NUM = 1
DO &I = 1 TO 37
SET &MYINT&I = LOW
END
DO &I = 1 TO 37
SET &MYCOL&I = GREEN
END
DO &I = 7 TO 37 BY 7
SET &MYCOL&I = RED
SET &MYINT&I = HIGH
END
DO &VECES = 1 TO &NVECES
SET &D&DISE = &NUM
/*CONTROL MSG LIST CONLIST SYMLIST FLUSH NOPROMPT
IF (&AA&MM&NUM = &FN1 ) OR +
   (&AA&MM&NUM = &FN2 ) OR +
   (&AA&MM&NUM = &FN3 ) OR +
   (&AA&MM&NUM = &FN4 ) OR +
   (&AA&MM&NUM = &FN5 ) OR +
   (&AA&MM&NUM = &FN6 ) OR +
   (&AA&MM&NUM = &FN7 ) OR +
   (&AA&MM&NUM = &FN8 ) OR +
   (&AA&MM&NUM = &FN9 ) OR +
   (&AA&MM&NUM = &FN10) OR +
   (&AA&MM&NUM = &FN11) OR +
   (&AA&MM&NUM = &FN12) OR +
   (&AA&MM&NUM = &FN13) OR +
   (&AA&MM&NUM = &FN14) THEN +
   DO
     SET &MYINT&DISE = HIGH
     SET &MYCOL&DISE = RED
   END
/*CONTROL NOMSG NOLIST NOCONLIST NOSYMLIST NOFLUSH NOPROMPT
IF &NUM = &DD THEN +
 DO
   SET &MYINT&DISE = HIGH
   SET &MYCOL&DISE = BLUE
 END
SET &DISE = &DISE + 1
SET &NUM  = &NUM  + 1
END
/*WRITE ENTRO EN DISPLAY DE PANEL = &PFK
ISPEXEC ADDPOP ROW(01) COLUMN(38)
IF &LENGTH(&D36) > 0 THEN ISPEXEC DISPLAY PANEL(MESP1)
ELSE IF &LENGTH(&D29) > 0 THEN ISPEXEC DISPLAY PANEL(MESP2)
     ELSE ISPEXEC DISPLAY PANEL(MESP3)
ISPEXEC REMPOP ALL
/*WRITE ENTRO EN DISPLAY DE 1 PFK = &PFK
IF (&PFK ¬= PF07) AND +
   (&PFK ¬= PF08) AND +
   (&PFK ¬= PF15) AND +
   (&PFK ¬= PF19) AND +
   (&PFK ¬= PF20) AND +
   (&PFK ¬= PF03) THEN SET &PFK = &STR(PF08)
/*WRITE ENTRO EN DISPLAY DE 2 PFK = &PFK
IF (&PFK = PF08) OR (&PFK = PF20) THEN +
   IF (&MM = 02) THEN +
      IF &SW = 3 THEN +
        DO
         SET &JD = &JD + 29
         SET &MM = &MM + 1
        END
      ELSE DO
            SET &JD = &JD + 28
            SET &MM = &MM + 1
           END
   ELSE IF (&MM = 04) OR +
           (&MM = 06) OR +
           (&MM = 09) OR +
           (&MM = 11) THEN +
             DO
              SET &JD = &JD + 30
              SET &MM = &MM + 01
             END
        ELSE IF (&MM = 01) OR +
                (&MM = 03) OR +
                (&MM = 05) OR +
                (&MM = 07) OR +
                (&MM = 08) OR +
                (&MM = 10) THEN +
                  DO
                   SET &JD = &JD + 31
                   SET &MM = &MM + 1
                  END
             ELSE IF (&MM = 12) THEN +
                       DO
                        SET &AA = &AA + 1
                        SET &JD = &DD
                        SET &MM = 1
                       END
/*CONTROL MSG LIST CONLIST SYMLIST FLUSH NOPROMPT
IF (&PFK = PF07) OR (&PFK = PF19) THEN +
   IF (&MM = 02) OR +
      (&MM = 04) OR +
      (&MM = 06) OR +
      (&MM = 08) OR +
      (&MM = 09) OR +
      (&MM = 11) THEN +
        DO
         SET &JD = &JD - 31
         SET &MM = &MM - 01
        END
   ELSE IF (&MM = 05) OR +
           (&MM = 07) OR +
           (&MM = 10) OR +
           (&MM = 12) THEN +
             DO
              SET &JD = &JD - 30
              SET &MM = &MM - 1
             END
        ELSE IF (&MM = 01) THEN +
                  DO
                   SET &AA = &AA - 1
                   SET &MM = 12
                   SET &AAAA = &AA
                   SET &AAAA = &AAAA - 1
                   SET &BIS  = &AAAA/4
                   SET &SW  = &AAAA-4*&BIS
                   IF &SW = 3 THEN SET &JD = 335 + &DD
                   ELSE SET &JD = 334 + &DD
                  END
             ELSE IF (&MM = 03) THEN +
                     DO
                      SET &MM = 02
                      IF &SW = 3 THEN SET &JD = &JD - 29
                      ELSE SET &JD = &JD - 28
                     END
IF &LENGTH(&MM) = 1 THEN SET &MM = &STR(0)&MM
IF &LENGTH(&JD) = 1 THEN SET &JD = &STR(00)&JD
IF &LENGTH(&JD) = 2 THEN SET &JD = &STR(0)&JD
SET &WSYSJDATE = &AA&STR(.)&JD
SET &WSYSDATE  = &STR(&MM)&STR(/)&DD&STR(/)&AA
/*WRITE WSYSDATE=&WSYSDATE WSYSJDATE=&WSYSJDATE
DO &I = 1 TO 37
SET &D&I = &STR(  )
END
END
/*CONTROL MSG LIST CONLIST SYMLIST FLUSH NOPROMPT
SET &ZPF07 = &STR(&WPF07)
SET &ZPF08 = &STR(&WPF08)
SET &ZPF19 = &STR(&WPF19)
SET &ZPF20 = &STR(&WPF20)
SET &WPF = &&WPF
SET &ZZ = &STR(01)
DO WHILE &ZZ <= 24
 IF &STR(&WPF&ZZ) = &STR(RETURN) THEN +
  IF (&ZZ ¬= 07) AND (&ZZ ¬= 08) AND (&ZZ ¬= 19) AND (&ZZ ¬= 20) THEN +
    DO
     SET &ZPF&ZZ = &STR(&WPF&ZZ)
     ISPEXEC VPUT (ZPF&ZZ)
    END
 SET &ZZ = &ZZ + 01
 IF &LENGTH(&ZZ) = 1 THEN SET &ZZ = &STR(0)&ZZ
END
SET &ZZ = &STR(01)
DO WHILE &ZZ <= 24
 ISPEXEC VPUT (ZPF&ZZ)
 SET &ZZ = &ZZ + 01
 IF &LENGTH(&ZZ) = 1 THEN SET &ZZ = &STR(0)&ZZ
END
ISPEXEC LIBDEF ISPPLIB
EXIT CODE(0)
