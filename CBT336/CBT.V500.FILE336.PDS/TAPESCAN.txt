 TITLE 'TAPESCAN  -  IRSS/GTEDS/ICSA TAPE SCANNING, ANALYSIS, AND COPYI*
               NG UTILITY - VERSION 5.1'
***********************************************************************
* COPYRIGHT   1982  BY RICE UNIVERSITY                                *
*                                                                     *
* PERMISSION TO MAKE PRIVATE COPIES OF THIS PROGRAM AND TO USE IT     *
* FREELY IS HEREBY GRANTED BY RICE UNIVERSITY. THIS PROGRAM MAY NOT   *
* BE SOLD, NOR MAY ANY SECTION OF IT BE INCORPORATED INTO ANY WORK    *
* FOR SALE WITHOUT PERMISSION OF RICE UNIVERSITY. THIS STATEMENT      *
* DOES NOT OVERRIDE OR OTHERWISE MODIFY ANY PREVIOUSLY EXISTING       *
* RIGHTS OF ANY OTHER PERSONS OR ORGANIZATIONS RELATING TO THIER      *
* CONTRIBUTIONS TO THIS PROGRAM.                                      *
***********************************************************************
         SPACE 5
TAPESCAN CSECT ,
         SPACE 2
COPYRITE CSECT ,                  COPYRIGHT THE LOAD MODULE TOO
         DC    C'TAPESCAN V5.1 COPYRIGHT RICE UNIVERSITY 1982'
         EJECT
*     V E R S I O N  5.1
* THIS VERSION OF TAPESCAN IS THE SAME AS VERSION 5.0 WITH A NUMBER
* OF USABILITY ENHANCEMENTS, NOTABLY IN THE AREA OF THE VTOC
* LISTING. HIGHLIGHTS ARE:
*
*        VTOC LISTING FOR NL TAPES.
*        IMPROVED DISTINCTION BETWEEN LABEL FILES AND DATA FILES
*             CONTAINING LABELS.
*        DIFFERENT VTOC FORMATS FOR THE COUNT AND NOCOUNT OPTIONS.
*        MAPONLY OPTION ADDED FOR VTOC LISTING ONLY.
*        DIAGNOSTIC NOTES ADDED TO VTOC LISTING.
*        PROTECTION STATUS ADDED TO LABEL SUMMARY (NOT VTOC).
*        ADDED LISTING OF USER LABELS AND UNSUPPORTED ANS
*              LABELS.
*        ASCII->ASCII COPY MADE WITHOUT PASSING THROUGH EBCDIC
*              (FOR WIERDOS WITH ANS LABELLED TAPES CONTAINING
*               BINARY DATA).
*        SUPPORT FOR INSTALLATION SECURITY EXIT (CAN BE USED
*              AS AN ACF2 INTERFACE).
*        SENSE EXCP FROM SYNAD EXIT REMOVED, DUE TO USELESSNESS.
*        SPECIFIC MESSAGE FOR FILE PROTECTED OUTPUT TAPE ADDED.
*
* PLACEMENT OF PROGRAM SECTIONS: INIT CODE, INIT VARS, COMMON
* ROUTINES, COMMON VARS, MAIN CODE, MAIN VARS, TERM CODE,
* TERM VARS, SAVE AREA, LITERAL POOL.
*
*        FOR ASSISTANCE, CONTACT:
*              ALAN BEALE
*              RICE UNIVERSITY, ICSA
*              P.O. BOX 1892
*              HOUSTON, TX 77251
*              (713) 527-4985
         EJECT
*     V E R S I O N  5.0
* THIS VERSION OF TAPESCAN WAS FORMED BY MERGING VERSION 4.0 WITH
* "VERSION 3.5", AS CONTAINED ON THE MVS CBT MODS TAPE. UPDATE
* COMMENTS FROM THIS VERSION ARE:
         SPACE
* VERSION 3.1 INCLUDES EXPIRATION DATE, AVERAGE BLOCK SIZES, AND VTOC
* LISTING AND WAS PRODUCED BY C. WRANDLE BARTH, GODDARD SPACE FLIGHT
* CENTER, JANUARY 1975.
*
* THIS PROGRAM WAS MODIFIED FOR MVS, JUNE 1978 BY:
*  STEVE R. HAGGERTY
*  GTE DATA SERVICES
*  MARINA DEL REY, CA 90291
*  (213) 821-0511 EXT. 285
*
*  INSTALLED AT UNIONBANC COMPUTER CORPORATION ON 08/25/78
*  BY HOWARD DEAN (TECHNICAL SERVICES). IF ANY PROBLEMS WITH
*  THIS PROGRAM ARE ENCOUNTERED, PLEASE CONTACT:
*
*   HOWARD M. DEAN
*   UNION BANK COMPUTER CORPORATION
*   TECHNICAL SERVICES (8TH FLOOR)
*   605 W. OLYMPIC BLVD.
*   LOS ANGELES, CA. 90015
*   PHONE - (213) 687-5719
*
* VERSION 3.2 CORRECTED VARIOUS BUGS.
* VERSION 3.3 CHANGED OUTPUT TAPE HANDLING TO USE ONLY EXCP.
* VERSION 3.4 CORRECTED FOR USE UNDER MVS REL. 3.7F *GTEDS LA*SRH*
* VERSION 3.5 CORRECT BUGS AND ADD LINECNT PARAMETER *GTEDS LA*HMD*
         SPACE
* NOTE THAT ALL MODIFICATIONS REFERRED TO ABOVE MAY NOT HAVE BEEN
* CARRIED OVER TO VERSION 5.0.
         EJECT
*     V E R S I O N  4.0
* THIS VERSION OF TAPESCAN WAS PRODUCED BY ALAN BEALE AT ICSA,
* RICE UNIVERSITY, USING IRSS VERSION 3.5 AS A STARTING POINT.
* MOST OF THE CODE OF THE PROGRAM HAS BEEN MODIFIED IN SOME WAY.
*
* SIGNIFICANT NEW FEATURES AND ENHANCEMENTS FOR THIS VERSION ARE:
*        ASCII TAPE HANDLING (INCLUDING ASCII->EBCDIC AND
*              EBCDIC->ASCII),
*        RESPECT FOR EXPIRATION DATE & PASSWORD PROTECTION,
*        7-TRACK LABELED TAPE HANDLING,
*        INPUT TAPE POSITIONING VIA LABEL JCL PARM,
*        COMPLETE OUTPUT TAPE LABEL CHECKING (NO BLP),
*        SUPPORT FOR 6250 BPI.
*
* CHANGES TO PREVIOUSLY SUPPORTED FUNCTIONS INCLUDE:
*
*        "STANDARD" PARAMETER SPECIFICATION (OPTION=VALUE),
*        IMPROVED OUTPUT FORMATTING,
*        FULLSUM OPTION REQUIRED FOR EXACT LABEL LIST,
*        MAXTM/MAXEOV CHANGED TO STOPTM/STOPEOV,
*        TM AND EOV PARMS MADE INDEPENDENT (NOT CUMULATIVE),
*        NOVOLSER MADE DEFAULT, OPPOSITE NAMED UNLABEL,
*        DEFAULT HEX CHANGED TO NOHEX,
*        COPIED LABELS MODIFIED FOR GREATER ACCURACY,
*        SCAN STATISTICS NOW GIVEN AFTER I/O ERROR.
*
*        THIS VERSION OF TAPESCAN REQUIRES THE 370 INSTRUCTION SET,
*        A VS OPERATING SYSTEM, AND AUTHORIZATION (LINK-EDIT PARM AC=1)
*
* CORRECTION MADE 7/3/80:
*
*        IMPROVED SEQUENCING OF OUTPUT LABELS DURING COPY       -ARB80-
         EJECT
*     V E R S I O N  3.5
* THIS PROGRAM, CALLED TAPESCAN, WAS WRITTEN BY WILL DALAND, SOCIAL
* SCIENCE STATISTICAL LABORATORY, INSTITUTE FOR RESEARCH IN SOCIAL
* SCIENCE, UNIVERSITY OF NORTH CAROLINA AT CHAPEL HILL, MARCH 1974.
* SEE IRSS MEMO SSSL-7-3 FOR FURTHER DOCUMENTATION.
*
* SINCE THE PROGRAMMING FOR TAPESCAN WAS PUBLICLY FUNDED PERMISSION
* IS GRANTED TO ANYONE TO USE THIS PROGRAM IN WHOLE OR IN PART.  IT IS
* REQUESTED THAT WHEN DOING SO YOU GIVE CREDIT (PREFERABLY BOTH IN
* SOURCE AND DOCUMENTATION) TO:
* WILL DALAND, INST. FOR RES. IN SOC. SCI., AND UNC AT CHAPEL HILL.
*
* PLACEMENT OF PROGRAM SECTIONS: INIT CODE, INIT SUBS, INIT VARS,
* INIT LITS, COMMON ROUTINES, COMMON VARS, COMMON LITS, MAIN CODE,
* MAIN SUBS, MAIN VARS, MAIN LITS.
*
* OPT1=MENTION EVERY (EVERY) TAPEMARK IN JOB CONSOLE LOG
* OPT8=DO NOT ATTEMPT TO REWIND TO LOAD POINT AT START OF RUN
* OPT64=TERMINATE ON ANY EXCP ERROR
* OP65536 =CONSIDER RECORDS STARTING WITH 'TAPEMARK' AS TAPEMARKS
* OPT268435456=IGNORE DOUBLE TAPEMARKS ON OUTPUT TAPE UNTIL NL LABEL
*              POSITION NUMBER IS REACHED.
*   ABOVE OPTION MADE DEFAULT.                                  -ARB79-
         EJECT
MSECT    DSECT
TRT1     DS    32D
BLANKBUF DS    CL136              BLANKS FOR BLANKING MSGBUF FAST
TRT2     DS    32D
MSGBUF   DS    CL136
PARMBUF  DS    CL120              PARM STRING COPY BUFFER       -ARB82-
ASCBUF   DS    CL120              RECORD COPY (ORIGINAL CODE)   -ARB79-
RECBUF   DS    CL120              MINIMUM ALLOC, MAX ALLOC=32K  -ARB79-
         SPACE 5
TAPESCAN CSECT
R0       EQU   0   WORK REG + SYSTEM USES
R1       EQU   1   WORK REG + SYSTEM USES
R2       EQU   2   MOSTLY FOR LINKAGE TO PUTLINE & GETNUM
R3       EQU   3   USED IN PARM SCANNER + VTOC ENTRY POINTER.     -CWB-
R4       EQU   4   BELOW LABEL 'PROCESS' COUNTS BLKS READ FOLLOWING A  *
                   TAPEMARK OR THE LOAD POINT.
R5       EQU   5   WORK, INTERNAL SUBROUTINE LINKAGE
R6       EQU   6   WORK REG
R7       EQU   7   WORK REG + LENGTH OF LAST BLK READ
R8       EQU   8   PARM FIELD LENGTH CTR, TOTAL BYTES ON TAPE CTR
R9       EQU   9   BASE REG FOR DSECT MSECT
R10      EQU   10  MAIN LOOP CONTROL REGISTER                   -ARB82-
R11      EQU   11  PERMANENT BASE REG
R12      EQU   12  SEMIPERMANENT INITIALIZATION/TERMINATION/EXTENSION  *
                   BASE REG.
R13      EQU   13  SAVE AREA POINTER + LITERALS                 -ARB79-
R14      EQU   14  WORK REG + SYSTEM USES
R15      EQU   15  WORK REG + SYSTEM USES
DCBTRTCH EQU   16
DCBDEN   EQU   18
DCBEODAD EQU   32
DCBTIOT  EQU   40                                               -ARB79-
DCBDEBAD EQU   44
DCBOFLGS EQU   48
DCBSYNAD EQU   56                                               -ARB82-
DCBBLKSI EQU   62
DCBIOBA  EQU   68  PTR TO BSAM'S IOB PREFIX
IOBSENS0 EQU   2                                                -ARB79-
IOBSENS1 EQU   3                                                -ARB79-
IOBECBPT EQU   4
IOBSTART EQU   16
IOBCSW   EQU   9   LAST SEVEN BYTES OF CSW
IOBDCBPT EQU   20
DEBSDVM  EQU   32                                               -ARB79-
DEBUCBAD EQU   32
JFCBLTYP EQU   66                 LABEL TYPE (AL, LTM, ETC.)
JFCBFLSQ EQU   68                 FILE SEQUENCE NUMBER, 0 OR 1 =1ST
JFCTRTCH EQU   93                                               -ARB79-
JFCOPTCD EQU   101                                              -ARB79-
JFCBVOLS EQU   118                1ST BYTE OF JFCB VOLUME LIST
TCBTIO   EQU   12                 TCB TIOT ADDR OFFSET          -ARB79-
TIOCNJOB EQU   0                                                -ARB79-
TIOCSTEP EQU   8                                                -ARB79-
TIOEFSRT EQU   16                                               -ARB79-
UCBNAME  EQU   13                                               -ARB79-
         SPACE 5
         PRINT NOGEN
         USING *,R12              BASE FOR INITIALIZATION ONLY
         STM   14,12,12(13)
         LR    R12,R15            R12 = TEMP BASE FOR INITIALIZATION
         L     R9,ASAVE           SET UP SAVE AREA/LIT PTR      -ARB79-
         ST    R9,8(R13)
         ST    13,4(R9)                                         -ARB79-
         LR    R13,R9
         USING SAVE,R13                                         -ARB79-
         L     R11,=A(EXIT)       PERM BASE FOR MAIN & COMMON   -ARB82-
         USING EXIT,R11                                         -ARB82-
         L     R1,0(R1)           GET PTR TO PARM FIELD
         LH    R8,0(R1)           LOAD PARM FIELD LENGTH
         LA    R3,2(R1)           SET PTR TO PARM FIELD CHAR STRING
         LA    R9,SRCHPRM         LOAD TEMPORARY BASE FOR MSECT DSECT
         USING MSECT,R9           TEMPORARY DECLARATION FOR MSECT BASE
         OPEN  (SYSPRINT,OUTPUT)
         TM    SYSPRINT+DCBOFLGS,X'10'
         BZ    EXITRC16           IF OPEN FAILED EXIT RC=16     -ARB79-
         GETMAIN VU,LA=GMCTRL,A=GMLOCS
         L     R9,GMLOCS          THERE IS AT LEAST THE MINIMUM CORE
*              ABOVE STMT SETS UP PERMANENT BASE FOR MSECT DSECT
         LR    R6,R9              START CLEARING GOTTEN MAIN TO        *
                                  SHORTEN POSSIBLE DUMPS.
         L     R4,GMLOCS+4        ACTUAL LENGTH OF GOTTEN MAIN
         LA    R5,256             OFT-USED CONSTANT FOR CLEARING MAIN
CLOOP    CR    R4,R5              R5 CONTAINS F'256'
         BNH   LE256              BIF ONLY 256 OR FEWER BYTES LEFT
         XC    0(256,R6),0(R6)    CLEAR 256 BYTES
         SR    R4,R5              R5 CONTAINS F'256'
         AR    R6,R5              R5 CONTAINS F'256'
         B     CLOOP
LE256    BCTR  R4,0               SET TO MACHINE LENGTH
         EX    R4,XCLEAR          CLEAR LAST 1 TO 256 BYTES
XCLEAR   XC    0(0,R6),0(R6)      EXECUTED IN STMT ABOVE
         MVI   BLANKBUF,C' '      PREPARE TO CREATE SOME BLANKS
         MVC   BLANKBUF+1(135),BLANKBUF      CREATE SOME BLANKS
         MVC   PARMBUF,BLANKBUF   COPY PARM STRING SINCE PARM   -ARB82-
         BCTR  R8,0               SCAN GOES PAST STRING END     -ARB82-
         EX    R8,MVPARM                                        -ARB82-
         LA    R8,1(,R8)                                        -ARB82-
         LA    R3,PARMBUF                                       -ARB82-
         LA    R1,MSGBUF+58       FOR ADCON IN DYNAMIC          -ARB79-
         ST    R1,AMSGBP58        STORAGE.                      -ARB79-
         LA    R1,MSGBUF+86                                     -ARB79-
         ST    R1,AMSGBP86                                      -ARB79-
         LA    R15,RECBUF         POINT THE WRITE
         O     R15,WRTCMND        CCW TO DYNAMICALLY
         ST    R15,WRTCMND        ALLOCATED RECORD BUFFER.
         LA    R15,RECBUF         SET FILE SEARCH CCW TO
         O     R15,READ81CM       POINT TO
         ST    R15,READ81CM       RECBUF.
         LA    R5,TRT1            START SETTING UP THE FIRST
         LA    R6,16              HEX TRANSLATE
         LA    R7,CTABLE          TABLE.
SETUPTR1 MVC   0(1,5),0(7)        MOVE SEED CHAR INTO TRT1
         MVC   1(15,5),0(5)       AND PROPAGATE IT
         LA    5,16(5)
         LA    7,1(7)
         BCT   6,SETUPTR1
         MVC   TRT2(16),CTABLE    MOVE 16 CHAR SEED INTO TRT2
         MVC   TRT2+16(240),TRT2  AND PROPAGATE IT 15 TIMES
         TIME  DEC                GET DATE IN R1 IN FORM 00YYJJJF
         ST    R0,BADLNGTH        SAVE TIME WHILE YOU'RE AT IT
         ED    TIMERSLT,BADLNGTH  EDIT INTO PAGE HEADER LINE BUFFER
         ST    R1,BADLNGTH+4      CONVERT JULIAN DATE           -ARB79-
         UNPK  TODAY(5),BADLNGTH+5(3)   TO ZONED                -ARB79-
         MVC   DATERSLT+2(6),=C'000000'                         -ARB79-
         MVC   WORK(8),=A(TODAY,DATERSLT+2)   JULIAN PARMS      -ARB79-
         MVI   WORK+4,X'80'       SET END OF LIST BIT           -ARB82-
         LA    R1,WORK                                          -ARB79-
         L     R15,=V(JULIAN)     CALL ICSA JULIAN DATE ROUTINE -ARB79-
         BALR  R14,R15            CONVERT TO GREGORIAN          -ARB79-
         MVC   DATERSLT(2),DATERSLT+2  INSERT SLASHES           -ARB79-
         MVC   DATERSLT+3(2),DATERSLT+4 TO IMPROVE THE FORMAT   -ARB79-
         MVI   DATERSLT+2,C'/'                                  -ARB79-
         MVI   DATERSLT+5,C'/'                                  -ARB79-
         DEVTYPE INPUT+40,DTYPE   CHECK TO SEE IF TAPE DEVICE     -HMD-
         LTR   R15,R15            DID WE FIND DD STATEMENT?       -HMD-
         BNZ   NOINPUT            NOPE                            -HMD-
         CLI   DTYPE+2,X'80'      IS THIS A TAPE DEVICE?          -HMD-
         BNE   NOTTAPEI           NOPE                            -HMD-
         RDJFCB (INPUT)           READ INPUT TAPE'S JFCB
         CLI   JFCBIN,X'00'       CHECK IF 1ST BYTE OF JFCB MODIFIED
         BE    NOINPUT            RDJFCB DIDN'T WORK IF JFCBIN STILL =0
         MVC   DDVOL,JFCBIN+JFCBVOLS  PUT VOL INTO PAGE HEAD.     -CWB-
         MVC   JFCLTSV,JFCBIN+JFCBLTYP SAVE INPUT TAPE'S LABEL TYPE
         NI    JFCLTSV,X'F7'      TURN OFF USER LABEL BIT       -ARB79-
         LH    R2,JFCBIN+JFCBFLSQ GET FILE SEQUENCE NUMBER      -ARB79-
         STH   R2,INFLPOS         SAVE FOR VTOC                 -ARB82-
         LTR   R2,R2              UNSPECIFIED?                  -ARB79-
         BZ    *+6                YES, DON'T DECREMENT          -ARB79-
         BCTR  R2,0                                             -ARB79-
         NI    JFCOPTCD+JFCBIN,X'F7'   CANCEL OPTCD=Q           -ARB79-
         TM    JFCLTSV,X'42'      ARE THERE LABELS?             -ARB79-
         BZ    NLIN                                             -ARB79-
         MH    R2,=H'3'           CONVERT TO BLP FILE NO        -ARB79-
NLIN     LTR   R2,R2              STARTING AT THE BEGINNING?    -ARB82-
         BZ    LABEL1             YES.                          -ARB82-
         MVI   POSFLG,C'Y'        NO, SET POSITIONING FLAG      -ARB79-
LABEL1   ST    R2,SKIPFLNO        STORE AS SKIPTM VALUE         -ARB82-
         MVI   JFCBIN+JFCBLTYP,X'10'  SET TO BLP                -ARB79-
         MVC   JFCBIN+JFCBFLSQ(2),=H'1' SET FILE SEQ. NO. TO = 1
         OPEN  (INPUT),TYPE=J
         TM    INPUT+DCBOFLGS,X'10'
         BZ    NOINPUT            TERMINAL ERROR
         MVC   ITRTCH(1),DCBTRTCH+INPUT     SAVE INPUT TRTCH    -ARB79-
         CLI   ITRTCH,0           TRTCH= (NULL)?                -ARB82-
         BNE   *+8                NO.                           -ARB82-
         MVI   ITRTCH,X'33'       YES, CHANGE FOR SFM LATER     -ARB82-
         L     R1,GMCTRL+4        LOAD MAXIMUM REQUESTED GETMAIN LENGTH
         S     R1,GMLOCS+4        SUBTRACT LENGTH ACTUALLY GOTTEN
         BZ    SRCHPRM            BIF GOT ALL CORE REQUESTED
         CVD   R1,BADLNGTH        CONVERT DIFERENCE TO PACKED DECIMAL
         UNPK  MORECORE+33(5),BADLNGTH
         OI    MORECORE+37,C'0'
         LH    R2,INPUT+DCBBLKSI  TO PREVENT DATA 'OVERRUNS'
         SR    R2,R1              SUBTRACT DIFFERENCE OF MORE CORE
         STH   R2,INPUT+DCBBLKSI  AND PUT BACK IN DCB
         BAL   R2,PUTLINE
         MVC   MSGBUF(L'MORECORE),MORECORE
         EJECT
SRCHPRM  LTR   R8,R8              LOAD AND TEST REMAINING PARM LENGTH
         BNH   ENDPARMS
         SR    R5,R5              ZERO CURRENT PARM LENGTH COUNTER
         LR    R4,R3              SAVE PTR TO START OF PARM
         MVC   THISPARM,BLANKBUF  BLANK OUT PARM COPY AREA      -ARB82-
         LA    R6,THISPARM                                      -ARB82-
SRCHCOMA CLI   0(R3),C','
         MVC   0(1,R6),0(R3)      SAVE PARM FOR DIAGNOSTIC      -ARB82-
         LA    R6,1(,R6)                                        -ARB82-
         LA    R3,1(R3)           BUMP PTR TO NEXT CHAR
         BE    GOTCOMMA
         LA    5,1(5)             COUNTS LENGTH OF CURRENT PARM
         BCT   R8,SRCHCOMA
GOTCOMMA CLC   0(6,R4),=C'NOLIST' GET HERE IF COMMA OR END OF PARM LIST
         BE    NOLIST
         CLC   0(5,4),=C'LIST='                                 -ARB79-
         BE    LIST
         CLC   0(7,4),=C'STOPTM=' INITIALLY 32767               -ARB79-
         BE    STOPTM                                           -ARB79-
         CLC   0(7,4),=C'SKIPTM='                               -ARB79-
         BE    SKIPTM
         CLC   0(3,4),=C'HEX'                                   -ARB79-
         BE    HEX                                              -ARB79-
         CLC   0(8,4),=C'STOPEOV='                              -ARB79-
         BE    STOPEOV                                          -ARB79-
         CLC   0(8,4),=C'SKIPEOV='                              -ARB79-
         BE    SKIPEOV
         CLC   0(5,4),=C'NOSUM'                                 -ARB79-
         BE    NOSUMARY
         CLC   0(7,4),=C'FULLSUM'                               -ARB79-
         BE    FULLSUM                                          -ARB79-
         CLC   0(5,4),=C'COUNT'
         BE    COUNT
         CLC   0(7,4),=C'NOCOUNT'
         BE    NOCOUNT
         CLC   0(7,4),=C'ERRLIM='                               -ARB79-
         BE    ERRLIM
         CLC   0(4,4),=C'COPY'
         BE    COPY               TAPE COPYING OPTION
         CLC   0(6,4),=C'EOVMOD'  MOD OPTION IMPLIES COPY & COUNT OPTNS
         BE    EOVMOD
         CLC   0(7,4),=C'UNLABEL'                               -ARB79-
         BE    UNLABEL                                          -ARB79-
         CLC   0(8,4),=C'ASCII=IN'                              -ARB79-
         BE    TRANSIN                                          -ARB79-
         CLC   0(9,4),=C'ASCII=OUT'                             -ARB79-
         BE    TRANSOUT                                         -ARB79-
         CLC   0(4,4),=C'OPT='                                  -ARB79-
         BE    OPT                MISCELLANEOUS OPTIONS
         CLC   0(5,4),=C'LINE='   LINE COUNT OPTION             -ARB82-
         BE    LINE                                        *HMD 04/79*
         CLC   0(7,R4),=C'MAPONLY'                              -ARB82-
         BE    MAPONLY                                          -ARB82-
         CLC   0(3,R4),=C'MAPONLY'     MAP IS ALIAS OF MAPONLY  -ARB82-
         BE    MAPABBR                                          -ARB82-
PRLENERR EQU   *                                                -ARB82-
UNRECOG  BAL   R2,PUTLINE
         MVC   MSGBUF(33),=C'0ERROR - UNRECOGNIZABLE PARAMETER'
DIAGPARM BAL   2,PUTLINE                                        -ARB82-
         MVC   MSGBUF(L'PARMIGN+100),PARMIGN                    -ARB82-*
                                  REPEAT IGNORED PARM           -ARB82-
SRCHPARM BCTR  R8,0
         B     SRCHPRM
GETNUM   DS    0H                 CHAR STRNG INTGR TO BIN INTGR CONV SB
         SR    6,6                CLEAR ACCUMULATOR
         CLI   0(4),C'9'          * R4=PTR TO 1ST CHAR OF NUM         *
         BH    NUMERR             * R5=ACTUAL LENGTH OF NUM           *
         CLI   0(4),C'0'          * RESULT RETURNED IN R6             *
         BL    NUMERR             * R2,R4,R5,R6, AND R7 MODIFIED BY   *
         MH    R6,=H'10'          * USING GETNUM SUBROUTINE.          *
         IC    R7,0(4)            PICK UP DECIMAL CHARACTER
         SLL   7,28               CHOP OFF LEFT 4 BITS
         SRL   7,28               AND SHIFT BACK
         AR    6,7                ADD DIGIT INTO RESULT
         LA    4,1(4)             BUMP PTR TO NEXT CHAR
         BCT   5,GETNUM+2         GO TO TOP OF LOOP
         BR    2                  RETURN FROM GETNUM SUBROUTINE
NUMERR   BAL   R2,PUTLINE         ERROR DESCRIPTOR SUBROUTINE
         MVC   MSGBUF(40),=C'0ERROR - INVALID NUMERIC PARAMETER VALUE' *
                                  INVALID COMPONENT INDEED      -ARB82-
         B     PRLENERR
NOCOUNT  CH    R5,=H'7'
         BNE   PRLENERR           *** WARNING *** MODIFIED IN COPY/MOD *
                                                  OPTION ROUTINES.
         MVI   COUNTFLG,C'N'
         B     SRCHPARM
COUNT    CH    R5,=H'5'           FINAL PROCESSING OF COUNT OPTION
         BNE   UNRECOG
         MVI   COUNTFLG,C'Y'
         B     SRCHPARM
UNLABEL  CH    R5,=H'7'                                         -ARB79-
         BNE   UNRECOG
         MVI   SVOUTFLG,C'N'      SET 'COPY VOLSER' FLAG        -ARB79-
         OI    COPYFLG,X'04'      OR IN 'UNLABEL' BIT           -ARB82-
         B     SRCHPARM
COPY     CH    R5,=H'4'
         BNE   UNRECOG
         OI    COPYFLG,X'01'      INDICATE COPY OPTION SPECIFIED
         OI    WRTFLG,X'01'       INDICATE COPY   REQUESTED (THAT BIT)
         OI    NOCOUNT+5,X'F0'    NOP OUT NOCOUNT & SET FOR WARN MSG
         B     COUNT+8            COPY OPTION INVOKES COUNT AUTOMATICLY
EOVMOD   CH    R5,=H'6'           ADD DATASETS ONTO EOV
         BNE   UNRECOG
         OI    COPYFLG,X'03'      BITS = 'COPY OPT + MOD OPT REQUESTED'
         B     COPY+8             MOD OPTION IMPLIES COPY OPTION
OPT      CH    R5,=H'4'                                         -ARB79-
         BNH   PRLENERR
         LA    R4,4(R4)                                         -ARB79-
         SH    R5,=H'4'                                         -ARB79-
         BAL   R2,GETNUM
         O     R6,OPTNO           OR PREVIOUS OPTIONS INTO NEW OPTIONS
         ST    R6,OPTNO
         B     SRCHPARM
ERRLIM   CH    R5,=H'7'           CHANGE ERROR COUNT LIMIT      -ARB79-
         BNH   PRLENERR
         LA    R4,7(R4)                                         -ARB79-
         SH    R5,=H'7'                                         -ARB79-
         BAL   R2,GETNUM
         ST    R6,SYNADNO
         B     SRCHPARM
NOLIST   CH    R5,=H'6'           FINAL PROCESSING OF NOLIST PARM
         BNE   UNRECOG
         SR    R0,R0
         ST    R0,LISTNO
         B     SRCHPARM
LIST     CH    R5,=H'5'                                         -ARB79-
         BNH   PRLENERR
         LA    R4,5(R4)                                         -ARB79-
         SH    R5,=H'5'           GET LENGTH OF PRESUMED NUMBER -ARB79-
         BAL   R2,GETNUM          GET PRESUMED NUMBER INTO BINARY FORM
         ST    R6,LISTNO          STORE NONNEGATIVE BINARY INTEGER
         B     SRCHPARM
HEX      CH    R5,=H'3'                                         -ARB79-
         BNE   UNRECOG
         MVI   HEXFLG,C'Y'                                      -ARB79-
         B     SRCHPARM
MAPONLY  CH    R5,=H'7'                                         -ARB82-
         BNE   UNRECOG                                          -ARB82-
SETMAP   MVI   MAPFLG,C'Y'        SHOW MAP SELECTED             -ARB82-
         B     SRCHPARM                                         -ARB82-
MAPABBR  CH    R5,=H'3'                                         -ARB82-
         BNE   UNRECOG                                          -ARB82-
         B     SETMAP                                           -ARB82-
TRANSIN  CH    R5,=H'8'                                         -ARB79-
         BNE   UNRECOG                                          -ARB79-
         MVI   ASCIIN,C'Y'        SET ASCII INPUT FLAG          -ARB79-
         B     SRCHPARM                                         -ARB79-
TRANSOUT CH    R5,=H'9'                                         -ARB79-
         BNE   UNRECOG                                          -ARB79-
         MVI   ASCIOUT,C'Y'       SET ASCII OUTPUT FLAG         -ARB79-
         B     SRCHPARM                                         -ARB79-
NOSUMARY CH    R5,=H'5'                                         -ARB79-
         BNE   UNRECOG
         MVI   SUMFLG,C'N'        SET SUMMARY FLAG TO 'NOSUMMARY'
         B     SRCHPARM
FULLSUM  CH    R5,=H'7'                                         -ARB79-
         BNE   UNRECOG                                          -ARB79-
         MVI   SUMFLG,C'F'        SET SUMMARY FLAG TO FULL      -ARB79-
         B     SRCHPARM                                         -ARB79-
SKIPTM   CH    R5,=H'7'                                         -ARB79-
         BNH   PRLENERR
         LA    R4,7(R4)                                         -ARB79-
         SH    R5,=H'7'                                         -ARB79-
         BAL   R2,GETNUM
         ST    R6,SKIPTMNO
         ST    R6,SKIPTMVL                                      -ARB82-
         LTR   R6,R6              SKIPTM=0?                     -ARB79-
         BZ    SRCHPARM           YES.                          -ARB79-
         MVI   POSFLG,C'Y'        NO, SET POSITIONING NEEDED    -ARB79-
         B     SRCHPARM
LINE     CH    R5,=H'5'            IS THIS PARM 'LINE'?         -ARB82-
         BNH   PRLENERR            NOPE, LENGTH ERROR      *HMD 04/79*
         LA    R4,5(R4)            BUMP POINTER                 -ARB82-
         SH    R5,=H'5'            DECREMENT COUNTER            -ARB82-
         BAL   R2,GETNUM           GET NUMERIC VALUE       *HMD 04/79*
         CH    R6,=H'30'           TOO LOW?                *HMD 04/79*
         BL    NUMERR              YES, FORGET IT          *HMD 04/79*
         CH    R6,=H'99'           TOO HIGH?               *HMD 04/79*
         BH    NUMERR              YES, FORGET IT          *HMD 04/79*
         ST    R6,LINECNT          SAVE LINE COUNT PARM    *HMD 04/79*
         B     SRCHPARM            GET SOME MORE PARMS     *HMD 04/79*
STOPTM   CH    R5,=H'7'                                         -ARB79-
         BNH   PRLENERR
         LA    R4,7(4)                                          -ARB79-
         SH    R5,=H'7'                                         -ARB79-
         BAL   R2,GETNUM
         LTR   R6,R6              MAKE SURE MAXTM IS NOT =0
         BZ    NUMERR             ERROR - INVALID NUMERICAL PARM
         ST    R6,MAXTMNO
         ST    R6,STOPTMNO        STORE FOR TERMINATION         -ARB82-
         B     SRCHPARM
SKIPEOV  CH    R5,=H'8'                                         -ARB79-
         BNH   PRLENERR
         LA    R4,8(R4)                                         -ARB79-
         SH    R5,=H'8'                                         -ARB79-
         BAL   R2,GETNUM
         ST    R6,SKPEOVNO
         LTR   R6,R6              SKIPEOV=0?                    -ARB79-
         BZ    SRCHPARM           YES.                          -ARB79-
         MVI   POSFLG,C'Y'        NO, INDICATE POSITIONING      -ARB79-
         B     SRCHPARM
STOPEOV  CH    R5,=H'8'           CHECK LENGTH OF PARM          -ARB79-
         BNH   PRLENERR           LENGTH MUST BE GREATER THAN 7
         LA    R4,8(R4)           BUMP PTR TO START OF NUMBER   -ARB79-
         SH    R5,=H'8'           GET LENGTH OF PRESUMED NUMBER -ARB79-
         BAL   R2,GETNUM          CONVERT NUMBER FOLLOWING 'MAXEOV' PRM
         LTR   R6,R6              MAKE SURE MAXEOV IS NOT=0
         BZ    NUMERR             INVALID NUMERICAL COMPONENT
         ST    R6,MAXEOVNO        STORE RESULT
         ST    R6,STPEOVNO        STORE FOR END OF VTOC         -ARB82-
         B     SRCHPARM
ENDPARMS DS    0H
         TM    COPYFLG,X'01'      COPYING?                      -ARB82-
         BZ    COPYSUM            NO.                           -ARB82-
         CLI   SUMFLG,C'N'        YES, NOSUM ALSO               -ARB82-
         BNE   COPYSUM            NO.                           -ARB82-
         MVI   SUMFLG,C'Y'        YES, FORCE SUMMARY INSTEAD    -ARB82-
COPYSUM  CLI   MAPFLG,C'Y'        MAPONLY SELECTED?             -ARB82-
         BNE   NOMAPO             NO.                           -ARB82-
         ICM   R1,15,LISTNO       LIST=N SPECIFIED?             -ARB82-
         BP    NOMAPO             YES, IGNORE MAPONLY           -ARB82-
         CLI   SUMFLG,C'Y'        NOSUM OR FULLSUM SELECTED?    -ARB82-
         BNE   NOMAPO             YES, IGNORE MAPONLY           -ARB82-
         MVC   LISTNO,=F'0'       NO, FORCE NOLIST              -ARB82-
         B     POSIT              GO DO POSITIONING             -ARB82-
NOMAPO   MVI   MAPFLG,C'N'        MAKE SURE MAPONLY IS OFF      -ARB82-
         CLI   COUNTFLG,C' '      COUNT STILL UNSET?            -ARB82-
         BNE   DEFLIST            NO.                           -ARB82-
         MVI   COUNTFLG,C'Y'      YES, DEFAULT TO YES           -ARB82-
DEFLIST  CLC   LISTNO,=F'-1'      LIST STILL UNSET?             -ARB82-
         BNE   POSIT              NO.                           -ARB82-
         MVC   LISTNO,=F'4'       YES, DEFAULT TO LIST=4        -ARB82-
         EJECT
POSIT    BALR  R12,0              SET UP BASE REG AGAIN         -ARB82-
         USING *,R12              (ADDRESS THE WORLD)           -ARB82-
         LA    R1,=AL3(SENSCMND)  SET UP PTR FOR EXECEXCP CALL  -ARB82-
         LA    R2,INPUT           PTR TO DCB  FOR EXCP CALL
         BAL   R4,EXECEXCP        CALL EXCP SUBROUTINE
         MVC   ITRACKS,SENSBYTS+1 SAVE SENSE FOR 7/9 TEST       -ARB79-
         MVC   CTRACKS,ITRACKS                                  -ARB79-
         TM    SENSBYTS+1,X'08'   SEE IF LOAD POINT SENSED
         BO    SENSTYP            BIF LOAD POINT SENSED         -ARB82-
         TM    OPTNO+3,X'08'      SEE IF 'DO NOT ATTEMPT REWIND' SPEC'D
         BO    COPYMOD            SKIP REWIND ATTEMPT IF SO SPECIFIED
         LA    R1,=AL3(RWNDCMND)  REWIND AND RE-SENSE COMMAND CHAIN
         LA    R2,INPUT           PTR TO DCB  FOR EXCP CALL
         BAL   R4,EXECEXCP
         TM    SENSBYTS+1,X'08'   SEE IF NOW AT LOAD POINT
         BO    SENSTYP            BIF AT LOAD POINT             -ARB82-
LDPTERR  BAL   R2,PUTLINE         PRINT ERROR MESSAGE             -HMD-
         MVC   MSGBUF(66),=C'0UNABLE TO REWIND INPUT TAPE TO LOAD POINT*
                - TERMINATING EXECUTION'
         B     EXITRC16                                         -ARB79-
SENSTYP  TM    SENSBYTS+1,X'10'   SEE IF 7-TRK                  -ARB82-
         BO    DEN7TRK            BIF 7-TRK(DEN NOT APPLICABLE) -ARB82-
         MVC   DENMSG+10(8),=C'PHYSICAL' CHANGE MSG FOR 9TRACK  -ARB82-
         LA    R1,HDEOD           FAKE EOD ADDRESS                -HMD-
         ST    R1,DCBEODAD+INPUT  SAVE EOD ADDRESS IN DCB       -ARB82-
         LA    R1,SYNCONTU        IGNORE READ ERRORS            -ARB82-
         ST    R1,DCBSYNAD+INPUT                                -ARB82-
         LA    R2,RECBUF                                        -ARB82-
         READ  SENSER,SF,INPUT,(R2),'S' READ TO GET SENSE INFO  -ARB82-
         CHECK SENSER               WAIT FOR I/O                -ARB82-
HDEOD    LA    R1,=AL3(SENSCMND)  NOW GET THE SENSE BYTES         -HMD-
         LA    R2,INPUT           POINT TO INPUT DCB              -HMD-
         XC    SENSBYTS(24),SENSBYTS                              -HMD-
         BAL   R4,EXECEXCP        ISSUE EXCP                      -HMD-
RESTREOD L     R1,=A(EODS)        NORMAL EOD ADDRESS            -ARB82-
         ST    R1,INPUT+DCBEODAD  PUT IT BACK AS IT WAS         -ARB82-
         LA    R1,SYNERR          RESTORE USUAL SYNAD           -ARB82-
         ST    R1,INPUT+DCBSYNAD                                -ARB82-
         TM    SENSBYTS+3,X'04'   BIT=1=PE MODE=1600 BPI=DEN=3  -ARB82-
         BO    DEN1600       SKIP IF DENSITY SENSED=1600 BPI    -ARB82-
         TM    SENSBYTS+5,X'C0'   SEE IF 3400 TYPE              -ARB82-
         BZ    HDEND                                            -ARB82-
         TM    SENSBYTS+6,X'08'   6250 FEATURE?                 -ARB82-
         BZ    HDEND              NO, SKIP 6250 MOVE            -ARB82-
         MVC   DENMSG+1(4),=C'6250'                             -ARB82-
         B     HDEND              REWIND TAPE TO LOAD POINT     -ARB82-
DEN1600  MVC   DENMSG+1(4),=CL4'1600' REPLACE  800 WITH 1600    -ARB82-
HDEND    DS    0H                 ENOUGH OF ALL THIS NONSENSE     -HMD-
         LA    R1,=AL3(RWNDCMND)  REWIND TO LOAD POINT            -HMD-
         LA    R2,INPUT           GET INPUT ADDRESS               -HMD-
         BAL   R4,EXECEXCP        GO DO IT                        -HMD-
         TM    SENSBYTS+1,X'08'   ARE WE AT LOAD POINT NOW?       -HMD-
         BZ    LDPTERR            NO, INDICATE LOAD ERROR         -HMD-
DENOUT   BAL   R2,PUTLINE                                       -ARB82-
         MVC   MSGBUF(40),DENMSG                                -ARB82-
         B     COPYMOD                                          -ARB82-
DEN7TRK  CLI   DCBDEN+INPUT,X'43' GET DENSITY FROM 7TRK DCB     -ARB82-
         BL    DEN200                                           -ARB82-
         BH    DENOUT                                           -ARB82-
TRY556   DS    0H ANYONE STILL USING THESE TURKEYS? DO YOUR OWN THING
* I AM, AND WHOSE CALLING THEM TURKEYS, SUCKER                   -HMD-
         MVC   DENMSG+1(4),=C' 556'                             -ARB82-
         B     DENOUT             GO WRITE DENSITY ASSUMED MSG  -ARB82-
DEN200   MVC   DENMSG+1(4),=C' 200'                             -ARB82-
         B     DENOUT             WRITE DENSITY ASSUMED MSG     -ARB82-
COPYMOD  DS    0H
         TM    COPYFLG,X'01'      SEE IF COPY AND/OR EOVMOD SPECIFIED
         BZ    NOTBOTH            BIF COPY OPTION NOT SPECIFIED
         DEVTYPE OUTPUT+40,DTYPE  CHECK FOR MAG TAPE DEVICE       -HMD-
         LTR   R15,R15            IS DD STMT THERE?               -HMD-
         BNZ   NOOUTPUT           NOPE                            -HMD-
         CLI   DTYPE+2,X'80'      IS THIS A TAPE DEVICE?          -HMD-
         BNE   NOTTAPEO           NOPE                            -HMD-
         RDJFCB (OUTPUT)          ELSE PROCESS COPY AND/OR EOVMOD OPTNS
         CLI   JFCBOUT,X'00'      SEE IF JFCBOUT STILL = ZERO
         BE    NOOUTPUT           ERROR IF RDJFCB LEFT JFCBOUT STILL =0
         MVC   OUTLTYP,JFCBOUT+JFCBLTYP SAVE LABEL TYPE FOR OUTPUT TAPE
         NI    OUTLTYP,X'F7'      OFF USER LABEL BIT            -ARB79-
         TM    OUTLTYP,X'01'      SEE IF LABEL IS NL OR LTM
         BO    *+8                BIF YES - IT IS NL OR LTM
         MVI   JFCBOUT+JFCBLTYP,X'10' ELSE SET TO BLP
         NI    JFCBOUT+JFCOPTCD,X'F7'  MAKE SURE OPTCD NOT Q    -ARB79-
         TM    OUTLTYP,X'02'      SL OUTPUT?                    -ARB79-
         BZ    NOUTSL                                           -ARB79-
         MVC   OTRTCH,JFCTRTCH+JFCBOUT SAVE TRTCH FROM JFCB     -ARB79-
         CLI   OTRTCH,0           TRTCH= (NULL)?                -ARB82-
         BNE   *+8                NO.                           -ARB82-
         MVI   OTRTCH,X'33'       YES, CHANGE FOR SFM LATER     -ARB82-
         MVI   JFCTRTCH+JFCBOUT,X'2B'  SET TRTCH=ET FOR LABEL   -ARB79-
NOUTSL   LA    R0,1                                             -ARB79-
         LH    R2,JOUTFLSQ        SAVE ORIGINAL FILE SEQ. NO. IN R2
         LTR   R2,R2              BUT IF IT'S
         BNZ   NZOUT              EQUAL TO ZERO THEN            -ARB80-
         LR    R2,R0              SET IT TO = 1.
         MVI   OUTZERO,X'FF'      NOTE OUT FILE = 0             -ARB80-
NZOUT    STH   R0,JOUTFLSQ        TEMPORARILY SET FLSQ = 1      -ARB80-
         OPEN  (OUTPUT,(INOUT)),TYPE=J                          -ARB79-
         TM    OUTPUT+DCBOFLGS,X'10' CHECK IF OPENED SUCCESSFULLY
         BZ    NOOUTPUT
         MVC   SECVOL,JFCBOUT+JFCBVOLS COPY VOLSER FOR EXIT     -ARB82-
         MVC   SECDCB,=A(OUTPUT)  STORE DCB ADDR FOR EXIT       -ARB82-
         STH   R2,JOUTFLSQ        RESTORE ORIGINAL FLSQ IN JFCB
         TM    OUTLTYP,X'42'      SEE IF AL, AUL, SL, OR SUL
         BZ    CHKOPOS            BIF NOT ONE OF ABOVE          -ARB82-
         MH    R2,=H'3'           CONVERT FILE NO TO TM NO      -ARB79-
         SH    R2,=H'2'                                         -ARB79-
         STH   R2,JOUTFLSQ                                      -ARB79-
CHKOPOS  CLC   JOUTFLSQ,=H'1'     OUTPUT FILE=1?                -ARB82-
         BH    SKIPVOL1           NO, DON'T COPY VOL1           -ARB82-
         TM    COPYFLG,X'02'      YES, WRITING TO END?          -ARB82-
         BZ    LABELCK            NO, LEAVE UNLABEL AS SET      -ARB82-
SKIPVOL1 NI    COPYFLG,X'FB'      MAKE SURE VOL1 NOT COPIED     -ARB82-
LABELCK  MVC   RECBUF(136),BLANKBUF BLANK PART OF RECBUF FAST
         MVI   ERRLAST,0          TURN OFF PREVIOUS ERR FLAG    -ARB79-
         LA    R2,RECBUF          PREPARE TO READ PRESUMED VOL LABEL
         READ  TAPEIO,SF,OUTPUT,(R2),'S' READ PRESUMED VOL LABEL
         CHECK TAPEIO             SYNAD=SYNERR,EODAD=EOLABEL
         L     R6,TAPEIO+16       START TO COMPUTE LENGTH OF BLK READ
         LH    R7,OUTPUT+DCBBLKSI
         SH    R7,14(R6)          COMPUTE BLOCK LENGTH
         BAL   R2,LENCK           CHECK FOR NEGATIVE LENGTH     -ARB79-
         TM    OUTLTYP,X'40'      SEE IF SL OR SUL, OR AL OR AUL
         BZ    COPYSL             BIF S(U)L                     -ARB79-
         MVI   SECLABEL,C'A'      SET AL FOR SECURITY           -ARB82-
         CLC   RECBUF(4),ASCIVOL1 SEE IF ANSI  'VOL1'           -ARB79-
         BNE   BADOUTVL           AL OR AUL IN JCL, BUT LABEL NOT ANSI
         MVI   ASCIOUT,C'Y'       NOTE OUTPUT ASCII             -ARB79-
         C     R7,=F'80'
         BL    BADOUTVL
         XLATE RECBUF,80,TO=E     GET LABEL INTO EBCDIC         -ARB79-
         CLI   RECBUF+10,C' '     ASCII VOLUME ACCESIBLE?       -ARB79-
         BE    CKVOLSER           YES.                          -ARB79-
         BAL   R2,PUTLINE         NO, STOP HERE                 -ARB79-
         MVC   MSGBUF(L'ASCURITY),ASCURITY                      -ARB79-
         B     EXITRC16                                         -ARB79-
COPYSL   TM    OUTLTYP,X'02'      SEE IF NO SL OR AL            -ARB79-
         BZ    COPYNL             MAKE SURE TAPE NOT LABELLED   -ARB79-
         MVI   SECLABEL,C'S'      TELL EXIT TAPE IS SL          -ARB82-
         C     R7,=F'80'          SEE IF PRESUMED IBM SL IS 80 BYTES
         BNE   BADOUTVL           BIF PRESUMED IBM LABEL NOT 80 BYTES
         MVI   ASCIOUT,C'N'       MAKE SURE ASCIIÂ¬=OUT          -ARB79-
         CLC   RECBUF(4),=C'VOL1'
         BNE   BADOUTVL
CKVOLSER CLC   JFCBOUT+JFCBVOLS(6),RECBUF+4 COMPARE VOL SERS    -ARB82-
         BNE   INCOROUT           ERROR IF NO MATCH             -ARB82-
         MVI   ERRLAST,0          FORGET PREVIOUS ERROR         -ARB79-
         MVC   DCBEODAD+OUTPUT(4),=A(MISNGHDR) DON'T BE UPSET BY EOF   *
                                                                -ARB79-
FINDHDR1 LA    R2,RECBUF                                        -ARB82-
         READ  TAPEIO2,SF,OUTPUT,(R2),'S' TRY TO READ HDR       -ARB79-
         CHECK TAPEIO2                                          -ARB79-
         OI    COPYFLG,X'80'      INDICATE MAY NEED TO BSP      -ARB79-
         L     R6,TAPEIO2+16      DETERMINE SIZE RECORD READ    -ARB79-
         LH    R7,OUTPUT+DCBBLKSI                               -ARB79-
         SH    R7,14(,R6)                                       -ARB79-
         BAL   R2,LENCK           CHECK FOR NEGATIVE LENGTH     -ARB79-
         C     R7,=F'80'          LABEL SIZED RECORD?           -ARB79-
         BL    SCUROUT            NO, NO HEADER LABEL           -ARB82-
         TM    OUTLTYP,X'02'      SL OR AL TAPE?                -ARB79-
         BZ    AHDR1              BRANCH IF AL                  -ARB79-
         C     R7,=F'80'          EXACTLY 80 CHARS?             -ARB79-
         BNE   SCUROUT            NO, NOT A LABEL               -ARB82-
         B     OUTSEQ                                           -ARB79-
AHDR1    XLATE RECBUF,(R7),TO=E   TRANSLATE TO EBCDIC           -ARB79-
OUTSEQ   CLC   RECBUF(4),=C'HDR1' IS IT REALLY A HDR LABEL?     -ARB79-
         BE    OUTHDR             YES.                          -ARB82-
         TM    OUTLTYP,X'02'      AL OUTPUT?                    -ARB82-
         BNZ   SCUROUT            NO.                           -ARB82-
         CLC   RECBUF(3),=C'UVL'  YES, USER VOLUME LABEL NEXT?  -ARB82-
         BE    FINDHDR1           YES, SKIP TO NEXT LABEL       -ARB82-
OUTHDR   CLI   RECBUF+53,C'1'     PROTECTED DATA SET?           -ARB82-
         BE    RQROUTPW           YES, ASK FOR PASSWORD         -ARB79-
         CLI   RECBUF+53,C'3'                                   -ARB79-
         BE    RQROUTPW                                         -ARB79-
         CLI   ASCIOUT,C'Y'       OUTPUT TAPE ASCII?            -ARB79-
         BNE   NOUTPW             NO, UNPROTECTED               -ARB79-
         CLI   RECBUF+53,C' '     YES, IS IT UNPROTECTED?       -ARB79-
         BE    NOUTPW                                           -ARB79-
         BAL   R2,PUTLINE         NO, DATASET CANNOT BE USED    -ARB79-
         MVC   MSGBUF(71),=C'0PROTECTED ASCII TAPE FILE MAY NOT BE OVER*
               WRITTEN - TAPESCAN TERMINATED'                   -ARB79-
         B     EXITRC16                                         -ARB79-
RQROUTPW L     R5,16              BUILD PASSWORD MESSAGE        -ARB79-
         L     R5,0(,R5)                                        -ARB79-
         L     R5,4(,R5)                                        -ARB79-
         L     R5,TCBTIO(,R5)     FIND TIOT FOR JOB & STEP NAME -ARB79-
         MVC   SMSGJOB,TIOCNJOB(R5)                             -ARB79-
         MVC   SMSGSTEP,TIOCSTEP(R5)                            -ARB79-
         MVC   SMSGDD,=CL8'OUTPUT'     INSERT DDNAME            -ARB79-
         MVI   PSWDSW,0           INDICATE FIRST PASSWORD TRY   -ARB79-
ASKOUTPW LA    R2,SMSG            IEC301 MSG ADDR               -ARB79-
         BAL   R5,SYSWTOR         ASK FOR PASSWORD              -ARB79-
         OC    PSWDREPL,BLANKBUF  UPPER CASE REPLY              -ARB79-
         LA    R2,JFCBOUT         POINT TO SUPPLIED DSN         -ARB79-
         BAL   R5,CHKPASS         SEE IF PASSWORD FOUND         -ARB79-
         LTR   R15,R15            WAS PASSWORD FOUND?           -ARB79-
         BZ    GOTOUTPW                                         -ARB79-
WROUTPW  CLI   PSWDSW,0           WAS THIS THE SECOND TRY?      -ARB79-
         BNE   BADOUTPW           YES, LAST CHANCE FAILED       -ARB79-
         MVI   PSWDSW,X'FF'       NO, SET SECOND TRY FLAG       -ARB79-
         B     ASKOUTPW           AND TRY AGAIN                 -ARB79-
BADOUTPW BAL   R2,PUTLINE         EXPLAIN TERMINATION           -ARB79-
         MVC   MSGBUF(62),=C'0OUTPUT PASSWORD NOT CORRECTLY SPECIFIED -*
                TAPESCAN TERMINATED'                            -ARB79-
         B     EXITRC16                                         -ARB79-
GOTOUTPW TM    PSWDBUF+2,X'01'    IS THIS A WRITE PASSWORD?     -ARB79-
         BZ    WROUTPW            NO, NOT GOOD ENOUGH           -ARB79-
         B     EXPIRED            YES, NO DATE CHECK AFTER PSWD -ARB79-
NOUTPW   TM    COPYFLG,X'02'      ADDING TO TAPE END            -ARB79-
         BNZ   EXPIRED            YES, NO DATE CHECK REQUIRED   -ARB79-
         CLC   RECBUF+48(5),TODAY HAS THE EXPIRATION DATE PAST? -ARB79-
         BNH   EXPIRED            YES, OK TO WRITE THIS TAPE    -ARB79-
         MVC   EXPDSN(17),RECBUF+4     NO, BUILD OPERATOR MSG   -ARB79-
         MVC   EXPVOL,JFCBOUT+JFCBVOLS PUT VOLSER INTO MSG      -ARB79-
         LH    R2,OUTPUT+DCBTIOT  FIND TIOT OFFSET FOR OUTPUT   -ARB79-
         L     R5,16              FIND TIOT THRU TCB            -ARB79-
         L     R5,0(,R5)                                        -ARB79-
         L     R5,4(,R5)                                        -ARB79-
         L     R5,TCBTIO(,R5)                                   -ARB79-
         MVC   EXPJOB(8),TIOCNJOB(R5)  PUT JOB & STEP NAME      -ARB79-
         MVC   EXPSTEP(8),TIOCSTEP(R5) INTO MSG                 -ARB79-
         AR    R2,R5              FIND OUTPUT TIOT ENTRY        -ARB79-
         L     R2,TIOEFSRT(R2)    FIND OUTPUT'S UCB             -ARB79-
         MVC   EXPUNIT,UCBNAME(R2)     GET TAPE UNIT FROM UCB   -ARB79-
EMSG     LA    R2,EXPMSG                                        -ARB79-
         BAL   R5,SYSWTOR         ASK FOR PERMISSION TO WRITE   -ARB79-
         OI    EXPREPLY,X'40'     UPPER CASE REPLY              -ARB79-
         CLI   EXPREPLY,C'U'      IS IT OK?                     -ARB79-
         BE    EXPIRED            YES, GO ON                    -ARB79-
         CLI   EXPREPLY,C'M'      WAS IT FORBIDDEN?             -ARB79-
         BNE   EMSG               NO, ASK AGAIN                 -ARB79-
         BAL   R2,PUTLINE         INDICATE COPY IMPOSSIBLE      -ARB79-
         MVC   MSGBUF(61),=C'0OUTPUT TAPE CANNOT BE USED - EXPIRATION D*
               ATE NOT YET REACHED'                             -ARB79-
         B     EXITRC16           AND THEN GIVE UP              -ARB79-
EXPIRED  MVC   SECDSN,RECBUF+4    COPY DSNAME FROM LABEL        -ARB82-
SCUROUT  BAL   R14,SCUREXIT       SEE IF INSTALLATION APPROVES  -ARB82-
         LTR   R15,R15            IS IT OK?                     -ARB82-
         BZ    SCUROK             YES.                          -ARB82-
         BAL   R2,PUTLINE         NO, SAY SO                    -ARB82-
         MVC   MSGBUF(65),=C'0TAPESCAN TERMINATED - YOU ARE UNAUTHORIZE*
               D TO WRITE ON THIS TAPE'                         -ARB82-
         B     EXITRC16           GIVE UP                       -ARB82-
SCUROK   CLI   SECLABEL,C'N'      NL OUTPUT?                    -ARB82-
         BE    REWNDCK2           YES, REJOIN NL FLOW           -ARB82-
         TM    COPYFLG,X'02'      COPYING TO END?               -ARB79-
         BNZ   SEQATEND           YES, GET FIRST FILE SEQ       -ARB79-
         CLI   POSFLG,C'Y'        INPUT TAPE FILE=1?            -ARB79-
         BNE   INPUTF1            IF SO, MAY USE INPUT FILE SEQ -ARB79-
         ZAP   FIRSTSEQ,=P'1'     SET OUTPUT SEQ TO 1           -ARB79-
INPUTF1  CLC   JOUTFLSQ(2),=H'1'  WRITING TO FILE>1?            -ARB79-
         BNE   GOUTSEQ                                          -ARB80-
         CLI   OUTZERO,X'FF'      OUTPUT FILE OMITTED?          -ARB80-
         BE    REWINDCK           YES.                          -ARB80-
         ZAP   FIRSTSEQ,=P'1'     NO, FORCE OUTPUT SEQ TO 1     -ARB80-
         B     REWINDCK                                         -ARB80-
SEQATEND CLC   RECBUF+31(4),=C'000000' IEHINITT HDR ONLY?       -ARB79-
         BNE   GOUTSEQ            NO, SAVE FIRST FILE SEQUENCE  -ARB79-
         NI    COPYFLG,X'FD'      YES, FORGET EOVMOD            -ARB79-
         ZAP   FIRSTSEQ,=P'1'     AND USE OUTPUT SEQ 1          -ARB79-
         B     REWINDCK                                         -ARB79-
GOUTSEQ  PACK  FIRSTSEQ,RECBUF+31(4)   SAVE FIRST FILE SEQ      -ARB79-
         B     REWINDCK                                         -ARB79-
COPYNL   C     R7,=F'80'          WAS AN 80-BYTE BLK READ?      -ARB79-
         BL    OUTNL              NO, TAPE IS NL                -ARB79-
         CLC   RECBUF(4),ASCIVOL1 CHECK FOR ASCII LABEL         -ARB79-
         BE    BADOUTNL           DON'T ALLOW IF FOUND          -ARB79-
         C     R7,=F'80'          IS LENGTH EXACTLY 80?         -ARB79-
         BNE   OUTNL              NO, SL IMPOSSIBLE             -ARB79-
         CLC   RECBUF(4),=C'VOL1' CHECK FOR S LABEL             -ARB79-
         BE    BADOUTNL                                         -ARB79-
OUTNL    TM    JFCLTSV,X'BD'      NL INPUT?                     -ARB82-
         BNZ   SCUROUT            YES, PROCEED                  -ARB82-
         CLI   SVOUTFLG,C'N'      NO, WAS LABEL CHANGE ALLOWED? -ARB82-
         BE    SCUROUT            YES, LET IT GO                -ARB82-
         BAL   R2,PUTLINE                                       -ARB83-
         MVC   MSGBUF(53),=C'0COPY OF LABELLED TAPE TO UNLABELLED TAPE *
               NOT ALLOWED'                                     -ARB82-
         B     LABELMX2           COMPLETE THE MSG              -ARB82-
MISNGHDR LA    R2,OUTPUT          BACKSPACE PAST SURPRISE       -ARB79-
         LA    R1,=AL3(BSFCMND)   TAPE MARK                     -ARB79-
         BAL   R4,EXECEXCP                                      -ARB79-
         MVC   RECBUF+31(4),=C'0001'   SIMULATE FILE SEQ OF 1   -ARB82-
         B     SCUROUT            GO MAKE SECURITY CHECK        -ARB82-
REWINDCK CLI   SVOUTFLG,C'Y'      CHECK FOR UNLABEL             -ARB79-
         BE    COPYOUT            NO REWIND IF NOT              -ARB79-
REWNDCK2 NI    COPYFLG,X'7F'      INDICATE NO BSP NEEDED        -ARB82-
         MVC   OUTLTYP,JFCLTSV    IF SO, MAKE OLABEL=ILABEL     -ARB79-
         CLC   JOUTFLSQ(2),=H'1' DO NOT REWIND IF SEQ. NO. > 1  -ARB82-
         BH    LABELSOK           NO REWIND IF FILE SEQ. NO. >1 -ARB79-
         TM    COPYFLG,X'02'      DO NOT REWIND IF EOVMOD SPECIFIED
         BO    LABELSOK           NO REWIND IF EOVMOD SPECIFIED -ARB79-
REWINDER LA    R1,=AL3(RWNDCMND)  REWIND CMD CHAINED TO SENSE   -ARB79-
         LA    R2,OUTPUT          OUTPUT DCB
         BAL   R4,EXECEXCP        REWIND AND SENSE
         MVC   OTRACKS,SENSBYTS+1 SAVE SENSE FOR 7/9 TEST       -ARB79-
         OC    CTRACKS,OTRACKS    SET ANY 7-TRACK BIT           -ARB79-
         TM    SENSBYTS+1,X'08'   SEE IF LOAD POINT SENSED
         BO    COPYOUT2           BIF SENSED LOADPOINT          -ARB79-
         BAL   R2,PUTLINE
         MVC   MSGBUF(67),=C'0UNABLE TO REWIND OUTPUT TAPE TO LOAD POIN*
               T - TERMINATING EXECUTION'
         B     EXITRC16                                         -ARB79-
BADOUTVL BAL   R2,PUTLINE
         MVC   MSGBUF(32),=C'0OUTPUT VOLUME LABEL IS INVALID:'
         BAL   R5,LISTON
         B     EXITRC16                                         -ARB79-
INCOROUT MVC   TRUESER,RECBUF+4   COPY ACTUAL VOLSER TO MSG     -ARB82-
         BAL   R2,PUTLINE         WRITE ERROR MSG               -ARB82-
         MVC   MSGBUF(INCORLEN),INCORMSG                        -ARB82-
         B     EXITRC16           GIVE UP WITH PREJUDICE        -ARB82-
LABELMIX BAL   R2,PUTLINE         SEND ERROR MSG                -ARB79-
         MVC   MSGBUF(72),=C'0COPY OF UNLABELED TAPE TO LABELED TAPE MA*
               Y DESTROY OUTPUT TAPE VALIDITY'                  -ARB79-
LABELMX2 BAL   R2,PUTLINE                                       -ARB82-
         MVC   MSGBUF(56),=C' SPECIFY THE ''UNLABEL'' OPTION IF THIS IS*
                TO BE PERMITTED'                                -ARB82-
         B     EXITRC16                                         -ARB79-
BADOUTNL BAL   R2,PUTLINE         STOP NOW IF LABEL WRONG       -ARB79-
         MVC   MSGBUF(73),=C'0LABEL FOUND ON OUTPUT TAPE DESCRIBED AS U*
               NLABELED - EXECUTION TERMINATED'                 -ARB79-
         B     EXITRC16                                         -ARB79-
COPYOUT  TM    OUTLTYP,X'42'      LABELLED OUTPUT?              -ARB79-
         BZ    COPYOUT2           NO, GO POSITION OUTPUT TAPE   -ARB82-
         TM    COPYFLG,X'80'      NEED TO BACKSPACE TO VOL1     -ARB82-
         BZ    NOBSP                                            -ARB79-
         CLC   JOUTFLSQ(2),=H'1'  WRITING TO FILE 1?            -ARB79-
         BNE   NOBSP              NO, NO BSP NEEDED             -ARB79-
         TM    COPYFLG,X'02'      WRITING TO END?               -ARB79-
         BNZ   NOBSP              YES, DON'T BOTHER BSPING      -ARB79-
         CNTRL OUTPUT,BSR         YES, SKIP BACK                -ARB79-
NOBSP    CLC   OUTLTYP,JFCLTSV    TAPES LABELLED SIMILARLY?     -ARB79-
         BE    LABELSOK           GREAT IF SO                   -ARB79-
         TM    JFCLTSV,X'BD'      NO, IS INPUT UNLABELLED?      -ARB79-
         BNZ   LABELMIX           YES, FORBID IT                -ARB79-
LABELSOK LA    R1,=AL3(SENSCMND)  ISSUE SENSE TO GET UNIT TYPE  -ARB79-
         LA    R2,OUTPUT                                        -ARB79-
         BAL   R4,EXECEXCP                                      -ARB79-
         MVC   OTRACKS,SENSBYTS+1      SAVE SENSE BITS          -ARB79-
         OC    CTRACKS,OTRACKS                                  -ARB79-
COPYOUT2 DS    0H                 EOVMOD PROCESSING DONE HERE   -ARB79-
         LA    R0,EODADOUT        ADDRESS OF NEW OUTPUT TAPE EODAD RTN
         ST    R0,OUTPUT+DCBEODAD AND SET IT UP IN DCB.
FILELOOP TM    COPYFLG,X'02'      SEE IF EOVMOD SPECIFIED       -ARB79-
         BO    FILEFSM            IF SO THEN DO SRCH FWRD FOR TPMK
         CLC   COUTFILE,JOUTFLSQ  ARE WE IN RIGHT FILE YET?
         BNL   FILEMSG            BIF YES
FILEFSM  LA    R1,=AL3(FSMCMND)   FWRD SPACE JUST PAST NEXT TAPEMARK
         LA    R2,OUTPUT
         BAL   R4,EXECEXCP
         LA    R5,1               MAINTAIN                      -ARB79-
         AH    R5,COUTFILE        CURRENT FILE                  -ARB79-
         STH   R5,COUTFILE        NUMBER.                       -ARB79-
         CH    R5,JOUTFLSQ        BIF NOT YET UP TO SPEC FILE   -ARB79-
         BL    FILEMKCK           BIF NOT YET UP TO SPECIFIED FILE
         TM    COPYFLG,X'02'      SEE IF EOVMOD SPECIFIED
         BZ    FILEMSG            BIF EOVMOD NOT SPECIFIED
FILEMKCK TM    OTRACKS,X'10'      7 TRACK OUTPUT?               -ARB79-
         BZ    PASTMARK           NO, SKIP MODE SET             -ARB79-
         TM    OUTLTYP,X'42'      SL OR AL OUTPUT?              -ARB79-
         BZ    PASTMARK           NO, NO SET MODE NEEDED        -ARB79-
         SR    R4,R4                                            -ARB79-
         D     R4,=F'3'           DETERMINE IF LABELS OR DATA   -ARB79-
         LA    R2,X'2B'           SET MODE=ET FOR LABELS        -ARB79-
         CH    R4,=H'2'                                         -ARB79-
         BNE   SETMODE                                          -ARB79-
         IC    R2,OTRTCH          SET USER MODE FOR DATA        -ARB79-
SETMODE  MODESET KEY=ZERO         OBTAIN WRITE ACCESS TO DEB    -ARB79-
         O     R2,DCBDEN+OUTPUT-3 ADD DENSITY TO OP CODE        -ARB79-
         L     R15,DCBDEBAD+OUTPUT     FIND OUTPUT DEB          -ARB79-
         STC   R2,DEBSDVM(R15)    SET NEW TRTCH                 -ARB79-
         MODESET KEY=NZERO        ABDICATE                      -ARB79-
PASTMARK LA    R2,RECBUF          READ WHAT IMMEDIATELY FOLLOWS -ARB79-
         READ  TPMKCHK,SF,OUTPUT,(R2),'S'  TAPEMARK TO CHECK FOR EOV'S.
         CHECK TPMKCHK            EXIT TO EODADOUT, NOT EOLABEL  ON EOF
         B     FILELOOP           CONTINUE SKIPPING             -ARB79-
FILEPOS  DS    0H                 BACKSPACE PAST 2ND TAPEMARK OF EOV
         LA    R2,OUTPUT          PTR TO OUTPUT TAPE'S DCB
         LA    R1,=AL3(BSFCMND)   BACKSPACE PAST ONE TAPEMARK
         BAL   R4,EXECEXCP
         LH    R0,COUTFILE        AND
         BCTR  R0,0               REDUCE CURRENT FILE NO. APPROPRIATELY
         STH   R0,COUTFILE
FILEMSG  DS    0H                 PRINT INITIAL OUTPUT POSITION MSG
         LH    R1,COUTFILE
         TM    OUTLTYP,X'42'      LABELLED OUTPUT TAPE?         -ARB79-
         BZ    NLPOS              NO.                           -ARB79-
         SR    R0,R0              YES, CONVERT POSITION BACK    -ARB79-
         D     R0,=F'3'           TO LABELLED FILE  NO          -ARB79-
         LA    R1,1(,R1)                                        -ARB79-
         MVC   POSMSG+54(1),SECLABEL  PUT LABEL TYPE IN MSG     -ARB82-
NLPOS    CVD   R1,BADLNGTH                                      -ARB79-
         OI    BADLNGTH+7,X'0F'
         UNPK  POSMSG+31(4),BADLNGTH TELL WHERE 1ST FILE OUTPUT GOES
         MVC   POSMSG+49(4),POSMSG+31 MOVE INTO EXPLANATORY COMMENT
         BAL   R2,PUTLINE
         MVC   MSGBUF(L'POSMSG),POSMSG
         TM    OUTLTYP,X'42'      OUTPUT LABELLED?              -ARB79-
         BZ    NOTBOTH                                          -ARB79-
         CLI   FIRSTSEQ,C'N'      YES, GETTING SEQ FROM OUTPUT? -ARB79-
         BE    NOTBOTH            NO.                           -ARB79-
         AP    FIRSTSEQ,BADLNGTH  YES, UPDATE FOR POSITIONING   -ARB79-
         SP    FIRSTSEQ,=P'1'                                   -ARB79-
         B     NOTBOTH
EODADOUT LA    R5,1               COUNT                         -ARB79-
         AH    R5,COUTFILE        THIS TAPEMARK                 -ARB79-
         STH   R5,COUTFILE        (THE 2ND ONE OF AN EOV).      -ARB79-
         CH    R5,JOUTFLSQ        SEE IF UP TO SPECIFIED TM     -ARB79-
         BL    FILELOOP           BIF NOT UP TO SPECIFIED TM    -ARB79-
         TM    COPYFLG,X'02'      SEE IF EOVMOD SPECIFIED
         BZ    FILELOOP           BIF NOT EOVMOD                -ARB79-
         CH    R5,JOUTFLSQ        SEE WHETHER COUTFILE GT OR JUST      *
                                  MERELY EQ JOUTFLSQ.           -ARB79-
         BE    FILEMKCK           BIF TO CONTINUE TO NEXT EOV IF EQ
         BL    FILELOOP           IGNORE OUTPUT DOUBLE TM       -ARB82-
         TM    OUTLTYP,X'42'      LABELLED OUTPUT?              -ARB82-
         BZ    FILEPOS            NO, WE'VE FOUND THE SPOT      -ARB82-
         SR    R4,R4              SEE IF EOV IS EMPTY DATA FILE -ARB82-
         D     R4,=F'3'                                         -ARB82-
         LTR   R4,R4                                            -ARB82-
         BZ    FILELOOP           IF SO ,CONTINUE               -ARB82-
         B     FILEPOS            ELSE THIS EOV IS THE ONE      -ARB82-
NOTTAPEO MVC   IDNTAPE+1(6),=C'OUTPUT'   MOVE 'OUTPUT' TO MSG     -HMD-
NOTTAPEI BAL   R2,PUTLINE         PUT OUT LINE.                   -HMD-
         MVC   MSGBUF(L'IDNTAPE),IDNTAPE MOVE MSG TO BUFFER       -HMD-
         B     EXITRC16           RETURN WITH BAD CODE          -ARB82-
EOLABEL  TM    OUTLTYP,X'42'      EOF SEEKING LABEL             -ARB79-
         BZ    OUTNL              OK IF NL EXPECTED             -ARB79-
         BAL   R2,PUTLINE
         MVC   MSGBUF(69),=C'0OUTPUT TAPE DESCRIBED AS LABELED HAS NO L*
               ABELS - TAPESCAN TERMINATED'                     -ARB79-
         B     EXITRC16                                         -ARB79-
NOTBOTH  DS    0H
         L     R12,=A(EXIT+4096)  LOAD 2ND BASE REG FOR MAIN CODE
         DROP  R12                DROP R12 FOR COMMON STUFF, ONLY R11
         MVI   GOSW,C'Y'          INDICATE INITIALIZATION DONE  -ARB79-
         B     SKIPTMPR           INITIALIZATION ENDS HERE      -ARB79-
         USING TAPESCAN,R12
NOOUTPUT MVC   BADINPUT+1(6),=C'OUTPUT'
NOINPUT  BAL   R2,PUTLINE         BAD OR MISSING DD ROUTINE
         MVC   MSGBUF(L'BADINPUT),BADINPUT
         B     EXITRC16                                         -ARB79-
         DROP  R12
         EJECT
BSFCMND  CCW   X'2F',0,X'70',1    BSF, CC,SLI,SKIP
         CCW   X'04',SENSBYTS,X'20',24 SENSE SLI UP TO 24 BYTES
READ81CM CCW   2,0,X'60',81       SET TO POINT TO RECBUF DYNAMICALLY
         CCW   4,SENSBYTS,X'20',24 AND SENSE FOR DEBUG
EXLSTOUT DC    0F'0',X'87',AL3(JFCBOUT)
GMCTRL   DC    A(RECBUF+124-TRT1)  MINIMUM LEN FOR THE GETMAIN  -ARB79-
         DC    A(RECBUF-TRT1+32768) MAXIMUM LENGTH FOR THE GETMAIN
GMLOCS   DC    2F'0'
DTYPE    DC    2F'0'              TO HOLD DEVTYPE INFO            -HMD-
ASAVE    DC    A(SAVE)            FOR SETTING UP R13            -ARB79-
EXITLIST DS    0F                 INPUT DCB EXIT LIST FOR RDJFCB
         DC    X'87'              LAST ENTRY AND RDJFCB
         DC    AL3(JFCBIN)        BUFFER FOR INPUT TAPE'S JFCB
EXPECB   DC    A(0)               EXPDT MSG ECB                 -ARB79-
EXPMSG   XWTO  'IEC107D E ',(EXPUNIT,3),',',(EXPVOL,6),',',            *
               (EXPJOB,8),',',(EXPSTEP,8),',',(EXPDSN,17),             *
               REPLY=(EXPREPLY,1,EXPECB),ROUTCDE=(1,3),DESC=2,MF=L     *
                                                                -ARB79-
MVPARM   MVC   PARMBUF(0),0(R3)   COPY PARM STR TO WK AREA      -ARB82-
EXPREPLY DS    CL1                EXPDT REPLY AREA              -ARB79-
BADINPUT DC    C'0INPUT  DD STATEMENT MISSING OR INVALID'
IDNTAPE  DC    C'0OUTPUT DEVICE IS NOT MAGNETIC TAPE - EXECUTION TERMIN*
               ATED'                                            -ARB82-
INCORMSG DC    C'0INCORRECT OUTPUT VOLUME MOUNTED - VOLSER = '  -ARB82-
TRUESER  DC    CL6' '                                           -ARB82-
INCORLEN EQU   *-INCORMSG                                       -ARB82-
POSMSG   DC    C'0FIRST OUTPUT FILE IS FILE NO. 0000 . . . LABEL=(XXXX,*
               NL)'
MORECORE DC    C'0WARNING:  TAPESCAN SHOULD HAVE 00000 MORE BYTES OF CO*
               RE FOR RELIABLE OPERATION; PROCESSING WILL BE ATTEMPTED *
               ANYWAY.'
TODAY    DS    CL5                TODAY'S JULIAN DATE           -ARB82-
CTABLE   DC    C'0123456789ABCDEF'                              -ARB82-
OUTZERO  DC    X'00'              LABEL=(,XX) FLAG FOR OUTPUT   -ARB82-
PARMIGN  DC    C' INVALID PARM IGNORED: '                       -ARB82-
THISPARM DC    CL100' '           PARM BEING IGNORED            -ARB82-
PSWDBUF  EQU   THISPARM           REUSE SPACE FOR PROTECT WORK  -ARB82-
         EJECT
* COMMON ROUTINES FOR BOTH INITIALIZATION AND MAIN ARE HERE.
EXIT     MVC   RCODE,OKCODE       NORMAL END, SET RET CODE      -ARB79-
         CLI   RCODE+3,12         I/O ERROR TERMINATION?        -ARB82-
         BE    EXITRC16           YES, ALLOW TAPE MARK WRITE    -ARB82-
         NI    OUTPUT+DCBOFLGS,X'7F'   SUPPRESS FINAL TAPE MARK -ARB79-
EXITRC16 CLOSE (INPUT,REWIND,OUTPUT,REWIND)                     -ARB82-
DOEXIT   L     R13,SAVE+4         MAY BYPASS SYNADRLS           -ARB79-
         MVC   16(4,13),RCODE     GET FINAL RC INTO 15          -ARB79-
         LM    14,12,12(13)
         BR    R14                FINAL EXIT FROM TAPESCAN IN ALL CASES
         SPACE 5
PUTLINE  DS    0H                 GENERAL PRINTING SUBROUTINE, ENTRY 1
         MVC   MSGBUF,BLANKBUF    CLEAR BUFFER FAST
PUTLINE2 EX    0,0(R2)            GENERAL PRINTING SUBROUTINE, ENTRY 2
PUTLINE3 CLI   SUPOUT,C'Y'        SUPPRESSING PRINTOUT?         -ARB82-
         BE    6(R2)              YES, DON'T PRINT              -ARB82-
         CLI   MSGBUF,C' '   GENERAL PRINTING ROUTINE, ENTRY 3  -ARB82-
         BE    LNCOUNT-4          BIF CARR. CTRL  CHAR IS A BLANK
         CLI   MSGBUF,C'0'        SEE IF CARRIAGE CONTROL CHAR IS ZERO
         BE    C0                 BIF IS A ZERO
         LA    R0,3               NO BLANK OR ZERO, MUST BE A MINUS
         B     LNCOUNT
C0       LA    R0,2               COUNT TWO LINES
         B     LNCOUNT
         LA    R0,1               COUNT ONE LINE
LNCOUNT  A     R0,LINENO
         ST    R0,LINENO
         C     R0,LINECNT         COMPARE WITH MAX LINES          -HMD-
         BNH   SAMEPAGE
         MVC   PAGECHAR,=X'40202120' EDIT PATTERN
         AP    PAGEPACK,=P'1'
         ED    PAGECHAR,PAGEPACK
         MVI   LINENO+3,4                                       -ARB79-
         PUT   SYSPRINT,PAGEHDR
         MVI   MSGBUF,C' '        CANCEL REQUESTED CARR CTL     -ARB79-
         PUT   SYSPRINT,BLANKBUF  LEAVE SOME SPACE              -ARB79-
         CLI   VTOCOUT,C'Y'       LISTING THE VTOC?             -ARB82-
         BNE   SAMEPAGE           NO.                           -ARB82-
         PUT   SYSPRINT,VTOCHED2  YES, ADD SUBTITLE             -ARB82-
         PUT   SYSPRINT,BLANKBUF                                -ARB82-
         MVI   LINENO+3,7         ADJUST LINE NO FOR SUBTITLE   -ARB82-
SAMEPAGE PUT   SYSPRINT,MSGBUF
         B     6(R2)              RETURN FROM PUTLINE SUBROUTINE
PAGECHK  DS    0H                 LINE RESERVATION SUBROUTINE
         CLI   SUPOUT,C'Y'        OUTPUT SUPPRESSION ACTIVE?    -ARB82-
         BER   R2                 YES, RESERVE NOTHING          -ARB82-
         A     R0,LINENO          ADD LINES TO BE RESERVED TO LINE NO
         C     R0,LINECNT         COMPARE TO MAXIMUM LINE  NUMBER -HMD-
         BCR   13,R2              RETURN IF CURRENT PAGE HAS ENUF ROOM
         ST    R0,LINENO          ELSE FORCE NEW PAGE NEXT LINE -ARB82-
         BR    R2
         SPACE 5
LENCK    BHR   R2                 RETURN IF LENGTH OK           -ARB79-
         BNE   NEGREC             DEFINITE ERROR IF LEN<0       -ARB79-
         CLI   ERRLAST,0          0 LENGTH AFTER ERROR?         -ARB79-
         BNER  R2                 YES, ALLOW IT                 -ARB79-
NEGREC   TM    OPTNO+3,X'01'      CHECK WTL OPTION              -ARB79-
         BZ    NOERRLOG                                         -ARB79-
         WTL   'BAD (0) BLOCK SIZE' USUALLY MEANS TAPE OFF END OF REEL
NOERRLOG BAL   R2,PUTLINE                                       -ARB79-
         MVC   MSGBUF(61),=C'0LOGIC OR HARDWARE ERROR - ZERO OR NEGATIV*
               E LENGTH BLOCK READ'                             -ARB82-
         B     EXITRC12           FINISH UP & TERMINATE         -ARB82-
         SPACE 5
         DROP  R13                                              -ARB79-
SYNERR   SYNADAF ACSMETH=BSAM
         MVI   ERRLAST,X'FF'      SET RECENT ERROR FLAG         -ARB79-
SYNERR2  ST    R14,SVR14
         STM   R1,R6,SVR1R6       SAVE REGS 1 THRU 6 FOR SYNAD USE
         L     R6,132(R1)         RETRIEVE R1 FROM BEFORE SYNADAF MACRO
         L     R3,4(,R13)         FIND OLD R13                  -ARB79-
         USING SAVE,R3                                          -ARB79-
         LH    R14,12(R1)         LOAD NO. OF BYTES READ        -ARB79-
         MVC   8(12,R1),=C'0I/O ERROR -'    MESSAGE PREFIX      -ARB79-
         CH    R0,=H'4'           CHECK SYNADAF'S RETURN CODE
         BNE   DIRECT
         CVD   R14,BADLNGTH
         OI    BADLNGTH+7,X'0F'
         UNPK  32(5,R1),BADLNGTH
         MVC   20(12,R1),=C' BYTES READ='                       -ARB79-
DIRECT   MVC   38(5,R1),=C'FILE=' ADD FILE NUMBER TO TEXT       -ARB82-
         L     R2,CTPMKNO         GET INPUT TM NUMBER           -ARB82-
         LA    R2,1(,R2)                                        -ARB82-
         CLI   75(R1),C'I'        INPUT ERROR?                  -ARB82-
         BE    INPERR             YES.                          -ARB82-
         LH    R2,COUTFILE        NO, GET OUTPUT FILE NUMBER    -ARB82-
INPERR   CVD   R2,BADLNGTH        CONVERT FILE NO TO DECIMAL    -ARB82-
         UNPK  43(4,R1),BADLNGTH                                -ARB82-
         OI    46(R1),X'F0'                                     -ARB82-
         L     R2,BLKNO           GET BLOCK NUMBER &            -ARB82-
         LA    R2,1(,R2)                                        -ARB82-
         CVD   R2,BADLNGTH                                      -ARB82-
         UNPK  107(7,R1),BADLNGTH REPLACE FALSE ONE IN          -ARB82-
         OI    113(R1),X'F0'      THE SYNADAF MESSAGE           -ARB82-
         MVC   MSGBUF(120),8(R1)  PRINT SYNAD ERROR MESSAGE     -ARB82-
         MVC   MSGBUF+120(13),BLANKBUF                          -ARB79-
         SYNADRLS ,               RETURN SYNADAF MSG            -ARB79-
         DROP  R3                                               -ARB79-
         USING SAVE,R13           OLD R13 IS RESTORED           -ARB79-
         MVC   SAVE2(72),SAVE     SAVE SAVE AREA CONTENTS       -ARB79-
         MVC   OLDSUP,SUPOUT      SAVE OUTPUT SUPPRESS FLAG     -ARB82-
         MVI   SUPOUT,C'N'        MAKE SURE OUR MSGS GET OUT    -ARB82-
         LA    R0,4               RESERVE 4 LINES FOR MSGS      -ARB79-
         BAL   R2,PAGECHK                                       -ARB79-
         LA    R2,*+2                                           -ARB79-
         B     PUTLINE3           GO PRINT THE MESSAGE          -ARB79-
         LR    R1,R6              RETRIEVE R1 FROM BEFORE SYNADAF MACRO
         TM    EXCPAGIN,X'02'     SEE IF EXCP RATHER THAN BSAM
         BO    *+12               BIF WAS EXCP, R1 POINTED TO IOB
         L     R1,DCBIOBA(R1)     IT WAS BSAM, R1 POINTED TO DCB
         B     *+8                R1 NOW POINTS TO BSAM IOB PREFIX
         SH    R1,=H'8'           SUBTRACT 8 TO GET PTR TO IOB PREFIX
         LR    R6,R1              SAVE PTR TO IOB BSAM PREFIX
         TM    IOBSENS1+8(R1),X'01'    NOT CAPABLE ERR?         -ARB79-
         BNZ   INCAP              YES, TERMINATE                -ARB79-
         TM    IOBSENS0+8(R1),X'10'   CHECK FOR EQUIP CHECK     -ARB79-
         BNZ   EQPCHECK           TERMINATE IF SO               -ARB79-
         TM    IOBCSW+3+8(R1),X'01'    UNIT EXCEPTION?          -ARB79-
         BNZ   OUTEOV             YES, GIVE OUTPUT EOV MSG      -ARB79-
         TM    IOBSENS0+8(R1),X'80'     COMMAND REJECT?         -ARB82-
         BO    NORING             YES, GIVE FILE PROTECT MSG    -ARB82-
         LM    R1,R6,SVR1R6       RESTORE REGS 1 THRU 6         -ARB79-
         LA    R14,1
         A     R14,ERRCOUNT       INCREMENT ERROR COUNT
         ST    R14,ERRCOUNT
         C     R14,SYNADNO        COMPARE ERROR COUNT WITH ERROR LIMIT
         BH    TOOMANY            PRINT MSG & EXIT IF ERR LIM EXCEEDED
         MVC   SUPOUT,OLDSUP      RESTORE OUTPUT SUPPRESS FLAG  -ARB82-
         MVC   SAVE(72),SAVE2     RESTORE FOR SYNAD RETURN      -ARB79-
         L     R14,SVR14
SYNCONTU BR    R14                                              -ARB82-
TOOMANY  BAL   R2,PUTLINE         YOU SHOULD PROBABLY SET ERRLIM TO 0 -
         MVC   MSGBUF(55),=C'0TAPESCAN TERMINATING DUE TO EXCESSIVE I/O*
                ERROR COUNT.'
         B     EXITRC12           FOR TAPE COPYING OPERATIONS.  -ARB82-
INCAP    BAL   R2,PUTLINE         DEFINE NOT CAPABLE            -ARB79-
         MVC   MSGBUF(75),=C'0THIS TAPE CANNOT BE READ ON THIS TYPE OF *
               TAPE UNIT - TERMINATING EXECUTION'               -ARB79-
         B     EXITRC16                                         -ARB79-
EQPCHECK LM    R1,R6,SVR1R6       RESTORE REGISTERS             -ARB79-
         BAL   R2,PUTLINE         SEND EXPLANATION              -ARB79-
         MVC   MSGBUF(48),=C'0EQUIPMENT CHECK - OFTEN MEANS TAPE RAN OF*
               F REEL'                                          -ARB79-
         B     EXITRC12           TRY TO TERMINATE CLEANLY      -ARB82-
NORING   LM    R1,R6,SVR1R6       RESTORE SYNAD REGS            -ARB82-
         BAL   R2,PUTLINE         SEND EXPLANATION              -ARB82-
         MVC   MSGBUF(54),=C'0OUTPUT TAPE IS FILE PROTECTED - TERMINATI*
               NG EXECUTION'                                    -ARB82-
         B     EXITRC16           QUIT HARD                     -ARB82-
OUTEOV   LM    R1,R6,SVR1R6       RESTORE SYNAD REGS            -ARB79-
         BAL   R2,PUTLINE                                       -ARB79-
         MVC   MSGBUF(54),=C'0OUTPUT TAPE CAPACITY EXCEEDED - TERMINATI*
               NG EXECUTION'                                    -ARB79-
EXITRC12 DS    0H                 TERMINATE RUN WITH RC=12, BUT        *
                                  GIVE STATISTICS FIRST         -ARB82-
         CLI   GOSW,C'Y'          PAST INITIALIZATION?          -ARB79-
         BNE   EXITRC16           NO, JUST TERMINATE            -ARB79-
         USING EXIT+4096,R12      ELSE R12 IS SET UP            -ARB79-
         MVI   OKCODE+3,12        SET LESS THAN PERFECT RC      -ARB82-
         MVI   GOSW,C'N'          PREVENT RECURSION             -ARB79-
         MVC   TPMKMSG+1(17),=CL17'PROCESSING ENDED'            -ARB79-
         TM    EXCPAGIN,X'02'     EXCP ERROR?                   -ARB84-
         BZ    TPMKLOGD           NO.                           -ARB84-
         L     R4,SVR4            YES, RESTORE REG 4            -ARB84-
         B     TPMKLOGD           GO FINISH CLEANLY             -ARB79-
         DROP  R12                                              -ARB79-
         EJECT
EXECEXCP DS    0H                 EXCP SUBROUTINE, R2=PTR TO DCB, AND  *
                                  R1=PTR TO 3-BYTE ADCON WHICH POINTS  *
                                  TO CHANNEL PROGRAM.
         L     R2,DCBIOBA(R2)     GET PTR TO A BSAM IOB PREFIX
         MVC   STARTSAV(3),IOBSTART+9(R2) SAVE PTR TO BSAM'S CCW
         MVC   IOBSTART+9(3,R2),0(R1) MOVE IN PTR TO CHAN PROG
         LA    R1,4(R2)           LOAD PTR TO ECB IN PREFIX
         ST    R1,IOBECBPT+8(R2)  STORE PTR TO ECB
         XC    0(4,R1),0(R1)      CLEAR ECB IN IOB PREFIX
         EXCP  8(R2)              EXCP USING BSAM'S IOB AND ECB
         WAIT  ECB=4(R2)
         MVC   IOBSTART+9(3,R2),STARTSAV RESTORE PTR TO BSAM'S CCW
         CLI   4(R2),X'7F'        SEE IF EXCP WORKED PERFECTLY
         BCR   8,R4               RETURN IF IT DID
         LA    R1,8(R2)           GET PTR TO IOB FOR SYNADAF MACRO
         OI    EXCPAGIN,X'02'     INDICATE EXCP, NOT BSAM, IN SYNAD
         SYNADAF ACSMETH=EXCP     WORKS OK EVEN THO IOBSTART RESTORED
         BAL   R14,SYNERR2        NOW DO REST OF ANALYSIS WITH SYNERR
         TM    OPTNO+3,X'40'      BIT=1=STOP FOR ANY EXCP ERROR
         BCR   8,R4               BRANCH TO NOT TERMINATE
         BAL   R2,PUTLINE         PRINT ERR MSG BEFORE STOP
         MVI   SUPOUT,C'N'        MAKE SURE MSG PRINTS          -ARB82-
         MVC   MSGBUF(34),=C'0EXCP ERROR TERMINATION THRU OPT64'
         B     EXITRC16                                         -ARB79-
         SPACE 5
LISTBLK  CLI   SUPOUT,C'Y'        SUPPRESSING LISTING?          -ARB82-
         BER   R5                 YES, DON'T TRY                -ARB82-
         MVC   MSGBUF+1(11),BLOCKNO   ADD BLOCK NO TO LISTING   -ARB82-
         MVI   HEXOFF+1,11        PUT TEXT AFTER BLOCKNO        -ARB79-
         LTR   R7,R7              IS THE LENGTH 0?              -ARB79-
         BZR   R5                 YES, DON'T LIST               -ARB79-
LISTON   MVI   MSGBUF,C'0'
         LA    R0,4               PREPARE TO RESERVE 4 LINES
         BAL   R2,PAGECHK         RESERVE 4 LINES
         LA    R6,MSGBUF+1                                      -ARB79-
         AH    R6,HEXOFF          FIND WHERE TO PUT TEXT        -ARB79-
         BAL   R2,PUTLINE2        LIST THE FIRST PART OF THE BLOCK
         MVC   0(120,R6),RECBUF   MOVE RECORD TO OUTPUT LINE    -ARB79-
HEXON    CLI   HEXFLG,C'N'        SEE IF HEXLIST SPECIFIED
         BCR   8,R5               BER R5 RETURN IF HEXLIST NOT SPECIFID
         MVC   MSGBUF,BLANKBUF    BLANK OUT BUFFER              -ARB79-
         LA    R6,MSGBUF+1                                      -ARB79-
         AH    R6,HEXOFF          COMPUTE WHERE TO PUT TEXT     -ARB79-
         MVC   0(120,R6),ASCBUF   MOVE IT IN                    -ARB79-
         MVC   TRINT1+1(1),LINESIZE+3 DEFAULT TRANSLATE LEN=120 -ARB79-
         MVC   TRINT2+1(1),LINESIZE+3 DEFAULT TRANSLATE LEN=120 -ARB79-
         C     R7,LINESIZE        SEE IF LENGTH EXCEEDS 1 LINE  -ARB79-
         BH    TRINT1             BIF BLOCK LENGTH GT 120 CHARS
         LR    R2,R7              DON'T CHANGE R7 ITSELF        -ARB79-
         BCTR  R2,0               DECREMENT BLK  LENGTH         -ARB79-
         STC   R2,TRINT1+1        AND STORE IN 1ST TR INSTR     -ARB79-
         STC   R2,TRINT2+1        AND IN THE SECOND ONE         -ARB79-
TRINT1   TR    0(120,R6),TRT1     MODIFIED INSTRUCTION (LENGTH) -ARB79-
         MVI   MSGBUF,C' '        SET CARRIAGE CONTROL
         BAL   R2,PUTLINE3        PRINT FIRST LINE OF HEX
         DC    C'TRT111'
         MVC   0(120,R6),ASCBUF                                 -ARB79-
TRINT2   TR    0(120,R6),TRT2     MODIFIED INSTRUCTION (LENGTH) -ARB79-
         BAL   R2,PUTLINE3        PRINT SECOND LINE OF HEX
         DC    C'TRT222'          FILLER
         MVI   LINESIZE+3,119     RESTORE DEFAULT HEX LENGTH    -ARB79-
         BR    R5                 RETURN FROM LISTON OR HEXON SUBR
         SPACE 5
JULDATE  DS    0H                 DATE VALIDATION FRONT-END TO JULIAN  *
                                                                -ARB82-
         LM    R15,R0,0(R1)       LOAD JULIAN PARM REGS         -ARB82-
         CLC   0(5,R15),=C'000000'     INPUT DATE ZERO?         -ARB82-
         BER   R14                YES, RETURN ZEROES            -ARB82-
         TRT   0(5,R15),NUBTABL   NO, IS DATE NUMERIC?          -ARB82-
         BNZ   BADDATE            NO, RETURN "INVALID"          -ARB82-
         L     R15,=V(JULIAN)     YES, CALL CONVERTER           -ARB82-
         BR    R15                                              -ARB82-
BADDATE  LR    R1,R0              COPY RETURN VALUE PTR         -ARB82-
         MVC   0(7,R1),=C'INVALID'     NOTE IT WAS BAD          -ARB82-
         BR    R14                AND RETURN                    -ARB82-
         SPACE 5
GETVTOC  DS    0H                 ROUTINE TO GET VTOC BLOCK     -ARB82-
         LTR   R3,R3              HAVE A PREVIOUS VTOC ENTRY?   -ARB82-
         BZ    NOVSTATS           NO.                           -ARB82-
         USING VTOC,R3                                          -ARB82-
         MVC   VTOCSTAT(12),FILESTAT   COPY ERROR FLAGS TO VTOC -ARB82-
         XC    FILESTAT(12),FILESTAT   AND RESET TO ZERO        -ARB82-
         SR    R14,R14            COUNT NUMBER OF NOTES         -ARB82-
         LA    R15,12                                           -ARB82-
         LA    R1,VTOCSTAT                                      -ARB82-
STATCNT  CLI   0(R1),0            IS THIS ERROR FLAGGED?        -ARB82-
         BE    *+8                                              -ARB82-
         LA    R14,1(,R14)        YES, INCREASE COUNT           -ARB82-
         LA    R1,1(,R1)                                        -ARB82-
         BCT   R15,STATCNT                                      -ARB82-
         STH   R14,VTOCMSG#       SAVE NUMBER OF NOTES          -ARB82-
         CLI   VNOMORE,C'Y'       GET ANY MORE ENTRIES?         -ARB82-
         BER   R2                 NO, RETURN                    -ARB82-
NOVSTATS L     R5,CURRVTOC        POINT TO CURRENT VTOC BLOCK.  -ARB82-
         CLI   0(R5),VTOCEPB      IS THIS BLOCK FULL?             -CWB-
         BL    NXTENTRY           IF NOT, BRANCH.                 -CWB-
         GETMAIN  R,LV=VTOCBLSZ   ELSE, GET CORE FOR ANOTHER BLOCK-CWB-
         ST    R1,0(R5)           SAVE FOREWARD POINTER.          -CWB-
         MVI   0(R5),VTOCEPB      REINSERT THE ENTRY COUNT.       -CWB-
         LR    R5,R1              MAKE NEW BLOCK CURRENT.         -CWB-
         ST    R1,CURRVTOC                                        -CWB-
         LA    R3,8(R5)           STEP OVER INITIAL DOUBLEWORD.   -CWB-
*                                      R3 POINTS AT CURRENT ENTRY.-CWB-
         SR    R0,R0              CLEAR ENTRY COUNT AND FORWARD   -CWB-
         ST    R0,0(R5)                POINTER IN NEW BLOCK.      -CWB-
         MVI   0(R5),1            CHANGE ENTRY COUNT TO 1.        -CWB-
         B     CLEARVEN           GO CLEAR THE FIRST ENTRY.       -CWB-
NXTENTRY LA    R3,VTOCSIZE(R3)    ADVANCE TO NEXT VTOC ENTRY.     -CWB-
         SR    R1,R1              INCREMENT ENTRY COUNT.          -CWB-
         IC    R1,0(R5)                                           -CWB-
         LA    R1,1(R1)                                           -CWB-
         STC   R1,0(R5)                                           -CWB-
CLEARVEN MVC   VTOC(VTOCCSIZ),BLANKBUF  CLEAR OUT VTOC ENTRY.   -ARB82-
         XC    VTOC2(VTOCBSIZ),VTOC2    ZERO BINARY PART        -ARB82-
         DROP  R3                                               -ARB82-
         BR    R2                 RETURN TO CALLER              -ARB82-
         EJECT
SYSWTOR  DS    0H                 ROUTINE TO ISSUE A SUPERVISORY WTOR  *
                                                                -ARB79-
         MODESET KEY=ZERO         TO PUT * BEFORE MESSAGE       -ARB79-
         XWTO  MF=(E,(R2))        SEND MSG & GET REPLY          -ARB79-
         L     R2,4(,R2)          FIND REPLY ECB                -ARB79-
         WAIT  ECB=(R2)           AWAIT THE REPLY               -ARB79-
         MODESET KEY=NZERO        GIVE UP PRIVILEGE             -ARB79-
         XC    0(4,R2),0(R2)      RESET THE ECB                 -ARB79-
         BR    R5                 AND RETURN TO CALLER          -ARB79-
         SPACE 5
CHKPASS  DS    0H                 ROUTINE TO LOOK UP DATASET PASSWORD  *
                                                                -ARB79-
         LA    R1,43(,R2)         LOOK FOR END OF JFCB DSN      -ARB79-
DSNSCAN  CLI   0(R1),C' '                                       -ARB79-
         BNE   DSNEND                                           -ARB79-
         BCT   R1,DSNSCAN                                       -ARB79-
DSNEND   LA    R0,1(,R1)                                        -ARB79-
         SR    R0,R2              COMPUTE DSN LENGTH            -ARB79-
         STC   R0,PSWDPARM+4      STORE LENGTH FOR PROTECT      -ARB79-
         CH    R0,=H'17'          IS DSN TOO BIG FOR LABEL?     -ARB79-
         BNH   SHORTDSN           NO.                           -ARB79-
         SH    R1,=H'16'          YES, POINT TO LAST 17 CHARS   -ARB79-
         B     TESTDSN                                          -ARB79-
SHORTDSN LR    R1,R2                                            -ARB79-
TESTDSN  CLC   0(17,R1),RECBUF+4  COMPARE JFCB DSN TO LABEL     -ARB79-
         BE    FULLDSN            A MATCH, LOOK UP BY JFCB DSN  -ARB79-
         LA    R2,RECBUF+4        ELSE USE DSN FROM LABEL       -ARB79-
         MVI   PSWDPARM+4,17      SET LABEL DSN LENGTH          -ARB79-
FULLDSN  STCM  R2,7,PSWDPARM+5    PUT DSN ADDR IN PARM LIST     -ARB79-
         PROTECT PSWDPARM         LOOK UP DSN-PASSWORD PAIR     -ARB79-
         BR    R5                 & RETURN TO CALLER            -ARB79-
         SPACE 5
SCUREXIT DS    0H                 ROUTINE TO CALL SECURITY EXIT -ARB82-
         WXTRN TAPESCUR           ALLOW EXIT TO BE ABSENT       -ARB82-
         L     R15,=V(TAPESCUR)   FIND ENTRY TO EXIT            -ARB82-
         LTR   R15,R15            IS THERE ONE?                 -ARB82-
         BZR   R14                NO, ANYTHING GOES             -ARB82-
         LA    R1,SCURPARM        YES, LOAD PARM LIST ADDR      -ARB82-
         BR    R15                AND CALL EXIT ROUTINE         -ARB82-
         EJECT
*        NEXT 3 CCW'S MOVED FOR ADDRESSABILITY                  -ARB79-
WRTCMND  CCW   1,0,X'20',0        PTR TO RECBUF UPDATED AFTER GETMAIN
WTMCMND  CCW   X'1F',0,X'60',1    WRITE TAPEMARK, SLI, CC TO SENSE
         CCW   X'04',SENSBYTS,X'20',1 SENSE CHAINED FOR DEVICE END INFO
FSMCMND  CCW   X'3F',0,X'30',1    FWRD SPACE FILE, SKIP/SLI
RWNDCMND CCW   7,0,X'60',1        REWIND TAPE, CHAIN CMND, SLI
SENSCMND CCW   4,SENSBYTS,X'20',24 SENSE UP TO 24 BYTES, SLI
SENSBYTS DC    3CL8'SENSBYTS'     BUFFER FOR SENSE BYTES
BADLNGTH DC    D'0'
PSWDECB  DC    F'0'               ECB FOR PASSWORD WTOR         -ARB79-
PSWDREPL DS    CL8                PASSWORD REPLY BUFFER         -ARB79-
SVR1R6   DS    6F                 FOR SAVING REGISTERS 1 THRU 6
SVR14    DC    F'0'               TO SAVE R14 TEMPORARILY       -ARB79-
WORK     DS    2A                 FOR DATE CONVERT WKAREA    *FD78.130
AMSGBP58 DC    A(0)               A(MSGBUF+58) SET DURING INIT  -ARB79-
AMSGBP86 DC    A(0)               A(MSGBUF+86)                  -ARB79-
CURRVTOC DC    A(FRSTVTOC)        CURRENT VTOC BLOCK.           -ARB82-
ERRCOUNT DC    F'0'               NUMBER OF SYNAD EXITS TAKEN (I/O ERRS
OPTNO    DC    F'0'               BIT ORIENTED OPTIONS - MISC/DEBUG
LINENO   DC    F'70'
LINESIZE DC    F'119'             MAX NUMBER CHARS/LINE-1       -ARB79-
LISTNO   DC    F'-1'         NUMBER OF BLKS TO LIST PER DATASET -ARB82-
SKIPTMNO DC    F'0'
SKIPTMVL DC    F'0'               SKIPTM PARM VALUE             -ARB82-
SKIPFLNO DC    F'0'               NUMBER FILES TO BE SKIPPED    -ARB79-
MAXTMNO  DC    F'32767'
STOPTMNO DC    F'32767'           STOPTM PARM (CONSTANT)        -ARB82-
STPEOVNO DC    F'1'               STOPEOV PARM (CONSTANT)       -ARB82-
SKPEOVNO DC    F'0'
MAXEOVNO DC    F'1'
SYNADNO  DC    F'5'          MAX SYNAD EXITS BEFORE TERMINATION -ARB79-
LINECNT  DC    F'59'              LINES/PAGE                    -ARB82-
RCODE    DC    F'16'              EXPECTED BAD RETURN CODE      -ARB79-
OKCODE   DC    F'0'               EXPECTED GOOD RETURN CODE     -ARB79-
BLKNO    DC    F'0'               BLOCK NO READ BY READER       -ARB82-
COUTFILE DC    H'1'               CURRENT OUTPUT FILE SEQ. NO.
INFLPOS  DC    H'1'               INPUT LABEL NUMBER            -ARB82-
CTPMKNO  DC    F'0'          # OF INPUT TAPEMARKS ENCOUNTERED   -ARB82-
HEXOFF   DC    H'0'               TEXT OFFSET FOR HEX RTN       -ARB79-
FIRSTSEQ DS    0PL3               SEQ OF FIRST INPUT FILE       -ARB79-
         DC    CL3'N'             INDICATE NOT YET SET          -ARB79-
PAGEPACK DC    PL2'0'             COUNTS PAGES
EXCPAGIN DC    X'00'              EXCP 'REENTRY' FLAG, X'01'= REENTRY, *
                                  X'02' BIT=EXCP TYPE ERROR NOT BSAM
ERRLAST  DC    X'00'              LAST READ HAD ERROR FLAG      -ARB79-
PHFLSTAT DC    XL12'00'           ERROR FLAGS FOR PHYISCAL FILE -ARB82-
FILESTAT DC    XL12'00'           ERROR FLAGS FOR LOGICAL FILE  -ARB82-
ASCURITY DC    C'0ASCII VOLUME SECURITY PROTECTED - TAPESCAN TERMINATED*
               '                                                -ARB79-
DENMSG   DC    CL40'0 800 BPI= ASSUMED DENSITY OF INPUT TAPE'   -ARB82-
ASCIVOL1 DC    X'564F4C31'        ASCII VOL1                    -ARB79-
BLOCKNO  DC    C'BLOCK NNN: '                                   -ARB79-
PAGEHDR  DC    CL71'1TAPESCAN  -  IRSS/GTEDS/ICSA TAPE ANALYSIS AND COP*
               YING PROGRAM  V5.1  '                            -ARB82-
DATERSLT DC    CL8'MM/DD/YY'      DATE WILL BE PLACED HERE
         DC    CL2'  '
TIMERSLT DC    XL11'4021207A20207A20204B20'
         DC    CL17'    INPUT VOLSER='                          -ARB82-
DDVOL    DC    CL6'VVVVVV'                                        -CWB-
         DC    CL8'    PAGE'                                    -ARB82-
PAGECHAR DC    X'40202120'        EXAMPLE EDIT PATTERN FOR PAGE NUMBER
         DC    CL12'           '  PAGE TRAILING BLANKS
VTOCHED2 DC    CL133' '                                         -ARB82-
         ORG   VTOCHED2           OVERLAY TEXT ON TITLE         -ARB82-
         DC    C'0SEQ.  DATA SET NAME     LABEL RECFM  LRECL  BLKSIZE E*
               XPIRES  BLK COUNT  '                             -ARB82-
VTOCHEDE EQU   *                                                -ARB82-
         ORG   ,                                                -ARB82-
VTOCOUT  DC    C'N'               VTOC LISTING IN PROGRESS      -ARB82-
JFCLTSV  DC    X'FF'              FOR SAVING JFCBLTYP BYTE
OUTLTYP  DC    C'0'               TO SAVE OUTPUT LABEL TYPE     -ARB79-
ITRTCH   DS    X                  INPUT TRTCH BITS              -ARB79-
OTRTCH   DS    X                  OUTPUT TRTCH BITS             -ARB79-
ITRACKS  DC    X'00'              X'10' BIT->7 TRACK            -ARB79-
OTRACKS  DC    X'00'              X'10' BIT->7 TRACK            -ARB79-
CTRACKS  DC    X'00'              OTRACKS .OR. ITRACKS          -ARB79-
STARTSAV DC    C'SAV'             FOR SAVING BSAM'S IOBSTART FIELD
COPYFLG  DC    X'00'              COPY AND MOD OPTION BITS      -ARB82-*
               X'01'=COPY,X'02'=EOVMOD,X'04'=UNLABEL,           -ARB82-*
               X'80'=BACKSPACE BEFORE COPY                      -ARB82-
WRTFLG   DC    X'00'              TWO BIT COPY FLAG, X'03'=WRITE BLOCK
SKIPFLG  DC    X'00'              TAPE POSITIONING FLAG         -ARB79-
POSFLG   DC    C'N'               LABEL INPUT POSITIONING FLAG  -ARB82-
COUNTFLG DC    C' '               COUNT OPTION FLAG (DEFAULT VARIES)   *
                                                                -ARB82-
HEXFLG   DC    C'N'                                             -ARB79-
SUMFLG   DC    C'Y'
*              THE NEXT TWO FIELDS MUST BE KEPT TOGETHER        -ARB82-
ASCIIN   DC    C'N'               TRANSLATE INPUT FROM ASCII    -ARB79-
ASCIOUT  DC    C'N'               TRANSLATE OUTPUT TO ASCII     -ARB79-
SVOUTFLG DC    C'Y'          C'N' = CHANGE OUTPUT VOL LABEL     -ARB82-
MAPFLG   DC    C'N'               MAPONLY OPTION FLAG           -ARB82-
SUPOUT   DC    C'N'               SUPPRESS LISTING FLAG         -ARB82-
OLDSUP   DS    CL1                SUPOUT COPY FOR SYNAD RTN     -ARB82-
GOSW     DC    C'N'               INITALIZATION DONE SWITCH     -ARB79-
VNOMORE  DC    C'N'               STOP GETVTOC SWITCH           -ARB82-
PSWDSW   DC    X'00'              FIRST/SECOND PASSWORD SWITCH  -ARB79-
SCURPARM DS    0A                 PARM LIST FOR SECURITY EXIT   -ARB82-
SECDCB   DS    A                  POINTER TO TAPE DCB           -ARB82-
SECIOTYP DC    C'O'               I=INPUT,O=OUTPUT              -ARB82-
SECLABEL DC    C'N'               N=NL, A=AL, S=SL              -ARB82-
SECDSN   DC    CL17' '            FROM HDR1 LABEL OR BLANK      -ARB82-
SECVOL   DC    CL6' '             VOLSER FROM LABEL OR JCL      -ARB82-
PSWDPARM DC    0A(0),X'04',AL3(PSWDBUF)     PROTECT PARM LIST   -ARB79-
         DS    A                  TO PERFORM LISTPW FUNCTION    -ARB79-
         DC    A(PSWDREPL)                                      -ARB79-
SMSG     XWTO  'IEC301A S JOB ',(SMSGJOB,8),', STEP ',(SMSGSTEP,8),    *
               ', DDNAME ',(SMSGDD,8),REPLY=(PSWDREPL,8,PSWDECB),      *
               ROUTCDE=9,DESC=2,MF=L                            -ARB79-
         EJECT
* MAIN LOOP AND MAIN LINE CODE STARTS HERE
SKIPEOVP DS    0H                 SKIPEOV OPTION CONTROL ROUTINE
         USING EXIT+4096,R12
         CLC   CEOVNO,SKPEOVNO
         BL    SKIPTMEV           DO SKIPEOV IF NEEDED          -ARB79-
EOVDONE  L     R2,CTPMKNO         FIND WHERE TO SKIPTM TO       -ARB79-
         A     R2,SKIPTMNO        SKIPTM IS RELATIVE TO SKIPEOV -ARB79-
         ST    R2,SKIPFLNO                                      -ARB79-
         L     R2,CEOVNO          COMPUTE MAXEOV FROM STOPEOV   -ARB79-
         A     R2,MAXEOVNO                                      -ARB79-
         ST    R2,MAXEOVNO        STORE FINAL EOV NUMBER        -ARB79-
         B     SKIPTMPR           GO SKIP ANY TM'S REQUESTED    -ARB79-
SKIPTMEV SR    R4,R4              ZERO TO GET GOOD BLK CNT EVEN IF     *
                                  SKIPTM OR SKIPEOV USED.
         SR    R8,R8              (RE)-ZERO BYTE COUNTER
         ST    R8,BLKCNT          (RE)-ZERO BLKCNT
         BAL   R5,READER          CHECK FOR DOUBLE TAPEMARK
         C     R7,=F'80'          SEE IF BLKSIZE=80 (LIKE ALL LABELS)
         BL    CONTROL            BIF BLKLNGTH LT 80 (I.E., IT'S NOT A *
                                                    LABEL).     -ARB79-
* ARE THE NEXT TWO INSTRUCTIONS NECESSARY?
         C     R4,=F'1'           SEE IF THIS IS THE 1ST BLK AFTER A   *
                                  TAPEMARK OR THE LOAD POINT.
         BNE   CONTROL            BIF IT ISN'T THE FIRST
         CLI   VERVOL,C'Y'        NEED TO VERIFY THE VOLUME?    -ARB79-
         BE    CHKVOL1            YES, CHECK FOR VOLUME LABEL   -ARB79-
NOHDRS   CLI   ASCIIN,C'Y'        ASCII INPUT?                  -ARB79-
         BE    EOVCK              YES, LONG LABEL POSSIBLE      -ARB79-
         C     R7,=F'80'          NO, IS LENGTH EXACTLY 80      -ARB79-
         BNE   DOCTL              NO, LABEL IMPOSSIBLE          -ARB79-
EOVCK    CLC   RECBUF(4),=C'EOV1'                               -ARB79-
         BNE   CONTROL
         OI    EOV1FLG,X'01'      INDICATE EOV1 TYPE OF EOV PENDING
         B     CONTROL            POSITION PAST TAPEMARK AND GOTO EODS
ENDVOLVR CLI   VERVOL,C'H'        NEED TO CHECK FOR PROTECTION? -ARB82-
         BNE   CONTROL            NO, SKIP TO NEXT FILE         -ARB82-
LOCHDR   BAL   R5,READER          YES, GET NEXT RECORD          -ARB82-
         C     R7,=F'80'          80 BYTE BLOCK?                -ARB82-
         BL    NOHDR1             NO, NOT A LABEL               -ARB82-
         B     CHKHDR1            YES (OR MORE), SEE IF HDR1    -ARB82-
SKIPTMPR DS    0H                 SKIPTM ROUTINE. ENTERED ONCE TO      *
                                  POSITION FOR LABEL PARM, AGAIN FOR   *
                                  SKIPTM PROCESSING.            -ARB79-
         SR    R3,R3              CLEAR R3 TILL WE GET A VTOC     -CWB-
*                                      BLOCK ENTRY.               -CWB-
         CLC   CTPMKNO,SKIPFLNO   TAPEMARKS SKIPPED YET?        -ARB79-
         BL    SKIPTMEV           BRANCH IF SKIPPING ANOTHER TAPEMARK
TMDONE   XI    SKIPFLG,X'80'      REVERSE POSITIONING FLAG      -ARB79-
         BNZ   SKIPEOVP           DO SKIPEOV AFTER LABEL PARM   -ARB79-
         MVI   SKIPFLG,X'FF'      INDICATE THRU ALL SKIPPING    -ARB79-
         L     R2,CTPMKNO         CONVERT STOPTM VALUE          -ARB79-
         A     R2,MAXTMNO         TO MAXTM VALUE (I.E., FINAL   -ARB79-
         ST    R2,MAXTMNO         TAPE MARK NUMBER)             -ARB79-
         MVC   SKIPTMNO,CTPMKNO   SAVE NUMBER TM'S SKIPPED      -ARB79-
         SR    R8,R8              CLEAR CTR FOR TOTAL BYTES ON TAPE
         ST    R8,BLKCNT          (RE)-ZERO BLKCNT
         OI    WRTFLG,X'02'       OR IN  'ALL TM & EOV SKIPPING DONE'
         SR    R3,R3              CLEAR R3 TILL WE GET A VTOC     -CWB-
*                                      BLOCK ENTRY.               -CWB-
PROCESS  DS    0H                 TOP OF OUTER MAIN LOOP
         SR    R4,R4              ZERO BLK COUNTER
         ST    R4,MAX             RESET MAX BLKSIZE WATCHER
         MVC   MIN,=F'32767'      RESET MIN BLKSIZE WATCHER
         MVC   PREVLAB,LABELID    SAVE PREV LABEL INDICATION    -ARB82-
         MVC   LABELID,=C'NN'     ASSUME THIS IS NO LABEL       -ARB82-
         MVI   GOODLAB,C'N'       ASSUME THIS FILE NOT LABELS   -ARB82-
         BAL   R10,READIN         ESTABLISH LOOP RETURN POINT   -ARB82-
NEXTIN   DS    0H                 TOP OF MAIN INNER LOOP        -ARB79-
         CLI   SUMFLG,C'N'        NOSUM OPERATION?              -ARB82-
         BNE   YESSUM             YES, READ FOR SUMMARY         -ARB82-
         CLI   VERVOL,C'H'        NO, NEED TO VERIFY HEADER?    -ARB82-
         BNE   OTHRCHKS           NO, CHECK FOR LIST            -ARB82-
         B     READIN             YES, READ FOR HEADER          -ARB82-
YESSUM   CLI   LABELIKE,C'Y'      ARE WE READING LABELS?        -ARB82-
         BNE   OTHRCHKS           NO, SEE IF LIST OR COUNT      -ARB82-
READIN   BAL   R5,READON     READ BLK, CALC LENGTH, MIN/MAX     -ARB82-
         C     R7,=F'80'          SEE IF BLKSIZE=80 (LIKE ALL LABELS)
         BL    NOLABEL            BIF BLKLNGTH LT 80 (I.E., IT'S NOT A *
                                                    LABEL).     -ARB79-
         CLI   VERVOL,C'Y'        VOLUME VERIFY TIME?           -ARB79-
         BE    CHKVOL1            YES, CHECK FOR VOL1           -ARB79-
         CLI   VERVOL,C'H'        HEADER VERIFY TIME?           -ARB79-
         BE    CHKHDR1            YES, DO IT                    -ARB79-
NOHDRP   CLI   ASCIIN,C'Y'        ASCII INPUT?                  -ARB79-
         BE    LONGCK             YES, LONG LABELS POSSIBLE     -ARB79-
         C     R7,=F'80'          NO, LABELS MUST BE 80         -ARB79-
         BNE   NOLABEL                                          -ARB79-
LONGCK   C     R4,=F'1'           SEE IF THIS IS THE 1ST BLK AFTER A   *
                                  TAPEMARK OR THE LOAD POINT.   -ARB79-
         BNE   *+18               BIF IT ISN'T THE FIRST
         CLC   RECBUF(4),=C'EOV1'
         BNE   *+8
         OI    EOV1FLG,X'01'      INDICATE EOV1 TYPE OF EOV PENDING
         CLI   SUMFLG,C'N'        NOSUM OPTION?                 -ARB82-
         BE    TRYLIST            YES, SKIP LABEL CHECKS        -ARB82-
         B     CHKLABEL           GO SEE IF WE HAVE A LABEL     -ARB79-
NOLABEL  CLI   VERVOL,C'Y'        FIRST RECORD EVER READ?       -ARB79-
         BE    NOVOL1             NO, LABEL NOT FOUND           -ARB79-
         MVI   VERVOL,C'N'        VOLUME VERIFICATION NOW DONE  -ARB79-
         CLI   GOODLAB,C'N'       MIGHT THIS FILE BE LABELS?    -ARB82-
         BE    TRYLIST            NO, DON'T INDICATE ERROR      -ARB82-
         MVI   PHFLSTAT+#DATA,X'FF'    NO, SHOW DATA IN LABELS  -ARB82-
TRYLIST  C     R4,LISTNO                                        -ARB79-
         BH    PRECOUNT           IF LIST NUMBER EXCEEDED       -ARB82-
         BAL   R10,LISTER         BRANCH TO LIST BLOCK          -ARB82-
OTHRCHKS BALR  R10,0              TOP OF LIST LOOP              -ARB82-
         C     R4,LISTNO                                        -ARB79-
         BNL   CHKCNT             BRANCH IF EVERYTHING ALREADY LISTED
         BAL   R5,READON
LISTER   CVD   R4,BADLNGTH                                      -ARB79-
         MVC   BLOCKNO+5(4),=X'40202120'                        -ARB79-
         ED    BLOCKNO+5(4),BADLNGTH+6 PUT BLK NO IN LIST       -ARB79-
         BAL   R5,LISTBLK                                       -ARB79-
PROCESS3 CLI   WRTFLG,X'03'       NEED TO DO OUTPUT?            -ARB82-
         BNER  R10                NO.                           -ARB82-
COPYIT   CLI   ASCIOUT,C'Y'       TRANSLATE OUTPUT TO ASCII?    -ARB82-
         BNE   NOUTRAN                                          -ARB79-
         CLI   ASCIIN,C'Y'        WAS IT ORIGINALLY ASCII?      -ARB82-
         BNE   OUTRAN             NO, TRANSLATE FROM EBCDIC     -ARB82-
         MVC   RECBUF(120),ASCBUF YES, RESTORE ORIGINAL FORM    -ARB82-
         B     NOUTRAN                                          -ARB82-
OUTRAN   XLATE RECBUF,(R7),TO=A   YES, DO SO                    -ARB82-
NOUTRAN  LA    R1,=AL3(WRTCMND)                                 -ARB79-
         LA    R2,OUTPUT                                        -ARB79-
         ST    R4,SVR4            SAVE R4 (CURRENT FILE BLKCNT) -ARB84-
         OI    OUTPUT+DCBOFLGS,X'80'   SET DCB OUTPUT FLAG      -ARB79-
         BAL   R4,EXECEXCP                                      -ARB79-
         L     R4,SVR4            RESTORE R4                    -ARB84-
         BR    R10                END OF VARIOUS INPUT LOOPS    -ARB82-
PRECOUNT BAL   R10,PROCESS3       PROCESS THIS RECORD, ONLY            *
                                  COUNT THEREAFTER              -ARB82-
CHKCNT   CLI   COUNTFLG,C'N'
         BE    CONTROL
         BALR  R10,0              ESTABLISH COUNT LOOP TOP      -ARB82-
         BAL   R5,READER          TOP    OF MAIN COUNTBLK OPTION LOOP
         B     PROCESS3           SEE IF NEEDS TO BE WRITTEN    -ARB82-
CONTROL  CLI   VERVOL,C'Y'        VOLUME VERIFICATION?          -ARB79-
         BE    NOVOL1             YES, NOTE NO LABEL            -ARB79-
DOCTL    LA    R2,INPUT           SKIP JUST PAST NEXT TAPEMARK  -ARB79-
         LA    R1,=AL3(FSMCMND)   SKIP JUST PAST NEXT TAPEMARK
         XC    SVR4,SVR4                                        -ARB84-
         BAL   R4,EXECEXCP        SKIP JUST PAST NEXT TAPEMARK
         SR    R4,R4              RE-ZERO CURRENT BLK COUNT
         ST    R4,MAX             ZERO MAX TO INHIBIT BLK COUNT MSG
         SR    R8,R8              IS THIS NECESSARY?
         LR    R7,R8              MAKE LENGTH NEGATIVE FOR EODS -ARB82-
         BCTR  R7,0                                             -ARB82-
         B     EODS               SIMULATE TAPEMARK READ
NOVOL1   MVI   VERVOL,C'N'                                      -ARB79-
         MVC   VSNSAVE,DDVOL      SAVE VOLSER FOR VTOC          -ARB82-
         TM    JFCLTSV,X'42'      SHOULD IT BE LABELLED?        -ARB79-
         BZ    SCURNL             NO, MAKE NO COMMENT           -ARB82-
         BAL   R2,PUTLINE         YES, GIVE WARNING             -ARB79-
         MVC   MSGBUF(60),=C'0WARNING - TAPE SPECIFIED AS LABELED IN TH*
               E JCL HAS NO LABEL'                              -ARB82-
         TM    WRTFLG,X'01'       ARE WE COPYING?               -ARB82-
         BZ    NASCWRN1           NO.                           -ARB82-
         CLI   SVOUTFLG,C'N'      YES, IS IT UNLABEL?           -ARB82-
         BE    SCURNL             YES, LET IT GO ON             -ARB82-
         BAL   R2,PUTLINE         NO, DON'T ALLOW COPY          -ARB82-
         MVC   MSGBUF(38),=C' COPY TERMINATED DUE TO LABEL MISMATCH'   *
                                                                -ARB82-
         B     EXITRC16                                         -ARB82-
SCURNL   CLI   COUNTFLG,C'Y'      COUNT OPTION?                 -ARB82-
         BE    SCURIN             YES, VERIFY SECURITY          -ARB82-
         OC    LISTNO,LISTNO      NO, LIST OPTION?              -ARB82-
         BNZ   SCURIN             YES, VERIFY SECURITY          -ARB82-
NLVER    TM    WRTFLG,X'01'       COPY OPERATION?               -ARB79-
         BZ    NASCWRN1           NO, NO NEED FOR WARNING       -ARB79-
ASCK1    CLC   ASCIIN,ASCIOUT     CHANGE IN CODE OCCURING?      -ARB82-
         BE    NASCWRN1           NO.                           -ARB79-
         BAL   R2,PUTLINE         YES, SEND WARNING             -ARB79-
         MVC   MSGBUF(L'ASCWARN),ASCWARN                        -ARB79-
NASCWRN1 CLI   COUNTFLG,C' '      COUNT OPTION SET YET?         -ARB82-
         BNE   COUNTSET           YES.                          -ARB82-
         MVI   COUNTFLG,C'Y'      NO, DEFAULT IS COUNT FOR NL   -ARB82-
COUNTSET MVC   SUPOUT,MAPFLG      SUPPRESS LISTING IF MAPONLY   -ARB82-
         LTR   R7,R7              IS LENGTH<0?                  -ARB82-
         BM    EONLVER            IF SO, RETURN TO EODS         -ARB82-
         TM    WRTFLG,X'02'       JUST SKIPPING NOW?            -ARB82-
         BZ    DOCTL              YES, BACK TO CONTROL RTN      -ARB79-
         B     TRYLIST                                          -ARB79-
         SPACE 5
READON   DS    0H
         MVC   RECBUF,BLANKBUF    CLEAR FIRST PART OF BUFFER FAST
READER   CLI   MARK,X'00'         JUST AFTER A TAPE MARK?       -ARB79-
         BE    NO7TRACK                                         -ARB79-
         TM    JFCLTSV,X'42'      YES, IS INPUT LABELLED?       -ARB79-
         BZ    NO7TRACK                                         -ARB79-
         ZAP   FILENO(4),MARKNO   SEE IF READING LABEL OR DATA  -ARB79-
         DP    FILENO(4),=P'3'                                  -ARB79-
         TM    CTRACKS,X'10'      IS INPUT | OUTPUT 7 TRACK?    -ARB79-
         BZ    NO7TRACK                                         -ARB79-
         CP    FILENO+3(1),=P'1'  IS IT DATA?                   -ARB79-
         BE    USERTCH            YES, SER USER TRTCH           -ARB79-
         LA    R2,X'2B'           LABELS, SET TRTCH=ET          -ARB79-
         LR    R6,R2                                            -ARB79-
         B     SETRTCH                                          -ARB79-
USERTCH  IC    R2,ITRTCH          RESET TRTCH FOR LABEL         -ARB79-
         IC    R6,OTRTCH                                        -ARB79-
SETRTCH  MODESET KEY=ZERO         IN ORDER TO STORE INTO DEB           *
                                  (REQUIRES AUTHORIZATION)      -ARB79-
         TM    ITRACKS,X'10'      IS INPUT 7 TRACK?             -ARB79-
         BZ    NO7IN                                            -ARB79-
         O     R2,DCBDEN+INPUT-3  YES, DETERMINE SETMODE OP     -ARB79-
         L     R15,DCBDEBAD+INPUT                               -ARB79-
         STC   R2,DEBSDVM(R15)    AND STORE INTO DEB            -ARB79-
NO7IN    TM    OTRACKS,X'10'      7 TRACK SL OUTPUT?            -ARB79-
         BZ    NO7OUT                                           -ARB79-
         TM    OUTLTYP,X'02'                                    -ARB79-
         BZ    NO7OUT                                           -ARB79-
         O     R6,DCBDEN+OUTPUT-3 CREATE SET MODE OP CODE       -ARB79-
         L     R15,DCBDEBAD+OUTPUT                              -ARB79-
         STC   R6,DEBSDVM(R15)    AND STORE INTO DEB            -ARB79-
NO7OUT   MODESET KEY=NZERO        GIVE UP PRIVILEGES            -ARB79-
NO7TRACK XC    TAPE(4),TAPE       CLEAR ECB
         MVI   ERRLAST,0          RESET RECENT ERROR FLAG
         NI    WRTFLG,X'03'       RESET DON'T COPY FLAG         -ARB82-
         ST    R4,BLKNO           STORE BLOCK NUMBER FOR SYNAD  -ARB82-
         LA    R2,RECBUF
         LA    R7,1               SET UP NEGATIVE LENGTH        -ARB82-
         LNR   R7,R7              FOR EODAD                     -ARB82-
         READ  TAPE,SF,INPUT,(R2),'S' READ A BLK OR TAPEMARK (WE HOPE)
         CHECK TAPE               EODAD=EODS
         TM    OPTNO+1,X'01'      *** DEBUG OPTION FOR DISK TAPE SIM **
         BZ    *+14               BIF TAPE SIM OPTION NOT SPECIFIED
         CLC   RECBUF(8),=C'TAPEMARK'  SEE IF TAPEMARK TO BE SIMULATED
         BE    EODS               BIF YES, SIMULATE A TAPEMARK
         MVI   MARK,X'00'         EODS SETS TO X'FF' WHEN TAPEMARK READ
         NI    EOV1FLG,X'FD'      TURN OFF INITT EOV FLAG       -ARB82-
         LA    R4,1(R4)           COUNT THE BLK JUST READ FOR DATASET
         OC    PHFLSTAT+#IO(1),ERRLAST  NOTE IF IOERR OCCURRED  -ARB82-
         L     R6,TAPE+16         GET PTR TO IOB
         LH    R7,INPUT+DCBBLKSI
         SH    R7,14(R6)
         BAL   R2,LENCK           CHECK FOR LENGTH ATROCITY     -ARB79-
         L     R1,FILEBYTS        ADD BLOCK LENGTH TO TOTAL       -CWB-
         AR    R1,R7                   BYTE COUNT FOR THIS        -CWB-
         ST    R1,FILEBYTS             FILE.                      -CWB-
         AR    R8,R7              COUNT ALL BYTES READ DURING RUN
         C     R7,MAX             COMPARE CURRENT BLK'S SIZE WITH MAX
         BNH   *+8                SKIP IF OLD MAX IS BIGGER
         ST    R7,MAX             STORE NEW MAX BLK LENGTH
         C     R7,MIN             COMPARE BLK'S SIZE WITH PREVIOUS MIN
         BNL   *+8                SKIP IF OLD MIN IS SMALLER
         ST    R7,MIN             STORE NEW MINIMUM BLOCK LENGTH
         MVC   ASCBUF,RECBUF      COPY RECORD TO 2ND BUFFER     -ARB79-
         CLI   ASCIIN,C'Y'        TRANSLATE FROM ASCII?         -ARB79-
         BNE   NOINTRAN                                         -ARB79-
         CLI   ASCIOUT,C'Y'       YES, COPYING TO ASCII?        -ARB82-
         BNE   INTRAN             NO, TRANSLATE ENTIRE BLOCK    -ARB82-
         CH    R7,=H'120'         LESS THAN 120 BYTES READ?     -ARB82-
         BL    INTRAN             YES, JUST XLATE WHAT'S READ   -ARB82-
         XLATE RECBUF,120,TO=E    YES, TRANSLATE PRINTING PART  -ARB82-
         B     NOINTRAN                                         -ARB82-
INTRAN   XLATE RECBUF,(R7),TO=E   MAKE WHOLE LINE EBCDIC        -ARB82-
NOINTRAN CLI   WRTFLG,X'03'       SEE IF WILL BE WRITING        -ARB79-
         BNER  R5                                               -ARB79-
         STH   R7,WRTCMND+6       STORE BLK LENGTH IN WRITE CCW
         BR    R5                 RETURN FROM READON SUBROUTINE
         SPACE 5
CHKVOL1  CLC   ASCBUF(4),ASCIVOL1 SEE IF ANS VOL LABEL          -ARB79-
         BE    ANSVOL1                                          -ARB79-
         C     R7,=F'80'          IS THIS RECORD LENGTH 80?     -ARB79-
         BNE   NOVOL1             NO, NOT AN IBM LABEL          -ARB79-
         CLC   ASCBUF(4),=C'VOL1' SEE IF IBM VOL LABEL          -ARB79-
         BE    IBMVOL1                                          -ARB79-
         B     NOVOL1             ELSE VOLUME  LABEL NOT FOUND  -ARB79-
         SPACE
CHKHDR1  CLC   RECBUF(4),=C'HDR1' IS THIS A HDR1 LABEL?         -ARB79-
         BE    FIRSTHDR           YES, CHECK FOR PROTECTION     -ARB79-
         MVC   SECDSN,BLANKBUF    START DSN AS BLANKS           -ARB82-
         CLI   ASCIIN,C'Y'        NO, ANS TAPE?                 -ARB82-
         BNE   NOHDR1             NO, MAKE SECURITY CHECK       -ARB82-
         CLC   RECBUF(3),=C'UVL'  YES, USER VOLUME LABEL?       -ARB82-
         BE    DEFERHDR           YES, HEADER MAY STILL APPEAR  -ARB82-
NOHDR1   MVI   GOODLAB,C'N'       LABELS BAD IF HDR1 NOT FOUND  -ARB82-
         MVI   PHFLSTAT+#LBSQ,X'FF'    SAY LABEL SEQUENCE ERR   -ARB82-
         MVC   SECLABEL,VOLLAB    COPY LABEL TYPE FOR SCUR EXIT -ARB82-
         MVC   VOLLAB,=C'BLP'     SET LABEL TYPE TO BLP         -ARB82-
         B     SCURINB            GO MAKE SECURITY CHECK        -ARB82-
HDRET    MVI   VERVOL,C'N'        VOLUME-START PROCESS DONE     -ARB82-
         MVC   SUPOUT,MAPFLG      KILL LISTING IF MAPONLY       -ARB82-
         LTR   R7,R7              REACHED END OF FILE?          -ARB82-
         BM    EONLVER            YES, RETURN TO EODS           -ARB82-
DEFERHDR TM    WRTFLG,X'02'       STILL SKIPPING?               -ARB82-
         BNZ   NOHDRP             NO, RESUME NORMAL PROCESSING  -ARB82-
         CLI   VERVOL,C'H'        STILL EXPECTING A HEADER?     -ARB82-
         BE    LOCHDR             YES, KEEP LOOKING             -ARB82-
         B     NOHDRS             NO, KEEP SKIPPING             -ARB82-
CHKLABEL DS    0H
         MVI   LABELIKE,C'Y'      ASSUME LABEL HERE             -ARB82-
         CLC   RECBUF(4),=C'HDR1'
         BE    HDR1
         CLC   RECBUF(4),=C'EOF1'
         BE    EOF1EOV1
         CLC   RECBUF(4),=C'HDR2'
         BE    HDR2
         CLC   RECBUF(4),=C'EOF2'
         BE    EOF2EOV2
         CLC   RECBUF(4),=C'VOL1'
         BE    VOL1
         CLC   RECBUF(4),=C'EOV1'
         BE    EOF1EOV1
         CLC   RECBUF(4),=C'EOV2'
         BE    EOF2EOV2
         CLC   RECBUF(3),=C'UHL'  USER HEADER?                  -ARB82-
         BE    USERLABL                                         -ARB82-
         CLC   RECBUF(3),=C'UTL'  USER TRAILER?                 -ARB82-
         BE    USERLABL                                         -ARB82-
         CLI   ASCIIN,C'Y'        CHECK FOR ODD ASCII LABELS?   -ARB82-
         BNE   UNLABLIK           NO, WE HAVE NO LABEL          -ARB82-
         CLC   RECBUF(3),=C'EOF1' ASCII EOFX LABEL?             -ARB82-
         BE    ANSXLAB                                          -ARB82-
         CLC   RECBUF(3),=C'EOV1'                               -ARB82-
         BE    ANSXLAB                                          -ARB82-
         CLC   RECBUF(3),=C'HDR1'                               -ARB82-
         BE    ANSXLAB                                          -ARB82-
         CLC   RECBUF(3),=C'UVL'  ASCII USER VOL LABEL?         -ARB82-
         BE    ANSUVL                                           -ARB82-
UNLABLIK MVI   LABELIKE,C'N'      RESET LABEL INDICATION        -ARB82-
         B     NOLABEL            IT'S NOT A LABEL AFTER ALL
         SPACE
PROCESS2 CLI   GOODLAB,C'Y'       IN A GOOD LABEL FILE?         -ARB82-
         BE    LABSIZCK           YES, PROCEED                  -ARB82-
         OI    PHFLSTAT+#LBSQ,X'FF'    NO, NOTE FOR VTOC        -ARB82-
         B     PROCESS3           AND CONTINUE                  -ARB82-
LABSIZCK CLI   WRTFLG,X'03'       COPYING THIS LABEL?           -ARB82-
         BNE   PROCESS3           NO.                           -ARB82-
         C     R7,=F'80'          YES, OVERSIZE LABEL?          -ARB82-
         BE    PROCESS3           NO.                           -ARB82-
         CLC   ASCIIN,ASCIOUT     YES, CHANGING TO EBCDIC?      -ARB82-
         BE    PROCESS3           NO, ALL IS WELL               -ARB82-
         MVC   WRTCMND+6(2),=H'80'     YES, TRUNCATE TO 80      -ARB82-
         MVI   PHFLSTAT+#TRNC,X'FF'    NOTE TRUNCATION IN VTOC  -ARB82-
         B     PROCESS3           AND CONTINUE                  -ARB82-
         SPACE
FIRSTHDR MVC   SECDSN,RECBUF+4    COPY DSNAME FROM HDR1         -ARB82-
         CLI   RECBUF+53,C'1'     IS DATASET1 READ PROTECTED?   -ARB82-
         BE    RQRINPW            YES.                          -ARB79-
         CLI   ASCIIN,C'Y'        IS INPUT ASCII?               -ARB79-
         BNE   SCURIN             NO, CHECK LOCAL SECURITY      -ARB82-
         CLI   RECBUF+53,C' '     YES, CHECK FOR VALID ACCESSIBILITY   *
                                                                -ARB79-
         BE    SCURIN             OK IF UNPROTECTED             -ARB79-
         CLI   RECBUF+53,C'3'                                   -ARB79-
         BE    SCURIN             OK IF WRITE-PROTECTED         -ARB82-
         BAL   R2,PUTLINE                                       -ARB79-
         MVC   MSGBUF(64),=C'0PROTECTED ASCII TAPE FILE MAY NOT BE READ*
                - TAPESCAN TERMINATED'                          -ARB79-
         B     EXITRC16                                         -ARB79-
RQRINPW  CLI   COUNTFLG,C'Y'      COUNT OPTION?                 -ARB79-
         BE    PWSETUP            YES, DATA MUST BE READ        -ARB79-
         OC    LISTNO,LISTNO      LIST OPTION?                  -ARB79-
         BZ    SCURIN             NO, IGNORE PASSWORD           -ARB82-
PWSETUP  L     R5,16              FIND JOB & STEP NAME          -ARB79-
         L     R5,0(,R5)          FOR CONSOLE PASSWORD WTOR     -ARB79-
         L     R5,4(,R5)                                        -ARB79-
         L     R5,TCBTIO(R5)                                    -ARB79-
         MVC   SMSGJOB,TIOCNJOB(R5)    PUT JOB & STEP NAME IN   -ARB79-
         MVC   SMSGSTEP,TIOCSTEP(R5)                            -ARB79-
         MVC   SMSGDD,=CL8'INPUT' INSERT DDNAME                 -ARB79-
         MVI   PSWDSW,0           INDICATE FIRST TRY            -ARB79-
ASKINPW  LA    R2,SMSG                                          -ARB79-
         BAL   R5,SYSWTOR         ASK OPERATOR FOR PASSWORD     -ARB79-
         OC    PSWDREPL,BLANKBUF  UPPER CASE REPLY              -ARB79-
         LA    R2,JFCBIN                                        -ARB79-
         BAL   R5,CHKPASS         SEE IF PASSWORD GOOD FOR DSN  -ARB79-
         LTR   R15,R15            PASSWORD FOUND?               -ARB79-
         BZ    SCURIN             YES, MAKE LOCAL CHECK         -ARB82-
         CLI   PSWDSW,0           WAS THAT THE FIRST TRY        -ARB79-
         BNE   BADINPW            NO, NO MORE ALLOWED           -ARB79-
         MVI   PSWDSW,X'FF'       YES, TRY ONCE MORE            -ARB79-
         B     ASKINPW                                          -ARB79-
SCURIN   MVC   SECLABEL,VOLLAB    STORE VOL1 OR JCL LABEL       -ARB82-
SCURINB  MVI   SECIOTYP,C'I'      NOTE INPUT                    -ARB82-
         MVC   SECVOL,DDVOL       COPY OVER VOLSER              -ARB82-
         MVC   SECDCB,=A(INPUT)   STORE INPUT DCB ADDR          -ARB82-
         BAL   R14,SCUREXIT       ASK INSTALLATION FOR OK       -ARB82-
         LTR   R15,R15            WELL?                         -ARB82-
         BZ    SCURRET            OK, CONTINUE                  -ARB82-
         MVC   MSGBUF(42),=C'0YOU ARE UNAUTHORIZED TO READ THIS TAPE - *
               '                  START UNAUTH MSGS             -ARB82-
         B     UNAUTH             GO COMPLETE IT                -ARB82-
BADINPW  MVC   MSGBUF(42),=C'0INPUT PASSWORD NOT CORRECTLY SPECIFIED - *
               '                  START OF SECURITY MSG         -ARB82-
UNAUTH   MVC   MSGBUF+42(91),BLANKBUF  CLEAR REST OF PRINT BUF  -ARB83-
         TM    WRTFLG,X'01'       COPY REQUESTED?               -ARB83-
         BZ    FORBIDRD           NO, WE CAN PROCEED            -ARB79-
         BAL   R2,PUTLINE2        YES, CANNOT BE DONE           -ARB82-
         MVC   MSGBUF+42(19),=C'TAPESCAN TERMINATED'            -ARB82-
         B     EXITRC16                                         -ARB79-
FORBIDRD BAL   R2,PUTLINE2        ANNOUNCE RESTRICTIONS         -ARB79-
         MVC   MSGBUF+42(29),=C'NOLIST AND NOCOUNT ARE FORCED'  -ARB82-
         XC    LISTNO,LISTNO      TURN OFF LIST OPTION          -ARB79-
         MVI   COUNTFLG,C'N'      TURN OFF COUNT OPTION         -ARB79-
SCURRET  CLI   VOLLAB,C'N'        INPUT VOL UNLABELLED?         -ARB82-
         BE    NLVER              YES, RESUME NL PROCESS        -ARB82-
         B     HDRET              NO, RESUME HDR PROCESS        -ARB82-
         SPACE
HDR1     LA    R0,7               PREPARE TO RESERVE 7  LINES   -ARB79-
         BAL   R2,PAGECHK         RESERVE THE 7  LINES
         MVC   MSGBUF+1(132),BLANKBUF  CLEAR PRINT BUFFER       -ARB82-
         CLC   POSTHEAD,=H'3'     TOO SOON FOR NEW HEADER?      -ARB82-
         BL    HDROOP             YES.                          -ARB82-
         BH    HREAL              NO, THIS ONE IS GOOD          -ARB82-
         MVC   VOLLAB,=C'BLP'     YES, A TRAILER WAS DROPPED    -ARB82-
HREAL    LR    R2,R4              COPY BLOCK NO OF FILE         -ARB82-
         SH    R2,VOL1NO          COMPENSATE FOR PRIOR VOL1     -ARB82-
         CH    R2,=H'1'           FIRST RECORD OF FILE?         -ARB82-
         BNE   HDROOP             NO, HDR OUT OF PLACE          -ARB82-
         MVC   POSTHEAD,=H'1'     NO, INDICATE HEADER FOUND     -ARB80-
         MVI   GOODLAB,C'Y'       SET GOOD LABEL FLAG           -ARB80-
         MVC   LABELID,=C'HH'     SHOW HEADER FILE              -ARB82-
         AP    HEADNO,=P'1'       INCREASE HEADER COUNT         -ARB80-
         MVC   DATASEQ,RECBUF+31  SAVE HEADER SEQ NO            -ARB79-
         SR    R2,R2              CLEAR FOR COMPARISON          -ARB82-
         C     R2,CTPMKNO         SEE IF T/MS ALREADY READ = 0  -ARB82-
         BNE   NOTHREAL           BIF NOT BEFORE FIRST TAPEMARK -ARB82-
         MVI   MSGBUF,C'0'                                      -ARB82-
         MVC   MSGBUF+1(75),MSGBUF   CLEAR TO 76CL1'0'          -ARB82-
         CLC   RECBUF+4(76),MSGBUF   FOR COMPARISON             -ARB82-
         BNE   NOTHREAL            SKIP IF THEY ARE UNEQUAL     -ARB82-
         OI    EOV1FLG,X'02'      ELSE SET IEHINITT EOV PENDING -ARB82-
         B     NOTHREAL                                         -ARB82-
HDROOP   CLI   GOODLAB,C'Y'       LABELS SUPPOSED TO BE GOOD?   -ARB82-
         BNE   NOTHREAL           NO, PROBLEM ALREADY FOUND     -ARB82-
         MVI   GOODLAB,C'?'       YES, NOTE TURNED BAD          -ARB82-
NOTHREAL MVC   MSGBUF+1(132),BLANKBUF  RECLEAR MSG BUF          -ARB82-
         CLI   WRTFLG,X'03'       COPYING?                      -ARB80-
         BNE   HDR1SUM            BR IF NOT                     -ARB79-
         TM    OUTLTYP,X'42'      LABELED OUTPUT TAPE?          -ARB79-
         BZ    HDR1SUM            SKIP FILESEQ STUFF IF NOT     -ARB79-
         CLC   POSTHEAD,=H'1'     REAL HEADER?                  -ARB80-
         BNE   HDR1SUM            NO, DON'T CHANGE SEQ.         -ARB80-
         CLI   FIRSTSEQ,C'N'      FIRST HDR FOUND?              -ARB79-
         BNE   NOTH1                                            -ARB79-
         PACK  FIRSTSEQ,DATASEQ   YES, SAVE SEQ NO AGAIN        -ARB79-
NOTH1    CLI   HDRFOUND,C'Y'      HAS OUTPUT SEQ BEEN FIXED?    -ARB79-
         BE    HDR1SUM            YES, DON'T DO IT AGAIN        -ARB79-
         AP    HEADNO(3),FIRSTSEQ COMPUTE OUTPUT LABEL NO       -ARB80-
         MVI   HDRFOUND,C'Y'                                    -ARB79-
HDR1SUM  CLI   GOODLAB,C'Y'       IS THIS REALLY A LABEL FILE?  -ARB82-
         BNE   NOH1VTOC           NO, LEAVE OUT OF VTOC         -ARB82-
         CLI   SUMFLG,C'N'        NOSUM OPTION?                 -ARB82-
         BE    LBL1OUT            YES, SKIP VTOC                -ARB82-
         BAL   R2,GETVTOC         GET A NEW VTOC ENTRY          -ARB82-
         USING VTOC,R3            R3 WILL ALWAYS POINT TO ENTRY   -CWB-
         MVI   PHFLSTAT+#NTRL,X'FF'     SET TRAILER NOT FOUND   -ARB82-
         MVC   VTOCLTYP,VOLLAB    SAVE LABEL TYPE FOR VTOC      -ARB82-
         CLC   VOLLAB(2),=C'NL'   PREVIOUS FILES NL?            -ARB82-
         BNE   SLABOK             NO.                           -ARB82-
         MVC   VOLLAB(3),=C'BLP'  YES, SWITCH TO BLP            -ARB82-
         MVI   PHFLSTAT+#BLP,X'FF'     FORCE NOTE IN VTOC       -ARB82-
SLABOK   MVC   VTOCSEQN,DATASEQ   COPY SEQ # FROM LABEL         -ARB82-
         MVC   VTOCRECF(2),=C'??' NOTE RECFM/LRECL/BLKSIZE      -ARB82-
         MVC   VTOCLREC,=C'   ??' UNKNOWN IN CASE HDR2 IS       -ARB82-
         MVC   VTOCBLKS,=C'   ??' MISSING                       -ARB82-
NOH1VTOC CLI   SUMFLG,C'F'        FULL SUMMARY?                 -ARB79-
         BE    HDR1LIST           YES, PRINT THE LABEL          -ARB79-
         MVC   MSGBUF+6(3),LABTYPE                              -ARB79-
         MVC   MSGBUF+10(12),=C'HEADER LABEL'                   -ARB79-
         MVI   MSGBUF+23,C'1'     IDENTIFY LABEL NO             -ARB79-
         MVC   MSGBUF+25(11),=C'(FILE NNNN)'                    -ARB79-
         MVC   MSGBUF+31(4),RECBUF+31  ADD THE FILE SEQ NO      -ARB79-
         LA    R2,*+2                                           -ARB79-
         B     PUTLINE3           PRINT LABEL ID LINE           -ARB79-
         MVI   MSGBUF,C' '        RESET CARRIAGE CNTL           -ARB79-
         B     LBL1               SKIP FULLSUM OUTPUT           -ARB79-
HDR1LIST MVC   MSGBUF+4(16),=C' HEADER LABEL  :'                -ARB79-
         MVC   MSGBUF+1(3),LABTYPE     STORE LABEL TYPE         -ARB79-
         MVI   MSGBUF+18,C'1'                                   -ARB79-
         MVC   MSGBUF+23(90),RECBUF    PUT LABEL INTO BUFFER    -ARB82-
         MVI   MSGBUF,C'0'        CCTRL FOR 1 BLANK LINE, THEN  -ARB79-
         MVC   MSGBUF+116(9),=C'FILE SEQ='                      -ARB82-
         MVC   MSGBUF+125(4),RECBUF+31 ADD FILE SEQ TO LINE     -ARB82-
         BAL   R2,PUTLINE3     PRINT
         DC    C'HDR111'
         MVI   HEXOFF+1,22        SET OFFSET FOR HEX            -ARB79-
         MVI   LINESIZE+3,89      PRINT ONLY 90 HEX CHARS       -ARB79-
         BAL   R5,HEXON           PRINT HEX IF OPTION IS YES
         MVI   MSGBUF,C'0'                                      -ARB79-
LBL1     C     R7,=F'80'          IS LABEL NORMAL SIZE?         -ARB79-
         BE    LBL180             YES, NO COMMENT               -ARB79-
         BAL   R2,PUTLINE         NO, MAKE NOTE                 -ARB79-
         MVC   MSGBUF(L'LABLSIZE),LABLSIZE                      -ARB79-
LBL180   MVC   MSGBUF+1(132),BLANKBUF                           -ARB79-
         MVC   MSGBUF+11(7),=C'DSNAME='
         MVC   MSGBUF+18(17),RECBUF+4
*        CLI   RECBUF+4,C'T'                                    -DEBUG-
*        BNE   *+8                                              -DEBUG-
*        MVI   PHFLSTAT+#DBUG,X'FF'                             -DEBUG-
         CLI   RECBUF,C'H'        IS THIS A TRAILER?            -ARB79-
         BE    VDSN               NO, SAVE DSN IN VTOC          -ARB82-
         CLI   HDRMISS,C'Y'       YES, HEADER MISSING?          -ARB82-
         BNE   LBL1PROT           NO, SKIP DSN STORE            -ARB82-
VDSN     CLI   GOODLAB,C'Y'       TRUSTWORTHY LABEL?            -ARB82-
         BNE   NOVDSN             IF NOT, DON'T SAVE DSN        -ARB82-
         MVC   VTOCDSN,RECBUF+4   SAVE DSN FOR VTOC.              -CWB-
         TM    EOV1FLG,X'02'      IEHINITT LABEL ACTIVE?        -ARB82-
         BZ    NOVDSN             NO.                           -ARB82-
         MVC   VTOCDSN,=CL17'  *IEHINITT*' YES, SAY SO          -ARB82-
NOVDSN   CLI   RECBUF,C'H'        WORKING FOR TRAILER?          -ARB82-
         BNE   LBL1PROT           YES, SKIP CREATE DATE         -ARB82-
         MVC   MSGBUF+72(14),=C'CREATION DATE='                 -ARB82-
         LA    R1,RECBUF+42        SET UP PARMLST               -ARB79-
         ST    R1,WORK             FIRST PARM IS FROM FIELD  *FD78.130
         L     R1,AMSGBP86         AGAIN FOR SECOND PARM        -ARB79-
         ST    R1,WORK+4           SECOND ONE IS "TO" FIELD  *FD78.130
         MVI   WORK+4,X'80'       SET VL BIT                    -ARB82-
         MVC   0(6,R1),=C'000000'  ZERO OUT THE "TO" FIELD   *FD78.130
         LA    R1,WORK             SET PARMLST PTR           *FD78.130
         BAL   R14,JULDATE        CONVERT DATE TO JULIAN        -ARB82-
         CLI   GOODLAB,C'Y'       IS THIS A TRUE LABEL?         -ARB82-
         BNE   *+10               IF NOT, SKIP SAVE             -ARB82-
         MVC   VTOCCREA,MSGBUF+86 SAVE DATE FOR VTOC.           -ARB82-
LBL1PROT CLI   RECBUF+54,C'1'     PROTECTED DS?                 -ARB82-
         BE    RWPROT             YES.                          -ARB82-
         CLI   RECBUF+54,C'3'                                   -ARB82-
         BNE   LBL1XPDT           NO, DON'T MENTION IT          -ARB82-
         MVC   MSGBUF+111(5),=C'WRITE' YES, WRITE PROTECTED     -ARB82-
         B     PROTMSG                                          -ARB82-
RWPROT   MVC   MSGBUF+111(3),=C'R/W'                            -ARB82-
PROTMSG  MVC   MSGBUF+100(11),=C'PROTECTION='                   -ARB82-
         CLI   RECBUF,C'H'        HAVE WE A HEADER?             -ARB82-
         BE    LBL1XPDT           YES.                          -ARB82-
         MVC   MSGBUF+72(16),MSGBUF+100     NO, COMPACT LINE    -ARB82-
         MVC   MSGBUF+100(16),BLANKBUF                          -ARB82-
LBL1XPDT MVC   MSGBUF+42(20),=C'EXPIRATION DATE=NONE'           -ARB79-
         CLC   RECBUF+48(5),=C'000000' IS THERE AN EXPDT?       -ARB79-
         BE    NOEXPDT            NO, LEAVE "NONE"              -ARB79-
         LA    R1,RECBUF+48       SET UP TO CONVERT TO GREG     -ARB79-
         ST    R1,WORK                                          -ARB79-
         L     R1,AMSGBP58                                      -ARB79-
         ST    R1,WORK+4                                        -ARB79-
         MVI   WORK+4,X'80'       SET VL BIT                    -ARB82-
         MVC   0(6,R1),=C'000000'                               -ARB79-
         LA    R1,WORK                                          -ARB79-
         BAL   R14,JULDATE        PUT EXPDT IN HUMAN FORM       -ARB82-
NOEXPDT  BAL   R2,PUTLINE3                                      -ARB79-
         DC    CL6'LBL111'
         CLI   GOODLAB,C'Y'       IS THIS A TRUE LABEL?         -ARB82-
         BNE   *+10               IF NOT, SKIP SAVE.            -ARB82-
         MVC   VTOCEXPR,MSGBUF+58 SAVE EXP DATE FOR VTOC.       -ARB82-
         CLI   SUMFLG,C'F'        FULLSUM OPTION?               -ARB79-
         BE    DOBLKCNT           YES, OUTPUT BLKCNT            -ARB79-
         CLI   RECBUF,C'H'        NO, PROCESSING TRAILERS?      -ARB82-
         BNE   DOBLKCNT           YES, OUTPUT BLKCNT            -ARB82-
LBL1OUT  CLI   WRTFLG,X'03'       ARE WE COPYING?               -ARB79-
         BNE   LBL1SUMD                                         -ARB82-
         TM    OUTLTYP,X'42'      YES, LABELLED OUTPUT?         -ARB79-
         BZ    LBL1SUMD                                         -ARB82-
         CLI   GOODLAB,C'Y'       IS THIS REALLY A LABEL?       -ARB80-
         BNE   LBL1SUMD           NO, DON'T MODIFY IT           -ARB82-
         UNPK  RECBUF+31(4),HEADNO(3)  PUT TRUE SEQ INTO LABEL  -ARB80-
         OI    RECBUF+34,C'0'                                   -ARB79-
         CLC   ASCIIN(2),=C'YY'   ASCII->ASCII COPY?            -ARB82-
         BNE   NOSEQTRN           NO.                           -ARB82-
         MVC   ASCBUF+31(4),=X'3F3F3F3F'    YES,                -ARB82-
         NC    ASCBUF+31(4),RECBUF+31  CHANGE ASCII LABEL COPY  -ARB82-
NOSEQTRN MVC   MSGBUF+1(132),BLANKBUF                           -ARB82-
         MVC   MSGBUF+6(19),=C'OUTPUT FILE NUMBER '             -ARB80-
         MVC   MSGBUF+25(4),RECBUF+31  PUT NEW FILE NO IN MSG   -ARB80-
         LA    R2,*+2                                           -ARB80-
         B     PUTLINE3           INFORM OF OUTPUT FILE NO      -ARB80-
         CLC   VOLLAB,=C'BLP'     HAVE LABELS GONE BAD?         -ARB82-
         BE    NOOUSEQ            YES, DON'T STORE OUTPUT SEQ   -ARB82-
         MVC   VTOCOUSQ,MSGBUF+25 COPY OUTPUT FILE SEQ FOR VTOC -ARB82-
NOOUSEQ  CLC   ASCIIN,ASCIOUT     ASCII TRANSLATION?            -ARB82-
         BE    LBL1SUMD           NO.                           -ARB82-
         TM    RECBUF+53,X'03'    YES, IS THIS FILE PROTECTED?  -ARB79-
         BNZ   LBL1SUMD           YES, NO CONVERSION NEEDED     -ARB82-
         XI    RECBUF+53,X'B0'    YES, SWITCH ' ' AND '0'       -ARB79-
LBL1SUMD MVC   MSGBUF(1),SUMFLG   SAVE SIGN OF SUMMARY OUTPUT   -ARB82-
         B     PROCESS2
EOF1EOV1 DS    0H
         CH    R4,=H'1'           FIRST BLOCK OF FILE?          -ARB82-
         BNE   FALSEOF            NO, NOT REALLY A LABEL        -ARB82-
         CLC   POSTHEAD,=H'3'     IS THIS TRAILER DATA?         -ARB82-
         BL    FALSEOF            YES, SKIP SEQ CHECK           -ARB82-
         MVI   HDRMISS,C'Y'       ASSUME UNMATCHED TRAILER      -ARB82-
         BE    NORMLTRL           NO, NORMAL TRAILER LABEL      -ARB82-
         CLC   POSTHEAD,=X'FFFE'  HEADER FOR THIS TRAILER?      -ARB80-
         BNL   CHEKPREV           NO.                           -ARB82-
         MVC   POSTHEAD,=X'FFFE'  YES, RESET AWAITING TRAILER   -ARB82-
CHEKPREV CLI   PREVLAB,C'E'       WAS PREVIOUS FILE TRAILERS?   -ARB82-
         BE    FALSEOF            YES, TREAT THIS AS DATA       -ARB82-
         B     ACCEPTRL                                         -ARB82-
NORMLTRL MVC   POSTHEAD,=X'FFFE'  YES, NOTE TRAILER PASSED      -ARB82-
         CLI   FILESTAT+#NTRL,X'FF'     SEEN HEADER ALREADY?    -ARB82-
         BNE   ACCEPTRL      SKIP ERR MSG IF NO PREVIOUS HDR1   -ARB82-
         MVI   HDRMISS,C'N'       NOTE MATCHING HEADER FOUND    -ARB82-
         CLC   DATASEQ,RECBUF+31  COMPARE OLD DATASET SEQ. NO. TO THE  *
                                  CURRENT ONE.
         BE    ACCEPTRL           SKIP ERROR MSG IF EQUAL       -ARB82-
         MVI   PHFLSTAT+#NCON,X'FF'    NOTE INCONSISTENCY       -ARB82-
         BAL   R2,PUTLINE                                       -ARB79-
         MVC   MSGBUF(099),=C'0ERROR - THE DATASET SEQ. NO. ON THE FOLL*
               OWING LABEL DOES NOT MATCH THAT ON THE PREVIOUS HDR1 LAB*
               EL'                                              -ARB79-
ACCEPTRL MVI   GOODLAB,C'Y'       NOTE LABEL IS OK              -ARB82-
         MVC   LABELID,=C'ET'     SHOW TRAILER FILE             -ARB82-
         MVI   DATASEQ,C'N'       FORGET THAT HEADER            -ARB82-
         B     TRUEOF             SKIP BAD LABEL CODE           -ARB82-
FALSEOF  CLI   GOODLAB,C'Y'       DO WE THINK WE HAVE LABELS?   -ARB82-
         BNE   TRUEOF             NO, NO ILLUSIONS              -ARB82-
         MVI   GOODLAB,C'?'       YES, WAKE UP                  -ARB82-
TRUEOF   CLI   SUMFLG,C'N'        SUMMARY WANTED?               -ARB82-
         BE    LBL1OUT                                          -ARB79-
         LA    R0,7               PREPARE TO RESERVE 7 LINES    -ARB79-
         BAL   R2,PAGECHK                                       -ARB79-
         MVC   MSGBUF,BLANKBUF                                  -ARB82-
         CLI   SUMFLG,C'F'        FULL SUMMARY?                 -ARB79-
         BE    TRL1LIST           YES, SKIP NORMAL SUM LINE     -ARB79-
         MVI   MSGBUF,C'0'                                      -ARB79-
         MVC   MSGBUF+6(3),LABTYPE                              -ARB79-
         MVC   MSGBUF+10(13),=C'TRAILER LABEL'                  -ARB79-
         MVI   MSGBUF+24,C'1'     IDENTIFY LABEL NO             -ARB79-
         MVC   MSGBUF+26(11),=C'(FILE NNNN)'                    -ARB79-
         MVC   MSGBUF+32(4),RECBUF+31  ADD THE FILE SEQ NO      -ARB79-
         LA    R2,TRL1WARN-6                                    -ARB79-
         B     PUTLINE3           IDENTIFY TRAILER LABEL        -ARB79-
TRL1LIST MVC   MSGBUF+4(17),=C' TRAILER LABEL  :'               -ARB79-
         MVC   MSGBUF+1(3),LABTYPE     ADD LABEL TYPE           -ARB79-
         MVI   MSGBUF+19,C'1'                                   -ARB79-
         MVC   MSGBUF+23(90),RECBUF    PUT LABEL INTO BUFFER    -ARB82-
         MVC   MSGBUF+116(9),=C'FILE SEQ='                      -ARB82-
         MVC   MSGBUF+125(4),RECBUF+31 STORE SEQ NO FROM LABEL  -ARB82-
         MVI   MSGBUF,C'0'        SKIP 1 LINE FOR LIST          -ARB79-
         BAL   R2,PUTLINE3
         DC    C'EOF1EV'
         MVI   HEXOFF+1,22        FOR HEX ROUTINE               -ARB82-
         MVI   LINESIZE+3,89                                    -ARB79-
         BAL   R5,HEXON
         MVI   MSGBUF,C'0'        DOUBLE SPACE EXPANSION        -ARB79-
TRL1WARN C     R7,=F'80'          USUAL LABEL SIZE?             -ARB79-
         BE    TRL180             YES, SAY NOTHING              -ARB79-
         BAL   R2,PUTLINE         NO, MAKE IT CLEAR             -ARB79-
         MVC   MSGBUF(L'LABLSIZE),LABLSIZE                      -ARB79-
TRL180   CLI   SUMFLG,C'F'        FULLY SUMMARIZING?            -ARB79-
         BE    LBL180             YES, PRINT HEADER-TYPE FIELDS -ARB79-
         MVI   MSGBUF,C' '        NO, SINGLE SPACE SUMMARY      -ARB82-
         CLI   HDRMISS,C'Y'       EVER SEEN MATCHING HEADER?    -ARB82-
         BE    LBL180             NO, PRINT OUT ALL             -ARB82-
DOBLKCNT MVC   MSGBUF,BLANKBUF                                  -ARB79-
         CLI   RECBUF,C'H'        IS THIS A HEADER?             -ARB79-
         BE    HDR1BLK            YES, SKIP MOD DATE            -ARB79-
         MVC   MSGBUF+40(18),=C'MODIFICATION DATE='             -ARB79-
         LA    R1,RECBUF+42        SET UP PARMLST               -ARB79-
         ST    R1,WORK             FIRST PARM IS FROM FIELD     -ARB79-
         L     R1,AMSGBP58         AGAIN FOR SECOND PARM        -ARB79-
         ST    R1,WORK+4           SECOND ONE IS "TO" FIELD     -ARB79-
         MVI   WORK+4,X'80'       SET VL BIT                    -ARB82-
         MVC   0(6,R1),=C'000000'  ZERO OUT THE "TO" FIELD      -ARB79-
         LA    R1,WORK             SET PARMLST PTR              -ARB79-
         BAL   R14,JULDATE        CONVERT DATE TO JULIAN        -ARB82-
HDR1BLK  MVC   MSGBUF+11(12),=C'BLOCK COUNT='                   -ARB79-
         MVC   MSGBUF+23(6),RECBUF+54                           -ARB79-
         CLI   GOODLAB,C'Y'       REALLY A LABEL FILE?          -ARB82-
         BNE   BLKPRT             IF NOT, LEAVE VTOC ALONE      -ARB82-
         CLI   RECBUF,C'H'        HDR LABEL?                    -ARB82-
         BE    BLKPRT             YES, AVOID VTOC               -ARB82-
         CLI   HDRMISS,C'Y'       LONELY TRAILER LABEL?         -ARB82-
         BNE   MATCHK             NO, CHECK OUT DSN             -ARB82-
         MVC   VTOCCREA(7),MSGBUF+58   YES, SAVE MOD DATE       -ARB82-
MATCHK   CLI   FILESTAT+#NTRL,X'FF'     HAS HEADER BEEN SEEN?   -ARB82-
         BE    MATCHD             YES.                          -ARB82-
         MVI   PHFLSTAT+#NHDR,X'FF'     NO, DIAGNOSE NO HDRS    -ARB82-
         CLC   VTOCLTYP,=C'BLP'   ALREADY BLP FOR VTOC?         -ARB82-
         BNE   SLBLP              NO, BECOME SO                 -ARB82-
         MVI   FILESTAT+#BLP,0    YES, CANCEL BLP NOTE (MORE ACCURATE  *
                                  EXPLANATION AVAILABLE)        -ARB82-
         B     SAMEDSN                                          -ARB82-
SLBLP    MVC   VTOCLTYP,=C'BLP'   SET VTOC TYPE TO BLP          -ARB82-
         MVC   VOLLAB,=C'BLP'     FORCE BLP HEREAFTER           -ARB82-
         B     SAMEDSN            SKIP DSNAME CHECK             -ARB82-
MATCHD   CLC   VTOCDSN,RECBUF+4   IS DSN SAME AS HEADER?        -ARB82-
         BE    SAMEDSN            YES, NO PROBLEM               -ARB82-
         OI    PHFLSTAT+#NCON,X'FF'    NO, GIVE WARNING         -ARB82-
SAMEDSN  MVI   FILESTAT+#NTRL,X'00'    TURN OFF NO TRAILERS     -ARB82-
         CLI   VTOCOUNT,C' '      HAS THE TRUE BLOCK COUNT BEEN -ARB82-
         BE    BLKFILL            FILLED IN?  IF NOT, BRANCH    -ARB82-
         CLC   VTOCOUNT,RECBUF+54 LABEL AGREE WITH COUNT?       -ARB82-
         BE    BLKPRT             YES.                          -ARB82-
COUNTERR MVI   PHFLSTAT+#COUN,X'FF'    NO, DIAGNOSE             -ARB82-
         B     BLKPRT                                           -ARB82-
BLKFILL  MVC   VTOCOUNT,RECBUF+54 FILL IT IN FROM THE LABEL.    -ARB82-
         CLI   COUNTFLG,C'N'      IS ANYONE COUNTING?           -ARB82-
         BE    BLKPRT             NO, CAN'T CHECK               -ARB82-
         CLC   VTOCOUNT,=C'000000'     ELSE MUST BE ZERO        -ARB82-
         BNE   COUNTERR           ERROR IF NOT                  -ARB82-
BLKPRT   LA    R2,LBL1OUT-6  RETURN TO LBL1OUT AFTER PRINTING   -ARB82-
         B     PUTLINE3           GO PRINT THE BLOCK COUNT      -ARB79-
         SPACE
EOF2EOV2 CLI   SUMFLG,C'N'        SUMMARY WANTED?               -ARB79-
         BE    LBL2OUT                                          -ARB79-
         MVC   MSGBUF+1(132),BLANKBUF                           -ARB82-
         LA    R0,7               RESERVE 7 LINES               -ARB79-
         BAL   R2,PAGECHK                                       -ARB79-
         CLI   SUMFLG,C'F'        FULL SUMMARY?                 -ARB79-
         BE    TRL2LIST           YES, GIVE FULL LIST           -ARB79-
         MVC   MSGBUF+6(3),LABTYPE                              -ARB79-
         MVC   MSGBUF+10(13),=C'TRAILER LABEL'                  -ARB79-
         MVI   MSGBUF+24,C'2'     IDENTIFY LABEL NO             -ARB79-
         CLI   MSGBUF,C'Y'        MOST RECENT OUTPUT SUMMARY?   -ARB82-
         BNE   SPACEOF2           NO, DOUBLE SPACE              -ARB82-
         MVI   MSGBUF,C' '        YES, SINGLE SPACE             -ARB82-
         B     SUMEOF2                                          -ARB82-
SPACEOF2 MVI   MSGBUF,C'0'        FORCE DOUBLE SPACE            -ARB82-
SUMEOF2  LA    R2,LBL2NLST-6                                    -ARB82-
         B     PUTLINE3           IDENTIFY THE LABEL            -ARB79-
TRL2LIST MVC   MSGBUF+4(17),=C' TRAILER LABEL  :'               -ARB79-
         MVC   MSGBUF+1(3),LABTYPE     DETERMINE LABEL TYPE     -ARB79-
         MVI   MSGBUF+19,C'2'                                   -ARB79-
         MVC   MSGBUF+23(90),RECBUF                             -ARB82-
         MVI   MSGBUF,C'0'        DOUBLE SPACE IT               -ARB79-
         MVI   HEXOFF+1,22        FOR HEX LISTING               -ARB82-
         MVI   LINESIZE+3,89                                    -ARB79-
         B     LBL2LIST                                         -ARB79-
HDR2     DS    0H
         CLI   SUMFLG,C'N'        SUMMARIZE LABELS?             -ARB79-
         BE    LBL2OUT            NO, SKIP IT                   -ARB79-
         MVC   MSGBUF+1(132),BLANKBUF                           -ARB82-
         LA    R0,7               RESERVE 7 LINES FOR PRINT     -ARB79-
         BAL   R2,PAGECHK                                       -ARB79-
         CLI   SUMFLG,C'F'        FULL SUMMARY OPTION?          -ARB79-
         BE    HDR2LIST                                         -ARB79-
         MVC   MSGBUF+6(3),LABTYPE                              -ARB79-
         MVC   MSGBUF+10(12),=C'HEADER LABEL'                   -ARB79-
         MVI   MSGBUF+23,C'2'     INDICATE LABEL 2              -ARB79-
         CLI   MSGBUF,C'Y'        MOST RECENT OUTPUT SUMMARY?   -ARB82-
         BNE   SPACHDR2           NO, DOUBLE SPACE              -ARB82-
         MVI   MSGBUF,C' '        YES, SINGLE SPACE             -ARB82-
         B     SUMHDR2                                          -ARB82-
SPACHDR2 MVI   MSGBUF,C'0'        FORCE DOUBLE SPACE            -ARB82-
SUMHDR2  LA    R2,LBL2NLST-6                                    -ARB82-
         B     PUTLINE3           IDENTIFY LABEL                -ARB79-
HDR2LIST MVC   MSGBUF+4(16),=C' HEADER LABEL  :'                -ARB79-
         MVC   MSGBUF+1(3),LABTYPE     ADD LABEL TYPE TO LINE   -ARB79-
         MVI   MSGBUF+18,C'2'     INDICATE LABEL 2              -ARB79-
         MVC   MSGBUF+23(90),RECBUF                             -ARB82-
         MVI   MSGBUF,C'0'        FORCE DOUBLE SPACING          -ARB79-
         MVI   HEXOFF+1,22                                      -ARB82-
         MVI   LINESIZE+3,89                                    -ARB79-
LBL2LIST BAL   R2,PUTLINE3                                      -ARB79-
         DC    C'HDR222'
         BAL   R5,HEXON
         MVI   LBL2MSG,C'0'       FORCE SKIP AFTER              -ARB79-
LBL2NLST C     R7,=F'80'          ORDINARY LABEL?               -ARB79-
         BE    LBL280                                           -ARB79-
         BAL   R2,PUTLINE         NOT QUITE, SEND WARNING       -ARB79-
         MVC   MSGBUF(L'LABLSIZE),LABLSIZE                      -ARB79-
LBL280   CLI   GOODLAB,C'N'       IN A LABEL FILE?              -ARB82-
         BE    TRUELBL2           NO, SKIP PLAUSIBILITY CHECKS  -ARB82-
         LR    R2,R4              GET OUR BLOCK NUMBER          -ARB82-
         SH    R2,VOL1NO          COMPENSATE FOR PRIOR VOL1     -ARB82-
         CH    R2,=H'2'           WHERE A 2 LABEL SHOULD BE?    -ARB82-
         BNE   BADLBL2            NO.                           -ARB82-
         CLC   LABELID(1),RECBUF  YES, SAME TYPE AS PREVIOUS?   -ARB82-
         BE    TRUELBL2           YES.                          -ARB82-
BADLBL2  MVI   GOODLAB,C'?'       NO, NOT TRUE LABEL            -ARB82-
TRUELBL2 CLI   SUMFLG,C'F'        GIVING FULL SUMMARY?          -ARB82-
         BE    LBL2SUMM           YES, DO IT                    -ARB79-
         CLI   RECBUF,C'H'        TRAILER LABEL?                -ARB79-
         BE    LBL2NSKP           NO, CONTINUE                  -ARB82-
         CLI   HDRMISS,C'Y'       WAS HEADER MISSING?           -ARB82-
         BNE   LBL2OUT            NO, SKIP DCB FORMATTING       -ARB82-
LBL2NSKP MVI   LBL2MSG,C' '                                     -ARB82-
LBL2SUMM MVC   BLKSIZE(5),RECBUF+5                              -ARB79-
         MVC   LRECL(5),RECBUF+10
         MVC   RECFM(1),RECBUF+4  MOVE F, U, OR V PART OF RECFM DESC.
         SR    R1,R1
         CLI   RECBUF+38,C'R'     SEE IF BLOCKING ATTRIBUTE IS 'BS'
         BNE   *+18               BIF NOT BS (BS AS IN 'VBS')
         LA    R1,2
         MVC   RECFM+1(2),=C'BS'
         B     CTRLCHAR
         CLI   RECBUF+38,C' '     SEE IF BLOCKING ATTRIBUTE IS UNBLKED
         BE    CTRLCHAR           BIF UNBLOCKED
         MVC   RECFM+1(1),RECBUF+38   MOVE B OR S BLOCK ATTRIBUTE CHAR
         LA    R1,1               BUMP PTR PAST THE B OR S
CTRLCHAR LA    R1,RECFM+1(R1)
         MVC   0(1,R1),RECBUF+36
         MVC   TRTCH(2),RECBUF+34
         CLC   TRTCH(2),LBL2MSG+1 SEE IF IT'S 2 BLANKS
         BNE   *+10
         MVC   TRTCH(8),=C'STANDARD' 9-TRK OR 7-TRK
         MVC   BUFOFFSP(16),BLANKBUF   BLANK OUT BUFOFF LINE    -ARB79-
         CLC   RECBUF+50(2),BLANKBUF   IS THERE A BUFOFF?       -ARB79-
         BE    NOBUFOFF                                         -ARB79-
         MVC   BUFOFFSP,=C'BUFFER OFFSET='                      -ARB79-
         MVC   BUFOFF,RECBUF+50   YES, ADD TO LINE              -ARB79-
NOBUFOFF BAL   R2,PUTLINE                                       -ARB79-
         MVC   MSGBUF(CRMSG-LBL2MSG),LBL2MSG                    -ARB79-
         CLI   GOODLAB,C'Y'       REALLY A LABEL FILE HERE?     -ARB82-
         BNE   NOVSTUFF           IF NOT, LET VTOC BE           -ARB82-
         CLI   RECBUF,C'H'        SEE IF IT IS 'HDR2'
         BE    HDR2DCB            SAVE DCB POOP IF SO           -ARB82-
         CLI   HDRMISS,C'Y'       TRAILER WITHOUT HEADER?       -ARB82-
         BNE   NOVSTUFF           NO, DON'T STORE DCB           -ARB82-
HDR2DCB  MVC   VTOCRECF,RECFM     COPY DATA FOR VTOC.             -CWB-
         MVC   VTOCLREC,LRECL                                     -CWB-
         MVC   VTOCBLKS,BLKSIZE                                   -CWB-
         MVC   VTOCJOBN,RECBUF+17                                 -CWB-
         MVC   VTOCSTEP,RECBUF+26                                 -CWB-
NOVSTUFF CLI   SUMFLG,C'F'        FULL SUMMARY?                 -ARB82-
         BE    LBL2CRBY           YES, PUT OUT CREATED-BY       -ARB79-
         CLI   RECBUF,C'H'        SEE IF IT IS 'HDR2'
         BE    LBL2CRBY           WRITE 'CREATED BY' IF SO      -ARB82-
         CLI   HDRMISS,C'Y'       NO, HAVE WE SEEN THE HEADER?  -ARB82-
         BNE   LBL2OUT            YES, SKIP "CREATED BY"        -ARB82-
LBL2CRBY MVC   MSGBUF,BLANKBUF    CLEAR MSGBUF                  -ARB79-
         MVC   CRMSG+15(8),RECBUF+17
         MVC   CRMSG+32(8),RECBUF+26
         BAL   R2,PUTLINE2
         MVC   MSGBUF+11(L'CRMSG),CRMSG                         -ARB79-
LBL2OUT  CLI   WRTFLG,X'03'       COPY OPERATION?               -ARB79-
         BNE   PROCESS2           NO, SKIP LABEL ADJUST         -ARB79-
         TM    OUTLTYP,X'42'      OUTPUT TAPE LABELLED?         -ARB79-
         BZ    PROCESS2                                         -ARB79-
         CLI   GOODLAB,C'Y'       IS THIS A TRUE LABEL?         -ARB80-
         BNE   PROCESS2           NO, COPY IT AS IS             -ARB80-
         TM    OUTLTYP,X'40'      ANSI OUTPUT?                  -ARB79-
         BZ    FIXDEN             NO.                           -ARB82-
         CLC   RECBUF+50(2),BLANKBUF   YES, IS BUFOFF SET?      -ARB79-
         BNE   BUFOFFOK                                         -ARB79-
         MVC   RECBUF+50(2),=C'000000' DEFAULT BUFOFF TO 0      -ARB79-
         CLI   ASCIIN,C'Y'        INPUT ASCII ALSO?             -ARB82-
         BNE   BUFOFFOK           NO.                           -ARB82-
         MVC   ASCBUF+50(2),=X'3030'  YES, STORE ASCII 0'S      -ARB82-
BUFOFFOK SR    R2,R2              SEE IF THE DEN IS ACCURATE    -ARB82-
         IC    R2,DCBDEN+INPUT    IN THE INPUT LABEL            -ARB82-
         SRL   R2,6               AND DON'T MODIFY THE OUTPUT   -ARB82-
         EX    R2,TESTDEN         LABEL IF NOT                  -ARB82-
         BNE   LBL2NTCH           ELSE STORE NEW DEN IN LABEL   -ARB82-
FIXDEN   CLI   DCBDEN+OUTPUT,X'D3'     6250 BPI?                -ARB82-
         BE    LBL26250                                         -ARB79-
         SR    R2,R2              NO, COMPUTE DENSITY NUMBER    -ARB79-
         IC    R2,DCBDEN+OUTPUT                                 -ARB79-
         SRL   R2,6                                             -ARB79-
         EX    R2,STOREDEN        PUT OUTPUT DENSITY IN LABEL   -ARB79-
         B     LBL2ADEN                                         -ARB82-
LBL26250 MVI   RECBUF+15,C'4'     SET DEN=4 FOR 6250            -ARB79-
LBL2ADEN CLC   ASCIIN(2),=C'YY'   ASCII->ASCII COPY?            -ARB82-
         BNE   LBL2TCH            NO.                           -ARB82-
         MVC   ASCBUF+15(1),RECBUF+15 YES, DUPLEX CHANGE        -ARB82-
         NI    ASCBUF+15,X'3F'    TO ASCII LABEL COPY           -ARB82-
         MVC   ASCBUF+34(2),=X'2020'   BLANK TRTCH              -ARB82-
         B     LBL2NTCH           CHANGE EBCDIC COPY            -ARB82-
LBL2TCH  CLI   OTRTCH,0           IS OUTPUT TRTCH GIVEN?        -ARB79-
         BE    LBL2NTCH           NO, SET TO BLANK              -ARB79-
         CLI   OTRTCH,X'33'       ALT FORM OF TRTCH=NULL?       -ARB82-
         BE    LBL2NTCH                                         -ARB82-
         IC    R2,OTRTCH          YES, CONVERT TO CHAR          -ARB79-
         SRL   R2,2                                             -ARB79-
         N     R2,=F'6'                                         -ARB79-
         LA    R2,TRTCHS(R2)      FIND STRING IN VECTOR         -ARB79-
         MVC   RECBUF+34(2),0(R2) PUT TRTCH CODE IN LABEL       -ARB79-
         B     PROCESS2                                         -ARB79-
LBL2NTCH MVC   RECBUF+34(2),BLANKBUF   SET BLANK TRTCH          -ARB79-
         B     PROCESS2                                         -ARB79-
         SPACE
USERLABL CLI   SUMFLG,C'N'        NOSUM OPTION?                 -ARB82-
         BE    PROCESS2           YES, DON'T LIST LABEL         -ARB82-
         MVC   MSGBUF,BLANKBUF    NO, LIST USER LABEL           -ARB82-
         MVC   MSGBUF+1(4),=C'USER' SHOW AS USER LABEL          -ARB82-
         CLI   RECBUF+1,C'H'      USER HEADER?                  -ARB82-
         BNE   UTL                NO, TRAILER                   -ARB82-
         MVC   MSGBUF+5(16),=C' HEADER LABEL  :'                -ARB82-
         MVC   MSGBUF+19(1),RECBUF+3   SAVE LABEL NO.           -ARB82-
         B     LISTULAB                                         -ARB82-
UTL      MVC   MSGBUF+5(17),=C' TRAILER LABEL  :'               -ARB82-
         MVC   MSGBUF+20(1),RECBUF+3                            -ARB82-
LISTULAB MVC   MSGBUF+23(90),RECBUF    COPY IN LABEL            -ARB82-
         MVI   MSGBUF,C'0'        FORCE DOUBLE SPACE            -ARB82-
         BAL   R2,PUTLINE3        LIST THE LABEL                -ARB82-
         DC    C'UXL111'                                        -ARB82-
         MVI   HEXOFF+1,22        LIST IN HEX IF HEX OPTION     -ARB82-
         MVI   LINESIZE+3,89                                    -ARB82-
         BAL   R5,HEXON           LIST IN HEX IF WANTED         -ARB82-
         LR    R2,R4              LOOK AT BLOCK NUMBER          -ARB82-
         SH    R2,VOL1NO                                        -ARB82-
         CH    R2,=H'1'           SHOULD HDR1/EOF1 BE HERE?     -ARB82-
         BE    LABSQERR           NO, ERROR                     -ARB82-
         CLC   LABELID+1(1),RECBUF+1   YES, MATCH STD LABEL?    -ARB82-
         BNE   LABSQERR           NO, FLAG ERROR                -ARB82-
         CLI   GOODLAB,C'Y'       LABELS STILL LOOK GOOD?       -ARB82-
         BNE   PROCESS2           NO, SKIP LAB TYPE CHANGE      -ARB82-
         CLI   VOLLAB+2,C'P'      YES, HAS TAPE BECOME BLP?     -ARB82-
         BE    PROCESS2           YES, LEAVE IT ALONE           -ARB82-
         MVC   VOLLAB+1(2),=C'UL' NO, ADD USER LABEL FLAG       -ARB82-
         MVC   VTOCLTYP+1(2),=C'UL'    FOR VTOC                 -ARB82-
         B     PROCESS2           AND CONTINUE                  -ARB82-
LABSQERR CLI   GOODLAB,C'Y'       PREVIOUS LABELS GOOD?         -ARB82-
         BNE   PROCESS2           NO, NOTHING TO ADD            -ARB82-
         MVI   GOODLAB,C'?'       YES, SET LABEL NO GOOD FLAG   -ARB82-
         B     PROCESS2           AND REJOIN MAINLINE           -ARB82-
         SPACE
ANSXLAB  CLI   SUMFLG,C'N'        NOSUM OPTION?                 -ARB82-
         BE    PROCESS2           YES, DON'T LIST LABEL         -ARB82-
         MVC   MSGBUF,BLANKBUF    NO, LIST ODD LABEL            -ARB82-
         MVC   MSGBUF+1(3),=C'ANS'                              -ARB82-
         CLI   RECBUF,C'H'        HEADER LABEL?                 -ARB82-
         BNE   ANSEOX                                           -ARB82-
         MVC   MSGBUF+4(16),=C' HEADER LABEL  :'                -ARB82-
         MVC   MSGBUF+18(1),RECBUF+3   SAVE LABEL NO.           -ARB82-
         B     LISTXLAB                                         -ARB82-
ANSEOX   MVC   MSGBUF+4(17),=C' TRAILER LABEL  :'               -ARB82-
         MVC   MSGBUF+19(1),RECBUF+3   SAVE LABEL NO.           -ARB82-
LISTXLAB MVC   MSGBUF+23(90),RECBUF    COPY IN LABEL            -ARB82-
         MVI   MSGBUF,C'0'        FORCE DOUBLE SPACE            -ARB82-
         BAL   R2,PUTLINE3        LIST THE LABEL                -ARB82-
         DC    C'AXL111'                                        -ARB82-
         MVI   HEXOFF+1,22        LIST IN HEX IF HEX OPTION     -ARB82-
         MVI   LINESIZE+3,89                                    -ARB82-
         BAL   R5,HEXON           LIST IN HEX IF WANTED         -ARB82-
         LR    R2,R4              LOOK AT BLOCK NUMBER          -ARB82-
         SH    R2,VOL1NO                                        -ARB82-
         CH    R2,=H'1'           SHOULD HDR1/EOF1 BE HERE?     -ARB82-
         BE    ANSSQERR           NO, ERROR                     -ARB82-
         CLC   LABELID(1),RECBUF  YES, SAME TYPE AS 1ST LABEL?  -ARB82-
         BE    ANSXWARN           YES, NO PROBLEM               -ARB82-
ANSSQERR CLI   GOODLAB,C'Y'       PREVIOUS LABELS OK?           -ARB82-
         BNE   ANSXWARN           NO, NO FURTHER COMMENT        -ARB82-
         MVI   GOODLAB,C'?'       YES, SET LABEL NO GOOD FLAG   -ARB82-
ANSXWARN BAL   R2,PUTLINE         WARN OF NON-SUPPORTED LABEL   -ARB82-
         MVC   MSGBUF(50),=C'0WARNING - THIS LABEL TYPE IS NOT SUPPORTE*
               D BY IBM'                                        -ARB82-
         CLI   GOODLAB,C'Y'       LABELS STILL LOOK GOOD?       -ARB82-
         BNE   PROCESS2           NO, LET THEM PASS             -ARB82-
         CLI   WRTFLG,X'03'       YES, BEING COPIED?            -ARB82-
         BNE   PROCESS2           NO.                           -ARB82-
         CLI   RECBUF+1,C'V'      IS THIS A VOLUME LABEL?       -ARB82-
         BNE   NOUVLCPY           NO.                           -ARB82-
         TM    COPYFLG,X'04'      VOLUME LABELS TO BE COPIED?   -ARB82-
         BZ    SUPVCOPY           NO, SUPPRESS COPY             -ARB82-
NOUVLCPY CLC   ASCIIN,ASCIOUT     CONVERTING TO EBCDIC?         -ARB82-
         BE    PROCESS2           NO.                           -ARB82-
         MVI   PHFLSTAT+#AXLB,X'FF'    YES, GENERATE NOTE       -ARB82-
SUPVCOPY OI    WRTFLG,X'04'       DON'T COPY THIS RECORD        -ARB82-
         B     PROCESS2           REJOIN MAINLINE               -ARB82-
         SPACE
ANSUVL   CLI   SUMFLG,C'N'        NOSUM OPTION?                 -ARB82-
         BE    COUNTVLS           YES, DON'T LIST LABEL         -ARB82-
         MVC   MSGBUF,BLANKBUF    NO, LIST ODD LABEL            -ARB82-
         MVC   MSGBUF+1(4),=C'USER'                             -ARB82-
         MVC   MSGBUF+5(13),=C' VOLUME LABEL:'                  -ARB82-
         MVC   MSGBUF+19(1),RECBUF+3   SAVE LABEL NO.           -ARB82-
         MVI   MSGBUF+20,C':'     PUT COLON AFTER               -ARB82-
         MVC   MSGBUF+23(90),RECBUF    COPY IN LABEL            -ARB82-
         MVI   MSGBUF,C'0'        FORCE DOUBLE SPACE            -ARB82-
         BAL   R2,PUTLINE3        LIST THE LABEL                -ARB82-
         DC    C'UVL111'                                        -ARB82-
         MVI   HEXOFF+1,22        LIST IN HEX IF HEX OPTION     -ARB82-
         MVI   LINESIZE+3,89                                    -ARB82-
         BAL   R5,HEXON           LIST IN HEX IF WANTED         -ARB82-
COUNTVLS CLC   VOL1NO,=H'1'       SEEN A VOL1 THIS FILE?        -ARB82-
         BL    ANSSQERR           NO, SEQUENCE ERROR            -ARB82-
         LR    R2,R4              LOOK AT BLOCK NUMBER          -ARB82-
         SH    R2,VOL1NO                                        -ARB82-
         CH    R2,=H'1'           SHOULD UVL LABEL BE HERE?     -ARB82-
         BNE   ANSSQERR           NO, ERROR                     -ARB82-
         STH   R4,VOL1NO          ELSE UPDATE NO. OF VOL LABELS -ARB82-
         B     ANSXWARN           GO WARN OF NON-STD LABEL      -ARB82-
         SPACE
ANSVOL1  CLI   ASCIIN,C'Y'        TRANSLATION BY PARM?          -ARB79-
         BE    NORETRAN           YES, DON'T TRANSLATE AGAIN    -ARB79-
         MVI   ASCIIN,C'Y'        REQUEST INPUT TRANSLATION     -ARB79-
         XLATE RECBUF,80,TO=E     TRANSLATE THE LABEL ITSELF    -ARB79-
NORETRAN CLI   RECBUF+10,C' '     CHECK FOR VOLUME SECURITY     -ARB79-
         BE    AVOLOK                                           -ARB79-
         BAL   R2,PUTLINE                                       -ARB79-
         MVC   MSGBUF(L'ASCURITY),ASCURITY                      -ARB79-
         B     EXITRC16                                         -ARB79-
AVOLOK   MVC   LABTYPE,=C'ANS'    INDICATE ANS LABELS           -ARB79-
         MVI   VOLLAB,C'A'                                      -ARB82-
         B     FIRSTVOL                                         -ARB79-
IBMVOL1  MVI   ASCIIN,C'N'        CANCEL ASCII TRANSLATION      -ARB79-
         MVI   VOLLAB,C'S'                                      -ARB82-
         MVC   RECBUF,ASCBUF      MAKE SURE RECBUF IN EBCDIC    -ARB79-
FIRSTVOL MVC   MSGBUF,BLANKBUF                                  -ARB79-
         MVC   MSGBUF(37),=C'0INPUT VOLUME HAS WHO STANDARD LABELS'    *
                                                                -ARB79-
         MVC   MSGBUF+18(3),LABTYPE                             -ARB79-
         LA    R2,*+2                                           -ARB79-
         B     PUTLINE3           IDENTIFY LABEL TYPE           -ARB79-
VOL1     DS    0H
         MVC   MSGBUF,BLANKBUF                                  -ARB79-
         LA    R0,6      PREPARE TO RESERVE 6 LINES OF PRINT
         CLI   SUMFLG,C'F'        FULL SUMMARY?                 -ARB79-
         BNE   VOL1NLST                                         -ARB79-
         BAL   R2,PAGECHK         AND RESERVE THEM
         MVC   MSGBUF+4(14),=C' VOLUME LABEL:'                  -ARB79-
         MVC   MSGBUF+1(3),LABTYPE     SAY WHOSE LABEL          -ARB79-
         MVC   MSGBUF+22(90),RECBUF                             -ARB79-
         MVI   MSGBUF,C'0'
         BAL   R2,PUTLINE3
         DC    C'VOL111'
         MVI   HEXOFF+1,21        PUT HEX IN RIGHT PLACE        -ARB79-
         MVI   LINESIZE+3,89      AND FOR RIGHT LENGTH          -ARB79-
         BAL   R5,HEXON
VOL1NLST C     R7,=F'80'          NORMAL LENGTH LABEL?          -ARB79-
         BE    VOL180                                           -ARB79-
         BAL   R2,PUTLINE         IF NOT, SAY SOMETHING         -ARB79-
         MVC   MSGBUF(L'LABLSIZE),LABLSIZE                      -ARB79-
VOL180   MVC   MSGBUF+1(132),BLANKBUF                           -ARB79-
         MVI   MSGBUF,C'0'                                      -ARB79-
         MVC   MSGBUF+6(21),=C'VOLUME SERIAL NUMBER='
         MVC   MSGBUF+27(6),RECBUF+4
         MVC   MSGBUF+40(19),=C'OWNER INFORMATION='''
         CLI   LABTYPE,C'A'       IS THIS AN ANS LABEL?         -ARB79-
         BE    ANSOWNER           YES, OWNER ID IS LONGER       -ARB79-
         MVC   MSGBUF+59(10),RECBUF+41
         MVC   OWNERSAV(10),RECBUF+41  SAVE OWNER FOR VTOC      -ARB82-
         MVI   MSGBUF+69,C''''
         B     PUTVOL1                                          -ARB79-
ANSOWNER MVC   MSGBUF+59(14),RECBUF+37                          -ARB79-
         MVC   OWNERSAV(14),RECBUF+41  SAVE OWNER FOR VTOC      -ARB82-
         MVI   MSGBUF+73,C''''                                  -ARB79-
PUTVOL1  BAL   R2,PUTLINE3                                      -ARB79-
         DC    C'VOL222'
         CLI   VERVOL,C'Y'        IS THIS THE FIRST RECORD?     -ARB82-
         BNE   NOTVER             NO, OUT OF PLACE VOL1         -ARB82-
         MVC   VSNSAVE,RECBUF+4   SAVE VSN FOR USE IN VTOC LISTING-CWB-
         MVC   DDVOL,VSNSAVE      MAKE TITLE VOLSER CORRECT     -ARB82-
         MVI   VERVOL,C'H'        YES, EXPECT HDR1 NEXT         -ARB82-
         TM    JFCLTSV,X'42'      DID WE EXPECT LABELLED INPUT? -ARB82-
         BNZ   NOMIX              YES.                          -ARB82-
         TM    WRTFLG,X'01'       NO, ARE WE COPYING?           -ARB82-
         BZ    NOMIX              NO, NO ONE CARES              -ARB82-
         CLI   SVOUTFLG,C'N'      YES, ARE WE UNLABELLING?      -ARB83-
         BE    NOMIX              YES, LET IT GO                -ARB82-
         BAL   R2,PUTLINE         NO, STOP IT NOW               -ARB82-
         MVC   MSGBUF(53),=C'0COPY OF LABELLED TAPE TO UNLABELLED TAPE *
               NOT ALLOWED'                                     -ARB82-
         BAL   R2,PUTLINE                                       -ARB82-
         MVC   MSGBUF(59),=C' SPECIFY THE ''UNLABEL'' PARAMETER IF THIS*
                IS TO BE PERMITTED'                             -ARB82-
         B     EXITRC16           NOW GIVE UP                   -ARB82-
NOMIX    CLI   COUNTFLG,C' '      IS COUNT OPTION SET YET?      -ARB82-
         BNE   SERCHK             YES.                          -ARB82-
         MVI   COUNTFLG,C'N'      NO, DEFAULT NOCOUNT FOR SL/AL -ARB82-
SERCHK   CLC   JFCBIN+JFCBVOLS(6),RECBUF+4 SEE IF VOLSER IN JCL -ARB82-
         BE    SAMESER            BIF SAME                      -ARB82-
         BAL   R2,PUTLINE                                       -ARB82-
         MVC   MSGBUF(098),=C'0WARNING - VOLUME SERIAL NUMBER IN ABOVE *
               LABEL DOES NOT MATCH THAT SPECIFIED ON INPUT DD STATEMEN*
               T'                                               -ARB82-
SAMESER  MVC   VOL1NO,=H'1'       NOTE VOL1 IN RIGHT PLACE      -ARB82-
         MVI   GOODLAB,C'Y'       NOTE WE HAVE A KOSHER LABEL   -ARB82-
         TM    WRTFLG,X'01'       OUTPUT TO OCCUR?              -ARB82-
         BZ    NOTVER             NO, SAY NOTHING               -ARB82-
         CLC   ASCIIN,ASCIOUT     DIFFERENT INPUT & OUTPUT?     -ARB79-
         BE    NASCWRN2                                         -ARB79-
         BAL   R2,PUTLINE         YES, GIVE WARNING             -ARB79-
         MVC   MSGBUF(L'ASCWARN),ASCWARN                        -ARB79-
NASCWRN2 TM    COPYFLG,X'04'      SHOULD VOL1 BE COPIED?        -ARB82-
         BNZ   COPYVOL1           YES (UNLABEL AND OFILE=1)     -ARB82-
         OI    WRTFLG,X'04'       NO, SUPPRESS THE COPY         -ARB82-
         B     NOTVER                                           -ARB82-
COPYVOL1 TM    OUTLTYP,X'42'      OUTPUT PREVIOUSLY LABELLED?   -ARB83-
         BNZ   NOLABCH            YES.                          -ARB83-
         MVI   OUTLTYP,X'02'      NO, MARK IT SL                -ARB83-
         CLI   ASCIOUT,C'Y'       IS OUTPUT ASCII?              -ARB83-
         BNE   NOLABCH            NO.                           -ARB83-
         MVI   OUTLTYP,X'40'      IF SO, MAKE THAT AL           -ARB83-
NOLABCH  TM    WRTFLG,X'02'       SKIPPING? (IFILE > 1)?        -ARB82-
         BNZ   PROCESS2           NO, JUST GO COPY              -ARB82-
         LA    R10,ENDVOLVR       YES, COPY VOL1 TO OUTPUT      -ARB82-
         STH   R7,WRTCMND+6       STORE LENGTH FOR WRITE        -ARB82-
         B     COPYIT             ANYWAY (SINCE UNLABEL)        -ARB82-
NOTVER   TM    WRTFLG,X'02'       ARE WE STILL SKIPPING?        -ARB82-
         BNZ   PROCESS2           NO, RETURN TO MAIN LOOP       -ARB79-
         B     ENDVOLVR           YES, SKIP SOME MORE           -ARB82-
         SPACE 5
EODS     DS    0H
         CLI   VERVOL,C'Y'        TM INSTEAD OF VOL LABEL?      -ARB82-
         BE    NOVOL1             YES, KNOW TAPE IS NL          -ARB82-
         CLI   VERVOL,C'H'        HDR1 EXPECTED?                -ARB82-
         BE    NOHDR1             YES, NOTE NOT FOUND           -ARB82-
         MVI   VERVOL,C'N'        NO VOLUME VERIFY AFTER TM     -ARB82-
EONLVER  XC    VOL1NO,VOL1NO      NO GOOD VOL1'S AFTER TM       -ARB82-
         AP    MARKNO,=P'1'       COUNT THE TAPEMARK
         UNPK  MARKNUM,MARKNO
         OI    MARKNUM+3,X'F0'
         CLC   POSTHEAD,=X'FFFE'  FOUND HDR BUT NO TRLR?        -ARB80-
         BNL   NOCTHEAD           NO, DON'T COUNT               -ARB80-
         LA    R1,1               YES, ADD 1 TO COUNT           -ARB80-
         AH    R1,POSTHEAD                                      -ARB80-
         STH   R1,POSTHEAD                                      -ARB80-
NOCTHEAD LA    R1,1                                             -ARB80-
         A     R1,CTPMKNO         CURRENT NUMBER OF TAPEMARKS PASSED
         ST    R1,CTPMKNO         IS NOW UPDATED TO ACTUAL VALUE
         TM    OPTNO+3,X'01'      SEE IF TAPEMARK LOGGING SPECIFIED
         BZ    TPMKLOGD           BIF TAPEMARK LOGGING NOT SPECIFIED
         MVC   TPMKLOG+17(4),MARKNUM
TPMKLOG  WTL   'TAPEMARK XXXX ENCOUNTERED'
TPMKLOGD DS    0H
         L     R1,BLKCNT          BLKS IN PREVIOUS DATASETS
         AR    R1,R4              ADD NO. OF BLKS IN DATASET JUST READ
         ST    R1,BLKCNT          TO GET TOTAL BLKS READ SO FAR (EXCEPT*
                                  FOR SKIPPING - SKIPEOV OR SKIPTM).
         CLI   WRTFLG,X'03'       SEE IF COPY BEING DONE NOW
         BNE   SKIPWTM            SKIP WRITING TAPEMARK IF NOT  -ARB82-
         CLI   OKCODE+3,12        I/O ERR TERMINATION?          -ARB82-
         BE    BUMPOUTM           YES, LET CLOSE WRITE TM       -ARB82-
         LA    R1,=AL3(WTMCMND)   PTR TO PTR TO WTM CCW FOR OUTPUT TAPE
         LA    R2,OUTPUT          DCB FOR WTM EXCP OUTPUT
         ST    R4,SVR4            SAVE R4 FOR EXEC EXCP CALL
         BAL   R4,EXECEXCP        CALL SUBROUTINE TO ISSUE EXCP
         L     R4,SVR4            RESTORE R4
BUMPOUTM LH    R1,COUTFILE        LOAD OUTPUT TM NUMBER         -ARB82-
         LA    R1,1(,R1)          BUMP FOR NEXT FILE            -ARB82-
         STH   R1,COUTFILE        AND STORE                     -ARB82-
SKIPWTM  CLI   PREVLAB,C'H'       PREVIOUS FILE A HEADER?       -ARB82-
         BE    SLFILE             YES.                          -ARB82-
         LTR   R4,R4              ANY RECORDS READ?             -ARB82-
         BNZ   NOTLTM             YES, NOT LEADING TAPE MARK    -ARB82-
         CLI   MARK,X'FF'         IS THIS AN EOV?               -ARB82-
         BE    NONLFILE           YES, SKIP VTOC FOR NOW        -ARB82-
NOTLTM   CLI   GOODLAB,C'N'       NO, IS THIS FILE LABELS?      -ARB82-
         BNE   NONLFILE           YES, NO VTOC ENTRY            -ARB82-
         CLI   SKIPFLG,X'FF'      STILL BUSY SKIPPING?          -ARB82-
         BNE   NONLFILE           YES, DON'T START A VTOC       -ARB82-
         CLI   SUMFLG,C'N'        NOSUM OPTION?                 -ARB82-
         BE    NONLFILE           YES, NO VTOC EITHER           -ARB82-
         BAL   R2,GETVTOC         NO, GET A VTOC BLOCK          -ARB82-
         CLC   VOLLAB(3),=C'BLP'  BLP ALREADY SET?              -ARB82-
         BE    NONLBLP            YES.                          -ARB82-
         CLC   VOLLAB(2),=C'NL'   NO, SHOULD THIS BE NL?        -ARB82-
         BE    NONLBLP            YES.                          -ARB82-
         MVC   VOLLAB,=C'BLP'     NO, MAKE IT BLP               -ARB82-
         MVI   PHFLSTAT+#BLP,X'FF'     FORCE VTOC NOTE          -ARB82-
NONLBLP  MVC   VTOCLTYP,VOLLAB    SHOW LABEL TYPE               -ARB82-
         MVC   VTOCDSN(12),=C'  *NO LABEL*' EYE-CATCHING DSN    -ARB82-
         CLI   COUNTFLG,C'N'      NOCOUNT OPERATION?            -ARB82-
         BE    VTOCTMNO           YES, CAN'T DISTINGUISH T/M    -ARB82-
         LTR   R4,R4              IS THIS JUST A TAPE MARK?     -ARB82-
         BNZ   VTOCTMNO           YES.                          -ARB82-
         MVC   VTOCDSN+3(8),=C'TAPEMARK'   YES, RELABEL         -ARB82-
         B     VTOCTMNO                                         -ARB82-
SLFILE   CLC   VOLLAB,=C'BLP'     ARE LABELS PERVERSE?          -ARB82-
         BNE   NONLFILE           NO, CONTINUE                  -ARB82-
*                                 ELSE REPLACE FILE SEQ. WITH          *
                                  TAPE MARK NUMBER              -ARB82-
VTOCTMNO CLI   OKCODE+3,12        STOPPING FOR I/O ERRORS?      -ARB82-
         BE    NOVTOCTM           YES, NO TAPE MARK NUMBER      -ARB82-
         MVC   VTOCSEQN,MARKNUM   TM NUMBER AS FILE NUMBER      -ARB82-
NOVTOCTM TM    WRTFLG,X'01'       WAS IT COPIED?                -ARB82-
         BZ    NONLFILE           NO.                           -ARB82-
         LH    R1,COUTFILE        GET OUTPUT FILE NUMBER        -ARB82-
         BCTR  R1,0               CORRECT FOR INCREMENT         -ARB82-
         CVD   R1,BADLNGTH                                      -ARB82-
         OI    BADLNGTH+7,X'0F'                                 -ARB82-
         UNPK  VTOCOUSQ,BADLNGTH       FORMAT INTO VTOC         -ARB82-
NONLFILE OC    FILESTAT(12),PHFLSTAT   PICK UP ERRORS FOR ENDED FILE   *
                                                                -ARB82-
         XC    PHFLSTAT(12),PHFLSTAT                            -ARB82-
         CLI   COUNTFLG,C'N'
         BE    SIMPLETM           BIF COUNT OPTION NOT SPECIFIED
         L     R0,MAX
         LTR   R0,R0
         BZ    SIMPLETM           BIF NO COUNTING WORK HAS BEEN DONE
         LTR   R4,R4              BYPASS MAX/MIN/AVG IF           -CWB-
         BZ    SIMPLETM                NO RECORDS.                -CWB-
         L     R1,MIN
         CVD   R1,BADLNGTH
         OI    BADLNGTH+7,X'0F'
         UNPK  MINMSG,BADLNGTH
         L     R1,MAX
         CVD   R1,BADLNGTH
         OI    BADLNGTH+7,X'0F'
         UNPK  MAXMSG,BADLNGTH
         LR    R1,R4              COPY COUNT OF RECORDS AND DIVIDE-CWB-
         SRA   R1,1                    BY 2 TO ROUND THE AVERAGE. -CWB-
         A     R1,FILEBYTS        ADD NUMBER OF BYTES IN FILE.    -CWB-
         SR    R0,R0              CLEAR R0 FOR DIVIDE.            -CWB-
         ST    R0,FILEBYTS        ALSO CLEAR FILEBYTS FOR NEXT    -CWB-
*                                      FILE.                      -CWB-
         DR    R0,R4              DIVIDE FOR AVERAGE BLOCK SIZE.  -CWB-
         CVD   R1,BADLNGTH        MAKE AVERAGE SIZE PRINTABLE.    -CWB-
         OI    BADLNGTH+7,X'0F'                                   -CWB-
         UNPK  AVGMSG,BADLNGTH                                    -CWB-
         CVD   R4,BADLNGTH
         OI    BADLNGTH+7,X'0F'
         UNPK  COUNTMSG,BADLNGTH
         LTR   R3,R3              DO WE HAVE A VTOC ENTRY?        -CWB-
         BZ    NOSAVE             IF NOT, BRANCH.                 -CWB-
         CLI   GOODLAB,C'N'       IS THIS A LABEL FILE?         -ARB82-
         BNE   NOSAVE             IF SO, DON'T SAVE STAT'S.     -ARB82-
         MVC   VTOCMINB,MINMSG    SAVE MIN, MAX, AVG, AND COUNT   -CWB-
         MVC   VTOCMAXB,MAXMSG         FOR VTOC LISTING.          -CWB-
         MVC   VTOCAVGB,AVGMSG                                    -CWB-
         MVC   VTOCOUNT,COUNTMSG                                  -CWB-
NOSAVE   EQU   *                                                  -CWB-
         MVI   EOVM+1,CNTSEND-TPMKMSG-1  SET LENGTH FOR MVC.      -CWB-
         MVC   MSGBUF,BLANKBUF    CLEAR BUFFER COMPLETELY.        -CWB-
         MVC   MSGBUF+18(CNTSEND-EOVNUM-3),EOVNUM+3 COPY MESG.    -CWB-
         LA    R5,PUTLINE2        SPECIFY LONG TAPEMARK MSG
         CLI   OKCODE+3,12        I/O ERROR TERMINATION?        -ARB79-
         BE    LINEOR2            YES, GO PUT OUT COUNTS        -ARB79-
         B     TMSGDONE
SIMPLETM CLI   OKCODE+3,12        FINISHING AFTER ERROR?        -ARB79-
         BE    PRENDMSG           YES, NO MESSAGE NEEDED        -ARB79-
         MVI   EOVM+1,33          SET LENGTH FOR POSSIBLE SHORT EOV MSG
         LA    R5,PUTLINE         SPECIFY SHORT TPMK FOUND MSG IF ANY
TMSGDONE TM    EOV1FLG,X'03'      SEE IF ANY EOV'S PENDING      -ARB82-
         BNZ   EOVPROC            BIF ANY EOV'S PENDING         -ARB82-
         CLI   MARK,X'FF'         TWO CONSECUTIVE MARKS?          -CWB-
         BNE   SKPCHK             IF NOT, BRANCH                  -CWB-
         CLI   PREVLAB,C'H'       PREV FILE A HDR1 LABEL?       -ARB82-
         BNE   EOVPROC            IF NOT, GO DO EOV.            -ARB82-
SKPCHK   MVI   MARK,X'FF'         NOTE THIS MARK FOUND.           -CWB-
         CLC   CEOVNO,SKPEOVNO    SKIP SKIPTM PROCESSING IF
         BL    SKIPTMEV           SKIPEOV IS BEING DONE.        -ARB79-
         CLC   CTPMKNO,SKIPFLNO   COMPARE CTPMKNO WITH SKIPFLNO -ARB79-
         BL    SKIPTMPR           BRANCH TO DO POSSIBLE SKIPTM PROCESS
         BH    LINEOR2            BIF NOT JUST DONE SKIPPING    -ARB79-
         TM    SKIPFLG,X'80'      DOING LABEL POSITIONING?      -ARB79-
         BNZ   LINEOR2            NO, REALLY THRU               -ARB79-
         OC    SKIPTMNO,SKIPTMNO  YES, SKIPTM ALSO PRESENT?     -ARB79-
         BNZ   TMDONE             YES, DON'T PRINT TM NOTICE    -ARB79-
         OC    SKPEOVNO,SKPEOVNO  ANY EOVS TO BE SKIPPED?       -ARB79-
         BNZ   TMDONE             YES, NO MESSAGE NOW           -ARB79-
LINEOR2  BALR  R2,R5              TO PUTLINE OR PUTLINE2
         MVC   MSGBUF(18),TPMKMSG
         CLI   OKCODE+3,12        FORCED END FOR I/O ERR?       -ARB79-
         BE    TPMKLIM            YES, ALLOW NO ESCAPE          -ARB82-
         CLC   CTPMKNO,SKIPFLNO   JUST FINISHED SKIPPING?       -ARB79-
         BE    TMDONE             YES, AVOID MAX CHECK          -ARB79-
         CLC   CTPMKNO,MAXTMNO
         BL    PROCESS
FORCEND  CLI   WRTFLG,X'03'       CHECK IF COPY BEING DONE      -ARB79-
         BNE   *+16               SKIP IF IT'S NOT BEING DONE
         LA    R1,=AL3(WTMCMND)   OTHERWISE WRITE AN EXTRA TAPEMARK,
         LA    R2,OUTPUT          JUST TO MAKE SURE.  THIS COULD
         ST    R4,SVR4            SAVE R4 OVER CALL             -ARB84-
         BAL   R4,EXECEXCP        RESULT  IN 3 TAPEMARKS IN LAST EOV.
         L     R4,SVR4            RESTORE R4                    -ARB84-
         B     TPMKLIM                                          -ARB82-
         SPACE 5
EOVPROC  DS    0H                 HANDLES END-OF-VOLUME INDICATIONS
         MVI   MARK,X'00'         RESET 1 TPMK INDICATOR        -ARB79-
         MVC   EOV1FLG+1(1),EOV1FLG SAVE FOR POSSIBLE DECISION ON      *
                                  HDR1000 . . . TERMINATION MSG.
         NI    EOV1FLG,X'FC'      CLEAR 'EOV PENDING' FLAG(S) IF ANY
         AP    EOVNO,=P'1'        COUNT EOV'S
         UNPK  EOVNUM,EOVNO
         OI    EOVNUM+2,X'F0'
         TM    OPTNO+3,X'01'      WTL OPTION?                   -ARB79-
         BZ    SKIPLOG                                          -ARB79-
         MVC   WRITELOG+11(3),EOVNUM
WRITELOG WTL   'EOV000 ENCOUNTERED'
SKIPLOG  MVC   OLDSUP,SUPOUT      SAVE SUPPRESS FLAG            -ARB82-
         MVI   SUPOUT,C'N'        FORCE OUT EOV MESSAGES        -ARB82-
         BAL   R2,PUTLINE                                       -ARB82-
EOVM     MVC   MSGBUF(00),TPMKMSG LENGTH SPECIFIED IN EODS EXIT
         MVC   SUPOUT,OLDSUP      RESTORE SUPPRESS LIST FLAG    -ARB82-
         TM    SKIPFLG,X'80'      DOING LABEL POSITIONING?      -ARB79-
         BZ    SKIPTMPR           DON'T COUNT EOVS IF SO        -ARB79-
         LA    R1,1
         A     R1,CEOVNO          COUNTS EOV INDICATIONS, INITIALLY=0
         ST    R1,CEOVNO
         C     R1,SKPEOVNO        SEE IF EOV SKIPPING           -ARB79-
         BL    SKIPTMEV                                         -ARB79-
         BE    EOVDONE                                          -ARB79-
         C     R1,MAXEOVNO        SEE IF MAXEOV'S REACHED
         BNL   PRENDMSG           EXIT IF MAX EOV'S REACHED
         TM    WRTFLG,X'02'       ALL SKIPPING DONE?            -ARB79-
         BZ    SKIPTMPR           NO MAX CHECK IF NOT           -ARB79-
         CLC   CTPMKNO,MAXTMNO    CHECK MAX TPMKS JUST IN CASE
         BNL   TPMKLIM            QUIT IF REACHED MAX           -ARB82-
         CLI   SUMFLG,C'N'        NOSUM REQUESTED? (WHY?)       -ARB82-
         MVI   LABELID,C'N'       FORGET LAST LABELS            -ARB82-
         MVC   POSTHEAD,=X'FFFE'  FORGET PREVIOUS HEADER        -ARB83-
         BE    PROCESS            SKIP VTOC IF SO               -ARB82-
         BAL   R2,GETVTOC         GET VTOC ENTRY FOR EOV        -ARB82-
         MVC   VTOCSEQN,=C'----'  INDICATE SPECIAL LINE         -ARB82-
         MVC   VTOCDSN(13),=C'END OF VOLUME'                    -ARB82-
         MVC   VTOCDSN+14(3),EOVNUM   STORE EOV NUMBER          -ARB82-
         CLC   VOLLAB(2),=C'NL'   TAPE GOOD NL?                 -ARB82-
         BE    PROCESS            YES.                          -ARB82-
         MVC   VOLLAB,=C'BLP'     NO, MAKE IT BLP AFTER EOV     -ARB82-
         B     PROCESS            CONTINUE IF MORE TM'S TO COME -ARB82-
TPMKLIM  MVI   TMQUIT,X'FF'       NOTE STOPTM CUTOFF            -ARB82-
         SPACE 5
PRENDMSG DS    0H
         BALR  R12,0              SET UP FINALE BASE REG        -ARB82-
         USING *,R12              FOR BETTER ADDRESSABILITY     -ARB82-
         B     FINALE             SKIP TO FINAL PROCESSING      -ARB82-
         EJECT
CEOVNO   DC    F'0'               # OF DOUBLE TAPEMARKS PASSED
SVR4     DC    F'0'               FOR SAVING R4 TEMPORARILY
BLKCNT   DC    F'0'               KEEPS TRACK OF TOTAL BLKS READ ON TAPE
                                  EXCLUDING THOSE READ DURING SKIPPING.
MAX      DC    F'0'               KEEPS TRACK OF MAXIMUM BLK LENGTH
MIN      DC    F'32767'           KEEPS TRACK OF MINIMUM BLK LENGTH
VOL1NO   DC    H'0'               NO. OF VOL1'S IN THIS FILE    -ARB82-
TESTDEN  CLI   RECBUF+15,C'0'     EX'D TO TEST LABEL DENSITY    -ARB82-
STOREDEN MVI   RECBUF+15,C'0'     EX'D TO PUT DENSITY IN LABEL  -ARB79-
POSTHEAD DC    X'FFFF'            NUMBER OF FILES AFTER HDR            *
                                  FFFF = NO HDR FOUND YET              *
                                  FFFE = HDR MATCHED BY TRLR    -ARB80-
MARKNO   DC    PL3'0'
FILENO   DS    PL4                                              -ARB79-
HEADNO   DC    PL3'-1'            HEADER LABEL COUNTER          -ARB80-
EOVNO    DC    PL2'0'
TPMKMSG  DC    C'0TAPEMARK NO. '
MARKNUM  DC    C'    '
         DC    C' -- EOV NO. '
EOVNUM   DC    CL3'000'
         DC    C'    BLOCK LENGTHS:  MINIMUM='                  -ARB82-
MINMSG   DC    C'00000'
         DC    C'  MAXIMUM='                                    -ARB82-
MAXMSG   DC    C'00000'
         DC    C'  AVERAGE='                                    -ARB82-
AVGMSG   DC    C'00000'                                           -CWB-
         DC    C'    NUMBER OF BLOCKS='
COUNTMSG DC    C'000000'
CNTSEND  EQU   *                                                  -CWB-
DATASEQ  DC    CL4'NONE'
LBL2MSG  DC    C'           RECFM='                             -ARB79-
RECFM    DC    CL18'          BLKSIZE='
BLKSIZE  DC    CL18'XXXXX       LRECL='
LRECL    DC    CL20'XXXXX       TRTCH='                         -ARB79-
TRTCH    DC    CL16' '
BUFOFFSP DC    CL14' '                                          -ARB79-
BUFOFF   DC    CL2'  '                                          -ARB79-
CRMSG    DC    C'CREATED BY JOB          IN STEP         '
TRTCHS   DC    C'E ETC T '        TRTCH CODE VECTOR             -ARB79-
LABTYPE  DC    C'IBM'             OR ANS IF TRANSLATING         -ARB79-
VERVOL   DC    C'Y'               VOLUME VERIFICATION SWITCH    -ARB79-
HDRFOUND DC    C'N'               HDR SEQ NO PROCESSED          -ARB79-
GOODLAB  DC    C'N'               LABEL DATA/TRUE LABEL FLAG           *
                                  Y=VALID LABEL FILE, N=DATA FILE      *
                                  ?=PERVERSE LABEL FILE         -ARB82-
MARK     DC    X'F0'              'TAPEMARK JUST READ' FLAG (00 = NOT)
TMQUIT   DC    X'00'              MAXTM TERMINATION FLAG        -ARB82-
EOV1FLG  DC    XL2'0000'          X'00'=EOV NOT PENDING, X'01'=EOV1    *
                                  TYPE OF EOV PENDING, X'02' BIT=      *
                                  'HDR10000 . . . ' IN 1ST FILE TYPE.
*                                 AND HDR2 RECORD DOES NOT FOLLOW IT.
LABELIKE DC    C'Y'               ALL RECORDS OF THIS FILE             *
                                  LOOK LIKE LABELS              -ARB82-
LABELID  DC    CL2'E?'            LABEL ID CHARS (HH OR ET)     -ARB82-
*                                      PROCESSING A LABEL FILE.   -CWB-
PREVLAB  DC    C'N'               PREVIOUS LABEL FLAG CHARACTER        *
                                  N=NOT, H=HEADER, E=TRAILER    -ARB82-
HDRMISS  DC    C'N'               'Y' IF NO HDR FOR TRLR        -ARB82-
         DS    0A                                               -ARB82-
FRSTVTOC DC    AL1(VTOCEPB),AL3(0)  ADDRESS OF FIRST VTOC BLOCK;  -CWB-
*                                      HIGH-ORDER BYTE SET TO     -CWB-
*                                      FORCE FIRST GETMAIN.       -CWB-
FILEBYTS DC    F'0'               NUMBER OF BYTES IN THIS FILE.   -CWB-
VTOCHEAD DC    C'0VOLUME TABLE OF CONTENTS FOR '                -ARB82-
VSNSAVE  DC    C'VSNVSN',C'    '                                  -CWB-
SAVETRK  DC    C'9 TRACK    '                                     -CWB-
OWNERSAV DC    CL14'          '                                 -ARB82-
         DC    C'   DENSITY='                                   -ARB82-
VTOCDEN  DC    C' 800 BPI, TRTCH='                              -ARB82-
VTOCTRTC DC    CL3' '                                           -ARB82-
         DC    C'   LABEL='                                     -ARB82-
VOLLAB   DC    C'NL '                                           -ARB82-
VHEADLEN EQU   *-VTOCHEAD                                         -CWB-
         EJECT
FINALE   MVI   SUPOUT,C'N'        ALWAYS PRINT FINAL MSGS       -ARB82-
         LTR   R3,R3              HAVE WE A VTOC?               -ARB82-
         BZ    NOFINVTC           NO.                           -ARB82-
         MVI   VNOMORE,C'Y'       PREVENT ADDITIONS TO VTOC     -ARB82-
         BAL   R2,GETVTOC         CALL VTOC ROUTINE TO UPDATE STATUS   *
                                  FOR LAST FILE                 -ARB82-
NOFINVTC TM    EOV1FLG+1,X'02'                                  -ARB82-
         BZ    *+14               SKIP IF NO 'HDR1000 . . . ' EOV
         BAL   R2,PUTLINE
         MVC   MSGBUF(114),=C'0TAPESCAN TERMINATING BECAUSE TAPE APPARE*
               NTLY ONLY INITIALIZED, NOT USED.  TO SCAN FURTHER USE OP*
               TIONS=''STOPEOV=2'''                             -ARB82-
         CLI   COUNTFLG,C'N'
         BE    SHORT              SKIP TO NOT PRINT COUNTS
         LA    R0,6               PREPARE TO RESERVE 6 LINES FOR PRINT
         BAL   R2,PAGECHK         RESERVE THEM
         LA    R1,=AL3(SENSCMND)  RE-SENSE AT END OF PROCESSING -ARB82-
         LA    R2,INPUT           PTR TO DCB FOR EXCP CALL      -ARB82-
         ST    R4,SVR4            SAVE R4 OVER CALL             -ARB84-
         BAL   R4,EXECEXCP                                      -ARB82-
         L     R4,SVR4            RESTORE R4                    -ARB84-
         TM    SENSBYTS+1,X'10'   CHECK IF 7-TRK, 1 BIT = 7-TRK
         BO    SEVENTRK
         MVI   INDEX+3,4          SINCE 9-TRK, LOAD OFFSET FOR NO CONV
         TM    SENSBYTS+3,X'04'   CHECK IF PE, 1 = PE = 1600 BPI
         BNO   EIGHTBPI           BIF 800 BPI 9-TRK
         MVI   DCBDEN+INPUT,X'C3' SET DEN=1600 BPI INDICATOR
         B     ADDDEN
EIGHTBPI DS    0H
         TM    SENSBYTS+5,X'C0'   6250 POSSIBLE?                -ARB79-
         BZ    NOT62              NO.                           -ARB79-
         TM    SENSBYTS+6,X'08'                                 -ARB79-
         BNZ   YES62                                            -ARB79-
NOT62    MVI   INPUT+DCBDEN,X'83' SET DEN=800 BPI INDICATOR     -ARB79-
         B     ADDDEN
YES62    MVI   INPUT+DCBDEN,X'D3' INDICATE 6250                 -ARB79-
         LA    R2,4                                             -ARB79-
         B     ADDDEN2                                          -ARB79-
SEVENTRK DS    0H
         MVI   INDEX+3,40         LOAD OFFSET FOR 7-TRK         -ARB79-
         LA    R0,3900            LOAD TAPEMARK LENGTH FOR 7-TRK
         STH   R0,TMLENGTH
         MVC   LNGTHEST+62(14),=C'C (CONVERSION)'
         CLI   INPUT+DCBTRTCH,X'13' SEE IF CONVERSION SPECIFIED
         BE    ADDDEN             BIF IT IS, OFFSET=0 FOR CONV
         MVI   INDEX+3,44         SET OFFSET FOR 7-TRK, NO CONV -ARB79-
         MVC   LNGTHEST+62(19),=C'7-TRK NO CONVERSION'
ADDDEN   DS    0H
         SR    R2,R2
         IC    R2,INPUT+DCBDEN
         SRL   R2,6               SHIFT INTO RIGHT-HAND 2 BITS
         LTR   R2,R2              SEE IF DEN=0 SPECIFIED
         BNE   ADDDEN2            BIF DEN=0 NOT SPECIFIED
         L     R2,DCBDEBAD+INPUT  GET PTR TO DEB
         L     R2,DEBUCBAD(R2)    GET PTR TO UCB
         TM    19(R2),X'03'       SEE IF IT'S A 3400 SERIES OR THE LIKE
         LA    R2,0               CLEAR R2 WITHOUT RESETTING CC
         BNO   ADDDEN2            BIF IT ISN'T, IT'S OK
         LA    R2,1               SET DENSITY USED FOR LENGTH ESTIMATE *
                                  TO 556 BPI BECAUSE 3400 SERIES       *
                                  DOESN'T SUPPORT 200 BPI (EXCEPT 3410)
ADDDEN2  DS    0H
         LA    R0,X'F0'           LOAD C'0' FOR OR OPERATION
         OR    R0,R2              GET PRINTABLE DENSITY CHARACTER
         STC   R0,LNGTHEST+50     STORE PRINTABLE DENSITY MSG
         SLL   R2,3               MULTIPLY BY 8 TO GET DEN OFFSET
         A     R2,INDEX           ADD PREVIOUS OFFSETS
         ST    R2,INDEX           STORE COMPLETED OFFSET FOR BPIBGTBL
* FORMULA FOR LENGTH IN INCHES FOLLOWS:
* INCHES=R8/BPI+(IBG*BLKCNT+MAX(0,CTPMKNO-SKIPTMNO)*TMLENGTH)/1000
         L     R15,CTPMKNO        LOAD NUMBER OF TAPEMARKS READ
         S     R15,SKIPTMNO       SUBTRACT NUMBER OF TAPEMARKS SKIPPED
         BP    *+6                CHANGE POSSIBLE NEGATIVES TO ZERO
         SR    R15,R15            CHANGE POSSIBLE NEGATIVES TO ZERO
         MH    R15,TMLENGTH       MULTIPLY BY (TAPEMARK LENGTH*1000)
         L     R2,INDEX           LOAD INDEX INTO BPI/IBG TABLE
         L     R1,BLKCNT          LOAD BLOCK COUNT
         MH    R1,BPIBGTBL-20(R2) MULTIPLY TO GET TOTAL GAP LENGTH*1000
         AR    R1,R15             GET TOTAL GAP + TAPEMARK LENGTH*1000
         SR    R0,R0
         D     R0,=F'1000'        GET TOTAL TAPEMARK+ GAP LENGTH INCHES
         LR    R15,R1             SAVE THIS FOR LATER
         LH    R2,BPIBGTBL-18(R2) LOAD PHYSICAL BPI (NOT LOGICAL BPI)
         LR    R1,R8              LOAD TOTAL BYTE COUNT
         SR    R0,R0
         DR    R0,R2              DIVIDE BYTE COUNT BY PHYSICAL BPI
         AR    R1,R15             GET TOTAL LENGTH IN INCHES
         SR    R0,R0
         D     R0,=F'12'          GET FEET IN R1, INCHES IN R0
         CVD   R1,BADLNGTH
         OI    BADLNGTH+7,X'0F'
         UNPK  LNGTHEST+17(4),BADLNGTH
         CVD   R0,BADLNGTH
         OI    BADLNGTH+7,X'0F'
         UNPK  LNGTHEST+27(2),BADLNGTH
         BAL   R2,PUTLINE
         MVC   MSGBUF(81),LNGTHEST
         BAL   R2,PUTLINE
         MVC   MSGBUF(106),LNGTHACC
         MVI   SHORTNOW+1,102     LENGTH OF LONG MSG FOR COUNTS -ARB79-
         CVD   R8,BADLNGTH        TOTAL BYTES READ (FOR COUNT OPT ONLY)
         OI    BADLNGTH+7,X'0F'   SET SIGN NIBBLE
         UNPK  BYTES,BADLNGTH
         L     R8,BLKCNT          TOTAL BLKS READ ON TAPE, EXCLUDING   *
                                  THOSE READ DURING SKIP PROCESSING.
         CVD   R8,BADLNGTH
         OI    BADLNGTH+7,X'0F'
         UNPK  NBLKS,BADLNGTH
SHORT    BAL   R2,PUTLINE         PRINT 'SUCCESSFUL END' MSG
SHORTNOW MVC   MSGBUF(L'ENDMSG),ENDMSG (LENGTH MODIFIED FOR COUNTBLKS)
         L     R2,ERRCOUNT        COUNT OF SYNAD EXITS TAKEN
         LTR   R2,R2              SEE IF ZERO
         BZ    LISTVTOC           GO DO VTOC IF SO.               -CWB-
         CVD   R2,BADLNGTH        ELSE PRINT ERROR COUNT
         OI    BADLNGTH+7,X'0F'
         UNPK  ERRSUMSG+22(5),BADLNGTH
         BAL   R2,PUTLINE
         MVC   MSGBUF(L'ERRSUMSG),ERRSUMSG
         SPACE 3
LISTVTOC CLC   FRSTVTOC+1(3),=AL3(0)  DID WE MAKE A VTOC?         -CWB-
         BE    EXIT               IF NOT, SKIP IT.                -CWB-
         LA    R0,100             RESERVE 100 LINES (FORCE EJECT).-CWB-
         BAL   R2,PAGECHK                                         -CWB-
         MVC   VTOCDEN(4),DENMSG+1 COPY DENSITY TO VTOC HEAD    -ARB82-
         MVC   VTOCTRTC,=C'STD'   ASSUME STANDARD TRTCH         -ARB82-
         CLI   ITRTCH,0           IS THAT SO?                   -ARB82-
         BE    STDTRTCH           YES.                          -ARB82-
         CLI   ITRTCH,X'33'       CHECK 7 TRACK TRTCH=NULL      -ARB82-
         BE    STDTRTCH
         IC    R2,ITRTCH          ELSE CONVERT TO LETTER        -ARB82-
         SRL   R2,2                                             -ARB82-
         N     R2,=F'6'                                         -ARB82-
         LA    R2,TRTCHS(R2)      FIND STRING FORM OF TRTCH     -ARB82-
         MVC   VTOCTRTC(2),0(R2)  ADD TO HEADING                -ARB82-
         MVI   VTOCTRTC+2,C' '    REMOVE THIRD CHARACTER        -ARB82-
STDTRTCH BAL   R2,PUTLINE         OUTPUT VTOC HEADING.            -CWB-
         MVC   MSGBUF(VHEADLEN),VTOCHEAD                          -CWB-
         CLI   COUNTFLG,C'Y'      COUNTING BLOCKS?              -ARB82-
         BE    COUNTHED           YES, SET UP HEADINGS          -ARB82-
         MVC   VTOCHEDE(L'VTOCHEDH),VTOCHEDH                    -ARB82-
         B     PUTVTOCH                                         -ARB82-
COUNTHED MVC   VTOCHEDE(L'VTOCHEDC),VTOCHEDC                    -ARB82-
PUTVTOCH BAL   R2,PUTLINE         OUTPUT COLUMN HEADINGS.       -ARB82-
         MVC   MSGBUF(L'VTOCHED2),VTOCHED2                        -CWB-
         MVI   VTOCOUT,C'Y'       NOTE VTOC BEING WRITTEN       -ARB82-
         MVI   MSGBUF,C'0'        FORCE SPACE OF FIRST LINE     -ARB82-
         CLI   POSFLG,C'Y'        DID WE START AT THE FRONT?    -ARB82-
         BNE   NOPOSLIN           YES, NO "MAP STARTED" LINE    -ARB82-
         MVC   MSGBUF+1(132),BLANKBUF                           -ARB82-
         MVC   MSGBUF+1(8),=23C'-'     MSG LINE INDICATOR       -ARB82-
         MVC   MSGBUF+9(12),=C'MAP STARTS -'                    -ARB82-
         LA    R2,MSGBUF+22       MSG INSERTION POINT           -ARB82-
         CLC   INFLPOS,=H'1'      LABEL=1?                      -ARB82-
         BNH   NOPFILE            YES.                          -ARB82-
         MVC   0(6,R2),=C'LABEL='                               -ARB82-
         LH    R1,INFLPOS         CONVERT LABEL # TO DECIMAL    -ARB82-
         CVD   R1,BADLNGTH                                      -ARB82-
         OI    BADLNGTH+7,X'0F'                                 -ARB82-
         UNPK  6(4,R2),BADLNGTH                                 -ARB82-
         MVI   11(R2),C'+'        SEPARATE FROM OTHER WORDS     -ARB82-
         LA    R2,13(,R2)                                       -ARB82-
NOPFILE  CLC   SKPEOVNO,=F'0'     ANY EOVS SKIPPED?             -ARB82-
         BE    NOPEOV             NO.                           -ARB82-
         MVC   0(8,R2),=C'SKIPEOV='                             -ARB82-
         L     R1,SKPEOVNO        CONVERT EOVNUMB TO DECIMAL    -ARB82-
         CVD   R1,BADLNGTH                                      -ARB82-
         OI    BADLNGTH+7,X'0F'                                 -ARB82-
         UNPK  8(3,R2),BADLNGTH                                 -ARB82-
         MVI   12(R2),C'+'        SEPARATE FROM OTHER WORDS     -ARB82-
         LA    R2,14(,R2)                                       -ARB82-
NOPEOV   CLC   SKIPTMVL,=F'0'     ANY TAPE MARKS SKIPPED?       -ARB82-
         BE    NOPTM              NO.                           -ARB82-
         MVC   0(7,R2),=C'SKIPTM='                              -ARB82-
         L     R1,SKIPTMVL        CONVERT TM NUMB TO DECIMAL    -ARB82-
         CVD   R1,BADLNGTH                                      -ARB82-
         OI    BADLNGTH+7,X'0F'                                 -ARB82-
         UNPK  7(4,R2),BADLNGTH                                 -ARB82-
         B     PUTPOS             GO WRITE START LINE           -ARB82-
NOPTM    S     R2,=F'2'           BACK UP OVER +                -ARB82-
         MVI   0(R2),C' '         AND COVER IT UP               -ARB82-
PUTPOS   BAL   R2,PUTLINE3                                      -ARB82-
         DC    C'VSTART'                                        -ARB82-
         MVI   MSGBUF,C' '        REMOVE CARRIAGE CONTROL       -ARB82-
NOPOSLIN MVI   VTOCPAGE,X'FF'     SHOW FIRST ENTRY OF PAGE      -ARB82-
         L     R4,FRSTVTOC        GET ADDRESS OF FIRST VTOC BLOCK.-CWB-
         SR    R8,R8              CLEAR R8 FOR 1-BYTE COUNTS.     -CWB-
         LA    R3,8(R4)           LOAD 1ST ENTRY &              -ARB82-
         IC    R8,0(R4)           NUMBER OF ENTRIES             -ARB82-
         STM   R3,R4,PAGESTRT     STORE FOR NOTE OUTPUT         -ARB82-
         ST    R8,PAGESTRT+8                                    -ARB82-
         B     NEXTLINE                                         -ARB82-
NEXTVBLK LA    R3,8(R4)           GET ADDRESS OF FIRST ENTRY IN   -CWB-
*                                      THE VTOC BLOCK.            -CWB-
         IC    R8,0(R4)           GET THE NUMBER OF ENTRIES IN    -CWB-
*                                      THIS VTOC BLOCK.           -CWB-
NEXTLINE LH    R2,VTOCMSG#        GET # OF NOTES FOR THIS ENTRY -ARB82-
         LTR   R2,R2              ARE THERE ANY?                -ARB82-
         BZ    *+8                                              -ARB82-
         LA    R2,1(,R2)          IF SO, RESERVE SEPARATION     -ARB82-
         AR    R2,R2              COMPUTE NOTE SPACE            -ARB82-
         AH    R2,QLINE#          COMPUTE TOTAL RESERVED SPACE  -ARB82-
         STH   R2,QLINE#                                        -ARB82-
         CLI   VTOCPAGE,X'FF'     FIRST ENTRY OF PAGE           -ARB82-
         BE    VTOCENT            YES, LET IT OVERFLOW          -ARB82-
         LA    R2,2(,R2)          ALLOW A LITTLE EXTRA          -ARB82-
         A     R2,LINENO          NO, SEE IF NOTES WILL FIT     -ARB82-
         C     R2,LINECNT                                       -ARB82-
         BH    PAGEFULL           IF NOT, SPILL QUEUED NOTES    -ARB82-
VTOCENT  MVI   VTOCPAGE,X'00'     RESET FIRST LINE FLAG         -ARB82-
         MVC   MSGBUF+1(132),BLANKBUF  BUILD VTOC ENTRY LINE:   -ARB82-
         MVC   MSGBUF+1(4),VTOCSEQN    DATA SET SEQUENCE NUMBER.  -CWB-
         CLI   MSGBUF+1,C'-'      SPECIAL VTOC LINE?            -ARB82-
         BNE   NOEOVLIN           NO.                           -ARB82-
         MVC   MSGBUF+5(4),=23C'-'     YES, PAD WITH HYPHENS    -ARB82-
         MVC   MSGBUF+9(17),VTOCDSN    LINE ID STORED AS DSN    -ARB82-
         MVC   MSGBUF+26(8),=23C'-'     ADD MORE AFTER          -ARB82-
         B     VTOCNONO           NO OTHER INFO ON LINE         -ARB82-
NOEOVLIN MVC   MSGBUF+7(17),VTOCDSN    DATA SET NAME.           -ARB82-
         MVC   MSGBUF+26(3),VTOCLTYP   LABEL TYPE.              -ARB82-
         MVC   MSGBUF+32(4),VTOCRECF   RECFM.                   -ARB82-
         MVC   MSGBUF+38(5),VTOCLREC   LRECL.                   -ARB82-
         MVC   MSGBUF+46(5),VTOCBLKS   BLKSIZE.                 -ARB82-
         MVC   MSGBUF+54(7),VTOCEXPR   EXPIRATION DATE.         -ARB82-
         MVC   MSGBUF+63(6),VTOCOUNT   NUMBER OF BLOCKS.        -ARB82-
         CLI   COUNTFLG,C'Y'      IN COUNT MODE?                -ARB82-
         BNE   VTOCHIST           NO, GIVE HISTORY INSTEAD      -ARB82-
         MVC   MSGBUF+74(5),VTOCMAXB   MAXIMUM BLOCK SIZE.      -ARB82-
         MVC   MSGBUF+83(5),VTOCMINB   MINIMUM BLOCK SIZE.      -ARB82-
         MVC   MSGBUF+92(5),VTOCAVGB   AVERAGE BLOCK SIZE.      -ARB82-
         B     PUTVTOC                                          -ARB82-
VTOCHIST MVC   MSGBUF+73(8),VTOCJOBN   JOB NAME.                -ARB82-
         MVC   MSGBUF+83(8),VTOCSTEP   STEP NAME.               -ARB82-
         MVC   MSGBUF+93(7),VTOCCREA   CREATION DATE.           -ARB82-
PUTVTOC  TM    WRTFLG,X'01'       WAS THIS A COPY OPERATION?    -ARB82-
         BZ    NOVCOPY            NO.                           -ARB82-
         MVC   MSGBUF+101(5),=C'COPY=' YES, ADD OUTPUT SEQ      -ARB82-
         MVC   MSGBUF+106(4),VTOCOUSQ   OR TM NUMBER            -ARB82-
NOVCOPY  CLC   VTOCMSG#,=H'0'     ANY NOTES FOR THIS ENTRY?     -ARB82-
         BE    VTOCNONO           NO.                           -ARB82-
         MVC   MSGBUF+115(16),=C'-->SEE NOTE A<--' YES,         -ARB82-
         LH    R2,NOTENUM         INSERT EYE CATCHER            -ARB82-
         IC    R2,NOTELETS-1(R2)  ADD NOTE LETTER               -ARB82-
         STC   R2,MSGBUF+127                                    -ARB82-
         LH    R2,NOTENUM         INCREASE NOTE NUMBER          -ARB82-
         LA    R2,1(,R2)                                        -ARB82-
         STH   R2,NOTENUM                                       -ARB82-
VTOCNONO BAL   R2,PUTLINE3        OUTPUT THE LINE.              -ARB82-
         DC    CL6'VTOC--'        REQUIRED DEAD SPACE.            -CWB-
         MVI   MSGBUF,C' '        SINGLE SPACE NEXT LINE        -ARB82-
         LA    R3,VTOCSIZE(R3)    ADVANCE TO NEXT ENTRY.          -CWB-
         BCT   R8,NEXTLINE        LOOP IF MORE IN THIS BLOCK.     -CWB-
         L     R4,0(R4)           GET ADDRESS OF NEXT BLOCK.      -CWB-
         LA    R4,0(R4)           CLEAR HIGH-ORDER BYTE.          -CWB-
         LTR   R4,R4              IS FOREWARD POINTER ZERO?       -CWB-
         BNZ   NEXTVBLK           IF NOT, GO DO NEXT BLOCK.     -ARB82-
         MVC   MSGBUF+1(132),BLANKBUF                           -ARB82-
         MVC   MSGBUF+1(8),=23C'-'     BUILD STOP LINE          -ARB82-
         MVC   MSGBUF+9(9),=C'MAP ENDED'   WHICH MAY NOT GO OUT -ARB82-
         CLI   OKCODE+3,12        I/O ERROR TERMINATION?        -ARB82-
         BNE   NORMSTOP           NO.                           -ARB82-
         MVC   MSGBUF+19(17),=C'DUE TO I/O ERRORS'              -ARB82-
         B     PUTSTOP            GO WRITE KISSOFF LINE         -ARB82-
NORMSTOP CLI   TMQUIT,X'FF'       MAXTM STOP?                   -ARB82-
         BNE   EOVQUIT            NO, STOPEOV REACHED           -ARB82-
         MVC   MSGBUF+19(9),=C'- STOPTM='  YES, SAY WHY         -ARB82-
         L     R1,STOPTMNO        CONVERT TM NO TO DECIMAL      -ARB82-
         LA    R2,MSGBUF+28       WHERE TO PUT NUMBER           -ARB82-
         B     STOPLINE                                         -ARB82-
EOVQUIT  L     R1,STPEOVNO        IS STOPEOV 1?                 -ARB82-
         CH    R1,=H'1'                                         -ARB82-
         BNH   PAGEFULL           NO COMMENT IF SO              -ARB82-
         MVC   MSGBUF+19(10),=C'- STOPEOV='                     -ARB82-
         LA    R2,MSGBUF+29                                     -ARB82-
STOPLINE CVD   R1,BADLNGTH                                      -ARB82-
         OI    BADLNGTH+7,X'0F'                                 -ARB82-
         UNPK  0(4,R2),BADLNGTH   INSERT NUMBER                 -ARB82-
PUTSTOP  BAL   R2,PUTLINE3        GO WRITE FINAL LINE           -ARB82-
         DC    C'VSTOP-'                                        -ARB82-
PAGEFULL CLC   QLINE#,=H'2'       ARE ANY NOTES PENDING?        -ARB82-
         BE    VEJECT             NO, JUMP TO NEXT PAGE         -ARB82-
         OI    OKCODE+3,8         SET RC=8 FOR NOTES ISSUED     -ARB82-
         MVC   NOTENUM,=H'1'      YES, RESET NOTE COUNTER       -ARB82-
         LM    R14,R0,PAGESTRT    RELOAD NOTE STARTING POINT    -ARB82-
         STM   R3,R4,PAGESTRT     STORE NEW ONES FOR NEXT PAGE  -ARB82-
         ST    R8,PAGESTRT+8                                    -ARB82-
         LR    R3,R14             COPY OVER TO STANDARD REGS    -ARB82-
         LR    R4,R15                                           -ARB82-
         LR    R8,R0                                            -ARB82-
         MVC   MSGBUF,BLANKBUF    BLANK OUTPUT LINE             -ARB82-
         MVI   MSGBUF,C'0'        ADD CARRIAGE CNTL             -ARB82-
         BAL   R2,PUTLINE2        ADD SEPARATOR LINE            -ARB82-
         MVC   MSGBUF+30(13),=C'*** NOTES ***'                  -ARB82-
NEXTNOTS CLC   VTOCMSG#,=H'0'     ANY NOTES FOR THIS ENTRY?     -ARB82-
         BE    NOTESKIP           NO.                           -ARB82-
         CLC   NOTENUM,=H'1'      YES, FIRST ON THE PAGE?       -ARB82-
         BE    NOTE1              YES.                          -ARB82-
         MVC   MSGBUF,BLANKBUF    NO, SEPARATE FROM PREVIOUS    -ARB82-
         MVI   MSGBUF,C'0'                                      -ARB82-
         BAL   R2,PUTLINE2                                      -ARB82-
         MVC   MSGBUF+25(23),=23C'-'                            -ARB82-
NOTE1    LA    R6,VTOCSTAT        POINT TO STATUS BYTES         -ARB82-
         LH    R7,VTOCMSG#        NUMBER OF NOTES TO EXPECT     -ARB82-
         MVC   MSGBUF,BLANKBUF                                  -ARB82-
         MVC   MSGBUF+2(3),=C'(A)'     ADD NOTE SET ID          -ARB82-
         LH    R2,NOTENUM                                       -ARB82-
         IC    R1,NOTELETS-1(R2)  FROM THE ALPHABET             -ARB82-
         STC   R1,MSGBUF+3                                      -ARB82-
         LA    R2,1(,R2)          BUMP NOTE COUNTER             -ARB82-
         STH   R2,NOTENUM                                       -ARB82-
NEXTNOTE MVI   MSGBUF,C'0'        DOUBLE SPACE ALL NOTES        -ARB82-
FINDNOTE CLI   0(R6),0            IS THIS NOTE WANTED?          -ARB82-
         BNE   NOTETHIS           YES.                          -ARB82-
         LA    R6,1(,R6)          NO, KEEP LOOKING              -ARB82-
         B     FINDNOTE                                         -ARB82-
NOTETHIS LR    R1,R6                                            -ARB82-
         LA    R0,VTOCSTAT        COMPUTE NUMBER OF NOTE        -ARB82-
         SR    R1,R0                                            -ARB82-
         SLL   R1,2                                             -ARB82-
         L     R2,NOTES(R1)       PICK UP LOCATION &            -ARB82-
         IC    R1,NOTES(R1)       LENGTH OF MESSAGE             -ARB82-
         EX    R1,MVNOTE          MOVE INTO MESSAGE BUFFER      -ARB82-
         BAL   R2,PUTLINE3        GO WRITE OUT THE NOTE         -ARB82-
         DC    C'NOTE--'                                        -ARB82-
         MVC   MSGBUF,BLANKBUF    CLEAR MSG BUFFER AGAIN        -ARB82-
         LA    R6,1(,R6)                                        -ARB82-
         BCT   R7,NEXTNOTE                                      -ARB82-
NOTESKIP LA    R3,VTOCSIZE(R3)    LOCATE NEXT ENTRY             -ARB82-
         C     R3,PAGESTRT        THRU WITH THIS PAGE?          -ARB82-
         BE    NOTESOUT           YES.                          -ARB82-
         BCT   R8,NEXTNOTS                                      -ARB82-
         L     R4,0(,R4)          PROCEEDING TO NEXT BLOCK      -ARB82-
         LTR   R4,R4              IF NECESSARY & POSSIBLE       -ARB82-
         BZ    EXIT               IF NOT, WE'RE DONE            -ARB82-
         LA    R3,8(R4)                                         -ARB82-
         IC    R8,0(R4)                                         -ARB82-
         C     R3,PAGESTRT        DONE WITH THIS PAGE?          -ARB82-
         BNE   NEXTNOTS           NO, CONTINUE                  -ARB82-
NOTESOUT MVC   NOTENUM,=H'1'      RESET NOTE COUNTER TO 1       -ARB82-
         MVC   QLINE#,=H'2'                                     -ARB82-
         LM    R3,R4,PAGESTRT     RESTORE VTOC SCAN REGS        -ARB82-
         L     R8,PAGESTRT+8                                    -ARB82-
VEJECT   LTR   R4,R4              ANYTHING LEFT TO WRITE?       -ARB82-
         BZ    EXIT               NO, TERMINATE                 -ARB82-
         LA    R0,100             YES, FORCE NEW PAGE           -ARB82-
         BAL   R2,PAGECHK                                       -ARB82-
         MVI   VTOCPAGE,X'FF'     SET TOP OF PAGE FLAG          -ARB82-
         B     NEXTLINE           GO RESUME VTOC LISTING        -ARB82-
         SPACE 3
MVNOTE   MVC   MSGBUF+8(0),0(R2)  MOVE NOTE TO MSGBUF           -ARB82-
NOTES    DC    12A(0)             SPACE FOR NOTE POINTERS       -ARB82-
         ORG   NOTES                                            -ARB82-
         DC    AL1(L'NOTEIO-1),AL3(NOTEIO)                      -ARB82-
         DC    AL1(L'NOTENHDR-1),AL3(NOTENHDR)                  -ARB82-
         DC    AL1(L'NOTENTRL-1),AL3(NOTENTRL)                  -ARB82-
         DC    AL1(L'NOTELBSQ-1),AL3(NOTELBSQ)                  -ARB82-
         DC    AL1(L'NOTEBLP-1),AL3(NOTEBLP)                    -ARB82-
         DC    AL1(L'NOTENCON-1),AL3(NOTENCON)                  -ARB82-
         DC    AL1(L'NOTEDATA-1),AL3(NOTEDATA)                  -ARB82-
         DC    AL1(L'NOTECOUN-1),AL3(NOTECOUN)                  -ARB82-
         DC    AL1(L'NOTETRNC-1),AL3(NOTETRNC)                  -ARB82-
         DC    AL1(L'NOTEAXLB-1),AL3(NOTEAXLB)                  -ARB82-
*        ORG   NOTES+44                                         -DEBUG-
*        DC    AL1(L'NOTEDBUG-1),AL3(NOTEDBUG)                  -DEBUG-
         ORG   ,                                                -ARB82-
PAGESTRT DS    3A                 VTOC ADDRS FOR PAGE START     -ARB82-
INDEX    DC    F'0'               NOCONV=+4, 7-TRK=+32, +DEN*8
*TBLORG EQU BPIBGTBL-20           THEORETICAL ORIGIN OF BPIBGTBL
*                                 WHICH IS LIKE A 3-D ARRAY:
*                                 7-TRK=+40,  NOCONV=+4, +DEN*8 -ARB79-
BPIBGTBL DC    H'601,800'   800 BPI NOCONV 9-TRK
         DC    H'1,1'      1600 BPI CONV   9-TRK (NOT USED)
         DC    H'651,1600' 1600 BPI NOCONV 9-TRK
         DC    H'1,1'      6250 BPI CONV   9-TRK (NOT USED)     -ARB79-
         DC    H'300,6250' 6250 BPI NOCONV 9-TRK                -ARB79-
         DC    H'752,150'   200 BPI CONV   7-TRK
         DC    H'752,200'   200 BPI NOCONV 7-TRK
         DC    H'751,417'   556 BPI CONV   7-TRK
         DC    H'751,556'   556 BPI NOCONV 7-TRK
         DC    H'751,600'   800 BPI CONV   7-TRK
         DC    H'751,800'   800 BPI NOCONV 7-TRK
TMLENGTH DC    H'3750'  DEFAULT TAPEMARK LENGTH*1000 (9-TRK)    -ARB82-
ENDMSG   DC    C'0PROCESSING OF THIS TAPE COMPLETED'            -ARB79-
         DC    C':    TOTAL BYTES READ='
BYTES    DC    C'XXXXXXXXXX'                                    -ARB79-
         DC    C'    NUMBER OF DATA BLOCKS READ='
NBLKS    DC    C'XXXXXX'
ERRSUMSG DC    C'0NUMBER OF I/O ERRORS=XXXXX'                   -ARB82-
LNGTHEST DC    C'0LENGTH ESTIMATE=XXXX FEET YY INCHES ASSUMING DEN=X AN*
               D TRTCH=STANDARD           '
LNGTHACC DC    C'0(LENGTH ESTIMATE USUALLY ACCURATE WITHIN PLUS OR MINU*
               S TEN PERCENT;  ALMOST ALWAYS WITHIN TWENTY PERCENT)'
NOTENUM  DC    H'1'               CURRENT NOTE NUMBER           -ARB82-
QLINE#   DC    H'2'               QUEUED NOTE LINES             -ARB82-
NOTELETS DC    C'ABCDEFGHIJKLMNOPQRSTUVWXYZ' NOTE LABELS        -ARB82-
VTOCPAGE DC    X'FF'              FIRST VTOC ENTRY OF PAGE FLAG -ARB82-
VTOCHEDH DC    C'JOB NAME  STEP      CREATED'                   -ARB82-
VTOCHEDC DC    C'MAX BLK  MIN BLK  AVG BLK'                     -ARB82-
#IO      EQU   0                  I/O ERROR FLAG NUMBER         -ARB82-
NOTEIO   DC    C'ONE OR MORE I/O ERRORS OCCURRED PROCESSING THIS DATA S*
               ET'                                              -ARB82-
#NHDR    EQU   1                  HEADERS MISSING NUMBER        -ARB82-
NOTENHDR DC    C'NO VALID HEADER LABELS PRESENT FOR THIS DATA SET'     *
                                                                -ARB82-
#NTRL    EQU   2                  TRAILERS MISSING NUMBER       -ARB82-
NOTENTRL DC    C'NO VALID TRAILER LABELS PRESENT FOR THIS DATA SET'    *
                                                                -ARB82-
#LBSQ    EQU   3                  LABEL SEQUENCE ERROR NUMBER   -ARB82-
NOTELBSQ DC    C'DATA FILE CONTAINS LABELS OR LABELS OUT OF SEQUENCE'  *
                                                                -ARB82-
#BLP     EQU   4                  LABEL TYPE CHANGE NUMBER      -ARB82-
NOTEBLP  DC    C'LABEL TYPE SET TO BLP DUE TO INCONSISTENCY WITH PREVIO*
               US DATA SETS'                                    -ARB82-
#NCON    EQU   5                  LABELS INCONSISTENT NUMBER    -ARB82-
NOTENCON DC    C'HEADER AND TRAILER LABELS INCONSISTENT FOR THIS DATA S*
               ET'                                              -ARB82-
#DATA    EQU   6                  DATA IN LABEL FILE NUMBER     -ARB82-
NOTEDATA DC    C'DATA RECORDS FOUND IN LABEL FILE'              -ARB82-
#COUN    EQU   7                  COUNT CONFLICT NUMBER         -ARB82-
NOTECOUN DC    C'THE BLOCK COUNT IN THE TRAILER LABEL IS INCORRECT'    *
                                                                -ARB82-
#TRNC    EQU   8                  LABEL TRUNCATION NUMBER       -ARB82-
NOTETRNC DC    C'LABELS TRUNCATED TO 80 BYTES DURING COPY'      -ARB82-
#AXLB    EQU   9                  NON-STD LABEL NUMBER          -ARB82-
NOTEAXLB DC    C'UNSUPPORTED ANS LABELS FOR THIS FILE NOT COPIED'      *
                                                                -ARB82-
*#DBUG    EQU  11                                               -DEBUG-
*NOTEDBUG DC   C'DSNAME FOR THIS FILE BEGINS WITH THE LETTER T' -DEBUG-
         EJECT
SAVE     DS    9D                 SAVE AREA FOR TAPESCAN        -ARB79-
SAVE2    DS    9D                 DUPLICATE SAVE AREA FOR SYNAD -ARB79-
         LTORG
ASCWARN  DC    C'0ASCII TRANSLATION TO BE PERFORMED - RECFM V OR D DATA*
               SETS MAY NOT REMAIN VALID'                       -ARB84-
LABLSIZE DC    C'0WARNING - ABOVE LABEL LONGER THAN 80 CHARACTERS'     *
                                                                -ARB84-
JFCBIN   DC    22D'0'             176 BYTES FOR INPUT JFCB      -ARB82-
JFCBOUT  DC    22D'0'             OUTPUT DD'S JFCB              -ARB82-
JOUTFLSQ EQU   JFCBOUT+JFCBFLSQ                                 -ARB82-
SYSPRINT DCB   DDNAME=SYSPRINT,MACRF=PM,DSORG=PS,RECFM=FA,             *
               BLKSIZE=133,LRECL=133                            -ARB82-
INPUT    DCB   DDNAME=INPUT,MACRF=RC,DSORG=PS,RECFM=U,NCP=1,           *
               BLKSIZE=32760,EODAD=EODS,SYNAD=SYNERR,EXLST=EXITLIST    *
                                                                -ARB82-
OUTPUT   DCB   DDNAME=OUTPUT,MACRF=(RC,WC),DSORG=PS,RECFM=U,NCP=1,     *
               BLKSIZE=32760,EODAD=EOLABEL,SYNAD=SYNERR,EXLST=EXLSTOUT *
                                                                -ARB82-
NUBTABL  DS    0C                 NUMERIC CHECK TABLE             -HMD-
         DC    256XL1'FD'                                         -HMD-
         ORG   NUBTABL+C'0'       NUMBERS ONLY                    -HMD-
         DC    10X'00'                                            -HMD-
         ORG   ,                  BACK TO REALITY                 -HMD-
         SPACE 5
VTOC     DSECT
VTOCSEQN DS    CL4                SEQUENCE NUMBER                 -CWB-
VTOCDSN  DS    CL17               DATA SET NAME.                  -CWB-
VTOCLTYP DS    CL3                LABEL TYPE.                   -ARB82-
VTOCRECF DS    CL4                RECFM.                          -CWB-
VTOCLREC DS    CL5                LRECL.                          -CWB-
VTOCBLKS DS    CL5                BLKSIZE.                        -CWB-
VTOCMAXB DS    CL5                MAXIMUM BLOCK SIZE.             -CWB-
VTOCMINB DS    CL5                MINIMUM BLOCK SIZE.             -CWB-
VTOCAVGB DS    CL5                AVERAGE BLOCK SIZE.             -CWB-
VTOCOUNT DS    CL6                BLOCK COUNT.                    -CWB-
VTOCCREA DS    CL8                CREATION DATE.                  -CWB-
VTOCJOBN DS    CL8                JOB NAME.                       -CWB-
VTOCSTEP DS    CL8                STEP NAME.                      -CWB-
VTOCEXPR DS    CL8                EXPIRATION DATE.                -CWB-
VTOCOUSQ DS    CL4                OUTPUT SEQUENCE NUMBER        -ARB82-
VTOCCSIZ EQU   *-VTOC             SIZE OF CHAR PART OF ENTRY    -ARB82-
VTOC2    DS    0H                 BINARY PART OF VTOC ENTRY     -ARB82-
VTOCMSG# DS    H                  NUMBER OF NOTES FOR ENTRY     -ARB82-
VTOCSTAT DS    XL12               FLAGS FOR POSSIBLE ERRORS     -ARB82-
VTOCBSIZ EQU   *-VTOC2            SIZE OF BINARY VTOC           -ARB82-
         DS    0D                 ADVANCE TO DOUBLE WORD BNDRY.   -CWB-
VTOCSIZE EQU   *-VTOC             SIZE OF VTOC ENTRY.             -CWB-
VTOCEPB  EQU   10                 NUMBER OF ENTRIES PER BLOCK.    -CWB-
VTOCBLSZ EQU   VTOCEPB*VTOCSIZE+8 SIZE OF VTOC BLOCK.             -CWB-
         END   TAPESCAN                                         -ARB82-
