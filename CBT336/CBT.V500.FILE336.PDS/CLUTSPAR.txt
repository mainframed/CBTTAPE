         TITLE 'CLUTSPAR -- LOCAL MACRO DEFINITIONS'
         MACRO
         TABSET &VAL
.*       THIS MACRO IS AN AID IN THE GENERATION OF TRANSLATE TABLES
         GBLA  &TABVAL(256),&TABNUM
         LCLA  &I,&N
&I       SETA  2
.NEXT    AIF   (N'&SYSLIST(&I) EQ 2).RANGE
         TABHEX &SYSLIST(&I)
&TABVAL(&TABNUM) SETA &TABVAL(&TABNUM)+&VAL
         AGO   .LOOP
.RANGE   TABHEX &SYSLIST(&I,1)
&N       SETA  &TABNUM
         TABHEX &SYSLIST(&I,2)
.RLOOP   ANOP
&TABVAL(&N) SETA &TABVAL(&N)+&VAL
&N       SETA  &N+1
         AIF   (&N LE &TABNUM).RLOOP
.LOOP    ANOP
&I       SETA  &I+1
         AIF   (&I LE N'&SYSLIST).NEXT
         MEND
         SPACE 3
         MACRO
         TABHEX &HEX
.*       THIS MACRO CONVERTS HEX TO DECIMAL FOR TABSET
         GBLA  &TABNUM
         LCLC  &D(2)
         LCLA  &I
&D(1)    SETC  '&HEX'(1,1)
&D(2)    SETC  '&HEX'(2,1)
&I       SETA  1
.HLOOP   AIF   ('&D(&I)' NE 'A').NA
&D(&I)   SETC  '10'
         AGO   .AGAIN
.NA      AIF   ('&D(&I)' NE 'B').NB
&D(&I)   SETC  '11'
         AGO   .AGAIN
.NB      AIF   ('&D(&I)' NE 'C').NC
&D(&I)   SETC  '12'
         AGO   .AGAIN
.NC      AIF   ('&D(&I)' NE 'D').ND
&D(&I)   SETC  '13'
         AGO   .AGAIN
.ND      AIF   ('&D(&I)' NE 'E').NE
&D(&I)   SETC  '14'
         AGO   .AGAIN
.NE      AIF   ('&D(&I)' NE 'F').AGAIN
&D(&I)   SETC  '15'
.AGAIN   ANOP
&I       SETA  &I+1
         AIF   (&I LE 2).HLOOP
&TABNUM  SETA  16*&D(1)+&D(2)+1
         MEND
         SPACE 3
         MACRO
&N       TABGEN
.*       THIS MACRO GENERATES THE TRANSLATE TABLE DEFINED BY TABSET
         GBLA  &TABVAL(256)
         LCLA  &I
&N       DC    AL1(&TABVAL(1),&TABVAL(2),&TABVAL(3),&TABVAL(4),&TABVAL(*
               5),&TABVAL(6),&TABVAL(7),&TABVAL(8))
&I       SETA  9
.LOOP    ANOP
         DC    AL1(&TABVAL(&I),&TABVAL(&I+1),&TABVAL(&I+2),&TABVAL(&I+3*
               ),&TABVAL(&I+4),&TABVAL(&I+5),&TABVAL(&I+6),&TABVAL(&I+7*
               ))
&I       SETA  &I+8
         AIF   (&I LE 256).LOOP
         MEND
         SPACE 3
         MACRO
         TABRESET
.*       THIS MACRO RESETS ACCUMULATED TABSET VALUES FOR GENERATION
.*       OF A NEW TABLE.
         GBLA  &TABVAL(256)
         LCLA  &I
&I       SETA  1
.LOOP    ANOP
&TABVAL(&I) SETA 0
&I       SETA  &I+1
         AIF   (&I LE 256).LOOP
         MEND
         SPACE 3
         MACRO
&NAME    SEG   &TEXT,&OFF=0
.*       THIS MACRO GENERATES A MESSAGE SEGMENT SUITABLE FOR USE
.*       WITH PUTLINE OR PUTGET.
&NAME    DC    AL2(L'SEG&SYSNDX+4,&OFF) MESSAGE LENGTH AND OFFSET
SEG&SYSNDX DC  C&TEXT             TEXT OF MESSAGE SEGMENT
         SPACE
         MEND
         TITLE 'CLUTSPAR -- IKJPARS-LIKE PARSING ROUTINE'
         GBLC  &OPSYS
&OPSYS   SETC  'MVS'              OR 'SVS' OR 'MVT'
         SPACE
         BANNER CLUTSPAR
         SPACE
***********************************************************************
*                                                                     *
* ROUTINE NAME -                                                      *
*        CLUTSPAR (CALLED AS IKJPARS)                                 *
*                                                                     *
* FUNCTION -                                                          *
*        TO PERFORM THE FUNCTION OF THE TSO SERVICE ROUTINE IKJPARS,  *
*        WHILE PROVIDING AS MUCH FLEXIBILITY AND USER-FRIENDLINESS    *
*        AS PERMITTED BY ITS FORMAL, INFORMAL AND TRADITIONAL         *
*        SPECIFICATIONS. VARIOUS USEFUL AND BENIGN EXTENSIONS ARE     *
*        ALSO PROVIDED.                                               *
*                                                                     *
* ENVIRONMENT -                                                       *
*        PSEUDO-PL/I CREATED ON ENTRY.                                *
*                                                                     *
* LINKAGE -                                                           *
*        LINK FROM A COMMAND PRCESSOR. (IN MVS, THE CALLTSSR MACRO    *
*        MAY ALSO BE USED.)                                           *
*                                                                     *
* PARAMETERS -                                                        *
*        PARSE PARAMETER LIST, AS MAPPED BY THE IKJPPL MACRO, AND     *
*        OTHER PARAMETER LISTS ADDRESSED THEREBY, NOTABLY THE OUTPUT  *
*        OF THE IKJPOSIT, IKJIDENT, IKJKEYWD AND OTHER SYNTAX-        *
*        DEFINITION MACROS.                                           *
*                                                                     *
* RETURN CODES (RETURNED VIA R15) -                                   *
*         0:   SUCCESSFUL EXECUTION                                   *
*         4:   UNABLE TO PROMPT FOR CORRECTION OF INVALID PARAMETERS  *
*         8:   PARSE TERMINATED BY ATTENTION                          *
*        12:   INVALID PARAMETER LIST OR INTERNAL ERROR IN PARSE      *
*        16:   CONDITIONAL GETMAIN FAILED (NO MEMORY AVAILABLE)       *
*        20:   TERMINATION REQUESTED BY CALLER'S VALIDITY CHECK EXIT  *
*        24:   CONFLICTS FOUND IN COBOL SYNTAX DEFINITIONS (IKJOPER,  *
*              IKJTERM AND IKJRSVWD)                                  *
*        28:   TERMINAL DISCONNECTED                                  *
*                                                                     *
* MAJOR INCOMPATIBILITIES -                                           *
*        THE VALIDITY CHECK EXIT FOR A RANGE MAY BE CALLED THREE      *
*        TIMES, ONCE FOR EACH PART AND ONCE FOR THE ENTIRE RANGE.     *
*        THIS HAS THE EFFECT THAT THE SECOND PART OF A RANGE MUST     *
*        ALSO BE A VALID FIRST PART.                                  *
*                                                                     *
*        ONLY FIVE LEVELS OF KEYWORD NESTING ARE ALLOWED (SURELY THAT *
*        IS ENOUGH).                                                  *
*                                                                     *
* DEBUGGING NOTE -                                                    *
*        TO ENABLE A RUN-TIME TRACE BACK ON PROGRAM CHECK, REMOVE     *
*        THE COMMENT INDICATOR FROM THE LIERRSET MACRO BELOW. THIS    *
*        CANNOT BE USED WHEN CLUTSPAR IS INSTALLED AS IKJPARS, AS     *
*        IT ISSUES THE SPIE MACRO, WHICH IS INVALID IN SOME CONTEXTS  *
*        IN WHICH IKJPARS EXECUTES (E.G., CALLED FROM LOGON).         *
*                                                                     *
* COPYRIGHT -                                                         *
*        1981, 1983 RICE UNIVERSITY                                   *
***********************************************************************
         EJECT
TSUCOPRT CSECT
         DC    C'TSU COPYRIGHT RICE UNIVERSITY 1981'
         SPACE
CLUTSPAR LISECT
CLUZPAR  LIENTRY BASE=R11,ISASIZE=DSALEN+768,PARM=R2
         EJECT
*
*        THIS ROUTINE IS A REPLACEMENT FOR THE TSO SERVICE ROUTINE
*        IKJPARS. IT ACCEPTS THE SAME INPUT PARAMETERS AND RETURNS
*        THE SAME OUTPUT TO ITS CALLER, BUT DIFFERS CONSIDERABLY
*        IN THE STYLE OF ITS INTERACTION WITH THE TERMINAL USER.
*        ITS MAIN VIRTUE IS ITS LIBERALITY, ACCEPTING PARAMETER FORMS
*        NOT ALLOWED BY IKJPARS, AND ALLOWING THE USER TO CORRECT
*        ERRORS IN MORE CASES.
*
         SPACE 3
*
*        INITIALIZE
*
         LR    R5,RDSA            SET UP PERM. DSA POINTER
         DROP  RDSA
         USING DSA,R5
         LA    R4,4095(,R11)      SET UP SECOND BASE REG
         USING CLUTSPAR+4095,R4
         XC    IOPL(256),IOPL     CLEAR DSA
         XC    IOPL+256(256),IOPL+256
         XC    IOPL+512(256),IOPL+512
         XC    IOPL+768(DSALEN-768-88),IOPL+768
         MVC   IOPL(12),0(R2)     SAVE UPT, ECT & ECB ADDRS
         MVC   PCL(12),12(R2)     SAVE PCL, ANSWER & CBUF PTRS
         MVI   PCL,0              CLEAR HIGH ORDER PCL BYTE
         MVC   UWA(4),24(R2)      SAVE USER WK AREA ADDR
         LA    R10,SUBWORKS       NO SUBFIELD RECURSION YET
         USING SUBWORK,R10
         L     R9,PCL             FIND FIRST KEYWORD ADDR
         LH    R2,4(,R9)
         AR    R2,R9
         ST    R2,KEY1            SAVE FOR LATER
         L     R2,ANSPTR          FIND ANSWER POINTER
         XC    0(4,R2),0(R2)      CLEAR OLD CONTENTS
         LIBEL PARSTERM,ADDR=GIVEUP    ESTABLISH ERROR ESCAPE POINT
         LH    R1,2(,R9)          GET SIZE OF ANSWER
         MVC   PDLPTR,NULL        CLEAR PDL POINTER FOR TROUBLE
         L     R15,=A(CLUZMINT)
         BALR  R14,R15            CALL MEMORY INITIALIZATION ROUTINE
         L     R3,PDLPTR          FIND PDL START
         LA    R2,8(,R3)          CLEAR THE PDL (EXCEPT THE HEADER)
         SR    R1,R1
         LA    R14,256
         LH    R15,2(,R9)
         SH    R15,=H'9'
INITCLR  BXH   R1,R14,INITLAST
         XC    0(256,R2),0(R2)
         AR    R2,R14
         B     INITCLR
INITLAST EX    R15,INITZERO
         LH    R2,0(,R9)
         AR    R2,R9              ADDRESS END OF PCL
         LH    R3,4(,R9)
         AR    R3,R9              ADDRESS FIRST KEYWORD OR FIELD END
         LA    R9,6(,R9)          FIND ADDR OF FIRST PARM
         ST    R9,PARM1           AND SAVE
         L     R8,CBUF            CLEAR HIGH-ORDER BYTE OF CBUF
         LA    R8,0(,R8)
         ST    R8,CBUF
         ST    R8,CURINP          SAVE AS CURRENT INPUT SOURCE
         LR    R7,R8
         AH    R8,2(,R8)          ADJUST SOURCE REG BY OFFSET
         LA    R8,4(,R8)
         AH    R7,0(,R7)
         BCTR  R7,0               FIND END OF STRING
         LA    R6,1               SET UP R6 & R7 FOR BXLE/BXH
*        LIERRSET PARSERRX        TRACE & STOP AFTER PGM CHECK
         SPACE
*
*        SCAN THE PCL FOR DSNAMES, USERIDS, OR PRINT BYPASS IDENTS.
*        IF ANY ARE FOUND, SLASH MUST BE TREATED SPECIALLY.
*
         LR    R1,R9              START WITH 1ST PARM
         MVI   SKIPF,NOSLASH      ASSUME SLASH IS JUST A CHARACTER
INITSLSH MVC   ALIGN(1),0(R1)
         NI    ALIGN,X'E0'        ISOLATE PCE TYPE
         BZ    INITSLEN           BRANCH IF IKJSUBF/ENDP
         CLI   ALIGN,X'80'        IKJIDENT?
         BE    INITID             YES, CHECK FOR PTBYPS
         CLI   ALIGN,X'20'        IKJPOSIT?
         BNE   INITSLOP           NO.
         CLI   6(R1),6            IS IT A USERID?
         BL    INITSLOP           NO.
         CLI   6(R1),8            MAYBE, CHECK FOR DSNAME OR DSTHING
         AIF   ('&OPSYS' NE 'MVS').NMVS3B
         BNH   INITHIDE           SOMETHING WITH A PASSWORD
         CLI   6(R1),12           IS IT A UID2PSW?
         BNE   INITSLOP           NO, PASS IT BY
         AGO   .JMVS3B
.NMVS3B  BH    INITSLOP           BRANCH IF NOT
.JMVS3B  ANOP
INITHIDE NI    SKIPF,255-NOSLASH  SLASH MUST BE SPECIAL
         B     SYN
INITID   TM    1(R1),X'04'        IS THIS REALLY AN IKJEXTEN?
         BNZ   INITSLOP           YES, CAN'T BE BYPASS
         TM    6(R1),X'20'        IS THIS IDENT BYPASS?
         BNZ   INITHIDE           YES, SLASH MUST BE FACED
INITSLOP MVC   ALIGN,2(R1)        FIND NEXT PCE
         AH    R1,ALIGN
         B     INITSLSH           AND CONTINUE
*
*        THE ACCOUNT COMMAND IS VERY CLEVER AND STICKS END OF FIELD
*        INDICATORS INTO THE MIDDLE OF A LIST OF IKJPOSIT PCE'S. WE
*        HAVE TO USE THE FIRST KEYWORD POINTER IN THE IKJPARM/IKJSUBF
*        EXPANSION TO FIND OUR WAY ONWARD TO THE TRUE FIELD END.
*
INITSLEN CR    R1,R3              IS THIS AT OR PAST THE FIRST KEYWORD?
         BNL   INITNORM           YES, PROCEED NORMALLY
         LR    R1,R3              NO, SKIP TO FIRST KEYWORD
         B     INITSLSH           AND PROCEED FROM THERE
INITNORM AR    R1,R6
         CR    R1,R2              REACHED THE END?
         BNL   SYN                YES, GO TO IT
         MVC   ALIGN,0(R1)        NO, FIND NEXT KEYWORD OFFSET
         LH    R3,ALIGN
         A     R3,PARM1           POINT TO IT
         SH    R3,=H'6'           AFTER ADJUSTMENTS
         LA    R1,2(,R1)          SKIP INTO SUBFIELD
         B     INITSLSH
INITZERO XC    0(0,R2),0(R2)      CLEAR PDL
         SPACE
         LTORG
         TITLE 'CLUTSPAR -- POSITIONAL PARAMETER HANDLING'
*
*        THIS CODE IS ENTERED TO PARSE AN ENTIRE COMMAND LINE.
*        IT IS ALSO CALLED RECURSIVELY TO PROCESS A KEYWORD SUBFIELD
*        OR VARIABLE SUBSCRIPT.
*
SYN      XC    COMMAX,COMMAX      NOTICE A LEADING COMMA
         SPACE
*
*        SCAN THE PCE'S
*
SYNLOOP  NI    SKIPF,NOSLASH      CLEAR SKIP FLAGS
         MVI   LISTF,0            NOT A LIST YET
         MVC   ALIGN(1),0(R9)     ISOLATE TYPE FLAGS
         NI    ALIGN,X'E4'
         BZ    SYNPEND            IF IKJENDP/IKJSUBF FOUND
         CLI   ALIGN,X'40'
         BL    SYNPOS             IF IKJPOSIT
         BE    SYNKEY             IF IKJKEYWD
         CLI   ALIGN,X'80'        CHECK FOR IKJIDENT
         BNE   SYNPOS2
         MVC   PCBYPASS,6(R9)     SET BYPASS FLAG IF NECESSARY
         NI    PCBYPASS,X'20'
         B     SYNPOS2            SKIP POSITIONAL CODE
         SPACE
*
*        PROCESS POSITIONAL
*
SYNPOS   CLI   6(R9),10           SPACE TYPE?
         BNE   SYNNSPAC           NO.
         OI    SKIPF,NOSKIP       YES, INHIBIT FULL SKIP
SYNDLMIS MVI   DELIM,X'FF'        SET MISSING DELIM JUST IN CASE
         B     SYNPOS2
SYNNSPAC CLI   6(R9),2            DELIM OR STRING?
         BE    SYNTEXT            STRING, DON'T SKIP AT ALL
         BL    SYNDLMIS           DELIM, SET MISSING IN ADVANCE
         MVI   DELIM,0            NEITHER, RESET DELIMITER
         SPACE
*
*        SKIP SEPARATORS BEFORE POSITIONAL
*
SYNPOS2  L     R15,=A(CLUZSKIP)
         BALR  R14,R15            SKIP BLANKS, TABS, COMMAS ETC.
         B     *+4(R15)           ANYTHING AFTER?
         B     SYNTEXT            YES.
         TM    COBFLAGS,COBSUB    NO, IN SUBSCRIPT LIST?
         BNZ   SYNOSUBS           YES.
         TM    OPERFLGS,OPERACT   NO, IN EXPRESSION MODE?
         BNZ   EXPROMIT           YES, ESCAPE TO EXPRESSION ROUTINE
         B     SYNORMIT           NO, HANDLE NORMAL OMISSION
         SPACE
*
*        CALL RECOGNITION ROUTINE
*
SYNTEXT  TM    COBFLAGS,COBSUB    SUBSCRIPT PROCESSING?
         BNZ   SYNNEST            YES, NESTING ALREADY SET
         TM    OPERFLGS,OPERACT   NO, WITHIN EXPRESSION?
         BNZ   SYNNEST            YES.
         MVI   NESTLEVL,LEVITEM   NO, SET ITEM NESTING LEVEL
         ST    R9,OWNERID         DEFINE PCE AS MEMORY OWNER
SYNNEST  SR    R2,R2              DETERMINE TYPE AGAIN
         IC    R2,0(,R9)
         SRL   R2,5
         SLL   R2,2
         B     *+4(R2)
         BAL   R6,PARSRC12        NOT POSSIBLE HERE
         B     SYNPOSIT           IKJPOSIT
         BAL   R6,PARSRC12
         BAL   R6,PARSRC12
         B     SYNID              IKJIDENT
         B     SYNWORD            IKJRSVWD
         B     SYNTERM            IKJTERM
         B     SYNOPER            IKJOPER
         SPACE
SYNPOSIT SR    R15,R15
         IC    R15,6(,R9)         GET TYPE OF IKJPOSIT
         SLL   R15,2
         L     R15,RECOGS-4(R15)  SELECT RECOGNIZER
         B     SYNREC             JOIN OTHER CASES
SYNWORD  L     R15,=A(CLUZWORD)   RESERVED WORD RECOGNIZER
         B     SYNREC
SYNOPER  L     R15,=A(CLUZOPER)   OPER START ROUTINE
         B     SYNREC
SYNTERM  TM    6(R9),X'08'        IS THIS A SUBSCRIPT TERM?
         BZ    SYNTNSUB           NO.
         TM    COBFLAGS,COBSUB    YES, ARE WE IN SUBSCRIPT MODE?
         BZ    SYNNEXT            NO, JUST SKIP IT
SYNTNSUB L     R15,=A(CLUZCNST)   ASSUME CONSTANT TERM
         TM    6(R9),X'20'
         BNZ   SYNREC             GO ON WHEN WE GET IT RIGHT
         L     R15,=A(CLUZSTMT)   NEXT TRY STMT
         TM    6(R9),X'80'
         BNZ   SYNREC
         L     R15,=A(CLUZCVAR)   ELSE VAR OR ANY
         B     SYNREC
SYNID    TM    1(R9),X'04'        REALLY IKJEXTEN?
         BNZ   SYNEXTN            YES.
         L     R15,=A(CLUZIDEN)   IKJIDENT RECOGNIZER
         B     SYNREC
SYNEXTN  L     R15,=A(CLUZEXTN)   USER EXTENSION INTERFACE
         SPACE
SYNREC   OC    BYPASS,PCBYPASS    FORCE SUPPRESSION OF BYPASS PARM
         NI    COBFLAGS,COBSUB
         BNZ   SYNSBPDE           BRANCH IF SUBSCRIPT
         MVI   COBFLGS1,0
         MVC   ALIGN,4(R9)        FIND PDL OFFSET
         LH    R1,ALIGN
         A     R1,PDLPTR          ADDRESS PDE
         TM    1(R9),X'80'        LIST POSSIBLE?
         BO    LIST               YES.
         MVI   SUBFD1,0           NO, TURN OFF SUBFIELD/LIST SWITCH
         MVI   RANGEF,0           RESET RANGE FLAGS
         B     SYNRECG
SYNSBPDE L     R1,SUBSPDE         FIND PDE FOR THIS SUBSCRIPT
         SPACE
SYNRECG  LR    R3,R15             SAVE RECOGNIZER ENTRY
         ST    R8,PREVPOS         SAVE STARTING POINT FOR SCAN
         MVI   PASSF,0            TURN OFF PASSWORD FLAGS
         BALR  R14,R15            CALL RECOGNIZER
         TM    COBFLAGS,COBSUB    HANDLING SUBSCRIPT?
         BNZ   SYNRET(R15)        YES, SKIP OPER CHECK
         TM    OPERFLGS,OPERACT   WITHIN AN EXPRESSION?
         BNZ   EXPRRET(R15)       YES, ENTER EXPRESSION ROUTINE
         B     *+4(R15)           WAS THIS PARM FOUND?
SYNRET   B     SYNOK1             YES.
         B     SYNOMIT            NO, IT IS MISSING
         B     SYNINV             YES, BUT IT IS INVALID
         BAL   R6,PARSRC12        SHOULD NOT OCCUR HERE
         B     SYNCORCT           PROMPTING FOR TERM SUCCESSFUL
         B     SYNBAD             PROMPTING FOR TERM UNSUCCESSFUL
         SPACE
*
*        CHECK FOR SUBSCRIPTS IF REQUESTED BY RECOGNIZER
*
SYNOK1   TM    COBFLAGS,COBSUBOK  MAY THERE BE A SUBSCRIPT?
         BZ    SYNNOSUB           NO.
SYNSUBS  BAL   R14,SUBSIN         YES, CALL SUBSCRIPT ROUTINE TO CHECK
         SPACE
*
*        GET A PASSWORD IF REQUESTED BY RECOGNIZER
*
SYNNOSUB TM    PASSF,PASSABLE     MAY THIS PARM HAVE A PASSWORD?
         BZ    SYNVEX             NO.
         L     R2,PASSPDE         YES, ADDR PASSWORD PDE PART
         BAL   R14,PASS           PROCESS PASSWORD
         SPACE
*
*        INTERFACE TO VALIDITY CHECK EXIT
*
SYNVEX   BAL   R14,VEX            INTERFACE TO VALIDITY CHECK EXIT
         B     *+4(R15)           WHAT HAPPENED?
         B     SYNVAL             EVERYTHING'S OK
         B     SYNCORCT           REPLACEMENT DATA ENTERED
         B     SYNMISS            UNABLE TO FIX IT UP
         LR    R2,R1              SUBSCRIPT REQUIRED, STACK (
         MVC   ALIGN,=H'1'
         LA    R1,=C'('
         OI    INSFLAG,DUMMYSUB   SHOW PUSHING DUMMY SUBSCRIPT
         L     R15,=A(CLUZPUSH)
         BALR  R14,R15
         LR    R1,R2              RESTORE R1
         OI    COBFLAGS,COBIMMSB  SHOW SUBSCRIPT NEARBY
         B     SYNSUBS            GO GET SOME SUBSCRIPTS
         SPACE
SYNMISS  OI    RANGEF,RANGE1NV    NOTE PROMPT FAILURE
         SPACE
*
*        CHECK FOR & HANDLE THE SECOND PART OF A RANGE
*
SYNVAL   TM    1(R9),X'20'        RANGE PERMITTED?
         BZ    SYNOK              NO.
         BAL   R14,RANG           YES, LOOK FOR SECOND PART
         B     *+4(R15)           WHAT HAPPENED?
         B     SYNOK              ALL IS WELL
         B     SYNRETRY           REPLACEMENT DATA PROVIDED
*                                 ELSE PROMPT FAILURE
         SPACE
*
*        SKIP TO NEXT PCE
*
SYNOK    XC    OMIT1,OMIT1        CLEAR BACKUP POINT
         MVI   PCBYPASS,X'00'     RESET PCE BYPASS FLAG
         MVI   BYPASS,0
         TM    COBFLAGS,COBSUB    IN SUBSCRIPT MODE?
         BNZ   SYNOKSUB           YES, SPECIAL CASE
         TM    DEFTF,DEFTPOS      WAS THIS A DEFAULT VALUE?
         BNZ   DEFTRET            YES, RETURN TO DEFAULT ROUTINE
SYNACPT  MVI   NESTLEVL,0         CANCEL ALL NESTING
         TM    ALCFLAGS,ACPTREQ   IS ACCEPT REQUIRED?
         BZ    SYNNEXT            NO.
         L     R15,=A(CLUZACPT)
         BALR  R14,R15            YES, TELL MEMORY MANAGER TO ACCEPT
SYNNEXT  BAL   R14,FNXT           FIND THE NEXT PCE
         LR    R9,R1              COPY TO USUAL REGISTER
         B     SYNLOOP
         SPACE
*
*        HANDLE OMITTED POSITIONAL
*
SYNOMIT  TM    COBFLAGS,COBSUB    IN A SUBSCRIPT LIST?
         BNZ   SYNOSUBS           YES, HANDLE SUBSCRIPT OMISSION
SYNORMIT TM    0(R9),X'10'        IS THIS REQUIRED?
         BNZ   SYNRQR             YES.
         OC    OMIT1,OMIT1        NO, IS THIS THE FIRST MISSING?
         BNZ   SYNNEXT            NO.
         ST    R9,OMIT1           YES, SET BACKUP POINT
         B     SYNNEXT
         SPACE
*
*        HANDLE PROMPT
*
SYNRQR   CR    R8,R7              ANYTHING LEFT TO REJECT?
         BNH   SYNFINV            YES, REJECT IT
SYNNOMIT BAL   R14,FDEF           NO, FIND THE PROMPT STRING
         L     R15,=A(CLUZREQR)
         BALR  R14,R15            FORCE ENTRY
         B     *+4(R15)           HOW DID IT GO?
         B     SYNRETRY           GREAT, TRY AGAIN
         B     SYNOK              COULDN'T PROMPT, SKIP TO NEXT
         SPACE
*
*        DECIDE WHAT TO REJECT FOR MISSING REQUIRED POSITIONAL
*
SYNFINV  CLI   0(R8),C')'         DOES A ) FOLLOW?
         BNE   SYNITEM            NO, FIND ITEM TO REJECT
         CLI   SUBFLAG,0          YES, ARE WE IN A SUBFIELD?
         BNE   SYNNOMIT           YES, NO REJECT, JUST PROMPT
SYNITEM  L     R15,=A(CLUZITEM)
         BALR  R14,R15            FIND TEXT TO REJECT
         C     R3,RECOGS+4        INVALID STRING?
         BNE   SYNINV             NO.
         CLI   DELIM,X'FF'        YES, WAS DELIMITER MISSING?
         BNE   SYNINV             NO.
         MVI   DELIM,0
         MVC   VMSG,=A(NODELIM)   YES, GIVE CLARIFYING LEVEL2 MSG
         SPACE
*
*        HANDLE INVALID POSITIONAL
*
SYNINV   BAL   R14,INV            GIVE INVALID/REENTER
         MVI   BYPASS,X'00'       RESET BYPASS FLAG
         B     *+4(R15)           WAS THE PROMPT OK?
         B     SYNCORCT           YES, TRY AGAIN
*                                 NO, JUST FORGET THIS
         SPACE
*
*        IF A PASSWORD OR SUBSCRIPT WAS POSSIBLE FOR AN UNCORRECTED
*        INVALID PARAMETER, CHECK FOR IT TO AVOID MISTAKING IT FOR
*        SOMETHING ELSE.
*
         TM    PASSF,PASSABLE     PASSWORD PERMITTED?
         BZ    SYNMISS            NO.
         TM    PASSF,PASSCAN      YES, ALREADY SCANNED FOR IT?
         BNZ   SYNMISS            YES.
         L     R2,PASSPDE         NO, FIND PASSWORD PDE
         MVI   LEFTOVER,X'FF'     SET PASSWORD FOR INVALID PARM FLAG
         BAL   R14,PASS           CHECK FOR A PASSWORD
         B     SYNPAST
SYNBAD   TM    COBFLAGS,COBSUBOK  SUBSCRIPT POSSIBLE HERE?
         BZ    SYNMISS            NO.
         MVI   LEFTOVER,X'FF'     YES, SET LEFTOVERS FLAG
         BAL   R14,SUBSIN         CHECK FOR DANGLING SUBSCRIPT
SYNPAST  MVI   LEFTOVER,0         RESET LEFTOVER PASSWORD FLAG
         B     SYNMISS            NOW TREAT AS MISSING
         SPACE
SYNCORCT TM    COBFLAGS,COBSUB    WORKING ON SUBSCRIPT?
         BNZ   SYNCLSUB           YES.
         LR    R2,R1              NO, SAVE PDE ADDRESS
         BAL   R14,SIZE           GET SIZE OF PDE
         BCTR  R1,0
         EX    R1,SYNCLR          CLEAR OUT THE PDL
         B     SYNRETRY
SYNCLSUB XC    0(19,R1),0(R1)     CLEAR OUT SUBSCRIPT PDE
         SPACE
SYNRETRY OC    OMIT1(4),OMIT1     ANY PREVIOUS ADJACENT PARMS OMITTED?
         BZ    SYNPOS2            NO.
         L     R9,OMIT1           YES, BACKUP TO 1ST TO RETRY
         MVC   OPERPART,OMITPART  RESTORE PREVIOUS EXPRESSION PART
         TM    OPERFLGS,OPERACT+OPEREXPR    HANDLING A LEVEL 88 ITEM?
         BNM   SYNPOS2            NO, LEAVE OPERFLGS ALONE
         TM    COBFLAGS,COBSUB    YES, IN A SUBSCRIPT?
         BNZ   SYNPOS2            YES, JUST PROCEED
         MVI   OPERFLGS,0         NO, LEAVE EXPRESSION MODE
         B     SYNPOS2
SYNCLR   XC    0(0,R2),0(R2)      CLEAR THE PDE FOR BAD PARM
         SPACE
*
*        HANDLE A VALID SUBSCRIPT. IF THIS IS THE THIRD ONE, GO TO
*        IGNORE ANY FURTHER ONES. IF NOT, GO PROCESS ANOTHER.
*
SYNOKSUB SR    R14,R14
         IC    R14,SUBSNO         GET NUMBER OF SUBSCRIPTS
         LA    R14,1(,R14)        INCREASE BY 1
         STC   R14,SUBSNO         STORE BACK
         CLI   SUBSNO,3           REACHED THE MAX?
         BE    SYNPEND            YES, EXIT
         L     R14,SUBSPDE        NO, BUMP THE PDE POINTER
         LA    R14,20(,R14)       BY 1 SUBSCRIPT
         ST    R14,SUBSPDE
         B     SYNPOS2            AND GO AGAIN
         SPACE
*
*        A SUBSCRIPT SEEMS TO BE MISSING. REJECT WHATEVER IS THERE, OR
*        PROMPT FOR ONE, IF NOTHING IS PRESENT.
*
SYNOSUBS CR    R8,R7              ANY TEXT LEFT?
         BH    SYNSBOUT           NO.
         CLI   0(R8),C')'         YES, HAVE WE A CLOSE PAREN?
         BNE   SYNITEM            NO, REJECT WHATEVER WE HAVE
SYNSBOUT CLI   SUBSNO,0           HAVE WE HAD ANY SUBSCRIPTS?
         BNE   SUBSOUT            YES, SUBSCRIPTS FINISHED
         L     R15,=A(CLUZRQSB)
         BALR  R14,R15            NO, CALL REQUIRE-SUBSCRIPT ROUTINE
         B     *+4(R15)           WAS IT CORRECTED?
         B     SYNPOS2            YES, TRY AGAIN
         B     SYNOKSUB           NO, JUST PUSH ON
         TITLE 'CLUTSPAR -- COBOL EXPRESSION HANDLING'
*
*        THIS CODE CONTROLS THE RECOGNITION AND VALIDATION OF SUBPARTS
*        OF A COBOL EXPRESSION. IT IS ENTERED UPON RETURN FROM THE
*        RECOGNIZER FOR A SUBPART, AS PROCESSING IS NORMAL UP TO THAT
*        POINT.
*
EXPRRET  B     EXPROK1            SUBPART RECOGNIZED
         B     EXPROMIT           SUBPART OMITTED
         B     EXPRINV            SUBPART INVALID
         B     SYNPOS2            GO PROCESS 1ST OPER SUBPART
         B     SYNRETRY           BACK UP & RETRY
         B     EXPRBAD            RECOGNIZER PROMPTING FAILED
         SPACE
*
*        SEE IF A SUBSCRIPT IS ALLOWED FOR THIS ITEM. IF SO, GO CHECK
*        FOR ONE.
*
EXPROK1  TM    COBFLAGS,COBSUBOK  COULD A SUBSCRIPT APPEAR?
         BZ    EXPRVEX            NO, GO VALIDATE
EXPRQSUB BAL   R14,SUBSIN         SEE IF SUBSCRIPT IS THERE
         SPACE
*
*        INTERFACE TO THE USER'S VALIDITY CHECK ROUTINE. IF THE
*        EXIT INDICATES THAT A SUBSCRIPT IS REQUIRED, IT MAY BE
*        SUFFICENT TO CALL THE SUBSCRIPT ROUTINE AGAIN, IF AN
*        AMBIGUOUS PARENTHESIS WAS FOUND NEAR THE VARIABLE.
*
EXPRVEX  BAL   R14,VEX            CALL VALIDATION ROUTINE
         MVI   RANGEF,0           MAKE SURE RANGE2 INDICATOR IS OFF
         B     *+4(R15)           WHAT HAPPENED?
         B     EXPROK             NO TROUBLE
         B     EXPRCRCT           NEW DATA OBTAINED, RE-RECOGNIZE
         B     EXPRMISS           PROMPT FAILED
         TM    COBFLAGS,COBSUB88  SUBSCRIPT NEEDED. ONE NEARBY?
         BNZ   EXPRQSUB           YES, GO GET IT
         LR    R2,R1
         MVC   ALIGN,=H'1'        NO, FORCE ONE TO BE ENTERED
         LA    R1,=C'('
         OI    INSFLAG,DUMMYSUB   SHOW STACKING DUMMY SUBSCRIPTS
         L     R15,=A(CLUZPUSH)   BY STACKING AN EMPTY LIST
         BALR  R14,R15
         LR    R1,R2
         OI    COBFLAGS,COBIMMSB  PROMISE IMMEDIATE SATISFACTION
         B     EXPRQSUB
         SPACE
*
*        IF WE ARE HANDLING A DEFAULT SUBPART, MAKE SURE NO INFO
*        IS LEFT OVER FROM THE DEFAULT.
*
EXPROK   TM    DEFTF,DEFTOPER     DEFAULTING SUBPARTS?
         BZ    EXPRNORM           NO.
         L     R15,=A(CLUZSKIP)   YES, SET UP TO SKIP BLANKS
         BALR  R14,R15
         LTR   R15,R15            ANYTHING LEFT?
         BNZ   EXPRPOPD           NO, EVERYTHING'S COOL
         BAL   R6,PARSRC12        YES, INDICATE PARM LIST ERROR
EXPRPOPD L     R15,=A(CLUZPOPS)
         BALR  R14,R15            REMOVE DEFAULT FROM STACK
         B     EXPRGOON           JOIN NON-DEFAULT CASE
         SPACE
*
*        KEEP TRACK OF THE FIRST AND LAST TOKENS FOUND IN THE
*        EXPRESSION IN CASE IT IS NEEDED FOR A LATER MESSAGE. THIS
*        INFORMATION IS STORED AS IF IT APPLIED TO THE SECOND HALF
*        OF A RANGE.
*
EXPRNORM CLI   OPERPART,8         HANDLING THE WHOLE EXPRESSION?
         BE    EXPRGOON           YES, SKIP THIS
         OC    COBHEAD2,COBHEAD2  HAD WE ANYTHING BEFORE?
         BNZ   EXPR2              YES.
         TM    COBFLGS1,COBOLVAR  NO, IS WHAT WE HAVE A VARIABLE?
         BZ    EXPR1NVR           NO.
         MVC   COBHEAD2(8),COBHEAD1    YES, SAVE INFO
         NI    COBFLGS2,255-COBHBYP    COPY HEAD BYPASS FLAG OVER
         MVI   ALIGN,COBHBYP
         NC    ALIGN(1),COBFLGS1
         OC    COBFLGS2,ALIGN
         TM    COBFLGS1,COBQVAR   IS THERE A TAIL TO THIS ONE?
         BZ    EXPRTSUB           NO.
EXPR2QVR MVC   COBTAIL2(8),COBTAIL1    YES, PRESERVE IT
EXPRTAIL OI    COBFLGS2,COBQVAR   INDICATE TAIL THERE
         NI    COBFLGS2,255-COBTBYP    COPY TAIL BYPASS FLAG OVER
         MVI   ALIGN,COBTBYP
         NC    ALIGN(1),COBFLGS1
         OC    COBFLGS2,ALIGN
EXPRTSUB NI    COBFLGS2,255-COBHASUB   COPY SUBSCRIPT EXISTENCE FLAG
         MVI   ALIGN,COBHASUB
         NC    ALIGN(1),COBFLGS1
         OC    COBFLGS2,ALIGN
         B     EXPRGOON
EXPR1NVR MVC   COBHEAD2,PREVPOS   GET START & END FOR NON-VAR
         ST    R8,COBHEAD2+4
         CLI   BYPASS,0           WAS IT HIDDEN?
         BE    EXPRGOON           NO.
         OI    COBFLGS2,COBHBYP   YES, SET HEAD BYPASS
         B     EXPRGOON
         SPACE
EXPR2    OI    COBFLGS2,COBQVAR   EXPR MUST NOW BE CONSIDERED          *
                                  QUALIFIED
         TM    COBFLGS1,COBOLVAR  IS THIS PART A VARIABLE?
         BZ    EXPR2NVR           NO.
         TM    COBFLGS1,COBQVAR   YES, QUALIFIED?
         BNZ   EXPR2QVR           YES, ITS TAIL IS OUR TAIL
         MVC   COBTAIL2(8),COBHEAD1    NO, ITS HEAD IS OUR TAIL
         B     EXPRTSUB           GO HANDLE SUBSCRIPTS
EXPR2NVR MVC   COBTAIL2,PREVPOS   START START & END OF NON-VAR
         ST    R8,COBTAIL2+4
         NI    COBFLGS2,255-COBHASUB   RESET SUBSCRIPT INDICATOR
         CLI   BYPASS,0           SUPPRESSION NEEDED?
         BE    EXPR2NBY           NO.
         OI    COBFLGS2,COBTBYP   YES, SET FLAG
         B     EXPRGOON
EXPR2NBY NI    COBFLGS2,255-COBTBYP    MAKE SURE TAIL CAN BE SEEN
         SPACE
*
*        FIND THE NEXT SUBPART TO BE PROCESSED.
*
EXPRGOON XC    OMIT1,OMIT1        RESET OMISSION STUFF
         MVI   OMITPART,0
         MVI   BYPASS,0           FORGET PREVIOUS SUPPRESSION
         SR    R14,R14
         TM    DEFTF,DEFTOPER     APPLYING DEFAULTS?
         BNZ   EXPRNEXT           YES, SET NO PRESENCE FLAG
         LA    R14,1              CAUSE SUBPART PRESENCE TO SHOW
EXPRNEXT SR    R15,R15
         IC    R15,OPERPART       FIND THE PART NUMBER
         SLL   R14,0(R15)         GET THE BIT TO SET
         EX    R14,EXPRSET        SET BIT IF PART PRESENT (NOT DEFAULT)
         SLL   R15,1              FOR BETTER BRANCHING
         B     *+4(R15)           DECIDE WHAT NOW
EXPRVEC  B     EXPRATOR           OPERATOR DONE
         B     EXPRAND1           OPERAND1 DONE
         B     EXPRAND2           OPERAND2 DONE, LOOK FOR EXTRANIA
         B     EXPR88             CHAINED TERM DONE
         B     EXPRFULL           FULL EXPRESSION COMPLETE
         SPACE
*
*        IF THE FIRST OPERAND OR THE OPERATOR IS DONE WITH, JUST
*        PROCEED TO THE NEXT PART IN LINE.
*
EXPRAND1 MVI   OPERPART,0         NUMBER OF OPERATOR
EXPRCONT TM    DEFTF,DEFTOPER     DEFAULTING NOW?
         BZ    SYNNEXT            NO, GO ON
         B     EXPRDNXT           YES, RETURN TO DEFAULTER
         SPACE
EXPRATOR MVI   OPERPART,4         SET OPERAND2 PART NO
         B     EXPRCONT
         SPACE
*
*        A LEVEL 88 ITEM HAS BEEN FINISHED WITH. IF IT WAS INDEED
*        PRESENT, GO TO VALIDATE IT CONSIDERED AS A FULL EXPRESSION.
*
EXPR88   TM    OPERPRES,X'40'     DID USER SUPPLY IT?
         BZ    EXPRNO88           NO.
EXPRFIN  NI    DEFTF,255-DEFTOPER YES, MAKE SURE NOT DEFAULTING
         L     R9,OPERPCE         FIND THE SOURCE
         OI    COBFLAGS,COBFORM   PREPARE FOR FAILURE
         MVC   ALIGN,4(R9)        LOCATE ORIGINAL PDE
         LH    R1,ALIGN
         A     R1,PDLPTR
         OI    6(R1),X'80'        INDICATE PRESENCE
         MVI   OPERPART,8         INDICATE VALIDATING WHOLE THING
         TM    OPERFLGS,OPERFAIL  PROMPT ALREADY FAILED?
         BNZ   EXPROK             YES, SKIP VALIDATE
         OI    RANGEF,RANGE2      USE SECOND PART STUFF IN ERROR MSG
         MVI   NESTLEVL,LEVRANGE  SET NESTING LEVEL TO "RANGE"
         B     EXPRVEX            VALIDATE THE WHOLE EXPRESSION
         SPACE
*
*        IF AN EXPECTED CHAINED TERM WAS NOT FOUND, DEFAULT IT IF
*        POSSIBLE. IF NOT, TREAT IT AND THE EXPRESSION AS NORMAL
*        OMITTED ITEMS.
*
EXPRNO88 TM    0(R9),X'08'        DOES IT HAVE A DEFAULT?
         BNZ   EXPRDEFT           YES, USE IT
         MVI   OPERFLGS,0         NO, LEAVE OPER MODE
         L     R9,OMIT1           FIND PCE OF BACKUP POINT
         B     SYNNEXT            AND BE OFF
         SPACE
*
*        COME HERE TO USE THE DEFAULT VALUE FOR AN IKJOPER SUBPART.
*
EXPRDEFT OI    DEFTF,DEFTOPER+DEFTINS  INDICATE OPER DEFAULT MODE
         BAL   R14,FDEF           FIND THE DEFAULT
         BAL   R14,INS            STACK IT
         XC    OMIT1(4),OMIT1     MAKE SURE NO BACKUP
         B     SYNPOS2            AND GO TRY THE DEFAULT
         SPACE
*
*        THE FULL EXPRESSION HAS BEEN HANDLED. SET UP TO RESUME WITH
*        THE NEXT PCE NOT WITHIN THE EXPRESSION, AND RETURN TO NORMAL
*        PROCESSING.
*
EXPRFULL TM    DEFTF,DEFTPOS      WAS THE WHOLE EXPR DEFAULTED?
         BNZ   DEFTRET            YES, RETURN TO DEFAULTER
         L     R15,OPEROFFS       FIND THE LIST OF OFFSETS
         XC    ALIGN,ALIGN
         OC    ALIGN,6(R15)       WAS LEVEL 88 ALLOWED?
         BNZ   EXPRF88            YES.
         MVC   ALIGN,4(R15)       NO, OPERAND2 ENDS THE EXPR
EXPRF88  LH    R9,ALIGN
         A     R9,PCL             FIND LAST PCE OF EXPRESSION
         MVI   OPERFLGS,0         LEAVE OPER MODE BEHIND
         B     SYNACPT            ON TO NEXT REAL ITEM
         SPACE
*
*        AFTER THE SECOND OPERAND HAS BEEN HANDLED, CHECK FOR OMITTED
*        SUBPARTS WITH DEFAULTS. IF ANY ARE FOUND, APPLY THE DEFAULT
*        VALUES.
*
EXPRAND2 TM    DEFTF,DEFTOPER     WERE WE DEFAULTING?
         BZ    SYNPEND            NO, LOOK FOR CLOSE PAREN             *
                                  (AND RETURN TO EXPROVER)
         OI    OPERFLGS,OPERINV   YES, SET FLAG FOR VALIDATION ERROR
         CR    R8,R7              IS CLOSE PAREN STILL LEFT?
         BH    EXPRFIN            NO.
         OI    OPERFLGS,OPERCLS   YES, ADD PAREN TO MSGS
         AR    R8,R6              SKIP OVER THE PARENTHESIS
         B     EXPRFIN            GO TO FINISH WHOLE OPER
EXPROVER MVI   OPERPART,2         CHECK OPERAND1 FOR DEFAULT
         OI    DEFTF,DEFTOPER     NOTE DEFAULT PROCESS BEGUN
         SPACE
*
*        HANDLE THE NEXT PART TO BE DEFAULTED.
*
EXPRDNXT L     R1,OPEROFFS        FIND THE SUBPART OFFSETS
         SR    R9,R9
         IC    R9,OPERPART
         LA    R14,1
         SLL   R14,0(R9)          FIND WHICH BIT TO TEST
         LA    R15,0(R9,R9)       DOUBLE SUBPART NUMBER
         LH    R9,0(R1,R9)
         A     R9,PCL             FIND PCE FOR NEXT PART
         EX    R14,EXPRTEST       WAS THIS PART ENTERED?
         BNZ   EXPRVEC(R15)       IF PRESENT, PROCEED TO THE NEXT
         TM    0(R9),X'08'        HAS THIS PART A DEFAULT?
         BNZ   EXPRDEFT           YES, GO TO TRY IT OUT
         BAL   R6,PARSRC12        NO, A MIRACLE HAS OCCURRED
         SPACE
*
*        WHEN WE GET HERE, A PART HAS BEEN OMITTED. IF IT HAS A
*        DEFAULT, ALLOW THE OMISSION FOR NOW. OTHERWISE, PROMPT
*        FOR THE PART.
*
EXPROMIT TM    0(R9),X'08'        HAS THIS PART A DEFAULT?
         BZ    EXPRNVAL           NO, MUST BE GOTTEN
         SR    R14,R14            YES, SET NO PRESENCE BIT
         TM    DEFTF,DEFTOPER     WERE WE DEFAULTING?
         BZ    EXPROKOM           NO, ALLOW OMISSION
         BAL   R6,PARSRC12        YES, CALLER HAS BLOWN IT
EXPROKOM OC    OMIT1,OMIT1        IS THIS THE FIRST OMISSION?
         BNZ   EXPRNEXT           NO.
         ST    R9,OMIT1           YES, STORE BACKUP POINT
         MVC   OMITPART,OPERPART  SAVE PART NUMBER
         B     EXPRNEXT           AND TRY THE NEXT PART
         SPACE
*
*        THIS PART MUST BE REQUIRED. IF A PROMPT IS SUPPLIED, USE IT.
*        OTHERWISE, PROMPT FOR IT BY FUNCTION, AS IN
*        "ENTER FIRST OPERAND".
*
EXPRNVAL TM    OPERFLGS,OPEREXPR  LEVEL 88 PART MISSING?
         BNZ   EXPRRQR            NO.
         TM    0(R9),X'10'        YES, IS IT REQUIRED?
         BZ    EXPROKOM           NO, JUST LET IT PASS
         CR    R8,R7              YES, IS ANYTHING LEFT?
         BNH   EXPRITEM           YES, REJECT SOMETHING
         B     EXPROMPT           NO, ISSUE FORMAL PROMPT
         SPACE
EXPRRQR  CR    R8,R7              ANYTHING LEFT TO REJECT?
         BNH   EXPRFINV           YES, REJECT IT.
EXPRFRCE TM    0(R9),X'10'        NO, IS THIS FORMALLY REQUIRED?
         BZ    EXPRINC            NO, GIVE INCOMPLETE MESSAGE
EXPROMPT BAL   R14,FDEF           FIND THE PROMPT STRING
         L     R15,=A(CLUZREQR)
         BALR  R14,R15            REQUEST NEW DATA
EXPRQRET B     *+4(R15)           DID WE GET IT?
         B     SYNRETRY           YES, TRY AGAIN
         SPACE
*
*        IF A SUBSCRIPT IS POSSIBLE FOR A PERMANENTLY INVALID TERM,
*        CHECK FOR ONE TO AVOID MISTAKING IT FOR SOMETHING ELSE.
*
EXPRBAD  TM    COBFLAGS,COBSUBOK  SUBSCRIPT POSSIBLE HERE?
         BZ    EXPRMISS           NO.
         MVI   LEFTOVER,X'FF'     YES, SET LEFTOVERS FLAG
         BAL   R14,SUBSIN         CHECK FOR DANGLING SUBSCRIPT
         MVI   LEFTOVER,0         RESET LEFTOVER SUBSCRIPT FLAG
EXPRMISS OI    OPERFLGS,OPERFAIL  NOTE PROMPTING FAILED
         B     EXPRNORM           AND PROCEED TO NEXT PART
         SPACE
EXPRINC  BAL   R14,XINC           GIVE INCOMPLETE EXPR PROMPT
         B     EXPRQRET(R15)      AND ACT ON RETURN CODE
         SPACE
EXPRFINV CLI   0(R8),C')'         DOES THE EXPRESSION CLOSE HERE?
         BE    EXPRFRCE           YES, TREAT EXPRESSION AS INCOMPLETE
EXPRITEM L     R15,=A(CLUZITEM)   NO, FIND ITEM TO REJECT
         BALR  R14,R15
         SPACE
*
*        COME HERE TO REJECT A BAD SUBPART AND GET A REPLACEMENT VALUE.
*
EXPRINV  BAL   R14,INV            ISSUE "INVALID" PROMPT
         MVI   BYPASS,0
         B     *+4(R15)           DID WE GET SOMETHING?
         B     EXPRCRCT           YES, GO TRY AGAIN
         B     EXPRMISS           NO, SIGH AND CONTINUE
         SPACE
EXPRCRCT LR    R2,R1
         C     R9,OPERPCE         WHOLE EXPRESSION REJECTED?
         BE    EXPRCFUL           YES.
         BAL   R14,SIZE           NO, GET SIZE OF ELEMENT'S PDE
         B     EXPRCLR
EXPRCFUL MVI   OPERFLGS,0         ALLOW EXIT FROM OPER MODE
         MVC   OMIT1,OPEROMIT     RESTORE ORIGINAL BACKUP POINT
         LH    R1,OPERSIZE        GET SIZE OF ALL OPER PARTS
EXPRCLR  BCTR  R1,0
         EX    R1,SYNCLR          WIPE THE PDE CLEAN
         B     SYNRETRY           NOW GO TRY WITH NEW DATA
         SPACE
EXPRSET  OI    OPERPRES,X'00'     SET A SUBPART PRESENCE BIT
EXPRTEST TM    OPERPRES,X'00'     TEST A SUBPART PRESENCE BIT
         TITLE 'CLUTSPAR -- POSITIONAL LIST HANDLING'
*
*        HANDLE LISTS OF POSITIONALS
*
LIST     LR    R0,R1              SAVE REGISTER OVER CALL
         BAL   R14,SIZE           GET PDE SIZE
         TM    1(R9),X'20'        RANGE PERMITTED?
         BZ    LISTNRNG           NO.
         AR    R1,R1              YES, DOUBLE SIZE
LISTNRNG STH   R1,LISTSIZE        SAVE FOR REFERENCE
         LR    R14,R1             RESHUFFLE REGISTERS
         AR    R14,R0
         MVC   0(4,R14),NULL      STORE END OF CHAIN
         CLI   SUBFD1,0           CHECK FOR 1ST ITEM IN SUBFIELD
         BNE   LISTSUB            YES.
LISTNSUB LR    R1,R0              RESTORE PDL POINTER
         CLI   0(R8),C'('         CHECK FOR EXPLICIT PARENTHESIS
         BNE   SYNRECG            ONLY 1 ITEM GIVEN
         MVI   SUBLIST,0          TURN OFF ITEM 1 FLAG
         XC    COMMAX,COMMAX      PREPARE FOR LEADING COMMA
         AR    R8,R6              SKIP PAST PARENTHESIS
         B     LISTENT
         SPACE
*
*        ENTRY FOR LONE LIST SUBFIELD
*
LISTSUB  MVI   SUBFD1,0           TURN OFF 1ST ITEM FLAG
         BAL   R14,FNXT           FIND THE NEXT PCE
         TM    0(R1),X'E0'        SEE IF THERE IS ONE
         BNZ   LISTNSUB           IF SO, PARENTHESIS IS REQUIRED
         MVI   SUBLIST,X'FF'      ALLOW MULTIPLE ITEMS W/OUT PAREN
LISTENT  MVI   LISTF,X'FF'        NOTE WORKING IN LIST
         LR    R1,R0
         SPACE
*
*        FIND THE FIRST LIST ELEMENT
*
LISTGO   LR    R3,R15             SAVE RECOGNIZER ENTRY
         L     R15,=A(CLUZSKIP)
         BALR  R14,R15            SKIP DELIMITERS
         B     *+4(R15)           ANYTHING AFTER?
         B     LISTTEXT           YES.
         B     LISTEMT            NO, EMPTY LIST
         SPACE
LISTTEXT CLI   0(R8),C')'         LIST ENDED HERE?
         BE    LISTOMIT           YES.
         SPACE
*
*        RECOGNIZE A LIST ELEMENT
*
LISTRECG LR    R15,R3             RESTORE RECOGNIZER ENTRY
         ST    R8,PREVPOS         SAVE STARTING POINT
         OC    BYPASS,PCBYPASS    ESTABLISH BYPASS FLAG
         MVI   PASSF,0            TURN OFF PASSWORD FLAGS
         BALR  R14,R15            CALL RECOGNIZER
         B     *+4(R15)           HOW'D IT GO?
         B     LISTOK1            GOT SOMETHING
         B     LISTFINV           NOT FOUND
         B     LISTINV            SOMETHING EVIL FOUND
         BAL   R6,PARSRC12
         BAL   R6,PARSRC12        SHOULD NOT OCCUR
         B     LISTBAD            UNABLE TO PROMPT FOR IKJTERM
         SPACE
*
*        HANDLE SUBCRIPTS IF REQUESTED
*
LISTOK1  TM    COBFLAGS,COBSUBOK  MAY A SUBSCRIPT APPEAR?
         BZ    LISTNSBS           NO.
LISTSUBS BAL   R14,SUBSIN         YES, CHECK IT OUT
         SPACE
*
*        HANDLE PASSWORD IF REQUESTED
*
LISTNSBS TM    PASSF,PASSABLE     MAY A PASSWORD APPEAR?
         BZ    LISTVEX            NO.
         L     R2,PASSPDE         YES, ADDRESS PDE PORTION
         BAL   R14,PASS           HANDLE PASSWORD
*
*        CALL VALIDATION EXIT FOR LIST ELEMENT
*
LISTVEX  BAL   R14,VEX            CALL USER VALIDATION ROUTINE
         B     *+4(R15)           WHAT HAPPENED?
         B     LISTVAL            ALL IS WELL
         B     LISTCRCT           NEW INFORMATION AVAILABLE
         B     LISTMISS           UNABLE TO PROMPT
         LR    R2,R1              SAVE R1 OVER STACK CALL
         MVC   ALIGN,=H'1'        SUBSCRIPT REQUIRED
         LA    R1,=C'('           STACK ( TO FORCE ENTRY
         OI    INSFLAG,DUMMYSUB   SHOW DUMMY SUBSCRIPT
         L     R15,=A(CLUZPUSH)
         BALR  R14,R15
         LR    R1,R2
         OI    COBFLAGS,COBIMMSB  SHOW SUBSCRIPT RIGHT THERE
         B     LISTSUBS           GO PROCESS EMPTY SUBSCRIPT LIST
         SPACE
LISTMISS OI    RANGEF,RANGE1NV    NOTE PROMPT FAILURE
         SPACE
*
*        CHECK FOR AND HANDLE SECOND PART OF RANGE
*
LISTVAL  TM    1(R9),X'20'        IS A RANGE ALLOWED?
         BZ    LISTOK             NO.
         BAL   R14,RANG           YES, HANDLE 2ND HALF OF RANGE
         B     *+4(R15)           HOW'D IT GO?
         B     LISTOK             NO PROBLEMS
         B     LISTRTRY           START OVER AGAIN
*                                 ELSE PROMPT FAILED
         SPACE
*
*        MOVE VALUE TO DYNAMIC AREA FOR ELEMENT>1
*
LISTOK   LA    R1,0(,R1)
         LA    R2,LISTEMP
         CR    R1,R2              IS THIS THE 1ST ELEMENT?
         BNE   LISTFRST           YES.
         LH    R2,LISTSIZE        NO, GET A NEW PDE
         LA    R1,4(,R2)
         L     R15,=A(CLUZALOC)
         BALR  R14,R15
         L     R1,NEWMEM          GET ADDR OF NEW PDE
         L     R14,PREVLIST
         ST    R1,0(R2,R14)       CHAIN TO PREVIOUS ELEMENT
         LA    R15,0(R2,R1)
         MVC   0(4,R15),NULL      CLOSE CHAIN AFTER
         ST    R1,PREVLIST        MAKE THIS THE NEW LAST ELEMENT
         BCTR  R2,0
         EX    R2,LISTMOVE        COPY VALUE TO ELEMENT
         B     LISTNEXT
LISTFRST ST    R1,PREVLIST        STORE ADDR OF 1ST PDE
         SPACE
*
*        SCAN FOR NEXT LIST ELEMENT
*
LISTNEXT LA    R1,LISTEMP         FIND TEMPORARY PDE
         XC    LISTEMP(76),LISTEMP     CLEAR IT
         LH    R2,LISTSIZE
         LA    R2,LISTEMP(R2)
         MVC   0(4,R2),NULL       STORE NULL IN TEMPORARY CHAIN
         MVI   BYPASS,0           OFF BYPASS
         NI    PCBYPASS,X'20'     RESET ALL BYPASS FLAGS BUT PCE'S
LISTRTRY MVI   COBFLAGS,0         RESET COBOL FLAGS
         MVI   COBFLGS1,0
         MVI   COBFLGS2,0
         L     R15,=A(CLUZSKIP)
         BALR  R14,R15            SKIP BLANKS ETC
         B     *+4(R15)           ANYTHING LEFT?
         B     LISTMORE           YES.
         LA    R2,LISTEMP         NO, IS THIS THE 1ST ELEMENT
         CR    R1,R2
         BNE   LISTOMIT           YES.
         B     SYNOK              NO, ACCEPT TERMINATION
LISTMOVE MVC   0(0,R1),LISTEMP    COPY TEMP LIST TO PDE
         SPACE
LISTMORE CLI   0(R8),C')'         FOUND CLOSING PAREN?
         BNE   LISTRECG           NO, RECOGNIZE NEXT ELEMENT
         SPACE
*
*        HANDLE END OF LIST
*
LISTEND  LA    R2,LISTEMP         SEE IF THE 1ST ONE IS MISSING
         CR    R1,R2
         BNE   LISTEMT            YES.
         CLI   SUBLIST,X'FF'      NO, IS A CLOSE REQUIRED?
         BE    SYNOK              NO, LET ) BE
         AR    R8,R6              YES, BUMP PAST IT
         B     SYNOK              AND CONTINUE
         SPACE
*
*        HANDLE EMPTY LIST
*
LISTEMT  AR    R8,R6              SKIP PARENTHESIS
LISTOMIT TM    0(R9),X'10'        IS THE LIST REQUIRED?
         BZ    SYNOK              NO.
         LR    R2,R1              YES, SAVE R1
         BAL   R14,FDEF           FIND THE PROMPT INFO
LISTRQR  L     R15,=A(CLUZREQR)
         BALR  R14,R15            REQUEST INPUT NOW
         LR    R1,R2              RESTORE R1
         B     *+4(R15)           WAS STUFF ENTERED?
         B     LISTRTRY           YES, TRY IT AGAIN
         B     SYNOK              NOPE, GIVE UP
         SPACE
*
*        DECIDE WHAT TO REJECT FOR AN UNRECOGNIZED LIST ELEMENT
*
LISTFINV L     R15,=A(CLUZITEM)   LOOK FOR SOMETHING TO REJECT
         BALR  R14,R15
         SPACE
*
*        HANDLE INVALID LIST ELEMENT
*
LISTINV  BAL   R14,INV            REQUEST CORRECTION
         LTR   R15,R15            SUCCESSFUL PROMPT?
         BNZ   LISTNOFX           NO, MAY BE PASSWORD AFTER
LISTCRCT LH    R2,LISTSIZE        GET SIZE OF PDL
         BCTR  R2,0
         EX    R2,LISTCLR         REINITIALIZE IT
         B     LISTRTRY           YES, GO TRY AGAIN
LISTCLR  XC    0(0,R1),0(R1)      CLEAR BAD PDL
         SPACE
*
*        CHECK FOR PASSWORD OR SUBSCRIPT FOR UNCORRECTABLY BAD
*        ELEMENT
*
LISTNOFX TM    PASSF,PASSABLE     PASSWORD PERMITTED?
         BZ    LISTMISS           NO.
         TM    PASSF,PASSCAN      YES, ALREADY SCANNED FOR IT?
         BNZ   LISTMISS           YES.
         L     R2,PASSPDE         NO, FIND PASSWORD PDE
         MVI   LEFTOVER,X'FF'     SET PASSWORD FOR INVALID PARM FLAG
         BAL   R14,PASS           CHECK FOR A PASSWORD
         B     LISTPAST
LISTBAD  TM    COBFLAGS,COBSUBOK  SUBSCRIPT POSSIBLE HERE?
         BZ    LISTMISS           NO.
         MVI   LEFTOVER,X'FF'     YES, SET LEFTOVERS FLAG
         BAL   R14,SUBSIN         CHECK FOR DANGLING SUBSCRIPT
LISTPAST MVI   LEFTOVER,0         RESET INVALID PARM FLAG
         B     LISTMISS           NOW TREAT AS MISSING
         TITLE 'CLUTSPAR -- END OF PARAMETERS ROUTINE'
*
*        END OF POSITIONALS, NO KEYWORDS. CHECK FOR EXTRA INFO
*
SYNPEND  L     R15,=A(CLUZSKIP)
         BALR  R14,R15            SKIP SPACES ET AL
         B     *+4(R15)           ANYTHING LEFT
         B     SYNXTRA            YES, GIVE WARNING
         B     SYNOMORE           NO, GO DO DEFAULTS
         SPACE
SYNXTRA  L     R15,=A(CLUZXTRA)   ROUTINE TO EXTRACT EXTRA INFO
         CLI   SUBFLAG,0          WITHIN SUBFIELD?
         BNE   SYNOPEN            YES.
         TM    COBFLAGS,COBSUB    WITHIN SUBSCRIPT LIST?
         BNZ   SYNOPEN            YES.
         TM    OPERFLGS,OPERACT+OPEREXPR    WITHIN COMPLEX EXPRESSION?
         BNO   SYNNKEY            NO.
SYNOPEN  CLI   0(R8),C')'         YES, IS THIS A )?
         BE    SYNOMORE           YES, GO DEFAULT
SYNNKEY  OC    OMIT1+1(3),OMIT1+1 IS ANYTHING NEARBY OMITTED?
         BNZ   SYNUNIN            YES, ALLOW REENTRY
         L     R1,=A(IGNORED)     NO, JUST IGNORE IT
         BALR  R14,R15
SYNOMORE TM    COBFLAGS,COBSUB    IN A SUBSCRIPT LIST?
         BNZ   SUBSOUT            YES, RETURN TO SUBSCRIPTED VAR
         B     SYNOVER            NO, CHECK FOR OPER MODE
         SPACE
*
*        PROMPT FOR RE-ENTRY OF EXTRA INFORMATION IF CORRECTION
*        POSSIBLE
*
SYNUNIN  L     R1,=A(UNINTELL)    SAY "UNINTELLIGIBLE INFORMATION"
         BALR  R14,R15
         BAL   R14,REEN           ASK FOR REENTRY
         B     *+4(R15)           DID WE GET SOMETHING?
         B     SYNRETRY           YES, TRY THIS OUT
SYNOVER  TM    OPERFLGS,OPERACT+OPEREXPR    NO, WITHIN AN EXPRESSION?
         BNO   DEFT               NO, GO HANDLE DEFAULTS
         B     EXPROVER           YES, GO HANDLE EXPRESSION END
         TITLE 'CLUTSPAR -- KEYWORD && DEFAULT HANDLING'
*
*        PARSE THE SUPPLIED KEYWORDS
*
SYNKEY   BAL   R14,KYWD           CALL KEYWORD ROUTINE
         B     *+4(R15)           WHAT HAPPENED?
         B     DEFT               NOTHING SPECIAL
         L     R9,OMIT1           BACKUP & RETRY POSITIONALS
         B     SYNLOOP
         SPACE
*
*        APPLY THE DEFAULTS FOR ANY PARAMETERS WHICH HAVE NOT YET
*        BEEN SUPPLIED.
*
DEFT     L     R9,PARM1           FIND THE FIRST PARM
         MVI   DEFTF,0            MAKE SURE OF DEFAULT FLAG
DEFTLOOP TM    0(R9),X'08'        HAS THIS PARM A DEFAULT?
         BZ    DEFTNEXT           NO, TRY THE NEXT
         MVC   ALIGN,4(R9)
         LH    R2,ALIGN
         A     R2,PDLPTR          LOCATE THE PDE
         CLI   0(R9),X'48'        IS THIS A KEYWORD?
         BE    DEFTKEY            YES.
         BAL   R14,SIZE           NO, GET SIZE OF PDE
         LTR   R1,R1              IS IT 0?
         BZ    DEFTNEXT           YES, SKIP IT
         BCTR  R1,0
         EX    R1,DEFTTEST        IS THERE ALREADY A VALUE?
         BNZ   DEFTNEXT           YES, NOTHING TO DO
         STM   R1,R2,DFSAVE       SAVE REGS OVER DEFAULT PARSE
         MVI   DEFTF,DEFTINS+DEFTPOS   SET DEFAULT FLAGS
         BAL   R14,FDEF           FIND DEFAULT VALUE
         BAL   R14,INS            INSERT IT
         XC    OMIT1(4),OMIT1     MAKE SURE NO BACKUP
         B     SYNLOOP            GO PROCESS THIS POSITIONAL
DEFTRET  MVI   NESTLEVL,0         RETURN POINT FROM SYN ROUTINE
         TM    ALCFLAGS,ACPTREQ   STORAGE OWNED BY DEFAULT?
         BZ    DEFTXTRA           NO, NO ACCEPT REQUIRED
         L     R15,=A(CLUZACPT)   YES, SHOW STORAGE TO BE KEPT
         BALR  R14,R15
DEFTXTRA L     R15,=A(CLUZSKIP)
         BALR  R14,R15            SEE IF ANY DEFAULT TEXT LEFT
         LM    R1,R2,DFSAVE       RESTORE REGS
         LTR   R15,R15
         BNZ   DEFTPOP            PROCEED IF NOTHING LEFT
         BAL   R6,PARSRC12        ELSE CALLER ERROR
         SPACE
DEFTKEY  OC    0(2,R2),0(R2)      WAS KEYWORD SPECIFIED?
         BNZ   DEFTNEXT           YES.
         MVI   DEFTF,DEFTINS      SET DEFAULT FLAGS FOR KEYWORD
         BAL   R14,FDEF           FIND DEFAULT VALUE
         BAL   R14,INS            INSERT IT
         XC    OMIT1(4),OMIT1     MAKE SURE NO BACKUP
         LR    R3,R9              SAVE PCE POINTER
         BAL   R14,KYWD           PROCESS KEYWORD DEFAULT
         LR    R9,R3              RESTORE
         LA    R1,1               PDE LENGTH IS 2
         SPACE
DEFTPOP  L     R15,=A(CLUZPOPS)
         BALR  R14,R15            POP OFF DEFAULT VALUE
         EX    R1,DEFTTEST        IS ITEM STILL MISSING?
         BNZ   DEFTNEXT           NO, PROCEED
         BAL   R6,PARSRC12        YES, CALLER ERROR
         SPACE
DEFTNEXT TM    0(R9),X'E0'        IS THIS AN OPER PCE?
         BNO   DEFTNOPR           NO.
         BAL   R14,FDSC           YES, SKIP OVER SUBPART PCE'S
         MVC   ALIGN,0(R1)        SKIP THE DESCRIPTION
         AH    R1,ALIGN
         XC    ALIGN,ALIGN
         OC    ALIGN,6(R1)        IS THERE A CHAINED TERM?
         BNZ   DEFTOP88           YES.
         MVC   ALIGN,4(R1)        NO, FIND OPERAND 2 OFFSET
DEFTOP88 L     R9,PCL
         AH    R9,ALIGN           LOCATE PCE FOR END OF EXPRESSION
         SPACE
DEFTNOPR MVC   ALIGN,2(R9)        ON TO NEXT PARM
         AH    R9,ALIGN
         TM    0(R9),X'E0'        FOUND THE END?
         BZ    DEFTDONE           YES.
         B     DEFTLOOP
DEFTTEST OC    0(0,R2),0(R2)      SEE IF PARM ENTERED
         SPACE
*
*        FOR SUBFIELD PROCESSING, RETURN TO PREVIOUS LEVEL.
*
DEFTDONE MVI   DEFTF,0            DEFAULT PROCESSING DONE
         CLI   SUBFLAG,0          WITHIN SUBFIELD?
         BE    PARSEND            NO, READY TO RETURN
         CR    R8,R7              YES, OUT OF TEXT?
         BH    CURSOUT            YES, FALL BACK A LEVEL
         SPACE
*
*        SKIP PAST CLOSING PARENTHESIS AND RETURN TO PREVIOUS LEVEL
*
SYNUP    AR    R8,R6
         B     CURSOUT            RETURN TO PREVIOUS LEVEL
         TITLE 'CLUTSPAR -- TERMINATION ROUTINE'
*
*        THE PARSE IS FINISHED. IF NO INFORMATION WAS MISSING, UPDATE
*        THE COMMAND BUFFER OFFSET AND RETURN
*
PARSEND  CLI   RC,0               DID ANY PROMPTS FAIL?
         BNE   PARSABRT           YES, BAD RET CODE
         L     R2,ANSPTR          NO, FIND THE ANSWER POINTER
         MVC   0(4,R2),PDLPTR     COPY PDL ADDRESS OVER
         L     R15,=A(CLUZWRAP)   GO RELEASE UNNEEDED MEMORY
         BALR  R14,R15
         L     R1,CBUF            RESET COMMAND OFFSET
         SR    R8,R1
         S     R8,=A(4)
         STH   R8,2(,R1)
         LIEXIT RC=0              RETURN WITH GOOD NEWS
         TITLE 'CLUTSPAR -- ABNORMAL TERMINATION ROUTINE'
*
*        THIS ROUTINE GETS CONTROL WHEN A BAD RETURN CODE IS TO BE
*        RETURNED. IT FREES PARSE STORAGE AND RETURNS
*
PARSABRT LIBEL PARSTERM           TARGET FOR LIGOTO
         L     R15,=A(CLUZWRAP)
         BALR  R14,R15            GIVE BACK ALL MEMORY
         L     R2,ANSPTR
         XC    0(4,R2),0(R2)      ZERO THE ANSWER POINTER
         SR    R15,R15
         IC    R15,RC             GET THE RETURN CODE
         LIEXIT RC=(15)           AND RETURN
         TITLE 'CLUTSPAR -- PASSWORD HANDLING ROUTINE'
*
*        THIS ROUTINE IS CALLED TO HANDLE A POSSIBLE PASSWORD
*        FOLLOWING A POSITIONAL PARAMETER
*
PASS     STM   R14,R1,PWSAVE
         LR    R0,R1              SAVE PROMPT POINTER
         MVC   ORIGBYP,BYPASS     SAVE BYPASS FLAGS
         ST    R8,PASSTART        SAVE WHERE PASSWORD SCAN BEGAN
         MVC   OLDPOS,PREVPOS     AND WHERE DSN/USERID STARTED
PASSRPT  MVC   SKIPSAVE,SKIPF     AND SKIP FLAGS
         MVI   SKIPF,NOSLASH      FORCE NO SLASH HANDLING
         MVI   EXPECT,C'/'        EXPECT TO FIND A SLASH
         XC    COMMAX,COMMAX      WITH NO COMMAS BEFORE IT
         L     R15,=A(CLUZSKIP)
         BALR  R14,R15            SKIP BLANKS
         B     *+4(R15)           GOT ANYTHING?
         B     PASSTRY            YES.
         SPACE
PASSRET  MVC   SKIPF,SKIPSAVE     RESTORE EVERYTHING
         MVC   BYPASS,ORIGBYP     RESTORE ORIGINAL BYPASS
         MVC   PREVPOS,OLDPOS     RESTORE PREVIOUS PARM START
         MVI   PCBYPASS,0         MAKE SURE PCE BYPASS NOT SET
         MVI   ORIGBYP,X'00'
         OI    PASSF,PASSCAN      SHOW PASSWORD SCAN DONE
         LM    R14,R1,PWSAVE
         BR    R14                AND RETURN
         SPACE
PASSTRY  CLI   0(R8),C'/'         IS A SLASH PRESENT?
         BNE   PASSRET            NO, RETURN
         OI    PASSF,PASSEEN      SHOW PASSWORD INDICATOR FOUND
         NI    SKIPF,255-NOSLASH  YES, RESET SKIP FLAGS
         MVI   BYPASS,PASSABLE    SET TO BYPASS
         MVI   PCBYPASS,PASSABLE
         SPACE
*
*        SKIP UP TO THE PASSWORD
*
PASSAGN  XC    COMMAX,COMMAX      NO COMMA BEFORE PASSWORD
         L     R15,=A(CLUZSKIP)
         BALR  R14,R15            SKIP PAST SLASH
         B     *+4(R15)           ANYTHING MORE?
         B     PASSPRES           YES.
         B     PASSRQR            NO, ASK FOR PASSWORD
         SPACE
*
*        SYNTAX CHECK THE PASSWORD
*
PASSPRES ST    R8,PREVPOS         SAVE STARTING POINT
         MVI   NESTLEVL,LEVSUBS   ASSIGN NEW LEVEL TO PASSWORD
         LR    R1,R2              R1->PASSWORD PDE PART
         L     R15,=A(CLUZCHAR)   PASSWORD RECOGNIZER
         BALR  R14,R15            IS IT A PASSWORD?
         B     *+4(R15)
         B     PASSOK             YES.
         B     PASSRQR            NO, FORCE ONE
         B     PASSINV            A LOUSY ONE
         BAL   R6,PARSRC12
         SPACE
*
*        THE PASSWORD IS INVALID. PROMPT & TRY AGAIN
*
PASSINV  MVC   MSGAREA(4),=A(INVALID)
         MVC   MSGAREA+4(4),=A(PASSWORD)
         AIF   ('&OPSYS' NE 'MVS').NMVS3F
         TM    PASSF,PASSABLE     PROCESSING NEW PASSWORD?
         BNZ   PASSOLD            NO.
         MVC   MSGAREA+4(4),=A(NEWPASS)     YES, CHANGE MSG TEXT
PASSOLD  EQU   *
.NMVS3F  LA    R1,MSGAREA+4
         OI    0(R1),X'02'        SET END OF MSG MARK
         LA    R1,MSGAREA
         L     R15,=A(CLUZINFO)
         BALR  R14,R15            SEND THE MESSAGE
         BAL   R14,REEN           ASK FOR CORRECTION
         AIF   ('&OPSYS' NE 'MVS').NMVS10D
         OI    PASSF,PASSPROM     SHOW PASSWORD PROMPT ISSUED
.NMVS10D ANOP
         LTR   R15,R15            DID WE GET IT?
         BNZ   PASSOK             NO, FORGET IT
         NI    SKIPF,255-SLASH1   YES, TRY AGAIN
         B     PASSAGN
         SPACE
*
*        THE PASSWORD IS MISSING. PROMPT FOR IT
*
PASSRQR  MVC   BYPASS,ORIGBYP     RESTORE ORIGINAL BYPASS
         LR    R1,R0              RESTORE PROMPT PTR
         L     R15,=A(CLUZRQPW)
         BALR  R14,R15            REQUIRE THE PASSWORD
         MVC   BYPASS,PCBYPASS    RESTORE BYPASS
         AIF   ('&OPSYS' NE 'MVS').NMVS10F
         OI    PASSF,PASSPROM     SHOW PASSWORD PROMPT ISSUED
.NMVS10F ANOP
         B     *+4(R15)           DID WE GET A PASSWORD?
         B     PASSAGN            YES, RETRY
*                                 NO, GIVE UP
         SPACE
         AIF   ('&OPSYS' NE 'MVS').NMVS3A
*
*        IF A NEW PASSWORD IS ALLOWED, HANDLE THE NEW PASSWORD.
*        IF A NEW PASSWORD IS OBTAINED BY PROMPT, CALL CLUZCNFM
*        TO CONFIRM IT
*
PASSOK   TM    PASSF,NWPASSOK     IS A NEW PASSWORD POSSIBLE?
         BZ    PASSCMIT           NO, THOU SHALT COMMIT
         TM    PASSF,PASSABLE     YES, HAVE WE PROCESSED IT?
         BE    PASSCNFM           YES, CONFIRM IT
         NI    PASSF,255-PASSABLE NO, NOW PROCESS NEW ONE
         BAL   R14,CMIT           FIRST COMMIT OLD ONE
         LA    R2,8(,R2)          JUMP TO NEW PASSWORD PDE PART
         B     PASSRPT            AND DO IT AGAIN
         SPACE
PASSCNFM TM    PASSF,PASSPROM     PASSWORD FROM PROMPT RESPONSE?
         BZ    PASSCMIT           NO, NO CONFIRMATION REQUIRED
         LR    R1,R2              YES, COPY PDE ADDRESS
         L     R15,=A(CLUZCNFM)
         BALR  R14,R15            CALL CONFIRMATION ROUTINE
         LTR   R15,R15            CONFIRMATION FAILED?
         BNZ   PASSRQR            YES, ASK AGAIN
PASSCMIT EQU   *
         AGO   .JMVS3A
.NMVS3A  ANOP
PASSOK   EQU   *
.JMVS3A  BAL   R14,CMIT           COMMIT TO PASSWORD STORAGE
         MVI   NESTLEVL,LEVITEM   RETURN TO NORMAL ITEM LEVEL
         B     PASSRET            RETURN TO CALLER
         TITLE 'CLUTSPAR -- RANGE HANDLING ROUTINE'
*
*        THIS ROUTINE DETECTS AND HANDLES THE SECOND HALF OF A RANGE
*
RANG     STM   R14,R3,RSAVE       SAVE REGISTERS
         LR    R2,R1              SAVE PDE POINTER
         MVC   RANGEPOS,PREVPOS   SAVE START OF FIRST PART
         ST    R8,RANGEND         SAVE END OF FIRST PART
         MVC   RANGBYP,BYPASS     SAVE BYPASS FLAG
         SPACE
*
*        SEE IF THE NEXT CHARACTER IS A COLON
*
RANGPOP  CR    R8,R7              ANY TEXT LEFT?
         BNH   RANGCHEK           YES.
         L     R15,CURINP         FIND CURRENT INPUT
         C     R15,CBUF           WORKING ON COMMAND LINE?
         BE    RANGMISS           YES, NOTHING IS LEFT
         TM    DEFTINPT,DEFTINS   IS THIS DEFAULT TEXT
         BNZ   RANGMISS           YES, PROCEED NO FURTHER
         TM    PREVINS-INPUT(R15),DUMMYSUB  LOOKING AT DUMMY SUBFIELD?
         BNZ   RANGMISS           YES, DON'T POP IT
         L     R15,=A(CLUZPOPS)
         BALR  R14,R15            POP USED TEXT FROM STACK
         B     RANGPOP            AND CHECK AGAIN
         SPACE
RANGCHEK CLI   0(R8),C':'         IS THERE ONE?
         BNE   RANGMISS           NO, RETURN
         AR    R8,R6              YES, SKIP IT
         MVC   BYPASS,PCBYPASS    SET UP BYPASS FOR 2ND HALF
RANGAIN  XC    COMMAX,COMMAX      NO COMMA AFTER COLON
         L     R15,=A(CLUZSKIP)
         BALR  R14,R15            SKIP BLANKS, ETC
         B     *+4(R15)           ANYTHING LEFT?
         B     RANGHERE           YES.
RANGINC  BAL   R14,RINC           ISSUE INCOMPLETE RANGE PROMPT
         LTR   R15,R15            SOMETHING ENTERED?
         BZ    RANGAIN            YES, TRY IT AGAIN
RANGMISS SR    R15,R15            NO, SET "DO NO MORE" RETURN
         SPACE
RANGRET  ST    R15,RSAVE+4        SET RETURN CODE
         LM    R14,R3,RSAVE       RESTORE REGS
         BR    R14                AND RETURN
         SPACE
*
*        CALL RECOGNIZER FOR SECOND HALF
*
RANGHERE OI    RANGEF,RANGE2      NOTE WORKING ON PART 2
         BAL   R14,SIZE           GET PDE SIZE
         STH   R1,RANGSIZE        SAVE IT
         BCTR  R1,0
         EX    R1,RANGSAVE        SAVE THE FIRST VALUE
         LA    R1,1(R1,R1)
         EX    R1,RANGCLR         RECLEAR THE PDE
         SPACE
RANGO    ST    R8,PREVPOS         STORE STARTING POINT
         MVI   COBFLGS2,0         RESET COBOL RANGE-END FLAGS
         NI    COBFLAGS,COBFORM   RESET TRANSITORY COBOL FLAGS
         MVI   PASSF,0            MAKE SURE PASSWORD FLAGS OK
         LR    R1,R2
         LR    R15,R3             RESTORE RECOGNIZER ADDRESS
         BALR  R14,R15            RECOGNIZE SECOND PART
         B     *+4(R15)           WHAT HAPPENED?
         B     RANGOK1            RECOGNITION
         B     RANGFINV           DREW A BLANK
         B     RANGINV            RECOGNIZED AS INVALID
         BAL   R6,PARSRC12        SHOULD NOT OCCUR
         BAL   R6,PARSRC12        NOR THIS
         B     RANGBAD            UNABLE TO PROMPT FOR TERM
         SPACE
RANGSAVE MVC   RANGTEMP(0),0(R2)  SAVE FIRST PART OF PDE
RANGCLR  XC    0(0,R2),0(R2)      CLEAR RANGE PDE
         SPACE
*
*        REJECT WHATEVER IS HANDY IF THE SECOND HALF CANNOT BE
*        RECOGNIZED
*
RANGFINV CLI   0(R8),C')'         LOOKING AT CLOSE PAREN?
         BNE   RANGNPRN           NO.
         CLI   SUBFLAG,0          YES, IN A SUBFIELD?
         BNE   RANGLOST           YES, TREAT AS INCOMPLETE RANGE?
         CLI   LISTF,0            YES, IN A LIST?
         BNE   RANGLOST           YES, CONSIDER IT INCOMPLETE
RANGNPRN L     R15,=A(CLUZITEM)
         BALR  R14,R15            PICK HANDY TEXT TO REJECT
         SPACE
RANGINV  BAL   R14,INV            REJECT & ASK FOR MORE
         B     *+4(R15)           DID WE GET IT?
RANGITAB B     RANGCRCT           YES, TRY AGAIN
         B     RANGMISS           NO,  GIVE UP
         SPACE
RANGCRCT LH    R15,RANGSIZE       GET SIZE OF ONE PDE
         BCTR  R15,0
         EX    R15,RANGCLR        CLEAR OUT BAD DATA
         SPACE
RANGRTRY L     R15,=A(CLUZSKIP)
         BALR  R14,R15            SKIP BLANKS AGAIN
         B     *+4(R15)           ANYTHING FOUND?
         B     RANGO              YES, RECOGNIZE AGAIN
RANGLOST LH    R15,RANGSIZE       RANGE HAS BECOME INCOMPLETE
         BCTR  R15,0
         EX    R15,RANGREST       RESTORE 1ST HALF DATA
         NI    RANGEF,255-RANGE2  OFF PART 2 FLAG
         B     RANGINC            GO ASK FOR PART 2
         SPACE
*
*        CHECK FOR PASSWORD OR SUBSCRIPT FOR UNCORRECTABLY BAD
*        ELEMENT
*
RANGBAD  TM    COBFLAGS,COBSUBOK  SUBSCRIPT POSSIBLE HERE?
         BZ    RANGMISS           NO.
         MVI   LEFTOVER,X'FF'     YES, SET LEFTOVERS FLAG
         BAL   R14,SUBSIN         CHECK FOR DANGLING SUBSCRIPT
         MVI   LEFTOVER,0         RESET LEFTOVER SUBSCRIPT FLAG
         B     RANGMISS
         SPACE
*
*        CHECK FOR FOLLOWING SUBSCRIPTS IF REQUESTED BY THE RECOGNIZER
*
RANGOK1  TM    COBFLAGS,COBSUBOK  SUBSCRIPT POSSIBLE?
         BZ    RANGVEX            NO.
RANGSUBS BAL   R14,SUBSIN         YES, LOOK FOR THEM
         SPACE
*
*        CALL THE USER EXIT TO VALIDATE THE SECOND PART
*
RANGVEX  BAL   R14,VEX            LET CALLER VALIDATE PART 2
         B     *+4(R15)           OK?
         B     RANGVEX2           YES.
         B     RANGCRCT           NO,  TRY REPLACEMENT
         B     RANGSKIP           COULDN'T PROMPT, CLEAR THE PDE
         MVC   ALIGN,=H'1'        SUBSCRIPT REQUIRED, FORCE A PROMPT
         LA    R1,=C'('           BY STACKING AN EMPTY LIST
         OI    INSFLAG,DUMMYSUB   SHOW WHAT WE'RE DOING
         L     R15,=A(CLUZPUSH)
         BALR  R14,R15
         OI    COBFLAGS,COBIMMSB  SHOW SUBSCRIPT AROUND THE CORNER
         LR    R1,R2
         B     RANGSUBS
         SPACE
*
*        VALIDATE BOTH PARTS OF THE RANGE TOGETHER
*
RANGVEX2 TM    RANGEF,RANGE1NV    WAS VALUE 1 MISSING?
         BNZ   RANGSKIP           YES, STOP NOW
         NI    RANGEF,255-RANGE2  OFF THE PART 2 FLAG
         MVI   NESTLEVL,LEVRANGE  SET COMBINED STORAGE LEVEL
         LH    R15,RANGSIZE       GET PDE SIZE
         LA    R14,0(R2,R15)
         BCTR  R15,0
         EX    R15,RANGMOVE       MOVE 2ND VALUE TO 2ND PART
         EX    R15,RANGREST       RESTORE 1ST PART
         MVI   RANGEF,RANGERR     SET FLAG FOR REENTER MSG
         OC    BYPASS,RANGBYP     BYPASS FOR EITHER HALF
         BAL   R14,VEX            VALIDATE
         NI    RANGEF,255-RANGERR-RANGE2
         B     *+4(R15)           HOW'D IT GO?
         B     RANGRET            FINE
         B     RANGOUCH           LOUSY, RESCAN FROM  BEGINNING
RANGSKIP SR    R15,R15            COULDN'T PROMPT, CLEAR & GIVE UP
RANGOUCH LH    R14,RANGSIZE       GET SIZE AGAIN
         AR    R14,R14
         BCTR  R14,0
         EX    R14,RANGCLR        WIPE OUT PDE CONTENTS
         B     RANGRET            AND RETURN
RANGMOVE MVC   0(0,R14),0(R2)     MOVE 2ND VALUE->2ND PDE PART
RANGREST MVC   0(0,R2),RANGTEMP   RESTORE 1ST VALUE
         TITLE 'CLUTSPAR -- VALIDITY CHECK EXIT INTERFACE'
*
*        THIS ROUTINE INTERFACES TO A USER VALIDITY CHECK EXIT
*
VEX      SR    R15,R15            ASSUME ALL IS WELL
         STM   R14,R3,VSAVE       SAVE REGS
         TM    0(R9),X'01'        DOES A VALIDCK EXIT EXIST?
         BZ    VEXCMIT            NO, GO COMMIT MEMORY
         LR    R2,R9              YES, COPY R9
         MVC   ALIGN,2(R9)        ALIGN TOTAL PCE LENGTH
         AH    R2,ALIGN
         SH    R2,=H'4'           VCEXIT ADDR IS 3 BYTES BACK          *
                                  (GO 4 TO SUPPORT 4-BYTE ADDRS)
         SPACE
VEXNHELP MVC   VRTN(4),0(R2)      SAVE ROUTINE ADDRESS
         ST    R1,VPDE            STORE PDE ADDR FOR EXIT
         LA    R1,VPDE            POINT R1->VALIDCK PARMS
         MVC   VMSG,NULL          NULLIFY LEVEL2 MSG POINTER
         L     R15,VRTN
         BALR  R14,R15            CALL USER EXIT
         B     *+4(R15)           WHAT HAPPENED?
         B     VEXCMIT            VALUE IS OK
         B     VEXINV             VALUE IS BAD (TELL THE WORLD)
         B     VEXREEN            VALUE IS BAD (JUST PROMPT)
         BAL   R6,PARSRC20        STOP THE WORLD RIGHT NOW
         B     VEXNOSUB           SUBSCRIPT REQUIRED
         SPACE
VEXINV   BAL   R14,INV            PUT OUT INVALID MESSAGE
         B     VEXFAIL
         SPACE
VEXREEN  BAL   R14,INVRENT        PUT OUT REENTER
         SPACE
*
*        CALL THE MEMORY ROLLBACK ROUTINE TO FREE ANY MEMORY ALLOCATED
*        FOR THE INVALID ITEM
*
VEXFAIL  BAL   R14,RLBK           DISCARD STORAGE FOR THIS ITEM
         LA    R15,4(,R15)        ADD 4 TO RETURN CODE
VEXRET   ST    R15,VSAVE+4        TO PASS BACK TO CALLER
         LM    R14,R3,VSAVE
         BR    R14                RETURN
         SPACE
*
*        THE EXIT CLAIMED THE ITEM SHOULD BE SUBSCRIPTED (VIA THE
*        UNDOCUMENTED RETURN CODE 16). SEE IF THAT IS POSSIBLE
*        BEFORE ACCEPTING IT.
*
VEXNOSUB TM    COBFLAGS,COBSUBOK  IS A SUBSCRIPT ALLOWED?
         BZ    VEXOUCH            NO, USER ERROR
         LA    R15,12             YES, CAUSE RETURN CODE OF 12
         B     VEXRET             DON'T COMMIT OR ROLLBACK (VEXIT      *
                                  SHOULD BE CALLED AGAIN)
VEXOUCH  BAL   R6,PARSRC12
         SPACE
*
*        CALL THE MEMORY MANAGER COMMIT ROUTINE, AS THE MEMORY FOR
*        ITEM CAN NOW NOT BE FREED EXCEPT BY AN ERROR AT A HIGHER
*        LEVEL OR BY ERASURE OF THE ENTIRE PARAMETER
*
VEXCMIT  BAL   R14,CMIT           ASSOCIATE ITEM WITH HIGHER LEVEL
         B     VEXRET             AND RETURN HAPPY
         TITLE 'CLUTSPAR -- KEYWORD AND SUBFIELD HANDLER'
*
*        THIS ROUTINE PARSES KEYWORDS
*
KYWD     STM   R14,R3,KSAVE       SAVE REGISTERS
         MVI   PCBYPASS,X'00'     KEYWORDS CAN NOT BE PTBYPS
         SPACE
*
*        SKIP TO THE NEXT KEYWORD IN THE INPUT
*
KYWDNEXT MVI   COBFLAGS,0
         MVI   PASSF,0            RESET PASSWORD FLAGS
         L     R15,=A(CLUZSKIP)
         BALR  R14,R15            SKIP BLANKS ETC
         B     *+4(R15)           ANY TEXT THERE?
         B     KYWDTEXT           YES.
KYWDDONE SR    R15,R15            NO, RETURN NORMALLY
KYWDRET  LM    R0,R3,KSAVE+8
         L     R14,KSAVE
         BR    R14
         SPACE
*
*        PICK THE KEYWORD NAME OUT OF THE INPUT
*
KYWDTEXT ST    R8,PREVPOS         SAVE STARTING POINT
         CLI   0(R8),C')'         CLOSING PAREN?
         BE    KYWDPREN           YES.
         L     R9,KEY1            ADDRESS FIRST KEYWORD PCE
         L     R15,=A(CLUZITEM)
         BALR  R14,R15            GET A KEYWORD CANDIDATE
         B     *+4(R15)           GOT ONE?
         B     KYWDFND            YEP.
         B     KYWDINV            GOT A BAD ONE
         SPACE
*
*        LOOK UP THE KEYWORD NAME
*
KYWDFND  L     R15,=A(CLUZKIDN)
         BALR  R14,R15            IDENTIFY THE KEYWORD
         B     *+4(R15)           FOUND?
         B     KYWDOK             YES.
         B     KYWDUNKN           NO, UNKNOWN
KYWDAMB  L     R1,=A(AMBIG)       NO, AMBIGUOUS
         B     KYWDERR
KYWDUNKN L     R1,=A(UNKNOWN)
         B     KYWDERR
KYWDPREN CLI   SUBFLAG,0          ARE WE IN A SUBFIELD?
         BNE   KYWDDONE           YES, END OF KEYWORDS
         AR    R8,R6              NO, SKIP PAREN & SAY INVALID
KYWDINV  L     R1,=A(INVALID)
KYWDERR  BAL   R14,KERR           SEND KEYWORD ERROR MSG
         MVI   BYPASS,X'00'       RESET BYPASS FLAG
         B     *+4(R15)           PROMPT RESULTS?
         B     KYWDRTRY           GOOD, RETRY
         B     KYWDNEXT           LOUSY, JUST SKIP
         SPACE
*
*        IF ENDING POSITIONALS WERE OMITTED, ATTEMPT TO MATCH THE
*        PROMPT RESPONSE AGAINST THEM BEFORE TREATING IT AS KEYWORDS
*
KYWDRTRY OC    OMIT1(4),OMIT1     CHECK FOR MISSING PREVIOUS POSITIONAL
         BZ    KYWDNEXT           NONE, PROCEED
         LA    R15,4              YES, RETRY NEW DATA AS POSITIONAL
         B     KYWDRET
         SPACE
*
*        SEE IF THE KEYWORD IS A RESPECIFICATION. IF SO, AND IF THE
*        KEYWORD IS NOT AN INSERT, SEND A WARNING MESSAGE.
*
KYWDOK   XC    OMIT1,OMIT1        CLEAR BACKUP POINT
         LH    R1,NAME#           GET KEYWORD NUMBER
         L     R3,NAMEADDR        GET IKJNAME PCE ADDR
         L     R9,KEYPTR          AND IKJKEYWD ADDR
         MVC   ALIGN,4(R9)
         LH    R2,ALIGN
         A     R2,PDLPTR          FIND PDE ADDR FOR KEYWORD
         OC    0(2,R2),0(R2)      ALREADY SPECIFIED?
         BZ    KYWD1ST            NO, FIRST TIME
         TM    INSFLAG,INSERTED   YES, OVERRIDDEN BY INSERT?
         BNZ   KYWDOVOK           YES, KEEP QUIET
         BAL   R14,KOVR           GIVE OVERRIDE WARNING
         SPACE
*
*        ERASE OVERRIDDEN SUBFIELD VALUES
*
KYWDOVOK BAL   R14,ERAS           ERASE RESULTS OF OLD SPECIFICATION
         L     R3,NAMEADDR        RESTORE IKJNAME PTR
         SPACE
*
*        IF A SUBFIELD IS ALLOWED, LOOK FOR THE SUBFIELD START
*
KYWD1ST  STH   R1,0(,R2)          STORE NAME NUMBER
         TM    0(R3),X'04'        IS A SUBFIELD ALLOWED?
         BZ    KYWDINS            NO.
         XC    COMMAX,COMMAX      CATCH COMMA BEFORE SUBFIELD
         MVI   EXPECT,C'('        NOTE OPEN PAREN EXPECTED
         L     R15,=A(CLUZSKIP)
         BALR  R14,R15            YES, SKIP SEPARATORS
         B     *+4(R15)           ANYTHING LEFT?
         B     KYWDAFT            YES.
         B     KYWDNSUB           NO, FORCE EMPTY SUBFIELD
         SPACE
*
*        IF A SUBFIELD DOES NOT FOLLOW, INSERT A DUMMY "("
*
KYWDAFT  CLI   0(R8),C'('         OPEN PAREN AFTER KEYWORD?
         BE    KYWDSUB            YES, SUBFIELD PRESENT
KYWDNSUB MVC   ALIGN,=H'1'        NO, CREATE EMPTY SUBFIELD
         LA    R1,=C'('
         OI    INSFLAG,DUMMYSUB   SHOW DUMMY SUBFIELD CREATING
         L     R15,=A(CLUZPUSH)
         BALR  R14,R15
         SPACE
*
*        RECURSE TO SYN TO PARSE THE SUBFIELD INFORMATION
*
KYWDSUB  AR    R8,R6              SKIP PARENTHESIS
         MVI   SUBFD1,X'FF'       NOTE STARTING SUBFIELD
         LA    R15,SYN            WHERE TO RECURSE TO
         BAL   R14,CURSIN         ENTER SUBFIELD
         SPACE
*
*        PERFORM ANY REQUIRED INSERTION
*
KYWDINS  L     R3,NAMEADDR        RESTORE IKJNAME PTR
         TM    1(R3),X'10'        DOES IT HAVE AN INSERT?
         BZ    KYWDNEXT           NO.
         SR    R14,R14
         IC    R14,4(,R3)
         LA    R1,6(R14,R3)
         TM    0(R3),X'04'        IS THERE A SUBFIELD?
         BZ    KYWDINSB           NO.
         LA    R1,2(,R1)          YES, SKIP OFFSET
KYWDINSB BAL   R14,INS            INSERT INSERTION
         MVI   BYPASS,X'00'       RESET BYPASS
         B     KYWDNEXT           AND CONTINUE
         TITLE 'CLUTSPAR -- SUBFIELD ERASURE ROUTINE'
*
*        THIS ROUTINE ERASES ANY VALUES ESTABLISHED BY SUBFIELDS OF
*        AN OVERRIDDEN KEYWORD.
*
ERAS     STM   R14,R3,ESAVE
         LR    R2,R9              SAVE CURRENT PCE POINTER
         SPACE
*
*        ERASE A KEYWORD. IF THE KEYWORD HAS A SUBFIELD, RECURSE TO
*        ERASE THE SUBFIELD
*
ERASKEY  MVC   ALIGN,4(R9)
         LH    R15,ALIGN
         A     R15,PDLPTR         ADDRESS THE KEYWORD NUMBER
         OC    0(2,R15),0(R15)    IS IT 0?
         BZ    ERASKNXT           YES, CONTINUE
         MVC   ALIGN,0(R15)
         LH    R14,ALIGN          NO, SAVE IT
         LR    R3,R9
         SPACE
ERASFIND XC    0(2,R15),0(R15)    WIPE OUT THE KEYWORD NUMBER
         MVC   ALIGN,2(R3)
         AH    R3,ALIGN
         BCT   R14,ERASFIND       FIND THE RIGHT IKJNAME PCE
         TM    0(R3),X'04'        DOES IT HAVE A SUBFIELD?
         BZ    ERASKNXT           NO.
         LR    R0,R9              YES, SAVE CURRENT KEYWORD
         LA    R15,ERASFLD        AND RECURSE TO ERASE SUBFIELD
         BAL   R14,CURSIN
ERASKNXT CR    R2,R9              ARE WE BACK TO CALLER'S LEVEL?
         BE    ERASOUT            YES, ALL DONE
         SPACE
*
*        PROCESS THE NEXT PCE
*
ERASNEXT MVC   ALIGN,2(R9)
         AH    R9,ALIGN           FIND NEXT PCE
ERASCONT SR    R14,R14
         IC    R14,0(,R9)         GET PCE TYPE
         SRL   R14,5
         SLL   R14,2
         B     *+4(R14)
         B     ERASEND            IKJENDP
         B     ERASPOS            IKJPOSIT
         B     ERASKEY            IKJKEYWD
         B     ERASNEXT           IKJNAME, IGNORE
         B     ERASPOS            IKJIDENT
         B     ERASWORD           IKJRSVWD
         B     ERASPOS            IKJTERM
         B     ERASPOS            IKJOPER
         SPACE
ERASFLD  ST    R0,KSAVE           SAVE PREVIOUS LEVEL'S KEYWORD PTR
         B     ERASCONT           AND ERASE SUBFIELD
         SPACE
*
*        ERASE A POSITIONAL
*
ERASWORD TM    1(R9),X'80'        IS THIS A CHAINED IKJRSVWD?
         BNZ   ERASNEXT           YES, IGNORE IT
ERASPOS  LR    R1,R9              USE PCE ADDR AS OWNER ID
         L     R15,=A(CLUZRLSE)
         BALR  R14,R15            FREE MEMORY OWNED BY PARM
         MVC   ALIGN,4(R9)
         LH    R15,ALIGN
         A     R15,PDLPTR         FIND PDE ADDRESS
         BAL   R14,SIZE           GET PDE SIZE
         LTR   R1,R1              IF ANY
         BZ    ERASNEXT           IF NOT, SKIP THIS ONE
ERASPOS2 TM    1(R9),X'20'        IS THIS A RANGE?
         BZ    ERASSING           NO.
         AR    R1,R1              YES, DOUBLE SIZE
         SPACE
ERASSING STH   R1,ALIGN           SAVE SIZE
         BCTR  R1,0
         EX    R1,ERASCLR         CLEAR THE PDE
         B     ERASNEXT           AND CONTINUE
ERASCLR  XC    0(0,R15),0(R15)    CLEAR PDE
         SPACE
ERASEND  L     R9,KSAVE           RESTORE PREVIOUS KEYWORD POINTER
         B     CURSOUT            AND RETURN FROM RECURSION
         SPACE
ERASOUT  LM    R14,R3,ESAVE
         BR    R14                RETURN TO ORIGINAL CALLER
         TITLE 'CLUTSPAR -- SUBFIELD RECURSION ROUTINES'
*
*        THIS ROUTINE PROVIDES FOR RECURSION DURING THE PROCESSING
*        OF SUBFIELDS
*
CURSIN   ST    R14,RECRET         STORE RETURN POINT
         MVC   MYSUB,CURINP       SAVE SOURCE OF SUBFIELD
         LA    R10,SUBWLEN(,R10)  ON TO NEXT SUBFIELD WORK AREA
         LA    R1,SUBWEND
         CR    R10,R1             TOO MANY LEVELS (>5)?
         BNH   CURSOK             NO, PROCEED
         BAL   R6,PARSRC12        YES, PARM LIST ERROR
CURSOK   XC    0(SUBWLEN,R10),0(R10)   CLEAR THE WORK AREA
         MVI   SUBFLAG,X'FF'      SET SUBFIELD FLAG
         SR    R9,R9
         IC    R9,4(,R3)
         LA    R9,6(R9,R3)
         MVC   ALIGN,0(R9)
         LH    R9,ALIGN
         A     R9,PCL             FIND SUBFIELD START
         MVC   ALIGN,0(R9)
         LH    R3,ALIGN
         A     R3,PCL             FIND FIRST KEYWORD IN FIELD
         ST    R3,KEY1
         LA    R9,2(,R9)          FIND FIRST PARM IN FIELD
         ST    R9,PARM1
         BR    R15                GO TO DESIRED ROUTINE
         SPACE 5
*
*        THIS ROUTINE IS ENTERED TO RETURN FROM A RECURSION
*
CURSOUT  S     R10,=A(SUBWLEN)    DOWN TO PREVIOUS SUBFIELD AREA
         LA    R1,SUBWORKS
         CR    R1,R10             REACHED THE BOTTOM?
         BNE   CURSTILL           NO.
         MVI   SUBFLAG,0          YES, TURN OFF SUBFIELD FLAG
CURSTILL XC    OMIT1,OMIT1        FORGET SUBFIELD BACKUP POINT
         L     R14,MYSUB          FIND SUBFIELD STARTING INPUT SEG
         C     R14,CBUF           STARTED FROM CMD BUFFER?
         BE    CURSNDUM           YES, NOT DUMMY
         NI    PREVINS-INPUT(R14),255-DUMMYSUB   OFF DUMMY ( FLAG
CURSNDUM XC    MYSUB,MYSUB        SHOW NO SUBFIELD NOW
         L     R14,RECRET         LOAD RETURN POINT
         BR    R14                & GO BACK
         SPACE 3
SUBWORK  DSECT ,                  SUBFIELD WORK AREA MAPPING
         SPACE
KSAVE    DS    6A                 KYWD'S SAVE AREA
RECRET   EQU   KSAVE+4            RECURSION RETURN POINT
NAMEADDR DS    A                  CURRENT IKJNAME PTR
KEY1     DS    A                  1ST KEYWORD IN SUBFIELD/SYNTAX
PARM1    DS    A                  1ST PARM IN SUBFIELD/SYNTAX
MYSUB    DS    A                  SOURCE INPUT FOR SUBFIELD/SUBSCRIPT
SUBWLEN  EQU   *-SUBWORK          LENGTH OF SUBFIELD WORK AREA
         SPACE
CLUTSPAR CSECT ,                  RESUME ORIGINAL CSECT
         TITLE 'CLUTSPAR -- SUBSCRIPT RECURSION ROUTINES'
*
*        THIS ROUTINE IS CALLED WHENEVER A SUBSCRIPT LIST MAY APPEAR.
*        IF PRESENT, IT SAVES STATUS AND ENTERS THE SYN ROUTINE
*        RECURSIVELY TO HANDLE THE SUBSCRIPTS.
*
SUBSIN   STM   R14,R1,PWSAVE      BORROW A SAVE AREA
         MVC   MYSUB,CURINP       SAVE INPUT SOURCE OF SUBSCRIPTS
         L     R1,SUBSAVE         HAVE WE ALREADY GOT A WORK AREA?
         LTR   R1,R1
         BNZ   SUBSRUSE           YES, REUSE IT
         LIFO  LV=SUBSLEN         NO, GET ONE
         ST    R1,SUBSAVE         SAVE THE ADDRESS
SUBSRUSE MVC   SUBSREGS-SUBSECT(16,R1),PWSAVE   MOVE REGSTERS
         STM   R2,R3,SUBSREGS-SUBSECT+16(R1)    SAVE A FEW MORE
         LR    R3,R1
         USING SUBSECT,R3
         ST    R9,SUBSVAR         SAVE ADDR OF SUBSCRIPTED VAR PCE
         LR    R15,R9
         MVC   ALIGN,2(R9)        FIND FOLLOWING PCE
         AH    R15,ALIGN
         BAL   R14,FDEF           FIND DEFAULT/PROMPT
         TM    0(R9),X'18'        IF ANY
         BZ    SUBSNDEF
         SR    R14,R14
         IC    R14,0(,R1)
         LA    R1,2(R14,R1)       SKIP PAST IT
SUBSNDEF MVC   ALIGN,0(R1)        ALIGN SUBSCRIPT PDE OFFSET
         LH    R14,ALIGN
         A     R14,PCL
         CR    R14,R15            ARE THEY THE SAME?
         BNE   SUBSFAIL           NO, ILLEGAL USAGE
         MVC   ALIGN(1),0(R15)    MAKE SURE RIGHT TYPE
         NI    ALIGN,X'E0'
         CLI   ALIGN,X'C0'        IS IT A TERM?
         BNE   SUBSFAIL           NO, EXPLODE
         TM    1(R15),X'B0'       LIST, RANGE OR SUBSCRIPTED?
         BNZ   SUBSFAIL           YES, NO FORGIVENESS
         TM    6(R15),X'80'       STATEMENT NUMBER TYPE?
         BNZ   SUBSFAIL           YES, EVIL.
         TM    6(R15),X'08'       IS IT OFFICIALLY A SUBSCRIPT?
         BZ    SUBSFAIL           NO, ERROR
         LR    R2,R15             SAVE THE PCE POINTER
         SPACE
*
*        IKJPARS ALLOWS A VALIDITY CHECK EXIT FOR AN IKJTERM CHAINED
*        FROM AN IKJOPER TO DECIDE WHETHER OR NOT THE TERM SHOULD
*        BE SUBSCRIPTED. (THIS FEATURE IS OF COURSE UNDOCUMENTED.)
*        AN OPEN PARENTHESIS IMMEDIATELY FOLLOWING SUCH A TERM IS
*        ALWAYS TAKEN AS A SUBSCRIPT INDICATOR; IF THERE IS A LATER
*        PARENTHESIS, IT DOES NOT START A SUBSCRIPT UNLESS THE
*        EXIT SO INDICATES.
*
         TM    COBFLAGS,COBIMMSB  OPEN PAREN RIGHT AFTER VAR?
         BNZ   SUBSYES            YES, MUST BE SUBSCRIPTED
         XC    COMMAX,COMMAX      CATCH COMMA BEFORE SUBSCRIPT
         MVI   EXPECT,C'('        SHOW PAREN EXPECTED
         L     R15,=A(CLUZSKIP)
         BALR  R14,R15            SKIP DELIMITERS
         B     *+4(R15)           ANYTHING AFTER?
         B     SUBSMAYB           YES.
SUBSRET  LM    R14,R3,SUBSREGS    NO, RESTORE REGS
         BR    R14                AND RETURN
         SPACE
SUBSMAYB CLI   0(R8),C'('         IS THERE A (?
         BNE   SUBSRET            NO, NO SUBSCRIPTS
         TM    OPERFLGS,OPERACT+OPEREXPR    WORKING AT LEVEL 88?
         BNM   SUBSYES            NO.
         XI    COBFLAGS,COBSUB88  YES, SET/RESET INDICATOR
         TM    COBFLAGS,COBSUB88  ARE WE READY FOR IT YET?
         BNZ   SUBSRET            NO, RETURN TO CALL VEXIT
SUBSYES  NI    COBFLAGS,255-COBSUBOK   YES, TURN OFF SUBSCRIPT OK FLAG
         MVC   SUBSWORK(SUBSTLEN),SUBSTAT    SAVE STATUS
         MVC   SUBSFLGS,SUBSOPTS
         XC    SUBSTAT(SUBSTLEN),SUBSTAT    RESET EVERYTHING
         XC    SUBSOPTS(SUBOPLEN),SUBSOPTS
         OI    COBFLAGS,COBSUB    SET SUBSCRIPT FLAG
         MVI   NESTLEVL,LEVSUBS   START NEW LEVEL FOR SUBSCRIPTS
         LR    R9,R2              ESTABLISH NEW PCE
         AR    R8,R6              SKIP THE PAREN
         MVI   SUBSNO,0           NO SUBSCRIPTS TO START
         L     R1,SUBSREGS+12     FIND ORIGINAL PDE
         LA    R1,20(,R1)         FIND PDE FOR SUBSCRIPT 1
         ST    R1,SUBSPDE         AND STORE FOR LATER
         XC    COMMAX,COMMAX      NO COMMA BEFORE FIRST SUBSCRIPT
         B     SYNPOS2            GO GET THOSE SUBSCRIPTS
         SPACE
SUBSFAIL BAL   R6,PARSRC24        GIVE SCREAM OF ANGUISH
         SPACE 5
*
*        THIS ROUTINE IS CALLED TO RETURN FROM A SUBSCRIPT RECURSION
*
SUBSOUT  CR    R8,R7              ANY TEXT LEFT?
         BH    SUBSNPRN           NO.
         AR    R8,R6              YES, SKIP OVER CLOSE PAREN
SUBSNPRN L     R3,SUBSAVE         FIND THE SUBSECT
         MVC   SUBSTAT(SUBSTLEN),SUBSWORK   COPY STATUS BACK
         MVC   SUBSOPTS(SUBOPLEN),SUBSFLGS
         L     R9,SUBSVAR         RESTORE OLD PCE
         L     R1,SUBSREGS+12     FIND VARIABLE'S PDE
         MVC   18(1,R1),SUBSNO    STORE NUMBER OF SUBSCRIPTS
         L     R1,MYSUB           FIND SUBSCRIPT SOURCE
         C     R1,CBUF            SUBSCRIPT FROM CMD BUFFER?
         BE    SUBSNDUM           YES, NOT DUMMY
         NI    PREVINS-INPUT(R1),255-DUMMYSUB    OFF DUMMY ( FLAG
SUBSNDUM XC    MYSUB,MYSUB        KILL SUBSCRIPT START PTR
         MVI   NESTLEVL,LEVITEM   RETURN TO NON-SUBSCRIPT LEVEL
         TM    RANGEF,RANGE2      WORKING ON PART2?
         BNZ   SUBSHAS2           YES.
         OI    COBFLGS1,COBHASUB  SET HAS SUBSCRIPT FLAG
         B     SUBSRET            AND RETURN
SUBSHAS2 OI    COBFLGS2,COBHASUB
         B     SUBSRET
         SPACE
         DROP  R3
         DROP  R10                NO LONGER GUARANTEED
         TITLE 'CLUTSPAR -- ABNORMAL EXIT ROUTINES'
         DROP  R11                NO LONGER GUARANTEED
         SPACE
*
*        THESE ROUTINES PROVIDE FOR EXIT FROM PARSE WITH A BAD
*        RETURN CODE.
*
PARSRC8  MVI   RC,8               SET ATTN RETURN CODE
         B     PARSEXIT
         SPACE
PARSERRX BAL   R6,PARSRC12        ENTERED FROM PGM CHECK EXIT
PARSRC12 MVI   RC,12              SET USER ERROR RET CODE
PARSGASP L     R1,IOPL            FIND THE UPT
         TM    UPTSWS-UPT(R1),UPTMID   USER HAVE PROF MSGID?
         BZ    PARSEXIT           NO, QUIT QUIETLY
         L     R15,=A(CLUZGASP)   YES, SEND DEBUGGING HINT
         BALR  R14,R15
         B     PARSEXIT           AND THEN STOP
         SPACE
PARSRC16 MVI   RC,16              SET NO MEMORY RET CODE
         B     PARSEXIT
         SPACE
PARSRC20 MVI   RC,20              SET FORCED QUIT RET CODE
         B     PARSEXIT
         SPACE
PARSRC24 MVI   RC,24              SET BAD COBOL LINKAGE CODE
         B     PARSGASP           SAY WHERE ERROR DETECTED
         SPACE
         AIF   ('&OPSYS' NE 'MVS').NMVS8A
PARSRC28 MVI   RC,28              SET DISCONNECT RETURN CODE
.NMVS8A  ANOP
PARSEXIT LIGOTO GIVEUP            RETURN TO INITAL LEVEL & QUIT
         TITLE 'CLUTSPAR -- SUBROUTINE RETURN CODE'
*
*        THIS CODE IS CALLED TO RETURN FROM A SUBROUTINE, WITHOUT
*        RESTORING THE INPUT POINTER REGISTERS (R7 AND R8)
*
RETURN   L     R14,4(,RDSA)       FIND PREVIOUS SAVE AREA
         STM   R7,R8,48(R14)      SAVE BUFFER POSITION REGS
         LIEXIT RC=(15)           NOW REALLY RETURN
         TITLE 'CLUTSPAR -- COMMIT/ROLLBACK INTERFACE ROUTINES'
*
*        THIS ROUTINE CALLS THE MEMORY COMMITMENT ROUTINE IF MEMORY
*        MANAGEMENT HAS BEEN INVOKED FOR THIS PARAMETER.
*
CMIT     TM    ALCFLAGS,ACPTREQ   IS ACCEPT NEEDED?
         BCR   8,R14              NO, NOTHING TO COMMIT
         CLC   NESTLEVL,ALCLEVEL  ALLOCATION AT THAT LEVEL?
         BCR   2,R14              NO, NOTHING TO DO
         STM   R14,R15,CSAVE      YES, SAVE SPECIAL REGS
         L     R15,=A(CLUZCMIT)   FIND FULL COMMIT ROUTINE
         BALR  R14,R15            AND CALL IT
         LM    R14,R15,CSAVE      RESTORE REGS
         BR    R14                AND RETURN
         SPACE 5
*
*        THIS ROUTINE CALLS THE MEMORY ROLLBACK ROUTINE IF MEMORY
*        MANAGEMENT HAS BEEN INVOKED FOR THIS PARAMETER.
*
RLBK     TM    ALCFLAGS,ACPTREQ   IS ACCEPT NEEDED?
         BCR   8,R14              NO, NOTHING TO ROLLBACK
         CLC   NESTLEVL,ALCLEVEL  ALLOCATION AT THAT LEVEL?
         BCR   2,R14              NO, NOTHING TO DO
         STM   R14,R15,CSAVE      YES, SAVE SPECIAL REGS
         L     R15,=A(CLUZRLBK)   FIND FULL ROLLBACK ROUTINE
         BALR  R14,R15            AND CALL IT
         LM    R14,R15,CSAVE      RESTORE REGS
         BR    R14                AND RETURN
         TITLE 'CLUTSPAR -- DETERMINE PDE SIZE ROUTINE'
*
*        THIS ROUTINE DETERMINES THE SIZE OF A PDE
*
SIZE     LA    R1,8               ASSUME SIZE IS 8
         MVC   ALIGN(1),0(R9)
         NI    ALIGN,X'E0'
         CLI   ALIGN,X'20'
         BE    SIZPOSIT
         CLI   ALIGN,X'C0'
         BE    SIZTERM
         BR    R14
SIZPOSIT IC    R1,6(,R9)          NO, LOAD IKJPOSIT TYPE
         IC    R1,PDESIZES-1(R1)  AND LOAD SIZE FROM TABLE
         BR    R14
SIZTERM  LA    R1,80              SIZE OF SUBSCRIPTED IKJTERM
         TM    1(R9),X'10'        MAY IT BE SUBSCRIPTED?
         BCR   7,R14              YES, RETURN
         LA    R1,20              NON-SUBSCRIPTED SIZE
         TM    6(R9),X'08'        IS THIS A SUBSCRIPT ITSELF?
         BCR   8,R14              NO, RETURN
         SR    R1,R1              YES, GIVE 0 SIZE
         BR    R14
         TITLE 'CLUTSPAR -- TEXT INSERTION ROUTINE'
*
*        THIS ROUTINE INSERTS A DEFAULT OR A KEYWORD INTO THE TEXT
*        TO BE PARSED
*
INS      ST    R14,ISAVE          SAVE RETURN POINT
         SR    R14,R14
         IC    R14,0(,R1)         GET LENGTH OF INSERTION
         LA    R14,1(,R14)
         STH   R14,ALIGN
         LA    R1,1(,R1)          POINT TO INSERT TEXT
         L     R14,ISAVE
         L     R15,=A(CLUZPUSH)   AND PUSH DOWN ON INPUT STACK
         BR    R15
         TITLE 'CLUTSPAR -- "INCOMPLETE RANGE" MESSAGE ROUTINE'
*
*        THIS ROUTINE WRITES AN "INCOMPLETE RANGE" MESSAGE AND PROMPTS
*        FOR ENTRY OF THE SECOND VALUE
*
RINC     STM   R14,R3,MSAVE
         OI    RANGEF,RANGE0      SET INCOMPLETE FLAG
         MVC   MSGAREA(4),=A(INCOMPLE) "INCOMPLETE "
         MVC   MSGAREA+4(4),=A(RANGE)  "RANGE"
         MVC   MSGAREA+8(4),ACOLON     :
         MVI   MSGAREA+12,X'0D'   PUT TEXT AFTER
         MVC   MSGAREA+16(4),=A(COLON)      :
         MVI   MSGAREA+16,X'03'   LAST OF MESSAGE
         LA    R1,MSGAREA
         L     R15,=A(CLUZINFO)
         BALR  R14,R15            SEND "INCOMPLETE RANGE: X:"
         NI    RANGEF,255-RANGE0  RESET ERROR BIT
         L     R14,=A(ENTERID)
         ST    R14,MSGAREA
         MVC   MSGID,4(R14)       SAVE PROMPT MSGID
         MVC   MSGAREA+4(4),=A(ENTER)  "ENTER "
         MVC   MSGAREA+8(4),=A(SECOND) "SECOND "
         BAL   R14,FDSC           FIND DESCRIPTION
         ST    R1,MSGAREA+12      ADD DESCRIPTION TO MESSAGE
         OI    MSGAREA+12,X'02'   SET END FLAG
         SR    R2,R2              ASSUME NO HELP
         TM    0(R9),X'02'        HOW ABOUT IT?
         BZ    INVREEN            RIGHT, SEND "ENTER SECOND DESC -"
         B     INVHELP            NO, GO PROMPT WITH HELP
         TITLE 'CLUTSPAR -- "INCOMPLETE EXPRESSION" MESSAGE ROUTINE'
*
*        THIS ROUTINE WRITES AN "INCOMPLETE EXPRESSION" MESSAGE AND
*        PROMPTS FOR ENTRY OF AN OPERAND OR OPERATOR.
*
XINC     STM   R14,R3,MSAVE       SAVE REGISTERS
         MVC   MSGAREA(4),=A(INCOMPLE) "INCOMPLETE "
         LR    R2,R9              SAVE CALLER'S R9
         L     R9,OPERPCE         FIND ORIGINAL IKJOPER
         BAL   R14,FDSC           FIND ITS DESCRIPTION
         ST    R1,MSGAREA+4       ADD TO MESSAGE
         MVC   MSGAREA+8(4),ACOLON     :
         MVI   MSGAREA+12,X'0F'   ADD PARTIAL EXPRESSION AFTER
         OI    RANGEF,RANGE2      SET RANGE2 FLAG TO WRITE FULL EXPR
         OI    OPERFLGS,OPERINV   INDICATE OPER ERROR
         OI    COBFLAGS,COBFORM   FORCE COBOL MESSAGE FORMAT
         LA    R1,MSGAREA
         L     R15,=A(CLUZINFO)
         BALR  R14,R15            SEND "INCOMPLETE XXX: (YYY"
         MVI   RANGEF,0           RESTORE VARIOUS FLAGS
         NI    OPERFLGS,255-OPERINV
         NI    COBFLAGS,255-COBFORM
         LR    R9,R2              RESTORE ORIGINAL PCE PTR
         L     R2,=A(ENTERID)
         ST    R2,MSGAREA
         MVC   MSGID,4(R2)        SAVE PROMPT'S MSGID
         MVC   MSGAREA+4(4),=A(ENTER)  "ENTER "
         CLI   OPERPART,2         WHICH PART DO WE NEED?
         BE    XINC1              1ST OPERAND
         BH    XINC2              2ND OPERAND
         MVC   MSGAREA+8(4),=A(OPERATOR)    "OPERATOR"
         MVI   MSGAREA+8,X'02'    INDICATE THE END
         B     XINCPROM           JOIN OTHER CASES
XINC1    MVC   MSGAREA+8(4),=A(FIRST)  "FIRST "
         B     XINCAND
XINC2    MVC   MSGAREA+8(4),=A(SECOND) "SECOND "
XINCAND  MVC   MSGAREA+12(4),=A(OPERAND)     "OPERAND"
         OI    MSGAREA+12,X'02'   INDICATE MSG END
XINCPROM SR    R2,R2              INDICATE NO LEVEL 2 TEXT
         B     INVREEN            GO TO REQUEST REENTRY
         TITLE 'CLUTSPAR -- "INVALID POSITIONAL" MESSAGE ROUTINE'
*
*        THIS ROUTINE PUTS OUT AN "INVALID" MESSAGE FOR AN INVALID
*        POSITIONAL PARAMETER
*
INV      STM   R14,R3,MSAVE       SAVE REGS IN MSG AREA
         MVC   MSGAREA(4),=A(INVALID)  "INVALID "
         MVC   MSGAREA+4(4),=A(QUALFOR)     "QUALIFIER FOR "
         TM    COBFLAGS,QUALFLG   PROMPTING FOR COBOL QUALIFIER?
         BNZ   INVQUAL            YES.
         OI    MSGAREA+4,X'80'    NO, SUPPRESS QUALIFIER
INVQUAL  BAL   R14,FDSC           FIND DESCRIPTION
         ST    R1,MSGAREA+8
         MVC   MSGAREA+12(4),ACOLON    :
         MVI   MSGAREA+16,X'0F'   REJECTED TEXT
         LA    R1,MSGAREA
         L     R15,=A(CLUZINFO)
         BALR  R14,R15            SEND "INVALID DESC: X"
         SPACE
*
*        PROMPT FOR REENTRY
*
INVENTRY SR    R2,R2              ASSUME NO HELP
         L     R14,=A(REENTER)
         ST    R14,MSGAREA        "REENTER"
         OI    MSGAREA,X'02'
         MVC   MSGID,4(R14)       SAVE REENTER MSGID
         TM    0(R9),X'02'        IS THERE HELP?
         BZ    INVREEN            NO.
INVHELP  BAL   R14,FDEF           FIND THE PROMPT
         BAL   R14,FHLP           SKIP TO HELP
         LA    R2,2(,R1)          SKIP OVER THE HELP LENGTH
INVREEN  LA    R1,MSGAREA
         L     R15,=A(CLUZPROM)
         BALR  R14,R15            ISSUE "REENTER -"
         NI    COBFLAGS,255-QUALFLG
         ST    R15,MSAVE+4        PASS RET CODE TO CALLER
         LM    R14,R3,MSAVE
         BR    R14                AND RETURN
         SPACE 3
*
*        ALTERNATE ENTRY TO INV TO JUST DO THE REENTER PROMPT
*
INVRENT  STM   R14,R3,MSAVE
         TM    RANGEF,RANGERR     ENTRY FOR REJECTED RANGE?
         BZ    INVENTRY           NO, NORMAL CASE
         MVI   RANGEF,0           YES, RESET RANGE FLAGS
         MVC   MSGAREA(4),=A(REENTER)  "REENTER"
         BAL   R14,FDSC           FIND DESCRIPTION
         MVC   MSGAREA+4(4),=A(BLANK)  ADD A SPACE
         ST    R1,MSGAREA+8       ADD TO MESSAGE
         MVC   MSGAREA+12(4),=A(RCOLON)     :
         ST    R1,MSGAREA+16      ADD AGAIN
         OI    MSGAREA+16,X'02'   SET END FLAG
         SR    R2,R2              ASSUME NO HELP
         TM    0(R9),X'02'        WERE WE RIGHT?
         BZ    INVREEN            YES.
         B     INVHELP            GO SEND "REENTER DESC:DESC -"
         TITLE 'CLUTSPAR -- "EVIL KEYWORD" MESSAGE ROUTINE'
*
*        THIS ROUTINE WRITES A BAD KEYWORD MESSAGE AND PROMPTS FOR
*        REENTRY
*
KERR     STM   R14,R3,MSAVE       SAVE REGS
         ST    R1,MSGAREA         STORE TYPE-OF-BADNESS IN MESSAGE
         MVC   MSGAREA+4(4),=A(KEYWORD)     ADD "KEYWORD"
         MVC   MSGAREA+8(4),ACOLON     :
         MVI   MSGAREA+12,X'0F'   ADD TEXT
         LA    R1,MSGAREA
         L     R15,=A(CLUZINFO)
         BALR  R14,R15            SEND "WICKED KEYWORD"
         B     REENKEY            GO ASK FOR REENTRY
         TITLE 'CLUTSPAR -- "REENTER" MESSAGE ROUTINE'
*
*        THIS ROUTINE ISSUES A NON-SPECIFIC "REENTER" PROMPT
*
REEN     STM   R14,R3,MSAVE       SAVE REGS
REENKEY  SR    R2,R2              SET NO HELP
         MVC   MSGAREA(4),=A(REENTER)  "REENTER"
         OI    MSGAREA,X'02'      SET END FLAG
         LA    R1,MSGAREA
         L     R15,=A(CLUZPROM)
         BALR  R14,R15            PROMPT WITH "REENTER -"
         L     R14,MSAVE          PRESERVE RETURN CODE
         LM    R0,R3,MSAVE+8
         BR    R14                AND RETURN
         TITLE 'CLUTSPAR -- "OVERRIDING KEYWORD" MESSAGE ROUTINE'
*
*        THIS ROUTINE WRITES AN "OVERRIDING KEYWORD" WARNING
*
KOVR     STM   R14,R1,MSAVE
         MVC   MSGAREA(4),=A(OVERRIDE)      OVERRIDING KEYWORD ACCEPTED
         MVC   MSGAREA+4(4),ACOLON     :
         MVI   MSGAREA+8,X'0F'    ADD TEXT TO MESSAGE
         LA    R1,MSGAREA
         L     R15,=A(CLUZINFO)
         BALR  R14,R15            SEND "OVERRIDING KEYWORD ACCEPTED: X"
         LM    R14,R1,MSAVE
         BR    R14                AND RETURN
         TITLE 'CLUTSPAR -- "CLOSING QUOTE ASSUMED" MESSAGE ROUTINE'
*
*        THIS ROUTINE IS CALLED TO WRITE A "CLOSING QUOTE ASSUMED"
*        MESSAGE FOR A RECOGNIZER
*
UNCL     STM   R14,R1,MSAVE       SAVE REGS
         MVC   MSGAREA(4),=A(ASSUMED)  "CLOSING QUOTE ASSUMED"
         MVC   MSGAREA+4(4),ACOLON      :
         MVI   MSGAREA+8,X'0F'    ADD TEXT TO MESSAGE
         LA    R1,MSGAREA
         L     R15,=A(CLUZINFO)
         BALR  R14,R15            SEND "CLOSING QUOTE ASSUMED: 'X"
         LM    R14,R1,MSAVE
         BR    R14                AND RETURN
         TITLE 'CLUTSPAR -- FIND NEXT UNCHAINED PCE ROUTINE'
*
*        THIS ROUTINE IS CALLED TO FIND THE NEXT PCE WHICH IS NOT A
*        SUBSCRIPT TERM, A CHAINED RESERVED WORD, OR AN IKJNAME.
*
FNXT     MVC   ALIGN,2(R9)        GET SIZE OF THIS PCE
         LR    R1,R9
         AH    R1,ALIGN           LOOK AT NEXT PCE
FNXTAGN  MVC   ALIGN(1),0(R1)     WHAT TYPE IS THIS ONE?
         NI    ALIGN,X'E0'
         CLI   ALIGN,X'60'        IKJNAME?
         BE    FNXTLOOP           YES, SKIP IT
         CLI   ALIGN,X'A0'        IKJRSVWD?
         BE    FNXTWORD           YES.
         CLI   ALIGN,X'C0'        IKJTERM?
         BCR   7,R14              NO, RETURN (R1->PCE)
         TM    6(R1),X'08'        YES, IS IT A SUBSCRIPT
         BCR   8,R14              NO, RETURN THIS
         B     FNXTLOOP           YES, SKIP IT
FNXTWORD TM    1(R1),X'80'        IS THIS A CHAINED WORD?
         BCR   8,R14              NO, THIS IS IT
FNXTLOOP MVC   ALIGN,2(R1)
         AH    R1,ALIGN           SKIP PAST THIS PCE
         B     FNXTAGN            AND CONTINUE
         TITLE 'CLUTSPAR -- FIND PARAMETER DESCRIPTION ROUTINE'
*
*        LOCATE THE PARAMETER DESCRIPTOR IN THE PCE (OR FIND IN-LINE
*        CONSTANT)
*
FDSC     MVC   ALIGN(1),0(R9)
         NI    ALIGN,X'E0'        ISOLATE PARM TYPE
         CLI   ALIGN,X'C0'
         BE    FDSCTERM
         CLI   ALIGN,X'20'
         BE    FDSCPOS
         CLI   ALIGN,X'80'
         BE    FDSCIDEN
         LA    R1,6(,R9)          LOCATE DESCRIPTION IN PCE
         BR    R14                AND RETURN
FDSCIDEN LA    R1,9(,R9)          OFFSET IS 9 FOR IKJIDENT
         BR    R14
FDSCTERM LA    R1,7(,R9)          OFFSET IS 7 FOR IKJTERM
         BR    R14
FDSCPOS  SR    R1,R1
         IC    R1,6(,R9)          FIND IKJPOSIT TYPE
         SLL   R1,2
         L     R1,POSITID-4(R1)   FIND INLINE DESCRIPTION
         AIF   ('&OPSYS' NE 'MVS').NMVS6H
         C     R1,POSITID+24      IS IT DSN-LIKE?
         BCR   7,R14              NO, RETURN
         TM    1(R9),X'01'        YES, DDNAME MODIFIED?
         BCR   8,R14              NO.
         L     R1,=A(DDNID)       YES, DESCRIBE AS DDNAME
.NMVS6H  ANOP
         BR    R14                AND RETURN
         TITLE 'CLUTSPAR -- FIND HELP INFORMATION ROUTINE'
*
*        LOCATE THE HELP INFORMATION IN THE PCE.
*
FHLP     TM    0(R9),X'18'        IS THERE A DEFAULT OR PROMPT?
         BZ    FHLPNDEF           NO.
         MVI   ALIGN,X'00'        YES, GET ITS LENGTH
         MVC   ALIGN+1(1),0(R1)
         AH    R1,ALIGN           AND SKIP PAST IT
         LA    R1,2(,R1)
FHLPNDEF MVC   ALIGN(1),0(R9)
         NI    ALIGN,X'E0'        GET PCE TYPE
         CLI   ALIGN,X'C0'        IS IT A TERM?
         BCR   7,R14              NO, RETURN
         TM    1(R9),X'10'        YES, IS THERE A SUBSCRIPT?
         BZ    FHLPNSUB           NO.
         LA    R1,2(,R1)          YES, SKIP PAST OFFSET
FHLPNSUB TM    1(R9),X'08'        IS THERE A CHAINED WORD?
         BCR   8,R14              NO, RETURN
         LA    R1,2(,R1)          YES, SKIP OFFSET
         BR    R14                AND THEN RETURN
         TITLE 'CLUTSPAR -- FIND DEFAULT/PROMPT INFORMATION ROUTINE'
*
*        LOCATE DEFAULT/PROMPT INFORMATION IN PCE
*
FDEF     MVC   ALIGN(1),0(R9)
         NI    ALIGN,X'E0'        DETERMINE PCE TYPE
         TM    ALIGN,X'A0'
         BO    FDEFWORD
         CLI   ALIGN,X'80'
         BE    FDEFID
         CLI   ALIGN,X'40'
         BE    FDEFKEY
         CLI   ALIGN,X'C0'
         BE    FDEFTERM
         LA    R1,7(,R9)          ELSE OFFSET IS 7
         BR    R14
         SPACE
FDEFID   LA    R1,9(,R9)
         MVC   ALIGN,9(R9)
         AH    R1,ALIGN
         TM    1(R9),X'04'        IS THIS A USER TYPE?
         BCR   7,R14              YES, RETURN
         TM    6(R9),X'40'        IS THERE A MAXLNTH?
         BCR   8,R14              NO, GOT IT
         LA    R1,1(,R1)          YES, SKIP PAST MAXLNTH
         BR    R14
         SPACE
FDEFWORD LA    R1,6(,R9)          SKIP OVER PARM TYPE
         MVC   ALIGN,6(R9)
         AH    R1,ALIGN
         TM    0(R9),X'40'        IS THIS A RESERVED WORD?
         BCR   8,R14              YES.
         LA    R1,8(,R1)          NO, SKIP OVER IKJOPER STUFF
         BR    R14
         SPACE
FDEFKEY  LA    R1,6(,R9)          KEYWORD OFFSET IS 6
         BR    R14
         SPACE
FDEFTERM LA    R1,7(,R9)
         MVC   ALIGN,7(R9)
         AH    R1,ALIGN
         BR    R14
         TITLE 'CLUTSPAR -- DATA AREAS'
PGMODEL  PUTGET OUTPUT=(*-*,SINGLE,PROMPT),MF=L  PROMPT PARM LIST
         SPACE
RECOGS   DC    A(CLUZDLIM,CLUZSTRG,CLUZVALU,CLUZADDR) RECOGNIZER ADDRS
         DC    A(CLUZPSTR,CLUZUSID,CLUZDSNM,CLUZDSNM)
         DC    A(CLUZQSTR,CLUZSPAC)
         AIF   ('&OPSYS' NE 'MVS').NMVS4A
         DC    A(CLUZJOBN,CLUZUSID,CLUZADDR)
.NMVS4A  ANOP
POSITID  DC    A(0,STRINGID,VALUEID,ADDRID,STRINGID) POSIT NAME ADDRS
         DC    A(USERID,DSNID,DSNID,STRINGID,0)
         AIF   ('&OPSYS' NE 'MVS').NMVS4B
         DC    A(JOBID,USERID,ADDRID)
.NMVS4B  ANOP
PDESIZES DC    AL1(0,8,8,36,8,16,24,24,8,0) POSIT PDE SIZES
         AIF   ('&OPSYS' NE 'MVS').NMVS4C
         DC    AL1(16,24,36)
.NMVS4C  ANOP
         SPACE
NULL     DC    0A(0),X'FF000000'  END-OF-LIST INDICATOR
ACOLON   DC    X'01',AL3(COLON)   SUPPRESSIBLE COLON
BYPSEG   DC    AL2(6,4)           SEGMENT FOR INSERTING BYPASS CHAR
BYCHAR   DC    X'24'              BYPASS CHARACTER
         DC    X'15'              FORCE NEW LINE AFTER BYPASS
RESTCHAR DC    X'14'              RESTORE CHARACTER
PDOTS    DC    C'(...)'           SUBSCRIPT INDICATOR
         SPACE 3
         DS    0H
PATCH    DC    24CL5'PATCH'       REPAIR AREA
         SPACE
         USING DSA,RDSA
         TITLE 'CLUZSKIP -- SKIP DELIMITERS ROUTINE'
CLUTSERV LISECT ,                 GENERAL SERVICE ROUTINES
         SPACE
*
*        THIS ROUTINE SKIPS SEPARATORS TO FIND THE NEXT TEXT
*
CLUZSKIP LIENTRY DSALEN=88,DSA=*,ENV=R5
         LH    R3,COMMAS         LOAD LEFT-OVER COMMA COUNT
         XC    COMMAS,COMMAS     AND RESET TO ZERO
         SPACE
*
*        SEE IF THE CURRENT TEXT SEGMENT IS USED UP
*
SKIPSEGS CR    R8,R7              AT END OF CURRENT SEGMENT?
         BNH   SKIPIN             NO, PROCEED
         TM    SKIPF,NOSKIP       YES, ARE WE FORBIDDEN TO SKIP?
         BNZ   SKIPEND            YES, GIVE NO TEXT RETURN CODE
SKIPPOP  L     R1,CURINP          FIND CURRENT INPUT
         C     R1,CBUF            ARE WE IN ORIGINAL INPUT?
         BE    SKIPEND            YES, NO TEXT LEFT
         TM    DEFTINPT,DEFTINS   ARE WE PROCESSING A DEFAULT?
         BNZ   SKIPEND            YES, GO NO FURTHER
         TM    PREVINS-INPUT(R1),DUMMYSUB   IS THIS INPUT A DUMMY      *
                                            SUBFIELD/SUBSCRIPT?
         BNZ   SKIPEND            YES, GO NO FURTHER
         AIF   ('&OPSYS' NE 'MVS').NMVS10B
         TM    INSFLAG,PSWDCONF   PASSWORD CONFIRMATION ACTIVE?
         BNZ   SKIPEND            YES, DON'T POP THE STACK
.NMVS10B L     R15,=A(CLUZPOPS)
         BALR  R14,R15            NO, POP TEXT FROM STACK
         AH    R3,COMMAS          ADD COMMAS FROM OLD ELEMENT
         XC    COMMAS,COMMAS      RESET COUNT TO ZERO
         B     SKIPSEGS           AND CHECK AGAIN
         SPACE
*
*        INDICATE NO MORE TEXT TO CALLER
*
SKIPEND  LA    R2,4               NO TEXT RET CODE
         LTR   R3,R3              ANY COMMAS ENCOUNTERED?
         BNZ   SKIPWARN           YES, WARNING REQUIRED
         B     SKIPAWAY           NO, RETURN
         SPACE
*
*        SEPARATORS ARE BLANK, TAB, COMMA, SEMICOLON AND SLASH
*
SKIPIN   TM    SKIPF,NOSKIP       ARE WE FORBIDDEN TO SKIP?
         BNZ   SKIPTEXT           YES, JUST SAY NOT ENDED
SKIPLOOP SR    R2,R2
         IC    R2,0(,R8)          LOAD NEXT CHARACTER
         LA    R2,SCANTAB(R2)
         TM    0(R2),X'10'        IS IT A SEPARATOR?
         BZ    SKIPTEXT           NO, TEXT FOUND
         CLI   0(R8),C','         YES, IS IT A COMMA?
         BE    SKIPCOMA           YES, PROCESS
         CLI   0(R8),C';'         NO, SEMI-COLON?
         BE    SKIPSEMI           YES.
         CLI   0(R8),C'/'         TRY SLASH
         BE    SKIPSLSH           YES.
SKIPNEXT BXLE  R8,R6,SKIPLOOP     SKIP TO NEXT CHARACTER
         B     SKIPPOP            POP STACK WHEN END REACHED
         SPACE
*
*        IKJPARS COMPATIBILITY REQUIRES THAT MULTIPLE COMMAS BE
*        IGNORED. FORCE A WARNING MESSAGE IN THIS CASE, OR ANY
*        TIME A COMMA IS USED OTHER THAN TO SEPARATE TWO
*        OPERANDS
*
SKIPCOMA TM    SKIPF,NOCOMMA      TREAT COMMA AS BLANK?
         BNZ   SKIPNEXT           YES, JUST CONTINUE
         TM    INSFLAG,VIRGINP    NO, COMMA FIRST IN INPUT LINE?
         BZ    SKIPCNT            NO, MAY BE OK
         LH    R3,COMMAX          YES, FORCE A WARNING
SKIPCNT  AR    R3,R6              ADD 1 TO COMMA COUNT
         B     SKIPNEXT           AND CONTINUE
         SPACE
*
*        A SEMI-COLON, OUTSIDE PARENTHESES, IN ORIGINAL INPUT,
*        ALWAYS STOPS THE SCAN.
*
SKIPSEMI CLI   SUBFLAG,0          IN A SUBFIELD?
         BNE   SKIPTEXT           YES, DON'T STOP
         CLI   LISTF,0            IN A LIST?
         BNE   SKIPTEXT           YES, DON'T STOP
         CLC   CURINP,CBUF        IN ORIGINAL TEXT?
         BNE   SKIPTEXT           NO, CONTINUE
         LR    R7,R8              MAKE SEMICOLON AFTER TEXT END
         BCT   R7,SKIPEND         AND STOP THE SCAN
         SPACE
SKIPSLSH EQU   *
         AIF   ('&OPSYS' NE 'MVS').NMVS1A
*
*        WHEN A SLASH IS FOUND, CHECK FOR /* STARTING A COMMENT
*
         CR    R8,R7              ROOM FOR A *?
         BNL   SKIPNCOM           NO.
         CLI   1(R8),C'*'         YES, IS IT THERE?
         BNE   SKIPNCOM           NO, NO COMMENT
         AR    R8,R6              YES, SKIP IT
*
*        LOOK FOR THE END OF COMMENT. END OF TEXT IS TREATED AS
*        END OF COMMENT
*
SKIPSKIM BXH   R8,R6,SKIPPOP      CONTINUE TILL THE END
SKIPCONT CLI   0(R8),C'*'         * FOUND?
         BNE   SKIPSKIM           NO, NO END YET
         BXH   R8,R6,SKIPPOP      YES, MOVE TO NEXT CHAR
         CLI   0(R8),C'/'         */?
         BNE   SKIPSKIM           NO.
         B     SKIPNEXT           YES, RESUME ORIGINAL SEARCH
         SPACE
SKIPNCOM EQU   *
.NMVS1A  ANOP
*
*        IF CALLER PERMITS, NOTE PRESENCE OF SLASH AND CONTINUE
*
         TM    SKIPF,SLASH1+NOSLASH    DOES SLASH MEAN BYPASS?
         BNZ   SKIPTEXT           NO.
         OI    SKIPF,SLASH1       YES, SET FLAG
         MVI   BYPASS,X'FF'       SET BYPASS FLAG
         B     SKIPNEXT           AND CONTINUE
         SPACE
*
*        WE HAVE A NON-SEPARATOR. IF IT IS A FUNCTIONAL CLOSE
*        PARENTHESIS PRECEDED BY A COMMA, FORCE A WARNING
*
SKIPTEXT SR    R2,R2              LOAD "TEXT FOUND" RETURN CODE
         LTR   R3,R3              ANY COMMAS ENCOUNTERED?
         BZ    SKIPAWAY           NO.
         CLI   0(R8),C')'         YES, CLOSE PAREN HIT?
         BNE   SKIPEXP            NO.
         CLI   SUBFLAG,0          YES, IN A SUBFIELD?
         BNE   SKIPWARN           YES, WARNING REQUIRED
         CLI   LISTF,0            DOES IT END A LIST?
         BNE   SKIPWARN           WARN IF SO
         TM    COBFLAGS,COBSUB    CHECK FOR END OF SUBSCRIPTS
         BNZ   SKIPWARN
         TM    OPERFLGS,OPERACT+OPEREXPR    OR END OF EXPRESSION
         BO    SKIPWARN
         SPACE
*
*        SEE IF OUR CALLER WAS EXPECTING A PARTICULAR CHARACTER.
*        IF SO, AND IT WAS NOT FOUND, SKIP THE WARNING FOR NOW AND
*        SAVE THE COMMA COUNT FOR LATER.  THIS LETS THE WARNING
*        COME OUT AT A MORE NATURAL TIME
*
SKIPEXP  CLI   EXPECT,X'00'       EXPECTING SOMETHING PARTICULAR?
         BE    SKIPANY            NO.
         CLC   EXPECT,0(R8)       YES, IS THAT WHAT WE HAVE?
         BE    SKIPANY            YES.
         STH   R3,COMMAS          NO, SAVE COMMA COUNT FOR NEXT CALL
         B     SKIPAWAY           AND JUST RETURN
         SPACE
SKIPANY  CH    R3,COMMAX          MORE COMMAS THAN ALLLOWED?
         BNH   SKIPAWAY           NO, NO WARNING NEEDED
SKIPWARN MVC   MSGAREA(4),=A(EXTRACOM) YES, GIVE WARNING
         OI    MSGAREA,X'02'
         LA    R1,MSGAREA
         L     R15,=A(CLUZINFO)
         BALR  R14,R15            SEND "EXTRANEOUS COMMA(S) IGNORED"
         SPACE
SKIPAWAY NI    SKIPF,255-SLASH1   RESET RECENT SLASH FLAG
         MVI   EXPECT,X'00'       CANCEL ANY EXPECTATIONS
         STH   R6,COMMAX          ASSIGN DEFAULT MAXIMUM COMMAS
         NI    INSFLAG,255-VIRGINP     SHOW USE OF INPUT LINE
         LR    R15,R2             COPY RETURN CODE
         B     RETURN
         TITLE 'CLUZPUSH/CLUZPOPS -- INPUT STACK MANAGEMENT ROUTINES'
*
*        PUSH TEXT ON THE INPUT STACK
*
CLUZPUSH LIENTRY DSALEN=88,DSA=*,ENV=R5,PARM=R3
         LH    R2,ALIGN           LOAD LENGTH OF STRING
         LA    R1,INPLEN(,R2)     ADD CHAIN LENGTH
         OI    ALCFLAGS,COLECTIV  SET COLLECTIVE STORAGE FLAG
         L     R15,=A(CLUZALOC)
         BALR  R14,R15            GET MEMORY FOR TEXT
         L     R1,NEWMEM
         USING INPUT,R1
         MVC   INPCHAIN,CURINP    SAVE CHAIN INFORMATION
         ST    R1,CURINP          SET NEW CURRENT INPUT
         STM   R7,R8,PREVEND
         MVC   BYPASS,PCBYPASS    RESET BYPASS FLAG
         MVC   PREVINS,INSFLAG
         MVI   INSFLAG,INSERTED   SET INSERTION FLAG
         MVC   PREVDEFT,DEFTINPT
         MVC   DEFTINPT,DEFTF     COPY DEFAULT INPUT FLAG
         NI    DEFTF,255-DEFTINS  AND RESET
         MVC   PREVCOMS,COMMAS    SAVE COMMA COUNT FROM PREVIOUS
         XC    COMMAS,COMMAS      AND MAKE NEW COUNT 0
         DROP  R1
         LA    R8,INPLEN(,R1)     POINT TO TEXT START
         LA    R7,0(R8,R2)        POINT TO TEXT END
         BCTR  R7,0
         LR    R2,R8              COPY TEXT TO STACK  ELEMENT
         LA    R14,256
         LR    R15,R7
         SR    R15,R14
         SR    R2,R14
PUSHLOOP BXH   R2,R14,PUSHLAST
         MVC   0(256,R2),0(R3)
         AR    R3,R14
         B     PUSHLOOP
         SPACE
PUSHLAST SR    R15,R2
         EX    R15,PUSHMOVE
         B     RETURN             AND THEN RETURN
PUSHMOVE MVC   0(0,R2),0(R3)      MOVE TEXT TO INPUT STACK
         EJECT
*
*        POP THE INPUT STACK
*
CLUZPOPS LIENTRY DSA=*,DSALEN=88,ENV=R5
         L     R1,CURINP          FIND CURRENT INPUT START
         USING INPUT,R1
         LM    R7,R8,PREVEND      RELOAD CURRENT & END POINTERS
         LH    R3,PREVCOMS        GET PREVIOUS COMMA COUNT
         AH    R3,COMMAS          ADD TO CURRENT COUNT
         STH   R3,COMMAS          AND STORE BACK
         MVC   CURINP,INPCHAIN    RESET CURRENT INPUT PTR
         MVC   INSFLAG,PREVINS    & INSERT FLAGS
         MVC   DEFTINPT,PREVDEFT  & DEFAULT FLAGS
         DROP  R1
         B     RETURN             THEN RETURN
         SPACE 3
INPUT    DSECT ,                  MAPPING OF INPUT CHAIN AREA
         SPACE
INPCHAIN DS    A                  CHAIN FIELD
PREVEND  DS    A                  PREVIOUS END POINT
PREVIPOS DS    A                  PREVIOUS CURRENT POINT
PREVCOMS DS    H                  PREVIOUS LEFT-OVER COMMAS
PREVINS  DS    B                  PREVIOUS INSERT FLAGS
PREVDEFT DS    B                  PREVIOUS DEFAULT FLAGS
         DS    0F
BUFLEN   DS    H                  TEXT SEGMENT LENGTH
BUFOFF   DS    H                  CURRENT TEXT OFFSET (FOR EXTENS)
INPLEN   EQU   *-INPUT            LENGTH OF HEADER
CLUTSERV CSECT ,                  RESUME OUR CSECT
         TITLE 'CLUZKIDN -- KEYWORD IDENTIFICATION ROUTINE'
*
*        THIS ROUTINE SCANS THE PCE'S TO FIND A KEYWORD
*
CLUZKIDN LIENTRY  DSALEN=88,DSA=*,ENV=R5
         SPACE
         L     R14,4(,R13)        FIND SAVE AREA
         L     R10,60(,R14)       LOAD SUBWORK POINTER
         USING SUBWORK,R10
         L     R2,PREVPOS         FIND KEYWORD START
         LR    R3,R8
         SR    R3,R2              GET LENGTH OF KEYWORD
         BCTR  R3,0               LESS 1 FOR EX
         XC    NAME#,NAME#        CLEAR NAME & NUMBER
         XC    NAMEADDR,NAMEADDR
         L     R9,KEY1            START WITH 1ST KEYWORD
         LR    R0,R6              R0 COUNTS IKJNAMES
         MVC   ALIGN,2(R9)
         LR    R15,R9
         AH    R15,ALIGN          ADDRESS 1ST IKJNAME
         MVI   AMBFLAG,0          TURN OFF AMBIGUITY FLAGS
         SPACE
*
*        CHECK AGAINST THE NEXT NAME
*
KIDLOOP  EX    R3,KIDCLEN         SAME LENGTH AS THIS NAME?
         BL    KIDNO              TOO SHORT, NO MATCH
         BE    KIDLENEQ           YES, MAY MATCH EXACTLY
         TM    AMBFLAG,X'80'      ALREADY AMBIGUOUS?
         BO    KIDNO              YES, NO NEED TO COMPARE
         EX    R3,KIDCNAM         COMPARE FRONT ENDS
         BNE   KIDNO              NO MATCH
         OC    NAMEADDR,NAMEADDR  DID SOMETHING ELSE MATCH?
         BNZ   KIDAMB             YES, AMBIGUITY
         STH   R0,NAME#           NO, STORE NUMBER
         ST    R15,NAMEADDR       & NAME
         ST    R9,KEYPTR          SAVE IKJKEYWD POINTER
         AIF   ('&OPSYS' NE 'MVS').NMVS2B
         OI    AMBFLAG,X'40'      SET MATCH FOUND FLAG
.NMVS2B  B     KIDNO              AND CONTINUE
KIDCLEN  CLI   4(R15),0           COMPARE LENGTHS-1
KIDCNAM  CLC   5(0,R15),0(R2)     COMPARE KEYWORD TEXTS
         SPACE
KIDAMB   OI    AMBFLAG,X'80'      SET AMBIGUOUS FLAG
         B     KIDNO              BUT CONTINUE LOOKING FOR EXACT MATCH
         SPACE
KIDLENEQ EX    R3,KIDCNAM         COMPARE KEYWORD & NAME
         BE    KIDFOUND           GOTCHA
         SPACE
KIDNO    EQU   *
         SPACE
         AIF   ('&OPSYS' NE 'MVS').NMVS2A
*
*        CHECK FOR A MATCH WITH AN ALIAS NAME
*
         TM    1(R15),X'02'       ARE THERE ALIASES?
         BZ    KIDNEXT            NO.
         SR    R1,R1
         IC    R1,4(,R15)         YES, FIND 1ST ONE
         LA    R1,6(R1,R15)
         TM    0(R15),X'04'       IS THERE A SUBFIELD?
         BZ    KIDNOSUB           NO.
         LA    R1,2(,R1)          YES, SKIP IT
KIDNOSUB TM    1(R15),X'10'       IS THERE INSERT?
         BZ    KIDANINS           NO.
         IC    R6,0(,R1)          YES, SKIP IT
         LA    R1,2(R6,R1)
KIDANINS IC    R6,0(,R1)          GET NUMBER OF ALIASES
         LA    R1,1(,R1)          SKIP PAST IT
KIDALOOP LA    R14,1(,R3)         GET TRUE LENGTH OF KEYWORD
         EX    R14,KIDCALEN       COMPARE ALIAS LENGTH
         BE    KIDALNEQ           BRANCH IF PERFECT MATCH
         BL    KIDANEXT           GIVE UP IF TOO SHORT
         TM    AMBFLAG,X'C0'      MATCHED BY THIS NAME | AMBIG?
         BNZ   KIDANEXT           YES, SKIP COMPARE
         EX    R3,KIDCANAM        COMPARE ALIAS & KEYWORD
         BNE   KIDANEXT           NOT EQUAL, PROCEED
         OC    NAMEADDR,NAMEADDR  MATCH ALREADY FOUND?
         BNZ   KIDAMBA            YES, AMBIGUOUS
         STH   R0,NAME#           NO, STORE NAME & NUMBER
         ST    R15,NAMEADDR
         ST    R9,KEYPTR          SAVE KEYWORD POINTER
         B     KIDANEXT           AND CONTINUE
KIDCALEN CLI   0(R1),0            COMPARE ALIAS LENGTH-1 TO WORD LEN-1
KIDCANAM CLC   1(0,R1),0(R2)      COMPARE KEYWORD TO ALIAS
         SPACE
KIDAMBA  MVI   AMBFLAG,X'80'      SET AMBIGUOUS FLAG
         B     KIDANEXT           AND CONTINUE
         SPACE
KIDALNEQ EX    R3,KIDCANAM        COMPARE KEYWORD TO ALIAS
         BE    KIDFOUND           GOT IT
         SPACE
KIDANEXT SR    R14,R14
         BCT   R6,*+8             ANY MORE ALIASES LEFT?
         B     KIDALNOT           NO.
         IC    R14,0(,R1)         YES,  TRY THE NEXT
         LA    R1,1(R1,R14)
         B     KIDALOOP
         SPACE
KIDALNOT LA    R6,1               RESTORE ORIG R6
.NMVS2A  ANOP
KIDNEXT  MVC   ALIGN,2(R15)       PROCEED TO NEXT IKJNAME
         AH    R15,ALIGN
         AR    R0,R6              INCREASE NAME COUNT
         AIF   ('&OPSYS' NE 'MVS').NMVS2C
         NI    AMBFLAG,X'BF'      TURN OFF FOUND-HERE FLAG
.NMVS2C  TM    0(R15),X'60'       STILL AN IKJNAME?
         BO    KIDLOOP            YES,  CONTINUE
         LR    R9,R15             NO, RESET R9
         LA    R0,0
         BM    KIDNEXT            CONTINUE IF NEW KEYWORD
         TM    AMBFLAG,X'80'      WAS THERE AMBIGUITY?
         BZ    KIDNAMB            NO.
         LA    R15,8              YES, SET RETURN CODE
         B     RETURN             AND GO BACK
         SPACE
KIDNAMB  OC    NAMEADDR,NAMEADDR  WAS ANYTHING FOUND?
         BZ    KIDUNOWN           NO, UNKNOWN KEYWORD
KIDRET   SR    R15,R15            YES, SET GOOD RETURN CODE
         B     RETURN
         SPACE
KIDUNOWN LA    R15,4              SET UNKNOWN RET CODE
         B     RETURN
         SPACE
KIDFOUND STH   R0,NAME#           STORE THE IKJNAME NUMBER
         ST    R15,NAMEADDR       AND ADDR
         ST    R9,KEYPTR          SAVE POINTER TO IKJKEYWD
         B     KIDRET             STORE R9 AND RETURN
         SPACE
         DROP  R10                NO LONGER VALID
         TITLE 'CLUZXTRA -- "EXTRANEOUS/UNINTELLIGIBLE INFORMATION" MES*
               SAGE ROUTINE'
*
*        THIS ROUTINE GENERATES AN "EXTRANEOUS INFORMATION" MESSAGE
*
CLUZXTRA LIENTRY  DSALEN=XTRALEN,DSA=XTRADSA,ENV=R5,                   *
               PARM=R2
         XC    XAREA(XTRALEN-88),XAREA CLEAR DSA
         ST    R2,IOWORK+12       STORE MSG HEADER IN MESSAGE
         LA    R9,XAREA           START OF COLLECTION AREA
         ST    R9,IOWORK+8        STORE INTO MSG
         LA    R9,4(,R9)          SKIP PAST HEADER
         LA    R10,157            MAXIMUM LENGTH ALLOWED-L''...'
         LA    R3,1               PAREN COUNTER
         MVC   HFLAG,BYPASS       INITIALIZE HIDING FLAG
         MVI   BFLAG,X'FF'        DON'T INSERT LEADING BLANK
         OI    SKIPF,NOCOMMA      DON'T BOTHER WITH COMMA WARNINGS
         SPACE
*
*        MOVE THE NON-SECRET PARTS OF THE REMAINING TEXT TO XAREA
*
XTRALOOP CLI   0(R8),C''''        GOT A QUOTE
         BE    XTRAQUOT           YES, GO HANDLE
         CLI   QFLAG,X'00'        WITHIN QUOTED STRING?
         BNE   XTRAQSTR           YES, SKIP SEMI CHECK
XTRARSUM CLI   0(R8),C';'         GOT A SEMI-COLON?
         BE    XTRASEMI           YES.
XTRAQSTR CLI   HFLAG,X'00'        ARE WE HIDING THIS WORD?
         BNE   XTRAHIDE           YES.
         CLI   QFLAG,X'00'        WITHIN A QUOTE?
         BNE   XTRACOPY           YES, JUST COPY TO OUTPUT
         CLI   0(R8),C'('         NO, CHECK FOR PARENS
         BE    XTRAOPEN
         CLI   0(R8),C')'
         BE    XTRACLOS
         SR    R1,R1
         IC    R1,0(,R8)
         LA    R1,SCANTAB(R1)
         TM    0(R1),X'10'        HAVE WE A SEPARATOR?
         BNZ   XTRASEP
         SPACE
XTRACOPY LTR   R9,R9              IS THE OUTPUT AREA FULL?
         BZ    XTRASKIP           YES.
         MVC   0(1,R9),0(R8)      NO, MOVE THIS CHARACTER
         MVI   BFLAG,X'00'        SHOW LAST CHAR NOT BLANK
         BXH   R8,R6,XTRADONE     ON TO NEXT ONE
XTRAINCR AR    R9,R6              NEXT OUTPUT CHAR
         BCT   R10,XTRALOOP       REPEAT IF STILL ROOM
         MVC   0(3,R9),=C'...'    IF NOT, ADD ELLIPSES
         MVC   XAREA(2),=H'164'   STORE SEGMENT LENGTH
         SR    R9,R9              SET BUFFER FULL FLAG
         B     XTRALOOP           AND CONTINUE
         SPACE
XTRASKIP BXLE  R8,R6,XTRALOOP     JUST SKIP THIS HIDDEN CHAR
         B     XTRADONE           EXIT IF DONE
         SPACE
XTRAQUOT CLI   QFLAG,X'00'        ALREADY WITHIN QUOTE?
         BNE   XTRAQCLO           YES.
         MVI   QFLAG,X'FF'        NO, SET QUOTE FLAG
         CLI   HFLAG,X'00'        SUPPRESSING?
         BE    XTRACOPY           NO, COPY THIS QUOTE
         B     XTRASKIP           YES, JUST SKIP IT
         SPACE
XTRAQCLO CR    R8,R7              ANYTHING AFTER?
         BE    XTRAQEND           NO, CONSIDER IT CLOSED
         CLI   1(R8),C''''        YES, IS IT A DOUBLED QUOTE?
         BNE   XTRAQEND           NO, IT IS CLOSED
         MVI   QFLAG,X'00'        CLOSE IT MOMENTARILY
         B     XTRACOPY           AND CONTINUE
         SPACE
XTRAQEND MVI   QFLAG,X'00'        CLOSE THE QUOTE
         CLI   HFLAG,X'00'        PRESENTLY HIDING?
         MVI   HFLAG,X'00'        STOP HIDING IF SO
         BNE   XTRASKIP           YES, HIDE LAST QUOTE
         B     XTRACOPY           NO, COPY IT
         SPACE
XTRAOPEN LA    R3,1(,R3)          INCREASE PAREN COUNT
         MVI   HFLAG,X'00'        STOP HIDING
         B     XTRACOPY           AND CONTINUE
         SPACE
XTRACLOS MVI   HFLAG,X'00'        STOP HIDING
         BCT   R3,XTRACOPY        CONTINUE IF STILL UNBALANCED
         CLI   SUBFLAG,0          IN A SUBFIELD?
         BNE   XTRAEND            YES, THIS IS WHERE WE STOP
         TM    COBFLAGS,COBSUB    IN A SUBSCRIPT?
         BNZ   XTRAEND            YES, STOP HERE
         TM    OPERFLGS,OPERACT+OPEREXPR    IN A COBOL EPRESSION?
         BO    XTRAEND            YES, STOP NOW
         B     XTRACOPY           NO, CONTINUE
         SPACE
*
*        WE HAVE A SEPARATOR. CALL THE SKIP ROUTINE TO FIND THE
*        NEXT NON-SEPARATOR, AND WHETHER FOLLOWING TEXT SHOULD BE
*        SUPPRESSED.
*
XTRASEP  LR    R2,R8              SAVE PRESENT LOCATION
         MVI   BYPASS,0           ASSUME NO SUPPRESS REQUIRED
         L     R15,=A(CLUZSKIP)
         BALR  R14,R15            CALL SKIP TO PROCESS
         MVC   HFLAG,BYPASS       SAVE BYPASS FLAG
         LTR   R15,R15            ANYTHING AFTER?
         BNZ   XTRAEND            NO, STOP
         CR    R2,R8              YES, DID WE SKIP ANYTHING?
         BE    XTRACOPY           NO, JUST CONTINUE
         LTR   R9,R9              IS THERE ROOM FOR MORE OUTPUT?
         BZ    XTRALOOP           NO, DON'T ADD BLANK
         CLI   BFLAG,X'00'        YES, WAS LAST CHAR A BLANK?
         BNE   XTRALOOP           YES, DON'T ADD ANOTHER
         MVI   BFLAG,X'FF'        NO, SHOW BLANK NOW LAST
         MVI   0(R9),C' '         REPLACE SEPARATORS WITH BLANK
         B     XTRAINCR           NOTE ONE MORE CHAR USED
         SPACE
XTRAHIDE CLI   QFLAG,X'00'        WITHIN A QUOTE?
         BNE   XTRASKIP           YES, SKIP THIS CHAR
         SR    R1,R1
         IC    R1,0(,R8)          LOAD THIS CHARACTER
         LA    R1,SCANTAB(R1)
         TM    0(R1),X'E0'        IS IT ALPHAMERIC?
         BNZ   XTRASKIP           YES, SKIP THIS CHAR
         CLI   0(R8),C''''        NO, QUOTE FOUND?
         BE    XTRAQUOT           YES, HANDLE HIDDEN QUOTATION
         MVI   HFLAG,X'00'        ELSE THROUGH WITH HIDING
         B     XTRARSUM           COPY THIS CHAR
         SPACE
XTRASEMI CH    R3,=H'1'           WITHIN PARENTHESES?
         BH    XTRACOPY           YES, IGNORE SEMICOLON
         CLI   SUBFLAG,0          WITHIN SUBFIELD?
         BNE   XTRACOPY           YES, IGNORE SEMICOLON
         CLC   CURINP,CBUF        WITHIN ORIGINAL INPUT?
         BNE   XTRACOPY           NO, IGNORE IT
         B     XTRAEND            ELSE TERMINATE PROCESSING
         SPACE
XTRADONE AR    R9,R6              SKIP LAST OUTPUT CHAR
XTRAPOP  CLC   CURINP,CBUF        IN ORIGINAL INPUT?
         BE    XTRAEND            YES, THE END
         TM    DEFTINPT,DEFTINS   NO, IN DEFAULT TEXT?
         BNZ   XTRAEND            YES, GO NO FURTHER
         L     R15,=A(CLUZPOPS)
         BALR  R14,R15            POP THIS TEXT AWAY
         CR    R8,R7              ANY THING LEFT HERE?
         BH    XTRAPOP            NO, POP THIS TOO
         LTR   R9,R9              ROOM FOR MORE OUTPUT?
         BZ    XTRALOOP           NO, JUST TRUCK ON
         CLI   BFLAG,X'00'        YES, IS LAST CHAR ALREADY BLANK?
         BNE   XTRALOOP           YES, PROCEED
         MVI   0(R9),C' '         NO, PUT ONE THERE
         MVI   BFLAG,X'FF'        SHOW LAST CHAR BLANK
         B     XTRAINCR           AND PROCEED
         SPACE
XTRAEND  NI    SKIPF,255-NOCOMMA  ALLOW COMMA WARNINGS AGAIN
         LTR   R9,R9              OUTPUT AREA ALREADY FULL?
         BZ    XTRASEND           YES.
         LA    R1,XAREA           NO, COMPUTE LENGTH
         SR    R9,R1
         STH   R9,XAREA           AND STORE
         XC    XAREA+2(2),XAREA+2 CLEAR OFFSET
         SPACE
XTRASEND MVC   IOWORK+4(4),=A(3)  SAY 3 SEGMENTS
         MVC   IOWORK+16(4),=A(COLON)  PUT : IN
         XC    PGPB(12),PGPB      INITIALIZE PUTLINE BLOCK
         PUTLINE PARM=PGPB,OUTPUT=IOWORK+4,MF=(E,IOPL) SEND IT
         B     *+4(R15)           INTERPRET RETURN CODES
         B     RETURN             NORMAL
         BAL   R6,PARSRC8         ATTENTION
         BAL   R6,PARSRC12        IMPOSSIBLE
         BAL   R6,PARSRC12        SOMEONE BLEW IT
         BAL   R6,PARSRC16        NO MEMORY
         AIF   ('&OPSYS' NE 'MVS').NMVS8E
         BAL   R6,PARSRC28        TERMINAL DISCONNECTED
.NMVS8E  SPACE 3
XTRADSA  DSECT ,                  CLUZXTRA'S DSA
         SPACE
         DS    22F
XAREA    DS    CL164              EXTRANEOUS INFO COLLECTION AREA
HFLAG    DS    B                  SUPPRESSION FLAG
QFLAG    DS    B                  QUOTATION FLAG
BFLAG    DS    B                  LAST CHAR BLANK FLAG
         DS    0D
XTRALEN  EQU   *-XTRADSA          LENGTH OF DSA
         TITLE 'CLUZINFO -- INFORMATIONAL MESSAGE ROUTINE'
CLUTSCOM LISECT ,                 USER COMMUNICATION ROUTINES
*
*        PUTLINE ROUTINE
*
CLUZINFO LIENTRY DSA=*,DSALEN=88,ENV=R5,PARM=R6
         BAL   R14,BMSG           BUILD MESSAGE SEGMENTS
         SPACE
*
*        SEND THE MESSAGE
*
         OC    VMSG+1(3),VMSG+1   IS THERE A LEVEL2 MESSAGE?
         BNZ   INFOMULT           YES.
         XC    PGPB(8),PGPB       INITIALIZE PUTLINE PARM BLOCK
         PUTLINE PARM=PGPB,OUTPUT=(IOWORK+4,TERM,SINGLE),MF=(E,IOPL)   *
                                  SEND THE MESSAGE
         B     INFORC             INTERPRET THE RETURN CODE
         SPACE
*
*        ADD SPECIFIED LEVEL 2 MESSAGE
*
INFOMULT L     R9,IOWORK+8        FIND MESSAGE TEXT
         AH    R9,0(,R9)
         BCTR  R9,0               FIND ITS LAST CHAR
         CLI   0(R9),C'+'         IS A + ALREADY THERE?
         BE    INFONPLS           YES.
         MVC   IOWORK+12(4),IOWORK+8
         MVC   IOWORK+8,=A(BPLUS)      NO, ADD IT ON
         MVI   IOWORK+7,2         UPDATE NUMBER OF SEGMENTS
INFONPLS LA    R3,IOWORK+16       WHERE TO BUILD LEVEL 2 MSG
         ST    R3,IOWORK
         XC    0(4,R3),0(R3)      CLEAR ITS CHAIN
         MVC   4(4,R3),=F'1'      1 SEGMENT FOR LEVEL2
         MVC   8(4,R3),VMSG       STORE 2ND LEVEL ADDR
         XC    PGPB(8),PGPB       INIT  PUTLINE PARM BLOCK
         PUTLINE PARM=PGPB,OUTPUT=(IOWORK,TERM,MULTLVL),MF=(E,IOPL)    *
                                  SEND THE MESSAGES
         SPACE
*
*        INTERPRET PUTLINE RETURN CODES
*
INFORC   B     *+4(R15)           HOW'D IT GO?
         B     INFORET            WONDERFUL
         BAL   R6,PARSRC8         ATTENTION
         BAL   R6,PARSRC12        IMPOSSIBLE CASE
         BAL   R6,PARSRC12        PUTLINE ERROR->PARM LIST ERROR
         BAL   R6,PARSRC16        NO MEMORY
         AIF   ('&OPSYS' NE 'MVS').NMVS8B
         BAL   R6,PARSRC28        TERMINAL DISCONNECTED
.NMVS8B  SPACE
INFORET  XC    VMSG,VMSG          FORGET THE LEVEL 2 MESSAGE
         B     RETURN             AND RETURN
         TITLE 'CLUZREQR/CLUZRQPW/CLUZRQSB -- "ENTER POSITIONAL/PASSWOR*
               D/SUBSCRIPT" MESSAGE ROUTINES'
*
*        THIS ROUTINE GENERATES A PROMPT FOR AN OMITTED POSITIONAL
*
CLUZREQR LIENTRY DSALEN=88,DSA=*,ENV=R5,PARM=R10
         SPACE
         L     R1,=A(ENTERID)
         ST    R1,MSGAREA         STORE MESSAGE ID
         MVC   MSGID,4(R1)        SAVE FOR USE WITH HELPS
         MVC   MSGAREA+4(4),=A(ENTER)  "ENTER "
         MVC   MSGAREA+8(4),=A(QUALFOR)     "QUALIFIER FOR "
         TM    COBFLAGS,QUALFLG   IS THIS AN "ENTER QUALIFIER"?
         BNZ   RQRQUAL            YES.
         OI    MSGAREA+8,X'80'    NO, SUPPRESS THE SEGMENT
RQRQUAL  ST    R10,MSGAREA+12     STORE PROMPT ADDRESS
         OI    MSGAREA+12,X'02'   SET END BIT
         TM    COBFLAGS,QUALFLG   QUALIFIER PROMPT?
         BNZ   RQRNPCE            YES.
         OI    MSGAREA+12,X'04'   YES, SET PCE FLAG
RQRNPCE  LR    R1,R10             SAVE PROMPT POINTER
         TM    0(R9),X'02'        IS THERE ANY HELP?
         BZ    RQRNHELP           NO.
         TM    COBFLAGS,QUALFLG   PROMPTING FOR QUALIFIER?
         BZ    RQRHNQL            NO.
         MVC   ALIGN,0(R1)        YES, FIND PROMPT AFTER DESCRIPTION
         AH    R1,ALIGN
RQRHNQL  BAL   R14,FHLP           CALL HELP FINDER
         LA    R2,2(,R1)          SKIP OVER HELP LENGTH
         B     RQRPROM            GO PROMPT
RQRNHELP SR    R2,R2              SAY NO  HELP AVAILABLE
         SPACE
RQRPROM  LA    R1,MSGAREA
         L     R15,=A(CLUZPROM)   ISSUE PROMPT
         BALR  R14,R15
         B     *+4(R15)           HOW'D IT GO?
         B     RETURN             WELL
         SPACE
*
*        IF THE PROMPT FAILED, GENERATE A "MISSING" MESSAGE
*
         TM    COBFLAGS,QUALFLG   WAS THIS AN "ENTER QUALIFIER"
         BNZ   RQRMISS            YES, SKIP "MISSING" MESSAGE
         MVC   MSGAREA(4),=A(MISSID)   PLUG IN MISSING WORDS
         MVC   MSGAREA+4(4),=A(MISSING)     ADD "MISSING"
         TM    0(R9),X'02'        IS THERE HELP?
         BZ    RQRMNHLP           BRANCH IF NO HELP
         NI    MSGAREA+12,X'FD'   TURN OFF END BIT
         MVC   MSGAREA+16,=A(PLUS)     ADD A +
         OI    MSGAREA+16,X'02'   SET THE END
         LA    R6,MSGAREA
         BAL   R14,BMSG           GO BUILD UP THE MESSAGE
         LA    R1,IOWORK+12       WHERE TO BUILD HELP
         ST    R1,IOWORK          CHAIN TO LEVEL 1
         XC    IOWORK+12(4),IOWORK+12  CLEAR LEVEL 2 CHAIN
         MVC   IOWORK+16(4),=A(3) 3 LEVEL 2 SEGMENTS
         MVC   IOWORK+24(8),MSGAREA    COPY MSGID & MISSING
         LA    R2,1(,R2)          POINT TO 1ST HELP SEG
         ST    R2,IOWORK+20       STORE IN MSG
         XC    PGPB(12),PGPB      INIT PUTLINE PARM BLOCK
         PUTLINE PARM=PGPB,OUTPUT=(IOWORK,TERM,MULTLVL),MF=(E,IOPL)    *
                                  SEND "MISSING XXX"
         B     RQRPUTRC           INTERPRET RET CODES
         SPACE
RQRMNHLP LA    R6,MSGAREA
         BAL   R14,BMSG           FORMAT THE MESSAGE
         XC    PGPB(12),PGPB      INIT PUTLINE PARMS
         PUTLINE PARM=PGPB,OUTPUT=(IOWORK+4,TERM,SINGLE),MF=(E,IOPL)   *
                                  SEND MISSING MSG
         SPACE
RQRPUTRC B     *+4(R15)           INTERPRET PUTLINE RET CODE
         B     RQRMISS            RETURN MISSING
         BAL   R6,PARSRC8         ATTENTION
         BAL   R6,PARSRC12        IMPOSSIBLE
         BAL   R6,PARSRC12        SOMEONE MADE AN ERROR
         BAL   R6,PARSRC16        NO MEMORY
         AIF   ('&OPSYS' NE 'MVS').NMVS8C
         BAL   R6,PARSRC28        TERMINAL DISCONNECTED
.NMVS8C  SPACE
RQRMISS  LA    R15,4              UNABLE TO PROMPT RET CODE
         B     RETURN
         SPACE 5
*
*        THIS ROUTINE ISSUES A PROMPT FOR A (DATASET OR USERID)
*        PASSWORD
*
CLUZRQPW LIENTRY DSALEN=88,DSA=*,ENV=R5,PARM=R3
         MVC   MSGAREA+8(4),=A(PASSWORD)    "PASSWORD"
         AIF   ('&OPSYS' NE 'MVS').NMVS3D
         TM    PASSF,PASSABLE     CHECK FOR NEW PASSWORD
         BNZ   RQROPASS           NO, OLD
         MVC   MSGAREA+8,=A(NEWPASS)   CHANGE TO "NEW PASSWORD"
RQROPASS EQU   *
.NMVS3D  ANOP
         ST    R3,MSGAREA+16      ADD TEXT TO MESSAGE
         OI    MSGAREA+16,X'0B'   SUPPRESSIBLE, PDE, END
RQRFOR   MVC   MSGAREA(4),=A(ENTERID)
         MVC   MSGAREA+4(4),=A(ENTER)  "ENTER "
         MVC   MSGAREA+12(4),=A(FOR)   "FOR"
         OI    MSGAREA+12,X'01'   SUPPRESSIBLE FOR
         CLI   LEFTOVER,0         PASSWORD/SUBSCRIPT FOR INVALID PARM?
         BE    RQRNINV            NO.
         MVC   MSGAREA+16(4),=A(INVPARM)    YES, SAY "PASSWORD FOR...  *
                                            ...INVALID TYPE"
         NI    MSGAREA+12,X'FE'   UNSUPPRESS FOR
         BAL   R14,FDSC           GET INVALID PARM DESCRIPTION
         ST    R1,MSGAREA+20      AND ADD TO MESSAGE
         MVI   MSGAREA+20,X'02'   SHOW END OF MESSAGE
RQRNINV  SR    R2,R2              SAY NO HELP
         LA    R1,MSGAREA
         L     R15,=A(CLUZPROM)
         BALR  R14,R15            ISSUE "ENTER PASSWORD FOR XXX -"
         B     *+4(R15)           WHAT RESULT?
         B     RQRCREST           NORMAL
         SPACE
*
*        IF THE PROMPT FAILED, PUT OUT A MISSING PASSWORD MESSAGE
*
         MVC   MSGAREA(4),=A(MISSID)   CHANGE MSGID
         MVC   MSGAREA+4(4),=A(MISSING) "ENTER"->"MISSING"
         LA    R6,MSGAREA
         BAL   R14,BMSG           WHIP MSG INTO SHAPE
         XC    PGPB(12),PGPB      CLEAR PUTLINE PARMS
         PUTLINE PARM=PGPB,OUTPUT=IOWORK+4,MF=(E,IOPL)                 *
                                  SEND "MISSING PASSWORD FOR X"
         B     *+4(R15)           INTERPRET RET CODE
         B     RQRPMISS           PASSWORD MISSING
         BAL   R6,PARSRC8         ATTENTION
         BAL   R6,PARSRC12        IMPOSSIBLE
         BAL   R6,PARSRC12        ERROR
         BAL   R6,PARSRC16        NO MEMORY
         AIF   ('&OPSYS' NE 'MVS').NMVS8D
         BAL   R6,PARSRC28        TERMINAL DISCONNECTED
.NMVS8D  SPACE
RQRPMISS LA    R15,4              SHOW PROMPT FAILED
RQRCREST CLC   MSGAREA+8(4),=A(SUBSCRS)     SUBSCRIPT REQUEST?
         BNE   RETURN             NO.
         XC    SUBSTAT(SUBSTLEN),SUBSTAT    YES, RESET EVERYTHING
         XC    SUBSOPTS(SUBOPLEN),SUBSOPTS
         OI    COBFLAGS,COBSUB
         B     RETURN
         SPACE 5
*
*        THIS ROUTINE ISSUES A PROMPT FOR A (COBOL VARIABLE) SUBSCRIPT
*
CLUZRQSB LIENTRY DSALEN=88,DSA=*,ENV=R5
         L     R3,SUBSAVE         FIND SUBSCRIPT SAVE AREA
         USING SUBSECT,R3
         MVC   SUBSTAT(SUBSTLEN),SUBSWORK   RESTORE OLD STATUS
         MVC   SUBSOPTS(SUBOPLEN),SUBSFLGS  FOR MESSAGE
         L     R9,SUBSVAR         FIND PCE FOR FATHER VARIABLE
         MVC   MSGAREA+8(4),=A(SUBSCRS)     "SUBSCRIPT(S) "
         MVI   MSGAREA+16,X'0F'   PUT COBOL VAR INTO MSG
         B     RQRFOR             GO BUILD REST OF MSG
         DROP  R3
         TITLE 'CLUZPROM -- TERMINAL PROMPTING ROUTINE'
*
*        PROMPT FOR NEW INPUT.
*
CLUZPROM LIENTRY DSALEN=PROMLEN,DSA=PROMDSA,ENV=R5,PARM=R6
         CLI   DEFTF,0            PROCESSING DEFAULT?
         BE    PROMALOW           NO, PROMPT OK
         BAL   R6,PARSRC12        YES, CALLER ERROR
PROMALOW BAL   R14,BMSG           GO TRANSFORM MESSAGE
         MVI   IOWORK+7,2         INDICATE TWO SEGMENTS
         MVC   IOWORK+12(4),IOWORK+8   MOVE TEXT PROPER
         MVC   IOWORK+8(4),=A(MINUS)   INDICATE PROMPT BY -
         LTR   R2,R2              IS HELP PROVIDED?
         BZ    PROMNHLP           NO.
         L     R1,IOWORK+12       YES, FIND END OF MESSAGE
         AH    R1,0(,R1)
         BCTR  R1,0
         CLI   0(R1),C'+'         IS A + ALREADY THERE?
         BE    PROMNHLP           NO.
         MVC   IOWORK+8(4),=A(PLMINUS) YES, INSERT A + BEFORE
PROMNHLP MVC   PGPB(16),PGMODEL   MOVE PUTGET PARMS
         CLI   PCBYPASS,X'00'     SHOULD WE GET WITH BYPASS?
         BE    PROMNBY1           NO.
         SPACE
*
*        AT THIS TIME, TSO/VTAM BEHAVES UNREASONABLY IF A 3270 IS
*        SENT TWO BYPASS CHARACTERS, SEPARATED BY A TGET NOWAIT.
*        THE FOLLOWING LOGIC CIRCUMVENTS THIS BY ARRANGING TO SEND
*        ONLY ONE. THIS FORCES A PASSWORD PROMPT TO BE ISSUED IN
*        ASIS MODE, WHICH IS (TO MY MIND) AN UNFORTUNATE
*        INCONSISTENCY
*
         MVI   IOWORK+7,3         ADD ANOTHER SEGMENT FOR BYPASS
         MVC   IOWORK+16(4),=A(BYPSEG)
         OI    PGPB+2,X'01'       FORCE ASIS PROMPT
         SPACE
PROMNBY1 LA    R3,IOWORK+4        FIND THE OLD
         ST    R3,PGPB+4          STORE IN PARM BLOCK
         SPACE
*
*        ISSUE INITIAL PROMPT, BUT DON'T WAIT FOR AN ANSWER
*
         PUTGET PARM=PGPB,TERMGET=(NOWAIT),MF=(E,IOPL) PROMPT W/NO WAIT
         B     *+4(R15)           ACT ON RET CODE
         BAL   R6,PARSRC12        IMPOSSIBLE
         BAL   R6,PARSRC12        IMPOSSIBLE
         BAL   R6,PARSRC8         ATTENTION
         B     PROMNPRO           NOPROMPT IN EFFECT
         BAL   R6,PARSRC12        IMPOSSIBLE
         B     PROMNBY2           PROMPT MODE
         BAL   R6,PARSRC12        PUTGET ERROR
         BAL   R6,PARSRC16        NO MEMORY
         AIF   ('&OPSYS' NE 'MVS').NMVS8G
         BAL   R6,PARSRC28        TERMINAL DISCONNECTED
.NMVS8G  SPACE
*
*        IN NOPROMPT MODE, SET FLAG AND RC=4
*
PROMNPRO MVI   RC,4               SET NOPROMPT RET CODE
         LA    R15,4
PROMRET  XC    VMSG,VMSG          CLEAR SECOND LEVEL MSG
         B     RETURN             AND RETURN
         SPACE
*
*        WAIT FOR A RESPONSE TO THE PROMPT
*
PROMGETL CLI   PCBYPASS,X'00'     BYPASS MODE?
         BE    PROMNBY2           NO.
         TPUT  BYCHAR,1,CONTROL   YES, SEND OUT A BYPASS
         SPACE
PROMNBY2 GETLINE PARM=PGPB+8,INPUT=(TERM),TERMGET=(WAIT),              *
               MF=(E,IOPL)        NOW GET A RESPONSE
         LR    R9,R15             SAVE RET CODE
         CLI   PCBYPASS,X'00'     BYPASSING?
         BE    PROMNBY3           NO.
         TPUT  RESTCHAR,1,CONTROL YES, SEND A RESTORE
PROMNBY3 B     *+4(R9)            THEN ACT ON RET CODE
         B     PROMGOT            GOT A RESPONSE
         BAL   R6,PARSRC12        IMPOSSIBLE
         BAL   R6,PARSRC8         ATTENTION
         BAL   R6,PARSRC12        IMPOSSIBLE
         BAL   R6,PARSRC12        IMPOSSIBLE
         BAL   R6,PARSRC12        GETLINE ERROR
         BAL   R6,PARSRC16        NO MEMORY
         AIF   ('&OPSYS' NE 'MVS').NMVS8I
         BAL   R6,PARSRC28        TERMINAL DISCONNECTED
.NMVS8I  SPACE 3
         SPACE
*
*        IF THE RESPONSE IS NOT ?, STACK IT AND RETURN
*
PROMGOT  L     R9,PGPB+12         ADDRESS RESPONSE
         CLC   0(2,R9),=H'4'      IS IT EMPTY?
         BE    PROMFRAN           YES, FREE IT & RETURN
         CLI   4(R9),C'?'         NO, IS IT ? ?
         BE    PROMQMRK           YES, HANDLE IT
         L     R1,IOPL+4          FIND ECT
         OI    8(R1),X'80'        SET 2ND LEVEL PURGE FLAG
         LH    R0,0(,R9)          GET LENGTH OF RESPONSE
         SH    R0,=H'4'           SUBTRACT HEADER LENGTH
         STH   R0,ALIGN           STORE FOR CLUZPUSH
         LA    R1,4(,R9)          POINT TO TEXT
         L     R15,=A(CLUZPUSH)
         BALR  R14,R15            STACK THE NEW INPUT
         MVI   INSFLAG,VIRGINP    NOTE INTACT INPUT LINE
         SPACE
*
*        FREE THE PROMPT REPLY AND RETURN
*
PROMFRAN LR    R1,R9              PROMPT ADDRESS->R1
         LH    R0,0(,R9)          LOAD LENGTH
         A     R0,=X'01000000'    INSERT SUBPOOL 1
         FREEMAIN R,LV=(0),A=(1)  FREE THE RESPONSE
         SR    R15,R15            SET GOOD RETURN CODE
         B     PROMRET            AND RETURN
         SPACE
*
*        IF ? WAS ENTERED, DISPLAY ALL EXISTING HELP INFORMATION
*
PROMQMRK LR    R1,R9              FREE THE RESPONSE
         LH    R0,0(,R9)
         A     R0,=X'01000000'
         FREEMAIN R,LV=(0),A=(1)
         OC    VMSG+1(3),VMSG+1   IS THERE A LEVEL2 MSG?
         BNZ   PROMVMSG           YES, PUT IT OUT
         L     R9,IOPL+4          FIND ECT
         OC    9(3,R9),9(R9)      IS THERE PENDING LEVEL2 TEXT?
         BNZ   PROMLV2            YES.
         LTR   R2,R2              NO, IS THERE HELP INFO?
         BNZ   PROMHELP           YES, SEND IT
         SPACE
*
*        IF THERE IS PUTLINE HELP OUTSTANDING, SPILL IT
*
PROMLV2  XC    PGPB(12),PGPB      CLEAR PUTLINE PARM BLOCK
         PUTLINE PARM=PGPB,OUTPUT=0,MF=(E,IOPL) OUTPUT LEVEL2 TEXT
PROMPTRC B     *+4(R15)           ACT ON RETURN CODE
         B     PROMNVMG           NORMAL
         BAL   R6,PARSRC8         ATTENTION
         BAL   R6,PARSRC12        IMPOSSIBLE
         BAL   R6,PARSRC12        BAD PARMS
         BAL   R6,PARSRC16        NO MEMORY
         AIF   ('&OPSYS' NE 'MVS').NMVS8F
         BAL   R6,PARSRC28        TERMINAL DISCONNECTED
.NMVS8F  SPACE
*
*        IF A VALIDATION EXIT MESSAGE EXISTS, PRINT IT
*
PROMVMSG XC    PGPB(12),PGPB      INITIALIZE PUTLINE BLOCK
         MVC   VWORK,=A(1)        BUILD A 1 SEGMENT MSG
         MVC   VWORK+4(4),VMSG
         XC    VMSG(4),VMSG       THEN CLEAR THE MSG ADDR
         PUTLINE PARM=PGPB,OUTPUT=(VWORK),MF=(E,IOPL) SEND EXIT MSG
         B     PROMPTRC(R15)      ACT ON RETURN CODES
         SPACE
*
*        IF HELP EXISTS, REPROMPT WITH FIRST HELP MESSAGE
*
PROMNVMG LTR   R2,R2              ANY HELP OUT?
         BZ    PROMGETL           NO, JUST REGET
PROMHELP SR    R9,R9              YES, BUILD FULL MULTI-LEVEL PROMPT
         IC    R9,0(,R2)          NUMBER OF LEVELS
         LR    R0,R9
         MH    R0,=H'32'          NUMBER SEGMENTS/LEVEL
         LIFO  LV=(0)             GET MEMORY FOR OLDS
         LR    R6,R1              SAVE ADDR
         LA    R14,1(,R2)         POINT TO 1ST HELP TEXT
         SPACE
PROMHLOP MVC   8(4,R6),=A(MINUS)
         MVC   MSGID-4(4),=AL2(13,0)   PUT MSGID IN MSG
         LA    R15,MSGID-4
         ST    R15,12(,R6)
         ST    R14,24(,R6)        PUT HELP INFO IN MSG
         MVC   16(4,R6),=A(ENTER) ADD "ENTER"
         CH    R9,=H'1'           ANY MORE HELP?
         BE    PROMNPLS           NO.
         LH    R15,0(,R14)
         AR    R15,R14
         BCTR  R15,0
         CLI   0(R15),C'+'        SEE IF TEXT ENDS WITH +
         BE    PROMNPLS           YES.
         MVC   8(4,R6),=A(PLMINUS)     NO, ADD +
PROMNPLS TM    COBFLAGS,QUALFLG   PROMPTING FOR QUALIFIERS?
         BZ    PROMNQUL           NO, NORMAL CASE
         MVC   4(4,R6),=A(5)      YES, SET NUMBER OF SEGMENTS
         MVC   20(4,R6),=A(QUALFOR)    ADD "QUALIFICATION FOR"
         B     PROMQUAL
PROMNQUL MVC   20(8,R6),24(R6)    REALIGN SEGMENTS
         MVC   4(4,R6),=A(4)      STORE NUMBER OF SEGMENTS
PROMQUAL BCT   R9,*+8             AT BOTTOM LEVEL?
         B     PROMHEND           YES.
         LA    R15,28(,R6)        NO, CHAIN TO NEXT LEVEL
         ST    R15,0(,R6)
         LR    R6,R15
         MVC   ALIGN,1(R2)
         AH    R2,ALIGN
         LA    R14,1(,R2)         SKIP TO NEXT HELP
         B     PROMHLOP           AND CONTINUE
         SPACE
*
*        PROMPT AGAIN
*
PROMHEND XC    0(4,R6),0(R6)      CLEAR FINAL CHAIN
         LR    R2,R1              SAVE OLD ADDR
         MVC   PGPB(16),PGMODEL   MOVE PUTGET PARMS
         MVI   PGPB,X'05'         MAKE MULTI-LEVEL
         CLI   PCBYPASS,X'00'     BYPASS NEEDED?
         BE    PROMNBY4           NO.
         OI    PGPB+1,X'10'       YES, SET BYPASS BIT
PROMNBY4 ST    R1,PGPB+4          STORE OLD IN PARMS
         PUTGET PARM=PGPB,MF=(E,IOPL)  ISSUE LEVEL2 PROMPT
         B     *+4(R15)           INTERPRET RETURN CODE
         B     PROMGOT            GOT INPUT
         BAL   R6,PARSRC12        IMPOSSIBLE
         BAL   R6,PARSRC8         ATTENTION
         BAL   R6,PARSRC12        IMPOSSIBLE
         BAL   R6,PARSRC12        IMPOSSIBLE
         BAL   R6,PARSRC12        IMPOSSIBLE
         BAL   R6,PARSRC12        PARM ERROR
         BAL   R6,PARSRC16        NO MEMORY
         AIF   ('&OPSYS' NE 'MVS').NMVS8H
         BAL   R6,PARSRC28        TERMINAL DISCONNECTED
.NMVS8H  SPACE 3
PROMDSA  DSECT ,                  PROMPT WORK AREA
         SPACE
         DS    22F
VWORK    DS    2A                 VALIDCK MSG WORK AREA
PROMLEN  EQU   *-PROMDSA          LENGTH OF CLUZPROM DSA
         SPACE
CLUTSCOM CSECT ,                  RESUME CSECT
         TITLE 'CLUTSCOM -- GENERALIZED MESSAGE BUILDING ROUTINE'
*
*        THIS ROUTINE IS CALLED TO BUILD A MESSAGE FROM A LIST OF
*        MESSAGE SEGMENTS
*
BMSG     ST    R2,BSAVE+4
         SR    R2,R2              CLEAR COUNT REGISTER
         ST    R14,BSAVE          SAVE RETURN POINT
         LIFO  LV=264             GET A BUFFER TO PUT THE MESSAGE IN
         LA    R3,4(,R1)          POINT R3 TO TEXT START LOCATION
         ST    R1,IOWORK+8        SAVE ADDRESS IN OLD
BMSGLOOP TM    0(R6),X'80'        SUPPRESS THIS SEGMENT?
         BNZ   BMSGNEXT           YES.
         TM    0(R6),X'01'        IS THIS PART SUPPRESSIBLE?
         BZ    BMSGNSUP           NO.
         CLI   BYPASS,X'00'       YES, SHOULD IT BE SUPPRESSED?
         BNE   BMSGNEXT           YES, ON TO NEXT TEXT
         TM    RANGEF,RANGE2      WHICH RANGE HALF?
         BNZ   BMSGSUP2           SECOND
         TM    COBFLGS1,COBHBYP+COBTBYP     CHECK FOR COBOL BYPASS
         BO    BMSGNEXT           FORGET COBOL IF HEAD & TAIL SECRET
         TM    RANGEF,RANGERR     BOTH HALVES NEEDED?
         BZ    BMSGNSUP           NO.
BMSGSUP2 TM    COBFLGS2,COBHBYP+COBTBYP     CHECK PART 2 FLAGS
         BO    BMSGNEXT
         SPACE
BMSGNSUP NI    0(R6),X'FE'        TURN OFF SUPPRESS FLAG
         LA    R14,BMSGNEXT       DEFAULT RESUME POINT
         SR    R15,R15
         IC    R15,0(,R6)
         N     R15,=F'-4'         GET SEGMENT TYPE
         B     *+4(R15)           AND CHOOSE POISON
         B     BMSGSEG            MSG SEGMENT HANDLER
         B     BMSGPCE            PCE FORMAT HANDLER
         B     BMSGPDE            PDE FORMAT HANDLER
         B     BMSGINPT           INPUT TEXT HANDLER
         SPACE
*
*        HANDLE SEGMENT ALREADY IN PUTLINE FORMAT
*
BMSGSEG  L     R1,0(,R6)          FIND THE SEGMENT
         LH    R15,0(,R1)         LOAD THE LENGTH
         S     R15,=A(5)          MAKE ACTUAL LENGTH-1
         LA    R1,4(,R1)          POINT TO TEXT START
         B     BMSGADD            GO ADD TO BUFFER
*
*        HANDLE SEGMENT CONTAINED IN A PCE
*
BMSGPCE  L     R1,0(,R6)          POINT TO THE LENGTH BYTE
         IC    R15,0(,R1)         LENGTH INTO R15
         LA    R1,1(,R1)          SKIP PAST LENGTH
         B     BMSGADD            GO ADD TO TEXT
         SPACE
*
*        HANDLE SEGMENT ADDRESSED BY A PDE
*
BMSGPDE  L     R1,0(,R6)          LOCATE THE PDE
         LH    R15,4(,R1)         GET THE LENGTH
         LTR   R15,R15            IS IT 0?
         BZ    BMSGMEM            YES, USE MEMBER NAME INSTEAD
         BCTR  R15,0              NO, ADJUST FOR EX
         L     R1,0(,R1)          FIND THE DATA
         B     BMSGADD            GO ADD TO MSG
         SPACE
*
*        HANDLE SEGMENT FOR MEMBER-ONLY DSNAME
*
BMSGMEM  SR    R15,R15            LENGTH 0
         LA    R1,PDOTS           ADD A (
         BAL   R14,BMSGADD
         L     R1,0(,R6)          GET BACK PDE ADDR
         LH    R15,12(,R1)        LENGTH OF MEMBER NAME
         BCTR  R15,0
         L     R1,8(,R1)          LOCATION OF MEMBER NAME
         BAL   R14,BMSGADD        GO ADD IT
         SR    R15,R15
         LA    R1,PDOTS+4         ADD A )
         BAL   R14,BMSGADD
         B     BMSGNEXT           AND THEN CONTINUE
         SPACE
*
*        HANDLE CURRENT AND/OR PREVIOUS INPUT TEXT SEGMENT
*
BMSGINPT TM    COBFLAGS,COBFORM+QUALFLG     FULL COBOL ITEM REJECTED?
         BNM   BMSGNCOB           NO.
         TM    OPERFLGS,OPERINV   INVALID EXPRESSION?
         BZ    BMSGNOPO           NO.
         LA    R1,PDOTS           YES, INSERT (
         SR    R15,R15
         BAL   R14,BMSGADD
         CLI   OPERPRES,0         HAS ANYTHING APPEARED YET?
         BE    BMSGNEXT           NO, NOTHING MORE TO SAY
BMSGNOPO TM    RANGEF,RANGE2      WORKING ON RANGE 2ND PART?
         BNZ   BMSGCHD2           YES.
         TM    COBFLGS1,COBOLVAR  IS IT A VARIABLE AT ALL?
         BZ    BMSGNCOB           NO.
         TM    COBFLGS1,COBQVAR   IS IT A COMPLEX COBOL VAR?
         BZ    BMSGSHD1           NO, NO DOTS NEEDED
         OI    COBFLAGS,COBDOTS   LET DOTS SEPARATE HEAD & TAIL
BMSGSHD1 L     R1,COBHEAD1        FIND THE FIRST QUALIFIER
         L     R15,COBHEAD1+4     & ITS END
         LR    R0,R1              SAVE START FOR LATER
         TM    COBFLGS1,COBHBYP   WAS THE HEAD HIDDEN?
         BZ    BMSGCBLN           NO.
         B     BMSGCDOT           YES.
BMSGCHD2 TM    COBFLGS2,COBOLVAR  REALLY A VARIABLE?
         BZ    BMSGNCOB           NO.
         TM    COBFLGS2,COBQVAR   COMPLEX COBOL VAR?
         BZ    BMSGSHD2           NO, NO DOTS NEEDED
         OI    COBFLAGS,COBDOTS
BMSGSHD2 L     R1,COBHEAD2
         L     R15,COBHEAD2+4
         LR    R0,R1              SAVE STARTING POINT
         TM    COBFLGS2,COBHBYP   FIRST PART SUPPRESSED?
         BNZ   BMSGCDOT           NO.
BMSGCBLN SR    R15,R1
         CH    R15,=H'50'         MUCH TOO LONG?
         BNH   BMSGNDOT           NO.
         LA    R15,50             YES, CUT IT SHORT
         OI    COBFLAGS,COBDOTS   NOTE DOTS NEEDED
         B     BMSGNDOT           GO JOIN NON-COBOL CODE
         SPACE
BMSGNCOB TM    RANGEF,RANGERR+RANGE0   HANDLING RANGE ERRORS?
         BZ    BMSGSING           NO.
         L     R1,RANGEPOS        FIND POSITION OF RANGE START
         L     R15,RANGEND        FIND END OF IT
         SR    R15,R1             GET LENGTH
         B     BMSGINP2           AND JOIN OTHER CASE
BMSGSING LR    R15,R8             MOVE ENDING POSITION
         TM    PASSF,PASSCAN      SCANNED FOR A PASSWORD?
         BZ    BMSGNPAS           NO, ENDING POS IS OK
         L     R15,PASSTART       YES, PARM ENDS WHERE PSWD STARTED
BMSGNPAS S     R15,PREVPOS        COMPUTE LENGTH
         L     R1,PREVPOS         FIND START OF TEXT
BMSGINP2 CH    R15,=H'77'         IS IT VERY LONG?
         BNH   BMSGNDOT           NO.
         LA    R15,80             YES, SET LENGTH TO 80
         SPACE
BMSGNDOT BCTR  R15,0              GET READY FOR EX
         BAL   R14,BMSGADD        ADD THE INPUT
         CH    R15,=H'76'         WAS IT TOO LONG?
         BNH   BMSGPASS           NO.
         LA    R1,PDOTS+1         YES, ADD ... AFTER
         LA    R15,2
         BAL   R14,BMSGADD
BMSGPASS TM    PASSF,PASSCAN+PASSEEN   REJECTED PARM INCLUDE PASSWORD?
         BNO   BMSGCDOT           NO.
         LA    R15,8              YES, ADD "/PASSWORD" TO REJECTED
         LA    R1,=C'/PASSWORD'   TEXT
         BAL   R14,BMSGADD
BMSGCDOT TM    COBFLAGS,COBFORM+QUALFLG     COBOL VAR-TYPE DISPLAY?
         BNM   BMSGCOLN           NO, SKIP COBOL JUNK
         TM    COBFLAGS,COBDOTS   ARE COBOL DOTS NEEDED?
         BZ    BMSGCNDT           NO.
         LA    R1,PDOTS+1         YES, ADD THEM
         LA    R15,2
         BAL   R14,BMSGADD
BMSGCNDT TM    RANGEF,RANGE2      WORKING ON 2ND PART OF RANGE?
         BNZ   BMSGCTL2           YES.
         C     R0,COBHEAD1        HEAD OR TAIL OF VAR?
         BNE   BMSGATL1           TAIL, CHECK FOR SUBSCRIPT
         TM    COBFLGS1,COBQVAR   ARE THERE QUALIFIERS?
         BZ    BMSGATL1           NO, CAN STOP NOW
         NI    COBFLAGS,255-COBDOTS    TURN OFF DOTS FOR TAIL
         TM    COBFLGS1,COBTBYP   IS THE TAIL QUALIFIER SECRET?
         BNZ   BMSGATL1           YES, LEAVE IT OFF
         L     R1,COBTAIL1        HEAD, NOW ADD THE TAIL
         L     R15,COBTAIL1+4
         LR    R0,R1
         B     BMSGCBLN
BMSGATL1 TM    COBFLGS1,COBHASUB  DO WE NEED TO ADD A SUBSCRIPT?
         BNZ   BMSGSDOT           YES.
         B     BMSGCLOS           NO, CHECK FOR OPER CLOSE
BMSGCTL2 C     R0,COBHEAD2
         BNE   BMSGATL2           TAIL ALREADY HANDLED
         TM    COBFLGS2,COBQVAR   FULLY QUALIFIED?
         BZ    BMSGATL2           NO.
         NI    COBFLAGS,255-COBDOTS    YES, FORGET THE DOTS
         TM    COBFLGS2,COBTBYP   TAIL PART SECRET?
         BNZ   BMSGATL2           YES, HIDE IT
         L     R1,COBTAIL2
         L     R15,COBTAIL2+4
         LR    R0,R1
         B     BMSGCBLN           GO DO THE TAIL
BMSGATL2 TM    COBFLGS2,COBHASUB  NEED SUBSCRIPT INDICATOR?
         BZ    BMSGCLOS           NO.
BMSGSDOT LA    R1,PDOTS           YES, ADD (...)
         LA    R15,4
         BAL   R14,BMSGADD
BMSGCLOS TM    OPERFLGS,OPERINV+OPERCLS     NEED TO CLOSE AN EXPR?
         BNO   BMSGCOLN           NO.
         LA    R1,PDOTS+4         YES, ADD )
         SR    R15,R15
         BAL   R14,BMSGADD
BMSGCOLN TM    RANGEF,RANGERR     NEED COLON?
         BZ    BMSGNEXT           NO, CONTINUE
         SR    R15,R15            YES, ADD COLON
         LA    R1,=C':'
         BAL   R14,BMSGADD
         XI    RANGEF,RANGERR+RANGE2   RESET FLAGS TO ADD VALUE2
         TM    COBFLGS2,COBFORM+QUALFLG     COBOL VAR-TYPE DISPLAY?
         BM    BMSGCHD2           YES, GO TO COBOL CODE
         B     BMSGSING
         SPACE
*
*        PROCEED TO NEXT SEGMENT
*
BMSGNEXT TM    0(R6),X'02'        LAST SEGMENT?
         BO    BMSGLAST           YES.
         LA    R6,4(,R6)          NO, INCREASE INPUT POINTER
         B     BMSGLOOP           AND PROCEED
         SPACE
BMSGLAST LA    R2,4(,R2)          ADD 4 TO LENGTH FOR PREFIX
         L     R1,IOWORK+8        FIND THE BUFFER
         STH   R2,0(,R1)          STORE THE LENGTH
         XC    2(2,R1),2(R1)      MAKE THE OFFSET 0
         MVC   IOWORK+4(4),=A(1)  INDICATE 1 SEGMENT
         L     R14,BSAVE          RESTORE RETURN ADDR
         L     R2,BSAVE+4
         BR    R14                AND RETURN
         SPACE
BMSGADD  LA    R2,1(R2,R15)       UPDATE LENGTH REG
         CH    R2,=H'255'         GOTTEN TOO LARGE?
         BNH   BMSGOK             NO.
         BAL   R6,PARSRC12        YES, INDICATE ERROR
BMSGOK   EX    R15,BMSGMV         MOVE IN TEXT
         LA    R3,1(R3,R15)       UPDATE OUTPUT CURSOR
         BR    R14                AND RETURN
BMSGMV   MVC   0(0,R3),0(R1)      MOVE TEXT TO OUTPUT SEGMENT
         AIF   ('&OPSYS' NE 'MVS').NMVS10C
         TITLE 'CLUZCNFM -- NEW PASSWORD CONFIRMATION ROUTINE'
*
*        THIS ROUTINE PROMPTS TO CONFIRM A NEW PASSWORD, AND RETURNS
*        TO ITS CALLER WHETHER CONFIRMATION WAS SUCCESSFUL
*
CLUZCNFM LIENTRY DSALEN=CNFMDSAL,DSA=CNFMDSA,ENV=R5,PARM=R2
         MVC   ORIGPASS(6),0(R2)  SAVE CURRENT PDE CONTENTS
         MVC   PGPB(16),PGMODEL   PREPARE FOR PROMPT
         PUTGET PARM=PGPB,OUTPUT=(CNFMOLD,SINGLE,PTBYPS),MF=(E,IOPL)   *
                                  ASK FOR CONFIRMATION
         B     *+4(R15)           GET A RESPONSE?
         B     CNFMVERF           YES, CHECK IT OUT
         BAL   R6,PARSRC12        IMPOSSIBLE
         BAL   R6,PARSRC8         ATTENTION
         B     CNFMOK             PROMPT FAILED, TREAT AS CONFIRMED
         BAL   R6,PARSRC12        IMPOSSIBLE
         BAL   R6,PARSRC12        IMPOSSIBLE
         BAL   R6,PARSRC12
         BAL   R6,PARSRC16        NO MEMORY LEFT
         BAL   R6,PARSRC28        TERMINAL DISCONNECTED
         SPACE
CNFMOK   SR    R15,R15            SHOW PASSWORD CONFIRMED
         B     RETURN             AND RETURN
         SPACE
CNFMVERF L     R3,PGPB+12         GET PROMPT RESPONSE ADDRESS
         LH    R10,0(,R3)         LENGTH OF RESPONSE
         SH    R10,=H'4'          ANYTHING SAID?
         BZ    CNFMFREP           NO, JUST FREE IT
         STH   R10,ALIGN          STORE LENGTH FOR PUSH ROUTINE
         LA    R1,4(,R3)          POINT TO "CONFIRMATION"
         L     R15,=A(CLUZPUSH)
         BALR  R14,R15            PUT RESPONSE ON STACK
         MVI   INSFLAG,PSWDCONF   LABEL AS PASSWORD CONFIRMATION
         SPACE
CNFMFREP LR    R1,R3              GIVE BACK PUTGET RESPONSE
         LR    R0,R10
         A     R0,=X'01000004'    FROM SUBPOOL 1
         FREEMAIN R,LV=(0),A=(1)
         LTR   R10,R10            DID WE GET ANYTHING?
         BZ    CNFMFAIL           NO, RETURN UNHAPPY
         SPACE
*
*        SKIP SEPARATORS AND SEE IF WHAT FOLLOWS LOOKS LIKE A
*        PASSWORD. IF SO, SEE IF IT'S THE SAME ONE
*
         L     R15,=A(CLUZSKIP)   LOOK FOR INPUT START
         BALR  R14,R15
         LTR   R15,R15            FIND SOMETHING?
         BNZ   CNFMEXIT           NO, CONFIRM FAILS
         LR    R1,R2              YES, COPY PDE ADDRESS
         L     R15,=A(CLUZCHAR)
         BALR  R14,R15            IS THIS A PASSWORD?
         LTR   R15,R15
         BNZ   CNFMEXIT           NO, TOO BAD
         LH    R15,ORIGPASS+4     YES, LOAD FIRST PASSWORD LENGTH
         CH    R15,4(,R2)         SAME AS THIS ONE?
         BNE   CNFMEXIT           NO, TOO BAD
         LR    R14,R15            YES, COPY TO BETTER PLACE
         L     R15,ORIGPASS       FIND ORIGINAL PASSWORD TEXT
         L     R1,0(,R2)          NEW PASSWORD TEXT
         BCTR  R14,0
         EX    R14,CNFMCOMP       ANY DIFFERENCE?
         BNE   CNFMEXIT           YES, WON'T DO
         SPACE
*
*        SEE IF THERE IS ANYTHING IN THE RESPONSE AFTER THE
*        PASSWORD. IF SO, TREAT IT AS CONFIRMATION FAILURE.
*
         L     R15,=A(CLUZSKIP)
         BALR  R14,R15            SEE IF ANYTHING FOLLOWING
         LCR   R15,R15
         LA    R15,4(,R15)        R15=0 IF NOT
         SPACE
*
*        UNSTACK THE PROMPT RESPONSE AND RETURN. IF CONFIRMATION
*        FAILED, SEND A MESSAGE FIRST
*
CNFMEXIT LR    R10,R15            SAVE SUCCESS INDICATION
         L     R15,=A(CLUZPOPS)
         BALR  R14,R15            THROW CONFIRMATION AWAY
         LTR   R15,R10            DID IT CONFIRM?
         BZ    RETURN             YES, HOORAH
         SPACE
CNFMFAIL MVC   MSGAREA(4),=A(CONFAIL)   NO, COMPLAIN
         OI    MSGAREA,X'02'
         LA    R1,MSGAREA
         L     R15,=A(CLUZINFO)
         BALR  R14,R15
         LA    R15,4              SET UNHAPPY RETURN CODE
         XC    0(8,R2),0(R2)      ZERO PASSWORD PDE
         B     RETURN             AND GO BACK
         SPACE
CNFMCOMP CLC   0(0,R15),0(R1)     COMPARE PASSWORD VERSIONS
         SPACE
CNFMOLD  DC    A(1,CONFIRM)       OLD FOR CONFIRMATION PROMPT
         SPACE
CNFMDSA  DSECT ,                  OUR DSA
         SPACE
         DS    22F
ORIGPASS DS    CL6                COPY OF PASSWORD PCE ON ENTRY
         DS    0D
CNFMDSAL EQU   *-CNFMDSA          OUR DSA LENGTH
         SPACE
CLUTSCOM CSECT ,                  RESTORE OUR CSECT
.NMVS10C TITLE 'CLUZGASP -- INTERNAL/PARM LIST ERROR MESSAGE ROUTINE'
*
*        THIS ROUTINE IS CALLED TO WRITE A MESSAGE TO THE TERMINAL
*        DESCRIBING WHERE A "PARM LIST" ERROR (REALLY ANYTHING WE
*        DON'T UNDERSTAND) WAS DETECTED. IT IS USED ONLY FOR USERS
*        WHO HAVE SET PROFILE MSGID TO PROVE THEIR SERIOUS INTEREST
*        IN OBSCURE AND HARD-TO-USE INFORMATION.
*
CLUZGASP LIENTRY DSALEN=GASPLEN,DSA=GASPDSA,ENV=R5
*
*        ABNORMAL EXITS FROM PARSE ARE MADE WITH "BAL  R6,".
*        THEREFORE, R6 ADDRESSES THE INSTRUCTION AFTER THE BAL
*        WHICH NOTES THE ERROR.
*
         S     R6,=A(TSUCOPRT+4)  FIND ERROR OFFSET FROM START
         SLL   R6,4
         LA    R6,15(,R6)         PREPARE TO UNPACK IT
         ST    R6,ERROFFH
         UNPK  ERROFF(4),ERROFFH+1(3)  UNPK TO PSEUDO-HEX
         TR    ERROFF,GASPHEX-240 MAKE IT REAL HEX
         MVC   ERRSEG(4),=AL2(8,0)     SET UP MESSAGE SEGMENT
         MVC   ERROLD(4),=A(2)    AND OLDS
         MVC   ERROLD+8(4),=A(ERRAT)   "ERROR DETECTED AT OFFSET"
         LA    R1,ERRSEG
         ST    R1,ERROLD+4        STORE VARIABLE SEGMENT LOCATION
         XC    PGPB(8),PGPB       PREPARE IOPB
         PUTLINE PARM=PGPB,OUTPUT=(ERROLD,TERM,SINGLE),MF=(E,IOPL)     *
                                  TELL USER WHERE ERROR NOTED
         B     RETURN             AND RETURN (IGNORING RETURN CODES)
         SPACE
GASPHEX  DC    C'0123456789ABCDEF'     TABLE FOR HEX TRANSLATION
         SPACE 3
GASPDSA  DSECT ,                  A NICE WORK AREA
         DS    22F
ERROFFH  DS    A                  ERROR OFFSET IN BINARY
ERROLD   DS    3A                 OLD FOR MESSAGE
ERRSEG   DS    2H                 MESSAGE SEGMENT HEADER
ERROFF   DS    CL4                PRINTABLE ERROR OFFSET
         DS    0D
         SPACE
GASPLEN  EQU   *-GASPDSA          LENGTH OF WORK AREA
         TITLE 'CLUTSMEM -- MEMORY MANAGEMENT ROUTINES'
CLUTSMEM LISECT ,                 DEFINE MEMORY MANAGEMENT CSECT
         SPACE
*
*        THIS CSECT CONTAINS THE PARSE MEMORY MANAGEMENT ROUTINES.
*        THEY WERE DESIGNED TO (A) REDUCE THE NUMBER OF GETMAINS
*        AND FREEMAINS REQUIRED, BOTH DURING EXECUTION AND ON CALL
*        TO IKJRLSA, AND (B) NOT WASTE MEMORY UNNECESSARILY.
*
*        THE HIGH POINTS OF THE MEMORY MANAGEMENT ALGORITHMS ARE:
*
* 1.     MEMORY IS ALLOCATED IN BLOCKS, OF PREFERRED SIZE 1K.
*        THE BLOCKS ARE CHAINED FOR EVENTUAL RELEASE BY IKJRLSA.
* 2.     THE BLOCKS ARE SUBALLOCATED INTO SEGMENTS. EACH SEGMENT
*        MAY BE OWNED BY A PARTICULAR PCE, OR COLLECTIVELY.  OWNED
*        SEGMENTS MAY BE FREED, IN WHICH CASE THEY BECOME AVAILABLE
*        FOR ALLOCATION OF COLLECTIVE STORAGE. COLLECTIVE SEGMENTS
*        ARE NEVER FREED.  OWNED STORAGE IS ALLOCATED FROM THE
*        BOTTOM OF A BLOCK; COLLECTIVE STORAGE IS ALLOCATED FROM
*        FREED OWNED SEGMENTS, AND THEN FROM THE TOP OF A BLOCK.
* 3.     AS AN OPERAND IS PROCESSED, ITS OWNED SEGMENT HAS A STACK
*        STRUCTURE, DIVIDED INTO "LEVELS" OF NESTING.  (THE DEFINED
*        LEVELS ARE LIST, RANGE/EXPRESSION, OPERAND, AND
*        SUBSCRIPT/PASSWORD.)  WHENEVER PROCESSING AT A LEVEL IS
*        COMPLETE, THE STORAGE WILL BE "COMMITTED" TO A HIGHER
*        LEVEL IF PROCESSING WAS SUCCESSFUL, OR "ROLLED BACK"
*        IF UNSUCCESSFUL.
*
         TITLE 'CLUZMINT -- MEMORY INITIALIZATION ROUTINE'
*
*        THIS ROUTINE ALLOCATES THE FIRST BLOCK OF STORAGE FOR
*        PARSE, AND SUBALLOCATES THE PDL FROM IT
*
CLUZMINT LIENTRY DSALEN=MEMLEN,DSA=MEMDSA,ENV=R5,PARM=R2
         LA    R1,128(,R2)        GET AT LEAST 128 BYTES MORE
         BAL   R6,GBLK            GO ALLOCATE A BLOCK
         L     R1,MEMBLOCK        GET ALLOCATED BLOCK ADDRESS
         ST    R1,PDLPTR          STORE AS PDL ADDRESS
         LA    R3,8(R2,R1)        FIND BYTE AFTER THE PDL
         ST    R3,HIGHMARK        STORE AS HIGH-WATER MARK
         A     R1,BLKLEN          FIND END OF BLOCK
         ST    R1,LOWMARK         STORE AS LOW-WATER MARK
         MVI   LOWLEVEL,X'FF'     INIT. EFFECTIVE LEVEL COUNTER
         B     RETURN             RETURN TO INITIALIZATION
         TITLE 'CLUZALOC -- PARSE MEMORY ALLOCATION ROUTINE'
*
*        THIS ROUTINE ALLOCATES MEMORY, ESTABLISHING A NEW BLOCK
*        AND/OR SEGMENT IF NECESSARY
*
CLUZALOC LIENTRY DSALEN=MEMLEN,DSA=MEMDSA,ENV=R5,PARM=R2
         LA    R2,3(,R2)          ROUND LENGTH UP TO FULLWORD
         N     R2,=F'-4'
         TM    ALCFLAGS,COLECTIV  REQUEST FOR COLLECTIVE MEMORY?
         BZ    ALCOWNED           NO, GET OWNED MEMORY
         SPACE
*
*        TRY TO GET SPACE FROM A FREED SEGMENT
*
         L     R3,FREESEGS        FIND FIRST FREED SEGMENT
         LA    R9,FREESEGS        SET UP CHAIN-BACK REGISTER
ALCSERCH LTR   R3,R3              REACHED END OF FREE CHAIN?
         BZ    ALCLOW             YES, ALLOC FROM BLOCK END
         USING SEGHEAD,R3
         C     R2,SEGLEN          NO, IS THIS SEGMENT BIG ENOUGH?
         BNH   ALCAVAIL           YES, SUBALLOCATE
         LA    R9,SEGCHAIN        NO, CHANGE BACKWARD REGISTER
         L     R3,SEGCHAIN        TRY NEXT ON CHAIN
         B     ALCSERCH
         SPACE
*
*        TAKE THE REQUIRED STORAGE FROM THE FREE SEGMENT. IF ANY
*        IS LEFT, RETURN IT TO THE FREE CHAIN.
*
ALCAVAIL ST    R3,NEWMEM          RETURN THE SEGMENT ADDRESS
         NI    ALCFLAGS,255-COLECTIV   KILL COLLECTIVE ALLOCATION FLAG
         L     R1,SEGLEN          GET SIZE OF THIS SEGMENT
         SR    R1,R2              GET OVERAGE
         CH    R1,=H'8'           ENOUGH TO CARE ABOUT?
         BNL   ALCSHRNK           YES, KEEP REMAINDER FREE
         MVC   0(4,R9),SEGCHAIN   NO, JUST TAKE SEGMENT OFF CHAIN
         B     RETURN             AND RETURN
         SPACE
ALCSHRNK L     R0,SEGCHAIN        GET OLD SEGMENT CHAIN
         AR    R3,R2              FIND NEXT BYTE OF SEGMENT
         ST    R1,SEGLEN          STORE NEW LENGTH
         ST    R0,SEGCHAIN        AND OLD CHAIN FIELD
         ST    R3,0(,R9)          PUT SMALLER BLOCK ON CHAIN
         B     RETURN             AND RETURN
         SPACE
*
*        ALLOCATE A NEW COLLECTIVE SEGMENT FROM THE HIGH END OF THE
*        CURRENT BLOCK
*
ALCLOW   L     R3,LOWMARK         LOAD CURRENT LOW WATER MARK
         SR    R3,R2              SUBTRACT LENGTH REQUIRED
         C     R3,HIGHMARK        ENOUGH SPACE LEFT?
         BL    ALCLCBLK           NO, NEW BLOCK REQUIRED
         ST    R3,LOWMARK         YES, STORE NEW LOW MARK
         ST    R3,NEWMEM          RETURN LOW END OF BLOCK
         NI    ALCFLAGS,255-COLECTIV   RESET COLLECTIVE FLAG
         B     RETURN
         SPACE
*
*        ALLOCATE A NEW BLOCK, AND TAKE A NEW SEGMENT FROM THAT
*
ALCLCBLK LR    R1,R2              PASS GBLK AMOUNT REQUIRED
         BAL   R6,GBLK            GET A NEW BLOCK
         B     ALCLOW             AND ALLOCATE FROM IT
         SPACE
*
*        DETERMINE WHETHER THE OWNED STORAGE NESTING LEVEL HAS
*        INCREASED, AND CREATE A NEW LEVEL IF SO.
*
ALCOWNED SR    R15,R15            MAKE A ZERO
         IC    R15,ALCLEVEL       LOAD ALLOCATION LEVEL
         LA    R10,LEVLSTAK       FIND LEVEL INFORMATION STACK
         AR    R10,R15            ADDRESS CURRENT LEVEL
         USING LEVLSECT,R10
         SR    R1,R1
         IC    R1,NESTLEVL        GET EXTERNAL NESTING LEVEL
         CR    R15,R1             AT SAME LEVEL?
         BL    ALCNULEV           LEVEL INCREASE, PUSH STACK
         BE    ALCHIGH            SAME LEVEL, JUST ALLOCATE
         BAL   R6,PARSRC12        DECREASE SHOULD NOT OCCUR
         SPACE
ALCNULEV LTR   R15,R15            HAS ANY LEVEL BEEN SET?
         BNZ   ALCOLDSG           YES, PROPOGATE FROM PREVIOUS LEVEL
         MVC   LEVMARK,HIGHMARK   NO, START THIS LEVEL FROM HWM
         MVC   LEVSTART,HIGHMARK
         MVC   SEGSTART,HIGHMARK  SHOW SEGMENT BEGUN
         B     ALCSTSEG
         SPACE
ALCOLDSG CLC   SEGSTART,HIGHMARK  SEGMENT HEADER CLAIMED?
         BNE   ALCPROP            YES.
ALCSTSEG LA    R2,12(,R2)         NO, ADD 12 BYTES TO LENGTH
ALCPROP  LA    R15,8(,R15)        BUMP LEVEL COUNTER
         MVC   LEVSTART+8,LEVMARK START NEXT LEVEL FROM END OF LAST
         MVC   LEVMARK+8,LEVMARK
         LA    R10,8(,R10)        BUMP LEVEL POINTER
         CR    R15,R1             REACHED RIGHT LEVEL YET?
         BL    ALCPROP            NO, CONTINUE
         STC   R1,ALCLEVEL        YES, UPDATE LEVEL POINTER
         CLI   LOWLEVEL,X'FF'     ANYTHING USEFUL ALLOCATED YET?
         BNE   ALCHIGH            YES.
         STC   R1,LOWLEVEL        NO, SHOW LOW ALLOCATION LEVEL
         SPACE
*
*        ALLOCATE OR EXTEND THE OWNED SEGMENT FROM THE TOP OF THE
*        CURRENT BLOCK
*
ALCHIGH  L     R3,HIGHMARK        GET HIGH-WATER MARK
         LA    R9,0(R3,R2)        COMPUTE NEW ONE
         C     R9,LOWMARK         IS THERE ROOM?
         BH    ALCOBLK            NO, NEW BLOCK REQUIRED
         TM    ALCFLAGS,ACPTREQ   YES, HAS SEGMENT BEEN STARTED?
         BNZ   ALCNOT1            YES.
         ST    R3,SEGSTART        NO, START SEGMENT NOW
         MVC   SEGOWNER,OWNERID   SAVE SEGMENT OWNER
         OI    ALCFLAGS,ACPTREQ   SET SEGMENT BEGUN FLAG
         LA    R3,12(,R3)         SKIP LENGTH OF SEGMENT HEADER
         SPACE
ALCNOT1  ST    R3,NEWMEM          STORE NEW MEMORY ADDRESS
         ST    R9,LEVMARK         STORE NEW END OF LEVEL PTR
         ST    R9,HIGHMARK        STORE NEW HIGH-WATER MARK
         B     RETURN
         SPACE
*
*        ALLOCATE A NEW BLOCK, AND TAKE A NEW SEGMENT FROM THAT
*
ALCOBLK  LR    R1,R2              PASS GBLK AMOUNT REQUIRED
         BAL   R6,GBLK            GET A NEW BLOCK
         B     ALCHIGH            AND ALLOCATE FROM IT
         SPACE
         DROP  R3,R10
         TITLE 'CLUZMINT/CLUZALOC -- BLOCK ALLOCATION SUBROUTINE'
*
*        THIS SUBROUTINE IS CALLED TO GET A NEW BLOCK OF MEMORY.
*
GBLK     STM   R14,R3,12(R13)     SAVE SOME REGISTERS
         SPACE
*
*        ADD PREVIOUS BLOCK (IF ANY) TO THE IKJRLSA CHAIN
*
         L     R14,MEMBLOCK       FIND PREVIOUS BLOCK
         LTR   R14,R14            IF ANY
         BZ    GBLKSIZE
         L     R15,BLKCHAIN       IF SO, FIND CHAIN FIELD
         LTR   R15,R15            IF ANY
         BZ    GBLK1ST
         ST    R14,0(,R15)        PUT LAST BLOCK ON CHAIN
GBLK1ST  ST    R14,BLKCHAIN       & SET NEW CHAIN POINTER
         SPACE
*
*        OUR CALLER HAS PASSED A MINIMUM BLOCK SIZE REQUIRED. WE
*        ISSUE A VARIABLE GETMAIN FOR AN AMOUNT BETWEEN THE MINIMUM
*        AND AT LEAST 512 BYTES MORE (ROUNDING UP TO A MULTIPLE
*        THEREOF). THIS MEANS THAT USUALLY WE GET A 1K BLOCK.
*
GBLKSIZE MVC   GETLIST(10),GETMODEL    SET UP L-FORM GETMAIN
         LA    R1,20(,R1)         ADD SPACE FOR BLOCK & SEGMENT HDRS
         ST    R1,MEMREQ          STORE MINIMUM AMOUNT
         LA    R3,1023(,R1)       UP BY 2X512
         N     R3,=F'-512'        & ROUND DOWN
         ST    R3,MEMREQ+4        TO GET THE MAXIMUM
         GETMAIN VC,LA=MEMREQ,A=MEMBLOCK,MF=(E,GETLIST)                *
                                  ASK FOR BLOCK OF MEMORY
         LTR   R15,R15            DID WE GET IT?
         BZ    GBLKOK             YES.
         BAL   R6,PARSRC16        NO, ABANDON SHIP.
         SPACE
*
*        SEE IF THE PREVIOUS BLOCK CONTAINS AN INCOMPLETE OWNED
*        SEGMENT. IF SO, IT MUST BE ADDED TO THE "PENDING" CHAIN
*        FOR ITS OWNER, AND A NEW SEGMENT BEGUN IN THE NEW BLOCK
*
GBLKOK   L     R3,HIGHMARK        GET OLD BLOCK HIGH-WATER MARK
         L     R2,LOWMARK         AND LOW MARK
         SR    R2,R3              COMPUTE AMOUNT BETWEEN
         L     R1,MEMBLOCK        GET NEW BLOCK ADDR
         MVC   0(4,R1),NULL       SHOW NOTHING CHAINED AFTER
         MVI   4(R1),X'01'        SHOW BLOCK FROM SUBPOOL 1
         MVC   5(3,R1),BLKLEN+1   SAVE LENGTH OF BLOCK
         LA    R1,8(,R1)          SKIP IKJRLSA HEADER
         OC    SEGSTART,SEGSTART  HAS A SEGMENT BEEN STARTED?
         BZ    GBLKMARK           NO, HOW FORTUNATE
         SR    R15,R15            YES.
         IC    R15,ALCLEVEL       GET CURRENT ALLOCATION LEVEL
         LA    R14,LEVLSTAK       FIND LEVEL INFO STACK
         AR    R15,R14            ADDRESS CURRENT ENTRY
         USING LEVLSECT,R15
         LR    R0,R1              COPY BLOCK START ADDRESS
         TM    ALCFLAGS,ACPTREQ   IS SEGMENT STILL EMPTY?
         BZ    GBLKNSEG           YES, LET ALOC BUILD HEADER
         LA    R0,12(,R1)         NO, LEAVE SPACE FOR SEGMENT HEADER
GBLKNSEG ST    R1,LEVSTART        SET LEVEL START & END TO
         ST    R0,LEVMARK         START OF NEW BLOCK
         DROP  R15
         L     R14,SEGSTART       FIND START OF OLD SEGMENT
         USING SEGHEAD,R14
         L     R15,HIGHMARK       FIND HIGH MARK OF OLD BLOCK
         SR    R15,R14            COMPUTE SIZE OF OLD SEGMENT
         BZ    GBLKOWN            CHECK FOR NO SEGMENT AT ALL
         CH    R15,=H'12'         ANYTHING BUT HEADER THERE?
         BH    GBLKOSEG           YES, PEND IT
         AR    R2,R15             NO, ADD REMNANT TO AVAIL LENGTH
         SR    R3,R15             AND PUSH HIGH-WATER MARK BACK
         B     GBLKOWN
         SPACE
GBLKOSEG CH    R2,=H'8'           IS LESS THAN 8 BYTES OF BLOCK UNUSED?
         BNL   GBLKREM            NO, PROCEED
         AR    R15,R2             YES, ADD TO CAST-OFF SEGMENT
         SR    R2,R2              SHOW NOTHING UNUSED
GBLKREM  ST    R15,SEGLEN         STORE IT BACK
         MVC   SEGCHAIN,OWNSEGS   CHAIN TO PREVIOUS PENDING SEGMENTS
         ST    R14,OWNSEGS        NOTE NEW CHAIN HEAD
         OC    OWNLINK,OWNLINK    IS THIS THE FIRST PENDING SEGMENT?
         BNZ   GBLKOWN            NO.
         ST    R14,OWNLINK        YES, WILL STAY THE TAIL
GBLKOWN  ST    R1,SEGSTART        STORE NEW SEGMENT START
         MVC   SEGOWNER-SEGHEAD(4,R1),OWNERID    COPY OWNER OVER
         LR    R1,R0              PUT HIGH WATER MARK AFTER SEGHEAD
         DROP  R14
         SPACE
*
*        FIX UP THE PREVIOUS BLOCK. IF IT CONTAINS ANY UNUSED
*        SPACE, IT IS "FREED" TO BECOME AVAILABLE FOR COLLECTIVE
*        STORAGE ALLOCATION
*
GBLKMARK ST    R1,HIGHMARK        STORE HIGH MARK IN NEW BLOCK
         L     R1,MEMBLOCK        GET NEW BLOCK ADDRESS
         A     R1,BLKLEN          ADD TO BLOCK SIZE
         ST    R1,LOWMARK         STORE AS LOW MARK
         ST    R1,BLKTOP          SAVE AS HIGH ADDR IN BLOCK ALSO
         LTR   R2,R2              IS THERE ANYTHING LEFT IN OLD BLOCK?
         BNZ   FSEGBGIN           YES, GO TO FREE SEGMENT
         LM    R14,R3,12(R13)     NO, RELOAD REGISTERS
         BR    R6                 AND RETURN
         SPACE
GETMODEL GETMAIN VC,SP=1,MF=L     MODEL GETMAIN PARM LIST
         TITLE 'CLUZCMIT -- PARSE MEMORY COMMIT ROUTINE'
*
*        THIS ROUTINE IS CALLED TO TRANSFER MEMORY ASSOCIATED WITH
*        THE CURRENT NESTING LEVEL TO THE PREVIOUS LEVEL.
*
CLUZCMIT LIENTRY DSALEN=88,DSA=*,ENV=R5
         SR    R15,R15
         IC    R15,NESTLEVL       GET CURRENT NESTING LEVEL
         SH    R15,=H'8'          BACK UP ONE
         LR    R2,R15             SAVE LEVEL FOR LATER
         SR    R14,R14
         IC    R14,ALCLEVEL       GET ALLOCATION LEVEL
         LA    R3,LEVLSTAK        FIND LEVEL INFORMATION STACK
         AR    R15,R3             POINT TO PREVIOUS LEVEL
         USING LEVLSECT,R15
         AR    R14,R3             POINT TO CURRENT LEVEL
         SPACE
*
*        SET THE HIGH-WATER MARK FOR THE PREVIOUS LEVEL EQUAL TO THE
*        HIGH-WATER MARK FOR THIS LEVEL.  IF THE CURRENT LEVEL'S
*        STARTING POINT IS NOT EQUAL TO THE PREVIOUS LEVEL'S HIGH
*        POINT, A NEW BLOCK HAS BEEN TAKEN, AND WE ALSO ADJUST THE
*        PREVIOUS LEVEL'S STARTING POINT.
*
         L     R1,LEVSTART-LEVLSECT(,R14)   GET START OF CURRENT LEVEL
         C     R1,LEVMARK         SAME AS END OF PREVIOUS?
         BE    CMITNORM           YES.
         ST    R1,LEVSTART        NO, MAKE OLD LEVEL START THERE TOO
CMITNORM L     R1,LEVMARK-LEVLSECT(,R14)    GET END OF CURRENT LEVEL
         ST    R1,LEVMARK         AND COPY BACK
         CLC   ALCLEVEL,LOWLEVEL  PREVIOUS LEVEL LOWEST ALLOCATED?
         BNE   CMITNLOW           NO.
         STC   R2,LOWLEVEL        YES, PUSH IT DOWN
CMITNLOW STC   R2,ALCLEVEL        RESET ALLOCATION LEVEL
         B     RETURN             AND RETURN
         DROP  R15
         TITLE 'CLUZRLBK -- PARSE MEMORY ROLLBACK ROUTINE'
*
*        THIS ROUTINE IS CALLED TO EFFECTIVELY DISCARD ANY MEMORY
*        ASSOCIATED WITH THE CURRENT NESTING LEVEL OF THE CURRENT
*        PARAMETER.
*
CLUZRLBK LIENTRY DSALEN=88,DSA=*,ENV=R5
         SR    R15,R15
         IC    R15,NESTLEVL       GET CURRENT NESTING LEVEL
         SH    R15,=H'8'          BACK UP ONE
         LR    R0,R15             SAVE LEVEL FOR LATER
         SR    R14,R14
         IC    R14,ALCLEVEL       GET ALLOCATION LEVEL
         LA    R3,LEVLSTAK        FIND LEVEL INFORMATION STACK
         AR    R15,R3             POINT TO PREVIOUS LEVEL
         USING LEVLSECT,R15
         AR    R14,R3             POINT TO CURRENT LEVEL
         SPACE
*
*        RESET THE GLOBAL HIGH-WATER MARK TO THE HIGH-WATER MARK FOR
*        THE PREVIOUS LEVEL.  EXCEPTION: IF THE STARTING POINT FOR
*        THE CURRENT LEVEL IS DIFFERENT FROM THE HIGH POINT OF THE
*        PREVIOUS LEVEL, A NEW BLOCK HAS BEEN TAKEN.  IN THAT CASE,
*        THE PREVIOUS LEVEL'S STARTING POINT BECOMES THE NEW HIGH-
*        WATER MARK, AND ANY STORAGE ASSOCIATED WITH THAT LEVEL IN
*        THE PREVIOUS BLOCK BECOMES WASTED.
*
         L     R1,LEVSTART-LEVLSECT(,R14)   FIND START OF CURRENT LEVEL
         C     R1,LEVMARK         SAME AS OLD HIGH POINT?
         BE    RLBKMARK           YES.
         ST    R1,LEVSTART        NO, CAN ONLY ROLL BACK THAT FAR
         ST    R1,LEVMARK
RLBKMARK ST    R1,HIGHMARK        RESET GLOBAL HIGH-WATER MARK
         CLC   ALCLEVEL,LOWLEVEL  OLD LEVEL LOWEST ALLOCATED?
         BH    RLBKNLOW           NO.
         SPACE
*
*        IF THERE IS NO USEFUL STORAGE AT ANY HIGHER LEVEL FOR
*        THIS PARAMETER, AND THERE ARE ANY PENDING SEGMENTS ALLOCATED,
*        FREE THEM
*
         L     R3,OWNSEGS         FIND FIRST PENDING SEGMENT
         LTR   R3,R3              IS THERE ONE?
         BZ    RLBKGONE           NO.
         USING SEGHEAD,R3
RLBKFREE L     R2,SEGLEN          GET THE SEGMENT LENGTH
         L     R9,SEGCHAIN        GET THE CHAIN POINTER
         BAL   R6,FSEG            FREE THIS SEGMENT
         LTR   R3,R9              ANY MORE LEFT?
         BNZ   RLBKFREE           YES, FREE IT
         XC    OWNSEGS(8),OWNSEGS NO MORE PENDING OWNED SEGMENTS
RLBKGONE MVI   LOWLEVEL,X'FF'     SHOW NO ALLOCATION AT ANY LEVEL
         DROP  R3
         SPACE
RLBKNLOW STC   R0,ALCLEVEL        RESET ALLOCATION LEVEL COUNT
         B     RETURN
         DROP  R15
         TITLE 'CLUZACPT -- PARSE PARAMETER ACCEPTANCE ROUTINE'
*
*        THIS ROUTINE IS CALLED WHEN PROCESSING OF AN "OWNER"
*        PARAMETER IS COMPLETE, TO FINALIZE ALLOCATION OF ITS
*        OWNED MEMORY SEGMENTS.  AFTER ACCEPTANCE, THESE SEGMENTS
*        CAN BE FREED ONLY BY A CALL TO CLUZRLSE.
*
CLUZACPT LIENTRY DSALEN=88,DSA=*,ENV=R5
*
*        ADD ANY ACTIVE SEGMENT TO THE CHAIN OF ALLOCATED SEGMENTS
*
         L     R9,HIGHMARK        GET HIGH WATER MARK
         NI    ALCFLAGS,255-ACPTREQ    TURN OFF ACCEPT-REQUIRED FLAG
         L     R3,SEGSTART        GET WHERE SEGMENT STARTED
         USING SEGHEAD,R3
         SR    R9,R3              COMPUTE SEGMENT LENGTH
         BZ    ACPTEMTY           SPECIAL CASE IF EMPTY
         MVC   SEGCHAIN,ALCSEGS   CHAIN NEW SEGMENT BEFORE OTHERS
         ST    R9,SEGLEN          STORE SEGMENT LENGTH
         SPACE
*
*        THERE MAY BE ONE OR MORE ADDITIONAL "PENDING" SEGMENTS
*        ASSOCIATED WITH THIS OWNER IF A NEW BLOCK WAS ALLOCATED
*        DURING ITS PROCESSING. IF SO, ADD THEM TO THE ALLOCATED
*        CHAIN AS WELL.
*
         L     R9,OWNLINK         FIND PENDING SEGMENT LINK FIELD
         LTR   R9,R9              ANYTHING PENDING?
         BZ    ACPTNPND           NAHHH.
         ST    R3,0(,R9)          YES, CHAIN ACTIVE SEGMENT AFTER
ACPTPEND L     R3,OWNSEGS         FIND THE FIRST PENDING SEGMENT
ACPTNPND ST    R3,ALCSEGS         STORE AS NEW ALLOCATED HEAD
         SPACE
ACPTEXIT XC    OWNSEGS(8),OWNSEGS CLEAR PENDING POINTERS
         XC    SEGSTART,SEGSTART  SHOW NO SEGMENT STARTED
         MVI   ALCLEVEL,0         MAKE SURE ALLOC LEVEL IS 0
         MVI   LOWLEVEL,X'FF'     RESET LOW ALLOC LEVEL MARK
         B     RETURN             AND WE ARE THROUGH
         SPACE
*
*        IF THE ACTIVE SEGMENT IS EMPTY, THERE MAY STILL BE PENDING
*        SEGMENTS ALLOCATED FROM PREVIOUS BLOCKS. WE REQUEUE THESE
*        TO THE ALLOCATED QUEUE.
*
ACPTEMTY L     R9,OWNLINK         GET PENDING LINK POINTER
         LTR   R9,R9              ANYTHING THERE?
         BZ    ACPTEXIT           NO, GIVE UP
         MVC   0(4,R9),ALCSEGS    YES, ADD THEM TO ALLOCATED LIST
         B     ACPTPEND
         SPACE
         DROP  R3
         TITLE 'CLUZRLSE -- PARSE STORAGE RELEASE ROUTINE'
*
*        THIS ROUTINE IS CALLED TO RELEASE THE STORAGE OWNED BY A
*        PCE. ANY SUCH SEGMENTS ARE ADDED TO THE FREE LIST, AND
*        CAN BE USED BY COLLECTIVE ALLOCATION REQUESTS.
*
CLUZRLSE LIENTRY DSALEN=88,DSA=*,ENV=R5,PARM=R10
*
*        SEARCH THE ALLOCATED SEGMENT LIST FOR SEGMENTS OWNED BY
*        THIS PCE
*
         L     R3,ALCSEGS         FIND FIRST ALLOCATED SEGMENT
         USING SEGHEAD,R3
         LA    R9,ALCSEGS         R9 POINTS TO PREVIOUS CHAIN FIELD
RLSELOOP LTR   R3,R3              REACHED CHAIN END?
         BZ    RETURN             YES, NOTHING MORE TO DO
         C     R10,SEGOWNER       NO, IS THIS SEGMENT OURS?
         BE    RLSETHIS           YES, TURN IT LOOSE
         LA    R9,SEGCHAIN        NO.
         L     R3,SEGCHAIN        CHECK THE NEXT ONE
         B     RLSELOOP
         SPACE
*
*        ADD THE SEGMENTS ALLOCATED TO THIS OWNER TO THE FREE CHAIN.
*        THEY SHOULD BE CONTIGUOUS ON THE ALLOCATED CHAIN, SO THERE
*        IS NO NEED TO CONTINUE SEARCHING AFTER A MATCH IS FOUND
*
RLSETHIS L     R2,SEGLEN          GET SEGMENT LENGTH
         L     R1,SEGCHAIN        SAVE CHAIN FIELD
         BAL   R6,FSEG            FREE THIS SEGMENT
         LR    R3,R1              ON TO NEXT SEGMENT
         C     R10,SEGOWNER       THE RIGHT OWNER?
         BE    RLSETHIS           YES, FREE IT TOO
         ST    R3,0(,R9)          NO, RECONNECT ALLOCATED CHAIN
         B     RETURN             RELEASE IS DONE
         SPACE
         DROP  R3
         TITLE 'CLUZALOC/CLUZRLBK/CLUZRLSE -- FREE MEMORY SEGMENT ROUTI*
               NE'
*
*        THIS ROUTINE IS CALLED TO ADD A SEGMENT TO THE LIST OF FREE
*        SEGMENTS
*
FSEG     STM   R14,R3,12(R13)     SAVE REGISTERS
         SPACE
*
*        THIS ENTRY TO THE FSEG ROUTINE IS USED BY THE GBLK ROUTINE
*        TO FREE THE UNALLOCATED PART OF THE PREVIOUS BLOCK WHEN
*        A NEW ONE IS ALLOCATED.
*
FSEGBGIN L     R14,FREESEGS       GET HEAD OF FREE SEG LIST
         LA    R15,FREESEGS       KEEP CHAIN-BACK RECORD
         LNR   R1,R15             INIT SEGMENT END POINTER
         SPACE
*
*        THE FREE CHAIN IS KEPT SORTED BY BLOCK ADDRESS. FIND THE
*        PROPER POINT TO INSERT THE NEW ARRIVAL.
*
FSEGLOOP LTR   R14,R14            REACHED END OF CHAIN?
         BZ    FSEGLAST           YES.
         USING SEGHEAD,R14
         CR    R3,R14             THIS BLOCK BEFORE THE ONE TO FREE?
         BL    FSEGINS            NO, THIS IS THE PLACE
         LA    R15,SEGCHAIN       YES, KEEP LOOKING
         L     R1,SEGLEN          SAVE LENGTH OF LAST SEGMENT
         L     R14,SEGCHAIN
         B     FSEGLOOP
         SPACE
*
*        THIS SEGMENT MUST BE ADDED TO THE END OF THE CHAIN. SEE IF
*        IT IS ADJACENT TO THE PREVIOUS ONE AND, IF SO, COMBINE THEM.
*
FSEGLAST AR    R1,R15             FIND END OF PREVIOUS SEGMENT
         CR    R1,R3              ADJACENT TO THIS SEGMENT?
         BE    FSEGJOIN           YES, PUT THEM TOGETHER
         ST    R3,0(,R15)         NO, JUST CHAIN ON
         DROP  R14
         USING SEGHEAD,R3
         XC    SEGCHAIN,SEGCHAIN  STOP CHAIN HERE
         ST    R2,SEGLEN          SAVE LENGTH OF FREE ELEMENT
         SPACE
FSEGRET  LM    R14,R3,12(R13)     RELOAD REGISTERS
         BR    R6                 AND RETURN TO CALLER
         DROP  R3
         SPACE
         USING SEGHEAD,R15
FSEGJOIN A     R2,SEGLEN          ADD TO PREVIOUS SEGMENT LENGTH
         ST    R2,SEGLEN          AND STORE BACK
         LTR   R14,R14            ANYTHING AFTER JUST FREED SEG?
         BZ    FSEGRET            NO.
         A     R2,SEGLEN          YES, FIND END OF EXPANDED SEGMENT
         CR    R2,R14             CONTIGUOUS TO ONE AFTER?
         BNE   FSEGRET            NO, QUIT
         L     R3,SEGLEN-SEGHEAD(,R2)  YES, GET LENGTH OF THAT ONE
         A     R3,SEGLEN          ADD TO THIS ONE'S LENGTH
         ST    R3,SEGLEN
         MVC   SEGCHAIN,SEGCHAIN-SEGHEAD(R14)    COPY CHAIN OVER
         B     FSEGRET            AND FREE IS DONE
         SPACE
FSEGINS  AR    R1,R15             FIND END OF PREVIOUS SEGMENT
         CR    R1,R3              CONTIGUOUS TO THIS ONE?
         BE    FSEGJOIN           YES, MERGE
         ST    R3,SEGCHAIN        NO, CHAIN TO IT
         LA    R1,0(R2,R3)        FIND END OF BLOCK BEING FREED
         CR    R1,R14             ADJACENT TO ONE AFTER?
         BE    FSEGJAFT           YES, JOIN THEM
         DROP  R15
         USING SEGHEAD,R3
         ST    R14,SEGCHAIN       NO, JUST FINISH CHAINING
         ST    R2,SEGLEN          STORE SEGMENT LENGTH
         B     FSEGRET
         SPACE
FSEGJAFT MVC   SEGCHAIN,SEGCHAIN-SEGHEAD(R14)    COPY CHAIN FROM       *
                                                 FOLLOWING SEGMENT
         L     R15,SEGLEN-SEGHEAD(,R14)     GET ITS LENGTH
         A     R15,SEGLEN         ADD TO LENGTH OF THIS ONE
         ST    R15,SEGLEN
         B     FSEGRET
         TITLE 'CLUZWRAP -- PARSE MEMORY MANAGEMENT WRAP-UP ROUTINE'
*
*        THIS ROUTINE CLEANS UP MEMORY FOR EITHER SUCCESSFUL OR
*        UNSUCCESSFUL PARSE COMPLETION
*
CLUZWRAP LIENTRY DSALEN=88,DSA=*,ENV=R5
         CLI   RC,0               SUCCESSFUL FINISH?
         BE    WRAPUP             YES.
         SPACE
*
*        FOR UNSUCCESSFUL PARSE TERMINATION, FREEMAIN THE CURRENT
*        BLOCK, AND USE IKJRLSA TO RELEASE ANY OTHERS
*
         CLC   PDLPTR,NULL        EVER GOTTEN ANY STORAGE?
         BE    RETURN             NO, NOTHING TO WRAP UP
         L     R1,MEMBLOCK        LOAD CURRENT BLOCK ADDRESS
         C     R1,PDLPTR          IS FIRST BLOCK ALSO LAST?
         BE    WRAPRLSA           YES, DON'T FREE IT SPECIAL
         L     R0,4(,R1)          NO, LOAD CURRENT BLOCK LENGTH
         FREEMAIN R,A=(1),LV=(0)  FREE CURRENT BLOCK
WRAPRLSA IKJRLSA PDLPTR           RELEASE OTHER BLOCKS
         B     RETURN
         SPACE
*
*        FOR SUCCESSFUL PARSE TERMINATION, FREEMAIN ANY UNUSED PART
*        OF THE CURRENT BLOCK, AND CHAIN THIS HIGH FRAGMENT TO ANY
*        OTHER BLOCKS FOR IKJRLSA
*
WRAPUP   NC    LOWMARK,=F'-8'     ROUND LOW WATER MARK DOWN
         L     R3,LOWMARK
         C     R3,BLKTOP          ANYTHING ALLOCATED AT HIGH END?
         BE    WRAPTOP            NO, NO HEADER NEEDED
         SH    R3,=H'8'           GET ROOM FOR IKJRLSA HEADER
         ST    R3,LOWMARK         AND STORE BACK
WRAPTOP  LR    R9,R3              SAVE LOWMARK
         L     R2,HIGHMARK
         LA    R2,7(,R2)          ROUND HIGH WATER-MARK UP
         N     R2,=F'-8'
         ST    R2,HIGHMARK
         SR    R3,R2              GET AMOUNT UNUSED
         BNP   WRAPFULL           IF ANY
         L     R2,HIGHMARK        FREE FROM HIGHMARK->LOWMARK
         FREEMAIN R,LV=(R3),A=(R2),SP=1
         L     R15,BLKLEN         GET LENGTH OF LAST BLOCK
         SR    R15,R3             GET AMOUNT LEFT
         BZ    WRAPLAST           FIX UP PREV BLOCK IF NOTHING LEFT
         L     R14,HIGHMARK
         L     R1,MEMBLOCK
         SR    R14,R1             COMPUTE SIZE OF LOW FRAGMENT
         BP    WRAPLOW            CHECK FOR NO LOW FRAGMENT
         LR    R1,R9              IF SO, TREAT HIGH AS LOW
         B     WRAPHIGH
WRAPLOW  ST    R14,4(,R1)         STORE FOR IKJRLSA
         MVI   4(R1),X'01'        STORE SUBPOOL NUMBER
         ST    R9,0(,R1)          CHAIN HIGH FRAGMENT TO LOW FRAGMENT
         SR    R15,R14            GET SIZE OF HIGH FRAGMENT
         BP    WRAPHIGH           IF ANY
         MVC   0(4,R1),NULL       IF NONE, KILL CHAIN HERE
         B     WRAPLINK           AND FINISH
WRAPHIGH MVC   0(4,R9),NULL       SHUT OFF IKJRLSA CHAIN
         ST    R15,4(,R9)         STORE FOR IKJRLSA
         MVI   4(R9),X'01'
         SPACE
WRAPLINK L     R3,BLKCHAIN        GET IKJRLSA CHAIN FIELD ADDR
         LTR   R3,R3              IS THERE A PREVIOUS BLOCK?
         BZ    RETURN             NO.
         ST    R1,0(,R3)          YES, CHAIN THESE ON
         B     RETURN
         SPACE
*
*        THE CURRENT BLOCK IS FULL. JUST ADD IT TO THE CHAIN.
*
WRAPFULL L     R1,MEMBLOCK        FIND THE CURRENT BLOCK
         MVC   0(4,R1),NULL       CHAIN IT NOWHERE
         MVC   4(4,R1),BLKLEN     SET UP LENGTH FIELD
         MVI   4(R1),X'01'        STORE SUBPOOL NUMBER
         B     WRAPLINK           FINISH FINISHING UP
         SPACE
WRAPLAST L     R1,NULL            CHAIN PREV BLOCK TO NULL
         B     WRAPLINK
         SPACE 5
MEMDSA   DSECT ,                  DSA FOR CLUZMINT/CLUZALOC
         DS    22F
GETLIST  DS    3A                 GETMAIN PARM LIST
MEMREQ   DS    2F                 MIN & MAX AMOUNTS WANTED
         SPACE
MEMLEN   EQU   *-MEMDSA           DSA LENGTH
         SPACE 3
SEGHEAD  DSECT ,                  MEMORY SEGMENT HEADER
         SPACE
SEGCHAIN DS    A                  ADDR OF NEXT SEGMENT
SEGLEN   DS    A                  LENGTH OF THIS SEGMENT
SEGOWNER DS    A                  ADDRESS OF OWNING PCE
         SPACE 3
LEVLSECT DSECT ,                  NESTING LEVEL INFO MAP
         SPACE
LEVSTART DS    A                  FIRST BYTE ALLOC AT THIS LEVEL
LEVMARK  DS    A                  HIGH WATER MARK FOR THIS LEVEL
         TITLE 'CLUZQSTR -- QUOTED STRING RECOGNIZER'
CLUTSREC LISECT ,                 COMMON RECOGNIZERS
         SPACE
*
*        THIS ROUTINE RECOGNIZES AND RETURNS QUOTED STRING VALUES
*
CLUZQSTR LIENTRY DSA=*,DSALEN=88,ENV=R5,PARM=R2
*
*        IF THE INPUT TEXT DOES NOT START WITH A QUOTE, IT IS NOT
*        A QSTRING
*
QSTRBEGN L     R0,=X'7FFFFFFF'    INDICATE NO MAXIMUM LENGTH
         CLI   0(R8),C''''        START WITH QUOTE?
         BE    QSTRYES            YES.
         LA    R15,4              NO, QSTRING NOT PRESENT
         B     RETURN
         SPACE
*
*        DETERMINE THE STRING'S LENGTH, AND THE NUMBER OF IMBEDDED
*        QUOTES
*
QSTRYES  LA    R3,1(,R8)          SAVE START OF STRING
         SR    R1,R1              INIT INTERNAL QUOTE COUNT
         L     R14,=V(CLUTSCAP)   UPPER CASE TABLE ADDR
         BXH   R8,R6,QSTRUNCL     PROCEED (UNLESS SOLITARY QUOTE)
         SPACE
QSTRCNT  CLI   0(R8),C''''        HAVE WE A CLOSE QUOTE?
         BE    QSTRQCNT           YES, MAYBE
         TM    1(R9),X'40'        NO, IS THIS STRING ASIS?
         BNZ   QSTRASIS           YES, LEAVE IT ALONE
         TR    0(1,R8),0(R14)     NO, UPPER CASE IT
QSTRASIS BXLE  R8,R6,QSTRCNT      AND CONTINUE
         B     QSTRUNCL           SAY UNCLOSED IF TEXT RUNS OUT
         SPACE
QSTRQCNT CR    R8,R7              AT THE END?
         BE    QSTRCEND           YES, STRING HAS ENDED
         CLI   1(R8),C''''        NO, IS THIS A DOUBLED QUOTE?
         BNE   QSTRCEND           NO.
         AR    R1,R6              YES, ADD TO QUOTE COUNT
         AR    R8,R6              BUMP CURSOR PAST QUOTE 1
         B     QSTRASIS           AND CONTINUE
         SPACE
*
*        SEND A "CLOSING QUOTE ASSUMED" MESSAGE IF NECESSARY
*
QSTRUNCL BAL   R14,UNCL           CALL UNCLOSED ROUTINE
         SPACE
*
*        IF THERE WERE NO IMBEDDED QUOTES, RETURN A POINTER TO THE
*        ORIGINAL STRING
*
QSTRCEND LR    R10,R8
         SR    R10,R3             COMPUTE LENGTH OF TEXT
         LTR   R1,R1              ANY QUOTES INSIDE?
         BNZ   QSTRQUOT           YES.
         CR    R10,R0             NO, IS IT TOO LONG?
         BH    QSTRLONG           YES.
         ST    R3,0(,R2)          NO, SET UP PDE
         STH   R10,4(,R2)
         SPACE
QSTRRET  MVI   6(R2),X'80'
         SR    R15,R15
QSTREXIT CR    R8,R7              DID WE PASS THE END?
         BH    RETURN             YES.
         CLI   0(R8),C''''        NO, WAS STRING CLOSED?
         BNE   RETURN             NO.
         AR    R8,R6              YES, SKIP PAST CLOSE QUOTE
         B     RETURN
         SPACE
*
*        COPY THE STRING TO DYNAMIC MEMORY, HALVING DOUBLED QUOTES
*
QSTRQUOT SR    R10,R1             COMPUTE TRUE STRING LENGTH
         CR    R10,R0             IS IT TOO MUCH?
         BH    QSTRLONG           YES.
         STH   R10,4(,R2)         NO, SET UP PDE LENGTH
         LR    R1,R10
         L     R15,=A(CLUZALOC)
         BALR  R14,R15            ASK FOR MEMORY
         MVC   0(4,R2),NEWMEM     STORE IN PDE
         L     R1,NEWMEM
         SPACE
QSTRMOVE CLI   0(R3),C''''        MOVE IN COMPRESSED VALUE
         BE    QSTRQMV
         MVC   0(1,R1),0(R3)
QSTRMLOP AR    R1,R6
         AR    R3,R6
         BCT   R10,QSTRMOVE
         B     QSTRRET
         SPACE
QSTRQMV  MVI   0(R1),C''''
         AR    R3,R6
         B     QSTRMLOP
         SPACE
QSTRLONG LA    R15,8              SET EVIL RETURN CODE
         B     QSTREXIT           AND RETURN
         TITLE 'CLUZSPAC/CLUZDLIM -- SPACE AND DELIMITER HANDLERS'
*
*        THIS ROUTINE HANDLES THE IKJPARS "SPACE" SYNTAX BY ADVANCING
*        THE INPUT POINTER 1 CHARACTER, UNLESS IT ADDRESSES A TAB
*
CLUZSPAC LIENTRY DSALEN=88,DSA=*,ENV=R5
         SR    R1,R1
         IC    R1,0(,R8)
         LA    R2,SCANTAB(R1)
         TM    0(R2),X'10'        IS CHAR A DELIMITER?
         BZ    SPACRET            NO, TREAT AS DATA
         CLI   0(R8),X'05'        IS THIS CHAR A TAB?
         BE    SPACRET            YES, LEAVE IT ALONE
         CLI   0(R8),C'/'         CHECK FOR SLASH TOO
         BE    SPACRET            YES, JUST A DATA CHAR
         AR    R8,R6              NO, SKIP IT
SPACRET  SR    R15,R15            SET GOOD RETURN CODE
         MVI   DELIM,0            INDICATE NO DELIMITER
         B     RETURN             AND RETURN
         SPACE 5
*
*        THIS ROUTINE RECOGNIZES AN EDIT-TYPE STRING DELIMITER.
*        QUOTE IS HANDLED SPECIALLY BECAUSE IT IS NOT YET KNOWN IF
*        THE FOLLOWING STRING IS A SQSTRING
*
CLUZDLIM LIENTRY DSALEN=88,DSA=*,ENV=R5
         MVI   DLQFLAG,0          TURN OFF QUOTE-DELIMITER FLAG
         SR    R1,R1
         IC    R1,0(,R8)
         LA    R2,SCANTAB(R1)
         TM    0(R2),X'48'        IS THIS CHAR NUMERIC OR SPECIAL?
         BZ    DLIMOK             NO, IT CAN DELIMIT
         CLI   0(R8),C''''        YES, IS IT A QUOTE?
         BE    DLIMQUOT           YES, HANDLE SPECIAL CASE
         MVI   DELIM,X'FF'        NO, INDICATE DELIMITER ABSENT
         LA    R15,4              SET MISSING RETURN CODE
         B     RETURN             AND GO BACK
         SPACE
DLIMOK   MVC   DELIM,0(R8)        SAVE DELIMITER
         A     R1,=V(CLUTSCAP)    FIND UPPER CASE VERSION
         MVC   CAPDELIM,0(R1)     AND SAVE THAT TOO
         AR    R8,R6              SKIP PAST IT
         SR    R15,R15
         B     RETURN             AND RETURN
         SPACE
DLIMQUOT MVI   DELIM,C''''        MAKE QUOTE THE DELIMITER
         MVI   CAPDELIM,C''''
         MVI   DLQFLAG,X'FF'      SET SPECIAL FLAG
         SR    R15,R15
         B     RETURN             AND RETURN
         TITLE 'CLUZSTRG -- STRING (OPTIONALLY DELIMITED) RECOGNIZER'
*
*        THIS ROUTINE RECOGNIZES A STRING. DEPENDING ON CIRCUMSTANCES,
*        THE STRING MAY BE ENCLOSED IN DELIMITERS OR QUOTED
*
CLUZSTRG LIENTRY DSALEN=88,DSA=*,ENV=R5,PARM=R2
         CLI   DELIM,0            HAS A DELIMITER BEEN FOUND?
         BE    STRGALL            NO, RECOGNIZE TO THE END
         CLI   DELIM,X'FF'        DELIMITER FOUND MISSING?
         BE    STRGMISS           YES, THE STRING IS MISSING TOO
         CLI   DLQFLAG,0          IS THE DELIMITER A QUOTE?
         BNE   STRGQDLM           YES.
         SPACE
STRGGO   MVI   DLQFLAG,0          TURN OFF QUOTE DELIM FLAG
         LR    R3,R8              SAVE STARTING POINT
         L     R14,=V(CLUTSCAP)   ADDRESS UPPERCASE TAB
         CR    R8,R7              ANY ROOM FOR A STRING?
         BH    STRGNDLM           NO, LENGTH 0
STRGSCAN CLC   DELIM,0(R8)        SEARCH FOR DELIMITER
         BE    STRGEDLM
         TM    1(R9),X'40'        ASIS?
         BNZ   STRGASIS           YES, NO TRANSLATE
         CLC   CAPDELIM,0(R8)     NO, SEE IF DELIMITER IN UPPER CASE
         BE    STRGEDLM           AND STOP IF SO
         TR    0(1,R8),0(R14)
STRGASIS BXLE  R8,R6,STRGSCAN     CONTINUE TILL END
         SPACE
STRGNDLM MVI   DELIM,X'FF'        DELIMITER NOW MISSING
STRGEDLM LR    R10,R8
         SR    R10,R3             COMPUTE STRING LENGTH
         STH   R10,4(,R2)         STORE INTO PDE
         ST    R3,0(,R2)
         MVI   6(R2),X'80'
         CR    R8,R7              DID WE REACH THE END?
         BH    STRGRET            YES.
         AR    R8,R6              NO, SKIP CLOSING DELIMITER
         SPACE
STRGRET  SR    R15,R15
         B     RETURN
         SPACE
*
*        FOR A SQSTRING WITH QUOTE DELIMITER, CALL CLUZSKIP TO SKIP
*        SEPARATORS, AND THEN PASS CONTROL TO THE QSTRING ROUTINE
*
STRGQDLM TM    1(R9),X'08'        WAS SQSTRING SPECIFIED?
         BZ    STRGGO             NO, QUOTE IS JUST A DELIMITER
         NI    SKIPF,255-NOSKIP   YES, SKIP BLANKS
         L     R15,=A(CLUZSKIP)
         BALR  R14,R15
         B     *+4(R15)
         B     QSTRBEGN           OFF TO QSTRING HANDLER
STRGMISS LA    R15,4              NOTE STRING IS MISSING
         B     RETURN             AND RETURN
         SPACE
STRGALL  LR    R3,R8              SAVE STARTING POINT
         LA    R8,1(,R7)          POINT CURRENT AFTER END
         ST    R3,0(,R2)          SET UP PDE
         LR    R15,R8
         SR    R15,R3             DETERMINE LENGTH
         STH   R15,4(,R2)
         MVI   6(R2),X'80'
         BNP   STRGRET            DON'T FOLD 0 LENGTH STRING
         TM    1(R9),X'40'        ASIS STRING?
         BNZ   STRGRET            YES, LEAVE IT ALONE
         SPACE
*
*        TRANSLATE THE UNDELIMITED STRING TO UPPER CASE
*
         L     R1,=V(CLUTSCAP)    FIND TRANSLATE TABLE
         LA    R14,256
         BCTR  R15,0
         SR    R10,R10
STRGFOLD BXH   R10,R14,STRGFLST
         TR    0(256,R3),0(R1)    TRANSLATE 256 CHARS
         AR    R3,R14
         B     STRGFOLD
STRGFLST EX    R15,STRGCAPS       TRANSLATE THE STRING END
         B     STRGRET
STRGCAPS TR    0(0,R3),0(R1)
         TITLE 'CLUZPSTR -- PARENTHESIZED STRING RECOGNIZER'
*
*        THIS ROUTINE RECOGNIZES AND RETURNS A PARENTHESIZED STRING
*
CLUZPSTR LIENTRY DSA=*,DSALEN=88,ENV=R5,PARM=R2
         CLI   0(R8),C'('         DOES IT START WITH A PAREN?
         BE    PSTROK             YES.
         LA    R15,4              NO, NO PSTRING PRESENT
         B     RETURN
         SPACE
*
*        SCAN THE INPUT UNTIL A CLOSING PARENTHESIS BALANCING THE
*        ORIGINAL OPEN PARENTHESIS IS LOCATED
*
PSTROK   AR    R8,R6              SKIP THE OPENING PAREN
         LR    R3,R8              SAVE STARTING POINT
         ST    R3,0(,R2)          STORE START IN PDE
         LR    R15,R6             INITIALIZE IMBALANCE COUNT
         L     R14,=V(CLUTSCAP)   FIND UPPER CASE TAB
PSTRLOOP CLI   0(R8),C'('         ANOTHER OPEN?
         BE    PSTROPEN           YES.
         CLI   0(R8),C')'         A CLOSE?
         BE    PSTRCLOS           YES.
         CLI   0(R8),C''''        A QUOTE?
         BE    PSTRQUOT           YES.
         AIF   ('&OPSYS' NE 'MVS').NMVS1C
         CLI   0(R8),C'/'         A SLASH (MAYBE STARTING A COMMENT)?
         BE    PSTRSLSH           YES.
.NMVS1C  TM    1(R9),X'40'        IS THIS ASIS?
         BNZ   PSTRASIS           YES, NO TRANSLATE
         TR    0(1,R8),0(R14)     NO, UPPER CASE THIS CHAR
PSTRASIS BXLE  R8,R6,PSTRLOOP     AND CONTINUE
         B     PSTREND
         SPACE
*
*        IGNORE PARENTHESES CONTAINED WITHIN SINGLE QUOTES
*
PSTRQUOT BXH   R8,R6,PSTREND      CHECK FOR THE END
         CLI   0(R8),C''''        CLOSING QUOTE?
         BE    PSTRASIS           YES, EXIT TO NORMAL MODE
         TM    1(R9),X'40'        NO, ASIS?
         BNZ   PSTRQUOT           YES, CONTINUE
         TR    0(1,R8),0(R14)     NO, TRANSLATE
         B     PSTRQUOT
         SPACE
         AIF   ('&OPSYS' NE 'MVS').NMVS1D
*
*        IGNORE PARENTHESES WITHIN COMMENTS
*
PSTRSLSH BXH   R8,R6,PSTREND      EXIT IF OUT OF TEXT
         CLI   0(R8),C'*'         * AFTER /?
         BNE   PSTRLOOP           NO, NOT A COMMENT
PSTRCONT BXH   R8,R6,PSTREND      YES, LOOK FOR */
         CLI   0(R8),C'*'
         BNE   PSTRCOM
         BXH   R8,R6,PSTREND
         CLI   0(R8),C'/'
         BE    PSTRASIS           FINALLY BACK TO NORMAL
PSTRCOM  TM    1(R9),X'40'        UPPERCASE IF ASIS
         BNZ   PSTRCONT
         TR    0(1,R8),0(R14)
         B     PSTRCONT
.NMVS1D  SPACE
PSTROPEN AR    R15,R6             INCREASE IMBALANCE COUNT
         B     PSTRASIS           AND CONTINUE
         SPACE
PSTRCLOS BCT   R15,PSTRASIS       DECREASE IMBALANCE COUNT
         SPACE
PSTREND  LR    R10,R8
         SR    R10,R3             COMPUTE STRING LENGTH
         STH   R10,4(,R2)         AND STORE IN PDE
         MVI   6(R2),X'80'
         SR    R15,R15            SET GOOD RETURN CODE
         MVI   BYPASS,0           PSTRINGS CANNOT BE BYPASS
         CR    R8,R7              STILL IN BOUNDS?
         BH    RETURN             NO, ESCAPE
         AR    R8,R6              YES, SKIP PAST CLOSING PAREN
         B     RETURN
         TITLE 'CLUZIDEN -- NON-DELIMITER DEPENDENT (IKJIDENT) RECOGNIZ*
               ER'
*
*        THIS ROUTINE RECOGNIZES NON-DELIMITER DEPENDENT POSITIONAL
*        PARAMETERS DESCRIBED BY THE IKJIDENT MACRO
*
CLUZIDEN LIENTRY DSA=IDENDSA,DSALEN=IDENLEN,PARM=R2,ENV=R5
*
*        GET THE MAXIMUM LENGTH ALLOWED, IF ONE WAS SPECIFIED
*
         AIF   ('&OPSYS' NE 'MVS').NMVS5A
         TM    6(R9),X'10'        INTEGER IDENT?
         BNZ   INTGIDEN           YES, MAXLEN IMPLIED
         MVI   IDENTYPE,0         NO, START WITH NO TYPE FLAGS
.NMVS5A  L     R15,=X'7FFFFFFF'   ASSUME NO MAX LEN
         TM    6(R9),X'40'        WERE WE RIGHT?
         BZ    IDENBEGN           YES.
         MVC   ALIGN,9(R9)        NO, FIND MAX LENGTH
         LH    R15,ALIGN
         LA    R15,9(R9,R15)
         SR    R1,R1
         IC    R1,0(,R15)
         LR    R15,R1             AND SAVE IN R15
         SPACE
*
*        SEE IF THE FIRST CHARACTER IS THE RIGHT KIND
*
IDENBEGN EQU   *
         AIF   ('&OPSYS' NE 'MVS').NMVS5B
         TM    6(R9),X'08'        CHAR IDENT?
         BNZ   CHARIDEN           YES, ESCAPE TO CHAR ROUTINE
         TM    6(R9),X'04'        HEX IDENT?
         BNZ   HEXIDEN            YES, ESCAPE TO HEX ROUTINE
.NMVS5B  ANOP
IDENSCAN SR    R1,R1
         IC    R1,0(,R8)
         LA    R3,SCANTAB(R1)     INSPECT 1ST CHAR
         CLI   0(R8),C')'         IS IT A )?
         BE    IDENABS            YES, IDENT NOT PRESENT
         IC    R1,7(,R9)          NO, LOAD CHAR 1 CODE
         CLI   7(R9),0            IS ANYTHING OK?
         BE    IDENHERE           YES.
         IC    R1,IDENBITS-1(R1)
         EX    R1,IDENTEST        NO, SEE IF FIRST CHAR OK
         BNZ   IDENHERE           YES IT IS
         SPACE
*
*        SEE IF ASTERISK IS ALLOWED AND WE HAVE ONE
*
IDENMISS TM    6(R9),X'80'        IS ASTERISK SPECIFIED?
         BZ    IDENABS            NO.
         CLI   0(R8),C'*'         YES, CHECK FOR *
         BE    IDENSTAR           OK
IDENABS  LA    R15,4              NO, MAKE IT MISSING
         B     RETURN             AND RETURN
         SPACE
IDENHERE SR    R10,R10            ZERO PAREN COUNT
         L     R14,=V(CLUTSCAP)   ADDR UPPER CASE TABLE
         CLI   0(R8),C'('         STARTING WITH PAREN?
         BNE   IDENNOPN           NO.
         LR    R10,R6             YES, SET COUNT TO 1
IDENNOPN ST    R8,0(,R2)          SAVE START IN PDE
         LR    R0,R8              SAVE STARTING POINT
         IC    R1,8(R9)           LOAD TYPE CODE FOR OTHER CHARS
         LTR   R1,R1              IS IT ALL?
         BZ    IDENALL            YES.
         IC    R1,IDENBITS-1(R1)
IDENALL  MVI   6(R2),X'80'        SET PRESENT FLAG
         SPACE
*
*        IF OTHER=ALL, SCAN UNTIL A NON-ALPHANUMERIC/NATIONAL IS FOUND
*
IDENOK   TM    1(R9),X'40'        ASIS MODE?
         BNZ   IDENASIS           YES.
         TR    0(1,R8),0(R14)     NO, TRANSLATE THIS CHAR
IDENASIS BXH   R8,R6,IDENEND      ANYTHING LEFT?
         SR    R3,R3              YES, LOOK AT IT
         IC    R3,0(,R8)
         LA    R3,SCANTAB(R3)
         LTR   R1,R1              TYPE=ALL?
         BZ    IDENCNT            YES.
         EX    R1,IDENTEST        NO, CHECK TYPE
         BNZ   IDENOK             BRANCH IF OK
         TM    0(R3),X'E0'        IS IT ALPHANUMERIC | NATIONAL?
         BZ    IDENEND            NO, STOP SCAN NOW
         B     IDENSKIP           YES, CONTINUE ERROR SCAN
         SPACE
*
*        IF OTHER=ALL, SCAN FOR A SEPARATOR OTHER THAN /
*
IDENCNT  TM    0(R3),X'10'        HAVE WE A SEPARATOR?
         BZ    IDENNSEP           NO.
         CLI   0(R8),C'/'         YES, ALLOW SLASH
         BNE   IDENEND            OTHERWISE IDEN IS ENDED
         B     IDENASIS
IDENNSEP CLI   0(R8),C'('         HAVE WE AN OPEN?
         BE    IDENOPEN           YES.
         CLI   0(R8),C')'         A CLOSE?
         BNE   IDENOK             NO.
         SR    R10,R6             DECREASE PAREN COUNT
         BM    IDENEND            STOP IF COUNT GOES NEGATIVE
         B     IDENASIS           ELSE CONTINUE
IDENOPEN AR    R10,R6             ADD 1 TO PAREN COUNT
         B     IDENASIS           AND CONTINUE
         SPACE
*
*        THIS CODE SKIPS TO THE NEXT NON-ALPHANUMERIC/NATIONAL AFTER
*        AN ERROR HAS BEEN DETECTED
*
IDENSKIP IC    R1,0(,R8)          GET NEXT CHAR
         LA    R3,SCANTAB(R1)
         TM    0(R3),X'E0'        IS IT ALPHANUMERIC | NATIONAL?
         BZ    IDENBAD            NO, TIME TO QUIT
         BXH   R8,R6,IDENBAD      QUIT WHEN TEXT GONE
         B     IDENSKIP
         SPACE
IDENEND  EQU   *
         AIF   ('&OPSYS' NE 'MVS').NMVS5C
         TM    6(R9),X'08'        IS THIS A CHAR IDENT?
         BNZ   IDENGOOD           YES, UNBALANCED PARENS ARE OK
.NMVS5C  LTR   R10,R10            WERE PARENTHESES BALANCED?
         BP    IDENBAD            NO, EVIL IDENT
IDENGOOD LR    R3,R8              YES, COMPUTE LENGTH
         SR    R3,R0
         CR    R3,R15             WAS IT TOO LONG?
         BH    IDENBAD            YES.
         STH   R3,4(,R2)          STORE LENGTH IN PDE
         AIF   ('&OPSYS' NE 'MVS').NMVS5D
         TM    6(R9),X'10'        INTEGER IDENT?
         BNZ   INTGEND            YES, RETURN TO INTEGER ROUTINE
.NMVS5D  B     IDENDONE           IDEN COMPLETED
         SPACE
*
*        THIS CODE REJECTS AN INVALID IDENT
*
IDENBEND AR    R8,R6              SKIP CURRENT CHAR
IDENBAD  LA    R15,8              SET EVIL RET CODE
         B     RETURN
         SPACE
*
*        THIS CODE HANDLES AN ASTERISK
*
IDENSTAR ST    R8,0(,R2)          STORE STAR ADDR
         AR    R8,R6              SKIP
         MVC   4(2,R2),=H'1'      LENGTH OF 1
         SPACE
IDENDONE MVI   6(R2),X'80'        INDICATE PRESENT
         AIF   ('&OPSYS' NE 'MVS').NMVS5E
         OC    6(1,R2),IDENTYPE   TURN ON AMS TYPE FLAGS
.NMVS5E  SR    R15,R15            RETURN HAPPY
         B     RETURN
         SPACE
IDENTEST TM    0(R3),X'00'        TEST TYPE OF CURRENT CHAR
IDENBITS DC    X'A040E02060'      IDENT TYPE CODES
         TITLE 'CLUZIDEN/CLUZCHAR -- CHARACTER/PASSWORD RECOGNITION'
*
*        THIS ROUTINE ROUTES CONTROL TO EITHER THE IDENT OR THE QUOTED
*        STRING RECOGNIZER FOR A PASSWORD
*
CLUZCHAR LIENTRY DSA=IDENDSA,DSALEN=IDENLEN,ENV=R5,PARM=R2
*
*        SET UP A FAKE PCE FOR THE IDENT ROUTINE
*
         AIF   ('&OPSYS' NE 'MVS').NMVS5F
         MVI   IDENTYPE,0         NO AMS FLAGS FOR PASSWORD
.NMVS5F  LA    R15,8              MAXIMUM PASSWORD LENGTH
         MVC   FAKEIDEN,CHARPASS  MOVE IN PSEUDO-PCE
CHARBEGN NC    FAKEIDEN+1(1),1(R9)     COPY ASIS FLAG
         LA    R9,FAKEIDEN
         CLI   0(R8),C''''        DOES IT START WITH A QUOTE?
         BE    CHARQUOT           YES.
         B     IDENSCAN           NO, TREAT AS ALPHANUM IDENT
CHARQUOT LR    R0,R15             MOVE MAX LEN FIELD
         B     QSTRYES            AND TREAT AS QSTRING
         SPACE
CHARPASS DC    X'8040',2H'0',X'400303'   PCE FOR PASSWORD-LIKE IDENT
         AIF   ('&OPSYS' NE 'MVS').NMVS5G
CHARPCE  DS    0H
         DC    X'8040',2H'0',X'080000'   PCE FOR ANY-ANY IDENT
         SPACE
*
*        HANDLE A CHAR-TYPE IDENT. THIS IS TREATED LIKE AN ANY-ANY
*        IDENT, EXCEPT THAT BALANCED PARENTHESES ARE NOT REQUIRED.
*
CHARIDEN MVC   FAKEIDEN,CHARPCE   MOVE IN PSEUDO-PCE
         OI    IDENTYPE,X'04'     INDICATE CHARACTER VALUE
         B     CHARBEGN
         SPACE
         TITLE 'CLUZIDEN -- INTEGER RECOGNITION'
*
*        DETERMINE IF THE INTEGER IS DECIMAL, HEXADECIMAL, BINARY
*        OR INVALID
*
INTGIDEN MVI   IDENTYPE,X'02'     INDICATE NUMERIC VALUE TYPE
         SR    R1,R1
         IC    R1,0(,R8)
         EX    R1,INTGSEEB        IS FIRST CHAR B?
         BE    BINTG              YES, CHECK FOR B'
         EX    R1,HEXSEEX         IS FIRST CHAR X?
         BE    HEXINTG            YES, CHECK FOR X'
         OI    IDENTYPE,X'10'     INDICATE DECIMAL INPUT
         MVC   FAKEIDEN,INTGPCE   SET UP ALL NUMERIC PCE FOR IDENT
         LA    R15,10             MAXLNTH FOR INTEGERS
         LA    R9,FAKEIDEN
         B     IDENBEGN           TREAT AS NORMAL IDENT
INTGSEEB CLI   =C'B',X'40'        LOWER OR UPPER CASE B?
INTGPCE  DC    X'8000',2H'0',X'500202' IDENT PCE FOR DECIMAL INTEG
         SPACE 5
*
*        SEE IF THE DECIMAL VALUE WILL FIT IN A FULLWORD
*
INTGEND  LR    R14,R0             COPY IDENT START POINT
         CH    R3,=H'9'           <10 DIGITS?
         BNH   INTGOK             YES, MUST BE IN BOUNDS
         CLC   0(10,R14),=C'4294967296' IS IT TOO BIG?
         BNL   IDENBAD            YES.
INTGOK   BCTR  R3,0
         EX    R3,INTGPACK        CONVERT TO BINARY
         CP    DECWORK,=P'2147483648'  WILL HIGH-ORDER BIT BE ON?
         BL    INTGPOS            NO.
         SP    DECWORK,=P'4294967296'  YES, MAKE NEGATIVE FOR CVB
INTGPOS  CVB   R14,DECWORK        MAKE IT BINARY
         ST    R14,INTWORK        STORE IN WORK AREA
         SPACE
*
*        RETURN THE INTEGER VALUE
*
INTGRET  LA    R1,4               LENGTH OF INTEGER VALUE
         L     R15,=A(CLUZALOC)
         BALR  R14,R15            GET MEMORY FOR INTEGER
         L     R14,NEWMEM
         MVC   0(4,R14),INTWORK   MOVE VALUE IN
         MVC   4(2,R2),=H'4'      STORE LENGTH
         ST    R14,0(,R2)         AND LOCATION
         MVI   6(R2),X'80'        INDICATE INTEGER PRESENT
         OC    6(1,R2),IDENTYPE   ADD TYPE BITS
         SR    R15,R15            SET GOOD RETURN CODE
         B     RETURN
INTGPACK PACK  DECWORK(8),0(0,R14)     PACK DIGITS
         TITLE 'CLUZIDEN -- BINARY INTEGER RECOGNITION'
*
*        THIS ROUTINE RECOGNIZES INTEGERS IN THE B'...' FORMAT
*
BINTG    CR    R7,R8              ANY MORE TEXT LEFT?
         BNH   IDENABS            NO, INTEGER MISSING
         CLI   1(R8),C''''        YES, IS THERE A QUOTE AFTER THE B?
         BNE   IDENABS            NO, IT'S MISSING
         OI    IDENTYPE,X'08'     YES, INDICATE BINARY INPUT
         SR    R1,R1
         LR    R15,R1             SET UP ACCUMULATOR
         LA    R14,33             NUMBER OF BITS
         AR    R8,R6              SKIP PAST B
         SPACE
BINLOOP  BXH   R8,R6,BINUNCL      GET A BIT
         CLI   0(R8),C'1'         IS IT 1?
         BE    BINBIT             YES.
         CLI   0(R8),C'0'         0?
         BE    BINBIT             YES.
         CLI   0(R8),C''''        CLOSING QUOTE?
         BE    BINEND             YES.
         IC    R1,0(,R8)
         LA    R3,SCANTAB(R1)
         TM    0(R3),X'11'        IS IT A TERMINATOR?
         BNZ   BINUNCL            YES.
         B     BINBAD             NO, BAD BINARY
         SPACE
BINBIT   SLL   R15,1              SHIFT VALUE ONE
         CLI   0(R8),C'0'         IS THIS BIT 0?
         BE    BINNEXT            YES, NO CHANGE
         ALR   R15,R6             NO, ADJUST AGAIN
BINNEXT  BCT   R14,BINLOOP        CONTINUE UP TO 32 BITS
         SPACE
BINBAD   BXH   R8,R6,IDENBAD
         CLI   0(R8),C''''        SKIP TO APOSTROPHE
         BE    IDENBEND
         IC    R1,0(,R8)
         LA    R3,SCANTAB(R1)
         TM    0(R3),X'11'        FOUND A TERMINATOR
         BNZ   IDENBAD            YES, ESCAPE
         B     BINBAD             NO, KEEP LOOKING
         SPACE
BINEND   AR    R8,R6              SKIP CLOSING QUOTE
         B     BINDONE            AND RETURN
         SPACE
BINUNCL  BAL   R14,UNCL           GIVE UNCLOSED MSG
BINDONE  MVI   6(R2),X'80'        SET PRESENT FLAG
         ST    R15,INTWORK        STORE VALUE IN INTWORK
         B     INTGRET            AND GO TO RETURN VALUE
         TITLE 'CLUZIDEN -- HEXADECIMAL RECOGNITION'
*
*        THIS ROUTINE HANDLES A HEXADECIMAL VALUE OF THE FORM
*        X'...'
*
HEXIDEN  SR    R1,R1
         IC    R1,0(,R8)
         EX    R1,HEXSEEX         DOES IT START X?
         BNE   CHARIDEN           NO, TREAT AS CHAR
         LA    R3,CHARIDEN
         B     HEXJOIN            JOIN INTEG CASE
HEXSEEX  CLI   =C'X',X'40'        SEE IF UPPER OR LOWER CASE X
         SPACE
HEXINTG  LA    R15,8              8 DIGITS ALLOWED FOR INTEGER
         LA    R3,IDENMISS        WHERE TO GO IF MISSING
         SPACE
*
*        FIRST SCAN THE INPUT TO SEE IF IT IS VALID HEX
*
HEXJOIN  CR    R8,R7              ANYTHING LEFT?
         BCR   11,R3              NO, NOT HEX
         CLI   1(R8),C''''        DOES A QUOTE FOLLOW X?
         BCR   7,R3               NO, NOT X
         LA    R8,2(,R8)          YES, SKIP X'
         LR    R10,R8             SAVE STARTING POINT
         OI    IDENTYPE,X'01'     INDICATE HEX INPUT
         TM    6(R9),X'40'        IS THERE A MAXLNTH?
         BZ    HEXLOOP            NO.
         AR    R15,R15            YES, TWICE AS MANY DIGITS ALLOWED
         SPACE
HEXLOOP  IC    R1,0(,R8)          INSPECT NEXT CHAR
         LA    R3,SCANTAB(R1)
         TM    0(R3),X'11'        IS IT A SEPARATOR?
         BNZ   HEXOVER            YES, END OF PARM
         TM    0(R3),X'60'        NO, IS IT ALPHAMERIC?
         BZ    HEXOFF             NO.
         TM    0(R3),X'80'        YES, IS IT A HEX DIGIT?
         BNZ   HEXBAD             NO, EVIL
         BXLE  R8,R6,HEXLOOP      YES, CONTINUE
         B     HEXOVER            QUIT WHEN NO TEXT LEFT
         SPACE
HEXOFF   CLI   0(R8),C''''        CLOSING QUOTE?
         BE    HEXOVER            YES.
HEXBAD   BXH   R8,R6,IDENBAD      NO, SEARCH FOR END OF BAD TEXT
         CLI   0(R8),C''''        STOP AT APOSTROPHE
         BE    IDENBEND
         IC    R1,0(,R8)
         LA    R3,SCANTAB(R1)
         TM    0(R3),X'11'        OR AT SEPARATOR
         BNZ   IDENBAD
         B     HEXBAD
         SPACE
HEXOVER  LR    R3,R8
         SR    R3,R10             COMPUTE LENGTH
         CR    R3,R15             COMPARE TO MAX
         BNH   HEXSHORT           ESCAPE IF TOO BIG
         CLI   0(R8),C''''        DID VALUE END WITH QUOTE?
         BNE   IDENBAD            NO.
         AR    R8,R6              YES, REJECT QUOTE TOO
         B     IDENBAD
         SPACE
HEXSHORT CR    R8,R7              DID WE REACH THE END?
         BH    HEXUNCL            NO, UNCLOSED
         CLI   0(R8),C''''        YES, DID IT CLOSE PROPERLY?
         BNE   HEXUNCL            NO.
         AR    R8,R6              YES, SKIP QUOTE
         B     HEXCVT             GO TO CONVERT
HEXUNCL  BAL   R14,UNCL           GIVE UNCLOSED MSG
         SPACE
*
*        CONVERT THE HEX TO INTERNAL BINARY
*
HEXCVT   TM    6(R9),X'10'        INTEGER IDENT?
         BNZ   HEXCVINT           YES, CONVERT TO INTEGER
         LA    R1,1(,R3)          TAKE FLOOR((LENGTH+1)/2)
         SRL   R1,1
         STH   R1,4(,R2)          STORE IN PDE
         LA    R1,3(,R1)          ROUND TO FULLWORD
         N     R1,=F'-3'
         L     R15,=A(CLUZALOC)
         BALR  R14,R15            GET MEMORY FOR VALUE
         L     R1,NEWMEM
         LA    R15,8              DIGITS TO CONVERT AT A TIME
         LCR   R14,R15
         LA    R3,7(,R3)          ADJUST FOR FIRST ENTRY
HEXCLOOP BXLE  R3,R14,HEXCLAST    8 DIGITS LEFT?
         MVC   DECWORK,0(R10)     YES, MOVE TO WORK AREA
         NC    DECWORK,=8X'1F'    TURN OFF USELESS BITS
         TR    DECWORK,HEXCVTAB   CONVERT TO BINARY
         PACK  HEXWORK(5),DECWORK(9)   PACK INTO FULLWORD
         MVC   0(4,R1),HEXWORK    MOVE TO OUTPUT BUFFER
         LA    R1,4(,R1)          BUMP OUTPUT POINTER
         SR    R10,R14            BUMP INPUT POINTER
         B     HEXCLOOP           AND CONTINUE
HEXCLAST XC    DECWORK,DECWORK    EXTRA ZEROES FOR LAST TIME
         EX    R3,HEXMLAST        MOVE LAST DIGITS IN
         NC    DECWORK,=8X'1F'    OR OFF BITS
         TR    DECWORK,HEXCVTAB   TRANSLATE
         PACK  HEXWORK(5),DECWORK(9)   PACK
         MVC   0(4,R1),HEXWORK    SAVE THE RESULT
         TM    6(R9),X'10'        INTEGER?
         BNZ   HEXINTOK           YES, EXIT
         L     R1,NEWMEM
         ST    R1,0(,R2)          NO, SAVE BUFFER ADDRESS IN PDE
         MVI   6(R2),X'80'        SET PRESENT FLAG
         OC    6(1,R2),IDENTYPE   SET TYPE BIT(S)
         SR    R15,R15            AND RETURN HAPPY
         B     RETURN
HEXMLAST MVC   DECWORK(0),0(R10)  MOVE LAST DIGITS TO WORK AREA
         SPACE
HEXCVINT LA    R6,32
         SLL   R3,2               CONVERT NUMBER DIGITS TO BITS
         SR    R6,R3              DETERMINE SCALE FACTOR
         LA    R1,INTWORK         DESTINATION ADDR
         LA    R3,7
         B     HEXCLAST           JOIN HEX CASE
         SPACE
HEXINTOK L     R0,INTWORK         LOAD HEX VALUE
         SRL   R0,0(R6)           COMPENSATE FOR UNENTERED DIGITS
         ST    R0,INTWORK         STORE
         MVI   6(R2),X'80'        SET PRESENT
         B     INTGRET            GO FINISH OFF INTEGER
         SPACE
HEXCVTAB DC    X'000A0B0C0D0E0F',9X'00' HEX->BINARY TRANSLATE TABLE
         DC    X'00010203040506070809'
.NMVS5G  SPACE 5
IDENDSA  DSECT ,                  FOR USE BY IDEN & CHAR ROUTINES
         SPACE
         DS    22A                SAVE AREA
         AIF   ('&OPSYS' NE 'MVS').NMVS5H
DECWORK  DS    D                  DECIMAL WORK AREA
INTWORK  DS    F                  INTEGER WORK AREA
HEXWORK  DS    CL5                HEX WORK AREA
IDENTYPE DS    BL1                AMS INPUT TYPE BITS
.NMVS5H  ANOP
FAKEIDEN DS    CL9                FAKE-PCE STORAGE
         DS    0D
IDENLEN  EQU   *-IDENDSA          LENGTH OF DSA
         SPACE
CLUTSREC CSECT
         TITLE 'CLUZITEM -- KEYWORD/MISCELLANEOUS RECOGNIZER'
*
*        THIS ROUTINE IS USED TO RECOGNIZE KEYWORDS. IT IS ALSO USED
*        TO GUESS WHAT PART OF THE FOLLOWING INPUT SHOULD BE REJECTED
*        WHEN UNINTELLIGIBLE INFORMATION MUST BE HANDLED.
*
CLUZITEM LIENTRY DSA=*,DSALEN=88,ENV=R5
         SR    R1,R1
         SR    R15,R15
         LR    R3,R8              SAVE STARTING POINT
         CLI   0(R8),C'/'         SLASH FIRST?
         BE    ITEMEVIL           YES, INVALID KEYWORD
         CLI   0(R8),C';'         LOOK FOR MISPLACED SEMICOLON
         BE    ITEMEVIL           JUST A CHARACTER IF FOUND
         CLI   0(R8),C'('         PAREN FIRST?
         BE    ITEMPREN           YES, DON'T REJECT MORE
         CLI   0(R8),C')'
         BNE   ITEMMORE
ITEMPREN AR    R8,R6              SKIP PARENTHESIS
ITEMBAD  LA    R15,4              AND SAY BAD ITEM
         B     RETURN
         SPACE
*
*        SCAN UNTIL A SEPARATOR OR A PARENTHESIS IS FOUND. IF, HOWEVER,
*        A QUOTE IS FOUND, GIVE UP WHEN THE QUOTE CLOSES.
*
ITEMNEXT BXH   R8,R6,ITEMEND      ON TO NEXT CHAR
         CLI   0(R8),C'('         OPEN PAREN?
         BE    ITEMEND            YES, TIME TO STOP
         CLI   0(R8),C')'         NO, CLOSE PAREN?
         BE    ITEMEND            YES, STOP NOW
ITEMMORE CLI   0(R8),C''''        OPEN QUOTE
         BE    ITEMQUOT           YES, STOP AT CLOSE QUOTE
         IC    R1,0(,R8)
         LA    R10,SCANTAB(R1)
         TM    0(R10),X'10'       IS NEXT CHAR A SEPARATOR?
         BNZ   ITEMEND            YES, QUIT NOW
         TM    0(R10),X'A0'       NO, IS IT ALPHA?
         BNZ   ITEMFOLD           YES.
         TM    0(R10),X'40'       NUMERIC?
         BZ    ITEMEVIL           NO, BAD KEYWORD
         CR    R3,R8              FIRST CHAR OF ITEM?
         BE    ITEMEVIL           YES, NUMERIC IS INVALID
         B     ITEMNEXT           NO, CONTINUE
ITEMFOLD TM    1(R9),X'40'        IS PDE ASIS?
         BNZ   ITEMNEXT           YES.
         OI    0(R8),X'40'        NO, UPPER CASE LETTER
         B     ITEMNEXT           AND CONTINUE
ITEMEVIL LA    R15,4              INDICATE BAD RESULT
         B     ITEMNEXT           AND CONTINUE
         SPACE
ITEMQUOT LA    R15,4              IF QUOTED, INVALID KEYWORD
ITEMQLOP BXH   R8,R6,ITEMEND      BUT LOOK FOR CLOSE QUOTE
         CLI   0(R8),C''''
         BNE   ITEMQLOP
         BXH   R8,R6,ITEMEND
         CLI   0(R8),C''''        CHECK FOR DOUBLED QUOTE
         BE    ITEMQLOP
         SPACE
ITEMEND  LTR   R15,R15            ANY PROBLEM FOUND?
         BNZ   RETURN             YES.
         LR    R10,R8             NO, CHECK KEYWORD LENGTH
         SR    R10,R3
         CH    R10,=H'31'         MUST BE <= 31
         BH    ITEMBAD
         B     RETURN
         TITLE 'CLUZDSNM -- DSNAME/DSTHING RECOGNIZER'
*
*        THIS ROUTINE RECOGNIZES DSNAMES AND DSTHINGS
*
CLUZDSNM LIENTRY DSA=DSNWORK,DSALEN=DSNWLEN,ENV=R5,PARM=R2
         SR    R14,R14
         SR    R0,R0
         SR    R1,R1
         MVI   DSNFLAG,0          INIT FLAGS
         MVI   PASSF,PASSABLE     INDICATE PASSWORD MAY FOLLOW
         LA    R10,16(,R2)        FIND PASSWORD PART OF PDE
         ST    R10,PASSPDE        AND STORE
         LR    R10,R8             SAVE STARTING POINT
         SPACE
*
*        SEE IF THE FIRST CHARACTER IS LEGAL. IF NOT, THE DSNAME IS
*        MISSING
*
         CLI   0(R8),C''''        STARTING WITH QUOTE?
         BE    DSNQUOT            YES.
         IC    R1,0(,R8)          NO, INSPECT 1ST CHAR
         LA    R3,SCANTAB(R1)
         TM    0(R3),X'A0'        IS IT ALPHANATIONAL?
         BNZ   DSNALPHA           YES.
         CLI   0(R8),C'('         NO, STARTING PAREN?
         BE    DSNMEM             YES, MEMBER ONLY
         CLI   6(R9),8            NO, IS THIS A DSTHING?
         BNE   DSNNSTAR           NO, * NOT ALLOWED
         CLI   0(R8),C'*'         YES, DOES IT START *?
         BE    DSNSTAR            YES.
         SPACE
DSNNSTAR EQU   *
         AIF   ('&OPSYS' NE 'MVS').NMVS6A
         TM    0(R3),X'40'        IS THIS A DIGIT?
         BZ    DSNMISS            NO, BAD DSNAME
         TM    1(R9),X'02'        YES, IS VOLSER SPECIFIED?
         BNZ   DSNVOL             YES, ALLOW VOLSER
.NMVS6A  ANOP
DSNMISS  LA    R15,4              SET MISSING RET CODE
         NI    PASSF,255-PASSABLE KILL PASSWORD OK FLAG
         B     RETURN             AND RETURN
         SPACE
*
*        ANALYZE THE DSNAME PROPER. CONSIDER IT TO STOP WHEN A
*        CHARACTER OTHER THAN A ALPHAMERIC/NATIONAL OR PERIOD IS FOUND.
*        (* IS ALSO ALLOWED FOR DSTHINGS)
*
DSNQUOT  OI    DSNFLAG,DSNQ       SET QUOTED FLAG
         BXH   R8,R6,DSNERR       ERROR IF NOTHING MORE
         LR    R10,R8             SAVE STARTING POINT
         SPACE
DSNCHAR  IC    R1,0(,R8)
         LA    R3,SCANTAB(R1)     LOOK AT NEXT CHAR
         TM    0(R3),X'A0'        IS IT ALPHA-NATIONAL?
         BNZ   DSNALPHA           YES.
         TM    0(R3),X'40'        A DIGIT?
         BNZ   DSNDIGIT           YES.
         TM    0(R3),X'10'        A SEPARATOR
         BNZ   DSNOVER            YES, DSN PROPER IS DONE
         CLI   0(R8),C'.'         A PERIOD?
         BE    DSNDOT             YES.
         CLI   6(R9),8            DSTHING ALLOWED?
         BNE   DSNOOPS            NO, BAD STUFF
         CLI   0(R8),C'*'         YES, HAVE WE A STAR?
         BE    DSNSTAR            YES.
DSNOOPS  EQU   *
         AIF   ('&OPSYS' NE 'MVS').NMVS6B
         CLI   0(R8),C'-'         IS THIS A HYPHEN?
         BE    DSNDIGIT           YES, TRAET AS DIGIT.
         CLI   0(R8),X'C0'        HOW ABOUT A +0?
         BE    DSNDIGIT           YES, ALSO PSEUDO-DIGIT
.NMVS6B  TM    0(R3),X'08'        HAVE WE A NON-DELIMITER (,),',*?
         BNZ   DSNOVER            YES, DSNAME ENDED
         B     DSNOUCH            NO, GO FIND END OF BAD DSN
         SPACE
DSNALPHA LTR   R0,R0              IS A LETTER ALLOWED?
         BM    DSNOUCH            NO, PERIOD REQUIRED.
         AR    R14,R6             YES, INCREASE LEVEL COUNT
         TM    1(R9),X'40'        ASIS?
         BNZ   DSNASIS            YES.
         OI    0(R8),X'40'        NO, UPPERCASE LETTER
DSNASIS  LR    R0,R6              ALLOW ANYTHING AFTER
         CH    R14,=H'8'          UNLESS 8 CHARS IN LEVEL
         BL    DSNNEXT
         LCR   R0,R0              IN WHICH CASE . IS REQUIRED
         B     DSNNEXT
         SPACE
DSNDIGIT LTR   R0,R0              IS A DIGIT OK?
         BNP   DSNDIG2            NO.
         AR    R14,R6             YES, INCREASE LEVEL COUNT
         B     DSNASIS            AND DECIDE NEXT STATE
         SPACE
DSNDOT   LTR   R0,R0              IS A . OK?
         BZ    DSNOUCH            NOT AFTER ANOTHER .
         SR    R0,R0              YES, FORCE LETTER NEXT
         LR    R14,R0             RESET LEVEL COUNT
         B     DSNNEXT
         SPACE
DSNSTAR  LTR   R0,R0              RIGHT AFTER A .?
         BNZ   DSNOUCH            NO, EVIL
         LCR   R0,R6              YES, FORCE . AFTER
         SPACE
DSNNEXT  BXLE  R8,R6,DSNCHAR      AND CONTINUE
         SPACE
*
*        SEE IF THERE IS A MEMBER NAME AFTER THE DSNAME
*
DSNOVER  LTR   R0,R0              WAS LAST CHAR .?
         BNZ   DSNOKEND           NO.
         OI    DSNFLAG,DSNERRF    YES, SET ERROR FLAG
DSNOKEND LR    R0,R8
         SR    R0,R10             COMPUTE DSN LENGTH
         SR    R14,R14            INIT. MEMBER LENGTH
DSNCONT  CR    R8,R7              ANY TEXT LEFT?
         BH    DSNALL             NO.
DSNCHMEM CLI   0(R8),C'('         YES, DOES A MEMBER FOLLOW?
         BE    DSNMEM             YES.
         B     DSNQCHEK           NO, CHECK FOR CLOSING QUOTE
         SPACE
         AIF   ('&OPSYS' EQ 'MVS').MVS6C
DSNDIG2  EQU   *
         AGO   .JMVS6C
.MVS6C   ANOP
*
*        A DIGIT HAS BEEN FOUND IN AN INVALID SPOT. SEE IF IT COULD
*        BE THE FIRST DIGIT OF A VOLSER.
*
DSNDIG2  CR    R8,R10             JUST STARTING?
         BNE   DSNOUCH            NO, ERROR
         TM    1(R9),X'02'        YES, CAN THIS BE A VOLSER?
         BZ    DSNOUCH            NO, ERROR
DSNVOL   LR    R10,R8             SAVE STARTING POINT
DSNVLOOP BXH   R8,R6,DSNVEND
         IC    R1,0(,R8)
         LA    R3,SCANTAB(R1)     CHECK NEXT CHAR
         TM    0(R3),X'10'        SEPARATOR?
         BNZ   DSNVEND            YES, END OF VOLSER
         TM    0(R3),X'E0'        ALPHANUMERIC | NATIONAL?
         BNZ   DSNVOK             YES.
         CLI   0(R8),C'-'         HAVE WE A HYPHEN?
         BE    DSNVOK             YES, THAT'S OK
         CLI   0(R8),X'C0'
         BE    DSNVOK             SO IS A +0
         TM    0(R3),X'08'        NON-DELIMITER?
         BNZ   DSNVEND            YES, VOLSER FINISHED
         B     DSNOUCH            NO, BAD DSNAME
DSNVOK   TM    1(R9),X'40'        ASIS?
         BNZ   DSNVLOOP           NO, DON'T SHIFT
         OI    0(R8),X'40'        YES, TO UPPER CASE
         B     DSNVLOOP
DSNVEND  LR    R0,R8
         SR    R0,R10             COMPUTE LENGTH OF VOLSER
         CH    R0,=H'6'           > 6?
         BNH   DSNCONT            NO.
         OI    DSNFLAG,DSNERRF    YES, INDICATE ERROR
         B     DSNSKMID           AND SKIP REST OF NAME
.JMVS6C  SPACE
*
*        AFTER A SYNTAX ERROR IS DETECTED, CONTINUE SCANNING TO FIND
*        THE END OF THE NAME
*
DSNOUCH  OI    DSNFLAG,DSNERRF     SET ERROR FLAG
DSNSKIM  BXH   R8,R6,DSNERR        LOOK FOR THE END
         IC    R1,0(,R8)
         LA    R3,SCANTAB(R1)
         TM    0(R3),X'E0'         ALPHANUMERIC | NATIONAL?
         BNZ   DSNSKIM             YES, CONTINUE
DSNSKMID CLI   0(R8),C''''         QUOTE?
         BE    DSNQCHK2            YES, EXIT
         CLI   0(R8),C'('          PAREN?
         BE    DSNMEM              YES, EXIT
         TM    0(R3),X'18'         SEPARATOR | NON-DELIMITER?
         BNZ   DSNALL              YES, EXIT
         B     DSNSKIM             CONTINUE LOOKING
         SPACE
*
*        HANDLE THE MEMBER NAME. THE MEMBER SCAN WILL BE STOPPED BY A
*        SEPARATOR, A CLOSE PARENTHESIS, OR (FOR A QUOTED NAME) A
*        CLOSING QUOTE
*
DSNMEM   TM    DSNFLAG,DSNERRF    HAS AN ERROR BEEN NOTED?
         BNZ   DSNMSKIM           YES, GO TO ERROR MEMBER SCAN
         BXH   R8,R6,DSNERR       NO, SKIP PAREN
         LR    R15,R8             SAVE MEMBER START POINT
         SPACE
DSNMLOOP IC    R1,0(,R8)
         LA    R3,SCANTAB(R1)
         TM    0(R3),X'A0'        THIS CHAR ALPHA?
         BNZ   DSNMALPH           YES.
         LTR   R14,R14            NO, IS THIS THE FIRST CHAR OF MEMBER?
         BZ    DSNMOUCH           YES, MEMBER ERROR
         TM    0(R3),X'40'        IS THIS CHAR A DIGIT?
         BNZ   DSNMNEXT           YES, CONTINUE
         TM    0(R3),X'10'        IS IT A SEPARATOR
         BNZ   DSNALL             YES, END OF MEMBER
         CLI   0(R8),C')'         CLOSING PAREN?
         BE    DSNMCLOS           YES, END OF MEMBER
         TM    DSNFLAG,DSNQ       NO, WAS DSN QUOTED?
         BZ    DSNMOUCH           NO, MUST BE ERROR
         CLI   0(R8),C''''        YES, HAVE WE A QUOTE?
         BE    DSNMQEND           YES, END OF DSN
         B     DSNMOUCH           NO, ERROR
         SPACE
DSNMALPH TM    1(R9),X'40'        ASIS DSN?
         BNZ   DSNMNEXT           YES.
         OI    0(R8),X'40'        NO, UPPER CASE THE LETTER
DSNMNEXT AR    R14,R6             INCREASE MEMBER LENGTH
         BXLE  R8,R6,DSNMLOOP     AND CONTINUE
         B     DSNALL
         SPACE
DSNMCLOS BXH   R8,R6,DSNALL       SKIP CLOSING PAREN
DSNQCHEK CLI   0(R8),C''''        IS THERE A QUOTE AFTER?
         BNE   DSNALL             NO.
DSNQCHK2 TM    DSNFLAG,DSNQ       YES, WAS IT EXPECTED?
         BZ    DSNALL             NO.
DSNMQEND OI    DSNFLAG,DSNQEND    YES, SET CLOSING QUOTE FLAG
         AR    R8,R6              SKIP THE QUOTE
         B     DSNALL
         SPACE
*
*        SKIM THROUGH THE REST OF AN ERRONEOUS MEMBER
*
DSNMOUCH OI    DSNFLAG,DSNERRF    SET ERROR FLAG
DSNMSKIM BXH   R8,R6,DSNERR
         IC    R1,0(,R8)
         LA    R3,SCANTAB(R1)     EXAMINE NEXT CHAR
         TM    0(R3),X'10'        SEPARATOR?
         BNZ   DSNERR             YES, SCAN CAN STOP
         CLI   0(R8),C')'         NO, CLOSE PAREN?
         BE    DSNMCLOS           YES, MEMBER HAS ENDED
         TM    DSNFLAG,DSNQ       NO, WAS NAME QUOTED?
         BZ    DSNMSKIM           NO, CONTINUE SCAN
         CLI   0(R8),C''''        YES, HAVE WE A QUOTE?
         BNE   DSNMSKIM           NO, CONTINUE
         AR    R8,R6              YES, SKIP IT
         B     DSNERR             AND EXIT
         SPACE
*
*        CHECK THE LENGTHS OF THE DSNAME AND MEMBER. IF THEY ARE
*        OK, SET UP THE PDE
*
DSNALL   TM    DSNFLAG,DSNERRF    WAS AN ERROR FOUND?
         BNZ   DSNERR             YES, EXIT
         CH    R0,=H'44'          IS THE DSNAME TOO LONG?
         BH    DSNERR             YES.
         CH    R14,=H'8'          IS THE MEMBER TOO LONG?
         BH    DSNERR             YES.
         LR    R1,R14             MOVE MEMBER LENGTH
         AIF   ('&OPSYS' NE 'MVS').NMVS6D
         TM    1(R9),X'04'        PREFIX TO BE ADDED?
         BZ    DSNNOPFX           NO.
         TM    DSNFLAG,DSNQ       YES, IS DSN QUOTED?
         BNZ   DSNNOPFX           YES, NO PREFIX TO ADD
         CLI   0(R10),C'*'        DOES NAME START *?
         BE    DSNNOPFX           YES, NO PREFIX
         L     R3,IOPL            FIND UPT
         USING UPT,R3
         IC    R6,UPTPREFL        FIND PREFIX LENGTH
         LTR   R6,R6              IS PREFIX NULL?
         BZ    DSNNOPFX           YES, LENGTH IS OK
         AR    R6,R0              ADD PREFIX LENGTH
         CH    R6,=H'44'          IS EXPANDED NAME TOO LONG?
         BNL   DSNERR             YES.
         OI    DSNFLAG,DSNUSID    NO, SET PREFIX FLAG
DSNNOPFX EQU   *
.NMVS6D  ANOP
         TM    DSNFLAG,DSNQ+DSNQEND    SEE IF PARTIALLY QUOTED
         BNM   DSNCLOSD           NO.
         BAL   R14,UNCL           YES, GIVE UNCLOSED MESSAGE
DSNCLOSD EQU   *
         AIF   ('&OPSYS' NE 'MVS').NMVS6E
         TM    DSNFLAG,DSNUSID    DSN TO BE PREFIXED?
         BNZ   DSNPFIX            YES.
.NMVS6E  LTR   R0,R0              ANY DSN PRESENT?
         BZ    DSNSTMEM           NO, JUST HANDLE MEMBER
         ST    R10,0(,R2)         YES, STORE LOCATION
         STH   R0,4(,R2)          AND LENGTH
         OI    6(R2),X'80'        SET PRESENT FLAG
         TM    DSNFLAG,DSNQ       WAS IT QUOTED
         BZ    DSNSTMEM           NO.
         OI    6(R2),X'40'        YES, SET QUOTED FLAG
DSNSTMEM LTR   R1,R1              ANY MEMBER GIVEN?
         BZ    DSNNOMEM           NO.
         ST    R15,8(,R2)         YES, STORE LOCATION
         STH   R1,12(,R2)         AND LENGTH
         OI    14(R2),X'80'       SET MEMBER PRESENT
DSNNOMEM SR    R15,R15            SET GOOD RETURN CODE
         B     RETURN
         SPACE
         AIF   ('&OPSYS' NE 'MVS').NMVS6F
*
*        IF THE "USID" OPTION WAS SPECIFIED, APPEND THE USERID
*
DSNPFIX  STM   R14,R1,DSNPSAVE    SAVE SYSTEM REGS
         LA    R1,1(,R6)          GET LENGTH OF NEW DSN
         L     R15,=A(CLUZALOC)
         BALR  R14,R15            GET MEMORY FOR IT
         LM    R14,R1,DSNPSAVE    RESTORE REGS
         L     R6,NEWMEM          POINT TO NEW MEMORY
         IC    R1,UPTPREFL        GET PREFIX LENGTH
         BCTR  R1,0
         EX    R1,DSNMVPFX        MOVE PREFIX
         ST    R6,0(,R2)          STORE DSN LOCATION
         MVI   6(R2),X'80'        SET PRESENT FLAG
         LTR   R0,R0              IS THERE A DSN PROPER?
         BNZ   DSNPFIX2           YES.
         LA    R1,1(,R1)          NO, GET ORIGINAL LENGTH BACK
         STH   R1,4(,R2)          STORE IN PDE
         B     DSNPMEM            GO HANDLE MEMBER
DSNPFIX2 LA    R6,1(R1,R6)        POINT AFTER PREFIX
         MVI   0(R6),C'.'         PERIOD AFTER PREFIX
         LR    R1,R0
         BCTR  R1,0
         EX    R1,DSNMVNAM        MOVE REST OF DSNAME
         LA    R6,2(R1,R6)        FIND END OF DSN
         S     R6,NEWMEM          COMPUTE TOTAL LENGTH
         STH   R6,4(,R2)          STORE IN PDE
DSNPMEM  LM    R14,R1,DSNPSAVE    RESTORE REGS FOR MEMBER
         B     DSNSTMEM           AND GO HANDLE IT
DSNMVPFX MVC   0(0,R6),UPTPREFX   MOVE PREFIX TO DYNAMIC MEMORY
DSNMVNAM MVC   1(0,R6),0(R10)     MOVE DSNAME TO DYNAMIC MEMORY
         SPACE
         DROP  R3
.NMVS6F  SPACE
DSNERR   LA    R15,8              SET ERROR RETURN CODE
         B     RETURN
         SPACE 3
DSNWORK  DSECT ,                  DSN WORK AREA
         SPACE
         DS    22A                SAVE AREA
         AIF   ('&OPSYS' NE 'MVS').NMVS6G
DSNPSAVE DS    4A                 PREFIXING SAVE AREA
DSNUSID  EQU   X'01'              USERID/PREFIX BIT
.NMVS6G  ANOP
DSNFLAG  DS    B                  FLAGS
DSNQ     EQU   X'80'              DSNAME QUOTED
DSNQEND  EQU   X'40'              ENDING QUOTE FOUND
DSNERRF  EQU   X'20'              ERROR FOUND
         DS    0D
DSNWLEN  EQU   *-DSNWORK          LENGTH OF DSA
         SPACE
CLUTSREC CSECT
         TITLE 'CLUZUSID -- USERID RECOGNIZER'
CLUTSRC2 LISECT ,                 LESS FREQUENTLY USED RECOGNIZERS
*
*        THIS ROUTINE RECOGNIZES A USERID, AND INDICATES THAT A
*        PASSWORD MAY FOLLOW
*
CLUZUSID LIENTRY DSA=*,DSALEN=88,ENV=R5,PARM=R2
         SR    R1,R1
         IC    R1,0(,R8)          CONSIDER FIRST CHAR
         LA    R3,SCANTAB(R1)
         TM    0(R3),X'A0'        IS IT ALPHANATIONAL?
         BNZ   USIDHERE           YES.
         LA    R15,4              NO, NO USERID PRESENT
         B     RETURN
         SPACE
USIDHERE OI    PASSF,PASSABLE     NOTE PASSWORD MAY FOLLOW
         AIF   ('&OPSYS' NE 'MVS').NMVS3E
         CLI   6(R9),12           IS THIS A UID2PSW?
         BNE   USIDNORM           NO.
         OI    PASSF,NWPASSOK     YES, INDICATE NEW PASSWORD OK
USIDNORM EQU   *
.NMVS3E  LA    R14,8(,R2)         POINT TO PASSWORD PART OF PDE
         ST    R14,PASSPDE        AND STORE FOR LATER
         LR    R14,R8             SAVE STARTING POINT
         AR    R8,R6              SKIP CHAR 1
USIDLOOP IC    R1,0(,R8)          CONSIDER NEXT CHAR?
         LA    R3,SCANTAB(R1)
         TM    0(R3),X'E0'        ALPHANUMERIC | NATIONAL?
         BZ    USIDEND            NO, USERID ENDED.
         BXLE  R8,R6,USIDLOOP     YES, CONTINUE
         SPACE
*
*        IF THE USERID HAS MORE THAN 7 CHARACTERS, REJECT IT
*
USIDEND  LR    R10,R8
         SR    R10,R14            COMPUTE USERID LENGTH
         CH    R10,=H'7'          TOO LARGE?
         BNH   USIDOK             NO.
         LA    R15,8              YES, BLOW IT OFF
         B     RETURN
         SPACE
USIDOK   ST    R14,0(,R2)         SET UP PDE
         STH   R10,4(,R2)
         MVI   6(R2),X'80'
         TM    1(R9),X'40'        ASIS?
         BNZ   USIDASIS           YES, NO TRANSLATE
         L     R15,=V(CLUTSCAP)   FIND UPPERCASE TABLE
         BCTR  R10,0
         EX    R10,USIDCAPS       TRANSLATE THE USERID
USIDASIS SR    R15,R15            SAY ALL IS WELL
         B     RETURN
USIDCAPS TR    0(0,R14),0(R15)    TO UPPERCASE USERID
         TITLE 'CLUZVALU -- VALUE RECOGNIZER'
*
*        THIS ROUTINE RECOGNIZES AND RETURNS VALUES SUCH AS THOSE
*        USED BY THE IBM TEST COMMAND
*
CLUZVALU LIENTRY DSA=*,DSALEN=88,ENV=R5,PARM=R2
         SPACE
*
*        IF THE INPUT DOES NOT START WITH AN ALPHA-NATIONAL CHARACTER
*        AND A QUOTE, IT IS NOT A VALUE
*
         SR    R1,R1
         IC    R1,0(,R8)
         LA    R3,SCANTAB(R1)
         TM    0(R3),X'A0'        IS FIRST CHAR ALPHA-NATIONAL?
         BZ    VALUNO             NO, NO VALUE
         CR    R8,R7              YES, IS THERE ROOM FOR A QUOTE?
         BNL   VALUNO             NO.
         CLI   1(R8),C''''        YES, IS IT THERE?
         BE    VALUHERE           YES.
VALUNO   LA    R15,4              NO, VALUE IS MISSING
         B     RETURN
         SPACE
VALUHERE MVC   7(1,R2),0(R8)      MOVE VALUE TYPE TO PDE
         OI    7(R2),X'40'        UPPER CASE IT
         LA    R8,2(,R8)          SKIP PAST QUOTE
         ST    R8,0(,R2)          STORE STARTING POINT
         MVI   6(R2),X'80'        SET PRESENT FLAG
         LR    R3,R8              SAVE START POINT
         L     R14,=V(CLUTSCAP)   POINT TO UPPER CASE TABLE
         SPACE
*
*        SCAN FOR THE CLOSING QUOTE
*
VALUSCAN CLI   0(R8),C''''        QUOTE?
         BE    VALUQUOT           YES, MAY BE THE END
         TM    1(R9),X'40'        NO, ASIS?
         BNZ   VALUASIS           YES.
         TR    0(1,R8),0(R14)     NO, INTO UPPER CASE
VALUASIS BXLE  R8,R6,VALUSCAN     AND CONTINUE
         B     VALUUNCL           IF OUT OF TEXT, END OF VALUE
         SPACE
VALUQUOT CR    R8,R7              ROOM FOR ANY MORE?
         BNL   VALUEND            NO.
         CLI   1(R8),C''''        YES, CHECK FOR DOUBLED QUOTE
         BNE   VALUEND            NOPE
         AR    R8,R6              YES, SKIP FIRST QUOTE
         B     VALUASIS           AND CONTINUE
         SPACE
VALUUNCL BAL   R14,UNCL           GIVE UNCLOSED MESSAGE
         SPACE
VALUEND  LR    R10,R8
         SR    R10,R3             COMPUTE LENGTH OF VALUE
         STH   R10,4(,R2)         STORE IN PDE
         CR    R8,R7              ANY TEXT LEFT?
         BH    VALUDONE           NO.
         AR    R8,R6              SKIP CLOSING QUOTE
         SPACE
VALUDONE SR    R15,R15            SET GOOD RETURN CODE
         B     RETURN
         TITLE 'CLUZADDR -- TEST ADDRESS RECOGNIZER'
*
*        THIS ROUTINE RECOGNIZES ADDRESSES IN THE SYNTAX USED BY THE
*        TEST COMMAND.
*
CLUZADDR LIENTRY DSALEN=@LEN,DSA=@DSA,ENV=R5,PARM=R2
         SR    R1,R1
         XC    @PDE(36),@PDE      CLEAR WORK PDE
         MVC   @PDEXPR,NULL       STORE NULL EXPRESSION PTR
         CLI   0(R8),C'.'         1ST CHAR A PERIOD?
         BE    ADDRNTRY           YES, GO HANDLE ENTRY NAME
         CLI   0(R8),C'+'         1ST CHAR A +?
         BE    ADDRREL            YES, HANDLE RELATIVE ADDR
         CLI   0(R8),C'-'         1ST CHAR A -?
         BE    ADDRREL            YES, HANDLE EXTENDED RELATIVE
         IC    R1,0(,R8)
         LA    R3,SCANTAB(R1)
         TM    0(R3),X'E0'        1ST CHAR ALPHANUMERIC | NATIONAL?
         BNZ   ADDRSTRT           YES.
         LA    R15,4              NO, NO ADDRESS PRESENT
         B     RETURN
         SPACE
*
*        ADDRESS MAY BE SYMBOLIC, ABSOLUTE, LOAD.ENTRY..., OR A
*        REGISTER.
*
ADDRSTRT MVC   @TYPE1,0(R3)       SAVE TYPE OF CHAR 1
         MVC   @TYPES,0(R3)       START CUMULATIVE TYPE BYTE
         ST    R8,@START          SAVE START POINT
ADDRLOOP TM    1(R9),X'40'        ASIS ADDRESS?
         BNZ   ADDRAS1            YES.
         OI    0(R8),X'40'        NO, UPPER CASE THIS CHAR
ADDRAS1  BXH   R8,R6,ADDREND      ON TO NEXT CHAR
         IC    R1,0(,R8)
         LA    R3,SCANTAB(R1)     EXAMINE IT
         TM    0(R3),X'E4'        IS IT ALPHNUMERIC, NATIONAL OR _?
         BZ    ADDROUT            NO.
         OC    @TYPES,0(R3)       YES, OR INTO CUM. TYPE
         B     ADDRLOOP           AND CONTINUE
         SPACE
ADDROUT  TM    0(R3),X'11'        REACHED A STOPPER?
         BNZ   ADDREND            YES.
         TM    0(R3),X'02'        NO, REACHED %, + OR -?
         BNZ   ADDRSYM            YES, ADDRESS MUST BE SYMBOLIC
         CLI   0(R8),C'.'         REACHED A DOT?
         BE    ADDRDOT            YES.
         SPACE
*
*        THE ADDRESS IS BAD. REJECT UP TO THE NEXT SEPARATOR.
*
ADDRBAD  CR    R8,R7              OUT OF TEXT?
         BH    ADDREJCT           YES.
ADDRBLOP IC    R1,0(,R8)
         LA    R3,SCANTAB(R1)
         TM    0(R3),X'11'         LOOK FOR A STOPPER
         BNZ   ADDREJCT            AND STOP WHEN FOUND
         BXLE  R8,R6,ADDRBLOP
ADDREJCT LA    R15,8               INDICATE BAD ADDRESS
         B     RETURN
         SPACE
*
*        DECIDE WHETHER WE HAVE AN ABSOLUTE ADDRESS OR A LOAD MODULE
*        NAME.
*
ADDRDOT  TM    @PDEENTF,X'80'     HAS AN ENTRY ALREADY BEEN FOUND?
         BNZ   ADDRBAD            YES, CAN'T BE ANOTHER
         BXH   R8,R6,ADDRABS      NO, SKIP THE DOT
         IC    R1,0(,R8)
         LA    R3,SCANTAB(R1)
         TM    0(R3),X'13'        IS NEXT CHAR A STOPPER OR % + -?
         BNZ   ADDRABS            YES, ABSOLUTE ADDRESS
         TM    0(R3),X'A0'        NO, IS IT ALPHANATIONAL?
         BZ    ADDRBAD            NO, BAD ADDRESS
         LR    R15,R8
         S     R15,@START         COMPUTE LENGTH OF LOAD NAME
         BCTR  R15,0
         CH    R15,=H'8'          IS IT TOO LONG?
         BH    ADDRBAD            YES.
         TM    @TYPES,X'04'       UNDERSCORE IN LOAD NAME?
         BNZ   ADDRBAD            YES, BAD LOAD NAME
         TM    @TYPE1,X'40'       FIRST CHAR A DIGIT?
         BNZ   ADDRBAD            YES, BAD LOAD NAME
         MVC   @PDELOD,@START     STORE LOAD NAME START
         STH   R15,@PDELODL       AND LENGTH
         MVI   @PDELODF,X'80'     INDICATE PRESENT
         B     ADDRENT            GO HANDLE ENTRY NAME
         SPACE
*
*        HANDLE THE ENTRY NAME AFTER THE LOAD MODULE NAME.
*
ADDRNTRY BXH   R8,R6,ADDRBAD      SKIP PRECEDING DOT
         IC    R1,0(,R8)
         LA    R3,SCANTAB(R1)
         TM    0(R3),X'A0'        IS FIRST CHAR ALPHANATIONAL?
         BZ    ADDRBAD            NO, BAD ENTRY
ADDRENT  ST    R8,@START          SAVE ENTRY START
ADDRENTR TM    1(R9),X'40'        ASIS ADDR?
         BNZ   ADDRAS2            YES.
         OI    0(R8),X'40'        NO, FOLD THIS CHAR
ADDRAS2  BXH   R8,R6,ADDREEND
         IC    R1,0(,R8)
         LA    R3,SCANTAB(R1)
         TM    0(R3),X'E0'        ALPHANUMERIC | NATIONAL?
         BNZ   ADDRENTR           YES.
         TM    0(R3),X'11'        STOPPER?
         BNZ   ADDREEND           YES.
         CLI   0(R8),C'.'         DOT?
         BNE   ADDRBAD            NO, EVIL ENTRY
         SPACE
ADDREEND LR    R15,R8
         S     R15,@START         COMPUTE ENTRY LENGTH
         CH    R15,=H'8'          IS IT TOO LONG?
         BH    ADDRBAD            YES.
         MVC   @PDEENT,@START     SAVE ENTRY PART OF ADDRESS
         STH   R15,@PDEENTL
         MVI   @PDEENTF,X'80'
         CR    R8,R7              ANYTHING LEFT?
         BH    ADDRENDT           NO.
         TM    0(R3),X'11'        STOPPER AFTER?
         BNZ   ADDRENDT           YES.
         BXH   R8,R6,ADDRBAD      SKIP FOLLOWING DOT
         CLI   0(R8),C'+'         PLUS OR MINUS AFTER?
         BE    ADDRREL            YES, GO HANDLE RELATIVE PART
         CLI   0(R8),C'-'
         BE    ADDRREL
         IC    R1,0(,R8)
         LA    R3,SCANTAB(R1)
         TM    0(R3),X'A0'        IS CHAR AFTER ALPHANATIONAL?
         BZ    ADDRBAD            NO, ERROR
         B     ADDRSTRT           YES, DO SYMBOLIC SCAN AGAIN
         SPACE
*
*        THE ADDRESS IS AN UNQUALIFIED ENTRY NAME.
*
ADDRENDT MVI   @PDETYPE,X'04'     SET JUST-ENTRY FLAG
         B     ADDRDONE           AND RETURN HAPPY
         SPACE
*
*        VALIDATE AN ABSOLUTE ADDRESS.
*
ADDRABS  TM    @TYPES,X'84'       ANY NON-HEX CHARS IN ADDRESS?
         BNZ   ADDRBAD            YES, NO GOOD
         LR    R15,R8
         S     R15,@START         COMPUTE LENGTH OF ADDRESS
         BCTR  R15,0
         AIF   ('&OPSYS' NE 'MVS').NMVS9A
         CH    R15,=H'8'          IS IT TOO LONG?
         BH    ADDRBAD            YES.
         CLI   6(R9),13           MAYBE, EXTENDED ADDRESS?
         BE    ADDRAOK            YES, ITS OK
.NMVS9A  CH    R15,=H'6'          ONLY 6 DIGITS ALLOWED
         BH    ADDRBAD            ADDR TOO LONG
ADDRAOK  MVC   @PDEADR,@START     SET UP PDE FOR ABS ADDR
         STH   R15,@PDEADRL
         MVI   @PDEADRF,X'80'
         B     ADDRAFT            GO ALLOW FOR INDIRECTION
         SPACE
*
*        THE ADDRESS IS SYMBOLIC OR A REGISTER.
*
ADDREND  TM    @PDEENTF,X'80'     HAS AN ENTRY BEEN FOUND?
         BNZ   ADDRSYMB           YES, REGISTER IMPOSSIBLE
         TM    @TYPE1,X'A0'       WAS 1ST CHAR ALPHA?
         BNZ   ADDRSYMB           YES, REGISTER IMPOSSIBLE
         LR    R15,R8
         BCTR  R15,0
         MVI   @PDETYPE,X'10'     ASSUME D TYPE REG
         OI    0(R15),X'40'       FOLD REG TYPE
         CLI   0(R15),C'D'        D TYPE?
         BE    ADDRFREG           YES.
         MVI   @PDETYPE,X'08'     ASSUME E TYPE
         CLI   0(R15),C'E'        E TYPE?
         BNE   ADDRGREG           NO.
ADDRFREG S     R15,@START
         BCT   R15,ADDRBAD        MAKE SURE ONLY 1 DIGIT
         L     R14,@START
         TM    0(R14),X'09'       MAKE SURE EVEN REG<8
         BNZ   ADDRBAD
         MVC   @PDEADR,@START     SET UP REGISTER PDE
         MVI   @PDEADRL+1,1
         MVI   @PDEADRF,X'80'
         B     ADDRDONE
         SPACE
ADDRSYM  LR    R15,R8
         TM    @PDEENTF,X'80'     ENTRY ALREADY FOUND?
         BNZ   ADDRSYMB           YES, REGISTER IMPOSSIBLE
         TM    @TYPE1,X'A0'       CHAR 1 ALPHA?
         BNZ   ADDRSYMB           YES, REGISTER IMPOSSIBLE
         CLI   0(R8),C'%'         IS REGISTER INDIRECTED?
         BNE   ADDRBAD            NO, CAN'T DISPLACE FROM NAKED REG
         BCTR  R15,0              BACK UP 1 CHAR
ADDRGREG OI    0(R15),X'40'       FOLD LAST CHAR
         CLI   0(R15),C'R'        WAS IT R?
         BNE   ADDRBAD            NO, NOT A REGISTER
         S     R15,@START         YES.
         CH    R15,=H'2'          LENGTH>2
         BH    ADDRBAD            YES, NO GOOD
         BL    ADDRGPR            LESS, GOOD REGISTER
         L     R14,@START
         CLI   0(R14),C'1'        IS FIRST DIGIT OF REG 1?
         BNE   ADDRBAD            NO.
         CLI   1(R14),C'0'        VALIDATE SECOND CHAR
         BL    ADDRBAD
         CLI   1(R14),C'6'
         BNL   ADDRBAD
ADDRGPR  MVI   @PDETYPE,X'20'     SET GPR FLAG
         MVC   @PDEADR,@START     SET UP PDE
         STH   R15,@PDEADRL
         MVI   @PDEADRF,X'80'
         B     ADDRAFT            GO HANDLE INDIRECTION
         SPACE
ADDRSYMB IC    R1,0(,R8)
         LA    R3,SCANTAB(R1)
         LR    R15,R8
         S     R15,@START         COMPUTE LENGTH OF ADDRESS NAME
         CH    R15,=H'31'         TOO LARGE?
         BH    ADDRBAD            YES.
         MVC   @PDEADR,@START     STORE IN PDE
         STH   R15,@PDEADRL
         MVI   @PDEADRF,X'80'
         MVI   @PDETYPE,X'80'     SET TYPE TO SYMBOLIC
         SPACE
*
*        HANDLE ANY TRAILING PERCENT SIGNS OR RELATIVE DISPLACEMENTS.
*
ADDRAFT  LA    R10,@PDEADR
         USING @XPR,R10
ADDRNEXP CR    R8,R7              ANY TEXT LEFT?
         BH    ADDRDONE           NO.
         TM    0(R3),X'11'        YES, REACHED A STOPPER?
         BNZ   ADDRDONE           YES.
         LR    R15,R8             SAVE CURRENT PLACE
         SPACE
ADDRPCNT CLI   0(R8),C'%'         SKIP PERCENTS
         BNE   ADDRIND
         BXLE  R8,R6,ADDRPCNT
ADDRIND  SR    R15,R8             COMPUTE NUMBER OF %'S
         LCR   R15,R15
         CH    R15,=H'255'        TOO MANY?
         BH    ADDRFLOP           YES (GASP).
         STH   R15,@XPRPCNT       STORE IN PDE
         SPACE
         CR    R8,R7              ANY MORE TEXT?
         BH    ADDRDONE           NO.
         IC    R1,0(,R8)
         LA    R3,SCANTAB(R1)
         TM    0(R3),X'11'        REACHED A STOPPER?
         BNZ   ADDRDONE           YES.
         CLI   0(R8),C'+'         PLUS OR MINUS AFTER?
         BE    ADDREXPR
         CLI   0(R8),C'-'
         BNE   ADDRFLOP           IF NOT, BAD ADDRESS
         B     ADDREXPR
         SPACE
*
*        ENTER HERE FOR AN INITIAL RELATIVE ADDRESS.
*
ADDRREL  MVC   @PDESIGN,0(R8)     SAVE INITIAL SIGN
         BXH   R8,R6,ADDRBAD      SKIP SIGN
         LA    R10,@PDEADR        POINT R10->INITIAL PDE PART
         B     ADDRRELX           JOIN OTHER CASE
         SPACE
ADDREXPR MVC   @XPRSIGN,0(R8)     SAVE SIGN OF EXPRESSION
         BXH   R8,R6,ADDRFLOP     SKIP PAST
         LA    R1,@XPRSIZE        SIZE OF EXPRESSION PDE
         L     R15,=A(CLUZALOC)
         BALR  R14,R15            GET MEMORY FOR EXPRESSION PART
         L     R14,NEWMEM
         ST    R14,@XPRCHN        STORE IN CHAIN FIELD
         XC    0(@XPRSIZE-4,R14),0(R14)     CLEAR EXPRESSION
         LR    R10,R14            LET R10 ADDRESS EXPRESSION
         MVC   @XPRCHN,NULL       CUT OFF CHAIN
         SPACE
ADDRRELX ST    R8,@START          SAVE START POINT
         MVI   @TYPES,0           REINIT TYPE ACCUMULATOR
ADDRXNXT IC    R1,0(R8)
         LA    R3,SCANTAB(R1)
         TM    0(R3),X'13'        IS NEXT CHAR A STOPPER, %, + OR -?
         BNZ   ADDRXEND           YES, END OF EXPRESSION
         TM    0(R3),X'60'        IS IT ALPHAMERIC?
         BZ    ADDRFLOP           NO, BAD NEWS
         TM    0(R3),X'80'        YES, IS IT A HEX DIGIT?
         BNZ   ADDRXOUT           NO.
         OC    @TYPES,0(R3)       YES, ACCUMULATE TYPE BITS
         TM    1(R9),X'40'        ASIS ADDRESS?
         BNZ   ADDRXAS            YES.
         OI    0(R8),X'40'        NO, UPPERCASE DIGIT
ADDRXAS  BXLE  R8,R6,ADDRXNXT     AND CONTINUE
         SPACE
*
*        SEEMINGLY, WE HAVE FINISHED A RELATIVE DISPLACEMENT.
*
ADDRXEND LR    R15,R8
         S     R15,@START         COMPUTE DISPLACEMENT LENGTH
         BZ    ADDRFLOP           0 IS EVIL
         AIF   ('&OPSYS' NE 'MVS').NMVS9B
         CH    R15,=H'8'          >8 IS VERY EVIL
         BH    ADDRFLOP
         CLI   6(R9),13           EXTENDED ADDRESS?
         BE    ADDRXTND           YES, LENGTH OK
.NMVS9B  CH    R15,=H'6'          >6 IS ALSO EVIL
         BH    ADDRFLOP
ADDRXTND TM    @PDEADRF,X'80'     HAS ADDRESS BEEN HANDLED?
         BZ    ADDRRHEX           NO, RELATIVE ADDRESS
ADDRXHEX MVI   @XPRTYPE,X'02'     SET EXPRESSION TYPE
ADDRXSTO MVC   @XPRADDR,@START    STORE EXPRESSION START ADDR
         STH   R15,@XPRLEN        AND LENGTH
         B     ADDRNEXP
         SPACE
ADDRRHEX CLI   @PDESIGN,C'-'      NEGATIVE DISPLACEMENT
         BE    ADDRXADD           YES, GO SNEAK IN A +0
         MVI   @PDETYPE,X'40'     NO, INDICATE RELATIVE ADDRESS
         MVC   @PDEADR,@START     SET UP PDE
         STH   R15,@PDEADRL
         MVI   @PDEADRF,X'80'
         B     ADDRNEXP
         SPACE
ADDRXOUT C     R8,@START          DID WE GET ANY TEXT?
         BE    ADDRFLOP           NO.
         OI    0(R8),X'40'        YES, FOLD THIS CHAR
         CLI   0(R8),C'N'         WAS IT AN N?
         BNE   ADDRFLOP           NO, NO GOOD
         TM    @TYPES,X'20'       YES, ANY LETTERS IN EXPRESSION?
         BNZ   ADDRFLOP           YES, NO GOOD
         OI    @TYPES,X'80'       NO, SET FLAG FOR LATER
         BXH   R8,R6,ADDRXDEC     SKIP N
         IC    R1,0(,R8)
         LA    R3,SCANTAB(R1)
         TM    0(R3),X'13'        DOES A STOPPER FOLLOW?
         BZ    ADDRFLOP           NO, ERROR
ADDRXDEC LR    R15,R8
         S     R15,@START         COMPUTE LENGTH
         BCTR  R15,0
         TM    @PDEADRF,X'80'     ADDRESS ALREADY HANDLED?
         BNZ   ADDRDEC            YES.
         SPACE
*
*        ADD A +0 IN FRONT OF THE EXPRESSION FOR THE EXTENSIONS TO THE
*        IBM SYNTAX.
*
ADDRXADD LR    R0,R15             SAVE LENGTH
         LA    R1,4+@XPRSIZE      MEMORY NEEDED FOR FAKE-OUT
         L     R15,=A(CLUZALOC)
         BALR  R14,R15            GET MEMORY
         L     R10,NEWMEM
         LA    R15,@XPRSIZE(,R10) FAKE INPUT
         MVC   0(2,R15),=C'+0'    OF +0
         LA    R15,1(,R15)
         ST    R15,@PDEADR
         MVI   @PDEADRL+1,1
         MVI   @PDEADRF,X'80'
         MVI   @PDETYPE,X'40'
         CLI   @PDESIGN,C'-'
         BE    ADDRXCHN
         MVI   @PDESIGN,C'+'      SIGN IS + IF NOT ALREADY -
ADDRXCHN LR    R15,R0             RESTORE R15
         ST    R10,@PDEXPR        CHAIN NEW MEMORY TO OLD
         XC    0(@XPRSIZE-4,R10),0(R10)     CLEAR IT
         MVC   @XPRCHN,NULL       STOP THE CHAIN
         TM    @TYPES,X'80'       DECIMAL EXPRESSION?
         BZ    ADDRXHEX           NO, HEX
ADDRDEC  EQU   *
         AIF   ('&OPSYS' NE 'MVS').NMVS9C
         CH    R15,=H'10'         TOO MANY DIGITS?
         BH    ADDRFLOP           MUCH TOO MUCH
         CLI   6(R9),13           MAYBE, EXTENDED ADDRESS FORMAT?
         BE    ADDRDOK            YES, ITS GOOD
.NMVS9C  CH    R15,=H'6'          TOO MANY DIGITS?
         BH    ADDRFLOP           YES.
ADDRDOK  MVI   @XPRTYPE,X'04'     SET TYPE TO DECIMAL
         B     ADDRXSTO           GO STORE INTO PDE
         SPACE
*
*        WHEW. RETURN THE PDE.
*
ADDRDONE MVC   0(36,R2),@PDE      MOVE TO REAL PDE
         SR    R15,R15            SET GOOD RETURN CODE
         B     RETURN
         SPACE
*
*        AN ERROR WAS DISCOVERED IN AN EXPRESSION. CALL THE ROLLBACK
*        ROUTINE TO FREE ANY EXPRESSION PDE'S BEFORE RETURNING.
*
ADDRFLOP BAL   R14,RLBK           CALL MEMORY ROLLBACK ROUTINE
         B     ADDRBAD            AND THEN QUIT
         SPACE
         DROP  R10
         SPACE
@DSA     DSECT ,                  ADDRESS WORK AREA
         SPACE
         DS    22A                SAVE AREA
@START   DS    A                  STARTING ADDRESS
@PDE     EQU   *                  PDE MAPPING
@PDELOD  DS    A
@PDELODL DS    H
@PDELODF DS    X
@PDEENT  DS    A
@PDEENTL DS    H
@PDEENTF DS    X
@PDEADR  DS    A
@PDEADRL DS    H
@PDEADRF DS    X
         DS    X
@PDETYPE DS    X
@PDESIGN DS    CL1
@PDEPCNT DS    H
@PDEXPR  DS    A
         DS    A
@TYPE1   DS    X                  TYPE OF FIRST CHAR
@TYPES   DS    X                  CUMULATIVE TYPE BITS
         DS    0D
         SPACE
@LEN     EQU   *-@DSA             LENGTH OF WORK AREA
         SPACE
@XPR     DSECT ,                  EXPRESSION PDE MAPPING
         SPACE
@XPRADDR DS    A
@XPRLEN  DS    H
         DS    H
@XPRTYPE DS    X
@XPRSIGN DS    CL1
@XPRPCNT DS    H
@XPRCHN  DS    A
         SPACE
@XPRSIZE EQU   *-@XPR
         SPACE
CLUTSRC2 CSECT ,                  RESTORE CSECT
         AIF   ('&OPSYS' NE 'MVS').NMVS7
         TITLE 'CLUZJOBN -- JOBNAME RECOGNIZER'
*
*        THIS ROUTINE RECOGNIZES THE JOBNAME(JOBID) SYNTAX
*
CLUZJOBN LIENTRY DSA=*,DSALEN=88,ENV=R5
         LR    R14,R8             SAVE STARTING POINT
         SR    R1,R1
         IC    R1,0(,R8)
         LA    R3,SCANTAB(R1)     EXAMINE FIRST CHAR
         TM    0(R3),X'A0'        IS IT ALPHA?
         BNZ   JOBHERE            YES.
         LA    R15,4              NO, JOBNAME IS MISSING
         B     RETURN
         SPACE
JOBLOOP  BXH   R8,R6,JOBGOOD      ON TO NEXT CHAR
         IC    R1,0(,R8)
         LA    R3,SCANTAB(R1)
         TM    0(R3),X'E0'        IS IT ALPHANUMERIC | NATIONAL?
         BZ    JOBEND             NO, END OF JOBNAME
JOBHERE  TM    1(R9),X'40'        ASIS?
         BNZ   JOBLOOP            YES, PROCEED
         OI    0(R8),X'40'        NO, TRANSLATE THIS CHAR
         B     JOBLOOP            AND CONTINUE
         SPACE
JOBEND   TM    0(R3),X'18'        SEPARATOR?
         BNZ   JOBGOOD            YES, GO STORE IN PDE
         SPACE
JOBBAD   BXH   R8,R6,JOBOUCH
         IC    R1,0(,R8)
         LA    R3,SCANTAB(R1)
         CLI   0(R8),C'('         LOOK FOR JOBID START
         BE    JOBSKPID
         TM    0(R3),X'18'        HAVE WE A STOP CHAR?
         BZ    JOBBAD             NO, CONTINUE SEARCH
JOBOUCH  LA    R15,8              SET BAD RETURN CODE
         B     RETURN
         SPACE
JOBGOOD  LR    R15,R8
         SR    R15,R14            COMPUTE JOBNAME LENGTH
         CH    R15,=H'8'          IS IT TOO LONG?
         BH    JOBBADI            YES.
         ST    R14,0(,R2)         NO, SET UP PDE
         STH   R15,4(,R2)
         CR    R8,R7              ARE WE OUT OF INPUT?
         BH    JOBRET             YES, RETURN
         CLI   0(R8),C'('         NO, IS THERE A FOLLOWING JOBID?
         BE    JOBIDHR            YES, HANDLE IT
JOBRET   MVI   6(R2),X'80'
         SR    R15,R15
         B     RETURN             AND RETURN
         SPACE
*
*        HANDLE THE JOBID PORTION
*
JOBIDHR  BXH   R8,R6,JOBOUCH      ERROR IF NOTHING AFTER PAREN
         LR    R14,R8             SAVE ID START POINT
JOBILOOP IC    R1,0(,R8)
         LA    R3,SCANTAB(R1)
         TM    0(R3),X'E0'        IS NEXT CHAR ALPHANUMERIC | NATIONAL?
         BZ    JOBIEND            NO, END OF JOB ID
         TM    1(R9),X'40'        YES, ASIS?
         BNZ   JOBIASIS           YES.
         OI    0(R8),X'40'        NO, TRANSLATE IT
JOBIASIS BXLE  R8,R6,JOBILOOP     AND CONTINUE
         LR    R15,R8
         B     JOBIOVER
         SPACE
JOBIEND  LR    R15,R8             SAVE ENDING POINT
         TM    0(R3),X'18'        FOUND A SEPARATOR?
         BZ    JOBSKPID           NO, JOBID IS EVIL
         CLI   0(R8),C')'         FOUND A CLOSE PAREN?
         BNE   JOBIOVER           NO.
         AR    R8,R6              YES, SKIP IT
JOBIOVER SR    R15,R14            COMPUTE LENGTH OF JOBID
         BZ    JOBOUCH            ERROR IF 0
         CH    R15,=H'8'          ERROR IF > 8
         BH    JOBOUCH
         CLI   0(R14),C'0'        IS FIRST CHAR A DIGIT?
         BNL   JOBNUM             YES, CHANGE JOB NUMBER TO JOB ID
         ST    R14,8(,R2)         STORE LOCATION & LENGTH OF ID
         STH   R15,12(,R2)
         MVI   14(R2),X'80'       NOTE ID PRESENT
         B     JOBRET             RETURN NORMALLY
         SPACE
JOBBADI  CR    R8,R7              ANYTHING MORE LEFT?
         BH    JOBOUCH            NO, JUST GIVE UP
         CLI   0(R8),C'('         IS THERE AN ID AFTER BAD JOBNAME?
         BNE   JOBOUCH            NO, TAKE FAST EXIT
JOBSKPID BXH   R8,R6,JOBOUCH      LOOK FOR END OF JOBID
JOBIBAD  IC    R1,0(,R8)
         LA    R3,SCANTAB(R1)
         CLI   0(R8),C')'         STOP AT CLOSE PAREN
         BE    JOBBOVER
         TM    0(R3),X'18'        STOP AT NON-DELIMITER
         BZ    JOBSKPID
         B     JOBOUCH            ELSE KEEP GOING
JOBBOVER AR    R8,R6              SKIP THE CLOSE PAREN
         B     JOBOUCH            AND GIVE UP
         SPACE
*
*        IF THE JOB ID BEGINS WITH A NUMERIC CHARACTER, AS AN
*        EXTENSION WE PREFIX IT WITH THE WORD "JOB" AND RETURN
*        THAT
*
JOBNUM   CH    R15,=H'5'          TOO LONG TO ADD "JOB"?
         BH    JOBOUCH            YES, ERROR
         LR    R3,R14             SAVE JOBID LOCATION
         LR    R10,R15            AND LENGTH
         LA    R1,8               GET 8 BYTES FOR CONSTRUCTED JOBID
         L     R15,=A(CLUZALOC)
         BALR  R14,R15
         L     R1,NEWMEM          FIND WHERE WE GOT IT
         MVC   0(3,R1),=C'JOB'    PUT IN CONSTANT PART
         BCTR  R10,0
         EX    R10,JOBMVNUM       MOVE JOB NUMBER AFTER
         ST    R1,8(,R2)          STORE JOB ID LOCATION
         LA    R10,4(,R10)        COMPUTE FULL JOB ID LENGTH
         STH   R10,12(,R2)        STORE
         MVI   14(R2),X'80'       SHOW JOB ID PRESENT
         B     JOBRET             AND RETURN
         SPACE
JOBMVNUM MVC   3(0,R1),0(R3)      COPY JOB NUMBER TO JOB ID
.NMVS7   TITLE 'CLUZEXTN -- USER RECOGNIZER INTERFACE'
*
*        THIS ROUTINE PROVIDES AN INTERFACE TO A USER-WRITTEN
*        RECOGNIZER. THE RECOGNIZER IS PASSED THE CURRENT SOURCE
*        PARAMETER PROVIDED IN THE PCE. IT IS ALSO PASSED THE ADDRESS
*        OF THE UPPER-CASE TABLE FOR A NON-ASIS PARM, AND THE ADDRESS
*        OF A ROUTINE TO GET DYNAMIC STORAGE.
*
CLUZEXTN LIENTRY DSA=XTNDSA,DSALEN=XTNLEN,ENV=R5,PARM=R2
         ST    R2,VPDE            STORE PDE POINTER
         MVC   VMSG,NULL          INITIALIZE 2ND LEVEL MSG
         L     R14,=V(CLUTSCAP)   FIND UPPER CASE TABLE
         TM    1(R9),X'40'        IS IT ASIS?
         BZ    EXTNFOLD           NO.
         SR    R14,R14            YES, PASS 0 INSTEAD
EXTNFOLD ST    R14,CAPTAB
         BAL   R14,FDEF           FIND DEFAULT OR PROMPT
         BAL   R14,FHLP           FIND ANY HELP
         TM    0(R9),X'02'        IS THERE ANY?
         BZ    EXTNNHLP           NO.
         MVC   ALIGN,0(R1)        YES, SKIP IT
         AH    R1,ALIGN
EXTNNHLP ST    R1,RPARM           STORE PCE PARM ADDRESS
         SPACE
*
*        TURN THE CURRENT INPUT INTO A PSEUDO-COMMAND-BUFFER FOR
*        PROCESSING BY THE USER RECOGNIZER.
*
         CLC   CBUF,CURINP        IN ORIGINAL INPUT?
         BE    EXTNCBUF           YES.
         L     R1,CURINP          NO, FIND CURRENT INPUT
         LA    R15,INPLEN-5(,R1)  POINT TO START OF PSEUDO-CBUF
         LR    R14,R7
         SR    R14,R15            COMPUTE CBUF LENGTH
         STH   R14,1(,R15)        STORE IT
         B     EXTNOFF            GO TO COMPUTE OFFSET
EXTNCBUF L     R15,CBUF           FIND ORIGINAL COMMAND BUFFER
EXTNOFF  LR    R14,R8
         SR    R14,R15            DETERMINE OFFSET
         S     R14,=A(4)          COMPENSATE FOR HEADER
         STH   R14,2(,R15)
         ST    R15,TEXTSEG        STORE PSEUDO-CBUF OFFSET
         SPACE
*
*        BEFORE CALLING THE RECOGNIZER, POINT R13 TO ANOTHER SAVE AREA.
*        THIS ALLOWS USE OF OUR ORIGINAL DSA IF RE-ENTERED FOR A
*        STORAGE MANAGEMENT REQUEST.
*
         LA    R1,XTNGET          SET UP GET ROUTINE ADDRESS
         ST    R1,GETRTN
         MVC   XTNGET(EXTNGLEN),EXTNCODE    COPY GET HEADER CODE
         LA    R1,VPDE            POINT TO RECOGNIZER PARM LIST
         ST    R13,XTNCSAVE+4     SET LINKAGE BACK TO OWN DSA
         STM   R14,R12,XTNCSAVE+12     SAVE REGS IN CASE USER FORGETS
         LA    R13,XTNCSAVE
         MVC   VRTN+1(3),6(R9)    MOVE ENTRY POINT
         L     R15,VRTN
         BALR  R14,R15            CALL USER RECOGNIZER
         L     R13,4(,R13)        RESTORE ORIGINAL DSA
         SPACE
*
*        INTERPRET THE RETURN CODE. IF THE ITEM IS NOT MISSING,
*        COMPUTE THE NEW CURRENT SCAN POINTER BEFORE RETURNING.
*
         LTR   R15,R15
         BNM   EXTNROK            MAKE SURE RETURN CODE DECENT
EXTNOUCH BAL   R6,PARSRC12
EXTNROK  CH    R15,=H'8'          MAXIMUM ALLOWED
         BH    EXTNOUCH
         BE    EXTNRLBK           ROLLBACK IF ITEM IS INVALID
         XC    VMSG,VMSG          WIPE OUT LEVEL 2 TEXT OTHERWISE
         LTR   R15,R15            GOOD INPUT?
         BZ    EXTNRPOS           YES, REPOSITION
EXTNRLBK BAL   R14,RLBK           FREE ANY MEMORY GOTTEN BY RECOGNIZER
         CH    R15,=H'4'          WAS ITEM MISSING?
         BE    RETURN             YES, NO REPOSITION REQUIRED
         SPACE
EXTNRPOS L     R14,TEXTSEG        FIND PSEUDO-BUFFER
         LA    R8,4(,R14)
         AH    R8,2(,R14)         COMPUTE NEW POSITION
         B     RETURN             AND RETURN R15
         SPACE
*
*        THIS CODE IS MOVED TO THE DSA BEFORE THE USER RECOGNIZER IS
*        CALLED. IT SWITCHES SAVE AREAS (TO RESTORE THIS ROUTINE'S
*        ORIGINAL DSA), AND THEN BRANCHES BACK TO MAINLINE CODE.
*
EXTNCODE EQU   *                  START OF MOVED CODE
         USING XTNGET,R15         R15->MOVED COP OF CODE
         STM   R0,R15,XTNLSAVE    SAVE CALLER'S REGS
         L     R13,XTNCSAVE+4     FIND ORIGINAL DSA
         LM    R14,R12,XTNCSAVE+12     LOAD ORIGINAL REGS
         DROP  R15                FORGET TEMPORARY BASE
         B     EXTNGET            RETURN TO REAL WORLD
EXTNGLEN EQU   *-EXTNCODE         LENGTH OF MOVED GET CODE
         SPACE
*
*        THIS CODE ACTUALLY CALLS THE GET ROUTINE FOR A USER
*        RECOGNIZER REQUEST.
*
EXTNGET  L     R1,XTNLSAVE+4      FIND OUR PARM
         LA    R1,7(,R1)          ROUND AMOUNT UP TO DOUBLE WORD
         N     R1,=F'-8'
         L     R15,=A(CLUZALOC)
         BALR  R14,R15            GO GET MEMORY
         MVC   XTNLSAVE+4(4),NEWMEM    RETURN ADDRESS IN R1
         LM    R0,R15,XTNLSAVE    RESTORE CALLER'S REGS
         BR    R14                AND GO BACK TO HIM
         SPACE 3
XTNDSA   DSECT ,                  WORK AREA FOR EXTENSION INTERFACE
         SPACE
         DS    22A                NORMAL DSA
XTNGET   DS    CL(EXTNGLEN)       GET ROUTINE HEADER CODE
XTNCSAVE DS    18A                SAVE AREA FOR RECOGNIZER USE
XTNLSAVE DS    16A                SAVE AREA FOR GET ROUTINE
         DS    0D
XTNLEN   EQU   *-XTNDSA           LENGTH OF DSA
         TITLE 'CLUZWORD -- RESERVED WORD HANDLER'
CLUTSCOB LISECT ,                 COBOL RECOGNIZERS
*
*        THIS ROUTINE HANDLES RECOGNITION OF RESERVED WORDS
*
CLUZWORD LIENTRY DSA=WDSA,DSALEN=WDSALEN,ENV=R5,PARM=R2
         SR    R1,R1
         LR    R15,R8             SAVE STARTING POINT
         SPACE
*
*        SCAN TO THE NEXT SEPARATOR
*
WORDLOOP IC    R1,0(,R8)
         LA    R3,COBTAB(R1)      CONSIDER NEXT CHAR
         TM    0(R3),X'11'        IS IT A SEPARATOR?
         BNZ   WORDEND            YES, THE END
         BXLE  R8,R6,WORDLOOP     NO, CONTINUE
         SPACE
*
*        COPY THE WORD TO DYNAMIC MEMORY AND TRANSLATE IT TO UPPER
*        CASE
*
WORDEND  LR    R10,R8
         SR    R10,R15            GET LENGTH OF WORD
         BZ    WORDMISS           MISSING IF STARTS WITH SEPARATOR
         CH    R10,=H'255'        TOO LONG?
         BH    WORDERR            YES.
         BCTR  R10,0
         L     R14,=V(CLUTSCAP)   UPPER CASE TABLE
         EX    R10,WORDMOVE       MOVE WORD TO WORK AREA
         EX    R10,WORDFOLD       UPPER CASE IT
         SR    R14,R14
         LR    R0,R9              SAVE R9 ELSEWHERE
         SPACE
WORDNEXT MVC   ALIGN,2(R9)
         AH    R9,ALIGN           FIND NEXT IKJNAME PCE
         AR    R14,R6
         IC    R1,0(,R9)
         SRL   R1,5
         CH    R1,=H'3'           IS IT AN IKJNAME?
         BNE   WORDMISS           NO, WORD NOT FOUND
         EX    R10,WORDCLEN       YES, COMPARE LENGTH-1'S
         BNE   WORDNEXT           NOPE, TRY THE NEXT
         EX    R10,WORDCOMP       OK, COMPARE THE WORDS
         BNE   WORDNEXT           NOPE
         SPACE
WORDOK   STH   R14,2(,R2)         SAVE WORD NUMBER
         MVI   6(R2),X'80'        SET PRESENT FLAG
         EX    R10,WORDREST       PUT THE FOLDED WORD BACK
         SR    R15,R15
         B     RETURN
         SPACE
WORDERR  LA    R15,8              AND REJECT
         B     RETURN
         SPACE
WORDMISS LR    R8,R15             RESTORE ORIGINAL CHAR POINTER
         LA    R15,4
         B     RETURN
         SPACE
WORDMOVE MVC   WORDSAVE(0),0(R15) SAVE PUTATIVE WORD
WORDFOLD TR    WORDSAVE(0),0(R14) FOLD PUTATIVE WORD
WORDREST MVC   0(0,R15),WORDSAVE  PUT WORD BACK (FOLDED)
WORDCLEN CLI   4(R9),0            COMPARE LENGTH-1'S
WORDCOMP CLC   5(0,R9),WORDSAVE   COMPARE WORD & NAME
         SPACE
WDSA     DSECT ,                  IKJRSVWD WORK AREA
         SPACE
         DS    22F                DSA
WORDSAVE DS    CL255              FOLDING AREA
         DS    0D
WDSALEN  EQU   *-WDSA             LENGTH OF WORK AREA
         SPACE
CLUTSCOB CSECT ,                  RESUME CSECT
         TITLE 'CLUZSTMT -- COBOL STATEMENT NUMBER RECOGNIZER'
*
*        THIS ROUTINE RECOGNIZES COBOL STATEMENT NUMBERS.
*
CLUZSTMT LIENTRY ENV=R5,PARM=R2,DSA=*,DSALEN=88
         SR    R1,R1
         LR    R15,R8             SAVE THE STARTING POINT
         SR    R14,R14            INDICATE NO ERROR FOUND YET
         IC    R1,0(,R8)
         LA    R3,COBTAB(R1)      INSPECT 1ST CHAR
         TM    0(R3),X'40'        IS IT A DIGIT?
         BNZ   STMTNXLN           YES.
         TM    0(R3),X'20'        IS IT A LETTER?
         BNZ   STMTPGM            YES.
         LA    R15,4              NO, STATEMENT NUMBER NOT THERE
         B     RETURN
         SPACE
STMTPGM  L     R10,=V(CLUTSCAP)   TO FOLD THE PROGRAM NAME
STMTPLOP TM    1(R9),X'40'        ARE WE ASIS?
         BNZ   STMTPGAS           YES.
         TR    0(1,R8),0(R10)     NO, FOLD THE CHARACTER
STMTPGAS BXH   R8,R6,STMTERR      CONTINUE ON
         IC    R1,0(,R8)
         LA    R3,COBTAB(R1)
         TM    0(R3),X'08'        FOUND A .?
         BNZ   STMTPDON           YES, PROGRAM NAME ENDED
         TM    0(R3),X'60'        FOUND A LETTER OR DIGIT?
         BNZ   STMTPLOP           YES, MORE PGM NAME
         TM    0(R3),X'11'        NO, FOUND A TERMINATOR?
         BNZ   STMTERR            YES, NOT GOOD
         LA    R14,1              SET ERROR FLAG
         B     STMTPGM            & CONTINUE PERIOD SEARCH
         SPACE
STMTERR  LA    R15,8              INDICATE ERROR
         B     RETURN             AND RETURN
         SPACE
STMTPDON BXH   R8,R6,STMTERR      IS THERE ANYTHING AFTER?
         LR    R0,R8
         SR    R0,R15
         BCTR  R0,0               COMPUTE LENGTH OF PROGRAM ID
         CH    R0,=H'8'           IS IT TOO BIG?
         BNH   STMTPLEN           NO.
         LA    R14,1              YES, INDICATE ERROR
         B     STMTAPGM
STMTPLEN STC   R0,0(,R2)          STORE PROGRAM LEN IN PDE
         ST    R15,8(,R2)         STORE THE STARTING POINT
         SPACE
STMTAPGM LR    R15,R8             SAVE START OF LINE NUMBER
STMTLINE IC    R1,0(,R8)
         LA    R3,COBTAB(R1)
         TM    0(R3),X'08'        HAVE WE A DOT?
         BNZ   STMTLDON           YES, FINISH WITH LINE NO
         TM    0(R3),X'11'        FOUND A TERMINATOR?
         BNZ   STMTLDON           YES.
         TM    0(R3),X'40'        NO, GOT A DIGIT?
         BNZ   STMTNXLN           YES.
         TM    0(R3),X'20'        NO, A LETTER?
         BZ    STMTLDON           NO, END OF STMT NUMBER
         LA    R14,1              YES, SET ERROR FLAG
STMTNXLN BXLE  R8,R6,STMTLINE
         B     STMTLDON           IF ALL THE TEXT RUNS OUT
         SPACE
STMTLDON LTR   R14,R14            ANY TROUBLE SO FAR?
         BNZ   STMTLAFT           YES, DON'T STORE ANYTHING
         LR    R0,R8
         SR    R0,R15             COMPUTE LINE NUMBER LENGTH
         CH    R0,=H'6'           IS IT TOO LONG?
         BNH   STMTLNOK           NO.
         LA    R14,1              YES, INDICATE ERROR
         B     STMTLAFT
STMTLNOK STC   R0,1(,R2)          STORE LENGTH
         ST    R15,12(,R2)        AND LOCATION
         SPACE
STMTLAFT CR    R8,R7              IS ANYTHING LEFT?
         BH    STMTRET            NO, WE'RE DONE
         CLI   0(R8),C'.'         ARE WE LOOKING AT A PERIOD?
         BNE   STMTRET            NO, THE NUMBER IS ENDED
         BXH   R8,R6,STMTERR      YES, SKIP PAST IT
         LR    R15,R8             SAVE THE START
         IC    R1,0(,R8)
         LA    R3,COBTAB(R1)
         TM    0(R3),X'40'        IS IT A DIGIT?
         BZ    STMTVERR           NO, BAD VERB NUMBER
         BXH   R8,R6,STMTVOK      OK IF NOTHING AFTER
         IC    R1,0(,R8)          YES, WHAT FOLLOWS
         LA    R3,COBTAB(R1)
         TM    0(R3),X'68'        LETTER DIGIT OR DOT AFTER?
         BNZ   STMTVERR           YES, BAD VERB NO
STMTVOK  MVI   2(R2),1            STORE LENGTH OF 1 FOR VERB
         ST    R15,16(R2)         STORE WHEREABOUTS OF VERB
         B     STMTRET
         SPACE
STMTVERR BXH   R8,R6,STMTERR      SCAN LIKELY REST OF TEXT
         IC    R1,0(,R8)
         LA    R3,COBTAB(R1)
         TM    0(R3),X'68'        LOOK FOR NON-ALPHAMERIC OR .
         BNZ   STMTVERR
         B     STMTERR
         SPACE
STMTRET  LTR   R14,R14            ANY TROUBLE?
         BNZ   STMTERR            YES, GIVE RET CODE 8
         OI    6(R2),X'90'        NO, SET PRESENT & STMT FLAGS
         SR    R15,R15
         B     RETURN             AND GO BACK
         TITLE 'CLUZCNST -- COBOL CONSTANT RECOGNIZER'
*
*        THIS ROUTINE RECOGNIZES COBOL CONSTANTS. IT USES THE RESERVED
*        WORD RECOGNIZER IF FIGURATIVE CONSTANTS ARE ALLOWED. THIS
*        ROUTINE IS ALSO ENTERED BY THE QUALIFIER RECOGNIZER FOR
*        A TERM WHICH MAY BE EITHER A CONSTANT OR A VARIABLE.
*
CLUZCNST LIENTRY ENV=R5,PARM=R2,DSA=COBDSA,DSALEN=COBLEN
         SR    R1,R1
         LR    R15,R8             SAVE STARTING POINT
         MVI   CUMCHARS,X'00'     INITIALIZE ACCUMULATOR
         IC    R1,0(,R8)          CONSIDER THE FIRST CHAR
         LA    R3,COBTAB(R1)
         TM    0(R3),X'20'        IS IT A LETTER?
         BNZ   CNSTFIG            YES, CHECK FOR FIGURATIVE
CNSTLIT  CLI   0(R8),C''''        IS IT A NON-NUMERIC LITERAL?
         BE    CNSTQUOT           SEEMS TO BE
         MVC   CUMCHARS,0(R3)     NO, SAVE CHARACTER TYPE
         TM    0(R3),X'04'        IS IT A MINUS?
         BNZ   CNSTSIGM           YES.
         TM    0(R3),X'02'        IS IT A PLUS?
         BNZ   CNSTSIGP           YES.
         TM    0(R3),X'08'        IS IT A DOT?
         BNZ   CNSTDOT            YES.
         TM    0(R3),X'40'        IS IT A DIGIT?
         BNZ   CNSTNUM            YES.
         SPACE
CNSTMISS LA    R15,4              INDICATE CONSTANT IS MISSING
         B     RETURN             AND RETURN
         SPACE
*
*        THIS CODE INTERFACES TO CLUZWORD TO FIND A FIGURATIVE
*        CONSTANT.
*
CNSTFIG  TM    1(R9),X'08'        IS A FIGURATIVE ALLOWED?
         BZ    CNSTMFIG           NOPE.
         BAL   R14,FDEF           YES, FIND THE DEFAULT
         LR    R14,R1
         TM    0(R9),X'18'        IS THERE ANY?
         BZ    CNSTFNOD           NO.
         SR    R14,R14
         IC    R14,0(,R1)         YES, SKIP OVER IT
         LA    R14,2(R14,R1)
CNSTFNOD TM    1(R9),X'10'        SUBSCRIPT ALLOWED?
         BZ    CNSTFNOS           NO.
         LA    R14,2(,R14)        YES, SKIP SUBSCRIPT OFFSET
CNSTFNOS MVC   ALIGN,0(R14)       GET OFFSET OF IKJRSVWD
         LH    R14,ALIGN
         A     R14,PCL            FIND IT
         LR    R10,R9             SAVE ORIGINAL PCE ADDRESS
         LR    R9,R14
         MVC   ALIGN(1),0(R9)
         NI    ALIGN,X'E0'
         CLI   ALIGN,X'A0'        IS IT REALLY A RSVWD?
         BE    CNSTFOK            YES.
CNSTFNOK BAL   R6,PARSRC24        NO, GIVE RETURN CODE
CNSTFOK  TM    1(R9),X'80'        IS IT THE RIGHT KIND OF RSVWD?
         BZ    CNSTFNOK           NO.
         LA    R15,CLUZWORD       YES, FIND RESERVED WORD ROUTINE
         LR    R1,R2
         BALR  R14,R15            RECOGNIZE RESERVED WORD
         LR    R9,R10             RESTORE PCE POINTER
         B     *+4(R15)           ACT ON RETURN CODE
         B     CNSTYFIG           GOT A WORD
         B     CNSTMFIG           NOT A WORD
         B     RETURN             ERROR, JUST PASS IT ON
         SPACE
CNSTYFIG MVC   4(2,R2),2(R2)      REFORMAT THE PDE
         XC    2(2,R2),2(R2)
         OI    6(R2),X'C2'        SET FIGURATIVE FLAGS
         SR    R15,R15            AND RETURN HAPPY
         B     RETURN
         SPACE
CNSTMFIG TM    6(R9),X'10'        IS THIS AN ANY?
         BNZ   QUALNFIG           YES, GO TRY AS VARIABLE
         B     CNSTMISS           NO, SAY CONSTANT IS MISSING
         SPACE
*
*        THIS CODE RECOGNIZES A QUOTED LITERAL (COBOL STYLE).
*
CNSTQUOT BXH   R8,R6,CNSTERR      ERROR IF NOTHING AFTER THE QUOTE
         LR    R15,R8             SAVE FIRST CHAR ADDR
         L     R14,=V(CLUTSCAP)
CNSTQLOP CLI   0(R8),C''''        FOUND THE CLOSE QUOTE?
         BE    CNSTQEND           YES.
         TM    1(R9),X'40'        NO, IS THIS ASIS?
         BNZ   CNSTQAS            YES.
         TR    0(1,R8),0(R14)     NO, UPPER CASE THE VALUE
CNSTQAS  BXLE  R8,R6,CNSTQLOP     CONTINUE TILL NOTHING LEFT
         SR    R14,R14            INDICATE NO QUOTE FOUND
         B     CNSTQUP
         SPACE
CNSTQEND LA    R14,1              INDICATE CLOSE QUOTE FOUND
CNSTQUP  LR    R0,R8
         AR    R8,R14
         SR    R0,R15             COMPUTE STRING LENGTH
         BZ    CNSTERR            NULL STRING NOT ALLOWED
         CH    R0,=H'120'         CHECK FOR TOO LONG
         BH    CNSTERR
         STC   R0,0(,R2)          SAVE LENGTH OF LITERAL
         MVI   6(R2),X'C4'        INDICATE NON-NUMERIC
         ST    R15,8(,R2)         STORE STARTING POINT
         LTR   R14,R14            WAS THE STRING CLOSED?
         BNZ   CNSTCLOS           YES.
         BAL   R14,UNCL           NO, MAKE RUDE COMMENT
CNSTCLOS SR    R15,R15            THEN SET RETURN CODE
         B     RETURN             AND GO BACK
         SPACE
CNSTERR  LA    R15,8
         B     RETURN             AND RETURN
         SPACE
*
*        THIS CODE RECOGNIZES (FIXED- AND FLOATING-POINT) NUMERIC
*        LITERALS
*
CNSTSIGM OI    7(R2),X'80'        INDICATE SIGN IS MINUS
CNSTSIGP BXH   R8,R6,CNSTERR      SKIP PAST THE SIGN
         LR    R15,R8             SAVE TRUE STARTING POINT
CNSTNEXT IC    R1,0(,R8)
         LA    R3,COBTAB(R1)
         TM    0(R3),X'08'        HAVE WE A DECIMAL POINT?
         BNZ   CNSTDOT            YES.
         TM    0(R3),X'40'        NO, GOT A DIGIT?
         BNZ   CNSTNUM            YES.
         EX    R1,CNSTSEEE        SEE IF WE HAVE AN E
         BE    CNSTNDON           YES, PROBABLY DONE WITH NUMBER
         TM    0(R3),X'24'        NO, GOT A LETTER OR A MINUS?
         BNZ   CNSTPERR           YES, PROBABLE ERROR
         TM    0(R3),X'02'        GOT A PLUS?
         BZ    CNSTNDON           NO, FOUND CONSTANT END
         B     CNSTNELP           YES, GO SCAN FOR END OF ERROR
CNSTSEEE CLI   =C'E',X'40'        LOOK FOR UPPER OR LOWER CASE E
         SPACE
CNSTNUM  OC    CUMCHARS,0(R3)     KEEP TRACK OF CHARACTER TYPE
         BXLE  R8,R6,CNSTNEXT     CONTINUE WHILE TEXT LEFT
         B     CNSTNDON
         SPACE
CNSTDOT  TM    7(R2),X'20'        PERIOD ALREADY FOUND?
         BNZ   CNSTNELP           YES, ONLY ONE PER CNSTOMER
         ST    R8,16(,R2)         STORE ADDRESS OF POINT
         OI    7(R2),X'20'        SET DECIMAL POINT FLAG
         B     CNSTNUM            AND CONTINUE
         SPACE
CNSTNDON LR    R0,R8
         SR    R0,R15             COMPUTE LENGTH OF NUMBER
         TM    7(R2),X'20'        WAS THERE A POINT?
         BZ    CNSTNDOT           NO.
         BCTR  R0,0               YES, ADJUST LENGTH
         LR    R14,R8
         BCTR  R14,0              LOOK AT LAST CHAR OF NUMBER
         C     R14,16(,R2)        SEE IF THE PERIOD WAS THERE
         BE    CNSTNSTY           EVIL IF SO
CNSTNDOT CH    R0,=H'18'          IS THE LENGTH OK?
         BH    CNSTPERR           NO, CAN'T BE A GOOD CONSTANT
         STC   R0,0(,R2)          YES, STORE IT
         ST    R15,8(,R2)         STORE START OF DIGITS
         CR    R8,R7              ANYTHING MORE?
         BH    CNSTFIX            NO.
         TM    0(R3),X'20'        YES, HAVE WE AN E?
         BZ    CNSTFIX            NO.
         TM    7(R2),X'20'        YES, HAS THERE BEEN A POINT?
         BZ    CNSTPERR           NO, NOT A CONSTANT
         CH    R0,=H'16'          TOO MANY DIGITS FOR FLOAT?
         BH    CNSTNELP           YES, DEFINITE ERROR
         SPACE
         BXH   R8,R6,CNSTERR      SKIP OVER THE E
         IC    R1,0(,R8)
         LA    R3,COBTAB(R1)      INSPECT NEXT CHAR
         TM    0(R3),X'06'        IS IT A SIGN?
         BZ    CNSTEXP            NO.
         TM    0(R3),X'02'        YES, IS IT +?
         BNZ   CNSTEPOS           YES.
         OI    7(R2),X'40'        SET MINUS EXPONENT FLAG
CNSTEPOS BXH   R8,R6,CNSTERR      SKIP THE SIGN
CNSTEXP  OI    6(R2),X'01'        SET FLOATING FLAG
         ST    R8,12(,R2)         STORE START OF EXPONENT
         SPACE
CNSTEXLP IC    R1,0(,R8)
         LA    R3,COBTAB(R1)      EXAMINE NEXT EXPONENT CHAR
         TM    0(R3),X'40'        NUMERIC?
         BNZ   CNSTEXNX           YES, GO ON
         TM    0(R3),X'2E'        NO, SOMETHING CLOSE?
         BNZ   CNSTNELP           YES, SCAN FOR ERROR END
         B     CNSTEXDN           NO, CONSTANT IS DONE
CNSTEXNX BXLE  R8,R6,CNSTEXLP
         SPACE
CNSTEXDN LR    R0,R8
         S     R0,12(,R2)         COMPUTE EXPONENT LENGTH
         CH    R0,=H'2'           TOO LONG?
         BH    CNSTERR            YES.
         STC   R0,1(,R2)          NO, STORE EXPONENT LENGTH
         OI    6(R2),X'C0'        SET NUMERIC BITS
         SR    R15,R15
         B     RETURN             AND RETURN
         SPACE
CNSTFIX  OI    6(R2),X'C8'        SET FIXED-POINT CONSTANT
         SR    R15,R15
         B     RETURN
         SPACE
*
*        WE CANNOT HAVE A GOOD NUMERIC CONSTANT, BUT WE MIGHT HAVE A
*        VARIABLE. IF THAT IS ALLOWED, RETURN TO THE CLUZQUAL ROUTINE
*        TO CHECK IT OUT.
*
CNSTPERR TM    6(R9),X'10'        IS THIS AN ANY?
         BZ    CNSTNSTY           NO, MUST REALLY BE AN ERROR
         TM    CUMCHARS,X'0A'     YES, ANY PERIODS OR PLUSSES FOUND?
         BNZ   CNSTNSTY           YES, NASTY CONSTANT
         TM    7(R2),X'80'        NO, DID IT START WITH A MINUS?
         BZ    QUALNLIT           NO, IT MIGHT BE A VARIABLE NAME
         SPACE
CNSTNSTY CR    R8,R7              IS ANYTHING LEFT?
         BH    CNSTERR            NO, INDICATE ERROR NOW
CNSTNERR IC    R1,0(,R8)
         LA    R3,COBTAB(R1)
         TM    0(R3),X'6E'        HAVE WE A COBOL KIND OF CHAR?
         BZ    CNSTERR            NO, MUST BE THE END
CNSTNELP BXLE  R8,R6,CNSTNERR     YES, KEEP LOOKING
         B     CNSTERR
         TITLE 'CLUZQUAL -- COBOL VARIABLE QUALIFIER RECOGNIZER'
*
*        THIS ROUTINE RECOGNIZES A COBOL VARIABLE QUALIFIER, POSSIBLY
*        PRECEDED BY A MODULE-ID. FOR "ANY" TYPE PARAMETERS, IT
*        INTERFACES TO THE CONSTANT RECOGNIZER TO SEE IF A CONSTANT
*        IS PRESENT BEFORE CHECKING FOR A VARIABLE.
*
CLUZQUAL LIENTRY ENV=R5,PARM=R2,DSA=COBDSA,DSALEN=COBLEN
         SR    R1,R1
         LR    R15,R8             SAVE STARTING POINT
         MVI   CUMCHARS,X'00'     INITIALIZE ACCUMULATOR
         IC    R1,0(,R8)
         LA    R3,COBTAB(R1)
         TM    6(R2),X'80'        IS THIS FOR THE 1ST QUALIFIER?
         BZ    QUAL2              NO.
         TM    6(R9),X'10'        YES, MIGHT IT BE A CONSTANT?
         BZ    QUALVAR            NO.
         TM    0(R3),X'20'        YES, START WITH A LETTER?
         BZ    CNSTLIT            NO, CHECK FOR LITERAL.
         B     CNSTFIG            YES, CHECK FOR FIGURATIVE CONSTANT
         SPACE
*
*        CONTROL COMES HERE IF AN POSSIBLE FIGURATIVE CONSTANT TURNS
*        OUT BAD FOR AN "ANY" TERM.
*
QUALNFIG SR    R1,R1              GET BACK 1ST CHAR INFO
         IC    R1,0(,R8)
         LA    R3,COBTAB(R1)
         LR    R15,R8             PUT STARTING POINT BACK IN R15
         SPACE
QUALVAR  TM    0(R3),X'60'        HAVE WE A LETTER OR DIGIT?
         BNZ   QUALGO             YES.
         LA    R15,4              NO, QUALIFIER IS MISSING
         NI    6(R2),X'7F'        TURN OFF PRESENCE FLAG
         B     RETURN             AND RETURN
         SPACE
QUAL2    TM    0(R3),X'60'        HAVE WE A LETTER OR DIGIT?
         BZ    QUALBAD            NO, BAD QUALIFIER
         SPACE
QUALGO   L     R14,=V(CLUTSCAP)   FIND UPPER CASE TABLE
         SPACE
QUALFOLD TM    1(R9),X'40'        IS THIS AN ASIS VAR?
         BNZ   QUALLOOP           YES.
         TR    0(1,R8),0(R14)     NO, FOLD THIS CHAR
QUALLOOP OC    CUMCHARS,0(R3)     SAVE CHARACTER TYPE
         BXH   R8,R6,QUALEND
QUALRSUM IC    R1,0(,R8)
         LA    R3,COBTAB(R1)
QUALCHAR TM    0(R3),X'64'        ALPHAMERIC OR HYPHEN?
         BNZ   QUALFOLD           YES.
         TM    0(R3),X'11'        NO, IS IT A STOPPER?
         BNZ   QUALFIN            YES.
         TM    0(R3),X'08'        IS IT A PERIOD?
         BNZ   QUALDOT            MIGHT BE OK THEN
         B     QUALBAD            NO, WE HAVE A BAD QUALIFIER
         SPACE
QUALFIN  CLI   0(R8),C'('         STOPPED BY OPEN PAREN?
         BNE   QUALEND            NO.
         OI    COBFLAGS,COBIMMSB  YES, SHOW FOR SUBSCRIPT HANDLER
QUALEND  LR    R3,R8
         BCTR  R3,0
         CLI   0(R3),C'-'         WAS LAST CHAR A HYPHEN?
         BE    QUALERR            YES, BAD VARIABLE
         TM    CUMCHARS,X'20'     WAS A LETTER FOUND?
         BZ    QUALERR            NO, NOT ALLOWED
         LR    R0,R8
         SR    R0,R15             GET LENGTH OF QUALIFIER
         CH    R0,=H'30'          COMPARE TO MAX
         BH    QUALERR
         ST    R15,0(,R2)         STORE START OF QUALIFIER
         STC   R0,4(,R2)          SAVE THE LENGTH
         SR    R15,R15            NORMAL RETURN CODE
         TM    6(R2),X'80'        1ST TIME CALL?
         BZ    RETURN             NO, RETURN
         OI    6(R2),X'20'        YES, INDICATE VAR
         B     RETURN
         SPACE
QUALDOT  TM    6(R2),X'80'        IS THIS A 1ST TIME CALL?
         BZ    QUALBAD            NO, PERIOD NOT ALLOWED
         CLI   16(R2),0           YES, ONE ALREADY NOTED?
         BNE   QUALBAD            YES, BAD NEWS
         LR    R0,R8
         SR    R0,R15             COMPUTE LENGTH OF PGM ID
         IC    R1,0(,R15)
         LA    R3,COBTAB(R1)
         TM    0(R3),X'20'        MAKE SURE FIRST CHAR ALPHA
         BZ    QUALBAD            BAD IF NOT
         TM    CUMCHARS,X'04'     MAKE SURE NO HYPHENS
         BNZ   QUALBAD
         CH    R0,=H'8'           MAKE SURE ID LENGTH OK
         BH    QUALBAD
         STC   R0,16(,R2)         STORE ID LENGTH
         ST    R15,12(,R2)        AND START OF IT
         BXH   R8,R6,QUALERR      SKIP PAST PERIOD
         LR    R15,R8             SAVE TRUE VAR START
         IC    R1,0(,R8)
         LA    R3,COBTAB(R1)
         TM    0(R3),X'11'        IS IT A TERMINATOR
         BNZ   QUALERR            OOPS.
         MVI   CUMCHARS,X'00'     RESET ACCUMULATOR
         B     QUALCHAR           AND START OVER AGAIN
         SPACE
*
*        CONTROL COMES HERE IF AN APPARENT CONSTANT TURNS OUT BAD FOR
*        AN "ANY" TERM.
*
QUALNLIT CR    R8,R7              IS ANYTHING LEFT?
         BH    QUALEND            NO, CHECK OUT VAR
         L     R14,=V(CLUTSCAP)
         B     QUALRSUM           YES, JUST RESUME SCANNING
         SPACE
QUALBAD  BXH   R8,R6,QUALERR      SKIP THIS CHAR
         IC    R1,0(,R8)
         LA    R3,COBTAB(R1)
         TM    0(R3),X'11'        LOOK FOR A STOPPER
         BZ    QUALBAD            CONTINUE UNTIL FOUND
         SPACE
QUALERR  LA    R15,8              GIVE ERROR RETURN
         B     RETURN
         SPACE 3
COBDSA   DSECT ,                  DSA FOR CONSTANT/QUALIFIER
         SPACE
         DS    22F
CUMCHARS DS    BL1                CHARACTER TYPE ACCUMULATOR
         DS    0D
         SPACE
COBLEN   EQU   *-COBDSA
         SPACE
CLUTSCOB CSECT ,                  RESUME PREVIOUS CSECT
         TITLE 'CLUZCVAR -- COBOL VARIABLE CONTROL ROUTINE'
*
*        THIS ROUTINE DIRECTS THE PARSING OF "VAR" AND "ANY" IKJTERM'S.
*        IT USES THE CLUZQUAL ROUTINE TO RECOGNIZE INDIVIDUAL
*        QUALIFIERS, AND USES THE PROMPTING SUBROUTINES DIRECTLY TO
*        ALLOW FOR CORRECTION OF INVALID SYNTAX.
*
CLUZCVAR LIENTRY PARM=R2,ENV=R5,DSA=VARDSA,DSALEN=VARLEN
         MVI   VARFLAGS,X'00'     INITIALIZE FLAG BYTE
         MVI   LASTBYP,X'FF'      IN CASE ONLY HIDDEN HEAD ENTERED
         LR    R3,R2              SAVE PDE ADDR FOR LATER
         SPACE
VARESTRT LR    R10,R8
         ST    R8,PREVPOS         SAVE STARTING POINT
         XC    0(20,R2),0(R2)     WIPE OUT PREVIOUS CONTENTS
         OI    6(R2),X'80'        IDENTIFY PDE AS FIRST QUAL
         MVI   8(R2),X'FF'        SET NULL CHAIN FIELD
         LR    R1,R2
         LA    R15,CLUZQUAL
         BALR  R14,R15            GO GET FIRST QUALIFIER
         B     *+4(R15)           ACT ON RET CODE
         B     VARSTART           GOT IT
         B     VARMISS            NOT THERE
         B     VARSTERR           1ST QUALIFIER BAD
         SPACE
VARMISS  MVI   6(R2),X'00'        TURN OFF PRESENCE FLAG
         B     RETURN             AND RETURN MISSING
         SPACE
VARSTERR BAL   R14,INV            SAY INVALID VARIABLE
         B     *+4(R15)           ANYTHING BACK?
         B     VARSTNEW           ANOTHER ATTEMPT
         OI    VARFLAGS,VMISS     SET MISSING INFO FLAG
         B     VARHEAD            AND LOOK FOR MORE QUALIFIERS
         SPACE
VARSTNEW MVI   BYPASS,0
         OC    OMIT1(4),OMIT1     ANYTHING OMITTED BEFORE?
         BZ    VARESKIP           NO, JUST PROCESS RESPONSE
         CLI   LISTF,0            YES, WITHIN A LIST?
         BNE   VARESKIP           YES, PROCESS RESPONSE OURSELF
         TM    RANGEF,RANGE2      HANDLING 2ND PART OF RANGE?
         BNZ   VARESKIP           YES, DON'T GO BACK
         LA    R15,16             NO, GIVE RETRY RETURN CODE
         B     RETURN             AND LET CALLER STRAIGHTEN IT OUT
         SPACE
VARESKIP MVI   BYPASS,0
         L     R15,=A(CLUZSKIP)   SKIP OVER EMPTINESS
         BALR  R14,R15
         B     *+4(R15)
         B     VARESTRT           GO HANDLE NEW INPUT
         B     VARMISS            RETURN MISSING VALUE
         SPACE
VARSTART TM    6(R2),X'40'        DID WE FIND A CONSTANT?
         BZ    VARHEAD            NO.
VARDONE  SR    R15,R15            SHOW SUCCESS
VARSUBCK TM    1(R9),X'10'        MAY THIS TERM HAVE A SUBSCRIPT?
         BZ    RETURN             NO.
         TM    6(R2),X'20'        YES, IS IT A VARIABLE?
         BZ    RETURN             NO, MAY NOT HAVE SUBSCRIPT
         OI    COBFLAGS,COBSUBOK  YES, SET FLAG
         B     RETURN
         SPACE
VARHEAD  OI    COBFLAGS,COBFORM   SET COBOL MESSAGE STYLE FLAG
         TM    RANGEF,RANGE2      IS THIS A 2ND RANGE PART?
         BNZ   VARHEADR           YES.
         ST    R10,COBHEAD1       NO, STORE START & END OF HEAD
         ST    R8,COBHEAD1+4
         OI    COBFLGS1,COBOLVAR  SET TRUE VARIABLE SWITCH
         B     VARHEADJ
VARHEADR ST    R10,COBHEAD2
         ST    R8,COBHEAD2+4
         OI    COBFLGS2,COBOLVAR  SET TRUE VARIABLE SWITCH
VARHEADJ CLI   BYPASS,0           WAS THIS QUALIFIER SECRETED?
         BE    VARCONT            NO.
         TM    RANGEF,RANGE2      YES, SET BYPASS FLAG
         BNZ   VARHBYP2
         OI    COBFLGS1,COBHBYP
         B     VARCONT
VARHBYP2 OI    COBFLGS2,COBHBYP
         SPACE
VARCONT  MVI   BYPASS,0           RESET BYPASS FLAG
         MVC   SKIPSAVE,SKIPF     SAVE CLUZSKIP FLAGS
         OI    SKIPF,NOSLASH      NO SUPPRESSION OF OF OR IN
         MVI   EXPECT,C' '        EXPECT THE IMPOSSIBLE
         L     R15,=A(CLUZSKIP)
         BALR  R14,R15            SKIP OVER DELIMITERS
         NI    SKIPSAVE,NOSLASH
         NI    SKIPF,255-NOSLASH
         OC    SKIPF,SKIPSAVE     RESTORE ORIGINAL SLASH HANDLING
         B     *+4(R15)           ANYTHING LEFT?
         B     VARMORE            YES.
         B     VARFIN             NO, RETURN
         SPACE
VARMORE  CR    R8,R7              ROOM FOR OF OR IN?
         BNL   VARFIN             NO, QUIT
         MVC   QWORD,0(R8)        YES, GRAB 2 CHARS
         L     R14,=V(CLUTSCAP)
         TR    QWORD,0(R14)       UPPER CASE THEM
         CLC   QWORD,=C'OF'       IS IT OF?
         BE    VAROF              YES.
         CLC   QWORD,=C'IN'       IS IT IN?
         BNE   VAROVER            NO, NO VAR LEFT
VAROF    LA    R14,2(,R8)         LOOK PAST CONNECTOR
         CR    R14,R7             ANYTHING THERE?
         BH    VAROF2             NO.
         SR    R1,R1
         IC    R1,0(,R14)         YES, WHAT IS IT?
         LA    R15,COBTAB(R1)
         TM    0(R15),X'6C'       ANYTHING PLAUSIBLE?
         BNZ   VAROVER            YES, ASSUME NOT OF OR IN
VAROF2   LR    R8,R14             SET NEW CURSOR POSITION
VAROFT   XC    COMMAX,COMMAX      NO COMMAS AFTER OF/IN
         L     R15,=A(CLUZSKIP)   SKIP TO NEXT TEXT
         BALR  R14,R15
         B     *+4(R15)           IS THERE ANY?
         B     VARNEXT            YES.
         B     VARINC             NO, HANDLE INCOMPLETE VAR
         SPACE
VARNEXT  SR    R1,R1
         IC    R1,0(,R8)          LOOK AT NEXT CHARACTER
         LA    R14,COBTAB(R1)
         TM    0(R14),X'11'       IS IT A STOPPER?
         BNZ   VARINC             YES, VARIABLE IS INCOMPLETE
         CLI   4(R3),X'00'        HAVE WE ALREADY GOT QUAL STORAGE?
         BE    VARQUAL            YES, DON'T DO IT AGAIN
         LA    R1,16              GET 16 BYTES FOR QUALIFIER PDE
         L     R15,=A(CLUZALOC)
         BALR  R14,R15
         L     R1,NEWMEM          FIND THE NEW MEMORY
         XC    0(16,R1),0(R1)     ZERO IT
         ST    R1,8(,R3)          CHAIN TO PREVIOUS
         LR    R3,R1
         MVI   8(R3),X'FF'        SET NULL CHAIN IN NEW ALLOCATION
         SPACE
VARQUAL  LR    R1,R3              PASS NEW PDE TO QUAL ROUTINE
         LR    R10,R8             SAVE CURRENT POSITION
         LA    R15,CLUZQUAL
         BALR  R14,R15            ASK FOR ANOTHER QUALIFIER
         SPACE
         B     *+4(R15)           ACT ON QUAL RET CODE
         B     VARADD             OK, ADD THIS QUALIFIER
         BAL   R6,PARSRC12        SHOULD NEVER OCCUR
         ST    R10,PREVPOS        BAD QUALIFIER, PREPARE TO PROMPT
         OI    COBFLAGS,QUALFLG   JUST ASK FOR 1 QUALIFIER
         BAL   R14,INV
         B     *+4(R15)           WAS IT CORRECTED?
         B     VAROFT             YES, TRY AGAIN
         OI    VARFLAGS,VMISS     NO, SET MISSING FLAG
         SPACE
VARADD   MVC   LASTBYP,BYPASS     SAVE THE BYPASS FLAG
         TM    RANGEF,RANGE2      SET FLAGS FOR RANGE ELEMENT
         BNZ   VARTL2
         ST    R10,COBTAIL1       SAVE START & END POINTS
         ST    R8,COBTAIL1+4
         OI    COBFLGS1,COBQVAR   SET QUALIFIED VAR FLAG
         B     VARCOUNT
VARTL2   ST    R10,COBTAIL2
         ST    R8,COBTAIL2+4
         OI    COBFLGS2,COBQVAR   SET QUALIFIED VAR FLAG
         SPACE
VARCOUNT SR    R1,R1
         IC    R1,17(,R2)         LOAD PREVIOUS COUNT
         LA    R1,1(,R1)          BUMP BY 1
         STC   R1,17(,R2)
         CH    R1,=H'255'         REACHED THE LIMIT?
         BL    VARCONT            NO.
         B     VARFIN             GOOD GRIEF, JUST GIVE UP NOW
         SPACE
VARINC   MVC   MSGAREA(4),=A(INCOMPLE) "INCOMPLETE "
         BAL   R14,FDSC           FIND THE DESCRIPTION
         ST    R1,MSGAREA+4
         MVC   MSGAREA+8(4),ACOLON     :
         MVI   QWORD-1,C' '       ADD BLANK PAD TO OF/IN
         MVI   QWORD-2,2          STORE LENGTH OF OF/IN-1
         LA    R0,QWORD-2
         MVI   MSGAREA+12,X'0D'   ADD VAR TEXT TO MESSAGE
         ST    R0,MSGAREA+16      ADD OF OR IN
         MVI   MSGAREA+16,X'07'   SET ENDING & SUPPRESSIBLE BITS
         LA    R1,MSGAREA
         L     R15,=A(CLUZINFO)
         BALR  R14,R15            SEND "INCOMPLETE XXX: AAA...BBB OF"
         OI    COBFLAGS,QUALFLG   ADD "QUALIFICATION FOR" TO PROMPT
         BAL   R14,FDSC           FIND THE VARIABLE DESCRIPTOR
         L     R15,=A(CLUZREQR)   CALL REQUIRE QUALIFIER ROUTINE
         BALR  R14,R15
         NI    COBFLAGS,255-QUALFLG    OFF QUALIFICATION FLAG
         B     *+4(R15)           ANYTHING COME BACK?
         B     VAROFT             YES, TRY AGAIN
         SPACE
VARFORGT LA    R15,20             NO, GIVE BLOW-IT-OFF RET CODE
         BAL   R14,RLBK           RELEASE QUALIFIER MEMORY
         B     VARSUBCK           SEE IF SUBSCRIPTS ARE ALLOWED
         SPACE
VAROVER  TM    VARFLAGS,VMISS     WAS THERE EVER ANY TROUBLE?
         BNZ   VARFORGT           YES, GIVE BACK SHRUG
         SPACE
VARFIN   TM    RANGEF,RANGE2      2ND RANGE PART?
         BNZ   VARFIN2            YES.
         CLI   LASTBYP,0          WAS THE END BYPASSED?
         BE    VARDONE            NO.
         OI    COBFLGS1,COBTBYP   YES, SET TAIL BYPASS BIT
         B     VARDONE
VARFIN2  CLI   LASTBYP,0          WAS THE END BYPASSED?
         BE    VARDONE            NO.
         OI    COBFLGS2,COBTBYP   YES, SET FLAG
         B     VARDONE
         SPACE 3
VARDSA   DSECT ,                  WORK AREA & DSA
         SPACE
         DS    22F
         DS    CL2
QWORD    DS    CL2                QUALIFIER CONNECTOR (OF OR IN)
VARFLAGS DS    BL1                FLAG BYTE
VMISS    EQU   X'80'              VARIABLE PROMPT FAILED
LASTBYP  DS    XL1                BYPASS FLAG FOR LAST QUALIFIER
         DS    0D
         SPACE
VARLEN   EQU   *-VARDSA
         SPACE
CLUTSCOB CSECT ,              RESUME COBOL CSECT
         TITLE 'CLUZOPER -- OPERATOR EXPRESSION BEGINNING ROUTINE'
*
*        THIS ROUTINE IS CALLED TO BEGIN TO HANDLE AN IKJOPER. IT
*        CHECKS THE CHAINED PCE'S TO MAKE SURE THEY ARE ALL OF THE
*        RIGHT TYPE, AND DETERMINES WHETHER AN OPEN PARENTHESIS IS
*        PRESENT IN THE INPUT. IF SO, RECOGNITION WILL PROCEED WITH
*        THE FIRST TERM OF THE EXPRESSION. IF NOT, AND A LEVEL 88
*        TERM IS ALLOWED, RECOGNITION PROCEEDS WITH THAT TERM;
*        OTHERWISE, THE EXPRESSION IS CONSIDERED MISSING.
*
CLUZOPER LIENTRY DSA=*,DSALEN=88,ENV=R5,PARM=R2
         BAL   R14,FDSC           FIND THE OPER DESCRIPTION
         LR    R15,R1
         MVC   ALIGN,0(R15)       SKIP PAST IT
         AH    R15,ALIGN          TO FIND THE SUBPART OFFSETS
         MVC   ALIGN,2(R15)       GET OFFSET OF FIRST TERM
         MVI   OPERPART,2         INDICATE WORKING ON TERM 1
         L     R0,PCL
         AH    R0,ALIGN           FIND THE FIRST TERM'S PCE
         BAL   R14,FNXT           FIND THE PCE AFTER THE OPER
         CR    R1,R0              IT SHOULD BE THE TERM
         BNE   OPERBAD            IF NOT, CHAINING ERROR
         SPACE
OPERTERM MVC   ALIGN(1),0(R1)     FIND TYPE OF CHAINED TERM
         NI    ALIGN,X'E0'
         CLI   ALIGN,X'C0'        IS IT A TERM?
         BNE   OPERBAD            NO, BLOW UP
         TM    1(R1),X'A0'        YES, IS IT A LIST OR RANGE?
         BNZ   OPERBAD            YES, ERROR
         TM    6(R1),X'08'        IS THIS A SUBSCRIPT?
         BNZ   OPERBAD            YES, NOT ALLOWED
         CLI   OPERPART,4         WHICH TERM ARE WE LOOKING AT?
         BE    OPER88             2ND TERM
         BH    OPERGOOD           LEVEL88 TERM
         SPACE
         LR    R3,R9              SAVE ORIGINAL PCE ADDRESS
         LR    R9,R1              POINT R1 TO 1ST TERM
         MVC   ALIGN,0(R15)       FIND OFFSET OF OPERATOR
         L     R0,PCL
         AH    R0,ALIGN           FIND OPERATOR'S PCE
         BAL   R14,FNXT           FIND NEXT PCE AFTER TERM 1
         CR    R1,R0              SAME AS THE OPERATOR?
         BNE   OPERBAD            NO, BAD PARMS
         MVC   ALIGN(1),0(R1)     YES, MAKE SURE ITS A RESERVED WORD
         NI    ALIGN,X'E0'
         CLI   ALIGN,X'A0'
         BNE   OPERBAD            NOPE, ERROR
         TM    1(R1),X'80'        IS IT CHAINED FROM A TERM?
         BNZ   OPERBAD            IT SHOULDN'T BE
         MVC   ALIGN,4(R15)       OFFSET OF TERM2
         L     R0,PCL
         AH    R0,ALIGN           POINT TO TERM2 PCE
         LR    R9,R1
         BAL   R14,FNXT           FIND PCE AFTER THE OPERATOR
         CR    R1,R0              SAME AS TERM2?
         BNE   OPERBAD            GASP, CHOKE
         MVI   OPERPART,4         INDICATE WORKING ON TERM2
         B     OPERTERM           MAKE SURE ITS A TERM
         SPACE
OPER88   MVC   ALIGN,6(R15)       GET OFFSET OF CHAINED TERM
         LH    R0,ALIGN
         LTR   R0,R0              IS ONE ALLOWED?
         BZ    OPERGOOD           NO, VALIDATION DONE
         A     R0,PCL             YES, FIND ITS PCE
         LR    R9,R1
         BAL   R14,FNXT           FIND NEXT PCE AFTER TERM2
         CR    R1,R0              IS IT THE SAME?
         BNE   OPERBAD            NOPE.
         MVI   OPERPART,6         YES, NOTE LEVEL 88 TERM ACTIVE
         B     OPERTERM           MAKE SURE ITS A TERM
         SPACE
OPERBAD  BAL   R6,PARSRC24        INDICATE CHAINING ERROR
         SPACE
OPERGOOD OI    OPERFLGS,OPERACT   SET OPERATOR ACTIVE FLAG
         ST    R3,OPERPCE         SAVE IKJOPER PCE
         ST    R15,OPEROFFS       SAVE ADDR OF SUBPART OFFSETS
         MVC   ALIGN,4(R1)        GET PDL OFFSET FOR FINAL PART
         LH    R10,ALIGN
         LR    R9,R1
         BAL   R14,SIZE           FIND SIZE OF THAT PART
         AR    R10,R1             GET OFFSET PAST THE END
         MVC   ALIGN,4(R3)        GET OFFSET FOR OPER ITSELF
         SH    R10,ALIGN          GET FULL SIZE OF OPER PDE
         STH   R10,OPERSIZE       SAVE FOR LATER
         MVI   COBFLGS2,COBOLVAR  INIT OPER'S COBFLGS FOR MSGS
         XC    COBHEAD2(16),COBHEAD2
         CLI   0(R8),C'('         IS THERE A ( NEARBY?
         BNE   OPERALT            NO, MIGHT BE A LEVEL 88 VAR
         OI    OPERFLGS,OPEREXPR  YES, INDICATE FULL EXPRESSION
         MVC   OPEROMIT,OMIT1     SAVE PREVIOUS BACKUP POINT
         XC    OMIT1,OMIT1
         MVI   COBFLGS2,COBOLVAR  INIT OPER'S COBFLGS FOR MSGS
         AR    R8,R6              SKIP OVER PAREN
         MVI   OPERPART,2         START RECOGNITION WITH TERM1
         MVC   ALIGN,2(R15)
         LH    R9,ALIGN
         A     R9,PCL             FIND THE PCE FOR THE FIRST TERM
         XC    COMMAX,COMMAX      CATCH COMMA BEFORE FIRST TERM
         SPACE
OPERRET  LA    R15,12             SET NEW PCE RETURN CODE
OPERRET2 L     R14,4(,R13)        FIND OLD SAVE AREA
         ST    R9,56(,R14)        PASS BACK NEW R9
         B     RETURN             AND RETURN TO CALLER
         SPACE
OPERALT  LTR   R0,R0              MAY THERE BE A LEVEL 88 NAME?
         BZ    OPERMISS           NO, EXPRESSION IS MISSING
         LR    R9,R0              YES, POINT R9 TO ITS PCE
         OC    OMIT1,OMIT1        ANYTHING OMITTED BEFORE?
         BNZ   OPERRET            YES.
         ST    R3,OMIT1           NO, SAY OPER PROPER FIRST OMITTED
         B     OPERRET            GO RECOGNIZE IT
         SPACE
OPERMISS LA    R15,4              SET MISSING RET CODE
         MVI   OPERFLGS,0         RESET OPERATOR FLAGS
         TM    0(R3),X'10'        IS THIS EXPRESSION REQUIRED?
         BNZ   OPERRQR            YES.
         LR    R9,R0              NO, CAUSE SKIP PAST END OF EXPR
         B     OPERRET2
         SPACE
OPERRQR  LR    R9,R3              RESTORE ORIGINAL R9
         B     OPERRET2           FOR PURPOSES OF REQUIREMENT
         TITLE 'CLUTSCOB -- COBOL-ONLY DATA AREAS'
*
*        DEFINE THE TRT TABLE FOR USE BY THE COBOL RECOGNIZERS.
*
         SPACE
         TABSET 64,(F0,F9)        X'40'=DIGITS
         TABSET 32,(81,89),(91,99),(A2,A9),(C1,C9),(D1,D9),(E2,E9)     *
                                  X'20'=ALPHA
         TABSET 16,05,40,5E,61,6B X'10'=SEPARATORS+SLASH
         TABSET 8,4B              X'08'=PERIOD
         TABSET 4,60              X'04'=HYPHEN
         TABSET 2,4E              X'02'=PLUS
         TABSET 1,4D,5D,7A        X'01'=( | ) | :
         SPACE
COBTAB   TABGEN ,                 GENERATE COBOL TABLE
         TITLE 'DYNAMIC WORK AREA MAPPING'
DSA      DSECT ,                  GENERAL WORK AREA
         SPACE
SAVEAREA DS    22A
IOPL     DS    4A                 IOPL FOR SERVICE ROUTINES
PCL      DS    A                  PCL POINTER
ANSPTR   DS    A                  ANSWER PLACE POINTER
CBUF     DS    A                  ORIGINAL COMMAND BUFFER PTR
VPDE     DS    A                  PDE FOR VALIDCK EXIT
UWA      DS    A                  WORK AREA PTR FOR VALIDCK EXIT
VMSG     DS    A                  LEVEL 2 MSG PTR
TEXTSEG  DS    A                  TEXT TO PARSE (FOR USER RECOGNIZER)
RPARM    DS    A                  USER RECOGNIZER PARM ADDR
CAPTAB   DS    A                  ADDR(CLUTSCAP) IF NOT ASIS
GETRTN   DS    A                  USER RECOGNIZER GET ENTRY
GIVEUP   DS    4A                 LIE SIMULATED LABEL VARIABLE
NEWMEM   DS    A                  NEWLY GOTTEN MEMORY ADDR
MEMBLOCK DS    A                  ADDR OF CURRENT MEMORY BLOCK
BLKLEN   DS    F                  LENGTH OF CURRENT BLOCK
BLKTOP   DS    A                  HIGH ADDR IN CURRENT BLOCK
BLKCHAIN DS    A                  BLOCK CHAIN PTR FOR IKJRLSA
FREESEGS DS    A                  HEAD OF FREE MEMORY SEGMENT CHAIN
ALCSEGS  DS    A                  HEAD OF ALLOCATED SEGMENT CHAIN
OWNSEGS  DS    A                  HEAD OF PENDING SEGMENT CHAIN
OWNLINK  DS    A                  LAST LINK IN PENDING SEGMENT CHAIN
SEGSTART DS    A                  START OF CURRENT OWNED SEGMENT
HIGHMARK DS    A                  HIGH-WATER MARK IN CURRENT BLOCK
LOWMARK  DS    A                  LOW-WATER MARK IN CURRENT BLOCK
OWNERID  DS    A                  PCE OF CURRENT MEMORY OWNER
LEVLSTAK DS    8A                 NESTING LEVEL INFORMATION STACK
CURINP   DS    A                  CURRENT INPUT BUFFER
PREVPOS  DS    A                  TEXT PTR WHERE SCAN STARTED
PASSTART DS    A                  TEXT PTR WHERE PASSWORD SCAN BEGAN
OLDPOS   DS    A                  PREVPOS SAVE AREA FOR PASSWORD
PDLPTR   DS    A                  POINTER TO START OF PDL
KEYPTR   DS    A                  POINTER TO SELECTED IKJKEYWD
PASSPDE  DS    A                  PSEUDO-PDE ADDR FOR PASSWORD
LISTEMP  DS    41A                WORK PDE FOR LISTS
RANGTEMP DS    20A                WORK PDE FOR RANGES
PREVLIST DS    A                  ADDR OF PREVIOUS LIST PDE
SUBSTAT  EQU   *                  START OF AREA SAVED OVER SUBSCRIPT
RANGEPOS DS    A                  POSITION OF RANGE VALUE 1 START
RANGEND  DS    A                  RANGE VALUE 1 END
COBHEAD1 DS    2A                 START & END FOR HEAD OF VAR
COBTAIL1 DS    2A                 START & END FOR TAIL OF VAR
COBHEAD2 DS    2A                 START & END FOR HEAD OF VAR #2
COBTAIL2 DS    2A                 START & END FOR TAIL OF VAR #2
OMIT1    DS    A                  PCE ADDR FOR RECENTLY OMITTED PARM
SUBSTLEN EQU   *-SUBSTAT          LENGTH OF SUBCRIPT-RELATED INFO
SUBSPDE  DS    A                  PDE FOR CURRENT SUBSCRIPT
OPERPCE  DS    A                  ADDR OF ACTIVE IKJOPER PCE
OPEROMIT DS    A                  SAVE AREA FOR OMIT1 OVER EXPRESSION
OPEROFFS DS    A                  ADDR OF CHAINS IN IKJOPER PCE
VRTN     DS    A                  VALIDCK ROUTINE ADDRESS
MSGAREA  DS    7A                 MESSAGE WORK AREA 1
IOWORK   DS    9A                 MESSAGE WORK AREA 2
PGPB     DS    4A                 PUTGET (ET AL) PARM BLOCK
DFSAVE   DS    2A                 SAVE AREA FOR DEFAULT ROUTINE
PWSAVE   DS    4A                 SAVE AREA FOR PASS
RSAVE    DS    6A                 SAVE AREA FOR RANG
VSAVE    DS    6A                 SAVE AREA FOR VEX
ESAVE    DS    6A                 SAVE AREA FOR ERAS
CSAVE    DS    2A                 SAVE AREA FOR CMIT/RLBK
ISAVE    DS    A                  SAVE AREA FOR INV
MSAVE    DS    6A                 SAVE AREA FOR MSG ROUTINES
BSAVE    DS    2A                 SAVE AREA FOR BMSG
SUBWORKS DS    5CL(SUBWLEN)       SUBFIELD WORK AREAS
SUBWEND  EQU   *                  END OF SUBFIELD WORK AREAS
SUBSAVE  DS    A                  ADDR(SUBSCRIPT-WORK-AREA)
         DS    2H
MSGID    DS    CL9                MSGID FOR PROMPT
         DS    0H                 FORCE VS ASSEMBLER TO ALIGN
ALIGN    DS    H
LISTSIZE DS    H                  PDE SIZE FOR LIST
RANGSIZE DS    H                  PDE SIZE FOR RANGE
OPERSIZE DS    H                  PDE SIZE FOR ALL OPER PARTS
NAME#    DS    H                  NUMBER OF IKJNAME FOR KEYWORD
COMMAS   DS    H                  NUMBER OF COMMAS FOUND BY CLUZSKIP
COMMAX   DS    H                  MAXIMUM COMMAS PERMITTED
SKIPF    DS    B                  FLAGS FOR CLUZSKIP
NOCOMMA  EQU   X'80'              TREAT COMMA AS BLANK
NOSLASH  EQU   X'40'              DON'T PROCESS SLASH
SLASH1   EQU   X'20'              1 SLASH ALREADY FOUND
NOSKIP   EQU   X'10'              DON'T SKIP ANYTHING
EXPECT   DS    CL1                EXPECTED NEXT CHARACTER
LISTF    DS    B                  WITHIN-LIST FLAG
SUBFD1   DS    B                  FIRST PARM IN SUBFIELD FLAG
SUBFLAG  DS    B                  WITHIN SUBFIELD FLAG
PASSF    DS    B                  PASSWORD FLAGS
PASSABLE EQU   X'80'              PASSWORD MAY FOLLOW
PASSCAN  EQU   X'40'              PASSWORD SCAN COMPLETE
PASSEEN  EQU   X'20'              PASSWORD DELIMITER (/) SEEN
         AIF   ('&OPSYS' NE 'MVS').NMVS3C
PASSPROM EQU   X'02'              PASSWORD FROM PROMPT RESPONSE
NWPASSOK EQU   X'01'              NEW PASSWORD MAY FOLLOW
.NMVS3C  ANOP
LEFTOVER DS    B                  PASSWORD/SUBSCRIPT FOR INVALID ITEM
SUBSOPTS EQU   *                  START OF FLAGS SAVED OVER SUBSCRIPT
RANGEF   DS    B                  RANGE FLAGS
RANGE0   EQU   X'80'              INCOMPLETE RANGE FLAG
RANGERR  EQU   X'40'              WHOLE RANGE BAD FLAG
RANGE1NV EQU   X'20'              UNABLE TO REPAIR BAD RANGE PART
RANGE2   EQU   X'10'              PART 2 IN PROGRESS
COBFLAGS DS    B                  COBOL VARIABLE FLAGS
QUALFLG  EQU   X'80'              PROMPTING FOR QUALIFIER
COBFORM  EQU   X'40'              USE COBOL ERROR DISPLAY FORMAT
COBDOTS  EQU   X'20'              DOTS NEEDED BETWEEN/AFTER VAR PART
COBSUBOK EQU   X'10'              SUBSCRIPT PERMITTED FOR THIS VAR
COBSUB   EQU   X'08'              PROCESSING A SUBSCRIPT
COBIMMSB EQU   X'04'              OPEN PAREN IMMEDIATELY AFTER VAR
COBSUB88 EQU   X'02'              LEVEL 88 SUBSCRIPT NEARBY
COBFLGS1 DS    B                  COBOL FLAGS FOR RANGE START
COBFLGS2 DS    B                  COBOL FLAGS FOR RANGE END
COBQVAR  EQU   X'80'              VARIABLE IS QUALIFIED
COBHBYP  EQU   X'40'              HEAD OF VARIABLE IS SECRET
COBTBYP  EQU   X'20'              TAIL OF VARIABLE IS SECRET
COBOLVAR EQU   X'10'              REALLY A COBOL VARIABLE
COBHASUB EQU   X'08'              THIS VARIABLE HAS A SUBSCRIPT
SUBOPLEN EQU   *-SUBSOPTS         LENGTH OF SUBSCRIPT-RELATED FLAGS
SUBSNO   DS    FL1                CURRENT SUBSCRIPT NUMBER
OPERFLGS DS    B                  OPERATOR FLAGS
OPERFAIL EQU   X'80'              PROMPTING FAILURE FLAG
OPERACT  EQU   X'40'              IKJOPER ACTIVE
OPEREXPR EQU   X'20'              WITHIN EXPRESSION FOR OPER
OPERINV  EQU   X'10'              INVALID OPERATOR EXPRESSION
OPERCLS  EQU   X'08'              OPERATOR EXPRESSION HAS BEEN CLOSED
OPERPRES DS    B                  PRESENCE INDICATORS FOR OPER SUBPARTS
OPERPART DS    FL1                OPER SUBPART NUMBER
OMITPART DS    FL1                OMITTED IKJOPER SUBPART
SUBLIST  DS    B                  NO PARENTHESES AROUND LIST FLAG
DEFTF    DS    B                  DEFAULT FLAGS
DEFTINS  EQU   X'80'              DEFAULT VALUE INSERTED
DEFTPOS  EQU   X'40'              DEFAULT POSITIONAL BEING HANDLED
DEFTOPER EQU   X'20'              DEFAULTING OPER SUBITEMS
DEFTINPT DS    B                  CURRENT INPUT IS DEFAULT FLAG
RC       DS    FL1                ULTIMATE RETURN CODE
BYPASS   DS    B                  PROMPT WITH BYPASS FLAG
SKIPSAVE DS    B                  SAVE AREA FOR SKIPF
ORIGBYP  DS    B                  SAVE AREA FOR BYPASS
RANGBYP  DS    B                  SAVE AREA FOR BYPASS WITHIN RANGE
INSFLAG  DS    B                  CURRENT INPUT INSERTED
INSERTED EQU   X'80'              INPUT IS INSERTED
DUMMYSUB EQU   X'40'              INPUT IS DUMMY SUBFIELD/SUBSCRIPT
VIRGINP  EQU   X'20'              INPUT NOT YET TOUCHED BY CLUZSKIP
         AIF   ('&OPSYS' NE 'MVS').NMVS10A
PSWDCONF EQU   X'01'              INPUT IS NEW PASSWORD CONFIRMATION
.NMVS10A ANOP
ALCFLAGS DS    B                  ALLOCATION FLAGS
COLECTIV EQU   X'80'              ALLOCATING COLLECTIVE MEMORY
ACPTREQ  EQU   X'40'              ACCEPT REQUIRED FOR CURRENT OWNER
NESTLEVL DS    FL1                LEVEL OF PCE NESTING
ALCLEVEL DS    FL1                LEVEL OF ALLOCATION NESTING
LOWLEVEL DS    FL1                LOWEST LEVEL OWNING MEMORY
LEVRANGE EQU   8                  RANGE/EXPRESSION LEVEL
LEVITEM  EQU   16                 NORMAL PARAMETER LEVEL
LEVSUBS  EQU   24                 SUBSCRIPT/PASSWORD LEVEL
AMBFLAG  DS    B                  AMBIGUOUS KEYWORD FLAG BYTE
PCBYPASS DS    B                  BYPASS SPECIFIED BY PCE FLAG
DELIM    DS    CL1                CURRENT DELIMITER
CAPDELIM DS    CL1                CURRENT DELIMITER IN UPPER CASE
DLQFLAG  DS    B                  QUOTE IS DELIMITER
         DS    0D
DSALEN   EQU   *-DSA              LENGTH OF WORK AREA
         SPACE 3
SUBSECT  DSECT ,                  SUBSCRIPT SAVE AREA
         SPACE
SUBSREGS DS    6A                 REGISTER SAVE AREA
SUBSVAR  DS    A                  SUBSCRIPTED VAR PCE
SUBSWORK DS    0A,CL(SUBSTLEN)    STATUS POINTERS
SUBSFLGS DS    XL(SUBOPLEN)       STATUS FLAGS
         DS    0D
SUBSLEN  EQU   *-SUBSECT          LENGTH OF SUBSCRIPT SAVE AREA
         TITLE 'CLUTSPAR -- TSO UPT MAPPING'
         IKJUPT ,                 GET THE UPT MAPPING
         TITLE 'CLUTSPAR -- LITERALS'
CLUTSPAR CSECT ,                  RESTORE 1ST CSECT
         SPACE
         LTORG ,                  CAPTURE ALL LITERALS HERE
         TITLE 'CLUTSPAR -- TRANSLATION TABLES'
*
*        GENERATE TRANSLATE TABLES. THEY ARE PUT AT THE END OF THE
*        CSECT, SINCE ONLY THE FIRST BYTE HAS TO BE ADDRESSIBLE.
*
         TABRESET ,               FORGET COBOL TABLE VALUES
         TABSET 128,5B,7B,7C,(87,89),(91,99),(A2,A9),(C7,C9),(D1,D9),  *
               (E2,E9)            X'80'=ALPHA+NATIONAL-HEX
         TABSET 64,(F0,F9)        X'40'=DIGITS
         TABSET 32,(81,89),(91,99),(A2,A9),(C1,C9),(D1,D9),(E2,E9)     *
                                  X'20'=ALPHA
         TABSET 16,05,40,5E,61,6B X'10'=SEPARATORS+SLASH
         TABSET 8,0D,4D,5C,5D,7D  X'08'=NON-DELIMITERS - DIGITS
         TABSET 4,6D              X'04'=UNDERSCORE
         TABSET 2,4E,60,6C        X'02'=% | + | -
         TABSET 1,4D,5D,7A        X'01'=( | ) | :
         SPACE
SCANTAB  TABGEN ,                 GENERATE THE TABLE
         SPACE 3
CLUTSCAP CSECT ,                  UPPER CASE TRANSLATE TABLE
         DC    X'000102030405060708090A0B0C0D0E0F'
         DC    X'101112131415161718191A1B1C1D1E1F'
         DC    X'202122232425262728292A2B2C2D2E2F'
         DC    X'301323333435363738393A3B3C3D3E3F'
         DC    X'404142434445464748494A4B4C4D4E4F'
         DC    X'505152535455565758595A5B5C5D5E5F'
         DC    X'606162636465666768696A6B6C6D6E6F'
         DC    X'707172737475767778797A7B7C7D7E7F'
         DC    X'80C1C2C3C4C5C6C7C8C98A8B8C8D8E8F'
         DC    X'90D1D2D3D4D5D6D7D8D99A9B9C9D9E9F'
         DC    X'A0A1E2E3E4E5E6E7E8E9AAABACADAEAF'
         DC    X'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF'
         DC    X'C0C1C2C3C4C5C6C7C8C9CACBCCCDCECF'
         DC    X'D0D1D2D3D4D5D6D7D8D9DADBDCDDDEDF'
         DC    X'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF'
         DC    X'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF'
         TITLE 'CLUTSMSG -- MESSAGE SEGMENTS'
CLUTSMSG CSECT ,                  HIDE MESSAGES IN SPECIAL CSECT
INVALID  SEG   'CLUTS001I INVALID '
REENTER  SEG   'CLUTS002A REENTER'
ENTERID  SEG   'CLUTS003A'
ENTER    SEG   ' ENTER '
MISSID   SEG   'CLUTS004I'
MISSING  SEG   ' MISSING '
INCOMPLE SEG   'CLUTS005I INCOMPLETE '
UNKNOWN  SEG   'CLUTS006I UNKNOWN '
AMBIG    SEG   'CLUTS007I AMBIGUOUS '
IGNORED  SEG   'CLUTS008I EXTRANEOUS INFORMATION IGNORED'
UNINTELL SEG   'CLUTS009I UNINTELLIGIBLE INFORMATION'
OVERRIDE SEG   'CLUTS010I OVERRIDING KEYWORD ACCEPTED'
ASSUMED  SEG   'CLUTS011I CLOSING QUOTE ASSUMED'
NODELIM  SEG   'CLUTS012I STRING NOT PRECEDED BY VALID DELIMITER'
EXTRACOM SEG   'CLUTS013I EXTRANEOUS COMMA(S) IGNORED'
         AIF   ('&OPSYS' NE 'MVS').NMVS10E
CONFIRM  SEG   'CLUTS014A CONFIRM NEW PASSWORD'
CONFAIL  SEG   'CLUTS015I PASSWORD NOT CONFIRMED'
.NMVS10E ANOP
ERRAT    SEG   'CLUTS099I IKJPARS ERROR DETECTED AT OFFSET '
FIRST    SEG   'FIRST '
SECOND   SEG   'SECOND '
RANGE    SEG   'RANGE'
OPERAND  SEG   'OPERAND'
OPERATOR SEG   'OPERATOR'
QUALFOR  SEG   'QUALIFIER FOR '
PASSWORD SEG   'PASSWORD'
SUBSCRS  SEG   'SUBSCRIPT(S)'
FOR      SEG   ' FOR '
KEYWORD  SEG   'KEYWORD'
INVPARM  SEG   'INVALID '
STRINGID SEG   'STRING'
VALUEID  SEG   'VALUE'
ADDRID   SEG   'ADDRESS'
USERID   SEG   'USERID'
DSNID    SEG   'DSNAME'
         AIF   ('&OPSYS' NE 'MVS').NMVS4D
DDNID    SEG   'DDNAME'
JOBID    SEG   'JOBNAME'
NEWPASS  SEG   'NEW PASSWORD'
.NMVS4D  ANOP
COLON    SEG   ': '
RCOLON   SEG   ':'
BPLUS    SEG   ' +'
PLUS     SEG   '+'
PLMINUS  SEG   '+ - '
MINUS    SEG   ' -  '
BLANK    SEG   ' '
         END   CLUZPAR       I CAN'T BELIEVE I WROTE THE WHOLE THING
