++USERMOD (RTS10Q1)
  /* MODIFIED VERSION OF A JES MOD FROM GTE LABORATORIES
     TO NOTIFY TSO USERS OF THE MAX CONDITION CODE OR ABEND CODE
     THIS CODE ACTUALLY TAKES THE SAVED RETURN CODE AND PLACES IT
     INTO THE NOTIFY MESSAGE.

     OBTAINED FROM THE CBT MODS TAPE, VERSION 187, FILE 232.

     MODIFIED BY ICSA TO PLACE INTO SMP4 FORMAT  MARCH 1982       */.
++VER (Z038) FMID(EJE1102)
              REQ(RTS10A1,RTS10T1                           /* ICSA */).
++SRCUPD (HASPPRPU).
./ CHANGE NAME=HASPPRPU
         COPY  RTS10Q1A        ADD CC/ABEND CODE TO MSG         RTS10Q1 Q0557050
++MAC (RTS10Q1A) DISTLIB(RICEHSRC) ASSEM(HASPPRPU).
         CLI   JCTCNVRC,X'77'  IS IT OURS ?                     RTS10Q1
         BNE   #RTS10#1        NO SKIP                          RTS10Q1
         CLI   JCTJTFLG,JCTJTJF IS IT OURS ?                    RTS10Q1
         BE    #RTS10#1          NO SKIP                        RTS10Q1
         TM    JCTJTFLG,JCTJTABD ABENDED?                       RTS10Q1
         BZ    #RTS10#2        NO ABEND                         RTS10Q1
         MVC   0(22,R1),=C'- ABENDED  SYSTEM XXX '              RTS10Q1
         L     R2,JCTCNVRC     GET CODE                         RTS10Q1
         LA    R2,0(R2)        CLEAR HIGH ORDER                 RTS10Q1
         SRL   R2,12           GET SYSTEM CODE IN               RTS10Q1
         LTR   R2,R2           TEST SYSTEM CODE                 RTS10Q1
         BZ    #RTS10#3        USER CODE                        RTS10Q1
         SRDL  R2,12           CONVERT IT TO HEX                RTS10Q1
         SR    R2,R2                   "                        RTS10Q1
         SLDL  R2,4                    "                        RTS10Q1
         IC    R2,$HEXTRAN+240(R2)     "                        RTS10Q1
         STC   R2,18(R1)               "                        RTS10Q1
         SR    R2,R2                   "                        RTS10Q1
         SLDL  R2,4                    "                        RTS10Q1
         IC    R2,$HEXTRAN+240(R2)     "                        RTS10Q1
         STC   R2,19(R1)               "                        RTS10Q1
         SR    R2,R2                   "                        RTS10Q1
         SLDL  R2,4                    "                        RTS10Q1
         IC    R2,$HEXTRAN+240(R2)     "                        RTS10Q1
         STC   R2,20(R1)               "                        RTS10Q1
         B     #RTS10#4        GO INCREMENT LINE LENGTH         RTS10Q1
#RTS10#2 MVC   0(22,R1),=C'- MAX COND CODE XXXX  '              RTS10Q1
         B     #RTS10#5        GO CONVERT TO DEC/PRINTABLE      RTS10Q1
#RTS10#3 MVC   0(22,R1),=C'- ABENDED  USER XXXX  '              RTS10Q1
#RTS10#5 LH    R2,JCTCNVRC+2   GET MAX COND/USER ABEND          RTS10Q1
         CVD   R2,16(R1)       CONVERT TO DECIMAL               RTS10Q1
         UNPK  16(4,R1),16(8,R1) UNPK FOUR LOW DIGITS           RTS10Q1
         OI    19(R1),X'F0'    SET ZONE                         RTS10Q1
         MVI   20(R1),X'40'    CLEAR NEXT TO LAST BYTE          RTS10Q1
         MVI   21(R1),X'40'    CLEAR LAST BYTE                  RTS10Q1
#RTS10#4 LA    R1,22(R1)       INCREMENT LENGTH                 RTS10Q1
         B     OPLOUSM2        RETURN TO NORMAL PROCESSING      RTS10Q1
#RTS10#1 NULL  ,               BE SURE OF ALLIGNMENT            RTS10Q1
