 /* ICSA Disk Mapper - MAPDISK.  Main Routine.  Richard Schafer 4/79  */
 MAPDISK:
   PROC(PARM) OPTIONS(MAIN) REORDER;
0/*********************************************************************/
 /*                                                                   */
 /* MAPDISK: ICSA Disk Mapper                                         */
 /*          This program reads a standard disk VTOC, and produces a  */
 /*          listing sorted by data set name of the contents of the   */
 /*          disk.  Two report forms are available, a short form      */
 /*          which produces one line per data set giving basic data   */
 /*          regarding the data set, and a long form which produces   */
 /*          several lines per data set giving complete VTOC          */
 /*          information.  Multiple volumes may be mapped in one      */
 /*          execution; the volumes to be mapped are identified by    */
 /*          DD names beginning with VOLUME.                          */
 /*                                                                   */
 /* Author:  Richard A. Schafer                                       */
 /*                                                                   */
 /* Date  :  April 1979                                               */
 /*                                                                   */
 /* Macros:  IECSDSL1 - PL/I version of the DSCB mapping macros       */
 /*                                                                   */
 /* Files :  SYSPRINT - Report output.                                */
 /*          VOLUMEnn - Volume pointers.                              */
 /*          SPILL    - Work file for VTOC copy.                      */
 /*          SORTWKnn - Sort work files.                              */
 /*                                                                   */
 /* External Routines:                                                */
 /*          FORMAT4  - Assembler routine which reads the TIOT to     */
 /*                     build a table indicating the number of        */
 /*                     volumes to be mapped, and the DDname, volser, */
 /*                     and UCB address of each volume.               */
 /*          COPYVTOC - Called once for each volume to copy the       */
 /*                     VTOC to a work file.                          */
 /*          UCFRETRV - ICSA routine to interpret the contents of     */
 /*                     the data set creator stamp field in the VTOC  */
 /*                     entry.                                        */
 /*          System sort                                              */
 /*                                                                   */
 /* Parameters: One parameter is used to indicate which report type   */
 /*             is to be produced:                                    */
 /*                SHORT - produce one line per data set report       */
 /*                LONG  - produce full information report            */
 /*                                                                   */
 /* Authorization :                                                   */
 /*          YES - COPYVTOC does a RESERVE on the VTOC to ensure      */
 /*                that no interference occurs while reading it.      */
 /*                                                                   */
 /*********************************************************************/
0  DCL
      PLIXOPT CHAR(13) VAR INIT('NOSPIE,NOSTAE') STATIC EXTERNAL,
      COPYVTOC EXTERNAL ENTRY(POINTER, FIXED BIN(15),
                              FIXED BIN(15),FIXED BIN(15))
               OPTIONS(ASSEMBLER INTER RETCODE),
      FORMAT4 EXTERNAL ENTRY(POINTER) OPTIONS(ASSEMBLER,INTER),
      UCFRETRV ENTRY OPTIONS(ASSEMBLER,RETCODE,INTER);
   DCL
      PARM CHAR(100) VAR,
      TIMEX CHAR(9),
      DATEX CHAR(6),
      (HOUR,
      MINUTE,
      SECOND,
      YEAR,
      MONTH,
      DAY) CHAR(2),
      DSCBS(*) CHAR(140) CONTROLLED,
      1 TABLE BASED(TABLE_POINTER),    /* DDNAME VOLSER UCB TABLE */
         2 # FIXED BIN(15),            /* NUMBER OF VOLUMEXX DD CARDS */
         2 VOLUME_ENTRY(#_DDS REFER(#)) UNALIGNED,
            3 DDNAME CHAR(8),
            3 VOLSER CHAR(6),
            3 UCB POINTER,
      #_DDS FIXED BIN(15),
      MAPDISK_RETURN_CODE FIXED BIN(31) INIT(0),
      RETURN_CODE FIXED BIN(31),
      (PLIRETC,
      PLIRETV,
      ADDR,
      PLISRTD,
      TIME,
      DATE,
      VERIFY,
      INDEX,
      SUBSTR) BUILTIN,
      (VTOC_START,
      DSCBS_PER_TRACK,
      TRACKS_PER_CYLINDER,
      TOTAL_ALLOCATED,
      TOTAL_USED,
      I,
      J,
      L) FIXED BIN(15),
      PAGE_NUMBER FIXED DEC(3) INIT(1),
      TABLE_POINTER POINTER,
      REQUEST_CODE BIT(32)
         INIT('11000000100000000000000000000000'B),
      MSG_BUFFER CHAR(128),   /* UNUSED */
      REASON_CODE FIXED BIN(31),
      SYSPRINT FILE PRINT,
      SPILL FILE RECORD ENV(REGIONAL(1),F),
      VTOC FILE RECORD SEQUENTIAL KEYED INPUT
         ENV(REGIONAL(2),F,BLKSIZE(96),KEYLENGTH(44));
      ON ENDPAGE(SYSPRINT)
         CALL HEADING;
      OPEN FILE(SYSPRINT)LINESIZE(132);
      CALL FORMAT4(TABLE_POINTER);
      DO I=1 TO #;
         TIMEX=TIME;
         DATEX=DATE;
         HOUR=SUBSTR(TIMEX,1,2);
         MINUTE=SUBSTR(TIMEX,3,2);
         SECOND=SUBSTR(TIMEX,5,2);
         YEAR=SUBSTR(DATE,1,2);
         MONTH=SUBSTR(DATE,3,2);
         DAY=SUBSTR(DATE,5,2);
         CALL
         COPYVTOC(ADDR(VOLUME_ENTRY(I)),DSCBS_PER_TRACK,
                  VTOC_START,TRACKS_PER_CYLINDER);
         OPEN FILE(SPILL) SEQUENTIAL INPUT;
         IF I=1 THEN
            SIGNAL ENDPAGE(SYSPRINT);
         IF PLIRETV ¬= 0 THEN
            DO;
               PUT SKIP(3)
                   EDIT('I/O ERRORS READING VTOC - SEE JOB LOG')(A);
               PUT SKIP(3);
               MAPDISK_RETURN_CODE = PLIRETV;
            END;
         ALLOCATE DSCBS(DSCBS_PER_TRACK);
         READ FILE(SPILL) INTO(DSCBS);
         TOTAL_ALLOCATED,TOTAL_USED=0;
         CALL
         PLISRTD(' SORT FIELDS=(1,44,CH,A) ',
                 ' RECORD TYPE=F,LENGTH=(140) ',
                 0,
                 RETURN_CODE,
                 DISKMAP_E15,
                 DISKMAP_E35,
                 '',
                 'CC');
         IF RETURN_CODE=16 THEN
            DO;
               PUT SKIP EDIT('SORT FAILED FOR VOLUME=', VOLSER(I))
                        (A,A);
               MAPDISK_RETURN_CODE = 16;
            END;
         PUT SKIP(2) EDIT('TOTAL TRACKS ALLOCATED= ',TOTAL_ALLOCATED)
                     (COL(10),A,F(5))
                     ('TOTAL TRACKS USED= ',TOTAL_USED)
                     (COL(50),A,F(5));
         FREE DSCBS;
         CLOSE FILE(SPILL);
         PAGE_NUMBER=1;
         PUT LINE(60);
      END;
      CALL PLIRETC(MAPDISK_RETURN_CODE);
 DISKMAP_E15:
   PROC RETURNS(CHAR(140)) REORDER;
   DCL
      DSCB_NUMBER FIXED BIN(15) STATIC INIT(1),
      SPILL_EOF BIT(1) INIT('0'B);
      ON ENDFILE(SPILL)
         SPILL_EOF='1'B;
 /* DSCBS HAS BEEN PRELOADED PRIOR TO CALLING SORT */
      DO UNTIL(SPILL_EOF);
         DO DSCB_NUMBER=DSCB_NUMBER REPEAT(DSCB_NUMBER+1)
         WHILE(DSCB_NUMBER <= DSCBS_PER_TRACK);
            IF SUBSTR(DSCBS(DSCB_NUMBER),45,1)='1' THEN
               DO;
                  CALL PLIRETC(12);    /* CONTINUE THE SORT */
                  DSCB_NUMBER=DSCB_NUMBER+1;
                  RETURN(DSCBS(DSCB_NUMBER-1));
               END;
         END;
         DSCB_NUMBER=1;
         READ FILE(SPILL) INTO(DSCBS);
      END;
      CLOSE FILE(SPILL);
      OPEN FILE(SPILL) DIRECT INPUT;
      CALL PLIRETC(8);                 /* TERMINATE THE SORT */
   END DISKMAP_E15;
 DISKMAP_E35:
   PROC(DSCB) REORDER;
   DCL
      DSCB CHAR(140),
      COPTCD CHAR(8) VAR INIT(''),
      K FIXED BIN(15),
      J FIXED BIN(15),
      (MIN,
       TRUNC,
       LINENO) BUILTIN,
      1 LAST STATIC,
         2 TRACK FIXED BIN(15),
         2 BLOCK FIXED BIN(15),
      FORMAT CHAR(4) VARYING STATIC,
      1 DSN_ENTRY STATIC,              /* ALL THE RELEVANT INFO ABOUT
                                       A DATA SET */
         2 DSNAME CHAR(44),
         2 CREATED_BY,
            3 ACCOUNT CHAR(10),
            3 USERID CHAR(7),
         2 CREATE_DATE FIXED BIN(31),
         2 EXPIRE_DATE FIXED BIN(31),
         2 LAST_REFERENCE,
           3 YEAR FIXED BIN(15),
           3 DAY  FIXED BIN(15),
         2 PROTECTION CHAR(10),
         2 DSORG CHAR(2),
         2 NOMOVE CHAR(1),
         2 RECFM CHAR(5),
         2 LRECL FIXED BIN(15),
         2 BLKSIZE FIXED BIN(15),
         2 OPTCD BIT(8) ALIGNED,
         2 KEYLEN FIXED BIN(15),
         2 REL_KEY_POS FIXED BIN(15),
         2 ALLOCATION_UNIT CHAR(9),    /* ABSTR, CYL, TRK, BLK */
         2 SECONDARY FIXED BIN(15),
         2 ALLOCATED FIXED BIN(15),    /* SPACE ALLOCATED IN TRACKS */
         2 EMPTY FIXED BIN(15),        /* EMPTY SPACE */
         2 #_EXTENTS FIXED BIN(15),
         2 EXTENTS(16),                /* ALLOW FOR MAXIMUM */
            3 LOW(2) FIXED BIN(15),
            3 HIGH(2) FIXED BIN(15),
      (F1PTR,
      F2PTR,
      F3PTR) POINTER DEF(DSCB_POINTER),
      DSCB_POINTER POINTER;
      %DCL (FORMAT1,FORMAT2,FORMAT3,FORMAT4,FORMAT5,FORMAT6) CHAR;
      %FORMAT1='YES';
      %FORMAT2='YES';
      %FORMAT3='YES';
      %FORMAT4='NO';
      %FORMAT5='NO';
      %FORMAT6='NO';
 %NOPRINT;
      %INCLUDE SYSLIB(IECSDSL1);       /* INCLUDE DSCB MAPPING MACROS */
 %PRINT;
      DSCB_POINTER=ADDR(DSCB);
      DSNAME=DS1DSNAM;
      /* Find the userid and account of the creator (Jackson Roykirk) */
      IF INDEX('0123456789',SUBSTR(DS1LGNID,1,1)) ¬= 0 THEN
         DO;
            CREATED_BY.ACCOUNT=SUBSTR(DS1LGNID,1,10);
            CREATED_BY.USERID=SUBSTR(DS1LGNID,11,7);
         END;
      ELSE IF SUBSTR(DS1LGNID,1,1) = ' ' THEN
         DO;
            CREATED_BY.ACCOUNT=' ';
            CREATED_BY.USERID=' ';
         END;
      ELSE
         DO;
            FETCH UCFRETRV;
            CALL UCFRETRV(REQUEST_CODE,MSG_BUFFER,REASON_CODE,
                          CREATED_BY.USERID,CREATED_BY.ACCOUNT,
                          DS1LGNID);
            /* FOR THE TIME BEING, IGNORE UCFRETRV RETURN CODE */
         END;
      CREATE_DATE=1000*DS1CRYR+DS1CRDAY;
      EXPIRE_DATE=1000*DS1EXYR+DS1EXDAY;
      LAST_REFERENCE.YEAR = DS1REFYR;
      LAST_REFERENCE.DAY  = DS1REFDA;
      SELECT (DS1DSIND&'00010100'B);
         WHEN('00000000'B)
            PROTECTION='  NONE';
         WHEN('00010000'B)
            PROTECTION='READ/WRITE';
         WHEN('00010100'B)
            PROTECTION='  WRITE';
         OTHERWISE
            ;
      END;
      IF SUBSTR(DS1DSORG,8,1) THEN
 /* UNMOVABLE */
         DO;
            NOMOVE='U';
            SUBSTR(DS1DSORG,8,1)='0'B;
         END;
      ELSE
         NOMOVE=' ';
      SELECT (DS1DSORG);
         WHEN('1000000000000000'B)
            DSORG='IS';
         WHEN('0100000000000000'B)
            DSORG='PS';
         WHEN('0010000000000000'B)
            DSORG='DA';
         WHEN('0000001000000000'B)
            DSORG='PO';
         WHEN('0000000000100000'B)
            DSORG='TQ';
         WHEN('0000000000001000'B)
            DSORG='AM';
         OTHERWISE
            DSORG='**';
      END;
      FORMAT='';
      IF DS1RECFM&'00010000'B THEN
         FORMAT=FORMAT||'B';
      IF DS1RECFM&'00001000'B THEN
         FORMAT=FORMAT||'S';
      IF DS1RECFM&'00000100'B THEN
         FORMAT=FORMAT||'A';
      ELSE
         IF DS1RECFM&'000000010'B THEN
            FORMAT=FORMAT||'M';
      IF DS1RECFM&'00100000'B THEN
         FORMAT=FORMAT||'T';
      SELECT (DS1RECFM&'11000000'B);
         WHEN('10000000'B)
            RECFM='F'||FORMAT;
         WHEN('01000000'B)
            RECFM='V'||FORMAT;
         WHEN('11000000'B)
            RECFM='U'||FORMAT;
         OTHERWISE
            RECFM='*****';
      END;
      LRECL=DS1LRECL;
      BLKSIZE=DS1BLKL;
      OPTCD=DS1OPTCD;
      KEYLEN=DS1KEYL;
      REL_KEY_POS=DS1RKP;
      SELECT (DS1STYPE&'11000000'B);
         WHEN('00000000'B)
            ALLOCATION_UNIT='ABSTR';
         WHEN('01000000'B)
            ALLOCATION_UNIT='BLOCKS';
         WHEN('10000000'B)
            ALLOCATION_UNIT='TRACKS';
         WHEN('11000000'B)
            ALLOCATION_UNIT='CYLINDERS';
         OTHERWISE
            ;
      END;
      SECONDARY=DS1SQUAN;
      #_EXTENTS=DS1NOEPV;
      LAST.TRACK=DS1LRELTR;
      LAST.BLOCK=DS1LBLCK;
      EXTENTS.LOW,EXTENTS.HIGH=0;
      EXTENTS(1).LOW(*)=DS1EX1LOW;
      EXTENTS(1).HIGH(*)=DS1EX1HGH;
      IF #_EXTENTS>1 THEN
         DO;
            EXTENTS(2).LOW(*)=DS1EX2LOW;
            EXTENTS(2).HIGH(*)=DS1EX2HGH;
            IF #_EXTENTS>2 THEN
               DO;
                  EXTENTS(3).LOW(*)=DS1EX3LOW;
                  EXTENTS(3).HIGH(*)=DS1EX3HGH;
                  IF #_EXTENTS>3 THEN
                     DO;
                        CALL GET_DSCB(DS1PTRDS);
                        IF DSORG='IS' THEN
                           CALL GET_DSCB(DS2PTRDS);
                        DSCB_POINTER=ADDR(DSCB);
                        DO K=1 TO 4 WHILE(K+3<=#_EXTENTS);
                           EXTENTS(K+3).LOW(*)= DS3EXTNT(K).
                           DS3EXTLOW(*);
                           EXTENTS(K+3).HIGH(*)= DS3EXTNT(K).
                           DS3EXTHGH(*);
                        END;
                        IF #_EXTENTS>7 THEN
                           DO K=1 TO 9 WHILE(K+7<=#_EXTENTS);
                              EXTENTS(K+7).LOW(*)= DS3ADEXT(K).
                              DS3ADLOW(*);
                              EXTENTS(K+7).HIGH(*)= DS3ADEXT(K).
                              DS3ADHGH(*);
                           END;
                     END;
               END;
         END;
      ALLOCATED=0;
      DO K=1 TO #_EXTENTS;
         ALLOCATED=ALLOCATED+
         (EXTENTS(K).HIGH(1)* TRACKS_PER_CYLINDER+ EXTENTS(K).HIGH(2))-
         (EXTENTS(K).LOW(1)*TRACKS_PER_CYLINDER+ EXTENTS(K).LOW(2))+1;
      END;
      IF DSORG='PS' | DSORG='PO' THEN
         DO;
            IF LAST.TRACK=0 THEN
               IF LAST.BLOCK=0 THEN
                  EMPTY=ALLOCATED;
               ELSE
                  EMPTY=ALLOCATED-1;
            ELSE
            EMPTY=ALLOCATED-LAST.TRACK-1;
         END;
      ELSE
         EMPTY=0;
      TOTAL_ALLOCATED=TOTAL_ALLOCATED+ALLOCATED;
      TOTAL_USED=TOTAL_USED+ALLOCATED-EMPTY;
      IF PARM ¬= 'SHORT' THEN
         DO;
            IF LINENO(SYSPRINT) > 51-TRUNC(#_EXTENTS/4) THEN
               PUT LINE(60);
            PUT SKIP EDIT('CREATED BY')(COL(50),A)
                         ('CREATED')(COL(65),A)
                         ('EXPIRES')(COL(74),A)
                         ('LAST REF')(COL(83),A)
                         ('PROTECTION')(COL(92),A)
                         ('DSORG')(COL(104),A)
                         ('RECFM')(COL(111),A)
                         ('LRECL')(COL(118),A)
                         ('BLKSIZE')(COL(125),A);
            PUT SKIP EDIT(DSNAME)(COL(1),A);
            IF VERIFY(SUBSTR(ACCOUNT,1,1),'0123456789')=0 THEN
               PUT EDIT(ACCOUNT,'-',USERID)(COL(46),A,A,A);
            PUT EDIT(CREATE_DATE)(COL(65),F(5))
                    (EXPIRE_DATE)(COL(74),F(5));
            IF LAST_REFERENCE.YEAR < 70 | LAST_REFERENCE.YEAR > 99 THEN
               PUT EDIT('*****')(COL(83),A);
            ELSE
               PUT EDIT(1000*LAST_REFERENCE.YEAR+LAST_REFERENCE.DAY)
                        (COL(83),F(5));
            PUT EDIT(PROTECTION)(COL(92),A)
                    (DSORG,NOMOVE)(COL(104),A,A)
                    (RECFM)(COL(111),A);
            IF LRECL=-32768 THEN
               PUT EDIT('X')(COL(119),A);
            ELSE
               PUT EDIT(LRECL)(COL(118),F(5));
            PUT EDIT(BLKSIZE)(COL(126),F(5));
            PUT SKIP(2) EDIT('OPTCD')(COL(47),A)
                            ('KEYLEN')(COL(55),A)
                            ('RKP')(COL(66),A)
                            ('ALLOCATION')(COL(72),A)
                            ('SECONDARY')(COL(84),A)
                            ('TRKS ALLOCATED')(COL(95),A)
                            ('TRKS USED')(COL(111),A);
             IF DSORG¬= 'IS' THEN
                PUT SKIP EDIT(OPTCD)(COL(46),B);
             ELSE
                DO;
                   IF OPTCD&'10000000'B THEN
                      COPTCD='W';
                   IF OPTCD&'01000000'B THEN
                      COPTCD=COPTCD||'U';
                   IF OPTCD&'00100000'B THEN
                      COPTCD=COPTCD||'M';
                   IF OPTCD&'00010000'B THEN
                      COPTCD=COPTCD||'I';
                   IF OPTCD&'00001000'B THEN
                      COPTCD=COPTCD||'Y';
                   IF OPTCD&'00000010'B THEN
                      COPTCD=COPTCD||'L';
                   IF OPTCD&'00000100'B THEN
                      COPTCD=COPTCD||'R';
                   IF COPTCD='' THEN
                      COPTCD='NONE';
                   PUT SKIP EDIT(COPTCD)(COL(46),A);
                END;
                 PUT EDIT(KEYLEN)(COL(56),F(3))
                         (REL_KEY_POS)(COL(64),F(5))
                         (ALLOCATION_UNIT)(COL(72),A)
                         (SECONDARY)(COL(86),F(4))
                         (ALLOCATED)(COL(99),F(5))
                         (ALLOCATED-EMPTY)(COL(113),F(5));
            IF #_EXTENTS=0 THEN
               PUT SKIP(2) EDIT('DATA SET HAS NO EXTENTS')(COL(45),A);
            ELSE
               DO;
                  PUT SKIP(2) EDIT('EXTENTS OF DATA SET:')(COL(45),A);
                  PUT SKIP EDIT((' #    LOW    HIGH   '
                                 DO K=1 TO MIN(4,#_EXTENTS)))
                               (COL(45),(MIN(4,#_EXTENTS))(A));
                  PUT SKIP EDIT((K,EXTENTS(K).LOW(1),'-',
                                 EXTENTS(K).LOW(2),EXTENTS(K).HIGH(1),
                                 '-',EXTENTS(K).HIGH(2)
                                   DO K=1 TO #_EXTENTS))
                                (COL(45),(MIN(4,#_EXTENTS))
                                 (P'99',2 (X(2),P'999',A,P'99'),X(2)));
                  PUT SKIP;
               END;
         END;
      ELSE
         DO;
            PUT SKIP EDIT(DSNAME)(A);
            IF VERIFY(SUBSTR(ACCOUNT,1,1),'0123456789')=0 THEN
               PUT EDIT(ACCOUNT,'-',USERID)(COL(46),A,A,A);
            IF LAST_REFERENCE.YEAR < 70 | LAST_REFERENCE.YEAR > 99 THEN
               PUT EDIT('*****')(COL(65),A);
            ELSE
               PUT EDIT(1000*LAST_REFERENCE.YEAR+LAST_REFERENCE.DAY)
                       (COL(65),F(5));
            PUT EDIT(PROTECTION,DSORG,NOMOVE,ALLOCATED,
                     (ALLOCATED-EMPTY),SECONDARY)
                    (COL(74),A,COL(87),A,A,
                    COL(93),F(5),COL(104),F(5),
                    COL(113),F(4));
            IF ALLOCATION_UNIT = 'BLOCKS' THEN
               PUT EDIT(' (',BLKSIZE,')') (A,F(5),A);
            ELSE
               IF ALLOCATION_UNIT= 'ABSTR' THEN
                  PUT EDIT(' CYLINDERS') (A);
               ELSE
                  PUT EDIT(' ',ALLOCATION_UNIT) (A,A);
         END;
      CALL PLIRETC(4);/* TELL SORT GO AHEAD */
 GET_DSCB:
   PROC(PTRDS) REORDER;
   DCL
      1 PTRDS UNALIGNED,
         2 CYL FIXED BIN(15),
         2 TRK FIXED BIN(15),
         2 REC BIT(8) ALIGNED;
      READ FILE(SPILL) INTO(DSCBS)
                       KEY((CYL*TRACKS_PER_CYLINDER+TRK)-VTOC_START);
      DSCB=DSCBS(REC);
   END GET_DSCB;
   END DISKMAP_E35;
 HEADING:
   PROC REORDER;
      PUT LINE(1)
      EDIT('CONTENTS OF VOLUME=',VOLSER(I),' AT ',HOUR,':',MINUTE, ':',
          SECOND,' ON ', MONTH,'/',DAY,'/',YEAR,'PAGE ',PAGE_NUMBER)
          (A,A,A,A,A,A,A,A,A,A,A,A,A,A,COL(120),A,F(3));
      IF PARM='SHORT' THEN
         DO;
            PUT SKIP EDIT('TRACKS')(COL(88),A);
            PUT SKIP EDIT('DSNAME')(COL(1),A)
                            ('CREATED BY')(COL(50),A)
                            ('LAST REF')(COL(65),A)
                            ('PROTECTION')(COL(74),A)
                            ('DSORG')(COL(86),A)
                            ('ALLOCATED  USED')(COL(93),A)
                            ('SECONDARY')(COL(111),A);
         END;
      IF PAGE_NUMBER=1 THEN
         PUT SKIP;
      ELSE
         PUT SKIP(2);
      PAGE_NUMBER=PAGE_NUMBER+1;
   END HEADING;
   END MAPDISK;
