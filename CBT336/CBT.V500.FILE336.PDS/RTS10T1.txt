++USERMOD (RTS10T1)
  /* MODIFIED VERSION OF A JES MOD FROM GTE LABORATORIES
     TO NOTIFY TSO USERS OF THE MAX CONDITION CODE OR ABEND CODE
     THIS CODE COMPUTES THE HIGHEST RC/ABEND CODE AND STORES IT

     OBTAINED FROM THE CBT MODS TAPE, VERSION 187, FILE 232.

     MODIFIED BY ICSA TO PLACE INTO SMP4 FORMAT  MARCH 1982       */.
++VER (Z038) FMID(EJE1102)
              PRE(UZ37498                                   /* IBM  */)
              REQ(RTS10A1,RTS10Q1                           /* ICSA */).
++SRCUPD (HASPSSSM).
./ CHANGE NAME=HASPSSSM
         $OSJCT LIST=&LIST      GENERATE PARTIAL OS JCT DSECT   RTS10T1 T0076100
         $SCT  LIST=&LIST       GENERATE OS SCT DSECT           RTS10T1 T0076200
         ST    R1,#SJBLCT       SAVE LCT ADDRESS FOR JOB DELETE RTS10T1 T2048100
         COPY  RTS10T1A         COPY CODE TO SET COND/ABEND CODERTS10T1 T2269950
++MAC (RTS10T1A) ASSEM(HASPSSSM) DISTLIB(RICEHSRC).
         SR    R0,R0            ZERO REGISTER                   RTS10T1
         L     R1,#SJBLCT       GET LCT ADDRESS                 RTS10T1
         L     R1,LCTJCTAD-LCTDSECT(,R1) GET ADDRESS OF JCT(OS) RTS10T1
         USING INJMJCT,R1       TELL ASSEMBLER                  RTS10T1
         TM    JCTJSTAT,JCTABEND WAS JOB ABENDED                RTS10T1
         BNO   #HJE1            NO                              RTS10T1
         L     R0,JCTACODE      GET ABEND CODE                  RTS10T1
         B     #HJE3            GO TO FINISH                    RTS10T1
#HJE1    L     R1,JCTSDKAD      FIRST SCT ADDRESS               RTS10T1
         USING INSMSCT-16,R1    TELL ASSEMBLER                  RTS10T1
#HJE4    SRL   R1,8             GET ADDRESS IN PROPER FORM      RTS10T1
         LTR   R1,R1            TEST FOR ZERO (END)             RTS10T1
         BZ    #HJE3            GO TO FINISH                    RTS10T1
         CH    R0,SCTSEXEC      TEST FOR NEW MAX                RTS10T1
         BNL   #HJE5              NO                            RTS10T1
         LH    R0,SCTSEXEC        YES SAVE NEW MAX              RTS10T1
#HJE5    L     R1,SCTANSCT      GET NEXT SCT                    RTS10T1
         B     #HJE4            LOOP                            RTS10T1
#HJE3    ST    R0,JCTCNVRC      SAVE IT                         RTS10T1
         MVI   JCTCNVRC,X'77'   MARK IT AS OURS                 RTS10T1
         DROP  R1                                               RTS10T1
++MAC ($OSJCT) DISTLIB(RICEHSRC) ASSEM(HASPSSSM).
         MACRO                                                  RTS10T1
         $OSJCT &LIST=NO,&DOC=NO                                RTS10T1
         AIF   ('&DOC' NE 'YES').A                              RTS10T1
.A       TITLE 'OS JOB CONTROL TABLE (JCT) DSECT'               RTS10T1
*        THIS IS A PARTIAL EXPANSION BECAUSE OF NAME CONFLICTS  RTS10T1
*        WITH THE JES2 JCT MACRO.  PREREQ PTF SUPPLIED MOST     RTS10T1
*        RECENT COPY OF THE MACRO FROM WHICH OFFSETS ARE TAKEN. RTS10T1
JCTDSCT  DSECT                                                  RTS10T1
INJMJCT  EQU   *             BEGINNING OF OS JCT                RTS10T1
         ORG   JCTDSCT+5     GET OFFSET RIGHT                   RTS10T1
JCTJSTAT DS    BL8           JOB STATUS INDICATORS              RTS10T1
JCTABEND EQU   X'08'         ABEND BIT                          RTS10T1
         ORG   JCTDSCT+32    GET OFFSET RIGHT                   RTS10T1
JCTSDKAD DS    AL3           DISK ADDR. OF FIRST SCT            RTS10T1
         ORG   JCTDSCT+168   GET OFFSET RIGHT                   RTS10T1
JCTACODE DS    XL4           ABEND CODE FIELD                   RTS10T1
&SYSECT  CSECT               END OF OS JCT DSECT                RTS10T1
         MEND                                                   RTS10T1
++MAC ($SCT) DISTLIB(RICEHSRC) ASSEM(HASPSSSM).
         MACRO                                                  RTS10T1
         $SCT  &LIST=NO,&DOC=NO                                 RTS10T1
         AIF   ('&DOC' NE 'YES').A                              RTS10T1
.A       TITLE 'OS STEP CONTROL TABLE (SCT) DSECT'              RTS10T1
SCTDSCT  DSECT                                                  RTS10T1
         IEFASCTB                                               RTS10T1
&SYSECT  CSECT               END OF OS SCT DSECT                RTS10T1
         MEND                                                   RTS10T1
