++USERMOD (RUA13Q1)
   /* ICSA HASPPRPU MODIFICATIONS TO SUPPORT PAGE COUNTING.
      THE PAGE COUNT FOR SMF TYPE 6 RECORDS IS COMPUTED
      IN A DIFFERENT MANNER IN ORDER TO BE ACCURATE          */.
++VER (Z038) FMID(EJE1102)
              PRE(RUA04A1,RUA04Q1                           /* ICSA */)
              REQ(RUA13A1,RUA13M1                           /* ICSA */).
++MACUPD (#ICSATY6).
./ CHANGE NAME=#ICSATY6
         MVC   SMF6$PNM,JCTPNAME    INSERT PROGRAMMER NAME FIELDRUA13Q1
         XC    SMF6COST(SMF6CSTL),SMF6COST ZERO COST FIELDS     RUA13A1
         MVC   SMF6BSET,PBSETCST    COPY BASE SETUP CHARGE      RUA13Q1
         MVC   SMF6FSET,PFSETCST    COPY SETUP COST             RUA13Q1
         MVC   SMF6CSET,PCSETCST    COPY SETUP COST             RUA13Q1
         MVC   SMF6TSET,PTSETCST    COPY SETUP COST             RUA13Q1
         MVC   SMF6SFLG,PPIFLG1     COPY FLAGS                  RUA13Q1
         XC    SMF6IFLG,SMF6IFLG    CLEAR EXTRA FLAGS           RUA13Q1
         STM   PC1,PC2,$DOUBLE      SAVE REGISTERS              RUA13Q1
         L     PC2,PCEDCT           FIND THE DCT                RUA13Q1
         L     PL,PCHJOE              AND THE CHAR-JOE          RUA13Q1
         USING DCTDSECT,PC2                                     RUA13Q1
         USING JOEDSECT,PL                                      RUA13Q1
         CLC   DCTFORMS,JOEFORM     FORMS THE SAME?             RUA13Q1
         BE    #T6NFOVR             YES                         RUA13Q1
         OI    SMF6IFLG,SMF6OPRF    NO, INDICATE FORMS OVERRIDE RUA13Q1
#T6NFOVR TM    PPIFLG1,PPI1CSTR     CTAPE SPECIFIED?            RUA13Q1
         BO    #T6NCOVR             NO, SKIP COMPARISON         RUA13Q1
         CLC   DCTFCB,JOEFCB        CTAPE THE SAME?             RUA13Q1
         BE    #T6NCOVR             YES                         RUA13Q1
         OI    SMF6IFLG,SMF6OPRC    NO, INDICATE CTAPE OVERRIDE RUA13Q1
#T6NCOVR TM    PPIFLG1,PPI1TSTR     TRAIN SPECIFIED?            RUA13Q1
         BO    #T6NTOVR             NO, SKIP COMPARISON         RUA13Q1
         CLC   DCTUCS,JOEUCS        TRAIN THE SAME?             RUA13Q1
         BE    #T6NTOVR             YES                         RUA13Q1
         OI    SMF6IFLG,SMF6OPRT    NO, INDICATE TRAIN OVERRIDE RUA13Q1
         DROP  PC2,PL                                           RUA13Q1
#T6NTOVR TM    PPIFLG1,PPI1LOCL     DO WE CHARGE?               RUA13Q1
         BNO   #T6REMOT             NO                          RUA13Q1
         L     PL,PCEDCT            FIND DCT                    RUA13Q1
         USING DCTDSECT,PL                                      RUA13Q1
         TM    DCTDEVTP,DCTPRPU     IS IT A LOCAL PRT/PUN?      RUA13Q1
         BO    #T6REMOT             YES, DON'T CHARGE           RUA13Q1
         DROP  PL                                               RUA13Q1
         MVC   SMF6TOT$,SMF6BSET    INITIALIZE TOTAL COST       RUA13Q1
         MVC   SMF6SCST,SMF6BSET      AND SETUP COST            RUA13Q1
         OI    SMF6IFLG,SMF6IBST    INDICATE BASE SETUP CHARGED RUA13Q1
         TM    PPIFLG2,PPI2FSET     FORMS SETUP?                RUA13Q1
         BNO   #T6NOFST             NO                          RUA13Q1
         OI    SMF6IFLG,SMF6IFST    YES, INDICATE               RUA13Q1
         L     PC2,PFSETCST         UPDATE                      RUA13Q1
         AL    PC2,SMF6TOT$           TOTAL                     RUA13Q1
         ST    PC2,SMF6TOT$             COST                    RUA13Q1
         L     PC2,SMF6FSET         UPDATE TOTAL                RUA13Q1
         AL    PC2,SMF6SCST           SETUP COST                RUA13Q1
         ST    PC2,SMF6SCST                                     RUA13Q1
#T6NOFST TM    PPIFLG2,PPI2CSET     CTAPE SETUP?                RUA13Q1
         BNO   #T6NOCST             NO                          RUA13Q1
         OI    SMF6IFLG,SMF6ICST    YES, INDICATE               RUA13Q1
         L     PC2,PCSETCST         UPDATE                      RUA13Q1
         AL    PC2,SMF6TOT$           TOTAL                     RUA13Q1
         ST    PC2,SMF6TOT$             COST                    RUA13Q1
         L     PC2,SMF6CSET         UPDATE TOTAL                RUA13Q1
         AL    PC2,SMF6SCST           SETUP COST                RUA13Q1
         ST    PC2,SMF6SCST                                     RUA13Q1
#T6NOCST TM    PPIFLG2,PPI2TSET     TRAIN SETUP?                RUA13Q1
         BNO   #T6NOTST             NO                          RUA13Q1
         OI    SMF6IFLG,SMF6ITST    YES, INDICATE               RUA13Q1
         L     PC2,PTSETCST         UPDATE                      RUA13Q1
         AL    PC2,SMF6TOT$           TOTAL                     RUA13Q1
         ST    PC2,SMF6TOT$             COST                    RUA13Q1
         L     PC2,SMF6TSET         UPDATE TOTAL                RUA13Q1
         AL    PC2,SMF6SCST           SETUP COST                RUA13Q1
         ST    PC2,SMF6SCST                                     RUA13Q1
#T6NOTST L     PC2,PRPAGECT         COMPUTE PAGE COST           RUA13Q1
         SLR   PC1,PC1                                          RUA13Q1
         M     PC1,PPFRMCST                                     RUA13Q1
         L     PC1,$DOUBLE          RESTORE BASE REGISTER       RUA13Q1
         ST    PC2,SMF6FCST         SAVE FOR FORMS              RUA13Q1
         AL    PC2,SMF6TOT$           AND TOTAL                 RUA13Q1
         ST    PC2,SMF6TOT$             COST                    RUA13Q1
         L     PC2,PPLNCDCT         GET LINE COUNT              RUA13Q1
         TM    PCEID,PCEPRSID       PRINTER?                    RUA13Q1
         BO    #T6PRNT1             YES, USE LINE & TRAIN COST  RUA13Q1
         OI    SMF6IFLG,SMF6IPCH    INDICATE A PUNCH            RUA13Q1
         SLR   PC1,PC1              COMPUTE LINE COST           RUA13Q1
         M     PC1,PPNCHCST         NO, USE PUNCH COST          RUA13Q1
         B     #T6PRNT2                                         RUA13Q1
         SPACE 1                                                RUA13Q1
#T6PRNT1 NULL  ,                                                RUA13Q1
         SLR   PC1,PC1              COMPUTE LINE COST           RUA13Q1
         M     PC1,PLINECST                                     RUA13Q1
         M     PC1,PPTRNCST         INCLUDING TRAIN MULTIPLIER  RUA13Q1
         D     PC1,=F'1000'           FIX UNIT                  RUA13Q1
#T6PRNT2 NULL  ,                                                RUA13Q1
         L     PC1,$DOUBLE          RESTORE BASE REGISTER       RUA13Q1
         ST    PC2,SMF6LCST         SAVE FOR LINE               RUA13Q1
         AL    PC2,SMF6TOT$           AND TOTAL                 RUA13Q1
         ST    PC2,SMF6TOT$             COST                    RUA13Q1
         TM    PCEID,PCEPRSID       PRINTER?                    RUA13Q1
         BO    #T6PRNT3             YES, USE LINE COST          RUA13Q1
         MVC   SMF6LRAT,PPNCHCST    NO, USE CARD COST           RUA13Q1
         LA    PC2,1000             MAKE TRAIN MULTIPLIER       RUA13Q1
         ST    PC2,SMF6TMUL           WORK                      RUA13Q1
         B     #T6REMOT                                         RUA13Q1
         SPACE 1                                                RUA13Q1
#T6PRNT3 MVC   SMF6FMUL,PPFRMCST    SAVE COST MULTIPLIERS       RUA13Q1
         MVC   SMF6TMUL,PPTRNCST      AS WELL                   RUA13Q1
         MVC   SMF6LRAT,PLINECST    COPY LINE COST              RUA13Q1
         SPACE 1                                                RUA13Q1
#T6REMOT EQU   *                                                RUA13Q1
         LM    PC1,PC2,$DOUBLE      RESTORE THOSE REGISTERS     RUA13Q1
++SRCUPD (HASPPRPU).
./ CHANGE NAME=HASPPRPU
         #FORMTBL                  GENERATE HASP FORMTBL DSECT  RUA13Q1 Q0198600
         #CTAPTBL                  GENERATE HASP CTAPTBL DSECT  RUA13Q1 Q0198610
         #TRAINS                   GENERATE HASP TRAINS DSECT   RUA13Q1 Q0198620
         #FORMENT                  GENERATE HASP FORMENT DSECT  RUA13Q1 Q0198630
         #CTAPENT                  GENERATE HASP CTAPENT DSECT  RUA13Q1 Q0198640
         #TRANENT                  GENERATE HASP TRANENT DSECT  RUA13Q1 Q0198650
         L     R15,=A(#PAGESET)     GET PAGE                    RUA13Q1 Q2057210
         BALR  R14,R15                SETUP INFORMATION         RUA13Q1 Q2057220
         XC    PPCTPLIN,PPCTPLIN    SET INDICATORS TO           RUA13Q1 Q2207210
         XC    PPFRMLIN,PPFRMLIN      START A                   RUA13Q1 Q2207212
         MVI   PCCWORK,X'89'            NEW PAGE                RUA13Q1 Q2207214
PRNPGCT  L     R15,=A(#CHANSK)      FIND THE                    RUA13Q1 Q2207216
         BALR  R14,R15                 TOP OF PAGE              RUA13Q1 Q2207218
         NI    PPIFLG1,X'FF'-PPI1PAGE TURN OFF NEW PAGE FLAG    RUA13Q1 Q2207220
PRNPGCT  L     R15,=A(#CHANSK)      HANDLE                      RUA13Q1 Q2509000
         BALR  R14,R15                 CHANNEL SKIPPING         RUA13Q1 Q2509010
         L     PW,PDDBPGCT         INCREMENT                    RUA13Q1 Q2509020
         TM    PPIFLG1,PPI1PAGE     NEW PAGE?                   RUA13Q1 Q2559700
         BNO   PPCKPGS              NO, DON'T INCREMENT COUNTER RUA13Q1 Q2559710
         NI    PPIFLG1,X'FF'-PPI1PAGE YES, TURN OFF INDICATOR   RUA13Q1 Q2559720
         L     R15,=A(#LINESP)      HANDLE                      RUA13Q1 Q2564910
         BALR  R14,R15                 LINE SPACING             RUA13Q1 Q2564920
         TM    PCEID,PCEPRSID       TEST PROCESSOR TYPE         RUA13Q1 Q2943100
         BNO   #UA13SK1             SKIP IF NOT PRINT           RUA13Q1 Q2943110
         MVI   PCCWORK,X'89'        TELL COUNTING               RUA13Q1 Q2943120
         L     R15,=A(#CHANSK)        WE ARE                    RUA13Q1 Q2943130
         BALR  R14,R15                  SKIPPING TO CHANNEL 1   RUA13Q1 Q2943140
         TM    PPIFLG1,PPI1PAGE     REALLY NEED A NEW PAGE      RUA13Q1 Q2943150
         BNO   #UA13SK1             SKIP IF NOT                 RUA13Q1 Q2943160
         SLR   PL,PL                CLEAR FOR INSERT            RUA13Q1 Q2943170
         IC    PL,PREVCPYN          UPDATE PAGE COUNT           RUA13Q1 Q2943180
         AL    PL,PRPAGECT            WITH NUMBER OF COPIES     RUA13Q1 Q2943190
         ST    PL,PRPAGECT              THIS TRANSMISSION       RUA13Q1 Q2943200
#UA13SK1 NULL  ,                                                RUA13Q1 Q2943210
         TITLE 'HASP PRINT/PUNCH SERVICE -- SETUP INFO LOAD'    RUA13Q1 Q4808100
         COPY  #UA13Q11             GET SETUP INFORMATION       RUA13Q1 Q4808110
         TITLE 'HASP PRINT/PUNCH SERVICE -- LINE SPACING'       RUA13Q1 Q4808120
         COPY  #UA13Q12             HANDLE LINE SPACING         RUA13Q1 Q4808130
         TITLE 'HASP PRINT/PUNCH SERVICE -- CHANNEL SKIPPING'   RUA13Q1 Q4808140
         COPY  #UA13Q13             HANDLE SKIPS                RUA13Q1 Q4808150
         COPY  #UA13Q15             COPY COUNTING CODE          RUA13Q1 Q4870100
         COPY  #UA13Q14             HANDLE COMMENT SPACING      RUA13Q1 Q5081600
         LTORG ,                                                RUA13Q1 Q5101100
         TM    PCEID,PCEPRSID       PRINTER?                    RUA13Q1 Q5248300
         BO    #PSMF6#1             YES                         RUA13Q1 Q5248310
         XC    PRPAGECT,PRPAGECT    NO, ZERO PAGE COUNT         RUA13Q1 Q5248320
#PSMF6#1 NULL  ,                                                RUA13Q1 Q5248330
++MAC (#UA13Q11) ASSEM(HASPPRPU) DISTLIB(RICEHSRC).
****************************************************************RUA13Q1
*        LOCATE THE CURRENT FORM, CTAPE, AND TRAIN IN THE GLOBALRUA13Q1
*        TABLES AND STORE THE NECESSARY INFORMATION IN PPPWORK  RUA13Q1
****************************************************************RUA13Q1
         SPACE 1                                                RUA13Q1
         PUSH  USING                                            RUA13Q1
         USING #PAGESET,BASE2     PROVIDE LOCAL ADDRESSABILITY  RUA13Q1
         USING PDBDSECT,PC2       PROVIDE PDDB ADDRESSABILITY   RUA13Q1
         SPACE 1                                                RUA13Q1
#PAGESET ST    BASE2,PPSAVE1      SAVE PREVIOUS BASE REG        RUA13Q1
         ST    R14,PPSAVE1+4        AND LINK REGISTER           RUA13Q1
         LR    BASE2,R15          LOAD BASE REGISTER            RUA13Q1
         SPACE 1                                                RUA13Q1
****************************************************************RUA13Q1
*        FIND THE CTAPE INFORMATION                             RUA13Q1
****************************************************************RUA13Q1
         SPACE 1                                                RUA13Q1
         ICM   PC1,15,$CTAPTBL    POINT TO CTAPE TABLE          RUA13Q1
         BZ    FORMSGET           SKIP IF NONE                  RUA13Q1
         USING #CTAPTBL,PC1                                     RUA13Q1
         L     R15,#CTP#ENT       GET NUMBER OF ENTRIES         RUA13Q1
         LA    PC1,#CTPENT1       POINT TO FIRST ENTRY          RUA13Q1
         NI    PPIFLG1,X'FF'-(PPI1CDEF+PPI1CSTR) TURN OFF FLAGS RUA13Q1
         CLC   PDBFCB,=CL4'****'  ANY VALUE SPECIFIED?          RUA13Q1
         BNE   CTAPFIND           YES, PROCEED                  RUA13Q1
         OI    PPIFLG1,PPI1CSTR   NO, INDICATE AS SUCH          RUA13Q1
         USING #CTAPENT,PC1       MAP A CTAPE ENTRY             RUA13Q1
CTAPFIND CLC   #CTAPNAM,PDBFCB    MATCH?                        RUA13Q1
         BE    CTAPFOUN           YES, GO FILL IN THE BLANKS    RUA13Q1
         LA    PC1,#CTPELEN(PC1)  NO, TRY NEXT ENTRY            RUA13Q1
         BCT   R15,CTAPFIND         AND TRY AGAIN               RUA13Q1
         SPACE 1                                                RUA13Q1
         OI    PPIFLG1,PPI1CDEF   NOT FOUND.  MARK DEFAULT USED RUA13Q1
         L     PC1,$CTAPTBL       AND POINT BACK TO FIRST ENTRY RUA13Q1
         USING #CTAPTBL,PC1                                     RUA13Q1
         LA    PC1,#CTPENT1                                     RUA13Q1
         USING #CTAPENT,PC1                                     RUA13Q1
         OI    PPIFLG2,PPI2CSET   FORCE SETUP CHARGES           RUA13Q1
         SPACE 1                                                RUA13Q1
CTAPFOUN NI    PPIFLG1,X'FF'-PPI18LPI TURN OFF 8 LPI FLAG       RUA13Q1
         CLI   #CTAPLPI+1,X'08'   8 OR 6 LPI?                   RUA13Q1
         BNE   CTAP6LPI           6 LPI                         RUA13Q1
         OI    PPIFLG1,PPI18LPI   INDICATE 8 LPI                RUA13Q1
CTAP6LPI EQU   *                                                RUA13Q1
         TM    PPIFLG1,PPI1CDEF   UNKNOWN CTAPE?                RUA13Q1
         BO    CTAPNSET           LEAVE SETUP FLAG ON IF SO     RUA13Q1
         NI    PPIFLG2,X'FF'-PPI2CSET TURN OFF CTAPE SETUP FLAG RUA13Q1
         TM    #CTAPFLG,#CTP$SET  SETUP CHARGE REQUIRED?        RUA13Q1
         BNO   CTAPNSET           NO                            RUA13Q1
         OI    PPIFLG2,PPI2CSET   YES, INDICATE SO              RUA13Q1
CTAPNSET MVC   PPCTPLEN,#CTAPLEN  COPY LENGTH OF CARRIAGE TAPE  RUA13Q1
         LA    PL,#CTAPMAP        POINT TO PUNCH MAP            RUA13Q1
         ST    PL,PPCTPMAP        SAVE POINTER                  RUA13Q1
         DROP  PC1                THROW AWAY CTAPE ENTRY POINTERRUA13Q1
         SPACE 2                                                RUA13Q1
****************************************************************RUA13Q1
*        NOW FIND THE FORMS INFORMATION                         RUA13Q1
****************************************************************RUA13Q1
         SPACE 1                                                RUA13Q1
FORMSGET ICM   PC1,15,$FORMTBL    POINT TO FORMS TABLE          RUA13Q1
         BZ    TRANSTAT           SKIP IF NONE                  RUA13Q1
         USING #FORMTBL,PC1                                     RUA13Q1
         L     R15,#FRM#ENT       GET NUMBER OF ENTRIES         RUA13Q1
         LA    PC1,#FRMENT1       POINT TO FIRST ENTRY          RUA13Q1
         NI    PPIFLG1,X'FF'-PPI1FDEF TURN OFF DEFAULT FLAG     RUA13Q1
         USING #FORMENT,PC1       MAP A FORM ENTRY              RUA13Q1
FORMFIND CLC   #FRMNAME,PDBFORMS  MATCH?                        RUA13Q1
         BE    FORMFOUN           YES, GO FILL IN THE BLANKS    RUA13Q1
         LA    PC1,#FRMELEN(PC1)  NO, TRY NEXT ENTRY            RUA13Q1
         BCT   R15,FORMFIND         AND TRY AGAIN               RUA13Q1
         SPACE 1                                                RUA13Q1
         OI    PPIFLG1,PPI1FDEF   NOT FOUND.  MARK DEFAULT USED RUA13Q1
         L     PC1,$FORMTBL       AND POINT BACK TO FIRST ENTRY RUA13Q1
         USING #FORMTBL,PC1                                     RUA13Q1
         LA    PC1,#FRMENT1                                     RUA13Q1
         USING #FORMENT,PC1                                     RUA13Q1
         OI    PPIFLG2,PPI2FSET   FORCE ON SETUP CHARGE FLAG    RUA13Q1
         B     FORMNSET                                         RUA13Q1
         SPACE 1                                                RUA13Q1
FORMFOUN EQU   *                                                RUA13Q1
         NI    PPIFLG2,X'FF'-PPI2FSET TURN OFF FORMS SETUP FLAG RUA13Q1
         TM    #FRMFLGS,#FRM$SET  SETUP CHARGE REQUIRED?        RUA13Q1
         BNO   FORMNSET           NO                            RUA13Q1
         OI    PPIFLG2,PPI2FSET   YES, INDICATE SO              RUA13Q1
FORMNSET NULL  ,                                                RUA13Q1
         MVC   PPFRMCST,#FRMCST   COPY FORM COST                RUA13Q1
         TM    PPIFLG1,PPI18LPI   8 LPI?                        RUA13Q1
         BO    FORM8LPI           YES, USE PROPER LENGTH        RUA13Q1
         MVC   PPFRMLEN,#FRM6LPI  NO, USE 6 LPI LENGTH          RUA13Q1
         B     TRANSTAT           GO TO TRAIN STATION           RUA13Q1
         SPACE 1                                                RUA13Q1
FORM8LPI EQU   *                                                RUA13Q1
         MVC   PPFRMLEN,#FRM8LPI  COPY 8 LPI LENGTH             RUA13Q1
         SPACE 2                                                RUA13Q1
****************************************************************RUA13Q1
*        NOW FIND THE TRAIN INFORMATION                         RUA13Q1
****************************************************************RUA13Q1
         SPACE 1                                                RUA13Q1
TRANSTAT EQU   *                                                RUA13Q1
         ICM   PC1,R15,$TRAINS    POINT TO GLOBAL TABLE         RUA13Q1
         BZ    #ROUTCHK           SKIP IF NONE                  RUA13Q1
         USING #TRAINS,PC1                                      RUA13Q1
         L     R15,#TRN#ENT       GET NUMBER OF TRAINS          RUA13Q1
         LA    PC1,#TRNENT1       POINT TO FIRST ENTRY          RUA13Q1
         NI    PPIFLG1,X'FF'-(PPI1TDEF+PPI1TSTR) TURN OFF FLAGS RUA13Q1
         CLC   PDBUCS,=CL4'****'  ANY VALUE SPECIFIED?          RUA13Q1
         BNE   TRANFIND           YES, PROCEED                  RUA13Q1
         OI    PPIFLG1,PPI1TSTR   NO, INDICATE AS SUCH          RUA13Q1
         USING #TRANENT,PC1                                     RUA13Q1
TRANFIND CLC   #TRANAME,PDBUCS    MATCH?                        RUA13Q1
         BE    TRANFOUN           YES, GO COPY DATA             RUA13Q1
         LA    PC1,#TRNELEN(PC1)  NO, TRY NEXT ONE              RUA13Q1
         BCT   R15,TRANFIND                                     RUA13Q1
         OI    PPIFLG1,PPI1TDEF   INDICATE DEFAULT TRAIN USED   RUA13Q1
         L     PC1,$TRAINS        POINT TO GLOBAL TABLE         RUA13Q1
         USING #TRAINS,PC1                                      RUA13Q1
         LA    PC1,#TRNENT1       POINT TO FIRST ENTRY          RUA13Q1
         USING #TRANENT,PC1                                     RUA13Q1
         OI    PPIFLG2,PPI2TSET   FORCE ON TRAIN SETUP FLAG     RUA13Q1
         B     TRANNSET                                         RUA13Q1
         SPACE 1                                                RUA13Q1
TRANFOUN EQU   *                                                RUA13Q1
         NI    PPIFLG2,X'FF'-PPI2TSET TURN OFF TRAIN SETUP FLAG RUA13Q1
         TM    #TRANFLG,#TRN$SET  SETUP CHARGE REQUIRED?        RUA13Q1
         BNO   TRANNSET           NO                            RUA13Q1
         OI    PPIFLG2,PPI2TSET   YES, INDICATE SO              RUA13Q1
TRANNSET NULL  ,                                                RUA13Q1
         MVC   PPTRNCST,#TRNCST   COPY MULTIPLIER               RUA13Q1
         DROP  PC1                                              RUA13Q1
         SPACE 1                                                RUA13Q1
****************************************************************RUA13Q1
*        DETERMINE WHETHER OR NOT THIS PRINTER IS PRINTING      RUA13Q1
*        ON A DEVICE WHERE WE CHARGE FOR FORMS/LINES            RUA13Q1
****************************************************************RUA13Q1
         SPACE 1                                                RUA13Q1
#ROUTCHK OI    PPIFLG1,PPI1LOCL   TURN ON LOCAL FLAG            RUA13Q1
         TM    PCEID,PCELCLID     IS IT LOCAL?                  RUA13Q1
         BO    #UA13Q11           YES, LEAVE FLAG SET           RUA13Q1
         L     PC1,PCEDCT         FIND THE DCT                  RUA13Q1
         USING DCTDSECT,PC1                                     RUA13Q1
         CLI   DCTNO,X'00'        REMOTE WITH LOCAL ROUTE?      RUA13Q1
         DROP  PC1                                              RUA13Q1
         BE    #UA13Q11           YES, KEEP CHARGING            RUA13Q1
         NI    PPIFLG1,X'FF'-PPI1LOCL NO, TURN OFF FOR REMOTES  RUA13Q1
#UA13Q11 EQU   *                                                RUA13Q1
         SPACE 1                                                RUA13Q1
****************************************************************RUA13Q1
*        PICK UP APPROPRIATE GLOBAL NUMBERS                     RUA13Q1
****************************************************************RUA13Q1
         SPACE 1                                                RUA13Q1
         MVC   PBSETCST,$BSTCST   COPY GLOBAL NUMBERS           RUA13Q1
         MVC   PFSETCST,$FSTCST                                 RUA13Q1
         MVC   PCSETCST,$CSTCST                                 RUA13Q1
         MVC   PTSETCST,$TSTCST                                 RUA13Q1
         MVC   PLINECST,$LNECST                                 RUA13Q1
         MVC   PPNCHCST,$PUNCST                                 RUA13Q1
         SPACE 1                                                RUA13Q1
         L     BASE2,PPSAVE1      RELOAD USUAL BASE REGISTER    RUA13Q1
         L     R14,PPSAVE1+4        AND LINK REGISTER           RUA13Q1
         BR    R14                RETURN                        RUA13Q1
         SPACE 2                                                RUA13Q1
         LTORG ,                                                RUA13Q1
         POP   USING                                            RUA13Q1
++MAC (#UA13Q12) ASSEM(HASPPRPU) DISTLIB(RICEHSRC).
         SPACE 1                                                RUA13Q1
****************************************************************RUA13Q1
*        CHECK MOVEMENT AGAINST CARRIAGE TAPE AND FORMS LENGTH, RUA13Q1
*        UPDATING BOTH POSITIONS.  IF A NEW FORM IS REQUIRED,   RUA13Q1
*        INCREMENT THE CHARGABLE FORMS PAGE COUNT (PRPAGECT).   RUA13Q1
****************************************************************RUA13Q1
         SPACE 1                                                RUA13Q1
         PUSH  USING                                            RUA13Q1
         USING #LINESP,BASE2      PROVIDE LOCAL ADDRESSABILITY  RUA13Q1
         SPACE 1                                                RUA13Q1
#LINESP  ST    BASE2,PPSAVE1      SAVE PREVIOUS BASE REGISTER   RUA13Q1
         ST    R14,PPSAVE1+4        AND LINK REGISTER           RUA13Q1
         LR    BASE2,R15          LOAD BASE REGISTER            RUA13Q1
         SPACE 1                                                RUA13Q1
         SLR   PL,PL              CLEAR FOR INSERT              RUA13Q1
         IC    PL,PCCWORK           OF SPACING COUNT            RUA13Q1
         SRL   PL,3               ISOLATE SPACING BITS          RUA13Q1
         AH    PL,PPCTPLIN        INCREMENT CTAPE LINE          RUA13Q1
         CH    PL,PPCTPLEN        WRAP AROUND                   RUA13Q1
         BNH   #UA13Q21             IF                          RUA13Q1
         SH    PL,PPCTPLEN            NECESSARY                 RUA13Q1
#UA13Q21 EQU   *                                                RUA13Q1
         STH   PL,PPCTPLIN        SAVE NEW POSITION             RUA13Q1
         SLR   PL,PL              NOW DO IT AGAIN FOR FORMS     RUA13Q1
         IC    PL,PCCWORK                                       RUA13Q1
         SRL   PL,3               COMPUTE MOVEMENT AMOUNT       RUA13Q1
         AH    PL,PPFRMLIN        INCREMENT FORMS LINE          RUA13Q1
         STH   PL,PPFRMLIN        SAVE NEW POSITION ON PAGE     RUA13Q1
         CH    PL,PPFRMLEN        WRAP AROUND TO                RUA13Q1
         BNH   #UA13Q22             NEXT PAGE                   RUA13Q1
         SH    PL,PPFRMLEN            IF NECESSARY              RUA13Q1
         STH   PL,PPFRMLIN        SAVE NEW POSITION ON PAGE     RUA13Q1
         CLC   PDDBSKIP,PDDBPGCT  ARE WE REPOSITIONING?         RUA13Q1
         BH    #UA13Q22           YES, LEAVE PAGE COUNT ALONE   RUA13Q1
         SLR   PL,PL              NO, INCREMENT CHARGABLE PAGE  RUA13Q1
         IC    PL,PREVCPYN        ADD NUMBER OF PAGES           RUA13Q1
         AL    PL,PRPAGECT          THIS TRANSMISSION           RUA13Q1
         ST    PL,PRPAGECT                                      RUA13Q1
         SPACE 1                                                RUA13Q1
#UA13Q22 EQU   *                                                RUA13Q1
         L     BASE2,PPSAVE1      RESTORE PREVIOUS BASE REGISTERRUA13Q1
         L     R14,PPSAVE1+4        AND LINK REGISTER           RUA13Q1
         BR    R14                RETURN                        RUA13Q1
         SPACE 1                                                RUA13Q1
         LTORG ,                                                RUA13Q1
         POP   USING                                            RUA13Q1
++MAC (#UA13Q13) ASSEM(HASPPRPU) DISTLIB(RICEHSRC).
****************************************************************RUA13Q1
*        COMPUTE NEW POSITION AFTER A SKIP-TO-CHANNEL.  IF A NEWRUA13Q1
*        FORM IS REQUIRED, INDICATE SO.                         RUA13Q1
****************************************************************RUA13Q1
         SPACE 1                                                RUA13Q1
         PUSH  USING                                            RUA13Q1
         USING #CHANSK,BASE2      PROVIDE LOCAL ADDRESSABILITY  RUA13Q1
         SPACE 1                                                RUA13Q1
#CHANSK  ST    BASE2,PPSAVE1      SAVE PREVIOUS BASE REGISTER   RUA13Q1
         ST    R14,PPSAVE1+4        AND LINK REGISTER           RUA13Q1
         LR    BASE2,R15          GET NEW BASE REGISTER         RUA13Q1
         SPACE 1                                                RUA13Q1
         SLR   PL,PL              GET SKIP-TO NUMBER            RUA13Q1
         IC    PL,PCCWORK                                       RUA13Q1
         SLL   PL,25                                            RUA13Q1
         SRL   PL,28              ISOLATE SKIP BITS             RUA13Q1
         XC    PMESSAGE(13),PMESSAGE CLEAR WORK AREA            RUA13Q1
         STC   PL,PMESSAGE(PL)    INSERT SKIP BYTE              RUA13Q1
         L     R15,PPCTPMAP       POINT TO PUNCH MAP            RUA13Q1
         LH    PL,PPCTPLEN        FIND OUT HOW MANY LINES       RUA13Q1
         SH    PL,PPCTPLIN          ARE LEFT ON THE CTAPE       RUA13Q1
         BNZ   #UA13Q31           SOME LEFT                     RUA13Q1
         LH    PL,PPCTPLEN        NONE, GET LENGTH OF CTAPE     RUA13Q1
         BCTR  PL,0               FIX FOR EXECUTE               RUA13Q1
         EX    PL,#UA13Q3A        FIND THE PUNCH                RUA13Q1
         BZ    #UA13Q32           NONE ON THE TAPE AT ALL       RUA13Q1
         SR    R1,R15             FOUND WHERE?                  RUA13Q1
         LA    R1,1(,R1)          CORRECT VALUE                 RUA13Q1
         STH   R1,$DOUBLE         SAVE MOTION                   RUA13Q1
         STH   R1,PPCTPLIN        SAVE IT FOR LATER             RUA13Q1
         B     #UA13Q33             AND CONTINUE                RUA13Q1
         SPACE 1                                                RUA13Q1
#UA13Q3A TRT   0(*-*,R15),PMESSAGE ***** EXECUTED ONLY *****    RUA13Q1
         SPACE 1                                                RUA13Q1
#UA13Q32 EQU   *                                                RUA13Q1
****************************************************************RUA13Q1
*        THIS CASE IS REALLY A DISASTER SINCE THIS MEANS THAT   RUA13Q1
*        USER IS SKIPPING TO A PUNCH NOT ON THE CARRIAGE TAPE ATRUA13Q1
*        ALL.  THIS CAUSES AN INFINITE PAGE EJECT.  ANY VALUE WERUA13Q1
*        USE FOR THE MOTION HERE IS AS GOOD AS ANYTHING.        RUA13Q1
****************************************************************RUA13Q1
         SPACE 1                                                RUA13Q1
         LH    PL,PPFRMLEN        SET MOTION TO FORMS LENGTH    RUA13Q1
         STH   PL,$DOUBLE         SAVE IN WORK AREA             RUA13Q1
         LA    PL,1               SET CTAPE POSITION TO         RUA13Q1
         STH   PL,PPCTPLIN          FIRST LINE                  RUA13Q1
         B     #UA13Q33           AND CONTINUE THIS FANTASY     RUA13Q1
         SPACE 2                                                RUA13Q1
****************************************************************RUA13Q1
*        THIS CASE MEANS THAT WE NEED TO LOOK FOR THE PUNCH     RUA13Q1
*        ON THIS GO-ROUND OF THE CARRIAGE TAPE.                 RUA13Q1
****************************************************************RUA13Q1
         SPACE 1                                                RUA13Q1
#UA13Q31 EQU   *                                                RUA13Q1
         AH    R15,PPCTPLIN       POINT TO RIGHT PLACE ON CTAPE RUA13Q1
         BCTR  PL,0               FIX FOR EXECUTE               RUA13Q1
         EX    PL,#UA13Q3A        FIND PUNCH ON REST OF CTAPE   RUA13Q1
         BZ    #UA13Q34           NOT FOUND, LOOK FROM START    RUA13Q1
         SPACE 1                                                RUA13Q1
****************************************************************RUA13Q1
*        THIS CASE MEANS THAT WE FOUND THE PUNCH BEFORE THE END RUA13Q1
*        OF THIS GO-ROUND OF THE CARRIAGE TAPE.  WE MAY HAVE    RUA13Q1
*        GONE TO A NEW FORM PAGE, HOWEVER.                      RUA13Q1
****************************************************************RUA13Q1
         SPACE 1                                                RUA13Q1
         SR    R1,R15             FOUND WHERE?                  RUA13Q1
         LA    R1,1(,R1)          CORRECT VALUE                 RUA13Q1
         STH   R1,$DOUBLE         SAVE MOTION                   RUA13Q1
         LH    PL,PPCTPLIN          AND UPDATE CTAPE POSITION   RUA13Q1
         AR    PL,R1                                            RUA13Q1
         STH   PL,PPCTPLIN        SAVE NEW CTAPE POSITION       RUA13Q1
         B     #UA13Q33           AND CONTINUE                  RUA13Q1
         SPACE 1                                                RUA13Q1
****************************************************************RUA13Q1
*        THIS CASE MEANS THAT WE HAD TO WRAP AROUND THE END     RUA13Q1
*        OF THE CARRIAGE TAPE TO FIND THE PUNCH.  WE MAY HAVE   RUA13Q1
*        GONE TO A NEW FORM PAGE (AND WE MAY NOT FIND THE PUNCH RUA13Q1
*        AT ALL).                                               RUA13Q1
****************************************************************RUA13Q1
         SPACE 1                                                RUA13Q1
#UA13Q34 EQU   *                                                RUA13Q1
         LA    PL,1(,PL)          SAVE REST OF TAPE MOTION      RUA13Q1
         STH   PL,$DOUBLE           FOR LATER USE               RUA13Q1
         LH    PL,PPCTPLEN        GET FULL LENGTH OF CTAPE      RUA13Q1
         BCTR  PL,0               FIX FOR EXECUTE               RUA13Q1
         L     R15,PPCTPMAP       POINT BACK AT BEGINNING       RUA13Q1
         EX    PL,#UA13Q3A        FIND PUNCH ON CTAPE           RUA13Q1
         BZ    #UA13Q32           NOT THERE AT ALL              RUA13Q1
         SR    R1,R15             FOUND WHERE?                  RUA13Q1
         LA    R1,1(,R1)          CORRECT VALUE                 RUA13Q1
         STH   R1,PPCTPLIN        SAVE NEW CTAPE POSITION       RUA13Q1
         AH    R1,$DOUBLE         INCLUDE PREVIOUS MOTION       RUA13Q1
         STH   R1,$DOUBLE         AND SAVE FOR LATER            RUA13Q1
         SPACE 2                                                RUA13Q1
****************************************************************RUA13Q1
*        NOW $DOUBLE CONTAINS THE AMOUNT OF MOTION NECESSARY TO RUA13Q1
*        FIND THE SKIP-TO PUNCH.  SEE IF WE HAVE GONE TO A      RUA13Q1
*        NEW FORM PAGE.                                         RUA13Q1
****************************************************************RUA13Q1
         SPACE 1                                                RUA13Q1
#UA13Q33 EQU   *                                                RUA13Q1
         LH    PL,PPFRMLIN        UPDATE POSITION ON FORM       RUA13Q1
         AH    PL,$DOUBLE                                       RUA13Q1
         STH   PL,PPFRMLIN        SAVE NEW POSITION             RUA13Q1
         NI    PPIFLG1,X'FF'-PPI1PAGE   TURN OFF NEWPAGE FLAG   RUA13Q1
         CH    PL,PPFRMLEN        NEW PAGE REQUIRED?            RUA13Q1
         BNH   #UA13Q35           NO, JUST CONTINUE             RUA13Q1
         SH    PL,PPFRMLEN        YES, SET TO NEW POSITION      RUA13Q1
         STH   PL,PPFRMLIN                                      RUA13Q1
         OI    PPIFLG1,PPI1PAGE   INDICATE NEW PAGE REQUIRED    RUA13Q1
         SPACE 1                                                RUA13Q1
#UA13Q35 EQU   *                                                RUA13Q1
         SPACE 1                                                RUA13Q1
         L     BASE2,PPSAVE1      RESTORE PREVIOUS BASE REGISTERRUA13Q1
         L     R14,PPSAVE1+4        AND LINK REGISTER           RUA13Q1
         BR    R14                RETURN                        RUA13Q1
         SPACE 1                                                RUA13Q1
         LTORG ,                                                RUA13Q1
         POP   USING                                            RUA13Q1
++MAC (#UA13Q14) ASSEM(HASPPRPU) DISTLIB(RICEHSRC).
         LH    PW,PPCTPLIN          INCREMENT                   RUA13Q1
         LA    PW,4(,PW)              CTAPE POSITION            RUA13Q1
         CH    PW,PPCTPLEN          WRAP                        RUA13Q1
         BNH   #UA13Q41               AROUND IF                 RUA13Q1
         SH    PW,PPCTPLEN              NECESSARY               RUA13Q1
#UA13Q41 STH   PW,PPCTPLIN          STORE NEW POSITION          RUA13Q1
         LH    PW,PPFRMLIN          INCREMENT                   RUA13Q1
         LA    PW,4(,PW)              FORM POSITION             RUA13Q1
         CH    PW,PPFRMLEN          WRAP                        RUA13Q1
         BNH   #UA13Q42               AROUND IF                 RUA13Q1
         SH    PW,PPFRMLEN              NECESSARY               RUA13Q1
         SLR   PL,PL                INCREMENT                   RUA13Q1
         IC    PL,PREVCPYN            CHARGABLE                 RUA13Q1
         AL    PL,PRPAGECT              PAGE COUNT              RUA13Q1
         ST    PL,PRPAGECT                IF SO                 RUA13Q1
#UA13Q42 STH   PW,PPFRMLIN          STORE NEW FORM POSITION     RUA13Q1
++MAC (#UA13Q15) ASSEM(HASPPRPU) DISTLIB(RICEHSRC).
         XC    $DOUBLE,$DOUBLE    CLEAR COST ACCUMULATOR        RUA13Q1
         L     PL,PCEFORM         FIND PAGE TYPE                RUA13Q1
         CLI   1(PL),C'E'         IS THIS AN END PAGE?          RUA13Q1
         BNE   #UA13Q51           NO, SKIP COUNTING CODE        RUA13Q1
#UA13Q50 ST    R1,PLNDISPL        SAVE LINE DISPLACEMENT        RUA13Q1
         LA    PC1,0(R1,PBUF)     GET NEW LINE BUFFER ADDRESS   RUA13Q1
         MVI   0(PC1),C' '        BLANK OUT                     RUA13Q1
         MVC   1(131,PC1),0(PC1)    THE LINE                    RUA13Q1
         MVC   0(7,PC1),=C'SERVICE' SET HEADER LINE             RUA13Q1
         MVC   17(6,PC1),=C'AMOUNT'                             RUA13Q1
         MVC   29(4,PC1),=C'COST'                               RUA13Q1
         AL    PC1,PRCCWID        CONSTRUCT                     RUA13Q1
         L     PC2,PRCCWID+4        PRINT CCW                   RUA13Q1
         BAL   PL,PPPUT           ADD CCW TO CHAIN              RUA13Q1
         TM    PCEID,PCELCLID     A TRUE LOCAL DEVICE?          RUA13Q1
         BNO   #UA13Q52           SKIP DISPATCHING LINE IF NOT  RUA13Q1
         L     R1,PLNDISPL        FIND NEW                      RUA13Q1
         LA    R1,132(,R1)          LINE DISPLACEMENT           RUA13Q1
         LA    R4,132(,R1)        CHECK FOR ROOM                RUA13Q1
         CH    R4,$BUFLENG          IN BUFFER                   RUA13Q1
         BNH   #UA13Q53           BR IF YES                     RUA13Q1
         BAL   PL,PPWRITE         FORCE WRITE                   RUA13Q1
         BAL   PL,PPCHECK         CHECK WRITE                   RUA13Q1
         LA    R1,BUFSTART-BUFDSECT SET STARTING LINE DISPLACEMENT 13Q1
#UA13Q53 ST    R1,PLNDISPL        STORE NEW DISPLACEMENT        RUA13Q1
         LA    PC1,0(R1,PBUF)     GET NEW LINE BUFFER ADDRESS   RUA13Q1
         MVI   0(PC1),C' '        BLANK OUT                     RUA13Q1
         MVC   1(131,PC1),0(PC1)    THE LINE                    RUA13Q1
         MVC   0(11,PC1),=C'DISPATCHING' DISPATCHING LINE       RUA13Q1
         MVC   22(11,PC1),=C'1      ----' ASSUME NO CHARGE      RUA13Q1
         ICM   PL,15,PBSETCST     CHECK RATE FOR ZERO           RUA13Q1
         BZ    #UA13Q54           SKIP IF IT IS                 RUA13Q1
         AL    PL,$DOUBLE                                       RUA13Q1
         ST    PL,$DOUBLE         INCREMENT TOTAL COST          RUA13Q1
         L     R15,PBSETCST       GET BASE COST BACK AGAIN      RUA13Q1
         SLR   R14,R14                                          RUA13Q1
         D     R14,=F'1000'       GET NUMBER IN CENTS           RUA13Q1
         MVC   24(10,PC1),=X'4020202021204B2020'                RUA13Q1
         LA    R1,29(,PC1)        POINT TO FORCE SIGNIF DIGIT   RUA13Q1
         CVD   R15,PMESSAGE                                     RUA13Q1
         EDMK  24(10,PC1),PMESSAGE+4                            RUA13Q1
         BCTR  R1,0                                             RUA13Q1
         MVI   0(R1),C'$'         INSERT DOLLAR SIGN            RUA13Q1
#UA13Q54 AL    PC1,PRCCWID        CONSTRUCT                     RUA13Q1
         L     PC2,PRCCWID+4        PRINT CCW                   RUA13Q1
         BAL   PL,PPPUT           ADD CCW TO CHAIN              RUA13Q1
#UA13Q52 L     R1,PLNDISPL        FIND NEW                      RUA13Q1
         LA    R1,132(,R1)          LINE DISPLACEMENT           RUA13Q1
         LA    R4,132(,R1)        CHECK FOR ROOM                RUA13Q1
         CH    R4,$BUFLENG          IN BUFFER                   RUA13Q1
         BNH   #UA13Q55           BR IF YES                     RUA13Q1
         BAL   PL,PPWRITE         FORCE WRITE                   RUA13Q1
         BAL   PL,PPCHECK         CHECK WRITE                   RUA13Q1
         LA    R1,BUFSTART-BUFDSECT SET STARTING LINE DISPLACEMENT 13Q1
#UA13Q55 ST    R1,PLNDISPL        STORE NEW DISPLACEMENT        RUA13Q1
         LA    PC1,0(R1,PBUF)     GET NEW LINE BUFFER ADDRESS   RUA13Q1
         MVI   0(PC1),C' '        BLANK OUT                     RUA13Q1
         MVC   1(131,PC1),0(PC1)    THE LINE                    RUA13Q1
         L     PL,PCEDCT          FIND THE DCT                  RUA13Q1
         USING DCTDSECT,PL        ADDRESS IT                    RUA13Q1
         MVC   0(4,PC1),=C'FORM'  INSERT FORM                   RUA13Q1
         MVC   6(4,PC1),DCTFORMS                                RUA13Q1
         MVC   11(5,PC1),=C'SETUP'                              RUA13Q1
         MVC   22(11,PC1),=C'-      ----' ASSUME NO CHARGE      RUA13Q1
         DROP  PL                                               RUA13Q1
         TM    PPIFLG2,PPI2FSET   SETUP CHARGE FOR THIS FORM?   RUA13Q1
         BNO   #UA13Q56           SKIP IF NOT                   RUA13Q1
         TM    PPIFLG1,PPI1LOCL   "LOCAL" DEVICE?               RUA13Q1
         BNO   #UA13Q56           SKIP SETUP COST IF NOT        RUA13Q1
         MVI   22(PC1),C'1'       INDICATE SINGLE CHARGE        RUA13Q1
         ICM   PL,15,PFSETCST     SEE IF ANY SETUP COST         RUA13Q1
         BZ    #UA13Q56           SKIP SETUP COST IF SO         RUA13Q1
         AL    PL,$DOUBLE                                       RUA13Q1
         ST    PL,$DOUBLE         INCREMENT TOTAL COST          RUA13Q1
         L     R15,PFSETCST       GET FORMS SETUP COST BACK     RUA13Q1
         SLR   R14,R14                                          RUA13Q1
         D     R14,=F'1000'       GET NUMBER IN CENTS           RUA13Q1
         MVC   24(10,PC1),=X'4020202021204B2020'                RUA13Q1
         LA    R1,29(,PC1)        POINT TO FORCE SIGNIF DIGIT   RUA13Q1
         CVD   R15,PMESSAGE                                     RUA13Q1
         EDMK  24(10,PC1),PMESSAGE+4                            RUA13Q1
         BCTR  R1,0                                             RUA13Q1
         MVI   0(R1),C'$'         INSERT DOLLAR SIGN            RUA13Q1
#UA13Q56 TM    PPIFLG1,PPI1FDEF   UNKNOWN FORM?                 RUA13Q1
         BNO   #UA13Q57           NO, SKIP THIS                 RUA13Q1
         MVC   35(40,PC1),=C'UNKNOWN FORM - DEFAULT PAGE SIZE ASSUMED'
#UA13Q57 AL    PC1,PRCCWID        CONSTRUCT                     RUA13Q1
         L     PC2,PRCCWID+4        PRINT CCW                   RUA13Q1
         BAL   PL,PPPUT           ADD CCW TO CHAIN              RUA13Q1
         L     R1,PLNDISPL        FIND NEW                      RUA13Q1
         LA    R1,132(,R1)          LINE DISPLACEMENT           RUA13Q1
         LA    R4,132(,R1)        CHECK FOR ROOM                RUA13Q1
         CH    R4,$BUFLENG          IN BUFFER                   RUA13Q1
         BNH   #UA13Q58           BR IF YES                     RUA13Q1
         BAL   PL,PPWRITE         FORCE WRITE                   RUA13Q1
         BAL   PL,PPCHECK         CHECK WRITE                   RUA13Q1
         LA    R1,BUFSTART-BUFDSECT SET STARTING LINE DISPLACEMENT 13Q1
#UA13Q58 ST    R1,PLNDISPL        STORE NEW DISPLACEMENT        RUA13Q1
         LA    PC1,0(R1,PBUF)     GET NEW LINE BUFFER ADDRESS   RUA13Q1
         MVI   0(PC1),C' '        BLANK OUT                     RUA13Q1
         MVC   1(131,PC1),0(PC1)    THE LINE                    RUA13Q1
         L     PL,PCEDCT          FIND THE DCT                  RUA13Q1
         USING DCTDSECT,PL        ADDRESS IT                    RUA13Q1
         MVC   0(5,PC1),=C'CTAPE' INSERT CTAPE                  RUA13Q1
         MVC   6(4,PC1),DCTFCB                                  RUA13Q1
         MVC   11(5,PC1),=C'SETUP'                              RUA13Q1
         MVC   22(11,PC1),=C'-      ----' ASSUME NO CHARGE      RUA13Q1
         DROP  PL                                               RUA13Q1
         TM    PPIFLG2,PPI2CSET   SETUP CHARGE FOR THIS CTAPE?  RUA13Q1
         BNO   #UA13Q59           SKIP IF NOT                   RUA13Q1
         TM    PPIFLG1,PPI1LOCL   "LOCAL" DEVICE?               RUA13Q1
         BNO   #UA13Q59           SKIP SETUP COST IF NOT        RUA13Q1
         MVI   22(PC1),C'1'       INDICATE SINGLE CHARGE        RUA13Q1
         ICM   PL,15,PCSETCST     SEE IF ANY SETUP COST         RUA13Q1
         BZ    #UA13Q59           SKIP SETUP COST IF SO         RUA13Q1
         AL    PL,$DOUBLE                                       RUA13Q1
         ST    PL,$DOUBLE         INCREMENT TOTAL COST          RUA13Q1
         L     R15,PCSETCST       GET SETUP COST BACK           RUA13Q1
         SLR   R14,R14                                          RUA13Q1
         D     R14,=F'1000'       GET NUMBER IN CENTS           RUA13Q1
         MVC   24(10,PC1),=X'4020202021204B2020'                RUA13Q1
         LA    R1,29(,PC1)        POINT TO FORCE SIGNIF DIGIT   RUA13Q1
         CVD   R15,PMESSAGE                                     RUA13Q1
         EDMK  24(10,PC1),PMESSAGE+4                            RUA13Q1
         BCTR  R1,0                                             RUA13Q1
         MVI   0(R1),C'$'         INSERT DOLLAR SIGN            RUA13Q1
#UA13Q59 TM    PPIFLG1,PPI1CDEF   UNKNOWN CTAPE?                RUA13Q1
         BNO   #UA13Q5A           NO, SKIP THIS                 RUA13Q1
         MVC   35(38,PC1),=C'UNKNOWN CTAPE - DEFAULT LAYOUT ASSUMED'
#UA13Q5A AL    PC1,PRCCWID        CONSTRUCT                     RUA13Q1
         L     PC2,PRCCWID+4        PRINT CCW                   RUA13Q1
         BAL   PL,PPPUT           ADD CCW TO CHAIN              RUA13Q1
         L     R1,PLNDISPL        FIND NEW                      RUA13Q1
         LA    R1,132(,R1)          LINE DISPLACEMENT           RUA13Q1
         LA    R4,132(,R1)        CHECK FOR ROOM                RUA13Q1
         CH    R4,$BUFLENG          IN BUFFER                   RUA13Q1
         BNH   #UA13Q5B           BR IF YES                     RUA13Q1
         BAL   PL,PPWRITE         FORCE WRITE                   RUA13Q1
         BAL   PL,PPCHECK         CHECK WRITE                   RUA13Q1
         LA    R1,BUFSTART-BUFDSECT SET STARTING LINE DISPLACEMENT 13Q1
#UA13Q5B ST    R1,PLNDISPL        STORE NEW DISPLACEMENT        RUA13Q1
         LA    PC1,0(R1,PBUF)     GET NEW LINE BUFFER ADDRESS   RUA13Q1
         MVI   0(PC1),C' '        BLANK OUT                     RUA13Q1
         MVC   1(131,PC1),0(PC1)    THE LINE                    RUA13Q1
         L     PL,PCEDCT          FIND THE DCT                  RUA13Q1
         USING DCTDSECT,PL        ADDRESS IT                    RUA13Q1
         MVC   0(5,PC1),=C'TRAIN' INSERT TRAIN                  RUA13Q1
         MVC   6(4,PC1),DCTUCS                                  RUA13Q1
         MVC   11(5,PC1),=C'SETUP'                              RUA13Q1
         MVC   22(11,PC1),=C'-      ----' ASSUME NO CHARGE      RUA13Q1
         DROP  PL                                               RUA13Q1
         TM    PPIFLG2,PPI2TSET   SETUP CHARGE FOR THIS TRAIN?  RUA13Q1
         BNO   #UA13Q5C           SKIP IF NOT                   RUA13Q1
         TM    PPIFLG1,PPI1LOCL   "LOCAL" DEVICE?               RUA13Q1
         BNO   #UA13Q5C           SKIP SETUP COST IF NOT        RUA13Q1
         MVI   22(PC1),C'1'       INDICATE SINGLE CHARGE        RUA13Q1
         ICM   PL,15,PTSETCST     SEE IF ANY SETUP COST         RUA13Q1
         BZ    #UA13Q5C           SKIP SETUP COST IF SO         RUA13Q1
         AL    PL,$DOUBLE                                       RUA13Q1
         ST    PL,$DOUBLE         INCREMENT TOTAL COST          RUA13Q1
         L     R15,PTSETCST       GET SETUP COST BACK           RUA13Q1
         SLR   R14,R14                                          RUA13Q1
         D     R14,=F'1000'       GET NUMBER IN CENTS           RUA13Q1
         MVC   24(10,PC1),=X'4020202021204B2020'                RUA13Q1
         LA    R1,29(,PC1)        POINT TO FORCE SIGNIF DIGIT   RUA13Q1
         CVD   R15,PMESSAGE                                     RUA13Q1
         EDMK  24(10,PC1),PMESSAGE+4                            RUA13Q1
         BCTR  R1,0                                             RUA13Q1
         MVI   0(R1),C'$'         INSERT DOLLAR SIGN            RUA13Q1
#UA13Q5C AL    PC1,PRCCWID        CONSTRUCT                     RUA13Q1
         L     PC2,PRCCWID+4        PRINT CCW                   RUA13Q1
         BAL   PL,PPPUT           ADD CCW TO CHAIN              RUA13Q1
         L     R1,PLNDISPL        FIND NEW                      RUA13Q1
         LA    R1,132(,R1)          LINE DISPLACEMENT           RUA13Q1
         LA    R4,132(,R1)        CHECK FOR ROOM                RUA13Q1
         CH    R4,$BUFLENG          IN BUFFER                   RUA13Q1
         BNH   #UA13Q5D           BR IF YES                     RUA13Q1
         BAL   PL,PPWRITE         FORCE WRITE                   RUA13Q1
         BAL   PL,PPCHECK         CHECK WRITE                   RUA13Q1
         LA    R1,BUFSTART-BUFDSECT SET STARTING LINE DISPLACEMENT 13Q1
#UA13Q5D ST    R1,PLNDISPL        STORE NEW DISPLACEMENT        RUA13Q1
         LA    PC1,0(R1,PBUF)     GET NEW LINE BUFFER ADDRESS   RUA13Q1
         MVI   0(PC1),C' '        BLANK OUT                     RUA13Q1
         MVC   1(131,PC1),0(PC1)    THE LINE                    RUA13Q1
         MVC   0(13,PC1),=C'LINES PRINTED'                      RUA13Q1
         MVC   29(4,PC1),=C'----' ASSUME NO CHARGE              RUA13Q1
         L     R1,PPLNCDCT        GET LINE COUNT                RUA13Q1
         CVD   R1,PMESSAGE        CONVERT TO DECIMAL            RUA13Q1
         MVC   15(8,PC1),=X'4020202020202020'                   RUA13Q1
         ED    15(8,PC1),PMESSAGE+4 MAKE PRINTABLE              RUA13Q1
         TM    PPIFLG1,PPI1LOCL   CHARGE FOR LINES?             RUA13Q1
         BNO   #UA13Q5E           NO, SKIP THIS                 RUA13Q1
         L     R15,PPLNCDCT       GET LINE COUNT AGAIN          RUA13Q1
         SLR   R14,R14                                          RUA13Q1
         M     R14,PLINECST       MULTIPLY BY LINE COST         RUA13Q1
         M     R14,PPTRNCST         AND TRAIN MULTIPLIER        RUA13Q1
         D     R14,=F'1000'       MAKE UNITS RIGHT              RUA13Q1
         SLR   R14,R14                                          RUA13Q1
         LR    PL,R15                                           RUA13Q1
         AL    PL,$DOUBLE                                       RUA13Q1
         ST    PL,$DOUBLE         INCREMENT TOTAL COST          RUA13Q1
         D     R14,=F'1000'       GET NUMBER IN CENTS           RUA13Q1
         LR    R14,R15                                          RUA13Q1
         CVD   R15,PMESSAGE       CONVERT TO DECIMAL            RUA13Q1
         MVC   24(9,PC1),=X'4020202021204B2020'                 RUA13Q1
         LA    R1,29(,PC1)        POINT TO FORCE SIGNIF DIGIT   RUA13Q1
         EDMK  24(9,PC1),PMESSAGE+4                             RUA13Q1
         BCTR  R1,0                                             RUA13Q1
         MVI   0(R1),C'$'         INSERT DOLLAR SIGN            RUA13Q1
         MVC   35(2,PC1),=CL2'AT' INSERT RATE                   RUA13Q1
         L     R14,PLINECST       GET LINE COST AGAIN           RUA13Q1
         CVD   R14,PMESSAGE                                     RUA13Q1
         MVC   38(7,PC1),=X'402021204B2020'                     RUA13Q1
         MVC   45(5,PC1),=C'/1000'                              RUA13Q1
         LA    R1,41(,PC1)        POINT TO FORCE SIGNIF DIGIT   RUA13Q1
         EDMK  38(7,PC1),PMESSAGE+5                             RUA13Q1
         BCTR  R1,0                                             RUA13Q1
         MVI   0(R1),C'$'         INSERT DOLLAR SIGN            RUA13Q1
         L     R14,PPTRNCST       GET TRAIN COST AGAIN          RUA13Q1
         CL    R14,=F'1000'       TRAIN MULTIPLIER > 1?         RUA13Q1
         BNH   #UA13Q5E           SKIP IF NOT                   RUA13Q1
         MVI   51(PC1),C'*'       INSERT MULTIPLICATION SYMBOL  RUA13Q1
         MVC   52(7,PC1),=X'4021204B202020'                     RUA13Q1
         CVD   R14,PMESSAGE       CONVERT TO DECIMAL            RUA13Q1
         ED    52(7,PC1),PMESSAGE+5                             RUA13Q1
         MVC   60(12,PC1),=C'TRAIN FACTOR'                      RUA13Q1
#UA13Q5E AL    PC1,PRCCWID        CONSTRUCT                     RUA13Q1
         L     PC2,PRCCWID+4        PRINT CCW                   RUA13Q1
         BAL   PL,PPPUT           ADD CCW TO CHAIN              RUA13Q1
         L     R1,PLNDISPL        FIND NEW                      RUA13Q1
         LA    R1,132(,R1)          LINE DISPLACEMENT           RUA13Q1
         LA    R4,132(,R1)        CHECK FOR ROOM                RUA13Q1
         CH    R4,$BUFLENG          IN BUFFER                   RUA13Q1
         BNH   #UA13Q5F           BR IF YES                     RUA13Q1
         BAL   PL,PPWRITE         FORCE WRITE                   RUA13Q1
         BAL   PL,PPCHECK         CHECK WRITE                   RUA13Q1
         LA    R1,BUFSTART-BUFDSECT SET STARTING LINE DISPLACEMENT 13Q1
#UA13Q5F ST    R1,PLNDISPL        STORE NEW DISPLACEMENT        RUA13Q1
         LA    PC1,0(R1,PBUF)     GET NEW LINE BUFFER ADDRESS   RUA13Q1
         MVI   0(PC1),C' '        BLANK OUT                     RUA13Q1
         MVC   1(131,PC1),0(PC1)    THE LINE                    RUA13Q1
         MVC   0(13,PC1),=C'PAGES PRINTED'                      RUA13Q1
         L     R1,PRPAGECT        GET PAGE COUNT                RUA13Q1
         CVD   R1,PMESSAGE        CONVERT TO DECIMAL            RUA13Q1
         MVC   15(8,PC1),=X'4020202020202020'                   RUA13Q1
         ED    15(8,PC1),PMESSAGE+4 MAKE PRINTABLE              RUA13Q1
         MVC   29(4,PC1),=C'----' ASSUME NO CHARGE              RUA13Q1
         TM    PPIFLG1,PPI1LOCL   CHARGE FOR PAGES?             RUA13Q1
         BNO   #UA13Q5G           NO, SKIP THIS                 RUA13Q1
         ICM   R15,15,PPFRMCST    CHECK FOR NO PAGE COST        RUA13Q1
         BZ    #UA13Q5G           SKIP IF ZERO COST             RUA13Q1
         L     R15,PRPAGECT       GET PAGE COUNT AGAIN          RUA13Q1
         SLR   R14,R14                                          RUA13Q1
         M     R14,PPFRMCST       MULTIPLY BY FORM COST         RUA13Q1
         LR    PL,R15             SAVE COST                     RUA13Q1
         D     R14,=F'1000'       GET NUMBER IN CENTS           RUA13Q1
         CVD   R15,PMESSAGE       CONVERT TO DECIMAL            RUA13Q1
         AL    PL,$DOUBLE                                       RUA13Q1
         ST    PL,$DOUBLE         INCREMENT TOTAL COST          RUA13Q1
         MVC   24(9,PC1),=X'4020202021204B2020'                 RUA13Q1
         LA    R1,29(,PC1)        POINT TO FORCE SIGNIF DIGIT   RUA13Q1
         EDMK  24(9,PC1),PMESSAGE+4                             RUA13Q1
         BCTR  R1,0                                             RUA13Q1
         MVI   0(R1),C'$'         INSERT DOLLAR SIGN            RUA13Q1
         MVC   35(2,PC1),=CL2'AT' INSERT RATE                   RUA13Q1
         L     R15,PPFRMCST       GET PAGE COST AGAIN           RUA13Q1
         CVD   R15,PMESSAGE                                     RUA13Q1
         MVC   38(7,PC1),=X'402021204B2020'                     RUA13Q1
         MVC   45(5,PC1),=C'/1000'                              RUA13Q1
         LA    R1,41(,PC1)        POINT TO FORCE SIGNIF DIGIT   RUA13Q1
         EDMK  38(7,PC1),PMESSAGE+5                             RUA13Q1
         BCTR  R1,0                                             RUA13Q1
         MVI   0(R1),C'$'         INSERT DOLLAR SIGN            RUA13Q1
#UA13Q5G TM    PPIFLG1,PPI1CDEF+PPI1FDEF UNKNOWN FORMS/CTAPE?   RUA13Q1
         BZ    #UA13Q5H           NO, ALL IS KNOWN              RUA13Q1
         MVC   51(42,PC1),=CL42'- PAGE COUNT ESTIMATED - SEE NOTE(S) ABX
               OVE'                                             RUA13Q1
#UA13Q5H AL    PC1,PRCCWID        CONSTRUCT                     RUA13Q1
         L     PC2,PRCCWID+4        PRINT CCW                   RUA13Q1
         BAL   PL,PPPUT           ADD CCW TO CHAIN              RUA13Q1
         L     R1,PLNDISPL        FIND NEW LINE DISPLACEMENT    RUA13Q1
         LA    R1,132(,R1)          LINE DISPLACEMENT           RUA13Q1
         LA    R4,132(,R1)        CHECK FOR ROOM                RUA13Q1
         CH    R4,$BUFLENG          IN BUFFER                   RUA13Q1
         BNH   #UA13Q5I           BR IF YES                     RUA13Q1
         BAL   PL,PPWRITE         FORCE WRITE                   RUA13Q1
         BAL   PL,PPCHECK         CHECK WRITE                   RUA13Q1
         LA    R1,BUFSTART-BUFDSECT SET STARTING LINE DISPLACEMENT 13Q1
#UA13Q5I ST    R1,PLNDISPL        STORE NEW DISPLACEMENT        RUA13Q1
         LA    PC1,0(R1,PBUF)     GET NEW LINE BUFFER ADDRESS   RUA13Q1
         MVI   0(PC1),C' '        BLANK OUT                     RUA13Q1
         MVC   1(131,PC1),0(PC1)    THE LINE                    RUA13Q1
         MVC   0(16,PC1),=C'TOTAL PRINT COST'                   RUA13Q1
         MVC   29(4,PC1),=C'----' ASSUME NO CHARGE              RUA13Q1
         L     R15,$DOUBLE                                      RUA13Q1
         SLR   R14,R14                                          RUA13Q1
         D     R14,=F'1000'       GET NUMBER IN CENTS           RUA13Q1
         CVD   R15,PMESSAGE                                     RUA13Q1
         MVC   24(10,PC1),=X'4020202021204B2020'                RUA13Q1
         LA    R1,29(,PC1)        POINT TO FORCE SIGNIF DIGIT   RUA13Q1
         CVD   R15,PMESSAGE                                     RUA13Q1
         EDMK  24(10,PC1),PMESSAGE+4                            RUA13Q1
         BCTR  R1,0                                             RUA13Q1
         MVI   0(R1),C'$'         INSERT DOLLAR SIGN            RUA13Q1
         AL    PC1,PRCCWID        CONSTRUCT                     RUA13Q1
         L     PC2,PRCCWID+4        PRINT CCW                   RUA13Q1
         BAL   PL,PPPUT           ADD CCW TO CHAIN              RUA13Q1
         L     R1,PLNDISPL        GET NEW LINE DISPLACEMENT     RUA13Q1
         LA    R1,132(,R1)          LINE DISPLACEMENT           RUA13Q1
         LA    R4,132(,R1)        CHECK FOR ROOM                RUA13Q1
         CH    R4,$BUFLENG          IN BUFFER                   RUA13Q1
         BNH   #UA13Q5J           BR IF YES                     RUA13Q1
         BAL   PL,PPWRITE         FORCE WRITE                   RUA13Q1
         BAL   PL,PPCHECK         CHECK WRITE                   RUA13Q1
         LA    R1,BUFSTART-BUFDSECT SET STARTING LINE DISPLACEMENT 13Q1
#UA13Q5J ST    R1,PLNDISPL        STORE NEW DISPLACEMENT        RUA13Q1
         LA    PC1,0(R1,PBUF)     GET NEW LINE BUFFER ADDRESS   RUA13Q1
         MVI   0(PC1),C' '        BLANK OUT                     RUA13Q1
         MVC   1(131,PC1),0(PC1)    THE LINE                    RUA13Q1
         AL    PC1,PRCCWID        CONSTRUCT                     RUA13Q1
         L     PC2,PRCCWID+4        PRINT CCW                   RUA13Q1
         BAL   PL,PPPUT           ADD CCW TO CHAIN              RUA13Q1
         L     R1,PLNDISPL        FIND NEW                      RUA13Q1
         LA    R1,132(,R1)          LINE DISPLACEMENT           RUA13Q1
         LA    R4,132(,R1)        CHECK FOR ROOM                RUA13Q1
         CH    R4,$BUFLENG          IN BUFFER                   RUA13Q1
         BNH   #UA13Q5K           BR IF YES                     RUA13Q1
         BAL   PL,PPWRITE         FORCE WRITE                   RUA13Q1
         BAL   PL,PPCHECK         CHECK WRITE                   RUA13Q1
         LA    R1,BUFSTART-BUFDSECT SET STARTING LINE DISPLACEMENT 13Q1
#UA13Q5K ST    R1,PLNDISPL        STORE NEW DISPLACEMENT        RUA13Q1
#UA13Q51 NULL  ,                  REJOIN IBM CODE               RUA13Q1
