++USERMOD (RUA13M1)
   /* THIS MODIFICATION TO HASPINIT INTRODUCES THE NEW PARMETER
      STATEMENTS REQUIRED FOR BUILDING THE FORMS, CTAPE, AND TRAIN
      TABLES USED BY HASPPRPU FOR PAGE COUNTING.                  */.
++VER (Z038) FMID(EJE1102)
              REQ(RUA13A1,RUA13Q1                           /* ICSA */).
++MAC (#UA13M11) ASSEM(HASPINIT) DISTLIB(RICEHSRC).
         TITLE 'HASP INITIALIZATION -- MAXFORM/TAPE/TRAN ROUTN' RUA13M1
NPLFRMGT NULL  ,                                                RUA13M1
         BAL   WC,NPLNUMBR        EXTRACT AND CHECK NUMBER      RUA13M1
         LR    WC,R1              SAVE THE NUMBER FOR LATER     RUA13M1
         MH    R1,=AL2(#FRMELEN)  COMPUTE                       RUA13M1
         LA    R1,#FRMENT1-#FORMTBL(,R1) SIZE                   RUA13M1
         LR    R0,R1                  OF TABLE                  RUA13M1
         GETMAIN R,LV=(0)         OBTAIN SPACE FOR TABLE        RUA13M1
         ST    R1,$FORMTBL        SAVE POINTER                  RUA13M1
         USING #FORMTBL,R1                                      RUA13M1
         MVC   #FORMEYE,=CL8'#FORMTBL' COPY IN EYECATCHER       RUA13M1
         XC    #FRM#ENT,#FRM#ENT  CLEAR ENTRY COUNTER           RUA13M1
         DROP  R1                                               RUA13M1
         LR    R1,WC              RE-OBTAIN NUMBER              RUA13M1
         B     NPLSTORE             AND GO STORE VALUE          RUA13M1
         SPACE 3                                                RUA13M1
NPLTAPGT NULL  ,                                                RUA13M1
         BAL   WC,NPLNUMBR        EXTRACT AND CHECK NUMBER      RUA13M1
         LR    WC,R1              SAVE THE NUMBER FOR LATER     RUA13M1
         MH    R1,=AL2(#CTPELEN)  COMPUTE                       RUA13M1
         LA    R1,#CTPENT1-#CTAPTBL(,R1) SIZE                   RUA13M1
         LR    R0,R1                  OF TABLE                  RUA13M1
         GETMAIN R,LV=(0)         OBTAIN SPACE FOR TABLE        RUA13M1
         ST    R1,$CTAPTBL        SAVE POINTER                  RUA13M1
         USING #CTAPTBL,R1                                      RUA13M1
         MVC   #CTAPEYE,=CL8'#CTAPTBL' COPY IN EYECATCHER       RUA13M1
         XC    #CTP#ENT,#CTP#ENT  CLEAR ENTRY COUNTER           RUA13M1
         DROP  R1                                               RUA13M1
         LR    R1,WC              RE-OBTAIN NUMBER              RUA13M1
         B     NPLSTORE             AND GO STORE VALUE          RUA13M1
         SPACE 3                                                RUA13M1
NPLTRNGT NULL  ,                                                RUA13M1
         BAL   WC,NPLNUMBR        EXTRACT AND CHECK NUMBER      RUA13M1
         LR    WC,R1              SAVE THE NUMBER FOR LATER     RUA13M1
         MH    R1,=AL2(#TRNELEN)  COMPUTE                       RUA13M1
         LA    R1,#TRNENT1-#TRAINS(,R1) SIZE                    RUA13M1
         LR    R0,R1                  OF TABLE                  RUA13M1
         GETMAIN R,LV=(0)         OBTAIN SPACE FOR TABLE        RUA13M1
         ST    R1,$TRAINS         SAVE POINTER                  RUA13M1
         USING #TRAINS,R1                                       RUA13M1
         MVC   #TRANEYE,=CL8'#TRAINS'  COPY IN EYECATCHER       RUA13M1
         XC    #TRN#ENT,#TRN#ENT  CLEAR ENTRY COUNTER           RUA13M1
         DROP  R1                                               RUA13M1
         LR    R1,WC              RE-OBTAIN NUMBER              RUA13M1
         B     NPLSTORE             AND GO STORE VALUE          RUA13M1
         SPACE 3                                                RUA13M1
         TITLE 'HASP INITIALIZATION -- FORM PARM PROCESSING'    RUA13M1
NPLFRMNN NULL  ,                                                RUA13M1
         LA    WA,3(,WA)          POINT TO THE "M" IN FORM      RUA13M1
         BXH   WA,R14,NPLSSERR    GET NEXT CHARACTER            RUA13M1
         CLI   0(WA),X'F0'        TEST                          RUA13M1
         BL    NPLSSERR           INVALID IF NOT NUMERIC        RUA13M1
         BAL   WE,NPLDCONV        GET FORM NUMBER               RUA13M1
         B     NPLFMERR           BR IF ILLEGAL NUMBER      +0  RUA13M1
         CLI   0(WA),C' '         TEST TERMINATOR CHARACTER +4  RUA13M1
         BNE   NPLFMERR           INVALID IF NOT BLANK          RUA13M1
         LTR   R0,R1              TEST FORM NUMBER              RUA13M1
         BZ    NPLFMERR           INVALID IF ZERO               RUA13M1
         CH    R0,$MAXFRMS          OR GREATER                  RUA13M1
         BH    NPLFMERR               THAN MAX                  RUA13M1
         L     R1,$FORMTBL        POINT TO TABLE                RUA13M1
         L     WE,#FRM#ENT-#FORMTBL(,R1) INCREMENT ENTRY COUNT  RUA13M1
         LA    WE,1(,WE)                                        RUA13M1
         ST    WE,#FRM#ENT-#FORMTBL(,R1) SAVE NEW COUNT         RUA13M1
         LA    R1,#FRMENT1-#FORMTBL(,R1) AND FIRST ENTRY        RUA13M1
         BCTR  WE,0               COMPUTE OFFSET                RUA13M1
         MH    WE,=AL2(#FRMELEN)    INTO TABLE                  RUA13M1
         AR    R1,WE              POINT TO APPROPRIATE ENTRY    RUA13M1
         L     R0,=A(NPLFRMST)    GET ADDRESS OF SCAN TABLE     RUA13M1
         BAL   WE,NPLSSCAN        SCAN SUB-PARAMETERS           RUA13M1
         B     NPLCOMNT           PROCESS AS COMMEND STATEMENT  RUA13M1
         SPACE 3                                                RUA13M1
NPLFMERR NULL  ,                  INVALID FORM NUMBER           RUA13M1
         L     R1,=A(NPLFMERM)    POINT TO INVALID FORM MSG     RUA13M1
         B     NPLERMSG             AND BR TO ISSUE DIAGNOSTIC  RUA13M1
         TITLE 'HASP INITIALIZATION -- TRAIN PARM PROCESSING'   RUA13M1
NPLTRNNN NULL  ,                                                RUA13M1
         LA    WA,4(,WA)          POINT TO THE "N" IN TRAIN     RUA13M1
         BXH   WA,R14,NPLSSERR    GET NEXT CHARACTER            RUA13M1
         CLI   0(WA),X'F0'        TEST                          RUA13M1
         BL    NPLSSERR           INVALID IF NOT NUMERIC        RUA13M1
         BAL   WE,NPLDCONV        GET FORM NUMBER               RUA13M1
         B     NPLTRERR           BR IF ILLEGAL NUMBER      +0  RUA13M1
         CLI   0(WA),C' '         TEST TERMINATOR CHARACTER +4  RUA13M1
         BNE   NPLTRERR           INVALID IF NOT BLANK          RUA13M1
         LTR   R0,R1              TEST TRAIN NUMBER             RUA13M1
         BZ    NPLTRERR           INVALID IF ZERO               RUA13M1
         CH    R0,$MAXTRNS          OR GREATER                  RUA13M1
         BH    NPLTRERR               THAN MAX                  RUA13M1
         L     R1,$TRAINS         POINT TO TABLE                RUA13M1
         L     WE,#TRN#ENT-#TRAINS(,R1) INCREMENT ENTRY COUNT   RUA13M1
         LA    WE,1(,WE)                                        RUA13M1
         ST    WE,#TRN#ENT-#TRAINS(,R1) SAVE NEW COUNT          RUA13M1
         LA    R1,#TRNENT1-#TRAINS(,R1) POINT TO FIRST ENTRY    RUA13M1
         BCTR  WE,0               COMPUTE OFFSET                RUA13M1
         MH    WE,=AL2(#TRNELEN)    INTO TABLE                  RUA13M1
         AR    R1,WE              POINT TO APPROPRIATE ENTRY    RUA13M1
         L     R0,=A(NPLTRNST)    GET ADDRESS OF SCAN TABLE     RUA13M1
         BAL   WE,NPLSSCAN        SCAN SUB-PARAMETERS           RUA13M1
         B     NPLCOMNT           PROCESS AS COMMEND STATEMENT  RUA13M1
         SPACE 3                                                RUA13M1
NPLTRERR NULL  ,                  INVALID TRAIN NUMBER          RUA13M1
         L     R1,=A(NPLTRERM)    POINT TO INVALID TRAIN MSG    RUA13M1
         B     NPLERMSG             AND BR TO ISSUE DIAGNOSTIC  RUA13M1
         TITLE 'HASP INITIALIZATION -- CTAPE PARM PROCESSING'   RUA13M1
NPLCTPNN NULL  ,                                                RUA13M1
         LA    WA,4(,WA)          POINT TO THE "E" IN CTAPE     RUA13M1
         BXH   WA,R14,NPLSSERR    GET NEXT CHARACTER            RUA13M1
         CLI   0(WA),X'F0'        TEST                          RUA13M1
         BL    NPLSSERR           INVALID IF NOT NUMERIC        RUA13M1
         BAL   WE,NPLDCONV        GET FORM NUMBER               RUA13M1
         B     NPLTPERR           BR IF ILLEGAL NUMBER      +0  RUA13M1
         CLI   0(WA),C' '         TEST TERMINATOR CHARACTER +4  RUA13M1
         BNE   NPLTPERR           INVALID IF NOT BLANK          RUA13M1
         LTR   R0,R1              TEST CTAPE NUMBER             RUA13M1
         BZ    NPLTPERR           INVALID IF ZERO               RUA13M1
         CH    R0,$MAXCTPS          OR GREATER                  RUA13M1
         BH    NPLTPERR               THAN MAX                  RUA13M1
         L     R1,$CTAPTBL        POINT TO TABLE                RUA13M1
         L     WE,#CTP#ENT-#CTAPTBL(,R1) INCREMENT ENTRY COUNT  RUA13M1
         LA    WE,1(,WE)                                        RUA13M1
         ST    WE,#CTP#ENT-#CTAPTBL(,R1) SAVE NEW COUNT         RUA13M1
         LA    R1,#CTPENT1-#CTAPTBL(,R1) POINT TO FIRST ENTRY   RUA13M1
         BCTR  WE,0               COMPUTE OFFSET                RUA13M1
         MH    WE,=AL2(#CTPELEN)    INTO TABLE                  RUA13M1
         AR    R1,WE              POINT TO APPROPRIATE ENTRY    RUA13M1
         L     R0,=A(NPLCTPST)    GET ADDRESS OF SCAN TABLE     RUA13M1
         BAL   WE,NPLSSCAN        SCAN SUB-PARAMETERS           RUA13M1
         B     NPLCOMNT           PROCESS AS COMMEND STATEMENT  RUA13M1
         SPACE 3                                                RUA13M1
NPLTPERR NULL  ,                  INVALID CTAPE NUMBER          RUA13M1
         L     R1,=A(NPLTPERM)    POINT TO INVALID CTAPE MSG    RUA13M1
         B     NPLERMSG             AND BR TO ISSUE DIAGNOSTIC  RUA13M1
         TITLE 'HASP INITIALIZATION -- PUNCH PARM PROCESSING'   RUA13M1
NPLCNLM  NULL  ,                                                RUA13M1
         LA    WA,2(,WA)          POINT TO THE "P" IN CTP       RUA13M1
         BXH   WA,R14,NPLSSERR    GET NEXT CHARACTER            RUA13M1
         CLI   0(WA),X'F0'        TEST                          RUA13M1
         BL    NPLSSERR           INVALID IF NOT NUMERIC        RUA13M1
         BAL   WE,NPLDCONV        GET FORM NUMBER               RUA13M1
         B     NPLTPERR           BR IF ILLEGAL NUMBER      +0  RUA13M1
         CLI   0(WA),C'.'         TEST TERMINATOR CHARACTER +4  RUA13M1
         BNE   NPLSSERR           INVALID IF NOT BLANK          RUA13M1
         LTR   R0,R1              TEST CTAPE NUMBER             RUA13M1
         BZ    NPLTPERR           INVALID IF ZERO               RUA13M1
         CH    R0,$MAXCTPS          OR GREATER                  RUA13M1
         BH    NPLTPERR               THAN MAX                  RUA13M1
         L     R1,$CTAPTBL        POINT TO TABLE                RUA13M1
         C     R0,#CTP#ENT-#CTAPTBL(,R1) ILLEGAL IF PAST LAST   RUA13M1
         BH    NPLTPERR             CARRIAGE TAPE SEEN          RUA13M1
         LA    R1,#CTPENT1-#CTAPTBL(,R1) POINT TO FIRST ENTRY   RUA13M1
         BCTR  R0,0               COMPUTE OFFSET                RUA13M1
         MH    R0,=AL2(#CTPELEN)    INTO TABLE                  RUA13M1
         AR    R1,R0              POINT TO APPROPRIATE ENTRY    RUA13M1
         LR    WC,R1              SAVE ENTRY POINTER            RUA13M1
         CLC   1(4,WA),=CL4'LINE' CHECK REST OF NAME            RUA13M1
         BNE   NPLTLERR           INVALID IF NOT LINENNN        RUA13M1
         LA    WA,5(,WA)          BUMP PAST "LINE" IF OK        RUA13M1
         BAL   WE,NPLDCONV        GET CTAPE LINE NUMBER         RUA13M1
         B     NPLTLERR           BR IF ILLEGAL NUMBER      +0  RUA13M1
         CLI   0(WA),C' '         TEST TERMINATOR CHARACTER +4  RUA13M1
         BNE   NPLSSERR           INVALID IF NOT BLANK          RUA13M1
         LTR   R0,R1              TEST LINE NUMBER              RUA13M1
         BZ    NPLTLERR           INVALID IF ZERO               RUA13M1
         CH    R0,#CTAPLEN-#CTAPENT(,WC) OR GREATER             RUA13M1
         BH    NPLTLERR              THAN MAX                   RUA13M1
         LA    R1,#CTAPMAP-#CTAPENT(,WC) POINT TO PUNCH MAP     RUA13M1
         BCTR  R0,0               COMPUTE PUNCH POSITION        RUA13M1
         AR    R1,0                                             RUA13M1
         L     R0,=A(NPLPUNST)    GET ADDRESS OF SCAN TABLE     RUA13M1
         BAL   WE,NPLSSCAN        SCAN SUB-PARAMETERS           RUA13M1
         B     NPLCOMNT           PROCESS AS COMMENT STATEMENT  RUA13M1
         SPACE 3                                                RUA13M1
NPLTLERR NULL  ,                  INVALID CTAPE LINE NUMBER     RUA13M1
         L     R1,=A(NPLTLERM)    POINT TO INVALID LINE MSG     RUA13M1
         B     NPLERMSG             AND BR TO ISSUE DIAGNOSTIC  RUA13M1
++MAC (#UA13M12) ASSEM(HASPINIT) DISTLIB(RICEHSRC).
NPLFRMST $SCNTBL #FORMENT         FORM SCAN TABLE               RUA13M1
         PUSH  PRINT                                            RUA13M1
         PRINT NOGEN                                            RUA13M1
         $SCNTBL NAME,L'#FRMNAME,#FRMNAME,NSCFLEFT+NSCFLBLK     RUA13M1
         $SCNTBL LINES8,L'#FRM8LPI,#FRM8LPI,NSCFLBIN            RUA13M1
         $SCNTBL LINES6,L'#FRM6LPI,#FRM6LPI,NSCFLBIN            RUA13M1
         $SCNTBL COST,L'#FRMCST,#FRMCST,NSCFLBIN                RUA13M1
         $SCNTBL (SETUP,NOSETUP),#FRM$SET,#FRMFLGS              RUA13M1
         $SCNTBL                  END OF SCAN TABLE             RUA13M1
         POP   PRINT                                            RUA13M1
         SPACE 3                                                RUA13M1
NPLCTPST $SCNTBL #CTAPENT         CTAP SCAN TABLE               RUA13M1
         PUSH  PRINT                                            RUA13M1
         PRINT NOGEN                                            RUA13M1
         $SCNTBL NAME,L'#CTAPNAM,#CTAPNAM,NSCFLEFT+NSCFLBLK     RUA13M1
         $SCNTBL LPI,L'#CTAPLPI,#CTAPLPI,NSCFLBIN               RUA13M1
         $SCNTBL LINES,L'#CTAPLEN,#CTAPLEN,NSCFLBIN             RUA13M1
         $SCNTBL (SETUP,NOSETUP),#CTP$SET,#CTAPFLG              RUA13M1
         $SCNTBL                  END OF SCAN TABLE             RUA13M1
         POP   PRINT                                            RUA13M1
         SPACE 3                                                RUA13M1
NPLPUNST $SCNTBL 0                PUNCH SCAN TABLE              RUA13M1
         PUSH  PRINT                                            RUA13M1
         PRINT NOGEN                                            RUA13M1
         $SCNTBL PUNCH,1,0,NSCFLBIN                             RUA13M1
         $SCNTBL                  END OF SCAN TABLE             RUA13M1
         POP   PRINT                                            RUA13M1
         SPACE 3                                                RUA13M1
NPLTRNST $SCNTBL #TRANENT         TRAIN SCAN TABLE              RUA13M1
         PUSH  PRINT                                            RUA13M1
         PRINT NOGEN                                            RUA13M1
         $SCNTBL NAME,L'#TRANAME,#TRANAME,NSCFLEFT+NSCFLBLK     RUA13M1
         $SCNTBL COST,L'#TRNCST,#TRNCST,NSCFLBIN                RUA13M1
         $SCNTBL (SETUP,NOSETUP),#TRN$SET,#TRANFLG              RUA13M1
         $SCNTBL                  END OF SCAN TABLE             RUA13M1
         POP   PRINT                                            RUA13M1
         SPACE 3                                                RUA13M1
++MAC (#UA13M13) ASSEM(HASPINIT) DISTLIB(RICEHSRC).
NPLFMERM DC    CL38'*****    INVALID FORM NUMBER     *****'     RUA13M1
NPLTPERM DC    CL38'*****    INVALID CTAPE NUMBER    *****'     RUA13M1
NPLTRERM DC    CL38'*****    INVALID TRAIN NUMBER    *****'     RUA13M1
NPLTLERM DC    CL38'***** INVALID CTAPE LINE NUMBER  *****'     RUA13M1
++MAC (#UA13M14) ASSEM(HASPINIT) DISTLIB(RICEHSRC).
****************************************************************RUA13M1
*                                                               RUA13M1
*        CREATE FORMS/TRAINS/CTAPES DEFAULTS IF NONE SPECIFIED  RUA13M1
*                                                               RUA13M1
****************************************************************RUA13M1
         SPACE 1                                                RUA13M1
         ICM   R15,15,$MAXFRMS       SEE IF TABLE EXISTS        RUA13M1
         BZ    #UA13M41              NO, LEAVE AS IS            RUA13M1
         L     R15,$FORMTBL          POINT TO TABLE             RUA13M1
         USING #FORMTBL,R15                                     RUA13M1
         ICM   R14,15,#FRM#ENT       SEE IF ANY ENTRIES         RUA13M1
         BNZ   #UA13M41              YES, LEAVE AS IS           RUA13M1
         LA    R14,1                 NO.  CREATE ONE            RUA13M1
         ST    R14,#FRM#ENT          INDICATE 1 ENTRY           RUA13M1
         DROP  R15                                              RUA13M1
#UA13M41 NULL  ,                                                RUA13M1
         ICM   R15,15,$MAXTRNS       SEE IF TABLE EXISTS        RUA13M1
         BZ    #UA13M42              NO, LEAVE AS IS            RUA13M1
         L     R15,$TRAINS           POINT TO TABLE             RUA13M1
         USING #TRAINS,R15                                      RUA13M1
         ICM   R14,15,#TRN#ENT       SEE IF ANY ENTRIES         RUA13M1
         BNZ   #UA13M42              YES, LEAVE AS IS           RUA13M1
         LA    R14,1                 NO.  CREATE ONE            RUA13M1
         ST    R14,#TRN#ENT          INDICATE 1 ENTRY           RUA13M1
         DROP  R15                                              RUA13M1
#UA13M42 NULL  ,                                                RUA13M1
         ICM   R15,15,$MAXCTPS       SEE IF TABLE EXISTS        RUA13M1
         BZ    #UA13M43              NO, LEAVE AS IS            RUA13M1
         L     R15,$CTAPTBL          POINT TO TABLE             RUA13M1
         USING #CTAPTBL,R15                                     RUA13M1
         ICM   R14,15,#CTP#ENT       SEE IF ANY ENTRIES         RUA13M1
         BNZ   #UA13M43              YES, LEAVE AS IS           RUA13M1
         LA    R14,1                 NO.  CREATE ONE            RUA13M1
         ST    R14,#CTP#ENT          INDICATE 1 ENTRY           RUA13M1
         LA    R15,#CTPENT1          POINT TO FIRST ENTRY       RUA13M1
         USING #CTAPENT,R15                                     RUA13M1
         LA    R14,2                                            RUA13M1
         STH   R14,#CTAPLEN          INDICATE 2 LINE CTAPE      RUA13M1
         XC    #CTAPMAP,#CTAPMAP     CLEAR PUNCH MAP            RUA13M1
         DROP  R15                                              RUA13M1
#UA13M43 NULL  ,                                                RUA13M1
         SPACE 1                                                RUA13M1
++SRCUPD (HASPINIT).
./ CHANGE NAME=HASPINIT
         #FORMTBL                  GENERATE HASP FORMTBL DSECT  RUA13M1 M0112510
         #CTAPTBL                  GENERATE HASP CTAPTBL DSECT  RUA13M1 M0112520
         #TRAINS                   GENERATE HASP TRAINS DSECT   RUA13M1 M0112530
         #FORMENT                  GENERATE HASP FORMENT DSECT  RUA13M1 M0112540
         #CTAPENT                  GENERATE HASP CTAPENT DSECT  RUA13M1 M0112550
         #TRANENT                  GENERATE HASP TRANENT DSECT  RUA13M1 M0112560
         COPY  #UA13M11           COPY NEW PROCESSING ROUTINES  RUA13M1 M1248000
         MVI   C'*'(R1),1         DISALLOW ASTERISK IN KEYWORD  RUA13M1 M1696210
         MVI   C'*'(R1),0         ALLOW ASTERISK AS VALUE       RUA13M1 M1711100
         COPY  #UA13M12           COPY NEW SCAN TABLES          RUA13M1 M1978100
         COPY  #UA13M13           COPY NEW ERROR MESSAGES       RUA13M1 M1987000
         $PTENT ,&&MAXFRMS,NPLFRMGT,,,,(1,999)                  RUA13M1 M2063700
         $PTENT ,&&MAXCTPS,NPLTAPGT,,,,(1,99)                   RUA13M1 M2063710
         $PTENT ,&&MAXTRNS,NPLTRNGT,,,,(1,20)                   RUA13M1 M2063720
         $PTENT ,&&LNECST,,,,,(1,99999999)                      RUA13M1 M2063730
         $PTENT ,&&PUNCST,,,,,(1,99999999)                      RUA13M1 M2063740
         $PTENT ,&&FSTCST,,,,,(1,99999999)                      RUA13M1 M2063750
         $PTENT ,&&CSTCST,,,,,(1,99999999)                      RUA13M1 M2063760
         $PTENT ,&&TSTCST,,,,,(1,99999999)                      RUA13M1 M2063770
         $PTENT ,&&BSTCST,,,,,(1,99999999)                      RUA13M1 M2063780
         $PTENT 5,CTAPE,NPLCTPNN                     CTAPENN    RUA13M1 M2076600
         $PTENT 5,TRAIN,NPTTRNNN                     TRAINNN    RUA13M1 M2076610
         $PTENT 4,FORM,NPLFRMNN                      FORMNN     RUA13M1 M2078100
         $PTENT 3,CTP,NPLCNLM                    CTPNN.LINENNN  RUA13M1 M2078600
         COPY  #UA13M14                                         RUA13M1 M2113000
