         MACRO
&SETLBL  SETMODE  &MODE=,&MODESV=
.*                                                                   *.
.* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *.
.*                                                                   *.
.*  SETMODE        SET THE PSW AMODE BIT, SAVING THE CURRENT AMODE   *.
.*                                                                   *.
.* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *.
.*                                                                   *.
.*  OPERANDS       NAME       VALUE         MEANING                  *.
.*                 ----       -----         -------                  *.
.*                                                                   *.
.*                 MODE       24            SET AMODE TO 24          *.
.*                                                                   *.
.*                            31            SET AMODE TO 31          *.
.*                                                                   *.
.*                            RX-ADDRESS    ADDRESS OF A FULLWORD    *.
.*                                          SAVE AREA FOR AMODE; IT  *.
.*                                          IS SAVED IN THE LEFTMOST *.
.*                                          BIT, WITH THE REMAINING  *.
.*                                          BITS SET TO BINARY ZEROES*.
.*                                                                   *.
.*                            REGISTER IN   REGISTER NUMBER OTHER    *.
.*                            PARENTHESES   THAN R14 TO BE USED AS   *.
.*                                          A SAVE AREA FOR AMODE;   *.
.*                                          IT IS SET AS FOR THE     *.
.*                                          RX-ADDRESS               *.
.*                                                                   *.
.*                 MODESV     RX-ADDRESS    ADDRESS OF A FULLWORD    *.
.*                                          SAVE AREA FOR AMODE; IT  *.
.*                                          IS SAVED IN THE LEFTMOST *.
.*                                          BIT, WITH THE REMAINING  *.
.*                                          BITS SET TO BINARY ZEROES*.
.*                                                                   *.
.*                            REGISTER IN   REGISTER NUMBER TO BE    *.
.*                            PARENTHESES   USED AS A SAVE AREA FOR  *.
.*                                          AMODE; IT IS SET AS FOR  *.
.*                                          THE RX-ADDRESS           *.
.*                                                                   *.
.*                 SETLBL     VALID LABEL   USED TO ASSOCIATE LABEL  *.
.*                                          WITH MACRO EXPANSION     *.
.*                                                                   *.
.* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *.
         SPACE 3
.* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *.
.*                                                                   *.
.*  NOTES:         1.  IF MVS/SP 1 IS THE ACTIVE OPERATING SYSTEM,   *.
.*                     NOTHING IS DONE                               *.
.*                                                                   *.
.*                 2.  THE MACRO ALTERS REGISTER 14 IN ALL CASES     *.
.*                                                                   *.
.*                                                                   *.
.* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *.
.*
.*
.*
         LCLC  &NDX                                SHORTENED &SYSNDX
.*
.*
.*
&NDX     SETC  '&SYSNDX'                           GET &SYSNDX
.*
.*  VALIDATE OPERANDS: ARE BOTH OMITTED?
.*
         AIF   (T'&MODE EQ 'O' AND T'&MODESV EQ 'O').BERR      OMITTED?
.*
.*  VALIDATE MODE
.*
         AIF   (T'&MODE EQ 'O').NOMODE                         NO MODE?
         AIF   ('&MODE' EQ '24').MOK                           AMODE 24
         AIF   ('&MODE' EQ '31').MOK                           AMODE 31
         AIF   ('&MODE'(1,1) EQ '(').MOK                       (REG)
         AIF   ('&MODE'(K'&MODE,1) EQ ')').MOK                 RX ADDR
         AIF   ('&MODE'(1,1) EQ '0').MERR                      ERROR
         AIF   ('&MODE'(1,1) EQ '1').MERR                      ERROR
         AIF   ('&MODE'(1,1) EQ '2').MERR                      ERROR
         AIF   ('&MODE'(1,1) EQ '3').MERR                      ERROR
         AIF   ('&MODE'(1,1) EQ '4').MERR                      ERROR
         AIF   ('&MODE'(1,1) EQ '5').MERR                      ERROR
         AIF   ('&MODE'(1,1) EQ '6').MERR                      ERROR
         AIF   ('&MODE'(1,1) EQ '7').MERR                      ERROR
         AIF   ('&MODE'(1,1) EQ '8').MERR                      ERROR
         AIF   ('&MODE'(1,1) EQ '9').MERR                      ERROR
.*
.MOK     AGO   .GENCODE                                        GEN CODE
.*
.*  GENERATE CODE TO SAVE CURRENT AMODE WITHOUT SETTING A NEW AMODE
.*
.NOMODE  ANOP
&SETLBL  LA    R14,1                  R14 = 1
         SLL   R14,30                 MOVE TO BIT 1
         LA    R14,0(R14)             CLEAR HIGH ORDER BIT/BYTE
         SLL   R14,1                  MOVE TO BIT 0; BIT 0 = AMODE
         AIF   ('&MODESV'(1,1) EQ '(').MSVR1                  REGISTER
         ST    R14,&MODESV            SAVE CURRENT AMODE IN STORAGE
         AGO   .DONE                                          FINISHED
.MSVR1   LR    &MODESV(1),R14         SAVE CURRENT AMODE IN REGISTER
         AGO   .DONE                                          FINISHED
.*
.*  GENERATE CODE TO SET NEW AMODE
.*
.GENCODE ANOP
&SETLBL  L     R14,16                 R14 --> CVT
         L     R14,116(R14)           R14 --> CVTDCB FIELD
         SRL   R14,31                 ISOLATE BIT 0 -- THE MVS/SP BIT
         LTR   R14,R14                IS OPERATING SYSTEM MVS/SP 2?
         AIF   (T'&MODESV EQ 'O').NMSV1                       NO MODESV
         BZ    ##E&NDX                IF NOT, ZERO MODE SAVE AREA
         AGO   .GEN1                                          CONTINUE
.NMSV1   BZ    ##F&NDX                IF NOT, DO NOTHING
.GEN1    AIF   ('&MODE' EQ '24').GEN24                        MODE 24
         AIF   ('&MODE' EQ '31').GEN31                        MODE 31
         AIF   ('&MODE'(1,1) EQ '(').MR                       REGISTER
         L     R14,&MODE              R14  =  NEW AMODE
         LTR   R14,R14                TEST FOR NEW AMODE
         AGO   .GEN1A                                         CONTINUE
.MR      LTR   R14,&MODE(1)           R14  =  NEW AMODE
.GEN1A   CNOP  0,4                    ALIGN ON FULLWORD
         BM    *+16                   SET FOR NEW AMODE = 31
         L     R14,*+8                R14  =  AMODE 24 ¦¦ AFTER BSM
         B     *+20                   ISSUE BSM
         AIF   (T'&MODESV EQ 'O').NMSV3                       NO MODESV
         DC    A(##G&NDX)             SET FOR NEW AMODE = 24
         AGO   .GEN1B                                         CONTINUE
.NMSV3   DC    A(##F&NDX)             SET FOR NEW AMODE = 24
.GEN1B   L     R14,*+8                R14  =  AMODE 31 ¦¦ AFTER BSM
         B     *+8                    ISSUE BSM
         AIF   (T'&MODESV EQ 'O').NMSV4                       NO MODESV
         DC    A(##G&NDX+X'80000000') SET FOR NEW AMODE = 31
         AGO   .GENBSM                                        CONTINUE
.NMSV4   DC    A(##F&NDX+X'80000000') SET FOR NEW AMODE = 31
         AGO   .GENBSM                                        CONTINUE
.GEN24   CNOP  0,4                    ALIGN ON FULLWORD
         L     R14,*+8                R14  =  AMODE ¦¦ AFTER BSM
         B     *+8                    ISSUE BSM
         AIF   (T'&MODESV EQ 'O').NMSV5                       NO MODESV
         DC    A(##G&NDX)             SET FOR NEW AMODE = 24
         AGO   .GENBSM                                        CONTINUE
.NMSV5   DC    A(##F&NDX)             SET FOR NEW AMODE = 24
         AGO   .GENBSM                                        CONTINUE
.GEN31   CNOP  0,4                    ALIGN ON FULLWORD
         L     R14,*+8                R14  =  AMODE ¦¦ AFTER BSM
         B     *+8                    ISSUE BSM
         AIF   (T'&MODESV EQ 'O').NMSV6                       NO MODESV
         DC    A(##G&NDX+X'80000000') SET FOR NEW AMODE = 31
         AGO   .GENBSM                                        CONTINUE
.NMSV6   DC    A(##F&NDX+X'80000000') SET FOR NEW AMODE = 31
         AGO   .GENBSM                                        CONTINUE
.GENBSM  AIF   (T'&MODESV NE 'O').GBSM54                      MODESV
         BSM   R0,R14                 SET AMODE, DON'T SAVE OLD AMODE
         AGO   .NMSV2                                         NO MODESV
.GBSM54  BSM   R14,R14                SET AMODE AND SAVE OLD AMODE
##E&NDX  SR    R14,R14                ZERO R14 -- OLD AMODE
         AIF   ('&MODESV'(1,1) EQ '(').MSVR2                 REGISTER
##G&NDX  ST    R14,&MODESV            SET MODE SAVE AREA
         AGO   .NMSV2                                        CONTINUE
.MSVR2   ANOP
##G&NDX  LR    &MODESV(1),R14         SET MODE SAVE REGISTER
.NMSV2   ANOP
##F&NDX  DS    0H                     ENDIF
         AGO   .DONE
.*
.*  MNOTES
.*
.BERR    MNOTE 8,'*** SETMODE ERROR *** -- BOTH OPERANDS MISSING'
         AGO   .DONE
.MERR    MNOTE 8,'*** SETMODE ERROR *** -- INVALID/MISSING MODE'
         AGO   .DONE
.MSERR   MNOTE 8,'*** SETMODE ERROR *** -- INVALID/MISSING MODESV'
         AGO   .DONE
.DONE    SPACE 3
         MEND
