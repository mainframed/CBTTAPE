ALTAOF   MENTER 12,EQU,COM=ALTAOF,CP=CP
         B     EYE1
         COPY  EYECATCH
EYE1     DS    0H
***********************************************************************
* THIS IS THE TSSO ALTAOF COMMAND. IT WILL SEEK OUT THE SPECIFIED     *
* ENTRY ID IN THE ACTIVE AOF TABLE, AND CHANGE ITS MATCHLIM VALUE     *
* TO THE VALUE SPECIFIED ON THE COMMAND LINE.                         *
*                                                                     *
*
* SYNTAX:
*
*      ALTAOF ENTRY(ENTRY-IDENTIFIER) MATCHLIM(MLIM-NUMBER)
* WHERE:
*    ENTRY  -  THE TSSO/AOF TABLE ENTRY ID.
*    MATCHLIM- THE NEW MATCHLIM VALUE FOR THE ENTRY.
***********************************************************************
*
* VERSION IDENTIFIER:  TSSO VERSION 4.2
* AUTHOR. MARC SCHARE
*
*
* VERSION IDENTIFIER:  TSSO VERSION 4.3
* IN TSSO 4.3, THE ALTAOF COMMAND ENTRYID PARAMETER IS THE LABEL
* OF THE TABENTRY MACRO. IN PREVIOUS RELEASES, IT WAS THE NUMBER.
*
*
*
***********************************************************************
*
* MODIFIED
*   20OCT92  GLA  USE NEW DSECTS FOR AOF ENTRIES
*
*   DEC 2002 PVW  ADD CALL TO TSSORATH TO CHECK AUTHORIZATION IN
*                 RACF TO USE THIS COMMAND.  IF NO RACF PROFILE FOUND,
*                 CONTINUE AND CHECK THE UADS USERDATA FIELD.  IF
*                 RACF PROFILE FOUND, SKIP CHECK OF UADS FIELD AND GO
*                 BASED ON AUTHORIZATION FOUND IN RACF.
*
***********************************************************************
* THE FIRST STEP IS TO PARSE THE INPUT, AND DETERMINE WHAT THE USER IS
* AFTER.
***********************************************************************
         TESTAUTH FCTN=1
         LTR   R15,R15
         BNZ   ERROR6
*
         LINK  EP=TSSORATH                  LINK TO PGM TO CHECK AUTH
*                                           TO TSSO.CMD.AUTH PROFILE
*                                           IN RACF FOR PERMISSION TO
*                                           USE THE POWERFUL ALTAOF
*                                           COMMAND
         LTR   R15,R15                      AUTHORIZED?
         BZ    AUTHZD                       YES, CONTINUE
         C     R15,=F'8'                    NOT AUTHORIZED?
         BE    ERROR6                       CORRECT, ISSUE MESSAGE
*
         EXTRACT PSCBADDR,'S',FIELDS=PSB    CHECK THE C0 BITS IN
         L     R9,PSCBADDR                  SYS1.UADS FOR PERMISSION
         USING PSCB,R9                      TO USE THE POWERFUL ALTAOF
         TM    PSCBATR2,X'C0'               COMMAND
         BNO   ERROR6
         DROP  R9
*
AUTHZD   DS    0H
         SETPARSE PCL=ALTPARSE
         LA    R10,CORE
         LINK  EP=IKJPARS
         L     R9,MYANS
         USING IKJPARMD,R9
*
CHEKENTR DS    0H
         MVI   ENTSPEC,C' '
         MVC   ENTSPEC+1(7),ENTSPEC
*
         LH    R2,ENTRY              CHECK IF SPECIFIED
         CH    R2,=H'1'              WELL? WAS IT?
         BNE   ERROR1                GUESS NOT...  FORGET WHOLE THING
         LH    R3,ENTSUBID+4         GET THE PDL FOR THE SPECIFIED ENT
         L     R2,ENTSUBID           GET THE PDL FOR THE ENTRY.
         BCTR  R3,0                  FOR EXECUTE,
         EX    R3,MOVEENT            AND MOVE IT.
         B     ARD1
MOVEENT  MVC   ENTSPEC(0),0(R2)
ARD1     DS    0H
*
CHEKMLIM DS    0H
         LH    R2,MLIM
         LTR   R2,R2                 WELL? WAS IT?
         BZ    ERROR2                GUESS NOT...  FORGET WHOLE THING
         L     R2,MLIMSBID           GET THE PDL FOR THE SPECIFIED ENT
         L     R2,0(R2)              GET ACTUAL VALUE
         ST    R2,MLIMSPEC           AND PUT IT AWAY...
*
         DROP  R9
***********************************************************************
* NOW THAT WE HAVE THE PARAMETERS, WE WILL EITHER USE THE CURRENT     *
* TABLE ID, IN STORAGE.                                               *
* WE WILL WALK THROUGH THE TABLE UNTIL WE FIND THE SPECIFIED ENTRY ID.*
***********************************************************************
         GETCVT (R9)                 START WITH TSSO CVT           RPS
         LTR   R9,R9
         BZ    ERROR3
         USING TSSOCVT,R9
         L     R9,TSSOTABL           GET TSSO TABLE ADDRESS
         LTR   R9,R9
         BZ    ERROR4                GET TSSO CURRENT TABLE ADDR.
         DROP  R9
***********************************************************************
* WE NOW HAVE A TSSO TABLE, POINTED TO BY THE ADDRESS IN TABADDR      *
* WE CAN (FINALLY) LOCATE THE SPECIFIED ENTRY ID.                     *
***********************************************************************
         L     R9,0(R9)                 POINT TO THE FIRST RECORD IN
*                                       THE TSSO AOF.
         USING AOFNTRY,R9               SETUP ADDRESSABILITY
ENTLOOP  DS    0H
         CLC   AOFMSGID,=CL8'ZZZZZZZZ'  IS THIS THE LAST RECORD ?
         BE    ENDRECS
         CLC   AOFENTR,ENTSPEC          GET ENTRY IDENTIFIER..
         BE    GOTENTR                  YUP, ON OUR WAY...
         L     R9,AOFNXT
         B     ENTLOOP
ENDRECS  DS    0H
         B     ERROR5
GOTENTR  DS    0H
***********************************************************************
* HERE, R9 POINTS TO THE SPECIFIED ENTRY IN THE AOF TABLE. WE WILL    *
* FORMAT A MESSAGE, AND ZAP THE AOF TABLE.                            *
***********************************************************************
*
         LR    R1,R8
         CALL  JCECVTBD
         MVC   MSG1T+22(8),ENTSPEC      MOVE SPECIFIED ENTRY TO MSG
*
         L     R7,AOFMLIM               GET PREVIOUS MATCHLIM VALUE.
         C     R7,=F'999999'            ANY THERE ?
         BE    NOPREVAL                 NO, MATCHLIM WAS NONE
         LR    R1,R7
         CALL  JCECVTBD                 CONVERT TO DECMIAL
         STCM  R1,B'1111',MSG1T+44
         B     ZAPVAL
NOPREVAL DS    0H
         MVC   MSG1T+44(4),=CL4'NONE'
ZAPVAL   DS    0H
         L     R7,MLIMSPEC              GET SPECIFIED MLIM.
         C     R7,=F'999999'            NO MATCHLIM DESIRED ?
         BE    NOZAPVAL
         LR    R1,R7
         CALL  JCECVTBD
         STCM  R1,B'1111',MSG1T+52
         B     ZAPVAL2
NOZAPVAL DS    0H
         MVC   MSG1T+52(4),=CL4'NONE'
ZAPVAL2  DS    0H
         MODESET KEY=ZERO              POWER REQUIRED FOR THIS COMMAND
         ST    R7,AOFMLIM              ** DONE **
         MODESET KEY=NZERO             NO MORE POWER.
         DROP  R9
PRTMSG   DS    0H
         PUTLINE PARM=PUTBLOK,OUTPUT=(MSG1H,TERM,SINGLE,DATA),         X
               MF=(E,IOPLADS)
         B     ENDPROG
*
*
ERROR1   DS    0H
         PUTLINE PARM=PUTBLOK,OUTPUT=(ERROR1H,TERM,SINGLE,DATA),       X
               MF=(E,IOPLADS)
         B     ENDPROG
ERROR2   DS    0H
         PUTLINE PARM=PUTBLOK,OUTPUT=(ERROR2H,TERM,SINGLE,DATA),       X
               MF=(E,IOPLADS)
         B     ENDPROG
ERROR3   DS    0H
         PUTLINE PARM=PUTBLOK,OUTPUT=(ERROR3H,TERM,SINGLE,DATA),       X
               MF=(E,IOPLADS)
         B     ENDPROG
ERROR4   DS    0H
         PUTLINE PARM=PUTBLOK,OUTPUT=(ERROR4H,TERM,SINGLE,DATA),       X
               MF=(E,IOPLADS)
         B     ENDPROG
ERROR5   DS    0H
         PUTLINE PARM=PUTBLOK,OUTPUT=(ERROR5H,TERM,SINGLE,DATA),       X
               MF=(E,IOPLADS)
         B     ENDPROG
ERROR6   DS    0H
         PUTLINE PARM=PUTBLOK,OUTPUT=(ERROR6H,TERM,SINGLE,DATA),       X
               MF=(E,IOPLADS)
         B     ENDPROG
ENDPROG  DS    0H
         MLEAVE
ENTSPEC  DS    CL8              ENTRY ID SPECIFIED BY USER
MLIMSPEC DS    F                MATCHLIM SPECIFIED BY USER.
PSCBADDR DS    F
MSG1H    DC    H'80',H'0'
MSG1T    DC    CL80'MATCHLIM FOR ENTRY ID XXXXXXXX CHANGED FROM XXXX TOC
                XXXX'
*
ERROR1H  DC    H'80',H'0'
ERROR1T  DC    CL80'A ENTRY ID MUST BE SPECIFIED - REQUEST ABORTED'
*
ERROR2H  DC    H'80',H'0'
ERROR2T  DC    CL80'A NEW MATCHLIM VALUE MUST BE SPECIFIED - REQUEST ABX
               ORTED'
*
ERROR3H  DC    H'80',H'0'
ERROR3T  DC    CL80'TSSO DOES NOT APPEAR TO BE ACTIVE IN THE SYSTEM - RX
               EQUEST DENIED'
*
*
ERROR4H  DC    H'80',H'0'
ERROR4T  DC    CL80'THE AUTOMATED OPERATIONS FACILITY IS NOT ACTIVE - RX
               EQUEST DENIED'
*
*
ERROR5H  DC    H'80',H'0'
ERROR5T  DC    CL80'THE SPECIFIED ENTRY IDENTIFIER IS NOT PRESENT IN THX
               E ACTIVE AOF TABLE'
*
*
ERROR6H  DC    H'80',H'0'
ERROR6T  DC    CL80'ALTAOF IS TERMINATING BECAUSE IT WAS NOT INVOKED AUX
               THORIZED'
*
CORE     DS    CL200
********************************************************************
* THIS IS THE DESCRIPTION OF THE COMMAND SYNTAX, AS PARSE WANTS IT *
********************************************************************
ALTPARSE IKJPARM
ENTRY    IKJKEYWD
         IKJNAME 'ENTRY',SUBFLD=ENTSUB
MLIM     IKJKEYWD
         IKJNAME 'MATCHLIM',SUBFLD=MLIMSUB
         IKJNAME 'MLIM',SUBFLD=MLIMSUB
ENTSUB   IKJSUBF
ENTSUBID IKJIDENT 'ENT',PROMPT='ENTRY IDENTIFIER',FIRST=ALPHANUM,      X
               OTHER=ALPHANUM,                                         X
               HELP=('TSSO AOF TABLE ENTRY IDENTIFIER',                X
               'IDENTIFIER THAT WAS SPECIFIED AS A LABLE ON THE TABENTRX
               Y MACRO')
MLIMSUB  IKJSUBF
MLIMSBID IKJIDENT 'ENT',PROMPT='MATCH LIMIT',INTEG,                    X
               HELP=('MATCH LIMIT FOR THIS ENTRY ID')
         IKJENDP
         IKJIOPL
         IKJPSCB
         IEFJESCT
         IEFJSCVT
         IEFJSSVT
         CVT   DSECT=YES
         TSSOCVT
         TAOFNTRY
         END   ALTAOF
 PUNCH ' SETCODE  AC(1)'
 PUNCH ' IDENTIFY ALTAOF(''TSSO V 4.3 &SYSDATE &SYSTIME'') '
 PUNCH ' INCLUDE  SYSLMOD(JCECVT)'
 PUNCH ' ENTRY    ALTAOF'
 PUNCH ' NAME     ALTAOF(R)'
         END
