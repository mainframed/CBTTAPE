        TITLE  'T S S O   R A C F   LOGON/LOGOFF PROCESSING'
TSSLRACF CSECT
***********************************************************************
* THIS IS THE RACF VERSION OF THE TSSO .LOGON COMMAND. THE FUNCTION   *
* OF THIS MODULE IS TO GET RACF TO CREATE AN ACEE FOR THE CONSOLE     *
* WHO IS ATTEMPTING TO LOGON.                                         *
*                                                                     *
* PARAMETERS PASSED:                                                  *
* R1 POINTS TO A 24 BYTE AREA. THE LAYOUT OF THE AREA, AND THE        *
*    EXPECTED RESULTS ARE DOCUMENTED BELOW.                           *
*                                                                     *
*                                                                     *
* FIELD NAME    LENGTH     INPUT                     OUTPUT           *
*                                                                     *
*    PRMFLAG       2      BYTE 1                                      *
*                         X'80' - LOGON               NONE            *
*                         X'40' - LOGOFF                              *
*                                                                     *
*                                                                     *
*    (none)        2      Reserved for future use               EEJ1102
*                                                                     *
*                                                                     *
*    PRMACEE       4      LOGON - NULL                LOGON - ADDRESS *
*                         LOGOFF - ACEE OF USER             OF ACEE OF*
*                                  LOGGING OFF TSSO.      NEWLY LOGGED*
*                                                         ON USER.    *
*                                                                     *
*    PRMUSER       8      USERID SPECIFIED ON                         *
*                         LOGON COMMAND, OR USERID    NONE            *
*                         ATTEMPTING TO LOGOFF.                       *
*                                                                     *
*                                                                     *
*                                                                     *
*    PRMPASS       8      PASSWORD SPECIFIED ON       NONE            *
*                         LOGON COMMAND.                              *
*                                                                     *
*    PRMCNID       4      Console ID                  None      EEJ1102
*                                                               EEJ1102
*    PRMCNAME      8      Console name                None      EEJ1102
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*    RETURN CODES:                                                    *
*                                                                     *
*    R15 = 0 : FUNCTION OK                                            *
*    R15 = 4 : FUNCTION FAILED.                                       *
*                                                                     *
* THE MODULE WILL ISSUE ITS OWN MESSAGES FOR FAILURE REASONS....      *
*                                                                     *
*                                                                     *
* MODULE HISTORY:                                                     *
*    7/25/86: TSSO 4.3: MODIFIED TO SUPPORT THE SAF INTERFACE.        *
*                                                               EEJ1102
*    NOV 2002 EEJ  Support MCS Extended Consoles                EEJ1102
*                                                                     *
* NOTES:                                                              *
*                                                                     *
*    THIS MODULE SHOULD WORK FOR ANY SECURITY SYSTEM THAT CLAIMS TO   *
*    SUPPORT THE SAF INTERFACE.                                       *
***********************************************************************
         EQUATES
         STM   R14,R12,12(R13)    SAVE CALLERS REGSITERS
         LR    R12,R15            LOAD ENTRY POINT OF TSSO
         USING TSSLRACF,R12       GET ADDRESSIBILITY
         B     EYE1
         COPY  EYECATCH
EYE1     DS    0H
         LR    R8,R1
         GETMAIN R,LV=72           WE NEED STORAGE FOR THE SAVE AREA
         ST    R13,4(R1)          STORE ADDRESS OF CALLERS SAVE AREA
*                                  SAVE AREA
         ST    R1,8(R13)          REMEMBER WHERE THIS CORE IS
         LR    R13,R1             KEEP A COPY IN R13
*
         GETMAIN R,LV=2048         GET STORAGE FOR A LOCAL      EEJ1102
         LR    R11,R1             DATA AREA
         USING TSSLDATD,R11       AND TELL ASSEMBLER
*                                                   PREPROCESSING EXIT
         MVC   WTO1D(WTO1CE-WTO1C),WTO1C
         MVC   WTO2D(WTO2CE-WTO2C),WTO2C
         MVC   WTO3D(WTO3CE-WTO3C),WTO3C
         MVC   RACINI1D(RACINI1E-RACINI1C),RACINI1C
         MVC   RACINI2D(RACINI2E-RACINI2C),RACINI2C
*
         TM    0(R8),X'80'        LOGGING ON ?
         BO    LON
         TM    0(R8),X'40'        LOGGING OFF ?
         BO    LOFF
         B     LRACFERR
LOFF     DS    0H
***********************************************************************
* WE COME HERE IF WE ARE TRYING TO LOG A CONSOLE OFF OF TSSO.         *
*                                                                     *
***********************************************************************
         LA    R2,4(R8)            GET ACEE OF USER LOGGING OFF...
         LTR   R2,R2
         BZ    LRACFERR            IF NO ACEE - LOGICAL ERROR
*        RACINIT ACEE=(R2),MF=(E,RACINI2D)
         RACROUTE REQUEST=VERIFY,ACEE=(R2),WORKA=(R11),MF=(E,RACINI2D)
*
*        LTR   R15,R15
*        BNZ   SAFFAIL              BAD NEWS... SAF FAILED...
*
         L     R15,RACINI2D         GET RACF RETURN CODE
         LTR   R15,R15
         BZ    DELOK
         WTO   'TSSL456S ERROR DELETING RACF ACEE - CONTACT SYSTEM PROGX
               RAMMER',DESC=1
         B     LRACRC12
*
DELOK    DS    0H
         MVC   WTO3D+21(8),28(R8)   Copy console name           EEJ1102
         MVC   WTO3D+31(8),8(R8)    Copy user ID                EEJ1102
         WTO   MF=(E,WTO3D)         (MSG 456)
         B     ENDPROG
*
*
*
LON      DS    0H
***********************************************************************
* HERE, WE WILL LOGON A USER. THIS IS ACCOMPLISHED BY ISSUING THE     *
* APPROPRAITE RACINIT CALL -                                          *
***********************************************************************
         MVC   CONSOLE,24(R8)      Copy console ID              EEJ1102
CHECKPW  DS    0H
         LA    R2,8
         LA    R3,8(R8)
         XR    R4,R4
UIDLOOP  DS    0H
         CLI   0(R3),C' '
         BE    FNDUSER
         LA    R3,1(R3)
         LA    R4,1(R4)
         BCT   R2,UIDLOOP
         B     INVUSER
FNDUSER  DS    0H
         STC   R4,RACUSER
*
         LA    R2,8
         LA    R3,16(R8)
         XR    R4,R4
PWLOOP   DS    0H
         CLI   0(R3),C' '
         BE    FNDPW
         OI    0(R3),X'40'          MAKE UPPER CASE
         LA    R3,1(R3)
         LA    R4,1(R4)
         BCT   R2,PWLOOP
FNDPW    DS    0H
         LTR   R4,R4                 ANY PASSWORD ENTERED ?
         BZ    BADPW
         STC   R4,RACPASS
*
         MVC   RACUSER+1(8),8(R8)
         MVC   RACPASS+1(8),16(R8)
         XC    ACEEADDR(4),ACEEADDR
         LA    R2,ACEEADDR
*        RACINIT USERID=RACUSER,PASSWRD=RACPASS,ACEE=(R2),            X
*              MF=(E,RACINITD)
         RACROUTE REQUEST=VERIFY,USERID=RACUSER,PASSWRD=RACPASS,       X
               ACEE=(R2),WORKA=(R11),MF=(E,RACINI1D)
*
*        LTR   R15,R15               CHECK SAF RETURN CODE
*        BNZ   SAFFAIL               SAF FAILED...
*
         L     R15,RACINI1D          GET RETURN CODE...
*
         C     R15,=XL4'00000030'    WAS RC OK
         BH    BADRC
         LTR   R15,R15
         BM    BADRC
         B     *+4(R15)              LETS SEE WHAT HAPPENED
         B     RACOK                 Y E S !!
         B     NOTDEF
         B     INVPW
         B     EXPIRED
         B     BADRC
         B     BADRC
         B     BADRC
         B     REVOKED
         B     BADRC
         B     REVOKED
         B     BADRC
         B     BADRC
         B     BADRC
NOTDEF   DS    0H
         MVC   ERRMSG(40),=CL40'XXXXXXXX IS NOT DEFINED TO RACF'
         MVC   ERRMSG(8),RACUSER+1
         B     WTOMSG
INVPW    DS    0H
         MVC   ERRMSG(40),=CL40'LOGON REJECTED - INCORRECT PASSWORD'
         B     WTOMSG
EXPIRED  DS    0H
         MVC   ERRMSG(40),=CL40'LOGON REJECTED - EXPIRED PASSWORD'
         B     WTOMSG
REVOKED  DS    0H
         MVC   ERRMSG(40),=CL40'LOGON REJECTED - REVOKED USERID'
         B     WTOMSG
BADPW    DS    0H
         MVC   ERRMSG(40),=CL40'LOGON REJECTED - NULL PASSWORD ENTERED'
         B     WTOMSG
INVUSER  DS    0H
         MVC   ERRMSG(40),=CL40'LOGON REJECTED - USERID > 7 CHARACTERS'
         B     WTOMSG
SAFFAIL  DS    0H
BADRC    DS    0H
         LA    R10,WORKAREA
         LR    R1,R15
         CALL  JCECVTBH
         MVC   ERRMSG(40),=CL40'SAF ERROR -    RC IS XXXXXXXX'
         STCM  R0,B'1111',ERRMSG+21
         STCM  R1,B'1111',ERRMSG+25
         B     WTOMSG
RACOK    DS    0H
***********************************************************************
* WE COME HERE IF THE USER HAS ACTUALLY BEEN SUCCUSSFULLY LOGGED ON   *
* TO RACF.                                                            *
***********************************************************************
         L     R2,ACEEADDR          GET RACF PROVIDED ACEE ADDRESS
         ST    R2,4(R8)             AND PUT IT AWAY...
         LA    R10,WORKAREA
         MVC   WTO2D+21(8),28(R8)   Copy console name           EEJ1102
         MVC   WTO2D+51(8),RACUSER+1 Copy user ID               EEJ1102
         WTO   MF=(E,WTO2D)
         XC    RETCODE(2),RETCODE     ZERO RETURN CODE
         B     ENDPROG
WTOMSG   DS    0H
***********************************************************************
* THIS ROUTINE WILL PRINT THE MESSAGE IN VARIABLE ERRMSG ON THE MVS   *
* CONSOLE THAT THE LOGON WAS ATTEMPTED FOR.                           *
***********************************************************************
         MVC   WTO1D+13(40),ERRMSG
         WTO   CONSID=CONSOLE,MF=(E,WTO1D)                      EEJ1102
         B     LRACRC12
LRACFERR DS    0H
         MVC   ERRMSG(40),=CL40'LOGICAL ERROR PROCESSING LOGON REQUEST'
         B     WTOMSG
LRACRC12 DS    0H
         MVC   RETCODE(2),=H'12'
         B     ENDPROG
ENDPROG  DS    0H
         LH    R8,RETCODE
         LR    R1,R13              GET ADDRESS OF CORE IN R1
         L     R13,4(R13)          RESTORE R13
         FREEMAIN R,LV=72,A=(R1)    FREE UP THE CORE
ENDPROG1 DS    0H
         L     R14,12(R13)         RELOAD RETURN POINT
         LR    R15,R8
         LM    R0,R12,20(R13)      RELOAD MORE REGISTERS
         BR    R14
*
*
WTO1C    WTO   'TSSL452I                                         ',    X
               CONSID=,MF=L                                     EEJ1102
WTO1CE   EQU   *
*
WTO2C    WTO   'TSSL455I CONSOLE XXXXXXXX LOGGED ON TO TSSO AS XXXXXXXXX
               ',MCSFLAG=BRDCST,MF=L
WTO2CE   EQU   *
*
WTO3C    WTO   'TSSL457I CONSOLE XXXXXXXX (XXXXXXXX) LOGGED OFF TSSO', X
               MCSFLAG=BRDCST,MF=L
WTO3CE   EQU   *
RACINI1C RACROUTE REQUEST=VERIFY,ENVIR=CREATE,SMC=YES,PASSCHK=YES,MF=L
RACINI1E EQU   *
*
RACINI2C RACROUTE REQUEST=VERIFY,ENVIR=DELETE,SMC=YES,MF=L
RACINI2E EQU   *
*
*
*
*
***********************************************************************
* TSSLRACF ONLY DATA AREA.                                            *
*                                                                     *
***********************************************************************
TSSLDATD DSECT
WORKAREA DS    CL512
CONSOLE  DS    F
RACUSER  DS    CL9          USERID FOR RACINIT.
RACPASS  DS    CL9          PASSWORD FOR RACINIT.
RETCODE  DS    F
ACEEADDR DS    F
ERRMSG   DC    CL40' '
WTO1D    WTO   'TSSL452I                                         ',    X
               CONSID=,MF=L                                     EEJ1102
*
WTO2D    WTO   'TSSL455I CONSOLE XXXXXXXX LOGGED ON TO TSSO AS XXXXXXXXX
               ',MCSFLAG=BRDCST,MF=L
*
*
WTO3D    WTO   'TSSL457I CONSOLE XXXXXXXX (XXXXXXXX) LOGGED OFF TSSO', X
               MCSFLAG=BRDCST,MF=L
*
RACINI1D RACROUTE REQUEST=VERIFY,ENVIR=CREATE,SMC=YES,PASSCHK=YES,MF=L
*
RACINI2D RACROUTE REQUEST=VERIFY,ENVIR=DELETE,SMC=YES,MF=L
*
         END   TSSLRACF
 PUNCH ' SETCODE  AC(1)'
 PUNCH ' IDENTIFY TSSLRACF(''TSSO V 4.3.0  &SYSDATE &SYSTIME'') '
 PUNCH ' INCLUDE  SYSLMOD(JCECVT)'
 PUNCH ' ENTRY    TSSLRACF'
 PUNCH ' NAME     TSSLRACF(R)'
         END
