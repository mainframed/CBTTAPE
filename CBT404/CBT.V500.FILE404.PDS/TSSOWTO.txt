         TITLE 'T S S O    WRITE TO OPERATOR'
TSSOWTO  CSECT
         EQUATES
***********************************************************************
* THIS MODULE IS THE TSSO WRITE TO OPERATOR MODULE. ITS PURPOSES ARE  *
* TWOFOLD. THE FIRST IS TO WRITE EVERY RECORD FROM THE SYSTSPRT FILE  *
* TO THE OPERATORS CONSOLE. THE SECOND IS TO INTERCEPT THOSE RECORDS  *
* THAT START WITH "*COM" AND ISSUE THE REST OF THE RECORD AS AN       *
* OPERATOR COMMAND.                                                   *
*                                                                     *
* PARAMETERS PASSED:                                                  *
* 0(R1) ADDRESS OF WORD CONTAINING CONSOLE ID FOR THE CONSOLE THAT    *
*       ISSUED THE COMMAND FOR WHICH OUTPUT IS BEING DISPLAYED.       *
*                                                                     *
* 4(R1) THE MAXIMUM NUMBER OF WTO'S TO BE DISPLAYED IN A SINGLE       *
*       COMMAND. THIS PARAMETER IS OBTAINED THROUGH THE TSSO          *
*       INITIALIZATION PROCEDURES.
*
* 8(R1) ADDRESS OF A FLAG WORD, USED TO PASS PROCESSING OPTIONS       *
*
* THIS SUBROUTING ASSUMES A DDCARD OF SYSTSPRT IS IN THE TSSO JCL.    *
* FURTHER, IT ASSUMES THAT TSSO HAS A LRECL OF 137 BYTES, AND IS RECFM*
* OF VARIABLE BLOCKED (VBA)
* THIS SHOULD BE SPECIFIED IN THE TSSO STARTUP JCL.
***********************************************************************
* NOTE: ALL MESSAGES PRODUCED BY THIS MODULE HAVE THE FORM TSSWXXXS   *
* WHERE 351 <= XXX <= 400 AND "S" IS THE SEVERITY OF THE MESSAGE.     *
***********************************************************************
*                                                               EEJ1102
* Change Activity:                                              EEJ1102
*                                                               EEJ1102
*   NOV 2002 EEJ Support MCS extended consoles                  EEJ1102
*                                                               EEJ1102
***********************************************************************
         STM   R14,R12,12(R13)    SAVE CALLERS REGSITERS
         LR    R12,R15            LOAD ENTRY POINT OF TSSO
         USING TSSOWTO,R12        GET ADDRESSIBILITY
         B     ARINIT1
         DC    CL8'&SYSDATE'      DATE FOR QVERS COMMAND
         DC    CL8'&SYSTIME'      TIME FOR QVERS COMMAND
ARINIT1  DS    0H
         B     EYE1
         COPY  EYECATCH
EYE1     DS    0H
         LR    R8,R1
         GETMAIN R,LV=72           WE NEED STORAGE FOR THE SAVE AREA
         LTR   R15,R15
         BNZ   GETMERR            GETMAIN FAILED
         ST    R13,4(R1)          STORE ADDRESS OF CALLERS SAVE AREA
*                                 SAVE AREA
         ST    R1,8(R13)          REMEMBER WHERE THIS CORE IS
         LR    R13,R1             KEEP A COPY IN R13
***********************************************************************
* GETMAIN DYNAMIC STORAGE FOR TSSOWTO VARIABLES.
***********************************************************************
         GETMAIN R,LV=1024
         LR    R11,R1
         USING DATD,R11
*
         MVC   OPEND(OPENCE-OPENC),OPENC
         MVC   CLOSED(CLOSECE-CLOSEC),CLOSEC
         MVC   INDCBD(INDCBCE-INDCBC),INDCBC
         MVC   WTO1D(WTO1CE-WTO1C),WTO1C
         XC    WTONUM(4),WTONUM    INITIALIZE NUMBER OF WTOS ENCOUNTER
         L     R2,0(R8)            GET ADDRESS OF CONSOLE ID.
         L     R3,0(R2)            AND GET ACTUAL VALUE OF CONSOLE ID
         ST    R3,CONSID           STORE IT AWAY. THIS CONSOLE ID WILL
*                                  BE USED TO ISSUE COMMANDS AND WTOS
         L     R2,4(R8)            GET MAX WTO ADDRESS
         L     R3,0(R2)            AND GET ACTUAL VALUE
         ST    R3,MAXWTO          STORE PARAMETER
*
         L     R2,8(R8)            GET A(TSSWFLAG)
         L     R2,0(R2)
         ST    R2,TSSWFLAG         AND PUT IT AWAY
*
         LA    R10,INDCBD          GET DCB ADDRESS
         OPEN  ((R10),INPUT),MF=(E,OPEND)
         TM    INDCBD+48,X'10'     IS IT REALLY (TRUELY) OPENED ?
         BNO   TSPRTERR            YIPES! MAJOR PROBLEMS !
*
NEXTREC  DS    0H                   BLANK OUT INPUT BUFFER.
         MVI   BUFFER,X'40'
         MVC   BUFFER+1(136),BUFFER
         MVI   WTO1D+4,X'40'       BLANK OUT WTO BUFFER TOO
         MVC   WTO1D+5(124),WTO1D+4
         LA    R10,INDCBD          RESTORE DCB ADDRESS
         GET   (R10),BUFFER        READ A RECORD..
         MVI   BUFFER+4,X'40'      BLANK OUT CARRAIGE CONTROL.
         LH    R2,BUFFER           GET LENGTH OF THIS RECORD.
         CH    R2,=H'125'          GREATER THEN 125 CHARS, THE MAX THAT
*                                 CAN FIT ON A WTO ?
         BNH   LENGTHOK            YUP, ADJUST.
         LH    R2,=H'125'          ADJUSTING...
LENGTHOK DS    0H
         ST    R2,LREC             THIS WILL BE LENGTH OF THIS RECORD.
PROCREC  DS    0H
         CLC   BUFFER+5(4),=CL4'*COM'  OPERATOR COMMAND REQUESTED ?
         BE    ISSUECMD                YUP, GO ISSUE COMMAND.
         CLC   BUFFER+5(3),=CL3'*HI'   HILIGHT THIS MESSAGE ?
         BE    WTOHI                   YUP.. DUMMY UP DESCRIPTOR CODE
***********************************************************************
* NO SPECIAL TSSO CLIST CONSTRUCT HAS BEEN FOUND, SO WE WILL
* OUTPUT THE LINE IN A WTO MESSAGE TO THE CONSOLE THAT ISSUED THE
* COMMAND
***********************************************************************
         L     R2,LREC                 PICK UP LENGTH OF RECORD.
         BCTR  R2,0                    -1 FOR EXECUTE
         EX    R2,MOVEWTO              MOVE BUFFER CONTENTS TO WTO
         B     AR1
MOVEWTO  MVC   WTO1D+4(0),BUFFER+5     ** EXECUTED **
AR1      DS    0H
         WTO   CONSID=CONSID,      Print buffer line on console EEJ1102+
               MF=(E,WTO1D)        (same)                       EEJ1102
         L     R2,WTONUM               INCREMENT WTO NUMBER
         LA    R2,1(R2)
         C     R2,MAXWTO               PAST THE MAXWTO LIMIT ?
         BNL   WTOLIM                  YUP, QUITE SAD.
         ST    R2,WTONUM
         B     RECEND                  ONWARD
WTOHI    DS    0H
***********************************************************************
* HILIGHTING OF THIS WTO MESSAGE HAS BEEN REQUESTED.
***********************************************************************
         L     R2,LREC                 PICK UP LENGTH OF RECORD.
         BCTR  R2,0                    -1 FOR EXECUTE
         EX    R2,MOVEWTO2
         B     AR2
MOVEWTO2 MVC   WTO1D+4(0),BUFFER+9
AR2      DS    0H
         LA    R3,WTO1D        ADDRESS OF WTO CONSTRUCT
         AH    R3,0(R3)        R1 IS NOW POINTING TO THE ROUTINE AND
*                              DESCRIPTOR CODES.
         OI    0(R3),X'80'     SET DESCRIPTOR CODE 1
         L     R0,CONSID               DESTINATION CONSOLE
         WTO   MF=(E,WTO1D)
         NI    0(R3),255-X'80' SET DESCRIPTOR CODE ZERO
         L     R2,WTONUM       INCREMENT WTO NUMBER
         LA    R2,1(R2)
         C     R2,MAXWTO               PAST THE MAXWTO LIMIT ?
         BNL   WTOLIM                  YUP, QUITE SAD.
         ST    R2,WTONUM
         B     RECEND
ISSUECMD DS    0H
***********************************************************************
* WE COME HERE WHEN A REQUEST TO ISSUE AN MVS COMMAND HAS BEEN        *
* ENCOUNTERED. THE CONSOLE AUTHORITY OF THE INVOKING CONSOLE IS USED  *
* FOR THE COMMAND.                                                    *
*                                                                     *
***********************************************************************
* FIRST, WE MUST SEE IF A DELAY FACTOR WAS SPECIFIED. WE ALLOWS DELAYS
* OF BETWEEN 1 AND 999 SECONDS, THEREFORE, THE DELAY FACTOR CAN BE
* BETWEEN ONE AND THREE DIGITS. WE WILL SCAN THE BUFFER, FIND THE
* NUMBER OF DIGITS, AND IMPLEMENT THE DELAY.
***********************************************************************
         TM    TSSWFLAG,X'80'       NOT ISSUE COMMANDS (#.L)
         BNO   NODUPL               YUP! GET NEXT RECORD.
         MVC   BUFFER+5(4),=CL4'*DUP'  INDICATE DUPLICATE COMMAND
         B     PROCREC                 AND REPROCESS THE RECORD.
NODUPL   DS    0H
***********************************************************************
* WE COME HERE IF THE COMMAND IS REALLY GOING TO BE ISSUED, I.E.
* NOT A #.L REPITION OF A COMMAND THAT HAS ALREADY BEEN EXECUTED.
***********************************************************************
         LA    R2,BUFFER+9          START AT POTENTIAL FIRST DIGIT.
         XR    R3,R3                 COUNT OF NUMBERS FOUND SO FAR.
         XC    CMDDELAY(4),CMDDELAY   ASSUME NO DELAY IN THIS COMMAND
DIGLOOP  DS    0H
         CLI   0(R2),C' '
         BE    ENDDLOOP             NO MORE BLANKS.
         CLI   0(R2),C'0'           CHECK FOR CHAR ZERO
         BL    ENDDLOOP
         CLI   0(R2),C'9'           CHECK FOR HIGHER THEN NINE
         BH    ENDDLOOP
         LA    R2,1(R2)             INCREMENT LOOP COUNTER
         LA    R3,1(R3)             INCREMENT COUNT
         B     DIGLOOP
ENDDLOOP DS    0H
         CH    R3,=H'3'             ONLY DELAYS OF 999 SECONDS ARE OK
         BH    INVDELAY
         LA    R2,1(R2)             R2 NOW POINTS TO START OF COMMAND.
         ST    R2,CMDSTART          SAVE STARTING ADDRESS OF COMMAND
         ST    R3,DELAYCHR          STORE NUMBER OF EBCIDIC CHARS THAT
*                                   MAKE UP THE DELAY.
         LTR   R3,R3                ANY DELAY ?
         BZ    BLANKEM              NO, SKIP DELAY STUFF.
         LR    R0,R3                COPY LENGTH OF DELAY
         LA    R1,BUFFER+9          STARTING ADDRESS OF DELAY.
         LA    R10,WORKAREA
         CALL  JCECVTDB             ** CONVERT **
         ST    R1,CMDDELAY          STORE DELAY AWAY.
***********************************************************************
* WE WILL BLANK OUT THE EBCIDIC CHARACTERS REPRESENTING THE DELAY
* AND THE "*COM" SO THESE CHARACTERS ARE NOT ISSUED AS PART OF THE
* OPERATOR COMMAND.
***********************************************************************
BLANKEM  DS    0H
         LA    R3,4                 START WITH CHARS FOR "*COM"
         A     R3,DELAYCHR          ADD CHARS FOR EBCIDIC NUMBERS IN
*                                   DELAY
         BCTR  R3,0                 -1 FOR EXECUTE
         EX    R3,BLANKBUF
         B     AR3
BLANKBUF MVC   BUFFER+5(0),=CL8' '   ** EXECUTED **
AR3      DS    0H
DELAYIT  DS    0H
         L     R3,CMDDELAY
         LTR   R3,R3                AND DELAY ?
         BZ    DOCMD
         M     R2,=F'100'           BECAUSE BINTVL IS ONLY .01 SECONDS
         ST    R3,CMDDELAY
         STIMER WAIT,BINTVL=CMDDELAY    DELAY THE COMMAND FOR
*                                  THE SPECIFIED TIME PERIOD
DOCMD    DS    0H
*                                                               EEJ1102
         L     R0,LREC             Get length of cmd buffer     EEJ1102
         SH    R0,=H'4'            Decrement for MGCR prefix    EEJ1102
         STH   R0,BUFFER+2         Store true command text len  EEJ1102
         MVC   WORKAREA(MGCREL),MGCRE Copy MGCRE parm list      EEJ1102
         LA    R2,BUFFER+2         Point to command text        EEJ1102
         MODESET MODE=SUP
         MGCRE TEXT=(2),           Issue the command            EEJ1102+
               CONSID=CONSID,      (same)                       EEJ1102+
               MF=(E,WORKAREA)     (same)                       EEJ1102
         MODESET MODE=PROB
         B    RECEND
RECEND   DS   0H
         B    NEXTREC
*                                                               EEJ1102
MGCRE    MGCRE MF=L                MGCRE parameter list         EEJ1102
MGCREL   EQU   *-MGCRE             (same)                       EEJ1102
*                                                               EEJ1102
WTOLIM   DS   0H
***********************************************************************
* WE COME HERE WHEN THE MAXIMUM NUMBER OF WTO LINES HAS BEEN REACHED, *
* WE WILL PRINT A LINE EXPLAINING WHAT HAPPENED, AND TERMINATE.       *
***********************************************************************
         WTO  'TSSW351A WTO LIMIT REACHED FOR THIS COMMAND, OUTPUT TERMX
               INATED'
         B     ENDRECS               AND WE TERMINATE.
TSPRTERR DS    0H
***********************************************************************
* WE COME HERE IF WE ENCOUNTERED A PROBLEM OPENING THE SYSTSPRT
* FILE. STANDARD PROCEDURE IS TO ISSUE A MESSAGE REQUESTING THAT TSSO
* BE SHUT DOWN AND RESTARTED.
***********************************************************************
         WTO   'TSSW352S ERROR OPENING SYSTSPRT DDNAME- PLEASE CONTACT X
               SYSTEMS PROGRAMMER',DESC=(1)
         B     ENDPROG
INVDELAY DS    0H
         WTO   'TSSW353E INVALID DELAY FOR OPERATOR COMMAND- COMMAND TEX
               XT FOLLOWS'
         MVC   BUFFER+5(4),=CL4'*ERR'      INFORM USER OF ERROR
         B     PROCREC                     AND SIMULATE A NORMAL MSG
ENDRECS  DS   0H
ENDPROG  DS   0H
         LA   R2,INDCBD
         CLOSE ((R2)),MF=(E,CLOSED)
*
***********************************************************************
* THE BELOW INSTRUCTIONS ARE NECCESARY BECAUSE TSSO CAN CONCEIVAVLY   *
* USE GREAT AMOUNTS OF VIRTUAL STORAGE DURING THE COURSE OF A DAY,    *
* WHAT WITH TSO CONSTRUCTING DCBS FOR EACH COMMAND ENTERED.           *
*                                                                     *
* THE FREEPOOL MACRO WILL FREE THESE BUFFERS, EXCEPT IF THEY WERE     *
* OBTAINED IN PROTECT KEY ZERO. SINCE TSO OPENED SYSTSPRT IN KEY ZERO *
* THE FREEMAIN MACRO MUST SPECIFY SUBPOOL 252. THIS IS ACCOMPLISHED BY*
* BACKING THE LOCATION COUNTER UP BY TWO BYTES, THUS OVERWRITING THE  *
* SVC 10, THEN "OR"ING THE SUBPOOL 252 INTO R0. THIS EFFECTIVELY ADDS *
* THE SP=252 OPERAND ONTO THE FREEMAIN MACRO.                         *
*                                                                     *
***********************************************************************
         FREEPOOL (R2)
         ORG  *-2
         LA   R15,252              GET SUBPOOL ID IN R15
         SLL  R15,24               MOVE TO HIGH ORDER BYTE.
         OR   R0,R15               SET SUBPOOL ID IN R0
         SVC  10                   ISSUE FREEMAIN
*
         FREEMAIN R,LV=1024,A=(R11)
         LR    R1,R13              GET ADDRESS OF CORE IN R1
         L     R13,4(R13)          RESTORE R13
         FREEMAIN R,LV=72,A=(R1)    FREE UP THE CORE
ENDPROG1 DS    0H
GETMERR  DS    0H
         L     R14,12(R13)         RELOAD RETURN POINT
         XR    R15,R15
         LM    R0,R12,20(R13)      RELOAD MORE REGISTERS
         BR    R14
OPENC    OPEN  (,INPUT),MF=L
OPENCE   DS    0H
CLOSEC   CLOSE (,),MF=L
CLOSECE  DS    0H
INDCBC   DCB   DDNAME=SYSTSPRT,RECFM=VBA,MACRF=GM,DSORG=PS,            X
               EODAD=ENDRECS,LRECL=137
INDCBCE  DS    0H
WTO1C    WTO   '                                                       X
                                                                       X
                             ',MCSFLAG=(RESP),DESC=(6),         EEJ1102+
               CONSID=,MF=L                                     EEJ1102
WTO1CE   DS    0H
*
*
*
DATD     DSECT
WORKAREA DS    CL200             WORKAREA FOR JCECVT
BUFFER   DS    CL137             BUFFER FOR SYSTSPRT.
OPEND    OPEN  (,INPUT),MF=L
CLOSED   CLOSE (,),MF=L
INDCBD   DCB   DDNAME=SYSTSPRT,RECFM=VBA,MACRF=GM,DSORG=PS,            X
               EODAD=ENDRECS,LRECL=137
WTO1D    WTO   '                                                       X
                                                                       X
                             ',MCSFLAG=(RESP),DESC=(6),         EEJ1102+
               CONSID=,MF=L                                     EEJ1102
CONSID   DS    F                 CONSOLE ID FOR THIS COMMAND.
MAXWTO  DS    F                 MAXIMUM NUMBER OF WTOS FOR TSSO
LREC     DS    F                 LENGTH OF RECORD BEING PROCESSED.
WTONUM   DS    F                 NUMBER OF WTO'S ISSUED.
CMDDELAY DS    F                 COMMAND DELAY VALUE FOR THIS RECORD.
CMDSTART DS    A                 STARTING ADDRESS OF COMMAND
TSSWFLAG DS    F                 FLAG WORD.
DELAYCHR DS    F                 NUMBER OF CHARS IN DELAY FACTOR.
         END   TSSOWTO
 PUNCH ' SETCODE  AC(1)'
 PUNCH ' IDENTIFY TSSOWTO(''TSSO V 4.1 &SYSDATE &SYSTIME'') '
 PUNCH ' INCLUDE  SYSLMOD(JCECVT)'
 PUNCH ' ENTRY    TSSOWTO'
 PUNCH ' NAME     TSSOWTO(R)'
         END
