JCECVT    TITLE 'TSSO CONVERT ROUTINES'
*.
* MODULE NAME -
*
*         JCECVT
*
* CONTENTS -
*
*         JCECVTBH - BINARY TO EBCDIC HEX
*         JCECVTHB - EBCDIC HEX TO BINARY
*         JCECVTDB - EBCDIC DECIMAL TO BINARY
*         JCECVTBD - BINARY TO EBCDIC DECIMAL
*         JCECVTGT - EXTRACT SYSTEM TIME IN PRINTABLE FORMAT
*         JCECVTGD - EXTRACT SYSTEM DATE IN JULIAN   FORMAT
*.
*. ***************************************************************
*. ON ENTRY TO THIS ROUTINE, REGISTER 10 MUST POINT TO A 200 BYTE*
*. WORKAREA, WHICH MUST BE PROVIDED BY THE CALLER !              *
*. ***************************************************************
          EJECT
JCECVT    CSECT
          SPACE 2
          ENTRY JCECVTBH
          ENTRY JCECVTHB
          ENTRY JCECVTBD
          ENTRY JCECVTDB
          ENTRY JCECVTGT
          ENTRY JCECVTGD
          SPACE 3
          EQUATES
          EJECT
*.
* SUBROUTINE NAME -
*
*         JCECVTBH
*
* FUNCTION -
*
*         CONVERTS A WORD OF BINARY INFORMATION INTO A DOUBLE-
*         WORD OF EBCDIC HEX DIGITS
*
* ATTRIBUTES -
*
*         SERIALLY REUSABLE, RESIDENT, CALLED VIA BALR
*
* ENTRY POINT -
*
*         JCECVTBH - CONVERT BINARY TO EBCDIC HEX
*
* ENTRY CONDITIONS -
*
*         GPR1 = WORD OF BINARY INFORMATION TO BE CONVERTED
*         GPR15 = BASE ADDRESS
*
* EXIT CONDITIONS -
*
*         GPR0, GPR1 = DOUBLEWORD OF EBCDIC HEX DIGITS
*
* CALLS TO OTHER ROUTINES -
*
*         NONE
*
* EXTERNAL REFERENCES -
*
*         NONE
*
* TABLES / WORK AREAS -
*
*         TEMPSAVE
*
* REGISTER USAGE -
*
*         GPR14 = RETURN REGISTER
*         GPR15 = BASE REGISTER
*
*         GPR0, GPR1 = WORK REGISTERS
*
*         ALL OTHER REGISTERS ARE NOT USED
*
* NOTES -
*
*         NONE
*
          EJECT
* OPERATION -
*
*         1. UNPACK BINARY INFORMATION.
*         2. TRANSLATE TO HEX EBCDIC DIGITS.
*
*.
          SPACE 3
JCECVTBH DS     0H
          USING *,R15
          USING DATD,R10
          ST    R1,TEMPSAVE    BINARY
          UNPK  TEMPSAVE+8(9),TEMPSAVE(5)     UNPACK
          TR    TEMPSAVE+8(8),TRTAB
          LM    R0,R1,TEMPSAVE+8    RETURN VALUE IN R0 AND R1
          BR    R14            RETURN
          DROP  R15,R10
          EJECT
*.
* SUBROUTINE NAME -
*
*         JCECVTHB
*
* FUNCTION -
*
*         CONVERTS THE EBCDIC HEX FIELD DESIGNATED TO A FULL WORD
*         OF BINARY
*
* ATTRIBUTES -
*
*         SERIALLY REUSABLE, RESIDENT, CALLED VIA BALR
*
* ENTRY POINTS -
*
*         JCECVTHB - CONVERT EBCDIC HEX TO BINARY
*
* ENTRY CONDITIONS -
*
*         GPR0 = LENGTH OF FIELD
*         GPR1 = ADDRESS OF THE FIRST BYTE OF THE FIELD
*
* EXIT CONDITIONS -
*
*         GPR1 = FULL WORD OF BINARY; IF ERROR GPR1 = 0
*
*         CC = 0; IF ERROR CC ¬= 0
*
* CALLS TO OTHER ROUTINES -
*
*         NONE
*
* EXTERNAL REFERENCES -
*
*         NONE
*
* TABLES / WORK AREAS -
*
*         BALRSAVE
*
* REGISTER USAGE -
*
*         GPR14 = RETURN REGISTER
*         GPR15 = BASE REGISTER
*
*         GPR0, GPR1, GPR2, GPR3 = WORK REGISTERS
*
*         ALL OTHER REGISTERS ARE NOT USED
*
* NOTES -
*
*         NONE
*
          EJECT
* OPERATION -
*
*         1. SAVE REGISTERS.
*         2. GET NEXT EBCDIC HEX DIGIT.
*         3. IF DIGIT GREATER THAN "0" AND LESS THAN "9" SUBTRACT X'F0'
*            OTHERWISE IF GREATER THAN "A" SUBTRACT X'B7';
*            OTHERWISE INDICATE CHARACTER ERROR IN FIELD.
*         4. ACCUMULATE BINARY DIGITS IN GPR0.
*         5. LOOP THROUGH ENTIRE FIELD.
*         6. RESTORE REGISTERS.
*
*.
          SPACE 3
JCECVTHB  DS     0H
          USING *,R15
          USING DATD,R10
          STM   R0,R3,BALRSAVE SAVE REGISTERS
          LR    R3,R0          SAVE FIELD LENGTH
          SR    R2,R2          CLEAR REG
          LR    R0,R2          ...
LOOP1     EQU   *
          IC    R2,0(,R1)      GET DIGIT
          CLI   0(R1),C'0'     GREATER THAN ZERO?
          BL    LOOP3          NO  TRY A-F
          CLI   0(R1),C'9'     GREATER THAN NINE?
          BH    ERR2           YES  ERROR
          S     R2,=F'240' (=A(C'0'))  MAKE DIGIT A HEX NUMBER
          B     LOOP2          CONTINUE
LOOP3     EQU   *
          CLI   0(R1),C'A'     LESS THAN "A"?
          BL    ERR2           YES  ERROR
          CLI   0(R1),C'F'     GREATER THAN "F"?
          BH    ERR2           YES  ERROR
          SH    R2,=AL2(C'A'-10)    MAKE CHAR A HEX NUMBER
LOOP2     EQU   *
          SLL   R0,4           ASSEMBLE NEXT DIGIT
          AR    R0,R2          ...
          LA    R1,1(,R1)      BUMP PTR
          BCT   R3,LOOP1       LOOP THROUGH ENTIRE FIELD
          ST    R0,BALR1       RETURN RESULT IN R1
          SR    R0,R0          SET CC=0
          LM    R0,R3,BALRSAVE RESTORE REGISTERS
          BR    R14            RETURN
*
ERR2      DS    0H
          LM    R0,R3,BALRSAVE RESTORE REGISTERS
          LA    R1,0           RETURN ZERO WITHOUT DISTURBING CC
          BR    R14
          DROP  R15,R10
        EJECT
*.
* SUBROUTINE NAME -
*
*         JCECVTDB
*
* FUNCTION -
*
*         CONVERTS THE EBCDIC DECIMAL FIELD DESIGNATED TO A FULL WORD
*         OF BINARY
*
* ATTRIBUTES -
*
*         SERIALLY REUSABLE, RESIDENT, CALLED VIA BALR
*
* ENTRY POINTS -
*
*         JCECVTDB - CONVERT EBCDIC DECIMAL TO BINARY
*
* ENTRY CONDITIONS -
*
*         GPR0 = LENGTH OF FIELD
*         GPR1 = ADDRESS OF THE FIRST BYTE OF THE FIELD
*
* EXIT CONDITIONS -
*
*         GPR1 = FULL WORD OF BINARY; IF ERROR GPR1 = 0
*
*         CC = 0; IF ERROR CC ¬= 0
*
* CALLS TO OTHER ROUTINES -
*
*         NONE
*
* EXTERNAL REFERENCES -
*
*         NONE
*
* TABLES / WORK AREAS -
*
*         TEMPSAVE
*
* REGISTER USAGE -
*
*         GPR14 = RETURN REGISTER
*         GPR15 = BASE REGISTER
*
*         GPR0, GPR1, GPR2 = WORK REGISTERS
*
          EJECT
*         ALL OTHER REGISTERS ARE NOT USED
*
* NOTES -
*
*         NONE
*
* OPERATION -
*
*         1. SAVE REGISTERS.
*         2. IF GPR0 GREATER THAN 10 DIGITS INDICATE ERROR.
*         3. VALIDITY CHECK ALL EBCDIC DECIMAL DIGITS.
*         4. PACK FIELD.
*         5. IF RESULT IS GREATER THAN 2**31-1 INDICATE ERROR.
*         6. CONVERT RESULT TO BINARY.
*         7. RESTORE REGISTERS.
*
*.
          SPACE 3
JCECVTDB  DS     0H
          USING *,R15
          USING DATD,R10
          STM   R1,R2,BALR1    SAVE REGS R1 AND R2
          C     R0,=F'10'      GREATER THAN 10 DIGITS ?
          BH    ERR3
          LR    R2,R0          GET LENGTH OF FIELD
          BCTR  R2,0           DECREMENT FOR EX
DECCHK    CLI   0(1),C'0'
          BL    ERR3
          CLI   0(1),C'9'
          BH    ERR3
          LA    R1,1(,R1)
          BCT   R0,DECCHK
          L     R1,BALR1       RESTORE R1
          EX    R2,PACK        ...
          CP    TEMPSAVE(8),=PL8'2147483647'  GREATER THAN 2**31-1 ?
          BH    ERR3           BRANCH IF YES
          CVB   R1,TEMPSAVE    CONVERT TO BINARY
          SR    R2,R2          SET CONDITION CODE 0
ERR3      EQU   *
          L     R2,BALR2       RESTORE R2
          BR    R14            RETURN
          SPACE 1
PACK      PACK  TEMPSAVE(8),0(0,R1) EXECUTED PACK INSTRUCTION
          SPACE 1
          DROP  R15,R10
          EJECT
*.
* SUBROUTINE NAME -
*
*         JCECVTBD
*
* FUNCTION -
*
*         CONVERT A WORD OF BINARY INFORMATION INTO A DOUBLE-
*         WORD OF EBCDIC DECIMAL DIGITS
*
* ATTRIBUTES -
*
*         SERIALLY REUSABLE, RESIDENT, CALLED VIA BALR
*
* ENTRY POINT -
*
*         JCECVTBD - CONVERT BINARY TO EBCDIC DECIMAL
*
* ENTRY CONDITIONS -
*
*         GPR1 = WORD OF BINARY INFORMATION TO BE CONVERTED
*         GPR0 = 1 IF ZEROES ARE TO BE CONVERTED TO BLANKS
*
* EXIT CONDITIONS -
*
*         GPR0, GPR1 = DOUBLEWORD OF EBCDIC DECIMAL DIGITS
*
* CALLS TO OTHER ROUTINES -
*
*         NONE
*
* EXTERNAL REFERENCES -
*
*         NONE
*
* TABLES / WORK AREAS -
*
*         TEMPSAVE
*
* REGISTER USAGE -
*
*         GPR14 = RETURN REGISTER
*         GPR15 = BASE REGISTER
*
*         GPR0, GPR1 = WORK REGISTERS
*
*         ALL OTHER REGISTERS ARE NOT USED
*
* NOTES -
*
*         NONE
*
          EJECT
* OPERATION -
*
*         1. CONVERT BINARY INFORMATION TO DECIMAL.
*         2. UNPACK DECIMAL INFORMATION.
*         3. SET SIGN TO POSITIVE.
*
*.
          SPACE 3
JCECVTBD  DS     0H
          USING *,R15
          USING DATD,R10
          STM   R2,R3,BALRSAVE
          CVD   R1,TEMPSAVE    BINARY TO PACKED DECIMAL
          UNPK  TEMPSAVE+8(8),TEMPSAVE+3(5)   UNPACK
          OI    TEMPSAVE+15,X'F0'   MAKE UP FOR HARDWARE DEFICIENCIES
          C     R0,=F'1'
          BE    LOADIT
          LA    R2,7
          LA    R3,TEMPSAVE+8
BDLOOP    DS    0H
          CLI   0(R3),C'0'
          BNE   BDNEXT
          MVI   0(R3),C' '
          LA    R3,1(R3)
BDNEXT    BCT   R2,BDLOOP
LOADIT    DS    0H
          LM    R0,R1,TEMPSAVE+8    RETURN VALUE IN R0 AND R1
          LM    R2,R3,BALRSAVE
          BR    R14            RETURN
          DROP  R15,R10
*
*
JCECVTGT DS    0H
***********************************************************************
* THIS ROUTINE WILL RETURN THE TIME IN EBCIDIC FORMAT IN REGISTERS
* R0 AND R1. THE CALLING SEQUENCE IS SIMPLY A CALL JCECVTGT
***********************************************************************
         USING *,R15
         USING DATD,R10
         ST    R15,SAVE15
         TIME  DEC
         L     R15,SAVE15              RELOAD R15
         ST    R0,PDATA                STORE THE TIME
         MVI   RETTIME,C' '            BLANK OUT RETURN
         MVC   RETTIME+1(7),RETTIME        FIELD
         MVC   RETTIME(8),TIMEPAT      MOVE IN PATTERN FOR TIME
         ED    RETTIME(8),PDATA        AND EDIT THE TIME
         LM    R0,R1,RETTIME           RETURN RESULTS
         BR    14                      RETURN
         DROP  R15,R10
JCECVTGD DS    0H
***********************************************************************
* THIS ROUTINE WILL RETURN THE DATE AS AN 8 BYTE EBCIDIC FIELD
* IN REGISTERS R0 AND R1.
***********************************************************************
         USING *,R15                   ADDRESS THE ROUTINE
         USING DATD,R10
         ST    R15,SAVE15              SAVE BASE
         TIME  DEC                     GET TIME
         L     R15,SAVE15              RELOAD BASE
         ST    R1,PDATA                SAVE RESULTS
         MVI   RETDATE,C' '             BLANK OUT DATE FIELD
         MVC   RETDATE+1(7),RETDATE
         MVC   RETDATE(9),DATEPAT       MOVE IN PATTERN
         ED    RETDATE(9),PDATA         EDIT DATE INTO PATTERN
         MVI   RETDATE,C' '             BLANK OUT BYTE 1 OF RESULTS
         MVI   RETDATE+1,C' '             "    "    "  2 OF RESULTS
         LM    R0,R1,RETDATE            RETURN WITH ANSWER
         BR    R14
         DROP  R15,R10
          LTORG
          EJECT
TRTAB     DC    CL240' '
          DC    CL16'0123456789ABCDEF'      FOR TRANSLATES
TIMEPAT   DC    XL8'20207A20207A2020'
DATEPAT   DC    XL9'202021204B20202040'
DATD      DSECT
BALR1     EQU   BALRSAVE+4
BALR2     EQU   BALRSAVE+8
TEMPSAVE  DS    2D
BALRSAVE  DS    18F
SAVE15   DS    F
PDATA    DS    D
RETTIME  DS    CL8
RETDATE  DS    CL8
WKA      DS    2D
          END
 PUNCH ' SETCODE  AC(0)'
 PUNCH ' IDENTIFY JCECVT(''V 2.0 &SYSDATE &SYSTIME'') '
 PUNCH ' ENTRY    JCECVT'
 PUNCH ' NAME     JCECVT(R)'
         END
