        PROC 1 &TABNAME TSSOLIB('TSSO.VERS43') RELOAD
        /*************************************************************/
        /* THIS CLIST WILL ASSEMBLE A TSSO AOF TABLE, AND INSTALL IT */
        /* INTO TSSO.VERS43.LOAD.                                    */
        /*************************************************************/
        CONTROL NOMSG
        FREE FI(SYSIN SYSPRINT SYSTERM SYSLIB SYSPUNCH SYSUT1 SYSLIN SYSIN)
        CONTROL MSG
        ALLOC FI(SYSIN) DSN('&TSSOLIB..AOFTABS(&TABNAME)') REUSE SHR
        ALLOC FI(SYSPRINT) DA(*)
        ALLOC FI(SYSTERM) DA(*)
        ALLOC FI(SYSLIB) DSN('&TSSOLIB..ASM' 'SYS1.AMODGEN' +
              'SYS1.MACLIB') SHR
        ALLOC FI(SYSPUNCH) DUMMY
        ALLOC FI(SYSUT1) NEW SPACE(1 1) CYL DELETE
        ALLOC FI(SYSLIN) NEW SPACE(1 1) CYL DELETE
        CALL  'SYS1.LINKLIB(IEV90)' +
         'RENT,NOALIGN,NOLIST'
        SET RC = &LASTCC
        IF &RC ¬= 0 THEN +
        DO
           OSWTO ASSEMBLER H FAILED - RETURN CODE &RC
           OSWTO LOAD MODULE NOT GENERATED
           EXIT
        END
        ELSE +
        DO
           OSWTO ASMT001I ASSEMBLY FOR TABLE &TABNAME SUCCESSFULL
        END
        CONTROL NOMSG
        FREE FI(SYSUT1 SYSLMOD)
        CONTROL MSG
        ALLOC FI(SYSUT1) NEW SPACE(1 1) CYL DELETE
        ALLOC FI(SYSLMOD) DSN('&TSSOLIB..LOAD(&TABNAME)') SHR
        CALL 'SYS1.LINKLIB(IEWL)' 'RENT,AC'
        SET RC = &LASTCC
        FREE FI(SYSIN SYSPRINT SYSTERM SYSLIB SYSPUNCH SYSUT1 SYSLIN)
        FREE FI(SYSLMOD)
        IF &RC ¬= 0 THEN +
        DO
           OSWTO ERROR IN LINKEDIT - RETURN CODE &RC
           OSWTO POSSIBLE PROBLEM WITH AOF TABLE &TABNAME
           EXIT
        END
        ELSE +
        DO
           OSWTO ASMT002I LINK EDIT FOR TABLE &TABNAME SUCCESSFULL
        END
        IF &RELOAD = RELOAD THEN +
        DO
           OSWTO ASMT003I RELOAD COMMAND BEING ISSUED.
           PDS  '&TSSOLIB..LOAD' COPY &TABNAME  'SYS1.TSSOLOAD' SHR REP
           SET RC = &LASTCC
           IF &RC = 0 THEN +
           DO
             OSCMD #.RELOAD &TABNAME
             OSCMD #.Q T
           END
        END
        EXIT
