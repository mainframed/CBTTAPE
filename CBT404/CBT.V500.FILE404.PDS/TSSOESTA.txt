TSSOESTA CSECT
         EQUATES
**********************************************************************
* THIS MODULE IS THE ESTAE ROUTINE FOR TSSO. ITS PURPOSE IS TO GAIN
* CONTROL OF TSSO AFTER A FAILURE, AND PERFORM THE FOLLOWING FUNCTIONS
*
* 1) DISCONNECT THE SUBSYSTEM FROM MVS.
* 2) FREEMAIN THE TSSO COMMAND RING
* 3) FREEMAIN THE TSSO SSVT
* 4) DELETE THE TSSO AUTOMATED OPERATOR PROCEDURE TABLE
* 5) DELETE THE TSSO SUBSYSTEM SUPPORT MODULE.
* 6) DECREMENT THE COUNT OF SUBSYSTEMS REQUESTING WTO BROADCASTS
* 7) RETURN THE JOB ID TO JES.
*
* IT ACCOMPLISHES THE ABOVE ITEMS BY LINKING TO THE TSSOTERM MODULE
*
* NOTE. IN TSSO 4.3, THE ESTAE FUNCTION NOW CHECKS TO SEE IF IT IS THE
* ACTIVE TSSO. IF IT IS, IT PROCEEDS WITH TERMINATION. IF NOT, IT
* MEARLY ALLOWS THE TERMINATION TO CONTINUE.
*
*
* LAST MESSAGE: 403


*        +---------+
*        + CHANGES +
*        +---------+
*  30 DEC 96  RPS FIXED ADDRESSABILITY ERROR WITH HLASM
*                                                               EEJ1102
*  NOV 2002   EEJ Removed extraneous PUSH USING added by RPS    EEJ1102
*                                                               EEJ1102

***********************************************************************
* NOTE: MESSAGES PRODUCED BY THE ESTAE MODULE HAVE MESSAGE ID'S IN THE*
* FORM TSSEXXXA WHERE 401 < XXX < 450                                 *
***********************************************************************
**********************************************************************
*
* IT DOESNT MUCH MATTER WHETHER WE HAVE AN SDWA OR NOT, BECAUSE WE
* CAN ACCESS TSSO PROGRAM VARIABLES FROM THE TSSO CVT.
         USING TSSOESTA,R15
         B     EYE1
         COPY  EYECATCH
EYE1     DS    0H
         XR    R9,R9            R9 = 0
         LR    R7,R14           SAVE RETURN POINT
         CH    R0,=H'12'
         BE    NOSDWA
         LR    R9,R1            SAVE ADDRESS OF THE SDWA.
NOSDWA   DS    0H
***********************************************************************
* R9 = 0 -> ESTAE WAS ENTERED WITHOUT SDWA                            *
* R9 Â¬= 0 -> R9 HAS ADDRESS OF THE SDWA                               *
***********************************************************************
         DROP  R15                                               RPS
         LR    R12,R15
         USING TSSOESTA,R12
         GETCVT (R10)
         USING TSSOCVT,R10
         TESTAUTH FCTN=1       (PTF2) INCLUSIVE THROUGH ESTAEX LABEL
         LTR   R15,R15
         BZ    ESTAEX
         WTO   'TSSE403E TSSO HAS BEEN DEAUTHORIZED - TSSO WILL ATTEMPTX
                TO REGAIN AUTHORIZATION'
NEEDAUTH DS    0H
         GETCVT (R6),NDSA=YES    GET THE NDSA
         USING TSSONDSA,R6
         L     R2,TSSOLX        GET TSSO LINKAGE INDEX
         DROP  R6
         O     R2,=X'00000001'  OBTAIN PC SERVICE NUMBER
         STM   R14,R12,12(R13)   REMEMBER REGS
         L     R15,=CL4'TAPF'   WHATEVER
         LA    R0,20            WHAT EVER
         PC    0(R2)            GET AUTHORIZED
         LM    R14,R12,12(R13)    (PTF2 END)
ESTAEX   DS    0H
         L     R6,X'224'           GET ASCB ADDRESS
         USING ASCB,R6
         LH    R6,ASCBASID         GET ASID FIELD
         DROP  R6
         GETCVT (R2),NDSA=YES
         USING TSSONDSA,R2
         CH    R6,TSSOASID         IS THIS THE ACTIVE TSSO ?
         BE    ACTTSSO             YES, PROCEED WITH TERMINATION
*
         DROP  R2
         WTO   'TSSE402I TSSO ESTAE IS IN CONTROL FOR INACTIVE TSSO'
         B     IACTTSSO
*
ACTTSSO  DS    0H
         WTO   'TSSE401I TSSO ESTAE IS IN CONTROL'
**********************************************************************
* AT THIS POINT, THE ESTAE ROUTINE IS READY TO GO...
* WE WILL LINK TO THE TERMINATION ROUTINE FOR TSSO, TO DISCONNECT THE
* SUBSYSTEM, FREEMAIN ALL AREAS, ETC...
**********************************************************************
         MODESET KEY=ZERO
         OI    TSSOFLG1,TSSOGONE     IMMEDIATELY DISCONTINUE...
         MODESET KEY=NZERO
         LINK  EP=TSSOTERM
**********************************************************************
* HERE, WE ARE READY TO RETURN TO MVS. NO RETRY WILL BE ATTEMPTED.
*
**********************************************************************
IACTTSSO DS    0H
         LTR   R9,R9                 DID WE GET AN SDWA
         BNZ   TERMSDWA              YES, TERMINATE WITH SDWA
* TERMINATE WITHOUT SDWA
*
         XR    R15,R15
         BR    R7                    GO AWAY
*
TERMSDWA DS    0H
         SETRP WKAREA=(R9),DUMP=YES,RC=0
**********************************************************************
* LASTLY, WE ARE FINALLY READY TO FREEMAIN THE TSSO CVT.
**********************************************************************
*
         WTO   'TSSE402E T S S O IS SADLY TERMINATING DUE TO AN'
         WTO   '         UNEXPECTED ABEND. NOTIFY THE TSSO SYSTEM     '
         WTO   '         PROGRAMMER OF THE ABEND CODE AND RESTART TSSO'
         WTO   '         WITH THE  "S TSSO,SUB=JES2" COMMAND'
         BR    R7                     GO AWAY...
         PRINT NOGEN
         IHASDWA
         TSSOCVT
         IEFJSCVT
         IEFJESCT
         IHAASCB
         TSSONDSA
         CVT  DSECT=YES
         PRINT GEN
         END  TSSOESTA
 PUNCH ' SETCODE  AC(1)'
 PUNCH ' IDENTIFY TSSOESTA(''TSSO V 4.3.0 (PTF2) &SYSDATE &SYSTIME'')'
 PUNCH ' ENTRY    TSSOESTA'
 PUNCH ' NAME     TSSOESTA(R)'
         END
