         TITLE ' T S S O   WRITE TO OPERATOR C O M M A N D '
OSWTO    CSECT
***********************************************************************
* THIS IS THE OSWTO COMMAND PROCESSOR. IT'S PURPOSE IS TO SUPPLY      *
* A REAL TIME MECHANISM FOR A TSSO CLIST TO SEND A MESSAGE TO THE     *
* MVS OPERATOR.                                                       *
*                                                                     *
* COMMAND SYNTAX:                                                     *
*                                                                     *
* OSWTO ANY-TEXT                                                      *
* OSWTOH ANY-TEXT                                                     *
*                                                                     *
*                                                                     *
* IF OSWTOH IS THE COMMAND USED, THE MESSAGE IS HILIGHTED TO THE      *
* MVS OPERATOR. OTHERWISE, THE MESSAGE IS SIMPLY ISSUED. THE CONSOLE  *
* UTILIZED WILL BE THE IN THE CURRCONS VARIABLE IN THE TSSO DATA      *
* AREA, THEREFORE, THIS COMMAND IS ONLY USABLE UNDER TSSO. NOTE THAT  *
* THE TSO SEND COMMAND MAY BE USED UNDER REGULAR TSO TO SEND A        *
* MESSAGE TO THE MVS OPERATOR.                                        *
*                                                                     *
*                                                                     *
****************************************************************EEJ1102
*                                                               EEJ1102
* Change Activity:                                              EEJ1102
*                                                               EEJ1102
*     NOV 2002 EEJ  Support MCS extended consoles               EEJ1102
*                                                                     *
*                                                                     *
***********************************************************************
         EQUATES
         STM   R14,R12,12(R13)    SAVE CALLERS REGSITERS
         LR    R12,R15            LOAD ENTRY POINT OF OSCMD
         LR    R8,R1              SAVE INPUT PARMS.
         USING OSWTO,R12
         B     EYE1
         COPY  EYECATCH
EYE1     DS    0H
*                                 THEN I EXPECTED.
         GETMAIN R,LV=72          WE NEED STORAGE FOR THE SAVE AREA
         ST    R13,4(R1)          STORE ADDRESS OF CALLERS SAVE AREA
*                                  SAVE AREA
         ST    R1,8(R13)          REMEMBER WHERE THIS CORE IS
         LR    R13,R1             KEEP A COPY IN R13
*
         GETMAIN R,LV=4096         GETMAIN SOME PRIVATE STORAGE...
         LR    R11,R1
         USING OSWRDATD,R11
***********************************************************************
* THIS SECTION OF THE CODE SETS UP THE TSO IOPL, ALLOWING FOR GETLINE *
* AND PUTLINE.                                                        *
***********************************************************************
         LA    R2,IOPLADS
         USING IOPL,R2
         L     R3,0(8)
         ST    R3,CPPLCBUF
         L     R3,4(8)
         ST    R3,CPPLUPT
         ST    R3,IOPLUPT
         L     R3,8(8)
         ST    R3,CPPLPSCB
         L     R3,12(8)
         ST    R3,CPPLECT
         ST    R3,IOPLECT
         L     R3,ECBADS
         ST    R3,IOPLECB
*
***********************************************************************
* SINCE OSWTO IS A RE-ENTRANT MODULE, WE WILL COPY THE CONSTANTS INTO *
* THE VARIABLE WORK AREA.                                             *
***********************************************************************
*
         MVC   LINE1HD(80),LINE1HC
         MVC   LINE2HD(80),LINE2HC
         MVC   MESSAGED(MESSAGEE-MESSAGEC),MESSAGEC
***********************************************************************
* STEP ONE IS TO GET THE MESSAGE AND MOVE IT INTO A BUFFER.           *
*                                                                     *
*                                                                     *
***********************************************************************
         L     R2,CPPLCBUF         GET THE COMMAND BUFFER.
         LH    R3,2(R2)            GET THE OFFSET TO THE FIRST OPERAND
         LH    R4,0(R2)            GET THE TOTAL LENGTH OF THE BUFFER
         LA    R5,4(R3,R2)         R5 NOW POINTS AT THE MESSAGE
         LA    R3,0(R2,R4)         R3 NOW POINTS AT THE END OF THE MSG
         SR    R3,R5               R3 NOW HAS LENGTH OF THE MESSAGE
         LTR   R3,R3
         CH    R3,=H'1'
         BL    ERROR1
         CH    R3,=H'125'
         BH    ERROR1
         MVI   MESSAGED+4,X'40'
         MVC   MESSAGED+5(124),MESSAGED+4
         BCTR  R3,0
         EX    R3,MOVEMSG
         B     AR1
MOVEMSG  MVC   MESSAGED+4(0),0(R5)
AR1      DS    0H
***********************************************************************
* THE MESSAGE HAS BEEN SAFELY MOVED TO MESSAGED. NOW, WE MUST PICK UP *
* THE CONSOLE ID TO WRITE THE MESSAGE OUT TO. THIS IS IN THE TSSODATD *
* DATA AREA                                                           *
*                                                                     *
***********************************************************************
         GETCVT (R10)
         USING TSSOCVT,R10
         L     R9,TSSODSEC
         USING DATD,R9
         L     R2,TSSOASCB             GET TSSO ASCB
         C     R2,X'224'               SAMES AS OURS ?
         BNE   UNDERTSO
         L     R2,CURRCONS
         ST    R2,CONSOLE          Save console ID              EEJ1102
         B     PROCCMD
UNDERTSO DS    0H
         XC    CONSOLE,CONSOLE     Zero console ID              EEJ1102
         DROP  R9,R10
*
***********************************************************************
* FINALLY, WE NEED TO DETERMINE IF THE MESSAGE IS TO BE HILIGHTED...  *
* IF YES, WE NEED TO TURN ON DESCRIPTOR CODE 1 IN THE MESSAGE.        *
***********************************************************************
PROCCMD  DS    0H
         L     R2,CPPLCBUF
         CLC   4(6,R2),=CL6'OSWTOH'   SPECIAL OSWTOH ?
         BE    HILIGHT
         CLC   4(5,R2),=CL5'OSWTO'
         BE    NOHIGH
         WTO   'OSWTO01S LOGIC ERROR -> CONTACT TSSO SUPPORT'
         B     ENDCMD
HILIGHT  DS    0H
         OI    MESSAGED+149,X'80'  Set descriptor code 1        EEJ1102
         B     ISSUEMSG
NOHIGH   DS    0H
         B     ISSUEMSG
ISSUEMSG DS    0H
         WTO   CONSID=CONSOLE,MF=(E,MESSAGED)                   EEJ1102
         B     ENDCMD
ENDCMD   DS    0H
ENDPROG  DS    0H
         FREEMAIN R,LV=4096,A=(R11)
         LR    R1,R13              GET ADDRESS OF CORE IN R1
         L     R13,4(R13)          RESTORE R13
         FREEMAIN R,LV=72,A=(R1)    FREE UP THE CORE
         XR    R8,R8
         B     ENDPROG1
ENDPROG1 DS    0H
         L     R14,12(R13)         RELOAD RETURN POINT
         LR    R15,R8              PICK UP RETURN CODE.
         LM    R0,R12,20(R13)      RELOAD MORE REGISTERS
         BR    R14
*
***********************************************************************
* E R R O R   C O N D I T I O N S ...                                 *
*                                                                     *
***********************************************************************
ERROR1   DS    0H
         PUTLINE PARM=PUTBLOK,OUTPUT=(LINE1HD,TERM,SINGLE,DATA),       X
               MF=(E,IOPLADS)
         B     ENDPROG
*
ERROR2   DS    0H
         PUTLINE PARM=PUTBLOK,OUTPUT=(LINE2HD,TERM,SINGLE,DATA),       X
               MF=(E,IOPLADS)
         B     ENDPROG
*
***********************************************************************
*                                                                     *
* CONSTANTS TO BE USED FOR THE OSWTO MODULE                           *
*                                                                     *
*                                                                     *
***********************************************************************
LINE1HC  DC    H'80',H'0'
LINE1    DC    CL76'OSWTO TERMINATING -> INVALID MESSAGE LENGTH < 1 OR X
               > 125'
*
LINE2HC  DC    H'80',H'0'
LINE2H   DC    CL76'OSWTO TERMINATING -> IT WAS NOT ISSUED FROM THE TSSX
               O ADDRESS SPACE'
*
MESSAGEC WTO   '                                                       X
                                                                       X
                             ',MCSFLAG=(RESP),DESC=(6),         EEJ1102+
               CONSID=,MF=L                                     EEJ1102
MESSAGEE EQU   *
************************************************************
*                                                          *
*        VARIABLE WORK AREAS, COVERED BY BASE REG R11.     *
*                                                          *
************************************************************
OSWRDATD DSECT
OSWKAREA DS    CL200
CPPLCBUF DS    A
CPPLUPT  DS    A
CPPLPSCB DS    A
CPPLECT  DS    A
IOPLADS  DS    4F               SPACE FOR IO PARM LIST
ECBADS   DC    F'0'             ECB FOR PUTLINE, GETLINE
PUTBLOK  PUTLINE MF=L             DEFAULT PUTLINE MACRO, LIST FORM
CONSOLE  DS    F                   Console ID                   EEJ1102
MESSAGED WTO   '                                                       X
                                                                       X
                             ',MCSFLAG=(RESP),DESC=(6),         EEJ1102+
               CONSID=,MF=L                                     EEJ1102
LINE1HD  DC    H'80',H'0'
         DC    CL76'OSWTO TERMINATING -> INVALID MESSAGE LENGTH < 1 OR X
               > 125'
*
LINE2HD  DC    H'80',H'0'
         DC    CL76'OSWTO TERMINATING -> IT WAS NOT ISSUED FROM THE TSSX
               O ADDRESS SPACE'
         IKJIOPL
         IKJPSCB
         IEFJSCVT
         IEFJESCT
         TSSOCVT
         COPY  TSSODATD
         CVT   DSECT=YES
         END   OSWTO
 PUNCH ' SETCODE  AC(1)'
 PUNCH ' IDENTIFY OSWTO(''TSSO V 4.3.0 &SYSDATE &SYSTIME'') '
 PUNCH ' ALIAS    OSWTOH'
 PUNCH ' ENTRY    OSWTO'
 PUNCH ' NAME     OSWTO(R)'
         END
