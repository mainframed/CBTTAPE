         MACRO
&LABEL   UPDATEF &COUNT
&LABEL   L      R14,&COUNT
         LA     R14,1(R14)
         ST     R14,&COUNT
         MEND
SPMON    MENTER 12,EQU,CP=CP,COM=TSSOSPMON
***********************************************************************
*  REGISTERS USED:                                                    *
*    R1 - BUFFER ADDRESS         R6 - PTR TO OUCB                     *
*    R2 - PTR TO ASVT ENTRY      R7 - R11  WORKING REGS               *
*    R3 - COUNT OF ASCBS         R12 - BASE REGISTER                  *
*    R4 - PTR TO OUXB                                                 *
*    R5 - PTR TO ASCB                                                 *
*                                                                     *
***********************************************************************
*                                                                     *
*                        LOCATE CVT AND ASVT POINTERS                 *
*                                                                     *
***********************************************************************
         B     AR7
         COPY EYECATCH
AR7      DS    0H
FINDUTIL DS    0H                 FIRST, WE FIND THE UTILIZATION
         SETPARSE PCL=SPMONPCL
         CALLTSSR EP=IKJPARS
         USING IKJPARMD,R9
         L     R9,MYANS
         TM    REQPARM+6,X'80'      ANY OPERANDS ?
         BZ    AROUND             NOPE, ASSUME DEFAULTS.
         L     R2,REQPARM
         LH    R3,REQPARM+4
         BCTR  R3,0
         STH   R3,LJOBNAME        REMEMBER FOR COMPARES
         MVC   JOBNAME(8),=CL8' '
         EX    R3,MOVEOPER
         B     AROUND
MOVEOPER MVC   JOBNAME(0),0(R2)
AROUND   DS    0H
         TM    REQTYPE+6,X'80'
         BZ    AROUND2            NO REQUESTED TYPE, DEFAULT=ALL
         L     R2,REQTYPE
         LH    R3,REQTYPE+4
         CH    R3,=H'3'           JOBTYPE HAS GOT TO BE THREE CHARS.
         BNE   AROUND2            INVALID, ASSUME OMITTED.
         MVC   PARMTYPE(4),=CL8' '
         MVC   PARMTYPE(3),0(R2)
AROUND2  DS    0H
         DROP  R9
         L     R2,16
         USING CVT,R2
         SETMODE MODE=31
         L     R2,CVTOPCTP
         L     R2,228(R2)         RCT
         CLC   0(3,R2),=CL3'RCT'  IS THIS THE RCT?
         BNE   FINDCVT
         ISXA  XA=XAUTIL
         LH    R3,64(R2)
         ST    R3,CPUUTIL         ** CPU UTILIZATION **
         LH    R3,62(R2)
         ST    R3,UIC             ** UIC **
         LH    R3,76(R2)
         ST    R3,PAGERATE        ** PAGING RATE **
         B     GOTUTIL
XAUTIL   DS    0H
         LH    R3,56(R2)
         ST    R3,CPUUTIL
         LH    R3,54(R2)         ** UIC/XA **
         ST    R3,UIC
         LH    R3,68(R2)         ** PAGERATE/XA **
         ST    R3,PAGERATE
         SETMODE MODE=24
GOTUTIL  DS    0H
         L     R3,CPUUTIL         GET CPU UTILIZATION
         SRA   R3,4               DEVIDE BY 16
         LR    R1,R3
         LA    R10,WORKAREA
         CALL  JCECVTBD           CONVERT THE ROUTINE
         STCM  R1,B'0111',HUTIL
HUNZERO  DS    0H
         L     R1,UIC
         CALL  JCECVTBD
         STCM  R1,B'0111',HUIC
HUNZERO1 DS    0H
         L     R1,PAGERATE
         CALL  JCECVTBD
         STCM  R1,B'1111',HPAGRATE
         SETMODE MODE=24
         PUTLINE PARM=PUTBLOK,OUTPUT=(HEADER,TERM,SINGLE,DATA),        X
               MF=(E,IOPLADS)
FINDCVT  L     R2,16              CVT POINTER
         USING CVT,R2
         L     R2,CVTASVT         ASVT POINTER
         DROP  R2
         USING ASVT,R2
         L     R3,ASVTMAXU        MAXIMUM NUMBER OF ENTRIES
         XR    R2,R2
         STH   R2,ASIDNUM
***********************************************************************
*                                                                     *
*            MAJOR LOOP (THROUGH ALL ASCB'S)                          *
*                                                                     *
***********************************************************************
ASCBLOOP DS    0H
         MVC   TYPE(3),=CL3' '
*
         LH    R2,ASIDNUM         R2 = LAST ASID USED.
         LA    R2,1(R2)
         STH   R2,ASIDNUM
*
         LOCASCB ASID=ASIDNUM     INCREMENT TO NEXT ASCB ENTRY
         LTR   R15,R15            As of Z/OS Brian Westerman 1/12/05
         BNZ   NOGOOD
         LR    R5,R1              GET ASCB ADDRESS
         SRL   R5,31              SHIFT ALL BUT HIGH ORDER BYTE.
         LTR   R5,R5              IF ON, ASID WAS NOGOOD
         BNZ   NOGOOD
         LR    R5,R1              OTHERWISE, USE IT!
         USING ASCB,R5
         SETMODE MODE=31
         L     R6,ASCBOUCB        OUCB POINTER
         USING OUCB,R6
         L     R4,ASCBOUXB        OUXB POINTER
         USING OUXB,R4
         TM    OUCBYFL,OUCBLOG    LOGON CREATED USER?
         MVC   JOB(8),=CL8'STARTING'
         ICM   R7,B'1111',ASCBJBNI JOBNAME POINTER
         BZ    NOTJOB             JOBNAME NOT AVAILABLE
         MVC   JOB,0(R7)          MOVE IN JOBNAME
         MVC   TYPE(3),=CL3'JOB'
         UPDATEF JCOUNT
         B     CONT1
NOTJOB   ICM   R7,B'1111',ASCBJBNS S/M/L POINTER
         BZ    NOGOOD             S/M/L NOT AVAILABLE
         MVC   JOB,0(R7)          MOVE IN JOBNAME FOR S/M/L
CONT1    DS    0H
***********************************************************************
*                                                                     *
*                    ADDRESS SPACE QUEUE LOCATION                     *
*                                                                     *
***********************************************************************
         CLC   JOB,=CL8'STARTING'
         BE    NOGOOD
         MVC   INOROUT(3),=CL3'IN'       ADDRESS SPACE ON 'IN' QUEUE
WAITQ    TM    OUCBQFL,OUCBOFF    'WAIT' QUEUE
         BZ    OUTQ               NO
         MVC   INOROUT(3),=CL3'OUT'          MUST BE OUTN
         B     DONE
OUTQ     TM    OUCBQFL,OUCBOUT    'OUT' QUEUE
         BZ    TRANS              NO
         MVC   INOROUT(3),=CL3'OUT'
         B     DONE
TRANS    TM    OUCBQFL,OUCBGOO+OUCBGOI+OUCBGOB    TRANSITIONING' STATUS
         BZ    DONE               NO
         MVC   INOROUT(3),=CL3'TRA'
DONE     DS    0H
***********************************************************************
*                                                                     *
*                 FRAMES ALLOCATED TO ADDRESS SPACE                   *
*                                                                     *
***********************************************************************
         LH    R1,ASCBFMCT        ALLOCATED PAGE FRAME COUNT
         CALL  JCECVTBD
         ST    R1,DECMEM
*        EJECT
***********************************************************************
*                                                                     *
*                   INTERVAL SERVICE ACCUMULATION                     *
*                                                                     *
***********************************************************************
         L     R1,OUCBWMS         SERVICE UNITS - XACTION
         CALL  JCECVTBD           CONVERT TO DECIMAL.
         STCM  R0,B'1111',DECSERV
         STCM  R1,B'1111',DECSERV+4
***********************************************************************
*                                                                     *
*                    M/S/L                                            *
*                                                                     *
***********************************************************************
         CLC   TYPE(3),=CL3'JOB'
         BE    CONT2
         TM    OUCBYFL,OUCBSTT    START CREATED TASK?
         BZ    TEST2              NO
         CLC   JOB(4),=CL4'INIT'    INITIATOR ?
         BNE   TEST2P5
         UPDATEF ICOUNT
         B     CONT2
TEST2P5  DS    0H
         MVC   TYPE(3),=CL3'STC'
         UPDATEF SCOUNT
         B     CONT2
TEST2    TM    OUCBYFL,OUCBLOG    LOGON CREATED TASK?
         BZ    TEST3              NO
         MVC   TYPE(3),=CL3'TSO'
         UPDATEF TCOUNT
         B     CONT2
TEST3    TM    OUCBYFL,OUCBMNT    MOUNT CREATED TASK?
         BZ    CONT2              NO
         MVC   TYPE(3),=CL3'MNT'
CONT2    DS    0H
         LH    R7,LJOBNAME        GET LENGTH FOR EXECUTE
         EX    R7,COMPJOB         DOIT !
         BE    CHECKTYP
         CLC   JOBNAME(8),=CL8'ALL'  ARE WE LOOKING FOR ALL JOBS ?
         BE    CHECKTYP
         B     NOGOOD              DONT PRINT, JUST GET NEXT ADDR SPACE
COMPJOB  CLC   JOB(0),JOBNAME     *** EXECUTED **
CHECKTYP DS    0H
* WE COME HERE, IF THE JOBNAME IS OK, BUT THE OPERATOR MAY HAVE
* REQUESTED A CERTAIN TYPE OF JOB.
         CLC   PARMTYPE(1),TYPE    CHECK TYPE
         BE    CHEKNEXT
         CLC   PARMTYPE(3),=CL3'ALL'
         BE    CHEKNEXT
         B     NOGOOD
CHEKNEXT DS    0H
PRINTLNE DS    0H
         SETMODE MODE=24
LINEDONE PUTLINE PARM=PUTBLOK,OUTPUT=(LINE,TERM,SINGLE,DATA),          X
               MF=(E,IOPLADS)
NOGOOD   DS    0H                 PROCESS NEXT ADDRESS SPACE
         BCT   R3,ASCBLOOP        PROCESS NEXT ADDRESS SPACE
*
*
*
***********************************************************************
* ALL DONE PROCESSING THESE ADDRESS SPACES.                           *
*                                                                     *
***********************************************************************
         DROP  R4,R5,R6
         L     R1,TCOUNT
         CALL  JCECVTBD
         ST    R1,TSOEBC
         L     R1,SCOUNT
         CALL  JCECVTBD
         ST    R1,STCEBC
         L     R1,JCOUNT
         CALL  JCECVTBD
         ST    R1,JOBEBC
         L     R1,ICOUNT
         CALL  JCECVTBD
         ST    R1,INITEBC
         PUTLINE PARM=PUTBLOK,OUTPUT=(TRAILER,TERM,SINGLE,DATA),       X
               MF=(E,IOPLADS)
         MLEAVE
         EJECT
         PRINT NOGEN
SPMONPCL IKJPARM
REQPARM  IKJIDENT 'JOBNAME',UPPERCASE,MAXLNTH=8,FIRST=ALPHA
REQTYPE  IKJIDENT 'TYPE',UPPERCASE,MAXLNTH=4,FIRST=ALPHA
         IKJENDP
WORKAREA DS    CL200
TRAILER  DC    H'80',H'0'
TSOEBC   DS    F
TRAILLNE DC    C' TSO USERS, '
STCEBC   DS    F
         DC    C' STARTED TASKS, '
JOBEBC   DS    F
         DC    C' BATCH JOBS, '
INITEBC  DS    F
         DC    C' INACTIVE INITS.'
         DC    CL50' '
HEADER   DC    H'80',H'0'
HLINE    DC    C'CPU UTILIZATION: '
HUTIL    DC    CL4'100%'
         DC    C'  UIC: '
HUIC     DC    CL4'255 '
         DC    C'   PAGING RATE: '
HPAGRATE DC    CL4'100 '
HFILL    DC    CL60' '
PTRN1    DC    X'2020202020202120'
ASIDNUM  DS    H
WKA      DS    2D
LINE     DS    0H
LINEL    DC    H'80,0'
TYPE     DC    CL4'JOB '
JOB      DC    CL8' '
ISWORD   DC    C' IS '
INOROUT  DC    CL5'OUT, '
DECSERV  DC    CL9' '
SERSTRIN DC    C'SU''S, '
DECMEM   DC    CL4' '
MEMWORD  DC    C' FRAMES'
BLANKS   DC    CL80' '
CPUUTIL  DS    F
PAGERATE DS    F
UIC      DS    F
JCOUNT   DC    F'0'
TCOUNT   DC    F'0'
SCOUNT   DC    F'0'
ICOUNT   DC    F'0'
LJOBNAME DC    H'6'
JOBNAME  DC    CL8'NOOPER'
PARMTYPE DC    CL4'ALL'
         CVT   DSECT=YES
         IRAOUCB
         IHAASCB
         IHAASVT
         IHAOUXB
         IKJIOPL
         END   SPMON
 PUNCH ' SETCODE  AC(0)'
 PUNCH ' IDENTIFY SPMON(''TSSO V 4.1.0 &SYSDATE &SYSTIME'') '
 PUNCH ' INCLUDE  SYSLMOD(JCECVT)'
 PUNCH ' ENTRY    SPMON'
 PUNCH ' NAME     SPMON(R)'
         END
