./ ADD NAME=HSMCYCLE 0102-92295-92303-1101-00012-00013-00000-CART1
PROC 0
 /*                                                                  */
 /*      PDSDOC *CLIST TO STOP AND RESTART DF/HSM                    */
 /*                                                                  */
 EXEC 'SYS2.TSSOR43.CLIST(HSMSTOP)'
 SET &ENDOK = &LASTCC
 IF &ENDOK NE 0 THEN EXIT CODE(8)
 /*                                                                  */
 /* NOW RESTART HSM.                                                 */
 /*                                                                  */
 OSCMD S HSM.HSM
END
./ ADD NAME=HSMSTOP  0101-92303-92303-1103-00011-00009-00000-CART1
PROC 0
 /*                                                                  */
 /*      PDSDOC *CLIST TO DO A SYNCHRONISED STOP OF DF/HSM           */
 /*                                                                  */
 OSCMD F HSM,STOP
 OSWAIT ENTRY(HSMEND) TIMEOUT(3600)
 SET ENDOK = &LASTCC
 IF &ENDOK EQ 0 THEN EXIT CODE(0)
    ELSE WTOH '*** CHECK HSM STATUS ***'
    EXIT CODE(8)
END
./ ADD NAME=HSMTAPE  0102-92295-92295-1429-00012-00014-00000-CART1
PROC 1 VOLSER
 /*                                                                  */
 /*      PDSDOC *CLIST TO TEST FOR TAPE AVAILABILITY AND REPLY 'Y'   */
 /*                                                                  */
 CALL 'SYS2.LINKLIB(MPFTAPEQ)' '&VOLSER'
 SET TAPEOK = &LASTCC
 IF &TAPEOK NE 0 THEN EXIT CODE(0)
 /*                                                                  */
 /* THE TAPE HAS RECENTLY BEEN USED. WE CAN SAY IT IS AVAILABLE.     */
 /*                                                                  */
 REPLY MSG(ARC0366A) TEXT('Y')
END
./ ADD NAME=NOPAPER  0113-92240-92268-1311-00051-00018-00000-CART1
PROC 1 RMT
 /*                                                                  */
 /*      PDSDOC *CLIST DISABLES JES328X PRINTERS WITH INTVN REQUIRED */
 /*                                                                  */
 /* FIRST CHECK IF IN PRIME SHIFT - LET OPERATOR FIX IT              */
     EXEC SYS2.TSSOR43.CLIST(PRIME)
     SET SHIFT = &LASTCC
 /*  IF &SHIFT = 0 THEN EXIT CODE(0)                                 */
 /*                                                                  */
 /* AS OPERATORS ARE NOTORIOUSLY SLOW WE PREFER TO GIVE THEM SOME    */
 /* TIME TO RELOAD THE PAPER.                                        */
 /*                                                                  */
 /* SCHEDULE A COMMAND TO RESTART THE PRINTER IN THE FUTURE          */
 /*                                                                  */
     SET HOURS = &SUBSTR(1:2,&SYSSTIME)
     SET MINS  = &SUBSTR(4:5,&SYSSTIME)
     IF &SHIFT = 0 THEN DO
                    SELECT (&MINS)
                     WHEN (30:44) SET STIME = &STR(&HOURS..45)
                     WHEN (15:29) SET STIME = &STR(&HOURS..30)
                     WHEN (00:14) SET STIME = &STR(&HOURS..15)
                     OTHERWISE DO
                               SET HOURS = &HOURS+1
                               SET STIME = &STR(&HOURS..00)
                               END
                    END
                   END
 /*                                                                  */
 /* OVERNIGHT WE JUST SHUT IT DOWN. DUE TO DEFICIENCIES IN JES2      */
 /* AUTOMATIC COMMAND SCHEDULING WE HAVE A LITTLE FLURRY OF ACTIVITY */
 /* AT MIDNIGHT.                                                     */
 /*                                                                  */
     ELSE DO
                    SELECT (&HOURS)
                     WHEN (17:23) SET STIME = &STR(23.59)
                     WHEN (00:06) SET STIME = &STR(07.00)
                     OTHERWISE DO
                               SET HOURS = &HOURS+1
                               SET STIME = &STR(&HOURS..00)
                               END
                    END
          END
    SET START1 = &STR($TA,T=&STIME,'$VS,''.RESTART &RMT.''')
 /*                                                                  */
     SET PPRT = &STR($PLNE&RMT)
     SET LINE = &STR($ELNE&RMT)
 /* DISABLE THE PRINTER                                              */
     WRITE *COM001 &PPRT
     WRITE *COM    &LINE
     WRITE *COM    &START1
END
./ ADD NAME=PRIME    0103-92240-92248-0911-00016-00010-00000-CART1
PROC 0
 /*                                                                  */
 /*      PDSDOC *CLIST TO TEST FOR PRIME SHIFT. RETURN RC(0) IF YES. */
 /*                                                                  */
 /* THE FIRST PART IS VALID IF 'DAYOWEEK' HAS BEEN INSTALLED.        */
 /* SEE CBT FILE 172                                                 */
 DAYOWEEK
 SET DAYNUM = &LASTCC
 IF &DAYNUM GT 5 THEN EXIT CODE(8)
 /*                                                                  */
 SET HOURS = &SUBSTR(1:2,&SYSSTIME)
 SELECT
   WHEN (&HOURS GT 17) EXIT CODE(8)
   WHEN (&HOURS LT 07) EXIT CODE(8)
 END
END
./ ADD NAME=RESTART  0104-92268-92268-1308-00018-00018-00000-CART1
PROC 1 RMT
 /*                                                                  */
 /*      PDSDOC *CLIST RE-ENABLES JES328X PRINTERS STOPPED BY NOPAPER*/
 /*                                                                  */
 /*                                                                  */
 /*                                                                  */
    SET START1 = &STR($SLNE&RMT.)
    SET START2 = &STR(F JES328X,S,RMT&RMT.)
 /*                                                                  */
     OSCMD &START1
RETRY: +
     OSPAUSE 1
     OSCMD &START2
     OSWAIT ENTRY(JSXOK) TIMEOUT(5)
     SET RESPOND = &LASTCC
     IF &RESPOND = 8 THEN EXIT CODE(0)
        ELSE GOTO RETRY
END
./ ADD NAME=RMTGO    0109-91199-92241-0748-00008-00003-00000-CART1
 /*                                                                  */
 /*      PDSDOC *AUTOMATICALL START RMT PRINTER ON FORM CHANGE...... */
 /*                                                                  */
PROC 1 DEVICE
 SET UNIT = &STR($S&DEVICE)
 DELAY 2
 WRITE *COM &UNIT
END
./ ADD NAME=DOWN
PROC 0 DEBUG TASK()

 /*                                                                  */
 /*      PDSDOC *GENERALISED SHUTDOWN CLIST..........................*/
 /*                                                                  */
 /*  23APR99 DHC CREATED USING CODE FROM JIM LANE'S "HOTSTART"       */

  IF &DEBUG = DEBUG THEN CONTROL  MSG LIST CONLIST SYMLIST FLUSH
                    ELSE CONTROL  NOMSG NOLIST FLUSH
 /*                                                                  */

        /*************************************************************/
        /* THE FIRST STEP IS DIRECTING THE OSCMD COMMAND PROCESSOR   */
        /* TO SEND THE RESPONSE OF THE COMMAND TO CLIST VARIABLES,   */
        /* TO SET THE EXPECTED COMMAND RESPONSE COUNT AND            */
        /* TO DECIDE HOW LONG THE CLIST SHOULD WAIT FOR THE RESPONSE */
        /*************************************************************/
        SET CMDRESP = CLIST
        SET MAXCMDOUT = 20
        SET CMDWAIT = 5

 /*                                                                  */
  OSCMD P &TASK
/* WAIT FOR TASK TO END                          */
  SET I = 1                                                               004200
  DO WHILE (&I <= 180)                                                    004400
    OSCMD D J,&TASK                                                       004500
    SET STATUS1 = &CMDOUT5W2                                              004600
    SET STATUS2 = &CMDOUT5W3                                              004700
    IF &STATUS1 = NOT AND &STATUS2 = FOUND THEN GOTO NOTASK               004800
    OSPAUSE 10                                                            004900
    SET I = &I + 1                                                        005000
  END                                                                     005100
  OSWTOH &TASK DID NOT COME DOWN IN EXPECTED TIME                         005200
  OSCMD C &TASK                                                           004500
  EXIT CODE (4)                                                           005400
/*                                                           */
NOTASK: +
  OSWTO TASK &TASK IS DOWN
  EXIT CODE(0)
./ ADD NAME=ONLYTSO
PROC 0
 /*                                                                  */
 /*      PDSDOC *TSSO CLIST TO SHUT SYSTEM DOWN TO JUST TSO..........*/
 /*                                                                  */
 /*                                                                 */
   CONTROL LIST NOPROMPT NOFLUSH MSG NOSYMLIST NOCONLIST
 /*                                                                  */
 %SURE
 SET &ENDOK = &LASTCC
 IF &ENDOK NE 0 THEN EXIT CODE(8)
 /*                                                                  */
   OSCMD P JSX1
   OSCMD P JRP
   OSCMD P IOAOSASF
   OSCMD P NETSPY
   OSCMD P NETVIEW
   OSCMD P XPE
   OSCMD P ADSM
   OSCMD F CA7ICOM,STOP
   OSCMD F CAL7,SHUTDOWN,ALL
   OSCMD C INETD4
   %DOWN TASK(TCPIP1)
   OSCMD $PLGN1
   OSCMD C ASCH
   OSCMD C APPC
   OSCMD P JESXCF
   OSCMD C IMWEBSRV
   OSCMD P NETVSSI
   OSCMD P DFSMSHSM
   OSCMD P HSM
   OSCMD C JCA7PROD
   OSCMD C CAL7
   OSCMD P RMF
./ ADD NAME=SURE
PROC 0 DEBUG

 /*                                                                  */
 /*      PDSDOC *TSSO CLIST TO CONFIRM ACTION........................*/
 /*                                                                  */

  IF &DEBUG = DEBUG THEN CONTROL  MSG LIST CONLIST SYMLIST FLUSH
                    ELSE CONTROL  NOMSG NOLIST FLUSH
 /*                                                                  */

         /* FIRST, FIND OUT IF THE OPERATOR IS READY TO PROCEED */
         /* SET TIME LIMIT TO WAIT (IN SECONDS)                 */
         SET REPLYWAIT = 60

         STARTAGN: +
         OSASK REPLY YES OR NO TO CONFIRM REQUEST

         IF &REPLY = NOREPLY THEN GOTO TIMEDOUT
         IF &REPLY = NO THEN GOTO BADEND
         IF &REPLY = YES THEN GOTO ENDPROG

         CONFUSED: +
           OSWTOH ANSWER YES OR NO...
           GOTO STARTAGN

         TIMEDOUT: +
           OSWTO FUNCTION TIMING OUT - REINVOKE WHEN YOU HAVE MORE TIME

         BADEND: +
           OSWTO FUNCTION TERMINATED
           EXIT CODE(8)

         ENDPROG: +
           EXIT CODE(0)
