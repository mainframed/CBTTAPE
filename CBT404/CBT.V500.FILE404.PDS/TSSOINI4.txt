         TITLE ' T S S O    I N I T I A L I Z A T I O N   (PART2)'
***********************************************************************
TSSOINI4 CSECT
***********************************************************************
* THIS MODULE IS THE FOURTH PART OF TSSO INITIALIZATION. IT'S PURPOSE *
* IS TO PROCESS THE TSSO SUBSYSTEM CONSOLES. THE NUMBER OF SUBSYSTEM  *
* CONSOLES IS DEFINED IN THE NUMCONS INITIALIZATION PARAMETER, AND    *
* THE PROCESSING OF THIS MODULE IS TO GET THAT PARAMETER, AND ATTEMPT *
* TO ALLOCATE ALL THE SUBSYSTEM CONSOLES. IT WILL ACCOMPLISH THIS     *
* THROUGH RECURSIVE CALLS TO TSSO GSSC.                               *
***********************************************************************
         STM    R14,R12,12(R13)    SAVE CALLERS REGSITERS
         LR     R12,R15            LOAD ENTRY POINT OF TSSO
         USING  TSSOINI4,R12       GET ADDRESSIBILITY
         B     EYE1
         COPY  EYECATCH
EYE1     DS    0H
         GETMAIN R,LV=72           WE NEED STORAGE FOR THE SAVE AREA
         ST     R13,4(R1)          STORE ADDRESS OF CALLERS SAVE AREA
*                                  SAVE AREA
         ST     R1,8(R13)          REMEMBER WHERE THIS CORE IS
         LR     R13,R1             KEEP A COPY IN R13
*
         GETCVT (R9)
         USING  TSSOCVT,R9
         L      R8,TSSODSEC
         USING  DATD,R8
***********************************************************************
* INITIALIZE TSSOINI4 DSECT VARIABLES
***********************************************************************
         GETMAIN R,LV=4096
         LR    R11,R1
         USING TSSOVARS,R11
*
         XC    INI4FLGS(4),INI4FLGS
         TM    PARMFLAG,X'10'       WAS "DEDICATE" SPECIFIED IN PARMS?
         BNO   INI4NODE             NO, DONT BOTHER DEDICATING SSCONS
         OI    INI4FLGS,X'80'       OH HECK, GO AHEAD.
INI4NODE DS    0H
***********************************************************************
* STEP 1 IS TO INITIALIZE THE SSCON CONTROL BLOCK AND THE SSCON CONTRO*
* BLOCK HEADER.                                                       *
*                                                                     *
*                                                                     *
***********************************************************************
         MODESET KEY=ZERO           TO ACCESS THESE CONTROL BLOCKS
         L    R10,SSCONSAD          GET ADDRESS OF SSCONCB HEADER.
         USING SSCONCBH,R10
         MVC  SSCONREQ(4),ININUMSS  MOVE NUMBER OF CONSOLES REQUESTED.
         LA   R5,SSCONFIR           GET ADDRESS OF FIRST SS CONS CB
         L    R7,SSCONREQ           ATTEMPT TO SATISFY REQUEST.
*                                   R7 HAS NUMBER OF SUBSYSTEM CONSOLES
*                                   REQUESTED VIA PARMLIB.
GETCONS  DS   0H
         USING SSCONCB,R5
         XR   R0,R0                 FLAG TO OBTAIN SS CONSOLE
         LINK EP=TSSOGSSC           GET A SUBSYSTEM CONSOLE
         LTR  R15,R15               CHECK RETURN CODE
         BNZ  INI4ERR               TROUBLE ALLOCATING A SUBSYSTEM
*                                   CONSOLE
*
***********************************************************************
* ASSUMING NO TROUBLE ALLOCATING THIS SUBSYSTEM CONSOLE, WE WILL      *
* FILL IN THE FIELDS OF THE SSCON CB.                                 *
*                                                                     *
***********************************************************************
         L     R2,SSCONAMT         GET # ALLOCATED TO DATE
         LA    R2,1(R2)            BUMP
         ST    R2,SSCONAMT         PUT BACK
*DRCICS* LH    R2,TSSCONID         GET THE CONSOLE ID
         L     R2,TSSCONID         GET THE CONSOLE ID
         ST    R2,SSCONNUM         PUT IN SSCON CB
         L     R2,TSSOSCNA         GET SSCONS UCMAUTH ADDRESS
         ST    R2,SSCONAUT         AND STORE AWAY.
         TM    INI4FLGS,X'80'      WAS A DEDICATED CONSOLE REQUESTED ?
         BNO   GETMCONS            NO, DONT BOTHER TO GET ONE
         TM    INI4FLGS,X'40'      IS THIS NOT THE FIRST TIME THROUGH
         BO    GETMCONS            NOT THE FIRST TIME THROUGH, DONT
*                                  DEDICATE THIS CONSOLE
         OI    SSCONFLG,X'40'      INDICATE DEDICATED CONSOLE
*
         OI    INI4FLGS,X'40'      AND CONTINUE
GETMCONS DS    0H
         LA    R5,SSCONCBL(R5)     BUMP TO NEXT ENTRY.
         BCT   R7,GETCONS          AND TRY FOR EVEN MORE SS CONSOLES
         B     ENDPROG
INI4ERR  DS    0H
***********************************************************************
* WE COME HERE IN CASE TSSOGSSC INFORMS US OF AN ERROR ALLOCATING A   *
* SUBSYSTEM CONSOLE. IN THIS DEPRESSING EVENT, WE WILL TERMINATE      *
* SSCONS INITIALIZATION WITH AN ERROR.                                *
***********************************************************************
         WTO   'TSSG153S TSSO IS UNABLE TO ALLOCATE ONE OR MORE SUBSYSTX
               EM CONSOLES'
         B     ENDPROG
ENDPROG  DS    0H
*
         MODESET KEY=NZERO
         FREEMAIN R,LV=4096,A=(R11)
         LR    R1,R13              GET ADDRESS OF CORE IN R1
         L     R13,4(R13)          RESTORE R13
         FREEMAIN R,LV=72,A=(R1)    FREE UP THE CORE
ENDPROG1 DS    0H
         L     R14,12(R13)         RELOAD RETURN POINT
         LR    R15,R8              RELOAD RETURN CODE
         LM    R0,R12,20(R13)      RELOAD MORE REGISTERS
         BR    R14
*
***********************************************************************
* TSSOINI4 ONLY DATA CONSTANTS.                                       *
*                                                                     *
***********************************************************************
*
***********************************************************************
* TSSOINI2 ONLY DSECT.
*                                                                     *
***********************************************************************
TSSOVARS DSECT
INI4FLGS DS    XL4
* FIRST BYTE:
* X'80'  A DEDICATED SUBSYSTEM CONSOLE IS REQUESTED. THIS BYTE IS
*        COPIED FROM TSSO PARMFLAG (X'10')
*
* X'40'  THIS IS THE FIRST CONSOLE... USED WHEN CHECKING FOR DEDICATED
*        CONSOLE REQUESTS... A DEDICATED CONSOLE WILL ALWAYS BE
*        SATISFIED FIRST.
RETCODE  DS    H           RETURN CODE FROM TSSOINI2
         TSSOCVT
         SSCONCBH
         SSCONCB
         IEFJESCT
         IEFJSCVT
         IEFJSSVT
         COPY  TSSODATD
         CVT   DSECT=YES
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END   TSSOINI4
 PUNCH ' SETCODE  AC(1)'
 PUNCH ' IDENTIFY TSSOINI4(''V 4.3.0 &SYSDATE &SYSTIME'') '
 PUNCH ' ENTRY    TSSOINI4'
 PUNCH ' NAME     TSSOINI4(R)'
         END
