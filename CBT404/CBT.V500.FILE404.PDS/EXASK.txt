         PROC 0
         /****************************************************/
         /* THIS CLIST WILL PROVIDE AN EXAMPLE OF THE OSASK  */
         /* FACILITY OF TSSO.                                */
         /****************************************************/

         /* FIRST, FIND OUT IF THE OPERATOR IS READY TO PROCEED */
         /* SET A TIME LIMIT OF 30 SECONDS...                   */
         SET REPLYWAIT = 30

         STARTAGN: +
         OSASK ARE YOU READY TO PROCEED

         IF &REPLY = NOREPLY THEN GOTO TIMEDOUT
         IF &REPLY = NO THEN GOTO ENDPROG
         IF &REPLY Â¬= YES THEN GOTO CONFUSED

         OSASK GREAT ! WHAT TASK(S) ARE YOU TRYING TO PERFORM ?

         /* UP TO 6 ITEMS MAY BE ENTERED ON THE RESPONSE LINE. THE */
         /* OSASK COMMAND PARSES THE OPERATOR RESPONSE INTO CLIST  */
         /* VARIABLES. WE WILL LOOP THROUGH, SUPPLYING THE OPERATOR*/
         /* WITH THE NEEDED ANSWERS.                               */

         IF &REPLY = NOREPLY THEN GOTO TIMEDOUT
         SET COUNT = &REPLYW

         SET I = 1
         DO WHILE (&I <= &COUNT)

           SET VAR = &&REPLYW&I       /* GET THE I'TH WORD */

           IF &VAR = IPL THEN LIST 'SYS1.HELPOPER(IPL)'
           ELSE IF &VAR = SHUTDOWN THEN LIST 'SYS1.HELPOPER(SHUTDOWN)'
           ELSE IF &VAR = STARTNET THEN LIST 'SYS1.HELPOPER(STARTNET)'
           /* MANY OTHER ENTRIES MAY BE PLACED HERE ...    */
           ELSE OSWTO   THERE IS NOT HELP FOR "&VAR"

           SET I = &I + 1
         END
         GOTO  ENDPROG

         CONFUSED: +
           OSWTOH ANSWER YES OR NO...
           GOTO STARTAGN

         TIMEDOUT: +
           OSWTO HELP FACILITY TIMING OUT - REINVOKE WHEN YOU HAVE MORE
           OSWTO TIME.
           GOTO  ENDPROG

         ENDPROG: +
           OSWTO GOODBYE.
           EXIT
