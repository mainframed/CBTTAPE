         TITLE ' T S S O  SUBSYSTEM CONSOLE SERVICES'
TSSOGSSC CSECT
         EQUATES
***********************************************************************
* THIS MODULE WILL PERFORM THE TSSO SUBSYSTEM CONSOLE SERVICES        *
* REQUIRED FOR THE OSCMD COMMAND. THE TWO SERVICES WILL BE: GET A     *
* SUBSYSTEM CONSOLE, AND RELEASE A SUBSYSTEM CONSOLE.                 *
*
***********************************************************************
* THIS ROUTINE PERFORMS ITS MAGIC BY ISSUING REQUEST CALLS TO MVS     *
* MODULE IEAVG700, WITH A PARAMETER LIST MAPPED BY IEZVG100. THIS     *
* ROUTINE WILL FILL OUT THAT PARAMETER LIST, AND CALL IEAVG700, THUS  *
* OBTAINING A SUBSYSTEM CONSOLE ID.                                   *
***********************************************************************
***********************************************************************
* NOTE: ALL MESSAGES PRODUCED WILL BE IN THE FORM TSSGNNNS WHERE      *
* 151 < NNN < 200                                                     *
***********************************************************************
         STM   R14,R12,12(R13)    SAVE CALLERS REGSITERS
         LR    R12,R15            LOAD ENTRY POINT OF TSSO
         USING TSSOGSSC,R12       GET ADDRESSIBILITY
         B     EYE1
         COPY  EYECATCH
EYE1     DS    0H
         LR    R7,R0              GET TO THE ENTRY CODE.
         GETMAIN R,LV=72           WE NEED STORAGE FOR THE SAVE AREA
         ST    R13,4(R1)          STORE ADDRESS OF CALLERS SAVE AREA
*                                  SAVE AREA
         ST    R1,8(R13)          REMEMBER WHERE THIS CORE IS
         LR    R13,R1             KEEP A COPY IN R13
*
         GETCVT (R10)             OBTAIN TSSO CVT
         USING TSSOCVT,R10
         L     R9,TSSODSEC        GET TSSO'S DSECT
         USING DATD,R9
***********************************************************************
* INITIALIZE TSSOGSSC DSECT VARIABLES
***********************************************************************
         CH    R7,=H'0'            ENTERED TO OBTAIN A CONSOLE ?
         BE    GETSSCON            YUP, GET A SUBSYSTEM CONSOLE
         CH    R7,=H'4'            ENTERED TO DELETE THE CONSOLE ?
         BE    DELSSCON            YUP !
         WTO   'TSSG151S INVALID ENTRY TO CONSOLE SERVICES',DESC=(1)
         B     ENDPROG
GETSSCON DS    0H
***********************************************************************
* HERE, WE HAVE A REQUEST TO OBTAIN A SUBSYSTEM CONSOLE. WE WILL      *
* FORMAT THE IEZVG100 PARM LIST AND CALL IEAVG700.                    *
***********************************************************************
         LA    R2,SCSLEN
         GETMAIN R,LV=256              GET SPACE FOR IEZVG100 PARM LIST
         LR    R11,R1
         USING SCSRPLST,R11
         XC    0(256,R11),0(R11)       BLANK OUT THE STORAGE
*
         MVC   SCSACRO(4),=CL4'SCSR'    MOVE IN ACRONYM
*
         LA    R2,SCSVERSN              GET VERSION NUMBER
         STC   R2,SCSVER                AND STORE AWAY
*
         OI    SCSFUNC1,SCSOBTAN        INDICATE A REQUEST TO GET A
*                                       SUBSYSTEM CONSOLE
*
         OI    SCSFUNC2,SCSPMSTR        MAKE IT A PSEUDO-MASTER CONSOLE
*
         MVC   SCSNAME(8),=CL8'TSSO'    MAKE NICE DISPLAY CONSOLE CMD
*
         OI    SCSFLGS1,SCSTYPE1        TYPE "1" PROTOCAL- WE DONT KNOW
*                                       WHAT TYPE 2 IS...
         MVC   SCSPJESN(4),=CL4'JES2'   INDICATE PRIMARY SUBSYSTEM
*
         MODESET KEY=ZERO,MODE=SUP      GET POWERFULL
         ST    R11,WORKAREA             PUT PARM LIST AWAY
         LA    R1,WORKAREA              LOAD ADDRESS OF WHERE IT IS.
         LINK  EP=IEAVG700              ** DO IT **
         LTR   R15,R15
         BZ    REQOK
         CH    R15,=H'4'           NO CONSOLE AVAIALBLE
         BE    REQNOTOK
         WTO   'TSSG152S BAD RETURN CODE FROM IEAVG700- NO SUBSYSTEM COX
               NSOLE ALLOCATED'
         LA    R3,8
         B     GETSSEXT
*
REQNOTOK DS    0H
         LA    R3,8                   SET RETURN CODE.
         B     GETSSEXT
*
REQOK    DS    0H
*DRCICS* LH    R2,SCSCNID             GET CONSOLE ID         02/08/2007
         L     R2,SCSCONID            GET CONSOLE ID         02/08/2007
*DRCICS* STH   R2,TSSCONID            PUT IN TSSO CVT.       02/08/2007
         ST    R2,TSSCONID            PUT IN TSSO CVT.       02/08/2007
*
***********************************************************************
* THE NEXT STEP AFTER OBTAINING THE SUBSYSTEM CONSOLE IS TO REMEMBER  *
* IT'S UCMAUTH ADDRESS. THIS BYTE WILL BE CHANGED FOR MOST TSSO       *
* COMMANDS ISSUED THROUGH THE OSCMD INTERFACE.                        *
***********************************************************************
         L     R6,16                  GET CVT
         USING CVT,R6
         L     R6,CVTCUCB             GET UCM PREFIX
         USING UCM,R6
         LM    R7,R9,UCMVEA           GET FIRST UCM, LENGTH,LAST UCM
         DROP  R6
UCMSLOOP DS    0H
         USING UCMLIST,R7
*DRCICS* CLC   UCMID(1),TSSCONID+1    COMPARE UCM IDS        02/08/2007
         CLC   UCMID(1),TSSCONID+3    COMPARE UCM IDS        02/08/2007
         BE    UCMFOUND               GOT IT...
         BXLE  R7,R8,UCMSLOOP
*
         WTO   'TSSG154S UNABLE TO FIND UCM FOR TSSO SUBSYSTEM CONSOLE'X
               ,DESC=1
         B     REQNOTOK
*
UCMFOUND DS    0H
         LA    R6,UCMAUTH
         ST    R6,TSSOSCNA            STORE CONSOLE AUTH BYTE ADDRESS
***********************************************************************
* NOTE THAT THIS ADDRESS WILL BE USED TO ZAP THE SUBSYSTEM CONSOLE    *
* AUTHORIZATION BYTE INTO APPROPRIATE VALUES FOR DIFFERENT CIRCUMSTANC*
***********************************************************************
         OI    TSSOFLG1,TSSOSCON      INDICATE WE HAVE A SUBSYSTEM
*                                     CONSOLE
         XR    R3,R3                  SET RETURN CODE 0
         B     GETSSEXT
*
GETSSEXT DS    0H
         MODESET MODE=PROB,KEY=NZERO
         FREEMAIN R,LV=256,A=(R11)
         B     ENDPROG
*
*
*
DELSSCON DS    0H
***********************************************************************
* HERE, WE HAVE A REQUEST TO DELETE THE SUBSYSTEM CONSOLE WE HAVE     *
* PREVIOUSLY OBTAINED. WE WILL FORMAT THE PARM LIST AND CALL IEAVG700 *
***********************************************************************
         TM    TSSOFLG1,TSSOSCON       DID WE EVER GET A CONSOLE ?
         BNO   ENDPROG                 NO, FORGET IT.
*
         MODESET KEY=ZERO
         LA    R2,SCSLEN
         GETMAIN R,LV=256              GET SPACE FOR IEZVG100 PARM LIST
         LR    R11,R1
         USING SCSRPLST,R11
         XC    0(256,R11),0(R11)       BLANK OUT THE STORAGE
*
         MVC   SCSACRO(4),=CL4'SCSR'    MOVE IN ACRONYM
*
         LA    R2,SCSVERSN              GET VERSION NUMBER
         STC   R2,SCSVER                AND STORE AWAY
*
         OI    SCSFUNC1,SCSRELSE        INDICATE A REQUEST TO RELEASE
*                                       A SUBSYSTEM CONSOLE
*
*DRCICS* MVC   SCSCNID(2),TSSCONID    INDICATE THE CONSOLE TO02/08/2007
         MVC   SCSCONID,TSSCONID      INDICATE CNSL TO RELEAS02/08/2007
*
         MVC   SCSNAME(8),=CL8'TSSO'    IT MUST MATCH, OR IEAVG700 WONT
*                                       GIVE UP THE CONSOLE
         MODESET MODE=SUP               GET POWERFULL
*
         ST    R11,WORKAREA
         LA    R1,WORKAREA
*
         LINK  EP=IEAVG700              ** DO IT **
*
         LTR   R15,R15
         BZ    DELOK
         WTO   'TSSG155S ERROR RELEASING SUBSYSTEM CONSOLE -> PERHAPS TX
               SSO SHOULD BE RESTARTED',DESC=(1)
         B     DELSSEXT
*
DELOK    DS    0H
         B     DELSSEXT
*
DELSSEXT DS    0H
         MODESET MODE=PROB,KEY=NZERO
         FREEMAIN R,LV=256,A=(R11)
         B     ENDPROG
*
ENDPROG  DS    0H
*
*
         LR    R1,R13              GET ADDRESS OF CORE IN R1
         L     R13,4(R13)          RESTORE R13
         FREEMAIN R,LV=72,A=(R1)    FREE UP THE CORE
ENDPROG1 DS    0H
         L     R14,12(R13)         RELOAD RETURN POINT
         LR    R15,R3              RELOAD RETURN CODE
         LM    R0,R12,20(R13)      RELOAD MORE REGISTERS
         BR    R14
*
         COPY  TSSODATD
         TSSOCVT
         SSCONCBH
         SSCONCB
         IEZVG100
         CVT   DSECT=YES
         IEFJESCT
         IEFJSCVT
UCM      DSECT
         IEECUCM
*
         END   TSSOGSSC
 PUNCH ' SETCODE  AC(1)'
 PUNCH ' IDENTIFY TSSOGSSC(''TSSO 4.3  &SYSDATE &SYSTIME'') '
 PUNCH ' ENTRY    TSSOGSSC'
 PUNCH ' NAME     TSSOGSSC(R)'
         END
