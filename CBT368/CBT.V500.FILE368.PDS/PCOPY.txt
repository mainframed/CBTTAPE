/*REXX*****************************************************************/
/*    edit macro to copy to an edited pds from proclib or a pds       */
/*    example: pcopy temp copies from proclib member temp to current  */
/*             pds                                                    */
/*    example: pcopy 'uid.temp(x)' copies member x        to current  */
/*             pds                                                    */
/**********************************************************************/
Address ISPEXEC
'ISREDIT MACRO (DATAII) NOPROCESS'
"Isredit (dsname) = dataset"
Right = ")"
Left = "("
Dataii = translate(dataii)
Address tso "allocate da('"dsname"') fi(list) shr reus"
Dummy = Listdsi(list file)
Address tso "free fi(list)"

If sysdsorg \= PO then
    Do
      Zedsmsg = "Target not a pds"
      Zedlmsg = "Xcopy command only works when used",
                "within a dataset with dsorg po"
      "Setmsg msg(isrz001)"
      Exit 12
    End

If length(dataii) <= 8 & dataii \= ""  Then
  Do
   Proc.1 = cpac.proclib
   Proc.2 = sys1.proclib
   Proc.3 = sys4.pp1tec.proclib
   Proc.4 = sys4.pp1prd.proclib
   Proc.5 = sys4.pp1cis.proclib
   Proc.6 = sys4.pp1dev.proclib
   Proc.7 = sys4.tp1cis.proclib
    Do I = 1 to 7
      Pdsn = "'"||proc.i||left||dataii||right||"'"
      If sysdsn(pdsn) = 'OK'
        Then
          Signal Makedsn
    End
      Call baddsn
      Exit 12
  End

  If sysdsn(dataii) \= 'OK' Then
     Do
       Call baddsn
       Exit 12
     End

Makedsn:
  If substr(dataii,1,1) \= "'"
   Then
    dataii = pdsn
  Lparn = Index(dataii,left)
  Dataid = substr(dataii,1,lparn - 1)
  Datat  = dataid
  Dataid = dataid||"'"

Makemem:
  Lparn = Index(dataii,left)
  Rparn = Index(dataii,right)
  Meml = (rparn - 1) - lparn
  Mem = substr(dataii,lparn + 1,meml)

Finish:
 "Lminit dataid(dd01) dataset("dataid") enq(shr)"
 "Lminit dataid(dd02) dataset('"Dsname"') enq(shr) volume("sysvolume")"
 "Lmcopy fromid("dd01") tomem(@@@temp) trunc replace",
  "Todataid("dd02") frommem("mem")"
     If rc = 8 Then
         Do
            Zedsmsg = "Member not found"
            Zedlmsg = ""datat"("mem")  not found "
           "Setmsg msg(isrz001)"
           Exit 12
         End
"Control errors return"
"Isredit process dest"
   If rc > 8 then isredit (first) = linenum 0
 Isredit copy @@@temp after .zdest
   If rc > 0 then
     Isredit copy @@@temp after .zfirst
"Control errors cancel"
Lmproc:
 "Lmfree dataid("dd01")"
 "Lmfree dataid("dd02")"
 "Lminit dataid(pdsid) dataset('"dsname"') enq(shrw) org(pdsorg)"
 "Lmopen dataid("pdsid") option(output)"
 "Lmmdel dataid("pdsid") member(@@@temp)"
 "Lmclose dataid("pdsid")"
 "Lmfree dataid("pdsid")"
Exit 0

Baddsn:
   Zedsmsg = "Entity not found"
   Zedlmsg = "Check "dataii" for valid dataset and member",
             "name (and if they exist)"
   "Setmsg msg(isrz001)"
Return
