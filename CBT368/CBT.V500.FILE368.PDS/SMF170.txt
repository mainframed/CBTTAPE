/*REXX*****************************************************************/
/*                                                                    */
/*    Name     - SMF170                                               */
/*    Author   - James A. Williams                                    */
/*    Date     - 02/16/1998                                           */
/*    Purpose  - read SMF170 to see is editing via FILEAID            */
/*    Invoke   - batch or foreground                                  */
/*                                                                    */
/**********************************************************************/
Arg infile

CHK_IN_DATASET:
X = outtrap('var.')
Infile = "'"||infile||"'"
Dummy  = listdsi(infile)
If dummy \= 0 then
  Do
    Say 'Dataset has problem or does not exits make sure it    '
    Say 'was created with the ifasmfdp utility parsing SM170type'
    Exit 4
  End

READ_SMF_IN_ARRAY:
Status=msg('off')
"Alloc fi(smfin) dsname("infile") shr reuse"
"Execio * diskr smfin (stem smfrec. finis"
"Free fi(smfin)"
"Alloc fi(acffile) dsname('acf10.pn1000.acflids') shr reuse"
"Execio * diskr acffile (stem acfrec. finis"
"Free fi(acffile)"
Status=msg('on')
Allocrc = rc

SMF_DECLARES:
SM170PTR = 0                   /*  record pointer    */
SM170RTY = 0                   /*  record type       */
SM170TME = 0                   /*  time in binary    */
SM170DTE = 0                   /*  date in pack      */
SM170SID = ""                  /*  smfid             */
SM170DSN = ""                  /*  dataset name      */
SM170MEM = ""                  /*  member if PDS     */
SM170UID = ""                  /*  user identifier   */

VAR_DECLARES:
SMF_CTR1 = 0
SMF_CTR2 = 0
SMF_CTR3 = 0
O        = 0
RPTID    = AUDR0001
TOP      = '1'                                /*  Top of form    */
SUP      = '+'                                /*  suppress formf */
SK2      = '0'                                /*  skip 2 lines   */
SK3      = '-'                                /*  skip 3 lines   */
Pagenum  = 0
Linenum  = 0
Firstpass= YES

CALL RPT_HEADING

SMF_PROCESS:
Do I = 1 to smfrec.0
  SM170RTY = substr(smfrec.i,SM170ptr+2,1)
  If SM170RTY \= 'AA'X
      THEN
        ITERATE
  SM170TME = substr(smfrec.i,SM170ptr+3,4)
  Parse var SM170tme temp
  TEMP = C2D(temp)
  HH = TEMP%360000
  L  = LENGTH(HH)
  MM = TEMP%6000-60*HH
  SS = TEMP%100-3600*HH-60*MM
  JOBTIM = HH*10000+MM*100+SS
  IF L = 1
    THEN
      SM170TME = 0||JOBTIM
  ELSE
      SM170TME = JOBTIM
  SM170TME = SUBSTR(SM170TME,1,2)||'.'||SUBSTR(SM170TME,3,2)
  SM170DTE = substr(smfrec.i,SM170ptr+7,4)
  Parse var SM170dte temp
  TEMP = c2X(temp)
  TEMP = SUBSTR(TEMP,3,5)
  TEMP = RDATE(TEMP GRE)
  X    = '/'
  TEMP = SUBSTR(TEMP,1,2)||X||SUBSTR(TEMP,3,2)||X||SUBSTR(TEMP,5,2)
  /* executil ts  */
  SM170DTE = TEMP
  SM170SID = substr(smfrec.i,SM170ptr+11,4)
  SM170UID = substr(smfrec.i,SM170ptr+24,8)
  SM170DSN = substr(smfrec.i,SM170ptr+32,44)
  SM170MEM = substr(smfrec.i,SM170ptr+76,8)
  CALL ACF_NAMES
  SMF_CTR1 = SMF_CTR1 + 1
CALL BLD_OUTREC
Linenum = Linenum + 1
If linenum > 51 then
     DO
       LINENUM = 0
       CALL RPT_HEADING
     END
End

Call Tally_DATA
Call WRT_OUTREC
Exit 0

ACF_NAMES:
Do J = 1 TO ACFREC.0             /* CHECK ACF2 ARRAY FOR MATCH  */
Acfid = substr(acfrec.j,3,7)
IF SM170UID = ACFID THEN         /* IF NOT IN DBASE MOVE ON     */
    Do
      Username = substr(acfrec.j,45,20)
      Leave J
    End
End
Return

RPT_HEADING:
/*executil ts */
Pagenum = Pagenum + 1
O = O + 1
Heading1 = TOP||LEFT("REPORT ID:",12)||RPTID||,
       CENTER(AGL RESOURCES SERVICE COMPANY,92,' ')||,
       LEFT("PAGE    : ",11)||PAGENUM
OUTREC.O = heading1
O = O + 1
Heading2 = SK2||CENTER(FILEAID MVS PRODUCTION,132)
OUTREC.O = heading2
O = O + 1
OUTREC.O = SK3
O = O + 1
OUTREC.O = " DATE     TIME ",
        " DSNAME                                          MEMBER  ",
        " USERID    NAME                                          "
O = O + 1
OUTREC.O = " -------- -----",
        " --------------------------------------------    --------",
        " --------  ---------------------                        "
Return

BLD_OUTREC:
O = O + 1
OUTREC.O = " "SM170DTE" "SM170TME"",
             " "SM170DSN"    "SM170MEM"",
             " "SM170UID"  "USERNAME"  "
Return

WRT_OUTREC:
/* "Alloc fi(smfout) dsname('oprjaw0.smfout') shr reuse"   */
"Execio "o" diskw smfout (stem outrec. finis"
/* "Free fi(smfout)"                                       */
Return

TALLY_DATA:
Pagenum = Pagenum + 1
O = O + 1
OUTREC.O = SK3
O = O + 1
Heading1 = TOP||LEFT("REPORT ID:",12)||RPTID||,
       CENTER(XXX XXXXXXXXX XXXXXXX COMPANY,92,' ')||,
       LEFT("PAGE    : ",11)||PAGENUM
OUTREC.O = heading1
O = O + 1
Heading2 = SK2||CENTER(FILEAID MVS PRODUCTION,132)
OUTREC.O = heading2
O = O + 1
OUTREC.O = SK2||center("-",132,"-")
O = O + 1
OUTREC.O = " TOTAL RECORDS READ:                  "SMFREC.0""
O = O + 1
OUTREC.O = " TOTAL FILEAID DATASETS EDITED INTERVAL:  "SMF_CTR1""
Return
