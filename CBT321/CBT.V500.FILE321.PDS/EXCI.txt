000100 Identification Division.
000200 Program-Id. EXCI is INITIAL
000300****************************************************************
000400*                                                              *
000500*  PROGRAM NAME: EXCI                                          *
000600*                                                              *
000700*  TITLE: Cemt Commands from Batch                             *
000800*                                                              *
000900*  DATE: March 1998                                            *
001000*                                                              *
001100*                                                              *
001200*  INPUT:         The target CICS system with which this       *
001300*                 batch client program communicates musst be   *
001400*                 specified via the EINGABE DD statement.      *
001500*                                                              *
001600*  OUTPUT:        The whole of the Target File will be output  *
001700*                 to the SYSPRINT log, together with status    *
001800*                 information.                                 *
001900*                                                              *
002000****************************************************************
002100 Environment Division.
002200*==============================================================*
002300 Input-Output Section.
002400 File-Control.
002500     Select Printer assign to SYSPRINT.
002600     Select Optional Eingabe assign to SYSIN.
002700 Data Division.
002800*==============================================================*
002900 File Section.
003000*==============================================================*
003100 fd  Printer
003200     Record contains 128 Characters
003210     Block  contains 0 Records
003300     Recording Mode S
003400     Label Records omitted.
003500 01  Output-Record      pic x(128).
003600 fd  Eingabe
003610     Record contains 80 Characters
003620     Block  contains 0 Records
003800     Recording mode f.
003900 01  Input-Record.
004000     05 I-Target-Applid pic x(8).
004100     05 Filler          pic x.
004200     05 I-Command       pic x(8).
004300     05 Filler          pic x.
004400     05 I-Type          pic x(8).
004500     05 Filler          pic x.
004600     05 I-Resource      pic x(8).
004700     05 Filler          pic x.
004800     05 I-Status        pic x(20).
004810     05 Filler          pic x(24).
004900*DCCTT01  SET      FILE     TRAC     CLOSE
005000*DCCTT01  START    TRANSID  D1SA     SBDC C=001,N=FTS....
005100 Working-Storage Section.
005200*==============================================================*
005300*   Declare Call level,DPL, and EXEC level Return Code areas.  *
005400*==============================================================*
005500 COPY DFHXCPLO.
005600*==============================================================*
005700*   Declare Areas to be used for outputting return codes.      *
005800*==============================================================*
005900 01  Output-RetArea.
006000   05                   pic x(20)  value Spaces.
006100   05  O-Resp           pic 9(8).
006200   05                   pic xx     value Spaces.
006300   05  O-Resp2          pic 9(8).
006400   05                   pic xx     value Spaces.
006500   05  OExci-Sub-Reason1 pic 9(8).
006600   05  O-AbCode-Line    redefines OExci-Sub-Reason1.
006700       10  O-AbCode     pic x(4).
006800       10  Opad-AbCode  pic x(4).
006900   05                   pic x(80) value Spaces.
007000 01  Sub                pic s9(8) binary.
007200 01  Exci-Sev-Highest   pic 9(2)  value 0.
007300 01  Out-Rec.
007400   05 Out-Rec-Elem      pic x occurs 128 times.
007500*==============================================================*
007600*   Initialise Target information variables.                   *
007700*==============================================================*
007800 01  Application        pic x(8)       value 'EXCI'.
007810 01  Target-Program     pic x(8)       value 'EXCISE'.
007900 01  Target-Transid     pic x(4)       value 'EXCI'.
007901 01  User-Token         pic S9(9) binary value zero.                    20
007910 01  Pipe-Token         pic S9(9) binary value zero.                    20
007911 01  Comm-Length        pic S9(8) binary.                               24
007920 01  Data-Length        pic S9(8) binary.                               24
007930 01  U-Cics-Rc          pic 99.                                         24
008000*==============================================================*
008100*   Define Commarea struct.                                    *
008200*==============================================================*
008300 01  Commarea.
008400   03  Commarea-Data.
008500     05 Cics-Command      pic x(8).
008600     05 Filler            pic x.
008700     05 Cics-Type         pic x(8).
008800     05 Filler            pic x.
008900     05 Cics-Resource     pic x(8).
009000     05 Filler            pic x.
009100     05 Cics-Status       pic x(20).
009200     05 Filler            pic x.
009300     05 Cics-RC           pic s9(4) binary.
009310     05 Cics-Message      pic x(80).
009400*==============================================================*
009500*   Initialise program specific variables and flags.           *
009600*==============================================================*
009700 01  Program-Flags.
009800   05  Abort-Flag       pic x(3)       value 'NO '.
009900       88 Abort                        value 'YES'.
010000       88 No-Abort                     value 'NO '.
010100   05  Eof-Flag         pic x(3)       value 'NO '.
010200       88 Eof                          value 'YES'.
010300       88 No-Eof                       value 'NO '.
010400 01  Program-Messages.
010500   05  Msg00  pic x(128) value Spaces.
010600   05  Msg01  pic x(128) value '*===================== Batch CICS-
010700-     'Commands ====================================*'.
010800   05  Msg02  pic x(128) value '*
010900-     '                                             *'.
011000   05  Msg03  pic x(128) value '*  Following Command will be proce
011100-     'ed:                                          *'.
011200   05  Msg11  pic x(128) value '*    The Link Request has failed.
011300-     ' Return codes are:                           *'.
011400   05  Msg13  pic x(128) value '*    A message was received from t
011500-     'he target CICS system:                       *'.
011600   05  MSg14  pic x(128) value '*    >>>> Aborting further process
011700-     'ing <<<<                                     *'.
011701   05  MSG23  PIC X(128) VALUE '*    The connection has been unsuc      37
011710-     'cessful.  Return codes are:                  *'.                 37
011711   05  MSG25  PIC X(128) VALUE '*      The DPL request has failed.      37
011712-     '                                             *'.                 37
011713   05  MSG26  PIC X(128) VALUE '*      Link return codes are:           38
011714-     '                                             *'.                 38
011715   05  MSG27  PIC X(128) VALUE '*      DPL return codes are:            38
011720-     '                                             *'.                 38
011800   05  Msg31  pic x(128) value '*=================== End of Batch
011900-     'CICS-Commands ===============================*'.
012000*==============================================================*
012100 linkage section.
012200   01  Null-ptr  usage is Pointer.
012300*
012400   01  Exec-Level-Msg.
012500     05 Exec-Level-Msg-Text pic x occurs 128 times.
012600*
012601   01  Call-Level-Msg.
012602     05 Call-Level-Msg-Len  pic s9(4) binary.
012603     05                     pic s9(4) binary.
012610     05 Call-Level-Msg-Text pic x occurs 128 times.
012700
012800 Procedure Division.
012900     Open output Printer
013000     Open input Eingabe
013100     Write Output-Record from Msg00
013200     Write Output-Record from Msg01
013300     Write Output-Record from Msg02
013310*
013320***  Perform the Initialise User Call
013330*
013340     Call 'DFHXCIS' using Version-1
013350                          Exci-Return-Code
013360                          User-Token
013370                          Init-User
013380                          Application
013390
013420*==============================================================*
013500*    Get data from Eingabe                                     *
013600*==============================================================*
013700     Perform until eof or abort
013800       Read Eingabe
013900         at end
014000           Set Eof to true
014100       End-Read
014200       If eof
014300         Continue
014400       else
014420*        Perform the Allocate Pipe Call
014440         Call 'DFHXCIS' using Version-1
014450                              Exci-Return-Code
014460                              User-Token
014470                              Allocate-Pipe
014480                              Pipe-Token
014490                              I-Target-Applid
014491                              Generic-Pipe
014493*        Perform the Open Pipe Call.
014495         Call 'DFHXCIS' using Version-1
014496                              Exci-Return-Code
014497                              User-Token
014498                              Open-Pipe
014499                              Pipe-Token
014500         if Exci-Response = 0
014501           Continue
014502         else
014503           Write Output-Record    from Msg23
014504           Move  Exci-Response    to   O-Resp
014505           Move  Exci-Reason      to   O-Resp2
014506           Move  Exci-Sub-Reason1 to   OExci-Sub-Reason1
014507           Write Output-record    from Output-Retarea
014508           Write Output-record    from Msg14
014509           Set Abort to true
014510         end-if
014511         If Abort
014512           Continue
014513         else
014520*          Set up the Commarea for transmission.
014600           Move Spaces       to Commarea
014700           Move I-Command    to Cics-Command
014800           Move I-Type       to Cics-Type
014900           Move I-Resource   to Cics-Resource
015000           Move I-Status     to Cics-Status
015300           Write Output-Record from Msg03
015400           Move Spaces       to Out-Rec
015500           Move '* '         to Out-Rec (1:3)
015600           Move 'Cics:'      to Out-Rec (4:5)
015700           Move I-Target-Applid to Out-Rec (9:8)
015800           Move Commarea     to Out-Rec (18:61)
015900           Move '*'          to Out-Rec (80:1)
016000           Write Output-Record from Out-Rec
016100*          Perform the Link Request
016110           Perform Dpl-Program
017900         end-If
017902*        Perform Close Pipe call
017904         Call 'DFHXCIS' using Version-1
017905                              Exci-Return-Code
017906                              User-Token
017907                              Close-Pipe
017908                              Pipe-Token
017910*        Perform Deallocate Pipe call
017912         Call 'DFHXCIS' using Version-1
017913                              Exci-Return-Code
017914                              User-Token
017915                              Deallocate-Pipe
017916                              Pipe-Token
017920       end-If
018000     end-Perform
018020
018100     Write Output-record from Msg02
018200     Write Output-record from Msg31
018300     Close Printer Eingabe
018400     Move  Exci-Sev-Highest to Return-Code
018500     If Abort
018600       Move 12  to Return-Code
018700     end-if
018800     stop run
018900     .
023900******************************************************************
024000*
024100*  Dpl-Program
024300*
024400******************************************************************
024500 Dpl-Program Section.
024501
024502     set address of null-ptr to nulls
024503     Move Length of Commarea to Comm-Length                             24
024504                                Data-Length                             24
024505*
024506*    set up the call, and perform the dpl request.
024507*
024508     Call 'DFHXCIS' using Version-1
024509                          Exci-Return-Code
024510                          User-Token
024511                          Dpl-Request
024512                          Pipe-Token
024513                          Target-Program
024514                          Commarea
024515                          Comm-Length                                   24
024516                          Data-Length                                   24
024520                          Target-TransId
024521                          Null-Ptr
024522                          Null-Ptr
024523                          Exci-Dpl-RetArea
024524                          SyncOnReturn
024525*
024526*  Check how successful the Call was.  If it has worked, then
024527*  we have a Record on the Commarea to be output to the
024528*  Sysprint log.
024529*
024530     If Exci-Response = 4 and Exci-Reason = 7
024531*      Server-Cics terminated
024532       Move Spaces         to Out-Rec
024533       Move '* '           to Out-Rec (1:3)
024534       Move 'RC:00'        to Out-Rec (4:5)
024536       Move 'Cics shutdown complete'  to Out-Rec (10:70)
024537       Move '*'            to Out-Rec (80:1)
024538       Write Output-Record from Out-Rec
024539       Write Output-Record from Msg02
024540       Go to Dpl-Program-End
024541     end-if
024542
024544     If Exci-Response = 0
024545       If Exci-Dpl-Resp = 0 then
024546         Move Spaces         to Out-Rec
024547         Move '* '           to Out-Rec (1:3)
024548         Move 'RC:'          to Out-Rec (4:3)
024549         Move Cics-RC        to U-Cics-Rc
024550         Move U-Cics-RC      to Out-Rec (7:2)
024551         Move Cics-Message   to Out-Rec (10:70)
024552         Move '*'            to Out-Rec (80:1)
024553         Write Output-Record from Out-Rec
024554         Write Output-Record from Msg02
024555         If Cics-Rc > Exci-Sev-Highest
024556            Move Cics-Rc    to Exci-Sev-Highest
024557         end-if
024558       else
024559         Write Output-Record from Msg25
024560         Write Output-Record from Msg27
024561         Move  Exci-Dpl-Resp to O-Resp
024562         Move  Exci-Dpl-Resp2 to O-Resp2
024563         Move  Spaces to Opad-Abcode
024564         Move  Exci-Dpl-Abcode to O-Abcode
024565         Write Output-Record from Output-Retarea
024566         Write Output-Record from Msg14
024567         Set Abort to true
024568       end-if
024569     else
024570       Write Output-Record from Msg11
024571       Move  Exci-Response to   O-Resp
024572       Move  Exci-Reason   to   O-Resp2
024573       move  Exci-Sub-Reason1 to OExci-Sub-Reason1                      84
024574       Write Output-record    from Output-Retarea
024575       Write Output-record    from Msg14
024576       Write Output-Record from Output-Retarea
024577       If Exci-Msg-Ptr equal Nulls
024578         Continue
024579       else
024580         Write Output-Record from Msg13
024581         Write Output-Record from Msg02
024582         Set address of Call-Level-Msg to Exci-Msg-Ptr
024583         Move Spaces to Out-Rec
024584         Subtract 4 from Call-Level-Msg-Len
024585         Perform test before
024586            varying Sub from 1 by 1
024587            until Sub > Call-Level-Msg-Len
024588           Move Call-Level-Msg-Text (sub) to Out-Rec-Elem (sub)
024589         End-Perform
024590         Write Output-Record from Out-Rec
024591         Write Output-Record from Msg02
024592       end-if
024593       Write Output-Record from Msg27
024594       Move  Exci-Dpl-Resp to O-Resp
024595       Move  Exci-Dpl-Resp2 to O-Resp2
024596       Move  Spaces to Opad-Abcode
024597       Move  Exci-Dpl-Abcode to O-Abcode
024598       Write Output-Record from Output-Retarea
024599       Write Output-Record from Msg14
024620       Set Abort to true
024700     end-if
026800     .
026900 Dpl-Program-End.
027000     exit
027100     .
