*ASM XOPTS(SP)
***********************************************************************
*                                                                     *
* Name:         EXCISE                                                *
*                                                                     *
* Author:       Roland Schiradin                                      *
*                                                                     *
* Date:         March 1998                                            *
*                                                                     *
* Descrition:   Cics ExCi-server for Cemt-Commands                    *
*                                                                     *
*                                                                     *
***********************************************************************
*=====================================================================*
*                                                                     *
*      Working Storage.                                               *
*                                                                     *
*=====================================================================*
*
DFHEISTG DSECT  ,                    |Program Variables.             |
EYECATCHER   DC CL10'*EXCISE*'       |                               |
OPT_FLAG     DS F                    |  Flag indicate Copy-option    |
CICS_RESP    DS F                    |  CICS-Response Code           |
TRY_COUNT    DS H                    |  Counter                      |
WS#ABSTI DS   PL8
WS#TXTMS     DS   0CL84
FILL1        DS   CL12
WS#APPL      DS   CL8
FILL2        DS   CL9
WS#SYSID     DS   CL4
FILL3        DS   CL23
WS#DATE      DS   CL10
FILL4        DS   CL4
WS#TIME      DS   CL8
FILL5        DS   CL6
WS#TXTLE     DS   F
*=====================================================================*
*                                                                     *
*      Commarea DSECT.                                                *
*                                                                     *
*=====================================================================*
*
COMMAREA DSECT
COM_COMMAND  DS CL8                Command
             DS CL1                Filler
COM_TYPE     DS CL8                Type
             DS CL1                Filler
COM_RESOURCE DS CL8                Resource
             DS CL1                Filler
COM_STATUS   DS CL20               Status
             DS CL1                Filler
COM_RC       DS H                  Feedback RC
COM_MSG      DS CL80               Message Feedback
*
EXCISE   DFHEIENT CODEREG=(R3),DATAREG=(R6,R7)
*
*=====================================================================*
*   Check that a COMMAREA has been passed. If we don't have a commarea*
*   we cannot even return a return code back to the client, so issue  *
*   an abend instead.                                                 *
*=====================================================================*
         CLC   EIBCALEN,=H'0'
         BNE   PARSE
         EXEC CICS ABEND ABCODE('NCOM')
*
*=====================================================================*
*   Map the COMMAREA DSECT onto the Commarea passed.                  *
*=====================================================================*
PARSE    DS    0H
         EXEC CICS ADDRESS COMMAREA(R4)
         USING COMMAREA,R4              tell assembly
         MVC   COM_RC,=H'0'             set rc
         EXEC CICS ASKTIME ABSTIME(WS#ABSTI) NOHANDLE
*
         EXEC CICS FORMATTIME ABSTIME(WS#ABSTI)                        *
               DATESEP('/')     TIMESEP(':')                           *
               DDMMYYYY(WS#DATE) TIME(WS#TIME) NOHANDLE
*
         EXEC CICS ASSIGN                                              *
               APPLID(WS#APPL)                                         *
               SYSID(WS#SYSID) NOHANDLE
         MVC   FILL1,=C'***** Cics: '
         MVC   FILL2,=C' (Sysid: '
         MVC   FILL3,=C') Batch-Command issued '
         MVC   FILL4,=C' at '
         MVC   FILL5,=C' *****'
         MVC   WS#TXTLE,=F'84'
         EXEC CICS WRITE OPERATOR                                      *
               TEXT(WS#TXTMS) TEXTLENGTH(WS#TXTLE)                     *
               EVENTUAL NOHANDLE
*
***      Check Command
*
         CLC   COM_COMMAND,=CL8'SET'      valid
         BE    CEMT_SET                      yes, do set
         CLC   COM_COMMAND,=CL8'PERFORM'  valid
         BE    CEMT_PERFORM                  yes, do perform
         MVC   COM_MSG,=CL80'Can not process unknown Command'
         MVC   COM_RC,=H'12'
         B     RETURN
CEMT_SET  DS    0H
         CLC   COM_TYPE,=CL8'PROG'        Programm?
         BE    CEMT_SET_PROG                 yes, do set
         CLC   COM_TYPE,=CL8'FILE'        File?
         BE    CEMT_SET_FILE                 yes, do set
         CLC   COM_TYPE,=CL8'MONITOR'     Monitoring?
         BE    CEMT_SET_MONITOR              yes, do set
         MVC   COM_MSG,=CL80'Can not process unknown Type'
         MVC   COM_RC,=H'12'
         B     RETURN
CEMT_SET_PROG  DS 0H
         MVC   COM_MSG,=CL80'Newcopy successfull'
         MVC   OPT_FLAG,DFHVALUE(NEWCOPY)
         EXEC CICS SET PROGRAM(COM_RESOURCE),COPY(OPT_FLAG),           *
               RESP(CICS_RESP)
         CLC   CICS_RESP,DFHRESP(NORMAL)    ok?
         BE    RETURN                        yes, back
         CLC   CICS_RESP,DFHRESP(PGMIDERR)  Program not found ?
         BNE   CEMT_SET_PROG_20               check next
         MVC   COM_MSG,=CL80'Program not found, Newcopy on first req.'
         B     RETURN                       back
CEMT_SET_PROG_20  DS 0H
         MVC   COM_MSG,=CL80'Program in use, Newcopy for new requests'
         MVC   OPT_FLAG,DFHVALUE(PHASEIN)
         EXEC CICS SET PROGRAM(COM_RESOURCE),COPY(OPT_FLAG),           *
               RESP(CICS_RESP)
         CLC   CICS_RESP,DFHRESP(NORMAL)    ok?
         BE    RETURN                        yes, back
         MVC   COM_MSG,=CL80'Can not perform the Newcopy-Request'
         B     RETURN                        yes, back
CEMT_SET_FILE  DS 0H
         CLC   COM_STATUS(8),=CL8'CLOSE'
         BE    CEMT_SET_FILE_CLOSE
         CLC   COM_STATUS(8),=CL8'OPEN'
         BE    CEMT_SET_FILE_OPEN
         MVC   COM_MSG,=CL80'Can not process unknown Status'
         MVC   COM_RC,=H'12'
         B     RETURN
CEMT_SET_FILE_CLOSE  DS 0H
         MVC   COM_MSG,=CL80'Close successfull'
         MVC   OPT_FLAG,DFHVALUE(CLOSED)
         EXEC CICS SET FILE(COM_RESOURCE),OPENSTATUS(OPT_FLAG),        *
               RESP(CICS_RESP)
         CLC   CICS_RESP,DFHRESP(NORMAL)    ok?
         BE    RETURN                        yes, back
         MVC   COM_MSG,=CL80'Can not close this File'
         MVC   COM_RC,=H'12'
         B     RETURN                       back
CEMT_SET_FILE_OPEN  DS 0H
         MVC   COM_MSG,=CL80'Open successfull'
         MVC   OPT_FLAG,DFHVALUE(OPEN)
         EXEC CICS SET FILE(COM_RESOURCE),OPENSTATUS(OPT_FLAG),        *
               RESP(CICS_RESP)
         CLC   CICS_RESP,DFHRESP(NORMAL)    ok?
         BE    RETURN                        yes, back
         MVC   COM_MSG,=CL80'Can not open this File'
         MVC   COM_RC,=H'12'
         B     RETURN                       back
CEMT_SET_MONITOR DS 0H
         CLC   COM_RESOURCE(8),=CL8'ON'
         BE    CEMT_SET_MONITOR_ON
         CLC   COM_RESOURCE(8),=CL8'OFF'
         BE    CEMT_SET_MONITOR_OFF
         MVC   COM_MSG,=CL80'Can not process unknown Status'
         MVC   COM_RC,=H'12'
         B     RETURN
CEMT_SET_MONITOR_ON DS 0H
         MVC   COM_MSG,=CL80'Monitoring on'
         MVC   OPT_FLAG,DFHVALUE(ON)
         EXEC CICS SET MONITOR STATUS(OPT_FLAG)                        *
               RESP(CICS_RESP)
         CLC   CICS_RESP,DFHRESP(NORMAL)    ok?
         BE    RETURN                        yes, back
         MVC   COM_MSG,=CL80'Can not change Monitoring-Status'
         MVC   COM_RC,=H'12'
         B     RETURN                       back
CEMT_SET_MONITOR_OFF DS 0H
         MVC   COM_MSG,=CL80'Monitoring off'
         MVC   OPT_FLAG,DFHVALUE(OFF)            RS0606
         EXEC CICS SET MONITOR STATUS(OPT_FLAG)                        *
               RESP(CICS_RESP)
         CLC   CICS_RESP,DFHRESP(NORMAL)    ok?
         BE    RETURN                        yes, back
         MVC   COM_MSG,=CL80'Can not change Monitoring-Status'
         MVC   COM_RC,=H'12'
         B     RETURN                       back
CEMT_PERFORM DS  0H
         CLC   COM_TYPE,=CL8'SHUT'        Shutdown ?
         BE    CEMT_PERFORM_SHUT             yes, do set
         MVC   COM_MSG,=CL80'Can not process unknown Type'
         MVC   COM_RC,=H'12'
         B     RETURN
CEMT_PERFORM_SHUT DS  0H
         CLC   COM_RESOURCE,=CL8'I'         Shutdown immediate
         BE    CEMT_PERFORM_SHUT_I           yes, do set
         MVC   COM_MSG,=CL80'Normal Shutdown'
         EXEC CICS PERFORM SHUTDOWN,                                   *
               RESP(CICS_RESP)
         CLC   CICS_RESP,DFHRESP(NORMAL)    ok?
         BE    CEMT_PERFORM_SHUT_10          yes, check status
         MVC   COM_MSG,=CL80'Can not shutdown this CICS'
         MVC   COM_RC,=H'12'
         B     RETURN                       back
CEMT_PERFORM_SHUT_10 DS  0H
         CLC   TRY_COUNT,=H'2'              Do it 3 times
         BH    CEMT_PERFORM_SHUT_I           then shutdown asap
         LH    R5,TRY_COUNT
         AH    R5,=H'1'
         STH   R5,TRY_COUNT
         EXEC CICS DELAY FOR SECONDS(30) NOHANDLE
         EXEC CICS INQUIRE SYSTEM CICSSTATUS(OPT_FLAG) NOHANDLE
         CLC   OPT_FLAG,DFHVALUE(ACTIVE)
         BE    CEMT_PERFORM_SHUT_10          yes, loop
         CLC   OPT_FLAG,DFHVALUE(FIRSTQUIESCE)
         BE    CEMT_PERFORM_SHUT_10          yes, loop
         CLC   OPT_FLAG,DFHVALUE(FINALQUIESCE)
         BE    RETURN                         ok leave
CEMT_PERFORM_SHUT_I DS  0H
         MVC   COM_MSG,=CL80'Shutdown immediate'
         EXEC CICS PERFORM SHUTDOWN IMMEDIATE                          *
               RESP(CICS_RESP)
         CLC   CICS_RESP,DFHRESP(NORMAL)    ok?
         BE    RETURN                        yes, back
         MVC   COM_MSG,=CL80'Can not shutdown this CICS'
         MVC   COM_RC,=H'12'
         B     RETURN                       back
*
*=====================================================================*
*   All calls return to here.  Control is passed back to the Client   *
*=====================================================================*
*
RETURN   DS    0H
         MVC   FILL1,=C'***** Cics: '
         MVC   FILL2,=C' (Sysid: '
         MVC   FILL3,=C') Batch-Command done   '
         MVC   FILL4,=C' at '
         MVC   FILL5,=C' *****'
         MVC   WS#TXTLE,=F'84'
         EXEC CICS WRITE OPERATOR                                      *
               TEXT(WS#TXTMS) TEXTLENGTH(WS#TXTLE)                     *
               EVENTUAL NOHANDLE
         EXEC CICS RETURN
BLANKS   DC  CL256' '
*
         DFHREGS
         END EXCISE
