         MACRO
&NAME    KEYVAL &STRING,&TABLE,&PRINT=OFF,&TYPE=PAREN
         GBLC  &KVRCSEC            CSECT NAME
         LCLC  &CSNAME             OUTER CSECT NAME
         LCLB  &KVRCSON            LOCAL SWITCH FOR CSECT GENERATION
         LCLB  &EQ                  INDICATE = SUPPORT             8/30
.**********************************************************************
.***                                                                ***
.***  MACRO  NAME: KEYWORD VALIDATION ROUTINE                       ***
.***                                                                ***
.***                                                                ***
.***  GENERAL FUNCTION: TO VALIDATE A CHARACTER STRING CONTAINING   ***
.***                    KEYWORDS AND VALUES AND PERFORM REQUESTED   ***
.***                    OPERATIONS AS SPECIFIED IN THE KEYWORD      ***
.***                    VALIDATION TABLE DEFINED WITH ONE OR MORE   ***
.***                    KEYT MACROS.  THIS MACRO EXPANDS TO AN      ***
.***                    INDEPENDENT CSECT UPON INITIAL INVOCATION   ***
.***                    AND IS ACCESSED VIA V-TYPE ADDRESSING       ***
.***                    FOR SUBSEQUENT INVOCATIONS.  THERE ARE 2    ***
.***                    BASIC PARTS TO THIS MACRO.  THE FIRST PART  ***
.***                    SETS UP THE APPROPRIATE PARMS BASED UPON    ***
.***                    THE USER PARAMETERS SUPPLIED.  THE SECOND   ***
.***                    PART DOES THE STRING MANIPULATION, PERFORMS ***
.***                    THE REQUESTED FUNCTIONS AND SETS RETURN     ***
.***                    CODES.                                      ***
.***                                                                ***
.***                                                                ***
.***  ENTRY REGS:   R1                                              ***
.***  (PART 1)      R2                                              ***
.***                R3                                              ***
.***                R4                                              ***
.***                R5                                              ***
.***                R6                                              ***
.***                R7                                              ***
.***                R8                                              ***
.***                R9                                              ***
.***                R10                                             ***
.***                R11                                             ***
.***                R12                                             ***
.***                R13  SAVE AREA                                  ***
.***                R14                                             ***
.***                R15                                             ***
.***                                                                ***
.***                                                                ***
.***  ENTRY REGS:   R0   AL4  ADDR OF CHAR STRING                   ***
.***  (PART 2)      R1   AL4  ADDRESS OF 1ST KEYT ENTRY             ***
.***                R2                                              ***
.***                R3                                              ***
.***                R4                                              ***
.***                R5                                              ***
.***                R6                                              ***
.***                R7                                              ***
.***                R8                                              ***
.***                R9                                              ***
.***                R10                                             ***
.***                R11                                             ***
.***                R12                                             ***
.***                R13  SAVE AREA                                  ***
.***                R14  RETURN ADDRESS                             ***
.***                R15  ENTRY ADDRESS                              ***
.***                                                                ***
.***  REGISTER USAGE:  R0   WORK                                    ***
.***                   R1   WORK                                    ***
.***                   R2   WORK                                    ***
.***                   R3   CHAR STRING LENGTH                      ***
.***                   R4   CHAR STRING ADDRESS                     ***
.***                   R5   WORK                                    ***
.***                   R6   WORK                                    ***
.***                   R7   WORK                                    ***
.***                   R8   WORK                                    ***
.***                   R9   SAVED RETURN CODE                       ***
.***                   R10  WORK                                    ***
.***                   R11  PROGRAM BASE                            ***
.***                   R12  WORKAREA BASE                           ***
.***                   R13  OLD SAVEAREA                            ***
.***                   R14  KEYWORD TABLE BASE                      ***
.***                   R15  RETURN CODE                             ***
.***                                                                ***
.***                                                                ***
.***                                                                ***
.***                                                                ***
.***  AUTHOR: E STEWART                DATE: 05/29/80               ***
.***                                                                ***
.***                                                                ***
.***  ATTRIBUTES: RENT,REUS,REFR                                    ***
.***                                                                ***
.***                                                                ***
.***  CALLED BY: UTILITY FUNCTION                                   ***
.***                                                                ***
.***                                                                ***
.***  MACROS USED: GETMAIN,FREEMAIN                                 ***
.***                                                                ***
.***                                                                ***
.***  ROUTINES CALLED: NONE                                         ***
.***                                                                ***
.***                                                                ***
.***  NORMAL EXIT: BR R14                                           ***
.***                                                                ***
.***  EXIT REGS:     R0   AL4  ADDR OF KEYWORD IN CHAR STRING WHERE ***
.***                           VALIDATION ERROR OCCURED             ***
.***                 R1   AL4  ADDR OF VALUE IN CHAR STRING WHERE   ***
.***                           VALIDATION ERROR OCCURED             ***
.***                 R15       00  KEYWORD(S)VALUE(S) FOUND AND     ***
.***                               OP(S) PERFORMED                  ***
.***                           04  KEYWORD(S) FOUND, SOME VALUE NOT ***
.***                               FOUND IN KEYT                    ***
.***                           08  A KEYWORD WAS NOT FOUND IN KEYT  ***
.***                           12  'VALCNT' DID NOT PASS VALIDATION ***
.***                           16  MORE INPUT EXPECTED, I.E. ONLY   ***
.***                               BLANKS, COMMENTS, OR ),BLANK WAS ***
.***                               FOUND. OTHERWISE RC=00 WOULD HAVE***
.***                               BEEN RETURNED.                   ***
.***                           20  'TYPE' DID NOT PASS VALIDATION.  ***
.***                           24  'LEN' DID NOT PASS VALIDATION.   ***
.***                                                                ***
.***                                                                ***
.***  ABEND CODES: NONE (I HOPE)                                    ***
.***                                                                ***
.***                                                                ***
.***  MESSAGES ISSUED: NONE                                         ***
.***                                                                ***
.***                                                                ***
.***                                                                ***
.***                                                                ***
.***  DSECTS/MACROS USED: NONE FROM AN EXTERNAL SOURCE              ***
.***                                                                ***
.***                                                                ***
.***  SPECIAL NOTES:                                                ***
.***                                                                ***
.***                                                                ***
.***  MODIFIED:                                                     ***
.***           08/30/88   GLA  SUPPORT KEYWORD FORMAT OF            ***
.***                            KEYWORD=VALUE                       ***
.***           08/31/88   GLA  ORIGINAL CODE BUG ON VALIDATING      ***
.***                            NULL VALUES                         ***
.***                                                                ***
.**********************************************************************
         AIF   ('&KVRCSEC' NE '').KVRBALR CSECT ALREADY ASSIGNED
&KVRCSEC SETC  'KVR'.'&SYSNDX'   SET CSECT NAME
&KVRCSON SETB  1                   SAY CSECT ASSIGNED THIS PASS
.KVRBALR ANOP
         AIF   (N'&SYSLIST(1) EQ  1 AND N'&SYSLIST(2) EQ 1).KVRM010
         MNOTE 8,'OPERAND SUBLISTS NOT PERMITTED'
         MEXIT
.KVRM010 ANOP
         AIF   ('&NAME' EQ '').KVRM014
&NAME    DS    0H                  SET LABEL NAME
.KVRM014 ANOP
         AIF   ('&SYSLIST(1)' EQ '(0)' OR '&SYSLIST(1)' EQ '(R0)').KVRMX
               040           STRING AND LENGTH ALREADY OK
         AIF   ('&SYSLIST(1)' EQ '(0)' OR '&SYSLIST(2)' EQ '(R0)').KVRMX
               020                CHAR STRING ALREADY IN R0
         AIF   ('&STRING'(1,1) EQ '(').KVRM016 REG NOTATION
         LA    0,&STRING           GET ADDR OF CHAR STRING
         AGO   .KVRM020
.KVRM016 ANOP
         LR    0,&STRING           GET ADDR OF CHAR STRING
.KVRM020 ANOP
         AGO   .KVRM040
.KVRM040 ANOP
         AIF   ('&SYSLIST(2)' EQ '(1)' OR '&SYSLIST(2)' EQ '(R1)').KVRMX
               060
         AIF   ('&TABLE'(1,1) EQ '(').KVRM050 REG NOTATION
         LA    1,&TABLE            KEYWORD TABLE ADDRESS
         AGO   .KVRM060
.KVRM050 ANOP
         LR    1,&TABLE            KEYWORD TABLE ADDRESS
.KVRM060 ANOP
&EQ      SETB  0                                                   8/30
         AIF   ('&TYPE' EQ 'PAREN').NOE                            8/30
&EQ      SETB  1                                                   8/30
.NOE     ANOP                                                      8/30
         L     15,=V(&KVRCSEC)     GET CSECT NAME
         BALR  14,15               GO TO VALIDATION ROUTINE
         DC    B'&EQ.0000000',X'00'                                8/30
         AIF   (NOT(&KVRCSON)).NOGO TERMINATE IF ALREADY EXPANDED
         PUSH  USING,PRINT
         AIF   ('&PRINT' EQ 'ON').KVRM070 NO LIST
         PRINT OFF                 TURN PRINT OFF
         AGO   .KVRM072
.KVRM070 ANOP
         PRINT ON,GEN              TURN ON PRINT
.KVRM072 ANOP
&CSNAME  SETC  '&SYSECT'
&KVRCSEC CSECT
         USING *,11                SET UP BASE
         STM   14,12,12(13)        SAVE REGS
         LR    11,15               GET BASE ADDRESS
         USING KVDSVR,14           TABLE ADDRESS BASE
         USING KVWAVR,12           WORKAREA BASE
         GETMAIN R,LV=KVWALNTH     GET WORKAREA
         LR    12,1                PUT INTO BASE REG
         L     4,20(13)            GET STRING ADDRESS INTO REG 4
         SR    3,3                 CLEAR WORK REG
         ICM   3,3,0(4)            GET STRING LENGTH
         LA    4,2(,4)             BUMP OVER STRING LENGTH
         L     14,12(13)            GET OPTIONS ADDRESS            8/30
         MVC   KVWAOPTS,0(14)       MOVE IN OPTIONS                8/30
         LA    14,2(,14)            RESET RETURN ADDRESS           8/30
         ST    14,12(13)            RESAVE ADDRESS                 8/30
         MVI   KVWAFL1,0           CLEAR FLAG BYTE
         MVI   KVWAKWVA,0          CLEAR COUNT BYTE
         MVC   KVWASAV1(4*13),20(13) SAVE REGS 0-12
         MVC   KVWASAV1+(4*14)(4*2),12(13) SAVE REGS 14-15
         ST    13,KVWASAV1+(4*13)  SAVE REG 13 FROM CALLER
KVR004   DS    0H
         L     14,24(13)           TABLE ADDRESS
         XC    0(KVWACLR,12),0(12)   CLEAR WORKAREA
         LTR   3,3                 Q. ANY DATA TO SCAN
         BP    KVR005              A. YES, CONTINUE AS NORMAL
         LA    9,16                SET BAD RC
         B     KVR900              AND RETURN
KVR005   DS    0H
         SR    2,2                 CLEAR FOR LATER
         ST    4,KVWALOC           SAVE STRING LOC
         ST    4,KVWAKEY           SAVE STRING LOC
         LR    15,3                GET LENGTH FOR TRT               GLA
         CH    15,KVH256           SEE IF TOO BIG FOR TRT           GLA
         BL    *+8                 NO SO USE AS IS                  GLA
         LH    15,KVH256           SET TO MAXIMUM                   GLA
         BCTR  15,0                GET MACHINE LENGTH               GLA
         EX    15,KVTR1VR          SEARCH FOR NON-BLANK             GLA
         BNZ   KVR006              OUT IF FOUND                     GLA
         LA    15,1(,15)           GET REAL LENGTH BACK             GLA
         AR    4,15                POINT TO NEXT STRING LOC         GLA
         SR    3,15                DECREMENT LENGTH                 GLA
         BP    KVR005              IF MORE LEFT THEN REPROCESS      GLA
         LA    9,16                SET BLANKS RETURN CODE
         B     KVR900              RETURN TO CALLER
KVR006   DS    0H
         LR    5,1                 SAVE REG 1
         SR    1,4                 GET NEW
         SR    3,1                 DATA LENGTH
         LR    4,5                 GET NEW LOCATION
         CLM   2,1,KVASLASH        Q. COULD THIS BE COMMENT
         BNE   KVR020              A. NO, CONTINUE
         CLI   1(4),C'*'           Q .WAS THIS A COMMENT
         BE    KVR008              A. YES, CONTINUE
         LA    9,8                 SAY INVALID KEYWORD
         B     KVR900              AND RETURN
KVR008   DS    0H                  VALIDATE COMMENT
         LA    4,2(4)              BUMP OVER /*
         BCTR  3,0                 REDUCE COUNT
         BCTR  3,0                 REDUCE COUNT
KVR010   DS    0H
         CLI   0(4),C'*'           Q. IS IT REALLY A COMMENT
         BE    KVR014              A. YES, CONTINUE
KVR012   DS    0H
         LA    4,1(4)              BUMP ADDRESS
         BCT   3,KVR010            LOOP THROUGH
         LA    9,8                 SET RETURN CODE
         B     KVR900              AND RETURN
KVR014   DS 0H
         CLI   1(4),C'/'           Q. IS 2ND CHAR A SLASH
         BNE   KVR012              A. NO, CONTINUE SCAN FOR END
         LA    4,2(4)              BUMP OVER */
         BCTR  3,0                 REDUCE COUNT
         BCTR  3,0                 REDUCE COUNT
         B     KVR005              AND CONTINUE SCAN
KVR020   DS    0H                  VALIDATE CHARACTERS FOUND
         ST    4,KVWALOC           SAVE STRING ADDR
         ST    4,KVWAKEY           SAVE STRING ADDR
         SR    5,5                 CLEAR FOR KEYWORD COMPARE
         SR    6,6                 CLEAR FOR KEYWORD COMPARE
         SR    7,7                 CLEAR FOR KEYWORD COMPARE
KVR022   DS    0H
         IC    5,KVLL0001          GET KEYWORD LEN
         EX    5,KVCVR             Q. KEYWORD FOUND
         BE    KVR080              A. YES,CONTINUE
KVR023   DS    0H
         IC    6,KVVN0001          GET COUNT OF VALUE ENTRIES
         LA    14,0(14,5)          BUMP OVER VARIABLE KEYWORD
         LTR   6,6                 Q. ANY MORE VALUES TO DO
         BZ    KVR028              A. NO, CONTINUE AS IS
KVR024   DS    0H
         IC    7,KVVL0001          LENGTH OF VALUE KEYWORD
         LA    14,KVVG0001(7,14)   BUMP OVER VALUE ENTRY
         BCT   6,KVR024            CONTINUE THRU ALL VALUES
KVR028   DS    0H
         LA    14,KVKL0001+1(14)   BUMP OVER KEYWORD ENTRY
         CLC   KVAFFFF,KVDSVR      Q. LAST ENTRY
         BNE   KVR022              A. NO, CONTINUE SCAN
         LA    9,8                 SET RETURN CODE
         B     KVR900              AND RETURN
KVR080   DS    0H                  KEYWORD FOUND
         LA    6,1(5,4)            GO TO NEXT BYTE BEYOND KEYWORD
         TM    KVWAOPT1,KVWAOEQ     Q. TYPE=EQUAL                  8/30
         BO    KVWEQ1               A. YES                         8/30
         CLI   0(6),C'('           Q. IS THIS DELIMITER
         BE    KVR082              A. YES, CONTINUE
         B     KVWNEQ1                                             8/30
KVWEQ1   DS    0H                                                  8/30
         CLI   0(6),C'='            Q. IS THIS DELEIMITER          8/30
         BE    KVR082               A. YES, CONTINUE               8/30
KVWNEQ1  DS    0H                                                  8/30
         CLI   0(6),C' '           Q. IS THIS DELIMITER
         BE    KVR082              A. YES, CONTINUE
         CLI   0(6),C','           Q. IS THIS DELIMITER
         BE    KVR082              A. YES, CONTINUE
         LR    9,3                 CURRENT LENGTH
         SR    9,5                 MINUS KEYWORD EX LENGTH
         BCTR  9,0                 MINUS ONE
         LTR   9,9                 Q. ANY DATA LEFT IN STRING
         BZ    KVR082              A. NO, JUST CONTINUE NORMALLY
         SR    6,6                 CLEAR FOR LATER
         B     KVR023              ELSE LOOK FOR ANOTHER KEYWORD
KVR082   DS    0H
         SR    6,6                 CLEAR WORK REG
         ICM   6,3,KVRA0001        GET EXIT ROUTINE ADDR
         BZ    KVR084              BRANCH IF NOT SPECIFIED
         SRDL  6,12                GET REG INTO R6
         SLL   6,2                 X 4
         LA    1,KVWASAV1          ADDR OF SAVE AREA
         L     6,0(6,1)            GET CONTENTS OF RTN BASE REG
         SRL   7,20                SHIFT TO LOWER END
         LA    7,0(7,6)            GET ROUTINE ADDRESS
         ST    7,KVWARTN           SAVE RTN ADDRESS
KVR084   DS    0H
         SR    6,6                 CLEAR WORK REG
         ICM   6,3,KVRP0001        GET EXIT ROUTINE PARM
         BZ    KVR088              BRANCH IF NOT SPECIFIED
         SRDL  6,12                GET REG INTO R6
         SLL   6,2                 X 4
         LA    1,KVWASAV1          ADDR OF SAVE AREA
         L     6,0(6,1)            GET CONTENTS OF PARM BASE REG
         SRL   7,20                SHIFT TO LOWER END
         LA    7,0(7,6)            GET PARM ADDRESS
         ST    7,KVWAPRM           SAVE PARM ADDRESS
KVR088   DS    0H
         LA    4,1(5,4)            GO TO NEXT BYTE BEYOND KEYWORD
         SR    3,5                 REDUCE STRING COUNT
         BCTR  3,0                 REDUCE STRING COUNT
         TM    KVWAOPT1,KVWAOEQ     Q. TYPE=EQUAL                  8/30
         BO    KVWEQ2               A. YES                         8/30
         CLI   0(4),C'('           Q. VALUE(S) SPECIFIED
         BE    KVR110              A. YES, CONTINUE
         B     KVWNEQ2                                             8/30
KVWEQ2   DS    0H                                                  8/30
         LTR   3,3                  Q. ANY INPUT LEFT              8/30
         BNP   KVR089               A. NO, SO NO VALUE             8/30
         CLI   0(4),C'='            Q. VALUE SPECIFIED             8/30
         BE    KVR110               A. YES, CONTINUE               8/30
KVR089   DS    0H                                                  8/30
KVWNEQ2  DS    0H                                                  8/30
         CLI   KVVN0001,0          Q. ANY VALUES REQUIRED
         BNE   KVR090              A. NO, JUST RETURN
         SR    9,9                 SET ZERO RETURN CODE
         B     KVR900              AND RETURN
KVR090   DS    0H
         MVI   KVWAKWVA,0          CLEAR COUNT
         B     KVR172              GO PROCESS THIS KEYWORD
KVR110   DS    0H                  MOVE ALL VALUES TO LOCAL STORAGE
         SR    1,1                 CLEAR FOR LATER
         MVI   KVWAKWVA,0          CLEAR COUNT
         BCTR  3,0                 REDUCE COUNT
         LA    4,1(4)              BUMP TO NEXT BYTE
         LR    5,4                  POINT AT START OF VALUE IF ANY 8/31
         ST    5,KVWALOC            SAVE POINTER                   8/31
         TM    KVWAOPT1,KVWAOEQ     Q. TYPE=EQUAL                  8/30
         BO    KVWEQ3               A. YES                         8/30
         CLI   0(4),C')'           Q. NULL KEYWORDS
         BNE   KVR120              A. NO, CONTINUE NORMALLY
         B     KVWNEQ3                                             8/30
KVWEQ3   DS    0H                                                  8/30
         LTR   3,3                  Q. ANY INPUT YES               8/30
         BNP   KVR140               A. YES, PROCESS NULL ENTRY     8/30
         CLI   0(4),C','            Q. NULL KEYWORDS               8/30
         BE    KVR140               A. YES, PROCESS NULL ENTRY     8/30
         CLI   0(4),C' '            Q. NULL KEYWORDS               8/30
         BNE   KVR120               A. NO, CONTINUE NORMALLY       8/30
KVWNEQ3  DS    0H                                                  8/30
         B     KVR140              GO PROCESS NULL ENTRY
KVR120   DS    0H
         SR    1,1                 CLEAR FOR LATER
         CLI   0(4),C''''          Q. QUOTED STRING FOR THIS VALUE
         BNE   KVR124              A. NO, DONT SET FLAG
         MVI   KVWAQUO,1           SAY IN QUOTE MODE
         LA    4,1(,4)             BUMP OVER QUOTE
         BCTR  3,0                 REDUCE STRING COUNT
KVR124   DS    0H
         LA    2,KVWATMP           ADDR OF TEMP WORK AREA
         LR    5,4                 SAVE OLD VALUES LOCATION
         ST    5,KVWALOC           SAVE STRING ADDR
KVR130   DS    0H
         TM    KVWAOPT1,KVWAOEQ     Q. TYPE=EQUAL                  8/30
         BO    KVWEQ4               A. YES                         8/30
         CLI   0(4),C','           Q. COMMA DELIMITER
         BE    KVR140              A. YES, SAVE THIS KEYWORD
         CLI   0(4),C')'           Q. TERMINATING DELIMITER
         BE    KVR140              A. YES, PROCESS END KEYWORD VALUES
         B     KVWNEQ4                                             8/30
KVWEQ4   DS    0H                                                  8/30
         LTR   3,3                  Q. ANY INPUT LEFT              8/30
         BNP   KVR140               A. YES, PROCESS END VALUES     8/30
         CLI   0(4),C' '            Q. TERMINATING DELIMITER       8/30
         BE    KVR140               A. YES, PROCESS END VALUES     8/30
         CLI   0(4),C','            Q. TERMINATING DELIMITER       8/30
         BE    KVR140               A. YES, PROCESS END VALUES     8/30
KVWNEQ4  DS    0H                                                  8/30
         CLI   0(4),C''''          Q. END OF QUOTED STRING
         BNE   KVR134              A. NO, CONTINUE
         LA    4,1(,4)             BUMP ADDRESS
         BCTR  3,0                 REDUCE COUNT
         LA    1,1(,1)             ADD 1 FOR LATER STRING MOVE
         CLI   0(4),C''''          Q. REALLY END OF STRING
         BE    KVR134              A. NO, NOT YET JUST DOUBLE QUOTES
         MVI   KVWAQUO,0           RESET QUOTE MODE FLAG
         B     KVR130              AND CONTINUE
KVR134   DS    0H
         MVC   0(1,2),0(4)         SAVE THIS BYTE
         LA    2,1(,2)             BUMP WORK BYTE
         LA    15,KVWATMP+L'KVWATMP GET MAXIMUM ADDRESS             GLA
         CR    2,15                SEE IF PAST                      GLA
         BH    KVR136              IF SO THEN EXIT WITH ERROR       GLA
         LA    4,1(4)              GO TO NEXT BYTE
         BCT   3,KVR130            CONTINUE SCAN
         TM    KVWAOPT1,KVWAOEQ     Q. TYPE=EQUAL                  8/30
         BZ    KVWNEQ4A                                            8/30
         CLI   KVWAQUO,0           Q. ARE WE IN QUOTE MODE         8/30
         BE    KVR140              A. NO, SO OK ENDING             8/30
KVWNEQ4A DS    0H                                                  8/30
KVR136   LA    9,4                 SET RETURN CODE                  GLA
         B     KVR900              AND RETURN
KVR140   DS    0H                  PROCESS FOUND VALUE
         CLI   KVWAQUO,0           Q. ARE WE IN QUOTE MODE
         BNE   KVR134              A. YES, IGNORE OTHER DELIMITERS
         LR    6,4                 SAVE NEW ADDR
         SR    6,5                 GET VALUE LENGTH
         SR    6,1                 SUBTRACT ANY QUOTED STRING BYTES
         SR    5,5                 CLEAR WORK REG
         ICM   5,1,KVWAKWVA        GET WORK AREA COUNT
         BNZ   KVR146              BRANCH IF ALREADY SOME IN THERE
         LA    8,KVWAKWVA+1        GET NEXT LOC ADDRESS
         B     KVR148              AND CONTINUE
KVR146   DS    0H                  LOOK FOR VALUES END LOCATION
         LA    8,KVWAKWVA+1        GET START ADDRESS
         SR    7,7                 CLEAR WORK REG
         ICM   7,1,KVWAKWVA+1      GET LENGTH OF LAST ONE
KVR147   DS    0H
         LA    8,1(7,8)            GO TO NEXT SLOT
         ICM   7,1,0(8)            GET NEXT LENGTH
         BCT   5,KVR147            LOOP THRU
KVR148   DS    0H                  MOVE NEW VALUE IN NOW
         LTR   6,6                 Q. NULL VALUE SPECIFIED
         BNZ   KVR150              A. NO, CONTINUE MVC
         STC   6,0(8)              SAVE NULL VALUE LENGTH
         B     KVR160              AND UPDATE VALUES COUNT
KVR150   DS    0H                  MOVE NEW VALUES INTO WORKAREA
         L     5,KVWALOC           RESTORE STRING ADDR
         STC   6,0(8)              SAVE STRING LENGTH
         BCTR  6,0                 REDUCE COUNT
         EX    6,KVM1VR            PERFORM THE MOVE
         TM    KVVT0001,X'FF'      Q. ANY VALIDATION REQUIRED
         BZ    KVR160              A. NO, JUST CONTINUE
         SR    2,2                 CLEAR WORK REG
         ICM   2,1,KVVT0001        GET VALIDATION CODE
         SLL   2,2                 X 4
         B     *+0(2)              GO TO VALIDATE IT
         B     KVR154              ALPHA
         B     KVR156              NUMERIC
         B     KVR158              ALPH/NUMERIC
KVR154   DS    0H                  ALPHA VALIDATION
         EX    6,KVTR2VR           A. ALPHA FIELD
         BZ    KVR160              A. YES, CONTINUE
KVR155   DS    0H                  ERROR RETURN
         LA    9,20                SET RC
         B     KVR900              AND RETURN
KVR156   DS    0H                  NUMERIC
         EX    6,KVTR3VR           Q. NUMERIC FIELD
         BZ    KVR160              A. YES, CONTINUE
         B     KVR155              ELSE SET BAD RC
KVR158   DS    0H                  ALPHA/NUMERIC
         EX    6,KVTR4VR           Q. ALPHA/NUMERIC
         BZ    KVR160              A. YES, CONTINUE
         B     KVR155              ELSE ERROR RETURN
KVR160   DS    0H
         CLI   KVVLL001,0          Q. WAS VALUE LEN VALIDATION REQUSTED
         BE    KVR169              A. NO, BYPASS LENGTH VALIDATION
         LA    6,1(,6)             RESTORE FULL LENGTH
         CLM   6,1,KVVLL001        Q. WAS LOW VALUE PRECEEDED
         BL    KVR168              A. YES, ERROR CONDITION
         CLM   6,1,KVVLH001        Q. WAS HIGH VALUE EXCEEDED
         BH    KVR168              A. YES, ERROR CONDITION
         B     KVR169              VALUE LENGTH PASSED VALIDATION
KVR168   DS    0H
         LA    9,24                SET VALUE ERROR
         B     KVR900              AND RETURN
KVR169   DS    0H
         IC    7,KVWAKWVA          GET CURRENT COUNT
         LA    7,1(,7)             ADD ONE
         STC   7,KVWAKWVA          AND SAVE THE COUNT
         TM    KVWAOPT1,KVWAOEQ     Q. TYPE=EQUAL                  8/30
         BO    KVWEQ5               A. YES                         8/30
         CLI   0(4),C')'           Q. WAS IT ENDING DELIMETER
         BE    KVR170              A. YES, TERMINATE SCAN
         B     KVWNEQ5                                             8/30
KVWEQ5   DS    0H                                                  8/30
         LTR   3,3                  Q. ANY INPUT LEFT              8/30
         BNP   KVR170               A. NO,  TERMINATE SCAN         8/30
         CLI   0(4),C' '            Q. TERMINATING DELIMITER       8/30
         BE    KVR170               A. YES, TERMINATE SCAN         8/30
         CLI   0(4),C','            Q. TERMINATING DELIMITER       8/30
         BE    KVR170               A. YES, TERMINATE SCAN         8/30
KVWNEQ5  DS    0H                                                  8/30
         LA    4,1(,4)             BUMP OVER COMMA
         BCTR  3,0                 REDUCE STRING LENGTH
         B     KVR120              CONTINUE FOR MORE KEYWORDS
KVR170   DS    0H                  PROCESS ENDING DELIMITER
         LA    4,1(,4)             BUMP OVER )
         BCTR  3,0                 REDUCE COUNT
KVR172   DS    0H                  PROCESS ENDING DELIMITER
         SR    5,5                 CLEAR WORK REG
         SR    6,6                 CLEAR WORK REG
         ICM   5,1,KVHV0001        GET VALUE(S) COUNT HIGH NUMBER
         BNZ   KVR180              GO HERE IF NOT ZERO COUNT
         CLM   5,1,KVWAKWVA        Q. WAS COUNT FOUND ALSO ZERO
         BE    KVR180              A. YES, CONTINUE WITHOUT ERROR YET
KVR174   DS    0H
         LA    9,12                SAY COUNT EXCEEDED
         B     KVR900              AND RETURN
KVR180   DS    0H
         CLM   5,1,KVWAKWVA        Q. HIGH COUNT EXCEEDED
         BL    KVR174              A. YES, ERROR AND GET OUT
         ICM   5,1,KVLV0001        GET LOW VALUE(S) COUNT
         BZ    KVR182              GET OUT IF ZERO COUNT
         CLM   5,1,KVWAKWVA        Q. COUNT LESS THAN LOW VALUE
         BH    KVR174              A. YES, GET OUT WITH ERROR
KVR182   DS    0H
         ICM   5,3,KVAA0001        Q. IS THERE AN AREA PROVIDED
         BZ    KVR200              A. NO, JUST CONTINUE
         SR    6,6                 CLEAR WORK REG
         SR    7,7                 CLEAR WORK REG
         ICM   6,3,KVAA0001        GET WORKAREA ADDRESS
         SRDL  6,12                GET REG NO. INTO R2
         SLL   6,2                 X 4
         LA    1,KVWASAV1          ADDR OF SAVE AREA
         L     6,0(6,1)            GET CONTENTS OF AREA BASE REG
         SRL   7,20                SHIFT TO LOWER END
         LA    6,0(7,6)            GET WORKAREA ADDRESS
         LA    5,KVWAKWVA          GET MY AREA ADDRESS
         SR    2,2                 CLEAR WORK REG
         IC    2,KVWAKWVA          GET COUNT
         MVC   0(1,6),KVWAKWVA     MOVE COUNT BYTE
         LA    6,1(,6)             BUMP OVER COUNT BYTE
         LA    5,1(,5)             BUMP OVER COUNT BYTE
KVR184   DS    0H
         MVC   0(1,6),0(5)         MOVE FIRST LENGTH BYTE
         LTR   2,2                 WAS ANYTHING IN THE AREA
         BNZ   KVR186              YES, CONTINUE WITH MOVE
         MVI   KVWAKWVA,1          SAY SOMETHING WAS THERE
         B     KVR200              AND CONTINUE
KVR186   DS    0H
         SR    7,7                 CLEAR FOR NEXT INSTR
         ICM   7,1,0(5)            GET BYTE COUNT
         BNZ   KVR188              BRANCH FOR NON ZERO MOVE
         LA    6,1(,6)             BUMP OVER NULL BYTE
         LA    5,1(,5)             BUMP OVER NULL BYTE
         B     KVR194              AND CONTINUE
KVR188   DS    0H
         BCTR  7,0                 REDUCE BY ONE
         EX    7,KVM2VR            MOVE DATA OVER
KVR190   DS    0H
         LA    6,2(7,6)            BUMP OVER OUTPUT
         LA    5,2(7,5)            BUMP OVER INPUT
KVR194   DS    0H
         BCT   2,KVR184            COUNTINUE MOVE
KVR200   DS    0H                  SEE IF VALUES TO BE COMPARED
         SPACE
*        AT THIS POINT THE VALUES HAVE BEEN MOVED TO MY WORKAREA
*        AND TO THE USER WORK AREA IF AREA HAS BEEN SPECIFIED
*        IN THE KEYT MACRO
         SPACE
         SR    2,2                 CLEAR WORK REG
         ICM   2,1,KVVN0001        Q. WERE ANY VALUES TO BE VERIFIED
*                                     OR WAS THERE WORK TO BE DONE
         BNZ   KVR220              A. YES, CONTINUE AS USUAL
         SR    9,9                 CLEAR RC
         B     KVR900              AND RETURN TO CALLER
KVR220   DS    0H
         ST    2,KVVNCNT           SAVE COUNT
         ST    14,KVWAR14          SAVE KEYT ADDR
         LA    5,KVWAKWVA+1        ADDR OF FOUND VALUES
         ST    5,KVWASV            SAVE VALUES ADDR
         MVC   KVWASVC,KVWAKWVA    SAVE VALUES COUNT
         SR    2,2                 CLEAR REG
         ICM   2,1,KVWASVC         Q. ANY VALUES FOUND
         BNZ   KVR230              A. YES, CONTINUE WITH COUNT
         MVI   KVWASVC,1           ELSE SAY AT LEAST ONE FOUND
KVR230   DS    0H
         L     2,KVVNCNT           GET COUNT
         L     14,KVWAR14          GET TABLE ADDR
         L     5,KVWASV            GET ADDR OF CURRENT VALUE TO FIND
         SR    1,1                 CLEAR WORK REG
         ICM   1,1,KVWASVC         GET COUNT
         BZ    KVR500              STOP SCAN IF NONE LEFT
         BCTR  1,0                 REDUCE COUNT
         STC   1,KVWASVC           SAVE NEW COUNT
         ICM   1,1,0(5)            GET LENGTH OF CURRENT VALUE
         LA    1,1(1,5)            BUMP TO WHERE NEW VALUE WILL BE
         ST    1,KVWASV            AND SAVE ADDRESS
         SR    7,7                 CLEAR FOR LATER
         IC    7,KVLL0001          GET KEYWORD LENGTH
         LA    14,0(14,7)          BUMP OVER VARIABLE LENGTH KEYWORD
KVR240   DS    0H                  SCAN TABLE FOR THIS VALUE
         ST    2,KVWAR2            SAVE CURRENT COUNT
         ICM   7,1,KVVL0001        LENGTH OF VALUE
         BZ    KVR300              IF NO KEYWORD VALUE HAS BEEN PUT
*                                  IN KEYT DONT LOOK FOR A MATCH, JUST
*                                  DO THE WORK IF ANY
         BCTR  7,0                 REDUCE FOR EX INSTR
         EX    7,KVC2VR            Q. ARE VALUES EQUAL
         BE    KVR300              A. YES, GO SEE IF ANY WORK TO DO
KVR250   DS    0H
         L     2,KVWAR2            RELOAD LOOP COUNTER
         SR    1,1                 CLEAR WORK REG
         IC    1,KVVL0001          GET LENGTH OF VALUE
         LA    14,KVVG0001(1,14)   GO TO NEXT ENTRY
         BCT   2,KVR240            AND CONTINUE SCAN
         CLI   KVWAVF,X'FF'        Q. WERE VALUE(S) MATCHED IN KEYT
         BE    KVR260              A. YES, CONTINUE WITH NO ERRORS
         LA    9,4                 VALUE NOT FOUND RC
         B     KVR900              AND RETURN
KVR260   DS    0H
         MVI   KVWAVF,0            RESET VALUES FOUND FLAG
         B     KVR230              AND CONTINUE WITH OTHER VALUES
KVR300   DS    0H                  VALUE FOUND SEE IF ANY WORK TO DO
         MVI   KVWAVF,X'FF'        SAY VALUES FOUND
         TM    KVOP0001,X'FF'      Q. ANY OP CODES SPECIFIED
         BNZ   KVR320              A. YES, SEE IF SOME WORK TO DO
         B     KVR250              ELSE CONTINUE VALUES SCAN
KVR320   DS    0H                  DECODE OP CODES AND DO THEM
         LA    6,KVVV0001          GET ADDR OF VALUES ENTRY
         LA    1,KVWAFLN           ADDR OF SAVE TABLE
         ICM   7,15,KVWACN1        GET CURRENT COUNT OF VALUES ALREADY
*                                  PROCESSED
         BZ    KVR326              ADD THIS ONE IF NONE YET
KVR322   DS    0H
         C     6,0(1)              Q. ALREADY PROCESSED THIS ENTRY
         BE    KVR250              A. YES BYPASS FURTHER PROCESSING
         LA    1,4(,1)             BUMP TABLE ADDRESS
         BCT   7,KVR322            CONTINUE SCAN
KVR326   DS    0H
         ST    6,0(1)              SAVE ADDRESS OF THIS ENTRY FOR LATER
*                                  COMPARE OF OTHER VALUES
         L     7,KVWACN1           GET OLD COUNT
         LA    7,1(,7)             ADD 1 TO IT
         ST    7,KVWACN1           AND SAVE IT
         SR    6,6                 CLEAR WORK REG
         SR    7,7                 CLEAR WORK REG
         ICM   6,3,KVTO0001        GET TO ADDRESS
         SRDL  6,12                GET REG NO. INTO R2
         SLL   6,2                 X 4
         LA    1,KVWASAV1          ADDR OF SAVE AREA
         L     6,0(6,1)            GET CONTENTS OF TO BASE REG
         SRL   7,20                SHIFT TO LOWER END
         LA    2,0(7,6)            GET TO ADDREESS
         SR    6,6                 CLEAR WORK REG
         SR    7,7                 CLEAR WORK REG
         ICM   6,3,KVFR0001        GET FROM ADDRESS
         SRDL  6,12                GET REG NO. INTO R6
         SLL   6,2                 X 4
         LTR   6,6                 Q. WAS THIS SELF DEFINING TERM
         BZ    KVR330              A. YES, DONT LOAD NONEXISTENT BASE
         LA    1,KVWASAV1          ADDR OF SAVE AREA
         L     6,0(6,1)            GET CONTENTS OF FROM BASE REG
KVR330   DS    0H
         SRL   7,20                SHIFT TO LOWER END
         LA    8,0(7,6)            GET FROM ADDRESS
         SR    1,1                 CLEAR WORK REG
         ICM   1,3,KVOL0001        GET LENGTH OF FIELD
         BCTR  1,0                 GET EX LENGTH
         SR    7,7                 CLEAR WORK REG
         IC    7,KVOP0001          GET OP CODE
         SLL   7,2                 MULTIPLY BY 4
         B     *+0(7)              BRANCH TO OP CODE ROUTINE
         B     KVR360              N OP CODE SPECIFIED
         B     KVR380              O OP CODE SPECIFIED
         B     KVR400              M OP CODE SPECIFIED
         B     KVR420              X OP CODE SPECIFIED
KVR360   DS    0H                  N OP CODE
         LTR   6,6                 Q. WAS FROM FIELD SELF DEFINING
         BNZ   KVR366              A. NO, CONTINUE STOR-STOR OPERATION
         EX    8,KVOPNI            ISSUE THE AND OP CODE STOR-IMED
         B     KVR250              AND CONTINUE WITH OTHER VALUES
KVR366   DS    0H                  N OP CODE
         EX    1,KVOPN             ISSUE THE AND OP CODE STOR-STOR
         B     KVR250              AND CONTINUE WITH OTHER VALUES
KVR380   DS    0H                  O OP CODE
         LTR   6,6                 Q. WAS FROM FIELD SELF DEFINING
         BNZ   KVR386              A. NO, CONTINUE STOR-STOR OPERATION
         EX    8,KVOPOI            ISSUE THE OR OP CODE STOR-IMED
         B     KVR250              AND CONTINUE WITH OTHER VALUES
KVR386   DS    0H                  N OP CODE
         EX    1,KVOPO             ISSUE THE OR OP CODE STOR-STOR
         B     KVR250              AND CONTINUE WITH OTHER VALUES
KVR400   DS    0H                  M OP CODE
         LTR   6,6                 Q. WAS FROM FIELD SELF DEFINING
         BNZ   KVR406              A. NO, CONTINUE STOR-STOR OPERATION
         EX    8,KVOPMI            ISSUE THE MVC OP CODE STOR-IMED
         B     KVR250              AND CONTINUE WITH OTHER VALUES
KVR406   DS    0H                  N OP CODE
         EX    1,KVOPM             ISSUE THE MVC OP CODE STOR-STOR
         B     KVR250              AND CONTINUE WITH OTHER VALUES
KVR420   DS    0H                  N OP CODE
         LTR   6,6                 Q. WAS FROM FIELD SELF DEFINING
         BNZ   KVR426              A. NO, CONTINUE STOR-STOR OPERATION
         EX    8,KVOPXI            ISSUE THE XI OP CODE STOR-IMED
         B     KVR250              AND CONTINUE WITH OTHER VALUES
KVR426   DS    0H                  N OP CODE
         EX    1,KVOPX             ISSUE THE XC OP CODE STOR-STOR
         B     KVR250              AND CONTINUE WITH OTHER VALUES
KVR500   DS    0H                  ALL VALUES SCANNED
         SR    9,9                 CLEAR RC
         B     KVR900              AND RETURN
KVR900   DS    0H                  RETURN POINT
         LTR   9,9                 Q . RC=00
         BNZ   KVR930              A. NO, CONTINUE RETURN TO CALLER
         ICM   1,15,KVWARTN        Q. RTN TO CALL
         BZ    KVR910              A. NO, CONTINUE NO CALL
         ST    14,KVWAR14          SAVE REG 14
         LR    15,1                GET RTN ADDR
         LA    0,KVWAKWVA          GET STRING ADDR
         ST    13,KVWASAV+4        SAVE CALLERS SAVE AREA ADDR
         LA    13,KVWASAV          MY SAVE AREA
         L     1,KVWAPRM           GET PARM DATA ADDR
         BALR  14,15               CALL USERS ROUTINE
         L     14,KVWAR14          RESTORE REG 14
         L     13,KVWASAV+4        RESTORE REG 13
KVR910   DS    0H
         MVI   KVWAFL1,1           SAY AT LEAST 1 GOOD FIND
         B     KVR004              GO DO MORE NOW
KVR930   DS    0H
         L     4,KVWALOC           SAVE STRING LOCATION
         L     5,KVWAKEY           SAVE KEYWORD LOCATION
         CLI   KVWAFL1,0           Q. ANY GOOD FINDS
         BE    KVR950              A. NO, LEAVE RC AS IS
         C     9,KVASIX            Q. WAS RC=16
         BNE   KVR950              A. NO, LEAVE RC AS IS
         SR    9,9                 SET ZERO RC
KVR950   DS    0H
         LR    1,12                GET WORKAREA ADDRESS
         LA    0,KVWALNTH          GET WORKAREA LENGTH
         FREEMAIN R,LV=(0),A=(1)   FREE WORKAREA
         LR    15,9                SETUP RETURN CODE
         SLL   15,8                CLEAR HIGH BYTE FOR       ECS04/86
         SRL   15,8                31 BIT MODE XA            ECS04/86
         LM    6,12,11*4(13)       RESTORE REGS
         LR    0,5                 PUT IN KEYWORD LOCATION
         LR    1,4                 PUT IN STRING LOCATION
         LM    2,5,7*4(13)         RESTORE OTHER REGS
         L     14,3*4(13)          RESTORE RETURN REG
         BR    14                  RETURN TO CALLER
         SPACE 4
KVOPN    NC    0(0,2),0(8)
KVOPNI   NI    0(2),0
KVOPO    OC    0(0,2),0(8)
KVOPOI   OI    0(2),0
KVOPM    MVC   0(0,2),0(8)
KVOPMI   MVI   0(2),0
KVOPX    XC    0(0,2),0(8)
KVOPXI   XI    0(2),0
KVCVR    CLC   KVKV0001(0),0(4)
KVTR1VR  TRT   0(0,4),KTVRT1-X'40'
KVTR2VR  TRT   1(0,8),KTVRT3-X'40' ALPHA TEST
KVTR3VR  TRT   1(0,8),KTVRT2-X'40' NUMERIC TEST
KVTR4VR  TRT   1(0,8),KTVRT4-X'40' ALPHA/NUMERIC TEST
KVC2VR   CLC   KVVV0001(0),1(5)
KVM1VR   MVC   1(0,8),KVWATMP
KVM2VR   MVC   1(0,6),1(5)
KTVRT1   DC    10X'00',C'¢.<(+|&&',9X'00',C'!$*);¬-/',10X'00',C'%_>?'
         DC    9X'00',C''':#@ ="',X'00',C'ABCDEFGHI',7X'00'
         DC    C'JKLMNOPQR',8X'00',C'STUVWXYZ',23X'00',C'ABCDEFGHI'
         DC    7X'FF'
         DC    C'ABCDEFGHI',7X'00',C'JKLMNOPQR',8X'00',C'STUVWXYZ'
         DC    6X'00',C'0123456789'
KTVRT2   DC    176X'FF',10X'00'
KTVRT3   DC    129X'FF',41X'00',16X'FF'
KTVRT4   DC    11X'FF',X'00',15X'FF',2X'00',30X'FF',2X'00'
         DC    68X'FF',63X'00'
KVAKVVG  DC    Y(KVVG0001)
KVAFFFF  DC    X'FFFF'
KVASIX   DC    F'16'
KVH256   DC    H'256'
KVAX00   DC    X'00'
KVASLASH DC    C'/'
KVDSVR   DSECT
KVLL0001 DS    AL1                 LENGTH OF KEY-1
KVRA0001 DS    SL2                 RTN ADDRESS
KVRP0001 DS    SL2                 RTN PARM
KVAA0001 DS    SL2                 AREA ADDRESS
KVVLL001 DS    AL1                 VALUE LENGTH LOW
KVVLH001 DS    AL1                 VALUE LENGTH HIGH
KVLV0001 DS    AL1                 VALUE COUNT LOW
KVHV0001 DS    AL1                 VALUE COUNT HIGH
KVVT0001 DS    XL1                 VALUE TYPE
KVTA0001 EQU   X'01'               ALPHA TYPE
KVTN0001 EQU   X'02'               NUMERIC TYPE
KVTB0001 EQU   X'03'               ALPHA OR NUMERIC TYPE
KVVN0001 DS    AL1                 COUNT OF VALUE ENTRIES
KVKL0001 EQU   *-KVLL0001          LENGTH OF TABLE ENTRY MINUS KEYWORD
KVKV0001 DS    C                   KEYWORD
KVVL0001 DS    AL1                 VALUE LENGTH
KVOP0001 DS    XL1                 OP CODE
KVVA0001 EQU   X'01'               N  OP CODE
KVVO0001 EQU   X'02'               O  OP CODE
KVVM0001 EQU   X'03'               M  OP CODE
KVVX0001 EQU   X'04'               X  OP CODE
KVTO0001 DS    SL2                 TO ADDRESS
KVFR0001 DS    SL2                 FROM ADDRESS
KVOL0001 DS    AL2                 LENGTH OF TO FIELD
KVVG0001 EQU   *-KVVL0001          ENTRY LENGTH MINUS VALUE CONSTANT
KVVV0001 DS    C                   VALUE CONSTANT
KVLA0001 EQU   *,2                 LAST ENTRY =X'FFFF'
KVWAVR   DSECT
KVWAVF   DS    X                   VALUES FOUND FLAG
KVWAQUO  DS    X                   QUOTED STRING MODE FLAG
KVWASV   DS    F                   ADDR OF VALUES FOUND
KVWASAV  DS    18F                 SAVE AREA TO CALL RTN
KVWARTN  DS    F                   ADDRESS OF RTN TO CALL
KVWAPRM  DS    F                   ADDRESS OF RTN PARM
KVWALOC  DS    F                   LOCATION OF INPUT STRING
KVWAR14  DS    F                   SAVE AREA FOR REG 14
KVWAR2   DS    F                   SAVE AREA FOR REG 2
KVVNCNT  DS    F                   VALUE COUNT SAVEAREA
KVWAKEY  DS    F                   ADDRESS OF CURRENT KEYWORD IN STRING
KVWACN1  DS    F                   COUNT OF VALUE ADDRS IN KVWAFLN
KVWASVC  DS    X                   COUNT OF VALUES FOUND
KVWACLR  EQU   *-KVWAVR            LENGTH OF WORKAREA TO CLEAR
KVWAFL1  DS    X                   FLAG TO SHOW SOME GOOD WORDS FOUND
KVWAOPTS DS    XL2                 OPTIONS FROM CALL               8/30
KVWAOPT1 EQU   KVWAOPTS,1          FIRST BYTE                      8/30
KVWAOEQ  EQU   X'80'               IF SET THEN TYPE=EQUAL          8/30
KVWASAV1 DS    16F                 SAVE AREA FOR CALLERS REGS
KVWAKWVA DS    512X                WORKAREA FOR VALUE MOVE
KVWAFLN  DS    15XL4               SAVEAREA FOR MAX COUNT OF FIELDN'S
KVWATMP  DS    XL256               WORKAREA FOR TEMP VALUE MOVE
KVWALNTH EQU   *-KVWAVR            LENGTH OF WORKAREA TO ALLOC
&CSNAME  CSECT
         POP   USING,PRINT
.NOGO    ANOP
         MEND
