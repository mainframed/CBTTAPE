DUDASD   TITLE 'TSO CP TO DISPLAY ON THE ON-LINE VOLUME SERIAL'
******************************************************************
*  SOURCE IS ORIGINATED FROM CBT TAPE FILE 300
*
*  FUNCTION: SAME AS MVS COMMAND D U,DASD,ONLINE
*
*  MODIFIED BY ALBERT CHENG. FOR DETAIL PLEASE REFER THE COMMENT
*  THE PROGRAM IS CODED USING TSOCP CONVENTION AND PUTLINE.
*
*  REGISTER
*  R2   - IOPL
*  R3   - CPPL
*  R4   - LOOP COUNT (NO OF ELEMENT IN DISPLAY LINE)
*  R5   - OUTPUT BUFFER POINTER
*  R6   - ECT
*  R7   - NOT IN USE
*  R8   - VIKJPCL/UIKJPCL
*  R9   - LENGTH OF EXECUTE COMPARE
*         (IE LENGTH OF INPUT VOLUME SERAIL-1)
*  R10  - UCBOB
*  R11  - NOT IN USE
*  R12  - PROGRAM BASE REGISTER
*
*  ABSTRACT
*  1) REMOVE SOME BUG
*                                       (ALBERT CHENG)   10 MAR 86
*  2) MVS/XA ENHANCEMENT
*                                       (ALBERT CHENG)    1 MAR 87
*  3) ACCEPT GENERIC VOLUME SERIAL
*                                       (ALBERT CHENG)    9 OCT 90
*  4) SUPPORT 4 DIGITS UNIT NUMBER
*                                       (ALBERT CHENG)   10 FEB 03
*  5) REMOVE MACRO JVDENTER AND JVDLEAVE.
*     USE BAKR AND PR.                  (ALBERT CHENG)   14 APR 12
*
*  6) PREVENT X'00' FROM LEFT OVER OUPUT FOR THE
*     LAST UNIT RETURN FROM UCBSCAN     (SAM GOLOB)       4 AUG 14
*
*  7) A) ADD ALIAS COMMAND DU TO DISPLAY ALL THE ONLINE VOLUME
*        SERIAL BY UNIT ADDRESS
*        DUO 1000        (DISPLAY DASD FROM 1000 TO FFFF)
*        DUO 1000:1FFF   (DISPLAY DASD FROM 1000 TO 1FFF)
*     B) ADD ALIAS COMMAND DUA TO DISPLAY THE STATUS OF THE DASD
*        (ONLINE AND OFFLINE) BY UNIT ADDRESS
*        DUA             (DISPLAY DASD FROM 0000 TO FFFF)
*        DUA 1000        (DISPLAY DASD FROM 1000 TO FFFF)
*        DUA 0A00:1FFF   (DISPLAY DASD FROM 0A00 TO 1FFF)
*     C) CHANGE THE PROGRAM BASE REGISTER FROM R11 TO R12
*        USUALLY R12 IS THE PROGRAM BASE REGISTER
*     D) USE RELATIVE JUMP
*        CANNOT GET RID OF PROGRAM BASE REGISTER BECAUSE
*        PUTLINE MACRO STILL USE TRAIDITIONAL BRANCH INSTRUCTION
*                                       (ALBERT CHENG)    1 SEP 14
*  8) SUPPORT WILDCARD
*        DUDASD SY*
*        DUDASD SY??0*
*        DUDASD SY??01
*                                       (ALBERT CHENG)   20 NOV 14
*******************************************************************
 TITLE 'Z O S   D U D A S D R  ---  D A S D   V S N   D I S P L A Y'
         PUNCH ' ALIAS DUO'     D U,DASD,ONLINE,UUUU              @07A  00003
         PUNCH ' ALIAS DUA'     D U,DASD,,UUUU                    @07A  00003
DUDASD   CSECT
DUDASD   AMODE    31
DUDASD   RMODE    24
*        USING    CVTMAP,R11                                      @07C
         USING    CPPL,R3
         USING    IOPL,R2
         USING    ECT,R6                                          @07A
         USING    UCBOB,R10
         USING    DUDASD,R12                                      @07C
         J        START                                           @07A
         DC       CL8'DUDAD'                                      @07A
         DC       C'&SYSDATE &SYSTIME'   ASSEMBLE DATE AND TIME   @07A
START    BAKR     R14,0                  SAVE RSA INTO LINKAGE STK@05A
         LR       R12,R15                                         @07C
         XC       PREFIX(4),PREFIX       CLEAR PREFIX FOR PUTLINE
         LA       R15,L'BUFFER+4
         STH      R15,PREFIX             SAVE LENGTH OF DISPLAY LINE
         LR       R2,R13                                           @05A
* INITIALIZE THE IOPL CONTROL BLOCK
         LR       R3,R1
         LA       R2,IOPLIST
         MVC      IOPLECT,CPPLECT        ADDRESS OF ENVIRONMENT CTL TBL
         MVC      CMDECT,CPPLECT
         L        R6,CPPLECT                                       @07A
         MVC      IOPLUPT,CPPLUPT        ADDRESS OF USER PROFILE TABLE
         MVC      CMDUPT,CPPLUPT
         MVC      CMDCBUF,CPPLCBUF       ADDRESS OF COMMAND BUFFER
         XC       ECB,ECB                CLEAR IOPL ECB
         LA       R15,ECB
         ST       R15,CMDECB
         L        R15,VCLADCON           LOAD DUDAD PARSE PARM     @07C
         CLC      ECTPCMD,DUD                                      @07A
         JE       SAVEPCL                BRANCH IF YES             @07A
         L        R15,UCLADCON           LOAD DU PARSE PARM        @07A
SAVEPCL  ST       R15,CMDPCL
         LA       R15,CMDANSR
         ST       R15,CMDANS
         LA       R5,BUFFER                                        @07A
         LA       R1,CMDUPT              PROVIDE LIST FOR PARSE
         LINK     EP=IKJPARS             PARSE COMMAD
         LTR      R15,R15
         JNZ      INVALID_UNIT
         L        R15,CMDANSR            GET POINTER TO ANSWER AREA
         CLC      ECTPCMD,DUD            DUDASD COMMAND ?          @07A
         JNE      CHK_DUO_SYNTAX         BRANCH IF NO              @07A
         L        R8,PVOLUME-DVOL(,R15)    ADDRESS OF VOLSER       @07C
         LH       R9,PVOLUME+4-DVOL(,R15)  GET SIZE OF VOLSER      @07C
         ST       R9,VOLL                  SAVE SIZE OF VOLSER     @08A
         BCTR     R9,R0                  DECREMENT 1 FOR EXECUTE
         J        SETUP                                            @07A
* CHECK THE SYNTAX OF DUA AND DUO                                  @07A
CHK_DUO_SYNTAX    DS  0H                                           @07A
         L        R8,PSTARTU-DUNIT(,R15)   ADDRESS OF PSTARTU      @07A
         LH       R9,PSTARTU+4-DUNIT(,R15) GET SIZE OF PSTARTYU    @07A
         CHI      R9,4                   ONLY FIRST UNIT ?         @07A
         JE       STARTU                 BRANCH IF YES             @07A
         CHI      R9,9                                             @07A
         JNE      DU_ERR1                BRANCH IF YES             @07A
         CLI      PRMDLIM-PRM(R8),C':'   DELIMITER = ':'           @07A
         JNE      DU_ERR1                                          @07A
STARTU   MVC      WORKAREA(4),PRMSTART-PRM(R8)  TRANSLATE START    @07A
         TR       WORKAREA(4),TRHEX        UNIT ADDRESS TO HEX     @07A
         TRT      WORKAREA(4),TRTHEX     START UNIT HEXIDECIMAL TST@07A
         JNZ      DU_ERR2                BRANCH IF INVALID HEX     @07A
         PACK     DBLWD(4),WORKAREA(5)   PACK TO MAKE HEX MAKE HEX @07A
         MVC      DEVNSTRT,DBLWD+1       MOVE STARTING ADDRESS TO  @07A
         CHI      R9,4                   ONLY START UNIT           @07A
         JE       SETUP                  BRANCH IF YES             @07A
         MVC      WORKAREA(4),PRMEND-PRM(R8)  TRANSLATE END UNIT   @07A
         TR       WORKAREA(4),TRHEX        ADDRESS TO HEX          @07A
         TRT      WORKAREA(4),TRTHEX     END UNIT EXIDECIMAL TEST  @07A
         JNZ      DU_ERR3                BRANCH IF INVALID HEX     @07A
         PACK     DBLWD(4),WORKAREA(5)   PACK TO MAKE HEX MAKE HEX @07A
         MVC      DEVNEND,DBLWD+1        COPY END UNIT             @07A
         CLC      DEVNSTRT,DEVNEND       START UNIT > END UNIT ?   @07A
         JH       DU_ERR4                BRANCH IF IT IS TRUE      @07A
SETUP    DS       0H
         XC       SWITCH,SWITCH          CLEAR SWITCH              @07A
*        L        R11,CVTPTR             LOAD CVT POINTER          @07D
         XC       WORKAREA,WORKAREA      CLEAN UP WORKAREA
         XR       R10,R10
         EJECT
***  >>>>>>     UCB SCAN ROUTINE - COMMON RETURN POINT     <<<<<<  @04A
DUUNIT   DS    0H  -----------------FORMAT IOS SCAN PARM LIST      @04A
         XC    USCNWK,USCNWK                                       @04A
         XC    DSXAWORK,DSXAWORK       ZERO OUT UCB WORKAREA       @04A
         LA    R1,DSXAWORK             ADDR OF UCB WORKAREA        @04A
         ST    R1,DSXAWA               STORE IT IN PARM LIST       @04A
         LA    R1,DSXACLAS             PTR TO DEV CLAS TO SEARCH   @04A
         ST    R1,DSXADEVT             STORE IN PARM LIST          @04A
         MVI   DSXACLAS,UCB3DACC       ONLY SEARCH DASD            @04A
         LA    R1,DSXAUCBP             AREA TO STORE UCB ADDR      @04A
         ST    R1,DSXAUCB              IN PARM LIST                @04A
         OI    DSXAUCB,X'80'           SIGNIFY END OF PARMLIST     @04A
DULINE   DS    0H                        NEW DISPLAY LINE
         SPACE
         MVI   BUFFER,C' '
         MVC   BUFFER+1(L'BUFFER-1),BUFFER    INIT BUFFER BY BLANK
         LA    R5,BUFFER
         LA    R4,L'BUFFER/L'ELEMENT     NO. OF ENTRIES PER LINE
         USING ELEMENT,R5
         SPACE
UCBLOOP  UCBSCAN COPY,                 NOT APF AUTHORIZED              +
               WORKAREA=USCNWK,        100-BYTE WORK AREA/SCAN TOKEN   +
               UCBAREA=USCNUCB,        48-BYTE UCB BASE COPY AREA      +
               DEVNCHAR=ELEUNIT,       4-BYTE DEVICE NUMBER            +
               DCEAREA=DCEAREA,        UCB COMMON SEGMENT              +
               DCELEN=DCELENG,         LENGTH OF DCE                   +
               DYNAMIC=YES,            UCB CLASS EXTENSION AREA        +
               DEVN=DEVNSTRT,          START UNIT ADDRESS         @07C +
               RANGE=ALL,              INCLUDE 4-DIGIT NUMBERS         +
               NONBASE=NO,             JUST NEED 1 EXPOSURE PER DISK   +
               DEVCLASS=DASD,          ONLY INTERESTED IN DISKS        +
               RETCODE=RETCODE,        RETURN CODE                @07A +
               RSNCODE=RSNCODE,        REASON CODE                @07A +
               MF=(E,USCNPARM)         POINT TO PARAMETER LIST
         LTR   R15,R15                 HAS A UCB RETURN ?
         JNZ   LASTLINE                BRANCH IF NOTHING IS RETURNED
         LA    R10,USCNUCB               YES, POINT TO UCB COPY    @04A
         CLC   DUD,ECTPCMD             DUDASD ??                   @07A
         JE    FMTDASD                   BRANCH IF YES             @07A
         CLC   UCBCHAN,DEVNEND            LAST DEVICE ADDRESS ?    @07A
         JH    LASTLINE                   BRANCH IF YES            @07A
         J     ALLDASD                                             @07A
FMTDASD  DS    0H                        YES, POINT TO UCB COPY    @04A
*        CLC   0(3,R8),=CL3'ALL'       DISPLAY ALL THE DASD  ??    @07D
         CLC   DUD(6),0(R8)            DISPLAY ALL THE DASD  ??    @07C
         JE    ALLDASD
*        EX    R9,COMPARE                                          @08D
*        CALL WILDCARD MATCH SERVICE                               @08A
         ASAXWC PATTERNSTR=(8),                                        *
               PATTERNSTRLEN=VOLL,                                     *
               STRING=UCBVOLI,                                         *
               STRINGLEN=F0006,                                        *
               ZEROORMORE=WCZ,                                         *
               DELIMITER=BLANKS,                                       *
               ONECHAR=WCO
         LTR   R15,R15                 .PATTERN MATCH ?            @08A
         JNZ   UCBLOOP                 .NO                         @08A
*        JNE   UCBLOOP                                             @08D
ALLDASD  CLC   LASTCUU,ELEUNIT         SAME UNIT ??
         JE    UCBLOOP                                             @04A
         TM    SRTESTAT,UCBONLI    80  DEVICE IS ON-LINE ??
         JO    ONLINE                                              @07C
         CLC   ECTPCMD,DUA             DUA COMMAND ?               @07A
         JNE   UCBLOOP                 BRANCH IF NOT TRUE          @07A
         MVC   ELEVSN,OFFLINE          SET ELEVSN TO OFFLNE        @07A
         TM    UCBFLA,UCBNRY           DEVICE NOT READY ?          @07A
         JNO   RESERV                  BRANCH IF NOT TRUE          @07A
         MVC   ELEVSN,NOTRDY           SET ELEVSN TO F-NRD         @07A
         J     RESERV                  LOOP                        @07A
ONLINE   MVI   ELEDASH,C'-'
         MVC   ELEVSN,UCBVOLI          GET VOLUME SERIAL
         MVI   ELESTAT,C'O'
         MVC   LASTCUU,ELEUNIT
         TM    SRTESTAT,UCBALOC    40  ALLOCATED ??
         JNO   NALLOC
         MVI   ELESTAT,C'A'
NALLOC   TM    SRTESTAT,UCBCHGS    08
         JNO   PUBLIC
         MVI   ELESTAT,C'P'            ??????
PUBLIC   TM    UCBSTAB,UCBBPUB
         JNO   STORAGE
         MVC   ELESTAT2(L'SLASHP),SLASHP       PUBLIC '/P'         @07C
STORAGE  TM    UCBSTAB,UCBBSTR
         JNO   RESERV
         MVC   ELESTAT2(L'SLASHS),SLASHS        STORAGE '/S'       @07C
RESERV   CLI   UCBSQC,0
         JE    MOUNT
         MVI   ELERSRV,C'R'            RESERVED
MOUNT    TM    SRTESTAT,UCBRESV        IS IT RESERVED(MOUNT)
         JZ    RESIDENT                NO - CHECK RESIDENT
         MVC   ELEMSTAT(L'SRSV),SRSV   SET C'RSV' IN LINE          @07C
         J     TSTSYS                   CONTINUE TO NEXT UCB
RESIDENT TM    SRTESTAT,UCBPRES        IS IT RESIDENT
         JZ    TSTSYS                   NO - CONTINUE TO NEXT UCB
         MVC   ELEMSTAT(L'SRSD),SRSD   SET C'RSD' IN LINE          @07C
TSTSYS   TM    UCBSTAT,UCBSYSR         SYSTEM PACK
         JZ    ULOOP                   SKIP, IF NO
         MVI   ELESTAT,C'S'
ULOOP    LA    R5,ELENEXT
         BRCT  R4,UCBLOOP              NEXT UCB ENTRY
         SPACE
PUT      CLI   BUFFER,C' '             EMPTY LINE ?
         JE    DONE                    BRANCH IF EMPTY LINE
         SPACE
         PUTLINE                                                       +
               PARM=PUTLINE,MF=(E,IOPLIST),ECB=ECB,                    +
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK),                     +
               OUTPUT=(PREFIX,TERM,SINGLE,DATA)
         SPACE
         CLI   SWITCH,X'00'
         JE    DULINE                  IF NOT END THEN
         EJECT                            PROCEED TO NEXT ENTRY
DONE     DS    0H
         IKJRLSA  CMDANSR              FREE PARSE AREA
         PR                                                        @05A
LASTLINE OI    SWITCH,X'01'            TURN ON END SWITCH
         MVC   ELEUNIT(4),BLANKS       PREVENT X'00' FROM UCBSCAN  @06A
         J     PUT
INVALID_UNIT   DS  0H                                              @07A
         MVC   ELEUNIT(L'ERRMSG),ERRMSG                            @07A
         J     PUT                                                 @07A
DU_ERR1  DS    0H                                                  @07A
         MVC   ELEUNIT(L'DUM_ERR1),DUM_ERR1                        @07A
         J     PUT                                                 @07A
DU_ERR2  DS    0H                                                  @07A
         MVC   ELEUNIT(L'DUM_ERR2),DUM_ERR2                        @07A
         J     PUT                                                 @07A
DU_ERR3  DS    0H                                                  @07A
         MVC   ELEUNIT(L'DUM_ERR3),DUM_ERR3                        @07A
         J     PUT                                                 @07A
DU_ERR4  DS    0H                                                  @07A
         MVC   ELEUNIT(L'DUM_ERR4),DUM_ERR4                        @07A
         J     PUT                                                 @07A
COMPARE  CLC   UCBVOLI(1),0(R8)
VCLADCON DC    A(VIKJPCL)                                          @07A
UCLADCON DC    A(UIKJPCL)                                          @07A
DCELENG  DC    H'48'           SPACE FOR THE EVENT CONTROL BLOCK   @04A
DEVNSTRT DC    XL2'0'                                              @07A
DEVNEND  DC    XL2'FFFF'                                           @07A
RETCODE  DC    F'0'                                                @07A
RSNCODE  DC    F'0'                                                @07A
F0006    DC    F'6'                                                @08A
VOLL     DC    F'6'                                                @08A
DBLWD    DC    D'0'                                                @07A
SRSD     DC    C'RSD'                                              @07A
SRSV     DC    C'RSV'                                              @07A
SLASHS   DC    C'/S'                                               @07A
SLASHP   DC    C'/P'                                               @07A
DUO      DC    CL8'DUO'         D U,DASD,ONLINE,UUUU               @07A
DUA      DC    CL8'DUA'         D U,DASD,,UUUU                     @07A
DUD      DC    CL8'DUDASD'      D U,DASD,ONLINE                    @07A
NOTRDY   DC    CL6'F-NRD '                                         @07A
OFFLINE  DC    CL6'OFFLNE'                                         @07A
ERRMSG   DC    C'EXAMPLE: DU A0B0:A1FF'                            @07A
DUM_ERR1 DC    C'INVALID SYNTAX  EXAMPLE: DU A0A0:A2FF'            @07A
DUM_ERR2 DC    C'INVALID START UNIT ADDRESS'                       @07A
DUM_ERR3 DC    C'INVALID END UNIT ADDRESS'                         @07A
DUM_ERR4 DC    C'END UNIT ADDRESS IS SMALLER THAN START UNIT ADDRESS'
BLANKS   DC    CL8' '                                              @06A
WCZ      DC    C'*'                                                @06A
WCO      DC    C'?'                                                @06A
TRTHEX   DS    0X                      VERIFY HEX TABLE            @07A
         DC    XL16'00'                                            @07A
         DC    XL240'FF'                                           @07A
         ORG   TRTHEX+16                                           @07A
TRHEX    DC    256X'FF'                HEX TRANSLATION TABLE       @07A
         ORG   TRHEX+C'A'                                          @07A
         DC    X'0A0B0C0D0E0F'                                     @07A
         ORG   TRHEX+C'0'                                          @07A
         DC    X'00010203040506070809'                             @07A
PARMLIST DS    3F          PARMLIST MAPPING
         ORG   PARMLIST
WORKAREA DS    CL100       UCBSCAN ROUTINE WORK AREA
IOPLIST  DS    07F
PREFIX   DS    2H
BUFFER   DS    CL(L'ELEMENT*4)  OUTPUT LINE BUFFER
LASTCUU  DS    CL4            LAST UNIT ADDRESS                    @04C
SWITCH   DC    X'01'                                               @07C
ECB      DS    A(0)           ECB POINTER
CMDANSR  DS    A(0)           PARSE ANSWER AREA POINTER
* USER PPL (PARSE PARAMETER LIST)
CMDUPT   DS    A(0)           USER PROFILE TABLE POINTER
CMDECT   DS    A(0)           ENVIRONMENT CONTROL TABLE
CMDECB   DS    A(ECB)         ECB POINTER
*MDPCL   DS    A(IKJPCL)      ADDRESS OF IKJPARM                   @07D
CMDPCL   DS    A(0)           ADDRESS OF IKJPARM                   @07A
CMDANS   DS    A(CMDANSR)     PLACE TO PUT ANSWER
CMDCBUF  DS    A(0)           POINTER TO COMMAND BUFFER
CMDUWA   DS    A(0)           POINTER TO USER WORK AREA
CMDVEWA  DS    A(0)           POINTER TO USER WORK AREA USER EXITS
PUTLINE  PUTLINE MF=L
         EJECT
* IKJCPPL - TSO COMMAND PROCESSOR PARAMETER LIST
USCNWK   DS    XL100                    UCBSCAN WORK AREA          @04A
USCNUCB  DS    XL48                     UCBSCAN UCB COPY AREA      @04A
DCEAREA  DS    XL48                     @UCB COMMON SEGMENT        @04A
         UCBSCAN MF=(L,USCNPARM)                                   @04A
DSXALIST DS    0F                      PARMLIST FOR UCB
DSXAWA   DS    F                       %100-BYTE WORK AREA
DSXADEVT DS    F                       %DEV TYPE TO SEARCH
DSXAUCB  DS    F                       %UCB FULL WORD PTR
         DS    0D                      FOLLOWING MUST BE ALIGNED
DSXAWORK DS    CL100                   UCB WORK AREA
DSXAEUCB EQU   DSXAWORK+00,04          EMULATION CURR. UCB PTR
DSXAEILK EQU   DSXAWORK+04,04          EMULATION CVTILK2 ADDR
DSXACLAS DS    CL1                     DEVICE CLASS TO SEARCH
DSXAUCBP DS    F                       %UCB
*SAEND   DS    0D
         EJECT
         LTORG
VIKJPCL  IKJPARM DSECT=DVOL                                       @07C
* IKJPCL TO BEGIN PARAMETER CONTROL LIST (PCL)
PVOLUME  IKJIDENT 'VOLUME SERIAL',MAXLNTH=6,FIRST=ALPHA,               X
               DEFAULT='DUDASD',OTHER=ANY                         @07C
*              OTHER=ALPHANUM,DEFAULT='DUDASD'                    @07D
* CHANGE THE DEFAULT FROM ALL TO 123456 AS ALL* IS A VALID VOLUME
* SERIAL. IT IS UNLIKELY DUDASD IS A VOLUME SERAIL.
         IKJENDP
UIKJPCL  IKJPARM DSECT=DUNIT                                      @07A
PSTARTU  IKJIDENT UNIT,LIST,FIRST=ALPHANUM,OTHER=ALPHANUM,CHAR,   @07A X
               MAXLNTH=9,DEFAULT='0000'                           @07A
         IKJENDP
* IKJENDP TO END THE PARAMETER CONTROL LIST (PCL)
* IKJIOPL - THE I/O PARAMETER LIST FOR GETLINE/PUTLINE/PUTGET/STACK
         IKJIOPL
         SPACE
PRM      DSECT
PRMSTART DS    CL4     0   START UNIT ADDRESS                     @07A
PRMDLIM  DS    C       4   DELIMITER                              @07A
PRMEND   DS    CL4     5   END UNIT ADDRESS                       @07A
ELEWRK   DSECT
ELEMENT  DS    0CL20
ELEUNIT  DS    CL4     0   UNIT NAME                              @04C
         DS    CL1     4
ELEVSN   DS    CL6     5   VOLUMER SERIAL
ELEDASH  DS    CL1     B   -
ELESTAT  DS    CL1     C   ONLINE / ALLOCATE
ELERSRV  DS    CL1     D   RESERVED
ELEMSTAT DS    CL3     E   RESIDENT/PUB/RESERVED
ELESTAT2 DS    CL3    10                                          @04C
ELENEXT  DS    0CL1
         SPACE
******** ***** *********************** END OF UCB LOOKUP DSA
         PRINT OFF
         SPACE
         YREGS
         EJECT
         CVT   DSECT=YES
         IEFUCBOB
         IKJCPPL
         IKJECT                                                 @07A
         END
