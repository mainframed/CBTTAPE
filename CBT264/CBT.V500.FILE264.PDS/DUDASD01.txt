DUDASD   TITLE 'TSO CP TO DISPLAY ON THE ON-LINE VOLUUME SERIAL'
******************************************************************
*  SOURCE IS ORIGINATED FROM CBT TAPE FILE 300
*
*  MODIFIED BY ALBERT CHENG. FOR DETAIL PLEASE REFER THE COMMENT
*  THE PROGRAM IS CODED USING TSOCP CONVENTION AND PUTLINE.
*
*  ABSTRACT
*  1) REMOVE SOME BUG
*                                       (ALBERT CHENG)   10 MAR 86
*  2) MVS/XA ENHANCEMENT
*                                       (ALBERT CHENG)    1 MAR 87
*  3) ACCEPT GENERIC VOLUME SERIAL
*                                       (ALBERT CHENG)    9 OCT 90
*  4) SUPPORT 4 DIGITS UNIT NUMBER
*                                       (ALBERT CHENG)   10 FEB 03
*  5) REMOVE MACRO JVDENTER AND JVDLEAVE
*                                       (ALBERT CHENG)   14 APR 12
*
*  6) PREVENT X'00' FROM LEFT OVER      (SAM GOLOB)       4 AUG 14
*     OUTPUT  (SBG)
*
*******************************************************************
 TITLE 'X A    D U D A S D R  ---   D A S D   V S N   D I S P L A Y'
DUDASD   CSECT
         SPACE
*        USING    DSAWRK,R13                                      @05D
         USING    CVTMAP,R12
         USING    CPPL,R3
         USING    IOPL,R2
         USING    UCBOB,R10
*        R8       BASE REGISTER OF INPUT OPERAND
*        R9       LENGTH OF THE INPUT OPERAND
         SPACE
*        JVDENTER DSA=(DSAEND-DSAWRK),BASE=(R11),VERSION=&SYSPARM @05D
         USING    DUDASD,R11                                      @05A
         BAKR     R14,0                                           @05A
         LR       R11,R15                                         @05A
         LR       R2,R13                                          @05A
         SPACE
* INITIALIZE THE IOPL CONTROL BLOCK
         LR       R3,R1
         LA       R2,IOPLIST
         MVC      IOPLECT,CPPLECT        ADDRESS OF ENVIRONMENT CTL TBL
         MVC      CMDECT,CPPLECT
         MVC      IOPLUPT,CPPLUPT        ADDRESS OF USER PROFILE TABLE
         MVC      CMDUPT,CPPLUPT
         MVC      CMDCBUF,CPPLCBUF       ADDRESS OF COMMAND BUF
         XC       ECB,ECB                CLEAR IOPL ECB
         LA       R15,ECB
         ST       R15,CMDECB
         L        R15,PCLADCON
         ST       R15,CMDPCL
         LA       R15,CMDANSR
         ST       R15,CMDANS
         LA       R1,CMDUPT              PROVIDE LIST FOR PARSE
         LINK     EP=IKJPARS             PARSE COMMAD
         L        R15,CMDANSR            GET POINTER TO ANSWER AREA
         USING    IKJPARMD,R15
         L        R8,IKJOPT              ADDRESS OF VOLSER
         LH       R9,IKJOPT+4            GET SIZE OF VOLSER
         BCTR     R9,R0                  DECREMENT FOR EXECUTE
         DROP     R15
         XC       PREFIX(4),PREFIX       CLEAR PREFIX FOR PUTLINE
         LA       R15,L'BUFFER+4
         STH      R15,PREFIX             SAVE LENGTH OF DISPLAY LINE
         OI       SWITCH,X'00'
SETUP    DS       0H
         L        R12,CVTPTR             LOAD CVT POINTER
         XC       WORKAREA,WORKAREA      CLEAN UP WORKAREA
         XR       R10,R10
         EJECT
***  >>>>>>     UCB SCAN ROUTINE - COMMON RETURN POINT     <<<<<<  @04A
DUUNIT   DS    0H  -----------------FORMAT IOS SCAN PARM LIST      @04A
         XC    USCNWK,USCNWK                                       @04A
         XC    DSXAWORK,DSXAWORK       ZERO OUT UCB WORKAREA       @04A
         LA    R1,DSXAWORK             ADDR OF UCB WORKAREA        @04A
         ST    R1,DSXAWA               STORE IT IN PARM LIST       @04A
         LA    R1,DSXACLAS             PTR TO DEV CLAS TO SEARCH   @04A
         ST    R1,DSXADEVT             STORE IN PARM LIST          @04A
         MVI   DSXACLAS,UCB3DACC       ONLY SEARCH DASD            @04A
         LA    R1,DSXAUCBP             AREA TO STORE UCB ADDR      @04A
         ST    R1,DSXAUCB              IN PARM LIST                @04A
         OI    DSXAUCB,X'80'           SIGNIFY END OF PARMLIST     @04A
DULINE   DS    0H                        NEW DISPLAY LINE
         SPACE
         MVI   BUFFER,C' '
         MVC   BUFFER+1(L'BUFFER-1),BUFFER    INIT BUFFER BY BLANK
         LA    R5,BUFFER
         LA    R4,L'BUFFER/L'ELEMENT     NO. OF ENTRIES PER LINE
         USING ELEMENT,R5
         SPACE
UCBLOOP  UCBSCAN COPY,                 NOT APF AUTHORIZED              +
               WORKAREA=USCNWK,        100-BYTE WORK AREA/SCAN TOKEN   +
               UCBAREA=USCNUCB,        48-BYTE UCB BASE COPY AREA      +
               DEVNCHAR=ELEUNIT,       4-BYTE DEVICE NUMBER            +
               DCEAREA=DCEAREA,        UCB COMMON SEGMENT              +
               DCELEN=DCELENG,         LENGTH OF DCE                   +
               DYNAMIC=YES,            UCB CLASS EXTENSION AREA        +
               DEVN=0,                 START WITH THE FIRST UCB        +
               RANGE=ALL,              INCLUDE 4-DIGIT NUMBERS         +
               NONBASE=NO,             JUST NEED 1 EXPOSURE PER DISK   +
               DEVCLASS=DASD,          ONLY INTERESTED IN DISKS        +
               MF=(E,USCNPARM)         POINT TO PARAMETER LIST
         LTR   R15,R15                 HAS A UCB RETURN ?
         BNZ   LASTLINE                BRANCH IF NOTHING IS RETURNED
         LA    R10,USCNUCB               YES, POINT TO UCB COPY     @4A
         CLC   0(3,R8),=CL3'ALL'       DISPLAY ALL THE DASD  ??
         BE    ALLDASD
         EX    R9,COMPARE
         BNE   UCBLOOP                                             @04A
ALLDASD  CLC   LASTCUU,ELEUNIT         SAME UNIT ??
         BE    UCBLOOP                                             @04A
         TM    SRTESTAT,UCBONLI    80  DEVICE IS ON-LINE ??
         BNO   UCBLOOP                                             @04A
ONLINE   MVI   ELEDASH,C'-'
         MVC   ELEVSN,UCBVOLI          GET VOLUME SERIAL
         MVI   ELESTAT,C'O'
         MVC   LASTCUU,ELEUNIT                      (MODIFIED)
         TM    SRTESTAT,UCBALOC    40  ALLOCATED ??
         BNO   NALLOC
         MVI   ELESTAT,C'A'
NALLOC   TM    SRTESTAT,UCBCHGS    08
         BNO   PUBLIC
         MVI   ELESTAT,C'P'            ??????
PUBLIC   TM    UCBSTAB,UCBBPUB
         BNO   STORAGE
         MVC   ELESTAT2(2),=C'/P'      PUBLIC
STORAGE  TM    UCBSTAB,UCBBSTR
         BNO   RESERV
         MVC   ELESTAT2(2),=C'/S'      STORAGE
RESERV   CLI   UCBSQC,0
         BE    MOUNT
         MVI   ELERSRV,C'R'            RESERVED
MOUNT    TM    SRTESTAT,UCBRESV        IS IT RESERVED(MOUNT)
         BZ    RESIDENT                NO - CHECK RESIDENT
         MVC   ELEMSTAT(3),=C'RSV'     SET C'RSV' IN LINE
         B     TSTSYS                   CONTINUE TO NEXT UCB
RESIDENT TM    SRTESTAT,UCBPRES        IS IT RESIDENT
         BZ    TSTSYS                   NO - CONTINUE TO NEXT UCB
         MVC   ELEMSTAT(3),=C'RSD'     SET C'RSD' IN LINE
TSTSYS   TM    UCBSTAT,UCBSYSR         SYSTEM PACK  (MODIFIED)
         BZ    ULOOP                   SKIP, IF NO  (MODIFIED)
         MVI   ELESTAT,C'S'                         (MODIFIED)
ULOOP    LA    R5,ELENEXT
         BCT   R4,UCBLOOP              NEXT UCB ENTRY            @04A
         SPACE
PUT      DS    0H
         MVC   ELEUNIT(4),BLANKS       PREVENT X'00' FROM OUTPUT    SBG
         CLI   BUFFER,C' '
         BE    DONE
         SPACE
         PUTLINE                                                       +
               PARM=PUTLINE,MF=(E,IOPLIST),ECB=ECB,                    +
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK),                     +
               OUTPUT=(PREFIX,TERM,SINGLE,DATA)
         SPACE
         CLI   SWITCH,X'00'
         BE    DULINE                  IF NOT END THEN
         EJECT                            PROCEED TO NEXT ENTRY
DONE     DS    0H
         IKJRLSA  CMDANSR              FREE PARSE AREA
         PR                                                       @05A
*        JVDLEAVE RC=0                                            @05D
LASTLINE OI    SWITCH,X'01'            TURN ON END SWITCH
         B     PUT
COMPARE  CLC   UCBVOLI(1),0(R8)
PCLADCON DC    A(IKJPCL)
DCELENG  DC    H'48'           SPACE FOR THE EVENT CONTROL BLOCK  @04A
BLANKS   DC    CL8' '
*SAWRK   DSECT                                                    @05D
*SSA     DS    18F         SYSTEM SAVE AREA                       @05D
PARMLIST DS    3F          PARMLIST MAPPING
         ORG   PARMLIST
WORKAREA DS    CL100       UCBSCAN ROUTINE WORK AREA
IOPLIST  DS    07F
PREFIX   DS    2H
BUFFER   DS    CL(L'ELEMENT*4)  OUTPUT LINE BUFFER
LASTCUU  DS    CL4            LAST UNIT ADDRESS                  @04C
SWITCH   DS    C
ECB      DS    A(0)           ECB POINTER
CMDANSR  DS    A(0)           PARSE ANSWER AREA POINTER
* USER PPL (PARSE PARAMETER LIST)
CMDUPT   DS    A(0)           USER PROFILE TABLE POINTER
CMDECT   DS    A(0)           ENVIRONMENT CONTROL TABLE
CMDECB   DS    A(ECB)         ECB POINTER
CMDPCL   DS    A(IKJPCL)      ADDRESS OF IKJPARM
CMDANS   DS    A(CMDANSR)     PLACE TO PUT ANSWER
CMDCBUF  DS    A(0)           POINTER TO COMMAND BUFFER
CMDUWA   DS    A(0)           POINTER TO USER WORK AREA
CMDVEWA  DS    A(0)           POINTER TO USER WORK AREA USER EXITS
PUTLINE  PUTLINE MF=L
         EJECT
* IKJCPPL - TSO COMMAND PROCESSOR PARAMETER LIST
USCNWK   DS    XL100                    UCBSCAN WORK AREA       @04A
USCNUCB  DS    XL48                     UCBSCAN UCB COPY AREA   @04A
DCEAREA  DS    XL48                     @UCB COMMON SEGMENT     @04A
         UCBSCAN MF=(L,USCNPARM)                                @04A
DSXALIST DS    0F                      PARMLIST FOR UCB
DSXAWA   DS    F                       %100-BYTE WORK AREA
DSXADEVT DS    F                       %DEV TYPE TO SEARCH
DSXAUCB  DS    F                       %UCB FULL WORD PTR
         DS    0D                      FOLLOWING MUST BE ALIGNED
DSXAWORK DS    CL100                   UCB WORK AREA
DSXAEUCB EQU   DSXAWORK+00,04          EMULATION CURR. UCB PTR
DSXAEILK EQU   DSXAWORK+04,04          EMULATION CVTILK2 ADDR
DSXACLAS DS    CL1                     DEVICE CLASS TO SEARCH
DSXAUCBP DS    F                       %UCB
*SAEND   DS    0D
         EJECT
         LTORG
IKJPCL   IKJPARM
* IKJPCL TO BEGIN PARAMETER CONTROL LIST (PCL)
IKJOPT   IKJIDENT 'VOLUME SERIAL',MAXLNTH=6,FIRST=ALPHA,               X
               OTHER=ALPHANUM,DEFAULT='ALL'
* IKJIDENT TO DESCRIBE A NON-DELIMITER-DEPENDENT POSITIONAL OPERAND
         IKJENDP
* IKJENDP TO END THE PARAMETER CONTROL LIST (PCL)
* IKJIOPL - THE I/O PARAMETER LIST FOR GETLINE/PUTLINE/PUTGET/STACK
         IKJIOPL
         SPACE
ELEWRK   DSECT
ELEMENT  DS    0CL20
ELEUNIT  DS    CL4     0   UNIT NAME                            @04C
         DS    CL1     4
ELEVSN   DS    CL6     5   VOLUMER SERIAL
ELEDASH  DS    CL1     B   -
ELESTAT  DS    CL1     C   ONLINE / ALLOCATE
ELERSRV  DS    CL1     D   RESERVED
ELEMSTAT DS    CL3     E   RESIDENT/PUB/RESERVED
ELESTAT2 DS    CL3    10                                        @04C
ELENEXT  DS    0CL1
         SPACE
******** ***** *********************** END OF UCB LOOKUP DSA
         PRINT OFF
         SPACE
         YREGS
         EJECT
         CVT   DSECT=YES
         IEFUCBOB
         IKJCPPL
         END
