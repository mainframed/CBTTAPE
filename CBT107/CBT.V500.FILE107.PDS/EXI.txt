         MACRO
         EXI   &REG,                    REGISTER FOR EXECUTE.          +
               &INSTR,                  INSTRUCTION TO EXECUTE.        +
               &GEN=LOCTR               GENERATE WITH LOCTR (Â¬INLINE)
.*--------------------------------------------------------------------*
.*
.*  (C) COPYRIGHT 1985.  CLEMSON UNIVERSITY COMPUTER CENTER.
.*
.*  MACRO      EXI
.*
.*  TITLE      CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*             PROGRAMMING MACROS - EXI
.*
.*  PURPOSE    MACRO TO GENERATE AN EX INSTRUCTION AND INSTRUCTION
.*             THAT EX REFERS TO (EXECUTE IMMEDIATE).
.*
.*  NOTE       SEE THE FOLLOWING DOCUMENT FOR MORE INFORMATION ON
.*             THIS AND OTHER STRUCTURED PROGRAMMING MACROS:
.*
.*             . CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*               PROGRAMMING MACROS USER'S GUIDE AND REFERENCE.
.*
.*  ACTIVITY    1/31/85 PLH - NEW
.*
.*--------------------------------------------------------------------*
         GBLA  &#MNDX                   LAST TAG INDEX USED
         GBLA  &#PB#C                   NUMBER OF CURRENT PROGRAM BLOCK
         GBLB  &#PBLOCR                 PROGRAM BLOCK LOCTR OPTION
         GBLC  &#BTYPE(20)              BLOCK TYPE.
         LCLA  &NDX                     TAG INDEX FOR THIS MACRO
&#MNDX   SETA  &#MNDX+1                 CALCULATE NEW TAG INDEX.
&NDX     SETA  &#MNDX                   SET INDEX TO USE IN THIS MACRO.
         LCLC  &TAG1,&TAG2,&TAG3,&OPC
&TAG1    SETC  '#EX#&NDX'               GENERATE TAGS FOR TARGET OF EX
&TAG2    SETC  '#EXEND#&NDX'              BRANCH POINT FOR SKIP.
&OPC     SETC  '&INSTR(1)'              EXTRACT OPCODE.
         LCLC  &OPS                     WILL BUILD OPERAND STRING HERE.
         AIF   (N'&INSTR LE 1).X100     DETERMINE NUMBER OF OPERANDS,
&OPS     SETC  '&INSTR(2)'                  AND BUILD
         AIF   (N'&INSTR LE 2).X100             OPERAND
&OPS     SETC  '&OPS,&INSTR(3)'                   STRING.
         AIF   (N'&INSTR LE 3).X100
&OPS     SETC  '&OPS,&INSTR(4)'
.X100    ANOP
         EX    &REG,&TAG1               GENERATED EX INSTRUCTION.
         AIF   (NOT &#PBLOCR).X300         CHECK FOR INLINE OR LOCTR.
         AIF   ('&#BTYPE(1)' EQ 'SUBROUTINE').X200 IF SUBROUTINE BLOCK
         PSD   TYPE=PARTIAL             LOCATION CTR FOR EX'ED INSTR.
         AGO   .X400
.X200    ANOP                           IF IN SUBROUTINE BLOCK,
         SSD   TYPE=PARTIAL             LOCATION CTR FOR EX'ED INSTR.
         AGO   .X400
.X300    ANOP                           IF GENERATING INLINE,
         B     &TAG2                      BRANCH OVER EX'ED INSTR.
.X400    ANOP
         DS    0H
&TAG1    &OPC  &OPS
         AIF   (NOT &#PBLOCR).X600      CHECK FOR INLINE OR LOCTR.
         AIF   ('&#BTYPE(1)' EQ 'SUBROUTINE').X500 IF SUBROUTINE BLOCK
         ENDPSD                         RESUME PREVIOUS LOCTR.
         AGO   .X600
.X500    ANOP
         ENDSSD                         RESUME PREVIOUS LOCTR.
.X600    ANOP
&TAG2    DS    0H                       BRANCH HERE TO SKIP INSTR.
         MEND
