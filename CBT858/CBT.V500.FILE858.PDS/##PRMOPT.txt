.*--------------------------------------------------------------------*
.*  Copyright 2011 by Clemson University Research Foundation          *
.*                                                                    *
.*  Licensed under the Apache License, Version 2.0 (the "License");   *
.*  you may not use this file except in compliance with the License.  *
.*  You may obtain a copy of the License in this library as member    *
.*  $LICENSE, or from                                                 *
.*                                                                    *
.*      http://www.apache.org/licenses/LICENSE-2.0                    *
.*                                                                    *
.*  Unless required by applicable law or agreed to in writing,        *
.*  software distributed under the License is distributed on an "AS   *
.*  IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either   *
.*  express or implied.  See the License for the specific language    *
.*  governing permissions and limitations under the License.          *
.*--------------------------------------------------------------------*
         MACRO
         ##PRMOPT &OPTIONS=,            PARSE_OPTION PROCESS ONLY      $
               &PARMS=,                 GENERATE_REST PROCESS ONLY     $
               &PARMMAP=,               GENERATE_REST PROCESS ONLY     $
               &TYPE=,                  PARMS TYPE                     $
               &PROCESS=                SET OR GENERATE
.*--------------------------------------------------------------------*
.*
.*  (C) COPYRIGHT 1989.  CLEMSON UNIVERSITY COMPUTER CENTER.
.*
.*  MACRO      ##PRMOPT
.*
.*  TITLE      CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*             PROGRAMMING MACROS - ##PRMOPT
.*
.*  PURPOSE    INTERNAL MACRO USED AS PARSE THE PARMS OPTIONS.
.*
.*  NOTE       SEE THE FOLLOWING DOCUMENT FOR MORE INFORMATION ON
.*             THIS AND OTHER STRUCTURED PROGRAMMING MACROS:
.*
.*             . CLEMSON UNIVERSITY COMPUTER CENTER STRUCTURED
.*               PROGRAMMING MACROS USER'S GUIDE AND REFERENCE.
.*
.*  ACTIVITY    4/28/89 MGG - NEW
.*             10/18/89 MJM - Fixed a problem with the PARMMAP      @C1
.*                            keyword and a problem with its effect @C1
.*                            on the USING option.                  @C1
.*             10/19/89 MJM - Added the COUNT suboption of PARMS.   @C2
.*             08/02/05 MGG - Changed to use ##B macro for branching@C3
.*
.*--------------------------------------------------------------------*
         LCLC  &O                       OPTION PREFIX
         LCLC  &D                       DEFAULT OPTION PREFIX
&O       SETC  '&TYPE'(1,1)
&D       SETC  '#&O.D'                  PREFIX FOR DEFAULT PARMS OPTS
&O       SETC  '#&O.O'                  PREFIX FOR PARMS OPTIONS
         GBLC  &(&O.BASE)               PARM BASE REGISTER
         GBLB  &(&O.OPT)                PARM PARMS OPTION SPECIFIED
         GBLB  &(&O.VL)                 PARM VL OPTION
         GBLB  &(&O.COPY)               PARM COPY OPTION
         GBLC  &(&O.COUNT)              PARM COUNT OPTION           @C2
         GBLB  &(&O.PFX)                PARM PREFIX OPTION
         GBLC  &(&O.CERX)               ERROR EXIT NAME
         GBLC  &(&O.CERC)               ERROR EXIT CODE
         GBLC  &(&D.BASE)               DEFAULT BASE REGISTER
         GBLB  &(&D.OPT)                DEFAULT DEFAULTS OPTION
         GBLB  &(&D.VL)                 DEFAULT VL OPTION
         GBLB  &(&D.COPY)               DEFAULT COPY OPTION
         GBLC  &(&D.COUNT)              DEFAULT COUNT OPTION        @C2
         GBLB  &(&D.PFX)                DEFAULT PREFIX OPTION
         GBLC  &(&D.CERX)               DEFAULT ERROR EXIT NAME
         GBLC  &(&D.CERC)               DEFAULT ERROR EXIT CODE
         GBLC  &#PBNAME                 PROGRAM BLOCK NAME
         GBLA  &#BLVL                   BLOCK LEVEL
         LCLC  &PREFIX                  PREFIX
         LCLC  &TAG                     WORK TAG
         LCLA  &CTR                     WORK COUNTER
         AIF   ('&TYPE' NE 'PROGRAM').NOT_PROGRAM
&WAOPT   SETC  '##PBPWA'
&WA      SETC  'PWA'
&PARMTAG SETC  'PPL'
         AGO   .DSECT_GLOBALS
.NOT_PROGRAM ANOP
         AIF   ('&TYPE' NE 'SUBROUTINE').NOT_SUBROUTINE
&WAOPT   SETC  '##SBSWA'
&WA      SETC  'SWA'
&PARMTAG SETC  'SPL'
         AGO   .DSECT_GLOBALS
.NOT_SUBROUTINE ANOP
         AGO  .CEROR1
.DSECT_GLOBALS ANOP
         GBLB  &(&WAOPT)
         GBLC  &(&WA)
         GBLC  &(&PARMTAG)
.*
.*  Branch to the correct PROCESS= code.
.*
         AIF   ('&PROCESS' EQ 'RESET'                     ).RES0000
         AIF   ('&PROCESS' EQ 'SET_TO_SUBROUTINE_DEFAULTS').STD0000
         AIF   ('&PROCESS' EQ 'SET_SUBROUTINE_DEFAULTS'   ).SSD0000
         AIF   ('&PROCESS' EQ 'PARSE_OPTIONS'             ).POP0000
         AIF   ('&PROCESS' EQ 'GENERATE'                  ).RST0000
         AGO  .CEROR3
.*--------------------------------------------------------------------*
.*  PROCESS=RESET.
.*--------------------------------------------------------------------*
.RES0000    ANOP                    Process=RESET:
&(&O.OPT)   SETB 0                    Indicate PARMS option off.
&(&O.VL)    SETB 0                    Indicate NOVL
&(&O.COPY)  SETB 0                    Indicate NOCOPY
&(&O.COUNT) SETC ''                   Indicate NOCOUNT              @C2
&(&O.PFX)   SETB 0                    Indicate NOPREFIX
&(&O.BASE)  SETC ''                   Indicate NOUSING
&(&O.CERX)  SETC ''                   Indicate NOCOPYERROR name.
&(&O.CERC)  SETC ''                   Indicate NOCOPYERROR code.
            MEXIT                   End of process=RESET.
.*--------------------------------------------------------------------*
.*  PROCESS=SET_TO_SUBROUTINE_DEFAULTS
.*--------------------------------------------------------------------*
.STD0000    ANOP                    Process=SET_TO_SUBROUTINE_DEFAULTS
&(&O.BASE)  SETC '&(&D.BASE)'         Parm default base register.
&(&O.OPT)   SETB (&(&D.OPT))          Parm default PARMS option.
&(&O.VL)    SETB (&(&D.VL))           Parm default VL option.
&(&O.COPY)  SETB (&(&D.COPY))         Parm default COPY option.
&(&O.COUNT) SETC '&(&D.COUNT)'        Parm default COUNT option.    @C2
&(&O.PFX)   SETB (&(&D.PFX))          Parm default PREFIX option.
&(&O.CERX)  SETC '&(&D.CERX)'         Parm default ERROR EXIT name.
&(&O.CERC)  SETC '&(&D.CERC)'         Parm default ERROR EXIT code.
            MEXIT                   End of SET_TO_SUBROUTINE_DEFAULTS.
.*--------------------------------------------------------------------*
.*  PROCESS=SET_SUBROUTINE_DEFAULTS
.*--------------------------------------------------------------------*
.SSD0000    ANOP                    Process=SET_SUBROUTINE_DEFAULTS
&(&D.BASE)  SETC '&(&O.BASE)'         Parm default base register.
&(&D.OPT)   SETB (&(&O.OPT))          Parm default PARMS option.
&(&D.VL)    SETB (&(&O.VL))           Parm default VL option.
&(&D.COPY)  SETB (&(&O.COPY))         Parm default COPY option.
&(&D.COUNT) SETC '&(&O.COUNT)'        Parm default COUNT option.    @C2
&(&D.PFX)   SETB (&(&O.PFX))          Parm default PREFIX option.
&(&D.CERX)  SETC '&(&O.CERX)'         Parm default ERROR EXIT name.
&(&D.CERC)  SETC '&(&O.CERC)'         Parm default ERROR EXIT code.
            MEXIT                   End of SET_SUBROUTINE_DEFAULTS.
.*--------------------------------------------------------------------*
.*  PROCESS=PARSE_OPTIONS.
.*--------------------------------------------------------------------*
.POP0000   ANOP                    Process=PARSE_OPTIONS.
           AIF   ('&OPTIONS(1)' NE 'PARMS').CEROR4
&(&O.OPT)  SETB  1
&CTR       SETA  2                        SET COUNTER TO 1
.POP1000   AIF   (&CTR GT N'&OPTIONS).POP7000
           AIF   ('&OPTIONS(&CTR)'   EQ 'VL'          ).POP1100
           AIF   ('&OPTIONS(&CTR)'   EQ 'NOVL'        ).POP1110
           AIF   ('&OPTIONS(&CTR)'   EQ 'COPY'        ).POP1120
           AIF   ('&OPTIONS(&CTR)'   EQ 'NOCOPY'      ).POP1130
           AIF   ('&OPTIONS(&CTR,1)' EQ 'COUNT'       ).POP1140     @C2
           AIF   ('&OPTIONS(&CTR)'   EQ 'NOCOUNT'     ).POP1150     @C2
           AIF   ('&OPTIONS(&CTR,1)' EQ 'USING'       ).POP1160
           AIF   ('&OPTIONS(&CTR)'   EQ 'NOUSING'     ).POP1170
           AIF   ('&OPTIONS(&CTR)'   EQ 'PREFIX'      ).POP1180
           AIF   ('&OPTIONS(&CTR)'   EQ 'NOPREFIX'    ).POP1190
           AIF   ('&OPTIONS(&CTR,1)' EQ 'COPYERROR'   ).POP1200
           AIF   ('&OPTIONS(&CTR)'   EQ 'NOCOPYERROR' ).POP1210
           MNOTE 8,'&OPTIONS(&CTR) is an invalid suboption of the PARMS$
                option'
           AGO   .POP1900               Check next option.
.*
.POP1100    ANOP                         VL Option:
&(&O.VL)    SETB  1                        Indicate VL
            AGO   .POP1900                 Check next option.
.POP1110    ANOP                         NOVL Option:
&(&O.VL)    SETB  0                        Indicate NOVL
            AGO   .POP1900                 Check next option.
.POP1120    ANOP                         COPY Option:
&(&O.COPY)  SETB  1                        Indicate COPY
            AGO   .POP1900                 Check next option.
.POP1130    ANOP                         NOCOPY Option:
&(&O.COPY)  SETB  0                        Indicate NOCOPY
            AGO   .POP1900                 Check next option.
.POP1140    ANOP                         COUNT Option:              @C2
            AIF   (N'&OPTIONS(&CTR) EQ 2).POP1142                   @C2
            MNOTE 8,'(COUNT,tag) option requires two parameters'    @C2
            AGO   .POP1900                 Check next option.       @C2
.POP1142    ANOP                           Count tag was specified. @C2
&(&O.COUNT) SETC '&OPTIONS(&CTR,2)'        Indicate NOCOUNT.        @C2
            AGO   .POP1900                 Check next option.       @C2
.POP1150    ANOP                         NOCOUNT Option:            @C2
&(&O.COUNT) SETC ''                        Indicate NOCOUNT.        @C2
            AGO   .POP1900                 Check next option.       @C2
.POP1160    ANOP                         USING Option:
            AIF   (N'&OPTIONS(&CTR) EQ 2).POP1162
            MNOTE 8,'USING option requires two parameters'
            AGO   .POP1900                 Check next option.
.POP1162    ANOP                           Base register was specified.
&(&O.BASE)  SETC  '&OPTIONS(&CTR,2)'       Set parm base register.
            AGO   .POP1900                 Check next option.
.POP1170    ANOP                         NOUSING Option:
&(&O.BASE)  SETC  ''                       Indicate NOUSING
            AGO   .POP1900                 Check next option.
.POP1180    ANOP                         PREFIX Option:
&(&O.PFX)   SETB  1                        Indicate PREFIX
            AGO   .POP1900                 Check next option.
.POP1190    ANOP                         NOPREFIX Option:
&(&O.PFX)   SETB  0                        Indicate NOPREFIX
            AGO   .POP1900                 Check next option.
.POP1200    ANOP                         COPYERROR Option:
            AIF   (N'&OPTIONS(&CTR) GT 3).POP1202
            AIF   (N'&OPTIONS(&CTR) GE 2).POP1204
.POP1202    MNOTE 8,'COPYERROR option requires two or three parameters'
            AGO   .POP1900                 Check next option.
.POP1204    ANOP                           Right number of parameters.
&(&O.CERX)  SETC '&OPTIONS(&CTR,2)'        Indicate exit name.
&(&O.CERC)  SETC '&OPTIONS(&CTR,3)'        Indicate exit code.
            AGO   .POP1900                 Check next option.
.POP1210    ANOP                         NOCOPYERROR Option:
&(&O.CERX)  SETC  ''                       Indicate no exit name.
&(&O.CERC)  SETC  ''                       Indicate no exit code.
            AGO   .POP1900                 Check next option.
.*
.POP1900    ANOP
&CTR        SETA  &CTR+1                 Increment counter.
            AGO   .POP1000               Return to top-of-loop.
.POP7000    ANOP
.POP9000    MEXIT                     End of PARSE_OPTIONS.
.*--------------------------------------------------------------------*
.*  PROCESS=GENERATE
.*--------------------------------------------------------------------*
.RST0000 ANOP                        Process=GENERATE.
         AIF   (&(&O.OPT)).RST0010
         AIF   ('&PARMS' EQ '' AND '&PARMMAP' EQ '').RST9000
         MNOTE 8,'PARMS and PARMMAP keywords require the PARMS option'
         AGO   .RST9000
.RST0010 ANOP
.**-----------------------------------------------------------------
.**  LOAD PARAMETER LIST BASE AND ESTABLISH ADDRESSIBILITY AS
.**  REQUESTED.
.**-----------------------------------------------------------------
         AIF   ('&(&O.BASE)' EQ '').RST0080
         AIF   (&(&O.COPY)).RST0020
         LR    &(&O.BASE),R1            LOAD PARM LIST BASE
         AGO   .RST0030
.RST0020 LA    &(&O.BASE),&(&PARMTAG)   POINT TO COPY OF PARM LIST
.RST0030 AIF   ('&PARMMAP' NE '').RST0040                           @C1
         USING &(&PARMTAG),&(&O.BASE)   ESTABLISH BASE FOR PARM LIST@C1
         AGO   .RST0080                                             @C1
.RST0040 USING &PARMMAP(1).,&(&O.BASE)  ESTABLISH BASE FOR PARMMAP  @C1
.**-----------------------------------------------------------------
.**  PROCESS THE PARMS KEYWORD, IF SPECIFIED.
.**-----------------------------------------------------------------
.RST0080 AIF   ('&PARMS' EQ '').RST1000
         AIF   ('&PARMMAP' EQ '').RST0090
         MNOTE 8,'PARMS and PARMMAP keywords are mutually exclusive'
.RST0090 ANOP
         AIF   (&(&O.COPY)).RST0100
&(&PARMTAG)  DSECT ,                    PARAMETER LIST DSECT
         AGO   .RST0190
.RST0100 &WA   TYPE=PARTIAL             BUILD LIST IN WORK AREA
         DS    0F                       INSURE FULLWORD ALIGNMENT
&X       SETA  N'&PARMS*4
&(&PARMTAG) EQU *,&X                    DEFINE TAG FOR PARAMETER LIST
.RST0190 ANOP
.* START OF LOOP
&CTR     SETA  0
&PREFIX  SETC  ''
         AIF   (NOT &(&O.PFX)).RST0200
&PREFIX  SETC  '&(&PARMTAG)'
.RST0200 AGO   .RST0480
.RST0400 ANOP
&TAG     SETC  '&PREFIX.&PARMS(&CTR)'
&TAG     DS    A                         PARAMETER ADDRESS
.RST0480 ANOP
&CTR     SETA  &CTR+1
         AIF   (&CTR LE N'&PARMS).RST0400
.* END OF LOOP
         AIF   (&(&O.COPY)).RST0570
&#PBNAME CSECT ,                        GET BACK IN PROGRAM CSECT
         AGO   .RST0590
.RST0570 END&WA ,                       GET OUT OF WORK AREA
.RST0590 ANOP
.RST1000 ANOP
.**-----------------------------------------------------------------
.**  PROCESS THE PARMMAP KEYWORD, IF SPECIFIED.
.**-----------------------------------------------------------------
         AIF   ('&PARMMAP' EQ '').RST1900
         AIF   (N'&PARMMAP LE 2).RST1010
         MNOTE 8,'Too many parameters specified on PARMMAP keyword'
.RST1010 ANOP
         AIF   (&(&O.COPY)).RST1020
&(&PARMTAG) EQU &PARMMAP(1)
         AGO   .RST1900
.RST1020 ANOP
         AIF   (N'&PARMMAP EQ 2).RST1030
         MNOTE 8,'Second parameter required on PARMMAP keyword if COPY *
               PARMS option is specified'
         AGO   .RST1900
.RST1030    &WA TYPE=PARTIAL             DEFINE FIELD IN WORK AREA
            DS  0F                       INSURE FULLWORD ALIGNMENT
&(&PARMTAG) DS  XL(&PARMMAP(2).)         DEFINE FIELD FOR COPY      @C1
            END&WA ,                     GET OUT OF WORK AREA
.RST1900 ANOP
.RST2000 ANOP
.**-----------------------------------------------------------------
.**  Implement the COPY (VL or NOVL) and COUNT (VL only) options.   @C2
.**-----------------------------------------------------------------
&TAG     SETC  '##PRMOPT_PARMS_&SYSNDX._'
         AIF   (&(&O.VL)).RST2100
         AIF   ('&(&O.COUNT)' EQ '').RST2050                        @C2
         MNOTE 8,'COUNT option requires the VL option.'             @C2
.RST2050 AIF   (NOT &(&O.COPY)).RST2900
         MVC   &(&PARMTAG).,0(R1)       | Move parms to copy area.
         AGO   .RST2900
.RST2100 AIF   (NOT &(&O.COPY)).RST2200
         AIF   ('&(&O.CERX)' EQ '' AND '&(&O.CERC)' EQ '').CEROR5
         XC    &(&PARMTAG).,&(&PARMTAG) | Clear parmlist area.
         SLR   R15,R15                  | Clear index register.
&TAG.000 DS    0H                       | Loop over parameters.
         L     R0,0(R15,R1)             |   Load parameter from input.
         ST    R0,&(&PARMTAG).(R15)     |   Store in copy area.
         LTR   R0,R0                    |   Check for final parm.
         ##B M,&TAG.100                 |   Finish up if last one.  @C3
         LA    R15,4(R15)               |   Increment index register.
         CH    R15,=Y(L'&(&PARMTAG.).)  |   Check for overflow.
         ##B L,&TAG.000                 | Continue looping if okay. @C3
         ERREXIT NAME=&(&O.CERX),CODE=&(&O.CERC)  CopyError exit.
&TAG.100 DS    0H                       | Finished with VL copying.
         AIF   ('&(&O.COUNT)' EQ '').RST2170                        @C2
         LA    R0,4(,R15)               | R0 := length of parmlist. @C2
         SRL   R0,2                     | R0 := number of parms.    @C2
         ST    R0,&(&O.COUNT)           | Store in COUNT tag.       @C2
.RST2170 LA    R15,&(&PARMTAG).(R15)    | Get address of final parm.
         NI    0(R15),X'7F'             | Turn off the VL bit.
         AGO   .RST2900
.RST2200 AIF   ('&(&O.COUNT)' EQ '').RST2900
         LR    R15,R1                   | R15 -> Start of parmlist. @C2
&TAG.000 TM    0(R15),X'80'             | Test for VL bit.          @C2
         LA    R15,4(,R15)              | Point to next parameter.  @C2
         ##B NO,&TAG.000                | Loop if not past VL bit.  @C3
         SLR   R15,R1                   | R15 := parmlist length.   @C2
         SRL   R15,2                    | R15 := number of parms.   @C2
         ST    R15,&(&O.COUNT)          | Store in COUNT tag.       @C2
.RST2900 ANOP
.*
.RST9000 MEXIT                          End of Process=GENERATE.
.*--------------------------------------------------------------------*
.CEROR1  MNOTE 8,'&TYPE is invalid'
         MEXIT
.CEROR2  MNOTE 8,'##PRMOPT does not support &OPTIONS(1) option'
         MEXIT
.CEROR3  MNOTE 8,'&PROCESS is an invalid value for PROCESS keyword'
         MEXIT
.CEROR4  MNOTE 8,'The first parameter in PARM options must be PARMS'
         MEXIT
.CEROR5  MNOTE 8,'COPYERROR is required for (PARMS,COPY,VL) options.'
         MEXIT
         MEND
