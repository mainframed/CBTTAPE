        /*********************************REXX**********************************
        * Execute a TSO Command and Browse the Output                          *
        ***********************************************************************/
        Trace "OFF"
        Call on error
        Address ISPEXEC "CONTROL ERRORS RETURN"
        Call outtrap "LINE."
        Address TSO arg(1)
        Call outtrap "OFF"
        If line.0 > 0
        Then do
           cmd_dd = "C"random()
           "ALLOCATE FILE("cmd_dd") REUSE UNIT(SYSVIO) SPACE(1 1) CYLINDER",
                    "DSORG(PS) RECFM(V B) LRECL(255)"
           "EXECIO" line.0 "DISKW" cmd_dd "(STEM LINE. FINIS)"
           Address ISPEXEC "LMINIT DATAID(DATAID) DDNAME("cmd_dd")"
           Address ISPEXEC "BROWSE DATAID("dataid")"
           Address ISPEXEC "LMFREE DATAID("dataid")"
           "FREE FILE("cmd_dd")"
           End
        Exit

        Error:

        Zerrsm = "Return code" rc
        Zerrlm = "Return code from command is" rc
        Zerralrm = "YES"
        Zerrhm = "*"
        Address ISPEXEC "SETMSG MSG(ISRZ002)"
        Return
