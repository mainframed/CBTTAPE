        /* --------------------  rexx procedure  -------------------- *
         * Name:      TSOTrap                                         *
         *                                                            *
         * Function:  Issue a TSO Command, CLIST, EXEC and            *
         *            capture (trap) the generated messages           *
         *            then display those in ISPF Browse               *
         *                                                            *
         * Syntax:    %tsotrap opt tso-command command-parameters     *
         *                                                            *
         *            opt is optional and if used must be:            *
         *                -B to indicate browse of the messages       *
         *                -E to indicate edit of the messages         *
         *                                                            *
         *            tso-command is any TSO Command, Clist, or Exec  *
         *            command-parameters are any paramters to the     *
         *            tso-command                                     *
         *                                                            *
         * Author:    Lionel B. Dyck                                  *
         *                                                            *
         * History:                                                   *
         *            07/14/16 - Change from stemview to stemedit     *
         *            03/11/16 - Add -E and -B options to edit/browse *
         *                       - use stemview for Browse or Edit if *
         *                         stemview variable set              *
         *            03/08/16 - Addition of comments and to use      *
         *                       a more accurate LRECL                *
         *                     - Use STEMVIEW if installed            *
         *                       (see *custom* below)                 *
         *            12/14/94 - Creation                             *
         * ---------------------------------------------------------- */

        /* ---------------------- *
         * Define our environment *
         * ---------------------- */
        savemsg = msg("on")
        Call on error
        Address ISPEXEC "CONTROL ERRORS RETURN"

        /* ----------------------------------------- *
         * Update the stemview variable to 0 if the  *
         * STEMVIEW tool is not installed.  If it is *
         * installed set it to 1.     *custom*       *
         * ----------------------------------------- */
        stemview = 1
        trapopt = '-B'

        /* ------------------------------------------ *
         * Process the TSO Command and its parameters *
         * ------------------------------------------ */
        parse arg command
        if length(command) = 0 then do
           zedsmsg = ""
           zedlmsg = "Try TSOTRAP one more time with a command"
           Address ISPExec "SetMSG MSG(isrz000)"
           x = msg(savemsg)
           exit
           end

        /* ----------------------------------------------------- *
         * Test for a -E or -B option prior to the trap command. *
         * ----------------------------------------------------- */
         if left(command,1) = '-' then do
            parse value command with trapopt command
            trapopt = translate(trapopt)
            if wordpos(trapopt,'-E -B') = 0 then do
               say 'Error'
               say 'Valid options are:'
               say '   -B to browse the results (default)'
               say '   -E to edit the results'
               say ' '
               say 'Exiting - try again.'
               exit 8
               end
           end

        /* -------------------------------- *
         * 1) Setup the OUTTRAP environment *
         * 2) Issue the TSO command         *
         * 3) Close the OUTTRAP environment *
         * -------------------------------- */
        Call outtrap "LINE."
        Address TSO command
        Call outtrap "OFF"

        /* ----------------------------------------- *
         * Test for captured (trapped) messages      *
         *                                           *
         * Then find the max LRECL of those messages *
         *                                           *
         * Then Allocate a temporary dataset         *
         * Write the messages to that dataset        *
         * Browse the dataset                        *
         * ----------------------------------------- */
        If line.0 > 0
        Then do
           /* find the max lrecl */
              maxl = 0
              do i = 1 to line.0
                 if maxl < length(line.i) then maxl = length(line.i)
                 end
            if stemview = 1 then do
               if trapopt = '-E' then
               CALL stemedit 'Edit','line.',,,"TSOTRAP Command:" command,,maxl
               if trapopt = '-B' then
               CALL stemedit 'Browse','line.',,,"TSOTRAP Command:" command,,maxl
               end
            else do
              /* allocate a temp file to browse the results */
              cmd_dd = "C"random()
              "ALLOCATE FILE("cmd_dd") REUSE UNIT(vio) SPACE(1 1)" ,
                       "CYL DSORG(PS) RECFM(V B) LRECL("maxl+4")"
              "EXECIO" line.0 "DISKW" cmd_dd "(STEM LINE. FINIS)"
              Address ISPEXEC "LMINIT DATAID(DATAID) DDNAME("cmd_dd")"
              if trapopt = '-B' then
                  Address ISPEXEC "BROWSE DATAID("dataid")"
              if trapopt = '-E' then
                  Address ISPEXEC "EDIT DATAID("dataid")"
              Address ISPEXEC "LMFREE DATAID("dataid")"
              "FREE FILE("cmd_dd")"
              End
           End

           x = msg(savemsg)
        Exit

        /* ---------------- *
         * Error Processing *
         * ---------------- */
        Error:
        Zerrsm = "Return code" rc
        Zerrlm = "Return code from command is" rc
        Zerralrm = "YES"
        Zerrhm = "*"
        Address ISPEXEC "SETMSG MSG(ISRZ002)"
        Return
