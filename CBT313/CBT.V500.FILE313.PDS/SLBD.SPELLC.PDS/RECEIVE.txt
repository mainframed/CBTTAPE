        /* this rexx exec is expected to be invoked by the EXEC command
           thus:

           EXec 'dsname(RECEIVE)'

           or from ISPF 3.4 Edit or Browse place EX on the selection
           field next to the RECEIVE member.

           Prompting will occur for hlq and optional volser.

           and it will then issue the TSO RECEIVE command for the
           the following members to create these new data sets:

           Member       Dataset
           EXEC         hlq.NEW.SPELLC.EXEC
           PANELS       hlq.NEW.SPELLC.PANELS
           GERMAN       hlq.NEW.SPELLC.GERMAN.WORDS

           You will then need to copy these datasets/members into
           datasets for production use.

           After these data sets are created several of the members
           of this install data set will be browsed.
           */

        parse source x1 x2 x3 x4 dsn .
         x = PROMPT("ON") /* enable prompting */

        say "If you received this package from a location other than"
        say "the authors at http://www.lbdsoftware.com you might want"
        say "to check the authors site now for a more current"
        say "version of this package."
        say " "
        say "To cancel this receive so you can verify that you have"
        say "the most current version enter CANCEL, otherwise any key"
        say "to continue"
        pull cont
        if cont = "CANCEL" then exit

        say "Enter desired hlq for target data sets",
            "(default is" sysvar("syspref")".SPELLC.NEW):"
        pull hlq
        hlq = strip(hlq)

        if hlq = "" then hlq = sysvar("syspref")".SPELLC.NEW"

        say "Enter optional volser for target data sets",
            "default is to allow standard allocation to",
            "find a volume."
        pull volser
        volser = strip(volser)

        if volser <> "" then vol = "vol("volser")"
                        else vol = ""

        Say "Using HLQ:" hlq
        if vol <> "" then
           Say "Using Volser:" volser

        queue "dsn('"hlq".exec')" vol
        "Receive inds('"dsn"(exec)')"
        queue "dsn('"hlq".panels')" vol
        "Receive inds('"dsn"(panels)')"
        queue "dsn('"hlq".german.words')" vol
        "Receive inds('"dsn"(german)')"

        if sysvar('sysispf') <> "ACTIVE" then exit
        Address ISPEXEC
           "Browse dataset('"dsn"($doc)')"
           "Browse dataset('"dsn"($more)')"
           "Browse dataset('"dsn"(changes)')"
           "Edit   dataset('"hlq".exec(spellcdf)')"
