/*.REXX EDIT MACRO */
/*.********************************************************************/
/*.SUBCAN: EDIT MACRO TO SUBMIT JCL AND CANCEL CHANGES                */
/*.        ALSO CHANGES USERID ON JOBCARD TO CURRENT USER FOR SUBMIT  */
/*.********************************************************************/
/*. THIS IS USEFUL WHEN EDITING JCL IN SHARED LIBRARIES AND THE      */
/*. JOBCARD IS SET UP FOR SOMEONE ELSE'S TSOID.                      */
/*. SUBCAN WILL DO THE FOLLOWING:                                    */
/*. 1. CHANGE THE JOBNAME ON THE JOBCARD TO THE TSOID OF CURRENT USER*/
/*.    (ALSO CHANGES USER= AND NOTIFY= ON THE JOBCARD)               */
/*. 2. SUBMIT THE JCL                                                */
/*. 3. CANCEL THE EDIT SESSION, WITHOUT THE CONFIRMATION PANEL       */
/*.                                                                  */
/*. THIS IS ALSO USEFUL WHEN EDITING YOUR OWN JCL WHEN YOU WANT TO   */
/*. MAKE TEMPORARY CHANGES, SUBMIT THE JCL AND THEN CANCEL THESE     */
/*. CHANGES ALL WITH ONE COMMAND (I FREQUENTLY FORGET THAT I WANTED  */
/*. TO CANCEL AND HIT PF3 BY MISTAKE, AND SUBCAN PREVENTS THIS).     */
/*.                                                                  */
/*.********************************************************************/
/*.AUTHOR: GARY SCARCELLA  EMAIL: SCARCELLAGV@YAHOO.COM              */
/*.*******************************************************************/
TRACE OFF
ADDRESS ISPEXEC "ISREDIT MACRO (EDITPARM)"
PARMS = STRIP(EDITPARM,B)
TSOENV = SYSVAR(SYSENV)  /* FORE OR BACK */
/*
IF (PARMS = '?' ) & TSOENV = 'FORE'
THEN DO
       EXECNAME = SYSVAR(SYSICMD)
       ADDRESS TSO "HELPX " EXECNAME
       EXIT 0
     END
*/

USRID = USERID()
ADDRESS ISREDIT " BOUNDS 1 72 "
ADDRESS ISREDIT "("ITOP")      = LINENUM .ZFIRST"
ADDRESS ISREDIT "("IBOTTOM")   = LINENUM .ZLAST "
LINENBR = 0

DO I = ITOP TO IBOTTOM
   LINENBR = LINENBR + 1
   LINEDATA = "LINEDATA"
   ADDRESS ISREDIT "(LINEDATA) = LINE " LINENBR
   PARSE VAR LINEDATA WORD1 WORD2 WORDREST
   IF LEFT(WORD1,2) ¬= '//' THEN ITERATE  /* SKIP IF NOT JCL LINE */
   IF WORD2 = 'EXEC' THEN LEAVE           /* STOP AFTER JOBCARD   */
   IF WORD2 = 'JOB' & LEFT(WORD1,3) ¬= '//*'
   THEN DO
          PARSE VAR WORD1 '//' CURRUSER
          CURRUSER_SFX = RIGHT(CURRUSER,1)
          NEWLINE = '//' || USRID || CURRUSER_SFX 'JOB' WORDREST
          ADDRESS ISREDIT "LINE " LINENBR "= (NEWLINE)"
          LINEDATA = "LINEDATA"
          ADDRESS ISREDIT "(LINEDATA) = LINE " LINENBR
        END
   IF POS("USER=",LINEDATA) > 0
   THEN DO
          PARSE VAR LINEDATA WORDBEGIN 'USER=' CURRUSER .
          IF POS(",",CURRUSER) > 0
          THEN PARSE VAR CURRUSER CURRUSER ',' .
          IF LENGTH(CURRUSER) <= 8
          THEN ADDRESS ISREDIT "CHANGE "||CURRUSER USRID
        END
   IF POS("NOTIFY=",LINEDATA) > 0
   THEN DO
          PARSE VAR LINEDATA WORDBEGIN 'NOTIFY=' CURRUSER .
          IF POS(",",CURRUSER) > 0
          THEN PARSE VAR CURRUSER CURRUSER ',' .
          IF LENGTH(CURRUSER) <= 8
          THEN ADDRESS ISREDIT "CHANGE "||CURRUSER USRID
        END
END  /* DO I = ITOP TO IBOTTOM */

ADDRESS ISREDIT " BOUNDS "

ADDRESS ISREDIT " SUBMIT"
ADDRESS ISREDIT " CANCEL"

EXIT 0  /* EXIT WITH RC = 0 TO KEEP CURSOR ON LAST SET POSITION */
