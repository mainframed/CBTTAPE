ICFCAT   TITLE ' - PROCESS AN ICF OR VSAM CATALOG'
***********************************************************************
*
*  THIS IS A FIRST ATTEMPT AT WRITING THE MISSING ICF CATALOG CODE
*  FOR IEHMAP. IF EITHER CNAME= IS NOT SPECIFIED, OR IS NOT A VSAM
*  DATASET, THEN CONTROL IS PASSED TO MAPCATLG TO TREAT AS OS CVOL.
*
*  ** FOR NOW ONLY THE DEPT, SPACE, AND USERID CARDS ARE PROCESSED. **
*  ** THE CATLG CARD IS *NOT* PROCESSED YET. (SOMEDAY MAYBE)        **
*
*
***  THANKS  ****  THANKS  ****  THANKS  ****  THANKS  ****  THANKS ***
*
*  MANY THANKS FOR CODE SHOWING HOW TO READ ICF AND VSAM CATALOGS GOES
*  TO MR. JIM LANE OF SUNCOR, INC.  HIS CODE IN LISTICAT (FROM CBT TAPE
*  VERSION 328) INSPIRED THE ATTEMPT HERE. IN FACT A LOT OF THE CODE
*  IS HIS. ANY MISTAKES OF COURSE ARE MINE.
*
***  THANKS  ****  THANKS  ****  THANKS  ****  THANKS  ****  THANKS ***
*
*
*  REGISTER USAGE
*     R13  -  SAVEAREA - ALLOCATED VIA IMAP0228
*     R12  -  THIS SECTION BASE REGISTER
*     R11  -  MAIN IEHMAP WORKAREA
*     R10  -  ADDITIONAL ICF CATALOG WORK AREA (THIS MODULE ONLY)
*     R9   -  INTERNAL SUBROUTINE CALL
*     R6   -  BASE OF CSECT IEHMAP FOR UTILITY SUBROUTINES
*     R3   -  POINTER TO CURRENT BCS CELL
*
***********************************************************************
ICFCAT   CSECT
         USING EXTREFS,R6
         USING ICFCAT,R15
         B     HDR                 SKIP HEADER
         DC    C'ICFCAT - &SYSDATE &SYSTIME'
HDR      STM   R14,R12,12(R13)     SAVE REGISTERS
         BAL   R14,IMAP0228        GET NEW SAVEAREA
         LR    R12,R15            SETUP NEW BASE REGISTER
         DROP  R15
         USING ICFCAT,R12
*
**       DETERMINE IF CATALOG REQUESTED IS ICF OR NOT
*
         CLI   2656(R11),C' '     WAS CNAME= SPECIFIED
         BE    OSCVOL             IF NOT THEN IS OS CVOL
         L     R2,608(,R11)       POINT TO DSN LOCATION
         MVC   0(44,R2),2656(R11)  MOVE IN CNAME= SPECIFIED
         LA    R1,840(,R11)       POINT TO CAMLST
         SVC   27                 OBTAIN DSCB
         LTR   R15,R15
         BNZ   BOMB1
         L     R15,820(,R11)      POINT AT DSCB
         USING DSCB,R15
         TM    DS1DSORG+1-44,DS1ORGAM IS THIS A VSAM DATASET
         BO    REALICF            YES, SO DO NEW PROCESSING
*
**       CALL OLD OS CVOL PROCESSOR
*
OSCVOL   L     R15,=V(MAPCATLG)
         BALR  R14,R15
         B     IMAP02E8            RETURN TO CALLER WITH RC
         EJECT
**********************************************************************
*
*    WE ARE NOW PROCESSING A REAL ICF/VSAM CATALOG, SO GO TO IT
*
**********************************************************************
         SPACE 2
REALICF  DS    0H
*
**       GET A NEW WORKAREA
*
         LA     R0,ICFWKLN         GET LENGTH OF AREA
         GETMAIN R,LV=(0)
         LR     R10,R1             SET POINTER
         USING  ICFWK,R10          SETUP ADDRESSABILITY
         XC     TAIL,TAIL          CLEAR VARIABLE
*
**       DYNAMICALLY ALLOCATE THE CATALOG
*
         MVC   TXT1(TXTLEN),RTXT1 SET REAL TEXT UNIT DATA
         MVC   TXT1+6(44),2656(R11)  SET DSNAME
         LA    R1,DYRB          SETUP
         ST    R1,PLIST           PLIST POINTER
         OI    PLIST,X'80'      SET END OF LIST INDICATOR
         LA    R1,TXT1          SETUP
         ST    R1,TXTUP            TEXT
         LA    R1,TXT2                UNIT
         ST    R1,TXTUP+4                POINTER
         LA    R1,TXT3                      LIST
         ST    R1,TXTUP+8
         OI    TXTUP+8,X'80'    SET END OF LIST INDICATOR
         LA    R15,DYRB
         USING S99RB,R15
         LA    R1,TXTUP
         ST    R1,S99TXTPP
         MVI   S99VERB,S99VRBAL SET TO DSN ALLOCATION
         MVI   S99FLAG1,S99NOMNT  MARK NOT TO MOUNT
         MVI   S99RBLN,20         SET LENGTH OF BLK
         DROP  R15
         LA    R1,PLIST
         DYNALLOC ,
         LTR   R15,R15          ANY ERROR
         BNZ   BOMB2            ISSUE ERROR MESSAGE
*
**       GENERATE AN ACB TO ACCESS THE ALLOCATED CATALOG
*
         GENCB BLK=ACB,            GENERATE THE ACB                    X
               AM=VSAM,                                                X
               DDNAME=(*,TXT3+6),                                      X
               EXLST=EXLICAT,                                          X
               BUFNI=16,                                               X
               BUFND=16,                                               X
               RMODE31=CB,                                             X
               MF=(G,GENPLST,GPLLEN1)
         STM   R0,R1,ACBPTRL       SAVE ACB LENGTH AND ADDRESS
*
**       ATTAIN KEY ZERO STATE TO AFFECT THE JSCB PASSWORD BIT
*
*        LA    R0,1                INDICATE AUTHORIZATION ON.
*        SVC   232                 ***** INSTALLATION DEPENDANT ******
         SPACE ,
         USING PSA,R0
         L     R2,PSATOLD          MY TCB.
         USING TCB,R2
         L     R2,TCBJSCB          TCB -> JSCB.
         DROP  R2
         USING IEZJSCB,R2
         L     R2,JSCBACT          ACTIVE JSCB.
         MODESET KEY=ZERO          JSCB IS IN KEY ZERO.
         SPACE ,
*
* OPENING A CATALOG AS A DATASET SEEMS TO CAUSE PASSWORD PROMPTING
* EVEN FOR READING. HERE I SET THE BYPASS PASSWORD BIT TO GIVE OPER
* A BREAK.
*
         OI    JSCBSWT1,JSCBPASS   SAY, NO PASSWORDS PLEASE.
         SPACE ,
*
**       RETURN TO NORMAL KEY
*
         MODESET KEY=NZERO         BACK TO USER KEY.
*
**       ATTEMPT TO OPEN CATALOG
*
         L     R3,ACBPTR           POINT AT ACB
         MVI   OPNLST,X'80'        SET END OF LIST INDICATOR
         OPEN  ((R3)),MF=(E,OPNLST) OPEN THE BCS CLUSTER.
         LTR   R15,R15             DID IT OPEN?
         BZ    OPENOK              YES.
         SHOWCB  ACB=(*,ACBPTR),                                       X
               AREA=(S,OPENERR),                                       X
               LENGTH=4,                                               X
               FIELDS=(ERROR),     STORE OPEN ERROR CODE.              X
               MF=(G,GENPLST,GPLLEN3)
         L     R3,OPENERR          PICK ERROR CODE.
         ABEND 14,DUMP             AND DIE.
*
**       NOW THAT THE CATALOG IS OPEN, TURN OFF THE JFCB PASSWORD BIT
*
OPENOK   MODESET KEY=ZERO          KEY ZERO TO MODIFY JSCB.
         NI    JSCBSWT1,255-JSCBPASS  BYPASS PASSWORD OFF.
         MODESET KEY=NZERO         BACK TO USER KEY.
         DROP  R2
*        SR    R0,R0               INDICATE AUTHORIZATION OFF.
*        SVC   232                 ***** INSTALLATION DEPENDANT. *****
         TITLE ' - GENERATE AN RPL TO ACCESS THE BCS CLUSTER.'
         L     R0,BUFLEN           LENGTH TO GETMAIN
         GETMAIN R,LV=(0)          INPUT BUFFER FOR BCS RECORDS.
         ST    R1,BUFPTR           SAVE ADDRESS OF RECORD BUFFER.
         GENCB BLK=RPL,                                                X
               ACB=(*,ACBPTR),                                         X
               AM=VSAM,                                                X
               AREA=(*,BUFPTR),                                        X
               AREALEN=131072,                                         X
               OPTCD=(KEY,SEQ,MVE),                                    X
               MF=(G,GENPLST,GPLLEN2)
         STM   R0,R1,RPLICATL      SAVE GENERATED RPL ADDR.
*
**       SEE IF DSN VS. VOLUME PROCESSING NEEDED (DEPT, SPACE, AND
**       USERID CARDS), IF NOT THEN GENERATE TITLE INFO.
*
         TM    2943(R11),X'80'     IS DSN VS. VOLUME PROCESSING WANTED
         BO    GETNEXT             YES, SO SKIP TITLES
*
**       CREATE TITLE LINE
*
         MVC   2284+23(100,R11),1624(R11)  BLANK OUT LINE AREA
         MVC   2284+14(40,R11),TITLES
         L     R2,608(,R11)        ACCESS GOD KNOWS WHAT
         MVC   2284+42(6,R11),118(R2)      MOVE IN VOLSER
         MVC   2284+54(44,R11),2656(R11)   MOVE IN CATALOG NAME
         LA    R7,36               GET TITLE INDEX
         BAL   R14,IMAP0520
*#*#*# FIX UP 1852(R11) - IF NECESSARY  1852 IS CC
         BAL   R14,IMAP0328        OUTPUT DESCRITION
         TM    2943(R11),X'01'     IS OPT=NOPRINT SPECIFIED
         BO    TITL1               YES, SO SKIP TITLES
         LA    R7,32
         ST    R7,1620(,R11)       SAVE DETAIL TITLE NUMBER
         OI    1620(R11),X'80'     INDICATE 2 LINE TITLE
         BAL   R14,IMAP0500        DUMP OUT DETAIL HEADERS
TITL1    MVI   1852(R11),C'0'      FORCE NEXT TO DOUBLE SPACE
         TITLE ' - READ BCS RECORDS AND SCAN FOR CELL TYPES.'
GETNEXT  L     R1,RPLICAT          PICK UP RPL ADDRESS.
         GET   RPL=(1)             READ A BCS RECORD.
         LTR   R15,R15             RECORD READ OK?
         BZ    GETOK               GO PROCESS IT.
         ABEND 15,DUMP             TOO BAD!
GETOK    L     R3,BUFPTR           ADDR OF CATALOG RECORD
         L     R1,RPLICAT          ADDR OF BCS RPL.
         L     R1,RPLRLEN-IFGRPL(,R1) LENGTH OF RECORD READ
         AR    R1,R3               POINT TO END OF CATALOG RECORD
         ST    R1,ENDBCS           SAVE END OF RECORD.
         LA    R3,2(,R3)           SKIP OVER PREFIX
NEXTCELL MVC   L1DSNAME,=CL44' '   CLEAR DSNAME GARBAGE
         MVC   L1BCSTYP,2(R3)      SAVE TYPE
         CLI   2(R3),C'A'          A = NONVSAM
         BE    NONVSAM
         CLI   2(R3),C'D'          D = DATA
         BE    DATA
         CLI   2(R3),C'I'          I = INDEX
         BE    INDEX
         CLI   2(R3),C'B'          B = GDG BASE
         BE    GDGBASE
         CLI   2(R3),C'H'          H = GDG ENTRY
         BE    GDGENT
         CLI   2(R3),C'C'          C = CLUSTER
         BE    CLUSTER
         CLI   2(R3),C'G'          G = ALTERNATE INDEX
         BE    AIX
         CLI   2(R3),C'R'          R = PATH
         BE    PATH
ENDCELL  DS    0H
         SR    R1,R1               CLEAR REGISTER.
         ICM   R1,3,0(R3)          PICK UP CELL LENGTH
         AR    R3,R1               POINT TO NEXT CELL
         C     R3,ENDBCS           ANY MORE CELLS IN THIS RECORD?
         BL    NEXTCELL            YES, KEEP GOING
         B     GETNEXT             READ ANOTHER RECORD.
         TITLE ' - CLOSE, CLEANUP AND EXIT'
EODICAT  EQU   *                   LAST BCS RECORD READ.
*
**       CLOSE THE CATALOG
*
         L     R2,ACBPTR           POINT AT ACB
         MVI   OPNLST,X'80'        SET END OF LIST INDICATOR
         CLOSE ((R2)),MF=(E,OPNLST)
*
**       DYNAMICALLY DEALLOCATE THE CATALOG
*
         LA    R1,DYRB          SETUP
         ST    R1,PLIST           PLIST POINTER
         OI    PLIST,X'80'      SET END OF LIST INDICATOR
         LA    R1,TXT3          SETUP TEXT
         ST    R1,TXTUP            UNIT ADDRESS
         OI    TXTUP,X'80'      SET END OF LIST INDICATOR
         MVI   TXT3+1,DUNDDNAM  FLAG TO DEALLOCATE DDNAME
         LA    R15,DYRB
         USING S99RB,R15
         LA    R1,TXTUP
         ST    R1,S99TXTPP
         MVI   S99VERB,S99VRBUN SET TO DSN ALLOCATION
         MVI   S99FLAG1,0       SET NO FLAGS
         MVI   S99RBLN,20         SET LENGTH OF BLK
         DROP  R15
         LA    R1,PLIST
         DYNALLOC ,
         LTR   R15,R15          ANY ERROR
         BNZ   BOMB2            ISSUE ERROR MESSAGE
*
**       FREE UP USED STORAGE
*
         LM    R0,R1,RPLICATL      GET LENGTH AND ADDR OF RPL
         FREEMAIN R,LV=(0),A=(1)   FREE IT
         LM    R0,R1,ACBPTRL       GET LENGTH AND ADDR OF ACB
         FREEMAIN R,LV=(0),A=(1)   FREE IT
         L     R0,BUFLEN           LENGTH TO GETMAIN
         L     R1,BUFPTR           GET ADDRESS OF RECORD BUFFER.
         FREEMAIN R,LV=(0),A=(1)   FREE BUFFER
         LA     R0,ICFWKLN         GET LENGTH OF AREA
         LR     R1,R10             POINT TO WORKAREA
         FREEMAIN R,LV=(0),A=(1)   FREE WORKAREA
         SR    R15,R15             SET GOOD RETURN CODE
         B     IMAP02E8            EXIT
         SPACE 3
BOMB1    DS    0H                  OBTAIN FAILED FOR CNAME=
         OI    2942(R11),X'10'
         LA    R7,84               ERROR MESSAGE NUMBER
         BAL   R14,IMAP04B0        BUILD ERROR MESSAGE
         MVC   1862(6,R11),1112(R11)  MOVE IN VOLSER TO ERROR MESSAGE
         MVI   1852(R11),X'60'
         BAL   R14,IMAP0328        OUTPUT LINE
         NI    2942(R11),X'EF'
         NI    2943(R11),X'7F'
         B     IMAP02C8
BOMB2    DS    0H                  DYNAMIC ALLOCATION FAILED
         OI    2942(R11),X'10'
         LA    R7,73*2             ERROR MESSAGE NUMBER
         BAL   R14,IMAP04B0        BUILD ERROR MESSAGE
         LA    R2,DYRB+S99ERROR-S99RB   POINT AT CODES
         BAL   R14,IMAP09A8        CONVERT TO HEX
         MVC   1901(8,R11),2632(R11)  MOVE IN DATA
         BAL   R14,IMAP0328        OUTPUT LINE
         NI    2942(R11),X'EF'
         NI    2943(R11),X'7F'
         B     IMAP02C8
BOMB3    DS    0H                  IEHMAP OUT OF MEMORY
         B     IMAP0088
         TITLE ' - LIST A NONVSAM RECORD.'
         USING NVNC,R3
NONVSAM  DS    0H
         MVC   L1DSNAME,NVNAME     SETUP DSNAME
         MVC   L1TYPE,=C'NONVSAM'  SAY WHAT TYPE IT IS.
         LH    R1,NVOLCNT          GET NUMBER OF VOLUMES
         BAL   R9,DUMPIT
         B     ENDCELL
         DROP  R3
         TITLE ' - LIST A DATA RECORD.'
         USING DINC,R3
DATA     MVC   L1TYPE,=CL7'DATA'   DATA CELL (PARDON THE EXPRESSION)
         B     SKIPDATA            GOTO COMMON FORMATTING LOGIC.
         TITLE ' - LIST A DATA RECORD.'
INDEX    MVC   L1TYPE,=CL7'INDEX'  CALL IT AN INDEX
SKIPDATA SR    R1,R1               CLEAR A REGISTER.
         IC    R1,DIDKEYLN         LENGTH OF INDEX/DATA NAME
         SH    R1,=H'2'            CORRECT LENGTH PLEASE
         EX    R1,DIDMOVE          MOVE COMPONENT NAME FOR PRINT
         LA    R1,1                NUMBER OF VOLUMES ????
         BAL   R9,DUMPIT
         B     ENDCELL
         USING DINC,R3
DIDMOVE  MVC   L1DSNAME(0),DIDKEY  -> (EXECUTED) <-
         DROP  R3
         TITLE ' - LIST A GDG BASE RECORD.'
         USING GDGNC,R3
GDGBASE  DS    0H
         MVC   L1TYPE,=CL7'GDGBASE'  IDENTIFY RECORD TYPE
         MVC   L1DSNAME,GDGNAME    SETUP GDGNAME.
         MVC   GDGSAVE,GDGNAME     SAVE GDGNAME.
         B     ENDCELL
         DROP  R3
         TITLE ' - LIST A GDG ENTRY RECORD.'
         USING GDSNC,R3
GDGENT   DS    0H
         MVC   L1TYPE,=CL7'GDG ENT'  IDENTIFY RECORD TYPE
         MVC   GDGWORK,GDGSAVE     START WITH BASE NAME.
         LA    R1,GDGWORK          GET ADDRESS OF BASE NAME
         LA    R8,44               44 BYTES AT MOST
GDGENT1  CLI   0(R1),C' '          SCAN FOR A BLANK
         BE    GDGENT2             YES.
         LA    R1,1(0,R1)          TRY NEXT CHARACTER.
         BCT   R8,GDGENT1          KEEP LOOKING
GDGENT2  SR    R8,R8               CLEAR A REGISTER.
         ICM   R8,3,GDSGENO        PICK UP GENERATION NUMBER.
         CVD   R8,GWORD            PACK IT.
         UNPK  2(4,R1),GWK3        UNPACK GENERATION NUMBER.
         OI    5(R1),X'F0'         FIX SIGN
         SR    R8,R8               CLEAR A REGISTER.
         ICM   R8,1,GDSVERNO       PICK UP VERSION NUMBER.
         CVD   R8,GWORD            PACK IT.
         UNPK  7(2,R1),GWK2        UNPACK VERSION NUMBER.
         OI    8(R1),X'F0'         FIX SIGN
         MVC   0(2,R1),=C'.G'      SET G0000
         MVI   6(R1),C'V'          G0000V00
         MVC   L1DSNAME,GDGWORK    MOVE IN DEVELOPED NAME
         LH    R1,GDSVOLCT         GET NUMBER OF VOLUMES
         BAL   R9,DUMPIT
         B     ENDCELL
         TITLE ' - LIST A CLUSTER RECORD.'
         USING VCNC,R3
CLUSTER  DS    0H
         MVC   L1DSNAME,CLNAME     SETUP CLUSTER NAME.
         MVC   L1TYPE,=CL7'CLUSTER'
         OC    L1DSNAME,L1DSNAME   SEE IF SELF DEFINING NAME
         BNE   CLUS1
         MVC   L1DSNAME,2656(R11)  FORCE TO REAL NAME
CLUS1    DS    0H
         LA    R1,0                NO VOLUME ENTRIES
         BAL   R9,DUMPIT
         B     ENDCELL
         DROP  R3
         TITLE ' - LIST AN ALTERNATE INDEX RECORD.'
         USING ANC,R3
AIX      DS    0H
         MVC   L1TYPE,=CL7'AIX'    CALL IT AN AIX.
         SR    R1,R1               CLEAR A REGISTER.
         ICM   R1,1,AIXKEYLN       PICK UP AIX NAME LENGTH
         SH    R1,=H'2'            CORRECT LENGTH.
         EX    R1,AIX1             MOVE AIX NAME TO PRINT LINE.
         LA    R1,0                NO VOLUME ENTRIES
         BAL   R9,DUMPIT
         B     ENDCELL
AIX1     MVC   L1DSNAME(0),AIXKEY  EXECUTED
         TITLE ' - LIST A PATH RECORD.'
         USING PNC,R3
PATH     DS    0H
         MVC   L1TYPE,=CL7'PATH'   CALL IT A PATH.
         SR    R1,R1               CLEAR A REGISTER.
         ICM   R1,1,PATHKYLN       PICK UP PATH NAME LENGTH
         SH    R1,=H'2'            CORRECT LENGTH
         EX    R1,PATH1            MOVE PATH NAME TO PRINT LINE.
         LA    R1,0                NO VOLUME ENTRIES
         BAL   R9,DUMPIT
         B     ENDCELL
PATH1    MVC   L1DSNAME(0),PATHNAME  EXECUTED
         TITLE ' - FINDBCS. ADVANCE R3 TO A SPECIFIED BCS CELL.'
* I ENTER HERE WITH R3 POINTING TO A CELL I HAVE JUST PROCESSED. THIS
* CODE EITHER MOVES AHEAD TO THE NEXT CELL BE IT WHATEVER OR SEARCHES
* FOR A PARTICULAR CELL TYPE.
* ON ENTRY THE NEXT-TO-RIGHT-MOST BYTE OF R0 CONTAINS X'FF' IF THE
* CELL TYPE THE CALLER WANTS IS OPTIONAL (IT'S EITHER THE NEXT ONE OR
* IT'S NOT THERE AT ALL) OR X'00' IF THE DESIRED CELL TYPE IS REQUIRED
* BUT MAY NOT BE THE NEXT ONE.
* THE RIGHT-MOST BYTE OF R0 CONTAINS THE ID OF THE CELL TYPE THE
* CALLER WANTS.
* IF R0 IS ALL ZEROS THE CALLER JUST WANTS THE NEXT CELL.
* IF THE REQUESTED CELL TYPE WAS FOUND R0 IS CLEARED.
FINDBCS  DS    0H
         ST    R3,BCS2             SAVE CELL ADDRESS.
FINDBCS1 SR    R1,R1               CLEAR REGISTER
         ICM   R1,3,0(R3)          PICK UP LENGTH OF CURRENT CELL.
         AR    R3,R1               AND SKIP OVER IT.
         C     R3,ENDBCS           ANY MORE CELLS IN THIS RECORD?
         BL    FINDBCS2            YES.
         ST    R0,BCS0             SAVE CALLERS PARMS
         L     R1,RPLICAT          GET RPL ADDR.
         GET   RPL=(1)
         L     R0,BCS0             RESTORE CALLERS PARMS
         LTR   R15,R15             RECORD READ OK?
         L     R3,BUFPTR           ADDRESS OF RECORD JUST READ.
         L     R1,RPLICAT          GET RPL ADDR.
         L     R1,48(0,R1)         RECORD LENGTH.
         AR    R1,R3               END OF RECORD.
         ST    R1,ENDBCS           SAVE FOR NEXT TIME.
         LA    R3,2(0,R3)          SKIP OVER PREFIX.
FINDBCS2 CLM   R0,1,2(R3)          CHECK FOR REQUESTED CELL TYPE
         BE    FINDBCS9            YES.
         CLM   R0,2,=XL1'FF'       IS THIS CELL TYPE OPTIONAL?
         BNE   FINDBCS1            NO, SO KEEP LOOKING.
         L     R3,BCS2             BACKUP TO WHERE WE STARTED.
         SR    R0,R0               LET HIM KNOW ABOUT IT.
FINDBCS9 BR    R9
         TITLE ' - PROCESS EXTRACTED CATALOG ENTRY'
*
**       THIS ROUTINE INTERFACES THE ICF/VSAM CATALOG READ CODE
**       TO THE REST OF IEHMAP. IT HANDLES TWO COMPLETELY DIFFERENT
**       CASES:
**       1.  THE DEPT, SPACE, AND USERID CARDS WHICH NEED ONLY
**           SPACE ALLOCATING ENTRIES PASSED TO THEM
**       2.  THE CATLG CARD, WHICH PROCESSES ALL CATALOG ENTRIES
**
**   INPUT
**       R1  -  NUMBER OF VOLUME ENTRIES TO PROCESS
**       R3  -  POINTS TO CURRENT BCS ENTRY
**       R9  -  RETURN ADDRESS
*
DUMPIT   DS    0H
*
**       BLANK PAD DSNAME, AND CHECK FOR STARTING NAME
*
         OC    L1DSNAME,=CL44' '   BLANK PAD DSNAME
         CLC   L1DSNAME(44),2800(R11)   CHECK FOR STARTING NAME
         BLR   R9                  OUT IF NOT
*
**       SEE IF DSN VS. VOLUME PROCESSING NEEDED (DEPT, SPACE, AND
**       USERID CARDS).
*
         TM    2943(R11),X'80'     IS DSN VS. VOLUME PROCESSING WANTED
         BZ    DPTCAT              NOPE, THEN MUST BE CATLG CARD.
*
**       FOR DSN VS. VOLUME PROCESSING, CERTAIN ENTRIES CAN BE IGNORED
*
         CLI   L1BCSTYP,C'C'       C = CLUSTER         --|
         BER   R9                                        | NOT USED
         CLI   L1BCSTYP,C'G'       G = ALTERNATE INDEX   |   IN DSN
         BER   R9                                        |     VS.
         CLI   L1BCSTYP,C'R'       R = PATH              |      VOLUME
         BER   R9                                      --|
         SPACE 2
*
**       SCAN FOR THE VOLUME CELL
*
         STM   R0,R15,DPTSAVE      SAVE REGISTERS
         LTR   R1,R1               CHECK COUNT
         BNP   DUMPIT5
         ICM   R0,1,=XL1'04'       VOLUME CELL TO BE FOUND.
         BAL   R9,FINDBCS          SO GO LOOK FOR IT.
         DROP  R3
         USING VOLCELL,R3
         MVC   L1VOLUME,VOLSERN    SETUP VOLUME SERIAL NUMBER
         MVC   L1DEVTYP,VOLDEVTP   SET UP DEVICE TYPE IN PRINT LINE
         MVC   L1FILSEQ,VOLFLSEQ   SAVE FILE SEQUENCE NUMBER
         B     DUMPIT6
DUMPIT5  DS    0H
         MVC   L1VOLUME,=C'UNKNWN'   SET VOL SERIAL NUMBER
         XC    L1DEVTYP,L1DEVTYP   SET UP DEVICE TYPE IN PRINT LINE
         XC    L1FILSEQ,L1FILSEQ   SAVE FILE SEQUENCE NUMBER
DUMPIT6  DS    0H
*
**       CREATE IEHMAP DATASET NAME BLOCK, CHAIN IT PROPERLY TO
**       THE DSN CHAIN, AND FILL IN THE BLOCK PROPERLY
*
         ST    R3,BCS2             SAVE IT
         LA    R4,200              SIZE OF BLOCK TO ALLOCATE
         BAL   R14,IMAP0100        GET A BLOCK
         B     BOMB3               +0 GET FAILED
*                                  +4 ALL OK
         LR    R8,R3               SAVE ADDRESS
         MVC   0(44,R8),L1DSNAME   MOVE IN DSNAME
         MVC   184(6,R8),L1VOLUME  MOVE IN VOLUME SERIAL
         MVC   192(4,R8),L1DEVTYP  MOVE IN CATALOG DEVICE TYPE
*
**       SEPERATE OUT THE FIRST INDEX LEVEL AS BLANK PADDED 8 CHAR
*
         MVC   160(8,R8),L1DSNAME  MOVE IN FIRST 8 BYTES
         LA    R0,8                SET COUNT
         LA    R1,160(,R8)         POINT AT STRING
DUMPIT2  CLI   0(R1),C'.'          END OF INDEX?
         BE    DUMPIT3             FOUND, NOW BLANK REST
         CLI   0(R1),C' '          END OF DSN?
         BE    DUMPIT3             FOUND, NOW BLANK REST
         LA    R1,1(,R1)           INCREMENT POINTER
         BCT   R0,DUMPIT2          LOOP TILL ALL 8 PAST
         B     DUMPIT4             DONE
DUMPIT3  MVI   0(R1),C' '          BLANK OUT REST
         LA    R1,1(,R1)           INCREMENT POINTER
         BCT   R0,DUMPIT3          LOOP TILL ALL DONE
DUMPIT4  DS    0H
*
**       NOW CHAIN NEW BLOCK ON DSN BLOCK CHAIN
*
         L     R3,TAIL             IS MY TAIL
         LTR   R3,R3                    POINTER SET
         BZ    DUMPIT1             NOPE, SO GO AND SET IT
         ST    R8,TAIL             SET NEW TAIL POINTER
         MVC   152(4,R8),152(R3)   INSERT INTO
         ST    R8,152(,R3)            CURRENT CHAIN
         LM    R0,R15,DPTSAVE      RESTORE REGISTERS
         L     R3,BCS2             RESTORE GOOD BCS PTR
         BR    R9                  ALL DONE
DUMPIT1  L     R3,2900(,R11)       MAKE CURRENT
         ST    R3,152(,R8)            FIRST ENTRY (X'FFFF'S)
         ST    R8,2900(,R11)             THE LAST
         ST    R8,TAIL                     ENTRY AND SAVE NEW TAIL
         LM    R0,R15,DPTSAVE      RESTORE REGISTERS
         L     R3,BCS2             RESTORE GOOD BCS PTR
         BR    R9
         EJECT
*
**       THIS CODE WILL FORMAT A CATALOG ENTRY AND PRINT THE LINE
**       USING IEHMAP SERVICES
*
DPTCAT   DS    0H
         STM   R0,R15,DPTSAVE
         ST    R3,BCS2             SAVE IT
         LA    R7,1853(,R11)       POINT AT START OF LINE
         MVC   0(44,R7),L1DSNAME   SET DSNAME
         MVC   48(7,R7),L1TYPE     SET ENTRY TYPE
         CLI   L1BCSTYP,C'C'       C = CLUSTER         --|
         BE    DPTCAT9                                   | DO NOT
         CLI   L1BCSTYP,C'G'       G = ALTERNATE INDEX   |   HAVE
         BE    DPTCAT9                                   |     VOLUME
         CLI   L1BCSTYP,C'R'       R = PATH              |      ENTRIES
         BE    DPTCAT9                                 --|
*
**       SCAN FOR THE VOLUME CELL
*
         LTR   R5,R1               GET NUMBER TO DO
         BNP   DPTCAT9
         LA    R4,2                NUMBER PER LINE
*#*#*# FORMAT VOLUME COUNT (R5)
         LA    R7,1853+65(,R11)    POINT AT START OF LINE
*#*#*#*   PROCESS VOLUME ENTRIES
*  +58   5    VOLUME COUNT
*  +65   34   VOLUME ENTRY - 2 ENTRIES ON LINE
*          +0   6   VOLSER
*          +8   8   FORMATTED DEVICE TYPE
*          +17 10   DEVICE NAME (CATDEV)
*          +28  4   SEQ NUMBER
*#*#*#*   PROCESS VOLUME ENTRIES
DPTCAT1  DS    0H                  TOP OF LOOP
         L     R3,BCS2             SAVE IT
         ICM   R0,1,=XL1'04'       VOLUME CELL TO BE FOUND.
         BAL   R9,FINDBCS          SO GO LOOK FOR IT.
         ST    R3,BCS2             SAVE IT
         USING VOLCELL,R3
         MVC   L1VOLUME,VOLSERN    SETUP VOLUME SERIAL NUMBER
         MVC   L1DEVTYP,VOLDEVTP   SET UP DEVICE TYPE IN PRINT LINE
         MVC   L1FILSEQ,VOLFLSEQ   SAVE FILE SEQUENCE NUMBER
         DROP  R3
         MVC   0(6,R7),L1VOLUME    MOVE IN VOLSER
         LA    R2,L1DEVTYP         POINT AT DEV TYPE
         BAL   R14,IMAP09A8        CONVERT TO HEX
         MVC   8(8,R7),2632(R11)   MOVE IN CONVERTED DATA
         L     R1,L1DEVTYP         GET DEVICE TYPE
         CLI   L1DEVTYP+2,X'20'    IS IT DASD?
         BNE   DPTCAT1A            NO SO USE CATALOG TABLE
         L     R15,AMAPDEV         TRY MAPDEV CODE
         BALR  R14,R15             TRY CODE
         USING MAPDEVD,R1
         MVC   17(10,R7),MDDEVNAM  MOVE IN DEVICE NAME
         CLI   MDDEVTYP,X'FF'      IS IT UNKNOWN?
         BNE   DPTCAT1B            NO, IT IS GOOD SO CONTINUE
         DROP  R1
DPTCAT1A DS    0H
         L     R15,=V(CATDEV)      POINT TO ENTRY POINT
         BALR  R14,R15             GET VALUE
         MVC   17(10,R7),4(R1)     MOVE IN DEVICE NAME
DPTCAT1B DS    0H
         LA    R2,L1FILSEQ         POINT AT FILE SEQUENCE NUMBER
         BAL   R14,IMAP0700        CONVERT TO DECIMAL
         MVC   28(4,R7),2616(R11)  MOVE IN ZERO PADDED NUMBER
         LA    R7,34(,R7)          POINT TO NEXT ENTRY IN LINE
         BCT   R5,DPTCAT2          SEE IF ANY LEFT TO DO
         B     DPTCAT9             IF NONE LEFT TO DO THEN EXIT
DPTCAT2  BCT   R4,DPTCAT1          SEE IF ANY SPACE ON LINE
         LA    R4,2                NEED TO DUMP LINE, RESET COUNT
         LA    R7,1853+65(,R11)    POINT AT START OF LINE
         TM    2943(R11),X'01'     IS OPT=NOPRINT SPECIFIED
         BO    DPTCAT1             YES SO SKIP
         BAL   R14,IMAP0328        OUTPUT LINE
         B     DPTCAT1             GO AND DO NEXT
DPTCAT9  DS    0H
         LM    R0,R15,DPTSAVE
         L     R3,BCS2             RESTORE IT
         TM    2943(R11),X'01'     IS OPT=NOPRINT SPECIFIED
         BOR   R9                  YES SO SKIP
         BAL   R14,IMAP0328        OUTPUT LINE
         BR    R9
         TITLE ' -  CONSTANTS AREA'
         LTORG ,
         DS    0F
BUFLEN   DC    AL4(1024*128)       128K RECORD BUFFER
EXLICAT  EXLST EODAD=EODICAT
RTXT1    DC    AL2(DALDSNAM,1,44),CL44' '     DATASET NAME
         DC    AL2(DALSTATS,1,1),X'08'        DISP=OLD
         DC    AL2(DALRTDDN,1,8),CL8' '       RETURN DDNAME
TXTLEN   EQU   *-RTXT1
TITLES   DC    CL40'CATALOG LISTING FOR VOLUME "D06DB0" DSN='
         TITLE ' - ICF CATALOG WORKAREA '
ICFWK    DSECT
DPTSAVE DS     18F
GWORD    DS    D
GWK2     EQU   GWORD+6,2
GWK3     EQU   GWORD+5,3
DPTCATZ  DS    F                   RETURN ADDRESS FOR DUMPIT
TAIL     DS    F                   POINTER TO DSN BLOCK
OPENERR  DS    F                   OPEN ERROR CODE.
BUFPTR   DS    F                   POINTER TO BUFFER
RPLICATL DS    F                   LENGTH OF RPL
RPLICAT  DS    F                   MOVE MODE RPL FOR BCS ACCESS.
ACBPTRL  DS    F                   LENGTH OF ACB
ACBPTR   DS    F                   ADDRESS OF ACB
OPNLST   DS    F                   AREA FOR OPEN/CLOSE LIST
ENDBCS   DC    F'0'                BCS RECORD END ADDRESS
BCS9     DC    F'0'                SAVE R9 IN HERE.
BCS2     DC    F'0'                SAVE R3 IN HERE.
BCS0     DC    F'0'                SAVE R0 IN HERE.
GENPLST  DS    F                   GENCB PLIST
         ORG   GENPLST+GPLLEN1     SIZE OF PLIST FOR GENCB ACB
         DS    0C
         ORG   GENPLST+GPLLEN2     SIZE OF PLIST FOR GENCB RPL
         DS    0C
         ORG   GENPLST+GPLLEN3     SIZE OF PLIST FOR SHOWCB  ERROR
         DS    0C
         ORG   ,
         DS    0F
GDGSAVE  DS    CL44
GDGWORK  DS    CL44
L1DSNAME DS    CL44
L1TYPE   DS    CL7
L1BCSTYP DS    CL1
L1DEVTYP DS    CL4
L1VOLUME DS    CL6
L1FILSEQ DS    XL2
PLIST    DS    F
DYRB     DS    XL(S99RBEND-S99RB)
TXTUP    DS    4F
TXT1     DS    AL2(DALDSNAM,1,44),CL44' '     DATASET NAME
TXT2     DS    AL2(DALSTATS,1,1),X'08'        DISP=OLD
TXT3     DS    AL2(DALRTDDN,1,8),CL8' '       RETURN DDNAME
ICFWKLN  EQU   *-ICFWK
         TITLE 'DSECTS FOR CATALOG CELLS.'
VVDSTAB  DSECT ,
VVDSVOL  DS    CL6                 VOLSER OF A VOLUME WITH A VVDS
VVDSACB  DS    0F,AL4              ADDRESS OF A GENERATED ACB
VVDSRPL  DS    AL4                 ADDRESS OF A GENERATED RPL
VVDSBUF  DS    AL4                 ADDRESS OF A RECORD BUFFER
VVDSARG  DS    AL4                 ADDRESS OF RBA TO READ THE VVDS
VVDSFLG  DS    0CL1                INDEXED VTOC FLAG               @131
         DS    AL4                                                 @131
VVDSTLEN EQU   *-VVDSTAB           LENGTH OF AN ENTRY
         SPACE 2
VCNC     DSECT                     VSAM CLUSTER NAME CELL
CLCELLN  DS    XL2                 LENGTH OF VCNC INCLUDING ITSELF
CLTYPE   DS    CL1                 TYPE 'C' FOR CLUSTER
CLCOMPLN DS    XL2                 LENGTH OF THE CLUSTER COMPONENT
CLNOEXT  DS    XL1                 NUMBER OF EXTENSION RECORDS
CLNMLEN  DS    XL1                 LENGTH OF THE CLUSTER KEY (45)
CLNAME   DS    CL44                CLUSTER NAME
CLNMPAD  DS    XL1                 PAD VALUE, SET TO X'00'
         SPACE 2
DINC     DSECT                     DATA OR INDEX NAME CELL
DIDLEN   DS    XL2                 LENGTH OF DINC
DIDTYPE  DS    CL1                 TYPE 'D' FOR DATA OR INDEX
DIDCMPLN DS    XL2                 LENGTH OF DINC COMPONENT
DIDIRFLG DS    XL1                 INTERRUPT RECOGNITION FLAGS
DIDELETE EQU   B'1000000'          DELETE IN PROGRESS
DIDUPDAT EQU   B'0100000'          UPDATE-EXTEND IN PROGRESS
DIDSRMU  EQU   B'0010000'          SUB-RECORD MOVE/UPDATE IN PROGRESS
DIDKEYLN DS    XL1                 CONDENSED KEYLENGTH
DIDKEY   DS    0XL1                CONDENSED KEY
         SPACE 2
ANC      DSECT                     ALTERNAME INDEX CELL
AIXLEN   DS    XL2                 LENGTH OF ANC
AIXTYPE  DS    CL1                 TYPE 'G' FOR ANC
AIXCMPLN DS    XL2                 LENGTH OF AIX COMPONENT
AIXFLAG  DS    XL1                 FLAG BYTE
AIXUPG   EQU   B'1000000'          AIX IS MEMBER OF UPGRADE SET
AIXKEYLN DS    XL1                 CONDENSED KEYLENGTH
AIXKEY   DS    0XL1                CONDENSED KEY 2-45 BYTES
         SPACE 2
PNC      DSECT                     PATH NAME CELL
PATHCELN DS    XL2                 LENGTH OF PATH CELL
PATHTYPE DS    CL1                 TYPE 'R' FOR PATH
         DS    XL2
PATHATTR DS    XL1                 PATH ATTRIBUTES
PATHUPG  EQU   B'1000000'          UPGRADE IF RELATED TO AIX
PATHUPD  EQU   B'1000000'          UPDATE IF RELATED TO CLUSTER.
PATHREL  EQU   B'0010000'          0=RELATED TO AIX,1=RELATED TO
*                                  CLUSTER
PATHKYLN DS    XL1                 LENGTH OF PATH KEY
PATHNAME DS    CL44                PATH NAME.
         SPACE 2
NVNC     DSECT                     NONVSAM DATASET CELL
NVCELEN  DS    XL2                 LENGTH OF NVNC
NVTYPE   DS    CL1                 TYPE 'A' FOR NONVSAM
NVRESV   DS    XL1                 RESERVED
NVOLCNT  DS    XL2                 NUMBER OF VOLUME CELLS (255 MAX)
NVKEYLN  DS    XL1                 LENGTH OF THE NONVSAM KEY
NVNAME   DS    CL44                DATASET NAME
NVPAD    DS    XL1                 PAD VALUE X'00'
         SPACE 2
OWNERC   DSECT                     OWNERSHIP CELL FORMAT.
OWNCELLN DS    XL2                 LENGTH OF OWNERSHIP CELL
OWNTYPE  DS    XL1                 TYPE X'01' FOR OWNER
OWNID    DS    XL8                 OWNER ID
OWNFLAG  DS    XL1                 FLAG
OWNRACF  EQU   B'10000000'         RACF PROTECTED
OWNKSDS  EQU   B'01000000'         INDEX COMPONENT DATASET
OWNREUS  EQU   B'00100000'         REUSABLE DATASET.
OWNERASE EQU   B'00010000'         ERASE
OWNSWAP  EQU   B'00000010'         SWAPSPACE
OWNPAGE  EQU   B'00000001'         PAGESPACE
OWNCREDT DS    XL3                 CREATION DATE
OWNEXPDT DS    XL3                 EXPIRY DATE
         SPACE 2
SECURITY DSECT                     SECURITY CELL
SECELLN  DS    XL2                 LENGTH OF SECURITY CELL
SECTYPE  DS    XL1                 TYPE X'02' FOR SECURITY.
SECMSTR  DS    CL8                 MASTER PASSWORD.
SECCI    DS    CL8                 CONTROL INTERVAL PASSWORD.
SECUPDTE DS    CL8                 UPDATE PASSWORD.
SECREAD  DS    CL8                 READ PASSWORD.
SECPMTCD DS    CL8                 PASSWORD PROMPTING CODE.
SECATMP  DS    XL2                 MAXIMUM NUMBER OF ATTEMPTS.
SECVRMOD DS    CL8                 USER SECURITY VERIFICATION MOD.
SECRCDLN DS    XL2                 USER AUTHORIZATION RECORD LENGTH.
SECRCD   DS    0XL1                USER AUTHORIZATION RECORD (256 MAX)
         SPACE 2
ASSOCC   DSECT                     ASSOCIATION CELL
ASCLEN   DS    XL2                 ASSOCIATION CELL LENGTH
ASCTYPE  DS    XL1                 TYPE X'03' FOR ASSOC
ASCOUNT  DS    XL2                 COUNT OF ASSOCIATIONS
* THE FOLLOWING FIELDS ARE REPEATED THE NUMBER OF TIMES IN ASCOUNT
ASCKEYS  DS    0XL2
ASCFLAG  DS    XL1                 FLAG
ASCREUS  EQU   B'10000000'         USED IN RELATION CELL ONLY.
ASCKYLEN DS    XL1                 LENGTH OF CONDENSED ASSOC KEY.
ASCKEY   DS    0XL1                2-45 BYTE CONDENSED KEY.
         SPACE 2
GDGNC    DSECT                     GDG BASE NAME CELL
GDGCELLN DS    XL2                 LENGTH OF GDGNC INCLUDING ITSELF
GDGTYPE  DS    CL1                 TYPE 'B' FOR GDG
GDGCMPLN DS    XL2                 LENGTH OF THE GDG COMPONENT
GDGNOEXT DS    XL1                 NUMBER OF EXTENSION RECORDS
GDGKYLEN DS    XL1                 LENGTH OF THE GDG
GDGNAME  DS    CL44                GDG NAME 1-35 BYTES BLANK PADDED
GDGPAD   DS    XL1                 PAD VALUE, SET TO X'00'
         SPACE 2
GDSNC    DSECT                     GDG ENTRY NAME CELL
GDSCELLN DS    XL2                 LENGTH OF GDSNC INCLUDING ITSELF
GDSTYPE  DS    CL1                 TYPE 'H' FOR GDS
GDSCMPLN DS    XL2                 LENGTH OF THE GDS COMPONENT
GDSGENO  DS    XL2                 GENERATION NUMBER
GDSVERNO DS    XL1                 VERSION NUMBER
GDSVOLCT DS    XL2                 NUMBER OF VOLUME CELLS IN GDS
         SPACE 2
VOLCELL  DSECT                     VOLUME CELL
VOLLEN   DS    XL2                 LENGTH OF VOLCELL
VOLTYPE  DS    XL1                 TYPE X'04' FOR VOLUME CELL
VOLOFSET DS    XL1                 UNUSED
VOLSERN  DS    CL6                 VOLUME SERIAL NUMBER
VOLDEVTP DS    XL4                 DEVICE TYPE
VOLFLAG1 DS    XL1                 FLAG
VOLPRIME EQU   B'10000000'         PRIME VOLUME-ALLOCATED SPACE
VOLCANDI EQU   B'01000000'         CANDIDATE VOLUME NOSPACE
VOLOFLOW EQU   B'00100000'         OVERFLOW KEYRANGE ONLY
VOLCONV  EQU   B'00010000'         CONVERTED VSAM DATASET VOLUME
VOLNVSAM EQU   B'00001000'         NONVSAM VOLUME CELL
VOLKYRNG EQU   B'00000100'         KEYRANGE QUALIFIER PRESENT
VOLVPCI  EQU   B'00000010'         PRIMARY VVR CI
VOLSSQWD EQU   B'00000001'         SEQUENCE SET WITH DATA
VOLFLAG2 DS    XL1                 RESERVED
VOLVVRBA DS    0XL4                RELATIVE BYTE ADDRESS OF VVR
VOLDSTTR DS    XL3                 NONVSAM FORMAT 1 DSCB TTR
         DS    XL1                 RESERVED
VOLFLSEQ DS    XL2                 NONVSAM FILE SEQUENCE NUMBER (TAPE)
VOLKRQL  DS    XL4                 KEYRANGE QUALIFIER (VSAM ONLY)
VOLLKYLN DS    XL2                 LENGTH OF LOW KEY RANGE KEY
         SPACE 2
RELCELL  DSECT                     RELATIONSHIP CELL.
RELLEN   DS    XL2                 CELL LENGTH.
RELTYPE  DS    XL1                 TYPE X'06'
RELRESV  DS    XL1                 RESERVED.
RELCNT   DS    XL1                 COUNT OF RELKEYS.
RELKEYS  DS    0XL2
RELFLAG  DS    XL1                 FLAG BYTE
RELUPG   EQU   B'10000000'         UPGRADE AIX.
RELGLEN  DS    XL1                 LENGTH OF AIX CONDENSED KEY.
RELGKEY  DS    0XL1                AIX CONDENSED KEY.
         TITLE ' - DSECTS FOR SYSTEM DATA AREAS.'
         PRINT NOGEN
         IEFZB4D0
         IEFZB4D2
DSCB     DSECT
         IECSDSL1 1
         IHAPSA ,
         IEZJSCB ,
         IKJTCB  DSECT=YES
         IFGRPL  ,
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         COPY  EXTREFS
         COPY  WORKAREA
         END
