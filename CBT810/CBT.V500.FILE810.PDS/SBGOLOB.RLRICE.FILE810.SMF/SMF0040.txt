*---------------------------------------------------------------------*
*                                                                     *
*         Format SMF record type 40                                   *
*                                                                     *
*                                                                     *
*   At entry                                                          *
*       R15    program base address                                   *
*       R14    return address                                         *
*       R9     OSSPFD address                                         *
*       R8     VDATA buffer address                                   *
*       R7     SMF record address +4 (does not include length)        *
*                                                                     *
* ------------------------------------------------------------------- *
SMF0040  CSECT
SMF0040  AMODE 31
SMF0040  RMODE ANY
         USING SESSION,R11
         USING SMF0040,R15
         B     INIT0000
MODID    DC    CL8'SMF0040'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         USING OSCOMM,R12                     DEFINE COMMON AREA BASE
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT
         USING SMF0040,R10                    DEFINE BASE
         DROP  R15
         L     R1,SESS_DXD_ADDR
         A     R1,DXDSTART
         ST    R13,4(R1)                      SAVE R13
         ST    R1,8(R13)                      CHAIN SAVE AREA
         LR    R13,R1                         COPY WORK AREA ADDRESS
         USING WK0040,R13                     DEFINE BASE
         USING OSSPFD,R9                      DEFINE BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
         USING TDDRFLG,R7
         MVC   0(MSG_01_L,R8),MSG_01          COPY MESSAGE
         USING MSG_01,R8                      DEFINE BASE
         MVC   MSG_01_UIF,TDDUIF              COPY JOB
         MVC   MSG_01_UDATA,TDDUDATA          COPY USER ID
         SR    R1,R1                          CLEAR REGISTER
         IC    R1,TDDINVSQ                    STEP NUMBER
         CVD   R1,COMM_DWORD                  CONVERT TO DECIMAL
         ED    MSG_01_INVSQ,COMM_DWORD+6
         CLI   TDDFLG,2                       UNALLOCATION?
         BE    MAIN0010                       YES
         CLI   TDDFLG,3                       CONCATENATION?
         BE    MAIN0020                       YES
         CLI   TDDFLG,4                       DE-CONCATENATION?
         BE    MAIN0030                       YES
MAIN0010 DS    0H
         MVC   MSG_01_FLG(L'UNALLOCATION),UNALLOCATION
         B     MAIN0040
MAIN0020 DS    0H
         MVC   MSG_01_FLG(L'CONCATENATION),CONCATENATION
         B     MAIN0040
MAIN0030 DS    0H
         MVC   MSG_01_FLG(L'DECONCATENATION),DECONCATENATION
MAIN0040 DS    0H
         DROP  R8
         A     R8,SPF_VWIDTH                  NEXT LINE
         BCTR  R6,R0

         A     R8,SPF_VWIDTH                  NEXT LINE
         BCTR  R6,R0

         SR    R5,R5
         ICM   R5,3,TDDVAR                    SIZE OF DEVICE DATA
         BZ    EXIT0000                       NO DATA
         SH    R5,H2                          MINUS 2
         BZ    EXIT0000                       NO DATA
         SRL   R5,3                           DIVIDE BY 8 (CHEAP WAY)
         LA    R7,TDDDEVC                     FIRST DEVICE
         USING TDDDEVC,R7
MAIN0050 DS    0H
         ITRACE ID=BLD,                                                +
               RDATA1=R8,                                              +
               RDATA2=R6
         MVC   0(MSG_02_L,R8),MSG_02          COPY MESSAGE
         USING MSG_02,R8                      DEFINE BASE
         UNPK  WORK_A(5),TDDCUAD(3)           UNPACK DEVICE ADDRESS
         TR    WORK_A(4),COMM_HEXCHAR         TRANSLATE
         MVC   MSG_02_CUAD,WORK_A             COPY DEVICE ADDRESS
         UNPK  WORK_A(3),TDDDEVC(2)           UNPACK DEVICE CLASS
         TR    WORK_A(2),COMM_HEXCHAR         TRANSLATE
         MVC   MSG_02_DEVC,WORK_A             COPY DEVICE CLASS
         UNPK  WORK_A(3),TDDUTYP(2)           UNPACK DEVICE TYPE
         TR    WORK_A(2),COMM_HEXCHAR         TRANSLATE
         MVC   MSG_02_UTYP,WORK_A             COPY DEVICE TYPE
         ICM   R1,15,TDDNEXCP                 NUMBER OF EXCP'S
         CVD   R1,COMM_DWORD                  CONVERT TO DECIMAL
         ED    MSG_02_EXCP,COMM_DWORD+3
         A     R8,SPF_VWIDTH                  NEXT LINE
         BCT   R6,MAIN0060
         B     EXIT0000                       SCREEN FULL
MAIN0060 DS    0H
         LA    R7,8(,R7)                      NEXT DEVICE
         BCT   R5,MAIN0050                    LOOP
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
EXIT0000 DS    0H
         SR    R2,R2
         B     EXITEXIT
EXIT0004 DS    0H
         LA    R2,4
EXITEXIT DS    0H
         ITRACE ID=EXIT,                                               +
               RDATA1=R2
         L     R13,4(R13)                     RESTORE SAVE AREA ADDRESS
         L     R14,12(,R13)                   RESTORE R14
         SR    R15,R15                        SET RC
         LM    R0,R12,20(R13)                 RESTORE REGISTERS
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDSTART       DC    Q(WK0040)
H2             DC    H'2'
UNALLOCATION    DC   C'UNALLOCATION'
CONCATENATION   DC   C'CONCATENATION'
DECONCATENATION DC   C'DE-CONCATENATION'

MSG_01         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Job name'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_01_UIF     DC    CL8' '
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C' User id'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_01_UDATA   DC    CL8' '
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C' Step number'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_01_INVSQ   DC    X'40202120'
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C' Function'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_01_FLG     DC    CL20' '
MSG_01_L       EQU   *-MSG_01

MSG_02         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Device'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_02_CUAD    DC    CL4' '
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C' Type'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_02_UTYP    DC    CL2' '
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C' Class'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_02_DEVC    DC    CL2' '
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C' EXCP''s'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_02_EXCP    DC    X'402020206B2020206B202120'
MSG_02_L       EQU   *-MSG_02


* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
WK0040         DSECT
               COPY     DXDPREF
WORK_A         DS       CL8
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               SESSION
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
SMF_RECORD     DSECT
               IFASMFR  40
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     REGEQU
               END      SMF0040
