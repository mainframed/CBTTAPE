*---------------------------------------------------------------------*
*                                                                     *
*         Format SMF record type 42 (hex 2A)                          *
*         DFP statistics and configuration                            *
*                                                                     *
*   At entry                                                          *
*       R15    program base address                                   *
*       R14    return address                                         *
*       R9     OSSPFD address                                         *
*       R8     VDATA buffer address                                   *
*       R7     SMF record address +4 (does not include length)        *
*                                                                     *
* ------------------------------------------------------------------- *
SMF0042  CSECT
SMF0042  AMODE 31
SMF0042  RMODE ANY
         USING SMF0042,R15
         B     INIT0000
MODID    DC    CL8'SMF0042'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         USING OSCOMM,R12                     DEFINE COMMON AREA BASE
         USING SESSION,R11
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT
         USING SMF0042,R10                    DEFINE BASE
         DROP  R15
         L     R1,SESS_DXD_ADDR
         A     R1,DXDSTART
         ST    R13,4(R1)                      SAVE R13
         ST    R1,8(R13)                      CHAIN SAVE AREA
         LR    R13,R1                         COPY WORK AREA ADDRESS
         USING WK0042,R13                     DEFINE BASE
         USING OSSPFD,R9                      DEFINE BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
         USING SMF42FLG,R7
         MVC   0(MSG_01_L,R8),MSG_01          COPY TO BUFFER
         USING MSG_01,R8                      DEFINE BASE
         MVC   MSG_01_SSI,SMF42SSI            COPY SUBSYSTEM ID
         MVC   MSG_01_STY,SMF42STY+1          COPY RECORD SUB TYPE
         OI    MSG_01_STY,X'F0'               MAKE IT PRINTABLE
         DROP  R8
         BAL   R14,LINE0000                   NEXT LINE
         CLI   SMF42STY+1,X'01'               SUBTYPE 01?
         BE    T0010000                       YES
         CLI   SMF42STY+1,X'06'               SUBTYPE 06?
         BE    T0060000                       YES
         B     EXIT0000
*---------------------------------------------------------------------*
*        Subtype 01 (BMF statistics)                                  *
*---------------------------------------------------------------------*
T0010000 DS    0H
         ITRACE ID=TYPE001
         USING SMF42S1,SMF42OPS
         L     R5,SMF42BMO                    OFFSET TO BMF STATISTICS
         AR    R5,R7                          PLUS ADDRESS OF BASE
         SH    R5,H4                          MINUS 4
         USING SMF4201A,R5                    DEFINE BASE
         MVC   0(MSG_02_L,R8),MSG_02
         USING MSG_02,R8                      DEFINE BASE
         L     R1,SMF42TNA                    NUMBER OF STORAGE CLASSES
         CVD   R1,COMM_DWORD                  CONVERT TO DECIMAL
         ED    MSG_02_TNA,COMM_DWORD+5
         BAL   R14,LINE0000
         DROP  R8

         MVC   0(MSG_03_L,R8),MSG_03
         USING MSG_03,R8                      DEFINE BASE
         L     R1,SMF42TRT                    MEMBER PAGE READS
         CVD   R1,COMM_DWORD                  CONVERT TO DECIMAL
         ED    MSG_03_TRT,COMM_DWORD+5
         L     R1,SMF42TRH                    MEMBER PAGE HITS
         CVD   R1,COMM_DWORD                  CONVERT TO DECIMAL
         ED    MSG_03_TRH,COMM_DWORD+5
         BAL   R14,LINE0000
         DROP  R8

         MVC   0(MSG_04_L,R8),MSG_04
         USING MSG_04,R8                      DEFINE BASE
         L     R1,SMF42TDT                    DIRECTORY PAGE READS
         CVD   R1,COMM_DWORD                  CONVERT TO DECIMAL
         ED    MSG_04_TDT,COMM_DWORD+5
         L     R1,SMF42TDH                    DIRECTORY PAGE HITS
         CVD   R1,COMM_DWORD                  CONVERT TO DECIMAL
         ED    MSG_04_TDH,COMM_DWORD+5
         BAL   R14,LINE0000
         DROP  R8

         MVC   0(MSG_05_L,R8),MSG_05
         USING MSG_05,R8                      DEFINE BASE
         L     R1,SMF42BUF                    ACTIVE 4K BUFFERS
         CVD   R1,COMM_DWORD                  CONVERT TO DECIMAL
         ED    MSG_05_BUF,COMM_DWORD+5
         L     R1,SMF42BMX                    BUFFER HIGH WATER MARK
         CVD   R1,COMM_DWORD                  CONVERT TO DECIMAL
         ED    MSG_05_BMX,COMM_DWORD+5
         LH    R1,SMF42LRU                    BMF LRU INTERVAL
         CVD   R1,COMM_DWORD                  CONVERT TO DECIMAL
         ED    MSG_05_LRU,COMM_DWORD+5
         LH    R1,SMF42UIC                    LRU CYCLES
         CVD   R1,COMM_DWORD                  CONVERT TO DECIMAL
         ED    MSG_05_UIC,COMM_DWORD+5
         DROP  R8
         B     EXIT0000
*---------------------------------------------------------------------*
*        Subtype 06 (dataset by job)                                  *
*---------------------------------------------------------------------*
T0060000 DS    0H
         ITRACE ID=TYPE006
         BAL   R14,LINE0000
         MVC   0(T6MSG_01_L,R8),T6MSG_01
         USING T6MSG_01,R8
         ICM   R4,15,SMF42OPS                 OFFSET TO PRODUCT SECTION
         AR    R4,R7                          PLUS BASE
         SH    R4,H4                          MINUS 4
         USING SMF42PRD,R4                    DEFINE BASE
         MVC   T6MSG_01_PDL,SMF42PDL          COPY PRODUCT LEVEL
         MVC   T6MSG_01_PDN,SMF42PDN          COPY PRODUCT NAME
         BAL   R14,LINE0000
         DROP  R4,R8

         LA    R5,SMF42END                    NEXT TRIPLET
         USING SMF42S6,R5                     DEFINE BASE
         ICM   R4,15,SMF42JHO                 OFFSET TO JOB HEADER
         AR    R4,R7                          PLUS BASE
         SH    R4,H4                          MINUS 4
         USING SMF4206A,R4
         MVC   0(T6MSG_02_L,R8),T6MSG_02
         USING T6MSG_02,R8
         MVC   T6MSG_02_JNM,S42JDJNM
         OC    S42JDRSD,S42JDRSD              DATE PRESENT?
         BZ    T0060010                       ON
         MVC   DXD_DATE,S42JDRSD
         AP    DXD_DATE,P1900000
         MVC   DXD_A(L'EDIT_WORD_2),EDIT_WORD_2
         ED    DXD_A(L'EDIT_WORD_2),DXD_DATE
         MVC   T6MSG_02_RSD,DXD_A+1
         ICM   R1,15,S42JDRST
         BAL   R15,TIME0000                   CONVERT TIME
         MVC   T6MSG_02_RST,DXD_B             COPY CONVERTED TIME
         BAL   R14,LINE0000
         DROP  R8

T0060010 DS    0H
         MVC   0(T6MSG_03_L,R8),T6MSG_03
         USING T6MSG_03,R8
         MVC   T6MSG_03_UID,S42JDUID          COPY USER ID
         BAL   R14,LINE0000
         DROP  R8

         MVC   0(T6MSG_04_L,R8),T6MSG_04
         USING T6MSG_04,R8
         CLI   S42JDCOD,0                     CLOSE?
         BE    T0060020                       YES
         MVC   T6MSG_04_COD,INTERVAL          TYPE IS 'INTERVAL'
         B     T0060030
T0060020 DS    0H
         MVC   T6MSG_04_COD,CLOSE             TYPE IS 'CLOSE'
T0060030 DS    0H
         SR    R1,R1                          CLEAR REGISTER
         ICM   R1,3,S42JDPGN                  PERFORMANCE GROUP
         CVD   R1,COMM_DWORD                  CONVERT TO DECIMAL
         ED    T6MSG_04_PGN,COMM_DWORD+5
         DROP  R8
         BAL   R14,LINE0000

         MVC   0(T6MSG_05_L,R8),T6MSG_05
         USING T6MSG_05,R8
         MVC   T6MSG_05_WSC,S42JDWSC          COPY WLM CLASS
         MVC   T6MSG_05_WLD,S42JDWLD          COPY WLM WORKLOAD NAME
         DROP  R8
         BAL   R14,LINE0000

         BAL   R14,LINE0000

         MVC   0(T6MSG_06_L,R8),T6MSG_06
         BAL   R14,LINE0000

         AH    R4,SMF42JHL                    DATA SET HEADER
         USING S42DSH,R4                      DEFINE BASE
T0060040 DS    0H
         MVC   0(T6MSG_07_L,R8),T6MSG_07
         USING T6MSG_07,R8
         MVC   T6MSG_07_DSN,S42DSN            COPY DATA SET NAME
         UNPK  DXD_A(5),S42DSDEV(3)           UNPACK DEVICE
         TR    DXD_A(4),COMM_HEXCHAR          TRANSLATE
         MVC   T6MSG_07_DEV,DXD_A
         MVC   T6MSG_07_VOL,S42DSVOL          COPY VOLSER
         ICM   R3,15,S42DSIOO                 DISP TO I/O SECTION
         AR    R3,R7                          PLUS BASE
         SH    R3,H4                          MINUS LENGTH OF LENGTH
         USING S42DSIO,R3                     DEFINE BASE
         ICM   R0,15,S42DSION                 NBR OF I/O'S
         CVD   R0,COMM_DWORD                  CONVERT TO DECIMAL
         ED    T6MSG_07_IOS,COMM_DWORD+2      EDIT I/O'S
         ICM   R0,15,S42DSIOR                 RESPONSE TIME
         CVD   R0,COMM_DWORD                  CONVERT TO DECIMAL
         ED    T6MSG_07_TIME,COMM_DWORD+5     EDIT RESPONSE TIME
         BAL   R14,LINE0000
         ICM   R4,15,S42DSNXT                 ANOTHER DATASET SECTION?
         BZ    EXIT0000                       NO
         AR    R4,R7                          PLUS BASE
         SH    R4,H4                          MINUS LENGTH OF LENGTH
         B     T0060040
         DROP  R3,R4,R8
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
TIME0000 DS    0H
         SR    R0,R0
         D     R0,F100                        CONVERT TO WHOLE SECONDS
         SR    R0,R0                          CLEAR REMAINDER
         D     R0,F3600                       COMPUTE HOURS
         CVD   R1,COMM_DWORD                  CONVERT HOURS
         MVC   DXD_A(L'EDIT_WORD_1),EDIT_WORD_1
         ED    DXD_A(L'EDIT_WORD_1),COMM_DWORD+6
         MVC   DXD_B+0(2),DXD_A+2             COPY HOURS
         MVI   DXD_B+2,C':'                   INSERT COLON
         LR    R1,R0                          COPY REMAINDER
         SR    R0,R0                          CLEAR R0
         D     R0,F60                         COMPUTE MINUTES
         CVD   R1,COMM_DWORD                  CONVERT MINUTES
         MVC   DXD_A(L'EDIT_WORD_1),EDIT_WORD_1
         ED    DXD_A(L'EDIT_WORD_1),COMM_DWORD+6
         MVC   DXD_B+3(2),DXD_A+2             COPY MINUTES
         MVI   DXD_B+5,C':'                   INSERT COLON
         CVD   R0,COMM_DWORD                  CONVERT SECONDS
         MVC   DXD_A(L'EDIT_WORD_1),EDIT_WORD_1
         ED    DXD_A(L'EDIT_WORD_1),COMM_DWORD+6
         MVC   DXD_B+6(2),DXD_A+2             COPY SECONDS
         BR    R15
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
LINE0000 DS    0H
         A     R8,SPF_VWIDTH
         BCTR  R6,R14
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
EXIT0000 DS    0H
         SR    R2,R2
         B     EXITEXIT
EXIT0004 DS    0H
         LA    R2,4
EXITEXIT DS    0H
         ITRACE ID=EXIT,                                               +
               RDATA1=R2
         L     R13,4(R13)                     RESTORE SAVE AREA ADDRESS
         L     R14,12(,R13)                   RESTORE R14
         SR    R15,R15                        SET RC
         LM    R0,R12,20(R13)                 RESTORE REGISTERS
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDSTART       DC    Q(WK0042)
F3600          DC    F'3600'
F100           DC    F'100'
F60            DC    F'60'

H4             DC    H'4'

P1900000       DC    P'1900000'

EDIT_WORD_1    DC    X'F0202020'
EDIT_WORD_2    DC    X'F0202020204B202020'

CLOSE          DC    CL8'Close   '
INTERVAL       DC    CL8'Interval'

MSG_01         DS    0C
               DC    CL10' '
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Subsystem ID'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_01_SSI     DC    CL4' '
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C' Record subtype'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_01_STY     DC    C' '
MSG_01_L       EQU   *-MSG_01

MSG_02         DS    0C
               DC    CL10' '
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Storage classes'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_02_TNA     DC    X'402020202120'
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C' Measurement time'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_02_TMT     DC    X'402020202120'
MSG_02_L       EQU   *-MSG_02

MSG_03         DS    0C
               DC    CL10' '
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Member data page reads   '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_03_TRT     DC    X'402020202120'
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C' Hits'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_03_TRH     DC    X'402020202120'
MSG_03_L       EQU   *-MSG_03

MSG_04         DS    0C
               DC    CL10' '
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Directory data page reads'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_04_TDT     DC    X'402020202120'
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C' Hits'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_04_TDH     DC    X'402020202120'
MSG_04_L       EQU   *-MSG_04

MSG_05         DS    0C
               DC    CL10' '
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Total 4k buffers'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_05_BUF     DC    X'402020202120'
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C' High water mark'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_05_BMX     DC    X'402020202120'
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C' BMF LRU time'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_05_LRU     DC    X'402020202120'
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C' BMF UIC'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_05_UIC     DC    X'402020202120'
MSG_05_L       EQU   *-MSG_05

*  ------------------------------------------------------
*              SUB TYPE 6 MESSAGES
*  ------------------------------------------------------
T6MSG_01       DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Product level    '
               DC    AL1($SCREEN_ATTR_NORMAL)
T6MSG_01_PDL   DC    CL8' '
               DC    CL3' '
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Product name     '
               DC    AL1($SCREEN_ATTR_NORMAL)
T6MSG_01_PDN   DC    CL10' '
T6MSG_01_L     EQU   *-T6MSG_01

T6MSG_02       DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'JOB name         '
               DC    AL1($SCREEN_ATTR_NORMAL)
T6MSG_02_JNM   DC    CL8' '
               DC    CL3' '
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Reader date/time '
               DC    AL1($SCREEN_ATTR_NORMAL)
T6MSG_02_RSD   DC    CL8' '
               DC    C' '
T6MSG_02_RST   DC    CL8' '
T6MSG_02_L     EQU   *-T6MSG_02

T6MSG_03       DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'User             '
               DC    AL1($SCREEN_ATTR_NORMAL)
T6MSG_03_UID   DC    CL8' '
T6MSG_03_L     EQU   *-T6MSG_03

T6MSG_04       DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Type             '
               DC    AL1($SCREEN_ATTR_NORMAL)
T6MSG_04_COD   DC    CL8' '
               DC    CL3' '
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Performance group'
               DC    AL1($SCREEN_ATTR_NORMAL)
T6MSG_04_PGN   DC    X'402020202120'
T6MSG_04_L     EQU   *-T6MSG_04

T6MSG_05       DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'WLM service class'
               DC    AL1($SCREEN_ATTR_NORMAL)
T6MSG_05_WSC   DC    CL8' '
               DC    CL3' '
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'WLM workload name'
               DC    AL1($SCREEN_ATTR_NORMAL)
T6MSG_05_WLD   DC    CL8' '
T6MSG_05_L     EQU   *-T6MSG_05

T6MSG_06       DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    CL43'DSN'
               DC    C' '
               DC    CL4'DEV'
               DC    C' '
               DC    CL6'VOLSER'
               DC    C' '
               DC    CL15'         I/O''S'
               DC    CL06' Resp'
               DC    AL1($SCREEN_ATTR_NORMAL)
T6MSG_06_L     EQU   *-T6MSG_06

T6MSG_07       DS    0C
T6MSG_07_DSN   DC    CL44' '
               DC    C' '
T6MSG_07_DEV   DC    CL4' '
               DC    C' '
T6MSG_07_VOL   DC    CL6' '
T6MSG_07_IOS   DC    X'4020206B2020206B2020206B202120'
T6MSG_07_TIME  DC    X'40206B202120'
T6MSG_07_L     EQU   *-T6MSG_07
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
WK0042         DSECT
               COPY     DXDPREF
DXD_A          DS       CL80
DXD_B          DS       CL80
DXD_DATE       DS       PL4
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               SESSION
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               OSSPFD TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
SMF_RECORD  DSECT
            IGWSMF  SMF42_01=YES,                                      +
               SMF42_02=YES,                                           +
               SMF42_06=YES,                                           +
               SMF42_0L=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY REGEQU
               END  SMF0042
