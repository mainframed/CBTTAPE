         MACRO
         RECORD &T,&M,&D,&S
.* ------------------------------------------------------------------ *
.*                                                                    *
.*      T    SMF record type                                          *
.*      M    Processing module name                                   *
.*      D    Description                                              *
.*      S    Highest screen number for this record type               *
.*                                                                    *
.* ------------------------------------------------------------------ *
         LCLA  &NBR
&NBR     SETA  (4*&T)
         ORG   RECORD_TABLE+&NBR
         DC    A(A&SYSNDX)
         ORG
A&SYSNDX DC    AL2(&T)
         DC    CL3'&T'
         AIF   ('&M' EQ '0').LBL0010
         AIF   ('&M' EQ 'BYPASS').LBL0020
         DC    VL4(&M)
         AGO   .LBL0030
.LBL0010 ANOP
         DC    AL4(0)
         AGO   .LBL0030
.LBL0020 DS    0H
         DC    AL4(-1)
.LBL0030 ANOP
         AIF   (T'&S EQ 'O').LBL0040
         DC    AL1(&S)
         AGO   .LBL0050
.LBL0040 ANOP
         DC    AL1(1)
.LBL0050 ANOP
         DC    AL2(C&SYSNDX-B&SYSNDX)

B&SYSNDX DC    C&D
C&SYSNDX EQU   *
         MEND
* ------------------------------------------------------------------- *
*                                                                     *
*  SMF record formatter for On Screen                                 *
*                                                                     *
* ------------------------------------------------------------------- *
SMFMAIN  CSECT
SMFMAIN  AMODE 31
SMFMAIN  RMODE ANY
         USING OSCOMM,R12
         USING SMFMAIN,R15
         B     INIT0000
MODID    DC    CL8'SMFMAIN'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         USING OSCOMM,R12                     DEFINE COMMON AREA BASE
         USING SESSION,R11
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT
         DROP  R15
         USING SMFMAIN,R10                    DEFINE BASE
         L     R9,COMM_OSSPFD                 SPF DATA
         USING OSSPFD,R9                      DEFINE BASE
         CLI   SESS_FORMAT_FLAGS,$FORMAT_INITIALIZE
         BNE   INIT0020                       NO
         LR    R3,R0                          COPY COLUMN
         LR    R4,R1                          COPY RECORD NUMBER
         ICM   R2,15,SESS_DXD_ADDR
         BNZ   INIT0010
         L     R0,DXDSIZE
         ST    R0,SESS_DXD_LENGTH
         GETMAIN RU,                                                   +
               LV=(0),                                                 +
               LOC=ANY
         ST    R1,SESS_DXD_ADDR
         LR    R2,R1                          SAVE ADDRESS
         LR    R0,R1
         L     R1,DXDSIZE
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14                         INITIALIZE WORK AREA
INIT0010 DS    0H
         A     R2,DXDSTART                    OFFSET TO OUR PORTION
         ST    R13,4(R2)                      SAVE R13
         ST    R2,8(R13)                      CHAIN SAVE AREA
         LR    R13,R2                         COPY WORK AREA ADDRESS
         USING WKMAIN,R13                     DEFINE BASE
         ITRACE ID=ENTRY1,                                             +
               RDATA2=R13
         MVC   DXD_CSECT,MODID                COPY MODULE ID
         LA    R1,PANEL_NAME                  PANEL NAME
         MVI   COMM_VDATA_FUNC,$VDATA_GETMAIN SET FUNCTION
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15                        GETMAIN VDATA
         ST    R0,WORK_VDATA_LENGTH           SAVE LENGTH
         ST    R1,WORK_VDATA_ADDR             SAVE ADDRESS
         MVC   WORK_VWIDTH,SPF_VWIDTH
         MVC   WORK_VDEPTH,SPF_VDEPTH
         LA    R1,DATASPACE_1
         ST    R1,DSPCREQ_DATASPACE
         MVC   SPF_DSN,SESS_DSN
         MVC   SPF_MEMBER,SESS_MEMBER
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
INIT0020 DS    0H
         ICM   R15,15,SESS_DXD_ADDR
         BZ    ERR0040
         ST    R13,4(R15)
         ST    R15,8(R13)
         LR    R13,R15
         ITRACE ID=ENTRY2,                                             +
               DATA1=(SESS_FORMAT_FLAGS,1)
         XC    WORK_RC,WORK_RC
         CLI   SESS_FORMAT_FLAGS,$FORMAT_CONTROL
         BE    EXIT0000
         CLI   SESS_FORMAT_FLAGS,$FORMAT_CLEANUP
         BE    CLEAN000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         ITRACE ID=FORMAT
         L     R7,SESS_IO_AREA                I/O AREA ADDRESS
         ST    R7,DSPCREQ_RECORD_ADDR
         USING SMF0FLG,R7
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0010 DS    0H
         ITRACE ID=FORMAT,                                             +
               DATA1=(DATASPACE_1_DISPLAY_FIRST_RECORD,1)
         MVC   SPF_ZCMD,COMM_BLANKS
         BAL   R15,CLEAR000
* ------------------------------------------------------------------- *
*        Retrieve the record from the data space                      *
* ------------------------------------------------------------------- *
         L     R0,SESS_IO_AREA
         L     R1,COMM_IO_SIZE
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14
         MVC   DSPCREQ_RECORD_NBR,DATASPACE_1_DISPLAY_FIRST_RECORD
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETRIEVE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE              OSDSPACE ENTRY POINT
         BALR  R14,R15                        READ THE RECORD
         CLI   DSPCREQ_RC,$DSPCREQ_OK
         BNE   ERR0010                        ERROR
         ITRACE ID=RECORD,                                             +
               DATA1=(DSPCREQ_RECORD_ADDR,4),                          +
               DATA2=(DSPCREQ_RECORD_LENGTH,4)
         MVI   WORK_SCREEN,1
* ------------------------------------------------------------------- *
*        Build common SMF heading                                     *
* ------------------------------------------------------------------- *
MAIN0030 DS    0H
         L     R8,WORK_VDATA_ADDR             VDATA BUFFER ADDRESS
         L     R6,WORK_VDEPTH                 NUMBER OF LINES OF VDATA
         USING RECORD_DSECT,R4
         MVC   WORK_A,COMM_BLANKS             INITIALIZE
         MVC   WORK_A(HEADING_01_L),HEADING_01
         L     R1,DATASPACE_1_DISPLAY_FIRST_RECORD
         CVD   R1,COMM_DWORD
         MVC   WORK_B(L'EDIT_WORD_3),EDIT_WORD_3
         ED    WORK_B(L'EDIT_WORD_3),COMM_DWORD+4
         MVC   WORK_A+(HEADING_RECORD-HEADING_01)(L'HEADING_RECORD),WOR+_+
               K_B+1
         SR    R2,R2
         IC    R2,SMF0RTY
         CVD   R2,COMM_DWORD
         MVC   WORK_B(4),=X'40202120'
         ED    WORK_B(4),COMM_DWORD+6
         MVC   WORK_A+(HEADING_TYPE-HEADING_01)(3),WORK_B+1
         UNPK  WORK_B(3),SMF0RTY(2)           UNPACK RECORD TYPE
         TR    WORK_B(2),COMM_HEXCHAR         TRANSLATE
         MVC   WORK_A+(HEADING_HEX-HEADING_01)(L'HEADING_HEX),WORK_B
         MVC   0(HEADING_01_L,R8),WORK_A
         A     R8,WORK_VWIDTH
         BCTR  R6,0
         MVC   SPF_SMF_DESC,COMM_BLANKS       INITIALIZE RECORD DESC
         SR    R2,R2                          CLEAR REGISTER
         IC    R2,SMF0RTY                     RECORD TYPE
         ITRACE ID=TYPE,                                               +
               RDATA1=R2
         SLL   R2,2                           MULTIPLY RECORD ID BY 4
         L     R4,RECORD_TABLE(R2)            ADDRESS OF RECORD INFO
         LTR   R4,R4                          DEFINED RECORD TYPE?
         BZ    MAIN0070                       NO
         MVC   WORK_A,COMM_BLANKS             INITIALIZE
         MVC   WORK_A(HEADING_02_L),HEADING_02
         CLI   SMF0RTY,48                     JES3 LOGOFF/SIGN OFF?
         BE    MAIN0040                       YES.. NO DATE
         CLI   RECORD_PROC,X'FF'              BYPASSED TYPE?
         BE    MAIN0040
         OC    SMF0DTE,SMF0DTE                DATE PRESENT?
         BZ    MAIN0040                       NO
         MVC   WORK_DATE,SMF0DTE
         AP    WORK_DATE,P1900000             ADD CENTURY
         MVC   WORK_B(L'EDIT_WORD_2),EDIT_WORD_2
         ED    WORK_B(L'EDIT_WORD_2),WORK_DATE
         MVC   WORK_A+(HEADING_DATE-HEADING_02)(8),WORK_B+1
         ICM   R1,15,SMF0TME                  READER TIME
         BAL   R15,TIME0000                   CONVERT
         MVC   WORK_A+(HEADING_TIME-HEADING_02)(9),WORK_C
MAIN0040 DS    0H
         UNPK  WORK_B(3),SMF0FLG(2)
         TR    WORK_B(2),COMM_HEXCHAR
         MVC   WORK_A+(HEADING_FLAG-HEADING_02)(L'HEADING_FLAG),WORK_B
         MVC   WORK_A+(HEADING_ID-HEADING_02)(L'HEADING_ID),SMF0SID
         MVC   0(HEADING_02_L,R8),WORK_A
         A     R8,WORK_VWIDTH                 NEXT LINE
         BCTR  R6,0
         SR    R15,R15                        CLEAR REGISTER
         ICM   R15,3,RECORD_DESC_LENGTH
         BZ    MAIN0060                       NO TEXT
         CH    R15,=Y(L'SPF_SMF_DESC)         TOO LONG?
         BNH   MAIN0050
         LH    R15,=Y(L'SPF_SMF_DESC)         LIMIT LENGTH
MAIN0050 DS    0H
         BCTR  R15,0
         EX    R15,MVC_03
* ------------------------------------------------------------------- *
*        Call the record formatter                                    *
* ------------------------------------------------------------------- *
MAIN0060 DS    0H
         MVC   SPF_VDEPTH,WORK_VDEPTH
         MVC   SPF_VWIDTH,WORK_VWIDTH
         ICM   R15,15,RECORD_PROC             RECORD PROCESSOR
         BM    MAIN0070                       BYPASSED TYPE
         BZ    MAIN0070                       NONE
         SR    R1,R1                          CLEAR REGISTER
         IC    R1,WORK_SCREEN
         ITRACE ID=CALL,                                               +
               RDATA1=R15
         BALR  R14,R15                        CALL RECORD PROCESSOR
         B     MAIN0100
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0070 DS    0H
         A     R8,WORK_VWIDTH                 NEXT LINE
         A     R8,WORK_VWIDTH                 NEXT LINE
         A     R8,WORK_VWIDTH                 NEXT LINE
         MVC   WORK_A,COMM_BLANKS
         MVC   WORK_A(MSG_01_L),MSG_01
         EX    R3,MVC_02                      COPY DEFAULT MESSAGE
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0100 DS    0H
         ITRACE ID=DISPLAY
         MVC   SESS_DISP_PANEL,PANEL_NAME
         MVC   SESS_DISP_VDATA_ADDR,WORK_VDATA_ADDR
         MVC   SESS_DISP_VDATA_LENGTH,WORK_VDATA_LENGTH
         XC    SESS_DISP_CURSOR,SESS_DISP_CURSOR
         L     R15,COMM_V_OSDISP
         BALR  R14,R15
         ITRACE ID=DISP_RC,                                            +
               DATA1=(SESS_RC,2),                                      +
               DATA2=(SESS_STATUS,1)
         CLC   COMM_NEW_FORMAT,COMM_BLANKS    NEW FORMAT?
         BNE   EXIT0000                       YES
         CLI   COMM_SESS_FUNC,0               SESSION FUNCTION?
         BNE   EXIT0000                       YES
         OC    SESS_RC,SESS_RC                RC=0?
         BZ    MAIN0300                       YES
         ITRACE ID=ENDING
         MVI   COMM_SESS_FUNC,$SESS_REMOVE    SET FUNCTION
         B     EXIT0000                       EXIT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0300 DS    0H
         CLI   SPF_ZVERB,C'B'                 'BOTTOM'?
         BE    MAIN0610                       YES
         CLI   SPF_ZVERB,C'D'                 SCROLL DOWN?
         BE    MAIN0400                       YES
         CLI   SPF_ZVERB,C'L'                 'LEFT?
         BE    MAIN0700                       YES
         CLI   SPF_ZVERB,C'R'                 'RIGHT?
         BE    MAIN0800                       YES
         CLI   SPF_ZVERB,C'T'                 'TOP'?
         BE    MAIN0600                       YES
         CLI   SPF_ZVERB,C'U'                 SCROLL UP?
         BE    MAIN0500                       YES
         CLC   ABEND,SPF_ZCMD                 'ABEND'?
         BE    ABEND000                       YES
         CLC   SPF_ZCMD,COMM_BLANKS           BLANKS?
         BNE   EXIT0000                       NO
         B     MAIN0010
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0400 DS    0H
         ITRACE ID=DOWN
         CLI   SPF_ZSCROLLA,C'M'
         BE    MAIN0610
         L     R1,DATASPACE_1_DISPLAY_FIRST_RECORD  RECORD DISPLAYED    D
         A     R1,F1                                PLUS 1
         C     R1,DATASPACE_1_LAST_RECORD           TOO HIGH?
         BH    MAIN0610                             YES
         ST    R1,DATASPACE_1_DISPLAY_FIRST_RECORD  CHANGE RCD NBR
         B     MAIN0010                             DISPLAY THIS RECORD
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0500 DS    0H
         ITRACE ID=UP
         CLI   SPF_ZSCROLLA,C'M'
         BE    MAIN0600
         L     R1,DATASPACE_1_DISPLAY_FIRST_RECORD
         S     R1,F1
         BL    MAIN0600
         ST    R1,DATASPACE_1_DISPLAY_FIRST_RECORD
         B     MAIN0010
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0600 DS    0H
         ITRACE ID=TOP
        MVC   DATASPACE_1_DISPLAY_FIRST_RECORD,DATASPACE_1_FIRST_RECORD
         MVC   SPF_MSG_1(MSG_04_L),MSG_04
         B     MAIN0010
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0610 DS    0H
         ITRACE ID=BOTTOM
         L     R1,DATASPACE_1_LAST_RECORD
         BCTR  R1,0
         ST    R1,DATASPACE_1_DISPLAY_FIRST_RECORD
         MVC   SPF_MSG_1(MSG_03_L),MSG_03
         B     MAIN0010
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0700 DS    0H
         ITRACE ID=LEFT
         SR    R1,R1                          CLEAR REGISTER
         ICM   R1,1,WORK_SCREEN               CURRENT SCREEN NUMBER
         BCT   R1,MAIN0710                    MINUS 1
         B     MAIN0720                       ALREADY AT 1
MAIN0710 DS    0H
         STC   R1,WORK_SCREEN                 SET NEW SCREEN NUMBER
MAIN0720 DS    0H
         BAL   R15,CLEAR000
         B     MAIN0030                       BUILD DATA
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0800 DS    0H
         ITRACE ID=RIGHT
         CLC   WORK_SCREEN,RECORD_SCREENS     ALREADY AT MAXIMUM?
         BE    MAIN0810                       YES
         SR    R1,R1                          CLEAR REGISTER
         IC    R1,WORK_SCREEN                 CURRENT SCREEN NUMBER
         LA    R1,1(,R1)                      PLUS 1
         STC   R1,WORK_SCREEN                 SET NEW SCREEN NUMBER
MAIN0810 DS    0H
         BAL   R15,CLEAR000
         B     MAIN0030                       BUILD DATA
* ------------------------------------------------------------------- *
*        Clear VDATA                                                  *
* ------------------------------------------------------------------- *
CLEAR000 DS    0H
         L     R1,WORK_VDATA_ADDR             VDATA BUFFER ADDRESS
         L     R2,WORK_VDEPTH                 NUMBER OF LINES
         L     R3,WORK_VWIDTH                 SCREEN WIDTH
         BCTR  R3,0                           MINUS 1
CLEAR010 DS    0H
         EX    R3,MVC_01                      CLEAR THE DATA
         A     R1,WORK_VWIDTH                 NEXT LINE
         BCT   R2,CLEAR010                    LOOP
         BR    R15
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
         B     MAIN0100
ERR0020  DS    0H
         DC    H'2',C'BEYOND HIGH RECORD'
ERR0030  DS    0H
         DC    H'3'
         DC    C'BEYOND HIGH RECORD'
ERR0040  DS    0H
         DC    H'0'
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
TIME0000 DS    0H
         ITRACE ID=TIME,                                               +
               RDATA1=R1
         SR    R0,R0
         D     R0,F100                        CONVERT TO WHOLE SECONDS
         SR    R0,R0                          CLEAR REMAINDER
         D     R0,F3600                       COMPUTE HOURS
         CVD   R1,COMM_DWORD                  CONVERT HOURS TO DECIMAL
         MVC   WORK_B(L'EDIT_WORD_1),EDIT_WORD_1
         ED    WORK_B(L'EDIT_WORD_1),COMM_DWORD+6
         MVC   WORK_C+0(2),WORK_B+2           COPY HOURS
         MVI   WORK_C+2,C':'                  INSERT COLON
         LR    R1,R0                          COPY REMAINDER
         SR    R0,R0                          CLEAR R0
         D     R0,F60                         COMPUTE MINUTES
         CVD   R1,COMM_DWORD                  CONVERT TO DECIMAL
         MVC   WORK_B(L'EDIT_WORD_1),EDIT_WORD_1
         ED    WORK_B(L'EDIT_WORD_1),COMM_DWORD+6
         MVC   WORK_C+3(2),WORK_B+2           COPY MINUTES
         MVI   WORK_C+5,C':'                  INSERT COLON
         CVD   R0,COMM_DWORD                  CONVERT TO DECIMAL
         MVC   WORK_B(L'EDIT_WORD_1),EDIT_WORD_1
         ED    WORK_B(L'EDIT_WORD_1),COMM_DWORD+6
         MVC   WORK_C+6(2),WORK_B+2           COPY SECONDS
         BR    R15
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MSG0000  DS    0H
         MVC   COMM_MSG_CSECT,MODID           IDENTIFY CSECT
         L     R15,COMM_V_OSMSG               OSMSG ENTRY POINT
         BALR  R14,R15                        BUILD THE MESSAGE
         BR    R2
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ABEND000 DS    0H
         ITRACE ID=ABEND
         DC    H'0'
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
CLEAN000 DS    0H
         ITRACE ID=CLEAN_UP
         ICM   R1,15,WORK_VDATA_ADDR          VDATA ADDRESS
         BZ    CLEAN010                       NOT PRESENT
         L     R0,WORK_VDATA_LENGTH           LENGTH
         MVI   COMM_VDATA_FUNC,$VDATA_FREEMAIN
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15                        FREE VDATA
CLEAN010 DS    0H
         LA    R0,WKMAIN_L                    WORK AREA SIZE
         LR    R1,R13                         COPY WORK AREA ADDRESS
         L     R13,4(R13)                     RESTORE R13
         FREEMAIN RU,                         FREE WORK AREA           +
               A=(1),                                                  +
               LV=(0)
         XC    SESS_DXD_ADDR,SESS_DXD_ADDR
         XC    SESS_DXD_LENGTH,SESS_DXD_LENGTH
         SR    R15,R15
         B     EXIT0010
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         LH    R15,WORK_RC                    RETURN CODE
         L     R13,4(R13)                     RESTORE R13
EXIT0010 DS    0H
         L     R14,12(,R13)                   RESTORE R14
         LM    R0,R12,20(R13)                 RESTORE REGISTERS
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
MVC_01   MVC   0(0,R1),COMM_BLANKS            COPY BLANKS
MVC_02   MVC   0(0,R8),WORK_A                 COPY TO BUFFER
MVC_03   MVC   SPF_SMF_DESC(0),RECORD_DESC

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDSIZE        CXD
DXDSTART       DC  Q(WKMAIN)
F1             DC  F'1'
F60            DC  F'60'
F100           DC  F'100'
F3600          DC  F'3600'

P1900000       DC  P'1900000'

EDIT_WORD_1    DC  X'F0212020'
EDIT_WORD_2    DC  X'F0212020204B202020'
EDIT_WORD_3    DC  X'4020202020202120'

ABEND          DC  C'ABEND'
DISPLAY        DC  CL8'DISPLAY'
PANEL_NAME     DC  CL8'OSSMF'
TOP            DC  C'TOP'

HEADING_01     DS  0C
               DC  CL10' '
               DC  AL1($SCREEN_ATTR_HIGH)
               DC  C'Record number '
               DC  AL1($SCREEN_ATTR_NORMAL)
HEADING_RECORD DC  CL7' '
               DC  CL3' '
               DC  AL1($SCREEN_ATTR_HIGH_TURQUOISE)
               DC  C'Record type '
               DC  AL1($SCREEN_ATTR_HIGH_YELLOW)
HEADING_TYPE   DC  CL3' '
               DC  C'  ('
HEADING_HEX    DC  CL2' '
               DC  C')'
               DC  Al1($SCREEN_ATTR_HIGH_TURQUOISE)
               DC   C'HEX'
               DC  AL1($SCREEN_ATTR_NORMAL)
HEADING_01_L   EQU *-HEADING_01

HEADING_02     DS  0C
               DC  AL1($SCREEN_ATTR_HIGH)
               DC  CL10' '
HEADING_DATE   DC  CL8' '
               DC  CL2' '
HEADING_TIME   DC  CL8' '
               DC  CL10' '
               DC  AL1($SCREEN_ATTR_HIGH)
               DC  C'Flags'
               DC  AL1($SCREEN_ATTR_NORMAL)
HEADING_FLAG   DC  CL2' '
               DC  AL1($SCREEN_ATTR_HIGH)
               DC  C'SYSTEM ID'
               DC  AL1($SCREEN_ATTR_NORMAL)
HEADING_ID     DC  CL4' '
HEADING_02_L   EQU *-HEADING_02

MSG_01         DS  0C
               DC  C'No formatting for this record type available'
MSG_01_L       EQU *-MSG_01

MSG_02         DS  0C
               DC  C'Invalid command'
MSG_02_L       EQU *-MSG_02

MSG_03         DS  0C
               DC  C'At bottom of data'
MSG_03_L       EQU *-MSG_03

MSG_04         DS  0C
               DC  C'At top of data'
MSG_04_L       EQU *-MSG_04

               LTORG

RECORD_TABLE   DC  256A(0,0)
               RECORD 000,SMF0000,'IPL'
               RECORD 002,SMF0002,'SMF dump start'
               RECORD 003,SMF0002,'SMF dump end'
               RECORD 004,SMF0004,'Job step termination'
               RECORD 005,SMF0005,'Job termination'
               RECORD 006,0,'JES2 JOE (Job output Element)'
               RECORD 007,SMF0007,'Data lost'
               RECORD 008,0,'I/O configuration'
               RECORD 009,SMF0009,'Vary device online'
               RECORD 010,0,'Allocation recovery'
               RECORD 011,SMF0009,'Vary device offline'
               RECORD 014,SMF0014,'Data set (input)'
               RECORD 015,SMF0014,'Data set (output)'
               RECORD 017,SMF0017,'Scratch data set'
               RECORD 018,SMF0018,'Rename data set'
               RECORD 020,SMF0020,'JOB initiation'
               RECORD 025,SMF0025,'JES3 device allocation'
               RECORD 030,SMF0030,'Common address space work',6
               RECORD 031,SMF0031,'TIOC initialization'
               RECORD 032,SMF0032,'TSO user work accounting'
               RECORD 040,SMF0040,'Dynamic DD'
               RECORD 042,SMF0042,'DFP statistics and configuration'
               RECORD 048,SMF0048,'JES3 SIGNOFF/Stop Line/LOGOFF'
               RECORD 060,SMF0060,'VSAM volume data set updated'
               RECORD 061,SMF0061,'ICF define'
               RECORD 062,SMF0062,'VSAM component or cluster opened'
               RECORD 064,SMF0064,'VSAM component or cluster status'
               RECORD 066,SMF0066,'ICF ALTER activity'
               RECORD 070,SMF0070,'RMF CPU and CRYPTO measurements',15
               RECORD 071,SMF0071,'RMF paging activity',15
               RECORD 075,SMF0075,'Page dataset activity'
               RECORD 080,SMF0080,'RACF operator record'
               RECORD 090,SMF0090,'System status'
               RECORD 092,SMF0092,'Open Edition Filsystem event'
               RECORD 094,BYPASS,'BYPASSED'
               RECORD 110,SMF0110,'CICS'
               ORG

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
WKMAIN            DSECT
                  COPY     DXDPREF

WORK_VDATA_ADDR   DS       A
WORK_VDATA_LENGTH DS       F

WORK_VDEPTH       DS       F
WORK_VWIDTH       DS       F

WORK_SPF_PARMS    DS       0A
WORK_SPF_1        DS       A
WORK_SPF_2        DS       A

WORK_RC           DS       H

WORK_DATE         DS       PL4
WORK_A            DS       CL80
WORK_B            DS       CL80
WORK_C            DS       CL80

WORK_SCREEN       DS       X

                  DSPCREQ  DSECT=NO
WKMAIN            DSECT
WKMAIN_L          EQU      *-WKMAIN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                    COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                    SESSION
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                    BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
RECORD_DSECT        DSECT
RECORD_TYPE         DS    AL2
RECORD_TYPE_CHAR    DS    CL3
RECORD_PROC         DS    AL4
RECORD_SCREENS      DS    AL1
RECORD_DESC_LENGTH  DS    AL2
RECORD_DESC         DS    C
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                    OSSPFD TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
SMF_RECORD          DSECT
                    IFASMFR 0
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                    COPY    ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                    COPY    TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                    COPY    REGEQU
                    END     SMFMAIN
