*---------------------------------------------------------------------*
*                                                                     *
*         Format SMF record type 90                                   *
*         System status                                               *
*                                                                     *
*   At entry                                                          *
*       R15    program base address                                   *
*       R14    return address                                         *
*       R9     OSSPFD address                                         *
*       R8     VDATA buffer address                                   *
*       R7     SMF record address +4 (does not include length)        *
*                                                                     *
* ------------------------------------------------------------------- *
SMF0090  CSECT
SMF0090  AMODE 31
SMF0090  RMODE ANY
         USING SESSION,R11
         USING SMF0090,R15
         B     INIT0000
MODID    DC    CL8'SMF0090'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         USING OSCOMM,R12                     DEFINE COMMON AREA BASE
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT
         USING SMF0090,R10                    DEFINE BASE
         DROP  R15
         L     R1,SESS_DXD_ADDR
         A     R1,DXDSTART
         ST    R13,4(R1)                      SAVE R13
         ST    R1,8(R13)                      CHAIN SAVE AREA
         LR    R13,R1                         COPY WORK AREA ADDRESS
         USING WK0090,R13                     DEFINE BASE
         MVC   DXD_CSECT,MODID
         USING OSSPFD,R9                      DEFINE BASE
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
         ITRACE ID=ENTRY
         USING SMF90FLG,R7
         ICM   R5,15,SMF90POF                 OFFSET TO PRODUCT SECTION
         AR    R5,R7                          PLUS BASE
         SH    R5,H4                          MINUS 4
         USING SMF90PRH,R5                    DEFINE BASE
         MVC   0(MSG_01_L,R8),MSG_01          COPY MESSAGE
         USING MSG_01,R8                      DEFINE BASE
         MVC   MSG_01_RVN,SMF90RVN            COPY RECORD VERSION
         MVC   MSG_01_PNM,SMF90PNM            COPY PRODUCT NAME
         DROP  R8

         A     R8,SPF_VWIDTH                  NEXT LINE
         BCTR  R6,0
         MVC   0(MSG_02_L,R8),MSG_02          COPY MESSAGE
         USING MSG_02,R8
         SR    R1,R1                          CLEAR REGISTER 1
         ICM   R1,3,SMF90TID                  SUB-TYPE ID
         CVD   R1,COMM_DWORD                  CONVERT TO DECIMAL
         ED    MSG_02_TID,COMM_DWORD+6
         LTR   R1,R1                          ZERO?
         BZ    EXIT0000                       YES
         CH    R1,=Y(TID_TABLE_L/TID_L)       TOO HIGH?
         BH    EXIT0000                       YES
         BCTR  R1,0                           CONVERT TO RELATIVE
         MH    R1,=Y(TID_L)                   MULTIPLY BY LENGTH EACH
         LA    R1,TID_TABLE(R1)               TID MESSAGE ADDRESS
         USING TID,R1
         MVC   MSG_02_DESC,TID_DESC           COPY TID DESCRIPTOR
         DROP  R8
         A     R8,SPF_VWIDTH                  NEXT LINE
         BCTR  R6,0                           MINUS 1
         ICM   R15,15,TID_ADDR                SUBTYPE PROCESSOR
         BZ    EXIT0000                       NOT PROCESSED FURTHER
         BR    R15                            PROCESS THE SUB-TYPE
         DROP  R5

TYPE6000 DS    0H
         ICM   R5,15,SMF90DOF                 OFFSET TO DATA SECTION
         AR    R5,R7                          PLUS BASE
         SH    R5,H4                          MINUS 4
         USING SMF90T6,R5
         MVC   0(T6_MSG_01_L,R8),T6_MSG_01
         USING T6_MSG_01,R8
         MVC   T6_MSG_01_SWO,SMF90SWO
         DROP  R8

         A     R8,SPF_VWIDTH
         MVC   0(T6_MSG_02_L,R8),T6_MSG_02
         USING T6_MSG_02,R8
         MVC   T6_MSG_02_SWN,SMF90SWN
         B     EXIT0000
         DROP  R5
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
EXIT0000 DS    0H
         SR    R2,R2
         B     EXITEXIT
EXIT0004 DS    0H
         LA    R2,4
EXITEXIT DS    0H
         ITRACE ID=EXIT,                                               +
               RDATA1=R2
         L     R13,4(R13)                     RESTORE SAVE AREA ADDRESS
         L     R14,12(,R13)                   RESTORE R14
         LR    R15,R2                         SET RC
         LM    R0,R12,20(R13)                 RESTORE REGISTERS
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDSTART       DC    Q(WK0090)
H4             DC    H'4'

TID_TABLE      DS    0C
               DC    AL4(0),CL35'SET TIME'                 1
               DC    AL4(0),CL35'SET DATE'                 2
               DC    AL4(0),CL35'SETDMN'                   3
               DC    AL4(0),CL35'SET IPS'                  4
               DC    AL4(0),CL35'SET SMF'                  5
               DC    AL4(TYPE6000),CL35'SWITCH SMF'        6
               DC    AL4(0),CL35'HALT EOD'                 7
               DC    AL4(0),CL35'IPL PROMPT'               8
               DC    AL4(0),CL35'IPL SMF'                  9
               DC    AL4(0),CL35'IPL SRM'                 10
               DC    AL4(0),CL35'SET OPT'                 11
               DC    AL4(0),CL35'SET ICS'                 12
               DC    AL4(0),CL35'SETSMF'                  13
               DC    AL4(0),CL35'SETMPF'                  14
               DC    AL4(0),CL35'SET SMF (RESTART SMF)'   15
               DC    AL4(0),CL35'SET DAE'                 16
               DC    AL4(0),CL35'SET PFK'                 17
               DC    AL4(0),CL35'SET GRSRNL'              18
               DC    AL4(0),CL35'SET APPC'                19
               DC    AL4(0),CL35'SET SCHPRM'              20
               DC    AL4(0),CL35'SET SCH'                 21
               DC    AL4(0),CL35'SET CNGRP'               22
               DC    AL4(0),CL35'WLM service definition'  23
               DC    AL4(0),CL35'WLM service policy activation'
               DC    AL4(0),CL35'WLM mode change'         25
               DC    AL4(0),CL35'IPL LOGREC'              26
               DC    AL4(0),CL35'ARM restarts enabled'    27
               DC    AL4(0),CL35'ARM restarts disabled'   28
               DC    AL4(0),CL35'CSV LNKLST set activated'
               DC    AL4(0),CL35'WLM reset'               30
               DC    AL4(0),CL35'Add/remove from LPA'     31
               DC    AL4(0),CL35'WLM scheduling environment'
TID_TABLE_L    EQU   *-TID_TABLE

               LTORG

MSG_01         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Product name/version'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_01_PNM     DC    CL8' '
               DC    C'/'
MSG_01_RVN     DC    CL2' '
MSG_01_L       EQU   *-MSG_01

MSG_02         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Record sub-type  '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_02_TID     DC    X'40202120'
               DC    CL3' '
MSG_02_DESC    DC    CL35'Unknown'
MSG_02_L       EQU   *-MSG_02

T6_MSG_01      DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Old SMF dataset'
               DC    AL1($SCREEN_ATTR_NORMAL)
T6_MSG_01_SWO  DC    CL10' '
T6_MSG_01_L    EQU   *-T6_MSG_01

T6_MSG_02      DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'New SMF dataset'
               DC    AL1($SCREEN_ATTR_NORMAL)
T6_MSG_02_SWN  DC    CL10' '
T6_MSG_02_L    EQU   *-T6_MSG_02

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
WK0090         DSECT
               COPY     DXDPREF
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               SESSION
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
TID            DSECT
TID_ADDR       DS       AL4
TID_DESC       DS       CL35
TID_L          EQU      *-TID
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
SMF_RECORD     DSECT
               IFASMFR  90
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     REGEQU
               END      SMF0090
