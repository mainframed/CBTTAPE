*---------------------------------------------------------------------*
*                                                                     *
*         Format SMF record type 6                                    *
*                                                                     *
*                                                                     *
*   At entry                                                          *
*       R15    program base address                                   *
*       R14    return address                                         *
*       R9     OSSPFD address                                         *
*       R8     VDATA buffer address                                   *
*       R7     SMF record address +4 (does not include length)        *
*                                                                     *
* ------------------------------------------------------------------- *
SMF0006  CSECT
SMF0006  AMODE 31
SMF0006  RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING SMF0006,R15
         USING OSSPFD,R9
         B     INIT0000
MODID    DC    CL8'SMF0006'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)       SAVE REGS
         LR    R10,R15               COPY ENTRY POINT
         DROP  R15
         USING SMF0006,R10           DEFINE BASE
         L     R1,SESS_DXD_ADDR
         A     R1,DXDSTART
         ST    R13,4(R1)             SAVE R13
         ST    R1,8(R13)             CHAIN SAVE AREA
         LR    R13,R1                COPY WORK AREA ADDRESS
         USING WK0006,R13            DEFINE BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0010 DS    0H
         USING SMF6FLG,R7
         A     R8,SPF_VWIDTH              LEAVE A BLANK LINE
         BCTR  R6,0

         MVC   0(MSG_01_L,R8),MSG_01
         USING MSG_01,R8
         MVC   MSG_01_JBN,SMF6JBN         COPY JOB NAME
         MVC   MSG_01_JBID,SMF6JNM        COPY JOB NUMBER
         MVC   MSG_01_UIF,SMF6UIF         COPY USER
         A     R8,SPF_VWIDTH              NEXT LINE
         BCTR  R6,0
         A     R8,SPF_VWIDTH              NEXT LINE
         BCTR  R6,0
         DROP  R8
* ------------------------------------------------------------------- *
*        Process reader start date/time                               *
* ------------------------------------------------------------------- *
         MVC   0(MSG_02_L,R8),MSG_02
         USING MSG_02,R8                  DEFINE BASE
         MVC   DXD_DATE,SMF6RSD           COPY DATE
         AP    DXD_DATE,P1900000           ADD CENTURY
         MVC   DXD_A(L'EDIT_WORD_2),EDIT_WORD_2
         ED    DXD_A(L'EDIT_WORD_2),DXD_DATE
         MVC   MSG_02_RSD,DXD_A+1         COPY READER START DATE
         ICM   R1,15,SMF6RST              READER START TIME
         BAL   R15,TIME0000               CONVERT TIME
         MVC   MSG_02_RST,DXD_B           COPY READER START TIME
         A     R8,SPF_VWIDTH              NEXT LINE
         BCTR  R6,0
         DROP  R8
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
         MVC   0(MSG_03_L,R8),MSG_03
         USING MSG_03,R8                  DEFINE BASE
         ICM   R1,15,SMF6NLR              NUMBER OF LOGICAL RECORDS
         CVD   R1,COMM_DWORD              CONVERT TO DECIMAL
         ED    MSG_03_NLR,COMM_DWORD+3    EDIT NUMBER OF RECORDS
         SR    R1,R1                      CLEAR REGISTER
         IC    R1,SMF6NDS                 NUMBER OF DATA SETS
         CVD   R1,COMM_DWORD              CONVERT TO DECIMAL
         ED    MSG_03_NDS,COMM_DWORD+6    EDIT NUMBER OF DATA SETS
         LA    R1,MSG_03_STATUS
         TM    SMF6IOE,X'04'              DATA BUFFER READ ERROR?
         BNO   MAIN0030                   NO
         MVC   0(L'STATUS_1,R1),STATUS_1  COPY IN STATUS MSG
         LA    R1,L'STATUS_1+1(R1)        UPDATE OUTPUT ADDRESS
MAIN0030 DS    0H
         TM    SMF6IOE,X'01'              CONTROL BUFFER READ ERROR?
         BNO   MAIN0040                   NO
         MVC   0(L'STATUS_2,R1),STATUS_2  COPY STATUS MSG
MAIN0040 DS    0H
         A     R8,SPF_VWIDTH
         BCTR  R6,0
         DROP  R8
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
         MVC   0(MSG_04_L,R8),MSG_04
         USING MSG_04,R8                  DEFINE BASE
         MVC   MSG_04_DEVICE,SMF6OUT      OUTPUT DEVICE NAME
         MVC   MSG_04_GROUP,SMF6GRP       OUTPUT DEVICE GROUP
         MVC   MSG_04_FCB,SMF6FCB         FCB NAME
         MVC   MSG_04_UCS,SMF6UCS         UCS IMAGE NAME
         A     R8,SPF_VWIDTH
         BCTR  R6,0
         DROP  R8
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
         MVC   0(MSG_05_L,R8),MSG_05
         USING MSG_05,R8                  DEFINE BASE
         ICM   R1,15,SMF6PGE              NUMBER OF PAGES
         CVD   R1,COMM_DWORD              CONVERT TO DECIMAL
         ED    MSG_05_PAGES,COMM_DWORD+3  EDIT NUMBER OF PAGES
         SR    R1,R1                      CLEAR REGISTER
         ICM   R1,3,SMF6OPR               PRIORITY
         CVD   R1,COMM_DWORD              CONVERT TO DECIMAL
         ED    MSG_05_PRIORITY,COMM_DWORD+5
         A     R8,SPF_VWIDTH
         BCTR  R6,0
         DROP  R8
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
         TM    SMF6PAD1,X'40'             COMMON SECTION?
         BO
*---------------------------------------------------------------------*
*        Process 3800 printing subsystem section                      *
*---------------------------------------------------------------------*
         LA    R1,SMF6
         MVC   0(MSG_06_L,R8),MSG_06
         USING MSG_06,R8                  DEFINE BASE
         LA    R1,MSG_06_COPIES
         LA    R2,SMF
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
TIME0000 DS    0H
         SR    R0,R0
         D     R0,F100               CONVERT TO WHOLE SECONDS
         SR    R0,R0                 CLEAR REMAINDER
         D     R0,F3600              COMPUTE HOURS
         CVD   R1,COMM_DWORD         CONVERT HOURS TO DECIMAL
         MVC   DXD_A(L'EDIT_WORD_1),EDIT_WORD_1
         ED    DXD_A(L'EDIT_WORD_1),COMM_DWORD+6
         MVC   DXD_B+0(2),DXD_A+2    COPY HOURS
         MVI   DXD_B+2,C':'          INSERT COLON
         LR    R1,R0                 COPY REMAINDER
         SR    R0,R0                 CLEAR R0
         D     R0,F60                COMPUTE MINUTES
         CVD   R1,COMM_DWORD         CONVERT MINUTES TO DECIMAL
         MVC   DXD_A(L'EDIT_WORD_1),EDIT_WORD_1
         ED    DXD_A(L'EDIT_WORD_1),COMM_DWORD+6
         MVC   DXD_B+3(2),DXD_A+2    COPY MINUTES
         MVI   DXD_B+5,C':'          INSERT COLON
         CVD   R0,COMM_DWORD         CONVERT SECONDS TO DECIMAL
         MVC   DXD_A(L'EDIT_WORD_1),EDIT_WORD_1
         ED    DXD_A(L'EDIT_WORD_1),COMM_DWORD+6
         MVC   DXD_B+6(2),DXD_A+2    COPY SECONDS
         BR    R15
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
EXIT0000 DS    0H
         SR    R2,R2
         B     EXITEXIT
EXIT0004 DS    0H
         LA    R2,4
EXITEXIT DS    0H
         ITRACE ID=EXIT,                                               +
               RDATA1=R2
         L     R13,4(R13)            RESTORE SAVE AREA ADDRESS
         L     R14,12(,R13)          RESTORE R14
         LR    R15,R2                SET RC
         LM    R0,R12,20(R13)        RESTORE REMAINING REGISTERS
         BR    R14                   RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDSTART       DC    Q(WK0006)
F60            DC    F'60'
F100           DC    F'100'
F3600          DC    F'3600'

P1900000       DC    P'1900000'

EDIT_WORD_1    DC    X'F0202020'
EDIT_WORD_2    DC    X'F0202020204B202020'

STATUS_1       DC    C'Data read error'
STATUS_2       DC    C'Control read error'

MSG_01         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'  Job name:'
MSG_01_JBN     DC    CL8' '
               DC    C'  Job number:'
MSG_01_JBID    DC    CL8' '
               DC    C'  User:'
MSG_01_UIF     DC    CL8' '
MSG_01_L       EQU   *-MSG_01

MSG_02         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Reader start date/time   '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_02_RSD     DC    CL8' '
               DC    C' '
MSG_02_RST     DC    CL8' '
MSG_02_L       EQU   *-MSG_02

MSG_03         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Records'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_03_NLR     DC    X'402020206B2020206B202120'
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Datasets'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_03_NDS     DC    X'40202120'
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'I/O status'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_03_STATUS  DC    CL30' '
MSG_03_L       EQU   *-MSG_03

MSG_04         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Device'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_04_DEVICE  DC    CL8' '
               DC    C' '
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Device group'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_04_GROUP   DC    CL8' '
               DC    C' '
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'FCB'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_04_FCB     DC    CL4' '
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'UCS'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_04_UCS     DC    CL4' '
MSG_04_L       EQU   *-MSG_04

MSG_05         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Pages'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_05_PAGES   DC    X'402020206B4040406B202120'
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Priority'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_05_PRIORITY DC   X'40202120'
MSG_05_L       EQU   *-MSG_05

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
WK0006         DSECT
               COPY     DXDPREF
DXD_A          DS       CL80
DXD_B          DS       CL80
DXD_DATE       DS       PL4
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              SESSION
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              BPXYSTAT  DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              OSSPFD    TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
SMF_RECORD    DSECT
              IFASMFR   6
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COPY      ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COPY      TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COPY      REGEQU
              END       SMF0006
