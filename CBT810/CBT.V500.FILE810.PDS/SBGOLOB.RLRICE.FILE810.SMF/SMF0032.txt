*---------------------------------------------------------------------*
*                                                                     *
*         Format SMF record type 32 (TSO user work accounting)        *
*                                                                     *
*                                                                     *
*   At entry                                                          *
*       R15    program base address                                   *
*       R14    return address                                         *
*       R9     OSSPFD address                                         *
*       R8     VDATA buffer address                                   *
*       R7     SMF record address +4 (does not include length)        *
*                                                                     *
* ------------------------------------------------------------------- *
SMF0032  CSECT
SMF0032  AMODE 31
SMF0032  RMODE ANY
         USING SESSION,R11
         USING SMF0032,R15
         B     INIT0000
MODID    DC    CL8'SMF0032'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         USING OSCOMM,R12                     DEFINE COMMON AREA BASE
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT
         DROP  R15
         USING SMF0032,R10                    DEFINE BASE
         L     R1,SESS_DXD_ADDR
         A     R1,DXDSTART
         ST    R13,4(R1)                      SAVE R13
         ST    R1,8(R13)                      CHAIN SAVE AREA
         LR    R13,R1                         COPY WORK AREA ADDRESS
         USING WK0032,R13                     DEFINE BASE
         USING OSSPFD,R9                      DEFINE BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0000 DS    0H
         USING SMF32FLG,R7
         A     R8,SPF_VWIDTH                  LEAVE A BLANK LINE
         BCTR  R6,0

*---------------------------------------------------------------------*
*        PROCESS PRODUCT SECTION(S)                                   *
*---------------------------------------------------------------------*
         MVC   0(MSG_01_L,R8),MSG_01
         A     R8,SPF_VWIDTH                  NEXT LINE
         BCTR  R6,0
MAIN0010 DS    0H
         SR    R3,R3
         ICM   R3,3,SMF32PON                  NBR OF PRODUCT SECTIONS
         BZ    MAIN0100
         ICM   R4,15,SMF32POF
         AR    R4,R7                          PLUS BASE
         SH    R4,H4                          MINUS RDW LENGTH
         USING SMF32PSS,R4
MAIN0020 DS    0H
         MVC   0(MSG_02_L,R8),MSG_02
         USING MSG_02,R8
         MVC   MSG_02_PRODUCT,SMF32PNM        COPY PRODUCT NAME
         MVC   MSG_02_VERSION,SMF32RVN        COPY VERSION
         MVC   WORK_TYPE,SMF32TYP+1
         TM    SMF32TYP+1,1                   INTERVAL?
         BO    MAIN0030                       YES
         TM    SMF32TYP+1,2                   SESSION END?
         BO    MAIN0040                       YES
         TM    SMF32TYP+1,3                   INTERVAL WITH DETAIL?
         BO    MAIN0050                       YES
         TM    SMF32TYP+1,4                   SESSION END WITH DETAIL?
         BO    MAIN0060
         B     MAIN0070
MAIN0030 DS    0H
         MVC   MSG_02_TYPE,TYPE_INTERVAL
         B     MAIN0070
MAIN0040 DS    0H
         MVC   MSG_02_TYPE,TYPE_END
         B     MAIN0070
MAIN0050 DS    0H
         MVC   MSG_02_TYPE,TYPE_INTERVAL_DETAIL
         B     MAIN0070
MAIN0060 DS    0H
         MVC   MSG_02_TYPE,TYPE_END_DETAIL
MAIN0070 DS    0H
         A     R8,SPF_VWIDTH
         BCT   R6,MAIN0080
         B     EXIT0000                       SCREEN IS FULL
MAIN0080 DS    0H
         AH    R4,SMF32PLN                    NEXT SECTION
         BCT   R3,MAIN0020
         DROP  R4,R8
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0100 DS    0H
         A     R8,SPF_VWIDTH
         BCTR  R6,0
         MVC   0(MSG_03_L,R8),MSG_03
         A     R8,SPF_VWIDTH                  NEXT LINE
         BCTR  R6,0
         ICM   R3,3,SMF32ION                  NUMBER OF SECTIONS
         BZ    MAIN0200
         ICM   R4,15,SMF32IOF                 OFFSET TO IDENTIFICATION
         AR    R4,R7
         SH    R4,H4                          MINUS LENGTH OF RDW
         USING SMF32ID,R4                     DEFINE BASE
MAIN0110 DS    0H
         MVC   0(MSG_04_L,R8),MSG_04
         USING MSG_04,R8
         MVC   MSG_04_JBN,SMF32JBN            COPY JOB NAME
         MVC   MSG_04_STM,SMF32STM            COPY STEP NAME
         SR    R1,R1
         ICM   R1,3,SMF32STN                  STEP NUMBER
         CVD   R1,COMM_DWORD
         ED    MSG_04_STN,COMM_DWORD+4
         MVC   MSG_04_PGM,SMF32PGM            COPY PROGRAM NAME
         DROP  R8
         A     R8,SPF_VWIDTH
         BCT   R6,MAIN0120
         B     EXIT0000
MAIN0120 DS    0H
         MVC   0(MSG_05_L,R8),MSG_05
         USING MSG_05,R8
         MVC   MSG_05_RUD,SMF32RUD            COPY RACF USER ID
         MVC   MSG_05_GRP,SMF32GRP            COPY RACF GROUP
         MVC   MSG_05_USR,SMF32USR            COPY USER'S NAME
         DROP  R8
         A     R8,SPF_VWIDTH
         BCT   R6,MAIN0130
         B     EXIT0000                       SCREEN IS FULL
MAIN0130 DS    0H
         AH    R4,SMF32ILN                    NEXT SECTION
         BCT   R3,MAIN0110                    LOOP
         DROP  R4

*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN0200 DS    0H
         A     R8,SPF_VWIDTH
         BCTR  R6,0
         MVC   0(MSG_06_L,R8),MSG_06
         A     R8,SPF_VWIDTH                  NEXT LINE
         BCTR  R6,0
         MVC   0(MSG_07_L2,R8),MSG_07
         A     R8,SPF_VWIDTH                  NEXT LINE
         BCTR  R6,0
         ICM   R3,3,SMF32CON                  NUMBER OF SECTIONS
         BZ    MAIN0200
         ICM   R4,15,SMF32COF                 OFFSET TO IDENTIFICATION
         AR    R4,R7
         SH    R4,H4                          MINUS LENGTH OF RDW
         USING SMF32CDS,R4                    DEFINE BASE
MAIN0210 DS    0H
         CLI   WORK_TYPE,X'02'                TYPE 3 OR 4?
         BH    MAIN0220                       YES
         MVC   0(MSG_07_L1,R8),MSG_07
         B     MAIN0230
MAIN0220 DS    0H
         MVC   0(MSG_07_L2,R8),MSG_07
MAIN0230 DS    0H
         USING MSG_07,R8
         MVI   0(R8),$SCREEN_ATTR_NORMAL
         MVC   MSG_07_CMD,SMF32CMD            COPY COMMAND
         ICM   R1,15,SMF32CNT                 USES
         CVD   R1,COMM_DWORD
         MVC   MSG_07_CNT,EDIT_WORD_1         COPY EDIT WORD
         ED    MSG_07_CNT,COMM_DWORD+4        EDIT TIMES USED
         CLI   WORK_TYPE,X'03'                TYPE 3 OR 4?
         BL    MAIN0240                       NO
         ICM   R1,15,SMF32TCB                 TCB TIME
         CVD   R1,COMM_DWORD
         MVC   MSG_07_TCB,EDIT_WORD_1         COPY EDIT WORD
         ED    MSG_07_TCB,COMM_DWORD+4
         ICM   R1,15,SMF32SRB                 SRB TIME
         CVD   R1,COMM_DWORD
         MVC   MSG_07_SRB,EDIT_WORD_1         COPY EDIT WORD
         ED    MSG_07_SRB,COMM_DWORD+4
         ICM   R1,15,SMF32TGT                 TGETS
         CVD   R1,COMM_DWORD
         MVC   MSG_07_TGT,EDIT_WORD_1         COPY EDIT WORD
         ED    MSG_07_TGT,COMM_DWORD+4
         ICM   R1,15,SMF32TPT                 TPUTS
         CVD   R1,COMM_DWORD
         MVC   MSG_07_TPT,EDIT_WORD_1         COPY EDIT WORD
         ED    MSG_07_TPT,COMM_DWORD+4
         ICM   R1,15,SMF32EXP                 EXCPS
         CVD   R1,COMM_DWORD
         MVC   MSG_07_EXP,EDIT_WORD_1         COPY EDIT WORD
         ED    MSG_07_EXP,COMM_DWORD+4
         ICM   R1,15,SMF32TCT                 DEVICE CONNECT TIME
         CVD   R1,COMM_DWORD
         MVC   MSG_07_TCT,EDIT_WORD_1         COPY EDIT WORD
         ED    MSG_07_TCT,COMM_DWORD+4
MAIN0240 DS    0H
         A     R8,SPF_VWIDTH                  NEXT LINE ON SCREEN
         BCT   R6,MAIN0250
         B     EXIT0000                       SCREEN FULL
MAIN0250 DS    0H
         AH    R4,SMF32CLN                    PLUS LENGTH OF EACH
         BCT   R3,MAIN0210

         B     EXIT0000
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
EXIT0000 DS    0H
         SR    R2,R2
         ITRACE ID=EXIT,                                               +
               RDATA1=R2
         L     R13,4(R13)                     RESTORE SAVE AREA ADDRESS
         L     R14,12(,R13)                   RESTORE R14
         LR    R15,R2                         SET RC
         LM    R0,R12,20(R13)                 RESTORE REGISTERS
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDSTART       DC    Q(WK0032)

H4             DC    H'4'

EDIT_WORD_1    DC    X'4020202020202120'

TYPE_INTERVAL        DC CL25'Interval'
TYPE_END             DC CL25'Session end'
TYPE_INTERVAL_DETAIL DC CL25'Interval with detail'
TYPE_END_DETAIL      DC CL25'Session end with detail'

MSG_01         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    32C'-'
               DC    C'Product section'
               DC    32C'-'
MSG_01_L       EQU   *-MSG_01

MSG_02         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Product       '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_02_PRODUCT DC    CL8' '
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'  Version'      '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_02_VERSION DC    CL2' '
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'  Type'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_02_TYPE    DC    CL25' '
MSG_02_L       EQU   *-MSG_02

MSG_03         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    23C'-'
               DC    C'Identification section'
               DC    34C'-'
MSG_03_L       EQU   *-MSG_03


MSG_04         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Job name '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_04_JBN     DC    CL8' '
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'  Step number '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_04_STN     DC    X'4020202020202120'
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'  Step name   '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_04_STM     DC    CL8' '
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'  Program name'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_04_PGM     DC    CL8' '
MSG_04_L       EQU   *-MSG_04

MSG_05         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'RACF ID  '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_05_RUD     DC    CL8' '
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'  RACF Group  '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_05_GRP     DC    CL8' '
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'  User''s name '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_05_USR     DC    CL8' '
MSG_05_L       EQU   *-MSG_05

MSG_06         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    32C'-'
               DC    C'Command section'
               DC    33C'-'
MSG_06_L       EQU   *-MSG_06

MSG_07         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
MSG_07_CMD     DC    CL8'COMMAND '
               DC    CL2' '
MSG_07_CNT     DC    CL8'    USES'
MSG_07_L1      EQU   *-MSG_07
               DC    CL2' '
MSG_07_TCB     DC    CL8'TCB TIME'
               DC    CL2' '
MSG_07_SRB     DC    CL8'SRB TIME'
               DC    CL2' '
MSG_07_TGT     DC    CL8'   TGETS'
               DC    CL2' '
MSG_07_TPT     DC    CL8'   TPUTS'
               DC    CL2' '
MSG_07_EXP     DC    CL8'   EXCPS'
               DC    CL2' '
MSG_07_TCT     DC    CL8'DEV CONNECT'
MSG_07_L2      EQU   *-MSG_07
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
WK0032         DSECT
               COPY     DXDPREF
WORK_TYPE      DS       X
DXD_A          DS       CL80
DXD_B          DS       CL80
DXD_DATE       DS       PL4
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              SESSION
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
SMF_RECORD    DSECT
              IFASMFR  32
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COPY     REGEQU
              END      SMF0032
