*---------------------------------------------------------------------*
*                                                                     *
*         Format SMF record type 80                                   *
*         RACF operator record                                        *
*                                                                     *
*   At entry                                                          *
*       R15    program base address                                   *
*       R14    return address                                         *
*       R9     OSSPFD address                                         *
*       R8     VDATA buffer address                                   *
*       R7     SMF record address +4 (does not include length)        *
*                                                                     *
* ------------------------------------------------------------------- *
SMF0080  CSECT
SMF0080  AMODE 31
SMF0080  RMODE ANY
         USING SESSION,R11
         USING SMF0080,R15
         B     INIT0000
MODID    DC    CL8'SMF0080'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         USING OSCOMM,R12            DEFINE COMMON AREA BASE
         STM   R14,R12,12(R13)       SAVE REGS
         LR    R10,R15               COPY ENTRY POINT
         USING SMF0080,R10           DEFINE BASE
         DROP  R15
         L     R1,SESS_DXD_ADDR
         A     R1,DXDSTART
         ST    R13,4(R1)             SAVE R13
         ST    R1,8(R13)             CHAIN SAVE AREA
         LR    R13,R1                COPY WORK AREA ADDRESS
         USING WK0080,R13            DEFINE BASE
         MVC   DXD_CSECT,MODID
         USING OSSPFD,R9             DEFINE BASE
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
         ITRACE ID=ENTRY
         USING SMF80FLG,R7
         MVC   0(MSG_01_L,R8),MSG_01
         USING MSG_01,R8
         MVC   MSG_01_USER,SMF80USR
         MVC   MSG_01_GROUP,SMF80GRP
         MVC   MSG_01_JOB,SMF80JBN
         OC    SMF80TRM,SMF80TRM
         BZ    MAIN0010
         MVC   MSG_01_TERM,SMF80TRM
         DROP  R8
MAIN0010 DS    0H
         A     R8,SPF_VWIDTH
         BCTR  R6,0
         MVC   0(MSG_02_L,R8),MSG_02
         USING MSG_02,R8
         TM    SMF80DES,X'80'        VIOLATION?
         BNO   MAIN0020              NO
         MVC   MSG_02_DESC(DESC1_L),DESC1
         A     R8,SPF_VWIDTH
         BCTR  R6,0
MAIN0020 DS    0H
         TM    SMF80DES,X'40'        UNDEFINED USER?
         BNO   MAIN0030              NO
         MVC   MSG_02_DESC(DESC2_L),DESC2
         A     R8,SPF_VWIDTH
         BCTR  R6,0
MAIN0030 DS    0H
         TM    SMF80DES,X'10'        WARNING?
         BNO   MAIN0040              NO
         MVC   MSG_02_DESC(DESC3_L),DESC3
MAIN0040 DS    0H
         A     R8,SPF_VWIDTH
         BCTR  R6,0
         DROP  R8
MAIN0050 DS    0H
         L     R1,=A(EVENTS)
         USING EVENT_DSECT,R1
MAIN0060 DS    0H
         CLI   EVENT_CODE,X'FF'
         BE    MAIN0070
         CLC   EVENT_CODE,SMF80EVT
         BE    MAIN0070
         LA    R1,EVENT_L(,R1)
         B     MAIN0060
MAIN0070 DS    0H
         MVC   0(MSG_03_L,R8),MSG_03
         USING MSG_03,R8
         SR    R0,R0
         IC    R0,SMF80EVT
         CVD   R0,COMM_DWORD
         ED    MSG_03_EVENT,COMM_DWORD+6
         MVC   MSG_03_DESC,EVENT_DESC
         DROP  R8
         A     R8,SPF_VWIDTH
         BCTR  R6,0

         ICM   R1,15,EVENT_QUAL
         DROP  R1
         BZ    MAIN0100
         USING QUAL_DSECT,R1
         MVC   0(MSG_04_L,R8),MSG_04
         USING MSG_04,R8
MAIN0080 DS    0H
         CLI   QUAL_CODE,X'FF'
         BE    MAIN0090
         CLC   SMF80EVQ,QUAL_CODE
         BE    MAIN0090
         LA    R1,QUAL_L(R1)
         B     MAIN0080
MAIN0090 DS    0H
         MVC   MSG_04_DESC,QUAL_DESC
         A     R8,SPF_VWIDTH
         BCTR  R6,0
         DROP  R1,R8

MAIN0100 DS    0H
         ITRACE ID=MSG_05,                                             +
               DATA1=(SMF80ATH,1)
         MVC   0(MSG_05_L,R8),MSG_05
         USING MSG_05,R8
         TM    SMF80ATH,X'80'           NORMAL?
         BNO   MAIN0110                 NO
         ITRACE ID=NORMAL
         MVC   MSG_05_AUTH,AUTH_NORMAL
         A     R8,SPF_VWIDTH
         BCTR  R6,0
MAIN0110 DS    0H
         TM    SMF80ATH,X'40'           SPECIAL?
         BNO   MAIN0120
         ITRACE ID=SPECIAL
         MVC   MSG_05_AUTH,AUTH_SPECIAL
         A     R8,SPF_VWIDTH
         BCTR  R6,0
MAIN0120 DS    0H
         TM    SMF80ATH,X'20'           OPERATIONS?
         BNO   MAIN0130
         ITRACE ID=OPERATNS
         MVC   MSG_05_AUTH,AUTH_OPERATIONS
         A     R8,SPF_VWIDTH
         BCTR  R6,0
MAIN0130 DS    0H
         TM    SMF80ATH,X'10'           AUDITOR?
         BNO   MAIN0140
         ITRACE ID=AUDITOR
         MVC   MSG_05_AUTH,AUTH_AUDITOR
         A     R8,SPF_VWIDTH
         BCTR  R6,0
MAIN0140 DS    0H
         TM    SMF80ATH,X'08'           INSTALLATION EXIT
         BNO   MAIN0150
         ITRACE ID=INSTEXIT
         MVC   MSG_05_AUTH,AUTH_EXIT
         A     R8,SPF_VWIDTH
         BCTR  R6,0
MAIN0150 DS    0H
         TM    SMF80ATH,X'04'           FAILSOFT?
         BNO   MAIN0160                 NO
         ITRACE ID=FAILSOFT
         MVC   MSG_05_AUTH,AUTH_FAILSOFT
         A     R8,SPF_VWIDTH
         BCTR  R6,0
MAIN0160 DS    0H
         TM    SMF80ATH,X'02'           BYPASSED
         BNO   MAIN0170                 NO
         ITRACE ID=BYPASSED
         MVC   MSG_05_AUTH,AUTH_BYPASSED
         A     R8,SPF_VWIDTH
         BCTR  R6,0
MAIN0170 DS    0H
         TM    SMF80ATH,X'01'           TRUSTED
         BNO   MAIN0180                 NO
         ITRACE ID=TRUSTED
         MVC   MSG_05_AUTH,AUTH_TRUSTED
         A     R8,SPF_VWIDTH
         BCTR  R6,0
         DROP  R8
MAIN0180 DS    0H
         A     R8,SPF_VWIDTH
         BCTR  R6,0
*---------------------------------------------------------------------*
*        Process relocate section(s)                                  *
*---------------------------------------------------------------------*
MAIN1000 DS    0H
         SR    R2,R2                 CLEAR REGISTER
         ICM   R2,3,SMF80CNT         NUMBER OF RELOCATE SECTIONS
         BZ    MAIN1060              NO RELOCATE DATA
         LH    R3,SMF80REL           OFFSET TO FIRST SECTION
         AR    R3,R7                 PLUS BASE
         SH    R3,H4                 BACK UP TO RECORD PREFIX
         USING SMF80DTP,R3
         ITRACE ID=PROCRELO,                                           +
               RDATA1=R3,                                              +
               RDATA2=R2
MAIN1010 DS    0H
         L     R1,=A(SUBTYPES)
         USING SUBTYPE_DSECT,R1
MAIN1020 DS    0H
         CLI   0(R1),X'FF'           END OF TABLE?
         BE    MAIN1050              YES
         CLC   SUBTYPE_CODE,SMF80DTP THIS SUBTYPE?
         BE    MAIN1030              YES
         LA    R1,SUBTYPE_L(R1)
         B     MAIN1020              LOOP
MAIN1030 DS    0H
         MVC   0(MSG_06_L,R8),MSG_06
         USING MSG_06,R8
         MVC   MSG_06_DESC,SUBTYPE_DESC
         ICM   R15,15,SUBTYPE_PROC
         BZ    MAIN1040
         ITRACE ID=PROC,                                               +
               DATA1=SMF80DTP,                                         +
               RDATA2=R15
         BALR  R14,R15
MAIN1040 DS    0H
         A     R8,SPF_VWIDTH
         BCT   R6,MAIN1050           NEXT SECTION
         B     EXIT0000              SCREEN FULL.. STOP
MAIN1050 DS    0H
         SR    R1,R1
         IC    R1,SMF80DLN           LENGTH OF DATA
         LA    R3,2(R3,R1)
         BCT   R2,MAIN1010
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN1060 DS    0H
         SR    R2,R2
         ICM   R2,3,SMF80CT2         ANY EXTENDED LENGTH AREA(S)?
         BZ    MAIN1140              NO
         MVC   0(EXTENDED_HEADING_L,R8),EXTENDED_HEADING
         A     R8,SPF_VWIDTH
         BCT   R6,MAIN1070
         B     EXIT0000
MAIN1070 DS    0H
         LH    R3,SMF80RL2           OFFSET TO EXTENDED LENGTH DATA
         AR    R3,R7                 PLUS BASE
*        SH    R3,H4                 MINUS LLBB LENGTH
         USING SMF80TP2,R3           DEFINE BASE
MAIN1100 DS    0H
         L     R1,=A(EXTENDED)       EXTENDED SECTION TABLE
         USING EXTENDED_DSECT,R1
         ITRACE ID=SMF80TP2,                                           +
               DATA1=SMF80TP2
MAIN1110 DS    0H
         ITRACE ID=FIND_EXT,                                           +
               RDATA1=R1,                                              +
               DATA2=0(R1)
         CLI   0(R1),X'FF'           END OF TABLE?
         BE    MAIN1130              YES
         CLC   EXTENDED_CODE,SMF80TP2
         BE    MAIN1120
         LA    R1,EXTENDED_L(R1)     NEXT ENTRY
         B     MAIN1110
MAIN1120 DS    0H
         ICM   R15,15,EXTENDED_PROC
         ITRACE ID=CALL_EXT,                                           +
               RDATA1=R15
         BALR  R14,R15               LINK
MAIN1130 DS    0H
         SR    R1,R1
         ICM   R1,3,SMF80DL2
         LA    R3,4(R3,R1)           PLUS PREFIX LENGTH
         BCT   R2,MAIN1100           LOOP
         B     EXIT0000
         DROP  R1,R8
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN1140 DS    0H
         A     R8,SPF_VWIDTH
         BCT   R6,MAIN1150
         B     EXIT0000
MAIN1150 DS    0H
         MVC   0(MSG_07_L,R8),MSG_07
         B     EXIT0000
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
MAIN2000 DS    0H
         DROP  R7
         USING MSG_06,R8
         USING SMF80DTP,R3
         SR    R1,R1
         ICM   R1,1,SMF80DLN         DATA LENGTH
         BZR   R14
         CH    R1,=Y(L'MSG_06_VAL)
         BNH   MAIN2010
         LH    R1,=Y(L'MSG_06_VAL)
MAIN2010 DS    0H
         BCTR  R1,0
         EX    R1,MVC_01
         BR    R14
MVC_01   MVC   MSG_06_VAL(0),SMF80DTA

MAIN2020 DS    0H
         TM    SMF80DTA,X'80'        ALTER?
         BO    MAIN2030              YES
         TM    SMF80DTA,X'40'        CONTROL?
         BO    MAIN2040              YES
         TM    SMF80DTA,X'20'        UPDATE?
         BO    MAIN2050              YES
         TM    SMF80DTA,X'10'        READ?
         BO    MAIN2060              YES
         TM    SMF80DTA,X'08'        NONE?
         BO    MAIN2070              YES
         TM    SMF80DTA,X'02'        WRITE?
         BO    MAIN2080              YES
         BR    R14
MAIN2030 DS    0H
         MVC   MSG_06_VAL(5),=C'ALTER'
         BR    R14
MAIN2040 DS    0H
         MVC   MSG_06_VAL(7),=C'CONTROL'
         BR    R14
MAIN2050 DS    0H
         MVC   MSG_06_VAL(6),=C'UPDATE'
         BR    R14
MAIN2060 DS    0H
         MVC   MSG_06_VAL(4),=C'READ'
         BR    R14
MAIN2070 DS    0H
         MVC   MSG_06_VAL(4),=C'NONE'
         BR    R14
MAIN2080 DS    0H
         MVC   MSG_06_VAL(5),=C'WRITE'
         BR    R14

MAIN2100 DS    0H
         DROP  R3
         USING SMF80FLG,R7
         CLI   SMF80EVQ,29           DIRECTORY AUTH?
         BE    MAIN2170
         DROP  R7
         USING SMF80DTP,R3
         TM    SMF80DTA,X'80'        ALTER?
         BO    MAIN2110              YES
         TM    SMF80DTA,X'40'        CONTROL?
         BO    MAIN2120              YES
         TM    SMF80DTA,X'20'        UPDATE?
         BO    MAIN2130              YES
         TM    SMF80DTA,X'10'        READ?
         BO    MAIN2140              YES
         TM    SMF80DTA,X'08'        NONE?
         BO    MAIN2150              YES
         TM    SMF80DTA,X'04'        EXECUTE?
         BO    MAIN2160              YES
         BR    R14
MAIN2110 DS    0H
         MVC   MSG_06_VAL(5),=C'ALTER'
         BR    R14
MAIN2120 DS    0H
         MVC   MSG_06_VAL(7),=C'CONTROL'
         BR    R14
MAIN2130 DS    0H
         MVC   MSG_06_VAL(6),=C'UPDATE'
         BR    R14
MAIN2140 DS    0H
         MVC   MSG_06_VAL(4),=C'READ'
         BR    R14
MAIN2150 DS    0H
         MVC   MSG_06_VAL(4),=C'NONE'
         BR    R14
MAIN2160 DS    0H
         MVC   MSG_06_VAL(7),=C'EXECUTE'
         BR    R14
MAIN2170 DS    0H
         TM    SMF80DTA,X'80'        MANDATORY?
         BO    MAIN2180              YES
         TM    SMF80DTA,X'40'        REVERSE MANDATORY?
         BO    MAIN2190              YES
         TM    SMF80DTA,X'20'        EQUAL MANDATORY?
         BO    MAIN2200              YES
         BR    R14
MAIN2180 DS    0H
         MVC   MSG_06_VAL(9),=C'MANDATORY'
         BR    R14
MAIN2190 DS    0H
         MVC   MSG_06_VAL(17),=C'REVERSE MANDATORY'
         BR    R14
MAIN2200 DS    0H
         MVC   MSG_06_VAL(15),=C'EQUAL MANDATORY'
         BR    R14

MAIN2210 DS    0H
         SR    R1,R1                 CLEAR REGISTER
         IC    R1,SMF80DTA           DATA SET LEVEL
         CVD   R1,COMM_DWORD         CONVERT TO DECIMAL
         MVC   MSG_06_VAL(L'EDIT_WORD_1),EDIT_WORD_1
         ED    MSG_06_VAL(L'EDIT_WORD_1),COMM_DWORD+4
MAIN2215 DS    0H
         CLI   MSG_06_VAL,C' '       BLANK
         BNER  R14
         MVC   MSG_06_VAL(L'MSG_06_VAL-1),MSG_06_VAL+1
         MVI   MSG_06_VAL+(L'MSG_06_VAL-1),C' '
         B     MAIN2215
EDIT_WORD_1    DC     X'40202120'


MAIN2220 DS    0H
         MVC   MSG_06_VAL(6),SMF80DTA+1
         TM    SMF80DTA,X'80'
         BNOR  R14
         MVC   MSG_06_VAL+8,=C'Not processed'
         BR    R14

MAIN2230 DS    0H
         SR    R0,R0
         IC    R0,SMF80DLN           LENGTH OF DATA
         LA    R1,SMF80DTA           COMPUTE NUMBER OF ENTRIES
MAIN2240 DS    0H
         MVC   MSG_06_VAL(8),1(R1)   COPY USER ID
         A     R8,SPF_VWIDTH         NEXT LINE
         BCT   R6,MAIN2250
         B     EXIT0000              SCREEN FULL
MAIN2250 DS    0H
         SH    R0,H9                 MINUS LENGTH OF FLAG AND ID
         BZR   R14                   ALL ID'S PROCESSED
         MVC   0(80,R8),COMM_BLANKS
         LA    R1,9(,R1)             NEXT ID
         B     MAIN2240

MAIN2260 DS    0H
         MVC   MSG_06_VAL+00(6),SMF80DTA+0
         MVC   MSG_06_VAL+10(6),SMF80DTA+6
         BR    R14

MAIN2300 DS    0H
         MVC   MSG_06_VAL(8),SMF80DTA+1
         A     R8,SPF_VWIDTH
         BCT   R6,MAIN2310
         B     EXIT0000
MAIN2310 DS    0H
         TM    SMF80DTA,X'80'        STATISTICS IN EFFECT?
         BNO   MAIN2320              NO
         MVC   MSG_06_VAL(20),=C'Statistics in effect'
         A     R8,SPF_VWIDTH
         BCT   R6,MAIN2320
         B     EXIT0000
MAIN2320 DS    0H
         TM    SMF80DTA,X'40'        AUDITING IN EFFECT?
         BNO   MAIN2330              NO
         MVC   MSG_06_VAL(20),=C'Auditing in effect'
         A     R8,SPF_VWIDTH
         BCT   R6,MAIN2330
         B     EXIT0000
MAIN2330 DS    0H
         TM    SMF80DTA,X'20'        PROTECTION in EFFECT?
         BNO   MAIN2340              NO
         MVC   MSG_06_VAL(20),=C'Protection in effect'
         A     R8,SPF_VWIDTH
         BCT   R6,MAIN2340
         B     EXIT0000
MAIN2340 DS    0H
         TM    SMF80DTA,X'10'        GENERIC PROFILE IN EFFECT?
         BNO   MAIN2350              NO
         MVC   MSG_06_VAL(20),=C'Generic profile in effect'
         A     R8,SPF_VWIDTH
         BCT   R6,MAIN2350
         B     EXIT0000
MAIN2350 DS    0H
         TM    SMF80DTA,X'08'        GENERIC COMMAND IN EFFECT?
         BNO   MAIN2360              NO
         MVC   MSG_06_VAL(20),=C'Generic command in effect'
         A     R8,SPF_VWIDTH
         BCT   R6,MAIN2360
         B     EXIT0000
MAIN2360 DS    0H
         TM    SMF80DTA,X'04'        GLOBAL ACCESS CHECKING?
         BNO   MAIN2370              NO
         MVC   MSG_06_VAL(20),=C'Global access checking'
         A     R8,SPF_VWIDTH
         BCT   R6,MAIN2370
         B     EXIT0000
MAIN2370 DS    0H
         TM    SMF80DTA,X'02'        RACLIST IN EFFECT?
         BNO   MAIN2380              NO
         MVC   MSG_06_VAL(20),=C'RACLIST in effect'
         A     R8,SPF_VWIDTH
         BCT   R6,MAIN2380
         B     EXIT0000
MAIN2380 DS    0H
         TM    SMF80DTA,X'01'        GENLIST IN EFFECT?
         BNO   MAIN2390              NO
         MVC   MSG_06_VAL(20),=C'GENLIST in effect'
         A     R8,SPF_VWIDTH
         BCT   R6,MAIN2390
         B     EXIT0000
MAIN2390 DS    0H

MAIN2400 DS    0H
         TM    SMF80DTA,X'40'        LOGOPTIONS(ALWAYS)?
         BNO   MAIN2410              NO
         MVC   MSG_06_VAL(18),=C'LOGOPTIONS(ALWAYS)'
         A     R8,SPF_VWIDTH
         BCT   R6,MAIN2410
         B     EXIT0000
MAIN2410 DS    0H
         TM    SMF80DTA,X'20'        LOGOPTIONS(NEVER)?
         BNO   MAIN2420              NO
         MVC   MSG_06_VAL(17),=C'LOGOPTIONS(NEVER)'
         A     R8,SPF_VWIDTH
         BCT   R6,MAIN2420
         B     EXIT0000
MAIN2420 DS    0H
         TM    SMF80DTA,X'10'        LOGOPTIONS(SUCCESSES)?
         BNO   MAIN2430              NO
         MVC   MSG_06_VAL(20),=C'LOGOPTIONS(SUCCESSES)'
         A     R8,SPF_VWIDTH
         BCT   R6,MAIN2430
         B     EXIT0000
MAIN2430 DS    0H
         TM    SMF80DTA,X'08'        LOGOPTIONS(FAILURES)?
         BNO   MAIN2440              NO
         MVC   MSG_06_VAL(20),=C'LOGOPTIONS(FAILURES)'
         A     R8,SPF_VWIDTH
         BCT   R6,MAIN2440
         B     EXIT0000
MAIN2440 DS    0H
         TM    SMF80DTA,X'04'        LOGOPTIONS(DEFAULT)?
         BNOR  R14                   NO
         MVC   MSG_06_VAL(20),=C'LOGOPTIONS(DEFAULT)'
         A     R8,SPF_VWIDTH
         BCTR  R6,R14
         B     EXIT0000

MAIN2450 DS    0H
         SR    R15,R15
         IC    R15,SMF80DLN          DATA LENGTH
         SH    R15,H12
         BCTR  R15,0
         EX    R15,MVC_02
         A     R8,SPF_VWIDTH
         BCT   R6,MAIN2460
         B     EXIT0000
MAIN2460 DS    0H
         TM    SMF80DTA,X'80'        DATABASE ACTIVE?
         BNO   MAIN2470              NO
         MVC   MSG_06_VAL(6),=C'ACTIVE'
         B     MAIN2480
MAIN2470 DS    0H
         TM    SMF80DTA,X'40'        BACKUP?
         BNO   MAIN2480              NO
         MVC   MSG_06_VAL(6),=C'BACKUP'
MAIN2480 DS    0H
         MVC   MSG_06_VAL+6(4),=C' ON '
         MVC   MSG_06_VAL+10(6),SMF80DTA+4
         A     R8,SPF_VWIDTH
         BCTR  R6,R14
         B     EXIT0000
MVC_02   MVC   MSG_06_VAL(0),SMF80DTA+1

MAIN2500 DS    0H
         TM    SMF80DTA,X'40'        OLD NAME?
         BO    MAIN2510
         MVC   MSG_06_VAL(8),=C'NEW NAME'
         B     MAIN2520
MAIN2510 DS    0H
         MVC   MSG_06_VAL(8),=C'OLD NAME'
MAIN2520 DS    0H
         SR    R15,R15
         IC    R15,SMF80DLN
         BCTR  R15,0
         BCTR  R15,0
         EX    R15,MVC_03
         A     R8,SPF_VWIDTH
         BCTR  R6,R14
         B     EXIT0000
MVC_03   MVC   MSG_06_VAL+9(0),SMF80DTA+1

*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
EXIT0000 DS    0H
         SR    R2,R2
         B     EXITEXIT
EXIT0004 DS    0H
         LA    R2,4
EXITEXIT DS    0H
         ITRACE ID=EXIT,                                               +
               RDATA1=R2
         L     R13,4(R13)            RESTORE SAVE AREA ADDRESS
         L     R14,12(,R13)          RESTORE R14
         LR    R15,R2                SET RC
         LM    R0,R12,20(R13)        RESTORE REMAINING REGISTERS
         BR    R14                   RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                LTORG
DXDSTART        DC    Q(WK0080)
H4              DC    H'4'
H9              DC    H'9'
H12             DC    H'12'

EXTENDED_HEADING     DS    0C
                     DC    24C'-'
                     DC    AL1($SCREEN_ATTR_HIGH)
                     DC    C'Extended length section(s)'
                     DC    AL1($SCREEN_ATTR_NORMAL)
                     DC    24C'-'
EXTENDED_HEADING_L   EQU   *-EXTENDED_HEADING

DESC1           DC    C'Violation'
DESC1_L         EQU   *-DESC1
DESC2           DC    C'Undefined user'
DESC2_L         EQU   *-DESC2
DESC3           DC    C'Warning'
DESC3_L         EQU   *-DESC3

AUTH_NORMAL     DC    CL20'Normal'
AUTH_SPECIAL    DC    CL20'Special'
AUTH_OPERATIONS DC    CL20'Operations'
AUTH_AUDITOR    DC    CL20'auditor'
AUTH_EXIT       DC    CL20'Installation exit'
AUTH_FAILSOFT   DC    CL20'Failsoft'
AUTH_BYPASSED   DC    CL20'Bypassed'
AUTH_TRUSTED    DC    CL20'Trusted'


MSG_01         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'User:'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_01_USER    DC    CL8' '
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C' GROUP:'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_01_GROUP   DC    CL8' '
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C' JOB:'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_01_JOB     DC    CL8' '
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C' Terminal:'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_01_TERM    DC    CL8' '
MSG_01_L       EQU   *-MSG_01

MSG_02         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Descriptors:'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_02_DESC    DC    CL70'None'
MSG_02_L       EQU   *-MSG_02

MSG_03         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Event:           '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_03_EVENT   DC    X'40202120'
               DC    CL2' '
MSG_03_DESC    DC    CL40' '
MSG_03_L       EQU   *-MSG_03

MSG_04         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Status:       '
               DC    AL1($SCREEN_ATTR_HIGH_GREEN)
               DC    CL6' '
MSG_04_DESC    DC    CL45' '
MSG_04_L       EQU   *-MSG_04

MSG_05         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Special authorities '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_05_AUTH    DC    CL20'None'
MSG_05_L       EQU   *-MSG_05

* ------------------------------------------------------------------- *
*                                                                     *
*   WARNING!  Any changes to this message must also be made to        *
*             the MSG_06 DSECT in the extended length processors.     *
*                                                                     *
* ------------------------------------------------------------------- *
MSG_06         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
MSG_06_DESC    DC    CL20' '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_06_VAL     DC    CL58' '
MSG_06_L       EQU   *-MSG_06

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MSG_07         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'No extended length sections'
MSG_07_L       EQU   *-MSG_07

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EVENTS  DS  0H
        DC  AL1(01),AL4(QUAL_01),CL40'JOB INITIATION/TSO LOGON-LOGOFF'
        DC  AL1(02),AL4(QUAL_02),CL40'RESOURCE ACCESS'
        DC  AL1(03),AL4(QUAL_03),CL40'ADDVOL/CHANGEVOL'
        DC  AL1(04),AL4(QUAL_04),CL40'RENAME DATASET'
        DC  AL1(05),AL4(QUAL_05),CL40'SCRATCH DATASET OR TAPE VOLUME'
        DC  AL1(06),AL4(QUAL_06),CL40'DELETE 1 VOLUME OF MULTIVOLUME'
        DC  AL1(07),AL4(QUAL_07),CL40'DEFINE DATASET OR TAPE VOLUME'    '
        DC  AL1(08),AL4(QUAL_08),CL40'ADDSD'
        DC  AL1(09),AL4(QUAL_08),CL40'ADDGROUP'
        DC  AL1(10),AL4(QUAL_08),CL40'ADDUSER'
        DC  AL1(11),AL4(QUAL_08),CL40'ALTDSD'
        DC  AL1(12),AL4(QUAL_08),CL40'ALTGROUP'
        DC  AL1(13),AL4(QUAL_08),CL40'ALTUSER'
        DC  AL1(14),AL4(QUAL_08),CL40'CONNECT'
        DC  AL1(15),AL4(QUAL_08),CL40'DELDSD'
        DC  AL1(16),AL4(QUAL_08),CL40'DELGROUP'
        DC  AL1(17),AL4(QUAL_08),CL40'DELUSER'
        DC  AL1(18),AL4(QUAL_08),CL40'PASSWORD'
        DC  AL1(19),AL4(QUAL_08),CL40'PERMIT'
        DC  AL1(20),AL4(QUAL_08),CL40'RALTER'
        DC  AL1(21),AL4(QUAL_08),CL40'RDEFINE'
        DC  AL1(22),AL4(QUAL_08),CL40'RDELETE'
        DC  AL1(23),AL4(QUAL_08),CL40'REMOVE'
        DC  AL1(24),AL4(QUAL_08),CL40'SETROPTS'
        DC  AL1(25),AL4(QUAL_08),CL40'RVARY'
        DC  AL1(26),AL4(QUAL_26),CL40'APPCLU'
        DC  AL1(27),AL4(0),CL40'RESERVED'
        DC  AL1(28),AL4(QUAL_28),CL40'DIRECTORY SEARCH'
        DC  AL1(29),AL4(QUAL_28),CL40'CHECK ACCESS TO A DIRECTORY'
        DC  AL1(30),AL4(QUAL_28),Cl40'CHECK ACCESS TO A FILE'
        DC  AL1(31),AL4(QUAL_28),CL40'CHAUDIT'
        DC  AL1(32),AL4(QUAL_28),CL40'CHDIR'
        DC  AL1(33),AL4(QUAL_28),CL40'CHMOD'
        DC  AL1(34),AL4(QUAL_28),CL40'CHOWN'
        DC  AL1(35),AL4(QUAL_28),CL40'CLEAR SETID BITS FOR FILE'
        DC  AL1(36),AL4(QUAL_28),CL40'EXEC WITH SETUID/SETGID'
        DC  AL1(37),AL4(QUAL_28),CL40'GETPSENT'
        DC  AL1(38),AL4(QUAL_38),CL40'DUB'
        DC  AL1(39),AL4(QUAL_28),CL40'UNDUB'
        DC  AL1(40),AL4(QUAL_28),CL40'KILL'
        DC  AL1(41),AL4(QUAL_28),CL40'LINK'
        DC  AL1(42),AL4(QUAL_28),CL40'MKDIR'
        DC  AL1(43),AL4(QUAL_28),CL40'MKNOD'
        DC  AL1(44),AL4(QUAL_28),CL40'MOUNT FILE SYSTEM'
        DC  AL1(45),AL4(QUAL_28),CL40'OPEN (NEW FILE)'
        DC  AL1(46),AL4(QUAL_28),CL40'PTRACE'
        DC  AL1(47),AL4(QUAL_28),CL40'RENAME'
        DC  AL1(48),AL4(QUAL_28),CL40'RMDIR'
        DC  AL1(49),AL4(QUAL_28),CL40'SETEGID'
        DC  AL1(50),AL4(QUAL_28),CL40'SETEUID'
        DC  AL1(51),AL4(QUAL_28),CL40'SETGID'
        DC  AL1(52),AL4(QUAL_28),CL40'SETUID'
        DC  AL1(53),AL4(QUAL_28),Cl40'SYMLINK'
        DC  AL1(54),AL4(QUAL_28),CL40'UNLINK'
        DC  AL1(55),AL4(QUAL_28),CL40'UNMOUNT FILE SYSTEM'
        DC  AL1(56),AL4(QUAL_56),CL40'CHECK FILE OWNER'
        DC  AL1(57),AL4(QUAL_28),CL40'CHECK PRIV'
        DC  AL1(58),AL4(QUAL_28),CL40'OPEN SLAVE TTY'
        DC  AL1(59),AL4(QUAL_59),CL40'RACLINK'
        DC  AL1(60),AL4(QUAL_28),CL40'XXXCHK (CHECK IPC ACCESS)'
        DC  AL1(61),AL4(QUAL_28),CL40'XXXGET (MAKE ISP)'
        DC  AL1(62),AL4(QUAL_28),CL40'XXXCTL (R_IPC_CTL)'
        DC  AL1(63),AL4(QUAL_28),CL40'SETGROUP'
        DC  AL1(64),AL4(QUAL_28),CL40'CHECK OWNER 2 FILES'
        DC  AL1(65),AL4(QUAL_28),CL40'ACCESS RIGHTS PASSED'
        DC  AL1(66),AL4(QUAL_28),CL40'RACDCERT'
        DC  AL1(67),AL4(QUAL_67),CL40'initACEE'
        DC  AL1(68),AL4(QUAL_28),CL40'Grant of initial KERBEROS ticket'
        DC  AL1(69),AL4(QUAL_69),CL40'R_PKIServ GENCERT'
        DC  AL1(70),AL4(QUAL_70),CL40'R_PKIServ export'
        DC  AL1(71),AL4(QUAL_71),CL40'Policy access control decision'
        DC  AL1(72),AL4(QUAL_72),CL40'R_PKIServ QUERY'
        DC  AL1(73),AL4(QUAL_28),CL40'R_PKIServ UPDATEREQ'
        DC  AL1(74),AL4(QUAL_74),CL40'R_PKIServ Update cert'
        DC  AL1(75),AL4(QUAL_28),CL40'Change file ACL'
        DC  AL1(76),AL4(QUAL_28),CL40'Remove file ACL'
        DC  AL1(77),AL4(QUAL_28),CL40'Set file security label'
        DC  AL1(78),AL4(QUAL_28),CL40'Set WRITE-DOWN priviledge'
        DC  AL1(79),AL4(QUAL_28),CL40'CRL publication'
        DC  AL1(80),AL4(QUAL_28),CL40'R_PKIServ RESPOND'
        DC  AL1(81),AL4(QUAL_28),CL40'PASSTICKET EVALUATION'
        DC  AL1(82),AL4(QUAL_28),CL40'PASSTICKET GENERATION'
        DC  X'FF',AL4(0),CL40'UNKNOWN'

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
QUAL_01  DS  0C         QUALIFIERS FOR EVENT 1
         DC  AL1(00),CL45'Successful'
         DC  AL1(01),CL45'Invalid password'
         DC  AL1(02),CL45'Invalid group'
         DC  AL1(03),CL45'Invalid OID card'
         DC  AL1(04),CL45'Invalid terminal/console'
         DC  AL1(05),CL45'Invalid application'
         DC  AL1(06),CL45'Revoked user attempting logon'
         DC  AL1(07),CL45'User automatically revoked'
         DC  AL1(08),CL45'Successful termination'
         DC  AL1(09),CL45'Undefined user ID'
         DC  AL1(10),CL45'Insufficient security label authority'
         DC  AL1(11),CL45'Not authorized to security label'
         DC  AL1(12),CL45'Successful RACINIT initiation'
         DC  AL1(13),CL45'Successful RACINIT DELETE'
         DC  AL1(14),CL45'System now requires more authority'
         DC  AL1(15),CL45'Remote job entry - job not authorized'
         DC  AL1(16),CL45'Surrogate class is inactive'
         DC  AL1(17),CL45'Submittor not authorized by user'
         DC  AL1(18),CL45'Submittor not authorized to security label'
         DC  AL1(19),CL45'User not authorized to job'
         DC  AL1(20),CL45'WARNING insufficent security label authority'
         DC  AL1(21),CL45'WARNING security label missing'
         DC  AL1(22),CL45'WARNING not authorized to security label'
         DC  AL1(23),CL45'Security labels not compatible'
         DC  AL1(24),CL45'WARNING Security labels not compatible'
         DC  AL1(25),CL45'Password has expired'
         DC  AL1(26),CL45'Invalid new password'
         DC  AL1(27),CL45'Verification failed by installation'
         DC  AL1(28),CL45'Group access has been revoked'
         DC  AL1(29),CL45'OIDCARD is required'
         DC  AL1(30),CL45'Network job entry - job not authorized'
         DC  AL1(31),CL45'WARNING unknown user from trusted node'
         DC  AL1(32),CL45'Successful initiation using PASSTICKET'
         DC  AL1(33),CL45'Attempted replay of PASSTICKET'
         DC  AL1(34),CL45'Client security label not equivalent'
         DC  X'FF',CL45'Unknown'

QUAL_02  DS  0C         QUALIFIERS FOR EVENT 2
         DC  AL1(00),CL45'Successful'
         DC  AL1(01),CL45'Insufficient authority'
         DC  AL1(02),CL45'Profile not found - RACFIND specified'
         DC  AL1(03),CL45'Access permitted with WARNING'
         DC  AL1(04),CL45'Failed due to PROTECTALL'
         DC  AL1(05),CL45'WARNING due to PROTECTALL'
         DC  AL1(06),CL45'Insufficent category/seclevel'
         DC  AL1(07),CL45'Insufficient security label authority'
         DC  AL1(08),CL45'Security label missing'
         DC  AL1(09),CL45'WARNING insufficient security label auth'
         DC  AL1(10),CL45'WARNING data set not cataloged'
         DC  AL1(11),CL45'Data set not cataloged'
         DC  AL1(12),CL45'Profile not found'
         DC  AL1(13),CL45'WARNING insufficent category/seclevel'
         DC  AL1(14),CL45'WARNING non-main execution environment'
         DC  AL1(15),CL45'Conditional access allowed'
         DC  X'FF',CL45'Unknown'

QUAL_03  DS  0C         QUALIFIERS FOR EVENT 3
         DC  AL1(00),CL45'Successful processing of new volume'
         DC  AL1(01),CL45'Insufficient authority'
         DC  AL1(02),CL45'Insufficient security label authority'
         DC  AL1(03),CL45'Less specific profile exists'
         DC  X'FF',CL45'Unknown'

QUAL_04  DS  0C         QUALIFIERS FOR EVENT 4
         DC  AL1(00),CL45'Successful'
         DC  AL1(01),CL45'Invalid group'
         DC  AL1(02),CL45'User not in group'
         DC  AL1(03),CL45'Insufficient authority'
         DC  AL1(04),CL45'Resource name already defined'
         DC  AL1(05),CL45'User not defined to RACF'
         DC  AL1(06),CL45'Resource not protected'
         DC  AL1(07),CL45'WARNING resource not protected'
         DC  AL1(08),CL45'User in second qualifier not defined'
         DC  AL1(09),CL45'Less specified profile exists'
         DC  AL1(10),CL45'Insufficient security label authority'
         DC  AL1(11),CL45'Resource not protected by security label'
         DC  AL1(12),CL45'New name not protected by security label'
         DC  AL1(13),CL45'New security label must dominate old label'
         DC  AL1(14),CL45'Insufficient security label authority'
         DC  AL1(15),CL45'WARNING resource not protected by sec label'
         DC  AL1(16),CL45'WARNING new name not protected by sec label'
         DC  AL1(17),CL45'WARNING new sec label must dominate old'
         DC  X'FF',CL45'Unknown'

QUAL_05  DS  0C         QUALIFIERS FOR EVENT 5
         DC  AL1(00),CL45'Successful'
         DC  AL1(01),CL45'Resource not found'
         DC  AL1(02),CL45'Invalid volume'
         DC  X'FF',CL45'Unknown'

QUAL_06  DS  0C         QUALIFIERS FOR EVENT 6
         DC  AL1(00),CL45'Successful'
         DC  X'FF',CL45'Unknown'

QUAL_07  DS  0C         QUALIFIERS FOR EVENT 7
         DC  AL1(00),CL45'Successful'
         DC  AL1(01),CL45'Group undefined'
         DC  AL1(02),CL45'User not in group'
         DC  AL1(03),CL45'Insufficient authority'
         DC  AL1(04),CL45'Resource already defined'
         DC  AL1(05),CL45'User not defined'
         DC  AL1(06),CL45'Resource not protected'
         DC  AL1(07),CL45'WARNING resource not protected'
         DC  AL1(08),CL45'WARNING security label missing'
         DC  AL1(09),CL45'Insufficient security label authority'
         DC  AL1(10),CL45'User in second qualifier not defined'
         DC  AL1(11),CL45'Insufficient security label authority'
         DC  AL1(12),CL45'Less specific profile exists'
         DC  X'FF',CL45'Unknown'

QUAL_08  DS  0C         QUALIFIERS FOR EVENT 8-25
         DC  AL1(00),CL45'Successful'
         DC  AL1(01),CL45'Insufficent authority'
         DC  AL1(02),CL45'Keyword violation detected'
         DC  AL1(03),CL45'Successful listing of data sets'
         DC  AL1(04),CL45'System error listing data sets'
         DC  X'FF',CL45'Unknown'

QUAL_26  DS  0C         QUALIFIERS FOR EVENT 26
         DC  AL1(00),CL45'Successful'
         DC  AL1(01),CL45'Session established without verification'
         DC  AL1(02),CL45'Local LU key will expire in 5 days or less'
         DC  AL1(03),CL45'Partner LU access has been revoked'
         DC  AL1(04),CL45'Partner LU key does not match this LU key'
         DC  AL1(05),CL45'Session terminated for security reasons'
         DC  AL1(06),CL45'Required session key not defined'
         DC  AL1(07),CL45'Possible security attack by partner LU'
         DC  AL1(08),CL45'Session key not defined for partner LU'
         DC  AL1(09),CL45'Session key not defined for this LU'
         DC  AL1(10),CL45'SNA security related protocol error'
         DC  AL1(11),CL45'Profile change during verification'
         DC  AL1(12),CL45'Expired session key'
         DC  X'FF',CL45'Unknown'

QUAL_28  DS  0C         QUALIFIERS FOR EVENT 28
         DC  AL1(00),CL45'Successful'
         DC  AL1(01),CL45'Not authorized'
         DC  AL1(02),CL45'Security label failure'
         DC  X'FF',CL45'Unknown'

QUAL_31  DS  0C         QUALIFIERS FOR EVENT 31
         DC  AL1(00),CL45'Successful'
         DC  AL1(01),CL45'User not authorized for specific file'
         DC  AL1(02),CL45'User not authorized for change audit'
         DC  AL1(03),CL45'Security label failure'
         DC  X'FF',CL45'Unknown'

QUAL_38  DS  0C         QUALIFIERS FOR EVENT 38
         DC  AL1(00),CL45'Successful'
         DC  AL1(01),CL45'User does not have OMVS segment'
         DC  AL1(02),CL45'No UID in OMVS segment'
         DC  AL1(03),CL45'User''s group has no GID'
         DC  X'FF',CL45'Unknown'

QUAL_56  DS  0C         QUALIFIERS FOR EVENT 56
         DC  AL1(00),CL45'User is owner'
         DC  AL1(01),CL45'User is not owner'
         DC  AL1(02),CL45'Security label failure'
         DC  X'FF',CL45'Unknown'

QUAL_59  DS  0C         QUALIFIERS FOR EVENT 59
         DC  AL1(00),CL45'Successful'
         DC  AL1(01),CL45'Insufficient authority'
         DC  AL1(02),CL45'Keyword violation detected'
         DC  AL1(03),CL45'Association already defined'
         DC  AL1(04),CL45'Association already approved'
         DC  AL1(05),CL45'Association does not match'
         DC  AL1(06),CL45'Association does not exist'
         DC  AL1(07),CL45'Invalid password or revoked user'
         DC  X'FF',CL45'Unknown'

QUAL_67  DS  0C         QUALIFIERS FOR EVENT 67
         DC  AL1(00),CL45'Successful registration'
         DC  AL1(01),CL45'Successful deregistration'
         DC  AL1(02),CL45'Insufficent authority to register a cert'
         DC  AL1(03),CL45'Insufficent authority to deregister a cert'
         DC  AL1(04),CL45'No user ID found for certificate'
         DC  AL1(05),CL45'Certificate is not trusted'
         DC  AL1(06),CL45'Successful CERTAUTH registration'
         DC  AL1(07),CL45'Insufficent authority to register CERTAUTH'
         DC  AL1(08),CL45'Client sec label not equivalent to user''s'
         DC  X'FF',CL45'Unknown'

QUAL_69  DS  0C         QUALIFIERS FOR EVENT 69
         DC  AL1(00),CL45'Successful GENCERT'
         DC  AL1(01),CL45'Insufficient authority for GENCERT'
         DC  AL1(02),CL45'Successful REQCERT'
         DC  AL1(03),CL45'Insufficient authority for REQCERT'
         DC  AL1(04),CL45'Successful GENRENEW'
         DC  AL1(05),CL45'Insufficient authority for GENRENEW'
         DC  AL1(04),CL45'Successful REQRENEW'
         DC  AL1(05),CL45'Insufficient authority for REQRENEW'
         DC  X'FF',CL45'Unknown'

QUAL_70  DS  0C         QUALIFIERS FOR EVENT 70
         DC  AL1(00),CL45'Successful'
         DC  AL1(01),CL45'Insufficient authority'
         DC  AL1(02),CL45'Incorrect pass phrase'
         DC  X'FF',CL45'Unknown'

QUAL_71  DS  0C         QUALIFIERS FOR EVENT 71
         DC  AL1(00),CL45'Successful'
         DC  AL1(01),CL45'Permitted with WARNING'
         DC  AL1(02),CL45'WARNING for insufficient traverse authority'
         DC  AL1(03),CL45'WARNING for time-of-day'
         DC  AL1(04),CL45'Not authorized'
         DC  AL1(05),CL45'Not authorized due to traverse authority'
         DC  AL1(06),CL45'Not authorized due to time-of-day'
         DC  X'FF',CL45'Unknown'

QUAL_72  DS  0C         QUALIFIERS FOR EVENT 72
         DC  AL1(00),CL45'Successful'
         DC  AL1(01),CL45'Insufficient authority'
         DC  AL1(02),CL45'Successful VERIFY'
         DC  AL1(03),CL45'Insufficient authority for VERIFY'
         DC  AL1(04),CL45'No record found for this certificate'
         DC  X'FF',CL45'Unknown'

QUAL_74  DS  0C         QUALIFIERS FOR EVENT 74
         DC  AL1(00),CL45'Successful'
         DC  AL1(01),CL45'Insufficient authority'
         DC  AL1(02),CL45'Successful REVOKE'
         DC  AL1(03),CL45'Insufficient authority for REVOKE'
         DC  X'FF',CL45'Unknown'

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
SUBTYPES DS  0C
         DC  AL1(01),AL4(MAIN2000),CL20'Resource name'
         DC  AL1(02),AL4(MAIN2000),CL20'New name'
         DC  AL1(03),AL4(MAIN2020),CL20'Access requested'
         DC  AL1(04),AL4(MAIN2100),CL20'Access allowed'
         DC  AL1(05),AL4(MAIN2210),CL20'Data set level'
         DC  AL1(06),AL4(0),CL20' '
         DC  AL1(07),AL4(0),CL20' '
         DC  AL1(08),AL4(MAIN2000),CL20'User name'
         DC  AL1(09),AL4(MAIN2000),CL20'Resource name'
         DC  AL1(10),AL4(MAIN2220),CL20'VOLSER'
         DC  AL1(11),AL4(MAIN2220),CL20'VOLSER'
         DC  AL1(12),AL4(MAIN2230),CL20'ID names'
         DC  AL1(13),AL4(MAIN2260),CL20'From resource'
         DC  AL1(14),AL4(MAIN2000),CL20'VOLSER(s)'
         DC  AL1(15),AL4(MAIN2000),CL20'VOLSER'
         DC  AL1(16),AL4(MAIN2000),CL20'Old VOLSER'
         DC  AL1(17),AL4(MAIN2000),CL20'Class name'
         DC  AL1(18),AL4(MAIN2000),CL20'MENTITY'
         DC  AL1(19),AL4(MAIN2000),CL20'VOLSER of model'
         DC  AL1(20),AL4(MAIN2000),CL20'Application name'
         DC  AL1(21),AL4(MAIN2400),CL20'Class options'
         DC  AL1(22),AL4(MAIN2000),CL20'Class name'
         DC  AL1(23),AL4(MAIN2000),CL20'Class name'
         DC  AL1(24),AL4(MAIN2220),CL20'Options'
         DC  AL1(25),AL4(MAIN2000),CL20'Resource Name'
         DC  AL1(26),AL4(MAIN2000),CL20'Class Name'
         DC  AL1(27),AL4(MAIN2000),CL20'Class Name'
         DC  AL1(28),AL4(MAIN2220),CL20'Class Name'
         DC  AL1(29),AL4(MAIN2220),CL20'Application data'
         DC  AL1(30),AL4(MAIN2450),CL20'RACF DB status'
         DC  AL1(31),AL4(MAIN2000),CL20'Dataset name'
         DC  AL1(33),AL4(MAIN2500),CL20'Rename'
         DC  AL1(34),AL4(MAIN2000),CL20'Class name'
         DC  AL1(35),AL4(MAIN2000),CL20'Class name'
         DC  AL1(36),AL4(MAIN2000),CL20'Class name'
         DC  AL1(37),AL4(MAIN2000),CL20'Model name'
         DC  AL1(38),AL4(MAIN2000),CL20'User or group'
         DC  AL1(42),AL4(MAIN2000),CL20'Class name'
         DC  AL1(43),AL4(MAIN2000),CL20'Class name'
         DC  AL1(46),AL4(MAIN2000),CL20'LOGSTR value'
         DC  AL1(47),AL4(MAIN2000),CL20'JOB name'
         DC  AL1(48),AL4(MAIN2000),CL20'USER ID'
         DC  AL1(49),AL4(MAIN2000),CL20'User name'
         DC  AL1(50),AL4(MAIN2000),CL20'Security label'
         DC  AL1(51),AL4(MAIN2000),CL20'Security label'
         DC  X'FF'

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXTENDED       DS       0X
               DC       XL2'0100',VL4(SMF80100)
               DC       XL2'0101',VL4(SMF80101)
               DC       XL2'0102',VL4(SMF80102)
               DC       XL2'0103',VL4(SMF80103)
               DC       XL2'0104',VL4(SMF80104)
               DC       XL2'0105',VL4(SMF80105)
               DC       XL2'0106',VL4(SMF80106)
               DC       XL2'0107',VL4(SMF80107)
               DC       XL2'0108',VL4(SMF80108)
               DC       XL2'0109',VL4(SMF80109)
               DC       XL2'010A',VL4(SMF8010A)
               DC       XL2'010B',VL4(SMF8010B)
               DC       XL2'010C',VL4(SMF8010C)
               DC       XL2'010D',VL4(SMF8010D)
               DC       XL2'010E',VL4(SMF8010E)
               DC       XL2'010F',VL4(SMF8010F)
               DC       XL2'0110',VL4(SMF80110)
               DC       XL2'0111',VL4(SMF80111)
               DC       XL2'0112',VL4(SMF80112)
               DC       XL2'0113',VL4(SMF80113)
               DC       XL2'0114',VL4(SMF80114)
               DC       XL2'0115',VL4(SMF80115)
               DC       XL2'0116',VL4(SMF80116)
               DC       XL2'0117',VL4(SMF80117)
               DC       XL2'0118',VL4(SMF80118)
               DC       XL2'0119',VL4(SMF80119)
               DC       XL2'011A',VL4(SMF8011A)
               DC       XL2'011B',VL4(SMF8011B)
               DC       XL2'011C',VL4(SMF8011C)
               DC       XL2'011D',VL4(SMF8011D)
               DC       XL2'011E',VL4(SMF8011E)
               DC       XL2'011F',VL4(SMF8011F)
               DC       XL2'0128',VL4(SMF80120)
               DC       X'FF'
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
WK0080         DSECT
               COPY     DXDPREF
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EVENT_DSECT    DSECT
EVENT_CODE     DS       X
EVENT_QUAL     DS       XL4
EVENT_DESC     DS       CL40
EVENT_L        EQU      *-EVENT_DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
QUAL_DSECT     DSECT
QUAL_CODE      DS       X
QUAL_DESC      DS       CL45
QUAL_L         EQU      *-QUAL_DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
SUBTYPE_DSECT  DSECT
SUBTYPE_CODE   DS       X
SUBTYPE_PROC   DS       AL4
SUBTYPE_DESC   DS       CL20
SUBTYPE_L      EQU      *-SUBTYPE_DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXTENDED_DSECT DSECT
EXTENDED_CODE  DS       XL2
EXTENDED_PROC  DS       AL4
EXTENDED_L     EQU      *-EXTENDED_DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               SESSION
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
SMF_RECORD     DSECT
               IFASMFR  80
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY     REGEQU
               END      SMF0080
