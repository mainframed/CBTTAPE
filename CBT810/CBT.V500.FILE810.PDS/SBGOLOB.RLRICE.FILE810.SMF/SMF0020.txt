*---------------------------------------------------------------------*
*                                                                     *
*         Format SMF record type 20                                   *
*         JOB initiation                                              *
*                                                                     *
*                                                                     *
*   At entry                                                          *
*       R15    program base address                                   *
*       R14    return address                                         *
*       R9     OSSPFD address                                         *
*       R8     VDATA buffer address                                   *
*       R7     SMF record address +4 (does not include length)        *
*                                                                     *
* ------------------------------------------------------------------- *
SMF0020  CSECT
SMF0020  AMODE 31
SMF0020  RMODE ANY
         USING SESSION,R11
         USING SMF0020,R15
         B     INIT0000
MODID    DC    CL8'SMF0020'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         USING OSCOMM,R12            DEFINE COMMON AREA BASE
         STM   R14,R12,12(R13)       SAVE REGS
         LR    R10,R15               COPY ENTRY POINT
         USING SMF0020,R10           DEFINE BASE
         DROP  R15
         L     R1,SESS_DXD_ADDR
         A     R1,DXDSTART
         ST    R13,4(R1)             SAVE R13
         ST    R1,8(R13)             CHAIN SAVE AREA
         LR    R13,R1                COPY WORK AREA ADDRESS
         USING WK0020,R13            DEFINE BASE
         MVC   DXD_CSECT,MODID
         USING OSSPFD,R9             DEFINE BASE
         ITRACE ID=ENTRY
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
         USING SMF20FLG,R7

         MVC   0(MSG_01_L,R8),MSG_01 COPY MESSAGE
         USING MSG_01,R8             DEFINE BASE
         MVC   MSG_01_JOB,SMF20JBN   COPY JOB NAME
         DROP  R8
         A     R8,SPF_VWIDTH
         BCTR  R6,0

         MVC   0(MSG_02_L,R8),MSG_02 COPY MESSAGE
         USING MSG_02,R8
         MVC   MSG_02_USER,SMF20UID  COPY USER ID
         DROP  R8
         A     R8,SPF_VWIDTH
         BCTR  R6,0

         MVC   0(MSG_03_L,R8),MSG_03 COPY MESSAGE
         USING MSG_03,R8
         MVC   MSG_03_NAME,SMF20PGM  COPY USER'S NAME
         DROP  R8
         A     R8,SPF_VWIDTH
         BCTR  R6,0

         SR    R1,R1                 CLEAR REGISTER
         ICM   R1,3,SMF20RLO         OFFSET TO RELOCATABLE SECTION
         BZ    MAIN0030              NO RELOCATABLE DATA
         AR    R1,R7                 PLUS BASE
         USING SMF20RLA,R1           DEFINE BASE
         CLI   SMF20RUD,0            ZERO?
         BE    MAIN0010              YES
         MVC   0(MSG_04_L,R8),MSG_04 COPY MESSAGE
         USING MSG_04,R8
         MVC   MSG_04_UID,SMF20RUD   COPY RACF ID
         DROP  R8
         A     R8,SPF_VWIDTH
         BCTR  R6,0
MAIN0010 DS    0H
         CLI   SMF20GRP,0            GROUP ZERO?
         BE    MAIN0020              YES
         MVC   0(MSG_05_L,R8),MSG_05 COPY MESSAGE
         USING MSG_05,R8
         MVC   MSG_05_GID,SMF20GRP   COPY RACF GROUP
         DROP  R8
         A     R8,SPF_VWIDTH
         BCTR  R6,0
MAIN0020 DS    0H
         CLI   SMF20TID,0            TERMINAL ID ZERO?
         BE    MAIN0030              YES
         MVC   0(MSG_06_L,R8),MSG_06 COPY MESSAGE
         USING MSG_06,R8
         MVC   MSG_06_TERMINAL,SMF20TID
         DROP  R1,R8
         A     R8,SPF_VWIDTH
         BCTR  R6,0

MAIN0030 DS    0H
         SR    R1,R1                 CLEAR REGISTER
         ICM   R1,1,SMF20NAF         NUMBER OF ACCOUNTING FIELDS
         BZ    EXIT0000              NONE
         LA    R2,SMF20ACT           FIRST FIELD
         ZAP   WORK_COUNTER,P0       INITIALIZE FIELD COUNTER
         CR    R6,R1                 MORE LINES THAN FIELDS?
         BL    MAIN0040              NO
         LR    R6,R1                 LIMIT TO NBR OF FIELDS
MAIN0040 DS    0H
         MVC   0(MSG_07_L,R8),MSG_07
         USING MSG_07,R8
         AP    WORK_COUNTER,P1       ADD 1 TO FIELD NUMBER
         ED    MSG_07_NBR,WORK_COUNTER
         SR    R1,R1
         IC    R1,0(R2)              LENGTH OF THIS FIELD
         CVD   R1,COMM_DWORD         CONVERT TO DECIMAL
         ED    MSG_07_LENGTH,COMM_DWORD+6
         LTR   R1,R1                 LENGTH ZERO?
         BZ    MAIN0060              YES
         CH    R1,=Y(L'MSG_07_DATA)  TOO LONG?
         BL    MAIN0050              NO
         LH    R1,=Y(L'MSG_07_DATA)  LIMIT LENGTH
MAIN0050 DS    0H
         BCTR  R1,0
         EX    R1,MVC_01             COPY ACCOUNTING FIELD
MAIN0060 DS    0H
         IC    R1,0(R2)              FIELD LENGTH
         AR    R2,R1                 NEXT FIELD
         A     R8,SPF_VWIDTH
         BCT   R6,MAIN0040           PROCESS NEXT FIELD
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
EXIT0000 DS    0H
         SR    R2,R2
         B     EXITEXIT
EXIT0004 DS    0H
         LA    R2,4
EXITEXIT DS    0H
         ITRACE ID=EXIT,                                               +
               RDATA1=R2
         L     R13,4(R13)            RESTORE SAVE AREA ADDRESS
         L     R14,12(,R13)          RESTORE R14
         SR    R15,R15               SET RC
         LM    R0,R12,20(R13)        RESTORE REMAINING REGISTERS
         BR    R14                   RETURN
* ------------------------------------------------------------------- *
MVC_01   MVC   MSG_07_DATA(0),1(R2)
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDSTART       DC    Q(WK0020)
H4             DC    H'4'
P0             DC    P'0'
P1             DC    P'1'

MSG_01         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Job name    '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_01_JOB     DC    CL8' '
MSG_01_L       EQU   *-MSG_01

MSG_02         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'User id     '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_02_USER    DC    CL8' '
MSG_02_L       EQU   *-MSG_02

MSG_03         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Programmer  '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_03_NAME    DC    CL20' '
MSG_03_L       EQU   *-MSG_03

MSG_04         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'RACF user id'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_04_UID     DC    CL8' '
MSG_04_L       EQU   *-MSG_04

MSG_05         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'RACF group  '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_05_GID     DC    CL8' '
MSG_05_L       EQU   *-MSG_05

MSG_06         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Terminal    '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_06_TERMINAL DC   CL8' '
MSG_06_L       EQU   *-MSG_06


MSG_07         DS    0C
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'ACCT field'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_07_NBR     DC    X'40202120'
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C'Length'
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_07_LENGTH  DC    X'40202120'
               DC    AL1($SCREEN_ATTR_HIGH)
               DC    C' '
               DC    AL1($SCREEN_ATTR_NORMAL)
MSG_07_DATA    DC    CL40' '
MSG_07_L       EQU   *-MSG_07

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
WK0020         DSECT
               COPY     DXDPREF
WORK_COUNTER   DS       PL2
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              SESSION
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
SMF_RECORD    DSECT
              IFASMFR  20
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COPY     REGEQU
              END      SMF0020
