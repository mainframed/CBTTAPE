* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: DUMP                                                  *
*                                                                     *
*  DUMP a dataspace                                                   *
*                                                                     *
* ------------------------------------------------------------------- *
DUMP     CSECT
DUMP     AMODE 31
DUMP     RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING DUMP,R15
         B     INIT0000
MODID    DC    CL8'DUMP'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT ADDR
         DROP  R15
         USING DUMP,R10                       DEFINE BASE
         USING DSPCREQ,R9                     DEFINE BASE
         L     R8,DSPCREQ_DATASPACE           DATASPACE INFO ADDRESS
         USING SCONTROL_DATASPACE,R8
         L     R15,COMM_OSDSPACE_DXD          WORK AREA BASE
         A     R15,DXD_START                  PLUS DISPLACEMENT
         ST    R13,4(R15)                     SAVE R13
         ST    R15,8(R13)                     CHAIN SAVE AREA
         LR    R13,R15                        COPY WORK AREA ADDRESS
         USING DXDDUMP,R13
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         ITRACE ID=DUMP,                                               +
               DATA1=SCONTROL_DATASPACE_NAME
         L     R7,DSPCREQ_RECORD_ADDR
         USING PR_LINE,R7
         L     R6,DSPCREQ_DCB
         MVC   PR_LINE(HEADING_1_L),HEADING_1
         BAL   R5,PRINT000
         MVC   PR_FIELD,C_NAME
         MVC   PR_VALUE(L'SCONTROL_DATASPACE_NAME),SCONTROL_DATASPACE_N+ME
               AME
         BAL   R5,PRINT000
         MVC   PR_FIELD,C_TOKEN
         UNPK  PR_VALUE+0(9),SCONTROL_DATASPACE_TOKEN+0(5)
         UNPK  PR_VALUE+8(9),SCONTROL_DATASPACE_TOKEN+4(5)
         TR    PR_VALUE(16),COMM_HEXCHAR
         MVI   PR_VALUE+16,C' '
         BAL   R5,PRINT000
         MVC   PR_FIELD,C_ALET
         UNPK  PR_VALUE(9),SCONTROL_DATASPACE_ALET(5)
         TR    PR_VALUE(8),COMM_HEXCHAR
         MVI   PR_VALUE+8,C' '
         BAL   R5,PRINT000
         MVC   PR_FIELD,C_BLOCKS
         L     R1,SCONTROL_DATASPACE_BLOCKS
         CVD   R1,COMM_DWORD
         MVC   PR_VALUE(L'EDIT_WORD_1),EDIT_WORD_1
         ED    PR_VALUE(L'EDIT_WORD_1),COMM_DWORD+3
         BAL   R5,PRINT000
         MVC   PR_FIELD,C_START
         L     R1,SCONTROL_DATASPACE_DISPLAY_FIRST_RECORD
         CVD   R1,COMM_DWORD
         MVC   PR_VALUE(L'EDIT_WORD_1),EDIT_WORD_1
         ED    PR_VALUE(L'EDIT_WORD_1),COMM_DWORD+3
         BAL   R5,PRINT000
         MVC   PR_FIELD,C_HIGH
         L     R1,SCONTROL_DATASPACE_LAST_RECORD
         CVD   R1,COMM_DWORD
         MVC   PR_VALUE(L'EDIT_WORD_1),EDIT_WORD_1
         ED    PR_VALUE(L'EDIT_WORD_1),COMM_DWORD+3
         BAL   R5,PRINT000
         MVC   PR_FIELD,C_LAST
         UNPK  PR_VALUE(9),SCONTROL_DATASPACE_LAST_ADDRESS(5)
         TR    PR_VALUE(8),COMM_HEXCHAR
         MVI   PR_VALUE+8,C' '
         BAL   R5,PRINT000
         MVC   PR_LINE(HEADING_2_L),HEADING_2
         BAL   R5,PRINT000
         L     R3,SCONTROL_DATASPACE_DISPLAY_FIRST_RECORD
         SR    R4,R4                           DATA STARTS AT ADDRESS 0
         USING DATASPACE_INDEX,R4
DUMP0010 DS    0H
         C     R3,SCONTROL_DATASPACE_LAST_RECORD AT LAST RECORD?
         BH    DUMP0030                        YES
         LAM   R4,R4,SCONTROL_DATASPACE_ALET   LOAD AR WITH ALET
         SAC   512                             ENTER AR MODE
         CVD   R3,COMM_DWORD                   CONVERT TO DECIMAL
         MVC   PR_RECORD_NBR,EDIT_WORD_2
         ED    PR_RECORD_NBR,COMM_DWORD+4      EDIT RECORD NUMBER
         UNPK  PR_RECORD_ADDR(9),DATASPACE_INDEX_ADDR(5)
         TR    PR_RECORD_ADDR,COMM_HEXCHAR     TRANSLATE DATA ADDRESS
         MVI   PR_RECORD_ADDR+8,C' '
         L     R1,DATASPACE_INDEX_RBA          RBA (VSAM RECORDS)
         CVD   R1,COMM_DWORD                   CONVERT TO DECIMAL
         MVC   PR_RECORD_RBA,EDIT_WORD_2       COPY EDIT WORD
         ED    PR_RECORD_RBA,COMM_DWORD+4      EDIT RBA
         L     R1,DATASPACE_INDEX_LENGTH       DATA LENGTH
         CVD   R1,COMM_DWORD                   CONVERT TO DECIMAL
         MVC   PR_RECORD_LENGTH,EDIT_WORD_2    COPY EDIT WORD
         ED    PR_RECORD_LENGTH,COMM_DWORD+4   EDIT LENGTH
         UNPK  PR_RECORD_CCHHR(11),DATASPACE_INDEX_CCHHR(6)
         TR    PR_RECORD_CCHHR,COMM_HEXCHAR    TRANSLATE DISK ADDRESS
         MVI   PR_RECORD_CCHHR+10,C' '
         L     R14,DATASPACE_INDEX_ADDR        DATA ADDRESS
         CH    R1,=Y(L'PR_RECORD_DATA)         DATA TOO LONG?
         BNH   DUMP0020                        NO
         LH    R1,=Y(L'PR_RECORD_DATA)         LIMIT LENGTH
DUMP0020 DS    0H
         BCTR  R1,0                            FOR EXECUTE
         LAM   R14,R14,SCONTROL_DATASPACE_ALET LOAD AR WITH ALET
         EX    R1,DUMP_MVC                     COPY DATA
         LA    R3,1(,R3)                       ADD 1 TO RECORD COUNTER
         LA    R4,DATASPACE_INDEX_L(,R4)       NEXT RECORD
         SAC   0                               TURN OFF AR MODE
         BAL   R5,PRINT000                     PRINT
         B     DUMP0010                        PROCESS NEXT REGISTER
DUMP0030 DS    0H
         SAC   0                               MAKE SURE AR MODE IS OFF
         B     EXIT0000
PRINT000 DS    0H
         PUT   (R6),(R7)
         MVC   PR_LINE(PR_LINE_L),COMM_BLANKS
         BR    R5
DUMP_MVC MVC   PR_RECORD_DATA(0),0(R14)

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT,                                               +
               DATA1=(DSPCREQ_RC,1)
         LAM   R4,R4,F0                        RESET AR
         LAM   R14,R14,F0                      RESET AR
         L     R13,4(R13)                      RESTORE SAVE AREA
         LM    R14,R12,12(R13)                 RESTORE REGISTERS
         BR    R14                             RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START     DC    Q(DXDDUMP)
F0            DC    F'0'

EDIT_WORD_1   DC    X'40202020202020202120'
EDIT_WORD_2   DC    X'4020202020202020'
EDIT_WORD_3   DC    X'F020202020202120'

C_NAME        DC    CL8'NAME'
C_TOKEN       DC    CL8'TOKEN'
C_ALET        DC    CL8'ALET'
C_BLOCKS      DC    CL8'BLOCKS'
C_NEXT        DC    CL8'NEXT'
C_START       DC    CL8'START'
C_HIGH        DC    CL8'HIGH'
C_LAST        DC    CL8'LAST'

HEADING_1     DS    0C
              DC    C'1'
              DC    61C'-'
              DC    C'DATA SPACE'
              DC    61C'-'
HEADING_1_L   EQU   *-HEADING_1

HEADING_2     DS    0C
              DC    C'0'
              DC    CL8' REC NBR'
              DC    CL2' '
              DC    CL8'  ADDR  '
              DC    CL2' '
              DC    CL8' LENGTH '
              DC    CL2' '
              DC    CL8'    RBA '
              DC    CL2' '
              DC    CL10'CCCCHHHHRR'
              DC    CL2' '
              DC    41C'_'
              DC    CL4'DATA'
              DC    41C'_'
HEADING_2_L   EQU   *-HEADING_2

              LTORG
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDDUMP           DSECT
                  COPY      DXDPREF
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
PR_LINE          DSECT
                 DS     C
PR_FIELD         DS     CL8
                 DS     CL4
PR_VALUE         DS     CL120

                 ORG    PR_FIELD
PR_RECORD_NBR    DS     CL8
                 DS     CL2
PR_RECORD_ADDR   DS     CL8
                 DS     CL2
PR_RECORD_LENGTH DS     CL8
                 DS     CL2
PR_RECORD_RBA    DS     CL8
                 DS     CL2
PR_RECORD_CCHHR  DS     CL10
                 DS     CL2
PR_RECORD_DATA   DS     CL090
                 ORG
PR_LINE_L        EQU    *-PR_LINE
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  DSPCREQ  DSECT=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
SCONTROL          DSECT
                  SCONTROL SCONTROL
SCONTROL_L        EQU      *-SCONTROL
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COPY     SINDEX
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                  COPY     REGEQU
                  END      DUMP
