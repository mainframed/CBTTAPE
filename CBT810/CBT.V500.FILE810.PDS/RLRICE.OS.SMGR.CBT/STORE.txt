* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: STORE                                                 *
*                                                                     *
*  STORE a record                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
STORE    CSECT
STORE    AMODE 31
STORE    RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING STORE,R15
         B     INIT0000
MODID    DC    CL8'STORE'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)                  SAVE REGS
         LR    R10,R15                          COPY ENTRY POINT ADDR
         DROP  R15
         USING STORE,R10                        DEFINE BASE
         USING DSPCREQ,R9                       DEFINE BASE
         L     R8,DSPCREQ_DATASPACE             DATASPACE INFO ADDRESS
         USING SCONTROL_DATASPACE,R8
         L     R15,COMM_OSDSPACE_DXD            WORK AREA BASE
         A     R15,DXD_START                    PLUS DISPLACEMENT
         ST    R13,4(R15)                       SAVE R13
         ST    R15,8(R13)                       CHAIN SAVE AREA
         LR    R13,R15                          COPY WORK AREA ADDRESS
         USING DXDSTORE,R13
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         ITRACE ID=STORE,                                              +
               DATA1=SCONTROL_DATASPACE_NAME,                          +
               DATA2=(DSPCREQ_RECORD_NBR,4)
         ITRACE ID=DATA,                                               +
               DATA1=(DSPCREQ_RECORD_ADDR,4),                          +
               DATA2=(DSPCREQ_RECORD_LENGTH,4)
         SR    R6,R6                              ADDR OF CONTROL INFO
         LAM   R6,R6,SCONTROL_DATASPACE_ALET      SET THE ALET
         SAC   512                                TURN ON AR MODE
         L     R5,DSPCREQ_RECORD_NBR              RECORD NBR TO STORE
         STCM  R5,15,COMM_INFO_01
         C     R5,SCONTROL_DATASPACE_FIRST_RECORD TOO LOW?
         BL    ERR0040                            YES
         S     R5,SCONTROL_DATASPACE_FIRST_RECORD CONVERT TO RELATIVE
         MH    R5,=Y(DATASPACE_INDEX_L)           MULTIPLY BY SIZE EACH
         LAM   R5,R5,SCONTROL_DATASPACE_ALET      SET AR
         USING DATASPACE_INDEX,R5
         ICM   R2,15,DATASPACE_INDEX_ADDR
         BNZ   ERR0090                            RECORD ALREADY STORED
         CLC   SCONTROL_DATASPACE_LAST_RECORD,DSPCREQ_RECORD_NBR
         BH    STORE010                           NO
         MVC   SCONTROL_DATASPACE_LAST_RECORD,DSPCREQ_RECORD_NBR
STORE010 DS    0H
         L     R0,SCONTROL_DATASPACE_BYTES        BYTES STORED
         A     R0,DSPCREQ_RECORD_LENGTH           PLUS NEW RECORD
         ST    R0,SCONTROL_DATASPACE_BYTES        UPDATE
         L     R2,SCONTROL_DATASPACE_LAST_ADDRESS ADDR OF LAST STORED
         L     R3,DSPCREQ_RECORD_LENGTH           LENGTH OF NEW RECORD
         SR    R2,R3                              COMPUTE ADDRESS
         ST    R2,SCONTROL_DATASPACE_LAST_ADDRESS UPDATE DATA SPACE
         LA    R1,DATASPACE_INDEX_L(,R5)          PLUS LENGTH OF INDEX
         CR    R1,R2                              OVERLAP?
         BNL   ERR0100                                 YES
         ST    R2,SCONTROL_DATASPACE_LAST_ADDRESS      SAVE ADDRESS
         ST    R2,DATASPACE_INDEX_ADDR
         ST    R3,DATASPACE_INDEX_LENGTH
STORE020 DS    0H
         TM    SESS_DATA_TYPE,$DATA_TYPE_VSAM          VSAM?
         BO    STORE050                                YES
         MVC   DATASPACE_INDEX_CCHHR,DSPCREQ_CCHHR
         MVC   DATASPACE_INDEX_SEQ,DSPCREQ_SEQ
         B     STORE060
STORE050 DS    0H
         MVC   DATASPACE_INDEX_RBA,DSPCREQ_RBA
STORE060 DS    0H
         LAM   R2,R2,SCONTROL_DATASPACE_ALET           SET AR
         L     R14,DSPCREQ_RECORD_ADDR                 NEW RECORD ADDR
         LR    R15,R3                                  COPY LENGTH
         MVCL  R2,R14                                  COPY
         SAC   0                                       TURN OFF AR MODE
         ITRACE ID=NEW_LAST,                                           +
               DATA1=(SCONTROL_DATASPACE_LAST_RECORD,4),               +
               DATA2=(SCONTROL_DATASPACE_LAST_ADDRESS,4)
         CLC   DSPCREQ_RECORD_LENGTH,SCONTROL_DATASPACE_LONGEST_RECORD
         BNH   STORE070
         MVC   SCONTROL_DATASPACE_LONGEST_RECORD,DSPCREQ_RECORD_LENGTH
         ITRACE ID=NEW_LONG,                                           +
               DATA1=(SCONTROL_DATASPACE_LONGEST_RECORD,4)
STORE070 DS    0H
         MVI   DSPCREQ_RC,$DSPCREQ_OK                  SET RC
         B     EXIT0000                                DONE
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0040  DS    0H
         MVI   COMM_MSG_ID+1,4
         B     ERR1000
ERR0080  DS    0H
         ITRACE ID=BAD_LNTH,                                           +
               DATA1=(DSPCREQ_RECORD_LENGTH,4)
         MVI   COMM_MSG_ID+1,8
         B     ERR1000
ERR0090  DS    0H
         MVI   COMM_MSG_ID+1,9
         B     ERR1000
ERR0100  DS    0H
         SAC   0
*        ITRACE ID=FULL
         MVI   COMM_MSG_ID+1,10
         MVI   DSPCREQ_RC,$DSPCREQ_FULL
         B     ERR1010
ERR1000  DS    0H
         SAC   0
ERR1010  DS    0H
         MVC   COMM_INFO_02,SCONTROL_DATASPACE_NAME
         L     R15,COMM_V_OSMSG
         BALR  R14,R15
         CLI   DSPCREQ_RC,$DSPCREQ_FULL
         BE    EXIT0000
         MVI   DSPCREQ_RC,$DSPCREQ_ERROR
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         SAC   0
         LAM   R2,R2,F0
         LAM   R5,R5,F0
         LAM   R6,R6,F0
         ITRACE ID=EXIT,                                               +
               DATA1=(DSPCREQ_RC,1)
         L     R13,4(R13)                       RESTORE R13
         LM    R14,R12,12(R13)                  RESTORE REGISTERS
         BR    R14                              RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START     DC    Q(DXDSTORE)
F0            DC    F'0'

              LTORG
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDSTORE      DSECT
              COPY      DXDPREF
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              DSPCREQ  DSECT=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
SCONTROL      DSECT
              SCONTROL SCONTROL
SCONTROL_L    EQU      *-SCONTROL
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COPY     SINDEX
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
              COPY     REGEQU
              END      STORE
