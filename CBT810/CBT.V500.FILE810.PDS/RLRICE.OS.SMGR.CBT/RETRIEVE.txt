* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: RETRIEVE                                              *
*                                                                     *
*  RETRIEVE a record                                                  *
*                                                                     *
* ------------------------------------------------------------------- *
RETRIEVE CSECT
RETRIEVE AMODE 31
RETRIEVE RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING RETRIEVE,R15
         B     INIT0000
MODID    DC    CL8'RETRIEVE'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT ADDR
         DROP  R15
         USING RETRIEVE,R10                   DEFINE BASE
         USING DSPCREQ,R9                     DEFINE BASE
         L     R8,DSPCREQ_DATASPACE           DATASPACE INFO ADDRESS
         USING SCONTROL_DATASPACE,R8
         L     R15,COMM_OSDSPACE_DXD          WORK AREA BASE
         A     R15,DXD_START                  PLUS DISPLACEMENT
         ST    R13,4(R15)                     SAVE R13
         ST    R15,8(R13)                     CHAIN SAVE AREA
         LR    R13,R15                        COPY WORK AREA ADDRESS
         USING DXDRTRV,R13
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         ITRACE ID=RETRIEVE,                                           +
               DATA1=SCONTROL_DATASPACE_NAME,                          +
               DATA2=(DSPCREQ_RECORD_NBR,4)
         OC    SCONTROL_DATASPACE_LAST_RECORD,SCONTROL_DATASPACE_LAST_R+CORD
               ECORD
         BZ    ERR0030                        NO DATA IN DATA SPACE
         L     R5,DSPCREQ_RECORD_NBR          RECORD NUMBER TO RETRIEVE
         STCM  R5,15,COMM_INFO_01
         C     R5,SCONTROL_DATASPACE_FIRST_RECORD TOO LOW?
         BL    ERR0040                            YES
         C     R5,SCONTROL_DATASPACE_LAST_RECORD  TOO HIGH?
         BH    ERR0050                            YES
         S     R5,SCONTROL_DATASPACE_FIRST_RECORD CONVERT TO RELATIVE
         MH    R5,=Y(DATASPACE_INDEX_L)           MULTIPLY BY SIZE EACH
         LAM   R5,R5,SCONTROL_DATASPACE_ALET      SET AR
         SAC   512
         USING DATASPACE_INDEX,R5
         ICM   R2,15,DATASPACE_INDEX_ADDR
         BZ    ERR0060                        RECORD NOT STORED
         L     R3,DATASPACE_INDEX_LENGTH      LENGTH OF STORED RECORD
         ICM   R14,15,DSPCREQ_RECORD_ADDR     CALLER'S I/O AREA ADDRESS
         BZ    ERR0070
         LR    R15,R3                         COPY RECORD'S LENGTH
         ST    R3,DSPCREQ_RECORD_LENGTH       RETURN LENGTH TO CALLER
         MVC   DSPCREQ_RBA,DATASPACE_INDEX_RBA
         MVC   DSPCREQ_CCHHR,DATASPACE_INDEX_CCHHR
         MVC   DSPCREQ_SEQ,DATASPACE_INDEX_SEQ
         LAM   R2,R2,SCONTROL_DATASPACE_ALET  SET AR
         MVCL  R14,R2                         COPY DATA TO CALLER
         SAC   0
         MVI   DSPCREQ_RC,$DSPCREQ_OK         SET RC
         ITRACE ID=RC,                                                 +
               DATA1=(DSPCREQ_RC,1),                                   +
               DATA2=(DSPCREQ_RECORD_NBR,4)
         B     EXIT0000                       DONE
         DROP  R5
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0030  DS    0H
         MVI   COMM_MSG_ID+1,3
         B     ERR1000
ERR0040  DS    0H
         MVI   COMM_MSG_ID+1,4
         B     ERR1000
ERR0050  DS    0H
         MVI   COMM_MSG_ID+1,5
         B     ERR1000
ERR0060  DS    0H
         MVI   COMM_MSG_ID+1,6
         MVI   DSPCREQ_RC,$DSPCREQ_NOT_STORED  SET RC
         B     ERR1000
ERR0070  DS    0H
         MVI   COMM_MSG_ID+1,7
ERR1000  DS    0H
         SAC   0
         MVC   COMM_INFO_02,SCONTROL_DATASPACE_NAME
         L     R15,COMM_V_OSMSG
         BALR  R14,R15
         CLI   DSPCREQ_RC,$DSPCREQ_OK          RC STILL OK?
         BNE   EXIT0000                        NO
         MVI   DSPCREQ_RC,$DSPCREQ_ERROR       SET RC
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         SAC   0
         ITRACE ID=EXIT,                                               +
               DATA1=(DSPCREQ_RC,1)
         LAM   R2,R2,F0
         LAM   R5,R5,F0
         L     R13,4(R13)                RERETRIEVE SAVE AREA ADDRESS
         LM    R14,R12,12(R13)           RERETRIEVE REMAINING REGISTERS
         BR    R14                       RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START   DC    Q(DXDRTRV)
F0          DC    F'0'
F1          DC    F'1'

MSG_02      DC    C'NOT FOUND'
MSG_02_L    EQU   *-MSG_02

            LTORG
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDRTRV     DSECT
            COPY      DXDPREF
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
            COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
            SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
            BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
            DSPCREQ  DSECT=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
SCONTROL    DSECT
            SCONTROL SCONTROL
SCONTROL_L  EQU      *-SCONTROL
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
            COPY     SINDEX
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
            COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
            COPY     REGEQU
            END      RETRIEVE
