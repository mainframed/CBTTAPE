* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: FMTIF                                                 *
*                                                                     *
*  Process IF FMAT blocks during a 'format'                           *
*                                                                     *
* ------------------------------------------------------------------- *
FMTIF    CSECT
FMTIF    AMODE 31
FMTIF    RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING FMTIF,R15
         B     INIT0000
MODID    DC    CL8'FMTIF'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)               SAVE REGS
         LR    R10,R15                       COPY ENTRY POINT
         DROP  R15
         USING FMTIF,R10                     DEFINE BASE
         L     R9,SESS_DXD_ADDR              WORK AREA
         LR    R15,R9                        COPY WORK AREA ADDRESS
         USING FMT_COMMON,R9
         L     R15,SESS_DXD_ADDR             WORK AREA BASE
         A     R15,DXD_START                 PLUS DISP
         ST    R13,4(R15)                    SAVE R13
         ST    R15,8(R13)                    CHAIN SAVE AREA
         LR    R13,R15                       COPY WORK AREA ADDRESS
         USING DXDFIF,R13                    DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         MVC   DXD_SAVED_RETRY,SESS_RETRY
         L     R7,DXD_FMAT_FMAT              FMAT BEING PROCESSED
         USING FMAT,R7
         ITRACE ID=ENTRY,                                              +
               RDATA1=R7
         LA    R1,RTRY0000                   ABEND RETRY POINT
         ST    R1,SESS_RETRY
         LH    R6,FMAT_START                 FIELD'S STARTING POINT
         LH    R1,FMAT_LENGTH                FIELD LENGTH
         AR    R1,R6                         ENDING POINT
         BCTR  R1,0                          RELATIVE TO ZERO
         C     R1,DXD_RECORD_LENGTH          BEYOND END OF RECORD?
         BH    IF0020                        YES
         L     R6,DXD_RECORD_ADDR            RECORD ADDRESS
         LA    R15,IF0010                    MATCH POINT
         MVC   FMAT_RETURN,B_IF0020          NOT MATCH POINT
EX_CMPR  EX    R0,FMAT_COMPARE               COMPARE THE DATA
         B     FMAT_BRANCH                   BRANCH
* ------------------------------------------------------------------- *
*        An IF was just executed (data matched)                       *
* ------------------------------------------------------------------- *
IF0010   DS    0H
         LH    R1,DXD_IF_LEVEL               CURRENT IF LEVEL
         SLL   R1,2                          MULTIPLY BY 4
         LA    R1,DXD_IF_TABLE(R1)           COMPUTE ADDRESS
         ST    R7,0(,R1)                     SAVE IF BLOCK ADDRESS
         LH    R1,DXD_IF_LEVEL               CURRENT IF LEVEL
         LA    R1,1(,R1)                     PLUS 1
         STH   R1,DXD_IF_LEVEL               SAVE UPDATED IF LEVEL
         ITRACE ID=MATCH,                                              +
               RDATA1=R7,                                              +
               RDATA2=R1
         MVC   DXD_FMAT_FMAT,FMAT_FIELDS     FIRST DEPENDANT FMAT
         B     EXIT0000                      PROCESS FIELDS
* ------------------------------------------------------------------- *
*        An IF was just executed (data did not match)                 *
* ------------------------------------------------------------------- *
IF0020   DS    0H
         ITRACE ID=NOMATCH,                                            +
               RDATA1=R7,                                              +
               DATA2=(FMAT_NEXT,4)
         NI    SESS_ABEND_FLAG,255-$ABENDED  FORCE ABEND FLAG OFF
         MVC   DXD_FMAT_FMAT,FMAT_NEXT       CONTINUE WITH NEXT FMAT
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         MVI   COMM_MSG_ID+1,1               START NOT VALID
         B     ERR1000
ERR1000  DS    0H
         MVC   COMM_MSG_CSECT,MODID
         MVI   COMM_MSG_ID,0
         L     R15,COMM_V_OSMSG              OSMSG ENTRY POINT
         BALR  R14,R15                       BUILD MESSAGE
         MVI   DXD_RC+1,8                    SET RC
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
RTRY0000 DS    0H
         ITRACE ID=ABEND,                                              +
               DATA1=SESS_ABEND_DISP
         CLC   SESS_ABEND_DISP,EX_CMPR_DISP  ABEND ON EX OF CP?
         BE    IF0020                        YES..
         L     R15,COMM_V_OSABEND
         BALR  R14,R15                       LINK TO OSABEND
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT,                                               +
               DATA1=(DXD_FMAT_FMAT,4)
         LH    R15,DXD_RC                    SET RC
         MVC   SESS_RETRY,DXD_SAVED_RETRY
         L     R13,4(,R13)                   RESTORE R13
         L     R14,12(,R13)                  RESTORE R14
         LM    R0,R12,20(R13)                RESTORE REGISTERS
         BR    R14                           RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START      DC      Q(DXDFIF)
B_IF0020       B       IF0020
EX_CMPR_DISP   DC      Y((EX_CMPR-FMTIF)+4)

               LTORG
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDFIF          DSECT
                COPY    DXDPREF

DXD_SAVED_RETRY DS     A

DXD_RC          DS     H

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               SESSION
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
FMT_COMMON     DSECT
               DS      (DXD_PREFIX_L)X
               COPY    FMTCOMM
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY    FMAT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY    TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY    REGEQU
               END     FMTIF
