* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: CNTRLEIF                                              *
*                                                                     *
*  Interpret ENDIF control statements                                 *
*                                                                     *
* ------------------------------------------------------------------- *
CNTRLEIF CSECT
CNTRLEIF AMODE 31
CNTRLEIF RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING CNTRLEIF,R15
         B     INIT0000
MODID    DC    CL8'CNTRLEIF'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)               SAVE REGS
         LR    R10,R15                       COPY ENTRY POINT
         DROP  R15
         USING CNTRLEIF,R10                  DEFINE BASE
         L     R9,SESS_DXD_ADDR              WORK AREA
         USING FMT_COMMON,R9
         LR    R8,R1                         CONTROL STATEMENT ADDRESS
         USING FORMAT,R8                     DEFINE BASE
         USING FMAT,R7
         LR    R15,R9                        WORK AREA BASE
         A     R15,DXD_START                 PLUS DISP
         ST    R13,4(R15)                    SAVE R13
         ST    R15,8(R13)                    CHAIN SAVE AREA
         LR    R13,R15                       COPY WORK AREA ADDRESS
         USING DXDCEIF,R13                   DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         XC    DXD_RC,DXD_RC                 INITIALIZE RETURN CODE
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         GETMAIN RU,                                                   +
               LV=FMAT_L,                                              +
               LOC=ANY
         OC    DXD_FIRST_FMAT,DXD_FIRST_FMAT BLOCKS ALREADY ON CHAIN?
         BZ    ERR0010                       NO
         L     R15,DXD_FMAT_CHAIN_POINT      CHAIN POINT
         ST    R1,0(R15)                     ADD TO CHAIN
         LR    R7,R1
         XC    FMAT(FMAT_L),FMAT
         MVI   FMAT_TYPE,$FMAT_END_IF        SET TYPE
         SR    R1,R1                         CLEAR R1
         ICM   R1,3,DXD_IF_LEVEL             IF LEVEL
         LR    R0,R1                         COPY IF LEVEL
         BZ    ERR0020                       BETTER NOT BE ZERO
         BCTR  R1,0                          MINUS 1
         STCM  R1,3,DXD_IF_LEVEL             SAVE NEW IF LEVEL
         SLL   R1,2                          MULTIPLBY BY 4
         LA    R1,DXD_IF_TABLE(R1)           COMPUTE ADDRESS
         MVC   DXD_FMAT_CHAIN_POINT,0(R1)    RESTORE CHAIN POINT
         ITRACE ID=ENDIF,                                              +
               RDATA1=R0,                                              +
               DATA2=(DXD_FMAT_CHAIN_POINT,4)
         B     EXIT0000                      EXIT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         MVI   COMM_MSG_ID+1,1               NO FMAT BLOCKS ON CHAIN
         B     ERR1000
ERR0020  DS    0H
         MVI   COMM_MSG_ID+1,2               ENDIF WITH NO MATCHING IF
         B     ERR1000
ERR1000  DS    0H
         MVC   COMM_MSG_CSECT,MODID
         MVI   COMM_MSG_ID,0
         L     R15,COMM_V_OSMSG              OSMSG ENTRY POINT
         BALR  R14,R15                       BUILD MESSAGE
         MVI   DXD_RC+1,8                    SET RC
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         LH    R15,DXD_RC                    SET RC
         L     R13,4(,R13)                   RESTORE R13
         L     R14,12(,R13)                  RESTORE R14
         LM    R0,R12,20(R13)                RESTORE REGISTERS
         BR    R14                           RETURN
* ------------------------------------------------------------------- *
*        CONSTANTS                                                    *
* ------------------------------------------------------------------- *
DXD_START      DC   Q(DXDCEIF)
* ------------------------------------------------------------------- *
*              WORK AREAS                                             *
* ------------------------------------------------------------------- *
DXDCEIF        DSECT
               COPY    DXDPREF

DXD_RC         DS      H

* ------------------------------------------------------------------- *
*              COMMON AREA                                            *
* ------------------------------------------------------------------- *
               COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               SESSION
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*              FMT COMMON                                             *
* ------------------------------------------------------------------- *
FMT_COMMON     DSECT
               DS      (DXD_PREFIX_L)X
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY    FMTCOMM
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY    FORMAT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY    FMAT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY    TRENTRY
* ------------------------------------------------------------------- *
*              EQUATES                                                *
* ------------------------------------------------------------------- *
               COPY    REGEQU
               END     CNTRLEIF
