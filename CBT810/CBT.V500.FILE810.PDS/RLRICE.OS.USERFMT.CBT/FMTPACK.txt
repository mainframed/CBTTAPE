* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: FMTPACK                                               *
*                                                                     *
*  Process packed decimal fields during 'format'                      *
*                                                                     *
*                                                                     *
*  If no "edit word" is specified, the number is displayed in vanilla *
*  99999 type format with leading zeros displayed, no plus or minus   *
*  sign.                                                              *
*                                                                     *
*  Rather than use hardware edit instruction (ED), the editing is     *
*  done more 'manually'.  "Edit words" are much like "PICTURE"        *
*  values valid to COBOL.                                             *
*                                                                     *
* ------------------------------------------------------------------- *
FMTPACK  CSECT
FMTPACK  AMODE 31
FMTPACK  RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING FMTPACK,R15
         B     INIT0000
MODID    DC    CL8'FMTPACK'
         DC    CL8'&SYSDATE'
         DC    CL7'&SYSTIME'
INIT0000 DS    0H
         STM   R14,R12,12(R13)               SAVE REGS
         LR    R10,R15                       COPY ENTRY POINT
         DROP  R15
         USING FMTPACK,R10                   DEFINE BASE
         L     R9,SESS_DXD_ADDR              WORK AREA
         LR    R15,R9                        COPY WORK AREA ADDRESS
         USING FMT_COMMON,R9
         L     R15,SESS_DXD_ADDR             WORK AREA BASE
         A     R15,DXD_START                 PLUS DISP
         ST    R13,4(R15)                    SAVE R13
         ST    R15,8(R13)                    CHAIN SAVE AREA
         LR    R13,R15                       COPY WORK AREA ADDRESS
         USING DXDFPACK,R13                  DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         L     R7,DXD_FMAT_FMAT              FMAT BEING PROCESSED
         USING FMAT,R7
         ITRACE ID=ENTRY,                                              +
               RDATA1=R7
         L     R8,COMM_OSSPFD
         USING OSSPFD,R8
* ------------------------------------------------------------------- *
*        Unpack the data first                                        *
* ------------------------------------------------------------------- *
         LH    R1,FMAT_START                 STARTING POSITION
         LH    R2,FMAT_LENGTH                LENGTH
         A     R1,DXD_RECORD_ADDR            PLUS RECORD ADDRESS
         BCTR  R1,0                          RELATIVE TO ZERO
         ITRACE ID=UNPACK,                                             +
               DATA1=0(R1),                                            +
               RDATA2=R2
         LA    R3,COMM_INFO_01               WORK AREA
         XC    COMM_INFO_01,COMM_INFO_01     INITIALIZE
PACK0010 DS    0H
         UNPK  0(3,R3),0(2,R1)               UNPACK
         TR    0(2,R3),COMM_HEXCHAR          TRANSLATE
         MVI   2(R3),x'00'
         LA    R1,1(,R1)                     NEXT IN SOURCE
         LA    R3,2(,R3)                     NEXT IN WORK AREA
         BCT   R2,PACK0010                   LOOP
* ------------------------------------------------------------------- *
*        Verify the data is all valid numeric                         *
* ------------------------------------------------------------------- *
         ITRACE ID=VERIFY,                                             +
               DATA1=COMM_INFO_01+0,                                   +
               DATA2=COMM_INFO_01+8
         LA    R1,COMM_INFO_01               1ST UNPACKED DIGIT
         LH    R2,FMAT_LENGTH                LENGTH
         SLL   R2,1                          MULTIPLY BY 2
         BCTR  R2,0                          MINUS 1
PACK0020 DS    0H
         CLI   0(R1),C'0'                    VALID DIGIT?
         BL    ERR0010                       NO
         CLI   0(R1),C'9'                    VALID DIGIT?
         BH    ERR0010                       NO
         LA    R1,1(,R1)                     NEXT DIGIT
         BCT   R2,PACK0020                   LOOP
* ------------------------------------------------------------------- *
*        Verify the sign is valid                                     *
* ------------------------------------------------------------------- *
         MVC   DXD_SIGN,0(R1)                SAVE SIGN
         CLI   0(R1),C'C'                    VALID POSITIVE SIGN?
         BE    PACK0030                      YES
         CLI   0(R1),C'D'                    VALID NEGATIVE SIGN?
         BE    PACK0030                      YES
         CLI   0(R1),C'F'                    VALID POSITIVE SIGN?
         BE    PACK0030                      YES
         B     ERR0020                       BAD
* ------------------------------------------------------------------- *
*                                                                     *
*        Data is valid, sign is valid.  "Edit" it.                    *
*                                                                     *
*    If no edit word is specified, copy it unpacked for "as is".      *
*                                                                     *
*    If an edit word is specified, the data is 'edited' into          *
*    COMM_INFO_02, then copied to the VDATA buffer.                   *
*                                                                     *
* ------------------------------------------------------------------- *
PACK0030 DS    0H
         ITRACE ID=PRE_EDIT,                                           +
               DATA1=COMM_INFO_01+0,                                   +
               DATA2=COMM_INFO_02+8
         TM    FMAT_OPTIONS,$FMAT_EDITWORD   EDIT WORD SPECIFIED?
         BO    PACK0050                      YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         ITRACE ID=NOWORD
PACK0040 DS    0H
         LH    R0,FMAT_ROW                   DISPLAY ROW
         LH    R1,FMAT_COLUMN                DISPLAY COLUMN
         BCTR  R0,0                          ROW RELATIVE TO ZERO
         MH    R0,SPF_VWIDTH+2               MULTIPLY BY LINE LENGTH
         BCTR  R1,0                          COLUMN RELATIVE TO ZERO
         AR    R0,R1                         PLUS COLUMN
         A     R0,DXD_VDATA_ADDR             PLUS VDATA ADDRESS
         LH    R1,FMAT_LENGTH                SOURCE LENGTH
         SLL   R1,1                          MULTIPLY BY 2
         BCTR  R1,0                          MINUS 1
         LA    R14,COMM_INFO_01              UNPACKED NUMBER
         LR    R15,R1                        COPY LENGTH
         MVCL  R0,R14                        COPY TO VDATA BUFFER
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
*     Perform "custom editing"                                        *
*                                                                     *
*     The number to be edited has been unpacked into COMM_INFO_01.    *
*     The edit word is in FMAT_DATA.                                  *
*     Output will be performed into COMM_INFO_02, then copied to      *
*     the VDATA buffer.                                               *
*                                                                     *
*     The length of the output is determined by the user supplied     *
*     edit mask (columns 50-80 on the FIELD control statement).       *
*     The end of the mask will be indicated by a byte of binary zero. *
*                                                                     *
* ------------------------------------------------------------------- *
PACK0050 DS    0H
         ITRACE ID=CALLEDIT
         L     R15,V_FMTEDIT                 FMTEDIT ENTRY POINT
         BALR  R14,R15                       LINK TO FMTEDIT
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         MVC   COMM_MSG_1(MSG_01_L),MSG_01
         B     ERR1000
ERR0020  DS    0H
         MVC   COMM_MSG_1(MSG_02_L),MSG_02
ERR1000  DS    0H
         MVI   DXD_RC+1,8
         MVC   COMM_INFO_01,COMM_BLANKS
         MVC   COMM_INFO_01(L'BAD),BAD
         B     PACK0040                      <-- DON'T LIKE 'UPHILL' BR
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         LH    R15,DXD_RC                    SET RC
         L     R13,4(,R13)                   RESTORE R13
         L     R14,12(,R13)                  RESTORE R14
         LM    R0,R12,20(R13)                RESTORE REGISTERS
         BR    R14                           RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START      DC      Q(DXDFPACK)
V_FMTEDIT      DC      V(FMTEDIT)

BAD            DC      C'*BAD*'

MSG_01         DS      0C
               DC      C'Bad packed number'
MSG_01_L       EQU     *-MSG_01

MSG_02         DS      0C
               DC      C'Bad sign on packed number'
MSG_02_L       EQU     *-MSG_02

               LTORG
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDFPACK       DSECT
               COPY  DXDPREF

DXD_RC         DS    H

DXD_SIGN       DS    X
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               SESSION
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
FMT_COMMON     DSECT
               DS      (DXD_PREFIX_L)X
               COPY    FMTCOMM
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY    FMAT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               OSSPFD  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY    TRENTRY
* ------------------------------------------------------------------- *
*              EQUATES                                                *
* ------------------------------------------------------------------- *
               COPY    REGEQU
               END     FMTPACK
