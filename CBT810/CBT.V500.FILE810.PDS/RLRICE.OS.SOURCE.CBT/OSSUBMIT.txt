* ------------------------------------------------------------------- *
*                                                                     *
*        SUBMIT files or PDS members as batch jobs.                   *
*                                                                     *
* ------------------------------------------------------------------- *
OSSUBMIT CSECT
OSSUBMIT AMODE 31
OSSUBMIT RMODE 24
         USING OSCOMM,R12
         USING SESSION,R11
         USING OSSUBMIT,R15
         B     INIT0000
MODID    DC    CL8'OSSUBMIT'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(SUBMITEND-OSSUBMIT)
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT
         DROP  R15
         USING OSSUBMIT,R10                   DEFINE BASE
         LR    R9,R1
         USING SUBREQ,R9                      DEFINE BASE
         GETMAIN RU,                                                   +
               LV=OSSUBMIT_WORK_L,                                     +
               LOC=BELOW
         ST    R13,4(,R1)                     SAVE R13
         ST    R1,8(,R13)                     CHAIN SAVE AREA
         LR    R13,R1                         COPY WORK AREA ADDRESS
         USING OSSUBMIT_WORK,R13              DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY,                                              +
               RDATA1=R9
         MVC   WORK_IN_DCB(INDCB_L),INDCB
         MVC   WORK_OUT_DCB(OUTDCB_L),OUTDCB
         MVC   WORK_OPEN(OPEN_L),OPEN
         MVC   WORK_CLOSE(CLOSE_L),CLOSE
         LA    R8,WORK_IN_DCB
IN       USING IHADCB,R8
         LA    R7,WORK_OUT_DCB
OUT      USING IHADCB,R7
         ZAP   WORK_RECORDS,P0                INIT RECORD COUNTER
* ------------------------------------------------------------------- *
*        Allocate the input data set                                  *
* ------------------------------------------------------------------- *
         ITRACE ID=ALLOC_IN
         MVI   DAIR_FUNC,$DAIR_ALLOC          SET COMMAND
         MVI   DAIR_OPTS,0                    USE DEFAULT OPTIONS
         MVC   DAIR_DSN,SUBREQ_DSN            COPY DATA SET NAME
         MVC   DAIR_MEMBER,SUBREQ_MEMBER
         MVC   DAIR_VOLSER,SESS_VOLSER
         MVC   DAIR_UNIT,COMM_BLANKS
         MVI   DAIR_DISP,$DAIR_DISP_SHR
         LA    R1,DAIRREQ                     DAIR PARMS
         L     R15,COMM_V_OSDAIR              OSDAIR ENTRY POINT
         BALR  R14,R15                        ALLOCATE INPUT DATA SET
         OC    DAIR_R15,DAIR_R15              SUCCESSFUL?
         BNZ   ERR0010                        NO
         MVC   IN.DCBDDNAM,DAIR_DD            COPY DD NAME
         MVC   WORK_IN_DDNAME,DAIR_DD
* ------------------------------------------------------------------- *
*        Allocate the output to internal reader                       *
* ------------------------------------------------------------------- *
         ITRACE ID=ALLOCOUT
         MVC   DAIR_DSN,COMM_BLANKS
         MVC   DAIR_MEMBER,COMM_BLANKS
         MVC   DAIR_SYSOUT,COMM_INTRDR
         MVI   DAIR_FUNC,$DAIR_ALLOC
         MVI   DAIR_OPTS,$DAIR_INTRDR
         LA    R1,DAIRREQ                     DAIR PARMS
         L     R15,COMM_V_OSDAIR              OSDAIR ENTRY POINT
         BALR  R14,R15                        ALLOCATE
         OC    DAIR_R15,DAIR_R15              SUCCESSFUL?
         BNZ   ERR0010                        NO
         MVC   OUT.DCBDDNAM,DAIR_DD           COPY DDNAME
         MVC   WORK_OUT_DDNAME,DAIR_DD
* ------------------------------------------------------------------- *
*        OPEN the input and output                                    *
* ------------------------------------------------------------------- *
         ITRACE ID=OPEN_IN,                                            +
               DATA1=IN.DCBDDNAM
         OPEN  (WORK_IN_DCB,INPUT),                                    +
               MODE=31,                                                +
               MF=(E,WORK_OPEN)
         ITRACE ID=OPEN_OUT,                                           +
               DATA1=OUT.DCBDDNAM
         OPEN  (WORK_OUT_DCB,OUTPUT),                                  +
               MODE=31,                                                +
               MF=(E,WORK_OPEN)
* ------------------------------------------------------------------- *
*        Copy the input to the output                                 *
* ------------------------------------------------------------------- *
SUB0010  DS    0H
         GET   WORK_IN_DCB,WORK_IO            READ A RECORD
         AP    WORK_RECORDS,P1
         PUT   WORK_OUT_DCB,WORK_IO           WRITE IT TO OUTPUT
         B     SUB0010                        LOOP
* ------------------------------------------------------------------- *
*        CLOSE the DCBs                                               *
* ------------------------------------------------------------------- *
SUB0020  DS    0H
         ITRACE ID=CLOSE_IN
         CLOSE WORK_IN_DCB,                                            +
               MODE=31,                                                +
               MF=(E,WORK_OPEN)
         FREEPOOL WORK_IN_DCB
         ITRACE ID=CLOSEOUT
         CLOSE WORK_OUT_DCB,                                           +
               MODE=31,                                                +
               MF=(E,WORK_OPEN)
         FREEPOOL WORK_OUT_DCB
* ------------------------------------------------------------------- *
*        De-allocate input                                            *
* ------------------------------------------------------------------- *
         ITRACE ID=DEALLOC,                                            +
               DATA1=IN.DCBDDNAM
         MVC   DAIR_DD,WORK_IN_DDNAME
         MVI   DAIR_FUNC,$DAIR_FREE           SET FUNCTION
         LA    R1,DAIRREQ                     DAIR PARMS
         L     R15,COMM_V_OSDAIR              OSDAIR ENTRY POINT
         BALR  R14,R15                        DEALLOCATE INPUT DATA SET
         MVI   SUBREQ_RC,$SUBREQ_OK
         MVC   COMM_MSG_2(MSG_01_L),MSG_01
         MVC   COMM_MSG_2+(MSG_01_MEMBER-MSG_01)(L'MSG_01_MEMBER),SUBRE+
               Q_MEMBER
         ED    COMM_MSG_2+(MSG_01_RECORDS-MSG_01)(L'MSG_01_RECORDS),WOR+
               K_RECORDS
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         ITRACE ID=DAIR_ERR
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         SR    R15,R15
         B     EXITEXIT
EXIT0004 DS    0H
         LA    R15,4
EXITEXIT DS    0H
         ITRACE ID=EXIT,                                               +
               RDATA1=R15
         LR    R1,R13
         L     R13,4(,R13)                    RESTORE SAVE AREA ADDRESS
         FREEMAIN RU,                                                  +
               A=(R1),                                                 +
               LV=OSSUBMIT_WORK_L
         L     R14,12(,R13)
         LM    R0,R12,20(R13)                 RESTORE REGISTERS
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
INDCB    DCB   DSORG=PS,                                               +
               DDNAME=ANYDD,                                           +
               EODAD=SUB0020,                                          +
               RECFM=FB,                                               +
               LRECL=80,                                               +
               MACRF=GM
INDCB_L  EQU   *-INDCB
OUTDCB   DCB   DSORG=PS,                                               +
               DDNAME=ANYDD,                                           +
               RECFM=FB,                                               +
               LRECL=80,                                               +
               MACRF=PM
OUTDCB_L EQU   *-OUTDCB
OPEN     OPEN  (*,INPUT),                                              +
               MODE=31,                                                +
               MF=L
OPEN_L   EQU   *-OPEN
CLOSE    CLOSE (*),                                                    +
               MODE=31,                                                +
               MF=L
CLOSE_L  EQU   *-CLOSE

P0       DC    P'0'
P1       DC    P'1'
MSG_01         DS   0C
               DC   C'OSSUBMIT01I '
MSG_01_MEMBER  DC   CL8' '
               DC   C' submitted,'
MSG_01_RECORDS DC   X'4020202020202120'
               DC   C' records'
MSG_01_L       EQU  *-MSG_01
SUBMITEND      EQU  *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
OSSUBMIT_WORK   DSECT
                COPY     DXDPREF
WORK_IN_DCB     DS       0F,(INDCB_L)X
WORK_OUT_DCB    DS       0F,(OUTDCB_L)X
WORK_OPEN       DS       0F,(OPEN_L)X
WORK_CLOSE      DS       0F,(CLOSE_L)X

WORK_IN_DDNAME  DS       CL8
WORK_OUT_DDNAME DS       CL8

WORK_RECORDS    DS       PL4

                DAIRREQ  DSECT=NO

WORK_IO         DS       CL80
OSSUBMIT_WORK_L EQU      *-OSSUBMIT_WORK
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                SUBREQ   DSECT=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                IBMMAC   DCBD=PS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                COPY     REGEQU
                END      OSSUBMIT
