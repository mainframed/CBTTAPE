* ------------------------------------------------------------------- *
*                                                                     *
*        Offload members to a sequential file                         *
*                                                                     *
* ------------------------------------------------------------------- *
OSOFFLD  CSECT
OSOFFLD  AMODE 31
OSOFFLD  RMODE 24
         USING OSCOMM,R12
         USING SESSION,R11
         USING OSOFFLD,R15
         B     INIT0000
MODID    DC    CL8'OSOFFLD'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(OFFLDEND-OSOFFLD)
INIT0000 DS    0H
         USING DS1FMTID,SESS_DSCB
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT
         DROP  R15
         USING OSOFFLD,R10                    DEFINE BASE
         LR    R8,R1
         USING OFFLDREQ,R8                    DEFINE BASE
         GETMAIN RU,                                                   +
               LV=OSOFFLD_WORK_L,                                      +
               LOC=BELOW
         ST    R13,4(,R1)                     SAVE R13
         ST    R1,8(,R13)                     CHAIN SAVE AREA
         LR    R13,R1                         COPY WORK AREA ADDRESS
         USING OSOFFLD_WORK,R13               DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         L     R9,COMM_OSSPFD                 SPF DATA
         USING OSSPFD,R9
         ITRACE ID=ENTRY,                                              +
               RDATA1=R8
         CLI   OFFLDREQ_FUNC,$OFFLDREQ_OPEN
         BE    OPEN0000              YES
         CLI   OFFLDREQ_FUNC,$OFFLDREQ_WRITE
         BE    MAIN0000              YES
         CLI   OFFLDREQ_FUNC,$OFFLDREQ_CLOSE
         BE    CLOSE000              YES
         DC    H'0',C'INVALID FUNCTION'
* ------------------------------------------------------------------- *
*        Allocate the output data set                                 *
* ------------------------------------------------------------------- *
OPEN0000 DS    0H
         ITRACE ID=GET_OFILE
         LA    R1,PANEL_NAME                  PANEL NAME
         L     R15,COMM_V_OSOFILE             OSOFILE ENTRY POINT
         BALR  R14,R15                        PROMPT FOR OUTPUT DATASET
         LTR   R15,R15                        CONTINUE?
         BNZ   ABORT000                       NO
         XC    DAIRREQ(DAIRREQ_L),DAIRREQ
         MVI   DAIR_FUNC,$DAIR_ALLOC
         CLI   SPF_OUTPUT_DSN,X'7D'           QUOTED NAME?
         BE    OPEN0010                       YES
         MVC   DAIR_DSN,COMM_BLANKS           INITIALIZE TO BLANKS
         LH    R2,COMM_USER_LENGTH            USER ID LENGTH
         BCTR  R2,0                           MINUS 1 FOR EXECUTE
         EX    R2,DSN_MVC1                    COPY USER ID
         LA    R3,DAIR_DSN+1(R2)              END OF DSN
         MVI   0(R3),C'.'                     INSERT DELIMITING PERIOD
         LA    R4,L'DAIR_DSN-3                MAXIMUM LENGTH
         SR    R4,R2                          MINUS USER ID LENGTH
         BCTR  R4,0                           MINUS 1 FOR PERIOD
         EX    R4,DSN_MVC2                    COPY SPECIFIED PART
         B     OPEN0040
OPEN0010 DS    0H
         MVC   DAIR_DSN(L'DAIR_DSN-1),SPF_OUTPUT_DSN+1
         LA    R14,DAIR_DSN+(L'DAIR_DSN-1)
         LA    R15,L'DAIR_DSN                 LOOP LIMITER
OPEN0020 DS    0H
         CLI   0(R14),X'7D'                   ENDING QUOTE?
         BE    OPEN0030                       YES
         BCTR  R14,0
         BCT   R15,OPEN0020
OPEN0030 DS    0H
         MVI   0(R14),C' '                    REMOVE QUOTE
OPEN0040 DS    0H
         MVC   DAIR_MEMBER,COMM_BLANKS
         MVC   DAIR_VOLSER,COMM_BLANKS
         MVC   DAIR_UNIT,COMM_BLANKS
         CLC   SPF_OUTPUT_DISP,NEW            NEW OUTPUT DATA SET?
         BE    OPEN0060                       YES
         CLC   SPF_OUTPUT_DISP,MOD            MOD (APPEND?)
         BE    OPEN0050                       YES
         ITRACE ID=DISP_SHR
         MVI   DAIR_DISP,$DAIR_DISP_SHR       SET DISP TO SHR
         B     OPEN0070
OPEN0050 DS    0H
         ITRACE ID=DISP_MOD
         MVI   DAIR_DISP,$DAIR_DISP_MOD       SET DISP TO MOD
         B     OPEN0070
OPEN0060 DS    0H
         ITRACE ID=DISP_NEW
         MVI   DAIR_DISP,$DAIR_DISP_NEW       SET DISP TO NEW
         MVC   DAIR_VOLSER,SPF_OUTPUT_VOLSER
         MVC   DAIR_UNIT,SPF_OUTPUT_UNIT
         PACK  COMM_DWORD,SPF_OUTPUT_SPACE_PRI
         CVB   R1,COMM_DWORD
         STCM  R1,7,DAIR_PRI
         PACK  COMM_DWORD,SPF_OUTPUT_SPACE_SEC
         CVB   R1,COMM_DWORD
         STCM  R1,7,DAIR_SEC
OPEN0070 DS    0H
         LA    R1,DAIRREQ                     ALLOCATION PARMS
         L     R15,COMM_V_OSDAIR              OSDAIR ENTRY POINT
         ITRACE ID=ALOC_OUT,                                           +
               RDATA1=R1,                                              +
               RDATA2=R15
         BALR  R14,R15                        LINK TO OSDAIR
         ITRACE ID=OUT_RC,                                             +
               DATA1=(DAIR_R15,2),                                     +
               DATA2=(DAIR_ERROR,4)
         OC    DAIR_R15,DAIR_R15              SUCCESSFUL?
         BZ    OPEN0080                       YES
         MVC   SPF_MSG_1,COMM_MSG_1           COPY MESSAGES
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
         B     OPEN0000                       DISPLAY PANEL AGAIN
OPEN0080 DS    0H
         MVC   OFFLDREQ_DDNAME,DAIR_DD        SAVE DD NAME
         ITRACE ID=OUT_DD,                                             +
               DATA1=OFFLDREQ_DDNAME
* ------------------------------------------------------------------- *
*        Make sure we don't over-write without user's permission      *
* ------------------------------------------------------------------- *
         CLI   DAIR_DISP,$DAIR_DISP_SHR       ALLOCATED DISP=SHR?
         BNE   OPEN0090                       NO
         CLI   SPF_YES_OR_NO,C'Y'             OVERWRITE?
         BNE   ABORT010                       NO
* ------------------------------------------------------------------- *
*        OPEN the output data set                                     *
* ------------------------------------------------------------------- *
OPEN0090 DS    0H
         GETMAIN RU,                                                   +
               LV=ODCB_L,                                              +
               LOC=BELOW
         LR    R7,R1
         ST    R7,OFFLDREQ_DCB                SAVE DCB ADDRESS
         ITRACE ID=OUT_DCB,                                            +
               RDATA1=R7
OUTPUT   USING IHADCB,R7
         MVC   0(ODCB_L,R7),ODCB_I            COPY DCB
         MVC   OUTPUT.DCBDDNAM,DAIR_DD        COPY DD NAME
         MVC   WORK_OPEN(OPEN_L),OPEN_I
         CLI   DAIR_DISP,$DAIR_DISP_MOD       OPEN 'EXTEND'?
         BE    OPEN0110                       YES
         CLI   DAIR_DISP,$DAIR_DISP_NEW       NEW?
         BE    OPEN0100                       YES
         CLC   DAIR_MEMBER,COMM_BLANKS        MEMBER NAME PRESENT?
         BNE   OPEN0110                       YES
OPEN0100 DS    0H
         ITRACE ID=COPY_DCB
         MVC   OUTPUT.DCBRECFM,DS1RECFM       COPY RECFM
         MVC   OUTPUT.DCBLRECL,DS1LRECL       COPY LRECL
         MVC   OUTPUT.DCBBLKSI,DS1BLKL        COPY BLOCK SIZE
OPEN0110 DS    0H
         ITRACE ID=OPEN_OUT,                                           +
               RDATA1=R7,                                              +
               DATA2=OUTPUT.DCBDDNAM
         OPEN  ((R7),OUTPUT),                                          +
               MODE=31,                                                +
               MF=(E,WORK_OPEN)
         B     OPEN0130
OPEN0120 DS    0H
         ITRACE ID=OPEN_EXT
         OPEN  ((R7),EXTEND),                                          +
               MODE=31,                                                +
               MF=(E,WORK_OPEN)
OPEN0130 DS    0H
         GETMAIN RU,                                                   +
               LV=32768,                                               +
               LOC=BELOW
         ST    R1,OFFLDREQ_IO                 SAVE I/O AREA
         ITRACE ID=IO_AREA,                                            +
               RDATA1=R1
         XC    OFFLDREQ_COUNT,OFFLDREQ_COUNT
         XC    WORK_RECORDS,WORK_RECORDS
         XC    WORK_BYTES,WORK_BYTES
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0000 DS    0H
         ITRACE ID=MAIN
         L     R7,OFFLDREQ_DCB                DCB ADDRESS
         L     R6,OFFLDREQ_IO                 I/O AREA
         ITRACE ID=ALLOCIN,                                            +
               DATA1=SESS_DSN,                                         +
               DATA2=OFFLDREQ_MEMBER
         MVI   DAIR_FUNC,$DAIR_ALLOC          SET COMMAND
         MVC   DAIR_DSN,SESS_DSN              COPY DATA SET NAME
         MVC   DAIR_MEMBER,OFFLDREQ_MEMBER
         MVC   DAIR_VOLSER,SESS_VOLSER
         MVC   DAIR_UNIT,SYSALLDA
         MVI   DAIR_DISP,$DAIR_DISP_SHR
         LA    R1,DAIRREQ                     ALLOCATION PARMS
         L     R15,COMM_V_OSDAIR              OSDAIR ENTRY POINT
         BALR  R14,R15                        ALLOCATE INPUT DATASET
         ITRACE ID=IN_RC,                                              +
               DATA1=(DAIR_R15,4)
         OC    DAIR_R15,DAIR_R15
         BNZ   ERR0000
         MVC   WORK_IN_DDNAME,DAIR_DD         SAVE DD NAME
MAIN0010 DS    0H
         MVC   WORK_DCB(IDCB_L),IDCB_I
         LA    R5,WORK_DCB
INPUT    USING IHADCB,R5
         MVC   INPUT.DCBDDNAM,WORK_IN_DDNAME  COPY DDNAME
         MVC   WORK_OPEN(OPEN_L),OPEN_I
         ITRACE ID=OPEN_IN,                                            +
               RDATA1=R5,                                              +
               DATA2=WORK_IN_DDNAME
         OPEN  ((R5),INPUT),                  OPEN INPUT DATASET       +
               MODE=31,                                                +
               MF=(E,WORK_OPEN)
         L     R1,OFFLDREQ_COUNT              NUMBER OFFLOADED SO FAR
         LA    R1,1(,R1)                      PLUS 1
         ST    R1,OFFLDREQ_COUNT              SAVE TOTAL
         ST    R1,SEPREQ_SEQ                  SERVES AS SEQ NBR ALSO
         LA    R1,SPF_SEPARATOR_1
         ST    R1,SEPREQ_FIRST_SEPARATOR
         LA    R1,3
         ST    R1,SEPREQ_NBR                  NBR OF SEPARATOR LINES
         ST    R7,SEPREQ_DCB                  OUTPUT DCB ADDR
         MVC   SEPREQ_MEMBER,OFFLDREQ_MEMBER  SET MEMBER NAME
         LA    R1,SEPREQ                      SEPARATOR PARMS
         L     R15,COMM_V_OSSEP               OSSEP ENTRY POINT
         BALR  R14,R15                        OUTPUT LEADING SEPARATORS
MAIN0020 DS    0H
         ITRACE ID=NOTIFY,                                             +
               DATA1=OFFLDREQ_MEMBER
         MVC   WORK_MSG(MSG01_L),MSG01
         MVC   WORK_MSG+(MSG01_MEMBER-MSG01)(L'MSG01_MEMBER),OFFLDREQ_M+
               EMBER
         LA    R2,WORK_MSG
         LA    R3,MSG01_L
         TPUT  (R2),(R3),R                    ISSUE TPUT
MAIN0030 DS    0H
         ITRACE ID=OFFLOAD
MAIN0040 DS    0H
         GET   (R5),(R6)                      READ
         L     R1,WORK_RECORDS                RECORDS READ
         LA    R1,1(,R1)                      PLUS 1
         ST    R1,WORK_RECORDS                SAVE NUMBER
         L     R1,WORK_BYTES                  BYTES FOR THIS FILE
         LA    R1,80(,R1)                     PLUS 80
         ST    R1,WORK_BYTES                  SAVE TOTAL
         PUT   (R7),(R6)                      PUT
         B     MAIN0040                       LOOP
MAIN0050 DS    0H
         ITRACE ID=EOF,                                                +
               DATA1=(WORK_RECORDS,4)
         MVC   WORK_CLOSE(CLOSE_L),CLOSE_I
         ITRACE ID=CLS_IN,                                             +
               RDATA1=R5
         CLOSE ((R5)),                        CLOSE INPUT              +
               MODE=31,                                                +
               MF=(E,WORK_CLOSE)
         FREEPOOL (R5)                        FREE BUFFERS
         MVI   DAIR_FUNC,$DAIR_FREE           SET FUNCTION
         MVC   DAIR_DD,WORK_IN_DDNAME         COPY DDNAME
         ITRACE ID=FREE_IN,                                            +
               DATA1=DAIR_DD
         LA    R1,DAIRREQ                     DAIR PARMS
         L     R15,COMM_V_OSDAIR              OSDAIR ENTRY POINT
         BALR  R14,R15                        FREE INPUT DATASET
         OC    DAIR_R15,DAIR_R15              SUCCESSFUL?
         BNZ   ERR0000                        NO
         LA    R1,SPF_SEPARATOR_4             FIRST OUTPUT SEPARATOR
         ST    R1,SEPREQ_FIRST_SEPARATOR
         MVC   SEPREQ_RECORDS,WORK_RECORDS    NBR OF RECORDS
         MVC   SEPREQ_BYTES,WORK_BYTES        NBR OF BYTES
         LA    R1,SEPREQ                      SEPARATOR PARMS
         L     R15,COMM_V_OSSEP               OSSEP ENTRY POINT
         ITRACE ID=CALL_SEP,                                           +
               RDATA1=R15,                                             +
               RDATA2=R1
         BALR  R14,R15                        OUTPUT TRAILING SEPARATOR
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
CLOSE000 DS    0H
         ICM   R7,15,OFFLDREQ_DCB             DCB ADDRESS
         BZ    EXIT0000
         MVC   WORK_CLOSE(CLOSE_L),CLOSE_I
         ITRACE ID=CLS_OUT,                                            +
               RDATA1=R7
         CLOSE ((R7)),                        CLOSE OUTPUT DCB         +
               MODE=31,                                                +
               MF=(E,WORK_CLOSE)
         FREEPOOL (R7)                        FREE BUFFERS
         ITRACE ID=FREE_DCB,                                           +
               RDATA1=R7
         FREEMAIN RU,                         FREEMAIN DCB             +
               A=(R7),                                                 +
               LV=ODCB_L
         XC    OFFLDREQ_DCB,OFFLDREQ_DCB      CLEAR DCB ADDRESS
         L     R6,OFFLDREQ_IO                 I/O AREA
         ITRACE ID=FREE_IO,                                            +
               RDATA1=R6
         FREEMAIN RU,                         FREEMAIN I/O AREA        +
               A=(R6),                                                 +
               LV=32768
         XC    OFFLDREQ_IO,OFFLDREQ_IO        CLEAR I/O AREA ADDRESS
         ITRACE ID=FREE_OUT,                                           +
               DATA1=OFFLDREQ_DDNAME
         MVI   DAIR_FUNC,$DAIR_FREE           SET COMMAND
         MVC   DAIR_DD,OFFLDREQ_DDNAME        COPY DD NAME
         LA    R1,DAIRREQ
         L     R15,COMM_V_OSDAIR
         BALR  R14,R15                        FREE OUTPUT DD
         OC    DAIR_R15,DAIR_R15              SUCCESSFUL?
         BNZ   ERR0000                        NO
         MVC   OFFLDREQ_DDNAME,COMM_BLANKS    CLEAR DDNAME
         B     EXIT0000
* ------------------------------------------------------------------- *
*        User pressed PF3 or PF4                                      *
* ------------------------------------------------------------------- *
ABORT000 DS    0H
         ITRACE ID=ABORT
         MVC   COMM_MSG_CSECT,MODID
         MVI   COMM_MSG_ID+1,1
         L     R15,COMM_V_OSMSG
         BALR  R14,R15
         B     EXIT0004
* ------------------------------------------------------------------- *
*        Output dataset already exists and overwrite was not 'yes'    *
* ------------------------------------------------------------------- *
ABORT010 DS    0H
         ITRACE ID=NO_WRITE
         MVI   DAIR_FUNC,$DAIR_FREE           DE-ALLOCATE OUTPUT FILE
         MVC   DAIR_DD,OFFLDREQ_DDNAME        COPY DDNAME
         LA    R1,DAIRREQ
         L     R15,COMM_V_OSDAIR
         BALR  R14,R15
         MVC   OFFLDREQ_DDNAME,COMM_BLANKS    CLEAR DDNAME
         MVC   COMM_MSG_CSECT,MODID           COPY CSECT NAME
         MVI   COMM_MSG_ID+1,2                SET MSG ID
         L     R15,COMM_V_OSMSG
         BALR  R14,R15                        BUILD MESSAGE
         B     EXIT0004                       EXIT WITH RC=4
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0000  DS    0H
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
         B     EXIT0004
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         SR    R2,R2
         B     EXITEXIT
EXIT0004 DS    0H
         LA    R2,4
EXITEXIT DS    0H
         ITRACE ID=EXIT,                                               +
               RDATA1=R2
         LR    R1,R13
         L     R13,4(,R13)                    RESTORE SAVE AREA ADDRESS
         FREEMAIN RU,                                                  +
               A=(R1),                                                 +
               LV=OSOFFLD_WORK_L
         L     R14,12(,R13)
         LR    R15,R2                         SET RETURN CODE
         LM    R0,R12,20(R13)                 RESTORE REGISTERS
         BR    R14                            RETURN
BOMBIT   DC    H'0'
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DSN_MVC1 MVC   DAIR_DSN(0),COMM_USER          COPY USER ID
DSN_MVC2 MVC   1(0,R3),SPF_OUTPUT_DSN         COPY DATA SET NAME
* ------------------------------------------------------------------- *
*        CONSTANTS                                                    *
* ------------------------------------------------------------------- *
IDCB_I   DCB   DSORG=PS,                                               +
               DDNAME=ANYDD,                                           +
               EODAD=MAIN0050,                                         +
               MACRF=GM
IDCB_L   EQU   *-IDCB_I
ODCB_I   DCB   DSORG=PS,                                               +
               DDNAME=ANYDD,                                           +
               MACRF=PM
ODCB_L   EQU   *-ODCB_I
OPEN_I   OPEN  (*,OUTPUT),                                             +
               MODE=31,                                                +
               MF=L
OPEN_L   EQU   *-OPEN_I
CLOSE_I  CLOSE (*),                                                    +
               MODE=31,                                                +
               MF=L
CLOSE_L  EQU   *-CLOSE_I

NEW      DC    CL3'NEW'
MOD      DC    CL3'MOD'
SHR      DC    CL3'SHR'

SYSALLDA DC    CL8'SYSALLDA'

PANEL_NAME   DC  CL8'OSOFFLD'

MSG01        DS  0C
             DC  C'OSOFFLD02I  Offloading member:'
MSG01_MEMBER Dc  CL8' '
MSG01_L      EQU *-MSG01

OFFLDEND     EQU *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
OSOFFLD_WORK    DSECT
                COPY     DXDPREF
WORK_DCB        DS       0F,(IDCB_L)X
WORK_OPEN       DS       0F,(OPEN_L)X
WORK_CLOSE      DS       0F,(CLOSE_L)X

WORK_RECORDS    DS       F
WORK_BYTES      DS       F

WORK_IN_DDNAME  DS       CL8

WORK_MSG        DS       CL80

                DAIRREQ  DSECT=NO
                SEPREQ   DSECT=NO
OSOFFLD_WORK_L  EQU      *-OSOFFLD_WORK
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                OFFLDREQ DSECT=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                IBMMAC   DCBD=PS
FORMAT_1        DSECT
                IBMMAC   VTOC=1
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                COPY TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                COPY REGEQU
                END  OSOFFLD
