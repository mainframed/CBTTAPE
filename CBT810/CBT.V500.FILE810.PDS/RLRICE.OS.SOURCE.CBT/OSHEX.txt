* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: OSHEX                                                 *
*                                                                     *
*  Build main body for HEX                                            *
*                                                                     *
* ------------------------------------------------------------------- *
OSHEX    CSECT
OSHEX    AMODE 31
OSHEX    RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING OSHEX,R15
         B     INIT0000
MODID    DC    CL8'OSHEX'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(HEXEND-OSHEX)
INIT0000 DS    0H
         USING OSCOMM,R12                     DEFINE COMMON AREA BASE
         USING DS1FMTID,SESS_DSCB             DEFINE BASE
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT
         DROP  R15
         USING OSHEX,R10                      DEFINE BASE
         L     R9,COMM_OSSPFD
         USING OSSPFD,R9
         CLI   SESS_FORMAT_FLAGS,$FORMAT_INITIALIZE
         BNE   INIT0020
         ICM   R2,15,SESS_DXD_ADDR
         BNZ   ERR0030
         LA    R0,OSHEX_DXD_L
         ST    R0,SESS_DXD_LENGTH
         GETMAIN RU,                                                   +
               LV=(0),                                                 +
               LOC=ANY
         ST    R1,SESS_DXD_ADDR
         LR    R2,R1
         LR    R0,R1
         LA    R1,OSHEX_DXD_L
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14
         ST    R13,4(,R2)                     SAVE R13
         ST    R2,8(,R13)                     CHAIN SAVE AREA
         LR    R13,R2                         COPY WORK AREA ADDRESS
         USING OSHEX_DXD,R13                  DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY1
         OI    SESS_STATUS,$STATUS_HEX
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETRIEVE
         LA    R1,DATASPACE_1                 USE DATA SPACE 1
         ST    R1,DSPCREQ_DATASPACE
         MVC   DXD_PANEL,OS0001               NON VSAM PANEL
         TM    SESS_DATA_TYPE,$DATA_TYPE_VSAM
         BNO   INIT0010
         MVC   DXD_PANEL,OS0008               VSAM PANEL
INIT0010 DS    0H
         LA    R1,DXD_PANEL
         MVI   COMM_VDATA_FUNC,$VDATA_GETMAIN
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15
         ST    R0,DXD_VDATA_LENGTH            SAVE VDATA LENGTH
         ST    R1,DXD_VDATA_ADDR              SAVE VDATA ADDR
         MVC   DXD_VWIDTH,SPF_VWIDTH          SAVE WIDTH
         MVC   DXD_VDEPTH,SPF_VDEPTH          SAVE DEPTH
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
INIT0020 DS    0H
         ICM   R15,15,SESS_DXD_ADDR
         BZ    ERR0040
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15
         ITRACE ID=ENTRY2
         XC    DXD_RC,DXD_RC
         CLI   SESS_FORMAT_FLAGS,$FORMAT_CONTROL
         BE    EXIT0000
         CLI   SESS_FORMAT_FLAGS,$FORMAT_CLEANUP
         BE    CLEAN000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0000 DS    0H
         MVC   DSPCREQ_RECORD_ADDR,SESS_IO_AREA
MAIN0050 DS    0H
         MVC   DSPCREQ_RECORD_NBR,DATASPACE_1_DISPLAY_FIRST_RECORD
* ------------------------------------------------------------------- *
*        Clear VDATA                                                  *
* ------------------------------------------------------------------- *
MAIN0070 DS    0H
         L     R0,DXD_VDATA_ADDR              BUFFER ADDRESS
         L     R1,DXD_VDATA_LENGTH            BUFFER LENGTH
         SR    R14,R14                        CLEAR R14
         SR    R15,R15                        CLEAR R15
         MVCL  R0,R14                         INITIALIZE VDATA BUFFER
* ------------------------------------------------------------------- *
*        "Retrieve" the data and build the body of the SPF panel      *
* ------------------------------------------------------------------- *
         L     R8,DXD_VDATA_ADDR              BUFFER ADDRESS
         L     R7,DXD_VDEPTH                  NUMBER OF LINES
MAIN0090 DS    0H
         CLC   DSPCREQ_RECORD_NBR,DATASPACE_1_LAST_RECORD
         BH    MAIN0140                       END OF RECORDS REACHED
         ITRACE ID=GET_RECD,                                           +
               DATA1=(DSPCREQ_RECORD_NBR,4),                           +
               DATA2=(DATASPACE_1_LAST_RECORD,4)
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE              OSDSPACE ENTRY POINT
         BALR  R14,R15                        LINK TO OSDSPACE
         CLI   DSPCREQ_RC,$DSPCREQ_NOT_STORED
         BE    MAIN0110                       NOT STORED
         CLI   DSPCREQ_RC,$DSPCREQ_OK
         BNE   ERR0010
         ITRACE ID=RECORD,                                             +
               DATA1=(DSPCREQ_RECORD_ADDR,4),                          +
               DATA2=(DSPCREQ_RECORD_LENGTH,4)
         L     R2,DATASPACE_1_DISPLAY_FIRST_COLUMN
         L     R3,DSPCREQ_RECORD_ADDR
         L     R4,DSPCREQ_RECORD_LENGTH
         CR    R4,R2                          BEYOND END OF RECORD?
         BL    MAIN0110                       YES, NO DATA TO DISPLAY
         BCTR  R2,0                           ADJUST FOR RELATIVE
         AR    R3,R2                          1ST BYTE TO DISPLAY
         SR    R4,R2                          LENGTH DISPLAYABLE
         L     R14,DXD_VWIDTH                 COLUMNS AVAILABLE
         LR    R15,R4                         COPY LENGTH OF DATA
         SLL   R15,1                          DOUBLE IT
         CR    R15,R14                        CAN IT BE DISPLAYED?
         BNH   MAIN0100                       NO
         LR    R15,R14                        LIMIT TO WIDTH
MAIN0100 DS    0H
         BCTR  R15,0                          FOR EXECUTE
         UNPK  DXD_WORK_2+00(9),00(5,R3)      UNPACK
         UNPK  DXD_WORK_2+08(9),04(5,R3)      UNPACK
         UNPK  DXD_WORK_2+16(9),08(5,R3)      UNPACK
         UNPK  DXD_WORK_2+24(9),12(5,R3)      UNPACK
         UNPK  DXD_WORK_2+32(9),16(5,R3)      UNPACK
         UNPK  DXD_WORK_2+40(9),20(5,R3)      UNPACK
         UNPK  DXD_WORK_2+48(9),24(5,R3)      UNPACK
         UNPK  DXD_WORK_2+56(9),28(5,R3)      UNPACK
         UNPK  DXD_WORK_2+64(9),32(5,R3)      UNPACK
         UNPK  DXD_WORK_2+72(9),36(5,R3)      UNPACK
         TR    DXD_WORK_2,COMM_HEXCHAR        TRANSLATE
         EX    R15,MVC_01                     COPY DATA
MAIN0110 DS    0H
         A     R8,DXD_VWIDTH                  NEXT LINE
         BCT   R7,MAIN0120                    MINUS 1 LINE
         L     R1,DSPCREQ_RECORD_NBR
         CVD   R1,COMM_DWORD
         MVC   DXD_WORK_1,EDIT_WORD_1
         ED    DXD_WORK_1,COMM_DWORD+3
         MVC   SPF_RECORD_TO,DXD_WORK_1+1
         CLC   DSPCREQ_RECORD_NBR,DATASPACE_1_LAST_RECORD
         B     MAIN0140
MAIN0120 DS    0H
         L     R1,DSPCREQ_RECORD_NBR          CURRENT RECORD NUMBER
MAIN0130 DS    0H
         A     R1,F1                          ADD 1
         ST    R1,DSPCREQ_RECORD_NBR          SAVE NEW RECORD NUMBER
         B     MAIN0090                       GET/PROCESS NEXT RECORD
MAIN0140 DS    0H
         CLC   DSPCREQ_RECORD_NBR,DATASPACE_1_LAST_RECORD
         BL    MAIN0200
* ------------------------------------------------------------------- *
*        End of data reached and there is room for the                *
*        "End of data" line                                           *
* ------------------------------------------------------------------- *
         MVC   DXD_WORK_2(EOF_01_L),EOF_01
         L     R1,DATASPACE_1_LAST_RECORD     NUMBER OF RECORDS READ
         CVD   R1,COMM_DWORD                  CONVERT TO DECIMAL
         MVC   DXD_WORK_1(L'EDIT_WORD_1),EDIT_WORD_1
         ED    DXD_WORK_1(L'EDIT_WORD_1),COMM_DWORD+3
         MVC   DXD_WORK_2+(EOF_01_RECORDS-EOF_01)(L'EOF_01_RECORDS),DXD++
               _WORK_1+1
         L     R1,DATASPACE_1_BYTES           NUMBER OF BYTES STORED
         CVD   R1,COMM_DWORD                  CONVERT TO DECIMAL
         MVC   DXD_WORK_1(L'EDIT_WORD_1),EDIT_WORD_1
         ED    DXD_WORK_1(L'EDIT_WORD_1),COMM_DWORD+3
         MVC   DXD_WORK_2+(EOF_01_BYTES-EOF_01)(L'EOF_01_BYTES),DXD_WOR+
               K_1+1
         LA    R1,EOF_01_L                    LENGTH OF TRAILER LINE
         C     R1,DXD_VWIDTH                  GREATER THAN WIDTH?
         BNH   MAIN0150                       NO
         L     R1,DXD_VWIDTH                  LIMIT TO SCREEN WIDTH
MAIN0150 DS    0H
         BCTR  R1,0                           FOR EXECUTE
         EX    R1,MVC_01                      COPY TRAILER LINE
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0200 DS    0H
         MVC   DATASPACE_1_DISPLAY_LAST_RECORD,DSPCREQ_RECORD_NBR
         MVC   SPF_COLUMN_FROM,DATASPACE_1_DISPLAY_FIRST_COLUMN
         L     R15,COMM_V_OSDSCB
         BALR  R14,R15                        BUILD DSCB DATA
         ITRACE ID=DISPLAY
         MVC   DXD_PANEL,OS0001               NON VSAM PANEL
         TM    SESS_DATA_TYPE,$DATA_TYPE_VSAM
         BNO   MAIN0210
         MVC   DXD_PANEL,OS0008               VSAM PANEL
MAIN0210 DS    0H
         MVC   SESS_DISP_PANEL,DXD_PANEL
         MVC   SESS_DISP_VDATA_ADDR,DXD_VDATA_ADDR
         MVC   SESS_DISP_VDATA_LENGTH,DXD_VDATA_LENGTH
         XC    SESS_DISP_CURSOR,SESS_DISP_CURSOR
         L     R15,COMM_V_OSDISP
         BALR  R14,R15                        LINK TO OSDISP
         ITRACE ID=DISP_RC,                                            +
               DATA1=(SESS_RC,2),                                      +
               DATA2=(SESS_STATUS,1)
         NI    SESS_SEARCH_FLAGS,255-$SESS_SEARCH_NOW
         CLC   COMM_NEW_FORMAT,COMM_BLANKS    NEW FORMAT WANTED?
         BNE   EXIT0000                       YES
         CLI   COMM_SESS_FUNC,0               SESSION FUNCTION?
         BNE   EXIT0000                       YES
         OC    SESS_RC,SESS_RC                RC=0
         BZ    MAIN0500                       YES
         ITRACE ID=ENDING
         MVI   COMM_SESS_FUNC,$SESS_REMOVE    SET FUNCTION
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
*        Check for commands                                           *
*                                                                     *
*      NOTE:  Horizontal scrolling is 50% of the screen width         *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0500 DS    0H
         ITRACE ID=CMDS,                                               +
               DATA1=SPF_ZVERB,                                        +
               DATA2=(SPF_ZSCROLLN,4)
         L     R1,SPF_ZSCROLLN
         ST    R1,SESS_VERTICAL_SCROLL
         SRL   R1,1
         ST    R1,SESS_HORIZONTAL_SCROLL
         CLI   SPF_ZVERB,C'B'                 BOTTOM?
         BE    MAIN0510                       YES
         CLI   SPF_ZVERB,C'D'                 DOWN?
         BE    MAIN0530                       YES
         CLI   SPF_ZVERB,C'L'                 LEFT?
         BE    MAIN0550                       YES
         CLI   SPF_ZVERB,C'R'                 RIGHT?
         BE    MAIN0580                       YES
         CLI   SPF_ZVERB,C'T'                 TOP?
         BE    MAIN0620                       YES
         CLI   SPF_ZVERB,C'U'                 UP?
         BE    MAIN0630                       YES
         OC    SPF_ZCMD,COMM_BLANKS           'TRANSLATE' TO UPPER CASE
         CLC   SPF_ZCMD,COMM_BLANKS           ANY COMMAND?
         BE    MAIN0070                       NO
         ITRACE ID=PRI_CMD,                                            +
               DATA1=SPF_ZCMD+00,                                      +
               DATA2=SPF_ZCMD+08
         SR    R3,R3                          CLEAR REGISTER
         ICM   R3,3,COMM_OPERANDS_NBR         NUMBER OF OPERANDS
         BZ    MAIN0200                       NO OPERANDS
         MVC   COMM_MSG_CSECT,MODID
         MVI   COMM_MSG_ID,0
         MVI   COMM_MSG_ID+1,1
         L     R15,COMM_V_OSMSG
         BALR  R14,R15
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
         B     MAIN0200
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0510 DS    0H
         ITRACE ID=BOTTOM
         MVC   SPF_ZCMD,COMM_BLANKS           CLEAR ZCMD
         L     R2,DATASPACE_1_LAST_RECORD     HIGHEST RECORD AVAILABLE
         S     R2,DXD_VDEPTH                  MINUS LINES ON DISPLAY
         A     R2,F2                          PLUS 2
         C     R2,F1                          STILL HAVE AT LEAST 1?
         BNL   MAIN0520                       YES
         L     R2,F1                          FORCE RECORD 1
MAIN0520 DS    0H
         ST    R2,DATASPACE_1_DISPLAY_FIRST_RECORD CHANGE FIRST RECORD  BR
         B     MAIN0050                       REBUILD EVERYTHING
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0530 DS    0H
         ITRACE ID=DOWN
         MVC   SPF_ZCMD,COMM_BLANKS                 CLEAR ZCMD
         CLI   SPF_ZSCROLLA,C'M'                    MAX?
         BE    MAIN0510                             YES
         L     R1,SESS_VERTICAL_SCROLL              NUMBER TO SCROLL
         L     R2,DATASPACE_1_DISPLAY_FIRST_RECORD  FIRST DISPLAYED     D
         AR    R2,R1                                PLUS SCROLL
         C     R2,DATASPACE_1_LAST_RECORD           BEYOND END OF DATA?
         BH    MAIN0540                             YES
         ST    R2,DATASPACE_1_DISPLAY_FIRST_RECORD  CHANGE FIRST RECORD BR
         B     MAIN0050                             REBUILD EVERYTHING
MAIN0540 DS    0H
         L     R2,DATASPACE_1_LAST_RECORD           HIGHEST STORED
         S     R2,DATASPACE_1_DISPLAY_FIRST_RECORD  MINUS FIRST         AYED
         C     R2,DXD_VDEPTH                        MORE THAN ONE SCRN?
         BNH   MAIN0070                             NO.. STAY PUT
         L     R2,DATASPACE_1_LAST_RECORD           HIGHEST STORED
         S     R2,DXD_VDEPTH                        MINUS 1 SCREEN
         A     R2,F2                                PLUS 2
         ST    R2,DATASPACE_1_DISPLAY_FIRST_RECORD  SET FIRST
         B     MAIN0050                             REBUILD EVERYTHING
* ------------------------------------------------------------------- *
*        SCROLL LEFT                                                  *
* ------------------------------------------------------------------- *
MAIN0550 DS    0H
         ITRACE ID=LEFT,                                               +
               DATA1=(SESS_HORIZONTAL_SCROLL,2)
         MVC   SPF_ZCMD,COMM_BLANKS           CLEAR ZCMD
         CLI   SPF_ZSCROLLA,C'M'              MAX?
         BE    MAIN0560                       YES
         L     R1,SESS_HORIZONTAL_SCROLL      COLUMNS TO SCROLL
         L     R2,DATASPACE_1_DISPLAY_FIRST_COLUMN
         SR    R2,R1                          MINUS COLUMNS TO SCROLL
         C     R2,F1                          AT LEAST 1 COLUMN LEFT?
         BNL   MAIN0570                       NO
MAIN0560 DS    0H
         L     R2,F1                          STOP AT COLUMN 1
MAIN0570 DS    0H
         ITRACE ID=LEFT_1,                                             +
               RDATA1=R2
         ST    R2,DATASPACE_1_DISPLAY_FIRST_COLUMN
         B     MAIN0050                       REBUILD EVERYTHING
* ------------------------------------------------------------------- *
*        SCROLL RIGHT                                                 *
* ------------------------------------------------------------------- *
MAIN0580 DS    0H
         ITRACE ID=RIGHT,                                              +
               DATA1=(DATASPACE_1_DISPLAY_FIRST_COLUMN,4),             +
               DATA2=(SESS_HORIZONTAL_SCROLL,4)
         MVC   SPF_ZCMD,COMM_BLANKS           CLEAR ZCMD
         CLI   SPF_ZSCROLLA,C'M'              MAX?
         BE    MAIN0610                       YES
         L     R1,SESS_HORIZONTAL_SCROLL      COLUMNS TO SCROLL
         L     R2,DATASPACE_1_DISPLAY_FIRST_COLUMN
         AR    R2,R1                          PLUS COLUMNS TO SCROLL
         TM    SESS_DATA_TYPE,$DATA_TYPE_HFS  HFS?
         BO    MAIN0600                       YES.. ALLOW ANYTHING
         OC    DS1LRECL,DS1LRECL              LRECL = ZERO?
         BZ    MAIN0590                       USE BLOCK SIZE FOR LIMIT
         CH    R2,DS1LRECL                    MORE THAN RECORD SIZE?
         BH    MAIN0050                       YES.. STAY PUT
         B     MAIN0600
MAIN0590 DS    0H
         CH    R2,DS1BLKL                     MORE THAN BLOCK SIZE?
         BH    MAIN0050                       YES.. STAY PUT
MAIN0600 DS    0H
         ITRACE ID=RIGHT_1,                                            +
               RDATA1=R2
         ST    R2,DATASPACE_1_DISPLAY_FIRST_COLUMN
         B     MAIN0050                       REBUILD EVERYTHING
MAIN0610 DS    0H
         LH    R2,DS1LRECL                    RECORD LENGTH
         S     R1,DXD_VWIDTH                  MINUS 1 SCREEN WIDTH
         A     R1,F1                          PLUS 1
         ITRACE ID=RIGHT_2,                                            +
               RDATA1=R2
         ST    R2,DATASPACE_1_DISPLAY_FIRST_COLUMN
         B     MAIN0050                       REBUILD EVERYTHING
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0620 DS    0H
         ITRACE ID=TOP
         MVC   SPF_ZCMD,COMM_BLANKS           CLEAR ZCMD
        MVC   DATASPACE_1_DISPLAY_FIRST_RECORD,DATASPACE_1_FIRST_RECORD
         B     MAIN0050                       REBUILD EVERYTHING
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0630 DS    0H
         ITRACE ID=UP
         MVC   SPF_ZCMD,COMM_BLANKS                CLEAR ZCMD
         CLI   SPF_ZSCROLLA,C'M'                   MAX?
         BE    MAIN0620                            YES
         L     R1,SESS_VERTICAL_SCROLL             RECORDS TO SCROLL
         L     R2,DATASPACE_1_DISPLAY_FIRST_RECORD FIRST DISPLAYED      D
         SR    R2,R1                               MINUS SCROLL AMOUNT
         C     R2,DATASPACE_1_FIRST_RECORD         LESS THAN FIRST?
         BNL   MAIN0640                            NO
         L     R2,DATASPACE_1_FIRST_RECORD         LIMIT TO FIRST RECD
MAIN0640 DS    0H
         ST    R2,DATASPACE_1_DISPLAY_FIRST_RECORD CHANGE FIRST RECORD  BR
         B     MAIN0050
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         DC    H'0'
ERR0020  DS    0H
         DC    H'0'
ERR0030  DS    0H
         DC    H'0'
ERR0040  DS    0H
         CLI   SESS_FORMAT_FLAGS,$FORMAT_CLEANUP
         BE    EXIT0000
         DC    H'0'
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
CLEAN000 DS    0H
         ITRACE ID=CLEAN_UP
         ICM   R1,15,DXD_VDATA_ADDR           VDATA ADDRESS
         BZ    CLEAN010                       NOT GETMAINED
         L     R0,DXD_VDATA_LENGTH            VDATA LENGTH
         MVI   COMM_VDATA_FUNC,$VDATA_FREEMAIN
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15                        FREE VDATA
CLEAN010 DS    0H
         LA    R0,OSHEX_DXD_L                 WORK AREA SIZE
         LR    R1,R13                         COPY WORK AREA ADDRESS
         L     R13,4(,R13)                    RESTORE R13
         FREEMAIN RU,                         FREE WORK AREA           +
               A=(1),                                                  +
               LV=(0)
         XC    SESS_DXD_ADDR,SESS_DXD_ADDR
         XC    SESS_DXD_LENGTH,SESS_DXD_LENGTH
         SR    R15,R15
         B     EXIT0010
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         LH    R15,DXD_RC                     RETURN CODE
         L     R13,4(,R13)                    RESTORE R13
EXIT0010 DS    0H
         L     R14,12(,R13)                   RESTORE R14
         LM    R0,R12,20(R13)                 RESTORE REGISTERS
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
MVC_01   MVC   0(0,R8),DXD_WORK_2             COPY DATA TO BUFFER
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
F1              DC    F'1'
F2              DC    F'2'

OS0001          DC    CL8'OS0001'
OS0008          DC    CL8'OS0008'

EDIT_WORD_1     DC    X'40202020202020202120'
EOF_01          DS    0C
                DC    AL1($SCREEN_ATTR_HIGH)
                DC    C'---- END OF DATA ----'
EOF_01_RECORDS  DC    CL9' '
                DC    C' RECORDS, '
EOF_01_BYTES    DC    CL9' '
                DC    C' BYTES ------------------'
EOF_01_L        EQU   *-EOF_01
                LTORG

HEXEND          EQU   *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
OSHEX_DXD       DSECT
                COPY     DXDPREF

DXD_VDATA_ADDR   DS      A
DXD_VDATA_LENGTH DS      F

DXD_VWIDTH      DS       F
DXD_VDEPTH      DS       F

DXD_RC          DS       H

DXD_PANEL       DS       CL8

DXD_WORK_1      DS       CL10
DXD_WORK_2      DS       CL100
                DSPCREQ  DSECT=NO
OSHEX_DXD       DSECT
OSHEX_DXD_L     EQU      *-OSHEX_DXD
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                COPY     OPERAND
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                OSSPFD TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                COPY ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DUMMY           DSECT
                IBMMAC VTOC=1
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                COPY TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                COPY REGEQU
                END  OSHEX
