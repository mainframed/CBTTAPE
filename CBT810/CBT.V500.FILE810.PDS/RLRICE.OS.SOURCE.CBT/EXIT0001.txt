* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: EXIT0001                                              *
*                                                                     *
*                                                                     *
*  Sample user exit for formats specifying EXIT.                      *
*                                                                     *
*  On entry R1 is the address of the USEREXIT parameter block.        *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0001 CSECT
EXIT0001 AMODE 31
EXIT0001 RMODE ANY
         USING OSCOMM,R12
         USING EXIT0001,R15
         B     INIT0000
MODID    DC    CL8'EXIT0001'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(EXITEND-EXIT0001)
INIT0000 DS    0H
         USING SESSION,R11                      DEFINE SESSION BASE
         STM   R14,R12,12(R13)                  SAVE REGS
         LR    R10,R15                          COPY ENTRY POINT
         USING EXIT0001,R10                     DEFINE BASE
         DROP  R15
         LR    R9,R1                            COPY ADDR
         USING USEREXIT,R9                      DEFINE BASE
         CLI   USEREXIT_FUNC,$USEREXIT_INITIALIZE
         BE    INIT0010
         CLI   USEREXIT_FUNC,$USEREXIT_FORMAT
         BE    FMAT0000
         CLI   USEREXIT_FUNC,$USEREXIT_CMD
         BE    CMD0000
         CLI   USEREXIT_FUNC,$USEREXIT_CLEANUP
         BE    CLEAN000
         DC    H'0'                             <-- BAD FUNCTION
* ------------------------------------------------------------------- *
*        Initialization                                               *
* ------------------------------------------------------------------- *
INIT0010 DS    0H
         ITRACE ID=INIT
         GETMAIN RU,                                                   +
               LV=WORKAREA_L,                                          +
               LOC=ANY                          <-- MAY NEED TO CHANGE
         ST    R1,USEREXIT_USERWORD             SAVE WORK AREA ADDRESS
         USING WORKAREA,R1
         MVC   DXD_CSECT,MODID
         DROP  R1
         B     EXIT0010
* ------------------------------------------------------------------- *
*        Build display                                                *
* ------------------------------------------------------------------- *
FMAT0000 DS    0H
         L     R1,USEREXIT_USERWORD             WORK AREA
         ST    R13,4(R1)                        SAVE R13
         ST    R1,8(R13)                        CHAIN SAVE AREA
         LR    R13,R1                           COPY WORK AREA ADDRESS
         USING WORKAREA,R13                     DEFINE WORK AREA BASE
         ITRACE ID=BUILD
         L     R8,USEREXIT_RECORD_ADDR          RECORD ADDRESS
SOURCE   USING TR_ENTRY,R8                      DEFINE BASE
         L     R7,USEREXIT_VDATA_ADDR           DYNAMIC STORAGE ADDR
         L     R6,USEREXIT_ROWS                 NUMBER OF ROWS
FMAT0010 DS    0H
         MVC   0(MSG_01_L,R7),MSG_01            INITIALIZE MESSAGE
         USING MSG_01,R7
         MVC   MSG_01_CSECT,SOURCE.TR_CSECT     COPY CSECT
         DROP  R7
         A     R7,USEREXIT_COLUMNS
         BCT   R6,FMAT0020
         B     EXIT0000
FMAT0020 DS    0H
         MVC   0(MSG_02_L,R7),MSG_02            INITIALIZE MESSAGE
         USING MSG_02,R7
         MVC   MSG_02_ID,SOURCE.TR_ID           COPY TRACE ID
         DROP  R7
         A     R7,USEREXIT_COLUMNS
         BCT   R6,FMAT0030
         B     EXIT0000
FMAT0030 DS    0H
         MVC   0(MSG_03_L,R7),MSG_03            INITIALIZE MESSAGE
         USING MSG_03,R7
         MVC   MSG_03_DATA1,SOURCE.TR_DATA1     COPY DATA 1
         TR    MSG_03_DATA1,PRINTABLES          TRANSLATE UNPRINTABLES
         UNPK  MSG_03_HEX1+0(9),SOURCE.TR_DATA1+0(5)
         UNPK  MSG_03_HEX1+8(9),SOURCE.TR_DATA1+4(5)
         TR    MSG_03_HEX1,COMM_HEXCHAR
         MVI   MSG_03_HEX1+16,C' '
         DROP  R7
         A     R7,USEREXIT_COLUMNS
         BCT   R6,FMAT0040
         B     EXIT0000
FMAT0040 DS    0H
         MVC   0(MSG_04_L,R7),MSG_04            INITIALIZE MESSAGE
         USING MSG_04,R7
         MVC   MSG_04_DATA2,SOURCE.TR_DATA2     COPY DATA 2
         TR    MSG_04_DATA2,PRINTABLES          TRANSLATE UNPRINTABLES
         UNPK  MSG_04_HEX2+0(9),SOURCE.TR_DATA2+0(5)
         UNPK  MSG_04_HEX2+8(9),SOURCE.TR_DATA2+4(5)
         TR    MSG_04_HEX2,COMM_HEXCHAR
         MVI   MSG_04_HEX2+16,C' '
         DROP  R7
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
CMD0000  DS    0H
         L     R1,USEREXIT_USERWORD             WORK AREA
         ST    R13,4(R1)                        SAVE R13
         ST    R1,8(R13)                        CHAIN SAVE AREA
         LR    R13,R1                           COPY WORK AREA ADDRESS
         USING WORKAREA,R13                     DEFINE WORK AREA BASE
         ITRACE ID=CMD,                                                +
               DATA1=USEREXIT_ZCMD,                                    +
               DATA2=USEREXIT_ZVERB
         CLI   USEREXIT_ZVERB,C'B'              BOTTOM?
         BE    CMD0010                          YES
         CLI   USEREXIT_ZVERB,C'D'              SCROLL DOWN?
         BE    CMD0020                          YES
         CLI   USEREXIT_ZVERB,C'T'              TOP?
         BE    CMD0030                          YES
         CLI   USEREXIT_ZVERB,C'U'              SCROLL UP?
         BE    CMD0040                          YES
         CLC   USEREXIT_ZCMD,COMM_BLANKS        ALL BLANK?
         BE    EXIT0000                         YES
         B     ERR0010
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
CMD0010  DS    0H
         ITRACE ID=BOTTOM
         MVC   USEREXIT_ZCMD,COMM_BLANKS        CLEAR CMD
         MVC   USEREXIT_RECORD_NBR,DATASPACE_1_LAST_RECORD
         B     EXIT0000
CMD0020  DS    0H
         ITRACE ID=DOWN
         MVC   USEREXIT_ZCMD,COMM_BLANKS        CLEAR COMMAND
         CLI   USEREXIT_ZSCROLLA,C'M'           MAX?
         BE    CMD0010                          YES
         L     R1,USEREXIT_RECORD_NBR           RECORD NBR
         A     R1,F1                            PLUS 1
         C     R1,USEREXIT_MAX_RECORD_NBR       BEYOND LAST?
         BH    ERR0020                          YES
         ST    R1,USEREXIT_RECORD_NBR           SET RECORD NBR
         B     EXIT0000
CMD0030  DS    0H
         ITRACE ID=TOP
         MVC   USEREXIT_ZCMD,COMM_BLANKS        CLEAR COMMAND
         MVC   USEREXIT_RECORD_NBR,F1           SET RECORD NBR
         B     EXIT0000
CMD0040  DS    0H
         ITRACE ID=UP
         MVC   USEREXIT_ZCMD,COMM_BLANKS        CLEAR COMMAND
         CLI   USEREXIT_ZSCROLLA,C'M'           MAX?
         BE    CMD0050                          YES
         L     R1,USEREXIT_RECORD_NBR           CURRENT RECORD
         BCT   R1,CMD0050                       MINUS 1
         B     ERR0030                          NO..
CMD0050  DS    0H
         ST    R1,USEREXIT_RECORD_NBR           SET RECORD NBR
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         MVC   USEREXIT_MSG_1(ERROR_MSG_01_L),ERROR_MSG_01
         B     EXIT0000
ERR0020  DS    0H
         MVC   USEREXIT_MSG_1(ERROR_MSG_02_L),ERROR_MSG_02
         B     EXIT0000
ERR0030  DS    0H
         MVC   USEREXIT_MSG_1(ERROR_MSG_03_L),ERROR_MSG_03
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
CLEAN000 DS    0H
         ITRACE ID=CLEAN_UP
         L     R1,USEREXIT_USERWORD             WORK AREA ADDRESS
         FREEMAIN RU,                           FREEMAIN IT            +
               A=(1),                                                  +
               LV=WORKAREA_L
         XC    USEREXIT_USERWORD,USEREXIT_USERWORD
         B     EXIT0010
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT0000
         L     R13,4(R13)                       RESTORE R13
EXIT0010 DS    0H
         ITRACE ID=EXIT0010
         LM    R14,R12,12(R13)                  RESTORE REGISTERS
         SR    R15,R15                          SET RC
         BR    R14                              RETURN
* ------------------------------------------------------------------- *
*                   Constants                                         *
* ------------------------------------------------------------------- *
F1              DC       F'1'

PRINTABLES      DS       0C
                DC       X'40404040404040404040404040404040' 00-0F
                DC       X'40404040404040404040404040404040' 10-1F
                DC       X'40404040404040404040404040404040' 20-2F
                DC       X'40404040404040404040404040404040' 30-3F
                DC       X'404040404040404040404A4B4C4D4E4F' 40-4F
                DC       X'504040404040404040405A5B5C5D5E5F' 50-5F
                DC       X'60614040404040404040406B6C6D6E6F' 60-6F
                DC       X'404040404040404040407A7B7C7D7E7F' 70-7F
                DC       X'40818283848586878889408B8C8D8E8F' 80-8F
                DC       X'40919293949596979899409B9C9D9E9F' 90-9F
                DC       X'4040A2A3A4A5A6A7A8A940ABACADAEAF' A0-AF
                DC       X'4040404040404040404040BBBCBDBEBF' B0-BF
                DC       X'40C1C2C3C4C5C6C7C8C9404040404040' C0-CF
                DC       X'40D1D2D3D4D5D6D7D8D9404040404040' D0-DF
                DC       X'4040E2E3E4E5E6E7E8E9404040404040' E0-EF
                DC       X'F0F1F2F3F4F5F6F7F8F9404040404040' F0-FF

MSG_01          DS       0C
                DC       AL1($SCREEN_ATTR_HIGH)
                DC       C'CSECT   '
                DC       CL3' '
                DC       AL1($SCREEN_ATTR_NORMAL)
MSG_01_CSECT    DC       CL8' '
MSG_01_L        EQU      *-MSG_01

MSG_02          DS       0C
                DC       AL1($SCREEN_ATTR_HIGH)
                DC       CL8'ID    '
                DC       CL3' '
                DC       AL1($SCREEN_ATTR_NORMAL)
MSG_02_ID       DC       CL8' '
MSG_02_L        EQU      *-MSG_02

MSG_03          DS       0C
                DC       AL1($SCREEN_ATTR_HIGH)
                DC       CL8'DATA 1'
                DC       CL3' '
                DC       AL1($SCREEN_ATTR_NORMAL)
MSG_03_DATA1    DC       CL8' '
                DC       CL4' '
MSG_03_HEX1     DC       CL16' '
                DC       C' '
MSG_03_L        EQU      *-MSG_03

MSG_04          DS       0C
                DC       AL1($SCREEN_ATTR_HIGH)
                DC       CL8'DATA 2'
                DC       CL3' '
                DC       AL1($SCREEN_ATTR_NORMAL)
MSG_04_DATA2    DC       CL8' '
                DC       CL4' '
MSG_04_HEX2     DC       CL16' '
                DC       C' '
MSG_04_L        EQU      *-MSG_04

ERROR_MSG_01    DC       C'UNRECOGNIZED COMMAND'
ERROR_MSG_01_L  EQU      *-ERROR_MSG_01

ERROR_MSG_02    DC       C'AT BOTTOM OF DATA'
ERROR_MSG_02_L  EQU      *-ERROR_MSG_02

ERROR_MSG_03    DC       C'AT TOP OF DATA'
ERROR_MSG_03_L  EQU      *-ERROR_MSG_03

                LTORG
EXITEND         EQU      *
* ------------------------------------------------------------------- *
*               Work area                                             *
* ------------------------------------------------------------------- *
WORKAREA        DSECT
                COPY     DXDPREF
                USEREXIT DSECT=NO
WORKAREA_L      EQU      *-WORKAREA
* ------------------------------------------------------------------- *
*               Common data                                           *
* ------------------------------------------------------------------- *
                COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                COPY     ATTRS
* ------------------------------------------------------------------- *
*               EQUATES                                               *
* ------------------------------------------------------------------- *
                COPY     REGEQU
                END      EXIT0001
