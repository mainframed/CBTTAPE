* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: LMODRLD                                               *
*                                                                     *
*  Process Relocation dictionary data (RLD)                           *
*                                                                     *
*  This program runs as a subroutine of OSLMOD.                       *
*  It loads RLD (relocation dictionary data) into a data space.       *
*  OSLMRLD is the complementary 'display'.                            *
*                                                                     *
*  At entry OSLMOD already has a binder "dialog" active.  The         *
*  WORKMOD's "intent" is "access".  The dialog token is in            *
*  SESS_DIALOG_TOKEN, the WORKMOD is in SESS_DIALOG_WORKMOD.          *
*  The data space for storing the data has already been created.      *
*                                                                     *
* ------------------------------------------------------------------- *
LMODRLD  CSECT
LMODRLD  AMODE 31
LMODRLD  RMODE ANY
         USING LMODRLD,R15
         USING OSCOMM,R12
         USING SESSION,R11
         B     INIT0000
MODID    DC    CL8'LMODRLD'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(RLDEND-LMODRLD)
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGISTERS
         LR    R10,R15                        COPY BASE ADDRESS
         LA    R9,2048(,R10)
         LA    R9,2048(,R9)
         DROP  R15
         USING LMODRLD,R10,R9
         L     R6,COMM_OSSPFD
         USING OSSPFD,R6
         L     R15,SESS_DXD_ADDR
         A     R15,DXD_START                  PLUS OFFSET
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15                        COPY DXD AREA ADDRESS
         USING DXDRLD,R13                     DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
         L     R5,COMM_NEW_SESSION
NEW      USING SESSION,R5
         OC    NEW.DATASPACE_1_LAST_RECORD,NEW.DATASPACE_1_LAST_RECORD
         BZ    INIT0010
         ITRACE ID=NOT_ZERO
         DC    H'0'
INIT0010 DS    0H
         XC    COMM_MSG_ID,COMM_MSG_ID
         MVC   DXD_IEWBIND(IEWBIND_L),IEWBIND_I
         MVI   DXD_FLAGS,0
         LA    R1,NEW.DATASPACE_1
         ST    R1,DSPCREQ_DATASPACE
* ------------------------------------------------------------------- *
*        Obtain and initialize RLD buffer                             *
* ------------------------------------------------------------------- *
         ITRACE ID=GET_SECT
         IEWBUFF FUNC=GETBUF,                                          +
               TYPE=RLD
         ST    R8,DXD_RLD_BUFFER
         ITRACE ID=INIT_RLD,                                           +
               RDATA1=R8,                                              +
               RDATA2=R7
         IEWBUFF FUNC=INITBUF,                                         +
               TYPE=RLD
* ------------------------------------------------------------------- *
*        Store the info for the specified section                     *
* ------------------------------------------------------------------- *
         LA    R1,LMOD_RLD_DATA
         ST    R1,DSPCREQ_RECORD_ADDR
         LA    R1,LMOD_RLD_DATA_L
         ST    R1,DSPCREQ_RECORD_LENGTH
         XC    DSPCREQ_RECORD_NBR,DSPCREQ_RECORD_NBR
         MVI   DSPCREQ_FUNC,$DSPCREQ_STORE
         XC    DXD_RLD_CURSOR,DXD_RLD_CURSOR
* ------------------------------------------------------------------- *
*                                                                     *
*     Store the RLD info                                              *
*                                                                     *
*     R8 IS THE RLD HEADER (RLDH) BASE                                *
*     R7 IS THE RLD ENTRY  (RLD_ENTRY) BASE                           *
*                                                                     *
* ------------------------------------------------------------------- *
RLD0000  DS    0H
         ITRACE ID=GETD_RLD,                                           +
               DATA1=NEW.SESS_DIALOG_SECTION_LENGTH+0,                 +
               DATA2=NEW.SESS_DIALOG_SECTION_LENGTH+8
         L     R8,DXD_RLD_BUFFER
         LA    R7,RLDH_END
         IEWBIND FUNC=GETD,                                            +
               VERSION=5,                                              +
               WORKMOD=NEW.SESS_DIALOG_WORKMOD,                        +
               CLASS=B_RLD,                                            +
               SECTION=NEW.SESS_DIALOG_SECTION_LENGTH,                 +
               AREA=IEWBRLD,                                           +
               CURSOR=DXD_RLD_CURSOR,                                  +
               COUNT=DXD_RLD_RETURNED,                                 +
               RETCODE=DXD_RETURN_CODE,                                +
               RSNCODE=DXD_REASON_CODE,                                +
               MF=(E,DXD_IEWBIND,COMPLETE)
         ITRACE ID=GETD_RC,                                            +
               DATA1=(DXD_RETURN_CODE,4),                              +
               DATA2=(DXD_REASON_CODE,4)
         CLC   DXD_RETURN_CODE,F8
         BH    ERR0010
         BE    ERR0020                        NO DATA
         CLC   DXD_RETURN_CODE,F4
         BL    RLD0020
         CLC   DXD_REASON_CODE,X_83000800
         BE    RLD0020
         CLC   DXD_REASON_CODE,X_83000801
         BE    ERR0030
RLD0020  DS    0H
         ICM   R4,15,DXD_RLD_RETURNED
         BZ    ERR0020
         OI    DXD_FLAGS,$FLAGS_DISPLAYED
RLD0030  DS    0H
         ITRACE ID=BLD_SECT,                                           +
               RDATA1=R7,                                              +
               RDATA2=R4

         MVC   LMOD_RLD_TYPE,RLD_TYPE
         MVC   LMOD_RLD_STATUS,RLD_STATUS
         MVC   LMOD_RLD_RESIDENT,COMM_BLANKS
         SR    R14,R14
         ICM   R14,3,RLD_RESIDENT_CHARS
         BZ    RLD0050
         CH    R14,=Y(L'LMOD_RLD_RESIDENT)
         BL    RLD0040
         LH    R14,=Y(L'LMOD_RLD_RESIDENT)
RLD0040  DS    0H
         BCTR  R14,0
         ICM   R15,15,RLD_RESIDENT_PTR
         EX    R14,RESIDENT_MVC
RLD0050  DS    0H
         MVC   LMOD_RLD_ADCON_LENGTH,RLD_ADCON_LENG
         MVC   LMOD_RLD_RESIDENT_CLASS,COMM_BLANKS
         ICM   R14,3,RLD_RES_CLASS_CHARS
         BZ    RLD0070
         CH    R14,=Y(L'LMOD_RLD_RESIDENT_CLASS)
         BL    RLD0060
         LH    R14,=Y(L'LMOD_RLD_RESIDENT_CLASS)
RLD0060  DS    0H
         BCTR  R14,0
         ICM   R15,15,RLD_RES_CLASS_PTR
         EX    R14,RES_CLASS_MVC
RLD0070  DS    0H
         MVC   LMOD_RLD_RESIDENT_OFFSET,RLD_RES_OFFSET
         MVC   LMOD_RLD_CLASS_OFFSET,RLD_CLASS_OFFSET
         MVC   LMOD_RLD_BIND_ATTR,RLD_BIND_ATTR
         MVC   LMOD_RLD_XATTR_CLASS,COMM_BLANKS
         ICM   R14,3,RLD_XATTR_CLASS_CHARS
         BZ    RLD0090
         CH    R14,=Y(L'LMOD_RLD_XATTR_CLASS)
         BL    RLD0080
         LH    R14,=Y(L'LMOD_RLD_XATTR_CLASS)
RLD0080  DS    0H
         BCTR  R14,0
         ICM   R15,15,RLD_XATTR_CLASS_PTR
         EX    R14,XATTR_CLASS_MVC
RLD0090  DS    0H
         MVC   LMOD_RLD_XATTR_OFFSET,RLD_XATTR_OFFSET
         MVC   LMOD_RLD_TARGET,COMM_BLANKS
         ICM   R14,3,RLD_TARGET_CHARS
         BZ    RLD0110
         CH    R14,=Y(L'LMOD_RLD_TARGET)
         BL    RLD0100
         LH    R14,=Y(L'LMOD_RLD_TARGET)
RLD0100  DS    0H
         BCTR  R14,0
         ICM   R15,15,RLD_TARGET_PTR
         EX    R14,TARGET_MVC
RLD0110  DS    0H
         MVC   LMOD_RLD_RESIDENT_PART,COMM_BLANKS
         ICM   R14,3,RLD_RES_PART_LL
         BZ    RLD0130
         CH    R14,=Y(L'LMOD_RLD_RESIDENT_PART)
         BL    RLD0120
         LH    R14,=Y(L'LMOD_RLD_RESIDENT_PART)
RLD0120  DS    0H
         BCTR  R14,0
         ICM   R15,15,RLD_RES_PART_PTR
         EX    R14,RESIDENT_PART_MVC
RLD0130  DS    0H
         L     R1,DSPCREQ_RECORD_NBR
         LA    R1,1(,R1)
         ST    R1,DSPCREQ_RECORD_NBR
         ITRACE ID=STORE,                                              +
               RDATA1=R1
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
         CLI   DSPCREQ_RC,$DSPCREQ_FULL
         BE    RLD0140
         CLI   DSPCREQ_RC,$DSPCREQ_OK
         BNE   ERR0040
         A     R7,RLDH_ENTRY_LENG
         BCT   R4,RLD0030
         CLC   DXD_REASON_CODE,X_83000800
         BNE   RLD0000
         ITRACE ID=DONE
         B     RLD0150
RLD0140  DS    0H
         ITRACE ID=FULL
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
         ICM   R1,15,DSPCREQ_RECORD_NBR
         BCTR  R1,0
         STCM  R1,15,DSPCREQ_RECORD_NBR
RLD0150  DS    0H
         MVC   NEW.DATASPACE_1_DISPLAY_FIRST_RECORD,F1
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         ITRACE ID=ERR0010
         MVI   COMM_MSG_ID+1,1
         MVC   COMM_INFO_01(20),INFO_GET_RLD
         MVC   COMM_INFO_02,DXD_RETURN_CODE
         MVC   COMM_INFO_03,DXD_REASON_CODE
         B     ERR0100
ERR0020  DS    0H
         MVC   COMM_INFO_01,NEW.SESS_DIALOG_SECTION
         TM    DXD_FLAGS,$FLAGS_DISPLAYED
         BO    ERR0030
         MVI   COMM_MSG_ID+1,2
         B     ERR0100
ERR0030  DS    0H
         MVC   COMM_INFO_01,NEW.SESS_DIALOG_SECTION
         MVI   COMM_MSG_ID+1,3
         B     ERR0100
ERR0040  DS    0H
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
         MVI   COMM_SESS_FUNC,$SESS_REMOVE
         B     EXIT0000
ERR0100  DS    0H
         ITRACE ID=CALL_MSG,                                           +
               DATA1=(COMM_MSG_ID,2)
         MVC   COMM_MSG_CSECT,MODID
         L     R15,COMM_V_OSMSG
         BALR  R14,R15
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
         MVI   COMM_SESS_FUNC,$SESS_REMOVE
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
RESIDENT_MVC          MVC  LMOD_RLD_RESIDENT(0),0(R15)
RES_CLASS_MVC         MVC  LMOD_RLD_RESIDENT_CLASS(0),0(R15)
XATTR_CLASS_MVC       MVC  LMOD_RLD_XATTR_CLASS(0),0(R15)
TARGET_MVC            MVC  LMOD_RLD_TARGET(0),0(R15)
RESIDENT_PART_MVC     MVC  LMOD_RLD_RESIDENT_PART(0),0(R15)
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT,                                               +
               DATA1=NEW.DATASPACE_1_LAST_RECORD
         L     R13,4(,R13)                    RESTORE SAVE AREA ADDRESS
         LM    R14,R12,12(R13)                RESTORE R14
         SR    R15,R15                        SET RC
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         IEWBUFF FUNC=MAPBUF,                                          +
               TYPE=RLD,                                               +
               HEADREG=R8,                                             +
               ENTRYREG=R7,                                            +
               SIZE=50,                                                +
               VERSION=5

               IEWBIND MF=(L,IEWBIND_I)
IEWBIND_L      EQU     *-IEWBIND_I
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START         DC   Q(DXDRLD)

F1                DC   F'1'
F4                DC   F'4'
F8                DC   F'8'
B_RLD             DC   H'5',C'B_RLD'
X_83000800        DC   X'83000800'
X_83000801        DC   X'83000801'
INFO_GET_RLD      DC   CL20'GET RLD info'

RLDEND            EQU  *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDRLD            DSECT
                  COPY    DXDPREF

DXD_IEWBIND       DS      0F,(IEWBIND_L)X

DXD_RETURN_CODE   DS      XL4            RETURN CODE
DXD_REASON_CODE   DS      XL4            REASON CODE

DXD_RLD_BUFFER    DS      A

DXD_RLD_RETURNED  DS      F
DXD_RLD_CURSOR    DS      F

DXD_FLAGS         DS      X
$FLAGS_DISPLAYED  EQU     X'80'          SOME DATA DISPLAYED

                  COPY    RLDDATA
                  DSPCREQ DSECT=NO

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         SESSION
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     REGEQU
         END      LMODRLD
