* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: OSCNFRM                                               *
*                                                                     *
*  Before various functions such as deleting a data set or a PDS      *
*  member, On Screen prompts the user to confirm their intention.     *
*                                                                     *
* ------------------------------------------------------------------- *
OSCNFRM  CSECT
OSCNFRM  AMODE 31
OSCNFRM  RMODE ANY
         USING OSCOMM,R12
         USING OSCNFRM,R15
         B     INIT0000
MODID    DC    CL8'OSCNFRM'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(CNFRMEND-OSCNFRM)
INIT0000 DS    0H
         USING OSCOMM,R12                     DEFINE COMMON AREA BASE
         USING SESSION,R11                    DEFINE SESSION BASE
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT
         DROP  R15
         USING OSCNFRM,R10                    DEFINE BASE
         L     R9,COMM_OSSPFD
         USING OSSPFD,R9
         L     R1,COMM_DXD
         A     R1,DXD_START
         ST    R13,4(,R1)                     SAVE R13
         ST    R1,8(,R13)                     CHAIN SAVE AREA
         LR    R13,R1                         COPY WORK AREA ADDRESS
         USING DXDCNFRM,R13                   DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
         MVC   SPF_YES_OR_NO,COMM_BLANKS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0000 DS    0H
         ITRACE ID=DISPLAY
         LA    R1,C_DISPLAY                   COMMAND
         ST    R1,DXD_SPF_1                   SET ADDRESS
         LA    R1,SESS_DISP_PANEL             PANEL NAME
         ST    R1,DXD_SPF_2                   SET ADDRESS
         OI    DXD_SPF_2,X'80'                FLAG END-OF-LIST
         LA    R1,DXD_SPF_1
         L     R15,COMM_ISPLINK
         BALR  R14,R15
         MVC   SPF_MSG_1,COMM_BLANKS
         MVC   SPF_MSG_2,COMM_BLANKS
         MVC   SPF_MSG_3,COMM_BLANKS
         MVC   SPF_MSG_4,COMM_BLANKS
         MVC   SPF_MSG_5,COMM_BLANKS
         LTR   R15,R15
         BNZ   EXIT0000
         OC    SPF_YES_OR_NO,COMM_BLANKS
         CLC   YES,SPF_YES_OR_NO
         BE    EXIT0000
         CLC   NO,SPF_YES_OR_NO
         BE    EXIT0000
         CLC   YE,SPF_YES_OR_NO
         BE    EXIT0000
         CLC   Y,SPF_YES_OR_NO
         BE    EXIT0000
         CLC   N,SPF_YES_OR_NO
         BE    EXIT0000
         CLC   ALL,SPF_YES_OR_NO
         BE    EXIT0000
         MVC   SPF_MSG_1(MSG_01_L),MSG_01
         B     MAIN0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT,                                               +
               DATA1=(SPF_YES_OR_NO,3)
         L     R13,4(,R13)                    RESTORE SAVE AREA
         L     R14,12(,R13)                   RESTORE R14
         LM    R0,R12,20(R13)                 RESTORE REGISTERS
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START      DC    Q(DXDCNFRM)
C_DISPLAY      DC    CL8'DISPLAY'
ALL            DC    C'ALL'
N              DC    C'N  '
NO             DC    C'NO '
Y              DC    C'Y  '
YE             DC    C'YE '
YES            DC    C'YES'

MSG_01         DS    0C
               DC    C'Reply y(es) or n(o)'
MSG_01_L       EQU   *-MSG_01

               LTORG

CNFRMEND       EQU    *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDCNFRM       DSECT
               COPY   DXDPREF
DXD_SPF_1      DS     A
DXD_SPF_2      DS     A
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               OSSPFD TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY   ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY   TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
               COPY   REGEQU
               END    OSCNFRM
