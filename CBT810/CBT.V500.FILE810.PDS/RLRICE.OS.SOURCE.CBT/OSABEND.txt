* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
          MACRO
          ABMSG &S,&U,&R,&M
          DC    XL2'&S'
          DC    XL2'&U'
          DC    XL2'&R'
          DC    CL80&M
          MEND

* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: OSABEND                                               *
*                                                                     *
*  Interpret ABEND codes                                              *
*                                                                     *
* ------------------------------------------------------------------- *
OSABEND  CSECT
OSABEND  AMODE 31
OSABEND  RMODE ANY
         USING OSCOMM,R12
         USING SESSION,R11
         USING OSABEND,R15
         B     INIT0000
MODID    DC    CL8'OSABEND'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(ABENDEND-OSABEND)
INIT0000 DS    0H
         USING OSCOMM,R12                        DEFINE COMMON BASE
         STM   R14,R12,12(R13)                   SAVE REGS
         LR    R10,R15                           COPY ENTRY POINT
         DROP  R15
         USING OSABEND,R10                       DEFINE BASE
         L     R15,COMM_DXD
         A     R15,DXD_START
         ST    R13,4(,R15)                       SAVE R13
         ST    R15,8(,R13)                       CHAIN SAVE AREA
         LR    R13,R15                           COPY WORK AREA ADDRESS
         USING WKABEND,R13                       DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY,                                              +
               DATA1=(SESS_SYSTEM_CODE,6)
         L     R9,COMM_OSSPFD
         USING OSSPFD,R9
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ABND0000 DS    0H
         MVC   COMM_MSG_CSECT,MODID
         OC    SESS_SYSTEM_CODE,SESS_SYSTEM_CODE  SYSTEM CODE?
         BZ    ABND0010                         MUST BE USER CODE
         MVI   COMM_MSG_ID+1,1                  SET MSG ID
         B     ABND0020
ABND0010 DS    0H
         MVI   COMM_MSG_ID+1,2                  SET MSG ID
ABND0020 DS    0H
         L     R15,COMM_V_OSMSG
         BALR  R14,R15                          BUILD MESSAGE
* ------------------------------------------------------------------- *
*        Find ABEND message                                           *
* ------------------------------------------------------------------- *
         LA    R1,ABEND_TABLE
         USING ABEND_DSECT,R1
ABND0030 DS    0H
         CLC   ABEND_CODE,SESS_SYSTEM_CODE      ABEND FOUND?
         BNE   ABND0040                         NO
         CLC   ABEND_R15,H0                     REASON CODE ALSO?
         BZ    ABND0050                         NO.. WE'RE GOOD
         CLC   ABEND_R15,SESS_ABEND_R15         REASONS MATCH?
         B     ABND0050                         YES
ABND0040 DS    0H
         CLI   ABEND_CODE,X'FF'                 END OF TABLE?
         BE    ABND0060
         LA    R1,ABEND_DSECT_L(,R1)            NEXT CODE
         B     ABND0030                         LOOP
ABND0050 DS    0H
         MVC   COMM_MSG_2,ABEND_MSG             COPY MESSAGE
         DROP  R1
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ABND0060 DS    0H
         MVI   COMM_VDATA_FUNC,$VDATA_GETMAIN   SET FUNCTION CODE
         LA    R1,PANEL_NAME
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15                          OBTAIN VDATA
         ST    R0,WK_VDATA_LENGTH               SAVE VDATA LENGTH
         ST    R1,WK_VDATA_ADDR                 SAVE VDATA ADDR
         MVI   COMM_VDATA_FUNC,$VDATA_VDEFINE   SET FUNCTION CODE
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15                          VDEFINE VDATA
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*        Display trace entries                                        *
*                                                                     *
*        The number of entries displayable can vary from one display  *
*        device type to another.  The SPF PQUERY service will have    *
*        returned the number of rows (SPF_VDEPTH) and columns         *
*        (SPF_VWIDTH) available.  The trace entries shown will be     *
*        displayed in cronological order with last entry at the       *
*        bottom of the display.  The trace table is a 'wrap around'   *
*        type.  Therefore the displayble entries may start near the   *
*        "bottom" of the table and "wrap" back to the "top".          *
*                                                                     *
*        So, the first thing to do is determine which entries can     *
*        be displayed.                                                *
*                                                                     *
* ------------------------------------------------------------------- *
         L     R8,WK_VDATA_ADDR                 VDATA STORAGE ADDR
         A     R8,WK_VDATA_LENGTH               PLUS LENGTH
         S     R8,SPF_VWIDTH                    ADDR OF LAST LINE
         L     R7,SPF_VDEPTH
         L     R2,COMM_TRACE_CURR               CURRENT TRACE
ABND0070 DS    0H
         ST    R2,0(,R8)
         C     R2,COMM_TRACE_1ST                AT FIRST ENTRY?
         BE    ABND0080                         YES
         SH    R2,=Y(TR_ENTRY_L)                MINUS 1 ENTRY
         B     ABND0090
ABND0080 DS    0H
         L     R2,COMM_TRACE_LAST              'WRAP' TO BOTTOM
ABND0090 DS    0H
         S     R8,SPF_VWIDTH                    PREVIOUS LINE
         BCT   R7,ABND0070                      LOOP
* ------------------------------------------------------------------- *
*        Display the trace entries                                    *
* ------------------------------------------------------------------- *
         L     R8,WK_VDATA_ADDR                 VDATA ADDRESS
         L     R7,SPF_VDEPTH                    LINES AVAILABLE
ABND0100 DS    0H
         L     R2,0(,R8)                        SAVED ADDRESS
ABNDTR   USING TR_ENTRY,R2
         MVC   0(MSG_01_L,R8),MSG_01            INITIALIZE MSG
         USING MSG_01,R8
         MVC   MSG_01_CSECT,ABNDTR.TR_CSECT     COPY CSECT NAME
         MVC   MSG_01_ID,ABNDTR.TR_ID           COPY TRACE ID
         MVC   MSG_01_DATA1,ABNDTR.TR_DATA1     COPY DATA 1
         MVC   MSG_01_DATA2,ABNDTR.TR_DATA2     COPY DATA 2
         UNPK  MSG_01_HEX1+0(9),ABNDTR.TR_DATA1+0(5) UNPACK DATA1
         UNPK  MSG_01_HEX1+8(9),ABNDTR.TR_DATA1+4(5) UNPACK DATA1
         TR    MSG_01_HEX1,COMM_HEXCHAR
         MVI   MSG_01_HEX1+16,C' '
         UNPK  MSG_01_HEX2+0(9),ABNDTR.TR_DATA2+0(5) UNPACK DATA2
         UNPK  MSG_01_HEX2+8(9),ABNDTR.TR_DATA2+4(5) UNPACK DATA2
         TR    MSG_01_HEX2,COMM_HEXCHAR
         MVI   MSG_01_HEX2+16,C' '
         DROP  R8,ABNDTR
         A     R8,SPF_VWIDTH                    NEXT LINE
         BCT   R7,ABND0100                      LOOP

         ITRACE ID=DISPABND
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
         LA    R1,DISPLAY
         ST    R1,WK_SPF_1
         LA    R1,PANEL_NAME
         ST    R1,WK_SPF_2
         OI    WK_SPF_2,X'80'
         LA    R1,WK_SPF_PARMS
         L     R15,COMM_ISPLINK
         BALR  R14,R15                          DISPLAY DATA

         ITRACE ID=ABEND_99
         ABEND 99,DUMP,,USER

         L     R0,WK_VDATA_LENGTH
         L     R1,WK_VDATA_ADDR
         MVI   COMM_VDATA_FUNC,$VDATA_FREEMAIN  SET FUNCTION CODE
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15                          FREE VDATA

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT0000
         L     R13,4(,R13)                      RESTORE R13
         LM    R14,R12,12(R13)                  RESTORE REGISTERS
         SR    R15,R15
         BR    R14                              RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START       DC    Q(WKABEND)

H0              DC    H'0'

DISPLAY         DC    CL8'DISPLAY'
PANEL_NAME      DC    CL8'OSABEND'

MSG_01          DS    0C
MSG_01_ATTR     DC    AL1($SCREEN_ATTR_NORMAL)
MSG_01_CSECT    DC    CL8' '
                DC    CL2' '
MSG_01_ID       DC    CL8' '
                DC    CL2' '
MSG_01_DATA1    DC    CL8' '
                DC    CL2' '
MSG_01_DATA2    DC    CL8' '
                DC    CL2' '
MSG_01_HEX1     DC    CL16' '
                DC    CL2' '
MSG_01_HEX2     DC    CL16' '
                DC    C' '
MSG_01_L        EQU   *-MSG_01

                LTORG

ABEND_TABLE     DS    0C
                ABMSG 0C1,0,0,'OPERATION EXCEPTION'
                ABMSG 0C4,0,0,'PROTECTION EXCEPTION'
                ABMSG 0C7,0,0,'DATA EXCEPTION'
                ABMSG 213,0,30,'Data set is already in use'
                DC    X'FF'

ABENDEND        EQU   *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
WKABEND            DSECT
                   COPY     DXDPREF
WK_SPF_PARMS       DS       0A
WK_SPF_1           DS       A
WK_SPF_2           DS       A
WK_VDATA_ADDR      DS       A
WK_VDATA_LENGTH    DS       F
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                   COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                   SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                   BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ABEND_DSECT        DSECT
ABEND_CODE         DS      0XL4             ABEND CODE(S)
ABEND_SYSTEM_CODE  DS       XL2             .. SYSTEM CODE
ABEND_USER_CODE    DS       XL2             .. USER CODE
ABEND_R15          DS       XL2             .. R15 AT TIME OF ABEND
ABEND_MSG          DS       CL80
ABEND_DSECT_L      EQU      *-ABEND_DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                   OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                   COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                   COPY     ATTRS
* ------------------------------------------------------------------- *
*                  EQUATES                                            *
* ------------------------------------------------------------------- *
                   COPY     REGEQU
                   END      OSABEND
