* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: OSLMOD                                                *
*                                                                     *
*  Report information about LMODs using a BINDER dialog               *
*                                                                     *
*                                                                     *
*                                                                     *
*  As of 05/01/08 the LMOD info is a "SESSION" rather than a          *
*  sub-function of the load module directory display.                 *
*                                                                     *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
OSLMOD   CSECT
OSLMOD   AMODE 31
OSLMOD   RMODE ANY
         USING OSLMOD,R15
         USING OSCOMM,R12
         USING SESSION,R11
NEW      USING SESSION,R2
         B     INIT0000
MODID    DC    CL8'OSLMOD'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(OSLMODEND-OSLMOD)
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGISTERS
         LR    R10,R15                        COPY BASE ADDRESS
         LA    R9,2048(,R10)
         LA    R9,2048(,R9)
         DROP  R15
         USING OSLMOD,R10,R9
         L     R5,COMM_OSSPFD
         USING OSSPFD,R5
         CLI   SESS_FORMAT_FLAGS,$FORMAT_INITIALIZE
         BNE   LMOD0000                       NO
         L     R0,DXDSIZE
         ST    R0,SESS_DXD_LENGTH             SET WORK AREA LENGTH
         GETMAIN RU,                          SOME FUNCTIONS STILL     +
               LV=(0),                          REQUIRE 24-BIT         +
               LOC=BELOW                        STORAGE
         ST    R1,SESS_DXD_ADDR               SAVE WORK AREA ADDRESS
         LR    R0,R1                          COPY ADDRESS
         L     R1,DXDSIZE                     WORK AREA SIZE
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14                         CLEAR WORK AREA
         L     R1,SESS_DXD_ADDR               WORK AREA BASE
         A     R1,DXD_START                   PLUS OFFSET
         ST    R13,4(,R1)
         ST    R1,8(,R13)
         LR    R13,R1                         COPY DXD AREA ADDRESS
         USING DXDLMOD,R13                    DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         MVC   COMM_MSG_CSECT,MODID
         XC    COMM_MSG_ID,COMM_MSG_ID
         ITRACE ID=ENTRY1,                                             +
               RDATA1=R13
         MVC   DXD_IEWBIND(IEWBIND_L),IEWBIND_I
         XC    DXD_NAMES_RETURNED,DXD_NAMES_RETURNED
         XC    DXD_TOTAL_NAMES,DXD_TOTAL_NAMES
         XC    DXD_NAME_CURSOR,DXD_NAME_CURSOR
         LA    R1,PANEL_NAME
         MVI   COMM_VDATA_FUNC,$VDATA_GETMAIN
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15
         ST    R0,DXD_VDATA_LENGTH
         ST    R1,DXD_VDATA_ADDR
         ST    R1,DXD_CURRENT_VDATA
         MVC   DXD_VDEPTH,SPF_VDEPTH
         MVC   DXD_VWIDTH,SPF_VWIDTH
         LA    R1,DATASPACE_1
         ST    R1,DSPCREQ_DATASPACE
         MVI   DAIR_FUNC,$DAIR_ALLOC
         MVI   DAIR_OPTS,0
         MVC   DAIR_DD,COMM_BLANKS
         MVC   DAIR_DSN,SESS_DSN
         MVC   DAIR_MEMBER,COMM_BLANKS
         MVC   DAIR_VOLSER,COMM_BLANKS
         MVI   DAIR_DISP,$DAIR_DISP_SHR
         LA    R1,DAIRREQ
         L     R15,COMM_V_OSDAIR
         BALR  R14,R15                     ALLOCATE THE LIBRARY
         OC    DAIR_R15,DAIR_R15           SUCCESSFUL?
         BNZ   ERR0070
         MVC   SESS_DD,DAIR_DD             SAVE DDNAME
* ------------------------------------------------------------------- *
*        Start the dialog                                             *
* ------------------------------------------------------------------- *
         NI    DXD_FLAGS,255-$FLAG_ENDD_BYPASS
         ITRACE ID=STARTD
         IEWBIND FUNC=STARTD,                                          +
               VERSION=5,                                              +
               DIALOG=SESS_DIALOG_TOKEN,                               +
               RETCODE=DXD_RETURN_CODE,                                +
               RSNCODE=DXD_REASON_CODE,                                +
               MF=(E,DXD_IEWBIND,COMPLETE)
         ITRACE ID=STARTDRC,                                           +
               DATA1=(DXD_RETURN_CODE,4),                              +
               DATA2=(DXD_REASON_CODE,4)
         OC    DXD_RETURN_CODE,DXD_RETURN_CODE
         BNZ   ERR0010
* ------------------------------------------------------------------- *
*        Create a workmod                                             *
* ------------------------------------------------------------------- *
         ITRACE ID=CREATEW
         IEWBIND FUNC=CREATEW,                                         +
               VERSION=5,                                              +
               DIALOG=SESS_DIALOG_TOKEN,                               +
               WORKMOD=SESS_DIALOG_WORKMOD,                            +
               INTENT=ACCESS,                                          +
               RETCODE=DXD_RETURN_CODE,                                +
               RSNCODE=DXD_REASON_CODE,                                +
               MF=(E,DXD_IEWBIND,COMPLETE)
         ITRACE ID=CREATWRC,                                           +
               DATA1=(DXD_RETURN_CODE,4),                              +
               DATA2=(DXD_REASON_CODE,4)
         OC    DXD_RETURN_CODE,DXD_RETURN_CODE
         BNZ   ERR0020
* ------------------------------------------------------------------- *
*        Set list options to ALL                                      *
* ------------------------------------------------------------------- *
         ITRACE ID=SET_ALL
         IEWBIND FUNC=SETO,                                            +
               VERSION=5,                                              +
               WORKMOD=SESS_DIALOG_WORKMOD,                            +
               OPTION=LIST,                                            +
               OPTVAL=ALL,                                             +
               RETCODE=DXD_RETURN_CODE,                                +
               RSNCODE=DXD_REASON_CODE,                                +
               MF=(E,DXD_IEWBIND,COMPLETE)
         ITRACE ID=SET_RC,                                             +
               DATA1=(DXD_RETURN_CODE,4),                              +
               DATA2=(DXD_REASON_CODE,4)
         OC    DXD_RETURN_CODE,DXD_RETURN_CODE
         BNZ   ERR0030
* ------------------------------------------------------------------- *
*        INCLUDE the member                                           *
* ------------------------------------------------------------------- *
         ITRACE ID=INCLUDE,                                            +
               DATA1=SESS_MEMBER,                                      +
               DATA2=SESS_DD
         MVC   DXD_DDNAME_LENGTH,H8
         MVC   DXD_DDNAME,SESS_DD
         MVC   DXD_MEMBER_NAME_LENGTH,H8
         MVC   DXD_MEMBER_NAME,SESS_MEMBER
         IEWBIND FUNC=INCLUDE,                                         +
               VERSION=5,                                              +
               WORKMOD=SESS_DIALOG_WORKMOD,                            +
               INTYPE=NAME,                                            +
               DDNAME=DXD_DDNAME_LENGTH,                               +
               MEMBER=DXD_MEMBER_NAME_LENGTH,                          +
               RETCODE=DXD_RETURN_CODE,                                +
               RSNCODE=DXD_REASON_CODE,                                +
               MF=(E,DXD_IEWBIND,COMPLETE)
         ITRACE ID=INCL_RC,                                            +
               DATA1=(DXD_RETURN_CODE,4),                              +
               DATA2=(DXD_REASON_CODE,4)
         OC    DXD_REASON_CODE,DXD_REASON_CODE
         BNZ   ERR0040
* ------------------------------------------------------------------- *
*        Obtain and initialize NAME buffer                            *
* ------------------------------------------------------------------- *
         ITRACE ID=NAMEBUFF
         IEWBUFF FUNC=GETBUF,                                          +
               TYPE=NAME
         ST    R8,DXD_NAME_BUFFER
         ITRACE ID=INIT_NAME,                                          +
               RDATA1=R8,                                              +
               RDATA2=R7
         IEWBUFF FUNC=INITBUF,                                         +
               TYPE=NAME
* ------------------------------------------------------------------- *
*        Create or clear the data space                               *
* ------------------------------------------------------------------- *
INIT0040 DS    0H
         OC    DATASPACE_1_ALET,DATASPACE_1_ALET
         BNZ   INIT0050
         ITRACE ID=CREATE
         MVI   DSPCREQ_FUNC,$DSPCREQ_CREATE
         B     INIT0060
INIT0050 DS    0H
         ITRACE ID=CLEAR
         MVI   DSPCREQ_FUNC,$DSPCREQ_CLEAR
INIT0060 DS    0H
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
         ITRACE ID=DSPC_RC1,                                           +
               DATA1=(DSPCREQ_RC,1)
         CLI   DSPCREQ_RC,$DSPCREQ_OK
         BNE   ERR0110
         MVC   DATASPACE_1_FIRST_RECORD,F1
* ------------------------------------------------------------------- *
*        Store the section names in the data space                    *
* ------------------------------------------------------------------- *
INIT0070 DS    0H
         LA    R1,DXD_NAME_DATA
         ST    R1,DSPCREQ_RECORD_ADDR
         LA    R1,DXD_NAME_DATA_L
         ST    R1,DSPCREQ_RECORD_LENGTH
         MVC   DSPCREQ_RECORD_NBR,F1
         XC    DXD_NAME_CURSOR,DXD_NAME_CURSOR
* ------------------------------------------------------------------- *
*                                                                     *
*     R8 is the NAMES HEADER (BNLH) base                              *
*     R7 is the NAMES ENTRY  (BNL_ENTRY) base                         *
*                                                                     *
* ------------------------------------------------------------------- *
INIT0080 DS    0H
         ITRACE ID=GET_NAME,                                           +
               DATA1=DXD_NAME_CURSOR
         L     R8,DXD_NAME_BUFFER
         LA    R7,BNLH_END
         IEWBIND FUNC=GETN,                                            +
               VERSION=5,                                              +
               WORKMOD=SESS_DIALOG_WORKMOD,                            +
               AREA=IEWBBNL,                                           +
               CURSOR=DXD_NAME_CURSOR,                                 +
               COUNT=DXD_NAMES_RETURNED,                               +
               TCOUNT=DXD_TOTAL_NAMES,                                 +
               NTYPE=S,                                                +
               RETCODE=DXD_RETURN_CODE,                                +
               RSNCODE=DXD_REASON_CODE,                                +
               MF=(E,DXD_IEWBIND,COMPLETE)
         ITRACE ID=NAMES_RC,                                           +
               DATA1=(DXD_RETURN_CODE,4),                              +
               DATA2=(DXD_REASON_CODE,4)
         C     R15,F4
         BH    ERR0010
         BL    INIT0090
         CLC   DXD_REASON_CODE,X_83000800
         BNE   ERR0010
         CLC   DXD_REASON_CODE,X_83000801
         BE    ERR0030
INIT0090 DS    0H
         ICM   R4,15,DXD_NAMES_RETURNED
         ITRACE ID=NAMES,                                              +
               RDATA1=R6,                                              +
               DATA2=DXD_TOTAL_NAMES
         LTR   R4,R4
         BZ    ERR0020
INIT0100 DS    0H
         ITRACE ID=STORE,                                              +
               RDATA1=R7
         SR    R14,R14
         MVC   DXD_NAME,COMM_BLANKS
         ICM   R14,3,BNL_NAME_CHARS
         BZ    INIT0120
         CH    R14,H8
         BL    INIT0110
         LH    R14,H8
INIT0110 DS    0H
         BCTR  R14,0
         ICM   R15,15,BNL_NAME_PTR
         EX    R14,NAME_MVC
         B     INIT0130
INIT0120 DS    0H
         MVC   DXD_NAME(L'NO_NAME),NO_NAME
INIT0130 DS    0H
         MVC   DXD_NAME_CLASS_LENGTH,BNL_CLS_LENGTH
         MVC   DXD_NAME_BIND_FLAGS,BNL_BIND_FLAGS
         MVC   DXD_NAME_LOAD_FLAGS,BNL_LOAD_FLAGS
         MVC   DXD_NAME_ELEMENTS,BNL_ELEM_COUNT
         MVC   DXD_NAME_SEGMENT_ID,BNL_SEGM_ID
         MVC   DXD_NAME_SEGMENT_OFFSET,BNL_SEGM_OFF
         MVI   DSPCREQ_FUNC,$DSPCREQ_STORE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
         L     R1,DSPCREQ_RECORD_NBR
         C     R1,DXD_TOTAL_NAMES
         BE    INIT0140
         A     R1,F1
         ST    R1,DSPCREQ_RECORD_NBR
         A     R7,BNLH_ENTRY_LENG
         BCT   R4,INIT0100
         B     INIT0080
NAME_MVC MVC   DXD_NAME(0),0(R15)
* ------------------------------------------------------------------- *
*                                                                     *
*        All section names stored in data space                       *
*                                                                     *
* ------------------------------------------------------------------- *
INIT0140 DS    0H
         MVC   DATASPACE_1_DISPLAY_FIRST_RECORD,F1
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
*        Display the names as a scrollable selection list             *
*                                                                     *
* ------------------------------------------------------------------- *
LMOD0000 DS    0H
         L     R1,SESS_DXD_ADDR
         ST    R13,4(,R1)
         ST    R1,8(,R13)
         LR    R13,R1
         ITRACE ID=ENTRY2,                                             +
               RDATA1=R13
         L     R5,COMM_OSSPFD
         CLI   SESS_FORMAT_FLAGS,$FORMAT_CONTROL
         BE    EXIT0000
         CLI   SESS_FORMAT_FLAGS,$FORMAT_CLEANUP
         BE    CLEAN000
         TM    DXD_FLAGS,$INIT                   EVER INITIALIZED?
         BNO   LMOD0010                          NO
         OI    DXD_FLAGS,$INIT                   SET FLAG
         CLI   COMM_SESS_FUNC,$SESS_SWITCH       SESSION SWITCH?
         BNE   LMOD0010                          NO
         L     R4,DXD_CURRENT_VDATA              RESTORE POSITION
         L     R3,DXD_LINES_REMAINING            LINES REMAINING
         B     DISP0020
LMOD0010 DS    0H
         MVC   DSPCREQ_RECORD_NBR,DATASPACE_1_DISPLAY_FIRST_RECORD
         ITRACE ID=DISPLIST,                                           +
               DATA1=(DSPCREQ_RECORD_NBR,4)
         ITRACE ID=INITVDATA
         L     R0,DXD_VDATA_ADDR                 INITIALIZE VDATA
         L     R1,DXD_VDATA_LENGTH
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14

         L     R4,DXD_VDATA_ADDR
         L     R3,DXD_VDEPTH
         ITRACE ID=BUILD,                                              +
               RDATA1=R4,                                              +
               RDATA2=R3
         L     R8,DXD_NAME_BUFFER
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETRIEVE
         LA    R0,DATASPACE_1
         ST    R0,DSPCREQ_DATASPACE
LMOD0020 DS    0H
         ITRACE ID=RETRIEVE,                                           +
               DATA1=(DSPCREQ_RECORD_NBR,4)
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
         ITRACE ID=DSPC_RC2,                                           +
               DATA1=(DSPCREQ_RC,1)
         CLI   DSPCREQ_RC,0
         BNE   ERR0110
         MVC   0(NAME_MSG_L,R4),NAME_MSG
M        USING NAME_MSG,R4
         MVC   M.NAME_MSG_NAME,DXD_NAME
         UNPK  M.NAME_MSG_BIND_FLAGS(3),DXD_NAME_BIND_FLAGS(2)
         TR    M.NAME_MSG_BIND_FLAGS,COMM_HEXCHAR
         MVI   M.NAME_MSG_BIND_FLAGS+2,C' '
         UNPK  M.NAME_MSG_LOAD_FLAGS(3),DXD_NAME_LOAD_FLAGS(2)
         TR    M.NAME_MSG_LOAD_FLAGS,COMM_HEXCHAR
         MVI   M.NAME_MSG_LOAD_FLAGS+2,C' '
         L     R1,DXD_NAME_ELEMENTS
         CVD   R1,COMM_DWORD
         ED    M.NAME_MSG_ELEMENTS,COMM_DWORD+5
         LH    R1,DXD_NAME_SEGMENT_ID
         CVD   R1,COMM_DWORD
         ED    M.NAME_MSG_SEGMENT_ID,COMM_DWORD+5
         LH    R1,DXD_NAME_SEGMENT_OFFSET
         CVD   R1,COMM_DWORD
         ED    M.NAME_MSG_SEGMENT_OFFSET,COMM_DWORD+5
         A     R4,DXD_VWIDTH
         BCT   R3,LMOD0030
         B     DISP0000
LMOD0030 DS    0H
         L     R1,DSPCREQ_RECORD_NBR
         C     R1,DXD_TOTAL_NAMES
         BE    DISP0000
         LA    R1,1(,R1)
         ST    R1,DSPCREQ_RECORD_NBR
         B     LMOD0020
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DISP0000 DS    0H
         ITRACE ID=DISPLAY
         L     R15,COMM_V_OSDSCB              REBUILD DSCB INFO
         BALR  R14,R15
         MVC   SESS_DISP_PANEL,PANEL_NAME
         MVC   SESS_DISP_VDATA_ADDR,DXD_VDATA_ADDR
         MVC   SESS_DISP_VDATA_LENGTH,DXD_VDATA_LENGTH
         XC    SESS_DISP_CURSOR,SESS_DISP_CURSOR
         L     R15,COMM_V_OSDISP
         BALR  R14,R15
         ITRACE ID=DISP_RC,                                            +
               DATA1=(SESS_RC,2),                                      +
               DATA2=(SESS_STATUS,1)
         CLC   COMM_NEW_FORMAT,COMM_BLANKS    NEW FORMAT?
         BNE   EXIT0000                       YES
         CLI   COMM_SESS_FUNC,0               SESSION FUNCTION?
         BNE   EXIT0000                       YES
         OC    SESS_RC,SESS_RC                RC = 0?
         BZ    DISP0010                       YES
         ITRACE ID=ENDING
         MVI   COMM_SESS_FUNC,$SESS_REMOVE    SET FUNCTION
         B     EXIT0000                       EXIT
DISP0010 DS    0H
         CLI   SPF_ZVERB,C'B'                 BOTTOM?
         BE    DISP0040                       YES
         CLI   SPF_ZVERB,C'D'                 DOWN?
         BE    DISP0040                       YES
         CLI   SPF_ZVERB,C'T'                 TOP?
         BE    DISP0040                       YES
         CLI   SPF_ZVERB,C'U'                 UP?
         BE    DISP0080                       YES
* ------------------------------------------------------------------- *
*        No scroll command, check for line command(s)                 *
* ------------------------------------------------------------------- *
         L     R4,DXD_VDATA_ADDR
         L     R3,DXD_VDEPTH
DISP0020 DS    0H
         OI    M.NAME_MSG_SELECT,C' '
         MVI   M.NAME_MSG_ATTR,$SCREEN_ATTR_NORMAL
         ITRACE ID=LINE_CMD,                                           +
               DATA1=(M.NAME_MSG_SELECT,1),                            +
               RDATA2=R4
         CLI   M.NAME_MSG_SELECT,C' '              ANY COMMAND?
         BE    DISP0030                            NO
         LA    R1,M.NAME_MSG_SELECT
         ST    R1,SESS_COMMAND_ADDRESS
         MVC   SESS_COMMAND_LENGTH,H1              SET LENGTH
         MVI   SESS_COMMAND_FLAGS,$SESS_COMMAND_SEARCH
         LA    R0,LINE_TABLE
         ST    R0,COMM_MAIN_COMMAND_TABLE
         XC    COMM_SUB_COMMAND_TABLE,COMM_SUB_COMMAND_TABLE
         L     R15,COMM_V_OSCMD
         BALR  R14,R15                             SEARCH FOR COMMAND
         TM    SESS_COMMAND_FLAGS,$SESS_COMMAND_FOUND
         BOR   R15                                 BRANCH TO COMMAND
         ITRACE ID=BAD_CMD,                                            +
               DATA1=(M.NAME_MSG_SELECT,1),                            +
               RDATA2=R4
         MVC   COMM_MSG_CSECT,MODID
         MVI   COMM_MSG_ID+1,3
         L     R15,COMM_V_OSMSG
         BALR  R14,R15
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
         MVI   M.NAME_MSG_ATTR,$SCREEN_ATTR_HIGH
         B     DISP0000                            DISPLAY WITH MESSAGE
DISP0030 DS    0H
         A     R4,DXD_VWIDTH
         BCT   R3,DISP0020
         B     DISP0000
* ------------------------------------------------------------------- *
*        SCROLL DOWN                                                  *
* ------------------------------------------------------------------- *
DISP0040 DS    0H
         ITRACE ID=DOWN
         MVC   SPF_ZCMD,COMM_BLANKS                CLEAR ZCMD
         CLI   SPF_ZSCROLLA,C'M'                   MAX?
         BE    DISP0060                            YES
         L     R1,SPF_ZSCROLLN                     NUMBER TO SCROLL
         A     R1,DATASPACE_1_DISPLAY_FIRST_RECORD FIRST DISPLAYED      D
         C     R1,DXD_TOTAL_NAMES                  TOO MANY?
         BH    DISP0050                            YES
         ST    R1,DATASPACE_1_DISPLAY_FIRST_RECORD CHANGE 1ST RCD NBR
         B     LMOD0000                            REBUILD EVERYTHING
DISP0050 DS    0H
         L     R1,DXD_TOTAL_NAMES                  HIGHEST RECORD
         S     R1,DATASPACE_1_DISPLAY_FIRST_RECORD MINUS 1ST DISPLAYED
         C     R1,DXD_VDEPTH                       MORE THAN 1?
         BNH   DISP0000                            NO.. STAY PUT
         L     R1,DXD_TOTAL_NAMES                  HIGHEST RECORD
         S     R1,DXD_VDEPTH                       MINUS 1 SCREEN
         A     R1,F2                               PLUS 2
         ST    R1,DATASPACE_1_DISPLAY_FIRST_RECORD SET 1ST TO DISPLAY
         B     LMOD0000                            REBUILD EVERYTHING
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DISP0060 DS    0H
         ITRACE ID=BOTTOM
         MVC   SPF_ZCMD,COMM_BLANKS                CLEAR ZCMD
         L     R2,DXD_TOTAL_NAMES                  HIGHEST RECORD
         S     R2,DXD_VDEPTH                       MINUS DEPTH
         A     R2,F2                               PLUS 2
         C     R2,F1                               STILL HAVE 1?
         BNL   DISP0070                            YES
         L     R2,F1                               FORCE RECORD 1
DISP0070 DS    0H
         ST    R2,DATASPACE_1_DISPLAY_FIRST_RECORD CHANGE 1ST RCD NBR
         B     LMOD0000                            REBUILD EVERYTHING
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DISP0080 DS    0H
         ITRACE ID=UP
         MVC   SPF_ZCMD,COMM_BLANKS                CLEAR ZCMD
         CLI   SPF_ZSCROLLA,C'M'                   MAX?
         BE    DISP0090                            YES
         L     R1,DATASPACE_1_DISPLAY_FIRST_RECORD 1ST RCD DISPLAYED
         S     R1,SPF_ZSCROLLN                     MINUS SCROLL
         BM    DISP0090                            NEGATIVE, FORCE TOP
         ST    R1,DATASPACE_1_DISPLAY_FIRST_RECORD CHANGE 1ST RCD NBR
         B     LMOD0000                            REBUILD EVERYTHING
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DISP0090 DS    0H
         ITRACE ID=TOP
         MVC   DATASPACE_1_DISPLAY_FIRST_RECORD,F1 FORCE RECORD 1
         B     LMOD0000                            REBUILD EVERYTHING
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DISP0100 DS    0H
         ITRACE ID=ESD
         ST    R3,DXD_LINES_REMAINING
         ST    R4,DXD_CURRENT_VDATA
         BAL   R3,SESS0000                         CREATE NEW SESSION
         MVC   NEW.SESS_FORMAT,ESD                 SET FORMAT NAME
         MVC   NEW.SESS_DEFAULT_FORMAT,ESD         SET FORMAT NAME
         MVC   NEW.SESS_FUNCTION_MODULE,OSLMESD    SET FUNCTION NAME
         L     R15,V_LMODESD                       ESD INFO OBTAINER
         B     DISP0200
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DISP0110 DS    0H
         ITRACE ID=ESDD
         ST    R3,DXD_LINES_REMAINING
         ST    R4,DXD_CURRENT_VDATA
         BAL   R3,SESS0000                         CREATE NEW SESSION
         MVC   NEW.SESS_FORMAT,ESDD                SET FORMAT NAME
         MVC   NEW.SESS_DEFAULT_FORMAT,ESDD        SET FORMAT NAME
         MVC   NEW.SESS_FUNCTION_MODULE,OSLMESDD   SET FUNCTION NAME
         L     R15,V_LMODESD                       ESD INFO OBTAINER
         B     DISP0200
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DISP0120 DS    0H
         ITRACE ID=IDRB
         ST    R3,DXD_LINES_REMAINING
         ST    R4,DXD_CURRENT_VDATA
         BAL   R3,SESS0000                         CREATE NEW SESSION
         MVC   NEW.SESS_FORMAT,IDRB                SET FORMAT NAME
         MVC   NEW.SESS_DEFAULT_FORMAT,IDRB        SET FORMAT NAME
         MVC   NEW.SESS_FUNCTION_MODULE,OSLMIDRB   SET FUNCTION NAME
         L     R15,V_LMODIDRB
         B     DISP0200
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DISP0130 DS    0H
         ITRACE ID=DMAP
         ST    R3,DXD_LINES_REMAINING
         ST    R4,DXD_CURRENT_VDATA
         BAL   R3,SESS0000                         CREATE NEW SESSION
         MVC   NEW.SESS_FORMAT,DMAP                SET FORMAT NAME
         MVC   NEW.SESS_DEFAULT_FORMAT,DMAP        SET FORMAT NAME
         MVC   NEW.SESS_FUNCTION_MODULE,OSLMLMAP   SET FUNCTION NAME
         L     R15,V_LMODMAP
         B     DISP0200
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DISP0140 DS    0H
         ITRACE ID=LMAP
         ST    R3,DXD_LINES_REMAINING
         ST    R4,DXD_CURRENT_VDATA
         BAL   R3,SESS0000                         CREATE NEW SESSION
         MVC   NEW.SESS_FORMAT,LMAP                SET FORMAT NAME
         MVC   NEW.SESS_DEFAULT_FORMAT,LMAP        SET FORMAT NAME
         MVC   NEW.SESS_FUNCTION_MODULE,OSLMLMAP   SET FUNCTION NAME
         L     R15,V_LMODMAP
         B     DISP0200
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DISP0150 DS    0H
         ITRACE ID=IDRL
         ST    R3,DXD_LINES_REMAINING
         ST    R4,DXD_CURRENT_VDATA
         BAL   R3,SESS0000                         CREATE NEW SESSION
         MVC   NEW.SESS_FORMAT,IDRL                SET FORMAT NAME
         MVC   NEW.SESS_DEFAULT_FORMAT,IDRL        SET FORMAT NAME
         MVC   NEW.SESS_FUNCTION_MODULE,OSLMIDRL   SET FUNCTION NAME
         L     R15,V_LMODIDRL
         B     DISP0200
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DISP0160 DS    0H
         ITRACE ID=RLD
         ST    R3,DXD_LINES_REMAINING
         ST    R4,DXD_CURRENT_VDATA
         BAL   R3,SESS0000                         CREATE NEW SESSION
         MVC   NEW.SESS_FORMAT,RLD                 SET FORMAT NAME
         MVC   NEW.SESS_DEFAULT_FORMAT,RLD         SET FORMAT NAME
         MVC   NEW.SESS_FUNCTION_MODULE,OSLMRLD    SET FUNCTION NAME
         L     R15,V_LMODRLD
         B     DISP0200
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DISP0170 DS    0H
         ITRACE ID=IDRZ
         ST    R3,DXD_LINES_REMAINING
         ST    R4,DXD_CURRENT_VDATA
         BAL   R3,SESS0000                         CREATE NEW SESSION
         MVC   NEW.SESS_FORMAT,IDRZ                SET FORMAT NAME
         MVC   NEW.SESS_DEFAULT_FORMAT,IDRZ        SET FORMAT NAME
         MVC   NEW.SESS_FUNCTION_MODULE,OSLMIDRZ   SET FUNCTION NAME
         L     R15,V_LMODIDRZ
         B     DISP0200
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DISP0180 DS    0H
         ITRACE ID=LINECMDS
         MVI   M.NAME_MSG_SELECT,C' '           RESET SELECTION
         MVI   SESS_COMMAND_FLAGS,$SESS_COMMAND_DISPLAY
         LA    R0,LINE_TABLE
         ST    R0,COMM_MAIN_COMMAND_TABLE
         XC    COMM_SUB_COMMAND_TABLE,COMM_SUB_COMMAND_TABLE
         L     R15,COMM_V_OSCMD
         BALR  R14,R15
         B     DISP0030
* ------------------------------------------------------------------- *
*                                                                     *
*        Get the requested info for the new function                  *
*                                                                     *
* ------------------------------------------------------------------- *
DISP0200 DS    0H
         ITRACE ID=GET_INFO,                                           +
               RDATA1=R15,                                             +
               DATA2=M.NAME_MSG_NAME
         ST    R2,COMM_NEW_SESSION              SET NEW SESSION ADDR
         MVC   NEW.SESS_DIALOG_SECTION,M.NAME_MSG_NAME   SET CSECT NAME
         MVC   SPF_CSECT,M.NAME_MSG_NAME        SET CSECT NAME
         MVI   M.NAME_MSG_SELECT,C' '           RESET SELECTION
         BALR  R14,R15                          OBTAIN INFO
* ------------------------------------------------------------------- *
*                                                                     *
*        Request session switch to new session                        *
*                                                                     *
* ------------------------------------------------------------------- *
         MVI   COMM_SESS_FUNC,$SESS_SWITCH      INDICATE SWITCH
         B     EXIT0000                         EXIT
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*        Create a new "SESSION"                                       *
*          GETMAIN the SESSION block                                  *
*          Chain the new SESSION into the existing SESSIONs           *
*          GETMAIN the I/O area                                       *
*          Create the data space                                      *
*                                                                     *
*       R3 is return address                                          *
*                                                                     *
*       At exit R2 is address of new SESSION block                    *
*                                                                     *
* ------------------------------------------------------------------- *
SESS0000 DS    0H
         ITRACE ID=BLD_NEW
         GETMAIN RU,                                                   +
               LV=SESSION_L,                                           +
               LOC=ANY
         LR    R2,R1                            SAVE ADDRESS
         ST    R2,COMM_NEW_SESSION
         LR    R0,R2
         LA    R1,SESSION_L
         LR    R14,R11                          CURRENT SESSION ADDR
         LR    R15,R1                           COPY LENGTH
         MVCL  R0,R14                           COPY SESSION BLOCK
         L     R1,SESS_NEXT                     NEXT ON CHAIN
NEXT     USING SESSION,R1
         ST    R1,NEW.SESS_NEXT                 SET NEXT IN NEW BLOCK
         ST    R11,NEW.SESS_PREV                SET PREV IN NEW BLOCK
         ST    R2,SESS_NEXT                     SET NEXT IN CURRENT
         LTR   R1,R1                            'NEXT' BLOCK?
         BZ    SESS0010                         NO
         ST    R2,NEXT.SESS_PREV                SET PREV IN NEXT BLOCK
         DROP  NEXT
SESS0010 DS    0H
         MVI   NEW.SESS_FORMAT_FLAGS,0
         MVC   NEW.DATASPACE_1_FIRST_RECORD,F1
         MVC   NEW.DATASPACE_1_DISPLAY_FIRST_RECORD,F1
         MVC   NEW.DATASPACE_1_DISPLAY_FIRST_COLUMN,F1
         XC    NEW.SESS_DXD_ADDR,NEW.SESS_DXD_ADDR
         XC    NEW.SESS_DXD_LENGTH,NEW.SESS_DXD_LENGTH
         XC    NEW.SESS_LAST_FOUND,NEW.SESS_LAST_FOUND
         XC    NEW.SESS_SEARCH_ARG_LENGTH,NEW.SESS_SEARCH_ARG_LENGTH
         MVC   NEW.SESS_SEARCH_ARG,COMM_BLANKS
         MVC   NEW.SESS_DSN,SESS_DSN
         MVC   NEW.SESS_PATH,SESS_PATH
         XC    NEW.SESS_DISP_VDATA_ADDR,NEW.SESS_DISP_VDATA_ADDR
         XC    NEW.SESS_DISP_VDATA_LENGTH,NEW.SESS_DISP_VDATA_ADDR
         XC    NEW.SESS_DCB_ADDR,NEW.SESS_DCB_ADDR
         XC    NEW.SESS_DCB_LENGTH,NEW.SESS_DCB_LENGTH
         MVI   NEW.SESS_KEYWORD_FLAGS,$KEYWORD_DSN+$KEYWORD_MEMBER+$KEY+
               WORD_VOL
         L     R0,COMM_IO_SIZE                  I/O AREA SIZE
         GETMAIN RU,                                                   +
               LV=(0),                                                 +
               LOC=BELOW
         ST    R1,NEW.SESS_IO_AREA              SAVE ADDRESS
         ITRACE ID=IO_AREA,                                            +
               RDATA1=R1
         ITRACE ID=CRE_DSPC                     CREATING A DATA SPACE
         LA    R0,NEW.DATASPACE_1
         ST    R0,DSPCREQ_DATASPACE
         MVI   DSPCREQ_FUNC,$DSPCREQ_CREATE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                          CREATE DATASPACE
         ITRACE ID=DSPC_RC3,                                           +
               DATA1=(DSPCREQ_RC,1)
         CLI   DSPCREQ_RC,$DSPCREQ_OK
         BNE   ERR0080
         LA    R0,DATASPACE_1                   OUR DATASPACE INFO
         ST    R0,DSPCREQ_DATASPACE             RESET ADDRESS
         DROP  NEW
         BR    R3


* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         ITRACE ID=ERR0010
         MVC   COMM_INFO_01(20),INFO_START_DIALOG
         B     ERR0100
ERR0020  DS    0H
         ITRACE ID=ERR0020
         MVC   COMM_INFO_01(20),INFO_CREATE_WORKMOD
         B     ERR0100
ERR0030  DS    0H
         ITRACE ID=ERR0030
         MVC   COMM_INFO_01(20),INFO_SET_OPTIONS
         B     ERR0100
ERR0040  DS    0H
         ITRACE ID=ERR0040
         MVC   COMM_INFO_01(20),INFO_INCLUDE
         B     ERR0100
ERR0050  DS    0H
         ITRACE ID=ERR0050
         OI    DXD_FLAGS,$FLAG_ENDD_BYPASS
         MVC   COMM_INFO_01(20),INFO_DELETEW
         B     ERR0100
ERR0060  DS    0H
         ITRACE ID=ERR0060
         OI    DXD_FLAGS,$FLAG_ENDD_BYPASS
         MVC   COMM_INFO_01(20),INFO_ENDD
         B     ERR0100
ERR0070  DS    0H
         ITRACE ID=DAIR_ERR,                                           +
               DATA1=(DAIR_R15,6)
         MVI   COMM_MSG_ID+1,4
         MVC   COMM_INFO_01(6),DAIR_R15
         B     MSG0000
ERR0080  DS    0H
         ITRACE ID=DSPC_ERR
         MVI   COMM_MSG_ID+1,5
         B     MSG0000
ERR0100  DS    0H
         MVI   COMM_MSG_ID+1,1
         MVC   COMM_INFO_02(4),DXD_RETURN_CODE
         MVC   COMM_INFO_03(4),DXD_REASON_CODE
         B     MSG0000
ERR0110  DS    0H
         ITRACE ID=DSPCFAIL
         B     DISP0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MSG0000  DS    0H
         ITRACE ID=CALL_MSG,                                           +
               DATA1=COMM_MSG_ID
         L     R15,COMM_V_OSMSG
         BALR  R14,R15
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
         MVI   COMM_SESS_FUNC,$SESS_REMOVE
         B     DISP0000
* ------------------------------------------------------------------- *
*                                                                     *
*        Clean up                                                     *
*                                                                     *
*         end the dialog                                              *
*         free the DD                                                 *
*         FREEMAIN VDATA                                              *
*         FREEMAIN work area                                          *
*                                                                     *
* ------------------------------------------------------------------- *
CLEAN000 DS    0H
         ITRACE ID=CLEANUP
         TM    DXD_FLAGS,$FLAG_ENDD_BYPASS
         BO    CLEAN010
* ------------------------------------------------------------------- *
*        End the dialog                                               *
* ------------------------------------------------------------------- *
         ITRACE ID=DELETEW,                                            +
               DATA1=SESS_DIALOG_WORKMOD
         IEWBIND FUNC=DELETEW,                                         +
               VERSION=5,                                              +
               WORKMOD=SESS_DIALOG_WORKMOD,                            +
               PROTECT=NO,                                             +
               RETCODE=DXD_RETURN_CODE,                                +
               RSNCODE=DXD_REASON_CODE,                                +
               MF=(E,DXD_IEWBIND,COMPLETE)
         ITRACE ID=DELW_RC,                                            +
               DATA1=(DXD_RETURN_CODE,4),                              +
               DATA2=(DXD_REASON_CODE,4)
         OC    DXD_RETURN_CODE,DXD_RETURN_CODE
         BNZ   ERR0050
         IEWBIND FUNC=ENDD,                                            +
               VERSION=5,                                              +
               DIALOG=SESS_DIALOG_TOKEN,                               +
               RETCODE=DXD_RETURN_CODE,                                +
               RSNCODE=DXD_REASON_CODE,                                +
               MF=(E,DXD_IEWBIND,COMPLETE)
         ITRACE ID=ENDD_RC,                                            +
               DATA1=(DXD_RETURN_CODE,4),                              +
               DATA2=(DXD_REASON_CODE,4)
         OC    DXD_RETURN_CODE,DXD_RETURN_CODE
         BNZ   ERR0060
CLEAN010 DS    0H
         L     R0,DXD_VDATA_LENGTH             VDATA LENGTH
         L     R1,DXD_VDATA_ADDR               VDATA ADDRESS
         MVI   COMM_VDATA_FUNC,$VDATA_FREEMAIN SET FUNCTION
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15                         FREE VDATA
         L     R0,SESS_DXD_LENGTH
         L     R1,SESS_DXD_ADDR
         XC    SESS_DXD_ADDR,SESS_DXD_ADDR
         XC    SESS_DXD_LENGTH,SESS_DXD_LENGTH
         XC    COMM_MAIN_COMMAND_TABLE,COMM_MAIN_COMMAND_TABLE
         XC    COMM_SUB_COMMAND_TABLE,COMM_SUB_COMMAND_TABLE
         LR    R1,R13
         L     R13,4(,R13)
         FREEMAIN RU,                                                  +
               A=(1),                                                  +
               LV=(0)
         LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(,R13)
         LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDSIZE    CXD
DXD_START  DC   Q(DXDLMOD)

V_LMODESD  DC   V(LMODESD)
V_LMODIDRB DC   V(LMODIDRB)
V_LMODIDRL DC   V(LMODIDRL)
V_LMODIDRZ DC   V(LMODIDRZ)
V_LMODMAP  DC   V(LMODMAP)
V_LMODRLD  DC   V(LMODRLD)

         IEWBUFF FUNC=MAPBUF,                                          +
               TYPE=NAME,                                              +
               HEADREG=R8,                                             +
               ENTRYREG=R7,                                            +
               SIZE=50,                                                +
               VERSION=5

          IEWBIND MF=(L,IEWBIND_I)
IEWBIND_L EQU     *-IEWBIND_I

F1                   DC   F'1'
F2                   DC   F'2'
F4                   DC   F'4'
H1                   DC   H'1'
H8                   DC   H'8'
LIST                 DC   H'4',C'LIST'
ALL                  DC   H'3',C'ALL'
ESD                  DC   CL8'ESD'
ESDD                 DC   CL8'ESDD'
IDRB                 DC   CL8'IDRB'
IDRL                 DC   CL8'IDRL'
IDRZ                 DC   CL8'IDRZ'
DMAP                 DC   CL8'DMAP'
LMAP                 DC   CL8'LMAP'
OSLMESD              DC   CL8'OSLMESD'
OSLMESDD             DC   CL8'OSLMESDD'
OSLMIDRB             DC   CL8'OSLMIDRB'
OSLMIDRL             DC   CL8'OSLMIDRL'
OSLMIDRZ             DC   CL8'OSLMIDRZ'
OSLMDMAP             DC   CL8'OSLMDMAP'
OSLMLMAP             DC   CL8'OSLMLMAP'
OSLMRLD              DC   CL8'OSLMRLD'
PANEL_NAME           DC   CL8'OSLMOD'
RLD                  DC   CL8'RLD'
X_83000800           DC   X'83000800'
X_83000801           DC   X'83000801'

INFO_START_DIALOG    DC   CL20'Start dialog'
INFO_CREATE_WORKMOD  DC   CL20'Create workmod'
INFO_SET_OPTIONS     DC   CL20'Set options (all)'
INFO_INCLUDE         DC   CL20'Include'
INFO_DELETEW         DC   CL20'Deletew'
INFO_ENDD            DC   CL20'End dialog'

NO_NAME              DC   C'<NO NAME>'

                     LTORG

NAME_MSG             DS   0C
                     DC   AL1($SCREEN_ATTR_INPUT)
NAME_MSG_SELECT      DC   C' '
                     DC   AL1($SCREEN_ATTR_HIGH)
NAME_MSG_NAME        DC   CL8' '
NAME_MSG_ATTR        DC   AL1($SCREEN_ATTR_NORMAL)
                     DC   CL5' '
NAME_MSG_BIND_FLAGS  DC   CL2' '
                     DC   CL12' '
NAME_MSG_LOAD_FLAGS  DC   CL2' '
                     DC   CL8' '
NAME_MSG_ELEMENTS    DC   X'402020202120'
                     DC   CL7' '
NAME_MSG_SEGMENT_ID  DC   X'402020202120'
                     DC   CL7' '
NAME_MSG_SEGMENT_OFFSET   DC   X'402020202120'
NAME_MSG_L           EQU  *-NAME_MSG

LINE_TABLE           DS   0C
                     CMD  B,DISP0120,'Display BINDER data'
                     CMD  D,DISP0130,'Display map detail'
                     CMD  E,DISP0100,'Display ESD data'
                     CMD  X,DISP0110,'Display ESD data detail'
                     CMD  M,DISP0140,'Display map data'
                     CMD  L,DISP0150,'Display language data'
                     CMD  R,DISP0160,'Display RLD data'
                     CMD  Z,DISP0170,'Display ZAP data'
                     CMD  ?,DISP0180,'Display available line commands'
                     DC   X'FF'

OSLMODEND            EQU  *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDLMOD                 DSECT
                        COPY  DXDPREF
DXD_IEWBIND             DS    0F,(IEWBIND_L)X

DXD_NAMES_RETURNED      DS    F
DXD_TOTAL_NAMES         DS    F
DXD_NAME_CURSOR         DS    F
DXD_NAME_BUFFER         DS    A

DXD_VDATA_ADDR          DS    A
DXD_VDATA_LENGTH        DS    F
DXD_VWIDTH              DS    F
DXD_VDEPTH              DS    F

DXD_CURRENT_VDATA       DS    A
DXD_LINES_REMAINING     DS    F

DXD_RETURN_CODE         DS    XL4            RETURN CODE
DXD_REASON_CODE         DS    XL4            REASON CODE

DXD_DDNAME_LENGTH       DS    H
DXD_DDNAME              DS    CL44

DXD_MEMBER_NAME_LENGTH  DS    H
DXD_MEMBER_NAME         DS    CL8

DXD_FLAGS               DS    X
$INIT                   EQU   X'80'
$FLAG_ENDD_BYPASS       EQU   X'40'

DXD_NAME_DATA           DS    0C
DXD_NAME                DS    CL8
DXD_NAME_CLASS_LENGTH   DS    F
DXD_NAME_ELEMENTS       DS    F
DXD_NAME_SEGMENT_OFFSET DS    F
DXD_NAME_SEGMENT_ID     DS    H
DXD_NAME_BIND_FLAGS     DS    X
DXD_NAME_LOAD_FLAGS     DS    X
DXD_NAME_DATA_L         EQU   *-DXD_NAME_DATA

                        DAIRREQ  DSECT=NO
                        DSPCREQ  DSECT=NO
DXDLMOD_L               EQU   *-DXDLMOD
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         IBMMAC DCBD=PO,                                               +
               VTOC=1
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY TRENTRY
* ------------------------------------------------------------------- *
*              EQUATES                                                *
* ------------------------------------------------------------------- *
         COPY REGEQU
         END  OSLMOD
