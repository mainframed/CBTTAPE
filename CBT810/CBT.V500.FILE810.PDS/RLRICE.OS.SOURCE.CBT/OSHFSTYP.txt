* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: OSHFSTYP                                              *
*                                                                     *
*  Determine HFS file type                                            *
*                                                                     *
*                                                                     *
*  The area returned by the STAT call is mapped by BPXYSTAT.          *
*  The MODE (file type and permissions) is mapped by BPXYMODE.        *
*  File types are defined in BPXYFTYP.                                *
*                                                                     *
*  To indicate a new (empty) file, the file type is set to X'FF'.     *
*  As of 11/28/06 X'FF' is not a valid file type and should not       *
*  conflict with a valid type.                                        *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
OSHFSTYP CSECT
OSHFSTYP AMODE 31
OSHFSTYP RMODE 24
         USING OSCOMM,R12
         USING SESSION,R11
         USING OSHFSTYP,R15
         B     INIT0000
MODID    DC    CL8'OSHFSTYP'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(OSHFSTYPEND-OSHFSTYP)
INIT0000 DS    0H
         STM   R14,R12,12(R13)                  SAVE REGS
         LR    R10,R15                          COPY ENTRY POINT
         DROP  R15
         USING OSHFSTYP,R10                     DEFINE BASE
         GETMAIN RU,                                                   +
               LV=OSHFSTYP_WORK_L,                                     +
               LOC=ANY
         ST    R13,4(,R1)                       SAVE R13
         ST    R1,8(,R13)                       CHAIN SAVE AREA
         LR    R13,R1                           COPY WORK AREA ADDRESS
         USING OSHFSTYP_WORK,R13                DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID                  COPY CSECT NAME
         ITRACE ID=ENTRY
         MVC   COMM_MSG_CSECT,MODID
* ------------------------------------------------------------------- *
*        Determine type for path user entered                         *
* ------------------------------------------------------------------- *
         ITRACE ID=STAT,                                               +
               DATA1=(SESS_PATH_LENGTH,4),                             +
               DATA2=SESS_PATH
         CALL  BPX1LST,                         CALL STAT              +
               (SESS_PATH_LENGTH,                                      +
               SESS_PATH,                                              +
               STAT_LENGTH,                                            +
               SESS_STAT,                                              +
               WORK_RETURN_VALUE,                                      +
               WORK_RETURN_CODE,                                       +
               WORK_REASON_CODE),                                      +
               VL,                                                     +
               MF=(E,WORK_HFS_PARMLIST)
         ITRACE ID=STAT_RC,                                            +
               DATA1=(WORK_RETURN_VALUE,4),                            +
               DATA2=(WORK_RETURN_CODE,4)
         TM    WORK_RETURN_VALUE,X'80'          SUCCESSFUL?
         BO    MAIN0100                         NO
PATH     USING STAT,SESS_STAT
         ITRACE ID=PATHTYPE,                                           +
               DATA1=(PATH.ST_MODE,4)
         USING S_MODE,PATH.ST_MODE
         CLI   S_TYPE,FT_SYMLINK                SYMBOLIC LINK?
         BNE   MAIN0010                         NO
* ------------------------------------------------------------------- *
*        Path is a symbolic link, read real path name                 *
* ------------------------------------------------------------------- *
         LA    R1,SESS_REAL_PATH                BUFFER FOR REAL PATH
         ST    R1,WORK_REAL_PATH_ADDR
         ITRACE ID=READLINK,                                           +
               DATA1=(SESS_PATH_LENGTH,4),                             +
               DATA2=SESS_PATH
         CALL  BPX1RDL,                         READ LINK              +
               (SESS_PATH_LENGTH,                                      +
               SESS_PATH,                                              +
               F256,                                                   +
               WORK_REAL_PATH_ADDR,                                    +
               WORK_RETURN_VALUE,                                      +
               WORK_RETURN_CODE,                                       +
               WORK_REASON_CODE),                                      +
               VL,                                                     +
               MF=(E,WORK_HFS_PARMLIST)
         ITRACE ID=LINK_RC,                                            +
               DATA1=(WORK_RETURN_VALUE,4),                            +
               DATA2=(WORK_RETURN_CODE,4)
         TM    WORK_RETURN_VALUE,X'80'          SUCCESSFUL?
         BO    ERR0020                          NO
         MVC   SESS_REAL_PATH_LENGTH,WORK_RETURN_VALUE
* ------------------------------------------------------------------- *
*        Determine file type for real path                            *
* ------------------------------------------------------------------- *
         ITRACE ID=REALSTAT,                                           +
               DATA1=(SESS_REAL_PATH_LENGTH,4),                        +
               DATA2=SESS_REAL_PATH+00
         ITRACE ID=+,                                                  +
               DATA1=SESS_REAL_PATH+08,                                +
               DATA2=SESS_REAL_PATH+16
         CALL  BPX1LST,                         CALL STAT              +
               (SESS_REAL_PATH_LENGTH,                                 +
               SESS_REAL_PATH,                                         +
               STAT_LENGTH,                                            +
               SESS_REAL_STAT,                                         +
               WORK_RETURN_VALUE,                                      +
               WORK_RETURN_CODE,                                       +
               WORK_REASON_CODE),                                      +
               VL,                                                     +
               MF=(E,WORK_HFS_PARMLIST)
         ITRACE ID=STAT_RC,                                            +
               DATA1=(WORK_RETURN_VALUE,4),                            +
               DATA2=(WORK_RETURN_CODE,8)
         TM    WORK_RETURN_VALUE,X'80'          SUCCESSFUL?
         BO    ERR0010                          NO
REALPATH USING STAT,SESS_REAL_STAT
         ITRACE ID=REALTYPE,                                           +
               DATA1=(REALPATH.ST_MODE,4)
MAIN0010 DS    0H
         SR    R3,R3                            SET RC TO ZERO
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0100 DS    0H
         CLC   WORK_RETURN_CODE,ENOENT_CODE     NO SUCH FILE?
         BNE   ERR0010                          NO.. OTHER ERROR
         TM    SESS_EDIT_FLAGS,$SESS_EDIT       EDIT SESSION?
         BNO   ERR0010                          NO.. DOES NOT EXIST
         ITRACE ID=SET_XFF
         MVI   PATH.ST_MODE,X'FF'               SET FILE TYPE TO X'FF'
         SR    R3,R3                            SET RC TO ZERO
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         LA    R3,1
         B     ERR1000
ERR0020  DS    0H
         LA    R3,2
ERR1000  DS    0H
         MVC   COMM_INFO_01,WORK_RETURN_VALUE
         MVC   COMM_INFO_02,WORK_RETURN_CODE
         MVC   COMM_INFO_03,WORK_REASON_CODE
ERR1010  DS    0H
         STCM  R3,3,COMM_MSG_ID
         L     R15,COMM_V_OSMSG
         BALR  R14,R15
         LA    R3,8
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         LR    R1,R13                           COPY WORK AREA ADDRESS
         L     R13,4(,R13)                      RESTORE R13
         FREEMAIN RU,                                                  +
               A=(1),                                                  +
               LV=OSHFSTYP_WORK_L
         L     R14,12(,R13)
         LR    R15,R3                           SET RC
         LM    R0,R12,20(R13)                   RESTORE REGISTERS
         BR    R14                              RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
STAT_LENGTH          DC      A(STAT#LENGTH)
F256                 DC      F'256'
*
*
*
*   FOLLOW CODE IS IN SYS1.MACLIB(BPXYERNO)
ENOENT_CODE          DC      A(ENOENT)          NO SUCH FILE
                     LTORG

OSHFSTYPEND          EQU     *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
OSHFSTYP_WORK        DSECT
                     COPY    DXDPREF
WORK_REAL_PATH_ADDR  DS      A
WORK_RETURN_VALUE    DS      F
WORK_RETURN_CODE     DS      F
WORK_REASON_CODE     DS      F
WORK_HFS_PARMLIST    DS      10F
OSHFSTYP_WORK_L      EQU     *-OSHFSTYP_WORK
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         BPXYSTAT DSECT=YES,LIST=YES
         BPXYMODE DSECT=YES,LIST=YES
         BPXYFTYP LIST=YES
         BPXYERNO LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY     REGEQU
         END      OSHFSTYP
