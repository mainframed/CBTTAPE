* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: OSTRACE                                               *
*                                                                     *
*  Display internal trace table                                       *
*                                                                     *
* ------------------------------------------------------------------- *
OSTRACE  CSECT
OSTRACE  AMODE 31
OSTRACE  RMODE ANY
         USING OSTRACE,R15
         USING OSCOMM,R12
         USING SESSION,R11
         B     INIT0000
MODID    DC    CL8'OSTRACE'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(TRACEEND-OSTRACE)
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGISTERS
         LR    R10,R15                        COPY BASE ADDRESS
         DROP  R15
         USING OSTRACE,R10
         L     R15,COMM_DXD                   DXD AREA BASE ADDRESS
         A     R15,DXD_START                  PLUS OFFSET
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15                        COPY DXD AREA ADDRESS
         USING DXDTRACE,R13                   DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
         L     R9,COMM_OSSPFD
         USING OSSPFD,R9

         MVC   SPF_ZCMD,COMM_BLANKS
         LA    R1,PANEL_NAME                  COPY PANEL NAME
         MVI   COMM_VDATA_FUNC,$VDATA_GETMAIN SET FUNCTION
         L     R15,COMM_V_OSVDATA
         ST    R0,DXD_VDATA_LENGTH
         ST    R1,DXD_VDATA_ADDR
         BALR  R14,R15                        OBTAIN VDATA STORAGE
         MVI   COMM_VDATA_FUNC,$VDATA_VDEFINE SET FUNCTION
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15                        VDEFINE VDATA
         ITRACE ID=VDATA,                                              +
               DATA1=(DXD_VDATA_ADDR,4),                               +
               DATA2=(DXD_VDATA_LENGTH,4)
* ------------------------------------------------------------------- *
*                                                                     *
*        From here on limit the use of ITRACE.                        *
*        Otherwise the trace table will be flooded with               *
*        trace points from this program.                              *
*                                                                     *
* ------------------------------------------------------------------- *
         L     R0,DXD_VDATA_ADDR
         L     R1,DXD_VDATA_LENGTH
         SR    R14,R14
         SR    R15,R15
         ICM   R15,8,COMM_BLANKS
         MVCL  R0,R14

         MVC   DXD_FIRST,COMM_TRACE_1ST
MAIN0000 DS    0H
         L     R8,DXD_VDATA_ADDR
         L     R7,SPF_VDEPTH                    NUMBER OF LINES
         L     R6,DXD_FIRST                     FIRST TO DISPLAY
Z        USING TRENTRY,R6                       DEFINE BASE
MAIN0010 DS    0H
         MVC   0(MSG_01_L,R8),MSG_01            INITIALIZE
         USING MSG_01,R8                        DEFINE BASE
         MVC   MSG_01_CSECT,Z.TRECSECT          COPY CSECT
         MVC   MSG_01_ID,Z.TREID                COPY TRACE ID
         MVC   MSG_01_DATA1,Z.TREDATA1          COPY DATA ITEM
         TR    MSG_01_DATA1,PRINTABLES          TRANSLATE UNPRINTABLES
         UNPK  MSG_01_HEX1+0(9),Z.TREDATA1+0(5) UNPACK DATA 1
         UNPK  MSG_01_HEX1+8(9),Z.TREDATA1+4(5)
         TR    MSG_01_HEX1,COMM_HEXCHAR         TRANSLATE
         MVI   MSG_01_HEX1+16,C' '
         MVC   MSG_01_DATA2,Z.TREDATA2          COPY DATA ITEM
         TR    MSG_01_DATA2,PRINTABLES          TRANSLATE UNPRINTABLES
         UNPK  MSG_01_HEX2+0(9),Z.TREDATA2+0(5) UNPACK DATA 2
         UNPK  MSG_01_HEX2+8(9),Z.TREDATA2+4(5)
         TR    MSG_01_HEX2,COMM_HEXCHAR         TRANSLATE
         MVI   MSG_01_HEX2+16,C' '
         C     R6,COMM_TRACE_CURR               CURRENT ENTRY?
         BNE   MAIN0020                         NO
         MVC   MSG_01_CURR,=C'-->'              FLAG AS CURRENT
MAIN0020 DS    0H
         DROP  R8
         ST    R6,DXD_LAST                      LAST DISPLAYED
         A     R8,SPF_VWIDTH                    NEXT LINE
         BCT   R7,MAIN0030
         B     DISP0000                         SCREEN FULL
MAIN0030 DS    0H
         C     R6,COMM_TRACE_LAST               LAST TRACE ENTRY?
         BE    DISP0000                         YES
         LA    R6,TRENTRYL(,R6)                 NEXT TRACE ENTRY
         B     MAIN0010                         LOOP
* ------------------------------------------------------------------- *
*        DISPLAY                                                      *
* ------------------------------------------------------------------- *
DISP0000 DS    0H
         LA    R1,DISPLAY                       ISPF FUNCTION
         ST    R1,DXD_SPF_1                     SET ADDRESS
         LA    R1,PANEL_NAME                    PANEL NAME
         ST    R1,DXD_SPF_2                     SET ADDRESS
         OI    DXD_SPF_2,X'80'                  FLAG END OF LIST
         LA    R1,DXD_SPF_PARMS                 SPF PARM LIST
         L     R15,COMM_ISPLINK                 SPF ENTERFACE ADDRESS
         BALR  R14,R15                          DISPLAY
         CH    R15,H8                           SERIOUS ERROR?
         BH    ERR0010                          YES
         LTR   R15,R15                          RC ZERO?
         BNZ   EXIT0000                         NO.. EXIT
DISP0010 DS    0H
         LA    R1,VGET                          COMMAND
         ST    R1,DXD_SPF_1                     SET ADDRESS
         LA    R1,NAME_LIST                     SYSTEM VARIABLES
         ST    R1,DXD_SPF_2                     SET ADDRESS
         OI    DXD_SPF_2,X'80'                  FLAG END-OF-LIST
         LA    R1,DXD_SPF_PARMS                 SPF PARM LIST
         L     R15,COMM_ISPLINK                 SPF ENTRY POINT
         BALR  R14,R15                          VGET SPF VARIABLE(S)
         LTR   R15,R15                          SUCCESSFUL?
         BNZ   DISP0020                         NO
         TM    DXD_FLAGS,$VPUT                  VPUT ALREADY ISSUED?
         BO    DISP0030                         YES
* ------------------------------------------------------------------- *
*       UPDATE SPF VARIABLES                                          *
* ------------------------------------------------------------------- *
DISP0020 DS    0H
         OI    DXD_FLAGS,$VPUT                  SET FLAG
         LA    R1,VPUT                          COMMAND
         ST    R1,DXD_SPF_1                     SET ADDRESS
         LA    R1,NAME_LIST
         ST    R1,DXD_SPF_2
         OI    DXD_SPF_2,X'80'                  SET END-OF-LIST
         LA    R1,DXD_SPF_PARMS                 PARM LIST ADDRESS
         L     R15,COMM_ISPLINK                 SPF ENTRY POINT
         BALR  R14,R15                          UPDATE SPF VARIABLE(S)
         B     DISP0010                         DO VGET
DISP0030 DS    0H
         CLC   CURRENT(4),SPF_ZCMD              GO TO CURRENT ENTRY?
         BE    DISP0080                         YES
         CLI   SPF_ZVERB,C'B'                   BOTTOM?
         BE    DISP0040                         YES
         CLI   SPF_ZVERB,C'D'                   DOWN?
         BE    DISP0050                         YES
         CLI   SPF_ZVERB,C'T'                   TOP?
         BE    DISP0060                         YES
         CLI   SPF_ZVERB,C'U'                   UP?
         BE    DISP0070                         YES
         B     DISP0000
* ------------------------------------------------------------------- *
*        Bottom                                                       *
* ------------------------------------------------------------------- *
DISP0040 DS    0H
         L     R1,COMM_TRACE_LAST               LAST TRACE ENTRY
         L     R0,SPF_VDEPTH                    SCREEN DEPTH
         BCTR  R0,0                             MINUS 1
         MH    R0,=Y(TRENTRYL)                  MULTIPLY BY LENGTH
         SR    R1,R0                            SUBTRACT FROM LAST
         ST    R1,DXD_FIRST                     SET FIRST TO DISPLAY
         B     MAIN0000                         BUILD DISPLAY
* ------------------------------------------------------------------- *
*        Down                                                         *
* ------------------------------------------------------------------- *
DISP0050 DS    0H
         CLI   SPF_ZSCROLLA,C'M'                MAX?
         BE    DISP0040                         YES
         L     R1,SPF_ZSCROLLN                  NUMBER TO SCROLL
         MH    R1,=Y(TRENTRYL)                  MULTIPLY BY LENGTH
         A     R1,DXD_FIRST                     PLUS FIRST
         C     R1,COMM_TRACE_LAST               BEYOND END OF TABLE?
         BH    DISP0040                         YES.. TO TO BOTTOM
         ST    R1,DXD_FIRST                     SET FIRST TO DISPLAY
         B     MAIN0000                         REBUILD DISPLAY
* ------------------------------------------------------------------- *
*        Top                                                          *
* ------------------------------------------------------------------- *
DISP0060 DS    0H
         MVC   DXD_FIRST,COMM_TRACE_1ST         SET FIRST TO DISPLAY
         B     MAIN0000
* ------------------------------------------------------------------- *
*        Up                                                           *
* ------------------------------------------------------------------- *
DISP0070 DS    0H
         CLI   SPF_ZSCROLLA,C'M'                MAX?
         BE    DISP0060                         YES
         L     R0,SPF_ZSCROLLN                  NUMBER TO SCROLL
         MH    R0,=Y(TRENTRYL)                  MULTIPLY BY LENGTH
         L     R1,DXD_FIRST                     FIRST
         SR    R1,R0                            MINUS SCROLL
         C     R1,COMM_TRACE_1ST                ABOVE TOP OF TABLE?
         BL    DISP0060                         YES.. GO TO TOP
         ST    R1,DXD_FIRST                     SET FIRST TO DISPLAY
         B     MAIN0000
* ------------------------------------------------------------------- *
*        Make current last on display                                 *
* ------------------------------------------------------------------- *
DISP0080 DS    0H
         MVC   SPF_ZCMD,COMM_BLANKS             RESET COMMAND
         L     R1,COMM_TRACE_CURR               CURRENT ENTRY
         L     R0,SPF_VDEPTH                    SCREEN DEPTH
         BCTR  R0,0                             MINUS 1
         MH    R0,=Y(TRENTRYL)                  MULTIPLY BY LENGTH
         SR    R1,R0                            SUBTRACT
         C     R1,COMM_TRACE_1ST                ABOVE TOP OF TABLE?
         BL    DISP0060                         YES.. FORCE TOP
         ST    R1,DXD_FIRST                     SET FIRST TO DISPLAY
         B     MAIN0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         DC    H'0'
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R0,DXD_VDATA_LENGTH
         L     R1,DXD_VDATA_ADDR
         MVI   COMM_VDATA_FUNC,$VDATA_FREEMAIN  SET FUNCTION
         L     R15,COMM_V_OSVDATA               OSVDATA ENTRY POINT
         BALR  R14,R15                          FREE VDATA STORAGE
         L     R13,4(,R13)                      RESTORE R13
         LM    R14,R12,12(R13)                  RESTORE REGISTERS
         SR    R15,R15                          SET RC
         BR    R14                              RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START        DC    Q(DXDTRACE)

H8               DC    H'8'

CURRENT          DC    C'CURRENT'
DISPLAY          DC    CL8'DISPLAY'
NAME_LIST        DC    C'(ZSCROLLA ZSCROLLN ZVERB)'
PANEL_NAME       DC    CL8'OSTRACE'
VGET             DC    CL8'VGET'
VPUT             DC    CL8'VPUT'

PRINTABLES       DS    0C
*                          0 1 2 3 4 5 6 7 8 9 A B C D E F
                 DC    X'40404040404040404040404040404040' 00-0F
                 DC    X'40404040404040404040404040404040' 10-1F
                 DC    X'40404040404040404040404040404040' 20-2F
                 DC    X'40404040404040404040404040404040' 30-3F
                 DC    X'404040404040404040404A4B4C4D4E4F' 40-4F
                 DC    X'504040404040404040405A5B5C5D5E5F' 50-5F
                 DC    X'60614040404040404040406B6C6D6E6F' 60-6F
                 DC    X'404040404040404040407A7B7C7D7E7F' 70-7F
                 DC    X'40818283848586878889408B8C8D8E8F' 80-8F
                 DC    X'40919293949596979899409B9C9D9E9F' 90-9F
                 DC    X'4040A2A3A4A5A6A7A8A940ABACADAEAF' A0-AF
                 DC    X'4040404040404040404040BBBCBDBEBF' B0-BF
                 DC    X'40C1C2C3C4C5C6C7C8C9404040404040' C0-CF
                 DC    X'40D1D2D3D4D5D6D7D8D9404040404040' D0-DF
                 DC    X'4040E2E3E4E5E6E7E8E9404040404040' E0-EF
                 DC    X'F0F1F2F3F4F5F6F7F8F9404040404040' F0-FF

                 LTORG

MSG_01           DS    0C
MSG_01_CURR      DC    CL03' '
                 DC    CL01' '
MSG_01_CSECT     DC    CL08' '
                 DC    CL02' '
MSG_01_ID        DC    CL08' '
                 DC    CL02' '
MSG_01_DATA1     DC    CL08' '
                 DC    CL02' '
MSG_01_HEX1      DC    CL16' '
                 DC    CL03' '
MSG_01_DATA2     DC    CL08' '
                 DC    CL02' '
MSG_01_HEX2      DC    CL16' '
                 DC    CL01' '
MSG_01_L         EQU   *-MSG_01

TRACEEND         EQU   *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDTRACE         DSECT
                 COPY    DXDPREF

DXD_SPF_PARMS    DS     0A
DXD_SPF_1        DS      A
DXD_SPF_2        DS      A

DXD_FIRST        DS      A              FIRST DISPLAYED
DXD_LAST         DS      A              LAST DISPLAYED

DXD_PANEL        DS      CL8
DXD_VDATA_ADDR   DS      A
DXD_VDATA_LENGTH DS      F

DXD_FLAGS        DS      X
$VPUT            EQU     X'80'
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     REGEQU
                 END      OSTRACE
