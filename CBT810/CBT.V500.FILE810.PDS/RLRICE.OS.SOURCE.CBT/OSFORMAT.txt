* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: OSFORMAT                                              *
*                                                                     *
*  Manage FORMAT data.                                                *
*                                                                     *
*  OS's function is to read and then display data.  To display data   *
*  in a 'user friendly' manner, the same data may be displayed in     *
*  multiple formats.                                                  *
*                                                                     *
*  Formats are defined by members in the library (PDS or PDS/E)       *
*  allocated to OSFORMAT.                                             *
*                                                                     *
*                                                                     *
*  To allow the same formatting program to perform multiple           *
*  functions, data specific for the format being defined can          *
*  be supplied in the FORMAT definition (USERFMT for example).        *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
OSFORMAT CSECT
OSFORMAT AMODE 31
OSFORMAT RMODE 24
         USING OSCOMM,R12
         USING SESSION,R11
         USING OSFORMAT,R15
         B     INIT0000
MODID    DC    CL8'OSFORMAT'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(FORMATEND-OSFORMAT)
INIT0000 DS    0H
         STM   R14,R12,12(R13)               SAVE REGS
         LR    R10,R15                       COPY ENTRY POINT
         DROP  R15
         USING OSFORMAT,R10                  DEFINE BASE
         LR    R9,R1                         PARAMETER BLOCK ADDRESS
         USING FMATREQ,R9                    DEFINE BASE
         GETMAIN RU,                                                   +
               LV=OSFORMAT_WORK_L,                                     +
               LOC=BELOW
         ST    R13,4(,R1)                    SAVE R13
         ST    R1,8(,R13)                    CHAIN SAVE AREA
         LR    R13,R1                        COPY WORK AREA ADDRESS
         USING OSFORMAT_WORK,R13             DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         USING DECB,WORK_DECB
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         ITRACE ID=ENTRY,                                              +
               DATA1=(FMATREQ_FUNC,1),                                 +
               DATA2=FMATREQ_FORMAT
         ICM   R7,15,COMM_FORMAT_DCB         FORMAT(S) DCB ADDRESS
         BNZ   INIT0010                      ALREADY HAVE DCB
         ITRACE ID=INIT
         GETMAIN RU,                         GETMAIN STORAGE FOR DCB   +
               LV=DCB_L,                                               +
               LOC=BELOW
         LR    R7,R1                         COPY DCB ADDRESS
         ST    R7,COMM_FORMAT_DCB            SAVE DCB ADDRESS
         MVC   0(DCB_L,R7),DCB_I             INITIALIZE DCB
         USING IHADCB,R7
         ITRACE ID=DCB,                                                +
               RDATA1=R7,                                              +
               RDATA2=R1
         MVC   WORK_OPEN(OPEN_L),OPEN_I
         OPEN  ((R7),INPUT),                                           +
               MODE=31,                                                +
               MF=(E,WORK_OPEN)
         TM    DCBOFLGS,DCBOFOPN             SUCCESSFUL?
         BNO   ERR0010                       NO
         SR    R0,R0
         ICM   R0,3,DCBBLKSI                 BLOCK SIZE
         GETMAIN RU,                                                   +
               LV=(0),                                                 +
               LOC=BELOW
         ST    R1,COMM_FORMAT_IO             SAVE I/O AREA ADDRESS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
INIT0010 DS    0H
         MVI   FMATREQ_RC,$FMATREQ_OK
         CLI   FMATREQ_FUNC,$FMATREQ_FREE
         BE    FMAT0100                      FREE UP A FORMAT
         CLI   FMATREQ_FUNC,$FMATREQ_CLOSE
         BE    FMAT0300                      CLOSE UP
* ------------------------------------------------------------------- *
*       'FIND' the FORMAT                                             *
* ------------------------------------------------------------------- *
FMAT0010 DS    0H
         ITRACE ID=LOAD_FMT
         ITRACE ID=FIND,                                               +
               RDATA1=R7,                                              +
               DATA2=FMATREQ_FORMAT
         FIND  (R7),                         ISSUE FIND                +
               FMATREQ_FORMAT,                                         +
               D
         ITRACE ID=FIND_RC,                                            +
               RDATA1=R15
         LTR   R15,R15                       SUCCESSFUL?
         BNZ   ERR0020                       NO
         CLI   FMATREQ_FUNC,$FMATREQ_VERIFY  VERIFYING A FORMAT NAME?
         BE    EXIT0000                      YES
* ------------------------------------------------------------------- *
*                                                                     *
*        We have a valid new format (at least the name is valid)      *
*        Free up any existing format info before loading new format.  *
*                                                                     *
*        Note that the function module itself is not deleted until    *
*        final clean up.                                              *
*                                                                     *
* ------------------------------------------------------------------- *
         OC    SESS_DXD_ADDR,SESS_DXD_ADDR
         BZ    FMAT0030                      NO WORK AREA
         ICM   R15,15,SESS_FUNCTION_EP       FUNCTION ENTRY POINT
         BZ    FMAT0030                      NO FUNCTION ACTIVE
         MVI   SESS_FORMAT_FLAGS,$FORMAT_CLEANUP
         ITRACE ID=CLEANUP1,                                           +
               RDATA1=R15
         BALR  R14,R15                       ALLOW CLEANUP
         ITRACE ID=CLEANED1
         B     FMAT0040
* ------------------------------------------------------------------- *
*                                                                     *
*        There was no format data, no clean up necessary              *
*                                                                     *
* ------------------------------------------------------------------- *
FMAT0030 DS    0H
         ITRACE ID=NOCLEAN1
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
FMAT0040 DS    0H
         ITRACE ID=RESET
         XC    SESS_FUNCTION_EP,SESS_FUNCTION_EP
         MVC   SESS_FORMAT,FMATREQ_FORMAT    COPY NAME TO COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
FMAT0050 DS    0H
         XC    WORK_DECB(DECB_L),WORK_DECB
         L     R8,COMM_FORMAT_IO             I/O AREA ADDRESS
         USING FORMAT,R8                     DEFINE BASE
         ITRACE ID=READ,                                               +
               RDATA1=R7,                                              +
               RDATA2=R8
         READ  WORK_DECB,                    READ                      +
               SF,                           .. SEQUENTIALLY FORWARD   +
               (R7),                         .. DCB                    +
               (R8),                         .. I/O AREA               +
               'S',                                                    +
               MF=E
         CHECK WORK_DECB                     WAIT FOR THE I/O
         L     R6,DECIOBPT                   IOB ADDRESS
         USING IOBSTDRD,R6                   DEFINE IOB BASE
         LH    R5,DCBBLKSI                   BLOCK SIZE
         SR    R1,R1                         CLEAR R1
         ICM   R1,3,IOBSTDRD+14              RESIDUAL LENGTH
         SR    R5,R1                         LENGTH READ
         BZ    FMAT0050                      READ NEXT BLOCK
FMAT0060 DS    0H
         ITRACE ID=DATAREAD,                                           +
               DATA1=FORMAT_TYPE,                                      +
               DATA2=FORMAT_DATA
         CLI   FORMAT_TYPE,C'*'              COMMENT?
         BE    FMAT0070                      YES
         CLC   FORMAT_TYPE,TYPE_EXIT         EXIT STATEMENT?
         BE    UEXIT000                      YES
         CLC   FORMAT_TYPE,TYPE_PROGRAM      PROGRAM STATEMENT?
         BE    PGM0000                       YES
         CLC   FORMAT_TYPE,TYPE_PANEL        PANEL STATEMENT?
         BE    PANL0000                      YES
         ICM   R15,15,SESS_FUNCTION_EP       FUNCTION ENTRY POINT
         BZ    ERR0030                       NO FUNCTION ACTIVE
         MVI   SESS_FORMAT_FLAGS,$FORMAT_CONTROL
         LA    R1,FORMAT                     CONTROL STATEMENT ADDR
         ITRACE ID=CONTROL,                                            +
               RDATA1=R15,                                             +
               RDATA2=R1
         BALR  R14,R15                       LINK TO FUNCTION
         LTR   R15,R15                       SUCCESSFUL?
         BNZ   FMAT0090                      NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
FMAT0070 DS    0H
         AH    R8,DCBLRECL                   NEXT CONTROL RECORD
         SH    R5,DCBLRECL                   MINUS 1 RECORD'S LENGTH
         BNZ   FMAT0060                      PROCESS LOGICAL RECORD
         B     FMAT0050                      READ NEXT PHYSICAL RECORD
FMAT0080 DS    0H
         ITRACE ID=EOF                       EOF HAS BEEN REACHED
         OC    SESS_FUNCTION_EP,SESS_FUNCTION_EP
         BZ    ERR0030
         B     EXIT0000
FMAT0090 DS    0H
         MVI   FMATREQ_RC,$FMATREQ_ERROR     SET ERROR INDICATOR
         MVI   COMM_MSG_ID+1,4
         L     R15,COMM_V_OSMSG
         BALR  R14,R15                       BUILD MESSAGE
         B     FMAT0300                      CLOSE DCB AND EXIT
* ------------------------------------------------------------------- *
*                                                                     *
*        Free FMAT blocks                                             *
*                                                                     *
* ------------------------------------------------------------------- *
FMAT0100 DS    0H
         ITRACE ID=FREE
         ICM   R15,15,SESS_FUNCTION_EP       FUNCTION ADDRESS
         BZ    EXIT0000                      NO FUNCTION.. EXIT
         MVI   SESS_FORMAT_FLAGS,$FORMAT_CLEANUP
         ITRACE ID=CLEANUP2,                                           +
               RDATA1=R15
         BALR  R14,R15                       ALLOW CLEANUP
         ITRACE ID=CLEANED2
         MVC   SESS_FUNCTION_MODULE,COMM_BLANKS
         XC    SESS_FUNCTION_EP,SESS_FUNCTION_EP
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
FMAT0300 DS    0H
         ICM   R7,15,COMM_FORMAT_DCB DCB ADDRESS
         BZ    EXIT0000
         ITRACE ID=CLOSE,                                              +
               RDATA1=R7
         LH    R2,DCBBLKSI                   BLOCK SIZE
         MVC   WORK_CLOSE(CLOSE_L),CLOSE_I
         CLOSE ((R7)),                                                 +
               MODE=31,                      CLOSE THE DCB             +
               MF=(E,WORK_CLOSE)
         FREEPOOL (R7)
         L     R1,COMM_FORMAT_IO
         ITRACE ID=FREE_IO,                                            +
               RDATA1=R1,                                              +
               RDATA2=R2
         FREEMAIN RU,                        FREE THE I/O AREA         +
               A=(1),                                                  +
               LV=(2)
         ITRACE ID=FREE_DCB,                                           +
               RDATA1=R7
         FREEMAIN RU,                        FREE DCB STORAGE          +
               A=(R7),                                                 +
               LV=DCB_L
         XC    COMM_FORMAT_DCB,COMM_FORMAT_DCB
         XC    COMM_FORMAT_IO,COMM_FORMAT_IO
         B     EXIT0000
* ------------------------------------------------------------------- *
*        Process PANEL statements                                     *
* ------------------------------------------------------------------- *
PANL0000 DS    0H
         MVC   SESS_DISP_PANEL,FORMAT_PANEL
         ITRACE ID=PANEL,                                              +
               DATA1=SESS_DISP_PANEL
         B     FMAT0070
* ------------------------------------------------------------------- *
*        Process PROGRAM statements                                   *
* ------------------------------------------------------------------- *
PGM0000  DS    0H
         MVC   SESS_FUNCTION_MODULE,FORMAT_PROGRAM      COPY PGM NAME
         ITRACE ID=LOADPGM,                                            +
               DATA1=SESS_FUNCTION_MODULE
         MVI   LOADREQ_FUNC,$LOADREQ_LOAD               OSLOAD FUNCTION
         MVC   LOADREQ_MODULE,FORMAT_PROGRAM            COPY PGM NAME
         LA    R1,LOADREQ                               OSLOAD PARMS
         L     R15,COMM_V_OSLOAD                        OSLOAD EP
         BALR  R14,R15                                  LINK TO OSLOAD
         CLI   LOADREQ_RC,$LOADREQ_OK                   SUCCESSFUL?
         BNE   ERR0050                                  NO
         ITRACE ID=LOADED
         MVI   SESS_FORMAT_FLAGS,$FORMAT_INITIALIZE     SET FUNCTION
         ICM   R15,15,LOADREQ_EP                        FUNCTION EP
         ST    R15,SESS_FUNCTION_EP                     SAVE ADDRESS
         ITRACE ID=FMATINIT,                                           +
               DATA1=(SESS_FUNCTION_EP,4)
         BALR  R14,R15                                  LINK
         ITRACE ID=INITDONE
         B     FMAT0070
* ------------------------------------------------------------------- *
*        Process exit statements                                      *
* ------------------------------------------------------------------- *
UEXIT000 DS    0H
         MVC   SESS_EXIT_MODULE,FORMAT_EXIT
         ITRACE ID=LOADEXIT,                                           +
               DATA1=SESS_EXIT_MODULE
         MVI   LOADREQ_FUNC,$LOADREQ_LOAD
         MVC   LOADREQ_MODULE,FORMAT_PROGRAM
         LA    R1,LOADREQ
         L     R15,COMM_V_OSLOAD
         BALR  R14,R15
         MVC   SESS_FUNCTION_EXIT,LOADREQ_EP
         ITRACE ID=PGM_ADDR,                                           +
               DATA1=(SESS_FUNCTION_EXIT,4)
         B     FMAT0070
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
NBR0000  DS    0H
         MVC   COMM_INFO_01(10),C_0000000000
NBR0010  DS    0H
         CLI   0(R1),C' '            BLANK?
         BE    NBR0020               YES
         CLI   0(R1),C','            COMMA?
         BE    NBR0020               YES
         CLI   0(R1),C'0'            ZERO?
         BL    NBR0030               LESS.. BAD
         CLI   0(R1),C'9'            NINE?
         BH    NBR0030               HIGHER.. BAD
         MVC   COMM_INFO_01(9),COMM_INFO_01+1
         MVC   COMM_INFO_01+9(1),0(R1)
         LA    R1,1(,R1)             NEXT DIGIT
         B     NBR0010               LOOP
NBR0020  DS    0H
         PACK  COMM_DWORD,COMM_INFO_01(10)
         CVB   R0,COMM_DWORD         CONVERT TO BINARY
         BR    R15
NBR0030  DS    0H
         ICM   R0,15,MINUS_ONE       INDICATE BAD
         BR    R15
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         MVI   COMM_MSG_ID+1,1       SET MESSAGE ID
         STH   R15,COMM_DWORD        SAVE RETURN CODE
         B     ERR1000
ERR0020  DS    0H
         MVI   COMM_MSG_ID+1,2       SET MESSAGE ID
         MVC   COMM_INFO_01,FMATREQ_FORMAT
         B     ERR1000
ERR0030  DS    0H
         MVI   COMM_MSG_ID+1,3       NO FUNCTION
         B     ERR1000
ERR0040  DS    0H
         DC    H'0'                  FORMAT DATA, NO FORMAT PROGRAM
ERR0050  DS    0H
         DC    H'0'                  LOAD FAILED
ERR1000  DS    0H
         MVC   COMM_MSG_CSECT,MODID
         MVI   COMM_MSG_ID,0
         L     R15,COMM_V_OSMSG      OSMSG ENTRY POINT
         BALR  R14,R15               BUILD MESSAGE
         MVI   FMATREQ_RC,$FMATREQ_ERROR
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT,                                               +
               DATA1=(FMATREQ_RC,1),                                   +
               RDATA2=R13
         LR    R1,R13
         L     R13,4(,R13)           RESTORE SAVE AREA ADDRESS
         FREEMAIN RU,                                                  +
               A=(1),                                                  +
               LV=OSFORMAT_WORK_L
         LM    R14,R12,12(R13)       RESTORE REMAINING REGISTERS
         BR    R14                   RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DCB_I    DCB   DSORG=PO,                                               +
               DDNAME=OSFORMAT,                                        +
               EODAD=FMAT0080,                                         +
               RECFM=FB,                                               +
               LRECL=80,                                               +
               MACRF=R
DCB_L    EQU   *-DCB_I
OPEN_I   OPEN  (*,INPUT),                                              +
               MODE=31,                                                +
               MF=L
OPEN_L   EQU   *-OPEN_I
CLOSE_I  CLOSE (*),                                                    +
               MODE=31,                                                +
               MF=L
CLOSE_L  EQU   *-CLOSE_I

MINUS_ONE       DC   F'-1'

H16             DC   H'16'
H30             DC   H'30'

C_0000000000    DC   C'0000000000'
TYPE_EXIT       DC   CL8'EXIT'
TYPE_PANEL      DC   CL8'PANEL'
TYPE_PROGRAM    DC   CL8'PROGRAM'


                LTORG

FORMATEND       EQU  *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
OSFORMAT_WORK       DSECT
                    COPY    DXDPREF

WORK_OPEN           DS      0F,(OPEN_L)X
WORK_CLOSE          DS      0F,(CLOSE_L)X
WORK_DECB           DS      0F,(DECB_L)X

                    LOADREQ DSECT=NO
OSFORMAT_WORK_L     EQU     *-OSFORMAT_WORK
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                    COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                    SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                    BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                    FMATREQ DSECT=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                    COPY    FORMAT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         IBMMAC DCBD=PS
         IBMMAC IOB=YES
         IBMMAC DECB=YES
DECB_L   EQU    *-DECB
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                    COPY    TRENTRY
* ------------------------------------------------------------------- *
*                   EQUATES                                           *
* ------------------------------------------------------------------- *
                    COPY    REGEQU
                    END     OSFORMAT
