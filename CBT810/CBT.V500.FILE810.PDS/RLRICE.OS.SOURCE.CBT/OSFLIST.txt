* ------------------------------------------------------------------- *
*                                                                     *
*  MODULE NAME: OSFLIST                                               *
*                                                                     *
*  Display a list of available FORMATs                                *
*                                                                     *
*  This module does not use OSDISP for displaying panels.             *
*  Therefore do not set SESS_DISP_PANEL, SESS_DISP_VDATA_ADDR,        *
*  SESS_DISP_VDATA_LENGTH, or SESS_DISP_CURSOR.                       *
*                                                                     *
* ------------------------------------------------------------------- *
OSFLIST  CSECT
OSFLIST  AMODE 31
OSFLIST  RMODE ANY
         USING OSFLIST,R15
         USING OSCOMM,R12
         USING SESSION,R11
         B     INIT0000
MODID    DC    CL8'OSFLIST'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(FLISTEND-OSFLIST)
INIT0000 DS    0H
         STM   R14,R12,12(R13)                    SAVE REGISTERS
         LR    R10,R15                            COPY BASE ADDRESS
         DROP  R15
         USING OSFLIST,R10
         L     R15,COMM_DXD                       DXD AREA BASE ADDRESS
         A     R15,DXD_START                      PLUS OFFSET
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15                            COPY DXD AREA ADDRESS
         USING DXDFLIST,R13                       DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
         L     R9,COMM_OSSPFD
         USING OSSPFD,R9

         MVC   COMM_MSG_CSECT,MODID               SET MODULE'S NAME
         MVI   COMM_MSG_ID,0                      FORCE ZERO

         MVC   DXD_SAVE_VDATA_ADDR,SESS_DISP_VDATA_ADDR
         MVC   DXD_SAVE_VDATA_LENGTH,SESS_DISP_VDATA_LENGTH
         MVC   DXD_SAVE_PANEL,SESS_DISP_PANEL
         MVC   DXD_SAVE_CURSOR,SESS_DISP_CURSOR

         LA    R1,PANEL_NAME                      PANEL NAME
         MVI   COMM_VDATA_FUNC,$VDATA_GETMAIN
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15                            GETMAIN VDATA
         ST    R0,DXD_VDATA_LENGTH
         ST    R1,DXD_VDATA_ADDR
         MVI   COMM_VDATA_FUNC,$VDATA_VDEFINE
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15                            VDEFINE VDATA
         ITRACE ID=VDATA,                                              +
               DATA1=(DXD_VDATA_ADDR,4),                               +
               DATA2=(DXD_VDATA_LENGTH,4)

         LA    R1,DISPLAY                         ADDR OF COMMAND
         ST    R1,DXD_SPF_1                       SET ADDR
         LA    R1,PANEL_NAME                      ADDR OF PANEL NAME
         ST    R1,DXD_SPF_2                       SET ADDR
         OI    DXD_SPF_2,X'80'                    FLAG END-OF-LIST

         MVC   DXD_VDEPTH,SPF_VDEPTH              SAVE DEPTH
         MVC   DXD_VWIDTH,SPF_VWIDTH              SAVE WIDTH

         SR    R0,R0
         L     R1,DXD_VWIDTH                      SCREEN WIDTH
         D     R0,=A(MSG01_L+4)                   COMPUTE NBR PER LINE
         ST    R1,DXD_DATA_COLUMNS                SAVE NBR OF COLUMNS
         ITRACE ID=DATACOLS,                                           +
               RDATA1=R0

         L     R0,DXD_VDATA_ADDR
         L     R1,DXD_VDATA_LENGTH
         SR    R14,R14
         SR    R15,R15
         ICM   R15,8,COMM_BLANKS
         MVCL  R0,R14                             INITIALIZE VDATA

         L     R8,DXD_VDATA_ADDR
         L     R7,DXD_VDEPTH                      NUMBER OF LINES
         L     R5,DXD_DATA_COLUMNS                NBR OF COLUMNS
         XC    DXD_COLUMN_DISP,DXD_COLUMN_DISP    INITIALIZE DISP

* ------------------------------------------------------------------- *
*        ISSUE DESERV TO OBTAIN DIRECTORY INFO FOR ALL MEMBERS        *
* ------------------------------------------------------------------- *
         L     R4,COMM_FORMAT_DCB                 FORMATS DCB ADDR
         MVC   DXD_DESERV(DESERV_L),DESERV_I      INITIALIZE PARM LIST
         ITRACE ID=GET_ALL
         DESERV FUNC=GET_ALL,                     ALL  ENTRIES         +
               AREAPTR=DXD_DATA_ADDR,             .. BUFFER FOR DATA   +
               CONN_INTENT=NONE,                  .. NO INTENT         +
               DCB=(R4),                          .. DCB ADDRESS       +
               RETCODE=DXD_RETURN_CODE,                                +
               RSNCODE=DXD_REASON_CODE,                                +
               MF=(E,DXD_DESERV,COMPLETE)
         ITRACE ID=DE_RC,                                              +
               DATA1=(DXD_RETURN_CODE,4),                              +
               DATA2=(DXD_REASON_CODE,4)
         OC    DXD_RETURN_CODE,DXD_RETURN_CODE
         BNZ   ERR0010
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         L     R4,DXD_DATA_ADDR                   ADDR OF RETURNED DATA
         USING DESB,R4
MAIN0010 DS    0H
         L     R3,DESB_COUNT                      NBR OF DESB'S
         LA    R2,DESB_DATA                       FIRST DESB
         USING SMDE,R2
MAIN0020 DS    0H
         MVC   0(MSG01_L,R8),MSG01
V        USING MSG01,R8
         SR    R14,R14
         ICM   R14,3,SMDE_NAME_OFF                OFFSET TO NAME
         AR    R14,R2                             PLUS BASE
         SR    R15,R15
         ICM   R15,3,0(R14)                       LENGTH OF NAME
         CH    R15,=Y(L'MSG01_NAME)               TOO LONG?
         BNH   MAIN0030                           NO
         LH    R15,=Y(L'MSG01_NAME)               LIMIT LENGTH
MAIN0030 DS    0H
         BCTR  R15,0                              ADJUST FOR EX
         EX    R15,NAME_MVC                       COPY NAME
         CLC   V.MSG01_NAME,SESS_FORMAT           CURRENT FORMAT?
         BNE   MAIN0040                           NO
         MVI   V.MSG01_ATTR_2,$SCREEN_ATTR_HIGH   HIGHLIGHT CURRENT
MAIN0040 DS    0H
         ICM   R1,15,SMDE_LEN                     LENGTH OF THIS SMDE
         AR    R2,R1                              PLUS CURRENT
         BCT   R3,MAIN0050                        PROCESS NEXT SMDE

         ICM   R0,15,DESB_LEN                     LENGTH OF DESB
         ICM   R0,8,DESB_SUBPOOL                  SUBPOOL NBR
         LR    R1,R4                              COPY ADDRESS
         ICM   R4,15,DESB_NEXT                    NEXT DESB
         ITRACE ID=FREEDESB,                                           +
               RDATA1=R1,                                              +
               RDATA2=R0
         FREEMAIN RU,                             FREE CURRENT DESB    +
               A=(1),                                                  +
               LV=(0)
         ITRACE ID=NEXTDESB,                                           +
               RDATA1=R4
         LTR   R4,R4                              ANOTHER DESB?
         BZ    MAIN0070                           NO.. DISPLAY DATA
         L     R3,DESB_COUNT                      NBR OF DESB'S
         LA    R2,DESB_DATA                       FIRST DESB

MAIN0050 DS    0H
         A     R8,DXD_VWIDTH                      NEXT LINE
         BCT   R7,MAIN0020                        CONTINUE, W/NEXT LINE
         L     R8,DXD_COLUMN_DISP                 CURRENT DISP
         A     R8,=A(MSG01_L+4)                   DISP FOR NEXT COLUMN
         ST    R8,DXD_COLUMN_DISP                 UPDATE DISP
         A     R8,DXD_VDATA_ADDR                  PLUS BASE
         BCT   R5,MAIN0020
* ------------------------------------------------------------------- *
*                                                                     *
*        Free any remaining data                                      *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0060 DS    0H
         ICM   R0,15,DESB_LEN                     LENGTH OF DESB
         ICM   R0,8,DESB_SUBPOOL                  SUBPOOL NBR
         LR    R1,R4                              COPY ADDRESS
         ICM   R4,15,DESB_NEXT                    NEXT DESB
         ITRACE ID=FREEDESB,                                           +
               RDATA1=R1,                                              +
               RDATA2=R0
         FREEMAIN RU,                             FREE CURRENT DESB    +
               A=(1),                                                  +
               LV=(0)
         ITRACE ID=NEXTDESB,                                           +
               RDATA1=R4
         LTR   R4,R4                              ANOTHER DESB?
         BNZ   MAIN0060                           YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0070 DS    0H
         ITRACE ID=DISPLAY
         LA    R1,DXD_SPF_PARMS                   DISPLAY PARMS
         L     R15,COMM_ISPLINK
         BALR  R14,R15                            DISPLAY LIST
         MVC   SPF_MSG_1,COMM_BLANKS
         MVC   SPF_MSG_2,COMM_BLANKS
         MVC   SPF_MSG_3,COMM_BLANKS
         MVC   SPF_MSG_4,COMM_BLANKS
         MVC   SPF_MSG_5,COMM_BLANKS
         ITRACE ID=DISP_RC,                                            +
               RDATA1=R15
         LTR   R15,R15                            RC=0?
         BNZ   EXIT0000                           NO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         L     R8,DXD_VDATA_ADDR                  VDATA ADDRESS
         L     R7,DXD_VDEPTH                      SCREEN DEPTH
         L     R5,DXD_DATA_COLUMNS                DATA COLUMNS
         XC    DXD_COLUMN_DISP,DXD_COLUMN_DISP    INIT COLUMN DISP
         MVC   DXD_SELECTED_FORMAT,COMM_BLANKS
MAIN0100 DS    0H
         ITRACE ID=SELECT,                                             +
               RDATA1=R8,                                              +
               DATA2=V.MSG01_NAME
         CLI   V.MSG01_SELECTION,0                SELECTED?
         BE    MAIN0110                           NO
         CLI   V.MSG01_SELECTION,C' '             SELECTED?
         BE    MAIN0110                           NO
         CLC   DXD_SELECTED_FORMAT,COMM_BLANKS    ANOTHER ALREADY?
         BNE   MAIN0120                           YES
         MVC   DXD_SELECTED_FORMAT,V.MSG01_NAME   SAVE SELECTED FORMAT
MAIN0110 DS    0H
         A     R8,DXD_VWIDTH                      NEXT LINE
         BCT   R7,MAIN0100                        LOOP
         L     R8,DXD_COLUMN_DISP                 CURRENT DISP
         ITRACE ID=NEXT_COL,                                           +
               RDATA1=R8,                                              +
               RDATA2=R5
         A     R8,=A(MSG01_L+4)                   DISP FOR NEXT COLUMN
         ST    R8,DXD_COLUMN_DISP                 UPDATE DISP
         A     R8,DXD_VDATA_ADDR                  PLUS BASE
         L     R7,DXD_VDEPTH                      SCREEN DEPTH
         BCT   R5,MAIN0100                        LOOP
         CLC   DXD_SELECTED_FORMAT,COMM_BLANKS    NEW FORMAT?
         BE    EXIT0000                           NO
         CLC   DXD_SELECTED_FORMAT,SESS_FORMAT    SAME AS CURRENT?
         BE    EXIT0000                           YES
         MVC   COMM_NEW_FORMAT,DXD_SELECTED_FORMAT
         B     EXIT0000
MAIN0120 DS    0H
         MVI   COMM_MSG_ID+1,1                    SET MESSAGE ID
         L     R15,COMM_V_OSMSG                   OSMSG ENTRY POINT
         BALR  R14,R15                            BUILD MESSAGE
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
         B     MAIN0070                           DISPLAY WITH MSG
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         MVI   COMM_MSG_ID+1,2
         MVC   COMM_INFO_01,DXD_RETURN_CODE       COPY RETURN CODE
         MVC   COMM_INFO_02,DXD_REASON_CODE       COPY REASON CODE
         L     R15,COMM_V_OSMSG                   OSMSG ENTRY POINT
         BALR  R14,R15                            BUILD MESSAGE
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT

         L     R0,DXD_VDATA_LENGTH
         L     R1,DXD_VDATA_ADDR
         MVI   COMM_VDATA_FUNC,$VDATA_FREEMAIN
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15

         MVC   SESS_DISP_VDATA_ADDR,DXD_SAVE_VDATA_ADDR
         MVC   SESS_DISP_VDATA_LENGTH,DXD_SAVE_VDATA_LENGTH
         MVC   SESS_DISP_PANEL,DXD_SAVE_PANEL
         MVC   SESS_DISP_CURSOR,DXD_SAVE_CURSOR

         L     R13,4(,R13)                        RESTORE R13
         LM    R14,R12,12(R13)                    RESTORE REGISTERS
         SR    R15,R15                            SET RC
         BR    R14                                RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
NAME_MVC MVC   V.MSG01_NAME(0),2(R14)             COPY MEMBER NAME

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START         DC    Q(DXDFLIST)

DESERV_I          DESERV FUNC=GET_ALL,MF=L
DESERV_L          EQU   *-DESERV_I

DISPLAY           DC    CL8'DISPLAY'
PANEL_NAME        DC    CL8'OSFLIST'

                  LTORG

MSG01             DS    0C
MSG01_ATTR_1      DC    AL01($SCREEN_ATTR_INPUT)
MSG01_SELECTION   DC    CL1' '
MSG01_ATTR_2      DC    AL01($SCREEN_ATTR_NORMAL)
MSG01_NAME        DC    CL8' '
                  DC    CL01' '
MSG01_L           EQU   *-MSG01

FLISTEND          EQU   *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDFLIST            DSECT
                    COPY DXDPREF

DXD_SPF_PARMS       DS   0A
DXD_SPF_1           DS   A
DXD_SPF_2           DS   A

DXD_VDATA_LENGTH    DS   F
DXD_VDATA_ADDR      DS   A

DXD_VDEPTH          DS   F
DXD_VWIDTH          DS   F

DXD_SAVE_VDATA_ADDR DS   A
DXD_SAVE_VDATA_LENGTH DS A
DXD_SAVE_PANEL      DS   CL8
DXD_SAVE_CURSOR     DS   F

DXD_DATA_COLUMNS    DS   F
DXD_COLUMN_DISP     DS   F

DXD_RETURN_CODE     DS   F
DXD_REASON_CODE     DS   F

DXD_DATA_ADDR       DS   A

DXD_DESERV          DS   0F,(DESERV_L)X

DXD_SELECTED_FORMAT DS   CL8
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             IBMMAC   DES=YES,SMDE=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             COPY     TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
             COPY     REGEQU
             END      OSFLIST
