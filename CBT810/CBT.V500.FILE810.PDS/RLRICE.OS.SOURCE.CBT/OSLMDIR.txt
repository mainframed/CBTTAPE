* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: OSLMDIR                                               *
*                                                                     *
*  Build main body for load module PDS directories.                   *
*                                                                     *
* ------------------------------------------------------------------- *
OSLMDIR  CSECT
OSLMDIR  AMODE 31
OSLMDIR  RMODE ANY
         USING OSCOMM,R12
         USING OSLMDIR,R15
         B     INIT0000
MODID    DC    CL8'OSLMDIR'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(OSLMDIREND-OSLMDIR)
INIT0000 DS    0H
         USING SESSION,R11                    DEFINE SESSION BASE
         STM   R14,R12,12(R13)                SAVE REGS
         LR    R10,R15                        COPY ENTRY POINT
         LA    R9,2048(,R10)                  SET 2ND BASE REGISTER
         LA    R9,2048(,R9)                     ..........
         DROP  R15
         USING OSLMDIR,R10,R9                 DEFINE BASE
         L     R5,COMM_OSSPFD                 SPF DATA AREA
         USING OSSPFD,R5                      DEFINE BASE
         USING VDATA,R8
         CLI   SESS_FORMAT_FLAGS,$FORMAT_INITIALIZE
         BNE   INIT0010
* ------------------------------------------------------------------- *
*                                                                     *
*        Perform initialization                                       *
*                                                                     *
* ------------------------------------------------------------------- *
         ICM   R0,15,SESS_DXD_ADDR
         BNZ   ERR0110
         L     R0,DXD_SIZE
         ST    R0,SESS_DXD_LENGTH
         GETMAIN RU,                          GETMAIN WORK AREA        +
               LV=(0),                                                 +
               LOC=BELOW
         ST    R1,SESS_DXD_ADDR
         LR    R2,R1
         LR    R0,R1
         L     R1,DXD_SIZE
         SR    R14,R14
         SR    R15,R15
         MVCL  R0,R14                         INITIALIZE WORK AREA
         A     R2,DXD_START
         ST    R13,4(,R2)                     SAVE R13
         ST    R2,8(,R13)                     CHAIN SAVE AREA
         LR    R13,R2                         COPY WORK AREA ADDRESS
         USING DXD006,R13                     DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY1
         MVC   DXD_SAVED_RETRY,SESS_RETRY
         LA    R15,RTRY0000                   ABEND RETRY POINT
         ST    R15,SESS_RETRY                 SET IN COMMON AREA
         MVC   DXD_OPEN(OPEN_L),OPEN_I
         MVC   DXD_CLOSE(CLOSE_L),CLOSE_I
         LA    R1,DIR_RECORD
         ST    R1,DSPCREQ_RECORD_ADDR
         LA    R1,DATASPACE_1                 USE DATA SPACE 1
         ST    R1,DSPCREQ_DATASPACE
         ICM   R4,15,SESS_DCB_ADDR            HAVE A DCB?
         BNZ   EXIT0000                       YES
         LA    R0,PDSDCB_L
         ST    R0,SESS_DCB_LENGTH
         GETMAIN RU,                          GETMAIN STORAGE FOR DCB  +
               LV=(0),                                                 +
               LOC=BELOW
         LR    R4,R1                          COPY ADDRESS
         ST    R4,SESS_DCB_ADDR               SAVE ADDRESS
         MVC   0(PDSDCB_L,R4),PDSDCB_I
         USING IHADCB,R4                      DEFINE BASE
         MVC   DCBDDNAM,SESS_DD               COPY DD NAME
         OPEN  ((R4),OUTPUT),                 OPEN DCB                 +
               MODE=31,                                                +
               MF=(E,DXD_OPEN)
         OI    SESS_DCB_FLAGS,$SESS_DCB_OPEN  INDICATE DCB IS OPEN
         LA    R1,PANEL_OS0012                PANEL NAME
         MVI   COMM_VDATA_FUNC,$VDATA_GETMAIN
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15                        GETMAIN VDATA
         ST    R0,DXD_VDATA_LENGTH            SAVE LENGTH
         ST    R1,DXD_VDATA_ADDR              SAVE ADDRESS
         MVC   DXD_VDEPTH,SPF_VDEPTH          COPY DEPTH
         MVC   DXD_VWIDTH,SPF_VWIDTH          COPY WIDTH
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
INIT0010 DS    0H
         L     R4,SESS_DCB_ADDR
         ICM   R15,15,SESS_DXD_ADDR           WORK AREA ADDRESS
         BZ    ERR0130                        ERROR IF ZERO
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15
         ITRACE ID=ENTRY2
         XC    DXD_RC,DXD_RC
         CLI   SESS_FORMAT_FLAGS,$FORMAT_CONTROL
         BE    EXIT0000
         CLI   SESS_FORMAT_FLAGS,$FORMAT_CLEANUP
         BE    CLEAN000
         CLI   COMM_SESS_FUNC,$SESS_SWITCH    SESSION SWITCH?
         BNE   MAIN0000                       NO
         TM    DXD_FLAGS,$DXD_INIT            EVER BUILT DATA?
         BNO   MAIN0000                       NO
         L     R7,DXD_LINES_REMAINING         RESTORE LINES REMAINING
         L     R8,DXD_CURRENT_VDATA           RESTORE VDATA POSITION
         ITRACE ID=RESUME,                                             +
               RDATA1=R7,                                              +
               RDATA2=R8
         LTR   R7,R7                          RESUMING A LINE CMD?
         BZ    MAIN0160                       NO
         B     MAIN0230                       RESUME LINE CMDS
* ------------------------------------------------------------------- *
*          (Re)build display                                          *
* ------------------------------------------------------------------- *
MAIN0000 DS    0H
         OI    DXD_FLAGS,$DXD_INIT
         MVC   DSPCREQ_RECORD_NBR,DATASPACE_1_DISPLAY_FIRST_RECORD
         MVC   SPF_ZCMD,COMM_BLANKS
* ------------------------------------------------------------------- *
*            Clear the VDATA buffer                                   *
* ------------------------------------------------------------------- *
         XC    DXD_LINES_BUILT,DXD_LINES_BUILT
         L     R0,DXD_VDATA_ADDR              VDATA BUFFER ADDRESS
         L     R1,DXD_VDATA_LENGTH            VDATA BUFFER LENGTH
         SR    R14,R14                        CLEAR R14
         SR    R15,R15                        CLEAR R15
         MVCL  R0,R14                         INITIALIZE VDATA BUFFER
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         XC    DXD_CURSOR_POSITION,DXD_CURSOR_POSITION
* ------------------------------------------------------------------- *
*        Add heading to body                                          *
* ------------------------------------------------------------------- *
         L     R7,DXD_VDEPTH                  LINES AVAILABLE
         L     R8,DXD_VDATA_ADDR              BUFFER ADDRESS
         LA    R2,HEADING_01_L                HEADING LENGTH
         BCTR  R2,0                           CONVERT LENGTH
         EX    R2,MVC_01                      COPY HEADING
         B     MAIN0140
* ------------------------------------------------------------------- *
*        "Retrieve" data and build body of SPF panel                  *
* ------------------------------------------------------------------- *
MAIN0010 DS    0H
         CLC   DSPCREQ_RECORD_NBR,DATASPACE_1_LAST_RECORD
         BH    MAIN0150                       END OF RECORDS REACHED
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETRIEVE
         ITRACE ID=GET_RECD,                                           +
               DATA1=(DSPCREQ_RECORD_NBR,4),                           +
               DATA2=(DSPCREQ_RECORD_ADDR,4)
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE              OSDSPACE ENTRY POINT
         BALR  R14,R15                        LINK TO OSDSPACE
         CLI   DSPCREQ_RC,$DSPCREQ_OK
         BNE   ERR0020
         MVC   VDATA(VDATA_L),COMM_BLANKS
         MVI   VDATA_ATTR_1,$SCREEN_ATTR_INPUT
         MVC   VDATA_NAME,DIR_NAME
         TM    DIR_FLAGS,$DIR_TAGGED
         BO    MAIN0020
         MVI   VDATA_ATTR_2,$SCREEN_ATTR_HIGH_TURQUOISE
         B     MAIN0030
MAIN0020 DS    0H
         MVI   VDATA_ATTR_2,$SCREEN_ATTR_HIGH
MAIN0030 DS    0H
         MVI   VDATA_ATTR_3,$SCREEN_ATTR_NORMAL
         UNPK  COMM_INFO_01(7),DIR_TTR(4)
         TR    COMM_INFO_01(6),COMM_HEXCHAR
         MVC   VDATA_TTR,COMM_INFO_01
         TM    DIR_FLAGS,$DIR_PMAR
         BNO   MAIN0090
* ------------------------------------------------------------------- *
*                  Format PMAR data                                   *
* ------------------------------------------------------------------- *
         ICM   R1,15,DIR_PMAR_STOR            VIRTUAL STORAGE REQUIRED
         CVD   R1,COMM_DWORD                  CONVERT TO DECIMAL
         MVC   VDATA_SIZE,EDIT_WORD_1
         ED    VDATA_SIZE,COMM_DWORD+4
         UNPK  VDATA_SIZE_HEX(9),DIR_PMAR_STOR(5)
         TR    VDATA_SIZE_HEX,COMM_HEXCHAR    TRANSLATE
         MVI   VDATA_DELIM1,C'('              INSERT DELIMITER
         MVI   VDATA_DELIM2,C')'              INSERT DELIMITER
         MVC   VDATA_AC,DIR_PMAR_AC           COPY AUTH CODE
         OI    VDATA_AC,X'F0'                 'TRANSLATE' TO EBCDIC
         TM    DIR_PMAR_ATR4,X'03'            AMODE ANY?
         BO    MAIN0040                       YES
         TM    DIR_PMAR_ATR4,X'01'            AMODE 64?
         BO    MAIN0050                       YES
         TM    DIR_PMAR_ATR4,X'10'            AMODE 31?
         BO    MAIN0060                       YES
         MVC   VDATA_AM,AM_24                 AMODE IS 24
         B     MAIN0070
MAIN0040 DS    0H
         MVC   VDATA_AM,AM_ANY                AMODE IS ANY
         B     MAIN0070
MAIN0050 DS    0H
         MVC   VDATA_AM,AM_64                 AMODE IS 64
         B     MAIN0070
MAIN0060 DS    0H
         MVC   VDATA_AM,AM_31                 AMODE IS 31
MAIN0070 DS    0H
         MVI   VDATA_SLASH,C'/'               INSERT THE SLASH
         TM    DIR_PMAR_ATR4,X'10'            RMODE 31?
         BO    MAIN0080                       YES
         MVC   VDATA_RM,RM_24                 SET RMODE 24
         B     MAIN0090
MAIN0080 DS    0H
         MVC   VDATA_RM,RM_31                 SET RMODE 31
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0090 DS    0H
         TM    DIR_FLAGS,$DIR_DELETED
         BO    MAIN0100
         TM    DIR_FLAGS,$DIR_RENAMED
         BO    MAIN0110
         B     MAIN0130
MAIN0100 DS    0H
         MVC   VDATA_OVERLAY(DELETED_MSG_L),DELETED_MSG
         B     MAIN0120
MAIN0110 DS    0H
         MVC   VDATA_OVERLAY(RENAMED_MSG_L),RENAMED_MSG
MAIN0120 DS    0H
         MVI   VDATA_ATTR_2,$SCREEN_ATTR_HIGH_TURQUOISE
MAIN0130 DS    0H
         L     R1,DSPCREQ_RECORD_NBR          CURRENT RECORD NUMBER
         A     R1,F1                          ADD 1
         ST    R1,DSPCREQ_RECORD_NBR          SAVE NEW RECORD NUMBER
         LH    R1,DXD_LINES_BUILT             LINES DISPLAYED
         A     R1,F1                          ADD 1
         STH   R1,DXD_LINES_BUILT             SAVE LINES DISPLAYED
MAIN0140 DS    0H
         A     R8,DXD_VWIDTH                  NEXT LINE
         BCT   R7,MAIN0010                    MINUS 1 LINE
* ------------------------------------------------------------------- *
*            Build DSCB info                                          *
* ------------------------------------------------------------------- *
MAIN0150 DS    0H
         ITRACE ID=OSDSCB
         MVC   DATASPACE_1_DISPLAY_LAST_RECORD,DSPCREQ_RECORD_NBR
         MVC   SPF_COLUMN_FROM,H1             LEFT/RIGHT NOT USED
         L     R15,COMM_V_OSDSCB              OSDSCB ENTRY POINT
         BALR  R14,R15                        FORMAT THE DSCB INFO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0160 DS    0H
         ITRACE ID=DISPLAY
         MVC   SESS_DISP_PANEL,PANEL_OS0012   SET PANEL NAME
         MVC   SESS_DISP_VDATA_ADDR,DXD_VDATA_ADDR
         MVC   SESS_DISP_VDATA_LENGTH,DXD_VDATA_LENGTH
         MVC   SESS_DISP_CURSOR,DXD_CURSOR_POSITION
         L     R15,COMM_V_OSDISP              OSDISP ENTRY POINT
         BALR  R14,R15                        LINK TO OSDISP
         ITRACE ID=DISP_RC,                                            +
               DATA1=(SESS_RC,2),                                      +
               DATA2=(SESS_STATUS,1)
         CLC   COMM_NEW_FORMAT,COMM_BLANKS    NEW FORMAT?
         BNE   EXIT0000                       YES
         CLI   COMM_SESS_FUNC,0               SESSION FUNCTION?
         BNE   SESS0100                       YES
         OC    SESS_RC,SESS_RC                RC=0?
         BZ    MAIN0200                       YES
         ITRACE ID=ENDING
         MVI   COMM_SESS_FUNC,$SESS_REMOVE    SET FUNCTION
         B     EXIT0000                       EXIT
* ------------------------------------------------------------------- *
*                                                                     *
*        Process commands                                             *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0200 DS    0H
         NI    DXD_FLAGS,255-$LINE_CMD        RESET INDICATOR
         MVC   SPF_MSG_1,COMM_BLANKS          CLEAR MESSAGE
         MVC   DSPCREQ_RECORD_NBR,DATASPACE_1_DISPLAY_FIRST_RECORD
         LH    R7,DXD_LINES_BUILT             LINES OF DISPLAY BUILT
         L     R8,DXD_VDATA_ADDR              SPF DISPLAY BUFFER ADDR
         A     R8,DXD_VWIDTH                  SKIP HEADING LINE
         L     R1,DXD_VWIDTH                  SCREEN WIDTH
         AH    R1,=Y(VDATA_SELECTION-VDATA+1) PLUS DISP TO SELECTION
         ST    R1,DXD_CURSOR_POSITION         INITIALIZE CURSOR
         ITRACE ID=CMDS,                                               +
               RDATA1=R8,                                              +
               RDATA2=R7
MAIN0210 DS    0H
         ITRACE ID=LINE_CMD,                                           +
               DATA1=(VDATA_SELECTION,1),                              +
               DATA2=VDATA_NAME
         OI    VDATA_SELECTION,C' '           'TRANSLATE' TO UPPER CASE
         CLI   VDATA_SELECTION,C' '           BLANK?
         BE    MAIN0240                       YES
         LA    R1,VDATA_SELECTION             COMMAND ADDRESS
         ST    R1,SESS_COMMAND_ADDRESS        SET ADDRESS
         MVC   SESS_COMMAND_LENGTH,H1         SET LENGTH
         MVI   SESS_COMMAND_FLAGS,$SESS_COMMAND_SEARCH
         LA    R0,LINE_COMMANDS
         ST    R0,COMM_MAIN_COMMAND_TABLE
         XC    COMM_SUB_COMMAND_TABLE,COMM_SUB_COMMAND_TABLE
         L     R15,COMM_V_OSCMD               OSCMD ENTRY POINT
         BALR  R14,R15                        SEARCH COMMANDS
         TM    SESS_COMMAND_FLAGS,$SESS_COMMAND_FOUND
         BOR   R15                            COMMAND FOUND
         MVC   COMM_INFO_01,VDATA_SELECTION
         MVC   COMM_MSG_ID,H1                 SET MESSAGE ID
         BAL   R2,MSG0000
         MVC   SPF_MSG_1,COMM_MSG_1           COPY MESSAGE
         MVC   SPF_MSG_2,COMM_MSG_2           COPY MESSAGE
         MVC   SPF_MSG_3,COMM_MSG_3           COPY MESSAGE
         MVC   SPF_MSG_4,COMM_MSG_4           COPY MESSAGE
         MVC   SPF_MSG_5,COMM_MSG_5           COPY MESSAGE
         B     MAIN0150
MAIN0230 DS    0H
         OI    DXD_FLAGS,$LINE_CMD            LINE CMD WAS FOUND
MAIN0240 DS    0H
         L     R1,DSPCREQ_RECORD_NBR          RECORD NUMBER
         A     R1,F1                          PLUS 1
         ST    R1,DSPCREQ_RECORD_NBR          SAVE RECORD NUMBER
MAIN0250 DS    0H
         L     R1,DXD_CURSOR_POSITION         CURSOR POSITION
         A     R1,DXD_VWIDTH                  NEXT LINE
         ST    R1,DXD_CURSOR_POSITION         UPDATE CURSOR POSITION
         A     R8,DXD_VWIDTH                  NEXT LINE
         BCT   R7,MAIN0210                    LOOP
         XC    DXD_CURSOR_POSITION,DXD_CURSOR_POSITION
         TM    DXD_FLAGS,$LINE_CMD            LINE COMMAND FOUND?
         BO    MAIN0000                       YES
* ------------------------------------------------------------------- *
*                                                                     *
*       No line command(s) were found.                                *
*                                                                     *
*       Process any primary command.                                  *
*                                                                     *
* ------------------------------------------------------------------- *
         CLC   SPF_ZCMD,COMM_BLANKS           ALL BLANK?
         BE    MAIN0290                       YES
         ITRACE ID=PRI_CMD,                                            +
               DATA1=SPF_ZCMD+00,                                      +
               DATA2=SPF_ZCMD+08
         SR    R3,R3                          CLEAR REGISTER
         ICM   R3,3,COMM_OPERANDS_NBR
         BZ    MAIN0290                       NO OPERANDS
         MVC   SESS_COMMAND_ADDRESS,OPERAND_01_ADDRESS
         MVC   SESS_COMMAND_LENGTH,OPERAND_01_LENGTH
         MVI   SESS_COMMAND_FLAGS,$SESS_COMMAND_SEARCH
         LA    R0,COMMAND_TABLE
         ST    R0,COMM_MAIN_COMMAND_TABLE
         XC    COMM_SUB_COMMAND_TABLE,COMM_SUB_COMMAND_TABLE
         L     R15,COMM_V_OSCMD               OSCMD ENTRY POINT
         BALR  R14,R15                        SEARCH COMMANDS
         TM    SESS_COMMAND_FLAGS,$SESS_COMMAND_FOUND
         BOR   R15                            COMMAND FOUND
         B     ERR0080                        NOT FOUND
* ------------------------------------------------------------------- *
*       No commands so check for scroll                               *
* ------------------------------------------------------------------- *
MAIN0290 DS    0H
         L     R1,SPF_ZSCROLLN
         ST    R1,SESS_HORIZONTAL_SCROLL
         BCTR  R1,0
         ST    R1,SESS_VERTICAL_SCROLL
         ITRACE ID=ZVERB,                                              +
               DATA1=SPF_ZVERB,                                        +
               DATA2=(SPF_ZSCROLLN,4)
         CLI   SPF_ZVERB,C'B'                 BOTTOM?
         BE    MAIN0350                       YES
         CLI   SPF_ZVERB,C'D'                 SCROLL DOWN?
         BE    MAIN0320                       YES
         CLI   SPF_ZVERB,C'T'                 TOP?
         BE    MAIN0340                       YES
         CLI   SPF_ZVERB,C'U'                 SCROLL UP?
         BE    MAIN0300                       YES
         B     MAIN0150                       RE-DISPLAY AS IS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0300 DS    0H
         ITRACE ID=UP
         MVC   SPF_ZCMD,COMM_BLANKS                 CLEAR ZCMD
         CLI   SPF_ZSCROLLA,C'M'                    MAX?
         BE    MAIN0340                             YES
         L     R1,SESS_VERTICAL_SCROLL              NUMBER TO SCROLL
         L     R2,DATASPACE_1_DISPLAY_FIRST_RECORD  1ST DISPLAYED       D
         SR    R2,R1                                MINUS SCROLL
         CH    R2,H1                                LESS THAN 1?
         BNL   MAIN0310                             NO
         LH    R2,H1                                SET TO 1ST RECORD
MAIN0310 DS    0H                                   YES
         ST    R2,DATASPACE_1_DISPLAY_FIRST_RECORD  CHANGE 1ST RCD NBR
         B     MAIN0000                             REBUILD EVERYTHING
* ------------------------------------------------------------------- *
*        SCROLL DOWN                                                  *
* ------------------------------------------------------------------- *
MAIN0320 DS    0H
         ITRACE ID=DOWN
         MVC   SPF_ZCMD,COMM_BLANKS                 CLEAR ZCMD
         CLI   SPF_ZSCROLLA,C'M'                    MAX?
         BE    MAIN0350                             YES
         L     R1,SESS_VERTICAL_SCROLL              NUMBER TO SCROLL
         L     R2,DATASPACE_1_DISPLAY_FIRST_RECORD  FIRST DISPLAYED
         AR    R2,R1                                PLUS SCROLL
         C     R2,DATASPACE_1_LAST_RECORD
         BH    MAIN0330                             BEYOND END OF DATA
         ST    R2,DATASPACE_1_DISPLAY_FIRST_RECORD  CHANGE 1ST RCD NBR
         B     MAIN0000                             REBUILD EVERYTHING
MAIN0330 DS    0H
         L     R2,DATASPACE_1_LAST_RECORD           HIGHEST RECD STORED
         S     R2,DATASPACE_1_DISPLAY_FIRST_RECORD  MINUS 1ST DISPLAYED
         C     R2,DXD_VDEPTH                        MORE THAN DEPTH?
         BNH   MAIN0000                             NO.. STAY PUT
         L     R2,DATASPACE_1_LAST_RECORD           HIGHEST STORED
         S     R2,DXD_VDEPTH                        MINUS 1 SCREEN
         A     R2,F2                                PLUS 2
         ST    R2,DATASPACE_1_DISPLAY_FIRST_RECORD  SET 1ST TO DISPLAY
         B     MAIN0000                             REBUILD EVERYTHING
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0340 DS    0H
         ITRACE ID=TOP
         MVC   SPF_ZCMD,COMM_BLANKS                 CLEAR ZCMD
         MVC   DATASPACE_1_DISPLAY_FIRST_RECORD,F1  SET TO RECORD 1
         XC    SESS_LAST_FOUND,SESS_LAST_FOUND
         B     MAIN0000                             REBUILD EVERYTHING
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0350 DS    0H
         ITRACE ID=BOTTOM
         MVC   SPF_ZCMD,COMM_BLANKS                 CLEAR ZCMD
         L     R2,DATASPACE_1_LAST_RECORD           HIGHEST RECORD
         S     R2,DXD_VDEPTH                        MINUS DEPTH
         A     R2,F2                                PLUS 2
         CH    R2,H1                                STILL HAVE 1?
         BNL   MAIN0360                             YES
         LH    R2,H1                                FORCE RECORD 1
MAIN0360 DS    0H
         ST    R2,DATASPACE_1_DISPLAY_FIRST_RECORD  CHANGE 1ST RCD      BR
         B     MAIN0000                             REBUILD EVERYTHING
* ------------------------------------------------------------------- *
*                                                                     *
*                                                                     *
*        LMOD info                                                    *
*                                                                     *
*          Create a new 'session'                                     *
*          set data type, function name, panel name                   *
*          set 'new session' indicator                                *
*          save vdata buffer postion info                             *
*                                                                     *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0370 DS    0H
         MVI   VDATA_SELECTION,C' '
         MVC   SPF_ZCMD,COMM_BLANKS
         TM    DIR_FLAGS,$DIR_DELETED+$DIR_RENAMED
         BNZ   ERR0100
         BAL   R6,SESS0000                    CREATE NEW SESSION
NEW      USING SESSION,R3
         MVC   NEW.SESS_MEMBER,VDATA_NAME     SET MEMBER NAME
         MVI   NEW.SESS_DATA_TYPE,$DATA_TYPE_OTHER
         MVC   NEW.SESS_FORMAT,FORMAT_LMOD
         MVC   NEW.SESS_DEFAULT_FORMAT,FORMAT_LMOD
         MVC   NEW.SESS_DISP_PANEL,PANEL_LMOD
         MVI   COMM_SESS_FUNC,$SESS_SWITCH
         B     SESS0100
         DROP  NEW
* ------------------------------------------------------------------- *
*        LOCATE (make a member the 1st shown on display)              *
* ------------------------------------------------------------------- *
MAIN0380 DS    0H
         ITRACE ID=LOCATE
         CH    R3,H2                          PROPER NUMBER OF PARMS?
         BL    ERR0050                        TOO FEW
         BH    ERR0060                        TOO MANY
         CLC   OPERAND_02_LENGTH,H8           MEMBER NAME TOO LONG?
         BH    ERR0070                        YES
         ICM   R2,15,OPERAND_02_ADDRESS
         MVC   SESS_SEARCH_ARG_LENGTH,OPERAND_02_LENGTH
         MVC   SESS_SEARCH_ARG(8),0(R2)
         MVC   DSPCREQ_MEMBER,0(R2)           COPY MEMBER NAME
         MVC   DSPCREQ_RECORD_NBR,DATASPACE_1_FIRST_RECORD
         B     MAIN0400
MAIN0390 DS    0H
         ITRACE ID=RFIND
         MVC   SPF_ZCMD,COMM_BLANKS           CLEAR THE COMMAND
         OC    SESS_SEARCH_ARG_LENGTH,SESS_SEARCH_ARG_LENGTH
         BZ    ERR0140
         MVC   DSPCREQ_MEMBER,SESS_SEARCH_ARG
         ICM   R1,15,SESS_SEARCH_RECORD
         LA    R1,1(,R1)
         STCM  R1,15,DSPCREQ_RECORD_NBR
         MVC   DSPCREQ_MEMBER,SESS_SEARCH_ARG
MAIN0400 DS    0H
         MVI   DSPCREQ_FUNC,$DSPCREQ_FIND_MEMBER_G
         LA    R1,DSPCREQ                     PARM BLOCK ADDRESS
         L     R15,COMM_OSDSPACE              OSDSPACE ENTRY POINT
         BALR  R14,R15                        FIND THE MEMBER
         CLI   DSPCREQ_RC,$DSPCREQ_OK
         BNE   ERR0020                        NOT FOUND
         MVC   SPF_ZCMD,COMM_BLANKS           CLEAR THE COMMAND
         MVC   DATASPACE_1_DISPLAY_FIRST_RECORD,DSPCREQ_RECORD_NBR
         MVC   SESS_SEARCH_RECORD,DSPCREQ_RECORD_NBR
         B     MAIN0000
* ------------------------------------------------------------------- *
*        Reset tag indicator (line command)                           *
* ------------------------------------------------------------------- *
MAIN0410 DS    0H
         ITRACE ID=LINE_RST
         MVI   VDATA_SELECTION,C' '           RESET THE SELECTION
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETRIEVE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE              OSDSPACE ENTRY POINT
         BALR  R14,R15                        RETRIEVE THE RECORD
         NI    DIR_FLAGS,255-$DIR_TAGGED
         MVI   DSPCREQ_FUNC,$DSPCREQ_UPDATE
         L     R15,COMM_OSDSPACE              OSDSPACE ENTRY POINT
         BALR  R14,R15                        UPDATE THE RECORD
         MVI   VDATA_ATTR_2,$SCREEN_ATTR_NORMAL
         MVC   VDATA_OVERLAY(RESET_MSG_L),RESET_MSG
         B     MAIN0230                       PROCESS NEXT SELECTION
* ------------------------------------------------------------------- *
*        Reset tag indicator (primary command)                        *
* ------------------------------------------------------------------- *
MAIN0420 DS    0H
         ITRACE ID=PRIM_RST
         BAL   R6,MAIN0660                    VERIFY THE MEMBER
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETRIEVE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE              OSDSPACE ENTRY POINT
         BALR  R14,R15                        RETRIEVE THE RECORD
         NI    DIR_FLAGS,255-$DIR_TAGGED
         MVI   DSPCREQ_FUNC,$DSPCREQ_UPDATE
         L     R15,COMM_OSDSPACE              OSDSPACE ENTRY POINT
         BALR  R14,R15                        UPDATE THE RECORD
         MVC   SPF_ZCMD,COMM_BLANKS           CLEAR COMMAND
         B     MAIN0000
* ------------------------------------------------------------------- *
*        Reset tag indicator (ALL)                                    *
* ------------------------------------------------------------------- *
MAIN0430 DS    0H
         ITRACE ID=RESETALL
         MVC   DSPCREQ_RECORD_NBR,DATASPACE_1_FIRST_RECORD
         SR    R2,R2                          CLEAR FOR COUNTER
MAIN0440 DS    0H
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETRIEVE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
         TM    DIR_FLAGS,$DIR_TAGGED          MEMBER TAGGED?
         BNO   MAIN0450                       NO
         A     R2,F1                          PLUS 1
         NI    DIR_FLAGS,255-$DIR_TAGGED
         MVI   DSPCREQ_FUNC,$DSPCREQ_UPDATE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
MAIN0450 DS    0H
         L     R1,DSPCREQ_RECORD_NBR          CURRENT RECORD NUMBER
         C     R1,DATASPACE_1_LAST_RECORD     AT LAST RECORD
         BE    MAIN0460                       YES
         A     R1,F1                          PLUS 1
         ST    R1,DSPCREQ_RECORD_NBR          SET UPDATED NUMBER
         B     MAIN0440
MAIN0460 DS    0H
         MVC   SPF_ZCMD,COMM_BLANKS           CLEAR COMMAND
         ST    R2,COMM_DWORD
         MVI   COMM_MSG_ID+1,12               SET MESSAGE NUMBER
         BAL   R2,MSG0000                     BUILD MESSAGE
         MVC   SPF_MSG_1,COMM_MSG_1
         B     MAIN0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0470 DS    0H
         ITRACE ID=LINE_SEL
         MVI   VDATA_SELECTION,C' '           RESET THE SELECTION
         B     MAIN0490
MAIN0480 DS    0H
         ITRACE ID=ZCMD_SEL
         CH    R3,H2                          PROPER NUMBER OF PARMS?
         BL    ERR0050                        TOO FEW
         BH    ERR0060                        TOO MANY
         CLC   OPERAND_02_LENGTH,H8           MEMBER NAME TOO LONG?
         BH    ERR0070                        YES
         ICM   R2,15,OPERAND_02_ADDRESS
         MVC   DSPCREQ_MEMBER,0(R2)           COPY MEMBER NAME
         MVI   DSPCREQ_FUNC,$DSPCREQ_FIND_MEMBER_S
         MVC   DSPCREQ_RECORD_NBR,DATASPACE_1_FIRST_RECORD
         LA    R1,DSPCREQ                     PARM BLOCK ADDRESS
         L     R15,COMM_OSDSPACE              OSDSPACE ENTRY POINT
         BALR  R14,R15                        FIND THE MEMBER
         CLI   DSPCREQ_RC,$DSPCREQ_OK
         BNE   ERR0020                        NOT FOUND
         MVC   VDATA_NAME,0(R2)
         MVC   SPF_ZCMD,COMM_BLANKS           RESET THE COMMAND
MAIN0490 DS    0H
         TM    DIR_FLAGS,$DIR_DELETED+$DIR_RENAMED
         BNZ   ERR0100
         ST    R7,DXD_LINES_REMAINING         SAVE LINES REMAINING
         ST    R8,DXD_CURRENT_VDATA           SAVE VDATA ADDRESS
         BAL   R6,SESS0000                    CREATE NEW SESSION
NEW      USING SESSION,R3
         MVC   NEW.SESS_MEMBER,VDATA_NAME     SET MEMBER NAME
         MVI   COMM_SESS_FUNC,$SESS_SWITCH    SET SESSION FUNCTION
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0500 DS    0H
         ITRACE ID=EDIT
         MVI   VDATA_SELECTION,C' '           RESET THE SELECTION
         TM    DIR_FLAGS,$DIR_DELETED+$DIR_RENAMED
         BNZ   ERR0100
         ST    R7,DXD_LINES_REMAINING         SAVE LINES REMAINING
         ST    R8,DXD_CURRENT_VDATA           SAVE VDATA ADDRESS
         BAL   R6,SESS0000                    CREATE NEW SESSION
NEW      USING SESSION,R3
         MVC   NEW.SESS_MEMBER,VDATA_NAME     SET MEMBER NAME
         MVI   NEW.SESS_EDIT_FLAGS,$SESS_EDIT INDICATE EDIT SESSION
         MVI   COMM_SESS_FUNC,$SESS_SWITCH    SET SESSION FUNCTION
         B     EXIT0000
* ------------------------------------------------------------------- *
*        Tag member (line command)                                    *
* ------------------------------------------------------------------- *
MAIN0510 DS    0H
         ITRACE ID=LINE_TAG
         MVI   VDATA_SELECTION,C' '           RESET THE SELECTION
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETRIEVE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE              OSDSPACE ENTRY POINT
         BALR  R14,R15                        RETRIEVE THE RECORD
         OI    DIR_FLAGS,$DIR_TAGGED
         MVI   DSPCREQ_FUNC,$DSPCREQ_UPDATE
         L     R15,COMM_OSDSPACE              OSDSPACE ENTRY POINT
         BALR  R14,R15                        UPDATE THE RECORD
         MVI   VDATA_ATTR_2,$SCREEN_ATTR_HIGH
         MVC   VDATA_OVERLAY(TAGGED_MSG_L),TAGGED_MSG
         B     MAIN0230                       PROCESS NEXT SELECTION
* ------------------------------------------------------------------- *
*        Tag member (primary command)                                 *
* ------------------------------------------------------------------- *
MAIN0520 DS    0H
         ITRACE ID=PRIMTAG
         BAL   R6,MAIN0660                    VERIFY MEMBER
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETRIEVE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE              OSDSPACE ENTRY POINT
         BALR  R14,R15                        RETRIEVE THE RECORD
         OI    DIR_FLAGS,$DIR_TAGGED
         MVI   DSPCREQ_FUNC,$DSPCREQ_UPDATE
         L     R15,COMM_OSDSPACE              OSDSPACE ENTRY POINT
         BALR  R14,R15                        UPDATE THE RECORD
         MVC   SPF_ZCMD,COMM_BLANKS           CLEAR THE COMMAND
         B     MAIN0150                       DISPLAY AGAIN
* ------------------------------------------------------------------- *
*        Delete (line command)                                        *
* ------------------------------------------------------------------- *
MAIN0600 DS    0H
         ITRACE ID=LINE_DEL,                                           +
               DATA1=VDATA_NAME
         MVC   SESS_MEMBER,VDATA_NAME         COPY MEMBER NAME
         MVC   SPF_MEMBER,VDATA_NAME          COPY TO SPF VARIABLE ALSO
         MVC   DXD_MEMBER,VDATA_NAME          COPY TO 24-BIT STORAGE
         MVC   SPF_ZCMD,COMM_BLANKS           CLEAR PRIMARY COMMAND
         MVI   VDATA_SELECTION,C' '           CLEAR LINE COMMAND
         TM    DIR_FLAGS,$DIR_DELETED+$DIR_RENAMED
         BNZ   ERR0100
         MVC   SESS_DISP_PANEL,PANEL_OS0006   SET PANEL ID
         L     R15,COMM_V_OSCNFRM
         BALR  R14,R15                        CONFIRM DELETION
         LTR   R15,R15                        ABORTED?
         BNZ   MAIN0610                       YES
         CLI   SPF_YES_OR_NO,C'N'             ABORTED?
         BE    MAIN0610                       YES
         LA    R3,DXD_MEMBER
         ITRACE ID=STOW,                                               +
               RDATA1=R4,                                              +
               RDATA2=R3
         STOW  (R4),(R3),D                    DELETE THE MEMBER
         ITRACE ID=STOW_RC,                                            +
               RDATA1=R15
         LTR   R15,R15                        SUCCESSFUL?
         BNZ   ERR0090                        NO
         MVI   DSPCREQ_FUNC,$DSPCREQ_RETRIEVE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE              OSDSPACE ENTRY POINT
         BALR  R14,R15                        RETRIEVE THE RECORD
         CLI   DSPCREQ_RC,$DSPCREQ_OK
         BNE   ERR0020
         OI    DIR_FLAGS,$DIR_DELETED
         MVI   DSPCREQ_FUNC,$DSPCREQ_UPDATE
         L     R15,COMM_OSDSPACE              OSDSPACE ENTRY POINT
         BALR  R14,R15                        UPDATE THE RECORD
         MVI   VDATA_ATTR_2,$SCREEN_ATTR_HIGH
         MVC   VDATA_OVERLAY(DELETED_MSG_L),DELETED_MSG
         B     MAIN0230
MAIN0610 DS    0H
         ITRACE ID=ABORT
         MVC   SPF_MSG_1(MSG_01_L),MSG_01
         B     MAIN0150
* ------------------------------------------------------------------- *
*        Delete (Primary command)                                     *
* ------------------------------------------------------------------- *
MAIN0620 DS    0H
         ITRACE ID=PRIM_DEL
         BAL   R6,MAIN0660                    VERIFY MEMBER
         MVC   SPF_ZCMD,COMM_BLANKS           CLEAR PRIMARY COMMAND
         MVC   SESS_DISP_PANEL,PANEL_OS0006   SET PANEL NAME
         L     R15,COMM_V_OSCNFRM
         BALR  R14,R15                        CONFIRM DELETE
         LTR   R15,R15                        ABORT
         BNZ   MAIN0610                       YES
         CLI   SPF_YES_OR_NO,C'N'             ABORT
         BE    MAIN0610                       YES
         LA    R3,DXD_MEMBER
         ITRACE ID=DELETE,                                             +
               DATA1=0(R4),                                            +
               DATA2=0(R3)
         STOW  (R4),(R3),D                    ISSUE STOW
         LTR   R15,R15
         BNZ   ERR0090
         OI    DIR_FLAGS,$DIR_DELETED
         LA    R1,DSPCREQ
         MVI   DSPCREQ_FUNC,$DSPCREQ_UPDATE
         L     R15,COMM_OSDSPACE              OSDSPACE ENTRY POINT
         BALR  R14,R15                        UPDATE THE RECORD
         MVC   SPF_MSG_1(MSG_02_L),MSG_02
         MVC   SPF_MSG_1+(MSG_02_MEMBER-MSG_02)(8),DXD_MEMBER
         B     MAIN0000
* ------------------------------------------------------------------- *
*        Disassemble (line command)                                   *
* ------------------------------------------------------------------- *
MAIN0630 DS    0H
         ITRACE ID=DISASM
         MVI   VDATA_SELECTION,C' '           RESET THE SELECTION
         TM    DIR_FLAGS,$DIR_DELETED+$DIR_RENAMED
         BNZ   ERR0100
         ST    R7,DXD_LINES_REMAINING         SAVE LINES REMAINING
         ST    R8,DXD_CURRENT_VDATA           SAVE VDATA ADDRESS
         MVC   DXD_MEMBER,VDATA_NAME
         B     MAIN0650
* ------------------------------------------------------------------- *
*        Disassemble (primary command)                                *
* ------------------------------------------------------------------- *
MAIN0640 DS    0H
         ITRACE ID=PRIM_ASM
         BAL   R6,MAIN0660                    VERIFY MEMBER
         MVC   SPF_ZCMD,COMM_BLANKS           CLEAR PRIMARY COMMAND
MAIN0650 DS    0H
         BAL   R6,SESS0000                    CREATE NEW SESSION
NEW      USING SESSION,R3
         MVC   NEW.SESS_MEMBER,DXD_MEMBER     SET MEMBER NAME
         MVC   NEW.SESS_DEFAULT_FORMAT,DISASM SET DEFAULT FORMAT
         MVC   NEW.SESS_FORMAT,DISASM         SET FORMAT
         MVC   NEW.SESS_DATA_LOADER,MODULE_OSDISASM
         MVI   COMM_SESS_FUNC,$SESS_SWITCH    SET SESSION FUNCTION
         GETMAIN RU,                                                   +
               LV=DIR_RECORD_L,                                        +
               LOC=ANY
         ST    R1,NEW.SESS_FUNCTION_DATA      PASS ADDR TO FUNCTION
         MVC   0(DIR_RECORD_L,R1),DIR_RECORD  COPY DATA
         B     EXIT0000
         DROP  NEW
* ------------------------------------------------------------------- *
*        Verify a member is valid and not renamed or deleted          *
* ------------------------------------------------------------------- *
MAIN0660 DS    0H
         ITRACE ID=PRIM_ASM
         CH    R3,H2                          CORRECT NUMBER OF PARMS?
         BL    ERR0050                        TOO FEW
         BH    ERR0060                        TOO MANY
         CLC   OPERAND_02_LENGTH,H8           OPERAND TOO LONG?
         BH    ERR0070                        YES
         ICM   R3,15,OPERAND_02_ADDRESS       MEMBER NAME ADDRESS
         MVC   SESS_MEMBER,0(R3)              COPY MEMBER NAME
         MVC   SPF_MEMBER,0(R3)
         MVC   DXD_MEMBER,0(R3)
         MVI   DSPCREQ_FUNC,$DSPCREQ_FIND_MEMBER_S
         MVC   DSPCREQ_MEMBER,DXD_MEMBER
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE              OSDSPACE ENTRY POINT
         BALR  R14,R15                        RETRIEVE THE RECORD
         CLI   DSPCREQ_RC,$DSPCREQ_OK         SUCCESSFUL?
         BNE   ERR0020                        NO
         TM    DIR_FLAGS,$DIR_DELETED+$DIR_RENAMED
         BNZ   ERR0100
         BR    R6
* ------------------------------------------------------------------- *
*        Display available commands                                   *
* ------------------------------------------------------------------- *
MAIN0690 DS    0H
         ITRACE ID=LISTCMDS
         MVI   VDATA_SELECTION,C' '           RESET COMMAND
         MVI   SESS_COMMAND_FLAGS,$SESS_COMMAND_DISPLAY
         LA    R0,LINE_COMMANDS
         ST    R0,COMM_MAIN_COMMAND_TABLE
         XC    COMM_SUB_COMMAND_TABLE,COMM_SUB_COMMAND_TABLE
         L     R15,COMM_V_OSCMD
         BALR  R14,R15                        DISPLAY COMMANDS
         B     MAIN0230
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN0700 DS    0H
         ITRACE ID=REFRESH
         MVI   DSPCREQ_FUNC,$DSPCREQ_CLEAR    CLEAR THE DATA SPACE
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                        'CLEAR' THE DATA SPACE
         MVI   LOADREQ_FUNC,$LOADREQ_LOAD
         MVC   LOADREQ_MODULE,MODULE_OSDESERV
         LA    R1,LOADREQ
         L     R15,COMM_V_OSLOAD
         BALR  R14,R15
         ICM   R15,15,LOADREQ_EP               OSDESERV ENTRY POINT
         BALR  R14,R15                         LINK TO OSDESERV
         ITRACE ID=DESERVRC,                                           +
               RDATA1=R15
         LTR   R15,R15                         SUCCESSFUL?
         BNZ   ERR0150                         NO
         OC    DATASPACE_1_LAST_RECORD,DATASPACE_1_LAST_RECORD
         BZ    ERR0160                         NO DATA NOW
         CLC   DATASPACE_1_DISPLAY_FIRST_RECORD,DATASPACE_1_LAST_RECORD
         BL    MAIN0000
         MVC   DATASPACE_1_DISPLAY_FIRST_RECORD,DATASPACE_1_LAST_RECORD
         B     MAIN0000                        BUILD DISPLAY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN2200 DS    0H
         ITRACE ID=SORT
         CH    R3,H2                           CORRECT NUMBER OF PARMS?
         BL    ERR0050                         TOO FEW
         BH    ERR0060                         TOO MANY
         ICM   R2,15,OPERAND_02_ADDRESS        2ND OPERAND'S ADDRESS
         CLC   NAME,0(R2)                      SORT BY NAME?
         BE    MAIN2210
         CLC   TTR,0(R2)                       SORT BY TTR?
         BE    MAIN2220
         MVI   COMM_MSG_ID+1,5
         B     ERR1000                         EXIT
MAIN2210 DS    0H
         ITRACE ID=BY_NAME
         MVI   DSPCREQ_FUNC,$DSPCREQ_SORT_NAME
         B     MAIN2270
MAIN2220 DS    0H
         ITRACE ID=BY_TTR
         MVI   DSPCREQ_FUNC,$DSPCREQ_SORT_TTR
         B     MAIN2270
MAIN2270 DS    0H
         ITRACE ID=SORTDSPC
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15                         SORT THE DATA SPACE
         MVC   SPF_ZCMD,COMM_BLANKS            CLEAR COMMAND
         B     MAIN0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN2300 DS    0H
         ITRACE ID=DSP_CMDS
         MVI   SESS_COMMAND_FLAGS,$SESS_COMMAND_DISPLAY
         LA    R0,COMMAND_TABLE
         ST    R0,COMM_MAIN_COMMAND_TABLE
         XC    COMM_SUB_COMMAND_TABLE,COMM_SUB_COMMAND_TABLE
         L     R15,COMM_V_OSCMD
         BALR  R14,R15                         DISPLAY COMMANDS
         B     MAIN0160
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAIN9000 DS    0H
         ITRACE ID=DUMPDATA
         MVC   DAIR_DD,OSDUMP_DD               SET DD NAME
         MVC   DAIR_SYSOUT,COMM_SYSOUT_CLASS   SET SYSOUT CLASS
         MVI   DAIR_FUNC,$DAIR_ALLOC           SET DAIR FUNCTION
         MVI   DAIR_OPTS,$DAIR_SYSOUT          TYPE OF DATA SET
         LA    R1,DAIRREQ                      OSDAIR PARM LIST
         L     R15,COMM_V_OSDAIR               OSDAIR ENTRY POINT
         BALR  R14,R15                         LINK TO OSDAIR
         OC    DAIR_R15,DAIR_R15               SUCCESSFUL?
         BNZ   ERR0020                         NO
         GETMAIN RU,                                                   +
               LV=DUMP_CONTROL_L,                                      +
               LOC=BELOW
         ITRACE ID=DUMPCNTL,                                           +
               RDATA1=R1
         LR    R6,R1
         USING DUMP_CONTROL,R6                 DEFINE BASE
         MVC   DUMP_DCB(DUMP_DCB_L),DUMP_DCB_I
         MVC   DUMP_OPEN(OPEN_L),OPEN_I
         MVC   DUMP_CLOSE(CLOSE_L),CLOSE_I
         LA    R2,DUMP_DCB                     DCB ADDRESS
         ST    R2,DSPCREQ_DCB                  PASS IT TO OSDSPACE
         ITRACE ID=OPENDUMP,                                           +
               RDATA1=R2
         OPEN  ((R2),OUTPUT),                                          +
               MODE=31,                                                +
               MF=(E,DUMP_OPEN)
         LA    R1,DUMP_IO_AREA                 I/O AREA ADDRESS
         ST    R1,DSPCREQ_RECORD_ADDR          PASS ADDRESS TO OSDSPACE
         MVI   DSPCREQ_FUNC,$DSPCREQ_DUMP
         LA    R1,DSPCREQ                      OSDSPACE PARMS
         L     R15,COMM_OSDSPACE               OSDSPACE ENTRY POINT
         ITRACE ID=DUMP,                                               +
               RDATA1=R1,                                              +
               RDATA2=R15
         BALR  R14,R15                         DUMP THE DATA SPACE
         ITRACE ID=CLS_DUMP,                                           +
               RDATA1=R2
         CLOSE ((R2)),                                                 +
               MODE=31,                                                +
               MF=(E,DUMP_CLOSE)
         FREEPOOL (R2)
         MVI   DAIR_FUNC,$DAIR_FREE
         LA    R1,DAIRREQ                      OSDAIR PARM LIST
         L     R15,COMM_V_OSDAIR               OSDAIR ENTRY POINT
         BALR  R14,R15                         LINK TO OSDAIR
         ITRACE ID=FREE_DMP,                                           +
               RDATA1=R6
         FREEMAIN RU,                                                  +
               A=(R6),                                                 +
               LV=DUMP_CONTROL_L
         MVI   COMM_MSG_ID+1,11                SET MESSAGE NUMBER
         BAL   R2,MSG0000                      LINK TO OSMSG
         MVC   SPF_MSG_1,COMM_MSG_1            COPY MESSAGE
         MVC   SPF_MSG_2,COMM_MSG_2            COPY MESSAGE
         MVC   SPF_MSG_3,COMM_MSG_3            COPY MESSAGE
         MVC   SPF_MSG_4,COMM_MSG_4            COPY MESSAGE
         MVC   SPF_MSG_5,COMM_MSG_5            COPY MESSAGE
         B     MAIN0150
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         DC    H'1',C'ERROR FROM OSDSPACE'
ERR0020  DS    0H
         ITRACE ID=DSPC_ERR
         MVC   SPF_MSG_1,COMM_MSG_1            COPY MESSAGE
         MVC   SPF_MSG_2,COMM_MSG_2            COPY MESSAGE
         MVC   SPF_MSG_3,COMM_MSG_3            COPY MESSAGE
         MVC   SPF_MSG_4,COMM_MSG_4            COPY MESSAGE
         MVC   SPF_MSG_5,COMM_MSG_5            COPY MESSAGE
         B     MAIN0150                        DISPLAY WITH MESSAGE
ERR0050  DS    0H
         MVI   COMM_MSG_ID+1,5
         B     ERR1000
ERR0060  DS    0H
         MVI   COMM_MSG_ID+1,6
         B     ERR1000
ERR0070  DS    0H
         MVI   COMM_MSG_ID+1,7
         B     ERR1000
ERR0080  DS    0H
         MVI   COMM_MSG_ID+1,8
         B     ERR1000
ERR0090  DS    0H
         MVI   COMM_MSG_ID+1,9
         ST    R15,COMM_DWORD
         B     ERR1000
ERR0100  DS    0H
         MVI   COMM_MSG_ID+1,10
         B     ERR1000
ERR0110  DS    0H
         DC    H'0'
ERR0120  DS    0H
         DC    H'0'
ERR0130  DS    0H
         CLI   SESS_FORMAT_FLAGS,$FORMAT_CLEANUP
         BE    EXIT0000
         DC    H'0'
ERR0140  DS    0H
         MVI   COMM_MSG_ID+1,13
         B     ERR1000
ERR0150  DS    0H
         MVI   COMM_MSG_ID+1,14
         B     ERR1000
ERR0160  DS    0H
         MVI   COMM_MSG_ID+1,15
ERR1000  DS    0H
         BAL   R2,MSG0000                     BUILD THE MESSAGE
         MVC   SPF_MSG_1,COMM_MSG_1           COPY MESSAGE
         MVC   SPF_MSG_2,COMM_MSG_2           COPY MESSAGE
         MVC   SPF_MSG_3,COMM_MSG_3           COPY MESSAGE
         MVC   SPF_MSG_4,COMM_MSG_4           COPY MESSAGE
         MVC   SPF_MSG_5,COMM_MSG_5           COPY MESSAGE
         B     MAIN0150                       EXIT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MSG0000  DS    0H
         MVC   COMM_MSG_CSECT,MODID           IDENTIFY CSECT
         MVI   COMM_MSG_ID,0                  FORCE 1ST BYTE TO ZERO
         L     R15,COMM_V_OSMSG               OSMSG ENTRY POINT
         BALR  R14,R15                        BUILD THE MESSAGE
         BR    R2
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
RTRY0000 DS    0H
         ITRACE ID=ABEND,                                              +
               DATA1=(SESS_SYSTEM_CODE,6)
         L     R15,COMM_V_OSABEND
         BALR  R14,R15                        LINK TO OSABEND
         NI    SESS_ABEND_FLAG,255-$ABENDED
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
CLEAN000 DS    0H
         ITRACE ID=CLEAN_UP
         ICM   R1,15,DXD_VDATA_ADDR
         BZ    CLEAN010
         L     R0,DXD_VDATA_LENGTH
         MVI   COMM_VDATA_FUNC,$VDATA_FREEMAIN
         L     R15,COMM_V_OSVDATA
         BALR  R14,R15
         XC    SESS_DISP_VDATA_ADDR,SESS_DISP_VDATA_ADDR
         XC    SESS_DISP_VDATA_LENGTH,SESS_DISP_VDATA_LENGTH
CLEAN010 DS    0H
         ICM   R4,15,SESS_DCB_ADDR            DCB PRESENT?
         BZ    CLEAN020                       NO
         ITRACE ID=CLOSE,                                              +
               RDATA1=R4,                                              +
               DATA2=SESS_DD
         CLOSE ((R4)),                        CLOSE DCB                +
               MODE=31,                                                +
               MF=(E,DXD_CLOSE)
         ITRACE ID=FREEPOOL,                                           +
               RDATA1=R4
         FREEPOOL (R4)                        FREE BUFFERS
         L     R0,SESS_DCB_LENGTH
         ITRACE ID=FREE_DCB,                                           +
               RDATA1=R4,                                              +
               RDATA2=R0
         FREEMAIN RU,                                                  +
               A=(R4),                                                 +
               LV=(0)
         XC    SESS_DCB_ADDR,SESS_DCB_ADDR
         XC    SESS_DCB_LENGTH,SESS_DCB_LENGTH
         NI    SESS_DCB_FLAGS,255-$SESS_DCB_OPEN
CLEAN020 DS    0H
         MVC   SESS_RETRY,DXD_SAVED_RETRY
         L     R0,DXD_SIZE                    WORK AREA SIZE
         LR    R1,R13                         COPY WORK AREA ADDRESS
         L     R13,4(R13)                     RESTORE R13
         FREEMAIN RU,                         FREE WORK AREA           +
               A=(1),                                                  +
               LV=(0)
         XC    SESS_DXD_ADDR,SESS_DXD_ADDR
         XC    SESS_DXD_LENGTH,SESS_DXD_LENGTH
         XC    COMM_MAIN_COMMAND_TABLE,COMM_MAIN_COMMAND_TABLE
         XC    COMM_SUB_COMMAND_TABLE,COMM_SUB_COMMAND_TABLE
         SR    R15,R15
         B     EXIT0010
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
SESS0000 DS    0H
         ITRACE ID=BLD_NEW
         GETMAIN RU,                                                   +
               LV=SESSION_L,                                           +
               LOC=ANY
         LR    R3,R1                            SAVE ADDRESS
NEW      USING SESSION,R3
         ST    R3,COMM_NEW_SESSION
         LR    R0,R3
         LA    R1,SESSION_L
         LR    R14,R11                          CURRENT SESSION ADDR
         LR    R15,R1                           COPY LENGTH
         MVCL  R0,R14                           COPY SESSION BLOCK
         L     R1,SESS_NEXT                     NEXT ON CHAIN
NEXT     USING SESSION,R1
         ST    R1,NEW.SESS_NEXT                 SET NEXT IN NEW BLOCK
         ST    R11,NEW.SESS_PREV                SET PREV IN NEW BLOCK
         ST    R3,SESS_NEXT                     SET NEXT IN CURRENT
         LTR   R1,R1                            'NEXT' BLOCK?
         BZ    SESS0010                         NO
         ST    R3,NEXT.SESS_PREV                SET PREV IN NEXT BLOCK
         DROP  NEXT
SESS0010 DS    0H
         MVI   NEW.SESS_FORMAT_FLAGS,0
         XC    NEW.SESS_DXD_ADDR,NEW.SESS_DXD_ADDR
         XC    NEW.SESS_DXD_LENGTH,NEW.SESS_DXD_LENGTH
         XC    NEW.SESS_LAST_FOUND,NEW.SESS_LAST_FOUND
         XC    NEW.SESS_SEARCH_ARG_LENGTH,NEW.SESS_SEARCH_ARG_LENGTH
         MVC   NEW.SESS_SEARCH_ARG,COMM_BLANKS
         MVC   NEW.SESS_DEFAULT_FORMAT,COMM_BLANKS
         MVC   NEW.SESS_FORMAT,COMM_BLANKS
         MVC   NEW.SESS_DATA_LOADER,COMM_BLANKS
         MVC   NEW.SESS_FUNCTION_MODULE,COMM_BLANKS
         MVC   NEW.SESS_EXIT_MODULE,COMM_BLANKS
         MVC   NEW.SESS_DSN,SESS_DSN
         MVC   NEW.SESS_PATH,SESS_PATH
         XC    NEW.SESS_DISP_VDATA_ADDR,NEW.SESS_DISP_VDATA_ADDR
         XC    NEW.SESS_DISP_VDATA_LENGTH,NEW.SESS_DISP_VDATA_ADDR
         XC    NEW.SESS_DCB_ADDR,NEW.SESS_DCB_ADDR
         XC    NEW.SESS_DCB_LENGTH,NEW.SESS_DCB_LENGTH
         MVC   NEW.SESS_DIALOG_SECTION_LENGTH,H8

         MVI   NEW.SESS_KEYWORD_FLAGS,$KEYWORD_DSN+$KEYWORD_MEMBER+$KEY+
               WORD_VOL
         L     R0,COMM_IO_SIZE                  I/O AREA SIZE
         GETMAIN RU,                                                   +
               LV=(0),                                                 +
               LOC=BELOW
         ST    R1,NEW.SESS_IO_AREA                  SAVE ADDRESS
         ITRACE ID=IO_AREA,                                            +
               RDATA1=R1
         MVC   NEW.DATASPACE_1_FIRST_RECORD,F1
         MVC   NEW.DATASPACE_1_DISPLAY_FIRST_RECORD,F1
         MVC   NEW.DATASPACE_1_DISPLAY_FIRST_COLUMN,F1
         XC    NEW.DATASPACE_1_ALET,NEW.DATASPACE_1_ALET
         XC    NEW.DATASPACE_1_TOKEN,NEW.DATASPACE_1_TOKEN
         MVC   NEW.DATASPACE_1_NAME,COMM_BLANKS
         DROP  NEW
         BR    R6
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
SESS0100 DS    0H
         ITRACE ID=NEWSESS,                                            +
               RDATA1=R7,                                              +
               RDATA2=R8
         ST    R7,DXD_LINES_REMAINING         SAVE LINES REMAINING
         ST    R8,DXD_CURRENT_VDATA           SAVE VDATA POSITION
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         LH    R15,DXD_RC                     RETURN CODE
         L     R13,4(,R13)                    RESTORE R13
EXIT0010 DS    0H
         L     R14,12(,R13)                   RESTORE R14
         LM    R0,R12,20(R13)                 RESTORE REGISTERS
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
MVC_01   MVC   0(0,R8),HEADING_01             COPY HEADING
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_SIZE       CXD
DXD_START      DC    Q(DXD006)
F1             DC    F'1'
F2             DC    F'2'
H1             DC    H'1'
H2             DC    H'2'
H6             DC    H'6'
H8             DC    H'8'

PDSDCB_I DCB   DSORG=PO,                                               +
               DDNAME=ANYDD,                                           +
               MACRF=W
PDSDCB_L       EQU   *-PDSDCB_I
DUMP_DCB_I     DCB DSORG=PS,                                           +
               DDNAME=OSDUMP,                                          +
               RECFM=FBA,                                              +
               LRECL=133,                                              +
               BLKSIZE=13300,                                          +
               MACRF=PM
DUMP_DCB_L     EQU      *-DUMP_DCB_I
OPEN_I         OPEN     (*,OUTPUT),                                    +
               MODE=31,                                                +
               MF=L
OPEN_L         EQU      *-OPEN_I
CLOSE_I        CLOSE    (*),                                           +
               MODE=31,                                                +
               MF=L
CLOSE_L        EQU      *-CLOSE_I

P1900000        DC    P'1900000'

EDIT_WORD_1     DC    X'4020202020202120'
EDIT_WORD_2     DC    X'F0212020204B202021'
EDIT_WORD_3     DC    X'40202020202020202120'

ALL             DC    C'ALL '
AM_ANY          DC    C'ANY'
AM_24           DC    C' 24'
AM_31           DC    C' 31'
AM_64           DC    C' 64'
RM_24           DC    C'24'
RM_31           DC    C'31'
DISASM          DC    C'DISASM  '
DUMPDATA        DC    C'DUMPDATA'
PANEL_OS0012    DC    CL8'OS0012'
PANEL_OS0006    DC    CL8'OS0006'
PANEL_LMOD      DC    CL8'OSLMOD'
FORMAT_LMOD     DC    CL8'LMOD'
MODULE_OSDESERV DC    CL8'OSDESERV'
MODULE_OSDISASM DC    CL8'OSDISASM'
NAME            DC    C'NAME'
RESET           DC    C'RESET'
OSDUMP_DD       DC    CL8'OSDUMP'
SIZE            DC    C'SIZE'
SORT            DC    C'SORT'
TTR             DC    C'TTR'
USER            DC    C'USER'

RESET_MSG       EQU   *
                DC    AL1($SCREEN_ATTR_HIGH)
                DC    CL26'<-------- Reset'
                DC    AL1($SCREEN_ATTR_NORMAL)
RESET_MSG_L     EQU   *-RESET_MSG

DELETED_MSG     EQU   *
                DC    AL1($SCREEN_ATTR_HIGH)
                DC    CL26'<-------- Deleted'
                DC    AL1($SCREEN_ATTR_NORMAL)
DELETED_MSG_L   EQU   *-DELETED_MSG

RENAMED_MSG     EQU   *
                DC    AL1($SCREEN_ATTR_HIGH)
                DC    CL26'<-------- Renamed'
                DC    AL1($SCREEN_ATTR_NORMAL)
RENAMED_MSG_L   EQU   *-RENAMED_MSG

TAGGED_MSG      EQU   *
                DC    AL1($SCREEN_ATTR_HIGH)
                DC    CL26'<-------- Tagged'
                DC    AL1($SCREEN_ATTR_NORMAL)
TAGGED_MSG_L    EQU   *-TAGGED_MSG

MSG_01          DC    C'Delete aborted'
MSG_01_L        EQU   *-MSG_01

MSG_02          DC    C'Member '
MSG_02_MEMBER   DC    CL8' '
                DC    C' deleted'
MSG_02_L        EQU   *-MSG_02

HEADING_01      DS    0C
                DC    AL1($SCREEN_ATTR_HIGH_YELLOW)
                DC    CL18' '
                DC    C'Name____'
                DC    C' '
                DC    C'_TTR__'
                DC    C' '
                DC    C'______Size________'
                DC    C' '
                DC    C'AC'
                DC    C' '
                DC    C'AM'
                DC    C' '
                DC    C'RM'
                DC    C' '
HEADING_01_L    EQU   *-HEADING_01

                LTORG

LINE_COMMANDS   DS    0C
                CMD   B,MAIN0470,'Browse'
                CMD   D,MAIN0600,'Delete'
                CMD   E,MAIN0500,'Edit'
                CMD   L,MAIN0370,'Display LMOD data'
                CMD   S,MAIN0470,'Select (browse)'
                CMD   T,MAIN0510,'Tag'
                CMD   V,MAIN0470,'View (browse)'
                CMD   X,MAIN0410,'Reset tag'
                CMD   Z,MAIN0630,'Disassemble'
                CMD   ?,MAIN0690,'Display available commands'
                DC    X'FF'

COMMAND_TABLE   DS    0C
                CMD   B,MAIN0480,'Browse member'
                CMD   D,MAIN0620,'Delete member'
                CMD   F,MAIN0380,'Find member'
                CMD   L,MAIN0380,'Locate member'
                CMD   S,MAIN0480,'Select (browse) member'
                CMD   T,MAIN0520,'Tag member'
                CMD   V,MAIN0480,'View (browse) member'
                CMD   X,MAIN0420,'Reset tag for member'
                CMD   Z,MAIN0640,'Disassemble a member'
                CMD   FI,MAIN0380,'Find member'
                CMD   FL,MAIN0520,'List available formats'
                CMD   DEL,MAIN0620,'DELete member'
                CMD   LOC,MAIN0380,'Locate member'
                CMD   REF,MAIN0000,'Refresh member list'
                CMD   TAG,MAIN0520,'Tag member'
                CMD   FIND,MAIN0380,'Find member'
                CMD   FLAG,MAIN0520,'Flag (tag) member'
                CMD   LIST,MAIN0520,'List available formats'
                CMD   VIEW,MAIN0480,'View (browse) member'
                CMD   FLIST,MAIN0520,'List available formats'
                CMD   RESET,MAIN0420,'Reset tag for member'
                CMD   RFIND,MAIN0390,'Repeat find'
                CMD   BROWSE,MAIN0480,'Browse member'
                CMD   DELETE,MAIN0620,'Delete member'
                CMD   DISASM,MAIN0640,'Disassemble a member'
                CMD   DUMPDATA,MAIN9000,'Dump data space'
                CMD   LOCATE,MAIN0380,'Locate member'
                CMD   REFRESH,MAIN0700,'Refresh member list'
                CMD   ??,MAIN2300,'Display list of available commands'
                DC    X'FF'

OSLMDIREND      EQU   *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD006              DSECT
                    COPY   DXDPREF

DXD_OPEN            DS     0F,(OPEN_L)X
DXD_CLOSE           DS     0F,(CLOSE_L)X
DXD_DCB             DS     0F,(PDSDCB_L)X

DXD_VDATA_ADDR      DS     A
DXD_VDATA_LENGTH    DS     F

DXD_CURRENT_VDATA   DS     A
DXD_LINES_REMAINING DS     F
DXD_CURSOR_POSITION DS     F

DXD_VWIDTH          DS     F                   DYNAMIC AREA WIDTH
DXD_VDEPTH          DS     F                   DYNAMIC AREA DEPTH

DXD_SAVED_RETRY     DS     A                   CALLERS RETRY POINT

DXD_RC              DS     H                   RETURN CODE
DXD_LINES_BUILT     DS     H                   LINES BUILT

DXD_MEMBER          DS     CL8                 SELECTED MEMBER

DXD_FLAGS           DS     X                   FLAGS/INDICATORS
$DXD_INIT           EQU    X'80'               .. DATA HAS BEEN BUILT
$LINE_CMD           EQU    X'40'               .. A MEMBER WAS SELECTED

                    COPY   DIR

                    DAIRREQ  DSECT=NO
                    DSPCREQ  DSECT=NO
                    LOADREQ  DSECT=NO
* ------------------------------------------------------------------- *
*                                                                    *
* ------------------------------------------------------------------- *
VDATA            DSECT
                 DS     XL15
VDATA_ATTR_1     DS     X
VDATA_SELECTION  DS     X
VDATA_ATTR_2     DS     X
                 DS     C
VDATA_NAME       DS     CL8
VDATA_ATTR_3     DS     X
VDATA_TTR        DS     XL6
VDATA_OVERLAY    DS    0CL(VDATA_OVERLAY_L)
VDATA_SIZE       DS     XL8
                 DS     C
VDATA_DELIM1     DS     C
VDATA_SIZE_HEX   DS     CL8
VDATA_DELIM2     DS     C
                 DS     X
VDATA_AC         DS     C
                 DS     X
VDATA_AM         DS     CL3
VDATA_SLASH      DS     X
VDATA_RM         DS     CL2
VDATA_OVERLAY_L  EQU    *-VDATA_SIZE
VDATA_L          EQU    *-VDATA
* ------------------------------------------------------------------- *
*                                                                    *
* ------------------------------------------------------------------- *
                 COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 SESSION  TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     OPERAND
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 BPXYSTAT DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DUMP_CONTROL     DSECT
DUMP_DCB         DS       0F,(DUMP_DCB_L)X
DUMP_OPEN        DS       0F,(OPEN_L)X
DUMP_CLOSE       DS       0F,(CLOSE_L)X
DUMP_IO_AREA     DS       CL133
DUMP_CONTROL_L   EQU      *-DUMP_CONTROL
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
COMMAND_DSECT    DSECT
COMMAND_LENGTH   DS       X
COMMAND_COMMAND  DS       CL8
COMMAND_ADDRESS  DS       AL4
COMMAND_L        EQU      *-COMMAND_DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 IBMMAC   DCBD=PO
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 OSSPFD   TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     ATTRS
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
                 COPY     TRENTRY
* ------------------------------------------------------------------- *
*                EQUATES                                              *
* ------------------------------------------------------------------- *
                 COPY     REGEQU
                 END      OSLMDIR
