* ------------------------------------------------------------------- *
*                                                                     *
*  Module name: LMODMAP                                               *
*                                                                     *
*  Obtain/store data for LMOD MAP (or LMOD MAP detail)                *
*                                                                     *
* ------------------------------------------------------------------- *
LMODMAP  CSECT
LMODMAP  AMODE 31
LMODMAP  RMODE ANY
         USING LMODMAP,R15
         USING OSCOMM,R12
         USING SESSION,R11
         B     INIT0000
MODID    DC    CL8'LMODMAP'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DC    A(MAPEND-LMODMAP)
INIT0000 DS    0H
         STM   R14,R12,12(R13)                SAVE REGISTERS
         LR    R10,R15                        COPY BASE ADDRESS
         LA    R9,2048(,R10)
         LA    R9,2048(,R9)
         USING LMODMAP,R10,R9
         DROP  R15
         L     R15,SESS_DXD_ADDR
         A     R15,DXD_START                  PLUS OFFSET
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15                        COPY DXD AREA ADDRESS
         USING DXDMAP,R13                     DEFINE WORK AREA BASE
         MVC   DXD_CSECT,MODID
         ITRACE ID=ENTRY
         L     R5,COMM_NEW_SESSION
NEW      USING SESSION,R5
         MVC   COMM_MSG_CSECT,MODID
         XC    COMM_MSG_ID,COMM_MSG_ID
         L     R6,COMM_OSSPFD
         USING OSSPFD,R6
         MVC   DXD_IEWBIND(IEWBIND_L),IEWBIND_I
         MVI   DXD_FLAGS,0
         LA    R1,NEW.DATASPACE_1
         ST    R1,DSPCREQ_DATASPACE
* ------------------------------------------------------------------- *
*        Obtain and initialize ESD buffer                             *
* ------------------------------------------------------------------- *
         ITRACE ID=GET_BUF
         IEWBUFF FUNC=GETBUF,                                          +
               TYPE=MAP
         ST    R8,DXD_MAP_BUFFER
         ITRACE ID=INIT_BUF,                                           +
               RDATA1=R8,                                              +
               RDATA2=R7
         IEWBUFF FUNC=INITBUF,                                         +
               TYPE=MAP
* ------------------------------------------------------------------- *
*        Store the info for the specified section                     *
* ------------------------------------------------------------------- *
         LA    R1,LMOD_MAP_DATA
         ST    R1,DSPCREQ_RECORD_ADDR
         LA    R1,LMOD_MAP_DATA_L
         ST    R1,DSPCREQ_RECORD_LENGTH
         XC    DSPCREQ_RECORD_NBR,DSPCREQ_RECORD_NBR
         MVI   DSPCREQ_FUNC,$DSPCREQ_STORE
         XC    DXD_MAP_CURSOR,DXD_MAP_CURSOR
* ------------------------------------------------------------------- *
*                                                                     *
*     Store the MAP info                                              *
*                                                                     *
*     R8 IS THE MAP HEADER (MAPH) BASE                                *
*     R7 IS THE ESD ENTRY  (MAP_ENTRY) BASE                           *
*                                                                     *
* ------------------------------------------------------------------- *
MAP0000  DS    0H
         ITRACE ID=GETD_MAP,                                           +
               DATA1=NEW.SESS_DIALOG_SECTION_LENGTH+0,                 +
               DATA2=NEW.SESS_DIALOG_SECTION_LENGTH+8
         L     R8,DXD_MAP_BUFFER
         LA    R7,MAPH_END
         IEWBIND FUNC=GETD,                                            +
               VERSION=5,                                              +
               WORKMOD=NEW.SESS_DIALOG_WORKMOD,                        +
               CLASS=B_MAP,                                            +
               SECTION=DUMMY_SECTION,                                  +
               AREA=IEWBMAP,                                           +
               CURSOR=DXD_MAP_CURSOR,                                  +
               COUNT=DXD_SECTIONS_RETURNED,                            +
               RETCODE=DXD_RETURN_CODE,                                +
               RSNCODE=DXD_REASON_CODE,                                +
               MF=(E,DXD_IEWBIND,COMPLETE)
         ITRACE ID=GETD_RC,                                            +
               DATA1=(DXD_RETURN_CODE,4),                              +
               DATA2=(DXD_REASON_CODE,4)
         CLC   DXD_RETURN_CODE,F8
         BH    ERR0010                        SERIOUS PROBLEM
         BE    ERR0030                        NO DATA
         CLC   DXD_RETURN_CODE,F4
         BL    MAP0010
         CLC   DXD_REASON_CODE,X_83000800
         BE    MAP0010
         CLC   DXD_REASON_CODE,X_83000801
         BE    ERR0030
MAP0010  DS    0H
         ICM   R4,15,DXD_SECTIONS_RETURNED
         BZ    ERR0020
         OI    DXD_FLAGS,$FLAGS_DISPLAYED
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
MAP0020  DS    0H
         ITRACE ID=BLD_SECT,                                           +
               RDATA1=R7,                                              +
               RDATA2=R4

         MVC   LMOD_MAP_NEXT,MAP_NEXT
         MVC   LMOD_MAP_TYPE,MAP_TYPE
         MVC   LMOD_MAP_FLAGS,MAP_FLAGS
         MVC   LMOD_MAP_NAME,COMM_BLANKS
         SR    R14,R14
         ICM   R14,3,MAP_NAME_CHARS
         BZ    MAP0040
         CH    R14,=Y(L'LMOD_MAP_NAME)
         BL    MAP0030
         LH    R14,=Y(L'LMOD_MAP_NAME)
MAP0030  DS    0H
         BCTR  R14,0
         ICM   R15,15,MAP_NAME_PTR
         EX    R14,NAME_MVC
MAP0040  DS    0H
         MVC   LMOD_MAP_OFFSET,MAP_OFFSET
         MVC   LMOD_MAP_QUANTITY,MAP_QUANTITY
         MVC   LMOD_MAP_NAME_SPACE,MAP_NAME_SPACE
         MVC   LMOD_MAP_RECFM,MAP_RECORD_FMT

         L     R1,DSPCREQ_RECORD_NBR
         LA    R1,1(,R1)
         ST    R1,DSPCREQ_RECORD_NBR
         ITRACE ID=STORE,                                              +
               RDATA1=R1
         LA    R1,DSPCREQ
         L     R15,COMM_OSDSPACE
         BALR  R14,R15
         CLI   DSPCREQ_RC,$DSPCREQ_FULL
         BE    MAP0050
         CLI   DSPCREQ_RC,$DSPCREQ_OK
         BNE   ERR0040

         A     R7,MAPH_ENTRY_LENG
         BCT   R4,MAP0020
         CLC   DXD_REASON_CODE,X_83000800
         BNE   MAP0000
         ITRACE ID=DONE
         B     MAP0060
MAP0050  DS    0H
         ITRACE ID=FULL
         L     R1,DSPCREQ_RECORD_NBR
         BCTR  R1,0
         ST    R1,DSPCREQ_RECORD_NBR
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
MAP0060  DS    0H
         MVC   DATASPACE_1_DISPLAY_FIRST_RECORD,F1
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
ERR0010  DS    0H
         ITRACE ID=ERR0010
         MVI   COMM_MSG_ID+1,1
         MVC   COMM_INFO_01(20),INFO_GET_ESD
         MVC   COMM_INFO_02,DXD_RETURN_CODE
         MVC   COMM_INFO_03,DXD_REASON_CODE
         B     ERR0100
ERR0020  DS    0H
         MVC   COMM_INFO_01,NEW.SESS_DIALOG_SECTION
         TM    DXD_FLAGS,$FLAGS_DISPLAYED
         BNO   ERR0030
         MVI   COMM_MSG_ID+1,2
         B     ERR0100
ERR0030  DS    0H
         MVC   COMM_INFO_01,NEW.SESS_DIALOG_SECTION
         MVI   COMM_MSG_ID+1,3
         B     ERR0100
ERR0040  DS    0H
         ITRACE ID=ERR0040
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
         DC    H'0'
         B     EXIT0000
ERR0100  DS    0H
         ITRACE ID=CALL_MSG,                                           +
               DATA1=(COMM_MSG_ID,2)
         MVC   COMM_MSG_CSECT,MODID
         L     R15,COMM_V_OSMSG
         BALR  R14,R15
         MVC   SPF_MSG_1,COMM_MSG_1
         MVC   SPF_MSG_2,COMM_MSG_2
         MVC   SPF_MSG_3,COMM_MSG_3
         MVC   SPF_MSG_4,COMM_MSG_4
         MVC   SPF_MSG_5,COMM_MSG_5
         MVI   COMM_SESS_FUNC,$SESS_REMOVE
         B     EXIT0000
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
NAME_MVC       MVC   LMOD_MAP_NAME(0),0(R15)
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
EXIT0000 DS    0H
         ITRACE ID=EXIT
         L     R13,4(,R13)                    RESTORE R14
         LM    R14,R12,12(R13)                RESTORE REGISTERS
         SR    R15,R15
         BR    R14                            RETURN
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         IEWBUFF FUNC=MAPBUF,                                          +
               TYPE=MAP,                                               +
               HEADREG=R8,                                             +
               ENTRYREG=R7,                                            +
               SIZE=50,                                                +
               VERSION=5

               IEWBIND MF=(L,IEWBIND_I)
IEWBIND_L      EQU     *-IEWBIND_I
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXD_START            DC   Q(DXDMAP)

F1                   DC   F'1'
F4                   DC   F'4'
F8                   DC   F'8'
B_MAP                DC   H'5',C'B_MAP'

DUMMY_SECTION        DC   H'8',X'00000001',C'    '

X_83000800           DC   X'83000800'
X_83000801           DC   X'83000801'
INFO_GET_ESD         DC   CL20'Get MAP info'

                     LTORG

MAPEND               EQU  *
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
DXDMAP                  DSECT
                        COPY  DXDPREF

DXD_IEWBIND             DS    0F,(IEWBIND_L)X

DXD_RETURN_CODE         DS    XL4            RETURN CODE
DXD_REASON_CODE         DS    XL4            REASON CODE

DXD_MAP_BUFFER          DS    A

DXD_SECTIONS_RETURNED   DS    F
DXD_MAP_CURSOR          DS    F

DXD_FLAGS               DS    X
$FLAGS_DISPLAYED        EQU   X'80'         SOME DATA DISPLAYED

                        COPY  MAPDATA
                        DSPCREQ  DSECT=NO

* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COMMON
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         SESSION
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         BPXYSTAT  DSECT=YES,LIST=YES
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         OSSPFD    TYPE=DSECT
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY      TRENTRY
* ------------------------------------------------------------------- *
*                                                                     *
* ------------------------------------------------------------------- *
         COPY      REGEQU
         END       LMODMAP
