 /*      SYS1.BRODCAST ANALYZER PROGRAM   (BRDANL)                    */
0/*  THIS PROGRAM MAY BE USED TO ANALYZE THE CONTENTS AND USE OF
 THE SYS1.BRODCAST DATASET.
 INPUT TO THE PROGRAM IS THE BRODCAST DATASET - DDNAME BRODCST;
 OUTPUT FROM THE PROGRAM IS PRINTED OUTPUT - DDNAME OUTPUT.

 THE PARM ON THE EXECUTE STATEMENT SPECIFIES WHAT OPTIONSARE TO BE TAKEN
 AS FOLLOWS

      PARAMETER   ABBREV       MEANING

      USERMSG     USERM     THE MESSAGES (MAIL) WHICH ARE STORED ON
                            BRODCAST ARE PRINTED BY USER.
  *   NOUSERMSG   NOUSERM   MAIL IS NOT TO BE PRINTED.
      USERTOTALS  USERT     THE NUMBER OF MESSAGES (MAIL) FOR EACH USER
                            ARE TO BE PRINTED (UNLESS ZERO).
  *   NOUSERTOT   NOUSERT   MESSAGE TOTALS ARE NOT TO BE PRINTED.
      USERINDEX   USERI     AN INDEX TO THE USERMSG AND/OR USERTOTALS
                            REPORT IS TO BE PRINTED.
  *   NOUSERIND   NOUSERI   NO INDEX IS TO BE PRINTED.
      ANALYSIS    ANAL      AN ANALYSIS BY RECORD TYPE IS TO BE DONE.
  *   NOANALYSIS  NOANAL    NO ANALYSIS BY RECORD TYPE IS TO BE DONE.
      DUMP        DUMP      A SEQUENTIAL (FORMATTED) DUMP OF BRODCST
                            IS TO BE PRINTED.
  *   NODUMP      NODUMP    NO DUMP IS TO BE PRINTED.
      CHAINS      CHAIN     A LISTING WHICH FOLLOWS THE USER MAIL
                            CHAINS IS TO BE PRODUCED.
      UNCHAIN               ONLY UNCHAINED RECORDS WILL BE SHOWN ON
                            CHAIN REPORT (ASSUMES CHAIN).
  *   NOCHAINS    NOCH      NO CHAIN LISTING IS TO BE PRODUCED.
      ALL         ALL       ALL OPTIONS ABOVE ARE TO BE TAKEN (EXCEPT
                            THOSE FOR WHICH 'NO' PARAMETERS APPEAR).

      MMSG=NNN              NNN = WARNING THRESHOLD FOR USER TOTALS.

      SELECT                UP TO 9 USERIDS ARE TO BE SELECTED FOR
                            PRINTING BASED ON A 'SYSIN' CARD.
                            THIS OPTION IS IN CONJUNCTION WITH
                            'USERMSG' AND/OR 'USERTOT'.
 (*  INDICATES DEFAULTS)
1PARAMETERS ARE SEPARATED FROM EACH OTHER BY ANY CHARACTER, OR MAY BE
 RUN TOGETHER AS ONE CONTIGUOUS STRING; THE PARAMETERS SHOULD BE
 PRECEDED BY A SLASH (/).

 EXAMPLE - PARM='/ALL,NODUMP,NOUSERT,NOCH' MEANS THE SAME AS
           PARM='/ALLNODUMPNOUSERTNOCH'    WHICH MEANS THE SAME AS
           PARM='/USERM.NOUSERT.USERI.ANAL'

 FUTURE PLANNED OPTIONS INCLUDE:
    1) AUTOMATIC DELETION OF UNCHAINED RECORDS,
    2) ANALYSIS BY THE "AGE" OF THE RECORDS; AND
    3) AUTOMATIC DELETION OF "OLD" RECORDS.
-THE PROGRAM ALLOWS FOR 3000 TOTAL RECORDS IN THE FILE, AND
                        1800 TOTAL USERS.                             */
1        BROD: PROC(EXEC_PARM) OPTIONS(MAIN);
         DEFAULT RANGE(*) STATIC; /*  MAKE ALL "STATIC" BY DEFAULT    */
-/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
 /*    FILE DECLARATIONS                                              */
 /* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
0   DCL BRODCST FILE ENV(F RECSIZE(129) BLKSIZE(129)
        REGIONAL(1)) KEYED;
    DCL OUTPUT FILE ENV(F RECSIZE(133) BLKSIZE(133));
0/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
 /*    SUBROUTINE DECLARATIONS                                        */
 /* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
0   DCL ATJ07S ENTRY OPTIONS(ASM);     /* TABLE SORT SUBROUTINE       */
0   DCL BRDCNV ENTRY(CHAR(4) VARYING) RETURNS(FIXED BINARY(31));
 /*     BRDCNV IS A FUNCTION TO CONVERT LESS THAN FULLWORD CHARACTER
        CONSTANTS TO BE FULLWORD BINARY CONSTANTS.                    */
0   DCL BRDREAD ENTRY(CHAR(130));
 /*     BRDREAD IS A SUBROUTINE TO READ SYS1.BRODCAST SEQUENTIALLY.   */
1/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
 /*    MISCELLANEOUS FIELD DECLARATIONS                               */
 /* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
0   DCL VERSION                   CHAR(3)   INIT('4.0');
0   DCL EXEC_PARM                 CHAR(100) VARYING;
    DCL OPTION_INFO(2)            CHAR(100) VARYING INIT('','');
0   DCL (DATE, TIME, PLIDUMP, ADDR, INDEX, SUBSTR, HBOUND, UNSPEC,
         LINENO, TRANSLATE, STRING)  BUILTIN;
0   DCL MSG_LIMIT                 FIXED DECIMAL(3)  INIT(50);
    DCL MMSG_FORMATTED            PIC'999';
    DCL COUNT_FIELD               FIXED DECIMAL  INIT(0);
    DCL COUNT_FIELD_FORMATED      PIC'9999';
    DCL RECORD_NUMBER             FIXED DECIMAL  INIT(0);
    DCL SAVE_USERID               CHAR(7);
    DCL  (KEYFLD, HOLDKEY)        PIC'(8)9';
    DCL  (USER_COUNT, DIR_COUNT)  FIXED BIN(15)  INIT(0);
    DCL  HOLD_TYPE                CHAR(1);
    DCL  TEMPHOLD                 FIXED BIN(15,0);
    DCL  LAST_MAIL                FIXED BIN(15,0)  INIT(0);
    DCL  UNCHAINED_SECTION        BIT(1) INIT('0'B);
0/*      REPORT OPTION & OTHER FLAGS:   (0 = NO, 1 = YES)             */
0   DCL USERMSG_FLAG BIT(1) INIT('0'B),        /* PRINT USER MSGS     */
        TOTALS_FLAG  BIT(1) INIT('0'B),        /* # OF MSGS PER USER  */
        INDEX_FLAG   BIT(1) INIT('0'B),        /* INDEX OF USERS      */
        ANALYZE_FLAG BIT(1) INIT('0'B),        /* BRODCAST ANALYSIS   */
        CHAIN_FLAG   BIT(1) INIT('0'B),        /* FOLLOW CHAINS       */
        DUMP_FLAG    BIT(1) INIT('0'B),        /* DUMP OF BRODCAST    */
        CTBL_FLAG    BIT(1) INIT('0'B),        /* NEEDS CHECK_TBL     */
        UNCHAIN_FLAG BIT(1) INIT('0'B),        /* UNCHAIN RPT ONLY    */
        SELECT_FLAG  BIT(1) INIT('0'B),        /* ONLY SELECTED USERS */
        SELECTED     BIT(1) INIT('1'B);        /* THIS IS ONE OF 'EM  */
1   DCL  SORT_ENTRY_LENGTH FIXED BIN(15,0)  INIT(8),
         SORT_ENTRY_NUMBER FIXED BIN(15,0)  INIT(0),
         SORT_KEY_LOC      FIXED BIN(15,0)  INIT(1),
         SORT_KEY_LENGTH   FIXED BIN(15,0)  INIT(4),
         SORT_SEQUENCE     CHAR(1)          INIT('A');
0   DCL LINES_PER_PAGE     FIXED BIN(15,0)  INIT(55);
    DCL LINES_IN_INDEX     FIXED BIN(15,0);
    DCL LINES_TO_SKIP      FIXED DEC(3)     INIT(1);
    DCL PAGE_NUMBER        FIXED DEC(3)     INIT(0);
    DCL RPT_NUMBER         FIXED BIN(15,0)  INIT(1);
0   DCL HEX00              BIT(8)           INIT('00000000'B),
        HEX01              BIT(8)           INIT('00000001'B),
        HEX02              BIT(8)           INIT('00000010'B),
        HEX03              BIT(8)           INIT('00000011'B),
        HEX04              BIT(8)           INIT('00000100'B),
        HEX88              BIT(8)           INIT('10001000'B),
        HEXFF              BIT(8)           INIT('11111111'B);
0   DCL (I, J, K, K1, IDIR, INDX, INDX_HOLD)
                           FIXED BIN(15,0);
0   DCL BDATE              PIC'99/99/99',
        BTIME              PIC'9999';
0   DCL SELECT_AREA        CHAR(72)         INIT(' ');
    DCL 1  SEL_USER_AREA   DEFINED(SELECT_AREA),
           2  SEL_USER(9)  CHAR(8);
1/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
 /*    ANALYSIS AND INDEX TABLE DECLARATIONS                          */
 /* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
0   DCL  1  CHECK_TABLE(0:3000) CONTROLLED,
            3  CHECK_TYPE  CHAR(1),
            3  CHECK_FWD   FIXED DEC(5,0),
            3  CHECK_BWD   FIXED DEC(5,0);
0/* USER_TABLE CONTAINS AN ENTRY FOR EACH USER THAT HAS MAIL.
    (NOTE THAT IF THIS TABLE CHANGES LENGTH, ETC. THE ATJ07S SORT
     PARAMETERS (SORT_ENTRY_LENGTH, ETC.) MUST REFLECT THE CHANGE.    */
    DCL  1  USER_TABLE(1800)  CONTROLLED,   /* ONE PER USER ID        */
            3  USER_ID     CHAR(4),
            3  USER_PTR    FIXED BIN(15),   /* RBA OF 1ST MAIL RCD    */
            3  USER_PAGE   FIXED BIN(15);   /* PAGE # OF 1ST MESSAGE  */
 /*  NOTE THAT THE TABLE ABOVE MUST HAVE 9 TIMES THE ENTRIES OF
     THE TABLE BELOW.                                                 */
0   DCL  1    MSGDIR_TABLE(200) CONTROLLED, /* ONE PER MAIL DIR. RCD  */
              3    MSGDIR_RBA              FIXED BIN(15),
              3    MSGDIR_ENTRY(9),
                   5    MSGDIR_USERID      CHAR(4),
                   5    MSGDIR_USER_RBA    FIXED BIN(15);
0   DCL  TYPE_COUNT(0:6)   FIXED DEC(4,0) INIT((7)(0));
0   DCL  DESCRIPTIONS(0:6)  CHAR(30)  INIT(
         'BROADCAST DIRECTORY ENTRY',
         'MAIL DIRECTORY ENTRY',
         'BROADCAST MESSAGE ENTRY',
         'USER MAIL ENTRY',
         'BROADCAST DATASET HEADER',
         'UNUSED MAIL ENTRY',
         'START OF UNCHAINED MAIL');
1/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
 /*    SYS1.BRODCAST RECORD DECLARATIONS                              */
 /* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
0/*    FIRST RECORD -- POINTS TO ALL CHAINS:                          */
    DCL  1  BCREC1   BASED(BC1POINT),
            3  R1BCFLGS BIT(8),     /* BRODCAST FLAGS (NOT USED)      */
            3  R1BCPTR  CHAR(3),    /* PTR TO 1ST BRODCAST DIR RCD    */
            3  R1USFLGS BIT(8),     /* USER MAIL FLAGS (NOT USED)     */
            3  R1USPTR  CHAR(3),    /* PTR TO 1ST USER MAIL DIR RCD   */
            3  R1RECNUM CHAR(4),    /* NUMBER OF RECORDS IN DATA      */
            3  R1BCMAX  CHAR(2),    /* MAXIMUM BRODCAST MSG NUMBER,
                                       FROM MASTER SCHED BASEA        */
            3  R1DSN    CHAR(24),   /* DATA SET NAME IN EBCDIC EQUAL
                                       ' SYS1.BRODCAST DATA SET'      */
            3  R1NOTUSD CHAR(87),   /* FUTURE USE                     */
            3  R1RES    CHAR(4);    /* RESERVED                       */
    DCL  BC1AREA        CHAR(129);
    DCL  BC1POINT       PTR;
    BC1POINT = ADDR(BC1AREA);
0/*      USER MAIL DIRECTORY RECORD:                                  */
    DCL  1  USDIR  BASED(USDPOINT),
            3  USDENTRY(9),         /* ENTRY FOR EACH VALID USERID    */
               5  USDID    CHAR(7), /* USERID CURRENTLY VALID         */
               5  USDRBA   CHAR(3), /* RELATIVE BLOCK ADDRESS OF MSG
                                       FOR THIS USERID, OR ZERO IF
                                       NO MESSAGE.                    */
               5  USDEND   CHAR(3), /* RBA OF LAST MSG FOR USER       */
            3  USDRES   CHAR(8),    /* RESERVED                       */
            3  USDREND  CHAR(1),    /* END OF RECORD INDICATOR, X'7F' */
            3  USDNEXT  CHAR(3);    /* PTR TO NEXT USER MAIL DIR RCD  */
    DCL  USDAREA        CHAR(129);
    DCL  USDPOINT       PTR;
    USDPOINT = ADDR(USDAREA);
0/*      USER MAIL MESSAGE RECORD   (ALSO USED FOR BRODCAST MSG)      */
    DCL  1  USMSG  BASED(USMPOINT),
            3  USMLNG   CHAR(1),    /* LENGTH OF USER MESSAGE         */
            3  USMTEXT  CHAR(125),  /* USER MESSAGE TEXT              */
            3  USMPTR   CHAR(3);    /* POINTER TO NEXT USER MESSAGE
                                       RECORD FOR THIS USERID, OR
                                       ZERO IF NO MORE RECORDS        */
    DCL  USMAREA        CHAR(129);
    DCL  USMPOINT       PTR;
    USMPOINT = ADDR(USMAREA);
1/*      BRODCAST MESSAGE DIRECTORY RECORD:                           */
    DCL  1  BCDIR  BASED(BCDPOINT),
            3  BCDENTRY(25),        /* ENTRY FOR EACH MESSAGE         */
               5  BCDMSGNO CHAR(2), /* MESSAGE NUMBER                 */
               5  BCRBA    CHAR(3), /* RELATIVE BLOCK ADDRESS OF MSG*/
            3  BCREND   CHAR(1),    /* END OF RECORD INDICATOR, X'7F' */
            3  BCDPTR   CHAR(3);    /* PTR TO NEXT USER MAIL DIR RCD  */
    DCL  BCDPOINT       PTR;
-/*      RECORD DESCRIPTION FOR SEQUENTIAL READ OF FILE               */
    DCL  1  SEQ_RECORD  BASED(SEQPTR),
            3  SEQ_KEY  BIT(8),     /* RECORD KEY                     */
            3  SEQ_BODY CHAR(129);
    DCL  SEQ_RECORD_130 CHAR(130);
    DCL  SEQPTR PTR;
         SEQPTR = ADDR(SEQ_RECORD_130);
    BCDPOINT = ADDR(SEQ_BODY);   /* BC DIR IS NOT READ RANDOMLY       */
1/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
 /*    OUTPUT AREA DECLARATIONS (FOR FILE "OUTPUT")                   */
 /* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
0   DCL  OPT_AREA                 CHAR(132);
0   DCL  1  OPTA_AREA  DEFINED(OPT_AREA),  /* ANALYSIS PRINT LINE     */
            3  FILLER1            CHAR(20),
            3  OPTA_FIRST         PIC'9999', /* 1ST NUMBER OF RANGE   */
            3  OPTA_THRU          CHAR(1),   /*     '-'               */
            3  OPTA_LAST          PIC'9999', /* LAST NUMBER OF RANGE
                                          (ALSO USED FOR TOTAL RCDS)  */
            3  FILLER2            CHAR(2),
            3  OPTA_DESCRIPTION   CHAR(98);   /*  TEXT                */
0   DCL  1  OPTC_AREA  DEFINED(OPT_AREA),  /* CHAIN PRINT LINE        */
            3  FILLER1            CHAR(3),
            3  OPTC_DIR_RBA       PIC'9999', /* DIRECTORY RBA         */
            3  OPTC_DIR_DASH      CHAR(4),
            3  OPTC_DOWN_LINE     CHAR(1),
            3  OPTC_USER_STUFF,
               5  OPTC_USER_DASH  CHAR(3),
               5  OPTC_RBAS(23),
                  7  OPTC_RBA     PIC'9999',
                  7  OPTC_RBA_DASH  CHAR(1),
               5  FILLER2         CHAR(1);
    DCL   1  OPTC_REDO  DEFINED(OPT_AREA),
             3  FILLER1           CHAR(2),
             3  OPTC_MAILTOP      CHAR(6),
             3  FILLER2           CHAR(3),
             3  FILLER3(23),
                5  FILLER6        CHAR(4),
                5  OPTC_END_CHAR  CHAR(1);
0   DCL  1  OPTD_AREA  DEFINED(OPT_AREA),  /* DETAILED PRINT LINE     */
            3  FILLER1            CHAR(1),
            3  OPTD_RNUM          PIC'9999', /* RBA OF RECORD         */
            3  FILLER2            CHAR(2),
            3  OPTD_TEXT          CHAR(125); /* MESSAGE TEXT          */
0   DCL  1  OPTI_AREA  DEFINED(OPT_AREA),  /* INDEX PRINT LINE        */
            3  FILLER1            CHAR(9),
            3  OPTI_ENTRIES(7),
               5  OPTI_ID         CHAR(5),     /* USER ID             */
               5  OPTI_SEP        CHAR(4),     /* SEPARATOR CHARACTER */
               5  OPTI_PAGE       PIC'999',
               5  FILLER2         CHAR(5);
0   DCL  1  OPT0_AREA  DEFINED(OPT_AREA),  /* DUMP RECORD 0 FORMAT    */
            3  OPT0_RTYPE         CHAR(1),   /* RECORD NUMBER         */
            3  FILLER1            CHAR(2),
            3  OPT0_RNUM          PIC'9999', /* RBA OF RECORD         */
            3  FILLER2            CHAR(2),
            3  OPT0_RCD_CHN       PIC'9999', /* RBA OF NEXT RECORD    */
            3  OPT0_RBAS(9),
               5  FILLER3         CHAR(3),
               5  OPT0_MSGNO      PIC'999',  /* MESSAGE NUMBER        */
               5  FILLER4         CHAR(1),
               5  OPT0_BCCHN      PIC'9999'; /* RBA OF MESSAGE        */
0   DCL  1  OPT1_AREA  DEFINED(OPT_AREA),   /* DUMP RECORD 1 FORMAT   */
            3  OPT1_RTYPE         CHAR(1),    /* RECORD TYPE - '1'    */
            3  FILLER1            CHAR(2),
            3  OPT1_RNUM          PIC'9999',  /* RBA                  */
            3  FILLER2            CHAR(2),
            3  OPT1_RCD_CHN       PIC'9999',  /* NEXT RBA             */
            3  OPT1_USERS(5),                 /* USER ID ENTRIES      */
               5  FILLER3         CHAR(3),
               5  OPT1_USER       CHAR(8),    /* USER ID              */
               5  OPT1_USCHN      PIC'9999',  /* FIRST MAIL RCD RBA   */
               5  OPT1_SLASH      CHAR(1),    /* "/"                  */
               5  OPT1_USEND      PIC'9999';  /* LAST MAIL RCD RBA    */
0   DCL  1  OPT2_AREA  DEFINED(OPT_AREA),    /* DUMP RECORD 2 FORMAT  */
            3  OPT2_RTYPE         CHAR(1),   /* RECORD TYPE - '2'     */
            3  FILLER1            CHAR(2),
            3  OPT2_RNUM          PIC'9999', /* RECORD NUMBER         */
            3  FILLER2            CHAR(2),
            3  OPT2_FLAG          CHAR(4),   /* NOT USED INDICATOR    */
            3  FILLER3            CHAR(2),
            3  OPT2_TEXT          CHAR(117);
0   DCL  1  OPT3_AREA   DEFINED(OPT_AREA),  /* DUMP RECORD 3 FORMAT   */
            3  OPT3_RTYPE         CHAR(1),    /* RECORD TYPE - '3'    */
            3  FILLER1            CHAR(2),
            3  OPT3_RNUM          PIC'9999',  /* RBA                  */
            3  FILLER2            CHAR(2),
            3  OPT3_RCD_CHN       PIC'9999',  /* NEXT RBA             */
            3  FILLER3            CHAR(2),
            3  OPT3_MESSAGE       CHAR(117);  /* MESSAGE TEXT         */
0   DCL  1  OPT4_AREA   DEFINED(OPT_AREA),  /* DUMP RECORD 3 FORMAT   */
            3  OPT4_RTYPE         CHAR(1),    /* RECORD TYPE - '4'    */
            3  FILLER1            CHAR(2),
            3  OPT4_RNUM          PIC'9999',  /* RBA                  */
            3  FILLER2            CHAR(8),
            3  OPT4_BC_CONSTANT   CHAR(4),    /* 'B->'                */
            3  OPT4_BCRBA         PIC'9999',  /* RBA OF 1ST BC DIR    */
            3  FILLER3            CHAR(2),
            3  OPT4_US_CONSTANT   CHAR(4),    /* 'U->'                */
            3  OPT4_USDRBA        PIC'9999',  /* RBA OF 1ST USER DIR  */
            3  FILLER4            CHAR(2),
            3  OPT4_NUM_CONSTANT  CHAR(6),    /* '#RCDS'              */
            3  OPT4_RECNUM        PIC'9999',  /* NUMBER OF RCDS IN DS */
            3  FILLER5            CHAR(2),
            3  OPT4_MAX_CONSTANT  CHAR(7),    /* 'MAX.BC'             */
            3  OPT4_BCMAX         PIC '9999', /* MAX # BRODCAST MSGS  */
            3  FILLER6            CHAR(2),
            3  OPT4_HDR_CONSTANT  CHAR(24);   /* HEADER CONSTANT      */
0   DCL  1  OPT9_AREA   DEFINED(OPT_AREA),  /* DUMP DELETD RCD FORMAT */
            3  FILLER1            CHAR(3),
            3  OPT9_RNUM          PIC'9999',  /* RBA                  */
            3  FILLER2            CHAR(2),
            3  OPT9_FLAG          CHAR(6),    /* DELETED INDICATOR    */
            3  FILLER             CHAR(6),
            3  OPT9_MESSAGE       CHAR(111);  /* MESSAGE TEXT         */
1   DCL PAGE_CONSTANT CHAR(28) INIT('S Y S 1 . B R O D C A S T');
0   DCL PAGE_HEADER1(4) CHAR(50)  VARYING;
    DCL PAGE_HEADER2(4) CHAR(100) VARYING;
0   PAGE_HEADER1(1) = 'U S E R   M E S S A G E   R E P O R T';
    PAGE_HEADER2(1) = '  RBA   TEXT/TOTALS';
0   PAGE_HEADER1(2) = 'R E C O R D   A N A L Y S I S';
    PAGE_HEADER2(2) = (22)' ' || 'RBA''S      USE';
0   PAGE_HEADER1(3) = 'F O R M A T T E D   D U M P';
    PAGE_HEADER2(3) = 'T  RBA   NRBA';
0   PAGE_HEADER1(4) = 'U S E R   M A I L   C H A I N S';
    PAGE_HEADER2(4) = '';
-   DCL DASH_LINE                 CHAR(132);
0   DASH_LINE = (11)' '  ||  '| |'  ||  (116)'-'  ||  '|';
0   DCL DOWN_CHARACTER            CHAR(1)   INIT('|');
1/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
 /*    ON CONDITIONS                                                  */
 /* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
0   ON ERROR BEGIN;
         ON ERROR BEGIN;
              CALL PLIDUMP('TFSHB');
              SIGNAL FINISH;
              END;
         PUT DATA(COUNT_FIELD, RECORD_NUMBER, USER_COUNT,
                  DIR_COUNT, KEYFLD, HOLDKEY);
         PUT DATA;
         SIGNAL FINISH;
         END;
0   ON ENDPAGE(OUTPUT) BEGIN;      /* END OF PAGE ROUTINE             */
         PAGE_NUMBER = PAGE_NUMBER + 1;
         PUT FILE(OUTPUT) EDIT(PAGE_CONSTANT, PAGE_HEADER1(RPT_NUMBER),
              BDATE, BTIME, 'PAGE ', PAGE_NUMBER)
           (PAGE,COL(10),A,A,COL(100),A,COL(110),A,
            COL(120),A,P'999');
         IF PAGE_HEADER2(RPT_NUMBER) ¬= ' ' THEN
              PUT FILE(OUTPUT) EDIT(PAGE_HEADER2(RPT_NUMBER))
                   (SKIP(2),A);
         PUT FILE(OUTPUT) SKIP(2);
         LINES_TO_SKIP = 1;
         END;
0   ON ENDFILE(SYSIN)  CALL ERRINPT;
    ON UNDEFINEDFILE(SYSIN) CALL ERRINPT;
0   ERRINPT: PROC;
      OPT_AREA =
        'SELECT CARD MISSING OR BLANKS - ''SELECT'' OPTION CANCELLED';
      LINES_TO_SKIP = 2;
      CALL WRITER;
      SELECT_FLAG = '0'B;
      END ERRINPT;
1/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
 /*    MAIN LINE PROGRAM                                              */
 /* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
0   CALL INIT;            /* INITIALIZATION -
                             OPEN FILES, CHECK EXEC PARAMETERS,
                             ALLOCATE & INITIALIZE TABLES, AND VERIFY
                             THAT DATASET USED IS SYS1.BRODCAST.      */
0   IF TOTALS_FLAG  |
       ANALYZE_FLAG |
       CHAIN_FLAG   THEN
         CALL READRAND;   /* READ BRODCAST RANDOMLY -
                             FOLLOW USER MAIL CHAINS; OPTIONALLY,
                             PRINT USER MESSAGES AND/OR TOTALS,
                             FILL IN TABLES, AND PRINT INDEX.         */
0   IF ANALYZE_FLAG |
       DUMP_FLAG    |
       CHAIN_FLAG THEN
         CALL READSEQ;    /* READ BRODCAST SEQUENTIALLY -
                             FILL IN TABLES; OPTIONALLY, PRINT DUMP.  */
0   IF ANALYZE_FLAG THEN
         CALL PRTANAL;    /* PRINT ANALYSIS REPORT.                   */
0   IF CHAIN_FLAG THEN
         CALL PRTCHAIN;   /* PRINT CHAIN REPORT.                      */
0   CLOSE FILE (OUTPUT);
    RETURN;
-
-/*    WRITER SUBROUTINE                                              */
0WRITER: PROC;
    PUT FILE(OUTPUT) EDIT(OPT_AREA) (SKIP(LINES_TO_SKIP),A);
    OPT_AREA = ' ';
    LINES_TO_SKIP = 1;
 END WRITER;
1/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
 /*    INIT:    INITIALIZATION                                        */
 /* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
0 INIT:  PROC;
0   OPEN FILE (BRODCST) INPUT DIRECT UNBUFFERED;
    OPEN FILE (OUTPUT)  PAGESIZE(LINES_PER_PAGE) LINESIZE(132)
         STREAM PRINT OUTPUT;
0   BDATE = DATE;
    BTIME = SUBSTR(TIME,1,4);
    OPT_AREA = 'SYS1.BRODCAST ANALYZER PROGRAM (BRDANL)'    ||
               ' - VERSION '  ||  VERSION;
    CALL WRITER;
    OPT_AREA = 'REPORT(S) AS OF '  ||
               BDATE  ||  ' AT '  ||  BTIME;
    LINES_TO_SKIP = 3;
    CALL WRITER;
    LINES_TO_SKIP = 2;
0/*  CHECK THE EXECUTE CARD PARMS.                                    */
0   IF EXEC_PARM = '' THEN DO;
         OPT_AREA = 'OPTION(S) SPECIFIED: NONE';
         CALL WRITER;
         END;
    ELSE DO;
         OPT_AREA = 'OPTION(S) SPECIFIED: ' || EXEC_PARM;
         CALL WRITER;
0        IF INDEX(EXEC_PARM,'ALL')             ¬= 0 THEN
              USERMSG_FLAG, TOTALS_FLAG, ANALYZE_FLAG, DUMP_FLAG,
              INDEX_FLAG, CHAIN_FLAG = '1'B;
         IF INDEX(EXEC_PARM,'NOUSERM')       ¬= 0 THEN
              USERMSG_FLAG = '0'B;
         ELSE IF INDEX(EXEC_PARM,'USERM')    ¬= 0 THEN
              USERMSG_FLAG = '1'B;
         IF INDEX(EXEC_PARM,'NOUSERT')       ¬= 0 THEN
              TOTALS_FLAG = '0'B;
         ELSE IF INDEX(EXEC_PARM,'USERT')    ¬= 0 THEN
              TOTALS_FLAG = '1'B;
         IF INDEX(EXEC_PARM,'NOUSERI')       ¬= 0 THEN
              INDEX_FLAG = '0'B;
         ELSE IF INDEX(EXEC_PARM,'USERI')    ¬= 0 THEN
              INDEX_FLAG = '1'B;
         IF INDEX(EXEC_PARM,'NOANAL')        ¬= 0 THEN
              ANALYZE_FLAG = '0'B;
         ELSE IF INDEX(EXEC_PARM,'ANAL')     ¬= 0 THEN
              ANALYZE_FLAG = '1'B;
         IF INDEX(EXEC_PARM,'NODUMP')        ¬= 0 THEN
              DUMP_FLAG = '0'B;
         ELSE IF INDEX(EXEC_PARM,'DUMP')     ¬= 0 THEN
              DUMP_FLAG = '1'B;
         IF INDEX(EXEC_PARM,'NOCH')          ¬= 0 THEN
              CHAIN_FLAG = '0'B;
         ELSE IF INDEX(EXEC_PARM,'CHAIN')    ¬= 0 THEN
              CHAIN_FLAG = '1'B;
         IF INDEX(EXEC_PARM,'UNCHAIN')       ¬= 0 THEN
              UNCHAIN_FLAG = '1'B;
         IF INDEX(EXEC_PARM,'SELECT')        ¬= 0 THEN
              SELECT_FLAG = '1'B;
0        I = INDEX(EXEC_PARM,'MMSG=');
         IF I ¬= 0 THEN
              MSG_LIMIT = SUBSTR(EXEC_PARM,I+5,3);
         END;
0   IF ¬ (USERMSG_FLAG | TOTALS_FLAG | DUMP_FLAG |
          CHAIN_FLAG | ANALYZE_FLAG) THEN DO;
         OPT_AREA = 'NO PROGRAM OUTPUT REQUESTED.';
         CALL WRITER;
         STOP;
         END;
0   IF USERMSG_FLAG THEN          /* 'USERMSG' ASSUMES 'USERTOT'      */
         TOTALS_FLAG = '1'B;
0   IF INDEX_FLAG & ¬ TOTALS_FLAG THEN
         INDEX_FLAG = '0'B;
0   IF SELECT_FLAG & ¬ TOTALS_FLAG THEN
         SELECT_FLAG = '0'B;
    IF ANALYZE_FLAG & ¬ CHAIN_FLAG THEN
         CHAIN_FLAG, UNCHAIN_FLAG = '1'B;
    IF USERMSG_FLAG THEN
         OPTION_INFO(1) = OPTION_INFO(1)  ||  'USERMSG ';
    ELSE
         OPTION_INFO(2) = OPTION_INFO(2)  ||  'NOUSERMSG ';
    IF TOTALS_FLAG THEN DO;
         OPTION_INFO(1) = OPTION_INFO(1)  ||  'USERTOTALS ';
         MMSG_FORMATTED = MSG_LIMIT;
         OPTION_INFO(1) = OPTION_INFO(1)  ||
                          'MMSG='  ||  MMSG_FORMATTED  || ' ';
         END;
    ELSE
         OPTION_INFO(2) = OPTION_INFO(2)  ||  'NOUSERTOT ';
    IF INDEX_FLAG THEN
         OPTION_INFO(1) = OPTION_INFO(1)  ||  'USERINDEX ';
    ELSE
         OPTION_INFO(2) = OPTION_INFO(2)  ||  'NOUSERINDEX ';
    IF SELECT_FLAG THEN
         OPTION_INFO(1) = OPTION_INFO(1)  ||  'SELECT ';
    IF ANALYZE_FLAG THEN
         OPTION_INFO(1) = OPTION_INFO(1)  ||  'ANALYSIS ';
    ELSE
         OPTION_INFO(2) = OPTION_INFO(2)  ||  'NOANALYSIS ';
    IF DUMP_FLAG THEN
         OPTION_INFO(1) = OPTION_INFO(1)  ||  'DUMP ';
    ELSE
         OPTION_INFO(2) = OPTION_INFO(2)  ||  'NODUMP ';
    IF UNCHAIN_FLAG THEN
         OPTION_INFO(1) = OPTION_INFO(1)  ||  'UNCHAIN ';
    IF CHAIN_FLAG THEN
         OPTION_INFO(1) = OPTION_INFO(1)  ||  'CHAINS ';
    ELSE
         OPTION_INFO(2) = OPTION_INFO(2)  ||  'NOCHAINS ';
    OPT_AREA = 'OPTIONS IN EFFECT: ' || OPTION_INFO(1);
    CALL WRITER;
    OPT_AREA = (19)' '  ||  OPTION_INFO(2);
    CALL WRITER;
0   IF SELECT_FLAG THEN
         OPEN FILE(SYSIN);
0   IF SELECT_FLAG THEN
         GET EDIT(SELECT_AREA) (A(80));
    IF SELECT_FLAG THEN DO;
        OPT_AREA = 'USERS TO BE SELECTED:  ' ||
                   SELECT_AREA;
        LINES_TO_SKIP = 2;
        CALL WRITER;
        END;
0   IF DUMP_FLAG | ANALYZE_FLAG | CHAIN_FLAG THEN DO;
         ALLOCATE CHECK_TABLE;
         CTBL_FLAG = '1'B;
         DO I = 0 TO HBOUND(CHECK_FWD,1);         /* CLEAR THE TABLE  */
              CHECK_TYPE(I) = '9';
              CHECK_FWD(I), CHECK_BWD(I) = 0;
              END;
         END;
    IF INDEX_FLAG THEN
         ALLOCATE USER_TABLE;
    IF CHAIN_FLAG THEN
         ALLOCATE MSGDIR_TABLE;
0   KEYFLD = 0;                     /* SET KEY TO THE FIRST RECORD    */
    READ FILE (BRODCST) INTO (BCREC1) KEY (KEYFLD);
    IF R1DSN ¬= ' SYS1.BRODCAST DATA SET ' THEN DO;
         LINES_TO_SKIP = 3;
         OPT_AREA = 'WARNING - DDNAME "BRODCST" IS NOT SYS1.BRODCAST.';
         CALL WRITER;
         OPT_AREA = '          PROGRAM WILL ATTEMPT TO DUMP DATASET.';
         CALL WRITER;
         DUMP_FLAG = '1'B;
         USERMSG_FLAG, TOTALS_FLAG, INDEX_FLAG, ANALYZE_FLAG,
              CHAIN_FLAG, UNCHAIN_FLAG = '0'B;
         END;
    IF CTBL_FLAG THEN
         CHECK_TYPE(0) = '4';      /* '4' SHOWS FIRST RECORD         */
    KEYFLD = BRDCNV(R1USPTR);       /* SET KEY TO 1ST MAIL DIR ENTRY  */
    IF ¬ USERMSG_FLAG THEN          /* CLEAR 2ND HEADER IF NO USER    */
         PAGE_HEADER2(1) = ' ';     /*   MESSAGES ARE TO BE PRINTED.  */
0   END INIT;
1/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
 /*    READRAND: PROCESS THE USER MAIL, FOLLOWING THE CHAINS.         */
 /*    PRINT THE 'USERMSG' AND/OR 'USERTOTALS' REPORT, IF REQUESTED.  */
 /* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
0READRAND:    PROC;
    IF TOTALS_FLAG THEN
         SIGNAL ENDPAGE(OUTPUT);
    DO WHILE (KEYFLD ¬= 0);
         READ FILE (BRODCST) INTO (USDIR) KEY (KEYFLD);
         IF CTBL_FLAG THEN
              CHECK_TYPE(KEYFLD) = '1'; /* '1' = MAIL DIRECTORY ENTRY */
         HOLDKEY = KEYFLD;              /* SAVE FOR LATER             */
0        IF CHAIN_FLAG THEN DO;
              DIR_COUNT = DIR_COUNT + 1;
              MSGDIR_RBA(DIR_COUNT) = KEYFLD;
              END;
0        DO INDX = 1 TO 9;
 /*      (THERE ARE 9 USER POINTERS IN EACH DIRECTORY ENTRY.)        */
              KEYFLD = BRDCNV(USDRBA(INDX)); /* KEY -> 1ST MAIL RCD */
              IF CHAIN_FLAG THEN DO;
                   MSGDIR_USERID(DIR_COUNT,INDX) = '*NU*';
                   MSGDIR_USER_RBA(DIR_COUNT,INDX) = KEYFLD;
                   END;
              IF UNSPEC(SUBSTR(USDID(INDX),1,1)) ¬= HEX00 THEN DO;
 /*           (IF THE USER ID = 0 THEN IT IS AN UNUSED ENTRY.)        */
                   IF CHAIN_FLAG THEN
                        MSGDIR_USERID(DIR_COUNT,INDX) = USDID(INDX);
                   IF KEYFLD ¬= 0 THEN DO;
 /*           (IF THE POINTER = 0 THEN THERE IS NO MAIL FOR THE USER) */
                        IF SELECT_FLAG THEN DO;
                             SELECTED = '0'B;
                             DO I = 1 TO 9 WHILE(SEL_USER(I) ¬= ' ');
                                  IF USDID(INDX) = SEL_USER(I) THEN
                                       SELECTED = '1'B;
                                  END;
                             END;
                        IF INDEX_FLAG & SELECTED THEN DO;
                             USER_COUNT = USER_COUNT + 1;
                             USER_ID(USER_COUNT) = USDID(INDX);
                             USER_PTR(USER_COUNT) = KEYFLD;
                             END;
                        IF CTBL_FLAG THEN
                             CHECK_BWD(KEYFLD) = HOLDKEY;
0                       IF USERMSG_FLAG & SELECTED THEN DO;
                             LINES_TO_SKIP = 2;
                             OPTD_TEXT = 'MESSAGES FOR USER ' ||
                                         USDID(INDX);
                             CALL WRITER;
                             IF INDEX_FLAG THEN
                                  USER_PAGE(USER_COUNT) = PAGE_NUMBER;
                             END;
                        SAVE_USERID = USDID(INDX);
                        END;
                   DO WHILE ((KEYFLD ¬= 0) & TOTALS_FLAG);
                        READ FILE (BRODCST) INTO (USMSG) KEY (KEYFLD);
                        IF USERMSG_FLAG & SELECTED THEN DO;
                             OPTD_RNUM = KEYFLD;
                             OPTD_TEXT = USMTEXT;
                             CALL WRITER;
                             END;
                        COUNT_FIELD = COUNT_FIELD + 1; /* COUNT MSGS  */
                        HOLDKEY = KEYFLD;
                        KEYFLD = BRDCNV(USMPTR);
                        END;
                   IF (COUNT_FIELD ¬= 0) & TOTALS_FLAG & SELECTED
                      THEN DO;
                        COUNT_FIELD_FORMATED = COUNT_FIELD;
                        OPTD_TEXT = 'USER ' || SAVE_USERID ||
                                    ' - ' || COUNT_FIELD_FORMATED ||
                                    ' MESSAGE(S)';
                        IF COUNT_FIELD > MSG_LIMIT THEN
                             SUBSTR(OPT_AREA,1,3) = '*->';
                        IF LINENO(OUTPUT) > (LINES_PER_PAGE - 4) THEN
                             SIGNAL ENDPAGE(OUTPUT);
                        CALL WRITER;
                        IF INDEX_FLAG & (¬ USERMSG_FLAG) THEN
                             USER_PAGE(USER_COUNT) = PAGE_NUMBER;
                        COUNT_FIELD = 0;
                        END;
                   END;
              END;
         KEYFLD = BRDCNV(USDNEXT);
         END;
0   CLOSE FILE(BRODCST);
1/*    PRINT AN INDEX (IF REQUESTED).                                 */
0   IF INDEX_FLAG THEN DO;
         SORT_ENTRY_NUMBER = USER_COUNT;
         CALL ATJ07S (USER_TABLE, SORT_ENTRY_LENGTH, SORT_ENTRY_NUMBER,
               SORT_KEY_LOC, SORT_KEY_LENGTH, SORT_SEQUENCE);
         PAGE_HEADER2(1) = (20)' ' || 'INDEX OF USERIDS ' ||
                           '   (USERID ... PAGE#)';
         LINES_IN_INDEX = LINES_PER_PAGE - 8;  /* ALLOW FOR HEADINGS  */
         DO I = 0 TO USER_COUNT BY (LINES_IN_INDEX * 7);
              SIGNAL ENDPAGE(OUTPUT);
              DO J = 1 TO LINES_IN_INDEX
                 WHILE((I + J) ¬> USER_COUNT);
                   DO K = 1 TO 7;
                        K1 = ((K - 1) * LINES_IN_INDEX) + I + J;
                        IF K1 ¬> USER_COUNT THEN DO;
                             OPTI_ID(K) = USER_ID(K1);
                             OPTI_SEP(K) = '...';
                             OPTI_PAGE(K) = USER_PAGE(K1);
                             END;
                        END;
                   CALL WRITER;
                   END;
              END;
         PUT FILE(OUTPUT) SKIP(3) DATA(USER_COUNT);
         END;
0   END READRAND;
1/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
 /*    READSEQ: PROCESS THE FILE SEQUENTIALLY FOR 'ANALYSIS' AND/OR   */
 /*    'DUMP' AND/OR 'CHAIN'; PRINT THE 'DUMP', IF REQUESTED.         */
 /* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
0READSEQ:     PROC;
0   IF DUMP_FLAG THEN DO;
         BC1POINT, USDPOINT, USMPOINT = ADDR(SEQ_BODY);
         RPT_NUMBER = 3;
         SIGNAL ENDPAGE(OUTPUT);
         END;
0   CALL BRDREAD(SEQ_RECORD_130);       /* READ THE FIRST RECORD      */
    COUNT_FIELD = 0;
0   DO WHILE (SEQ_KEY ¬= HEX88);
         IF ANALYZE_FLAG | CHAIN_FLAG THEN DO;
              IF SEQ_KEY = HEXFF THEN
                 CHECK_TYPE(COUNT_FIELD) = '5';    /* '5' DLTD ENTRY  */
              ELSE IF SEQ_KEY = HEX03 THEN
                   CHECK_TYPE(COUNT_FIELD) = '3';  /* '3' MAIL RCD    */
              ELSE IF SEQ_KEY = HEX00 THEN
                   CHECK_TYPE(COUNT_FIELD) = '0';  /* '0' BRCST DIR.  */
              ELSE IF SEQ_KEY = HEX02 & CHECK_TYPE(COUNT_FIELD) = '9'
                   THEN
                   CHECK_TYPE(COUNT_FIELD) = '2';   /* '2' BRCST MSG  */
0             /* RECORD FORWARD AND BACKWARD POINTERS                 */
              IF SEQ_KEY ¬= HEX04 THEN DO;
                   I, CHECK_FWD(COUNT_FIELD) = BRDCNV(BCDPTR);
                   IF SEQ_KEY ¬= HEXFF THEN
                        CHECK_BWD(I) = COUNT_FIELD;
                   END;
              END;
0        IF DUMP_FLAG THEN
              CALL PRTDUMP;
         CALL BRDREAD(SEQ_RECORD_130);  /* READ A RECORD SEQUENTIALLY */
         COUNT_FIELD = COUNT_FIELD + 1;
         END;
    LAST_MAIL = COUNT_FIELD - 1;
0   END READSEQ;
1/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
 /*    PRTDUMP: PRINT THE BRODCAST DUMP.                              */
 /* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
0   PRTDUMP:  PROC;
0/*   PRINT BRODCAST MESSAGE DIRECTORY RECORD                         */
0   IF SEQ_KEY = HEX00 THEN DO;
         OPT0_RTYPE = '0';
         OPT0_RNUM = COUNT_FIELD;
         OPT0_RCD_CHN = BRDCNV(BCDPTR);
         DO INDX = 1 TO 9;
              TEMPHOLD, OPT0_BCCHN(INDX) = BRDCNV(BCRBA(INDX));
              IF SUBSTR(UNSPEC(BCDMSGNO(INDX)),1,1) = '1'B THEN DO;
                   CHECK_TYPE(TEMPHOLD) = 'X';
                   UNSPEC(BCDMSGNO(INDX)) =
                        UNSPEC(BCDMSGNO(INDX)) & '0111111111111111'B;
                   END;
              OPT0_MSGNO(INDX) = BRDCNV(BCDMSGNO(INDX));
              END;
         LINES_TO_SKIP = 2;
         CALL WRITER;
         DO INDX = 10 TO 18;
              TEMPHOLD, OPT0_BCCHN(INDX-9) = BRDCNV(BCRBA(INDX));
              IF SUBSTR(UNSPEC(BCDMSGNO(INDX)),1,1) = '1'B THEN DO;
                   CHECK_TYPE(TEMPHOLD) = 'X';
                   UNSPEC(BCDMSGNO(INDX)) =
                        UNSPEC(BCDMSGNO(INDX)) & '0111111111111111'B;
                   END;
              OPT0_MSGNO(INDX-9) = BRDCNV(BCDMSGNO(INDX));
              END;
         CALL WRITER;
         DO INDX = 19 TO 25;
              TEMPHOLD, OPT0_BCCHN(INDX-18) = BRDCNV(BCRBA(INDX));
              IF SUBSTR(UNSPEC(BCDMSGNO(INDX)),1,1) = '1'B THEN DO;
                   CHECK_TYPE(TEMPHOLD) = 'X';
                   UNSPEC(BCDMSGNO(INDX)) =
                        UNSPEC(BCDMSGNO(INDX)) & '0111111111111111'B;
                   END;
              OPT0_MSGNO(INDX-18) = BRDCNV(BCDMSGNO(INDX));
              END;
         CALL WRITER;
         LINES_TO_SKIP = 2;
         END;
1/*   PRINT MAIL DIRECTORY RECORD                                     */
0   ELSE IF SEQ_KEY = HEX01 THEN DO;       /* MAIL DIRECTORY RECORD   */
         IF (LINES_PER_PAGE - LINENO(OUTPUT)) < 4 THEN
              SIGNAL ENDPAGE(OUTPUT);
         OPT1_RTYPE = '1';
         OPT1_RNUM = COUNT_FIELD;
         OPT1_RCD_CHN = BRDCNV(USDNEXT);
         DO INDX = 1 TO 5;
              IF UNSPEC(SUBSTR(USDID(INDX),1,1)) = HEX00 THEN
                   OPT1_USER(INDX) = '*NU*';
              ELSE
                   OPT1_USER(INDX) = TRANSLATE(USDID(INDX),' ','.');
              OPT1_USCHN(INDX) = BRDCNV(USDRBA(INDX));
              OPT1_USEND(INDX) = BRDCNV(USDEND(INDX));
              OPT1_SLASH(INDX) = '/';
              END;
         LINES_TO_SKIP = 2;
         CALL WRITER;
         DO INDX = 6 TO 9;
              IF UNSPEC(SUBSTR(USDID(INDX),1,1)) = HEX00 THEN
                   OPT1_USER(INDX-5) = '*NU*';
              ELSE
                   OPT1_USER(INDX-5) = TRANSLATE(USDID(INDX),' ','.');
              OPT1_USCHN(INDX-5) = BRDCNV(USDRBA(INDX));
              OPT1_USEND(INDX-5) = BRDCNV(USDEND(INDX));
              OPT1_SLASH(INDX-5) = '/';
              END;
         CALL WRITER;
         LINES_TO_SKIP = 2;
         END;
-/*   PRINT BRODCAST MESSAGE RECORD                                   */
0   ELSE IF SEQ_KEY = HEX02 THEN DO;
         IF CHECK_TYPE(COUNT_FIELD) = 'X' THEN DO;
              OPT2_FLAG = '*NU*';
              CHECK_TYPE(COUNT_FIELD) = '2';
              END;
         OPT2_RTYPE = '2';
         OPT2_RNUM = COUNT_FIELD;
         OPT2_TEXT = USMTEXT;
         CALL WRITER;
         END;
1/*   PRINT MAIL MESSAGE RECORD                                       */
0   ELSE IF SEQ_KEY = HEX03 THEN DO;
         OPT3_RTYPE = '3';
         OPT3_RNUM = COUNT_FIELD;
         OPT3_MESSAGE = USMTEXT;
         OPT3_RCD_CHN = BRDCNV(USMPTR);
         CALL WRITER;
         END;
-/*   PRINT DATASET HEADER RECORD                                     */
0   ELSE IF SEQ_KEY = HEX04 THEN DO;
         OPT4_RTYPE = '4';
         OPT4_RNUM = COUNT_FIELD;
         OPT4_BC_CONSTANT = 'B->';
         OPT4_BCRBA = BRDCNV(R1BCPTR);
         OPT4_US_CONSTANT = 'U->';
         OPT4_USDRBA = BRDCNV(R1USPTR);
         OPT4_NUM_CONSTANT = '#RCDS';
         OPT4_RECNUM = BRDCNV(R1RECNUM);
         OPT4_MAX_CONSTANT = 'MAX.BC';
         OPT4_BCMAX = BRDCNV(R1BCMAX);
         OPT4_HDR_CONSTANT = R1DSN;
         CALL WRITER;
         END;
- /*   PRINT UNUSED RECORD                                           */
0   ELSE IF SEQ_KEY = HEXFF THEN DO;
         OPT9_RNUM = COUNT_FIELD;
         OPT9_FLAG = '*NU*';
         OPT9_MESSAGE = USMTEXT;
         CALL WRITER;
         END;
- /*   PRINT UNKNOWN RECORD TYPE                        */
0   ELSE DO;
         OPT9_RNUM = COUNT_FIELD;
         OPT9_MESSAGE = '** UNKNOWN RECORD TYPE - RECORD FOLLOWS **';
         LINES_TO_SKIP = 2;
         CALL WRITER;
         OPT_AREA = SEQ_BODY;
         CALL WRITER;
         LINES_TO_SKIP = 2;
         END;
0   END PRTDUMP;
1/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
 /*    PRTANAL: PRINT THE BRODCAST 'ANALYSIS' REPORT.                 */
 /* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
0PRTANAL:     PROC;
 /* DO I = 1 TO LAST_MAIL + 1;
         PUT SKIP FILE(OUTPUT) DATA(CHECK_TABLE(I));
         END;  */
    INDX_HOLD, HOLD_TYPE = CHECK_TYPE(0);
    COUNT_FIELD = 0;                /* COUNT FIELD IS USED HERE TO
                                       HOLD STARTING RECORD OF SERIES.*/
    RPT_NUMBER = 2;
    SIGNAL ENDPAGE(OUTPUT);
    DO INDX = 0 TO (LAST_MAIL + 1);
         IF CHECK_BWD(INDX) = 0 & CHECK_TYPE(INDX) = '3' THEN
              CHECK_TYPE(INDX) = '6';  /* '6' SHOWS UNCHAINED RECORD */
0        IF CHECK_TYPE(INDX) ¬= HOLD_TYPE THEN DO;
              OPTA_DESCRIPTION = DESCRIPTIONS(INDX_HOLD);
              OPTA_FIRST = COUNT_FIELD;
              IF COUNT_FIELD ¬= (INDX-1) THEN DO;  /* IF > 1 RECORD   */
                   OPTA_THRU = '-';
                   OPTA_LAST = INDX-1;
                   END;
              CALL WRITER;
              OPT_AREA = ' ';
              INDX_HOLD, HOLD_TYPE = CHECK_TYPE(INDX);
              COUNT_FIELD = INDX;
              END;
         IF INDX_HOLD ¬> 6 THEN
              TYPE_COUNT(INDX_HOLD) = TYPE_COUNT(INDX_HOLD) + 1;
         END;
    LINES_TO_SKIP = 3;             /* TRIPLE SPACE                    */
    OPTA_DESCRIPTION = 'TOTAL NUMBER OF EACH RECORD TYPE:';
    CALL WRITER;
    LINES_TO_SKIP = 2;             /*  DOUBLE SPACE                   */
         IF (LINES_PER_PAGE - LINENO(OUTPUT)) < 10 THEN
              SIGNAL ENDPAGE(OUTPUT);
    DO INDX = 0 TO 6;
         OPTA_LAST = TYPE_COUNT(INDX);
         OPTA_DESCRIPTION = DESCRIPTIONS(INDX);
         CALL WRITER;
         END;
0   END PRTANAL;
1/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
 /*    PRTCHAIN: CREATE THE USER MESSAGE CHAIN REPORT                 */
 /* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
0PRTCHAIN:    PROC;
    RPT_NUMBER = 4;
    IF ¬ UNCHAIN_FLAG THEN DO INDX = 1 TO DIR_COUNT;
         SIGNAL ENDPAGE(OUTPUT);
         OPTC_DIR_RBA = MSGDIR_RBA(INDX);
         OPTC_DIR_DASH = '----';
         DOWN_CHARACTER = '|';
         DO IDIR = 1 TO 9;
              TEMPHOLD = MSGDIR_USER_RBA(INDX,IDIR);
              STRING(OPTC_USER_STUFF) = '(USER ' ||
                                        MSGDIR_USERID(INDX,IDIR) ||
                                        ')';
              OPTC_DOWN_LINE = '|';
              CALL MAILTOP;
              CALL WRITER;
              OPTC_USER_DASH = '---';
              IF IDIR = 9 THEN
                   DOWN_CHARACTER = ' ';
              CALL TRCHAIN;    /* PRINT OUT THE CHAINS              */
              IF IDIR ¬= 9 THEN DO;
                   OPTC_DOWN_LINE = '|';
                   CALL MAILTOP;
                   CALL WRITER;
                   END;
              END;
         END;
0/*  PROCESS UNCHAINED MAIL RECORDS.                                  */
    UNCHAINED_SECTION = '1'B;
    DOWN_CHARACTER = ' ';
    PAGE_HEADER2(4) = (11)' ' || 'UNCHAINED MAIL RECORDS:';
    SIGNAL ENDPAGE(OUTPUT);
    INDX = 1;
    DO WHILE (INDX < LAST_MAIL);
         IF ((CHECK_TYPE(INDX) = '6' |
              CHECK_TYPE(INDX) = '3') &
             (CHECK_BWD(INDX) = 0)) THEN DO;
              LINES_TO_SKIP = 2;
              TEMPHOLD = INDX;
              CALL TRCHAIN;
              END;
         INDX = INDX + 1;
         END;
1/*    PRINT THE CHAINS SUBROUTINE                                    */
0TRCHAIN:     PROC;
0   DCL  IPRT                FIXED BIN(15);
0   DO UNTIL(TEMPHOLD = 0);
         DO IPRT = 1 TO 23 WHILE(TEMPHOLD ¬= 0);
              OPTC_RBA_DASH(IPRT) = '-';
              OPTC_RBA(IPRT) = TEMPHOLD;
              TEMPHOLD = CHECK_FWD(TEMPHOLD);  /* GET NEXT MAIL RBA   */
              END;
         OPTC_END_CHAR(IPRT) = '|';
         IF OPTC_USER_DASH = '---' THEN
              OPTC_DOWN_LINE = '|';
         ELSE
              OPTC_DOWN_LINE = DOWN_CHARACTER;
         CALL MAILTOP;
         CALL WRITER;
         IF TEMPHOLD ¬= 0 THEN DO;
              OPT_AREA = DASH_LINE;
              OPTC_DOWN_LINE = DOWN_CHARACTER;
              CALL MAILTOP;
              CALL WRITER;
              OPTC_USER_DASH = ' |-';
              END;
         END;
0   END TRCHAIN;
1/*    "MAIL TOP" SUBROUTINE                                          */
0MAILTOP:     PROC;
0   DCL  ILINE               FIXED BIN(15);
0   IF LINENO(OUTPUT) > 8 THEN
         RETURN;
    IF UNCHAINED_SECTION THEN
         RETURN;
    ILINE = LINENO(OUTPUT);
    IF INDX = DIR_COUNT THEN DO;
         SELECT (ILINE);
              WHEN (4)  OPTC_MAILTOP = '  |';
              WHEN (5)  OPTC_MAILTOP = '  |';
              WHEN (6)  OPTC_MAILTOP = ' END ';
              OTHERWISE;
              END;
         END;
    ELSE DO;
         SELECT (ILINE);
              WHEN (4)  OPTC_MAILTOP = '  |';
              WHEN (5)  OPTC_MAILTOP = '  |';
              WHEN (6)  OPTC_MAILTOP = ' NEXT';
              WHEN (7)  OPTC_MAILTOP = ' PAGE';
              WHEN (8)  DO;
                   OPTC_MAILTOP = '(    )';
                   OPTC_DIR_RBA = MSGDIR_RBA(INDX+1);
                   END;
              OTHERWISE;
              END;
         END;
0   END MAILTOP;
0   END PRTCHAIN;
-END BROD;
