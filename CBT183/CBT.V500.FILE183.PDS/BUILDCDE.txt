//GILBERTL JOB (ACCT#),BUILDCDE,
// NOTIFY=&SYSUID,
// CLASS=A,MSGCLASS=H,COND=(0,NE)
//ASMH EXEC PGM=IEV90,PARM=(OBJECT,NODECK,NOESD,NORLD,NOXREF)
***********************************************************************
*                                                                     *
* MACRO NAME = BUILDCDE                                               *
*                                                                     *
* DESCRIPTIVE NAME = BUILD A MAJOR CDE TO IDENTIFY A STORAGE AREA     *
*                                                                     *
* FUNCTION = MAKES A STORAGE AREA EASIER TO LOCATE IN A               *
*            DUMP BY MAKING IT APPEAR AS A LOAD_MODULE.               *
*                                                                     *
* STATUS = R200                                                       *
*                                                                     *
* AUTHOR = GILBERT SAINT-FLOUR <carlos@gsf-soft.com>                  *
*                                                                     *
* ENVIRONMENT = SEE BELOW                                             *
*                                                                     *
*     AMODE = ANY                                                     *
*     SCP   = MVS/XA OR MVS/ESA                                       *
*     KEY   = USER                                                    *
*     MODE  = PROBLEM                                                 *
*     APF   = OFF                                                     *
*                                                                     *
* OPERATION = BUILDCDE USES THE "LOADER" FORM OF IDENTIFY TO CREATE   *
*             A MAJOR CDE AND CORRESPONDING XL, THEN ISSUES A LOAD    *
*             SVC TO CREATE AN LLE AND ASSOCIATE THE CDE WITH THE     *
*             CURRENT TCB.                                            *
*                                                                     *
* INVOCATION = SEE BELOW                                              *
*                                                                     *
*          (BEFORE)     GETMAIN RU,LV=20000                           *
*                                                                     *
*          (AFTER)      GETMAIN RU,LV=20000                           *
*                       BUILDCDE LENGTH=(0),ADDR=(1),EP=DYNAM         *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*        BUILDCDE MAY BE ISSUED MORE THAN ONCE, PROVIDED THE EP       *
*        KEY-WORD HAS A DIFFERENT VALUE EACH TIME.                    *
*                                                                     *
*        IF STORAGE WAS ALLOCATED IN SUB-POOL ZERO (SP=0 IS           *
*        SPECIFIED OR DEFAULTED TO IN THE GETMAIN MACRO),             *
*        THE DELETE MACRO MAY BE ISSUED AGAINST THE SPECIFIED EP      *
*        TO DELETE THE CDE AND FREE UP THE STORAGE.                   *
*                                                                     *
*        IF GETMAIN SPECIFIES A NON-ZERO SUBPOOL, THE DELETE MACRO    *
*        DELETES THE CDE BUT DOES NOT FREE UP THE STORAGE:  YOU       *
*        MUST ISSUE FREEMAIN YOURSELF.                                *
*                                                                     *
*        IN A MAJOR CDE CREATED BY THE "LOADER" FORM OF IDENTIFY,     *
*        THE "RENT" AND "REUS" ATTRIBUTES ARE OFF.  THIS MEANS        *
*        THAT YOU MAY NOT ISSUE LOAD AGAINST THE EP NAME (BUILDCDE    *
*        HAS ALREADY ISSUED LOAD), BUT YOU MAY ISSUE LINK, ATTACH     *
*        OR XCTL IF YOU WANT TO (ONCE ONLY).  YOU MAY ALSO ISSUE      *
*        THE STANDARD FORM OF IDENTIFY TO DEFINE SECONDARY ENTRY      *
*        POINTS WHICH HAVE THE "RENT" ATTRIBUTE.                      *
*                                                                     *
*        IF THIS MACRO IS USED BY AN AUTHORIZED PROGRAM, THEN         *
*        A S306 ABEND WILL OCCUR (THE CDE CREATED HAS ITS CDSYSLIB    *
*        BIT OFF).  LOOK FOR A SECOND VERSION OF THE MACRO THAT MAY   *
*        BE USED IN AN AUTHORIZED PROGRAM AT THE END OF THIS JOB.     *
*                                                                     *
*        THE IDENTIFY MACRO SUPPLIED IN SYS1.MACLIB SUPPORTS THE      *
*        "LOADER" FORM, BUT THE FORMAT OF THE PARAMETER LIST IS       *
*        NOT DOCUMENTED, AS FAR AS I KNOW.  THE RETURN CODES X'18'    *
*        OR X'1C' MAY BE RETURNED IN R15 WHEN THE PARAMETER LIST      *
*        IS INVALID.  THESE RETURN CODES ARE DOCUMENTED ALONG WITH    *
*        THE IDENTIFY MACRO.                                          *
*                                                                     *
*        WHEN THE LOAD-LIST IS EMPTY (I.E. NO LOAD HAS BEEN ISSUED    *
*        FOR THIS TCB YET), IDENTIFY FAILS WITH A RETURN CODE 12.     *
*        THIS CAN BE CIRCUMVENTED BY ISSUING THE FOLLOWING SEQUENCE:  *
*                                                                     *
*                       LOAD    EP=IEFBR14                            *
*                       GETMAIN RU,LV=20000                           *
*                       BUILDCDE LENGTH=(0),ADDR=(1),EP=DYNAM         *
*                       DELETE  EP=IEFBR14                            *
*                                                                     *
*        I'VE NOTICED THAT ON MVS SYSTEMS PRIOR TO MVS/ESA 4.3, THE   *
*        "LOADER" FORM OF IDENTIFY RETURNS THE ADDRESS OF THE CDE IT  *
*        CREATED IN REG1.  IN MVS/ESA 4.3 THIS IS NO LONGER TRUE.     *
*                                                                     *
***********************************************************************
         MACRO
&REF     BUILDCDE &LENGTH=,&ADDR=,&EP=
         LCLC   &EP8                   XF
&EP8     SETC  '&EP          '(1,8)
&REF     IHBINNRA &ADDR,&LENGTH        SET R0,R1
         ST    R1,0(,R1)               STORE ENTRY-POINT ADDRESS
         MVC   4(8,R1),=C'&EP8'        CDNAME
         XC    12(4,R1),12(R1)         CLEAR BYTES 12-15
         LA    R14,16                  LENGTH OF PSEUDO XL
         LA    R15,1                   NUMBER OF RELOCATION FACTORS
         STM   R14,R1,16(R1)           FORMAT BYTES 16-31
         MVI   24(R1),X'80'            END OF LIST
         MNOTE *,'       IDENTIFY MF=(E,(R1))'
         SLR   R0,R0                   LOADER-TYPE IDENTIFY
         SVC   41                      ISSUE IDENTIFY SVC
         LR    R14,R1                  PASS CDE ADDRESS
         MNOTE *,'       LOAD EP=&EP,DCB=0'
         LA    R0,=C'&EP8'             POINT TO EP NAME
         SLR   R1,R1                   NO DCB FOR LOAD
         SVC   8                       ISSUE LOAD SVC
         LR    R1,R0                   EP ADDRESS IN R1
         SLR   R0,R0
         ICM   R0,B'0111',25(R1)       LENGTH IN R0
         MEND
***********************************************************************
********* TEST PROGRAM ************************************************
***********************************************************************
TEMPNAME CSECT
TEMPNAME AMODE 31
         BALR  R3,0
         USING *,R3
         OPEN  (SNAPDD,OUTPUT)
*
         GETMAIN R,LV=3333
         BUILDCDE LENGTH=(0),ADDR=(1),EP=DYNAM/24
         LR    R4,R14
         LR    R5,R15
         LR    R6,R0
         LR    R7,R1
*
         GETMAIN RU,LV=5678,LOC=ANY
         BUILDCDE LENGTH=(0),ADDR=(1),EP=DYNAM/31
         LR    R8,R14
         LR    R9,R15
         LR    R10,R0
         LR    R11,R1
*
         SNAP  DCB=SNAPDD,SDATA=CB,PDATA=(REGS,JPA)
*
         MVC   0(8,R7),=X'0A23,0A03'   SVC 35 (WTO) AND SVC 3 (EXIT)
         LA    R1,WTOL                 POINT TO WTO PARM LIST
         LINK  EP=DYNAM/24             GO EXECUTE SVC35
*
         SNAP  DCB=SNAPDD,SDATA=CB     SHOW UPDATED CDE
*
         SVC   3                       EXIT
*
WTOL     WTO   'BUILDCDE - LINK WORKED!',ROUTCDE=11,MF=L
*
SNAPDD   DCB   DSORG=PS,MACRF=W,DDNAME=SNAPDD,                         X
               RECFM=VBA,LRECL=125,BLKSIZE=882
         YREGS
         END
//SYSLIB   DD DSN=SYS1.MACLIB,DISP=SHR
//SYSUT1   DD UNIT=VIO,SPACE=(CYL,1)
//SYSLIN   DD UNIT=VIO,SPACE=(TRK,1),DISP=(,PASS),DCB=BLKSIZE=3200
//SYSPRINT DD SYSOUT=*
//*
//GO      EXEC PGM=LOADER,PARM=NOPRINT
//SYSLIN   DD DSN=*.ASMH.SYSLIN,DISP=(OLD,PASS)
//SNAPDD   DD SYSOUT=*
//SYSUDUMP DD SYSOUT=*
***********************************************************************
********* AUTHORIZED VERSION OF THE BUILDCDE MACRO ********************
***********************************************************************
         MACRO (AUTHORIZED VERSION)
&REF     BUILDCDE &RU,&LENGTH=,&SP=0,&WORKREG=,                        X
               &BNDRY=DBLWD,&EP=BUILDCDE,&LOC=
      MNOTE *,'       GETMAIN &RU,LV=&LENGTH,SP=&SP,BNDRY=&BNDRY,&LOC='
&REF     GETMAIN &RU,LV=&LENGTH,SP=&SP,BNDRY=&BNDRY,&LOC=
         ST    R1,0(,R1)               ENTRY POINT
         MVC   4(8,R1),=CL8'&EP'       CDNAME
         XC    12(4,R1),12(R1)         CLEAR BYTES 12-15
         LA    R14,016                 LENGTH OF PSEUDO XL
         LA    R15,001                 NUMBER OF RELOCATION FACTORS
         STM   R14,R1,16(R1)           FORMAT SVC41 PARM
         MVI   24(R1),X'80'            END OF LIST
         LR    R14,R1                  SAVE ADDRESS ACROSS SVC 41
         MNOTE *,'       IDENTIFY EPLOC=0,ENTRY=(R1)'
         SLR   R0,R0                   LOADER-TYPE IDENTIFY
         SVC   41                      ISSUE IDENTIFY SVC
&LABEL   SETC  'IHB&SYSNDX'
&R       SETC  '&WORKREG(1)'           SHORT FORM
         AIF   (T'&WORKREG NE 'O').WKREG2
         MNOTE 4,'WORKREG OPERAND OMITTED, WORKREG=(R2) ASSUMED'
&R       SETC  'R2'
.WKREG2  L     &R,540(,0)              PSATOLD (MY TCB)
         L     &R,TCBJSCB-TCB(,&R)     MY JSCB
         USING IEZJSCB,&R
         TM    JSCBOPTS,JSCBAUTH       CHECK AUTHORIZATION
         BZ    &LABEL.N                JUMP IF NOT AUTHORIZED
         MODESET KEY=ZERO              SWITCH TO KEY ZERO
         NI    JSCBOPTS,255-JSCBAUTH   TURN APF FLAG OFF
         SYNCH &LABEL.L                EXECUTE ROUTINE WITH TCB KEY
         OI    JSCBOPTS,JSCBAUTH       TURN APF FLAG BACK ON
         DROP  &R
         LR    &R,R0                   SAVE EP ADDRESS
         MODESET KEY=NZERO             SWITCH TO TCB KEY
         LR    R0,&R                   EP ADDRESS
         B     &LABEL.X
&LABEL.I EQU   &R-2                    WORKREG MUST BE R2-R13
&LABEL.J EQU   13-&R                   WORKREG MUST BE R2-R13
&LABEL.N LA    R14,&LABEL.X            RETURN ADDRESS
         MNOTE *,'       LOAD EP=&EP,DCB=0'
&LABEL.L LA    R0,=CL8'&EP'            POINT TO EP NAME
         SLR   R1,R1                   NO DCB FOR LOAD
         SVC   8                       ISSUE LOAD SVC
         BR    R14
&LABEL.X LR    R1,R0                   ADDRESS IN R1
         SLR   R0,R0
         ICM   R0,B'0111',25(R1)       LENGTH IN R0
         MEND
         IEZJSCB
         IKJTCB
