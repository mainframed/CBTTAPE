/*************************************************************** REXX */
/*                                                                    */
/* Module Name: WP                                                    */
/*                                                                    */
/* Descriptive Name: Invoke ISPF Workplace                            */
/*                                                                    */
/* Status: R100                                                       */
/*                                                                    */
/* Function: This REXX program allows you to invoke the               */
/*           Workplace from the Data Set List panel.                  */
/*                                                                    */
/* Author: Gilbert Saint-Flour <carlos@gsf-soft.com>                  */
/*                                                                    */
/* Environment: ISPF/PDF V4.2                                         */
/*                                                                    */
/* Operation: see below                                               */
/*                                                                    */
/*         Enter WP as a line command on the DSLIST panel             */
/*         to enter the ISPF Workplace and process                    */
/*         the corresponding data set.                                */
/*                                                                    */
/* Change Activity:                                                   */
/*                                                                    */
/**********************************************************************/
ADDRESS 'ISPEXEC'
"CONTROL ERRORS RETURN"
"ISREDIT MACRO (zwrkdsn)"             /* invoked from EDIT/VIEW?      */
IF rc>0 THEN ARG zwrkdsn              /* no, 1st arg must be dsn      */
IF zwrkdsn='' THEN EXIT 4             /* no dsn, quit                 */
Zwrkvol=''                            /* blank out volume serial      */
Zreftype='D'                          /* Data Set View                */
"VGET (ZDLDSN ZDLVOL ZDLCAT)"         /* get catalog status, volser   */
IF rc=0 THEN
  IF zdlcat>0 THEN                    /* uncatalogued data set?       */
    Zwrkvol=zdlvol                    /* pass actual volume serial    */
"VPUT (Zwrkdsn Zwrkvol Zreftype) PROFILE"  /* update ISRWORK vars     */
"SELECT PGM(ISRUDA) PARM(ISRWORK) SCRNAME(WORK)"
IF zerrmsg\='' THEN "SETMSG MSG("zerrmsg")"
