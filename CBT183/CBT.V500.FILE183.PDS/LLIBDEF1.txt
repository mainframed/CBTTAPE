//GILBERTL JOB (ACCT#),LLIBDEF1,
// NOTIFY=&SYSUID,
// CLASS=A,MSGCLASS=H,COND=(0,NE)
//*
//ASMH EXEC PGM=IEV90,PARM=(OBJECT,NODECK,NOESD,NORLD,NOXREF)
***********************************************************************
*                                                                     *
*        PRELOAD A PROGRAM FROM ISPLLIB LIBDEF LIBRARY                *
*                                                                     *
*        THIS PROGRAM ALLOWS YOU TO PRE-LOAD A REUS OR                *
*        RENT MODULE FROM A LOAD LIBRARY DEFINED VIA                  *
*        LIBDEF.  THIS CAN PREVENT S806-4 ABENDS THAT                 *
*        OCCUR WHEN A PROGRAM THAT IS NOT LIBDEF-AWARE                *
*        TRIES TO ACCESS ANOTHER PROGRAM VIA LOAD OR LINK.            *
*                                                                     *
*        EXAMPLE:                                                     *
*                                                                     *
*        /*           DEFINE ALTERNATE LOADLIB                 */     *
*        "ISPEXEC LIBDEF ISPLLIB DATASET ID('MYAPPL.LOADLIB')"        *
*        /*           PRE-LOAD THE TABLE                       */     *
*        "ISPEXEC SELECT PGM(LLIBDEF1) PARM(MYTABLE)"                 *
*        /*           INVOKE THE APPLICATION'S MAIN MENU       */     *
*        "ISPEXEC SELECT PANEL(MYPANEL)"                              *
*                                                                     *
***********************************************************************
LLIBDEF1 START 0
         SAVE  (14,12),,'GSF UTILITIES - LLIBDEF1 R100'
         LR    R12,R15
         USING LLIBDEF1,R12
         ICM   R2,B'1111',0(R1)        GET PARM ADDR, CHECK X'80' BIT
         BP    QUIT12                  QUIT IF INVOKED AS A CP
*----------------------------------------------------------------------
*
*        MOVE PROGRAM NAME TO BLDL LIST
*
*----------------------------------------------------------------------
         LA    R14,2(,R2)              ADDR OF MODULE IN PARM
         LH    R15,0(,R2)              PARM LENGTH
         ICM   R15,B'1000',=C' '       PADDING (X'40')
         LA    R0,PDS2NAME             TARGET AREA
         LA    R1,L'PDS2NAME           TARGET AREA
         MVCL  R0,R14                  MOVE PGM NAME
*----------------------------------------------------------------------
*
*        RETRIEVE DCB ADDRESS FROM LINK PARM LIST
*
*----------------------------------------------------------------------
         L     R1,540(,0)              PSATOLD
         L     R2,00(,R1)              TCBRBP
         L     R3,RBGRS15-RBBASIC(,R2) SVC 6 PARM LIST
         LM    R5,R6,0(R3)             PGM NAME/DEFLLIB DCB
*----------------------------------------------------------------------
*
*        LOCATE PROGRAM USING BLDL MACRO AND LOAD IT
*
*----------------------------------------------------------------------
         BLDL  (R6),BLDL_HDR           LOCATE THE PROGRAM IN TASKLIB
         LTR   R15,R15                 CHECK RETURN CODE
         BNZ   QUIT16                  QUIT IF PGM NOT FOUND
         LOAD  DE=PDS2NAME,            LOAD THE MODULE                 X
               DCB=(R6),               FROM LIBDEF DCB                 X
               LSEARCH=YES,            LIMIT SEARCH                    X
               ERRET=QUIT24            PREVENT TASK ABEND
         RETURN (14,12),RC=00
QUIT12   RETURN (14,12),RC=12          PROGRAM INVOKED AS A COMMNAD
QUIT16   RETURN (14,12),RC=16          BLDL FAILED
QUIT24   RETURN (14,12),RC=24          LOAD FAILED
*----------------------------------------------------------------------
BLDL_HDR DC    0F'0',H'1,80'           BLDL HDR
         IHAPDS PDSBLDL=YES,DSECT=NO
RBBASIC  DSECT                         IKJRB/IHARB
RBSTAB2  EQU   *+X'0B',1,C'B'          STATUS BYTE
RBCDE    EQU   *+X'0C',4,C'A'          PTR TO CDE
RBTCBNXT EQU   X'80'                   FIRST (OLDEST) RB IN CHAIN
RBGRS15  EQU   *+X'5C',4,C'F'          R15 SLOT
         YREGS                         SYS1.MACLIB
         END
//SYSLIB   DD DSN=SYS1.MACLIB,DISP=SHR
//         DD DSN=SYS1.MODGEN,DISP=SHR
//SYSUT1   DD UNIT=VIO,SPACE=(CYL,2)
//SYSLIN   DD UNIT=VIO,SPACE=(TRK,1),DISP=(,PASS),DCB=BLKSIZE=3200
//SYSPRINT DD SYSOUT=*
//*
//LKED    EXEC PGM=HEWL,PARM=(MAP,REUS)
//SYSLIN   DD DSN=*.ASMH.SYSLIN,DISP=(OLD,PASS)
//SYSPRINT DD SYSOUT=*
//SYSLMOD  DD DSN=CBTTAPE.FILE183.LOAD(LLIBDEF1),DISP=SHR
