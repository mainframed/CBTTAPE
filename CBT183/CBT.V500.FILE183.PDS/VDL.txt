/*************************************************************** REXX */
/*                                                                    */
/* Module Name: VDL                                                   */
/*                                                                    */
/* Descriptive Name:  VIEW Data set List.                             */
/*                                                                    */
/* Status: R102                                                       */
/*                                                                    */
/* Function: This REXX program makes the list of data set created     */
/*           by DSLIST available as data in a VIEW session.           */
/*                                                                    */
/*           Using VDL is faster than using the SAVE command to       */
/*           store the dsnames into a data set then editing it.       */
/*                                                                    */
/*           VDL retrieves the dsnames from the table internally      */
/*           built by the DSLIST utility, copies the dsnames to a     */
/*           stemmed variable, then invokes STEMVIEW to VIEW the      */
/*           resulting data set list with each data set name in       */
/*           pos 1 of a data record.                                  */
/*                                                                    */
/* Author: Gilbert Saint-Flour <carlos@gsf-soft.com>                  */
/*                                                                    */
/* Environment: ISPF/PDF V3                                           */
/*              STEMVIEW R105                                         */
/*                                                                    */
/* Operation: VDL must be invoked from the DSLIST screen, either as   */
/*            a line command or as a TSO command, such as  TSO %VDL   */
/*                                                                    */
/* Change Activity:                                                   */
/*  R101 Misc changes                                                 */
/*  R102 Stop using DTB in ISPF 5.9 and above                         */
/*                                                                    */
/**********************************************************************/
ADDRESS 'ISPEXEC'; "CONTROL ERRORS RETURN"
              /*---------------------------------------*/
              /* Retrieve the name of the DSLIST table */
              /*---------------------------------------*/
TCB    = PTR(540)                  /* TCB (EXEC COMMAND)    PSATOLD  */
TCB    = PTR(TCB+132)              /* TCB (ISPTASK)         TCBOTC   */
FSA    = PTR(TCB+112)              /* FIRST SAVE AREA       TCBFSA   */
R1     = PTR(FSA+24)               /* ISPTASK'S R1                   */
TLD    = PTR(R1)                   /* TLD ADDRESS                    */
dta    = PTR(tld+076)              /* DTA address                    */
table  = STORAGE(D2X(dta+12),8)    /* Name of temp table    DSL12345 */
"VGET ZENVIR"                      /* Retrieve ISPF Version          */
IF LEFT(Zenvir,8) < 'ISPF 5.9' THEN DO
  dtb    = PTR(dta+016)            /* DTB address                    */
  IF dtb=0 THEN
    dtb  = PTR(dta+044)            /* OPEN Table Chain               */
  IF STORAGE(D2X(dtb),3) /= 'DTB' THEN DO
    SAY 'Error: Table name could not be located (DTB)' zenvir
    EXIT 12
    END
  table = STORAGE(D2X(dtb+6),8)    /* Name of temp table    DSL12345 */
  END

"TBQUERY" table "KEYS(KEYS) NAMES(NAMES) NAMENUM(NAMENUM)",
                "ROWNUM(ROWNUM) KEYNUM(KEYNUM) POSITION(CRP)"
IF rc>0 THEN SIGNAL TBQUERY_Failed
              /*---------------------------------------*/
              /* Scan the DSLIST table and build array */
              /*---------------------------------------*/
"TBTOP" table                      /* start from the top             */
IF rc>0 THEN EXIT rc

DO N=1 TO rownum                   /* read all the table entries     */
  "TBSKIP" table
  IF rc>0 THEN LEAVE
  space=''; attributes=''; dates=''
  IF zurecfm='FF'X THEN DO
     space=COPIES(' ',20)
     attributes = zuorg
     END
  ELSE
    IF zuobtatt='Y' THEN DO
      attributes = LEFT(zuorg,4) LEFT(zurecfm,4) zulrecl zublksiz
      IF zuobtspc='Y' THEN DO
        space      = zusize zuus || '%' zux zudevice
        IF zuobtdat='Y' THEN
          dates      = zucdate zuedate zurdate
        END
      END
  ds.n = LEFT(zudsns,44) zudvol space attributes dates
  ds.n = LEFT(ds.n,132) zuobtatt zuobtspc zuobtdat
END
"TBSKIP" table "ROW("CRP")"        /* re-position the table          */
                 /*-------------------*/
                 /* Display the array */
                 /*-------------------*/
SIGNAL ON SYNTAX                  /*    STEMVIEW not available     */
CALL STEMVIEW 'VIEW','ds.',1,rownum,"Data set name list"
EXIT rc

SYNTAX:                            /* STEMVIEW FUNCTION NOT AVAILABLE */
  'CONTROL DISPLAY LINE START(1)'
  SAY 'REXX error' rc 'in line' sigl':' errortext(rc)
  SAY sourceline(sigl)
  SAY ''
  DO i=1 TO rownum; SAY ds.i ; END
  EXIT
                  /*----------------*/
                  /*  Sub-routines  */
                  /*----------------*/

PTR: RETURN C2D(BITAND(STORAGE(D2X(ARG(1)),4),'7FFFFFFF'X))

Invalid_Environment:
  ZEDSMSG = 'Invalid environment'
  ZEDLMSG = 'This command must be invoked from the Data Set List',
            'utility; the current panel is' panel'.'
  "SETMSG MSG(ISRZ001)"
  EXIT 12

TBQUERY_Failed:
  ZEDSMSG = 'TBQUERY failed'
  ZEDLMSG = 'TBQUERY' table 'service failed with RC=' rc
  "SETMSG MSG(ISRZ001)"
  EXIT 12

        /*------------------------------------*/
        /* Variables (for documentation only) */
        /*------------------------------------*/

  SAY 'KEYS='KEYS 'NAMES='NAMES 'NAMENUM='NAMENUM
  SAY 'ROWNUM='ROWNUM 'KEYNUM='KEYNUM 'POSITION='CRP

  ZUBLKSIZ      ZDLBLKSZ
  ZUCDATE       ZDLCDATE
  ZUCDAT2
  ZUDEVICE      ZDLDEV
  ZUDMVOL       ZDLMVOL
  ZUDSNS        ZDLDSN
  ZUDSPACU
  ZUDVOL        ZDLVOL
  ZUDXSTAT      ZDLXSTAT
  ZUEDATE       ZDLEDATE
  ZUEDAT2
  ZULCMDL
                ZDLCMD
  ZULMSG
                ZDLMSG
  ZULOCVOL
                ZDLCAT
  ZULRECL       ZDLLRECL
  ZUOBTATT
  ZUOBTDAT
  ZUOBTSPC
  ZUORG         ZDLDSORG
  ZURDATE       ZDLRDATE
  ZURDAT2
  ZURECFM       ZDLRECFM
  ZUSIZE        ZDLSIZE
  ZUSTAT
  ZUUS          ZDLUSED
  ZUX           ZDLEXT
