//GILBERTT JOB (ACCT#),GETPUT31,
// NOTIFY=&SYSUID,
// CLASS=A,MSGCLASS=H,COND=(0,NE)
//ASMH EXEC PGM=IEV90,PARM=(OBJECT,NODECK,NOESD,NORLD,NOXREF)
*
*        ISSUE GET/PUT/PUTX WHILE RUNNING WITH AMODE31
*        NO LONGER NECESSARY WITH DFSMS/MVS
*
         MACRO
&NAME    GET31 &CB,&AREA,&EODAD=
&NAME    IHBINNRA &CB,&AREA             LOAD R1, R0
         SLR   15,15
         ICM   15,B'0111',49(1)         LOAD GET/PUT ROUTINE ADDR
         BASSM 14,15                    LINK TO GET/PUT ROUTINE
         AL    14,=F'6'                 L'AL+L'BSM
         BSM   0,14                     SWITCH BACK TO AMODE31
         AIF   (T'&EODAD EQ 'O').MEND
         CL    0,=F'6'                  END-OF-FILE?
         BE    &EODAD                   YES, GO TO &EODAD
         AIF   (D'EODAD31).MEND
EODAD31  CSECT
EODAD31  AMODE 31
         LA    0,006                    GET INCREMENT IN R0
         ALR   14,0                     ADD IT TO RETURN ADDRESS
         BSM   0,14                     GOBACK TO R14+6
&SYSECT  CSECT
.MEND    MEND
*----------------------------------------------------------------------
         MACRO
&NAME    PUT31 &CB,&AREA
&NAME    IHBINNRA &CB,&AREA             LOAD R1, R0
         SLR   15,15
         ICM   15,B'0111',49(1)         LOAD GET/PUT ROUTINE ADDR
         BASSM 14,15                    LINK TO GET/PUT ROUTINE
         AL    14,=F'6'                 L'AL+L'BSM
         BSM   0,14                     SWITCH BACK TO AMODE31
.MEND    MEND
*----------------------------------------------------------------------
         MACRO
&NAME    PUTX31 &CB,&AREA
&NAME    IHBINNRA &CB,&AREA             LOAD R1, R0
         L     15,48(,1)                LOAD GET/PUT ROUTINE ADDR
         LA    14,4(,15)                OFFSET TO PUTX RTNE
         BASSM 14,14                    LINK TO PUTX ROUTINE
         AL    14,=F'6'                 L'AL+L'BSM
         BSM   0,14                     SWITCH BACK TO AMODE31
.MEND    MEND
***********************************************************************
GETPUT31 CSECT
GETPUT31 AMODE 31
         BALR  12,0
         USING *,12
         OPEN  (IN,,PUNCH,OUTPUT)
*
GET1     GET31 IN,EODAD=EOJ
         LR    0,1                     PASS RECORD ADDRESS
***      PUT31 PUNCH,(0)
         PUTX31 PUNCH,IN               USE EXCHANGE BUFFERING
         B     GET1
*
EOJ      CLOSE (IN,,PUNCH)
         FREEPOOL IN
         FREEPOOL PUNCH
         SLR   15,15
         BSM   15,0                    IF AMODE=31, THEN R15=80000000
         SRL   15,28                   IF AMODE=31, THEN R15=00000008
         SVC   3                       GOBACK WITH RETURN CODE IN R15
IN       DCB   DSORG=PS,MACRF=GL,DDNAME=SYSIN,EODAD=EODAD31
PUNCH    DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPUNCH,RECFM=FB,LRECL=80
         END
//SYSLIB   DD DSN=SYS1.MACLIB,DISP=SHR
//SYSUT1   DD UNIT=VIO,SPACE=(CYL,4)
//SYSPRINT DD SYSOUT=*
//SYSLIN   DD UNIT=VIO,SPACE=(TRK,1),DISP=(,PASS),DCB=BLKSIZE=3200
//*
//GO      EXEC PGM=LOADER,PARM=NOPRINT
//SYSLIN   DD DSN=*.ASMH.SYSLIN,DISP=(OLD,PASS)
//SYSPRINT DD SYSOUT=*
//SYSPUNCH DD SYSOUT=*
//SYSUDUMP DD SYSOUT=*
444444444444444444444444
88888888888888888888888888888888
