/*************************************************************** REXX */
/*                                                                    */
/* Module Name = WHEREIS                                              */
/*                                                                    */
/* Descriptive Name = Search for a member in allocated libraries      */
/*                                                                    */
/* Status = R200                                                      */
/*                                                                    */
/* Function = Look for all occurences of a member in the libraries    */
/*            currently allocated to your TSO session.                */
/*            If multiple versions of the member are present in       */
/*            a concatenation, they are all displayed.                */
/*            ISPF stats are displayed if present.                    */
/*                                                                    */
/* Author   =  Gilbert Saint-Flour <carlos@gsf-soft.com>              */
/*                                                                    */
/* Dependencies = TSO/E V2                                            */
/*                ISPF V3 or V4                                       */
/*                STEMDISP Sub-routine (optional)                     */
/*                                                                    */
/* SYNTAX   =  WHEREIS                                                */
/*                 member                Member Name                  */
/*                                                                    */
/* DEFAULTS =  none                                                   */
/*                                                                    */
/* OPERANDS = See Below                                               */
/*                                                                    */
/*             member is the name of the member you are looking for.  */
/*                                                                    */
/* INVOCATION = WHEREIS can be invoked as a command or as an          */
/*              EDIT macro.                                           */
/*                                                                    */
/**********************************************************************/
ARG Mbr
IF Mbr='' THEN DO                      /*  IF NO ARGUMENT SPECIFIED,  */
  ADDRESS ISREDIT                      /*  SWITCH TO "EDIT MACRO"     */
  'MACRO (Mbr)'                        /*  MODE TO LOCATE MEMBER      */
  IF RC>0 THEN EXIT RC                 /*  BEING EDITED               */
  IF Mbr='' THEN DO
    "(Mbr) = MEMBER"
    IF RC>0 | Mbr='' THEN DO
      SAY 'ENTER MEMBER NAME'
      PULL Mbr
      IF Mbr='' THEN EXIT 1            /*  User hit ENTER             */
    END
  END
UPPER Mbr
END
X=OUTTRAP('Hline.')                    /*  TRAP LISTALC OUTPUT        */
ADDRESS TSO "LISTALC SY ST"            /*  GET DDN/DSN LIST           */
X=OUTTRAP('OFF')                       /*  STOP TRAPPING              */
ADDRESS ISPEXEC  ;I=0;J=0;K=1

DO I=2 TO Hline.0
  IF LEFT(Hline.I,2)='  ' THEN DO
    X=SUBSTR(Hline.I,3,8)
    IF LEFT(X,1)=' ' THEN
      J=J+1
    ELSE DO;
      J=0;
      DDN=STRIP(X)
      END
    IF POS('(',Dsn)>0 THEN ITERATE
    IF LEFT(Dsn,1)='*' THEN ITERATE
    IF LEFT(Dsn,9)='TERMFILE ' THEN ITERATE
    IF LEFT(Dsn,9)='NULLFILE ' THEN ITERATE
    IF RIGHT(Dsn,1)='?' THEN ITERATE
    IF SYSDSN("'"Dsn"("Mbr")'")='OK' THEN
      CALL GET_STATS
    END
  ELSE
    Dsn=Hline.I
END

IF K>1 THEN DO
  'VGET (ZENVIR)'
  SIGNAL ON SYNTAX                /* REXXTOOLS NOT PRESENT ON SYSTEM */
  CALL STEMDISP 'BROWSE','Line.',,,'Where is' Mbr
  END
EXIT

GET_STATS:
  "LMINIT DATAID(DD1) DATASET('"Dsn"') ENQ(SHR)"
  IF RC>0 THEN SIGNAL ERROR
  "LMOPEN DATAID("DD1")"
  IF RC>0 THEN SIGNAL ERROR
  "LMMFIND DATAID("DD1") MEMBER("Mbr") STATS(YES)"
  RC2=RC
  "LMCLOSE DATAID("DD1")"
  IF RC>0 THEN SIGNAL ERROR
  "LMFREE DATAID("DD1")"
  IF RC>0 THEN SIGNAL ERROR
  IF RC2=0 THEN DO
    X=ZLVERS'.'ZLMOD  ZLCDATE  ZLMDATE  ZLMTIME,
      ZLCNORC ZLINORC ZLMNORC ZLUSER
    IF LEFT(X,1)='Z' THEN X='(NO STATS)'
    Line.K=LEFT(DDN'+'J,12) Dsn;  L=K+1
    Line.L='            ' LEFT(Mbr,9) X
    K=K+2
    END
RETURN

SYNTAX:                            /* STEMDISP FUNCTION NOT AVAILABLE */
  'CONTROL DISPLAY LINE START(1)'
  DO I=1 TO K-1
    SAY Line.I; I=I+1; SAY Line.I
  END
