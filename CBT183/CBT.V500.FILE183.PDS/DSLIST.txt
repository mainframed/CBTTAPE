/*************************************************************** REXX */
/*                                                                    */
/* Module Name: DSLIST                                                */
/*                                                                    */
/* Descriptive Name: Cursor-driven entry into PDF 3.4 panel           */
/*                                                                    */
/* Status: R213                                                       */
/*                                                                    */
/* Function: This REXX program allows you to invoke the data-set      */
/*           list function of ISPF/PDF (option 3.4) with the          */
/*           DSNAME LEVEL field set to the dsname under which         */
/*           the cursor is placed when you enter the command.         */
/*           When running in ISPF 4.2, the Work Place (opt 11)        */
/*           is invoked instead of the Data Set List Utility.         */
/*                                                                    */
/* Author: Gilbert Saint-Flour <carlos@gsf-soft.com>                  */
/*                                                                    */
/* Environment: TSO/E V2                                              */
/*              ISPF/PDF V3 or V4                                     */
/*                                                                    */
/* Operation:                                                         */
/*                                                                    */
/*     DSLIST may be invoked as a command or as an EDIT macro.        */
/*                                                                    */
/*     o  To invoke DSLIST as a command, enter TSO %DSLIST in the     */
/*        command field, move the cursor under a data set name        */
/*        currently appearing on your screen and press ENTER.         */
/*        DSLIST extracts the data set name from the screen,          */
/*        invokes the Data Set List utility (opt 3.4) and places      */
/*        the data set name in the DSNAME LEVEL field.  In the        */
/*        ISPF 4.2 environment, DSLIST invokes the ISPF Workplace     */
/*        (opt 11) instead of the Data Set List utility.              */
/*                                                                    */
/*     o  To invoke DSLIST as an EDIT macro, type %DSLIST in the      */
/*        command field, move the cursor under a data set name        */
/*        currently appearing on your screen and press ENTER.         */
/*                                                                    */
/* Notes:                                                             */
/*                                                                    */
/*     o  DSLIST extracts data set names in upper-case only.          */
/*                                                                    */
/*     o  A data set name in a message can not be extracted.          */
/*                                                                    */
/*     o  When invoked as a command, the data set name must be        */
/*        entirely visible on the screen.                             */
/*                                                                    */
/*     o  When invoked as an EDIT macro, the data set name only       */
/*        needs to be partially visible but it must be on a           */
/*        DATA line.                                                  */
/*                                                                    */
/*     o  When invoked as an EDIT macro, DSLIST uses standard ISREDIT */
/*        functions to retrieve the text at the cursor position.      */
/*                                                                    */
/*     o  When invoked as a command, DSLIST accesses control          */
/*        blocks that are not part of the standard ISPF               */
/*        programming interface to retrieve the screen image and      */
/*        cursor position.  This method works with ISPF/PDF V2R3      */
/*        thru V4R5 but may not work in future releases.              */
/*        This technique is borrowed from ISPCDSN, a program written  */
/*        by Gordon J. Schillinger, to whom I'm for ever grateful.    */
/*                                                                    */
/*     o  Assign TSO %DSLIST to a PFK in those ISPF applications      */
/*        that allow you to view job output: SDSF, SAR, IOF, etc.     */
/*        In some applications (such as SYSVIEW), you MUST invoke     */
/*        DSLIST using a PFK.                                         */
/*                                                                    */
/*     o  When DSLIST is invoked outside of PDF (i.e ZAPPLID is NOT   */
/*        equal to ISR), DSLIST re-invokes itself with NEWAPPL(ISR)   */
/*        to ensure the same PFK setting as when option 3.4 is        */
/*        invoked from the PDF main menu.                             */
/*                                                                    */
/*     o  To enable their cursor-driven capability, commercial        */
/*        products like MVS/QuickRef require you to modify the        */
/*        main panel of every application in which you intend to      */
/*        use it.  DSLIST does not require any panel modification.    */
/*                                                                    */
/* Change Activity:                                                   */
/*                                                                    */
/*  @206 Fix DEBUG option, add "_" as dsname delimiter                */
/*  @207 Rewrite DSN extraction algorithm to support dynamic areas    */
/*       and windows; dsname must be in upper-case.                   */
/*  @208 If the cursor is placed under a DSN= or DSNAME= string,      */
/*       extract the dsname to the right of the = sign.               */
/*  @209 Invoke ISPF Workplace in the ISPF 4.2 environment            */
/*  @210 Minor technical changes and documentation improvements       */
/*  @211 Prevent error when csrw=0                                    */
/*  @212 Add PROFILE option on VPUT commands                          */
/*  @213 Rewrite dsname extraction algorithm                          */
/**********************************************************************/
ADDRESS ISPEXEC
"CONTROL ERRORS RETURN"
Zerrmsg=''
Dsname=''
"ISREDIT MACRO (DEBUG)"
If rc = 0 THEN DO                     /*   if we've been invoked as   */
  UPPER debug                         /*   an edit macro, use ISREDIT */
  "ISREDIT (LINE,CSRP) = CURSOR"      /*   to retrieve the line and   */
  "ISREDIT (LINE) = LINE .ZCSR"       /*   cursor position            */
  IF csrp>0 THEN                      /*   then                       */
    CALL extract_dsname               /*   extract the dsname         */
  END
ELSE DO                               /*  DSLIST invoked as a command */
  ARG debug
  "VGET ZAPPLID"                      /*   Get current application ID */
  IF zapplid \= 'ISR' THEN DO         /*   If ZAPPLID\=ISR, re-invoke */
    "SELECT CMD(%"SYSVAR(SYSICMD) debug ") NEWAPPL(ISR)"
    EXIT rc                           /*   DSLIST with NEWAPPL(ISR)   */
    END
  CALL find_cursor_position           /*   find the cursor position,  */
  CALL extract_dsname                 /*   extract the dsname         */
  END
"VGET ZENVIR"                         /* Retrieve ISPF Version        */
 /* Zenvir='ISPF 4.1' */              /* Force usage of DSLIST        */
IF LEFT(Zenvir,8) < 'ISPF 4.2' THEN
  CALL Invoke_ISRUDL                  /* Data Set List Utility (3.4)  */
ELSE
  CALL Invoke_Work_Place              /* Work Place Utility (opt 11)  */
IF zerrmsg\='' THEN "SETMSG MSG("zerrmsg")"
EXIT
/*-------------------------------------------------------- sub-routine*/
/*                                                                    */
/*            RETRIEVE LINE ADDRESS AND CURSOR POSITION               */
/*                                                                    */
/*--------------------------------------------------------------------*/
find_cursor_position:
  tcb    = PTR(540)               /* TCB (EXEC command)    PSATOLD  */
  tcb    = PTR(tcb+132)           /* TCB (ISPTASK)         TCBOTC   */
  fsa    = PTR(tcb+112)           /* first save area       TCBFSA   */
  r1     = PTR(fsa+24)            /* ISPTASK's R1                   */
  tld    = PTR(r1)                /* TLD address                    */
  tls    = PTR(tld+096)           /* screen buffer         TLDTLSP  */
  csr    = PTR(tld+164)           /* relative cursor pos.  TLDCSR   */
  scrw   = PTR(tld+192)           /* screen width          TLDCLSWD */
  IF scrw>0 then
    csrp = csr//scrw+1            /* cursor position in curr. line  */
  ELSE
    csrp = 1                      /* ISPSTRT                        */
  linead = D2X(tls+csr-csrp+1)    /* address of current line        */
  line   = STORAGE(linead,scrw)   /* text of current line           */
  IF debug='DEBUG' THEN SAY 'tcb='D2X(tcb) 'fsa='D2X(fsa) 'r1='D2X(r1),
          'tld='D2X(tld) STORAGE(D2X(tld),4) 'tls='D2X(tls),
          'csr='csr 'scrw='scrw 'linead='linead
  RETURN
PTR: RETURN C2D(BITAND(STORAGE(D2X(ARG(1)),4),'7FFFFFFF'X))
/*-------------------------------------------------------- sub-routine*/
/*                                                                    */
/*           Scan current line to extract the dsname                  */
/*                                                                    */
/*           line -> TEXT OF LINE AT CURSOR POSITION                  */
/*           csrp -> CURSOR POSITION IN LINE                          */
/*                                                                    */
/*--------------------------------------------------------------------*/
extract_dsname:
  valid=COPIES(' ',64)||XRANGE('40'x,'FF'x)
  IF debug='DEBUG' THEN                            /*     debug     */
    DO                                             /*     debug     */
      PARSE SOURCE . . cmd ddn dsn . . . . .       /*     debug     */
      SAY 'cmd='cmd 'ddn='ddn 'dsn='dsn            /*     debug     */
      DO I=1 BY 16 TO LENGTH(Line)                 /*     debug     */
        HEX=''; CHAR='*'                           /*     debug     */
        DO J=I BY 4 UNTIL J>I+16                   /*     debug     */
          L=LENGTH(Line)-J+1                       /*     debug     */
          IF L<1 THEN LEAVE                        /*     debug     */
          S=SUBSTR(Line,J,L)                       /*     debug     */
          HEX=HEX || ' ' || LEFT(C2X(S),8)         /*     debug     */
          CHAR=CHAR||LEFT(S,4)                     /*     debug     */
        END                                        /*     debug     */
        char=TRANSLATE(char,valid)                 /*     debug     */
        SAY '  +'RIGHT(I-1,4,'0') ' ' LEFT(HEX,36) ' ' LEFT(CHAR,17)'*'
      END                                          /*     debug     */
      SAY 'CURSOR POSITION='csrp                   /*     debug     */
    END                                            /*     debug     */
  If SUBSTR(line,csrp,1)=' ' THEN RETURN

  /* If the cursor is positionned under a DSN= or DSNAME= string,   */
  /* move the cursor pointer to the right of the = sign.            */

  p=SUBSTR(line,MAX(csrp-3,1),MIN(csrp+3,LENGTH(line))+1)
  IF POS('DSN=',p) > 0 THEN csrp=csrp+4

  p=SUBSTR(line,MAX(csrp-6,1),MIN(csrp+6,LENGTH(line))+1)
  IF POS('DSNAME=',p) > 0 THEN csrp=csrp+7

  /*  Pad the line at both ends with spaces; convert to upper-case  */

  line=COPIES(' ',10) || TRANSLATE(line,valid) || COPIES(' ',10)
  csrp=csrp+10
  UPPER line                              /* convert to upper case  */

  /* If the cursor is positionned under a parenthesis, ignore it    */

  DO WHILE SUBSTR(line,csrp,1)='('; csrp=csrp+1; END
  DO WHILE SUBSTR(line,csrp,1)=')'; csrp=csrp-1; END

  valid='ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789@#$-{.'
  ld=0;lp=0

  /*     Check that the cursor is under a valid dsname character.   */
  /*     If it's under an apostrophe, determine if it's the start   */
  /*     or the end of the dsname.                                  */

  c=SUBSTR(line,csrp,1)
  IF VERIFY(c,valid)=1 THEN DO            /* invalid dsname char    */
    IF c\="'" THEN RETURN                 /* invalid, not a quote   */
    IF VERIFY(SUBSTR(line,csrp+1,1),valid)=0 THEN
      ld=csrp                             /* left delimiter found   */
    ELSE DO                               /* apost marks start dsn  */
      p=csrp-1                            /* move csr before apost  */
      IF SUBSTR(line,p,1)=')' | VERIFY(SUBSTR(line,p,1),valid)=0 THEN
        csrp=csrp-2                       /* move csr before )      */
      ELSE                                /* apost surounded by     */
        RETURN                            /* invalid characters     */
      END
    END
  IF debug='DEBUG' THEN SAY 'ld='ld

  /*                Find start of dsname                            */

  IF ld=0 THEN DO
    DO csrp=csrp to 1 by -1
      c=SUBSTR(line,csrp,1)
      IF VERIFY(c,valid)>0 THEN           /* invalid dsname char    */
        IF c='(' THEN DO                  /* left paren             */
          IF lp>0 THEN LEAVE              /* not first left paren   */
          DO I=1 to 9                     /* search for a period    */
            IF SUBSTR(line,csrp+i,1) ='.' THEN LEAVE
          END
          IF i>8 THEN                     /* no period found after  */
            lp=csrp                       /* start of member        */
          ELSE
            LEAVE                         /* start of member        */
          END
        ELSE                              /* invalid char, not "("  */
          LEAVE
      END
    ld=csrp                               /* left delimiter         */
    IF lp=ld+1 THEN DO
      lp=0                                /* no member              */
      ld=ld+1
      END
    END
  IF debug='DEBUG' THEN SAY 'ld='ld 'lp='lp 'csrp='csrp

  /*              Find start of member name                         */

  IF lp=0 THEN DO                         /* "(" not seen yet       */
    csrp=ld+1                             /* 1st byte of dsname     */
    p=VERIFY(line,valid,,csrp)            /* Find delimiter         */
    IF debug='DEBUG' THEN SAY 'csrp='csrp 'p='p SUBSTR(line,p,1)
    IF SUBSTR(line,p,1)='(' THEN DO
      lp=p                                /* "(" found              */
      csrp=p+1                            /* 1st byte of mbr name   */
      END
    END
  ELSE
    csrp=lp+1                             /* 1st byte of member     */
  IF debug='DEBUG' THEN SAY 'ld='ld 'lp='lp 'csrp='csrp

  /*                 Find end of dsname                             */

  rd=VERIFY(line,valid,,csrp)             /* Find delimiter aft dsn */
  IF debug='DEBUG' THEN SAY 'csrp='csrp 'rd='rd SUBSTR(line,rd,1)
  IF lp>0 & SUBSTR(line,rd,1)=')' THEN rd=rd+1

  IF SUBSTR(line,ld,1)="'" & SUBSTR(line,rd,1)\="'" THEN RETURN

  Dsname = SUBSTR(Line,ld+1,rd-ld-1)
  IF LEFT(Dsname,9) = 'CLUSTER--',        /* IDCAMS LISTCAT         */
   | LEFT(Dsname,9) = 'DATA-----',        /* IDCAMS LISTCAT         */
   | LEFT(Dsname,9) = 'INDEX----',        /* IDCAMS LISTCAT         */
   | LEFT(Dsname,9) = 'NONVSAM--' THEN    /* IDCAMS LISTCAT         */
    dsname=RIGHT(dsname,LENGTH(dsname)-9) /* IDCAMS LISTCAT         */
  IF debug='DEBUG' THEN SAY 'Dsname="'Dsname'"'
  IF LENGTH(Dsname) < 5 | LENGTH(Dsname) > 44 THEN Dsname=''
  RETURN
/*-------------------------------------------------------- sub-routine*/
/*                                                                    */
/*     Invoke the Data Set List Utility (ISPF 2.3 through 4.1)        */
/*                                                                    */
/*--------------------------------------------------------------------*/
Invoke_ISRUDL:
  IF Dsname\='' THEN DO
    i=INDEX(Dsname,'(')                   /* check for member name    */
    IF i>0 THEN                           /* If abcd.edfgh(ijkl)      */
      Zdldsnlv=LEFT(Dsname,i-1)           /* remove (ijkl)            */
    ELSE
      Zdldsnlv=Dsname                     /* pass data set name       */
    zdlpvl=''                             /* blank out volume serial  */
    "VPUT (Zdldsnlv Zdlpvl) PROFILE"      /* update ISRUDLP variables */
    END
  "SELECT PGM(ISRUDL) PARM(ISRUDLP)"
  RETURN
/*-------------------------------------------------------- sub-routine*/
/*                                                                    */
/*     Invoke the Work Place Utility (ISPF 4.2 and above)             */
/*                                                                    */
/*--------------------------------------------------------------------*/
Invoke_Work_Place:
  IF Dsname\='' THEN DO
    Zwrkdsn="'"Dsname"'"                  /* enclose dsname in quotes */
    Zwrkvol=''                            /* blank out volume serial  */
    Zreftype='D'                          /* Data set view            */
    "VPUT (Zwrkdsn Zwrkvol Zreftype) PROFILE" /* update ISRWORK vars */
    END
  "SELECT PGM(ISRUDA) PARM(ISRWORK) SCRNAME(WORK)"
  RETURN
