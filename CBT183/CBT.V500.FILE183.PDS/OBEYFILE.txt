/*************************************************************** REXX */
/*                                                                    */
/* Module Name: OBEYFILE                                              */
/*                                                                    */
/* Descriptive Name:  OBEYFILE EDIT macro for ISPF and MVS TCP/IP.    */
/*                                                                    */
/* Status: R100                                                       */
/*                                                                    */
/* Function: This ISPF EDIT macro allows the TCP/IP administrator     */
/*           to use the data being edited as input to the TSO         */
/*           OBEYFILE command without having to SAVE it.              */
/*                                                                    */
/*           This EDIT macro writes the data being edited to the ISPF */
/*           control data set and issues the TSO OBEYFILE command.    */
/*                                                                    */
/*           The user can use the C, Cnn or CC/CC line command to     */
/*           select a range of lines to be passed to OBEYFILE.        */
/*                                                                    */
/*           This EDIT macro is a convenient way to test changes      */
/*           to the TCP/IP profile data set without having to         */
/*           actually save the modifications into the profile         */
/*           or another data set.                                     */
/*                                                                    */
/* Author: Gilbert Saint-Flour <carlos@gsf-soft.com>                  */
/*                                                                    */
/* Environment: ISPF/PDF V3                                           */
/*              TCP/IP for MVS                                        */
/*                                                                    */
/* Operation: OBEYFILE must be invoked as an EDIT macro without       */
/*            any parameter.  A range selection line command such     */
/*            as C, Cnn or CC/CC can be specified.                    */
/*                                                                    */
/* Change Activity:                                                   */
/*                                                                    */
/**********************************************************************/

ADDRESS ISPEXEC
"CONTROL ERRORS RETURN"
"ISREDIT MACRO (parm) NOPROCESS"
IF rc>0 THEN SIGNAL Not_an_EDIT_macro
IF parm\='' THEN SIGNAL No_Parms_allowed

/**********************************************************************/
/*                                                                    */
/*          Determine what temporary data set to use.                 */
/*                                                                    */
/*  1. If the current ISPF control data set is non-temporary          */
/*     (ztempf=userid.SPFTEMPn.CNTL), use it.                         */
/*                                                                    */
/*  2. If the control data set is preallocated to DD(ISPCTLn),        */
/*     allocate userid.SPFTEMPn.CNTL and use it.                      */
/*                                                                    */
/**********************************************************************/

"VGET (ZTEMPF ZTEMPN ZSCREEN ZUSER)"
IF ztempn = 'ISPCTL' || zscreen then DO
  ztempn = 'OBEYFILE'
  rc=LISTDSI(ztempn FILE)
  IF rc=0 THEN
    ztempf = "'" || sysdsname || "'"
  ELSE DO
    ztempf = "'" || zuser || ".SPFTEMP" || zscreen || ".CNTL'"
    rc=LISTDSI(ztempf NORECALL)
    IF rc=0 THEN DO
      ADDRESS TSO "ALLOCATE DD(" ztempn ") DS(" ztempf ") SHR"
      IF rc>0 THEN EXIT rc
      END
    ELSE DO
      IF sysreason = 9 | sysreason = 26 THEN
        "HDELETE" ztempf "WAIT"
      ADDRESS TSO "ALLOCATE DD(" ztempn ") DS(" ztempf ") NEW CATALOG",
                  "SPACE (1 1) TRACKS UNIT(SYSALLDA)",
                  "DSORG(PS) RECFM(F B) LRECL(80)"
      END
    END
  END
ELSE
  ztempf = "'" || ztempf || "'"

/**********************************************************************/
/*                                                                    */
/*   If the user selected a range by entering a line command such     */
/*   as C, Cnn or C/CC, set line1 and line2 to the first and last     */
/*   line numbers, respectively.  Otherwise, set line1 to 1 and       */
/*   line2 to the last line of the data set.                          */
/*                                                                    */
/**********************************************************************/

'ISREDIT PROCESS RANGE C'
SELECT
  WHEN rc=0 THEN DO
    "ISREDIT (LINE1) = LINENUM .ZFRANGE"         /* First line  */
    "ISREDIT (LINE2) = LINENUM .ZLRANGE"         /* Last line   */
    END
  WHEN rc=4 THEN DO
    Line1=1                                    /*   DEFAULTS  */
    "ISREDIT (LINE2) = LINENUM .ZLAST"         /*   DEFAULTS  */
    END
  WHEN rc=20 THEN                       /* All lines deleted        */
    SIGNAL Empty_dataset                /* No lines to ROUTE        */
  OTHERWISE DO                          /* Line command conflict    */
    Pgmrc=rc                            /* Save ISREDIT Return code */
    IF zerrmsg\='' THEN
      'SETMSG MSG('zerrmsg')'           /* Edit has created message */
    EXIT Pgmrc
    END
END

/**********************************************************************/
/*                                                                    */
/*  QUEUE the data lines to the stack and write them out to &ztempf   */
/*                                                                    */
/**********************************************************************/

DO line1=line1 TO line2
  "ISREDIT (LINE) = LINE" Line1               /* GET CURRENT LINE */
  QUEUE Line
  END
QUEUE ''                                      /* end of file      */

ADDRESS TSO
"EXECIO * DISKW" ztempn "(FINIS"

/**********************************************************************/
/*                                                                    */
/*       Reallocate &ztempf with DISP(SHR) to prevent error 8538      */
/*                                                                    */
/**********************************************************************/

"FREE DD(" ztempn ") KEEP"
IF rc>0 THEN EXIT rc
"ALLOCATE DD(" ztempn ") DS(" ztempf ")" SHR
IF rc>0 THEN EXIT rc

/**********************************************************************/
/*                                                                    */
/*             Execute the TCP/IP OBEYFILE command                    */
/*                                                                    */
/**********************************************************************/

"OBEYFILE" ztempf
EXIT rc

/**********************************************************************/
/*                      Exception Routines                            */
/**********************************************************************/
Not_an_EDIT_macro:
  Zedsmsg = "EDIT Macro Only"                  /* Short message    */
  Zedlmsg = "OBEYFILE may only be invoked as an EDIT macro"
  "SETMSG MSG(ISRZ001)"                        /* send the message */
  EXIT 8
No_Parms_allowed:
  Zedsmsg = "No parms allowed"                 /* Short message    */
  Zedlmsg = "The OBEYFILE EDIT macro does not accept any parameters",
            "other than a range selection line command such as",
            "C, Cnn or CC/CC"
  "SETMSG MSG(ISRZ001)"                        /* send the message */
  EXIT 8
Empty_dataset:
  zedsmsg='Empty data set'
  zedlmsg='There are no lines to print'
  'SETMSG MSG(ISRZ001)'
  EXIT 12
