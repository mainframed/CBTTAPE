//GILBERTI JOB (ACCT#),IEC149I,
// NOTIFY=&SYSUID,
// CLASS=A,MSGCLASS=H,COND=(0,NE)
//HLASM EXEC PGM=ASMA90,PARM=(OBJECT,NODECK,NOESD,NORLD,NOXREF)
***********************************************************************
*                                                                     *
* MODULE NAME = IEC149I                                               *
*                                                                     *
* DESCRIPTIVE NAME = MPF exit for IEC149I 813-04 message              *
*                                                                     *
* STATUS = R101                                                       *
*                                                                     *
* FUNCTION = This MPF exit is invoked for the following messages:     *
*                                                                     *
*              1. IEC149I 813-04,IFG0195H,jobname,stepname,ddname ... *
*              2. IEC023I 237-08,IFG0553F,jobname,stepname,ddname ... *
*                                                                     *
*            These messages are issued when OPEN or EOV detect        *
*            that the dsname in the header label is not equal to      *
*            the dsname in the JFCB.                                  *
*                                                                     *
*            This exit issues a WTO to display the contents of the    *
*            HDR1 label and help the programmer debug the problem     *
*            and correct the JCL.                                     *
*                                                                     *
*            The message has the following format:                    *
*                                                                     *
*            **HDR1,'17-char label',fileseq,volseq,credt,expdt,fsec   *
*                                                                     *
*                    17-char label      data set name                 *
*                    fileseq            file sequence number          *
*                    volseq             volume sequence number        *
*                    credt              creation date (julian)        *
*                    expdt              expiration date (julian)      *
*                    fsec               file security indicator       *
*                                                                     *
* AUTHOR = Gilbert Saint-Flour <carlos@gsf-soft.com>                  *
*                                                                     *
* ENVIRONMENT = SEE BELOW                                             *
*                                                                     *
*    DEPENDENCIES: MVS/ESA, OS/390                                    *
*                                                                     *
* MODULE TYPE = PROCEDURE, (CSECT TYPE)                               *
*                                                                     *
*    PROCESSOR = IBM OS/ASSEMBLER H VERSION 2 OR                      *
*                IBM HIGH LEVEL ASSEMBLER/MVS                         *
*                                                                     *
*    MODULE SIZE = 1K                                                 *
*                                                                     *
*    ATTRIBUTES = REENTERABLE, RMODE ANY, AMODE 31,                   *
*                 SUPERVISOR STATE, KEY 0                             *
*                                                                     *
* OPERATION = See documentation for IEAVMXIT                          *
*                                                                     *
* INSTALLATION = See Below                                            *
*                                                                     *
*    1. Link-edit IEC149I into an APF-authorized link-list            *
*       library and refresh LLA.                                      *
*                                                                     *
*    2. Add the following lines to the active MPFLSTnn                *
*       member in SYS1.PARMLIB:                                       *
*                                                                     *
*         IEC149I,SUP(NO),RETAIN(NO),USEREXIT(IEC149I)                *
*         IEC023I,SUP(NO),RETAIN(NO),USEREXIT(IEC149I)                *
*                                                                     *
*    3. Issue the SET MPF=nn command to activate IEC149I.             *
*                                                                     *
* PARAMETERS = NONE                                                   *
*                                                                     *
*    IEC149I retrieves the address of the OPEN/CLOSE/EOV              *
*    work area from the R4 slot of the current SVRB.                  *
*                                                                     *
* SECURITY = See Below                                                *
*                                                                     *
*    The author considers that this MPF exit does NOT introduce       *
*    a breach into the integrity of the MVS system, as the            *
*    information it reveals is already available to the               *
*    DCB ABEND exit of an unauthorised assembler program.             *
*                                                                     *
* RETURN-CODES = NONE                                                 *
*                                                                     *
* CHANGE ACTIVITY                                                     *
*                                                                     *
* 100 Initial release                                                 *
* 101 Replace STRING macro with MVC instructions                      *
*                                                                     *
&REL     SETC  '101'                                                  *
***********************************************************************
IEC149I  CSECT
IEC149I  RMODE ANY                     AMODE(31),RMODE(ANY)
         DC    C'GSF UTILITIES - IEC149I R101'
BEGIN    BAKR  R14,0
         SLR   R4,R4                   prevent recursion
         LR    R5,R15                  BASE REG
         USING BEGIN,R5
         L     R6,0(,R1)               A(CTXT)
         USING CTXT,R6
         L     R7,CTXTTXPJ             TEXT MAJOR POINTER
         USING CTXTATTR,R7
*
*        Retrieve the address of the O/C/E work area
*
         L     R8,X'21C'(,0)           PSATOLD
         L     R8,0(,R8)               TCBRBP (WTO's SVRB)
         USING RBBASIC,R8
         L     R9,RBGRS4               R4 in OPEN is O/C/E area
         USING FORCORE,R9
         CLC   =C'HDR1',FL1AREA        am I lost?
         BNE   EXIT                    yes, quit
*
*        Issue WTO to display the IEC149I message
*
         L     R1,RBGRS1               get R1 (WTO parm list)
         WTO   MF=(E,(1))              re-issue WTO
*
*        now move the contents of the HDR1 label into the message text
*
         MVC   CTXTTMSG+00(8),=C'**HDR1,'''
         MVC   CTXTTMSG+08(17),FL1ID   17-char label
         MVI   CTXTTMSG+25,C''''       '
         MVI   CTXTTMSG+26,C','        ,
         MVC   CTXTTMSG+27(4),FL1FILSQ file seq no
         MVI   CTXTTMSG+31,C','        ,
         MVC   CTXTTMSG+32(4),FL1VOLSQ vol seq no
         MVI   CTXTTMSG+36,C','        ,
         MVC   CTXTTMSG+37(6),FL1CREDT creation date YYDDD
         MVI   CTXTTMSG+43,C','        ,
         MVC   CTXTTMSG+44(6),FL1EXPDT expiration date YYDDD
         MVI   CTXTTMSG+50,C','        ,
         MVC   CTXTTMSG+51(1),FL1FSEC  file security indicator
         MVI   CTXTTLEN+1,52           adjust length
*
         OI    CTXTRFB1,CTXTRCMT       CHANGE THE MESSAGE TEXT
         OI    CTXTCOLR,CTXTWHIT       COLOR=WHITE
         OI    CTXTERF1,CTXTEMCO       CHANGE THE COLOR
*
EXIT     PR                            RETURN TO WTO SVC
*
         PRINT NOGEN
         IEZVX100 DSECT=YES            CTXT
         IKJRB DSECT=YES               SVRB
FORCORE  DSECT                         O/C/E WORK AREA
         IECDSECT IOB=NO               O/C/E WORK AREA
         YREGS
         END   BEGIN
//SYSLIB   DD DSN=SYS1.MACLIB,DISP=SHR
//         DD DSN=SYS1.MODGEN,DISP=SHR
//SYSUT1   DD UNIT=VIO,SPACE=(CYL,1)
//SYSPRINT DD SYSOUT=*
//SYSLIN   DD UNIT=VIO,SPACE=(TRK,1),DISP=(,PASS),BLKSIZE=3200
//*
//LKED    EXEC PGM=IEWL,PARM='MAP,RENT'
//SYSLIN   DD DSN=*.HLASM.SYSLIN,DISP=(OLD,PASS)
//SYSPRINT DD SYSOUT=*
//SYSLMOD  DD DSN=USER.LINKLIB(IEC149I),DISP=SHR


Sample test job:

    //GILBERTJ JOB (ACCT#),IEC149J,
    // NOTIFY=&SYSUID,
    // CLASS=A,MSGCLASS=H
    //*
    //* TEST JOB FOR THE IEC149I EXIT
    //*
    //TEST149 EXEC PGM=IDCAMS
    //TAPE1    DD UNIT=3400-6,VOL=SER=123456
    //TAPE2    DD UNIT=3400-6,VOL=SER=123456,DSN=FILE0001,
    // RECFM=U,BLKSIZE=32760
    //         DD UNIT=3400-6,VOL=SER=123456
    //DUMMY    DD DUMMY,RECFM=U,BLKSIZE=32760
    //SYSPRINT DD SYSOUT=*
    //SYSUDUMP DD SYSOUT=*
     REPRO IFILE(TAPE1) OFILE(DUMMY)          /* 813-04 */
     REPRO IFILE(TAPE2) OFILE(DUMMY)          /* 237-08 */
