//GILBERTG JOB (ACCT#),GETDIR,
// NOTIFY=&SYSUID,
// CLASS=A,MSGCLASS=H,COND=(0,NE)
//ASMH EXEC PGM=IEV90,PARM=(OBJECT,NODECK,NOESD,NORLD,NOXREF)
*
*    THE "GETDIR" MACRO ALLOWS YOU TO READ A PDS DIRECTORY
*    SEQUENTIALLY, AS FOLLOWS:
*
*        IT WORKS IN "LOCATE" MODE; AFTER EACH INVOCATION, THE
*        ADDRESS OF A DIRECTORY ENTRY (MEMBER NAME, TTR, ETC) IS
*        RETURNED IN R1.
*
*        IT USES A BPAM DCB (DSORG=PO,MACRF=R), NOT A QSAM DCB
*        (WHICH WOULD HAVE DSORG=PS).  THIS ALLOWS YOU TO READ THE
*        DIRECTORY AND THE MEMBERS WITH THE SAME DCB.
*
*        R1 IS SET TO ZERO WHEN THE END OF THE DIRECTORY IS REACHED.
*        THE "EODAD" KEY-WORD OF THE "GETDIR" MACRO MAY BE USED TO
*        SPECIFY THE ADDRESS OF THE END-OF-DIRECTORY ROUTINE.
*        THE EODAD KEY-WORD OF THE DCB IS ONLY USED AT END-OF-MEMBER.
*
*        THE "GETDIR" MACRO ONLY PROCESSES THE FIRST PDS IN A
*        CONCATENATION, AND WILL NOT WORK WITH A PDSE.
*
         MACRO
&NAME    GETDIR &DCB,&EODAD=
&NAME    IHBINNRA &DCB
         L     R15,=A(@GETDIR)         ADDR OF "GETDIR" RTNE
         BALR  R14,R15                 GET A DIRECTORY ENTRY
         AIF   (T'&EODAD EQ 'O').GEN
         LTR   R1,R1                   END OF DIRECTORY?
         BZ    &EODAD                  YES, GO AWAY
.GEN     AIF   (D'GETDIR).MEND
.**********************************************************************
.*
.*       GETDIR SUB-ROUTINE
.*
.**********************************************************************
@GETDIR  CSECT
         SAVE  (14,12),,'GETDIR 840510'
         BALR  R2,0
         USING *,R2
         LR    R3,R1                   ADRESSE DU DCB
         ST    R13,@GETDSAV+4
         LA    R13,@GETDSAV
         LA    R5,X'000100'            TT=00, R=1, N=0
         ICM   R1,B'1111',@GETDR1      CURRENT POS IN BUFFER
         BZ    @GETD22                 PICK UP PARM LIST OFFSET
         LA    R14,X'1F'               MASQUE POUR "AND"
         IC    R0,11(,R1)              "C" BYTE
         NR    R14,R0                  NOMBRE DE HALFWORDS
         LA    R14,12(R14,R14)         LONGUEUR DU POSTE DANS R14
         LH    R15,@GETDBUF            LONGUEUR UTILISEE
         LA    R15,@GETDBUF-1(R15)     ADRESSE DERNIER OCTET UTILISE
         BXLE  R1,R14,@GETD24
         AL    R5,@GETDR5              INCREMENTER LE NUMERO D'ENREG
@GETD22  ST    R5,@GETDR5              TTRN DU BLOC SUIVANT
         POINT (R3),@GETDR5            POINTER SUR LE BON BLOC
         LH    R4,62(,R3)              SAUVER DCBBLKSI
         MVI   62(R3),1                BLKSIZE=256
         MVI   63(R3),0                BLKSIZE=256
         READ  @GETDECB,SF,(R3),@GETDBUF,'S'
         STH   R4,62(,R3)              RESTAURER DCBBLKSI
         CHECK @GETDECB
         LA    R1,@GETDBUF+2           1ER POSTE
@GETD24  CLI   0(R1),X'FF'             FIN DE BLOC?
         BNE   @GETD29                 NON, RENDRE L'ADRESSE
         SLR   R1,R1                   FIN-DE-REPERTOIRE
@GETD29  ST    R1,@GETDR1              GARDER L'ADRESSE
         L     R13,4(,R13)
         L     R14,12(,R13)            ADRESSE DE RETOUR
         LM    R2,R12,28(R13)          R2-R3
         BR    R14
@GETDR1  DS    A(@GETDBUF+2)           POINTEUR COURANT DANS LE BUFFER
@GETDR5  DS    F                       TTRN DU BLOC COURANT
@GETDSAV DS    18F
@GETDBUF DS    H,254X
&SYSECT  CSECT
.MEND    MEND
**********************************************************************
**********************************************************************
********* TEST PROGRAM FOR THE 'GETDIR' MACRO ************************
**********************************************************************
**********************************************************************
TESTPGM  START 0
         BALR  R12,0
         USING *,R12
         OPEN  (SYSLIB,,SYSPRINT,OUTPUT)
*LOOP
GETD     GETDIR SYSLIB,EODAD=EOJ
         MVC   WORK80(11),0(R1)         SAVE MEMBER NAME AND TTR
         PUT   SYSPRINT,WORK80
         FIND  SYSLIB,WORK80+8,C        FIND WITH TTR0
         READ  DECB1,SF,SYSLIB,BUFF1    READ FIRST BLOCK
         CHECK DECB1
         L     R6,=A(BUFF1)
         PUT   SYSPRINT,(R6)            WRITE FIRST LINE
         B     GETD
*ENDLOOP
EOJ      SLR   R15,R15
         SVC   3                        GOBACK
WORK80   DS    XL80
SYSPRINT DCB   DSORG=PS,DDNAME=SYSPRINT,MACRF=PM,RECFM=FB,LRECL=80
SYSLIB   DCB   DSORG=PO,DDNAME=SYSLIB,MACRF=R,EODAD=EOJ
         YREGS
BUFF1    CSECT
         DS    32760X
         END
//SYSPRINT DD SYSOUT=*
//SYSLIB   DD DSN=SYS1.MACLIB,DISP=SHR
//SYSUT1   DD UNIT=VIO,SPACE=(CYL,2)
//SYSLIN   DD UNIT=VIO,SPACE=(TRK,1),DISP=(,PASS),DCB=BLKSIZE=3200
//*
//GO      EXEC PGM=LOADER,PARM=NOPRINT
//SYSLIN   DD DSN=*.ASMH.SYSLIN,DISP=(OLD,PASS)
//SYSLIB   DD DSN=SYS1.PROCLIB,DISP=SHR
//SYSPRINT DD SYSOUT=*,OUTLIM=666
//ABNLTERM DD SYSOUT=*
//ABNLDUMP DD DUMMY
//SYSUDUMP DD SYSOUT=*
