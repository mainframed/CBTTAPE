/*****************************************************************REXX*/
/*                                                                    */
/* MODULE NAME = LPR                                                  */
/*                                                                    */
/* DESCRIPTIVE NAME = LPR EDIT Macro for ISPF/PDF                     */
/*                                                                    */
/* STATUS = R100                                                      */
/*                                                                    */
/* FUNCTION = The LPR EDIT macro is used to transmit a copy of        */
/*            the data currently being edited via LPR to a            */
/*            remote host.  The data are converted to ASCII           */
/*            during the transmission.  This EDIT macro is useful     */
/*            to transmit data that does not exist on disk in a       */
/*            transmittable form, such as packed data, data           */
/*            modified during the current EDIT session, or data       */
/*            only accessible via the EDIT Interface (EDIF).          */
/*                                                                    */
/* AUTHOR   =  Gilbert Saint-Flour <carlos@gsf-soft.com>              */
/*                                                                    */
/* Dependencies = TSO/E V2                                            */
/*                ISPF/PDF V3 or ISPF V4                              */
/*                TCP/IP V3.2                                         */
/*                ROUTEPGM utility R605 (optional)                    */
/*                FASTPATH utility R128 (optional)                    */
/*                                                                    */
/* SYNTAX   =  LPR                                                    */
/*                 host                  remote host or IP address    */
/*                 printer               printer name                 */
/*                                       (default: LPT1)              */
/*                                                                    */
/* OPERATION = see below                                              */
/*                                                                    */
/*        LPR creates a temporary data set, copies the data into      */
/*        into it, then invokes the TCP/IP LPR command.               */
/*                                                                    */
/* CHANGES = see below                                                */
/*                                                                    */
/* 100 New packaging for the CBT tape                                 */
/*                                                                    */
/**********************************************************************/
Routepgm='NO'                  /* Use the REXX stack and EXECIO       */
Routepgm='YES'                 /* Use the ROUTEPGM program            */
FastPath='NO'                  /* Invoke the FTP command directly     */
FastPath='YES'                 /* Invoke FTP via the FASTPATH utility */
              /* -------------------------- */
              /* Allocate Tempftp file      */
              /* -------------------------- */
ftpdsn='tempftp.text'
RC=LISTDSI(ftpdsn 'NORECALL')
IF RC>0 THEN DO
  IF RC=16 & SYSREASON=9 THEN               /* Migrated data set */
    "HDELETE" ftpdsn "WAIT"
  "ALLOC DS("ftpdsn") NEW CATALOG",
       "TRACKS SPACE(10 10) UNIT(SYSALLDA)",
       "DSORG(PS) RECFM(V B) LRECL(255)"
  "FREE DS("ftpdsn")"
  END
RC=LISTDSI('TEMPFTP' 'FILE')
IF RC>0 THEN
  "ALLOC DD(TEMPFTP) DS("ftpdsn") OLD DELETE"
            /* -------------------------------- */
            /*     Retrieve ISPF variables      */
            /* -------------------------------- */
ADDRESS 'ISPEXEC'
"CONTROL ERRORS RETURN"
"ISREDIT MACRO (HOST)";  IF rc>0 THEN EXIT rc
IF host='' THEN EXIT 12
"VGET (ZTEMPF ZTEMPN)"
            /* -------------------------------- */
            /* Write the text to tempftp.userid */
            /* -------------------------------- */
IF Routepgm='YES' THEN
  "SELECT PGM(ROUTEPGM) PARM(TEMPFTP)"
ELSE DO
    DO I=1 BY 1
      "ISREDIT (LINE) = LINE" I             /* GET CURRENT LINE */
      IF RC>0 THEN LEAVE
      IF line='' THEN
        QUEUE ' '                           /* blank line */
      ELSE
        QUEUE STRIP(line,'T')
    END
  QUEUE ''
  ADDRESS 'TSO' "EXECIO * DISKW TEMPFTP (FINIS"
  END
/**********************************************************************/
/*                                                                    */
/*             Build the LPR control statements                       */
/*                                                                    */
/**********************************************************************/
ADDRESS 'TSO'
/* -------------------------- */
/* Clear out variables        */
/* -------------------------- */
parse value "" with printer

/* -------------------------- */
/* test for printer           */
/* -------------------------- */
if words(host) > 1 then
   parse value host with host printer .

/* -------------------------- */
/* if printer then use it     */
/* -------------------------- */
if length(printer) = 0 then
   printer='LPT1'

/**********************************************************************/
/*                                                                    */
/*             Invoke the LPR client program                          */
/*                                                                    */
/**********************************************************************/
parm=ftpdsn "HOST" host "PRINTER" printer
IF FastPath='YES' THEN
  cmd="ISPEXEC SELECT PGM(FASTPATH) PARM(EXECPGM,LPR,"parm
ELSE
  cmd="LPR" parm
SAY cmd
cmd
