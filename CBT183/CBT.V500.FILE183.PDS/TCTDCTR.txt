//GILBERTT JOB (ACCT#),TCTDCTR,
// NOTIFY=&SYSUID,
// CLASS=A,MSGCLASS=H,COND=(0,NE)
//*
//HLASM EXEC PGM=ASMA90,PARM=(OBJECT,NODECK,NOESD,NORLD,NOXREF)
*
*
*        EDITION DU NOMBRE D'EXCP SUR TOUS LES FICHIERS DU STEP
*
*        VERSION CHAPEAU DE TCTDCTR, XA 2.2
*
*
$TCTDCTR START 0
$TCTDCTR AMODE 31
         L     R15,6(,R15)             AMODE31
         BSM   0,R15                   AMODE31
         DC    AL4(*+X'80000004')      AMODE31
         SAVE  (14,12),,TCTDCTR-&SYSDATE.-&SYSTIME
         BALR  R12,0                   LOAD BASE REGISTER
         USING *,R12                   SPECIFY IT TO ASSEMBLER
         LR    R14,R13                 SYSTEM SAVE-AREA
         LA    R13,=18C'SAVE'          NEW SAVE-AREA
         ST    R14,4(,R13)             FORWARD CHAIN
         ST    R13,8(,R14)             BACKWARD CHAIN
*
         WXTRN MEMBER
         ICM   R15,B'1111',=A(MEMBER)  PROGRAM TO BE TRACED
         BZ    NOCALL                  NO PROGRAM TO CALL
         BALR  R14,R15                 CALL PROGRAM TO BE TRACED
NOCALL   STH   R15,RETCODE
*
         L     R4,540(,0)              PSATOLD
         USING TCB,R4
*=       C     RTCB,TCBJSTCB           CHECK IF I'M A BATCH PGM
*=       BNE   GOBACK                  QUIT IF TSO
*
         LA    R1,=A(S99RB+X'80000000') SVC99 REQUEST BLOCK    HEADER
         SVC   99                      ALLOCATE SYSDBOUT
         OPEN  MF=(E,OPENL1)           OPEN SYSDBOUT
         LTR   R15,R15                 CHECK OPEN RETURN CODE
         BNZ   GOBACK                  QUIT IF OPEN FAILED
         L     R5,TCBTIO               MY TIOT
         USING TIOT1,R5
         L     R6,X'0224'              MY ASCB
         L     R6,ASCBOUCB-ASCB(,R6)   MY OUCB
         USING OUCB,R6
         L     R7,TCBJSCB              MY JSCB
         USING IEZJSCB,R7
         BAL   R11,PUTDCB2         <== LOCATE OUTPUT RECORD
         STRING '1 JOB=',TIOCNJOB,'  STEP=',(TIOCSTEP+0,8),            X
               '  PROCSTEP=',(TIOCSTEP+8,8),                           X
               '  PGM=',JSCBPGMN,                                      X
               '  PERFORM=',(OUCBNPG,H,L),                             X
               INTO=((R1),121)
         BAL   R11,PUTDCB2             WRITE BLANK LINE
         BAL   R11,PUTDCB2
         STRING '   DDNAME     DISP  VOLSER    EXCP   DATA SET NAME',  X
               34X,'DEVICE     CUA    LABEL',                          X
               INTO=((R1),121)
         BAL   R11,PUTDCB2
         MVI   1(R1),C'-'              LINE OF DASHES
         MVC   2(109,R1),1(R1)         LINE OF DASHES
*LOOP
TIOT_GET CLI   TIOEDDNM,C' '           FREED DD?
         BL    TIOT_NXT                YES, JUMP
         LA    R6,TENWORDS+4           WORK AREA
         ST    R6,TENWORDS             WORK AREA
         USING ZB505,R6
         XC    SWAEPAX,SWAEPAX
         MVC   SWVA,TIOEJFCB           SVA OF THE JFCB
         SWAREQ FCODE=RL,EPA=TENWORDS,MF=(E,SWAREQL1),UNAUTH=YES
         L     R6,SWBLKPTR             31-BIT ADDRESS OF JFCB
         USING INFMJFCB,R6
         MVC   WKCELL1,BLANKS          CLEAR WORK AREA
         MVC   STATUS,BLANKS           CLEAR WORK AREA
         MVC   UNITNAME,BLANKS         CLEAR WORK AREA
         MVC   VOLSER,=C'JES     '     SYSIN/SYSOUT
         SLR   R1,R1                   EXCP=0
         SLR   R7,R7                   NO UCB
         TM    TIOELINK,TIOESSDS       IS THIS A JES DATA SET?
         BO    TIOT61                  YES, JUMP
         MVC   VOLSER,=C'DUMMY   '     DUMMY DATA SET
         ICM   R7,B'0111',TIOEFSRT     UCB ADDRESS
         BZ    TIOT64                  NO UCB, SKIP TCT SCAN
         USING UCBOB,R7
         MVC   UNITNAME+8(4),UCBTYP    DEVICE TYPE
         BAL   R14,GETUNIT         <== GET UNITNAME
         MVC   VOLSER,=C'VIO     '     UNIT=VIO
         TM    UCBOB,UCBVRDEV          IS THIS A VIO DATA SET?
         BO    TIOT41                  YES, JUMP
         MVC   VOLSER,JFCBVOLS         NO, MOVE FIRST VOLSER
*
TIOT41   BAS   R1,TIOT41IC
         DC    C'NEW',AL1(JFCNEW)
         DC    C'MOD',AL1(JFCMOD)
         DC    C'SHR',AL1(JFCOLD+JFCSHARE)
         DC    C'OLD',AL1(JFCOLD)
         DC    C'???',0H'0'
TIOT41TM TM    JFCBIND2,*-*            TEST LABEL TYPE FLAGS
*--LOOP
TIOT41IC IC    R15,3(,R1)              PICK UP MASK FOR "TM"
         EX    R15,TIOT41TM
         BO    TIOT41X
         LA    R1,3+1(,R1)             BUMP TABLE PTR
         CLI   0(R1),C'?'              END OF TABLE?
         BNE   TIOT41IC                NEXT TABLE ENTRY
*--ENDLOOP
TIOT41X  MVC   STATUS,0(R1)            DISP
*
         TM    UCBFL5,UCBSMS           SMS VOL?     X'20'
         BZ    TIOT46                  NO, JUMP
         MVC   WKCELL1(3),=C'SMS'      SMS-MANAGED
*
TIOT46   CLI   UCBTBYT3,UCB3TAPE       TAPE DEVICE?
         BNE   TIOT61                  NO, JUMP
         BAS   R1,TIOT46IC
         DC    C'LTM',AL1(JFCBLTM)
         DC    C'BLP',AL1(JFCBLP)
         DC    C'SUL',AL1(JFCSUL)
         DC    C'AUL',AL1(JFCSUL+JFCBAL)
         DC    C'AL ',AL1(JFCBAL)
         DC    C'NSL',AL1(JFCNSL)
         DC    C'SL ',AL1(JFCSL)
         DC    C'NL ',AL1(JFCNL)
         DC    C'???',0H'0'
TIOT46TM TM    JFCBLTYP,*-*            TEST LABEL TYPE FLAGS
*--LOOP
TIOT46IC IC    R15,3(,R1)              PICK UP MASK FOR "TM"
         EX    R15,TIOT46TM
         BO    TIOT46X
         LA    R1,3+1(,R1)             BUMP TABLE PTR
         CLI   0(R1),C'?'              END OF TABLE?
         BNE   TIOT46IC                NEXT TABLE ENTRY
*--ENDLOOP
TIOT46X  LH    R0,JFCBFLSQ             PICK UP FILE SEQUENCE NUMBER
         LTR   R0,R0                   LABEL=0?
         BNZ   TIOT46Y                 NO, JUMP
         LA    R0,0001                 YES, CHANGE TO LABEL=1
TIOT46Y  STRING ((R0),,L),',',((R1),3),INTO=WKCELL1
*
TIOT61   LA    R0,TIOENTRY             POINT TO CURRENT TIOT ENTRY
         SL    R0,TCBTIO               CHANGE ADDRESS TO OFFSET
         BAL   R14,EXCP00          <== GET EXCP COUNT IN R1
         STRING ((R1),,R5B),INTO=WKCELL2
         CL    R1,=F'100000'           OVER 100K?
         BL    TIOT64                  NO, JUMP
         SLR   R0,R0                   YES, DIVIDE BY 1000
         LA    R1,500(,R1)             YES, DIVIDE BY 1000
         D     R0,=F'1000'             YES, DIVIDE BY 1000
         STRING ((R1),,R4B),'K',INTO=WKCELL2
*
TIOT64   LTR   R7,R7                   DO WE HAVE AN UCB?
         BNZ   TIOT_PRT                YES, JUMP
         LA    R7,BLANKS               NO, POINT TO A DUMMY UCB
TIOT_PRT BAL   R11,PUTDCB2         <== LOCATE OUTPUT RECORD
         STRING $$$,TIOEDDNM,$$$,STATUS,$$$,                          XX
               VOLSER,$$$,(WKCELL2,5),$$$,JFCBDSNM,$$$,               XX
               UNITNAME,$$$,UCBNAME,$$$,WKCELL1,$$$,                  XX
               INTO=((R1),121)
*
TIOT_NXT SLR   R0,R0                   PREPARE IC
         IC    R0,TIOELNGH             LOAD LENGTH OF CURRENT ENTRY
         ALR   R5,R0                   BUMP UP TO NEXT ENTRY
         CLI   TIOELNGH,0              IS THIS THE END?
         BNZ   TIOT_GET                NOT YET, LOOP THROUGH TIOT
*ENDLOOP
         BAL   R11,PUTDCB2             WRITE BLANK LINE
         MVI   1(R1),C'-'              LINE OF DASHES
         MVC   2(109,R1),1(R1)         LINE OF DASHES
         BAL   R11,PUTDCB2             WRITE BLANK LINE
         CLOSE MF=(E,OPENL1)           CLOSE SYSDBOUT (IF NECESSARY)
         FREEPOOL DCB2
*
GOBACK   L     R13,4(,R13)
         L     R14,12(,R13)            RESTORE REGISTERS
         LH    R15,RETCODE
         LM    R0,R12,20(R13)          RESTORE REGISTERS
         BSM   0,R14                   GOBACK WITH CALLER'S AMODE
*----------------------------------------------------------------------
*        ISSUE PUT WITH AMODE=24                                      *
*---------------------------------------------------------------------*
PUTDCB2  LA    R15,PUTDCB2P            AMODE24
         BSM   0,R15                   AMODE24
PUTDCB2P PUT   DCB2
         MVI   0(R1),C' '              ALL SPACES
         MVC   1(120,R1),0(R1)         ALL SPACES
         BSM   0,R11                   GOBACK, RESET AMODE
*---------------------------------------------------------------------*
*        EXCP00: RETRIEVE EXCP COUNT IN TCTIOTSD AREA                 *
*---------------------------------------------------------------------*
EXCP00   L     R1,540(,0)              MY TCB
         L     R1,TCBTCT-TCB(,R1)      TCBTCT
         L     R1,TCTIOTBL-SMFTCT(,R1) START OF I/O MEASUREMENT TABLE
         LA    R1,TCTIODSP-TCTTIOT(,R1) FIRST DD ENTRY
         USING TCTDCBTD,R1
*LOOP
EXCP11   CL    R0,TCTDCBTD             SAME TIOT OFFSET?
         BE    EXCP21                  YES, EXIT LOOP
         ICM   R15,B'1111',TCTDCBLE    END OF TABLE ?
         LA    R1,TCTDCBLE             NEXT LOOKUP TABLE ENTRY
         BNZ   EXCP11                  NOT YET, TRY NEXT DD ENTRY
*ENDLOOP
         SLR   R0,R0                   EXCP COUNT IS ZERO
         B     EXCP99
*
EXCP21   L     R1,TCTIOTSD             OFFSET IN I/O MEASURE. TABLE
         L     R15,540(,0)             MY TCB
         L     R15,TCBTCT-TCB(,R15)    MY TCT
         A     R1,TCTIOTBL-SMFTCT(,R15) START OF I/O MEASUREMENT TABLE
         USING TCTDDENT,R1
         ICM   R0,B'1111',TCTDCTR      EXCP COUNT IN R0
EXCP99   LR    R1,R0                   EXCP COUNT IN R1 TOO
         BR    R14
         DROP  R1
*---------------------------------------------------------------------*
*        CONVERT UCBTYP TO UNITNAME                                   *
*---------------------------------------------------------------------*
GETUNIT  ST    R14,TENWORDS+32         SAVE R14
         MVI   TENWORDS+20,X'01'       RETURN A LOOK-UP VALUE (BIT7)
         MVI   TENWORDS+21,X'00'       CLEAR UNUSED BYTE
         LA    R14,UNITNAME            UNITNAME+DEVTYPE
         LA    R15,TENWORDS+20         FLAGS
         STM   R14,R15,TENWORDS
         OI    TENWORDS+4,X'80'        END-OF-LIST FLAG
         LA    R1,TENWORDS             PARM LIST ADDRESS
         L     R14,CVTPTR              POINT TO CVT
         L     R14,CVTLINK-CVTMAP(,R14) POINT TO LINKLIST DCB
         LINK  SF=(E,IEFEB4UV),DCB=(R14)   GET UNIT NAME
         LTR   R15,R15
         BNZ   GETUNIT6                BAD RETURN CODE, QUIT
         MVI   TENWORDS+20,X'20'       RETURN A UNIT NAME (BIT2)
         LA    R1,TENWORDS             PARM LIST ADDRESS
         LINK  SF=(E,IEFEB4UV)         GET UNIT NAME
         LTR   R15,R15
         BZ    GETUNIT9                GOOD RETURN CODE, GOBACK
GETUNIT6 MVC   UNITNAME,BLANKS         CONVERSION DID NOT WORK
GETUNIT9 L     R14,TENWORDS+32         RESTORE R14
         BR    R14
*---------------------------------------------------------------------*
*        WORK AREAS                                                   *
*---------------------------------------------------------------------*
BLANKS   DC    CL40' '
$$$      DC    C' | '
         PRINT NOGEN
PRINT    OPSYN ANOP
IEFEB4UV LINK  EP=IEFEB4UV,SF=L
DCB2     DCB   DSORG=PS,MACRF=PL,DDNAME=SYSDBOUT,RECFM=FBA,LRECL=121
OPENL1   OPEN  (DCB2,OUTPUT),MF=L
S99RB    DC    A(X'14010000',0,*+12,0,0)
         DC    A(S99TXT01,X'80000000'+S99TXT18)
S99TXT01 DC    AL2(X'01',1,8),CL8'SYSDBOUT '  DDNAME
S99TXT18 DC    AL2(X'18',0)                   SYSOUT=*
RETCODE  DS    H
WKCELL1  DS    D
WKCELL2  DS    D
STATUS   DS    C'OLD'
VOLSER   DS    CL6
UNITNAME DS    CL8,XL4
SWAREQL1 SWAREQ MF=L                   WORK AREA FOR SWAREQ
TENWORDS DS    10F
*-------------------------------------------------------------------
*        DSECTS FOR MVS SYSTEM CONTROL BLOCKS
*               SOME OF THESE MACRO INSTRUCTION WILL BE FOUND
*               IN SYS1.MODGEN INSTEAD OF SYS1.MACLIB.
*-------------------------------------------------------------------
         IKJTCB LIST=NO    TCB MAPPING       TASK CONTROL BLOCK
         IEFTCT            TCT MAPPING       TIMING CONTROL TABLE
TIOT     DSECT
         IEFTIOT1          TIOT MAPPING      TASK INPUT-OUTPUT TABLE
         IKJRB             PRB DSECT         PROGRAM REQUEST BLOCK
         IHACDE            CDE DSECT         CONTENTS DIRECTORY ENTRY
         IEFZB505 LOCEPAX=YES          SWAREQ
         IEFJFCBN                      JOB FILE CONTROL BLOCK
         IEFJESCT                      JES COMMUNICATION TABLE
         IEZJSCB                       JOB STEP CONTROL BLOCK
         CVT   DSECT=YES
         IEFUCBOB LIST=NO,PREFIX=NO    UCB DSECT
         IHAASCB                       ADDRESS SPACE CONTROL BLOCK
         IRAOUCB                       SRM PARMS
         YREGS             REGISTER EQUATES
         STRING GENERATE
         END
//SYSLIB   DD DSN=SYS1.MACLIB,DISP=SHR
//         DD DSN=SYS1.MODGEN,DISP=SHR
//         DD DSN=CBTTAPE.FILE183.PDS,DISP=SHR          STRING
//SYSUT1   DD UNIT=VIO,SPACE=(CYL,5)
//SYSPRINT DD SYSOUT=*
//SYSLIN   DD UNIT=VIO,SPACE=(TRK,1),DISP=(,PASS),BLKSIZE=3200
//*
//GO      EXEC PGM=LOADER,PARM=NOPRINT
//SYSPRINT DD SYSOUT=*
//SYSUDUMP DD SYSOUT=*
//SYSDBOUT DD SYSOUT=*
//SYSLIN   DD DSN=*.HLASM.SYSLIN,DISP=(OLD,PASS)
//
//
//LKED    EXEC PGM=IEWL,PARM='MAP,REUS'
//SYSPRINT DD SYSOUT=*
//SYSLIN   DD DSN=*.HLASM.SYSLIN,DISP=(OLD,PASS)
//SYSUT1   DD UNIT=VIO,SPACE=(CYL,5)
//SYSLMOD  DD DSN=CBTTAPE.FILE183.LOAD(TCTDCTR),DISP=SHR
