/*****************************************************************REXX*/
/*                                                                    */
/* MODULE NAME = FTP                                                  */
/*                                                                    */
/* DESCRIPTIVE NAME = FTP EDIT Macro for ISPF/PDF                     */
/*                                                                    */
/* STATUS = R102                                                      */
/*                                                                    */
/* FUNCTION = The FTP EDIT macro is used to transmit a copy of the    */
/*            data currently being edited to a remote host using      */
/*            text-mode FTP.  This EDIT macro is useful to transmit   */
/*            data that does not exist on disk in a transmittable     */
/*            form, such as packed data, data modified during the     */
/*            current EDIT session, or data only accessible via the   */
/*            EDIT Interface (EDIF).                                  */
/*                                                                    */
/* AUTHOR   =  Gilbert Saint-Flour <carlos@gsf-soft.com>              */
/*                                                                    */
/* Dependencies = TSO/E V2                                            */
/*                ISPF/PDF V3 or ISPF V4                              */
/*                TCP/IP V3                                           */
/*                ROUTEPGM utility R605 (optional)                    */
/*                FASTPATH utility R128 (optional)                    */
/*                                                                    */
/* SYNTAX   =  FTP                                                    */
/*                 host                  remote host or IP address    */
/*                 userid                userid to use                */
/*                                       (default: anonymous)         */
/*                 password              password for userid          */
/*                 dir                   directory to change to       */
/*                                                                    */
/* OPERATION = see below                                              */
/*                                                                    */
/*        FTP creates a temporary data set, copies the data           */
/*        into it, then creates the control data set and issues       */
/*        the TCP/IP FTP command.                                     */
/*                                                                    */
/* CHANGES = see below                                                */
/*                                                                    */
/* 100 New packaging for the CBT tape                                 */
/* 101 added userid/password and dir options - Lionel Dyck            */
/* 102 Replace EXECIO with ROUTEPGM                                   */
/*                                                                    */
/**********************************************************************/
Routepgm='NO'                  /* Use the REXX stack and EXECIO       */
Routepgm='YES'                 /* Use the ROUTEPGM program            */
FastPath='NO'                  /* Invoke the FTP command directly     */
FastPath='YES'                 /* Invoke FTP via the FASTPATH utility */
              /* -------------------------- */
              /* Allocate Tempftp file      */
              /* -------------------------- */
ftpdsn='tempftp.text'
RC=LISTDSI(ftpdsn 'NORECALL')
IF RC>0 THEN DO
  IF RC=16 & SYSREASON=9 THEN
    "HDELETE" ftpdsn "WAIT"
  "ALLOC DS("ftpdsn") NEW CATALOG",
       "TRACKS SPACE(10 10) UNIT(SYSALLDA)",
       "DSORG(PS) RECFM(V B) LRECL(255)"
  "FREE DS("ftpdsn")"
  END
RC=LISTDSI('TEMPFTP' 'FILE')
IF RC>0 THEN
  "ALLOC DD(TEMPFTP) DS("ftpdsn") OLD DELETE"
            /* -------------------------------- */
            /*     Retrieve ISPF variables      */
            /* -------------------------------- */
ADDRESS 'ISPEXEC'
"CONTROL ERRORS RETURN"
"ISREDIT MACRO (HOST)";  IF rc>0 THEN EXIT rc
IF host='' THEN EXIT 12
"VGET (ZTEMPF ZTEMPN)"
            /* -------------------------------- */
            /* Write the text to tempftp.userid */
            /* -------------------------------- */
IF Routepgm='YES' THEN
  "SELECT PGM(ROUTEPGM) PARM(TEMPFTP)"
ELSE DO
    DO I=1 BY 1
      "ISREDIT (LINE) = LINE" I             /* GET CURRENT LINE */
      IF RC>0 THEN LEAVE
      IF line='' THEN
        QUEUE ' '                           /* blank line */
      ELSE
        QUEUE STRIP(line,'T')
    END
  QUEUE ''
  ADDRESS 'TSO' "EXECIO * DISKW TEMPFTP (FINIS"
  END
/**********************************************************************/
/*                                                                    */
/*             Build the FTP control statements                       */
/*                                                                    */
/**********************************************************************/
ADDRESS 'TSO'
/* -------------------------- */
/* Clear out variables        */
/* -------------------------- */
parse value "" with userid password dir

/* -------------------------- */
/* test for userid/password   */
/* -------------------------- */
if words(host) > 1 then
   parse value host with host userid password dir .

QUEUE host

/* -------------------------- */
/* if userid then use it      */
/* -------------------------- */
if length(userid) > 0 then
   QUEUE userid password
else
   QUEUE 'anonymous'

/* -------------------------- */
/* test for dir               */
/* -------------------------- */
if length(dir) > 0 then
   QUEUE 'cd' dir

/* -------------------------- */
/* queue out put command      */
/* -------------------------- */
QUEUE 'put' ftpdsn 'tempftp.'userid()
QUEUE 'quit'
QUEUE ''
"EXECIO * DISKW" ZTEMPN "(FINIS"
/**********************************************************************/
/*                                                                    */
/*             Invoke the FTP client program                          */
/*                                                                    */
/**********************************************************************/
"ALLOC DD(INPUT) OLD DS('"ZTEMPF"') REUSE"
"ALLOC DD(OUTPUT) DS(*)"
IF FastPath='YES' THEN
  "ISPEXEC SELECT PGM(FASTPATH) PARM(EXECPGM,FTP)"
ELSE
  "FTP"
"FREE DD(INPUT OUTPUT)"
