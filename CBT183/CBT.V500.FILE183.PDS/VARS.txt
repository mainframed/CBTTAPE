/*************************************************************** REXX */
/*                                                                    */
/* Module Name: VARS                                                  */
/*                                                                    */
/* Descriptive Name:  Display ISPF variables currently in storage.    */
/*                                                                    */
/* Status: R100                                                       */
/*                                                                    */
/* Function: This REXX program displays the variables                 */
/*           currently in storage using STEMVIEW.                     */
/*                                                                    */
/* Author: Gilbert Saint-Flour <carlos@gsf-soft.com>                  */
/*                                                                    */
/* Environment: ISPF/PDF V3                                           */
/*              STEMVIEW R105                                         */
/*                                                                    */
/* Operation: VARS can be invoked as:                                 */
/*                                                                    */
/*               1. a TSO command:    TSO %VARS                       */
/*                                                                    */
/*               2. an EDIT macro:    VARS                            */
/*                                                                    */
/*               3. a line command on the DSLIST panel                */
/*                                                                    */
/* Sample Output:                                                     */
/*                                                                    */
/*         TLD2 0980C0                                                */
/*           VCB  0A8A8C                                              */
/*             VPL  0B01D8                                            */
/*             VPL  0AFD48                                            */
/*               VDT  0C9DD8 44                                       */
/*                 ZPDMIX__ 28000020                                  */
/*               VDT  0C9BB8 228                                      */
/*                 ZSCML___ 28000020 3 CSR                            */
/*                 MLI2____ 2800001C                                  */
/*                 MLI3____ 2800001C                                  */
/*                 ZMSCPOS_ 28000020 4 0002                           */
/*                 ZEDEXTVN 28000020 28 (ZEDPIMAC ZEDPFLG2 ZEDPFLG3)  */
/*                 ZEDTZCMD 28000020 256 vars                         */
/*                 ZSCED___ 28000020 3 CSR                            */
/*               VDT  0A888C 228                                      */
/*                 Z_______ 6808001C                                  */
/*                 ZLOGON__ 680C0020 8 TSOPROC                        */
/*                 ZENVIR__ 680D0020 32 ISPF 4.4MVS     TSO           */
/*                 ZSCREEN_ 680E0020 1 2                              */
/*                                                                    */
/**********************************************************************/
ADDRESS 'ISPEXEC';"CONTROL ERRORS RETURN";"ISREDIT MACRO"
TCB    = PTR(540)               /* TCB (EXEC COMMAND)    PSATOLD  */
TCB    = PTR(TCB+132)           /* TCB (ISPTASK)         TCBOTC   */
FSA    = PTR(TCB+112)           /* FIRST SAVE AREA       TCBFSA   */
R1     = PTR(FSA+24)            /* ISPTASK'S R1                   */
TLD    = PTR(R1)                /* TLD ADDRESS                    */

l.1 = STORAGE(D2X(tld),4) RIGHT(D2X(tld),6,'0')

vcb    = PTR(TLD+80)            /* vcb ADDRESS                    */
l.2 = ' ' STORAGE(D2X(vcb),4) RIGHT(D2X(vcb),6,'0')

vpl = PTR(vcb+8)                /* first VPL                      */
n=3
DO UNTIL vpl=0
  l.n = '   ' STORAGE(D2X(vpl),4) RIGHT(D2X(vpl),6,'0')
  n=n+1
  vdt = PTR(vpl+12)             /* first VDT                      */
  IF vdt>0 THEN DO UNTIL vdt=0
    vdtlen=C2D(STORAGE(D2X(vdt+6),2))
    l.n = '     ' STORAGE(D2X(vdt),4) RIGHT(D2X(vdt),6,'0') vdtlen
    n=n+1
    i=12
    DO WHILE i<vdtlen
      flags=C2X(STORAGE(D2X(vdt+i+16),4))
      varlen=C2D(STORAGE(D2X(vdt+i+22),2))
      var= STORAGE(D2X(vdt+i+24),varlen)
   /* say  n var d2x(vdt) i vdtlen varlen var */
      addr=PTR(vdt+i)
      IF addr=0 THEN
        l.n = '       ' LEFT(var,8,'_') flags
      ELSE DO
        len=C2D(STORAGE(D2X(vdt+i+12),2))
        l.n = '       ' LEFT(var,8,'_') flags len STORAGE(D2X(addr),len)
        END
      n=n+1
   /* say  n l.n */
      IF varlen>4 THEN i=i+32; ELSE i=i+28
    END
    vdt=PTR(vdt+8)              /* next VDT                       */
  END
  vpl=PTR(vpl+8)                /* next VPL                       */
END

SIGNAL ON SYNTAX                  /*    STEMVIEW NOT AVAILABLE     */
CALL STEMVIEW 'VIEW','l.',1,,"Show variables for",
              STORAGE(D2X(tld),4) RIGHT(D2X(tld),6,'0')
EXIT rc

SYNTAX:                            /* STEMVIEW FUNCTION NOT AVAILABLE */
  error=rc
  'CONTROL DISPLAY LINE START(1)'
  SAY 'REXX error' error 'in line' sigl':' errortext(error)
  SAY sourceline(sigl)
  SAY ''
  DO n=1 TO i-1; SAY l.n ; END
  EXIT

PTR: RETURN C2D(BITAND(STORAGE(D2X(ARG(1)),4),'7FFFFFFF'X))

