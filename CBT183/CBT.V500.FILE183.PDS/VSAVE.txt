/*************************************************************** REXX */
/*                                                                    */
/* MODULE NAME = VIEWSAVE                                             */
/*                                                                    */
/* DESCRIPTIVE NAME = SAVE command for VIEW                           */
/*                                                                    */
/* STATUS   =  R404                                                   */
/*                                                                    */
/* FUNCTION =  This ISPF EDIT macro provides the equivalent of the    */
/*             SAVE command in a VIEW session (SAVE is only available */
/*             in EDIT).  VIEWSAVE uses REPLACE to write the data     */
/*             and LMMSTATS to set the statistics.  This macro        */
/*             supports PDS members and sequential data sets.         */
/*                                                                    */
/*             LMMSTATS is only used if STATS ON is in effect and     */
/*             the member's directory entry contains ISPF statistics. */
/*             In that case, the VERSION, LEVEL, CREATED, INIT and    */
/*             MOD values are set to the values that would have been  */
/*             set by the SAVE command in EDIT.                       */
/*                                                                    */
/* AUTHOR   =  Gilbert Saint-Flour <carlos@gsf-soft.com>              */
/*             http://gsf-soft.com/Freeware/                          */
/*                                                                    */
/* SYNTAX   =  VIEWSAVE <DEBUG>                                       */
/*                                                                    */
/* DEPENDENCIES =  ISPF V4                                            */
/*                                                                    */
/* Change Activity:                                                   */
/*                                                                    */
/*  401   Prevent LMMSTATS failure when zlvers=00                     */
/*  402   Support for sequential data sets (Carl Hafner)              */
/*  403   ISRE017                                                     */
/*  404   Blank Dsname                                                */
/*                                                                    */
/**********************************************************************/
ADDRESS 'ISPEXEC'
"ISREDIT MACRO (DEBUG)"
IF rc>0 THEN EXIT rc                            /* not an EDIT macro */
UPPER debug
"CONTROL ERRORS RETURN"
"ISREDIT (zdsn) = DATASET"
IF zdsn='' THEN DO                                  /* SDSF=SE   */
  VGET zapplid
  Zedsmsg = "Blank Dsname"                       /* Short message    */
  Zedlmsg = "Data Set Name only contains spaces, APPLID=" || zapplid
  "SETMSG MSG(ISRZ001)"                          /* send the message */
  EXIT 12
  END
"ISREDIT (zmemb) = MEMBER"
IF zmemb='' THEN                                    /* DSORG=PS  */
  CALL Process_sequential_data_set
 /*------------------------------------------------------------------*/
 /*     Fast-path processing when STATS=OFF                          */
 /*------------------------------------------------------------------*/
"ISREDIT (stats) = STATS"
IF stats='OFF' THEN DO                              /* STATS=OFF    */
  "ISREDIT REPLACE" zmemb ".ZFIRST .ZLAST"          /* SAVE MEMBER  */
  CALL Check_RC rc,0,'REPLACE'
  "SETMSG MSG(ISRE016)"                       /* member zmemb saved */
  EXIT
  END
 /*------------------------------------------------------------------*/
 /*     Prevent LMMSTATS failure when VERSION=00                     */
 /*------------------------------------------------------------------*/
"ISREDIT (zlvers) = VERSION"                        /* NEW VERSION   */
IF zlvers=0 THEN DO
  Zedsmsg = "Invalid Version"                    /* Short message    */
  Zedlmsg = "Version 00 is not supported by the LMMSTATS service".
  "SETMSG MSG(ISRZ001)"                          /* send the message */
  EXIT 4                    /* not supported in this release */
  END
 /*------------------------------------------------------------------*/
 /*     Retrieve current statistics from member's directory entry    */
 /*------------------------------------------------------------------*/
"ISREDIT (dataid) = DATAID"
"LMOPEN DATAID("dataid")"
CALL Check_RC rc,0,'LMOPEN'
"LMMFIND DATAID("dataid") MEMBER("zmemb") STATS(YES)" /* GET STATS   */
lmmfind_rc=rc                                    /* save return code */
CALL Check_RC rc,8,'LMMFIND'
 /*------------------------------------------------------------------*/
 /*     Rewrite member using the REPLACE command                     */
 /*------------------------------------------------------------------*/
"ISREDIT (n1,n2) = NUMBER"
modlines=0                                        /* modified lines  */
IF n1='ON' & LEFT(n2,1)='S' THEN DO               /* NUMBER ON STD   */
  "ISREDIT (ustat) = USER_STATE"                  /* push user_state */
  CALL Count_modlines                      /* Cound modified lines   */
  "ISREDIT REPLACE" zmemb ".ZF .ZL"               /* SAVE MEMBER     */
  xrc=rc
  "ISREDIT USER_STATE = (ustat)"                  /* pop user_state  */
  CALL Check_RC xrc,0,'REPLACE'
  END
ELSE DO                                           /* NUMBER OFF      */
  "ISREDIT REPLACE" zmemb ".ZF .ZL"               /* SAVE MEMBER     */
  CALL Check_RC rc,0,'REPLACE'
  END
 /*------------------------------------------------------------------*/
 /*     Update statistics using LMMSTATS                             */
 /*------------------------------------------------------------------*/
"ISREDIT (zlvers) = VERSION"                        /* NEW VERSION   */
"ISREDIT (zlmod) = LEVEL"                           /* NEW LEVEL     */
IF lmmfind_rc=8 THEN                                /* new member    */
  "LMMSTATS DATAID("dataid") MEMBER("zmemb")",
      "VERSION("zlvers") MODLEVEL("zlmod")",
      "MODRECS("modlines+0")"
ELSE                                        /* member existed before */
  "LMMSTATS DATAID("dataid") MEMBER("zmemb")",
      "VERSION("zlvers") MODLEVEL("zlmod")",
      "CREATED("zlcdate") INITSIZE("zlinorc")",
      "MODRECS("modlines+0")"
CALL Check_RC rc,0,'LMMSTATS'
 /*------------------------------------------------------------------*/
 /*     Issue completion message and exit                            */
 /*------------------------------------------------------------------*/
"LMCLOSE DATAID("dataid")"
CALL Check_RC rc,0,'LMCLOSE'
"SETMSG MSG(ISRE016)"                         /* member zmemb saved */
EXIT

 /*------------------------------------------------------------------*/
 /*     Count_modlines internal function: Count modified lines       */
 /*------------------------------------------------------------------*/
Count_modlines:
  "ISREDIT NUMBER OFF"
  "ISREDIT (recfm) = RECFM"
  IF recfm='F' THEN
    "ISREDIT (lrecl) = LRECL"
  ELSE
    lrecl=8                                         /* RECFM=V        */
  "ISREDIT (modlines) = LINENUM .ZL"          /* total number of lines*/
  "ISREDIT SEEK '00' ALL" lrecl-1             /*count unmodified lines*/
  IF rc=0 THEN DO
    "ISREDIT (c1,c2) = SEEK_COUNTS"
    modlines=modlines-c1                            /* modified lines*/
    END
  IF debug='DEBUG' THEN
    SAY recfm lrecl xrc rc c1 c2 c3 modlines
  RETURN
 /*------------------------------------------------------------------*/
 /*           Process a sequential data set                          */
 /*------------------------------------------------------------------*/
Process_sequential_data_set:
  "ISREDIT REPLACE '"zdsn"'" .zfirst .zlast
  "SETMSG MSG(ISRE017)"
  EXIT
 /*------------------------------------------------------------------*/
 /*           Error routine                                          */
 /*------------------------------------------------------------------*/
Check_rc:
  IF arg(1)\>arg(2) THEN RETURN                 /* RC=0 */
  "SETMSG MSG("zerrmsg")"
  "LMCLOSE DATAID("dataid")"
  IF debug='DEBUG' THEN
    SAY 'FUNC='arg(3) 'RC='arg(1) 'ZERRMSG='zerrmsg
  EXIT 20
