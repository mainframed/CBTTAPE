/*REXX*/
/*****************************************************************/
/*LISTSVCJ: REXX program to display SVC update journal.          */
/*    Note: Developed and tested on an MVS/SP 2.2.3 system.      */
/*****************************************************************/
/* MVS DATA AREAS:                                               */
/*                CVT - Communications Vector Table (@'10'x)     */
/*               SCVT - Supervisor Call Vector Table             */
/*               SVCR - SVC Recording Table                      */
/*                                                               */
/*                                                               */
/*****************************************************************/
@cvt = STORAGE(10,4)                  /* get CVT address from PSAAOLD */
@cvt = D2X(C2D(@cvt))                    /* binary to hex             */
@cvtabend = STORAGE(D2X(X2D(@cvt)+200),4)    /* get CVTABEND address  */
@cvtabend = D2X(C2D(@cvtabend))                /* binary to hex       */
@scvtsvcr = D2X(X2D(@cvtabend)+136)          /* SCVT addressibility   */
@scvtsvcr = STORAGE(@scvtsvcr,4)                  /* SVCR table       */
@scvtsvcr = D2X(C2D(@scvtsvcr))                /* binary to hex       */
Say ' '
Say ' '
Say 'The CVT  is located at '@cvt '('X2D(@cvt)')'
Say 'The CVTABEND field points to '@cvtabend '('X2D(@cvtabend)')'
Say 'The SCVTSVCR field points to '@scvtsvcr '('X2D(@scvtsvcr)')'
Say ' '
Say 'The SVC Update Journal table is at '@scvtsvcr '('X2D(@scvtsvcr)')'
Say ' '
hits = 0
Do i = 0 to 255                                 /* loop thru storage  */
   bytes = STORAGE(D2X(X2D(@scvtsvcr)+(i*24)),24)
   bytes = C2X(bytes)
   If SUBSTR(bytes,1,16) \= '0000000000000000'
      Then Do
         hits = hits + 1
         Say ' '
  Say '  SVC 'RIGHT(i,3,0) 'updated.  Old SVCTABLE entry: 'SUBSTR(bytes,1,16)
  Say '           Updating pgm return address: 'SUBSTR(bytes,17,8)
  Say '                    New SVC EP address: 'SUBSTR(bytes,26,8)
  Say '                         Times updated: 'X2D(SUBSTR(bytes,41,4))
         If SUBSTR(bytes,35,5) = '00000'
            Then Say '                   Date of last update: unknown'
       Else Say '                   Date of last update: 'SUBSTR(bytes,35,5)
      End
End
Say ' '
Say 'SVCTABLE entries showing updates in the journal = 'hits
Exit
