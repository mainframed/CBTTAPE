/*REXX*/
/*****************************************************************/
/*LISTPART: REXX program to show ASM paging data set status.     */
/*    Note: Developed and tested on an MVS/SP 2.2.3 system.      */
/*****************************************************************/
/* MVS DATA AREAS:                                               */
/*                CVT - Communications Vector Table (@'10'x)     */
/*              ASMVT - ASM Vector Table                         */
/*               PART - ASM Paging Activity Reference Table      */
/*                                                               */
/*****************************************************************/
@cvt = STORAGE(10,4)                  /* get CVT address from PSAAOLD */
@cvt = D2X(C2D(@cvt))                         /* binary to dec to hex */
@cvtasmvt = STORAGE(D2X(X2D(@cvt)+704),4)     /* get CVTASMVT address */
@asmvt    = RIGHT(C2X(@cvtasmvt),7)                   /* trim address */
@asmpart  = STORAGE(D2X(X2D(@asmvt)+8),4)         /* get PART address */
@part     = RIGHT(C2X(@asmpart),7)                    /* trim address */
partsize  = STORAGE(D2X(X2D(@part)+4),4)      /* get number of PART's */
partsize  = C2D(partsize)                            /* binary to dec */
parteuse  = STORAGE(D2X(X2D(@part)+8),2)      /* get # of PARTs in use*/
parteuse  = C2D(parteuse)                            /* binary to dec */
@partdsnl = STORAGE(D2X(X2D(@part)+24),4)     /* get dsn list address */
@partdsnl = C2X(@partdsnl)                            /* need 32 bits */
partlcnt  = STORAGE(D2X(X2D(@part)+32),2)     /* get number of locals */
partlcnt  = C2D(partlcnt)                            /* binary to dec */
@partplpa = STORAGE(D2X(X2D(@part)+48),4)      /* get plpa part entry */
@partplpa = RIGHT(C2X(@partplpa),7)                   /* trim address */
@partloca = STORAGE(D2X(X2D(@part)+52),4)      /* get 1st local entry */
@partloca = RIGHT(C2X(@partloca),7)                   /* trim address */
@partlsta = STORAGE(D2X(X2D(@part)+56),4)      /* get last locl entry */
@partlsta = RIGHT(C2X(@partlsta),7)                   /* trim address */
@partents  = STORAGE(D2X(X2D(@part)+80),4)      /* get 1st part entry */
@partents = RIGHT(C2X(@partents),7)                   /* trim address */
Address TSO ; "CLEAR"
Say 'CVT:'@cvt' CVTASMVT:'@asmvt ' ASMPART:'@part'   'TIME() DATE()
Say ' '
Say 'The PART table starts at '@partplpa' and contains 'partsize' entries,'parte
Say ' '
Say ' # Type   Full   Stat  VolSer Addr DataSet Name'
Say ' '
p = 0 ; i = 0 ; @partix = @partplpa ; @dsnl = @partdsnl
Do p = 1 To partsize                           /* scan the PART table */
   pareflg1 = STORAGE(D2X(X2D(@partix)+9),1)         /* get flag byte */
   If BITAND(pareflg1,'80'x) \= '80'x Then Do  /* process in use only */
      i = i + 1                              /* count active pageds's */
      pagedsn  = STORAGE(@dsnl,44)         /* pickup the dsn from csa */
      paretype = STORAGE(D2X(X2D(@partix)+8),1)      /* get type byte */
      ptype = '??????'
      If BITAND(paretype,'80'x) = '80'x Then ptype = 'PLPA  '
      If BITAND(paretype,'40'x) = '40'x Then ptype = 'Common'
      If BITAND(paretype,'20'x) = '20'x Then ptype = 'Duplex'
      If BITAND(paretype,'10'x) = '10'x Then ptype = 'Local '
      pareszsl = STORAGE(D2X(X2D(@partix)+16),4) /* get size in slots */
      pareszsl = C2D(pareszsl)
      pareslta = STORAGE(D2X(X2D(@partix)+20),4)   /* slots available */
      pareslta = C2D(pareslta) ; used = pareszsl - pareslta
      pctused  = TRUNC((used*100)/pareszsl,0)
      pctused  ='('RIGHT(pctused,3)'%)'
      @pareucbp = STORAGE(D2X(X2D(@partix)+44),4)  /* get UCB address */
      pioaddr  = STORAGE(D2X(C2D(@pareucbp)+4),2)  /* channel address */
      pioaddr  = RIGHT(C2X(pioaddr),4)             /* 4 character UCB */
      pvolser  = STORAGE(D2X(C2D(@pareucbp)+28),6)     /* dasd volser */
      Say RIGHT(i,2) ptype RIGHT(pareszsl,5) pctused pvolser pioaddr pagedsn
   End
   @partix = D2X(X2D(@partix)+96)          /* bump to next PART entry */
   @dsnl   = D2X(X2D(@dsnl)+44)             /* bump to next DSN entry */
End
Say ' '
Say 'Paging activity table analysis completed: 'TIME()
Exit
