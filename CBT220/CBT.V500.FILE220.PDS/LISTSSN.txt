/*REXX*/
/*****************************************************************/
/* LISTSSN: REXX program to show all MVS subsystems.             */
/*    Note: Developed and tested on an MVS/SP 2.2.3 system.      */
/*****************************************************************/
/* MVS DATA AREAS:                                               */
/*                CVT - Communications Vector Table (@'10'x)     */
/*              JESCT - JES Communications Table                 */
/*              SSCVT - Subsystem Communications Vector Table    */
/*               SSVT - Subsystem vector Table                   */
/*                                                               */
/*****************************************************************/
@cvt = STORAGE(10,4)                  /* get CVT address from PSAAOLD */
@cvt = D2X(C2D(@cvt))                         /* binary to dec to hex */
@cvtjesct = STORAGE(D2X(X2D(@cvt)+296),4)     /* get CVTJESCT address */
@cvtjesct = D2X(C2D(@cvtjesct))               /* binary to dec to hex */
@jesct    = STORAGE(D2X(X2D(@cvtjesct)+24),4)    /* get JESCT address */
@jesct    = D2X(C2D(@jesct))                  /* binary to dec to hex */
Address TSO ; "CLEAR"
Say 'CVT:'@cvt' JESCT:'@jesct' SSCVT chain trace started: 'TIME() DATE()
Say ' '
i = 0 ; endofchn = 0 ; j = 0 ; @ssct = @jesct ; e = 0
ina = 0 ; inacsnam = '' ; inaline1 = ''
Say '    Snam Fnum  Sysevents Monitored'
Say '    ---- ----  -----------------------------------------------------------'
Do While endofchn = 0 & i < 50
   i = i + 1                                 /* count all SSCTs found */
   @scta   = STORAGE(D2X(X2D(@ssct)+4),4)    /* get next SSCT address */
   @scta   = D2X(C2D(@scta))                     /* bin to dec to hex */
   snam    = STORAGE(D2X(X2D(@ssct)+8),4)     /* get subsystem's name */
   @ssvt   = STORAGE(D2X(X2D(@ssct)+16),4)        /* get SSVT address */
   @ssvt   = D2X(C2D(@ssvt))                     /* bin to dec to hex */
   If @ssvt = 0 Then Do           /* make note of inactive subsystems */
      ina = ina + 1
      inacsnam = inacsnam snam           /* build primary output line */
      If LENGTH(inacsnam) >= 70 Then Do   /* logic for 2 output lines */
         inaline1 = inacsnam
         inacsnam = ''
         End
      End
   If @ssvt \= 0 Then Do             /* detail only active subsystems */
      j = j + 1                                  /* count active ones */
      sysevents = ''                    /* SYSEVENTS output code line */
      fnum = STORAGE(D2X(X2D(@ssvt)+2),2)      /* number of functions */
      fnum = C2D(fnum)
      fmat = STORAGE(D2X(X2D(@ssvt)+4),256)   /* function code matrix */
      Do e = 1 to 256          /* scan fmat, load sysevents code line */
         If C2D(SUBSTR(fmat,e,1)) \= 0 Then sysevents = sysevents e
      End
      Say RIGHT(j,3,' ') snam'  'RIGHT(fnum,2,0)'  'sysevents
      End
   @ssct = @scta
   If @ssct = 0 Then endofchn = 1    /* zero address ends chain */
End
If ina > 0 Then Do
   Say ' '
   Say RIGHT(ina,3,' ') 'Subsystems defined, no SSVT built:'
   If LENGTH(inaline1) \= 0 Then Do
      Say '     'inaline1
      If LENGTH(inacsnam) \= 0 Then Say '     'inacsnam
      End
      Else Say '     'inacsnam
   End
Say '---'
Say RIGHT(i,3,' ')' SSCVT blocks scanned.  Completed: 'TIME()
Exit
