  /* REXX ************************************************************/
  /*                                                                 */
  /* EXEC:      CHECKGRP                                             */
  /*                                                                 */
  /* AUTHOR:    Richard Haydock                                      */
  /*                                                                 */
  /* WRITTEN:   Aug 1992                                             */
  /*                                                                 */
  /* CALLED BY: TSO users                                            */
  /*                                                                 */
  /* PURPOSE:   Check whether user belongs to a specified group      */
  /*                                                                 */
  /* FUNCTION:  Input a RACF group to be tested                      */
  /*                                                                 */
  /*            Optionally, input another user. By default, the      */
  /*            user who called CHECKGRP will be tested.             */
  /*                                                                 */
  /*            The output from an LU command is scanned.            */
  /*                                                                 */
  /*            If one of the user's connect groups matches the one  */
  /*            input then rc = 0 otherwise rc = 8                   */
  /*                                                                 */
  /* CODES   :  0 - user is connected to group being checked         */
  /*            8 - user is definitely not connected to the group    */
  /*           12 - non-zero RC from LU command (not authorised to   */
  /*                list user, or unknown user specified)            */
  /*                                                                 */
  /*******************************************************************/
  arg group user .

  if user = "" then user = userid()

  trapvar = outtrap("temp.",'*',"noconcat")
  "LU" user
  list_rc = rc
  trapvar = outtrap('off')

  if list_rc Â¬= 0 then exit 12

  do count = 1 to temp.0
    testword = " GROUP=" || group || " "
    x = pos(testword,temp.count)
    if x = 0
      then
        iterate
      else
        exit 0                              /* group found           */
  end

  exit 8

