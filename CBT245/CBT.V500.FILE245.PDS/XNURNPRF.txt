  /* REXX ************************************************************/
  /*                                                                 */
  /* EXEC        : XNURNPRF                                          */
  /*                                                                 */
  /* AUTHOR      : Richard Haydock                                   */
  /*                                                                 */
  /* LAST UPDATE : 12/10/92                                          */
  /*                                                                 */
  /* PURPOSE     : Back up current ISPF profile                      */
  /*               Rename new ISPF profile to live name              */
  /*                                                                 */
  /* COMMENTS    : Must be run outside ISPF                          */
  /*                                                                 */
  /* REXX ************************************************************/
  if sysvar("SYSISPF") = "ACTIVE" then
    do
      say "This command must be run outside ISPF"
      exit
    end
  "FINDGRP"
  "FINDSYS"

  /*                                                                 */
  /* 1) Check what ISPPROF is currently in use                       */
  /*                                                                 */

  x      = listdsi("ISPPROF" "FILE")
  curdsn = "'"sysdsname"'"
  bakdsn = "'"sysdsname".OLD'"
  newdsn = "'NUTSO."userid()"."sysid".ISPPROF.NEW'"
  nxtdsn = "'NUTSO."userid()"."sysid".ISPPROF'"
  if sysdsn(newdsn) ¬= "OK" then
    do
      newdsn = "'"grpid"."userid()"."sysid".ISPPROF.NEW'"
      nxtdsn = "'"grpid"."userid()"."sysid".ISPPROF'"
    end

  say "Deleting old copy of" bakdsn "(if it exists)"
  "DELETE" bakdsn

  say "Renaming" curdsn "to" bakdsn "..."
  "RENAME" curdsn bakdsn
  if rc ¬= 0 then
    do
      say "Rename failed - terminating"
      say "Contact x87709 for advice if necessary"
      exit
    end

  say "Renaming" newdsn "to" nxtdsn "..."
  "RENAME" newdsn nxtdsn
  if rc ¬= 0 then
    do
      say "Rename failed - terminating"
      say "Contact x87709 for advice if necessary"
      exit
    end
  else
    do
      say "Renaming worked OK. Now do one of the following:"
      say ""
      say "a) Type the command %STARTUP to resume ISPF"
      say "b) Log off and then log on again"
      say ""
      say "DO NOT type ISPF and expect it to work properly ..."
      say ""
    end
