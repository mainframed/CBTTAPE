  /* REXX ************************************************************/
  /*                                                                 */
  /* EXEC:      STARTUP                                              */
  /*                                                                 */
  /* AUTHOR:    CTS/MVS/RNH                                          */
  /*                                                                 */
  /* WRITTEN:   SEPTEMBER 1991                                       */
  /*                                                                 */
  /* CALLED BY: LIVE LOGON PROCEDURES                                */
  /*                                                                 */
  /* PURPOSE:   SEE STEPS OUTLINED BELOW                             */
  /*                                                                 */
  /*                                                                 */
  /* STEP1                                                           */
  /*            SET UP VARIABLES AND DATASET NAMES                   */
  /*                                                                 */
  /*            FIND THE SYSTEM ID AND PROJECT_PREFIX VALUES         */
  /*                                                                 */
  /*                                                                 */
  /* STEP2_ALLOC_ISPPROF      (SUBROUTINE)                           */
  /*                                                                 */
  /*            ALLOCATE ISPF PROFILE DATASET (CREATE IF NEEDED)     */
  /*                                                                 */
  /* STEP2_COPY_OLDPROF       (SUBROUTINE)                           */
  /*                                                                 */
  /*            COPY MEMBERS FROM OLD ISPF PROFILE TO A NEW ONE      */
  /*                                                                 */
  /*                                                                 */
  /* STEP3                                                           */
  /*            IF OPT = 'RESET' THEN RUN REXX 'ROGR'                */
  /*                                                                 */
  /*                                                                 */
  /* STEP4_PROCESS_PARM_FILES  (SUBROUTINE)                          */
  /*                                                                 */
  /*          - DETERMINE IF THE USER IS AN END USER, AND IF SO      */
  /*            ALLOCATE THE ISPPROF BEFORE OTHER DATASETS           */
  /*          - CHECK EXISTENCE OF USER, PROJECT AND MASTER PARAMETER*/
  /*            TABLES                                               */
  /*          - CHECK EXISTENCE OF ENDUSER PARAMETER TABLE IF USER   */
  /*            BELONGS TO A NUXXP OR NUXXT GROUP                    */
  /*          - CALL STEP4_READ_PARMS AND STEP4_PROCESS_PARMS FOR    */
  /*            EACH TABLE FOUND                                     */
  /*          - CALL 'PDFBASIC' REXX IF MASTER TABLE NOT AVAILABLE   */
  /*            AND SET FLAG TO SHOW THIS HAS BEEN DONE              */
  /*          - ALLOCATE DATASETS                                    */
  /*                                                                 */
  /*        +++ SEE STEP4 COMMENT BOX FOR PERMISSIBLE KEYWORDS   +++ */
  /*                                                                 */
  /* STEP4_READ_PARMS          (SUBROUTINE)                          */
  /*                                                                 */
  /*            READ CONTENTS OF A PARAMETER TABLE INTO A STEM       */
  /*                                                                 */
  /* STEP4_PROCESS_PARMS       (SUBROUTINE)                          */
  /*                                                                 */
  /*          - PROCESS ALL PARAMETERS USING THE STEM                */
  /*            CREATED BY THE PREVIOUS ROUTINE                      */
  /*          - CALL 'STEP4_MERGE_FILE' WHEN THE KEYWORD IS 'ALLOC'  */
  /*          - ISSUE ERROR MESSAGE FOR AN INVALID KEYWORD           */
  /*                                                                 */
  /* STEP4_MERGE_FILE          (SUBROUTINE)                          */
  /*                                                                 */
  /*            PROCESS A PARAMETER TABLE RECORD WHOSE KEYWORD IS    */
  /*            'ALLOC'                                              */
  /*                                                                 */
  /*                                                                 */
  /* STEP5                                                           */
  /*            PUT ISPF PROFILE DATASET AT TOP OF ISPTLIB CONCAT    */
  /*                                                                 */
  /*                                                                 */
  /* STEP6                                                           */
  /*            CHECK RC - INVOKE REXX 'ROGR' IF RC > 0              */
  /*                                                                 */
  /*                                                                 */
  /* STEP7                                                           */
  /*            ISSUE 'ISPSTART' WITH PARM OF REXX 'ROGS'            */
  /*            UNLESS EMERGENCY STARTUP HAS BEEN USED               */
  /*                                                                 */
  /*        +++ PROCESS 'PREREQ.' STEM IF THIS HAS BEEN USED  +++    */
  /*                                                                 */
  /*                                                                 */
  /* STEP8_START_COMMAND                                             */
  /*                                                                 */
  /*            INVOKE ISPF UNLESS ANOTHER COMMAND WAS SPECIFIED     */
  /*            IN WHICH CASE, RUN THE COMMAND                       */
  /*                                                                 */
  /*        +++ PROCESS 'POSTREQ.' STEM IF THIS HAS BEEN USED +++    */
  /*                                                                 */
  /*            CHECK LOGOFF_INDICATOR AND LOG OFF TSO IF SET TO "Y" */
  /*                                                                 */
  /*******************************************************************/
  /*                                                                 */
  /*            ARGUMENTS - PASSED BY THE LOGON PROCEDURE            */
  /*            ----------                                           */
  /*                                                                 */
  /* OPT      : IF 'RESET' IS USED, THIS RESETS ISPF PROFILE VALUES  */
  /*                                                                 */
  /*                                                                 */
  /*            VARIABLES - SOME CAN BE RESET ACCORDING TO CONTENTS  */
  /*            ---------   OF PARAMETER DATASETS                    */
  /*                                                                 */
  /* APPL            : APPL_ID FOR ISPSTART                          */
  /*                   (INITIALISED TO 'ISR')                        */
  /*                                                                 */
  /* ENDUSER_PREFIX  : PREFIX OF END-USER MASTER PARAMETER TABLE     */
  /*                                                                 */
  /* PDFBASIC        : EMERGENCY-START PARAMETER FILE IF NO OTHER    */
  /*                   VALID PARAMETER TABLES FOUND OR ERRORS OCCUR  */
  /*                                                                 */
  /* ISPF_PANEL      : START PANEL FOR ISPF                          */
  /*                   (INITIALISED TO 'ISR@PRIM')                   */
  /*                                                                 */
  /* LOGOFF_INDICATOR: SET TO 'Y' IF KEYWORD 'LOGOFF' IS FOUND IN    */
  /*                   ANY PARAMETER TABLE                           */
  /*                   (INITIALISED TO 'N')                          */
  /*                   LOGOFF AFTER "ISPF" OR "START_COMMAND" IF SET */
  /*                                                                 */
  /* MEMBER_NAME     : NAME OF PARAMETER DATASET MEMBER TO USE       */
  /*                                                                 */
  /* MASTER_PREFIX   : PREFIX OF MASTER PARAMETER TABLE              */
  /*                                                                 */
  /* PARMFILE_SUFFIX : PRODUCTION-SUFFIX FOR ALL PARAMETER TABLE     */
  /*                                                                 */
  /* POSTREQ.        : POSSIBLE POSTREQUISITE COMMAND STEM           */
  /*                   (INITIALLY EMPTY)                             */
  /*                   RUN AFTER "ISPF" OR "START_COMMAND" IF SET    */
  /*                                                                 */
  /* PREREQ.         : POSSIBLE PREREQUISITE COMMAND STEM            */
  /*                   (INITIALLY EMPTY)                             */
  /*                   RUN BEFORE "ISPF" OR "START_COMMAND" IF SET   */
  /*                                                                 */
  /* PROJECT_PREFIX  : PREFIX OF PROJECT PARAMETER TABLE             */
  /*                                                                 */
  /* STANDARD        : FLAG TO SAY WHETHER STANDARD STARTUP IS USED  */
  /*                   (INITIALISED TO 'YES')                        */
  /*                                                                 */
  /* START_COMMAND   : START COMMAND                                 */
  /*                   (INITIALISED TO 'ISPF')                       */
  /*                                                                 */
  /* USERID          : ID OF USER LOGGING ON                         */
  /*                                                                 */
  /*******************************************************************/
  /*                                                                 */
  /* THE PARAMETER TABLENAMES ARE SET UP IN THE FOLLOWING WAY:       */
  /* --------------------------------------------------------        */
  /*                                                                 */
  /* MASTER  : MASTER_PREFIX.PARMFILE_SUFFIX(MEMBER_NAME)            */
  /*                                                                 */
  /* ENDUSER : ENDUSER_PREFIX.PARMFILE_SUFFIX(MEMBER_NAME)           */
  /*           (ONLY CHECKED FOR NUXXT, NUXXP, NUADS OR NUUPS USERS) */
  /*                                                                 */
  /* PROJECT : PROJECT_PREFIX."MASTER".PARMFILE_SUFFIX(MEMBER_NAME)  */
  /*                                                                 */
  /* USER    : PROFDSN(MEMBER_NAME)                                  */
  /*                                                                 */
  /* TESTS FOR DUPLICATE DATASETS IN THE SAME CONCATENATION          */
  /* TESTS FOR EXISTENCE OF DATASETS BEFORE THEY ARE ALLOCATED       */
  /* THE ENDUSER, PROJECT AND USER PARMS NEED NOT EXIST              */
  /* EMERGENCY STARTUP IS USED IF NO MASTER PARMS ARE FOUND          */
  /*                                                                 */
  /*******************************************************************/

  ADDRESS TSO

  ARG OPT                                /* GET OPTION IF SPECIFIED  */

  X = TIME(R)                            /* RECORD START TIME        */

  "FINDSYS"                              /* WHAT SYSTEM IS THIS ?    */
  SYSTEM = SUBSTR(SYSID,4,1)             /* USE 4TH CHARACTER        */

  /*---------------  DEFINE GLOBAL VARIABLES  -----------------------*/

  APPL             = "ISR"
  DS_COUNT         = 0
  DS_COUNT_T       = 0
  DSN_#_MAX        = 1
  DSNTAB.          = ""
  PDFBASIC         = "'SYS1.ISRCLIB(PDFBASIC)'"
  ENDUSER_FLAG     = "N"
  ENDUSER_PREFIX   = "SYSA.OP" || SYSTEM || "TSOEU"
  F_COUNT          = 0
  FILE_#_MAX       = 0
  FILEDATA         = ""
  FILETAB.         = ""
  ISPF_PANEL       = "ISR@PRIM"
  ISPFTEST         = ""
  LOGOFF_INDICATOR = "N"
  LOGON_PROC       = SYSVAR("SYSPROC")
  MASTER_PREFIX    = "SYSA.OP" || SYSTEM || "TSO"

  MEMBER_SUFFIX    = ""
  IF SUBSTR(LOGON_PROC,5,3) = "DB2" THEN
    MEMBER_SUFFIX  = "DB2"
  IF SUBSTR(LOGON_PROC,1,7) = "TSOTEST" THEN
    MEMBER_SUFFIX  = SUBSTR(LOGON_PROC,8,1)
  MEMBER_NAME      = "TSO" || SYSTEM || MEMBER_SUFFIX

  PARMFILE_SUFFIX  = "ISPTLIB"
  STANDARD         = "YES"
  START_COMMAND    = "ISPF"
  STOP_INDICATOR   = ""
  POSTREQ.0        = 0
  PREREQ.0         = 0
  USERID           = SYSVAR("SYSUID")

  "FINDGRP"
  PROJECT_PREFIX = GRPID

  /*------------------  MAIN CONTROL OF REXX   ----------------------*/

  "PROFILE PREFIX("USERID")"

  CALL STEP2_ALLOC_ISPPROF
  "ALLOCATE F(ISPPROF) DA('"PROFDSN"') SHR REUSE"

  IF OPT = "RESET" THEN "%ROGR"

  CALL STEP4_PROCESS_PARM_FILES

  LRC = LISTDSI("ISPTLIB" "FILE")
  IF SYSDSNAME ¬= PROFDSN THEN
    "CONCAT F(ISPTLIB) DA('" || PROFDSN || "') SHR"

  IF RC > 1 THEN
    DO
      SAY "CONCAT OF ISPF PROFILE TO ISPTLIB FAILED"
      "%ROGR"
      EXIT
    END

  IF ISPFTEST ¬= "" THEN
    SAY "- ISPF WILL BE STARTED IN" ISPFTEST "MODE"
  "ISPSTART  CMD(%ROGS " || OPT || ") NEWAPPL(AAAA)" ISPFTEST

  DO COUNT = 1 TO PREREQ.0
    INTERPRET '"' || PREREQ.COUNT || '"'
  END
  "K"

  "%CHECKGRP NUMANTSO"                      /* MANAGEMENT TSO ?      */
  IF RC = 0 & START_COMMAND = "ISPF"
    THEN
      START_COMMAND = "%MANTSO"

  STEP8_RC =  STEP8_START_COMMAND()

  DO COUNT = 1 TO POSTREQ.0
    INTERPRET '"' || POSTREQ.COUNT || '"'
  END

  IF LOGOFF_INDICATOR = "Y" THEN "LOGOFF"

  EXIT 0

  /*------------------  END EXECUTION          ----------------------*/

  /*******************************************************************/
  /*                                                                 */
  /* ROUTINE   : STEP2_ALLOC_ISPPROF                                 */
  /*                                                                 */
  /* PURPOSE   : ALLOCATE ISPPROF DATASET                            */
  /*                                                                 */
  /* PROCESSING: CHECK WHETHER 'NUTSO.<USERID>.<SYSID>.ISPPROF'      */
  /*             EXISTS. IF SO, THIS IS THE VALUE OF 'PROFDSN'       */
  /*                                                                 */
  /*             IF NOT, CHECK WHETHER                               */
  /*             '<PROJECT_PREFIX>.<USERID>.<SYSID>.ISPPROF'         */
  /*             EXISTS, CREATE IT IF NOT                            */
  /*                                                                 */
  /*             IF A NEW PROFILE IS CREATED, SEE IF THERE IS A      */
  /*             DATASET 'PROJECT_PREFIX.USERID.ISPPROF'             */
  /*                                                                 */
  /*             IF SO, CALL ROUTINE 'STEP2_COPY_OLDPROF' TO COPY    */
  /*             ALL MEMBERS INTO THE NEW PROFILE DATASET            */
  /*                                                                 */
  /*             ALLOCATE THE PROFILE TO DDNAME 'ISPPROF'            */
  /*                                                                 */
  /*******************************************************************/

STEP2_ALLOC_ISPPROF:

  PROFDSN = "NUTSO."USERID"."SYSID".ISPPROF"
  IF SYSDSN("'"PROFDSN"'") = "OK" THEN RETURN 0

  PROFDSN = PROJECT_PREFIX"."USERID"."SYSID".ISPPROF"
  OLDPROF = PROJECT_PREFIX"."USERID".ISPPROF"

  IF SYSDSN("'"PROFDSN"'") = "OK" THEN RETURN 0

  "ALLOCATE F(PROF) DA('"PROFDSN"') NEW,
  UNIT(SYSALLDA) TRACKS SPACE(5,1) LRECL(80) CATALOG,
  RECFM(F B) BLKSIZE(15760) DSORG(PO) DIR(45) REUSE"

  IF RC ¬= 0 THEN
    DO
      SAY "ERROR CREATING ISPF PROFILE" PROFDSN "FOR" USERID
      EXIT
    END

  "FREE F(PROF)"
  SAY "ISPF PROFILE" PROFDSN "CREATED FOR" USERID
  IF SYSDSN("'"OLDPROF"'") = "DATASET NOT FOUND" THEN RETURN 0

  COPY_RC = STEP2_COPY_OLDPROF(OLDPROF PROFDSN)

RETURN 0

  /*******************************************************************/
  /*                                                                 */
  /* ROUTINE  : STEP2_COPY_OLDPROF                                   */
  /*                                                                 */
  /* CALLED BY: STEP2_ALLOC_ISPPROF                                  */
  /*                                                                 */
  /* PURPOSE  : COPY CONTENTS OF OLD PROF TO NEW IF NECESSARY        */
  /*                                                                 */
  /* RETURNS  :  0 - OK                                              */
  /*             8 - IEBCOPY FAILURE                                 */
  /*                                                                 */
  /*******************************************************************/

STEP2_COPY_OLDPROF:

  ARG OLDPROF PROFDSN

  SAY "ABOUT TO COPY FROM" OLDPROF "TO" PROFDSN

  "ALLOC F(PROFIN)   DA('"OLDPROF"') SHR REUSE"
  "ALLOC F(PROFOUT)  DA('"PROFDSN"') SHR REUSE"
  "ALLOC F(SYSPRINT) SYSOUT(X) REUSE"
  "ALLOC F(SYSIN)    SPACE(1)  TRACKS         REUSE ",
                    "LRECL(80) BLKSIZE(15440) RECFM(F B) DSORG(PS) "

/* ----------------------------------------------------------------- */
/* PUT COMMANDS TO DRIVE IEBCOPY ONTO STACK, WRITE STACK TO TEMP DS. */
/* ----------------------------------------------------------------- */

  DELSTACK
  QUEUE "     COPY    OUTDD=PROFOUT,INDD=PROFIN"
  QUEUE ""
  "EXECIO * DISKW SYSIN (FINIS"

/* ----------------------------------------------------------------- */
/* RUN IEBCOPY. THIS IS AN AUTHORISED PROGRAM, AND NEEDS TSOEXEC.    */
/* ----------------------------------------------------------------- */

  "TSOEXEC CALL 'SYS1.LINKLIB(IEBCOPY)' "
  IF RC = 0
    THEN
      SAY "ALL MEMBERS COPIED FROM" OLDPROF "TO" PROFDSN
    ELSE
      SAY "ERROR COPYING MEMBERS FROM" OLDPROF "TO" PROFDSN

/* ------------------------------------------------------------------ */
/* FREE/REALLOCATE THE FILES USED BY IEBCOPY.                         */
/* ------------------------------------------------------------------ */

  "FREE  F(PROFIN PROFOUT)"
  "ALLOC F(SYSIN)    DA(*) REUSE"
  "ALLOC F(SYSPRINT) DA(*) REUSE"

RETURN 0

  /*******************************************************************/
  /*                                                                 */
  /* ROUTINE   : STEP4                                               */
  /*                                                                 */
  /* PURPOSE   : CHECK IF THE USER IS AN END USER. SET THE END USER  */
  /*             FLAG IF SO AND ALLOCATE THE ISPPROF DATASET         */
  /*             CHECK FOR EXISTENCE OF MASTER, PROJECT & USER PARMS */
  /*             ISSUE 'STANDARD STARTUP' MESSAGE IF NO PROJECT PARMS*/
  /*             EMERGENCY START IF NO MASTER PARMS                  */
  /*             ALLOCATE DATASETS AND SET UP OPTIONS AFTER SCANNING */
  /*             PARAMETER TABLES                                    */
  /*                                                                 */
  /* PROCESSING: CHECK WHETHER OPT IS 'RESET' AND INVOKE REXX 'ROGR' */
  /*             IF SO                                               */
  /*                                                                 */
  /* KEYWORDS  : ALLOC    - DEFINE PERMANENT DATASET TO BE ALLOCATED */
  /*             COMMAND  - DEFINE COMMAND TO ISSUE IN STEP8         */
  /*             DUMMY    - DDNAME TO ALLOCATE TO DUMMY              */
  /*             ISPFTEST - CAUSES ISPF TO BE INVOKED IN TEST MODE   */
  /*             LOGOFF   - CAUSES AUTO-LOGOFF ON RETURN FROM STEP8  */
  /*             PANEL    - NAME OF ISPF PRIMARY OPTION PANEL TO USE */
  /*             POSTREQ  - DEFINE COMMAND TO ISSUE AFTER STEP8      */
  /*             PREREQ   - DEFINE COMMAND TO ISSUE BEFORE STEP8     */
  /*             STOP     - STOPS SUBSEQUENT PARM TABLES BEING USED  */
  /*             SYSOUT   - DDNAME TO ALLOCATE TO SYSOUT             */
  /*             TEMPDSN  - DEFINE TEMPORARY DATASET TO BE ALLOCATED */
  /*             TERMINAL - DDNAME TO ALLOCATE 'TERM=TS'             */
  /*             WILDCARD - COMMAND TO BE EXECUTED AS SOON AS READ   */
  /*                                                                 */
  /*******************************************************************/

STEP4_PROCESS_PARM_FILES:

  /*=================================================================*/
  /*  SEE IF USER IS LOGGING ON WITH A LIVE ENDUSER GROUP (NU??P),   */
  /*  TEST ENDUSER GROUP (NU??T), NUADS GROUP OR AS A NUUPS GROUP    */
  /*  IF SO, PUT THE ISPPROF D/S AT THE TOP OF 'ISPPLIB' AS THEY     */
  /*  USE IT FOR THEIR PERSONAL MENUS AND PANELS                     */
  /*=================================================================*/

  USER_TYPE = SUBSTR(PROJECT_PREFIX,5,1)
  IF USER_TYPE = "P" | USER_TYPE = "T" |,
     PROJECT_PREFIX = "NUADS" | PROJECT_PREFIX = "NUUPS" THEN
      DO
        ENDUSER_FLAG="Y"
        CNTL.0 = 1
        CNTL.1 = "ALLOC    ISPPLIB  " || PROFDSN
        CALL STEP4_PROCESS_PARMS     /* ISPF PROFILE => ISPPLIB  */
      END

  /*=================================================================*/
  /*  CHECK FOR EXISTENCE OF USER PARAMETER TABLE                    */
  /*  PROCESS IF FOUND                                               */
  /*=================================================================*/

  CNTLDSN = "'" || PROFDSN || "(" || MEMBER_NAME || ")'"

  IF SYSDSN(CNTLDSN) = "OK" THEN
    DO
      STANDARD = "NO"
      CALL STEP4_READ_PARMS
      CALL STEP4_PROCESS_PARMS
    END

  /*=================================================================*/
  /*  CHECK FOR EXISTENCE OF GROUP PARAMETER TABLE                   */
  /*  PROCESS IF FOUND                                               */
  /*  ISSUE 'STANDARD STARTUP' MESSAGE IF NOT FOUND                  */
  /*=================================================================*/

  IF STOP_INDICATOR = "" THEN
    DO
      CNTLDSN = "'" || PROJECT_PREFIX || ".MASTER." ||,
        PARMFILE_SUFFIX || "(" || MEMBER_NAME || ")'"

      IF SYSDSN(CNTLDSN) = "OK" THEN
        DO
          STANDARD = "NO"
          CALL STEP4_READ_PARMS
          CALL STEP4_PROCESS_PARMS
        END
      ELSE
        IF ISPFTEST = "TEST" THEN
          SAY CNTLDSN "NOT PROCESSED -" SYSDSN(CNTLDSN)
    END                                  /* OF IF STOP_INDICATOR...  */

  /*=================================================================*/
  /*  CHECK FOR EXISTENCE OF END-USER PARAMETER TABLE IF 5TH CHAR    */
  /*  OF PROJECT_PREFIX IS "T" OR "P" OR PROJECT_PREFIX IS "NUADS"   */
  /*  OR PROJECT_PREFIX IS "NUUPS"                                   */
  /*  PROCESS IF FOUND                                               */
  /*  ISSUE ERROR MESSAGE IF NOT FOUND                               */
  /*=================================================================*/

  IF ENDUSER_FLAG = "Y" THEN
      DO
        CNTLDSN = "'" || ENDUSER_PREFIX || "." ||,
          PARMFILE_SUFFIX || "(" || MEMBER_NAME || ")'"
        IF SYSDSN(CNTLDSN) = "OK" THEN
          DO
            STANDARD = "NO"
            CALL STEP4_READ_PARMS
            CALL STEP4_PROCESS_PARMS     /* PROCESS ENDUSER PARMS    */
          END
        ELSE
          IF ISPFTEST = "TEST" THEN
            SAY CNTLDSN "NOT PROCESSED -" SYSDSN(CNTLDSN)
      END                                /* OF IF SUBSTR(PROJECT...  */

  /*=================================================================*/
  /*  CHECK FOR EXISTENCE OF MASTER PARAMETER TABLE                  */
  /*  PROCESS IF FOUND                                               */
  /*  INVOKE EMERGENCY STARTUP IF NOT FOUND                          */
  /*=================================================================*/

  IF STOP_INDICATOR = "" THEN
    DO
      CNTLDSN = "'" || MASTER_PREFIX || "." ||,
        PARMFILE_SUFFIX || "(" || MEMBER_NAME || ")'"

      IF SYSDSN(CNTLDSN) = "OK" THEN
        DO
          CALL STEP4_READ_PARMS
          CALL STEP4_PROCESS_PARMS
        END
      ELSE
        DO
          SAY "-" SYSDSN(CNTLDSN) "==>" CNTLDSN
          SAY "- CONTACT MVS TEAM FOR ADVICE"
          "EXEC " PDFBASIC
          "ISPSTART  CMD(%ROGS " || OPT || ") NEWAPPL(AAAA)" ISPFTEST
          "K"
          "ISPSTART PANEL(" || ISPF_PANEL || ") NEWAPPL(",
            || APPL || ")" ISPFTEST
          EXIT
        END
    END                                  /* OF IF STOP_INDICATOR...  */

  IF ISPFTEST = "TEST" THEN
    IF STANDARD = "YES" THEN SAY "STANDARD STARTUP WILL BE USED"

  /*----------------------------------------------*/
  /* FIND DATASETS FOR FILES                      */
  /*----------------------------------------------*/

  FILE_# = WORDS(FILEDATA)
  FILE = ""
  DSN  = ""

  DO F_#   = 1 TO FILE_#_MAX BY 1
    FILE   = WORD(FILETAB.F_#,1)
    D_#    = 1
    CONCAT = 0

    DO UNTIL DSNTAB.F_#.D_# = ""
      RACFDSN =  WORD(DSNTAB.F_#.D_#,1)
      DSN = DSN || " " || RACFDSN
      DS_COUNT = DS_COUNT+1
      CONCAT = CONCAT + 1
      D_#    = D_#    + 1
    END                                  /* OF DO UNTIL DSNTAB       */

    "ALLOCATE F(" || FILE || ") DA(" DSN ") SHR REUSE"
    IF ISPFTEST ¬= "" THEN
      DO
        SAY  DS_COUNT "DATASET(S) OF" CONCAT "WERE",
          "ALLOCATED TO FILE(" || FILE || ")"
        DS_COUNT_T = DS_COUNT_T + DS_COUNT
        F_COUNT = F_COUNT + 1
        DS_COUNT = 0
      END                                /* OF DO OF ISPFTEST        */

    FILE = ""
    DSN  = ""
  END                                    /* OF F_# = 1 TO FILE       */

  IF ISPFTEST ¬= "" THEN
    SAY DS_COUNT_T "DATASETS WERE ALLOCATED TO" F_COUNT "FILES"

  RETURN 0

  /*******************************************************************/
  /*                                                                 */
  /* ROUTINE   : STEP4_READ_PARMS                                    */
  /*                                                                 */
  /* PURPOSE   : READ ALL PARAMETER RECORDS IN A PARM TABLE          */
  /*                                                                 */
  /* PROCESSING: READ ALL RECORDS FROM PARAMETER TABLE INTO A STEM   */
  /*             CALLED 'CNTL'                                       */
  /*                                                                 */
  /*******************************************************************/

STEP4_READ_PARMS:

  "ALLOCATE F(CNTL) DA(" || CNTLDSN || ") SHR REUSE"
  "EXECIO * DISKR CNTL (STEM CNTL. FINIS"
  "FREE F(CNTL)"

  RETURN 0

  /*******************************************************************/
  /*                                                                 */
  /* ROUTINE   : STEP4_PROCESS_PARMS                                 */
  /*                                                                 */
  /* PURPOSE   : PROCESS EACH PARAMETER RECORD IN A PARM TABLE       */
  /*                                                                 */
  /* PROCESSING: CHECK KEYWORD IN COLUMNS 1-8 OF PARM D/S RECORD     */
  /*             IF 'ALLOC' THEN CALL STEP4_MERGE_FILE               */
  /*             PROCESS OTHER VALID KEYWORD TYPES                   */
  /*             ISSUE AN ERROR MESSAGE FOR ANY INVALID KEYWORD      */
  /*                                                                 */
  /*******************************************************************/

STEP4_PROCESS_PARMS:

  DO C_# = 1 TO CNTL.0 BY 1
    KEYWORD = SUBSTR(CNTL.C_#,1,8)
    SELECT

      WHEN KEYWORD = "ALLOC   " THEN
        CALL STEP4_MERGE_FILE

      WHEN KEYWORD = "ALLOCATE" THEN
        CALL STEP4_MERGE_FILE

      WHEN KEYWORD = "PANEL   " THEN
        ISPF_PANEL     = SUBSTR(CNTL.C_#,10,8)

      WHEN KEYWORD = "COMMAND " THEN
        DO
          IF START_COMMAND = "ISPF" THEN
            START_COMMAND  = SUBSTR(CNTL.C_#,10,71)
        END

      WHEN KEYWORD = "PREREQ  " THEN
        DO
          PREREQ.0     = PREREQ.0 + 1
          TEMP         = PREREQ.0
          PREREQ.TEMP  = SUBSTR(CNTL.C_#,10,71)
        END

      WHEN KEYWORD = "POSTREQ " THEN
        DO
          POSTREQ.0    = POSTREQ.0 + 1
          TEMP         = POSTREQ.0
          POSTREQ.TEMP = SUBSTR(CNTL.C_#,10,71)
        END

      WHEN KEYWORD = "TEMPDSN " THEN
        DO
          TEMPDDN  = SUBSTR(CNTL.C_#,10,8)
          TEMPATTR = SUBSTR(CNTL.C_#,19,62)
          TEMPATTR = "NEW UNIT(SYSDA) TRACKS" TEMPATTR
          "ALLOCATE F(" || TEMPDDN || ")" TEMPATTR
          IF RC > 0 THEN
            SAY "ERROR - TEMPDDN " DDNAME "ALREADY ALLOCATED"
        END

      WHEN KEYWORD = "DUMMY" THEN
        DO
          DUMMYDD = WORD(SUBSTR(CNTL.C_#,10,8),1)
          "ALLOCATE F(" || DUMMYDD || ") DUMMY REUSE"
          IF RC > 0 THEN
            SAY "ERROR - DDNAME " DUMMYDD "NOT ALLOCATED"
        END

      WHEN KEYWORD = "SYSOUT  " THEN
        DO
          SYSDDN  = SUBSTR(CNTL.C_#,10,8)
          SYSATTR = SUBSTR(CNTL.C_#,19,62)
          SYSATTR = "SYSOUT" SYSATTR
          "ALLOCATE F(" || SYSDDN || ")" SYSATTR "REUSE"
          IF RC > 0 THEN
            SAY "ERROR - SYSOUT " SYSDDN "NOT ALLOCATED"
        END

      WHEN KEYWORD = "TERMINAL" THEN
        DO
          TERMDD  = WORD(SUBSTR(CNTL.C_#,10,8),1)
          "ALLOCATE F(" || TERMDD || ") DA(*) REUSE"
          IF RC > 0 THEN
            SAY "ERROR - DDNAME " TERMDD "NOT ALLOCATED"
        END

      WHEN KEYWORD = "LOGOFF  " THEN
        LOGOFF_INDICATOR = "Y"

      WHEN KEYWORD = "STOP    " THEN
        STOP_INDICATOR = "STOP"

      WHEN KEYWORD = "ISPFTEST" THEN
        ISPFTEST = "TEST"

      WHEN KEYWORD = "WILDCARD" THEN
        INTERPRET SUBSTR(CNTL.C_#,10,71)

      OTHERWISE
        SAY "INVALID KEYWORD - " KEYWORD

    END                                  /* OF SELECT                */

  END                                    /* OF DO C_# = 1 TO CNTL.0  */

  RETURN 0

  /*******************************************************************/
  /*                                                                 */
  /* ROUTINE   : STEP4_MERGE_FILE                                    */
  /*                                                                 */
  /* PURPOSE   : PROCESS A PARAMETER RECORD WITH THE KEYWORD 'ALLOC' */
  /*                                                                 */
  /* PROCESSING: ADD SPECIFIED DATASET TO THE APPROPRIATE LIST PRIOR */
  /*             TO ALLOCATION                                       */
  /*                                                                 */
  /*******************************************************************/

STEP4_MERGE_FILE:

  FILE       = SUBSTR(CNTL.C_#,10,8)
  DSN        = "'" || WORD(SUBSTR(CNTL.C_#,19,44),1) || "'"
  IF SYSDSN(DSN) = "OK" THEN
    DO
      NEW_FILE   = "YES"
      DOBBLE     = "NO"
      DO F_NO = 1 TO FILE_#_MAX BY 1
        IF FILE = FILETAB.F_NO THEN
          DO
            NEW_FILE = "NO"
            /* NEXT CHECK STOPS CONCATENATIONS FOR THESE DDNAMES    */
            IF FILE = "ISPTABL " | FILE = "SMPTABL" | FILE = "ISPFILE",
            THEN
              DO
                SAY "ONLY 1 DATASET SHOULD BE ALLOCATED TO" FILE
                SAY DSN "WILL NOT BE ALLOCATED"
              END
            ELSE
              DO
                DSN_# = 1
                DO WHILE DSNTAB.F_NO.DSN_# ¬= ""
                  IF DSNTAB.F_NO.DSN_# = DSN THEN
                    DOBBLE = "YES"
                  DSN_# = DSN_# + 1
                END                      /* OF WHILE DSNTAB          */
                IF DOBBLE = "NO" THEN
                  DSNTAB.F_NO.DSN_# = DSN
              END
          END                            /* OF IF FILE = FILE        */
      END                                /* OF F_NO = 1 TO MAX       */
      IF NEW_FILE = "YES" THEN
        DO
          FILE_#_MAX = FILE_#_MAX + 1
          FILETAB.FILE_#_MAX = FILE
          DSNTAB.FILE_#_MAX.1 = DSN
        END                              /* OF NEW_FILE = YES        */
    END                                  /* OF SYSDSN = OK           */
  ELSE
    DO
      SAY DSN "===>" SYSDSN(DSN)
      SAY "- WILL NOT BE CONCATENATED TO" FILE
    END                                  /* OF SYSDSN ¬= OK          */

  RETURN 0

  /*******************************************************************/
  /*                                                                 */
  /* ROUTINE   : STEP8_START_COMMAND                                 */
  /*                                                                 */
  /* PURPOSE   : INVOKE ISPF OR ALTERNATIVE COMMAND IF SPECIFIED     */
  /*                                                                 */
  /* PROCESSING: PROCESS COMMAND CONTAINED IN THE VARIABLE           */
  /*             START_COMMAND                                       */
  /*                                                                 */
  /*******************************************************************/

STEP8_START_COMMAND:

  IF ISPFTEST ¬= "" THEN
    SAY "STARTUP ENDED AT " TIME() "FOR" USERID,
      || " <ELAPSED TIME " SUBSTR(TIME(R),1,4) "SECONDS>"

  IF START_COMMAND = "ISPF" THEN
    "ISPSTART PANEL(" || ISPF_PANEL || ") NEWAPPL(",
      || APPL || ")" ISPFTEST
  ELSE
    START_COMMAND

  RETURN RC

