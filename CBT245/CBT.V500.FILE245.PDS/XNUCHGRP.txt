  /* REXX ************************************************************/
  /*                                                                 */
  /* EXEC:      XNUCHGRP                                             */
  /*                                                                 */
  /* AUTHOR:    Richard Haydock                                      */
  /*                                                                 */
  /* WRITTEN:   Aug 1992                                             */
  /*                                                                 */
  /* CALLED BY: TSO users                                            */
  /*                                                                 */
  /* PURPOSE:   Display a list of all groups a user belongs to       */
  /*                                                                 */
  /*            Identify RACF default group and current connect      */
  /*            group                                                */
  /*                                                                 */
  /*            Allow the user to select one of the groups as a new  */
  /*            RACF default group as long as it:                    */
  /*                                                                 */
  /*            1) Begins with NU                                    */
  /*            2) Is 5 characters long                              */
  /*                                                                 */
  /* FUNCTION:  List the user to determine the RACF default group    */
  /*                                                                 */
  /*            Find the user's current connect group from the ACEE  */
  /*                                                                 */
  /*            Find all the groups the user is connected to by      */
  /*            checking through the connect group table             */
  /*                                                                 */
  /*            Build an ISPF table (temporary) with one row per     */
  /*            group. The groups are checked one by one and when    */
  /*            the default and current connect groups are found,    */
  /*            the corresponding rows are updated to show this.     */
  /*                                                                 */
  /*            The final ISPF table is displayed using panel        */
  /*            NUCHGRP. The user can select ONE of the groups as    */
  /*            the new RACF default group if necessary.             */
  /*                                                                 */
  /*******************************************************************/

/*                                                                   */
/*                  Find RACF default group                          */
/*                                                                   */

  trapvar = outtrap("temp.",'*',"noconcat")
  "LU" userid()
  trapvar = outtrap('off')

  do i = 1 to temp.0
    if substr(temp.i,2,13) = "DEFAULT-GROUP" then
      do
        default = word(substr(temp.i,16,8),1)
        leave
      end
    else
        iterate
  end

/*                                                                   */
/*                  Find the ACEE via the ASCB and ASXB              */
/*                                                                   */

  ascb              = storage(220,4)     /* ASCB address             */
  decimal_ascb      = c2d(ascb)          /* decimal ascb             */

  asxb_addr_dec     = decimal_ascb + 108 /* decimal asxb address     */
  asxb_address      = d2x(asxb_addr_dec) /* asxb address             */
  asxb              = storage(asxb_address,4)   /* asxb              */
  decimal_asxb      = c2d(asxb)          /* decimal asxb             */

  acronym_address   = d2x(decimal_asxb)
  acronym           = storage(acronym_address,4)

  if acronym = "ASXB"
    then
      nop
    else
      do
        say "ASXB not found - terminating"
        exit 4
      end

  acee_addr_dec     = decimal_asxb + 201 /* decimal ACEE address     */
  acee_address      = d2x(acee_addr_dec) /* ACEE address             */
  acee              = storage(acee_address,3)   /* acee              */
  decimal_acee      = c2d(acee)          /* decimal acee             */

  acronym_address   = d2x(decimal_acee)
  acronym           = storage(acronym_address,4)

  if acronym = "ACEE"
    then
      nop
    else
      do
        say "ACEE not found - terminating"
        exit 4
      end

/*                                                                   */
/*                  Find current connect group                       */
/*                                                                   */

grouplen_addr_d     = decimal_acee + 29  /* decimal group address    */
group_addr_d        = decimal_acee + 30  /* decimal group address    */
/*                                                                   */
grouplen_addr       = d2x(grouplen_addr_d) /*       group address    */
group_addr          = d2x(group_addr_d)    /*       group address    */
/*                                                                   */
group_length        = storage(grouplen_addr,1)
group_length_d      = c2d(group_length)
/*                                                                   */
current             = storage(group_addr,8)
current             = substr(current,1,group_length_d)

/*                                                                   */
/*                  Find start of connect group table                */
/*                                                                   */

  grouptab_pointer_d  = decimal_acee + 108
  grouptab_pointer    = d2x(grouptab_pointer_d)

  grouptab_address_c  = storage(grouptab_pointer,4)
  grouptab_address    = c2x(grouptab_address_c)
  grouptab_address_d  = c2d(grouptab_address_c)

  grouptab_fixed      = storage(grouptab_address,32)
  grouptab_total_x    = substr(grouptab_fixed,9,2)
  grouptab_total_d    = c2d(grouptab_total_x)

/*                                                                   */
/*                  Set up and process an ISPF table                 */
/*                                                                   */

  address ISPEXEC
  "CONTROL ERRORS RETURN"
  "TBEND    TEMP"
  "TBCREATE TEMP KEYS(CONGROUP) ",
               "NAMES(DFTGROUP CURGROUP COMMENTS)",
               "NOWRITE REPLACE"
  "TBSORT   TEMP FIELDS(CONGROUP,C,A)"

  /*******************************************************************/
  /*                                                                 */
  /*      Position 24 bytes before first entry in the table          */
  /*                                                                 */
  /*******************************************************************/

  grouptab_entry_d    = grouptab_address_d + 8

  do count = 1 to grouptab_total_d

    grouptab_entry_d    = grouptab_entry_d + 24
    congroup_addr       = d2x(grouptab_entry_d)
    congroup            = storage(congroup_addr,8)
    congroup            = word(congroup,1)

    if substr(congroup,1,1) = "D" & congroup ¬= default then iterate

    if congroup = default
      then
        dftgroup = "YES"
      else
        dftgroup = ""

    if congroup = current
      then
        curgroup = "YES"
      else
        curgroup = ""

    comments = ""
    "TBADD    TEMP ORDER"

  end                                       /* of do count...        */

  trow = 1                                  /* Display selected rows */

/*********************************************************************/
/*                                                                   */
/*            Keep processing display until user presses PF3         */
/*                                                                   */
/*********************************************************************/

  do forever
    "TBTOP    TEMP"
    "TBSKIP   TEMP NUMBER("trow")"
    "TBDISPL  TEMP PANEL(NUCHGRP) CURSOR(ZCMD)",
                 " CSRROW("trow") AUTOSEL(NO)"
    if rc = 8 then leave                    /* if PF3 pressed        */

    trow    = ZTDTOP

    do while ztdsels > 0
      if ztdsels > 1 then
        do
          comments = "** ERROR ** - only one selection allowed"
          "TBPUT TEMP"
          "SETMSG MSG(SPUM013)"
          leave
        end

      if opt = "S" then                     /* Process selected group*/
        do

          grplen = length(congroup)

          select
            when congroup = dfltgrp then
              do
                comments = "Selected group is already the default"
                "TBPUT TEMP"
                leave
              end

            when congroup = "NUSSSTS" then
              do
                comments = "Terminal supervisor group"
                "TBPUT TEMP"
                "SETMSG MSG(SPUM014)"
                leave
              end

            when substr(congroup,grplen-2,3) = "LUS" then
              do
                comments = "LUS control group selected"
                "TBPUT TEMP"
                "SETMSG MSG(SPUM015)"
                leave
              end

            when substr(congroup,1,2) ¬= "NU" | ,
                 length(congroup) ¬= 5 then
              do
                comments = "This is not a TSO group"
                "TBPUT TEMP"
                "SETMSG MSG(SPUM012)"
                leave
              end

            otherwise                       /* OK - carry on         */
              do
                address TSO "ALU" userid() "DFLTGRP("congroup")"
                if rc = 0 then
                  do
                    dftgroup = "YES"
                    comments = "This is your new default group"
                    "TBPUT TEMP"

                    "CONTROL  DISPLAY  SAVE"
                    old_congroup = congroup
                    old_curgroup = curgroup
                    congroup     = default
                    "TBTOP    TEMP"
                    "TBSCAN   TEMP ARGLIST(congroup) POSITION(POS)"
                    dftgroup = ""
                    comments = "Previous default group"
                    "TBPUT    TEMP"
                    curgroup = old_curgroup
                    congroup = old_congroup
                    default  = congroup     /* Set new default group */
                    "CONTROL  DISPLAY  RESTORE"

                  end
                else
                  do
                    comments = "** ERROR ** Change failed RC" rc
                    "TBPUT TEMP"
                  end
              end                           /* of 'otherwise'        */

          end                               /* of select             */

        end                                 /* of option 'S'         */

    if ztdsels = 1 then leave
    "TBDISPL  TEMP"

    end                                     /* of do while ztdsels   */

  end                                       /* of do forever         */

  "TBEND    TEMP"

  exit 0

