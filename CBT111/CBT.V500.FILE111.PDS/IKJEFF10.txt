//S40DA01S JOB (11613109,0402,1,1,,LONG,,,104),PURDY,CLASS=H,
//            MSGCLASS=X
//ASM     EXEC PGM=IEV90,
//             PARM='NODECK,OBJ,LINECNT=99,BUFSIZE(MAX),RENT',
//             REGION=4096K
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR
//         DD  DSN=SYS1.AMODGEN,DISP=SHR
//SYSUT1   DD  DSN=&&SYSUT1,UNIT=VIO,SPACE=(1700,(600,100))
//SYSUT2   DD  DSN=&&SYSUT2,UNIT=VIO,SPACE=(1700,(300,50))
//SYSUT3   DD  DSN=&&SYSUT3,UNIT=VIO,SPACE=(1700,(300,50))
//SYSPRINT DD  SYSOUT=*,DCB=BLKSIZE=1089
//SYSPUNCH DD  DUMMY
//SYSLIN   DD  DSN=&&OBJSET,UNIT=SYSSQ,SPACE=(80,(200,50)),
//             DISP=(MOD,PASS)
//SYSIN    DD  *
IKJEFF10 TITLE 'REVLON IKJEFF10 TSO SUBMIT EXIT - V1.2'
IKJEFF10 CSECT
         USING *,R15
         SPACE
***********************************************************************
*                                                                     *
* REVLON IKJEFF10 TSO SUBMIT EXIT V1.2                                *
*                                                                     *
* THIS EXIT WILL CONDITIONALLY CHECK THE JOB CARD SUBMITTED TO VERIFY *
* THAT THE JOBNAME STARTS WITH THE USERID OR FIRST N CHAR OF USERID.  *
* THE EXECUTION CLASS, MSGCLASS, AND TIME PARAMETERS ARE ALSO         *
* CONDITIONALLY CHECKED AND MAY BE MANDITORY AND/OR IN THE APPROPRIATE*
* TABLES.  A TIME PARAMETER MAY ALSO BE INSERTED BY THE EXIT.         *
* ALL CHECKING IS BASED ON THE USERID AND THE PRIVILIGES ALLOWED THAT *
* USER IN THE UIDTABLE.                                               *
*                                                                     *
* JOB CLASSES MAY HAVE FLAGS TO REQUIRE TIME PARAMETERS OR REQUIRE NOT*
* TO HAVE THEM. ALSO, THERE MAY BE MAX TIME AND DEFAULT TIME BY CLASS.*
*                                                                     *
* COMMAND AND CERTAIN JES 2 CONTROL CARDS WILL NOT BE ALLOWED IN THE  *
* SUBMITTED JCL.                                                      *
*                                                                     *
* THE SCHEME IS TO TURN ON BITS IN FLAG BYTES WHEN WE FIND SOMETHING  *
* AND SAVE THE INFO, THEN CHECK THE FLAGS AND INFO AFTER WE HAVE      *
* THE ENTIRE JOB CARD.  THE CHECKING SECTION WILL THEN TURN ON FLAGS  *
* TO ABORT AND SEND MESSAGES.  THE WRAPUP SECTION WILL DO THE MESSAGE *
* SENDING AND AS EACH MESSAGE IS SENT, TURN OFF THE FLAG THAT CAUSED  *
* THE MESSAGE TO BE SENT.  WHEN ALL FLAGS ARE OFF, THE WRAPUP CODE    *
* WILL EITHER ALLOW THE SUBMIT TO COMPLETE OR WILL ABORT THE SUBMIT.  *
*                                                                     *
* THIS EXIT MUST BE LINKEDITED WITH THE NAME IKJEFF10 INTO            *
* SYS1.LINKLIB WITH THE ATTRIBUTES OF REUSABLE, REENTRANT AND         *
* REFRESHABLE.                                                        *
*                                                                     *
* SEE GC28-0629 - TSO SPL - FOR FURTHER DETAILS ON SUBMIT EXIT.       *
*                                                                     *
***********************************************************************
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3    ---  BASE REG
R4       EQU   4    ===> INPUT PARM
R5       EQU   5    ===> GOTTEN WORK AREA
R6       EQU   6    ===> CURRENT STATEMENT
R7       EQU   7
R8       EQU   8    ===> USERID RULE TABLE
R9       EQU   9    ===> MESSAGE AREA
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13   ===> GOTTEN REG SAVE AREA
R14      EQU   14
R15      EQU   15
         EJECT
**********************************************************************
*                                                                    *
*        SETUP AND SEE WHY WE WERE CALLED                            *
*                                                                    *
**********************************************************************
         SPACE 2
         SAVE  (14,12),,REVLON.IKJEFF10.V1.1.&SYSDATE..&SYSTIME
         LR    R3,R15
         SPACE
         DROP  R15
         USING IKJEFF10,R3
         SPACE
         L     R4,0(R1)      LOAD POINTER TO PASSED PARM LIST
         USING IEDSECTD,R4   ESTABLISH ADDRESSABILITY TO PARM LIST
         SPACE
         GETMAIN R,LV=AREAREGS GET AREA FOR REGISTER SAVE
         SPACE
         LR    R10,R1        SAVE POINTER TO GOTTEN AREA
         MVI   0(R10),X'00'  CLEAR GOTTEN AREA
         MVC   1(AREAREGS-1,R10),0(R10)
         ST    R13,4(R10)    EXCHANGE THE REGISTER SAVE AREA
         ST    R10,8(R13)    POINTERS TRICK
         LR    R13,R10
         SPACE
         L     R5,IEEXITWD   LOAD EXIT WORD FROM PARM LIST
         LTR   R5,R5         CHECK FOR FIRST TIME IN EXIT
         BNZ   AFTERSET      ... MUST HAVE BEEN HERE BEFORE
         SPACE
SETUP    GETMAIN R,LV=AREAWORK GET AREA FOR EXIT WORK - FIRST TIME ONLY
         SPACE
         USING WORKAREA,R5   ESTABLISH ADDRESSABILITY TO WORK AREA
         LR    R5,R1         SAVE POINTER TO GOTTEN AREA
         ST    R5,IEEXITWD   SAVE POINTER IN EXIT WORD IN PARM LIST
         MVI   0(R5),X'00'   CLEAR GOTTEN AREA
         MVC   1(255,R5),0(R5)
         MVC   257(AREAWORK-257,R5),256(R5)
         SPACE
         USING IESUBCTD,R7   ESTABLISH ADDRESSABILITY TO FLAG DSECT
AFTERSET L     R6,IECARDP    LOAD POINTER TO CURRENT STATEMENT
         L     R7,IESUBCTP   LOAD FLAGS POINTER
         OI    IETAKEEX,IETJOB+IETJES+IETCMD TURN ON EXIT FOR CMDS+JOB
         SPACE
         LTR   R6,R6         SEE IF READY TO INSERT CARD
         BZ    DOINSERT      GO INSERT NEW CARD
         SPACE
         L     R9,IEMSGP     LOAD POINTER TO MESSAGE AREA
         LA    R10,MESSAGE   LOAD ADDRESS OF MESSAGE AREA
         CR    R9,R10        SEE IF MESSAGE WAS SENT BY THIS EXIT
         BE    WRAPUP        ... YES WE ARE SENDING MESSAGES-GO FINISH
         SPACE
         TM    IESTMTP2,IESCOMNT SEE IF INVALIDLY CALLED FOR COMMENT
         BO    RETURN0       THIS EXIT ONLY VALIDATES JOBCARD INFO
         TM    IESTMTYP,IESCMD  VERIFY EXIT CALLED FOR COMMAND
         BO    COMMAND       GO CHECK WHAT THIS JOB SHOULDNT DO
         TM    IESTMTP2,IESJES  VERIFY EXIT CALLED FOR JES CNTL
         BO    JESCTL        GO CHECK WHAT THIS JOB SHOULDNT DO
         TM    IESTMTYP,IESJOB  VERIFY EXIT CALLED FOR JOB CARD
         BNO   RETURN0       THIS EXIT ONLY VALIDATES JOBCARD INFO
         EJECT
**********************************************************************
*                                                                    *
*        SETUP TO SCAN JOBCARD                                       *
*                                                                    *
**********************************************************************
         SPACE 2
*        NOW WE MUST BE HERE TO VALIDATE A JOBCARD
         SPACE
         TM    IESTMTYP,IESCONTN SEE IF CONTINUATION OF JOB CARD
         BO    JOBCARD2      ... MUST HAVE BEEN HERE BEFORE
         DROP  R7            DROP ADDRESSABILITY TO FLAG DSECT
         SPACE 2
*        SETUP THE FIRST CARD OF A JOBCARD SEQUENCE
         SPACE
JOBCARD1 MVC   JOBNAM,2(R6)  SAVE JOBNAME
         OI    FLAGFND,FLJOBNAM FLAG THAT WE FOUND PARM
         LA    R1,71         LOAD LENGTH TO SCAN = CARD-SEQ-CONT
         MVI   SCANCHR,C')'  NOTE SCAN ARGUMENT
         BAL   R14,SCAN      GO LOOK FOR END OF ACCOUNTING INFO
         B     SCANCRD       GO TO BASIC KEYWORD SCAN
         SPACE
*        SETUP THE SECOND AND SUBSEQUENT CARDS OF A JOBCARD SEQUENCE
         SPACE
JOBCARD2 LA    R1,69         LOAD LENGTH TO SCAN = CARD-SEQ-CONT-//
         LA    R6,2(R6)      INCR PAST //
         BAL   R14,SCANNB    GO SCAN FOR FIRST NON BLANK
         EJECT
**********************************************************************
*                                                                    *
*        SCAN THE JOBCARD FOR INTERESTING KEYWORDS                   *
*                                                                    *
**********************************************************************
         SPACE 2
SCANCRD  CLC   0(2,R6),=C', ' SEE IF AT END OF CARD WITH CONTINUATION
         BE    RETURN0       BUY THIS ONE
         CLI   0(R6),C''''   LOOK FOR QUOTE
         BE    QUOTE         GO PROCESS AND SCAN TO AFTER NEXT QUOTE
         CLI   0(R6),C' '    SEE IF AT END OF CARD
         BE    ENDCARD
         CLC   0(9,R6),=C'MSGCLASS='   FIND THE KEYWORDS WE ARE
         BE    RMSGCLS                 INTERESTED IN AND GO TO
         CLC   0(6,R6),=C'CLASS='      THE APPORPRIATE ROUTINES
         BE    RXEQCLS                 TO GET THE INFO TO MAKE
         CLC   0(5,R6),=C'TIME='       THE FINAL DECISION WITH
         BE    RCPUTIM
         LA    R6,1(R6)      INCR TO NEXT POSITION ON CURRENT CARD
         BCT   R1,SCANCRD    ONLY SCAN TO CONTINUATION POSITION
         SPACE
         CLI   0(R6),C','    SEE IF COMMA AS CONTINUATION IN 71
         BE    RETURN0       BUY THIS ONE
         B     ERR           THIS JOB IS IN TROUBLE NOW
         SPACE
*        SCAN FOR THE "SCANCHR"
         SPACE
SCAN     CLC   SCANCHR,0(R6) LOOK FOR SCAN ARGUMENT
         BER   R14           RETURN FROM WHENCE WE CAME
         LA    R6,1(R6)      INCR TO NEXT CHAR
         BCT   R1,SCAN       ONLY LOOK SO FAR
         B     ERR           WE HAVE NO SENSE OF HUMOR
         SPACE
*        SCAN FOR THE FIRST NONBLANK POSITION
         SPACE
SCANNB   CLI   0(R6),C' '    LOOK FOR NON BLANK SCAN ARGUMENT
         BNER  R14           RETURN FROM WHENCE WE CAME
         LA    R6,1(R6)      INCR TO NEXT CHAR
         BCT   R1,SCANNB     ONLY LOOK TO END OF CARD IMAGE
         B     ERR           CANT HAVE A // WITH NOTHING AFTER IT
         SPACE
*        SCAN FOR THE NEXT QUOTE MARK
         SPACE
QUOTE    LA    R6,1(R6)      INCR TO NEXT POSITION AFTER FIRST QUOTE
         BCTR  R1,0          COUNT THIS CHAR
         MVI   SCANCHR,C'''' NOTE SCAN ARGUMENT AS QUOTE
         BAL   R14,SCAN      GO FIND NEXT QUOTE
         LA    R6,1(R6)      INCR TO NEXT POSITION AFTER SECOND QUOTE
         BCTR  R1,0          COUNT THIS CHAR
         B     SCANCRD       GO BACK TO MAIN SCAN FOR MORE KEYWORDS
         SPACE
*        PROCESS THE MESSAGE CLASS
         SPACE
RMSGCLS  MVC   MSGCLS,9(R6)  SAVE MSGCLASS PARM
         OI    FLAGFND,FLMSGCLS FLAG THAT WE FOUND PARM
         LA    R6,10(R6)     INCR TO NEXT PARM
         S     R1,=F'10'     COUNT THE CHARS IN THE KEYWORD+1
         B     SCANCRD       GO BACK TO MAIN SCAN FOR MORE KEYWORDS
         SPACE
*        PROCESS THE EXECUTION CLASS
         SPACE
RXEQCLS  MVC   XEQCLS,6(R6)  SAVE EXECUTION CLASS
         OI    FLAGFND,FLXEQCLS FLAG THAT WE FOUND PARM
         LA    R6,7(R6)      INCR TO NEXT PARM
         S     R1,=F'7'      COUNT THE CHARS IN THE KEYWORD+1
         B     SCANCRD       GO BACK TO MAIN SCAN FOR MORE KEYWORDS
         SPACE
*        PROCESS THE TIME PARAMETER
         SPACE
RCPUTIM  MVC   CPUTIM,5(R6)  SAVE TIME PARAMETER
         OI    FLAGFND,FLCPUTIM FLAG THAT WE FOUND PARM
         LA    R6,6(R6)      INCR TO NEXT PARM
         S     R1,=F'6'      COUNT THE CHARS IN THE KEYWORD+1
         B     SCANCRD       GO BACK TO MAIN SCAN FOR MORE KEYWORDS
         EJECT
**********************************************************************
*                                                                    *
*        JOBCARD HAS BEEN SCANNED AND PARSED - PROCESS DATA          *
*                                                                    *
**********************************************************************
         SPACE 2
*        SETUP TO FIND THE USERID OR PREFIX OF USERID IN TABLE
         SPACE
ENDCARD  L     R8,IEUSRIDP   LOAD POINTER TO USERID
         L     R15,=A(UIDTAB) LOAD ADDRESS OF UIDTABLE
         USING UIDDSECT,R15  ESTABLISH ADDRESSABILITY TO UIDTABLE
         SPACE
*        FIND THE USERID OR PREFIX OF USERID IN TABLE
         SPACE
UIDSRCH  SR    R14,R14       CLEAR REG 14 FOR EXEC INSTRUCTION
         CLI   0(R15),C' '   LOOK FOR END OF TABLE
         BE    DEFAULT       DEFAULT GETS BLANK ENTRIES
         SPACE
UIDSRCH1 LA    R9,0(R14,R8)  POINT TO ONE CHAR OF USERID
         LA    R10,0(R14,R15) POINT TO CORRESPONDING CHAR IN UIDTAB
         CLI   0(R10),C'*'   SEE IF THIS CHAR IS TO BE COMPARED
         BE    UIDSRCH2      ... NOT TO BE COMPARED
         CLI   0(R10),C' '   SEE IF AT END OF STRING OF CHAR
         BE    UIDFOUND      ... YES - NO MORE CHAR TO CHECK
         CLC   0(1,R10),0(R9) COMPARE THE CORRESPONDING CHAR
         BNE   UIDSRCH3      ... CHAR DONT MATCH - GO TO NEXT ENTRY
         SPACE
UIDSRCH2 LA    R14,1(R14)    MAKE INDEX TO NEXT CHARACTER
         C     R14,=F'7'     SEE IF WE HAVE CHECKED ALL CHAR
         BE    UIDFOUND      ... YES ALL 7 CHAR HAVE BEEN CHECKED
         B     UIDSRCH1      GO CHECK NEXT CHAR
         SPACE
UIDSRCH3 LA    R15,UIDELEN(R15) INDEX TO NEXT TABLE ENTRY
         B     UIDSRCH
         EJECT
**********************************************************************
*                                                                    *
*        PROCESS JOBNAME                                             *
*                                                                    *
**********************************************************************
         SPACE 2
*              R15 POINTS TO UID ENTRY
*              R14 HAS LENGTH OF PREFIX
         SPACE
*        COMPARE THE JOBNAME TO THE USERID OR PREFIX OF USERID
         SPACE
DEFAULT  EQU   *
         LA    R14,7         DEFAULT - JOBNAME = USERID + CHAR
         SPACE
UIDFOUND BCTR  R14,0         DECREMENT LENGTH WHEN PREFIX FOUND
         CLI   JOBN,C'N'     SEE IF ANY JOBNAME COMPARE
         BE    JOBCMPR1      THIS ONE CAN SUBMIT ANYTHING
         CLI   JOBN,C'P'     SEE IF PREFIX ONLY COMPARE
         BE    JOBCMPR       JOBNAM MUST START WITH FIRST N CHAR OF UID
         LA    R14,6         DEFAULT - JOBNAME = USERID + CHAR
JOBCMPR  EX    R14,JOBNCMPR  EXEC COMPARE INST
         BE    JOBCMPR1      ... OK WE WONT ABORT THE SUBMIT FOR JOBNAM
         OI    FLAGABR,FLJOBNAM  FLAG THAT WE WILL ABORT
         B     JOBCMPR2      GO DETERMINE WHAT KIND OF MESSAGE TO SEND
         SPACE
JOBNCMPR CLC   JOBNAM(1),0(R8) EXECUTED COMPARE
         SPACE
JOBCMPR1 CLC   JOBNAM(7),0(R8) SEE IF JOBNAME = USERID + CHAR
         BE    XCLS            ... GOOD - NO MESSAGE NEEDED FOR NAME
         SPACE
JOBCMPR2 OI    FLAGMSG,FLJOBNAM  FLAG TO SEND MESSAGE
         C     R14,=F'6'       SEE IF WHOLE JOBNAME USED
         BE    XCLS            MUST HAVE BEEN HIS USERID
         MVC   UIDPFX,UID      SAVE PREFIX TO SEND IN MESSAGE
         OI    FLAGMSG,FLJOBPRF  FLAG TO SEND PREFIX MESSAGE
         EJECT
**********************************************************************
*                                                                    *
*        PROCESS EXECUTION CLASS                                     *
*                                                                    *
**********************************************************************
         SPACE 2
*        COMPARE THE EXECUTION CLASS TO THE ALLOWABLE CLASSES
         SPACE
XCLS     CLI   XEQCLSI,C'*'  SEE IF EXECUTION CLASS IS NOT REQUIRED
         BE    XEQCLSOK      IF NOT REQUIRED - DONT CHECK AT ALL
         L     R14,=A(CLSTAB) POINT TO EXECUTION CLASS TABLE
         SPACE
XCLS1    CLC   XEQCLSI,0(R14) LOOK FOR CLASS TABLE ENTRY
         BE    XCLS2         ... FOUND ENTRY IN CLASS TABLE
         LA    R14,38(R14)   INCR TO NEXT ENTRY
         B     XCLS1         ... KEEP LOOKING
         SPACE
XCLS2    ST    R14,UIDXEQCP  SAVE POINTER TO TABLE FOR POSSIBLE MESSAGE
         TM    FLAGFND,FLXEQCLS  MAKE SURE EXECUTION CLASS IS THERE
         BNO   XCLS4         ... NO ENTRY - BOMB THIS ONE OUT
         SPACE
XCLS3    LA    R14,1(R14)    POINT TO REAL TABLE
         CLI   0(R14),C' '   NO REAL CLASS ENTRY
         BE    XCLS4         ... GO BOMB THIS JOB OUT
         CLC   XEQCLS,0(R14) SEE IF EXECUTION CLASS IS IN TABLE
         BE    XEQCLSOK      ... YES ITS OK
         B     XCLS3         GO LOOK SOME MORE
         SPACE
XCLS4    OI    FLAGABR,FLXEQCLS FLAG THE ERROR FOR ABORT
         OI    FLAGMSG,FLXEQCLS FLAG THE ERROR FOR MESSAGE
         OI    FLAGMS1,FLXEQCLS FLAG THE ERROR FOR MESSAGE
         EJECT
**********************************************************************
*                                                                    *
*        PROCESS MESSAGE CLASS                                       *
*                                                                    *
**********************************************************************
         SPACE 2
XEQCLSOK EQU   *
         SPACE
*        COMPARE THE MESSAGE CLASS TO THE ALLOWABLE CLASSES
         SPACE
MCLS     CLI   MSGCLSI,C'*'  SEE IF MSGCLASS IS NOT REQUIRED
         BE    MSGCLSOK      IF NOT REQUIRED - DONT CHECK AT ALL
         L     R14,=A(MSGTAB) POINT TO MSGCLASS TABLE
         SPACE
MCLS1    CLC   MSGCLSI,0(R14) LOOK FOR CLASS TABLE ENTRY
         BE    MCLS2         ... FOUND ENTRY IN CLASS TABLE
         LA    R14,38(R14)   INCR TO NEXT ENTRY
         B     MCLS1         ... KEEP LOOKING
         SPACE
MCLS2    ST    R14,UIDMSGCP  SAVE POINTER TO TABLE FOR POSSIBLE MESSAGE
         TM    FLAGFND,FLMSGCLS  MAKE SURE MSGCLASS IS THERE
         BNO   MCLS4         ... NO ENTRY - BOMB THIS ONE OUT
         SPACE
MCLS3    LA    R14,1(R14)    POINT TO REAL TABLE
         CLI   0(R14),C' '   NO REAL CLASS ENTRY
         BE    MCLS4         ... GO BOMB THIS JOB OUT
         CLC   MSGCLS,0(R14) SEE IF EXECUTION CLASS IS IN TABLE
         BE    MSGCLSOK      ... YES ITS OK
         B     MCLS3         GO LOOK SOME MORE
         SPACE
MCLS4    OI    FLAGABR,FLMSGCLS FLAG THE ERROR FOR ABORT
         OI    FLAGMSG,FLMSGCLS FLAG THE ERROR FOR MESSAGE
         OI    FLAGMS1,FLMSGCLS FLAG THE ERROR FOR MESSAGE
         SPACE
MSGCLSOK EQU   *
         EJECT
**********************************************************************
*                                                                    *
*        PROCESS CPU TIME PARAMETER - IF SPECIFIED                   *
*                                                                    *
**********************************************************************
         SPACE 2
*        COMPARE THE CPU TIME PARAMETER AND:
*          1) DENY SUBMISSION IF THE EXECUTION CLASS DOES NOT ALLOW
*             A TIME PARAMETER AND THE USER SPECIFIES ONE.
*          2) DENY SUBMISSION IF THE EXECUTION CLASS REQUIRES A TIME
*             PARAMETER AND THE USER DID NOT SPECIFY ONE.
*          3) INSERT A TIME PARAMETER IF THE CLASS REQUIRES ONE
*             AND THE USER DIDNT SPECIFY ONE
*          4) CHECK FOR USER SPECIFIED TIME GREATER THAN MAX TIME
*             FOR THE CLASS
         SPACE
CTIM     L     R14,=A(TIMTAB) POINT TO TIME PARM TABLE
         SPACE
CTIM1    CLI   0(R14),C' '   MAKE SURE TABLE ENTRY IS VALID
         BE    CTIM8         ... NOT VALID - GO FLAG AND ABORT
         CLC   XEQCLS,0(R14) LOOK FOR THE EXECUTION CLASS IN TABLE
         BE    CTIM2         ... OK GO PROCESS
         LA    R14,6(R14)    INCR TO NEXT ENTRY
         B     CTIM1         ... KEEP LOOKING
         SPACE
CTIM2    CLI   1(R14),C'N'   SEE IF MANDITORY TO NOT HAVE TIME SPEC
         BNE   CTIM3
         TM    FLAGFND,FLCPUTIM SEE IF SPECIFIED
         BO    CTIM6         IF NOT SUPPOSED TO HAVE TIME AND DOES ...
         B     CTIM5         ... GOOD - NOT SUPPOSED TO AND DOESNT
         SPACE
CTIM3    CLI   1(R14),C'Y'   SEE IF MANDITORY TO HAVE TIME SPEC
         BNE   CTIM4         ... GO SEE IF TIME IS OPTIONAL
         TM    FLAGFND,FLCPUTIM SEE IF SPECIFIED
         BNO   CTIM6         ... ITS MANDITORY AND DOESNT - BOMB
         B     CTIM9         ... GOOD SUPPOSED TO AND DOES HAVE IT
         SPACE
CTIM4    CLI   1(R14),C'O'   SEE IF OPTIONAL TO HAVE TIME
         BNE   WRAPUP        ... NOTHING MORE TO DO HERE - LETS FINISH
         TM    FLAGFND,FLCPUTIM SEE IF SPECIFIED
         BO    CTIM9         IF OPTIONAL AND SPECIFIED THEN OK
         SPACE
CTIM5    LH    R1,2(R14)     SEE IF TIME IN TABLE TO USE AS INSERT
         LTR   R1,R1
         BZ    WRAPUP        ... NOTHING TO INSERT - GET OUT NOW
         BAL   R15,CTIMD1    GO FORMAT TIME
         L     R2,IECARDP    GET START OF CARD IMAGE
         LR    R1,R6         GET ADDRESS OF LOCATION TO INSERT COMMA
         SR    R1,R2         SEE WHERE WE MIGHT WANT TO INSERT COMMA
         C     R1,=F'71'     SEE IF THERE IS ROOM FOR THE COMMA
         BNL   CTIM7         CANT INSERT COMMA IN CC 72
         MVI   0(R6),C','    PUT IN COMMA FOR CONTINUATION ON JOB CARD
         MVC   TIMECARD,TIMESKEL  SET UP CARD TO INSERT
         MVC   TIMECARD+17(8),CPUTIMAX GET TIME TO USE
         B     RETURN4       GO TO ACCEPT THIS MODIFIED CARD
         SPACE
CTIM6    OI    FLAGABR,FLCPUTIM FLAG REASON FOR ABORT
         OI    FLAGMSG,FLCPUTIM FLAG MESSAGE
         B     WRAPUP        NOW GO TELL THE SUCKER OF HIS PLIGHT
         SPACE
CTIM7    OI    FLAGABR,FLCPUTIN FLAG REASON FOR ABORT
         OI    FLAGMSG,FLCPUTIN FLAG MESSAGE
         B     WRAPUP        NOW GO TELL THE SUCKER OF HIS PLIGHT
         SPACE
CTIM8    OI    FLAGABR,FLXEQCLS FLAG THE ERROR FOR ABORT
         OI    FLAGMSG,FLXEQCLS FLAG THE ERROR FOR MESSAGE
         B     WRAPUP        WHY LET THIS CONTINUE
         SPACE 1
CTIM9    LH    R1,4(R14)     LOAD MAX TIME
         LTR   R1,R1         SEE IF SPECIFIED / NON ZERO
         BZ    WRAPUP        GO WRAPUP IF NOTHING TO DO
         LA    R1,CPUTIM     GET ADDR OF TIME PARM
         LR    R10,R1        POINT R10 AT START OF TIME TOO
         XC    TIMEFLG,TIMEFLG CLEAR OUT THE FLAGS
         SR    R11,R11       AND ZERO OUT R11 TOO
         SPACE 1
CTIM10   CLI   0(R10),C'('   CHECK FOR START OF PARM IN PARENDS
         BE    CTIM11
         CLI   0(R10),C')'   CHECK FOR END OF PARM IN PARENDS
         BE    CTIM14
         CLI   0(R10),C' '   CHECK FOR END OF PARM
         BE    CTIM14
         CLI   0(R10),C','   CHECK FOR END OF PARM OR END OF INNER
         BE    CTIM12
         CLI   0(R10),C'0'   CHECK FOR VALID NUMERIC
         BL    CTIM6
         CLI   0(R10),C'9'   CHECK FOR VALID NUMERIC
         BH    CTIM6
         LA    R10,1(R10)    INCR TO NEXT POSITION
         B     CTIM10        GO LOOK AT NEXT PLACE
         EJECT
CTIM11   OI    TIMEFLG,TIMEFLGO INDICATE OPENING PAREND
         B     CTIM13        GO TO INCREMENT OVER PAREND
         SPACE
CTIM12   TM    TIMEFLG,TIMEFLGO SEE IF THERE WAS AN OPENING PAREND
         BNO   CTIM14        GO PROCESS COMMA AS END OF PARM
         CR    R1,R10        SEE IF ANY DATA TO WORK WITH
         BNE   CTIM15        GO PROCESS THE DATA
         OI    TIMEFLG,TIMEFLGS SET FLAGS FOR SECONDS COMING NEXT
         SPACE 1
CTIM13   LA    R10,1(R10)    INCR TO NEXT POSITION
         LR    R1,R10        SAVE START OF PARM IN R1 TOO
         B     CTIM10        GO LOOK FOR NEXT REAL PARM
         SPACE 1
CTIM14   OI    TIMEFLG,TIMEFLGE FLAG END OF PARM
         CR    R1,R10        SEE IF ANY DATA TO WORK WITH
         BE    CTIM6         ERROR IF NO DIGITS AT ALL
         SPACE 1
CTIM15   SR    R10,R1        GET NUMBER OF DIGITS TO WORK WITH
         BCTR  R10,0         DECREASE THE NUMBER BY 1 FOR EX INSTR.
         EX    R10,CTIMPACK  EXEC PACK INSTRUCTION
         TM    TIMEFLG,TIMEFLGS SEE IF WORKING WITH SECONDS
         BO    CTIM16        GO ADD IN SECONDS
         CVB   R11,DWWORK    FIRST MAKE IT BINARY IN REG11
         MH    R11,=H'60'    NOW MAKE MINUTES INTO SECONDS
         TM    TIMEFLG,TIMEFLGE SEE IF AT END OF PARM
         BO    CTIM17        GO SEE IF TOO BIG
         LA    R10,1(R10,R1) RESTORE PREVIOUS VALUE TO R10
         OI    TIMEFLG,TIMEFLGS SET FLAGS FOR SECONDS COMING NEXT
         B     CTIM13
         SPACE 1
CTIM16   CVB   R12,DWWORK    CONVERT TO BINARY
         AR    R11,R12       ADD SECONDS TO SECONDS FROM MINUTES
         SPACE 1
CTIM17   LH    R1,4(R14)     LOAD MAX CPU TIME VALUE FROM TABLE
         CR    R11,R1        SEE IF SPECIFIED VALUE IS TOO BIG
         BNH   WRAPUP        OK .. GO LET IT HAPPEN
         OI    FLAGMS1,FLCPUTIM FLAG FOR CPU TIME ERR
         BAL   R15,CTIMD1    GO EDIT THE MAX VALUE FOR MESSAGE
         B     CTIM6         GO MAKE SURE THIS ONE DOESNT HAPPEN
         SPACE 1
CTIMPACK PACK  DWWORK,0(1,R1) PACK DIGITS INTO WORK AREA
         SPACE 2
*        MAKE HALFWORD VALUE INTO PRINTABLE MINUTES AND SECONDS
         SPACE 1
CTIMD1   ST    R15,R15D1     SAVE RETURN ADDRESS
         LR    R11,R1        LOAD VALUE TO DECODE INOT R11
         SR    R10,R10       CLEAR EVEN REG OF 10-11 PAIR
         D     R10,=F'60'    MAKE THE  SECONDS INTO MINUTES AND SECS
         MVI   CPUTIMAX,C' ' BLANK OUT THE FIELD
         MVC   CPUTIMAX+1(7),CPUTIMAX
         MVI   CPUTIMAX,C'(' ALWAYS START WITH PAREND
         LA    R12,CPUTIMAX+1 POINT TO THE OUTPUT FIELD
         BAL   R15,CTIME1    GO EDIT THE VALUE IN R11
         LTR   R10,R10       SEE IF ANY SECONDS TO EDIT
         BZ    CTIMD2        ..  NO ... GO INSERT ENDING PAREND
         MVI   0(R12),C','   NOW PUT IN THE COMMA
         LA    R12,1(R12)    INCR TO THE NEXT OUTPUT POSITION
         LR    R11,R10       PICK UP THE SECONDS
         BAL   R15,CTIME1    NOW EDIT THE SECONDS
CTIMD2   MVI   0(R12),C')'   PUT IN THE ENDING PAREND
         L     R15,R15D1 RESTORE THE RETURN ADDRESS
         BR    R15           GO BACK TO THE CALLER
         SPACE 2
*        THE REAL TIME EDIT
         SPACE 1
CTIME1   CVD   R11,DWWORK    CONVERT THE VALUE TO DECIMAL
         MVC   EDWORK,=X'4020202020202020' MOVE IN THE EDIT MASK
         ED    EDWORK,DWWORK+4 EDIT THE TIME
         LA    R11,EDWORK+1  POINT AT THE FIRST REAL PLACE IN THE TIME
         LA    R1,7          CREATE THE NUMBER OF POSSIBLE DIGITS
CTIME2   CLI   0(R11),C' '   SEE IF THE POSITION IS BLANK
         BE    CTIME3        DONT MOVE BLANKS
         MVC   0(1,R12),0(R11) MOVE ONE CHARACTER
         LA    R12,1(R12)    INCR TO NEXT OUTPUT POSITION
CTIME3   LA    R11,1(R11)    INCR TO NEXT INPUT POSITION
         BCT   R1,CTIME2     GO BACK AND TRY NEXT SPOT
         BR    R15           RETURN TO CALLER
         SPACE 2
*        HANDLE JOBCARDS THAT ARE TOO LONG
         SPACE
ERR      OI    FLAGABR,FLJERRIC  JOB CARD ERROR > 72 BYTES
         OI    FLAGMSG,FLJERRIC
         B     WRAPUP20      GO TO SEND MESSAGE ABOUT THIS THING
         EJECT
**********************************************************************
*                                                                    *
*        HANDLE JES2 OR SYSTEM CONTROL CARDS                         *
*                                                                    *
**********************************************************************
         SPACE
JESCTL   EQU   *
         CLI   2(R6),C'$'    SEE IF JES COMMAND   "/*$" NO-NO
         BE    BADCMND       GO TURN ON THE NASTYGRAM FLAGS
         CLI   2(R6),C'P'    SEE IF PRIORITY CARD "/*PRIORITY"
         BE    BADCMND       GO TURN ON THE NASTYGRAM FLAGS
         B     RETURN0       LET THIS JES CARD SLIP THROUGH
         SPACE
COMMAND  LA    R1,71         LOAD LENGTH TO SCAN
         MVI   SCANCHR,C' '  NOTE SCAN ARGUMENT - FIRST BLANK
         BAL   R14,SCAN      GO SCAN FOR FIRST BLANK
         BAL   R14,SCANNB    NOW SCAN FOR FIRST NONBLANK
         L     R14,=A(OPERCMDS)  SET UP TO LOOK FOR OPER CMDS
COMMAND1 SR    R15,R15       CLEAR REG 15 FOR INSERT CHAR
         IC    R15,0(R14)    GET LENGTH OF COMMAND
         LTR   R15,R15       SEE IF AT END OF TABLE
         BZ    RETURN0       ... NOT COMMAND ... LET IT GO
         EX    R15,CMNDCHEK  IS THIS IN TABLE
         BE    BADCMND       ... YES ... PUT A NOOSE ON IT AND PULL
         LA    R14,10(R14)   INCR TO NEXT ENTRY IN TABLE
         B     COMMAND1
         SPACE
CMNDCHEK CLC   0(1,R6),1(R14)  COMPARE WORD ON JCL TO COMMAND TABLE
         SPACE
BADCMND  OI    FLAGABR,FLINVCMD  INVALID CONTROL CARD DETECTED
         OI    FLAGMSG,FLINVCMD    ABORT + MESSAGE + SEND BAD CARD BACK
         OI    FLAGMS1,FLINVCMD
         L     R6,IECARDP    GET THE ADDRESS OF START OF BAD COMMAND
         MVC   INVCARD,0(R6) SAVE THIS CARD TO SEND IT BACK WITH MSG
         B     WRAPUP22      GO TO SEND MESSAGE ABOUT THIS THING
         EJECT
**********************************************************************
*                                                                    *
*        SETUP TO INSERT TIME PARAMETER CARD                         *
*                                                                    *
**********************************************************************
         SPACE
DOINSERT LA    R15,TIMECARD  LOAD ADDRESS OF CARD TO INSERT
         ST    R15,IECARDP   STORE IN CURRENT CARD POINTER
         SPACE 2
**********************************************************************
*                                                                    *
*        ISSUE THE APPROPRIATE MESSAGES TO THE TERMINAL              *
*                                                                    *
**********************************************************************
         SPACE
WRAPUP   EQU   *             SEE IF WE HAVE ANY MESSAGES TO SEND
         TM    FLAGMSG,FLJOBNAM+FLXEQCLS+FLMSGCLS+FLCPUTIM+FLJERRIC+FLC*
               PUTIN+FLINVCMD
         BNZ   WRAPUP1       ... PRIMARY MESSAGES TO ISSUE - DO IT
         TM    FLAGMS1,FLXEQCLS+FLMSGCLS+FLINVCMD+FLCPUTIM
         BZ    WRAPUP98      ... NO MESSAGES TO ISSUE
         SPACE
WRAPUP1  TM    FLAGABR,FLJOBNAM  SEE IF ABORT FOR JOBNAME
         BNO   WRAPUP2       ... NOT ABORT BUT LETS SEE IF WARNING
         TM    FLAG0XL,FLJOBNAX  SEE IF WE ISSUED "REJECTED"
         BO    WRAPUP3       ... IF YES THEN GO SEE IF JOBNAME MSG
         MVC   MESSAGE(L'ABORTMS1+2),ABORTMSG
         OI    FLAG0XL,FLJOBNAX  FLAG SENDING "REJECTED"
         B     RETURN8
         SPACE
WRAPUP2  TM    FLAGMSG,FLJOBNAM  DO WE NEED TO ISSUE JOBNAME MSG
         BNO   WRAPUP6
         TM    FLAG0XL,FLJOBNAX  SEE IF WE ISSUED "WARNING"
         BO    WRAPUP3       ... IF YES THEN GO SEE IF JOBNAME MSG
         MVC   MESSAGE(L'WARNMS1+2),WARNMSG
         OI    FLAG0XL,FLJOBNAX  FLAG SENDING "WARNING"
         B     RETURN8
         SPACE
WRAPUP3  TM    FLAGMSG,FLJOBNAM  SEE IF WE ISSUED JOBNAME MSG
         BNO   WRAPUP6
         TM    FLAGABR,FLJOBNAM  SEE IF ABORTING FOR JOBNAME
         BZ    WRAPUP4           ... NOT ABORT - WARNING ONLY
         TM    FLAGMSG,FLJOBPRF  SEE IF WE SHOULD ISSUE PREFIX
         BO    WRAPUP5
WRAPUP4  MVC   MESSAGE(L'JOBNMS1+2),JOBNMSG
         NI    FLAGMSG,X'FF'-FLJOBNAM TURN OFF MSG FLAG TO NOT RESEND
         B     RETURN8
         SPACE
WRAPUP5  TM    FLAGMSG,FLJOBNAM  SEE IF WE ISSUED JOBNAME MSG
         BNO   WRAPUP6
         MVC   MESSAGE(L'PREFMS1+2),PREFMSG
         MVC   MESSAGE+51(7),UIDPFX
         NI    FLAGMSG,X'FF'-FLJOBPRF-FLJOBNAM TURN OFF MSG FLAGS
         B     RETURN8
         SPACE
WRAPUP6  TM    FLAGABR,FLXEQCLS SEE IF ABORT FOR XEQCLS
         BNO   WRAPUP11
         TM    FLAG0XL,FLXEQCLX  SEE IF WE HAVE SENT "REJECTED"
         BO    WRAPUP7
         MVC   MESSAGE(L'ABORTMS1+2),ABORTMSG
         OI    FLAG0XL,FLXEQCLX  FLAG SENDING "REJECTED"
         B     RETURN8
         SPACE
WRAPUP7  TM    FLAGMSG,FLXEQCLS SEE IF MSG FOR XEQCLS
         BNO   WRAPUP10
         TM    FLAGFND,FLMSGCLS SEE IF MSGCLASS FOUND
         BO    WRAPUP8
         MVC   MESSAGE(L'XEQCMS3+2),XEQCMS2
         B     WRAPUP9
WRAPUP8  MVC   MESSAGE(L'XEQCMS1+2),XEQCMSG
WRAPUP9  NI    FLAGMSG,X'FF'-FLXEQCLS TURN OFF MSG FLAG TO NOT RESEND
         B     RETURN8
         SPACE
WRAPUP10 TM    FLAGMS1,FLXEQCLS SEE IF MSG FOR XEQCLS
         BNO   WRAPUP11
         MVC   MESSAGE(L'VALCMS1+2),VALCMSG
         L     R14,UIDXEQCP
         MVC   MESSAGE+22(37),1(R14)
         NI    FLAGMS1,X'FF'-FLXEQCLS TURN OFF MSG FLAG TO NOT RESEND
         B     RETURN8
         SPACE
WRAPUP11 TM    FLAGABR,FLMSGCLS SEE IF ABORT FOR MSGCLS
         BNO   WRAPUP16
         TM    FLAG0XL,FLMSGCLX  SEE IF WE HAVE SENT "REJECTED"
         BO    WRAPUP12
         MVC   MESSAGE(L'ABORTMS1+2),ABORTMSG
         OI    FLAG0XL,FLMSGCLX  FLAG SENDING "REJECTED"
         B     RETURN8
         SPACE
WRAPUP12 TM    FLAGMSG,FLMSGCLS SEE IF MSG FOR MSGCLS
         BNO   WRAPUP15
         TM    FLAGFND,FLMSGCLS SEE IF MSGCLASS FOUND
         BO    WRAPUP13
         MVC   MESSAGE(L'MSGCMS3+2),MSGCMS2
         B     WRAPUP14
WRAPUP13 MVC   MESSAGE(L'MSGCMS1+2),MSGCMSG
WRAPUP14 NI    FLAGMSG,X'FF'-FLMSGCLS TURN OFF MSG FLAG TO NOT RESEND
         B     RETURN8
         SPACE
WRAPUP15 TM    FLAGMS1,FLMSGCLS SEE IF MSG FOR MSGCLS
         BNO   WRAPUP16
         MVC   MESSAGE(L'VALCMS1+2),VALCMSG
         L     R14,UIDMSGCP
         MVC   MESSAGE+22(37),1(R14)
         NI    FLAGMS1,X'FF'-FLMSGCLS TURN OFF MSG FLAG TO NOT RESEND
         B     RETURN8
         SPACE
WRAPUP16 TM    FLAGABR,FLCPUTIM SEE IF ABORT FOR CPUTIM
         BNO   WRAPUP18
         TM    FLAG0XL,FLCPUTIX  SEE IF WE HAVE SENT "REJECTED"
         BO    WRAPUP17
         MVC   MESSAGE(L'ABORTMS1+2),ABORTMSG
         OI    FLAG0XL,FLCPUTIX  FLAG SENDING "REJECTED"
         B     RETURN8
         SPACE
WRAPUP17 TM    FLAGMSG,FLCPUTIM SEE IF MSG FOR CPUTIM
         BNO   WRAPU17A
         MVC   MESSAGE(L'CPUTMS1+2),CPUTMSG
         NI    FLAGMSG,X'FF'-FLCPUTIM TURN OFF MSG FLAG TO NOT RESEND
         B     RETURN8
         SPACE
WRAPU17A TM    FLAGMS1,FLCPUTIM SEE IF MSG FOR CPUTIME
         BNO   WRAPUP18
         MVC   MESSAGE(L'VALTMS1+2),VALTMSG
         MVC   MESSAGE+22(8),CPUTIMAX
         NI    FLAGMS1,X'FF'-FLCPUTIM TURN OFF MSG FLAG TO NOT RESEND
         B     RETURN8
         SPACE
WRAPUP18 TM    FLAGABR,FLCPUTIN SEE IF ABORT FOR UNABLE TO INSERT COMMA
         BNO   WRAPUP20
         TM    FLAG0XL,FLCPUTIX  SEE IF WE HAVE SENT "REJECTED"
         BO    WRAPUP19       ... DONT ISSUE ABORT MESSAGE TWICE
         MVC   MESSAGE(L'ABORTMS1+2),ABORTMSG
         OI    FLAG0XL,FLCPUTIX  FLAG SENDING "REJECTED"
         B     RETURN8
         SPACE
WRAPUP19 TM    FLAGMSG,FLCPUTIN SEE IF MSG FOR UNABLE TO INSERT COMMA
         BNO   WRAPUP20
         MVC   MESSAGE(L'CPUTMS3+2),CPUTMS2
         NI    FLAGMSG,X'FF'-FLCPUTIN TURN OFF MSG FLAG TO NOT RESEND
         B     RETURN8
         SPACE
WRAPUP20 TM    FLAGABR,FLJERRIC SEE IF ABORT FOR INVALID JOBCARD
         BNO   WRAPUP22       ... NOT ABORTING FOR JOBCARD >71 CHAR
         TM    FLAG0XL,FLJERRIX  SEE IF WE HAVE SENT "REJECTED"
         BO    WRAPUP21       ... DONT ISSUE ABORT MESSAGE TWICE
         MVC   MESSAGE(L'ABORTMS1+2),ABORTMSG
         OI    FLAG0XL,FLJERRIX  FLAG SENDING "REJECTED"
         B     RETURN8
         SPACE
WRAPUP21 TM    FLAGMSG,FLJERRIC SEE IF MSG FOR INVALID JOBCARD
         BNO   WRAPUP22      ... JOBCARD WAS < 72 CHAR
         MVC   MESSAGE(L'JERRMS1+2),JERRMSG
         NI    FLAGMSG,X'FF'-FLJERRIC TURN OFF MSG FLAG TO NOT RESEND
         B     RETURN8
         SPACE
WRAPUP22 TM    FLAGABR,FLINVCMD SEE IF ABORT FOR INVALID COMMAND
         BNO   WRAPUP24       ... NOT ABORTING FOR COMMAND IN JCL
         TM    FLAG0XL,FLINVCMX  SEE IF WE HAVE SENT "REJECTED"
         BO    WRAPUP23       ... DONT ISSUE ABORT MESSAGE TWICE
         MVC   MESSAGE(L'ABORTMS1+2),ABORTMSG
         OI    FLAG0XL,FLINVCMX  FLAG SENDING "REJECTED"
         B     RETURN8
         SPACE
WRAPUP23 TM    FLAGMSG,FLINVCMD SEE IF MSG FOR COMMAND IN JCL
         BNO   WRAPUP24      ... JES 2 OR SYSTEM COMMAND IN JCL
         MVC   MESSAGE(L'INVCMMS1+2),INVCMMSG
         NI    FLAGMSG,X'FF'-FLINVCMD TURN OFF MSG FLAG TO NOT RESEND
         B     RETURN8
         SPACE
WRAPUP24 TM    FLAGMS1,FLINVCMD SEE IF MSG FOR COMMAND IN JCL
         BNO   WRAPUP98      ... JES 2 OR SYSTEM COMMAND IN JCL
         MVC   MESSAGE(2),=AL2(80)
         MVC   MESSAGE+2(78),INVCARD MOVE IN DETECTED JES OR SYSTEM
         NI    FLAGMS1,X'FF'-FLINVCMD TURN OFF MSG FLAG TO NOT RESEND
         B     RETURN8
         SPACE
WRAPUP98 LA    R10,RETURN0   DEFAULT TO RETURN0
*                            SEE IF WE WANT TO ABORT THIS SUBMISSION
         TM    FLAGABR,FLJOBNAM+FLXEQCLS+FLMSGCLS+FLCPUTIM+FLJERRIC+FLC*
               PUTIN+FLINVCMD
         BZ    WRAPUP99      WE WILL LET THIS SUBMIT GO THROUGH
         LA    R10,RETURN16  TERMINATE THE SUBMIT
         SPACE
WRAPUP99 LA    R15,TIMECARD  LOAD ADDRESS OF INSERT CARD
         C     R15,IECARDP   SEE IF ABOUT TO INSERT CARD
         BER   R10           GO INSERT - DONT DO THE FREEMAIN
         SPACE
         LA    R0,AREAWORK   LOAD LENGTH OF AREA TO FREE
         L     R1,IEEXITWD   LOAD ADDRESS OF AREA TO FREE
         FREEMAIN R,LV=(0),A=(1)  FREE GOTTEN AREA FOR EXIT WORK
         SR    R1,R1         CLEAR ADDRESS OF GOTTEN AREA SINCE
         ST    R1,IEEXITWD   WE JUST FREED IT - MAY COME FOR NEXT JOB
         BR    R10           GO END THE SUBMIT WITH EITHER 0 OR 16
         EJECT
**********************************************************************
*                                                                    *
*        RETURN THE APPROPRIATE RETURN CODES TO THE CALLING PROGRAM  *
*                                                                    *
**********************************************************************
         SPACE
RETURN16 LA    R10,IEABORT   LOAD ABORT RETURN CODE
         B     RETURN        ... THE ULTIMATE INSULT - SUBMIT REJECTED
         SPACE
RETURN8  LA    R10,MESSAGE   GET ADDRESS OF MESSAGE TO SEND
         ST    R10,IEMSGP    STORE IN PARM WORD
         LA    R10,IEMSG     LOAD ISSUE MESSAGE RETURN CODE
         B     RETURN
         SPACE
RETURN4  LA    R10,IERETURN  LOAD PROCESS & RETURN FOR ANOTHER STMT
         B     RETURN        ... ONLY USED TO DO AN INSERT
         SPACE
RETURN0  LA    R10,IECONTIN  LOAD OK TO CONTINUE RETURN CODE
         SPACE
RETURN   LR    R1,R13        LOAD ADDRESS OF GOTTEN AREA FOR REGS
         L     R13,4(R13)    LOAD PREVIOUS SAVE AREA ADDRESS
         LA    R0,AREAREGS   LOAD SIZE OF GOTTEN AREA FOR REGISTERS
         FREEMAIN R,LV=(0),A=(1)  FREE GOTTEN AREA FOR REGISTER SAVE
         LR    R15,R10       PLACE RETURN CODE IN REG15
         RETURN (14,12),RC=(15) RETURN TO THE CALLER
         EJECT
**********************************************************************
*                                                                    *
*        MESSAGES TO SEND REGARDING THE STATUS OF THIS SUBMISSION    *
*                                                                    *
**********************************************************************
         SPACE 2
ABORTMSG DC    AL2(L'ABORTMS1+2)
ABORTMS1 DC    C'**  SUBMIT REJECTED  **'
         SPACE
WARNMSG  DC    AL2(L'WARNMS1+2)
WARNMS1  DC    C'WARNING - OUTPUT COMMAND WILL NOT DISPLAY THIS JOB'
         SPACE
JOBNMSG  DC    AL2(L'JOBNMS1+2)
JOBNMS1  DC    C'JOBNAME DOES NOT START WITH YOUR USERID'
         SPACE
PREFMSG  DC    AL2(L'PREFMS1+2)
PREFMS1  DC C'JOBNAME DOES NOT START WITH YOUR USERID PREFIX -        '
         SPACE
XEQCMSG  DC    AL2(L'XEQCMS1+2)
XEQCMS1  DC    C'EXECUTION CLASS SPECIFIED IS INVALID'
         SPACE
XEQCMS2  DC    AL2(L'XEQCMS3+2)
XEQCMS3  DC    C'EXECUTION CLASS IS REQUIRED BUT NOT SPECIFIED'
         SPACE
MSGCMSG  DC    AL2(L'MSGCMS1+2)
MSGCMS1  DC    C'MSGCLASS SPECIFIED IS INVALID'
         SPACE
MSGCMS2  DC    AL2(L'MSGCMS3+2)
MSGCMS3  DC    C'MSGCLASS IS REQUIRED BUT NOT SPECIFIED'
         SPACE
CPUTMSG  DC    AL2(L'CPUTMS1+2)
CPUTMS1  DC    C'CPU TIME IS MISSING, INVALID, OR TOO LARGE'
         SPACE
CPUTMS2  DC    AL2(L'CPUTMS3+2)
CPUTMS3  DC    C'JOB CARD IS TOO LONG, CAN''T INSERT TIME PARAMETER'
         SPACE
VALCMSG  DC    AL2(L'VALCMS1+2)
VALCMS1  DC    CL57'VALID CLASSES ARE - '
         SPACE
VALTMSG  DC    AL2(L'VALTMS1+2)
VALTMS1  DC    CL57'MAX CPU TIME IS   - '
         SPACE
INVCMMSG DC    AL2(L'INVCMMS1+2)
INVCMMS1 DC    C'JES2 OR SYSTEM CONTROL CARD DETECTED - NOT PERMITTED'
         SPACE
JERRMSG  DC    AL2(L'JERRMS1+2)
JERRMS1  DC    C'INVALID JOBCARD'
         SPACE 2
**********************************************************************
*                                                                    *
*        CONSTANTS USED IN THIS ROUTINE                              *
*                                                                    *
**********************************************************************
         SPACE
TIMESKEL DC    CL80'//          TIME=          CARD INSERTED BY SUBMIT'
         SPACE
         LTORG
         EJECT
**********************************************************************
*                                                                    *
*        ALL TABLES WHICH FOLLOW ARE ADDRESSED VIA ADCONS            *
*        NO ADDRESSABILITY IS NEEDED                                 *
*                                                                    *
*        TABLES TO CHECK - (NOTE TABLES ALL END WITH A BLANK ENTRY)  *
*                                                                    *
**********************************************************************
         SPACE 2
**********************************************************************
*                                                                    *
*        USERID TABLE                                                *
*                                                                    *
**********************************************************************
         SPACE 2
UIDTAB   DS    0C      USERID OR USERID PREFIX TABLE
*
*        (NOTE: THIS TABLE IS MAPPED BY UIDDSECT DSECT)
*
*        (NOTE: USERIDS OR PREFIXES MUST BE IN SIGNIFICANCE SEQUENCE
*               I.E. FIRST MATCH IS TAKEN                           )
*
*             USERID OR  | XCLSI  | MCLSI  | CHECK JOBNAME/USERID
*             PREFIX -   |        |        |
*                        | KEY TO TABLE OR |  Y = YES - 7 CHAR
*             "*"        | "*" FOR NOT     |  N = NO
*             INDICATES  | REQUIRED AND    |  P = PREFIX LENGTH ONLY
*             ANY CHAR   | NOT CHECKED IF  |
*             WILL DO    | SPECIFIED       |
*
         DC    CL7'DRNHLCG',CL1'0',CL1'*',CL1'Y'  NO SUBMIT ALLOWED
         DC    CL7'E40CG01',CL1'H',CL1'H',CL1'Y'  PREFIX CHK
         DC    CL7'E40CH  ',CL1'T',CL1' ',CL1'Y'  CHECK JOBNAME
         DC    CL7'E6TSA  ',CL1'T',CL1'B',CL1'Y'  CHECK JOBNAME
         DC    CL7'MOCSY05',CL1'B',CL1' ',CL1'Y'  CHECK JOBNAME
         DC    CL7'PCBFL  ',CL1'H',CL1'H',CL1'P'  PREFIX CHK
         DC    CL7'P00TF  ',CL1'D',CL1' ',CL1'Y'  CHECK JOBNAME
         DC    CL7'P00CI02',CL1'I',CL1' ',CL1'Y'  CHECK JOBNAME
         DC    CL7'P00CS01',CL1'I',CL1' ',CL1'Y'  CHECK JOBNAME
         DC    CL7'P08CC  ',CL1'D',CL1'E',CL1'Y'  CHECK JOBNAME
         DC    CL7'P08IC  ',CL1'D',CL1'E',CL1'Y'  CHECK JOBNAME
         DC    CL7'P08PP  ',CL1'D',CL1'E',CL1'Y'  CHECK JOBNAME
         DC    CL7'P4ECTR1',CL1'I',CL1' ',CL1'Y'  CHECK JOBNAME
         DC    CL7'P4ECTR2',CL1'I',CL1' ',CL1'Y'  CHECK JOBNAME
         DC    CL7'P4E0B01',CL1'*',CL1'*',CL1'N'  NO JOBNAME CHK
         DC    CL7'P4E0B02',CL1'*',CL1'*',CL1'N'  NO JOBNAME CHK
         DC    CL7'P40BF01',CL1'D',CL1'D',CL1'Y'  CHECK JOBNAME
         DC    CL7'P40CD  ',CL1'S',CL1'*',CL1'N'  NO JOBNAME CHK
         DC    CL7'P40CF  ',CL1'*',CL1'*',CL1'N'  NO JOBNAME CHK
         DC    CL7'P40PR  ',CL1'*',CL1'*',CL1'N'  NO JOBNAME CHK
         DC    CL7'P**PN  ',CL1'D',CL1'B',CL1'Y'  CHECK JOBNAME
         DC    CL7'P64FC  ',CL1'D',CL1'E',CL1'Y'  CHECK JOBNAME
         DC    CL7'P63PR  ',CL1'D',CL1'B',CL1'Y'  CHECK JOBNAME
         DC    CL7'P64RP  ',CL1'D',CL1'B',CL1'Y'  CHECK JOBNAME
         DC    CL7'P74PR01',CL1'G',CL1'*',CL1'N'  NO JOBNAME CHK
         DC    CL7'P75PR  ',CL1'G',CL1'*',CL1'N'  NO JOBNAME CHK
         DC    CL7'PBHDD  ',CL1'H',CL1' ',CL1'Y'  CHECK JOBNAME
         DC    CL7'PDI    ',CL1'F',CL1'F',CL1'N'  NO JOBNAME CHK
         DC    CL7'PDJ    ',CL1'F',CL1'F',CL1'N'  NO JOBNAME CHK
         DC    CL7'S      ',CL1'H',CL1'H',CL1'P'  PREFIX CHK
         DC    CL7'TDI    ',CL1'F',CL1'F',CL1'N'  NO JOBNAME CHK
         DC    CL7'T00AA05',CL1'L',CL1' ',CL1'Y'  CHECK JOBNAME
         DC    CL7'T00AA07',CL1'L',CL1' ',CL1'Y'  CHECK JOBNAME
         DC    CL7'T40AB06',CL1'9',CL1' ',CL1'Y'  CHECK JOBNAME
         DC    CL7'T40AC0 ',CL1'J',CL1' ',CL1'Y'  CHECK JOBNAME
         DC    CL7'T40AC1 ',CL1'J',CL1' ',CL1'Y'  CHECK JOBNAME
         DC    CL7'T40AC21',CL1'Z',CL1' ',CL1'Y'  CHECK JOBNAME
         DC    CL7'T40AF  ',CL1'E',CL1'C',CL1'Y'  CHECK JOBNAME
         DC    CL7'T40AG01',CL1'9',CL1' ',CL1'Y'  CHECK JOBNAME
         DC    CL7'T40AG02',CL1'9',CL1' ',CL1'Y'  CHECK JOBNAME
         DC    CL7'T40AG05',CL1'9',CL1' ',CL1'Y'  CHECK JOBNAME
         DC    CL7'T40AG10',CL1'9',CL1' ',CL1'Y'  CHECK JOBNAME
         DC    CL7'T40AG06',CL1'9',CL1' ',CL1'Y'  CHECK JOBNAME
         DC    CL7'T40AG13',CL1'9',CL1' ',CL1'Y'  CHECK JOBNAME
         DC    CL7'T40AG34',CL1'9',CL1' ',CL1'Y'  CHECK JOBNAME
         DC    CL7'T40BC07',CL1'M',CL1' ',CL1'Y'  CHECK JOBNAME
         DC    CL7'T40BC00',CL1'9',CL1' ',CL1'Y'  CHECK JOBNAME
         DC    CL7'T40BC01',CL1'9',CL1' ',CL1'Y'  CHECK JOBNAME
         DC    CL7'T40BC03',CL1'9',CL1' ',CL1'Y'  CHECK JOBNAME
         DC    CL7'T40BC16',CL1'9',CL1' ',CL1'Y'  CHECK JOBNAME
         DC    CL7'T40BC19',CL1'9',CL1' ',CL1'Y'  CHECK JOBNAME
         DC    CL7'T40BE  ',CL1'U',CL1' ',CL1'Y'  CHECK JOBNAME
         DC    CL7'T40BG01',CL1'9',CL1' ',CL1'Y'  CHECK JOBNAME
         DC    CL7'T40BG07',CL1'9',CL1' ',CL1'Y'  CHECK JOBNAME
         DC    CL7'T40BV01',CL1'J',CL1' ',CL1'Y'  CHECK JOBNAME
         DC    CL7'T40DB20',CL1'J',CL1' ',CL1'Y'  CHECK JOBNAME
         DC    CL7'T40EK  ',CL1'9',CL1' ',CL1'Y'  CHECK JOBNAME
         DC    CL7'T40PC00',CL1'9',CL1' ',CL1'Y'  CHECK JOBNAME
         DC    CL7'T4E0A  ',CL1'T',CL1'C',CL1'Y'  CHECK JOBNAME
         DC    CL7'T      ',CL1'T',CL1' ',CL1'Y'  CHECK JOBNAME
         DC    CL7'       ',CL1' ',CL1' ',CL1' '  DEFAULT
         EJECT
**********************************************************************
*                                                                    *
*        EXECUTION CLASS TABLE                                       *
*                                                                    *
**********************************************************************
         SPACE 2
CLSTAB   DS    0C      EXECUTION CLASS TABLE
*
*              XCLSI       EXECUTION CLASS ALLOWED
*
         DC    CL1'A',CL37'ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789 '
         DC    CL1'B',CL37'P1234                                ' MOCS
         DC    CL1'D',CL37'P                                    ' PAY
         DC    CL1'E',CL37'FG1234                               ' NHL
         DC    CL1'F',CL37'DP1234                               ' RCA
         DC    CL1'G',CL37'FG                                   ' NHL
         DC    CL1'H',CL37'CHP1234789                           ' SOFT
         DC    CL1'I',CL37'A                                    ' FINA
         DC    CL1'J',CL37'H12349                               ' DATAB
         DC    CL1'L',CL37'L1234                                ' PAYR
         DC    CL1'M',CL37'M1234                                ' PAYR
         DC    CL1'S',CL37'PS                                   ' DATA
         DC    CL1'T',CL37'1234                                 ' PGMRS
         DC    CL1'U',CL37'K1234                                ' PAYR
         DC    CL1'Z',CL37'Z                                    ' AUD
         DC    CL1'0',CL37' ** BATCH SUBMISSION NOT ALLOWED **  ' CIBA
         DC    CL1'9',CL37'12349                                ' DCS
         DC    CL1' ',CL37'1234                                 ' DEF.
         SPACE 2
**********************************************************************
*                                                                    *
*        MESSAGE CLASS TABLE                                         *
*                                                                    *
**********************************************************************
         SPACE 2
MSGTAB   DS    0C      MESSAGE CLASS TABLE
*
*              MCLSI       MESSAGE CLASS ALLOWED
*
         DC    CL1'A',CL37'ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789 '
         DC    CL1'B',CL37'ACDSXZ                               ' USV
         DC    CL1'C',CL37'ACX                                  ' NHL
         DC    CL1'D',CL37'ACDX                                 ' CONS
         DC    CL1'E',CL37'AD                                   ' CONS
         DC    CL1'F',CL37'ACDX                                 ' RCA
         DC    CL1'H',CL37'ACDJXZ0                              ' SOFT
         DC    CL1' ',CL37'CX                                   ' DEF.
         EJECT
**********************************************************************
*                                                                    *
*        TIME TABLE FOR EACH EXECUTION CLASS                         *
*                                                                    *
**********************************************************************
         SPACE 2
TIMTAB   DS    0H      TIME PARAMETER TABLE VS. EXECUTION CLASS
*
*        EXECUTION CLASS
*                      MAND - Y = MUST HAVE TIME PARAMETER ON JOB
*                           - N = MUST NOT HAVE TIME - PARM IS USED
*                           - O = OPTIONAL BUT PARM WILL BE USED IF
*                                 NOT SPECIFIED ON JOB CARD
*                            DEF. - DEFAULT TIME TO USE IF NOT
*                                   SPECIFIED - IN SECONDS -
*                                   0 INDICATES NO DEFAULT
*                                 MAX - MAX TIME ALLOWED FOR CLASS
*                                       SPECIFIED - IN SECONDS
*                                       0 INDICATES NO MAX VALUE
*
*              XEQCLS  MAND. DEF. MAX
*
         DC    CL1'A',CL1'O',H'0',H'0'
         DC    CL1'B',CL1'O',H'0',H'0'
         DC    CL1'C',CL1'O',H'0',H'0'    CICS
         DC    CL1'D',CL1'O',H'0',H'0'
         DC    CL1'E',CL1'O',H'0',H'0'    OTB
         DC    CL1'F',CL1'O',H'0',H'0'    NHL
         DC    CL1'G',CL1'O',H'0',H'0'    CHL
         DC    CL1'H',CL1'O',H'0',H'0'    SYSTEM SOFTWARE
         DC    CL1'I',CL1'O',H'0',H'0'    ONLINE
         DC    CL1'J',CL1'O',H'0',H'0'
         DC    CL1'K',CL1'O',H'0',H'0'
         DC    CL1'L',CL1'O',H'0',H'0'
         DC    CL1'M',CL1'O',H'0',H'0'
         DC    CL1'N',CL1'O',H'0',H'0'
         DC    CL1'O',CL1'O',H'0',H'0'    PRD
         DC    CL1'P',CL1'O',H'0',H'0'    PRODUCTION BATCH
         DC    CL1'Q',CL1'O',H'0',H'0'    BILLING
         DC    CL1'R',CL1'O',H'0',H'0'    O/P
         DC    CL1'S',CL1'O',H'0',H'0'    PACK & MANI
         DC    CL1'T',CL1'O',H'0',H'0'
         DC    CL1'U',CL1'O',H'0',H'0'
         DC    CL1'V',CL1'O',H'0',H'0'
         DC    CL1'W',CL1'O',H'0',H'0'
         DC    CL1'X',CL1'O',H'0',H'0'
         DC    CL1'Y',CL1'O',H'0',H'0'
         DC    CL1'Z',CL1'O',H'0',H'0'
         DC    CL1'0',CL1'O',H'0',H'0'
         DC    CL1'1',CL1'O',H'10',H'10'   TEST CLASS 1
         DC    CL1'2',CL1'O',H'20',H'120'  TEST CLASS 2
         DC    CL1'3',CL1'O',H'20',H'120'  TEST CLASS 3
         DC    CL1'4',CL1'O',H'10',H'10'   TEST CLASS 4
         DC    CL1'5',CL1'O',H'0',H'0'
         DC    CL1'6',CL1'O',H'0',H'0'
         DC    CL1'7',CL1'O',H'0',H'0'
         DC    CL1'8',CL1'O',H'0',H'0'
         DC    CL1'9',CL1'O',H'0',H'0'      DCS TEST
         DC    CL1' ',CL1' ',H'0',H'0'      END OF TABLE
         EJECT
**********************************************************************
*                                                                    *
*        OPERATOR COMMAND TABLE THAT WILL NOT BE ALLOWED IN JCL      *
*                                                                    *
**********************************************************************
         SPACE 2
OPERCMDS DC    AL1(6),CL9'CANCEL   '
         DC    AL1(1),CL9'C        '
         DC    AL1(8),CL9'CHNGDUMP '
         DC    AL1(2),CL9'CD       '
         DC    AL1(7),CL9'DISPLAY  '
         DC    AL1(1),CL9'D        '
         DC    AL1(4),CL9'HOLD     '
         DC    AL1(1),CL9'H        '
         DC    AL1(3),CL9'LOG      '
         DC    AL1(1),CL9'L        '
         DC    AL1(5),CL9'MODIFY   '
         DC    AL1(1),CL9'F        '
         DC    AL1(7),CL9'MONITOR  '
         DC    AL1(2),CL9'MN       '
         DC    AL1(5),CL9'MOUNT    '
         DC    AL1(1),CL9'M        '
         DC    AL1(7),CL9'PAGEADD  '
         DC    AL1(2),CL9'PA       '
         DC    AL1(7),CL9'RELEASE  '
         DC    AL1(1),CL9'A        '
         DC    AL1(5),CL9'REPLY    '
         DC    AL1(1),CL9'R        '
         DC    AL1(5),CL9'RESET    '
         DC    AL1(1),CL9'E        '
         DC    AL1(4),CL9'SEND     '
         DC    AL1(2),CL9'SE       '
         DC    AL1(3),CL9'SET      '
         DC    AL1(1),CL9'T        '
         DC    AL1(6),CL9'SETDMN   '
         DC    AL1(2),CL9'SD       '
         DC    AL1(4),CL9'SLIP     '
         DC    AL1(2),CL9'SL       '
         DC    AL1(5),CL9'START    '
         DC    AL1(1),CL9'S        '
         DC    AL1(4),CL9'STOP     '
         DC    AL1(1),CL9'P        '
         DC    AL1(6),CL9'STOPMN   '
         DC    AL1(2),CL9'PM       '
         DC    AL1(6),CL9'UNLOAD   '
         DC    AL1(1),CL9'U        '
         DC    AL1(4),CL9'VARY     '
         DC    AL1(1),CL9'V        '
         DC    AL1(8),CL9'WRITELOG '
         DC    AL1(1),CL9'W        '
         DC    AL1(0),CL9'         '  END OF TABLE - LENGTH 0
         SPACE
         EJECT
**********************************************************************
*                                                                    *
*        DSECTS USED BY THIS ROUTINE                                 *
*                                                                    *
**********************************************************************
         SPACE 2
**********************************************************************
*                                                                    *
*        DSECT USED TO MAP GOTTEN REGISTER SAVE AREA                 *
*                                                                    *
**********************************************************************
         SPACE
WORKREGS DSECT
         SPACE
REGAREA  DS    18F
         SPACE
AREAREGS EQU   *-WORKREGS
         EJECT
**********************************************************************
*                                                                    *
*        DSECT USED TO MAP GOTTEN WORK AREA                          *
*                                                                    *
**********************************************************************
         SPACE 2
WORKAREA DSECT
         SPACE
DWWORK   DS    D         DOUBLE WORD WORK AREA - CVB CVD
EDWORK   DS    D         DOUBLE WORD WORK AREA - EDIT
CPUTIMAX DS    D         EDITED OUTPUT FOR CPUTIME - DEFAULT AND MAX
R15D1    DS    F         REG 15 SAVE AREA
         SPACE
UIDXEQCP DS    F         POINTER TO EXECUTION CLASS TABLE FOR MESSAGE
UIDMSGCP DS    F         POINTER TO MSGCLASS TABLE FOR MESSAGE
         SPACE
FLAGFND  DS    XL1       FLAG FOR PARAMETER FOUND
         SPACE
FLJOBNAM EQU   X'80'     "JOBNAME FLAG"
FLXEQCLS EQU   X'40'     "EXECUTION CLASS FLAG"
FLMSGCLS EQU   X'20'     "MSGCLASS FLAG"
FLCPUTIM EQU   X'10'     "CPU TIME FLAG"
         SPACE
FLAGABR  DS    XL1       FLAG FOR ABORT REASON
         SPACE
* FLJOBNAM EQU   X'80'
* FLXEQCLS EQU   X'40'
* FLMSGCLS EQU   X'20'
* FLCPUTIM EQU   X'10'
FLJERRIC EQU   X'08'     "JOB CARD ERROR FLAG - > 72 CHAR"
FLCPUTIN EQU   X'02'     "UNABLE TO INSERT COMMA IN JOB CARD FLAG"
FLINVCMD EQU   X'01'     "INVALID COMMAND - JES OR SYSTEM IN JCL"
         SPACE
FLAGMSG  DS    XL1       FLAG FOR MESSAGE TO SEND
         SPACE
* FLJOBNAM EQU   X'80'
* FLXEQCLS EQU   X'40'
* FLMSGCLS EQU   X'20'
* FLCPUTIM EQU   X'10'
* FLJERRIC EQU   X'08'
FLJOBPRF EQU   X'04'     "USERID PREFIX FLAG"
* FLCPUTIN EQU   X'02'
* FLINVCMD EQU   X'01'
         SPACE
FLAGMS1  DS    XL1       FLAG FOR VALID CLASS MESSAGE TO SEND
         SPACE
* FLXEQCLS EQU   X'40'
* FLMSGCLS EQU   X'20'
* FLCPUTIM EQU   X'10'
* FLINVCMD EQU   X'01'
         SPACE
FLAG0XL  DS    XL1       FLAG FOR WARNING OR ABORT MESSAGE SENT
         SPACE
FLJOBNAX EQU   X'80'     FLAG THESE GUYS SO THAT WE ONLY SEND
FLXEQCLX EQU   X'40'     ONE WARNING OR ABORT MESSAGE PER ERROR
FLMSGCLX EQU   X'20'
FLCPUTIX EQU   X'10'
FLJERRIX EQU   X'08'
FLINVCMX EQU   X'01'
         SPACE
TIMEFLG  DS    XL1       FLAG FOR TIME CHECK USE
         SPACE
TIMEFLGS EQU   X'80'     FLAG TO INDICATE TIME IN SECONDS
TIMEFLGE EQU   X'40'     FLAG TO INDICATE END OF TIME PARMS
TIMEFLGO EQU   X'20'     FLAG TO INDICATE OPENING PAREND FOUND
         SPACE
JOBNAM   DS    CL8   JOBNAME FROM JOBCARD SUBMITTED
XEQCLS   DS    CL1   EXECUTION CLASS FROM JOBCARD SUBMITTED
MSGCLS   DS    CL1   MSGCLASS FROM JOBCARD SUBMITTED
CPUTIM   DS    CL8   TIME PARAMETER FROM JOBCARD
SCANCHR  DS    CL1   CHARACTER TO SCAN FOR USED BY "SCAN" ROUTINE
MESSAGE  DS    CL80  AREA FOR MESSAGE TO BE SENT TO USER
TIMECARD DS    CL80  AREA TO BUILD THE "TIME" CARD FOR INSERTION
INVCARD  DS    CL80  AREA TO SAVE DETECTED JES2 OR SYSTEM COMMAND
UIDPFX   DS    CL7   USERID PREFIX FOUND IN UID TABLE FOR MESSAGE
         SPACE
AREAWORK EQU   *-WORKAREA
         EJECT
**********************************************************************
*                                                                    *
*        DSECT USED TO MAP USERID TABLE ENTRY                        *
*                                                                    *
**********************************************************************
         SPACE
UIDDSECT DSECT
         SPACE
UIDENTRY EQU   *
         SPACE
UID      DS    CL7   USERID OR FIRST N CHARACTERS OF USERID
XEQCLSI  DS    CL1   INDEX TO EXECUTION CLASS TABLE
MSGCLSI  DS    CL1   INDEX TO MSGCLASS TABLE
JOBN     DS    CL1   Y/N/P
         SPACE
UIDELEN  EQU   *-UIDENTRY
         EJECT
         IKJEFFIE IETYPE=SUBMIT
         SPACE
         END
/*
//LKED1   EXEC PGM=IEWL,PARM='LIST,LET,MAP,XREF,RENT,REFR',
//             COND=(8,LT,ASM)
//SYSUT1   DD  DSN=&&SYSUT1,UNIT=VIO,SPACE=(1024,(50,20))
//SYSPRINT DD  SYSOUT=*,DCB=(RECFM=FB,LRECL=121,BLKSIZE=1210)
//SYSLIN   DD  DSN=&&OBJSET,DISP=(OLD,PASS)
//         DD  DDNAME=SYSIN
//SYSLMOD  DD  DSN=SYS1.LINKLIB,DISP=SHR
//SYSIN    DD *
 SETCODE AC(1)
 NAME IKJEFF10(R)
//*
