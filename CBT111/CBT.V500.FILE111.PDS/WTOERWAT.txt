//S40DA01S JOB (11613109,0402,1,1,,LONG,,,104),PURDY,CLASS=H,
//            MSGCLASS=X
//ASM     EXEC PGM=IEV90,
//             PARM='NODECK,OBJ,LINECNT=99,BUFSIZE(MAX),RENT',
//             REGION=4096K
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR
//         DD  DSN=SYS1.AMODGEN,DISP=SHR
//SYSUT1   DD  DSN=&&SYSUT1,UNIT=VIO,SPACE=(1700,(600,100))
//SYSUT2   DD  DSN=&&SYSUT2,UNIT=VIO,SPACE=(1700,(300,50))
//SYSUT3   DD  DSN=&&SYSUT3,UNIT=VIO,SPACE=(1700,(300,50))
//SYSPRINT DD  SYSOUT=*,DCB=BLKSIZE=1089
//SYSPUNCH DD  DUMMY
//SYSLIN   DD  DSN=&&OBJSET,UNIT=SYSSQ,SPACE=(80,(200,50)),
//             DISP=(MOD,PASS)
//SYSIN    DD  *
*          DATA SET WTOERWAT   AT LEVEL 005 AS OF 10/20/86
WTOERWAT TITLE 'WTOERWAT -- WTOR EXIT TO REPLY "WAIT" TO MESSAGE'
WTOERWAT CSECT
WTOERWAT AMODE 31
WTOERWAT RMODE ANY
         USING *,R12
         STM   R14,R12,12(R13)
         LR    R12,R15
         B     START
         DC    C'WTOERWAT'
         DC    C'&SYSDATE',C' &SYSTIME'
         PRINT NOGEN
START    L     R11,0(R1)           PICK UP ADDRESS OF CTXT
         USING CTXT,R11
         L     R10,CTXTTXPJ   LOAD POINTER TO THE MESSAGE ATTRIBUTES
         USING CTXTATTR,R10
         TM    CTXTTFB1,CTXTTFWR  WTOR?
         BNO   RETURN          NO, FORGET IT
         LA    R3,CTXTTMSG    LOAD START OF MESSAGE
         LA    R4,1           LOAD INCREMENT
         LA    R5,CTXTTMSG+L'CTXTTMSG-6 END OF MESSAGE
WAITCK   CLC   =C'''WAIT''',0(R3) IS THERE A "WAIT" IN MESSAGE?
         BE    WAITOK          YES, REPLY TO IT
         CLC   =C' WAIT ',0(R3) IS THERE A "WAIT" IN MESSAGE?
         BE    WAITOK          YES, REPLY TO IT
         CLC   =C' WAIT,',0(R3) IS THERE A "WAIT" IN MESSAGE?
         BE    WAITOK          YES, REPLY TO IT
         BXLE  R3,R4,WAITCK   NOT YET, TRY AGAIN
         B     RETURN         NO, FORGET IT
WAITOK   GETMAIN RU,LV=WORKEND,SP=230,LOC=(BELOW,ANY)
         ST    R13,4(R1)
         ST    R1,8(R13)
         LR    R13,R1
         USING WORKAREA,R13
         XC    MGCRPL(MGCRSIZ),MGCRPL CLEAR THE MGCR AREA
         MVC   MGCRTEXT(L'REPLY),REPLY MOVE REPLY TO MACRO AREA
         MVC   MGCRTEXT+6(L'CTXTRPID),CTXTRPID
         LA    R3,(MGCRTEXT-MGCRPL)+L'REPLY
         STC   R3,MGCRLGTH    SET THE LENGTH OF THE REPLY
         SR    R0,R0          CLEAR R0 FOR SOME REASON
         MGCR  MGCRPL
         SPACE
         MVC   SMFREC(SMFSLEN),SMFSKEL MOVE SKELETON SMF RECORD
         TIME  BIN           GET DATE AND TIME
         ST    R0,SMFDTIME   STORE DATE & TIME IN RECORD
         ST    R1,SMFDDATE
         L     R1,16         LOAD ADDRESS OF CVT
         L     R1,196(R1)    LOAD ADDRESS OF SMCA
         MVC   SMFDSID,16(R1) MOVE SYSTEM ID FROM SMCA
         SPACE
         SMFWTM SMFREC       WRITE SMF RECORD
         SPACE
         L     R10,4(R13)     RETURN TO MPF
         FREEMAIN RU,LV=WORKEND,A=(R13),SP=230
         LR    R13,R10
         SPACE
         SPACE
RETURN   LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
         SPACE 1
REPLY    DC    C'REPLY NN,''WAIT'' BY WTOERWAT'
         CNOP  2,4
SMFSKEL  DS    0H        SMF RECORD TO BE WRITTEN
SMFLEN   DC    H'19'     RECORD LENGTH
SMFSEG   DC    H'0'      SEGMENT INDICATOR
SMFSIND  DC    X'06'     SYSTEM INDICATOR
SMFRECTY DC    AL1(200)  RECORD TYPE
SMFTIME  DS    XL4       TIME
SMFDATE  DS    XL4       DATE
SMFSID   DS    CL4       SYSTEM ID
SMFTYPE  DC    C'A'      A - ALLOCATION RECOVERY
SMFSLEN  EQU   *-SMFSKEL
         SPACE
         LTORG
         SPACE 3
         PRINT   GEN
WORKAREA DSECT
         DS    18F            SAVE AREA
         CNOP  2,4
SMFREC   DS    0H        SMF RECORD TO BE WRITTEN
SMFDLEN  DS    H         LENGTH
SMFDSEG  DS    H         SEGMENT INDICATOR
SMFDSIND DS    XL1       SYSTEM INDICATOR
SMFDRECT DS    XL1       RECORD TYPE
SMFDTIME DS    XL4       TIME
SMFDDATE DS    XL4       DATE
SMFDSID  DS    CL4       SYSTEM ID
SMFDTYPE DS    CL1       A - ALLOCATION RECOVERY
         SPACE
         IEZMGCR DSECT=NO
         ORG
WORKEND  EQU   *-WORKAREA
         SPACE 3
         IEZVX100
         PRINT NOGEN
         REGEQU
         END   WTOERWAT
/*
//LKED1   EXEC PGM=IEWL,PARM='LIST,LET,MAP,XREF,RENT,REFR',
//             COND=(8,LT,ASM)
//SYSUT1   DD  DSN=&&SYSUT1,UNIT=VIO,SPACE=(1024,(50,20))
//SYSPRINT DD  SYSOUT=*,DCB=(RECFM=FB,LRECL=121,BLKSIZE=1210)
//SYSLIN   DD  DSN=&&OBJSET,DISP=(OLD,PASS)
//         DD  DDNAME=SYSIN
//SYSLMOD  DD  DSN=SYS1.LINKLIB,DISP=SHR
//SYSIN    DD *
  SETCODE AC(1)
  NAME WTOERWAT(R)
/*
