CNVDATE  TITLE 'GENERAL PURPOSE DATE MANIPULATION ROUTINE'
*---------------------------------------------------------------------*
*                                                                     *
*  PROGRAM: CNVDATE                                                   *
*  PURPOSE: DATE CONVERSION AND MANIPULATION                          *
*                                                                     *
*  ORIGINAL AUTHOR: WES CLEVELAND                                     *
*                                                                     *
*  CONTRIBUTOR:                                                       *
*           JIM SMITH                                                 *
*           NATIONAL SERVICE INDUSTRIES                               *
*           INFORMATION SYSTEMS                                       *
*           MAIL STOP 003                                             *
*           1420 PEACHTREE ST N.E.                                    *
*           ATLANTA, GEORGIA  30309                                   *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*    THIS PROGRAM IS A GENERAL PURPOSE DATE MANIPULATION ROUTINE THAT *
* MAY BE CALLED TO PERFORM THE FOLLOWING DATE MANIPULATION FUNCTIONS: *
*                                                                     *
*    1) VERIFY    JULIAN & GREGORIAN DATES                            *
*    2) CONVERT   JULIAN & GREGORIAN DATES                            *
*    3) INCREMENT JULIAN & GREGORIAN DATES                            *
*    4) DECREMENT JULIAN & GREGORIAN DATES                            *
*    5) CALCULATE DIFFERENCE BETWEEN JULIAN OR GREGORIAN DATES        *
*    6) CALCULATE DAY OF WEEK                                         *
*    7) CALCULATE DAY OF CENTURY                                      *
*                                                                     *
* LINKAGE:  R1 = CNVDATE WORK AREA ADDRESS                            *
*          R13 = SAVE AREA ADDRESS                                    *
*          R14 = RETURN ADDRESS                                       *
*          R15 = ENTRY ADDRESS                                        *
*                                                                     *
*  RETURN: R15 = RETURN CODE                                          *
*                00 = FUNCTION COMPLETE WITHOUT ERROR                 *
*                04 = INVALID DATE DATA                               *
*                08 = INVALID PARAMATER SPECIFICATION                 *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
* FORMATS: GREGORIAN - MMDDYY                                         *
*                      MMDDYYYY                                       *
*             JULIAN - YYDDD                                          *
*                      YYYYDDD                                        *
*              VALUE - DDDD                                           *
*                      DDDDDDDD                                       *
*                DAY - DXXXXXXXXX (D=DAY NUMBER, X=DAY SPELLED OUT)   *
*                                                                     *
*  NOTE: INITIALIZE FIELDS-1 AND FIELD-2 WITH BLANKS BEFORE MOVING    *
*        IN REQUESTED DATES (FIELD-1) OR INCREMENT/DECREMENT NBR      *
*        (FIELDS-2).  THE INCREMENT/DECREMENT NBR (FIELD-2) MUST BE   *
*        LEFT JUSTIFIED.                                              *
*                                                                     *
*          WORK AREA - FUNCTION  (1 BYTE)                             *
*                      FIELD-1   (8 BYTES)                            *
*                      FIELD-2   (8 BYTES)                            *
*                      RETURN   (10 BYTES)                            *
*                                                                     *
*                      FUNC    FIELD-1    FIELD-2    RETURN           *
*                      ----    -------    -------    -------          *
*                        1     DATE       N/A        N/A              *
*                        2     DATE       N/A        DATE             *
*                        3     DATE       DDDDDDDD   DATE             *
*                        4     DATE       DDDDDDDD   DATE             *
*                        5     DATE       DATE       DDDDDDDD         *
*                        6     DATE       N/A        DXXXXXXXXX       *
*                        7     DATE       N/A        DDDDDDDD         *
*                                                                     *
* EXAMPLE COBOL: CURRENT-DATE 071392 DECREMENT 30 DAYS                *
*                                                                     *
*      01  WS-CNVDATE-WORK-AREA    PIC X(27).                         *
*                                                                     *
*      01  FILLER REDEFINES WS-CNVDATE-WORK-AREA.                     *
*                                                                     *
*          05  FUNCTION            PIC X.                             *
*          05  SUBR-DATE.                                             *
*              10  SR-MM           PIC XX.                            *
*              10  SR-DD           PIC XX.                            *
*              10  SR-YY           PIC XX.                            *
*              10  FILLER          PIC XX.                            *
*          05  AGE-CRITERA         PIC X(8).                          *
*          05  AGED-DATE.                                             *
*              10  AD-MM           PIC XX.                            *
*              10  AD-DD           PIC XX.                            *
*              10  AD-YY           PIC XX.                            *
*              10  FILLER          PIC XXXX.                          *
*                                                                     *
*                                                                     *
*          MOVE CURRENT-DATE TO WORK-DATE.                            *
*          MOVE SPACES TO WS-CNVDATE-WORK-AREA.                       *
*          MOVE WD-MM TO SR-MM.                                       *
*          MOVE WD-DD TO SR-DD.                                       *
*          MOVE WD-YY TO SR-YY.                                       *
*          MOVE '4' TO FUNCTION.                                      *
*          MOVE '30      ' TO AGE-CRITERA.                            *
*          CALL 'CNVDATE' USING WS-CNVDATE-WORK-AREA.                 *
*                                                                     *
* EXAMPLE RETURNED AGED-DATE: 061392                                  *
*                                                                     *
*---------------------------------------------------------------------*
         TITLE 'GENERAL PURPOSE DATE MANIPULATION ROUTINE - REGISTER EQ+
               UATES'
*---------------------------------------------------------------------*
*        REGISTER EQUATES                                             *
*---------------------------------------------------------------------*
R0       EQU   0                   REGISTER ZERO
R1       EQU   1                   REGISTER ONE
R2       EQU   2                   REGISTER TWO
R3       EQU   3                   REGISTER THREE
R4       EQU   4                   REGISTER FOUR
R5       EQU   5                   REGISTER FIVE
R6       EQU   6                   REGISTER SIX
R7       EQU   7                   REGISTER SEVEN
R8       EQU   8                   REGISTER EIGHT
R9       EQU   9                   REGISTER NINE
R10      EQU   10                  REGISTER TEN
R11      EQU   11                  REGISTER ELEVEN
R12      EQU   12                  REGISTER TWELVE
R13      EQU   13                  REGISTER THIRTEEN
R14      EQU   14                  REGISTER FORTEEN
R15      EQU   15                  REGISTER FIFTEEN
*
SUBRLINK EQU   R14                 SUB-ROUTINE LINK REGISTER
DSABASE  EQU   R13                 DYNAMIC STORAGE AREA BASE
CNVBASE  EQU   R12                 CNVDATE BASE REGISTER
PARMBASE EQU   R11                 PARM BASE REGISTER
FUNCBASE EQU   R10                 FUNCTION BASE REGISTER
FUNCENT  EQU   R9                  FUNCTION ENTRY ADDRESS
FUNCLINK EQU   R8                  FUNCTION LINK REGISTER
INPREG   EQU   R7                  INPUT DATA ADDRESS
         TITLE 'GENERAL PURPOSE DATE MANIPULATION ROUTINE - DYNAMIC STO+
               RAGE AREA'
*---------------------------------------------------------------------*
*        DYNAMIC STORAGE AREA                                         *
*---------------------------------------------------------------------*
DSA      DSECT                     DYNAMIC STORAGE AREA
DSASAVE  DS    18F                   REGISTER SAVE AREA
DSADWORD DS    D                     DOUBLE WORD WORK AREA
*                                    FIELD FORMAT TYPES
DSA$NULL EQU   B'00000000'             NULL DATA (N/A)
DSA$GDAT EQU   B'10000000'             GREGORIAN DATE
DSA$JDAT EQU   B'01000000'             JULIAN DATE
DSA$VALU EQU   B'00100000'             VALUE
DSA$CHAR EQU   B'00010000'             CHARACTER
DSA$YEXT EQU   B'00000001'             EXTENDED YEAR
DSA$FLD1 DS    B                     FIELD 1 TYPE
DSA@FLD1 DS    AL3                   FIELD 1 BINARY DATA
DSA$FLD2 DS    B                     FIELD 2 TYPE
DSA@FLD2 DS    AL3                   FIELD 2 BINARY DATA
DSA$RET  DS    B                     RETURN FIELD TYPE
DSA@RET  DS    AL3                   RETURN FIELD BINARY DATA
DSA@RETC DS    CL10                  RETURN FIELD CHARACTER DATA
DSADATE  DS    0CL11                 DATE WORK AREA
DSAYYYY  DS    0CL4,CL2                EXTENDED YEAR
DSAYY    DS    CL2                     YEAR
DSAMM    DS    CL2                     MONTH
DSADD    DS    CL2                     GREGORIAN DAY OF MONTH
DSADDD   DS    CL3                     JULIAN DAY OF YEAR
DSAEND   EQU   *                     DSA END
DSALEN   EQU   DSAEND-DSA            DSA LENGTH
         TITLE 'GENERAL PURPOSE DATE MANIPULATION ROUTINE - PARM WORK A+
               REA'
*---------------------------------------------------------------------*
*        PARM WORK AREA                                               *
*---------------------------------------------------------------------*
PARM     DSECT                     PARM WORK AREA
PARMREQ  DS    C                     REQUEST CODE ('1'..'7')
PARMFLD1 DS    CL8                   FIELD 1
PARMFLD2 DS    CL8                   FIELD 2
PARMRET  DS    0CL8                  RETURN FIELD
PARMRETC DS    CL10                  RETURN FIELD CHARACTER
         TITLE 'GENERAL PURPOSE DATE MANIPULATION ROUTINE - HOUSKEEPING+
                ROUTINE'
*---------------------------------------------------------------------*
*        HOUSKEEPING ROUTINE                                          *
*---------------------------------------------------------------------*
CNVDATE  CSECT
         B     4+8+8+8(R15)        BYPASS PROGRAM HEADER INFORMATION
         DC    CL8'CNVDATE '         PROGRAM NAME
         DC    CL8'&SYSDATE'         DATE ASSEMBLED
         DC    CL8'&SYSTIME'         TIME ASSEMBLED
         STM   R14,R12,12(R13)     SAVE CALLERS REGISTERS
         LR    CNVBASE,R15         MAKE PROGRAM ADDRESSABLE
         USING CNVDATE,CNVBASE
         L     PARMBASE,0(R1)      MAKE PARM ADDRESSABLE
         USING PARM,PARMBASE
         LA    R0,DSALEN           GET DSA LENGTH
         GETMAIN R,LV=(0)          ACQUIRE DSA
         USING DSA,R1
         ST    R13,DSASAVE+4       CHAIN BACKWARD
         LA    R2,DSASAVE
         DROP  R1
         ST    R2,8(R13)           CHAIN FORWARD
         LR    DSABASE,R1          MAKE DSA ADDRESSABLE
         USING DSA,DSABASE
         TITLE 'GENERAL PURPOSE DATE MANIPULATION ROUTINE - FUNCTION LO+
               OK-UP && PROCESSING ROUTINE'
*---------------------------------------------------------------------*
*        FUNCTION LOOK-UP & PROCESSING ROUTINE                        *
*---------------------------------------------------------------------*
         MVC   PARMRETC,BLANKS     CLEAR RETURN DATA
         CLI   PARMREQ,C'1'        FUNCTION VALID
         BL    RETURN08              NO-RETURN CODE=16
         CLI   PARMREQ,C'7'
         BH    RETURN08
         IC    FUNCENT,PARMREQ     GET REQUEST CODE
         SLL   FUNCENT,28          MASK OFF HIGH-ORDER BITS
         SRL   FUNCENT,28
         BCTR  FUNCENT,R0          DECREMENT BY 1
         SLL   FUNCENT,4           TIMES 16 FOR ENTRY OFFSET
         LA    FUNCENT,FUNCTBL(FUNCENT)  POINT TO FUNCTION ENTRY
         LA    INPREG,PARMFLD1     POINT TO FIELD 1
         L     FUNCBASE,0(FUNCENT) GET INPUT ROUTINE ADDRESS
         BALR  FUNCLINK,FUNCBASE   GO TO PROCESSING ROUTINE
         STCM  INPREG,15,DSA$FLD1  SAVE BINARY FIELD DATA
         MVC   DSA$RET,DSA$FLD1    RESULT SAME AS INPUT DATE
         LA    INPREG,PARMFLD2     POINT TO FIELD 2
         L     FUNCBASE,4(FUNCENT) GET INPUT ROUTINE ADDRESS
         BALR  FUNCLINK,FUNCBASE   GO TO PROCESSING ROUTINE
         STCM  INPREG,15,DSA$FLD2  SAVE BINARY FIELD DATA
         L     FUNCBASE,8(FUNCENT) GET FUNCTION ROUTINE ADDRESS
         BALR  FUNCLINK,FUNCBASE   GO TO PROCESSING ROUTINE
         L     FUNCBASE,12(FUNCENT) GET RETURN ROUTINE ADDRESS
         BALR  FUNCLINK,FUNCBASE   GO TO PROCESSING ROUTINE
         B     RETURN00            GO RETURN TO CALLER
         TITLE 'GENERAL PURPOSE DATE MANIPULATION ROUTINE - PROGRAM TER+
               MINATION PROCESSING ROUTINE'
*---------------------------------------------------------------------*
*        PROGRAM TERMINATION PROCESSING ROUTINE                       *
*---------------------------------------------------------------------*
RETURN00 DS    0H
         SR    R15,R15             SET RETURN CODE = 00
         B     RETURN              GO RETURN TO CALLER
RETURN04 DS    0H
         LA    R15,04              SET RETURN CODE = 04
         B     RETURN              GO RETURN TO CALLER
RETURN08 DS    0H
         LA    R15,08              SET RETURN CODE = 08
RETURN   DS    0H
         LR    R1,DSABASE          POINT TO DSA FOR FREEMAIN
         L     R13,4(R13)          POINT TO CALLERS SAVE AREA
         LA    R0,DSALEN           GET DSA LENGTH
         LR    R3,R15              SAVE RETURN CODE
         FREEMAIN R,LV=(0),A=(1)   FREEMAIN DSA
         LR    R15,R3              RESTORE RETURN CODE
         RETURN (14,12),RC=(15)    RETURN TO CALLER
         TITLE 'GENERAL PURPOSE DATE MANIPULATION ROUTINE - PROCESSING +
               SUB-ROUTINES'
*---------------------------------------------------------------------*
*        CHECK LEAP YEAR SUB-ROUTINE                                  *
*                                                                     *
*        INPUT:  R1 = BINARY YEAR (YYYY)                              *
*                                                                     *
*        OUTPUT: R15 = 0 - YEAR IS NOT A LEAP YEAR                    *
*                R15 = 4 - YEAR IS A LEAP YEAR                        *
*                                                                     *
*        A YEAR IS A LEAP YEAR WHEN:                                  *
*                                                                     *
*            YEAR MOD 400 = 0 OR                                      *
*           (YEAR MOD 4 = 0 AND YEAR MOD 100 Â¬= 0)                    *
*---------------------------------------------------------------------*
CKLEAP   DS    0H
         SR    R15,R15             INDICATE NOT LEAP YEAR
         LR    R2,R1               GET ORIGINAL YEAR
         SRDL  R2,32               SET UP FOR DIVIDE
         D     R2,=F'4'            DIVIDE BY 4
         LTR   R2,R2               IS YEAR EVENLY DIVISIBLE BY 4?
         BNZR  SUBRLINK              NO-RETURN FROM SUB-ROUTINE
         LR    R2,R1               GET ORIGINAL YEAR
         SRDL  R2,32               SET UP FOR DIVIDE
         D     R2,=F'100'          DIVIDE BY 100
         LTR   R2,R2               IS YEAR EVENLY DIVISIBLE BYE 100?
         BNZ   CKLEAP10              NO-GO INDICATE IT'S A LEAP YEAR
         LR    R2,R1               GET ORIGINAL YEAR
         SRDL  R2,32               SET UP FOR DIVIDE
         D     R2,=F'400'          DIVIDE BY 400
         LTR   R2,R2               IS YEAR EVENLY DIVISIBLE BYE 400?
         BNZR  SUBRLINK              NO-RETURN FROM SUB-ROUTINE
CKLEAP10 DS    0H
         LA    R15,4               INDICATE LEAP YEAR
         BR    SUBRLINK            RETURN FROM SUB-ROUTINE
         EJECT
*---------------------------------------------------------------------*
*        CALCULATE DAYS BETWEEN 01/01/00 AND 01/01/YYYY               *
*                                                                     *
*        INPUT:  R1 = BINARY YEAR (YYYY)                              *
*                                                                     *
*        OUTPUT: R0 = DAYS BEFORE YYYY                                *
*                                                                     *
*        RELDAYS = YEAR * 365         (DAYS IN YEAR)                  *
*                + (YEAR - 1) / 4     (TOTAL POSSIBLE LEAP YEARS)     *
*                - (YEAR - 1) / 100   (CENTURY YEARS)                 *
*                + (YEAR - 1) / 400   (CENTURY YEARS EVENLY DIV 400)  *
*---------------------------------------------------------------------*
RELDAYS  DS    0H
         LR    R15,R1              SAVE YEAR
         LR    R2,R15              GET TOTAL NUMBER OF YEARS
         MH    R2,=H'365'          YEAR * 365 DAYS PER YEAR
         BCTR  R15,0               R15 = YEAR - 1
         LR    R0,R15              R0  = YEAR - 1
         SRDL  R0,32               SHIFT INTO REGISTER 1
         D     R0,=F'4'            DIVIDE BY 4
         AR    R2,R1               ADD NUMBER OF LEAP YEARS
         LR    R0,R15              R0  = YEAR - 1
         SRDL  R0,32               SHIFT INTO REGISTER 1
         D     R0,=F'100'          DIVIDE BY 100
         SR    R2,R1               SUB NUMBER OF CENTURY LEAP YEARS
         LR    R0,R15              R0  = YEAR - 1
         SRDL  R0,32               SHIFT INTO REGISTER 1
         D     R0,=F'400'          DIVIDE BY 400
         AR    R2,R1               ADD NUMBER OF 4-CENTURY LEAP YEARS
         LA    R1,1(R15)           RESTORE ORIGINAL YEAR
         LR    R0,R2               PLACE RESULTS IN REGISTER
         BR    SUBRLINK            RETURN FROM SUB-ROUTINE
         TITLE 'GENERAL PURPOSE DATE MANIPULATION ROUTINE - DATA CONSTA+
               NTS, TABLES, LITERALS, ETC...'
*---------------------------------------------------------------------*
*        FUNCTION PROCESSING TABLE                                    *
*---------------------------------------------------------------------*
*                  +------------------------------- FIELD 1 FORMAT
*                  |       +----------------------- FIELD 2 FORMAT
*                  |       |       +--------------- FUNCTION ROUTINE
*                  |       |       |       +------- RETURN  FORMAT
*                  |       |       |       |        FUNCTION NAME
*                  |       |       |       |        |
FUNCTBL  DS    0F  V       V       V       V        V
         DC    AL4(INPDATE,INPNULL,FUNNULL,OUTNULL) VERIFY
         DC    AL4(INPDATE,INPNULL,FUNCNVT,OUTDATE) CONVERT
         DC    AL4(INPDATE,INPVALU,FUNINCR,OUTDATE) INCREMENT
         DC    AL4(INPDATE,INPVALU,FUNDECR,OUTDATE) DECREMENT
         DC    AL4(INPDATE,INPDATE,FUNDIFF,OUTVALU) DIFFERENCE
         DC    AL4(INPDATE,INPNULL,FUNDAYW,OUTCHAR) DAY OF WEEK
         DC    AL4(INPDATE,INPNULL,FUNDAYC,OUTVALU) DAY OF CENTURY
*---------------------------------------------------------------------*
*        DAY OF WEEK TABLE                                            *
*---------------------------------------------------------------------*
DAYTBL   DS    0F                  DAY OF WEEK TABLE
         DC    CL10'1SUNDAY   '
         DC    CL10'2MONDAY   '
         DC    CL10'3TUESDAY  '
         DC    CL10'4WEDNESDAY'
         DC    CL10'5THURSDAY '
         DC    CL10'6FRIDAY   '
         DC    CL10'7SATURDAY '
*---------------------------------------------------------------------*
*        JULIAN DAYS TABLE                                            *
*---------------------------------------------------------------------*
JULDAYS  DS    0F           NORMAL/LEAP YEAR
         DC    H'000',H'000'       JANUARY
         DC    H'031',H'031'       FEBRUARY
         DC    H'059',H'060'       MARCH
         DC    H'090',H'091'       APRIL
         DC    H'120',H'121'       MAY
         DC    H'151',H'152'       JUNE
         DC    H'181',H'182'       JULY
         DC    H'212',H'213'       AUGUST
         DC    H'243',H'244'       SEPTEMBER
         DC    H'273',H'274'       OCTOBER
         DC    H'304',H'305'       NOVEMBER
         DC    H'334',H'335'       DECEMBER
         DC    2H'32767'
JULYEAR  DC    C'365 366 '         YEAR
*---------------------------------------------------------------------*
*        GREGORIAN DAYS TABLE                                         *
*---------------------------------------------------------------------*
GREGDAYS DS    0F  NORMAL/LEAP YEAR
         DC    C'31',C'31'         JANUARY
         DC    C'28',C'29'         FEBRUARY
         DC    C'31',C'31'         MARCH
         DC    C'30',C'30'         APRIL
         DC    C'31',C'31'         MAY
         DC    C'30',C'30'         JUNE
         DC    C'31',C'31'         JULY
         DC    C'31',C'31'         AUGUST
         DC    C'30',C'30'         SEPTEMBER
         DC    C'31',C'31'         OCTOBER
         DC    C'30',C'30'         NOVEMBER
         DC    C'31',C'31'         DECEMBER
*---------------------------------------------------------------------*
*        CONSTANTS, LITERALS, ETC ...                                 *
*---------------------------------------------------------------------*
BLANKS   DS    8C' '
         LTORG
         TITLE 'GENERAL PURPOSE DATE MANIPULATION ROUTINE - INPUT PROCE+
               SSING ROUTINES'
*---------------------------------------------------------------------*
*        NULL FUNCTION PROCESSING ROUTINE                             *
*---------------------------------------------------------------------*
INPNULL  DS    0H
FUNNULL  DS    0H
OUTNULL  DS    0H
         USING INPDATE,FUNCBASE    MAKE FUNCTION ADDRESSABLE
         SR    INPREG,INPREG       CLEAR INPUT DATA
         BR    FUNCLINK            RETURN FROM ROUTINE
         DROP  FUNCBASE
         EJECT
*---------------------------------------------------------------------*
*        DATE INPUT PROCESSING ROUTINE                                *
*                                                                     *
*        INPUT:  INPREG = ADDRESS OF DATE FIELD                       *
*                                                                     *
*        OUTPUT: INPREG = BINARY DATE                                 *
*---------------------------------------------------------------------*
INPDATE  DS    0H
         USING INPDATE,FUNCBASE    MAKE FUNCTION ADDRESSABLE
         LR    R1,INPREG           GET DATE ADDRESS
         LA    R0,8                MAX DATE SIZE
INPDAT10 DS    0H
         CLI   0(R1),C' '          END OF DATE?
         BE    INPDAT20              YS-CONTINUE PROCESSING
         CLI   0(R1),C'0'          IS IT A VALID NUMBER?
         BL    RETURN04              NO-INVALID DATE DATA
         CLI   0(R1),C'9'          IS IT A VALID NUMBER?
         BH    RETURN04              NO-INVALID DATE DATA
         LA    R1,1(R1)            POINT TO NEXT BYTE
         BCT   R0,INPDAT10         ANY MORE TO CHECK?
INPDAT20 DS    0H
         SR    R1,INPREG           CALCULATE DATE SIZE
         SH    R1,=H'5'            IS IT >= 5 BYTES LONG?
         BM    RETURN08              NO-INVALID DATE FORMAT
         SLL   R1,2                MULTIPLY BY 4 FOR BRANCH TABLE
         B     *+4(R1)             BRANCH TO PROPER ROUTINE
         B     INPDAT30            YYDDD
         B     INPDAT60            MMDDYY
         B     INPDAT40            YYYYDDD
         B     INPDAT70            MMDDYYYY
INPDAT30 DS    0H
         MVC   DSAYYYY,=C'1900'    DEFAULT CENTURY
         MVC   DSAYY,0(INPREG)     BUILD YEAR
         MVC   DSADDD,2(INPREG)    BUILD DDD
         LA    INPREG,DSA$JDAT     INDICATE FORMAT IS JULIAN
         B     INPDAT50            GO TO JULIAN COMMON ROUTINE
INPDAT40 DS    0H
         MVC   DSAYYYY,0(INPREG)   BUILD YEAR
         MVC   DSADDD,4(INPREG)    BUILD DDD
         LA    INPREG,DSA$JDAT+DSA$YEXT  INDICATE FORMAT IS JUL EXT
         B     INPDAT50            GO TO JULIAN COMMON ROUTINE
INPDAT50 DS    0H
         CLC   DSADDD,=C'000'      IS DAYS VALID?
         BNH   RETURN04              NO-INDICATE INVALID DATE DATA
         PACK  DSADWORD,DSAYYYY    GET YEAR
         CVB   R1,DSADWORD         MAKE YEAR BINARY FOR LATER USE
         BAL   SUBRLINK,CKLEAP     CHECK FOR LEAP LEAR
         LA    R15,JULYEAR(R15)    POINT TO DAYS IN YEAR
         CLC   DSADDD,0(R15)       IS DAYS VALID?
         BH    RETURN04              NO-INDICATE INVALID DATE DATA
         BAL   SUBRLINK,RELDAYS    CALCULATE DAYS IN PREVIOUS YEARS
         PACK  DSADWORD,DSADDD     GET DAYS IN THIS YEAR
         CVB   R1,DSADWORD         MAKE IT BINARY
         AR    R0,R1               ADD DAYS TO PREVIOUS YEAR DAYS
         SRDL  R0,24               SHIFT INTO R1
         LR    R0,INPREG           GET DATE TYPE
         SRDL  R0,8                SHIFT ALL OF IT INTO R1
         LR    INPREG,R1           PLACE DATA INTO INPUT REGISTER
         BR    FUNCLINK            RETURN FROM INPUT ROUTINE
INPDAT60 DS    0H
         MVC   DSAYYYY,=C'1900'    USE DEFAULT YEAR
         MVC   DSAYY,4(INPREG)     BUILD YEAR
         MVC   DSAMM,0(INPREG)     BUILD MONTH
         MVC   DSADD,2(INPREG)     BUILD DAY
         LA    INPREG,DSA$GDAT     INDICATE GREGORIAN DATE
         B     INPDAT80            GO TO COMMON GREGORIAN DATE
INPDAT70 DS    0H
         MVC   DSAYYYY,4(INPREG)   BUILD YEAR
         MVC   DSAMM,0(INPREG)     BUILD MONTH
         MVC   DSADD,2(INPREG)     BUILD DAY
         LA    INPREG,DSA$GDAT+DSA$YEXT  INDICATE GREG EXT DATE
         B     INPDAT80            GO TO COMMON GREGORIAN DATE
INPDAT80 DS    0H
         CLC   DSAMM,=C'01'        IS MONTH VALID?
         BL    RETURN04              NO-INVALID DATE DATA
         CLC   DSAMM,=C'12'        IS MONTH VALID?
         BH    RETURN04              NO-INVALID DATE DATA
         CLC   DSADD,=C'01'        IS DAY VALID?
         BL    RETURN04              NO-INVALID DATE DATA
         PACK  DSADWORD,DSAYYYY    GET YEAR
         CVB   R1,DSADWORD         MAKE YEAR BINARY
         BAL   SUBRLINK,CKLEAP     CHECK FOR LEAP YEAR
         SRL   R15,1               DIVIDE BY 2
         LR    R5,R15              SAVE RESULTS
         PACK  DSADWORD,DSAMM      GET MONTH
         CVB   R14,DSADWORD        MAKE MONTH BINARY
         BCTR  R14,0               MAKE BINARY MONTH RELATIVE TO ZERO
         SLL   R14,2               MULTIPLY BY 4
         AR    R5,R14              ADD TO LEAP YEAR FLAG
         LA    R15,GREGDAYS(R5)    POINT TO DAYS IN MONTH
         CLC   DSADD,0(R15)        IS DAYS VALID?
         BH    RETURN04              NO-INVALID DATE DATA
         BAL   SUBRLINK,RELDAYS    CALCULATE DAYS IN PREVIOUS YEARS
         AH    R0,JULDAYS(R5)      ADD PREVIOUS MONTHS DAYS
         PACK  DSADWORD,DSADD      GET DAY OF MONTH
         CVB   R1,DSADWORD         MAKE DAY BINARY
         AR    R0,R1               ADD TO PREVIOUS DAYS
         SRDL  R0,24               SHIFT INTO R1
         LR    R0,INPREG           GET FIELD TYPE
         SRDL  R0,8                SHIFT INTO R1
         LR    INPREG,R1           PLACE DATA INTO INPUT REGISTER
         BR    FUNCLINK            RETURN FROM INPUT ROUTINE
         LTORG
         DROP  FUNCBASE
         EJECT
*---------------------------------------------------------------------*
*        VALUE INPUT PROCESSING ROUTINE                               *
*---------------------------------------------------------------------*
INPVALU  DS    0H
         USING INPVALU,FUNCBASE    MAKE FUNCTION ADDRESSABLE
         LA    R1,8                MAXIMUM DATA LENGTH
         LR    R2,INPREG           INPUT DATA REGISTER
INPVAL00 DS    0H
         CLI   0(R2),C' '          END OF DATA?
         BE    INPVAL10              YS-GO CONVERT TO BINARY
         CLI   0(R2),C'0'          IS IT NUMERIC?
         BL    RETURN08              NO-INVALID PARM SPECIFICATION
         CLI   0(R2),C'9'
         BH    RETURN08
         LA    R2,1(R2)            POINT TO NEXT DATA BYTE
         BCT   R1,INPVAL00         GO CHECK IT OUT
INPVAL10 DS    0H
         SR    R2,INPREG           CALCULATE DATA LENGTH
         BNP   RETURN08              INVALID PARM SPECIFICATION
         BCTR  R2,R0               REDUCE DATA LENGTH FOR PACK
         EX    R2,INPVPACK         PACK NUMERIC DATA FOR CVB
         CVB   R1,DSADWORD         CONVERT TO BINARY
         SLDL  R0,4                SHIFT FOR DATA TYPE
         LA    R0,DSA$VALU         INDICATE FIELD IS A VALUE
         SRDL  R0,4                SHIFT BACK
         LR    INPREG,R1           SET FIELD DATA
         BR    FUNCLINK            RETURN FROM INPUT ROUTINE
INPVPACK PACK  DSADWORD,0(0,INPREG)  PACK INPUT DATA FOR CVB
         DROP  FUNCBASE
         TITLE 'GENERAL PURPOSE DATE MANIPULATION ROUTINE - FUNCTION PR+
               OCESSING ROUTINES'
*---------------------------------------------------------------------*
*        CONVERT FUNCTION PROCESSING ROUTINE                          *
*---------------------------------------------------------------------*
FUNCNVT  DS    0H
         USING FUNCNVT,FUNCBASE    MAKE FUNCTION ADDRESSABLE
         MVC   DSA$RET,DSA$FLD1    GET INPUT DATE FORMAT
         XI    DSA$RET,DSA$JDAT+DSA$GDAT SWAP RETURN DATE FORMAT
         MVC   DSA@RET,DSA@FLD1    SET RETURN DATE
         BR    FUNCLINK            RETURN FROM FUNCTION
         DROP  FUNCBASE
         EJECT
*---------------------------------------------------------------------*
*        INCREMENT FUNCTION PROCESSING ROUTINE                        *
*---------------------------------------------------------------------*
FUNINCR  DS    0H
         USING FUNINCR,FUNCBASE    MAKE FUNCTION ADDRESSABLE
         SR    R1,R1               CLEAR WORK REGISTER
         SR    R2,R2               CLEAR WORK REGISTER
         ICM   R1,7,DSA@FLD1       GET DATE
         ICM   R2,7,DSA@FLD2       GET INCREMENT VALUE
         AR    R1,R2               INCREMENT DATE
         STCM  R1,7,DSA@RET        SAVE NEW DATE
         BR    FUNCLINK            RETURN FROM FUNCTION
         DROP  FUNCBASE
         EJECT
*---------------------------------------------------------------------*
*        DECREMENT FUNCTION PROCESSING ROUTINE                        *
*---------------------------------------------------------------------*
FUNDECR  DS    0H
         USING FUNDECR,FUNCBASE    MAKE FUNCTION ADDRESSABLE
         SR    R1,R1               CLEAR WORK REGISTER
         SR    R2,R2               CLEAR WORK REGISTER
         ICM   R1,7,DSA@FLD1       GET DATE
         ICM   R2,7,DSA@FLD2       GET DECREMENT VALUE
         SR    R1,R2               DECREMENT DATE
         BNP   RETURN04            DECREMENT TO LARGE FOR US TO HANDLE
         STCM  R1,7,DSA@RET        SAVE NEW DATE
         BR    FUNCLINK            RETURN FROM FUNCTION
         DROP  FUNCBASE
         EJECT
*---------------------------------------------------------------------*
*        DIFFERENCE FUNCTION PROCESSING ROUTINE                       *
*---------------------------------------------------------------------*
FUNDIFF  DS    0H
         USING FUNDIFF,FUNCBASE    MAKE FUNCTION ADDRESSABLE
         SR    R1,R1               CLEAR WORK REGISTER
         SR    R2,R2               CLEAR WORK REGISTER
         ICM   R1,7,DSA@FLD1       GET DATE 1
         ICM   R2,7,DSA@FLD2       GET DATE 2
         CR    R1,R2               IS FIRST SMALLER THAN SECOND?
         BNL   FUNDIF10              NO-CONTINUE
         LR    R0,R1                 YS-SAVE FIRST
         LR    R1,R2                    MAKE SECOND FIRST
         LR    R2,R0                    MAKE FIRST SECOND
FUNDIF10 DS    0H
         SR    R1,R2               CALCULATE DIFFERENCE
         STCM  R1,7,DSA@RET        SAVE DIFFERENCE
         BR    FUNCLINK            RETURN FROM FUNCTION
         DROP  FUNCBASE
         EJECT
*---------------------------------------------------------------------*
*        DAY OF WEEK FUNCTION PROCESSING ROUTINE                      *
*---------------------------------------------------------------------*
FUNDAYW  DS    0H
         USING FUNDAYW,FUNCBASE    MAKE FUNCTION ADDRESSABLE
         SR    R0,R0               CLEAR WORK REGISTER
         ICM   R0,7,DSA@FLD1       GET DATE
         BCTR  R0,0                ADJUST FOR RELATIVE DAY
         SRDL  R0,32               SHIFT FOR DIVIDE
         D     R0,=F'7'            GET RELATIVE DAY OF WEEK (0-6)
         LR    R1,R0               PLACE REMAINDER IN R1
         MH    R1,=H'10'           MULTIPLY BY 10
         LA    R1,DAYTBL(R1)       POINT TO DAY OF WEEK
         MVC   DSA@RETC,0(R1)
         MVI   DSA$RET,DSA$CHAR    INDICATE FIELD IS CHARACTER
         BR    FUNCLINK            RETURN FROM FUNCTION
         LTORG
         DROP  FUNCBASE
         EJECT
*---------------------------------------------------------------------*
*        DAY OF CENTURY FUNCTION PROCESSING ROUTINE                   *
*---------------------------------------------------------------------*
FUNDAYC  DS    0H
         USING FUNDAYC,FUNCBASE    MAKE FUNCTION ADDRESSABLE
         MVI   DSA$RET,DSA$VALU    INDICATE FIELD IS A VALUE
         MVC   DSA@RET,DSA@FLD1    COPY BINARY DATE
         BR    FUNCLINK            RETURN FROM FUNCTION
         DROP  FUNCBASE
         TITLE 'GENERAL PURPOSE DATE MANIPULATION ROUTINE - OUTPUT PROC+
               ESSING ROUTINES'
*---------------------------------------------------------------------*
*        CHARACTER OUTPUT PROCESSING ROUTINE                          *
*---------------------------------------------------------------------*
OUTCHAR  DS    0H
         USING OUTCHAR,FUNCBASE    MAKE FUNCTION ADDRESSABLE
         MVC   PARMRETC,DSA@RETC   BUILD RETURN DATA
         BR    FUNCLINK            RETURN FROM FUNCTION
         DROP  FUNCBASE
         EJECT
*---------------------------------------------------------------------*
*        DATE OUTPUT PROCESSING ROUTINE                               *
*---------------------------------------------------------------------*
OUTDATE  DS    0H
         USING OUTDATE,FUNCBASE    MAKE FUNCTION ADDRESSABLE
         SR    R5,R5               CLEAR WORK REGISTER
         ICM   R5,7,DSA@RET        GET BINARY DATE
         LA    R1,1(R5)            ...
         M     R0,=F'400'          : MAKE BEST GUESS AT YEAR
         D     R0,=F'146097'       ...
OUTDAT10 DS    0H
         BAL   SUBRLINK,RELDAYS    GET DAYS PRIOR TO DSAYYYY
         SR    R5,R0               GET REMAINING DAYS IN THIS YEAR
         BP    OUTDAT20            GOT THE RIGHT YEAR
         AR    R5,R0               RESET BINARY DATE
         BCT   R1,OUTDAT10         GO TRY PREVIOUS YEAR
OUTDAT20 DS    0H
         CVD   R1,DSADWORD         CONVERT YEAR TO DECIMAL
         OI    DSADWORD+7,X'0F'    MAK UNSIGNED
         UNPK  DSAYYYY,DSADWORD    CONVERT YEAR TO DISPLAY FORMAT
         CVD   R5,DSADWORD         CONVERT DAYS TO DECIMAL
         OI    DSADWORD+7,X'0F'    MAKE UNSIGNED
         UNPK  DSADDD,DSADWORD     CONVERT DAYS TO DISPLAY FORMAT
         BAL   SUBRLINK,CKLEAP     CHECK FOR LEAP YEAR
         SRL   R15,1               DIVIDE RETURN BY 2
         LA    R2,JULDAYS-4(R15)   POINT TO JULIAN DAYS TABLE
         SR    R1,R1               CLEAR MONTH VALUE
OUTDAT30 DS    0H
         LA    R1,1(R1)            BUMP MONTH
         LA    R2,4(R2)            POINT TO JUL DAYS FOR MONTH
         CH    R5,4(R2)            PAST THIS MONTH?
         BH    OUTDAT30              YS-GO CHECK NEXT MONTH
         SH    R5,0(R2)            REMAINING DAYS TO DAY OF MONTH
         CVD   R1,DSADWORD         CONVERT MONTH TO DECIMAL
         OI    DSADWORD+7,X'0F'    MAKE UNSIGNED
         UNPK  DSAMM,DSADWORD      CONVERT MONTH TO DISPLAY FORMAT
         CVD   R5,DSADWORD         CONVERT DAY TO DECIMAL
         OI    DSADWORD+7,X'0F'    MAKE UNSIGNED
         UNPK  DSADD,DSADWORD      CONVERT DAY TO DISPLAY FORMAT
         TM    DSA$RET,DSA$GDAT    IS DATE TO BE GREGORIAN?
         BO    OUTDAT40              YS-GO TO GREGORIAN OUTPUT
         MVC   PARMRET(4),DSAYYYY  BUILD EXTENDED YEAR
         MVC   PARMRET+4(3),DSADDD BUILD JULIAN DAY
         TM    DSA$RET,DSA$YEXT    EXTENDED YEAR?
         BOR   FUNCLINK              YS-RETURN FROM ROUTINE
         MVC   PARMRET(5),PARMRET+2  NO-BUILD NEW DATE
         MVC   PARMRET+5(3),BLANKS      CLEAR REST OF FIELD
         BR    FUNCLINK                 RETURN FROM ROUTINE
OUTDAT40 DS    0H
         MVC   PARMRET(2),DSAMM    BUILD MONTH
         MVC   PARMRET+2(2),DSADD  BUILD DAY
         MVC   PARMRET+4(4),DSAYYYY    BUILD EXTENDED YEAR
         TM    DSA$RET,DSA$YEXT    EXTENDED YEAR?
         BOR   FUNCLINK              YS-RETURN FROM ROUTINE
         MVC   PARMRET+4(2),DSAYY    NO-BUILD NEW DATE
         MVC   PARMRET+6(2),BLANKS      CLEAR REST OF FIELD
         BR    FUNCLINK            RETURN FROM FUNCTION
         LTORG
         DROP  FUNCBASE
         EJECT
*---------------------------------------------------------------------*
*        VALUE OUTPUT PROCESSING ROUTINE                              *
*---------------------------------------------------------------------*
OUTVALU  DS    0H
         USING OUTVALU,FUNCBASE    MAKE FUNCTION ADDRESSABLE
         SR    R1,R1               CLEAR WORK REGISTER
         ICM   R1,7,DSA@RET        GET VALUE
         CVD   R1,DSADWORD         CONVERT TO PACKED DECIMAL
         OI    DSADWORD+7,X'0F'    CHANGH SIGN TO POSITIVE
         UNPK  PARMRET,DSADWORD    CONVERT TO ZONE NUMERIC
         BR    FUNCLINK            RETURN FROM FUNCTION
         DROP  FUNCBASE
         END
