         TITLE 'CLEANACL -- REMOVE NONEXISTENT USERS AND GROUPS FROM AC*
               CESS LISTS'
*---------------------------------------------------------------------*
*                                                                     *
*   CLEANACL'S PURPOSE IS TO CHECK EVERY ACCESS LIST IN THE RACF      *
*   DATASET TO SEE WHETHER EACH ONE CONTAINS ANY NON-EXISTENT         *
*   USERIDS OR GROUPNAMES.                                            *
*                                                                     *
*     **********************************************************      *
*     ***                                                    ***      *
*     ***                   W A R N I N G :                  ***      *
*     ***                                                    ***      *
*     *** +++ THIS PROGRAM IS INTENDED TO RUN AUTHORIZED +++ ***      *
*     *** +++   I SUGGEST THAT YOU REVIEW IT CAREFULLY   +++ ***      *
*     *** +++    BEFORE INSTALLING IT ON YOUR SYSTEM.    +++ ***      *
*     ***                                                    ***      *
*     **********************************************************      *
*                                                                     *
*   THIS PROGRAM HAS BEEN TESTED ON RACF RELEASES 1.4 THRU 1.7.       *
*                                                                     *
*   OPERATION:  (DETAILS DOCUMENTED IN INDIVIDUAL SECTIONS)           *
*                                                                     *
*     (1) READ ALL USER AND GROUP PROFILES, CREATING A SEQUENTIAL     *
*         TABLE ON PAGE-ALIGNED 4K PAGES. AN 'INDEX' OF PAGE-ADDRESS  *
*         AND HI-ENTRY-ON-PAGE IS ALSO CREATED. BASED ON PARMS,       *
*         A COPY OF THIS TABLE IS SAVED OR RELOADED FROM A DASD       *
*         DATASET FOR RESTART PURPOSES: THIS IS DESCRIBED LATER.      *
*                                                                     *
*     (2) READ ACCESS LISTS FOR ALL 'DATASET' PROFILES. FOR EACH      *
*         ACL ENTRY THAT DOES NOT EXIST AS A USER OR GROUP, GET       *
*         RID OF IT. SELECTION INCLUDES MULTI-VOLUME DATASETS,        *
*         MODEL PROFILES AND NON-VSAM AND VSAM DATASET PROFILES.      *
*         (NOTE THAT CLEANACL CAN ALSO RUN IN 'READ-ONLY' MODE.)      *
*                                                                     *
*     (3) FOR EACH CLASS DEFINED IN THE CDT, READ ACCESS LISTS        *
*         FOR ALL PROFILES IN THAT CLASS. FOR EACH ACL ENTRY THAT     *
*         DOES NOT EXIST AS A USER OR GROUP, GET RID OF IT.           *
*                                                                     *
*     (4) SPIT OUT SOME NUMBERS ABOUT WHAT WE DID AND QUIT.           *
*                                                                     *
*   ATTRIBUTES: REENTRANT, REUSABLE, REFRESHABLE, APF-AUTHORIZED,     *
*               RESIDENT IN PRIVATE AREA.                             *
*                                                                     *
*   MACRO LIBRARIES: SYS1.MACLIB (QSAM AND RACF MANAGER MACROS)       *
*                                                                     *
*   NOTE: RACF 1.4 MACROS USED HERE WERE NOT DISTRIBUTED IN A         *
*         CUSTOMER LIBRARY ORIGINALLY. IF YOU HAVE APPLIED PTF        *
*         UZ54891, THESE MACROS SHOULD NOW LIVE IN AMACLIB.           *
*         THIS PROBLEM SHOULD NOT OCCUR WITH RACF 1.5 AND UP.         *
*                                                 (CONTINUED)         *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*  SITE DEPENDENCIES: NONE (I THINK)                                  *
*                                                                     *
*  DDNAMES USED: SYSPRINT (LRECL=132,RECFM=FB) FOR MESSAGES           *
*                CHECKPT (LRECL=4096,RECFM=FB) FOR CHECKPOINTING      *
*                     THE USER/GROUP TABLE (OPTIONAL)                 *
*                SYSUDUMP - NEVER A BAD IDEA.                         *
*                COMMANDS - FILE OF 'PERMIT DELETE' COMMANDS          *
*                OWNERS   - FILE OF 'ALTDSD/RALTER OWNER(?????)' CMDS *
*                EXCLUDES - OPTIONAL LIST OF USERS AND GROUPS THAT    *
*                     CLEANACL WILL PRETEND DON'T EXIST               *
*                                                                     *
*  EXTERNAL REFERENCES: NONE                                          *
*                                                                     *
*  SUGGESTED ASSEMBLER AND LINKEDIT OPTIONS:                          *
*     ASSEMBLER - PARM='RENT'                                         *
*     LINK EDIT - PARM='RENT,REUS,REFR,AC=1'                          *
*  ASSEMBLY OF CLEANACL REQUIRES SYS1.AMODGEN.                        *
*                                                                     *
*  CLEANACL MUST BE LINKED INTO AN AUTHORIZED LIBRARY WITH AC=1       *
*  OR IT -> WILL NOT WORK <-. SINCE IT CONTAINS NO ACCESS CHECKING    *
*  AS DISTRIBUTED (E.G. ANYONE CAN RUN IT), I SUGGEST THAT YOU:       *
*                                                                     *
*    A) PUT IT IN A PROTECTED NON-LINKLIST APF LIBRARY, OR            *
*    B) DROP IN A RACHECK FOR AN APPL PROFILE OR SOMETHING.           *
*                                                                     *
*  HINTS FOR USAGE: AS THIS THING DEPENDS ON THE SIZE, CONTENTS       *
*    AND STRUCTURE OF YOUR RACF DATASET, THE TIME IT TAKES TO RUN     *
*    VARIES GREATLY, BUT WILL BE SIGNIFICANT. FOR EXAMPLE, CLEMSON    *
*    CAN RUN THIS ON ITS 3081K, WITH PRIMARY AND BACKUP RACF DATA-    *
*    SETS ON 3380'S ON AVERAGE-USE STRINGS, IN ABOUT 1 HOUR REAL      *
*    TIME DURING PRIME SHIFT, PROCESSING 4000+ USERS, 400+ GROUPS,    *
*    4000+ DATASETS AND 2600+ GENERAL-CLASS PROFILES. A RECENTLY-     *
*    REORG'D RACF DATABASE SPEEDS THINGS UP: SO DOES CACHED DASD.     *
*    IF RACF IS ON SHARED DASD, NOTE THIS THING READS NEARLY EVERY    *
*    PROFILE IN RACF AND MAY CAUSE DEVICE RESERVE PROBLEMS DURING     *
*    PRIME SHIFTS. I SUGGEST THAT ONCE YOU'RE SATISFIED WITH          *
*    THE OPERATION OF THE PROGRAM, YOU SET IT UP TO RUN REGULARLY     *
*    ONCE A WEEK OR SO. IT SHOULD DEFINITELY BE RUN AFTER MASSIVE     *
*    DELETIONS OF USERS AND/OR GROUPS FROM RACF, AS IT'S EASIER THAN  *
*    CLEANING UP ACCESS LISTS USING ICHUT100.                         *
*                                                                     *
*                                                       (CONTINUED)   *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*    SAMPLE JCL TO RUN THE PROGRAM:                                   *
*                                                                     *
*    (1) STRAIGHTFORWARD EXECUTION.                                   *
*                                                                     *
*    //CLEANJOB  JOB ...ACCOUNTING..., ETC.                           *
*    //CLEANUP  EXEC PGM=CLEANACL            <-- NOTE: NO PARM IS OK. *
*    //STEPLIB   DD  DSN=<AUTHORIZED LIBRARY>,DISP=SHR                *
*    //SYSPRINT  DD  SYSOUT=A                                         *
*    //COMMANDS  DD  DSN=<COMMAND FILE>,DISP=(NEW,CATLG),UNIT=<DISK>, *
*    //  DCB=(LRECL=80,BLKSIZE=6160,RECFM=FB),SPACE=(TRK,(5,5),RLSE)  *
*    //OWNERS    DD  DSN=<OWNERS FILE>,DISP=(NEW,CATLG),UNIT=<DISK>,  *
*    //  DCB=(LRECL=80,BLKSIZE=6160,RECFM=FB),SPACE=(TRK,(5,5),RLSE)  *
*    //SYSUDUMP  DD  SYSOUT=A                                         *
*                                                                     *
*    (2) CREATE A CHECKPOINT COPY OF THE USER/GROUP TABLE.            *
*                                                                     *
*    //CLEANJOB  JOB ...ACCOUNTING..., ETC.                           *
*    //CLEANUP  EXEC PGM=CLEANACL,PARM=C                              *
*    //STEPLIB   DD  DSN=<AUTHORIZED LIBRARY>,DISP=SHR                *
*    //SYSPRINT  DD  SYSOUT=A                                         *
*    //CHECKPT   DD  DSN=<ARBITRARY DSNAME>,DISP=(NEW,CATLG,CATLG),   *
*    //  UNIT=<DASD>,SPACE=(CYL,(1,1))                                *
*    //COMMANDS  DD  DSN=<COMMAND FILE>,DISP=(NEW,CATLG),UNIT=<DISK>, *
*    //  DCB=(LRECL=80,BLKSIZE=6160,RECFM=FB),SPACE=(TRK,(5,5),RLSE)  *
*    //OWNERS    DD  DSN=<OWNERS FILE>,DISP=(NEW,CATLG),UNIT=<DISK>,  *
*    //  DCB=(LRECL=80,BLKSIZE=6160,RECFM=FB),SPACE=(TRK,(5,5),RLSE)  *
*    //SYSUDUMP  DD  SYSOUT=A                                         *
*                                                                     *
*    (3) RESTART THE USER/GROUP TABLE FROM THE CHECKPOINT FILE,       *
*        BUT DO NOT CHANGE ANY ACCESS LISTS.                          *
*                                                                     *
*    //CLEANJOB  JOB ...ACCOUNTING..., ETC.                           *
*    //CLEANUP  EXEC PGM=CLEANACL,PARM='R'                            *
*    //STEPLIB   DD  DSN=<AUTHORIZED LIBRARY>,DISP=SHR                *
*    //SYSPRINT  DD  SYSOUT=A                                         *
*    //CHECKPT   DD  DSN=<CHECKPOINT FILE>,DISP=OLD                   *
*    //COMMANDS  DD  DSN=<COMMAND FILE>,DISP=(NEW,CATLG),UNIT=<DISK>, *
*    //  DCB=(LRECL=80,BLKSIZE=6160,RECFM=FB),SPACE=(TRK,(5,5),RLSE)  *
*    //OWNERS    DD  DSN=<OWNERS FILE>,DISP=(NEW,CATLG),UNIT=<DISK>,  *
*    //  DCB=(LRECL=80,BLKSIZE=6160,RECFM=FB),SPACE=(TRK,(5,5),RLSE)  *
*    //SYSUDUMP  DD  SYSOUT=A                                         *
*                                                                     *
*  RESTRICTIONS: CLEANACL DOES NOT KNOW HOW TO HANDLE MULTI-VOLUME    *
*                  DISK DATASETS, AS WE HAVE NONE OF THESE AT C.U.    *
*                CLEANACL DOES NOT KNOW HOW TO HANDLE MEMBERS OF      *
*                  RESOURCE GROUPS (EX: THE IMS AND CICS CLASSES).    *
*                  IF IT FINDS SUCH A CLASS IN THE CDT, IT WILL       *
*                  IGNORE IT BUT WILL ISSUE A MESSAGE.                *
*                                                                     *
*  COMMENTS (GOOD, BAD OR INDIFFERENT) ARE WELCOME.                   *
*                                                                     *
*                                     JIM BLALOCK                     *
*                                     COMPUTER CENTER                 *
*                                     50 NEW CHERRY ST.               *
*                                     CLEMSON UNIVERSITY              *
*                                     CLEMSON, SC  29634-2803         *
*                                     (803) 656-3466                  *
*                                                                     *
*  COPYRIGHT @ 1983  --  CLEMSON UNIVERSITY                           *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*  CHANGE HISTORY: 2/86 JCB ADDED OPTIONAL SYSIN FILE THAT ALLOWS   @C1
*                           YOU TO 'PRETEND' A USER IS DELETED.     @C1
*                           REFER TO SEGMENT 'EXCLUDE' FOR DETAILS. @C1
*                  7/86 JCB CHANGED 'LOAD' FOR ICHERCDE TO USE THE  @C2
*                           CDT POINTER IN THE RCVT, SINCE RACF 1.7 @C2
*                           CONSTRUCTS THE WORKING CDT FROM THE IBM @C2
*                           AND USER CLASS DESCRIPTOR TABLES.       @C2
*                  8/86 JCB VARIOUS FIXES FOR MIXED DISCRETE AND    @C3
*                           GENERIC PROFILES: ADDED 'GENERIC=YES'   @C3
*                           TO DATASET-NEXTC ICHEINTY, AND ADDED    @C3
*                           LOGIC TO MAKE A 'GENERIC=NO' AND A      @C3
*                           'GENERIC=UNCOND' PASS THROUGH EACH      @C3
*                           CLASS THAT HAS GENERIC CHECKING ON.     @C3
*                           ADD'L PROBLEMS IN THIS AREA FIXED 12/88.@C3
*                                                                     *
*  DEBUGGING NOTE: IF YOU FEEL LIKE CLEANACL ISN'T PICKING UP ALL THE *
*                  PROFILES OR ACL ENTRIES IT'S SUPPOSED TO, YOU CAN  *
*                  UNCOMMENT THE LINES THAT CONTAIN '** TRACEMSG **'. *
*                  THIS WILL CAUSE CLEANACL TO WRITE TO SYSPRINT EACH *
*                  PROFILE NAME AND ACL ENTRY THAT IT LOOKS AT.       *
*                                                                     *
*---------------------------------------------------------------------*
         PRINT OFF                  DON'T PRINT MACRO DEFINITIONS
         MACRO
&TAG     WIPE  &PLACE,&CHAR=' ',&LEN=
.*
.*   WIPE MACRO FILLS A CHARACTER STRING WITH A CHARACTER.
.*   EXAMPLES:
.*      WIPE XX                  SETS 'XX' TO SPACES FOR L'XX.
.*      WIPE XX,CHAR='*'         SETS 'XX' TO '*' FOR L'XX.
.*      WIPE XX,CHAR='-',LEN=32  SETS 'XX' TO '-' FOR 32 BYTES.
.*
         LCLA  &L
         AIF   ('&TAG' EQ '').NOTAG
&TAG     DS    0H
.NOTAG   ANOP
         AIF   ('&LEN' EQ '').DFLTLEN
&L       SETA  &LEN
         AGO   .DOIT
.DFLTLEN ANOP
&L       SETA  L'&PLACE
.DOIT    ANOP
         MVI   &PLACE.,C&CHAR.
         MVC   &PLACE.+1(&L.-1),&PLACE
         MEND
         MACRO
&TAG     COUNT &CTR
.*
.*   THE COUNT MACRO ADDS A 1 TO A FULLWORD.
.*
         AIF   ('&TAG' EQ '').NOTAG
&TAG     DS    0H
.NOTAG   ANOP
         L     R1,&CTR
         LA    R1,1(R1)
         ST    R1,&CTR
         MEND
         MACRO
&TAG     COPYFLD &TO,&FROM
.*
.*  THE COPYFLD MACRO COPIES A FIELD WITH LENGTH BYTE TO A FIELD
.*    WITHOUT THE LENGTH BYTE, FOR A LENGTH OF (LENGTH BYTE - 1).
.*
         AIF   ('&TAG' EQ '').NOTAG
&TAG     DS    0H
.NOTAG   ANOP
         WIPE  &TO                  CLEAR FIELD FIRST
         XR    R1,R1                CLEAR COUNTER
         IC    R1,&FROM.            GET LENGTH BYTE
         IC    R0,&FROM.(R1)        GET A BYTE
         STC   R0,&TO.-1(R1)         AND MOVE IT
         BCT   R1,*-8                 TIL THEY'RE ALL GONE.
         MEND
         MACRO
&TAG     MVCENTRY &TYPE='USR'
.*
.*  THE MVCENTRY MACRO ADDS AN ENTRY TO THE USER/GROUP TABLE,
.*    INCREASES ITS POINTERS ACCORDINGLY, AND GETS ANOTHER
.*    4K PAGE IF IT HAS TO. VALID TYPES ARE 'USR' AND 'GRP':
.*    ANY OTHER TYPE WILL CAUSE AN ASSEMBLY ERROR.
.*
         AIF   ('&TAG' EQ '').NOTAG
&TAG     DS    0H
.NOTAG   ANOP
         LA    R1,0(R9,R10)         GET WHERE THIS ONE GOES
         MVC   0(8,R1),PWAJ&TYPE    MOVE ENTRYNAME INTO TABLE
         LA    R9,8(R9)             POINT TO NEXT ENTRY
         LR    R0,R9                SEE IF WE RAN OUT OF A BLOCK:
         SRL   R0,12                  SHIFT ALL BUT 4K BIT
         LTR   R0,R0                  IF IT'S ZERO,
         BZ    *+18                     WE'RE OK, ELSE
         MVC   4(8,R8),PWAJ&TYPE      SET HI KEY FOR PAGE
         BAL   R6,GET4K                AND GET ANOTHER ONE
         LA    R8,12(R8)               AND POINT TO NEXT INDEX ENTRY.
         SPACE
         MEND
         MACRO
&TAG     FINDACL &TYPE='D'
.*
.*  THE FINDACL MACRO LOCATES THE FIRST ACCESS LIST ENTRY IN EITHER
.*  THE DATASET OR GENERAL-CLASS RACF MANAGER WORKAREA. ANY 'TYPE'
.*  OTHER THAN 'D' OR 'C' WILL CAUSE ASSEMBLY ERRORS.
.*
         AIF   ('&TAG' EQ '').NOTAG
&TAG     DS    0H
.NOTAG   ANOP
         AIF   ('&TYPE' NE 'D').NONDS
.SETD    ANOP
         LA    R1,RWDVOL+2
         AH    R1,RWDVOLL
         MEXIT
         AGO   .QUIT
.NONDS   ANOP
         LA    R1,RWCACL
.QUIT    ANOP
         MEND
         MACRO
&TAG     CONVERT &TO=,&FROM=,&LENGTH=5
.*
.*  THE CONVERT MACRO CONVERTS A FULLWORD TO A PRINTABLE VALUE.
.*  'TO' POINTS TO WHERE THE VALUE SHOULD GO, WHILE 'FROM' POINTS
.*  TO THE FULLWORD TO CONVERT. 'LENGTH' IS USED AS THE LENGTH
.*  TO UNPACK FOR.
.*
         LCLA  &L
&L       SETA  &LENGTH
         AIF   ('&TAG' EQ '').NOTAG
&TAG     DS    0H
.NOTAG   ANOP
         L     R1,&FROM                 GET VALUE TO CONVERT
         CVD   R1,PWACVD                CONVERT TO PACKED
         UNPK  &TO.(&L.),PWACVD         UNPACK IT TO DESTINATION
         OI    &TO+&L.-1,X'F0'          MAKE LAST DIGIT READABLE
         MEND
*        REGEQU
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         PRINT ON
         TITLE 'CLEANACL -- INITIALIZATION'
CLEANACL CSECT
         STM   R14,R12,12(R13)      SAVE REGS
         LR    R12,R15              SET CODE BASE
         USING CLEANACL,R12
         LA    R11,STC              SET BASE FOR CONSTANTS
         USING STC,R11
         L     R2,0(R1)             SAVE PARM POINTER (IF ANY)
         SPACE
         GETMAIN R,LV=PWALEN,SP=0   GET SOME STORAGE
         SPACE
         ST    R13,4(R1)            SET BACK LINK
         ST    R1,8(R13)            SET FWD LINK
         LR    R13,R1               NEW SAVEAREA
         USING PWA,R13               AND PROGRAM WORKAREA.
         SPACE
PARMS    DS    0H
         CLI   2(R2),C'C'           CHECKPOINT PARM?
         BNE   PRM0010
         MVI   PWAPRM1,C'C'         SAVE FOR LATER.
         B     PRM1000
         SPACE
PRM0010  DS    0H
         CLI   2(R2),C'R'           RESTART PARM?
         BNE   PRM0020
         MVI   PWAPRM1,C'R'         SAVE FOR LATER.
         B     PRM1000
         SPACE
PRM0020  DS    0H
         CLC   0(2,R2),=H'0'        NO PARM?
         BNE   PARMERR               NO, ERROR.
         SPACE
PRM0030  DS    0H
         MVI   PWAPRM1,C' '
         SPACE
PRM1000  DS    0H
         MVI   PWAPRM2,PWAPRM2Y
         EJECT
INITALIZ DS    0H
         WIPE  PWAUSER              CLEAR OUT CHARACTER FIELDS AND
         MVI   PWAUSER,X'01'           SET LENGTHS AS NEEDED
         WIPE  PWAGROUP
         MVI   PWAGROUP,X'01'
         WIPE  PWAJUSR
         WIPE  PWAJGRP
         XC    PWADSNAM,PWADSNAM                                    @C3
         WIPE  PWAENTRY
         MVI   PWAENTRY,X'01'
         SPACE
         MVC   PWAUINTY(LENUINTY),STCUINTY   COPY ICHEINTY
         MVC   PWAGINTY(LENGINTY),STCGINTY    PARAMETER LISTS
         MVC   PWADINTY(LENDINTY),STCDINTY
         MVC   PWACINTY(LENCINTY),STCCINTY
         SPACE
         MVC   PWAACNT(LENACNT),STCACNT      COPY ICHEACTN/ICHETEST
         MVC   PWAAACL(LENAACL),STCAACL       PARAMETER LISTS
         MVC   PWAAVOL(LENAVOL),STCAVOL
         EJECT
         MVC   PWAOPEN(LENOPEN),STCOPEN      COPY MISCELLANEOUS
         MVC   PWACLOSE(LENCLOSE),STCCLOSE    PARAMETER LISTS
         MVC   PWADCB(LENDCB),STCDCB
         MVC   PWACDCB(LENCDCB),STCCDCB
         MVC   PWAC2DCB(LENC2DCB),STCC2DCB
         SPACE
         MVI   PWAFLAG,X'00'
         XC    PWAINDEX(4),PWAINDEX
         LA    R1,100               SET USER AND GROUP
         ST    R1,RWUSIZE            RACF WORKAREA SIZES
         ST    R1,RWGSIZE
         SPACE
         XC    PWACNTRS(LENCNTRS),PWACNTRS    CLEAR ALL COUNT FIELDS
         SPACE
         MVC   PWATRACE,=CL12'OPEN OUTPUT'
         OPEN  (PWADCB,OUTPUT),MF=(E,PWAOPEN)
         LTR   R15,R15
         BNZ   QUIT16
         SPACE
         MVC   PWATRACE,=CL12'OPEN COMMAND'
         OPEN  (PWACDCB,OUTPUT),MF=(E,PWAOPEN)
         LTR   R15,R15
         BNZ   QUIT16
         OPEN  (PWAC2DCB,OUTPUT),MF=(E,PWAOPEN)
         LTR   R15,R15
         BNZ   QUIT16
         EJECT
INITMORE DS    0H
*
*  INITIALIZE SOME ICHEINTY PARAMETER LISTS
*
         ICHEINTY NEXT,             THESE SET ICHEINTY THINGS THAT     *
               ENTRY=PWAUSER,         ONLY NEED TO BE DONE ONCE.       *
               WKAREA=PWARWU,                                          *
               OPTIONS=NOEXEC,      (THIS ONE READS USERIDS)           *
               MF=(E,PWAUINTY)
         SPACE
         ICHEINTY NEXT,             (THIS ONE READS GROUPS)            *
               ENTRY=PWAGROUP,                                         *
               WKAREA=PWARWG,                                          *
               OPTIONS=NOEXEC,                                         *
               MF=(E,PWAGINTY)
         TITLE 'CLEANACL -- CREATE USER/GROUP INCORE TABLE'
         CLI   PWAPRM1,PWAPRM1R     WE NEED TO RESTART INSTEAD?
         BE    RESTART
         SPACE
*---------------------------------------------------------------------*
*                                                                     *
*  INITIALIZE THE USER AND GROUP ENTRYNAMES FOR ICHEINTY NEXT, THEN   *
*  SWEEP ALL USER AND GROUP PROFILES, ADDING 8-BYTE NAMES TO OUR      *
*  INCORE TABLE IN ALPHA ORDER. IF PARM=C WAS SPECIFIED, WRITE EACH   *
*  4K PAGE TO DDNAME 'CHECKPT' WHEN IT FILLS UP OR WHEN WE'VE SWEPT   *
*  ALL USER AND GROUP PROFILES.                                       *
*                                                                     *
*  LOCAL REGISTER USAGE: R10 -> START OF A 4K PAGE TO ADD ENTRIES TO  *
*                        R9  =  DISP. INTO 4K PAGE FOR A NEW ENTRY    *
*                        R8  -> INDEX ENTRY FOR PAGE WE'RE DOING      *
*                        R6  =  LOCAL SUBROUTINE LINKAGE              *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
         LA    R8,PWAINDEX+4        POINT TO FIRST INDEX ENTRY
         SPACE
         BAL   R6,GET4K             GET FIRST 4K PAGE
         SPACE
         BAL   R6,GETUSER           READ FIRST USERID
         SPACE
         BAL   R6,GETGROUP          READ FIRST GROUP
         TITLE 'CLEANACL -- CREATE USER/GROUP INCORE TABLE'
MAKETAB  DS    0H
*---------------------------------------------------------------------*
*                                                                     *
*  MAKETAB CREATES THE USER/GROUP TABLE FROM RACF PROFILES. IF ONE    *
*  RUNS OUT BEFORE THE OTHER, A FLAG IS SET AND THE CURRENT ENTRY     *
*  FOR THAT TYPE IS SET TO X'FFFF' SO COMPARES STILL WORK. BY THE     *
*  RACF RULES, AN IDENTICAL USERID AND GROUPNAME CANNOT EXIST.        *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
         TM    PWAFLAG,PWAGEOF+PWAUEOF  RUN OUT OF BOTH?
         BO    MTBDONE                   GO PUT OUT SOME NUMBERS.
         SPACE
         CLC   PWAJUSR,PWAJGRP      WHICH ONE?
         BL    MTBUSER               USER LESS, NEED A USER.
         BE    MTBDONE               THEY'RE EQUAL, WE'RE THROUGH.
         SPACE
MTBGROUP DS    0H
         MVCENTRY TYPE=GRP          MOVE A GROUP
         BAL   R6,GETGROUP          GET THE NEXT GROUP
         B     MAKETAB              KEEP ON.
         SPACE
MTBUSER  DS    0H
         MVCENTRY TYPE=USR          MOVE A USERID
         BAL   R6,GETUSER           GET ANOTHER ONE
         B     MAKETAB              KEEP ON.
         EJECT
MTBDONE  DS    0H
         CLI   PWAPRM1,PWAPRM1C     WERE WE CHECKPOINTING?
         BNE   MTBSETHI              NO, SKIP SUCCESS MESSAGE
         SPACE
         PUT   PWACPDCB,(R10)            SAVE LAST PAGE AND CLOSE
         CLOSE PWACPDCB,MF=(E,PWACLOSE)    CHECKPOINT DATASET.
         WIPE  PWAREC
         MVC   PWAREC(L'STCMSGCP),STCMSGCP
         PUT   PWADCB,PWAREC        SAY WHAT WE DID
         SPACE
MTBSETHI DS    0H
         MVC   4(8,R8),=8X'FF'      SET LAST TABLE ENTRY TO X'FFFF'.
         SPACE
         MVC   PWAREC(L'STCMSGTL),STCMSGTL
         CONVERT TO=PWAREC+DSPMTLU,FROM=PWAUCNT,LENGTH=5
         SPACE
         CONVERT TO=PWAREC+DSPMTLG,FROM=PWAGCNT,LENGTH=5
         SPACE
         PUT   PWADCB,PWAREC
         WIPE  PWAREC
         PUT   PWADCB,PWAREC        BLANK LINE AS SEPARATOR.
         TITLE 'CLEANACL -- OPTIONALLY EXCLUDE USERIDS FROM SEARCH' @C1
*---------------------------------------------------------------------*
*
*  'EXCLUDE' ALLOWS THE EXCLUSION OF USERIDS FROM THE IN-CORE TABLE
*  JUST BUILT: THIS ALLOWS THE CLEANACL USER TO 'PRETEND' THAT CERTAIN
*  USERIDS ARE DELETED, WITHOUT ACTUALLY DELETING THEM. THIS MAY BE
*  USEFUL FOR FORMERLY POWERFUL USERIDS (RACF ADMINISTRATORS, SYSTEM
*  PROGAMMERS, ETC) THAT YOU MAY NOT WANT TO DELETE RIGHT AWAY.
*
*---------------------------------------------------------------------*
         SPACE 1                    |                               @C1
EXCLUDE  DS    0H                   |                               @C1
         MVC   PWASYSIN(LENSYSIN),STCSYSIN  COPY SYSIN DCB BASE     @C1
         OPEN  (PWASYSIN,(INPUT)),  | SEE IF SYSIN IS PRESENT       @C1*
               MF=(E,PWAOPEN)       |                               @C1
         LTR   R15,R15              | NO DD STATEMENT?              @C1
         BNZ   DATASETS             |  THAT'S OK, NO EXCLUDES.      @C1
EXCLOOP  DS    0H                   |                               @C1
         GET   PWASYSIN,PWAIREC     | GET A RECORD, EODAD=EXCEND    @C1
         LA    R2,PWAIREC           | POINT TO IT                   @C1
         LA    R3,PWAIREC+L'PWAIREC | POINT PAST IT                 @C1
EXCSCAN  DS    0H                   | SKIP LEADING BLANKS:          @C1
         CLI   0(R2),C' '           |  BLANK?                       @C1
         BNE   EXCFOUND             |   NO, GRAB USERID.            @C1
         LA    R2,1(R2)             |  NEXT BYTE                    @C1
         CR    R2,R3                |  RAN OFF END?                 @C1
         BL    EXCSCAN              |   NO, TRY AGAIN.              @C1
         ABEND 1                    | BLANK CARD: NOTHING FANCY.    @C1
EXCFOUND DS    0H                   | GOT A USERID/GROUP:           @C1
         MVC   PWALKARG,0(R2)       |  MOVE TO LOOKUP'S ARGUMENT    @C1
         BAL   R6,LOOKUP            |  CALL USER/GROUP LOOKUP       @C1
         LTR   R15,R15              |  FIND AN ENTRY?               @C1
         BZ    EXCZAP               |   YES, GO ZAP IT.             @C1
         WIPE  PWAREC               |  NO ENTRY: WRITE A MESSAGE.   @C1
         MVC   PWAREC(L'STCXNTRY),STCXNTRY                          @C1
         MVC   PWAREC+DSPXNENT(L'PWALKARG),PWALKARG                 @C1
         PUT   PWADCB,PWAREC        |                               @C1
         B     EXCLOOP              |                               @C1
EXCZAP   DS    0H                   | FOUND ENTRY TO EXCLUDE:       @C1
         XC    0(8,R2),0(R2)        |  SET IT TO X'00'S.            @C1
         B     EXCLOOP              |                               @C1
EXCEND   DS    0H                   | EODAD ON //EXCLUDES:          @C1
         CLOSE (PWASYSIN),          |  CLOSE FILE.                  @C1*
               MF=(E,PWACLOSE)      |                               @C1
         TITLE 'CLEANACL -- PROCESS DATASET PROFILES'
*---------------------------------------------------------------------*
*                                                                     *
*   DATASET PROCESSING SWEEPS ALL DATASET PROFILES (MODELS, TOO)      *
*   AND CHECKS EACH ACCESS LIST ENTRY (IF ANY) AGAINST THE USER-      *
*   GROUP TABLE BY CALLING 'LOOKUP'. IF A NON-EXISTENT ACL ENTRY      *
*   IS FOUND, IT IS DELETED. NOTE THAT WRITING A FILE OF 'PERMIT      *
*   DELETE' COMMANDS INSTEAD WOULD NOT WORK HERE, AS PERMIT CHECKS    *
*   TO SEE IF USERS AND GROUPS EXIST FIRST AND FAILS IF NOT.          *
*                                                                     *
*   LOCAL REGISTER USAGE: R10 -> RACF MANAGER WORKAREA (32K NOW:      *
*                                IF THAT'S NOT ENOUGH, USE MORE).     *
*                          R6 =  LOCAL SUBROUTINE LINKAGE             *
*                          R4 -> ACCESS LIST ENTRIES FOR A DATASET    *
*                                                                     *
*---------------------------------------------------------------------*
DATASETS DS    0H
         GETMAIN R,LV=32768,SP=2    YET MORE MEMORY
         LR    R10,R1               SET A BASE ON IT
         USING RWD,R10
         L     R1,=F'32768'         ALLOW SOME BIG OLE ACCESS LISTS
         ST    R1,RWDSIZE
*        B     GENERALS             ** TESTING **
*        MVC   PWADSNAM+1(7),=CL7'SYSTEMS'  ** TESTING **
*        MVI   PWADSNAM,X'07'               ** TESTING **
         ICHEINTY NEXTC,            SET UP DATASET ICHEINTY PARMS      *
               ENTRY=PWADSNAM,                                         *
               WKAREA=RWD,                                             *
               ACTIONS=(STCAOWNR,PWAACNT,PWAAVOL,PWAAACL),             *
               OPTIONS=(ACTIONS,TESTC,TESTM,FLDEF,NOEXEC),             *
               MF=(E,PWADINTY)
         EJECT
DSMAIN   DS    0H
         MVC   PWATRACE,=CL12'READ DATASET'
         XC    RWDRBA(RWDFVRA-RWDRBA),RWDRBA                        @C3
         ICHEINTY MF=(E,PWADINTY)   GET A PROFILE
         SPACE
         CH    R15,=H'12'           DONE ALL DATASET PROFILES?
         BE    DSFINISH              GO PUT OUT NUMBERS MESSAGES.
         LTR   R15,R15              UNEXPECTED RACF ERROR?
         BNZ   RACFERR               GO REPORT IT.
         TM    RWDFLAG1,RWDGENPR    GENERIC PROFILE?                @C3
         BO    DSMAIN01              SEE IF WE WANT IT.             @C3
         TM    PWAFLAG,PWAF1GEN     NOT GENERIC: WE WANT IT?        @C3
         BO    DSMAIN                NO, GET THE NEXT ONE,          @C3
         B     DSMAIN02              ELSE PROCEED.                  @C3
DSMAIN01 TM    PWAFLAG,PWAF1GEN     GENERIC: WE WANT IT?            @C3
         BZ    DSMAIN                NO, GET THE NEXT ONE.          @C3
DSMAIN02 DS    0H                   WE GOT THE KIND WE WANTED.      @C3
         SPACE
         COUNT PWADCNT              SAY WE READ ANOTHER ONE
         MVC   PWALKARG(8),RWDOWNER PICK UP OWNER
         BAL   R6,LOOKUP            SEE IF IT'S IN THE TABLE
         LTR   R15,R15
         BZ    DSGETACL
         SPACE
         COUNT PWADCNTO
         WIPE  PWACMD               CONSTRUCT ALTDSD COMMAND        @C3
         MVC   PWACMD(4),=CL4'ALD '
         MVI   PWACMD+4,C''''
         XR    R1,R1
         IC    R1,PWADSNAM
         BCTR  R1,0
         EX    R1,MVCDSNAL
         LA    R1,PWACMD+6(R1)
         MVI   0(R1),C''''
         LA    R1,2(R1)
         TM    RWDFLAG1,RWDGENPR    IS IT A GENERIC PROFILE?
         BO    DSALDGEN              YES, NO VOLUMES HERE EITHER
         CLC   RWDVOLL,=H'0'        ANY VOLUMES, OR IS IT A MODEL?
         BE    DSALDPUT              NO VOLUMES, DON'T PUT ON COMMAND
         SPACE
         MVC   0(4,R1),=C'VOL('
         LA    R1,4(R1)
         MVC   0(6,R1),RWDVOL
         MVI   6(R1),C')'
         LA    R1,7(R1)
         B     DSALDPUT
DSALDGEN DS    0H
         MVC   0(8,R1),=C' GENERIC'
         LA    R1,8(R1)
DSALDPUT DS    0H
         MVC   0(16,R1),=C' OWNER(????????)'
         PUT   PWAC2DCB,PWACMD
         SPACE
         WIPE  PWAREC
         MVC   PWAREC(L'STCALDM),STCALDM
         MVC   PWAREC+DSPALDMO(8),RWDOWNER
         TM    RWDFLAG1,RWDGENPR
         BZ    DSARPTVO
         MVC   PWAREC+DSPALDMG(11),=C'< GENERIC >'
         B     DSARPTDS
DSARPTVO DS    0H
         CLC   RWDVOLL,=H'0'
         BNE   DSARPTMD
         MVC   PWAREC+DSPALDMV(6),RWDVOL
         B     DSARPTDS
DSARPTMD DS    0H
         MVC   PWAREC+DSPALDMV(6),=C'-NONE-'
DSARPTDS DS    0H
         XR    R1,R1
         IC    R1,PWADSNAM
         BCTR  R1,0
         EX    R1,MVCDSNAR
         LA    R1,PWAREC+DSPALDMD+1(R1)
         MVI   0(R1),C')'
         PUT   PWADCB,PWAREC
         SPACE
DSGETACL DS    0H
*        WIPE  PWAREC                 ** TRACEMSG **
*        IC    R1,PWADSNAM            ** TRACEMSG **
*        BCTR  R1,0                   ** TRACEMSG **
*        EX    R1,DSTMOVE             ** TRACEMSG **
*        PUT   PWADCB,PWAREC          ** TRACEMSG **
*        B     *+10                   ** TRACEMSG **
*DSTMOVE MVC   PWAREC+1(0),PWADSNAM+1 ** TRACEMSG **
         LH    R7,RWDCNT            GET # ACL ENTRIES
         LTR   R7,R7                ARE THERE ANY?
         BZ    DSMAIN                NO, DON'T BOTHER WITH THIS ONE.
         FINDACL TYPE=D             LOCATE ACCESS LIST
         LR    R4,R1                GET ADDRESS OF 1ST ACL ENTRY
         SPACE
DSLOOKUP DS    0H
         MVC   PWALKARG(8),0(R4)    GET AN ACCESS LIST ENTRY'S NAME
         BAL   R6,LOOKUP            SEE IF IT'S IN THE TABLE
         SPACE
         LTR   R15,R15              WAS IT THERE?
         BNZ   DSGOTONE              NO. GO GET RID OF IT.
         SPACE
DSLOOKNX DS    0H
*        WIPE  PWAREC                        ** TRACEMSG **
*        MVC   PWAREC+3(9),=C'JUST DID '     ** TRACEMSG **
*        MVC   PWAREC+12(8),0(R4)            ** TRACEMSG **
*        PUT   PWADCB,PWAREC                 ** TRACEMSG **
         LA    R4,11(R4)            POINT TO NEXT ACL ENTRY
         BCT   R7,DSLOOKUP          GO LOOK UP NEXT ONE.
         SPACE
*        EX    0,*                  ** TESTING **
         B     DSMAIN               DONE ONE, DO THE NEXT ONE.
         SPACE
DSGOTONE DS    0H
*
*  AHA. WE HAVE FOUND AN ACL ENTRY WITH NO CORRESPONDING USER OR
*       GROUP PROFILE. WE SHOULD NOW DELETE IT.
*
         CLI   PWAPRM2,PWAPRM2N     WE REALLY DELETING STUFF?
         BE    DSDELRPT              NAW, SKIP DELETE.
         SPACE
         MVI   PWACMD,C' '          CONSTRUCT PERMIT COMMAND
         MVC   PWACMD+1(L'PWACMD-1),PWACMD
         MVC   PWACMD(3),=CL3'PE '
         MVI   PWACMD+3,C''''
         XR    R1,R1
         IC    R1,PWADSNAM
         BCTR  R1,0
         EX    R1,MVCDSNPE
         LA    R1,PWACMD+5(R1)
         MVI   0(R1),C''''
         LA    R1,2(R1)
         TM    RWDFLAG1,RWDGENPR    IS IT A GENERIC PROFILE?
         BO    DSDELGEN              YES, NO VOLUMES HERE EITHER
         CLC   RWDVOLL,=H'0'        ANY VOLUMES, OR IS IT A MODEL?
         BE    DSDELMDL              NO VOLUMES, DON'T PUT ON COMMAND
         SPACE
         MVC   0(11,R1),=CL11'VOL(      )'
         SPACE
         LA    R1,4(R1)
         MVC   0(6,R1),RWDVOL
         LA    R1,8(R1)
         MVC   PWATRACE,=CL12'RMV DATASET'
         B     DSDELRPT
         SPACE
DSDELMDL DS    0H
         MVC   PWATRACE,=CL12'RMV DSMODEL'
         B     DSDELRPT
         SPACE
DSDELGEN DS    0H
         MVC   PWATRACE,=CL12'RMV GENERIC'
         MVC   0(8,R1),=CL8'GENERIC '
         LA    R1,8(R1)
         SPACE
DSDELRPT DS    0H
         MVC   0(16,R1),=C'DEL ID(        )'
         LA    R1,7(R1)
         MVC   0(8,R1),PWALKARG
*
* CODE ADDED 4/28/88 BY JCB TO ADD A COMMENTED 'ACCESS(WHATEVER)' TO
*                    THE PERMIT COMMAND BEING BUILT
*
         LA    R1,10(R1)
         MVC   0(11,R1),=C'/* AC(?) */'
         LA    R15,STCACTBL
DSACSCAN DS    0H
         CLC   0(1,R15),8(R4)
         BE    DSGOTAC
         CLI   0(R15),X'FF'
         BE    DSACFAIL
         LA    R15,2(R15)
         B     DSACSCAN
DSGOTAC  DS    0H
         MVC   6(1,R1),1(R15)
DSACFAIL DS    0H
         PUT   PWACDCB,PWACMD       SPIT OUT PERMIT COMMAND
*
         WIPE  PWAREC               CLEAR RECORD AND MOVE BASE MESSAGE
         MVC   PWAREC(L'STCDSM),STCDSM
         XR    R1,R1
         IC    R1,PWADSNAM
         BCTR  R1,0
         EX    R1,MVCDSN            MOVE IN DSNAME WE'RE ON
         LA    R1,PWAREC+DSPDSMD+1(R1)
         MVI   0(R1),C')'           MAKE IT LOOK NEAT
         TM    RWDFLAG1,RWDGENPR    A GENERIC?
         BO    DSGOTGEN              FIX UP MESSAGE.
         CLC   RWDVOLL,=H'0'        ANY VOLUMES?
         BE    DSGOTMOD              NO, ASSUME MODEL.
         MVC   PWAREC+DSPDSMV(6),RWDVOL   MOVE IN (FIRST) VOLUME
         B     DSGOTUSR
         SPACE
DSGOTMOD DS    0H
         MVC   PWAREC+DSPDSMV(6),=CL6'MODEL'   SAY IT'S A MODEL
         B     DSGOTUSR
         SPACE
DSGOTGEN DS    0H
         MVC   PWAREC+DSPDSMG(11),=CL11'< GENERIC >'
         SPACE
DSGOTUSR DS    0H
         MVC   PWAREC+DSPDSMU(8),PWALKARG      PUT IN ENTRY WE DID
         PUT   PWADCB,PWAREC
         SPACE
         COUNT PWADCNTD             COUNT ONE AS DELETED
         B     DSLOOKNX
         TITLE 'CLEANACL -- SAY HOW MANY DATASETS WE DID'
DSFINISH DS    0H
         TM    PWAFLAG,PWAF1GEN     TRY TO TURN OFF DO-GENERICS:    @C3
         BO    DSNCRPT               IF IT WAS ON, ALREADY DID IT.  @C3
         OI    PWAFLAG,PWAF1GEN     SET DO-GENERICS FLAG            @C3
         XC    PWADSNAM,PWADSNAM    CLEAR ENTRY                     @C3
         ICHEINTY GENERIC=YES,      MODIFY ICHEINTY LIST TO DO ONLY @C3*
               MF=(E,PWADINTY)       THE GENERICS                   @C3
         B     DSMAIN                                               @C3
         SPACE 1                                                    @C3
DSNCRPT  DS    0H                                                   @C3
         NI    PWAFLAG,X'FF'-PWAF1GEN                               @C3
         MVC   PWAREC(L'STCDSOKM),STCDSOKM
         SPACE
         CONVERT TO=PWAREC+DSPDSOKN,FROM=PWADCNT,LENGTH=5
         SPACE
         CONVERT TO=PWAREC+DSPDSOKR,FROM=PWADCNTD,LENGTH=5
         SPACE
         CONVERT TO=PWAREC+DSPDSOKO,FROM=PWADCNTO,LENGTH=5
         SPACE
         PUT   PWADCB,PWAREC        SAY WE DONE DEM DATASETS
         WIPE  PWAREC
         PUT   PWADCB,PWAREC        BLANK LINE AS SEPARATOR.
*        B     FINISHED             ** TESTING **
         TITLE 'CLEANACL -- PROCESS GENERAL-CLASS PROFILES'
GENERALS DS    0H
*---------------------------------------------------------------------*
*                                                                     *
*   NOW, GO THROUGH ALL CLASSES IN THE CLASS DESCRIPTOR TABLE AND     *
*   READ ALL PROFILES IN EACH CDT CLASS. THE CDT LIVES IN LPALIB      *
*   AS ICHERCDE AND IS MAPPED BY THE 'CDE' DSECT.                     *
*                                                                     *
*   CURRENTLY, RESOURCE GROUP CLASSES ARE NOT PROCESSED.              *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
*        LOAD  EP=ICHRRCDE          FIND CLASS DESCRIPTOR TABLE    @C2
*        ST    R0,PWAACDT            AND SAVE FOR LATER.           @C2
*        LR    R9,R0                R9 BECOMES CDT ENTRY BASE      @C2
         L     R9,CVTPTR                                           @C2
         L     R9,CVTRAC-CVT(,R9)                                  @C2
         L     R9,RCVTCDTP-RCVT(,R9)                               @C2
         ST    R9,PWAACDT                                          @C2
         USING CDE,R9
         TM    CDEPROCF,CDEGROUP    THIS A GROUP CLASS?
         BZ    GENERAL2              NO, PROCEED.
         WIPE  PWAREC
         MVC   PWAREC(L'STCGRPCM),STCGRPCM
         MVC   PWAREC+DSPGRPCL(8),CDECLASS
         PUT   PWADCB,PWAREC
         B     GENNXTCL
         SPACE
GENERAL2 DS    0H
         DROP  R10                  DROP RWD BASE
         USING RWC,R10              REUSE RWD CORE FOR RWC.
         XC    RWC+4(32),RWC+4      CLEAR RMWA HEADER
         SPACE
         ICHEINTY ENTRY=PWAENTRY,   SET UP GENERAL-CLASS READ CALLS    *
               CLASS=(*-*),                                            *
               WKAREA=RWC,                                             *
               ACTIONS=(STCAOWNR,PWAACNT,PWAAACL),                     *
               OPTIONS=(ACTIONS,TESTC,TESTM,FLDEF,NOEXEC),             *
               MF=(E,PWACINTY)
         SPACE
GENMAIN  DS    0H
         MVC   PWATRACE,=CL12'READ GENERAL'
         ICHEINTY CLASS=CDECLASS,   READ A PROFILE                     *
               MF=(E,PWACINTY)
*        ICHEINTY CLASS=STCDASDV,   ** TESTING **                    *
*              MF=(E,PWACINTY)      ** TESTING **
         SPACE
         CH    R15,=H'12'           FINISHED THIS CLASS YET?
         BE    GENNXTCL              YES, GO PICK UP NEXT ONE
         LTR   R15,R15              UNEXPECTED RACF ERROR?
         BNZ   RACFERR               GO REPORT IT.
         SPACE
         COUNT PWACCNT              SAY WE READ ONE
***
         MVC   PWALKARG(8),RWCOWNER PICK UP OWNER
         BAL   R6,LOOKUP            SEE IF IT'S IN THE TABLE
         LTR   R15,R15
         BZ    GNGETACL
         SPACE
         COUNT PWACCNTO
         WIPE  PWACMD               CONSTRUCT RALTER COMMAND
         MVC   PWACMD(5),=CL5'RALT '
         MVC   PWACMD+5(8),CDECLASS
         XR    R1,R1
         IC    R1,PWAENTRY
         BCTR  R1,0
         EX    R1,MVCENTRA
         CH    R1,=H'44'    ** TESTING **
         BNH   *+8          ** TESTING **
         EX    0,*          ** TESTING **
         LA    R1,PWACMD+14(R1)
         MVC   0(16,R1),=C' OWNER(????????)'
         PUT   PWAC2DCB,PWACMD
         SPACE
         WIPE  PWAREC
         MVC   PWAREC(L'STCRALTM),STCRALTM
         MVC   PWAREC+DSPRALTO(8),RWCOWNER
         MVC   PWAREC+DSPRALTC(8),CDECLASS
         XR    R1,R1
         IC    R1,PWAENTRY
         BCTR  R1,0
         EX    R1,MVCENTRP
         LA    R1,PWAREC+DSPRALTE+1(R1)
         MVI   0(R1),C')'
         PUT   PWADCB,PWAREC
         SPACE
***
GNGETACL DS    0H
         LH    R7,RWCCNT            GET # ACL ENTRIES
         LTR   R7,R7                ARE THERE ANY?
         BZ    GENMAIN               NO, DON'T BOTHER WITH THIS ONE.
         SPACE
         FINDACL TYPE=C             LOCATE ACCESS LIST
         LR    R4,R1                GET ADDRESS OF 1ST ACL ENTRY
*        WIPE  PWAREC                 ** TRACEMSG **
*        IC    R1,PWAENTRY            ** TRACEMSG **
*        BCTR  R1,0                   ** TRACEMSG **
*        EX    R1,GNTMOVE             ** TRACEMSG **
*        PUT   PWADCB,PWAREC          ** TRACEMSG **
*        B     GNLOOKUP               ** TRACEMSG **
*GNTMOVE MVC   PWAREC+1(0),PWAENTRY+1 ** TRACEMSG **
         SPACE
GNLOOKUP DS    0H
         MVC   PWALKARG(8),0(R4)    GET AN ACCESS LIST ENTRY'S NAME
         BAL   R6,LOOKUP            SEE IF IT EXISTS
         SPACE
         LTR   R15,R15              WAS IT THERE?
         BNZ   GNGOTONE              NO. GO GET RID OF IT.
         SPACE
GNLOOKNX DS    0H
*        WIPE  PWAREC                        ** TRACEMSG **
*        MVC   PWAREC+3(9),=C'JUST DID '     ** TRACEMSG **
*        MVC   PWAREC+12(8),0(R4)            ** TRACEMSG **
*        PUT   PWADCB,PWAREC                 ** TRACEMSG **
         LA    R4,11(R4)            POINT TO NEXT ACL ENTRY
         BCT   R7,GNLOOKUP          GO LOOK UP NEXT ONE.
         SPACE
         B     GENMAIN              DONE ONE, DO THE NEXT ONE.
         EJECT
GNGOTONE DS    0H
*
*  AHA. WE HAVE FOUND AN ACL ENTRY WITH NO CORRESPONDING USER OR
*       GROUP PROFILE. WE SHOULD NOW DELETE IT.
*
         CLI   PWAPRM2,PWAPRM2N     WE REALLY DELETING STUFF?
         BE    GNGOTRPT              NAW, SKIP IT.
         SPACE
         MVC   PWATRACE,=CL12'RMV GENERAL'
         WIPE  PWACMD
         MVC   PWACMD(3),=CL3'PE '
         XR    R1,R1
         IC    R1,PWAENTRY
         BCTR  R1,0
         EX    R1,MVCENTPE
         LA    R1,PWACMD+5(R1)
         MVC   0(6,R1),=CL6'CLASS('
         LA    R1,6(R1)
         MVC   0(8,R1),CDECLASS
         MVI   8(R1),C')'
         LA    R1,10(R1)
         MVC   0(16,R1),=C'DEL ID(        )'
         LA    R1,7(R1)
         MVC   0(8,R1),PWALKARG
*
* CODE ADDED 4/28/88 BY JCB TO ADD A COMMENTED 'ACCESS(WHATEVER)' TO
*                    THE PERMIT COMMAND BEING BUILT
*
         LA    R1,12(R1)
         MVC   0(11,R1),=C'/* AC(?) */'
         LA    R15,STCACTBL
GNACSCAN DS    0H
         CLC   0(1,R15),8(R4)
         BE    GNGOTAC
         CLI   0(R15),X'FF'
         BE    GNACFAIL
         LA    R15,2(R15)
         B     GNACSCAN
GNGOTAC  DS    0H
         MVC   6(1,R1),1(R15)
GNACFAIL DS    0H
         PUT   PWACDCB,PWACMD
         SPACE
GNGOTRPT DS    0H
         WIPE  PWAREC
         MVC   PWAREC(L'STCGENM),STCGENM    CONSTRUCT REPORT LINE
         XR    R1,R1
         IC    R1,PWAENTRY
         BCTR  R1,0
         EX    R1,MVCENTRY
         LA    R1,PWAREC+DSPGENME+1(R1)
         MVI   0(R1),C')'
         MVC   PWAREC+DSPGENMC(8),CDECLASS
         MVC   PWAREC+DSPGENMU(8),PWALKARG
         PUT   PWADCB,PWAREC
         COUNT PWACCNTD
         SPACE
GENLKNX  DS    0H
         LA    R4,11(R4)            POINT TO NEXT ACL ENTRY
         BCT   R7,GNLOOKUP           AND KEEP ON MUNCHING,
         B     GENMAIN              TIL THEY'RE ALL GONE GONE GONE....
         EJECT
GENNXTCL DS    0H                   FINISHED A CLASS, GET THE NEXT ONE.
         L     R1,CDECLPOS          GET CLASS MASK FOR CURRENT CLASS@C3
         L     R2,CVTPTR            GET ADDRESS OF RCVT             @C3
         L     R2,CVTRAC-CVT(,R2)                                   @C3
         USING RCVT,R2                                              @C3
         N     R1,RCVTCGEN          GENERICS TURNED ON FOR CLASS?   @C3
         BZ    GENNCRPT              NO, DON'T BOTHER TRYING.       @C3
         DROP  R2                                                   @C3
         TM    PWAFLAG,PWAF1GEN     TRY TO TURN OFF DO-GENERICS:    @C3
         BO    GENNCRPT              IF IT WAS ON, ALREADY DID IT.  @C3
         OI    PWAFLAG,PWAF1GEN     SET DO-GENERICS FLAG            @C3
         WIPE  PWAENTRY             CLEAR ENTRY                     @C3
         MVI   PWAENTRY,X'01'        AND SET ITS LENGTH TO 1        @C3
         ICHEINTY GENERIC=UNCOND,   MODIFY ICHEINTY LIST TO DO ONLY @C3*
               MF=(E,PWACINTY)       THE GENERICS                   @C3
         B     GENMAIN                                              @C3
GENNCRPT DS    0H                                                   @C3
         NI    PWAFLAG,X'FF'-PWAF1GEN                               @C3
         MVC   PWAREC(L'STCGNOKM),STCGNOKM
         SPACE
         CONVERT TO=PWAREC+DSPGNOKN,FROM=PWACCNT,LENGTH=5
         SPACE
         CONVERT TO=PWAREC+DSPGNOKR,FROM=PWACCNTD,LENGTH=5
         SPACE
         CONVERT TO=PWAREC+DSPGNOKO,FROM=PWACCNTO,LENGTH=5
         SPACE
         MVC   PWAREC+DSPGNOKC(8),CDECLASS
         SPACE
         PUT   PWADCB,PWAREC        SUMMARY LINE PER CLASS
         WIPE  PWAREC
         PUT   PWADCB,PWAREC        PUT A BLANK LINE
*        B     FINISHED             ** TESTING **
         SPACE
         XC    PWACCNT,PWACCNT      CLEAR PROFILE COUNTER
         XC    PWACCNTD,PWACCNTD     AND DELETE COUNTER
         XC    PWACCNTO,PWACCNTO      AND OWNER COUNTER
         SPACE
         AH    R9,CDELEN            POINT TO NEXT CDE
         CLC   CDELEN(2),=H'0'      WE JUST DO THE LAST ONE?
         BE    FINISHED              GREAT, WE'RE ALMOST DONE.
         TM    CDEPROCF,CDEGROUP    THIS A GROUP CLASS?
         BO    GENNCRPT              SKIP IT.
         WIPE  PWAENTRY             RESET ENTRY NAME FOR
         MVI   PWAENTRY,X'01'         ICHEINTY NEXT
         XC    RWC+4(32),RWC+4      CLEAR OUT RMWA HEADER AGAIN
*        ICHEINTY GENERIC=NO,       MODIFY ICHEINTY LIST TO DO ONLY  *
*              MF=(E,PWACINTY)       NON-GENERICS
         MVC   PWACINTY(LENCINTY),STCCINTY  REBUILD ICHEINTY PARMLIST
         ICHEINTY ENTRY=PWAENTRY,   SET UP GENERAL-CLASS READ CALLS    *
               CLASS=(*-*),                                            *
               WKAREA=RWC,                                             *
               ACTIONS=(STCAOWNR,PWAACNT,PWAAACL),                     *
               OPTIONS=(ACTIONS,TESTC,TESTM,FLDEF,NOEXEC),             *
               MF=(E,PWACINTY)
         B     GENMAIN              BACK INTO MAIN LOOP.
         TITLE 'CLEANACL -- TERMINATION'
FINISHED DS    0H
         CLOSE PWADCB,MF=(E,PWACLOSE)
         CLOSE PWACDCB,MF=(E,PWACLOSE)
         CLOSE PWAC2DCB,MF=(E,PWACLOSE)
*        WTO   ,MF=(E,STCWTO3)
         SPACE
*
*  FREE ALL THAT CORE WE GOT A LONG TIME AGO.
*
         FREEMAIN R,LV=32768,A=(R10),SP=2   FREE RACF MANAGER WORKAREA
         SPACE
         L     R3,PWAINDEX          GET INDEX ENTRY COUNT
         LA    R4,PWAINDEX+4        POINT TO FIRST INDEX ENTRY
         SPACE
FINFREES DS    0H
         L     R2,0(R4)             ADDRESS OF CORE TO FREE
         FREEMAIN R,LV=4096,A=(R2),SP=1   LET IT GO
         LA    R4,12(R4)            POINT TO NEXT INDEX ENTRY
         BCT   R3,FINFREES          DO 'EM ALL.
         EJECT
QUITZERO DS    0H
         XR    R2,R2                SAVE FINAL RC IN R2
         B     QUIT                 SKIP DOWN TO PWA FREE
         SPACE
QUIT16   DS    0H
         LA    R2,16                SAVE FINAL RC IN R2
         SPACE
QUIT     DS    0H
         LR    R1,R13               COPY PWA ADDRESS
         L     R13,4(R13)           POINT TO PRIOR SAVEAREA
         FREEMAIN R,LV=PWALEN,A=(1)   FREE PROGRAM WORKAREA
         SPACE
         LR    R15,R2               COPY RETURN CODE
         RETURN (14,12),RC=(15)     TERMINATE.
         TITLE 'CLEANACL -- RESTART USER/GROUP TABLE'
*---------------------------------------------------------------------*
*                                                                     *
*  THE RESTART ROUTINE READS THE USER/GROUP TABLE FROM THE CHKPT      *
*  DATASET INSTEAD OF FROM RACF. AS IT GOES, IT REBUILDS PWAINDEX.    *
*  WHEN THE TABLE IS REBUILT, RESTART EXITS TO 'DATASETS'.            *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
RESTART  DS    0H
         MVC   PWACPDCB(LENCRDCB),STCCRDCB       COPY RESTART DCB
         MVC   PWATRACE,=CL12'OPEN RESTART'
         OPEN  (PWACPDCB,INPUT),MF=(E,PWAOPEN)   OPEN RESTART DCB
         LTR   R15,R15
         BNZ   OPENERR                           OOPS, FORGOT A DDNAME.
         SPACE
RSTMAIN  DS    0H                   MAIN LOOP FOR READING BLOCKS
         BAL   R6,GET4K             GET A PAGE: SETS PWAINDEX
         GET   PWACPDCB,(R10)       GET RECORD: R10 = A(PAGE)
         B     RSTMAIN              DO TIL WE GOT 'EM ALL.
         SPACE
RSTEOF   DS    0H                   END OF CHECKPOINT BLOCKS.
         FREEMAIN R,LV=4096,A=(10),SP=1  YEAH, WE GOT ONE TOO MANY
         L     R1,PWAINDEX          GET INDEX ENTRY COUNT
         BCTR  R1,0                   (IT'S ONE TOO MANY, TOO)
         ST    R1,PWAINDEX            (SO FIX AND SAVE IT.   )
         LA    R2,PWAINDEX+4        GET START OF INDEX ENTRIES
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*  THE USER/GROUP TABLE HAS BEEN RE-CREATED. NOW REBUILD THE INDEX.   *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
RSTRBLD  DS    0H
         L     R3,0(R2)             GET ADDRESS OF A PAGE
         MVC   4(8,R2),4096-8(R3)   MOVE HI KEY
         LA    R2,12(R2)            POINT TO NEXT INDEX ENTRY
         BCT   R1,RSTRBLD           DO FOR ALL INDEXES
         SPACE
         WIPE  PWAREC
         MVC   PWAREC(L'STCMSGCR),STCMSGCR
         PUT   PWADCB,PWAREC        SAY 'CHECKPOINT DATASET LOADED'
         SPACE
         CLOSE PWACPDCB,MF=(E,PWACLOSE)  AND CLOSE CHKPT DATASET
         B     EXCLUDE              JUMP INTO EXCLUDE CODE.         @C3
         TITLE 'CLEANACL -- SUBROUTINES FOR THE MAINLINES'
GETUSER  DS    0H                   GET THE NEXT USER PROFILE
         MVC   PWATRACE(12),=CL12'READ USERID'
         ICHEINTY MF=(E,PWAUINTY)   GO AFTER ONE
         SPACE
         CH    R15,=H'12'           NO MORE?
         BE    GTUDONE               YES, GO SET FLAG.
         LTR   R15,R15              OTHER ERROR?
         BNZ   RACFERR               SNAFU.
         SPACE
         COPYFLD PWAJUSR,PWAUSER    MOVE W/O LENGTH
         COUNT PWAUCNT              COUNT HIM
         BR    R6                   THAT'S ALL.
         SPACE
GTUDONE  DS    0H
         MVC   PWAJUSR(8),=8X'FF'   MAKE COMPARE WORK RIGHT
         OI    PWAFLAG,PWAUEOF      SET RAN-OUT FLAG
         BR    R6
         EJECT
GETGROUP DS    0H                   GET NEXT GROUP PROFILE
         MVC   PWATRACE(12),=CL12'READ GROUP'
         ICHEINTY MF=(E,PWAGINTY)   GO AFTER ONE
         SPACE
         CH    R15,=H'12'           ETC ETC, JUST LIKE GETUSER
         BE    GTGDONE
         LTR   R15,R15
         BNZ   RACFERR
         SPACE
         COPYFLD PWAJGRP,PWAGROUP
         COUNT PWAGCNT
         BR    R6
         SPACE
GTGDONE  DS    0H
         MVC   PWAJGRP(8),=8X'FF'
         OI    PWAFLAG,PWAGEOF
         BR    R6
         EJECT
GET4K    DS    0H                   GET A PAGE TO PUT STUFF ON
         CLI   PWAPRM1,PWAPRM1C     CHECKPOINTING?
         BNE   GET4KGET              NO, DON'T TRY TO SAVE PAGE.
         SPACE
         L     R1,PWAINDEX          GET CURRENT # PAGES
         LTR   R1,R1                DONE ANY YET?
         BNZ   GET4KCKP              SURE, GO SAVE PAGE.
         SPACE
         MVC   PWACPDCB(LENCPDCB),STCCPDCB       COPY DCB
         MVC   PWATRACE,=CL12'OPEN CHECKPT'      TRACE IT
         OPEN  (PWACPDCB,OUTPUT),MF=(E,PWAOPEN)  OPEN IT
         SPACE
         LTR   R15,R15              PROBLEMS?
         BNZ   OPENERR               WON'T GET FAR LIKE THAT.
         SPACE
         B     GET4KGET             NOTHING TO PUT WITH YET.
         SPACE
GET4KCKP DS    0H
         PUT   PWACPDCB,(R10)       SAVE PRIOR 4K BLOCK
         SPACE
GET4KGET DS    0H
         GETMAIN RU,LV=4096,SP=1,BNDRY=PAGE   GET SOME CORE
         EJECT
         LR    R14,R1               SAVE ADDRESS OF NEW STORAGE
         COUNT PWAINDEX             COUNT HOW MANY BLOCKS THERE ARE
         LA    R15,PWAINDEX         FIGURE WHERE NEXT ENTRY GOES:
         LA    R15,4(R15)             POINT TO START OF ENTRIES
         L     R0,PWAINDEX            GET DISP. TO THE NEW ONE:
         SH    R0,=H'1'                 GET COUNT, SUBTRACT 1,
         MH    R0,=H'12'                MULT. BY ONE ENTRY'S LENGTH
         AR    R15,R0                 MAKE IT AN ADDRESS
         ST    R14,0(R15)           THAT'S WHERE.
         LR    R10,R14              SET NEW 4K PAGE BASE
         XR    R9,R9                CLEAR DIAPLACEMENT INTO PAGE
*
*   SET NEW PAGE TO HI-VALUES. R14 CONTAINS PAGE ADDRESS.
*
         LH    R15,=H'4096'         LENGTH
         XR    R0,R0                DON'T MOVE ANY DATA
         XR    R1,R1                LENGTH = 0
         ICM   R1,B'1000',=X'FF'    PAD CHARACTER = X'FF'
         MVCL  R14,R0               SPREAD FF'S THRU THE PAGE
         SPACE
         BR    R6                   RETURN TO CALLER.
         EJECT
LOOKUP   DS    0H                   TRY TO FIND AN ACL ENTRY
         L     R1,PWAINDEX          GET INDEX ENTRY COUNT
         LA    R2,PWAINDEX+4        START OF INDEX ENTRIES
         SPACE
LKUGO    DS    0H
         CLC   4(8,R2),PWALKARG     HI-KEY .GE. ARGUMENT?
         BNL   LKUPAGE               YES, SEE IF ENTRY'S ON PAGE.
         SPACE
         LA    R2,12(R2)            POINT TO NEXT ENTRY
         BCT   R1,LKUGO              AND COUNT 'EM DOWN.
         SPACE
LKUPAGE  DS    0H                   NOW LOCATE ENTRY ON 4K PAGE
         L     R2,0(R2)             GET ADDRESS OF PAGE
         LA    R1,512               LOOP THIS MANY TIMES
         SPACE
LKUSRCH  DS    0H
         CLC   0(8,R2),PWALKARG     GOT A HIT?
         BE    LKUOK
         LA    R2,8(R2)             TRY NEXT ONE
         BCT   R1,LKUSRCH
         SPACE
         LA    R15,8                NO HIT.
         BR    R6
         SPACE
LKUOK    DS    0H
         XR    R15,R15              HIT, LEAVE ALONE.
         BR    R6
         SPACE
MVCDSN   MVC   PWAREC+DSPDSMD(0),PWADSNAM+1 ** EXECUTE ONLY **
MVCDSNAR MVC   PWAREC+DSPALDMD(0),PWADSNAM+1 ** EXECUTE ONLY **
MVCDSNPE MVC   PWACMD+4(0),PWADSNAM+1       ** EXECUTE ONLY **
MVCDSNAL MVC   PWACMD+5(0),PWADSNAM+1       ** EXECUTE ONLY **
MVCENTRY MVC   PWAREC+DSPGENME(0),PWAENTRY+1 ** EXECUTE ONLY **
MVCENTPE MVC   PWACMD+3(0),PWAENTRY+1        ** EXECUTE ONLY **
MVCENTRA MVC   PWACMD+13(0),PWAENTRY+1        ** EXECUTE ONLY **
MVCENTRP MVC   PWAREC+DSPRALTE(0),PWAENTRY+1 ** EXECUTE ONLY **
         TITLE 'CLEANACL -- ERROR ROUTINES'
RACFERR  DS    0H                   REPORT WIERD RACF RETURNCODES
         MVC   PWAREC(L'STCRACFM),STCRACFM
         MVC   PWAREC+DSPRACFT,PWATRACE
         SPACE
         CVD   R15,PWACVD
         UNPK  PWAREC+DSPRACFC(4),PWACVD+6(2)
         OI    PWAREC+DSPRACFC+3,X'F0'
         SPACE
         PUT   PWADCB,PWAREC
         SPACE
         CLOSE PWADCB,MF=(E,PWACLOSE)
         B     QUIT16
         SPACE
PARMERR  DS    0H               //  EXEC PGM=CLEANACL,PARM=DUH
*        WTO   ,MF=(E,STCWTOPE)     SAY WHAT?
         B     QUIT16
         SPACE
OPENERR  DS    0H                   WHO FORGOT THE DDNAMES?
         MVC   PWAREC(L'STCOPENM),STCOPENM
         MVC   PWAREC+DSPOPENT(12),PWATRACE
         PUT   PWADCB,PWAREC
         B     QUIT16
         TITLE 'CLEANACL -- CONSTANTS'
STC      DS    0D                   CONSTANTS START HERE
*STCDASDV DC    CL8'CACCT  '         ** TESTING **
         PRINT NOGEN
STCDCB   DCB   DDNAME=SYSPRINT,LRECL=132,BLKSIZE=1320,RECFM=FB,        *
               DSORG=PS,MACRF=PM
LENDCB   EQU   *-STCDCB
         SPACE
STCCDCB  DCB   DDNAME=COMMANDS,LRECL=80,BLKSIZE=6160,RECFM=FB,         *
               DSORG=PS,MACRF=PM
LENCDCB  EQU   *-STCCDCB
         SPACE
STCC2DCB DCB   DDNAME=OWNERS,LRECL=80,BLKSIZE=6160,RECFM=FB,           *
               DSORG=PS,MACRF=PM
LENC2DCB EQU   *-STCC2DCB
         SPACE
STCCPDCB DCB   DDNAME=CHECKPT,LRECL=4096,BLKSIZE=4096,RECFM=FB,        *
               DSORG=PS,MACRF=PM
LENCPDCB EQU   *-STCCPDCB
         SPACE
STCCRDCB DCB   DDNAME=CHECKPT,LRECL=4096,BLKSIZE=4096,RECFM=FB,        *
               DSORG=PS,MACRF=GM,EODAD=RSTEOF
LENCRDCB EQU   *-STCCRDCB
         SPACE 1                    |                               @C1
STCSYSIN DCB   DDNAME=EXCLUDES,LRECL=80,BLKSIZE=80,RECFM=FB,        @C1*
               DSORG=PS,MACRF=GM,EODAD=EXCEND                       @C1
LENSYSIN EQU   *-STCSYSIN           |                               @C1
         PRINT GEN
         SPACE
STCMSGCP DC    C'*** CHECKPOINT DATASET CREATED.'
STCMSGCR DC    C'*** CHECKPOINT DATASET RELOADED.'
STCMSGTL DC    C'*** USER/GROUP TABLE LOADED: NNNNN USERS, NNNNN GROUPS*
               '
DSPMTLU  EQU   29,5
DSPMTLG  EQU   42,5
         SPACE 1                    |                               @C1
STCXNTRY DC    C'*** EXCLUDE ENTRY(........) NOT FOUND IN USER/GROUP TA*
               BLE'                 |                               @C1
DSPXNENT EQU   18,8                 |                               @C1
STCDSOKM DC    C'*** NNNNN DATASET PROFILES PROCESSED, NNNNN INVALID EN*
               TRIES REMOVED, NNNNN OWNERS CHANGED'
DSPDSOKN EQU   4,5
DSPDSOKR EQU   38,5
DSPDSOKO EQU   69,7
STCGNOKM DC    C'*** NNNNN CLASS(        ) PROFILES PROCESSED, NNNNN IN*
               VALID ENTRIES REMOVED, NNNNN OWNERS CHANGED'
DSPGNOKN EQU   4,5
DSPGNOKC EQU   16,8
DSPGNOKR EQU   46,8
DSPGNOKO EQU   77
         SPACE
STCOPENM DC    C'*** TTTTTTTTTTTT - OPEN ERROR'
DSPOPENT EQU   4,12
         SPACE
STCDSM   DC    C'REMOVED USER(        ) FROM VOL(      ) DATASET('
DSPDSMU  EQU   13,8
DSPDSMG  EQU   28,11
DSPDSMV  EQU   32,6
DSPDSMD  EQU   48,44
         SPACE
STCALDM  DC    C'CHANGED OWNER(        ) ON VOL(      ) DATASET('
DSPALDMO EQU   14,8
DSPALDMG EQU   27,11
DSPALDMV EQU   31,6
DSPALDMD EQU   47,44
         SPACE
STCGENM  DC    C'REMOVED USER(        ) FROM CLASS(        ) ENTRY('
DSPGENMU EQU   13,8
DSPGENMC EQU   34,6
DSPGENME EQU   50,44
         SPACE
STCRALTM DC    C'CHANGED OWNER(        ) ON CLASS(        ) ENTRY('
DSPRALTO EQU   14,8
DSPRALTC EQU   33,8
DSPRALTE EQU   49,44
         SPACE
STCGRPCM DC    C'*** RESOURCE-GROUP CLASS(        ) IGNORED.'
DSPGRPCL EQU   25,8
         SPACE
STCRACFM DC    C'*** RACF MANAGER ERROR - TTTTTTTTTTTT RC CCCC (DEC)'
DSPRACFT EQU   25,12
DSPRACFC EQU   41,4
         SPACE
STCOPEN  OPEN  (STCDCB,OUTPUT),MF=L
LENOPEN  EQU   *-STCOPEN
         SPACE
STCCLOSE CLOSE STCDCB,MF=L
LENCLOSE EQU   *-STCCLOSE
         SPACE
STCUINTY ICHEINTY NEXT,                                                *
               TYPE='USR',                                             *
               ENTRY=*-*,                                              *
               WKAREA=*-*,                                             *
               MF=L
LENUINTY EQU   *-STCUINTY
         SPACE
STCGINTY ICHEINTY NEXT,                                                *
               TYPE='GRP',                                             *
               ENTRY=*-*,                                              *
               WKAREA=*-*,                                             *
               MF=L
LENGINTY EQU   *-STCGINTY
         SPACE
STCDINTY ICHEINTY NEXT,             ICHEINTY TO READ DATASET PROFILES  *
               TYPE='DS',                                              *
               ENTRY=*-*,                                              *
               WKAREA=*-*,                                             *
               GENERIC=NO,                                          @C3*
               ACTIONS=(,,,),                                          *
               MF=L
LENDINTY EQU   *-STCDINTY
         SPACE
STCCINTY ICHEINTY NEXT,             ICHEINTY TO READ GENERAL PROFILES  *
               TYPE='GEN',                                             *
               CLASS=*-*,                                              *
               GENERIC=NO,                                             *
               ENTRY=*-*,                                              *
               WKAREA=*-*,                                             *
               ACTIONS=(,,),                                           *
               MF=L
LENCINTY EQU   *-STCCINTY
         SPACE
STCAOWNR ICHEACTN FIELD=OWNER,MF=L
         SPACE
STCACNT  ICHEACTN FIELD=ACLCNT,MF=L READ ACCESS LIST COUNT
LENACNT  EQU   *-STCACNT
         SPACE
STCAACL  ICHEACTN FIELD=ACL,MF=L    READ ENTIRE ACCESS LIST
LENAACL  EQU   *-STCAACL
         SPACE
STCAVOL  ICHEACTN FIELD=VOLSER,MF=L READ VOLSER LIST
LENAVOL  EQU   *-STCAVOL
         SPACE
STCACTBL DS    0CL2
         DC    X'80',C'A'
         DC    X'40',C'C'
         DC    X'20',C'U'
         DC    X'10',C'R'
         DC    X'01',C'N'
         DC    X'FF',C'@'
         LTORG
         TITLE 'CLEANACL -- PROGRAM WORKAREA DSECT'
PWA      DSECT
PWASAVE  DS    18F
PWASAVE2 DS    16F
         SPACE
PWAARWU  DS    A                    ADDRESS OF USER WORKAREA
PWAARWG  DS    A                    ADDRESS OF GROUP WORKAREA
PWAARWP  DS    A                    ADDRESS OF PROFILE WORKAREA
         SPACE
PWACNTRS DS    0F                   FULLWORDS FOR COUNTING THINGS:
PWAUCNT  DS    A                      HOW MANY USERIDS WE READ
PWAGCNT  DS    A                      HOW MANY GROUPS WE READ
PWADCNT  DS    A                      HOW MANY DATASETS WE READ
PWADCNTD DS    A                      HOW MANY DATASET REMOVES WE DID
PWADCNTO DS    A                      HOW MANY DATASET OWNERS WE DID
PWACCNT  DS    A                      HOW MANY GENERAL-CLASSES WE READ
PWACCNTD DS    A                      HOW MANY GENERAL REMOVES WE DID
PWACCNTO DS    A                      HOW MANY GENERAL OWNERS WE DID
LENCNTRS EQU   *-PWACNTRS           HOW LONG TO DO AN XC FOR.
         SPACE
PWAACDT  DS    A                    ADDRESS OF CDT IN LPA
         SPACE
PWACVD   DS    D                    CONVERT-TO-DECIMAL WORKAREA
         SPACE
PWAOPEN  DS    CL(LENOPEN)          OPEN PARMLIST
PWACLOSE DS    CL(LENCLOSE)         CLOSE PARMLIST
         SPACE
PWAFLAG  DS    X                    PROCESS FLAGS:
PWAUEOF  EQU   X'80'                 RAN OUT OF USERIDS
PWAGEOF  EQU   X'40'                 RAN OUT OF GROUPS
PWAF1GEN EQU   X'20'                 MAKE GENERIC PASS OVER CLASS
         SPACE
PWAPRM1  DS    C                    PARAMETER FLAG:
PWAPRM1C EQU   C'C'                  CHECKPOINT USER/GROUP TABLE
PWAPRM1R EQU   C'R'                  RESTART USER/GROUP TABLE OFF CKPT
PWAPRM1N EQU   C' '                  NORMAL PROCESSING.
         SPACE
PWAPRM2  DS    C                    ARE WE REALLY GONNA DELETE?
PWAPRM2Y EQU   C'Y'                  YES, OR
PWAPRM2N EQU   C'N'                  NO.
         SPACE
PWAUSER  DS    CL9                  USERID FOR ICHEINTY NEXT
PWAGROUP DS    CL9                  GROUP FOR ICHEINTY NEXT
PWAJUSR  DS    CL8                  USERID WITH BLANK PADDING
PWAJGRP  DS    CL8                  GROUP WITH BLANK PADDING
PWACLASS DS    CL8                  CLASS FOR NON-DS ICHEINTY
PWADSNAM DS    CL45                 DATASET FOR DATASET ICHEINTY
PWAENTRY DS    CL45                 ENTRYNAME FOR GENERAL ICHEINTY
PWATRACE DS    CL12                 TRACE INFO FOR WHAT WE JUST DID
PWALKARG DS    CL8                  SEARCH ARGUMENT FOR LOOKUP ROUTINE
         SPACE
PWAREC   DS    CL132                PLACE TO WRITE THINGS OUT WITH
PWACMD   DS    CL80                 PLACE TO WRITE COMMANDS TO
         SPACE
         DS    0F
PWADCB   DS    CL(LENDCB)           DCB FOR MESSAGES
PWACDCB  DS    CL(LENCDCB)           DCB FOR COMMANDS
PWAC2DCB DS    CL(LENC2DCB)          DCB FOR COMMANDS
PWASYSIN DS    CL(LENSYSIN)         DCB FOR OPTIONAL EXCLUDES       @C1
PWAIREC  DS    CL80                 EXCLUDE RECORD AREA             @C1
         SPACE
         DS    0F
PWACPDCB DS    CL(LENCPDCB)         DCB FOR CHECKPOINTING U/G TABLE
         SPACE
         DS    0F
PWAUINTY DS    CL(LENUINTY)         USER ICHEINTY
PWAGINTY DS    CL(LENGINTY)         GROUP ICHEINTY
PWADINTY DS    CL(LENDINTY)         DATASET ICHEINTY
PWACINTY DS    CL(LENCINTY)         CLASS ICHEINTY
         SPACE
PWAACNT  DS    CL(LENACNT)          GET ACL COUNT ACTION
PWAAACL  DS    CL(LENAACL)          GET ACL ACTION
PWAAVOL  DS    CL(LENAVOL)          GET VOL ACTION
         SPACE
         DS    0F
PWAINDEX DS    CL(4+(12*64))        INDEX TO USER/GROUP INCORE TABLE
         SPACE
PWARWU   DS    0F
RWUSIZE  DS    F                    SIZE OF RACF-MANAGER WORKAREA
         DS    CL100                 AND THE WORKAREA ITSELF.
         ORG   PWARWU+4             GO BACK AND REDEFINE:
RWURBA   DS    CL6                   RBA RETURN AREA
RWUFLAG1 DS    X                     FLAG BYTE: (RACF 1.5)
RWUGENPR EQU   X'80'  |1... ....|      GENERIC PROFILE RETRIEVED
*        EQU   X'7F'  |.111 1111|      UNUSED
RWURSV01 DS    X                     RESERVED
RWUDUPDS DS    F                     DUPLICATE DSNAME COUNT
RWURSV02 DS    2F                    RESERVED
RWUDRLEN DS    F                     LENGTH OF RETURNED DATA
RWUFVRA  EQU   *                     FIELD VALUE RETURN AREA:
         ORG
PWARWG   DS    0F
RWGSIZE  DS    F                    SIZE OF RACF-MANAGER WORKAREA
         DS    CL100                 AND THE WORKAREA ITSELF.
         ORG   PWARWG+4             GO BACK AND REDEFINE:
RWGRBA   DS    CL6                   RBA RETURN AREA
RWGFLAG1 DS    X                     FLAG BYTE: (RACF 1.5)
RWGGENPR EQU   X'80'  |1... ....|      GENERIC PROFILE RETRIEVED
*        EQU   X'7F'  |.111 1111|      UNUSED
RWGRSV01 DS    X                     RESERVED
RWGDUPDS DS    F                     DUPLICATE DSNAME COUNT
RWGRSV02 DS    2F                    RESERVED
RWGDRLEN DS    F                     LENGTH OF RETURNED DATA
RWGFVRA  EQU   *                     FIELD VALUE RETURN AREA:
         ORG
PWALEN   EQU   *-PWA
         TITLE 'CLEANACL -- RACF MANAGER WORKAREA FOR DATASETS'
RWD      DSECT
RWDSIZE  DS    F                    SIZE OF RACF-MANAGER WORKAREA
         DS    CL32760               AND THE WORKAREA ITSELF.
         ORG   RWD+4                GO BACK AND REDEFINE:
RWDRBA   DS    CL6                   RBA RETURN AREA
RWDFLAG1 DS    X                     FLAG BYTE: (RACF 1.5)
RWDGENPR EQU   X'80'  |1... ....|      GENERIC PROFILE RETRIEVED
*        EQU   X'7F'  |.111 1111|      UNUSED
RWDRSV01 DS    X                     RESERVED
RWDDUPDS DS    F                     DUPLICATE DSNAME COUNT
RWDRSV02 DS    2F                    RESERVED
RWDDRLEN DS    F                     LENGTH OF RETURNED DATA
RWDFVRA  EQU   *                     FIELD VALUE RETURN AREA:
RWDOWNL  DS    XL2
RWDOWNER DS    CL8
RWDCNTL  DS    XL2                  LENGTH OF ACL COUNT
RWDCNT   DS    XL2                  NUMBER OF ACL ENTRIES
RWDVOLL  DS    XL2                  LENGTH OF VOLUME
RWDVOL   DS    CL6                  VOLSER
RWDACLL  DS    XL2                  LENGTH OF ALL ACL ENTRIES
RWDACL   EQU   *,11                 ACL ENTRIES START HERE:
RWDACLU  DS    CL8                    ACL USERID
RWDACLC  DS    XL2                    ACCESS COUNT
RWDACLA  DS    X                      ACCESS ALLOWED
         ORG
RWDPLLEN EQU   *-RWD                LENGTH OF RACF-MANAGER WORKAREA.
         TITLE 'CLEANACL -- GENERAL CLASS RACF MANAGER WORKAREA'
RWC      DSECT
RWCSIZE  DS    F                    SIZE OF RACF-MANAGER WORKAREA
         DS    CL32760               AND THE WORKAREA ITSELF.
         ORG   RWC+4                GO BACK AND REDEFINE:
RWCRBA   DS    CL6                   RBA RETURN AREA
RWCFLAG1 DS    X                     FLAG BYTE: (RACF 1.5)
RWCGENPR EQU   X'80'  |1... ....|      GENERIC PROFILE RETRIEVED
*        EQU   X'7F'  |.111 1111|      UNUSED
RWCRSV01 DS    X                     RESERVED
RWCDUPDS DS    F                     DUPLICATE DSNAME COUNT
RWCRSV02 DS    2F                    RESERVED
RWCDRLEN DS    F                     LENGTH OF RETURNED DATA
RWCFVRA  EQU   *                     FIELD VALUE RETURN AREA:
RWCOWNL  DS    XL2
RWCOWNER DS    CL8
RWCCNTL  DS    XL2                  LENGTH OF ACL COUNT
RWCCNT   DS    XL2                  NUMBER OF ACL ENTRIES
RWCACLL  DS    XL2                  LENGTH OF ALL ACL ENTRIES
RWCACL   EQU   *,11                 ACL ENTRIES START HERE:
RWCACLU  DS    CL8                    ACL USERID
RWCACLC  DS    XL2                    ACCESS COUNT
RWCACLA  DS    X                      ACCESS ALLOWED
         ORG
RWCPLLEN EQU   *-RWC                LENGTH OF RACF-MANAGER WORKAREA.
         TITLE 'CLEANACL -- CLASS DESCRIPTOR TABLE DSECT'
CDE      DSECT
CDELEN   DS    H         LENGTH OF THIS ENTRY, 00 IS THE END
CDECLID  DS    X         CLASS ID NUMBER
CDECLASS DS    CL8       EBCDIC CLASS NAME, LJ AND PADDED
CDERESGR DS    CL8       RESOURCE GROUP OR MAIN GROUP -
*                        SEE CDEPROCF
CDEMAXLN DS    X         MAX LENGTH OF RESOURCE NAME
CDESYNTX DS    X         SYNTAX OF FIRST NAME BYTE
CDESYNT2 DS    X         SYNTAX OF REST OF NAME
CDEUACC  DS    X         DEFAULT UACC FOR RESOURCES
CDEPROCF DS    X         PROCESS FLAGS:
CDEGROUP EQU   X'80'       THIS IS A RESOURCE GROUP
CDEACEEU EQU   X'40'       USE ACEE UACC
CDEOPERA EQU   X'20'       PAY ATTENTION TO OPERATIONS ATTR
CDECLPOS DS    F         BITMASK FOR CLAUTH, AUDIT, ETC.
         SPACE 1
         CVT   DSECT=YES
         SPACE 1
         ICHPRCVT
         END
