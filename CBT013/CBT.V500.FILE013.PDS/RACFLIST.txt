PROC 0 PREFIX(&SYSUID) PRINTER()
CONTROL NOFLUSH NOMSG
/**********************************************************************/
/* RACFLIST (LIST RACF PROTECTION)                                    */
/*                                                                    */
/* THIS CLIST WILL DETERMINE WHICH RACF PROFILES PROTECT WHICH        */
/* CATALOGED DATASETS FOR A GIVEN USERID OR GROUPID. THE RACF SEARCH  */
/* COMMAND IS USED TO LIST THE PROFILES DEFINED TO RACF. IN ORDER TO  */
/* HAVE THE SEARCH COMMAND LIST THE PROFILES, YOU MUST HAVE AT LEAST  */
/* READ PERMISSION TO THE PROFILES. THE LISTCAT COMMAND EXTRACTS      */
/* NAMES OF CATALOGED DATASETS AND WRITES THEM TO A TEMPORARY FILE.   */
/* THE NAMES OF PROFILES AND CATALOGED DATASETS ARE THEN PROCESSED IN */
/* THE PROGRAM 'LISTRP'.       S. SABEL, BCWA 5/85                    */
/*                                                                    */
/* SYNTAX -                                                           */
/* %RACFLIST PREFIX(PREFIX) PRINTER(PRINTER-ID)                       */
/*                                                                    */
/* REQUIRED - NONE                                                    */
/*                                                                    */
/* OPERANDS -                                                         */
/* PREFIX() SPECIFIES A HIGH LEVEL QUALIFIER FOR A USER OR            */
/*          GROUP THAT YOU WANT TO LIST THE RACF PROTECTION           */
/*          FOR. IF YOU DO NOT SPECIFY A PREFIX, THE                  */
/*          DEFAULT IS YOUR USERID.                                   */
/*                                                                    */
/* PRINTER() SPECIFIES A LOCAL PRINTER ID TO SEND THE OUTPUT TO       */
/*           VIA DSPRINT                                              */
/*                                                                    */
/**********************************************************************/
/* USE THE RACF SEARCH COMMAND TO WRITE THE PROFILE NAMES OUT TO THE  */
/* DATASET '&SYSUID.EXEC.RACF.CLIST'.                                 */
/**********************************************************************/
WRITE <*> EXECUTING 'RACFLIST'
WRITE     -LOADING RACF FILE FOR PREFIX '&PREFIX'.
SEARCH MASK(&PREFIX) CLIST NOLIST
SET &LCC = &LASTCC
IF &LCC = 4 THEN DO
     WRITE +THERE ARE NO RACF PROFILES DEFINED FOR '&PREFIX'
     WRITE  ANY DATASETS UNDER PREFIX '&PREFIX' ARE UNPROTECTED.

     EXIT
END
IF &LCC = 8 THEN DO
     WRITE +YOU ARE NOT AUTHORIZED TO LIST RACF
     WRITE  INFORMATION FOR PREFIX '&PREFIX'.

     EXIT
END
FREE DA('&SYSUID..EXEC.RACF.CLIST')
FREE FI(RACFIN)
ALLOC FI(RACFIN) DA('&SYSUID..EXEC.RACF.CLIST') SHR
SET &LCC = &LASTCC
IF &LCC ¬= 0 THEN DO
     WRITE +YOU ARE NOT AUTHORIZED TO LIST RACF
     WRITE  INFORMATION FOR PREFIX '&PREFIX'.

     EXIT
END
/**********************************************************************/
/* ALLOCATE THE TEMPORARY FILE 'LISTCAT' TO HOLD LISTCAT OUTPUT.      */
/**********************************************************************/
FREE FI(LISTCAT)
ALLOC FI(LISTCAT) NEW DELETE TRACKS SPACE(5 5) RECFM(V B A) +
      LRECL(125) BLKSIZE(6129) DSORG(PS) UNIT(WORK)
IF &LASTCC ¬= 0 THEN DO
     WRITE +UNABLE TO ALLOCATE LISTCAT OUTPUT FILE.
     WRITE  COMMAND TERMINATING DUE TO ERROR.

     EXIT
     END
/**********************************************************************/
/* ALLOCATE A TEMPORARY OUTPUT DATASET                                */
/**********************************************************************/
SET &D = &STR(T&SUBSTR(01:02,&SYSTIME)&SUBSTR(04:05,&SYSTIME)+
               &SUBSTR(07:08,&SYSTIME))
FREE FI(SYSPRINT)
ALLOC FI(SYSPRINT) DA('&SYSUID..RACFLIST.&D') NEW CATALOG TRACKS +
                   SPACE(2 2) RECFM(F B A) BLKSIZE(6156) LRECL(81)
IF &LASTCC ¬= 0 THEN DO
     WRITE +UNABLE TO ALLOCATE OUTPUT FILE.
     WRITE  COMMAND TERMINATING DUE TO ERRROR.
     EXIT
END
/**********************************************************************/
/* EXECUTE THE LISTCAT COMMAND TO WRITE OUT THE DATASET NAMES.        */
/**********************************************************************/
WRITE     -LOADING LISTCAT FILE FOR PREFIX '&PREFIX'.
LISTCAT LEVEL(&PREFIX) OFILE(LISTCAT)
/**********************************************************************/
/* CLEAR THE SCREEN AND EXECUTE THE PROGRAM 'LISTRP'                  */
/**********************************************************************/
WRITE     -EXECUTING PROGRAM LISTRP.
CALL 'SYSX.LINKLIB1(LISTRP)'
ISPEXEC BROWSE DATASET('&SYSUID..RACFLIST.&D')
IF &PRINTER ¬= THEN DSPRINT '&SYSUID..RACFLIST.&D' &PRINTER CCHAR NONUM
FREE FI(RACFIN LISTCAT SYSPRINT)
ALLOC FI(SYSPRINT) DA(*)
CONTROL NOFLUSH NOMSG
DELETE '&SYSUID..RACFLIST.&D'
EXIT
