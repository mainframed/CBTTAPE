ACFEXIT  TITLE 'USER EXIT - ACCESS CONTROL - RACF'
*---------------------------------------------------------------------|
* ACFEXIT - ROSCOE SIGNON EXIT                                        |
*---------------------------------------------------------------------|
***-->   USE SYS1.AMODGEN IN THE ASSEMBLY                             |
*                                                                     |
* THIS IS THE ACCESS FACILITY USER EXIT.                              |
* IT EMPLOYS R.A.C.F. AS ITS ACCESS CHECK TECHNIQUE.                  |
* COMMUNICATION WITH ROSCOE IS VIA THE PARAMETER BLOCK 'UXDSECT'      |
* ON ENTRY, R1 -> PARAMETER LIST WITH ONE ENTRY, VIZ. A(UXDSECT)      |
* THERE ARE VARIOUS TYPES OF CALLS:                                   |
* 1. VERIFICATION CALL - TO VERIFY THAT ALL THE SIGNON PARAMETERS     |
*                        ARE VALID                                    |
*    INPUT:                                                           |
*          ROSCOE KEY                                                 |
*          PASSWORD                                                   |
*          NEW PASSWORD                                               |
*          RACF GROUP CODE                                            |
*          TERMINAL NAME                                              |
*    OUTPUT:                                                          |
*          R15 - STATUS (0 => NORMAL;4 => LOOK AT R0 FOR FURTHER INFO)|
*          R0  - FURTHER DESCRIPTION OF STATUS                        |
*                VALUES AS FOR RESPONSE CODE VALUES FOR RACINIT, WITH |
*                THE ADDITION THAT A VALUE OF X'40' INDICATES THAT    |
*                THE PASSWORD HAS BEEN CHECKED, AND THAT ROSCOE       |
*                NEED NOT CHECK THE PASSWORD ITSELF (A RESPONSE FROM  |
*                R.A.C.F. OF 0 IS CONVERTED TO X'40')                 |
*                                                                     |
*    NOTE: IN ORDER TO ENSURE THAT THIS IS A VERIFY CALL, AND NO      |
*          MORE, THE MODULE ISSUES A 'RACINIT ENVIR=DELETE'           |
*          AFTER A SUCCESSFUL 'RACINIT ENVIR=CREATE'                  |
* 2. INITIALIZATION CALL - TO TAKE WHATEVER ACTION IS NECESSARY       |
*    TO SIGN THE USER ON.                                             |
*    IN THE CASE OF RACF, THE RACINIT ISSUED IN THE VERIFY CALL       |
*    MUST SIMPLY BE REPEATED                                          |
* 3. TERMINATION CALL - TO SIGN THE USER OFF FROM RACF                |
*    INPUT:                                                           |
*          ROSCOE KEY                                                 |
*    OUTPUT:                                                          |
*          NONE                                                       |
* 4. PASSWORD UPDATE CALL - TO CHANGE THE PASSWORD OF A SIGNED-ON     |
*                           USER                                      |
*    INPUT:                                                           |
*          ROSCOE KEY                                                 |
*          CURRENT PASSWORD                                           |
*          NEW PASSWORD                                               |
*    OUTPUT:                                                          |
*          R15 - STATUS (0 => NORMAL;4 => LOOK AT R0 FOR FURTHER INFO)|
*          R0  - FURTHER DESCRIPTION OF STATUS                        |
*                VALUES AS FOR REPONSE CODE VALUES FOR RACINIT        |
*                                                                     |
*  THIS EXIT HAS BEEN USED SO FAR IN RELEASES 5.3, 5.3A, 5.4, 5.4A    |
*                                                                     |
*---------------------------------------------------------------------|
         PRINT NOGEN
         ROPGMHDR ARC='¢LVNO',UPD='¢UPNO',DATE='¢DATEUPD',             X
               TIME='¢TIMEUPD',NAME='¢MODNAME',                        X
               DSN='¢DATA-SET-NAME-FOR-THE-LIBRARIAN-MASTER-FILE'
ACFEXIT  ROSUB WORK=ACWK,CLEAR=YES                              CHNGENM
         L     R8,0(R1)          POINTER TO UXDSECT
         USING UXDSECT,R8
         L     R2,UXCODE         A(CALL CODE)
         L     R2,0(R2)          CALL CODE
         B     *+4(R2)           CALL APPROPRIATE ROUTINE
         B     ROSUVER           CODE 0 VERIFY CALL
         B     ROSUINIT          CODE 4 INITIALIZE CALL
         B     ROSUTRM           CODE 8 TERMINATE CALL
         B     ROSUPASS          CODE 12 PASSWORD UPDATE CALL
         TITLE 'ROSUVER - VERIFY SIGNON PARAMETERS USING RACINIT'
ROSUVER  EQU   *
         XC    SCBACFB,SCBACFB   CLEAR A(ACEE)
         L     R2,UXACFPSW       ADDRESS OF PASSWORD
         LTR   R2,R2             PASSWORD SUPPLIED?
         BZ    ROSUV20           NO - CLEAR PRIVATE WORK AREA
         MVC   RACFPSW,0(R2)     MOVE TO PRIVATE WORK AREA
         B     ROSUV40
ROSUV20  EQU   *
         XC    RACFPSW,RACFPSW   CLEAR PASSWORD FIELD
ROSUV40  EQU   *
         L     R2,UXACFNPS       ADDRESS OF NEW PASSWORD
         LTR   R2,R2             NEW PASSWORD SUPPLIED?
         BZ    ROSUV60           NO - CLEAR PRIVATE WORK AREA
         MVC   RACFNPS,0(R2)     MOVE TO PRIVATE WORK AREA
         B     ROSUV80
ROSUV60  EQU   *
         XC    RACFNPS,RACFNPS   CLEAR NEW PASSWORD FIELD
ROSUV80  EQU   *
         BAL   R14,RACINITC      CALL SUBROUTINE - ISSUE RACINIT CREATE
         L     R15,RACFRESP      RESPONSE CODE
         LTR   R15,R15           SUCCESSFUL?
         BNZ   ROSUV100          NO - BYPASS RACINIT ENVIR=DELETE
         BAL   R14,RACINITD      GO DELETE ENVIRONMENT
ROSUV100 EQU   *
         L     R0,RACFRESP       RESTORE RACF RESPONSE CODE
         LTR   R0,R0
         BNZ   ROSUV900          PROBLEM?
         LA    R0,X'40'          TELL SIGNON NOT TO CHECK PASSWORD
         CLC   RACFKEY,UACCKEY    IS ACEE FOR UNIVERSAL ACCESS USER
         BNE   ROSUV900           IF SO
         LA    R0,X'00'              HAVE ROSCOE VALIDATE PASSWORD
ROSUV900 EQU   *
         LA    R15,4             TELL ROSCOE TO LOOK AT R0
         B     ROSEX             RETURN
         TITLE 'ROSUINIT - INITIALIZE CALL'
ROSUINIT EQU   *
         XC    SCBACFB,SCBACFB   CLEAR A(ACEE)
*  NOTE: THE PRECEDING VERIFY CALL WILL HAVE CHANGED THE PASSWORD
*        ALREADY, SO WE MUST USE THE 'NEW PASSWORD' FIELD AS THE
*        PASSWORD
         L     R2,UXACFNPS       ADDRESS OF NEW PASSWORD
         LTR   R2,R2             WAS NEW PASSWORD SUPPLIED?
         BNZ   ROSUI40           YES - NEW PASSWORD TO BE MOVED TO
*                                PASSWORD WORK AREA
         L     R2,UXACFPSW       POINT TO PASSWORD
         LTR   R2,R2             PASSWORD SUPPLIED?
         BNZ   ROSUI40           YES - MOVE IT IN
         XC    RACFPSW,RACFPSW   CLEAR PASSWORD FIELD
         B     ROSUI60
ROSUI40  EQU   *
         MVC   RACFPSW,0(R2)     SET UP PASSWORD IN WORK AREA
ROSUI60  EQU   *
         XC    RACFNPS,RACFNPS   CLEAR NEW PSSWORD FIELD REGARDLESS
         BAL   R14,RACINITC      CALL SUBROUTINE - ISSUE RACINIT CREATE
         L     R15,RACFRESP      RESPONSE CODE
         LTR   R15,R15           SUCCESSFUL?
         BZ    ROSUI70           YES - SETUP SUBMIT PARAMETERS
         LA    R15,X'04'         TELL ROSCOE TO LOOK AT R0
         LA    R0,X'04'          TELL ROSCOE TO SIGN USER OFF
         B     ROSEX             RETURN
ROSUI70  XC    SCBUSER1(12),SCBUSER1 BE SURE  SCBUSER1,2,3 ARE CLEAR
         CLC   RACFKEY(7),UACCKEY  IF  USER IS UNIVERSAL ACCESS USER
         BE    ROSEX                   JUST EXIT, ELSE
         MVC   SCBUSER1(9),RACFPSWL  SAVE RACF PASSWORD & LENGTH
         XC    SCBUSER1(12),ACCSONT+1     MASK PASSWORD (SIGNON TIME)
         B     ROSEX             AND EXIT
         TITLE 'ROSUTRM - TERMINATE CALL'
ROSUTRM  EQU   *
         XC    SCBUSER1(12),SCBUSER1 CLEAR SCBUSER1,2,3
ROSUT10  EQU   *
         BAL   R14,RACINITD      GO DELETE ACEE
         B     ROSEX             RETURN
         TITLE 'ROSUPASS - PASSWORD UPDATE CALL'
ROSUPASS EQU   *
         BAL   R14,RACINITD      GO DELETE OLD ACEE
         L     R2,UXACFPSW       ADDRESS OF PASSWORD
         LTR   R2,R2             CURRENT PASSWORD SUPPLIED?
         BNZ   ROSUP20           YES - GO SET UP
         XC    RACFPSW,RACFPSW   CLEAR CURRENT PASSWORD FIELD
         B     ROSUP40
ROSUP20  EQU   *
         MVC   RACFPSW,0(R2)     SET UP PASSWORD IN WORK AREA
ROSUP40  EQU   *
         L     R2,UXACFNPS       ADDRESS OF NEW PASSWORD
         LTR   R2,R2             NEW PASSWORD SUPPLIED?
         BNZ   ROSUP60           YES - GO SET UP
         MVC   RACFNPS,=C'        ' CLEAR CURRENT PASSWORD FIELD
         B     ROSUP80
ROSUP60  EQU   *
         MVC   RACFNPS,0(R2)     SET UP PASSWORD IN WORK AREA
ROSUP80  EQU   *
         BAL   R14,RACINITC      CALL SUBROUTINE - ISSUE RACINIT CREATE
         L     R15,RACFRESP      RESPONSE CODE
         LTR   R15,R15           SUCCESSFUL?
         BZ    ROSEX             YES - RETURN WITH R15 = 0
         L     R2,UXACFPSW       ADDRESS OF PASSWORD
         MVC   RACFPSW,0(R2)     SET UP PASSWORD IN WORK AREA
         XC    RACFNPS,RACFNPS   CLEAR NEW PASSWORD
         BAL   R14,RACINITC      CALL SUBROUTINE - ISSUE RACINIT CREATE
         LTR   R15,R15           SUCCESSFUL?
         BZ    ROSUP200          JUST RETURN BAD STATUS
         LA    R15,X'03'         RETURN JUNK STATUS - BOMB
         B     ROSEX             RETURN
ROSUP200 EQU   *
         LA    R15,X'04'         TELL ROSCOE TO LOOK AT R0
         LA    R0,X'04'          RESPONSE - NEW PASSWORD INVALID
         B     ROSEX             RETURN
         TITLE 'SUBROUTINE TO EXECUTE RACINIT  ENVIR=CREATE'
RACINITC EQU   *
         ST    R14,SAVER14      SAVE RETURN ADDRESS
         LA    R1,ACCNUM         A(ROSCOE KEY)                  EXITFIX
         MVC   RACFKEY,0(R1)     SET UP KEY FROM PARAMETER AREA
         LA    R1,6(R1)          R1 -> LAST CHAR. OF KEY
         LA    R2,7              COUNTER FOR LENGTH
RACCLP1  EQU   *
         CLC   0(1,R1),BLANK     END OF FIELD?
         BNE   RACCKEYF          YES
         CH    R2,=H'1'          LOOKED AT WHOLE KEY?
         BE    RACCKEYF          YES
         BCTR  R2,0              SUBTRACT 1
         BCTR  R1,0              SUBTRACT 1 FROM KEY POINTER
         B     RACCLP1           LOOK AT PREVIOUS CHAR.
RACCKEYF EQU   *
         STC   R2,RACFKEYL       ESTABLISHED LENGTH
         LA    R1,RACFPSW+7      R1 -> LAST CHAR. OF PSWD
         LA    R2,8              COUNTER FOR LENGTH
RACCLP2  EQU   *
         CLC   0(1,R1),BLANK     END OF FIELD?
         BNE   RACCPSWF          YES
         CH    R2,=H'1'          LOOKED AT WHOLE PSWD?
         BE    RACCPSWF          YES
         BCTR  R2,0              SUBTRACT 1
         BCTR  R1,0              SUBTRACT 1 FROM PSWD POINTER
         B     RACCLP2           LOOK AT PREVIOUS CHAR.
RACCPSWF EQU   *
         STC   R2,RACFPSWL       ESTABLISHED LENGTH
         LA    R1,RACFNPS+7      R1 -> LAST CHAR. OF NPSW
         LA    R2,8              COUNTER FOR LENGTH
RACCLP3  EQU   *
         CLC   0(1,R1),BLANK     END OF FIELD?
         BNE   RACCNPSF          YES
         CH    R2,=H'1'          LOOKED AT WHOLE NPSW?
         BE    RACCNPSF          YES
         BCTR  R2,0              SUBTRACT 1
         BCTR  R1,0              SUBTRACT 1 FROM NPSW POINTER
         B     RACCLP3           LOOK AT PREVIOUS CHAR.
RACCNPSF EQU   *
         STC   R2,RACFNPSL       ESTABLISHED LENGTH
         L     R1,UXACFGRP       A(GROUP CODE)
         LTR   R1,R1             GROUP CODE SUPPLIED?
         BZ    RACF00            BYPASS GROUP SCAN
         MVC   RACFGRP,0(R1)     SET UP GROUP FROM PARAMETER AREA
         LA    R1,7(R1)          R1 -> LAST CHAR. OF GROUP
         LA    R2,8              COUNTER FOR LENGTH
RACCLP4  EQU   *
         CLC   0(1,R1),BLANK     END OF FIELD?
         BNE   RACCGRPF          YES
         CH    R2,=H'1'          LOOKED AT WHOLE GROUP?
         BE    RACCGRPF          YES
         BCTR  R2,0              SUBTRACT 1
         BCTR  R1,0              SUBTRACT 1 FROM GROUP POINTER
         B     RACCLP4           LOOK AT PREVIOUS CHAR.
RACCGRPF EQU   *
         STC   R2,RACFGRPL       ESTABLISHED LENGTH
RACF00   EQU   *
         LA    R7,RACFPSWL       CURRENT PASSWORD - LTH + FIELD
         CLC   0(4,R7),=X'00000000' PASSWORD SUPPLIED
         BNZ   RACF10            BYPASS CLEAR
         SLR   R7,R7
RACF10   EQU   *
         LA    R5,RACFNPSL       POINTER TO NEW PASSWORD
         CLC   RACFNPS(4),=X'00000000' NEW PASSWORD SUPPLIED?
         BNE   RACF20            BYPASS CLEAR
         SLR   R5,R5             CLEAR ADDRESS OF NEW PASSWORD
RACF20   EQU   *
         L     R6,UXACFGRP       ADDRESS OF GROUP CODE
         LTR   R6,R6             GROUP CODE SUPPLIED?
         BNZ   RACF25            BYPASS CLEAR
         SLR   R6,R6             CLEAR GROUP POINTER
         B     RACF30
RACF25   EQU   *
         LA    R6,RACFGRPL       POINTER TO GROUP CODE + LENGTH
RACF30   EQU   *
*
*    NOTE: FOR A RACINIT CALL SPECIFYING A TERMID PARAMETER, THE
*          TERMINAL NAME MUST RESIDE IN A "NON TASK-RELATED STORAGE
*          SUBPOOL". THEREFORE ISSUE A GETMAIN FOR MEMORY
*          FROM SUBPOOL 241, AND SET UP THE TERMINAL NAME THERE.
*
         MODESET MF=(E,SUPSVSL)  INTO KEY ZERO
         LA    R2,8              NEED 8 BYTES OF STORAGE
         LA    R3,RACFTERM       ADDRESS TO STORE A(ALLOCATED STORAGE)
         XC    RACEXEC,RACEXEC   CLEAR EXECUTE FORM WORK AREA
         LA    R1,RACEXEC        POINT TO EXECUTE FORM WORK AREA
         GETMAIN EC,                                                   X
               LV=(R2),                                                X
               SP=241,                                                 X
               A=(R3)
         B     *+4(R15)          INTERPRET RESPONSE CODE
         B     RACC70X           GOOD RESPONSE - GO DO RACINIT
         NOP   *                 SAME ACTION AS NEXT RESPONSE
         B     RACC99X           RETURN TO PROBLEM STATE & RETURN
RACC70X  EQU   *
         L     R2,RACFTERM       GET ADDRESS OF ALLOCATED STORAGE
         L     R1,UXACFTRM       ADDRESS OF TERMINAL NAME
         LTR   R1,R1             TERMINAL NAME SUPPLIED
         BNZ   RACC80X           YES - SET UP TERMINAL NAME
         SLR   R2,R2             CLEAR TERMINAL PARAMETER
         B     RACC85X           BYPASS TERMINAL NAME MOVE
RACC80X  EQU   *
         MVC   0(8,R2),0(R1)     SET UP TERM. NAME IN NON-TASK STORAGE
RACC85X  EQU   *
         LA    R4,RACEXEC        EXECUTE FORM WORK AREA
         MVC   0(RACFILEN,R4),RACFINT MOVE IN LIST FORM SKELETON
         SPACE 1
         PRINT GEN
         RACINIT ENVIR=CREATE,                                         X
               USERID=RACFKEYL,                                        X
               PASSWRD=(R7),                                           X
               NEWPASS=(R5),                                           X
               GROUP=(R6),                                             X
               ACEE=SCBACFB,                                           X
               TERMID=(R2),                                            X
               MF=(E,(4))
         CH    R15,=H'04'           CHECK FOR UNKNOWN TO RACF
         BNE   RACC90X          IF KNOWN, CARRY ON
         SLR   R5,R5             ELSE
         SLR   R6,R6            INIT FOR ROSUSER/ROSU
         LA    R7,RACFPSWL      R7 ---> PAWSSWORD LENGTH
         MVC   RACFKEYL(17),UACCKEYL  MOVE IN ROUUSER,ROSU
         B     RACC85X          BRANCH BACK TO TRY AGAIN
UACCKEYL DC    AL1(7)           LENGTH OF USER ID
UACCKEY  DC    CL7'ROSUSER'     USER ID (DEFAULT)
UACCPSWL DC    AL1(5)           LENGTH OF PASSWORD
UACCPSW  DC    CL5'ROSUS'       PASSWORD FOR ROSUSER
         DC    XL3'000000'      FILLER FOR 17 BYTES
         DC    XL1'00'          FILLER FOR HALFWORD ALIGNMENT
RACC90X  EQU   *                RESUME "NORMAL" PROCESSING
         ST    R15,RACFRESP      SAVE RETURN CODE FROM RACINIT
         L     R2,RACFTERM       GET ADDRESS OF ALLOCATED STORAGE
         FREEMAIN RC,                                                  X
               LV=8,                                                   X
               A=(R2),                                                 X
               SP=241,
         PRINT NOGEN
         B     *+4(R15)          INTERPRET RESPONSE CODE
         B     RACC100X          GOOD RESPONSE - CONTINUE
RACC99X  EQU   *                 PROBLEM WITH SOME MACRO CALL
         LA    R0,X'03'          BAD RESPONSE CODE
         ST    R0,RACFRESP       SAVE IN RESPONSE AREA
RACC100X EQU   *
         MODESET MF=(E,PROBSVSL) RETURN TO PROBLEM PROGRAM STATE
         L     R14,SAVER14       RESTORE RETURN ADDRESS
         BR    R14               RETURN
         TITLE 'SUBROUTINE TO EXECUTE RACINIT ENVIR=DELETE'
RACINITD EQU   *
         ST    R14,SAVER14      SAVE RETURN ADDRESS
         L     R7,SCBACFB        A(ACEE)
         LTR   R7,R7             HAS AN ACEE BEEN ALLOCATED?
         BZ    ROSUF20           NO, EXIT
         LA    R4,RACEXEC        R4 --> EXECUTE FORM WORK AREA
         MVC   0(RACFILEN,R4),RACFINT SET RACF LIST
         MODESET MF=(E,SUPSVSL)  INTO KEY ZERO
         SPACE 1
         PRINT GEN
         RACINIT ENVIR=DELETE,                                         X
               ACEE=SCBACFB,                                           X
               MF=(E,(4))
         PRINT NOGEN
         SPACE 3
         MODESET MF=(E,PROBSVSL)
ROSUF20  EQU   *
         SLR   R15,R15           RETURN GOOD STATUS REGARDLESS
         L     R14,SAVER14       RESTORE RETURN ADDRESS
         BR    R14               RETURN
         TITLE 'ROSEX - COMMON EXIT POINT'
ROSEX    EQU   *
         SLR   R1,R1             TELL ROSCOE TO USE DEFAULT MESSAGES
         L     R2,4(,R13)        GET PREVIOUS SAVE AREA ADDRESS
         ST    R1,24(R2)         PREPARE FOR LM R1,R12
         ROSEX DROP=NO
         TITLE 'DATA AREAS'
SUPSVSL  MODESET KEY=ZERO,MF=L
PROBSVSL MODESET KEY=NZERO,MF=L
RACFINT  DC    0F'0'
         RACINIT MF=L
RACFILEN EQU   *-RACFINT
         PRINT NOGEN
         TITLE 'USER ACCESS FACILITY EXIT - RACF - DATA AREAS'
*---------------------------------------------------------------------*
*        PATCH AREA                                                   *
*---------------------------------------------------------------------*
         ROPATCH
*---------------------------------------------------------------------*
*        WORKAREA                                                     *
*---------------------------------------------------------------------*
         ROWRK
RACFRESP DS    F                 SAVE AREA FOR RESPONSE CODE
RACFTERM DS    F                 ADDRESS OF STORAGE FROM GETMAIN
SAVER14  DS    F                 SAVE AREA FOR R14
         DS    0D
RACEXEC  DS    XL(RACFILEN)        EXECUTE FORM FOR RACINIT
RACFKEYL DS    X                   LENGTH OF USER ID
RACFKEY  DS    CL7                 USER ID
RACFPSWL DS    X                   LENGTH OF PASSWORD
RACFPSW  DS    CL8                 PASSWORD
RACFNPSL DS    X                   LENGTH OF NEW PASSWORD
RACFNPS  DS    CL8                 NEW PASSWORD
RACFGRPL DS    X                   LENGTH OF GROUP
RACFGRP  DS    CL8                 GROUP
         ROWRKEND
         PRINT GEN
         UXDSECT EXIT=ACF
         ROPGMEND
