*          DATA SET RACRIX01   AT LEVEL 005 AS OF 06/11/82
ICHRIX01 TITLE 'RACINIT PRE-PROCESSING EXIT'
*---------------------------------------------------------------------*
*                                                                     *
*  THE RACINIT PRE-PROCESSING EXIT GETS CONTROL BEFORE THE ACEE HAS   *
*  BEEN BUILT FOR THE USER. IT HAS THE OPPORTUNITY TO CONSTRUCT A     *
*  USERID OR EFFECT SUCH OPTIONS AS BYPASSING PASSWORD CHECKING.      *
*                                                                     *
*  THIS EXIT UTILIZES THE FOLLOWING LOGIC:                            *
*                                                                     *
*      IF THE USER IS A STARTED TASK, BUILD THE USERID FROM THE       *
*      FIRST 7 CHARACTERS OF THE STC NAME AND EXIT.                   *
*                                                                     *
*      IF THE USER IS A TSU WITH NO USERID, FAIL RACINIT.             *
*                                                                     *
*      IF THE JOB IS TESTING WITH NO USERID, FAIL RACINIT.            *
*                                                                     *
*      IF THERE IS A RACF USERID AND THERE IS NO RACF PASSWORD THEN   *
*         BYPASS PASSWORD VERIFICATION AND EXIT.                      *
*                                                                     *
*      LOOK UP THE MAJOR ACCOUNTING CODE IN THE ACCOUNT LOOKUP TABLE, *
*      LOAD THE ADDRESS OF THE APPROPRIATE PROCESSING ROUTINE FOR     *
*      THAT MAJOR ACCOUNT AND BRANCH TO IT.                           *
*                                                                     *
*      REGISTERS ARE AS FOLLOWS:                                      *
*                                                                     *
*           R0  =    WORK                                             *
*           R1  =    WORK                                             *
*           R2  -->  STC/BATCH JOBNAME                                *
*           R3  -->  ACCOUNTING INFORMATION                           *
*           R4  -->  RACF USERID (OR BLANK)                           *
*           R5  -->  RACF FLAG BYTE ONE                               *
*           R6  =    WORK                                             *
*           R7  =    WORK                                             *
*           R8  =    WORK                                             *
*           R9  =    WORK                                             *
*           R10 =    WORK                                             *
*           R11 -->  PARAMETER LIST                                   *
*           R12 =    MODULE BASE REGISTER                             *
*           R13 -->  SAVE AREA                                        *
*           R14 =    RETURN REGISTER                                  *
*           R15 =    RETURN CODE REGISTER                             *
*                                                                     *
*        RETURN CODES ARE AS FOLLOWS:                                 *
*                                                                     *
*           00  NORMAL RETURN CONTINUE PROCESSING                     *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
ICHRIX01 CSECT
         SPACE
         STM   R14,R12,12(R13)         * SAVE CALLER'S REGS
         LR    R12,R15                 * CHANGE MODULE BASE REG
         USING ICHRIX01,R12            * SET ADDRESSABILITY
         B     START                   * SKIP OVER CONSTANTS
         SPACE
         DC    C'ICHRIX01'             * CSECT NAME
         DC    C'&SYSDATE'             * DATE ASSEMBLED
         DC    C'&SYSTIME'             * TIME ASSEMBLED
         SPACE
START    LR    R11,R1                  * SAVE PARM REG
         USING RPPL,R11                * SET ADDRESSABILITY
         EJECT
         L     R1,RPPLFLG1             * R1 ---> ACEE FLAG 1
         TM    0(R1),X'C0'             * ENVIRON=CHANGE|DELETE?
         BNZ   EXIT                    *    YES..EXIT
         SPACE
         L     R2,RPPLSTCN             * R2 ---> STARTED TASK NAME
         CLI   0(R2),C' '              * STC NAME BLANK?
         BNE   STC                     *    NO...PROCESS STC
         SPACE
         L     R2,RPPLJOBN             * R2 ---> BATCH JOBNAME
         CLI   0(R2),C' '              * BATCH JOB NAME BLANK?
         BE    TSU                     *    YES..PROCESS TSU
         SPACE
         L     R3,RPPLACCT             * R3 ---> ACCOUNTING DATA
         LTR   R3,R3                   * IS THERE ANY?
         BZ    TSU                     *    NO...PROCESS AS TSU
         SPACE
         CLI   0(R3),X'02'             * IS IT AT LEAST 2 FIELDS
         BL    TSU                     *    NO...PROCESS AS TSU
         SPACE
         SR    R7,R7                   * RESET WORK REGISTER
         LA    R3,1(R3)                * R3 --> FIRST FIELD LENGTH
         IC    R7,0(R3)                * R7 = LENGTH OF FIRST FIELD
         SPACE
         LA    R3,1(R3)                * R3 --> FIRST FIELD DATA
         LR    R4,R3                   * R4 --> FIRST FIELD DATA
         AR    R4,R7                   * R4 --> SECOND FIELD LENGTH
         LA    R4,1(R4)                * R4 --> SECOND FIELD DATA
         CLI   0(R4),C'S'              * IS THIS TSO SUBMIT?
         BE    TESTING                 *    YES..PROCESS AS TESTING
         SPACE
         CLI   0(R4),C'T'              * IS THIS TEST JOB?
         BE    TESTING                 *    YES..PROCESS AS TESTING
         SPACE
         L     R4,RPPLUSRI             * R4 --> RACF USERID
         CLI   0(R4),X'00'             * IS THERE ONE?
         BNE   SETPW                   *    YES..GO TO PW BYPASS
         TITLE 'LOOK UP ACCOUNT AND BRANCH TO USERID BUILD ROUTINE'
         LA    R8,ACCTTBL              * ADDRESS OF ACCOUNT TABLE
         SPACE
ACCTLOOP CLC   0(4,R8),TBLEND          * END OF TABLE?
         BE    MATCH                   *    YES..DO NOT PROCESS
         SPACE
         CLC   0(4,R8),0(R3)           * ACCOUNT = TABLE?
         BE    MATCH                   *    YES..PROCESS
         SPACE
         LA    R8,8(R8)                * UPDATE POINTER
         B     ACCTLOOP                * AND LOOP
         SPACE 2
MATCH    L     R8,4(R8)                * LOAD ROUTINE ADDRESS
         BR    R8                      * AND BRANCH TO IT
         TITLE 'PROCESS STARTED TASKS'
*---------------------------------------------------------------------*
*                                                                     *
*        USERID = SUBSTR(STCNAME,1,7)                                 *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
STC      L     R4,RPPLUSRI             * R4 ---> RACF USERID
         MVC   1(7,R4),0(R2)           * MOVE STCNAME TO USERID
         LA    R5,1(R4)                * POINT TO ACTUAL USERID
         LA    R7,7                    * SET USERID MAX LENGTH
         LA    R8,7(R5)                * POINT TO END OF USERID
         SPACE
STCLOOP  CLI   0(R8),C' '              * BLANK?
         BNE   STCLEN                  *   NO...END OF USERID
         SPACE
         BCTR  R8,0                    * BUMP POINTER
         BCT   R7,STCLOOP              * AND LOOP
         SPACE
STCLEN   SR    R8,R5                   * COMPUTE USERID LENGTH
         LA    R8,1(R8)                * PLUS ONE
         STC   R8,0(R4)                * STORE INTO PARM LIST
         SPACE
         B     SETPW                   * AND EXIT
         TITLE 'PROCESS TSO USERS'
*---------------------------------------------------------------------*
*                                                                     *
*        FAIL RACINIT IF USERID NOT PRESENT                           *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
TSU      L     R4,RPPLUSRI             * R4 ---> RACF USERID
         CLI   0(R4),X'00'             * USERID MISSING?
         BNE   EXIT                    *    NO...EXIT
         TPUT  NOUSERID,72             * 'USER NOT IDENTIFIED TO RACF'
         SPACE
         LA    R15,4                   * FAIL RACINIT
         B     RETURN                  *
         TITLE 'PROCESS TESTING JOBS'
*---------------------------------------------------------------------*
*                                                                     *
*        FAIL RACINIT IF USERID NOT PRESENT                           *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
TESTING  L     R4,RPPLUSRI             * R4 ---> RACF USERID
         CLI   0(R4),X'00'             * USERID MISSING?
         BNE   EXIT                    *    NO...EXIT
         WTO   'ICHRIX01 - RACF USERID NOT SPECIFIED',ROUTCDE=(9,11)
         SPACE
         LA    R15,4                   * FAIL RACINIT
         B     RETURN                  *
         TITLE 'PROCESS HNB PRODUCTION JOBS'
*---------------------------------------------------------------------*
*                                                                     *
*        IF NO USERID THEN USERID = SUBSTR(JOBNAME,1,7)               *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
HNBPROD  L     R2,RPPLJOBN             * R2 ---> BATCH JOB NAME
         L     R4,RPPLUSRI             * R4 ---> RACF USERID
         MVC   1(7,R4),0(R2)           * MOVE JOBNAME TO USERID
         LA    R5,1(R4)                * POINT TO ACTUAL USERID
         LA    R7,7                    * SET USERID MAX LENGTH
         LA    R8,7(R5)                * POINT TO END OF USERID
         SPACE
UIDLOOP  CLI   0(R8),C' '              * BLANK?
         BNE   SETLEN                  *   NO...END OF USERID
         SPACE
         BCTR  R8,0                    * BUMP POINTER
         BCT   R7,UIDLOOP              * AND LOOP
         SPACE
SETLEN   SR    R8,R5                   * COMPUTE USERID LENGTH
         LA    R8,1(R8)                * PLUS ONE FOR REAL LENGTH
         STC   R8,0(R4)                * STORE INTO PARM LIST
         SPACE
HNBEXIT  B     SETPW                   * SET BYPASS PASSWORD
         TITLE 'PROCESS LEE ENTERPRISES JOBS'
*---------------------------------------------------------------------*
*                                                                     *
*        IF NO USERID THEN USERID = SUBSTR(JOBNAME,1,2)||'RJE'        *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
LEE      L     R4,RPPLUSRI             * R4 -> RACF USERID
         SPACE
         MVC   1(2,R4),0(R2)           * FIRST 2 BYTES OF JOBNAME
         MVC   3(3,R4),CRJE            * PLUS 'RJE'
         MVI   0(R4),X'05'             * USERID LENGTH IS FIVE (5)
         SPACE
         B     SETPW                   * SET BYPASS PASSWORD
         TITLE 'PROCESS GENERAL RJE USERS'
*---------------------------------------------------------------------*
*                                                                     *
*        IF NO USERID THEN USERID = SUBSTR(JOBNAME,1,3)||MAJORACCT    *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
RJE      L     R4,RPPLUSRI             * R4 -> RACF USERID
         SPACE
         MVC   1(3,R4),0(R2)           * FIRST 3 BYTES OF JOBNAME
         MVC   4(4,R4),0(R3)           * PLUS MAJOR ACCOUNT NUMBER
         MVI   0(R4),X'07'             * USERID LENGTH IS SEVEN (7)
         SPACE
         B     SETPW                   * SET BYPASS PASSWORD
         TITLE 'SET PASSWORD BYPASS FOR PRODUCTION, RJE, STCS'
SETPW    L     R5,RPPLPSWD             * ADDRESS OF USER PASSWORD
         SPACE
         CLI   0(R5),X'00'             * PASSWORD SUPPLIED?
         BNE   EXIT                    *   YES...EXIT
         SPACE
NOPW     L     R5,RPPLFLG1             * RACF FLAG BYTE ONE
         OI    0(R5),X'08'             * SET BYPASS PASSWORD PROTECTION
         SPACE
EXIT     SR    R15,R15                 * RC=0 (CONTINUE PROCESSING)
RETURN   L     R14,12(,R13)            * RESTORE R14
         LM    R0,R12,20(R13)          * RESTORE R0-R12
         BR    R14                     * RETURN TO CALLER
         TITLE 'CONSTANTS AND STORAGE'
CRJE     DC    CL3'RJE'
NOUSERID DC    CL72'ICHRIX01 - USERID NOT INDENTIFIED TO RACF'
         SPACE
         REGEQ
         SPACE
         LTORG
         TITLE 'ACCOUNT PROCESSING TABLE'
*---------------------------------------------------------------------*
*                                                                     *
*        EACH ENTRY IN THIS TABLE INDICATES THE TYPE OF RACINIT       *
*        PROCESSING TO BE PERFORMED FOR THAT MAJOR ACCOUNT NUMBER.    *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
ACCTTBL  DS   0F
         DC    CL4'9999',AL4(HNBPROD)  * HNB PRODUCTION
         DC    CL4'1002',AL4(LEE)      * LEE ENTERPRISES
         DC    CL4'1003',AL4(LEE)      * LEE ENTERPRISES
         DC    CL4'1004',AL4(RJE)      * UCC SOFTWARE
         DC    CL4'1006',AL4(RJE)      * MIDDLETOWN RJE
         DC    CL4'1009',AL4(RJE)      * CEM RJE
         DC    CL4'1010',AL4(RJE)      * RPD RJE
         DC    CL4'7777',AL4(HNBPROD)  * VNB PRODUCTION
TBLEND   DC    XL4'FFFFFFFF',AL4(EXIT) * DEFAULT = NO PROCESSING
         TITLE 'RACINIT PRE/POST PROCESSING PARAMETER LIST'
RPPL     DSECT
         SPACE
RPPLFCNT DS    A                       * ADDR(NUMBER OF WORDS IN LIST)
RPPLFLG1 DS    A                       * ADDR(FLAGE BYTE 1)
RPPLUSRI DS    A                       * ADDR(USERID)
RPPLPSWD DS    A                       * ADDR(PASSWORD)
RPPLSTCN DS    A                       * ADDR(STARTED TASK PROC NAME)
RPPLINST DS    A                       * ADDR(INSTALLATION PARMS)
RPPLGRP  DS    A                       * ADDR(GROUP NAME)
RPPLNPSW DS    A                       * ADDR(NEW PASSWORD)
RPPLACE1 DS    A                       * ADDR(ACEE FOR POST PROCESSING)
RPPLPGMN DS    A                       * ADDR(PROGRAMMER NAME)
RPPLACCT DS    A                       * ADDR(ACCOUNT INFORMTION)
RPPLOIDC DS    A                       * ADDR(OIDCARD IDENTIFICATION #)
RPPLTERM DS    A                       * ADDR(TERMINAL IDENTIFIER)
RPPLWORK DS    A                       * ADDR(FULLWORD WORK AREA)
RPPLABND DS    A                       * ADDR(ABEND CODE TO BE ISSUED)
RPPLRTRN DS    A                       * ADDR(RETURN CODE TO RACINIT)
RPPLFLG2 DS    A                       * ADDR(FLAG BYTE 2)
RPPLTRMD DS    A                       * ADDR(TERMINAL DATA FIELD)
RPPLUSDA DS    A                       * ADDR(INSTALLATION DATA)
RPPLTRML DS    A                       * ADDR(TERMINAL LEVEL NUMBER)
RPPLJOBN DS    A                       * ADDR(JOB NAME)
RPPLAPPL DS    A                       * ADDR(APPLICATION NAME)
RPPLSUBP DS    A                       * ADDR(SUBPOOL FOR ACEE)
RPPLACE2 DS    A                       * ADDR(ACEE)
RPPLAPLD DS    A                       * ADDR(APPLICATION DATA)
RPPLAPLL DS    A                       * ADDR(APPLICATION LEVEL)
RPPLPWIN DS    A                       * ADDR(PASSWORD INTERVAL)
RPPLPWDT DS    A                       * ADDR(PASSWORD DATE)
         SPACE 2
         END
