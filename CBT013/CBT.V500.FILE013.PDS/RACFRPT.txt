* //*******************************************************************
* //*
* //* RACFRPT PROVIDES A SYNOPSIS OF THE FOLLOWING INFORMATION:
* //*
* //*     FIELD                                DESCRIPTION
* //*  =============================================================
* //*  1. USER/JOB                       INTERACTIVE USERID OR
* //*                                    BATCH JOB CARD NAME
* //*
* //*  2. GRP/STEP                       RACF CONNECTED GROUP OR
* //*                                    JOB STEP NAME
* //*
* //*  3. DATE                           DATE EVENT OCCURED
* //*
* //*  4. TIME                           TIME EVENT OCCURED
* //*
* //*  5. DATA SET NAME / RACF RESOURCE  DATA SET OR PROFILE NAME
* //*
* //*  6. VOLID                          VOLUME WHERE RESOURCE RESIDES
* //*
* //*  7. TERMINAL ID                    NETWORK NODE ID
* //*
* //*  8. EVENT FLAG                     V = VIOLATIONS
* //*                                    W = WARNING
* //*                                    E = ERROR
* //*
* //*  9. EVENT DESCRIPTION              TEXT DESCRIPTION OF THE EVENT
* //*
* //* 10. SYSTEM ID                      JES2 SMF SYSTEM IDENTIFICATION
* //*
* //*******************************************************************
* //*
* //* REPORT CONTROL CARD OPTIONS:
* //*
* //* - SELECTION BY GROUPS
* //* - SELECTION BY USERS
* //* - SELECTION BY DATES
* //* - SELECTION BY TIMES
* //* - SELECTION BY EVENT(S)
* //* - SELECTION BY FULL OR PARTIAL DATA SET NAME
* //*                     AND OR RACF PROFILE NAME
* //*
* //* - SUPPRESSION BY FULL OR PARTIAL DATA SET NAME
* //*                      AND OR RACF PROFILE NAME
* //*
* //* - SORTING BY SYSID, GROUP, USER, EVENT, DATE AND TIME
* //*
* //*******************************************************************
* //*
* //* CONTROL CARDS:
* //*
* //*    ALL CONTROL CARDS ARE OPTIONAL.
* //*
* //*    THERE IS A MINIMUM OF ERROR CHECKING DONE ON CONTROL CARD
* //*    SYNTAX.  IF INVALID OPTIONS OR FORMATS ARE USED THE RESULTS
* //*    ARE UNPREDICTABLE.
* //*
* //*    THE RECORD SELECTION/SUPPRESSION HIERARCHY IS AS FOLLOWS:
* //*
* //*          *------------------------------*
* //*          *  ALL RECORDS (SEE SUPR CARD) *
* //*          *------------------------------*
* //*          * EVENT (TYPE / GROUP NUMBER)  *
* //*          *------------------------------*
* //*          *          DATASET             *
* //*          *------------------------------*
* //*          *           GROUP              *
* //*          *------------------------------*
* //*          *        USER / JOB            *
* //*          *------------------------------*
* //*          *           DATES              *
* //*          *------------------------------*
* //*          *           TIMES              *
* //*          *------------------------------*
* //*
* //* GROUPS  - SELECTIONS MAY BE MADE ON SPECIFIC GROUPS BY USING ONE
* //*           MORE GROUP CONTROL CARDS.  GROUP NAMES SHOULD BEGIN
* //*           IN CARD COLUMN 10 AND END ON OR BEFOR CARD COLUMN 70
* //*           SEPERATED BY ONE BLANK.  IF NO GROUP CONTROL CARD OR
* //*           GROUP NAMES ARE FOUND THEN NO FILTERING WILL BE DONE
* //*           BASED ON THE GROUP. THE FORMAT FOR THIS CARD IS:
* //*
* //*                    1         2         3         4         5
* //*           |---5----0----5----0----5----0----5----0----5----0
* //*           GROUPS   GROUPID1 GROUPID2 GROUPID3 ... GROUPIDX
* //*
* //*           GROUPS                  ( SELECT ALL GROUPS )
* //*           GROUPS   SYSTEMS TEST   ( SELECT ONLY GROUPS
* //*                                     SYSTEMS AND TEST )
* //*
* //* USERS   - SELECTIONS MAY BE MADE ON SPECIFIC USERS BY USING ONE
* //*           MORE USER CONTROL CARDS.  USERIDS SHOULD BEGIN IN
* //*           CARD COLUMN 10 AND END ON OR BEFOR CARD COLUMN 70
* //*           SEPERATED BY ONE BLANK.  IF NO USER CONTROL CARD OR
* //*           USERIDS ARE FOUND THEN NO FILTERING WILL BE DONE
* //*           BASED ON THE USERID. ( THIS OPTION NOT AVAILABLE IF
* //*           THE "JOB" PARM IS USED ). THE FORMAT FOR THIS CARD IS:
* //*
* //*                    1         2         3         4         5
* //*           |---5----0----5----0----5----0----5----0----5----0
* //*           USERS    USER01 USER02 USER03 USER04 ... USERXX
* //*
* //*           USERS                  ( SELECT ALL USERS )
* //*           USERS    SYS01 DEV01   ( SELECT ONLY SYS01 AND
* //*                                    DEV01 )
* //*
* //* JOBS    - SELECTIONS MAY BE MADE ON SPECIFIC JOBNAME BY USING
* //*           ONE OR MORE USER CONTROL CARDS.  JOB NAMES SHOULD BEGIN
* //*           IN CARD COLUMN 10 AND END ON OR BEFOR CARD COLUMN 70
* //*           SEPERATED BY ONE BLANK.  IF NO JOBS CONTROL CARD OR
* //*           JOBNAMES ARE FOUND THEN NO FILTERING WILL BE DONE
* //*           BASED ON THE JOBNAME. ( THIS OPTION IS ONLY AVAILABLE
* //*           IF THE "JOB" PARM IS USED ).
* //*           THE FORMAT FOR THIS CARD IS:
* //*
* //*                    1         2         3         4         5
* //*           |---5----0----5----0----5----0----5----0----5----0
* //*           JOBS     JOBNAME1 JOBNAME2 JOBNAME3 ... JOBNAMEX
* //*
* //*           JOBS                    ( SELECT ALL JOBS )
* //*           JOBS     CICS BATCH1    ( SELECT ONLY CICS AND BATCH1 )
* //*
* //* DATES   - SELECTION CAN BE LIMITED TO A SPECIFIC RANGE.
* //*           IF A DATES CONTROL CARD IS NOT FOUND OR CONTAINS
* //*           A BLANK IN CARD COLUMN 10, NO FILTERING WILL
* //*           BE DONE BASE ON THE DATE.  THE FORMAT FOR THIS
* //*           CARD IS:
* //*
* //*                    1         2         3         4         5
* //*           |---5----0----5----0----5----0----5----0----5----0
* //*           DATES                            ( SELECT ALL DATES )
* //*
* //*           DATES    01/01/86-12/31/86       ( SELECT A RANGE
* //*                      (LOW)   (HIGH)          OF DATES )
* //*
* //* TIMES   - SELECTION CAN BE LIMITED TO A SPECIFIC RANGE.
* //*           IF A TIMES CONTROL CARD IS NOT FOUND OR CONTAINS
* //*           A BLANK IN CARD COLUMN 10, NO FILTERING WILL
* //*           BE DONE BASED ON TIME.  THE FORMAT FOR THIS CARD IS:
* //*
* //*                    1         2         3         4         5
* //*           |---5----0----5----0----5----0----5----0----5----0
* //*           TIMES                            ( SELECT ALL TIMES )
* //*
* //*           TIMES    00:00-23:59             ( SELECT A RANGE
* //*                    (BEG) (END)               OF TIMES )
* //*
* //* DATASET - SELECTION CAN BE LIMITED BY A FULL OR PARTIAL DATA
* //*           SET NAME.  FOR RACF COMMAND EVENTS THE DATASET/RESOURCE
* //*           FIELD CONTAINS A RACF PROFILE NAME.  IF A DATASET
* //*           CONTROL CARD IS NOT FOUND OR CONTAINS A BLANK IN CARD
* //*           COLUMN 10, NO FILTERING WILL BE DONE BASE ON THE
* //*           DATASET/RESOURCE NAME.  IF AN ASTERISK IS FOUND AT THE
* //*           END OF A PARTIALLY SPECIFIED DATASET/RESOURCE NAME
* //*           SELECTION WILL BE MADE ON ALL ENTRIES THAT MATCH THE
* //*           THE CHARATERS UP TO THE ASTERISK.  DATASET/RESOURCE
* //*           NAMES THAT DO NOT CONTAIN AN ASTERISK MUST MATCH
* //*           EXACTLY.  THE FORMAT FOR THIS CARD IS:
* //*
* //*                    1         2         3         4         5
* //*           |---5----0----5----0----5----0----5----0----5----0
* //*           DATASET                 ( SELECT ALL )
* //*
* //*           DATASET  SYS1.*         ( SELECT HLQ SYS1. )
* //*           DATASET  SYS*           ( SELECT ALL BEGINING W/ SYS )
* //*           DATASET  SYS1.DATA      ( SELECT ONLY SYS1.DATA )
* //*
* //* EVENT   - SELECTION CAN BE LIMITED TO ONE OF THREE EVENT TYPES,
* //*           (IG. VIOLATIONS, WARNINGS AND EVENTS) AND UP TO 30
* //*           EVENT GROUP CODES.  IF AN EVENT CONTROL CARD IS NOT
* //*           FOUND OR CONTAINS A BLANK IN CARD COLUMN 10, NO
* //*           FILTERING WILL BE DONE BASED ON EVENT TYPE.  IF AN
* //*           EVENT CONTROL CARD IS NOT FOUND OR CONTAINS BLANKS
* //*           IN CARD COLUMNS 11 AND 12, NO FILTERING WILL BE DONE
* //*           BASED ON THE EVENT GROUP NUMBER.  THE FORMAT FOR THIS
* //*           CONTROL CARD IS:
* //*
* //*                    1         2         3         4         5
* //*           |---5----0----5----0----5----0----5----0----5----0
* //*           EVENT                ( SELECT ALL EVENT TYPES )
* //*
* //*           EVENT    TNN...      ( T  = EVENT TYPE )
* //*                                ( NN = GROUP NUMBER
* //*                                     - MUST BE TWO DIGITS )
* //*
* //*           EVENT     02         ( SELECT ALL FOR RESOURCE ACCESS )
* //*           EVENT    V01         ( SELECT VIOLATIONS FOR LOGON )
* //*           EVENT    W           ( SELECT ALL WARNINGS )
* //*           EVENT    E0213       ( SELECT ERRORS FOR RESOURCE
* //*                                  ACCESS AND ALT USER COMMANDS )
* //*
* //*           EVENT TYPES:
* //*
* //*           V = VIOLATIONS, W = WARNINGS, E = ERRORS
* //*
* //*           GROUP
* //*           NUMBER           DESCRIPTION
* //*          ===================================================
* //*             01             JOB INITIATION / LOGON
* //*             02             RESOURCE ACCESS
* //*             03             END OF VOLUME
* //*             04             RENAME DATA SET
* //*             05             SCRATCH DATA SET
* //*             06             DEL DATA SET / TAPE VOLUME
* //*             07             DEFINE DATA SET / TAPE VOLUME
* //*             08             ADDSD
* //*             09             ADDGROUP
* //*             10             ADDUSER
* //*             11             ALTDSD
* //*             12             ALTGROUP
* //*             13             ALTUSER
* //*             14             CONNECT
* //*             15             DELDSD
* //*             16             DELGROUP
* //*             17             DELUSER
* //*             18             PASSWORD
* //*             19             PERMIT
* //*             20             RALTER
* //*             21             RDEFINE
* //*             22             RDELETE
* //*             23             REMOVE
* //*             24             SETROPTS
* //*             25             RVARY
* //*
* //* SUPR    - REPORT RECORDS CAN BE SUPPRESSED BY EITHER A FULL
* //*           OR PARTIAL DATA SET NAME.  FOR RACF COMMAND EVENTS
* //*           THE DATASET/RESOURCE FIELD CONTAINS A RACF PROFILE
* //*           NAME.  IF A SUPR CONTROL CARD IS NOT FOUND OR CONTAINS
* //*           A BLANK IN CARD COLUMN 10, THEN NO RECORDS WILL BE
* //*           SUPPRESSED BASED ON THE DATASET/RESOURCE NAME. IF
* //*           AN ASTERISK IS FOUND AT THE END OF A PARTIALLY
* //*           SPECIFIED DATASET/RESOURCE NAME, RECORDS WILL BE
* //*           SUPPRESSED ON ALL ENTRIES THAT MATCH THE CHARATERS
* //*           UP TO THE ASTERISK.  A DATASET/RESOURCE NAME THAT
* //*           DOES NOT CONTAIN AN ASTERISK MUST MATCH EXACTLY TO BE
* //*           SUPPRESSED. THE FORMAT FOR THIS CARD IS:
* //*
* //*                    1         2         3         4         5
* //*           |---5----0----5----0----5----0----5----0----5----0
* //*           SUPR                    ( SUPPRESS NONE )
* //*
* //*           SUPR     SYS87*         ( SUPPRESS ALL 1987 SYSTEM
* //*                                     TEMPORARY DATA SETS )
* //*           SUPR     SYS1.DATA      ( SUPPRESS ALL RECORDS THAT
* //*                                     REFERENCE THE SYS1.DATA
* //*                                     FILE AND OR THE SYS1.DATA
* //*                                     RACF PROFILE )
* //*           SUPR     SYS1.*         ( SUPPRESS ALL RECORDS THAT
* //*                                     REFERENCE DATA SETS THAT
* //*                                     BEGIN WITH "SYS1." AND OR
* //*                                     THE "SYS1.* RACF" PROFILE )
* //*
* //*
* //* STATS  - USE THIS CARD TO REQUEST THAT A SUMMARY OF USERS
* //*          SUCCESSES, FAILURES AND ERROR STATISTICS BE PRINTED
* //*          OUT AS AN APPENDIX TO THE STANDARD REPORT.  SUCCESSES
* //*          VIOLATIONS AND ERRORS ARE ROLLED UP IN USER / GROUP
* //*          SORT SEQUENCE.  ALSO DISPLAYED ARE TOTAL "JOB STARTS",
* //*          "TOTAL RESOURCE ACESSES" AND "TOTAL RACF COMMANDS".
* //*
* //*                    1         2         3         4         5
* //*           |---5----0----5----0----5----0----5----0----5----0
* //*           STATS                ( GENERATE STATS REPORT )
* //*           STATS    ONLY        ( GENERATE ONLY STATS REPORT )
* //*
* //*
* //* SORT   - THE REPORT OUTPUT CAN BE SORTED ON ANY COMBINATION
* //*          OF SIX DESIGNATED FIELDS.  SORT OPTIONS SHOULD BEGIN
* //*          IN CARD COLUMN 10 AND END ON OR BEFOR CARD COLUMN 70,
* //*          AND SEPERATED BY ONE BLANK.  IF NO SORT CONTROL CARD
* //*          OR NO SORT CONTROL OPTIONS ARE FOUND THAN THE REPORT
* //*          OUTPUT WILL BE SORTED IN THE DEFAULT SEQUENCE.  THE
* //*          FORMAT FOR THIS CARD IS:
* //*
* //*                    1         2         3         4         5
* //*           |---5----0----5----0----5----0----5----0----5----0
* //*           SORT                            ( SORT IN DEFAULT SEQ )
* //*
* //*           SORT     EVENT DATE TIME        ( SORT BY EVENT DATE
* //*                                             AND TIME )
* //*
* //*           REPORT SORT OPTIONS: ( DEFAULT SEQUENCE SHOWN )
* //*
* //*                       - SORT BY SYSID
* //*                       - SORT BY GROUP
* //*                       - SORT BY USER / JOBS  ( SEE "1" BELOW )
* //*                       - SORT BY DATE
* //*                       - SORT BY TIME
* //*                       - SORT BY EVENT ( SEE "2" BELOW )
* //*
* //*                       - OR ANY COMBINATION OF THE ABOVE
* //*
* //*                       1. IF THE "JOB" PARM IS USED THE KEYWORD
* //*                          "JOBS" IS IN EFFECT OTHERWISE THE
* //*                          KEYWORD "USER" SHOULD BE USED.
* //*
* //*                       2. EVENT VIOLATIONS AND ERRORS WILL BE
* //*                          SORTED AHEAD OF SUCCESSES.
* //*
* //*******************************************************************
* //*
* //* PARMS  -
* //*
* //* JOB      IF THE "JOB" PARM OPTION IS IN EFFECT THE REPORT OUTPUT
* //*          WILL CONTAIN THE TASK JOB NAME IN THE USER/JOB COLUMN.
* //*          IF THE JOB NAME IS NOT SIGNIFICANT (HEX ZEROS) THEN
* //*          THE USER NAME FIELD WILL BE USED.  THIS OPTION ACTIVATES
* //*          THE "JOBS" CONTROL CARD AND THE "JOBS" SORT OPTION.
* //*
* //*******************************************************************
* //*
* //* RETURN CODES -
* //*
* //*          FOR NON-ZERO RETURN CODES REFER TO THE DFSORT OR
* //*          SYNCSORT SYSTEM REFERENCE GUIDE.  THE RETURN CODE PASSED
* //*          FROM THE SORT EXIT IS PASSED BY RACFRPT AT END OF JOB.
* //*
* //*******************************************************************
* //*
* //* NOTES  -
* //*
* //*          "RACFRPT" WAS WRITTEN TO PROVIDE A MORE READABLE
* //*          ALTERNATIVE FOR RACF ADMINISTRATORS THAN IBM'S
* //*          TSO RACF REPORT WRITER.  THE GOAL WAS TO BE EASY TO
* //*          USE AND EASY TO READ.
* //*
* //*          HOWEVER "RACFRPT" DOES NOT PROVIDED ALL THE DETAIL
* //*          SUMMARY INFORMATION THAT IBM'S RACF REPORT CAN PROVIDE.
* //*          THIS INCLUDES:
* //*
* //*           1) THE PROFILE NAME OF THE PROTECTED RESOURCE.
* //*           2) THE LEVEL OF PROTECTION FOR THE PROTECTED RESOURCE.
* //*           3) THE LEVEL OF ACCESS REQUESTED FOR A PROTECTED
* //*              RESOURCE AND THE HIGHEST LEVEL ALLOWED.
* //*           4) THE AUTHORIZATION GRANTED WITH THE PERMIT COMMAND.
* //*           5) SUCCESSFUL STARTS FOR TSO SESSIONS AND BATCH JOBS.
* //*              ( UNSUCCESSFUL STARTS ARE RECORDED IN SMF TYPE 80
* //*                AND THEREFORE SHOW UP IN THIS REPORT )
* //*
* //*          WHEN A MORE COMPLETE PICTURE OF AN AUTHORIZED OR UN-
* //*          AUTHORIZED EVENT SHOULD BE NEEDED THE IBM REPORTS CAN
* //*          BE USED TO PROVIDED THE DETAILS.
* //*
* //*          THE PROGRAM IS PROVIDED AS IS WITHOUT WARRANTY OF
* //*          ANY KIND.  CORRECTIONS, MODIFICATIONS AND COMMENTS
* //*          ARE ENCOURAGED.
* //*
* //*
* //*                              GARY H. COLLINS  01/01/88
* //*
* //*                              TRIDENT REFIT FACILITY
* //*                              IBM SYSTEMS SUPPORT DIVISION, C/633
* //*                              SILVERDALE, WASHINGTON 98315
* //*
* //*                              (206) 396 - 4321 EXT. 1024
* //*
* //*******************************************************************
* //*
* //* JCL -
* //*
* //*      RACF REPORT WRITER
* //*
* //STEP1  EXEC PGM=RACFRPT,REGION=1024K
* //*TEP1  EXEC PGM=RACFRPT,PARM=JOB,REGION=1024K
* //SMFIN    DD DSN=SYS2.SMFDUMPW,DISP=SHR    <== SMF ACCUMULATION FILE
* //SORTIN   DD DSN=&&SORT,DISP=(,PASS),
* //            UNIT=SYSDA,SPACE=(CYL,(10,5))
* //SORTWK00 DD UNIT=SYSDA,SPACE=(CYL,5)
* //SORTWK01 DD UNIT=SYSDA,SPACE=(CYL,5)
* //SORTWK02 DD UNIT=SYSDA,SPACE=(CYL,5)
* //SYSPRINT DD SYSOUT=*
* //SYSOUT   DD SYSOUT=*
* //SYSIN    DD *
* GROUPS
* USERS
* DATES
* TIMES
* EVENT
* DATASET
* STATS
* SUPR
* SORT
* /*
         TITLE 'RACF REPORT'
         PRINT NOGEN
         SPACE
*----------------------------------------------------------------------
* RACF REPORT PROGRAM
* WRITTEN BY:  GARY COLLINS
* DESCRIPTION: READS SMF FILE FOR TYPE 80 RECORDS AND CREATES A RACF
*              ACCESS REPORT.
*----------------------------------------------------------------------
* LINKAGE SECTION -----------------------------------------------------
*----------------------------------------------------------------------
         SPACE
RACFRPT  CSECT                                   STANDARD IRS LINKAGE
         B     72(0,15)
         DC    17F'0'
         STM   14,12,12(13)
         ST    13,4(0,15)
         ST    15,8(0,13)
         LR    13,15
         LA    12,4095(0,13)
         LA    12,1(0,12)
         LA    11,4095(0,12)
         LA    11,1(0,11)
         USING RACFRPT,13,12,11
         SPACE
         L     R2,0(,R1)                         PARM ROUTINE
         CLI   1(R2),X'00'
         BE    NOPARM
         CLI   1(R2),X'03'
         BNE   PARMERR
         CLC   =C'JOB',2(R2)
         BNE   PARMERR
         MVI   JOBSW+1,X'00'
         MVC   CK4USER+2(2),=S(JOBS)
         MVC   SRTUSR(5),JOBS
         B     REWA
         SPACE
PARMERR  WTO   'RACF REPORT - INVALID PARM FOUND'
         SPACE
NOPARM   B     REWA
         SPACE
         DC    C'WRITTEN BY GARY COLLINS 1986,1987 (C)'
         EJECT
*----------------------------------------------------------------------
* MAIN PROCESSING -----------------------------------------------------
*----------------------------------------------------------------------
         SPACE
REWA     BAL   R14,BEGIN                         INITIALIZATION
         BAL   R14,CONTROL                       PROCESS CONTROL CARDS
MAINLUP  BAL   R14,GETREC                        GET A RECORD
         BAL   R14,ANALYZE                       BUILD OUPUT RECORD
         BAL   R14,FILTER                        APPLY SELECT CRITERA
         BAL   R14,PUTIT                         PUT TO SORT FILE
         B     MAINLUP                           LOOP SMF FILE
SMFEOF   BAL   R14,SORTIT                        SORT OUTPUT
         B     EOJ                               END OF JOB
         EJECT
*----------------------------------------------------------------------
* INITIALIZATION PROCESSING -------------------------------------------
*----------------------------------------------------------------------
         SPACE
BEGIN    ST    R14,BEGIN14
         SPACE
         WTO   'RACF REPORT STARTING '
         SPACE
         BAL   R7,GETTBLS                        GETMAIN FOR GROUP TBL
         ST    R1,GROUPTBL
         ST    R1,GROUPNDX
         ST    R2,GROUPTBX
         SPACE
         BAL   R7,GETTBLS                        GETMAIN FOR USER TBL
         ST    R1,USERTBL
         ST    R1,USERNDX
         ST    R2,USERTBX
         SPACE
         B     GOTSPACE
         SPACE
GETTBLS  GETMAIN R,LV=8000
         LTR   R15,R15
         BNZ   NOSPACE
         L     R2,=F'7938'                       R2 = (8000 - 62)
*                                                      |      |
*                                        LENGTH OF TABLE      |
*                                                             |
*                                           LENGTH OF CARD DATA
         LA    R2,0(R2,R1)
         BR    R7
         SPACE
NOSPACE  WTO   'RACF REPORT - GETMAIN FAILED'
         SPACE
         B     EARLYOUT
         SPACE
GOTSPACE TIME
         SPACE
         BAL   R14,GETDATE                       GET TODAYS DATE
         SPACE
         MVC   TODAY,HLDDATE
         SPACE
         OPEN  (SMFIN,,SORTFILE,(OUTPUT))
         SPACE
         L     R14,BEGIN14
         BR    R14
         SPACE
BEGIN14  DS    F
         EJECT
*----------------------------------------------------------------------
* CONTROL CARD PROCESSING ---------------------------------------------
*----------------------------------------------------------------------
         SPACE
CONTROL  ST    R14,CNTL14
         SPACE
         OPEN  (CNTLFILE,(INPUT),MSGFILE,(OUTPUT))
         SPACE
         PUT   MSGFILE,CNTLDATA
         SPACE
CNTLREAD GET   CNTLFILE
         SPACE
         LR    R10,R1
         USING SYSINREC,R10
         SPACE
         MVI   CNTLDATA,C' '
         MVC   CNTLDATA+1(132),CNTLDATA
         MVC   CNTLDATA+1(80),0(R1)
         SPACE
         PUT   MSGFILE,CNTLDATA
         SPACE
CK4GROUP CLC   =C'GROUPS',SYSINREC               DEFAULT ALL
         BNE   CK4USER
         CLI   SYSINREC+9,X'40'
         BE    CK4USER
         MVI   SYSINREC+71,X'40'
         LA    R5,SYSINREC+9
         L     R6,GROUPNDX
GETGRP   C     R6,GROUPTBX
         BNL   TBLERR
         LA    R4,SYSINREC+72
         CR    R4,R5
         BNH   LASTGRP
         BAL   R14,SCAN
         CLI   0(R5),C' '
         BNE   GETGRP
LASTGRP  ST    R6,GROUPNDX
         B     CNTLREAD
         SPACE
CK4USER  CLC   =C'USERS',SYSINREC                DEFAULT ALL
         BNE   CK4DATE
         CLI   SYSINREC+9,X'40'
         BE    CK4DATE
         MVI   SYSINREC+71,X'40'
         LA    R5,SYSINREC+9
         L     R6,USERNDX
GETUSR   C     R6,USERTBX
         BNL   TBLERR
         LA    R4,SYSINREC+72
         CR    R4,R5
         BNH   LASTUSR
         BAL   R14,SCAN
         CLI   0(R5),C' '
         BNE   GETUSR
LASTUSR  ST    R6,USERNDX
         B     CNTLREAD
         SPACE
CK4DATE  CLC   =C'DATES',SYSINREC                DEFAULT ALL
         BNE   CK4TIME
         CLI   SYSINREC+9,X'40'
         BE    CK4TIME
         MVC   LOWDTE,SYSINREC+9
         MVC   HIGHDTE,SYSINREC+18
         SPACE
         B     CNTLREAD
         SPACE
CK4TIME  CLC   =C'TIMES',SYSINREC                DEFAULT ALL
         BNE   CK4EVENT
         CLI   SYSINREC+9,X'40'
         BE    CK4EVENT
         MVC   BEGTIME,SYSINREC+9
         MVC   ENDTIME,SYSINREC+15
         B     CNTLREAD
         SPACE
CK4EVENT CLC   =C'EVENT',SYSINREC
         BNE   CK4SYS
         CLI   SYSINREC+9,C' '                   DEFAULT ALL
         BE    CK4ENUM
         MVC   EVENTCK,SYSINREC+9
CK4ENUM  LA    R4,SYSINREC+10
         LA    R5,EVENTBL
CK4ELUP  CLC   0(2,R4),=CL2' '
         BE    CNTLREAD
         PACK  DWD,0(2,R4)
         CVB   R3,DWD
         STCM  R3,B'0001',0(R5)
         LA    R4,2(,R4)
         LA    R5,1(,R5)
         B     CK4ELUP
         SPACE
CK4SYS   CLC   =C'SUPR',SYSINREC                 DEFAULT ALL
         BNE   CK4DS
         MVI   SUPCK,C' '
         MVC   SUPCK+1(L'SUPCK-1),SUPCK
         CLI   SYSINREC+9,X'40'
         BE    CK4DS
         MVI   SYSINREC+54,X'40'
         LA    R5,SYSINREC+9
         LA    R6,SUPCK
         BAL   R14,GETPARM
         STCM  R4,R1,SUPCKL
         B     CNTLREAD
         SPACE
CK4DS    CLC   =C'DATASET',SYSINREC              DEFAULT ALL
         BNE   CK4STATS
         MVI   DSCK,C' '
         MVC   DSCK+1(L'DSCK-1),DSCK
         CLI   SYSINREC+9,X'40'
         BE    CK4STATS
         MVI   SYSINREC+54,X'40'
         LA    R5,SYSINREC+9
         LA    R6,DSCK
         BAL   R14,GETPARM
         STCM  R4,R1,DSCKL
         B     CNTLREAD
         SPACE
CK4STATS CLC   =C'STATS',SYSINREC                DEFAULT NO
         BNE   CK4SORT
         CLC   =C'ONLY',SYSINREC+9
         BNE   SETSW
         MVI   STATSCK,C'O'
         B     CNTLREAD
SETSW    MVI   STATSCK,C'Y'
         B     CNTLREAD
         SPACE
CK4SORT  NOP   CNTLREAD
         CLC   =C'SORT',SYSINREC
         BNE   CNTLREAD
         CLI   SYSINREC+9,X'40'
         BE    CNTLREAD
         MVI   CK4SORT+1,X'F0'
         MVI   SYSINREC+71,X'40'
         LA    R5,SYSINREC+9
         LA    R6,HLDSRT
         LA    R7,SRTCRD
GETSORT  LA    R4,SYSINREC+72
         CR    R4,R5
         BNH   LASTSRT
         SR    R4,R5
         EX    R4,SYSINTRT
         LR    R4,R1
         SR    R4,R5
         BCTR  R4,R0
         MVC   HLDSRT,BLANKS
         EX    R4,SYSINMVC
         STM   R1,R6,HOLDREGS
         BAL   R14,CKSRT
         LM    R1,R6,HOLDREGS
         LA    R5,1(,R1)
         CLI   0(R5),C' '
         BNE   GETSORT
LASTSRT  LA    R6,SRTTBL
         LA    R8,6
SRTLUP   CLI   5(R6),X'FF'
         BE    NEXTSRT
         SR    R4,R4
         ICM   R4,R1,6(R6)
         EX    R4,SRTMVC
         LA    R7,1(R4,R7)
NEXTSRT  LA    R6,24(,R6)
         BCT   R8,SRTLUP
         BCTR  R7,R0
         MVI   0(R7),C')'
         B     CNTLREAD
         SPACE
CKSRT    LA    R6,SRTTBL
         LA    R8,6
CKSRTLUP CLC   HLDSRT,0(R6)
         BE    SRTHIT
         LA    R6,24(,R6)
         BCT   R8,CKSRTLUP
         BR    R14
         SPACE
SRTHIT   MVI   5(R6),X'FF'
         SR    R4,R4
         ICM   R4,R1,6(R6)
         EX    R4,SRTMVC
         LA    R7,1(R4,R7)
         BR    R14
         SPACE
SCAN     SR    R4,R5
         EX    R4,SYSINTRT
         LR    R4,R1
         SR    R4,R5
         BCTR  R4,R0
         STCM  R4,R1,0(R6)
         LA    R6,1(,R6)
         EX    R4,SYSINMVC
         LA    R6,1(R4,R6)
         LA    R5,1(,R1)
         BR    R14
         SPACE
GETPARM  LA    R4,SYSINREC+55
         SR    R4,R5
         EX    R4,SYSINTRT
         LR    R4,R1
         SR    R4,R5
         BCTR  R4,R0
         EX    R4,SYSINMVC
         BR    R14
         SPACE
TBLERR   WTO   'RACFRPT - TABLE SIZE EXECEDED'
         B     CNTLREAD
         SPACE
CNTLEOF  CLOSE (CNTLFILE,,MSGFILE)
         SPACE
         L     R6,GROUPNDX
         C     R6,GROUPTBL
         BNE   GRPSETUP
         XC    GRPTBL,GRPTBL
         B     NOGRPCK
GRPSETUP ST    R6,GROUPTBX
         SPACE
NOGRPCK  L     R6,USERNDX
         C     R6,USERTBL
         BNE   USRSETUP
         XC    USRTBL,USRTBL
         B     NOUSRCK
USRSETUP ST    R6,USERTBX
         SPACE
NOUSRCK  L     R14,CNTL14
         BR    R14
         DROP  R10
         SPACE
CNTL14   DS    F
         EJECT
*----------------------------------------------------------------------
* INPUT RECORD PROCESSING ---------------------------------------------
*----------------------------------------------------------------------
         SPACE
         USING SMF80,R3
GETREC   ST    R14,GETREC14
GETSMF   GET   SMFIN
         ST    R1,WKAREA
         LR    R3,R1
         AP    TOTRECS,=P'1'
         SPACE
CK4SMF20 CLI   SMF80RTY,20                       START OF JOB ?
         BNE   CK4SMF30
         USING SMF20,R4
         USING SMF20RS,R5
         LR    R4,R3
         CLC   SMF20RLO,=H'0'
         BE    GETSMF
         LH    R5,SMF20RLO
         LA    R5,4(R4,R5)
         CLC   SMF20RLS,=H'26'
         BNE   CK4RACF
         CLC   SMF20TID,ZEROS
         BE    GETSMF
         MVC   HLDUSR,SMF20USR
         MVC   HLDGRP,SMF20GRP
         MVC   HLDTID,SMF20TID
         B     GETSMF
         DROP  R4,R5
         SPACE
CK4SMF30 CLI   SMF80RTY,30                       COMMON ADDRESS SPACE ?
         BNE   CK4RACF
         USING SMF30,R4
         USING SMF30SI,R5
         LR    R4,R3
         CLC   SMF30IOF,ZEROS
         BE    GETSMF
         MVC   FWD,SMF30IOF
         L     R5,FWD
         LA    R5,0(R4,R5)
         CLC   SMF30TID,ZEROS
         BE    GETSMF
         MVC   HLDGRP,SMF30GRP
         MVC   HLDUSR,SMF30USR
         MVC   HLDTID,SMF30TID
         B     GETSMF
         DROP  R4,R5
         SPACE
CK4RACF  CLI   SMF80RTY,80                       RACF RECORD ?
         BNE   GETSMF
         AP    TOTSMF80,=P'1'
         SPACE
GETEXIT  L     R14,GETREC14
         BR    R14
         SPACE
GETREC14 DS    F
         EJECT
*----------------------------------------------------------------------
* ANALYZE ROUTINE -----------------------------------------------------
*----------------------------------------------------------------------
         SPACE
ANALYZE  ST    R14,ANALZ14
         SPACE
         ICM   R1,B'1111',SMF80DTE                GET DATE
         BAL   R14,GETDATE
         MVC   DATE,HLDDATE
         SPACE
         CLI   HLDFLD,0                           GET TERMINAL
         BE    CK80TRM
         CLC   HLDFLD(16),BLANKS
         BE    CK80TRM
         CLC   SMF80GRP,HLDGRP
         BNE   CK80TRM
         CLC   SMF80USR,HLDUSR
         BNE   CK80TRM
         MVC   TERMID,HLDTID
         B     CLEARHLD
         SPACE
CK80TRM  CLC   SMF80TRM,ZEROS
         BE    CLEARHLD
         MVC   TERMID,SMF80TRM
CLEARHLD XC    HLDFLD,HLDFLD
         SPACE
         LM    R5,R7,EVTPARM1                    GET EVENT
EVTLUP1  CLC   0(2,R5),SMF80EVT
         BE    EVTFIND1
         BXLE  R5,R6,EVTLUP1
         B     TIMERTN
         SPACE
EVTFIND1 MVC   EVENT,2(R5)
         MVC   EVENTQ,12(R5)
         MVC   DESFLG,22(R5)
         MVC   EVTTAG,SMF80EVT
         SPACE
TIMERTN  SR    R4,R4                             GET TIME
         MVC   FWD,SMF80TME
         L     R5,FWD
         D     R4,=F'100'
         ST    R5,FWD
         SR    R4,R4
         D     R4,=F'3600'
         CVD   R5,DWD
         UNPK  HOURS,DWD+6(2)
         OI    HOURS+1,X'F0'
         MH    R5,=H'3600'
         L     R4,FWD
         SR    R4,R5
         LR    R5,R4
         SR    R4,R4
         D     R4,=F'60'
         CVD   R5,DWD
         UNPK  MINS,DWD+6(2)
         OI    MINS+1,X'F0'
         MVI   TIMEDEL,C':'
         SPACE
JOBSW    B     GETUSER
         CLC   SMF80JBN,=XL8'0'
         BE    GETUSER
         MVC   USER,SMF80JBN
         B     GETGROUP
         SPACE
GETUSER  MVC   USER,SMF80USR                     GET USER
         CLI   SMF80EVT,X'01'
         BNE   GETGROUP
         CLC   SMF80JBN,=XL8'0'
         BE    GETGROUP
         MVC   DATASET(8),SMF80JBN
         SPACE
GETGROUP MVC   GROUP,SMF80GRP                    GET GROUP
         MVC   SYSID,SMF80SID                    GET SYSID
         SPACE
         LH    R4,SMF80CNT                       GET VAR DATA
         CH    R4,=H'0'
         BNH   FINISH
         CH    R4,=H'10'
         BH    FINISH
         CLI   SMF80EVT,7
         BH    RACFCMD
         CLI   SMF80EVT,2
         BL    FINISH
         LR    R5,R3
         AH    R5,SMF80LEN
         ST    R5,EORADD
         AH    R3,SMF80REL
         LA    R3,4(R0,R3)
         C     R3,EORADD
         BNL   VARERR
         SPACE
         USING SMF80VAR,R3
LUPVARS  CLI   SMF80DTP,1
         BE    GETDSN
         LA    R14,LUPVARS
         B     NEXTSECT
         SPACE
GETDSN   SR    R5,R5
         ICM   R5,B'0001',SMF80DLN
         SH    R5,=H'1'
         LA    R6,SMF80DTA
         EX    R5,MOVEDSN
         SPACE
LUK4VOL  CLC   SMF80DTP,=X'0F06'
         BE    GETVOL
         LA    R14,LUK4VOL
         B     NEXTSECT
         SPACE
GETVOL   MVC   VOLID(6),SMF80DTA
         B     FINISH
         SPACE
NEXTSECT MVC   HWD,SMF80DTP
         NI    HWD,X'00'
         AH    R3,HWD
         LA    R3,2(R0,R3)
         C     R3,EORADD
         BH    VARERR
         BCTR  R4,R14
         B     FINISH
         SPACE
         USING SMF80,R3
RACFCMD  CLI   SMF80EVT,23
         BH    FINISH
         SPACE
         SR    R4,R4
         LH    R4,SMF80CNT
         LR    R5,R3
         AH    R5,SMF80LEN
         ST    R5,EORADD
         AH    R3,SMF80REL
         LA    R3,4(R0,R3)
         C     R3,EORADD
         BNL   VARERR
         SPACE
         USING SMF80VAR,R3
LUK4RES  CLI   SMF80DTP,6
         BE    TYPE06
         CLI   SMF80DTP,9
         BE    TYPE09
BUMPSECT MVC   HWD,SMF80DTP
         NI    HWD,X'00'
         AH    R3,HWD
         LA    R3,2(R0,R3)
         C     R3,EORADD
         BNL   VARERR
         BCT   R4,LUK4RES
         B     VARERR
         SPACE
TYPE06   CLI   EVTTAG,22
         BH    EVTDATA
         CLI   EVTTAG,19
         BL    EVTDATA
         B     BUMPSECT
         SPACE
EVTDATA  LM    R5,R7,EVTPARM2
EVTLUP2  CLC   0(1,R5),EVTTAG
         BE    EVTFIND2
         BXLE  R5,R6,EVTLUP2
         B     VARERR
         SPACE
EVTFIND2 ICM   R6,B'0001',2(R5)
         ICM   R5,B'0001',1(R5)
         LA    R6,SMF80DTA(R6)
         B     MOVEIT
         SPACE
TYPE09   SR    R5,R5
         ICM   R5,B'0001',SMF80DLN
         LA    R6,SMF80DTA
         SH    R5,=H'1'
         SPACE
MOVEIT   EX    R5,MOVEDSN
         B     FINISH
         SPACE
VARERR   MVC   DATASET,=CL44'* ERROR READING VARIABLE DATA *'
         SPACE
         B     FINISH
         SPACE
FINISH   L     R14,ANALZ14
         BR    R14
         SPACE
ANALZ14  DS    F
         EJECT
*----------------------------------------------------------------------
* FILTER ROUTINE ------------------------------------------------------
*----------------------------------------------------------------------
         SPACE
FILTER   ST    R14,FILTER14
         SPACE
         MVI   HEXTBL+C' ',X'00'
         MVI   HEXTBL+C'*',X'FF'
         SPACE
SUPTST   CLI   SUPCK,C' '                        SUPPRESSION TEST
         BE    EVTTST
         TRT   SUPCK,HEXTBL
         BNE   SUPGEN
         MVC   SUPCLC+1(1),=AL1(43)
         B     SUPCLC
SUPGEN   SR    R3,R3
         ICM   R3,R1,SUPCKL
         SH    R3,=H'1'
         BM    SKIP
         STCM  R3,R1,SUPCLC+1
SUPCLC   CLC   DATASET,SUPCK
         BE    SKIP
         SPACE
EVTTST   CLI   EVENTCK,C' '                      EVENT TYPE TEST
         BE    EVTTSTGP
         CLC   DESFLG,EVENTCK
         BE    EVTTSTGP
         B     SKIP
         SPACE
EVTTSTGP LA    R3,EVENTBL                        EVENT GROUP TEST
         CLI   0(R3),C' '
         BE    DSTST
EVTTSTLP CLI   0(R3),0
         BE    SKIP
         CLC   EVTTAG,0(R3)
         BE    DSTST
         LA    R3,1(,R3)
         B     EVTTSTLP
         SPACE
DSTST    CLI   DSCK,C' '                         DATA SET TEST
         BE    GRPTST
         TRT   DSCK,HEXTBL
         BNE   DSGEN
         MVC   DSCLC+1(1),=AL1(43)
         B     DSCLC
DSGEN    SR    R3,R3
         ICM   R3,R1,DSCKL
         SH    R3,=H'1'
         BM    GRPTST
         STCM  R3,R1,DSCLC+1
DSCLC    CLC   DATASET,DSCK
         BNE   SKIP
         SPACE
GRPTST   L     R3,GROUPTBL                       GROUP TEST
         C     R3,=F'0'
         BE    USRTST
GRPTSTLP C     R3,GROUPTBX
         BNL   SKIP
         SR    R4,R4
         ICM   R4,R1,0(R3)
         STCM  R4,R1,GRPCLC+1
         LA    R3,1(,R3)
GRPCLC   CLC   GROUP(0),0(R3)
         BE    USRTST
         LA    R3,1(R4,R3)
         B     GRPTSTLP
         SPACE
USRTST   L     R3,USERTBL                       USER TEST
         C     R3,=F'0'
         BE    DTETST
USRTSTLP C     R3,USERTBX
         BNL   SKIP
         SR    R4,R4
         ICM   R4,R1,0(R3)
         STCM  R4,R1,USRCLC+1
         LA    R3,1(,R3)
USRCLC   CLC   USER(0),0(R3)
         BE    DTETST
         LA    R3,1(R4,R3)
         B     USRTSTLP
         SPACE
DTETST   CLI   DTETBL,C' '                       DATE TEST
         BE    TMETST
         SPACE
         CLC   DATE,HIGHDTE
         BH    SKIP
         CLC   DATE,LOWDTE
         BL    SKIP
         SPACE
TMETST   CLI   TMETBL,C' '                       TIME TEST
         BE    PASS
         SPACE
         CLC   TIME,ENDTIME
         BH    SKIP
         CLC   TIME,BEGTIME
         BNL   PASS
         SPACE
SKIP     MVI   CARD,C' '                         CLEAR OLD CARD
         MVC   CARD+1(132),CARD
         SPACE
         AP    TOTSKIP,=P'1'
         SPACE
         L     R14,SKIPADD
         BR    R14
         SPACE
PASS     CLI   DESFLG,C' '
         BE    SUCCESS
         CLI   DESFLG,C'V'
         BE    FAILED
         CLI   DESFLG,C'E'
         BE    ERROR
         CLI   DESFLG,C'W'
         BE    WARN
         B     FILTEND
         SPACE
SUCCESS  AP    TOTOK,=P'1'
         B     FILTEND
FAILED   AP    TOTFAIL,=P'1'
         B     FILTEND
ERROR    AP    TOTERR,=P'1'
         B     FILTEND
WARN     AP    TOTWARN,=P'1'
         SPACE
FILTEND  MVI   HEXTBL+C' ',X'FF'
         MVI   HEXTBL+C'*',X'00'
         L     R14,FILTER14
         BR    R14
         SPACE
FILTER14 DS    F
SKIPADD  DC    A(MAINLUP)
         EJECT
*----------------------------------------------------------------------
* PUT ROUTINE ---------------------------------------------------------
*----------------------------------------------------------------------
         SPACE
PUTIT    ST    R14,PUTIT14
         SPACE
         PUT   SORTFILE,CARD
         SPACE
         MVI   CARD,C' '                         CLEAR OLD CARD
         MVC   CARD+1(132),CARD
         SPACE
         L     R14,PUTIT14
         BR    R14
         SPACE
PUTIT14  DS    F
         EJECT
*----------------------------------------------------------------------
* CALL SORT ROUTINE(S) ------------------------------------------------
*----------------------------------------------------------------------
         SPACE
SORTIT   ST    R14,SORT14
         SPACE
         CLOSE (SMFIN,,SORTFILE)
         SPACE
         OPEN  (PRTFILE,(OUTPUT))
         SPACE
         CLI   STATSCK,C'O'
         BE    CKSTATS
         SPACE
         LA    R1,1
         ST    R1,SORTSW
         SPACE
         LA    R1,SORTMAIN
         LINK  EP=SORT
         SPACE
         ST    R15,ERRSW
         SPACE
         LTR   R15,R15
         BNZ   NOSTATS
         SPACE
CKSTATS  CLI   STATSCK,C'N'
         BE    NOSTATS
         SPACE
         LA    R1,2
         ST    R1,SORTSW
         SPACE
         LA    R1,SORTSTAT
         LINK  EP=SORT
         SPACE
         ST    R15,ERRSW
         SPACE
NOSTATS  L     R14,SORT14
         BR    R14
         SPACE
SORT14   DS    F
         EJECT
*----------------------------------------------------------------------
* GETDATE ROUTINE -----------------------------------------------------
*----------------------------------------------------------------------
         SPACE
GETDATE  ST    R14,DATE14
         SPACE
         LR    R4,R1
         ZAP   PJDAY,=P'0'
         ZAP   PYEAR,=P'0'
         ZAP   TJD,=P'0'
         STCM  R4,R3,PJDAY+6
         STCM  R4,R4,BYTE
         MVO   PYEAR,BYTE
         SR    R4,R4
         CVB   R5,PYEAR
         D     R4,=F'4'
         LTR   R4,R4
         BNZ   NOLEAP
         MVC   DTABLE+6(2),=PL2'29'
         MVI   RESET+1,X'00'
NOLEAP   LA    R4,DTABLE
         LA    R5,12
DLOOP    AP    TJD,2(2,R4)
         CP    TJD,PJDAY+6(2)
         BNL   COMPDATE
         LA    R4,4(R0,R4)
         BCT   R5,DLOOP
DATERR   MVC   DAY,=C'??'
         MVC   MTH,=C'??'
         MVC   YEAR,=C'??'
         B     RESET
         SPACE
COMPDATE SP    TJD,2(2,R4)
         SP    PJDAY,TJD
         UNPK  DAY,PJDAY+6(2)
         OI    DAY+1,X'F0'
         MVC   MTH,0(R4)
         UNPK  YEAR(2),PYEAR+6(2)
         OI    YEAR+1,X'F0'
         SPACE
RESET    B     DATEXIT
         MVI   *-3,X'F0'
         MVC   DTABLE+6(2),=PL2'28'
         SPACE
DATEXIT  L     R14,DATE14
         BR    R14
         SPACE
DATE14   DS    F
         EJECT
*----------------------------------------------------------------------
* EOJ ROUTINE ---------------------------------------------------------
*----------------------------------------------------------------------
         SPACE
EOJ      LA    R4,MSGTBL
         LA    R5,TOTTBL
         LA    R6,7
MSGLUP   ED    80(12,R4),0(R5)
         PUT   PRTFILE,(R4)
         LA    R4,133(R4)
         LA    R5,5(R5)
         BCT   R6,MSGLUP
         SPACE
         WTO   'RACF REPORT EOJ'
         SPACE
EARLYOUT CLOSE (PRTFILE)
         SPACE
         L     15,ERRSW
         L     13,4(13)
         ST    15,16(,13)
         LM    14,12,12(13)
         BR    14
         EJECT
*----------------------------------------------------------------------
* PROGRAM VARS CONSTANTS AND TABLES -----------------------------------
*----------------------------------------------------------------------
         SPACE
DWD      DS    D
FWD      DS    F
HWD      DS    H
         SPACE
EORADD   DC    F'0'
WKAREA   DC    F'0'
ERRSW    DC    F'0'
         SPACE
HOLDREGS DS    16F
         SPACE
         DS    0F
GRPTBL   DS    0XL12
GROUPTBL DC    F'0'
GROUPNDX DC    F'0'
GROUPTBX DC    F'0'
         SPACE
         DS    0F
USRTBL   DS    0XL12
USERTBL  DC    F'0'
USERNDX  DC    F'0'
USERTBX  DC    F'0'
         SPACE
BYTE     DC    CL1' '
ZEROS    DC    256X'0'
BLANKS   DC    256C' '
JOBS     DC    CL5'JOBS'
         SPACE
EVTPARM1 DC    A(EVTBL1,23,EVTBL1X-1)
EVTPARM2 DC    A(EVTBL2,3,EVTBL2X-1)
         SPACE
SRTMVC   MVC   0(0,R7),7(R6)
MOVEDSN  MVC   DATASET(0),0(R6)
SYSINMVC MVC   0(0,R6),0(R5)
SYSINTRT TRT   0(0,R5),HEXTBL
         SPACE
         DS    0D
PJDAY    DC    PL8'0'
PYEAR    DC    PL8'0'
TJD      DC    PL4'0'
TODAY    DC    CL8' '
         SPACE
HLDFLD   DS    0CL29
HLDUSR   DC    CL8' '
HLDGRP   DC    CL8' '
HLDTID   DC    CL8' '
HLDSRT   DC    CL5' '
         SPACE
HLDDATE  DS    0CL8
MTH      DC    CL2' '
         DC    CL1'/'
DAY      DC    CL2' '
         DC    CL1'/'
YEAR     DC    CL2' '
         SPACE
         DS    0F
CARD     DS    0CL133
         DS    CL1
USER     DS    CL8
         DS    CL2
GROUP    DS    CL8
         DS    CL2
DATE     DS    CL8
         DS    CL2
TIME     DS    0CL5
HOURS    DS    CL2
TIMEDEL  DS    CL1
MINS     DS    CL2
         DS    CL2
DATASET  DS    CL44
         DS    CL2
VOLID    DS    CL6
         DS    CL2
TERMID   DS    CL8
         DS    CL2
DESFLG   DS    CL1
         DS    CL1
EVTTAG   DS    CL1
EVENT    DS    CL10
         DS    CL2
EVENTQ   DS    CL10
         DS    CL2
SYSID    DS    CL4
         ORG   CARD+133
         SPACE
STATSCK  DC    CL1'N'
EVENTCK  DC    CL1' '
EVENTBL  DC    C' ',29X'0'
DSCKL    DC    XL1'0'
DSCK     DC    CL44' '
SUPCKL   DC    XL1'0'
SUPCK    DC    CL44' '
DTETBL   DS    0CL8
LOWDTE   DC    CL8' '
HIGHDTE  DC    CL8' '
TMETBL   DS    0CL10
BEGTIME  DC    CL5' '
ENDTIME  DC    CL5' '
         SPACE
TOTTBL   DS    0F
TOTRECS  DC    PL5'0'
TOTSMF80 DC    PL5'0'
TOTSKIP  DC    PL5'0'
TOTOK    DC    PL5'0'
TOTFAIL  DC    PL5'0'
TOTERR   DC    PL5'0'
TOTWARN  DC    PL5'0'
         SPACE
SRTTBL   DS    0C
SRTSID   DC    CL5'SYSID',X'00',X'07',CL17'130,4,A,'
SRTGRP   DC    CL5'GROUP',X'00',X'06',CL17'12,8,A,'
SRTUSR   DC    CL5'USER ',X'00',X'05',CL17'2,8,A,'
SRTDTE   DC    CL5'DATE ',X'00',X'06',CL17'22,8,A,'
SRTTME   DC    CL5'TIME ',X'00',X'06',CL17'32,5,A,'
SRTEVT   DC    CL5'EVENT',X'00',X'10',CL17'103,1,D,106,10,A,'
         SPACE
CNTLDATA DC    CL133' RACF REPORT, LINKED &SYSDATE &SYSTIME, SYSIN CARDX
               (S):'
         SPACE
MSGTBL   DS    0F
         DC    CL40'1'
         DC    C'A)  RACF REPORT SMF RECORDS.............'
         DC    X'4B2020206B2020206B202120',CL41' '
         DC    CL40' '
         DC    C'B)  RACF REPORT SMF TYPE 80 RECORDS.....'
         DC    X'4B2020206B2020206B202120',CL41' '
         DC    CL40' '
         DC    C'C)  RACF REPORT RECORDS FILTERED........'
         DC    X'4B2020206B2020206B202120',CL41' '
         DC    CL40' '
         DC    C'D)  RACF REPORT SELECTED SUCCESSES......'
         DC    X'4B2020206B2020206B202120',CL41' '
         DC    CL40' '
         DC    C'E)  RACF REPORT SELECTED VIOLATIONS.....'
         DC    X'4B2020206B2020206B202120',CL41' '
         DC    CL40' '
         DC    C'F)  RACF REPORT SELECTED ERRORS.........'
         DC    X'4B2020206B2020206B202120',CL41' '
         DC    CL40' '
         DC    C'G)  RACF REPORT SELECTED WARNINGS.......'
         DC    X'4B2020206B2020206B202120',CL41' '
         SPACE
EVTBL1   DC    XL2'0100',CL10'JOB INIT  ',CL10'          ',CL1' '
         DC    XL2'0101',CL10'JOB INIT  ',CL10'INVAL PASS',CL1'V'
         DC    XL2'0102',CL10'JOB INIT  ',CL10'INVAL GRP ',CL1'V'
         DC    XL2'0103',CL10'JOB INIT  ',CL10'INVAL OID ',CL1'V'
         DC    XL2'0104',CL10'JOB INIT  ',CL10'INVAL TERM',CL1'V'
         DC    XL2'0105',CL10'JOB INIT  ',CL10'INVAL APPL',CL1'V'
         DC    XL2'0106',CL10'JOB INIT  ',CL10'REVOK USER',CL1'V'
         DC    XL2'0107',CL10'JOB INIT  ',CL10'AUTO REVOK',CL1'V'
         DC    XL2'0200',CL10'RES ACCESS',CL10'          ',CL1' '
         DC    XL2'0201',CL10'RES ACCESS',CL10'INSUF AUTH',CL1'V'
         DC    XL2'0202',CL10'RES ACCESS',CL10'NOT FOUND ',CL1'E'
         DC    XL2'0203',CL10'RES ACCESS',CL10'WARNING   ',CL1'W'
         DC    XL2'0300',CL10'E O V     ',CL10'          ',CL1' '
         DC    XL2'0301',CL10'E O V     ',CL10'INSUF AUTH',CL1'V'
         DC    XL2'0400',CL10'RENAME    ',CL10'          ',CL1' '
         DC    XL2'0401',CL10'RENAME    ',CL10'INVAL GRP ',CL1'V'
         DC    XL2'0402',CL10'RENAME    ',CL10'NO GROUP  ',CL1'E'
         DC    XL2'0403',CL10'RENAME    ',CL10'INSUF AUTH',CL1'V'
         DC    XL2'0404',CL10'RENAME    ',CL10'RENAME ERR',CL1'E'
         DC    XL2'0405',CL10'RENAME    ',CL10'INVAL USER',CL1'V'
         DC    XL2'0500',CL10'SCRATCH   ',CL10'          ',CL1' '
         DC    XL2'0501',CL10'SCRATCH   ',CL10'NOT FOUND ',CL1'E'
         DC    XL2'0502',CL10'SCRATCH   ',CL10'INVAL VOL ',CL1'V'
         DC    XL2'0600',CL10'DELETE 1ST',CL10'MULT VOLS ',CL1'E'
         DC    XL2'0700',CL10'DEFINE    ',CL10'          ',CL1' '
         DC    XL2'0701',CL10'DEFINE    ',CL10'INVAL GRP ',CL1'V'
         DC    XL2'0702',CL10'DEFINE    ',CL10'NO GROUP  ',CL1'E'
         DC    XL2'0703',CL10'DEFINE    ',CL10'INSUF AUTH',CL1'V'
         DC    XL2'0704',CL10'DEFINE    ',CL10'DEFINE ERR',CL1'E'
         DC    XL2'0705',CL10'DEFINE    ',CL10'INVAL USER',CL1'V'
         DC    XL2'0800',CL10'ADDSD     ',CL10'          ',CL1' '
         DC    XL2'0801',CL10'ADDSD     ',CL10'INSUF AUTH',CL1'V'
         DC    XL2'0802',CL10'ADDSD     ',CL10'INSUF AUTH',CL1'V'
         DC    XL2'0900',CL10'ADDGROUP  ',CL10'          ',CL1' '
         DC    XL2'0901',CL10'ADDGROUP  ',CL10'INSUF AUTH',CL1'V'
         DC    XL2'0902',CL10'ADDGROUP  ',CL10'INSUF AUTH',CL1'V'
         DC    XL2'0A00',CL10'ADDUSER   ',CL10'          ',CL1' '
         DC    XL2'0A01',CL10'ADDUSER   ',CL10'INSUF AUTH',CL1'V'
         DC    XL2'0A02',CL10'ADDUSER   ',CL10'INSUF AUTH',CL1'V'
         DC    XL2'0B00',CL10'ALTDSD    ',CL10'          ',CL1' '
         DC    XL2'0B01',CL10'ALTDSD    ',CL10'INSUF AUTH',CL1'V'
         DC    XL2'0B02',CL10'ALTDSD    ',CL10'INSUF AUTH',CL1'V'
         DC    XL2'0C00',CL10'ALTGROUP  ',CL10'          ',CL1' '
         DC    XL2'0C01',CL10'ALTGROUP  ',CL10'INSUF AUTH',CL1'V'
         DC    XL2'0C02',CL10'ALTGROUP  ',CL10'INSUF AUTH',CL1'V'
         DC    XL2'0D00',CL10'ALTUSER   ',CL10'          ',CL1' '
         DC    XL2'0D01',CL10'ALTUSER   ',CL10'INSUF AUTH',CL1'V'
         DC    XL2'0D02',CL10'ALTUSER   ',CL10'INSUF AUTH',CL1'V'
         DC    XL2'0E00',CL10'CONNECT   ',CL10'          ',CL1' '
         DC    XL2'0E01',CL10'CONNECT   ',CL10'INSUF AUTH',CL1'V'
         DC    XL2'0E02',CL10'CONNECT   ',CL10'INSUF AUTH',CL1'V'
         DC    XL2'0F00',CL10'DELDSD    ',CL10'          ',CL1' '
         DC    XL2'0F01',CL10'DELDSD    ',CL10'INSUF AUTH',CL1'V'
         DC    XL2'0F02',CL10'DELDSD    ',CL10'INSUF AUTH',CL1'V'
         DC    XL2'1000',CL10'DELGROUP  ',CL10'          ',CL1' '
         DC    XL2'1001',CL10'DELGROUP  ',CL10'INSUF AUTH',CL1'V'
         DC    XL2'1002',CL10'DELGROUP  ',CL10'INSUF AUTH',CL1'V'
         DC    XL2'1100',CL10'DELUSER   ',CL10'          ',CL1' '
         DC    XL2'1101',CL10'DELUSER   ',CL10'INSUF AUTH',CL1'V'
         DC    XL2'1102',CL10'DELUSER   ',CL10'INSUF AUTH',CL1'V'
         DC    XL2'1200',CL10'PASSWORD  ',CL10'          ',CL1' '
         DC    XL2'1201',CL10'PASSWORD  ',CL10'INSUF AUTH',CL1'V'
         DC    XL2'1202',CL10'PASSWORD  ',CL10'INSUF AUTH',CL1'V'
         DC    XL2'1300',CL10'PERMIT    ',CL10'          ',CL1' '
         DC    XL2'1301',CL10'PERMIT    ',CL10'INSUF AUTH',CL1'V'
         DC    XL2'1302',CL10'PERMIT    ',CL10'INSUF AUTH',CL1'V'
         DC    XL2'1400',CL10'RALTER    ',CL10'          ',CL1' '
         DC    XL2'1401',CL10'RALTER    ',CL10'INSUF AUTH',CL1'V'
         DC    XL2'1402',CL10'RALTER    ',CL10'INSUF AUTH',CL1'V'
         DC    XL2'1500',CL10'RDEFINE   ',CL10'          ',CL1' '
         DC    XL2'1501',CL10'RDEFINE   ',CL10'INSUF AUTH',CL1'V'
         DC    XL2'1502',CL10'RDEFINE   ',CL10'INSUF AUTH',CL1'V'
         DC    XL2'1600',CL10'RDELETE   ',CL10'          ',CL1' '
         DC    XL2'1601',CL10'RDELETE   ',CL10'INSUF AUTH',CL1'V'
         DC    XL2'1602',CL10'RDELETE   ',CL10'INSUF AUTH',CL1'V'
         DC    XL2'1700',CL10'REMOVE    ',CL10'          ',CL1' '
         DC    XL2'1701',CL10'REMOVE    ',CL10'INSUF AUTH',CL1'V'
         DC    XL2'1702',CL10'REMOVE    ',CL10'INSUF AUTH',CL1'V'
         DC    XL2'1800',CL10'SETROPTS  ',CL10'          ',CL1' '
         DC    XL2'1801',CL10'SETROPTS  ',CL10'INSUF AUTH',CL1'V'
         DC    XL2'1802',CL10'SETROPTS  ',CL10'INSUF AUTH',CL1'V'
         DC    XL2'1900',CL10'RVARY     ',CL10'          ',CL1' '
         DC    XL2'1901',CL10'RVARY     ',CL10'INSUF AUTH',CL1'V'
         DC    XL2'1902',CL10'RVARY     ',CL10'INSUF AUTH',CL1'V'
EVTBL1X  EQU   *
         SPACE
EVTBL2   DC    X'082B04',X'090702',X'0A070D',X'0B2B06',X'0C0703'
         DC    X'0D070D',X'0E0704',X'0F2B02',X'100700',X'110700'
         DC    X'120707',X'170702'
EVTBL2X  EQU   *
         SPACE
DTABLE   DC    CL2'01',PL2'31',CL2'02',PL2'28',CL2'03',PL2'31'
         DC    CL2'04',PL2'30',CL2'05',PL2'31',CL2'06',PL2'30'
         DC    CL2'07',PL2'31',CL2'08',PL2'31',CL2'09',PL2'30'
         DC    CL2'10',PL2'31',CL2'11',PL2'30',CL2'12',PL2'31'
         SPACE
HEXTBL   DC    256X'0'
         ORG   HEXTBL+C' '
         DC    X'FF'
         ORG   HEXTBL+256
         SPACE
SORTSW   DS    F'0'
SORTMAIN DC    X'80',AL3(SPARMS)
         CNOP  2,4
SPARMS   DC    AL2(PARMSEND-PARMSBEG)
PARMSBEG DC    A(SORTBEG)
         DC    A(SORTEND)
         DC    A(RECBEG)
         DC    A(RECEND)
         DC    A(0)
         DC    A(E35RTN)
PARMSEND EQU   *
SORTBEG  DC    C' SORT FIELDS=('
SRTCRD   DC    C'130,4,A,12,8,A,2,8,A,22,8,A,32,5,A,103,1,D,106,10,A'
         DC    C'),FORMAT=CH'
SORTEND  DC    C' '
RECBEG   DC    C' RECORD TYPE=F,LENGTH=(133,133,133)'
RECEND   DC    C' '
         SPACE
         DS    0F
SORTSTAT DC    X'80',AL3(STATSP)
         CNOP  2,4
STATSP   DC    AL2(STATSEND-STATSBEG)
STATSBEG DC    A(STATBEG)
         DC    A(STATEND)
         DC    A(STABEG)
         DC    A(STAEND)
         DC    A(0)
         DC    A(E35RTN)
STATSEND EQU   *
STATBEG  DC    C' SORT FIELDS=('
         DC    C'2,8,A,12,8,A'
         DC    C'),FORMAT=CH'
STATEND  DC    C' '
STABEG   DC    C' RECORD TYPE=F,LENGTH=(133,133,133)'
STAEND   DC    C' '
         SPACE
         EJECT
*----------------------------------------------------------------------
* FILE DCB'S ----------------------------------------------------------
*----------------------------------------------------------------------
         SPACE
CNTLFILE DCB   DDNAME=SYSIN,                                           X
               DSORG=PS,                                               X
               RECFM=FB,                                               X
               LRECL=80,                                               X
               MACRF=(GL),                                             X
               EODAD=CNTLEOF
         SPACE
SMFIN    DCB   DDNAME=SMFIN,                                           X
               DSORG=PS,                                               X
               RECFM=VBS,                                              X
               BFTEK=A,                                                X
               MACRF=(GL),                                             X
               EODAD=SMFEOF
         SPACE
SORTFILE DCB   DDNAME=SORTIN,                                          X
               DSORG=PS,                                               X
               RECFM=FBA,                                              X
               LRECL=133,                                              X
               BLKSIZE=133,                                            X
               MACRF=(PM)
         SPACE
PRTFILE  DCB   DDNAME=SYSPRINT,                                        X
               DSORG=PS,                                               X
               RECFM=FBA,                                              X
               LRECL=133,                                              X
               BLKSIZE=133,                                            X
               MACRF=(PM)
         SPACE
MSGFILE  DCB   DDNAME=SYSOUT,                                          X
               DSORG=PS,                                               X
               RECFM=FBA,                                              X
               LRECL=133,                                              X
               BLKSIZE=133,                                            X
               MACRF=(PM)
         EJECT
*----------------------------------------------------------------------
* LITERALS FOR BASE1 AND BASE2 ----------------------------------------
*----------------------------------------------------------------------
         SPACE
         LTORG
         EJECT
*----------------------------------------------------------------------
* EQUATES FOR PROGRAM -------------------------------------------------
*----------------------------------------------------------------------
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
*----------------------------------------------------------------------
* DSECTS --------------------------------------------------------------
*----------------------------------------------------------------------
         SPACE
* OPTIONAL: THIS CODE MAKES USE OF IBM'S RACF DSECT MACRO
* SMF80  DSECT
*        IFASMFR9 80
         SPACE
SMF80    DSECT
SMF80LEN DS    CL2           TOTAL RECORD LENGTH
         DS    CL2           RESERVED
SMF80FLG DS    CL1           SYSTEM INDICATOR:
*                            BIT    MEANING WHEN SET -
*                            5      MVS/XA
*                            6      VS2
*                            7      VS1
SMF80RTY DS    CL1           RECORD TYPE
SMF80TME DS    CL4           SVC 83 TIME IN BINARY
SMF80DTE DS    CL4           DATE RECORD CREATED 00YYDDDF
SMF80SID DS    CL4           SVC 83 SYSTEM ID
SMF80DES DS    CL2           DESCRIPTOR FLAGS:
*                            BIT    MEANING WHEN SET -
*                            0      THE EVENT IS A VIOLATION
*                            1      USER NOT DEFINED TO RACF
*                            3      THE EVENT IS A WARNING
*                            2      RECORD HAS A VER IND SEE SMF80OVER
SMF80EVT DS    CL1           EVENT CODE - SEE TABLE 1 AND SMF80EVQ
SMF80EVQ DS    CL1           EVENT CODE QUALIFIER
SMF80USR DS    CL8           USER OR JOBNAME - EBCDIC
SMF80GRP DS    CL8           GROUP OR STEPNAME - EBCDIC
SMF80REL DS    CL2           OFFSET FROM BEGINING OF RECORD HEADER TO
*                            FIRST RELOCATABLE SECTION
SMF80CNT DS    CL2           COUNT OF THE NUMBER OF RELOCATE SECTIONS
SMF80ATH DS    CL1           AUTHORITIES USED
*                            BIT    MEANING WHEN SET -
*                            0      NORMAL AUTH CHECK (RESOURCE)
*                            1      SPECIAL ATTRIBUTE (COMMAND EXEC)
*                            2      OPERATIONS ATTRIBUTE (RESOURCE)
*                            3      AUDITOR ATTRIBUTE (COMMAND EXEC)
*                            4      INSTALLATION EXIT (RESOURCE)
*                            5      FAILSOFT PROCESSING (RESOURCE)
SMF80REA DS    CL1           REASON FOR LOGGING
*                            BIT    MEANING WHEN SET -
*                            0      CHANGES BEING MADE
*                            1 - 3  AUDITING OF USER OR RESOURCE
*                            4      RACINIT FAILURE
*                            5      COMMAND ALLWAYS AUDITED
*                            6      VIOLATION W/CMDVIOL
*                            7      AUDIT DUE TO GLOBALAUDIT
SMF80TLV DS    CL1           TERMINAL LEVEL NO
SMF80ERR DS    CL1           COMMAND PROCESSING ERROR FLAG:
*                            BIT    MEANING WHEN SET -
*                            0      COMMAND HAD ERROR, AND COULD NOT
*                                   BACK OUT CHANGES
*                            1      NO CHANGES MADE DUE TO ERROR
SMF80TRM DS    CL8           TERMINAL ID OF FOREGROUND USER - EBCDIC
SMF80JBN DS    CL8           JOB NAME
SMF80RST DS    CL4           TIME - JOB CARD WAS IN THE READER
SMF80RSD DS    CL4           DATE - JOB CARD WAS IN THE READER
SMF80UID DS    CL8           USER ID FROM COMMON SMF EXIT AREA
SMF80VER DS    CL1           VERSION INDICATOR (1.6)
         SPACE
SMF80VAR DSECT
SMF80DTP DS    CL1           DATA TYPE:
SMF80DLN DS    CL1           LENGTH OF DATA THAT FOLLOWS
SMF80DTA DS    0CL255        VARIABLE DATA:
EVENT16  DS    0CL8          DELGROUP GROUP NAME  +0
EVENT17  DS    0CL8          DELUSER USER ID      +0
         DS    CL2
EVENT09  DS    0CL8          ADDGROUP GROUP NAME  +2
EVENT15  DS    0CL44         DELDSD DATA SET NAME +2
         DS    CL1
EVENT12  DS    0CL8          ALTGROUP GROUP NAME  +3
         DS    CL1
EVENT08  DS    0CL44         ADDSD DATA SET NAME  +4
EVENT14  DS    0CL8          CONNECT USER ID      +4
         DS    CL2
EVENT11  DS    0CL44         ALTDSD DATA SET NAME +6
         DS    CL1
EVENT18  DS    0CL8          PASSWORD USER ID     +7
         DS    CL6
EVENT10  DS    0CL8          ADDUSER USER ID      +D
EVENT13  DS    0CL8          ALTUSER USER ID      +D
         SPACE
SMF20    DSECT               SMF 20 JOB INITIATION RECORD
SMF20LEN DS    CL2
SMF20SEG DS    CL2
SMF20FLG DS    CL1
SMF20RTY DS    CL1
SMF20TME DS    CL4
SMF20DTE DS    CL4
SMF20SID DS    CL4
SMF20JBN DS    CL8
SMF20RST DS    CL4
SMF20RSD DS    CL4
SMF20UID DS    CL8
SMF20RLO DS    CL2           OFFSET TO IDENTIFICATION SECTION
         SPACE
SMF20RS  DSECT               RELOCATABLE SECTION FOR SMF 20 RECORDS
SMF20RLS DS    CL2
SMF20GRP DS    CL8
SMF20USR DS    CL8
SMF20TID DS    CL8
         SPACE
SMF30    DSECT               SMF 30 COMMON ADDRESS SPACE WORK RECORD
SMF30LEN DS    CL2
SMF30SEG DS    CL2
SMF30FLG DS    CL1
SMF30RTY DS    CL1
SMF30TME DS    CL4
SMF30DTE DS    CL4
SMF30SID DS    CL4
SMF30WID DS    CL4
         DS    CL2
SMF30SO  DS    CL4           OFFSET TO SUBSYSTEM SECTION
SMF30SLN DS    CL2           LENGTH OF SUBSYSTEM SECTION
SMF30SON DS    CL2           NUMBER OF SUBSYSTEM SECTIONS
SMF30IOF DS    CL4           OFFSET TO IDENTIFICATION SECITON
SMF30ILN DS    CL2           LENGTH OF IDENTIFICATION SECTION
SMF30ION DS    CL2           NUMBER OF IDENTIFICATION SECTIONS
         SPACE
SMF30SU  DSECT               SUB SYS DSECT FOR SMF 30 RECORDS
SMF30TYP DS    CL2
SMF30RS1 DS    CL2
SMF30RVN DS    CL2
SMF30PNM DS    CL8
         SPACE
SMF30SI  DSECT               SYS ID DSECT FOR SMF 30 RECORDS
         DS    CL100
SMF30GRP DS    CL8
SMF30USR DS    CL8
SMF30TID DS    CL8
         SPACE
SYSINREC DSECT               SYSIN CARD
         DS    CL80
         EJECT
*----------------------------------------------------------------------
* E35 SYNCSORT ROUTINE ------------------------------------------------
*----------------------------------------------------------------------
         SPACE
         DROP  R13,R12
         SPACE
E35RTN   CSECT
         B     72(0,15)
         DC    17F'0'
         STM   14,12,12(13)
         ST    13,4(0,15)
         ST    15,8(0,13)
         LR    13,15
         LA    12,4095(0,13)
         LA    12,1(0,12)
         USING E35RTN,13
         USING WCARD,R10
         SPACE
         L     R10,0(R0,R1)
         SPACE
         L     R2,SWADD
         L     R2,0(R0,R2)
         CH    R2,=H'1'
         BE    RACFPRT
         B     STATPRT
         SPACE
RACFPRT  LTR   R10,R10
         BNZ   MORE1
         ZAP   LINECNT,PKDPAGE
         B     E35FINI
         SPACE
MORE1    BAL   R14,WHATPAGE
         BCT   R15,PUTREC1
         BAL   R14,HEADER1
         SPACE
PUTREC1  MVI   WEVTTAG,X'40'
         L     R1,DCBADD
         PUT   (R1),(R10)
         AP    LINECNT,=P'1'
         B     E35MORE
         SPACE
STATPRT  LTR   R10,R10
         BNZ   MORE2
         BAL   R14,LASTSTAT
         B     E35FINI
         SPACE
MORE2    BAL   R14,WHATPAGE
         BCT   R15,PUTREC2
         BAL   R14,HEADER2
PUTREC2  BAL   R14,STATSTOT
         SPACE
E35MORE  LA    R1,04
         B     E35EXIT
         SPACE
E35FINI  LA    R1,08
         SPACE
E35EXIT  L     R13,4(R13)
         ST    R1,16(,R13)
         LM    R14,R12,12(R13)
         BR    R14
         EJECT
*----------------------------------------------------------------------
* E35 SUB ROUTINES ----------------------------------------------------
*----------------------------------------------------------------------
         SPACE
HEADER1  ST    R14,E35SAVE
         BAL   R7,LINECLR
         MVI   HCARD,X'F1'
         SPACE
         L     R3,DTEADD
         MVC   HCARD+1(8),0(R3)
         MVC   HCARD+58(44),=CL44'TRF SMF RACF REPORT'
         MVC   HCARD+123(5),=CL5'PAGE'
         MVC   HCARD+127(6),PAGEPAT
         ED    HCARD+127(6),PAGECNT
         BAL   R7,LINEPUT
         SPACE
         BAL   R7,LINECLR
         BAL   R7,LINEPUT
         BAL   R7,LINEPUT
         BAL   R7,LINEPUT
         SPACE
         MVC   HCARD+1(8),=CL8'USER/JOB'
         MVC   HCARD+11(8),=CL8'GRP/STEP'
         MVC   HCARD+21(8),=CL8'DATE'
         MVC   HCARD+31(5),=CL5'TIME'
         SPACE
         MVC   HCARD+38(44),=CL44'DATA SET NAME / RACF RESOURCE'
         MVC   HCARD+84(6),=CL6'VOLID'
         MVC   HCARD+92(8),=CL8'TERM ID'
         MVC   HCARD+105(10),=CL10'EVENT'
         MVC   HCARD+129(4),=C'SYS'
         BAL   R7,LINEPUT
         SPACE
         MVI   HCARD,C'='
         MVC   HCARD+1(132),HCARD
         MVI   HCARD,C' '
         BAL   R7,LINEPUT
         BAL   R7,LINECLR
         SPACE
         L     R14,E35SAVE
         BR    R14
         SPACE
*----------------------------------------------------------------------
         SPACE
HEADER2  ST    R14,E35SAVE
         BAL   R7,LINECLR
         MVI   HCARD,X'F1'
         SPACE
         L     R3,DTEADD
         MVC   HCARD+1(8),0(R3)
         MVC   HCARD+58(44),=CL44'TRF SMF RACF REPORT SUMMARY'
         MVC   HCARD+123(5),=CL5'PAGE'
         MVC   HCARD+127(6),PAGEPAT
         ED    HCARD+127(6),PAGECNT
         BAL   R7,LINEPUT
         SPACE
         BAL   R7,LINECLR
         BAL   R7,LINEPUT
         BAL   R7,LINEPUT
         BAL   R7,LINEPUT
         SPACE
         MVC   HCARD+1(8),=CL8'USER'
         MVC   HCARD+10(8),=CL8'GROUP'
         MVC   HCARD+21(12),=CL12'SUCCESSES'
         MVC   HCARD+32(12),=CL12'VIOLATIONS'
         MVC   HCARD+48(12),=CL12'ERRORS'
         MVC   HCARD+55(12),=CL12'JOBS/LOGONS'
         MVC   HCARD+68(12),=CL12'RES ACCESS'
         MVC   HCARD+81(12),=CL12'RACF CMDS'
         BAL   R7,LINEPUT
         SPACE
         MVI   HCARD,C'='
         MVC   HCARD+1(132),HCARD
         MVI   HCARD,C' '
         BAL   R7,LINEPUT
         BAL   R7,LINECLR
         SPACE
         L     R14,E35SAVE
         BR    R14
         SPACE
*----------------------------------------------------------------------
         SPACE
WHATPAGE ST    R14,E35SAVE
         CVB   R3,LINECNT
         CH    R3,HWDPAGE
         BL    SAMEPAGE
         AP    PAGECNT,=P'1'
         ZAP   LINECNT,=P'0'
         LA    R15,1
         B     PGEXIT
         SPACE
SAMEPAGE LA    R15,0
         SPACE
PGEXIT   L     R14,E35SAVE
         BR    R14
         SPACE
*----------------------------------------------------------------------
         SPACE
STATSTOT ST    R14,E35SAVE
         SPACE
ONETIME  NOP   AROUND
         OI    *-3,X'F0'
         MVC   HUSER,WUSER
         MVC   HGROUP,WGROUP
         SPACE
AROUND   CLC   HUSER,WUSER
         BNE   PRINTIT
         CLC   HGROUP,WGROUP
         BE    CKDESFLG
         SPACE
PRINTIT  BAL   R7,EDITSTAT
         MVC   HUSER,WUSER
         MVC   HGROUP,WGROUP
         BAL   R7,ZAPHSTAT
         BAL   R7,LINEPUT
         SPACE
CKDESFLG CLI   WDESFLG,C' '
         BNE   CK4WARN
         AP    HOK,=P'1'
         B     CKJOBST
         SPACE
CK4WARN  CLI   WDESFLG,C'W'
         BNE   CK4VIO
         AP    HOK,=P'1'
         B     CKJOBST
         SPACE
CK4VIO   CLI   WDESFLG,C'V'
         BNE   CK4ERR
         AP    HVIO,=P'1'
         B     CKJOBST
         SPACE
CK4ERR   CLI   WDESFLG,C'E'
         BNE   CKJOBST
         AP    HERR,=P'1'
         SPACE
CKJOBST  CLI   WEVTTAG,1
         BL    STATSXIT
         BH    CKRESST
         AP    HJOB,=P'1'
         B     STATSXIT
         SPACE
CKRESST  CLI   WEVTTAG,7
         BH    CKRACST
         AP    HRES,=P'1'
         B     STATSXIT
         SPACE
CKRACST  CLI   WEVTTAG,25
         BH    STATSXIT
         AP    HRAC,=P'1'
         SPACE
STATSXIT L     R14,E35SAVE
         BR    R14
         SPACE
*----------------------------------------------------------------------
         SPACE
EDITSTAT MVC   HCARD+1(8),HUSER
         MVC   HCARD+10(8),HGROUP
         MVC   HCARD+20(72),STATSPAT
         ED    HCARD+20(10),HOK
         ED    HCARD+32(10),HVIO
         ED    HCARD+44(10),HERR
         ED    HCARD+56(10),HJOB
         ED    HCARD+68(10),HRES
         ED    HCARD+80(10),HRAC
         BR    R7
         SPACE
*----------------------------------------------------------------------
         SPACE
ZAPHSTAT ZAP   HOK,=P'0'
         ZAP   HVIO,=P'0'
         ZAP   HERR,=P'0'
         ZAP   HJOB,=P'0'
         ZAP   HRES,=P'0'
         ZAP   HRAC,=P'0'
         BR    R7
         SPACE
*----------------------------------------------------------------------
         SPACE
LASTSTAT ST    R14,E35SAVE
         SPACE
         CLC   HUSER,=CL8' '
         BE    LASTXIT
         BAL   R7,EDITSTAT
         BAL   R7,LINEPUT
         SPACE
LASTXIT  L     R14,E35SAVE
         BR    R14
         SPACE
*----------------------------------------------------------------------
         SPACE
LINECLR  MVI   HCARD,C' '
         MVC   HCARD+1(132),HCARD
         BR    R7
         SPACE
*----------------------------------------------------------------------
         SPACE
LINEPUT  L     R1,DCBADD
         PUT   (R1),HCARD
         AP    LINECNT,=P'1'
         BR    R7
         EJECT
*----------------------------------------------------------------------
* E35 VAR AND CONST ---------------------------------------------------
*----------------------------------------------------------------------
         SPACE
         DS    0D
LINECNT  DC    PL8'60'
HWDPAGE  DC    H'60'
PKDPAGE  DC    P'60'
         SPACE
PAGECNT  DC    PL3'0'
         SPACE
PAGEPAT  DC    X'402020202020'
STATSPAT DS    0CL72
         DC    X'40206B2020206B2021204040'
         DC    X'40206B2020206B2021204040'
         DC    X'40206B2020206B2021204040'
         DC    X'40206B2020206B2021204040'
         DC    X'40206B2020206B2021204040'
         DC    X'40206B2020206B2021204040'
         SPACE
DCBADD   DC    A(PRTFILE)
DTEADD   DC    A(TODAY)
SWADD    DC    A(SORTSW)
         SPACE
E35SAVE  DC    F'0'
         SPACE
HSTATS   DS    0CL40
HUSER    DC    CL8' '
HGROUP   DC    CL8' '
HOK      DC    PL4'0'
HVIO     DC    PL4'0'
HERR     DC    PL4'0'
HJOB     DC    PL4'0'
HRES     DC    PL4'0'
HRAC     DC    PL4'0'
         SPACE
         DS    0F
HCARD    DS    CL133
         SPACE
         LTORG
         EJECT
*----------------------------------------------------------------------
* E35 DSECTS ----------------------------------------------------------
*----------------------------------------------------------------------
         SPACE
WCARD    DSECT
         DS    0CL133
         DS    CL1
WUSER    DS    CL8
         DS    CL2
WGROUP   DS    CL8
         DS    CL2
WDATE    DS    0CL8
WDAY     DS    CL2
         DS    CL1
WMTH     DS    CL2
         DS    CL1
WYEAR    DS    CL2
         DS    CL2
WTIME    DS    0CL5
WHOURS   DS    CL2
WTIMEDEL DS    CL1
WMINS    DS    CL2
         DS    CL2
WDATASET DS    CL44
         DS    CL2
WVOLID   DS    CL6
         DS    CL2
WTERMID  DS    CL8
         DS    CL2
WDESFLG  DS    CL1
         DS    CL1
WEVTTAG  DS    CL1
WEVENT   DS    CL10
         DS    CL2
WEVENTQ  DS    CL8
         DS    CL4
WSYSID   DS    CL4
         END
