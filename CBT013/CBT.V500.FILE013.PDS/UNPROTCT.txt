TEC417   TITLE 'UNPROTECT DASD DATA SETS'
***********************************************************************
* MEMBER: TEC417                                                      *
* MACRO LIBS NEEDED: SYS1.AMODGEN (IEFJFCBN, IECSDSL1)                *
*                    SYS1.MACLIB                                      *
* LINKAGE EDITOR PARMS: LET,NCAL,XREF,MAP,LIST                        *
*                                                                     *
* PURPOSE:                                                            *
*        THIS PROGRAM IS PART OF A DISASTER RECOVERY PLAN. IT WILL    *
*        PRODUCE AMASPZAP CONTROL CARDS TO TURN OFF RACF AND          *
*        PASSWORD PROTECTION FOR ALL NON-VSAM DATA SETS ON A DASD     *
*        VOLUME. THIS IS NECESSARY IF A PACK DUMPED AT RNB MUST BE    *
*        RESTORED AT ANOTHER SITE FOR DISASTER RECOVERY, SO THAT      *
*        THE DEPENDENCE ON THAT SITE HAVING RACF IS REMOVED, AS WELL  *
*        AS NOT REQUIRING ENTRIES IN THE PASSWORD DATA SET FOR OUR    *
*        DATA SETS TO BE ACCESSED AT THAT SITE. THE PROGRAM SHOULD    *
*        BE RUN AT THE RECOVERY SITE AFTER THE DATA SETS HAVE BEEN    *
*        RESTORED.                                                    *
* ABEND CODES:                                                        *
*        U0001 - SYSPUNCH OPEN FAILED (PROBABLY SYSPUNCH MISSING)     *
*        U0002 - RDJFCB FAILED (PROBABLY SYSUT1 MISSING)              *
*        U0003 - SYSUT1 OPEN FAILED (PROBABLY SYSUT1 MISSING)         *
*        U0004 - FIRST DSCB READ FROM SYSUT1 WAS NOT FMT4             *
* JCL REQUIRED:                                                       *
*                                                                     *
*        //S1       EXEC PGM=TEC417                                   *
*        //STEPLIB  DD DSN=. . .                                      *
*    (1) //SYSPUNCH DD DSN=WHATEVER,UNIT=SYSDA,SPACE=(TRK,(1,1)),     *
*        //            DISP=(,CATLG),DCB=BLKSIZE=A-MULTIPLE-OF-80     *
*    (2) //SYSUT1   DD UNIT=SYSDA,VOL=SER=XXXXXX,DISP=OLD             *
*        //*                                                          *
*        //S2       EXEC PGM=AMASPZAP                                 *
*        //SYSPRINT DD SYSOUT=*                                       *
*    (3) //SYSLIB   DD DSN=FORMAT4.DSCB,DISP=OLD,UNIT=SYSDA,          *
*        //            VOL=SER=XXXXXX,DCB=KEYLEN=44                   *
*        //SYSIN    DD DSN=WHATEVER,DISP=OLD                          *
*                                                                     *
*        (1) SYSPUNCH WILL CONTAIN THE AMASPZAP CONTROL CARDS.        *
*            THE BLKSIZE WILL DEFAULT TO 80 IF NOT SPECIFIED.         *
*        (2) SYSUT1 POINTS TO THE DASD VOLUME TO BE PROCESSED         *
*        (3) SYSLIB POINTS TO THE SAME VOLUME AS SYSUT1. AN OPERATOR  *
*            REPLY WILL BE NECESSARY FOR THIS STEP TO RUN. KEYLEN     *
*            MUST BE SPECIFIED AS 44.                                 *
*                                                                     *
* CHANGE LOG:                                                         *
*        04/22/80  INITIAL VERSION, RAINIER NATIONAL BANK        WBF  *
*        04/23/80  ADD SUPPORT FOR PARM=TEST                     WBF  *
*        04/25/80  CHANGED TO NOT REWRITE JFCB. ADDED MACRO      WBF  *
*                  BASED JFCB/DSCB REFERENCES.                   WBF  *
***********************************************************************
*
         EJECT
TEC417   CSECT
*
R0       EQU   0     WORK REG
R1       EQU   1     PARAMETER REG, WORK REG
R2       EQU   2     BASE FOR SYSUT1 DCB, WORK REG IN DCB EXIT
R3       EQU   3     WORK REG IN DCB EXIT
R4       EQU   4     WORK REG IN DCB EXIT
R5       EQU   5     NOT USED
R6       EQU   6     NOT USED
R7       EQU   7     NOT USED
R8       EQU   8     NOT USED
R9       EQU   9     NOT USED
R10      EQU   10    NOT USED
R11      EQU   11    NOT USED
R12      EQU   12    PROGRAM BASE
R13      EQU   13    SAVE AREA POINTER
R14      EQU   14    LINK REG
R15      EQU   15    LINK REG
*
         USING *,R15               #####TEMPORARY PROGRAM BASE
         SAVE  (14,12),,TEC417_&SYSDATE_&SYSTIME
         LR    R12,R15             GET GOOD BASE REG
         DROP  R15                 #####
         USING TEC417,R12          #####PERMANENT PROGRAM BASE
         LA    R2,SAVE             LINK SAVE AREAS
         ST    R13,SAVE+4            .
         ST    R2,8(,R13)            .
         LA    R13,SAVE              .
         L     R1,0(,R1)           GET EXEC PARM
         CLC   0(2,R1),=H'4'       IS PARM AT LEAST 4 BYTES?
         BL    OPENPUN             /NO  - CAN'T BE TEST
*                                  /YES - CHECK IT
         CLC   2(4,R1),=C'TEST'    TEST?
         BNE   OPENPUN             /NO  - NORMAL RUN
         MVI   ZAP5,C'*'           /YES - COMMENT OUT THE REP CARD
         MVC   COM2TEST(6),=C'(TEST)'  AND SET A FLAG IN THE 2ND
*                                      COMMENT RECORD
OPENPUN  EQU   *
         OPEN  (SYSPUNCH,OUTPUT)   OPEN THE OUTPUT DATA SET
         TM    SYSPUNCH+DCBOFLGS-IHADCB,DCBOFOPN   DID IT OPEN?
         BO    RDJFCB                              /YES
         ABEND U0001                               /NO
*
RDJFCB   EQU   *
         LA    R2,SYSUT1           BASE FOR SYSUT1 DCB
         USING IHADCB,R2           #####SYSUT1 BASE
         RDJFCB ((R2))             GET THE JFCB FOR SYSUT1
         LTR   R15,R15             SUCCESSFUL?
         BZ    OPENUT1             /YES
         ABEND U0002               /NO
*
OPENUT1  EQU   *
         MVI   JFCBDSNM,X'04'      RESET JFCB DSNAME TO 44 X'04'
         MVC   JFCBDSNM+1(43),JFCBDSNM
         OI    JFCBTSDM,JFCNWRIT   DON'T REWRITE JFCB
         OPEN  ((R2),INPUT),       OPEN THE VTOC ON SYSUT1 PACK        X
               TYPE=J
         TM    DCBOFLGS,DCBOFOPN   DID IT OPEN?
         BO    LOGVOLSR            /YES
         ABEND U0003               /NO
*
LOGVOLSR EQU   *
         PUT   SYSPUNCH,COM1       PUT COMMENT TO SYSPUNCH
         MVC   COM2VOL,JFCBVOLS    LOG VOLSER BEING PROCESSED TO
         PUT   SYSPUNCH,COM2       SYSPUNCH AS A COMMENT
*
         READ  FMT4DECB,           READ THE FORMAT 4 DSCB              X
               SF,                                                     X
               (R2),                                                   X
               FMT4DSCB
         CHECK FMT4DECB            WAIT FOR COMPLETION OF FMT4 READ
*
         CLI   DS4IDFMT,C'4'       DID WE GET THE FMT4 DSCB?
         BE    FMT1LOOP            /YES
         ABEND U0004,DUMP          /NO
*
FMT1LOOP EQU   *
         CLC   DCBFDAD+3(5),DS4HPCHR HAVE WE REACHED FMT1 HWM?
         BH    FMT1DONE              /YES
*                                    /NO
         READ  FMT1DECB,           READ A DSCB                         X
               SF,                                                     X
               (R2),                                                   X
               FMT1DSCB
         CHECK FMT1DECB            WAIT FOR THE READ TO COMPLETE
*
         CLI   DS1FMTID,C'1'       IS IT A FORMAT 1 DSCB?
         BNE   FMT1LOOP            /NO  - SKIP IT
*                                  /YES
         CLC   DS1DSORG(2),=X'0008'   VSAM DATA SET?
         BE    FMT1LOOP            /YES - SKIP IT
*                                  /NO
         TM    DS1DSIND,DS1IND40+DS1IND10    RACF OR PSWD PROTECTED?
         BZ    FMT1LOOP            /NO  - SKIP IT
*                                  /YES - PRODUCE ZAP CARDS
*
*
         UNPK  WORK(11),DCBFDAD+3(6) GET CCHHR FOR 1ST CONTROL CARD
         TR    WORK(10),HEXTAB
         MVC   ZAP1CHR,WORK        PUT CCHHR IN CONTROL CARD
         MVC   ZAP1DSN,DS1DSNAM    PUT DSNAME IN AS A COMMENT
         PUT   SYSPUNCH,ZAP1       OUTPUT THE CONTROL CARD
*
         UNPK  WORK+00(15),DS1DSNAM+00(8)  GET DSNAME IN HEX FOR
         UNPK  WORK+14(15),DS1DSNAM+07(8)  CONTROL CARDS
         UNPK  WORK+28(15),DS1DSNAM+14(8)    .
         UNPK  WORK+42(15),DS1DSNAM+21(8)    .
         UNPK  WORK+56(15),DS1DSNAM+28(8)    .
         UNPK  WORK+70(15),DS1DSNAM+35(8)    .
         UNPK  WORK+84(05),DS1DSNAM+42(3)    .
         TR    WORK(88),HEXTAB               .
*
         MVC   ZAP2DSN(44),WORK    VERIFY 1ST HALF OF DSN
         PUT   SYSPUNCH,ZAP2
*
         MVC   ZAP3DSN(44),WORK+44 VERIFY 2ND HALF OF DSN
         PUT   SYSPUNCH,ZAP3
*
         UNPK  WORK(3),DS1DSIND(2) GET DS1DSIND IN HEX
         TR    WORK(2),HEXTAB      FOR CONTROL CARD
         MVC   ZAP4DATA,WORK         .
         PUT   SYSPUNCH,ZAP4         .
*
         NI    DS1DSIND,X'FF'-(DS1IND40+DS1IND10+DS1IND04) TURN OFF
         UNPK  WORK(3),DS1DSIND(2) PROTECT FLAGS IN DSCB COPY AND
         TR    WORK(2),HEXTAB      MAKE THE RESULE HEX FOR THE
         MVC   ZAP5DATA,WORK       CONTROL CARD
         PUT   SYSPUNCH,ZAP5         .
         B     FMT1LOOP            PROCESS NEXT DSCB
*
FMT1DONE EQU   *
         CLOSE (SYSUT1,,SYSPUNCH)  CLOSE THE DCB'S
         L     R13,SAVE+4          RESTORE THE REGS AND
         RETURN (14,12),RC=0       RETURN
         DROP  R2,R12
*
* DCB EXIT
*
DCBEXIT  EQU   *
         USING *,R15             ON ENTRY, R15 POINTS TO US
         USING IHADCB,R1         AND R1 POINTS TO THE DCB
         LH    R4,DCBLRECL       GET THE LRECL AND
         LH    R3,DCBBLKSI       THE BLOCKSIZE
         STH   R4,DCBBLKSI       SET BLKSIZE TO LRECL
         LTR   R3,R3             HAS BLKSIZE BEEN GIVEN?
         BZR   R14               /NO  - LEAVE AS LRECL
         SR    R2,R2             /YES - ENSURE MULTIPLE OF LRECL
         DR    R2,R4
         MR    R2,R4
         STH   R3,DCBBLKSI
         BR    R14               RETURN TO OPEN
         DROP  R1,R15
         LTORG ,
SAVE     DS    18F
SYSPUNCH DCB   DDNAME=SYSPUNCH,                                        X
               DEVD=DA,                                                X
               DSORG=PS,                                               X
               EXLST=EXPUN,                                            X
               LRECL=80,                                               X
               MACRF=PM,                                               X
               RECFM=FB
SYSUT1   DCB   BLKSIZE=96,                                             X
               DDNAME=SYSUT1,                                          X
               DEVD=DA,                                                X
               DSORG=PS,                                               X
               EODAD=FMT1DONE,                                         X
               EXLST=EXUT1,                                            X
               KEYLEN=44,                                              X
               MACRF=R,                                                X
               RECFM=F
*
EXUT1    DC    0F'0',X'87',AL3(JFCB)
EXPUN    DC    0F'0',X'85',AL3(DCBEXIT)
*
HEXTAB   EQU   *-240               THESE 2 CARDS MUST
         DC    C'0123456789ABCDEF'   STAY TOGETHER
*
COM1     DC    CL80'*  CONTROL CARDS TO TURN OFF PROTECTION FOR ALL'
*
COM2     DC    CL80' '                             |THESE
         ORG   COM2                                | CARDS
         DC    C'*  NON-VSAM DATA SETS ON VOLUME ' |  MUST
COM2VOL  DC    C'XXXXXX'                           |   STAY
         DC    C'  '                               |    TOGETHER
COM2TEST DC    C'      '                           |     .
         ORG   ,                                   |      .
*
ZAP1     DC    CL80' '                             |THESE
         ORG   ZAP1                                |  CARDS
         DC    C'CCHHR '                           |    MUST
ZAP1CHR  DC    C'CCCCHHHHRR'                       |      STAY
         DC    C'   '                              |        TOGETHER
ZAP1DSN  DC    CL44'DSNAME'                        |          .
         ORG   ,                                   |            .
*
ZAP2     DC    CL80' '                             | THESE
         ORG   ZAP2                                |  CARDS
         DC    C'  VER 00 '                        |   MUST
ZAP2DSN  DC    CL44'HEX DSNAME'                    |    STAY
         ORG   ,                                   |     TOGETHER
*
ZAP3     DC    CL80' '                             | THESE
         ORG   ZAP3                                |  CARDS
         DC    C'  VER 16 '                        |   MUST
ZAP3DSN  DC    CL44'HEX DSNAME'                    |    STAY
         ORG   ,                                   |     TOGETHER
*
ZAP4     DC    CL80' '                             | THESE
         ORG   ZAP4                                |  CARDS
         DC    C'  VER 5D '                        |   MUST
ZAP4DATA DC    C'XX'                               |    STAY
         ORG   ,                                   |     TOGETHER
*
ZAP5     DC    CL80' '                             | THESE
         ORG   ZAP5                                |  CARDS
         DC    C'  REP 5D '                        |   MUST
ZAP5DATA DC    C'XX'                               |    STAY
         ORG   ,                                   |     TOGETHER
*
FMT4DSCB DS    0D
FMT4KEY  DS    XL44
         IECSDSL1 4
*
FMT1DSCB DS    0D
         IECSDSL1 1
*
JFCB     DS    0D
         IEFJFCBN LIST=YES
*
WORK     DS    XL89
U0001    EQU   1
U0002    EQU   2
U0003    EQU   3
U0004    EQU   4
         PRINT NOGEN
         DCBD  DEVD=DA,DSORG=PS
         END
