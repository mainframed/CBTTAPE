//RACFLIST JOB (,E217),'DAN SQUILLACE',NOTIFY=
/*JOBPARM FETCH
//*
//* PRODUCE CONCISE RACF ACCESS RULE LIST
//*
//* USE ONE OR MORE LD ID() OR LD PREFIX() COMMANDS TO
//* SELECT PROFILES FOR THE REPORT
//*
//S1      EXEC PGM=IKJEFT01
//SYSTSPRT  DD DSN=&&PROFLST,UNIT=SYSDA,DISP=(,PASS),
// SPACE=(CYL,(10,5)),DCB=(RECFM=FB,LRECL=80,BLKSIZE=6160)
//SYSPRINT  DD SYSOUT=*
//SYSTSIN   DD *
 LD ID(SSD)     AUTHUSER
//S2    EXEC SAS
//RACFIN    DD DSN=&&PROFLST,DISP=OLD
 /*************************************************************
   THIS PROGRAM PRODUCES A CONCISE DATASET ACCESS RULE REPORT
   FROM THE OUTPUT OF THE LISTDSD COMMAND.  EACH ENTRY IS OF THE
   FORM:

     DSNAME VOLUME UACC  /ID1,PRIV1/ID2,PRIV2/...../IDN,PRIVN/

   VOLUME IS APPLICABLE ONLY FOR DISCRETE PROFILES.

  *************************************************************/

  DATA DPROFS(KEEP = DATASET ID ACCESS OWNER CGROUP UACC VOL);
  LENGTH LINE $ 13;
  LENGTH UACC $ 8;
  LENGTH VOL $ 6;
  INFILE RACFIN;
   DO UNTIL(LINE  = 'INFORMATION');
       INPUT @2 LINE   $CHAR11.
             @26 DATASET $CHAR44.;
       END;

   DO UNTIL(LINE  = 'LEVEL');
       INPUT @2 LINE  $CHAR5.;
       END;

  INPUT /   @9 OWNER $CHAR8.
            @23 UACC $8.;

  DO UNTIL(LINE  = 'YOUR ACCESS');
       INPUT @2 LINE  $CHAR11.;
       END;

  INPUT /   @18 CGROUP $CHAR8.;


  VOL = ' ';
  IF SCAN(DATASET,2,'(') Â¬= ' ' /* INDICATES GENERIC ENTRY */
   THEN GOTO SKIPVOL;

  DO UNTIL(LINE  = 'VOLUMES');
       INPUT @2 LINE  $CHAR7.;
       END;

  INPUT /   @2 VOL $CHAR6.;
  IF VOL = 'RACF01' OR SUBSTR(VOL,1,1) = ' '
  THEN VOL = ' ';   /* RACF01 INDICATES A MODEL PROFILE */

  SKIPVOL:
  DO UNTIL(LINE  = 'ID     ACCESS');
    INPUT @5 LINE  $CHAR13.;
    END;

  INPUT;
  NOACC = 1;
  NEXT: INPUT @2 ID $CHAR8.
              @13 ACCESS $CHAR1.;
        IF ID = ' ' THEN DO;
                         IF NOACC THEN DO;
                            ID = '******';
                            ACCESS = '*';
                            OUTPUT;
                            END;
                         RETURN;
                         END;
        NOACC = 0;
        IF ACCESS = 'P' THEN ACCESS = 'U';       /* UPDATE */
        ELSE IF ACCESS = 'O' THEN ACCESS = 'C'; /* CONTROL */
        OUTPUT;
        GO TO NEXT;
        RUN;

  PROC SORT DATA=DPROFS NODUPS;BY DATASET VOL ID;
  RUN;

  DATA DPROF2(KEEP= DATASET ACCLIST CGROUP UACC VOL OWNER);
  LENGTH ACCLIST $  200;
  RETAIN ACCLIST;
  SET DPROFS;BY DATASET VOL;
  ACCLIST = TRIM(ACCLIST)||TRIM(ID)||','||ACCESS||'/';
  IF LAST.VOL THEN DO;
     OUTPUT;
     ACCLIST = ' ';
     END;
  RUN;

 PROC PRINT;ID DATASET;VAR VOL UACC ACCLIST;
 FORMAT ACCLIST $CHAR75.;
 TITLE "PROFILES IN DATASET NAME ORDER";
 RUN;

 PROC SORT DATA=DPROF2;BY ACCLIST DATASET;
 RUN;

 PROC PRINT;ID DATASET;VAR VOL UACC ACCLIST;
 FORMAT ACCLIST $CHAR75.;
 TITLE "PROFILES BY DATASET WITHIN ACCESS LIST";
 RUN;
