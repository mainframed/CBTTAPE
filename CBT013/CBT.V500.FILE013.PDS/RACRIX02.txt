*          DATA SET RACRIX02   AT LEVEL 037 AS OF 06/08/82
ICHRIX02 TITLE 'RACINIT POST PROCESSING EXIT'
*---------------------------------------------------------------------*
*                                                                     *
*  THE RACINIT POST PROCESSING EXIT GETS CONTROL AFTER THE RACINIT    *
*  MACRO INSTRUCTION HAS BEEN ISSUED. IT HAS THE OPPORTUNITY TO CHECK *
*  ANY ABEND OR RETURN CODES AND MAKE A DECISION ON WHETHER OR NOT    *
*  TO KILL THE JOB OR TO CONTINUE PROCESSING.                         *
*                                                                     *
*  THIS EXIT WILL CHECK FOR A BLANK USERID AND WILL INDICATE TO       *
*  RACF THAT IT IS AN 'INVALID USERID' (RC=4).                        *
*                                                                     *
*       REGISTERS ARE AS FOLLOWS:                                     *
*                                                                     *
*             R0   =  WORK                                            *
*             R1   =  WORK                                            *
*             R2   =  WORK                                            *
*             R3   =  WORK                                            *
*             R4   =  WORK                                            *
*             R5   =  WORK                                            *
*             R6   =  WORK                                            *
*             R7  --> RACINIT RETURN CODE                             *
*             R8  --> ABEND CODE                                      *
*             R9  --> ACCOUNTING INFORMATION                          *
*             R10 --> JOB NAME                                        *
*             R11  =  PARAMETER LIST REG                              *
*             R12  =  MODULE BASE REG                                 *
*             R13  =  SAVE AREA REG                                   *
*             R14  =  RETURN REG                                      *
*             R15  =  RETURN CODE REG                                 *
*                                                                     *
*        RETURN CODES ARE AS FOLLOWS:                                 *
*                                                                     *
*           00  NORMAL RETURN CONTINUE PROCESSING                     *
*           04  RETRY RACINIT PROCESSING (NOT USED)                   *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT 'RACINIT POST PROCESSING EXIT'
ICHRIX02 CSECT
         USING ICHRIX02,R15            * SET ADDRESSABILITY
         B     START                   * BRANCH OVER CONSTANTS
         DC    C'ICHRIX02'             * CSECT NAME
         DC    C'&SYSDATE'             * DATE ASSEMBLED
         DC    C'&SYSTIME'             * TIME ASSEMBLED
START    STM   R14,R12,12(R13)         * SAVE CALLER'S REGS
         LR    R12,R15                 * CHANGE MODULE BASE REG
         USING ICHRIX02,R12            * CHANGE BASE REGISTER
         DROP  R15                     * DROP OLD BASE REGISTER
         LR    R11,R1                  * SAVE PARM REG
         USING RPPL,R11                * SET ADDRESSABILITY
         SPACE
         L     R1,RPPLFLG1             * R1 --> ACEE FLAG 1
         TM    0(R1),X'C0'             * ENVIRON=CHANGE|DELETE?
         BNZ   EXIT                    *   YES..EXIT
         SPACE
         L     R2,RPPLSTCN             * R2 --> STARTED TASK NAME
         CLI   0(R2),C' '              * IS IT BLANK?
         BNE   RESET                   *   NO...THIS IS AN STC
         SPACE
         L     R1,RPPLTERM             * R1 --> TERMINAL ADDRESS
         LTR   R1,R1                   * IS THERE ANY?
         BNZ   EXIT                    *   YES..TSO/CICS - EXIT
         SPACE
         L     R9,RPPLACCT             * R9 --> ACCOUNTING DATA
         LTR   R9,R9                   * IS THERE ANY?
         BZ    EXIT                    *   NO...ERROR
         SPACE
         CLI   0(R9),X'02'             * LESS THAN 2 ACCOUNTING FIELDS?
         BL    EXIT                    *   YES..TSO USER/STC
         SPACE
RESET    L     R7,RPPLRTRN             * R7  --> RACINIT RETURN CODE
         CLI   3(R7),X'00'             * RACINIT RETURN CODE = 0?
         BNE   EXIT                    *    NO...EXIT WITH RACINIT RC
         SPACE
         L     R10,RPPLUSRI            * R10 --> USERID
         CLI   0(R10),X'00'            * USERID = BLANK?
         BNE   EXIT                    *    NO...EXIT WITH RACINIT RC
         SPACE
         MVI   3(R7),X'04'             * RACINIT 'INVALID USERID' RC
         SPACE
EXIT     SR    R15,R15                 * 'CONTINUE PROCESSING'
         L     R14,12(,R13)            * RESTORE R14
         LM    R0,R12,20(R13)          * RESTORE R0-R12
         BR    R14                     * RETURN TO CALLER
         TITLE 'STORAGE, LITERALS, EQUATES'
F0       DC    F'0'
         SPACE
         LTORG
         SPACE
         REGEQ
         TITLE 'RACINIT PRE/POST PROCESSING PARAMETER LIST'
RPPL     DSECT
         SPACE
RPPLFCNT DS    A                       * ADDR(NUMBER OF WORDS IN LIST)
RPPLFLG1 DS    A                       * ADDR(FLAGE BYTE 1)
RPPLUSRI DS    A                       * ADDR(USERID)
RPPLPSWD DS    A                       * ADDR(PASSWORD)
RPPLSTCN DS    A                       * ADDR(STARTED TASK PROC NAME)
RPPLINST DS    A                       * ADDR(INSTALLATION PARMS)
RPPLGRP  DS    A                       * ADDR(GROUP NAME)
RPPLNPSW DS    A                       * ADDR(NEW PASSWORD)
RPPLACE1 DS    A                       * ADDR(ACEE FOR POST PROCESSING)
RPPLPGMN DS    A                       * ADDR(PROGRAMMER NAME)
RPPLACCT DS    A                       * ADDR(ACCOUNT INFORMTION)
RPPLOIDC DS    A                       * ADDR(OIDCARD IDENTIFICATION #)
RPPLTERM DS    A                       * ADDR(TERMINAL IDENTIFIER)
RPPLWORK DS    A                       * ADDR(FULLWORD WORK AREA)
RPPLABND DS    A                       * ADDR(ABEND CODE TO BE ISSUED)
RPPLRTRN DS    A                       * ADDR(RETURN CODE TO RACINIT)
RPPLFLG2 DS    A                       * ADDR(FLAG BYTE 2)
RPPLTRMD DS    A                       * ADDR(TERMINAL DATA FIELD)
RPPLUSDA DS    A                       * ADDR(INSTALLATION DATA)
RPPLTRML DS    A                       * ADDR(TERMINAL LEVEL NUMBER)
RPPLJOBN DS    A                       * ADDR(JOB NAME)
RPPLAPPL DS    A                       * ADDR(APPLICATION NAME)
RPPLSUBP DS    A                       * ADDR(SUBPOOL FOR ACEE)
RPPLACE2 DS    A                       * ADDR(ACEE)
RPPLAPLD DS    A                       * ADDR(APPLICATION DATA)
RPPLAPLL DS    A                       * ADDR(APPLICATION LEVEL)
         SPACE 2
         END
