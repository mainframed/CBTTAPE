/* REXX - PROZEDUR ZUM FILTERN DER VTOC-LISTE */
/* FUER PDS-CROSS-REFERENCE.                  */
/*                                            */
/*                                            */
COUNTOUT = 0
"EXECIO * DISKR VTOCOUT (STEM L. FINIS"
L.0 = L.0 - 1 /* IGNORE LAST RECORD */
SAY "VTOC-RECORDS READ: "L.0
DO I = 1 TO L.0
   PARSE VAR L.I Q1 "." X .
   Q1 = STRIP(Q1)
   /* DO NOT INDEX DATASETS STARTING WITH U,Q,YY,ZZ */
   /* DO NOT INDEX ISPF PROFILES */
   IF SUBSTR(Q1,1,1) = "U" | ,
      SUBSTR(Q1,1,1) = "Q" | ,
      SUBSTR(Q1,1,2) = "YY" | ,
      SUBSTR(Q1,1,2) = "ZZ" | ,
      X = "ISPF.PROFILE" THEN ITERATE

   PARSE VAR L.I DSN .
   /* DO NOT PROCESS SLR/ARO LIBRARIES */
   IF SUBSTR(DSN,1,18) = "SLRARO.SLRARO.GDF." THEN ITERATE
   /* DO NOT PROCESS TEXT FILES / HFS FILES */
   IF RIGHT(DSN,6) = ".TEXTQ" |,
      RIGHT(DSN,6) = ".TEXTH" |,
      RIGHT(DSN,4) = ".HFS" |,
      RIGHT(DSN,6) = ".TEXTL" THEN ITERATE

   COUNTOUT = COUNTOUT + 1
   PARSE VAR L.I DSN VOL REFDT UNIT  .
   LNEU.COUNTOUT = " "LEFT(DSN,44)" "VOL" "REFDT" "UNIT
END

/* ZURUECKSCHREIBEN */
"EXECIO "COUNTOUT" DISKW VTOCOUT (STEM LNEU. FINIS"
SAY "VTOC RECORDS WRITTEN: "COUNTOUT
