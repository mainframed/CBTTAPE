SHOW     TITLE '---  S H O W M V S  -  ISPF, TSO or BATCH run ---'
         PRINT OFF
* Here follows the Assembly SYSPARM(...) options process which     EU00
*        allows you to select :                                    EU00
*        1. NONE or omitted = no list of macros development        EU00
*           STRING = list only STRING macros development           EU00
*           FULL = list all macros development                     EU00
*        2. NONE = skip JES2 data information assembly             EU16
*           JES2 or omitted = generate JES2 data information (need EU16
*                             SYS1.HASPSRC to SYSLIB DD-name be    EU16
*                             concatenated)                        EU16
*        How to define options : i.e - SYSPARM(FULL)               EU00
*                                      SYSPARM(,NONE)              EU00
*        If you want add another selection, you can easily do      EU00
*        it adjusting below the statements marked "<===", define   EU00
*        the new OPT(n) default, and supply the new option coding  EU00
*        just before the comment card marked "---> Add option(s)". EU00
         GBLC  &EULST,&EUSTL,&EUJES                           <=== EU16
         LCLA  &CN,&CT,&LN,&MX,&SB,&ST                             EU00
         LCLC  &OPT(2)                                        <=== EU16
&MX      SETA  2                                              <=== EU16
&OPT(1)  SETC  'NONE'                                              EU00
&OPT(2)  SETC  'JES2'                                              EU16
&CN      SETA  K'&SYSPARM                                          EU00
         AIF   (K'&SYSPARM EQ 0).SP99                              EU00
&CT      SETA  1                                                   EU00
&SB      SETA  1                                                   EU00
&ST      SETA  &CT                                                 EU00
.SP1     AIF   ('&SYSPARM'(&CT,1) EQ ',').SP2                      EU00
&CT      SETA  &CT+1                                               EU00
         AIF   (&CT LE &CN).SP1                                    EU00
.SP2     ANOP                                                      EU00
&LN      SETA  &CT-&ST                                             EU00
         AIF   (&LN EQ 0).SP3                                      EU00
&OPT(&SB) SETC '&SYSPARM'(&ST,&LN)                                 EU00
.SP3     ANOP                                                      EU00
&SB      SETA  &SB+1                                               EU00
         AIF   (&SB GT &MX).SP99                                   EU16
&CT      SETA  &CT+1                                               EU00
         AIF   (&CT GT &CN).SP99                                   EU00
&ST      SETA  &CT                                                 EU00
         AGO   .SP1                                                EU00
.SP99    ANOP                                                      EU00
&EULST   SETC  'GEN'                                               EU00
&EUSTL   SETC  'GEN'                                               EU00
         AIF   ('&OPT(1)' EQ 'FULL').SL99                          EU00
&EULST   SETC  'NOGEN'                                             EU00
         AIF   ('&OPT(1)' EQ 'STRING').SL99                        EU00
&EUSTL   SETC  'NOGEN'                                             EU00
.SL99    ANOP                                                      EU00
&EUJES   SETC  'JES2'                                              EU16
         AIF   ('&OPT(2)' EQ 'JES2').SJ99                          EU16
&EUJES   SETC  'NONE'                                              EU16
.SJ99    ANOP                                                      EU16
* ---> Add option(s)                                               EU00
         MACRO
         IHASVC
SVCENTRY DSECT
SVCEP    DS    F              SVC ENTRY POINT ADDRERSS
SVCAMODE EQU   X'80'          SVC AMODE INDICATOR
SVCATTR1 DS    0XL2           ATTRIBUTES
SVCTP    DS    XL1            TYPE FIELD
SVCTP1   EQU   X'00'          TYPE 1 SVC
SVCTP2   EQU   X'80'          TYPE 2 SVC
SVCTP34  EQU   X'C0'          TYPE 3 OR 4 SVC
SVCTP6   EQU   X'20'          TYPE 6 SVC
SVCAPF   EQU   X'08'          APF AUTHORIZED 1-AUTHORIZED
SVCESR   EQU   X'04'          SVC IS PART OF THE ESR
SVCNP    EQU   X'02'          NON-PREEMPTIVE SVC
SVCASF   EQU   X'01'          SVC CAN BE ASSISTED
SVCRESV1 DS    XL1            RESERVED BYTE
SVCLOCKS DS    XL2            LOCK ATTRIBUTES
SVCLL    EQU   X'80'          LOCAL LOCK NEEDED
SVCCMS   EQU   X'40'          CMS LOCK NEEDED
SVCOPT   EQU   X'20'          OPT LOCK NEEDED
SVCALLOC EQU   X'10'          SALLOC LOCK NEEDED
SVCDISP  EQU   X'08'          DISPATCHER LOCK NEEDED
         SPACE 1
SVCESRAD EQU   SVCEP,4        ADDRESS OF ESR TABLE IF SVCESR IS ON
SVCESRMX EQU   SVCATTR1,4     MAXIMUM ESR NUMBER SUPPORTED BY THIS ESR
         SPACE 1
SVCLEN   EQU   *-SVCENTRY
         MEND
         MACRO
         ICHPDSDT &LIST=YES,&V=1,&R=8,&M=
.*-------------------------------------------------------------------*
.*    MACRO ID : ICHPDSDT                                            *
.*  DSECT NAME : DSDT                                                *
.*  POINTED BY : RCVTDSDT FIELD OF THE RCVT DATA AREA                *
.*    FUNCTION : THIS TABLE DESCRIBES PRIMARY AND BACK-UP RACF       *
.*               DATA-SETS                                           *
.*  SELECTIONS : LIST=YES - LIST ALL DSECT                           *
.*               V=1      - RACF VERSION NUMBER                      *
.*               R=9      - RACF RELEASE NUMBER                      *
.*               M=       - RACF MODIFICATION NUMBER (IF NEEDED)     *
.*                          I.E. : V=1,R=7,M=1 (RACF 1.7.1)          *
.*-------------------------------------------------------------------*
         AIF   ('&LIST' EQ 'YES').LIST
         PUSH  PRINT
         PRINT OFF
.LIST    AIF   ('&V' EQ '1').TEST
         MNOTE 8,'--- Unsupported RACF Version specified : &V..&R..&M '
         AGO   .LEND
.TEST    AIF   ('&R' LT '6').RERR
         AIF   ('&R' LE '9').SKP0
.RERR    MNOTE 8,'--- Unsupported RACF Release specified : &V..&R..&M '
         AGO   .LEND
.SKP0    ANOP
DSDT     DSECT
DSDTID   DS    CL4 .          EBCDIC ID
DSDTNUM  DS    F .            NUMBER OF ENTRIES IN TABLE
         AIF   ('&R' EQ '6').SKP3
         AIF   ('&R' NE '7').SKP1
         AIF   ('&M' EQ '').SKP3
.SKP1    ANOP
DSDTDSDX DS    A .            POINTER TO EXTENSION
DSDTVRSN DS    X .            VERSION NUMBER OF DSDT
         AIF   ('&R' NE '7').SKP2
         DS    3X .           DOUBLE-WORD ALIGNMENT
         AGO   .SKP3
.SKP2    ANOP
DSDTFLAG DS    X .            DSDT FLAGS OR INDICATORS
DSDTPFMT EQU   X'80' .        FLAG FOR PRIMARY FORMAT 0 = OLD/1 = NEW
DSDTBFMT EQU   X'40' .        FLAG FOR BACK-UP FORMAT 0 = OLD/1 = NEW
         DS    2X .           DOUBLE-WORD ALIGNMENT
         DS    8X .           RESERVED FOR EXPANSION
.SKP3    ANOP
DSDTENTY DS    0XL160 .       ENTRY FOR DATA-SET INFORMATION
DSDTPRIM DS    0XL80 .        ENTRY FOR PRIMARY DATA-SET
DSDPDCB  DS    A .            PTR DCB PRIMARY RACF DATA-SET
DSDPDEB  DS    A .            PTR DEB PRIMARY RACF DATA-SET
DSDPINDX DS    A .            PTR TO IN-STORAGE BUFFERS OR RESIDENT
*                             INDEX TABLE (ZERO IF NO RESIDENT BLOCKS
*                             FOR THE PRIMARY DATA-SET)
DSDPHRD  DS    A .            PTR RACF IN-STORAGE DS HEADER RECORD OR
*                             ZERO IF PRIMARY RACF DATA-SET IS ON A
*                             SHARED DEVICE
DSDPRUCB DS    A .            PTR UCB PRIMARY RACF DATA-SET
DSDPXLEN DS    A .            LENGTH OF IN-STORAGE BUFFERS OR RESIDENT
*                             INDEX TABLE FOR THE PRIMARY RACF DATA-SET
DSDPBAM  DS    A .            LOCATES IN-STORAGE BAM INFORMATION FOR
*                             PRIMARY DATA-SET
DSDPDSNL DS    X .            LENGTH OF PRIMARY RACF DATA-SET NAME
DSDPSTAT DS    X .            PRIMARY RACF DATA-SET STATUS
DSDPACTV EQU   X'80' .        THIS DATA-SET IS ACTIVE
DSDPPRIM EQU   X'40' .        THIS DATA-SET IS A PRIMARY
DSDPMSTR EQU   X'20' .        THIS DATA-SET IS THE MASTER RACF DATA-SET
*                             (ITS ICB CONTAINS STATUS OPTIONS)
DSDPRFSH EQU   X'10' .        REFRESH ICB
DSDPSHR  EQU   X'08' .        DATA-SET IS (OR WAS) SHARED
DSDPALTI EQU   X'04' .        ALTERI REQUESTS ARE BACKED-UP
DSDPDAT  EQU   X'02' .        IN-STORAGE BLOCKS CAN BE DATA BLOCKS
DSDPNREC DS    H .            # RECORDS PER TRACK PRIMARY DATA-SET
DSDPRXNO DS    X .            # IN-STORAGE BUFFERS OR RESIDENT INDEX
*                             BLOCKS
DSDPDSN  DS    CL44 .         DSN OF RACF PRIMARY DATA-SET
DSDPDSNO DS    X .            DATA-SET SEQUENCE NUMBER
         AIF   ('&R' GE '8').SKP4
         DS    XL2 .          RESERVED
         AGO   .SKP5
.SKP4    ANOP
DSDPCBLN DS    H .            LENGTH OF PRIMARY DSDE
.SKP5    ANOP
DSDTBACK DS    0XL80 .        ENTRY FOR BACK-UP DATA-SET
DSDBDCB  DS    A .            PTR DCB OF BACK-UP RACF DATA-SET
DSDBDEB  DS    A .            PTR DEB OF BACK-UP RACF DATA-SET
DSDBINDX DS    A .            PTR TO IN-STORAGE BUFFERS OR RESIDENT
*                             INDEX TABLE (ZERO IF NO RESIDENT BLOCKS
*                             FOR THE BACK-UP RACF DATA-SET)
DSDBHRD  DS    A .            PTR RACF IN-STORAGE DS HEADER RECORD OR
*                             ZERO IF BACK-UP RACF DATA-SET IS ON A
*                             SHARED DEVICE
DSDBRUCB DS    A .            PTR UCB OF BACK-UP DATA-SET
DSDBXLEN DS    A .            LENGTH OF IN-STORAGE BUFFERS OR RESIDENT
*                             INDEX TABLE FOR THE BACK-UP RACF DATA-SET
DSDBBAM  DS    A .            LOCATES IN-STORAGE BAM INFORMATION FOR
*                             BACK-UP DATA-SET
DSDBDSNL DS    X .            LENGTH OF BACK-UP DATA-SET NAME
DSDBSTAT DS    X .            STATUS OF BACK-UP DATA-SET
DSDBACTV EQU   X'80' .        THIS DATA-SET IS ACTIVE
DSDBPRIM EQU   X'40' .        THIS DATA-SET IS A PRIMARY
DSDBMSTR EQU   X'20' .        THIS DATA-SET IS THE MASTER RACF DATA-SET
*                             (ITS ICB CONTAINS STATUS OPTIONS)
DSDBRFSH EQU   X'10' .        REFRESH ICB
DSDBSHR  EQU   X'08' .        DATA-SET IS (OR WAS) SHARED
DSDBALTI EQU   X'04' .        ALTERI REQUESTS ARE BACKED-UP
DSDBDAT  EQU   X'02' .        IN-STORAGE BLOCKS CAN BE DATA BLOCKS
DSDBNREC DS    H .            # RECORDS PER TRACK BACK-UP DATA-SET
DSDBRXNO DS    X .            # IN-STORAGE BUFFERS OR RESIDENT INDEX
*                             BLOCKS
DSDBDSN  DS    CL44 .         DSN OF BACK-UP RACF DATA-SET
DSDBDSNO DS    X .            DATA-SET SEQUENCE NUMBER
         AIF   ('&R' GE '8').SKP6
         DS    XL2 .          RESERVED
         AGO   .SKP7
.SKP6    ANOP
DSDBCBLN DS    H .            LENGTH OF BACK-UP DSDE
.SKP7    SPACE 1
*        DSECT TO MAP A SINGLE ENTRY IN TABLE
DSDE     DSECT
DSDENT   DS    0XL80          ENTRY FOR DATA-SET
DSDEDCB  DS    A .            PTR DCB FOR DATA-SET
DSDEDEB  DS    A .            PTR DEB FOR DATA-SET
DSDEINDX DS    A .            PTR TO IN-STORAGE BUFFERS OR RESIDENT
*                             INDEX TABLE (ZERO IF NO RESIDENT BLOCKS
*                             FOR THE DATA-SET)
DSDEHRD  DS    A .            PTR RACF IN-STORAGE DS HEADER RECORD OR
*                             ZERO IF DATA-SET IS ON A SHARED DEVICE
DSDERUCB DS    A .            PTR UCB FOR DATA-SET
DSDEXLEN DS    A .            LENGTH OF IN-STORAGE INDEX RELATED
*                             CONTROL BLOCKS FOR DATA-SET
DSDEBAM  DS    A .            LOCATES IN-STORAGE BAM INFORMATION FOR
*                             DATA-SET
DSDEDSNL DS    X .            LENGTH OF DATA-SET NAME
DSDESTAT DS    X .            DATA-SET STATUS
DSDEACTV EQU   X'80' .        THIS DATA-SET IS ACTIVE
DSDEPRIM EQU   X'40' .        THIS DATA-SET IS A PRIMARY
DSDEMSTR EQU   X'20' .        THIS DATA-SET IS THE MASTER RACF DATA-SET
*                             (ITS ICB CONTAINS STATUS OPTIONS)
DSDERFSH EQU   X'10' .        REFRESH ICB
DSDESHR  EQU   X'08' .        DATA-SET IS (OR WAS) SHARED
DSDEALTI EQU   X'04' .        ALTERI REQUESTS ARE BACKED-UP
DSDEDAT  EQU   X'02' .        IN-STORAGE BLOCKS CAN BE DATA BLOCKS
DSDECMS  EQU   X'01' .        DATA-SET IS VM CMS FILE
DSDENREC DS    H .            # RECORDS PER TRACK ON DATA-SET
DSDERXNO DS    X .            # IN-STORAGE BUFFERS OR RESIDENT INDEX
*                             BLOCKS
DSDEDSN  DS    CL44 .         NAME OF DATA-SET
DSDEDSNO DS    X .            DATA-SET SEQUENCE NUMBER
         AIF   ('&R' GE '8').SKP8
         DS    XL2 .          RESERVED
         AGO   .LEND
.SKP8    ANOP
DSDECBLN DS    H .            LENGTH OF DSDE
.LEND    AIF   ('&LIST' EQ 'YES').MEND
         POP   PRINT
.MEND    MEND
         COPY  STRING                                              EU00
         PRINT ON
         SPACE 1
***********************************************************************
*                                                                     *
*          SSSS  H   H  OOOOO  W   W  M   M  V   V   SSSS             *
*         S      H   H  O   O  W   W  MM MM  V   V  S                 *
*          SSS   HHHHH  O   O  W W W  M M M  V   V   SSS              *
*             S  H   H  O   O  WW WW  M   M   V V       S             *
*         SSSS   H   H  OOOOO  W   W  M   M    V    SSSS              *
*                                                                     *
***********************************************************************
*      The material in this file is placed in the public domain       *
*      on an "AS IS" basis by its author, Gilbert Saint-Flour.        *
***********************************************************************
*                                                                     *
*     This program runs under ISPF and displays information from      *
*     various control blocks of the MVS system you're running on.     *
*     It uses ISPF's BRIF services to display the data on your        *
*     screen which allows you to browse easily through hundred of     *
*     lines of output.                                                *
*                                                                     *
*     SHOWMVS displays two different types of data:                   *
*       1. Data related to the MVS operating system:                  *
*          MVS level, DFP level, OSLVL flags, JES2 level              *
*          IPL DATE, TIME, VOLSER, NUC-ID, CLPA, CVTUSER              *
*          SMFSID, JWT, GRS' SNAME                                    *
*          Product levels for: TSO/E, ISPF, DFDSS, HSM, RACF, VTAM    *
*          SORT level and PPT table list                              *
*          Name and status of the RACF data-sets                      *
*          On-line CPU's and STORAGE (REAL and EXTENDED)              *
*          VIRTUAL STORAGE map (CSA, SQA, LPA, etc)                   *
*          SRM data (IPS/ICS/OPT, APG, ETC)                           *
*          OPEN catalogs                                              *
*          PAGE data sets in use                                      *
*          Name and status of SYS1.DUMP data-sets                     *
*          Sub-system vector table with functions processed           *
*          JES2 data information                                      *
*          TCAS data (requires SHOWMVS to run authorized)             *
*          TSO PARMLIB data                                           *
*          Address space usage, Active jobs, TSO users, Started tasks *
*          LINK-LIST data-sets, with creation data                    *
*          LPA-LIST data-sets, with creation date                     *
*          List of AUTHORIZED libraries                               *
*          ACTIVE LPA queue                                           *
*          SVC Table with name of the corresponding module            *
*          T1, T2, T3 and T6 ESR tables                               *
*          Device Classes and corresponding unit names                *
*          On-line devices, with unit-name, VOLSER, owning job,       *
*                use attribute, storage group                         *
*          System consoles, with status & Routcde list                *
         EJECT
*       2. Data related to the current address space:                 *
*          RACF profile (from ACEE)                                   *
*          TSO profile (from PSCB & UPT)                              *
*          TCB tree and PRB chain                                     *
*          Allocated Data sets (from TIOT, SWA, TCT)                  *
*          Enhanced view of the JPAQ and Load-lists                   *
*                                                                     *
*=====> SHOWMVS can be invoked as a program, as a TSO command, or     *
*       as an edit macro and can run authorized or non-authorized.    *
*       Displays that requires access to fetch-protected storage      *
*       areas (such as TCAS and PPT) are bypassed unless SHOWMVS      *
*       runs APF-authorized (TSO : IKJTSOxx member in SYS1.PARMLIB,   *
*       but in this case, even if you are under ISPF, the Hard-copy   *
*       mode (see below) is forced because dialog service requests    *
*       cannot be issued, invocation has been done under TSO TMP).    *
*                                                                     *
*     Hard-copy mode.                                                 *
*       In Hard-copy mode, SHOWMVS writes the output to a QSAM DCB    *
*       with RECFM=FBA,LRECL=101 instead of displaying it on the      *
*       terminal. Hard-copy mode is automatically activated in the    *
*       following circumstances:                                      *
*         1. SHOWMVS runs outside of an on-line TSO/ISPF              *
*            environment, for example as a job step, as a TSO         *
*            command (when no ISPF environment exists), or under      *
*            ISPF, in a batch TMP job.                                *
*         2. SHOWMVS runs in an on-line TSO/ISPF environment, and     *
*            a //SHOWMVS DD is currently allocated to the TSO         *
*            session.                                                 *
*                                                                     *
*       SHOWMVS output is in lower case. If you want to print it      *
*       in upper case, you must invoke SHOWMVS as a program and       *
*       specify PARM=CAPS.                                            *
*                                                                     *
*       Note that this is the only case where SHOWMVS accepts a PARM. *
*       Any other parm (when invoked as a program) or any parameters  *
*       (when invoked as a TSO command) are ignored.                  *
*                                                                     *
*       If hard-copy mode is triggered (for any of the reasons        *
*       described above) and there is no //SHOWMVS DD allocated,      *
*       SHOWMVS allocates one with SYSOUT=*.                          *
*                                                                     *
*    Assembly/link-edit information:                                  *
*         Environment:          MVS/XA 2.2 thru MVS/ESA 4.2           *
*         AMODE:                31                                    *
*         RMODE:                ANY                                   *
*         Non-IBM macro used:   STRING                                *
*         Processor:            Assembler H V2                        *
*         LKED Attributes:      RENT                                  *
*         Size of load-module:  28K                                   *
*                                                                     *
***********************************************************************
         EJECT
*@384 TSO PARMLIB data and authorization tables
*@383 Use NUCLKUP to find the address of IGCERROR
*@382 A few glitches corrected
*@381 TCAS data (only if APF-authorized)
*@381 System consoles, with status and route codes
*@381 RACF and TSO profiles (from ACEE, PSCB, UPT)
*@381 IDENTIFY removed, ATTACH now uses main entry point
*@380 Enhanced device status which is refreshed when you hit ENTER
*@373 Address space usage, active jobs, TSU, STC
*@373 Hardcopy gets title, gets converted to uppercase if PARM=CAPS
*@372 Display device class table and corresponding unit names
*@372 Display extent information for link-list data sets
*@372 Change register assignment and TRTPRINT build routine
*@371 Added a SNAP routine (and #SNAP macro) for debugging purposes
*@371 Moved Processor speed loop to sub-task
*@370 DISPLAY EXISTING SYS1.DUMP DATA SETS WITH FULL/EMPTY STATUS
*@370 DISPLAY RESOURCE MANAGER PARAMETERS (SRM)
*@364 FIXED RANDOM ABENDS (S0C4 & SA03) AND BRIF FAILURES
*@363 FIXED JES2 410 OFFSETS (WITH A LITTLE HELP FROM MY FRIEND SAM)
*@362 ESTIMATE PROCESSOR SPEED IN "MIPS", IMPROVE IPL DATE FORMAT
*@361 PREVENT S13E ABENDS
*----------------------------------------------------------------------
         EJECT
*    Origin : extracted from tape CBT 93 FEB, file 183.
*    --------
*
*   Updates : by MOINIL P.A.
*   ---------        COMPUTING CENTRE (TP 361)
*                    J.R.C. - ISPRA ESTABLISHMENT
*                    21020 ISPRA (VA), ITALY
*   13JUL93 : EU00 - Included COPY STRING macro needed to Assembly this
*                    code. Added SYSPARM(FULL or STRING) as Assembly
*                    list options. Macro STRING documentation list with
*                    DOC=YES specified at FINAL_CALL.
*   14JUL93 : EU01 - Interrupt 0C4 in "TCAS00" (move from VTAM key 6
*                    to private area key 8) / fixed : move in key 0.
*                    Interrupt 0C4 in "INITIAL" (when in BATCH and no
*                    PARM= specified) / fixed : test for PARM= length.
*             EU02 - Interrupt 0C4 in "USERS" (LOCASCB function return
*                    back R1 was only tested for zero = invalid) /
*                    fixed : test also negative R1 = invalid. Also 1
*                    instruction missing in Started Tasks loop, and 2
*                    instructions adjusted after label "RDRTN50".
*             EU03 - Interrupt 0C4 in "INITIAL" (when a Batch job has
*                    no PARM) / fixed : test zero length of PARM field.
*             EU04 - Correct NOTYPE(... when large number of no-types
*                    in SMF data) / fixed : use next lines to continue.
*             EU05 - Correct WMSTIPM lablel (MSO Service coefficient)
*                    definition / fixed : EQU WMST+X'9C',4 value.
*   16JUL93 : EU06 - In APF list, display the volume serial instead of
*                    "UNCAT" when data-set catalog'd on another volume.
*   19JUL93 : Some minor changes or rearrangements (i.e. : I like
*                    diagnose errors, not receive Abends).
*   20JUL93 : EU07 - Get RACF Version, Release and Modification number
*                    from field "RCVTVRMN".
*   22JUL93 : EU08 - The DUMP data-sets cannot be set FULL or EMPTY by
*                    just testing the DS1LSTAR field of their OBTAINed
*                    DSCB format 1 / fixed by scanning status in the
*                    DUMP data-set queue.
*   26JUL93 : EU09 - Processor speed in MIPS is a litle too low / fixed
*                    by adjust number of instructions (+ 0.25).
*   29JUL93 : EU10 - Restore display of RACF connected application and
*                    add some other stuff.
*   30JUL93 : EU11 - Add RACF data display information.
*   02AUG93 : EU12 - Add PPT data display information.
*   04AUG93 : EU13 - Allow SHOWMVS to run as TSO command (not under
*                    ISPF, so, all environment are accepted). In this
*                    case, SYSOUT=* is changed to SYSOUT=X (our HOLD
*                    class) with free at CLOSE (dynamically queued to
*                    the session). Also fixed not filled last line when
*                    "SHOWMVS" DD is pre-allocated and thus used (the
*                    error is caused by the PUT locate wrongly placed).
*                    Don't use ATTACH when not under ISPF (not needed).
         EJECT
*   06AUG93 : EU14 - Change the way to see if running Batch and check
*                    if TSO foreground or background. Also allows to
*                    have CAPS operand when invoked as command.
*   10AUG93 : EU15 - Added PARM= options or command operands :
*                    1. DDN(...) to use an alternate DD-name (default
*                                is SHOWMVS). Hardcopy use only.
*                       Mandatory : when this keyword is used the FILE
*                       =========== must be pre-allocated.
*                    2. LPG(...) to set the number of Lines per Page
*                                (default is 60).
*                    3. PGN(...) to set the starting Page Number, it's
*                                the initial count (default is 1).
*                    4. HPG(YES/NO) to have or not have the heading
*                                at top of all pages (default is YES).
*                                HPG(NO) conflicts with PGN(...).
*                    Note: each option must be separated from the other
*                          by at least a blank or comma.
*                    Ex. - program EXEC statement:
*                          PARM='CAPS,DDN(MYPRINT),LPG(50),PGN(10)'
*                          TSO command operands:
*                          SHOWMVS CAPS DDN(MYPRINT) LPG(50) PGN(10)
*                    Also the LINK, ATTACH or LOAD/CALL/DELETE macros
*                    instructions can be used to invoke SHOWMVS from
*                    another program/command (be careful to AMODE).
*                    The format must be as follows :
*                    ...  LINK/ATTACH  EP=SHOWMVS,
*                                      PARAM=(optaddr),VL=1
*                    ...  LOAD  EP=SHOWMVS
*                         LR    R15,R0  <--- (*) see Note below
*                         CALL  (15),
*                               (optaddr),VL=1
*                         DELETE  EP=SHOWMVS
*                    where "optaddr" specifies the address of the
*                    option list (equivalent to PARM= parameter of an
*                    EXEC statement and must be present).
*                    (*) Note: after the LOAD bit0 of R0 = AMODE, so
*                              may be you must use BASSM / BSM, and
*                              bit0 of R1 = AC code.
*                    Typical Parameter List :
*                    R1 = address of A(optaddr+X'80000000')
*                                 0F'0',H'0'
*                    optaddr ---> H'0'
*                              or H'4',C'CAPS'
*                              or H'25',C'CAPS DDN(MYPRINT) LPG(50)'
*   12AUG93 : EU16 - Add JES2 data display information.
*   16AUG93 : EU17 - Reworked Virtual Storage Map.
*   17AUG93 : EU18 - Added PARM= option MSG(NO/YES) just to debug
*                    (default is NO).
*   28SEP93 : Updated to version V3R8M4 (tape CBT 93 AUG, file 183).
         EJECT
*   15NOV93   EU19 - Correct SORT first n bytes dump.
*                    Correct Sub-system Vector Table when large number
*                    of functions / fixed : use next lines to continue.
*                    Correct Consoles ROUTCDE=(... when large number
*                    of types) / fixed : use next lines to continue.
*   16NOV93 : EU20 - Adjust Assembly of JES2 data display information
*                    (check supported/solved HASP version).
         EJECT
*---------------------- local macros definition -----------------------
         MACRO                                               EU17 MACRO
         MEMORY_MAP &MSG,&START,&END,&SIZE                   EU17 MACRO
         AIF   (T'&START NE 'O').AA                          EU17 MACRO
         MNOTE 8,'--- Start Address missing '                EU17 MACRO
         AGO   .ZZ                                           EU17 MACRO
.AA      L     R1,&START               START ADDRESS         EU17 MACRO
         AIF   (T'&END EQ 'O').CC                            EU17 MACRO
         L     R2,&END                 END ADDRESS           EU17 MACRO
         AIF   (T'&SIZE NE 'O').BB                           EU17 MACRO
         LA    R0,1                    ONE BYTE              EU17 MACRO
         ALR   R0,R2                   END ADDRESS           EU17 MACRO
         SLR   R0,R1                   SIZE IN BYTES         EU17 MACRO
         AGO   .EE                                           EU17 MACRO
.BB      L     R0,&SIZE                SIZE IN BYTES         EU17 MACRO
         AGO   .EE                                           EU17 MACRO
.CC      AIF   (T'&SIZE NE 'O').DD                           EU17 MACRO
         MNOTE 8,'--- End Address and/or Size missing '      EU17 MACRO
         AGO   .ZZ                                           EU17 MACRO
.DD      L     R0,&SIZE                SIZE IN BYTES         EU17 MACRO
         LR    R2,R0                   SIZE IN BYTES         EU17 MACRO
         AR    R2,R1                   END ADDRESS           EU17 MACRO
         BCTR  R2,0                    END ADDRESS           EU17 MACRO
.EE      STM   R1,R2,WKWORDS           START-END ADDRESSES   EU17 MACRO
         BAL   R14,MEM_SUB             EDIT SIZE (KB AND MB) EU17 MACRO
         STRING 4X,&MSG,2X,(WKWORDS,4,X),2X,(WKWORDS+4,4,X),2X,   MACROX
               WKCELL1,'K  ',WKCELL2,'M',INTO=LINE           EU17 MACRO
         BAL   R14,SPACE0              NEXT LINE             EU17 MACRO
.ZZ      MEND                                                EU17 MACRO
*----------------------------------------------------------------------
         MACRO                                                    MACRO
&NAME    STATUS &BYTE,&BIT,&MSG,&OFF=*+14,&REG=R4                 MACRO
&NAME    TM    &BYTE,&BIT              check flag                 MACRO
         BZ    &OFF                    OFF, JUMP                  MACRO
&N       SETA  K'&MSG-2                                           MACRO
         MVC   0(&N,&REG),=C&MSG       MOVE MESSAGE TEXT          MACRO
&N       SETA  K'&MSG-1                                           MACRO
         LA    &REG,&N.(,&REG)         BUMP POINTER               MACRO
         MEND                                                     MACRO
*----------------------------------------------------------------------
         EJECT
&VERMOD  SETC  'V3R8M4'      VERSION/RELEASE/MODIFICATION
&NLBRIF  SETA  5000          number of lines for BRIF
&SYSOHC  SETC  'X'           SYSOUT HOLD class                     EU13
&LPGCHK  SETC  '200'         max. lines per page value control     EU15
&PGNCHK  SETC  '90000'       max. starting page value control      EU15
&QRYEP   SETC  '02A'         (ISPQRY-ISPLINK) entry displacement   EU13
         PRINT &EULST                                              EU00
*======================================================================
*        MAIN PROCESSING
*----------------------------------------------------------------------
SHOWMVS  START 0
SHOWMVS  AMODE 31
SHOWMVS  RMODE ANY
         SPLEVEL SET                   SET &SYSSPLV SYMBOL
         AIF   ('&EUJES' EQ 'JES2').JS20A                          EU20
         GBLC  &SYSSPLV                MVS/SP LEVEL
         AGO   .JS20B                                              EU20
.JS20A   PRINT OFF                                                 EU20
         COPY  $HASPGBL                                            EU20
         PRINT ON                                                  EU20
         PRINT &EULST                                              EU20
         AIF   ('&VERSION' EQ 'SP 2.2.0').JS20B                    EU20
&EUJES   SETC  'NONE'                  NOT SUPPORTED/SOLVED        EU20
         MNOTE 0,'--- Sorry, unsupported JES2 Version : &VERSION ' EU20
.JS20B   SAVE  (14,12),,'SHOWMVS &VERMOD '                         EU20
         BALR  R11,0
         USING *,R11
BASEADDR LTR   R1,R1                   SUB_TASK? (ATTACH by BRIF)  @380
         L     R15,=A(SUB_TASK)        prime R15, in case it's yes @380
         BMR   R15                     yes, go there/else continue @380
         L     R0,=A(DYNAML)           LENGTH OF DYNAMIC STORAGE AREA
         GETMAIN RU,LV=(0),BNDRY=PAGE
         ST    R13,4(,R1)
         ST    R1,8(,R13)
         LM    R13,R1,8(R13)           SET R13, RELOAD R1
         USING DYNAM,R13
         ST    R1,PARMADDR             SAVE PARM/CPPL ADDRESS      EU14
         MVC   DYNAML2,=A(DYNAML)      STORE LENGTH FOR FREEMAIN
         BAL   R14,INITIAL             INITIALIZE THE ENVIRONMENT
         LTR   R2,R15                  Valid environment?          EU15
         BNZ   GOBACK                  NO, QUIT
         BAL   R14,SPACE1              BLANK LINE AT THE TOP
         BAL   R14,SPLEVEL             MVS/SP & DFP LEVELS
         BAL   R14,IPLDATA             IPL DATE
         BAL   R14,PRODUCTS            TSO, SPF, DFDSS, HSM, RACF, VTAM
         BAL   R14,SORT                SORT ALIAS
         BAL   R14,PPTDATA             PPT DATA                    EU12
         BAL   R14,RACFDATA            RACF DATA                   EU11
         BAL   R14,SMFDATA             SMF DATA
         BAL   R14,HARDWARE            HARDWARE DATA
         BAL   R14,MEMORY              VIRTUAL MEMORY MAP
         BAL   R14,SRM00               SRM DATA
         BAL   R14,OPENCAT             OPEN CATALOGS
         BAL   R14,PAGEDS              PAGE DATA SETS
         BAL   R14,DUMPDS              DUMP DATA SETS
         BAL   R14,SUBSYSTM            SUB-SYSTEMS
         AIF   ('&EUJES' NE 'JES2').JS21                           EU16
         BAL   R14,JES2DATA            JES2 data                   EU16
.JS21    BAL   R14,TCAS00              TCAS data                   EU16
         AIF   ('&SYSSPLV' LT '2').SPL1X     LOW MACLIB LEVEL, JUMP
         BAL   R14,TSOP00              TSO PARMLIB data
.SPL1X   BAL   R14,USERS               Active jobs, STC, TSU
         BAL   R14,LINKLIST            LNKLSTXX
         BAL   R14,LPALIST             LPALSTXX
         BAL   R14,APFLIST             APF LIST
         BAL   R14,LPACTIV             ACTIVE LPA
         BAL   R14,SVCTABLE            SVC TABLE
         BAL   R14,DEVICES             DEVICE CLASS & UNIT NAMES       X
                                       ON-LINE UNITS
         BAL   R14,CONSOLES            CONSOLES
*
*        Data from user's address space
*
         BAL   R14,RACF00              RACF Profile
         BAL   R14,PROF00              TSO profile
         BAL   R14,TIOT00              TIOT
         BAL   R14,TCB_TREE            TCB TREE
         BAL   R14,JPAQ                JPAQ
         BAL   R14,LOADLIST            LOAD LISTS
         ESTAE 0                       DELETE RECOVERY ENVIRONMENT
         BAL   R14,BRIF                START BROWSE MODE
         LR    R2,R15                  get RC                      EU15
GOBACK   L     R0,DYNAML2              LENGTH OF DYNAMIC STORAGE AREA
         LR    R1,R13                  ADDRESS OF DYNAMIC STORAGE AREA
         L     R13,4(,R13)
         FREEMAIN RU,LV=(0),A=(1)      FREE DYNAMIC STORAGE
         LR    R15,R2                  back RC                     EU15
         RETURN (14,12),RC=(15)
         SPACE 1
GO_PROC  AH    R15,0(R15)              Add offset to routine
         BR    R15                     Enter it
         EJECT
*===================== general macros definitions =====================
         MACRO                                                    MACRO
&NAME    BEGIN_PROC   ,                                           MACRO
         GBLC  &ENDPROC                                           MACRO
&ENDPROC SETC  'PROC&SYSNDX'                                      MACRO
$LTORG   LOCTR                         Addressable code           MACRO
&NAME    BAL   R15,GO_PROC                                        MACRO
         DC    Y(&ENDPROC-*)           Offset to far routine      MACRO
$FARRTNE LOCTR                         Far routines               MACRO
&ENDPROC BALR  R12,0                   Local base                 MACRO
         USING *,R12                                              MACRO
         LA    R15,PEND&SYSNDX         Load retry address         MACRO
         STM   R14,R15,END_PROC        RETURN/RETRY addresses     MACRO
&ENDPROC SETC  'PEND&SYSNDX'           For END_PROC macro         MACRO
         MEND                                                     MACRO
*----------------------------------------------------------------------
         MACRO                                                    MACRO
&NAME    XCALL &EP,&OPRNDS,&ERRET=,&VL=1                          MACRO
         GBLB  &IHBSWA                                            MACRO
&IHBSWA  SETB  (&VL EQ 1)              VL=1                       MACRO
&NAME    LA    R1,WKWORDS              parm list                  MACRO
         IHBOPLST ,&OPRNDS,MF=(E,(1))                             MACRO
         L     R15,&EP                 EP address                 MACRO
         BALR  R14,R15                 invoke routine             MACRO
         AIF   (T'&ERRET EQ 'O').MEND                             MACRO
         LTR   R15,R15                 Any error?                 MACRO
         BNZ   &ERRET                  yes, jump                  MACRO
.MEND    MEND                                                     MACRO
*----------------------------------------------------------------------
         MACRO                                                    MACRO
&NAME    END_PROC      ,                                          MACRO
         GBLC  &ENDPROC                                           MACRO
         AIF   (T'&NAME EQ 'O').NNM1                              MACRO
&NAME    EQU   *                                                  MACRO
.NNM1    ANOP                                                     MACRO
&ENDPROC L     R14,END_PROC            RETURN address             MACRO
         BR    R14                                                MACRO
         MEND                                                     MACRO
*----------------------------------------------------------------------
         MACRO                                                    MACRO
&NAME   #SNAP  &ADDR=(1),&LENGTH=(0)                              MACRO
&NAME    STM   R14,R3,SNAPREGS         save registers             MACRO
         IHBINNRA &ADDR,&LENGTH                                   MACRO
         L     R3,=A(SNAP00)           SNAP routine               MACRO
         BALR  R14,R3               <- go SNAP storage            MACRO
         LM    R14,R3,SNAPREGS         restore registers          MACRO
         MEND                                                     MACRO
*----------------------------------------------------------------------
         EJECT
*======================================================================
*        INITIALIZE THE ENVIRONMENT
*----------------------------------------------------------------------
INITIAL  BEGIN_PROC ,
         ST    R11,BASEREG             for all routines
         MVC   MY_DDN,=CL8'SHOWMVS '   DDN(SHOWMVS) - default      EU15
         MVC   MAXLPG,=F'60'           LPG(60) - default           EU15
         MVC   CURPGN,=F'1'            PGN(1) - default            EU15
         L     R8,CVTPTR               CVT ADDRESS
         USING CVTMAP,R8               PERMANENT ASSIGNMENT
         L     R9,PSATOLD-PSA          MY TCB
         USING TCB,R9
         MVC   JSTCB,TCBJSTCB          THE JOB STEP TCB
         LA    R10,LINES               START OF TABLES
         XR    R0,R0
         LA    R1,L'LINE
         M     R0,=A(&NLBRIF)
         AR    R1,R10
         ST    R1,DYN_CHK
         USING LINE,R10
*
*        Build a translate table for NON-PRINTABLE characters
*
         MVI   TRTPRINT,C' '           PRINTABLE CHARACTERS
         MVC   TRTPRINT+1(L'TRTPRINT-1),TRTPRINT
         MVC   BLANKS,TRTPRINT         a bunch of blanks
         MVC   ZENVIR,BLANKS
         SLR   R15,R15
         SLR   R0,R0
         BAL   R1,INIT10
         DC    X'4A,7,5A,8,6A,6,7A,6'  Special characters
         DC    X'81,9,91,9,A2,8'       Lowercase
         DC    X'C1,9,D1,9,E2,8'       Uppercase
         DC    X'F0,A'                 Digits
         DC    X'00,0'                 end of table
*-> LOOP (1)
INIT10   IC    R15,0(,R1)              FIRST BYTE
         IC    R0,1(,R1)               ITERATIONS
*-> LOOP (2)
INIT11   STC   R15,TRTPRINT(R15)       STORE X'4A' INTO TRTPRINT+X'4A'
         LA    R15,1(,R15)             BUMP INDEX
         BCT   R0,INIT11               NEXT CHARACTER
*-> end LOOP (2)
         LA    R1,2(,R1)               NEXT ENTRY IN TABLE
         CLI   0(R1),0                 END OF TABLE?
         BNE   INIT10                  NEXT STRING
*-> end LOOP (1)
*                                                                  EU14
*        Check if we are entered as TSO command                    EU14
*                                                                  EU14
         L     R2,PARMADDR             get entry R1                EU14
         GETMAIN R,LV=EXTR24L          get 24-bit work area        EU14
         LR    R5,R1                                               EU14
         USING EXTR24,R5                                           EU14
         MVC   EXTR(LEXTR),EXTRP                                   EU14
         XC    ATSO(L'ATSO+L'APSCB),ATSO                           EU14
        EXTRACT ATSO,'S',FIELDS=(TSO,PSB),MF=(E,EXTR)              EU14
* Results : BATCH-exec       TSO-background   TSO-foreground       EU14
*         +----------------+----------------+----------------+     EU14
*   APSCB +     0          +     a.PSCB     +     a.PSCB     +     EU14
*    ATSO +     a.Flag     +     a.Flag     +     a.Flag     +     EU14
*    Flag +     X'00'      +     X'00'      +     x'80'      +     EU14
*         +----------------+----------------+----------------+     EU14
* Initial R1 -> a.CPPL (IKJCPPL) if TSO-command                    EU14
*            -> a.Communication Area (IKJEBECA) if TSO-subcommmand EU14
*            -> a.Parameters List in all other cases               EU14
         L     R1,APSCB                PSCB address                EU14
         LTR   R1,R1                   TSO running?                EU14
         BZ    INIT22                  no, this is a Batch PGM     EU14
         USING CPPL,R2                                             EU14
         USING PSCB,R1                                             EU14
         CLC   PSCBUPT,CPPLUPT         CPPL+4 = UPT address?       EU14
         BNE   *+L'*+10                no                          EU14
         DROP  R1                                                  EU14
         CLC   APSCB(L'APSCB),CPPLPSCB CPPL+8 = PSCB address?      EU14
         BE    INIT20                  yes, TSO command            EU14
         LR    R1,R2                                               EU14
         USING IKJEBECA,R1                                         EU14
         L     R2,CAPTTMP              CPPL address                EU14
         DROP  R1                                                  EU14
         CLC   APSCB(L'APSCB),CPPLPSCB CPPL+8 = PSCB address?      EU14
         BNE   INIT21                  no, not TSO sub-command     EU14
INIT20   L     R1,CPPLCBUF             get command buffer address  EU14
         DROP  R2                                                  EU14
         LA    R4,LINES                build a like PARM field     EU14
         AH    R4,=H'4096'                                         EU14
         ST    R4,PARMADDR                                         EU14
         LA    R0,6(R4)                                            EU14
         ST    R0,0(R4)                                            EU14
         OI    0(R4),X'80'                                         EU14
         XC    4(4,R4),4(R4)                                       EU14
         LH    R3,0(R1)                buffer lentgh               EU14
         SH    R3,=H'4'                prefix length               EU14
         LH    R2,2(R1)                offset to operand(s)        EU14
         SR    R3,R2                   operand(s) length           EU14
         BNP   INIT21                  none, jump                  EU14
         STH   R3,6(R4)                store length                EU14
         LA    R2,4(R1,R2)             start address of operand(s) EU14
         BCT   R3,*+L'*+12             compute machine length      EU14
         MVC   8(*-*,R4),0(R2)         <<executed>>                EU14
         OC    8(*-*,R4),BLANKS        <<executed>>                EU14
         EX    R3,*-12                 move operand(s)             EU14
         EX    R3,*-10                 uppercase operand(s)        EU14
INIT21   L     R4,ATSO                 TSO-flag address            EU14
         TM    0(R4),X'80'             TSO background?             EU14
         BO    INIT23                  no, foreground              EU14
         OI    FSWITCH,TSORUN          yes, remember it            EU13
         B     INIT23                                              EU14
INIT22   OI    FSWITCH,BATCHRUN        remember it                 EU14
INIT23   FREEMAIN R,LV=EXTR24L,A=(R5)  free 24-bit work area       EU14
         DROP  R5                                                  EU14
*
*        Check if there is PARM= supplied and process it
*
         OI    FSWITCH,FLGNOMSG        NOMSG flag (debug)          EU18
         NI    MY_DDN,X'BF'            indicator reset             EU15
         ICM   R2,B'1111',PARMADDR     do we have a parm address?  EU14
         BNP   INIT39                  no, jump                    EU14
         ICM   R1,B'1111',0(R2)        do we have a parm?          EU14
         BNM   INIT39                  no, jump                    EU14
         XR    R2,R2                                               EU01
         ICM   R2,B'0011',0(R1)        is it PARM= specified?      EU01
         BZ    INIT39                  no, jump                    EU01
         LA    R1,2(R1)                start of PARM=field text    EU15
         AR    R2,R1                   end+1 of PARM=field text    EU15
INIT30   CLI   0(R1),C','                                          EU15
         BE    INIT30A                                             EU15
         CLI   0(R1),C' '                                          EU15
         BNE   INIT31                                              EU15
INIT30A  LA    R1,1(R1)                skip separator              EU15
         CLR   R1,R2                                               EU15
         BL    INIT30                                              EU15
         B     INIT39                                              EU15
INIT30B  LA    R1,1(R1)                                            EU15
INIT30C  CLR   R1,R2                                               EU15
         BNL   INIT39                                              EU15
         CLI   0(R1),C','                                          EU15
         BE    INIT30A                                             EU15
         CLI   0(R1),C' '                                          EU15
         BE    INIT30A                                             EU15
         LR    R3,R1                                               EU15
INIT_ERR LA    R15,L'WTOSK1M-1(R3)                                 EU15
         CLR   R15,R2                                              EU15
         BL    *+L'*+4                                             EU15
         LR    R15,R2                                              EU15
         BCTR  R15,0                                               EU15
         SR    R15,R3                                              EU15
         L     R1,=A(WTOSK1)           diagnose error              EU15
         MVC   WK256(WTOSK1L),0(R1)                                EU15
         EX    R15,INIT_MV                                         EU15
         B     INIT98                  QUIT                        EU15
INIT_MV  MVC   WK256+WTOSK1M(*-*),0(R3) <<executed>>               EU15
INIT31   LR    R3,R1                                               EU15
         CLI   0(R1),C'C'              check for CAPS              EU15
         BNE   INIT32                  no                          EU15
         LA    R0,1(R1)                                            EU15
         CLR   R0,R2                                               EU15
         BNL   INIT31A                                             EU15
         CLI   1(R1),C','                                          EU15
         BE    INIT31A                                             EU15
         CLI   1(R1),C' '                                          EU15
         BE    INIT31A                                             EU15
         LA    R0,4(R1)                                            EU15
         CLR   R0,R2                                               EU15
         BH    INIT_ERR                                            EU15
         CLC   0(4,R1),=CL4'CAPS'                                  EU15
         BNE   INIT_ERR                                            EU15
INIT31A  TM    TRTPRINT+X'81',X'40'                                EU15
         BO    INIT_ERR                                            EU15
         OC    TRTPRINT+X'81'(48),BLANKS convert to uppercase      EU15
         LR    R1,R0                                               EU15
         B     INIT30C                                             EU15
INIT32   CLI   0(R1),C'L'              check for LPG(              EU15
         BNE   INIT33                  no                          EU15
         LA    R0,2(R1)                                            EU15
         CLR   R0,R2                                               EU15
         BNL   INIT_ERR                                            EU15
         CLI   1(R1),C'('                                          EU15
         BE    INIT32A                                             EU15
         LA    R0,4(R1)                                            EU15
         CLR   R0,R2                                               EU15
         BNL   INIT_ERR                                            EU15
         CLC   0(4,R1),=CL4'LPG('                                  EU15
         BNE   INIT_ERR                                            EU15
INIT32A  LR    R1,R0                                               EU15
         LA    R4,MAXLPG               target field address        EU15
         LA    R5,=A(20,&LPGCHK)       values control              EU15
         BAL   R14,INIT_S                                          EU15
         B     INIT30B                                             EU15
INIT33   CLI   0(R1),C'P'              check for PGN(              EU15
         BNE   INIT34                  no                          EU15
         LA    R0,2(R1)                                            EU15
         CLR   R0,R2                                               EU15
         BNL   INIT_ERR                                            EU15
         CLI   1(R1),C'('                                          EU15
         BE    INIT33A                                             EU15
         LA    R0,4(R1)                                            EU15
         CLR   R0,R2                                               EU15
         BNL   INIT_ERR                                            EU15
         CLC   0(4,R1),=CL4'PGN('                                  EU15
         BNE   INIT_ERR                                            EU15
INIT33A  TM    CURPGN,X'40'                                        EU15
         BO    INIT_ERR                                            EU15
         LR    R1,R0                                               EU15
         LA    R4,CURPGN               target field address        EU15
         LA    R5,=A(1,&PGNCHK)        values control              EU15
         BAL   R14,INIT_S                                          EU15
         B     INIT30B                                             EU15
INIT34   CLI   0(R1),C'D'              check for DDN(              EU15
         BNE   INIT35                  no                          EU15
         LA    R0,2(R1)                                            EU15
         CLR   R0,R2                                               EU15
         BNL   INIT_ERR                                            EU15
         CLI   1(R1),C'('                                          EU15
         BE    INIT34A                                             EU15
         LA    R0,4(R1)                                            EU15
         CLR   R0,R2                                               EU15
         BNL   INIT_ERR                                            EU15
         CLC   0(4,R1),=CL4'DDN('                                  EU15
         BNE   INIT_ERR                                            EU15
INIT34A  LR    R1,R0                                               EU15
INIT34B  CLI   0(R1),C')'              end of dd-name?             EU15
         BE    INIT34C                 yes                         EU15
         CLI   0(R1),C' '                                          EU15
         BL    INIT_ERR                                            EU15
         LA    R1,1(R1)                                            EU15
         CLR   R1,R2                                               EU15
         BL    INIT34B                                             EU15
         B     INIT_ERR                                            EU15
INIT34C  LR    R4,R1                                               EU15
         LR    R5,R0                                               EU15
         SR    R4,R0                                               EU15
         BNP   INIT30B                 null, retain default        EU15
         LA    R0,L'MY_DDN                                         EU15
         CLR   R4,R0                                               EU15
         BH    INIT_ERR                too long                    EU15
         TM    MY_DDN,X'40'            first time?                 EU15
         BO    INIT_ERR                no                          EU15
         MVC   MY_DDN,BLANKS                                       EU15
         BCT   R4,*+L'*+6                                          EU15
         MVC   MY_DDN(*-*),0(R5)       <<executed>>                EU15
         EX    R4,*-6                  move new DD-name            EU15
         B     INIT30B                                             EU15
INIT35   CLI   0(R1),C'H'              check for HPG(              EU15
         BNE   INIT36                  no                          EU15
         LA    R0,2(R1)                                            EU15
         CLR   R0,R2                                               EU15
         BNL   INIT_ERR                                            EU15
         CLI   1(R1),C'('                                          EU15
         BE    INIT35A                                             EU15
         LA    R0,4(R1)                                            EU15
         CLR   R0,R2                                               EU15
         BNL   INIT_ERR                                            EU15
         CLC   0(4,R1),=CL4'HPG('                                  EU15
         BNE   INIT_ERR                                            EU15
INIT35A  TM    CURPGN,X'40'                                        EU15
         BO    INIT_ERR                                            EU15
         LR    R1,R0                                               EU15
         LA    R0,1(R1)                                            EU15
         CLR   R0,R2                                               EU15
         BH    INIT_ERR                                            EU15
         CLC   0(2,R1),=CL2'N)'                                    EU15
         BE    INIT35B                                             EU15
         CLC   0(2,R1),=CL2'Y)'                                    EU15
         BE    INIT35C                                             EU15
         LA    R0,2(R1)                                            EU15
         CLR   R0,R2                                               EU15
         BH    INIT_ERR                                            EU15
         CLC   0(3,R1),=CL3'NO)'                                   EU15
         BE    INIT35B                                             EU15
         LA    R0,3(R1)                                            EU15
         CLR   R0,R2                                               EU15
         BH    INIT_ERR                                            EU15
         CLC   0(4,R1),=CL4'YES)'                                  EU15
         BE    INIT35C                                             EU15
         B     INIT_ERR                                            EU15
INIT35B  TM    CURPGN,X'80'                                        EU15
         BO    INIT_ERR                                            EU15
         MVI   CURPGN,X'80'+X'40'                                  EU15
INIT35C  LR    R1,R0                                               EU15
         B     INIT30B                                             EU15
INIT36   CLI   0(R1),C'M'              check for MSG(              EU18
         BNE   INIT_ERR                no                          EU18
         LA    R0,2(R1)                                            EU18
         CLR   R0,R2                                               EU18
         BNL   INIT_ERR                                            EU18
         CLI   1(R1),C'('                                          EU18
         BE    INIT36A                                             EU18
         LA    R0,4(R1)                                            EU18
         CLR   R0,R2                                               EU18
         BNL   INIT_ERR                                            EU18
         CLC   0(4,R1),=CL4'MSG('                                  EU18
         BNE   INIT_ERR                                            EU18
INIT36A  LR    R1,R0                                               EU18
         LA    R0,1(R1)                                            EU18
         CLR   R0,R2                                               EU18
         BH    INIT_ERR                                            EU18
         CLC   0(2,R1),=CL2'N)'                                    EU18
         BE    INIT36B                                             EU18
         CLC   0(2,R1),=CL2'Y)'                                    EU18
         BE    INIT36C                                             EU18
         LA    R0,2(R1)                                            EU18
         CLR   R0,R2                                               EU18
         BH    INIT_ERR                                            EU18
         CLC   0(3,R1),=CL3'NO)'                                   EU18
         BE    INIT36B                                             EU18
         LA    R0,3(R1)                                            EU18
         CLR   R0,R2                                               EU18
         BH    INIT_ERR                                            EU18
         CLC   0(4,R1),=CL4'YES)'                                  EU18
         BE    INIT36C                                             EU18
         B     INIT_ERR                                            EU18
INIT36B  OI    FSWITCH,FLGNOMSG        NOMSG flag (debug)          EU18
         B     INIT36D                                             EU18
INIT36C  NI    FSWITCH,255-FLGNOMSG    NOMSG flag (debug)          EU18
INIT36D  LR    R1,R0                                               EU18
         B     INIT30B                                             EU18
*        Process NNN...) routine                                   EU15
*              R14 = link register                                 EU15
*              R0, R15 = work registers                            EU15
*              R1 = scan current pointer                           EU15
*              R2 = scan end+1 pointer                             EU15
*              R3 = current parameter start scan pointer           EU15
*              R4 = target field address                           EU15
*              R5 = address of low-high pair of values control     EU15
INIT_S   XR    R15,R15                 process NNN...) routine     EU15
INIT_S1  CLI   0(R1),C')'              end of value?               EU15
         BE    INIT_S2                 yes                         EU15
         CLI   0(R1),C'0'                                          EU15
         BL    INIT_ERR                                            EU15
         CLI   0(R1),C'9'                                          EU15
         BH    INIT_ERR                                            EU15
         XR    R0,R0                                               EU15
         ICM   R0,B'1000',0(R1)                                    EU15
         SLL   R0,4                                                EU15
         SRL   R0,28                                               EU15
         LTR   R15,R15                                             EU15
         BNP   *+L'*+4                                             EU15
         MH    R15,=H'10'                                          EU15
         AR    R15,R0                                              EU15
         LA    R1,1(R1)                                            EU15
         CLR   R1,R2                                               EU15
         BL    INIT_S1                                             EU15
         B     INIT_ERR                                            EU15
INIT_S2  LTR   R15,R15                                             EU15
         BNPR  R14                     null value, retain default  EU15
         CL    R15,0(R5)               check against low value     EU15
         BL    INIT_ERR                too small                   EU15
         CL    R15,4(R5)               check against high value    EU15
         BH    INIT_ERR                too big                     EU15
         TM    0(R4),X'80'             first time?                 EU15
         BO    INIT_ERR                no                          EU15
         ST    R15,0(R4)               set new value               EU15
         OI    0(R4),X'80'             indicate stored             EU15
         BR    R14                     return                      EU15
INIT39   OI    MY_DDN,X'40'            indicators reset            EU15
         NI    MAXLPG,X'7F'                                        EU15
         TM    CURPGN,X'40'                                        EU15
         BO    *+L'*+4                                             EU15
         NI    CURPGN,X'7F'                                        EU15
*
*        Check if there is a pre-allocated DD
*
         DEVTYPE MY_DDN,WKCELL1        CHECK FOR "SHOWMVS" DD
         LTR   R15,R15                 DD allocated?
         BNZ   *+L'*+8                 no, jump
         OI    FSWITCH,HARDCOPY        yes, remember it's hardcopy
         B     INIT70                                              EU13
         CLC   MY_DDN,=CL8'SHOWMVS '   was DDN=SHOWMVS (default)?  EU15
         BE    INIT49                  yes                         EU15
         L     R1,=A(WTOSK6)           diagnose error              EU15
         MVC   WK256(WTOSK6L),0(R1)                                EU15
         MVC   WK256+WTOSK6N(L'WTOSK6N+L'WTOSK6T),BLANKS           EU15
         MVC   WK256+WTOSK6N(L'MY_DDN),MY_DDN                      EU15
         LA    R15,WK256+WTOSK6N+L'WTOSK6N-1                       EU15
         CLI   0(R15),C' '                                         EU15
         BNE   *+L'*+4                                             EU15
         BCT   R15,*-8                                             EU15
         MVC   1(L'WTOSK6T,R15),WTOSK6T(R1)                        EU15
         B     INIT98                  QUIT                        EU15
INIT49   TM    FSWITCH,BATCHRUN        is it a Batch PGM?          EU14
         BO    INIT60                  yes                         EU14
         TM    FSWITCH,TSORUN          are we in TSO background?   EU14
         BO    INIT60                  yes                         EU14
*
*        TSO foreground: check if we are running under ISPF
*
         L     R9,JSTCB                the job step TCB            EU13
         XR    R3,R3                   indentation index           EU13
INIT50   L     R4,TCBRBP               get the RB pointer          EU13
         USING RBSECT,R4                                           EU13
         CLI   RBSTAB1,RBFTPRB         is this a PRB?              EU13
         BNE   INIT51                  no                          EU13
         XR    R5,R5                   yes                         EU13
         ICM   R5,B'0111',RBCDE1       get the CDE pointer         EU13
         DROP  R4                                                  EU13
         USING CDENTRY,R5                                          EU13
         CLC   CDNAME(3),=C'ISP'       ISPF task?                  EU13
         BNE   INIT51                  no                          EU13
         DROP  R5                                                  EU13
         L     R9,PSATOLD-PSA          yes, restore my TCB         EU13
         B     INIT52                  go process under ISPF       EU13
INIT51   BAL   R14,SCANTCB             get next TCB                EU13
         BNZ   INIT50                  process it if more          EU13
         L     R9,PSATOLD-PSA          end of chain, restore TCB   EU13
         B     INIT59                  no more                     EU13
*
*        We are running under ISPF, so LOAD needed modules
*
INIT52   LOAD  EP=ISPLINK,ERRET=INIT5L load ISPLINK
         ST    R0,ISPLINK              keep the address
         LR    R15,R0                  EP address                  EU13
         LA    R15,X'&QRYEP'(R15)      ISPQRY address              EU13
         BALR  R14,R15                 invoke routine              EU13
         LTR   R15,R15                 ISPF servives available?    EU13
         BNZ   INIT5Q                  no, APF-authorized running  EU13
         XCALL ISPLINK,                ISPF interface                  X
               (=CL8'CONTROL',         ISPF function                   X
               =CL8'ERRORS',           Key-word                        X
               =CL8'RETURN'),          Key-word                        X
               ERRET=INIT5C            in case of a problem
         LA    R0,L'ZENVIR             variable length
         ST    R0,WKCELL1
         XCALL ISPLINK,(=CL8'VCOPY',   ISPF function                   X
               =C'(ZENVIR)',           variable list                   X
               WKCELL1,                length                          X
               ZENVIR,                 area address                    X
               =CL8'MOVE'),            MOVE mode                       X
               ERRET=INIT5V            in case of a problem
         XCALL ISPLINK,                ISPF interface                  X
               (=CL8'ISREDIT',         ISPF function                   X
               =F'5',                  length                          X
               =C'MACRO'),             Key-words                       X
               ERRET=INIT5E            in case we are not in EDIT  EU13
INIT53   OI    CURPGN,X'40'            force no page numbered      EU15
         B     INIT70                  proceed with initialization EU15
INIT5L   LA    R2,=CL8'LOAD'
         B     INIT5X
INIT5Q   LA    R2,=CL8'ISPQRY'
         B     INIT5X
INIT5C   LA    R2,=CL8'CONTROL'
         B     INIT5X
INIT5V   LA    R2,=CL8'VCOPY'
         B     INIT5X
INIT5E   LA    R2,=CL8'ISREDIT'
INIT5X   TM    FSWITCH,FLGNOMSG        NOMSG flag (debug)          EU18
         BO    INITTT                  skip diagnose               EU18
         L     R1,=A(WTOSK2)           diagnose error
         MVC   WK256(WTOSK2L),0(R1)
         MVC   WK256+WTOSK2N(L'WTOSK2N),0(R2)
         CVD   R15,WKCELL1
         MVC   WKCELL1(4),=X'40202120'
         ED    WKCELL1(4),WKCELL1+L'WKCELL1-2
         MVC   WK256+WTOSK2R(L'WTOSK2R),WKCELL1
         WTO   MF=(E,WK256)
INITTT   CLC   =CL8'ISREDIT',0(R2)     was EDIT macro trying?
         BE    INIT53                  yes, can proceed, ISPF only
INIT59   OI    FSWITCH,TSORUN          set as TSO only             EU13
*
*        Allocate DDN=SHOWMVS as a SYSOUT data-set
*
INIT60   LA    R1,WKWORDS+4            WORK AREA ADDRESS           EU13
         ST    R1,WKWORDS              POINTER TO REQUEST BLOCK    EU13
         OI    WKWORDS,S99RBPND                                    EU13
         USING S99RB,R1                                            EU13
         XC    0(S99RBEND-S99RB,R1),0(R1) CLEAR STORAGE            EU13
         MVI   S99RBLN,S99RBEND-S99RB  LENGTH                      EU13
         MVI   S99VERB,S99VRBAL        VERB=ALLOC                  EU13
         MVI   S99FLG11,S99NOCNV+S99NOMNT FLAGS                    EU13
         LA    R2,WKWORDS+(S99RBEND-S99RB)+4  PTR TO T.U. PTRS     EU13
         ST    R2,S99TXTPP                                         EU13
         DROP  R1                                                  EU13
         TM    FSWITCH,TSORUN          are we under TSO?           EU13
         BO    INIT61                  yes                         EU13
         LA    R1,2*4(R2)              SET TEXT UNIT POINTERS      EU13
         ST    R1,0(R2)                                            EU13
         LA    R3,6+L'MY_DDN(R1)                                   EU13
         ST    R3,4(R2)                                            EU13
         OI    4(R2),S99TUPLN                                      EU13
         USING S99TUNIT,R1                                         EU13
         MVC   S99TUKEY,=Y(DALDDNAM)   DDN=SHOWMVS                 EU13
         MVC   S99TUNUM,=Y(1)                                      EU13
         MVC   S99TULNG,=Y(L'MY_DDN)                               EU13
         MVC   S99TUPAR(L'MY_DDN),MY_DDN                           EU13
         DROP  R1                                                  EU13
         USING S99TUNIT,R3                                         EU13
         MVC   S99TUKEY,=Y(DALSYSOU)   SYSOUT=*                    EU13
         MVC   S99TUNUM,=Y(0)                                      EU13
         DROP  R3                                                  EU13
         B     INIT62                                              EU13
INIT61   LA    R1,3*4(R2)              SET TEXT UNIT POINTERS      EU13
         ST    R1,0(R2)                                            EU13
         LA    R3,8(R1)                                            EU13
         ST    R3,4(R2)                                            EU13
         LA    R4,4(R3)                                            EU13
         ST    R4,8(R2)                                            EU13
         OI    8(R2),S99TUPLN                                      EU13
         USING S99TUNIT,R1                                         EU13
         MVC   S99TUKEY,=Y(DALSYSOU)   SYSOUT=X                    EU13
         MVC   S99TUNUM,=Y(1)                                      EU13
         MVC   S99TULNG,=Y(1)                                      EU13
         MVI   S99TUPAR,C'&SYSOHC'                                 EU13
         DROP  R1                                                  EU13
         USING S99TUNIT,R3                                         EU13
         MVC   S99TUKEY,=Y(DALCLOSE)   FREE AT CLOSE               EU13
         MVC   S99TUNUM,=Y(0)                                      EU13
         DROP  R3                                                  EU13
         USING S99TUNIT,R4                                         EU13
         MVC   S99TUKEY,=Y(DALRTDDN)   RETURN DD-NAME              EU13
         MVC   S99TUNUM,=Y(1)                                      EU13
         MVC   S99TULNG,=Y(L'RT_DDN)                               EU13
         MVC   S99TUPAR(L'RT_DDN),BLANKS                           EU13
         DROP  R4                                                  EU13
INIT62   LA    R1,WKWORDS              REQUEST BLOCK ADDRESS       EU13
         DYNALLOC ,                    Allocate SYSOUT=* (Batch)   EU13X
                                       or SYSOUT=X (TSO)           EU13
         LTR   R15,R15                 DYNALLOC WENT OK?
         BNZ   INIT63                  no
         OI    FSWITCH,HARDCOPY        yes, set it like hardcopy
         TM    FSWITCH,TSORUN          are we under TSO?           EU13
         BZ    INIT70                  no                          EU13
         L     R1,=A(WTOSK0)           yes, advice session alloc'd EU13
         WTO   MF=(E,(1))                                          EU13
         B     INIT70
INIT63   L     R1,=A(WTOSK3)           diagnose error
         MVC   WK256(WTOSK3L),0(R1)
         CVD   R15,WKCELL1
         MVC   WKCELL1(4),=X'40202120'
         ED    WKCELL1(4),WKCELL1+L'WKCELL1-2
         MVC   WK256+WTOSK3C(L'WTOSK3C),WKCELL1
         LA    R1,WKWORDS+4            REQUEST BLOCK ADDRESS
         USING S99RB,R1
         UNPK  WK256+WTOSK3R(2*L'S99ERROR+1),S99ERROR(L'S99ERROR+1)
         L     R15,=A(@STRHEXT-240)    (from STRING macro)
         TR    WK256+WTOSK3R(2*L'S99ERROR),0(R15) translate to hex
         MVI   WK256+WTOSK3R+2*L'S99ERROR,C' '
         UNPK  WK256+WTOSK3I(2*L'S99INFO+1),S99INFO(L'S99INFO+1)
         TR    WK256+WTOSK3I(2*L'S99INFO),0(R15) translate to hex
         MVI   WK256+WTOSK3I+2*L'S99INFO,C' '
         DROP  R1
         B     INIT98                  QUIT
INIT70   TIME  DEC
         STM   R0,R1,WKCELL1           HHMMSSHH,00YYDDDF
         L     R1,TCBTIO               TIOT
         USING TIOT1,R1
         MVC   EDQNAME,TIOCNJOB
         DROP  R1
         MVI   EDRNAME,C'T'
         UNPK  EDRNAME+1(9),WKCELL1(5)
         MVI   EDRNAME+9,C'.'
         MVI   EDRNAME+10,C'D'
         UNPK  EDRNAME+11(5),WKCELL1+5(3)
         OI    EDRNAME+15,C'0'
         TM    CURPGN,X'40'                                        EU15
         BO    *+L'*+10                                            EU15
         MVC   WKCELL2,=CL8'  / Page'                              EU15
         B     *+L'*+6                                             EU15
         MVC   WKCELL2,BLANKS                                      EU15
         ST    R10,TLNEPT              retain TITLE line pointer   EU15
         STRING (TITLE,TITLE_L),INTO=LINE
         STRING (WKCELL1+4,P,YY/MM/DD),INTO=(LINE+60,24),          EU15X
               2X,(WKCELL1,1,X),':',(WKCELL1+1,1,X),WKCELL2        EU15
PGNEPT   EQU   60+24,6                 page count edit pointer     EU15
*        OC    LINE,BLANKS             X'00' -> X'40'              EU15
         BAL   R14,SPACE0              First line is title
         L     R2,=A(RECOVERY)         RECOVERY ROUTINE            EU15
         ESTAE (R2),                   RECOVERY ROUTINE                X
               CT,                     CREATE                          X
               PARAM=DYNAM,            PARAM FOR RECOVERY ROUTINE      X
               MF=(E,ESTAEL)
         L     R1,CVTLINK              DCB FOR SYS1.LINKLIB
         LA    R2,=C'IEFEB4UV'         device type scan rtne
         LOAD  EPLOC=(R2),DCB=(1)      GET ADDRESS OF IEFEB4UV ROUTINE
         ST    R0,IEFEB4UV             KEEP ADDRESS
         DELETE EPLOC=(R2)             IEFEB4UV is in LPA
         SLR   R15,R15                 RC=00
         B     INIT99                  QUIT                        EU15
INIT98   WTO   MF=(E,WK256)                                        EU15
         LA    R15,16                  pass back RC for error      EU15
INIT99   END_PROC
         SPACE 1
EXTRP   EXTRACT *-*,'S',MF=L                                       EU14
         DROP  R12                     END_PROC
         EJECT
*======================================================================
*        PRINT CVT FIELDS
*----------------------------------------------------------------------
SPLEVEL  BEGIN_PROC ,
*        #SNAP ADDR=0,LENGTH=12                                  <SNAP>
         STRING 'Operating System:',INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
*
*        MVS/SP1 OR MVS/SP2
*
         LR    R6,R8                   A(CVTMAP)
         SH    R6,=Y(CVTMAP-CVTFIX)    SUBTRACT PREFIX LENGTH
         USING CVTFIX,R6
         STRING '  CVTPRODN: ',CVTPRODN,'  CVTPRODI: ',CVTPRODI,       X
               '  CVTRELNO: ',CVTRELNO,INTO=LINE
         BAL   R14,SPACE0              NEXT LINE
         STRING '   CVTDCB: X''',(CVTDCB,1,X),                         X
               ''' - CVTDCB FLAGS: (12-CVT8AOS2)',INTO=LINE
         BAL   R14,SPACE0              NEXT LINE
         STRING '       80-CVTMVSE 40-CVT1SSS 20-CVT2SPS 10-CVT4MS1 04-X
               CVT4MPS 02-CVT6DAT 01-CVTMVS2',INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
         AIF   ('&SYSSPLV' LT '2').SPL2X     LOW MACLIB LEVEL, JUMP
         L     R7,CVTDFA               DATA FACILITIES AREA
         USING DFA,R7
         MVC   WKCELL1(6),=X'40204B204B20' ' X.X.X'
         ED    WKCELL1(6),DFAREL       X'3200'
         CLI   CVTDCB,CVTMVSE+CVTMVS2+CVT8AOS2 DO WE HAVE MVS/XA?
         BE    SPLVLXA                 YES
         AIF   ('&SYSSPLV' LT '3').SPL2A     NOT MVS/ESA MACLIB, JUMP
         TM    CVTDCB,CVTOSEXT         DO WE HAVE OSLVL?
         BO    SPLVLESA                NO, JUMP
.SPL2A   STRING '  DFP LEVEL:',(WKCELL1,6),INTO=LINE
         B     SPLVL99
*
*        MVS/XA (SP2) + JES2 LEVEL
*
SPLVLXA  STRING '  MVS/XA ',(CVTPRODN+2,6),                            X
               ' - DFP LEVEL:',(WKCELL1,6),INTO=LINE
         L     R1,CVTJESCT             JES COMM TABLE
         L     R5,JESSSCT-JESCT(,R1)   FIRST JSCVT
         USING SSCT,R5
         B     SPLVL11                 FIRST TIME, JUMP
*-> LOOP
SPLVL10  ICM   R5,B'1111',SSCTSCTA     NEXT SSCVT
         BZ    SPLVL99                 JES2 NOT FOUND, QUIT
SPLVL11  CLC   =C'JES2',SSCTSNAM       IS THIS JES2?
         BNE   SPLVL10                 NO, LOOP
*-> end LOOP
         BAL   R14,SPACE0              NEXT LINE
         L     R6,SSCTSUSE             A(HASPSSSM LEVEL)
         STRING 2X,SSCTSNAM,' Level: ',((R6),20),INTO=LINE
         LA    R0,25
         LA    R1,LINE+35
         CLI   0(R1),C' '
         BNL   *+L'*+4
         MVI   0(R1),C' '
         BCTR  R1,0
         BCT   R0,*-14
         DROP  R5
         AIF   ('&SYSSPLV' LT '3').SPL2X     NOT MVS/ESA MACLIB, JUMP
         B     SPLVL99
*
*        MVS/ESA (SP3 OR SP4) + JES2 LEVEL
*
SPLVLESA STRING '  MVS/ESA ',(CVTPRODN+2,6),                           X
               '      MVS/DFP ',(WKCELL1,6),                           X
               '       CVTOSLVL: ',                                    X
               (CVTOSLVL,1,X),1X,(CVTOSLVL+1,1,X),1X,                  X
               (CVTOSLVL+2,1,X),1X,(CVTOSLVL+3,1,X),1X,                X
               (CVTOSLVL+4,1,X),1X,(CVTOSLVL+5,1,X),1X,                X
               (CVTOSLVL+6,1,X),1X,(CVTOSLVL+7,1,X),1X,                X
               INTO=LINE
         L     R1,CVTJESCT             JES COMM TABLE
         L     R5,JESSSCT-JESCT(,R1)   FIRST JSCVT
         USING SSCT,R5
         B     SPLVL31                 FIRST TIME, JUMP
*-> LOOP
SPLVL30  ICM   R5,B'1111',SSCTSCTA     NEXT SSCVT
         BZ    SPLVL99                 JES2 NOT FOUND, QUIT
SPLVL31  CLC   =C'JES2',SSCTSNAM       IS THIS JES2?
         BNE   SPLVL30                 NO, LOOP
*-> end LOOP
         L     R6,SSCTSUS2             A(HCCT)
         BAL   R1,SPLVL35              BRANCH AROUND TABLE
JLVTBES  DC    X'008C'             +00 OFFSET TO CCTSSVT  (313)    @363
         DC    X'010C'             +02 OFFSET TO CCTNDENM (313)    @363
         DC    X'01F0'             +04 OFFSET TO CCTSNV   (313)    @363
JLVTBEL  EQU   *-JLVTBES
OFFSSV   EQU   0,2
OFFNDE   EQU   2,2
OFFSNV   EQU   4,2
         DC    X'008C'             +00 OFFSET TO CCTSSVT  (410)    @363
         DC    X'0120'             +02 OFFSET TO CCTNDENM (410)    @363
         DC    X'021C'             +04 OFFSET TO CCTSNV   (410)    @363
         DC    X'009C'             +00 OFFSET TO CCTSSVT  (420)
         DC    X'0130'             +02 OFFSET TO CCTNDENM (420)
         DC    X'0244'             +04 OFFSET TO CCTSNV   (420)
         DC    X'0000'                 END OF TABLE
*-> LOOP
SPLVL35  LH    R2,OFFSSV(,R1)          GET OFFSET TO SSVT ADDRESS
         LTR   R2,R2                   END OF TABLE?
         BZ    SPLVL99                 YES, QUIT
         ALR   R2,R6                   CHANGE OFFSET TO ADDRESS
         CLC   SSCTSSVT,0(R2)          IS THIS MY JES2?
         BNE   SPLVL36                 NO, JUMP
         LH    R2,OFFSNV(,R1)          GET OFFSET TO CCTSNV
         ALR   R2,R6                   R2=>SUBSYS NAME
         CLC   SSCTSNAM,0(R2)          IS THIS MY JES2?
         BE    SPLVL40                 YES, EXIT LOOP
SPLVL36  LA    R1,JLVTBEL(,R1)         NEXT ENTRY
         B     SPLVL35                 TRY NEXT ENTRY IN TABLE
*-> end LOOP
SPLVL40  LH    R7,OFFNDE(,R1)          GET OFFSET TO CCTNDENM
         ALR   R7,R6                   R7=>OWN NODE
         BAL   R14,SPACE2              BLANK LINE
         CLI   8(R6),C' '              IS THIS JES2 420?          @363
         BE    SPLVL41                 NO, JUMP                   @363
         STRING 2X,((R2),4),' Level: ',(4(R2),4),                      X
               '    HASPSSSM: ',(8(R6),8),' at ',(16(R6),4,X),         X
               '    NJE Node: ',((R7),8),                              X
               INTO=LINE
         B     SPLVL99
SPLVL41  STRING 4X,((R2),4),' Level: ',(4(R2),4),                 @363 X
               '    NJE NODE: ',((R7),8),                         @363 X
               INTO=LINE                                          @363
         DROP  R5
.SPL2X   ANOP
SPLVL99  BAL   R14,SPACE2              BLANK LINE
         END_PROC
         DROP  R6,R7
         DROP  R12                     END_PROC
         EJECT
*======================================================================
*        IPL DATA
*----------------------------------------------------------------------
IPLDATA  BEGIN_PROC ,
         STRING 'Last IPL:',INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
         L     R6,CVTSMCA              SMF SMCA
         USING SMCABASE,R6
*
*        DETERMINE DAY OF THE WEEK                                 @363
*
         UNPK  WKCELL1(3),SMCAIDTE+1(2)  YY FROM YY.DDD           YY
         PACK  WKCELL1,WKCELL1(2)        YY FROM YY.DDD           YY
         CVB   R0,WKCELL1              R0=YEAR
         LR    R1,R0                   R1=YEAR
         BCTR  R1,0                    R1=YEAR-1
         SRL   R1,2                    DIVIDE YEAR-1 BY 4
         AR    R1,R0                   ADD RESULT TO NUMBER OF YEARS
         ZAP   WKCELL1,SMCAIDTE+2(2)   DDD FROM YY.DDD
         SLR   R14,R14                 PREPARE DIVISION
         CVB   R15,WKCELL1             R15=DAY
         BCTR  R15,0                   R15=DAY-1
         AR    R15,R1                  ADD NUMBER OF YEARS
         D     R14,=F'7'               DIVIDE BY NUM OF DAYS IN WEEK
         MH    R14,=H'9'               MULT BY LEN OF DAY-OF-THE-WEEK
         LA    R7,TABLEDAY(R14)        POINT TO DAY-OF-THE-WEEK
*
*        CALCULATE NUMBER OF DAYS SINCE LAST IPL
*
*        LA    R7,=PL4'1'                                          ****
*        LA    R8,=PL4'99365'                                      ****
*        USING CVTDATE,R7                                          ****
*        USING SMCAIDTE,R8                                         ****
         ZAP   WKCELL2,CVTDATE         TODAY'S DATE
         BAL   R14,CHECK0              CHECK NEXT LINE AVAILABLE
         STRING '(Today)',INTO=NEXTLINE
         SP    WKCELL2,SMCAIDTE        DIFFERENCE IN DAYS
         BZ    IPLDATA6                TODAY, JUMP
         BP    IPLDATA4                SAME CENTURY, JUMP
         AP    WKCELL2,=P'100000'      COMPENSATE FOR MILLENIUM CHANGE
IPLDATA4 CLC   SMCAIDTE(2),CVTDATE     SAME YEAR/CENTURY?
         BE    IPLDATA5                YES, JUMP
         SP    WKCELL2,=P'635'         DAYS DIFFERENCE (NORMAL YEAR)
         TM    SMCAIDTE+1,X'01'        LEAP YEAR?
         BO    IPLDATA5                NO, JUMP
         TM    SMCAIDTE+1,X'12'        LEAP YEAR? (88 92 96 00)
         BM    IPLDATA5                NO, JUMP
         AP    WKCELL2,=P'1'           COMPENSATE FOR LEAP YEAR
IPLDATA5 STRING '(Yesterday)',INTO=NEXTLINE
         CP    WKCELL2,=P'1'           WAS IT YESTERDAY?
         BE    IPLDATA6                YES, JUMP
         STRING '(',(WKCELL2,P,L0),' days ago)',INTO=NEXTLINE
*
*        CONVERT IPL TIME FROM BINARY TO HH:MM                     @364
*
IPLDATA6 SLR   R0,R0
         L     R1,SMCAITME             IPL TIME (BINARY)
         D     R0,=F'00360000'         GET HOURS
         LR    R2,R1                   HH
         LR    R1,R0                   REMAINDER
         SLR   R0,R0
         D     R0,=F'6000'             GET MINUTES IN R1
IPLDATA7 STRING '  Date:  ',((R7),9,T),1X,                             X
               (SMCAIDTE,P,YY/MM/DD),1X,(NEXTLINE,,T),                 X
               '     Time:  ',((R2),,R2Z),':',((R1),,R2Z),         @364X
               '     Julian:  ',(SMCAIDTE+1,1,X),'.',              @364X
               (SMCAIDTE+2,P,R3Z),INTO=LINE                        @364
         BAL   R14,SPACE0              NEXT LINE
         L     R7,CVTSYSAD             IPL UCB
         USING UCBOB,R7
         L     R3,CVTEXT2              CVT EXTENSION
         USING CVTXTNT2,R3
         L     R15,CVTASMVT            POINT TO ASM VECTOR TABLE
         LA    R4,=C'NO '              CLPA=NO
         TM    ASMFLAG2-ASMVT(R15),ASMQUICK   QUICK START?
         BO    *+L'*+4                 YES, JUMP
         LA    R4,=C'YES'              NO, CLPA=YES
         STRING  '  From:  ',UCBVOLI,'/',UCBNAME,                      X
               '     NUC Id:  ',CVTNUCLS,                              X
               '     CLPA:  ',((R4),3),                                X
               '     CVTUSER:  ',(CVTUSER,,X),INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
         END_PROC
TABLEDAY DC    C'Monday   Tuesday  WednesdayThursday '
         DC    C'Friday   Saturday Sunday   ',0H'0'
         DROP  R3,R6,R7
         DROP  R12                     END_PROC
         EJECT
*======================================================================
*        TSO/E, DFDSS, HSM, RACF, VTAM
*----------------------------------------------------------------------
PRODUCTS BEGIN_PROC ,
         STRING 'System Software:',INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
         L     R7,CVTTVT               TSO VECTOR TABLE
         USING TSVT,R7
         PACK  WKCELL1,TSVTLREL        2-DIGIT RELEASE NUMBER
         STRING '  TSO/E Level:   ',TSVTLVER,'.',(WKCELL1,P,L0),'.',   X
               TSVTLMOD,4X,(ZENVIR,8),INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
         MVC   WK256(2+2),=Y(1,80)     BLDL HDR
         MVC   WK256+4(8),=C'ADRRELVL' EPNAME
         L     R2,CVTLINK              LINKLIST DCB
         BLDL  (R2),WK256              ISSUE BLDL
         LTR   R15,R15
         BNZ   DFDSS8                  NOT FOUND, JUMP
         LOAD  DE=WK256+4,DCB=(R2)     LOAD ADRRELVL, IF PRESENT
         LR    R1,R0                   PASS EP ADDR
         STRING '  DF/DSS Level:  ',((R1),H,L),'.',(2(R1),FL1,L),'.',  X
               (3(R1),FL1,L),INTO=LINE
         DELETE DE=WK256+4             I'M DONE WITH IT
DFDSS8   BAL   R14,SPACE2              BLANK LINE
         ICM   R7,B'1111',CVTHSM       HSM VECTOR TABLE
         BZ    RACFVM00
         USING MQCT,R7
         CLI   MQCTID,C'Q'             CHECK CB ID
         BNE   RACFVM00
         STRING '  DF/HSM Level: ',MQCTVER,'.',MQCTREL,'.',MQCTMOD,    X
               INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
RACFVM00 ICM   R7,B'1111',CVTRAC       RACF VECTOR TABLE
         BZ    RACFVM99
         USING RCVT,R7                                             EU07
         LA    R1,RCVTVRMN                                         EU07
         LA    R2,RCVTVRMN+1                                       EU07
         LA    R3,RCVTVRMN+3                                       EU07
         STRING '  RACF Level:    ',((R1),1),'.',((R2),2),         EU07X
               '.',((R3),1),INTO=LINE                              EU07
         BAL   R14,SPACE2              BLANK LINE
RACFVM99 L     R3,PSAATCVT-PSA         ADDR OF VTAM CVT
         STRING '  VTAM Level:    ',((R3),4),                          X
               '    PSAATCVT: ',((R3),,X),INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
         L     R1,CVTJESCT             JES COMM TABLE
         L     R5,JESSSCT-JESCT(,R1)   FIRST JSCVT
         USING SSCT,R5
*-> LOOP
DB2SYS22 CLC   =C'DSN ',SSCTSNAM       IS THIS DB2?
         BE    DB2SYS70                YES, EXIT LOOP
         ICM   R5,B'1111',SSCTSCTA     NEXT SSCVT
         BNZ   DB2SYS22
*-> end LOOP
         B     DB2SYS99
DB2SYS70 ICM   R6,B'1111',SSCTSUSE     DB2'S ERLY
         BZ    DB2SYS99                INACTIVE SUB-SYSTEM
         CLC   =C'ERLY',4(R6)          IS THIS REALLY DB2?
         BNE   DB2SYS99
         STRING '  DB2 Level:     ',(92(R6),4+5),INTO=LINE   5740XYR01
         BAL   R14,SPACE2              BLANK LINE
DB2SYS99 END_PROC
         DROP  R5,R7
         DROP  R12                     END_PROC
         EJECT
*======================================================================
*        IDENTIFY WHAT SORT'S REAL NAME IS
*----------------------------------------------------------------------
SORT     BEGIN_PROC ,
         LOAD  EPLOC=SORTNAME          LOAD THE SORT UTILITY
         L     R4,TCBLLS               POINT TO LAST LLE IN CHAIN
         USING LLE,R4
*-> LOOP
SORT21   L     R6,LLECDPT              CDE PTR
         USING CDENTRY,R6
         CLC   SORTNAME,CDNAME         IS IT SORT'S LLE?
         BE    SORT33                  YES, JUMP
         ICM   R4,B'1111',LLECHN       CHECK FOR END OF CHAIN
         BNZ   SORT21                  LOOP THROUGH LOAD LIST
*-> end LOOP
         B     SORT99                  SOMETHING'S WRONG HERE
SORTNAME DC    CL8'SORT'               NAME OF SORT PGM (ANY SORT)
SORT33   L     R5,CDENTPT              ENTRY POINT ADDRESS
         SLL   R5,1                    ELIMINE AMODE BIT
         SRL   R5,1
         TM    CDATTR,CDMIN            IS THIS A MINOR CDE?
         BNO   SORT36                  NO, JUMP
         L     R6,CDXLMJP              YES, POINT TO MAJOR CDE/LPDE
SORT36   L     R7,LPDEXTAD-LPDE(,R6)   LOAD POINT ADDRESS (IF LPDE)
         TM    CDATTRB,CDELPDE         IS THIS A LPDE?
         BO    SORT41                  YES, JUMP
         L     R7,CDXLMJP              POINT TO XTLST
         L     R7,XTLMSBAD-XTLST(R7)   LOAD POINT
SORT41   SR    R5,R7                                               EU19
         BNZ   SORT41X                                             EU19
         ICM   R4,B'1111',0(R7)                                    EU19
         SRDL  R4,12                                               EU19
         CL    R4,=A(X'00047F0F')                                  EU19
         BNE   *+L'*+8                                             EU19
         SRL   R5,20                                               EU19
         B     SORT41X                                             EU19
         LA    R5,032                  DEFAULT TO 2 LINES          EU19
SORT41X  STRING '  SORT''s true name is ',CDNAME,                  EU19X
               '   (First ',((R5),,L0),' Bytes Follow)',INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
*
*        DUMP THE FIRST N-BYTES OF THE "SORT" FOR IDENTIFICATION
*
         LA    R3,15(R5)               LOOP COUNTER
         SRL   R3,4
         LR    R4,R3
         SLL   R4,4
         SLR   R4,R5
         LR    R5,R3
*-> LOOP
SORT42L  STRING 4X,((R7),4,X),1X,(4(R7),4,X),1X,(8(R7),4,X),1X,        X
               (12(R7),4,X),3X,((R7),16),INTO=LINE
         BCT   R5,SORT43
         LTR   R4,R4
         BNP   SORT43
         XR    R1,R1
         LR    R0,R1
         IC    R0,SORTDS-1(R4)
         IC    R1,SORTLG-1(R4)
         LA    R5,LINE
         ALR   R5,R0
         EX    R1,*+L'*+10
         LA    R5,LINE+58
         SLR   R5,R4
         BCT   R4,*+L'*+6
         MVC   0(*-*,R5),BLANKS        <<EXECUTED>>
         EX    R4,*-6
SORT43   TR    LINE,TRTPRINT           GET RID OF NON-PRINTABLE CHARS
         BAL   R14,SPACE0              NEXT LINE
         LA    R7,016(,R7)             BUMP POINTER
         BCT   R3,SORT42L
*-> end LOOP
         BAL   R14,SPACE1              BLANK LINE
SORT99   DELETE EPLOC=SORTNAME         NOW GET RID OF IT
         END_PROC
         DROP  R4,R6
SORTDS   DC    AL1(37,35,33,31,28,26,24,22,19,17,15,13,10,8,6)
SORTLG   DC    AL1(1,3,5,7,10,12,14,16,19,21,23,25,28,30,32)
         DROP  R12                     END_PROC
         EJECT ,                                                   EU12
*==================================================================EU12
*        DISPLAY PPT DATA                                          EU12
*        (this display requires APF-authorization)                 EU12
*------------------------------------------------------------------EU12
PPTDATA  BEGIN_PROC ,                                              EU12
         TESTAUTH FCTN=1               check APF status            EU12
         LTR   R15,R15                 may I use MODESET?          EU12
         BNZ   PPTDT99                 no, exit                    EU12
         L     R1,CVTJESCT             JES COMM table              EU12
         ICM   R7,B'1111',JESPPT-JESCT(R1) PPT pointer             EU12
         BZ    PPTDT99                 none                        EU12
         USING PPT,R7                                              EU12
         MODESET MODE=SUP,KEY=ZERO     supervisor mode and key 0   EU12
         XR    R6,R6                                               EU12
         ICM   R6,B'0011',PPTUSED      number of used PPT entries  EU12
         BZ    PPTDT1                  none                        EU12
         LR    R4,R7                                               EU12
         LR    R3,R6                   number of used PPT entries  EU12
         MH    R3,PPTENTLN             multiply by entry length    EU12
         AH    R3,PPTHDRLN             add header length           EU12
         LR    R5,R3                                               EU12
         LR    R0,R3                   <- get length of work area  EU12
         BAL   R14,WORKADDR            <- get address of work area EU12
         LR    R2,R1                                               EU12
         MVCL  R2,R4                   copy PPT in work area       EU12
         LR    R7,R1                                               EU12
PPTDT1   MODESET MODE=PROB,KEY=NZERO   Switch back to key 8        EU12
         LTR   R6,R6                   PPT empty?                  EU12
         BNP   PPTDT99                 yes                         EU12
         STRING 'PPT - Programs Properties Table:',INTO=LINE       EU12
         BAL   R14,SPACE2              blank line                  EU12
         XR    R3,R3                                               EU12
         ICM   R3,B'0011',PPTENTS      total number of PPT entries EU12
         STRING '  PPT entries number - Total: ',((R3),,L),        EU12X
               ' - Used: ',((R6),,L),INTO=LINE                     EU12
         BAL   R14,SPACE2              blank line                  EU12
         STRING '  PGM-name  NC  SK  NS  PR  ST  ND  BP  Key  2P  1P  NX
               P  CPU-mask  Origin',INTO=LINE                      EU12
         BAL   R14,SPACE2              blank line                  EU12
         LR    R5,R7                                               EU12
         AH    R5,PPTHDRLN             first PPT entry             EU12
         XR    R3,R3                                               EU12
         ICM   R3,B'0011',PPTENTLN     entry length                EU12
         USING PPT1,R5                                             EU12
PPTDT10  MVC   LINE,BLANKS             reset line                  EU12
         MVC   LINE+2(L'PPTNAME),PPTNAME                           EU12
         TM    PPTBYTE1,PPTNCNCL                                   EU12
         BZ    *+L'*+4                                             EU12
         MVI   LINE+L'PPTNAME+5,C'x'                               EU12
         TM    PPTBYTE1,PPTSKEY                                    EU12
         BZ    *+L'*+4                                             EU12
         MVI   LINE+L'PPTNAME+9,C'x'                               EU12
         TM    PPTBYTE1,PPTNSWP                                    EU12
         BZ    *+L'*+4                                             EU12
         MVI   LINE+L'PPTNAME+13,C'x'                              EU12
         TM    PPTBYTE1,PPTPRIV                                    EU12
         BZ    *+L'*+4                                             EU12
         MVI   LINE+L'PPTNAME+17,C'x'                              EU12
         TM    PPTBYTE1,PPTSYSTK                                   EU12
         BZ    *+L'*+4                                             EU12
         MVI   LINE+L'PPTNAME+21,C'x'                              EU12
         TM    PPTBYTE1,PPTNDSI                                    EU12
         BZ    *+L'*+4                                             EU12
         MVI   LINE+L'PPTNAME+25,C'x'                              EU12
         TM    PPTBYTE1,PPTNOPAS                                   EU12
         BZ    *+L'*+4                                             EU12
         MVI   LINE+L'PPTNAME+29,C'x'                              EU12
         TM    PPTBYTE1,PPTSKEY        System key?                 EU12
         BZ    PPTDT11                 no, skip                    EU12
         XR    R2,R2                   yes, get key                EU12
         IC    R2,PPTKEY                                           EU12
         SRL   R2,4                                                EU12
         CVD   R2,WKCELL1                                          EU12
         MVC   WKCELL1(4),=X'40202120'                             EU12
         ED    WKCELL1(4),WKCELL1+L'WKCELL1-2                      EU12
         MVC   LINE+L'PPTNAME+33(2),WKCELL1+2                      EU12
PPTDT11  TM    PPTPUBYT,PPT2LPU                                    EU12
         BZ    *+L'*+4                                             EU12
         MVI   LINE+L'PPTNAME+38,C'x'                              EU12
         TM    PPTPUBYT,PPT1LPU                                    EU12
         BZ    *+L'*+4                                             EU12
         MVI   LINE+L'PPTNAME+42,C'x'                              EU12
         TM    PPTPUBYT,PPTN2LP                                    EU12
         BZ    *+L'*+4                                             EU12
         MVI   LINE+L'PPTNAME+46,C'x'                              EU12
         UNPK  LINE+L'PPTNAME+51(2*L'PPTCPUA+1),PPTCPUA(L'PPTCPUA+1)
         L     R15,=A(@STRHEXT-240)    (from STRING macro)         EU12
         TR    LINE+L'PPTNAME+51(2*L'PPTCPUA),0(R15) translate to hex
         MVI   LINE+L'PPTNAME+2*L'PPTCPUA+51,C' '                  EU12
         TM    PPTORIG,PPTDEFLT        origin is IBM?              EU12
         BZ    *+L'*+6                 no                          EU12
         MVC   LINE+L'PPTNAME+2*L'PPTCPUA+57(3),=C'IBM'            EU12
         BAL   R14,SPACE0              next line                   EU12
         AR    R5,R3                   next entry                  EU12
         BCT   R6,PPTDT10              loop if more                EU12
         STRING '    SCHEDxx keyword synonyms list:',INTO=LINE     EU12
         BAL   R14,SPACE0              next line                   EU12
         STRING '      NC=NOCANCEL SK=KEY(x)   NS=NOSWAP   PR=PRIV     X
               ST=SYST     ND=NODSI',INTO=LINE                     EU12
         BAL   R14,SPACE0              next line                   EU12
         STRING '      BP=NOPASS   2P=SPREF    1P=LPREF    NP=NOPREF', X
               INTO=LINE                                           EU12
         BAL   R14,SPACE0              next line                   EU12
         STRING '      CPU mask=AFF(y) - FFFF is AFF(NONE)',       EU12X
               INTO=LINE                                           EU12
PPTDT90  BAL   R14,SPACE2              blank line                  EU12
PPTDT99  END_PROC ,                                                EU12
         DROP  R5,R7                                               EU12
         DROP  R12                     END_PROC                    EU12
         EJECT ,                                                   EU11
*==================================================================EU11
*        DISPLAY RACF DATA                                         EU11
*------------------------------------------------------------------EU11
RACFDATA BEGIN_PROC ,                                              EU11
         ICM   R7,B'1111',CVTRAC       RACF Vector table           EU11
         BZ    RACFDT99                                            EU11
         USING RCVT,R7                                             EU11
         STRING 'RACF Data:',INTO=LINE                             EU11
         BAL   R14,SPACE2              blank line                  EU11
         TM    RCVTSTAT,RCVTRNA        inactive?                   EU11
         BZ    RACFDT1                 no, active                  EU11
         STRING '  RACF is inactive',INTO=LINE                     EU11
         B     RACFDT90                                            EU11
RACFDT1  TM    RCVTFLGS,RCVTROFF       deactivated?                EU11
         BZ    RACFDT10                no, active                  EU11
         STRING '  RACF has been deactivated by a RVARY command',  EU11X
               INTO=LINE                                           EU11
         B     RACFDT90                                            EU11
RACFDT10 L     R6,RCVTDSDT             DSN table                   EU11
         USING DSDT,R6                                             EU11
         L     R2,DSDTNUM              number of table entries     EU11
         SLL   R2,1                    each consists of 2 entries  EU11
         LA    R5,DSDTENTY             start of table              EU11
         USING DSDE,R5                                             EU11
RACFDT11 L     R3,DSDERUCB             UCB pointer                 EU11
         USING UCBOB,R3                                            EU11
         STRING 2X,DSDEDSN,1X,UCBVOLI,INTO=LINE                    EU11
         LA    R4,LINE+2(R15)          status                      EU11
         STATUS DSDESTAT,DSDEACTV,'Active'                         EU11
         STATUS DSDESTAT,DSDEMSTR,'Master'                         EU11
         STATUS DSDESTAT,DSDEPRIM,'Primary'                        EU11
         TM    DSDESTAT,DSDEPRIM       was primary?                EU11
         BO    *+L'*+6                 yes                         EU11
         MVC   0(7,R4),=C'Back-up'     no, show it                 EU11
         BCT   R2,*+L'*+4              any other entry?            EU11
         B     RACFDT12                no                          EU11
         BAL   R14,SPACE0              yes, next line              EU11
         LA    R5,L'DSDENT(R5)         bump to next                EU11
         B     RACFDT11                                            EU11
RACFDT12 CLI   RCVTUADS,0              TSO?                        EU11
         BE    RACFDT20                no                          EU11
         BAL   R14,SPACE0              yes, next line              EU11
         STRING 2X,RCVTUADS,1X,RCVTUVOL,INTO=LINE                  EU11
         LA    R4,LINE+2(R15)          show it                     EU11
         MVC   0(3,R4),=C'TSO'                                     EU11
RACFDT20 BAL   R14,SPACE2              blank line                  EU11
         XR    R2,R2                                               EU11
         IC    R2,RCVTPINV                                         EU11
         STRING '  Password interval in days = ',((R2),,L),        EU11X
               INTO=LINE                                           EU11
         BAL   R14,SPACE0              next line                   EU11
         XR    R2,R2                                               EU11
         IC    R2,RCVTRVOK                                         EU11
         STRING '  Number of tries before ID revoked = ',          EU11X
               ((R2),,L),INTO=LINE                                 EU11
         ICM   R6,B'1111',RCVTSPT      ICHRIN03 table              EU11
         BZ    RACFDT30                none                        EU11
         XR    R5,R5                                               EU11
         ICM   R5,B'1100',0(R6)                                    EU11
         SLL   R5,1                    clear out format flag       EU11
         SRL   R5,17                                               EU11
         LTR   R5,R5                                               EU11
         BZ    RACFDT30                none                        EU11
         BAL   R14,SPACE2              blank line                  EU11
         STRING '  ICHRIN03 - Started procedures table:',          EU11X
               INTO=LINE                                           EU11
         LA    R3,2(R6)                start of entries            EU11
RACFDT21 BAL   R14,SPACE0              next line                   EU11
         STRING 4X,(0(R3),8),1X,(8(R3),8),1X,(16(R3),8),           EU11X
               INTO=LINE                                           EU11
         TM    0(R6),X'80'             new format?                 EU11
         BZ    RACFDT22                no, old                     EU11
         LA    R4,LINE+1(R15)                                      EU11
         MVI   0(R4),C'/'                                          EU11
         LA    R4,2(R4)                status                      EU11
         STATUS 24(R3),BIT0,'Privileged'                           EU11
         STATUS 24(R3),BIT1,'Trusted'                              EU11
RACFDT22 BCT   R5,*+L'*+4              loop if more                EU11
         B     RACFDT30                else end                    EU11
         LA    R3,24(R3)               next entry                  EU11
         TM    0(R6),X'80'             new format?                 EU11
         BZ    RACFDT21                no, old                     EU11
         LA    R3,8(R3)                yes                         EU11
         B     RACFDT21                                            EU11
RACFDT30 ICM   R6,B'1111',RCVTAUTP     ICHAUTAB table              EU11
         BZ    RACFDT90                none                        EU11
         CLC   0(8,R6),BLANKS          empty (last entry)?         EU11
         BE    RACFDT90                yes, none                   EU11
         BAL   R14,SPACE2              blank line                  EU11
         STRING '  ICHAUTAB - Authorized caller (program) table:', EU11X
               INTO=LINE                                           EU11
RACFDT31 BAL   R14,SPACE0              next line                   EU11
         STRING 4X,(0(R6),8),INTO=LINE                             EU11
         TM    8(R6),BIT0+BIT1                                     EU11
         BZ    RACFDT32                                            EU11
         LA    R4,LINE+1(R15)                                      EU11
         MVI   0(R4),C'/'                                          EU11
         LA    R4,2(R4)                authorization               EU11
         STATUS 8(R6),BIT0,'RACINIT'                               EU11
         STATUS 8(R6),BIT1,'RACLIST'                               EU11
RACFDT32 LA    R6,12(R6)               next entry                  EU11
         CLC   0(8,R6),BLANKS          last entry?                 EU11
         BNE   RACFDT31                no                          EU11
RACFDT90 BAL   R14,SPACE2              blank line                  EU11
RACFDT99 END_PROC ,                                                EU11
         DROP  R3,R5,R6,R7                                         EU11
         DROP  R12                     END_PROC                    EU11
         EJECT
*======================================================================
*        DISPLAY SMF DATA
*----------------------------------------------------------------------
SMFDATA  BEGIN_PROC ,
         STRING 'SMF Data:',INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
         L     R6,CVTSMCA              SMF SMCA
         USING SMCABASE,R6
         STRING '  SID: ',SMCASID,'  JWT: ',SMCASJWT,INTO=LINE,        X
               '   System-ID: ',CVTSNAME,' (ownership GRS)'
         BAL   R14,SPACE2              BLANK LINE
         L     R7,SMCAFRDS             FIRST RDS
         USING IEEMBRDS,R7
*-> LOOP
SMFDS1   MVC   LINE,BLANKS             CLEAR LINE
         MVC   DSNAME(L'RDSNAME),RDSNAME    SYS1.MAX%
         MVC   VOLSER,RDSVOLID         VOLSER
         TM    RDSFLG1,RDSREADY        READY?
         BO    *+L'*+6                 YES, BRANCH AROUND MVC
         MVC   DSNAME+30(6),=C'ACTIVE' THIS IS THE CURRENT DATA SET
         TM    RDSFLG1,RDSDUMP         DATA-SET FULL MSG ISSUED?
         BNO   *+L'*+6                 NO, BRANCH AROUND MVC
         MVC   DSNAME+30(13),=C'DUMP REQUIRED'
         L     R1,RDSNXTBL             NEXT BLOCK
         M     R0,=F'100'              CALCULATE PERCENTAGE USED
         D     R0,RDSCAPTY             CALCULATE PERCENTAGE USED
         STRING ((R1),,R3B),'%',INTO=CATUNCAT
         BAL   R14,SPACE0              NEXT LINE
         L     R7,RDSNEXT              NEXT RDS IN CHAIN
         C     R7,SMCALRDS             IS THIS THE END OF THE CHAIN?
         BNE   SMFDS1                  NO, PROCESS NEXT DATA SET
*-> end LOOP
         BAL   R14,SPACE1              BLANK LINE
         DROP  R7
         L     R3,SMCASSTP             SMF SELECTION CONTROL TABLE
         LH    R4,SMCANSST             number of SST's
*-> LOOP (1)
SMF201   CLI   0(R3),0                 this SST in use?
         BZ    SMF249                  no, skip it
         STRING 2X,((R3),4),'  NOTYPE(',INTO=LINE
         LA    R0,032                  BYTE LOOP                   EU04
         LA    R1,16(,R3)              SELECTION BIT STRING
         LA    R2,000                  RECORD NUMBER (0-255)
*-> LOOP (2)
SMF210   LA    R5,008                  BIT LOOP
         LA    R7,X'80'                BIT MASK
*-> LOOP (3)
SMF220   EX    R7,SMFTST               CHECK FOR RECORDING OFF
         BO    SMF227                  RECORDING ON, JUMP
         CLC   LINE+76(3),BLANKS       CHECK LINE COMPLETE         EU04
         BE    SMF221                  NO, CONTINUE                EU04
         BAL   R14,SPACE0              YES, BUMP TO NEXT LINE      EU04
         STRING 15X,((R2),,L),',',INTO=LINE                        EU04
         B     SMF227                                              EU04
SMFTST   TM    0(R1),*-*               <<EXECUTED>>
SMF221   STRING (LINE,,L),((R2),,L),',',INTO=LINE                  EU04
SMF227   LA    R2,1(,R2)               BUMP RECNO
         SRL   R7,1                    OFFSET MASK
         BCT   R5,SMF220
*-> end LOOP (3)
         LA    R1,1(,R1)               BUMP BYTE ADDRESS
         BCT   R0,SMF210
*-> end LOOP (2)
         LA    R15,LINE(R15)           CHANGE OFFSET TO ADDRESS
         BCTR  R15,0                   OFFSET TO LAST ','
         CLI   0(R15),C','             is it really a comma?
         BE    SMF247                  yes, jump
         LA    R15,1(,R15)             no, do not overlay
SMF247   MVI   0(R15),C')'             CLOSE PARENTHESIS
         BAL   R14,SPACE2              BLANK LINE
*-> end LOOP (1)
SMF249   AH    R3,SMCALSST             next SST
         BCT   R4,SMF201
         END_PROC
         DROP  R6
         DROP  R12                     END_PROC
         EJECT
*======================================================================
*        ON-LINE CPU'S AND STORAGE
*----------------------------------------------------------------------
HARDWARE BEGIN_PROC
         STRING 'Hardware Configuration:',INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
         SLR   R6,R6                   NUMBER OF CPU'S ON-LINE
         L     R7,CVTPCCAT             PCCA VECTOR TABLE
         LA    R3,0016                 16 IS THE MAX NUMBER OF CPU'S
*-> LOOP
HARDW1   ICM   R4,B'1111',0(R7)        PCCA
         BZ    HARDW4                  THIS CPU ACTIVE, JUMP
         USING PCCA,R4
         STRING '  CPU ',(PCCACPUA,H,L),'  Serial: ',(PCCACPID+0,8),   X
               '  Model: ',(PCCACPID+8,4),INTO=LINE
         LA    R6,1(,R6)               COUNT ON-LINE CPU'S
         BAL   R14,SPACE0              NEXT LINE
HARDW4   LA    R7,4(,R7)               BUMP PCCAT PTR
         BCT   R3,HARDW1
*-> end LOOP
         BAL   R14,SPACE1              BLANK LINE
*
*        Place holder for processor speed (filled in by sub-task)
*
         ALR   R6,R6                   # of CPUs * 2
         LH    R6,MP_MIPS-2(R6)        Get pct of usable MIPS
         LA    R5,LINE                 Addr of current line
         STM   R5,R6,CPUONLNE          save them for the sub-task
         BAL   R14,SPACE1              BLANK LINE
         BAL   R14,SPACE1              BLANK LINE
*
*        REAL STORAGE
*
         LA    R1,0001
         AL    R1,CVTEORM              HI-ADDR
         SRL   R1,0010                 GET IT IN "K"
         STRING '  On-line Real Storage: ',(CVTRLSTG,F,L),'K',         X
               '   Highest Real Storage Address: ',((R1),,L),'K',      X
               INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
         L     R3,CVTRCEP              RSM CTL & ENUM AREA
         L     R4,RCEESPL-RCE(,R3)     # OF EXTENDED STORAGE FRAMES
         SLL   R4,2                    CHANGE TO "K"
         STRING '      Extended Storage: ',((R4),,L),'K',INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
         END_PROC
         DROP  R4
*
*        The table below gives the multipliers used to compute the
*        MIPS value for multi-processors, based on the number of
*        CPUs on-line.  I made these numbers up, based on guesstimates.
*        If you have better ones, tell Sam Golob.
*
*       # CPU -> 1   2   3   4   5   6   7   8   9   10  11  12  13
MP_MIPS  DC    H'100,182,260,335,410,484,556,625,691,755,816,875,930'
         DROP  R12                     END_PROC
         EJECT
*======================================================================
*        VIRTUAL MEMORY MAP
*----------------------------------------------------------------------
MEMORY   BEGIN_PROC ,
         STRING 'Virtual Storage Map:',INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
         STRING 10X,'Area',3X,                                    EU17 X
               'Start(hex)-End(hex)    Size(K)    Size(M)',INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
         L     R7,CVTSMEXT             STORAGE MAP EXTENSION
         USING CVTVSTGX,R7
         L     R6,CVTGDA               GLOBAL DATA AREA           EU17
         USING GDA,R6                                             EU17
         XC    WKWORDS,WKWORDS                                    EU17
         MEMORY_MAP 'PSA+System  ',WKWORDS,GDAVR                  EU17
         MEMORY_MAP 'Region V=R  ',GDAVR,,GDAVRSZ                 EU17
         L     R0,GDAVR                                           EU17
         A     R0,GDAVRSZ                                         EU17
         L     R1,GDAPVTSZ                                        EU17
         S     R1,GDAVR                                           EU17
         S     R1,GDAVRSZ                                         EU17
         STM   R0,R1,WKWORDS                                      EU17
         MEMORY_MAP 'Region V=V  ',WKWORDS,,WKWORDS+4             EU17
         MEMORY_MAP '       CSA  ',GDACSA,,GDACSASZ               EU17
         MEMORY_MAP '      MLPA  ',CVTMLPAS,CVTMLPAE              EU17
         MEMORY_MAP '      FLPA  ',CVTFLPAS,CVTFLPAE              EU17
         MEMORY_MAP '      PLPA  ',CVTPLPAS,CVTPLPAE              EU17
         MEMORY_MAP '       SQA  ',GDASQA,,GDASQASZ               EU17
         MVI   WKWORDS,0                                          EU17
         MVI   WKWORDS+1,X'FF'                                    EU17
         MVC   WKWORDS+2(2),WKWORDS+1                             EU17
         MEMORY_MAP '   Nucleus  ',CVTRWNS,WKWORDS                EU17
         STRING 8X,'16M-line',2X,                                 EU17 X
               '----------------------------------------',INTO=LINE
         BAL   R14,SPACE0              next line                  EU17
         XC    WKWORDS,WKWORDS                                    EU17
         MVI   WKWORDS,1                                          EU17
         MEMORY_MAP 'Nucl. Ext.  ',WKWORDS,CVTERWNE               EU17
         MEMORY_MAP '  SQA EXT.  ',GDAESQA,,GDAESQAS              EU17
         MEMORY_MAP ' PLPA Ext.  ',CVTEPLPS,CVTEPLPE              EU17
         MEMORY_MAP ' FLPA Ext.  ',CVTEFLPS,CVTEFLPE              EU17
         MEMORY_MAP ' MLPA Ext.  ',CVTEMLPS,CVTEMLPE              EU17
         MEMORY_MAP '  CSA Ext.  ',GDAECSA,,GDAECSAS              EU17
         MEMORY_MAP ' Reg. Ext.  ',GDAEPVT,,GDAEPVTS              EU17
         BAL   R14,SPACE1              BLANK LINE                 EU17
         L     R1,GDACSACV             CSA space converted to SQA
         SRL   R1,10                   in kilobytes
         STRING '  CSA space converted to SQA: ',                      X
               ((R1),,R8B),'K',INTO=LINE
         LA    R10,NEXTLINE            next line
         L     R1,GDACSARE             unallocated space in common area
         SRL   R1,10                   in kilobytes
         STRING '  Common Area Space Available: ',                     X
               ((R1),,R7B),'K  (CSA+SQA)',INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
         DROP  R6,R7                                              EU17
         END_PROC ,                                               EU17
*---------------------------------------------------------------- EU17
* "MEM_SUB" routine => EDIT SIZE IN K-BYTES AND M-BYTES           EU17
*                      R14 - link register                        EU17
*---------------------------------------------------------------- EU17
MEM_SUB  LR    R5,R0                   size to compute Mb         EU17
         M     R4,=F'10'               multiply by 10             EU17
         D     R4,=A(X'00100000')      and divide by 1 Mb         EU17
         CVD   R5,WKCELL3              convert                    EU17
         MVC   WKCELL2,=XL8'4040202020214B20' mask                EU17
         ED    WKCELL2,WKCELL3+5       edit                       EU17
         SRA   R0,10                   size in Kb                 EU17
         CVD   R0,WKCELL3              convert                    EU17
         MVC   WKCELL1,=XL8'4020202020202120' mask                EU17
         ED    WKCELL1,WKCELL3+4       edit                       EU17
         BR    R14                     return                     EU17
         DROP  R12                     END_PROC                   EU17
         EJECT
*======================================================================
*        RESOURCE MANAGER PARAMETERS (SRM)                         @370
*----------------------------------------------------------------------
SRM00    BEGIN_PROC ,
         L     R2,CVTOPCTP             RESOURCE MANAGER CONTROL TABLE
         USING RMCT,R2
         L     R5,RMCTRMPT             SRM PARAMETER TABLE
         USING RMPT,R5
         L     R6,RMCTICST             SRM ICS TABLE
         USING ICST,R6
         L     R7,RMCTWMST             SRM WORKLOAD MANAGER TABLE
         USING WMST,R7
         STRING 'Resource Manager Data:',INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
         BAS   R1,SRM20
         DC    AL1(RMCTSLTN),C'NEXT  '
         DC    AL1(RMCTSLTR),C'RANDOM'
         DC    AL1(RMCTSLTL),C'LAST  '
         DC    AL1(RMCTSLTF),C'FIRST '
         DC    AL1(00000000),C'????? ',0H'0'
SRM20    BAL   R14,SCAN_TM             CALL SCAN RTNE
         DC    Y(1+6)               0  LENGTH OF A TABLE ENTRY
         TM    RMCTTAPE,*-*         2  TEST SELTAPE OPTION
         STRING '  IPS: ',WMSTID,      IEAIPSXX                        X
               '        ICS: ',ICSTID, IEAICSXX                        X
               '        OPT: ',RMPTOPTN, IEAICSXX                      X
               '      SELTAPE: ',(1(R1),6),                            X
               INTO=LINE
         BAL   R14,SPACE0              NEXT LINE
         STRING '  CPU: ',WMSTIPC,     CPU SERVICE COEFFICIENT         X
               '      IOC: ',WMSTIPI,  I/O SERVICE COEFFICIENT         X
               '      SRB: ',WMSTIPB,  SRB SERVICE COEFFICIENT         X
               '        MSO: ',WMSTIPM, MSO SERVICE COEFFICIENT        X
               INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
*
*        CALCULATE LENGTH OF A DOMAIN TABLE ENTRY FROM FIRST ENTRY,
*        LAST ENTRY, AND NUMBER OF ENTRIES.   THIS SHOULD WORK IN
*        MVS/XA THROUGH MVS/ESA 4.3 (DMDT LENGTH WAS INCREASED IN
*        MVS/ESA 4.2).
*
         SLR   R0,R0
         L     R1,RMCTDMDE             LAST ENTRY IN DOMAIN TABLE
         SL    R1,RMCTDMDT             LENGTH OF DOMAIN TABLE
         LH    R14,RMCTDMNC            NUMBER OF DOMAINS IN DOMAIN TBLE
         BCTR  R14,0                   MINUS ONE FOR "DR"
         DR    R0,R14                  SIZE OF AN ENTRY IN DOMAIN TABLE
         LH    R6,RMCTDMNC             NUMBER OF DOMAINS IN DOMAIN TBLE
         L     R7,RMCTDMDT             FIRST ENTRY IN DOMAIN TABLE
         USING DMDT,R7
         STRING '     DMN   MIN    MAX   CMPL    RUA    INC    NSW',   X
               '   OUTU   TWSR   CIDX',                                X
               INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
*-> LOOP
SRM30    STRING (DMDTNO,FL1,R7B),(DMDTLO,H,R7B),(DMDTHI,H,R7B),        X
               (DMDTCMPL,H,R7B),(DMDTRUA,H,R7B),(DMDTINCU,H,R7B),      X
               (DMDTNSW,H,R7B),(DMDTOUTU,H,R7B),(DMDTTWSR,F,R7B),      X
               (DMDTCIDX,H,R7B),                                       X
               INTO=LINE
         BAL   R14,SPACE0              NEXT LINE
         LA    R7,0(R1,R7)             INCREMENT
         BCT   R6,SRM30
*-> end LOOP
         BAL   R14,SPACE1              BLANK LINE
         END_PROC
         DROP  R2,R5,R6,R7
         DROP  R12                     END_PROC
         EJECT
*======================================================================
*        LIST OPEN CATALOGS
*----------------------------------------------------------------------
OPENCAT  BEGIN_PROC ,
         STRING 'Open Catalogs:',INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
         ST    R10,APFTABLE            FOR SORT RTNE
         L     R5,CVTCBSP              AMCBS
         L     R6,CBSCAXCN-CBS(,R5)    FIRST CAXWA IN CHAIN
         USING CAXWA,R6
*-> LOOP
OPENCAT2 MVC   LINE,BLANKS
         L     R7,CAXUCB               UCB ADDR
         USING UCBOB,R7
         MVC   UNITNAME+8(4),UCBTYP    DEVICE TYPE
         BAL   R14,GETUNIT             GET UNITNAME
         MVC   DSNAME,CAXCNAM          DSNAME
         MVC   VOLSER,UCBVOLI          VOLSER
*        MVC   VOLSER,CAXVOLID         VOLSER (CRA)                ****
         MVC   DEVTYPE,UNITNAME        DEVICE TYPE
         STRING (CAXFLGS,2,X),INTO=(DEVTYPE+10,4)
         TM    CAXFLGS,CAXMCT
         BZ    OPENCAT4
         STRING 'MASTER',INTO=(DEVTYPE+16,6)
OPENCAT4 BAL   R14,SPACE0              NEXT LINE
         ICM   R6,B'1111',CAXCHN       NEXT CAXWA
         BNZ   OPENCAT2                PROCESS IF NOT END-OF-CHAIN
*-> end LOOP
         DROP  R6,R7
         LA    R2,APFTABLE             TABLE DESC
         BAL   R14,TRIZO               SORT BY DSN
         BAL   R14,SPACE1              BLANK LINE
         END_PROC
         DROP  R12                     END_PROC
         EJECT
*======================================================================
*        DISPLAY PAGE DATA SETS
*----------------------------------------------------------------------
PAGEDS   BEGIN_PROC ,
         STRING 'Page Data Sets:',INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
         L     R4,CVTASMVT             POINT TO ASM VECTOR TABLE
         L     R2,ASMPART-ASMVT(,R4)   POINT TO PAGE ACT REF TABLE
         USING PART,R2
         L     R5,PARTDSNL             POINT TO 1ST PAGE DSN
         LA    R6,PARTENTS             POINT TO 1ST PARTE
         USING PARTENT,R6
         L     R7,PARTSIZE             NUMBER OF PART ENTRIES
*-> LOOP
PAGEDS1  TM    PAREFLG1,PARENUSE       THIS PARTE IN USE?
         BO    PAGEDS8                 NO, SKIP IT
         MVC   LINE,BLANKS             BLANK LINE
         MVC   DSNAME,0(R5)            MOVE DSNAME
         L     R1,PAREUCBP             UCB ADDRESS
         MVC   VOLSER,UCBVOLI-UCBOB(R1)  VOLSER
         TM    PAREFLG1,PARENVIO       IS THIS A NON-VIO DATA SET?
         BZ    PAGEDS4                 NO, JUMP
         MVC   YYMMDD,=C'NONVIO'       YES, SHOW IT
PAGEDS4  BAS   R1,PAGEDS4B
         DC    AL1(PAREPLPA),C'PLPA  '
         DC    AL1(PARECOMM),C'COMMON'
         DC    AL1(PAREDPLX),C'DUPLEX'
         DC    AL1(PARELOCL),C'LOCAL '
         DC    AL1(00),0H'0'
PAGEDS4B BAL   R14,SCAN_TM             CALL SCAN RTNE
         DC    Y(1+6)               0  LENGTH OF A TABLE ENTRY
         TM    PARETYPE,*-*         2  TEST LABEL TYPE FLAGS
         MVC   DEVTYPE(6),1(R1)     6  TYPE OF PAGE DATA SET
         L     R1,PARESZSL             GET TOTAL SIZE
         S     R1,PARESLTA             SLOTS AVAILABLE
         M     R0,=F'100'              CALCULATE PERCENTAGE USED
         D     R0,PARESZSL             CALCULATE PERCENTAGE USED
         STRING ((R1),,R3B),'%',INTO=CATUNCAT
         BAL   R14,SPACE0              NEXT LINE
PAGEDS8  LA    R5,44(,R5)              NEXT DSN
         LA    R6,PARTELEN(,R6)        NEXT PARTE
         BCT   R7,PAGEDS1
*-> end LOOP
         BAL   R14,SPACE1              BLANK LINE
         END_PROC
         DROP  R2,R6
         DROP  R12                     END_PROC
         EJECT
*======================================================================
*        NAME AND STATUS OF SYS1.DUMP DATA SETS
*----------------------------------------------------------------------
DUMPDS   BEGIN_PROC ,
         STRING 'DUMP Data Sets:',INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
         L     R0,=F'2048'          <- GET LENGTH OF WORK AREA
         BAL   R14,WORKADDR         <- GET ADDRESS OF WORK AREA
         LR    R2,R1
         USING CTGPL,R2
         LA    R0,DUMPNODE             ENTRY NAME
         ST    R0,CTGENT               POINTER TO ENTRY NAME OR CI#
         OI    CTGOPTN1,CTGGENLD       GENERIC LOCATE
         OI    CTGFUNC,CTGAM0          OS/VS2 CATALOG MGMT REQUEST
         OI    CTGFUNC,CTGSUPLT        SUPER LOCATE
         MVI   CTGTYPE,CTGTALIN        NON-VSAM ENTRIES ONLY
         LA    R1,CTGPL+CTGPLLEN       WORK AREA FOR SVC 26
         ST    R1,CTGWKA               STORE ADDRESS INTO CTGPL
         MVC   0(4,R1),=Y(1000,0)      BUILD WORK AREA HEADER
         LOCATE CTGPL                  ISSUE GENERIC LOCATE REQUEST
         LTR   R1,R15                  ANY ERROR?
         BNZ   DUMPDS81                YES, ISSUE ERROR MESSAGE
         LA    R1,CTGPL+CTGPLLEN+4     POINT TO FIRST ENTRY
*-> LOOP
DUMP20   CLI   0(R1),C'A'              CHECK ENTRY TYPE
         BNE   DUMP29                  JUMP IF NOT A NON-VSAM DATA SET
         MVC   LINE,BLANKS             CLEAR LINE
         MVC   DSNAME,1(R1)            SYS1.DUMP%%
         CLI   DSNAME+11,C' '          IS THIS REALLY SYS1.DUMP%%?
         BNE   DUMP29                  NO, JUMP
         MVI   VOLSER,C'?'             $LOCATE REQUIRED
         MVI   YYMMDD,C'?'             $OBTAIN REQUIRED
         MVC   CATUNCAT(5),=CL5'NOT-Q'                             EU08
         L     R5,CVTRTMCT             REC. TERM. CONTROL TABLE    EU08
         ICM   R6,B'1111',36(R5)       GET AND TEST "RTCTSDDS"     EU08
         BZ    DUMP25                  NONE, JUMP                  EU08
DUMP21   CLC   DSNAME+9(3),12(R6)      THIS DUMP NN "SDDSQDSN"?    EU08
         BE    DUMP22                  YES                         EU08
         ICM   R6,B'1111',4(R6)        NEXT ELEMENT "SDDSQFWD"?    EU08
         BNZ   DUMP21                  YES                         EU08
         B     DUMP25                  ELSE NO MORE                EU08
DUMP22   MVC   CATUNCAT(5),=CL5'EMPTY'                             EU08
         TM    16(R6),X'80'            IS IT EMPTY "SDDSQDDS"?     EU08
         BZ    DUMP25                  YES                         EU08
         MVC   CATUNCAT(5),=CL5'FULL ' NO                          EU08
DUMP25   BAL   R14,SPACE0              BUMP LINE PTR
*        MVI   LINE,0                                              ****
DUMP29   LA    R14,1+44                LENGTH OF AN ENTRY
         LH    R15,CTGPL+CTGPLLEN+2    GET ACTUAL LENGTH OF TABLE
         LA    R15,CTGPL+CTGPLLEN+3(R15)  R15->LAST BYTE OF LAST ENTRY
         BXLE  R1,R14,DUMP20           NEXT ENTRY FROM CATALOG
*-> end LOOP
         BAL   R14,SPACE1              BLANK LINE
         B     DUMPDS99                EXIT
DUMPNODE DC    AL1(9),C'SYS1.DUMP'     NODE FOR GENERIC LOCATE
DUMPDS81 STRING '  Generic Locate Error, Return-code is ',((R1),,L),   X
               ', Reason is IGG0CL',CTGFDBK,'-',(CTGRSNCD,FL1,L0),     X
               INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
DUMPDS99 END_PROC
         DROP  R2
         DROP  R12                     END_PROC
         EJECT
*======================================================================
*        SUB-SYSTEMS AND FUNCTIONS PROCESSED
*----------------------------------------------------------------------
SUBSYSTM BEGIN_PROC ,
         STRING 'Sub-system Vector Table:',INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
         L     R1,CVTJESCT             JES COMM TABLE
         L     R5,JESSSCT-JESCT(,R1)   FIRST JSCVT
         USING SSCT,R5
*-> LOOP (1)
SUBSYS21 MVC   LINE,BLANKS             CLEAR LINE                  EU19
         STRING 2X,SSCTSNAM,1X,(SSCTSSVT,,X),INTO=LINE             EU19
         LA    R1,LINE+L'SSCTSNAM+1
         LA    R0,L'SSCTSNAM
SUBSYS22 CLI   0(R1),C' '
         BNL   *+L'*+4
         MVI   0(R1),C' '
         BCTR  R1,0
         BCT   R0,SUBSYS22
         ICM   R6,B'1111',SSCTSSVT     SUB-SYSTEM VECTOR TABLE
         BZ    SUBSYS70                INACTIVE SUB-SYSTEM
         USING SSVT,R6
         SLR   R1,R1                   FIRST FUNCTION CODE
         LA    R2,256                  MAX NUMBER OF FUNCTIONS
*-> LOOP (2)
SUBSYS30 LA    R14,SSVTFCOD(R1)        FUNCTION BYTE
         LA    R1,1(,R1)               NEXT FUNCTION CODE
         CLI   0(R14),0
         BE    SUBSYS35
         CLC   LINE+76(3),BLANKS       CHECK LINE COMPLETE         EU19
         BE    SUBSYS31                NO, CONTINUE                EU19
         BAL   R14,SPACE0              YES, BUMP TO NEXT LINE      EU19
         MVC   LINE,BLANKS                                         EU19
         STRING 16X,((R1),,L0),INTO=LINE                           EU19
         B     SUBSYS35                                            EU19
SUBSYS31 STRING (LINE,,T),1X,((R1),,L0),INTO=LINE                  EU19
SUBSYS35 BCT   R2,SUBSYS30
*-> end LOOP (2)
SUBSYS70 BAL   R14,SPACE0              NEXT LINE                   EU19
         ICM   R5,B'1111',SSCTSCTA     NEXT SSCVT
         BNZ   SUBSYS21
*-> end LOOP (1)
         BAL   R14,SPACE1              BLANK LINE
         END_PROC
         DROP  R5,R6
         DROP  R12                     END_PROC
         AIF   ('&EUJES' NE 'JES2').JS22                           EU16
         EJECT ,                                                   EU16
*==================================================================EU16
*        DISPLAY JES2 DATA INFORMATION                             EU16
*------------------------------------------------------------------EU16
JES2DATA BEGIN_PROC ,                                              EU16
         L     R1,CVTJESCT             JES COMM table              EU16
         USING JESCT,R1                                            EU16
         ICM   R2,B'1111',JESSSCT      SSCT pointer                EU16
         BZ    JESDT99                 none                        EU16
         USING SSCT,R2                                             EU16
JESDT10  CLC   SSCTID,=CL4'SSCT'       right ID?                   EU16
         BNE   JESDT99                 no, jump                    EU16
         CLC   SSCTSNAM,=CL4'JES2'     JES2 SSCT?                  EU16
         BE    JESDT11                 yes                         EU16
         ICM   R2,B'1111',SSCTSCTA     no, get next SSCT           EU16
         BNZ   JESDT10                 and loop if more            EU16
         B     JESDT99                 else JES2 not found, QUIT   EU16
JESDT11  STRING 'JES2 data:',INTO=LINE                             EU16
         CLC   JESPJESN,=CL4'JES2'     JES2 is primary?            EU16
         BNE   JESDT12                 no, jump                    EU16
         STRING 'Primary Sub-system',INTO=(LINE+11,18)             EU16
         DROP  R1                                                  EU16
JESDT12  ICM   R7,B'1111',SSCTSSVT     SSVT pointer                EU16
         BZ    JESDT90                 none, exit                  EU16
         DROP  R2                                                  EU16
         XR    R5,R5                                               EU16
         LA    R2,SVTCKPT1(R7)         HFAME 1                     EU16
         BAL   R6,JESDT20              display it                  EU16
         LA    R2,SVTCKPT2(R7)         HFAME 2                     EU16
         LA    R6,JESDT30              display it                  EU16
         USING HFE,R2                                              EU16
JESDT20  TM    HFEFLAG1,HFE1INUS       data-set in use?            EU16
         BZR   R6                      no, skip it                 EU16
         LTR   R5,R5                   first time?                 EU16
         BNZ   JESDT21                 no, jump                    EU16
         BAL   R14,SPACE2              blank line                  EU16
         LR    R5,R14                  close this way              EU16
         STRING '  Checkpoint data-set(s):',INTO=LINE              EU16
JESDT21  BAL   R14,SPACE0              next line                   EU16
         STRING 4X,'Name: ',(HFEDSN,,T),' on ',HFEVOL,INTO=LINE    EU16
         BR    R6                      go back                     EU16
         DROP  R2                                                  EU16
JESDT30  ICM   R5,B'1111',SVTRDT(R7)   RMT destination table       EU16
         BZ    JESDT40                 none, jump                  EU16
         ICM   R6,B'1111',SVTRDTE(R7)  last RDT element address    EU16
         BZ    JESDT40                 none, jump                  EU16
         CLR   R5,R6                                               EU16
         BH    JESDT40                 none, jump                  EU16
         BAL   R14,SPACE2              blank line                  EU16
         STRING '  RMT destination table:',INTO=LINE               EU16
         LA    R3,2                                                EU16
         USING RDT,R5                                              EU16
JESDT31  BAL   R14,SPACE0              next line                   EU16
         MVC   WKCELL2,BLANKS                                      EU16
         XR    R2,R2                                               EU16
         TM    RDTFLAG,RDTFLAGN        N number?                   EU16
         BZ    *+L'*+8                 no                          EU16
         ICM   R2,B'0011',RDTNODE      yes                         EU16
         B     JESDT32                                             EU16
         TM    RDTFLAG,RDTFLAGR+RDTFLAGU R or U number?            EU16
         BZ    *+L'*+8                 no                          EU16
         ICM   R2,B'0011',RDTRMTNO     yes                         EU16
         B     JESDT32                                             EU16
         CLC   RDTNAME,=CL8'LOCAL   '                              EU16
         BNE   *+L'*+10                                            EU16
         MVC   WKCELL1,=CL8'Default '                              EU16
         B     JESDT35                                             EU16
         MVC   WKCELL1,=CL8'| none ?'                              EU16
         B     JESDT35                                             EU16
JESDT32  CVD   R2,WKCELL3                                          EU16
         MVC   WKCELL1,=XL8'4020202020202120'                      EU16
         ED    WKCELL1,WKCELL3+L'WKCELL3-4                         EU16
JESDT33  CLI   WKCELL1+1,C' '                                      EU16
         BNE   JESDT34                                             EU16
         MVC   WKCELL1+1(6),WKCELL1+2                              EU16
         MVI   WKCELL1+7,C' '                                      EU16
         B     JESDT33                                             EU16
JESDT34  MVI   WKCELL1,C'R'                                        EU16
         TM    RDTFLAG,RDTFLAGR        R number?                   EU16
         BO    JESDT35                 yes                         EU16
         MVI   WKCELL1,C'U'                                        EU16
         TM    RDTFLAG,RDTFLAGU        U number?                   EU16
         BO    JESDT35                 yes                         EU16
         MVI   WKCELL1,C'N'                                        EU16
         CLM   R2,B'0011',SVTTONOD(R7) own node ID?                EU16
         BNE   JESDT35                                             EU16
         MVC   WKCELL2,=C'own Node'                                EU16
JESDT35  BCT   R3,JESDT36                                          EU16
         STRING RDTNAME,' (',WKCELL1,') ',WKCELL2,                 EU16X
               INTO=(LINE+40,30)                                   EU16
         B     JESDT37                                             EU16
JESDT36  STRING 4X,RDTNAME,' (',WKCELL1,') ',WKCELL2,INTO=LINE     EU16
JESDT37  LA    R5,RDTSIZ(R5)           next element                EU16
         CLR   R5,R6                   last done?                  EU16
         BH    JESDT40                 yes                         EU16
         LTR   R3,R3                                               EU16
         BP    JESDT31+L'JESDT31                                   EU16
         LA    R3,2                                                EU16
         B     JESDT31                                             EU16
         DROP  R5                                                  EU16
JESDT40  ICM   R6,B'1111',SVTPIT(R7)   PIT's table pointer         EU16
         BZ    JESDT90                 none, jump                  EU16
         BAL   R14,SPACE2              blank line                  EU16
         STRING '  PIT table (JES2 initiators):',INTO=LINE         EU16
         BAL   R14,SPACE0              next line                   EU16
         USING PIT,R6                                              EU16
JESDT41  STRING '    PIT ',PITPATID,'  Class(es): ',PITCLASS,      EU16X
               INTO=LINE                                           EU16
         ICM   R1,B'1111',PITSJB       SJB for executing job       EU16
         CLC   SJBJOBID(L'SJBJOBID,R1),=XL8'0' job-name?           EU16
         BE    JESDT42                 none, jump                  EU16
         BAL   R14,SPACE0              next line                   EU16
         STRING 12X,'Executing: ',(SJBJOBID(R1),L'SJBJOBID,L),     EU16X
               ' (',(SJBJOBNM(R1),L'SJBJOBNM,L),')',INTO=LINE      EU16
JESDT42  BAL   R14,SPACE0              next line                   EU16
         STRING 12X,'PIT status:',INTO=LINE                        EU16
         LA    R4,LINE+1(R15)                                      EU16
         STATUS PITSTAT,PITHOLDA,'HOLDed-A'                        EU16
         STATUS PITSTAT,PITHOLD1,'HOLDed'                          EU16
         STATUS PITSTAT,PITBUSY,'BUSY'                             EU16
         STATUS PITSTAT,PITHALTA,'HALTed-A'                        EU16
         STATUS PITSTAT,PITHALT1,'HALTed'                          EU16
         STATUS PITSTAT,PITINIT,'INIT'                             EU16
         STATUS PITSTAT,PITIDLE,'IDLE'                             EU16
         STATUS PITSTAT,PITDUPL,'DUPL''d'                          EU16
         TM    PITFLAGS,PITXBJCL+PITXBM+PITSMVER+PITSIVER+PITSIERR EU16
         BZ    JESDT43                 none, jump                  EU16
         BAL   R14,SPACE0              next line                   EU16
         STRING 13X,'PIT flags:',INTO=LINE                         EU16
         LA    R4,LINE+1(R15)                                      EU16
         STATUS PITFLAGS,PITXBJCL,'Wait-XBM'                       EU16
         STATUS PITFLAGS,PITXBM,'Batch-Mon'                        EU16
         STATUS PITFLAGS,PITSMVER,'Wait-Mem'                       EU16
         STATUS PITFLAGS,PITSIVER,'Wait-$SI'                       EU16
         STATUS PITFLAGS,PITSIERR,'Start-rejected'                 EU16
JESDT43  ICM   R6,B'1111',PITNEXT      next PIT?                   EU16
         BZ    JESDT90                 no more                     EU16
         BAL   R14,SPACE0              next line                   EU16
         B     JESDT41                 go process this PIT         EU16
         DROP  R6                                                  EU16
JESDT90  BAL   R14,SPACE2              blank line                  EU16
JESDT99  END_PROC ,                                                EU16
         DROP  R12                     END_PROC                    EU16
.JS22    EJECT                                                     EU16
*======================================================================
*        Terminal Control Address Space (TCAS)
*        (this display requires APF-authorization)
*----------------------------------------------------------------------
TCAS00   BEGIN_PROC
         TESTAUTH FCTN=1               CHECK APF STATUS
         LTR   R15,R15                 may I use MODESET?
         BNZ   TCAS99                  no, exit
         STRING 'TCAS parameters:',INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
         L     R0,=F'2048'          <- get length of work area
         BAL   R14,WORKADDR         <- get address of work area
         LR    R6,R1                   save its address
         USING TCAST,R6
         MODESET MODE=SUP,KEY=ZERO     supervisor mode and key 0   EU01
*        MODESET MODE=SUP              Switch to supervisor        EU01
*        MODESET EXTKEY=VTAM           Switch to KEY 6             EU01
         L     R2,CVTTCASP             addr of TCAS area
         MVC   TCAST(TCASTEND-TCAST),0(R2)
         MODESET MODE=PROB,KEY=NZERO   Switch back to key 8
         STRING '  CVTTCASP: ',((R2),,X),                              X
               '  USERMAX: ',(TCASUMAX,H,L8B),                         X
               'USERACT: ',(TCASUSEC,H,L8B),                           X
               'RCONLIM: ',(TCASRCON,H,L8B),INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
TCAS99   END_PROC
         DROP  R6
         DROP  R12                     END_PROC
         AIF   ('&SYSSPLV' LT '2').SPL3X     LOW MACLIB LEVEL, JUMP
*======================================================================
*        TSO PARMLIB data and authorization tables
*----------------------------------------------------------------------
TSOP00   BEGIN_PROC
         L     R2,CVTTVT
         USING TSVT,R2
         L     R3,TSVTTPVT             TSO/PARMLIB vector table
         USING TPVT,R3
         L     R4,TPVTCTLT             Control table
         USING CTLT,R4
         STRING 'TSO-PARMLIB data:',INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
         AIF   ('&SYSSPLV' LT '3').SPL3A     NOT MVS/ESA MACLIB, JUMP
         STRING '  Member(',TPVTMBR,')',INTO=LINE
         AGO   .SPL3B
.SPL3A   STRING '  Member(IKJTSOxx)',INTO=LINE
.SPL3B   BAL   R14,SPACE2              BLANK LINE
         LA    R5,CTLTE2               A(IKJEFTE2)
         BAL   R7,TSOP10
         DC    CL8'AUTHCMD '           IKJEFTE2
         DC    CL8'AUTHPGM '           IKJEFTE8
         DC    CL8'NOTBKGND'           IKJEFTNS
         DC    CL8'AUTHTSF '           IKJEFTAP
         DC    XL2'FFFF'
*-> LOOP (1)
TSOP10   STRING 2X,((R7),8),' NAMES ( +',INTO=LINE
         BAL   R14,SPACE0              next line
         MVC   LINE,BLANKS             blank line
         LA    R6,LINE                 first entry
         LH    R0,8(,R5)               # of entries
         L     R1,0(,R5)               start of table
         LA    R1,16(,R1)              skip table header
*-> LOOP (2)
TSOP11   CLI   2(R1),C' '              blank entry?
         BE    TSOP14                  yes, jump
         LA    R14,9                   increment in print line
         LA    R15,LINE+70             end of line
         BXLE  R6,R14,TSOP12           this line full?
         MVI   0(R6),C'+'              continuation
         BAL   R14,SPACE0              next line
         MVC   LINE,BLANKS             blank line
         LA    R6,LINE+9               continuation
TSOP12   CLI   11(R5),8                entry length is 8?
         BE    TSOP13                  yes, jump
         MVC   0(8,R6),2(R1)           no, move name
         B     TSOP14
TSOP13   MVC   0(8,R6),0(R1)           move name
TSOP14   AH    R1,10(,R5)              length of an entry
         BCT   R0,TSOP11
*-> end LOOP (2)
         MVI   9(R6),C')'              close parenthsis
         BAL   R14,SPACE2              BLANK LINE
         LA    R5,CTLTE8-CTLTE2(,R5)   next table
         LA    R7,8(,R7)               next name
         CLI   0(R7),X'FF'             last table processed?
         BL    TSOP10                  not yet, process next one
*-> end LOOP (1)
TSOP99   END_PROC
         DROP  R2,R3,R4
.SPL3X   EJECT
*======================================================================
*        Address Space Usage
*        Active jobs, started tasks, TSO users
*----------------------------------------------------------------------
USERS    BEGIN_PROC
         L     R6,CVTASVT
         USING ASVT,R6
         STRING 'Address Space Usage:',INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
         STRING 26X,'Total',9X,'In use',6X,'Available',INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
         L     R1,ASVTMAXI             MAXUSER
         SL    R1,ASVTAAV              AVAILABLE
         STRING '  Address spaces',(ASVTMAXI,F,R15B),                  X
               ((R1),,R15B),(ASVTAAV,F,R15B),INTO=LINE
         BAL   R14,SPACE0              NEXT LINE
         L     R1,ASVTSTRT             MAXUSER
         SL    R1,ASVTAST              AVAILABLE
         STRING '    Started/SASI',(ASVTSTRT,F,R15B),                  X
               ((R1),,R15B),(ASVTAST,F,R15B),INTO=LINE
         BAL   R14,SPACE0              NEXT LINE
         L     R1,ASVTNONR             MAXUSER
         SL    R1,ASVTANR              AVAILABLE
         STRING '    Non-reusable',(ASVTNONR,F,R15B),                  X
               ((R1),,R15B),(ASVTANR,F,R15B),INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
         DROP  R6
         STRING 'Active Jobs:',INTO=LINE
         BAL   R14,SPACE2              blank line
         SLR   R2,R2                   first time switch
         LA    R4,1                    first ASID
         L     R5,CVTASVT              point to ASVT
         L     R5,ASVTMAXU-ASVT(,R5)   max ASID
*-> LOOP
USERS11  LOCASCB ASID=(R4)             GET ADDR OF OWNER'S ASCB
         LTR   R15,R1                  VALID ASID?
         BNP   USERS18                 NO, JUMP                    EU02
         ICM   R1,B'1111',ASCBJBNI-ASCB(R15) JOBNAME?
         BZ    USERS18                 no - CONTINUE
         BAL   R14,EOL                 check for end-of-line
         MVC   0(8,R2),0(R1)           move job name
         LA    R2,10(,R2)            BUMP TO 10 BYTES PAST WHERE WE ARE
USERS18  LA    R4,1(,R4)               bump ASID
         BCT   R5,USERS11              CHECK AGAINST # OF UNIT NAMES
*-> end LOOP
         LTR   R2,R2                   any JOB found?
         BNZ   USERS19                 yes, jump
         STRING '  (none)',INTO=LINE   no, tell it
USERS19  BAL   R14,SPACE2              BLANK LINE
         STRING 'TSO Users:',INTO=LINE
         BAL   R14,SPACE2              blank line
         SLR   R2,R2                   first time switch
         LA    R4,1                    first ASID
         L     R5,CVTASVT              point to ASVT
         L     R5,ASVTMAXU-ASVT(,R5)   max ASID
*-> LOOP
USERS21  LOCASCB ASID=(R4)             GET ADDR OF OWNER'S ASCB
         LTR   R15,R1                  VALID ASID?
         BNP   USERS28                 NO, JUMP                    EU02
         ICM   R1,B'1111',ASCBJBNI-ASCB(R15) JOBNAME?
         BNZ   USERS28                 yes - CONTINUE
         ICM   R1,B'1111',ASCBJBNS-ASCB(R15) START/MOUNT/LOGON
         BZ    USERS28                 no - CONTINUE
         L     R14,ASCBOUCB-ASCB(,R15) point to OUCB
         TM    OUCBYFL-OUCB(R14),OUCBSTT+OUCBMNT  START OR MOUNT?
         BNZ   USERS28                 YES, IGNORE
         BAL   R14,EOL                 check for end-of-line
         MVC   0(8,R2),0(R1)           move job name
         LA    R2,10(,R2)            BUMP TO 10 BYTES PAST WHERE WE ARE
USERS28  LA    R4,1(,R4)               bump ASID
         BCT   R5,USERS21              CHECK AGAINST # OF UNIT NAMES
*-> end LOOP
         LTR   R2,R2                   any TSU found?
         BNZ   USERS29                 yes, jump
         STRING '  (none)',INTO=LINE   no, tell it
USERS29  BAL   R14,SPACE2              BLANK LINE
         STRING 'Started Tasks:',INTO=LINE
         BAL   R14,SPACE2              blank line
         SLR   R2,R2                   first time switch
         LA    R4,1                    first ASID
         L     R5,CVTASVT              point to ASVT
         L     R5,ASVTMAXU-ASVT(,R5)   max ASID
*-> LOOP
USERS31  LOCASCB ASID=(R4)             GET ADDR OF OWNER'S ASCB
         LTR   R15,R1                  VALID ASID?
         BNP   USERS38                 NO, JUMP                    EU02
         ICM   R1,B'1111',ASCBJBNI-ASCB(R15) JOBNAME
         BNZ   USERS38                 yes - CONTINUE
         ICM   R1,B'1111',ASCBJBNS-ASCB(R15) START/MOUNT/LOGON
         BZ    USERS38                 no - CONTINUE               EU02
         L     R14,ASCBOUCB-ASCB(,R15) point to OUCB
         TM    OUCBYFL-OUCB(R14),OUCBSTT+OUCBMNT  START OR MOUNT?
         BZ    USERS38                 NO, IGNORE
         BAL   R14,EOL                 check for end-of-line
         MVC   0(8,R2),0(R1)           move job name
         LA    R2,10(,R2)            BUMP TO 10 BYTES PAST WHERE WE ARE
USERS38  LA    R4,1(,R4)               bump ASID
         BCT   R5,USERS31              CHECK AGAINST # OF UNIT NAMES
*-> end LOOP
         BAL   R14,SPACE2              BLANK LINE
USERS99  END_PROC
*----------------------------------------------------------------------
*        "EOL" routine => CHECK FOR END-OF-LINE
*                         R14 - link register
*----------------------------------------------------------------------
EOL      LTR   R2,R2                   first time?
         BZ    EOL1                    yes, jump
         LA    R0,LINE+76              last position
         CR    R2,R0                   end of line reached yet?
         BLR   R14                     no, exit
         LA    R10,NEXTLINE            yes, next line
         CL    R10,DYN_CHK
         BNL   SPERR
EOL1     MVC   LINE,BLANKS             CLEAR OUT MSG AREA
         LA    R2,LINE+2               GET BEGINNING ADDR OF THE LINE
         BR    R14                     exit
         DROP  R12                     END_PROC
         EJECT
*======================================================================
*        LNKLSTXX LIBRARIES
*----------------------------------------------------------------------
LINKLIST BEGIN_PROC ,
         L     R4,CVTLINK              SYS1.LINKLIB DCB
         ICM   R4,B'0111',DCBDEBA-IHADCB(R4) DEB ADDRESS
         USING DEBBASIC,R4
         LA    R1,=C'LNKLST'
         TM    DEBFLGS1,DEBAPFIN       AUTH=YES?
         BO    *+L'*+4                 YES, JUMP
         LA    R1,=C'APFTAB'
         L     R7,CVTLLTA              LINK LIST TABLE
         USING LLT,R7
         STRING 'Link-list: ',(LLTNO,F,R7B),' Entries     LNKAUTH=',   X
               ((R1),6),(DEBNMEXT,FL1,R6B),' extents open',INTO=LINE
         ST    R10,LINKXTNT            save line address for sub-task
         BAL   R14,SPACE2              BLANK LINE
         L     R3,LLTNO                # OF ENTRIES
         LA    R4,LLTENTRY             LINK LIST TABLE ENTRY
         USING LLTENTRY,R4
         ST    R10,LNKTABLE            SAVE ADDR OF 1ST LINE
*-> LOOP
LNKLST41 MVC   LINE,BLANKS
         MVC   DSNAME,LLTDSNAM         MOVE DSNAME
         MVI   VOLSER,C'?'             $LOCATE REQUIRED
         MVI   YYMMDD,C'?'             $OBTAIN REQUIRED
         MVI   LINE_APF,C'?'           LNK/LPA/APF STATUS
         BAL   R14,SPACE0              NEXT LINE
         LA    R4,LLTNEXT              NEXT ENTRY
         BCT   R3,LNKLST41
*-> end LOOP
         LA    R0,L'LINE               LONGUEUR D'UN POSTE
         LR    R1,R10                  A(NEXTLINE)
         SLR   R1,R0                   DERNIER POSTE DE LA TABLE
         STM   R0,R1,LNKTABLE+4        LONGUEUR, DERNIER POSTE
         BAL   R14,SPACE1              BLANK LINE
         END_PROC
         DROP  R4,R7
         DROP  R12                     END_PROC
         EJECT
*======================================================================
*        LPALSTXX LIBRARIES
*----------------------------------------------------------------------
LPALIST  BEGIN_PROC ,
         TM    CVTDCB,CVTMVSE          XA/ESA?
         BZ    LPALST99                NO, JUMP
         L     R1,CVTSMEXT             STORAGE MAP EXTENSION
         L     R7,CVTEPLPS-CVTVSTGX(,R1)  LPA TABLE
         USING LLT,R7
         STRING 'LPA list: ',((R7),,X),                                X
               (LLTNO,F,R9B),' Entries.',INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
         L     R3,LLTNO                # OF ENTRIES
         LA    R4,LLTENTRY             FIRST LPA LIST TABLE ENTRY
         USING LLTENTRY,R4
         ST    R10,LPATABLE            SAVE ADDR OF 1ST LINE
*-> LOOP
LPALST42 MVC   LINE,BLANKS
         MVC   DSNAME,LLTDSNAM         MOVE DSNAME TO UNPROTECTED STRGE
         MVI   VOLSER,C'?'             $LOCATE REQUIRED
         MVI   YYMMDD,C'?'             $OBTAIN REQUIRED
         MVI   LINE_APF,C'?'           LNK/LPA/APF STATUS
         BAL   R14,SPACE0              NEXT LINE
         LA    R4,LLTNEXT              NEXT ENTRY
         BCT   R3,LPALST42
*-> end LOOP
         LA    R0,L'LINE               LONGUEUR D'UN POSTE
         LR    R1,R10                  A(NEXTLINE)
         SLR   R1,R0                   DERNIER POSTE DE LA TABLE
         STM   R0,R1,LPATABLE+4        LONGUEUR, DERNIER POSTE
         BAL   R14,SPACE1              BLANK LINE
LPALST99 END_PROC
         DROP  R4,R7
         DROP  R12                     END_PROC
         EJECT
*======================================================================
*        AUTHORIZED LIBRARIES
*----------------------------------------------------------------------
APFLIST  BEGIN_PROC ,
         L     R7,CVTAUTHL             APF TABLE
         LH    R3,0(,R7)               # OF ENTRIES
         STRING 'APF list: ',(CVTAUTHL,,X),                            X
               ((R3),,R9B),' Entries.',INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
         ST    R10,APFTABLE            SAVE ADDR OF 1ST LINE
*-> LOOP
APFLIST3 MVC   LINE,BLANKS
         SLR   R1,R1
         IC    R1,2(,R7)
         SH    R1,=H'6'                VOLSER LENGTH
         STRING (9(R7),(R1)),INTO=DSNAME MOVE DSNAME
         MVC   VOLSER,3(R7)            MOVE VOLSER
         MVI   YYMMDD,C'?'             $OBTAIN REQUIRED
         MVI   CATUNCAT,C'?'           $LOCATE REQUIRED
         MVI   LINE_APF,C'?'           LNK/LPA/APF STATUS
         BAL   R14,SPACE0              NEXT LINE
         LA    R7,7(R1,R7)             NEXT ENTRY
         BCT   R3,APFLIST3
*-> end LOOP
         LA    R2,APFTABLE             APFTABLE DESCRIPTOR
         BAL   R14,TRIZO               SORT APFTABLE
         BAL   R14,SPACE1              BLANK LINE
         END_PROC
         DROP  R12                     END_PROC
         EJECT
*======================================================================
*        ACTIVE LPA Q (FLPA/MLPA)
*----------------------------------------------------------------------
LPACTIV  BEGIN_PROC ,
         L     R5,CVTQLPAQ             ACTIVE LPA QUEUE
         ICM   R5,B'1111',0(R5)        FIRST LPDE ON QUEUE
         BZ    LPACT99                 EMPTY QUEUE, SKIP SEARCH
         USING LPDE,R5
         STRING 'Active LPA Queue:',INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
         STRING '   NAME     ENTPT    ATTRB SP ATTR  ATTR2  USE ',     X
               '   MAJ-CDE   LENGTH   LOAD-PNT',INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
*-> LOOP
LPACT11  TM    LPDEATTR,LPDEMIN        MINOR LPDE?
         BO    LPACT12                 YES, JUMP
         STRING 3X,LPDENAME,1X,(LPDENTP,,X),3X,(LPDEATTB,,X),6X,       X
               (LPDEATTR,,X),4X,(LPDEATT2,,X),4X,                      X
               (LPDEUSE,,X),12X,                                       X
               (LPDEXTLN,,X),2X,(LPDEXTAD,,X),INTO=LINE
         B     LPACT20
LPACT12  L     R2,LPDEMJP              MAJOR CDE
         TM    LPDEATT2,LPDEXLE        EXTENT LIST BUILD?
         BZ    LPACT13                 NO, JUMP
         STRING 3X,LPDENAME,1X,(LPDENTP,,X),3X,(LPDEATTB,,X),6X,       X
               (LPDEATTR,,X),4X,(LPDEATT2,,X),4X,                      X
               (LPDEUSE,,X),2X,(LPDENAME-LPDE(R2),8),2X,               X
               (LPDEXTLN,,X),2X,(LPDEXTAD,,X),INTO=LINE
         B     LPACT20
LPACT13  STRING 3X,LPDENAME,1X,(LPDENTP,,X),3X,(LPDEATTB,,X),6X,       X
               (LPDEATTR,,X),4X,(LPDEATT2,,X),4X,                      X
               (LPDEUSE,,X),2X,(LPDENAME-LPDE(R2),8),INTO=LINE
LPACT20  BAL   R14,SPACE0              NEXT LINE
         ICM   R5,B'1111',LPDECHN      NEXT LPDE ADDR
         BNZ   LPACT11                 NO FINISHED YET, LOOP FURTHER
*-> end LOOP
LPACT99  BAL   R14,SPACE1              BLANK LINE
         END_PROC
         DROP  R5
         DROP  R12                     END_PROC
         EJECT
*======================================================================
*        SVC TABLE
*----------------------------------------------------------------------
SVCTABLE BEGIN_PROC ,
         STRING 'SVC Table:',INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
         L     R5,CVTABEND             SECONDARY CVT
         USING SCVTSECT,R5
         NUCLKUP BYNAME,NAME='IGCERROR',ADDR=(0)
         ST    R0,IGCERROR             save address of IGCERROR
         L     R5,SCVTSVCT             START OF SVC TABLE
         LA    R3,00256/2
         OI    K1+L'K1-1,15            INIT PACKED CTR
         USING SVCENTRY,R5
*-> LOOP
SVCTAB3  L     R1,SVCEP                SVC EP ADDR
         BAL   R14,CSVQUERY            GET EP NAME
         BAL   R7,SVCTYPE              DECODE TYPE
         STRING (K1,P,R5B),2X,(SVCEP,,X),1X,EP10,2X,(WK256,12),        X
               INTO=LINE
         LA    R5,8(,R5)               NEXT SVC (ODD NUMBER)
         ZAP   K2,K1
         AP    K2,=P'1'
         L     R1,SVCEP                SVC EP ADDR
         BAL   R14,CSVQUERY            GET EP NAME
         BAL   R7,SVCTYPE              DECODE TYPE
         STRING (K2,P,R5B),2X,(SVCEP,,X),1X,EP10,2X,(WK256,12),        X
               INTO=(LINE+40,L'LINE-40)
         BAL   R14,SPACE0              NEXT LINE
         AP    K1,=P'2'
         LA    R5,8(,R5)               NEXT SVC (EVEN)
         BCT   R3,SVCTAB3
*-> end LOOP
         STRING '             ++                                      +X
               +',INTO=LINE
         BAL   R14,SPACE0              NEXT LINE
         STRING '   LOCKS: 80-LOCAL   40-CMS     20-OPT     10-SALLOC  X
               08-DISP',INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
*
*        TYPE-X ESR ENTRIES
*
SVCESR1  BAL   R3,SVCESR2              BRANCH AROUND TABLE
         DC    AL2(116),C'1'           T1-ESR
         DC    AL2(122),C'2'           T2-ESR
         DC    AL2(109),C'3'           T3-ESR
         DC    AL2(137),C'6'           T6-ESR
*-> LOOP (1)
SVCESR2  L     R1,CVTABEND             SECONDARY CVT
         L     R5,SCVTSVCT-SCVTSECT(,R1)  START OF SVC TABLE
         LH    R1,0(,R3)               GET SVC NUMBER
         SLL   R1,3                    MULT BY 8
         L     R5,0(R1,R5)             ADDR OF T3-ESR TABLE
         L     R0,SVCESRMX             GET MAX NUMBER OF ENTRIES
         CVD   R0,WKCELL1              KEEP IT
         STRING 'Type-',(2(R3),1),' ESR  (SVC ',(0(R3),H,R3Z),         X
               ' - Maximum ',(WKCELL1,P,L),' Entries)',INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
         SP    K1,K1                   RESET CTR
         LA    R5,8(,R5)               SKIP HEADER
*-> LOOP (2)
SVCESR3  LR    R1,R5                   CURRENT ENTRY
         SH    R1,=H'16'               BACK UP 2 ENTRIES
         CLC   0(16,R1),0(R5)          SAME UNUSED ENTRIES?
         BE    SVCESR3P                YES, SKIP THEM
         L     R1,SVCEP                SVC EP ADDR
         BAL   R14,CSVQUERY            GET EP NAME
         BAL   R7,SVCTYPE              DECODE TYPE
         STRING (K1,P,R5B),2X,(SVCEP,,X),1X,EP10,2X,(WK256,12),        X
               INTO=LINE
         LA    R5,8(,R5)               NEXT SVC (ODD NUMBER)
         CP    K1,WKCELL1              LAS NUMBER REACHED?
         BNL   SVCESR3N                YES, EXIT LOOP
         ZAP   K2,K1
         AP    K2,=P'1'
         L     R1,SVCEP                SVC EP ADDR
         BAL   R14,CSVQUERY            GET EP NAME
         BAL   R7,SVCTYPE              DECODE TYPE
         STRING (K2,P,R5B),2X,(SVCEP,,X),1X,EP10,2X,(WK256,12),        X
               INTO=(LINE+40,L'LINE-40)
SVCESR3N BAL   R14,SPACE0              NEXT LINE
         B     SVCESR3Q
SVCESR3P LA    R5,8(,R5)               SKIP EVEN #
SVCESR3Q AP    K1,=P'2'
         LA    R5,8(,R5)               NEXT SVC (EVEN)
         CP    K1,WKCELL1              LAST NUMBER REACHED?
         BNH   SVCESR3                 NO, KEEP LOOPING
*-> end LOOP (2)
         BAL   R14,SPACE1              BLANK LINE
         LA    R3,3(,R3)               NEXT ESR-TYPE TABLE ENTRY
         CLI   0(R3),0                 END OF TABLE?
         BE    SVCESR2                 NOT YET, DO IT AGAIN
*-> end LOOP (1)
         END_PROC
*
*        DECODE SVC TYPE FLAGS
*
SVCTYPE  MVC   WK256(12),BLANKS        FORMAT OUTPUT FIELD
         MVC   WK256(2),=C'T3'         FORMAT OUTPUT FIELD
         TM    SVCTP,SVCTP34           TYPE 3 (OR 4)
         BO    SVCTYPE2                YES, JUMP
         MVI   WK256+1,C'2'            CHANGE TO T2
         TM    SVCTP,SVCTP2            TYPE 2?
         BO    SVCTYPE2                YES, JUMP
         MVI   WK256+1,C'6'            CHANGE TO T6
         TM    SVCTP,SVCTP6            TYPE 6?
         BO    SVCTYPE2                YES, JUMP
         MVI   WK256+1,C'1'            CHANGE TO T1
SVCTYPE2 TM    SVCTP,SVCAPF            APF REQUIRED?
         BNO   SVCTYPE3                NO, JUMP
         MVC   WK256+2(4),=C'-APF'     YES, SHOW APF
SVCTYPE3 TM    SVCTP,SVCESR            ESR?
         BNO   SVCTYPE6                NO, JUMP
         MVC   WK256+2(4),=C'-ESR'     YES, SHOW ESR
SVCTYPE6 CLI   SVCLOCKS,0              ANY LOCK REQUIRED?
         BE    SVCTYPE9                NO, JUMP
         STRING (WK256,12,T),'-',(SVCLOCKS,,X),INTO=(WK256,12)
SVCTYPE9 BR    R7
         DROP  R5
         DROP  R12                     END_PROC
         EJECT
*======================================================================
*        Device class table and corresponding unit names
*        On-line devices
*----------------------------------------------------------------------
DEVICES  BEGIN_PROC ,
         L     R3,CVTDCQA              GET DCQ ADDRESS
         L     R3,DCQFIRST-DCQ(,R3)    GET FIRST DCQ ADDRESS
         USING DCQELMNT,R3             DCQ element
*-> LOOP (1)
DEV101   ICM   R0,B'0011',DCQUCBCT     any devices gen'd?
         BZ    DEV190                  no, exit
         MVC   UNITNAME+18(1),DCQDEVCL Device class code
         XCALL IEFEB4UV,                                               X
               (UNITNAME,              IEFEB4UV parm list              X
               =X'0010'),              FUNCTION CODE '11'              X
               ERRET=DEV190            error, try next device class
         L     R4,UNITNAME+4           point to the returned work area
*        #SNAP ADDR=UNITNAME,LENGTH=32                           <SNAP>
         ICM   R5,B'1111',4(R4)        get the number of unit-names
         BZ    DEV180                  none found, try next class
         STRING 'Device Class: ',DCQDEVNM,INTO=LINE
         BAL   R14,SPACE2              blank line
         LA    R6,8(,R4)               first unit name
         STRING '  Unit Names:',INTO=LINE
         B     DEV122                  first time, jump
*-> LOOP
DEV121   LA    R0,LINE+76              last position
         CR    R2,R0                   end of line reached yet?
         BL    DEV124                  no, jump
         BAL   R14,SPACE0              yes, next line
         MVC   LINE,BLANKS             CLEAR OUT MSG AREA
DEV122   LA    R2,LINE+19              Point to beginning of line
DEV124   MVC   0(8,R2),0(R6)           MOVE UNITNAME TO BUFFER
         LA    R6,8(,R6)               GO TO NEXT UNIT NAMES (RETURNED)
         LA    R2,10(,R2)              Bump pointer in print line
         BCT   R5,DEV121               next unit name
*-> end LOOP
         BAL   R14,SPACE2              BLANK LINE
DEV180   L     R0,0(,R4)               get sub-pool and length
         FREEMAIN R,A=(R4),LV=(R0)
*
*        Display on-line devices
*
         SLR   R2,R2
         LH    R4,DCQUCBCT             number of UCBs
         L     R7,DCQUCBAD             addr of first UCB
         USING UCBOB,R7
*-> LOOP
DEV201   TM    UCBSTAT,UCBONLI         THIS UNIT ONLINE?
         BNO   DEV202                  NO, IGNORE IT
         LA    R2,1(,R2)               count on-line devices
DEV202   L     R7,UCBNXUCB             addr of next UCB
         BCT   R4,DEV201               count next UCB
*-> end LOOP
         STRING '  UCBs:',(DCQUCBCT,H,R7B),' (defined)',               X
               ((R2),,R7B),' (on-line)',INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
         LTR   R2,R2                   any on-line devices?
         BZ    DEV190                  no, exit
         STRING '    CUA  UCBTYP    Unitname  Volser  Status',         X
               INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
         LH    R4,DCQUCBCT             number of UCBs
         L     R7,DCQUCBAD             addr of first UCB
*-> LOOP
DEV221   TM    UCBSTAT,UCBONLI         THIS UNIT ONLINE?
         BNO   DEV228                  NO, IGNORE IT
         MVC   UNITNAME+8(4),UCBTYP    DEVICE TYPE
         BAL   R14,GETUNIT             GET UNITNAME
         CLI   UCBTBYT3,UCB3DACC       DASD?
         BNE   DEV227                  NO, JUMP
*
*        get 3380 type
*
         BAS   R1,DEV_3380             BRANCH AROUND TABLE
         DC    AL1(D3380D),C'D'        DCEMDRDT
         DC    AL1(D3380E),C'E'        DCEMDRDT
         DC    AL1(D3380J),C'J'        DCEMDRDT
         DC    AL1(D3380K),C'K'        DCEMDRDT
         DC    AL1(00),0H'0'
DEV_3380 SLR   R2,R2
         ICM   R2,B'0111',UCBEXTP         UCB common extension
         L     R2,UCBCLEXT-UCBCMEXT(,R2)  Device class extension
         USING DCE,R2
         BAL   R14,SCAN_CLI            SCAN TABLE
         DC    Y(1+1)                  SIZE OF A TABLE ENTRY
         CLI   DCEMDRDT,*-*            TEST 3380 MODEL
         BNE   *+L'*+6                 NOT FOUND, JUMP AROUND "MVC"
         MVC   UNITNAME+4(1),1(R1)     MOVE 3380 TYPE (D/E/J/K)
         DROP  R2
DEV227   STRING 4X,UCBNAME,2X,(UCBTYP,4,X),2X,UNITNAME,INTO=LINE
LINE_UCB EQU   LINE+29,4,C'A'          ucbaddr in line
         STCM  R7,B'1111',LINE_UCB     store UCB addr for DEVSTAT rtne
         BAL   R14,SPACE0              NEXT LINE
DEV228   L     R7,UCBNXUCB             addr of next UCB
         BCT   R4,DEV221               Next UCB
*-> end LOOP
DEV280   BAL   R14,SPACE1              BLANK LINE
DEV190   ICM   R3,B'1111',DCQCHAIN     NEXT DCQ ELEMENT
         BNZ   DEV101                  process it
*-> end LOOP (1)
DEV999   END_PROC
         DROP  R3,R7
         DROP  R12                     END_PROC
         EJECT
*======================================================================
*        Display MCS Consoles
*----------------------------------------------------------------------
CONSOLES BEGIN_PROC ,
         L    R4,CVTCUCB               Unit Control Module (UCM)
         USING UCM,R4
         STRING 'Consoles:',INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
         L     R5,UCMVDATA             1st UCME
         USING UCMLIST,R5              Individual device entry
*-> LOOP (1)
CONSOL21 L     R7,UCMUCB               associated UCB
         USING UCBOB,R7
         STRING '  Id(',(UCMID,FL1,L),')',INTO=LINE
         STRING '  Device(',UCBNAME,')  Inactive',INTO=(LINE+12,30)
         TM    UCMATR,UCMUF            This console active?
         BZ    CONSOL28                no, jump
         BAL   R14,CHECK0              check next line available
         L     R7,UCMFEXTP             addr of fixed extension
         USING UCMEFEXT,R7
         STRING 'Active  ROUTCDE=(',INTO=(LINE+27,NEXTLINE)
         LA    R0,L'UCMEFRC            BYTE LOOP
         LA    R1,UCMEFRC              Routcde bit string
         LA    R2,001                  Route code (1-255)
*-> LOOP (2)
CONS210  TM    0(R1),*-*               CHECK FOR RECORDING OFF
         LA    R3,008                  BIT LOOP
         LA    R6,X'80'                BIT MASK
*-> LOOP (3)
CONS220  EX    R6,CONS210              EXECUTE "TM"
         BZ    CONS227                 Route code off, jump
         CLC   LINE+76(3),BLANKS       check line complete         EU19
         BE    CONS221                 no, continue                EU19
         BAL   R14,SPACE0              yes, bump to next line      EU19
         STRING 44X,((R2),,L),',',INTO=LINE                        EU19
         B     CONS227                                             EU19
CONS221  STRING (LINE,,L),((R2),,L),',',INTO=LINE                  EU19
CONS227  LA    R2,1(,R2)               BUMP RECNO
         SRL   R6,1                    OFFSET MASK
         BCT   R3,CONS220
*-> end LOOP (3)
         LA    R1,1(,R1)               BUMP BYTE ADDRESS
         BCT   R0,CONS210
*-> end LOOP (2)
         LA    R15,LINE(R15)           CHANGE OFFSET TO ADDRESS
         BCTR  R15,0                   OFFSET TO LAST ','
         CLI   0(R15),C','             is it really a comma?
         BE    CONS247                 yes, jump
         LA    R15,1(,R15)             no, do not overlay
CONS247  MVI   0(R15),C')'             CLOSE PARENTHESIS
CONSOL28 BAL   R14,SPACE0              next line
         LM    R14,R15,UCMVDATA+4      length(UCME) Last(UCME)
         BXLE  R5,R14,CONSOL21
*-> end LOOP (1)
*        #SNAP ADDR=UCM,LENGTH=X'170'                            <SNAP>
*        #SNAP ADDR=(R1),LENGTH=UCMESIZE                         <SNAP>
         BAL   R14,SPACE1              BLANK LINE
CONSOL99 END_PROC ,
         DROP  R4,R5,R7
         DROP  R12                     END_PROC
         DROP  R8                      CVT ****************************
         EJECT
*======================================================================
*        Display RACF profile from ACEE
*----------------------------------------------------------------------
RACF00   BEGIN_PROC ,
         L     R2,PSAAOLD-PSA          POINT TO MY ASCB.
         L     R2,ASCBASXB-ASCB(,R2)   POINT TO MY ASXB.
         L     R2,ASXBSENV-ASXB(,R2)   POINT TO MY ACEE.
         USING ACEE,R2
         TM    ACEEFLG1,ACEERACF       this user defined to RACF?
         BZ    RACF99                  no, quit
         STRING 'RACF Profile:',INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
         STRING '  User: ',ACEEUSRI,   RACF user                       X
               '  Group: ',ACEEGRPN,   Connect Group name              X
               '  Terminal: ',ACEETRID, terminal ID                    X
               '    Flags: ',(ACEEFLG1,,X),1X,(ACEEFLG2,,X),1X,        X
               (ACEEFLG3,,X),1X,(ACEEFLG4,,X),1X,                      X
               INTO=LINE
         BAL   R14,SPACE0              NEXT LINE
         BAS   R1,RACF10               BRANCH AROUND TABLE
         DC    AL1(ACEEALTR),C'Alter  '
         DC    AL1(ACEECNTL),C'Control'
         DC    AL1(ACEEUPDT),C'Update '
         DC    AL1(ACEEREAD),C'Read   '
         DC    AL1(ACEENONE),C'None   '
         DC    AL1(00),0H'0'
RACF10   BAL   R14,SCAN_TM             SCAN TABLE
         DC    Y(1+7)                  SIZE OF A TABLE ENTRY
         TM    ACEEFLG2,*-*            Test SMS status on this system
         BO    *+L'*+4                 NOT FOUND, JUMP AROUND "MVC"
         LA    R1,BLANKS               UACC is ondetermined
         STRING '  UACC: ',(1(R1),7),'   Attributes:',                 X
               INTO=LINE
         LA    R4,LINE+1(R15)          authority level
         STATUS ACEEFLG1,ACEESPEC,'Special'
         STATUS ACEEFLG1,ACEEADSP,'ADSP'
         STATUS ACEEFLG1,ACEEOPER,'Operations'
         STATUS ACEEFLG1,ACEEAUDT,'Auditor'
         STATUS ACEEFLG1,ACEELOGU,'Logged'
         STATUS ACEEFLG1,ACEEPRIV,'Privileged'
         TM    ACEEFLG1,ACEESPEC+ACEEADSP+ACEEOPER+ACEEAUDT+ACEELOGU+ACX
               EEPRIV
         BNZ   RACF20                  at least one attribute, jump
         MVC   0(6,R4),=C'(none)'      show it
RACF20   ICM   R3,B'1111',ACEEUNAM                                 EU10
         BZ    RACF40                                              EU10
         XR    R1,R1                                               EU10
         IC    R1,0(R3)                                            EU10
         BCT   R1,*+L'*+4                                          EU10
         B     RACF40                                              EU10
         BAL   R14,SPACE0              next line                   EU10
         STRING '  User name: ',(1(R3),(R1)),INTO=LINE             EU10
RACF40   CLI  ACEEAPLN,C' '            connected Application?      EU10
         BE   RACF41                   no                          EU10
         BAL   R14,SPACE0              next line                   EU10
         STRING '  Connected Application: ',ACEEAPLN,              EU10X
               INTO=LINE                                           EU10
RACF41   TM   ACEEFLG4,ACEEUATH+ACEEDASD+ACEETAPE+ACEETERM         EU10
         BZ   RACF50                   none                        EU10
         TM   ACEEFLG4,ACEEUATH        add user?                   EU10
         BZ   RACF42                   no                          EU10
         BAL   R14,SPACE0              next line                   EU10
         STRING '  User is authorized to define other users',      EU10X
               INTO=LINE                                           EU10
RACF42   TM   ACEEFLG4,ACEEDASD        DASD protect?               EU10
         BZ   RACF43                   no                          EU10
         BAL   R14,SPACE0              next line                   EU10
         STRING '  User is authorized to protect DASD volumes',    EU10X
               INTO=LINE                                           EU10
RACF43   TM   ACEEFLG4,ACEETAPE        TAPE protect?               EU10
         BZ   RACF44                   no                          EU10
         BAL   R14,SPACE0              next line                   EU10
         STRING '  User is authorized to protect TAPE volumes',    EU10X
               INTO=LINE                                           EU10
RACF44   TM   ACEEFLG4,ACEETERM        Terminals protect?          EU10
         BZ   RACF50                   no                          EU10
         BAL   R14,SPACE0              next line                   EU10
         STRING '  User is authorized to protect TERMINALS',       EU10X
               INTO=LINE                                           EU10
RACF50   BAL   R14,SPACE2              BLANK LINE
         DROP  R2
RACF99   END_PROC
         DROP  R12                     END_PROC
         EJECT
*======================================================================
*        Display TSO profile
*----------------------------------------------------------------------
PROF00   BEGIN_PROC ,
         L     R5,TCBJSCB              point at JSCB
         USING IEZJSCB,R5
         ICM   R6,B'1111',JSCBPSCB     point at PSCB
         BZ    PROF99                  not a TSU, exit
         USING PSCB,R6
         STRING 'TSO Profile:',INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
         STRING '  Attributes:',INTO=LINE
         LA    R4,LINE+1(R15)          start of attribute list
         STATUS PSCBATR1,PSCBCTRL,'OPER'
         STATUS PSCBATR1,PSCBACCT,'ACCT'
         STATUS PSCBATR1,PSCBJCL,'SUBMIT'
         STATUS PSCBATR1,PSCBVMNT,'MOUNT'
         STATUS PSCBATR1,PSCBCNAU,'CONSOLE'
         TM    PSCBATR1,PSCBCTRL+PSCBACCT+PSCBJCL+PSCBVMNT+PSCBCNAU
         BNZ   PROF12                  at least one attribute, jump
         MVC   0(6,R4),=C'(none)'      show it
PROF12   BAL   R14,SPACE0              NEXT LINE
         MVC   WKCELL1,=C'Local   '    default destination
         CLI   PSCBDEST,0              any special dest?
         BE    PROF15                  no, jump
         MVC   WKCELL1,PSCBDEST        yes, use it
PROF15   STRING '  Unit: ',PSCBGPNM,'   Dest: ',WKCELL1,               X
               INTO=LINE
         BAL   R14,SPACE0              NEXT LINE
         MVC   WKCELL1(1),PSCBSUBH
         MVC   WKCELL1+1(1),PSCBSUBC
         MVC   WKCELL1+2(1),PSCBSUBM
         MVC   WKCELL1+3(1),PSCBSOUT
         LA    R0,4
         LA    R1,WKCELL1+3
PROF20   CLI   0(R1),0
         BNE   *+L'*+4
         MVI   0(R1),C' '
         BCTR  R1,0
         BCT   R0,PROF20
         STRING '  Submit Hold Class: ',(WKCELL1,1),                   X
               '   Submit Class: ',(WKCELL1+1,1),                      X
               '   Msgclass: ',(WKCELL1+2,1),                          X
               '   Sysout Class: ',(WKCELL1+3,1),                      X
               INTO=LINE
         BAL   R14,SPACE0              NEXT LINE
*
*        Display Profile options and DSN prefix from UPT
*
PROF31   L     R7,PSCBUPT              User profile table
         USING UPT,R7
         MVC   LINE,BLANKS             blank line
         LA    R4,LINE+2               start of options
         BAL   R1,PROF32
         DC    AL1(UPTNPRM),C'Noprompt  '
         DC    AL1(UPTNCOM),C'Nointercom'
         DC    AL1(UPTPAUS),C'Pause     '
         DC     AL1(UPTMID),C'Msgid     '
         DC    AL1(UPTMODE),C'Mode      '
         DC     AL1(UPTWTP),C'WTPMSG    '
         DC    AL1(UPTRCVR),C'Recover   '
         DC    X'00',0H'0'             end of table
PROF31TM TM    UPTSWS,*-*              check UPT switch
*-> LOOP
PROF32   IC    R15,0(,R1)              pick up TM mask
         EX    R15,PROF31TM            test UPT switch
         BO    PROF34                  jump if one
         CLI   1(R1),C'N'              NO-something?
         BE    PROF33                  yes, jump
         STRING 'No',(1(R1),10,T),INTO=((R4),10)
         NI    2(R4),255-X'40'         lowercase
         B     PROF36
PROF33   STRING (1+2(R1),10-2,T),INTO=((R4),10)
         OI    0(R4),X'40'             uppercase
         B     PROF36
PROF34   STRING (1(R1),10,T),INTO=((R4),10)
PROF36   LA    R4,1(R15,R4)            bump pointer
         LA    R1,1+10(,R1)
         CLI   0(R1),0                 end of table?
         BNE   PROF32                  not yet, loop
*-> end LOOP
         BAL   R14,CHECK0              check next line available
         SLR   R1,R1
         IC    R1,UPTPREFL             Prefix length
         STRING 'Prefix(',(UPTPREFX,(R1)),')',INTO=((R4),NEXTLINE)
         BAL   R14,SPACE2              BLANK LINE
PROF99   END_PROC ,
         DROP  R5,R6,R7
         DROP  R12                     END_PROC
         EJECT
*======================================================================
*        Scan the TIOT to display my file allocations
*----------------------------------------------------------------------
TIOT00   BEGIN_PROC ,
         STRING 'TIOT:',INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
         L     R5,TCBTIO               TIOT
         USING TIOT1,R5
         L     R6,PSAAOLD-PSA          MY ASCB
         L     R6,ASCBOUCB-ASCB(,R6)   MY OUCB
         USING OUCB,R6
         L     R1,TCBTCT               SMF TCT
         L     R1,TCTCRTBL-SMFTCT(,R1) STORAGE TABLE
         L     R1,TCTRSZ-TCTCORE(,R1)  REGION SIZE (BELOW)
         ALR   R1,R1                   GET REGION SIZE IN "K"
         STRING '  JOB name: ',TIOCNJOB,'  Step: ',(TIOCSTEP+0,8),     X
               ' Procstep: ',(TIOCSTEP+8,8),                           X
               ' Region=',((R1),,L),'K',                               X
               ' Perform=',(OUCBNPG,H,L),                              X
               INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
*
*        display current allocations
*
         STRING '  DDname  Disp Volser   EXCP Data Set Name',32X,      X
               'Device   Cuu Miscellaneous',INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
*-> LOOP
TIOT_GET CLI   TIOEDDNM,C' '           FREED DD?
         BL    TIOT_NXT                YES, JUMP
         LA    R6,WKWORDS+4            WORK AREA
         ST    R6,WKWORDS              WORK AREA
         USING ZB505,R6
         XC    SWAEPAX,SWAEPAX         CLEAR WORK AREA             @364
         MVC   SWVA,TIOEJFCB           SVA OF THE JFCB
         SWAREQ FCODE=RL,EPA=WKWORDS,MF=(E,SWAREQL1),UNAUTH=YES
         L     R6,SWBLKPTR             31-BIT ADDRESS OF JFCB
         USING INFMJFCB,R6
         MVC   WKCELL1,BLANKS          CLEAR WORK AREA
         MVC   WKCELL2,BLANKS          CLEAR WORK AREA
         MVC   STATUS,BLANKS           CLEAR WORK AREA
         MVC   UNITNAME,BLANKS         CLEAR WORK AREA
         MVC   VOLSER,=C'JES     '     SYSIN/SYSOUT
         SLR   R1,R1                   EXCP=0
         SLR   R7,R7                   NO UCB
         TM    TIOELINK,TIOESSDS       IS THIS A JES DATA SET?
         BO    TIOT61                  YES, JUMP
         MVC   VOLSER,=C'DUMMY   '     DUMMY DATA SET
         ICM   R7,B'0111',TIOEFSRT     UCB ADDRESS
         BZ    TIOT64                  NO UCB, SKIP TCT SCAN
         USING UCBOB,R7
         MVC   UNITNAME+8(4),UCBTYP    DEVICE TYPE
         BAL   R14,GETUNIT2        <== GET UNITNAME
         MVC   VOLSER,=C'VIO     '     UNIT=VIO
         TM    UCBOB,UCBVRDEV          IS THIS A VIO DATA SET?
         BO    TIOT41                  YES, JUMP
         MVC   VOLSER,JFCBVOLS         NO, MOVE FIRST VOLSER
TIOT41   BAS   R1,TIOT41B
         DC    AL1(JFCNEW),C'NEW'
         DC    AL1(JFCMOD),C'MOD'
         DC    AL1(JFCOLD+JFCSHARE),C'SHR'
         DC    AL1(JFCOLD),C'OLD'
         DC    X'00',0H'0'
TIOT41B  BAL   R14,SCAN_TM             CALL SCAN RTNE
         DC    Y(1+3)               0  LENGTH OF A TABLE ENTRY
         TM    JFCBIND2,*-*         2  TEST DATA SET STATUS
         MVC   STATUS,1(R1)         6  DISP
         TM    UCBFL5,UCBSMS           SMS VOL? (X'20')
         BZ    TIOT46                  NO, JUMP
         MVC   WKCELL1(3),=C'SMS'      SMS-MANAGED
TIOT46   CLI   UCBTBYT3,UCB3TAPE       TAPE DEVICE?
         BNE   TIOT61                  NO, JUMP
         BAS   R1,TIOT46B
         DC    AL1(JFCBLTM),C'LTM'
         DC    AL1(JFCBLP),C'BLP'
         DC    AL1(JFCSUL),C'SUL'
         DC    AL1(JFCSUL+JFCBAL),C'AUL'
         DC    AL1(JFCBAL),C'AL '
         DC    AL1(JFCNSL),C'NSL'
         DC    AL1(JFCSL),C'SL '
         DC    AL1(JFCNL),C'NL '
         DC    X'00',0H'0'
TIOT46B  BAL   R14,SCAN_TM             CALL SCAN RTNE
         DC    Y(1+3)               0  LENGTH OF A TABLE ENTRY
         TM    JFCBLTYP,*-*         2  TEST LABEL TYPE FLAGS
         LH    R0,JFCBFLSQ             PICK UP FILE SEQUENCE NUMBER
         LTR   R0,R0                   LABEL=0?
         BNZ   TIOT46Y                 NO, JUMP
         LA    R0,0001                 YES, CHANGE TO LABEL=1
TIOT46Y  STRING ((R0),,L),',',(1(R1),3),INTO=WKCELL1
TIOT61   LA    R0,TIOENTRY             POINT TO CURRENT TIOT ENTRY
         SL    R0,TCBTIO               CHANGE ADDRESS TO OFFSET
         BAL   R14,EXCP00          <== GET EXCP COUNT IN R1
         STRING ((R1),,R6B),INTO=WKCELL2
         CL    R1,=F'100000'           OVER 100K?
         BL    TIOT64                  NO, JUMP
         SLR   R0,R0                   YES, DIVIDE BY 1000
         LA    R1,500(,R1)             YES, DIVIDE BY 1000
         D     R0,=F'1000'             YES, DIVIDE BY 1000
         STRING ((R1),,R4B),'K',INTO=WKCELL2
TIOT64   LTR   R7,R7                   DO WE HAVE AN UCB?
         BNZ   TIOT_PRT                YES, JUMP
         LA    R7,BLANKS               NO, POINT TO A DUMMY UCB
TIOT_PRT STRING 2X,TIOEDDNM,1X,STATUS,1X,VOLSER,1X,(WKCELL2,7),        X
               JFCBDSNM,1X,UNITNAME,1X,UCBNAME,1X,WKCELL1,             X
               INTO=LINE
         MVI   LINE_LNK2,C'?'          SHOW LNKLST STATUS
         MVI   LINE_LPA2,C'?'          SHOW LPALST STATUS
         BAL   R14,SPACE0              NEXT LINE
TIOT_NXT SLR   R0,R0                   PREPARE IC
         IC    R0,TIOELNGH             LOAD LENGTH OF CURRENT ENTRY
         ALR   R5,R0                   BUMP UP TO NEXT ENTRY
         CLI   TIOELNGH,0              IS THIS THE END?
         BNZ   TIOT_GET                NOT YET, LOOP THROUGH TIOT
*-> end LOOP
         BAL   R14,SPACE1              BLANK LINE
         END_PROC
         DROP  R5,R6,R7
*----------------------------------------------------------------------
*        "EXCP00" routine => GET EXCP COUNT IN R1
*                            R14 - link register
*----------------------------------------------------------------------
EXCP00   L     R1,PSATOLD-PSA          MY TCB
         L     R1,TCBTCT-TCB(,R1)      TCBTCT
         L     R1,TCTIOTBL-SMFTCT(,R1) START OF I/O MEASUREMENT TABLE
         LA    R1,TCTIODSP-TCTTIOT(,R1) FIRST DD ENTRY
         USING TCTDCBTD,R1
*-> LOOP
EXCP11   CL    R0,TCTDCBTD             SAME TIOT OFFSET?
         BE    EXCP21                  YES, EXIT LOOP
         LA    R1,TCTDCBLE             NEXT LOOKUP TABLE ENTRY
         ICM   R15,B'1111',TCTDCBLE    END OF TABLE?
         BNZ   EXCP11                  NOT YET, TRY NEXT DD ENTRY
*-> end LOOP
         SLR   R0,R0                   EXCP COUNT IS ZERO
         B     EXCP99
EXCP21   L     R1,TCTIOTSD             OFFSET IN I/O MEASURE. TABLE
         L     R15,PSATOLD-PSA         MY TCB
         L     R15,TCBTCT-TCB(,R15)    MY TCT
         A     R1,TCTIOTBL-SMFTCT(,R15) START OF I/O MEASUREMENT TABLE
         USING TCTDDENT,R1
         ICM   R0,B'1111',TCTDCTR      EXCP COUNT IN R0
EXCP99   LR    R1,R0                   EXCP COUNT IN R1 TOO
         BR    R14
         DROP  R1
         DROP  R12                     END_PROC
         EJECT
*======================================================================
*        DISPLAY TCB TREE AND RB CHAINS
*----------------------------------------------------------------------
TCB_TREE BEGIN_PROC
         L     R9,JSTCB                THE JOB STEP TCB
         STRING 'TCB Tree and RB Chains:',INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
         STRING '    TCB Address',18X,INTO=LINE,                       X
               'Program    IC  STAB  FLAGS1 CDFLGS    DDname       DataX
                 Programs'
         BAL   R14,SPACE2              BLANK LINE
         SLR   R3,R3                   INDENTATION INDEX
*-> LOOP (1)
TREE100  STCM  R9,B'0111',WKCELL1      STORE TCB ADDR
         STRING (BLANKS,4(R3)),(WKCELL1,3,X),INTO=LINE
         L     R5,TCBRBP               POINT TO TOP RB
         LA    R4,WK256                START OF RB TABLE
*
*        BUILD RB TABLE
*-> LOOP
TREE110  ICM   R5,B'1000',CVTPTR       CLEAN UP HI-ORDER BYTE
         SH    R5,=Y(RBBASIC-RBPREFIX) POINT TO RBPREFIX
         USING RBPREFIX,R5
         ST    R5,0(,R4)               STORE RB ADDRESS
         TM    RBSTAB2,RBTCBNXT        CHECK FOR END OF CHAIN
         L     R5,RBLINK               POINT TO PREVIOUS RB (OR TCB)
         LA    R4,4(,R4)               BUMP UP TO NEXT TABLE ENTRY
         BZ    TREE110                 JUMP IF RB FOR 1ST ATTACHED PGM
*-> end LOOP
*
*        PROCESS RB TABLE BACKWARDS
*-> LOOP
TREE200  SH    R4,=H'4'                PREVIOUS ENTRY IN RB TABLE
         L     R5,0(,R4)               LOAD RB ADDRESS
         CLI   RBSTAB1,RBFTPRB         IS THIS A PRB?
         BNE   TREE280                 NO, IGNORE IT
         TM    RBCDFLGS,RBCDSYNC       CHECK FLAGS
         BO    TREE260                 JUMP IF IT IS A SYNCH PRB
         MVC   WKCELL1,BLANKS          TASKLIB DDNAME
         ICM   R1,B'1111',TCBJLB       LOAD/TEST DCB ADDRESS
         BZ    TREE250                 NO TASKLIB, JUMP
         MVC   WKCELL1,=C'*LNKLST '
         L     R14,CVTPTR              POINT AT CVT
         CL    R1,CVTLINK-CVTMAP(,R14) SYS1.LINKLIB
         BE    TREE250                 JUMP IF SYS1.LINKLIB DCB ADDR
         LH    R1,DCBTIOT-IHADCB(,R1)  LOAD TIOT OFFSET
         A     R1,TCBTIO               CHANGE OFFSET INTO AN ADDRESS
         MVC   WKCELL1,4(R1)           MOVE TASKLIB DDNAME
TREE250  BAL   R14,TALLY            <- TALLY allocated storage
         STM   R0,R1,WKCELL3           data, programs
         L     R1,RBCDE                POINT TO CDE/LPDE
         STRING INTO=(LINE+30,L'LINE),3X,                              X
               (CDNAME-CDENTRY(R1),8),3X,      PGM NAME                X
               (RBWLIC+3,1,X),2X,      IC                              X
               (RBSTAB,2,X),4X,        STATUS BYTE                     X
               (RBFLAGS1,,X),5X,       Flags                           X
               (RBCDFLGS,,X),6X,       FLAGS                           X
               WKCELL1,                DDNAME OF TASKLIB               X
               (WKCELL3,F,R8B),'K',    storage (data)                  X
               (WKCELL3+4,F,R9B),'K'   storage (programs)
         B     TREE270
TREE260  MVC   WKCELL1,RBGRS15         PICK UP ENTRY POINT ADDRESS
         NI    WKCELL1+3,X'FE'         SET BIT 31 TO ZERO
         STRING INTO=(LINE+30,L'LINE),3X,                              X
               (WKCELL1,4,X),3X,       EP ADDRESS                      X
               (RBWLIC+3,1,X),2X,                                      X
               (RBSTAB,2,X),4X,        Flags                           X
               (RBFLAGS1,,X),5X,       Flags                           X
               (RBCDFLGS,,X)           Flags
TREE270  BAL   R14,SPACE0              NEXT LINE
TREE280  LA    R0,WK256                CHECK FOR END OF CHAIN
         CR    R4,R0                   CHECK FOR END OF CHAIN
         BH    TREE200                 LOOP THROUGH RB TABLE
*-> end LOOP
         BAL   R14,SCANTCB             GET NEXT TCB
         BNZ   TREE100                 PROCESS NEXT TCB
*-> end LOOP (1)
         BAL   R14,SPACE1              BLANK LINE
         STRING '   STAB1: 00-PRB    C0-SVRB   40-IRB    60-TIRB   ',  X
               '08-WAITP  02-ATNXIT 01-PMSVRB',INTO=LINE
         BAL   R14,SPACE0              NEXT LINE
         STRING '   STAB2: 80-TCBNXT 40-FACTV  20-ATTN   10-ETXR   ',  X
               '0C-IQETP  02-FDYN   01-ECBWT',INTO=LINE
         BAL   R14,SPACE0              NEXT LINE
         STRING '  FLAGS1: 80-SLOCK  40-XWAIT  20-ABEND  10-XWPRM  ',  X
               '08-ASIR   04-LONGWT 02-SCB     01-SSSYN',INTO=LINE
         BAL   R14,SPACE0              NEXT LINE
         STRING '  CDFLGS: 80-NOCELL 20-CDATCH 10-CDSAVE 08-CDNODE ',  X
               '04-CDSYNC 02-CDXCTL 01-CDLOAD',INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
         END_PROC
         DROP  R5
         DROP  R12                     END_PROC
*----------------------------------------------------------------------
*        TALLY allocated storage for current TCB
*----------------------------------------------------------------------
TALLY    BALR  R15,0
         SAVE  (14,12),,TALLY
         BALR  R12,0
         USING *,R12                   local base register
         SLR   R0,R0                   STORAGE ALLOCATED (DATA)
         SLR   R1,R1                   STORAGE ALLOCATED (PROGRAMS)
         L     R2,TCBMSS               FIRST SUB-POOL FOR THIS TCB
         USING SPQE,R2
*-> LOOP (1)
TALLY21  CL    R9,TCBJSTCB             Am I the JS TCB?
         BE    TALLY21L                yes, then I assume ownership
         CL    R9,SPQETCB              Do I own this subpool?
         BNE   TALLY29                 no, ignore it
TALLY21L L     R4,SPQESPQA             chain descriptors
         USING SPQA,R4
         LA    R5,3                    3 ENTRIES IN SPQA
*-> LOOP (2)
TALLY22  CL    R4,0(,R4)               EMPTY ENTRY?
         BE    TALLY25                 YES, EXIT
         L     R6,0(,R4)               FIRST/LAST DQE
         USING DQE,R6
*-> LOOP (3)
TALLY23  CLI   SPQEID,251              PROGRAM SUB-POOL?
         BE    TALLY23P                YES, JUMP
         CLI   SPQEID,252              PROGRAM SUB-POOL?
         BE    TALLY23P                YES, JUMP
         AL    R0,DQESIZE              ADD SIZE (DATA)
         B     TALLY23Q
TALLY23P AL    R1,DQESIZE              ADD SIZE (PROGRAMS)
TALLY23Q CL    R6,4(,R4)               LAST DQE ON THIS CHAIN?
         L     R6,DQENEXT
         BNE   TALLY23                 NO, PROCESS NEXT ONE
*-> end LOOP (3)
TALLY25  LA    R4,8(,R4)               NEXT ENTRY IN SPQA
         BCT   R5,TALLY22
*-> end LOOP (2)
TALLY29  ICM   R2,B'1111',SPQENEXT     NEXT SUBPOOL
         BNZ   TALLY21
*-> end LOOP (1)
         SRL   R0,10                   data bytes, in K
         SRL   R1,10                   program bytes, in K
         L     R14,12(,R13)
         LM    R2,R12,28(R13)
         BR    R14
         DROP  R2,R4,R6
         DROP  R12                     local base register
         EJECT
*======================================================================
*        DISPLAY JPAQ (CDE CHAIN)
*----------------------------------------------------------------------
JPAQ     BEGIN_PROC ,
         L     R9,JSTCB                THE JOB STEP TCB
         STRING 'JPAQ:',INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
         L     R5,TCBJPQ               POINT TO FIRST CDE IN CHAIN
         USING CDENTRY,R5
         STRING '   NAME     ENTPT    ATTRB SP ATTR  ATTR2  USE ',     X
               '   MAJ-CDE   LENGTH   LOAD-PNT',INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
*-> LOOP
JPAQ21   L     R6,CDXLMJP              POINT TO XL (OR MAJOR CDE)
         TM    CDATTR,CDMIN            CHECK ATTRIBUTES
         BO    JPAQ25                  JUMP IF THIS IS A MINOR CDE
         USING XTLST,R6
         STRING 3X,CDNAME,1X,(CDENTPT,,X),3X,(CDATTRB,,X),2X,          X
               (CDSP,,X),2X,(CDATTR,,X),4X,(CDATTR2,,X),4X,(CDUSE,,X), X
               13X,(XTLMSBLN,,X),3X,(XTLMSBAD,,X),INTO=LINE
         B     JPAQ29
JPAQ25   STRING 3X,CDNAME,1X,(CDENTPT,,X),3X,(CDATTRB,,X),6X,          X
               (CDATTR,,X),4X,(CDATTR2,,X),11X,(CDNAME-CDENTRY(R6),8), X
               INTO=LINE
JPAQ29   BAL   R14,SPACE0              NEXT LINE
         ICM   R5,B'1111',CDCHAIN      CHECK FOR END OF CHAIN
         BNZ   JPAQ21                  LOOP THROUGH RB TABLE
*-> end LOOP
         DROP  R5,R6
         BAL   R14,SPACE1              BLANK LINE
         STRING '   ATTRB: 80-EOM      40-IDENTY 20-DIRTY         ',   X
               '08-LPDE      04-GLOBAL 02-CONTAMINATION 01-RACF',      X
               INTO=LINE
         BAL   R14,SPACE0              NEXT LINE
         STRING '    ATTR: 80-NIP/MLPA 40-NIC    20-RENT  10-REUS ',   X
               '08-NREUS     04-MINOR  02-JPAQ          01-NOT_OL',    X
               INTO=LINE
         BAL   R14,SPACE0              NEXT LINE
         STRING '   ATTR2: 80-SP0      40-REL    20-XLE   10-RLC  ',   X
               '08-AMODE_ANY 04-OVLY   02-APFLIB        01-AC=1',      X
               INTO=LINE
         BAL   R14,SPACE2              BLANK LINE
         END_PROC
         DROP  R12                     END_PROC
         EJECT
*======================================================================
*        DISPLAY LOAD-LISTS
*----------------------------------------------------------------------
LOADLIST BEGIN_PROC
         L     R9,JSTCB                THE JOB STEP TCB
         STRING 'LOAD list:',INTO=LINE
         LA    R2,LINE+1(R15)
         BAL   R14,SPACE0              NEXT LINE
         SLR   R3,R3                   INDENTATION INDEX
*
*        Tally allocated storage
*
*-> LOOP (1)
LOADL11  BAL   R14,SPACE1
*
*        Get name of current program
*
         L     R5,TCBRBP               TOP RB
         USING RBBASIC,R5
LOADL26  TM    RBCDFLGS,RBCDSYNC       THIS RB CREATED BY SYNCH?
         BO    LOADL26I                YES, JUMP
         TM    RBSTAB1,RBFTSVRB        IS THIS AN SVRB?
         BNO   LOADL27                 NO, JUMP
*        #SNAP ADDR=RBBASIC,LENGTH=44                            <SNAP>
LOADL26I ICM   R5,B'0111',RBLINK+1     POINT TO PREVIOUS RB
*        #SNAP ADDR=RBBASIC,LENGTH=44                            <SNAP>
         B     LOADL26                 TEST SYNCH FLAG AGAIN
LOADL27  L     R5,RBCDE                POINT TO CDE/LPDE
         USING CDENTRY,R5
         STCM  R9,B'0111',WKCELL1      STORE TCB ADDR
         STRING '  TCB: ',(WKCELL1,3,X),'   Program: ',CDNAME,INTO=LINE
         BAL   R14,SPACE2
*
*        Display load-list
*
         ICM   R4,B'1111',TCBLLS       POINT TO LAST LLE IN CHAIN
         BZ    LOADL80                 SKIP THIS TCB IF NO LLE CHAIN
         USING LLE,R4
*-> LOOP (2)
LOADL70  STCM  R4,B'0111',WKCELL1      STORE LLE ADDR
         L     R5,LLECDPT              CDE PTR
         TM    CDATTR,CDMIN            CHECK ATTRIBUTES
         BNO   LOADL72                 JUMP IF THIS IS A MAJOR CDE
         L     R5,CDXLMJP              POINT TO MAJOR CDE
LOADL72  L     R6,CDXLMJP              POINT TO XL
         USING XTLST,R6
         TM    CDATTRB,CDELPDE         CHECK ATTRIBUTES
         BO    LOADL75                 JUMP IF THIS IS A LPA MODULE
         STRING 3X,CDNAME,1X,(CDENTPT,,X),3X,(CDATTRB,,X),2X,          X
               (CDSP,,X),2X,(CDATTR,,X),4X,(CDATTR2,,X),4X,            X
               (LLECOUNT,,X),3X,(LLESYSCT,,X),6X,                      X
               (XTLMSBLN,,X),3X,(XTLMSBAD,,X),INTO=LINE
         B     LOADL79
         USING LPDE,R5
LOADL75  STRING 3X,LPDENAME,1X,(LPDENTP,,X),3X,(LPDEATTB,,X),2X,       X
               (LPDESP,,X),2X,(LPDEATTR,,X),4X,(LPDEATT2,,X),4X,       X
               (LLECOUNT,,X),3X,(LLESYSCT,,X),6X,                      X
               (LPDEXTLN+1,3,X),3X,(LPDEXTAD,,X),INTO=LINE
LOADL79  BAL   R14,SPACE0              NEXT LINE
         ICM   R4,B'1111',LLECHN       CHECK FOR END OF CHAIN
         BNZ   LOADL70                 LOOP THROUGH LOAD LIST
*-> end LOOP (2)
LOADL80  BAL   R14,SCANTCB             NEXT TCB IN TREE
         BNZ   LOADL11
*-> end LOOP (1)
         END_PROC
         DROP  R4,R5,R6
         DROP  R12                     END_PROC
         DROP  R9                      TCB ****************************
         EJECT
*======================================================================
*        INVOKE ISPF/PDF "BRIF" SERVICE
*----------------------------------------------------------------------
BRIF     BEGIN_PROC
         LA    R0,LINES                FIRST LINE
         LR    R1,R10                  LAST LINE
         SR    R1,R0                   SIZE
         SR    R0,R0
         D     R0,=A(L'LINE)           NUMBER OF LINES
         ST    R1,NUMLINES             FOR BRIF
         AIF   (NOT D'DFASMS).SMS1     NOT DFP V3, JUMP
*
*        Free the unused part of the working-storage area
*        (reserve an additional 4K for the DEVSTAT routine).
*
         LA    R1,LINE+4095            4K work area
         N     R1,=F'-4096'            ROUND UP
         ST    R1,WORK4K               save address for DEVSTAT rtne
         A     R1,=H'4096'             leave room for a 4K work area
         CL    R1,DYN_CHK              end of work area overflow?
         BL    BRIF100                 no, OK
         L     R1,=A(WTOSK4)           yes, signal shortage
         WTO   MF=(E,(1))
         OI    FSWITCH,ERRBRIF
         B     BRIF900                 QUIT
BRIF100  L     R2,DYNAML2              LENGTH OF DYNAMIC STORAGE AREA
         LA    R2,DYNAM(R2)            END OF DYNAMIC STORAGE AREA
         SR    R2,R1                   LENGTH OF UNUSED STORAGE
         FREEMAIN RU,LV=(R2),A=(R1)    FREE UNUSED STORAGE
         SLR   R1,R13                  LENGTH OF STORAGE USED
         ST    R1,DYNAML2              STORE IT FOR NEXT FREEMAIN
.SMS1    TM    FSWITCH,HARDCOPY        Is this a hardcopy run?
         BO    BRIF300                 YES, JUMP
*
*        ATTACH the LOCATE/OBTAIN SUB-TASK
*
         MVC   ENQL1(ENQL1L),ENQM1     MODEL ENQ TO DYNAMIC STRGE  @364
         L     R2,PSATOLD-PSA          my TCB                      @380
         L     R2,TCBRBP-TCB(,R2)      my PRB                      @380
         L     R2,RBCDE-RBBASIC(,R2)   my CDE/LPDE                 @380
         XC    ECB1(2*L'ECB1),ECB1     clear ECB,TCB
         LA    R1,SUB_WORK             PARM for sub task
         LNR   R1,R1                   make it negative            @380
         ATTACH EPLOC=CDNAME-CDENTRY(R2),   my own name            @380X
               SZERO=NO,SF=(E,ATTACHL1)
         LTR   R15,R15                 successful completion?      EU13
         BZ    BRIF200                 yes                         EU13
         L     R1,=A(WTOSK7)           no, diagnose error
         MVC   WK256(WTOSK7L),0(R1)
         CVD   R15,WKCELL1
         MVC   WKCELL1(4),=X'40202120'
         ED    WKCELL1(4),WKCELL1+L'WKCELL1-2
         MVC   WK256+WTOSK7C(L'WTOSK7C),WKCELL1
         WTO   MF=(E,WK256)
         OI    FSWITCH,ERRBRIF
         B     BRIF900                 QUIT
BRIF200  ST    R1,ECB1+4               SAVE TCB ADDRESS
         LA    R0,DYNAM                DIALOG DATA addr
         ST    R0,WKWORDS+32           DIALOG DATA PTR
         XCALL ISPLINK,(=CL8'BRIF',    ISPF function                   X
               TITLE,                  data-name (dsname)              X
               =C'F ',                 rec-format                      X
               =A(L'LINE),             rec-len                         X
               =A(RDRTNE),             read-routine                    X
               0,                      cmd-routine address             X
               WKWORDS+32),            dialog-data                     X
               ERRET=BRIF212           in case of a problem        EU13
BRIF210  ENQ   (EDQNAME,EDRNAME,E,L'EDRNAME),                      @364X
               MF=(E,ENQL1)            SERIALIZE DETACH            EU15
         TM    ECB1,X'40'              SUB TASK ENDED ALREADY?     @364
         BO    BRIF211                 YES, EXIT                   @364
         DETACH ECB1+4                 DETACH IT                   @364
BRIF211  DEQ   MF=(E,ENQL1)            DE-SERIALIZE DETACH         @364
         B     BRIF900                 EXIT                        @364
BRIF212  TM    FSWITCH,FLGNOMSG        NOMSG flag (debug)          EU18
         BO    BRIF210                 skip diagnose               EU18
         L     R1,=A(WTOSK2)           diagnose error
         MVC   WK256(WTOSK2L),0(R1)
         MVC   WK256+WTOSK2N(L'WTOSK2N),=CL8'BRIF'
         CVD   R15,WKCELL1
         MVC   WKCELL1(4),=X'40202120'
         ED    WKCELL1(4),WKCELL1+L'WKCELL1-2
         MVC   WK256+WTOSK2R(L'WTOSK2R),WKCELL1
         WTO   MF=(E,WK256)
         OI    FSWITCH,ERRBRIF
         B     BRIF210
ENQM1    ENQ   (*-*,*-*,E,0),MF=L                                  @364
*                                                                  EU13
*        Execute the SUB-TASK as a subroutine                      EU13
*                                                                  EU13
BRIF300  LA    R1,SUB_WORK             PARM for sub task           EU13
         LNR   R1,R1                   make it negative as needed  EU13
         MVI   ECB1+7,X'FF'            mark as routine called      EU13
         L     R15,=A(SUB_RTNE)        get SUB_TASK routine entry  EU13
         BALR  R14,R15                 go there                    EU13
*
*        Write lines to DD=SHOWMVS or DD=dyn.alloc.                EU13
*
         GETMAIN R,LV=DYNAM24L         GET 24-BIT WORK AREA
         LR    R7,R1
         USING DYNAM24,R7
         LA    R0,CODE24M              MOVE 24-BIT CODE MODEL
         L     R1,=A(CODE24L)
         LA    R14,CODE24
         LR    R15,R1
         MVCL  R14,R0
         TM    FSWITCH,TSORUN          are we under TSO?           EU13
         BO    *+L'*+10                yes                         EU13
         MVC   DCB24+(DCBDDNAM-IHADCB)(L'MY_DDN),MY_DDN            EU13
         B     *+L'*+6                                             EU13
         MVC   DCB24+(DCBDDNAM-IHADCB)(L'RT_DDN),RT_DDN            EU13
         LA    R1,SYNAD24              set SYNAD address
         ST    R1,DCB24+(DCBSYNAD-IHADCB)
         MVI   WTO24T,0                set indicator
         LA    R1,XLST24               set DCB exit list address
         STCM  R1,B'0111',DCB24+(DCBEXLSA-IHADCB)
         LA    R1,XDCB24               set DCB exit address
         STCM  R1,B'0111',XLST24+1
         OPEN  (DCB24,OUTPUT),MF=(E,OPEN24)
         LTR   R15,R15                 how about OPEN RC?
         BZ    BRIF301                 ok
         L     R1,=A(WTOSK8)           diagnose error
         MVC   WK256(WTOSK8L),0(R1)
         CVD   R15,WKCELL1
         MVC   WKCELL1(4),=X'40202120'
         ED    WKCELL1(4),WKCELL1+L'WKCELL1-2
         MVC   WK256+WTOSK8C(L'WTOSK8C),WKCELL1
         WTO   MF=(E,WK256)
         OI    FSWITCH,ERRBRIF
         B     BRIF420
BRIF301  TM    DCB24+(DCBOFLGS-IHADCB),DCBOFOPN successfully complete?
         BO    BRIF302                 ok
         L     R1,=A(WTOSK9)           it's not opened
         WTO   MF=(E,(1))
         OI    FSWITCH,ERRBRIF
         B     BRIF420
BRIF302  CLI   XERR24C,0               all ok?
         BE    BRIF303                 yes, continue
         XR    R3,R3                   no, give error message
         IC    R3,XERR24C
         BCTR  R3,0
         SLL   R3,2
         L     R1,BRIFEMV(R3)
         WTO   MF=(E,(1))
         OI    FSWITCH,ERRBRIF
         B     BRIF400
BRIF303  LA    R3,1                    LINE COUNT (for page eject)
         LA    R4,1                    record count
*-> LOOP
BRIF310  ST    R4,WKCELL2              relative record number      EU13
         ST    R13,WKCELL3             data area for read routine
         XCALL =A(RDRTNE),             BRIF READ ROUTINE               X
               (WKCELL1,               addr of record                  X
               =A(L'LINE),             LRECL                           X
               WKCELL2,                RRN                             X
               WKCELL3),               DYNAM-DATA                      X
               ERRET=BRIF400           end-of-data
         LA    R15,CODE24              24-BIT CODE                 EU13
         BASSM R2,R15                  EXECUTE 24-BIT CODE         EU13
         CLI   WTO24T,0                PUT error occurred?         EU13
         BNE   BRIF360                 yes                         EU13
         LR    R5,R1                   save line address           EU13
*        TM    WORK4K,X'F0'                                        ****
*        BZ    *+L'*+4                                             ****
*        EX    0,*                                                 ****
         L     R10,WKCELL1             A(LINE)
         CL    R10,TLNEPT              first (TITLE) line?         EU15
         BNE   BRIF312                 no, jump                    EU15
         MVI   0(R5),C'1'              ASA control                 EU15
BRIF311  MVC   1(L'LINE,R5),LINE       move current line to buffer EU15
         BAL   R14,BRIF350             go set page number          EU15
         B     BRIF316                                             EU15
BRIF312  CLI   LINE,C' '               IS THIS A NEW SECTION?
         BE    BRIF313                 NO, JUMP
         CH    R3,=H'6'                AT LEAST 6 LINES LEFT?
         BNH   BRIF314                 NO, NEW PAGE
BRIF313  BCT   R3,BRIF315              DECREMENT LINE COUNT
BRIF314  TM    CURPGN,X'40'                                        EU15
         BO    BRIF311                                             EU15
         MVI   0(R5),C'1'              ASA control                 EU15
         L     R14,TLNEPT              get TITLE line pointer      EU15
         MVC   1(L'LINE,R5),0(R14)     move TITLE line to buffer   EU15
         BAL   R14,BRIF350             go set page number          EU15
         LA    R15,CODE24              24-BIT CODE                 EU15
         BASSM R2,R15                  EXECUTE 24-BIT CODE         EU15
         CLI   WTO24T,0                PUT error occurred?         EU15
         BNE   BRIF360                 yes                         EU15
         LR    R5,R1                   save line address           EU15
         MVI   0(R5),C' '              ASA control                 EU15
         MVC   1(L'LINE,R5),BLANKS     move SPACE line to buffer   EU15
         LA    R15,CODE24              24-BIT CODE                 EU15
         BASSM R2,R15                  EXECUTE 24-BIT CODE         EU15
         CLI   WTO24T,0                PUT error occurred?         EU15
         BNE   BRIF360                 yes                         EU15
         LR    R5,R1                   save line address           EU15
         BCTR  R3,0                    minus 2 lines               EU15
         BCTR  R3,0                                                EU15
BRIF315  MVI   0(R5),C' '              ASA control                 EU15
         MVC   1(L'LINE,R5),LINE       move current line to buffer EU15
BRIF316  LA    R4,1(,R4)               BUMP LINE counter
         B     BRIF310                 NEXT LINE
*-> end LOOP
BRIF350  TM    CURPGN,X'40'                                        EU15
         BO    BRIF351                                             EU15
         L     R3,CURPGN               current page routine        EU15
         CVD   R3,WKCELL2              edit current page count     EU15
         MVC   PGNEPT(L'PGNEPT,R5),=XL6'402020202120'              EU15
         ED    PGNEPT(L'PGNEPT,R5),WKCELL2+L'WKCELL2-3             EU15
         A     R3,=F'1'                update current page count   EU15
         ST    R3,CURPGN                                           EU15
BRIF351  L     R3,MAXLPG               Max. lines per page         EU15
         BR    R14                     return                      EU15
BRIF360  LA    R1,WTO24                PUT error message
         WTO   MF=(E,(1))
         OI    FSWITCH,ERRBRIF
BRIF400  CLOSE MF=(E,OPEN24)
         LTR   R15,R15                 how about CLOSE RC?
         BZ    BRIF410                 ok
         L     R1,=A(WTOSK10)          diagnose error
         MVC   WK256(WTOSK10L),0(R1)
         CVD   R15,WKCELL1
         MVC   WKCELL1(4),=X'40202120'
         ED    WKCELL1(4),WKCELL1+L'WKCELL1-2
         MVC   WK256+WTOSK10C(L'WTOSK10C),WKCELL1
         WTO   MF=(E,WK256)
         OI    FSWITCH,ERRBRIF
BRIF410  FREEPOOL DCB24
BRIF420  FREEMAIN R,LV=DYNAM24L,A=(R7) FREE 24-BIT WORK AREA
         DROP  R7
BRIF900  XR    R15,R15                 set RC=0
         TM    FSWITCH,ERRBRIF
         BZ    BRIF999
         LA    R15,16                  pass back RC for error
BRIF999  END_PROC
BRIFEMV  DC    A(WTOSK12,WTOSK13,WTOSK14,WTOSK15)
*----------------------------------------------------------------------
*        RMODE24 PUT Routine (24-bit code model)
*----------------------------------------------------------------------
         CNOP  0,4
CODE24M  ST    R13,4(,R7)              24-BIT SAVE AREA
         LR    R13,R7                  24-BIT SAVE AREA
         LA    R1,DCB24-CODE24(,R15)   GET DCB ADDRESS
         PUT   (1)                     PUT LOCATE
         L     R13,4(,R13)             31-BIT SAVE AREA
         BSM   0,R2                    GOBACK WITH AMODE31
         CNOP  0,4
SYNAD24M STM   R13,R2,SAVE24(R15)      save registers
         LR    R2,R15                  use as base
         USING SYNAD24M,R2
         SYNADAF ACSMETH=QSAM          get error message from OS
         MVC   WTO24M(L'WTO24M,R2),68(R1) get usefull text
         SYNADRLS                      free OS message
         DROP  R2
         LM    R13,R2,SAVE24(R2)       restore registers
         BR    R14                     return
         OPEN  (*-*,OUTPUT),MF=L
         DC    0F'0',XL1'85',AL3(*-*)  DCB exit list
         USING IHADCB,R1
         USING XDCB24M,R15
XDCB24M  XR    R0,R0
         ICM   R0,B'0011',DCBLRECL     get LRECL
         BZ    *+L'*+14                branch if undefined
         CLM   R0,B'0011',XLRL24(R15)  LRECL= as needed?
         BE    *+L'*+6                 yes
         MVI   XERR24(R15),1           no, set error 1
         BR    R14                     return
         TM    DCBRECFM,DCBRECV        RECFM= V or U?
         BZ    *+L'*+6                 no
         MVI   XERR24(R15),2           yes, set error 2
         BR    R14                     return
         TM    DCBRECFM,DCBRECF        RECFM= F?
         BO    *+L'*+4                 yes
         OI    XREC24(R15),DCBRECF     no, set it
         TM    DCBRECFM,DCBRECCM       CC= Machine defined?
         BZ    *+L'*+6                 no
         MVI   XERR24(R15),3           yes, set error 3
         BR    R14                     return
         TM    DCBRECFM,DCBRECCA       CC= ASA defined?
         BO    *+L'*+4                 yes
         OI    XREC24(R15),DCBRECCA    no, set it
         TM    DCBRECFM,DCBRECBR       Blocked records?
         BZ    ZDCB24M                 no
         LH    R0,DCBBLKSI             get BLKSIZE
         SH    R0,XLRL24(R15)          is it a multiple?
         BP    *-4                     continue check
         BZ    ZDCB24M                 yes
         MVI   XERR24(R15),4           no, set error 4
         BR    R14                     return
ZDCB24M  MVC   DCBLRECL,XLRL24(R15)    complete DCB
         OC    DCBRECFM,XREC24(R15)
         BR    R14                     return
         DC    Y(L'LINE+1)
         DC    AL1(0)
         DC    AL1(0)
         DROP  R1,R15
         DCB   DSORG=PS,MACRF=PL,DDNAME=XXXXXXXX                   EU13
         DC    6F'0'                   save registers
         WTO   '-- PUT error :                                         X
                                   ',ROUTCDE=11,DESC=7,MF=L
         DROP  R12                     END_PROC
         EJECT
*========================== general routines ==========================
$LTORG   LOCTR
*----------------------------------------------------------------------
*        GET A WORK AREA SPACE
*----------------------------------------------------------------------
WORKADDR L     R1,DYN_CHK              POINT TO END OF WORK AREA
         SLR   R1,R0                   POINT TO WORK AREA
         N     R1,=F'-32'              ALIGN ADDRESS
         CLR   R1,R10                  CHECK AGAINST CURRENT POINTER
         BNH   SPERR
         XC    0(256,R1),0(R1)         CLEAR BEGINNING TO X'00'
         BR    R14
*----------------------------------------------------------------------
*        PRINT A BLANK LINE
*----------------------------------------------------------------------
SPACE2   LA    R10,NEXTLINE
         CL    R10,DYN_CHK
         BNL   SPERR
SPACE1   MVC   LINE,BLANKS
SPACE0   LA    R10,NEXTLINE
         CL    R10,DYN_CHK
         BLR   R14
         B     SPERR
CHECK0   LA    R0,NEXTLINE
         CL    R0,DYN_CHK
         BLR   R14
SPERR    TM    FSWITCH,DYNSHORT        already occured?
         BO    SPEX                    yes
         OI    FSWITCH,DYNSHORT        no, remember it
         L     R1,=A(WTOSK5)           and signal shortage
         WTO   MF=(E,(1))
SPEX     L     R14,END_PROC            return address
         BR    R14                     abort remaining processing
*----------------------------------------------------------------------
*ITLE    DC    C'SHOWMVS',X'00',C'PROGRAM',X'00',C'&VERMOD '       ****
*ITLE    DC    C'TCN/AMS',X'00',C'UTILITIES',X'006000'
*        DC    C'SHOWMVS',X'00',C'&VERMOD '
TITLE    DC    C'TCN/AMS Utilities - SHOWMVS &VERMOD '             EU15
TITLE_L  EQU   *-TITLE
*----------------------------------------------------------------------
*        TRI DE LA TABLE APF-LIST
*----------------------------------------------------------------------
TRIZO    LA    R0,L'LINE               LONGUEUR D'UN POSTE
         LR    R1,R10                  A(NEXTLINE)
         SLR   R1,R0                   DERNIER POSTE DE LA TABLE
         STM   R0,R1,4(R2)             LONGUEUR, DERNIER POSTE
*-> LOOP (1)
TRIZO1   MVI   0(R13),0                ETAT INITIAL DE L'INDICATEUR
         L     R15,0(,R2)              DEBUT DE LA TABLE  N=1
         USING LINE,R15
         SLR   R1,R0                   LE DERNIER POSTE EST TRIE
*-> LOOP (2)
TRIZOC   CLC   DSNAME,DSNAME+L'LINE    (POSTE N) GT (POSTE N+1)?
         BNH   TRIZOH                  SI NON, BRANCH
         XC    LINE,NEXTLINE           SI (POSTE N+1) LT (POSTE N)
         XC    NEXTLINE,LINE            INVERSER LES
         XC    LINE,NEXTLINE             DEUX POSTES
         MVI   0(R13),8                NOTER LE DECLASSEMENT
TRIZOH   BXLE  R15,R0,TRIZOC           FAIRE N=N+1
*-> end LOOP (2)
TRIZON   CLI   0(R13),0                Y-A-T-IL EU UN DECLASSEMENT?
         BNE   TRIZO1                  SI OUI, REFAIRE UN PASSAGE
*-> end LOOP (1)
         BR    R14                     GOBACK
         DROP  R15
*----------------------------------------------------------------------
*        SCAN LPA QUEUES TO LOCATE EPNAME FOR ADDR IN (R1)
*----------------------------------------------------------------------
CSVQUERY MVC   EP10,BLANKS             CLEAR OUTPUT AREA
         CL    R1,IGCERROR             THIS SVC USED?
         BE    CSVQRY88                NO, GOBACK
         LA    R1,0(,R1)               CLEAN-UP AMODE BIT
         USING CVTMAP,R8
*
*        SCAN ACTIVE LPA QUEUE (MLPA/FLPA)
*
         L     R2,CVTQLPAQ             ACTIVE LPA QUEUE
         ICM   R2,B'1111',0(R2)        FIRST LPDE ON QUEUE
         BZ    CSVQRY20                EMPTY QUEUE, SKIP SEARCH
         USING LPDE,R2
         MVI   EP10,C'A'               ACTIVE LPA Q
*-> LOOP
CSVQRY11 L     R15,LPDENTP             ENTRY POINT
         LA    R15,0(,R15)             CLEAN UP AMODE BIT
         CR    R1,R15                  IS THIS MY ENTRY POINT?
         BE    CSVQRY82                MODULE FOUND, JUMP
         ICM   R2,B'1111',LPDECHN      NEXT LPDE ADDR
         BNZ   CSVQRY11                NO FINISHED YET, LOOP FURTHER
*-> end LOOP
*
*        SCAN PAGEABLE LPA QUEUE (PLPA)
*
CSVQRY20 L     R2,CVTLPDIA             FIRST LPDE
         USING LPDE,R2
         MVI   EP10,C'P'               PAGEABLE LPA Q
*-> LOOP
CSVQRY21 L     R15,LPDENTP             ENTRY POINT
         LA    R15,0(,R15)             CLEAN UP AMODE BIT
         CR    R1,R15                  IS THIS MY ENTRY POINT?
         BE    CSVQRY82                MODULE FOUND, JUMP
         TM    LPDEATTR,LPDEMIN        MINOR LPDE?
         BO    CSVQRY22                YES, IGNORE
         LM    R15,R0,LPDEXTLN         LENGTH/LOAD ADDR
         CR    R0,R1
         BH    CSVQRY22                OUTSIDE BOUNDARIES, JUMP
         AR    R0,R15
         CR    R0,R1
         BH    CSVQRY82                MODULE FOUND, JUMP
CSVQRY22 LA    R2,LPDEXTAD+4           BUMP LPDE ADDR
         CLI   LPDENAME,X'FF'          END OF LPA DIRECTORY?
         BNE   CSVQRY21                NO, LOOP FURTHER
*-> end LOOP
         MVI   EP10,C'N'               NUCLEUS
*        MVC   EP10+2(4),=C'*NUC'      EP FOUND IN THE NUCLEUS     @380
         STM   R14,R1,WKWORDS          save registers              @380
         LA    R0,0(,R1)               pass address                @380
         NUCLKUP BYADDR,ADDR=(0),NAME=EP10+2                       @380
         LTR   R15,R15                 FOUND?                      @380
         LM    R14,R1,WKWORDS          restore registers           @380
         BZR   R14                     yes, goback                 @380
         L     R2,CVTSMEXT             STORAGE MAP EXTENSION
         USING CVTVSTGX,R2
*        CL    R1,CVTRWNS              NUC?                        @380
*        BL    CSVQRY61                NO, JUMP                    @380
*        CL    R1,CVTERWNE             NUC?                        @380
*        BLR   R14                     YES, GOBACK                 @380
CSVQRY61 MVC   EP10+2(8),=C'*FLPA   '  EP FOUND IN FIXED LPA
         MVI   EP10,C'F'               FIXED LPAQ
         CL    R1,CVTFLPAS             FLPA (BELOW)
         BL    CSVQRY62                NO, JUMP
         CL    R1,CVTFLPAE             END OF FLPA (BELOW)
         BLR   R14                     YES, GOBACK
CSVQRY62 CL    R1,CVTEFLPS             FLPA (ABOVE)
         BL    CSVQRY63                NO, JUMP
         CL    R1,CVTEFLPE             END OF FLPA (ABOVE)
         BLR   R14                     YES, GOBACK
CSVQRY63 MVC   EP10+2(5),=C'*MLPA'     EP FOUND IN FIXED LPA
         MVI   EP10,C'F'               MODIFIED LPAQ
         CL    R1,CVTMLPAS             MLPA (BELOW)
         BL    CSVQRY64                NO, JUMP
         CL    R1,CVTMLPAE             END OF MLPA (BELOW)
         BLR   R14                     YES, GOBACK
CSVQRY64 CL    R1,CVTEMLPS             MLPA (ABOVE)
         BL    CSVQRY71                NO, JUMP
         CL    R1,CVTEMLPE             END OF MLPA (ABOVE)
         BLR   R14                     YES, GOBACK
CSVQRY71 L     R2,CVTGDA               POINT TO GDA
         USING GDA,R2
         MVC   EP10+2(5),=C'*CSA '     EP FOUND IN CSA
         MVI   EP10,C'C'               CSA
         L     R0,GDACSA               CSA (BELOW)
         CLR   R1,R0                   WITHIN CSA?
         BL    CSVQRY72                NO, JUMP
         AL    R0,GDACSASZ             END OF CSA (BELOW)
         CLR   R1,R0                   WITHIN CSA?
         BLR   R14                     YES, GOBACK
CSVQRY72 L     R0,GDAECSA              CSA (ABOVE)
         CLR   R1,R0                   WITHIN CSA?
         BL    CSVQRY73                NO, JUMP
         AL    R0,GDAECSAS             END OF CSA (ABOVE)
         CLR   R1,R0                   WITHIN CSA?
         BLR   R14                     YES, GOBACK
CSVQRY73 MVC   EP10,BLANKS             RETURN BLANK NAME
         BR    R14
         USING LPDE,R2
CSVQRY82 MVC   EP10+2(8),LPDENAME      PASS EP NAME
         BR    R14
         DROP  R2,R8
CSVQRY88 MVI   EP10,C'-'               SVC IS NOT IN SERVICE
         BR    R14
*----------------------------------------------------------------------
*        CONVERT UCBTYP TO UNITNAME
*----------------------------------------------------------------------
GETUNIT  CLC   UNITNAME+12(4),UNITNAME+8   SAME DEVICE TYPE?
         BER   R14                     YES, UNITNAME IS OK ALREADY
         MVC   UNITNAME+12(4),UNITNAME+8   SAVE DEVICE TYPE
GETUNIT2 ST    R14,DYNAM               SAVE R14
         XCALL IEFEB4UV,                                               X
               (UNITNAME,              work area                       X
               =X'0100'),              function flags  "7"             X
               ERRET=GETUNIT6
         XCALL IEFEB4UV,                                               X
               (UNITNAME,              work area                       X
               =X'2000')               function flags  "2"
         LTR   R15,R15
         BZ    GETUNIT9                GOOD RETURN CODE, GOBACK
GETUNIT6 MVC   UNITNAME,BLANKS         CONVERSION DID NOT WORK
GETUNIT9 L     R14,DYNAM               RESTORE R14
         BR    R14
*----------------------------------------------------------------------
*        TCB TREE SCAN SOUTINE
*----------------------------------------------------------------------
SCANTCB  LR    R1,R9                   SAVE TCB ADDRESS
         L     R9,TCBLTC-TCB(,R9)      DAUGHTER
         LA    R3,1(,R3)               INDENTATION INDEX
*-> LOOP
SCANTCB2 LTR   R9,R9                   CHECK FOR END OF CHAIN
         BNZR  R14                     PASS VALID TCB ADDRESS
         L     R9,TCBNTC-TCB(,R1)      SISTER
         L     R1,TCBOTC-TCB(,R1)      MOTHER
         BCT   R3,SCANTCB2             INDENTATION INDEX
*-> end LOOP
         SR    R9,R9                   SET CC=0
         BR    R14                     GOBACK
*----------------------------------------------------------------------
*        TABLE SCAN ROUTINES
*----------------------------------------------------------------------
*-> LOOP
SCAN_TM  IC    R15,0(,R1)              PICK UP MASK FOR "TM"
         EX    R15,2(,R14)             EXECUTE TM UNSTRUCTION
         BO    6(,R14)                 FOUND, GOBACK
         AH    R1,0(,R14)              BUMP TABLE PTR
         CLI   0(R1),0                 END OF TABLE?
         BNE   SCAN_TM                 NEXT TABLE ENTRY
*-> end LOOP
         LA    R1,BLANKS               ALL BLANKS
         LTR   R1,R1                   SET CC TO 'Z'
         B     6(,R14)                 NOT FOUND, GOBACK
*-> LOOP
SCAN_CLI IC    R15,0(,R1)              PICK UP MASK FOR "TM"
         EX    R15,2(,R14)             EXECUTE CLI UNSTRUCTION
         BE    6(,R14)                 FOUND, GOBACK
         AH    R1,0(,R14)              BUMP TABLE PTR
         CLI   0(R1),0                 END OF TABLE?
         BNE   SCAN_CLI                NEXT TABLE ENTRY
*-> end LOOP
         LA    R1,BLANKS               ALL BLANKS
         SR    R15,R15                 SET CC TO 'NE'
         B     6(,R14)                 NOT FOUND, GOBACK
*
*        Define the DFA map (Data Facilities area).
*
*        The DFP V3 version of this macro defines the
*        DFASMS symbol.  The existence of DFASMS is
*        tested in the DEVSTAT routine to generate
*        SMS-dependent code and macros.
*
         PUSH  PRINT
         PRINT NOGEN
         IHADFA  DSECT=YES             DFP AREA
         POP   PRINT
$FARRTNE LOCTR
*----------------------------------------------------------------------
*        SNAP ROUTINE    R0=LENGTH,R1=ADDRESS
*----------------------------------------------------------------------
         USING SNAP00,R3
SNAP00   SLR   R15,R15                 offset=0000
         STM   R14,R15,SNAPREGS+24     return address/offset
         STRING '0-3 ',(SNAPREGS+08,4,X),1X,(SNAPREGS+12,4,X),         X
               1X,(SNAPREGS+16,4,X),1X,(SNAPREGS+20,4,X),              X
               '  4-7 ',((R4),,X),1X,((R5),,X),                        X
               1X,((R6),,X),1X,((R7),,X),INTO=LINE
         BAL   R14,SPACE0          <== next line
         STRING '8-B ',((R8),,X),1X,((R9),,X),1X,((R10),,X),           X
               1X,((R11),,X),'  C-F ',((R12),,X),1X,((R13),,X),        X
               1X,(SNAPREGS+00,4,X),1X,(SNAPREGS+04,4,X),INTO=LINE
         BAL   R14,SPACE0          <== next line
*-> LOOP
SNAP10   L     R14,SNAPREGS+24         return address
         LTR   R0,R0                   # of remaining bytes
         BNPR  R14                     finished, QUIT
         STRING '  +',(SNAPREGS+30,2,X),1X,((R1),,X),3X,36X,'   *',    X
               16X,'*',INTO=LINE
         LA    R14,16                  # of bytes on a line
         AL    R14,SNAPREGS+28         increment offset
         ST    R14,SNAPREGS+28         increment offset
         LA    R14,16                  # of chars on a line
         CLR   R14,R0                  is this a short line?
         BL    *+L'*+2                 no, jump
         LR    R14,R0                  truncate last line
         BCT   R14,*+L'*+6             FOR EX
         MVC   LINE+59(*-*),0(R1)      <<EXECUTED>>
         EX    R14,*-6                 MOVE FIELD
         TR    LINE+59(16),TRTPRINT
         LA    R2,LINE+19              start of hex data
         BAL   R14,SNAP90              edit 4 bytes
         BAL   R14,SNAP90              edit 4 bytes
         LA    R2,1(,R2)               2-byte margin
         BAL   R14,SNAP90              edit 4 bytes
         BAL   R14,SNAP90              edit 4 bytes
         BAL   R14,SPACE0          <== next line
         B     SNAP10                  snap next line
*-> end LOOP
SNAP90   LTR   R0,R0                   # OF REMAINING BYTES
         BNPR  R14                     FINISHED, QUIT
         UNPK  0(9,R2),0(5,R1)         translate to hex
         L     R15,=A(@STRHEXT-240)    (from STRING macro)
         TR    0(8,R2),0(R15)          translate to hex
         SH    R0,=H'4'                decrement length
         LA    R1,4(,R1)               ptr in input area
         MVI   8(R2),C' '
         LA    R2,9(,R2)               ptr in output line
         BR    R14
         DROP  R3
         DROP  ,  -------------------- all registers killed -----------
         EJECT
*======================================================================
*        BRIF READ ROUTINE
*----------------------------------------------------------------------
*
*        This routine is invoked by BRIF and returns the address
*        of a record.  BRIF invokes it each time the line is
*        displayed on the screen, i.e when you scroll UP, DOWN,
*        LEFT, RIGHT, or simply hit ENTER.
*
*        It also provides real-time status for on-line devices.
*
RDRTNE   SAVE  (14,12),,*
         BALR  R12,0
         USING *,R12
         L     R6,00(,R1)              RECORD DATA READ
*        L     R3,04(,R1)              LENGTH                      ****
         L     R4,08(,R1)              RELATIVE RECORD NUMBER
         L     R5,12(,R1)              PTR TO DIALOG DATA AREA
         L     R5,0(,R5)               POINT TO DYNAMIC STORAGE AREA
         USING DYNAM,R5
         L     R11,BASEREG             BASE REGISTER
         USING BASEADDR,R11
         L     R1,0(,R4)               LINE NUMBER REQUESTED BY BRIF
         C     R1,NUMLINES             CHECK FOR END OF DATA
         BH    RDRTN80                 JUMP IF TOO BIG
         BCTR  R1,0                    DOWN BY 1
         M     R0,=A(L'LINE)           OFFSET TO LAST LINE
         LA    R10,LINES(R1)           CHANGE OFFSET TO ADDRESS
         USING LINE,R10
         CLI   LINE_UCB,00             refresh device status?
         BNE   RDRTN70                 no, jump
*
*        refresh status for on-line devices
*
         ICM   R7,B'1111',LINE_UCB     A(UCBOB)
         BZ    RDRTN70                 no, jump
         USING UCBOB,R7
         ST    R13,RCVYSAVE+4
         LA    R13,RCVYSAVE            save area for IEFSSREQ & STRING
         MVC   WK256(LINE_UCB-LINE),LINE  move first half of line
         LA    R10,WK256               now point to modified line
         LA    R4,LINE_UCB             message area
         MVC   0(LINE+L'LINE-LINE_UCB,R4),BLANKS
*
*        show VOLSER (DASD/TAPE)
*
         CLI   UCBTBYT3,UCB3TAPE       TAPE?
         BE    RDRTN20                 YES, JUMP
         CLI   UCBTBYT3,UCB3DACC       DASD?
         BE    RDRTN20                 YES, JUMP
*
*        Check for a system console
*
         LA    R4,8(,R4)               skip volser slot
         CLI   UCBTBYT3,UCB3DISP       Display device (eg 3270)?
         BNE   RDRTN50                 no, JUMP
         STATUS UCBSTAT,UCBSYSR,'Console'
         B     RDRTN50                 skip TAPE/DASD section
*
*        TAPE/DASD section
*
RDRTN20  CLI   UCBVOLI,C' '            VALID VOLSER?
         BNH   RDRTN49                 NO, JUMP
         MVC   0(6,R4),UCBVOLI         VOLSER FOR TAPE/DASD
         LA    R4,8(,R4)               skip volser
         CLI   UCBTBYT3,UCB3DACC       DASD?
         BNE   RDRTN40                 NO, JUMP
         STATUS UCBSTAT,UCBSYSR,'System'
         AIF   (NOT D'DFASMS).SMS2     NOT DFP V3, JUMP
*
*        GET SMS status and storage group
*        This section is only generated if you assemble the
*        program with a DFP V3 MACLIB.
*
         TM    UCBFL5,UCBSMS           SMS-managed volume?
         BZ    RDRTN40                 no, jump
         L     R1,WORK4K               point to the 4K work area
         USING SSOB,R1
         XC    SSOB(256),SSOB          clear working storage
         LA    R0,SSOBHSIZ             BUILD SSOB
         STH   R0,SSOBLEN              BUILD SSOB
         MVC   SSOBID,=C'SSOB'         BUILD SSOB
         LA    R0,SSOBSSMS             FUNCTION CODE 55
         STH   R0,SSOBFUNC             BUILD SSOB
         LA    R14,SSOB+SSOBHSIZ       POINT PAST SSOB HEADER
         ST    R14,SSOBSSIB            STORE INTO SSOB
         USING SSIB,R14
         LA    R0,SSIBSIZE             BUILD SSIB
         STH   R0,SSIBLEN              BUILD SSIB
         MVC   SSIBID,=C'SSIB'         BUILD SSIB
         MVC   SSIBSSNM,=C'SMS '       SUB-SYSTEM NAME
         LA    R14,SSIB+SSIBSIZE       POINT PAST SSIB
         ST    R14,SSOBINDV            FUNCTION-DEPENDENT AREA
         USING IEFSSSA,R14
         LA    R0,SSSALN+SSSA1LN       LENGTH
         STH   R0,SSSALEN
         MVC   SSSAID,=C'SSSA'         SSSA
         MVI   SSSAVER+1,SSOBSSVR      VERSION
         MVI   SSSASFN+1,SSSAACTV      SUB-FUNCTION CODE
         MVI   SSSAIFLG,SSSANAUT       NON-AUTHORIZED CALLER
         MVI   SSSA1TYP,SSSA1VOL       GIVE VOLSER, GET VLD BACK
         MVI   SSSA1CNT+3,1            ONE CONSTRUCT PASSED
         MVI   SSSA1NML+1,L'UCBVOLI    LENGTH OF NAME
         MVC   SSSA1NAM(L'UCBVOLI),UCBVOLI  MOVE VOLUME SERIAL
         LA    R2,SSSA+SSSALN+SSSA1LN  WORK AREA
         ST    R2,SSSA1PTR             ADDR OF WORK AREA
         LA    R0,1024                 LENGTH OF WORK AREA
         ST    R0,SSSA1LEN             LENGTH OF WORK AREA
         DROP  R1,R14
*
*        call SMS to get a VLD (Volume Record Descriptor)
*
         ST    R1,WKWORDS              BUILD SSOB_PTR
         OI    WKWORDS,X'80'           BUILD SSOB_PTR
         LA    R1,WKWORDS              SSOB_PTR
         IEFSSREQ                  <== CALL SMS
         LTR   R15,R15                 SSI OK?
         BNZ   RDRTN40                 NO, jump
         MVC   0(7,R4),=C'SMS SG='     SMS storage group
         USING VLD,R2
         OC    7(8,R4),VLDSTGRP        storage group (X'00'->X'40')
         LA    R4,16(,R4)              point past string
*
*        SMS volume status
*
         BAS   R1,RDRTN30              BRANCH AROUND TABLE
         DC    AL1(VLDENBL),C'Enabled     '
         DC     AL1(VLDQUI),C'Quiesced/all'
         DC    AL1(VLDQUIN),C'Quiesced/new'
         DC     AL1(VLDDIS),C'Disabled/all'
         DC    AL1(VLDDISN),C'Disabled/new'
         DC    AL1(00),0H'0'
RDRTN30  BAL   R14,SCAN_CLI            SCAN TABLE
         DC    Y(1+12)                 SIZE OF A TABLE ENTRY
         CLI   VLDSMSS,*-*             Test SMS status
*        CLI   VLDSGST,*-*        Test SMS status on this system   ****
         BNE   *+L'*+6                 NOT FOUND, JUMP AROUND "MVC"
         MVC   0(12,R4),1(R1)          move SMS status
         LA    R4,13(,R4)              point past status
*
*        display free space
*
         ICM   R1,B'1111',VLDNTCPY     total capacity
         BZ    RDRTN60                 prevent S0C9
         S     R1,VLDNFREE             free space
         M     R0,=F'100'              space used * 100
         D     R0,VLDNTCPY             R1 = %USED
         STRING ((R1),,R5B),'% used',INTO=((R4),11)
         LA    R4,1(R15,R4)            point past string
         B     RDRTN60
         DROP  R2
.SMS2    ANOP
*
*        VOLSER/MOUNT FOR TAPE, DASD
*
RDRTN40  STATUS UCBSTAT,UCBPRES,'Resident'
         STATUS UCBSTAT,UCBRESV,'Reserved'
         STATUS UCBSTAB,UCBBPRV,'Private'
         STATUS UCBSTAB,UCBBPUB,'Public'
         STATUS UCBSTAB,UCBBSTR,'Storage'
*
*        Label type and position: LABEL=(nnn,SL)
*
         CLI   UCBTBYT3,UCB3TAPE       TAPE?
         BNE   RDRTN50                 no, JUMP
         ICM   R0,B'1100',UCBFSEQ      fileseq=0?
         BZ    RDRTN50                 yes, JUMP
         STRING (UCBFSEQ,H,L),',',INTO=((R4),12)
         ALR   R4,R15                  point R4 past string
         STATUS UCBSTAT,UCBDADI,'SL'
         STATUS UCBTFL1,UCBNLTP,'NL'
         STATUS UCBTFL1,UCBNSLTP,'NSL'
         STATUS UCBTFL1,UCBBLP,'BLP'
         B     RDRTN50
RDRTN49  LA    R4,8(,R4)               skip volser slot
*
*        SHOW OWNER OF NON-DASD DEVICE
*
RDRTN50  STATUS UCBSTAT,UCBALOC,'Allocated',OFF=RDRTN60
         SLR   R1,R1
         ICM   R1,B'0111',UCBEXTP      UCB COMMON EXTENSION
         LH    R1,UCBASID-UCBCMEXT(,R1)  GET OWNER'S ASID
         LOCASCB ASID=(1)              GET ADDR OF OWNER'S ASCB
         LTR   R15,R1                  VALID ASID?
         BNP   RDRTN60                 NO, JUMP                    EU02
         ICM   R1,B'1111',ASCBJBNI-ASCB(R15) JOB name present?
         BNZ   RDRTN59                 yes, use it
         ICM   R1,B'1111',ASCBJBNS-ASCB(R15) STC/MOUNT/LOGON
         BZ    RDRTN60                 NO, JUMP                    EU02
RDRTN59  MVC   0(2,R4),=C'J='          SMS storage group
         MVC   2(8,R4),0(R1)           JOB/STC/TSU name
         LA    R4,11(,R4)              point past string
RDRTN60  STATUS UCBFLA,UCBNRY,'Not-ready'
         STATUS UCBFLA,UCBBOX,'Boxed'
*        DEV_STAT FLA,NOPTH,'No Path Available'                    ****
         STATUS UCBFLC,UCBIVRS,'Intervention Required'
         L     R13,4(,R13)             BRIF save area
RDRTN70  ST    R10,0(,R6)              PASS DATA ADDRESS
         TR    0(L'LINE,R10),TRTPRINT  Get rid of garbage          EU14
         RETURN (14,12),RC=0           GOBACK TO EDIT
*
*        END OF DATA - RETURN MAX LINE#
*
RDRTN80  L     R1,NUMLINES             NUMBER OF LINES
         ST    R1,0(,R4)               PASS IT TO BRIF
         BCTR  R1,0                    DOWN BY 1
         M     R0,=A(L'LINE)           OFFSET TO LAST LINE
         LA    R10,LINES(R1)           CHANGE OFFSET TO ADDRESS
         ST    R10,0(,R6)              PASS DATA ADDRESS
         RETURN (14,12),RC=8
         DROP  ,  -------------------- all registers killed -----------
         EJECT
*======================================================================
*        RECOVERY ROUTINE
*----------------------------------------------------------------------
RECOVERY LA    R15,0012                R15=12
         CR    R0,R15                  SDWA ALLOCATED?
         BALR  R15,0                   LOCAL BASE
         BNE   RCVY$200-*(,R15)        YES, JUMP
         SR    R15,R15                 SET RC=0 (IF R0=12)
         BR    R14                     RETURN TO EXIT PROLOG
RCVY$200 BALR  R15,0
         SAVE  (14,12),,'RECOVERY ROUTINE'
         BALR  R12,0
         USING *,R12
         LR    R8,R1
         USING SDWA,R8
         LR    R14,R13
         L     R13,SDWAPARM            =A(DYNAM)
         LA    R13,RCVYSAVE-DYNAM(,R13) =A(RCVYSAVE)
         ST    R14,4(,R13)
         ST    R13,8(,R14)
         USING RCVYSAVE,R13
         ICM   R4,B'1111',RETRY        LOAD/TEST RETRY ADDRESS
         BNP   RCVY$999                NO RETRY, CONTINUE WITH ABEND
         MVI   RETRY,X'FF'             INVALIDATE RETRY ADDRESS
         MVC   SDWASRSV,SDWAGRSV       MOVE REGISTERS
         MVC   SDWASR08,CVTPTR         R8=CVTADDR
         MVC   SDWASR09,PSATOLD-PSA    R9=TCBADDR
         MVC   SDWASR11,BASEREG        R11 (BASE REG)
         MVC   SDWASR13,SDWAPARM       R13
         L     R13,4(,R13)             R/TM SAVE AREA
         SETRP WKAREA=(R8),RETADDR=(R4),RC=4,  <== RETRY               X
               FRESDWA=YES,RETREGS=YES,REGS=(14,12)
RCVY$999 L     R13,4(,R13)
         SETRP WKAREA=(R8),REGS=(14,12),RC=00
         DROP  ,  -------------------- all registers killed -----------
         EJECT
*======================================================================
*        SUB-TASK USED FOR ASYNCHRONOUS ACCESS TO CATALOGS AND VTOCS
*
*        THIS SUB-TASK ALLOWS IMPATIENT USERS (LIKE ME) TO SEE THE
*        FIRST SCREEN WITHOUT ANY DELAY WHILE VOLUME AND VTOC
*        INFORMATION (WHICH REQUIRE I/O) IS OBTAINED FOR THE LINK-LIST,
*        LPA-LIST AND APF-LIST DISPLAYS.
*
*        WHEN THE USER SCROLLS TO THESE SCREENS (WHICH TAKES A FEW
*        SECONDS), VOLUME AND VTOC INFORMATION WILL HAVE BEEN OBTAINED
*        IN THE BACKGROUND, WITHOUT THE USER HAVING TO WAIT.
*
*        IF A SPEEDY USER CHAINS A "FIND" COMMAND TO THE INVOCATION
*        OF THIS PROGRAM (BY ISSUING "TSO SHOWMVS;F LPA-LIST" FOR
*        EXAMPLE), THEN HE/SHE WILL PROBABLY GET A BUNCH OF QUESTION
*        MARKS INSTEAD OF VOLUME AND VTOC INFORMATION (SURPRISE,
*        SURPRISE !!).  AFTER A FEW SECONDS OF HESITATION, THIS USER
*        IS LIKELY TO HIT "ENTER" OR SCROLL (UP OR DOWN) TO SEE IF
*        OTHER BIZARRE THINGS HAPPEN, WHICH LEAVES ENOUGH TIME TO THE
*        SUBTASK TO FINISH OBTAINING THE INFORMATION AND REPLACE THE
*        QUESTION MARKS WITH PERTINENT DATA.
*----------------------------------------------------------------------
SUB_RTNE SAVE  (14,12),,'SUB-TASK'                                 EU13
         CNOP  0,8
SUB_TASK LPR   R9,R1                   SUB_WORK                    @380
         USING SUB_WORK,R9
         ST    R13,4(,R9)                                          EU13
         ST    R9,8(,R13)                                          EU13
         LR    R13,R9                                              EU13
         USING LINE,R10
         L     R11,BASEREG             BASE REGISTER
         USING BASEADDR,R11
         BALR  R12,0
         USING *,R12
*
*        Estimate processor speed
*
         XR    R4,R4                   LOOP COUNT
         LA    R6,SUB_DWD              WORK AREA
         MVC   0(LOOPL,R6),SUB_LOOP    MOVE LOOP INSTRUCTION
         BAL   R15,SUBT050
         MVI   LOOPL-1(R6),R4*16+R5    CHANGE R4,R6 TO R4,R5
         BR    R14
SUB_LOOP BCTR  R4,R6                   LOOP COUNT - RR INSTRUCTION
LOOPL    EQU   *-SUB_LOOP              INSTRUCTION LOOP LENGTH
ONETENTH DC    FL4'10'                 TEN 1/100TH OF A SECOND
SUBT050  STIMER TASK,(R15),BINTVL=ONETENTH
         BALR  R5,R6                   EXECUTE LOOP
         LPR   R15,R4                  NUMBER OF ITERATIONS
         LR    R5,R15                  ADJUST # OF INSTRUCTIONS    EU09
         SRL   R5,2                                                EU09
         AR    R15,R5                                              EU09
         M     R14,CPUONLNE+4          MULT BY # OF CPU'S ON-LINE
         D     R14,=F'1000000'         .1 MIPS
         CVD   R15,SUB_DWD             .1 MIPS
         MVC   SUB_W265(7),=X'40202021204B20' 0999.9
         ED    SUB_W265(7),SUB_DWD+5   0999.9
         L     R10,CPUONLNE            processor speed line
         STRING '       Processor Speed: ',(SUB_W265+2,5),' MIPS',     X
               INTO=LINE
         LA    R10,LINES               First line
*
*        Pass 1: Complete VOLSER
*-> LOOP
SUBT100  CLI   VOLSER,C'?'             VOLSER FOUND ALREADY?
         BNE   SUBT110                 YES, JUMP
         MVC   VOLSER,=C'??????'       NOT FOUND
         L     R14,=X'44,00,00,00'     CAMLST FLAGS
         LA    R15,DSNAME              ENTRY NAME
         SLR   R0,R0                   NO CVOL PTR
         LA    R1,SUB_W265             WORK AREA
         STM   R14,R1,CAMLST1          BUILD CAMLIST               @364
         LOCATE CAMLST1                GET VOLSER FROM CATALOG     @364
         LTR   R15,R15
         BNZ   SUBT110                 NOT FOUND (OR OTHER ERROR)
         MVC   VOLSER,SUB_W265+6       MOVE VOLSER
*        => UNCAT=UNCAT
SUBT110  CLI   CATUNCAT,C'?'           CATALOG STATUS KNOWN ALREADY?
         BNE   SUBT120                 YES, JUMP
         MVC   CATUNCAT,=C'UNCAT'      MOVE STATUS
         L     R14,=X'44,00,00,00'     CAMLST FLAGS
         LA    R15,DSNAME              ENTRY NAME
         SLR   R0,R0                   NO CVOL PTR
         LA    R1,SUB_W265             WORK AREA
         STM   R14,R1,CAMLST1          BUILD CAMLIST               @364
         LOCATE CAMLST1                GET VOLSER FROM CATALOG     @364
         LTR   R15,R15
         BNZ   SUBT120                 NOT FOUND (OR OTHER ERROR)
         CLC   VOLSER,SUB_W265+6       SAME VOLSER?
         BNE   *+L'*+10                NO                          EU06
         MVC   CATUNCAT,BLANKS         CATALOGED=YES
         B     SUBT120                                             EU06
         MVC   CATUNCAT(2),=CL2'C='                                EU06
         MVC   CATUNCAT+2(6),SUB_W265+6 MOVE OTHER VOLSER          EU06
         MVI   LINE_APF,C' '           DON'T PROCESS               EU06
SUBT120  LA    R10,NEXTLINE            NEXT LINE
         CLI   LINE,0                  LAST LINE?
         BNE   SUBT100                 NO, LOOP MORE
*-> end LOOP
*
*        Pass 2: Cross-reference LNK/LPA/APF table entries
*
         LA    R10,LINES               First line
*        => UNCAT=APF
*-> LOOP
SUBT200  CLI   LINE_APF,C'?'           LNK/LPA/APF STATUS REQUIRED?
         BNE   SUBT210                 NO, JUMP
         LM    R15,R1,APFTABLE         APF TABLE
         BAL   R14,SUBT800             SCAN APFTABLE
         DC    C'APF '                 LITERAL
         MVC   LINE_APF,0(R1)          LITERAL OR SPACES
         CLI   LINE_LNK,C' '           UNCAT APFLST ENTRY?
         BNE   SUBT210                 YES, JUMP
*        => LNK=LPA
         LM    R15,R1,LPATABLE         LPA TABLE
         BAL   R14,SUBT800             SCAN LPATABLE
         DC    C'LPA '                 LITERAL
         MVC   LINE_LPA,0(R1)          LITERAL OR SPACES
*        => LPA=LNK
         LM    R15,R1,LNKTABLE         LNK TABLE
         BAL   R14,SUBT800             SCAN LNKTABLE
         DC    C'LNK '                 LITERAL
         MVC   LINE_LNK,0(R1)          LITERAL OR SPACES
SUBT210  LA    R10,NEXTLINE            NEXT LINE
         CLI   LINE,0                  LAST LINE?
         BNE   SUBT200                 NO, LOOP MORE
*-> end LOOP
*
*        Pass 3: Detect TIOT data-sets in LPALST or LNKLST
*
         LA    R10,LINES               First line
*        => TIOT=LPA
*-> LOOP
SUBT300  CLI   LINE_LPA2,C'?'          LPA-LIST STATUS KNOWN ALREADY?
         BNE   SUBT310                 YES, JUMP
         LM    R15,R1,LPATABLE         LPA TABLE
         BAL   R14,SUBT810             SCAN APFTABLE
         DC    C'LPA '                 LITERAL
         MVC   LINE_LPA2,0(R1)         LITERAL OR SPACES
*        => TIOT=LNK
SUBT310  CLI   LINE_LNK2,C'?'          LNK-LIST STATUS KNOWN ALREADY?
         BNE   SUBT320                 YES, JUMP
         LM    R15,R1,LNKTABLE         LNK TABLE
         BAL   R14,SUBT810             SCAN APFTABLE
         DC    C'LNK '                 LITERAL
         MVC   LINE_LNK2,0(R1)         LITERAL OR SPACES
SUBT320  LA    R10,NEXTLINE            NEXT LINE
         CLI   LINE,0                  LAST LINE?
         BNE   SUBT300                 NO, LOOP MORE
*-> end LOOP
*
*        Pass 4: Read F1-DSCB to diaplay creation date
*                (this pass takes the longest and is done last)
*
         LA    R10,LINES               First line
         SLR   R6,R6                   extent counter
*-> LOOP
SUBT400  CLI   YYMMDD,C'?'             DATE FOUND ALREADY?
         BNE   SUBT420                 YES, JUMP
         MVC   YYMMDD,BLANKS           BLANK OUT DATE FIELD
         L     R14,=X'C1,00,00,00'     CAMLST FLAGS
         LA    R15,DSNAME              DATA SET NAME OF CCHHR
         LA    R0,VOLSER               VOLUME SERIAL
         LA    R1,DS1FMTID             WORK AREA
         STM   R14,R1,CAMLST1          BUILD CAMLIST               @364
         OBTAIN CAMLST1                READ F1-DSCB                @364
         LTR   R15,R15
         BNZ   SUBT420                 NOT FOUND (OR OTHER ERROR)
         SLR   R0,R0
         ICM   R0,B'0001',DS1CREDT+0   CREATION YEAR
         CVD   R0,SUB_DWD
         ICM   R1,B'0011',SUB_DWD+6    ....0YYC
         SLL   R1,012                  .0YYC000
         ICM   R0,B'0011',DS1CREDT+1   CREATION DAY
         CVD   R0,SUB_DWD
         STCM  R1,B'0100',SUB_DWD+5    00YYDDDC
         STRING (SUB_DWD+4,P,YYMMDD),INTO=YYMMDD CONVERT DATE
         CL    R10,LNKTABLE+0          LINK-LIST?
         BL    SUBT410                 NO, JUMP
         CL    R10,LNKTABLE+8          LINK-LIST?
         BH    SUBT410                 NO, JUMP
         STRING (DS1NOEPV,FL1,R3B),' Extents',INTO=(LINE+85,11)
         SLR   R0,R0
         IC    R0,DS1NOEPV
         ALR   R6,R0                   Tally allocated extents
SUBT410  CLI   CATUNCAT,C'%'           PCT USED?
         BNE   SUBT420                 NO, JUMP
         MVC   CATUNCAT(4),=C'FULL'    PRIME STATUS
         ICM   R0,B'0011',DS1LSTAR     DATA SET FULL?
         BNZ   SUBT420                 YES, JUMP
         MVC   CATUNCAT(5),=C'EMPTY'   NO, TELL IT
SUBT420  LA    R10,NEXTLINE            NEXT LINE
         CLI   LINE,0                  LAST LINE?
         BNE   SUBT400                 NO, LOOP MORE
*-> end LOOP
         L     R10,LINKXTNT            link-list hdr line
         STRING (LINE,,T),' (',((R6),,L),' allocated)',INTO=LINE
         CLI   ECB1+7,X'FF'            called as rouitne?          EU13
         BE    SUBT999                 yes                         EU13
*
*        Serialize sub_task termination with DETACH to prevent Sx3E
*
         L     R13,4(,R13)             original "DYNAM"
         ENQ   (EDQNAME,EDRNAME,E,L'EDRNAME),                      @364X
               MF=(E,ENQL1)            SERIALIZE DETACH            EU15
         POST  ECB1,0                  POST COMPLETION ECB
         SVC   3                       GOOD BYE
SUBT999  L     R13,4(,R13)                                         EU13
         RETURN (14,12),RC=0           That's all folks            EU13
*
*        Scan Tables routines
*
SUBT800  LTR   R15,R15                 TEST
         BZ    SUBT820
*-> LOOP
SUBT801  CLC   DSNAME,DSNAME-LINE(R15) MY DSNAME?
         BNE   SUBT802                 NO, JUMP
         CLC   VOLSER,VOLSER-LINE(R15) MY VOLSER?
         BE    SUBT820                 YES, QUIT
SUBT802  BXLE  R15,R0,SUBT801          NEXT ENTRY
*-> end LOOP
         B     SUBT821
SUBT810  LTR   R15,R15                 TEST
         BZ    SUBT820
*-> LOOP
SUBT811  CLC   LINE_DSN2,DSNAME-LINE(R15) MY DSNAME?
         BNE   SUBT812                 NO, JUMP
         CLC   LINE_VOL2,VOLSER-LINE(R15) MY VOLSER?
         BE    SUBT820                 YES, QUIT
SUBT812  BXLE  R15,R0,SUBT811          NEXT ENTRY
         B     SUBT821
*-> end LOOP
SUBT820  LR    R1,R14                  APF=YES
         B     4(,R14)
SUBT821  LA    R1,BLANKS               APF=NO
         B     4(,R14)
         EJECT
*------------------------------------------------------------------EU14
*        24-bit dynamic storage area for EXTRACT                   EU14
*------------------------------------------------------------------EU14
EXTR24   DSECT                         Dynamic storage area        EU14
EXTR    EXTRACT *-*,'S',MF=L                                       EU14
LEXTR    EQU   *-EXTR                                              EU14
ATSO     DS    A                                                   EU14
APSCB    DS    A                                                   EU14
EXTR24L  EQU   (((*-EXTR24)+7)/8)*8    Length of storage area      EU14
*----------------------------------------------------------------------
*        24-BIT DYNAMIC STORAGE AREA
*----------------------------------------------------------------------
DYNAM24  DSECT                         DYNAMIC STORAGE AREA
         DS    18F                     24-BIT SAVE AREA FOR PUT
CODE24   ST    R13,4(,R7)              24-BIT SAVE AREA
         LR    R13,R7                  24-BIT SAVE AREA
         LA    R1,DCB24-CODE24(,R15)   GET DCB ADDRESS
         PUT   (1)                     PUT LOCATE
         L     R13,4(,R13)             31-BIT SAVE AREA
         BSM   0,R2                    GOBACK WITH AMODE31
         CNOP  0,4
SYNAD24  STM   R13,R2,SAVE24(R15)      save registers
         LR    R2,R15                  use as base
         USING SYNAD24,R2
         SYNADAF ACSMETH=QSAM          get error message from OS
         MVC   WTO24M(L'WTO24M,R2),68(R1) get usefull text
         SYNADRLS                      free OS message
         DROP  R2
         LM    R13,R2,SAVE24(R2)       restore registers
         BR    R14                     return
         PRINT NOGEN                                               EU00
OPEN24   OPEN  (DCB24,OUTPUT),MF=L
XLST24   DC    0F'0',XL1'85',AL3(*-*)  DCB exit list
         USING IHADCB,R1
         USING XDCB24,R15
XDCB24   XR    R0,R0
         ICM   R0,B'0011',DCBLRECL     get LRECL
         BZ    *+L'*+14                branch if undefined
         CLM   R0,B'0011',XLRL24(R15)  LRECL= as needed?
         BE    *+L'*+6                 yes
         MVI   XERR24(R15),1           no, set error 1
         BR    R14                     return
         TM    DCBRECFM,DCBRECV        RECFM= V or U?
         BZ    *+L'*+6                 no
         MVI   XERR24(R15),2           yes, set error 2
         BR    R14                     return
         TM    DCBRECFM,DCBRECF        RECFM= F?
         BO    *+L'*+4                 yes
         OI    XREC24(R15),DCBRECF     no, set it
         TM    DCBRECFM,DCBRECCM       CC= Machine defined?
         BZ    *+L'*+6                 no
         MVI   XERR24(R15),3           yes, set error 3
         BR    R14                     return
         TM    DCBRECFM,DCBRECCA       CC= ASA defined?
         BO    *+L'*+4                 yes
         OI    XREC24(R15),DCBRECCA    no, set it
         TM    DCBRECFM,DCBRECBR       Blocked records?
         BZ    ZDCB24                  no
         LH    R0,DCBBLKSI             get BLKSIZE
         SH    R0,XLRL24(R15)          is it a multiple?
         BP    *-4                     continue check
         BZ    ZDCB24                  yes
         MVI   XERR24(R15),4           no, set error 4
         BR    R14                     return
ZDCB24   MVC   DCBLRECL,XLRL24(R15)    complete DCB
         OC    DCBRECFM,XREC24(R15)
         BR    R14                     return
XLRL24   EQU   *-XDCB24
         DC    Y(L'LINE+1)
XREC24   EQU   *-XDCB24
         DC    AL1(0)
XERR24   EQU   *-XDCB24
XERR24C  DC    AL1(0)
         DROP  R1,R15
DCB24    DCB   DSORG=PS,MACRF=PL,DDNAME=XXXXXXXX                   EU13
         DS    0F
SAVE24   EQU   *-SYNAD24
         DC    6F'0'                   save registers
WTO24    WTO   '-- PUT error :                                         X
                                   ',ROUTCDE=11,DESC=7,MF=L
WTO24T   EQU   WTO24+22,1
WTO24M   EQU   (WTO24+19)-SYNAD24,60
CODE24L  EQU   *-CODE24                PUT LOCATE
DYNAM24L EQU   (((*-DYNAM24)+7)/8)*8   LENGTH OF DYNAMIC STORAGE AREA
*----------------------------------------------------------------------
*        DESCRIPTION OF A PRINT LINE
*----------------------------------------------------------------------
         DSECT
LINE     DS    CL100                   CURRENT LINE
DSNAME   EQU   LINE+2,44
VOLSER   EQU   LINE+2+44+3,6
DEVTYPE  EQU   LINE+2+44+3+6+3,8
YYMMDD   EQU   LINE+2+44+3+6+3,6
CATUNCAT EQU   LINE+2+44+3+6+3+6+3,5   UNCAT
LINE_LNK EQU   CATUNCAT+0,3            LNK
LINE_LPA EQU   LINE_LNK+5,3            LPA
LINE_APF EQU   LINE_LPA+5,3            APF
LINE_VOL2 EQU  LINE+15,6               VOLSER   (TIOT DISPLAY)
LINE_DSN2 EQU  LINE+28,44              DSNAME   (TIOT DISPLAY)
LINE_LNK2 EQU  LINE+90,3               LNKLST   (TIOT DISPLAY)
LINE_LPA2 EQU  LINE+90+4,3             LPALST   (TIOT DISPLAY)
NEXTLINE DS    CL(L'LINE)              NEXT LINE
*----------------------------------------------------------------------
*        Catalog Parameter List (SVC 26)
*----------------------------------------------------------------------
CTGPL    DSECT
CTGOPTN1 DS    B              FIRST OPTION BYTE:
CTGNAME  EQU   X'04' .... .1..  CTGENT CONTAINS DSNAME OR SERIAL ADDR
CTGGENLD EQU   X'01' .... ...1  GENERIC LOCATE REQUEST
CTGOPTN2 DS    B              SECOND OPTION BYTE:
CTGFUNC  DS    B     XXX. ....  SPECIFIES THE CALLER-REQUESTED FUNCTION
CTGSUPLT EQU   X'10' ...1 ....  SUPERLOCATE FUNCTION
CTGAM0   EQU   X'01' .... ...1  OS/VS2 CATALOG MANAGMENT REQUEST
CTGOPTN4 DS    B              FOURTH OPTION BYTE
CTGENT   DS    A(DUMPNODE)    ADDRESS OF CATALOG RECORD IDENTIFIER
CTGCAT   DS    A(0)           ADDRESS OF CATALOG DSNAME OR ACB
CTGWKA   DS    A(*-*)         ADDRESS OF CALLER'S WORK AREA
CTGOPTNS DS    B              CATALOG MANAGMENT SERVICES REQUEST OPTION
         DS    B              CMS REQUEST OPTIONS (2)
CTGTYPE  DS    C              TYPE OF CATALOG RECORD
CTGTALIN EQU   C'A'             NON-VSAM
CTGNOFLD DS    FL1'0'         NUMBER OF ENTRIES IN CTGFIELD
CTGFDBK  DS    XL2            FEEDBACK AREA (IF NOT SUPERLOCATE)
CTGFBFLG DS    B              FLAGS (SUPERLOCATE)
CTGRSNCD DS    B              REASON CODE IF R15 > 0
CTGPSWD  DS    A(0)           ADDRESS OF CALLER-SUPPLIED PASSWORD
CTGPLLEN EQU   *-CTGPL                 LENGTH OF CTGPL
*----------------------------------------------------------------------
*        DYNAMIC STORAGE AREA (RMODE=ANY)
*----------------------------------------------------------------------
DYNAM    DSECT                         DYNAMIC STORAGE AREA
         DS    18F                     SAVE AREA FOR MAINLINE
PARMADDR DS    A                       ADDRESS OF CALLER'S PARM OR CPPL
DYNAML2  DS    A(DYNAML)               LENGTH OF DYNAMIC STORAGE AREA
RCVYSAVE DS    18F                     SAVE AREA FOR RECOVERY ROUTINE
WKCELL1  DS    D                       WORK AREA
WKCELL2  DS    D                       WORK AREA
WKCELL3  DS    D                       WORK AREA
WKWORDS  DS    10F                     WORK AREA
         DS    6F                      ADDITIONAL WORK AREA        EU13
*- - - Defined here below : dynamic allocation DD-name return - -  EU13
RT_DDN   EQU   WKWORDS+4+(S99RBEND-S99RB)+3*4+2*4+4+6,8            EU13
JSTCB    DS    A(TCB)                  ADDRESS OF THE JOB-STEP TCB
         AIF   (NOT D'DFASMS).SMS3     NOT DFP V3, JUMP
WORK4K   DS    A(DYNAM+DYNAML-4096)    4K work area ptr (DEVSTAT)
.SMS3    ANOP
ESTAEL   ESTAE MF=L
END_PROC DS    2A                      RETURN/RETRY ADDRESS
RETRY    EQU   END_PROC+4,4,C'A'       RETRY ADDRESS
NUMLINES DS    F                       NUMBER OF LINES
IEFEB4UV DS    V(IEFEB4UV)             UNITNAME CONVERSION RTNE
ISPLINK  DS    V(ISPLINK)              ISPF DIALOG INTERFACE
UNITNAME DS    CL8,XL4,XL4,XL4,XL8     IEFEB4UV
ZENVIR   DS    CL32                    ISPF ENVIRONMENT
K1       DS    PL4                     COUNTER
K2       DS    PL4                     COUNTER
EP10     DS    CL10                    SVC TABLE
STATUS   DS    C'OLD'                  DATA SET STATUS
FSWITCH  DS    XL1                     Functional switches
HARDCOPY EQU   X'80'                   BRIF/HARDCOPY switch
TSORUN   EQU   X'40'                   TSO running (not ISPF)      EU13
BATCHRUN EQU   X'20'                   BATCH running               EU13
FLGNOMSG EQU   X'10'                   NOMSG flag (debug)          EU18
DYNSHORT EQU   X'08'                   DYNAMIC AREA SHORTAGE switch
ERRBRIF  EQU   X'04'                   BRIF error signal
IGCERROR DS    A                       UNUSED SVC NUMBER
SMS_R14  DS    A(R14)                  return address
SWAREQL1 SWAREQ MF=L                   WORK AREA FOR SWAREQ
ATTACHL1 ATTACH SF=L                   WORK AREA FOR ATTACH
SNAPREGS DS    A(14,15,0,1,2,3,14,15)
DYN_CHK  DS    F                       lines for BRIF space
WK256    DS    XL256,2D                265-BYTE WORK AREA
TRTPRINT DS    XL256                   printable characters
*----------------------------------------------------------------------
*        SUB_TASK work area
*----------------------------------------------------------------------
SUB_WORK DS    0D
         DS    18F                     SAVE AREA FOR SUB_TASK      EU13
ENQL1    ENQ   (*-*,*-*,E,0),MF=L                                  @364
ENQL1L   EQU   *-ENQL1                                             @364
ECB1     DS    F,A(TCB)                COMMUNICATION ECB,TCB
MAXLPG   DS    A(60)                   Max. lines per page         EU15
CURPGN   DS    A(1)                    current page number         EU15
TLNEPT   DS    A(*-*)                  TITLE line pointer          EU15
MY_DDN   DS    CL8                     DDN=SHOWMVS (default)       EU15
EDQNAME  DS    CL8                     ENQ/DEQ : Q-name (Job-name)
EDRNAME  DS    CL16                    ENQ/DEQ : R-name (Day.Time)
BLANKS   DS    CL(L'LINE)              A BUNCH OF BLANKS
BASEREG  DS    A(R11)                  BASE REGISTER FOR RECOVERY ...
CPUONLNE DS    A(LINE),F'100'          Number of cpu's on-line
LNKTABLE DS    A(LINE,L'LINE,NEXTLINE) LNK-LIST
LPATABLE DS    A(LINE,L'LINE,NEXTLINE) LPA-LIST
APFTABLE DS    A(LINE,L'LINE,NEXTLINE) APF-LIST
LINKXTNT DS    A(LINE)                 link-list header line
CAMLST1  CAMLST NAME,DSNAME,VOLSER,SUB_W265 LOCATE/OBTAIN
SUB_W265 DS    265X                    LOCATE
         IECSDSL1 1                    F1-DSCB
         DS    XL5                     PADDING FOR OBTAIN
SUB_DWD  DS    D
*----------------------------------------------------------------------
LINES    DS    (&NLBRIF)CL(L'LINE)     LINES FOR BRIF
DYNAML   EQU   (((*-DYNAM)+7)/8)*8     LENGTH OF DYNAMIC STORAGE AREA
         EJECT
*======================================================================
*        DEFINE MVS CONTROL-BLOCKS
*----------------------------------------------------------------------
         IHASDWA DSECT=YES,VRAMAP=NO   SDWA DSECT
         IHAPSA DSECT=YES              PREFIXED STORAGE AREA
         CVT   PREFIX=YES,DSECT=YES,LIST=NO
         IARRCE                        RSM CTL & ENUM AREA
         IHAGDA                        GLOBAL DATA AREA
         IHASCVT DSECT=YES,LIST=NO     SECONDARY CVT
         IEFJESCT                      JES VECTOR TABLE
         IEFJSCVT                      SUB-SYSTEM COMM. VECTOR TABLE
         IEFJSSVT ,                    SUB-SYSTEM VECTOR TABLE
         AIF   ('&EUJES' NE 'JES2').JS23                           EU16
* Here follows those dsect's needed for JES2DATA assembly.         EU16
*        Note: when the original dsect contains a TITLE card, then EU16
*              just the needed fields only are defined below.      EU16
* From : $SVT - HASP SUB-SYSTEM VECTOR TABLE DSECT                 EU16
SVTTO    EQU   X'01F8',3     OWN NODE INFORMATION            @130  EU16
SVTTONOD EQU   X'01F8',2      OWN NODE ID        (BINARY)    @130  EU16
SVTTOQUL EQU   X'01FA',1      OWN NODE SYSTEM ID (EBCDIC)    @130  EU16
SVTRDT   EQU   X'0208',4     ADDRESS OF REMOTE DESTINATION TABLE   EU16
SVTRDTE  EQU   X'020C',4     ADDRESS OF LAST RDT ELEMENT           EU16
SVTCKPT1 EQU   X'0224',51     CKPT1 HFAME                    @220  EU16
SVTCKPT2 EQU   X'025C',51     CKPT2 HFAME                    @220  EU16
SVTPIT   EQU   X'338',4      PARTITION INFORMATION TABLE ADDRESS   EU16
         $HFAME ,            HASP FILE ALLOCATION MAP ENTRY        EU16
         $RDT  ,             HASP REMOTE DESTINATION TABLE         EU16
         $PIT  ,             HASP PARTITION INFORMATION TABLE      EU16
* From : $SJB  HASP SUB-SYSTEM JOB BLOCK                           EU16
SJBID    EQU   X'048',4      SJB IDENTIFIER                        EU16
SJBJOBID EQU   X'114',8      JOB IDENTIFIER - EBCDIC, NUMERIC      EU16
SJBJOBNM EQU   X'11C',8      JOBNAME FROM JOB CARD                 EU16
.JS23    IEFZB4D0 ,                                                EU16
         IEFZB4D2 ,
         IEFZB505 LOCEPAX=YES          EPA MAPPING FOR SWAREQ      @364
         IEFZB610 ,                    PPT MAPPING                 EU12
         IKJTCB DSECT=YES,LIST=NO      TASK CONTROL BLOCK
         IHARD  ,                      REGION DESCRIPTOR
         IHASPQE ,                     sub-pool queue element
         IHASPQA ,                     sub-pool queue element
         IHADQE  ,                     descriptor queue element
         IKJRB  DSECT=YES,LIST=NO      REQUEST BLOCK
         IHACDE ,                      CONTENTS DIRECTORY ENTRY
         IHALLE ,                      LOAD-LIST ELEMENT
         IHAXTLST ,                    EXTENT LIST
         IHASVC ,                      SVC TABLE ENTRY
         IKTTCAST DSECT=YES            TCAS (a key 0 area)
         IEFUCBOB LIST=NO,PREFIX=NO    UNIT CONTROL BLOCK
         IECDDCE ,                     DEVICE CLASS EXTENSION
D3380    EQU   X'14'                   MDR TYPE FOR STD 3380 MODELS
D3380D   EQU   X'1C'                   MDR TYPE FOR 3380-D
D3380E   EQU   X'1B'                   MDR TYPE FOR 3380-E
*        IECDCQ  ,                     Device class table          ****
DCQ      DSECT
DCQNAME  DS    C'DCQ '
DCQLNGTH DS    H
DCQCOUNT DS    H
DCQFIRST DS    A(DCQELMNT)             First element
DCQDSTCT DS    XL4
DCQUCBNO DS    F
DCQELMNT DSECT
DCQCHAIN DS    A(DCQELMNT)             NEXT DCQ
DCQDEVCL DS    X                       DEVICE CLASS
DCQFLG1  DS    X                       RESERVED
DCQUCBCT DS    H                       # OF UCBS
DCQUCBAD DS    V(UCBOB)                FIRST UCB
DCQDEVNM DS    CL8                     CLASS NAME
         DS    XL8                     RESERVED
         IEECUCM DSECT=YES             Unit Control Module
         DCBD  DSORG=(XE,PS),DEVD=DA   IHADCB
JFCB     DSECT
         IEFJFCBN LIST=NO              JOB FILE CONTROL BLOCK
TIOT     DSECT
         IEFTIOT1                      TASK INPUT-OUTPUT TABLE
         IEZDEB  LIST=NO               DATA EXTENT BLOCK
         IEZJSCB ,                     JOB STEP CONTROL BLOCK
         IKJCPPL ,                                                 EU14
         IKJPSCB ,                     PROTECTED STEP CONTROL BLOCK
         IKJEBECA ,                                                EU14
         AIF   (D'PSCBCNAU).TSO1
PSCBCNAU EQU   X'01'                   CONSOLE authority    (TSO/E 2.3)
.TSO1    IKJUPT ,                      User Profile Table
         AIF   (D'UPTPLANG).TSO2
UPTPLANG DS    CL3                     Primary language     (TSO/E 2.2)
UPTSLANG DS    CL3                     Secondary language   (TSO/E 2.2)
.TSO2    ANOP
*        IHALLT DSECT=YES              LINK-LIST TABLE             ****
LLT      DSECT
LLTLLT   DS    C'LLT '                 BLOCK ACRONYM
LLTNO    DS    F                       NUMBER OF ENTRIES
LLTENTRY DS    0CL45                   DSNAME ENTRY
LLTDSNL  DS    FL1                     DSNAME LENGTH AFTER TRUNCATION
LLTDSNAM DS    CL44                    DATA SET NAME
LLTNEXT  EQU   *                       NEXT ENTRY
*        IHAPCCAT DSECT=YES            PCCA TABLE                  ****
         IHAPCCA DSECT=YES             PHYSICAL CONFIG. COMM. AREA
*        IDAAMCBS                      ACCESS METHOD CB STRUCTURE  ****
CBS      DSECT
CBSID    DS    C'AM'                   AMCBS ID
CBSSIZ   DS    H'144'                  LENGTH OF THE AMCBS
CBSMCSTA DS    C'CCHH'                 CCHH OF MASTER CATALOG
CBSACB   DS    V(ACB)                  ACB FOR THE MASTER CATALOG
CBSCBP   DS    V(IDC019C1)             CONTROL BLOCK MANIPULATION RTNE
CBSCMP   DS    V(IGG0CLA1)             CATALOG MANAGEMENT ROUTINE
CBSCAXCN DS    V(CAXWA)                ADDRESS OF THE CAXWA CHAIN
CBSCRACA DS    V(CAXWA)                ADDRESS OF THE CRA CAXWA CHAIN
*        IDACAXWA                                                  ****
CAXWA    DSECT                         Catalog Work Area
CAXID    DS    X'CA'                   CAX ID
CAXFLG4  DS    X,2X                    FLAGS (4)
CAXCHN   DS    A(CAXWA)                NEXT CAXWA IN CHAIN
CAXFLGS  DS    X                       FLAGS (1)
CAXMCT   EQU   X'04'                   MASTER CATALOG
CAXFLG2  DS    X                       FLAGS (2)
CAXFLG3  DS    X                       FLAGS (3)
CAXFLG5  DS    X,3F                    FLAGS (5)
CAXACB   DS    V(IFGACB)               CATALOG'S ACB
CAXUCB   DS    V(UCBOB)                CATALOG'S UCB
         DS    FL3,FL3,FL3,FL3,H,H,A
CAXCNAM  DS    CL44                    CATALOG'S DSNAME
CAXVOLID DS    CL6                     CATALOG'S VOLUME (CRA ONLY)
*        ILRASMVT DSECT=YES       AUXILIARY STRGE MGR VECTOR TABLE ****
ASMVT    DSECT                         ILRASMVT
ASMFLAG1 DS    X                       FLAGS 1
ASMFLAG2 DS    X,2X                    FLAGS 2
ASMQUICK EQU   X'08'                   QUICK START IPL
ASMSART  DS    V(SART)                 SWAP ACTIVITY REFERENCE TABLE
ASMPART  DS    V(PART)                 PAGE ACTIVITY REFERENCE TABLE
*        ILRPART                                                   ****
PART     DSECT                         PAGING ACTIVITY REFERENCE TABLE
PARTIDEN DS    C'PART'                 EYE CATCHER
PARTSIZE DS    F             +4        NUMBER OF ENTRIES IN THE PART
PARTEUSE DS    F             +8
PARTCIR0 DS    A             +C
PARTCIR1 DS    A             +10
PARTCIR2 DS    A             +14
PARTTPAR DS    0A            +18
PARTDSNL DS    A             +18       ADDR OF DSN LIST
PARTPCTQ DS    A             +1C
PARTLCNT DS    H             +20
PARTFLG1 DS    B             +22
         DS    X             +23
PARTNPCF DS    A             +24
PARTNPCL DS    A             +28
PARTTIME DS    F             +2C
         DS    CL32          +30
PARTENTS EQU   *
         SPACE 2
PARTENT  DSECT                         PAGE DATA SET TABLE ENTRY
PAREPARE DS    A             +0
PAREBRST DS    AL1           +4
PAREIORN DS    AL1           +5
         DS    XL2           +6
PARETYPE DS    B             +8
PAREPLPA EQU   X'80'                   PLPA
PARECOMM EQU   X'40'                   COMMON
PAREDPLX EQU   X'20'                   DUPLEX
PARELOCL EQU   X'10'                   LOCAL
PAREBPF  EQU   X'08'                   CACHED DEVICE
PARESPP  EQU   X'04'                   SET PAGING PARAMETERS OK
PAREPD   EQU   X'02'                   PAGEDEL IN PROCESS
PAREDRN  EQU   X'01'                   DAINING
PAREFLG1 DS    B             +9
PARENUSE EQU   X'80'                   NOT IN USE
PARENVIO EQU   X'10'                   VIO=NO
PARENN   DS    H             +A
PAREDEIB DS    A             +C        POINTER TO DEIB
PARESZSL DS    F             +10       SIZE OF DATA SET (IN SLOTS)
PARESLTA DS    F             +14       NUMBER OF AVAILABLE SLOTS
PARERRCT DS    F             +18       NUMBER OF PERMANENT I/O ERRORS
PAREIORB DS    A             +1C       ADDR OF 1ST IORB
PAREPATP DS    A             +20       POINTER TO PAT
PAREPCTP DS    A             +24       POINTER TO PCT
PAREEDBP DS    A             +28       POINTER TO EDB
PAREUCBP DS    A             +2C       POINTER TO UCB
PARETIOR DS    A             +30
PARETIME DS    F             +34
PARERQTM DS    F             +38       LATEST REQUEST SERVICE TIME
         DS    H             +3C       RESERVED
PAREREQS DS    H             +3E       OUTSTANDING I/O REQUESTS
PAREFLG2 DS    B             +40       FLAGS
PARELSLT DS    XL3           +41       WRITE CURSOR
PAREOCTB DS    A             +44       OLD CTB ADDR
PAREMIGA DS    A             +48       MIGRATED SLOT COUNT
PARECADR DS    FL1           +4C       INDEX NUMBER FOR CACHE ENTRY
         DS    XL19          +4D       RESERVED
PARTELEN EQU   *-PARTENT               96 BYTES
         IKJTSVT                       TSO VECTOR TABLE
         AIF   ('&SYSSPLV' LT '2').SPL4X     LOW MACLIB LEVEL, JUMP
TPVT     DSECT ,                       TSO PARMLIB vector table
TPVTTPVT DS    C'TPVT'                 BLOCK ID
         AIF   ('&SYSSPLV' LT '3').SPL4A     NOT MVS/ESA MACLIB, JUMP
TPVTLEN  DS    H'72'                   LENGTH OF TPVT
TPVTLVL  DS    X'02'                   LEVEL
TPVTMBR  DS    C'IKJTSO00'             CURRENT PARMLIB MEMBER
         DS    X,4X                    UNUSED
TPVTCTLT DS    A(CTLT),F'60'           IKJCTLT
         DS    4X                      UNUSED
TPVTSCB  DS    V(SCB)                  IKJEESCB   (SYS1.BRODCAST)
TPVTALPL DS    V(ALPL)                 ?
TPVTTPT  DS    V(TPT)                  ?
TPVTXPRM DS    V(XPRM)                 INMXPARM   (XMIT)
TPVTCNPR DS    V(CNPRM)                IKJCNPRM   (CONSOLE)
         DS    8X                      UNUSED
TPVTHCB  DS    V(HCB)                  IKJEFHCB   (HELP)
         DS    8X                      UNUSED
         AGO   .SPL4B
.SPL4A   ANOP
TPVTLEN  DS    H'44'                   LENGTH OF TPVT
TPVTLVL  DS    X'01'                   LEVEL
         DS    X                       UNUSED
TPVTCTLT DS    A(CTLT)                 IKJCTLT
         DS    4X                      UNUSED
TPVTEXTV DS    V(EXTV)                 ?
TPVTSCB  DS    V(SCB)                  IKJEESCB   (SYS1.BRODCAST)
         DS    20X                     UNUSED
.SPL4B   ANOP
CTLT     DSECT
CTLTCTLT DS    C'CTLT'                 BLOCK ID
         AIF   ('&SYSSPLV' LT '3').SPL4C     NOT MVS/ESA MACLIB, JUMP
CTLTLEN  DS    H'60'                   LENGTH OF TPVT
CTLTLVL  DS    X'02'                   LEVEL
         AGO   .SPL4D
.SPL4C   ANOP
CTLTLEN  DS    H'48'                   LENGTH OF TPVT
CTLTLVL  DS    X'01'                   LEVEL
.SPL4D   DS    X                       UNUSED
CTLTE2   DS    A,F,H,H                 IKJEFTE2
CTLTE8   DS    A,F,H,H                 IKJEFTE2
CTLTNS   DS    A,F,H,H                 IKJEFTNS
CTLTAP   DS    A,F,H,H                 IKJEFTAP
.SPL4X   IEESMCA                       SMF
*        IEEMBRDS ,                    SMF RDS                     ****
IEEMBRDS DSECT ,
RDSID    DS    C'RDS '                 CONTROL BLOCK ID
RDSNEXT  DS    A(IEEMBRDS)             FORWARD CHAIN POINTER
RDSPREV  DS    A(IEEMBRDS)             BACKWARD CHAIN POINTER
*
RDSFLG1  DS    X
RDSFREE  EQU   X'10'                   NEED TO FREE DATASET
RDSDUMP  EQU   X'08'                   OPER HAS BEEN TOLD TO DUMP DS
RDSREADY EQU   X'04'                   DS IS OPEN AND READY
RDSCLOSE EQU   X'02'                   DS NEEDS TO BE CLOSED
RDSNDOP  EQU   X'01'                   DS NEEDS TO BE OPENED
*
RDSFLG2  DS    X
RDSLOST  EQU   X'80'                   DUMMY RDS FOR LOST DATA
RDSPHYER EQU   X'40'                   PHYSICAL I/O ERROR
RDSLOGER EQU   X'20'                   LOGICAL I/O ERROR
*
RDSFLG3  DS    X                       RESERVED
RDSFLG4  DS    X                       RESERVED
RDSNAME  DS    C'SYS1.MANX '           DATASET NAME
RDSVOLID DS    C'SYSRES'               VOLUME SERIAL
RDSDDN   DS    C'SYS00001'             DDNAME RETND BY DYNALLOC
RDSCAPTY DS    F                       CAPACITY IN # OF BLOCKS
RDSNXTBL DS    F                       NUMBER OF NEXT AVAIL BLOCK
RDSBQE   DS    F                       ADDR OF BQE BEING WRITTEN
RDSAVSPC DS    F                       AVAIL SPACE FROM SHOWCB
RDSENRBA DS    F                       ENDRBA FROM SHOWCB
RDSCINV  DS    F                       CONTROL INTERVAL SIZE
RDSDOMID DS    F                       MSG # OF DUMP MSG
RDSRPL   DS    19F                     VSAM RPL
RDSACB   DS    19F                     VSAM ACB
         IEFTCT ,                      SMF TCT
         ICHPRCVT ,                    RACF <=== CVTRAC            EU07
         ICHPDSDT ,                    RACF <=== RCVTDSDT          EU11
*        ARCQCT                        HSM                         ****
MQCT     DSECT                         ARCACT <=== CVTHSM
         ORG   *+X'002C'
MQCTID   DS    C'QCT*'                 BLOCK ID
MQCTVER  DS    C' 2'                   VERSION
MQCTREL  DS    C'5'                    RELEASE
MQCTMOD  DS    C'0'                    MODIFICATION
         IHALPDE ,                     LPA DIRECTORY ENTRY
         IHAASCB DSECT=YES             ADDRESS SPACE CONTROL BLOCK
         IHAASVT DSECT=YES             ADDRESS SPACE VECTOR TABLE
         IHAASXB DSECT=YES         MAP THE ASXB.
         IHAACEE ,                 MAP THE ACEE.
         IRAOUCB DSECT=YES             SRM PARMS
*        IRARMCT DSECT=YES                                         ****
RMCT     DSECT                     RESOURCE MANAGER CONTROL TABLE
RMCTNAME DS    C'RMCT' -           BLOCK IDENTIFICATION
RMCTCCT  DS    V(CCT) -            CPU MANAGEMENT CONTROL TABLE
RMCTICT  DS    V(ICT) -            I/O MANAGEMENT CONTROL TABLE
RMCTMCT  DS    V(MCT) -            STORAGE MANAGEMENT CONTROL TABLE
RMCTRMPT DS    V(RMPT) -           CTL ALGORITHM PARAMETER TABLE
RMCTRMCA DS    V(RMCA) -           CTL ALGORITHM CONTROL AREA
RMCTWMST DS    A(0) -              ADDR OF WLM SPECIFICATION TABLE
RMCTRLCT DS    A(0) -              ADDR OF LOGICAL CHANNEL MGMT TABLE
RMCTRMSA DS    A(0) -              ADDR OF RESOURCES MANAGER SAVE AREA
RMCTRMPD DS    A(0) -              ADDR OF RESOURCES MANAGER PERF DATA
RMCTRMEX DS    V(RMEX) -           ROUTINE EXITING VECTOR TABLE
RMCTRMSB DS    V(RMSB) -           SUBROUTINE CALLING VECTOR TABLE
RMCTEPPA DS    V(IRAEPPA) -        PRTL ANALYSIS ENTRY TABLE
RMCTEPDT DS    V(EPDTSCN) -        USER ACTION ENTRY TABLE
RMCTEPAT DS    V(EPATSCN) -        ALGORITHM ENTRY TABLE
RMCTEPBG DS    V(EPAT) -           ADDR OF ALG ENTRY PT TABLE
RMCTADJC DS    F'0' -              ADJUSTMENT FACTOR FOR CPU RATE
RMCTITT  DS    V(IRASECHT) -       INTFC EVENT CHARACTERISTICS TABLE
RMCTEPET DS    V(IRAPRCSR) -       EVENT ROUTING VECTOR TABLE
RMCTFLTM DS    V(IRAFLTM) -        TIME OF DAY ADJUST VECTOR TABLE
RMCTEPPR DS    V(IRAEPPR) -        PROCESS RATE DEPENDENT ENTRY TABLE
RMCTWAST DS    A(0) -              ADDR OF WAR SPECIFICATION TABLE
RMCTWAMT DS    A(0) -              ADDR OF WAR MEASUREMENT TABLE
RMCTTMQE DS    V(IRACTMQE) -       SCHEDULED RTNE QUEUE HEADER ADDR
RMCTAQCT DS    F'0' -              ACTION QUEUE MEMBER COUNT
RMCTAQHD DS    V(IRAOUCB) -        ACTION QUEUE FORWARD POINTER
RMCTWTQE DS    V(IRAWTQE) -        WAIT - QUEUE HEADER BLOCK ADDRESS
RMCTOTQE DS    V(IRAOTQE) -        OUT - QUEUE HEADER BLOCK ADDRESS
RMCTINQE DS    V(IRAINQE) -        IN - QUEUE HEADER BLOCK ADDRESS
RMCTR10  DS    F'0' -              RESERVED
RMCTTBS  DS    F'0' -              STARTING TIME BASE FOR TIME OF DAY
RMCTTOD  DS    F'0' -              TIME OF DAY - SYST RSRC MGR INVOKE
RMCTTOC  DS    D'0' -              TIME OF CENTURY - 64BIT BINARY NMB
RMCTALA  DS    4B'00000000' -      ALGORITHM REQUEST ACCUMULATOR FIELD
RMCTALR  DS    4B'00000000' -      IMMEDIATE ALGORITHM REQUEST FIELD
RMCTRQSV DS    V(RQSV) -           ADDR OF REQ SRV DATA AREA
RMCTFLGS DS    1B'00000000' -      PROCESSING CONTROL FLAGS
RMCTMFA  EQU   BIT0 -              MEASUREMENT FACILITY ACTIVE
RMCTCPS1 EQU   BIT1                CAP SWITCH
RMCTINIT EQU   BIT3 -              SRM INITIALIZATION WAS PERFORMED
RMCTRSV1 EQU   BIT4 -              RESERVED
RMCTSTW  EQU   BIT5 -              SET HAS STOPPED WAR COLLECTION
RMCTRSV2 EQU   BIT6 -              RESERVED
         DS    1B'00000000' -      OVERRIDE CONDITION FLAGS
RMCTMFS  EQU   BIT1 -              MF1 ACTIVE,SET IPS RCVD
RMCTTAPE DS    X,X                     SELTAPE
RMCTSLTN EQU   X'80'                     NEXT
RMCTSLTR EQU   X'40'                     RANDOM
RMCTSLTL EQU   X'20'                     LAST
RMCTSLTF EQU   X'10'                     FIRST
RMCTTELM DS    V(IEATSELM) -       RESOURCES MANAGER TIMING ELEMENT
RMCTCPID DS    C'RM1',X'03' -      RESOURCES MANAGER CELL POOL ID
RMCTTOCI DS    D'0' -              CLOCK READ AREA - 64BIT BINARY NMB
RMCTOUCB DS    V(IRAOUCB) -        PREASSEMBLED MODEL OUCB
RMCTOUXB DS    V(IRAOUXB) -        INTERPOSED DUMMY OUXB
RMCTSRBT DS    V(IRASRBT) -        RESOURCES MANAGER SRB TABLE
RMCTDMDT DS    A(DMDT)             ADDR OF DOMAIN TABLE
RMCTDMDE DS    A(DMDT)             ADDR OF LAST DMN TAB ENTRY
RMCTDMNC DS    H'0' -              NUMBER OF DOMAINS
         DS    H,7F
RMCTICST DS    V(ICST)             ICS TABLE
*        IRACCT DSECT=YES                                          ****
CCT      DSECT , -                 CPU MANAGEMENT CONTROL TABLE
CCTCCT   DS    C'CCT '  -          ACRONYM IN EBCDIC -CCT-
CCCAPMET DS    F'200' -            MINIMUM USER EXECUTION INTERVAL FOR
*                                  MEAN TIME TO WAIT COMPUTATION
CCCR00   DS    F'0'                RESERVED
CCCMNSIN DS    F'5000' -           MINIMUM INTERVAL FOR COMPUTING
*        APG DISPATCHING PRIORITY COMPUTATION CONSTANTS
CCCR000  DS    H'0'                RESERVED
CCCAPGHI DS    H'0'                APG HIGH VALUE
CCCAPLDP DS    H'1' -              DP FOR UNDISPATCHED APG USERS
CCCAPGLO DS    H'0'                APG LOW  VALUE
CCCAPLEN DS    H'0'                LENGTH OF MTW GROUPS 1
*        IRARMPT DSECT=YES                                         ****
RMPT     DSECT                         SRM PARAM TABLE (IRARMPT)
         DC    C'RMPT'
         ORG   RMPT+X'53'
RMPTOPTN DC    C'00'                   SYS1.PARMLIB(IEAIPSXX)
*        IRAWMST DSECT=YES                                         ****
WMST     DSECT                         WORKLOAD MGR SPECIFICATIONS TBLE
         DC    C'WMST'
WMSTID   DC    C'00'                   SYS1.PARMLIB(IEAIPSXX)
WMSTIPC  EQU   WMST+X'94',4            CPU SERVICE COEFFICIENT
WMSTIPI  EQU   WMST+X'98',4            I/O SERVICE COEFFICIENT
WMSTIPM  EQU   WMST+X'9C',4            MSO SERVICE COEFFICIENT     EU05
WMSTIPB  EQU   WMST+X'A0',4            SRB SERVICE COEFFICIENT
*        IRAICST DSECT=YES                                         ****
ICST     DSECT                         SRM ICS TABLE
ICSTID   DC    C'00'                   SYS1.PARMLIB(IEAICSXX)
*
*        SRM DOMAIN TABLE (IRADMDT)
*
*        THIS MAPPING IS VALID FOR MVS/XA THROUGH MVS/ESA 4.1.
*        STARTING WITH MVS/ESA 4.2, THE DOMAIN TABLE ENTRIES ARE
*        X'B4' BYTES LONG.
*
*        IRADMDT DSECT=YES                                         ****
DMDT     DSECT
DMDTNO   DS    FL1,X,X                 DOMAIN NUMBER
DMDTWT   DS    FL1                     WEIGHTING FACTOR
DMDTMPLT DS    H                       CURRENT MPL TARGET
DMDTRUA  DS    H,2X                    AVG # OF READY USERS * 16
DMDTCMPL DS    H                       CURRENT MPL
DMDTOUTU DS    H                       USERS SWAPPED OUT
DMDTINCU DS    H                       INCORE USERS
DMDTRUC  DS    F,F                     ACCUM FOR READY USER AVG
DMDTTWSR DS    F                       WEIGHTED INTVL DMN SVCE
DMDTAOBJ DS    FL1                     USER PERF OBJ NUMBER
DMDTDOBJ DS    FL1                     USER PERF OBJ NUMBER
DMDTFWKL DS    FL2                     DOMAIN FIXED WKLD * 256
DMDTCIDX DS    FL2                     CONTENTION INDEX
DMDTNSW  DS    H                       NON-SWAPPABLE USERS
DMDTRUMX DS    H                       MAX # READY USERS IN INTVL
DMDTFLGS DS    X,X                     FLAGS
DMDTTRNC DS    F                       XACTN COUNT FOR RTO
DMDTTRNT DS    F                       ELAPSED TIME ACCUM FOR RTO
DMDTTWET DS    F                       ELAPSED TIME AVG FOR RTO
DMDTLO   DS    H                       MIN MPL
DMDTHI   DS    H                       MAX MPL
DMDTHIRV DS    V(ASCB),4X              USER WITH MAX RECOMMENDATION VAL
DMDTEND  EQU   *                X'40'
         YREGS                         REGISTER EQUATES
         IEZBITS                       BIT0-BIT7
         AIF   (NOT D'DFASMS).SMS4     NOT DFP V3, JUMP
         IEFJSSIB                      SSIB
         IEFJSSOB ,                    SSOB header
         IEFSSSA                       SSOB extension for SMS
         IGDVLD                        Volume record definition
.SMS4    PRINT &EULST                                              EU00
         EJECT
*======================================================================
*        GENERATE SUB-RTNE & LITERALS
*----------------------------------------------------------------------
         SPACE 1
         STRING FINAL_CALL,DOC=YES                                 EU00
         EJECT
*======================================================================
*        LITERAL POOL
*----------------------------------------------------------------------
         PRINT GEN                                                 EU00
         LTORG
         EJECT
*======================================================================
*        MESSAGES (LOG) DEFINITIONS
*----------------------------------------------------------------------
         PRINT &EULST                                              EU00
WTOSK0   WTO   '-- Output dynamically allocated to TSO session as SYSOUX
               T=&SYSOHC --',ROUTCDE=11,DESC=7,MF=L                EU13
WTOSK1   WTO   '-- PARM= error at :                                --',X
               ROUTCDE=11,DESC=7,MF=L                              EU15
WTOSK1M  EQU   24,30                                               EU15
WTOSK1L  EQU   *-WTOSK1                                            EU15
WTOSK2   WTO   '-- ISPLINK :          failed - RC =     -- ',          X
               ROUTCDE=11,DESC=7,MF=L
WTOSK2N  EQU   17,8
WTOSK2R  EQU   39,4
WTOSK2L  EQU   *-WTOSK2
WTOSK3   WTO   '-- Dynamic allocation error - RC =     REASON=     INFOX
               =     --',ROUTCDE=11,DESC=7,MF=L
WTOSK3C  EQU   38,4
WTOSK3R  EQU   50,4
WTOSK3I  EQU   60,4
WTOSK3L  EQU   *-WTOSK3
         AIF   (NOT D'DFASMS).SMS5     NOT DFP V3, JUMP
WTOSK4   WTO   '-- DEVSTAT 4K storage unavailable (increment "NLBRIF" vX
               alue --',ROUTCDE=11,DESC=7,MF=L
.SMS5    ANOP
WTOSK5   WTO   '-- Dynamic storage area shortage (increment "NLBRIF" vaX
               lue --',ROUTCDE=11,DESC=7,MF=L
WTOSK6   WTO   '-- Requested FILE(        ) not pre-allocated -- ',    X
               ROUTCDE=11,DESC=7,MF=L                              EU15
WTOSK6N  EQU   22,8                                                EU15
WTOSK6T  EQU   30,22                                               EU15
WTOSK6L  EQU   *-WTOSK6                                            EU15
WTOSK7   WTO   '-- Sub-Task ATTACH failed - RC =     --',              X
               ROUTCDE=11,DESC=7,MF=L
WTOSK7C  EQU   36,4
WTOSK7L  EQU   *-WTOSK7
WTOSK8   WTO   '-- Output file : OPEN failed - RC =     --',           X
               ROUTCDE=11,DESC=7,MF=L
WTOSK8C  EQU   39,4
WTOSK8L  EQU   *-WTOSK8
WTOSK9   WTO   '-- Output file : OPEN unsuccessfully complete --',     X
               ROUTCDE=11,DESC=7,MF=L
WTOSK10  WTO   '-- Output file : CLOSE failed - RC =     --',          X
               ROUTCDE=11,DESC=7,MF=L
WTOSK10C EQU   40,4
WTOSK10L EQU   *-WTOSK10
WTOSK11  WTO   '-- Output file : PUT error has occurred --',           X
               ROUTCDE=11,DESC=7,MF=L
WTOSK12  WTO   '-- Output file : invalid LRECL (must be 101) --',      X
               ROUTCDE=11,DESC=7,MF=L
WTOSK13  WTO   '-- Output file : invalid RECFM (must be Fixed) --',    X
               ROUTCDE=11,DESC=7,MF=L
WTOSK14  WTO   '-- Output file : invalid RECFM (must be ASA) --',      X
               ROUTCDE=11,DESC=7,MF=L
WTOSK15  WTO   '-- Output file : invalid BLKSIZE (not multiple of 101) X
               --',ROUTCDE=11,DESC=7,MF=L
         PRINT GEN                                                 EU00
         SPACE 1
         END
