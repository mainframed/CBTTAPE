         MACRO ,
&NAME    STRING &INTO=,&PRINT=NOGEN,&DOC=NO                        EU-0
         GBLC  &EUSTL                                              EU-0
         AIF   ('&EUSTL' EQ 'GEN').FGENL                           EU-0
         AIF   ('&PRINT' EQ 'GEN').FGENL                           EU-0
         AGO   .NOGEN                                              EU-0
.FGENL   PUSH  PRINT                                               EU-0
         PRINT GEN
.NOGEN   LCLA  &I,&J,&N                                            EU-0
         GBLA  &$_FIELD,&$_MAXBL
         GBLC  &$_FIELDS(9999)
         AIF   (N'&SYSLIST EQ 1 AND '&SYSLIST(1)' EQ 'FINAL_CALL'      X
               AND T'&INTO EQ 'O').GENL
&LABEL   SETC  'IHB&SYSNDX'            stem for local labels
&LQ      SETC  'L'''                   length attribute
&NAME    BAS   R14,$STRING             Call @STRING sub-routine
         AIF   (N'&SYSLIST EQ 0).ERR1
         AIF   (T'&INTO EQ 'O').ERR2
         DC    AL2(&LABEL.P-*)
         AIF   (D'$STRING).LOCTR2
$LTORG   LOCTR ,                       ADDRESSABLE CONSTANTS
         CNOP  0,4
$STRING  BALR  R15,0                   local base
         L     R15,6(,R15)             routine address           00
         BSM   0,R15                   go there with amode31     04
         DC    A(@STRING+X'80000000')  routine address           06
$FARRTNE LOCTR ,                       far routines
.LOCTR2  ANOP
$LITERAL LOCTR ,                       NON-ADDRESSABLE CONSTANTS
&TO1     SETC  '&INTO(1)'
&TO2     SETC  '&LQ&INTO'
         AIF   (N'&INTO EQ 1).PUNTO8            JUMP IF INTO=XXX
         AIF   ('&INTO(1)'(1,1) NE '(').PUNTO3  JUMP IF INTO=(XXX,44)
&TO1     SETC  '0&INTO(1)'                              INTO=((R3),44)
.PUNTO3  ANOP
&TO2     SETC  '&INTO(2)'                               INTO=(XXX,LL)
         AIF   ('&INTO(2)'(1,1) NE '(').PUNTO8  JUMP IF INTO=(XXX,44)
&TO2     SETC  '0&INTO(2)'                              INTO=(XXX,(R1))
.PUNTO8  ANOP
&LABEL.P DC    S(&TO1,&TO2)
.*---------------------------------------------------------------------
.*-------      FIELDS       -------------------------------------------
.*---------------------------------------------------------------------
         LCLB  &LAST,&BIN,&HEXA,&REG,&PACKED,&LEFT,&ZERO,&TRUNC
&I       SETA  1
.*
.LOOP1   ANOP
&LAST    SETB  (&I EQ N'&SYSLIST)                LOOP
         AIF   ('&SYSLIST(&I)'(1,1) EQ '''').LIT00
.*---------------------------------------------------------------------
.*       PROCESS FIRST SUBPARAMETER (ADDRESS)
.*---------------------------------------------------------------------
&P1S     SETC  '&SYSLIST(&I,1)'
&P2L     SETC  '0'                     INPUT LENGTH
&P3L     SETC  '0'                     OUTPUT LENGTH
         AIF   ('&SYSLIST(&I)'(1,1) GE '0').FLD180 SPACES
         AIF   ('&SYSLIST(&I)' EQ '%TIME').FLD190 %TIME
         AIF   ('&SYSLIST(&I,1)'(1,1) NE '(').FLD115 (R2)
         AIF   (T'&SYSLIST(&I,2) EQ 'O').FLD250
&P1S     SETC  '0&SYSLIST(&I,1)'       CHANGE (R1) TO 0(R1)
.FLD115  ANOP
.*
         AIF   (T'&SYSLIST(&I,2) NE 'O').FLD200
.*
         AIF   (NOT D'&SYSLIST(&I)).FLD140
&P2C     SETC  T'&SYSLIST(&I)
.*MNOTE *,'&P1 &P2C'
         AIF   ('&P2C' EQ 'F' OR '&P2C' EQ 'H' OR '&P2C' EQ 'P').FLD220
         AIF   ('&P2C' EQ 'G').FLD210  FL2
.FLD140  ANOP
&P2L     SETC  '&LQ&SYSLIST(&I,1)'     L'ABCDEF
         AGO   .FLD300
.*
.FLD180  AIF   ('&SYSLIST(&I)'(K'&SYSLIST(&I),1) NE 'X').FLD800
&P1S     SETC  'BLANKS'
&P2L     SETC  '&SYSLIST(&I)'(1,K'&SYSLIST(&I)-1) 12
&L       SETA  &P2L                    NUMBER OF BLANKS
         AIF   (&L LE &$_MAXBL).FLD800
&$_MAXBL SETA   &L                     MAX NUMBER OF BLANKS
         AGO   .FLD800
.*
.FLD190  ANOP                          %TIME
&P1S     SETC  '1(14)'                 %TIME
         AGO   .FLD800
.*---------------------------------------------------------------------
.*       PROCESS SECOND SUBPARAMETER (LENGTH/TYPE)
.*---------------------------------------------------------------------
.FLD200  AIF   (T'&SYSLIST(&I,2) EQ 'O').FLD300 NO LENGTH SPECIFIED
&P2C     SETC  '&SYSLIST(&I,2)'
         AGO   .FLD220
.*T'&P1=G
.FLD210  ANOP
&L       SETA  L'&SYSLIST(&I)          T'&P1 = 'G'
&P2C     SETC  'FL&L'                  T'&P1 = 'G'
.*
.FLD220  ANOP
&P2L     SETC  '0&P2C'                 (R2) LENGTH
         AIF   ('&P2C'(1,1) EQ '(').FLD300
&P2L     SETC  '&P2C'                  3(R2) LENGTH
         AIF   ('&P2C'(K'&P2C,1) EQ ')').FLD300
&P2L     SETC  '0'
&PACKED  SETB  ('&P2C' EQ 'P')
         AIF   (&PACKED).FLD300
&P2L     SETC  '1'
         AIF   ('&P2C' EQ 'FL1').FLD240
&P2L     SETC  '3'
         AIF   ('&P2C' EQ 'FL2' OR '&P2C' EQ 'H').FLD240
&P2L     SETC  '7'
         AIF   ('&P2C' EQ 'FL3').FLD240
&P2L     SETC  '15'
         AIF   ('&P2C' EQ 'F').FLD240
&P2L     SETC  '&P2C'                  IMMEDIATE LENGTH, FIELD
         AGO   .FLD300
.*
.FLD240  ANOP                          BINARY VARIABLE
&BIN     SETB  1
         AGO   .FLD300
.*
.FLD250  ANOP                          REGISTER CONTENT
&REG     SETB  1
.*---------------------------------------------------------------------
.*       PROCESS THIRD SUBPARAMETER (OUTPUT FORMAT)
.*---------------------------------------------------------------------
.FLD300  AIF   (T'&SYSLIST(&I,3) EQ 'O').FLD800
&HEXA    SETB  ('&SYSLIST(&I,3)' EQ 'X') HEXADECIMAL
&TRUNC   SETB  ('&SYSLIST(&I,3)' EQ 'T') TRUNCATE
         AIF   (&HEXA OR &TRUNC).FLD800
.*
&P3C     SETC  '&SYSLIST(&I,3)'
&P3L     SETC  '250'
         AIF   ('&P3C' EQ 'YY/MM/DD' AND &PACKED).FLD800
&P3L     SETC  '251'
         AIF   ('&P3C' EQ 'DD/MM/YY' AND &PACKED).FLD800
&P3L     SETC  '252'
         AIF   ('&P3C' EQ 'MM/DD/YY' AND &PACKED).FLD800
&P3L     SETC  '253'
         AIF   ('&P3C' EQ 'YYMMDD'   AND &PACKED).FLD800
&P3L     SETC  '0'
         AIF   (T'&SYSLIST(&I,2) NE 'N').FLD310
         MNOTE 8,'EDIT PATTERN NOT ALLOWED WITH CHARACTER STRING'
.*LOOP
.FLD310  AIF   ('&P3C'(1,1) EQ 'R').FLD318       DEFAULT
         AIF   ('&P3C'(1,1) EQ 'B').FLD318       DEFAULT
         AIF   ('&P3C'(1,1) NE 'L').FLD311
&LEFT    SETB  1
         AGO   .FLD318
.FLD311  AIF   ('&P3C'(1,1) NE 'Z').FLD312
&ZERO    SETB  1
         AGO   .FLD318
.FLD312  AIF   ('&P3C'(1,1) LT '0').FLD993
&P3L     SETC  '&P3L'.'&P3C'(1,1)
.FLD318  ANOP
.*MNOTE *,'&SYSLIST(&I) P3C=/&P3C/ P3L=/&P3L/'
&P3C     SETC  '&P3C'(2,K'&P3C-1)     STRIP OFF FIRST CHARACTER
         AIF   (K'&P3C GT 0).FLD310
.*ENDLOOP
.*---------------------------------------------------------------------
.FLD800  AIF   ('&BIN&PACKED&REG' EQ '000').FLD810
         AIF   (&LEFT OR '&P3L' NE '0').FLD810
         AIF   (&HEXA).FLD810
&P3L     SETC  '7'                     DEFAULT OUTPUT LENGTH ((R3))
         AIF   (&REG).FLD810
&P3L     SETC  '3'                     DEFAULT OUTPUT LENGTH
         AIF   ('&P2C' EQ 'FL1').FLD810
&P3L     SETC  '5'                     DEFAULT OUTPUT LENGTH
         AIF   ('&P2C' EQ 'H' OR '&P2C' EQ 'FL2').FLD810
&P3L     SETC  '7'                     DEFAULT OUTPUT LENGTH
.FLD810  ANOP
&FLAG    SETA  &LAST*128+&HEXA*8+&BIN*4+&PACKED*2+&REG*1
&LEN2    SETA  &TRUNC*128+&LEFT*128+&ZERO*64+&P3L
         DC    S(&P1S,&P2L),AL1(&FLAG,&LEN2)
&BIN     SETB  0                    RESET FLAGS
&HEXA    SETB  0                    RESET FLAGS
&REG     SETB  0                    RESET FLAGS
&PACKED  SETB  0                    RESET FLAGS
&LEFT    SETB  0                    RESET FLAGS
&ZERO    SETB  0                    RESET FLAGS
&TRUNC   SETB  0                    RESET FLAGS
         AGO   .LIT99
.FLD990  MNOTE 8,'MISSING OR INCORRECT PARAMETER'
         AGO   .LIT99
.FLD993  MNOTE 8,'THIRD SUBPARAMETER IS INVALID: ''&SYSLIST(&I,3)'''
         AGO   .LIT99
.*---------------------------------------------------------------------
.*------------ LITERALS -----------------------------------------------
.*---------------------------------------------------------------------
.LIT00   AIF   (&$_FIELD EQ 0).LIT50
&N       SETA  1
.LIT10   AIF   (&N GT &$_FIELD).LIT50                LOOP
&L       SETA  &N+1000                               LOOP
         AIF   ('&SYSLIST(&I)' EQ '&$_FIELDS(&N)').LIT80 LOOP
&N       SETA  &N+1                                  LOOP
         AGO   .LIT10                                LOOP
.LIT50   ANOP
&$_FIELD SETA  &$_FIELD+1
&$_FIELDS(&$_FIELD) SETC '&SYSLIST(&I)'
&L       SETA  &$_FIELD+1000
.LIT80   ANOP
&J       SETA  X'4000'+&LAST*X'8000'
         DC    AL2($LIT&L-*,&LQ.$LIT&L,&J)
.LIT99   ANOP
.*---------------------------------------------------------------------
&I       SETA  1+&I                              LOOP
         AIF   (&I LE N'&SYSLIST).LOOP1          LOOP
&SYSLOC  LOCTR
         AGO   .MEND
.ERR1    MNOTE 12,'AT LEAST ONE INPUT FIELD MUST BE SPECIFIED'
         AGO   .MEND
.ERR2    MNOTE 12,'INVALID OUTPUT AREA SPECIFICATION'
         AGO   .MEND
.**********************************************************************
.*       FINAL_CALL: GENERATE LITERALS
.**********************************************************************
.GENL    AIF   (&$_FIELD EQ 0).GENL3
$LITERAL LOCTR
.GENL2   ANOP                                LOOP
&N       SETA  &N+1                          LOOP
&I       SETA  &N+1000                       LOOP
$LIT&I   DC    C&$_FIELDS(&N)                LOOP
         AIF   (&N LT &$_FIELD).GENL2        LOOP
.GENL3   AIF   ('&DOC' NE 'YES').NODOC                             EU-0
         AIF   ('&EUSTL' EQ 'GEN').OKDOC                           EU-0
         AIF   ('&PRINT' EQ 'GEN').OKDOC                           EU-0
         PUSH  PRINT                                               EU-0
         PRINT GEN                                                 EU-0
.OKDOC   EJECT ,                                                   EU-0
*         +----------------------------------------+
*         +                                        +
*         +   Documentation for the STRING macro   +
*         +      (Last update: 08 Dec 1992)        +
*         +       (EU update: 15 Jul 1993)         +
*         +----------------------------------------+
*
*        The STRING macro is functionally similar to the COBOL DISPLAY
*   or PL/I PUT EDIT instructions.
*
*        Using STRING, you can concatenate any number of fields, edit
*   each of them if necessary, and get the result in the work area
*   you specify.
*
*   Formats:
*
*        (1) label  STRING  field_specification1
*                           ,field_specification2,...
*                           ,INTO=workarea|(workarea,length)
*                           ,PRINT=GEN|NOGEN
*
*        (2) label  STRING  FINAL_CALL
*                           ,PRINT=GEN|NOGEN
*
*
*   field_specification
*
*        Each field to be printed is described as a positional operand.
*        Each operand specifies the field address, its length, and its
*        formatting requirements.
*
*        Four field description formats are supported:
*
*        1.   symbol
*        2.   (symbol,length,format)
*        3.   (d(r)|(r),length,format)
*        4.   ((r),,format)
*        5.   'character string'
*
*        Symbol specifies the field address. It must be an
*        S-type (relocatable) address.
*
*        d(r) may be used to specify the field address in S/370
*        base-displacement format. If d is zero, it may be omitted.
*        If d(r) or (r) is used, length must also be specified. R14
*        and R15 may not be used. If d(0) is used, it is handled the
*        way the assembler does, i.e. R0 as a base register is assumed
*        to contain zero: 16(0) is equivalent to 16, CVTPTR or, X'10'.
*
*        ((r),,format) specifies that (r) contains the value
*        itself, not an address. R14 and R15 may not be used.
         EJECT
*        'character string' specifies a literal enclosed in single
*        quotes as specified in a DC instruction.
*
*        Length specifies the length and/or the type of the input
*        field. It may be specified as an integer, a symbol, a
*        register, or a constant. When used with symbol, it overrides
*        the assembled length and/or type. Length is required if field
*        is specified as d(r) or (r).
*
*        nn   field length in bytes
*        H    half-word
*        F    full-word
*        FL1  1-byte binary integer
*        FL3  3-byte binary integer
*        P    packed field
*        (r)  length of character string (R0 thru R9)
*        d(r) length of character string (R1 thru R9)
*
*        Notes: If the field address is specified as a symbol that has
*                  been defined previously in the program, the symbol
*                  type is known and there is no need to specify it.
*               If the length is specified as (r) or d(r) and the value
*                  is greater than the address itself, (r) or d(r) is
*                  considered to be the end address +1 instead of the
*                  length.
*               The length is not specified for packed fields. The
*                  @STRING subroutine scans the field until a byte with
*                  X'0C' is found.
*
*        format  optionally indicates editing options that must
*        be applied to a field.
*
*             L        left justified
*             R        right justified
*             nn       output length
*             0        adjust length
*             Z        leading zeroes
*             B        leading/trailing blanks
*             T        truncate character string after last non-blank
*             X        display in hexadecimal
*             YYMMDD   convert julian date to YYMMDD
*             YY/MM/DD convert julian date to YY/MM/DD
*             DD/MM/YY convert julian date to DD/MM/YY
*             MM/DD/YY convert julian date to MM/DD/YY
*
*             The default format is L for character-type fields and
*             R7B for numeric fields. L0 and T are equivalent.
*
*        'character string' is any character string enclosed in
*        single quotes. Blank spaces may be specified as nnX,
*        where nn is the number of X'40' bytes you want to be
*        inserted in the output line. %TIME may be specified to
*        obtain the current time in hh.mm.ss.hh format.
         EJECT
*   INTO=workarea|(workarea,length)
*
*        INTO indicates the address and length of the output work area
*        into which the result of the concatenation should be placed
*        (left justified). If the work area is too small, truncation
*        will occur. If it is too large, the rest is padded with
*        blanks.
*
*        The address may be a symbol, d(r) (S-type address) or (r). The
*        length is required for d(r) and (r). If no length is specified
*        for a symbol-type address, it defaults to the assembled length
*        of the symbol.
*
*        Upon return from STRING, R15 contains the length actually used
*        in the output work area (before padding).
*
*   PRINT=GEN|NOGEN
*
*        This operand allows you to temporarily override the PRINT
*        specification (GEN or NOGEN).
*
*   FINAL_CALL (format 2)
*
*        The FINAL_CALL format must be specified once at the end of
*        the program. It generates the literals used in previous
*        invocations and the @STRING sub-routine.
*
*   Examples:
*
*        STRING 'ERROR===>',LINE1,'<=== POS ',((R6),,L0),INTO=WORKAREA
*
*        STRING 8X,'ERRORS FOUND: ',(ERRORS,,L0),INTO=((R7),44)
*
*        STRING 'CVT ADDR IS ',(CVTPTR,4,X),INTO=LINE
*
*        LA    R5,WORK+16              end addr +1
*        STRING 'R4=',((R4),,X),INTO=(WORK,(R5)),PRINT=GEN
*
*        STRING '//JOBLIB DD DSN=',(DSN1,,T),',DISP=SHR',INTO=((R2),72)
*
*        PUT31 SYSLIN
*        LH    R0,SYSLIN+82            LRECL
*        STRING '   NAME  ',(4(R3),8,T),'(R)',INTO=((R1),(R0))
*
*        STRING FINAL_CALL       Generate literals and sub-routine
         EJECT
*   Programming Notes:
*
*        A STRING macro generates only 6 bytes that need to be covered
*        by base registers. More code is generated at the end of the
*        current CSECT (using LOCTR pseudo instruction), but this code
*        does not require addressability. This is particularly useful
*        if STRING calls contain lots of literals.
*
*        Additionally, STRING does not use A-type constants (ACON),
*        but S-type constants (SCON) which require symbols to be
*        addressable at the point in the program where STRING is
*        issued.
*
*        While this reduces the number of base registers required
*        to cover the program's code and makes it easier to write
*        reentrant programs, it will produce assembly errors in the
*        following situations:
*
*        a. STRING is used in more than one CSECT in the same assembly
*
*        b. the CSECT in which STRING is used is longer than 64K
*
*        c. symbols are not addressable at the point in the program
*           where STRING is issued
*
*        d. the $STRING symbol (generated by the first call to the
*           STRING macro) is not addressable in subsequent STRING calls
*
*   Assembly JCL:
*        //ASMH EXEC PGM=IEV90,PARM=(OBJECT,NODECK,NOESD,NORLD,NOXREF)
         SPACE 1
*930130  Add $FARRTNE LOCTR to prevent negative offsets
*921205  Restructure module, optimize, AMODE31
*921204  Return length used in output work area in R15
*920926  @JDATE routine integrated into @STRING CSECT
         SPACE 1
* Origin :     Extracted from CBT 93 FEB, file 183.
* --------
*
* Updates :    done by MOINIL P.A.
* ---------            COMPUTING CENTRE (TP 361)
*                      J.R.C. - ISPRA ESTABLISHMENT
*                      21020 ISPRA (VA), ITALY
*   15JUL93 : EU-0 - Included GBLC &EUSTL in macro to handle external
*                    SYSPARM(FULL or STRING) Assembly list options.
*                    Added DOC=YES to allow macro documentation list.
*           : EU-1 - Added missing process to set trailing blanks (B)
*                    when left justified (L) edit option is required.
         AIF   ('&EUSTL' EQ 'GEN').NODOC                           EU-0
         AIF   ('&PRINT' EQ 'GEN').NODOC                           EU-0
         POP   PRINT                                               EU-0
.NODOC   EJECT ,                                                   EU-0
.**********************************************************************
.*
.*       STRING SUB-ROUTINE
.*
.*             CAUTION: BYTES 49-72 OF THE CALLER'S SAVE AREA
.*                      (R7-R12 SLOTS) ARE USED AS WORK SPACE
.*
.**********************************************************************
@STRING  CSECT
@STRING  RMODE ANY
.*R0     EQU   0                       WORK REGISTER
.*R1     EQU   1                       WORK REGISTER
.*R2     EQU   2                       WORK REGISTER
.*R3     EQU   3                       WORK REGISTER
.*R4     EQU   4                       WORK REGISTER
.*R5     EQU   5                       WORK REGISTER
.*R6     EQU   6                       WORK REGISTER
.*R7     EQU   7                       NOT SAVED, NOT USED
.*R8     EQU   8                       NOT SAVED, NOT USED
.*R9     EQU   9                       NOT SAVED, NOT USED
.*R10    EQU   10                      NOT SAVED, NOT USED
.*R11    EQU   11                      NOT SAVED, NOT USED
.*R12    EQU   12                      NOT SAVED, NOT USED
.*R13    EQU   13                      NOT SAVED, NOT USED
.*R14    EQU   14                      WORK REGISTER
.*R15    EQU   15                      base reg
         SAVE  (14,6),,'@STRING 921205 '
         USING @STRING,R15
         USING @STRSAVE,R13
         LR    R6,R14                  KEEP ADDRESS OF PARMLIST OFFSET
         USING @STRPARM,R6
         SLR   R0,R0
         ICM   R0,B'0011',0(R6)        PICK UP PARM LIST OFFSET
         ALR   R6,R0                   GET PARM LIST ADDRESS
         LA    R2,@STRSCON             RESOLVE FIRST SCON (ADDR)
         BAL   R14,@STRS2A             GET ADDRESS IN R2
         LR    R4,R2                   KEEP ADDRESS OF "INTO" FIELD
         OI    0(R4),0                 get clean S0C4 if address is bad
         LA    R2,@STRFLEN             RESOLVE SECOND S-CON (LEN)
         BAL   R14,@STRS2A             GET LENGTH IN R2
         LR    R5,R1                   KEEP LENGTH OF "INTO" FIELD
         CR    R5,R4                   end address?
         BL    @STR282                 NO, JUMP
         SR    R5,R4                   CALCULATE LENGTH
@STR282  ST    R4,8(,R13)              SAVE ADDRESS FOR LATER
.**********************************************************************
.*       MOVE FIELDS TO OUTPUT AREA
.**********************************************************************
         LA    R6,@STRFLAG             POINT TO 1ST FIELD DESC
.*LOOP
@STR310  CLI   @STRSCON,X'E0'          CHECK FOR %TIME
         BE    @STR388T                JUMP IF %TIME
         TM    @STRFLAG,@STRLIT        IS THIS A LITERAL?
         BO    @STR388L                yes, process it
         TM    @STRFLAG,@STRREG        CHECK CONVERSION TYPE
         BO    @STR335                 JUMP IF REGISTER
.*IT'S A FIELD (SCON)
         LA    R2,@STRFLEN             RESOLVE SECOND S-CON (LEN)
         BAL   R14,@STRS2A             GET LENGTH IN R2
         LR    R3,R2                   KEEP LENGTH
         LA    R2,@STRSCON             RESOLVE FIRST SCON (ADDR)
         BAL   R14,@STRS2A             GET ADDRESS IN R2
         CLI   0(R2),0                 get clean S0C4 if address is bad
         CR    R3,R2                   end address?
         BL    @STR312                 NO, JUMP
         LA    R0,X'0080'              PSA address              @921221
         CLR   R2,R0                   PSA reference?           @921221
         BL    @STR312                 yes, jump                @921221
         SR    R3,R2                   CALCULATE LENGTH
.*
@STR312  TM    @STRFLAG,@STRPACK       CHECK CONVERSION TYPE
         BO    @STR330                 JUMP IF PACKED FIELD
         TM    @STRFLAG,@STRHEX        CHECK CONVERSION TYPE
         BO    @STR361                 JUMP IF HEX STRING
         TM    @STRFLAG,@STRBIN        CHECK CONVERSION TYPE
         BZ    @STR370                 JUMP IF NO CONVERSION REQ'D
.*BINARY VARIABLE: R3 CONTAINS THE ICM MASK (1 3 7 F)
@STR320  SLR   R0,R0
         EX    R3,@STRICM              LOAD THE BINARY VARIABLE
         B     @STR340                 EDIT R0 VALUE
@STRICM  ICM   R0,*-*,0(R2)            LOAD A BINARY VARIABLE
.*PACKED FIELD
@STR330  LR    R3,R2                   FIRST BYTE OF PACKED FIELD
         BALR  R14,0
         TM    0(R3),X'0C'             IS THIS THE SIGN BYTE?
         LA    R3,1(,R3)                (NEXT BYTE)
         BNOR  R14                     NO, LOOP MORE
         SLR   R3,R2                   GET LENGTH OF PACKED FIELD
         BCTR  R3,0
         EX    R3,@STRZAP              EXECUTE ZAP
         CLI   @STRLEN2,250            JULIAN-TO-YYMMDD CONV?
         BL    @STR341                 no, edit DWD
         B     @STR380                 yes, convert it
.*REGISTER (R0-R13)
@STR335  LH    R1,@STRSCON             REG NUMBER IN BITS 28-31
         EX    R1,@STR335S             copy R7-R13 INTO r0
         CLI   @STRSCON+1,R7           IS THIS R7-R13?
         BNL   @STR336                 yes, jump
         SLL   R1,2                    R1= 000000BB BASE * 4
         L     R0,20(R1,R13)           PICK UP VALUE FOR R0-R6
@STR336  TM    @STRFLAG,@STRHEX        CHECK CONVERSION TYPE
         BO    @STR360                 JUMP IF HEX STRING
.*EDIT @STRDWD
@STR340  CVD   R0,@STRDWD              CONVERT VALUE TO DECIMAL
@STR341  IC    R0,@STRLEN2             OUTPUT LENGTH
         LA    R3,X'003F'              MASK FOR "AND"
         NR    R3,R0                   OUTPUT LENGTH
         MVC   @STRWKE(L'@STRWKE),@STRMASK EDIT MASK
         TM    @STRLEN2,@STRLEFT       CHECK JUSTIFICATION
         BO    @STR350                 JUMP IF LEFT JUSTIFICATION
         TM    @STRLEN2,@STRZERO       leading zeroes req'd?
         BNO   @STR344                 no, jump
         MVI   @STRWKE,C'0'            yes, change X'40' to C'0'
@STR344  ED    @STRWKE,@STRDWD         ZONED DECIMAL
         LA    R2,@STRWKE+L'@STRWKE    FIRST POSITION AFTER STRING
         SR    R2,R3                   FIRST STRING POSITION
         B     @STR390                 MOVE STRING TO OUTPUT LINE
.*L
@STR350  LA    R1,@STRWKE+L'@STRWKE-1  PREVENT BAD R1
         EDMK  @STRWKE,@STRDWD         ZONED DECIMAL
         LR    R2,R1                   FIRST STRING POSITION
         LTR   R3,R3                   CHECK OUTPUT LENGTH
         BNZ   @STR390                 JUMP IF NOT ZERO
         LA    R3,@STRWKE+L'@STRWKE    FIRST POSITION AFTER STRING
         SR    R3,R2                   COMPUTE STRING LENGTH
         B     @STR390                 MOVE FIELD TO OUTPUT LINE
.*HEX STRING
@STR360  STCM  R0,B'1111',@STRDWD-1    STORE IT FOR CVD/UNPK
         LA    R3,8                    output LENGTH
         B     @STR363
@STR361  LTR   R3,R3                   ZERO LENGTH?
         BZ    @STR398                 YES, IGNORE IT
         LA    R0,8                    MAX LENGTH
         CLR   R3,R0                   CHECK LENGTH
         BNH   @STR362                 JUMP IF LE 8
         LR    R3,R0                   TRUNCATE TO MAXIMUM LENGTH
@STR362  LR    R1,R3                   input length
         BCTR  R1,0
         EX    R1,@STR360M             MOVE DATA TO SAFE STORAGE
         ALR   R3,R3                   output length
@STR363  LA    R2,@STRWKE              work area
         UNPK  0(9,R2),@STRDWD-1(5)    EXPAND SOURCE BYTES FOR "TR"
         UNPK  8(9,R2),@STRDWD+3(5)    EXPAND SOURCE BYTES FOR "TR"
         TR    0(16,R2),@STRHEXT-240   =C'0123456789ABCDEF'
.*TRUNCATE CHARACTER STRING
@STR370  TM    @STRLEN2,@STRLEFT       CHECK JUSTIFICATION, OUTPUT LEN
         BZ    @STR390                 NO STRING TRUNCATION, JUMP
         LA    R1,0(R3,R2)             FIRST BYTE AFTER FIELD
@STR372  BCTR  R1,0                    DOWN 1 BYTE                 LOOP
         CLI   0(R1),C' '              IS IT A SPACE ?             LOOP
         BNE   @STR390                 LAST NON-BLANK BYTE         LOOP
         BCT   R3,@STR372              LOOP UNTIL 1ST NON-BLANK    LOOP
         B     @STR398                 BLANK FIELD, DO NOT EDIT
@STR335S LR    R0,*-*                  COPY VALUE FOR EDIT
@STRZAP  ZAP   @STRDWD,0(*-*,R2)       MOVE TO @STRDWD
@STR360M MVC   @STRDWD-1(*-*),0(R2)    PREVENT S0C4 IN UNPK
@STRHEXT DC    C'0123456789ABCDEF'
@STRMASK DC    X'40202020202020202020202020202120'
.*CONVERT JULIAN DATE TO YYMMDD
@STR380  ZAP   @STRDWD,@STRDWD         DATE=0000000?               @JDT
         BZ    @STR380Z                YES, JUMP                   @JDT
         MVO   @STRDWD+0(2),@STRDWD+4+1(1)           0YY00000,00YY0DDC
         MVI   @STRDWD+2,X'0C'                       0YY00C00,00YY0DDC
         BAL   R1,@STR380T+2*13        TABLE OF MONTHS (NUMBER OF DAYS)
@STR380T DC    P'59,31,29,31,30,31,30,31,31,30,31,30,31'
         TM    @STRDWD+4+1,X'01'       ODD YEARS
         BO    @STR380N                  AREN'T LEAP YEARS
         TM    @STRDWD+4+1,X'12'       ZEROES IN 1980, ALL ONES IN 1992
         BNM   @STR380L                MIXED IN 1982/1990
.*IF IT'S NOT A LEAP YEAR AND DDD>59, THEN ADD 1 TO DDD
@STR380N CP    @STRDWD+4+2(2),@STR380T ARE WE PAST FEB 28 (DDD>59) ?
         BNH   @STR380L                NO, JUMP
         AP    @STRDWD+4+2(2),@STR380T+3(1) ADD 1 (FROM 31) TO DDD
.*loop while DDD > 0
@STR380L AP    @STRDWD+1(2),@STR380T+3(1)   ADD 1 (FROM 31) TO MONTH
         LA    R1,2(,R1)               NEXT ENTRY IN "MONTHS" TABLE
         SP    @STRDWD+4+2(2),0(2,R1)  SUB DAYS-IN-MONTH FROM DDD
         BP    @STR380L
.*endloop
         AP    @STRDWD+4+2(2),0(2,R1)  UNDO LAST "SP" INSTRUCTION
         UNPK  @STRDWD+0(4),@STRDWD+0(3) FYFYFMCM,00YY0DDC
         UNPK  @STRDWD+4(2),@STRDWD+6(2) FYFYFMCM,FDCD0DDC
         MVZ   @STRDWD+3(3),@STRDWD+2    FYFYFMFM,FDFD0DDC
         MVI   @STRDWD+6,C'/'          SEPARATOR
         LA    R0,250                  MASK FOR 'NR'
         SLR   R1,R1
         IC    R1,@STRLEN2             250-255
         SLR   R1,R0                   000-005
         LA    R0,1+8                  L'@STR380W
         MR    R0,R0                   COMPUTE OFFSET
         LA    R1,@STR380W(R1)         entry in "TR" MASK TABLE
         LA    R2,@STRWKE              WORK AREA
         SLR   R3,R3
         IC    R3,0(,R1)               LENGTH OF DATE (6 OR 8)
         MVC   0(8,R2),1(R1)           MOVE CORRESPONDING MASK
         TR    0(8,R2),@STRDWD         CONVERT DATE TO THE RIGHT FORMAT
         B     @STR390                 MOVE FIELD TO OUTPUT LINE
@STR380W DC    AL1(8,0,1,6,2,3,6,4,5)  YY/MM/DD   250
         DC    AL1(8,4,5,6,2,3,6,0,1)  DD/MM/YY   251
         DC    AL1(8,2,3,6,4,5,6,0,1)  MM/DD/YY   252
         DC    AL1(6,0,1,2,3,4,5,6,6)  YYMMDD//   253
.*       DC    AL1(6,4,5,2,3,0,1,6,6)  DDMMYY//   254
.*       DC    AL1(6,2,3,4,5,0,1,6,6)  MMDDYY//   255
@STR380Z BAL   R2,*+4+8                WORK AREA
         DC    8C' '                   RETURN SPACES
         LA    R3,008                  LENGTH OF 'YY/MM/DD'
         CLI   @STRLEN2,253            YY/MM/DD OK?
         BL    @STR390                 YES, JUMP
         LA    R3,006                  LENGTH OF 'YYMMDD'
         B     @STR390
.*%TIME
@STR388T TIME  DEC                     GET HHMMSSHH
         L     R15,16(,R13)            RESTORE BASE REG
         ST    R0,@STRDWD              STORE HHMMSSHH
         MVC   @STRWKE(L'@STRTIME-1),@STRTIME MOVE EDIT MASK
         ED    @STRWKE(L'@STRTIME-1),@STRDWD EDIT HH:MM:SS:HH
         LA    R2,@STRWKE+1            WORK AREA
         LA    R3,12                   HH:MM:SS:HH+ SPACE
         B     @STR390
@STRTIME DC    X'4021207A20207A20207A20204000'    0X.XX.XX.XX
.*literal (@STRSCON is a 16-bit offset)
@STR388L SLR   R2,R2
         ICM   R2,B'0011',@STRSCON     LOAD LITERAL OFFSET
         LA    R2,@STRSCON(R2)         CONVERT OFFSET TO FULL ADDRESS
         LH    R3,@STRFLEN             GET LITERAL LENGTH
.*MOVE
@STR390  CR    R5,R3                   COMPARE REM. LEN TO CURR LEN
         BNL   @STR391                 JUMP IF HIGH OR EQUAL
         LR    R3,R5                   TRUNCATE TO REMAINING LENGTH
@STR391  SR    R5,R3                   COMPUTE REMAINING LENGTH
         LR    R0,R4                   POINTER IN OUTPUT LINE
         LR    R1,R3                   PASS REMAINING LENGTH
         ICM   R3,B'1000',@STRPAD      PAD WITH BLANKS
         TM    @STRLEN2,@STRLEFT       CHECK LEFT JUSTIFICATION    EU-1
         BZ    *+L'*+10                NO, JUMP                    EU-1
         STCM  R3,B'1000',@STRDWD      SET TRAILING BLANKS FOR     EU-1
         MVC   @STRDWD+1(L'@STRDWD-1),@STRDWD NEXT MOVE            EU-1
         MVCL  R0,R2                   MOVE FIELD TO OUTPUT LINE
         LR    R4,R0                   NEW POINTER IN OUTPUT LINE
@STR398  TM    @STRFLAG,@STRLAST       TEST LAST-ENTRY INDICATOR
         LA    R6,@STRNEXT             BUMP UP TO NEXT ENTRY
         BNO   @STR310                 PROCESS NEXT ENTRY
.*ENDLOOP
.*END-OF-LINE PROCESSING
         LA    R14,2                   increment
         AL    R14,12(,R13)            RETURN ADDRESS
         ICM   R1,B'1111',@STRPAD      SET UP R1 FOR PADDING
         LR    R15,R4                  CURRENT POINTER IN OUTPUT FIELD
         SL    R15,8(,R13)             CALCULATE LENGTH USED
         MVCL  R4,R0                   PAD WITH BLANKS
         LM    R0,R6,20(R13)           RESTORE WORK REGISTERS
         STM   R7,R12,48(R13)          MAKE SAVE AREA LOOK NORMAL
         OI    15(R13),1               SIMULATE "T" OPTION OF RETURN
         BSM   0,R14                   RETURN TO CALLER
.**********************************************************************
.*       convert S-CON to 31-bit address
.**********************************************************************
@STRS2A  SLR   R0,R0
         ICM   R0,B'0011',0(R2)        R0 = 0000BDDD
         SRDL  R0,12                   R0 = 0000000B, R1= DDD.....
         SRL   R1,20                   R1 = 00000DDD  (DISPLACEMENT)
         CLI   0(R2),R7*16             R7-R13?
         BL    @STRS2A5                NO, JUMP
.*BASE REG IS R7-R13
         LR    R2,R0                   R2= 0000000B (BASE REG NUMBER)
         EX    R2,@STRS2A4             ADD BASE REG VALUE TO DISPL
         B     @STRS2A7                clean up address
@STRS2A4 LA    R2,0(*-*,R1)            ADD BASE REG VALUE TO DISPL
.*BASE REG IS R0-R6
@STRS2A5 LTR   R2,R0                   is R0 the base reg?
         BNZ   @STRS2A6                no, jump
         LTR   R2,R1                   IS THIS A PSA ADDRESS?
         BNZR  R14                     yes, goback
@STRS2A6 SLL   R2,2                    R2= 000000BB BASE * 4
         L     R2,20(R2,R13)           PICK UP BASE REG VALUE
         LA    R2,0(R2,R1)             ADD BASE REG VALUE TO DISPL
.*clean up R1 based on caller's amode
@STRS2A7 TM    12(R13),X'80'           caller's in amode31?
         BOR   R14                     yes, goback
         ICM   R2,B'1000',@STRPAD+1    no, zero hi-order byte
         BR    R14
@STRPAD  DC    X'40,000000'            PADDING
.**********************************************************************
@STRSAVE DSECT                         24-BYTE WORK AREA
         DS    A(0,@STRSAVE,@STRSAVE,14,15,0,1,2,3,4,5,6)
@STRWKE  DS    0XL16,F'7,8,9,10'       WORK AREA
@STRDWD  DS    0XL8,D'1112'            WORK AREA
@STRPARM DSECT
@STRSCON DS    S                   +0  FIELD ADDRESS
@STRFLEN DS    S                   +2  FIELD LENGTH
@STRFLAG DS    B                   +4  FORMAT, FLAGS
@STRLAST EQU   X'80'                     LAST ENTRY
@STRLIT  EQU   X'40'                     LITERAL, @STRSCON IS AN OFFSET
@STRJDT  EQU   X'20'                     JULIAN TO YYMMDD
.*             X'3F'                   CONVERSION REQUIRED
@STRHEX  EQU   X'08'                     HEXADECIMAL
@STRBIN  EQU   X'04'                     BINARY
@STRPACK EQU   X'02'                     PACKED
@STRREG  EQU   X'01'                     REGISTER
@STRLEN2 DS    B                   +5  FORMAT, OUTPUT LENGTH
@STRLEFT EQU   X'80'                     LEFT JUSTIFICATION
@STRZERO EQU   X'40'                     LEADING ZEROES
.*             X'3F'                     OUTPUT LENGTH, 0 MEANS TRUNC.
@STRNEXT EQU   *                   +6
$LTORG   LOCTR
         AIF   (D'BLANKS).MEND
BLANKS   DC    &$_MAXBL.C' '       A BUNCH OF BLANKS
.MEND    AIF   ('&EUSTL' EQ 'GEN').DGENL                           EU-0
         AIF   ('&PRINT' EQ 'GEN').DGENL                           EU-0
         AGO   .MEND99                                             EU-0
.DGENL   POP   PRINT                                               EU-0
.MEND99  MEND
