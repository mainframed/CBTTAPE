VSM7     TITLE 'VSM77000 - HARDCOPY MODULE.'
         PRINT OFF
         COPY  VSMMAC
         PRINT ON
         SPACE 1
***********************************************************************
*                                                                     *
*    MODULE :           VSM77000                                      *
*                       VERSION : 1.0                                 *
*                       DATE : 12 NOV 1986                            *
*                       NAME : HARDCOPY MODULE                        *
*                       ATTRIBUTES : REUS                             *
*                                                                     *
*    AUTHOR :           MOINIL P.A. - COMPUTING CENTRE                *
*                       J.R.C. - ISPRA ESTABLISHMENT                  *
*                       21020 ISPRA (VA), ITALY                       *
*                                                                     *
*    FUNCTION :         THIS MODULE TAKES HARDCOPIES ALL SCREEN       *
*                       DISPLAYS OF VSM. THE SCREEN DISPLAYS          *
*                       ADDRESSES ARE PASSED TO VSM77000 VIA THE      *
*                       LINKAGE AREA.                                 *
*                                                                     *
*    NOTE :             SYSOUT DYNAMIC ALLOCATION IS USED.            *
*                                                                     *
*    REGS AT ENTRY :    R0   - IF ZERO = HARDCOPY REQUEST             *
*                              IF POSITIVE = OPEN REQUEST             *
*                              IF NEGATIVE = CLOSE REQUEST            *
*                       R1   - INTER MODULE LINKAGE                   *
*                       R14  - RETURN ADDRESS                         *
*                       R15  - EPA                                    *
*                                                                     *
*    REGS USAGE :       R0   - WORK REGSITER                          *
*                       R1   - POINTER TO INTER MODULE LINKAGE        *
*                       R2-R5 - WORK REGISTERS                        *
*                       R10  - LINKAGE AREA POINTER                   *
*                       R11  - BASE REGISTER                          *
*                       R12  - 2ND BASE REGISTER (RESERVED)           *
*                       R13  - SAVE AREA POINTER                      *
*                                                                     *
*    REGS AT EXIT :     R14  - RETURN ADDRESS                         *
*                                                                     *
*    LABELS :           SOME OF THE FIELD USAGE IN THIS MODULE CAN    *
*                       BE DETERMINED BY THE FIRST CHARACTER OF THE   *
*                       FIELD. THE KEYS ARE :                         *
*                            @ - FIELD IS IN INTER MODULE LINKAGE     *
*                                AREA                                 *
*                            # - FIELD IS PART OF THE OUTPUT SCREEN   *
*                            $ - DSECT                                *
*                                                                     *
***********************************************************************
         EJECT
VSM77000 START 0
         SPACE 1
        $DEFREG
         EJECT
***********************************************************************
*        STANDARD OS LINKAGE                                          *
***********************************************************************
         SPACE 1
        $XENT  BASE=R11            SAVE CALLER'S REGS
         LR    R10,R1              POINT TO LINKAGE
         USING $LNKSECT,R10        DSECT ADDRESSABILITY
         TM    @SWITCH,@HFAIL      HARDCOPY OPEN HAS FAILED ?
         BO    RETURN              YES, BYPASS ALL
         LTR   R0,R0               TEST HARCOPY REQUEST
         BP    HOPEN               BRANCH IF OPEN REQUESTED
         BM    HCLOSE              BRANCH IF CLOSE REQUESTED
         EJECT
***********************************************************************
*        HARDCOPY A SCREEN DISPLAY, FORMAT DATE/TIME HEADING          *
***********************************************************************
         SPACE 1
         TM    @SWITCH,@HCOPY      IS HARDCOPY ALREADY OPENED ?
         BZ    RETURN              NO, BYPASS ALL
        TIME   DEC
         ST    R1,BINDATE          SAVE DATE FOR LATER
         STCM  R0,B'1111',FIVEBYTE CONVERT PACKED TO DECIMAL
         MVI   FIVEBYTE+4,X'0F'
         UNPK  TENBYTE,FIVEBYTE
         MVC   HTMEHRS,TENBYTE+1   MOVE TIME TO HEADING
         MVC   HTMMINS,TENBYTE+3
         MVC   HTMSECS,TENBYTE+5
         MVC   FIVEBYTE(4),BINDATE MOVE DATE TO WORK AREA
         MVO   FIVEBYTE+1(3),FIVEBYTE(2)  MAKE 00YYDDDS INTO 00000YYS
         UNPK  HCYR(2),FIVEBYTE+2(2)    FORMAT YEAR
         XC    DBLEWORD,DBLEWORD
         MVC   DBLEWORD+4(4),FIVEBYTE   CONVERT YEAR TO BINARY
         CVB   R0,DBLEWORD
         ST    R0,BINYEAR          AND SAVE
         TM    BINYEAR+3,X'03'     LEAP YEAR ?
         BNZ   MON1                NO
         MVI   HMONMSK+10,29       YES, CORRECT
MON1     XC    DBLEWORD,DBLEWORD   CONVERT DAYS TO BINARY
         MVC   DBLEWORD+6(2),BINDATE+2
         CVB   R2,DBLEWORD
         ST    R2,BINDAYS          SAVE FOR LATER
         XR    R1,R1
         LA    R3,11
MON2     IC    R1,HMONMSK(R3)      COMPUTE MON AND DAY
         SR    R2,R1
         BNP   MONOVR
         BCT   R3,MON2
         B     MONOVR+L'MONOVR
MONOVR   AR    R2,R1               CORRECT OVERDRAW
         CVD   R2,DBLEWORD         FORMAT DAY
         UNPK  HCDAYN(2),DBLEWORD+6(2)
         OI    HCDAYN+1,C'0'       FIX UP SIGN
         MH    R3,=Y(L'HMONTAB)    GET TABLE OFFSET
         LA    R3,HMONTAB(R3)      AND POINT AT MONTH
         MVC   HCMON(L'HCMON),0(R3)     MOVE IT TO HEADING
         L     R3,BINYEAR          DAY OF WEEK
         SH    R3,=H'69'
         BNP   OUTT                MUST BE GREATER OR BAD
         XR    R2,R2               CLEAR DAY REG
         D     R2,=F'4'
         MH    R3,=H'5'
         AR    R2,R3
         A     R2,BINDAYS
         SRDA  R2,32
         D     R2,=F'7'            MODULO 7 FOR WEEK
         MH    R2,=Y(L'HDAYTAB)
         LA    R2,HDAYTAB(R2)      POINT AT TODAY
         MVC   HCDAY(L'HCDAY),0(R2)     INSERT IN HEADING
         B     MOVEJD
OUTT     MVC   HCDAY(L'HCDAY),=CL3'???'
MOVEJD   MVC   HJDATE,=XL7'4020204B202020'
         ED    HJDATE,BINDATE+1
         MVI   HJDATE,C'='
         L     R1,@TIOT
         USING TIODSECT,R1
         MVC   HUSER,TIOCNJOB      MOVE IN USERID
         DROP  R1
         MVI   HPAGS,C' '
         MVC   HPAGE(L'HPAGE),=CL8' '   CLEAR OUT PAGE FIELD
         MVI   HHEAD1,C'0'         SET DOUBLE SPACE
         EJECT
***********************************************************************
*        PUT OUT FORMAT DATE/TIME HEADING                             *
***********************************************************************
         SPACE 1
         CLI   @RSPONSE,C'R'       IS IT A RESHOW ?
         BE    CNTNUE              YES
         CLC   @RSPONSE,PREVIOUS   WAS THE SAME AS PREVIOUS ?
         BE    CNTNUE              YES
         MVC   PREVIOUS,@RSPONSE   NO,... NEW PAGE
         B     SKIPPG
CNTNUE   CP    HLINE#,=PL2'+35'    EJECT TO BE SCHEDULED ?
         BNH   NOPAGE              NO,... SKIP PAGE SETTING
SKIPPG   AP    HPAGE#,=PL1'+1'     BUMP PAGE COUNT
         MVI   HPAGS,C'/'
         MVC   HPAGE,=XL6'402020202021' MOVE IN MASK
         ZAP   DBLEWORD,HPAGE#     MOVE INTO AREA
         ED    HPAGE,DBLEWORD+5    EDIT IN PAGE NUMBER
         MVI   HHEAD1,C'1'         SET EJECT
         ZAP   HLINE#,=PL1'+0'     RESET LINES COUNT
NOPAGE  $TSWXA 24,EXPAND=ONLY      BACK TO 24-BIT MODE FOR I/O'S
        PUT    CPYDCB,HHEAD1       PUT OUT TITLE LINE
        PUT    CPYDCB,HHEAD2       PUT OUT UNDERLINE
         AP    HLINE#,=PL1'+3'     BUMP LINES COUNT
         MVI   HLINE,C' '
         MVC   HLINE+1(L'HLINE-1),HLINE
        PUT    CPYDCB,HDETAIL      PRINT A BLANK LINE
         AP    HLINE#,=PL1'+1'     BUMP LINES COUNT
         EJECT
***********************************************************************
*        PUT OUT ENTIRE SCREEN OF DETAIL LINES                        *
***********************************************************************
         SPACE 1
         L     R2,@PUTAREA         SET OUTPUT SCREEN ADDRESS
         LA    R3,HLINE            SET TARGET LINE ADDRESS
         L     R4,@HCPY            GET HARDCOPY SCREEN ADDRESS
         XR    R5,R5               INCREMENT REGISTER
SCLOOP   BAS   R14,0(R4,R5)        BUILD A LINE
        PUT    CPYDCB,HDETAIL      PUT DETAIL LINE OUT
         AP    HLINE#,=PL1'+1'     BUMP LINES COUNT
         CH    R5,=Y(23*4)         LAST LINE DONE ?
         BNL   *+L'*+18            YES
         LA    R5,4(R5)            GO GET NEXT
         MVI   HLINE,C' '
         MVC   HLINE+1(L'HLINE-1),HLINE
         B     SCLOOP
         SPACE 1
        $TSWXA 31,EXPAND=ONLY      RESTORE 31-BIT MODE
RETURN  $XRET  CC=0                B O O G I E
         EJECT
***********************************************************************
*        CHANGE HARDCOPY DEFAULT IF SPECIFIED                         *
***********************************************************************
         SPACE 1
HOPEN    TM    @SWITCH,@HCOPY      IS HARDCOPY ALREADY OPENED ?
         BO    ERRAO               YES
         TM    @SWITCH,@BATCH      BATCH RUNNING ?
         BO    BATCH01             YES
         LA    R1,P99TUKY3
         ST    R1,P99TUPL+8
         MVC   P99DDN,=CL8' '
         MVC   P99DEST,=CL8' '
         XC    P99DESTL,P99DESTL
RESHOW   LA    R0,#SCRNLEN         LOAD SCREEN LENGTH INTO R0
         LA    R1,#SCREEN          POINT TO SCREEN
         XR    R2,R2
         ICM   R2,B'1000',=B'00100011'  BIT SETTINGS FOR PUT TO R2
         OR    R1,R2               PUT FLAGS IN R1 HI-ORDER BYTE
        TPUT   (1),(0),R           PUT SCREEN
         MVI   #MSG,C' '           CLEAR ANY ERROR MSG
         MVC   #MSG+1(L'#MSG-1),#MSG
         MVC   #ARROW(L'#ARROW),#MSG
         XC    GETAREA(GETLEN),GETAREA  CLEAR INPUT BUFFER
         LA    R0,GETLEN           INPUT BUFFER LENGTH IN R0
         LA    R1,GETAREA          INPUT BUFFER ADDRESS IN R1
         XR    R2,R2
         ICM   R2,B'1000',=B'10100001'  BIT SETTINGS FOR GET TO R2
         OR    R1,R2               PUT FLAGS IN R1 HI-ORDER BYTE
        TGET   (1),(0),R           READ SCREEN - WAIT FOR RESPONSE
         CLI   PFKCODE,X'7D'       SPECIFIC REQUEST ?
         BE    ENTER               YES
         CLI   PFKCODE,X'F3'       PF 3 ENTERED ?
         BE    BACK                YES
         CLI   PFKCODE,X'C3'       PF 15 ENTERED (ALT. PF 3) ?
         BE    BACK                YES
         MVC   #MSG(30),=CL30'INVALID, PLEASE USE ENTER ONLY'
SETARR   MVC   #ARROW,=CL4'===>'
         B     RESHOW
ENTER    OI    RSPCLASS,C' '       UPPERCASE DATA
         OC    RSPDEST,=CL8' '
         CLI   RSPCLASS,C'A'       IS IT ALPHA ?
         BL    ERRCL               NO, SYSOUT CLASS ERROR
         CLC   RSPDEST,=CL8'LOCAL' ANY DESTINATION SPECIFIED ?
         BE    NODEST
         CLC   RSPDEST,=CL8' '
         BE    NODEST
         LA    R0,L'RSPDEST        SCAN SPECIFIED DESTINATION
         LA    R1,RSPDEST+L'RSPDEST-1
SCDEST   CLI   0(R1),C' '
         BNE   CSDEST
         BCTR  R1,0
         BCT   R0,SCDEST
ERRDS    MVC   #MSG(39),=CL39'DESTINATION (        ) ERROR, RESPECIFY'
         MVC   #MSG+13(L'RSPDEST),RSPDEST
         B     SETARR
ERRCL    MVC   #MSG(33),=CL33'SYSOUT CLASS ( ) ERROR, RESPECIFY'
         MVC   #MSG+14(L'RSPCLASS),RSPCLASS
         B     SETARR
CSDEST   LR    R2,R0
         BCTR  R1,0
         BCT   R0,*+L'*+4
         B     OKDEST
         CLI   0(R1),C' '
         BE    ERRDS
         B     CSDEST+L'CSDEST
OKDEST   MVC   P99DEST,RSPDEST     MOVE DESTINATION
         STCM  R2,B'0011',P99DESTL MOVE LENGTH
         B     DYALL
BATCH01  MVI   #MSG,C' '           CLEAR ANY ERROR MSG
         MVC   #MSG+1(L'#MSG-1),#MSG
         LA    R2,CPYDCB           ADDRESS OF OUTPUT DCB
         USING IHADCB,R2           ADDRESSABILITY TO OUTPUT DCB
         L     R3,@TIOT
         USING TIODSECT,R3
         XR    R1,R1
HCDDS    CLC   TIOELNGH(4),=F'0'
         BE    HCDDN
         CLC   TIOEDDNM(L'TIOEDDNM),DCBDDNAM
         BE    HCDDF
         IC    R1,TIOELNGH
         ALR   R3,R1
         B     HCDDS
         DROP  R2,R3               KILL DCB ADDRESSABILITY
HCDDN    MVC   RSPCLASS,@HCLASS    SET SYSOUT CLASS
         MVC   P99DDN,=CL8' '
NODEST   MVC   P99TUPL+8(4),P99TUPL+12  OVERRIDE DEST= PARM
DYALL    MVC   P99SYSOC,RSPCLASS   MOVE SYSOUT CLASS
         EJECT
***********************************************************************
*        ALLOCATE/OPEN AND START HARDCOPY PROCESS                     *
***********************************************************************
         SPACE 1
         XC    P99ERROR,P99ERROR
         XC    P99INFO,P99INFO
         LA    1,P99RBPTR          ADDR OF PARM LIST FOR DYNALLOC.
        DYNALLOC ,
         LTR   R15,R15             CHECK RETURN CODE ?
         BNZ   ERRAL               ERROR, CAN DO POST MESSAGE
         LA    R2,CPYDCB           ADDRESS OF OUTPUT DCB
         USING IHADCB,R2           ADDRESSABILITY TO OUTPUT DCB
         MVC   DCBDDNAM,=CL8' '
         ICM   R1,B'0011',P99DDNL
         BCT   R1,*+L'*+6
         MVC   DCBDDNAM(*-*),P99DDN     <<EXECUTED>>
         EX    R1,*-6              SET DDNAME
HCDDF   OPEN   (CPYDCB,OUTPUT)     OPEN THE FILE
         TM    DCBOFLGS,DCBOFOPN   CHECK FOR SUCCESSFUL OPEN
         BZ    ERROP               BYPASS IF BAD OPEN
         OI    @SWITCH,@HCOPY      SET HARDCOPY FILE AVAILABLE
         ZAP   HPAGE#,=PL1'+0'     RESET PAGE NUMBER
         ZAP   HLINE#,=PL2'+999'   RESET LINES COUNT
         MVC   @MSG(L'MSSTART),MSSTART  MOVE START MSG
         CLC   DCBDDNAM,=CL8'VSMHCOPY'
         BNE   *+L'*+10
         MVC   @MSG+MSJCL(L'MSJCL),=CL20'PRE-ALLOCATED BY JCL'
         B     BACK
         DROP  R2                  KILL DCB ADDRESSABILITY
         MVC   @MSG+MSCLASS(L'MSCLASS),P99SYSOC   SYSOUT CLASS
         MVC   @MSG+MSDEST(L'MSDEST),P99DEST
         CLI   @MSG+MSDEST,C' '    ANY DESTINATION ?
         BNE   *+L'*+6             YES
         MVC   @MSG+MSDEST(L'MSDEST),=CL8'LOCAL'  SAY LOCAL
         B     BACK
ERROP    NI    @SWITCH,255-@HCOPY  TURN OFF HARDCOPY FLAG
         OI    @SWITCH,@HFAIL      SET HARDCOPY OPEN FAILED FLAG
         MVC   @MSG(31),=CL31'SORRY, ... HARDCOPY OPEN FAILED'
         B     BACK
ERRAO    MVC   @MSG(34),=CL34'HARDCOPY IS STILL ALREADY OPENED |'
         B     BACK
         EJECT
***********************************************************************
*        FORMULATE TEXT FOR SVC99 ALLOCATE FAILURE                    *
***********************************************************************
         SPACE 1
ERRAL    TM    @SWITCH,@BATCH      BATCH RUNNING ?
         BO    *+L'*+10            YES
         CLC   P99ERROR,=XL2'046C' WAS IT 'RMT NOT DEF TO JES2' ?
         BE    BADRMT              YES, POST MSG AND EXIT
         MVC   #MSG(L'MSERR),MSERR
         CVD   R15,DBLEWORD        CONVERT SVC 99 RETURN CODE
         MVC   #MSG+MSRC(L'MSRC),=XL4'40202020'   TO DECIMAL
         ED    #MSG+MSRC(L'MSRC),DBLEWORD+6
         UNPK  #MSG+MSERC(L'MSERC+1),P99ERROR(3)  ERROR CODE
         NC    #MSG+MSERC(L'MSERC),=XL4'0F0F0F0F' TO HEX.
         TR    #MSG+MSERC(L'MSERC),=CL16'0123456789ABCDEF'
         MVI   #MSG+MSERC+L'MSERC,C' '
         UNPK  #MSG+MSINFO(L'MSINFO+1),P99INFO(3) INFO CODE
         NC    #MSG+MSINFO(L'MSINFO),=XL4'0F0F0F0F'    TO HEX.
         TR    #MSG+MSINFO(L'MSINFO),=CL16'0123456789ABCDEF'
         MVI   #MSG+MSINFO+L'MSINFO,C' '
         TM    @SWITCH,@BATCH      BATCH RUNNING ?
         BZ    SETARR              NO, GO SEND THE MESSAGE
         MVC   @MSG,#MSG           YES, PASS THE MESSAGE
         B     BACK
BADRMT   MVC   #MSG(L'MSNRMT),MSNRMT    MOVE IN NO SUCH REMOTE MSG
         MVC   #MSG+MSRMTN(L'MSRMTN),P99DEST RMT ASKED FOR
         B     SETARR              GO SEND THE MESSAGE
         EJECT
***********************************************************************
*        STOP HARDCOPY PROCESS AND FREE SYSOUT FILE                   *
***********************************************************************
         SPACE 1
HCLOSE   TM    @SWITCH,@HCOPY      IS HARDCOPY ACTIVE?
         BZ    ERRCH               NO
        CLOSE  (CPYDCB)            CLOSE THE FILE
        FREEPOOL CPYDCB            FREE THE BUFFERS TOO
         NI    @SWITCH,255-@HCOPY  TURN OFF HARDCOPY FLAG
         LA    R2,CPYDCB           ADDRESS OF OUTPUT DCB
         USING IHADCB,R2           ADDRESSABILITY TO OUTPUT DCB
         CLC   DCBDDNAM,=CL8'VSMHCOPY'
         BNE   *+L'*+10
         MVC   @MSG(36),=CL36'HARDCOPY STOPPED : JCL PRE-ALLOCATED'
         B     BACK
         DROP  R2                  KILL DCB ADDRESSABILITY
         MVC   @MSG(36),=CL36'HARDCOPY STOPPED : SYSOUT NOW FREE''D'
BACK     MVI   @RSPONSE,C'M'       SHOW PRIMARY MENU
         B     RETURN
ERRCH    MVC   @MSG(30),=CL30'HARDCOPY IS ALREADY INACTIVE |'
         B     BACK
         EJECT
DBLEWORD DC    D'0'
TENBYTE  DC    XL10'0'
FIVEBYTE DC    XL5'0'
PREVIOUS DC    CL1' '
         SPACE 1
         PRINT NOGEN
CPYDCB   DCB   DDNAME=VSMHCOPY,DSORG=PS,MACRF=(PM),                    1
               RECFM=FA,LRECL=91,BLKSIZE=91
         PRINT GEN
         SPACE 1
BINYEAR  DC    F'0'
BINDAYS  DC    F'0'
BINDATE  DC    F'0'
HPAGE#   DC    PL3'+1'             PAGE NUMBER
HLINE#   DC    PL3'+0'             LINES COUNT
GETAREA  EQU   *
PFKCODE  DC    XL6'0'
RSPCLASS DC    XL1'0'
         DC    XL3'0'
RSPDEST  DC    XL8'0'
GETLEN   EQU   *-GETAREA
         SPACE 1
P99RBPTR DC    A(X'80000000'+P99RB)     SVC 99 REQUEST BLOCK PTR
P99RB    DS    0F                  SVC 99 REQUEST BLOCK
         DC    AL1(P99RBLN)             RB LENGTH IN BYTES
P99VERB  DC    AL1(S99VRBAL)            VERB CODE (DSNAME ALL.)
P99FLAG1 DC    AL1(S99NOCNV+S99NOMNT,0) FLAGS 1
P99ERROR DC    XL2'0'                   ERROR CODE
P99INFO  DC    XL2'0'                   INFO CODE
P99TXTPP DC    A(P99TUPL)               POINTER TO TEXT UNIT PTRS
P99RSVD1 DC    XL4'0'                   RESERVED
P99FLAG2 DC    XL4'0'                   FLAGS 2
P99RBLN  EQU   *-P99RB                  RB LENGTH
P99TUPL  DS    0F                  TEXT UNIT POINTERS
         DC    A(P99TUKY1)              SYSOUT
         DC    A(P99TUKY2)              FREE AT CLOSE
         DC    A(P99TUKY3)              LAST PARM IF NO DEST=
         DC    A(X'80000000'+P99TUKY4)  LAST PARM IF DEST= GIVEN
P99TUKY1 DC    AL2(DALSYSOU),AL2(1),AL2(1)
P99SYSOC DC    C'A'                     SYSOUT=A
P99TUKY2 DC    AL2(DALCLOSE),AL2(0)     UNALLOC AT CLOSE
P99TUKY3 DC    AL2(DALSUSER),AL2(1)     OPTIONAL : DEST=RMTXXX
P99DESTL DC    AL2(*-*)                 LENGTH OF DEST
P99DEST  DC    CL8' '                   DEST PARAMETER
P99TUKY4 DC    AL2(DALRTDDN),AL2(1)     DDNAME RETURN
P99DDNL  DC    AL2(8)                   LENGTH OF DDNAME
P99DDN   DC    CL8' '                   DDNAME
         SPACE 1
HHEAD1   DC    CL1'1',CL10' '      ASA ON HEADING
         DC    CL20'VSM HARDCOPY : USER='
HUSER    DC    CL8' ',CL4' '       FOR USERID
         DC    CL4'DATE'
HJDATE   DC    CL7' ',CL3' - '
HTMEHRS  DC    CL2' ',CL1':'
HTMMINS  DC    CL2' ',CL1':'
HTMSECS  DC    CL2' ',CL1' '
HCDAY    DC    CL3' ',CL1' '
HCMON    DC    CL3' ',CL1' '
HCDAYN   DC    CL2' ',CL3',19'
HCYR     DC    CL2' ',CL1' '
HPAGS    DC    CL1' ',CL2'  '
HPAGE    DC    CL6' '
         SPACE 1
HHEAD2   DC    CL11' ',80C'-'
         SPACE 1
HDETAIL  DC    CL11' '
HLINE    DC    CL80' '             TO HOLD PRINT LINE
         SPACE 1
HMONMSK  DC    AL1(31,30,31,30,31,31,30,31,30,31,28,31)
HDAYTAB  DS    0CL3
         DC    C'TUEWEDTHUFRISATSUNMON'
HMONTAB  DS    0CL3
         DC    C'DECNOVOCTSEPAUGJULJUNMAYAPRMARFEBJAN'
MSSTART  DC    CL42'HARDCOPY STARTED : SYSOUT=  DEST=         '
MSJCL    EQU   19,20
MSCLASS  EQU   26,1
MSDEST   EQU   33,8
MSERR    DC    CL50'HARDCOPY ALL. ERR. : DARC=     INFO=     R15=     '
MSERC    EQU   26,4
MSINFO   EQU   36,4
MSRC     EQU   45,4
MSNRMT   DC    CL50'HARDCOPY ERR. : REMOTE          UNDEFINED TO JES2 '
MSRMTN   EQU   23,8
         SPACE 1
#SCREEN  DS    0F
         $FS   WCC=(KBR,RMDT),MF=L
         $FS   SBA=(1,1),RA=(1,1,00),MF=L
         $FS   SBA=(1,1),SF=(PROT),MF=L
         $FS   SBA=(1,25),SF=(PROT,INT),MF=L
         $FS   TEXT='VSM - HARDCOPY SELECT OPTIONS',MF=L
         $FS   SF=(PROT),MF=L
         $FS   SBA=(5,10),TEXT='SYSOUT CLASS =',MF=L
         $FS   SF=(INT,MDT),TEXT='A',MF=L
         $FS   SF=(PROT),MF=L
         $FS   SBA=(7,11),TEXT='DESTINATION =',MF=L
         $FS   SF=(INT,MDT),TEXT='LOCAL   ',MF=L
         $FS   SF=(PROT),MF=L
         $FS   SBA=(11,1),SF=(PROT,INT),MF=L
#ARROW   $FS   TEXT=(' ',4),MF=L
         $FS   SF=(PROT),MF=L
#MSG     $FS   TEXT=(' ',50),MF=L
         $FS   SBA=(15,1),SF=(PROT,INT),TEXT='PFK-3/15',MF=L
         $FS   SF=(PROT),TEXT='ALLOWS YOU TO NULLIFY ',MF=L
         $FS   TEXT='THE START HARDCOPY',MF=L
         $FS   SBA=(1,1),SF=(PT,IC),MF=L
#SCRNLEN EQU   *-#SCREEN
         EJECT
         LTORG
         EJECT
         $LNKSECT                  LINKAGE SECTION DSECT
         SPACE 2
         PRINT NOGEN
         CVT   DSECT=YES,LIST=YES
TIODSECT DSECT
         IEFTIOT1
         IEFZB4D0
         IEFZB4D2
         DCBD  DSORG=PS,DEVD=DA
         SPACE 2
         END
