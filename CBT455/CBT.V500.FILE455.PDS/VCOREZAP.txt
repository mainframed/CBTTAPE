VCRZ     TITLE 'VCOREZAP - VIRTUAL CORE ZAP.'
        PRINT  OFF
         MACRO
&NAME   @INIT  &LABEL
&NAME   $HEDIT &LABEL
         USING &NAME,R10           LOCAL ADDRESSABILITY
         MEND
         MACRO
&DUML   @END   &DUMA
        LTORG
         SPACE 1
         DROP  R10                 END OF LOCAL ADDRESSABILITY
         MEND
         MACRO
&LABEL  @SEND  &A,&OP=L
         AIF   ('&OP' EQ 'L').A
         AIF   ('&OP' EQ 'LA').B
         AIF   ('&OP' EQ 'NONE').C
        MNOTE  4,'*** INVALID OP= SPECIFIED, OP=L ASSUMED.'
.A       ANOP
&LABEL   L     R1,=A(&A)
         AGO   .Z
.B       ANOP
&LABEL   LA    R1,&A
.Z       BAS   R8,SEND
         MEXIT
.C       ANOP
&LABEL   BAS   R8,SEND
         MEND
         MACRO
&LABEL  @WL    &N
         LCLA  &Z
&Z       SETA  &N
         AIF   (&Z LT 1).A
         AIF   (&Z LE 24).B
&Z       SETA  24
         AGO   .B
.A       ANOP
&Z       SETA  1
.B       ANOP
&LABEL  $FS    SF=(PROT),TEXT=(' ',79),MF=L
.C       AIF   (&Z EQ 1).D
        $FS    SF=(PROT),TEXT=(' ',79),MF=L
&Z       SETA  &Z-1
         AGO   .C
.D       MEND
         MACRO
&LABEL  @WTO   &TEXT
&LABEL  WTO    &TEXT,MF=L
         MEND
        PRINT  ON
         SPACE 1
VCOREZAP START 0
         SPACE 1
* PURPOSE :    THIS MODULE ALLOWS THE ALTERATION OR DISPLAY OF ANY
* ---------    VIRTUAL STORAGE LOCATIONS IN THE NUCLEUS, CSA, ADDRESS
*              SPACES (NOT SWAP-OUT), ETC...
*              NOTE : IT DOES'NT REPLACE THE 'INCORZAP' PROGRAM TO
*                     MODIFY THE LPA WHICH IS STORE PROTECTED.
*
* CBT ORIGIN : BASE IDEA FROM CBT TAPE FEB 85, FILE 056.
* ------------ ENLARGED BY : MOINIL P.A.
*                            COMPUTING CENTRE
*                            J.R.C. - ISPRA ESTABLISHMENT
*                            21020 ISPRA (VA), ITALY
*
* ADDS/CHANGES : - CHECK CALLER AUTHORITY AND ENVIRONMENT.
* -------------- - EXTENDED COMMANDS LANGUAGE TO USE LABELS FACILITY.
*                - ADDRESS SPACE COMMANDS (CROSS MEMORY).
*                - USER-AREAS COMMANDS.
*                - FULL SCREEN DISPLAY.
*                - HARDCOPY FUNCTION.
*                - DSECTS AVAILABILITY (PRE-REQUISITE GDTUTIL/DTUR).
*                - SEGMENT-TABLE PROCESSING USE WITH "FIND FULL".
*
***********************************************************************
* NOTE :       A DEFAULT DATA-SET NAME FOR DSECTS TABLES IS DEFINED   *
* ------       IN THE ASSEMBLY FIELD 'DRFDSN'.                        *
***********************************************************************
         EJECT
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*        AUTHORITY LEVELS DEFINITIONS.                                *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
         SPACE 1
*                             X-M = CROSS MEMORY REFERENCE.
LV15     EQU   15             FULL DISPLAY/STORE/TEST LEVEL.
LV10     EQU   10 - 14        FULL DISPLAY AND STORE WITHOUT X-M LEVEL.
LV5      EQU   5 - 9          FULL DISPLAY LEVEL.
LV1      EQU   1 - 4          ONLY DISPLAY WITHOUT X-M LEVEL.
         SPACE 1
        $MDL@IX
         SPACE 2
*        TABLES DIMENSIONS.
*        ------------------
         SPACE 1
NASSTE   EQU   100            MAX. NO. OF ASSIGNED LABELS
NFIXTE   EQU   50             MAX. NO. OF PREDEFINED LABELS
NASNTE   EQU   300            MAX. NO. OF AS-NAMES/AS-ID'S.
NTKWAS   EQU   10             MAX. NO. OF TANKS USER-AREAS (MAX=999).
         EJECT
* VIRTUAL STORAGE COMMANDS AT DISPOSAL :
* --------------------------------------
*
*        D NNNN :
*              TO DISPLAY 16 HEX BYTES STARTING AT LOCATION NNNN.
*              JUST ENTER OR TYPE ONLY D TO DISPLAY 16 HEX BYTES
*              STARTING AT THE CURRENT OR +16 ADDRESS POINTER
*              (CONTINUE THE DISPLAY OF DATA). THIS POINTER IS
*              UPDATED BY ANY D, S, F AND A COMMANDS. ENTER + OR
*              - DECIMAL 1 UP TO 999 TO DISPLAY FORWARD/BACKWARD
*              FROM CURRENT LOCATION POINTER.'
*              NNNN MAY BE :
*              A LABEL (1 TO 8 CHARACTERS),
*                OR AN HEX ADDRESS (1 TO 8 HEX DIGITS PRECEEDED BY
*                   A PERIOD, WHICH MAY BE OMITTED IF THE FIRST ONE
*                   IS A NUMERIC 0-9),
*                OR A FIELD-NAME AND DSECT-NAME (SEPARATED BY A
*                   SLASH AND GIVEN IN THIS ORDER, WHERE DSECT-NAME
*                   MUST ALSO EXIST AS LABEL),
*              FOLLOWED BY AN EVENTUAL DISPLACEMENT EXPRESSED AS +
*              OR - DDDD (ANOTHER LABEL OR HEX VALUE),
*                   OR A FIELD-NAME AND DSECT-NAME (SEPARATED BY A
*                      SLASH AND GIVEN IN THIS ORDER).
*
*        S NNNN HHHH,... OR @N :
*              TO STORE DATA BYTES (HHHH,...) OR THE CONTENTS OF A
*              USER-AREA (@N) STARTING AT NNNN LOCATION (SEE AT
*              PREVIOUS D NNNN COMMAND FOR HOW TO SPECIFY IT).
*              HHHH ARE PAIRS OF HEX DIGITS SEPARATED BY BLANKS OR
*              COMMAS AS DESIRED. @N INDICATE WHICH USER-AREA TO USE.
*              AN * IN PLACE OF NNNN CONTINUE TO STORE IN SEQUENCE.
*
*        F '...' OR HHHH... :
*              TO FIND THE NEXT FIRST OCCURRENCE OF A CHARACTER
*              (1 TO 16 CHARACTERS) OR HEXADECIMAL (2 TO 32 HEX
*              DIGITS) STRING FROM THE CURRENT LOCATION POINTER
*              UNTIL THE NEXT END OF PAGE BOUNDARY ADDRESS (4K
*              BOUNDARY ADDRESS). JUST ENTER OR TYPE ONLY F TO
*              CONTINUE THE SEARCH IN THE SUCCESSIVE 4K.
*              'FULL' KEYWORD : MAY BE SPECIFIED BEFORE OR AFTER
*                     THE SEARCH STRING. IT ALLOWS TO SCAN THE
*                     ENTIRE ADDRESS SPACE, STARTING-ENDING AT
*                     THE CURRENT LOCATION POINTER.
*              NOTE : THE LAST 16 BYTES OF EVERY 4K PAGE SEARCH
*                     ARE NEVER TESTED FOR THE REQUESTED STRING
*                     (THEY ARE JUST DISPLAYED WITH THE ADVICE
*                     '<--NF'), BUT THEY ARE RETAINED AND USED
*                     WITH THE 4K SUCCESSIVE IMMEDIATE CHECK, SO
*                     SCAN OVER PAGES BOUNDARIES IS SUPPLIED.
         EJECT
* LABELS DEFINE COMMANDS AT DISPOSAL :
* ------------------------------------
*
*        A LLLL NNNN :
*              TO ASSIGN TO LABEL LLLL (1 TO 8 CHARACTERS) THE
*              SPECIFIED ADDRESS OR DISPLACEMENT VALUE NNNN (SEE AT
*              PREVIOUS D NNNN COMMAND FOR HOW TO SPECIFY IT).
*              IF NNNN IS OMITTED, THE LABEL LLLL IS ASSIGNED THE
*              CURRENT ADDRESS POINTER VALUE.
*
*        Z LLLL :
*              TO DELETE THE SPECIFIED LABEL LLLL REFERENCE.
*
*        L LLLL :
*              TO DISPLAY THE VALUE OF THE SPECIFIED LABEL OR
*              ALL THE LABELS STARTING BY THE CHARACTERS DEFINED
*              BY STRING LLLL. IF LLLL IS NOT SPECIFIED, ALL THE
*              EXISTING LABELS WILL BE LISTED.
*
*        I :   TO DISPLAY THE INTERNAL PREDEFINED LABELS.
*
* DSECT'S COMMANDS AT DISPOSAL :
* ------------------------------
*
*        B :   BROWSE ALL DSECTS NAMES IN THE CURRENT DATA-SET.
*
*        B DDDD :
*              BROWSE SEQUENTIALLY THE CONTENTS OF AN ENTIRE DSECT
*              (DDDD IS A DSECT-NAME).
*
*        B DDDD ... :
*              BROWSE INFORMATION FROM A DSECT (DDDD IS A DSECT-NAME).
*              THEN FOLLOWS ... WHICH MAY BE :
*              FFFF : A FIELD-NAME, OR
*              FFFF : A FLAG-NAME, OR
*              HHHH : A FIELD-DISPLACEMENT (HEX), OR
*              FFFF HH : A FIELD-NAME AND FLAG-VALUE (HEX).
*
*        Q :   DISPLAY HOW CORE STORAGE SPACE HAS GROWN.
*
*        Q * : RESTART THE CURRENT DSECT'S DATA-SET.
*
*        Q SSSS :
*              START A NEW DSECT'S DATA-SET (SSSS IS A DATA-SET NAME).
*              NOTE : Q * AND Q SSSS COMMANDS MAY BE USED TO FREE ALL
*                     CORE STORAGE SPACE AND START AGAIN WITH THE SAME
*                     OR ANOTHER DSECTS TABLES DATA-SET.
         EJECT
* USER-AREAS COMMANDS AT DISPOSAL :
* ---------------------------------
*
*        M @N :
*              SET AND RETAIN AS CURRENT THE USER-AREA NUMBER N.
*
*        M :   DISPLAY THE CURRENT USER-AREA.
*
*        M Z OR R :
*              RESET TO ZEROS (Z) OR BLANKS (R) THE CURRENT USER-AREA.
*
*        M HHHH,... OR '...' :
*              STORE DATA BYTES OR CHARACTERS STRING IN CURRENT
*              USER-AREA STARTING AT THE CURRENT LOCATION POINTER.
*              HHHH ARE PAIRS OF HEX DIGITS SEPARATED BY BLANKS OR
*              COMMAS AS DESIRED.
*
*        M + OR - XX :
*              MOVE THE LOCATION POINTER FORWARD (+) OR BACKWARD (-)
*              OF XX (HEX) DATA BYTES IN THE CURRENT USER-AREA.
*
*        M L XX :
*              SET USER-AREA TEXT DATA LENGTH TO XX (HEX).
*
* MISCELLANEOUS COMMANDS AT DISPOSAL :
* ------------------------------------
*
*        G TTT :
*              THIS COMMAND IS AVAILABLE ONLY IN FULL SCREEN MODE,
*              AND MAY BE USED TO INSERT IN THE INPUT AREA 1 UP TO
*              16 BYTES (TTT, DEFAULT IS 4) LOCATED AT THE CURRENT
*              POINTER (USEFULL TO THEN ASSIGN A LABEL OR STORE DATA
*              IN A USER-AREA).
*
*        P ON C(?) D(?) :
*              START HARDCOPY (CLASS/DESTINATION).
*
*        P OFF :
*              STOP HARDCOPY.
*
*        C ON/OFF :
*              TO ENABLE/DISABLE THE USE OF THE ADDRESS SPACE
*              COMMANDS.
*
*        Y :   DISPLAY CURRENT ADDRESS SPACE SEGMENT-TABLE.
         EJECT
* ADDRESS SPACE COMMANDS AT DISPOSAL :
* ------------------------------------
*
*        XT :  TO LOAD OR REFRESH THE AS-TABLE AND RESET THE
*              AS-ID MODE.
*
*        XA XXXX :
*              TO ACTIVATE THE AS-ID XXXX MODE FOR THE NEXT XD,
*              XS AND XF COMMANDS. XXXX MAY BE AN AS-NAME (1 TO
*              8 CHARACTERS) OR AN HEX AS-ID NUMBER (1 TO 4 HEX
*              DIGITS PRECEEDED BY A PERIOD, WHICH MAY BE OMITTED
*              IF THE FIRST ONE IS A NUMERIC 0-9).
*
*        XC :  TO CLEAR THE AS-TABLE AND RESET THE AS-ID MODE.
*
*        XR :  TO RESET THE AS-ID MODE.
*
*        XL XXXX :
*              TO DISPLAY THE VALUE OF THE SPECIFIED AS-NAME (1
*              TO 8 CHARACTERS), AS-ID NUMBER (1 TO 4 HEX DIGITS
*              PRECEEDED BY A PERIOD, WHICH MAY BE OMITTED IF THE
*              FIRST ONE IS A NUMERIC 0-9) OR ALL THE AS-NAMES
*              STARTING BY THE CHARACTERS DEFINED BY STRING XXXX.
*              IF XXXX IS NOT SPECIFIED, ALL THE AS-TABLE WILL BE
*              LISTED.
*
*        XD ..., XS ... AND XF ... :
*              TO DISPLAY, STORE AND FIND IN THE ACTIVE AS-ID.
*              SYNTAX AND PROCESSING ARE IDENTICAL TO D, S AND
*              F COMMANDS.
*
*        XG TTT :
*              THIS COMMAND IS AVAILABLE ONLY IN FULL SCREEN MODE,
*              AND HAS THE SAME FUNCTION AS G TTT COMMAND, BUT THE
*              BYTES ARE EXTRACTED FROM THE ACTIVE AS-ID.
         EJECT
* SPECIAL TEST COMMANDS AT DISPOSAL :
* ------------------------------------
*
*        T ON/OFF :
*              TO ENABLE/DISABLE THE ACCESS TO THE "VCOREZAP"
*              PROGRAM AND ARRAYS STORAGE (AVAILABLE ONLY WITH
*              "C OFF").
*
*        T L(INE)/S(CREEN) :
*              TO CHANGE THE DISPLAY MODE BETWEEN LINE AND FULL
*              SCREEN (ONLY FOR SCREEN TERMINAL OBVIOUSLY).
*
*        T Y(ES)/N(O) :
*              TO ENABLE/DISABLE THE SEGMENT-TABLE PROCESSING USE
*              WHEN "FIND FULL" COMMAND.
*
* HELPS COMMANDS AT DISPOSAL :
* ----------------------------
*
*        H :   TO HAVE THE VIRTUAL STORAGE COMMANDS SUMMARY.
*
*        HL :  TO HAVE THE LABEL'S AND DSECT'S COMMANDS SUMMARY.
*
*        HM :  TO HAVE THE MISCELLANEOUS COMMANDS SUMMARY.
*
*        XH :  TO HAVE THE ADDRESS SPACE COMMANDS SUMMARY.
*
* AT ANY TIME, ENTER :       ?     TO OBTAIN INFO STATUS.
* --------------------       E     TO TERMINATE.
         EJECT
        $DEFREG
KB       EQU   1024                1 K-BYTES
         SPACE 1
VCOREZAP AMODE 24
VCOREZAP RMODE 24
         EJECT
        $HEDIT
        $XENT  BASE=R12
         L     R15,AVSVERR
         MVC   0(18*4,R15),0(R13)  COPY SAVE AREA
         LR    R13,R15             SUBSTITUTION
         USING VSVERR,R13
         L     R11,AVCWORK
         USING VCWORK,R11
         L     R10,=A(VCINIT)
         BR    R10                 -> INITIALIZATION
         SPACE 1
AVSVERR  DC    A(VSVERR)           SAVE AREA - ERROR RETURNS
AVCWORK  DC    A(VCWORK)           WORK AREAS ADDRESS/BASE REGISTER
         EJECT
***********************************************************************
*        GET A COMMAND OR CONTINUE WITH PREVIOUS.                     *
***********************************************************************
INQUIRT  TM    FSCRSW,FSCRSF       FULL SCREEN?
         BZ    INQUIRY             NO
         L     R10,=A(INFST)       YES, GET STATUS
         USING INFST,R10
         B     INFST0
         DROP  R10
INQUIRY  NI    SWITCH,255-XMEMRF   RESET X-MEM REF. REQUEST
         NI    OPERSW,255-GETBSW
        @SEND  INQRY
         OC    ANSR(L'ANSR),ALLBLKS
         LM    R3,R5,SCVAL
         CLI   0(R3),C' '
         BNE   WHAT
         BXLE  R3,R4,*-8
         TM    STATUS,TESTRUN
         BZ    *+L'*+6
         MVC   RCVIND(L'RCVIND),ALLBLKS
         TM    FSCRSW,FSCRSF       FULL SCREEN?
         BZ    *+L'*+8             NO
         TM    FSCRSW,FSCRSC       CONTINUATION?
         BO    CONTOUT             YES
         CLI   PRVOP,C'S'
         BNE   INQ1
         L     R6,PVPTR
         TM    SWITCH,POPXMR       WAS PREVIOUS OP. X-MEM. REF. MODE?
         BZ    *+L'*+4             NO
         OI    SWITCH,XMEMRF       YES, CONTINUE IN X-MEM. REF. MODE
         NI    SWITCH,255-ASFULL-PVALID RESET
         L     R10,=A(DSPLY)
         USING DSPLY,R10
         B     DSPLY0
         DROP  R10
INQ1     L     R6,CRPTR
         ST    R6,DATA
         TM    SWITCH,POPXMR       WAS PREVIOUS OP. X-MEM. REF. MODE?
         BZ    *+L'*+4             NO
         OI    SWITCH,XMEMRF       YES, CONTINUE IN X-MEM. REF. MODE
         CLI   PRVOP,C'F'
         BNE   *+L'*+8
         L     R10,=A(FIND)
         USING FIND,R10
         B     FNCNT
         DROP  R10
         NI    SWITCH,255-ASFULL-PVALID RESET
         L     R10,=A(DSPLY)
         USING DSPLY,R10
         B     DSPLY1
         DROP  R10
WHAT     L     R10,=A(ENCMD)
         BR    R10
LISTOUT  TM    FSCRSW,FSCRSF       FULL SCREEN?
         BZ    ADDOUT              NO
         CLC   FSPOINT,=A(16*81)   YES, SCREEN FULL?
         BL    ADDOUT              NO
         MVC   MSGSVT(L'MSGTXT),MSGTXT  YES, SAVE DATA
         OI    FSCRSW,FSCRSC       SAY CONTINUATION
         NI    FSCRSW,255-FSCRSL   RESET DATA
        $FS    SF=(PROT,INT),MF=(I,FSCRSDC)
         MVC   FSCRTDC,=CL6'*MORE*'     INDICATE THAT DATA CONTINUE
         STM   R2,R10,FSSAVE  SAVE REGISTERS AROUND SCREEN OPERATIONS
         L     R10,=A(INFST)
         USING INFST,R10
         B     INFST0
         DROP  R10
CONTOUT  LM    R2,R10,FSSAVE       RESTORE REGISTERS
         MVC   MSGTXT(L'MSGTXT),MSGSVT  RESTORE DATA
         XC    FSPOINT,FSPOINT
         OI    FSCRSW,FSCRSL       SAY DATA LINES
         NI    FSCRSW,255-FSCRSC   RESET CONTINUATION
ADDOUT  @SEND  MSGOUT,OP=LA
         BR    R9                  RETURN
         EJECT
***********************************************************************
*        RECOVERY PROCESS.                                            *
***********************************************************************
RECOVER0 TM    STATUS,TESTRUN ------------------------------ * DANGER *
         BZ    RECOVER                                                *
         MVI   RCVIND,C'0'                                            *
RECOVER  BAS   R8,DISABLE ---------------------------------- * DANGER *
         TM    SWITCH,ASFULL       ENTIRE A.S. SEARCH?
         BO    RCVR4               YES
         TM    FSCRSW,FSCRSF       FULL SCREEN?
         BZ    RCVR2               NO, PROCESS 0C4
         TM    FSPRSW,FSPRDS       DISPLAY OPERATION?
         BZ    RCVR2               NO, PROCESS 0C4
         LM    R0,R15,FSRCVR       RESTORE REGISTERS
         AR    R2,R6               0C4 IS OCCURED ON NEXT PAGE
         LR    R1,R2
         SRL   R2,8                COMPUTE HOW MANY BYTES IN GOOD PAGE
         SLL   R2,8
         SR    R2,R6
         BNP   RCVR2               NONE, PROCESS 0C4
         LR    R0,R6
         SRL   R0,12
         SLL   R0,12
         LTR   R0,R0
         BZ    RCVR1               PAGE 0 IS AN EXCEPTION
         SRL   R1,12
         SLL   R1,12
         CLR   R0,R1
         BE    RCVR2               SAME PAGE, PROCESS 0C4
RCVR1    LR    R1,R2
         XR    R0,R0
         D     R0,=A(L'HXDATA)
         LR    R4,R1               NEW NUMBER OF LINES TO DISPLAY
         NI    FSPRSW,255-FSPRDS   RESET
         TM    SWITCH,POPXMR       WAS PREVIOUS OP. X-MEM. REF. MODE?
         BZ    *+L'*+4             NO
         OI    SWITCH,XMEMRF       YES, CONTINUE IN X-MEM. REF. MODE
         L     R10,=A(DSPLY)
         USING DSPLY,R10
         B     DSPLY3              RESTART
         DROP  R10
RCVR2    L     R1,=A(DISCC)
         UNPK  32(9,R1),RCVPTR(5)
         TR    32(8,R1),HEXTAB-X'F0'
         MVI   40(R1),C' '
        @SEND  OP=NONE             DISPLAY 0C4.
         CLI   PRVOP,C'S'
         BE    INQUIRT
         L     R1,RCVPTR           RECOVER POINTERS
         ST    R1,PVPTR
         CLI   PRVOP,C'F'
         BE    *+L'*+8
         LA    R1,L'HXDATA(R1)     NEXT DATA DISPLAY
         B     RCVR3
         NI    SWITCH,255-PVALID
         LA    R1,2048(R1)         NEXT PAGE FIND
         LA    R1,2048(R1)
RCVR3    ST    R1,CRPTR
         B     INQUIRT
RCVR4    NI    SWITCH,255-PVALID
         L     R4,RCVPTR
         TM    SGTPSW,STPDIS+STPUSE     SEGMENT PROCESSING DISABLED?
         BNZ   RCVR5               YES
         LR    R10,R4              NO, CHECK IF ON A NEW M-BYTE SEGMENT
         SRL   R10,20
         SLL   R10,20
         CLR   R10,R4
         BNE   RCVR5
         L     R10,=A(SGTCHK)
         BASR  R14,R10             GO CHECK SEGMENT-TABLE
         L     R4,RCVPTR
         LTR   R15,R15             HOW COMPLETE?
         BZ    RCVR5               OK, CONTINUE
         ST    R4,CRPTR            ERROR, GO DISPLAY DIAGNOSE
         B     INQUIRT
RCVR5    L     R10,=A(FIND)
         USING FIND,R10
         B     FNRCV
         DROP  R10
         EJECT
***********************************************************************
*        DIAGNOSTIC MESSAGE.                                          *
***********************************************************************
ERROR    TM    STATUS,TESTRUN
         BZ    *+L'*+6
         MVC   RCVIND(L'RCVIND),ALLBLKS
         TM    FSCRSW,FSCRSF       FULL SCREEN?
         BZ    ERRORMS             NO
         XC    FSPOINT,FSPOINT     YES, RESET
         NI    FSCRSW,255-FSCRSC-FSCRSH-FSCRST-FSCRSL-FSCRSI
         NI    FSPRSW,255-FSPRDS
ERRORMS @SEND  OP=NONE
         B     INQUIRT
         SPACE 1
***********************************************************************
*        THAT'S ALL FOLKS.                                            *
***********************************************************************
FINISH   L     R10,=A(VCEND)
         BR    R10                 -> TERMINATION
EXRET    L     R15,RETCC
        $XRET  CC=(R15)
         EJECT
***********************************************************************
*        SUBROUTINE TO DECODE AN HEX FIELD (MAX. LENGTH 8).           *
*        REGISTERS USE : R1 - WORK REGISTER / NO. OF HEX DIGITS       *
*                        R2 - WORK REGISTER                           *
*                        R3-R4-R5 - ANSWER SCAN REGISTERS             *
*                        R14 - LINK REGISTER                          *
*                               +0 - NO FIELD FOUND                   *
*                               +4 - INVALID HEX                      *
*                               +8 - TOO LONG                         *
*                              +12 - FIELD PROCESSED                  *
*                                    (VALUE IS IN 'DATA')             *
***********************************************************************
SCAN     XR    R1,R1
         XC    DATA,DATA
         LR    R2,R3               START OF DATA FIELD
SCAN1    CLI   0(R3),C','          DELIMITER ,?
         BE    SCAN2               YES
         CLI   0(R3),C'+'          DELIMITER +?
         BE    SCAN2               YES
         CLI   0(R3),C'-'          DELIMITER -?
         BE    SCAN2               YES
         CLI   0(R3),C' '          END OF DATA?
         BE    SCAN2               YES
         CLI   0(R3),C'A'
         BL    4(R14)              EXIT ---------------------------> +4
         CLI   0(R3),C'9'
         BH    4(R14)              EXIT ---------------------------> +4
         CLI   0(R3),C'0'
         BNL   *+L'*+8
         CLI   0(R3),C'F'
         BH    4(R14)              EXIT ---------------------------> +4
         BXLE  R3,R4,SCAN1         INC PTR
SCAN2    LR    R1,R3               TURN TO WK REG
         SR    R1,R2               GET NO OF CHAR SCANNED
         BNPR  R14                 SKIP MOVE IF ZERO --------------> +0
         CH    R1,=H'8'
         BH    8(R14)              SKIP MOVE IF INVALID ADDRESS ---> +8
         BCTR  R1,0                DECR. BY 1
         XC    FXLAB,FXLAB         CLEAR WORK AREA
         EX    R1,MVTMP            MOVE TO WORK AREA
         EX    R1,NRTMP            NORMALIZE FOR TRANSLATE
         EX    R1,TRTMP            TRANSLATE TO BINARY
         LA    R1,1(R1)            INCR. BY 1
         EX    R1,PKTMP            CONVERT TO HEX
         B     12(R14)             EXIT --------------------------> +12
MVTMP    MVC   FXLAB(*-*),0(R2)    <<EXECUTED>>
NRTMP    NC    FXLAB(*-*),=XL8'1F1F1F1F1F1F1F1F'  <<EXECUTED>>
TRTMP    TR    FXLAB(*-*),TRANTBL  <<EXECUTED>>
PKTMP    PACK  DATA(L'DATA+1),FXLAB(*-*)     <<EXECUTED>>
         EJECT
***********************************************************************
*        SUBROUTINE TO EXTRACT A LABEL (MAX. LENGTH 8).               *
*        REGISTERS USE : R1 - WORK REGISTER / NO. OF CHAR. -1         *
*                        R2 - WORK REGISTER                           *
*                        R3-R4-R5 - ANSWER SCAN REGISTERS             *
*                        R7-R8-R9 - P.LABELS TABLE SCAN REGISTERS     *
*                        R14 - LINK REGISTER                          *
*                               +0 - NO LABEL FOUND                   *
*                               +4 - INVALID LABEL                    *
*                               +8 - TOO LONG                         *
*                              +12 - PREDEFINED LABEL + R7            *
*                                    (NAME IS IN 'LBLF')              *
*                              +16 - LABEL PROCESSED + NOT FOUND      *
*                                    (NAME IS IN 'LBLF')              *
***********************************************************************
LABEL    CLI   0(R3),C'0'          FIRST CHAR NUMERIC?
         BNL   4(R14)              YES, INVALID -------------------> +4
         CLI   0(R3),C'A'          FIRST CHAR ALPHA?
         BNL   LABEL1              YES
         CLI   0(R3),C'$'          DOLLAR SIGN?
         BE    LABEL1              YES
         CLI   0(R3),C'#'          NUMBER SIGN?
         BE    LABEL1              YES
         CLI   0(R3),C'@'          AT SIGN?
         BNE   4(R14)              NO, INVALID --------------------> +4
LABEL1   LR    R2,R3               START OF LABEL
LABEL2   BXLE  R3,R4,*+L'*+4       INC PTR
         B     LABEL3
         CLI   0(R3),C' '          END OF LABEL?
         BE    LABEL3              YES
         CLI   0(R3),C'/'          END OF LABEL BY /?
         BE    LABEL3              YES
         CLI   0(R3),C'+'          END OF LABEL BY +?
         BE    LABEL3              YES
         CLI   0(R3),C'-'          END OF LABEL BY -?
         BNE   LABEL2              NO
LABEL3   LR    R1,R3               TURN TO WK REG
         SR    R1,R2               GET NO OF CHAR SCANNED
         BNPR  R14                 SKIP MOVE IF ZERO --------------> +0
         CH    R1,=H'8'
         BH    8(R14)              SKIP MOVE IF INVALID LABEL -----> +8
         MVC   LBLF(L'LBLF),ALLBLKS     CLEAR WORK AREA
         BCT   R1,*+L'*+6          DECR. BY 1
         MVC   LBLF(*-*),0(R2)     <<EXECUTED>>
         EX    R1,*-6              MOVE TO WORK AREA
         LA    R14,12(R14)         ADJUST RETURN
SCITBL   LM    R7,R9,SCFIX         SCAN PREDEFINED LABELS TABLE
         CLC   0(L'LBLF,R7),LBLF
         BER   R14                 EXIT -----------------------> +12/+0
         BXLE  R7,R8,*-8
         B     4(R14)              EXIT -----------------------> +16/+4
         EJECT
***********************************************************************
*        SUBROUTINE TO EXTRACT A NAME (MAX. LENGTH 8).                *
*        REGISTERS USE : R1 - WORK REGISTER / NO. OF CHAR. -1         *
*                        R2 - WORK REGISTER                           *
*                        R3-R4-R5 - ANSWER SCAN REGISTERS             *
*                        R14 - LINK REGISTER                          *
*                               +0 - NO NAME FOUND                    *
*                               +4 - INVALID NAME                     *
*                               +8 - TOO LONG                         *
*                              +12 - NAME PROCESSED                   *
*                                    (NAME IS IN 'NMEF')              *
***********************************************************************
NAME     CLI   0(R3),C'*'          FIRST CHAR *?
         BE    NAME1               YES, VALID
NAMER    CLI   0(R3),C'0'          FIRST CHAR NUMERIC?
         BNL   4(R14)              YES, INVALID -------------------> +4
         CLI   0(R3),C'A'          FIRST CHAR ALPHA?
         BNL   NAME1               YES
         CLI   0(R3),C'$'          DOLLAR SIGN?
         BE    NAME1               YES
         CLI   0(R3),C'#'          NUMBER SIGN?
         BE    NAME1               YES
         CLI   0(R3),C'@'          AT SIGN?
         BNE   4(R14)              NO, INVALID --------------------> +4
NAME1    LR    R2,R3               START OF NAME
         BXLE  R3,R4,*+L'*+4       INC PTR
         B     *+L'*+8
         CLI   0(R3),C' '          END OF NAME?
         BNE   *-12                NO
         LR    R1,R3               TURN TO WK REG
         SR    R1,R2               GET NO OF CHAR SCANNED
         BNPR  R14                 SKIP MOVE IF ZERO --------------> +0
         CH    R1,=H'8'
         BH    8(R14)              SKIP MOVE IF INVALID NAME ------> +8
         MVC   NMEF(L'NMEF),ALLBLKS     CLEAR WORK AREA
         BCT   R1,*+L'*+6          DECR. BY 1
         MVC   NMEF(*-*),0(R2)     <<EXECUTED>>
         EX    R1,*-6              MOVE TO WORK AREA
         B     12(R14)             EXIT --------------------------> +12
         EJECT
***********************************************************************
*        SUBROUTINE TO SCAN LABELS TABLE.                             *
*        REGISTERS USE : R7-R8-R9 - LABELS TABLE SCAN REGISTERS       *
*                        R14 - LINK REGISTER                          *
***********************************************************************
SCLTBL   CLC   TBVAL+8(4),TBVAL
         BL    ERR19
         LM    R7,R9,TBVAL         SCAN LABELS TABLE
         CLC   0(L'LBLF,R7),LBLF
         BER   R14
         BXLE  R7,R8,*-8
         B     ERR20
         SPACE 2
***********************************************************************
*        SUBROUTINE TO POSITION ON NEXT CHARACTER.                    *
*        REGISTERS USE : R3-R4-R5 - ANSWER SCAN REGISTERS             *
*                        R14 - LINK REGISTER                          *
*                              +0 - NO CHARACTER FOUND                *
*                              +4 - NEXT CHARACTER POINTED            *
***********************************************************************
NEXT     CLR   R3,R5               NO MORE CHAR?
         BHR   R14                 YES, LIKE NO CHARACTER FOUND ---> +0
         CLI   0(R3),C' '          IS THIS CHAR BLANK?
         BNE   4(R14)              NO, OK POINTED -----------------> +4
NEXTCH   BXLE  R3,R4,*-8           BUMP POINTER
         BR    R14                 NO CHARACTER FOUND -------------> +0
         EJECT
***********************************************************************
*        I/O ROUTINE (WTO/WTOR OR TPUT/TGET AND FULL SCREEN).         *
*        REGISTERS USE : R0 - WORK REGISTER                           *
*                        R1 - WTO(R) LIST / WORK REGISTER             *
*                        R2 - ALTERNATE SCREEN ADDRESS                *
*                        R3 - ALTERNATE SCREEN LENGTH                 *
*                        R4 - ALTERNATE SCREEN INPUT                  *
*                        R14-R15 - WORK REGISTERS                     *
*                        R8 - LINK REGISTER                           *
***********************************************************************
SEND     TM    STATUS,TSORUN
         BO    STSO
*        --------------------------------------------------------------
*        WTO/WTOR PROCESSING.
*        --------------------------------------------------------------
         CLI   0(R1),0             REPLY?
         BNE   SENDR               YES
         TM    HCPRSW,HCCOPY       IS HARDCOPY ACTIVE?
         BZ    *+L'*+6             NO
         L     R15,=A(HCOPYSC)     HARDCOPY LINE
         BASR  R14,R15
        WTO    MF=(E,(1))
         BR    R8
SENDR    L     R14,4(R1)           ECB
         TM    OPERSW,INPEND       INPUT IS ALREADY PENDING?
         BZ    SENDM               NO
         TM    OPERSW,FFSTUP       FIND FULL UPDATE?
         BZ    *+L'*+12            NO
         NI    OPERSW,255-FFSTUP
         USING ECB,R14
         TM    ECBCC,ECBPOST       REPLY HAS COMPLETED?
         BZ    SENDF               NO
         DROP  R14
         NI    OPERSW,255-INPEND
         ST    R14,SVAD
         B     SENDW
SENDM    XC    0(4,R14),0(R14)
         ST    R14,SVAD
         XR    R14,R14
         IC    R14,8(R1)           REPLY LENGTH
         BCTR  R14,0
         BCTR  R14,0
         ICM   R15,B'1111',0(R1)   REPLY AREA
         MVI   0(R15),C' '
         EX    R14,SCLR
        WTOR   MF=(E,(1))
         TM    OPERSW,FFSTUP       FIND FULL UPDATE?
         BZ    SENDW               NO
         OI    OPERSW,INPEND       SAY INPUT PENDING
         NI    OPERSW,255-FFSTUP
SENDF    L     R10,=A(FIND)        BACK TO FIND FULL
         USING FIND,R10
         B     FNFLC
         DROP  R10
SENDW    L     R1,SVAD
        WAIT   ECB=(1)
         TM    HCPRSW,HCCOPY       IS HARDCOPY ACTIVE?
         BZR   R8                  NO
         OI    HCPRSW,HCANSR
         L     R15,=A(HCOPYSC)     HARDCOPY COMMAND
         BASR  R14,R15
         BR    R8
         SPACE 1
*        --------------------------------------------------------------
*        TPUT/TGET PROCESSING.
*        --------------------------------------------------------------
         SPACE 1
STSO     TM    FSCRSW,FSCRSF       FULL SCREEN?
         BO    FSC                 YES
         CLI   0(R1),0             REPLY?
         BNE   STSOR               YES
         LH    R0,0(R1)
         SH    R0,=H'4'
         LA    R1,4(R1)
         TM    HCPRSW,HCCOPY       IS HARDCOPY ACTIVE?
         BZ    *+L'*+6             NO
         L     R15,=A(HCOPYSC)     HARDCOPY LINE
         BASR  R14,R15
        TPUT   (1),(0)
         TM    STATUS,TESTRUN
         BZR   R8
         NI    OPERSW,255-INPEND
         BR    R8
STSOR    ST    R1,SVAD
         TM    STATUS,TESTRUN
         BZ    *+L'*+8
         TM    OPERSW,INPEND       INPUT IS ALREADY PENDING?
         BZ    *+L'*+8             NO
         NI    OPERSW,255-INPEND
         B     STBK+L'STBK
         XR    R0,R0
         IC    R0,9(R1)
         SH    R0,=H'4'
         LA    R1,12(R1)
        TPUT   (1),(0)
STBK     L     R1,SVAD
         XR    R14,R14
         IC    R14,8(R1)           REPLY LENGTH
         BCTR  R14,0
         BCTR  R14,0
         ICM   R15,B'1111',0(R1)   REPLY AREA
         MVI   0(R15),C' '
         EX    R14,SCLR
         LA    R0,1(R14)
         LR    R1,R15
         TM    OPERSW,FFSTUP       FIND FULL UPDATE?
         BZ    STSON               NO
         NI    OPERSW,255-FFSTUP   YES
        TGET   (1),(0),NOWAIT
         LTR   R15,R15             ANY INPUT AVAILABLE?
         BZ    STSOI               YES, INTERRUPT
         CH    R15,=H'4'           NORMAL NOWAIT?
         BNE   STSOA               NO, INTERRUPT
         TM    STATUS,TESTRUN
         BZ    *+L'*+4
         OI    OPERSW,INPEND       YES, SAY INPUT PENDING
         L     R10,=A(FIND)        CONTINUE FIND FULL
         USING FIND,R10
         B     FNFLC
         DROP  R10
STSON   TGET   (1),(0)
         LTR   R15,R15
         BNZ   STSOA
STSOI    TM    HCPRSW,HCCOPY       IS HARDCOPY ACTIVE?
         BZR   R8                  NO
         OI    HCPRSW,HCANSR
         L     R15,=A(HCOPYSC)     HARDCOPY COMMAND
         BASR  R14,R15
         BR    R8
STSOA    CH    R15,=H'12'
         BE    CLRIQ
         ST    R15,RETCC
         B     FINISH
CLRIQ   TCLEARQ INPUT
        TPUT   TSRTL,L'TSRTL
         B     STBK
SCLR     MVC   1(*-*,R15),0(R15)   <<EXECUTED>>
         SPACE 1
*        --------------------------------------------------------------
*        FULL SCREEN TPUT/TGET PROCESSING.
*        --------------------------------------------------------------
         SPACE 1
FSC      CLI   0(R1),0             REPLY?
         BNE   FSC10               YES
         LH    R14,0(R1)
         SH    R14,=H'4'
         BNPR  R8
         BCTR  R14,0
         LA    R1,4(R1)
         TM    FSCRSW,FSCRSL       DATA LINE?
         BO    FSC1                YES
         TM    FSCRSW,FSCRST       STATUS LINE?
         BO    FSC2                YES
         CH    R14,=H'79'
         BNH   *+L'*+4
         LH    R14,=H'79'
         EX    R14,FSCMVM
         BR    R8
FSCMVM   MVC   FSCRMSG(*-*),0(R1)  <<EXECUTED>>
FSC1     LA    R15,FSCRLNS
         B     FSC3
FSC2     LA    R15,FSCRLST
FSC3     L     R0,FSPOINT
         ALR   R15,R0
         CH    R14,=H'78'
         BNH   *+L'*+4
         LH    R14,=H'78'
         TM    FSCRSW,FSCRSI       INTENSIVE LINE REQUEST?
         BZ    FSC4                NO
        $FS    SF=(PROT,INT),MF=(I,(R15))
         NI    FSCRSW,255-FSCRSI
FSC4     EX    R14,FSCMVL
         AH    R0,=H'81'
         ST    R0,FSPOINT
         BR    R8
FSCMVL   MVC   2(*-*,R15),0(R1)    <<EXECUTED>>
FSC10    TM    FSCRSW,FSCRSH       HELP PANEL TO DISPLAY?
         BZ    *+L'*+8             NO
         NI    FSCRSW,255-FSCRSH
         B     *+L'*+12
         LA    R2,FSCREEN
         LA    R3,FSCRLNG
         LA    R4,FSCRTXT
         TM    FSCRSW,FSCRSS+FSCRSR     FIRST TPUT DONE?
         BNO   FSC11               YES
        $FS    CC=EW,MF=(I,(R2))   SET ERASE/WRITE
FSC11    TM    OPERSW,FFSTUP       FIND FULL UPDATE?
         BZ    FSC12               NO
         LA    R1,2(R2)
        $FS    WCC=(KBR),MF=(I,(R1))
         LA    R3,FSCRLNW          PRESERVE COMMAND INPUT AREA
        TPUT   (R2),(R3),FULLSCR
         LA    R1,2(R2)
        $FS    WCC=(KBR,RMDT),MF=(I,(R1))
         B     FSC13
FSC12   TPUT   (R2),(R3),FULLSCR
FSC13    TM    FSCRSW,FSCRSS+FSCRSR     FIRST TPUT DONE?
         BNO   FSC14          YES
        $FS    CC=W,MF=(I,(R2))    RESTORE WRITE
         NI    FSCRSW,255-FSCRSS   SAY FIRST TPUT DONE
FSC14    TM    HCPRSW,HCCOPY       IS HARDCOPY ACTIVE?
         BZ    *+L'*+6             NO
         L     R15,=A(HCOPYSC)     HARDCOPY SCREEN
         BASR  R14,R15
         XC    0(FSRPLYL,R4),0(R4)
         XC    FSCODE(REPLYL),FSCODE
         TM    OPERSW,FFSTUP       FIND FULL UPDATE?
         BZ    FSC15               NO
         NI    OPERSW,255-FFSTUP   YES
        TGET   FSCODE,REPLYL-1,ASIS,NOWAIT   DON'T WAIT FOR REPLY
         LTR   R15,R15             ANY INPUT AVAILABLE?
         BZ    FSC22               YES, INTERRUPT
         CH    R15,=H'4'           NORMAL NOWAIT?
         BNE   FSC16               NO, INTERRUPT
         L     R10,=A(FIND)        YES, CONTINUE FIND FULL
         USING FIND,R10
         B     FNFLC
         DROP  R10
FSC15   TGET   FSCODE,REPLYL-1,ASIS     WAIT FOR REPLY
FSC16    CH    R15,=H'12'          INPUT LONGER THAN BUFFER?
         BNE   FSC20               NO, CONTINUE
        TCLEARQ INPUT              CLEAR THE QUEUE
         XR    R15,R15             RESET RC
FSC20    LTR   R15,R15             INPUT OK?
         BZ    *+L'*+8             YES
         ST    R15,RETCC
         B     FSC26
         CLI   FSCODE,X'6E'        PA2? (RESHOW)
         BE    FSC21               YES
         CLI   FSCODE,X'7C'        PF 12 ENTERED?
         BE    FSC21               YES
         CLI   FSCODE,X'4C'        PF 24 ENTERED (ALT. PF 12)?
         BNE   FSC22               NO
FSC21    MVC   0(FSRPLYL,R4),LSTDSP     YES, RE-DISPLAY LAST SCREEN
         B     FSC11               WITH THE LAST DISPLAY INPUT COMMAND
FSC22    MVC   FSCRMSG,ALLBLKS
         CLI   FSCODE,X'F1'        PF 1 ENTERED?
         BE    *+L'*+8             YES
         CLI   FSCODE,X'C1'        PF 13 ENTERED (ALT. PF 1)?
         BNE   *+L'*+10            NO
         MVC   FSTEXT(3),=CL3'H  '
         B     FSC40
         CLI   FSCODE,X'F2'        PF 2 ENTERED?
         BE    *+L'*+8             YES
         CLI   FSCODE,X'C2'        PF 14 ENTERED (ALT. PF 2)?
         BNE   *+L'*+10            NO
         MVC   FSTEXT(3),=CL3'X H'
         B     FSC40
         CLI   FSCODE,X'7A'        PF 10 ENTERED?
         BE    *+L'*+8             YES
         CLI   FSCODE,X'4A'        PF 22 ENTERED (ALT. PF 10)?
         BNE   *+L'*+10            NO
         MVC   FSTEXT(3),=CL3'H L'
         B     FSC40
         CLI   FSCODE,X'7B'        PF 11 ENTERED?
         BE    *+L'*+8             YES
         CLI   FSCODE,X'4B'        PF 23 ENTERED (ALT. PF 11)?
         BNE   *+L'*+10            NO
         MVC   FSTEXT(3),=CL3'H M'
         B     FSC40
         CLI   FSCODE,X'F5'        PF 5 ENTERED?
         BE    *+L'*+8             YES
         CLI   FSCODE,X'C7'        PF 17 ENTERED (ALT. PF 5)?
         BNE   FSC24               NO
         TM    SWITCH,POPXMR
         BO    *+L'*+10
         MVC   FSTEXT(3),=CL3'F  '
         B     FSC23
         MVC   FSTEXT(3),=CL3'X F'
FSC23    XC    FSTEXT+3(L'FSTEXT-3),FSTEXT+3
         B     FSC30
FSC24    CLI   FSCODE,X'F6'        PF 6 ENTERED?
         BE    *+L'*+8             YES
         CLI   FSCODE,X'C6'        PF 18 ENTERED (ALT. PF 6)?
         BNE   FSC25               NO
         TM    SWITCH,POPXMR
         BO    *+L'*+10
         MVC   FSTEXT(3),=CL3'G  '
         B     FSC23
         MVC   FSTEXT(3),=CL3'X G'
         B     FSC23
FSC25    CLI   FSCODE,X'F3'        PF 3 ENTERED?
         BE    FSC26               YES
         CLI   FSCODE,X'C3'        PF 15 ENTERED (ALT. PF 3)?
         BE    FSC26               YES
         CLI   FSCODE,X'F4'        PF 4 ENTERED?
         BE    FSC26               YES
         CLI   FSCODE,X'C4'        PF 16 ENTERED (ALT. PF 4)?
         BNE   *+L'*+10            NO
FSC26    MVC   FSTEXT(3),=CL3'END'
         B     FSC40
         CLI   FSCODE,X'F7'        PF 7 ENTERED?
         BE    *+L'*+8             YES
         CLI   FSCODE,X'C7'        PF 19 ENTERED (ALT. PF 7)?
         BNE   *+L'*+10            NO
         MVC   FSTEXT(5),=CL5'- 128'
         B     FSC27
         CLI   FSCODE,X'F8'        PF 8 ENTERED?
         BE    *+L'*+8             YES
         CLI   FSCODE,X'C8'        PF 20 ENTERED (ALT. PF 8)?
         BNE   FSC30               NO
         MVC   FSTEXT(5),=CL5'+ 128'
FSC27    XC    FSTEXT+5(L'FSTEXT-5),FSTEXT+5
FSC30    LA    R14,FSCRLNS
         LA    R15,16              RESET LINES 5 TO 20
FSC31   $FS    SF=(PROT),MF=(I,(R14))
         MVC   2(79,R14),ALLBLKS
         LA    R14,81(R14)
         BCT   R15,FSC31
         LA    R14,FSCRLST
         LA    R15,3               RESET LINES 22 TO 24
FSC32   $FS    SF=(PROT),MF=(I,(R14))
         MVC   2(79,R14),ALLBLKS
         LA    R14,81(R14)
         BCT   R15,FSC32
         MVC   FSCRSCL(L'FSCRSCL),ALLBLKS    RESET SCALING
        $FS    SF=(PROT),MF=(I,FSCRSDC)
         MVC   FSCRTDC(L'FSCRTDC),ALLBLKS    RESET DATA CONTINUE
         B     FSC40+L'FSC40
FSC40    XC    FSTEXT+3(L'FSTEXT-3),FSTEXT+3
         MVC   ANSR(L'ANSR),ALLBLKS
         OC    ANSR(L'FSTEXT),FSTEXT
         BR    R8                  RETURN TO CALLER
         EJECT
***********************************************************************
***********************************************************************
***      DANGEROUS ROUTINES : ENABLE/DISABLE ACTIVE/DEACTIV.        ***
***      REGISTERS USE : R0-R1-R14-R15 - WORK REGISTERS             ***
***                      R8 - LINK REGISTER                         ***
***********************************************************************
***********************************************************************
         SPACE 1
ENABLE  $HEDIT
***********************************************************************
*        WHEN IN SUPERVISOR MODE OR X-MEM REF. MODE :                 *
*              R6 = START ADDRESS TO BE ACCESSED                      *
*              R2 = NUMBER OF BYTES TO BE ACCESSED (FIELD LENGTH)     *
*        RETURN TO CALLER :                                           *
*              R8 - LINK REGISTER +0 = X-MEM REF. MODE                *
*                                 +4 = PROBLEM OR SUPERVISOR MODE     *
***********************************************************************
         SPACE 1
         TM    STATUS,TESTRUN
         BZ    ENBLE1+L'ENBLE1
         CLI   RCVIND,C' '
         BE    ENBLE1+L'ENBLE1
         LA    R0,L'RCVIND-1
         LA    R1,RCVIND+L'RCVIND-2
         MVC   1(1,R1),0(R1)
         BCTR  R1,0
         BCT   R0,*-8
ENBLE1   MVI   RCVIND,C' '
         L     R1,=A(ESPLST)
        ESPIE  SET,MF=(E,(1))      INTERCEPT 0C4
         ST    R1,OLDPICA          OLD PICA ADDRESS
         TM    STATUS,DANGER
         BZ    4(R8)               EXIT ---------------------------> +4
        ZEROKEY ,                  ENTER SYSTEM MODE
         TM    SWITCH,ACTIVM+XMEMRF     ACTIVE MODE + X-MEM.REF.?
         BO    ENBLE5              YES
         XR    R1,R1
         TPROT 0(R6),0(R1)         TEST PROTECTION/TRANSLATION
         BZ    ENBLE4              OK
         BP    RECOVER2            NO ACCESS, SHOW 0C4
         BO    RECOVER3            NO TRANSLATION, SHOW 0C4
         CLI   PRVOP,C'S'          ONLY FETCH, IS IT A STORE REQUESTED?
         BE    RECOVER1            YES, BUT PROTECTED, SO SHOW 0C4
ENBLE4   TM    SWITCH,TSTEAP       END ADDRESS PROTECTION REQUESTED?
         BZ    4(R8)               NO, EXIT -----------------------> +4
         LR    R0,R6
         ALR   R6,R2               COMPUTE END ADDRESS
         BCTR  R6,0
         TPROT 0(R6),0(R1)         TEST PROTECTION/TRANSLATION
         LR    R6,R0
         BZ    4(R8)               OK, EXIT -----------------------> +4
         BP    RECOVER5            NO ACCESS, SHOW 0C4
         BO    RECOVER6            NO TRANSLATION, SHOW 0C4
         CLI   PRVOP,C'S'          ONLY FETCH, IS IT A STORE REQUESTED?
         BE    RECOVER4            YES, BUT PROTECTED, SO SHOW 0C4
         B     4(R8)               NO, EXIT -----------------------> +4
ENBLE5   L     R1,CVTPTR           POINT TO CVT
         USING CVT,R1
         L     R14,CVTASVT         POINT TO ASVT
         DROP  R1
         L     R1,WASID            GET SECONDARY WORK AS-ID
         USING ASVT,R14
         C     R1,ASVTMAXU         IS AS-ID TOO BIG?
         BH    ERR29               YES
         SLL   R1,2                (AS-ID)*4
         LA    R1,ASVTENTY-4(R1)   POINT AT ENTRY
         TM    0(R1),ASVTAVAL      IS IT AVAILABLE?
         BO    ERR30               NO
         DROP  R14
         L     R15,0(R1)           POINT TO ASCB
         USING ASCB,R15
         TM    ASCBRCTF,ASCBOUT    IS ASCB SWAPPED OUT?
         BO    ERR31               YES
         DROP  R15
         L     R15,ACSQA           COMMON SQA AREA
         LTR   R15,R15             ALREADY BUILDED?
         BNZ   ENBLE6              YES
        GETMAIN R,LV=CSQAL,SP=226  NO, GET IT
         ST    R1,ACSQA            START ADDRESS
         MVC   0(CSQALL,R1),CSQATXT     MOVE IN INSTRUCTIONS DATA
         LR    R15,R1              SET HIS ADDRESSABILITY
         OI    SWITCH,CSQAF        SET SQA AREA ACQUIRED
ENBLE6   L     R1,WASID            GET SECONDARY WORK AS-ID
         SSAR  R1                  SET IT AS SECONDARY
         LA    R0,2
         XR    R1,R1
         BASR  R14,R15             TEST ACCESS TO SECONDARY AS-ID
         LTR   R0,R0               WHAT ABOUT PROTECTION?
         BZ    ENBLE7              OK
         BM    *+L'*+8             ONLY FETCH
         BCT   R0,RECOVER9         NO TRANSLATION, SHOW 0C4
         B     RECOVER8            NO ACCESS, SHOW 0C4
         CLI   PRVOP,C'S'          IS IT A STORE REQUESTED?
         BE    RECOVER7            YES, BUT PROTECTED, SO SHOW 0C4
ENBLE7   TM    SWITCH,TSTEAP       END ADDRESS PROTECTION REQUESTED?
         BZR   R8                  NO, EXIT -----------------------> +0
         ST    R6,SVSADD
         ALR   R6,R2               COMPUTE END ADDRESS
         BCTR  R6,0
         LA    R0,2
         XR    R1,R1
         BASR  R14,R15             TEST ACCESS TO SECONDARY AS-ID
         L     R6,SVSADD
         LTR   R0,R0               WHAT ABOUT PROTECTION?
         BZR   R8                  OK, EXIT -----------------------> +0
         BM    *+L'*+8             ONLY FETCH
         BCT   R0,RECOVERC         NO TRANSLATION, SHOW 0C4
         B     RECOVERB            NO ACCESS, SHOW 0C4
         CLI   PRVOP,C'S'          IS IT A STORE REQUESTED?
         BE    RECOVERA            YES, BUT PROTECTED, SO SHOW 0C4
         BR    R8                  EXIT ---------------------------> +0
         SPACE 1
RECOVER1 BAS   R8,RECOVERS
RECOVER2 BAS   R8,RECOVERS
RECOVER3 BAS   R8,RECOVERS
RECOVER4 BAS   R8,RECOVERS
RECOVER5 BAS   R8,RECOVERS
RECOVER6 BAS   R8,RECOVERS
RECOVER7 BAS   R8,RECOVERS
RECOVER8 BAS   R8,RECOVERS
RECOVER9 BAS   R8,RECOVERS
RECOVERA BAS   R8,RECOVERS
RECOVERB BAS   R8,RECOVERS
RECOVERC BAS   R8,RECOVERS
RECOVERS TM    STATUS,TESTRUN
         BZ    RECOVER
         LA    R1,RECOVERS
         LA    R8,0(R8)
         SLR   R1,R8
         SRL   R1,2
         STC   R1,RCVIND           SET RECOVER INDICATOR
         TR    RCVIND(1),=C'CBA987654321'
         B     RECOVER
         SPACE 1
PRMODE   EQU   X'000'              PRIMARY A.S. MODE
SCMODE   EQU   X'100'              SECONDARY A.S. MODE
         USING *,R15
CSQATXT  SAC   SCMODE              SET A.S. CONTROL TO SECONDARY
         TPROT 0(R6),0(R1)         TEST PROTECTION/TRANSLATION
         BZ    *+L'*+10            FETCH + STORE (R0=0)
         BP    *+L'*+8             NONE (R0=+1)
         BO    *+L'*+6             NO TRANSLATION (R0=+2)
         BCTR  R0,0                ONLY FETCH (R0=-1)
         BCTR  R0,0
         BCTR  R0,0
         SAC   PRMODE              RESTORE A.S. CONTROL TO PRIMARY
         BR    R14                 BACK TO CALLER
         DROP  R15
CSQALL   EQU   *-CSQATXT           LENGTH
CSQAL    EQU   ((CSQALL+7)/8)*8    GETMAIN/FREEMAIN LENGTH
         EJECT
DISABLE $HEDIT
         NI    SWITCH,255-TSTEAP   RESET E.A. PROT. REQUEST
         TM    STATUS,DANGER
         BZ    DSBLE2
         TM    SWITCH,ACTIVM+XMEMRF     ACTIVE MODE + X-MEM.REF.?
         BNO   DSBLE1              NO
         L     R1,SASID            RESTORE SECONDARY AS-ID
         SSAR  R1                  SHOULD BE AS PRIMARY
         NI    SWITCH,255-XMEMRF   RESET X-MEM. REF. REQUEST
DSBLE1  RESETKEY ,                 RESTORE PROBLEM MODE
DSBLE2  ESPIE  RESET,OLDPICA       RESTORE SPIE/ESPIE ENVIRONMENT
         BR    R8
         EJECT
ACTIVE  $HEDIT ACTIVE
         TM    SWITCH,ACTIVM       ALREADY ACTIVE MODE?
         BOR   R8                  YES, GO BACK, DON'T DO IT TWICE
         XC    WASID,WASID         SET SECONDARY WORK AS-ID
         MVC   WASID+2(L'ASIDN),ASIDN
        ZEROKEY ,                  ENTER SYSTEM MODE
         MVC   AXPL,=XL4'00010000'
        AXRES  AXLIST=AXPL
         ST    R15,RCXR            SAVE RC
         LTR   R15,R15             SUCCESSFULL?
         BNZ   ACTIVE1             NO, GO BACK
         L     R0,=F'1'            FULLY AUTHORIZED AX
        AXSET  AX=(0)              SET AX TO ALLOW IT AS 2ND
         ST    R15,RCXS            SAVE RC
         LTR   R15,R15             SUCCESSFULL?
         BNZ   ACTIVE1             NO, GO BACK
         OI    SWITCH,ACTIVM       ACTIVE MODE READY
ACTIVE1 RESETKEY ,                 RESTORE PROBLEM MODE
         TM    SWITCH,ACTIVM       ACTIVE MODE SET?
         BOR   R8                  YES
         L     R15,=A(FSCLEAR)
         BASR  R8,R15              RESET FULL SCREEN IF APPLY
         OC    RCXR,RCXR           AXRES FAILED?
         BZ    ACTIVE2             NO, IT IS AXSET
        @SEND  XRFAIL
         L     R1,=A(DISRC)
         UNPK  19(9,R1),RCXR(5)
         B     EXIT                GO DISPLAY R15 (RC)
ACTIVE2 @SEND  XSFAIL
         L     R1,=A(DISRC)
         UNPK  19(9,R1),RCXS(5)
         B     EXIT                GO DISPLAY R15 (RC)
         EJECT
DEACTIV $HEDIT DEACTIV
         TM    SWITCH,ACTIVM       ACTIVE MODE?
         BZR   R8                  NO, GO BACK, NOTHING TO DO
        ZEROKEY ,                  ENTER SYSTEM MODE
        AXFRE  AXLIST=AXPL         FREE AX
         ST    R15,RCXF            SAVE RC
        RESETKEY ,                 RESTORE PROBLEM MODE
         NI    SWITCH,255-ACTIVM   RESET ACTIVE MODE
         XC    WASID,WASID         ZEROES WORK AS-ID
         OC    RCXF,RCXF           AXFRE FAILED?
         BZR   R8                  NO
         L     R15,=A(FSCLEAR)
         BASR  R8,R15              RESET FULL SCREEN IF APPLY
        @SEND  XFFAIL
         L     R1,=A(DISRC)
         UNPK  19(9,R1),RCXF(5)
EXIT     TR    19(8,R1),HEXTAB-X'F0'
         MVI   27(R1),C' '
        @SEND  OP=NONE
         B     FINISH              ABORT PROCESS
         EJECT
***********************************************************************
*        LITERAL POOL.                                                *
***********************************************************************
         SPACE 1
        LTORG
         SPACE 1
*--- --- END OF BASE REGISTER (R12) ADDRESSING --- --- --- --- --- ---*
         EJECT
VCINIT  @INIT  INITIAL
         SPACE 1
***********************************************************************
*        CONTROL CALLER AUTHORITY AND ENVIRONMENT.                    *
***********************************************************************
        $EACM  REQAUT              GET USER AUTHORITY
         CLI   AUTH,LV1            AUTHORIZED?
         BNL   OKAUTH              YES
        @SEND  UNAUT
         B     FINISH
OKAUTH  EXTRACT ATSO,'S',FIELDS=(TSO)
         L     R1,ATSO
         TM    0(R1),X'80'         CALLED IN TSO?
         BZ    INMSG               NO
         OI    STATUS,TSORUN       YES
        GTSIZE ,                   GET TERMINAL SIZE
         LTR   R0,R0               IS IT A SCREEN?
         BZ    INMSG               IF ZERO, NOT A SCREEN
         CH    R0,=H'24'           STANDARD NUMBER OF LINES?
         BL    INMSG               LESS
         BE    TSTLNL              YES
         CH    R1,=H'80'           STANDARD LINE LENGTH?
         BL    INMSG               LESS
         B     SETSSZ
TSTLNL   CH    R1,=H'80'           STANDARD LINE LENGTH?
         BL    INMSG               LESS
         BE    SETFSC              YES
SETSSZ   ST    R0,SZSCRN           SAVE SCREEN SIZE
         ST    R1,SZLINE           SAVE LINE SIZE
SETFSC   OI    STATUS,TSORSC       SAY RUNNING IN TSO ON A SCREEN
         L     R15,=A(FSSETFS)
         BASR  R8,R15              SET FULL SCREEN
         B     SETMODE
INMSG   @SEND  INITM
        @SEND  INITH
         SPACE 2
***********************************************************************
*        SET MVS/XA AMODE.                                            *
***********************************************************************
SETMODE $TSWXA 31,EXPAND=ONLY      ENTER 31-BIT MODE
         EJECT
***********************************************************************
*        RECOGNIZE MYSELF.                                            *
***********************************************************************
         USING PSA,R0
MYSELF   L     R1,PSAAOLD
         ST    R1,MYASCB
         USING ASCB,R1
         MVC   MYASIDN,ASCBASID    GET MY AS-ID NUMBER
         L     R15,ASCBJBNI
         LTR   R15,R15
         BNZ   *+L'*+10
         L     R15,ASCBJBNS
         LTR   R15,R15
         BZ    *+L'*+6
         MVC   MYASNME(L'MYASNME),0(R15)     GET MY AS-NAME
         DROP  R0,R1
        ZEROKEY ,                  ENTER SYSTEM MODE
         ESAR  R1
         ST    R1,SASID            GET SECONDARY AS-ID
        RESETKEY ,                 RESTORE PROBLEM MODE
         EJECT
***********************************************************************
*        SET PROGRAM SPACE BOUNDARIES.                                *
***********************************************************************
         L     R0,=V(VCOREZAH)
         L     R1,BEGADD
         CLR   R0,R1
         BNL   *+L'*+2
         LR    R1,R0
         SRL   R1,4
         SLL   R1,4
         ST    R1,BEGADD
         L     R0,=V(VCOREZHE)
         L     R1,ENDADD
         CLR   R0,R1
         BNH   *+L'*+2
         LR    R1,R0
         LA    R1,15(R1)
         SRL   R1,4
         SLL   R1,4
         ST    R1,ENDADD
         SPACE 2
***********************************************************************
*        INITIALIZE TANKS USER-AREAS.                                 *
***********************************************************************
         LA    R1,NTKWAS
         L     R3,ATKWAS
         USING TKWA,R3
INT1     XC    TKWPT,TKWPT
         XC    TKWDL,TKWDL
         XC    TKWTX,TKWTX
         LA    R3,TKWAL(R3)
         BCT   R1,INT1
         DROP  R3
         EJECT
***********************************************************************
*        INITIALIZE PREDEFINED LABELS TABLE.                          *
***********************************************************************
         LM    R3,R5,SCFIX
         XR    R1,R1
         BAS   R8,STFIX
         DC    CL8'PSA'            PSA
         USING PSA,R0
         L     R1,PSAAOLD
         BAS   R8,STFIX
         DC    CL8'ASCB'           ASCB
         DROP  R0
         L     R2,CVTPTR
         LR    R1,R2
         BAS   R8,STFIX
         DC    CL8'CVT'            CVT
         USING CVT,R2
         L     R1,CVTTCBP
         L     R1,4(R1)
         BAS   R8,STFIX
         DC    CL8'TCB'            TCB
         L     R1,CVTSMCA
         BAS   R8,STFIX
         DC    CL8'SMCA'           SMCA
         L     R1,CVTABEND
         BAS   R8,STFIX
         DC    CL8'SCVT'           SCVT
         L     R1,CVTMSER
         BAS   R8,STFIX
         DC    CL8'BASE'           BASE
         L     R1,CVTIXAVL
         BAS   R8,STFIX
         DC    CL8'IOCOM'          IOCOM
         L     R1,CVTGDA
         BAS   R8,STFIX
         DC    CL8'GDA'            GDA
         L     R1,CVTCSD
         BAS   R8,STFIX
         DC    CL8'CSD'            CSD
         L     R1,CVTPCCAT
         BAS   R8,STFIX
         DC    CL8'PCCAVT'         PCCAVT
         L     R1,CVTLCCAT
         BAS   R8,STFIX
         DC    CL8'LCCAVT'         LCCAVT
         DROP  R2
         B     SRTFIX
         SPACE 1
STFIX    MVC   0(L'LBLF,R3),0(R8)
         STCM  R1,B'1111',L'LBLF(R3)
         ST    R3,SCFIX+8
         BXLE  R3,R4,8(R8)
SRTFIX   L     R5,SCFIX            SORT LABELS
         SR    R3,R5
         XR    R2,R2
         DR    R2,R4
        $SORT  (R5),(R3),L'FIXTB,0,L'LBLF
         B     INQUIRT
         SPACE 1
        @END   ,
         EJECT
VCEND   @INIT  LEAVE
         SPACE 1
***********************************************************************
*        END OF PROCESS.                                              *
***********************************************************************
         BAS   R8,DEACTIV          RESET AS-ID MODE IF ANY
         TM    SWITCH,CSQAF        SQA AREA ACQUIRED?
         BZ    NOSQA               NO
        ZEROKEY ,                  ENTER SYSTEM MODE
         L     R1,ACSQA            COMMON SQA AREA
        FREEMAIN R,A=(1),LV=CSQAL,SP=226     FREE IT
        RESETKEY ,                 RESTORE PROBLEM MODE
NOSQA    TM    HCPRSW,HCCOPY       IS HARDCOPY ACTIVE?
         BZ    NOHCPY              NO
         OI    HCPRSW,HCCLOSE      SAY CLOSE IT
         L     R10,=A(HCPRTSS)     CLOSE HARDCOPY FILE
         BR    R10
NOHCPY  $TSWXA 24,EXPAND=ONLY      BACK TO 24-BIT
         L     R15,=A(FSCLEAR)
         BASR  R8,R15              RESET FULL SCREEN IF APPLY
         TM    OPERSW,DRFDDS       DATA-SET ALREADY ENDED
         BZ    EXRET               YES
         LA    R1,=AL1(255)        NO, ENDS IT
         L     R15,=V(DTUSE)
         BASR  R14,R15
         OC    RETCC,RETCC
         BZ    EXRET+L'EXRET
         B     EXRET
         SPACE 1
        @END   ,
         EJECT
ENCMD   @INIT  COMMANDS
         SPACE 1
***********************************************************************
*        ENTER A COMMAND.                                             *
***********************************************************************
         SPACE 1
         TM    FSCRSW,FSCRSF       FULL SCREEN?
         BZ    *+L'*+4             NO
         NI    FSCRSW,255-FSCRSC-FSCRSH-FSCRST-FSCRSL-FSCRSI     RESET
         CLI   0(R3),C'?'          INFO STATUS
         BNE   *+L'*+6
         L     R10,=A(INFST)
         BR    R10
         CLI   0(R3),C'B'          DSECTS INFO
         BNE   *+L'*+6
         L     R10,=A(BRWSE)
         BR    R10
         CLI   0(R3),C'H'          HELP
         BNE   *+L'*+6
         L     R10,=A(HELP)
         BR    R10
         CLI   0(R3),C'I'          INTERNAL
         BNE   *+L'*+6
         L     R10,=A(PRDEF)
         BR    R10
         CLI   0(R3),C'L'          LIST
         BNE   *+L'*+8
         L     R10,=A(PRDEF)
         DROP  R10
         USING PRDEF,R10
         B     LIST
         DROP  R10
         USING ENCMD,R10
         CLI   0(R3),C'M'          MEMORIZE
         BNE   *+L'*+6
         L     R10,=A(MMTK)
         BR    R10
         CLI   0(R3),C'Q'          DSECTS DATA-SET
         BNE   *+L'*+8
         L     R10,=A(BRWSE)
         DROP  R10
         USING BRWSE,R10
         B     DSDST
         DROP  R10
         USING ENCMD,R10
         CLI   0(R3),C'X'          MEM. X-REF
         BNE   *+L'*+6
         L     R10,=A(ASNID)
         BR    R10
         TM    STATUS,TESTRUN
         BZ    *+L'*+6
         MVC   RCVIND(L'RCVIND),ALLBLKS
         CLI   0(R3),C'+'          FORWARD DISPLAY
         BE    *+L'*+8
         CLI   0(R3),C'-'          BACKWARD DISPLAY
         BNE   *+L'*+8
         L     R10,=A(DSPLY)
         DROP  R10
         USING DSPLY,R10
         B     FBDSPL
         DROP  R10
         USING ENCMD,R10
         NI    SWITCH,255-POPXMR-ASFULL RESET X-MEM. MODE AND A.S.
         CLI   0(R3),C'F'          FIND
         BNE   *+L'*+6
         L     R10,=A(FIND)
         BR    R10
         NI    SWITCH,255-PVALID   RESET PREVIOUS LAST DATA
         CLI   0(R3),C'A'          ASSIGN
         BNE   *+L'*+6
         L     R10,=A(ASSGN)
         BR    R10
         CLI   0(R3),C'C'          CONTROL
         BNE   *+L'*+8
         L     R10,=A(TEST)
         DROP  R10
         USING TEST,R10
         B     CNTRL
         DROP  R10
         USING ENCMD,R10
         CLI   0(R3),C'V'          VIEW
         BNE   *+L'*+8
         L     R10,=A(TEST)
         DROP  R10
         USING TEST,R10
         B     SGVIEW
         DROP  R10
         USING ENCMD,R10
         CLI   0(R3),C'D'          DISPLAY
         BNE   *+L'*+6
         L     R10,=A(DSPLY)
         BR    R10
         CLI   0(R3),C'E'          END
         BE    FINISH
         TM    FSCRSW,FSCRSF       FULL SCREEN?
         BZ    *+L'*+16            NO
         CLI   0(R3),C'G'          GET
         BNE   *+L'*+8
         L     R10,=A(DSPLY)
         DROP  R10
         USING DSPLY,R10
         B     GBYTES
         DROP  R10
         USING ENCMD,R10
         CLI   0(R3),C'P'          HARDCOPY
         BNE   *+L'*+6
         L     R10,=A(HCPRTSS)
         BR    R10
         CLI   0(R3),C'S'          STORE
         BNE   *+L'*+6
         L     R10,=A(STORE)
         BR    R10
         CLI   0(R3),C'T'          TEST
         BNE   *+L'*+6
         L     R10,=A(TEST)
         BR    R10
         CLI   0(R3),C'Z'          DELETE
         BNE   *+L'*+8
         L     R10,=A(ASSGN)
         DROP  R10
         USING ASSGN,R10
         B     DELET
         DROP  R10
         USING ENCMD,R10
         B     ERR1
         SPACE 1
        @END   ,
         EJECT
INFST   @INIT  INFO''S
         SPACE 1
***********************************************************************
*        INFO STATUS REQUEST.                                         *
***********************************************************************
         SPACE 1
         TM    FSCRSW,FSCRSF       FULL SCREEN?
         BZ    *+L'*+10            NO
INFST0   OI    FSCRSW,FSCRST       YES, SAY STATUS
         XC    FSPOINT,FSPOINT
         MVC   MSGTXT(L'MSGTXT),ALLBLKS
         MVC   MSGTXT(7),=CL7'AS-ID ='
         UNPK  MSGTXT+8(5),MYASIDN(3)
         TR    MSGTXT+8(4),HEXTAB-X'F0'
         MVC   MSGTXT+12(2),=CL2' ('
         MVC   MSGTXT+14(L'MYASNME),MYASNME
         MVC   MSGTXT+22(12),=CL12') : DISABLED'
         TM    STATUS,DANGER
         BZ    *+L'*+6
         MVC   MSGTXT+26(8),=CL8'ENABLED '
         MVC   MSGTXT+35(9),=CL9'/ SGT-OFF'
         TM    SGTPSW,STPDIS+STPUSE     SEGMENT PROCESSING DISABLED?
         BNZ   *+L'*+6             YES
         MVC   MSGTXT+42(2),=CL2'N '
         TM    STATUS,TESTRUN
         BZ    *+L'*+6
         MVC   MSGTXT+45(6),=CL6'/ TEST'
         MVC   MSGTXT+54(7),=CL7'D-PT. ='
         CLI   PRVOP,C'S'
         BE    INFST1
         UNPK  MSGTXT+62(9),CRPTR(5)
         B     INFST2
INFST1   MVC   MSGTXT+52(2),=CL2'F/'
         UNPK  MSGTXT+62(9),PVPTR(5)
INFST2   TR    MSGTXT+62(8),HEXTAB-X'F0'
         MVI   MSGTXT+70,C' '
        @SEND  MSGOUT,OP=LA
         MVC   MSGTXT(L'MSGTXT),ALLBLKS
         MVC   MSGTXT(10),=CL10'F-STRING ='
         XR    R15,R15
         IC    R15,STRNGL
         TM    STATUS,FTYPE
         BZ    INFST10
         MVI   MSGTXT+11,C''''
         LA    R1,MSGTXT+12
         LA    R14,STRNG
         EX    R15,INFSTMV
         LA    R1,MSGTXT+13(R15)
         MVI   0(R1),C''''
         LA    R1,1(R1)
         B     INFST11
INFST10  UNPK  TBUF(15),STRNG(8)
         UNPK  TBUF+14(15),STRNG+7(8)
         UNPK  TBUF+28(5),STRNG+14(3)
         TR    TBUF(32),HEXTAB-X'F0'
         SLL   R15,1
         LA    R15,1(R15)
         LA    R14,TBUF
         LA    R1,MSGTXT+11
         EX    R15,INFSTMV
         LA    R1,MSGTXT+12(R15)
INFST11  TM    SWITCH,ASFULL
         BZ    *+L'*+6
         MVC   1(6,R1),=CL6'(FULL)'
         MVC   MSGTXT+54(7),=CL7'S-PT. ='
         CLI   PRVOP,C'S'
         BE    INFST12
         MVC   MSGTXT+52(2),=CL2'F/'
         UNPK  MSGTXT+62(9),PVPTR(5)
         B     INFST13
INFST12  UNPK  MSGTXT+62(9),CRPTR(5)
INFST13  TR    MSGTXT+62(8),HEXTAB-X'F0'
         MVI   MSGTXT+70,C' '
        @SEND  MSGOUT,OP=LA
         TM    STATUS,DANGER
         BO    *+L'*+8
         TM    STATUS,TESTRUN
         BZ    INFST30
         MVC   MSGTXT(L'MSGTXT),ALLBLKS
         TM    SWITCH,VASTB
         BO    INFST20
         MVC   MSGTXT(18),=CL18'NO VALID AS-TABLE.'
         B     INFST22
INFST20  TM    SWITCH,ACTIVM
         BO    INFST21
         MVC   MSGTXT(21),=CL21'NO AS-ID MODE ACTIVE.'
         B     INFST22
INFST21  MVC   MSGTXT(20),=CL20'ACTIVE AS-ID MODE IS'
         UNPK  MSGTXT+21(5),ASIDN(3)
         TR    MSGTXT+21(4),HEXTAB-X'F0'
         MVC   MSGTXT+25(2),=CL2' ('
         MVC   MSGTXT+27(L'ASNME),ASNME
         MVC   MSGTXT+35(2),=CL2').'
INFST22  TM    STATUS,TESTRUN
         BZ    INFST23
         CLC   RCVIND(L'RCVIND),ALLBLKS
         BE    INFST23
         MVC   MSGTXT+50(11),=CL11'TRACE 0C4 :'
         MVC   MSGTXT+62(L'RCVIND),RCVIND
INFST23  TM    FSCRSW,FSCRSF       FULL SCREEN?
         BZ    *+L'*+4             NO
         OI    FSCRSW,FSCRSI       SAY INTENSIVE LINE
        @SEND  MSGOUT,OP=LA
INFST30  TM    FSCRSW,FSCRSF       FULL SCREEN?
         BZ    INFST31             NO
         NI    FSCRSW,255-FSCRST   RESET STATUS
         XC    FSPOINT,FSPOINT
         B     INQUIRY
INFST31  L     R1,CATKWA
         LTR   R1,R1
         BZ    INQUIRY
         MVC   MSGTXT(L'MSGTXT),ALLBLKS
         USING TKWA,R1
         MVC   MSGTXT(11),=CL11'USER-AREA :'
         MVC   MSGTXT+12(L'CTKWAN),CTKWAN
         MVC   MSGTXT+L'CTKWAN+13(10),=CL10'- LENGTH :'
         UNPK  DBLWD(5),TKWDL+2(3)
         TR    DBLWD+1(3),HEXTAB-X'F0'
         MVC   MSGTXT+L'CTKWAN+23(3),DBLWD+1
         MVC   MSGTXT+L'CTKWAN+27(11),=CL11'- POINTER :'
         UNPK  DBLWD(5),TKWPT+2(3)
         TR    DBLWD+2(2),HEXTAB-X'F0'
         MVC   MSGTXT+L'CTKWAN+39(2),DBLWD+2
         DROP  R1
        @SEND  MSGOUT,OP=LA
         B     INQUIRY
INFSTMV  MVC   0(*-*,R1),0(R14)    <<EXECUTED>>
         SPACE 1
        @END   ,
         EJECT
HELP    @INIT  ,
         SPACE 1
***********************************************************************
*        HELP COMMAND.                                                *
***********************************************************************
         SPACE 1
         MVC   CRPTR,PVPTR         ADJUST POINTERS FOR AFTER
         LR    R1,R3               RETAIN ACTUAL POSITION
         TM    FSCRSW,FSCRSF       FULL SCREEN?
         BZ    HELPNF              NO
         OI    FSCRSW,FSCRSH       YES, SAY HELP PANEL
         BAS   R14,NEXTCH          POSITION TO NEXT
         B     HELPDF              +0 - DEFAULT
         CLI   0(R3),C'X'          +4 - CROSS MEMORY?
         BE    HELPXM              YES
         CLI   0(R3),C'L'          LABELS AND DSECTS?
         BE    HELPLS              YES
         CLI   0(R3),C'M'          MISCELLANEOUS?
         BE    HELPMS              YES
HELPDF   L     R1,=V(VCOREZAH)
         USING HELPVP,R1
         LM    R2,R4,HLDF
         LM    R14,R0,HLAUTHF
HELPFLL  CLI   AUTH,LV5            AUTHORIZED?
         BL    *+L'*+22            NO
         MVC   0(HLRESL,R15),0(R14)
         LA    R14,HLRESL(R14)
         LA    R15,HLRESL(R15)
         BCT   R0,*-14
         B     INQUIRY
         MVC   2(HLRESL-2,R15),ALLBLKS
         LA    R14,HLRESL(R14)
         LA    R15,HLRESL(R15)
         BCT   R0,*-14
         B     INQUIRY
HELPMS   L     R1,=V(VCOREZAH)
         LM    R2,R4,HLMS
         LM    R14,R0,HLAUTHM
         B     HELPFLL
HELPLS   L     R1,=V(VCOREZAH)
         LM    R2,R4,HLLS
         B     INQUIRY
         DROP  R1
HELPNF   BAS   R14,NEXTCH          POSITION TO NEXT
         B     HELPDL              +0 - DEFAULT
         CLI   0(R3),C'X'          +4 - CROSS MEMORY?
         BE    HELPXM              YES
         CLI   0(R3),C'L'          LABELS AND DSECTS?
         BE    HELPHL              YES
         CLI   0(R3),C'M'          MISCELLANEOUS?
         BE    HELPHM              YES
        PRINT  NOGEN
HELPDL  @SEND  HELP01
        @SEND  HELP02
        @SEND  HELP03
        @SEND  HELP04
        @SEND  HELP05
        @SEND  HELP06
        @SEND  HELP07
        @SEND  HELP08
        @SEND  HELP09
        @SEND  HELP10
        @SEND  HELP11
        @SEND  HELP12
        @SEND  HELP13
        @SEND  HELP14
        @SEND  HELP15
        @SEND  HELP16
        @SEND  HELP17
        @SEND  HELP18
        @SEND  HELP19
        @SEND  HELP20
        @SEND  HELP21
         CLI   AUTH,LV5            X-MEM AUTHORIZED?
         BL    INQUIRY             NO
        @SEND  HELP22
        @SEND  HELP23
        @SEND  HELP24
        @SEND  HELP25
        @SEND  HELP26
         B     INQUIRY
HELPHL  @SEND  HELP30
        @SEND  HELP31
        @SEND  HELP32
        @SEND  HELP33
        @SEND  HELP34
        @SEND  HELP35
        @SEND  HELP36
        @SEND  HELP37
        @SEND  HELP38
        @SEND  HELP39
        @SEND  HELP40
        @SEND  HELP41
        @SEND  HELP42
        @SEND  HELP43
        @SEND  HELP44
        @SEND  HELP45
        @SEND  HELP46
        @SEND  HELP47
        @SEND  HELP48
        @SEND  HELP49
        @SEND  HELP50
         B     INQUIRY
HELPHM  @SEND  HELP60
        @SEND  HELP61
        @SEND  HELP62
        @SEND  HELP63
        @SEND  HELP64
        @SEND  HELP65
        @SEND  HELP66
        @SEND  HELP67
        @SEND  HELP68
        @SEND  HELP69
         CLI   AUTH,LV5            AUTHORIZED?
         BL    INQUIRY             NO
        @SEND  HELP70
         B     INQUIRY
HELPXM   MVI   0(R1),C'X'          SET X H COMMAND
         MVI   0(R3),C'H'
         LR    R3,R1
         L     R10,=A(ASNID)
         BR    R10
        PRINT  GEN
         SPACE 1
        @END   ,
         EJECT
DSPLY   @INIT  DISPLAY
         SPACE 1
***********************************************************************
*        DISPLAY COMMAND.                                             *
***********************************************************************
         SPACE 1
         BAS   R14,NEXTCH     POSITION TO NEXT
         B     DS11           +0
         BAS   R14,LABEL      +4 - GET LABEL
         B     DS11                +0
         B     DS2                 +4 - MAY BE HEX ADDRESS
         B     ERR6                +8
         B     DS3                 +12
         BAS   R14,NEXT            +16 - POSITION TO NEXT
         B     DS1                       +0
         CLI   0(R3),C'/'                +4
         BNE   DS1
         BAS   R14,NEXTCH     POSITION TO NEXT
         B     ERR37          +0
         BAS   R14,NAMER      +4 - GET DSECT-NAME
         B     ERR37               +0
         B     ERR38               +4
         B     ERR39               +8
         MVC   DRFDSCT,NMEF        +12 - DSECT NAME
         MVC   DRFFLDN,LBLF   FIELD NAME
         MVC   LBLF,NMEF
         BAS   R14,SCITBL     PREDEFINED LABELS
         B     *+L'*+4        +0
         BAS   R14,SCLTBL     +4 - LABELS
         ICM   R6,B'1111',L'LBLF(R7)    SET ADDRESS
         XC    DRFDSPL(L'DRFDSPL+L'DRFLNGH),DRFDSPL
         MVC   DRFFLGN(L'DRFFLGN),ALLBLKS
         XC    DRFFLVL(L'DRFFLVL+L'DRFFLLG),DRFFLVL
         LA    R0,1           ASK SEARCH
         L     R15,=A(DRFCALL)
         BASR  R14,R15
         B     *+L'*(R15)
         B     *+L'*+8        +0
         B     *+L'*+4        +4
         B     ERROR          +8
         XR    R0,R0
         ICM   R0,B'0011',DRFDSPL  SET DISPLACEMENT
         ALR   R6,R0          COMPUTE ADDRESS
         B     DS3+L'DS3
DS1      BAS   R14,SCLTBL
         B     DS3
DS2      CLI   0(R3),C'.'     HEX ADDRESS?
         BNE   *+L'*+8
         BAS   R14,NEXTCH     POSITION TO NEXT
         B     ERR2           +0
         BAS   R14,SCAN       +4 - GET ADDRESS
         B     ERR2                +0
         B     ERR17               +4
         B     ERR3                +8
         L     R6,DATA             +12 - SET ADDRESS
         B     DS3+L'DS3
DS3      ICM   R6,B'1111',L'LBLF(R7)    SET ADDRESS
         NI    STATUS,255-SUBSTRCT
         BAS   R14,NEXT       POSITION TO NEXT
         B     DS12           +0 - NO DISPLACEMENT
         CLI   0(R3),C'+'     +4 - TEST IF DELIMITER
         BE    DS4
         CLI   0(R3),C'-'
         BNE   ERR10
         OI    STATUS,SUBSTRCT
DS4      BAS   R14,NEXTCH     POSITION TO NEXT
         B     ERR9           +0
         BAS   R14,LABEL      +4 - GET DISPLACEMENT
         B     ERR9                +0
         B     DS6                 +4 - MAY BE HEX DISPLACEMENT
         B     ERR6                +8
         B     DS5+L'DS5           +12
         BAS   R14,NEXT            +16 - POSITION TO NEXT
         B     DS5                       +0
         CLI   0(R3),C'/'                +4
         BNE   ERR37
         BAS   R14,NEXTCH     POSITION TO NEXT
         B     ERR37          +0
         BAS   R14,NAMER      +4 - GET DSECT-NAME
         B     ERR37               +0
         B     ERR38               +4
         B     ERR39               +8
         MVC   DRFDSCT,NMEF        +12 - DSECT NAME
         MVC   DRFFLDN,LBLF   FIELD NAME
         XC    DRFDSPL(L'DRFDSPL+L'DRFLNGH),DRFDSPL
         MVC   DRFFLGN(L'DRFFLGN),ALLBLKS
         XC    DRFFLVL(L'DRFFLVL+L'DRFFLLG),DRFFLVL
         LA    R0,1           ASK SEARCH
         L     R15,=A(DRFCALL)
         BASR  R14,R15
         B     *+L'*(R15)
         B     *+L'*+8        +0
         B     *+L'*+4        +4
         B     ERROR          +8
         XR    R0,R0
         ICM   R0,B'0011',DRFDSPL  SET DISPLACEMENT
         B     DS10
DS5      BAS   R14,SCLTBL
         ICM   R0,B'1111',L'LBLF(R7)    SET DISPLACEMENT
         B     DS10
DS6      CLI   0(R3),C'.'     HEX DISPLACEMENT?
         BNE   *+L'*+8
         BAS   R14,NEXTCH     POSITION TO NEXT
         B     ERR9           +0
         BAS   R14,SCAN       +4 - GET DISPLACEMENT
         B     ERR9                +0
         B     ERR17               +4
         B     ERR3                +8
         L     R0,DATA             +12
DS10     TM    STATUS,SUBSTRCT     APPLY DISPLACEMENT
         BO    *+L'*+6
         ALR   R6,R0
         B     *+L'*+2
         SLR   R6,R0
         LA    R6,0(R6)
         B     DS12
DS11     CLI   PRVOP,C'S'
         BNE   *+L'*+8
         L     R6,PVPTR
         B     DS12
         L     R6,CRPTR
DS12     LA    R0,FSRPLYL     RETAIN DISPLAY INPUT COMMAND IF NOT
         LA    R1,ANSR             ONLY A CONTINUE D (RESHOW)
         CLI   0(R1),C' '
         BNE   *+L'*+12
         LA    R1,1(R1)
         BCT   R0,*-12
         B     DSPLY0
         LA    R15,ANSR+FSRPLYL-1
         CLI   0(R15),C' '
         BNE   *+L'*+4
         BCT   R15,*-8
         SR    R15,R1
         BNP   DSPLY0
         LA    R0,1(R15)
         LR    R14,R1
         CLI   0(R14),C'X'
         BNE   DS14
DS13     LA    R14,1(R14)
         BCT   R0,*+L'*+4
         B     DSPLY0
         CLI   0(R14),C' '
         BE    DS13
DS14     CLI   0(R14),C'D'
         BNE   DSPLY0
DS15     LA    R14,1(R14)
         BCT   R0,*+L'*+4
         B     DSPLY0
         CLI   0(R14),C' '
         BE    DS15
         XC    LSTDSP,LSTDSP
         EX    R15,*+L'*+4
         B     DSPLY0
         MVC   LSTDSP(*-*),0(R1)   <<EXECUTED>>
FBDSPL   TM    SWITCH,POPXMR  WAS PREVIOUS OP. X-MEM. REF. MODE?
         BZ    *+L'*+4        NO
         OI    SWITCH,XMEMRF  YES, CONTINUE IN X-MEM. REF. MODE
         NI    SWITCH,255-ASFULL-PVALID RESET
         NI    STATUS,255-SUBSTRCT
         CLI   0(R3),C'-'
         BNE   *+L'*+4
         OI    STATUS,SUBSTRCT
         BAS   R14,NEXTCH     POSITION TO NEXT
         B     ERR2           +0
         LA    R0,15          +4 - GET NUMBER OF BYTES (1 TO 999)
         XR    R1,R1
         XR    R2,R2
FB1      CLI   0(R3),C'0'
         BL    ERR4
         CLI   0(R3),C'9'
         BH    ERR4
         IC    R1,0(R3)
         NR    R1,R0
         LTR   R2,R2
         BNP   *+L'*+4
         MH    R2,=H'10'
         AR    R2,R1
         CH    R2,=H'999'
         BH    ERR4
         BXLE  R3,R4,*+L'*+4
         B     *+L'*+8
         CLI   0(R3),C' '
         BNE   FB1
         LTR   R2,R2
         BNP   ERR4
         CLI   PRVOP,C'S'
         BE    *+L'*+8
         L     R6,PVPTR
         B     *+L'*+4
         L     R6,CRPTR
         TM    STATUS,SUBSTRCT     APPLY DISPLACEMENT
         BO    *+L'*+6
         ALR   R6,R2
         B     *+L'*+2
         SLR   R6,R2
         LA    R6,0(R6)
         B     DSPLY0
GBYTES   LA    R6,3           SET DEFAULT (FOUR BYTES)
         BAS   R14,NEXTCH     POSITION TO NEXT
         B     GB2            +0 - DEFAULT
         LR    R2,R3          +4 - GET NUMBER OF BYTES (1-16)
GB1      CLI   0(R3),C'0'
         BL    ERR48
         CLI   0(R3),C'9'
         BH    ERR48
         BXLE  R3,R4,*+L'*+4
         B     *+L'*+8
         CLI   0(R3),C' '
         BNE   GB1
         LR    R1,R3
         SR    R1,R2
         CH    R1,=H'8'
         BH    ERR48
         BCT   R1,*+L'*+6
         PACK  DBLWD,0(*-*,R2)
         EX    R1,*-6
         CVB   R6,DBLWD
         LTR   R6,R6
         BNP   ERR48
         CH    R6,=H'16'
         BH    ERR48
         BCTR  R6,0
GB2      STC   R6,GNBYT
         OI    OPERSW,GETBSW
         CLI   PRVOP,C'D'
         BNE   *+L'*+8
         L     R6,PVPTR
         B     DSPLY0
         L     R6,CRPTR
DSPLY0   ST    R6,DATA
         MVI   PRVOP,C'D'
DSPLY1   MVC   ADVICE(L'ADVICE),ALLBLKS
         TM    SWITCH,ACTIVM+XMEMRF ACTIVE MODE + X-MEM.REF.?
         BO    DSPLY2         YES
         TM    STATUS,DANGER
         BO    *+L'*+8
         TM    STATUS,TESTRUN
         BO    DSPLY2
         CL    R6,BEGADD
         BL    DSPLY2
         CL    R6,ENDADD
         BL    ERR23
DSPLY2   ST    R6,RCVPTR
         L     R3,=A(HXDATA)  ADDRESS OF MOVE (=BLDATA)
         TM    FSCRSW,FSCRSF  FULL SCREEN?
         BO    *+L'*+8        YES
         L     R2,=A(L'HXDATA)     LENGTH OF MOVE
         B     DS20
         SRL   R6,7           SET START OF BLOCK
         SLL   R6,7
         ST    R6,BLKPTR
         L     R2,=A(L'BLDATA)     LENGTH OF MOVE
         LA    R4,L'BLDATA/L'HXDATA     NUMBER OF LINES TO DISPLAY
         OI    FSPRSW,FSPRDS  SAY FULL SCREEN DISPLAY TO RECOVER
         STM   R0,R15,FSRCVR  SET REGISTERS IN CASE OF RECOVER
DS20     OI    SWITCH,TSTEAP  SET E.A. PROT. REQUEST
DSPLY3   BAS   R8,ENABLE ----------------------------------- * DANGER *
         B     DS21           +0 - X-MEM. REF.                        *
         BCTR  R2,0           +4 - GET DATA AT ADDRESS                *
         EX    R2,*+L'*+4                                             *
         B     DS23                                                   *
         MVC   0(*-*,R3),0(R6)     <<EXECUTED>>                       *
DS21     XR    R15,R15        FROM KEY                                *
DS22     MVCP  0(R2,R3),0(R6),R15  GET DATA AT ADDRESS                *
         BZ    DS23                                                   *
         AL    R6,=F'256'     BUMP FROM ADDRESS                       *
         AL    R3,=F'256'     BUMP TO ADDRESS                         *
         SL    R2,=F'256'     DECREMENT THE LENGTH                    *
         B     DS22           GO BACK AND GET MORE                    *
DS23     BAS   R8,DISABLE ---------------------------------- * DANGER *
         TM    FSCRSW,FSCRSF  FULL SCREEN?
         BO    DS30           YES
DSPLY4   CLI   PRVOP,C'F'
         BNE   *+L'*+10
         XR    R7,R7
         IC    R7,STRNGL      BUMP WITH LENGTH OF STRING
         B     *+L'*+8
         CLI   PRVOP,C'S'
         BNE   *+L'*+8
         LA    R1,1(R6,R7)    BUMP WITH LENGTH OF STORE
         B     *+L'*+4
         LA    R1,L'HXDATA(R6)
         ST    R1,CRPTR       CURRENT POINTER
         ST    R6,PVPTR       PREVIOUS POINTER
         LA    R2,MSGTXT
         MVC   MSGTXT(L'MSGTXT),ALLBLKS
         USING CONSLOUT,R2
         UNPK  CONSL1(L'CONSL1+1),DATA(5)
         TR    CONSL1(L'CONSL1),HEXTAB-X'F0'
         MVI   CONSL1+L'CONSL1,C' '
         MVI   CONSL2,C':'
         L     R1,=A(HXDATA)  ADDRESS OF MOVED DATA
         UNPK  CONSL3(L'CONSL3+1),0(5,R1)
         TR    CONSL3(L'CONSL3),HEXTAB-X'F0'
         MVI   CONSL3+L'CONSL3,C' '
         UNPK  CONSL4(L'CONSL4+1),4(5,R1)
         TR    CONSL4(L'CONSL4),HEXTAB-X'F0'
         MVI   CONSL4+L'CONSL4,C' '
         UNPK  CONSL5(L'CONSL5+1),8(5,R1)
         TR    CONSL5(L'CONSL5),HEXTAB-X'F0'
         MVI   CONSL5+L'CONSL5,C' '
         UNPK  CONSL6(L'CONSL6+1),12(5,R1)
         TR    CONSL6(L'CONSL6),HEXTAB-X'F0'
         MVI   CONSL6+L'CONSL6,C' '
         MVI   CONSL7,C'*'
         MVC   CONSL8(L'HXDATA),0(R1)
         L     R1,=A(CHARTB)
         TR    CONSL8,0(R1)
         MVI   CONSL9,C'*'
         MVC   CONSLA,ADVICE
         TM    SWITCH,POPXMR  WAS PREVIOUS OP. X-MEM. REF. MODE?
         BO    DS24           YES
         TM    STATUS,DANGER
         BO    *+L'*+8
         TM    STATUS,TESTRUN
         BO    DS24
         CLI   PRVOP,C'D'
         BNE   DS24
         CLC   PVPTR,BEGADD
         BNL   DS24
         CLC   CRPTR,BEGADD
         BNH   DS24
         L     R1,BEGADD
         SL    R1,PVPTR       NUMBER OF VIEWABLE DATA BYTES
         MH    R1,=Y(L'DSVD+4)
         B     DSVD-L'DSVD-4(R1)
DSVD     MVC   CONSL3+2(2),ALLBLKS
         MVI   CONSL8+1,C' '
         MVC   CONSL3+4(2),ALLBLKS
         MVI   CONSL8+2,C' '
         MVC   CONSL3+6(2),ALLBLKS
         MVI   CONSL8+3,C' '
         MVC   CONSL4(2),ALLBLKS
         MVI   CONSL8+4,C' '
         MVC   CONSL4+2(2),ALLBLKS
         MVI   CONSL8+5,C' '
         MVC   CONSL4+4(2),ALLBLKS
         MVI   CONSL8+6,C' '
         MVC   CONSL4+6(2),ALLBLKS
         MVI   CONSL8+7,C' '
         MVC   CONSL5(2),ALLBLKS
         MVI   CONSL8+8,C' '
         MVC   CONSL5+2(2),ALLBLKS
         MVI   CONSL8+9,C' '
         MVC   CONSL5+4(2),ALLBLKS
         MVI   CONSL8+10,C' '
         MVC   CONSL5+6(2),ALLBLKS
         MVI   CONSL8+11,C' '
         MVC   CONSL6(2),ALLBLKS
         MVI   CONSL8+12,C' '
         MVC   CONSL6+2(2),ALLBLKS
         MVI   CONSL8+13,C' '
         MVC   CONSL6+4(2),ALLBLKS
         MVI   CONSL8+14,C' '
         MVC   CONSL6+6(2),ALLBLKS
         MVI   CONSL8+15,C' '
         DROP  R2
DS24    @SEND  MSGOUT,OP=LA
         B     INQUIRY
DS30     NI    FSPRSW,255-FSPRDS   RESET
         L     R6,RCVPTR      RESTORE POINTER
         CLI   PRVOP,C'S'
         BNE   *+L'*+12
         LA    R1,1(R6,R7)    BUMP WITH LENGTH OF STORE
         ST    R1,WDSPT
         B     DS32
         CLI   PRVOP,C'F'
         BNE   DS31
         XR    R7,R7
         IC    R7,STRNGL
         LA    R1,1(R6,R7)    BUMP WITH LENGTH OF STRING
         B     DS31+L'DS31
DS31     LA    R1,L'HXDATA(R6)
         MVC   WDSPT,DATA
DS32     ST    R1,CRPTR       CURRENT POINTER
         ST    R6,PVPTR       PREVIOUS POINTER
         TM    OPERSW,GETBSW
         BZ    DSPLY5
         XC    GFBYT(L'GFBYT+L'GLBYT),GFBYT
DSPLY5   L     R3,=A(HXDATA)  ADDRESS OF MOVED DATA (=BLDATA)
         LA    R2,MSGSVT
         MVC   WDSLN,BLKPTR
         XC    FSPOINT,FSPOINT     SAY DATA LINES
         OI    FSCRSW,FSCRSL
DS40     TM    SWITCH,POPXMR  WAS PREVIOUS OP. X-MEM. REF. MODE?
         BO    DS41           YES
         TM    STATUS,DANGER
         BO    *+L'*+8
         TM    STATUS,TESTRUN
         BO    DS41
         CLI   PRVOP,C'D'
         BNE   DS41
         CLC   WDSLN,BEGADD
         BL    DS41
         CLC   WDSLN,ENDADD
         BNL   DS41
         L     R1,WDSLN       UNVIEWABLE DATA
         AL    R1,=A(L'HXDATA)
         ST    R1,WDSLN
         B     DS45
DS41     MVC   MSGSVT(L'MSGSVT),ALLBLKS
         USING FSLOUT,R2
         UNPK  FSL1(L'FSL1+1),WDSLN(5)
         TR    FSL1(L'FSL1),HEXTAB-X'F0'
         MVI   FSL1+L'FSL1,C' '
         LA    R0,4
         LA    R1,FSL2
         LR    R14,R3
         TM    OPERSW,GETBSW
         BZ    DS42
         OC    GFBYT,GFBYT
         BNZ   DS42
         ST    R14,GFBYT
DS42     UNPK  TBUF(9),0(5,R14)
         TR    TBUF(8),HEXTAB-X'F0'
         MVC   1(4,R1),TBUF
         MVC   6(4,R1),TBUF+4
         BCT   R0,*+L'*+4
         B     DS43
         LA    R1,L'FSL2(R1)
         LA    R14,4(R14)
         B     DS42
DS43     TM    OPERSW,GETBSW
         BZ    *+L'*+6
         BCTR  R14,0
         ST    R14,GLBYT
         MVI   FSL3,C'|'
         MVC   FSL4(L'HXDATA),0(R3)
         L     R1,=A(CHARTB)
         TR    FSL4,0(R1)
         MVI   FSL5,C'|'
         L     R1,WDSLN
         AL    R1,=A(L'HXDATA)
         CLC   WDSPT,WDSLN
         BL    DS44
         BE    *+L'*+8
         CL    R1,WDSPT
         BNH   DS44
         L     R1,WDSPT
         SL    R1,WDSLN
         SLL   R1,1
         AL    R1,=A(FSTAPD)
         XR    R0,R0
         IC    R0,0(R1)
         LA    R14,FSLOUT
         ALR   R14,R0
         MVC   0(1,R14),1(R1) MARK CURRENT ADDRESS POINTER
         MVC   FSL6,ADVICE
         DROP  R2
         OI    FSCRSW,FSCRSI  SAY INTENSIVE LINE
         L     R1,WDSLN
         AL    R1,=A(L'HXDATA)
DS44     ST    R1,WDSLN
        @SEND  MSGSVTO,OP=LA
DS45     AL    R3,=A(L'HXDATA)
         BCT   R4,DS40
         NI    FSCRSW,255-FSCRSL   RESET DATA
         MVC   FSCRSCL,=CL54'ADDRESS+    0 1  2 3   4 5  6 7   8 9  A B1
                  C D  E F '       SET SCALE
         TM    OPERSW,GETBSW
         BZ    INQUIRT
         NI    OPERSW,255-GETBSW
         SL    R6,BLKPTR
         AL    R6,=A(HXDATA)
         CL    R6,GFBYT
         BL    INQUIRT
         CL    R6,GLBYT
         BH    INQUIRT
         XR    R14,R14
         IC    R14,GNBYT
         ALR   R14,R6
         CL    R14,GLBYT
         BNH   *+L'*+4
         BCT   R14,*-8
         SLR   R14,R6
         LA    R15,FSCRTXT
GBX1     CH    R14,=H'5'
         BNH   GBX2
         MVC   TBUF(6),0(R6)
         UNPK  0(13,R15),TBUF(7)
         TR    0(12,R15),HEXTAB-X'F0'
         LA    R6,6(R6)
         LA    R15,12(R15)
         SH    R14,=H'6'
         B     GBX1
GBX2     EX    R14,GBMV1
         UNPK  TBUF(13),TBUF+16(7)
         TR    TBUF(12),HEXTAB-X'F0'
         SLL   R14,1
         LA    R14,1(R14)
         EX    R14,GBMV2
         B     INQUIRT
GBMV1    MVC   TBUF+16(*-*),0(R6)
GBMV2    MVC   0(*-*,R15),TBUF
         SPACE 1
        @END   ,
         EJECT
STORE   @INIT  ,
         SPACE 1
***********************************************************************
*        STORE COMMAND.                                               *
***********************************************************************
         SPACE 1
         CLI   AUTH,LV10      AUTHORIZED?
         BL    ERR22          NO
         BAS   R14,NEXTCH     POSITION TO NEXT
         B     ERR2           +0
         BAS   R14,LABEL      +4 - GET LABEL
         B     ERR5                +0
         B     ST2                 +4 - MAY BE HEX ADDRESS
         B     ERR6                +8
         B     ST3                 +12
         BAS   R14,NEXT            +16 - POSITION TO NEXT
         B     ST1                       +0
         CLI   0(R3),C'/'                +4
         BNE   ST1
         BAS   R14,NEXTCH     POSITION TO NEXT
         B     ERR37          +0
         BAS   R14,NAMER      +4 - GET DSECT-NAME
         B     ERR37               +0
         B     ERR38               +4
         B     ERR39               +8
         MVC   DRFDSCT,NMEF        +12 - DSECT NAME
         MVC   DRFFLDN,LBLF   FIELD NAME
         MVC   LBLF,NMEF
         BAS   R14,SCITBL     PREDEFINED LABELS
         B     *+L'*+4        +0
         BAS   R14,SCLTBL     +4 - LABELS
         ICM   R6,B'1111',L'LBLF(R7)    SET ADDRESS
         XC    DRFDSPL(L'DRFDSPL+L'DRFLNGH),DRFDSPL
         MVC   DRFFLGN(L'DRFFLGN),ALLBLKS
         XC    DRFFLVL(L'DRFFLVL+L'DRFFLLG),DRFFLVL
         LA    R0,1           ASK SEARCH
         L     R15,=A(DRFCALL)
         BASR  R14,R15
         B     *+L'*(R15)
         B     *+L'*+8        +0
         B     *+L'*+4        +4
         B     ERROR          +8
         XR    R0,R0
         ICM   R0,B'0011',DRFDSPL  SET DISPLACEMENT
         ALR   R6,R0          COMPUTE ADDRESS
         B     ST3+L'ST3
ST1      BAS   R14,SCLTBL
         B     ST3
ST2      CLI   0(R3),C'.'     HEX ADDRESS?
         BNE   *+L'*+8
         BAS   R14,NEXTCH     POSITION TO NEXT
         B     ERR2           +0
         BAS   R14,SCAN       +4 - GET ADDRESS
         B     ERR2                +0
         B     ST15                +4
         B     ERR3                +8
         L     R6,DATA             +12 - SET ADDRESS
         B     ST3+L'ST3
ST3      ICM   R6,B'1111',L'LBLF(R7)    SET ADDRESS
         NI    STATUS,255-SUBSTRCT
         BAS   R14,NEXT       POSITION TO NEXT
         B     ERR2           +0
         CLI   0(R3),C'+'     +4 - TEST IF DELIMITER
         BE    ST4
         CLI   0(R3),C'-'
         BNE   ST12
         OI    STATUS,SUBSTRCT
ST4      BAS   R14,NEXTCH     POSITION TO NEXT
         B     ERR9           +0
         BAS   R14,LABEL      +4 - GET DISPLACEMENT
         B     ERR9                +0
         B     ST10                +4 - MAY BE HEX DISPLACEMENT
         B     ERR6                +8
         B     ST5+L'ST5           +12
         BAS   R14,NEXT            +16 - POSITION TO NEXT
         B     ST5                       +0
         CLI   0(R3),C'/'                +4
         BNE   ERR37
         BAS   R14,NEXTCH     POSITION TO NEXT
         B     ERR37          +0
         BAS   R14,NAMER      +4 - GET DSECT-NAME
         B     ERR37               +0
         B     ERR38               +4
         B     ERR39               +8
         MVC   DRFDSCT,NMEF        +12 - DSECT NAME
         MVC   DRFFLDN,LBLF   FIELD NAME
         XC    DRFDSPL(L'DRFDSPL+L'DRFLNGH),DRFDSPL
         MVC   DRFFLGN(L'DRFFLGN),ALLBLKS
         XC    DRFFLVL(L'DRFFLVL+L'DRFFLLG),DRFFLVL
         LA    R0,1           ASK SEARCH
         L     R15,=A(DRFCALL)
         BASR  R14,R15
         B     *+L'*(R15)
         B     *+L'*+8        +0
         B     *+L'*+4        +4
         B     ERROR          +8
         XR    R0,R0
         ICM   R0,B'0011',DRFDSPL  SET DISPLACEMENT
         B     ST11
ST5      BAS   R14,SCLTBL
         ICM   R0,B'1111',L'LBLF(R7)    SET DISPLACEMENT
         B     ST11
ST10     CLI   0(R3),C'.'     HEX DISPLACEMENT?
         BNE   *+L'*+8
         BAS   R14,NEXTCH     POSITION TO NEXT
         B     ERR9           +0
         BAS   R14,SCAN       +4 - GET DISPLACEMENT
         B     ERR9                +0
         B     ERR17               +4
         B     ERR3                +8
         L     R0,DATA             +12
ST11     TM    STATUS,SUBSTRCT     APPLY DISPLACEMENT
         BO    *+L'*+6
         ALR   R6,R0
         B     *+L'*+2
         SLR   R6,R0
         LA    R6,0(R6)
ST12     ST    R6,DTPTR
         B     ST20
ST15     CLI   0(R3),C'*'     CONTINUE IN SEQUENCE...
         BNE   ERR4           NO, ERROR
         BAS   R14,NEXTCH     POSITION TO NEXT
         B     ERR2           +0
         CLI   PRVOP,C'S'     +4
         BNE   ST16
         OC    CRPTR,CRPTR
         BZ    ERR4
         MVC   DTPTR,CRPTR
         B     ST20
ST16     MVC   DTPTR,PVPTR
ST20     BAS   R14,NEXT       POSITION TO NEXT
         B     ERR2           +0
         CLI   0(R3),C'@'     +4 - TANK NUMBER?
         BE    ST25           YES
         LA    R6,TBUF
         XR    R7,R7
ST21     XC    DATA,DATA      GET HEX DATA
         LA    R1,L'DATA
         LR    R2,R3          START OF DATA FIELD
ST22     CLI   0(R3),C','     DELIMITER ,?
         BE    ST23           YES
         CLI   0(R3),C' '     END OF DATA?
         BE    ST23           YES
         CLI   0(R3),C'A'
         BL    ERR17
         CLI   0(R3),C'9'
         BH    ERR17
         CLI   0(R3),C'0'
         BNL   *+L'*+8
         CLI   0(R3),C'F'
         BH    ERR17
         BXLE  R3,R4,*+L'*+4  INCREMENT POINTER
         B     ST23
         BCT   R1,ST22
ST23     LR    R1,R3
         SR    R1,R2          COMPUTE NUMBER OF CHARACTERS
         BP    ST24
         CLR   R3,R5          NO MOVE IF NONE
         BH    ST30
         BAS   R14,NEXTCH     POSITION TO NEXT
         B     ST30           +0 - NO MORE DATA
         B     ST21           +4 - CONTINUE
ST24     STC   R1,TSTBT
         TM    TSTBT,X'01'
         BO    ERR18
         BCTR  R1,0           DECREMENT BY 1
         XC    FXLAB,FXLAB    CLEAR WORK AREA
         EX    R1,STMWA       MOVE TO WORK AREA
         EX    R1,STNFT       NORMALIZE FOR TRANSLATE
         EX    R1,STTTB       TRANSLATE TO BINARY
         LA    R1,1(R1)       INCR. BY 1
         EX    R1,STCTH       CONVERT TO HEX
         LR    R14,R1
         SRL   R14,1
         LA    R15,DATA+L'DATA
         SLR   R15,R14
         EX    R14,STDTA      SET DATA IN TEMPORARY AREA
         ALR   R6,R14
         ALR   R7,R14
         CLR   R3,R5
         BH    ST30
         BAS   R14,NEXT       POSITION TO NEXT
         B     ST30           +0 - NO MORE DATA
         B     ST21           +4 - CONTINUE
ST25     BAS   R14,NEXTCH     POSITION TO NEXT
         B     ERR2           +0 - NO MORE DATA
         XR    R2,R2          +4 - GET TANK NUMBER
         LA    R0,15
ST26     CLI   0(R3),C'0'     NUMERIC?
         BL    ERR4
         CLI   0(R3),C'9'
         BH    ERR4
         IC    R1,0(R3)       COMPUTE NUMBER
         NR    R1,R0
         LTR   R2,R2
         BZ    *+L'*+4
         MH    R2,=H'10'
         AR    R2,R1
         CH    R2,=Y(NTKWAS)
         BH    ERR4
         BXLE  R3,R4,*+L'*+4  TO NEXT
         B     *+L'*+8        NO MORE DATA
         CLI   0(R3),C' '     END?
         BNE   ST26           NO, CONTINUE
         LTR   R2,R2
         BNP   ERR4
         BCTR  R2,0
         MH    R2,=Y(TKWAL)
         AL    R2,ATKWAS      REQUESTED TANK ADDRESS
         USING TKWA,R2
         L     R7,TKWDL       GET TEXT LENGTH
         LA    R3,TKWTX       START OF TEXT
         DROP  R2
         LTR   R7,R7
         BNP   ERR36
         B     ST31
ST30     LA    R3,TBUF        ADDRESS OF TEXT TO MOVE
         LTR   R7,R7
         BNP   ERR2
ST31     L     R6,DTPTR
         LA    R1,0(R6,R7)
         ST    R1,CRPTR       CURRENT POINTER
         ST    R6,PVPTR       PREVIOUS POINTER
         BCTR  R7,0           LENGTH OF MOVE
         TM    SWITCH,ACTIVM+XMEMRF ACTIVE MODE + X-MEM.REF.?
         BNO   *+L'*+8        NO
         LA    R2,1(R7)       YES, LENGTH OF MOVE
         B     ST40
         TM    STATUS,DANGER
         BO    *+L'*+8
         TM    STATUS,TESTRUN
         BO    ST40
         CL    R6,BEGADD
         BL    ST32
         CL    R6,ENDADD
         BNL   ST40
         B     ERR24          UNTOUCHABLE DATA
ST32     LA    R1,0(R6,R2)
         CL    R1,BEGADD
         BNL   ERR24          OVERFLOW IN UNTOUCHABLE DATA
ST40     MVI   PRVOP,C'S'
         OI    SWITCH,TSTEAP  SET E.A. PROT. REQUEST
         BAS   R8,ENABLE ----------------------------------- * DANGER *
         B     ST41           +0 - X-MEM. REF.                        *
         EX    R7,*+L'*+4     +4 - MOVE INTO VIRTUAL STORAGE          *
         B     ST43                                                   *
         MVC   0(*-*,R6),0(R3)     <<EXECUTED>>                       *
ST41     XR    R15,R15        FROM KEY                                *
ST42     MVCS  0(R2,R6),0(R3),R15  MOVE INTO VIRTUAL STORAGE          *
         BZ    ST43                                                   *
         AL    R3,=F'256'     BUMP FROM ADDRESS                       *
         AL    R6,=F'256'     BUMP TO ADDRESS                         *
         SL    R2,=F'256'     DECREMENT THE LENGTH                    *
         B     ST42           GO BACK AND GET MORE                    *
ST43     BAS   R8,DISABLE ---------------------------------- * DANGER *
         L     R6,PVPTR
         ST    R6,DATA
         TM    SWITCH,POPXMR  WAS PREVIOUS OP. X-MEM. REF. MODE?
         BZ    *+L'*+4        NO
         OI    SWITCH,XMEMRF  YES, CONTINUE IN X-MEM. REF. MODE
         L     R10,=A(DSPLY)
         DROP  R10
         USING DSPLY,R10
         B     DSPLY1
         DROP  R10
         USING STORE,R10
STMWA    MVC   FXLAB(*-*),0(R2)    <<EXECUTED>>
STNFT    NC    FXLAB(*-*),=XL8'1F1F1F1F1F1F1F1F'  <<EXECUTED>>
STTTB    TR    FXLAB(*-*),TRANTBL  <<EXECUTED>>
STCTH    PACK  DATA(L'DATA+1),FXLAB(*-*)     <<EXECUTED>>
STDTA    MVC   0(*-*,R6),0(R15)    <<EXECUTED>>
         SPACE 1
        @END   ,
         EJECT
FIND    @INIT  ,
         SPACE 1
***********************************************************************
*        FIND A STRING COMMAND.                                       *
***********************************************************************
         SPACE 1
         BAS   R14,NEXTCH     POSITION TO NEXT
         B     FN10                +0 - USE STRING (CONTINUE)
         CLC   0(4,R3),=CL4'FULL'  +4 - ENTIRE A.S. SEARCH?
         BNE   FN1            NO
         OI    SWITCH,ASFULL
         LA    R3,4(R3)
         BAS   R14,NEXTCH     POSITION TO NEXT
         B     FN10                +0 - USE STRING (CONTINUE)
FN1      NI    STATUS,255-FTYPE    +4 - GET STRING TYPE
         MVI   STRNGL,0
         XC    STRNG,STRNG
         CLI   0(R3),C''''    CHAR STRING?
         BNE   FN2            NO
         OI    STATUS,FTYPE   YES, SET CHAR TYPE
         MVC   STRNG(L'STRNG),ALLBLKS
         LA    R3,1(R3)       START OF STRING
FN2      CLI   0(R5),C' '     SEARCH END OF STRING
         BNE   *+L'*+4
         BCT   R5,FN2
         LR    R2,R5
         SH    R2,=H'4'
         CLR   R2,R3
         BNH   FN4
         CLC   1(4,R2),=CL4'FULL'  ENTIRE A.S. SEARCH?
         BNE   FN4            NO
         OI    SWITCH,ASFULL
         LR    R5,R2
FN3      CLI   0(R5),C' '     SEARCH END OF STRING
         BNE   *+L'*+4
         BCT   R5,FN3
FN4      TM    STATUS,FTYPE   DATA STRING TYPE?
         BZ    FN5            BR IF HEX
         CLI   0(R5),C''''    END OF CHAR STRING?
         BNE   ERR35          NO
         LR    R4,R5
         SR    R4,R3
         BNP   ERR4
         CH    R4,=Y(L'STRNG)
         BH    ERR3           TOO LONG
         BCTR  R4,0
         STC   R4,STRNGL      MCH LENGTH OF FIND CHAR DATA
         EX    R4,*+L'*+4     SET FIND CHAR DATA
         B     FN10
         MVC   STRNG(*-*),0(R3)    <<EXECUTED>>
FN5      LA    R4,1(R5)
         SR    R4,R3
         CH    R4,=Y(2*L'STRNG)
         BH    ERR3           TOO LONG
         STC   R4,STRNGL      LENGTH OF FIND HEX DATA
         TM    STRNGL,X'01'   ODD NUMBER OF DIGITS?
         BO    ERR18          YES, ERROR
         LR    R0,R4          VALID STRING VALUE CONTROL
FN6      CLI   0(R5),C'A'
         BL    ERR17
         CLI   0(R5),C'9'
         BH    ERR17
         CLI   0(R5),C'0'
         BNL   FN7
         CLI   0(R5),C'F'
         BH    ERR17
FN7      NI    0(R5),X'1F'    NORMALIZE FOR TRANSLATE
         TR    0(1,R5),TRANTBL     TRANSLATE TO BINARY
         BCTR  R5,0
         BCT   R0,FN6
         SRL   R4,1           NUMBER OF BINARY BYTES
         LR    R1,R4
         BCTR  R1,0
         STC   R1,STRNGL      MCH LENGTH OF FIND HEX DATA
         XR    R0,R0
         XR    R1,R1
         LA    R5,STRNG       BUILD BINARY DATA
FN8      IC    R1,0(R3)
         IC    R0,1(R3)
         SLL   R1,4
         OR    R1,R0
         STC   R1,0(R5)
         LA    R3,2(R3)
         LA    R5,1(R5)
         BCT   R4,FN8
FN10     CLI   PRVOP,C'F'     GET REAL FIND START ADDRESS
         BE    *+L'*+8
         L     R6,PVPTR
         B     *+L'*+4
         L     R6,CRPTR
         TM    SWITCH,ASFULL  ENTIRE A.S. SEARCH?
         BZ    FNCNT          NO
         LA    R4,2048(R6)    COMPUTE ENDING ADDRESS
         LA    R4,2047(R4)
         SRL   R4,12
         SLL   R4,12
         ST    R4,WRPTR
         OI    WRPTR,X'80'
         TM    STATUS,TSORUN
         BZ    FNCNT
         TM    FSCRSW,FSCRSF
         BZ    FNCNT
         L     R4,=A(256*KB)  COMPUTE CONTROL ADDRESS
         LA    R4,0(R4,R6)
         SRL   R4,18
         ST    R4,CFFPTR
FNCNT    LR    R4,R6          COMPUTE PAGE START ADDRESS
         SRL   R4,12
         SLL   R4,12
         L     R2,=A(FNBUF)
         L     R3,=A(4*KB)
         LR    R5,R3
         LR    R7,R6          COMPUTE START DISPLACEMENT
         SLR   R7,R4
         TM    SWITCH,ACTIVM+XMEMRF ACTIVE MODE + X-MEM.REF.?
         BO    FN13           YES
         TM    STATUS,DANGER
         BO    *+L'*+8
         TM    STATUS,TESTRUN
         BO    FN13
         CL    R6,BEGADD
         BH    FN11
         BE    FN12
         LR    R1,R4
         AL    R1,=A(4*KB)
         CL    R1,BEGADD
         BNH   FN13
         B     FN12
FN11     CL    R6,ENDADD
         BNL   FN13
FN12     TM    SWITCH,ASFULL  ENTIRE A.S. SEARCH?
         BZ    ERR25          NO, UNACCESSIBLE PAGE
FNRCV    L     R6,=A(4*KB)    YES, SKIP THIS PAGE
         LA    R6,0(R6,R4)
         B     FN37
FN13     LR    R6,R4          KEEP PAGE START ADDRESS
         ST    R6,RCVPTR
         MVI   PRVOP,C'F'
         BAS   R8,ENABLE ----------------------------------- * DANGER *
         B     FN20           +0 - X-MEM. REF.                        *
         MVCL  R2,R4          +4 - GET REQUESTED 4K PAGE              *
         B     FN22                                                   *
FN20     XR    R15,R15        FROM KEY                                *
FN21     MVCP  0(R3,R2),0(R4),R15  GET REQUESTED 4K PAGE              *
         BZ    FN22                                                   *
         AL    R4,=F'256'     BUMP FROM ADDRESS                       *
         AL    R2,=F'256'     BUMP TO ADDRESS                         *
         SL    R3,=F'256'     DECREMENT THE LENGTH                    *
         B     FN21           GO BACK AND GET MORE                    *
FN22     BAS   R8,DISABLE ---------------------------------- * DANGER *
         XR    R1,R1
         TM    SWITCH,PVALID
         BZ    FN30
         L     R3,=A(PVBUF)
         L     R1,=A(L'HXDATA)
         B     *+L'*+4
FN30     L     R3,=A(FNBUF)
         L     R5,=A(4*KB)
         TM    SWITCH,PVALID
         BO    *+L'*+4
         SL    R5,=A(L'HXDATA)
         LA    R5,0(R5,R3)
         ALR   R3,R7
         CLR   R3,R5
         BH    FN32
         LA    R4,1
         XR    R2,R2
         IC    R2,STRNGL
FN31     EX    R2,FNCLC       SEARCH A MATCH
         BE    FN33
         BXLE  R3,R4,FN31
FN32     OI    SWITCH,PVALID
         L     R3,=A(PVBUF)
         MVC   0(L'HXDATA,R3),0(R5)
         TM    SWITCH,ASFULL  ENTIRE A.S. SEARCH?
         BO    FN36           YES
         MVC   ADVICE,=CL5'<--NF'
         LR    R3,R5
         B     FN34
FN33     NI    SWITCH,255-PVALID
         MVC   ADVICE,=CL5'FOUND'
FN34     L     R14,=A(HXDATA)
         MVC   0(L'HXDATA,R14),0(R3)
         ALR   R3,R1          COMPUTE DISPLACEMENT
         SL    R3,=A(FNBUF)
         SLR   R6,R1          REAL ADDRESS OF MATCH
         LA    R6,0(R6,R3)
         ST    R6,DATA
         ST    R6,CRPTR
         TM    FSCRSW,FSCRSF  FULL SCREEN?
         BO    *+L'*+8        YES
         L     R10,=A(DSPLY)
         DROP  R10
         USING DSPLY,R10
         B     DSPLY4
         DROP  R10
         USING FIND,R10
         CLC   ADVICE,=CL5'FOUND'
         BNE   FN35
         TM    SWITCH,POPXMR  WAS PREVIOUS OP. X-MEM. REF. MODE?
         BZ    *+L'*+4        NO
         OI    SWITCH,XMEMRF  YES, CONTINUE IN X-MEM. REF. MODE
         L     R10,=A(DSPLY)
         DROP  R10
         USING DSPLY,R10
         B     DSPLY2
         DROP  R10
         USING FIND,R10
FN35     LA    R1,L'HXDATA(R6)
         ST    R1,CRPTR       CURRENT POINTER
         ST    R6,PVPTR       PREVIOUS POINTER
         SRL   R6,4           SET ADDRESS FOR WORK
         SLL   R6,4
         ST    R6,BLKPTR
         LA    R4,1
         MVC   WDSPT,DATA
         L     R10,=A(DSPLY)
         DROP  R10
         USING DSPLY,R10
         B     DSPLY5
         DROP  R10
         USING FIND,R10
FN36     SL    R5,=A(FNBUF)   COMPUTE DISPLACEMENT
         LA    R6,L'HXDATA(R6,R5)  COMPUTE REAL ADDRESS
FN37     TM    WRPTR,X'80'    FIRST TIME?
         BZ    *+L'*+8        NO
         NI    WRPTR,X'7F'
         B     FN40
         CL    R6,WRPTR       END OF A.S. SEARCH?
         BNE   FN40           NO
        @SEND  NFNDAS
         B     INQUIRT
FN40     ST    R6,CRPTR       UPDATE POINTERS
         ST    R6,PVPTR
         TM    STATUS,TSORUN
         BZ    FN41
         TM    FSCRSW,FSCRSF
         BZ    FN41
         LR    R4,R6
         SRL   R4,18
         CL    R4,CFFPTR
         BNE   FN50
         L     R4,=A(256*KB)  COMPUTE NEW CONTROL ADDRESS
         LA    R4,0(R4,R6)
         SRL   R4,18
         ST    R4,CFFPTR
         OI    OPERSW,FFSTUP
         L     R10,=A(INFST)  GO GET INFO STATUS
         BR    R10
FN41     OI    OPERSW,FFSTUP
         B     INQUIRY        GET COMMAND INPUT IF ANY
FNFLC    L     R6,CRPTR
FN50     TM    SWITCH,POPXMR  WAS PREVIOUS OP. X-MEM. REF. MODE?
         BZ    FNCNT          NO
         OI    SWITCH,XMEMRF  YES, CONTINUE IN X-MEM. REF. MODE
         B     FNCNT
FNCLC    CLC   STRNG(*-*),0(R3)    <<EXECUTED>>
         SPACE 1
        @END   ,
         EJECT
MMTK    @INIT  MEMORIZE
         SPACE 1
***********************************************************************
*        MEMORIZE - BUILD A TANK USER-AREA.                           *
***********************************************************************
         SPACE 1
         MVC   CRPTR,PVPTR    ADJUST POINTERS FOR AFTER
         TM    STATUS,TESTRUN
         BZ    *+L'*+6
         MVC   RCVIND(L'RCVIND),ALLBLKS
         BAS   R14,NEXTCH     POSITION TO NEXT
         B     MMTK40         + 0
         CLI   0(R3),C'@'     + 4 - TANK NUMBER?
         BE    MMTK10         YES
         OC    CATKWA,CATKWA  HOW CURRENT TANK?
         BZ    ERR11          NONE
         CLI   0(R3),C'Z'     RESET CURRENT TO ZEROS?
         BE    MMTK30         YES
         CLI   0(R3),C'R'     RESET CURRENT TO BLANKS?
         BE    MMTK31         YES
         CLI   0(R3),C'L'     SET TEXT LENGTH?
         BE    MMTK35         YES
         NI    STATUS,255-SUBSTRCT
         CLI   0(R3),C'+'     POINTER ADJUSTEMENT?
         BE    MMTK21         YES
         CLI   0(R3),C'-'     POINTER ADJUSTEMENT?
         BE    MMTK20         YES
*        --------------------------------------------------------------
*        M HHHH,...  OR '...' COMMAND.
*        --------------------------------------------------------------
         CLI   0(R3),C''''    CHARACTERS STORE?
         BE    MMTK6          YES
         LA    R6,TBUF
         XR    R7,R7
MMTK1    XC    DATA,DATA      GET HEX DATA
         LA    R1,L'DATA
         LR    R2,R3          START OF DATA FIELD
MMTK2    CLI   0(R3),C','     DELIMITER ,?
         BE    MMTK4          YES
         CLI   0(R3),C' '     END OF DATA?
         BE    MMTK4          YES
         CLI   0(R3),C'A'
         BL    ERR17
         CLI   0(R3),C'9'
         BH    ERR17
         CLI   0(R3),C'0'
         BNL   MMTK3
         CLI   0(R3),C'F'
         BH    ERR17
MMTK3    BXLE  R3,R4,*+L'*+4  INCREMENT POINTER
         B     MMTK4
         BCT   R1,MMTK2
MMTK4    LR    R1,R3
         SR    R1,R2          COMPUTE NUMBER OF CHARACTERS
         BP    MMTK5          SKIP MOVE IF ZERO
         CLR   R3,R5
         BH    MMTK8
         BAS   R14,NEXTCH     POSITION TO NEXT
         B     MMTK8          +0 - NO MORE DATA
         B     MMTK1          +4 - CONTINUE
MMTK5    STC   R1,TSTBT
         TM    TSTBT,X'01'
         BO    ERR18
         BCTR  R1,0           DECREMENT BY 1
         XC    FXLAB,FXLAB    CLEAR WORK AREA
         EX    R1,MMMWA       MOVE TO WORK AREA
         EX    R1,MMNFT       NORMALIZE FOR TRANSLATE
         EX    R1,MMTTB       TRANSLATE TO BINARY
         LA    R1,1(R1)       INCR. BY 1
         EX    R1,MMCTH       CONVERT TO HEX
         LR    R14,R1
         SRL   R14,1
         LA    R15,DATA+L'DATA
         SLR   R15,R14
         EX    R14,MMDTA      SET DATA IN TEMPORARY AREA
         ALR   R6,R14
         ALR   R7,R14
         CLR   R3,R5
         BH    MMTK8
         BAS   R14,NEXT       POSITION TO NEXT
         B     MMTK8          +0 - NO MORE DATA
         B     MMTK1          +4 - CONTINUE
MMTK6    BXLE  R3,R4,*+L'*+4
         B     ERR2
         LR    R6,R3          HANDLE THE CHARACTERS STRING
         LR    R7,R5
MMTK7    CLI   0(R7),C''''
         BE    *+L'*+10
         CLR   R7,R6
         BNH   ERR35
         BCT   R7,MMTK7
         SR    R7,R6
         LR    R15,R6
         B     MMTK8+L'MMTK8
MMTK8    LA    R15,TBUF
         LTR   R7,R7
         BNP   ERR2
         L     R2,CATKWA
         USING TKWA,R2
         L     R6,TKWPT
         LR    R1,R6
         AR    R1,R7
         CH    R1,=H'256'
         BH    ERR3           TOO LONG, TANK OVERFLOW
         LR    R0,R1
         BL    *+L'*+2
         BCTR  R1,0
         ST    R1,TKWPT       NEW TEXT POINTER
         CL    R0,TKWDL
         BNH   *+L'*+4
         ST    R0,TKWDL       NEW TEXT LENGTH
         LA    R6,TKWTX(R6)
         BCTR  R7,0
         EX    R7,MMDTA       MOVE TEXT IN TANK
         DROP  R2
         B     MMTK41
*        --------------------------------------------------------------
*        M @N COMMAND.
*        --------------------------------------------------------------
MMTK10   BAS   R14,NEXTCH     POSITION TO NEXT
         B     ERR2           +0 - NO MORE DATA
         XR    R2,R2          +4 - GET TANK NUMBER
         LA    R0,15
MMTK11   CLI   0(R3),C'0'     NUMERIC?
         BL    ERR4
         CLI   0(R3),C'9'
         BH    ERR4
         IC    R1,0(R3)       COMPUTE NUMBER
         NR    R1,R0
         LTR   R2,R2
         BZ    *+L'*+4
         MH    R2,=H'10'
         AR    R2,R1
         CH    R2,=Y(NTKWAS)
         BH    ERR4
         BXLE  R3,R4,*+L'*+4  TO NEXT
         B     *+L'*+8        NO MORE DATA
         CLI   0(R3),C' '     END?
         BNE   MMTK11         NO, CONTINUE
         LTR   R2,R2
         BNP   ERR4
         CVD   R2,DBLWD
         MVC   CTKWAN,=XL4'40202120'
         ED    CTKWAN,DBLWD+6 NEW CURRENT TANK NUMBER
         LA    R1,CTKWAN+L'CTKWAN-2
         CLI   0(R1),C' '
         BE    *+L'*+4
         BCT   R1,*-8
         MVI   0(R1),C'@'
         BCTR  R2,0
         MH    R2,=Y(TKWAL)
         AL    R2,ATKWAS
         ST    R2,CATKWA      CURRENT TANK ADDRESS
         B     MMTK41
*        --------------------------------------------------------------
*        M + OR - XX COMMAND.
*        --------------------------------------------------------------
MMTK20   OI    STATUS,SUBSTRCT
MMTK21   BAS   R14,NEXTCH     POSITION TO NEXT
         B     ERR9           +0
         BAS   R8,MMTKRT      +4 - GET DISPLACEMENT
         L     R0,DATA        GET DISPLACEMENT VALUE
         L     R2,CATKWA
         USING TKWA,R2
         L     R1,TKWPT       GET CURRENT TEXT POINTER
         TM    STATUS,SUBSTRCT     APPLY DISPLACEMENT
         BO    *+L'*+6
         AR    R1,R0
         B     *+L'*+2
         SR    R1,R0
         LTR   R1,R1          CONTROL
         BNM   *+L'*+2
         XR    R1,R1
         CH    R1,=H'256'
         BL    *+L'*+4
         LA    R1,255
         ST    R1,TKWPT       NEW TEXT POINTER
         DROP  R2
         B     MMTK41
*        --------------------------------------------------------------
*        M Z OR R COMMAND.
*        --------------------------------------------------------------
MMTK30   L     R2,CATKWA
         USING TKWA,R2
         XC    TKWTX,TKWTX    RESET TO ZEROS
         B     MMTK32
MMTK31   L     R2,CATKWA
         MVI   TKWTX,C' '     RESET TO BLANKS
         MVC   TKWTX+1(L'TKWTX-1),TKWTX
MMTK32   XC    TKWPT,TKWPT
         XC    TKWDL,TKWDL
         DROP  R2
         B     MMTK41
*        --------------------------------------------------------------
*        M L XX COMMAND.
*        --------------------------------------------------------------
MMTK35   BAS   R14,NEXTCH     POSITION TO NEXT
         B     ERR2           +0 - NO MORE DATA
         BAS   R8,MMTKRT      +4 - GET NEW LENGTH
         L     R1,DATA        GET LENGTH VALUE
         LH    R0,=H'256'
         CLR   R1,R0          CONTROL
         BNH   *+L'*+2
         LR    R1,R0
         L     R2,CATKWA
         USING TKWA,R2
         ST    R1,TKWDL       NEW LENGTH
         DROP  R2
         B     MMTK41
*        --------------------------------------------------------------
*        M COMMAND - DISPLAY CURRENT USER-AREA.
*        --------------------------------------------------------------
MMTK40   OC    CATKWA,CATKWA  HOW CURRENT TANK?
         BZ    ERR11          NONE
MMTK41   L     R6,CATKWA      DISPLAY DATA IN TANK
         USING TKWA,R6
         TM    FSCRSW,FSCRSF  FULL SCREEN?
         BO    MMTK50         YES
         LA    R2,MSGTXT
         MVC   MSGTXT(L'MSGTXT),ALLBLKS
         L     R3,TKWPT
         LTR   R3,R3
         BZ    MMTK42
         SRL   R3,4
         SLL   R3,4
         CL    R3,TKWPT
         BNE   MMTK42
         SL    R3,=A(L'HXDATA)
MMTK42   ST    R3,DATA
         LA    R3,TKWTX(R3)
         LA    R1,TBUF
         MVC   0(L'HXDATA,R1),0(R3)
         USING CONSLOUT,R2
         UNPK  CONSL1(L'CONSL1+1),DATA(5)
         TR    CONSL1(L'CONSL1),HEXTAB-X'F0'
         MVI   CONSL1+L'CONSL1,C' '
         MVI   CONSL2,C':'
         UNPK  CONSL3(L'CONSL3+1),0(5,R1)
         TR    CONSL3(L'CONSL3),HEXTAB-X'F0'
         MVI   CONSL3+L'CONSL3,C' '
         UNPK  CONSL4(L'CONSL4+1),4(5,R1)
         TR    CONSL4(L'CONSL4),HEXTAB-X'F0'
         MVI   CONSL4+L'CONSL4,C' '
         UNPK  CONSL5(L'CONSL5+1),8(5,R1)
         TR    CONSL5(L'CONSL5),HEXTAB-X'F0'
         MVI   CONSL5+L'CONSL5,C' '
         UNPK  CONSL6(L'CONSL6+1),12(5,R1)
         TR    CONSL6(L'CONSL6),HEXTAB-X'F0'
         MVI   CONSL6+L'CONSL6,C' '
         MVI   CONSL7,C'*'
         MVC   CONSL8(L'HXDATA),0(R1)
         L     R1,=A(CHARTB)
         TR    CONSL8,0(R1)
         MVI   CONSL9,C'*'
         DROP  R2
        @SEND  MSGOUT,OP=LA
         B     INQUIRY
MMTK50   MVC   WDSPT,TKWPT
         LA    R3,TKWTX
         LA    R2,MSGSVT
         LA    R4,L'TKWTX/L'HXDATA NUMBER OF LINES TO DISPLAY
         XC    WDSLN,WDSLN
         XC    FSPOINT,FSPOINT     SAY DATA LINES
         OI    FSCRSW,FSCRSL
MMTK51   MVC   MSGSVT(L'MSGSVT),ALLBLKS
         USING FSLOUT,R2
         UNPK  FSL1(L'FSL1+1),WDSLN(5)
         TR    FSL1(L'FSL1),HEXTAB-X'F0'
         MVI   FSL1+L'FSL1,C' '
         LA    R0,4
         LA    R1,FSL2
         LR    R14,R3
MMTK52   UNPK  TBUF(9),0(5,R14)
         TR    TBUF(8),HEXTAB-X'F0'
         MVC   1(4,R1),TBUF
         MVC   6(4,R1),TBUF+4
         BCT   R0,*+L'*+4
         B     MMTK53
         LA    R1,L'FSL2(R1)
         LA    R14,4(R14)
         B     MMTK52
MMTK53   MVI   FSL3,C'|'
         MVC   FSL4(L'HXDATA),0(R3)
         L     R1,=A(CHARTB)
         TR    FSL4,0(R1)
         MVI   FSL5,C'|'
         L     R1,WDSLN
         AL    R1,=A(L'HXDATA)
         CLC   WDSPT,WDSLN
         BL    MMTK54
         BE    *+L'*+8
         CL    R1,WDSPT
         BNH   MMTK54
         L     R1,WDSPT
         SL    R1,WDSLN
         SLL   R1,1
         AL    R1,=A(FSTAPD)
         XR    R0,R0
         IC    R0,0(R1)
         LA    R14,FSLOUT
         ALR   R14,R0
         MVC   0(1,R14),1(R1) MARK CURRENT ADDRESS POINTER
         OI    FSCRSW,FSCRSI  SAY INTENSIVE LINE
         L     R1,WDSLN
         AL    R1,=A(L'HXDATA)
MMTK54   ST    R1,WDSLN
        @SEND  MSGSVTO,OP=LA
         AL    R3,=A(L'HXDATA)
         BCT   R4,MMTK51
         DROP  R2
         NI    FSCRSW,255-FSCRSL   RESET DATA
         MVC   FSCRSCL,=CL54'ADDRESS+    0 1  2 3   4 5  6 7   8 9  A B1
                  C D  E F '       SET SCALE
         MVC   FSCRMSG(11),=CL11'USER-AREA :'
         MVC   FSCRMSG+12(L'CTKWAN),CTKWAN
         MVC   FSCRMSG+L'CTKWAN+13(10),=CL10'- LENGTH :'
         UNPK  DBLWD(5),TKWDL+2(3)
         TR    DBLWD+1(3),HEXTAB-X'F0'
         MVC   FSCRMSG+L'CTKWAN+24(3),DBLWD+1
         DROP  R6
         B     INQUIRT
         SPACE 1
MMTKRT   XC    DATA,DATA      GET DISPLACEMENT ROUTINE
         LA    R1,L'DATA
         LR    R2,R3
MMTKRT1  CLI   0(R3),C' '     SCAN HEX DIGITS
         BE    MMTKRT3
         CLI   0(R3),C'A'
         BL    ERR17
         CLI   0(R3),C'9'
         BH    ERR17
         CLI   0(R3),C'0'
         BNL   MMTKRT2
         CLI   0(R3),C'F'
         BH    ERR17
MMTKRT2  BXLE  R3,R4,*+L'*+4  INCREMENT POINTER
         B     MMTKRT3
         BCT   R1,MMTKRT1
         B     ERR3
MMTKRT3  LR    R1,R3
         SR    R1,R2
         BCTR  R1,0           DECREMENT BY 1
         XC    FXLAB,FXLAB    CLEAR WORK AREA
         EX    R1,MMMWA       MOVE TO WORK AREA
         EX    R1,MMNFT       NORMALIZE FOR TRANSLATE
         EX    R1,MMTTB       TRANSLATE TO BINARY
         LA    R1,1(R1)       INCR. BY 1
         EX    R1,MMCTH       CONVERT TO HEX
         BR    R8             RETURN
         SPACE 1
MMMWA    MVC   FXLAB(*-*),0(R2)    <<EXECUTED>>
MMNFT    NC    FXLAB(*-*),=XL8'1F1F1F1F1F1F1F1F'  <<EXECUTED>>
MMTTB    TR    FXLAB(*-*),TRANTBL  <<EXECUTED>>
MMCTH    PACK  DATA(L'DATA+1),FXLAB(*-*)     <<EXECUTED>>
MMDTA    MVC   0(*-*,R6),0(R15)    <<EXECUTED>>
         SPACE 1
        @END   ,
         EJECT
ASSGN   @INIT  ASSIGN
         SPACE 1
***********************************************************************
*        ASSIGN - DELETE LABELS COMMANDS.                             *
***********************************************************************
         SPACE 1
         BAS   R14,NEXTCH     POSITION TO NEXT
         B     ERR5           +0
         BAS   R14,LABEL      +4 - GET LABEL
         B     ERR5                +0
         B     ERR7                +4
         B     ERR6                +8
         B     ERR26               +12
         MVC   ASSNME,LBLF         +16
         BAS   R14,NEXT       POSITION TO NEXT
         B     ASS20          +0 - SET TO ACTUAL DATA POINTER
         BAS   R14,LABEL      +4 - GET LABEL
         B     ERR2                +0
         B     ASS2                +4 - MAY BE HEX ADDRESS
         B     ERR6                +8
         B     ASS3                +12
         BAS   R14,NEXT            +16 - POSITION TO NEXT
         B     ASS1                      +0
         CLI   0(R3),C'/'                +4
         BNE   ASS1
         BAS   R14,NEXTCH     POSITION TO NEXT
         B     ERR37          +0
         BAS   R14,NAMER      +4 - GET DSECT-NAME
         B     ERR37               +0
         B     ERR38               +4
         B     ERR39               +8
         MVC   DRFDSCT,NMEF        +12 - DSECT NAME
         MVC   DRFFLDN,LBLF   FIELD NAME
         MVC   LBLF,NMEF
         BAS   R14,SCITBL     PREDEFINED LABELS
         B     *+L'*+4        +0
         BAS   R14,SCLTBL     +4 - LABELS
         ICM   R6,B'1111',L'LBLF(R7)    SET ADDRESS
         XC    DRFDSPL(L'DRFDSPL+L'DRFLNGH),DRFDSPL
         MVC   DRFFLGN(L'DRFFLGN),ALLBLKS
         XC    DRFFLVL(L'DRFFLVL+L'DRFFLLG),DRFFLVL
         LA    R0,1           ASK SEARCH
         L     R15,=A(DRFCALL)
         BASR  R14,R15
         B     *+L'*(R15)
         B     *+L'*+8        +0
         B     *+L'*+4        +4
         B     ERROR          +8
         XR    R0,R0
         ICM   R0,B'0011',DRFDSPL  SET DISPLACEMENT
         ALR   R6,R0          COMPUTE ADDRESS
         B     ASS3+L'ASS3
ASS1     BAS   R14,SCLTBL
         B     ASS3
ASS2     CLI   0(R3),C'.'     HEX ADDRESS?
         BNE   *+L'*+8
         BAS   R14,NEXTCH     POSITION TO NEXT
         B     ERR2           +0
         BAS   R14,SCAN       +4 - GET ADDRESS
         B     ERR2                +0
         B     ERR17               +4
         B     ERR3                +8
         L     R6,DATA             +12 - SET ADDRESS
         B     ASS3+L'ASS3
ASS3     ICM   R6,B'1111',L'LBLF(R7)    SET ADDRESS
         NI    STATUS,255-SUBSTRCT
         BAS   R14,NEXT       POSITION TO NEXT
         B     ASSGN0         +0 - NO DISPLACEMENT
         CLI   0(R3),C'+'     +4 - TEST IF DELIMITER
         BE    ASS4
         CLI   0(R3),C'-'
         BNE   ERR10
         OI    STATUS,SUBSTRCT
ASS4     BAS   R14,NEXTCH     POSITION TO NEXT
         B     ERR9           +0
         BAS   R14,LABEL      +4 - GET DISPLACEMENT
         B     ERR9                +0
         B     ASS6                +4 - MAY BE HEX DISPLACEMENT
         B     ERR6                +8
         B     ASS5+L'ASS5         +12
         BAS   R14,NEXT            +16 - POSITION TO NEXT
         B     ASS5                      +0
         CLI   0(R3),C'/'                +4
         BNE   ERR37
         BAS   R14,NEXTCH     POSITION TO NEXT
         B     ERR37          +0
         BAS   R14,NAMER      +4 - GET DSECT-NAME
         B     ERR37               +0
         B     ERR38               +4
         B     ERR39               +8
         MVC   DRFDSCT,NMEF        +12 - DSECT NAME
         MVC   DRFFLDN,LBLF   FIELD NAME
         XC    DRFDSPL(L'DRFDSPL+L'DRFLNGH),DRFDSPL
         MVC   DRFFLGN(L'DRFFLGN),ALLBLKS
         XC    DRFFLVL(L'DRFFLVL+L'DRFFLLG),DRFFLVL
         LA    R0,1           ASK SEARCH
         L     R15,=A(DRFCALL)
         BASR  R14,R15
         B     *+L'*(R15)
         B     *+L'*+8        +0
         B     *+L'*+4        +4
         B     ERROR          +8
         XR    R0,R0
         ICM   R0,B'0011',DRFDSPL  SET DISPLACEMENT
         B     ASS10
ASS5     BAS   R14,SCLTBL
         ICM   R0,B'1111',L'LBLF(R7)    SET DISPLACEMENT
         B     ASS10
ASS6     CLI   0(R3),C'.'     HEX DISPLACEMENT?
         BNE   *+L'*+8
         BAS   R14,NEXTCH     POSITION TO NEXT
         B     ERR9           +0
         BAS   R14,SCAN       +4 - GET DISPLACEMENT
         B     ERR9                +0
         B     ERR17               +4
         B     ERR3                +8
         L     R0,DATA             +12
ASS10    TM    STATUS,SUBSTRCT     APPLY DISPLACEMENT
         BO    *+L'*+6
         ALR   R6,R0
         B     *+L'*+2
         SLR   R6,R0
         LA    R6,0(R6)
         B     ASSGN0
ASS20    CLI   PRVOP,C'S'
         BE    *+L'*+8
         L     R6,PVPTR
         B     ASSGN0
         L     R6,CRPTR
ASSGN0   ST    R6,DATA
         LM    R3,R5,TBVAL    SCAN LABELS TABLE
         LR    R2,R5
         ALR   R2,R4
         CLR   R5,R3
         BL    ASSGN3
ASSGN1   CLC   0(L'ASSNME,R3),ASSNME
         BNE   ASSGN2
         MVC   L'ASSNME(L'DATA,R3),DATA
         B     ASSGN4
ASSGN2   BXLE  R3,R4,ASSGN1
         CL    R2,=A(TABLEND)
         BNL   ERR8
ASSGN3   MVC   0(L'ASSNME,R2),ASSNME
         MVC   L'ASSNME(L'DATA,R2),DATA
         ST    R2,TBVAL+8
ASSGN4   MVI   PRVOP,C'D'
         MVC   PVPTR,DATA     PREVIOUS POINTER
         MVC   CRPTR,PVPTR    CURRENT POINTER
         B     SRTTB
         SPACE 1
DELET    MVC   CRPTR,PVPTR    ADJUST POINTERS FOR AFTER
         CLC   TBVAL+8(4),TBVAL
         BL    ERR19
         BAS   R14,NEXTCH     POSITION TO NEXT
         B     ERR5           +0
         BAS   R14,LABEL      +4 - GET LABEL
         B     ERR5                +0
         B     ERR7                +4
         B     ERR6                +8
         B     ERR26               +12
         LM    R3,R5,TBVAL         +16 - SCAN LABELS TABLE
DELET1   CLC   0(L'LBLF,R3),LBLF
         BE    DELET2
         BXLE  R3,R4,DELET1
         B     ERR20
DELET2   L     R2,TBVAL+8
         CLR   R2,R3
         BE    DELET3
         MVC   0(TBELL,R3),0(R2)   REPLACE BY LAST
DELET3   XC    0(TBELL,R2),0(R2)
         SL    R2,TBVAL+4
         ST    R2,TBVAL+8
         SPACE 1
SRTTB    CLC   TBVAL+8(4),TBVAL
         BNH   INQUIRT        TABLE EMPTIED OR JUST ONE
         LM    R4,R6,TBVAL    SORT LABELS TABLE
         XR    R2,R2
         LA    R3,0(R5,R6)
         SR    R3,R4
         DR    R2,R5
        $SORT  (R4),(R3),L'TABLE,0,L'LBLF
         B     INQUIRT
         SPACE 1
        @END   ,
         EJECT
PRDEF   @INIT  LABELS
         SPACE 1
***********************************************************************
*        LIST EXTERNAL/INTERNAL LABELS COMMANDS.                      *
***********************************************************************
         SPACE 1
         MVC   CRPTR,PVPTR    ADJUST POINTERS FOR AFTER
         NI    STATUS,255-ONELSTD-MATCHREQ
         LM    R3,R5,SCFIX    SCAN PREDEFINED LABELS TABLE
         TM    FSCRSW,FSCRSF  FULL SCREEN?
         BZ    LIST2          NO
         XC    FSPOINT,FSPOINT     YES, SAY DATA LINES
         OI    FSCRSW,FSCRSL
         B     LIST2
PRDEF1   LR    R2,R1
         MVC   MSGTXT(L'MSGTXT),ALLBLKS
         LA    R6,MSGTXT
         USING CNSLOUT,R6
         MVC   CNSLLB(L'CNSLLB),LBLF
         MVI   CNSLLS,C':'
         MVC   DATA(L'DATA),L'LBLF(R7)
         UNPK  CNSLAD(L'CNSLAD+1),DATA(L'DATA+1)
         TR    CNSLAD(L'CNSLAD),HEXTAB-X'F0'
         MVC   CNSLAD+L'CNSLAD(20),=CL20' (PREDEFINED LABEL)'
         DROP  R6
         BAS   R9,LISTOUT
         OI    STATUS,ONELSTD
         B     LIST0
LIST     MVC   CRPTR,PVPTR    ADJUST POINTERS FOR AFTER
         NI    STATUS,255-ONELSTD-MATCHREQ
         TM    FSCRSW,FSCRSF  FULL SCREEN?
         BZ    *+L'*+10       NO
         XC    FSPOINT,FSPOINT     YES, SAY DATA LINES
         OI    FSCRSW,FSCRSL
         BAS   R14,NEXTCH     POSITION TO NEXT
         B     LIST1          +0
         BAS   R14,LABEL      +4 - GET LABEL OR MATCH STRING
         B     LIST1               +0
         B     ERR7                +4
         B     ERR6                +8
         B     PRDEF1              +12
         LR    R2,R1               +16
LIST0    OI    STATUS,MATCHREQ
LIST1    CLC   TBVAL+8(4),TBVAL
         BL    ERR19
         LM    R3,R5,TBVAL    SCAN LABELS TABLE
LIST2    MVC   MSGTXT(L'MSGTXT),ALLBLKS
         LA    R6,MSGTXT
         USING CNSLOUT,R6
         LA    R7,3
LIST3    TM    STATUS,MATCHREQ
         BZ    LIST5
LIST4    EX    R2,CMTCH
         BE    LIST5
         BXLE  R3,R4,LIST4
         B     LIST7
CMTCH    CLC   0(*-*,R3),LBLF      <<EXECUTED>>
LIST5    MVC   CNSLLB(L'CNSLLB),0(R3)
         MVI   CNSLLS,C':'
         UNPK  CNSLAD(L'CNSLAD+1),L'LBLF(L'DATA+1,R3)
         TR    CNSLAD(L'CNSLAD),HEXTAB-X'F0'
         MVI   CNSLAD+L'CNSLAD,C' '
         DROP  R6
         LA    R6,CNSLBL(R6)
         BXLE  R3,R4,LIST6
         B     LIST7
LIST6    BCT   R7,LIST3
         BAS   R9,LISTOUT
         OI    STATUS,ONELSTD
         B     LIST2
LIST7    CLC   MSGTXT(L'MSGTXT),ALLBLKS
         BE    LIST8
         BAS   R9,LISTOUT
         B     *+L'*+8
LIST8    TM    STATUS,ONELSTD
         BZ    ERR20
         TM    FSCRSW,FSCRSF  FULL SCREEN?
         BZ    INQUIRY        NO
         NI    FSCRSW,255-FSCRSL   YES, RESET DATA
         L     R10,=A(INFST)
         DROP  R10
         USING INFST,R10
         B     INFST0         GO GET STATUS
         SPACE 1
        @END   ,
         EJECT
TEST    @INIT  ,
         SPACE 1
***********************************************************************
*        TEST ON/OFF AND LINE/SCREEN COMMAND.                         *
***********************************************************************
         SPACE 1
         MVC   CRPTR,PVPTR    ADJUST POINTERS FOR AFTER
         CLI   AUTH,LV15      AUTHORIZED?
         BL    ERR22          NO
         BAS   R14,NEXTCH     POSITION TO NEXT
         B     ERR4                +0
         CLC   0(4,R3),=CL4'OFF'   +4 - TEST OPERAND
         BE    TESTOFF
         CLC   0(3,R3),=CL3'ON'
         BE    TESTON
         CLI   0(R3),C'L'
         BE    TESTLNE
         CLI   0(R3),C'S'
         BE    TESTSCR
         TM    SGTPSW,STPDIS       SEGMENT-TABLE PROCESS DISABLED?
         BO    ERR49               YES
         CLI   0(R3),C'Y'
         BE    TESTYES
         CLI   0(R3),C'N'
         BE    TESTNO
         B     ERR4
TESTON   MVC   MSGTXT(L'MSGTXT),ALLBLKS
         MVC   MSGTXT(8),VCOREZAP+10
         MVI   MSGTXT+9,C':'
         ST    R12,DATA
         UNPK  MSGTXT+11(9),DATA(5)
         TR    MSGTXT+11(8),HEXTAB-X'F0'
         MVI   MSGTXT+19,C' '
         MVC   MSGTXT+22(10),=CL10'V.TABLE  :'
         UNPK  MSGTXT+33(9),TBVAL(5)
         TR    MSGTXT+33(8),HEXTAB-X'F0'
         MVI   MSGTXT+41,C' '
         MVC   MSGTXT+44(10),=CL10'V.FIXED  :'
         UNPK  MSGTXT+55(9),SCFIX(5)
         TR    MSGTXT+55(8),HEXTAB-X'F0'
         MVI   MSGTXT+63,C' '
        @SEND  MSGOUT,OP=LA
         OI    STATUS,TESTRUN
         B     INQUIRT
TESTOFF  NI    STATUS,255-TESTRUN
         B     INQUIRT
TESTLNE  TM    STATUS,TSORSC  ARE WE IN TSO ON A SCREEN?
         BZ    ERR21          NO
         TM    FSCRSW,FSCRSF  FULL SCREEN?
         BZ    ERR21          NO, ALREADY LINE WORKING
         L     R15,=A(FSCLEAR)
         BASR  R8,R15         RESET FULL SCREEN
         TM    HCPRSW,HCCOPY  IS HARDCOPY ACTIVE?
         BZ    INQUIRY        NO
         OI    HCPRSW,HCANSR
         L     R15,=A(HCOPYSC)     HARDCOPY COMMAND
         BASR  R14,R15
         B     INQUIRY
TESTSCR  TM    STATUS,TSORSC  ARE WE IN TSO ON A SCREEN?
         BZ    ERR21          NO
         TM    FSCRSW,FSCRSF  FULL SCREEN?
         BO    ERR21          YES, ALREADY SCREEN WORKING
         L     R15,=A(FSSETFS)     NO
         BASR  R8,R15         SET FULL SCREEN
         L     R10,=A(INFST)
         DROP  R10
         USING INFST,R10
         B     INFST0         GO GET STATUS
TESTYES  NI    SGTPSW,255-STPUSE
         B     INQUIRT
TESTNO   OI    SGTPSW,STPUSE
         B     INQUIRT
         SPACE 1
         DROP  R10
         EJECT
***********************************************************************
*        CONTROL ON/OFF COMMAND.                                      *
***********************************************************************
         SPACE 1
         USING TEST,R10       LOCAL ADDRESSABILITY
CNTRL    MVC   CRPTR,PVPTR    ADJUST POINTERS FOR AFTER
         CLI   AUTH,LV5       AUTHORIZED?
         BL    ERR22          NO
         BAS   R14,NEXTCH     POSITION TO NEXT
         B     ERR4                +0
         CLC   0(4,R3),=CL4'OFF'   +4 - TEST OPERAND
         BE    CNTRLOFF
         CLC   0(3,R3),=CL3'ON'
         BNE   ERR4
         OI    STATUS,DANGER
        @SEND  ENBLM
         B     INQUIRT
CNTRLOFF NI    SWITCH,255-VASTB    KILL VALID TABLE
         BAS   R8,DEACTIV     RESET AS-ID MODE IF ANY
         NI    STATUS,255-DANGER
        @SEND  DSBLM
         B     INQUIRT
         EJECT
***********************************************************************
*        DISPLAY ADDRESS SPACE SEGMENT-TABLE COMMAND.                 *
***********************************************************************
         SPACE 1
SGVIEW   CLI   AUTH,LV5            AUTHORIZED?
         BL    ERR22               NO
         TM    SGTPSW,STPDIS+STPUSE     YES, SEGMENT PROCESS DISABLED?
         BNZ   ERR49               YES
         OI    SGTPSW,STPLST       NO, SET DISPLAY REQUEST
         L     R10,=A(SGTCHK)
         BASR  R14,R10             GO DISPLAY SEGMENT-TABLE
         B     INQUIRT             END OR ERROR
         SPACE 1
        @END   ,
         EJECT
ASNID   @INIT  XM-REF
         SPACE 1
***********************************************************************
*        ADDRESS SPACE (CROSS MEMORY REFERENCE) COMMANDS.             *
***********************************************************************
         SPACE 1
         TM    STATUS,DANGER  AVAILABLE?
         BZ    ERR22          NO
         BAS   R14,NEXTCH     POSITION TO NEXT
         B     ERR1           +0 - UNKNOWN
         CLI   0(R3),C'H'     HELP
         BE    XH0
         TM    STATUS,TESTRUN
         BZ    *+L'*+6
         MVC   RCVIND(L'RCVIND),ALLBLKS
         NI    SWITCH,255-POPXMR-ASFULL RESET X-MEM. MODE AND A.S.
         CLI   0(R3),C'F'     FIND
         BE    XF0
         NI    SWITCH,255-PVALID   RESET PREVIOUS LAST DATA
         CLI   0(R3),C'A'     +4 - ACTIVATE
         BE    XA0
         CLI   0(R3),C'C'     CLEAR AND RESET
         BE    XC0
         CLI   0(R3),C'D'     DISPLAY
         BE    XD0
         CLI   0(R3),C'F'     FIND
         BE    XF0
         TM    FSCRSW,FSCRSF  FULL SCREEN?
         BZ    *+L'*+8        NO
         CLI   0(R3),C'G'     GET
         BE    XG0
         CLI   0(R3),C'L'     LIST
         BE    XL0
         CLI   0(R3),C'R'     RESET
         BE    XR0
         CLI   0(R3),C'S'     STORE
         BE    XS0
         CLI   0(R3),C'T'     REFRESH/LOAD AND RESET
         BE    XT0
         B     ERR1
*        --------------------------------------------------------------
*        ACTIVATE AS-ID MODE FOR NEXT XD, XF AND XS COMMANDS.
*        --------------------------------------------------------------
XA0      MVC   CRPTR,PVPTR    ADJUST POINTERS FOR AFTER
         TM    SWITCH,VASTB   VALID TABLE?
         BZ    ERR13          NO
         BAS   R14,NEXTCH     POSITION TO NEXT
         B     ERR2           +0
         BAS   R14,NAME       +4 - GET AS-NAME
         B     ERR14               +0
         B     XA5                 +4 - MAY BE AS-ID NO.
         B     ERR15               +8
         LM    R3,R5,SCASTB        +12 - SCAN AS-ID TABLE
XA1      CLC   0+L'ASIDN(L'ASNME,R3),NMEF    THIS NAME?
         BE    XA2            YES
         BXLE  R3,R4,XA1      LOOP TO NEXT
         B     ERR16          NOT FOUND
XA2      LR    R2,R3          RETAIN THIS ONE
         BXLE  R3,R4,XA3      LOOP TO NEXT
         B     XA4            NO MORE, OK
XA3      CLC   0+L'ASIDN(L'ASNME,R3),NMEF    THIS NAME?
         BE    ERR27          YES, MULTIPLE ...
         BXLE  R3,R4,XA3      LOOP TO NEXT
XA4      LR    R3,R2          POINT TO AS-TABLE ENTRY
         B     XA8
XA5      CLI   0(R3),C'.'     HEX AS-ID NO.?
         BNE   XA6
         BAS   R14,NEXTCH     POSITION TO NEXT
         B     ERR2           +0
XA6      BAS   R14,SCAN       +4 - GET AS-ID NUMBER
         B     ERR2                +0
         B     ERR17               +4
         B     ERR3                +8
         CH    R1,=H'4'            +12
         BH    ERR3
         LM    R3,R5,SCASTB   SCAN AS-ID TABLE
XA7      CLC   0(L'ASIDN,R3),DATA+2     THIS AS-ID NUMBER?
         BE    XA8            YES
         BXLE  R3,R4,XA7      LOOP TO NEXT
         B     ERR16          NOT FOUND
XA8      TM    SWITCH,ACTIVM  ALREADY ACTIVE MODE?
         BZ    XA9            NO
         CLC   0(L'ASIDN,R3),ASIDN SAME AS CURRENT?
         BE    INQUIRT        YES, NO CHANGE NEEDED
         BAS   R8,DEACTIV     NO, RESET AS-ID MODE
XA9      MVC   ASIDN(L'ASIDN),0(R3)     MAKE THE CHANGE
         MVC   ASNME(L'ASNME),0+L'ASIDN(R3)
         BAS   R8,ACTIVE      SET NEW AS-ID MODE
         B     INQUIRT
*        --------------------------------------------------------------
*        CLEAR AS-TABLE AND RESET AS-ID MODE.
*        --------------------------------------------------------------
XC0      MVC   CRPTR,PVPTR    ADJUST POINTERS FOR AFTER
         NI    SWITCH,255-VASTB    KILL VALID TABLE
         BAS   R8,DEACTIV     RESET AS-ID MODE
         B     INQUIRT
*        --------------------------------------------------------------
*        DISPLAY IN ACTIVE AS-ID.
*        --------------------------------------------------------------
XD0      TM    SWITCH,ACTIVM  ALREADY ACTIVE MODE?
         BZ    ERR28          NO
         OI    SWITCH,XMEMRF+POPXMR     SET/REMEMBER X-MEM.REF.
         L     R10,=A(DSPLY)  GO TO DISPLAY COMMAND
         BR    R10
*        --------------------------------------------------------------
*        FIND IN ACTIVE AS-ID.
*        --------------------------------------------------------------
XF0      TM    SWITCH,ACTIVM  ALREADY ACTIVE MODE?
         BZ    ERR28          NO
         OI    SWITCH,XMEMRF+POPXMR     SET/REMEMBER X-MEM.REF.
         L     R10,=A(FIND)   GO TO FIND COMMAND
         BR    R10
*        --------------------------------------------------------------
*        GET BYTES IN ACTIVE AS-ID.
*        --------------------------------------------------------------
XG0      TM    SWITCH,ACTIVM  ALREADY ACTIVE MODE?
         BZ    ERR28          NO
         OI    SWITCH,XMEMRF+POPXMR     SET/REMEMBER X-MEM.REF.
         L     R10,=A(DSPLY)
         DROP  R10
         USING DSPLY,R10
         B     GBYTES
         DROP  R10
         USING ASNID,R10
*        --------------------------------------------------------------
*        X-MEM REF. HELP COMMAND.
*        --------------------------------------------------------------
XH0      MVC   CRPTR,PVPTR    ADJUST POINTERS FOR AFTER
         TM    FSCRSW,FSCRSF  FULL SCREEN?
         BZ    XH1            NO
         OI    FSCRSW,FSCRSH  YES, SAY HELP PANEL
         L     R1,=V(VCOREZAH)
         USING HELPVP,R1
         LM    R2,R4,HLXH
         DROP  R1
         B     INQUIRY
XH1     @SEND  HELP80
        @SEND  HELP81
        @SEND  HELP82
        @SEND  HELP83
        @SEND  HELP84
        @SEND  HELP85
        @SEND  HELP86
        @SEND  HELP87
        @SEND  HELP88
        @SEND  HELP89
        @SEND  HELP90
         B     INQUIRY
*        --------------------------------------------------------------
*        LIST AS-TABLE OR A SPECIFIC AS-ID FROM AS-TABLE.
*        --------------------------------------------------------------
XL0      MVC   CRPTR,PVPTR    ADJUST POINTERS FOR AFTER
         TM    SWITCH,VASTB   VALID TABLE?
         BZ    ERR13          NO
         NI    STATUS,255-ONELSTD-MATCHREQ
         TM    FSCRSW,FSCRSF  FULL SCREEN?
         BZ    *+L'*+10       NO
         XC    FSPOINT,FSPOINT     YES, SAY DATA LINES
         OI    FSCRSW,FSCRSL
         BAS   R14,NEXTCH     POSITION TO NEXT
         B     XL4            +0 - LIST ALL
         BAS   R14,NAME       +4 - GET AS-NAME OR MATCH STRING
         B     ERR14               +0
         B     XL1                 +4 - MAY BE AS-ID NO.
         B     ERR15               +8
         LR    R2,R1               +12
         B     XL3
XL1      CLI   0(R3),C'.'     HEX AS-ID NO.?
         BNE   XL2
         BAS   R14,NEXTCH     POSITION TO NEXT
         B     ERR2           +0
XL2      BAS   R14,SCAN       +4 - GET AS-ID NUMBER
         B     ERR2                +0
         B     ERR17               +4
         B     ERR3                +8
         CH    R1,=H'4'            +12
         BH    ERR3
         LNR   R2,R1
XL3      OI    STATUS,MATCHREQ
XL4      LM    R3,R5,SCASTB   SCAN AS-ID TABLE
XL5      MVC   MSGTXT(L'MSGTXT),ALLBLKS
         LA    R6,MSGTXT
         USING CNSASOUT,R6
         LA    R7,4
XL6      TM    STATUS,MATCHREQ
         BZ    XL9
         LTR   R2,R2
         BM    XL8
XL7      EX    R2,XMTCH
         BE    XL9
         BXLE  R3,R4,XL7
         B     XL11
XL8      CLC   0(L'ASIDN,R3),DATA+2
         BE    XL9
         BXLE  R3,R4,XL8
         B     XL11
XL9      UNPK  CNSASID(L'CNSASID+1),0(L'ASIDN+1,R3)
         TR    CNSASID(L'CNSASID),HEXTAB-X'F0'
         MVI   CNSASID+L'CNSASID,C' '
         MVI   CNSASSP,C':'
         MVC   CNSASNM(L'CNSASNM),L'ASIDN(R3)
         L     R1,=A(CHARTB)
         TR    CNSASNM,0(R1)
         DROP  R6
         LA    R6,CNSASL(R6)
         BXLE  R3,R4,XL10
         B     XL11
XL10     BCT   R7,XL6
         BAS   R9,LISTOUT
         OI    STATUS,ONELSTD
         B     XL5
XL11     CLC   MSGTXT(L'MSGTXT),ALLBLKS
         BE    XL12
         BAS   R9,LISTOUT
         B     *+L'*+8
XL12     TM    STATUS,ONELSTD
         BZ    ERR16
         TM    FSCRSW,FSCRSF  FULL SCREEN?
         BZ    INQUIRY        NO
         NI    FSCRSW,255-FSCRSL   YES, RESET DATA
         L     R10,=A(INFST)
         DROP  R10
         USING INFST,R10
         B     INFST0         GO GET STATUS
         DROP  R10
         USING ASNID,R10
XMTCH    CLC   0+L'ASIDN(*-*,R3),NMEF   <<EXECUTED>>
*        --------------------------------------------------------------
*        RESET AS-ID MODE.
*        --------------------------------------------------------------
XR0      MVC   CRPTR,PVPTR    ADJUST POINTERS FOR AFTER
         BAS   R8,DEACTIV     RESET AS-ID MODE
         B     INQUIRT
*        --------------------------------------------------------------
*        STORE IN ACTIVE AS-ID.
*        --------------------------------------------------------------
XS0      CLI   AUTH,LV15      AUTHORIZED?
         BL    ERR22          NO
         TM    SWITCH,ACTIVM  ALREADY ACTIVE MODE?
         BZ    ERR28          NO
         OI    SWITCH,XMEMRF+POPXMR     SET/REMEMBER X-MEM.REF.
         L     R10,=A(STORE)  GO TO STORE COMMAND
         BR    R10
*        --------------------------------------------------------------
*        REFRESH/LOAD AS-TABLE AND RESET AS-ID MODE.
*        --------------------------------------------------------------
XT0      MVC   CRPTR,PVPTR    ADJUST POINTERS FOR AFTER
         BAS   R8,DEACTIV     RESET AS-ID MODE
         NI    SWITCH,255-VASTB    KILL VALID TABLE
         LM    R3,R4,SCASTB
         SLR   R3,R4
         L     R5,=A(ASTBE)
         USING PSA,R0
         L     R1,FLCCVT      GET CVT ADDRESS
         USING CVT,R1
         L     R2,CVTASVT     GET ASVT POINTER
         USING ASVT,R2
         L     R6,ASVTMAXU    NUMBER OF ASCB'S
         LA    R7,ASVTENTY    FIRST ENTRY ADDRESS
         DROP  R0,R1,R2
XT1      TM    0(R7),ASVTAVAL IS IT AN AVAILABLE ASID?
         BO    XT4            YES, GO GET NEXT
         L     R2,0(R7)       GET ASCB ADDRESS
         USING ASCB,R2
         CLC   MYASIDN,ASCBASID    IS IT MYSELF?
         BE    XT4            YES, GO GET NEXT
         L     R1,ASCBJBNI
         LTR   R1,R1
         BNZ   XT2
         L     R1,ASCBJBNS
         LTR   R1,R1
         BZ    XT4
XT2      BXLE  R3,R4,XT3
         ST    R5,SCASTB+8    SET END ADDRESS
         OI    SWITCH,VASTB   SET VALID TABLE
         B     ERR12
XT3      MVC   0(L'ASIDN,R3),ASCBASID
         MVC   0+L'ASIDN(L'ASNME,R3),0(R1)
         DROP  R2
XT4      LA    R7,4(R7)       NEXT ENTRY
         BCT   R6,XT1         LOOP UNTIL END
         CL    R3,SCASTB      TABLE OK?
         BL    INQUIRT        NO, LET INVALID AS-TABLE
         ST    R3,SCASTB+8    SET END ADDRESS
         OI    SWITCH,VASTB   SET VALID TABLE
         B     INQUIRT
         SPACE 1
        @END   ,
         EJECT
BRWSE   @INIT  DSECT''S
         SPACE 1
***********************************************************************
*        DSECTS INFO - DATA-SET COMMANDS.                             *
***********************************************************************
         SPACE 1
         MVC   CRPTR,PVPTR    ADJUST POINTERS FOR AFTER
         BAS   R14,NEXTCH     POSITION TO NEXT
         B     BRDIR          +0 - BROWSE DIRECTORY
         BAS   R14,NAMER      +4 - GET DSECT NAME
         B     ERR37               +0
         B     ERR38               +4
         B     ERR39               +8
         MVC   DRFDSCT,NMEF        +12 - DSECT NAME
         MVC   DRFFLDN(L'DRFFLDN),ALLBLKS
         XC    DRFDSPL(L'DRFDSPL+L'DRFLNGH),DRFDSPL
         MVC   DRFFLGN(L'DRFFLGN),ALLBLKS
         XC    DRFFLVL(L'DRFFLVL+L'DRFFLLG),DRFFLVL
         NI    OPERSW,255-DRF1ST-DRFL1F-DRFL2F
         BAS   R14,NEXTCH     POSITION TO NEXT
         B     BLIST          +0 - LIST DSECT
         BAS   R14,NAMER      +4 - GET FIELD OR FLAG NAME
         B     ERR40               +0
         B     BRW10               +4 - MAY BE HEX REQUEST
         B     ERR41               +8
         MVC   DRFFLDN,NMEF        +12 - FIELD OR FLAG NAME
         BAS   R14,NEXTCH     POSITION TO NEXT
         B     BRW20          +0
         BAS   R14,SCAN       +4 - GET FLAG HEX VALUE
         B     ERR42               +0
         B     ERR17               +4
         B     ERR43               +8
         OC    DATA(L'DATA-L'DRFFLVL),DATA   +12 - FLAG VALUE
         BNZ   ERR43
         MVC   DRFFLVL(L'DRFFLVL),DATA+L'DATA-L'DRFFLVL
         MVI   DRFFLGN,C'*'
         TM    FSCRSW,FSCRSF  FULL SCREEN?
         BZ    BRW1           NO
         XC    FSPOINT,FSPOINT     YES, SAY DATA LINES
         OI    FSCRSW,FSCRSL
BRW1     LA    R0,1           ASK SEARCH
         L     R15,=A(DRFCALL)
         BASR  R14,R15
         B     *+L'*(R15)
         B     BRW4           +0
         B     *+L'*+4        +4
         B     BRW5           +8
         LA    R6,MSGTXT+2    BROWSE BY FLAG VALUE
         TM    OPERSW,DRFL1F
         BO    BRW3
         MVC   MSGTXT(L'MSGTXT),ALLBLKS
         TM    OPERSW,DRF1ST
         BO    BRW2
         USING CNSDST,R6
         OI    OPERSW,DRF1ST
         MVC   CNSDSCT,DRFDSCT
         MVC   CNSDSF1,DRFFLDN
         MVI   CNSDSS1,C':'
         UNPK  CNSDSD1(L'CNSDSD1+1),DRFDSPL(L'DRFDSPL+1)
         TR    CNSDSD1(L'CNSDSD1),HEXTAB-X'F0'
         MVI   CNSDSD1+L'CNSDSD1,C' '
         UNPK  CNSDSL1(L'CNSDSL1+1),DRFLNGH(L'DRFLNGH+1)
         TR    CNSDSL1(L'CNSDSL1),HEXTAB-X'F0'
         MVI   CNSDSL1+L'CNSDSL1,C' '
         MVI   CNSDSS2,C'-'
BRW2     MVC   CNSDSF3,DRFFLGN
         MVI   CNSDSS4,C':'
         UNPK  CNSDSV1(L'CNSDSV1+1),DRFFLVL(L'DRFFLVL+1)
         TR    CNSDSV1(L'CNSDSV1),HEXTAB-X'F0'
         MVI   CNSDSV1+L'CNSDSV1,C' '
         OI    OPERSW,DRFL1F
         B     BRW1
BRW3     MVI   CNSDSS5,C'-'
         MVC   CNSDSF4,DRFFLGN
         MVI   CNSDSS6,C':'
         UNPK  CNSDSV2(L'CNSDSV2+1),DRFFLVL(L'DRFFLVL+1)
         TR    CNSDSV2(L'CNSDSV2),HEXTAB-X'F0'
         MVI   CNSDSV2+L'CNSDSV2,C' '
         NI    OPERSW,255-DRFL1F
         DROP  R6
         BAS   R9,LISTOUT
         B     BRW1
BRW4     TM    OPERSW,DRFL1F
         BZ    BRWEX
BRWLST   BAS   R9,LISTOUT
BRWEX    TM    FSCRSW,FSCRSF  FULL SCREEN?
         BZ    INQUIRY        NO
         NI    FSCRSW,255-FSCRSL   YES, RESET DATA
         L     R10,=A(INFST)
         DROP  R10
         USING INFST,R10
         B     INFST0         GO GET STATUS
         DROP  R10
         USING BRWSE,R10      LOCAL ADDRESSABILITY
BRW5     TM    OPERSW,DRFL1F
         BZ    ERROR
         LR    R6,R1
         BAS   R9,LISTOUT
         LR    R1,R6
         B     ERROR
BRW10    CLI   0(R3),C'.'     HEX DISPLACEMENT?
         BNE   *+L'*+8
         BAS   R14,NEXTCH     POSITION TO NEXT
         B     ERR40          +0
         BAS   R14,SCAN       +4 - GET DISPLACEMENT
         B     ERR45              +0
         B     ERR17              +4
         B     ERR46              +8
         OC    DATA(L'DATA-L'DRFFLVL),DATA   +12 - DISPLACEMENT
         BNZ   ERR46
         MVC   DRFDSPL(L'DRFDSPL),DATA+L'DATA-L'DRFDSPL
         TM    FSCRSW,FSCRSF  FULL SCREEN?
         BZ    BRW11          NO
         XC    FSPOINT,FSPOINT     YES, SAY DATA LINES
         OI    FSCRSW,FSCRSL
BRW11    LA    R0,1           ASK SEARCH
         L     R15,=A(DRFCALL)
         BASR  R14,R15
         B     *+L'*(R15)
         B     BRW13          +0
         B     *+L'*+4        +4
         B     BRW14          +8
         LA    R6,MSGTXT+9    BROWSE BY FIELD DISPLACEMENT
         TM    OPERSW,DRFL1F
         BO    BRW12
         MVC   MSGTXT(L'MSGTXT),ALLBLKS
         USING CNSDST,R6
         TM    OPERSW,DRF1ST
         BO    *+L'*+10
         OI    OPERSW,DRF1ST
         MVC   CNSDSCT,DRFDSCT
         OI    OPERSW,DRFL1F
         MVC   CNSDSF1,DRFFLDN
         MVI   CNSDSS1,C':'
         UNPK  CNSDSD1(L'CNSDSD1+1),DRFDSPL(L'DRFDSPL+1)
         TR    CNSDSD1(L'CNSDSD1),HEXTAB-X'F0'
         MVI   CNSDSD1+L'CNSDSD1,C' '
         UNPK  CNSDSL1(L'CNSDSL1+1),DRFLNGH(L'DRFLNGH+1)
         TR    CNSDSL1(L'CNSDSL1),HEXTAB-X'F0'
         MVI   CNSDSL1+L'CNSDSL1,C' '
         B     BRW11
BRW12    NI    OPERSW,255-DRFL1F
         MVI   CNSDSS2,C'-'
         MVC   CNSDSF2,DRFFLDN
         MVI   CNSDSS3,C':'
         UNPK  CNSDSD2(L'CNSDSD2+1),DRFDSPL(L'DRFDSPL+1)
         TR    CNSDSD2(L'CNSDSD2),HEXTAB-X'F0'
         MVI   CNSDSD2+L'CNSDSD2,C' '
         UNPK  CNSDSL2(L'CNSDSL2+1),DRFLNGH(L'DRFLNGH+1)
         TR    CNSDSL2(L'CNSDSL2),HEXTAB-X'F0'
         MVI   CNSDSL2+L'CNSDSL2,C' '
         DROP  R6
         BAS   R9,LISTOUT
         B     BRW11
BRW13    TM    OPERSW,DRFL1F
         BO    BRWLST
         B     BRWEX
BRW14    TM    OPERSW,DRF1ST
         BZ    ERROR
         LR    R6,R1
         BAS   R9,LISTOUT
         LR    R1,R6
         B     ERROR
BRW20    MVC   DRFFLGN,DRFFLDN
         MVC   DRFFLDN(L'DRFFLDN),ALLBLKS
         LA    R0,1           ASK SEARCH (FIRST TRY AS FLAG NAME)
         L     R15,=A(DRFCALL)
         BASR  R14,R15
         B     *+L'*(R15)
         B     *+L'*+8        +0
         B     ERR44          +4
         B     BRW21          +8
         TM    FSCRSW,FSCRSF  FULL SCREEN?
         BZ    *+L'*+10       NO
         XC    FSPOINT,FSPOINT     YES, SAY DATA LINES
         OI    FSCRSW,FSCRSL
         MVC   MSGTXT(L'MSGTXT),ALLBLKS
         LA    R6,MSGTXT+13   BROWSE BY FLAG NAME
         USING CNSDST,R6
         MVC   CNSDSCT,DRFDSCT
         MVC   CNSDSF1,DRFFLDN
         MVI   CNSDSS1,C':'
         UNPK  CNSDSD1(L'CNSDSD1+1),DRFDSPL(L'DRFDSPL+1)
         TR    CNSDSD1(L'CNSDSD1),HEXTAB-X'F0'
         MVI   CNSDSD1+L'CNSDSD1,C' '
         UNPK  CNSDSL1(L'CNSDSL1+1),DRFLNGH(L'DRFLNGH+1)
         TR    CNSDSL1(L'CNSDSL1),HEXTAB-X'F0'
         MVI   CNSDSL1+L'CNSDSL1,C' '
         MVI   CNSDSS2,C'-'
         MVC   CNSDSF3,DRFFLGN
         MVI   CNSDSS4,C':'
         UNPK  CNSDSV1(L'CNSDSV1+1),DRFFLVL(L'DRFFLVL+1)
         TR    CNSDSV1(L'CNSDSV1),HEXTAB-X'F0'
         MVI   CNSDSV1+L'CNSDSV1,C' '
         DROP  R6
         B     BRWLST
BRW21    MVC   DRFFLDN,DRFFLGN
         MVC   DRFFLGN(L'DRFFLGN),ALLBLKS
         TM    FSCRSW,FSCRSF  FULL SCREEN?
         BZ    BRW22          NO
         XC    FSPOINT,FSPOINT     YES, SAY DATA LINES
         OI    FSCRSW,FSCRSL
BRW22    LA    R0,1           ASK SEARCH (THEN TRY AS FIELD NAME)
         L     R15,=A(DRFCALL)
         BASR  R14,R15
         B     *+L'*(R15)
         B     BRW26          +0
         B     *+L'*+4        +4
         B     BRW28          +8
         LA    R6,MSGTXT+2    BROWSE BY FIELD NAME
         TM    OPERSW,DRF1ST
         BO    BRW23
         MVC   MSGTXT(L'MSGTXT),ALLBLKS
         USING CNSDST,R6
         OI    OPERSW,DRF1ST
         MVC   CNSDSCT,DRFDSCT
         MVC   CNSDSF1,DRFFLDN
         MVI   CNSDSS1,C':'
         UNPK  CNSDSD1(L'CNSDSD1+1),DRFDSPL(L'DRFDSPL+1)
         TR    CNSDSD1(L'CNSDSD1),HEXTAB-X'F0'
         MVI   CNSDSD1+L'CNSDSD1,C' '
         UNPK  CNSDSL1(L'CNSDSL1+1),DRFLNGH(L'DRFLNGH+1)
         TR    CNSDSL1(L'CNSDSL1),HEXTAB-X'F0'
         MVI   CNSDSL1+L'CNSDSL1,C' '
         B     BRW22
BRW23    TM    OPERSW,DRFL2F
         BO    BRW25
         TM    OPERSW,DRFL1F
         BO    BRW24
         OI    OPERSW,DRFL1F
         MVI   CNSDSS2,C'-'
         B     BRW24+L'BRW24
BRW24    MVC   MSGTXT(L'MSGTXT),ALLBLKS
         MVC   CNSDSF3,DRFFLGN
         MVI   CNSDSS4,C':'
         UNPK  CNSDSV1(L'CNSDSV1+1),DRFFLVL(L'DRFFLVL+1)
         TR    CNSDSV1(L'CNSDSV1),HEXTAB-X'F0'
         MVI   CNSDSV1+L'CNSDSV1,C' '
         OI    OPERSW,DRFL2F
         B     BRW22
BRW25    MVI   CNSDSS5,C'-'
         MVC   CNSDSF4,DRFFLGN
         MVI   CNSDSS6,C':'
         UNPK  CNSDSV2(L'CNSDSV2+1),DRFFLVL(L'DRFFLVL+1)
         TR    CNSDSV2(L'CNSDSV2),HEXTAB-X'F0'
         MVI   CNSDSV2+L'CNSDSV2,C' '
         NI    OPERSW,255-DRFL2F
         BAS   R9,LISTOUT
         B     BRW22
BRW26    TM    OPERSW,DRF1ST
         BZ    BRW27
         TM    OPERSW,DRFL2F
         BO    BRWLST
         B     BRWEX
BRW27    LA    R6,MSGTXT+13
         MVC   MSGTXT(L'MSGTXT),ALLBLKS
         MVC   CNSDSCT,DRFDSCT
         MVC   CNSDSF1,DRFFLDN
         MVI   CNSDSS1,C':'
         UNPK  CNSDSD1(L'CNSDSD1+1),DRFDSPL(L'DRFDSPL+1)
         TR    CNSDSD1(L'CNSDSD1),HEXTAB-X'F0'
         MVI   CNSDSD1+L'CNSDSD1,C' '
         UNPK  CNSDSL1(L'CNSDSL1+1),DRFLNGH(L'DRFLNGH+1)
         TR    CNSDSL1(L'CNSDSL1),HEXTAB-X'F0'
         MVI   CNSDSL1+L'CNSDSL1,C' '
         DROP  R6
         B     BRWLST
BRW28    TM    OPERSW,DRF1ST
         BZ    ERROR
         TM    OPERSW,DRFL2F
         BZ    ERROR
         LR    R6,R1
         BAS   R9,LISTOUT
         LR    R1,R6
         B     ERROR
         SPACE 1
BLIST    TM    FSCRSW,FSCRSF  FULL SCREEN?
         BZ    *+L'*+10       NO
         XC    FSPOINT,FSPOINT     YES, SAY DATA LINES
         OI    FSCRSW,FSCRSL
         MVC   DRFFRMN,DRFDSCT
         LA    R0,127         ASK MOD-ID
         L     R15,=A(DRFCALL)
         BASR  R14,R15
         B     *+L'*(R15)
         B     *+L'*+8        +0
         NOP   0              +4
         B     ERROR          +8
         MVC   MSGTXT(L'MSGTXT),ALLBLKS
         MVC   MSGTXT+2(L'DRFFRMN),DRFFRMN
         MVC   MSGTXT+L'DRFFRMN+4(12),=CL12'MACRO NAME ='
         MVC   MSGTXT+L'DRFFRMN+17(L'DRFFRMA),DRFFRMA
         MVC   MSGTXT+L'DRFFRMN+L'DRFFRMA+18(10),=CL10'/ ASS-ID ='
         MVC   MSGTXT+L'DRFFRMN+L'DRFFRMA+29(L'DRFFRMD),DRFFRMD
         BAS   R9,LISTOUT
BLS1     LA    R0,2           ASK LIST
         L     R15,=A(DRFCALL)
         BASR  R14,R15
         B     *+L'*(R15)
         B     BLS5           +0
         B     *+L'*+4        +4
         B     BLS6           +8
         LA    R6,MSGTXT+2
         CLC   DRFFLGN(L'DRFFLGN),ALLBLKS
         BNE   BLS2
         TM    OPERSW,DRFL1F
         BZ    *+L'*+8
         NI    OPERSW,255-DRFL1F-DRFL2F
         BAL   R9,LISTOUT
         MVC   MSGTXT(L'MSGTXT),ALLBLKS
         USING CNSDST,R6
         TM    OPERSW,DRF1ST
         BO    *+L'*+10
         OI    OPERSW,DRF1ST
         MVC   CNSDSCT,DRFDSCT
         MVC   CNSDSF1,DRFFLDN
         MVI   CNSDSS1,C':'
         UNPK  CNSDSD1(L'CNSDSD1+1),DRFDSPL(L'DRFDSPL+1)
         TR    CNSDSD1(L'CNSDSD1),HEXTAB-X'F0'
         MVI   CNSDSD1+L'CNSDSD1,C' '
         UNPK  CNSDSL1(L'CNSDSL1+1),DRFLNGH(L'DRFLNGH+1)
         TR    CNSDSL1(L'CNSDSL1),HEXTAB-X'F0'
         MVI   CNSDSL1+L'CNSDSL1,C' '
         OI    OPERSW,DRFL1F
         B     BLS1
BLS2     TM    OPERSW,DRFL1F
         BZ    BLS3
         TM    OPERSW,DRFL2F
         BO    BLS4
         MVI   CNSDSS2,C'-'
         B     BLS3+L'BLS3
BLS3     MVC   MSGTXT(L'MSGTXT),ALLBLKS
         MVC   CNSDSF3,DRFFLGN
         MVI   CNSDSS4,C':'
         UNPK  CNSDSV1(L'CNSDSV1+1),DRFFLVL(L'DRFFLVL+1)
         TR    CNSDSV1(L'CNSDSV1),HEXTAB-X'F0'
         MVI   CNSDSV1+L'CNSDSV1,C' '
         OI    OPERSW,DRFL1F+DRFL2F
         B     BLS1
BLS4     MVI   CNSDSS5,C'-'
         MVC   CNSDSF4,DRFFLGN
         MVI   CNSDSS6,C':'
         UNPK  CNSDSV2(L'CNSDSV2+1),DRFFLVL(L'DRFFLVL+1)
         TR    CNSDSV2(L'CNSDSV2),HEXTAB-X'F0'
         MVI   CNSDSV2+L'CNSDSV2,C' '
         DROP  R6
         NI    OPERSW,255-DRFL1F-DRFL2F
         BAS   R9,LISTOUT
         B     BLS1
BLS5     TM    OPERSW,DRFL1F
         BO    BRWLST
         B     BRWEX
BLS6     TM    OPERSW,DRF1ST+DRFL1F
         BNO   ERROR
         LR    R6,R1
         BAS   R9,LISTOUT
         LR    R1,R6
         B     ERROR
         SPACE 1
BRDIR    TM    FSCRSW,FSCRSF  FULL SCREEN?
         BZ    BRD1           NO
         XC    FSPOINT,FSPOINT     YES, SAY DATA LINES
         OI    FSCRSW,FSCRSL
BRD1     MVC   MSGTXT(L'MSGTXT),ALLBLKS
         LA    R0,64          ASK RD-DIR
         L     R15,=A(DRFCALL)
         BASR  R14,R15
         B     *+L'*(R15)
         B     BRD3           +0
         B     *+L'*+4        +4
         B     BRD4           +8
         LA    R6,MSGTXT+3
         CLI   0(R6),C' '
         BE    BRD2
         LA    R6,6+L'DRFFRMN+L'DRFFRMA(R6)
         CLI   0(R6),C' '
         BE    BRD2
         LA    R6,6+L'DRFFRMN+L'DRFFRMA(R6)
BRD2     MVC   0(L'DRFFRMN,R6),DRFFRMN
         CLC   DRFFRMA(L'DRFFRMA),ALLBLKS
         BE    *+L'*+10
         MVI   1+L'DRFFRMN(R6),C'-'
         MVC   3+L'DRFFRMN(L'DRFFRMA,R6),DRFFRMA
         CLI   MSGTXT+(2*(L'DRFFRMN+L'DRFFRMA+6))+3,C' '
         BE    BRD1+L'BRD1
         BAS   R9,LISTOUT
         B     BRD1
BRD3     CLI   MSGTXT+3,C' '
         BNE   BRWLST
         B     BRWEX
BRD4     CLI   MSGTXT+3,C' '
         BE    ERROR
         LR    R6,R1
         BAS   R9,LISTOUT
         LR    R1,R6
         B     ERROR
         SPACE 1
DSDST    MVC   CRPTR,PVPTR    ADJUST POINTERS FOR AFTER
         BAS   R14,NEXTCH     POSITION TO NEXT
         B     DSD10          +0 - GET STATISTICS
         CLI   0(R3),C'*'     +4 - JUST FREE ALL SPACE?
         BE    DSD1                YES
         LR    R2,R3
         BXLE  R3,R4,*+L'*+4
         B     *+L'*+8
         CLI   0(R3),C' '
         BNE   *-12
         SR    R3,R2
         CH    R3,=H'44'
         BH    ERR47
         STC   R3,DRFDSL      SET DATA-SET NAME
         MVI   DRFDSN,C' '
         MVC   DRFDSN+1(L'DRFDSN-1),DRFDSN
         BCT   R3,*+L'*+6
         MVC   DRFDSN(*-*),0(R2)   <<EXECUTED>>
         EX    R3,*-6
DSD1     XR    R0,R0          ASK START
         L     R15,=A(DRFCALL)
         BASR  R14,R15
         B     *+L'*(R15)
         B     INQUIRT        +0
         NOP   0              +4
         B     ERROR
DSD10    LA    R0,128         ASK STATS
         L     R15,=A(DRFCALL)
         BASR  R14,R15
         B     *+L'*(R15)
         B     *+L'*+8        +0
         NOP   0              +4
         B     ERROR
         MVC   MSGTXT(L'MSGTXT),ALLBLKS
         MVC   MSGTXT(8),=XL8'4020202020202120'
         ICM   R1,B'1111',DRFTOTN
         CVD   R1,DBLWD
         ED    MSGTXT(8),DBLWD+4
         MVC   MSGTXT+9(22),=CL22'LOAD(S) FOR A TOTAL OF'
         MVC   MSGTXT+31(6),=XL6'402020202120'
         ICM   R1,B'1111',DRFTOTL
         SLL   R1,3
         CL    R1,=A(16*KB)
         BH    *+L'*+10
         MVC   MSGTXT+38(5),=CL5'BYTES'
         B     DSD11
         AL    R1,=A(1*KB)
         SRL   R1,10
         MVC   MSGTXT+38(7),=CL7'K-BYTES'
         CL    R1,=A(1*KB)
         BNH   DSD11
         AL    R1,=A(1*KB)
         SRL   R1,10
         MVI   MSGTXT+38,C'M'
DSD11    CVD   R1,DBLWD
         ED    MSGTXT+31(6),DBLWD+5
        @SEND  MSGOUT,OP=LA
         B     INQUIRT
         SPACE 1
        @END   ,
         EJECT
HCPRTSS @INIT  SS-HCOPY
         SPACE 1
***********************************************************************
*        PRINT - START/STOP HARDCOPY COMMAND.                         *
***********************************************************************
         SPACE 1
         MVC   CRPTR,PVPTR    ADJUST POINTERS FOR AFTER
         TM    HCPRSW,HCFAIL  IS HARDCOPY OPEN FAILED?
         BO    ERR33          YES, BYPASS ALL
         TM    HCPRSW,HCCLOSE IS HARDCOPY TO BE CLOSED?
         BO    HCPSTOP        YES, GO DO IT
         BAS   R14,NEXTCH     POSITION TO NEXT
         B     HCPSTP              +0 - ASSUME STOP
         CLC   0(4,R3),=CL4'OFF'   +4 - IS PRINT OFF?
         BE    HCPSTP         YES, GO STOP IT
         CLC   0(3,R3),=CL3'ON'    IS IT ON?
         BNE   ERR4           NO, INVALID OPERAND
         TM    HCPRSW,HCCOPY  IS HARDCOPY ALREADY ACTIVE?
         BO    ERR32          YES
         LA    R1,H99TUKY3    INITIALIZE
         ST    R1,H99TUPL+8
         MVI   H99SYSOC,C'A'  SET DEFAULTS
         MVC   H99DEST(L'H99DEST),ALLBLKS
         XC    H99DESTL,H99DESTL
         LA    R3,2(R3)       PROCESS PARMS IF ANY
HCPNEXT  BAS   R14,NEXTCH     POSITION TO NEXT
         B     HCPNDEST            +0 - NONE OR NO MORE
         CLC   0(2,R3),=CL2'C('    +4 - SYSOUT CLASS?
         BE    HCPSCCHG       YES, CHANGE OF SYSOUT CLASS
         CLC   0(2,R3),=CL2'D('    DESTINATION?
         BNE   ERR4           NO, INVALID OPERAND
         LA    R3,2(R3)       EXTRACT DESTINATION NAME
         LR    R1,R3
         CLC   0(2,R3),=CL2') '
         BE    *+L'*+8
         BXLE  R3,R4,*-10
         B     ERR4
         LR    R14,R3
         SR    R14,R1
         BNP   ERR4
         STCM  R14,B'0011',H99DESTL     SET LENGTH
         BCTR  R14,0
         EX    R14,*+L'*+4         SET DESTINATION NAME
         B     HCPNEXT
         MVC   H99DEST(*-*),0(R1)  <<EXECUTED>>
HCPSCCHG CLC   3(2,R3),=CL2') '    VERIFY
         BNE   ERR4           INVALID OPERAND
         CLI   2(R3),C'A'     IS IT ALPHA?
         BL    ERR4           NO, INVALID OPERAND
         MVC   H99SYSOC,2(R3) MOVE IN PARM FOR SYSOUT CLASS
         LA    R3,5(R3)       BUMP
         B     HCPNEXT
HCPNDEST CLC   H99DEST(L'H99DEST),ALLBLKS
         BNE   *+L'*+6
         MVC   H99TUPL+8(4),H99TUPL+12  OVERRIDE DEST= PARM
         LA    1,H99RBPTR     ADDRESS OF PARM LIST FOR DYNALLOC
        DYNALLOC ,            DO THE ALLOCATION
         LTR   R15,R15        CHECK RETURN CODE?
         BNZ   HCPCANT        ERROR, CAN DO POST MESSAGE
         LA    R6,HCPYDCB     ADDRESS OF OUTPUT DCB
         USING IHADCB,R6      ADDRESSABILITY TO OUTPUT DCB
         MVC   DCBDDNAM(L'DCBDDNAM),ALLBLKS
         ICM   R15,B'0011',H99DDNL
         BCT   R15,*+L'*+6
         MVC   DCBDDNAM(*-*),H99DDN     <<EXECUTED>>
         EX    R15,*-6        SET DDNAME
        $TSWXA 24,EXPAND=ONLY BACK TO 24-BIT
        OPEN  (HCPYDCB,OUTPUT)     OPEN THE FILE
        $TSWXA 31,EXPAND=ONLY RE-ENTER 31-BIT
         TM    DCBOFLGS,DCBOFOPN   CHECK FOR SUCCESSFUL OPEN
         DROP  R6             KILL DCB ADDRESSABILITY
         BO    *+L'*+12       OK, OPENED
         NI    HCPRSW,255-HCCOPY   TURN OFF HARDCOPY FLAG
         OI    HCPRSW,HCFAIL  SET HARDCOPY OPEN FAILED FLAG
         B     ERR33
         OI    HCPRSW,HCCOPY  SAY HARDCPY FILE ACTIVE
         ZAP   HCPAGE#,=PL1'+0'    RESET PAGE NUMBER
         ZAP   HCLINE#,=PL2'+999'  RESET LINES COUNT
         MVC   MSGTXT(L'MSGTXT),ALLBLKS
         MVC   MSGTXT(33),=CL33'HARDCOPY STARTED : SYSOUT= ,DEST='
         MVC   MSGTXT+26(L'H99SYSOC),H99SYSOC     SYSOUT CLASS
         MVC   MSGTXT+33(L'H99DEST),H99DEST
         CLI   MSGTXT+33,C' ' ANY DEST?
         BNE   *+L'*+6
         MVC   MSGTXT+33(L'H99DEST),=CL8'LOCAL'   SAY LOCAL
HCPMSG  @SEND  MSGOUT,OP=LA
         B     INQUIRT
HCPCANT  CLC   H99ERROR,=XL2'046C' WAS IT 'RMT NOT DEF TO JES2'?
         BE    HCPBRMT        YES, POST MSG AND EXIT
         MVC   MSGTXT(L'MSGTXT),ALLBLKS
         MVC   MSGTXT(45),=CL45'HARDCOPY ALL. ERR. : DARC=     INFO=   1
                 R15='
         CVD   R15,HCWORK     CONVERT SVC 99 RETURN CODE
         MVC   MSGTXT+45(4),=XL4'40202020'   TO DEC
         ED    MSGTXT+45(4),HCWORK+6
         UNPK  MSGTXT+26(5),H99ERROR(3) ERR CODE
         TR    MSGTXT+26(4),HEXTAB-X'F0'
         MVI   MSGTXT+30,C' '
         UNPK  MSGTXT+36(5),H99INFO(3)  INFO CODE
         TR    MSGTXT+36(4),HEXTAB-X'F0'
         MVI   MSGTXT+40,C' '
         B     HCPMSG         GO SEND THE MESSAGE
HCPBRMT  MVC   MSGTXT(L'MSGTXT),ALLBLKS
         MVC   MSGTXT(49),=CL49'HARDCOPY ERR. : REMOTE          UNDEFIN1
               ED TO JES2'
         MVC   MSGTXT+23(L'H99DEST),H99DEST  RMT ASKED FOR
         B     HCPMSG         GO SEND THE MESSAGE
HCPSTP   TM    HCPRSW,HCCOPY  IS HARDCOPY ACTIVE?
         BZ    ERR34          NO
HCPSTOP $TSWXA 24,EXPAND=ONLY BACK TO 24-BIT
        CLOSE  (HCPYDCB)      CLOSE THE FILE
        FREEPOOL HCPYDCB      FREE THE BUFFERS TOO
        $TSWXA 31,EXPAND=ONLY RE-ENTER 31-BIT
         TM    HCPRSW,HCCLOSE WAS HARDCOPY TO BE CLOSED?
         BZ    HCPBACK        NO
         L     R10,=A(VCEND)  YES, BACK TO LEAVE
         DROP  R10
         USING VCEND,R10
         B     NOHCPY
         DROP  R10
         USING HCPRTSS,R10
HCPBACK  NI    HCPRSW,255-HCCOPY   TURN OFF HARDCOPY FLAG
        @SEND  HCSFM
         B     INQUIRT
         SPACE 1
        @END   ,
         EJECT
HCOPYSC @INIT  HARDCOPY
         SPACE 1
***********************************************************************
*        HARDCOPY ROUTINE.                                            *
*        AT ENTRY : R0 - TSO/TPUT LINE LENGTH                         *
*                   R1 - WTO ADDRESS                                  *
*                        TSO/TPUT LINE ADDRESS                        *
*                   R2 - FULL SCREEN DISPLAY ADDRESS                  *
*                   ANSR - WTOR REPLY                                 *
*                          TSO/TGET RESPONSE                          *
*                   R14 - RETURN ADDRESS                              *
*                   R15 - ENTRY ADDRESS                               *
***********************************************************************
         SPACE 1
         STM   R0,R15,HCSAVE  SAVE ALL REGISTERS AROUND HARDCOPY
         LR    R10,R15   LOCAL ADDRESSABILITY
        $TSWXA 24,EXPAND=ONLY BACK TO 24-BIT
         TM    FSCRSW,FSCRSF  FULL SCREEN?
         BO    HCFULL         YES
         TM    HCPRSW,HCANSR  WTOR OR TSO/TGET?
         BZ    HCSEND         NO
         NI    HCPRSW,255-HCANSR
         MVC   HCLINE(L'HCLINE),ALLBLKS
         OC    HCLINE+8(L'ANSR),ANSR    COPY COMMAND
         CLC   HCLINE(L'HCLINE),ALLBLKS
         BE    HCTEXIT+L'HCTEXIT   DON'T HARDCOPY JUST ENTER
         MVC   HCLINE(7),=CL7'ENTER :'
         BAS   R8,HCTPAGE     TEST IF EJECT NEEDED
HCPRTL  PUT    HCPYDCB,HCDETAIL
         B     HCTEXIT
HCSEND   MVC   HCLINE(L'HCLINE),ALLBLKS COPY TEXT
         TM    STATUS,TSORUN
         BZ    *+L'*+8
         LM    R14,R15,HCSAVE
         B     *+L'*+16
         L     R1,HCSAVE+4
         LH    R14,0(R1)
         SH    R14,=H'4'
         LA    R15,4(R1)
         BCTR  R14,0
         EX    R14,*+L'*+8
         BAS   R8,HCTPAGE     TEST IF EJECT NEEDED
         B     HCPRTL
         MVC   HCLINE(*-*),0(R15)  <<EXECUTED>>
HCFULL   BAS   R7,HCPHEAD     PREPARE HEADING
         MVI   HCHEAD1,C'0'   SET DOUBLE SPACE
         CP    HCLINE#,=PL2'+35'   EJECT TO BE SCHEDULED?
         BNH   HCNPAGE        NO, ... SKIP PAGE SETTING
         AP    HCPAGE#,=PL1'+1'    BUMP PAGE COUNT
         MVI   HCPAGS,C'/'
         MVC   HCPAGE,=XL6'402020202021'     MOVE IN MASK
         ZAP   HCWORK,HCPAGE# MOVE INTO AREA
         ED    HCPAGE,HCWORK+5     EDIT IN PAGE NUMBER
         MVI   HCHEAD1,C'1'   SET EJECT
         ZAP   HCLINE#,=PL1'+0'    RESET LINES COUNT
HCNPAGE PUT    HCPYDCB,HCHEAD1     PUT OUT TITLE LINE
        PUT    HCPYDCB,HCHEAD2     PUT OUT UNDERLINE
         AP    HCLINE#,=PL1'+3'    BUMP LINES COUNT
         MVC   HCLINE(L'HCLINE),ALLBLKS RECONSTRUCT IMAGE OF
         MVC   HCLINE(12),=CL12'COMMAND ===>'     COMMAND LINE
         OC    HCLINE+13(L'FSTEXT),FSTEXT
        PUT    HCPYDCB,HCDETAIL
         AP    HCLINE#,=PL1'+1'    BUMP LINES COUNT
         LA    R2,FSCRFIXL(R2)     SET SCAN REGISTERS
HCSNEXT  LA    R3,HCLINE
         LA    R4,L'HCLINE
         MVC   HCLINE,ALLBLKS
HCSLOOP  CLI   0(R2),X'1D'    IS IT A SF ORDER?
         BNE   *+L'*+8        NO
         LA    R2,1(R2)       SKIP IT
         B     HCTLNE
         CLI   0(R2),X'11'    IS IT A SBA ORDER?
         BE    HCTLEND        YES
         CLI   0(R2),X'3C'    IS IT A RA ORDER?
         BNE   HCTMOVE        NO
         MVI   0(R3),C'-'     YES, THIS IS STATUS LINE (LINE 21)
         MVC   1(L'HCLINE-3,R3),0(R3)
         MVC   36(8,R3),=CL8' STATUS '
         LA    R2,FSCRSTLL(R2)     SKIP IT
         B     HCTPDL
HCTMOVE  MVC   0(1,R3),0(R2)  NO, GET THIS CHAR IN LINE
HCTLNE   LA    R2,1(R2)       BUMP POINTERS
         LA    R3,1(R3)
         BCT   R4,HCSLOOP     CONTINUE THROUGH SCREEN
HCTPDL   CLC   HCLINE,ALLBLKS BLANK LINE?
         BE    HCSNEXT        YES
        PUT    HCPYDCB,HCDETAIL    PUT DETAIL LINE OUT
         AP    HCLINE#,=PL1'+1'    BUMP LINES COUNT
         B     HCSNEXT
HCTLEND  MVI   HCDETAIL,C'0'  DOUBLE SPACE
         MVC   HCLINE,ALLBLKS BLANK LINE
        PUT    HCPYDCB,HCDETAIL    AND PRINT IT
         MVI   HCDETAIL,C' '  RESTORE TO SINGLE SPACE
HCTEXIT  AP    HCLINE#,=PL1'+1'    BUMP LINES COUNT
        $TSWXA 31,EXPAND=ONLY RE-ENTER 31-BIT
         LM    R0,R15,HCSAVE  RESTORE ALL REGISTERS
         BR    R14            RETURN
         SPACE 1
HCPHEAD TIME   DEC       GET DATE/TIME FOR HEADING
         ST    R1,HCBDATE     SAVE DATE FOR LATER
         ST    R0,HCWORK CONVERT PACKED TO DEC
         MVI   HCWORK+3,X'0F'
         UNPK  DBLWD(7),HCWORK(4)
         MVC   HTMEHRS,DBLWD  MOVE TIME TO MSG
         MVC   HTMMINS,DBLWD+2
         MVC   HTMSECS,DBLWD+4
         MVC   DBLWD(4),HCBDATE    MOVE DATE TO WORK AREA
         MVO   DBLWD+1(3),DBLWD(2) MAKE 00YYDDDS INTO 00000YYS
         UNPK  HCYR(2),DBLWD+2(2)  FORMAT YEAR
         XC    HCWORK,HCWORK
         MVC   HCWORK+4(4),DBLWD   CONVERT YEAR TO BIN
         CVB   R0,HCWORK
         ST    R0,HCBYEAR     AND SAVE
         TM    HCBYEAR+3,X'03'     LEAP YEAR?
         BNZ   HCMN1          NO
         MVI   HCMNMSK+10,29  YES, CORRECT
HCMN1    XC    HCWORK,HCWORK  CONVERT DAYS TO BIN
         MVC   HCWORK+6(2),HCBDATE+2
         CVB   R14,HCWORK
         ST    R14,HCBDAYS    SAVE FOR LATER
         XR    R1,R1
         LA    R15,11
HCMN2    IC    R1,HCMNMSK(R15)     COMPUTE MON AND DAY
         SR    R14,R1
         BNP   *+L'*+8
         BCT   R15,HCMN2
         B     *+L'*+2
         AR    R14,R1         CORRECT OVERDRAW
         CVD   R14,HCWORK     FORMAT DAY
         UNPK  HCDAYN(2),HCWORK+6(2)
         OI    HCDAYN+1,C'0'  FIX UP SIGN
         MH    R15,=Y(L'HCMONTAB)  GET TABLE OFFSET
         LA    R15,HCMONTAB(R15)   AND POINT AT MONTH
         MVC   HCMON(L'HCMON),0(R15)    MOVE IT TO THE MSG
         L     R15,HCBYEAR    DAY OF WEEK
         SH    R15,=H'69'
         BNP   HCOUTT         MUST BE GREATER OR BAD
         XR    R14,R14        CLEAR DAY REG
         D     R14,=F'4'
         MH    R15,=H'5'
         AR    R14,R15
         A     R14,HCBDAYS
         SRDA  R14,32
         D     R14,=F'7'      MODULO 7 FOR WEEK
         MH    R14,=Y(L'HCDAYTAB)
         LA    R14,HCDAYTAB(R14)   POINT AT TODAY
         MVC   HCDAY(L'HCDAY),0(R14)    INSERT IN MSG
         B     HCMVEJD
HCOUTT   MVC   HCDAY(L'HCDAY),=CL3'???'
HCMVEJD  MVC   HJDATE,=XL7'4020204B202020'
         ED    HJDATE,HCBDATE+1
         MVI   HJDATE,C'='
         MVC   HCUSER,MYASNME MOVE IN USERID
         MVI   HCPAGS,C' '
         MVC   HCPAGE(L'HCPAGE),ALLBLKS CLEAR OUT PAGE FIELD
         BR    R7             RETURN
         SPACE 1
HCTPAGE  CP    HCLINE#,=PL2'+55'   EJECT TO BE SCHEDULED?
         BNHR  R8             NO, ... SKIP PAGE SETTING
         BAS   R7,HCPHEAD     PREPARE HEADING
         AP    HCPAGE#,=PL1'+1'    BUMP PAGE COUNT
         MVI   HCPAGS,C'/'
         MVC   HCPAGE,=XL6'402020202021'     MOVE IN MASK
         ZAP   HCWORK,HCPAGE# MOVE INTO AREA
         ED    HCPAGE,HCWORK+5     EDIT IN PAGE NUMBER
         MVI   HCHEAD1,C'1'   SET EJECT
         ZAP   HCLINE#,=PL1'+0'    RESET LINES COUNT
        PUT    HCPYDCB,HCHEAD1     PUT OUT TITLE LINE
        PUT    HCPYDCB,HCHEAD2     PUT OUT UNDERLINE
         AP    HCLINE#,=PL1'+3'    BUMP LINES COUNT
         BR    R8             RETURN
         SPACE 1
        @END   ,
         EJECT
SGTCHK  @INIT  'SEGMENT  TABLE   CHECK  PROCESS'
         SPACE 1
         STM   R0,R14,SGSAVE       SAVE REGISTERS 0-14
         L     R0,LSVSGT           SIZE FOR GETMAIN REQUEST
        GETMAIN RC,LV=(0),BNDRY=PAGE
         LTR   R15,R15
         BNZ   SGERR1
         ST    R1,ASVSGT           SAVE ADDRESS
         L     R0,=F'-1'
         ST    R0,0(R1)
         A     R1,=A(4*KB)
         ST    R0,0(R1)
         L     R1,=A(CC0)
         ST    R0,0(R1)
         MVI   CC1,X'FF'
         MVI   CC2,X'FF'
        SETAUTH ,                  SET APF
         L     R1,=A(SGELST)
        ESPIE  SET,MF=(E,(1))      INTERCEPT 0C4
         ST    R1,OLDPICA          SAVE OLD PICA ADDRESS
        MODESET KEY=ZERO,MODE=SUP  ENTER SYSTEM MODE
         ST    R13,SGSVR13
         LA    R13,SGASVA
        SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,REGS=SAVE,RELATED=O$R1
         L     R1,ASVSGT
         LR    R2,R1
         AL    R2,LSVSGT
         BCTR  R2,0
        PGSER  R,FIX,A=(1),EA=(2),ECB=0,BRANCH=Y,RELATED=FREEPAGE
        SETLOCK RELEASE,TYPE=LOCAL,REGS=SAVE,RELATED=R$O1
         L     R13,SGSVR13
         TM    SWITCH,ACTIVM       ACTIVE MODE?
         BZ    SGNOACT             NO
         L     R1,CVTPTR           POINT TO CVT
         USING CVT,R1
         L     R14,CVTASVT         POINT TO ASVT
         DROP  R1
         L     R1,WASID            GET SECONDARY WORK AS-ID
         USING ASVT,R14
         C     R1,ASVTMAXU         IS AS-ID TOO BIG?
         BH    ERR29               YES
         SLL   R1,2                (AS-ID)*4
         LA    R1,ASVTENTY-4(R1)   POINT AT ENTRY
         TM    0(R1),ASVTAVAL      IS IT AVAILABLE?
         BO    ERR30               NO
         DROP  R14
         L     R2,0(R1)            POINT TO ASCB
         USING ASCB,R2
         TM    ASCBRCTF,ASCBOUT    IS ASCB SWAPPED OUT?
         BO    ERR31               YES
         B     SGNOACT+L'SGNOACT
SGNOACT  L     R2,MYASCB           GET MY ASCB ADDRESS
         L     R3,ASCBASTE         POINTER TO ASTE
         DROP  R2
         USING ASTE,R3
         L     R4,ASTESTD          SEGMENT-TABLE DESIGNATOR
         DROP  R3
         SRDL  R4,7
         SRL   R4,5
         SLL   R4,13
         SRL   R4,1
         LR    R6,R4
         SRL   R5,25
         LA    R5,1(R5)
         SLL   R5,6                MULTIPLY BY (16*SGTEL)
         ST    R5,MVSGT
         LR    R7,R5
         CL    R7,=A(4*KB)         OVER PAGE SIZE?
         BNH   *+L'*+4             NO
         L     R5,=A(4*KB)         YES, GET 1ST PAGE
         L     R2,ASVSGT
         LR    R3,R5
         XR    R1,R1
         LRA   R2,0(R2)
         BZ    *+L'*+20
         LA    R1,4(R1)
         BM    *+L'*+12
         LA    R1,14(R1)
         BP    *+L'*+4
         LA    R1,14(R1)
         STC   R1,CC1
         CLI   CC1,0
         BNE   SGRCVRY
        DATOFF INDMVCL0
         CL    R7,=A(4*KB)
         BNH   SGRCVRY
         LR    R5,R7               GET 2ND PAGE
         S     R5,=A(4*KB)
         LR    R4,R6
         A     R4,=A(4*KB)
         L     R2,ASVSGT
         A     R2,=A(4*KB)
         LR    R3,R5
         XR    R1,R1
         LRA   R2,0(R2)
         BZ    *+L'*+20
         LA    R1,4(R1)
         BM    *+L'*+12
         LA    R1,14(R1)
         BP    *+L'*+4
         LA    R1,14(R1)
         STC   R1,CC2
         CLI   CC2,0
         BNE   SGRCVRY
        DATOFF INDMVCL0
SGRCVRY  ST    R13,SGSVR13
         LA    R13,SGASVA
        SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,REGS=SAVE,RELATED=O$R2
         L     R1,ASVSGT
         LR    R2,R1
         AL    R2,LSVSGT
         BCTR  R2,0
        PGSER  R,FREE,A=(1),EA=(2),BRANCH=Y,RELATED=FIXEPAGE
        SETLOCK RELEASE,TYPE=LOCAL,REGS=SAVE,RELATED=R$O2
         L     R13,SGSVR13
        MODESET KEY=NZERO,MODE=PROB     RESTORE PROBLEM MODE
        ESPIE  RESET,OLDPICA       RESTORE SPIE/ESPIE ENVIRONMENT
        RESAUTH ,                  RESET APF
         L     R15,=A(CC0)
         CLC   0(4,R15),=F'-1'
         BNE   SGERR2
         CLI   CC1,X'FF'
         BE    *+L'*+12
         XR    R15,R15
         IC    R15,CC1
         LTR   R15,R15
         BNZ   SGERR3
         CLI   CC2,X'FF'
         BE    *+L'*+12
         XR    R15,R15
         IC    R15,CC2
         LTR   R15,R15
         BNZ   SGERR4
         L     R2,ASVSGT
         CLC   0(4,R2),=F'-1'
         BE    SGERR5
         TM    SGTPSW,STPLST       DISPLAY REQUESTED?
         BO    SGLST               YES
         SPACE 1
***********************************************************************
*        CHECK ADDRESS WITH SEGMENT-TABLE.                            *
***********************************************************************
         SPACE 1
         L     R3,RCVPTR
         SRL   R3,20
         SLL   R3,2                MULTIPLY BY SGTEL
         LR    R4,R3
         TM    WRPTR,X'80'         FIRST TIME?
         BZ    *+L'*+8             NO
         L     R6,=F'-1'           YES, INHIBIT WRAP TEST
         B     SGCHECK
         L     R6,WRPTR
         SRL   R6,20
         SLL   R6,2                MULTIPLY BY SGTEL
SGCHECK  CL    R3,MVSGT
         BL    *+L'*+2
         XR    R3,R3               REMAINING M-BYTES INVALID, WRAP
         LA    R5,0(R2,R3)
         TM    3(R5),X'20'         TEST (I) BIT
         BZ    SGMBVL              THIS M-BYTE IS VALID
         CLR   R3,R6               WRAP DONE?
         BE    *+L'*+8             YES
         LA    R3,SGTEL(R3)        OTHERWISE FIND NEXT VALID
         B     SGCHECK
         L     R3,WRPTR
         LTR   R3,R3
         BNZ   SGSTAP
SGSTPP   L     R3,=A(X'7FFFF000')  2GB - 4KB (1 PAGE)
         ST    R3,RCVPTR
         B     SGXOK
SGMBVL   CLR   R3,R4
         BE    SGXOK
         ST    R3,CFFPTR           NEXT VALID M-BYTE ADDRESS
         LTR   R3,R3
         BZ    SGSTPP
         SLL   R3,18
SGSTAP   SL    R3,=A(4*KB)
         ST    R3,RCVPTR
         B     SGXOK
         SPACE 1
***********************************************************************
*        DISPLAY SEGMENT-TABLE.                                       *
***********************************************************************
         SPACE 1
SGLST    XR    R3,R3
         TM    FSCRSW,FSCRSF       FULL SCREEN?
         BZ    SGLST2              NO
         XC    FSPOINT,FSPOINT     YES, SAY DATA LINES
         OI    FSCRSW,FSCRSL
         B     SGLST3
SGLST1   TM    FSCRSW,FSCRSF       FULL SCREEN?
         BZ    SGLST3              NO
         CLC   FSPOINT,=F'81'      YES, NEW SCREEN-PAGE?
         BNE   SGLST3              NO
         NI    FSCRSW,255-FSCRSL   YES
SGLST2   MVC   MSGTXT(L'MSGTXT),ALLBLKS
         MVI   MSGTXT,C'"'
         TM    SWITCH,ACTIVM
         BO    *+L'*+10
         MVC   MSGTXT+1(L'MYASNME),MYASNME
         B     *+L'*+6
         MVC   MSGTXT+1(L'ASNME),ASNME
         LA    R1,MSGTXT+L'ASNME
         CLI   0(R1),C' '
         BNE   *+L'*+4
         BCT   R1,*-8
         MVC   1(29,R1),=CL29'" ADDRESS SPACE SEGMENT-TABLE'
        @SEND  MSGOUT,OP=LA
         TM    FSCRSW,FSCRSF       FULL SCREEN?
         BZ    SGLST3              NO
         OI    FSCRSW,FSCRSL       YES
SGLST3   LA    R5,0(R2,R3)
         LA    R0,4
         MVC   MSGTXT(L'MSGTXT),ALLBLKS
         LR    R1,R3
         SLL   R1,2
         ST    R1,DBLWD+4
         UNPK  DBLWD(3),DBLWD+6(2)
         OI    DBLWD+2,X'F0'
         TR    DBLWD(3),HEXTAB-X'F0'
         MVC   MSGTXT+1(3),DBLWD
         LA    R1,MSGTXT+13
SGLST4   LA    R4,0(R2,R3)
         MVC   DBLWD(SGTEL),0(R4)
         UNPK  0(2*SGTEL+1,R1),DBLWD(SGTEL+1)
         TR    0(2*SGTEL,R1),HEXTAB-X'F0'
         MVI   2*SGTEL(R1),C' '
         LA    R1,2*SGTEL+2(R1)
         BCT   R0,*+L'*+4
         B     *+L'*+8
         LA    R3,SGTEL(R3)
         B     SGLST4
         LR    R1,R3
         LA    R3,SGTEL(R3)
SGLST5   CL    R3,MVSGT
         BNL   SGLST6
         LA    R4,0(R2,R3)
         CLC   0(4*SGTEL,R4),0(R5)
         BNE   SGLST6
         LA    R3,4*SGTEL(R3)
         LA    R1,4*SGTEL(R1)
         B     SGLST5
SGLST6   SLL   R1,2
         ST    R1,DBLWD+4
         UNPK  DBLWD(3),DBLWD+6(2)
         OI    DBLWD+2,X'F0'
         TR    DBLWD(3),HEXTAB-X'F0'
         MVI   MSGTXT+5,C'-'
         MVC   MSGTXT+7(3),DBLWD
         MVI   MSGTXT+11,C':'
         BAS   R9,LISTOUT
         CL    R3,MVSGT
         BL    SGLST1
         TM    FSCRSW,FSCRSF       FULL SCREEN?
         BZ    SGXOK               NO
         NI    FSCRSW,255-FSCRSL   YES, RESET DATA
         B     SGXOK
         SPACE 1
***********************************************************************
*        ERRORS DIAGNOSE.                                             *
***********************************************************************
         SPACE 1
SGERR1   TM    FSCRSW,FSCRSF       FULL SCREEN?
         BZ    *+L'*+10            NO
         XC    FSPOINT,FSPOINT     YES, SAY DATA LINES
         OI    FSCRSW,FSCRSL
         MVC   MSGTXT(L'MSGTXT),ALLBLKS
         CVD   R15,DBLWD
         MVC   DBLWD(4),=XL4'40202120'
         ED    DBLWD(4),DBLWD+L'DBLWD-2
         MVC   MSGTXT(37),=CL37'GETMAIN OF 8K (2 PAGES) FAILED - RC ='
         MVC   MSGTXT+38(2),DBLWD+2
        @SEND  MSGOUT,OP=LA
         B     SGERRX
SGERR2   TM    FSCRSW,FSCRSF       FULL SCREEN?
         BZ    *+L'*+10            NO
         XC    FSPOINT,FSPOINT     YES, SAY DATA LINES
         OI    FSCRSW,FSCRSL
         MVC   MSGTXT(L'MSGTXT),ALLBLKS
         MVC   MSGTXT(36),=CL36'=> 0C4 INTERRUPT PROCESS AT OFFSET +'
         L     R2,=A(SV0C4)
         LA    R2,L'EPIEGPR(R2)
         MVC   DBLWD(4),4(R2)
         NI    DBLWD,255-X'80'
         CLC   DBLWD(4),BEGADD
         BL    *+L'*+10
         CLC   DBLWD(4),ENDADD
         BNH   *+L'*+10
         MVC   MSGTXT+25(15),=CL15'/ OUT OF BOUNDS'
         B     *+L'*+28
         L     R0,DBLWD
         SL    R0,BEGADD
         ST    R0,DBLWD
         UNPK  MSGTXT+36(5),DBLWD(5)
         TR    MSGTXT+36(4),HEXTAB-X'F0'
         MVI   MSGTXT+40,C' '
        @SEND  MSGOUT,OP=LA
         XR    R2,R2
         LA    R3,4
         L     R4,=A(SV0C4)
SGLBIG   LA    R5,4
         MVC   MSGTXT(L'MSGTXT),ALLBLKS
         LA    R6,MSGTXT
SGLLOW   MVC   1(2,R6),=CL2'GR'
         CVD   R2,DBLWD
         MVC   DBLWD(4),=XL4'40202120'
         ED    DBLWD(4),DBLWD+L'DBLWD-2
         MVC   4(2,R6),DBLWD+2
         MVI   7(R6),C'='
         UNPK  9(9,R6),0(L'SV0C4+1,R4)
         TR    9(8,R6),HEXTAB-X'F0'
         MVI   17(R6),C' '
         LA    R2,1(R2)
         LA    R4,L'SV0C4(R4)
         LA    R6,18(R6)
         BCT   R5,SGLLOW
        @SEND  MSGOUT,OP=LA
         BCT   R3,SGLBIG
         MVC   MSGTXT(L'MSGTXT),ALLBLKS
         MVC   MSGTXT+1(25),=CL25'EC MODE PROGRAM OLD PSW ='
         UNPK  MSGTXT+27(9),0(L'SV0C4+1,R4)
         TR    MSGTXT+27(8),HEXTAB-X'F0'
         MVI   MSGTXT+35,C' '
         LA    R4,L'SV0C4(R4)
         UNPK  MSGTXT+36(9),0(L'SV0C4+1,R4)
         TR    MSGTXT+36(8),HEXTAB-X'F0'
         MVI   MSGTXT+44,C' '
         LA    R4,L'SV0C4(R4)
        @SEND  MSGOUT,OP=LA
         MVC   MSGTXT(L'MSGTXT),ALLBLKS
         MVC   MSGTXT+1(31),=CL31'PROGRAM INTERRUPT INFORMATION ='
         UNPK  MSGTXT+33(9),0(L'SV0C4+1,R4)
         TR    MSGTXT+33(8),HEXTAB-X'F0'
         MVI   MSGTXT+41,C' '
         LA    R4,L'SV0C4(R4)
        @SEND  MSGOUT,OP=LA
         MVC   MSGTXT(L'MSGTXT),ALLBLKS
         MVC   MSGTXT+1(31),=CL31'TRANSLATION EXCEPTION ADDRESS ='
         UNPK  MSGTXT+33(9),0(L'SV0C4+1,R4)
         TR    MSGTXT+33(8),HEXTAB-X'F0'
         MVI   MSGTXT+41,C' '
        @SEND  MSGOUT,OP=LA
         B     SGERRX
SGERR3   TM    FSCRSW,FSCRSF       FULL SCREEN?
         BZ    *+L'*+10            NO
         XC    FSPOINT,FSPOINT     YES, SAY DATA LINES
         OI    FSCRSW,FSCRSL
         MVC   MSGTXT(L'MSGTXT),ALLBLKS
         MVC   MSGTXT(3),=CL3'1ST'
         BAS   R6,SGSTCC
         CLI   CC2,X'FF'
         BE    SGERRX
         XR    R15,R15
         IC    R15,CC2
         LTR   R15,R15
         BNZ   SGERRC
         B     SGERRX
SGERR4   TM    FSCRSW,FSCRSF       FULL SCREEN?
         BZ    SGERRC              NO
         XC    FSPOINT,FSPOINT     YES, SAY DATA LINES
         OI    FSCRSW,FSCRSL
SGERRC   MVC   MSGTXT(L'MSGTXT),ALLBLKS
         MVC   MSGTXT(3),=CL3'2ND'
         BAS   R6,SGSTCC
         B     SGERRX
SGSTCC   MVC   MSGTXT+4(8),=CL8'LRA CC ='
         MVI   MSGTXT+15,C'('
         B     *(R15)
         MVI   MSGTXT+13,C'1'
         MVC   MSGTXT+16(28),=CL28'SEGMENT-TABLE ENTRY INVALID)'
         B     *+L'*+24
         MVI   MSGTXT+13,C'2'
         MVC   MSGTXT+16(25),=CL25'PAGE-TABLE ENTRY INVALID)'
         B     *+L'*+10
         MVI   MSGTXT+13,C'3'
         MVC   MSGTXT+16(39),=CL39'SEGMENT- OR PAGE-TABLE LENGTH EXCEEDX
               ED)'
        @SEND  MSGOUT,OP=LA
         BR    R6
SGERR5   TM    FSCRSW,FSCRSF       FULL SCREEN?
         BZ    *+L'*+10            NO
         XC    FSPOINT,FSPOINT     SAY DATA LINES
         OI    FSCRSW,FSCRSL
        @SEND  SGERM1
SGERRX   TM    FSCRSW,FSCRSF       FULL SCREEN?
         BZ    *+L'*+4             NO
         NI    FSCRSW,255-FSCRSL   YES, RESET DATA
        @SEND  SGERM2
         OI    SGTPSW,STPDIS       DISABLE THIS PROCESSING
         LA    R2,4                SET COMPLETION CODE = ERROR
         B     SGXOK+L'SGXOK
SGXOK    XR    R2,R2               SET COMPLETION CODE = OK
         NI    SGTPSW,255-STPLST   RESET REQUEST(S)
         LM    R0,R1,LSVSGT        LENGTH-ADDRESS FOR FREEMAIN
         LTR   R1,R1
         BZ    SGQUIT
        FREEMAIN R,LV=(0),A=(1)    FREE WORKING STORAGE
         XC    ASVSGT,ASVSGT
SGQUIT   LR    R15,R2              PASS BACK THE COMPLETION CODE
         LM    R0,R14,SGSAVE       RESTORE REGISTERS 0-14
         BR    R14                 RETURN
         SPACE 1
        @END   ,
         EJECT
        $HEDIT ROUTINES
         SPACE 1
***********************************************************************
*        SUBROUTINE TO SET FULL SCREEN DISPLAY.                       *
*        REGISTERS USE : R0-R1-R14-R15 - WORK REGISTERS               *
*                        R8 - LINK REGISTER                           *
*                        R10 - LOCAL BASE REGISTER (PREVIOUS SAVED    *
*                              AND RESTORED AROUND ROUTINE BY R15)    *
***********************************************************************
         USING FSSETFS,R15
FSSETFS  ST    R10,FSR10SV         SAVE PREVIOUS R10
         LR    R10,R15             SET LOCAL BASE
         DROP  R15
         USING FSSETFS,R10
        STFSMODE ON,INITIAL=YES    VTAM INITIALIZATION
         MVI   FSCRSW,FSCRSF       SAY FULL SCREEN
         MVI   FSPRSW,0
         OC    SZLINE(L'SZLINE+L'SZSCRN),SZLINE   SIZE TO BE SET?
         BZ    FSS1                NO
        STSIZE SIZE=80,LINE=24     SET STANDARD SCREEN SIZE
         OI    FSCRSW,FSCRSS+FSCRSR     SAY SIZE SET AND TO BE RESET
FSS1     XC    FSTEXT(L'FSTEXT),FSTEXT
         MVC   FSCRMSG,ALLBLKS
         LA    R14,FSCRLNS
         LA    R15,16              RESET LINES 5 TO 20
FSS2    $FS    SF=(PROT),MF=(I,(R14))
         MVC   2(79,R14),ALLBLKS
         LA    R14,81(R14)
         BCT   R15,FSS2
         LA    R14,FSCRLST
         LA    R15,3               RESET LINES 22 TO 24
FSS3    $FS    SF=(PROT),MF=(I,(R14))
         MVC   2(79,R14),ALLBLKS
         LA    R14,81(R14)
         BCT   R15,FSS3
         MVC   FSCRSCL(L'FSCRSCL),ALLBLKS    RESET SCALING
        $FS    SF=(PROT),MF=(I,FSCRSDC)
         MVC   FSCRTDC(L'FSCRTDC),ALLBLKS    RESET DATA CONTINUE
         MVC   ANSR(L'ANSR),ALLBLKS
         L     R10,FSR10SV         RESTORE PREVIOUS R10
         DROP  R10
         BR    R8                  RETURN
         SPACE 1
***********************************************************************
*        SUBROUTINE TO RESET FULL SCREEN DISPLAY.                     *
*        REGISTERS USE : R0-R1-R14-R15 - WORK REGISTERS               *
*                        R8 - LINK REGISTER                           *
***********************************************************************
         USING FSCLEAR,R15
FSCLEAR  TM    FSCRSW,FSCRSF       FULL SCREEN?
         BZR   R8                  NO
         ST    R10,FSR10SV         SAVE PREVIOUS R10
         LR    R10,R15             SET LOCAL BASE
         DROP  R15
         USING FSCLEAR,R10
         TM    FSCRSW,FSCRSR       SCREEN SIZE TO BE RESET?
         BZ    FSCLEAZ             NO
        STSIZE SIZELOC=SZLINE,LINELOC=SZSCRN RESTORE
        $FS    CC=EWA,MF=(I,FSCLR)
FSCLEAZ TPUT   FSCLR,FSCLRL,FULLSCR,,HOLD    CLEAR SCREEN
        STFSMODE OFF               TELL VTAM WE'RE DONE
         MVI   FSCRSW,0            RESET ALL FLAGS
         MVI   FSPRSW,0
         L     R10,FSR10SV         RESTORE PREVIOUS R10
         DROP  R10
         BR    R8
         SPACE 1
FSR10SV  DC    F'0'
         SPACE 1
        LTORG
         SPACE 1
***********************************************************************
*        SUBROUTINE TO CALL DTUR ROUTINE FUNCTIONS.                   *
*        REGISTERS USE : R2-R10 SAVED AND RESTORED                    *
*                        R11-R13 UNCHANGED (MUST BE SET AND RETAINED) *
*        AT ENTRY : R0 - FUNCTION NUMBER                              *
*                   R14 - LINK REGISTER                               *
*                   R15 - ENTRY REGISTER                              *
*        AT EXIT : R15 - 0 = OPERATION CORRECTLY DONE (SEE DTUR)      *
*                        4 = OPERATION MAY CONTINUE (SEE DTUR)        *
*                        8 = OPERATION ERROR (R1 = MESSAGE ADDRESS /  *
*                            SEE DTUR WHEN RC IS 8 OR 12)             *
***********************************************************************
         USING DRFCALL,R15
DRFCALL  STM   R2,R10,DRFSV        SAVE R2-R10
         LR    R10,R15             SET LOCAL BASE
         DROP  R15
         USING DRFCALL,R10
         ST    R14,DRFRET
         STC   R0,DRFFSE
         CLI   DRFFSE,0            START
         BE    DRFSR1
         CLI   DRFFSE,1            SEARCH
         BE    DRFSL1
         CLI   DRFFSE,2            LIST
         BE    DRFSL1
         CLI   DRFFSE,64           RD-DIR
         BE    DRFRM1
         CLI   DRFFSE,127          MOD-ID
         BE    DRFRM1
         CLI   DRFFSE,128          STATS
         BE    DRFST1
         CLI   DRFFSE,255          END
         BE    DRFEN1
         LA    R1,DRFLSE           JUST TO HAVE DIAGNOSTIC MESSAGE
         BAS   R8,DRFDTUR
DRFSMS   MVI   DRFMSG+4,C' '
         MVC   DRFMSG+5(78),DRFMSG+4
         XR    R14,R14
         IC    R14,0(R1)
         CH    R14,=H'79'
         BNH   *+L'*+4
         LH    R14,=H'79'
         BCT   R14,*+L'*+6
         LTR   R14,R14
         BM    *+L'*+4
         EX    R14,DRFMVM
         CLC   DRFMSG+77(6),ALLBLKS
         BNE   DRFMEX
         LA    R14,DRFMSG+76
         CLI   0(R14),C' '
         BNE   *+L'*+4
         BCT   R14,*-8
         MVC   1(6,R14),=CL6'/RC=  '
         CVD   R15,DRFWKD
         UNPK  DRFWKD(5),DRFWKD+6(3)
         MVC   5(2,R14),DRFWKD+1
DRFMEX   LA    R1,DRFMSG           MESSAGE ADDRESS
         LA    R15,8               CC=8
DRFEXIT  L     R14,DRFRET
         LM    R2,R10,DRFSV        RESTORE R2-R10
         BR    R14
DRFMVM   MVC   DRFMSG+4(*-*),1(R1) <<EXECUTED>>
         SPACE 1
DRFSR1   TM    OPERSW,DRFDDS       DATA-SET ALREADY STARTED
         BZ    DRFSR2              NO
         MVI   DRFFSE,255          YES, CLOSE CURRENT
         LA    R1,DRFLSE
         BAS   R8,DRFDTUR
         NI    OPERSW,255-DRFDDS   SAY ENDED
         LTR   R15,R15
         BNZ   DRFSMS              ERROR
         MVI   DRFFSE,0            RESTORE START
DRFSR2   LA    R1,DRFLSE
         BAS   R8,DRFDTUR
         LTR   R15,R15
         BNZ   DRFSMS              ERROR
         OI    OPERSW,DRFDDS       SAY STARTED
         B     DRFEXIT
         SPACE 1
DRFSL1   MVC   DRFFSL,DRFFSE
         BAS   R7,DRFDSOP
         LA    R1,DRFLSL
DRFSS1   BAS   R8,DRFDTUR
         CH    R15,=H'4'
         BNH   DRFEXIT
         B     DRFSMS              ERROR
         SPACE 1
DRFRM1   MVC   DRFFRM,DRFFSE
         BAS   R7,DRFDSOP
         LA    R1,DRFLRM
         B     DRFSS1
         SPACE 1
DRFST1   XC    DRFTOTN(L'DRFTOTN+L'DRFTOTL),DRFTOTN
         LA    R1,DRFLST
         B     DRFSS1
         SPACE 1
DRFEN1   TM    OPERSW,DRFDDS       DATA-SET ALREADY ENDED
         BO    *+L'*+6             NO
         XR    R15,R15             YES, CC=0
         B     DRFEXIT
         LA    R1,DRFLSE
         BAS   R8,DRFDTUR
         LTR   R15,R15
         BNZ   DRFSMS              ERROR
         NI    OPERSW,255-DRFDDS   SAY ENDED
         B     DRFEXIT
         SPACE 1
DRFDSOP  TM    OPERSW,DRFDDS       DATA-SET ALREADY STARTED
         BOR   R7                  YES, RETURN
         MVI   DRFFSE,0            SAY START
         LA    R1,DRFLSE
         BAS   R8,DRFDTUR
         LTR   R15,R15
         BNZ   DRFSMS              ERROR
         OI    OPERSW,DRFDDS       SAY STARTED
         BR    R7                  RETURN
         SPACE 1
DRFDTUR $TSWXA 24,EXPAND=ONLY      BACK TO 24-BIT
         L     R15,=V(DTUSE)
         BASR  R14,R15
        $TSWXA 31,EXPAND=ONLY      RE-ENTER 31-BIT
         BR    R8
         DROP  R10
         SPACE 1
DRFWKD   DC    D'0'
DRFSV    DC    9F'0'               R2-R10
DRFRET   DC    F'0'                R14
DRFLSE   DC    A(X'80000000'+DRFFSE)
DRFLSL   DC    A(X'80000000'+DRFFSL)
DRFLRM   DC    A(X'80000000'+DRFFRM)
DRFLST   DC    A(X'80000000'+DRFFST)
DRFMSG  WTO    '----+----1----+----2----+----3----+----4----+----5----+1
               ----6----+----7----+----',MF=L
         SPACE 1
        LTORG
         EJECT
         DROP  R11
         SPACE 1
         DS    0D
VCWORK  $HEDIT WORK
         USING VCWORK,R11          SERVICE ROUTINES AND WORK AREAS
         SPACE 1
***********************************************************************
*        CONSTANTS AND WORK AREAS.                                    *
***********************************************************************
         SPACE 1
DBLWD    DC    D'0'
REQAUT   DC    0F'0',B'00000000',AL3(MDL@IX),AL4(AUTH)
ATSO     DC    A(*-*)
ACSQA    DC    A(*-*)              COMMON SQA SUBPOOL 226 ADDRESS
OLDPICA  DC    F'0'                SAVE OLD PICA ADDRESS
SVSADD   DC    F'0'
BEGADD   DC    A(VCOREZAP)
ENDADD   DC    A(VCOREEND)
SVAD     DC    A(*-*)
SCVAL    DC    A(ANSR,1,ANSR+L'ANSR-1)
SCFIX    DC    A(FIXTB,L'FIXTB,FIXTBE)
TBVAL    DC    A(TABLE,L'TABLE,TABLE-L'TABLE)
SCASTB   DC    A(ASTB,ASTBL,*-*)
ATKWAS   DC    A(STKWAS)
CATKWA   DC    A(*-*)
ECBR     DC    F'0'
AXPL     DC    0F'0',H'1',H'0'
SASID    DC    F'0'                SECONDARY AS-ID
WASID    DC    F'0'                SECONDARY WORK AS-ID
MYASCB   DC    F'0'                MY ASCB ADDRESS
RCXR     DC    F'0'                AXRES RETURN CODE
RCXS     DC    F'0'                AXSET RETURN CODE
RCXF     DC    F'0'                AXFRE RETURN CODE
DTPTR    DC    F'0'                DATA ADDRESS
PVPTR    DC    F'0'                PREVIOUS DATA POINTER
CRPTR    DC    F'0'                CURRENT DATA POINTER
RCVPTR   DC    F'0'                RECOVERY DATA POINTER
BLKPTR   DC    F'0'                BLOCK DATA ADDRESS
WRPTR    DC    F'0'                A.S. WRAP POINTER
CFFPTR   DC    F'0'                CONTROL FIND FULL POINTER
WDSLN    DC    F'0'
WDSPT    DC    F'0'
GFBYT    DC    F'0'
GLBYT    DC    F'0'
RETCC    DC    F'0'                RETURN CODE (CC)
DATA     DC    F'0',X'0'
AUTH     DC    XL1'0'
LBLF     DC    CL8' '
NMEF     DC    CL8' '
ASSNME   DC    CL8' '
FXLAB    DC    XL8'0',X'0'
TSTBT    DC    XL1'0'
TRANTBL  DC    X'000A0B0C0D0E0F'
         DC    9X'00'
         DC    X'00010203040506070809'
         DC    6X'00'
MSGOUT   DC    0F'0',AL2(MSGLL),XL2'0'
MSGTXT   DC    CL72' '
MSGLL    EQU   *-MSGOUT
ANSR     DC    CL72' '
MSGSVTO  DC    0F'0',AL2(MSGSVTL),XL2'0'
MSGSVT   DC    CL79' '
MSGSVTL  EQU   *-MSGSVTO
STRNGL   DC    XL1'0'              LENGTH OF FIND DATA
STRNG    DC    XL16'0'             STORAGE OF FIND DATA
TBUF     DC    XL40'0'             TEMPORARY STORAGE OF DATA
TSRTL    DC    C'INPUT TOO LONG - RESPECIFY :'
MYASIDN  DC    H'0'                MY AS-ID NUMBER
ASIDN    DC    H'0'                AS-ID NUMBER
MYASNME  DC    CL8' '              MY AS-ID NAME
ASNME    DC    CL8' '              AS-ID NAME
CTKWAN   DC    CL4' '              CURRENT TANK NUMBER
PRVOP    DC    CL1'S'
GNBYT    DC    XL1'0'
ADVICE   DC    CL5' '
RCVIND   DC    CL5' '
HEXTAB   DC    CL16'0123456789ABCDEF'   HEX TABLE TRANSLATION
ALLBLKS  DC    CL80' '
         EJECT
***********************************************************************
*        DTUR ROUTINE FUNCTIONS WORK AREAS.                           *
***********************************************************************
         SPACE 1
DRFFSE   DC    AL1(*-*)            FUNCTION = 0 (START) OR 255 (END)
DRFDSL   DC    AL1(16)
DRFDSN   DC    CL44'SYS.TDSECTS.DATA'
DRFFSL   DC    AL1(*-*)            FUNCTION = 1 (SEARCH) OR 2 (LIST)
DRFDSCT  DC    CL8' '
DRFFLDN  DC    CL8' '
DRFDSPL  DC    XL2'0'
DRFLNGH  DC    XL2'0'
DRFFLGN  DC    CL8' '
DRFFLVL  DC    XL2'0'
DRFFLLG  DC    XL2'0'
DRFFRM   DC    AL1(*-*)       FUNCTION = 64 (RD-DIR) OR 127 (MOD-ID)
DRFFRMN  DC    CL8' '
DRFFRMA  DC    CL8' '
DRFFRMD  DC    CL16' '
DRFFST   DC    AL1(128)            FUNCTION = 128 (STATS)
DRFTOTN  DC    XL4'0'
DRFTOTL  DC    XL4'0'
         EJECT
***********************************************************************
*        FULL SCREEN DISPLAY WORK AREAS.                              *
***********************************************************************
         SPACE 1
FSRCVR   DC    7F'0'               SAVE R0-R6 (OVERLAP IN NEXT FIELD)
FSSAVE   DC    9F'0'               SAVE R2-R10 (OR R7-R15 OF PREVIOUS)
SZLINE   DC    F'0'                LINE SIZE (LINE LENGTH)
SZSCRN   DC    F'0'                SCREEN SIZE (NUMBER OF LINES)
FSPOINT  DC    F'0'                SCREEN LINE POINTER
FSRPLYL  EQU   66                  LENGTH OF REPLY AREA
FSCODE   DC    XL6'0'              AID + JUNK
FSTEXT   DC    XL(FSRPLYL)'0'      REAL REPLY
REPLYL   EQU   L'FSCODE+L'FSTEXT
LSTDSP   DC    XL(FSRPLYL)'0'      RETAIN LAST DISPLAY COMMAND
         SPACE 1
FSCREEN $FS    CC=W,MF=L           FULL SCREEN DEFINITIONS
        $FS    WCC=(KBR,RMDT),SBA=(24,80),MF=L
        $FS    SBA=(1,1),SF=(PROT),RA=(1,30,'-'),MF=L       LINE 1
        $FS    SF=(PROT,INT),TEXT='VIRTUAL CORE   Z A P',MF=L
        $FS    SF=(PROT),RA=(1,80,'-'),MF=L
        $FS    SF=(PROT,INT),MF=L
        $FS    TEXT='COMMAND ===>',MF=L                     LINE 2
        $FS    SBA=(2,80),MF=L     SKIP OVER COMMAND INPUT AREA
        $FS    SF=(PROT,INT),MF=L
FSCRFIXL EQU   *-FSCREEN           LENGTH OF FIXED PART (LINES 1 AND 2)
FSCRMSG $FS    TEXT=(' ',80),MF=L                           LINE 3
        $FS    SF=(PROT),MF=L                               LINE 4
FSCRSCL $FS    TEXT=(' ',54),MF=L
        $FS    TEXT=(' ',18),MF=L
FSCRSDC $FS    SF=(PROT),MF=L
FSCRTDC $FS    TEXT=(' ',6),MF=L
        PRINT  NOGEN
FSCRLNS @WL    16                                     LINES 5 TO 20
        PRINT  GEN
        $FS    SF=(PROT),MF=L                               LINE 21
FSCRSTL $FS    RA=(21,37,'-'),MF=L
        $FS    SF=(PROT,INT),TEXT='STATUS',MF=L
        $FS    SF=(PROT),RA=(21,80,'-'),MF=L
        $FS    TEXT=' ',MF=L
FSCRSTLL EQU   *-FSCRSTL
        PRINT  NOGEN
FSCRLST @WL    3                                     LINES 22 TO 24
        PRINT  GEN
FSCRLNW  EQU   *-FSCREEN
        $FS    SBA=(2,13),SF=NORMAL,MF=L     COMMAND INPUT AREA
FSCRTXT  DC    XL(FSRPLYL)'0'
        $FS    SBA=(1,1),SF=(PT,IC),MF=L
FSCRLNG  EQU   *-FSCREEN
         SPACE 1
FSCLR   $FS    CC=EW,MF=L          CLEAR SCREEN DEFINITIONS
        $FS    WCC=(AL,KBR,RMDT),SBA=(24,79),MF=L
        $FS    SBA=(1,1),RA=(1,1,00),MF=L
        $FS    SF=(INT,IC),MF=L
FSCLRL   EQU   *-FSCLR
         EJECT
***********************************************************************
*        PROCESSING SWITCHES.                                         *
***********************************************************************
         SPACE 1
STATUS   DC    XL1'0'         STATUS INDICATORS
         SPACE 1
FTYPE    EQU   X'80'               FIND TYPE IS CHAR
MATCHREQ EQU   X'40'               LIST MATCH REQUESTED
ONELSTD  EQU   X'20'               AT LEAST ONE LINE LISTED
SUBSTRCT EQU   X'10'               SUBSTRACT REQUESTED
TSORUN   EQU   X'08'               WE ARE RUNNING IN TSO
TSORSC   EQU   X'04'               WE ARE RUNNING IN TSO ON A SCREEN
TESTRUN  EQU   X'02'               TEST RUN REQUESTED
DANGER   EQU   X'01'               BE CARE WITH ALTERATION
         SPACE 1
SWITCH   DC    XL1'0'         ACTION INDICATORS
         SPACE 1
VASTB    EQU   X'80'               VALID AS-ID TABLE
ACTIVM   EQU   X'40'               ACTIVE AS-ID MODE
XMEMRF   EQU   X'20'               X-MEM. REF. REQUEST
POPXMR   EQU   X'10'               PREVIOUS OP. X-MEM. REF. MODE
ASFULL   EQU   X'08'               FIND THROUGH ENTIRE A.S.
PVALID   EQU   X'04'               VALID PREVIOUS LAST 16 BYTES
TSTEAP   EQU   X'02'               TEST END ADDRESS PROTECTION
CSQAF    EQU   X'01'               COMMON SQA SUBPOOL 226
         SPACE 1
OPERSW   DC    XL1'0'         OPERATION INDICATORS
         SPACE 1
FFSTUP   EQU   X'80'               FIND FULL STATUS UPDATE
INPEND   EQU   X'40'               INPUT IS PENDING
GETBSW   EQU   X'20'               GET BYTES REQUESTED
DRFDDS   EQU   X'10'               DTUR DATA-SET STARTED
DRF1ST   EQU   X'08'               DTUR 1ST OF LIST DONE
DRFL1F   EQU   X'04'               DTUR LIST 1ST FLIP-FLOP
DRFL2F   EQU   X'02'               DTUR LIST 2ND FLIP-FLOP
         EJECT
FSCRSW   DC    XL1'0'         FULL SCREEN INDICATORS
         SPACE 1
FSCRSF   EQU   X'80'               FULL SCREEN
FSCRSS   EQU   X'40'               SET SCREEN SIZE
FSCRSR   EQU   X'20'               RESET SCREEN SIZE
FSCRSC   EQU   X'10'               SET CONTINUATION
FSCRSH   EQU   X'08'               HELP PANEL DISPLAY
FSCRST   EQU   X'04'               SET STATUS LINES
FSCRSL   EQU   X'02'               SET DATA LINES
FSCRSI   EQU   X'01'               SET INTENSIVE DISPLAY LINE
         SPACE 1
FSPRSW   DC    XL1'0'         FULL SCREEN PROCESSING INDICATORS
         SPACE 1
FSPRDS   EQU   X'80'               FULL SCREEN DISPLAY FOR RECOVER
         SPACE 1
HCPRSW   DC    XL1'0'         HARDCOPY REQUESTS - STATUS
         SPACE 1
HCCOPY   EQU   X'80'               HARDCOPY ACTIVE
HCCLOSE  EQU   X'40'               CLOSE HARDCOPY
HCFAIL   EQU   X'20'               HARDCOPY OPEN FAILED
HCANSR   EQU   X'01'               WTOR OR TSO/TGET DONE
         EJECT
***********************************************************************
*        HARDCOPY WORK AREAS.                                         *
***********************************************************************
         SPACE 1
HCSAVE   DC    16F'0'
HCWORK   DC    D'0'
         SPACE 1
H99RBPTR DC    A(H99RB+X'80000000')     SVC 99 REQUEST BLOCK PTR
H99RB    DS    0F             SVC 99 REQUEST BLOCK
         DC    AL1(H99RBLN)             RB LENGTH IN BYTES
H99VERB  DC    AL1(S99VRBAL)            VERB CODE (DSNAME ALL.)
H99FLAG1 DC    AL1(S99NOCNV+S99NOMNT,0) FLAGS 1
H99ERROR DC    XL2'0'                   ERROR CODE
H99INFO  DC    XL2'0'                   INFO CODE
H99TXTPP DC    A(H99TUPL)               POINTER TO TEXT UNIT PTRS
H99RSVD1 DC    XL4'0'                   RESERVED
H99FLAG2 DC    XL4'0'                   FLAGS 2
H99RBLN  EQU   *-H99RB                  RB LENGTH
H99TUPL  DS    0F                  TEXT UNIT POINTERS
         DC    A(H99TUKY1)              SYSOUT
         DC    A(H99TUKY2)              FREE AT CLOSE
         DC    A(H99TUKY3)              LAST PARM IF NO DEST=
         DC    A(H99TUKY4+X'80000000')  LAST PARM IF DEST= GIVEN
H99TUKY1 DC    AL2(DALSYSOU),AL2(1),AL2(1)
H99SYSOC DC    CL1'A'                   SYSOUT=A
H99TUKY2 DC    AL2(DALCLOSE),AL2(0)     UNALLOC AT CLOSE
H99TUKY3 DC    AL2(DALSUSER),AL2(1)     OPTIONAL : DEST=RMTXXX
H99DESTL DC    AL2(*-*)                 LENGTH OF DEST
H99DEST  DC    CL8' '                   DEST PARAMETER
H99TUKY4 DC    AL2(DALRTDDN),AL2(1)     DDNAME RETURN
H99DDNL  DC    AL2(8)                   LENGTH OF DDNAME
H99DDN   DC    CL8' '                   DDNAME
         SPACE 1
HCPYDCB  DCB   DDNAME=VCZHCOPY,DSORG=PS,MACRF=(PM),                    1
               RECFM=FA,LRECL=91,BLKSIZE=91
         SPACE 1
HCBYEAR  DC    F'0'
HCBDAYS  DC    F'0'
HCBDATE  DC    F'0'
HCPAGE#  DC    PL3'+1'             PAGE NUMBER
HCLINE#  DC    PL3'+0'             LINES COUNT
HCMNMSK  DC    AL1(31,30,31,30,31,31,30,31,30,31,28,31)
HCDAYTAB DC    CL3'TUE',C'WEDTHUFRISATSUNMON'
HCMONTAB DC    CL3'DEC',C'NOVOCTSEPAUGJULJUNMAYAPRMARFEBJAN'
         SPACE 1
HCHEAD1  DC    CL1'1',CL10' '      ASA ON HEADING
         DC    CL23'VCZ-HARDCOPY LOG  USER='
HCUSER   DC    CL7' ',CL2' '       FOR USERID
         DC    CL4'DATE'
HJDATE   DC    CL7' ',CL3' - '
HTMEHRS  DC    CL2' ',CL1':'
HTMMINS  DC    CL2' ',CL1':'
HTMSECS  DC    CL2' ',CL1' '
HCDAY    DC    CL3' ',CL1' '
HCMON    DC    CL3' ',CL1' '
HCDAYN   DC    CL2' ',CL3',19'
HCYR     DC    CL2' ',CL1' '
HCPAGS   DC    CL1' ',CL2'  '
HCPAGE   DC    CL6' '
         SPACE 1
HCHEAD2  DC    CL1' ',CL10' ',80C'-'
         SPACE 1
HCDETAIL DC    CL1' ',CL10' '      ASA CONTROL CHARACTER
HCLINE   DC    CL80' '             TO HOLD PRINT LINE
         EJECT
***********************************************************************
*        SEGMENT-TABLE CHECK WORK AREAS.                              *
***********************************************************************
         SPACE 1
SGASVA   DC    18F'0'
SGSAVE   DC    15F'0'
SGSVR13  DC    F'0'
SGTEL    EQU   4                   SEGMENT-TABLE ENTRY LENGTH
LSVSGT   DC    A(2*KB*SGTEL)
ASVSGT   DC    A(*-*)
MVSGT    DC    F'0'
CC1      DC    XL1'0'
CC2      DC    XL1'0'
         SPACE 1
SGTPSW   DC    XL1'0'         SEGMENT-TABLE PROCESSING INDICATORS
STPDIS   EQU   X'80'               SEGMENT-TABLE PROCESSING DISABLED
STPUSE   EQU   X'40'               SEGMENT-TABLE PROCESSING INHIBIT
STPLST   EQU   X'01'               SEGMENT-TABLE DISPLAY REQUEST
         SPACE 1
*--- --- END OF BASE REGISTER (R11) ADDRESSING --- --- --- --- --- ---*
         EJECT
         DROP  R13
         SPACE 1
         DS    0D
VSVERR  $HEDIT ERRORS
         USING VSVERR,R13          SAVE AREA AND ERROR RETURNS
         SPACE 1
***********************************************************************
*        SAVE AREA.                                                   *
***********************************************************************
         SPACE 1
         DC    18F'0'
         SPACE 1
***********************************************************************
*        ERROR RETURNS.                                               *
***********************************************************************
         SPACE 1
ERR1     L     R1,=A(ERM1)
         B     ERROR
ERR2     L     R1,=A(ERM2)
         B     ERROR
ERR3     L     R1,=A(ERM3)
         B     ERROR
ERR4     L     R1,=A(ERM4)
         B     ERROR
ERR5     L     R1,=A(ERM5)
         B     ERROR
ERR6     L     R1,=A(ERM6)
         B     ERROR
ERR7     L     R1,=A(ERM7)
         B     ERROR
ERR8     L     R1,=A(ERM8)
         B     ERROR
ERR9     L     R1,=A(ERM9)
         B     ERROR
ERR10    L     R1,=A(ERM10)
         B     ERROR
ERR11    L     R1,=A(ERM11)
         B     ERROR
ERR12    L     R1,=A(ERM12)
         B     ERROR
ERR13    L     R1,=A(ERM13)
         B     ERROR
ERR14    L     R1,=A(ERM14)
         B     ERROR
ERR15    L     R1,=A(ERM15)
         B     ERROR
ERR16    L     R1,=A(ERM16)
         B     ERROR
ERR17    L     R1,=A(ERM17)
         B     ERROR
ERR18    L     R1,=A(ERM18)
         B     ERROR
ERR19    L     R1,=A(ERM19)
         B     ERROR
ERR20    L     R1,=A(ERM20)
         B     ERROR
ERR21    L     R1,=A(ERM21)
         B     ERROR
ERR22    L     R1,=A(ERM22)
         B     ERROR
ERR23    L     R1,=A(ERM23)
         B     ERROR
ERR24    L     R1,=A(ERM24)
         B     ERROR
ERR25    L     R1,=A(ERM25)
         B     ERROR
ERR26    L     R1,=A(ERM26)
         B     ERROR
ERR27    L     R1,=A(ERM27)
         B     ERROR
ERR28    L     R1,=A(ERM28)
         B     ERROR
ERR29    NI    SWITCH,255-XMEMRF-POPXMR-ASFULL-PVALID  RESET
         BAS   R8,DISABLE ---------------------------------- * DANGER *
         L     R1,=A(ERM29)
         B     ERROR
ERR30    NI    SWITCH,255-XMEMRF-POPXMR-ASFULL-PVALID  RESET
         BAS   R8,DISABLE ---------------------------------- * DANGER *
         L     R1,=A(ERM30)
         B     ERROR
ERR31    NI    SWITCH,255-XMEMRF-POPXMR-ASFULL-PVALID  RESET
         BAS   R8,DISABLE ---------------------------------- * DANGER *
         L     R1,=A(ERM31)
         B     ERROR
ERR32    L     R1,=A(ERM32)
         B     ERROR
ERR33    L     R1,=A(ERM33)
         B     ERROR
ERR34    L     R1,=A(ERM34)
         B     ERROR
ERR35    L     R1,=A(ERM35)
         B     ERROR
ERR36    L     R1,=A(ERM36)
         B     ERROR
ERR37    L     R1,=A(ERM37)
         B     ERROR
ERR38    L     R1,=A(ERM38)
         B     ERROR
ERR39    L     R1,=A(ERM39)
         B     ERROR
ERR40    L     R1,=A(ERM40)
         B     ERROR
ERR41    L     R1,=A(ERM41)
         B     ERROR
ERR42    L     R1,=A(ERM42)
         B     ERROR
ERR43    L     R1,=A(ERM43)
         B     ERROR
ERR44    L     R1,=A(ERM44)
         B     ERROR
ERR45    L     R1,=A(ERM45)
         B     ERROR
ERR46    L     R1,=A(ERM46)
         B     ERROR
ERR47    L     R1,=A(ERM47)
         B     ERROR
ERR48    L     R1,=A(ERM48)
         B     ERROR
ERR49    L     R1,=A(ERM49)
         B     ERROR
         SPACE 1
        LTORG
         SPACE 1
*--- --- END OF BASE REGISTER (R13) ADDRESSING --- --- --- --- --- ---*
         EJECT
         DROP  R11,R12,R13         KILL ADDRESSABILITIES
         SPACE 2
***********************************************************************
*        NEXT COMMAND REQUEST.                                        *
***********************************************************************
         SPACE 1
INQRY   WTOR   'ENTER :',ANSR,L'ANSR,ECBR,MF=L
         EJECT
***********************************************************************
*        INTERCEPT 0C4 PARAMETER LISTS.                               *
***********************************************************************
         SPACE 1
ESPLST  ESPIE  SET,ESPEXIT,(4),MF=L
         SPACE 1
SGELST  ESPIE  SET,SGEXIT,(4,17),MF=L
         EJECT
***********************************************************************
*        INTERCEPT 0C4 ROUTINES.                                      *
***********************************************************************
         SPACE 1
         CNOP  0,8
         USING *,R15
         USING EPIE,R1
ESPEXIT  MVC   EPIEPSW+4(4),RCVADDR     SET RETURN ADDRESS
         BR    R14                 BACK TO CONTROL PROGRAM
         DROP  R1
         SPACE 1
RCVADDR  DC    A(X'80000000'+RECOVER0)
         DROP  R15
         SPACE 2
         CNOP  0,8                 INTERCEPT 0C4 EXIT ROUTINE
         USING *,R15
         USING EPIE,R1
SGEXIT   MVC   EPIEPSW+4(4),SGRCVA SET RETURN ADDRESS
         MVC   SV0C4(PLGTH),EPIEGPR     MOVE ALL INFORMATION FROM EPIE
         XC    CC0,CC0             SAY INTERCEPTED
         BR    R14                 BACK TO CONTROL PROGRAM
         DROP  R1
         SPACE 1
SGRCVA   DC    A(X'80000000'+SGRCVRY)
CC0      DC    F'0'
PLGTH    EQU   L'EPIEGPR+L'EPIEPSW+L'EPIEINT+L'EPIETEA
NWRDS    EQU   PLGTH/4
SV0C4    DC    (NWRDS)F'0'
         DROP  R15
         EJECT
***********************************************************************
*        MESSAGES TEXT.                                               *
***********************************************************************
         SPACE 1
        PRINT  NOGEN
INITM  @WTO    '    --- VIRTUAL CORE ZAP ---'
INITH  @WTO    '    ENTER H TO OBTAIN COMMANDS SUMMARY'
UNAUT  @WTO    'UNAUTHORIZED USE - END PROCESS FORCED'
ENBLM  @WTO    'STORE ANYWHERE ENABLED. - TAKE CARE |||'
DSBLM  @WTO    'STORE ANYWHERE DISABLED. - O.K.'
XRFAIL @WTO    'AXRES (RESERVE AUTHORIZATION INDEX) FAILED'
XSFAIL @WTO    'AXSET (SET AUTHORIZATION INDEX) FAILED'
XFFAIL @WTO    'AXFRE (FREE AUTHORIZATION INDEX) FAILED'
DISRC  @WTO    '    R15 (RC) =           '
DISCC  @WTO    '    0C4 --- PROTECTION AT :           '
NFNDAS @WTO    'NOT FOUND - ADDRESS SPACE SCAN ENDED'
HCSFM  @WTO    'HARDCOPY STOPPED - SYSOUT FREED FOR PRINT'
SGERM1 @WTO    'SEGMENT-TABLE EMPTY (NO ENTRIES HAS BEEN MOVED)'
SGERM2 @WTO    'SEGMENT-TABLE PROCESS ERROR (DISABLED FOR EVER)'
         SPACE 1
ERM1   @WTO    'UNKNOWN COMMAND - REQUEST IGNORED'
ERM2   @WTO    'OPERAND MISSING - REQUEST IGNORED'
ERM3   @WTO    'OPERAND TOO LONG - REQUEST IGNORED'
ERM4   @WTO    'INVALID OPERAND - REQUEST IGNORED'
ERM5   @WTO    'LABEL MISSING - REQUEST IGNORED'
ERM6   @WTO    'LABEL TOO LONG - REQUEST IGNORED'
ERM7   @WTO    'INVALID LABEL - REQUEST IGNORED'
ERM8   @WTO    'LABELS TABLE FULL - REQUEST IGNORED'
ERM9   @WTO    'DISPLACEMENT MISSING - REQUEST IGNORED'
ERM10  @WTO    '+ OR - OPERATOR MISSING - REQUEST IGNORED'
ERM11  @WTO    'NO ACTIVE USER-AREA - REQUEST IGNORED'
ERM12  @WTO    'AS-ID TABLE FULL : REMAINING AS-ID LOST'
ERM13  @WTO    'NO VALID AS-ID TABLE - REQUEST IGNORED'
ERM14  @WTO    'INVALID NAME - REQUEST IGNORED'
ERM15  @WTO    'NAME TOO LONG - REQUEST IGNORED'
ERM16  @WTO    'NOT FOUND IN TABLE - REQUEST IGNORED'
ERM17  @WTO    'INVALID HEX DIGITS ENTERED - REQUEST IGNORED'
ERM18  @WTO    'ODD NO. OF DIGITS ENTERED - REQUEST IGNORED'
ERM19  @WTO    'NO LABELS ENTERED IN TABLE - REQUEST IGNORED'
ERM20  @WTO    'UNDEFINED LABEL - REQUEST IGNORED'
ERM21  @WTO    'INVALID COMMAND - REQUEST IGNORED'
ERM22  @WTO    'UNAUTHORIZED COMMAND - REQUEST IGNORED'
ERM23  @WTO    'UNAUTHORIZED DISPLAY - REQUEST IGNORED'
ERM24  @WTO    'UNAUTHORIZED STORE - REQUEST IGNORED'
ERM25  @WTO    'UNAUTHORIZED FIND - REQUEST IGNORED'
ERM26  @WTO    'PREDEFINED LABEL NAME - REQUEST IGNORED'
ERM27  @WTO    'NOT UNIQUE AS-ID NAME - REQUEST IGNORED'
ERM28  @WTO    'NO AS-ID MODE ACTIVE - REQUEST IGNORED'
ERM29  @WTO    'INVALID ACTIVE AS-ID - REQUEST IGNORED / RESET'
ERM30  @WTO    'UNEXISTANT ACTIVE AS-ID - REQUEST IGNORED / RESET'
ERM31  @WTO    'ACTIVE AS-ID SWAP-OUT - REQUEST IGNORED / RESET'
ERM32  @WTO    '=== HARDCOPY IS ALREADY ACTIVE ==='
ERM33  @WTO    'SORRY, ... HARDCOPY OPEN FAILED'
ERM34  @WTO    '=== HARDCOPY IS ALREADY INACTIVE ==='
ERM35  @WTO    'FINAL DELIMITER MISSING - REQUEST IGNORED'
ERM36  @WTO    'USER-AREA EMPTY - REQUEST IGNORED'
ERM37  @WTO    'DSECT-NAME MISSING - REQUEST IGNORED'
ERM38  @WTO    'INVALID DSECT-NAME - REQUEST IGNORED'
ERM39  @WTO    'DSECT-NAME TOO LONG - REQUEST IGNORED'
ERM40  @WTO    'DSECT REQUEST MISSING - REQUEST IGNORED'
ERM41  @WTO    'FIELD OR FLAG NAME TOO LONG - REQUEST IGNORED'
ERM42  @WTO    'INVALID FLAG VALUE - REQUEST IGNORED'
ERM43  @WTO    'FLAG VALUE TOO LONG - REQUEST IGNORED'
ERM44  @WTO    'UNEXPECTED RC=4 FROM "DTUR" ROUTINE'
ERM45  @WTO    'INVALID DISPLACEMENT - REQUEST IGNORED'
ERM46  @WTO    'DISPLACEMENT TOO LONG - REQUEST IGNORED'
ERM47  @WTO    'DATA-SET TOO LONG - REQUEST IGNORED'
ERM48  @WTO    'INVALID NUMBER OF BYTES (1-16) - REQUEST IGNORED'
ERM49  @WTO    'SEGMENT-TABLE PROCESS HAS BEEN DISABLED FOR EVER'
         EJECT
***********************************************************************
*        HELP MESSAGES TEXT.                                          *
***********************************************************************
         SPACE 1
HELP01 @WTO  'VIRTUAL STORAGE COMMANDS SUMMARY :'
HELP02 @WTO  '  D NNNN : DISPLAY 16 BYTES AT LOCATION NNNN'
HELP03 @WTO  '           (ADDR + OR - DISPL ALLOWED).'
HELP04 @WTO  '           JUST ENTER OR TYPE ONLY D TO DISPLAY'
HELP05 @WTO  '           16 HEX BYTES STARTING AT THE CURRENT OR'
HELP06 @WTO  '           +16 ADDRESS POINTER (CONTINUE DISPLAY).'
HELP07 @WTO  '           UPDATED BY D, S, F AND A COMMANDS.'
HELP08 @WTO  '           ENTER + OR - DECIMAL 1 UP TO 999 TO DISPLAY'
HELP09 @WTO  '           FORWARD/BACKWARD FROM CURRENT POINTER.'
HELP10 @WTO  '  S NNNN HHHH,... OR @N : STORE HHHH,... OR CONTENTS OF'
HELP11 @WTO  '           USER-AREA N AT LOC. NNNN'
HELP12 @WTO  '           (ADDR + OR - DISPL ALLOWED).'
HELP13 @WTO  '  S * HHHH,... OR @N : CONTINUE STORE AT LOC. IN SEQ.'
HELP14 @WTO  '  F ''...'' OR HHHH... : TO FIND A STRING.'
HELP15 @WTO  '           JUST ENTER OR TYPE ONLY F TO CONTINUE'
HELP16 @WTO  '           THE SEARCH IN THE NEXT 4K PAGE.'
HELP17 @WTO  '           FULL : SEARCH ENTIRE ADDRESS SPACE.'
HELP18 @WTO  '  --- ? : STATUS          --- E : TERMINATE'
HELP19 @WTO  '  --- H : HELP VIRTUAL STORAGE COMMANDS.'
HELP20 @WTO  '  --- HL : HELP LABEL''S AND DSECT''S COMMANDS.'
HELP21 @WTO  '  --- HM : HELP MISCELLANEOUS COMMANDS.'
HELP22 @WTO  '  --- HX : HELP ADDRESS SPACE COMMANDS (OR XH).'
HELP23 @WTO  '  C ON/OFF : TO ALTER VIRTUAL CORE STORAGE.'
HELP24 @WTO  '  T L(INE)/S(CREEN) : FLIP-FLOP LINE/FULL SCREEN.'
HELP25 @WTO  '  T ON/OFF : TO ACCESS OWN PROGRAM STORAGE (C OFF ONLY).'
HELP26 @WTO  '  T YES/NO : TO USE SEGMENT-TABLE PROCESS (FIND FULL).'
         SPACE 2
HELP30 @WTO  'LABEL''S AND DSECT''S COMMANDS SUMMARY :'
HELP31 @WTO  '  A LLLL NNNN : ASSIGN TO LABEL LLLL VALUE NNNN'
HELP32 @WTO  '           (ADDR + OR - DISPL ALLOWED).'
HELP33 @WTO  '           IF NNNN OMITTED = CURRENT POINTER.'
HELP34 @WTO  '  Z LLLL : DELETE LABEL LLLL AND HIS VALUE.'
HELP35 @WTO  '  L LLLL : DISPLAY VALUE OF LABEL(S) LLLL'
HELP36 @WTO  '           (IF LLLL OMITTED = ALL LABELS).'
HELP37 @WTO  '  I : TO DISPLAY ALL THE PREDEFINED LABELS.'
HELP38 @WTO  '  B : BROWSE ALL DSECT''S NAMES IN THE CURRENT DATA-SET.'
HELP39 @WTO  '  B DDDD : BROWSE SEQUENTIALLY AN ENTIRE DSECT (DDDD IS'
HELP40 @WTO  '           THE DSECT-NAME).'
HELP41 @WTO  '  B DDDD ... : BROWSE INFORMATION FROM A DSECT (DDDD IS'
HELP42 @WTO  '           THE DSECT-NAME). THEN ... MAY BE :'
HELP43 @WTO  '           FFFF : A FIELD-NAME, OR'
HELP44 @WTO  '           FFFF : A FLAG-NAME, OR'
HELP45 @WTO  '           HHHH : A FIELD-DISPLACEMENT (HEX), OR'
HELP46 @WTO  '           FFFF HH : A FIELD-NAME AND FLAG-VALUE (HEX).'
HELP47 @WTO  '  Q : DISPLAY HOW CORE STORAGE SPACE HAS GROWN.'
HELP48 @WTO  '  Q * : RESTART THE CURRENT DSECT''S DATA-SET.'
HELP49 @WTO  '  Q SSSS : START A NEW DSECT''S DATA-SET (SSSS IS THE'
HELP50 @WTO  '           DATA-SET NAME).'
         SPACE 2
HELP60 @WTO  'MISCELLANEOUS COMMANDS SUMMARY :'
HELP61 @WTO  '  M @N : SET AND RETAIN AS CURRENT USER-AREA N.'
HELP62 @WTO  '  M : DISPLAY THE CURRENT USER-AREA.'
HELP63 @WTO  '  M Z OR R : ZEROS (Z) OR BLANKS (R) CURRENT USER-AREA.'
HELP64 @WTO  '  M HHHH,... : STORE HHHH,... AT CURRENT LOC. POINTER.'
HELP65 @WTO  '  M ''...'' : STORE CHARACTERS AT CURRENT LOC. POINTER.'
HELP66 @WTO  '  M + OR - XX : FORWARD/BACKWARD MOVE OF LOC. POINTER.'
HELP67 @WTO  '  M L XX : SET USER-AREA TEXT LENGTH TO XX (HEX).'
HELP68 @WTO  '  P ON C(?) D(?) : START HARDCOPY (CLASS/DESTINATION).'
HELP69 @WTO  '  P OFF : STOP HARDCOPY.'
HELP70 @WTO  '  V : DISPLAY CURRENT ADDRESS SPACE SEGMENT-TABLE.'
         SPACE 2
HELP80 @WTO  'ADDRESS SPACE COMMANDS SUMMARY :'
HELP81 @WTO  '  XT : LOAD/REFRESH AS-TABLE AND RESET AS-ID MODE.'
HELP82 @WTO  '  XA XXXX : ACTIVATE AS-ID XXXX MODE (FOR NEXT'
HELP83 @WTO  '           XD, XF AND XS COMMANDS).'
HELP84 @WTO  '  XC : CLEAR AS-TABLE AND RESET AS-ID MODE.'
HELP85 @WTO  '  XR : RESET AS-ID MODE.'
HELP86 @WTO  '  XL XXXX : DISPLAY AS-NAME/AS-ID OF AS-TABLE'
HELP87 @WTO  '           (IF XXXX OMITTED = ALL AS-TABLE).'
HELP88 @WTO  '  XD, XF AND XS : DISPLAY, FIND AND STORE IN THE'
HELP89 @WTO  '           ACTIVE AS-ID (SAME AS D, F AND S).'
HELP90 @WTO  '  --- XH : HELP ADDRESS SPACE COMMANDS (OR HX).'
         EJECT
***********************************************************************
*        TABLE OF PRINTABLE CHARACTERS.                               *
***********************************************************************
         SPACE 1
        PRINT  GEN
CHARTB   DC    256C'.'             TRANSLATE ALL GARBAGE TO '.'
         ORG   CHARTB+C' '         BLANK
         DC    C' '
         ORG   CHARTB+C''         CENT
         DC    C'.<(+|&&'
         ORG   CHARTB+C'!'         EXCLAMATION
         DC    C'!$*);-/'
         ORG   CHARTB+C''         BROKEN BAR
         DC    C',%_>?'
         ORG   CHARTB+C'`'         ACCENT GRAVE
         DC    C'`:#@''="'
         ORG   CHARTB+C'A'-X'40'
         DC    X'818283848586878889'
         ORG   CHARTB+C'J'-X'40'
         DC    X'919293949596979899'
         ORG   CHARTB+C'S'-X'40'
         DC    X'A2A3A4A5A6A7A8A9'
         ORG   CHARTB+C'A'
         DC    C'ABCDEFGHI'
         ORG   CHARTB+C'J'
         DC    C'JKLMNOPQR'
         ORG   CHARTB+C'S'
         DC    C'STUVWXYZ'
         ORG   CHARTB+C'0'
         DC    C'0123456789'
         ORG   CHARTB+C'{'         LEFT BRACE
         DC    C'{'
         ORG   CHARTB+C'}'         RIGHT BRACE
         DC    C'}'
         ORG   CHARTB+C'~'         TILDE
         DC    C'~'
         ORG   CHARTB+C'\'         BACKSLASH
         DC    C'\'
         ORG
         EJECT
***********************************************************************
*        TABLE OF ADDRESS POINTER DISPLACEMENTS (FULL SCREEN).        *
***********************************************************************
         SPACE 1
FSTAPD   DC    AL1(FSL2-FSLOUT),CL1'>'                 0
         DC    AL1(FSL2+5-FSLOUT),CL1'<'               1
         DC    AL1(FSL2+5-FSLOUT),CL1'>'               2
         DC    AL1(FSL2+10-FSLOUT),CL1'<'              3
         DC    AL1(FSL2+L'FSL2-FSLOUT),CL1'>'          4
         DC    AL1(FSL2+L'FSL2+5-FSLOUT),CL1'<'        5
         DC    AL1(FSL2+L'FSL2+5-FSLOUT),CL1'>'        6
         DC    AL1(FSL2+L'FSL2+10-FSLOUT),CL1'<'       7
         DC    AL1(FSL2+2*L'FSL2-FSLOUT),CL1'>'        8
         DC    AL1(FSL2+2*L'FSL2+5-FSLOUT),CL1'<'      9
         DC    AL1(FSL2+2*L'FSL2+5-FSLOUT),CL1'>'      A
         DC    AL1(FSL2+2*L'FSL2+10-FSLOUT),CL1'<'     B
         DC    AL1(FSL2+3*L'FSL2-FSLOUT),CL1'>'        C
         DC    AL1(FSL2+3*L'FSL2+5-FSLOUT),CL1'<'      D
         DC    AL1(FSL2+3*L'FSL2+5-FSLOUT),CL1'>'      E
         DC    AL1(FSL2+3*L'FSL2+10-FSLOUT),CL1'<'     F
         EJECT
***********************************************************************
*        TABLES, ...                                                  *
***********************************************************************
         SPACE 1
TBELL    EQU   0+L'LBLF+L'DATA
         SPACE 1
         DS    0D
TABLE    DC    (NASSTE)XL(TBELL)'0'     ASSIGNED LABELS
TABLEND  EQU   *-L'TABLE
         DC    XL2'0'
         SPACE 1
         DS    0D
FIXTB    DC    (NFIXTE)XL(TBELL)'0'     PREDEFINED LABELS
FIXTBE   EQU   *-L'FIXTB
         DC    XL2'0'
         SPACE 2
ASTBL    EQU   0+L'ASIDN+L'ASNME
         DS    0D
ASTB     DS    (NASNTE)XL(ASTBL)   AS-ID TABLE AREA SPACE
ASTBE    EQU   *-L'ASTB
         SPACE 2
         DS    0D
HXDATA   DS    0XL16               MOVED DATA AREA
BLDATA   DS    XL256               MOVED BLOCK DATA AREA (FULL SCREEN)
PVBUF    DS    XL(L'HXDATA)        PREVIOUS LAST 16 BYTES OF 4K PAGE
FNBUF    DS    (4*KB)X             4K PAGE AREA SPACE
STKWAS   DS    (NTKWAS*TKWAL)X     TANKS USER-AREAS POOL
         SPACE 2
VCOREEND EQU   *                   PROGRAM SPACE END CONTROL
         EJECT
***********************************************************************
*        WORK AREAS DESCRIPTIONS.                                     *
***********************************************************************
         SPACE 1
CONSLOUT DSECT ,              DATA LIST LINE
         DS    CL1
CONSL1   DS    CL8                 ADDRESS
         DS    CL1
CONSL2   DS    CL1                 :
         DS    CL1
CONSL3   DS    CL8                 DATA (HEX)
         DS    CL1
CONSL4   DS    CL8                 DATA (HEX)
         DS    CL1
CONSL5   DS    CL8                 DATA (HEX)
         DS    CL1
CONSL6   DS    CL8                 DATA (HEX)
         DS    CL1
CONSL7   DS    CL1                 *
CONSL8   DS    CL16                DATA (CHAR)
CONSL9   DS    CL1                 *
         DS    CL1
CONSLA   DS    CL5                 ADVICE
CONSLL   EQU   *-CONSLOUT          MAX. LENGTH = L'MSGTXT
         SPACE 1
FSLOUT   DSECT ,              DATA LIST FULL SCREEN LINE
FSL1     DS    CL8                 ADDRESS
         DS    CL2
FSL2     DS    4CL11               DATA (HEX)
         DS    CL1
FSL3     DS    CL1                 |
FSL4     DS    CL16                DATA (CHAR)
FSL5     DS    CL1                 |
         DS    CL1
FSL6     DS    CL5                 ADVICE
FSLL     EQU   *-FSLOUT            MAX. LENGTH = L'MSGSVT
         SPACE 1
CNSLOUT  DSECT ,              LABELS LIST LINE
         DS    CL3
CNSLLB   DS    CL8                 LABEL
         DS    CL1
CNSLLS   DS    CL1                 :
         DS    CL1
CNSLAD   DS    CL8                 ADDRESS
CNSLBL   EQU   *-CNSLOUT           STAY 3 TIMES IN L'MSGTXT
         SPACE 1
CNSASOUT DSECT ,              AS-ID'S LIST LINE
         DS    CL3
CNSASID  DS    CL4                 AS-ID NUMBER (HEX)
         DS    CL1
CNSASSP  DS    CL1                 :
         DS    CL1
CNSASNM  DS    CL8                 AS-ID NAME
CNSASL   EQU   *-CNSASOUT          STAY 4 TIMES IN L'MSGTXT
         SPACE 1
CNSDST   DSECT ,              DSECT'S LIST LINE
CNSDSCT  DS    CL8                 DSECT NAME
         DS    CL2
CNSDSF1  DS    CL8                 FIELD NAME
         DS    CL1
CNSDSS1  DS    CL1                 :
         DS    CL1
CNSDSD1  DS    CL4                 FIELD DISPLACEMENT
         DS    CL1
CNSDSL1  DS    CL4                 FIELD LENGTH
         DS    CL1
CNSDSS2  DS    CL1                 -
         DS    CL1
CNSDSF2  DS    CL8                 FIELD NAME
         DS    CL1
CNSDSS3  DS    CL1                 :
         DS    CL1
CNSDSD2  DS    CL4                 FIELD DISPLACEMENT
         DS    CL1
CNSDSL2  DS    CL4                 FIELD LENGTH
         DS    CL1
CNSDSTL  EQU   *-CNSDST            MAX. LENGTH = L'MSGTXT
         ORG   CNSDSF2
CNSDSF3  DS    CL8                 FLAG NAME
         DS    CL1
CNSDSS4  DS    CL1                 :
         DS    CL1
CNSDSV1  DS    CL4                 FLAG VALUE
         DS    CL1
CNSDSS5  DS    CL1                 -
         DS    CL1
CNSDSF4  DS    CL8                 FLAG NAME
         DS    CL1
CNSDSS6  DS    CL1                 :
         DS    CL1
CNSDSV2  DS    CL4                 FLAG VALUE
         DS    CL1
CNSDSTF  EQU   *-CNSDST            MAX. LENGTH = L'MSGTXT
         EJECT
HELPVP   DSECT ,              VECTOR OF POINTERS TO HELP PANELS DATA
HLDF     DS    3A                  H PANEL
HLLS     DS    3A                  H L PANEL
HLMS     DS    3A                  H M PANEL
HLXH     DS    3A                  X H PANEL
HLAUTHF  DS    3A                  1ST RESERVED LINE(S) ADDRESSES
HLAUTHM  DS    3A                  2ND RESERVED LINE(S) ADDRESSES
HLRESL   EQU   81                  LENGTH OF RESERVED LINES TEXT
         SPACE 1
TKWA     DSECT ,              TANK USER-AREA
TKWPT    DS    F                   TEXT POINTER
TKWDL    DS    F                   LENGTH OF TEXT STORED IN AREA
TKWTX    DS    XL256               TEXT AREA
TKWAL    EQU   *-TKWA              TANK USER-AREA LENGTH
         SPACE 2
        PRINT  NOGEN
        IHAPSA DSECT=YES,LIST=YES
        CVT    DSECT=YES,LIST=YES
        IHAASCB DSECT=YES,LIST=YES
        IHAASVT
        IHAASTE DSECT=YES
        IHAEPIE
        IHAECB
        DCBD   DSORG=PS
        IEFZB4D0
        IEFZB4D2
         SPACE 2
         END
