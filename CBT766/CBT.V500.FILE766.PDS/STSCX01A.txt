STSCX01A TITLE 'PRINT/PUNCH Separator Exit -- PROLOG'          @430P270
**********************************************************************
** *  I DON'T THINK ANY PRINTER ACTUALLY USE THIS EXIT ANYMORE,   * **
** * BUT SINCE WE USED TO ALTER THE HASPPRPR SEPERATOR PAGE       * **
** * ROUTINES DIRECTLY, I AM ADDING THIS EXIT TO SIMULATE THE     * **
** * SINGLE CHANGE WHICH IS TO CHANGE THE LITERAL OF THE ROOM     * **
** * NUMBER TO BIN NUMBER.         SGM 08-08-00                   * **
**********************************************************************
* **PROPRIETARY-STATEMENT**********************************************
*                                                                     *
*   LICENSED MATERIALS-PROPERTY OF IBM                                *
*   THIS MODULE IS "RESTRICTED MATERIALS OF IBM"                      *
*   5694-A01 (C) COPYRIGHT IBM CORP 1990, 2005                        *
*                                                                     *
*   STATUS = HJE7720                                                  *
*                                                                     *
*01* EXTERNAL CLASSIFICATION:  NONE                            @R01P003
*01* END OF EXTERNAL CLASSIFICATION:                           @R01P003
*                                                                     *
* **END-OF-PROPRIETARY-STATEMENT***************************************
***********************************************************************
*                                                                     *
* MODULE NAME = STSCX01A (STSCX01A load module)                       *
*                                                                     *
* DESCRIPTIVE NAME = EXIT 1                                           *
*                                                                     *
*                    PRINT/PUNCH Separator                            *
*                                                                     *
* FUNCTION =                                                          *
*                                                                     *
*        STSCX01A is a sample exit routine that produces a            *
*        job separator page ( see sample separator page               *
*        below ).                                                     *
*                                                                     *
*        The separator page produced by this exit is                  *
*        identical to the default separator page ( produced           *
*        in HASPPRPU ) with the exception of the detail box           *
*        information field labels.  This sample exit produces         *
*        detail box information field labels that are delimited       *
*        by an '=' sign as opposed to a ':' ( on the default          *
*        separator page ).                                            *
*                                                                     *
*        An installation could use this sample exit to                *
*        customize the appearance of a job separator page.            *
*                                                                     *
*        Exit 1 is called by HASPPRPU during print/punch              *
*        processing.  It is called for job header and job             *
*        trailer separators.                                          *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*             JJJJJJJJJJ  OOOOOOOOOOOO  BBBBBBBBBBB     00000000      *
*            JJJJJJJJJJ  OOOOOOOOOOOO  BBBBBBBBBBBB   0000000000      *
*               JJ      OO        OO  BB        BB  00      0000      *
*              JJ      OO        OO  BB        BB  00     00 00       *
*             JJ      OO        OO  BB       BB   00    00  00        *
*            JJ      OO        OO  BBBBBBBBBB    00   00   00       1 *
*           JJ      OO        OO  BBBBBBBBBB    00  00    00       11 *
*          JJ      OO        OO  BB       BB   00 00     00       11  *
*   JJ    JJ      OO        OO  BB        BB  0000      00       11   *
*  JJ    JJ      OO        OO  BB        BB  000       00       11    *
* JJJJJJJJ      OOOOOOOOOOOO  BBBBBBBBBBBB   0000000000    1111111111 *
* JJJJJJ       OOOOOOOOOOOO  BBBBBBBBBBB     00000000     1111111111  *
*                                                                     *
*     JJJJJJJJJJ   000000000        1       1       1       1         *
*     JJJJJJJJJJ  00000000000      11      11      11      11         *
*         JJ      00      000     111     111     111     111         *
*         JJ      00     0000    1111    1111    1111    1111         *
*         JJ      00    00 00   11111   11111   11111   11111         *
*         JJ      00   00  00      11      11      11      11         *
*         JJ      00  00   00      11      11      11      11         *
*         JJ      00 00    00      11      11      11      11         *
*     JJ  JJ      0000     00      11      11      11      11         *
*     JJ  JJ      000      00      11      11      11      11         *
*     JJJJJJ      00000000000      11      11      11      11         *
*     JJJJJJ       000000000       11      11      11      11         *
*                                                                     *
* ***START*****START*****START*****START*****START*****START****STA** *
* *                                                                 * *
* * JOBID=          J0111                        SEGMENT ID: 0021   * *
* * JOB NAME=       JOB01                                           * *
* * USER ID=        JSMITH                                          * *
* * SYSOUT CLASS=   A                                               * *
* * OUTPUT GROUP=   GRP01.CLASSA.D3289                              * *
* * TITLE=          Smitty Corporation Checking Account History     * *
* *                                                                 * *
* * DESTINATION=    NEW YORK                                        * *
* * NAME=           Joseph P. Smith                                 * *
* * ROOM=           2G-54                                           * *
* * BUILDING=       Smitty Textile Building                         * *
* * DEPARTMENT=     Accounting                                      * *
* * ADDRESS=        999 W. 99th Street                              * *
* *                 New York, New York                              * *
* *                 10000                                           * *
* *                 212-555-3487                                    * *
* *                                                                 * *
* * PRINT TIME=     12:03:41                                        * *
* * PRINT DATE=     12 OCT 89                                       * *
* * PRINTER NAME=   PRINTER1                                        * *
* * SYSTEM=         SYSA                                            * *
* *                                                                 * *
* ***START*****START*****START*****START*****START*****START****STA** *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
* NOTES =                                                             *
*                                                                     *
*    DEPENDENCIES = JES2 $EXIT Facility, Standard JES2 services,      *
*                   SWBTUREQ services                                 *
*                                                                     *
*    RESTRICTIONS = This code is provided as an example of            *
*                   installation extensions to JES2.  This code       *
*                   is not considered TYPE 1 supported code of IBM.   *
*                   Any problems encountered in the use of this       *
*                   sample code is a user responsibility.  The        *
*                   IBM support center does not support user          *
*                   extensions of sample user exits.                  *
*                                                                     *
* MODULE TYPE = Procedure ( CSECT type )                              *
*                                                                     *
*    PROCESSOR = IBM High Level Assembler/MVS 1.5.0            @Z07LASM
*                                                                     *
*    MODULE SIZE = See $MODEND macro expansion at end of assembly     *
*                                                                     *
*    ATTRIBUTES = JES2 REENTRANT, RMODE ANY, AMODE 31                 *
*                                                                     *
*                                                                     *
* ENTRY POINTS =                                                      *
*                                                                     *
*        SEPARATE  -  Produce our own separator page.  Suppress       *
*                     the production of the default separator         *
*                     page.                                           *
*                                                                     *
*                     Suppress the production of JESNEWS if this      *
*                     exit has been called for a job-continuation     *
*                     separator page.                                 *
*                                                                     *
* MACROS = JES2 - $CALL, $DEST, $ENTRY, $ESTAE, $GETWORK, $MODEND,    *
*                 $MODULE, $PBLOCK, $PRPUT, $RETURN, $RETWORK, $SAVE, *
*                 $SEPPDIR, $SETRP, $STORE                            *
*                                                                     *
* MACROS = MVS  - SWBTUREQ, TIME                                      *
*                                                                     *
* CHANGE ACTIVITY                                                     *
*                                                                     *
*        @410  MVS/SP-JES2 VERSION 4 RELEASE 1 LEVEL 0                *
*              (SP4.1.0, HJE4410)                                     *
*                                                                     *
* $420P306=PTM      HJE4420 910118 R_W1: Sample Exit Prolog Problems  *
*                                                                     *
* $430P050=10X      HJE4430 920310 RJH:  Enhance STSCX01A(USER EXIT1) *
* $430P329=10X      HJE4430 920429 B_R2: PMX0329: Sample Exit 1       *
* $430P270=10X      HJE4430 920721 HGF:  $MODULE, ENVIRON, RMODE      *
* $510P214=PTM      HJE5510 930810 KCK:  EXIT1 SEPPDIR/$DEST UPDATES  *
*                                                                     *
* $520LHLA=BASEQ    HJE5520 941008 GMD:  Change HASM to HLASM         *
*                                                                     *
* $R01P010=PTM      HJE6601 950217 JMO:  PTM PQJ0012                  *
*                                                                     *
* $R03P010=ENHPSO   HJE6603 960521 J_K2: Segment ID for trailer page  *
*                                                                     *
* $Z07LASM=NJETCP   HJE7720 050203 CLW:  HLASM 5.0 support            *
*                                                                     *
* A000000-999999    CREATED FOR JES2 4.1.0                         @410
***********************************************************************
         TITLE 'PRINT/PUNCH Separator Exit -- PROLOG ($HASPGBL)'
         COPY  $HASPGBL
         TITLE 'PRINT/PUNCH Separator Exit -- PROLOG ($MODULE)'
STSCX01A $MODULE ENVIRON=JES2,                                 @430P270C
               RMODE=ANY,                                      @430P270C
               IBMJES2=SAMPLE,                                 @430P270C
               TITLE='PRINT/PUNCH Separator Exit',                     C
               CVT,                Generate MVS CVT dsect              C
               DOTUM,              IEFDOTUM-Dyn. OUTPUT text unit map'gC
               JESCT,              Generate MVS IEFJESCT dsect         C
               SJOKY,              IEFSJOKY-Dynamic OUTPUT key mapping C
               SJTRC,              IEFSJTRC-SWBTUREQ return/reason codeC
               (SJTRP,GEN),        IEFSJTRP-SWBTUREQ RETRIEVE parm listC
               $BUFFER,            Buffer mapping (for $JCT)           C
               $CADDR,             Common storage address list         C
               $DCT,               Device Control Table                C
               $DSCT,              DSCT (for APPC Output)      @430P329C
               $ERA,               Error Recovery Area                 C
               $HASPEQU,           General EQUATES                     C
               $HCCT,              Common storage control table        C
               $HCT,               Private storage control table       C
               $HFAME,             File alloacation map (for $HCCT)    C
               $JCT,               Job Control Table                   C
               $JOE,               Job Output Element                  C
               $MIT,               Module information table            C
               $MITETBL,           MIT entry table                     C
               $PADDR,             Private storage address list        C
               $PARMLST,           Parameter list values/equates       C
               $PCE,               Processor control element           C
               $PDDB,              Peripheral data definition block    C
               $PRE,               Processor Recovery Element          C
               $PSV,               Processor save area                 C
               $SCAT,              Sysout class attribute (for $HCCT)  C
               $TQE,               jes2 timer queue element            C
               $USERCBS,           User defined Control Blocks         C
               $XECB,              Extended ECB (for $HCCT)            C
               ($XPL,GEN)          Exit parameter list         @430P270
         TITLE 'PRINT/PUNCH Separator Exit -- Entry Prolog'
***********************************************************************
*                                                                     *
*        SEPARATE - Produce a separator page.  Suppress production    *
*                   of the default separator page.                    *
*                   Suppress JESNEWS for job-continuation calls.      *
*                                                                     *
* FUNCTION :                                                          *
*                                                                     *
*        Produces a separator page that is almost identical           *
*        to the default separator page produced in HASPPRPU           *
*        ( see beginning prolog for explanation of the                *
*        difference between the default separator and that            *
*        produced in this exit ).                                     *
*                                                                     *
*        During a job continuation separator page call, the exit      *
*        response byte is set to indicate to HASPPRPU that JESNEWS    *
*        should not be produced.                                      *
*                                                                     *
* LINKAGE :                                                           *
*                                                                     *
*        This routine is entered via a $EXIT call in HASPPRPU.        *
*        Control is returned via a $RETURN.                           *
*                                                                     *
* OPERATION :                                                         *
*                                                                     *
*        LOADMOD(STSCX01A)                                     @420P306
*        EXIT(1)  ROUTINES=(SEPARATE),STATUS=ENABLED           @420P306
*                                                                     *
*                                                                     *
* ENVIRONMENT :                                                       *
*                                                                     *
*        JES2 main task                                               *
*                                                                     *
* RECOVERY :                                                          *
*                                                                     *
*        A $ESTAE recovery environment is establish on entry to       *
*        the exit routine.  The recovery routine releases all         *
*        storage obtained in the exit and then percolates to          *
*        the next higher level of recovery in HASPPRPU.               *
*                                                                     *
*        Debugging text is put in field PRETRACK in the Processor     *
*        Recovery Element (PRE).  The contents of this field          *
*        are put in the SDWA VRA ( variable recording area ).         *
*                                                                     *
*        HASPPRPU establishes an ESTAE around the call to this        *
*        exit.  In the event of a program check, control will         *
*        be passed to the label PGMCKMSG in HASPPRPU when that        *
*        level of recovery is invoked.                                *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
* REGISTER USAGE (ENTRY/EXIT) :                                       *
*                                                                     *
*    REG         VALUE ON ENTRY              VALUE ON EXIT            *
*                                                                     *
*    R0          N/A                         unchanged                *
*    R1          Parameter list address      unchanged                *
*    R2-R10      N/A                         unchanged                *
*    R11         HCT Address                 unchanged                *
*    R12         N/A                         unchanged                *
*    R13         PCE Address                 unchanged                *
*    R14         Return address              unchanged                *
*    R15         Entry address               Return code              *
*                                                                     *
***********************************************************************
         SPACE 2
***********************************************************************
*                                                                     *
*            PARAMETER LIST FOR EXIT 1  ( Mapped by $XPL )            *
*                                                                     *
*                                                                     *
*    LABEL         DESCRIPTION                                        *
*    -----         -----------                                        *
*                                                                     *
*    XPLID         Eyecatcher ('$XPL')                                *
*    XPLLEVEL      Maintenance Level                                  *
*    XPLXITID      Exit ID number (1)                                 *
*    XPLEXLEV      Version number                                     *
*    X001IND       Indicator byte                              @R01P010
*    X001COND      Condition byte                              @R01P010
*    X001RESP      Response byte                               @R01P010
*    XPLSIZE       Length of parameter list including base section    *
*                                                                     *
*    X001DCT       DCT address                                        *
*    X001JCT       JCT address                                        *
*    X001JQE       JQE address                                        *
*    X001WJOE      Work-JOE address                                   *
*    X001CJOE      Characteristics-JOE address                        *
*    X001PDDB      Address of first PDDB of JOE, or zero              *
*    X001SWBT      SWBTU pointer list address for the first           *
*                  PDDB in the JOE, or zero                           *
*    X001NSWB      Number of SWBITs despooled                         *
*    X001RSVD      Reserved for future use                            *
*    X001HBUF      Address of HASP buffer for exit use                *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
* REGISTER USAGE (INTERNAL) :                                         *
*                                                                     *
*    REG        VALUE                                                 *
*                                                                     *
*    R0         N/A                                                   *
*    R1         Work register                                         *
*    R2-R4      N/A                                                   *
*    R5         Exit 1 work area address                              *
*    R6         N/A                                                   *
*    R7         Exit 1 parameter list address                         *
*    R8         N/A                                                   *
*    R9-R10     N/A                                                   *
*    R11        HCT address                                           *
*    R12        Local base address                                    *
*    R13        PCE address                                           *
*    R14-R15    Link/Work registers                                   *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
* DATA AREA USAGE (INTERNAL) :                                        *
*                                                                     *
*    NAME          VALUE/DESCRIPTION                                  *
*    ----          -----------------                                  *
*                                                                     *
*    EX1WORK       Exit 1 work area                                   *
*    SEPDSECT      Separator page detail box DSECT                    *
*    PAGELINE      Separator page output line                         *
*                                                                     *
*    X001IND       Indicator byte in XPL (exit parameter list) @R01P010
*      X001JHDR       Indicator in X001IND for a job header    @R01P010
*                     separator call                                  *
*      X001JTLR       Indicator in X001IND for a job trailer   @R01P010
*                     separator call                                  *
*      X001JCNT       Indicator in X001IND for a               @R01P010
*                     job-continuation separator call          @R01P010
*    X001RESP       Response byte in XPL                       @R01P010
*      X001DFSP       Indicator in X001RESP to suppress        @R01P010
*                     production of the default separator page @R01P010
*      X001JNWS       Indicator in X001RESP to suppress        @R01P010
*                     production of JESNEWS                    @R01P010
*                                                                     *
*    $BUFFER       HASP buffer DSECT                                  *
*      BUFDSECT       Start of buffer prefix area                     *
*      BUFSTART       Start of buffer work area                       *
*    $SID          System id                                          *
*    DCTDEVN       Device name                                        *
*    JCTJNAME      Job name from job card                             *
*    JCTJOBID      System assigned job id                             *
*    JCTPNAME      Programmer name from JOB card                      *
*    JCTROOMN      Room, from JOB card                                *
*    JOECRUID      Creator userid for data set                        *
*    JOECURCL      JOE current SYSOUT class                           *
*    JOENAME       JOE's output group name                            *
*    JOEROUT       Route code                                         *
*    JOEUSER       User id                                            *
*                                                                     *
*    DOADDRES      Dynamic output ADDRESS    key                      *
*    DOBUILD       Dynamic output BUILDING   key                      *
*    DODEPT        Dynamic output DEPARTMENT key                      *
*    DONAME        Dynamic output NAME       key                      *
*    DOROOM        Dynamic output ROOM       key                      *
*    DOTITLE       Dynamic output TITLE      key                      *
*                                                                     *
*    DOCNUNIT      Text unit  ( TU ) mapping                          *
*    DOCNTFLD      TU length/parameter pairs mapping                  *
*    SJTRP         SWBTUREQ RETRIEVE parameter list                   *
*                                                                     *
*    PCEUSER0      Address of storage obtained for exit work area     *
*    PCEUSER1      Count of number of times that 2nd level recovery   *
*                  has been entered                                   *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
* RETURN CODES ( R15 ON EXIT ):                                       *
*                                                                     *
*        0 - Continue normal processing                               *
*                                                                     *
*                                                                     *
* OTHER CONSIDERATIONS :                                              *
*                                                                     *
*        $WAIT can occur.                                             *
*                                                                     *
***********************************************************************
         TITLE 'PRINT/PUNCH Separator Exit -- Entry'
***********************************************************************
*                                                                     *
*        Entry point to routine SEPARATE.                             *
*                                                                     *
***********************************************************************
         SPACE 2
EXIT01A  $ENTRY BASE=R12           Establish entry point
         SPACE 2
        $SAVE  TRACE=YES,NAME=EXIT01A      Save callers registers
         SPACE 2
         LR    R12,R15             Set local base
         SPACE 1
         LR    R7,R1               Establish addressability
         USING XPL,R7                  to exit parameter list
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*        Establish an ESTAE recovery environment                      *
*                                                                     *
*        Put debugging data in Processor Recovery Element ( PRE )     *
*        - maximum 32 characters                                      *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
        $ESTAE RECADDR==A(EX1RECV),NAME=STSCX01A,RETRY=RELWORKA,       C
               BASE=(R12)          Establish ESTAE environment
         SPACE 2
         L     R14,PCEPRE          Get PRE address from PCE
         USING PRE,R14             Establish addressability
         SPACE 1
         MVC   PRETRACK(L'DEBUGMSG),DEBUGMSG     Put debug data in PRE
         SPACE 1
         MVI   PRELOGLN,L'DEBUGMSG     Put length of debug data in PRE
         SPACE 2
         DROP  R14                 Drop PRE addressability
         TITLE 'PRINT/PUNCH Separator Exit -- Main Routine'
*------------------------------------------------------------* @430P050
*                                                            * @430P050
*        If the SEP= parameter on the printer initial-       * @430P050
*        ization statement is currently set to NO, or the    * @430P050
*        suppress flag has been set by previous code,        * @430P050
*        do not build a separator page.                      * @430P050
*                                                            * @430P050
*------------------------------------------------------------* @430P050
         SPACE 2                                               @430P050
         TM    X001RESP,X001DFSP   Suppress Separators?        @R01P010
         BO    ENDMAIN             Yes, exit                   @430P050
         SPACE 2                                               @430P050
*---------------------------------------------------------------------*
*                                                                     *
*        If this is a job continuation call, set the exit             *
*        response byte to indicate that the production of             *
*        JESNEWS should be suppressed.                                *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
         TM    X001IND,X001JCNT    Job continuation call?      @R01P010
         BZ    NOTJCNT               No, go build the separator page
         SPACE 1
         OI    X001RESP,X001JNWS   Suppress JESNEWS            @R01P010
         SPACE 2
NOTJCNT  DS    0H                  Fall thru to build separator page
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*        Storage must be obtained for a work area to be used          *
*        by the routine PRODSEP that produces the separator           *
*        page.                                                        *
*        The exit work area dsect ( EX1WORK ) is mapped over          *
*        this storage to store and extract the data used in the       *
*        PRODSEP routine.                                             *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*        Check if we have already obtained storage for the            *
*        exit work area ( as may be the case during a RETRY           *
*        after a program check in the exit ).  If so, reuse           *
*        it.  Otherwise issue a $GETWORK to get storage.  Save        *
*        the address in PCEUSER0.                                     *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
         ICM   R1,B'1111',PCEUSER0     Check if already got storage
         BZ    GETEX1W                   No storage, so go get some
         XC    PCEUSER0,PCEUSER0       Clear PCEUSER0 field
         SPACE 1
         CLC   0(4,R1),=C'EX1W'    Is this our work area storage ?
         BNE   GETEX1W               No, go get storage
         B     GOTEX1W               Yes, branch around GETWORK
         SPACE 2
GETEX1W  DS    0H                  Get storage for work area
         SPACE 1
         LA    R1,(((EX1WKLEN+3)/4)+1)   Get the length of the EX1WORK C
                                           DSECT + eyecatcher in words
         SPACE 1
        $GETWORK WORDS=(R1),USE=EX1W,ERRET=RELWORKA  Get storage for   C
                                               exit work area (EX1WORK)
         SPACE 1
GOTEX1W  DS    0H                  Got storage
         EJECT
         ST    R1,PCEUSER0         Save address in PCE user field
         LA    R5,4(,R1)           Add 4 for eyecatcher and save addr
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*        Call the PRODSEP routine to build and print a separator      *
*        page.  Suppress production of the default separator page     *
*        only if PRODSEP returns a return code of zero.               *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
        $CALL  PRODSEP             Call routine to produce separator
         SPACE 2
         LTR   R15,R15             Check return code from PRODSEP
         BNZ   RELWORKA            Bypass suppressing def. separator
         SPACE 1
         OI    X001RESP,X001DFSP   Suppress def separator      @R01P010
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*        Release exit work area storage                               *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
RELWORKA DS    0H                      Release work area storage
         SPACE 1
         ICM   R1,B'1111',PCEUSER0     Load work area address
         BZ    ENDMAIN                 Return to caller if no storage
         XC    PCEUSER0,PCEUSER0       Clear PCEUSER0 field
         SPACE 1
         CLC   0(4,R1),=C'EX1W'    Is this storage ours?
         BNE   ENDMAIN               No, branch around RETWORK
         SPACE 1
        $RETWORK (R1)              Release work area storage
         SPACE 2
ENDMAIN  DS    0H                  End of routine SEPARATE
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*        Cancel $ESTAE recovery environment                           *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
        $ESTAE CANCEL              Cancel ESTAE environment
         TITLE 'PRINT/PUNCH Separator Exit -- Return'
***********************************************************************
*                                                                     *
*        Return to the caller.                                        *
*                                                                     *
***********************************************************************
         SPACE 2
        $RETURN RC=0               Return to caller with RC=0
         SPACE 2
DEBUGMSG DC    C'ESTAE SET UP IN SAMPLE EXIT 1'    Debugging text
         SPACE 2
         LTORG ,                   Define literal origin
         SPACE 1
         DROP  R7,R12              Drop addressabilities
         TITLE 'PRODSEP  -- Produce a separator page detail box'
***********************************************************************
*                                                                     *
*        PRODSEP  - Produce a separator page detail box.              *
*                                                                     *
* FUNCTION :                                                          *
*                                                                     *
*        This subroutine produces a separator page detail box         *
*        that is identical to that on the default separator           *
*        page produced by HASPPRPU during print/punch processing.     *
*                                                                     *
*        The SWBTUREQ retrieve service is used to extract             *
*        requested values from an OUTPUT statement and return         *
*        them in a contiguous string.  The service is passed a        *
*        list of keys that identify the keywords ( TITLE, NAME,       *
*        ROOM, BUILDING, DEPARTMENT and ADDRESS ) whose values        *
*        are to be extracted.                                         *
*                                                                     *
*        Each value in the string is represented in text unit (TU)    *
*        format, which means that it is preceded by its key and       *
*        length.                                                      *
*                                                                     *
* LINKAGE :                                                           *
*                                                                     *
*      Accessed via $CALL                                             *
*      Returned via $RETURN                                           *
*                                                                     *
*                                                                     *
* REGISTER USAGE (ENTRY/EXIT) :                                       *
*                                                                     *
*    REG         VALUE ON ENTRY              VALUE ON EXIT            *
*                                                                     *
*    R0-R4       N/A                         unchanged                *
*    R5          Exit 1 work area address    unchanged                *
*    R6          N/A                         unchanged                *
*    R7          Parameter list address      unchanged                *
*    R8-R10      N/A                         unchanged                *
*    R11         HCT Address                 unchanged                *
*    R12         Local base address          unchanged                *
*    R13         PCE Address                 unchanged                *
*    R14         Return address              unchanged                *
*    R15         Entry address               Return code (see below)  *
*                                                                     *
*                                                                     *
* RETURN CODE ( R15 ON EXIT ):                                        *
*                                                                     *
*        0 - Processing successful;  Separator page produced          *
*                                                                     *
*        4 - Processing unsucccessful;  Separator page not produced   *
*                                                                     *
*                                                                     *
* OTHER CONSIDERATIONS :                                              *
*                                                                     *
*        None                                                         *
*                                                                     *
***********************************************************************
         SPACE 2
         USING PRODSEP,R12         Establish routine addressability
         USING XPL,R7              Establish XPL addressability
         USING EX1WORK,R5          Establish work area addressability
         EJECT
         SPACE 2
PRODSEP $SAVE  ,                   Save the caller's registers
         SPACE 3
         LR    R12,R15             Set local base
         SPACE 1
         CLC   X001SWBT,$ZEROS     Are there any SWBIT's ?
         BE    REQOK               No, branch around SWBTUREQ RETRIEVE C
                                            service call
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*        Storage must be obtained for the SWBTUREQ RETRIEVE           *
*        service to provide an area to return the retrieved           *
*        TUs and an area to set up a Key List in.  Pointers           *
*        to the TUs requested by the specific keys will be            *
*        returned by the service to the Key List area if they         *
*        exist.  Working storage for SWBTUREQ service is also         *
*        obtained.                                                    *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
*---------------------------------------------------------------------*
*        Get storage for output TU area                               *
*---------------------------------------------------------------------*
         SPACE 1
         ICM   R1,B'1111',EX1TUADR     Check if already got storage
         BZ    GETOTUA                   No storage, so go get some
         XC    EX1TUADR,EX1TUADR       Clear out save area
         SPACE 1
         CLC   0(4,R1),=C'OTUA'    Is this storage ours?
         BNE   GETOTUA               No, go get storage
         B     GOTOTUA               Yes, branch around GETWORK
         SPACE 2
GETOTUA  DS    0H                  Get storage for work area
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*        Of the 6 TU's that we are interested in, 5 have a single     *
*        parameter ( TITLE, ROOM, NAME, DEPT, BUILDING ), whereas     *
*        ADDRESS has up to 4 parameters.                              *
*                                                                     *
*        The total output TU area required is ...                     *
*                                                                     *
*        5*(size of one-parameter TU) + (size of four-parameter TU)   *
*        |                               |                            *
*        |                               |                            *
*        |                               +--> for ADDRESS             *
*        |                                                            *
*        +--> for  TITLE, ROOM, NAME, DEPT and BUILDING               *
*                                                                     *
*                                                                     *
*        where a TU mapping is defined as follows:                    *
*                                                                     *
*        TU key                              .....   2 bytes          *
*        Number of length/parameter pairs    .....   2 bytes          *
*  +-- { Length of first (or only) parameter .....   2 bytes          *
*  |   { First (or only) parameter           .....  60 bytes ( max )  *
*  |              .                                                   *
*  |              .                                                   *
*  |              .                                                   *
*  +----> (repeated for multiple parameter TU)                        *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         LA    R1,((5*(2+2+2+60))+(2+2+4*(2+60)))                      C
                                   Size of current TU area
         STH   R1,EX1TUSZ          Save the size
         LA    R1,3(,R1)           Convert size from
         SRL   R1,2                  bytes to words
         LA    R1,1(,R1)           Add 1 for the eyecatcher
         SPACE 1
        $GETWORK WORDS=(R1),USE=OTUA,ERRET=NOSEPRT    Get storage for  C
                                                   the TU output area
         SPACE 1
GOTOTUA  DS    0H                  Got storage
         SPACE 1
         LA    R1,4(,R1)           Add 4 for eyecatcher
         ST    R1,EX1TUADR         Save the address of the TU output   C
                                     area
         EJECT
*---------------------------------------------------------------------*
*    Get storage for Key / TU pointer list                            *
*---------------------------------------------------------------------*
         SPACE 1
         ICM   R1,B'1111',EX1KYLST     Check if already got storage
         BZ    GETKEYL                   No storage, so go get some
         XC    EX1KYLST,EX1KYLST       Clear out save area
         SPACE 1
         CLC   0(4,R1),=C'KEYL'    Is this storage ours?
         BNE   GETKEYL               No, go get storage
         B     GOTKEYL               Yes, branch around GETWORK
         SPACE 2
GETKEYL  DS    0H                  Get storage for work area
         SPACE 1
         LA    R1,(((6*SJTRKLEN)+3)/4+1)   Size of Key/TU ptr list in  C
                                                 words
         SPACE 1
        $GETWORK WORDS=(R1),USE=KEYL,ERRET=NOSEPRT    Get storage for  C
                                                    the Key / TU list
         SPACE 1
GOTKEYL  DS    0H                  Got storage
         SPACE 1
         LA    R1,4(,R1)           Add 4 for eyecatcher
         ST    R1,EX1KYLST         Save the address of the Key / TU    C
                                     List
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*        Insert each delivery/identification OUTPUT JCL               *
*        parameter key, in the keys list.                             *
*        If any of these parameters were specified by the             *
*        user, a pointer to the appropriate TU ( text unit )          *
*        will be returned by the SWBTUREQ RETIREVE service.           *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         USING SJTRKEYL,R4         Establish
         L     R4,EX1KYLST             addressability
         SPACE 1
         MVC   SJTRKYID+KYLSTTL,=Y(DOTITLE)       Title Key
         MVC   SJTRKYID+KYLSTNM,=Y(DONAME)        Name Key
         MVC   SJTRKYID+KYLSTRM,=Y(DOROOM)        Room Key
         MVC   SJTRKYID+KYLSTBL,=Y(DOBUILD)       Building Key
         MVC   SJTRKYID+KYLSTDP,=Y(DODEPT)        Dept key
         MVC   SJTRKYID+KYLSTAD,=Y(DOADDRES)      Address Key
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*        Initialize the SWBTUREQ REQUEST=RETRIEVE parameter           *
*        list. Set storage length to 0 - we dont know exactly how     *
*        much storage we need for the SWBTUREQ Service Work Area.     *
*        The service will return the Required Size which we will      *
*        then obtain via $GETWORK (in MORESTOR routine).              *
*---------------------------------------------------------------------*
         SPACE 1
         USING SJTRP,R3            Establish addressability
         SPACE 1
         LA    R3,EX1RETPL         Get the address of the              C
                                     RETRIEVE parameter list
         SPACE 1
         XC    SJTRP(SJTRLGTH),SJTRP        Clear the parameter list
         MVC   SJTRID,=A(SJTRCID)  Parameter list ID
         MVI   SJTRVERS,SJTRCVER   Parameter list version
         LA    R1,SJTRLGTH         Parameter
         STH   R1,SJTRLEN            list length
         MVC   SJTRSTOR,$ZEROS     Zero storage address        @430P329
         MVC   SJTRSTSZ,$ZEROS     Zero storage length         @430P329
         MVC   SJTRSWBN,X001NSWB   Number of pointers in SWBTU list
         MVC   SJTRSWBA,X001SWBT   Address of SWBTU pointer list
         MVC   SJTRAREA,EX1TUADR   Address of the output area          C
                                     for contiguous text units
         MVC   SJTRSIZE,EX1TUSZ    Size of output area for TUs
         MVC   SJTRKIDN,$H6        Number of keys in the Key/TU        C
                                     Pointer list
         MVC   SJTRKIDL,EX1KYLST   Address of Key List
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*        Issue the SWBTUREQ REQUEST=RETRIEVE macro                    *
*        ------------------------------------------                   *
*                                                                     *
*        The service returns with a zero return code if               *
*        processing was successful.                                   *
*                                                                     *
*        If the current working storage size ( SJTRSTSZ ) is          *
*        less than the required size for the service, then            *
*        the service returns the following values:                    *
*               R15        =  4 (SJTRCSIZ)                            *
*               SJTRREAS   =  4 (SJTRWSZR)                            *
*               SJTRWKSZ   =  amount of working storage required      *
*                             for the SWBTUREQ REQUEST=RETRIEVE       *
*                             service.                                *
*                                                                     *
*        If none of the requested items were found, then the          *
*        service returns the following values:                        *
*               R15        =  4 (SJTRCSIZ)                            *
*               SJTRREAS   =  100 (SJTRNOKY)                          *
*                                                                     *
*        If R15 > 4, then an error occurred in the service.           *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 3
RETRYREQ DS    0H
         ST    R3,EX1PLPTR         Pass SWBTUREQ RETRIEVE parm list
         LA    R1,EX1PLPTR           address by indirect addressing
         SPACE 2
         SWBTUREQ REQUEST=RETRIEVE  Issue the SJF macro
         SPACE 2
         STH   R15,EX1SWBRC        Save the return code
         SPACE 1
         CLC   EX1SWBRC,$H4             Check return code
         BL    REQOK               Service successful, produce sep.
         BH    NOSEPRT             Error, don't produce separator
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*        If working storage is too small, call MORESTOR to            *
*        release the current storage ( if any ), get storage          *
*        for the amount that is required, and issue the               *
*        service call again.                                          *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
         CLC   SJTRREAS,=A(SJTRWSZR)    Working storage too small?
         BNE   CHECKEYS                 No, check if no keys matched
         LH    R4,SJTRSTSZ         Current storage size
         L     R1,SJTRSTOR         Current storage address
         LH    R2,SJTRWKSZ         Required storage size
         SPACE 1
        $CALL  MORESTOR            Obtain required storage
         SPACE 1
         LTR   R15,R15             Check return code from MORESTOR
         BNZ   NOSEPRT             If non-zero don't produce separator
         SPACE 1
         LA    R1,4(,R1)           Add 4 for eyecatcher
         ST    R1,EX1SRWA          Save working storage address in     C
                                       exit work area (EX1WORK)
         MVC   EX1SRWL,SJTRWKSZ    Set the length in EX1WORK
         MVC   SJTRSTOR,EX1SRWA    Reset WS addr in parm list
         MVC   SJTRSTSZ,EX1SRWL    Reset WS size in parm list
         B     RETRYREQ            Branch to issue macro again
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*        Check if reason for RC=4 from SWBTUREQ service is that       *
*        none of the requested values were found.                     *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
CHECKEYS CLC   SJTRREAS,=A(SJTRNOKY)   Did any keys match?
         BE    REQOK                     No, produce separator anyway
         B     NOSEPRT                 Go set error return code
         SPACE 2
REQOK    DS    0H
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*        Call PRINTBLK routine to print the block letters on the      *
*        separator page.                                              *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
        $CALL  PRINTBLK            Call routine to print block letters
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*        Call BUILDBOX routine to build and print the detail          *
*        box on the separator page.                                   *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
        $CALL  BUILDBOX            Call routine to build detail box
         EJECT
***********************************************************************
*                                                                     *
*        Return to the caller.                                        *
*                                                                     *
***********************************************************************
         SPACE 2
         SLR   R6,R6               Set return code = 0         @430P329
         B     GOBACK              Branch to return to caller
         SPACE 2
NOSEPRT  LA    R6,4                Set return code = 4         @430P329
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*        The TU output area, the Keylist/Pointer area and the         *
*        working storage area for the SWBTUREQ service are no         *
*        longer needed so their respective work areas are             *
*        returned.                                                    *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2                                               @430P329
*---------------------------------------------------------------------*
*        Release TU output area storage                               *
*---------------------------------------------------------------------*
         SPACE 1                                               @430P329
GOBACK   ICM   R1,B'1111',EX1TUADR  Load TU output area addr   @430P329
         BZ    CHKYLST             Branch if no TU storage     @430P329
         S     R1,$F4              Subtract 4 for eyecatcher   @430P329
         SPACE 1                                               @430P329
         XC    EX1TUADR,EX1TUADR   Set address to zero         @430P329
         SPACE 1                                               @430P329
        $RETWORK (R1)              Release the TU storage      @430P329
         SPACE 2                                               @430P329
*---------------------------------------------------------------------*
*        Release Key/TU pointer list storage area                     *
*---------------------------------------------------------------------*
         SPACE 1                                               @430P329
CHKYLST  ICM   R1,B'1111',EX1KYLST  Load Key/TU ptr list addr  @430P329
         BZ    CHKSRWA             Branch if no Key/TU storage @430P329
         S     R1,$F4              Subtract 4 for eyecatcher   @430P329
         SPACE 1                                               @430P329
         XC    EX1KYLST,EX1KYLST   Set address to zero         @430P329
         SPACE 1                                               @430P329
        $RETWORK (R1)              Release the keylist storage @430P329
         SPACE 2                                               @430P329
*---------------------------------------------------------------------*
*        Release SWBTUREQ RETRIEVE service work area storage          *
*---------------------------------------------------------------------*
         SPACE 1                                               @430P329
CHKSRWA  ICM   R1,B'1111',EX1SRWA  Load service work area addr @430P329
         BZ    ENDSEP              Branch if no work storage   @430P329
         S     R1,$F4              Subtract 4 for eyecatcher   @430P329
         SPACE 1                                               @430P329
         XC    EX1SRWA,EX1SRWA     Set address to zero         @430P329
         SPACE 1                                               @430P329
        $RETWORK (R1)              Release the working storage @430P329
         SPACE 1                                               @430P329
ENDSEP  $RETURN RC=(R6)            Return to the caller        @430P329
         SPACE 2
         LTORG ,                   DEFINE LITERAL ORIGIN
         SPACE 1
         DROP  R3,R4,R5,R7,R12     Drop register addressabilities
         TITLE 'MORESTOR -- Get more working storage for SWBTUREQ RETRIC
               EVE service'
***********************************************************************
*                                                                     *
*        MORESTOR - Get more working storage for the SWBTUREQ         *
*                   RETRIEVE service.                                 *
*                                                                     *
*   FUNCTION:                                                         *
*                                                                     *
*        This subroutine is called to release the current SWBTUREQ    *
*        working storage and obtain the amount required by the        *
*        SWBTUREQ RETRIEVE service.                                   *
*                                                                     *
*   LINKAGE:                                                          *
*                                                                     *
*      Accessed via $CALL                                             *
*      Returned via $RETURN                                           *
*                                                                     *
*                                                                     *
*   REGISTER USAGE (ENTRY/EXIT):                                      *
*                                                                     *
*        REG          VALUE ON ENTRY            VALUE ON EXIT         *
*                                                                     *
*        R0           N/A                       unchanged             *
*        R1           Current working           new working           *
*                       storage address           storage address     *
*        R2           Required working          unchanged             *
*                       storage size                                  *
*        R3           N/A                       unchanged             *
*        R4           Current working           unchanged             *
*                       storage size                                  *
*        R5-R13       N/A                       unchanged             *
*        R14          Return address            unchanged             *
*        R15          Entry address             Return code           *
*                                                                     *
*                                                                     *
*   RETURN CODE ( R15 ON EXIT ):                                      *
*                                                                     *
*        0 - Processing successful; More storage obtained             *
*                                                                     *
*        4 - Processing unsucccessful                                 *
*                                                                     *
*   OTHER CONSIDERATIONS :                                            *
*                                                                     *
*        None                                                         *
*                                                                     *
***********************************************************************
         SPACE 1
         USING MORESTOR,R12        Establish addressability
         EJECT
MORESTOR $SAVE ,                   Save the caller's registers
         SPACE 1
         LR    R12,R15             Set local base
         SPACE 1
         LTR   R4,R4               Is the current working storage      C
                                     size equal to zero ?
         BZ    GETSTOR                 No, skip the retwork
         LTR   R1,R1               Is the current working storage      C
                                     addr equal to zero?
         BZ    GETSTOR                 No, skip the retwork
         S     R1,$F4              Subtract 4 for the eyecatcher
         SPACE 1
        $RETWORK (R1)              Release the current working storage
         SPACE 1
GETSTOR  DS    0H
         LA    R2,3(,R2)           Convert the size in bytes
         SRL   R2,2                   into the size in words
         LA    R2,1(,R2)           Add 1 for the eyecatcher
         SPACE 1
        $GETWORK WORDS=(R2),USE=SRWA,ERRET=NOMOREST    Get required    C
                                                     working storage
         SPACE 2
        $STORE R1                  Pass the new working storage        C
                                     address back to the caller
         LA    R15,0               Set return code = 0
         SPACE 2
         B     ENDMORE             Go return to caller
         EJECT
***********************************************************************
*                                                                     *
*        Return to the caller.                                        *
*                                                                     *
***********************************************************************
         SPACE 2
NOMOREST LA    R15,4               Set return code = 4
         B     ENDMORE             Go return to caller
         SPACE 2
ENDMORE $RETURN RC=(R15)           Return to the caller
         SPACE 1
         LTORG ,                   Define literal origin
         SPACE 1
         DROP  R12                 Drop local base register
         TITLE 'PRINTBLK -- Print the separator page block letters'
***********************************************************************
*                                                                     *
*        PRINTBLK - Print the job name and job id in block            *
*                   letters on the separator page.                    *
*                                                                     *
*   FUNCTION:                                                         *
*                                                                     *
*        This routine is called to print the job name and job         *
*        id in block letters on the separator page.                   *
*                                                                     *
*   LINKAGE:                                                          *
*                                                                     *
*        Accessed via $CALL                                           *
*        Returned via $RETURN                                         *
*                                                                     *
*                                                                     *
*   REGISTER USAGE (ENTRY/EXIT):                                      *
*                                                                     *
*        REG          VALUE ON ENTRY            VALUE ON EXIT         *
*                                                                     *
*        R0-R4        N/A                         Unchanged           *
*        R5           EX1WORK address             Unchanged           *
*        R6           N/A                         Unchanged           *
*        R7           Exit 1 parm list addr       Unchanged           *
*        R8-R10       N/A                         Unchanged           *
*        R11          HCT address                 Unchanged           *
*        R12          Local base address          Unchanged           *
*        R13          PCE address                 Unchanged           *
*        R14          Return address              Unchanged           *
*        R15          Entry address               Unchanged           *
*                                                                     *
*                                                                     *
*   RETURN CODES:                                                     *
*                                                                     *
*        None                                                         *
*                                                                     *
*   OTHER CONSIDERATIONS:                                             *
*                                                                     *
*        None                                                         *
*                                                                     *
***********************************************************************
         SPACE 2
         USING PRINTBLK,R12        Establish local addressability
         USING XPL,R7              Establish parm list addressability
         USING JCT,R10             Establish JCT addressability
         EJECT
PRINTBLK $SAVE ,                   Save the caller's registers
         SPACE 2
         LR    R12,R15             Set local base
         L     R10,X001JCT         Load JCT address
         SPACE 1                                               @430P329
         L     R3,X001DSCT         Load DSCT Address           @430P329
         SPACE 1                                               @430P329
         USING DSCT,R3             Establish Addressability    @430P329
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*        Print the job name in block letters ( slanted ).             *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
         LTR   R3,R3               Is the DSCT Valid?          @430P329
         BZ    NOTDSCT1            No, use JCT Jobname         @430P329
         LA    R0,DSJBN            Address of TP Job Name      @430P329
         B     YESDSCT1                                        @430P329
         SPACE 1                                               @430P329
NOTDSCT1 LA    R0,JCTJNAME         Get address of jobname      @430P329
         SPACE 1                                               @430P329
YESDSCT1 L     R1,X001HBUF         Get buffer address          @430P329
         SPACE 1                                               @510P214
        $SEPPDIR (R1)         Send a PDIR in case a SNA REMOTE @510P214
         SPACE 2
        $PBLOCK DATA=(R0),BUFFER=(R1),SLANT=YES     Call routine to    C
                                                  print block letters
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*        Print a blank line                                           *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
         L     R4,X001HBUF         Load buffer address
         USING SEPDSECT,R4         Establish buffer addressability
         SPACE 1
         LA    R4,BUFSTART-BFPDSECT(,R4)     Get address of work space C
                                                in buffer
         SPACE 1
         MVI   PAGELINE,C' '                 Put a blank in buffer
         LA    R0,1                          Set length to 1
         SPACE 2
        $PRPUT DATA=(R4),LEN=(R0),WAIT=YES   Print a blank line
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*        Print the job id in block letters ( without slant ).         *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
         LTR   R3,R3               Is the DSCT Valid?          @430P329
         BZ    NOTDSCT2            No, use JCT JOBID           @430P329
         LA    R0,DSWKID            Address of TP Work ID      @430P329
         B     YESDSCT2                                        @430P329
         SPACE 1                                               @430P329
NOTDSCT2 LA    R0,JCTJOBID         Get address of Job ID       @430P329
         SPACE 1                                               @430P329
YESDSCT2 L     R1,X001HBUF         Get buffer address          @430P329
         SPACE 2
        $PBLOCK DATA=(R0),BUFFER=(R1)      Call routine to print       C
                                                 block letters
         SPACE 2
         DROP  R3                  Drop DSCT                   @430P329
         SPACE 1                                               @430P329
*---------------------------------------------------------------------*
*                                                                     *
*        Print a blank line                                           *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
         L     R4,X001HBUF         Load buffer address
         LA    R4,BUFSTART-BFPDSECT(,R4)     Get address of work space C
                                                in buffer
         SPACE 1
         MVI   PAGELINE,C' '                 Put a blank in buffer
         LA    R0,1                          Set length to 1
         SPACE 2
        $PRPUT DATA=(R4),LEN=(R0),WAIT=YES   Print a blank line
         EJECT
***********************************************************************
*                                                                     *
*        Return to the caller.                                        *
*                                                                     *
***********************************************************************
         SPACE 2
        $RETURN ,                  Return to the caller
         SPACE 1
         LTORG ,                   Define literal origin
         SPACE 1
         DROP  R4,R7,R10,R12       Drop addressabilities
         TITLE 'BUILDBOX -- Build and print each line of the separator C
               page detail box'
***********************************************************************
*                                                                     *
*        BUILDBOX - Build and print each line of the separator        *
*                   page detail box.                                  *
*                                                                     *
*   FUNCTION:                                                         *
*                                                                     *
*        This sub-routine is called to build and print each           *
*        line of the separator page detail box.                       *
*                                                                     *
*        Addressability to the HASP buffer work space offset          *
*        is established and the routine CLRLINE is called to          *
*        clear the separator page line buffer.                        *
*                                                                     *
*        The routine PUTFRAME is then called to set up the            *
*        detail box frame in the top line and the macro $PRPUT        *
*        is invoked to print it.                                      *
*                                                                     *
*        CLRLINE is called to clear the next line and reset           *
*        the buffer work space pointer.  The detail box               *
*        information is then inserted in the line buffer and          *
*        $PRPUT is invoked to print this line.  This process          *
*        is repeated for all subsequent lines of the detail           *
*        box.                                                         *
*                                                                     *
*        PUTFRAME is called to set up the detail box frame in         *
*        the bottom line and $PRPUT is called to print it.            *
*                                                                     *
*                                                                     *
*   LINKAGE:                                                          *
*                                                                     *
*        Accessed via $CALL                                           *
*        Returned via $RETURN                                         *
*                                                                     *
*        INPUT:                                                       *
*                R7           -  Address of Exit 1 parameter list     *
*                SEPDSECT     -  Separator page line/box DSECT        *
*        OUTPUT:                                                      *
*                The separator page detail box is produced.           *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*   REGISTER USAGE (ENTRY/EXIT):                                      *
*                                                                     *
*        REG          VALUE ON ENTRY            VALUE ON EXIT         *
*                                                                     *
*        R0-R4        N/A                         Unchanged           *
*        R5           EX1WORK address             Unchanged           *
*        R6           N/A                         Unchanged           *
*        R7           Exit 1 parm list addr       Unchanged           *
*        R8-R10       N/A                         Unchanged           *
*        R11          HCT address                 Unchanged           *
*        R12          Local base address          Unchanged           *
*        R13          PCE address                 Unchanged           *
*        R14          Return address              Unchanged           *
*        R15          Entry address               Unchanged           *
*                                                                     *
*                                                                     *
*   RETURN CODES:                                                     *
*                                                                     *
*        None                                                         *
*                                                                     *
*   OTHER CONSIDERATIONS:                                             *
*                                                                     *
*        None                                                         *
*                                                                     *
***********************************************************************
         SPACE 1
         USING BUILDBOX,R12        Establish local addressability
         USING XPL,R7              Establish parm list addressability
         USING EX1WORK,R5          Establish work area addressability
         USING SEPDSECT,R4         Establish addr'ty to HASP buffer
         EJECT
BUILDBOX $SAVE ,                   Save the caller's registers
         SPACE 1
         LR    R12,R15             Set local base
         EJECT
*---------------------------------------------------------------------*
*               BUILD AND PRINT TOP LINE                              *
*---------------------------------------------------------------------*
         SPACE 1
         BAL   R8,CLRLINE          Clear line and get buffer offset
         SPACE 1
        $CALL  PUTFRAME            Build top line of detail box
         SPACE 1
        $PRPUT DATA=(R4),LEN=L'PAGELINE,WAIT=YES      Call PRPUT macro C
                                                 to print the top line
         SPACE 1
*---------------------------------------------------------------------*
*               BUILD AND PRINT A BLANK LINE                          *
*         ( FRAME CHARACTERS IN COL 1 AND COL 80 )                    *
*---------------------------------------------------------------------*
         SPACE 1
         BAL   R8,CLRLINE          Clear line and get buffer offset
         SPACE 1
        $PRPUT DATA=(R4),LEN=L'PAGELINE,WAIT=YES    Print a blank line
         EJECT
*---------------------------------------------------------------------*
*               BUILD AND PRINT JOB ID LINE                           *
*---------------------------------------------------------------------*
         SPACE 1
         BAL   R8,CLRLINE          Clear line and get buffer offset
         SPACE 1
         L     R10,X001JCT         Establish addressability
         USING JCT,R10                to JCT
         SPACE 1
         L     R3,X001DSCT         Load DSCT Address           @430P329
         USING DSCT,R3             Establish Addressability    @430P329
         SPACE 1                                               @430P329
         MVC   BOXDESC,LBJOBID     Put Job ID label in line    @430P329
         SPACE 1                                               @430P329
         LTR   R3,R3               Is the DSCT Valid?          @430P329
         BZ    NODSCT1             No, use JCT JobID           @430P329
         MVC   BOXJOBID,DSWKID     Use TP Work ID              @430P329
         B     YEADSCT1                                        @430P329
         SPACE 1                                               @430P329
NODSCT1  MVC   BOXJOBID,JCTJOBID   Use JCT JOBID in line       @430P329
         SPACE 1
*---------------------------------------------------------------------*
*               Segment ID                                            *
*---------------------------------------------------------------------*
         SPACE 1
YEADSCT1 ICM   R6,B'1111',X001PDDB Addr of first PDDB          @R03P010
         BZ    JOBIDLN               Skip if job trailer exit  @R03P010
         SPACE 1                                               @R03P010
         USING PDB,R6              Declare PDDB addressability @R03P010
         SPACE 1
         ICM   R2,B'1111',PDBSEGID      Check for segment ID
         BZ    JOBIDLN             Branch to print line if no seg id
         MVC   BOXSGLBL,LBSEGID          Fill in Segment ID label
         SPACE 1
         CL    R2,=F'99999'        Is segment greater than 99999?
         BH    BADSEGID                Yes, segment is invalid
         SPACE 1
         CVD   R2,EX1CVDWA            Convert the
         L     R15,=A(SEGMASK)          Segment number
         MVC   EX1SEGWA,0(R15)            from binary
         EDMK  EX1SEGWA,EX1CVDWA+5           to EBCDIC
         SPACE 1
         LA    R2,EX1SEGWA+L'EX1SEGWA-1    Truncate all leading
         SLR   R2,R1                          zeros from Segment
         EX    R2,EXECSEG          Execute the move
         B     JOBIDLN             Skip over the move
         SPACE 1
EXECSEG  MVC   BOXSGINF(*-*),0(R1)     *** EXECUTE *** Seg Number
         SPACE 1
BADSEGID MVC   BOXSGINF,=C'*****'       Show segment is invalid
         SPACE 1
JOBIDLN $PRPUT DATA=(R4),LEN=L'PAGELINE,WAIT=YES      Produce the line
         SPACE 2
         DROP  R6                  Drop PDDB addressability
         EJECT
*---------------------------------------------------------------------*
*               BUILD AND PRINT JOB NAME LINE                         *
*---------------------------------------------------------------------*
         SPACE 1
         BAL   R8,CLRLINE          Clear line and get buffer offset
         SPACE 1
         MVC   BOXDESC,LBJOBNAM    Job Name label              @430P329
         SPACE 1                                               @430P329
         LTR   R3,R3               Is the DSCT Valid?          @430P329
         BZ    NODSCT2             No, use JCT Jobname         @430P329
         MVC   BOXJNAME,DSJBN      Use TP Job Name             @430P329
         B     YEADSCT2                                        @430P329
NODSCT2  MVC   BOXJNAME,JCTJNAME   Fill in Job Name field      @430P329
         SPACE 1
YEADSCT2 $PRPUT DATA=(R4),LEN=L'PAGELINE,WAIT=YES Produce line @430P329
         SPACE 1                                               @430P329
         DROP  R3                  Drop DSCT                   @430P329
         SPACE 2                                               @430P329
*---------------------------------------------------------------------*
*               BUILD AND PRINT USER ID LINE                          *
*---------------------------------------------------------------------*
         SPACE 1
         BAL   R8,CLRLINE          Clear line and get buffer offset
         SPACE 1
         L     R9,X001WJOE         Establish work-JOE
         USING JOE,R9                 addressability
         SPACE 1
         MVC   BOXDESC,LBUSERID          User ID label
         MVC   BOXCRUID,JOECRUID         Fill in USER ID field
         SPACE 1
        $PRPUT DATA=(R4),LEN=L'PAGELINE,WAIT=YES      Produce the line
         EJECT
*---------------------------------------------------------------------*
*               BUILD AND PRINT SYSOUT CLASS LINE                     *
*---------------------------------------------------------------------*
         SPACE 1
         BAL   R8,CLRLINE          Clear line and get buffer offset
         SPACE 1
         MVC   BOXDESC,LBSYSCL           SYSOUT Class label
         MVC   BOXSYSCL,JOECURCL         Fill in SYSYOUT class
         SPACE 1
        $PRPUT DATA=(R4),LEN=L'PAGELINE,WAIT=YES      Produce the line
         SPACE 2
*---------------------------------------------------------------------*
*               BUILD AND PRINT OUTPUT GROUP LINE                     *
*---------------------------------------------------------------------*
         SPACE 1
         BAL   R8,CLRLINE          Clear line and get buffer offset
         SPACE 1
         MVC   BOXDESC,LBOUTGRP          Output Group label
         MVC   BOXINFO(L'JOENAME),JOENAME    Fill in JOE Output Name
         LA    R2,BOXINFO          Point to start of OUTGRP name
         LA    R6,L'JOENAME        Set length of OUTGRP name
         SPACE 1
NAMELOOP CLI   0(R2),C' '          Is this character a blank?
         BE    SETSYNTX              Yes, go set syntax
         LA    R2,1(,R2)             No, point to next character
         BCT   R6,NAMELOOP         Go find end of outgrp name
         SPACE 1
SETSYNTX MVI   0(R2),C'.'          Set syntax for separator
         LA    R2,1(,R2)           Set pointer for separator
         LH    R0,JOEID1           1st qualifier to convert
         CVD   R0,EX1CVDWA         Convert to decimal
         SPACE 1
         MVC   EX1CVDWA-1(6),=X'402020202120'    Move mask for result
         ED    EX1CVDWA-1(6),EX1CVDWA+5          Convert to EBCDIC
         SPACE 1
         LA    R6,4                Set for                     @510APAR
         LA    R1,EX1CVDWA           execute
         EJECT
JID1LOOP CLI   0(R1),C' '          Is this beginning of JOEid?
         BNE   JID1FINI              Yes, beginning found
         BCTR  R6,0                  No, reset count
         LA    R1,1(,R1)               and pointer
         B     JID1LOOP                  continue checking
         SPACE 1
JID1FINI EX    R6,EXECJOID         Move id to JOEid1
         LA    R2,1(R6,R2)           Reset pointer             @510APAR
         MVI   0(R2),C'.'          Set period in
         LA    R2,1(,R2)           Reset pointer
         LH    R0,JOEID2           2nd qualifier to convert
         CVD   R0,EX1CVDWA         Convert to decimal
         SPACE 1
         MVC   EX1CVDWA-1(6),=X'402020202120'    Move mask for result
         ED    EX1CVDWA-1(6),EX1CVDWA+5          Convert to EBCDIC
         SPACE 1
         LA    R6,4                Set for                     @510APAR
         LA    R1,EX1CVDWA           execute
JID2LOOP CLI   0(R1),C' '          Is this beginning of JOEid?
         BNE   JID2FINI              Yes, beginning found
         BCTR  R6,0                  No, reset count
         LA    R1,1(,R1)               and pointer
         B     JID2LOOP                  continue checking
         SPACE 1
JID2FINI EX    R6,EXECJOID         Move id to JOEid2
         SPACE 1
        $PRPUT DATA=(R4),LEN=L'PAGELINE,WAIT=YES      Produce the line
         SPACE 1
EXECJOID MVC 0(*-*,R2),0(R1)       **** EXECUTE ****
         EJECT
*---------------------------------------------------------------------*
*               BUILD AND PRINT TITLE LINE                            *
*---------------------------------------------------------------------*
         SPACE 1
         BAL   R8,CLRLINE          Clear line and get buffer offset
         SPACE 1
         USING SJTRKEYL,R2                   Establish addressability
         SPACE 1
         MVC   BOXDESC,LBTITLE               Title label
         ICM   R2,B'1111',EX1KYLST           Load Key list address
         BZ    PRTITLE                       Branch if no Key list
         LA    R2,KYLSTTL(,R2)               Load address of the Title C
                                                key in the Key List
         ICM   R1,B'1111',SJTRTPAD           Load Title TU address
         BZ    PRTITLE                       Branch if no Title
         LA    R1,DOCNTENT-DOCNUNIT(,R1)     Address of TU length/     C
                                               parameter pair
         SPACE 1
         BAL   R8,MOVETU                     Get Title data from TU
         SPACE 1
PRTITLE $PRPUT DATA=(R4),LEN=L'PAGELINE,WAIT=YES      Produce the line
         SPACE 1
*---------------------------------------------------------------------*
*               BUILD AND PRINT A BLANK LINE                          *
*         ( FRAME CHARACTERS IN COL 1 AND COL 80 )                    *
*---------------------------------------------------------------------*
         SPACE 1
         BAL   R8,CLRLINE          Clear line and get buffer offset
         SPACE 1
        $PRPUT DATA=(R4),LEN=L'PAGELINE,WAIT=YES     Produce the line
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*               BUILD AND PRINT DESTINATION LINE                      *
*               --------------------------------                      *
*                                                                     *
*        Convert the binary route code ( in JOEROUT ) to its          *
*        corresponding symbolic destination value using the           *
*        $DEST macro.                                                 *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
         BAL   R8,CLRLINE          Clear line and get buffer offset
         SPACE 1
         MVC   BOXDESC,LBDEST                Fill in destination label
         MVC   EX1DEST(L'JOEROUT),JOEROUT    Copy binary route code
         SPACE 1
         L     R9,X001CJOE          Get addressability to char-JOE
         SPACE 1
        $DEST  DEST=EX1DEST,LEN=L'EX1DEST,CONV=SBINARY,                C
               USER=JOEUSER                  Convert route code
         SPACE 1
         L     R9,X001WJOE          Reset addressability to work-JOE
         SPACE 1
         MVC   BOXDEST,EX1DEST               Fill in destination value
         SPACE 1
        $PRPUT DATA=(R4),LEN=L'PAGELINE,WAIT=YES      Produce the line
         EJECT
*---------------------------------------------------------------------*
*               BUILD AND PRINT NAME LINE                             *
*---------------------------------------------------------------------*
         SPACE 1
         BAL   R8,CLRLINE          Clear line and get buffer offset
         SPACE 1
         MVC   BOXDESC,LBNAME                Fill in Name label
         ICM   R2,B'1111',EX1KYLST           Load Key/TU prt list addr
         BZ    NONAME                        Branch if no Key list
         LA    R2,KYLSTNM(,R2)               Load address of the Name  C
                                                key in the Key List
         ICM   R1,B'1111',SJTRTPAD           Load Name TU address
         BZ    NONAME                        Branch if no Name
         LA    R1,DOCNTENT-DOCNUNIT(,R1)     Address of TU length/     C
                                                 parameter pair
         SPACE 1
         BAL   R8,MOVETU                     Get Name data from TU
         SPACE 1
         B     PRTNAME                       Branch to print the Name
         SPACE 1
NONAME   MVC   BOXPNAME,JCTPNAME             Name from the JOB stmt
         SPACE 1
PRTNAME $PRPUT DATA=(R4),LEN=L'PAGELINE,WAIT=YES      Produce the line
         SPACE 1
*---------------------------------------------------------------------*
*               BUILD AND PRINT ROOM LINE                             *
*---------------------------------------------------------------------*
         SPACE 1
         BAL   R8,CLRLINE          Clear line and get buffer offset
         SPACE 1
         MVC   BOXDESC,LBROOM                Fill in Room label
         ICM   R2,B'1111',EX1KYLST           Load Key/TU prt list addr
         BZ    NOROOM                        Branch if no Key list
         LA    R2,KYLSTRM(,R2)               Load address of the Room  C
                                                key in the Key List
         ICM   R1,B'1111',SJTRTPAD           Load Room TU address
         BZ    NOROOM                        Branch if no Room
         LA    R1,DOCNTENT-DOCNUNIT(,R1)     Address of TU length/     C
                                                 parameter pair
         SPACE 1
         BAL   R8,MOVETU                     Get Room data from TU
         SPACE 1
         B     PRTROOM                       Branch to print the Room
         SPACE 1
NOROOM   MVC   BOXROOMN,JCTROOMN             Room from the JOB stmt
         EJECT
PRTROOM $PRPUT DATA=(R4),LEN=L'PAGELINE,WAIT=YES      Produce the line
         EJECT
*---------------------------------------------------------------------*
*               BUILD AND PRINT BUILDING LINE                         *
*---------------------------------------------------------------------*
         SPACE 1
         BAL   R8,CLRLINE          Clear line and get buffer offset
         SPACE 1
         MVC   BOXDESC,LBBLDG                Fill in Building label
         ICM   R2,B'1111',EX1KYLST           Load Key/TU prt list addr
         BZ    PRTBLDG                       Branch if no Key list
         LA    R2,KYLSTBL(,R2)               Load address of the Bldg  C
                                                key in the Key List
         ICM   R1,B'1111',SJTRTPAD           Load Building TU address
         BZ    PRTBLDG                       Branch if no Building
         LA    R1,DOCNTENT-DOCNUNIT(,R1)     Address of TU length/     C
                                                 parameter pair
         SPACE 1
         BAL   R8,MOVETU                     Get Building data from TU
         SPACE 1
PRTBLDG $PRPUT DATA=(R4),LEN=L'PAGELINE,WAIT=YES      Produce the line
         SPACE 1
*---------------------------------------------------------------------*
*               BUILD AND PRINT DEPARTMENT LINE                       *
*---------------------------------------------------------------------*
         SPACE 1
         BAL   R8,CLRLINE          Clear line and get buffer offset
         SPACE 1
         MVC   BOXDESC,LBDEPT                Fill in Department label
         ICM   R2,B'1111',EX1KYLST           Load Key/TU prt list addr
         BZ    PRTDEPT                       Branch if no Key list
         LA    R2,KYLSTDP(,R2)               Load address of the Dept  C
                                                key in the Key List
         ICM   R1,B'1111',SJTRTPAD           Load Department TU address
         BZ    PRTDEPT                       Branch if no Department
         LA    R1,DOCNTENT-DOCNUNIT(,R1)     Address of TU length/     C
                                                 parameter pair
         SPACE 1
         BAL   R8,MOVETU                    Get Department data from TU
         SPACE 1
PRTDEPT $PRPUT DATA=(R4),LEN=L'PAGELINE,WAIT=YES      Produce the line
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*               BUILD AND PRINT ADDRESS LINES                         *
*               -----------------------------                         *
*        The address can be from 1 to 4 lines long.  Any lines        *
*        that are unused must be printed as blank lines, but the      *
*        label "ADDRESS:" must appear on the first line even if       *
*        no address was specified on the OUTPUT JCL.                  *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         BAL   R8,CLRLINE          Clear line and get buffer offset
         SPACE 1
         MVC   BOXDESC,LBADDR                Fill in Address label
         ICM   R2,B'1111',EX1KYLST           Load Key/TU prt list addr
         BZ    NOADDR                        Branch if no Key list
         LA    R2,KYLSTAD(,R2)               Load address of the Addr  C
                                                key in the Key/TU List
         ICM   R1,B'1111',SJTRTPAD           Load Address TU address
         BZ    NOADDR                        Branch if no Address
         SPACE 1
         LH    R3,DOCNTNUM-DOCNUNIT(,R1)     Number of lines used for  C
                                                 Address
         CL    R3,$F4                        Number greater than four?
         BNH   SAVENUML                        No, save number of lines
         LA    R3,4                            Yes, set to max of four
         SPACE 1
SAVENUML STH   R3,EX1ADNUM                   Save number of lines
         LTR   R3,R3                         Number greater than zero?
         BZ    NOADDR                          No, produce blank lines
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*        Loop to build/print multiple line address                    *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         LA    R1,DOCNTENT-DOCNUNIT(,R1)     Address of TU length/     C
                                                 parameter pair
         SPACE 1
         USING DOCNTFLD,R1                   Establish addressability
         SPACE 1
ADDRLOOP LR    R6,R1                         Save the TU address
         SPACE 1
         BAL   R8,MOVETU                     Get Address data from TU
         SPACE 1
        $PRPUT DATA=(R4),LEN=L'PAGELINE,WAIT=YES      Produce the line
         SPACE 1
         LR    R1,R6                         Restore TU address
         SPACE 1
         LH    R15,DOCNTLEN                  Move to next
         LA    R1,L'DOCNTLEN(R15,R1)           Address Length/Data pair
         SPACE 1
         BAL   R8,CLRLINE          Clear line and get buffer offset
         SPACE 1
         BCT   R3,ADDRLOOP                   Loop again if more data
         SPACE 1
         DROP  R1                            Drop addressability
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*        Loop to build/print multiple blank lines                     *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
NOADDR   LH    R6,$H4                       Maximum number of          C
                                             available lines of address
         LH    R3,EX1ADNUM                  Number Address lines used
         SR    R6,R3                        Determine the number       C
                                                    of unused lines
         LA    R6,1(,R6)                    Add 1 for extra blank line
         SPACE 1
BLKLINE $PRPUT DATA=(R4),LEN=L'PAGELINE,WAIT=YES    Print blank lines
         SPACE 1
         BAL   R8,CLRLINE          Clear line and get buffer offset
         SPACE 1
         BCT   R6,BLKLINE                       Loop if more to print
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*               BUILD AND PRINT THE PRINT TIME LINE                   *
*                                                                     *
*    Note: The TIME macro with the DEC parameter returns the time     *
*          of day in R0 and the date in R1.                           *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         BAL   R8,CLRLINE          Clear line and get buffer offset
         SPACE 1
         MVC   BOXDESC,LBPRTIME             Fill in Printer Time label
         SPACE 1
         TIME  DEC                          Get time
         SPACE 1
         ST    R0,EX1CVDWA                  Save the time
         LR    R6,R1                        Save the date
         L     R15,=A(TIMEMASK)             Get Edit mask address
         MVC   BOXPRTIM,0(R15)              Copy edit pattern
         ED    BOXPRTIM,EX1CVDWA            Edit time into proper field
         CLI   BOXPRTIM,X'21'      LEADING 0?                    @430GO
         BNE   PRLINE                NO, NO NEED TO EDIT         @430GO
         MVI   BOXPRTIM,X'F0'      PUT PRINTABLE 0 IN FIELD      @430GO
         SPACE 1
PRLINE  $PRPUT DATA=(R4),LEN=L'PAGELINE,WAIT=YES  PRODUCE LINE   @430GO
         SPACE 1
*---------------------------------------------------------------------*
*               BUILD AND PRINT THE PRINT DATE LINE                   *
*---------------------------------------------------------------------*
         SPACE 1
         BAL   R8,CLRLINE          Clear line and get buffer offset
         SPACE 1
         MVC   BOXDESC,LBPRDATE             Fill in Printer Date label
         SPACE 1
        $CALL  FMTDATE                      Format date
         SPACE 1
        $PRPUT DATA=(R4),LEN=L'PAGELINE,WAIT=YES      Produce the line
         EJECT
*---------------------------------------------------------------------*
*               BUILD AND PRINT PRINTER NAME LINE                     *
*---------------------------------------------------------------------*
         SPACE 1
         BAL   R8,CLRLINE          Clear line and get buffer offset
         SPACE 1
         MVC   BOXDESC,LBPRNAME             Printer Name label
         SPACE 1
         USING DCT,R3                       Establish addressability
         L     R3,X001DCT                      to printer DCT
         SPACE 1
         MVC   BOXPDEVN,DCTDEVN             Device Name
         SPACE 1
         DROP  R3                           Drop DCT addressability
         SPACE 1
        $PRPUT DATA=(R4),LEN=L'PAGELINE,WAIT=YES      Produce the line
         SPACE 1
*---------------------------------------------------------------------*
*               BUILD AND PRINT SYSTEM NAME LINE                      *
*---------------------------------------------------------------------*
         SPACE 1
         BAL   R8,CLRLINE          Clear line and get buffer offset
         SPACE 1
         MVC   BOXDESC,LBSYSTEM             System label
         MVC   BOXSYSTM,$SID                System ID
         SPACE 1
        $PRPUT DATA=(R4),LEN=L'PAGELINE,WAIT=YES      Produce the line
         EJECT
*---------------------------------------------------------------------*
*               BUILD AND PRINT A BLANK LINE                          *
*         ( FRAME CHARACTERS IN COL 1 AND COL 80 )                    *
*---------------------------------------------------------------------*
         SPACE 1
         BAL   R8,CLRLINE          Clear line and get buffer offset
         SPACE 1
        $PRPUT DATA=(R4),LEN=L'PAGELINE,WAIT=YES     Produce the line
         SPACE 1
*---------------------------------------------------------------------*
*               BUILD AND PRINT BOTTOM LINE                           *
*---------------------------------------------------------------------*
         SPACE 1
         BAL   R8,CLRLINE          Clear line and get buffer offset
         SPACE 1
        $CALL  PUTFRAME            Build last line of detail box
         SPACE 1
        $PRPUT DATA=(R4),LEN=L'PAGELINE,WAIT=YES      Call PRPUT macro C
                                                to print the last line
         EJECT
***********************************************************************
*                                                                     *
*        Return to the caller.                                        *
*                                                                     *
***********************************************************************
         SPACE 2
ENDBOX   DS    0H
         SPACE 1
        $RETURN ,                  Return to the caller
         EJECT
***********************************************************************
*                                                                     *
*        Static data and the literal pool                             *
*                                                                     *
***********************************************************************
         SPACE 2
         LTORG ,                            Define literal origin
         SPACE 1
         DROP  R2,R4,R5,R7,R9,R10,R12       Drop addressabilities
         TITLE 'CLRLINE  -- Clear the output line; Reset buffer work spC
               ace addressability'
***********************************************************************
*                                                                     *
*        CLRLINE -  Resets addressability to the beginning of         *
*                   the buffer work space within the HASP             *
*                   buffer that is passed to the exit.                *
*                   Clears the detail box output buffer line          *
*                   and inserts the box frame character in            *
*                   columns 1 and 80.                                 *
*                                                                     *
*                                                                     *
*   FUNCTION:                                                         *
*                                                                     *
*        This subroutine loads R4 with the address of the work        *
*        space in the HASP buffer passed via the exit parameter       *
*        list.  This ensures that the data to be printed by           *
*        $PRPUT is at the correct offset in the buffer.               *
*                                                                     *
*        The detail box output buffer line is cleared so that         *
*        residual data is not printed on the current output           *
*        line.  The box frame character is inserted in columns        *
*        1 and 80.                                                    *
*                                                                     *
*   LINKAGE:                                                          *
*                                                                     *
*        Accessed via a  BAL R8,CLRLINE                               *
*        Returned via address in R8                                   *
*                                                                     *
*                                                                     *
*   REGISTER USAGE:                                                   *
*                                                                     *
*        REG          VALUE ON ENTRY         VALUE ON EXIT            *
*                                                                     *
*        R0-R3        N/A                      Unchanged              *
*        R4           N/A                      Buffer work space      *
*                                                address              *
*        R5-R6        N/A                      Unchanged              *
*        R7           Parameter list address   Unchanged              *
*        R8           Return address           Unchanged              *
*        R9-R15       N/A                      Unchanged              *
*                                                                     *
*   RETURN CODES:                                                     *
*                                                                     *
*        None                                                         *
*                                                                     *
*   OTHER CONSIDERATIONS:                                             *
*                                                                     *
*        None                                                         *
*                                                                     *
***********************************************************************
         SPACE 1
         USING SEPDSECT,R4                 Establish
         USING XPL,R7                           addressability
         EJECT
CLRLINE  L     R4,X001HBUF                 Load buffer address and
         LA    R4,BUFSTART-BFPDSECT(,R4)     point to the work space   C
                                                offset
         SPACE 1
         MVI   PAGELINE,C' '                        Clear the
         MVC   PAGELINE+1(L'PAGELINE-1),PAGELINE       separator line
         MVI   BOXCOL1,C'*'                         Insert
         MVI   BOXCOL80,C'*'                           frame characters
         SPACE 1
         BR    R8                          Return to caller
         SPACE 1
         LTORG ,                           Define literal origin
         SPACE 1
         DROP  R4,R7                       Drop addressability
         TITLE 'MOVETU   -- Move TEXT UNIT text into separator page detC
               ail box'
***********************************************************************
*                                                                     *
*        MOVETU -  Move the Text Unit ( TU ) text from the TU         *
*                  output area to the separator page detail           *
*                  box line.                                          *
*                                                                     *
*   FUNCTION:                                                         *
*                                                                     *
*        This subroutine is called to move the TU text from the       *
*        TU output area to the detail box output line.  Since         *
*        TUs are variable length (up to sixty characters long),       *
*        the detail line is padded with blanks on the right after     *
*        the move is performed.                                       *
*                                                                     *
*   LINKAGE:                                                          *
*                                                                     *
*        Accessed via a  BAL R8,MOVETU                                *
*        Returned via address in R8                                   *
*                                                                     *
*                                                                     *
*   REGISTER USAGE:                                                   *
*                                                                     *
*        REG          VALUE ON ENTRY         VALUE ON EXIT            *
*                                                                     *
*        R0           N/A                      Destroyed              *
*        R1           Address of TU            Destroyed              *
*                      length/parameter pair                          *
*        R2-R3        N/A                      Unchanged              *
*        R4           Address of output        Unchanged              *
*                      buffer work space                              *
*        R5-R13       N/A                      Unchanged              *
*        R14-R15      N/A                      Destroyed              *
*                                                                     *
*   RETURN CODES:                                                     *
*                                                                     *
*        None                                                         *
*                                                                     *
*   OTHER CONSIDERATIONS:                                             *
*                                                                     *
*        None                                                         *
*                                                                     *
***********************************************************************
         SPACE 1
         USING DOCNTFLD,R1                  Establish
         USING SEPDSECT,R4                      addressability
         EJECT
MOVETU   LA    R14,DOCNTPRM                 Load TU text address
         LH    R15,DOCNTLEN                 Length of TU text
         ICM   R15,B'1000',$BLANKS          Set pad character to blank
         LA    R0,BOXINFO                   Set up the
         LA    R1,L'BOXINFO                    receiving field
         SPACE 1
         MVCL  R0,R14                       Move the text
         SPACE 1
         BR    R8                           Return to the caller
         SPACE 1
         LTORG ,                            Define literal origin
         SPACE 1
         DROP  R1,R4                        Drop TU, SEPDSECT addr'ty
         TITLE 'PUTFRAME -- Create the detail box frame in the top and C
               bottom lines'
***********************************************************************
*                                                                     *
*        PUTFRAME - Create the detail box frame in the top and        *
*                   bottom lines.                                     *
*                                                                     *
*   FUNCTION:                                                         *
*                                                                     *
*        This subroutine is called to create the frame in the         *
*        top and bottom lines of the separator page detail box.       *
*                                                                     *
*        The detail box frame will contain ...                        *
*                                                                     *
*          '**START***'  for job header separator calls               *
*          '**END*****'  for job trailer separator calls              *
*          '**CONT****'  for job continuation separator calls         *
*                                                                     *
*        propagated across the length of the top and bottom           *
*        lines of the detail box.                                     *
*                                                                     *
*   LINKAGE:                                                          *
*                                                                     *
*        Accessed via $CALL                                           *
*        Returned via $RETURN                                         *
*                                                                     *
*   REGISTER USAGE ( ENTRY/EXIT ):                                    *
*                                                                     *
*        REG          VALUE ON ENTRY         VALUE ON EXIT            *
*                                                                     *
*        R0-R3        N/A                      Unchanged              *
*        R4           Address of output        Unchanged              *
*                      buffer work space                              *
*        R5-R6        N/A                      Unchanged              *
*        R7           Parameter list address   Unchanged              *
*        R8-R10       N/A                      Unchanged              *
*        R11          HCT address              Unchanged              *
*        R12          N/A                      Unchanged              *
*        R13          PCE address              Unchanged              *
*        R14          Return address           Unchanged              *
*        R15          Entry address            Unchanged              *
*                                                                     *
*   RETURN CODES:                                                     *
*                                                                     *
*        None                                                         *
*                                                                     *
*   OTHER CONSIDERATIONS:                                             *
*                                                                     *
*        None                                                         *
*                                                                     *
***********************************************************************
         SPACE 2
         USING PUTFRAME,R12                 Establish
         USING SEPDSECT,R4                       addressabilities
         USING XPL,R7
         SPACE 1
PUTFRAME $SAVE ,                   Save the caller's registers
         SPACE 2
         LR    R12,R15             Set local base register
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*        Determine the type of call and branch to insert              *
*        frame characters in the detail box output line.              *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         TM    X001IND,X001JTLR    Is this a trailer call?     @R01P010
         BO    TRAILER                If yes, go to TRAILER
         SPACE 1
         TM    X001IND,X001JCNT    Else, continuation call?    @R01P010
         BO    CONTINUE               If yes, go to CONTINUE
         SPACE 1
*---------------------------------------------------------------------*
*        Insert frame for job header calls.                           *
*---------------------------------------------------------------------*
         SPACE 1
         MVC   BOXLINE(10),=CL10'**START***'   Insert 'START'
         SPACE 1
         B     FILLFRAM            Go propagate frame characters
         SPACE 1
*---------------------------------------------------------------------*
*        Insert frame for job continuation calls.                     *
*---------------------------------------------------------------------*
         SPACE 1
CONTINUE MVC   BOXLINE(10),=CL10'**CONT****'   Insert 'CONT'
         SPACE 1
         B     FILLFRAM            Go propagate frame characters
         SPACE 1
*---------------------------------------------------------------------*
*        Insert frame for job trailer calls.                          *
*---------------------------------------------------------------------*
         SPACE 1
TRAILER  MVC   BOXLINE(10),=CL10'**END*****'       Insert 'END'
         SPACE 1
*---------------------------------------------------------------------*
*        Propagate frame characters across the length of              *
*        the detail box output line.                                  *
*---------------------------------------------------------------------*
         SPACE 1
FILLFRAM MVC   BOXLINE+10(L'BOXLINE-10),BOXLINE    Fill in frame
         EJECT
***********************************************************************
*                                                                     *
*        Return to caller.                                            *
*                                                                     *
***********************************************************************
         SPACE 2
        $RETURN ,                  Return to caller
         SPACE 2
         LTORG ,                   Define literal origin
         SPACE 2
         DROP  R4,R7,R12           Drop addressabilities
         TITLE 'FMTDATE  -- Format date'
***********************************************************************
*                                                                     *
*        FMTDATE  - Convert the date to a readable format.            *
*                                                                     *
*   FUNCTION:                                                         *
*                                                                     *
*        The date is converted from a packed decimal format           *
*        0CYYDDDF to a readable format 'DD MMM YYYY' where ..    @R07AE
*                                                                     *
*          Packed Decimal Format:                                     *
*                                                                     *
*          C   represents number of centuries after 1900 A.D.         *
*         YY   represents the last two digits of the year             *
*        DDD   represents the JULIAN value for the date               *
*                                                                     *
*          Readable Format:                                           *
*                                                                     *
*         DD   represents the date                                    *
*        MMM   represents the three character abbreviation            *
*                 for the month                                       *
*       YYYY   represents the YEAR                               @R07AE
*                                                                     *
*   LINKAGE:                                                          *
*                                                                     *
*        Accessed via $CALL                                           *
*        Returned via $RETURN                                         *
*                                                                     *
*   REGISTER USAGE ( ENTRY/EXIT ):                                    *
*                                                                     *
*        REG          VALUE ON ENTRY         VALUE ON EXIT            *
*                                                                     *
*        R0-R3        N/A                      Unchanged              *
*        R4           Current buffer address   Unchanged              *
*        R5           Exit work area address   Unchanged              *
*        R6           Date in packed decimal   Unchanged              *
*                     format 0CYYDDDF                                 *
*        R7           Parameter list address   Unchanged              *
*        R8-R10       N/A                      Unchanged              *
*        R11          HCT address              Unchanged              *
*        R12          N/A                      Unchanged              *
*        R13          PCE address              Unchanged              *
*        R14          Return address           Unchanged              *
*        R15          Entry address            Unchanged              *
*                                                                     *
*   RETURN CODES:                                                     *
*                                                                     *
*        None                                                         *
*                                                                     *
*   OTHER CONSIDERATIONS:                                             *
*                                                                     *
*        None                                                         *
*                                                                     *
***********************************************************************
         SPACE 2
         USING FMTDATE,R12         Establish local addressability
         USING XPL,R7              Establish XPL addressability
         USING SEPDSECT,R4         Establish buffer addr'ty
         USING EX1WORK,R5          Establish exit work area addr'ty
         SPACE 1
FMTDATE $SAVE  ,                   Save the caller's registers
         SPACE 2
         LR    R12,R15             Set local base register
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*        Setup date conversion table.                                 *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         LTR   R6,R6               Is there a date to format?
         BZ    NODATE                No, go return to caller
         SPACE 1
         LA    R3,BOXINFO          Get addr of output area
         XC    EX1CVDWA,EX1CVDWA   Clear out conversion work area
         ST    R6,EX1CVDWA+4       Store date in conversion work area
         SPACE 1
         L     R2,=A(YEARTABL)     Copy date conversion table
         MVC   MONTHS,0(R2)          incase it needs to be updated
         SPACE 1
         TM    EX1CVDWA+5,X'01'    Adjust
         BO    EDITYEAR              table
         TM    EX1CVDWA+5,X'12'        on
         BM    EDITYEAR                  leap
         MVI   FEB,29                      years
         SPACE 1
*---------------------------------------------------------------------*
*                                                                     *
*        Convert year to readable format.                             *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
EDITYEAR MVC   8(3,R3),=X'F02120'    Get pattern                 @R07AE
         ED    8(3,R3),EX1CVDWA+5    Edit last two digit of yr   @R07AE
         MVI   6(R3),X'40'           Blank fill character        @R07AE
         CLI   EX1CVDWA+4,X'00'      Is this 20th Century yr?    @R07AE
         BNE   X121CENT              No, must be 21st Century    @R07AE
         MVC   7(2,R3),=CL2'19'      Year will be '19xx'         @R07AE
         B     XYRCNT                edit last 2 digits of yr    @R07AE
         SPACE 1                                                 @R07AE
X121CENT MVC   7(2,R3),=CL2'20'      Year will be '20xx'         @R07AE
         SPACE 1
XYRCNT   MVC   EX1CVDWA(6),$ZEROS  Clear all but JULIAN day      @R07AE
         SLR   R0,R0               Clear register
         CVB   R6,EX1CVDWA         Convert day to binary
         LA    R2,MONTHS-4         Get addr of date conversion table
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*        Convert day and month to readable format.                    *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
DATELOOP SLR   R6,R0               Convert
         LA    R2,4(,R2)             JULIAN day
         IC    R0,0(,R2)               value to
         CLR   R0,R6                     standard day
         BL    DATELOOP                     value
         SPACE 1
         CVD   R6,EX1CVDWA         Convert day to decimal value
         UNPK  0(2,R3),EX1CVDWA+6(2)   Place DAY (DD)
         OI    1(R3),X'F0'               into work area
         SPACE 1
         MVI   2(R3),C' '          Insert delimiter
         MVC   3(3,R3),1(R2)       Move EBCDIC month (MMM)
         SPACE 1
NODATE   DS    0H                  End of conversion
         EJECT
***********************************************************************
*                                                                     *
*        Return to caller.                                            *
*                                                                     *
***********************************************************************
         SPACE 2
        $RETURN ,                  Return to caller
         SPACE 2
         LTORG ,                   Define literal origin
         SPACE 2
         DROP  R4,R5,R7,R12        Drop addressabilities
         TITLE 'EX1RECV  -- First level recovery routine'
***********************************************************************
*                                                                     *
*        EX1RECV  - First level recovery for exit 1                   *
*                                                                     *
*                                                                     *
*   FUNCTION:                                                         *
*                                                                     *
*        This routine gets control if an abend occurs within          *
*        the sample exit.  The registers that are required for        *
*        retry processing are restored from the ERA ( Error           *
*        Recovery Area ).                                             *
*                                                                     *
*        Since the abend may have occurred while attempting           *
*        to return the exit 1 work area storage, an additional        *
*        $ESTAE recovery environment will be created to handle        *
*        an abend within this first level recovery routine.           *
*        Note that HASPTERM will pop the save areas until it          *
*        has found the save area level that issued the $ESTAE         *
*        give control to the recovery routine specified.              *
*                                                                     *
*                                                                     *
*   LINKAGE:                                                          *
*                                                                     *
*        Accessed via $CALL                                           *
*        Returned via $RETURN                                         *
*                                                                     *
*   REGISTER USAGE ( ENTRY/EXIT ):                                    *
*                                                                     *
*        REG          VALUE ON ENTRY         VALUE ON EXIT            *
*                                                                     *
*        R0           R0 at time of error      Unchanged              *
*        R1           ERA address              Unchanged              *
*        R2-R10       R2-R10 at time of error  Unchanged              *
*        R11          HCT address              Unchanged              *
*        R12          R12 at time of error     Unchanged              *
*        R13          PCE address              Unchanged              *
*        R14          Return address           Unchanged              *
*        R15          Entry address            Unchanged              *
*                                                                     *
*   RETURN CODES:                                                     *
*                                                                     *
*        None                                                         *
*                                                                     *
*   OTHER CONSIDERATIONS:                                             *
*                                                                     *
*        None                                                         *
*                                                                     *
***********************************************************************
         SPACE 2
         USING ERA,R8              Establish ERA addressability
         USING EX1RECV,R12         Establish local addressability
         SPACE 1
EX1RECV $SAVE  ,                   Save the caller's registers
         SPACE 2
         LR    R12,R15             Set local base register
         SPACE 1
         LR    R8,R1               Set ERA address
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*        Establish second level ESTAE recovery environment.           *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
        $ESTAE RECADDR==A(EX1REC2),NAME=EX1RECV,RETRY=ENDRECOV,        C
               BASE=(R12)          Establish ESTAE environment
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*        Check if PCEUSER0 has address of exit work area by           *
*        verifying that the eyecatcher is valid.  If the              *
*        eyecatcher is valid release the storage obtained             *
*        for work areas needed to produce a separator page            *
*        ( see PRODSEP routine ).  The addresses pointing to          *
*        these work areas were previously saved in the exit           *
*        work area.                                                   *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
         ICM   R4,B'1111',PCEUSER0     Get work area address
         BZ    ENDRECOV                Bypass RETWORK if no storage
         XC    PCEUSER0,PCEUSER0       Clear PCEUSER0 field
         SPACE 1
         CLC   0(4,R4),=CL4'EX1W'      Check if work area      @430P329C
                                          eyecatcher is valid  @430P329
         BNE   ENDRECOV            Bypass RETWORK if not valid @430P329
         SPACE 1
         LA    R4,4(,R4)           Add 4 for eyecatcher
         USING EX1WORK,R4          Establish EX1WORK addressability
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*        Release TU output area storage                               *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
         ICM   R1,B'1111',EX1TUADR     Load TU output area addr
         BZ    RELKEYL             Branch if no TU storage
         S     R1,$F4              Subtract 4 for eyecatcher
         SPACE 1
         XC    EX1TUADR,EX1TUADR       Set address to zero
         SPACE 1
        $RETWORK (R1)              Release the TU storage
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*        Release Key/TU pointer list storage area                     *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
RELKEYL  ICM   R1,B'1111',EX1KYLST     Load Key/TU ptr list addr
         BZ    RELSRWA             Branch if no Key/TU storage
         S     R1,$F4              Subtract 4 for eyecatcher
         SPACE 1
         XC    EX1KYLST,EX1KYLST       Set address to zero
         SPACE 1
        $RETWORK (R1)              Release the keylist storage
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*        Release SWBTUREQ RETRIEVE service work area storage          *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
RELSRWA  ICM   R1,B'1111',EX1SRWA      Load service work area addr
         BZ    ENDRECOV            Branch if no work storage
         S     R1,$F4              Subtract 4 for eyecatcher
         SPACE 1
         XC    EX1SRWA,EX1SRWA     Set address to zero
         SPACE 1
        $RETWORK (R1)              Release the working storage
         EJECT
***********************************************************************
*                                                                     *
*        Return to caller.                                            *
*                                                                     *
***********************************************************************
         SPACE 2
ENDRECOV $ESTAE CANCEL             Cancel 2nd level recovery environ
         SPACE 2
        $SETRP PERCOLATE           Percolate to callers recovery
         SPACE 2
        $RETURN ,                  Return to caller
         SPACE 2
         LTORG ,                   Define literal origin
         SPACE 2
         DROP  R4,R8,R12           Drop addressabilities
         TITLE 'EX1REC2  -- Second level recovery routine'
***********************************************************************
*                                                                     *
*        EX1REC2  - Second level recovery for sample exit 1           *
*                                                                     *
*                                                                     *
*   FUNCTION:                                                         *
*                                                                     *
*        This routine gets control if an abend occurs within          *
*        the first level recovery routine.                            *
*                                                                     *
*        If this is not the first time that this recovery             *
*        routine has been called, PERCOLATE to the first              *
*        level recovery routine ( EX1RECV ).  Otherwise,              *
*        resume processing at the retry label ENDRECOV in             *
*        EX1RECV.                                                     *
*                                                                     *
*                                                                     *
*   LINKAGE:                                                          *
*                                                                     *
*        Accessed via $CALL                                           *
*        Returned via $RETURN                                         *
*                                                                     *
*   REGISTER USAGE ( ENTRY/EXIT ):                                    *
*                                                                     *
*        REG          VALUE ON ENTRY         VALUE ON EXIT            *
*                                                                     *
*        R0           R0 at time of error      Unchanged              *
*        R1           ERA address              Unchanged              *
*        R2-R10       R2-R10 at time of error  Unchanged              *
*        R11          HCT address              Unchanged              *
*        R12          R12 at time of error     Unchanged              *
*        R13          PCE address              Unchanged              *
*        R14          Return address           Unchanged              *
*        R15          Entry address            Unchanged              *
*                                                                     *
*   RETURN CODES:                                                     *
*                                                                     *
*        None                                                         *
*                                                                     *
*   OTHER CONSIDERATIONS:                                             *
*                                                                     *
*        None                                                         *
*                                                                     *
***********************************************************************
         SPACE 2
         USING ERA,R8              Establish ERA addressability
         USING EX1REC2,R12         Establish local addressability
         SPACE 1
EX1REC2 $SAVE  ,                   Save the caller's registers
         SPACE 2
         LR    R12,R15             Set local base register
         SPACE 1
         LR    R8,R1               Set ERA address
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*        If this is not the first time through this ESTAE             *
*        environment, PERCOLATE to next ESTAE recovery level.         *
*        Otherwise, retry at resume point stored in PRE.              *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
         CLC   PCEUSER1,$ZEROS     Has ESTAE been recently entered?
         BNE   EX1PERK               Yes, percolate
         MVC   PCEUSER1,$F1        Indicate ESTAE entered
         SPACE 1
         L     R1,ERAPRE                    Get PRE address
         MVC   ERAREG12,PREBASE-PRE(R1)     Set local base
         L     R2,PRERESUM-PRE(,R1)         Get resume address
         SPACE 2
        $SETRP RECOVER,RESUME=(R2)          Set recovery address
         SPACE 2
         B     ENDREC2             Return to caller
         SPACE 2
EX1PERK $SETRP PERCOLATE          Percolate to callers recovery
         EJECT
***********************************************************************
*                                                                     *
*        Return to caller.                                            *
*                                                                     *
***********************************************************************
         SPACE 2
ENDREC2 $RETURN ,                 Return to caller
         SPACE 2
         DROP  R8,R12             Drop addressabilities
         SPACE 2
         LTORG ,
         TITLE 'PRINT/PUNCH Separator Exit -- Data Definitions'
***********************************************************************
*                                                                     *
*               DATA DEFINITIONS                                      *
*                                                                     *
***********************************************************************
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*        All labels that will appear in the separator page            *
*        detail box are listed below.                                 *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
LBJOBID  DC    CL(L'BOXDESC)'JOBID='
LBSEGID  DC    CL(L'BOXDESC)'SEGMENT ID='
LBJOBNAM DC    CL(L'BOXDESC)'JOB NAME='
LBUSERID DC    CL(L'BOXDESC)'USER ID='
LBSYSCL  DC    CL(L'BOXDESC)'SYSOUT CLASS='
LBOUTGRP DC    CL(L'BOXDESC)'OUTPUT GROUP='
LBTITLE  DC    CL(L'BOXDESC)'TITLE='
LBDEST   DC    CL(L'BOXDESC)'DESTINATION='
LBNAME   DC    CL(L'BOXDESC)'NAME='
LBROOM   DC    CL(L'BOXDESC)'BIN= '
LBBLDG   DC    CL(L'BOXDESC)'BUILDING='
LBDEPT   DC    CL(L'BOXDESC)'DEPARTMENT='
LBADDR   DC    CL(L'BOXDESC)'ADDRESS='
LBPRTIME DC    CL(L'BOXDESC)'PRINT TIME='
LBPRDATE DC    CL(L'BOXDESC)'PRINT DATE='
LBPRNAME DC    CL(L'BOXDESC)'PRINTER NAME='
LBSYSTEM DC    CL(L'BOXDESC)'SYSTEM='
         SPACE 1
TIMEMASK DC    X'21207A20207A2020'        TIME MASK
SEGMASK  DC    X'402020202020'            SEGMENT ID MASK
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*        JULIAN date to standard DAY and MONTH conversion table.      *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
YEARTABL DC    AL1(31),C'JAN',AL1(28),C'FEB'
         DC    AL1(31),C'MAR',AL1(30),C'APR'
         DC    AL1(31),C'MAY',AL1(30),C'JUN'
         DC    AL1(31),C'JUL',AL1(31),C'AUG'
         DC    AL1(30),C'SEP',AL1(31),C'OCT'
         DC    AL1(30),C'NOV',AL1(255),C'DEC'
         SPACE 1
MONTHS   EQU   $REGSAVE,12*4       Save area for copy of above table
FEB      EQU   MONTHS+4            Entry for FEBRUARY
         TITLE 'PRINT/PUNCH Separator Exit -- OUTPUT Key List Equates'
***********************************************************************
*                                                                     *
*        OUTPUT JCL Keys List equates                                 *
*                                                                     *
***********************************************************************
         SPACE 2
KYLSTTL  EQU   0*SJTRKLEN               Title Key
KYLSTNM  EQU   1*SJTRKLEN               Name Key
KYLSTRM  EQU   2*SJTRKLEN               Room Key
KYLSTBL  EQU   3*SJTRKLEN               Building Key
KYLSTDP  EQU   4*SJTRKLEN               Dept key
KYLSTAD  EQU   5*SJTRKLEN               Address Key
         TITLE 'PRINT/PUNCH Separator Exit -- Exit 1 Work Area DSECT'
***********************************************************************
*                                                                     *
*        EXIT 1 WORK AREA DSECT                                       *
*                                                                     *
***********************************************************************
         SPACE 2
EX1WORK  DSECT
         DS    0F
EX1SWBRC DS    XL2                 SWBTUREQ logical error return code
EX1PLPTR DS    A                   Address of SWBTUREQ parameter list  C
                                     (the SWBTUREQ service requires    C
                                      R1 to point to a word that       C
                                      points to the parameter list)
EX1SRWA  DS    F                   Address of SWBTUREQ service         C
                                     working storage
EX1SRWL  DS    H                   Length of SWBTUREQ service          C
                                     working storage
EX1KYLST DS    F                   Address of the Key List             C
                                     used for SWBTUREQ SERVICE
EX1TUADR DS    F                   Address of TU output area           C
                                     used for SWBTUREQ SERVICE
EX1TUSZ  DS    H                   Size of the TU output area          C
                                     used for SWBTUREQ SERVICE
EX1ADNUM DS    H                   Number of lines of ADDRESS ( OUTPUT C
                                                 JCL keyword )
EX1SEGWA DS    XL6                 Segment number work area
         DS    0D
EX1CVDWA DS    D                   'Convert to decimal' work area
EX1DEST  DS    0CL16               Destination conversion
         DS    D                        work area
         DS    D
         DS    0F
EX1RETPL DS    XL(SJTRLGTH)        SWBTUREQ RETRIEVE parameter list
EX1WKLEN EQU   *-EX1WORK           Length of EX1WORK DSECT
         TITLE 'PRINT/PUNCH Separator Exit -- Separator Page Detail BoxC
                DSECT'
***********************************************************************
*                                                                     *
*        SEPARATOR PAGE DETAIL BOX LINE DSECT                         *
*                                                                     *
***********************************************************************
         SPACE 2
SEPDSECT DSECT                     Separator page detail box DSECT
PAGELINE DS    0CL132
         SPACE 1
BOXLINE  DS    0CL80
         SPACE 1
*---------------------------------------------------------------------*
*                                  COL           DESCRIPTION          *
*---------------------------------------------------------------------*
         SPACE 1
BOXCOL1  DS    CL1                 1             Frame Character
         DS    CL1                 2             Blank
BOXDESC  DS    CL13                3-15          Line Description
         DS    CL2                 16-17         Blanks
BOXINFO  DS    CL60                18-77         Line Information
         DS    CL2                 78-79         Blanks
BOXCOL80 DS    CL1                 80            Frame Character
         SPACE 1
         DS    CL52                Unused section
         SPACE 2
*---------------------------------------------------------------------*
*                                  COL           DESCRIPTION          *
*---------------------------------------------------------------------*
         SPACE 2
         ORG   BOXINFO                    Job ID Area
BOXJOBID DS    CL(L'JCTJOBID)       18-25         Job id
         SPACE 1
         ORG   BOXINFO+22                 Segment Area
BOXSGLBL DS    C'SEGMENT ID:  '    40-52         Segment ID label
BOXSGINF DS    CL5                 53-57         Segment ID Number
         SPACE 1
         ORG   BOXINFO                    Job Name Area
BOXJNAME DS    CL(L'JCTJNAME)      18-25         Job name
         SPACE 1
         ORG   BOXINFO                    User ID Area
BOXCRUID DS    CL(L'JOECRUID)      18-25         User ID
         SPACE 1
         ORG   BOXINFO                    Sysout class Area
BOXSYSCL DS    CL(L'JOECURCL)      18            Sysout class
         EJECT
*---------------------------------------------------------------------*
*                                  COL           DESCRIPTION          *
*---------------------------------------------------------------------*
         SPACE 2
         ORG   BOXINFO                    Destination Area
BOXDEST  DS    CL(L'EX1DEST)       18-25         Destination
         SPACE 1
         ORG   BOXINFO                    Programmer name Area
BOXPNAME DS    CL(L'JCTPNAME)      18-25         Programmer name
         SPACE 1
         ORG   BOXINFO                    Programmer room Area
BOXROOMN DS    CL(L'JCTROOMN)      18-25         Programmer room
         SPACE 1
         ORG   BOXINFO                    Print Time Area
BOXPRTIM DS    C'HH:MM:SS'         18-25         Print Time
         SPACE 1
         ORG   BOXINFO                    Print Date Area
BOXPRDAT DS    C'DD MMM YY'        18-26         Print Date
         SPACE 1
         ORG   BOXINFO                    Printer Name Area
BOXPDEVN DS    CL(L'DCTDEVN)       18-25         Printer Name
         SPACE 1
         ORG   BOXINFO                    System Name Area
BOXSYSTM DS    CL(L'$SID)          18-21         System Name
         EJECT
***********************************************************************
*        END OF DECLARES                                              *
***********************************************************************
         SPACE 1
&J2SECTN &J2SECTT ,                Restore code section        @430P270
         SPACE 1
         LTORG ,                   DEFINE LITERAL ORIGIN
         SPACE 1
         DROP  ,                   Kill addressabilities
         TITLE 'PRINT/PUNCH Separator Exit -- Module End ($MODEND)'
        $MODEND
APARNUM  DC    CL8'NONE'           APAR NUMBER
PTFNUM   DC    CL8'NONE'           PTF NUMBER
         END   ,                   END OF STSCX01A
