         TITLE 'Y2KJTOG - CONVERT JULIAN DATE TO GREGORIAN DATE'
*                                                                     *
*   YY   YY   222   KK   KK      JJTTTTTTTT  OOOOO   GGGGG            *
*   YY   YY  2   2  KK  KK       JJ   TT    OO   OO GG   GG           *
*   YY   YY      2  KK KK        JJ   TT    OO   OO GG   GG           *
*    YY YY       2  KKKK         JJ   TT    OO   OO GG                *
*     YYY     222   KKKK         JJ   TT    OO   OO GG  GGG           *
*     YY     2      KK KK   JJ   JJ   TT    OO   OO GG   GG           *
*     YY     2      KK  KK  JJ   JJ   TT    OO   OO GG   GG           *
*     YY     22222  KK   KK  JJJJJ    TT     OOOOO   GGGGG            *
*                                                                     *
* ******************************************************************* *
* ******************************************************************* *
*    THIS SUBROUTINE RETURNS THE GREGORIAN DATE (MMDDYYYY) FOR        *
*    A GIVEN JULIAN DATE (YYYYDDD)                                    *
*                                                                     *
*    1.  VALID INPUT YEARS FOR THIS ROUTINE ARE THE YEARS 1601        *
*        THROUGH 3399 A. D. (INCLUSIVE)                               *
*                                                                     *
*    2.  VALID INPUT DAY VALUES FOR THIS ROUTINE ARE 001 THROUGH      *
*        365 (NON-LEAP YEARS) AND 001 THROUGH 366 (LEAP YEARS).       *
*                                                                     *
*    3.  THE DATE FORMAT MUST BE YYYYDDD, WHERE YYYY = THE 4 DIGIT    *
*        YEAR VALUE AND DDD = THE 3 DIGIT JULIAN DAY NUMBER VALUE.    *
*                                                                     *
*    5.  THREE FIELDS ARE PASSED AS PARAMETERS TO THE ROUTINE:        *
*        1)  A 7 BYTE FIELD CONTAINING THE JULIAN DATE (IN ZONED      *
*            DECIMAL FORMAT),                                         *
*        2)  A 1 BYTE ZONED-DECIMAL RETURN CODE, AND                  *
*        3)  AN 8 BYTE FIELD WHICH WILL RECEIVE THE CORRESPONDING     *
*            GREGORIAN DATE (IN ZONED-DECIMAL FORMAT).                *
*        THE ROUTINE WILL NOT ALTER THE DATE PASSED TO IT.            *
*                                                                     *
*        SUGGESTED CALLING SYNTAX FOR ALC CALLERS:                    *
*                                                                     *
*                      CALL  Y2KJTOG,(DATEJ)                          *
*                         . . .                                       *
*             DATEJ    DS    ZL7'1997158'                             *
*             RC       DS    ZL1'0'                                   *
*             DATEG    DS    ZL8'00000000'                            *
*                                                                     *
*        SUGGESTED CALLING SYNTAX FOR COBOL CALLERS:                  *
*                                                                     *
*             01  JTOG-PARAMETERS.                                    *
*                 02  DATEJ PIC 9(7) VALUE 1997158.                   *
*                 02  RC    PIC 9(1) VALUE 0.                         *
*                 02  DATEG PIC 9(8) VALUE 0.                         *
*             CALL 'Y2KJTOG' USING JTOG-PARAMETERS.                   *
*                                                                     *
*    6.  THE POSSIBLE VALUES FOR THE RETURN CODE FIELD ARE:           *
*                                                                     *
*        0 INDICATES SUCCESSFUL EXECUTION OF THE ROUTINE.             *
*                                                                     *
*        2 INDICATES INVALID DATA WAS FOUND IN THE DATE FIELD.        *
*        INVALID DATA ARE DETERMINED IF THE FIELD'S LOW ORDER         *
*        BYTE'S ZONE CONTAINS AN INVALID SIGN, (NOT ONE OF            *
*        X'C', X'D' OR X'F'), OR IF THE PRECEDING BYTES' ZONES        *
*        ARE OTHER THAN X'F', OR IF ANY BYTE'S LOW ORDER NIBBLE       *
*        CONTAINS A VALUE GREATER THAN X'9'.                          *
*                                                                     *
*        4 INDICATES THE VALUE OF THE YEAR SPECIFIED WAS NOT IN       *
*        THE RANGE SPECIFIED IN 1 (ABOVE).                            *
*                                                                     *
*        8 INDICATES THE VALUE OF THE DAY SPECIFIED WAS NOT IN        *
*        THE RANGE SPECIFIED IN 2 (ABOVE).                            *
*                                                                     *
*    7.  UPON SUCCESSFUL EXECUTION, THE 8 DIGIT GREGORIAN DATE        *
*        (MMDDYYYY) WILL BE PLACED IN THE 3RD PARAMETER FIELD (IN     *
*        ZONED DECIMAL FORMAT).  IF THE RETURN CODE IS A NON-ZERO     *
*        VALUE (UNSUCCESSFUL EXECUTION), THE 3RD PARAMETER WILL       *
*        CONTAIN ZEROS.                                               *
*                                                                     *
* ******************************************************************* *
* ******************************************************************* *
         EJECT
Y2KJTOG  CSECT
*         PRINT NOGEN
         EYEC  'CONVERT JULIAN (DAY OF YEAR) TO GREGORIAN'
*
         SAVE  (14,12)                 SAVE CALLER'S REGISTERS
*
         LR    R12,R15                 LOAD BASE WITH ENTRY ADDRESS
         USING Y2KJTOG,R12             ESTABLISH ADDRESSABILITY
*
         L     R11,0(,R1)              LOAD A(PARAMETERS)
         USING $PARMS,R11              ESTABLISH ADDRESSABILITY
*
         GETMAIN R,LV=$DYNLEN          GETMAIN DYNAMIC WORKAREA
         LR    R10,R1                  SAVE A(MEMORY)
*
         ST    R13,4(,R10)             STORE A(CALLER'S SAVE AREA)
         ST    R10,8(,R13)             BACK CHAIN A(MY SAVE AREA)
         LR    R13,R10                 SET A(MY SAVE AREA)
         USING $DYNMEM,R13             ESTABLISH ADDRESSABILITY
*
*    INSERT EYECATCHER MARKS AT BEGIN/END OF GETMAIN'D AREA.
*
         MVC   TOPFLAG,=C'Y2KJTOG GETMAIN BEGINS->'
         MVC   BOTFLAG,=C'<-Y2KJTOG GETMAIN ENDS'
*
*    INITIALIZE RETURN CODE AND OUTPUT DATE FIELD.
*
         MVI   PRC,C'0'                ASSUME GOOD RETURN
         MVC   PRC+1(8),PRC            NULL GREGORIAN DATE
*
*    TEST DATE FOR INVALID CHARACTER
*
         LA    R2,PDATEJ               ADDRESS PASSED DATE
         LA    R3,6                    LENGTH MINUS 1
P1VLOOP  CLI   0(R2),X'F0'             TEST LESS THAN C'0'
         BL    P1ERR1                  IF YES, DATE CONTAINS BAD DATA
         CLI   0(R2),X'F9'             TEST GREATER THAN C'9'
         BH    P1ERR1                  IF YES, DATE CONTAINS BAD DATA
         LA    R2,1(0,R2)              ADDRESS NEXT BYTE
         BCT   R3,P1VLOOP              CONTINUE TESTING
         IC    R3,0(,R2)               LOAD RIGHT-MOST CHARACTER
         LR    R4,R3                   COPY INTO R4
         N     R3,=F'15'               ISOLATE DIGIT PORTION
         CH    R3,=H'9'                TEST GREATER THAN 9
         BH    P1ERR1                  IF YES, DATE CONTAINS BAD DATA
         SRL   R4,4                    ISOLATE ZONE PORTION
         CH    R4,=H'12'               TEST FOR 'C' ZONE
         BE    P1OK1                   IF YES, DATE IS VALID NUMERIC
         CH    R4,=H'13'               TEST FOR 'D' ZONE
         BE    P1OK1                   IF YES, DATE IS VALID NUMERIC
         CH    R4,=H'15'               TEST FOR 'F' ZONE
         BE    P1OK1                   IF YES, DATE IS VALID NUMERIC
*
*    DATE CONTAINED AN INVALID CHARACTER
*
P1ERR1   MVI   PRC,C'2'                INDICATE BAD DATA
         B     RETURN                  AND RETURN TO CALLER
*
*    TEST YEAR FOR VALID RANGE
*
P1OK1    PACK  WX,PDATEJ(4)            PACK PASSED YEAR
         CP    WX,=P'1601'             IS YEAR PRIOR TO 1601?
         BL    P1ERR2                  IF YES, YEAR NOT VALID
         CP    WX,=P'3399'             IS YEAR AFTER 3399?
         BNH   P1OK2                   IF NOT, YEAR IS VALID
*
*    YEAR CONTAINED AN INVALID VALUE (OUT OF RANGE)
*
P1ERR2   MVI   PRC,C'4'                INDICATE BAD VALUE
         B     RETURN                  AND RETURN TO CALLER
*
*    CALL Y2KGTOA TO GET JANUARY 1 ASTRO NUMBER
*
P1OK2    MVC   DATEG(4),=C'0101'       JANUARY 1
         MVC   DATEG+4(4),PDATEJ       OF REQUESTED YEAR
         LA    R1,GTOAPARM             ADDRESS OF Y2KGTOA PARMS
         ST    R1,PLIST                STORE IN DYNAMIC AREA
         LA    R1,PLIST                ADDRESS TO PASS
         L     R15,=V(Y2KGTOA)         ADDRESS OF ROUTINE
         BALR  R14,R15                 CALL ROUTINE
*
*    ADJUST ASTRO NUMBER NUMBER FOR JULIAN DATE
*
         PACK  WX(4),ANUMG(7)          PACK IT
         SP    WX,=P'1'                ADJUST TO 12/31 OF PRIOR YEAR
         PACK  WY(2),PDATEJ+4(3)       PACK JULIAN DAY NUMBER
         AP    WX,WY                   ADD TO PRIOR YEAR'S ASTRO #
*
*    CALL Y2KATOG TO GET NEW GREGORIAN DATE
*
         UNPK  ANUMA(7),WX             UNPACK COMPUTED ASTRO #
         LA    R1,ATOGPARM             ADDRESS OF Y2KATOG PARMS
         ST    R1,PLIST                STORE IN DYNAMIC AREA
         LA    R1,PLIST                ADDRESS TO PASS
         L     R15,=V(Y2KATOG)         ADDRESS OF ROUTINE
         BALR  R14,R15                 CALL ROUTINE
*
*    IF YEAR IS DIFFERENT, DDD WAS INVALID FOR YEAR SPECIFIED
*
         CLC   PDATEJ(4),DATEA+4       COMPARE YEARS
         BE    MOVEOUT                 IF SAME, ALL OK
         MVI   PRC,C'8'                ELSE INDICATE ERROR
         B     RETURN
*
*    MOVE RETURNED DATE TO OUR CALLER'S RETURN AREA
*
MOVEOUT  MVC   PDATEG,DATEA            STORE OUTPUT FIELD
*
*    RESTORE REGISTERS, FREE STORAGE, AND RETURN
*
RETURN   LR    R1,R13                  LOAD ADDRESS OF GETMAINED AREA
         L     R13,4(,R13)             RELOAD PREVIOUS SAVE AREA
         LA    R0,$DYNLEN              LOAD LENGTH OF GETMAINED AREA
         FREEMAIN R,LV=(0),A=(1)       FREE GETMAINED AREA
         RETURN (14,12),RC=0           RETURN TO CALLER WITH RC=0
*
         LTORG
         EJECT
* ******************************************************************* *
* ***************************** EQUATES ***************************** *
* ******************************************************************* *
         YREGS
* ******************************************************************* *
         EJECT
* ******************************************************************* *
* ***************************** DSECTS ****************************** *
* ******************************************************************* *
$DYNMEM  DSECT                         DYNAMICALLY ALLOCATED MEMORY
SAVEAREA DS    18F                     REGISTER SAVE AREA
TOPFLAG  DS    CL24                    EYECATCHER
PLIST    DS    A                       PARMLIST ADDR FOR SUB CALLS
GTOAPARM DS    0H                      PARAMETERS FOR Y2KGTOA
DATEG    DS    CL8                       INPUT GREGORIAN DATE
RCG      DS    CL1                       RETURN CODE
ANUMG    DS    ZL7                       RETURNED ASTRO NUMBER
ATOGPARM DS    0H                      PARAMETERS FOR Y2KATOG
ANUMA    DS    ZL7                       INPUT ASTRO NUMBER
RCA      DS    CL1                       RETURN CODE
DATEA    DS    CL8                       RETURNED GREGORIAN DATE
WX       DS    PL4                     WORK FIELD
WY       DS    PL2                     WORK FIELD
BOTFLAG  DS    CL22                    EYECATCHER
*
$DYNLEN  EQU   (*-$DYNMEM)             LENGTH OF GETMAIN'D AREA
*
$PARMS   DSECT                         PARAMETERS FROM/TO CALLER
PDATEJ   DS    CL7                     JULIAN DATE INPUT
PRC      DS    CL1                     RETURN CODE (0/2/4/8)
PDATEG   DS    CL8                     GREGORIAN DATE OUTPUT
* ******************************************************************* *
         END
