         TITLE 'Y2KLEAP - DETERMINES LEAP YEAR STATUS'
* ******************************************************************* *
* ******************************************************************* *
*  THIS PROGRAM IS COPYRIGHTED BY:  MR. JAY MOSELEY, CCP              *
*                                   200 HEYWOOD AVE 1503              *
*                                   SPARTANBURG, SC 29307-1792        *
*  NO USE MAY BE MADE OF THE DESIGN, CODING STRUCTURE, OR ANY PART    *
*  THEREOF WITHOUT THE EXPRESS WRITTEN PERMISSION OF THE AUTHOR IN    *
*  ADVANCE.  (1996, 1997, 1998, 1999, 2000)                           *
*                                                                     *
*  PERMISSION HAS BEEN GRANTED BY THE AUTHOR, THAT THIS PROGRAM BE    *
*  DISTRIBUTED ON THE CBT TAPE BY WWW.CBTTAPE.ORG.                    *
* ******************************************************************* *
* ******************************************************************* *
*                                                                     *
*   YY   YY   222   KK   KK LL      EEEEEE     A    PPPPPP            *
*   YY   YY  2   2  KK  KK  LL      EE        AAA   PP   PP           *
*   YY   YY      2  KK KK   LL      EE       AA AA  PP   PP           *
*    YY YY       2  KKKK    LL      EEEE    AA   AA PP   PP           *
*     YYY     222   KKKK    LL      EE      AA   AA PPPPPP            *
*     YY     2      KK KK   LL      EE      AAAAAAA PP                *
*     YY     2      KK  KK  LL      EE      AA   AA PP                *
*     YY     22222  KK   KK LLLLLLL EEEEEEE AA   AA PP                *
*                                                                     *
* ******************************************************************* *
* ******************************************************************* *
*    THIS SUBROUTINE DETERMINES GREGORIAN LEAP YEARS                  *
*                                                                     *
*    1.  VALID INPUT YEARS FOR THIS ROUTINE ARE THE YEARS 1601        *
*        THROUGH 3399 A. D. (INCLUSIVE)                               *
*                                                                     *
*    2.  THREE FIELDS ARE PASSED AS PARAMETERS TO THE ROUTINE:        *
*        1)  A 4 BYTE FIELD CONTAINING THE YEAR TO BE TESTED          *
*            (IN ZONED-DECIMAL FORMAT),                               *
*        2)  A 1 BYTE ZONED-DECIMAL RETURN CODE, AND                  *
*        3)  A 1 BYTE LEAP YEAR INDICATOR (IN ZONED-DECIMAL FORMAT).  *
*        THE ROUTINE WILL NOT ALTER THE YEAR PASSED TO IT.            *
*                                                                     *
*        SUGGESTED CALLING SYNTAX FOR ALC CALLERS:                    *
*                                                                     *
*                      CALL  Y2KLEAP,(YEAR)                           *
*                         . . .                                       *
*             YEAR     DS    ZL4'1997'                                *
*             RC       DS    ZL1'0'                                   *
*             LEAP     DS    ZL1'0'                                   *
*                                                                     *
*        SUGGESTED CALLING SYNTAX FOR COBOL CALLERS:                  *
*                                                                     *
*             01  LEAP-PARAMETERS.                                    *
*                 02  YEAR PIC 9(4) VALUE 1997.                       *
*                 02  RC   PIC 9(1) VALUE 0.                          *
*                 02  LEAP PIC 9(1) VALUE 0.                          *
*             CALL 'Y2KLEAP' USING LEAP-PARAMETERS.                   *
*                                                                     *
*    3.  THE POSSIBLE VALUES FOR THE RETURN CODE FIELD ARE:           *
*                                                                     *
*        0 INDICATES SUCCESSFUL EXECUTION OF THE ROUTINE.             *
*                                                                     *
*        2 INDICATES INVALID DATA WAS FOUND IN THE YEAR FIELD.        *
*        INVALID DATA ARE DETERMINED IF THE FIELD'S LOW ORDER         *
*        BYTE'S ZONE CONTAINS AN INVALID SIGN, (NOT ONE OF            *
*        X'C', X'A', X'E', OR X'F'), OR IF THE PRECEDING BYTES'       *
*        ZONES ARE OTHER THAN X'F', OR IF ANY BYTE'S LOW ORDER        *
*        NIBBLE CONTAINS A VALUE GREATER THAN X'9'.                   *
*                                                                     *
*        4 INDICATES THE VALUE OF THE YEAR SPECIFIED WAS NOT IN       *
*        THE RANGE SPECIFIED IN 1 (ABOVE).                            *
*                                                                     *
*    4.  UPON SUCCESSFUL EXECUTION, THE LEAP INDICATOR FIELD WILL     *
*        CONTAIN A VALUE OF 1 IF THE YEAR IS A LEAP YEAR.  A VALUE    *
*        OF 0 INDICATES NO LEAP YEAR.  IF THE RETURN CODE IS A        *
*        NON-ZERO VALUE (UNSUCCESSFUL EXECUTION), THE LEAP YEAR       *
*        INDICATOR WILL CONTAIN THE VALUE OF 9 TO PROTECT AGAINST     *
*        UNINTENTIONAL USE OF AN INVALID RESULT.                      *
* ------------------------------------------------------------------- *
* CHANGE LOG:                                                         *
* 0.1 04/17/2008 - CHANGED METHOD OF DETERMINING LEAP YEAR AT THE     *
*                  SUGGESTION OF ANGEL LUIS DOMINGUEZ.  TESTS ARE     *
*                  NOW DONE ON BINARY FORM OF YEAR, ELIMINATING THE   *
*                  USE OF PACKED DECIMAL INSTRUCTIONS.  TESTS ON MY   *
*                  EMULATED HARDWARE SHOW A SAVINGS OF 3.13 SECONDS   *
*                  ON 500,000 ITERATIONS.                             *
* ******************************************************************* *
* ******************************************************************* *
         EJECT
Y2KLEAP  CSECT
         PRINT NOGEN
         EYEC  'RETURN LEAP YEAR STATUS FOR PASSED YEAR'
*
         SAVE  (14,12)                 SAVE CALLER'S REGISTERS
*
         LR    R12,R15                 LOAD BASE WITH ENTRY ADDRESS
         USING Y2KLEAP,R12             ESTABLISH ADDRESSABILITY
*
         L     R11,0(,R1)              LOAD A(PARAMETERS)
         USING $PARMS,R11              ESTABLISH ADDRESSABILITY
*
         GETMAIN R,LV=$DYNLEN          GETMAIN DYNAMIC WORKAREA
         LR    R10,R1                  SAVE A(MEMORY)
*
         ST    R13,4(,R10)             STORE A(CALLER'S SAVE AREA)
         ST    R10,8(,R13)             BACK CHAIN A(MY SAVE AREA)
         LR    R13,R10                 SET A(MY SAVE AREA)
         USING $DYNMEM,R13             ESTABLISH ADDRESSABILITY
*
*    INSERT EYECATCHER MARKS AT BEGIN/END OF GETMAIN'D AREA.
*
         MVC   TOPFLAG,=C'Y2KLEAP GETMAIN BEGINS->'
         MVC   BOTFLAG,=C'<-Y2KLEAP GETMAIN ENDS'
*
*    INITIALIZE RETURN CODE AND LEAP YEAR STATUS PARAMETER FIELDS.
*
         MVI   PRC,C'0'                ASSUME GOOD RETURN
         MVI   PLEAP,C'9'              UNDETERMINED LEAP STATUS
*
*    TEST YEAR FOR INVALID CHARACTER
*
         LA    R2,PYEAR                ADDRESS PASSED YEAR
         LA    R3,3                    LENGTH MINUS 1
P1VLOOP  CLI   0(R2),X'F0'             TEST LESS THAN C'0'
         BL    P1ERR1                  IF YES, YEAR CONTAINS BAD DATA
         CLI   0(R2),X'F9'             TEST GREATER THAN C'9'
         BH    P1ERR1                  IF YES, YEAR CONTAINS BAD DATA
         LA    R2,1(0,R2)              ADDRESS NEXT BYTE
         BCT   R3,P1VLOOP              CONTINUE TESTING
         IC    R3,0(,R2)               LOAD RIGHT-MOST CHARACTER
         LR    R4,R3                   COPY INTO R4
         N     R3,=F'15'               ISOLATE DIGIT PORTION
         CH    R3,=H'9'                TEST GREATER THAN 9
         BH    P1ERR1                  IF YES, YEAR CONTAINS BAD DATA
         SRL   R4,4                    ISOLATE ZONE PORTION
         CH    R4,=H'12'               TEST FOR 'C' ZONE
         BE    P1OK1                   IF YES, YEAR IS VALID NUMERIC
         CH    R4,=H'13'               TEST FOR 'D' ZONE
         BE    P1OK1                   IF YES, YEAR IS VALID NUMERIC
         CH    R4,=H'15'               TEST FOR 'F' ZONE
         BE    P1OK1                   IF YES, YEAR IS VALID NUMERIC
*
*    YEAR CONTAINED AN INVALID CHARACTER
*
P1ERR1   MVI   PRC,C'2'                INDICATE BAD DATA
         B     RETURN                  AND RETURN TO CALLER
*
*    TEST YEAR FOR VALID RANGE
*
P1OK1    PACK  P3(3),PYEAR             PACK PASSED YEAR
         CP    P3(3),=P'1601'          IS YEAR PRIOR TO 1601?
         BL    P1ERR2                  IF YES, YEAR NOT VALID
         CP    P3(3),=P'3399'          IS YEAR AFTER 3399?
         BNH   P1OK                    IF NOT, YEAR IS VALID
*
*    YEAR CONTAINED AN INVALID VALUE (OUT OF RANGE)
*
P1ERR2   MVI   PRC,C'4'                INDICATE BAD VALUE
         B     RETURN                  AND RETURN TO CALLER
*
*    DETERMINE LEAP YEAR STATUS
*
P1OK     MVI   PLEAP,C'0'              ASSUME NOT A LEAP YEAR
         MVO   B3(3),P3(3)             DROP SIGN                    0.1
         TM    B3+1,X'01'              ODD YEAR?                    0.1
         BO    RETURN                  IF YES, IT IS *NOT* LEAP YEAR0.1
         TM    B3+1,X'12'              EVENLY DIVISIBLE BY 4?       0.1
         BM    RETURN                  IF NO, IT IS *NOT* LEAP YEAR 0.1
         CLI   B3+1,X'00'              DOES YEAR END IN 00?         0.1
         BNE   ISLEAP                  IF YES, IT IS A LEAP YEAR    0.1
         TM    B3,X'01'                IS CENTURY ODD?              0.1
         BO    RETURN                  IF YES, IT IS *NOT* LEAP YEAR0.1
         TM    B3,X'12'                EVENLY DIVISIBLE BY 400?     0.1
         BM    RETURN                  IF NO, IT IS *NOT* LEAP YEAR 0.1
*                                      ELSE IT IS A LEAP YEAR       0.1
*
*    TEST FOR LEAP YEAR SUCCEEDED, RESET LEAP INDICATOR.
*
ISLEAP   MVI   PLEAP,C'1'              INDICATE LEAP YEAR
*
*    RESTORE REGISTERS, FREE STORAGE, AND RETURN
*
RETURN   LR    R1,R13                  LOAD ADDRESS OF GETMAINED AREA
         L     R13,4(,R13)             RELOAD PREVIOUS SAVE AREA
         LA    R0,$DYNLEN              LOAD LENGTH OF GETMAINED AREA
         FREEMAIN R,LV=(0),A=(1)       FREE GETMAINED AREA
         RETURN (14,12),RC=0           RETURN TO CALLER WITH RC=0
*
         LTORG
         EJECT
* ******************************************************************* *
* ***************************** EQUATES ***************************** *
* ******************************************************************* *
         YREGS
* ******************************************************************* *
         EJECT
* ******************************************************************* *
* ***************************** DSECTS ****************************** *
* ******************************************************************* *
$DYNMEM  DSECT                         DYNAMICALLY ALLOCATED MEMORY
SAVEAREA DS    18F                     REGISTER SAVE AREA
TOPFLAG  DS    CL24                    EYECATCHER
P3       DS    PL3                     WORK AREA FOR PACKED YEAR
B3       DS    XL3                     WORK AREA FOR BINARY YEAR    0.1
BOTFLAG  DS    CL22                    EYECATCHER
*
$DYNLEN  EQU   (*-$DYNMEM)             LENGTH OF GETMAIN'D AREA
*
$PARMS   DSECT                         PARAMETERS FROM/TO CALLER
PYEAR    DS    CL4                     YEAR TO BE TESTED
PRC      DS    CL1                     RETURN CODE (0/2/4)
PLEAP    DS    CL1                     LEAP YEAR STATUS (0/1/9)
* ******************************************************************* *
         END
