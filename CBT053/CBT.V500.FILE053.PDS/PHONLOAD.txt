         TITLE 'CREATE PHONE TABLE FROM SEQUENTIAL FILE'
*
*        REGISTER USAGE:
*              R0
*              R1   STANDARD PARAMETER PASSING
*              R2   HIGHEST RETURN CODE
*              R3
*              R4
*              R5
*              R6
*              R7
*              R8
*              R9
*              R10  RETURN ADDRESS WITHIN PROGRAM
*              R11
*              R12  PROGRAM BASE 1
*              R13  SAVE AREA POINTER
*              R14  RETURN ADDRESS
*              R15
PHONLOAD CSECT
         SAVE  (14,12),,PHONLOAD_CREATE_PHONE_TABLE_FROM_SEQUENTIAL_FILX
               E_&SYSDATE_&SYSTIME
         LR    R12,R15                ESTABLISH R12 AS BASE REG
         USING PHONLOAD,R12           TELL ASSEMBLER
         ST    R13,SAVEAREA+4         SAVE OS SAVE AREA ADDRESS
         LR    R6,R13                 SAVE IT IN R6
         LA    R13,SAVEAREA           POINT R13 AT OUR SAVE AREA
         ST    R13,8(R6)              STORE OUR SAVEAREA ADDR IN OS
         SR    R2,R2                  ZERO R2
         SR    R15,R15                ZERO R15
         ZAP   COUNT,=P'0'            ZERO COUNT OF I271C RECORDS
* END OF PROLOG                       SAVE AREA
*        PRINT NOGEN
         OPEN  (SYSOUT,(OUTPUT),I271CFIL,(INPUT))
         GET   I271CFIL,I271CREC      GET I271C RECORD
         ISPCALL ('CONTROL','NONDISPL','ENTER'),WKAREA=PARAMS,         X
               EPADDR=ISPADDR
         ISPCALL ('CONTROL','ERRORS','RETURN')
         ISPCALL ('VDEFINE',VARLIST1,PLANT,'CHAR',4)
         ISPCALL ('VDEFINE',VARLIST2,FIRSTAID,'CHAR',1)
         ISPCALL ('VDEFINE',VARLIST3,EXTENS,'CHAR',4)
         ISPCALL ('VDEFINE',VARLIST4,PAGENUM,'CHAR',4)
         ISPCALL ('VDEFINE',VARLIST5,NAME,'CHAR',40)
         ISPCALL ('VDEFINE',VARLIST6,EXCLUDE,'CHAR',1)
         ISPCALL ('VDEFINE',VARLIST7,BUDGDEPT,'CHAR',4)
         ISPCALL ('VDEFINE',VARLIST8,SUPFLAG,'CHAR',1)
         ISPCALL ('VDEFINE',VARLIST9,SORTORD,'CHAR',1)
*        ISPCALL ('VDEFINE',VARLISTA,TBLNAMES,'CHAR',69)
         ISPCALL ('VDEFINE',VARLISTB,ZERRMSG,'CHAR',8)
         ISPCALL ('VDEFINE',VARLISTC,ZERRSM,'CHAR',24)
         ISPCALL ('VDEFINE',VARLISTD,ZERRLM,'CHAR',80)
         ISPCALL ('TBCREATE','PHONETAB',' ','(PLANT,FIRSTAID,EXTENS,PAGX
               ENUM,NAME,EXCLUDE,BUDGDEPT,SUPFLAG)','WRITE','REPLACE','X
               PHNTLIB')
         BAL   R10,CHECKRC            CHECK FOR HIGHER RETURN CODE
         B     CKCREATE(R15)
CKCREATE EQU   *                      ERROR CREATING PHONE TABLE?
         B     CREATEOK               00
         B     DUPREPLC               04 DUPLICATE TABLE REPLACED
         B     NOREPLAC               08 TABLE NOT REPLACED
         B     ERRORA                 12 TABLE IN USE; ENQ FAILED
         B     ERRORA                 16 LIBRARY NOT ALLOCATED WITH
*                                        WRITE
         B     ERRORA                 20 SEVERE ERROR
DUPREPLC EQU   *
         PUT   SYSOUT,=CL132'EXISTING PHONE TABLE WAS REPLACED'
CREATEOK EQU   *                      NO ERRORS FOUND IN CREATE
         ISPCALL ('TBSORT','PHONETAB','(NAME,C,A,EXTENS,C,A)')
         BAL   R10,CHECKRC            CHECK FOR HIGHER RETURN CODE
         B     CKSORT(R15)
CKSORT   EQU   *                      ERROR SORTING TABLE?
         B     NEXTREC                00
         B     NEXTREC                04
         B     NEXTREC                08
         B     ERRORA                 12 TABLE IS NOT OPEN
         B     ERRORB                 16 NUMERIC CONVERT ERROR
         B     ERRORA                 20 SEVERE ERROR
NEXTREC  EQU   *
         ISPCALL ('TBADD','PHONETAB',' ','ORDER') ADD TABLE RECORD
         BAL   R10,CHECKRC            CHECK FOR HIGHER RETURN CODE
         B     CKTBADD(R15)
CKTBADD  EQU   *
         B     ADDOK                  00
         B     ADDOK                  04
         B     DUPREC                 08 ROW WITH KEY ALREADY EXISTS
         B     ERRORA                 12 TABLE IS NOT OPEN
         B     ADDOK                  16
         B     ERRORB                 20 SEVERE ERROR
DUPREC   EQU   *
         PUT   SYSOUT,=CL132'DUPLICATE RECORD:'
         PUT   SYSOUT,I271CREC
ADDOK    EQU   *
         AP    COUNT,=P'1'            ADD 1 TO COUNT OF I271C RECORDS
         MVC   I271CREC,I271CREC-1    CLEAR INPUT RECORD
         GET   I271CFIL,I271CREC      GET
         BAL   R10,CHECKRC            CHECK FOR HIGHER RETURN CODE
         B     NEXTREC
***********************************************************************
CHECKRC  EQU   *                      SAVE HIGHEST RETURN CODE
         CR    R2,R15                 COMPARE R15 TO SAVED RC
         BNLR  R10                    BRANCH IF R2 NOT < R15
         LR    R2,R15                 SAVE HIGHER RETURN CODE
         BR    R10                    RETURN
***********************************************************************
NOREPLAC EQU   *
         PUT   SYSOUT,=CL132'PHONE TABLE ALREADY EXISTS, REPLACE NOT SPX
               ECIFIED'
         PUT   SYSOUT,ERRMSG
         B     CLOSFILE
ERRORA   EQU   *
         PUT   SYSOUT,ERRMSG
         B     CLOSFILE
ERRORB   EQU   *
         PUT   SYSOUT,ERRMSG
         B     ENDTABLE
EOFRTN   EQU   *
         CP    COUNT,=P'0'
         BNE   NOERROR
         PUT   SYSOUT,=CL132'I271C FILE IS EMPTY'
         B     CLOSFILE
NOERROR  EQU   *
         MVI   I271CREC,C'Â¢'
         MVC   I271CREC+1(L'I271CREC-1),I271CREC
         ISPCALL ('TBADD','PHONETAB','SORTORD','ORDER') ADD SORT RECORD
         BAL   R10,CHECKRC            CHECK FOR HIGHER RETURN CODE
         B     CKTBADD2(R15)
CKTBADD2 EQU   *
         B     OK                     00
         B     OK                     04
         B     SRTEXSTS               08 ROW WITH KEY ALREADY EXISTS
         B     ERRORA                 12 TABLE IS NOT OPEN
         B     OK                     16
         B     ERRORB                 20 SEVERE ERROR
SRTEXSTS EQU   *
         PUT   SYSOUT,=CL132'SORTING RECORD ALREADY EXISTS'
         BAL   R10,CHECKRC            CHECK FOR HIGHER RETURN CODE
         B     ENDTABLE
OK       EQU   *
         PUT   SYSOUT,=CL132'END OF PHONE TABLE - NO ERRORS'
         ED    FORMAT,COUNT
         PUT   SYSOUT,COUNTMSG
         B     CLOSETBL
COUNT    DS    P'12345'               COUNT OF I271C RECORDS
COUNTMSG DS    0CL132
FORMAT   DC    X'4020206B202021'
         DC    C' RECORDS READ FROM THE I271C FILE'
         DC    CL91' '
         CNOP  0,4                    ALIGNMENT
ENDTABLE EQU   *
         ISPCALL ('TBEND','PHONETAB')
         BAL   R10,CHECKRC            CHECK FOR HIGHER RETURN CODE
         B     CLOSFILE
CLOSETBL EQU   *
         ISPCALL ('TBCLOSE','PHONETAB',' ',' ',' ','PHNTLIB')
         BAL   R10,CHECKRC            CHECK FOR HIGHER RETURN CODE
CLOSFILE EQU   *
         ST    R2,REG2
         ST    R15,REG15
         PUT   SYSOUT,RCMSG
         CLOSE   (SYSOUT,,I271CFIL)
         BAL   R10,CHECKRC            CHECK FOR HIGHER RETURN CODE
         ISPCALL ('VRESET')
         B     RETURN
* BEGINNING OF EPILOG
RETURN   EQU   *
         L     R13,SAVEAREA+4         LOAD OS SAVE AREA ADDR TO R13
         LR    R15,R2                 MOVE HIGHEST RETURN CODE TO R15
         RETURN (14,12),RC=(15)       RETURN TO OS, RC IN REG 15
RCMSG    DS    0CL132
         DC    C'REG 2 = '
REG2     DS    F
         DC    C' REG 15 = '
REG15    DS    F
         DC    CL132' '
* END OF EPILOG
ISPADDR  DC    A(0)                   STORAGE FOR EP ADDRESS OF ISPLINK
VARLIST1 DC    C'(PLANT)'
VARLIST2 DC    C'(FIRSTAID)'
VARLIST3 DC    C'(EXTENS)'
VARLIST4 DC    C'(PAGENUM)'
VARLIST5 DC    C'(NAME)'
VARLIST6 DC    C'(EXCLUDE)'
VARLIST7 DC    C'(BUDGDEPT)'
VARLIST8 DC    C'(SUPFLAG )'
VARLIST9 DC    C'(SORTORD )'
VARLISTB DC    C'(ZERRMSG )'
VARLISTC DC    C'(ZERRSM  )'
VARLISTD DC    C'(ZERRLM  )'
*VARLISTA DC  C'(PLANT,FIRSTAID,EXTENS,PAGENUM,NAME,EXCLUDE,BUDGDEPT,SX
*              UPFLAG,SORTORD)'
*TBLNAMES DC    CL69' '
         CNOP  0,4                    ALIGNMENT
SAVEAREA DC    18F'0'                 OUR SAVE AREA
PARAMS   DC    10A(0)                 PARAMETER WORKAREA
         DC    CL01' '
I271CREC DS    0CL80
PLANT    DC    CL04' '                BUILDING ABBR/PLANT NUMBER
         DC    CL01' '
FIRSTAID DC    CL01' '                FIRST AID FLAG
         DC    CL01' '
EXTENS   DC    CL04' '                EXTENSION NUMBER
         DC    CL02' '
PAGENUM  DC    CL04' '                PAGING NUMBER
         DC    CL02' '
NAME     DC    CL40' '                NAME/ROOM
EXCLUDE  DC    CL01' '                MIS035/MIS036 EXCLUSION FLAG
BUDGDEPT DC    CL04' '                BUDGET DEPARTMENT
SUPFLAG  DC    CL01' '                MIS077 SUPPRESSION FLAG
         DC    CL15' '
SORTORD  DC    CL01'N'
ERRMSG   DS    0CL132
ZERRMSG  DC    CL08' '
         DC    CL02' '
ZERRSM   DC    CL24' '
         DC    CL02' '
ZERRLM   DC    CL80' '
         DC    CL16' '
I271CFIL DCB   DDNAME=I271C,DSORG=PS,MACRF=(GM),RECFM=FB,LRECL=80,     X
               BLKSIZE=6160,EODAD=EOFRTN
SYSOUT   DCB   DDNAME=SYSOUT,DSORG=PS,MACRF=(PM),RECFM=FB,LRECL=132,   X
               BLKSIZE=6204
***********************************************************************
         EQUATE                       REGISTER EQUATES
         END   PHONLOAD
