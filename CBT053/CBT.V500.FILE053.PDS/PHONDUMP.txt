         TITLE 'CREATE SEQUENTIAL FILE FROM PHONE TABLE'
*
*        REGISTER USAGE:
*              R0
*              R1   STANDARD PARAMETER PASSING
*              R2   HIGHEST RETURN CODE
*              R3
*              R4
*              R5
*              R6
*              R7
*              R8
*              R9
*              R10  RETURN ADDRESS WITHIN PROGRAM
*              R11
*              R12  PROGRAM BASE 1
*              R13  SAVE AREA POINTER
*              R14  RETURN ADDRESS
*              R15
PHONDUMP CSECT
         SAVE  (14,12),,PHONDUMP_CREATE_SEQUENTIAL_FILE_FROM_PHONE_TABLX
               E_&SYSDATE_&SYSTIME
         LR    R12,R15                ESTABLISH R12 AS BASE REG
         USING PHONDUMP,R12           TELL ASSEMBLER
         ST    R13,SAVEAREA+4         SAVE OS SAVE AREA ADDRESS
         LR    R6,R13                 SAVE IT IN R6
         LA    R13,SAVEAREA           POINT R13 AT OUR SAVE AREA
         ST    R13,8(R6)              STORE OUR SAVEAREA ADDR IN OS
         SR    R2,R2                  ZERO R2
         SR    R15,R15                ZERO R15
         ZAP   COUNT,=P'0'            ZERO COUNT OF O271C RECORDS
* END OF PROLOG                       SAVE AREA
*        PRINT NOGEN
         OPEN  (SYSOUT,(OUTPUT),O271CFIL,(OUTPUT))
         BAL   R10,CHECKRC            CHECK FOR HIGHER RETURN CODE
         ISPCALL ('CONTROL','NONDISPL','ENTER'),WKAREA=PARAMS,         X
               EPADDR=ISPADDR
         ISPCALL ('CONTROL','ERRORS','RETURN')
         ISPCALL ('VDEFINE',VARLIST1,PLANT,'CHAR',4)
         ISPCALL ('VDEFINE',VARLIST2,FIRSTAID,'CHAR',1)
         ISPCALL ('VDEFINE',VARLIST3,EXTENS,'CHAR',4)
         ISPCALL ('VDEFINE',VARLIST4,PAGENUM,'CHAR',4)
         ISPCALL ('VDEFINE',VARLIST5,NAME,'CHAR',40)
         ISPCALL ('VDEFINE',VARLIST6,EXCLUDE,'CHAR',1)
         ISPCALL ('VDEFINE',VARLIST7,BUDGDEPT,'CHAR',4)
         ISPCALL ('VDEFINE',VARLIST8,SUPFLAG,'CHAR',1)
         ISPCALL ('VDEFINE',VARLISTA,ZERRMSG,'CHAR',8)
         ISPCALL ('VDEFINE',VARLISTB,ZERRSM,'CHAR',24)
         ISPCALL ('VDEFINE',VARLISTC,ZERRLM,'CHAR',80)
         ISPCALL ('TBOPEN','PHONETAB','NOWRITE','PHNTLIB')
         BAL   R10,CHECKRC            CHECK FOR HIGHER RETURN CODE
         B     CKOPEN(R15)
CKOPEN   EQU   *                      ERROR OPENING PHONE TABLE?
         B     OPENOK                 00
         B     OPENOK                 04
         B     NOTFOUND               08 TABLE DOES NOT EXIST
         B     ERROR1                 12 TABLE IN USE; ENQ FAILED
         B     ERROR1                 16 LIBRARY NOT ALLOCATED
         B     ERROR1                 20 SEVERE ERROR
OPENOK   EQU   *                      NO ERRORS FOUND IN OPEN
         ISPCALL ('TBTOP','PHONETAB') POSITION TO TOP OF TABLE
         BAL   R10,CHECKRC            CHECK FOR HIGHER RETURN CODE
         B     CKTBTOP(R15)
CKTBTOP  EQU   *                      ERROR POSITIONING TO TOP?
         B     TBTOPOK                00
         B     TBTOPOK                04
         B     TBTOPOK                08
         B     ERROR1                 12 TABLE IS NOT OPEN
         B     TBTOPOK                16
         B     ERROR2                 20 SEVERE ERROR
TBTOPOK  EQU   *
         ISPCALL ('TBSKIP','PHONETAB') POSITION TO NEXT TABLE RECORD
         BAL   R10,CHECKRC            CHECK FOR HIGHER RETURN CODE
         B     CKSKIP1(R15)
CKSKIP1  EQU   *                      ERROR POSITIONING TO NEXT RECORD?
         B     NEXTREC                00
         B     NEXTREC                04
         B     EMPTY                  08 END OF TABLE
         B     ERROR1                 12 TABLE NOT OPEN
         B     ERROR2                 16 DATA TRUNCATION OR
*                                        INSUFFICIENT SPACE PROVIDED
*                                        FOR ALL EXTENSION VAR NAMES
         B     ERROR2                 20 SEVERE ERROR
NEXTREC  EQU   *
         MVC   O271CREC,O271CREC-1    CLEAR OUTPUT RECORD
         ISPCALL ('TBGET','PHONETAB') ACCESS TABLE RECORD
         BAL   R10,CHECKRC            CHECK FOR HIGHER RETURN CODE
         B     CKGET(R15)
CKGET    EQU   *                      ERROR ACCESSING TABLE RECORD?
         B     WRITE                  00
         B     WRITE                  04
         B     WRITE                  08 CRP AT TOP
         B     ERROR1                 12 TABLE NOT OPEN
         B     ERROR2                 16 DATA TRUNCATION OR
*                                        INSUFFICIENT SPACE PROVIDED
*                                        FOR ALL EXTENSION VAR NAMES
         B     ERROR2                 20 SEVERE ERROR
WRITE    EQU   *
         CLC   EXTENS,=C'¢¢¢¢'
         BE    SKIP
         PUT   O271CFIL,O271CREC      WRITE O271C RECORD
         BAL   R10,CHECKRC            CHECK FOR HIGHER RETURN CODE
         AP    COUNT,=P'1'            ADD 1 TO COUNT OF O271C RECORDS
SKIP     EQU   *
         ISPCALL ('TBSKIP','PHONETAB') POSITION TO NEXT TABLE RECORD
         BAL   R10,CHECKRC            CHECK FOR HIGHER RETURN CODE
         B     CKSKIP2(R15)
CKSKIP2  EQU   *                      ERROR POSITIONING TO NEXT RECORD?
         B     NEXTREC                00
         B     NEXTREC                04
         B     NOERROR                08 END OF TABLE
         B     ERROR1                 12 TABLE NOT OPEN
         B     ERROR2                 16 DATA TRUNCATION OR
*                                        INSUFFICIENT SPACE PROVIDED
*                                        FOR ALL EXTENSION VAR NAMES
         B     ERROR2                 20 SEVERE ERROR
***********************************************************************
CHECKRC  EQU   *                      SAVE HIGHEST RETURN CODE
         CR    R2,R15                 COMPARE R15 TO SAVED RC
         BNHR  R10                    BRANCH IF R2 NOT > R15
         LR    R2,R15                 SAVE HIGHER RETURN CODE
         BR    R10                    RETURN
***********************************************************************
         CNOP  0,4                    ALIGNMENT
NOTFOUND EQU   *
         PUT   SYSOUT,=CL132'PHONE TABLE DOES NOT EXIST'
         PUT   SYSOUT,ERRMSG
         B     CLOSFILE
EMPTY    EQU   *
         PUT   SYSOUT,=CL132'PHONE TABLE IS EMPTY'
         PUT   SYSOUT,ERRMSG
         B     CLOSETBL
ERROR1   EQU   *
         PUT   SYSOUT,ERRMSG
         B     CLOSFILE
ERROR2   EQU   * *
         PUT   SYSOUT,ERRMSG
         B     CLOSETBL
NOERROR  EQU   *
         PUT   SYSOUT,=CL132'END OF PHONE TABLE - NO ERRORS'
         ED    FORMAT,COUNT
         PUT   SYSOUT,COUNTMSG
         B     CLOSETBL
COUNT    DS    P'12345'               COUNT OF O271C RECORDS
COUNTMSG DS    0CL132
FORMAT   DC    X'4020206B202021'
         DC    C' RECORDS WRITTEN TO THE O271C FILE'
         DC    CL91' '
         CNOP  0,4                    ALIGNMENT
CLOSETBL EQU   *
         ISPCALL ('TBCLOSE','PHONETAB',' ',' ',' ','PHNTLIB')
         BAL   R10,CHECKRC            CHECK FOR HIGHER RETURN CODE
CLOSFILE EQU   *
         CLOSE   (SYSOUT,,O271CFIL)
         BAL   R10,CHECKRC            CHECK FOR HIGHER RETURN CODE
         ISPCALL ('VRESET')
         B     RETURN
* BEGINNING OF EPILOG
RETURN   EQU   *
         L     R13,SAVEAREA+4         LOAD OS SAVE AREA ADDR TO R13
         LR    R15,R2                 MOVE HIGHEST RETURN CODE TO R15
         RETURN (14,12),RC=(15)       RETURN TO OS, RC IN REG 15
* END OF EPILOG
ISPADDR  DC    A(0)                   STORAGE FOR EP ADDRESS OF ISPLINK
VARLIST1 DC    C'(PLANT)'
VARLIST2 DC    C'(FIRSTAID)'
VARLIST3 DC    C'(EXTENS)'
VARLIST4 DC    C'(PAGENUM)'
VARLIST5 DC    C'(NAME)'
VARLIST6 DC    C'(EXCLUDE)'
VARLIST7 DC    C'(BUDGDEPT)'
VARLIST8 DC    C'(SUPFLAG )'
VARLISTA DC    C'(ZERRMSG )'
VARLISTB DC    C'(ZERRSM  )'
VARLISTC DC    C'(ZERRLM  )'
         CNOP  0,4                    ALIGNMENT
SAVEAREA DC    18F'0'                 OUR SAVE AREA
PARAMS   DC    7A(0)                  PARAMETER WORKAREA
         DC    CL01' '
O271CREC DS    0CL80
PLANT    DC    CL04' '                BUILDING ABBR/PLANT NUMBER
         DC    CL01' '
FIRSTAID DC    CL01' '                FIRST AID FLAG
         DC    CL01' '
EXTENS   DC    CL04' '                EXTENSION NUMBER
         DC    CL02' '
PAGENUM  DC    CL04' '                PAGING NUMBER
         DC    CL02' '
NAME     DC    CL40' '                NAME/ROOM
EXCLUDE  DC    CL01' '                MIS035/MIS036 EXCLUSION FLAG
BUDGDEPT DC    CL04' '                BUDGET DEPARTMENT
SUPFLAG  DC    CL01' '                MIS077 SUPPRESSION FLAG
         DC    CL15' '
ERRMSG   DS    0CL132
ZERRMSG  DC    CL8' '
         DC    CL2' '
ZERRSM   DC    CL24' '
         DC    CL2' '
ZERRLM   DC    CL96' '
O271CFIL DCB   DDNAME=O271C,DSORG=PS,MACRF=(PM),RECFM=FB,LRECL=80,     X
               BLKSIZE=6160
SYSOUT   DCB   DDNAME=SYSOUT,DSORG=PS,MACRF=(PM),RECFM=FB,LRECL=132,   X
               BLKSIZE=6204
***********************************************************************
         EQUATE                       REGISTER EQUATES
         END   PHONDUMP
