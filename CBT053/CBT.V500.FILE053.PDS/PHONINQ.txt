         TITLE 'PHONINQ - DELUXE PHONE LIST INQUIRY'
***********************************************************************
*                                                                     *
*        MODULE NAME = PHONINQ                                        *
*                                                                     *
*        DESCRIPTIVE NAME = DELUXE PHONE LIST INQUIRY                 *
*                                                                     *
*        FUNCTION =                                                   *
*              THIS PROGRAM WILL ALLOW USER TO DISPLAY ENTRIES        *
*              FROM THE DELUXE PHONE TABLE (PHONETAB) BY SPECIFYING   *
*              EITHER 'EXTENSION', 'PLANT', 'LASTNAME', 'FIRSTNAME',  *
*              AND/OR 'ROOM'. THE VARIABLES, 'EXTENSION', 'PLANT',    *
*              'LASTNAME', 'FIRSTNAME' AND 'ROOM', MAY BE INDICATED   *
*              SPECIFICALLY (E.G., JOHNSON) OR GENERICALLY            *
*              (E.G., JOH*).                                          *
*                                                                     *
*        OPERATION =                                                  *
*              GETMAIN WORK AREAS                                     *
*              DEFINE DIALOG VARIABLES                                *
*              OPEN 'PHONETAB'                                        *
*              SORT 'PHONETAB' BY NAME AND EXTENSION (IF NEEDED)      *
*              SET CRP TO TOP OF 'PHONETAB'                           *
*              DISPLAY SELECTION PANEL SO THAT USER MAY CHOOSE        *
*                  SCAN ARGUMENTS                                     *
*              PERFORM TBVCLEAR, TBSARG, TBSCAN AND TBTOP             *
*              DISPLAY THE ROWS WHICH WERE SCANNED                    *
*                  IF ENTER HIT, CONTINUE DISPLAY                     *
*                  IF END HIT, FINISH                                 *
*              CLOSE (DELETE) TEMPORARY TABLE                         *
*              FREE GETMAIN'ED AREAS                                  *
*              EXIT                                                   *
*                                                                     *
*        NOTES =                                                      *
*                                                                     *
*              REGISTER CONVENTIONS = STANDARD CONVENTIONS.           *
*                  REGISTERS 0 - 11 = WORK REGISTERS                  *
*                  REGISTER  12     = ADDRESSABILITY TO PHONINQ       *
*                  REGISTER  13     = SAVE AREA REGISTER AND          *
*                                     ADDRESSABILITY TO GETMAINED     *
*                                     WORK AREA                       *
*                  REGISTERS 14     = RETURN ADDRESS                  *
*                  REGISTERS 15     = ENTRY POINT ADDRESS AND         *
*                                     RETURN CODE REGISTER            *
*                                                                     *
*        MACROS =                                                     *
*              GETMAIN, ISPCALL, FREEMAIN, RETURN                     *
*                                                                     *
*        ASSEMBLER OPTIONS = LIST,RENT                                *
*                                                                     *
*        LINKAGE EDITOR OPTIONS = LET,LIST,XREF,RENT,REUS,REFR        *
*                                                                     *
*        CHANGE ACTIVITY =                                            *
*              03/04/86  WRITTEN.                               JWC.  *
*                                                                     *
***********************************************************************
         EJECT
         TITLE 'PHONINQ - ISPCALL MACRO'
         MACRO
&LBL     ISPCALL &LIST,        CALL PARAMETERS                         +
               &EP=ISPLINK,                                            +
               &LISTLBL=,      LABEL FOR PARAMETER LIST (NON-REENT)    +
               &EPADDR=,       LABEL FOR ADDRESS OF EP OF ISPLINK      +
               &WKAREA=,       TO BUILD CALL LIST (REENTERANT CODE)    +
               &WKREG=,        WORK REGISTER (DEFAULT 1)               +
               &TRACE=         'OFF' OR BIT FOR TRACE MODE
.*  ONCE WKAREA, WKREG, AND TRACE ARE SPECIFIED, THEY ARE USED IN ALL
.*  SUBSEQUENT OCCURRENCES OF THIS MACRO, UNLESS THE PARAMETER IS
.*  EXPLICITLY CODED.
.*  NOTE: FIRST 4 BYTES OF WKAREA ARE USED TO STORE NUMERIC VALUES,
.*  WHICH ARE PRIMARILY LENGTH FIELDS.
         LCLA  &S,&T,&U,&TRCEMSK
         LCLB  &NUMSW
         LCLC  &NDX,&WKAR,&WKRG,&WKEP
         GBLC  &ISPRREG,&ISPRAR,&ISPTRC1,&ISPEP
         GBLA  &ISPTRC2
&NDX     SETC  '&SYSNDX'
&S       SETA  1               SUBSCRIPT FOR GOING THRU &LIST
***********************************************************************
         AIF   (T'&LBL NE 'O').L1
*                  ISPCALL  -  INVOKE ISPF DIALOG SERVICES      ISPCALL
         AGO   .L2
.L1      ANOP
&LBL     DS    0H  ISPCALL  -  INVOKE ISPF DIALOG SERVICES      ISPCALL
.L2      ANOP
         AIF   (T'&LIST NE 'O').A1
         MNOTE 12,'*** ISPCALL PARAMETERS MUST BE PROVIDED ***'
         AGO   .EXIT
.A1      AIF   ('&LIST'(1,1) EQ '(').CHKTRCE
         MNOTE 12,'*** ISPCALL PARAMETERS MUST BE ENCLOSED IN PARENTHES+
               ES ***'
         AGO   .EXIT
.****************** ANALYZE "TRACE" PARAMETER *************************
.CHKTRCE AIF   (T'&TRACE EQ 'O' AND '&ISPTRC1' NE '').TRCE4
         AIF   (T'&TRACE EQ 'O' AND '&ISPTRC1' EQ '').TRCEOFF
         AIF   ('&TRACE'(1,1) EQ '(').TRCEON
         AIF   ('&TRACE' EQ 'OFF').TRCEOFF
         MNOTE 12,'*** INVALID TRACE PARAMETERS ***'
         AGO   .EXIT
.TRCEON  AIF   (T'&TRACE(2) EQ 'N').TRCE2
.TRCE8   MNOTE 12,'*** TRACE BIT (2ND PARAMETER) MUST BE 0 - 7 ***'
         AGO   .EXIT
.TRCE2   AIF   (&TRACE(2) GE 0 OR &TRACE(2) LE 7).TRCE3
         AGO   .TRCE8
.TRCE3   ANOP
&ISPTRC1 SETC  '&TRACE(1)'     SET GLOBAL VARIABLE
&ISPTRC2 SETA  &TRACE(2)       SET GLOBAL VARIABLE
.TRCE4   ANOP
&U       SETA  0
&TRCEMSK SETA  255
&T       SETA  128
.TRCE5   AIF   (&U GT 7).CHKREG
         AIF   (&ISPTRC2 NE &U).TRCE7
.TRCE6   ANOP
&U       SETA  &U+1
&T       SETA  &T/2
         AGO   .TRCE5
.TRCE7   ANOP
&TRCEMSK SETA  &TRCEMSK-&T
         AGO   .TRCE6
.TRCEOFF ANOP
&ISPTRC1 SETC  ''              SET GLOBAL VARIABLE
.****************** ANALYZE "WKREG" PARAMETER *************************
.CHKREG  AIF   (T'&WKREG EQ 'O').A2
&ISPRREG SETC  '&WKREG'        SET GLOBAL VARIABLE
.A2      AIF   ('&ISPRREG' EQ '').A3
&WKRG    SETC  '&ISPRREG'
         AGO   .CHKAREA
.A3      ANOP
&ISPRREG SETC  '1'             SET GLOBAL VARIABLE
         AGO   .A2
.****************** SEE IF USING PROVIDED WORKAREA ********************
.CHKAREA AIF   (T'&WKAREA NE 'O' OR '&ISPRAR' NE '').RENT
         AIF   (T'&LISTLBL EQ 'O').B1
&WKAR    SETC  '&LISTLBL'
         AGO   .B2
.B1      ANOP
&WKAR    SETC  'ISPC&NDX'      LABEL FOR MACRO WORKAREA
.B2      ANOP
&T       SETA  4*(N'&LIST+1)   LENGTH OF MACRO WORKAREA
         B     &WKAR.+&T             BRANCH AROUND DATA         ISPCALL
&T       SETA  N'&LIST+1
&WKAR    DC    &T.F'0'               PARAMETER LIST             ISPCALL
.******************** BUILD PARAMETER LIST ****************************
.LOOP    AIF   (&S GT N'&LIST).ENDLOOP
         AIF   ('&LIST(&S)'(1,1) EQ '''').C1 TEST FOR LITERAL
         AIF   (T'&LIST(&S) EQ 'N').NUM      TEST FOR NUMERIC
         AIF   ('&LIST(&S)'(1,1) EQ '(').REG TEST FOR REG NOTATION
         LA    &WKRG,&LIST(&S)       LOAD DATA ADDR             ISPCALL
.ST      ANOP
&T       SETA  4*&S
         ST    &WKRG,&WKAR.+&T       STORE IN PARAM LIST        ISPCALL
         AIF   ('&ISPTRC1' EQ '' OR &S GT 1).C3           TRACE
         TM    &ISPTRC1,&TRCEMSK     IS TRACE BIT ON?     TRACE ISPCALL
         BZ    ISPT&NDX                                   TRACE ISPCALL
         AIF   ('&WKRG' EQ '1' OR '&WKRG' EQ 'R1').C4     TRACE
         LR    1,&WKRG               LOAD SRVC NAME ADDR  TRACE ISPCALL
         EJECT
.C4      AIF   ('&LIST(&S)'(1,1) EQ '''').C5 LITERAL?     TRACE
         LA    0,8                   LOAD LENGTH          TRACE ISPCALL
         AGO   .C6                                        TRACE
.C5      ANOP                                             TRACE
&U       SETA  K'&LIST(&S)-2   LENGTH OF SERVICE NAME     TRACE
         LA    0,&U                  LOAD LENGTH          TRACE ISPCALL
.C6      ANOP                                             TRACE
         SVC   93                    ISSUE TPUT SVC       TRACE ISPCALL
ISPT&NDX EQU   *                                          TRACE ISPCALL
.C3      ANOP
&S       SETA  &S+1
         AGO   .LOOP
.C1      AIF   (&S EQ 1).C2    SPF SERVICE NAME - LENGTH IS OK
&T       SETA  K'&LIST(&S)-2
         AIF   (&T GT 7).C2    LENGTH 8 OR GREATER IS OK
&T       SETA  &T+1            ADD FOR ONE BLANK
         LA    &WKRG,=CL&T.&LIST(&S) LOAD DATA ADDR             ISPCALL
         AGO   .ST
.C2      ANOP
         LA    &WKRG,=C&LIST(&S)     LOAD DATA ADDR             ISPCALL
         AGO   .ST
.NUM     AIF   (&NUMSW).NUM2
.*  NOTE: SPF WILL MODIFY LENGTH FIELDS, SO THEY CANNOT BE IN
.*  REENTERANT CODE.
         MVC   &WKAR.(4),=F'&LIST(&S)'                                 +
                                     MOVE NUMERIC VALUE         ISPCALL
&NUMSW   SETB  1
         AGO   .LA
.NUM2    MNOTE 12,'*** ONLY ONE NUMERIC ALLOWED PER ISPCALL MACRO - LIS+
               T ITEM &S --&LIST(&S)-- IS SECOND OCCURRENCE ***'
         AGO   .EXIT
.LA      ANOP
         LA    &WKRG,&WKAR           LOAD DATA ADDR             ISPCALL
         AGO   .ST
.REG     ANOP
&T       SETA  4*&S
         ST    &LIST(&S),&WKAR.+&T                              ISPCALL
         AGO   .C3
.******************** USING PROVIDED WORKAREA *************************
.RENT    AIF   (T'&WKAREA EQ 'O').RENT2
&ISPRAR  SETC  '&WKAREA'       SET GLOBAL VARIABLE
.RENT2   ANOP
&WKAR    SETC  '&ISPRAR'
         AGO   .LOOP
         EJECT
.****************** GENERATE STATEMENTS FOR CALL **********************
.ENDLOOP ANOP
         OI    &WKAR.+&T,X'80'       TURN ON VL BIT             ISPCALL
.*       LA    1,&WKAR.+4                                       ISPCALL
.*       L     15,=V(&EP)                                       ISPCALL
.*       BALR  14,15                                            ISPCALL
         AIF   (T'&EPADDR EQ 'O').EP2
&ISPEP   SETC  '&EPADDR'
         LOAD  EP=ISPLINK
         ST    R0,&ISPEP
.EP2     ANOP
&WKEP    SETC  '&ISPEP'
         L     15,&WKEP
         LA    1,&WKAR.+4
         CALL  (15)
.EXIT    ANOP
***********************************************************************
         SPACE 2
         MEND
         EJECT
         TITLE 'PHONINQ - INITIALIZATION SECTION'
PHONINQ CSECT
***********************************************************************
*  INITIALIZATION SECTION - GETMAIN WORKAREA AND SAVE AREA CHAINING
***********************************************************************
         SAVE  (14,12),,PHONINQ_&SYSDATE_&SYSTIME    SAVE REGISTERS
         LR    R12,R15                 LOAD REG 12 WITH ENTRY ADDRESS
         USING PHONINQ,R12             ESTABLISH ADDRESSABILITY TO
*                                      PROGRAM CSECT
         GETMAIN R,LV=WORKLEN          GETMAIN DYNAMIC WORKAREA
         ST    R13,4(R1)               STORE ADDRESS PREVIOUS SAVEAREA
         ST    R1,8(,R13)              STORE ADDRESS OF CURRENT
*                                      SAVEAREA IN PREVIOUS SAVEAREA
         LR    R13,R1                  LOAD REG 13 WITH ADDRESS OF
*                                      CURRENT SAVEAREA
         USING WORKAREA,R13            ESTABLISH ADDRESSABILITY TO
*                                      DYNAMIC WORKAREA
***********************************************************************
*  INITIALIZATION SECTION - SET DIALOG CONTROL, DEFINE VARIABLES
***********************************************************************
*                                      SET DIALOG CONTROL
         ISPCALL ('CONTROL','ERRORS','RETURN'),                        +
               WKAREA=PARMS,                                           +
               EPADDR=ISPADDR
VDEF1    EQU   *
         MVC   PHNID_,DEFID            SET ISPF DIALOG SERVICE
*                                      DEFINE VARIABLES
         ISPCALL ('VDEFINE',DEFLIST1,EXTENS$,'CHAR',4)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEF2                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEF2    EQU   *
         ISPCALL ('VDEFINE',DEFLIST2,NAME$,'CHAR',40)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEF3                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEF3    EQU   *
         ISPCALL ('VDEFINE',DEFLIST3,LNAME$,'CHAR',20)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEF4                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEF4    EQU   *
         ISPCALL ('VDEFINE',DEFLIST4,FNAME$,'CHAR',20)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEF5                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEF5    EQU   *
         ISPCALL ('VDEFINE',DEFLIST5,EXTENS_,'CHAR',4)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEF6                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEF6    EQU   *
         ISPCALL ('VDEFINE',DEFLIST6,NAME_,'CHAR',40)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEF7                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEF7    EQU   *
         ISPCALL ('VDEFINE',DEFLIST7,LNAME_,'CHAR',20)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEF8                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEF8    EQU   *
         ISPCALL ('VDEFINE',DEFLIST8,FNAME_,'CHAR',20)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEF9                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEF9    EQU   *
         ISPCALL ('VDEFINE',DEFLIST9,SORTORD_,'CHAR',1)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEFA                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEFA    EQU   *
         ISPCALL ('VDEFINE',DEFLISTA,PHNMSGS_,'CHAR',24)
         LTR   R15,R15                 DEFINE SUCCESSFUL?
         BZ    VDEFB                   YES, CONTINUE DEFINE
         B     VDEF10(R15)             CHECK RETURN CODE
VDEFB    EQU   *
         ISPCALL ('VDEFINE',DEFLISTB,PHNMSGL_,'CHAR',78)
         B     VDEF10(R15)             CHECK RETURN CODE
VDEF10   EQU   *
         B     TBOPEN                  RC = 00 (CONTINUE)
         B     PHNERR04                RC = 04 (NOT VALID FOR VDEF)
         B     PHNERR08                RC = 08 (DISPLAY PANEL / MSG)
         B     PHNERR12                RC = 12 (NOT VALID FOR VDEF)
         B     PHNERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     PHNERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  INITIALIZATION SECTION - OPEN 'PHONETAB'
***********************************************************************
TBOPEN   EQU   *
         MVC   PHNID_,OPNID            SET ISPF DIALOG SERVICE
*                                      SORT 'PHONETAB' TABLE
         ISPCALL ('TBOPEN','PHONETAB','NOWRITE','PHNTLIB')
         B     TBOPEN10(R15)           CHECK RETURN CODE
TBOPEN10 EQU   *
         B     TBTOP1                  RC = 00 (CONTINUE)
         B     TBTOP1                  RC = 04 (CONTINUE)
         B     PHNERR08                RC = 08 (DISPLAY PANEL / MSG)
         B     PHNERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     PHNERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     PHNERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  INITIALIZATION SECTION - POINT TO TOP OF 'PHONETAB'
***********************************************************************
TBTOP1   EQU   *
         MVC   PHNID_,TOPID            SET ISPF DIALOG SERVICE
*                                      POSITION CRP AT TOP OF TABLE
         ISPCALL ('TBTOP','PHONETAB')
         B     TBTOP110(R15)           CHECK RETURN CODE
TBTOP110 EQU   *
         B     TBSCAN1                 RC = 00 (CONTINUE)
         B     PHNERR04                RC = 04 (NOT VALID FOR TBTOP)
         B     PHNERR08                RC = 08 (NOT VALID FOR TBTOP)
         B     PHNERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     PHNERR16                RC = 16 (NOT VALID FOR TBTOP)
         B     PHNERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  INITIALIZATION SECTION - SEARCH 'PHONETAB'
***********************************************************************
TBSCAN1  EQU   *
         MVC   PHNID_,SCNID            SET ISPF DIALOG SERVICE
         MVC   EXTENS_,SORTREC         MOVE SORT REC ID TO EXTENS_
         MVC   SORTORD_,ASTERICK       MOVE GENERIC ID TO SORTORD_
*                                      SEARCH A TABLE
         ISPCALL ('TBSCAN','PHONETAB','EXTENS','DEFLIST7')
         B     TBSCN110(R15)           CHECK RETURN CODE
TBSCN110 EQU   *
         B     TBDELETE                RC = 00 (CONTINUE)
         B     PHNERR04                RC = 04 (NOT VALID FOR TBSCAN)
         B     TBDELETE                RC = 08 (CONTINUE)
         B     PHNERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     PHNERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     PHNERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  INITIALIZATION SECTION - DELETE SORT RECORD FROM 'PHONETAB'
***********************************************************************
TBDELETE EQU   *
         MVC   PHNID_,DELID            SET ISPF DIALOG SERVICE
*                                      DELETE A ROW FROM A TABLE
         ISPCALL ('TBDELETE','PHONETAB')
         B     TBDEL10(R15)            CHECK RETURN CODE
TBDEL10  EQU   *
         B     TBSORT                  RC = 00 (CONTINUE)
         B     PHNERR04                RC = 04 (NOT VALID FOR TBDELETE)
         B     TBSORT                  RC = 08 (CONTINUE)
         B     PHNERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     PHNERR16                RC = 16 (NOT VALID FOR TBDELETE)
         B     PHNERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  INITIALIZATION SECTION - SORT 'PHONETAB' IF NEEDED
***********************************************************************
TBSORT   EQU   *
         MVC   PHNID_,SRTID            SET ISPF DIALOG SERVICE
         CLC   SORTORD_,XTENSION       NEED TO SORT BY 'NAME,EXTENS'?
         BNE   DISPLAY                 NO, CONTINUE
*                                      SORT 'PHONETAB' TABLE
         ISPCALL ('TBSORT','PHONETAB','(NAME,C,A,EXTENS,C,A)')
         B     TBSORT10(R15)           CHECK RETURN CODE
TBSORT10 EQU   *
         B     TBTOP2                  RC = 00 (CONTINUE)
         B     PHNERR04                RC = 04 (NOT VALID FOR TBSORT)
         B     PHNERR08                RC = 08 (NOT VALID FOR TBSORT)
         B     PHNERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     PHNERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     PHNERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  INITIALIZATION SECTION - POINT TO TOP OF 'PHONETAB'
***********************************************************************
TBTOP2   EQU   *
         MVC   PHNID_,TOPID            SET ISPF DIALOG SERVICE
*                                      POSITION CRP AT TOP OF TABLE
         ISPCALL ('TBTOP','PHONETAB')
         B     TBTOP210(R15)           CHECK RETURN CODE
TBTOP210 EQU   *
         B     DISPLAY                 RC = 00 (CONTINUE)
         B     PHNERR04                RC = 04 (NOT VALID FOR TBTOP)
         B     PHNERR08                RC = 08 (NOT VALID FOR TBTOP)
         B     PHNERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     PHNERR16                RC = 16 (NOT VALID FOR TBTOP)
         B     PHNERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  DISPLAY SECTION I - DISPLAY SELECTION PANEL
***********************************************************************
DISPLAY  EQU   *
         MVC   PHNID_,DISID            SET ISPF DIALOG SERVICE
         MVC   EXTENS$,VARINIT         INITIALIZE EXTENS$
         MVC   PLANT$,VARINIT          INITIALIZE PLANT$
         MVC   NAME$,VARINIT           INITIALIZE NAME$
         MVC   LNAME$,VARINIT          INITIALIZE LNAME$
         MVC   FNAME$,VARINIT          INITIALIZE FNAME$
         MVC   PHNMSGS_,PHONERRS       INITIALIZE SHORT MESSAGE
         MVC   PHNMSGL_,PHONERRL       INITIALIZE LONG MESSAGE
         NI    DISPFLAG,FLAGINIT       INITIALIZE DISPLAY FLAGS
*                                      DISPLAY SELECTION PANEL
         ISPCALL ('DISPLAY','PHONINQ1')
         B     DISPL10(R15)            CHECK RETURN CODE
DISPL10  EQU   *
         B     TBVCLEAR                RC = 00 (CONTINUE)
         B     PHNERR04                RC = 04 (NOT VALID FOR DISPLAY)
         B     FINISH                  RC = 08 (CONTINUE)
         B     PHNERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     PHNERR16                RC = 16 (NOT VALID FOR DISPLAY)
         B     PHNERR20                RC = 20 (DISPLAY PANEL / MSG)
         TITLE 'PHONINQ - DISPLAY SECTION II'
***********************************************************************
*  DISPLAY SECTION II - CLEAR 'PHONETAB' VARIABLES
***********************************************************************
TBVCLEAR EQU   *
         MVC   PHNID_,CLRID            SET ISPF DIALOG SERVICE
*                                      CLEAR TABLE VARIABLES
         ISPCALL ('TBVCLEAR','PHONETAB')
         B     TBCLR10(R15)            CHECK RETURN CODE
TBCLR10  EQU   *
         B     TBSARG                  RC = 00 (CONTINUE)
         B     PHNERR04                RC = 04 (NOT VALID FOR TBVCLEAR)
         B     PHNERR08                RC = 08 (NOT VALID FOR TBVCLEAR)
         B     PHNERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     PHNERR16                RC = 16 (NOT VALID FOR TBVCLEAR)
         B     PHNERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  DISPLAY SECTION II - DEFINE SCAN ARGUMENTS FOR 'PHONETAB'
***********************************************************************
TBSARG   EQU   *
         MVC   PHNID_,ARGID            SET ISPF DIALOG SERVICE
         MVC   EXTENS_,EXTENS$         SET EXTENS_ WITH SELECTION VALUE
         MVC   PLANT_,PLANT$           SET PLANT_ WITH SELECTION VALUE
         MVC   NAME_,NAME$             SET NAME_ WITH SELECTION VALUE
         MVC   LNAME_,LNAME$           SET LNAME_ WITH SELECTION VALUE
         MVC   FNAME_,FNAME$           SET FNAME_ WITH SELECTION VALUE
         CLC   EXTENS_(3),SCANALL      SCAN FOR ALL EXTENSIONS?
         BE    TBARG10                 YES, DON'T SET FLAG
         OI    DISPFLAG,DISPEXTN       SET FLAG TO DISPLAY EXTENSION
TBARG10  EQU   *
         CLC   PLANT_(3),SCANALL       SCAN FOR ALL PLANTS?
         BE    TBARG20                 YES, DON'T SET FLAG
         OI    DISPFLAG,DISPPLNT       SET FLAG TO DISPLAY PLANT
TBARG20  EQU   *
         CLC   LNAME_(3),SCANALL       SCAN FOR ALL LAST NAMES?
         BE    TBARG30                 YES, DON'T SET FLAG
         OI    DISPFLAG,DISPLNAM       SET FLAG TO DISPLAY LAST NAME
TBARG30  EQU   *
         CLC   FNAME_(3),SCANALL       SCAN FOR ALL FIRST NAMES?
         BE    TBARG40                 YES, DON'T SET FLAG
         OI    DISPFLAG,DISPFNAM       SET FLAG TO DISPLAY FIRST NAME
TBARG40  EQU   *
         CLC   NAME_(3),SCANALL        SCAN FOR ALL ROOMS?
         BE    TBARG50                 YES, DON'T SET FLAG
         OI    DISPFLAG,DISPROOM       SET FLAG TO DISPLAY ROOM
TBARG50  EQU   *
         CLI   DISPFLAG,FLAGINIT       SCANALL FOR ALL VARIABLES?
         BE    TBARGA0                 YES, SET UP FOR NORMAL SCAN
         TM    DISPFLAG,DISPROOM       SCAN FOR ROOM?
         BNO   TBARG70                 NO, CHECK FOR NAME SCAN
         MVC   LNAME_,ROOM             SET LNAME_ WITH ROOM REC ID
         CLC   ALL,NAME_               SHOULD ALL ROOMS BE DISPLAYED?
         BNE   TBARG60                 NO, CONTINUE
         MVC   NAME_(3),SCANALL        YES, SET UP GENERIC ALL SEARCH
TBARG60  EQU   *
*                                      DEFINE SCAN ARGUMENTS
         ISPCALL ('TBSARG','PHONETAB',NAMELST2)
         B     TBARGB0(R15)            CHECK RETURN CODE
TBARG70  EQU   *
         TM    DISPFLAG,DISPLNAM       SCAN FOR LAST NAME?
         BO    TBARG80                 YES, SET UP FOR NAME SCAN
         TM    DISPFLAG,DISPFNAM       SCAN FOR FIRST NAME?
         BNO   TBARGA0                 NO, SET UP FOR NORMAL SCAN
TBARG80  EQU   *
         CLC   ALL,LNAME_              SHOULD ALL NAMES BE DISPLAYED?
         BNE   TBARG90                 NO, CONTINUE
         MVC   LNAME_(3),SCANALL       YES, SET UP GENERIC ALL SEARCH
         MVC   FNAME_(3),SCANALL
TBARG90  EQU   *
*                                      DEFINE SCAN ARGUMENTS
         ISPCALL ('TBSARG','PHONETAB',NAMELST1)
         B     TBARGB0(R15)            CHECK RETURN CODE
TBARGA0  EQU   *
*                                      DEFINE SCAN ARGUMENTS
         ISPCALL ('TBSARG','PHONETAB')
         B     TBARGB0(R15)            CHECK RETURN CODE
TBARGB0  EQU   *
         B     TBSCAN2                 RC = 00 (CONTINUE)
         B     PHNERR04                RC = 04 (NOT VALID FOR TBSARG)
         B     PHNERR08                RC = 08 (DISPLAY PANEL / MSG)
         B     PHNERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     PHNERR16                RC = 16 (NOT VALID FOR TBSARG)
         B     PHNERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  DISPLAY SECTION II - SEARCH 'PHONETAB' TABLE
***********************************************************************
TBSCAN2  EQU   *
         MVC   PHNID_,SCNID            SET ISPF DIALOG SERVICE
*                                      SEARCH A TABLE
         ISPCALL ('TBSCAN','PHONETAB')
         B     TBSCN210(R15)           CHECK RETURN CODE
TBSCN210 EQU   *
         B     TBTOP3                  RC = 00 (CONTINUE)
         B     PHNERR04                RC = 04 (NOT VALID FOR TBSCAN)
         B     SCNERR08                RC = 08 (DISPLAY PANEL / MSG)
         B     PHNERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     PHNERR16                RC = 16 (DISPLAY PANEL / MSG)
         B     PHNERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  DISPLAY SECTION II - SET CRP TO TOP OF 'PHONETAB' TABLE
***********************************************************************
TBTOP3   EQU   *
         MVC   PHNID_,TOPID            SET ISPF DIALOG SERVICE
*                                      POSITION CRP AT TOP OF TABLE
         ISPCALL ('TBTOP','PHONETAB')
         B     TBTOP310(R15)           CHECK RETURN CODE
TBTOP310 EQU   *
         B     TBDISPL                 RC = 00 (CONTINUE)
         B     PHNERR04                RC = 04 (NOT VALID FOR TBTOP)
         B     PHNERR08                RC = 08 (NOT VALID FOR TBTOP)
         B     PHNERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     PHNERR16                RC = 16 (NOT VALID FOR TBTOP)
         B     PHNERR20                RC = 20 (DISPLAY PANEL / MSG)
***********************************************************************
*  DISPLAY SECTION II - DISPLAY SELECTED TABLE INFORMATION
***********************************************************************
TBDISPL  EQU   *
         MVC   PHNID_,TDSID            SET ISPF DIALOG SERVICE
         TM    DISPFLAG,DISPROOM       DISPLAY ROOMS?
         BO    TBDIS10                 YES, DISPLAY ROOMS
         TM    DISPFLAG,DISPLNAM       DISPLAY LAST NAME?
         BO    TBDIS20                 YES, DISPLAY SPECIFIC NAMES
         TM    DISPFLAG,DISPFNAM       DISPLAY FIRST NAME?
         BO    TBDIS20                 YES, DISPLAY SPECIFIC NAMES
*                                      DISPLAY BOTH ROOMS & NAMES
*                                      DISPLAY TABLE INFORMATION
         ISPCALL ('TBDISPL','PHONETAB','PHONINQ4')
         B     TBDIS30(R15)            CHECK RETURN CODE
TBDIS10  EQU   *
*                                      DISPLAY TABLE INFORMATION
         ISPCALL ('TBDISPL','PHONETAB','PHONINQ3')
         B     TBDIS30(R15)            CHECK RETURN CODE
TBDIS20  EQU   *
*                                      DISPLAY TABLE INFORMATION
         ISPCALL ('TBDISPL','PHONETAB','PHONINQ2')
         B     TBDIS30(R15)            CHECK RETURN CODE
TBDIS30  EQU   *
         B     TBDISPL                 RC = 00 (CONTINUE)
         B     TBDISPL                 RC = 04 (CONTINUE)
         B     DISPLAY                 RC = 08 (CONTINUE)
         B     PHNERR12                RC = 12 (DISPLAY PANEL / MSG)
         B     PHNERR16                RC = 16 (NOT VALID FOR TBDISPL)
         B     PHNERR20                RC = 20 (DISPLAY PANEL / MSG)
         TITLE 'PHONINQ - ERROR PROCESSING SECTION'
***********************************************************************
*  ERROR PROCESSING SECTION
***********************************************************************
PHNERR04 EQU   *
         MVC   PHNRC_,FOUR             SET RETURN CODE
         B     MSGDISP1                DISPLAY PANEL WITH MESSAGE
PHNERR08 EQU   *
         MVC   PHNRC_,EIGHT            SET RETURN CODE
         B     MSGDISP1                DISPLAY PANEL WITH MESSAGE
PHNERR12 EQU   *
         MVC   PHNRC_,TWELVE           SET RETURN CODE
         B     MSGDISP1                DISPLAY PANEL WITH MESSAGE
PHNERR16 EQU   *
         MVC   PHNRC_,SIXTEEN          SET RETURN CODE
         B     MSGDISP1                DISPLAY PANEL WITH MESSAGE
PHNERR20 EQU   *
         MVC   PHNRC_,TWENTY           SET RETURN CODE
         B     MSGDISP1                DISPLAY PANEL WITH MESSAGE
SCNERR08 EQU   *
         MVC   PHNMSGS_,SCNNERRS       INITIALIZE SHORT MESSAGE
         MVC   PHNMSGL_,SPACES         INITIALIZE LONG MESSAGE
         LA    R4,PHNMSGL_             R4 - INDEX THROUGH LONG MESSAGE
         LA    R6,5                    R6 - TO INDICATE MORE THAN 1
         LA    R5,4                    R5 - TO INDICATE MORE THAN 1
         TM    DISPFLAG,DISPEXTN       EXTENSION DISPLAY REQUEST?
         BNO   SCNERR18                NO, CHECK FOR PLANT
         MVC   0(9,R4),EXTEXT          MOVE EXTENSION TEXT TO LONG MSG
         LA    R4,9(R4)                INCREMENT R4
         MVC   0(1,R4),COMMA           MOVE COMMA TEXT TO LONG MSG
         LA    R4,1(R4)                INCREMENT R4
         BCTR  R6,0                    DECREMENT R6 BY ONE
SCNERR18 EQU   *
         TM    DISPFLAG,DISPPLNT       PLANT DISPLAY REQUEST?
         BNO   SCNERR28                NO, CHECK FOR LAST NAME
         MVC   0(6,R4),PLTEXT          MOVE PLANT TEXT TO LONG MSG
         LA    R4,6(R4)                INCREMENT R4
         MVC   0(1,R4),COMMA           MOVE COMMA TEXT TO LONG MSG
         LA    R4,1(R4)                INCREMENT R4
         BCTR  R6,0                    DECREMENT R6 BY ONE
SCNERR28 EQU   *
         TM    DISPFLAG,DISPLNAM       LAST NAME DISPLAY REQUEST?
         BNO   SCNERR38                NO, CHECK FOR FIRST NAME
         MVC   0(10,R4),LNTEXT         MOVE LNAME TEXT TO LONG MSG
         LA    R4,10(R4)               INCREMENT R4
         MVC   0(1,R4),COMMA           MOVE COMMA TEXT TO LONG MSG
         LA    R4,1(R4)                INCREMENT R4
         BCTR  R6,0                    DECREMENT R6 BY ONE
SCNERR38 EQU   *
         TM    DISPFLAG,DISPFNAM       FIRST NAME DISPLAY REQUEST?
         BNO   SCNERR48                NO, CHECK FOR ROOM
         MVC   0(11,R4),FNTEXT         MOVE FNAME TEXT TO LONG MSG
         LA    R4,11(R4)               INCREMENT R4
         MVC   0(1,R4),COMMA           MOVE COMMA TEXT TO LONG MSG
         LA    R4,1(R4)                INCREMENT R4
         BCTR  R6,0                    DECREMENT R6 BY ONE
SCNERR48 EQU   *
         TM    DISPFLAG,DISPROOM       ROOM DISPLAY REQUEST?
         BNO   SCNERR58                NO, ADD REST OF TEXT
         MVC   0(5,R4),RMTEXT          MOVE ROOM TEXT TO LONG MSG
         LA    R4,5(R4)                INCREMENT R4
         MVC   0(1,R4),COMMA           MOVE COMMA TEXT TO LONG MSG
         LA    R4,1(R4)                INCREMENT R4
         BCTR  R6,0                    DECREMENT R6 BY ONE
SCNERR58 EQU   *
         CR    R6,R5                   HAVE MORE THAN 1 BEEN REQUESTED?
         BL    SCNERR68                YES, CONTINUE
         BCTR  R4,0                    DECREMENT R4 BY ONE
         MVC   0(23,R4),SINGTEXT       ONE SELECTION TEXT
         B     MSGDISP2                YES, CONTINUE
SCNERR68 EQU   *
         BCTR  R4,0                    DECREMENT R4 BY ONE
         MVC   0(34,R4),MULTTEXT       MORE THAN ONE SELECTION TEXT
         B     MSGDISP2                YES, CONTINUE
MSGDISP1 EQU   *
*                                      DISPLAY SELECTION PANEL
         ISPCALL ('DISPLAY','PHONINQ1',PHONMSG)
         B     FINISH
MSGDISP2 EQU   *
*                                      DISPLAY SELECTION PANEL
         ISPCALL ('DISPLAY','PHONINQ1',PHONMSG)
         B     DISPLAY
         TITLE 'PHONINQ - CLOSING SECTION'
***********************************************************************
*  CLOSING SECTION - FREE GETMAINED AREA, SET RETURN CODE RESTORE REGS
***********************************************************************
FINISH   EQU   *
*                                      RELEASE AREA HELD BY PHONETAB
         ISPCALL ('TBEND','PHONETAB')
         LA    R0,WORKLEN              R0 - LENGTH OF WORKAREA
         LR    R1,R13                  R1 - ADDRESS OF WORKAREA
         L     R13,4(R13)              R13 - ADDR CALLER'S SAVE AREA
         FREEMAIN R,LV=(0),A=(1)       FREE GETMAINED VIRTUAL STORAGE
         RETURN (14,12),RC=(15)        RESTORE REGISTERS AND RETURN
         TITLE 'PHONINQ - REGISTER EQUATES'
***********************************************************************
*  REGISTER EQUATES
***********************************************************************
R0       EQU   00
R1       EQU   01
R2       EQU   02
R3       EQU   03
R4       EQU   04
R5       EQU   05
R6       EQU   06
R7       EQU   07
R8       EQU   08
R9       EQU   09
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
FLAGINIT EQU   X'00'                   DISPLAY FLAG INITIAL VALUE
DISPEXTN EQU   X'01'                   DISPLAY FLAG - NAME DISPLAY
DISPPLNT EQU   X'02'                   DISPLAY FLAG - ROOM DISPLAY
DISPLNAM EQU   X'04'                   DISPLAY FLAG - ROOM AND/OR NAME
DISPFNAM EQU   X'08'                   DISPLAY FLAG - ROOM AND/OR NAME
DISPROOM EQU   X'10'                   DISPLAY FLAG - ROOM AND/OR NAME
         TITLE 'PHONINQ - DATA CONSTANTS'
***********************************************************************
*  CONSTANTS
***********************************************************************
SPACES   DC    CL80' '                 SPACES
VARINIT  DC    CL40'*                                       '
COMMA    DC    CL1','                  COMMA
DASH     DC    CL1'-'                  DASH
ASTERICK DC    CL1'*'                  ASTERICK
XTENSION DC    CL1'E'                  'PHONETAB' SORTED BY EXTENSION
SCANALL  DC    CL3'*  '                SCAN ALL (GENERIC)
ALL      DC    CL3'ALL'                SCAN ALL OF PARTICULAR TYPE
SORTREC  DC    CL4'¢¢¢¢'               SORT REC IDENTIFIER
ROOM     DC    CL20'ROOM$$$$$$$$$$$$$$$$'    ROOM RECORD IDENTIFIER
PHONMSG  DC    CL8'PHON000'            ERROR MESSAGE ID
FOUR     DC    CL2'04'                 RETURN CODE OF '04'
EIGHT    DC    CL2'08'                 RETURN CODE OF '08'
TWELVE   DC    CL2'12'                 RETURN CODE OF '12'
SIXTEEN  DC    CL2'16'                 RETURN CODE OF '16'
TWENTY   DC    CL2'20'                 RETURN CODE OF '20'
ARGID    DC    CL8'TBSARG'             ISPF DIALOG SERVICE (TBSARG)
CLRID    DC    CL8'TBVCLEAR'           ISPF DIALOG SERVICE (TBVCLEAR)
DEFID    DC    CL8'VDEFINE'            ISPF DIALOG SERVICE (VDEFINE)
DELID    DC    CL8'TBDELETE'           ISPF DIALOG SERVICE (TBDELETE)
DISID    DC    CL8'DISPLAY'            ISPF DIALOG SERVICE (DISPLAY)
OPNID    DC    CL8'TBOPEN'             ISPF DIALOG SERVICE (TBOPEN)
SCNID    DC    CL8'TBSCAN'             ISPF DIALOG SERVICE (TBSCAN)
SRTID    DC    CL8'TBSORT'             ISPF DIALOG SERVICE (TBSORT)
TDSID    DC    CL8'TBDISPL'            ISPF DIALOG SERVICE (TBDISPL)
TOPID    DC    CL8'TBTOP'              ISPF DIALOG SERVICE (TBTOP)
EXTEXT   DC    C'EXTENSION'            TEXT FOR SCAN MESSAGE
PLTEXT   DC    C' PLANT'               TEXT FOR SCAN MESSAGE
LNTEXT   DC    C' LAST NAME'           TEXT FOR SCAN MESSAGE
FNTEXT   DC    C' FIRST NAME'          TEXT FOR SCAN MESSAGE
RMTEXT   DC    C' ROOM'                TEXT FOR SCAN MESSAGE
SINGTEXT DC    C' selected not in table.'    TEXT FOR SCAN MESSAGE
MULTTEXT DC    C' do not co-exist in any table row.'
NAMELST1 DS    0F                      NAMES OF VARIABLES IN TABLE
         DC    F'02'
         DC    F'00'
         DC    CL8'LNAME'
         DC    CL8'FNAME'
NAMELST2 DS    0F                      NAMES OF VARIABLES IN TABLE
         DC    F'01'
         DC    F'00'
         DC    CL8'LNAME'
DEFLIST1 DS    0F                      NAMES OF VARIABLES (LENGTH 4)
         DC    F'02'
         DC    F'00'
         DC    CL8'EXTENS#'
         DC    CL8'PLANT#'
DEFLIST2 DS    0F                      NAMES OF VARIABLES (LENGTH 40)
         DC    F'01'
         DC    F'00'
         DC    CL8'NAME#'
DEFLIST3 DS    0F                      NAMES OF VARIABLES (LENGTH 20)
         DC    F'01'
         DC    F'00'
         DC    CL8'LNAME#'
DEFLIST4 DS    0F                      NAMES OF VARIABLES (LENGTH 20)
         DC    F'01'
         DC    F'00'
         DC    CL8'FNAME#'
DEFLIST5 DS    0F                      NAMES OF VARIABLES (LENGTH 4)
         DC    F'02'
         DC    F'00'
         DC    CL8'EXTENS'
         DC    CL8'PLANT'
DEFLIST6 DS    0F                      NAMES OF VARIABLES (LENGTH 40)
         DC    F'01'
         DC    F'00'
         DC    CL8'NAME'
DEFLIST7 DS    0F                      NAMES OF VARIABLES (LENGTH 20)
         DC    F'01'
         DC    F'00'
         DC    CL8'LNAME'
DEFLIST8 DS    0F                      NAMES OF VARIABLES (LENGTH 20)
         DC    F'01'
         DC    F'00'
         DC    CL8'FNAME'
DEFLIST9 DS    0F                      NAMES OF VARIABLES (LENGTH 1)
         DC    F'01'
         DC    F'00'
         DC    CL8'SORTORD'
DEFLISTA DS    0F                      NAMES OF VARIABLES (LENGTH 24)
         DC    F'01'
         DC    F'00'
         DC    CL8'PHONMSGS'
DEFLISTB DS    0F                      NAMES OF VARIABLES (LENGTH 78)
         DC    F'01'
         DC    F'00'
         DC    CL8'PHONMSGL'
PHONERRS DC    CL24'PHONINQ program error.'
PHONERRL DC    CL78'ISPF Dialog Service, 12345678, ended with RC = 00. +
               Notify Systems Programming.'
SCNNERRS DC    CL24'No match found.'
         TITLE 'PHONINQ - LITERAL POOL'
         LTORG
         TITLE 'PHONINQ - DYNAMIC WORK AREA'
***********************************************************************
*  DYNAMIC WORK AREA
***********************************************************************
WORKAREA DSECT
SAVEAREA DS    18F                     PROGRAM SAVE AREA
ISPADDR  DS    A                       ADDRESS OF EP TO ISPLINK
PARMS    DS    7A                      PARM LIST FOR CALL
EXTENS$  DS    CL4                     BUILDING OR PLANT NUM (SELECTN)
PLANT$   DS    CL4                     BUILDING OR PLANT NUM (SELECTN)
NAME$    DS    CL40                    NAME (SELECTION)
LNAME$   DS    CL20                    LAST NAME (SELECTION)
FNAME$   DS    CL20                    FIRST NAME (SELECTION)
EXTENS_  DS    CL4                     EXTENSION NUMBER (PHONETAB)
PLANT_   DS    CL4                     BUILDING OR PLANT NUM (PHONETAB)
NAME_    DS    CL40                    NAME (PHONETAB)
LNAME_   DS    CL20                    LAST NAME (PHONETAB)
FNAME_   DS    CL20                    FIRST NAME (PHONETAB)
SORTORD_ DS    CL1                     SORTORD (PHONETAB)
DISPFLAG DS    XL1                     DISPLAY TYPE FLAGS
PHNMSGS_ DS    CL24                    SHORT VERSION OF ISPF ERROR MSG
PHNMSGL_ DS    0CL78                   LONG VERSION OF ISPF ERROR MSG
         DS    CL21
PHNID_   DS    CL8
         DS    CL18
PHNRC_   DS    CL2
         DS    CL21
WORKLEN  EQU   *-WORKAREA
         END   PHONINQ
