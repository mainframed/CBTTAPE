         TITLE 'DELUXE JOB STATEMENT ACCOUNTING FIELD SCAN EXIT - PREAM+
               BLE'
**********************************************************************
*                                                                    *
*           DELUXE JOB STATEMENT ACCOUNTING FIELD SCAN EXIT 3        *
*                                                                    *
*    PURPOSE:                                                        *
*       THE EXIT AS IMPLEMENTED AT DELUXE HAS THE PURPOSE            *
*       ENFORCING DELUXE ACCOUNT FIELD STANDARDS WHICH ARE:          *
*       ACCOUNT FIELD IS FOUR CHARACTERS OF THE FORM "ABCN".         *
*           A     MUST BE AN "M" "P" "G" OR "E"                      *
*           B     MUST BE ALPHABETIC                                 *
*           C     MUST BE ALPHABETIC                                 *
*           N     MUST BE NUMERIC                                    *
*       IF ANY ONE OF THE ABOVE IS NOT FOLLOWED, THE JOB IS          *
*       CANCELED WITH APPROPRIATE MESSAGE.                           *
*                                                                    *
*    ENTRY POINT = DLXXIT03                                          *
*                                                                    *
*    INPUT (REGISTERS):                                              *
*       R0     LENGTH OF ACCOUNTING FIELD IN BYTES; IF NO            *
*              ACCOUNTING FIELD IS SPECIFIED, THIS LENGTH IS 0       *
*       R1     POINTER TO A 3-WORD PARAMETER LIST:                   *
*              WORD1 (+0) - ADDR OF ACCNTING FIELD (JCTWORK IN JCT)  *
*              WORD2 (+4) - ADDR OF EXIT FLAG BYTE, RDWFLAGX, IN PCE *
*              WORD3 (+8) - ADDR OF JCTXWRK FIELD IN JCT             *
*       R2-R9  N/A                                                   *
*       R10    ADDRESS OF THE JCT                                    *
*       R11    ADDRESS OF THE HCT                                    *
*       R12    N/A                                                   *
*       R13    ADDRESS OF THE HASPRDR PCE                            *
*       R14    RETURN ADDRESS                                        *
*       R15    ENTRY ADDRESS                                         *
*                                                                    *
*    RETURN (REGISTERS);                                             *
*       R0-R1  N/A                                                   *
*       R2-14 SHOULD CONTAIN THE SAME CONTENTS AS ON ENTRY           *
*       R15   CONTAINS A RETURN CODE AS FOLLOWS:                     *
*             =0  CONTINUE WITH ADDITIONAL EXITS/HASPRDR PROCESSING. *
*             =4  BYPASS EXITS. USE ACCTFLD ON JOBDEF                *
*             =8  BYPASS HASPRSCN. COMPLETE HASPRJCS PROCESSING      *
*             =12 CANCEL JOB. PRODUCE OUTPUT. BYPASS HASPRSCN.       *
*                                                                    *
*    JES2 MACROS USED:                                               *
*        $ENTRY, $SAVE, $RETURN, $MODEND, $HASPGBL                   *
*                                                                    *
*    CHANGE ACTIVITY:                                                *
*        02/02/87  WRITTEN.                                    JWC.  *
*                                                                    *
**********************************************************************
         EJECT
         COPY  $HASPGBL                 COPY HASP GLOBALS
JAFEX003 $MODULE $BUFFER,                                              +
               $HASPEQU,                REQUIRED FOR REG CONVENTIONS   +
               $HCT,                    REQ'D BY $SAVE,$RETURN,ETC.    +
               $JCT,                    REQ'D FOR MAPPING              +
               $JOE,                    REQ'D BY $COMWORK              +
               $MIT,                    REQ'D BY $MODULE               +
               $PCE,                    HASP PROCESSOR CONTROL ELEMENT +
               $RDRWORK,                HASP INPUT SERVICE PCE WORK    +
               RPL                      REQ'D BY $BUFFER
         TITLE 'DELUXE JOB STATEMENT ACCOUNTING FIELD SCAN EXIT'
         SPACE 1
***********************************************************************
*        JES2 - INITIALIZATION AND ADDRESSABILITY
***********************************************************************
DLXXIT03 $ENTRY BASE=R12                EXIT ROUTINE ENTRY POINT
         $SAVE
         LR    R12,R15                  LOAD LOCAL BASE REG
         SLR   R15,R15                  CLEAR REG 15
         USING JCT,R10                  CLEAR REG 15
         CLC   JOB,JCTJOBID             THIS A BATCH JOB?
         BNE   RETURN                   NO, RETURN
         TM    JCTUSER0+2,EXIT003       EXIT 03 PROCESSING COMPLETE?
         BO    RETURN                   YES, RETURN
         C     R0,FULL00                ACCOUNTING FIELD SUPPLIED?
         BNE   ACCT00                   YES, VALIDATE JOB ACCTNG FIELDS
         MVC   JCTXWRK,BADACCT1         ISSUE MESSAGE
         B     CANJOB                   CANCEL JOB AND RETURN
***********************************************************************
*        JES2 - VALIDATE JOB ACCOUNTING FIELDS
***********************************************************************
ACCT00   EQU   *
         L     R2,0(R1)                 ADDR OF ACCOUNTING FIELD IN JCT
         CLC   LPAREN,0(R2)             LEFT PARENTHESIS?
         BNE   ACCT10                   NO, NO NEED TO INCREMENT R2
         LA    R2,1(R2)                 POINT R2 PAST LEFT PARENTHESIS
ACCT10   EQU   *
         CLC   CHAR_E,0(R2)             VALID 1ST CHARACTER?
         BE    ACCT20                   YES, CHECK 2ND CHARACTER
         CLC   CHAR_G,0(R2)             VALID 1ST CHARACTER?
         BE    ACCT20                   YES, CHECK 2ND CHARACTER
         CLC   CHAR_M,0(R2)             VALID 1ST CHARACTER?
         BE    ACCT20                   YES, CHECK 2ND CHARACTER
         CLC   CHAR_P,0(R2)             VALID 1ST CHARACTER?
         BE    ACCT20                   YES, CHECK 2ND CHARACTER
         MVC   JCTXWRK,BADACCT2         ISSUE MESSAGE
         B     CANJOB                   CANCEL JOB AND RETURN
ACCT20   EQU   *
         CLC   SCHAR1,1(R2)             VALID 2ND CHARACTER?
         BE    ACCT30                   NO, ISSUE MESSAGE & RETURN
         CLC   SCHAR2,1(R2)             VALID 2ND CHARACTER?
         BE    ACCT30                   NO, ISSUE MESSAGE & RETURN
         CLC   SCHAR3,1(R2)             VALID 2ND CHARACTER?
         BE    ACCT30                   NO, ISSUE MESSAGE & RETURN
         TM    1(R2),NUMERIC            VALID 2ND CHARACTER?
         BO    ACCT30                   NO, ISSUE MESSAGE & RETURN
         TM    1(R2),ALPHA              VALID 2ND CHARACTER?
         BNO   ACCT30                   NO, ISSUE MESSAGE & RETURN
         CLC   SCHAR1,2(R2)             VALID 3RD CHARACTER?
         BE    ACCT30                   NO, ISSUE MESSAGE & RETURN
         CLC   SCHAR2,2(R2)             VALID 3RD CHARACTER?
         BE    ACCT30                   NO, ISSUE MESSAGE & RETURN
         CLC   SCHAR3,2(R2)             VALID 3RD CHARACTER?
         BE    ACCT30                   NO, ISSUE MESSAGE & RETURN
         TM    2(R2),ALPHA              VALID 3RD CHARACTER?
         BNO   ACCT30                   NO, ISSUE MESSAGE & RETURN
         CLC   SCHAR4,3(R2)             VALID 4TH CHARACTER?
         BE    ACCT40                   NO, ISSUE MESSAGE & RETURN
         TM    3(R2),NUMERIC            VALID 4TH CHARACTER?
         BNO   ACCT40                   NO, ISSUE MESSAGE & RETURN
         B     RETURN                   OK, WE'RE DONE
ACCT30   EQU   *
         MVC   JCTXWRK,BADACCT3         ISSUE MESSAGE
         B     CANJOB                   CANCEL JOB AND RETURN
ACCT40   EQU   *
         MVC   JCTXWRK,BADACCT4         ISSUE MESSAGE
         B     CANJOB                   CANCEL JOB AND RETURN
***********************************************************************
*        JES2 - CANCEL JOB
***********************************************************************
CANJOB   EQU   *
         L     R2,4(R1)                 ADDRESS OF EXIT FLAG BYTE
         OI    0(R2),RDWXXSNC           WE'VE SUPPLIED NEXT STMT IMAGE
         OI    JCTUSER4,BYTEFF          JOB WILL BE CANCELED IN EXIT020
***********************************************************************
*        JES2 - RESTORE REGISTERS AND RETURN
***********************************************************************
RETURN   EQU   *
         OI    JCTUSER0+2,EXIT003       EXIT 03 PROCESSING COMPLETE
         $RETURN RC=(R15)               RETURN TO CALLER
***********************************************************************
*        JES2 - FLAGS
***********************************************************************
EXIT003  EQU   X'02'                    EXIT 03 PROCESSING COMPLETE
ALPHA    EQU   X'C0'                    INDICATES ALPHABETIC CHAR
NUMERIC  EQU   X'F0'                    INDICATES NUMERIC CHAR
BYTEFF   EQU   X'FF'                    BYTE CONSTANT, VALUE X'FF'
***********************************************************************
*        JES2 - CONSTANTS
***********************************************************************
FULL00   DC    F'00'                    FULLWORD CONSTANT, VALUE X'00'
BADACCT1 DC    CL80'//*** ERROR ***  NO ACCOUNTING FIELD SUPPLIED ON TH+
               E JOB STATEMENT              '
BADACCT2 DC    CL80'//*** ERROR ***  ILLEAGAL 1ST CHARACTER IN ACCOUNTI+
               NG FIELD. (''E'',''G'',''M'',''P'')  '
BADACCT3 DC    CL80'//*** ERROR ***  2ND AND 3RD CHARS OF JOB ACCOUNTIN+
               G FIELD MUST BE ALPHABETIC   '
BADACCT4 DC    CL80'//*** ERROR ***  4TH CHARACTER OF JOB ACCOUNTING FI+
               ELD MUST BE NUMERIC          '
JOB      DC    C'JOB'                   JCTJOBID FOR BATCH JOB
CHAR_E   DC    C'E'                     CHARACTER CONSTANT
CHAR_G   DC    C'G'                     CHARACTER CONSTANT
CHAR_M   DC    C'M'                     CHARACTER CONSTANT
CHAR_P   DC    C'P'                     CHARACTER CONSTANT
SCHAR1   DC    C'{'                     CHARACTER CONSTANT
SCHAR2   DC    C'}'                     CHARACTER CONSTANT
SCHAR3   DC    C'\'                     CHARACTER CONSTANT
SCHAR4   DC    C'|'                     CHARACTER CONSTANT
LPAREN   DC    C'('                     CHARACTER CONSTANT
         LTORG
         $MODEND
         END  ,
