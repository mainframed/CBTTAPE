SEPEXT01 TITLE 'JES2 USER EXIT 1 - PREAMBLE'
************************************************************
*        JES2 SEPARATOR PAGE EXIT
*
*        PURPOSE:
*
*
*        ENTRY POINT =
*
*        INPUT REGISTORS :
*              R0   N/A
*              R1   ADDRESS OF PRINTER OR PUNCH DCT
*              R2   2ND BASE REGISTER
*              R3   ADDRESS OF BUFFER
*              R4-6 WORK AND LINKAGE REGISTERS
*              R7   ADDRESS OF JOE
*              R8-9 WORK AND LINKAGE REGISTERS
*              R10  ADDRESS OF THE JOB'S JCT
*              R11  ADDRESS OF THE JES2 HCT
*              R12  LOCAL BASE REGISTER
*              R13  ADDRESS OF THE PRINTER OR PUNCH PCE
*              R14  RETURN ADDRESS
*              R15  ENTRY ADDRESS
*
*        RETURN REGISTERS :
*              R0-14 SHOULD CONTAIN THE SAME CONTENTS AS ON ENTRY
*              R15   CONTAINS A RETURN CODE OS FOLLOWS:
*                   =0  JES2 WILL PRODUCE THE STANDARD SEPARATOR
*                   =4  SAME AS 0;  BUT CALL NO OTHER EXIT ROUTINES
*                   =8  JES2 WILL UNCONDITIONALLY NOT PRINT A SEPARATOR
*                   =12 JES2 WILL UNCONDITIONALLY PRINT A SEPARATOR
*
*        JES2 MACROS USED :
*              $ENTRY, $SAVE, $RETURN, $MODEND,
*              $HASPGBL
***********************************************************************
         EJECT
         COPY  $HASPGBL                COPY HASP GLOBALS
SEPEXT01       $MODULE RPL,                                            +
               $BUFFER,                                                +
               $CAT,                   REQUIRED BY HCT                 +
               $DCT,                                                   +
               $HASPEQU,               REQUIRED BY REG CONVENTIONS     +
               $HCT,                   REQUIRED BY $SAVE,$RETURN, ETC. +
               $JCT,                   REQUIRED BY $CAT                +
               $JOE,                   REQUIRED TO GET SYSOUT CLASS    +
               $JQE,                   REQUIRED BY HCT                 +
               $MIT,                   REQUIRED BY $MODEND             +
               $PCE,                   REQUIRED BY HCT                 +
               $PDDB,                                                  +
               $PPPWORK                REQUIRED TO FIND JOE
               SPACE 1
               TITLE  'PRINT/PUNCH SEPARATOR EXIT'
         USING DCT,R1                  ESTABLISH DCT ADDRESSABILITY
         USING BFPDSECT,R3             ESTABLISH BUFFER ADDRESSABILITY
         USING JOE,R7                  ESTABLISH JOE ADDRESSABILITY
         USING JCT,R10                 ESTABLISH JCT ADDRESSABILITY
         USING PCE,R13                 ESTABLISH PCE ADDRESSABILITY
               SPACE 1
DLXXIT01 $ENTRY BASE=R12               EXIT ROUTINE ENTRY POINT
         $SAVE                         SAVE HASPPRPU REGISTERS
         LR    R12,R15                 SET LOCAL BASE REGISTER
         SLR   R15,R15                 SET ZERO RETURN CODE (DEFAULT)
         CLI   DCTDEVTP,DCTPRT         IS THIS A LOCAL PRINTER?
         BNE   RETURN                  IF NOT, IGNORE IT
         L     R7,PPPWKJOE             SET WORK JOE BASE
         CLI   JOECURCL,C'G'           IS SYSOUT CLASS G?
         BE    BEGIN                   IF YES, CONTINUE
         CLI   JOECURCL,C'U'           IS SYSOUT CLASS U?
         BE    BEGIN                   IF YES, CONTINUE
         CLI   JOECURCL,C'W'           IS SYSOUT CLASS W?
         BNE   RETURN                  IF NOT, GET OUT
BEGIN    EQU   *
         ST    R0,$DOUBLE              STORE START/CONT/END INDICATOR
         SPACE 1
         $GETBUF TYPE=HASP,FIX=YES,WAIT=YES  GET A WORK BUFFER
         LR    R3,R1                   SAVE ADDRESS OF BUFFER
         SPACE 1
CHKTYPE  EQU   *                       DECIDE WHICH PROCESSING -
         L     R0,$DOUBLE              RESTORE START/CONT/END INDICATOR
         LTR   R0,R0                   IS IT START?
         BZ    BEGSEP                  GO PROCESS
         CH    R0,=H'4'                IS IT CONTINUE?
         BE    CONTSEP                 GO PROCESS
         B     ENDSEP                  MUST BE A TRAILER.
BEGSEP   EQU   *
         BAL   R6,SEPPGE1              PRINT BEGINNING SEP PAGE
         B     FORMS                   GO TO PRINT FORM
CONTSEP  EQU   *                       CONTINUATION SEPERATOR
         BAL   R6,SEPPGE1              PRINT CONTINUATION  PAGE
         B     FORMS                   GO TO PRINT FORM
ENDSEP   EQU   *                       CONTINUATION SEPERATOR
         BAL   R6,SEPPGE1              PRINT TRAILER   SEP PAGE
         B     FREEBUF                 FREE THE BUFFER AND EXIT
FORMS    EQU   *
         L     R9,=V(FORMTABL)         R9 <-- ADDRESS OF FORM TABLE
         ST    R9,PCEUSER0             SAVE BASE ADDRESS OF FORMTABL
         BAL   R6,SENDDJDE             SEND DJDE'S TO XEROX PRINTER
         B     FREEBUF                 FREE THE BUFFER AND EXIT
FREEBUF  EQU   *
         $FREEBUF (R3)
         B     CLOSIT8                 TELL JES2 TO SUPPRESS THE SEP
CLOSIT0  EQU   *
         SR    R15,R15                 TELL JES2 TO PRODUCE STD SEP
         B     RETURN
CLOSIT4  EQU   *
         LA    R15,4                   TELL JES2 NO OTHER EXITS TO GO
         B     RETURN
CLOSIT8  EQU   *
         LA    R15,8                   TELL JES2 TO SUPPRESS THE SEP
         B     RETURN
CLOSIT12 EQU   *
         LA    R15,12                  TELL JES2 TO UNCOND PRT SEP
         B     RETURN
SEPPGE1  EQU   *
         MVI   BUFSTART,X'40'          BLANK OUT BUFFER
         MVC   BUFSTART+1(132),BUFSTART BLANK OUT BUFFER
         MVC   BUFSTART(80),DXSEP1     SEND FIRST SEPPAGE DJDE
         BAL   R8,JG$PRPUT             SEND SEPERATOR PAGE BORDER
         MVC   BUFSTART(80),DXSEP2     SEND SECOND SEPPAGE DJDE
         BAL   R8,JG$PRPUT             SEND SEPERATOR PAGE BORDER
SENDSEP2 EQU   *
         MVI   BUFSTART,X'40'          BLANK OUT BUFFER
         MVC   BUFSTART+1(132),BUFSTART BLANK OUT BUFFER
         LA    R1,BUFSTART             R1 <--- ADDRESS OF PRINTLINE
         LA    R0,80                   R0 <--- LENGTH OF PRINTLINE
         $PRPUT DATA=(R1),LEN=(R0),WAIT=YES,COUNT=9
         MVC   BUFSTART+13(8),JCTJOBID  MOVE JOB NUMBER
         MVC   BUFSTART+29(8),JCTJNAME  MOVE JOB NAME
         BAL   R8,JG$PRPUT             SEND SEPERATOR PAGE
SENDSEP3 EQU   *
         MVI   BUFSTART,X'40'          BLANK OUT BUFFER
         MVC   BUFSTART+1(132),BUFSTART BLANK OUT BUFFER
         L     R0,$DOUBLE              RESTORE START/CONT/END INDICATOR
         LTR   R0,R0                   IS IT START?
         BZ    SEPSTART                PRINT START OF JOB ENTRY
         CH    R0,=H'4'                IS IT CONTINUE?
         BE    SEPCONT                 PRINT CONTINUATION OF JOB ENTRY
         B     SEPEND                  MUST BE A TRAILER.
SEPSTART EQU   *
         MVC   BUFSTART+13(12),=C'START OF JOB'
         BAL   R8,JG$PRPUT             SEND SEPERATOR PAGE
         B     SEPNAME                 BRANCH TO CALL EXIT
SEPCONT  EQU   *
         MVC   BUFSTART+13(19),=C'CONTINUATION OF JOB'
         BAL   R8,JG$PRPUT             SEND SEPERATOR PAGE
         B     SEPNAME                 BRANCH TO CALL EXIT
SEPEND   EQU   *
         MVC   BUFSTART+13(10),=C'END OF JOB'
         BAL   R8,JG$PRPUT             SEND SEPERATOR PAGE
         B     SEPNAME                 BRANCH TO CALL EXIT
SEPNAME  EQU   *
         MVI   BUFSTART,X'40'          BLANK OUT BUFFER
         MVC   BUFSTART+1(132),BUFSTART BLANK OUT BUFFER
         BAL   R8,JG$PRPUT             PRINT BLANK LINE
         MVI   BUFSTART,X'40'          BLANK OUT BUFFER
         MVC   BUFSTART+1(132),BUFSTART BLANK OUT BUFFER
         MVC   BUFSTART+13(20),JCTPNAME  MOVE PROGRAMMER NAME
         MVC   BUFSTART+38(4),JCTROOMN  MOVE ROOM NUMBER
         BAL   R8,JG$PRPUT             SEND SEPERATOR PAGE
         B     SEPEXIT                 BRANCH TO SEPARATOR EXIT
SEPEXIT  EQU   *                       SEP PAGE EXIT
         BR    R6                      RETURN TO CALLER
SENDDJDE DS    0D
         L     R1,PPPCHJOE             GET ADDRESS OF CHAR JOE
         L     R9,PCEUSER0             GET ADDRESS OF FORMTABL BASE
         SPACE
SENDTEST EQU   *
         CLC   =4X'FF',0(R9)           IF FORMS TABLE NOT OUT?
         BNE   SENDCHCK                GET NEXT FCRTE FORM THE TABLE
         L     R9,PCEUSER0             GO BACK TO BEGINNING OF TABLE
         LA    R9,0(,R9)               PASS THE FORM 'STD ' BY DEFAULT
         B     SENDDFIT                THEN VERIFY THE BACKSIDE
         SPACE
SENDCHCK EQU   *
         CLC   0(4,R9),JOEFORM-JOE(R1)       IF MATCHING FCRTE FOUND?
         BE    SENDDFIT                SEND 'DJDE TO XEROX PRINTER
         LA    R9,16(,R9)              ELSE GET NEXT FCRTE
         B     SENDTEST                SEE IF THE FORM MATCH
         SPACE
SENDDFIT EQU   *
         L     R4,4(,R9)               R4 <-- A(DFITE)
         LA    R4,0(,R4)            ERASE THE CONTROL BYTE
         MVC   BUFSTART(80),0(R4)      MOVE DFITE TO THE BUFFER
         BAL   R8,JG$PRPUT             SEND DJDE DFITE
         SPACE
* <<<<<<<<<<<<<<<<<<<<<
         CLC   DCTFORMS,JOEFORM-JOE(R1)  IS PRINT FORM EQ TO JOEFORM
         BE    SENDDVIT                  YES, THEN BYPASS WAIT MSG DJDE
         CLC   12(1,R9),PAPERCHG         CHK CURRENT AND OLD PAPER FLAG
         BNE   SENDWAIT                  NO ,THEN PRINT WAIT MSG DJDE
         CLI   12(R9),X'11'              IS IT SPECIAL FORM?
         BNE   SENDDVIT                  IF NOT THEN BYPASS WAIT MSG.
*  <<<<<<<<<<<<<<<<<<<<
SENDWAIT EQU   *
         CLI   8(R9),X'00'             IS IT STANDARD PRINT FORM
         BNE   MSG10                   NO, THEN ISSUE SPEC FORM MSG.
STDMSG   EQU   *
         MVC   BUFSTART(80),JOBMSG1    MOVE MASTER JOBMSG TEXT TO BUF
         MVC   BUFSTART+20(8),JCTJNAME-JCT(R10)       JOB LOG
         MVC   BUFSTART+34(4),0(R9)    ADD FORM TO JOB MSG
         B     CONTWAIT
MSG10    EQU   *
         CLI   8(R9),X'10'             IS IT 3-HOLE SINGLE LANDSCAPE
         BNE   MSG11                   NO, GO ON
         MVC   BUFSTART(80),JOBMSG2    MOVE JOBMSG TEXT TO BUFFER
         B     CONTWAIT
MSG11    EQU   *
         CLI   8(R9),X'11'             IS IT 3-HOLE DUPLX LANDSCAPE
         BNE   MSG12                   NO, GO ON
         MVC   BUFSTART(80),JOBMSG3    MOVE JOBMSG TEXT TO BUFFER
         B     CONTWAIT
MSG12    EQU   *
         CLI   8(R9),X'12'             IS IT 3-HOLE SINGLE PORTRAIT
         BNE   MSG13                   NO, GO ON
         MVC   BUFSTART(80),JOBMSG4    MOVE JOBMSG TEXT TO BUFFER
         B     CONTWAIT
MSG13    EQU   *
         CLI   8(R9),X'13'             IS IT 3-HOLE DUPLX PORTRAIT
         BNE   STDMSG                  NO, GO ON
         MVC   BUFSTART(80),JOBMSG5    MOVE JOBMSG TEXT TO BUFFER
         B     CONTWAIT
CONTWAIT EQU   *
         BAL   R8,JG$PRPUT             ISSUE JOBLOG MSG TO XEROX CONSL
*  <<<<<<<<<<<<<<<<<<<<
         SPACE
SENDDVIT EQU   *
*  <<<<<<<<<<<<<<<<<<<<
         MVC   PAPERCHG(1),12(R9)      MOVE PAPER FLAG TO HOLD AREA
         MVI   12(R9),X'00'            RESET CHANGE PAPER FLAG
*  <<<<<<<<<<<<<<<<<<<<
         L     R4,12(,R9)              R4 <---- A(DVITE)
         LA    R4,0(,R4)             ADJUST FOR TABLE BASE
         LH    R5,0(,R4)               R5 <---- A(DVITE'S)
         LA    R4,2(,R4)               POSITION R4 TO HEAD OF DVITE
         SPACE
SENDFINI EQU   *
         MVC   BUFSTART(80),0(R4)      MOVE THE DVITE TO BUFFER
         BAL   R8,JG$PRPUT             SEND DJDE VFU IMAGE TABLE ENTRY
         LA    R4,80(,R4)              GET NEXT DVITE
         BCT   R5,SENDFINI             CHAIN IN NEXT DVITE
         SPACE
SENDEXIT EQU   *                       SENDDJDE EXIT
         BR    R6                      RETURN TO THE CALLER
JG$PRPUT DS    0D
         LA    R1,BUFSTART             R1 <--- ADDRESS OF PRINTLINE
         LA    R0,80                   R0 <--- LENGTH OF PRINTLINE
         $PRPUT DATA=(R1),LEN=(R0),WAIT=YES
         BR    R8                      RETURN TO CALLER
**********************************************************************
* +----------------------------------------------------------------+ *
* |            PAGE SETUP MESSAGE TABLE ENTRY                      + *
* +----------------------------------------------------------------+ *
**********************************************************************
         SPACE 2
DXSEP1  DC CL80'??DJDE  FORMS=SEPPGE,FORMAT=ADL007,JDL=ADMIN,TOF=1,;'
DXSEP2  DC CL80'??DJDE  ASSIGN=(1,1),DATA=(0,50),BOF=23,DUPLEX=NO,END;'
JOBMSG1 DC CL80'??DJDE  OTEXT=(''-0- XXXXXXXX FORM FFFF'',WAIT),;'
JOBMSG2 DC CL80'??DJDE  OTEXT=(''3-HOLE PUNCH SINGLE L017'',WAIT),;'
JOBMSG3 DC CL80'??DJDE  OTEXT=(''3-HOLE PUNCH DUPLEX L018'',WAIT),;'
JOBMSG4 DC CL80'??DJDE  OTEXT=(''3-HOLE PUNCH SINGLE P014'',WAIT),;'
JOBMSG5 DC CL80'??DJDE  OTEXT=(''3-HOLE PUNCH DUPLEX P015'',WAIT),;'
PAPERCHG DS    CL1                     PAPER CHANGE FLAG
         SPACE
RETURN   $RETURN RC=(R15)              RETURN TO CALLER
         SPACE 1
         DROP R1                       DROP ADDRESSABILITY OF DCT
         DROP R3                       DROP ADDRESSABILITY OF BUF
         DROP R7                       DROP ADDRESSABILITY OF JOE
         DROP R13                      DROP ADDRESSABILITY OF PCE
         $MODEND
LTORG    DS    0D
         LTORG
         END
* +----------------------------------------------------------------+ *
* | F O R M S  C R O S S  R E F E R E N C E  T A B L E  E N T R Y  | *
* +----------------------------------------------------------------+ *
**********************************************************************
*                                                                    *
*             FORMS NAME (4 BYTES)                                   *
*             |        COPY COUNT (1 BYTE)                           *
*             |        | ADDRESS(DFITE) (3 BYTES)                    *
*             |        | |      RESERVED  (1 BYTE)                   *
*             |        | |      | RESERVED (3 BYTES)                 *
*             |        | |      | |      PAPER CHANGE (1 BYTE)       *
*             |        | |      | |      | ADDRESS(DVITE) (3 BYTES)  *
*             |        | |      | |      | |                         *
*            +|--------|-|------|-|------|-|-----+                   *
*            |FORMNAME|NN......|DD......|II......|                   *
*            +-----------------------------------+                   *
**********************************************************************
         SPACE 2
FORMTABL CSECT
XSTDP  DC  C'STD ',X'00',AL3(FITL01),X'00',AL3(0),X'00',AL3(VFU1)
XL001  DC  C'L001',X'00',AL3(FITL01),X'00',AL3(0),X'00',AL3(VFU2)
XL002  DC  C'L002',X'00',AL3(FITL02),X'00',AL3(0),X'00',AL3(VFU4)
XL003  DC  C'L003',X'00',AL3(FITL02),X'00',AL3(0),X'00',AL3(VFU5)
XL004  DC  C'L004',X'00',AL3(FITL03),X'00',AL3(0),X'00',AL3(VFU6)
XL005  DC  C'L005',X'00',AL3(FITL03),X'00',AL3(0),X'00',AL3(VFU7)
XL006  DC  C'L006',X'00',AL3(FITL04),X'00',AL3(0),X'00',AL3(VFU10)
XL007  DC  C'L007',X'00',AL3(FITL05),X'00',AL3(0),X'00',AL3(VFU4)
XL008  DC  C'L008',X'00',AL3(FITL05),X'00',AL3(0),X'00',AL3(VFU5)
XL009  DC  C'L009',X'00',AL3(FITL06),X'00',AL3(0),X'00',AL3(VFU10)
XL010  DC  C'L010',X'00',AL3(FITL06),X'00',AL3(0),X'00',AL3(VFU11)
XL011  DC  C'L011',X'00',AL3(FITL07),X'00',AL3(0),X'00',AL3(VFU12)
XL012  DC  C'L012',X'00',AL3(FITL07),X'00',AL3(0),X'00',AL3(VFU13)
XL013  DC  C'L013',X'00',AL3(FITL08),X'00',AL3(0),X'00',AL3(VFU16)
XL014  DC  C'L014',X'00',AL3(FITL08),X'00',AL3(0),X'00',AL3(VFU17)
XL015  DC  C'L015',X'00',AL3(FITL08),X'00',AL3(0),X'00',AL3(VFU1)
XL016  DC  C'L016',X'00',AL3(FITL08),X'00',AL3(0),X'00',AL3(VFU2)
XL017  DC  C'L017',X'00',AL3(FITL08),X'10',AL3(0),X'11',AL3(VFU16)
XL018  DC  C'L018',X'00',AL3(FITL08),X'11',AL3(0),X'11',AL3(VFU17)
XP001  DC  C'P001',X'00',AL3(FITP01),X'00',AL3(0),X'00',AL3(VFU6)
XP002  DC  C'P002',X'00',AL3(FITP01),X'00',AL3(0),X'00',AL3(VFU7)
XP003  DC  C'P003',X'00',AL3(FITP02),X'00',AL3(0),X'00',AL3(VFU8)
XP004  DC  C'P004',X'00',AL3(FITP02),X'00',AL3(0),X'00',AL3(VFU9)
XP005  DC  C'P005',X'00',AL3(FITP03),X'00',AL3(0),X'00',AL3(VFU6)
XP006  DC  C'P006',X'00',AL3(FITP03),X'00',AL3(0),X'00',AL3(VFU7)
XP007  DC  C'P007',X'00',AL3(FITP04),X'00',AL3(0),X'00',AL3(VFU6)
XP008  DC  C'P008',X'00',AL3(FITP05),X'00',AL3(0),X'00',AL3(VFU6)
XP009  DC  C'P009',X'00',AL3(FITP05),X'00',AL3(0),X'00',AL3(VFU7)
XP010  DC  C'P010',X'00',AL3(FITP06),X'00',AL3(0),X'00',AL3(VFU6)
XP011  DC  C'P011',X'00',AL3(FITP06),X'00',AL3(0),X'00',AL3(VFU7)
XP012  DC  C'P012',X'00',AL3(FITP07),X'00',AL3(0),X'00',AL3(VFU14)
XP013  DC  C'P013',X'00',AL3(FITP07),X'00',AL3(0),X'00',AL3(VFU15)
XP014  DC  C'P014',X'00',AL3(FITP01),X'12',AL3(0),X'11',AL3(VFU6)
XP015  DC  C'P015',X'00',AL3(FITP01),X'13',AL3(0),X'11',AL3(VFU7)
XC1A   DC  C'C1A ',X'00',AL3(FITPM1),X'00',AL3(0),X'11',AL3(VFU3)
XC1B   DC  C'C1B ',X'00',AL3(FITPM1),X'00',AL3(0),X'11',AL3(VFU3)
XC2    DC  C'C2  ',X'00',AL3(FITPM1),X'00',AL3(0),X'11',AL3(VFU3)
XC3A   DC  C'C3A ',X'00',AL3(FITPM2),X'00',AL3(0),X'11',AL3(VFU3)
XC3B   DC  C'C3B ',X'00',AL3(FITPM2),X'00',AL3(0),X'11',AL3(VFU3)
XC3C   DC  C'C3C ',X'00',AL3(FITPM2),X'00',AL3(0),X'11',AL3(VFU3)
XC4    DC  C'C4  ',X'00',AL3(FITPM2),X'00',AL3(0),X'11',AL3(VFU3)
XC5A   DC  C'C5A ',X'00',AL3(FITPM2),X'00',AL3(0),X'11',AL3(VFU3)
XC5B   DC  C'C5B ',X'00',AL3(FITPM2),X'00',AL3(0),X'11',AL3(VFU3)
XC5C   DC  C'C5C ',X'00',AL3(FITPM2),X'00',AL3(0),X'11',AL3(VFU3)
XT001  DC  C'T001',X'00',AL3(FITT01),X'00',AL3(0),X'00',AL3(VFU18)
XX001  DC  C'X001',X'00',AL3(FITXICS),X'00',AL3(0),X'00',AL3(VFUXICS)
XX002  DC  C'X002',X'00',AL3(FITXICS),X'00',AL3(0),X'11',AL3(VFUXICS)
XDMY1  DC  C'DMY1',X'00',AL3(FITDY1),X'00',AL3(0),X'00',AL3(VFUD1)
XDMY2  DC  C'DMY2',X'00',AL3(FITDY2),X'00',AL3(0),X'00',AL3(VFUD2)
XDMY3  DC  C'DMY3',X'00',AL3(FITDY3),X'00',AL3(0),X'00',AL3(VFUD3)
XDMY4  DC  C'DMY4',X'00',AL3(FITDY4),X'00',AL3(0),X'00',AL3(VFUD4)
XDMY5  DC  C'DMY5',X'00',AL3(FITDY5),X'00',AL3(0),X'00',AL3(VFUD5)
XDMY6  DC  C'DMY6',X'00',AL3(FITDY6),X'00',AL3(0),X'00',AL3(VFUD6)
XDMY7  DC  C'DMY7',X'00',AL3(FITDY7),X'00',AL3(0),X'00',AL3(VFUD7)
XDMY8  DC  C'DMY8',X'00',AL3(FITDY8),X'00',AL3(0),X'00',AL3(VFUD8)
XDMY9  DC  C'DMY9',X'00',AL3(FITDY9),X'00',AL3(0),X'00',AL3(VFUD9)
XFFF   DC  16X'FF'                     END-OF-TABLE
         SPACE 2
**********************************************************************
* +----------------------------------------------------------------+ *
* |            DJDE FORMS IMAGE TABLE ENTRY                        + *
* +----------------------------------------------------------------+ *
**********************************************************************
         SPACE 2
FITL01   DC CL80'??DJDE  JDL=ADMIN,FORMAT=ADL001,DATA=(0,132),;'
FITL02   DC CL80'??DJDE  JDL=ADMIN,FORMAT=ADL002,DATA=(0,132),;'
FITL03   DC CL80'??DJDE  JDL=ADMIN,FORMAT=ADL003,DATA=(0,90),;'
FITL04   DC CL80'??DJDE  JDL=ADMIN,FORMAT=ADL004,DATA=(0,100),;'
FITL05   DC CL80'??DJDE  JDL=ADMIN,FORMAT=ADL005,DATA=(0,150),;'
FITL06   DC CL80'??DJDE  JDL=ADMIN,FORMAT=ADL006,DATA=(0,100),;'
FITL07   DC CL80'??DJDE  JDL=ADMIN,FORMAT=ADL007,DATA=(0,50),;'
FITL08   DC CL80'??DJDE  JDL=ADMIN,FORMAT=ADL008,DATA=(0,132),;'
FITP01   DC CL80'??DJDE  JDL=ADMIN,FORMAT=ADP001,DATA=(0,90),;'
FITP02   DC CL80'??DJDE  JDL=ADMIN,FORMAT=ADP002,DATA=(0,100),;'
FITP03   DC CL80'??DJDE  JDL=ADMIN,FORMAT=ADP003,DATA=(0,75),;'
FITP04   DC CL80'??DJDE  JDL=ADMIN,FORMAT=ADP004,DATA=(0,75),;'
FITP05   DC CL80'??DJDE  JDL=ADMIN,FORMAT=ADP005,DATA=(0,90),;'
FITP06   DC CL80'??DJDE  JDL=ADMIN,FORMAT=ADP006,DATA=(0,75),;'
FITP07   DC CL80'??DJDE  JDL=ADMIN,FORMAT=ADP007,DATA=(0,38),;'
FITPM1   DC CL80'??DJDE  JDL=ADMIN,FORMAT=ADPIM1,DATA=(0,80),;'
FITPM2   DC CL80'??DJDE  JDL=ADMIN,FORMAT=ADPIM2,DATA=(0,80),;'
FITT01   DC CL80'??DJDE  JDL=ADMIN,;'
FITXICS  DC CL80' '
FITDY1   DC CL80'??DJDE  JDL=ADMIN,FORMAT=DUMMY1,DATA=(0,132),;'
FITDY2   DC CL80'??DJDE  JDL=ADMIN,FORMAT=DUMMY1,DATA=(0,132),;'
FITDY3   DC CL80'??DJDE  JDL=ADMIN,FORMAT=DUMMY1,DATA=(0,132),;'
FITDY4   DC CL80'??DJDE  JDL=ADMIN,FORMAT=DUMMY1,DATA=(0,132),;'
FITDY5   DC CL80'??DJDE  JDL=ADMIN,FORMAT=DUMMY1,DATA=(0,132),;'
FITDY6   DC CL80'??DJDE  JDL=ADMIN,FORMAT=DUMMY1,DATA=(0,132),;'
FITDY7   DC CL80'??DJDE  JDL=ADMIN,FORMAT=DUMMY1,DATA=(0,132),;'
FITDY8   DC CL80'??DJDE  JDL=ADMIN,FORMAT=DUMMY1,DATA=(0,132),;'
FITDY9   DC CL80'??DJDE  JDL=ADMIN,FORMAT=DUMMY1,DATA=(0,132),;'
         SPACE 2
**********************************************************************
* +----------------------------------------------------------------+ *
* |            DJDE VFU   IMAGE TABLE ENTRY                        + *
* +----------------------------------------------------------------+ *
**********************************************************************
         SPACE 2
         DS    0D
VFU1     DC    H'2'                    STD LANDSCAPE DEFAULT
         DC    CL80'??DJDE  ASSIGN=(1,1),;'
         DC    CL80'??DJDE  TOF=1,BOF=66,COPIES=1,END;'
VFU2     DC    H'2'                    STD LANDSCAPE DUPLEX
         DC    CL80'??DJDE  ASSIGN=(1,1),DUPLEX=YES,;'
         DC    CL80'??DJDE  TOF=1,BOF=66,COPIES=1,END;'
VFU3     DC    H'2'                    PIMS PORTRAIT DEFAULT
         DC    CL80'??DJDE  ASSIGN=(1,4),ASSIGN=(2,10),;'
         DC    CL80'??DJDE  ASSIGN=(3,16),TOF=1,BOF=66,COPIES=1,END;'
VFU4     DC    H'2'
         DC    CL80'??DJDE  ASSIGN=(1,1),DUPLEX=NO,;'
         DC    CL80'??DJDE  TOF=1,BOF=88,COPIES=1,END;'
VFU5     DC    H'2'
         DC    CL80'??DJDE  ASSIGN=(1,1),DUPLEX=YES,;'
         DC    CL80'??DJDE  TOF=1,BOF=88,COPIES=1,END;'
VFU6     DC    H'2'
         DC    CL80'??DJDE  ASSIGN=(1,1),DUPLEX=NO,;'
         DC    CL80'??DJDE  TOF=1,BOF=60,COPIES=1,END;'
VFU7     DC    H'2'
         DC    CL80'??DJDE  ASSIGN=(1,1),DUPLEX=YES,;'
         DC    CL80'??DJDE  TOF=1,BOF=60,COPIES=1,END;'
VFU8     DC    H'2'
         DC    CL80'??DJDE  ASSIGN=(1,1),DUPLEX=NO,;'
         DC    CL80'??DJDE  TOF=1,BOF=80,COPIES=1,END;'
VFU9     DC    H'2'
         DC    CL80'??DJDE  ASSIGN=(1,1),DUPLEX=YES,;'
         DC    CL80'??DJDE  TOF=1,BOF=80,COPIES=1,END;'
VFU10    DC    H'2'
         DC    CL80'??DJDE  ASSIGN=(1,1),DUPLEX=NO,;'
         DC    CL80'??DJDE  TOF=1,BOF=49,COPIES=1,END;'
VFU11    DC    H'2'                    DMY9 DEFAULT
         DC    CL80'??DJDE  ASSIGN=(1,1),DUPLEX=YES,;'
         DC    CL80'??DJDE  TOF=1,BOF=49,COPIES=1,END;'
VFU12    DC    H'2'                    DMY9 DEFAULT
         DC    CL80'??DJDE  ASSIGN=(1,1),DUPLEX=NO,;'
         DC    CL80'??DJDE  TOF=1,BOF=23,COPIES=1,END;'
VFU13    DC    H'2'                    DMY9 DEFAULT
         DC    CL80'??DJDE  ASSIGN=(1,1),DUPLEX=YES,;'
         DC    CL80'??DJDE  TOF=1,BOF=23,COPIES=1,END;'
VFU14    DC    H'2'                    DMY9 DEFAULT
         DC    CL80'??DJDE  ASSIGN=(1,1),DUPLEX=NO,;'
         DC    CL80'??DJDE  TOF=1,BOF=28,COPIES=1,END;'
VFU15    DC    H'2'                    DMY9 DEFAULT
         DC    CL80'??DJDE  ASSIGN=(1,1),DUPLEX=YES,;'
         DC    CL80'??DJDE  TOF=1,BOF=28,COPIES=1,END;'
VFU16    DC    H'2'
         DC    CL80'??DJDE  ASSIGN=(1,6),DUPLEX=NO,;'
         DC    CL80'??DJDE  TOF=1,BOF=66,COPIES=1,END;'
VFU17    DC    H'2'
         DC    CL80'??DJDE  ASSIGN=(1,6),DUPLEX=YES,;'
         DC    CL80'??DJDE  TOF=1,BOF=66,COPIES=1,END;'
VFU18    DC    H'2'
         DC    CL80'??DJDE  ASSIGN=(1,1),DUPLEX=NO,;'
         DC    CL80'??DJDE  TOF=1,BOF=66,COPIES=1,END;'
VFUD1    DC    H'2'                    DMY1 DEFAULT
         DC    CL80'??DJDE  ASSIGN=(1,1),DUPLEX=NO,;'
         DC    CL80'??DJDE  TOF=1,BOF=66,COPIES=1,END;'
VFUD2    DC    H'2'                    DMY2 DEFAULT
         DC    CL80'??DJDE  ASSIGN=(1,1),DUPLEX=NO,;'
         DC    CL80'??DJDE  TOF=1,BOF=66,COPIES=1,END;'
VFUD3    DC    H'2'                    DMY3 DEFAULT
         DC    CL80'??DJDE  ASSIGN=(1,1),DUPLEX=NO,;'
         DC    CL80'??DJDE  TOF=1,BOF=66,COPIES=1,END;'
VFUD4    DC    H'2'                    DMY4 DEFAULT
         DC    CL80'??DJDE  ASSIGN=(1,1),DUPLEX=NO,;'
         DC    CL80'??DJDE  TOF=1,BOF=66,COPIES=1,END;'
VFUD5    DC    H'2'                    DMY5 DEFAULT
         DC    CL80'??DJDE  ASSIGN=(1,1),DUPLEX=NO,;'
         DC    CL80'??DJDE  TOF=1,BOF=66,COPIES=1,END;'
VFUD6    DC    H'2'                    DMY6 DEFAULT
         DC    CL80'??DJDE  ASSIGN=(1,1),DUPLEX=NO,;'
         DC    CL80'??DJDE  TOF=1,BOF=66,COPIES=1,END;'
VFUD7    DC    H'2'                    DMY7 DEFAULT
         DC    CL80'??DJDE  ASSIGN=(1,1),DUPLEX=NO,;'
         DC    CL80'??DJDE  TOF=1,BOF=66,COPIES=1,END;'
VFUD8    DC    H'2'                    DMY8 DEFAULT
         DC    CL80'??DJDE  ASSIGN=(1,1),DUPLEX=NO,;'
         DC    CL80'??DJDE  TOF=1,BOF=66,COPIES=1,END;'
VFUD9    DC    H'2'                    DMY9 DEFAULT
         DC    CL80'??DJDE  ASSIGN=(1,1),DUPLEX=NO,;'
         DC    CL80'??DJDE  TOF=1,BOF=66,COPIES=1,END;'
VFUXICS  DC    H'1'                    DMY9 DEFAULT
         DC    CL80' '
         END
