         TITLE 'BROWSE DELUXE PHONE LIST'
*        REGISTER USAGE:
*              R0
*              R1   STANDARD PARAMETER PASSING
*              R2
*              R3
*              R4
*              R5
*              R6
*              R7
*              R8
*              R9
*              R10
*              R11
*              R12  PROGRAM BASE 1
*              R13  SAVE AREA POINTER
*              R14  RETURN ADDRESS
*              R15
PHONBRWS CSECT
         SAVE  (14,12),,PHONBRWS_BROWSE_DELUXE_PHONE_LIST_&SYSDATE_&SYSX
               TIME
         LR    R12,R15                  ESTABLISH R12 AS BASE REG
         USING PHONBRWS,R12             TELL ASSEMBLER
         ST    R13,SAVEAREA+4           SAVE OS SAVE AREA ADDRESS
         LR    R6,R13                   SAVE IT IN R6
         LA    R13,SAVEAREA             POINT R13 AT OUR SAVE AREA
         ST    R13,8(R6)                STORE OUR SAVEAREA ADDR IN OS
* END OF PROLOG                         SAVE AREA
*        PRINT NOGEN
         ISPCALL ('CONTROL','DISPLAY','REFRESH'),WKAREA=PARAMS,        X
               EPADDR=ISPADDR
         ISPCALL ('CONTROL','SPLIT','ENABLE')
*0070068ISPEXEC  CONTROL ERRORS   RETURN
         ISPCALL ('VDEFINE',VARLIST1,PLANT,'CHAR',4)
         ISPCALL ('VDEFINE',VARLIST2,FIRSTAID,'CHAR',1)
         ISPCALL ('VDEFINE',VARLIST3,EXTENS,'CHAR',4)
         ISPCALL ('VDEFINE',VARLIST4,PAGENUM,'CHAR',4)
         ISPCALL ('VDEFINE',VARLIST5,NAME,'CHAR',40)
         ISPCALL ('VDEFINE',VARLIST6,EXCLUDE,'CHAR',1)
         ISPCALL ('VDEFINE',VARLIST7,BUDGDEPT,'CHAR',4)
         ISPCALL ('VDEFINE',VARLIST8,SUPFLAG,'CHAR',1)
         ISPCALL ('VDEFINE',VARLISTB,PHONORD,'CHAR',1)
         ISPCALL ('VDEFINE',VARLISTC,PHONSTR,'CHAR',10)
         ISPCALL ('VDEFINE',VARLISTD,ZERRMSG,'CHAR',8)
         ISPCALL ('VDEFINE',VARLISTE,SORTORD,'CHAR',1)
         ISPCALL ('VDEFINE',VARLISTF,SAVESTR,'CHAR',10)
         ISPCALL ('TBOPEN','PHONETAB','NOWRITE','PHNTLIB')
         B     CKOPEN(R15)
CKOPEN   EQU   *                      ERROR OPENING PHONE TABLE?
         B     OPENOK                 00
         B     OPENOK                 04
         B     CTRL                   08 TABLE DOES NOT EXIST
         B     CTRL                   12 TABLE IN USE; ENQ FAILED
         B     CTRL                   16 LIBRARY NOT ALLOCATED
         B     CTRL                   20 SEVERE ERROR
OPENOK   EQU   *                      NO ERRORS FOUND IN OPEN
         MVC   EXTENS,=C'¢¢¢¢'
         ISPCALL ('TBSCAN','PHONETAB','EXTENS','VARLISTE')
         B     CKTBSCN1(R15)
CKTBSCN1 EQU   *
         B     DELSORT                00
         B     DELSORT                04
         B     NOSORT                 08 ROW DOES NOT EXIST, NO MATCH
*                                        FOUND; CRP SET TO TOP
         B     CTRL                   12 TABLE IS NOT OPEN
         B     ENDTABLE               16 DATA TRUNCATION, OR
*                                        INSUFFICIENT SPACE PROVIDED
*                                        FOR ALL EXTENSION VAR NAMES
         B     ENDTABLE               20 SEVERE ERROR
NOSORT   EQU   *
         MVI   SORTORD,C' '
         B     TOP
DELSORT  EQU   *
         ISPCALL ('TBDELETE','PHONETAB')
         BAL   R10,CKTBDLTE(R15)
TOP      EQU   *
         ISPCALL ('TBTOP','PHONETAB') POSITION TO TOP OF TABLE
         BAL   R10,CKTBTOP(R15)
SCANTAB  EQU   *
         MVI   EXTENS,C' '
         ISPCALL ('TBSCAN','PHONETAB','EXCLUDE',' ',' ',' ',' ',' ','NEX
               ')
         B     CKTBSCN2(R15)
CKTBSCN2 EQU   *
         B     CKEXCLUD               00
         B     CKEXCLUD               04
         B     EXCLDONE               08 ROW DOES NOT EXIST, NO MATCH
*                                        FOUND; CRP SET TO TOP
         B     CTRL                   12 TABLE IS NOT OPEN
         B     ENDTABLE               16 DATA TRUNCATION, OR
*                                        INSUFFICIENT SPACE PROVIDED
*                                        FOR ALL EXTENSION VAR NAMES
         B     ENDTABLE               20 SEVERE ERROR
CKEXCLUD EQU  *
         CLI   EXCLUDE,C'*'
         BE    DELETE
         B     SCANTAB
DELETE   EQU   *
         ISPCALL ('TBDELETE','PHONETAB')
         BAL   R10,CKTBDLTE(R15)
         B     SCANTAB
EXCLDONE EQU   *
         ISPCALL  ('VGET','(PHONORD,PHONSTR)','ASIS')
         B     CKVGET(R15)
CKVGET   EQU   *
         B     GOODVGET               00
         B     GOODVGET               04
         B     ENDTABLE               08 VARIABLE NOT FOUND
         B     GOODVGET               12
         B     ENDTABLE               16 TRANSLATION ERROR OR DATA
*                                        TRUNCATION
         B     ENDTABLE               20 SEVERE ERROR
GOODVGET EQU   *
         CLI   PHONORD,C' '
         BE    PHONORDN
         B     CMPRORD
PHONORDN EQU   *
         MVI   PHONORD,C'N'
CMPRORD  EQU   *
         CLC   PHONORD,SORTORD
         BE    SCAN
         CLI   PHONORD,C'E'           DETERMINE SORTING ORDER
         BE    SORTEXT
         ISPCALL ('TBSORT','PHONETAB','(NAME,C,A,EXTENS,C,A)')
         B     CKSORT(R15)
SORTEXT  EQU   *
         ISPCALL ('TBSORT','PHONETAB','(EXTENS,C,A,NAME,C,A)')
         B     CKSORT(R15)
CKSORT   EQU   *                      ERROR SORTING TABLE?
         B     SCAN                   00
         B     SCAN                   04
         B     SCAN                   08
         B     CTRL                   12 TABLE IS NOT OPEN
         B     ENDTABLE               16 NUMERIC CONVERT ERROR
         B     ENDTABLE               20 SEVERE ERROR
***********************************************************************
SCAN     EQU   *
         CLC   PHONSTR,=C'          '   COMPARE PHONSTR TO SPACE
         BE    DISPLAY                  PHONSTR NOT ENTERED
         ISPCALL ('TBTOP','PHONETAB')   POSITION TO TOP (IN CASE
*                                       ALREADY BEYOND PHONSTR IN
*                                       TABLE)
         BAL   R10,CKTBTOP(R15)
         CLI   PHONORD,C'E'             SORTED BY EXTENSION?
         BE    EXTORD                   YES
         MVC   NAME(1),=C' '            SPACE OUT NAME
         MVC   NAME+1(L'NAME-1),NAME
         MVC   NAME(L'PHONSTR),PHONSTR  MOVE PHONSTR TO NAME
         ISPCALL ('TBSCAN','PHONETAB','NAME',' ',' ',' ',' ',' ','GE')
         B     CKTBSCAN(R15)
EXTORD   EQU   *
         MVC   EXTENS,PHONSTR
         ISPCALL ('TBSCAN','PHONETAB','EXTENS',' ',' ',' ',' ',' ','GE'X
               )
         B     CKTBSCAN(R15)
CKTBSCAN EQU   *
         B     DISPLAY                00
         B     DISPLAY                04
         B     NOTFOUND               08 ROW DOES NOT EXIST, NO MATCH
*                                        FOUND; CRP SET TO TOP
         B     CTRL                   12 TABLE IS NOT OPEN
         B     ENDTABLE               16 DATA TRUNCATION, OR
*                                        INSUFFICIENT SPACE PROVIDED
*                                        FOR ALL EXTENSION VAR NAMES
         B     ENDTABLE               20 SEVERE ERROR
NOTFOUND EQU   *
         MVC   SAVESTR,PHONSTR
         MVC   ZERRMSG,=CL8'PHN007  '
         B     DISPLAY
***********************************************************************
DISPLAY  EQU   *
         ISPCALL ('TBDISPL','PHONETAB','PHONBRWS',ZERRMSG)
         MVC   ZERRMSG,=CL8'PHN000  '
         B     CKTBDISP(R15)
CKTBDISP EQU   *
         B     SCAN                   00
         B     SCAN                   04 THE ENTER KEY OR SCROLL
*                                        COMMAND USED; ADDITIONAL
*                                        DATA REMAINS TO BE STORED
         B     ENDTABLE               08 END OR RETURN COMMAND ENTERED
         B     ENDTABLE               12 TABLE IS NOT OPEN
*                                        OR PANEL OR MESSAGE NOT FOUND
         B     SCAN                   16
         B     ENDTABLE               20 SEVERE ERROR
***********************************************************************
CKTBDLTE EQU   *
         B     RETADDR                00
         B     RETADDR                04
         B     RETADDR                08 CRP AT TOP
         B     CTRL                   12 TABLE IS NOT OPEN
         B     RETADDR                16
         B     ENDTABLE               20 SEVERE ERROR
***********************************************************************
CKTBTOP  EQU   *
         B     RETADDR                00
         B     RETADDR                04
         B     RETADDR                08
         B     CTRL                   12 TABLE IS NOT OPEN
         B     RETADDR                16
         B     ENDTABLE               20 SEVERE ERROR
***********************************************************************
RETADDR  BR    R10
***********************************************************************
ENDTABLE EQU   *
         ISPCALL ('TBEND','PHONETAB',' ',' ',' ','PHNTLIB')
CTRL     EQU   *
         ISPCALL ('CONTROL','SPLIT','ENABLE')
         ISPCALL ('VRESET')
         B     RETURN
* BEGINNING OF EPILOG
RETURN   EQU   *
         ISPCALL ('SETMSG',ZERRMSG)
         L     R13,SAVEAREA+4           LOAD OS SAVE AREA ADDR TO R13
         RETURN (14,12),RC=(15)         RETURN TO OS, RC IN REG 15
* END OF EPILOG
ISPADDR  DC    A(0)                  STORAGE FOR EP ADDRESS OF ISPLINK
VARLIST1 DC    C'(PLANT)'
VARLIST2 DC    C'(FIRSTAID)'
VARLIST3 DC    C'(EXTENS)'
VARLIST4 DC    C'(PAGENUM)'
VARLIST5 DC    C'(NAME)'
VARLIST6 DC    C'(EXCLUDE)'
VARLIST7 DC    C'(BUDGDEPT)'
VARLIST8 DC    C'(SUPFLAG )'
VARLISTB DC    C'(PHONORD)'
VARLISTC DC    C'(PHONSTR)'
VARLISTD DC    C'(ZERRMSG)'
VARLISTE DC    C'(SORTORD)'
VARLISTF DC    C'(SAVESTR)'
         CNOP  0,4                      ALIGNMENT
SAVEAREA DC    18F'0'                   OUR SAVE AREA
PARAMS   DC    10A(0)                   PARAMETER WORKAREA
* TABLE VARIABLES:
PLANT    DC    CL04' '                  BUILDING ABBR/PLANT NUMBER
FIRSTAID DC    CL01' '                  FIRST AID FLAG
EXTENS   DC    CL04' '                  EXTENSION NUMBER
PAGENUM  DC    CL04' '                  PAGING NUMBER
NAME     DC    CL40' '                  NAME/ROOM
EXCLUDE  DC    CL01' '                  MIS035/MIS036 EXCLUSION FLAG
BUDGDEPT DC    CL04' '                  BUDGET DEPARTMENT
SUPFLAG  DC    CL01' '                  MIS077 SUPPRESSION FLAG
SORTORD  DC    CL01' '                  SORTING ORDER OF TABLE
PHONORD  DC    CL01' '                  ENTERED SORTING ORDER
SAVEEXT  DC    CL04' '                  SAVED EXTENSION NUMBER FOR MSG
SAVENAME DC    CL40' '                  SAVED NAME FOR MESSAGE
PHONSTR  DC    CL10' '                  STARTING POINT
SAVESTR  DC    CL10' '                    SAVED FOR NOT FOUND MESSAGE
ZERRMSG  DC    CL08'PHN000  '           ERROR MESSAGE ID
***********************************************************************
         EQUATE                         REGISTER EQUATES
         END   PHONBRWS
