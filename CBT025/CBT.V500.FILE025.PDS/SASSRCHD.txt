//TDBC   JOB   (6496,B015),'TECH - TDB ',CLASS=O,MSGCLASS=T,
// MSGLEVEL=(2,0),NOTIFY=TDB
//*********************************************************************
//*                                                                   *
//******************************************************************* *
//*                                                                 * *
//*            D I S C L A I M E R    S E C T I O N                 * *
//*                                                                 * *
//******************************************************************* *
//*                                                                 * *
//* THIS MATERIAL HAS BEEN USED SUCCESSFULLY & TO THE BEST OF OUR   * *
//* KNOWLEDGE  THEY  ARE  OPERATIONAL, HOWEVER  NO  WARRANTY        * *
//* IS MADE TO THE ACCURACY OF THE PROGRAMS OR RELATED MATERIAL AND * *
//* NO RESPONSIBILITY IS ASSUMED  FOR ANY MODIFICATION  DIRECTLY OR * *
//* INDIRECTLY  CAUSED BY  THEIR USE.  IT IS THE USERS              * *
//* RESPONSIBILITY  TO  EVALUATE THE  USEFULLNESS  OF THE  MATERIAL * *
//* SUPPLIED .                                                      * *
//*                                                                 * *
//* WE DO NOT GUARANTEE TO  KEEP ANY  MATERIAL PROVIDED  UP TO DATE * *
//* NOR DO WE GUARANTEE TO  PROVIDE ANY  CORRECTIONS OR  EXTENSIONS * *
//* DESCRIBED BY THE USER OR  CORRECTIONS OR EXTENSIONS MADE IN THE * *
//* FUTURE .                                                        * *
//*                                                                 * *
//******************************************************************* *
//*                                                                   *
//*                                                                   *
//*  THIS SAS CODE WRITTEN BY TOM BURNS AT TEXAS UTILITIES SERVICES   *
//*  TO REPORT ON WHO WAS ACCESSING DELETING NONVSAM DATASETS. IT     *
//*  NEEDS TYPE 14, 15, 17 AND 18 RECORDS TO PROVIDE ALL THE INFO,    *
//*                                                                   *
//*  INSTRUCTIONS:                                                    *
//*                                                                   *
//*  1.  CHANGE THE DSNAMES DD INPUT TO THE DATA AND INDEX NAMES IN   *
//*      QUESTION.  THE CLUSTER NAME DOES NOT SHOW UP IN THESE RECORDS*
//*                                                                   *
//*  2.  CHANGE THE SMF INPUT DSN IN THE SMFTAPE DD TO THE SMF INPUT  *
//*      DATASET FOR YOUR INSTALLATION.                               *
//*                                                                   *
//* OPTIONALLY, YOU CAN CHANGE THE DATE/TIME RANGE IN THE OPTIONS.    *
//* WE NORMALLY JUST LEAVE IT THE WAY IT IS.                          *
//*                                                                   *
//*********************************************************************
//DSLIST   EXEC SAS,REGION=1024K,
// COPIES=1,SORT=99,OPTIONS='NOSTIMER,S=72'
//WORK      DD  UNIT=SYSDA,SPACE=(CYL,(250,200))
//FT11F001  DD  SYSOUT=*
//SMFTAPE DD DSN=TUSS.SM.PNDSYSA.SMFRECS.DAILY.G1497V00,DISP=OLD
//        DD DSN=TUSS.SM.PNDSYSB.SMFRECS.DAILY.G1473V00,DISP=OLD
//DSNAMES   DD  *
TUE0CDE.PRODPCL6.C1979.G0001V00
//SYSIN     DD  *
COMMENT
        THIS JOB SUMMARIZES USES OF DATASETS BY JOBS
WRITTEN BY TOM BURNS AT TEXAS UTILITIES  ;
OPTIONS SYSPARM='STARTDATE=01/03/00,STARTTIME=00:00:00,
ENDDATE=12/31/99,ENDTIME=23:59:59';
DATA DSNAMES;
   INFILE DSNAMES;
   LABEL  DSN = DSNAME;
   FORMAT DSN     $44.;
   INPUT DSN : $44.;
   OUTPUT DSNAMES;
   RETURN;
DATA TEMPACT
          (KEEP = XX ACTDSN JNAM RECDATE RECTIME RDRDATE TYPE USERID
                     DSORG RECFM LRECL BLKSIZE)
 TEMPREN  (KEEP = ACTDSN JNAM RECDATE RECTIME RDRDATE USERID NDSN)
 TEMPSCR  (KEEP = ACTDSN JNAM RECDATE RECTIME RDRDATE USERID VOL_ID);
   LABEL  JNAM    = JOBNAME
          TYPE    = TYPE
          RECDATE = DATE
          RECTIME = TIME
          RDRDATE = READER DATE
          USERID  = USERID
          NDSN    = NEW DSNAME
          DSORG   = DSORG
          RECFM   = RECFM
          LRECL   = LRECL
          BLKSIZE = BLKSIZE;
   FORMAT ACTDSN             $44.
          XX                  $6.
          JNAM                $8.
          TYPE                $2.
          RECDATE        MMDDYY8.
          RECTIME          TIME8.
          RDRDATE     DATETIME16.
          USERID              $8.
          VOL_ID              $6.
          NDSN               $44.
          DSORG               $3.
          RECFM               $4.
          LRECL                5.
          BLKSIZE              5.;
   INFILE SMFTAPE MISSOVER JFCB=JFCBMANX CLOSE=FREE;
   IF _N_=1 THEN DO;
      IF SUBSTR(JFCBMANX,100,1)='....1...'B
         THEN SMFCOL1=5;
      ELSE SMFCOL1=1;
      FOUND=1; NPARM=1;
      STRTDATE=0; STRTTIME=0; ENDDATE=0; ENDTIME=0;
      DO WHILE(FOUND);
         FOUND=0;
         IF 'STARTDATE'=SCAN(SYSPARM(),NPARM,' ,=') THEN DO;
            WORD=SCAN(SYSPARM(),NPARM+1,' ,=');
            IF WORD='TODAY' THEN STRTDATE=TODAY();
            ELSE STRTDATE=INPUT(WORD,MMDDYY8.);
            FOUND=1;
            END;
         IF 'STARTTIME'=SCAN(SYSPARM(),NPARM,' ,=') THEN DO;
            WORD=SCAN(SYSPARM(),NPARM+1,' ,=');
            IF WORD='NOW' THEN STRTTIME=HMS(HOUR(DATETIME()),0,0);
            ELSE STRTTIME=INPUT(WORD,TIME8.);
            FOUND=1;
            END;
         IF 'ENDDATE'=SCAN(SYSPARM(),NPARM,' ,=') THEN DO;
            WORD=SCAN(SYSPARM(),NPARM+1,' ,=');
            IF WORD='TODAY' THEN ENDDATE=TODAY();
            ELSE ENDDATE=INPUT(WORD,MMDDYY8.);
            FOUND=1;
            END;
         IF 'ENDTIME'=SCAN(SYSPARM(),NPARM,' ,=') THEN DO;
            WORD=SCAN(SYSPARM(),NPARM+1,' ,=');
            IF WORD='NOW' THEN ENDTIME=HMS(1+HOUR(DATETIME()),0,0);
            ELSE ENDTIME=INPUT(WORD,TIME8.);
            FOUND=1;
            END;
         IF FOUND THEN NPARM=NPARM+2;
         END;
      IF ENDDATE=0 THEN ENDDATE=MDY(12,31,99);
      IF ENDTIME=0 THEN ENDTIME=HMS(23,59,59);
      RETAIN SMFCOL1 STRTDATE STRTTIME ENDDATE ENDTIME VOL_ID;
      DROP SMFCOL1 STRTDATE STRTTIME ENDDATE ENDTIME FOUND NPARM;
      END;
   INPUT @SMFCOL1    SMFEOF   :      $12.
         @SMFCOL1+ 1 ID             PIB1.@;
   IF SMFEOF='SMFEOFMARK' THEN STOP; * END OF FILE ON INPUT;
   TYPE='  ';
   IF ID=14 THEN TYPE='I ';
   IF ID=15 THEN TYPE='O ';
   IF ID=17 THEN TYPE='S ';
   IF ID=18 THEN TYPE='R ';
   IF TYPE='  ' THEN RETURN;
   INPUT @SMFCOL1+  2 SMFTIME       PIB4.
              @SMFCOL1+  6 SMFDATE        PD4.
              @SMFCOL1+ 14 JNAM            $8.
              @SMFCOL1+ 22 RDRDATE  SMFSTAMP8.
              @SMFCOL1+ 30 USERID          $8.
              @SMFCOL1+ 90 VOL_ID          $6.@;
   RECDATE=DATEJUL(SMFDATE);
   SMFTIME=INT(SMFTIME/100);
   HOURS=INT(SMFTIME/3600);
   MINUTES=INT(MOD(SMFTIME,3600)/60);
   RECTIME=HMS(HOURS,MINUTES,MOD(SMFTIME,60));
   DROP ID SMFEOF SMFTIME SMFDATE HOURS MINUTES;
   IF ID=14 OR ID=15 THEN DO;* CLOSE INPUT/OUTPUT DATASET;
      IF RECDATE<STRTDATE THEN RETURN;
      IF RECDATE>ENDDATE THEN RETURN;
      IF RECDATE=STRTDATE AND RECTIME<STRTTIME THEN RETURN;
      IF RECDATE=ENDDATE AND RECTIME>ENDTIME THEN RETURN;
      INPUT @SMFCOL1+ 38 SMF14RIN  PIB2.
            @SMFCOL1+ 64 ACTDSN   : $44.
            @SMFCOL1+151 JFCBIND2  PIB1.@;
      IF SMF14RIN='...1............'B THEN RETURN;* SKIP TEMPORARIES;
      IF SMF14RIN='.......1........'B THEN RETURN;* SKIP VIO (TEMP);
      IF JFCBIND2='.......1'B THEN RETURN;* SKIP TEMPORARIES;
      IF ID=15 THEN DO;
         INPUT @SMFCOL1+162 JFCDSORG      PIB2.
               @SMFCOL1+164 JFCRECFM      PIB1.
               @SMFCOL1+166 BLKSIZE       PIB2.
               @SMFCOL1+168 LRECL         PIB2.
               @SMFCOL1+256 RLSE          PIB4.@;
         IF SMF14RIN='..1.............'B THEN
            IF RLSE>0 THEN SUBSTR(TYPE,2,1)='*';
         DSORG='***';
         IF JFCDSORG='100...00........'B THEN DSORG='IS ';
         IF JFCDSORG='100...01........'B THEN DSORG='ISU';
         IF JFCDSORG='010...00........'B THEN DSORG='PS ';
         IF JFCDSORG='010...01........'B THEN DSORG='PSU';
         IF JFCDSORG='001...00........'B THEN DSORG='DA ';
         IF JFCDSORG='001...01........'B THEN DSORG='DAU';
         IF JFCDSORG='000...10........'B THEN DSORG='PO ';
         IF JFCDSORG='000...11........'B THEN DSORG='POU';
         IF JFCRECFM='11......'B THEN RECFMTYP='U';
         ELSE IF JFCRECFM='10......'B THEN RECFMTYP='F';
            ELSE IF JFCRECFM='01......'B THEN RECFMTYP='V';
               ELSE RECFMTYP='*';
         IF JFCRECFM='...1....'B THEN RECFMBLK='B';
         ELSE RECFMBLK=' ';
         IF JFCRECFM='....1...'B THEN RECFMSTD='S';
         ELSE RECFMSTD=' ';
         IF JFCRECFM='.....10.'B THEN RECFMCC='A';
         ELSE IF JFCRECFM='.....01.'B THEN RECFMCC='M';
            ELSE RECFMCC=' ';
         RECFM=RECFMTYP||RECFMBLK||RECFMSTD||RECFMCC;
         END;
      DROP SMF14RIN JFCBIND2 JFCDSORG JFCRECFM
           RECFMTYP RECFMBLK RECFMSTD RECFMCC;
      OUTPUT TEMPACT;
      END;
   IF ID=17 THEN DO;* DATASET RENAME;
      IF RECDATE<STRTDATE THEN RETURN;
      IF RECDATE>ENDDATE THEN RETURN;
      IF RECDATE=STRTDATE AND RECTIME<STRTTIME THEN RETURN;
      IF RECDATE=ENDDATE AND RECTIME>ENDTIME THEN RETURN;
      INPUT @SMFCOL1+40 ACTDSN : $44.@;
      OUTPUT TEMPSCR;
      END;
   IF ID=18 THEN DO;* DATASET SCRATCH;
      IF RECDATE<STRTDATE THEN RETURN;
      IF RECDATE>ENDDATE THEN RETURN;
      IF RECDATE=STRTDATE AND RECTIME<STRTTIME THEN RETURN;
      IF RECDATE=ENDDATE AND RECTIME>ENDTIME THEN RETURN;
      INPUT @SMFCOL1+40 ACTDSN : $44.
            @SMFCOL1+84 NDSN   : $44.@;
      OUTPUT TEMPREN;
      END;
   INPUT;
   RETURN;
PROC SORT DATA=DSNAMES OUT=DSNAMES; BY DSN;
PROC SORT DATA=TEMPACT OUT=TEMPACT; BY ACTDSN;
DATA ACTIVITY
          (KEEP = DSN JNAM RECDATE RECTIME RDRDATE TYPE USERID
                     DSORG RECFM LRECL BLKSIZE);
   LABEL  DSN     = DSNAME
          JNAM    = JOBNAME
          RECDATE = DATE
          RECTIME = TIME
          RDRDATE = READER DATE
          TYPE    = TYPE
          USERID  = USERID
          DSORG   = DSORG
          RECFM   = RECFM
          LRECL   = LRECL
          BLKSIZE = BLKSIZE;
   FORMAT DSN                $44.
          JNAM                $8.
          RECDATE        MMDDYY8.
          RECTIME          TIME8.
          RDRDATE     DATETIME16.
          TYPE                $2.
          USERID              $8.
          DSORG               $3.
          RECFM               $4.
          LRECL                5.
          BLKSIZE              5.;
   IF _N_=1 THEN DO;
      LINK RDACTA;
      LINK RDDSNA;
      END;
   IF DSN<ACTDSN THEN DO;
      LINK RDDSNA;
      RETURN;
      END;
   IF DSN=ACTDSN THEN GOTO WRTUSES;
   IF DSN>ACTDSN THEN DO;
      LINK RDACTA;
      RETURN;
      END;
 WRTUSES: DROP ACTDSN; OUTPUT ACTIVITY; LINK RDACTA; RETURN;
 RDACTA: SET TEMPACT; RETURN;
 RDDSNA: SET DSNAMES; RETURN;
PROC PRINT DATA=ACTIVITY UNIFORM LABEL;
   ID DSN;
   VAR JNAM USERID TYPE RECDATE RECTIME DSORG RECFM LRECL BLKSIZE;
   TITLE 'DATASET USAGE: JOBNAME ORDER';
PROC SORT DATA=TEMPREN OUT=TEMPREN; BY ACTDSN;
DATA RENAMES(KEEP = DSN JNAM RECDATE RECTIME RDRDATE USERID NDSN);
   LABEL  DSN     = DSNAME
          JNAM    = JOBNAME
          RECDATE = DATE
          RECTIME = TIME
          RDRDATE = READER DATE
          NDSN    = NEW DSNAME;
   FORMAT DSN                $44.
          JNAM                $8.
          RECDATE        MMDDYY8.
          RECTIME          TIME8.
          RDRDATE     DATETIME16.
          USERID              $8.
          NDSN               $44.;
   IF _N_=1 THEN DO;
      LINK RDREN;
      LINK RDDSNR;
      END;
   IF DSN<ACTDSN THEN DO;
      LINK RDDSNR;
      RETURN;
      END;
   IF DSN=ACTDSN THEN GOTO WRTREN;
   IF DSN>ACTDSN THEN DO;
      LINK RDREN;
      RETURN;
      END;
 WRTREN: DROP ACTDSN; OUTPUT RENAMES; LINK RDREN; RETURN;
 RDREN: SET TEMPREN; RETURN;
 RDDSNR: SET DSNAMES; RETURN;
PROC PRINT DATA=RENAMES UNIFORM LABEL;
   ID DSN;
   VAR JNAM USERID RECDATE RECTIME NDSN;
   TITLE 'DATASET RENAMES';
PROC SORT DATA=TEMPSCR OUT=TEMPSCR; BY ACTDSN;
DATA SCRATCH(KEEP = DSN JNAM RECDATE RECTIME RDRDATE USERID VOL_ID);
   LABEL  DSN     = DSNAME
          JNAM    = JOBNAME
          RECDATE = DATE
          RECTIME = TIME
          RDRDATE = READER DATE
          USERID  = USER ID;
   FORMAT DSN                $44.
          JNAM                $8.
          RECDATE        MMDDYY8.
          RECTIME          TIME8.
          RDRDATE     DATETIME16.
          USERID              $8.;
   IF _N_=1 THEN DO;
      LINK RDSCR;
      LINK RDDSNR;
      END;
   IF DSN<ACTDSN THEN DO;
      LINK RDDSNR;
      RETURN;
      END;
   IF DSN=ACTDSN THEN GOTO WRTSCR;
   IF DSN>ACTDSN THEN DO;
      LINK RDSCR;
      RETURN;
      END;
 WRTSCR: DROP ACTDSN; OUTPUT SCRATCH; LINK RDSCR; RETURN;
 RDSCR: SET TEMPSCR; RETURN;
 RDDSNR: SET DSNAMES; RETURN;
PROC PRINT DATA=SCRATCH UNIFORM LABEL;
   ID DSN;
   VAR JNAM USERID RECDATE RECTIME VOL_ID;
   TITLE 'DATASET SCRATCHES';
