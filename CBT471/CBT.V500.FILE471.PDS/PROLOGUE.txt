         MACRO
         PROLOGUE &CSECT,       CSECT NAME                             @
               &MSG,            EYE-CATCHER MESSAGE                    @
               &AMODE=24,       ADDRESSING MODE                        @
               &RMODE=24        RESIDENCY MODE
.*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
.*  MACNAME:  PROLOGUE                                               *
.*  MACAUTH:  SKIP ABADIE                                            *
.*  MACDATE:  4/2/81      LAST UPDATE:  5/27/85                      *
.*                                                                   *
.*  MAY BE USED TO GENERATE CSECT CARDS AND STANDARD FORMAT MODULE   *
.*  EYE-CATCHERS.  BOTH THE CSECT AND MODULE EYE-CATCHERS ARE        *
.*  OPTIONAL.  SEE OPERAND DESCRIPTIONS AND EXAMPLES BELOW.          *
.*                                                                   *
.*  THIS MACRO IS CONDITIONALLY INVOKED BY MACRO ENTER.  IF FLAG     *
.*  &PROLOG IS NOT SET TO ONE, ENTER INVOKES PROLOGUE.  NOTE THAT    *
.*  THE ENTER MACRO ALWAYS SETS THE GLOBAL FLAG &PROLOG TO ZERO SO   *
.*  THAT SUBSEQUENT PROLOGUE OR ENTER MACROS WILL EXPAND PROPERLY.   *
.*                                                                   *
.*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
.*                                                                   *
.*  CSECT                                                            *
.*       IF SPECIFIED, THIS POSITIONAL PARAMETER WILL GENERATE A     *
.*       CSECT CARD USING THE SPECIFIED NAME.  IF IT IS OMITTED BY   *
.*       CODING A COMMA, THEN NO CSECT CARD IS GENERATED.  IN EITHER *
.*       CASE, THE FOLLOWING CARDS WILL BE GENERATED:                *
.*                USING *,15                                         *
.*                CNOP  0,4                                          *
.*                BC    15,*+LX                                      *
.*                DC    AL1(LY)                                      *
.*       WHERE 'LX' IS THE DISPLACEMENT TO THE FIRST EXECUTABLE      *
.*       INSTRUCTION FOLLOWING THE PROLOGUE MACRO, AND 'LY' IS THE   *
.*       LENGTH + 1 OF THE FOLLOWING EYE-CATCHER.                    *
.*  MSG                                                              *
.*       IF SPECIFIED, AN MVS STANDARD MODULE IDENTIFIER (EYE-       *
.*       CATCHER) IS BUILT CONSISTING OF THE CSECT NAME, THE DATE    *
.*       OF THE ASSEMBLY, AND THE CONTENTS OF THE MSG PARAMETER.     *
.*       IF OMITTED, AN MVS STANDARD MODULE IDENTIFIER IS BUILT      *
.*       CONSISTING OF THE CSECT NAME, THE DATE OF THE ASSEMBLY,     *
.*       AND THE CHARACTER STRING "ANDERSON, CLAYTON & CO."          *
.*       THE EYE-CATCHER MAY BE PADDED ON THE END WITH ONE BLANK,    *
.*       AS REQUIRED, TO CAUSE HALFWORD BOUNDARY ALIGNMENT FOR THE   *
.*       INSTRUCTION IMMEDIATELY FOLLOWING THE PROGLOGUE MACRO.      *
.*       AN ADDITIONAL BLANK WILL BE ASSEMBLED AT THE END FOR EVERY  *
.*       OCCURRENCE OF A DOUBLE SPECIAL CHARACTER, SUCH AS &&.       *
.*       THE MAXIMUM LENGTH OF THE MSG PARAMETER IS VARIABLE, AND    *
.*       MAY BE COMPUTED AS FOLLOWS:                                 *
.*          LENGTH = (256 - (CSECT NAME LENGTH + 13))                *
.*       THE MSG PARAMETER MUST BE SURROUNDED BY SINQLE QUOTES,      *
.*       WHICH DO NOT CONTRIBUTE TO THE TOTAL LENGTH OF THE STRING.  *
.*                                                                   *
.**** AMODE AND RMODE ADDED FOR MVS/XA SUPPORT @85.127 BY -DGA-   ****
.**** SEE GC28-1158 "MVS/XA SPL: 31-BIT ADDRESSING" FOR DETAILS   ****
.*                                                                   *
.*  AMODE={ |24|31|ANY}                                              *
.*       IF SPECIFIED, THIS  PARAMETER WILL GENERATE THE AMODE       *
.*       CARD FOR THE ASSEMBLER H.  AMODE IS A PROGRAM ATTRIBUTE     *
.*       WHICH CAN BE SET TO GIVE ADDRESSING MODE.
.*  RMODE={ |31|ANY}                                                 *
.*       IF SPECIFIED, THIS PARAMETER WILL GENERATE THE RMODE        *
.*       CARD FOR THE ASSEMBLER H.  RMODE IS A PROGRAM ATTRIBUTE     *
.*       WHICH CAN BE SET TO GIVE RESIDENCY MODE.                    *
.*                                                                   *
.*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
.*                                                                   *
.*  THE FOLLOWING EXAMPLES SHOW ONLY THE TEXT OF THE EYE-CATCHER     *
.*  GENERATED.                                                       *
.*                                                                   *
.*    PROLOGUE ,'EYE-CATCHER'                                        *
.*      GENERATES:  C'&SYSECT &SYSDATE EYE-CATCHER'                  *
.*                                                                   *
.*    PROLOGUE ,                                                     *
.*      GENERATES:  C'&SYSECT &SYSDATE ANDERSON, CLAYTON & CO.'      *
.*                                                                   *
.*    PROLOGUE MYPROG                                                *
.*      GENERATES:  C'MYPROG &SYSDATE ANDERSON, CLAYTON & CO.'       *
.*                                                                   *
.*    PROLOGUE TEST,'DOO-DAH DOO-DAH'                                *
.*      GENERATES:  C'TEST &SYSDATE DOO-DAH DOO-DAH'                 *
.*                                                                   *
.*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
.*
.*  DEFINE GLOBAL AND LOCAL SET SYMBOLS
.*
         GBLB  &PROLOG
         LCLC  &CS,&TEXT,&ASTUF,&RSTUF
         LCLA  &L1,&L2,&L3
&PROLOG  SETB  1
.*
.*  CHECK AMODE AND SET TRIVIA LINE TO ASTUF
.*
         AIF   ('&AMODE' EQ '').CRMODE
         AIF   ('&AMODE' EQ '24').TAM24
         AIF   ('&AMODE' EQ '31').TAM31
         AIF   ('&AMODE' EQ 'ANY').TAM31
         MNOTE 4,'INVALID AMODE FOUND...'
         AGO   .CRMODE
.TAM24   ANOP  ,
&ASTUF   SETC  '&AMODE         MODULE USES 24-BIT ADDRESSING'
         AGO   .CRMODE
.TAM31   ANOP  ,
&ASTUF   SETC  '&AMODE         MODULE SUPPORTS 31-BIT ADDRESSING'
.*
.*  CHECK RMODE AND SET TRIVIA LINE TO RSTUF
.*
.CRMODE  ANOP  ,
         AIF   ('&RMODE' EQ '').MODEX
         AIF   ('&RMODE' EQ '24').TRM24
         AIF   ('&RMODE' EQ 'ANY').TRM31
         MNOTE 4,'INVALID RMODE FOUND...'
         AGO   .MODEX
.TRM24   ANOP  ,
&RSTUF   SETC  '&RMODE         MODULE MUST RESIDE BELOW THE LINE'
         AGO   .MODEX
.TRM31   ANOP  ,
&RSTUF   SETC  '&RMODE         MODULE CAN RESIDE ANYWHERE IN VS'
.MODEX   ANOP   ,
.*
         AIF   (T'&CSECT NE 'O').P001
&CS      SETC  '&SYSECT'
.*
.*       PUT OUT RMODE AND AMODE CARDS IF SPECIFIED
.*
         AIF   ('&AMODE' EQ '').RMODE1
&CS      AMODE &ASTUF
.RMODE1  AIF   ('&RMODE' EQ '').MODE1X
&CS      RMODE &RSTUF
.MODE1X  ANOP  ,
.*
         USING *,15
         AGO   .P002
.P001    ANOP
&CS      SETC  '&CSECT'
&CS      CSECT ,
.*
.*       PUT OUT RMODE AND AMODE CARDS IF SPECIFIED
.*
         AIF   ('&AMODE' EQ '').RMODE2
&CS      AMODE &ASTUF
.RMODE2  AIF   ('&RMODE' EQ '').MODE2X
&CS      RMODE &RSTUF
.MODE2X  ANOP  ,
.*
         USING *,15
.P002    ANOP
         AIF   (T'&MSG NE 'O').P003
&TEXT    SETC  'ANDERSON, CLAYTON && CO.'
         AGO   .P004
.P003    ANOP
&L2      SETA  (((K'&SYSLIST(2,1)+2)/2)*2)-2
.P004    ANOP
&TEXT    SETC  '&CS'.' &SYSDATE'.' '.'&TEXT'
&L1      SETA  &L1+((K'&TEXT+2)/2)*2
&L3      SETA  &L1+&L2+4
         CNOP  0,4                      GET ALIGNED
         BC    15,*+&L3                 BRANCH AROUND CONSTANTS
&L1      SETA  &L1-1
         DC    AL1(&L1+&L2)             LENGTH OF CONSTANTS
         DC    CL&L1'&TEXT'             TEXT: CSECT AND ASSEMBLE DATE
         AIF   (N'&SYSLIST(2) EQ 0).P005
         DC    CL&L2&SYSLIST(2,1)
.P005    ANOP
         DROP  15
         MEND
