/*REXX*/
/*****************************************************************/
/*LISTCATS: REXX program to display all open catalogs.           */
/*    Note: Developed and tested on an MVS/SP 2.2.3 system.      */
/*****************************************************************/
/* MVS DATA AREAS:                                               */
/*                CVT - Communications Vector Table (@'10'x)     */
/*              AMCBS - Access Method Control Block Structure    */
/*              CAXWA - Calaog Auxiliary Work Area               */
/*                UCB - Device Unit Control Block                */
/*                                                               */
/*****************************************************************/
@cvt = STORAGE(10,4)                  /* get CVT address from PSAAOLD */
@cvt = D2X(C2D(@cvt))                         /* binary to dec to hex */
@cvtcbsp  = STORAGE(D2X(X2D(@cvt)+256),4)      /* get CVTCBSP address */
@cvtcbsp  = D2X(C2D(@cvtcbsp))                /* binary to dec to hex */
@cbscaxcn = STORAGE(D2X(X2D(@cvtcbsp)+20),4)  /* get CBSCAXCN address */
@cbscaxcn = D2X(C2D(@cbscaxcn))               /* binary to dec to hex */
@cbsacb   = STORAGE(D2X(X2D(@cvtcbsp)+8),4)     /* get CBSACB address */
@cbsacb   = D2X(C2D(@cbsacb))                 /* binary to dec to hex */
Address TSO ; "CLEAR"
Say 'CVT: '@cvt' CVTCBSP: '@cvtcbsp' ===> CAXWA chain trace started: 'TIME()
   Say 'CBSCAXCN: '@cbscaxcn' CBSACB:'@cbsacb
Say ' '
i = 0 ; endofcax = 0 ; @caxchn = @cbscaxcn
Do While endofcax = 0 & i < 500
   i = i + 1
   @caxacb = STORAGE(D2X(X2D(@caxchn)+24),4)   /* catalog acb address */
   @caxacb = D2X(C2D(@caxacb))                /* binary to dec to hex */
   If @caxacb = @cbsacb Then
    Do
     cattype = 'Master'
     @caxucb = STORAGE(D2X(X2D(@caxchn)+28),4) /* catalog ucb address */
    End
   Else
    Do
    cattype = 'User  '
    @caxucb = STORAGE(D2X(X2D(@caxchn)+188),4) /* catalog ucb address */
    End
   @caxucb = D2X(C2D(@caxucb))                /* binary to dec to hex */
/* Say 'CAXUCB: '@caxucb' CAXACB:'@caxacb */
   caxvol  = STORAGE(D2X(X2D(@caxucb)+28),6)        /* catalog volser */
   caxcnam = STORAGE(D2X(X2D(@caxchn)+52),44)          /* catalog dsn */
   caxflg3 = STORAGE(D2X(X2D(@caxchn)+10),1)             /* cax flags */
   catorg = 'CVOL' ; If BITAND(caxflg3,'20'x)='20'x Then catorg = 'ICF '
   @caxnext = STORAGE(D2X(X2D(@caxchn)+4),4)    /* next caxwa address */
   @caxnext = C2X(@caxnext)                   /* binary to dec to hex */
   If @caxnext = 0 Then endofcax = 1            /* end of chain value */
   Say RIGHT(i,2,' ') cattype catorg caxvol caxcnam
   @caxchn = @caxnext
End
Say ' '
Say 'Last CAXWA after 'i' blocks scanned.  Completed: 'TIME()
Exit
