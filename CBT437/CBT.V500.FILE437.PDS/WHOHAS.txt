./ ADD NAME=$ASM        01 00 99316 99316 131734 0014 0014 0000 IBMUSER
//IBMUSERA JOB GQSCAN,'JAN JAEGER',NOTIFY=IBMUSER,CLASS=D,MSGCLASS=T
//ASMA90   EXEC PGM=ASMA90,PARM='DECK,NOOBJ,XREF(SHORT)'
//SYSPRINT DD  SYSOUT=*
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR
//         DD  DSN=SYS1.MODGEN,DISP=SHR
//SYSUT1   DD  UNIT=SYSDA,SPACE=(CYL,10)
//SYSPUNCH DD  DSN=&&PUNCH,DISP=(NEW,PASS),
//          UNIT=SYSALLDA,SPACE=(CYL,10)
//SYSIN    DD  DSN=IBMUSER.SOURCE(JJ$LWA),DISP=SHR
//IEWL     EXEC PGM=IEWL
//SYSPRINT DD  SYSOUT=*
//SYSUT1   DD  UNIT=SYSDA,SPACE=(CYL,10)
//SYSLMOD  DD  DSN=IBMUSER.LOAD(JJ$LWA),DISP=SHR
//SYSLIN   DD  DSN=&&PUNCH,DISP=(OLD,DELETE)
./ ADD NAME=JJ$LWA      01 00 99228 99316 131628 0423 0373 0000 IBMUSER
JJ$LWA   CSECT
*                                                                920428
* Change History                                                 920428
* ==============                                                 920428
* 89.12.12     Jan Jaeger     Initial version of this program.   920428
* 92.04.28     Bob Hill       Display dummy entries for missing  920428
*                             RIBEs, and increase QSCNAREA size  920428
*                             from 1K to approx 63K to minimise  920428
*                             missing RIBEs as per APAR OY34580  920428
*                                                                920428
         SAVE  (14,12),,JJ$LWA-&SYSDATE-&SYSTIME-J.JAEGER
         LR    R12,R15
         USING JJ$LWA,R12
*
         LR    R2,R1                   Save PARM addr
*
         GETMAIN RU,LV=CORESIZE        Get storage
         LR    R11,R1                  Save corebase
         USING CORE,R11                Make core addressable
*
         LA    R15,SAVEAREA            Savearea addr in R15
         ST    R13,4(,R15)             Store R13 in savearea+4
         ST    R15,8(,R13)             Store R15 in old S.A.+8
         LR    R13,R15                 Load new savearea in R13
*
*    *** Start of main program ***
*
         LOAD  EP=ISPLINK
         ST    R0,AISPLINK
*
*    *** Get Resourcename
*
         L     R15,AISPLINK
         CALL  (15),(CONTROL,ERRORS,RETURN),VL,MF=(E,PL)
         LTR   R10,R15
         BNZ   EXIT
*
         MVC   TABLENM,TABLEID
         ST    R11,TABLEIDX            Make tablename unique
*
         L     R15,AISPLINK
         CALL  (15),(VDEFINE,ISN,CISN,CHAR,F8),VL,MF=(E,PL)
         L     R15,AISPLINK
         CALL  (15),(VDEFINE,IJN,CIJN,CHAR,F8),VL,MF=(E,PL)
         L     R15,AISPLINK
         CALL  (15),(VDEFINE,ISC,CISC,CHAR,F6),VL,MF=(E,PL)
         L     R15,AISPLINK
         CALL  (15),(VDEFINE,IS1,CIS1,CHAR,F4),VL,MF=(E,PL)
         L     R15,AISPLINK
         CALL  (15),(VDEFINE,IS2,CIS2,CHAR,F4),VL,MF=(E,PL)
         L     R15,AISPLINK
         CALL  (15),(VDEFINE,IQN,CIQN,CHAR,F8),VL,MF=(E,PL)
         L     R15,AISPLINK
         CALL  (15),(VDEFINE,IRN,CIRN,CHAR,F54),VL,MF=(E,PL)
*
         L     R15,AISPLINK
         CALL  (15),(VDEFINE,SYSNAME,CSYSNAME,CHAR,F8),VL,MF=(E,PL)
         L     R15,AISPLINK
         CALL  (15),(VDEFINE,JOBNAME,CJOBNAME,CHAR,F8),VL,MF=(E,PL)
         L     R15,AISPLINK
         CALL  (15),(VDEFINE,SCOPE,CSCOPE,CHAR,F6),VL,MF=(E,PL)
         L     R15,AISPLINK
         CALL  (15),(VDEFINE,STAT1,CSTAT1,CHAR,F4),VL,MF=(E,PL)
         L     R15,AISPLINK
         CALL  (15),(VDEFINE,STAT2,CSTAT2,CHAR,F4),VL,MF=(E,PL)
         L     R15,AISPLINK
         CALL  (15),(VDEFINE,QNAME,CQNAME,CHAR,F8),VL,MF=(E,PL)
         L     R15,AISPLINK
         CALL  (15),(VDEFINE,RNAME,CRNAME,CHAR,F54),VL,MF=(E,PL)
*
         MVC   CISN,=CL8'*'
         MVC   CIJN,=CL8'*'
         MVC   CISC,=CL6'*'
         MVC   CIS1,=CL4'*'
         MVC   CIS2,=CL4'*'
         MVC   CIQN,=CL8'*'
         MVI   CIRN,C' '
         MVC   CIRN+1(L'CIRN-1),CIRN
         LTR   R2,R2
         BZ    DEFAULT
         L     R2,0(R2)
         ICM   R3,B'0011',0(R2)
         BZ    DEFAULT
         LA    R4,2(R2)
         BCTR  R3,0
         EX    R3,RNCOPY
         B     CREATETB
DEFAULT  EQU   *
         L     R15,PSAAOLD-PSA
         L     R15,ASCBJBNS-ASCB(,R15)
         MVC   CIRN(8),0(R15)
         LA    R1,CIRN+L'CIRN
         TRT   CIRN,TRTBLANK
         MVC   0(2,R1),=C'.*'
         MVC   MESSAGE,BLANKS
CREATETB EQU   *
         LA    R1,CIRN+L'CIRN
         TRT   CIRN,TRTASTER
         LA    R2,CIRN
         SLR   R1,R2
         STC   R1,GQSLEN
*
         LA    R1,CIJN+L'CIJN
         TRT   CIJN,TRTASTER
         LA    R2,CIJN
         SLR   R1,R2
         STC   R1,CIJNL
*
         LA    R1,CIQN+L'CIQN
         TRT   CIQN,TRTASTER
         LA    R2,CIQN
         SLR   R1,R2
         STC   R1,CIQNL
*
         L     R15,AISPLINK
         CALL  (15),(TBCREATE,TABLENM,0,VARLIST,NOWRITE),VL,MF=(E,PL)
         L     R15,AISPLINK
         CALL  (15),(TBVCLEAR,TABLENM),VL,MF=(E,PL)
*
* *** GQSCAN LOOP
*
         XC    QSCNTKN,QSCNTKN         Zero token
         MVC   GQSRNAME,CIRN
LOOP     EQU   *
         MVC   LGQSCAN(EGQSCANL),EGQSCAN
         LA    R9,QSCNAREA
         LA    R8,QSCNTKN
         LA    R2,CIQN                 Point to QName
         LA    R3,GQSRNAME             Point to RName
         IC    R4,GQSLEN               Set RName length
         IC    R5,CIQNL
         GQSCAN AREA=((9),QSCNLEN),    Area for RIB and RIBE           X
               SCOPE=ALL,              No matter what system           X
               TOKEN=(8),              Token to proceed scanning       X
               RESNAME=((2),(3),(4),GENERIC,(5)),                      X
               MF=(E,LGQSCAN)
         LTR   R4,R1                   Save number of RIBs returned
         BZ    LOOPEND
         LTR   R3,R15                  Save returncode in R3
         BZ    RCOK
         CH    R3,=H'4'                No more users
         BE    RCOK
         CH    R3,=H'8'                More to come
         BNE   LOOPEND
*
RCOK     EQU   *
         USING RIB,R9
         LH    R6,RIBVLEN
         LA    R6,RIBEND-RIB(R6,R9)
         USING RIBE,R6
         LA    R5,RIBEND-RIB(R9)       RIBVAR
         USING RIBVAR,R5
         ICM   R7,B'1111',RIBNRIBE     Any RIBEs after this RIB? 920428
         BZ    ENDRIBES                No - end of RIB's RIBEs   920428
*
RIBELOOP EQU   *
         TM    RIBSCOPE,RIBSTEP
         BO    SKIPRIBE                                          920428
*
         ICM   R15,B'0001',CIJNL
         BZ    NOJN
         BCTR  R15,0
         EX    R15,CLCIJN
         BNE   SKIPRIBE                                          920428
NOJN     EQU   *
*
         MVC   CQNAME,RIBQNAME
         MVI   CRNAME,C' '
         MVC   CRNAME+1(L'CRNAME-1),CRNAME
         IC    R15,RIBRNMLN
         BCTR  R15,0
         EX    R15,MVCRNAME
*
         CLI   CISN,C'*'
         BE    ALLSYSN
         CLC   CISN,RIBESYSN
         BNE   SKIPRIBE                                          920428
ALLSYSN  EQU   *
*
         MVC   CSYSNAME,RIBESYSN
         MVC   CJOBNAME,RIBEJBNM
*
         MVC   CSCOPE,=CL6'Local'
         TM    RIBSCOPE,RIBGLBL
         BZ    ISLOCAL
         MVC   CSCOPE,=CL6'Global'
ISLOCAL  EQU   *
*
         MVC   CSTAT1,=CL4'Excl'
         TM    RIBERFLG,RIBETYPE
         BZ    ISEXCL
         MVC   CSTAT1,=CL4'Shr'
ISEXCL   EQU   *
*
         MVC   CSTAT2,=CL4'Wait'
         TM    RIBESFLG,RIBESTAT
         BZ    ISWAIT
         MVC   CSTAT2,=CL4'Own'
ISWAIT   EQU   *
*
         CLI   CISC,C'*'               Both Local and Global?
         BE    NOSC
         CLC   CISC(1),CSCOPE
         BNE   SKIPRIBE                                          920428
NOSC     EQU   *
*
         CLI   CIS1,C'*'               Both Shr and Excl?
         BE    NOS1
         CLC   CIS1(1),CSTAT1
         BNE   SKIPRIBE                                          920428
NOS1     EQU   *
*
         CLI   CIS2,C'*'               Both Own and Wait?
         BE    NOS2
         CLC   CIS2(1),CSTAT2
         BNE   SKIPRIBE                                          920428
NOS2     EQU   *
*
         CLC   RIBQNAME,=CL8'SPFEDIT'
         BNE   NOFORMAT
         CLI   RIBRNMLN,52
         BNE   NOFORMAT
         CLI   RIBRNAME+44,C' '
         BE    NOFORMAT
         MVC   CRNAME+44(8),BLANKS
         LA    R1,CRNAME+44
         TRT   CRNAME(44),TRTBLANK
         MVI   0(R1),C'('
         MVC   1(8,R1),RIBRNAME+44
         LA    R1,CRNAME+44
         TRT   CRNAME(44),TRTBLANK
         MVI   0(R1),C')'
NOFORMAT EQU   *
*
         L     R15,AISPLINK
         CALL  (15),(TBADD,TABLENM),VL,MF=(E,PL)
         LTR   R10,R15
         BNZ   EXIT
*
SKIPRIBE EQU   *                                                 920428
         LA    R6,RIBEEND-RIBE(R6)     Point to next RIBE        920428
         BCT   R7,RIBELOOP
ENDRIBES EQU   *                       End of RIB's RIBEs        920428
         CLC   RIBTRIBE,RIBNRIBE       Got all RIBEs? (OY34580)  920428
         BE    NEXTRIB                 Yes - go to next RIB      920428
*              If RIBTRIBE is not equal to RIBNRIBE, we have     920428
*              lost some RIBEs for this RIB (see APAR OY34580),  920428
*              so now we build an dummy table entry containing   920428
*              question marks to indicate incomplete data.       920428
         TM    RIBSCOPE,RIBSTEP        Internal ENQ?             920428
         BO    NEXTRIB                 Yes - skip dummy entry    920428
         MVC   CQNAME,RIBQNAME         Qname from RIB            920428
         MVI   CRNAME,C' '                                       920428
         MVC   CRNAME+1(L'CRNAME-1),CRNAME                       920428
         IC    R15,RIBRNMLN                                      920428
         BCTR  R15,0                                             920428
         EX    R15,MVCRNAME            Rname from RIB            920428
         MVC   CSYSNAME,=8C'?'         System name unknown       920428
         MVC   CJOBNAME,=8C'?'         Job name unknown          920428
         MVC   CSCOPE,=CL6'Local'                                920428
         TM    RIBSCOPE,RIBGLBL                                  920428
         BZ    *+10                                              920428
         MVC   CSCOPE,=CL6'Global'     Scope from RIB            920428
         MVC   CSTAT1,=8C'?'           Status-1 unknown          920428
         MVC   CSTAT2,=8C'?'           Status-2 unknown          920428
         CLC   RIBQNAME,=CL8'SPFEDIT'  Re-format SPFEDIT ENQs    920428
         BNE   DUMMYADD                                          920428
         CLI   RIBRNMLN,52                                       920428
         BNE   DUMMYADD                                          920428
         CLI   RIBRNAME+44,C' '                                  920428
         BE    DUMMYADD                                          920428
         MVC   CRNAME+44(8),BLANKS                               920428
         LA    R1,CRNAME+44                                      920428
         TRT   CRNAME(44),TRTBLANK                               920428
         MVI   0(R1),C'('                                        920428
         MVC   1(8,R1),RIBRNAME+44                               920428
         LA    R1,CRNAME+44                                      920428
         TRT   CRNAME(44),TRTBLANK                               920428
         MVI   0(R1),C')'                                        920428
DUMMYADD L     R15,AISPLINK                                      920428
         CALL  (15),(TBADD,TABLENM),VL,MF=(E,PL)                 920428
         LTR   R10,R15                                           920428
         BNZ   EXIT                                              920428
*
NEXTRIB  LR    R9,R6                   Next RIB starts here      920428
         BCT   R4,RCOK
*
NORIBE   EQU   *
         CH    R3,=H'8'
         BE    LOOP
*
LOOPEND  EQU   *
*
* *** GQSCAN END
*
         L     R15,AISPLINK
         CALL  (15),(TBTOP,TABLENM),VL,MF=(E,PL)
         L     R15,AISPLINK
         CALL  (15),(TBDISPL,TABLENM,DISPNL,MESSAGE),VL,MF=(E,PL)
         LR    R10,R15
         L     R15,AISPLINK
         CALL  (15),(TBEND,TABLENM),VL,MF=(E,PL)
         C     R10,F8                  Was 'END' pressed?
         BNE   CREATETB
*
EXIT     EQU   *
         DELETE EP=ISPLINK
         L     R13,4(,R13)             Recall R13 from savearea
         FREEMAIN RU,LV=CORESIZE,A=(11) Free core again
         LR    R15,R10
         RETURN (14,12),RC=(15)        Recall registers & return
*
MVCRNAME MVC   CRNAME(0),RIBRNAME
RNCOPY   OC    CIRN(0),0(R4)
CLCIJN   CLC   CIJN(0),RIBEJBNM
*
CHAR     DC    CL8'CHAR'
CONTROL  DC    CL8'CONTROL'
DISPLAY  DC    CL8'DISPLAY'
ERRORS   DC    CL8'ERRORS'
FIXED    DC    CL8'FIXED'
HEX      DC    CL8'HEX'
NEXT     DC    CL8'NEXT'
NOWRITE  DC    CL8'NOWRITE'
ORDER    DC    CL8'ORDER'
RETURN   DC    CL8'RETURN'
TBADD    DC    CL8'TBADD'
TBCREATE DC    CL8'TBCREATE'
TBVCLEAR DC    CL8'TBVCLEAR'
TBDISPL  DC    CL8'TBDISPL'
TBEND    DC    CL8'TBEND'
TBGET    DC    CL8'TBGET'
TBPUT    DC    CL8'TBPUT'
TBSARG   DC    CL8'TBSARG'
TBSCAN   DC    CL8'TBSCAN'
TBSORT   DC    CL8'TBSORT'
TBTOP    DC    CL8'TBTOP'
VDEFINE  DC    CL8'VDEFINE'
BLANKS   DC    CL8' '
*
TABLEID  DC    CL8'JJ$TXXXX'
*
DISPNL   DC    CL8'JJ$PWA'
*
SYSNAME  DC    CL8'SYSNAME'
JOBNAME  DC    CL8'JOBNAME'
SCOPE    DC    CL8'SCOPE'
STAT1    DC    CL8'STAT1'
STAT2    DC    CL8'STAT2'
QNAME    DC    CL8'QNAME'
RNAME    DC    CL8'RNAME'
*
ISN      DC    CL8'ISN'
IJN      DC    CL8'IJN'
ISC      DC    CL8'ISC'
IS1      DC    CL8'IS1'
IS2      DC    CL8'IS2'
IQN      DC    CL8'IQN'
IRN      DC    CL8'IRN'
*
VARLIST  DC    C'(SYSNAME JOBNAME SCOPE STAT1 STAT2 QNAME RNAME)'
*
F4       DC    F'4'
F6       DC    F'6'
F8       DC    F'8'
F44      DC    F'44'
F54      DC    F'54'
*
EGQSCAN  GQSCAN MF=L
EGQSCANL EQU   *-EGQSCAN
*
TRTBLANK DC    XL256'00'
         ORG   TRTBLANK+X'00'
         DC    C' '
         ORG   TRTBLANK+C' '
         DC    C' '
         ORG
*
TRTASTER DC    XL256'00'
         ORG   TRTASTER+C'*'
         DC    C'*'
         ORG
*
         LTORG
*
         YREGS
*
CORE     DSECT
SAVEAREA DS    18F                     Save area
TABLENM  DS    CL8
TABLEIDX EQU   TABLENM+4,1,C'F'
CIJN     DS    CL8
CIJNL    DS    1X
CISN     DS    CL8
CISC     DS    CL6
CIS1     DS    CL4
CIS2     DS    CL4
CIQN     DS    CL8
CIQNL    DS    1X
CIRN     DS    CL44
CJOBNAME DS    CL8
CSYSNAME DS    CL8
CSCOPE   DS    CL6                     LOCAL GLOBAL
CSTAT1   DS    CL4                     EXCL SHR
CSTAT2   DS    CL4                     OWN WAIT
CQNAME   DS    CL8
CRNAME   DS    CL256
GQSRNAME DS    CL44                    Datasetname for GQSCAN
AISPLINK DS    1F
GQSLEN   DS    1X
PL       DS    20F
MESSAGE  DS    CL8                     Name of table
LGQSCAN  GQSCAN MF=L
QSCNTKN  DS    1F
QSCNAREA DS    CL(QSCNLEN)             QSCAN area                920428
QSCNLEN  EQU   CORE+CORESIZE-QSCNAREA  QSCAN area length         920428
CORESIZE EQU   16*4096                 Getmain exactly 16 pages  920428
*
         ISGRIB
         IHAPSA
         IHAASCB
*
         END
./ ADD NAME=JJ$PWA      01 01 99228 99228 101409 0014 0014 0000 IBMUSER
)ATTR
  @ TYPE(OUTPUT) INTENS(LOW)
  # TYPE(INPUT) INTENS(HIGH)
)BODY
%------------------------------ GQSCAN -----------------------------------------
%Command ===>_ZCMD                                            %Scroll ===>_ZCSR%
%Sysname  Jobname  Scope  Type Stat  Qname    Rname
#ISN     #IJN     #ISC   #IS1 #IS2  #IQN     #IRN
)MODEL
@Z       @Z       @Z     @Z   @Z    @Z       @Z
)INIT
  .ZVARS = '(SYSNAME JOBNAME SCOPE STAT1 STAT2 QNAME RNAME)'
)END
