         MACRO
&LABEL   STCP#OPT &SECT=LABEL,&OPTS=STCPOPTS,&PFX=OPT,&SSN=TCP,        +
               &SLIM=30,&RLIM=30,&LPA=STCPLPA,&JPA=STCPJPA,&CUU=620,   +
               &SVC=255,&SVCMOD=STCPSVC,&ID=
.*
.* Copyright 2002 James M. Morrison
.*
         LCLC  &P
&P       SETC  '&PFX'
.*
         AIF   ('&ID' EQ 'NAME').NAME
&LABEL   IM#SECT &SECT       CSECT, DSECT, or LABEL
&P.MOD1  DC    CL8'&LPA'     name of module to be loaded to CSA
&P.MOD2  DC    CL8'&JPA'     name of module to be loaded to JPA
&P.MOD3  DC    CL8'&SVCMOD'  name of module to be loaded for SVC
&P.CUU   DC    XL2'&CUU'     default 3088 cuu (1st addr of pair)
         DC    XL2'00'       spare
&P.SSN   DC    CL4'&SSN'     Subsystem name
&P.SVCOP DC    X'0A'         TCP/IP SVC opcode
&P.SVC#  DC    AL1(&SVC)     TCP/IP SVC number
&P.SVC   EQU   &P.SVCOP,2    SVC instruction
&P.LADB  DC    H'20'         0xxx of LA R15,xxx when OPTSVC overlaid
&P.OPL1  EQU   *-&P.MOD1     SGD section length
         AIF   ('&ID' EQ 'SGD').MEX
*
&P.SLIM  DC    A(&SLIM)      Max SEND queue length
&P.RLIM  DC    A(&RLIM)      Max RECV queue length
*
.MEX     MEXIT ,
.NAME    ANOP
&LABEL   DC    CL8'&OPTS'    options module name               STCP#OPT
         MEXIT ,
.DOC     ANOP
.*---------------------------------------------------------------------
.*  STC#OPT - generate options module name, options module, or DSECT
.*---------------------------------------------------------------------
         END   ,
