         TITLE 'P D S  --  PDS ALIAS                         09/28/88'
***********************************************************************
***      ALIAS SUBCOMMAND                                           ***
***********************************************************************
*
ALIAS    CSECT
         ENTRY LIST170,DIRENTRY,REPLACE,FIND,PATTERN,INVABBR
         ENTRY FAMS
         WXTRN VTSOCMD
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         USING WORKAREA,R7
         USING PDSCOMM,R12
         SPACE 1
         USING *,R8
         TM    DS1SMSFG,DS1PDSE         PDSE DATASET?         DRK AUG05
         BNO   ALIASC                   NO, CONTINUE          DRK AUG05
         TM    FLAGSCC,RECFMU           RECFM=U?              DRK AUG05
         BNO   ALIASC                   NO, CONTINUE          DRK AUG05
         MVI   MTLEN,5                  LENGTH OF INSERT      DRK SEP05
         MVC   INSERT#1(5),=C'ALIAS'    INSERT COMMAND NAME   DRK SEP05
         M$MSG L996$1                   NOT SUPPORTED MSG     DRK SEP05
         M$ERRST MSGBLANK               ONE BLANK LINE        DRK SEP05
         B     NEWCMD                   NOBBLED               DRK AUG05
         SPACE 1                                              DRK AUG05
ALIASC   DS    0H                                             DRK AUG05
         TM    SPFLAG2,SPFLNCD            LINE COMMAND?        SS AUG85
         BNO   ALIAS0                     NO, BRANCH
         MVC   SPFSAVM2,MEMBER2           SAVE MEMBER NAME     SS AUG85
         B     ALIAS1                     NO, BRANCH
         SPACE 2
ALIAS0   MVC   MEMBERD+1(2),LMEMBER2      CHANGE DEFAULT MEMBER LENGTH
         MVC   MEMBERD+1+2(8),MEMBER2     CHANGE DEFAULT MEMBER NAME
         MVI   MEMBERD,FMEMBER1           ONLY ONE MEMBER NAME NOW
         NI    PMEMMIN,FF-X'80'           NO MEMBER LIST NOW
         BAS   R14,DEFGROUP                    ADD DEFAULT GROUP
         SPACE 2
ALIAS1   MVC   DIRNAME,MEMBER2            SET ALIAS NAME
         MVI   ENTRYPT,C'?'               ENTRY POINT NOT KNOWN YET
         TM    DIRFLAG,X'80'              MODULE A CURRENT ALIAS?
         BZ    ALIAS2                     NO, BRANCH
         TM    FLAGSCC,RECFMU             LOAD MODULE?
         BO    ALIAS3                     YES, BRANCH
         B     ALIAS4                     NO, BRANCH
         SPACE 1
ALIAS2   TM    FLAGSCC,RECFMU             LOAD MODULE?
         BZ    ALIAS4                     NO, BRANCH
         SPACE 1
         TM    DIRATTR2,DIRAOSLE+DIRAPFLG VS LKED & APF DATA PRESENT?
         BZ    ALIAS2Y                    NO, BRANCH
         SPACE 1
         LA    R2,DIRAPF                  POINT TO APF INFORMATION
         TM    DIRATTR,ATTRSCTR           SCATTER LOADED?
         BZ    *+8                        NO, BRANCH
         LA    R2,DIRAPF3                 YES, POINT TO APF DATA
         LA    R4,11(,R2)                 POINT TO ALIAS APF DATA
         TM    DIRATTR2,DIR2SSI           SSI PRESENT?
         BZ    ALIAS2B                    NO, BRANCH
         SPACE 1
         LA    R2,1(,R2)                  ROUND TO HALFWORD
         N     R2,=F'-2'                  FFFFFFFE MASK
         LA    R4,1(,R4)                  ROUND TO HALFWORD
         N     R4,=F'-2'                  FFFFFFFE MASK
         MVC   0(4,R4),0(R2)              MOVE SSI INFORMATION
         LA    R4,4(,R4)                  MOVE PAST SSI INFORMATION
         LA    R2,4(,R2)                  POINT TO APF DATA
         SPACE 1
ALIAS2B  CLI   0(R2),1                    IS APF LENGTH OK?
         BE    ALIAS2D                    YES, BRANCH
         M$MSG L710                       BAD APF INFORMATION FORMAT
         LA    R2,ZERO                    SET APF=0
         SPACE 1
ALIAS2D  MVI   0(R4),1                    SET PROPER LENGTH
         MVC   1(1,R4),1(R2)              MOVE IN APF DATA
         SPACE 3
ALIAS2Y  LA    R4,DIREP                   POINT TO START OF ALIAS INFO.
         LA    R5,(DIREND2-DIRUSER+1)/2   NUMBER OF HALFWORDS
         TM    DIRATTR,ATTRSCTR           SCATTER LOADED?
         BNO   ALIAS2Z                    NO, BRANCH
         LA    R4,DIREPSC                 POINT TO START OF ALIAS INFO.
         LA    R5,(DIREND3-DIRUSER+1)/2   NUMBER OF HALFWORDS
         SPACE 2
ALIAS2Z  MVC   0(3,R4),DIREPA             MOVE IN MAIN MODULE ENTRY
         MVC   3(8,R4),MEMBER1            ADD MAIN MODULE NAME
         TM    DIRATTR2,DIR2SSI           SSI INFORMATION?
         BNO   *+8                        NO, BRANCH
         LA    R5,2(,R5)                  YES, TWO MORE HALFWORDS
         NI    DIRFLAG,X'E0'              TURN OFF HALFWORD COUNT
         OI    DIRFLAG,*-*                <<EXECUTED>>
         EX    R5,*-4                     MOVE IN NUMBER OF HALFWORDS
         SPACE 1
ALIAS3   TM    DIRFLAG,X'80'              ALIAS ENTRY?
         BO    ALIAS3S                    YES, BRANCH
         NI    DIRATTR3,FF-DIRAA64-DIRAA31
         TM    DIRATTR3,DIRAM64           AMODE 64?           DRK OCT02
         BNO   *+8                        NO, BRANCH
         OI    DIRATTR3,DIRAA64           YES, SET THE DIRECTORY
         TM    DIRATTR3,DIRAM31           AMODE 31?
         BNO   *+8                        NO, BRANCH
         OI    DIRATTR3,DIRAA31           YES, SET THE DIRECTORY
         SPACE 1
ALIAS3S  L     R15,=A(READESD)            SCAN ESD FOR MEMBER NAME
         BASR  R14,R15                    MEMBER NAME IN ESD ENTRIES?
         B     ALIAS4                     NO, PSEUDO ENTRY
         SPACE 1
         OI    DIRATTR+1,ATTREP0          ASSUME ENTRY POINT ZERO
         LTR   R1,R1                      CORRECT?
         BZ    *+8                        YES, BRANCH
         XI    DIRATTR+1,ATTREP0          NO, INSURE ATTR FLAG OFF
         STCM  R1,B'0111',DIREPA          SAVE ENTRY ADDRESS
         TM    DIRATTR,ATTRSCTR           SCATTER LOADED?
         BNO   *+8                        NO, BRANCH
         STCM  R15,B'0011',DIRSCEP        YES, SAVE ESDID OF ENTRY PT.
         STC   R0,#ALIAESD                SAVE MODE RMODE/AMODE INFO
         TM    DIRATTR,ATTROVLY           OVERLAY ATTRIBUTES
         BO    ALIAS4                     YES, BRANCH
         NI    DIRATTR3,FF-DIRAA64-DIRAA31                    DRK OCT02
         TM    #ALIAESD,AMODE64           AMODE 64?           DRK OCT02
         BNO   *+8                        NO, BRANCH
         OI    DIRATTR3,DIRAA64           YES, SET THE DIRECTORY
         TM    #ALIAESD,AMODE31           AMODE 31?
         BNO   *+8                        NO, BRANCH
         OI    DIRATTR3,DIRAA31           YES, SET THE DIRECTORY
         SPACE 1
         TM    DIRATTR3,DIRRMANY          RMODEANY?
         BNO   ALIAS4                     NO, BRANCH
         SPACE 1
         NI    DIRATTR3,FF-DIRAA64                            DRK OCT02
         OI    DIRATTR3,DIRAA31           RESET AS AMODE31
         SPACE 1
ALIAS4   OI    DIRFLAG,X'80'              SET ALIAS FLAG
         BAS   R2,OPENSTOW                OPEN STOW DCB; ENQUEUES
         B     NEWCMD                     COULD NOT OPEN -- ERROR
         STOW  STOWDCB,DIRNAME,A          ADD ALIAS TO DIRECTORY
         SPACE 1
         B     *+4(R15)                PROCESS RETURN CODE
         B     ALIAS5                     00 - SUCCESSFUL
         B     MEMEXIST                   04 - MEMBER ALREADY EXISTS
         EX    0,*                        08 - SHOULD NOT HAPPEN - ADD
         B     FULLDIR                    12 - DIRECTORY IS FULL
         B     IOERROR                    16 - I/O ERROR IN DIRECTORY
         SPACE 3
ALIAS5   M$MSG L010                       MSG - ALIAS ASSIGNED
         TM    FLAGSCC,RECFMU             MUST WE GIVE ENTRY POINT?
         BZ    NEWCMD                     NO, BRANCH
         UNPK  INSERT#1(7),DIREPA(4)
         TR    INSERT#1(6),TRTABLE
         MVC   INSERT#1+6(2),BLANKS
         MVC   INSERT#2(8),ENTRYPT    NAME OF THE ENTRY POINT
         TR    INSERT#2(8),TRLINE     MAKE PRINTABLE
         LA    R1,L102$1              ASSUME NONE FOUND
         CLI   ENTRYPT,C'?'           ANY FOUND?
         BE    *+8                    NO, BRANCH
         LA    R1,L103$2              YES, SHOW ENTRY SYMBOL
         M$MSG (R1)
         B     NEWCMD
