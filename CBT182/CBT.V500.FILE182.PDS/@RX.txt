         TITLE 'P D S  --  PDS RX COMMAND INTERFACE          09/28/88'
***********************************************************************
***      RX INTERFACE                                               ***
***********************************************************************
*
         SPACE 1
RX       CSECT
         BASR  R5,0                        BASE REGISTER
         USING *,R5
         MVC   DIRNAME,##SUBCAL            WHO TO CALL
         L     R1,ADDRCBUF                 POINT TO THE COMMAND BUFFER
         SR    R15,R15
         IC    R15,1(,R1)                  LENGTH OF BUFFER
         BCTR  R15,0                       MACHINE LENGTH OF BUFFER
         MVC   MSGTEXT1(*-*),0(R1)         <<EXECUTED>>
         EX    R15,*-6                     MOVE IN THE COMMAND BUFFER
         LA    R1,MSGTEXT1                 NEW COMMAND BUFFER
         ST    R1,ADDRTEXT                 POINT TO THE COMMAND BUFFER
         SPACE 2
*** IN A LINKLIST LIBRARY?
         BLDL  0,BLDLLIST ----+            CHECK FOR A LOAD MODULE
         LA    R1,X'100'      |            TTR=1 (FOR SEQUENTIAL)
         ST    R1,DIRTTR      |            RESET TTR
         LTR   R15,R15    <---+            IN THE LINKLIST?
         BZ    RX020                       YES, LINK TO IT
         L     R15,CVTPTR(,R0)             NEED TO ALSO CHECK LPALIB
         L     R15,CVTLPDIA-CVTMAP(R15)    POINT TO LINK PACK DIRECTORY
         SPACE 2
*** MODULE NOT IN LINKLIST, TRY THE IN-STORAGE LPALIB DIRECTORY
RX010    CLC   8(8,R15),DIRNAME            THIS MODULE NAME?
         BE    RX020                       YES, LINK TO IT
         CLI   8(R15),X'FF'                END OF DIRECTORY?
         LA    R15,40(,R15)
         BNE   RX010                       NO, BRANCH
         MVC   INSERT#1(8),##SUBCAL        YES, ISSUE AN ERROR MESSAGE
         M$MSG L859$1
         B     NEWCMD
         SPACE 1
RX020    L     R1,ADDRTEXT                 CPPL START
*        M$ERRST (R1)                      DELETE * FOR OUTPUT
         L     R14,ADDRECT
         MVC   ECTPCMD-ECT(8,R14),ECTSCMD-ECT(R14)  PRIMARY   CMD NAME
         MVC   ECTSCMD-ECT(8,R14),BLANKS            SECONDARY CMD NAME
         LA    R1,ADDRTEXT                 CPPL START
         SPACE 2
         L     R3,0(,R1)             START OF ARGUMENT LIST
         AH    R3,0(,R3)             END OF ARGUMENT LIST
         MVC   0(15,R3),RX050        ADD PDS PARM FOR EXTERNAL CALL
         LA    R15,WORKTBL           START OF PARAMETER LIST
         ST    R15,10(,R3)           PASS ADDRESS OF RE-ENTRY LIST
         MVC   0(12,R15),RX060       SAVE RE-ENTRY CODE
         LA    R0,RX070              START OF WRITE SUBROUTINE
         ST    R0,12(,R15)           OPERAND OFFSET
         ST    R7,16(,R15)           SAVE WORK AREA ADDRESS
         LA    R3,##SUBCAL           COMMAND PROCESSOR
         SPACE 2
         XC    PARMLIST(8),PARMLIST  INITIALIZE LINK LIST
         LINK  EPLOC=((3)),SF=(E,PARMLIST)
         L     R14,ADDRECT
         MVC   ECTSCMD-ECT(8,R14),ECTPCMD-ECT(R14)  SECONDARY CMD NAME
         MVC   ECTPCMD-ECT(8,R14),PDSNAME           PRIMARY   CMD NAME
         BR    R2                    RETURN TO CALLER
         DROP  R5
RX050    DC    C'  PDSCALL(XXXX)'    PDS CALLER IDENTIFIER
         SPACE 1
RX060    ST    R1,20(,R15)         0 REENTER: SAVE PARAMETER ADDRESS
         L     R15,12(,R15)        4 REENTER: GET ROUTINE ADDRESS
         BAS   R1,0(,R15)          8 REENTER: LINK TO SUBROUTINE
***      DS    A                  12 REENTER: A(ROUTINE)
***      DS    A                  16 REENTER: A(WORK AREA)
***      DS    A                  20 REENTER: A(MESSAGE)
         SPACE 3
RX070    SAVE  (14,12)
         LR    R8,R15                BASE REGISTER ADDRESS
         USING RX070,R8              BASE REGISTER NOTIFICATION
         L     R7,4(,R1)             WORK AREA ADDRESS
         L     R1,8(,R1)             OUTPUT MESSAGE START
         LM    R9,R12,BASES          RESTORE BASE REGISTERS
         LA    R15,VALSAVE           SAVE AREA FOR THIS ROUTINE
         ST    R13,4(,R15)
         ST    R15,8(,R13)           CHAIN SAVE AREAS
         LR    R13,R15
         TM    SPFLAG0,SPFDON        DIALOG MODE               SS OCT88
         BZ    RX079                 NO, NORMAL MODE           SS OCT88
         SR    R15,R15               FOR INSERT                SS OCT88
         ICM   R15,1,0(R1)           GET LENGTH                SS OCT88
         BZ    RX079                 NO LENGTH, USE DEFAULT 80 SS OCT88
         CH    R15,RX078             GREATER THEN MAX          SS OCT88
         BNH   *+8                   NO, OK                    SS OCT88
         LH    R15,RX078             YES, SET TO MAX           SS OCT88
         BCTR  R15,0                 -1 FOR MVC                SS OCT88
         EX    R15,RX077             EXECUTE MVC               SS OCT88
         LA    R15,5(,R15)           COMPUTE BUFF LENGTH       SS OCT88
         SLL   R15,16                CLEAR OUT OTHER BYTES     SS OCT88
         ST    R15,WORKTBL+32        SET IN THE MESSAGE        SS OCT88
         LA    R1,WORKTBL+32         MESSAGE ADDRESS           SS OCT88
        M$PUTLIN (R1),ATTN=RX080     OUTPUT THIS MESSAGE       SS OCT88
         SR    R15,R15               SUCCESSFUL OUTPUT         SS OCT88
         B     RX090                 RETURN                    SS OCT88
RX077    MVC   WORKTBL+32+4(0),1(R1) EX MOVE MESSAGE           SS OCT88
RX078    DC    H'158'                MAX LENGTH                SS OCT88
RX079    DS    0H                       *                      SS OCT88
         MVC   MSGTEXT2+4(80),1(R1)
         LA    R1,79+4               LENGTH
         SLL   R1,16                 CLEAR OUT OTHER BYTES
         ST    R1,MSGTEXT2           SET IN THE MESSAGE
         LA    R1,MSGTEXT2           MESSAGE ADDRESS
        M$PUTLIN (R1),ATTN=RX080     OUTPUT THIS MESSAGE
         SR    R15,R15               SUCCESSFUL OUTPUT
         B     RX090                 RETURN
RX080    LA    R15,4                 SET REGISTER R15
RX090    L     R13,4(,R13)           UNCHAIN SAVE AREAS
         ST    R15,16(R13)           SAVE RETURN CODE
         RETURN (14,12),T,RC=(15)    RETURN
