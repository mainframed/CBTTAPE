         TITLE 'ISPF - O AND T SUBCOMMANDS      '              SS NOV85
         SPACE 2
ISPSETP  DS    0H          ****   O AND T SUBCOMMANDS ****     SS NOV84
         USING ISPSETP,R4                                      SS NOV85
         NI    SPFLAG4,255-SPFMCMD  RESET FLAG                 SS NOV84
         MVI   SPFVZCMD,C' '                                   SS NOV84
         MVC   SPFVZCMD+1(249),SPFVZCMD                        SS NOV84
         MVC   MEMLNMSV,ISPBLK8                                SS NOV84
         MVC   SPFSAVM2,ISPBLK8                                SS AUG85
         MVC   SPFMEMB,ISPBLK8                                 SS NOV84
         L     R14,SPFVTPT                                     SS NOV85
         TM    $TLF1(R14),#E       IS THIS FOR CMD SEARCH      SS AUG86
         BZ    ISPSETP1            NO, CONTINUE                SS AUG86
         LH    R15,PUTGETBF       GET LENG                     SS NOV84
         SH    R15,ISPH5          LESS 5 MVC                   SS NOV84
ISPSETP9 MVC   SPFVZCMD(0),SPFINPUT                            SS NOV84
         EX    R15,ISPSETP9       COPY TO CMD FROM INPUT       SS NOV84
         OI    SPFLAG4,SPFNOIV     DO NOT DISPLAY INVALID      SS AUG86
         B     ISPSETP2            FORCE NONDISPL              SS AUG86
ISPSETP1 DS    0H                       *                      SS NOV86
         TM    $TLF2(R14),#Z                                   SS NOV84
         BO    ISPSETP2                                        SS NOV84
         LH    R15,PUTGETBF       GET LENG                     SS NOV84
         SH    R15,ISPH5          LESS 5 MVC                   SS NOV84
ISPSETP4 MVC   SPFVZCMD(0),SPFINPUT                            SS NOV84
         EX    R15,ISPSETP4       COPY TO CMD FROM INPUT       SS NOV84
         BAS   R2,ISPSHCMD                                     SS NOV84
         CLI   SPFVZCMD,C' '                                   SS NOV84
         BE    ISPSETP3                                        SS NOV84
ISPSETP2 DS    0H                                              SS NOV84
         OI    SPFLAG4,SPFNOND      INVOKE PANEL NON-DISPLAY   SS NOV84
ISPSETP3 DS    0H                                              SS NOV84
         BAS   R2,ISPSETVI                                     SS NOV85
         L     R1,SPFPANT                                      SS NOV84
         ST    R1,SPFPANC                                      SS NOV84
         L     R14,SPFVTPT                                     SS NOV85
         SR    R15,R15                                         SS NOV84
         IC    R15,$TLEX(R14)                                  SS NOV84
         SLL   R15,3                 TIME 8 : INDEX TO OFFSET  SS OCT88
         AL    R15,ISP$PNA                                     SS MAR85
         MVC   SPFPSTK(8),0(R15)                               SS NOV84
         TM    DSORG,DS1DSGPO        IS IT PO                 ABL OCT85
         BO    ISPSETPP              YES, CONTINUE            ABL OCT85
         CLI   SPFPSTK+4,C'P'        NO, IS IT P..0P?         ABL OCT85
         BNE   ISPSETPP              NO, CONTINUE             ABL OCT85
         CLI   SPFPSTK+5,C'@'        YES, IS IT P..0P@?       ABL FEB87
         BNE   ISPSETPP              NO, CONTINUE             ABL FEB87
         MVI   SPFPSTK+4,C'S'        YES, USE P..OS@P         ABL OCT85
ISPSETPP DS    0H                                              SS NOV84
         BAS   R2,ISPPANDI                                     SS NOV85
         NI    SPFLAG4,255-SPFNOIV RESET, IF USED              SS NOV86
         CLC   SPFZSEL(4),ISPCEND                              SS NOV84
         BE    ISPSETPC               CONTINUE                 SS NOV86
         CLI   SPFZSEL,C'?'           INVALID                  SS NOV86
         BE    ISPSETPI               YES                      SS NOV86
         CLC   SPFZSEL(3),ISPPGM      SELECT PGM               SS NOV86
         BE    ISPSETPS               PGM  SELECT SUPPORT      SS NOV86
         CLC   SPFZSEL(3),ISPCCMD     SELECT CMD               SS NOV86
         BE    ISPSETPS               CMD  SELECT SUPPORT      SS NOV86
         CLC   SPFZSEL(3),ISPTRAP     TRAP   CMD               SS NOV88
         BE    ISPSETT                TRAP SUPPORT             SS NOV88
         MVC   SPFINPUT(250),SPFZSEL                           SS NOV84
         MVC   PUTGETBF(4),ISPLEN2                             SS NOV84
         L     R14,SPFVTPT                                     SS NOV85
ISPSETP5 DS    0H                                              SS NOV84
         TM    $TLF2(R14),#A        ACTIVATE RESCAN?           SS NOV84
         BZ    ISPSETP6             NO, CHECK COMMAND          SS NOV84
         LA    R15,104              TO ISPOPTT                 SS NOV85
         BR    R3                   RETURN TO ROUTER           SS NOV85
ISPSETP6 DS    0H                                              SS NOV85
         LA    R15,24               TO CMDCK                   SS NOV85
         BR    R3                   RETURN TO ROUTER           SS NOV85
ISPSETPS DS    0H                       *                      SS NOV86
         BAS   R2,ISPSLCTI              SELECT PROCESSOR       SS NOV86
         B     ISPSETPC                 *                      SS NOV86
*                                       *                      SS NOV88
*  SYSOUT TRAP CMD                      *                      SS NOV88
*                                       *                      SS NOV88
ISPSETT  DS    0H                       *                      SS NOV88
         CLC   SPFZENVI(8),ISPSETTV  CHECK FOR 2.3             SS NOV88
         BL    ISPSETTE                 NO ENV                 SS NOV88
         CLI   SPFTPSET,C'Y'            IS TRAP SET            SS NOV88
         BNE   ISPSETTE                 NO, NO                 SS NOV88
         BAS   R2,ISPTRAPI              CALL TRAP PROCESSOR    SS NOV88
         CLI   SPFTCNT,C'0'             ANY OUTPUT             SS NOV88
         BE    ISPSETPC                 NO, JUST RETURN        SS NOV88
         BAS   R2,ISPSETST         SET AND RETREIVE STATUS     SS NOV88
         LH    R1,SPFFUNLP+0       GET LOG LAST PANEL          SS NOV88
         LNR   R1,R1               SET FOR SWAP                SS NOV88
         BAS   R2,ISPSTKI          SWAP TO LOG PANEL           SS NOV88
         LA    R15,100             CODE.. PROCESS COMMAND      SS NOV88
         BR    R3                  RETURN TO ROUTER            SS NOV88
ISPSETTE DS    0H                       *                      SS NOV88
         MVC   SPFZSEL(3),ISPCCMD    CHANGE TO A COMMAND       SS NOV88
         B     ISPSETPS                 *                      SS NOV88
ISPSETTV DC    C'ISPF 2.3'           VERSION REQUIRES TSO/E    SS NOV88
         SPACE 2                        *                      SS AUG86
ISPSETPC DS    0H                       *                      SS NOV86
         LA    R15,40                   ISPDISP6               SS NOV86
         BR    R3                       RETURN TO ROUTER       SS NOV86
ISPSETPI DS    0H                       *                      SS NOV86
         LA    R15,28                   INVALID FUNCTION       SS NOV86
         BR    R3                       RETURN TO ROUTER       SS NOV86
         DROP  R4                                              SS NOV85
         SPACE 3                                               SS NOV84
         TITLE  'ISPF - DISPLAY PANEL ROUTINE '                SS AUG86
*                                                              SS NOV84
*   SELECT PANEL DISPLAY ROUTINE    RETURN = R3                SS NOV84
*                                                              SS NOV84
ISPPAND  DS    0H                                              SS NOV84
         USING *,R4                                            SS MAR85
         TM    SPFLAG4,SPFNOND    INVOKE PANEL NON-DISPLAY     SS NOV84
         BZ    ISPPAND2                                        SS NOV84
         BAS   R2,ISPCTLND              *                      SS JUL87
         NI    SPFLAG4,255-SPFNOND                             SS NOV84
         L     R1,SPFPANC                                     ABL FEB87
*        CLC   0(8,R1),ISP$OPUX   P80OPUX OPTION PANEL?       ABL FEB87
*        BNE   ISPPAND3           NO, BRANCH                  ABL FEB87
*        BAS   R2,ISPDSPSV        YES, CONTROL DISPLAY SAVE   ABL FEB87
*        OI    SPFLAG4,SPFNOND    SET A FLAG FOR LATER        ABL FEB87
         B     ISPPAND3                                        SS NOV84
ISPPAND2 DS    0H                                              SS NOV84
         OI    SPFLAG3,SPFREFR        MAKE SURE OF REFRESH     SS AUG86
ISPPAND3 DS    0H                                              SS NOV84
         OI    ##ADRPA#,$A+$D           IGNORE ATTENTION NOW
         BAS   R2,REFCHECK              CONDITIONAL REFRESH   ABL JAN87
         LA    R14,ISPDISP        DISPLAY TABLE                SS NOV84
         L     R15,SPFPANC        PANEL NAME                   SS NOV84
         LA    R0,ISPBLK8         NO MSG                       SS NOV86
         CLC   SPFMSGID(4),ISPPDSM ANY MSG                     SS NOV84
         BNE   ISPPAND4                                        SS NOV84
         LA    R0,SPFMSGID                                     SS NOV84
*    ALWAY SPECIFY PANEL ID             *                      SS DEC87
ISPPAND4 DS    0H                                              SS NOV84
         NI    SPFLAG2,255-SPFNPOS   RESET FREEZE/POSITION    ABL APR86
         LA    R1,VZCMD           CURSOR AT CMD LINE           SS NOV84
         STM   R14,R1,PARMLIST                                 SS NOV84
         LA    R1,SPFCURSO           CURSOR OFFSET            ABL JUN85
         ST    R1,PARMLIST+16                                 ABL JUN85
         OI    PARMLIST+16,X'80'                              ABL JUN85
         TM    SPFLAG4,SPFNOND       NON-DISPLAY AND P..OPUX? ABL FEB87
         BO    ISPPAND5              YES, BRANCH              ABL FEB87
         L     R1,SPFPANC            PANEL NAME               ABL FEB87
         MVC   SPFPANPR(8),0(R1)     SAVE LAST PANEL NAME     ABL FEB87
         SPACE 1                                               SS NOV84
ISPPAND5 DS    0H                                              SS NOV84
         LA    R1,PARMLIST                                     SS JUL84
         L     R15,ISPLINK                                     SS JUL84
         BASR  R14,R15                                         SS JUL84
         CLI   FLAGSSS,0          RETURN KEY ACTIVE?           SS NOV84
         MVI   FLAGSSS,0                                       SS NOV84
         BNE   ISPPAND5           YES, REPEAT THE DISPLAY      SS NOV84
         MVI   SPFMSGID,C' '      TURN OFF MSG                 SS NOV84
         MVI   SPFCURSO+3,0       RESET THE CURSOR OFFSET     ABL JUN85
         ST    R15,SPFFIXED       SAVE RC                      SS JUL84
         BAS   R2,REFSAVE         SAVE THE TPIN, TPOUT COUNTS ABL JAN87
*        TM    SPFLAG4,SPFNOND    NON-DISPLAY AND P..OPUX?    ABL FEB87
*        BNO   ISPPAND6           NO, BRANCH                  ABL FEB87
*        BAS   R2,ISPDSPRS        YES, CONTROL DISPLAY RESTOREABL FEB87
*        NI    SPFLAG4,255-SPFNOND  CLEAR THE FLAG            ABL FEB87
ISPPAND6 DS    0H                                             ABL FEB87
         AIF ('&RETURNX' EQ 'NO').RETURN4                      SS NOV84
         CLI   SPFFIXED+3,8           RETURN CODE=8?          ABL OCT84
         BNE   ISPPAND8               NO, BRANCH              ABL OCT84
         BAS   R2,ISPDVGET                                     SS NOV84
         SPACE 1                                              ABL OCT84
         CLI   SPFRETRN,C'R'         "RETURN"                 ABL OCT84
         BNE   ISPPAND8              YES, BRANCH               SS NOV84
         CLC   SPFPANT,SPFPANC       TOP  PANEL                SS NOV84
         BNE   ISPPAND8              NO,  NO PROBLEM           SS NOV84
         MVI   FLAGSSS,C'2'          REINVOKE ALL LATER       ABL OCT84
ISPPAND8 DS    0H                                             ABL OCT84
.RETURN4 ANOP                                                 ABL OCT84
         SPACE 1                                              ABL OCT84
         CLI   SPFFIXED+3,8           RETURN?                 ABL OCT84
         BNE   ISPPAND9               YES, BRANCH              SS NOV84
         MVI   SPFFIXED+3,0          RESET RETURN CODE         SS NOV84
         MVC   SPFZSEL(4),ISPCEND    SET TO END CODE           SS NOV84
         B     ISPPANDC                                        SS NOV84
ISPPAND9 DS    0H                                              SS NOV84
         CLI   SPFFIXED+3,12          NO PANEL                 SS NOV84
         BE    ISPPANDE               YES, BRANCH              SS NOV84
         CLI   SPFFIXED+3,0                                    SS NOV84
         BNE   ISPERR                                          SS NOV84
         CLC   SPFZSEL(6),ISPCPAN    NEXT PANEL                SS NOV84
         BNE   ISPPANDC              NO                        SS NOV84
         L     R15,SPFPANC           CURRENT PANEL             SS NOV84
         C     R15,SPFPANB           IS IT LAST                SS NOV84
         BE    ISPPANDD              YES, LEAVE AS LAST        SS NOV84
         LA    R15,8(R15)            NEXT                      SS NOV84
         ST    R15,SPFPANC           UPDATE DATA               SS NOV84
ISPPANDD MVC   0(8,R15),SPFZSEL+6    MOVE PANEL NAME           SS NOV84
         CLI   0(R15),C'A'           GOOD ENTRY                SS NOV84
         BL    ISPPANDE              BAD ENTRY                 SS NOV84
         LA    R1,9                  SCAN FOR 8                SS NOV84
         LA    R14,SPFZSEL+6                                  ABL FEB87
ISPPANDF CLI   0(R14),C'#'           NATIONAL?                ABL FEB87
         BE    ISPPAND#                                       ABL JAN87
         CLI   0(R14),C'$'           NATIONAL?                ABL FEB87
         BE    ISPPAND#                                       ABL JAN87
         CLI   0(R14),C'@'           NATIONAL?                ABL FEB87
         BE    ISPPAND#                                       ABL JAN87
         CLI   0(R14),C'A'           FIND END OF NAME         ABL FEB87
         BL    ISPPANDG                                        SS NOV84
ISPPAND# DS    0H                                             ABL JAN87
         LA    R14,1(,R14)           NEXT SPFZSEL CHARACTER   ABL FEB87
         LA    R15,1(,R15)           NEXT                      SS NOV84
         BCT   R1,ISPPANDF                                     SS NOV84
         B     ISPPANDK                                        SS NOV84
ISPPANDG MVI   0(R15),C' '           BLANK OUT REST            SS NOV84
         LA    R15,1(,R15)           NEXT                      SS NOV84
         BCT   R1,ISPPANDG                                     SS NOV84
ISPPANDH BAS   R2,ISPSHCMD           SHIFT ZCMD TO NEXT '.'    SS NOV84
         CLI   SPFVZCMD,C' '         IS IT BLANK               SS NOV84
         BE    ISPPAND               YES DISPLAY PANEL         SS NOV84
         OI    SPFLAG4,SPFNOND       NO, INVOKE PANEL NONDISPL SS NOV84
         B     ISPPAND                                         SS NOV84
ISPPANDC DS    0H                                              SS NOV84
         CLI   SPFZSEL,C' '          NULL                      SS NOV84
         BNH   ISPPANDN              YES                       SS NOV84
         CLI   SPFZSEL,C'?'          INVALID                   SS NOV84
         BE    ISPPANDK                                        SS NOV84
         B     ISPPANDJ              CONTINUE CHECKS           SS NOV84
ISPPANDK DS    0H                                              SS NOV84
         TM    SPFLAG4,SPFNOIV          NO INVALID MESSAGE     SS AUG86
         BO    ISPPANDX                 YES, CLEANUP AND RTN   SS AUG86
         MVC   SPFMSGID,ISPPDSM      SET BASE MSG              SS NOV84
         MVI   SPFMSGID+6,C'9'       COMMON MSG                SS NOV84
         MVI   SPFMSGID+7,C'A'       COMMON MSG                SS NOV84
         B     ISPPAND               TRY AGAIN                 SS NOV84
ISPPANDN DS    0H                                              SS NOV84
         MVC   SPFMSGID,ISPPDSM      SET BASE MSG              SS NOV84
         MVI   SPFMSGID+6,C'9'       COMMON MSG                SS NOV84
         MVI   SPFMSGID+7,C'E'       COMMON MSG                SS NOV84
         B     ISPPAND               TRY AGAIN                 SS NOV84
ISPPANDE DS    0H                                              SS NOV84
         MVC   SPFMSGID,ISPPDSM      SET BASE MSG              SS NOV84
         MVI   SPFMSGID+6,C'9'       COMMON MSG                SS NOV84
         MVI   SPFMSGID+7,C'P'       BAD PANEL                 SS NOV84
         MVC   SPFZSEL(4),ISPCEND    END CODE                  SS NOV84
ISPPANDJ DS    0H                                              SS NOV84
         CLC   SPFZSEL(4),ISPCEND    END CODE                  SS NOV84
         BNE   ISPPANDX              NO, GOT COMMAND           SS NOV84
         CLC   SPFPANC,SPFPANT       LAST PANEL                SS NOV84
         BER   R3                    RETURN END                SS NOV84
         L     R14,SPFPANC           GET ADDR                  SS NOV84
         S     R14,ISPL8             BACK UP 8                 SS NOV84
         ST    R14,SPFPANC                                     SS NOV84
         B     ISPPAND                                         SS NOV84
ISPPANDX DS    0H                                              SS NOV84
         NI    SPFLAG4,255-SPFNOIV      RESET IF USED          SS AUG86
         SPACE 2                                               SS NOV84
         NI    ##ADRPA#,FF-$A-$D        DO NOT IGNORE ATTENTION NOW
         SPACE 1
         ESTAE 0                        CANCEL THE ESTAE ADDRESS
ESTAE7   DS    0H                       Z/XDC PROBE           DRK DEC08
         MVC   STAEPARM(4),STAELIST     RESET THE ESTAE ADDRESS
         L     R2,ISPESTAE              ESTAE EXIT ADDRESS    ABL JUN85
         ESTAE (R2),CT,PARAM=(R7),MF=(E,STAEPARM)             ABL JUN85
ESTAE8   DS    0H                       Z/XDC PROBE           DRK DEC08
         BR    R3                                              SS NOV84
         SPACE 2                                               SS SEP84
         DROP  R4                                              SS MAR85
         TITLE  'ISPF - DISPLAY SINGLE PANEL  '                SS AUG86
*                                       *                      SS AUG86
*    DISPLAY A SINGLE PANEL             *                      SS AUG86
*    R15=ADDRESS OF PANEL NAME          *                      SS AUG86
*    R1 =MSG ID ADDRESS                 *                      SS AUG86
*    R15 RETURNS DISPLAY RC             *                      SS AUG86
*    SPFCURSO CAN BE SET FOR CURSOR OFFSET                     SS APR89
*    SPFCURNM CAN BE SET FOR CURSOR FIELD NAME                 SS APR89
*                                       *                      SS AUG86
ISPSINGL DS    0H                                              SS AUG86
         USING *,R4
         NI    HFLAGSDD,FF-X'02'     NO OPEN YET
ISPS020  OI    ##ADRPA#,$A+$D        IGNORE ATTENTION NOW
         ST    R1,SPFTEMPD           SAVE MSGID               ABL JAN87
         ST    R15,SPFTEMPD+4        SAVE PANEL ADDRESS       ABL JAN87
         BAS   R2,REFCHECK           CONDITIONAL REFRESH      ABL JAN87
         L     R1,SPFTEMPD           RESET MSGID              ABL JAN87
         L     R15,SPFTEMPD+4        RESET PANEL ADDRESS      ABL JAN87
         NI    HFLAGSDD,FF-X'01'     NO ATTRIBUTES
         LA    R14,ISPDISP           DISPLAY TABLE
ISPS030  MVC   SPFPANPR(8),0(R15)    SAVE LAST PANEL NAME
         LR    R0,R1                 SET MSG ID                SS AUG86
         LA    R1,VZCMD              SET TO CURSOR TO ZCMD     SS AUG86
         CLC   SPFCURNM,ISPBLK8      ANY FIELD NAME            SS APR89
         BNH   *+8                   NO, CONTINUE              SS APR89
         LA    R1,SPFCURNM           YES, POINT TO NAME        SS APR89
         STM   R14,R1,PARMLIST
         LA    R1,SPFCURSO           CURSOR OFFSET             SS JUL88
         ST    R1,PARMLIST+16                                  SS JUL88
         OI    PARMLIST+16,X'80'                               SS JUL88
         BAS   R14,ISPSXEQ           CALL ISPEXEC              SS JUL88
         NI    ##ADRPA#,FF-$Q-$A-$D         RESET QUIET MODE/ATTN
         MVC   SPFMSGID,ISPBLK8             CLEAR MSGID       ABL JAN87
         ST    R15,SPFFIXED          SAVE RC                  ABL JAN87
         C     R15,ISPL8                                       SS AUG86
         BH    ISPERR                                          SS AUG86
         BAS   R2,REFSAVE            SAVE  TPIN, TPOUT COUNTS ABL JAN87
         L     R15,SPFFIXED       RESET RC                    ABL JAN87
         MVI   SPFCURSO+3,0       RESET POSITION               SS JUL88
         MVC   SPFCURNM,ISPBLK8   RESET FIELD NAME             SS APR89
         BR    R3                                              SS AUG86
         DROP  R4                                             ABL APR86
*    CHECK OTHER PROCESSING BEFORE DISPLAY                     SS MAY88
ISPDISP6 DS    0H                                             ABL OCT84
         USING ISPDISP6,R4              *                      SS MAY88
         TM    SPFLAG5,SPFICMD    ISPF CMD NEED REQUESTED      SS JUL87
         BO    ISPICMDX           YES, EXECUTE  IT             SS JUL87
         TM    SPFLAG4,SPFCPOST   COMMAND POST PROCESSING      SS NOV84
         BO    ISPCPOST           YES                          SS NOV84
         TM    SPFLAG4,SPFMCMD    MULTY CMD                    SS NOV84
         BO    ISPMCMD            YES                          SS NOV84
         LA    R15,100            ROUTE TO PANEL DISPLAY       SS NOV85
         B     ISPROUT            ROUTE SUPPORT                SS NOV85
         SPACE 2                                               SS NOV84
ISPCPOST DS    0H                                              SS MAY88
         NI    SPFLAG4,255-SPFCPOST                            SS NOV84
         L     R14,SPFVTPT         GET MY TABLE ENTRY          SS NOV85
         SR    R15,R15                                         SS NOV84
         IC    R15,SPFPOST1         GET POST PROCESSOR INDEX   SS NOV85
         B     ISPROUT              INDEX TO PROCESSOR         SS NOV85
         SPACE 2                                               SS NOV84
ISPMCMD  DS    0H                                              SS NOV84
         NI    SPFLAG4,255-SPFMCMD  RESET FLAG                 SS NOV84
         MVI   SPFVZCMD,C' '                                   SS NOV84
         MVC   SPFVZCMD+1(249),SPFVZCMD                        SS NOV84
         L     R1,SPFSEC4A        BASE                         SS OCT88
         MVC   SPFVZCMD(250),SPFLSAVZ-SPFSEC4K(R1) OLD         SS OCT88
         BAS   R2,ISPSHCMD        EXTRACT NEXT COMMAND         SS NOV84
         MVC   SPFINPUT(250),SPFVZCMD                          SS OCT88
*        B     ISPOPTT                                         SS MAY88
         LA    R15,104            ROUTE TO ISPOPTT             SS MAY88
         B     ISPROUT            ROUTE SUPPORT                SS MAY88
         SPACE 2                                               SS NOV84
ISPICMDX DS    0H                                              SS JUL87
         MVC   PUTGETBF(4),ISPLEN2  SET MAX LENGTH             SS FEB88
         NI    SPFLAG5,255-SPFICMD  RESET FLAG                 SS JUL87
         L     R1,SPFSEC4A         GET  BASE                   SS MAY88
         LA    R1,SPFICMDS-SPFSEC4K(,R1)  POINT TO SPFICMDS    SS MAY88
         MVC   SPFVZCMD(254),0(R1)           GET THE CMD       SS JUL87
         MVC   SPFINPUT(252),SPFVZCMD                          SS JUL87
*        B     ISPOPTT                                         SS MAY88
         LA    R15,104            ROUTE TO ISPOPTT             SS MAY88
         BR    R3                 ROUTE SUPPORT                SS MAY88
         DROP  R4                                              SS JUL87
         TITLE  'ISPF - TABLE DISPLAY FUNCTION'                SS NOV85
*                                                              SS MAR85
*  TABLE DISPLAY FUNCTION                                      SS MAR85
*                                                              SS MAR85
ISPTBDSP DS    0H                                              SS SEP84
         USING *,R4                                            SS MAR85
         NI    SPFLAG4,255-SPFFFUN      CLEAR FROM FLAG        SS AUG86
         TM    SPFTBPF1,SPFTLIN   ACTIVE MEMLIST               SS NOV85
         BO    ISPMMLDS           YES, DISPLAY IT              SS SEP84
         TM    SPFTBPF2,SPFTFUNC  FUNCTION DISPLAY             SS OCT88
         BO    ISPTBDF0           YES                          SS OCT88
         BAS   R2,ISPQUERY                                     SS JUL87
         L     R15,SPFSTART       REPOSITION TO HERE           SS MAY85
         S     R15,SPFCRP         CALCULATE RELATIVE POSITION  SS NOV85
         BAS   R2,ISPSKIP                                      SS MAY85
         MVI   SPFLAG1,0          RESET ACTION FLAG           ABL OCT84
         B     ISPTBDP2                                        SS NOV85
*                                       *                      SS OCT88
*  PROCESS FUNCTION STATUS DISPLAY      *                      SS OCT88
*                                       *                      SS OCT88
ISPTBDF0 DS    0H                       *                      SS OCT88
         MVI   FLAGSSS,0          YES, CLEAR IT NOW            SS OCT88
*        MVC   SPFUSRXX,ISPPASST  SET PASSTHRU FOR RFIND ETC   SS OCT88
         CLI   SPFMSGID,C' '            ANY MESSAGE            SS OCT88
         BNH   ISPTBDF1                 NO                     SS OCT88
         LA    R1,SPFMSGID              SET MESSAGE PT         SS OCT88
         B     ISPTBDF2                 LEAVE ZCMD             SS OCT88
ISPTBDF1 DS    0H                       *                      SS OCT88
         MVI   SPFVZCMD,C' '            BLANK                  SS OCT88
         MVC   SPFVZCMD+1(249),SPFVZCMD      RIPPLE            SS OCT88
         LA    R1,ISPBLK8         NO MESSAGE                   SS OCT88
ISPTBDF2 DS    0H                       *                      SS OCT88
         LA    R15,SPFTBPNM             PANEL NAME             SS OCT88
         BAS   R2,ISPSINGI              DISPLAY THIS PANEL     SS OCT88
         ST    R15,SPFFIXED             SAVE RETURN CODE       SS OCT88
         CLI   FLAGSSS,0          RETURN KEY ACTIVE?           SS OCT88
         MVI   FLAGSSS,0                                       SS OCT88
         BNE   ISPTBDF0           YES, REPEAT THE DISPLAY      SS OCT88
         B     ISPTBDF5                 PROCESS REQUEST        SS OCT88
         SPACE 3                                               SS NOV84
ISPMMLDS DS    0H                                              SS SEP84
         TM    SPFTFLG1,SPFTLCD     LINE COMMAND ACTIVE        SS NOV85
         BO    ISPMMLLR           RETURN FROM LINE CMD         SS NOV85
         BAS   R2,ISPQUERY                                     SS JUL87
         BAS   R2,ISPOSTN                                      SS SEP84
ISPTBDP2 DS    0H                                              SS NOV85
         OI    SPFLAG3,SPFREFR        MAKE SURE OF REFRESH     SS AUG86
*  DISPLAY PROCESSING WAS REDESIGNED    *                      SS JUL87
*  ALL LINE COMMANDS ARE PREPROCESSED, SEE BELOW               SS JUL87
*  SCROLLING IS ALSO PREPROCESSED SO POSITIONING FLAGS NOT USEDSS JUL87
*  WILL NOW USE PANEL NAME ON ALL TABLE DISPLAYS               SS JUL87
ISPTBDNR DS    0H                                              SS NOV84
         OI    ##ADRPA#,$A+$D           IGNORE ATTENTION NOW
         BAS   R2,REFCHECK              CONDITIONAL REFRESH   ABL JAN87
         LA    R14,TBDISPL        DISPLAY TABLE                SS JUL84
         ST    R14,PARMLIST                                    SS JUL84
         LA    R14,SPFTBL         TABLE NAME                   SS JUL84
         LA    R15,SPFPANEL       PANEL NAME                   SS JUL84
         MVC   SPFPANPR(8),SPFPANEL  SAVE LAST PANEL NAME      SS JUL87
         LA    R0,ISPBLK8         NO MESSAGE                   SS NOV86
         CLC   SPFMSGID(4),ISPPDSM ANY MSG                     SS NOV84
         BNE   ISPTBDNM                                        SS NOV84
         LA    R0,SPFMSGID                                     SS NOV84
ISPTBDNM DS    0H                                              SS NOV84
         TM    SPFTFLG1,SPFTYOFF  OFFSET PROCESSING FOR FIND   SS OCT88
         BO    ISPTBDOF                 *                      SS OCT88
         TM    SPFTBPF1,SPFTLIN   MEMLIST TABLE?               SS NOV85
         BZ    ISPTBDNC           NO, NO CURSOR CRP            SS NOV85
         L     R1,SPFTCCRP        CURSOR AT A LINE             SS MAR85
         LTR   R1,R1                                           SS MAR85
         BZ    ISPTBDNC                                        SS MAR85
         L     R1,SPFTBPOA        SET CURSOR AT OPT FIELD      SS NOV85
         STM   R14,R1,PARMLIST+4                               SS MAR85
         LA    R14,SPFTCCRP       CRP                          SS NOV85
         ST    R14,PARMLIST+20                                 SS MAR85
         LA    R0,ISPL1           POSITION ONE                 SS NOV86
         ST    R0,PARMLIST+24     SET POSITION                 SS NOV86
         LA    R14,ISPNO          NO AUTOSEL                   SS MAR85
         ST    R14,PARMLIST+28                                 SS MAR85
         OI    PARMLIST+28,X'80'                               SS JUL84
         CLC   SPFMSGID(4),ISPPDSM  ERROR MESSAGE?            ABL JUN85
         BNE   ISPTBDNF             NO, BRANCH                ABL JUN85
         OI    PARMLIST+20,X'80'    YES, LET AUTOSEL DEFAULT  ABL JUN85
         B     ISPTBDNF                                        SS MAR85
ISPTBDOF DS    0H                                              SS OCT88
         NI    SPFTFLG1,255-SPFTYOFF  RESET FLAG               SS OCT88
         LA    R1,SPFCURSR        SET CURSOR AT GIVEN FIELD    SS OCT88
         STM   R14,R1,PARMLIST+4                               SS OCT88
         LA    R14,SPFTCCRP       CRP                          SS NOV88
         ST    R14,PARMLIST+20                                 SS OCT88
         LA    R0,SPFCUOFF        POSITION AT GIVEN OFFSET     SS OCT88
         ST    R0,PARMLIST+24     SET POSITION                 SS OCT88
         LA    R14,ISPNO          NO AUTOSEL                   SS OCT88
         ST    R14,PARMLIST+28                                 SS OCT88
         OI    PARMLIST+28,X'80'                               SS OCT88
         B     ISPTBDNF                                        SS OCT88
ISPTBDNC DS    0H                                              SS MAR85
         LA    R1,VZCMD           CURSOR AT CMD LINE           SS JUL84
         STM   R14,R1,PARMLIST+4                               SS JUL84
         OI    PARMLIST+16,X'80'                               SS MAR85
ISPTBDNF DS    0H                                              SS MAR85
         CLI   FLAGSSS,C'4'       RETURN FROM MEMLIST?        ABL OCT84
         BL    ISPREAP2           NO, BRANCH                  ABL OCT84
         TM    SPFTBPF1,SPFTLIN   MEMLIST                      SS NOV85
         BZ    ISPREAP2           NO, BRANCH                   SS NOV85
         MVI   FLAGSSS,0          YES, CLEAR IT NOW           ABL OCT84
         SPACE 1                                              ABL SEP84
ISPREAP2 DS    0H                                             ABL SEP84
         MVC   SPFUSRXX,ISPPASST  SET PASSTHRU FOR RFIND ETC   SS AUG86
         LA    R1,PARMLIST                                     SS JUL84
         L     R15,ISPLINK                                     SS JUL84
         BASR  R14,R15                                         SS JUL84
         SPACE 3
***
***
***  TBDISPL ARGUMENT LIST:
***
***    1.  NO LINE COMMANDS:
***        CALL ISPLINK('TBDISPL',SPFTBL,SPFPANEL,MSGID,'ZCMD')
***
***
***    2.  LINE COMMANDS WITH ISPFV1 (OR ISPFV2 AND AN ERROR MESSAGE):
***        CALL ISPLINK('TBDISPL',SPFTBL,SPFPANEL,MSGID,'ISPOPT',LINE)
***
***
***    3.  LINE COMMANDS WITH ISPFV2 AND NO ERROR MESSAGE:
***        CALL ISPLINK('TBDISPL',SPFTBL,SPFPANEL,MSGID,'ISPOPT',LINE,
***                     0,'NO ')   -- AUTOSELECT=NO
***
         SPACE 3
         CLI   FLAGSSS,0          RETURN KEY ACTIVE?          ABL SEP84
         MVI   FLAGSSS,0                                      ABL SEP84
         BNE   ISPREAP2           YES, REPEAT THE TBDISPL     ABL SEP84
ISPTBDF5 DS    0H                       *                      SS OCT88
         MVC   SPFUSRXX,ISPBLK8   SET OFF PASSTHRU             SS AUG86
         MVI   SPFMSGID,C' '      TURN OFF MSG                 SS NOV84
         ST    R15,SPFFIXED       SAVE RC                      SS JUL84
         BAS   R2,REFSAVE             SAVE TPIN, TPOUT COUNTS ABL JAN87
         BAS   R2,ISPDVGET            ALWAYS GET ZVERB         SS OCT88
         CLI   SPFFIXED+3,8           RETURN CODE=8?          ABL OCT84
         BNE   ISPSET8                NO, BRANCH              ABL OCT84
         SPACE 1                                              ABL OCT84
         CLI   SPFRETRN,C'R'         "RETURN"                 ABL OCT84
         BE    ISPSET4               YES, BRANCH              ABL DEC84
*        CLI   SPFRETRN,C'E'         "END"?                   ABL MAR86
*        BE    ISPSET0               YES, BRANCH              ABL MAR86
*** UNKNOWN SPF KEYWORD, IGNORE                               ABL MAR86
*        MVI   SPFFIXED+3,0          RESET RETURN CODE        ABL MAR86
*        B     ISPSET8               IGNORE                   ABL MAR86
*SPSET0  DS    0H                                             ABL MAR86
         MVC   SPFVZCMD(2),ISP8      SET TO 'END' CODE        ABL DEC84
         MVI   SPFFIXED+3,0          RESET RETURN CODE        ABL DEC84
         TM    SPFTBPF1,SPFTPRI      PANEL 1 (PRIMARY)?       ABL DEC84
         BZ    ISPSET8               NO, BRANCH               ABL DEC84
         CLI   PDSENTRY+1,0          ENTERED AS A DIALOG?     ABL DEC84
         BE    ISPSET8               YES, BRANCH              ABL DEC84
         MVC   SPFVZCMD(2),ISP1      END ON PANEL 1           ABL DEC84
         B     ISPSET8                                        ABL DEC84
ISPSET4  DS    0H                                             ABL DEC84
         MVC   SPFVZCMD(2),ISPR      SET TO 'RETURN' CODE     ABL DEC84
         TM    SPFTBPF1,SPFTPRI      PANEL 1 (PRIMARY)?       ABL DEC84
         BO    ISPSET8               YES, NO PROBLEM          ABL DEC84
         CLI   PDSENTRY+1,0          ENTRY FROM SPF PANEL?    ABL DEC84
         BE    ISPSET8               YES, NO PROBLEM          ABL DEC84
         MVI   SPFFIXED+3,0          RESET RETURN CODE        ABL DEC84
         AIF ('&RETURNX' EQ 'NO').RETURN3                     ABL DEC84
         MVI   FLAGSSS,C'2'          REINVOKE ALL LATER       ABL OCT84
         TM    SPFTBPF1,SPFTLIN   MEMLIST                      SS NOV85
         BZ    ISPSET8            NO, BRANCH                   SS NOV85
         MVI   FLAGSSS,C'4'          REINVOKE EXCEPT MEMLIST  ABL OCT84
.RETURN3 ANOP                                                 ABL DEC84
ISPSET8  DS    0H                                             ABL OCT84
         NI    SPFLAG3,FF-SPFLOGM     ASSUME NO LOGGING        SS NOV84
         CLI   SPFFIXED+3,8           VALID RETURN CODE?      ABL DEC84
         BH    ISPERR                 NO, ERROR               ABL DEC84
* ALL VALIDATION DONE BY TABLES AT ISPOPTT                     SS NOV84
         SPACE 2                                               SS NOV84
         NI    ##ADRPA#,FF-$A-$D        DO NOT IGNORE ATTENTION NOW
         SPACE 1
         ESTAE 0                        CANCEL THE ESTAE ADDRESS
ESTAE9   DS    0H                       Z/XDC PROBE           DRK DEC08
         MVC   STAEPARM(4),STAELIST     RESET THE ESTAE ADDRESS
         L     R2,ISPESTAE              ESTAE EXIT ADDRESS    ABL JUN85
         ESTAE (R2),CT,PARAM=(R7),MF=(E,STAEPARM)             ABL JUN85
ESTAE10  DS    0H                       Z/XDC PROBE           DRK DEC08
         TM    SPFTBPF1,SPFTLIN       MEMLIST                  SS SEP89
         BZ    ISPTBLG3                                        SS SEP89
         L     R15,SPFTDTOP           GET TOP OF SCREEN        SS SEP89
         ST    R15,SPFTTOP            NEXT DISPLAY SAME PLACE  SS SEP89
         ST    R15,SPFTTOP2           NEXT DISPLAY SAME PLACE  SS SEP89
         B     ISPTBLG5                                        SS SEP89
ISPTBLG3 DS    0H                                              SS SEP89
         MVC   SPFSTART,SPFTDTOP  SAVE INFO                    SS SEP89
ISPTBLG5 DS    0H                                              SS SEP89
         CLC   SPFVZCMD(2),ISP1       END AT PANEL 1?          SS SEP89
         BE    ISPTBLGD               YES, PROCESS             SS SEP89
         CLC   SPFVZCMD(2),ISP8       END SUBCOMMAND?          SS SEP89
         BE    ISPTBLGD               YES, PROCESS             SS SEP89
         CLC   SPFVZCMD(2),ISPR       RETURN COMMAND?          SS SEP89
         BE    ISPTBLGD               YES, PROCESS             SS SEP89
         TM    SPFTBPF1,SPFTLIN       MEMLIST                  SS SEP89
         BZ    ISPTBLNF                                        SS SEP89
         CLI   SPFFIXED+3,4           MORE THAN ONE LINE CMD?  SS SEP89
         BNE   ISPTBLB2                                        SS SEP89
         OI    SPFTFLG1,SPFTMUT       YES, SET BIT             SS SEP89
ISPTBLB2 DS    0H                                              SS SEP89
         BAS   R2,ISPQUERY                                     SS SEP89
         L     R15,SPFTCRP                                     SS SEP89
         LTR   R15,R15                ANY LINE COMMANDS?       SS SEP89
         BNZ   ISPLNCMD                                        SS SEP89
         ST    R15,SPFTCCRP           SET ZERO CURSOR          SS SEP89
ISPTBLNF DS    0H                                              SS AUG89
         LA    R0,80              CHARACTERS TO SCAN          ABL JUN85
         LA    R1,SPFVZCMD+79     WHERE TO START SCAN          SS FEB88
ISPTBLB  CLI   0(R1),X'40'        BLANK?                      ABL JUN85
         BNE   ISPTBLNB           NO, BRANCH                  ABL JUN85
         BCTR  R1,0               ONE LESS CHARACTER          ABL JUN85
         BCT   R0,ISPTBLB         CHECK ALL CHARACTERS?       ABL JUN85
         MVC   SPFVZCMD(2),ISPN   NULL COMMAND                 SS FEB88
         B     ISPTBLGD           YES, ALL BLANK               SS NOV85
ISPTBLNB CLI   0(R1),C'+'         CONTINUATION DESIRED?       ABL FEB87
         BE    ISPTBLNC           YES, BRANCH                 ABL FEB87
         CLI   0(R1),C'-'         CONTINUATION DESIRED?       ABL FEB87
         BNE   ISPTBLGD           NO, PROCESS AS IS           ABL FEB87
ISPTBLNC DS    0H                                             ABL FEB87
         LA    R0,SPFVZCMD-1      OFFSET BY -1                ABL JUN85
         SR    R1,R0                                          ABL JUN85
         ST    R1,SPFCURSO        SAVE THE CURSOR POSITION    ABL JUN85
         LA    R15,ISP$PANC         CONTINUED PANEL            SS JUL88
         LA    R1,ISPBLK8           NO MESSAGE                 SS JUL88
         BAS   R2,ISPSINGI          DISPLAY PANEL              SS JUL88
         C     R15,ISPL8            END KEY                    SS JUL88
         BNE   ISPTBLGD                                        SS NOV85
         MVI   SPFVZCMD,X'40'                                 ABL JUN85
         MVC   SPFVZCMD+1(249),SPFVZCMD  RIPPLE               ABL JUN85
         MVC   SPFVZCMD(2),ISPN   NULL COMMAND                 SS JUL88
         BAS   R2,ISPDVGET        MAY BE A RETURN              SS JUL88
         CLI   SPFRETRN,C'R'      IS IT                        SS JUL88
         BNE   ISPTBLGD           NO    *                      SS JUL88
         MVC   SPFVZCMD(2),ISPR   SET FOR RETURN               SS JUL88
         B     ISPTBLGD           PROCESS IT                   SS JUL88
ISPTBLND DS    0H                       *                      SS JUL88
         CLC   SPFVZCMD(8),ISPBLK8    USER RETURN BLANKS       SS JUL88
         BNE   ISPTBLGD               NO                       SS JUL88
         MVC   SPFVZCMD(2),ISPN   NULL COMMAND                 SS JUL88
ISPTBLGD DS    0H                                              SS SEP89
ISPMMLD8 DS    0H                                              SS SEP89
         CLC   SPFVZCMD(2),ISPN   NULL COMMAND                 SS FEB88
         BE    ISPTBDSP           IGNORE AND START OVER        SS FEB88
         LA    R1,PUTGETBF                                    ABL OCT84
         ST    R1,SPFSAV1                                     ABL OCT84
         BAS   R2,ISPQUERY                                     SS JUL84
         MVC   PUTGETBF(4),ISPLEN     AL2(52,0)               ABL OCT84
         MVC   SPFINPUT(48),SPFVZCMD                           SS NOV84
         MVI   SPFINPUT+48,C' '        BLANK REST              SS AUG89
         MVC   SPFINPUT+49(201),SPFINPUT+48                    SS AUG89
         CLC   SPFVZCMD+48(8),ISPBLK8  ANY ADDITIONAL OPERS?  ABL JUN85
         BE    ISPTBLG2                NO, BRANCH             ABL JUN85
         MVC   PUTGETBF(4),ISPLEN2     AL2(254,0)             ABL JUN85
         MVC   SPFINPUT(250),SPFVZCMD  CURRENT SUBCOMMAND     ABL JUN85
ISPTBLG2 LA    R15,104                 PROCESS OPTION NEXT     SS NOV85
         BR    R3                      RETURN TO ROUTER       ABL JUN85
         SPACE 2                                              ABL JUN85
ISPLNCMD DS    0H                                              SS SEP84
         CLC   SPFVZCMD(8),ISPBLK8 NULL COMMAND                SS AUG89
         BNE   ISPLNCM0             NOT NULL                   SS AUG89
         MVC   SPFVZCMD(2),ISPN     SET TO NULL                SS AUG89
         B     ISPLNCM2             NO, SKIP SAVE              SS AUG89
ISPLNCM0 DS    0H                                              SS AUG89
         LH    R15,SPFTBPZC         WHERE TO SAVE ZCMD         SS NOV85
         LA    R15,SPWRKC(R15)      CALC ADDR                  SS NOV85
         MVC   0(48,R15),SPFVZCMD   SAVE ZCMD                  SS NOV85
         OI    SPFTFLG1,SPFTZSV     INDICATE HAVE ZCMD         SS SEP84
ISPLNCM2 DS    0H                                              SS NOV85
         SR    R15,R15              ZERO VALUE                 SS MAR85
         ST    R15,SPFTCCRP         INIT TO ZERO               SS MAR85
         NI    SPFTFLG1,255-SPFTLPOS RESET FLAG                SS NOV85
*  LINE COMMAND PROCESSING REDESIGN                            SS JUL87
*  ALL LINE COMMANDS ARE PREPROCESSED AND STORED IN TABLE      SS JUL87
*  LEVEL 2 SUB ISPNXLN IS USED TO GET NEXT LINE COMMAND        SS JUL87
*    TO PROCESS, BLANK LINE COMMANDS ARE STORED AS HEX 60      SS JUL87
*  SAVE AND RESTORE ARE NOT USED        *                      SS JUL87
*  AFTER ALL LINES COMMANDS ARE PREPROCESSED ONE MORE          SS JUL87
*     DISPLAY WITH CNTL NONDISP IS USED TO GET SCROLL REQUESTS SS JUL87
*  A KILL REQUEST WILL LEAVE UNPROCESSED COMMANDS IN THE       SS JUL87
*    TABLE AND WILL BE REPROCESSED IF NOT REMOVED BY USER      SS JUL87
*    INDIVIDUALLY OR USING NEW LINE COMMAND .. KILL ..         SS JUL87
*                                       *                      SS JUL87
ISPLNCM3 DS    0H                       *                      SS JUL87
         LH    R1,SPFTBPOT          COMMAND OFFSET             SS JUL87
         LA    R1,SPWRKC(R1)                ADDRESS            SS JUL87
         CLC   0(4,R1),ISPBLK8      BLANK                      SS JUL87
         BH    *+8                      *                      SS JUL87
         MVI   0(R1),ISPBLKC        BLANK OUT LINE COMMAND     SS JUL87
         TM    SPFTBPF2,SPFTTCT     TCT LINE                   SS NOV88
         BZ    ISPLNCM7                 *                      SS NOV88
         L     R1,SPFSEC4A          GET SECOND BASE            SS NOV88
         L     R2,SPFTTSIZ          GET SIZE                   SS NOV88
         BCTR  R2,0                 ONE LESS FOR MVC           SS NOV88
         MVC   SPFZSEL(4),SPFTOPT   SAVE OPT                   SS NOV88
         MVC   SPFZSEL+4(1),SPFTTYPE     AND TYPE              SS NOV88
         MVC   SPFZSEL+5(0),SPFTCMDD-SPFSEC4K(R1)              SS NOV88
         EX    R2,*-6               SAVE ENTERED DATA          SS NOV88
         BAS   R2,ISPGET            GET ORIGINAL DATA          SS NOV88
         L     R1,SPFSEC4A          GET SECOND BASE            SS NOV88
         L     R2,SPFTTSIZ          GET SIZE                   SS NOV88
         BCTR  R2,0                 ONE LESS FOR MVC           SS NOV88
         MVC   SPFTOPT,SPFZSEL      RESTORE OPT                SS NOV88
         MVC   SPFTTYPE,SPFZSEL+4     AND TYPE                 SS NOV88
         MVC   SPFTCMDD-SPFSEC4K(0,R1),SPFZSEL+5               SS NOV88
         EX    R2,*-6               SAVE ENTERED DATA          SS NOV88
ISPLNCM7 DS    0H                       *                      SS NOV88
         BAS   R2,ISPTBPUT              *                      SS JUL87
         TM    SPFTFLG1,SPFTMUT MORE LINES TO PROCESS          SS JUL87
         BNO   ISPLNCM4         DONE WITH PREPROCESS           SS JUL87
         LA    R14,TBDISPL      DISPLAY TABLE                  SS JUL87
         LA    R15,SPFTBL                                      SS JUL87
         STM   R14,R15,PARMLIST  WITH NO                       SS JUL87
         OI    PARMLIST+4,X'80'         TABLE NAME OR MSG      SS JUL87
         BAS   R14,ISPSXEQ                                     SS JUL87
         C     R15,ISPL8        FORCED END                     SS SEP84
         BE    ISPMML8          YES                            SS SEP84
         BH    ISPERR                                          SS SEP84
         C     R15,ISPL4           STILL MORE LINES AFTER THIS SS SEP84
         BE    ISPLNCM3            CONTINUE                    SS SEP84
         NI    SPFTFLG1,255-SPFTMUT NO MORE AFTER THIS         SS NOV85
         B     ISPLNCM3            CONTINUE                    SS SEP84
ISPLNCM4 DS    0H                       *                      SS JUL87
         MVC   SPFTTOP2,SPFTTOP     SAVE TOP BEFORE SCROLLING  SS JUL87
         BAS   R2,ISPCTNDE              *                      SS JUL87
         LA    R14,TBDISPL      DISPLAY TABLE                  SS JUL87
         LA    R15,SPFTBL                                      SS JUL87
         STM   R14,R15,PARMLIST  WITH NO                       SS JUL87
         OI    PARMLIST+4,X'80'         TABLE NAME OR MSG      SS JUL87
         BAS   R14,ISPSXEQ                                     SS JUL87
         L     R15,SPFTDTOP             *                      SS JUL87
         ST    R15,SPFTTOP              SAVE SCROLL POSITION   SS JUL87
         BAS   R2,ISPTOP                *                      SS JUL87
         BAS   R2,ISPNXLN               *                      SS JUL87
         LTR   R15,R15                  *                      SS JUL87
         BNZ   ISPLNCKY                 *                      SS JUL87
ISPLNCM5 DS    0H                       *                      SS JUL87
         OI    SPFTFLG1,SPFTLCD                                SS JUL87
         LH    R15,SPFTBPLF         PROCESS LINE CMD           SS NOV85
         BR    R3                                              SS NOV85
* PROCESS RETURN FROM LINE COMMAND                             SS NOV85
ISPMMLLR DS    0H                                              SS NOV85
         NI    SPFTFLG1,255-SPFTLCD  RESET                     SS AUG85
         TM    SPFTFLG2,SPFLKILL TERMINATE ANY PROCESSING      SS AUG86
         BO    ISPLNCKY                 *                      SS AUG86
         BAS   R2,ISPNXLN               *                      SS JUL87
         LTR   R15,R15          FOUND NEXT LINE                SS JUL87
         BNZ   ISPLNCKZ         NO, CHECK FOR ZCMD             SS JUL87
         B     ISPLNCM5            PROCESS THE LAST            SS JUL87
         SPACE 2
ISPMML8  DS    0H                                              SS NOV85
         MVC   SPFVZCMD(2),ISP8                                SS NOV85
         B     ISPTBLGD                                        SS NOV85
         SPACE 2
ISPLNCKY DS    0H                                              SS AUG86
         NI    SPFTFLG2,255-SPFLKILL    RESET FLAG             SS AUG86
* TERMINATE LINE COMMANDS AND ZCMD IF ANY                      SS AUG86
         NI    SPFTFLG1,255-SPFTZSV-SPFTMUT                    SS AUG86
         L     R15,SPFTTOP2         GET NON SCROLL POSITION    SS JUL87
         ST    R15,SPFTTOP          RESET                      SS JUL87
         B     ISPMMLDS             DISPLAY TABLE              SS AUG86
         SPACE 2                                               SS AUG86
ISPLNCKZ DS    0H                                              SS JUL87
         BAS   R2,ISPSETST          SET AND RETRIEVE STATUS    SS MAR87
         TM    SPFTFLG1,SPFTZSV     ZCMD TO PROCESS?           SS NOV85
         BNO   ISPMMLDS             NO, BRANCH                ABL JUN85
         LH    R15,SPFTBPZC         WHERE IS SAVED ZCMD        SS NOV85
         LA    R15,SPWRKC(R15)      CALC ADDR                  SS NOV85
         MVC   SPFVZCMD(48),0(R15)  RESTORE ZCMD               SS NOV85
         MVI   SPFVZCMD+48,C' '     BLANK REST                 SS AUG89
         MVC   SPFVZCMD+49(201),SPFVZCMD+48                    SS AUG89
         NI    SPFTFLG1,255-SPFTZSV RESET FLAG                 SS NOV85
         B     ISPTBLNF             PROCESS GOOD CMD           SS AUG89
         DROP  R4                                              SS MAR85
