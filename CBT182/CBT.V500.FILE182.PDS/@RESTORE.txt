         TITLE 'P D S  --  PDS RESTORE                       09/28/88'
***********************************************************************
***      RESTORE SUBCOMMAND MODIFIED BY BRUCE LELAND -- JUNE, 1982  ***
***********************************************************************
*
         SPACE 1
RESTORE  CSECT
         USING *,R8
         TM    DS1SMSFG,DS1PDSE         PDSE DATASET?         DRK AUG05
         BNO   REST005                  NO, CONTINUE          DRK AUG05
         M$MSG L706                     DATA SET IS A PDSE    DRK AUG05
         M$ERRST MSGBLANK               ONE BLANK LINE        DRK AUG05
         B     NEWCMD                                         DRK AUG05
         SPACE 1                                              DRK AUG05
REST005  DS    0H                                             DRK AUG05
         ICM   R1,B'1111',MEMBER2       TTR TO BE USED
         CLI   LMEMBER2+1,1             COMPARE LENGTH:1
         BH    REST010                    HIGH (DO NOT ADJUST)
         SRL   R1,8
         BE    REST010                    EQUAL (ADJUSTED ONE TIME)
         SRL   R1,8                       LOW (ADJUSTED TWICE)
         SPACE 1
REST010  STCM  R1,B'1111',MEMBER2       SAVE THE UPDATED ADDRESS
         STCM  R1,B'1111',DIRTTR        SAVE THE UPDATED ADDRESS
         SPACE 1
*** REOPEN THE DATA SET TO ENSURE THAT DS1LSTAR IS CORRECT
         OI    FLAGSJJ,FNOREAD         CALL EXCP FOR OPEN ONLY
         MVC   STARTTR(3),DS1LSTAR     FIRST DISK ADDRESS TO READ
         L     R15,=V(EXCP)
         BASR  R14,R15
         NI    FLAGSJJ,FF-FNOREAD      EXCP OPEN IS COMPLETE
         SPACE 1
         TM    FLAGSCC,RECFMU           LOAD MODULE?
         BNO   REST020                  NO, BRANCH
         MVI   #RESTDIR,X'20'+12        1 TEXT TTR, 12 HALFWORDS
         MVI   #RESTDIR+9,ATTREXEC      EXECUTABLE
         MVI   #RESTDIR+10,ATTRNODC+ATTRZORG+ATTREP0+ATTRFLEV
*                                       NOT DC, ZERO ORG, EP=0, F-LEVEL
         MVI   #RESTDIR+19,DIRAOSLE+DIRAPFLG  VS EDITOR, APF DATA IS OK
         SPACE 1
REST020  CLI   #RESTLIK,0               ANY LIKE OPERAND?
         BE    REST080                  NO, BRANCH
         XC    DIRNAME(74),DIRNAME
         MVC   DIRNAME,#RESTLIK         LIKE NAME
         BLDL  INDCB,BLDLLIST           GET MEMBER DIRECTORY
         SPACE 1
         B     *+4(R15)                 PROCESS RETURN CODE
         B     REST030                    00 - FOUND
         B     NOMEMBER                   04 - NOT FOUND
         B     IOERROR                    08 - I/O ERROR IN DIRECTORY
         SPACE 1
REST030  MVC   DIRFLAG(DIREND-DIRFLAG),DIRFLAG+2  BLDL ADDS THINGS
         MVC   #RESTDIR,DIRFLAG         SAVE USER DATA
         NI    #RESTDIR,FF-X'80'        TURN OFF ANY ALIAS BIT
         TM    FLAGSCC,RECFMU           LOAD MODULE?
         BNO   REST080                  NO, BRANCH
         XC    #RESTDIR,#RESTDIR        CLEAR THE USER DATA
         LA    R2,DIRAPF                POINT TO APF DATA
         TM    DIRATTR,ATTRSCTR         SCATTER LOAD?
         BNO   *+8                      NO, BRANCH
         LA    R2,8(,R2)                YES, POSITION OVER FIELDS
         TM    DIRFLAG,X'80'            ALIAS?
         BO    REST033                  YES, BRANCH
***PDSMAN CHANGES: RESTORE LIKE (APF AND SSI)                 ABL NOV86
         TM    5(R2),X'0F'              PDSMAN DATE?          ABL NOV86
         BO    REST034                  YES, BRANCH           ABL NOV86
         TM    6(R2),X'0F'              PDSMAN DATE?          ABL NOV86
         BO    REST034                  YES, BRANCH           ABL NOV86
         TM    4+5(R2),X'0F'            PDSMAN DATE AND SSI?  ABL NOV86
         BO    REST034                  YES, BRANCH           ABL NOV86
         TM    4+6(R2),X'0F'            PDSMAN DATE AND SSI?  ABL NOV86
         BO    REST034                  YES, BRANCH           ABL NOV86
         CLI   8(R2),0                  CONVERTED ALIAS ENTRY?
*        BE    REST034                  NO, BRANCH
         B     REST034                  BRANCH                DRK JAN98
REST033  LA    R2,11(,R2)               ADD ALIAS LENGTH      ABL NOV86
REST034  DS    0H                                             ABL NOV86
         TM    DIRATTR2,DIRAOSLE        O/S MVS LINKAGE EDITOR?
         BNO   REST040                  NO, BRANCH
         TM    DIRATTR2,DIR2SSI         ANY SSI FIELD?
         BNO   REST050                  NO, BRANCH
REST040  LA    R1,1(,R2)                ADD FOR ROUNDING TO HALFWORD
         N     R1,=F'-2'                ROUND TO HALFWORD
         CLC   ZERO,0(R1)               ZERO?
         BE    REST050                  YES, BRANCH
         CLC   =F'-1',0(R1)             HIGH VALUES?
         BE    REST050                  YES, BRANCH
         MVC   #RESTSSI,0(R1)           SAVE THE SSI VALUE
         LA    R2,4(,R1)                POSITION AFTER SSI
         SPACE 1
REST050  TM    DIRATTR2,DIRAOSLE+DIRAPFLG  VS LKED AND APF DATA GOOD?
         BO    REST060                     YES, BRANCH
         XC    DIRATTR2(3),DIRATTR2        NO, CLEAR NEW FLAGS
         B     REST070
         SPACE 1
REST060  CLI   0(R2),X'01'              VALID APF LENGTH?
         BNE   REST070                  NO, BRANCH
         MVC   #RESTAPF,1(R2)           YES, SAVE APF DATA
         SPACE 1
REST070  MVI   #RESTDIR,X'20'+12        1 TEXT TTR, 12 HALFWORDS
         NI    DIRATTR,X'CA'            RENT, REUS, ONLY LOAD, EXEC.
         NI    DIRATTR+1,X'09'          NOT EDIT, REFR
         MVC   #RESTDIR+9(2),DIRATTR    COPY INTO THE HOLD AREA
         OI    #RESTDIR+10,ATTRNODC+ATTRZORG+ATTREP0+ATTRFLEV
*                                       NOT DC, ZERO ORG, EP=0, F-LEVEL
         NI    DIRATTR2,DIR2PAGA        PAGE BOUNDARY FLAG
         NI    DIRATTR2+1,X'1F'         RMODE, AMODE FLAGS
         MVC   #RESTDIR+19(2),DIRATTR2  COPY INTO THE HOLD AREA
         OI    #RESTDIR+19,DIRAOSLE+DIRAPFLG  VS EDITOR, APF DATA IS OK
         SPACE 1
REST080  MVC   DIRNAME,MEMBER1          MEMBER NAME TO BE RESTORED
         MVC   DIRTTR(3),MEMBER2        TTR OF MEMBER TO BE RESTORED
         SPACE 1
         LA    R1,L873
         CLC   DIRTTR(3),DS1LSTAR       IN DATA SET BOUNDS?
         BNL   MSGNEW                   NO, ERROR
         SPACE 1
         CLI   #RESTTST,1               TEST (SIMULATION)?    DRK MAY06
         BE    REST090                  YES, SKIP NEXT TM     DRK MAY06
*  THESE 2 LINES CAUSED A BAD RESTORE IF NODISPLAY, NOOREPEAT DRK NOV09
*        TM    #RESFLAG,#RESFREP+#RESFDIS  REPEAT, DISPLAY OR PROMPT? "
*        BZ    REST803                     NO, BRANCH         DRK NOV09
REST090  DS    0H                                             DRK MAY06
         SPACE 1
         OI    FLAGSII,FNOTDOUB         FORCE SINGLE OR MULTIPLE BUFFER
         ICM   R1,B'1110',DIRTTR        T T R X  ZERO?
         BZ    REST210                  YES, BRANCH
         SRL   R1,8                     0 T T R
         S     R1,=F'1'                 0 T T R - 1 = 0?
         BZ    REST210                  YES, BRANCH
         SLL   R1,8                     T T R 0 (WHERE R IS REDUCED)
         STCM  R1,B'1110',DIRTTR        T T R
         CLI   MEMBER2+2,X'01'          R OF TTR 1 OR LESS?
         BH    REST210                  YES, BRANCH
         SRL   R1,16                    0 0 T T
         BCTR  R1,0                     0 0 T T - 1
         SLL   R1,16                    T T 0 0 (WHERE TT IS REDUCED)
         STCM  R1,B'1100',DIRTTR        T T  UPDATED
         MVI   DIRTTR+2,X'01'           RESET R TO 1 OF PREVIOUS TRACK
         SPACE 1
REST210  TM    #RESFLAG,#RESFREP        REPEAT?
         BZ    REST220                  NO, BRANCH
         SPACE 2
         LA    R1,7                     MACHINE LENGTH
         LA    R2,DIRNAME+7             LAST CHARACTER
         SPACE 1
         CLI   0(R2),X'40'              BLANK?
         BNE   *+10                     NO, BRANCH
         BCTR  R2,0                     PREVIOUS CHARACTER
         BCT   R1,*-10                  TRY ALL BYTES
         SPACE 1
         ST    R1,#RESTLEN                          NAME LENGTH
         SPACE 3
REST220  MVI   SUBPOOLT,21              MARK TEMPORARY STORAGE IN USE
         LA    R5,1024                  1024 MEMBERS INITIALLY
         SPACE 1
REST240  LR    R0,R5
         SLL   R0,2                     MEMBERS*4 FOR TABLE SIZE
         ICM   R0,B'1000',SUBPOOLT
         GETMAIN R,LV=(0)
         ST    R1,#MEMPTR               START OF TTR LIST
         LR    R4,R1
         XC    0(4,R4),0(R4)            DUMMY FIRST ELEMENT
         LR    R3,R5                    NUMBER OF ELEMENTS
         MVI   STARTTR+2,X'01'          TTR=000001 (START OF DIRECTORY)
         SPACE 2
REST242  BAS   R14,READDIR              GET THE NEXT MEMBER
         B     REST246                  END OF DIRECTORY, BRANCH
         SPACE 1
         CLC   DIRTTR(3),MEMTTR         TTR START<TTR OF THIS MEMBER?
         BNL   REST242                  NO, IGNORE
         S     R3,=F'1'                 WILL THIS FIT?
         BNP   REST244                  NO, BRANCH
         LA    R4,4(,R4)                NEXT TTR TABLE POSITION
         MVC   0(3,R4),MEMTTR           SAVE THE MEMBER START TTR
         MVI   3(R4),0                  CLEAR THE END FLAG
         B     REST242                  GET THE NEXT ENTRY
         SPACE 1
REST244  SR    R0,R0
         ICM   R0,B'1000',SUBPOOLT      SUBPOOL TO FREE
         FREEMAIN R,SP=(0)
         SLL   R5,1                     DOUBLE THE NUMBER OF ENTRIES
         B     REST240
         SPACE 2
REST246  MVI   3(R4),X'FF'              MARK THE END OF THE TTR LIST
         L     R8,=A(CLEAR)             ENTRY POINT TO CALL
         BASR  R2,R8                    INVOKE CLEAR
         L     R8,=A(RESTORE)           RESET BASE REGISTER
         SPACE 2
REST247  TM    #RESFLAG,#RESFREP        REPEAT RESTORE?
         BNO   REST248                  NO, BRANCH
         L     R1,#RESTSEQ              THIS MEMBER NUMBER
         LA    R1,1(,R1)
         ST    R1,#RESTSEQ              NEW MEMBER NUMBER
         CVD   R1,DOUBLE
         OI    DOUBLE+7,X'0F'           CORRECT SIGN
         L     R1,#RESTLEN              MACHINE LENGTH
         UNPK  DIRNAME(8),DOUBLE(8)     CONVERT THE SEQUENTIAL NAME
         MVC   DIRNAME(*-*),MEMBER1     <<EXECUTED>>
         EX    R1,*-6                   ADD THE CODED NAME PORTION
         B     REST260                  SKIP THIS CLEAR
         SPACE 1
REST248  L     R8,=A(CLEAR)             ENTRY POINT TO CALL
         BASR  R2,R8                    INVOKE CLEAR
         L     R8,=A(RESTORE)           RESET BASE REGISTER
         SPACE 2
REST260  M$ERRST MSGBLANK               ONE BLANK LINE
         MVC   STARTTR(3),DIRTTR        FIRST DISK ADDRESS TO READ
         MVI   #RESTERR+1,3             ALLOW 3 PERMANENT ERRORS/MEMBER
         SPACE 2
REST262  L     R15,=V(EXCP)
         BASR  R14,R15
         MVC   INSERT#1(8),BLANKS                             DRK JUL16
         LA    R1,L006$1                ASSUME END OF DATA SET
         B     *+4(R15)                 PROCESS RETURN CODE
         B     REST262                    00 - SUCCESSFUL READ
         B     REST264                    04 - END OF MEMBER
         B     MSGNEW                     08 - END OF DATA SET
         B     REST263                    12 - I/O ERROR
         SPACE 1
*  PERMANENT I/O ERROR
*    A.  IF TOO MANY ERRORS FOR THIS MEMBER, TERMINATE
*    B.  SIMULATE AN END OF MEMBER CONDITION
*    C.  CONTINUE ON THE NEXT TRACK
REST263  LA    R1,L804                  ASSUME TOO MANY ERRORS
         LH    R0,#RESTERR
         S     R0,=F'1'                 CORRECT?
         BM    MSGNEW                   YES, BRANCH
         STH   R0,#RESTERR
         M$MSG L801
         STM   R2,R12,28(R13)
         LA    R2,CURMBB
         L     R1,INDCB+(DCBDEBAD-IHADCB)
         L     R15,ADDRRLTV
         LR    R3,R13
         BASR  R14,R15
         LR    R13,R3
         LM    R2,R12,28(R13)
         SRL   R0,16                    TT OF LAST INPUT TTRN
         A     R0,=F'1'
         STH   R0,STARTTR               TT OF NEXT TRACK
         MVI   STARTTR+2,X'01'          R OF TTR
         SPACE 2
*  END OF MEMBER, GET THE NEXT DATA BLOCK
REST264  L     R15,=V(EXCP)
         BASR  R14,R15
         MVC   INSERT#1(8),BLANKS                             DRK JUL16
         LA    R1,L006$1                ASSUME END OF DATA SET
         B     *+4(R15)                 PROCESS RETURN CODE
         B     REST266                    00 - SUCCESSFUL READ
         B     REST264                    04 - END OF MEMBER
         B     MSGNEW                     08 - END OF DATA SET
         B     REST263                    12 - I/O ERROR
         SPACE 1
REST266  STM   R2,R12,28(R13)           CONVERT MBBCCHHR TO TTR
         LA    R2,CURMBB
         L     R1,INDCB+(DCBDEBAD-IHADCB)
         L     R15,ADDRRLTV
         LR    R3,R13
         BASR  R14,R15
         LR    R13,R3
         LM    R2,R12,28(R13)
         STCM  R0,B'1110',DIRTTR        TTR OF THIS MEMBER
         CLM   R0,B'1110',MEMBER2       AT OR AFTER THE START POINT?
         BL    REST262                  NO, BRANCH
         SPACE 1
         L     R4,#MEMPTR               START OF TTR TABLE
REST268  CLC   DIRTTR(3),0(R4)          IN THE TABLE?
         BE    REST262                  YES, IGNORE THIS MEMBER
         CLI   3(R4),X'FF'              END OF TTR TABLE?
         LA    R4,4(,R4)
         BNE   REST268                  NO, BRANCH
         SPACE 1
         TM    FLAGSCC,RECFMU           RECFM=U?
         BNO   REST410                  NO, BRANCH
         CLI   #RESTNAM,X'40'           ANY MODULE NAME?
         BH    REST430                  YES, BRANCH
         SPACE 1
REST410  CLI   #RESTSTR+1,0             ANY STRING?
         BNE   REST490                  NO, BRANCH
         B     REST710
         SPACE 2
REST420  L     R15,=V(EXCP)
         BASR  R14,R15
         B     *+4(R15)                 PROCESS RETURN CODE
         B     REST430                    RC=00 -- GOOD READ
         B     REST264                    RC=04 -- END OF MEMBER
         B     REST264                    RC=08 -- END OF DATA SET
         B     REST263                    RC=12 -- I/O ERROR
         SPACE 2
REST430  L     R3,EXCPBUFF              START OF RECORD
         CLI   0(R3),X'80'              END OF ESD, FIRST IDR?
         BE    REST262                  YES, BRANCH
         CLI   0(R3),X'20'              ESD RECORD?
         BNE   REST420                  NO, BRANCH
         SPACE 1
         LH    R4,6(,R3)                LENGTH OF ESD DATA
         SRL   R4,4                     NUMBER OF ENTRIES
         LA    R3,8(,R3)                START OF ESD DATA
         SPACE 2
         USING ESDNAME,R3
REST440  CLI   ESDTYPE,X'03'            ENTRY SYMBOL?
         BE    REST444                  NO, BRANCH
         TM    ESDTYPE,X'0F'            CSECT ENTRY?
         BNZ   REST450                  NO, BRANCH
REST444  SR    R1,R1
         ICM   R1,B'0001',#RESTNA#      LENGTH TO TEST
         BCTR  R1,0                     MACHINE LENGTH TO TEST
         CLC   ESDNAME(*-*),#RESTNAM    <<EXECUTED>>
         EX    R1,*-6                   THIS MODULE NAME?
         BE    REST470                  YES, BRANCH
REST450  LA    R3,16(,R3)               NEXT ESD
         BCT   R4,REST440               DO ALL ESD SUBENTRIES
         B     REST420                  DO ALL ESD RECORDS
         DROP  R3
         SPACE 2
REST470  MVC   STARTTR(3),DIRTTR        REREAD THIS MEMBER
         CLI   #RESTSTR+1,0             ANY STRING TOO?
         BE    REST590                  NO, BRANCH
         L     R15,=V(EXCP)
         BASR  R14,R15
         LTR   R15,R15                  GOOD READ?
         BNZ   REST263                  NO, MUST BE I/O ERROR
         SPACE 3
REST490  XC    WORKTBL,WORKTBL
         SR    R15,R15
         IC    R15,#RESTSTR+2           FIRST SEARCH CHARACTER
         LA    R1,WORKTBL(R15)
         MVI   0(R1),X'77'              SET SEARCH CHARACTER OF STRING
         B     REST510                  SKIP FIRST READ
         SPACE 2
REST500  L     R15,=V(EXCP)
         BASR  R14,R15
         B     *+4(R15)                 PROCESS RETURN CODE
         B     REST510                    RC=00 -- GOOD READ
         B     REST264                    RC=04 -- END OF MEMBER
         B     REST264                    RC=08 -- END OF DATA SET
         B     REST263                    RC=12 -- I/O ERROR
         SPACE 2
REST510  L     R6,EXCPBUFF              START OF RECORD
         L     R1,LS                    LENGTH OF RECORD
         LA    R4,256                   MAXIMUM LENGTH FOR TRT
         LR    R5,R1
         AR    R5,R6                    END OF RECORD
         BCTR  R5,0                     END OF RECORD -1
         B     REST540
         SPACE 1
REST520  BXLE  R6,R4,REST540            GET NEXT DATA SEGMENT
         B     REST500                  GET NEXT RECORD
         SPACE 1
REST540  LR    R2,R4                    EXECUTE LENGTH +1
         LR    R14,R5
         SR    R14,R6                   MACHINE LENGTH VALID?
         BM    REST520                  NO, BRANCH
         SPACE 1
         CR    R2,R14                   LONGER THAN RECORD LEFT?
         BNH   *+8                      NO, BRANCH
         LA    R2,1(,R14)               YES, USE REMAINING LENGTH
         BCTR  R2,0                     EXECUTE LENGTH
         SPACE 1
         LR    R1,R6                    START OF DATA
         LA    R14,0(R6,R2)             END OF DATA -1
         LR    R3,R14
         SPACE 1
         LH    R15,#RESTSTR             STRING LENGTH
         AR    R14,R15                  MAXIMUM ACCESS
         BCTR  R15,0                    EXECUTE LENGTH
         CR    R14,R5                   BEYOND RECORD LENGTH?
         BL    *+6                      NO, BRANCH
         LR    R14,R5                   YES, USE RECORD END -1
         SPACE 2
REST550  EX    R2,REST560               FIRST BYTE OF STRING FOUND?
         BZ    REST520                  NO, BRANCH
         LR    R2,R14
         SR    R2,R1                    MACHINE LENGTH LEFT
         CR    R15,R2                   IN THIS SEGMENT?
         BH    REST520                  NO, BRANCH
         CLC   0(*-*,R1),#RESTSTR+2     <<EXECUTED>>
         EX    R15,*-6                  ENTIRE STRING PRESENT?
         BE    REST590                  YES, BRANCH
         LA    R1,1(,R1)                NO, TRY NEXT BYTE
         LR    R2,R3                    LAST ACCESS BYTE
         SR    R2,R1                    MACHINE LENGTH LEFT NEGATIVE?
         BNM   REST550                  NO, KEEP SEARCHING
         B     REST520                  YES, TRY NEXT SEGMENT
         SPACE 1
REST560  TRT   0(*-*,R1),WORKTBL        <<EXECUTED>>
         SPACE 2
REST590  MVC   STARTTR(3),DIRTTR        REREAD THIS MEMBER
         L     R15,=V(EXCP)
         BASR  R14,R15
         LTR   R15,R15                  GOOD READ?
         BNZ   REST263                  NO, MUST BE I/O ERROR
         SPACE 3
REST710  UNPK  INSERT#1(7),DIRTTR(4)    TTR OF THIS DELETED MEMBER
         TR    INSERT#1(6),TRTABLE      MAKE PRINTABLE
         MVC   INSERT#1+6(2),BLANKS
         M$MSG L101$1
         TM    #RESFLAG,#RESFDIS        DISPLAY OPTION?
         BNO   REST790                  NO, BRANCH
         M$ERRST MSGBLANK
         SR    R5,R5                    LINE IN PROGRESS POINTER
         SR    R6,R6                    OUTPUT COUNTER
         TM    FLAGSCC,RECFMU           RECFM=U?
         BO    REST736                  YES, BRANCH
         B     REST762                  NO, MUST BE V OR F
         SPACE 3
*  RECORD FORMAT U (LOAD MODULE)
REST730  L     R15,=V(EXCP)
         BASR  R14,R15
         B     *+4(R15)                 PROCESS RETURN CODE
         B     REST736                    00 - SUCCESSFUL READ
         B     REST732                    04 - END OF MEMBER
         B     REST732                    08 - END OF DATA SET
         B     REST263                    12 - I/O ERROR
         SPACE 1
REST732  LTR   R5,R5                    LINE IN PROGRESS?
         BNP   REST734                  NO, BRANCH
         LA    R1,MSGLINE+5
         SR    R5,R1                    MESSAGE LENGTH
         STC   R5,MTLEN
         MVC   INSERT#1(127),MSGLINE+4
         LA    R1,FULLWORD
         M$MSG (R1)
         MVI   MTLEN,8
         B     REST790
         SPACE 1
REST734  M$MSG L805                     NO CESD OR LKED IDR RECORDS
         MVC   STARTTR(3),DIRTTR        RE-READ THE MEMBER
         B     REST760                  OUTPUT WITH CHARACTER OUTPUT
         SPACE 2
REST736  L     R3,EXCPBUFF              BUFFER ADDRESS
         CLI   0(R3),X'20'              CESD RECORD?
         BNE   REST750                  NO, CHECK FOR LINKAGE-EDIT IDR
         C     R6,#RESTNUM              NEED TO BUILD MORE CSECT LINES?
         BNL   REST730                  NO, BRANCH
         LH    R4,6(,R3)                LENGTH OF ESD DATA
         SRL   R4,4                     LENGTH/16=NUMBER OF ENTRIES
         LA    R3,8(,R3)                START OF ESD DATA
         SPACE 1
         USING ESDNAME,R3
REST738  TM    ESDTYPE,X'0F'            CSECT ENTRY?
         BNZ   REST746                  NO, BRANCH
         CLI   ESDNAME,X'40'            BLANK CSECT NAME?
         BE    REST746                  YES, IGNORE
         LA    R2,ESDNAME+7
         LTR   R5,R5                    LINE IN PROGRESS?
         BP    REST740                  YES, BRANCH
         LA    R1,80                    ASSUME AN ACTIVE MODE
         TM    SPFLAG0,SPFDON           ISPMODE ACTIVE?
         BO    REST739                  YES, BRANCH
         TM    CONTOPTN,1               ANY LOG RECORDING?
         BO    REST739                  YES, BRANCH
*        GTSIZE ,                       TERMINAL SIZE          SS MAY89
         GTSIZE
         CH    R1,=H'120'               120 OR LESS BYTES?
         BL    *+8                      YES, BRANCH
         LH    R1,=H'120'               NO, USE 120 BYTES
REST739  TM    FLAGSCC,RECFMU           LOAD LIBRARY?
         BNO   *+8                      NO, BRANCH
         SH    R1,=H'7'                 YES, LESS SEVEN FOR PREFIX
         ST    R1,LINESIZE              CHARACTERS/LINE
         MVC   MSGLINE+4(L'MSGRESTO),MSGRESTO
         LA    R5,MSGLINE+4+L'MSGRESTO
         MVC   FULLWORD(3),L164$1
         SPACE 2
REST740  CLI   0(R2),X'40'              SCAN BACKWARDS
         BNE   *+8                                   FOR FIRST
         BCT   R2,REST740                                    NON-BLANK
         SPACE 1
         LA    R0,ESDNAME
         SR    R2,R0                    MACHINE LENGTH OF NAME
         LA    R15,2(R2,R5)             CURRENT + NAME-1 + BLANK + 1
         LA    R1,MSGLINE
         SR    R15,R1                   DISPLACEMENT INTO THE LINE
         C     R15,LINESIZE             PAST END OF LINE?
         BNH   REST744                  NO, BRANCH
         SPACE 1
         LA    R6,1(,R6)                ONE MORE OUTPUT LINE
         C     R6,#RESTNUM              ENOUGH LINES?
         BL    REST742                  NO, BRANCH
         MVC   0(4,R5),RESTLDOT         INDICATE MORE CSECTS EXIST
         LA    R5,5(,R5)                INCREMENT TO END +1
         B     REST730
         SPACE 2
REST742  LA    R1,MSGLINE+4
         SR    R5,R1                    MESSAGE LENGTH
         STC   R5,MTLEN
         MVC   INSERT#1(127),MSGLINE+4
         LA    R1,FULLWORD
         M$MSG (R1)
         MVI   MTLEN,8
         MVI   FULLWORD+2,C'9'
         LA    R5,MSGLINE+3             INCREMENT TO END
         SPACE 1
REST744  MVI   0(R5),X'40'              BLANK BEFORE CSECT NAME
         MVC   1(8,R5),ESDNAME          CSECT NAME
         TR    1(8,R5),TRLINE           MAKE PRINTABLE
         LA    R5,2(R2,R5)              POSITION TO FOLLOWING COMMA
         MVI   0(R5),C','               ADD THE COMMA
         LA    R5,1(,R5)                POSITION AFTER THE COMMA
         SPACE 1
REST746  LA    R3,16(,R3)               NEXT ESD ENTRY
         BCT   R4,REST738               LOOP FOR ALL ENTRIES
         B     REST730                  GET THE NEXT RECORD
         DROP  R3
         SPACE 3
REST750  CLI   0(R3),X'80'              IDR RECORD?
         BNE   REST730                  NO, CONTINUE INPUT
         TM    2(R3),IDRLKED            LINKAGE EDITOR IDR RECORD?
         BNO   REST730                  NO, CONTINUE INPUT
         LTR   R5,R5                    LINE IN PROGRESS?
         BNP   REST752                  NO, BRANCH
         LA    R1,MSGLINE+5
         SR    R5,R1                    LENGTH OF MESSAGE SO FAR
         STC   R5,MTLEN
         MVC   INSERT#1(127),MSGLINE+4
         LA    R1,FULLWORD
         M$MSG (R1)
         MVI   MTLEN,8
         SPACE 1
REST752  M$ERRST MSGBLANK
         LA    R1,12+3(,R3)             ADDRESS OF YYDDD FORMAT DATE
         LA    R15,LKEDDATE             ADDRESS OF YYMMDD FORMAT OUTPUT
         BAS   R14,CONVDATE             CONVERT TO MMDDYY FORMAT
         MVC   INSERT#1-1(9),DATEMASK
         ED    INSERT#1-1(9),LKEDDATE
         MVI   MTLEN+4,18+14                  LENGTH OF THIS INSERT
         MVC   INSERT#2(10),3(R3)             LINKAGE EDITOR NAME
         MVC   INSERT#2+10(2),=CL2' V'
         UNPK  INSERT#2+10+2(3),10+3(2,R3)        VV IN HEX
         MVC   INSERT#2+10+2+2(2),=CL2' M'
         UNPK  INSERT#2+10+2+2+2(3),10+3+1(2,R3)  MM IN HEX
         MVI   INSERT#2+10+2+2+2+2,C' '                       DRK JUN99
         SPACE 1                                              DRK MAY99
         CLC   LKEDTIME(4),=X'0000000F' ANY LINK EDIT TIME?   DRK JUL99
         BE    *+4+6+6+6                NO                    DRK MAY99
         MVC   INSERT#2+10+2+2+2+2(4),=CL4'  AT'              DRK MAY99
         MVC   INSERT#2+10+2+2+2+2+4(10),LKDMASK3             DRK MAY99
         ED    INSERT#2+10+2+2+2+2+4(10),LKEDTIME             DRK MAY99
         SPACE 1
         M$MSG L064$2                         LINKAGE EDIT MESSAGE
         MVI   MTLEN+4,8                      LENGTH OF STANDARD INSERT
         B     REST790                  PROMPT FOR RESTORE
LKDMASK3 DC    X'402120207A20207A2020'     HH:MM:SS           DRK MAY99
         SPACE 2
*  RECORD FORMAT V, F OR U
REST760  L     R15,=V(EXCP)
         BASR  R14,R15
         B     *+4(R15)                 PROCESS RETURN CODE
         B     REST762                    00 - SUCCESSFUL READ
         B     REST778                    04 - END OF MEMBER
         B     REST778                    08 - END OF DATA SET
         B     REST263                    12 - I/O ERROR
         SPACE 1
REST762  L     R5,LS                    BLOCK LENGTH
         L     R2,EXCPBUFF              BUFFER START
         LR    R1,R5
         AR    R5,R2                    END OF BUFFER
         BCTR  R5,0                     END OF BUFFER -1
         TM    FLAGSCC,RECFMF           RECFM=F?
         LH    R4,LRECL
         BO    REST768                  YES, BRANCH
         TM    FLAGSCC,RECFMU           RECFM=U?
         LR    R4,R1
         BO    REST768                  YES, BRANCH
         TM    FLAGSCC,RECFMV           RECFM=V?
         BNO   REST768                  NO, ASSUME RECFM=U
         AH    R2,=H'4'                 SKIP BLOCK LENGTH WORD
         B     REST766
         SPACE 2
REST764  BXLE  R2,R4,REST766            GET THE NEXT LOGICAL RECORD
         B     REST760                  END OF BLOCK, BRANCH
         SPACE 2
REST766  TM    FLAGSCC,RECFMV           RECFM=V?
         BNO   REST768                  NO, BRANCH
         SR    R4,R4
         ICM   R4,B'0011',0(R2)         RECORD LENGTH +4
         AH    R2,=H'4'                 SKIP RECORD LENGTH WORD
         SH    R4,=H'4'                 RECORD LENGTH VALID?
         BP    REST768                  YES, BRANCH
         SR    R4,R4                    NO, USE LENGTH ZERO
         B     REST764                  IGNORE THE RECORD
         SPACE 1
REST768  LA    R6,1(,R6)                ANOTHER OUTPUT RECORD
         C     R6,#RESTNUM              ENOUGH OUTPUT?
         BH    REST778                  YES, BRANCH
         MVC   INSERT#1(8),BLANKS       BLANK
         MVC   FULLWORD(8),BLANKS       BLANK
         CVD   R6,DOUBLE
         MVC   FULLWORD(4),=X'40202120'
         ED    FULLWORD(4),DOUBLE+6
         LA    R1,FULLWORD
         LA    R1,1(,R1)
         CLI   0(R1),X'40'
         BE    *-8
         MVC   INSERT#1(3),0(R1)
         M$MSG L144$1
         LR    R1,R4                    CURRENT RECORD LENGTH
         CH    R1,=H'256'
         BNH   *+8
         LH    R1,=H'256'               MAXIMUM DISPLAY LENGTH
         BCTR  R1,0                     MACHINE LENGTH
         MVC   MSGTEXT1+4(*-*),0(R2)    <<EXECUTED>>
         EX    R1,*-6                   MOVE IN THE DISPLAY TEXT
         TR    MSGTEXT1+4(256),TRLINE   CONVERT TO PRINTABLES
         LA    R1,4+1(,R1)              MESSAGE LENGTH
         CH    R4,=H'256'               TRUNCATED RECORD MESSAGE?
         BNH   *+14                     NO, BRANCH
         MVC   MSGTEXT1+4+256(4),RESTLDOT  INDICATE THAT MORE EXISTS
         LA    R1,4(,R1)                ADD TO THE MESSAGE LENGTH
         STH   R1,MSGTEXT1
         M$ERRST MSGTEXT1
         C     R4,LINESIZE              LRECL EXACTLY MATCH LINESIZE?
         BE    REST764                  YES, A BLANK LINE ALREADY (+1)
         M$ERRST MSGBLANK
         B     REST764
         SPACE 1
REST778  TM    FLAGSCC,RECFMU           RECFM=U?
         BO    REST260                  YES, INVALID LOAD MODULE
REST790  M$ERRST MSGBLANK
         CLI   #RESTTST,1               TEST (SIMULATION)?    DRK MAY06
         BE    REST248                  YES, NO RESTORE       DRK MAY06
         TM    #RESFLAG,#RESFPRO        PROMPT OPTION?
         BO    REST794                  YES, BRANCH
         TM    #RESFLAG,#RESFPRO*2+#RESFPRO*4  NOPROMPT OR NO.. GLOBAL?
         BNZ   REST805                         YES, BRANCH
REST794  TM    FLAGSEE,FBKGRND+FNOTTERM INPUT FROM THE TERMINAL?
         BNZ   REST805                  NO, RESTORE THIS MEMBER
         LA    R1,PDS390A               CORRECT MEMBER MESSAGE
         BAS   R2,YESNO1                RESPONSE "YES" OR "NO"?
         B     REST248                  NO, CONTINUE SEARCHING
         L     R8,=A(CLEAR)             ENTRY POINT TO CALL
         BASR  R2,R8                    INVOKE CLEAR
         L     R8,=A(RESTORE)           RESET BASE REGISTER
         B     REST805                  NO DIRECTORY CHECK NEEDED
         SPACE 3
REST803  MVI   STARTTR+2,X'01'          TTR=000001 (START OF DIRECTORY)
         SPACE 2
REST804  BAS   R14,READDIR              GET NEXT DIRECTORY MEMBER
         B     REST805                  LAST MEMBER PROCESSED
         SPACE 1
         CLC   DIRTTR(3),MEMTTR         TTR'S MATCH?
         BNE   REST804                  NO, BRANCH
         SPACE 1
         MVC   INSERT#1(8),MEMNAME
         LA    R1,L802$1                MEMBER ALREADY EXISTS AT TTR
         TM    MEMFLAG,X'80'            AN ALIAS?
         BNO   *+8                      NO, BRANCH
         LA    R1,L190$1                AN ALIAS ALREADY EXISTS AT TTR
         M$MSG (R1)
         SPACE 1
         TM    MEMFLAG,X'80'            ALIAS ENTRY?
         BO    REST804                  YES, CONTINUE SEARCHING
         SPACE 1
         CLC   RESTLTMP,DIRNAME         ALLOW IT ANYWAY?
         BNE   NEWCMD                   NO, BRANCH
         SPACE 2
REST805  TM    FLAGSCC,RECFMU      LOAD LIBRARY?
         BNO   REST896             NO, STOW AND QUIT
         XC    DIRUSER,DIRUSER     CLEAR THE ADDITIONAL FIELDS
         MVC   DIRFLAG(33),#RESTDIR  INITIALIZE THE USER AREA
         SPACE 1
         MVI   #RESTESD,AMODE24    AMODE 24 IS THE DEFAULT
         MVI   #RESTMOD,RMODEANY   SET RMODE ANY SUMMARY START
         XC    #BLKSI(4),#BLKSI    CLEAR THE MAXIMUM MEMBER BLOCKSIZE
         LA    R2,REST810          WHERE-TO-GO
         MVC   STARTTR(3),DIRTTR   START ADDRESS
         SPACE 3
REST806  L     R15,=V(EXCP)
         BASR  R14,R15
         B     *+4(R15)                 PROCESS RETURN CODE
         B     REST808                    00 - GOOD READ
         B     REST890                    04 - END OF MEMBER
         B     REST890                    08 - END OF DATA SET
         B     NEWCMD                     12 - I/O ERROR
         SPACE 2
REST808  L     R5,LS                    CURRENT LENGTH
         LR    R4,R0                    START OF BUFFER
         C     R5,#BLKSI                BLKSIZE>MAX?
         BNHR  R2                       NO, BRANCH
         ST    R5,#BLKSI                YES, SAVE NEW MAXIMUM
         BR    R2                       DECODE THE RECORD
         SPACE 3
REST810  MVI   ENTRYPT,C'?'             NO ENTRY POINT NAME YET
         CLI   0(R4),X'40'              TEST SYM RECORD?
         BNE   REST812                  NO, BRANCH
         OI    DIRATTR,ATTRTEST         TEST SYM FLAG
         OI    DIRATTR+1,ATTRSYMS       SYMBOLS ARE AVAILABLE
         SPACE 1
REST812  LA    R2,REST812               WHERE-TO-GO NEXT
         CLI   0(R4),X'40'              TEST SYM RECORD?
         BE    REST806                  YES, GET NEXT RECORD
         SPACE 1
         CLI   0(R4),X'20'              CESD RECORD?
         BNE   REST820                  NO, BRANCH
         SPACE 1
         LH    R6,4(,R4)                CURRENT ESDID
         LA    R3,8(,R4)                START OF ESD DATA
         USING ESDNAME,R3
         LH    R5,6(,R4)                LENGTH OF DATA IN BUFFER
         AR    R5,R3                    END OF DATA
         LA    R4,16                    LENGTH OF ONE ENTRY
         SR    R5,R4                    END OF DATA -(ONE ENTRY LENGTH)
         SPACE 3
REST813  IC    R0,ESDTYPE
         LA    R1,CODESEG          CHECK FOR SEGTAB/ENTAB
         NI    ESDTYPE,X'0F'
         NR    R0,R1
         CLI   ESDTYPE,CODESD      VALID EXTERNAL SYMBOL?
         BE    REST814             YES, BRANCH
         CLI   ESDTYPE,CODELR      EXTERNAL REFERENCE?
         BNE   REST817             NO, BRANCH
         SPACE 1
REST814  CLC   ESDNAME,DIRNAME     THIS MEMBER NAME?
         BNE   REST815             NO, BRANCH
         SR    R14,R14
         ICM   R14,B'0111',ESDADDR  ENTRY ADDRESS ZERO?
         BZ    *+8                  YES, BRANCH
         NI    DIRATTR+1,FF-ATTREP0 NO, INSURE ATTR FLAG OFF
         STCM  R14,B'0111',DIREPA  MOVE ENTRY ADDRESS TO DIRECTORY
         MVC   ENTRYPT,ESDNAME     SAVE THE NAME FOR LATER
         STCM  R6,B'0011',DIRSCEP  SAVE THE ESDID OF THE ENTRY POINT
         MVC   #RESTESD,ESDXAFLG   SAVE THE MVS/XA ESD FLAG ENTRY
         SPACE 1
REST815  OC    ESDADDR(3),ESDADDR  ZERO OFFSET?
         BNZ   REST817             NO, BRANCH
         CLI   ENTRYPT,C'?'        ANY SYMBOL NAME YET?
         BNE   REST817             YES, BRANCH
         MVC   ENTRYPT,ESDNAME     SAVE THE NAME FOR LATER
         STCM  R6,B'0011',DIRSCEP  SAVE THE ESDID OF THE ENTRY POINT
         MVC   #RESTESD,ESDXAFLG   SAVE THE MVS/XA ESD FLAG ENTRY
REST817  CR    R0,R1               THIS SEGTAB/ENTAB ENTRY?
         BE    REST818             YES, BRANCH
         CLI   ESDTYPE,CODESD      EXTERNAL DEFINITION?
         BE    REST818             YES, BRANCH
         CLI   ESDTYPE,CODEPC      PRIVATE CODE?
         BE    REST818             YES, BRANCH
         CLI   ESDTYPE,CODECM      COMMON STORAGE?
         BNE   REST819             NO, BRANCH
         SPACE 1
REST818  L     R1,ESDADDR-1        RELATIVE OFFSET
         L     R14,ESDLEN-1        LENGTH OF ESD ENTRY
         LA    R14,7(R1,R14)       MAXIMUM DISPLACEMENT +7
         N     R14,RESTLFF8        ROUND TO NEXT DOUBLE WORD
         CLM   R14,B'0111',DIRSTORE SMALLER THAN PREVIOUS HIGH?
         BL    *+8                 YES, BRANCH
         STCM  R14,B'0111',DIRSTORE NO, UPDATE MODULE LENGTH
         NC    #RESTMOD,ESDXAFLG   SUMMARIZE ALL RMODE ENTRIES
         OC    ESDADDR,ESDADDR     ORIGIN AT LOCATION ZERO?
         BNZ   REST819             NO, BRANCH
         OC    ESDLEN,ESDLEN       NULL LENGTH BLOCK?
         BZ    REST819             YES, BRANCH
         STCM  R6,B'0011',DIRSCET  SAVE THE FIRST TEXT ESDID
         SPACE 1
REST819  A     R6,=F'1'            NEXT ESDID
         BXLE  R3,R4,REST813
         B     REST806             GET NEXT RECORD
         SPACE 3
         DROP  R3
REST820  LA    R2,REST820          WHERE-TO-GO NEXT
         CLI   0(R4),X'80'         IDR RECORD?
         BE    REST806             YES, GET NEXT RECORD
         SPACE 1
         LA    R2,REST830          WHERE-TO-GO NEXT
         CLI   0(R4),X'0D'         FILLER RECORD?
         BNE   REST830             NO, BRANCH
         OI    DIRATTR+1,ATTRNRLD  NO RLD ENTRIES
         OI    DIRATTR,ATTR1TXT    ASSUME ONLY ONE TEXT RECORD TOO
         SPACE 1
REST830  CLI   0(R4),X'0D'         FILLER RECORD?
         BE    REST806             YES, IGNORE
         CLI   0(R4),X'01'         RLD ENTRY?
         BNE   REST831             NO, BRANCH
         MVC   DIRATTR4,3(R4)      GET THE NUMBER OF LATER ESD ENTRIES
         B     REST806
         SPACE 1
REST831  TM    DIRATTR+1,ATTRNRLD  NO RLD MARKED?
         BNO   REST833             NO, BRANCH
         CLM   R5,B'0111',DIRSTORE FIRST TEXT LENGTH=TOTAL LENGTH?
         BE    REST833             YES, ONLY ONE TEXT BLOCK
         NI    DIRATTR,FF-ATTR1TXT NO, MULTI-BLOCK TEXT AND NO RLD
         SPACE 1
REST833  STM   R14,R12,12(R13)            CONVERT CCHHR TO TTR
         LA    R2,CURMBB                  CCHHR TO CONVERT
         L     R1,INDCB+(DCBDEBAD-IHADCB) DEB ADDRESS
         L     R15,ADDRRLTV               ADDRESS OF CCHHR->TTR CONVERT
         LR    R3,R13                     ADDRESS OF SAVE AREA
         BASR  R14,R15                    CALL CONVERT ROUTINE
         LR    R13,R3                     RESTORE SAVE AREA ADDRESS
         ST    R0,12+4+4(R13)             RESULT TTR RETURNED IN R0
         LM    R14,R12,12(R13)            RESTORE REGISTERS
         CLI   0(R4),X'10'         SCATTER LOADED?
         BE    REST860             YES, BRANCH
         STCM  R0,B'1110',DIRSTART RESULT TTR
         STCM  R5,B'0011',DIRTEXTL SAVE LENGTH OF FIRST TEXT RECORD
         LA    R2,REST806          WHERE-TO-GO NEXT TIME
         CLI   0(R4),X'00'         OVERLAY DEFINITION RECORD?
         BNER  R2                  NO, LOOP UNTIL END OF MEMBER
         LA    R2,REST840          WHERE-TO-GO NEXT
         LA    R14,7(,R5)
         N     R14,RESTLFF8        ROUND TO NEXT DOUBLE WORD
         ST    R14,DOUBLE          SAVE POSSIBLE ENTRY POINT OFFSET
         B     REST806             GET NEXT RECORD
         SPACE 2
REST840  CLI   0(R4),X'00'         SECOND OVERLAY RECORD?
         BNE   REST806             NO, LOOP FOR ANOTHER
         OI    DIRATTR,ATTROVLY    OVERLAY MODULE
         NI    DIRFLAG,255-X'20'   TWO USER TTR'S NOW
         OI    DIRFLAG,X'40'       TWO USER TTR'S NOW
         L     R14,DOUBLE          GET POSSIBLE ENTRY POINT OFFSET
         OC    DIREPA,DIREPA       NON-ZERO ENTRY POINT?
         BNZ   REST844             YES, BRANCH
         STCM  R14,B'0111',DIREPA  MOVE ENTRY ADDRESS TO DIRECTORY
         NI    DIRATTR+1,FF-ATTREP0  INSURE ATTR FLAG OFF
         MVI   ENTRYPT,C'?'        ENTRY POINT ADDRESS IS NOT KNOWN
REST844  MVI   #RESTESD,AMODE24    AMODE 24 AGAIN
         MVI   #RESTMOD,0          RMODE 24 FOR SUMMARY
         SRL   R5,2                LENGTH/4
         STC   R5,DIRNOTE#         NUMBER OF ENTRIES
         STM   R14,R12,12(R13)            CONVERT CCHHR TO TTR
         LA    R2,CURMBB                  CCHHR TO CONVERT
         L     R1,INDCB+(DCBDEBAD-IHADCB) DEB ADDRESS
         L     R15,ADDRRLTV               ADDRESS OF CCHHR->TTR CONVERT
         LR    R3,R13                     ADDRESS OF SAVE AREA
         BASR  R14,R15                    CALL CONVERT ROUTINE
         LR    R13,R3                     RESTORE SAVE AREA ADDRESS
         ST    R0,12+4+4(R13)             RESULT TTR RETURNED IN R0
         LM    R14,R12,12(R13)            RESTORE REGISTERS
         STCM  R0,B'1110',DIRNOTE  SAVE OVERLAY TABLE TTR
         B     REST806             CONTINUE UNTIL END-OF-MEMBER
         SPACE 1
REST860  STCM  R0,B'1110',DIRNOTE  START OF SCATTER LIST TABLE
         LA    R2,REST863          WHERE-TO-GO NEXT
         MVI   DIRFLAG,X'40'+16    2 TEXT TTRS, 16 HALFWORDS
         OI    DIRATTR,ATTRSCTR    SCATTER LOADED MODULE
         SR    R3,R3               ACCUMULATE LENGTH OF OVERLAY RECORDS
         SPACE 1
REST863  CLI   0(R4),X'10'           SCATTER DEFINITION RECORD?
         BNE   REST865               NO, BRANCH
         AR    R3,R5                 ACCUMULATE SCATTER RECORD LENGTH
         SH    R3,=H'4'              LESS THE HEADER AMOUNT
         B     REST806               CONTINUE FOR ALL SCATTER RECORDS
         SPACE 1
REST865  LA    R2,REST867            WHERE-TO-GO NEXT
         CLI   0(R4),X'0D'           FILLER RECORD?
         BNE   REST867               NO, BRANCH
         OI    DIRATTR+1,ATTRNRLD    NO RLD ENTRIES
         OI    DIRATTR,ATTR1TXT      ASSUME ONLY ONE TEXT RECORD TOO
         SPACE 1
REST867  CLI   0(R4),X'0D'         FILLER RECORD?
         BE    REST806             YES, IGNORE
         CLI   0(R4),X'01'         RLD ENTRY?
         BNE   REST872             NO, BRANCH
         MVC   DIRATTR4,3(R4)      GET THE NUMBER OF LATER ESD ENTRIES
         B     REST806
         SPACE 1
REST872  TM    DIRATTR+1,ATTRNRLD    NO RLD MARKED?
         BNO   REST876               NO, BRANCH
         CLM   R5,B'0111',DIRSTORE   FIRST TEXT LENGTH=TOTAL LENGTH?
         BE    REST876               YES, ONLY ONE TEXT BLOCK
         NI    DIRATTR,FF-ATTR1TXT   NO, MULTI-BLOCK TEXT AND NO RLD
         SPACE 2
REST876  STM   R14,R12,12(R13)            CONVERT CCHHR TO TTR
         LA    R2,CURMBB                  CCHHR TO CONVERT
         L     R1,INDCB+(DCBDEBAD-IHADCB) DEB ADDRESS
         L     R15,ADDRRLTV               ADDRESS OF CCHHR->TTR CONVERT
         LR    R3,R13                     ADDRESS OF SAVE AREA
         BASR  R14,R15                    CALL CONVERT ROUTINE
         LR    R13,R3                     RESTORE SAVE AREA ADDRESS
         ST    R0,12+4+4(R13)             RESULT TTR RETURNED IN R0
         LM    R14,R12,12(R13)            RESTORE REGISTERS
         STCM  R0,B'1110',DIRSTART   START OF TEXT
         STCM  R5,B'0011',DIRTEXTL   LENGTH OF FIRST TEXT RECORD
         LA    R6,2(R6,R6)           (#CSECTS+1)*2+2
         SRL   R6,2                  ((#CSECTS+1)*2+2)/4
         SLL   R6,2                  ROUNDED TO NEXT FULLWORD*2
         STCM  R6,B'0011',DIRSCTL    SAVE LENGTH OF TRANSLATE TABLE
         SR    R3,R6
         STCM  R3,B'0011',DIRSCLL    SAVE LENGTH OF SCATTER LIST
         LA    R2,REST806            WHERE-TO-GO NEXT TIME
         BR    R2                    SCAN UNTIL END-OF-MEMBER
         SPACE 1
REST890  LA    R1,L800
         OC    DIRSTART(3),DIRSTART     ANY TEXT TTR?
         BZ    REST898                  NO, ERROR
         SPACE 1
         LA    R1,L703
         OC    DIRSTORE(3),DIRSTORE     STORAGE SIZE AVAILABLE?
         BZ    REST898                  NO, ERROR
         SPACE 1
         CLC   #BLKSI+2(2),=H'1024'     LARGEST BLOCK=1024?
         BNE   *+8                      NO, PROBABLY NOT DC
         NI    DIRATTR+1,FF-ATTRNODC    YES, TURN OFF NO DC
         SPACE 1
         LA    R2,DIRAPF                APF FLAG
         TM    DIRATTR,ATTRSCTR         SCATTER LOADED?
         BNO   *+8                      NO, BRANCH
         LA    R2,8(,R2)                YES, SKIP OVER SCATTER DATA
         OC    #RESTSSI,#RESTSSI        ANY SSI DATA?
         BZ    REST893                  NO, BRANCH
         LA    R2,1(,R2)                PREPARE FOR HALFWORD ROUNDING
         N     R2,=F'-2'                ROUND TO HALFWORD
         OI    DIRATTR2,DIR2SSI         SSI DATA IS PRESENT
         OI    DIRFLAG,X'02'            ADD TWO MORE HALFWORDS
         MVC   0(4,R2),#RESTSSI         ADD THE SSI DATA
         LA    R2,4(,R2)                POSITION OVER SSI DATA
         SPACE 1
REST893  MVI   0(R2),X'01'              APF LENGTH IS 1
         MVC   1(1,R2),#RESTAPF         ADD APF DATA
         CLI   #RESTLIK,0               ANY LIKE MEMBER?
         BH    REST896                  YES, BRANCH (RMODE, AMODE SET)
         SPACE 1
         TM    #RESTMOD,RMODEANY        ALL CSECTS RMODE ANY?
         BNO   *+8                      NO, BRANCH
         OI    DIRATTR3,DIRRMANY        YES, SET RMODE ANY
         TM    #RESTESD,AMODE24         LOW-ORDER BIT ON?
         BNO   *+8                      NO, BRANCH
         OI    DIRATTR3,DIRAM24         YES, SET AMODE 24 ON
         TM    #RESTESD,AMODE31         HIGH-ORDER BIT ON?
         BNO   *+8                      NO, BRANCH
         OI    DIRATTR3,DIRAM31         YES, SET AMODE 31 ON
         TM    #RESTESD,AMODE64         AMODE 64 BIT ON?      DRK OCT02
         BNO   *+8                      NO, BRANCH            DRK OCT02
         OI    DIRATTR3,DIRAM64         YES, SET AMODE 64 ON  DRK OCT02
         SPACE 1
REST896  BAS   R2,OPENSTOW         OPEN THE STOW DCB; ENQUEUE
         B     NEWCMD              COULD NOT OPEN -- ERROR
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR  TTR
         STOW  STOWDCB,DIRNAME,A   ADD AT THE GIVEN TTR
         LR    R5,R15              SAVE RETURN CODE
         BAS   R2,SHUTSTOW         CLOSE STOW, RELEASE ENQUEUES
         SPACE 1
         B     *+4(R5)             PROCESS RETURN CODE
         B     REST897               00 - ADDED CORRECTLY
         B     MEMEXIST              04 - NAME ALREADY IN THE DIRECTORY
         EX    0,*                   08 - SHOULD NOT HAPPEN WITH ADD
         B     FULLDIR               12 - DIRECTORY IS ALREADY FULL
         B     IOERROR               16 - I/O ERROR IN THE DIRECTORY
         SPACE 1
REST897  MVC   INSERT#1(8),DIRNAME   RESTORED MEMBER
         M$MSG L091$1
         SPACE 1
         TM    SPFLAG2,SPFLNCD            LINE COMMAND?        SS AUG85
         BO    REST89D                    YES, BRANCH
         SPACE 1
         MVC   MEMBERD+1(2+8+8+2),LMEMBER1          DEFAULT MEMBER1
         MVC   MEMBERD+1+2+8(8),LMEMBER1+2          DEFAULT MEMBER2
         MVC   MEMBERD+1+2+8+8(2),LMEMBER1          DEFAULT LENGTH
         MVI   MEMBERD,FMEMBER1                     ASSUME ONE MEMBER
         NI    PMEMMIN,FF-X'80'                     NO MEMBER LIST NOW
         TM    #RESFLAG,#RESFREP                    REPEAT?
         BZ    *+8                                  NO, BRANCH
         OI    MEMBERD,FMEMBER2+FMEMRANG+FMEM#MEM   MEMBER RANGE
         BAS   R14,DEFGROUP                    ADD DEFAULT GROUP
         SPACE 2
REST89D  TM    FLAGSCC,RECFMU         LOAD LIBRARY?
         BNO   REST899                NO, DONE WITH THIS MEMBER
         SPACE 1
         UNPK  INSERT#1(7),DIREPA(4)
         TR    INSERT#1(6),TRTABLE
         MVC   INSERT#1+6(2),BLANKS
         MVC   INSERT#2(8),ENTRYPT    NAME OF THE ENTRY POINT
         TR    INSERT#2(8),TRLINE     MAKE PRINTABLE
         LA    R1,L102$1              ASSUME NONE FOUND
         CLI   ENTRYPT,C'?'           ANY FOUND?
         BE    *+8                    NO, BRANCH
         LA    R1,L103$2              YES, SHOW ENTRY SYMBOL
         M$MSG (R1)
         SPACE 1
         UNPK  INSERT#1(7),DIRSTORE(4)
         TR    INSERT#1(6),TRTABLE
         MVC   INSERT#1+6(2),BLANKS
         ICM   R1,B'0111',DIRSTORE    ACTUAL MODULE LENGTH
         LA    R1,1023(,R1)           NEXT HIGHER
         SRL   R1,10                             1K VALUE
         CVD   R1,DOUBLE
         MVC   INSERT#2(8),=X'402020202120D2404040'
         ED    INSERT#2(6),DOUBLE+5
         M$MSG L104$2
         B     REST899
         SPACE 1
REST898  M$MSG (R1)
         SPACE 2
REST899  TM    #RESFLAG,#RESFREP   REPEAT OPTION?
         BNO   NEWCMD              NO, DONE
         TM    #RESFLAG,#RESFPRO*2+#RESFPRO*4  NOPROMPT OR NO.. GLOBAL?
         BZ    REST247                         NO, BRANCH
         TM    #RESFLAG,#RESFPRO   PROMPT OPTION?
         BO    REST247             YES, PROCESS THE NEXT DELETED MEMBER
         M$ERRST MSGBLANK          ONE BLANK LINE
         M$ERRST MSGBLANK          ONE BLANK LINE
         B     REST247             PROCESS THE NEXT DELETED MEMBER
         SPACE 1
RESTLFF8 DC    0F'0',X'FFFFFFF8'
RESTLDOT DC    C' ...'
RESTLTMP DC    C'TEMP'
MSGRESTO DC    C'CSECTS ARE:'
