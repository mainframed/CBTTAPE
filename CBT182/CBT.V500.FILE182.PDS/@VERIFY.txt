         TITLE 'P D S  --  PDS VERIFY                        09/28/88'
***********************************************************************
***      VERIFY SUBCOMMAND     ADDED BY BRUCE LELAND --  MAY, 1983  ***
***********************************************************************
*
         SPACE 1
VERIFY   CSECT
         USING *,R8
         XC    #VERMAXV(4),#VERMAXV                           DRK DEC06
         TM    DS1SMSFG,DS1PDSE         PDSE DATASET?         DRK AUG05
         BNO   VERP000                  NO, CONTINUE          DRK AUG05
         TM    FLAGSCC,RECFMU           RECFM=U?              DRK AUG05
         BNO   VERP000                  NO, CONTINUE          DRK AUG05
         TM    FLAGSAA,FMEMBER1+FMEMBER2  ALL MEMBERS?        DRK SEP05
         BNZ   VERP000                    NO, BRANCH          DRK SEP05
         MVI   #VERNLKD,X'02'             YES, NOLKED         DRK AUG05
         SPACE 1                                              DRK AUG05
VERP000  DS    0H                                             DRK AUG05
         TM    FLAGSCC,RECFMF      FIXED FORMAT RECORDS?
         BNO   VERP00              NO, BRANCH
         LH    R15,BLKSI           BLKSIZE
         SR    R14,R14
         LH    R1,LRECL            LRECL
         LTR   R1,R1               ANY LRECL?
         BZ    VERP00              NO, AVOID DIVIDE ERROR
         DR    R14,R1              BLKSIZE/LRECL
         LTR   R14,R14             INTEGRAL NUMBER OF LRECL RECORDS?
         BZ    VERP00              YES, BRANCH
         M$MSG L482                NO, ERROR
         SPACE 2
VERP00   L     R15,AVERS00         SEQUENTIAL CHECK ROUTINE
         TM    DSORG,DS1DSGPO      PARTITIONED DATA SET?
         BNOR  R15                 NO, PERFORM OTHER GROUP OF CHECKS
         SPACE 1
         TM    FLAGSAA,FMEMBER1+FMEMBER2  ALL MEMBERS?
         BNZ   VERP02                     NO, BRANCH
         TM    FLAGSAA,FMEM#MEM           MEMBER GROUP?
         BNO   VERP03                     NO, BRANCH
         SPACE 2
* SINGLE MEMBER CHECK IS DESIRED
VERP02   OI    #VERUPDT,4+2               SET FLAG FOR LATER
         MVC   MEMNAME(12),DIRNAME        SET THE MEMBER NAME
         SR    R1,R1                      NO MESSAGE YET
         BAS   R2,VERMBR                  OUTPUT MEMBER IDENTIFIER
         LA    R4,#VERR4                  POINT TO A WORK AREA
         ST    R4,#MEMPTR                 ADD AT END OF THE TABLE
         SR    R3,R3                      NO TABLE ENTRIES
         B     VERP18                     SKIP INITIALIZATION
         SPACE 2
VERP03   LA    R1,L875             ASSUME NO DIRECTORY BLOCKS
         CLC   DS1LSTAR(3),ZERO    CORRECT?
         BE    MSGNEW              YES, BRANCH
*        TM    #VERUPDT,2          NOUPDATE SPECIFIED?
*        BO    VERP09              YES, DO NOT OPEN STOW
         TM    #VERUPDT,1          UPDATE SPECIFIED?          DRK NOV04
         BO    VERP03U             YES, OPEN STOW             DRK NOV04
         B     VERP09              NO, DO NOT OPEN STOW (DEF) DRK NOV04
VERP03U  DS    0H                                             DRK NOV04
         TM    DS1SMSFG,DS1PDSE    PDSE DATASET?              DRK AUG05
         BO    VERP09              YES, DO NOT OPEN STOW      DRK AUG05
         BAS   R2,OPENSTOW         OPEN THE STOW DCB; ENQUEUE
         B     VERP08              COULD NOT OPEN -- ERROR
         SPACE 1
         MVI   MSGTEXT2,X'F9'          HIGH IN COLLATING SEQUENCE
         MVC   MSGTEXT2+1(7),=C'FIXPDS ' TEMPORARY STOW/DELETE NAME
         MVI   MSGTEXT2+11,B'1001111'  ALIAS/MAX LENGTH ENTRY
         STOW  STOWDCB,MSGTEXT2,A      ADD THIS MEMBER
         SPACE 1
         B     *+4(R15)                PROCESS RETURN CODE
         B     VERP06                    00 - SUCCESSFUL
         B     VERP06                    04 - MEMBER ALREADY EXISTS
         EX    0,*                       08 - SHOULD NOT OCCUR
         B     VERP04                    12 - FULL DIRECTORY
         M$MSG L836                      16 - I/O ERROR IN DIRECTORY
         B     VERP08
         SPACE 1
VERP04   M$MSG L855                    DIRECTORY IS FULL
         B     VERP08                    12 - FULL DIRECTORY
         SPACE 2
VERP06   STOW  STOWDCB,MSGTEXT2,D      DELETE THIS MEMBER
         SPACE 1
         LTR   R15,R15                 ANY ERROR?
         BNP   VERP08                  NO, BRANCH
         M$MSG L836                    YES, I/O ERROR IN DIRECTORY
VERP08   BAS   R2,SHUTSTOW         CLOSE THE STOW DCB; DEQUEUE
         SPACE 2
VERP09   MVI   SUBPOOLT,21         TEMPORARY STORAGE IN USE
         MVI   STARTTR+2,X'01'     READ THE DIRECTORY
         LA    R5,1024/2           1024 MEMBERS INITIALLY
         SPACE 1
VERP10   SLL   R5,1                MEMBERS*2 FOR EACH LOOP
         SR    R6,R6
         LR    R3,R5
         LR    R0,R5
         SLL   R0,4                MEMBERS*16 IS TABLE SIZE
         ICM   R0,B'1000',SUBPOOLT
         GETMAIN R,LV=(0)          ALLOCATE A NEW MEMBER TABLE
         LR    R4,R1               NEW TABLE ADDRESS
         ICM   R0,B'1111',#MEMPTR  ANY PREVIOUS MEMBERS?
         BZ    VERP12              NO, BRANCH
         SRL   R3,1                YES, USE SECOND HALF OF TABLE
         LR    R14,R4              NEW TABLE START ADDRESS
         LR    R15,R5
         SLL   R15,3               MEMBERS*8 IS OLD TABLE SIZE
         LR    R6,R15              DISPLACEMENT TO NEW PART
         LR    R1,R15
         MVCL  R14,R0              PRESERVE THE PREVIOUS TABLE
         L     R1,#MEMPTR
         LR    R0,R6               LENGTH TO FREE
         ICM   R0,B'1000',SUBPOOLT SUBPOOL TO FREE
         FREEMAIN R,LV=(0),A=(1)
         SPACE 1
VERP12   ST    R4,#MEMPTR          MEMBER TABLE BASE
         AR    R4,R6               WHERE TO ADD MEMBERS
         SPACE 2
VERP14   BAS   R14,READDIR         GET THE NEXT MEMBER
         B     VERP50              END OF MEMBERS, BRANCH
         MVI   #MEMPTR,0           CLEAR THE MEMBER FLAG
         SPACE 3
* CHECK FOR STANDARD MEMBER NAMES IF REQUIRED
         TM    #VERSTND,X'02'      STANDARD NAME CHECK?
         BNZ   VERP18              NO, BRANCH
         SPACE 1
         LA    R15,7                                            QF84002
         LA    R14,DOUBLE+8
         MVC   DOUBLE(8),MEMNAME
         SPACE 1
VERP16   BCTR  R14,0               SCAN
         CLI   0(R14),X'40'            BACKWARDS
         BNE   *+8                              FOR FIRST
         BCT   R15,VERP16                             NON-BLANK QF84002
         SPACE 1
         SR    R1,R1               NO MESSAGE YET
         LA    R2,VERP18           EXIT ADDRESS
         CLI   MEMNAME,C'0'        NUMERIC FIRST CHARACTER?
         BNL   VERMBR              YES, NON-STANDARD
         SPACE 1
         TRT   DOUBLE(*-*),TRTMEM  <<EXECUTED>>
         EX    R15,*-6             STANDARD MEMBER NAME?        QF84002
         LA    R1,0
         LA    R2,VERP18
         BNZ   VERMBR              NO, BRANCH
         SPACE 3
* CHECK FOR CONFLICTING LOAD MODULE ATTRIBUTES
VERP18   L     R14,DIRPTRS         CURRENT MEMBER
         MVC   DIRNAME(12),0(R14)  MOVE IN NAME, FIRST TTR
         TM    FLAGSCC,RECFMU      LOAD LIBRARY?
         BO    VERP18U             YES, BRANCH
*** GET SIZE FOR SOURCE
         CLI   #VERSIZE,2          NOSIZE REQUESTED?
         BE    VERP28              YES, BRANCH
         CLI   #VERREAD,X'02'      NO, INPUT REQUESTED?
         BNE   VERP28              YES, BRANCH
         TM    DIRFLAG,X'80'       ALIAS?
         BO    VERP28              YES, BRANCH
*        MVC   DIRNAME(40),0(R14)  MOVE IN NAME, OTHER DATA
         MVC   DIRNAME(52),0(R14)  MOVE IN NAME, OTHER DATA   DRK SEP09
         SR    R0,R0               ZERO FOR NO STATS
         TM    DIRFLAG,X'0F'       SPF STATISTICS PRESENT?
         BO    VERP18A             YES, BRANCH
         TM    DIRFLAG,X'14'       EXT SPF STATISTICS PRESENT?DRK AUG09
         BO    VERP18A             YES, BRANCH                DRK AUG09
         B     VERP18C             NO
VERP18A  DS    0H
         TM    DIRSPFCR,X'FE'      0C OF 0CYYDDDF ZEROS?      Y2K AUG97
         BNZ   VERP18C             NO, BRANCH                 Y2K AUG97
         TM    DIRSPFCD,X'FE'      0C OF OTHER 0CYYDDDF ZERO? Y2K AUG97
         BNZ   VERP18C             NO, BRANCH                 Y2K AUG97
         SPACE 1
         TM    DIRSPFFL,DIRSPFXS   EXTENDED STATS?            DRK AUG09
         BNO   VERP18B             NO                         DRK AUG09
         L     R0,DIRSPXSI         YES                        DRK AUG09
         B     VERP18C                                        DRK AUG09
VERP18B  DS    0H
         SR    R0,R0                                          DRK JAN98
         ICM   R0,B'0011',DIRSPFSI GET CURRENT SIZE           DRK JAN98
VERP18C  CVD   R0,DOUBLE           CONVERT TO DECIMAL
         L     R1,ATTRCNT2
         LA    R1,1(,R1)             ADD ONE MORE
         ST    R1,ATTRCNT2
         OI    ATTRTOT2+7,X'0F'      INSURE VALID PACKED SIGN
         AP    ATTRTOT2(8),DOUBLE(8) ADD IN CURRENT VALUE
*        LA    R1,VERSUMM          VERIFY SUMMARY
         L     R1,AVERSUMM         VERIFY SUMMARY
         ST    R1,ADDRSUMR         SET FOR A LATTER CALL
         XC    DIRNAME+12(28),DIRNAME+12   CLEAR AFTER NAME, TTR
         B     VERP28
*** GET SIZE FOR LOAD
VERP18U  TM    #VERNLKD,X'02'      NOLKED?
         BO    VERP24              YES, BRANCH
         SPACE 1
         CLI   DIRFLAG,0           LENGTH=12, DATA?           DRK NOV20
         BE    VERP24              YES, BRANCH                DRK NOV20
         MVC   DIRNAME(40),0(R14)  MOVE IN NAME, OTHER DATA
         SPACE 1
         TM    DIRFLAG,X'80'       ALIAS?
         BO    VERP18Z             YES, BRANCH
         L     R15,=A(VERAD00)
         BASR  R14,R15
*        BAS   R14,VERAD00
VERP18Z  DS    0H
         TM    DIRATTR2,DIRAOSLE           VS LINKAGE EDITOR?
         BNO   VERP20                      NO, BRANCH
         TM    DIRATTR3,DIRRMANY           RMODE ANY?
         BZ    VERP20                      NO, BRANCH
         SR    R1,R1
         IC    R1,DIRATTR3                 AMODE BITS
         TM    DIRFLAG,X'80'               ALIAS?
         BNO   *+8                         NO, BRANCH
         SRL   R1,2                        YES, SHIFT AMODE BITS
         STC   R1,MODESAVE                 SAVE CURRENT AMODE
         SPACE 1
         TM    MODESAVE,DIRRMANY+DIRRM64   RMODE 64?          DRK APR18
         BNO   VERP18ZA                    NO, BRANCH         DRK APR18
         L     R15,=A(VERRM64)             YES, CHECK AMODE           "
         BASR  R14,R15
         B     VERP20                                         DRK APR18
VERP18ZA DS    0H
         LA    R1,=C'ANY'                  ASSUME AMODE IS ANY
         TM    MODESAVE,DIRAM64+DIRAM31    CORRECT?           DRK OCT02
         BO    VERP19                      YES, INVALID
         TM    MODESAVE,DIRAM31            AMODE 31?
         BO    VERP20                      YES, BRANCH
         TM    MODESAVE,DIRAM64            AMODE 64?          DRK OCT02
         BO    VERP20                      YES, BRANCH        DRK OCT02
         LA    R1,=C'24 '                  AMODE MUST BE 24
VERP19   MVI   MTLEN,3                     MESSAGE LENGTH
         MVC   INSERT#1(3),0(R1)           MOVE IN THE AMODE TEXT
         LA    R1,L880$1                   RMODE ANY/AMODE CONFLICT
         BAS   R2,VERMBR                   OUTPUT AN ERROR MESSAGE
         SPACE 3
VERP20   TM    DIRATTR,ATTRRENT                REENTRANT?
         BNO   VERP22                          NO, BRANCH
         TM    DIRATTR,ATTRREUS                REUSABLE?
         BO    VERP22                          YES, BRANCH
         LA    R1,L881                         RENT AND NOT REUS
         BAS   R2,VERMBR                       YES, ERROR MESSAGE
VERP22   LA    R1,L882                         TEST AND NOT EDIT
         TM    DIRATTR+1,ATTRSYMS+ATTRNE       TEST AND NOT EDIT?
         BNO   *+8                             NO, BRANCH
         BAS   R2,VERMBR                       YES, ERROR MESSAGE
         LA    R1,L883                         REUSABLE AND SCATTER
         TM    DIRATTR,ATTRREUS+ATTRSCTR       REUSABLE AND SCATTER?
         BNO   *+8                             NO, BRANCH
         BAS   R2,VERMBR                       YES, ERROR MESSAGE
         TM    DIRATTR,ATTROVLY                OVERLAY DEFINED?
         BNO   VERP24                          NO, BRANCH
         MVC   INSERT#1(8),BLANKS              BLANKS
         MVC   INSERT#1(4),VERRENT             OVERLAY AND REENTRANT
         LA    R1,L884$1                       OVERLAY CONFLICT MESSAGE
         TM    DIRATTR,ATTRRENT                CORRECT?
         BNO   *+8                             NO, BRANCH
         BAS   R2,VERMBR                       YES, ERROR MESSAGE
         MVC   INSERT#1(4),VERREUS             OVERLAY AND REUSABLE
         LA    R1,L884$1                       OVERLAY CONFLICT MESSAGE
         TM    DIRATTR,ATTRREUS                CORRECT?
         BNO   *+8                             NO, BRANCH
         BAS   R2,VERMBR                       YES, ERROR MESSAGE
         MVC   INSERT#1(4),VERREFR             OVERLAY AND REFRESHABLE
         LA    R1,L884$1                       OVERLAY CONFLICT MESSAGE
         TM    DIRATTR+1,ATTRREFR              CORRECT?
         BNO   *+8                             NO, BRANCH
         BAS   R2,VERMBR                       YES, ERROR MESSAGE
         MVC   INSERT#1(4),VERSCTR             OVERLAY AND SCATTER
         LA    R1,L884$1                       OVERLAY CONFLICT MESSAGE
         TM    DIRATTR,ATTRSCTR                CORRECT?
         BNO   *+8                             NO, BRANCH
         BAS   R2,VERMBR                       YES, ERROR MESSAGE
         TM    DIRATTR2,DIRAOSLE               MVS LINKAGE-EDITOR?
         BNO   VERP24                          NO, BRANCH
         MVC   INSERT#1(5),=C'RMODE'
         MVC   INSERT#1+6(3),=C'ANY'           RMODE ANY
         LA    R1,L884$1                       OVERLAY CONFLICT MESSAGE
         TM    DIRATTR3,DIRRMANY               RMODE ANY?
         BNO   *+8                             NO, BRANCH
         BAS   R2,VERMBR                       YES, ERROR MESSAGE
         MVI   INSERT#1,C'A'                   AMODE ANY
         SR    R1,R1
         IC    R1,DIRATTR3                     AMODE BITS
         TM    DIRFLAG,X'80'                   ALIAS?
         BNO   *+8                             NO, BRANCH
         SRL   R1,2                            YES, SHIFT AMODE BITS
         STC   R1,MODESAVE                     SAVE CURRENT AMODE
         LA    R1,L884$1                       OVERLAY CONFLICT MESSAGE
         SPACE 1
         TM    MODESAVE,DIRAM31+DIRAM64        AMODE ANY?     DRK OCT02
         BO    VERP23                          YES, ERROR     DRK OCT02
         MVC   INSERT#1+6(3),=C'31 '           AMODE 31       DRK OCT02
         TM    MODESAVE,DIRAM31                AMODE 31?      DRK OCT02
         BO    VERP23                          YES, ERROR     DRK OCT02
         MVC   INSERT#1+6(3),=C'64 '           AMODE 64       DRK OCT02
         TM    MODESAVE,DIRAM64                AMODE 64?      DRK OCT02
         BO    VERP23                          YES, ERROR     DRK OCT02
         B     VERP24                          NO, BRANCH     DRK OCT02
VERP23   DS    0H
         BAS   R2,VERMBR                       ERROR MESSAGE
         SPACE 3
* LOAD EACH MEMBER IF REQUIRED AND THIS IS A LOAD LIBRARY
VERP24   TM    #VERLOAD,X'02'      LOAD CHECK REQUESTED?
         BNZ   VERP28              NO, BRANCH
         SPACE 1
         TM    FLAGSCC,RECFMU      LOAD LIBRARY?
         BZ    VERP28              NO, BRANCH
         SPACE 1
* SKIP LOAD OF SOURCE MEMBERS
         MVC   DOUBLE(1),DIRFLAG   PDS2LIBF                   DRK MAR11
         NI    DOUBLE,X'7F'        TURN OFF ALIAS BIT         DRK MAR11
         CLI   DOUBLE,X'00'        USER DATA?                 DRK MAR11
         BE    VERP28              NO, SKIP LOAD              DRK MAR11
         CLI   DOUBLE,X'0F'        SPF STATS?                 DRK MAR11
         BE    VERP28              YES, SKIP LOAD             DRK MAR11
         CLI   DOUBLE,X'14'        SPF XSTATS?                DRK MAR11
         BE    VERP28              YES, SKIP LOAD             DRK MAR11
         SPACE 1
         OI    ##ADRPA#,$Q         ENTER QUIET MODE PROCESSING
         L     R2,RECOVER          PREVIOUS RECOVERY ADDRESS
         LA    R1,VERP26           RESUME ADDRESS
         ST    R1,RECOVER          IN-LINE RECOVERY
         STM   R2,R8,#SAVEREG      SAVE REGISTERS
         LOAD   EPLOC=DIRNAME,DCB=INDCB
         DELETE EPLOC=DIRNAME
         ST    R2,RECOVER          RESET RECOVERY ADDRESS
         B     VERP28
         SPACE 1
VERP26   LM    R2,R8,#SAVEREG      SAVE REGISTERS
         ST    R2,RECOVER          RESTORE RECOVERY ADDRESS
         MVI   MTLEN,4             FOUR CHARACTERS FROM ABEND MESSAGE
*        MVC   INSERT#1(4),INSERT#1
         LA    R1,L998$1           "ABEND SNNN LOADING THIS MODULE"
         BAS   R2,VERMBR           OUTPUT AN ABENDED MESSAGE
         SPACE 3
* CHECK IF THIS MEMBER IS IN USE BY AN SPF EDIT SESSION
VERP28   OI    ##ADRPA#,$Q         ENTER QUIET MODE PROCESSING
         SPACE 1
         MVC   DSNMEMQ(8),DIRNAME  MEMBER NAME TO CHECK
         BAS   R2,ENQMTEST         MEMBER IN USE?
         BAS   R2,VERMBR           YES, OUTPUT AN IN-USE MESSAGE
         SPACE 3
* CHECK THAT THE PDS DIRECTORY MEMBERS ARE IN CORRECT COLLATING ORDER
VERP30   NI    ##ADRPA#,FF-$Q      LEAVE QUIET MODE PROCESSING
         CLC   #VERMBR(8),DIRNAME  PREVIOUS:CURRENT
         BH    VERP32                HIGH  -- ERROR, OUT OF ORDER
         MVC   #VERMBR(8),DIRNAME
         BL    VERP34                LOW   -- VALID
         LA    R1,L824               EQUAL -- ERROR, DUPLICATE
         BAS   R2,VERMBR
         B     VERP14              IGNORE THIS MEMBER
         SPACE 1
VERP32   LA    R1,L825             MEMBER OUT OF SEQUENCE
         BAS   R2,VERMBR
         B     VERP14              IGNORE THIS MEMBER
         SPACE 3
* SELECT THE HIGHEST TTR POINTERS FOR THIS MEMBER
VERP34   MVC   0(8+4,R4),DIRNAME   MOVE IN THE NAME, FIRST TTR
         XC    12(4,R4),12(R4)     CLEAR THE SECOND TTR ADDRESS
         TM    MEMTTR+3,DIR1TTR+DIR2TTR  ANY OTHER TTR'S?
         BZ    VERP35                    NO, BRANCH
         SPACE 1
         MVC   12(3,R4),DIRNAME+12 ADD FIRST USER TTR
         TM    MEMTTR+3,DIR2TTR    SECOND TTR?
         BZ    VERP35              NO, BRANCH
         SPACE 1
         CLC   12(3,R4),DIRNAME+16 FIRST TTR > SECOND TTR?
         BH    *+10                YES, BRANCH
         MVC   12(3,R4),DIRNAME+16 NO, USE SECOND TTR
         SPACE 1
VERP35   NI    11(R4),X'80'        SAVE ONLY THE ALIAS BIT
         CLC   8(3,R4),DS1LSTAR    IN DATA SET BOUNDS?
         BNH   VERP36              YES, BRANCH
         SPACE 1
*** PAST DS1LSTAR -- PERHAPS WE NEED TO REOPEN THE DATA SET
         OI    FLAGSJJ,FNOREAD     CALL EXCP FOR OPEN ONLY
         MVC   STARTTR(3),DIRTTR   READ ADDRESS
         L     R15,=V(EXCP)        READ THE FIRST RECORD OF THIS MEMBER
         BASR  R14,R15
         NI    FLAGSJJ,FF-FNOREAD  EXCP OPEN IS COMPLETE
         SPACE 2
         CLC   8(3,R4),DS1LSTAR    IN DATA SET BOUNDS?
         BNH   VERP36              YES, BRANCH
         LA    R1,L872
         BAS   R2,VERMBR           NO, OUTPUT AN ERROR MESSAGE
         B     VERP38
         SPACE 1
VERP36   CLC   12(3,R4),DS1LSTAR   IN DATA SET BOUNDS?
         BNH   VERP37              YES, BRANCH
         LA    R1,L873
         BAS   R2,VERMBR           NO, OUTPUT AN ERROR MESSAGE
         SPACE 1
VERP37   MVI   15(R4),X'FF'        ASSUME NOT AN OS/VS LINKAGE-EDITOR
         TM    FLAGSCC,RECFMU      RECFM=U?
         BNO   VERP38              NO, BRANCH
         TM    #VERNLKD,X'02'      NOLKED?
         BO    VERP38              YES, BRANCH
         TM    DIRATTR2,DIRAOSLE   OS/VS LINKAGE-EDITOR?
         BNO   VERP38              NO, BRANCH
         NI    DIRATTR3,DIRRMANY+DIRAM31+DIRAM64              DRK OCT02
         OC    11(1,R4),DIRATTR3   SAVE THE RMODE/AMODE CHARACTER
         MVC   15(1,R4),DIRATTR4   SAVE THE RLD/CONTROL COUNT
         SPACE 3
* PERFORM AN INSERTION SORT OF THIS ENTRY
VERP38   MVC   DIRNAME(16),0(R4)   SAVE ENTRY FOR INSERTION SORT
         LR    R14,R4              LAST SORT POSITION
         L     R1,#MEMPTR          FIRST SORT POSITION
         SH    R1,=H'16'           ADJUST FOR NEXT LA
         SPACE 1
VERP40   LA    R1,16(,R1)          NEXT TABLE ENTRY
         CLC   8(4,R1),DIRTTR      TABLE TTR:NEW TTR
         BL    VERP40                LOW   --  ITERATE
         BH    VERP41                HIGH  --  INSERT
         CLC   0(8,R1),DIRNAME       EQUAL --  TABLE NAME:NEW NAME
         BL    VERP40                            LOW   -- ITERATE
         BE    VERP42                            EQUAL -- IN PLACE
         SPACE 1
VERP41   SH    R14,=H'16'          PREVIOUS TABLE ENTRY
         MVC   16(16,R14),0(R14)   SLIDE DOWN TO MAKE ROOM
         CR    R14,R1              DONE WITH SHIFTING?
         BH    VERP41              NO, BRANCH
         MVC   0(16,R1),DIRNAME    INSERT NEW ENTRY TO ITS POSITION
         SPACE 2
VERP42   LA    R4,16(,R4)          NEXT TABLE ENTRY
         S     R3,=F'1'            ANY MORE ENTRIES FIT?
         BP    VERP14              YES, BRANCH
         BZ    VERP10              NO, REALLOCATE THE MEMBER TABLE
         SPACE 3
* SINGLE MEMBER -- CHECK DIRECTORY ATTRIBUTES
         ST    R4,#MEMLAST         UPDATE LAST MEMBER POINTER
         MVI   STARTTR+2,X'01'     TTR=00001 (START OF DIRECTORY)
         XC    INSERT#1(8),INSERT#1
         SPACE 1
VERP44   BAS   R14,READDIR         GET NEXT MEMBER
         B     VERP48              LAST MEMBER, BRANCH
         SPACE 1
         CLC   DIRTTR,MEMTTR       TTR MATCH?
         BNE   VERP44              NO, BRANCH
         TM    DIRFLAG,X'80'       FIRST THE MAIN ENTRY?
         BZ    VERP45              YES, BRANCH
         TM    MEMFLAG,X'80'       SECOND AN ALIAS?
         BO    VERP44              YES, IGNORE
         SPACE 1
* FOUND A REAL CORRESPONDING ENTRY
         MVC   INSERT#1(8),MEMNAME REAL ENTRY FOR THE ALIAS
         M$MSG L066$1
         TM    FLAGSCC,RECFMU      LOAD MODULE?
         BNO   VERP44              NO, DONE
         TM    #VERNLKD,X'02'      NOLKED?
         BO    VERP44              YES, BRANCH
         LA    R14,DIRREAL
         TM    DIRATTR,ATTRSCTR    SCATTER LOADED?
         BNO   *+8                 NO, BRANCH
         LA    R14,DIRREALS
         MVC   INSERT#2(8),0(R14)
         TR    INSERT#2(8),TRLINE
         LA    R1,L820$2
         CLC   0(8,R14),MEMNAME    CORRECT MEMBER NAME?
         BE    *+8                 YES, BRANCH
         BAS   R2,VERMBR           NO, MESSAGE
         B     VERP47
         SPACE 3
* FOUND AN ALIAS CORRESPONDING ENTRY
VERP45   MVC   INSERT#1(8),MEMNAME
         TM    MEMFLAG,X'80'       AN ALIAS?
         BO    VERP46              YES, BRANCH
         CLC   DIRNAME(8),MEMNAME  FIND MYSELF?
         BE    VERP44              YES, BRANCH
         M$MSG L864$1              FOUND AN APPARENT ALIAS
         B     VERP73
         SPACE 1
VERP46   TM    FLAGSCC,RECFMU      LOAD MODULE?
         BNO   VERP44              NO, BRANCH
         TM    #VERNLKD,X'02'      NOLKED?
         BO    VERP44              YES, BRANCH
         L     R3,DIRPTRS
         LA    R14,DIRREAL-DIRNAME(R3)
         TM    DIRATTR-DIRNAME(R3),ATTRSCTR
         BNO   *+8
         LA    R14,DIRREALS-DIRNAME(R3)
         MVC   INSERT#2(8),0(R14)
         TR    INSERT#2(8),TRLINE
         LA    R1,L827$2
         CLC   0(8,R14),DIRNAME    CORRECT MEMBER NAME?
         BE    *+8                 YES, BRANCH
         BAS   R2,VERMBR           NO, MESSAGE
         SPACE 1
VERP47   L     R3,DIRPTRS
         LA    R1,L823$1           RLD/CONTROL MISMATCH
         CLC   DIRATTR4(1),DIRATTR4-DIRNAME(R3)
         BE    *+8
         BAS   R2,VERMBR
         SPACE 1
         MVI   DOUBLE+0,DIRRMANY
         MVI   DOUBLE+1,DIRRMANY
         NC    DOUBLE+0(1),DIRATTR3
         NC    DOUBLE+1(1),DIRATTR3-DIRNAME(R3)
         LA    R1,L821$1           RMODE BITS DO NOT CORRESPOND
         CLC   DOUBLE(1),DOUBLE+1  EQUAL?
         BE    *+8                 YES, BRANCH
         BAS   R2,VERMBR           NO, ERROR
         SPACE 1
         MVI   DOUBLE+0,DIRAM31+DIRAM64                       DRK OCT02
         MVI   DOUBLE+1,DIRAM31+DIRAM64                       DRK OCT02
         NC    DOUBLE+0(1),DIRATTR3
         NC    DOUBLE+1(1),DIRATTR3-DIRNAME(R3)
         LA    R1,L822$1           AMODE BITS DO NOT CORRESPOND
         CLC   DOUBLE(1),DOUBLE+1  EQUAL?
         BE    *+8                 YES, BRANCH
         BAS   R2,VERMBR           NO, ERROR
         B     VERP44
         SPACE 1
* END OF MEMBERS
VERP48   TM    DIRFLAG,X'80'       ALIAS FIRST MEMBER?        DRK AUG05
*VERP48  TM    DIRNAME,X'80'       ALIAS FIRST MEMBER?
         BNO   VERP73              NO, BRANCH
         CLI   INSERT#1,0          ANY MAIN ENTRY FOUND?
         BNE   VERP73              YES, BRANCH
         M$MSG L860                ORPHAN MEMBER
         TM    FLAGSCC,RECFMU      LOAD MODULE?
         BNO   VERP73              NO, BRANCH
         TM    #VERNLKD,X'02'      NOLKED?
         BO    VERP73              YES, BRANCH
         MVC   INSERT#1(8),DIRREAL
         SPACE 1
         TM    DIRATTR,ATTRSCTR   SCATTER LOADED?
         BNO   *+10               NO, BRANCH
         MVC   INSERT#1(8),DIRREALS
         M$MSG L861$1
         L     R15,=A(VERAD00)
         BASR  R14,R15
*        BAS   R14,VERAD00
         B     VERP73
         SPACE 3
* DIRECTORY TTR SORT COMPLETE -- CHECK FOR ORPHAN MEMBERS, APPARENT
* ALIAS MEMBERS AND ALIAS MEMBERS WITH INCORRECT MAIN MEMBER POINTERS
VERP50   LA    R1,L400
         ST    R4,#MEMLAST         SAVE THE END POINTER
         ICM   R3,B'1111',#MEMPTR  FIRST MEMBER
         MVI   #MEMPTR,0           CLEAR THE MEMBER FLAG
         OI    #VERSTND,X'02'      NO MORE MEMBER NAME CHECKING
         LR    R5,R3
         TM    8+3(R3),X'80'       ALIAS ENTRY?
         BO    VERP54              YES, BRANCH
         B     VERP58+4
         SPACE 1
VERP52   LA    R3,16(,R5)          NEXT TABLE ENTRY
         MVI   #MEMPTR,0           CLEAR THE MEMBER FLAG
         CR    R3,R4               DONE?
         BE    VERP66              YES, BRANCH
         LR    R5,R3
         SPACE 1
         TM    8+3(R3),X'80'       ALIAS ENTRY?
         BNO   VERP58+4            NO, BRANCH
VERP54   LA    R1,L860             YES, THIS IS AN ORPHAN
         MVC   MEMNAME(8),0(R3)
         BAS   R2,VERMBR
         NI    8+3(R3),X'FF'-X'80' TURN OFF THE ALIAS BIT (ORPHAN)
         TM    FLAGSCC,RECFMU      LOAD MODULE?
         BNO   VERP52              NO, BRANCH
         TM    #VERNLKD,X'02'      NOLKED?
         BO    VERP52              YES, BRANCH
         XC    DIRUSER,DIRUSER
         MVC   DIRNAME,0(R3)       MEMBER NAME
         BLDL  INDCB,BLDLLIST
         LTR   R15,R15             ANY BLDL ERROR?
         BP    IOERROR             YES, I/O ERROR
         SPACE 1
         MVC   DIRFLAG(DIREND-DIRFLAG),DIRFLAG+2
         MVC   INSERT#1(8),DIRREAL
         SPACE 1
         TM    DIRATTR,ATTRSCTR   SCATTER LOADED?
         BNO   *+10               NO, BRANCH
         MVC   INSERT#1(8),DIRREALS
         M$MSG L861$1
         L     R15,=A(VERAD00)
         BASR  R14,R15
*        BAS   R14,VERAD00
         B     VERP52
         SPACE 2
VERP58   LA    R5,16(,R5)          NEXT POTENTIAL ALIAS ENTRY
         MVI   #MEMPTR,0           CLEAR THE MEMBER FLAG
         CR    R5,R4               DONE?
         BE    VERP66              YES, BRANCH
         CLC   8(3,R3),16+8(R5)    TTR FOR AN ALIAS?
         BNE   VERP52              NO, BRANCH
         TM    16+8+3(R5),X'80'    ALIAS FLAG SET?
         BNO   VERP64              NO, APPARENT ALIAS
         SPACE 1
         TM    FLAGSCC,RECFMU      LOAD MODULE?
         BNO   VERP58              NO, BRANCH
         TM    #VERNLKD,X'02'      NOLKED?
         BO    VERP58              YES, BRANCH
         XC    DIRUSER,DIRUSER
         MVC   DIRNAME,16(R5)      MEMBER NAME
         BLDL  INDCB,BLDLLIST
         LTR   R15,R15             ANY BLDL ERROR?
         BP    IOERROR             YES, I/O ERROR
         SPACE 1
         MVC   DIRFLAG(DIREND-DIRFLAG),DIRFLAG+2
         LA    R1,DIRREAL          NAME OF CORRESPONDING MAIN
         TM    DIRATTR,ATTRSCTR    SCATTER LOADED?
         BNO   *+8                 NO, BRANCH
         LA    R1,DIRREALS         YES, USE THIS NAME INSTEAD
         SPACE 1
         CLC   0(8,R1),0(R3)       CORRECT NAME RECORDED?
         BE    VERP60              YES, BRANCH
         MVI   MTLEN,8             MEMBER NAME 1
         MVI   MTLEN+4,8           MEMBER NAME 2
         MVC   INSERT#1(8),0(R3)
         MVC   INSERT#2(8),0(R1)
         TR    INSERT#2(8),TRLINE
         LA    R1,L820$2
         MVC   MEMNAME(8),16(R5)
         BAS   R2,VERMBR
         SPACE 1
VERP60   CLC   15(1,R3),16+15(R5)  MATCHING RLD/CONTROL COUNTS?
         BE    VERP62              YES, BRANCH
         MVC   INSERT#1(8),16(R5)
         MVC   MEMNAME(8),0(R3)
         LA    R1,L823$1           NO, RLD/CONTROL MISMATCH
         BAS   R2,VERMBR
         SPACE 1
VERP62   MVI   DOUBLE,DIRRMANY       BIT TO TEST
         MVI   DOUBLE+1,DIRRMANY     BIT TO TEST
         NC    DOUBLE(1),16+11(R5)   ALIAS RMODE BIT
         NC    DOUBLE+1(1),11(R3)    MAIN RMODE BIT
         CLC   DOUBLE(1),DOUBLE+1    RMODE BITS EQUAL?
         BE    VERP63                YES, BRANCH
         MVC   INSERT#1(8),16(R5)
         MVC   MEMNAME(8),0(R3)
         LA    R1,L821$1             NO, RMODE DOES NOT MATCH
         BAS   R2,VERMBR
         SPACE 1
VERP63   MVI   DOUBLE,DIRAM31+DIRAM64    BITS TO TEST         DRK OCT02
         MVI   DOUBLE+1,DIRAM31+DIRAM64  BITS TO TEST         DRK OCT02
         NC    DOUBLE(1),16+11(R5)   ALIAS AMODE BITS
         NC    DOUBLE+1(1),11(R3)    MAIN AMODE BIT
         CLC   DOUBLE(1),DOUBLE+1    AMODE BITS EQUAL?
         BE    VERP58                YES, BRANCH
         MVC   INSERT#1(8),16(R5)
         MVC   MEMNAME(8),0(R3)
         LA    R1,L822$1             NO, AMODE DOES NOT MATCH
         BAS   R2,VERMBR
         B     VERP58
         SPACE 1
VERP64   MVC   INSERT#1(8),16(R5)  MEMBER NAME INSERTED
         MVC   MEMNAME(8),0(R3)
         LA    R1,L864$1
         BAS   R2,VERMBR
         OI    16+8+3(R5),X'80'    TURN ON ALIAS BIT (APPARENT ALIAS)
         B     VERP58
         SPACE 3
* END OF DIRECTORY CHECK, NOW PERFORM ANY INPUT CHECK
VERP66   MVI   STARTTR+2,X'01'     TTR=000001 (START OF DIRECTORY)
         TM    #VERUPDT,4+2        SINGLE MEMBER CHECK?
         BO    VERP73              YES, BRANCH
         SPACE 1
VERP68   L     R15,=V(EXCP)        READ THE DIRECTORY RECORDS
         BASR  R14,R15
         B     *+4(R15)            PROCESS RETURN CODE
         B     VERP70                00 - SUCCESSFUL READ
         B     VERP72                04 - END OF MEMBER
         B     VERP72                08 - END OF DATA SET
         B     IOERROR               12 - I/O ERROR
         SPACE 2
VERP70   L     R0,LS               LENGTH OF RECORD (DD OF RKDD)
         ICM   R0,B'0100',=X'08'   KEY LENGTH OF RECORD (K OF RKDD)
         BAS   R2,VERCOMP          DATA IS RETAINED IN THE OUTPUT
         B     VERP68              READ ALL DIRECTORY BLOCKS
         SPACE 1
VERP72   SR    R0,R0               LENGTH OF RECORD & KEY (KDD OF RKDD)
         BAS   R2,VERCOMP          DATA IS RETAINED IN THE OUTPUT
         OI    ##ADRPA#,$Q         ENTER QUIET MODE PROCESSING
         CLI   CURMBB,0            MULTI-EXTENT DIRECTORY?
         BE    VERP73              NO, BRANCH
         M$ERRST MSGBLANK          YES, ERROR MESSAGE
         M$ERRST MSGBLANK
         M$MSG L870
         SPACE 1
VERP73   ICM   R6,B'1111',#MEMPTR  FIRST MEMBER
         TM    #VERUPDT,4+2        SINGLE MEMBER CHECK?
         BNO   *+8                 NO, BRANCH
         NI    8+3(R6),FF-X'80'    YES, READ THIS MEMBER
         CLI   #VERREAD,X'02'      NOINPUT REQUESTED?
         BE    VERP99              YES, BRANCH
         B     VERP74+4
         SPACE 2
VERP74   LA    R6,16(,R6)          NEXT TABLE ENTRY
         MVI   RLDCOUNT,X'FF'      ASSUME NO RLD/CONTROL IS FOUND
         C     R6,#MEMLAST         DONE?
         BE    VERP97              YES, BRANCH
         TM    8+3(R6),X'80'       ALIAS ENTRY?
         BO    VERP74              YES, BRANCH
         MVI   #MEMPTR,0           CLEAR THE MEMBER FLAG
         MVC   MEMNAME(8),0(R6)    INITIALIZE THE MEMBER NAME
         XC    #MEMCNT(4),#MEMCNT  COUNT OF INPUT LOGICAL RECORDS
         LA    R5,3                ALLOW 3 PERMANENT ERRORS/MEMBER
         SPACE 1
         CLC   8(3,R6),DS1LSTAR    BEYOND END OF DATA MARKER?
         BNL   VERP90              YES, DONE
         MVC   STARTTR(3),8(R6)    NO, FIRST DISK ADDRESS TO READ
         MVC   DIRNAME(8),0(R6)    MEMBER NAME FOR PAM        DRK AUG05
         SPACE 3
VERP76   L     R15,=V(EXCP)
         BASR  R14,R15
         B     *+4(R15)            PROCESS RETURN CODE
         B     VERP80                00 - SUCCESSFUL READ
         B     VERP92                04 - END OF MEMBER
         B     VERP90                08 - END OF DATA SET
         B     VERP78                12 - I/O ERROR
         SPACE 2
*  PERMANENT I/O ERROR  -- SIMULATE END OF MEMBER
VERP78   LA    R1,L991$1           OUTPUT THE I/O ERROR MESSAGE
         MVI   MTLEN,6             LENGTH OF THE TTR INSERT
         BAS   R2,VERMBR
         MVI   MTLEN,8             RESET TO EIGHT
         B     VERP92
         SPACE 2
VERP80   LR    R3,R0               BUFFER START
         L     R5,LS               BLOCK LENGTH
         C     R5,#VERMAX          GREATER THAN MAXIMUM?
         BNH   *+8                 NO, BRANCH
         ST    R5,#VERMAX          YES, SAVE NEW MAXIMUM
         L     R0,#VERCBLK         NUMBER
         A     R0,=F'1'                  OF PHYSICAL
         ST    R0,#VERCBLK                          BLOCKS
         L     R0,#VERCBLC         NUMBER
         AR    R0,R5                     OF PHYSICAL
         ST    R0,#VERCBLC                          CHARACTERS
         LR    R0,R5               LENGTH OF RECORD & KEY (KDD OF RKDD)
         BAS   R2,VERCOMP          DATA IS RETAINED IN THE OUTPUT
         SPACE 1
         CH    R5,#MAXBLKV         EXCEED MAXBLK?
         BNH   VERP81              YES, BRANCH
         OC    #MAXBLKV(2),#MAXBLKV  ANY MAXBLK()?
         BZ    VERP80A               NO, BRANCH
         TM    #MEMPTR,#MEMBLKE    BLKSIZE SIZE ERROR NOTED?
         BO    VERP81              YES, BRANCH
         OI    #MEMPTR,#MEMBLKE    BLKSIZE ERROR NOTED NOW
         CVD   R5,DOUBLE
         MVC   INSERT#1(8),VERP2020
         ED    INSERT#1(7),DOUBLE+5
         LA    R1,L806$1           BLKSIZE ERROR
         BAS   R2,VERMBR
         SPACE 1
VERP80A  CH    R5,BLKSI            BLKSIZE VALID?
         BNH   VERP81              YES, BRANCH
         TM    #MEMPTR,#MEMBLKE    BLKSIZE SIZE ERROR NOTED?
         BO    VERP81              YES, BRANCH
         OI    #MEMPTR,#MEMBLKE    BLKSIZE ERROR NOTED NOW
         CVD   R5,DOUBLE
         MVC   INSERT#1(8),VERP2020
         ED    INSERT#1(7),DOUBLE+5
         LA    R1,L812$1           BLKSIZE ERROR
         BAS   R2,VERMBR
         SPACE 1
VERP81   TM    FLAGSCC,RECFMF      FIXED FORMAT?
         BNO   VERP82              NO, BRANCH
         LH    R14,LRECL           CURRENT LRECL
         TM    #MEMPTR,#MEMDIVE    BLKSIZE/LRECL ERROR NOTED?
         BO    VERP82              YES, BRANCH
         LR    R0,R5               BLOCK LENGTH
         SRDA  R0,32
         DR    R0,R14              BLKSIZE/LRECL
         LTR   R0,R0               ANY REMAINDER?
         BZ    VERP82              YES, BRANCH
         OI    #MEMPTR,#MEMDIVE    BLKSIZE/LRECL ERROR NOTED NOW
         CVD   R5,DOUBLE
         MVC   INSERT#1(8),VERP2020
         ED    INSERT#1(7),DOUBLE+5
         LA    R1,L814$1           BLKSIZE/LRECL ERROR
         BAS   R2,VERMBR
         LH    R14,LRECL           CURRENT LRECL
         SPACE 1
VERP82   LR    R1,R5
         AR    R5,R3               END OF BUFFER
         BCTR  R5,0                END OF BUFFER -1
         TM    FLAGSCC,RECFMF      RECFM=F?
         LH    R4,LRECL
         BO    VERP88              YES, BRANCH
         TM    FLAGSCC,RECFMU      RECFM=U?
         LR    R4,R1
         BO    VERP87              YES, BRANCH
         TM    FLAGSCC,RECFMV      RECFM=V?
         BNO   VERP88              NO, ASSUME RECFM=U
         AH    R3,=H'4'            SKIP BLOCK LENGTH WORD
         B     VERP86
         SPACE 3
         CNOP  0,8                 ALIGN FOR LOOP
VERP84   BXLE  R3,R4,VERP86        GET THE NEXT LOGICAL RECORD
         B     VERP76              END OF BLOCK, BRANCH
         SPACE 2
VERP86   TM    FLAGSCC,RECFMV      RECFM=V?
         BNO   VERP88              NO, BRANCH
         SR    R4,R4
         ICM   R4,B'0011',0(R3)    RECORD LENGTH +4
         LR    R14,R4              RECORD LENGTH +4
         C     R14,#VERMAXV        GREATER THAN MAX?          DRK DEC06
         BNH   *+8                 NO                         DRK DEC06
         ST    R14,#VERMAXV        YES, SAVE NEW MAXIMUM      DRK DEC06
         AH    R3,=H'4'            SKIP RECORD LENGTH WORD
         SH    R4,=H'4'            RECORD LENGTH VALID?
         BNM   VERP88              YES, BRANCH
         TM    #MEMPTR,#MEMLRL0    LRECL ERROR NOTED?
         BO    VERP76              YES, BRANCH
         OI    #MEMPTR,#MEMLRL0    LRECL ERROR NOTED
         MVI   MTLEN,1             LENGTH OF MESSAGE
         LA    R14,240(,R14)       RECORD LENGTH IN DISPLAY
         STC   R14,INSERT#1        PLACE IN THE MESSAGE
         LA    R1,L811$1           LRECL TOO SMALL
         BAS   R2,VERMBR
         B     VERP76
         SPACE 2
VERP87   CLI   RLDCOUNT,X'FF'      ANY RLD/CONTROL RECORD YET?
         BNE   VERP88              YES, BRANCH
         TM    0(R3),X'F0'         TEST, ESD, IDR OR SCATTER RECORD?
         BM    VERP88              YES, BRANCH
         MVC   RLDCOUNT(1),3(R3)   NO, FIRST RLD/CONTROL ENTRY
         SPACE 2
VERP88   L     R1,#MEMCNT          ANOTHER
         LA    R1,1(,R1)                  OUTPUT
         ST    R1,#MEMCNT                       RECORD
         L     R0,#VERCLOG         NUMBER
         A     R0,=F'1'                  OF LOGICAL
         ST    R0,#VERCLOG                         RECORDS
         CH    R14,LRECL           VALID LRECL VALUE?
         BNH   VERP84              YES, BRANCH
         TM    FLAGSCC,RECFMU      RECFM=U?
         BO    VERP84              YES, BRANCH
         TM    #MEMPTR,#MEMLRLE    LRECL ERROR NOTED?
         BO    VERP84              YES, BRANCH
         OI    #MEMPTR,#MEMLRLE    LRECL ERROR HAS BEEN NOTED
         CVD   R14,DOUBLE
         MVC   INSERT#1(8),VERP2020
         ED    INSERT#1(7),DOUBLE+5
         LA    R1,L813$1           LRECL TOO LARGE
         BAS   R2,VERMBR
         B     VERP84
         SPACE 2
VERP90   MVI   #MEMCNT,X'FF'              END OF DATA SET
         SPACE 2
VERP92   DS    0H
         TM    DS1SMSFG,DS1PDSE           PDSE DATASET?       DRK NOV20
         BO    VERP93                     YES, BRANCH         DRK NOV20
         CLI   11(R6),0                   LENGTH=12, DATA?    DRK NOV20
         BE    VERP93                     YES, BRANCH         DRK NOV20
         SR    R0,R0               LENGTH OF RECORD & KEY (KDD OF RKDD)
         BAS   R2,VERCOMP          DATA IS RETAINED IN THE OUTPUT
         OC    12(3,R6),12(R6)            ANY USER TTR?
         BZ    VERP93                     NO, BRANCH
         STM   R14,R12,12(R13)            CONVERT CCHHR TO TTR
         LA    R2,CURMBB                  CCHHR TO CONVERT
         L     R1,INDCB+(DCBDEBAD-IHADCB) DEB ADDRESS
         L     R15,ADDRRLTV               ADDRESS OF CCHHR->TTR CONVERT
         LR    R3,R13                     ADDRESS OF SAVE AREA
         BASR  R14,R15                    CALL CONVERT ROUTINE
         LR    R13,R3                     RESTORE SAVE AREA ADDRESS
         ST    R0,12+4+4(R13)             RESULT TTR RETURNED IN R0
         LM    R14,R12,12(R13)            RESTORE REGISTERS
         CLM   R0,B'1110',12(R6)          TTR ACTUALLY INPUT?
         BH    VERP93                     YES, BRANCH
         LA    R1,L871                    NO, ERROR MESSAGE
         BAS   R2,VERMBR
         SPACE 1
VERP93   OC    #MEMCNT+1(3),#MEMCNT+1  ANY RECORDS?
         BNZ   VERP94                  YES, BRANCH
         LA    R1,L510                 NO, NULL MEMBER WARNING
         BAS   R2,VERMBR
         SPACE 2
VERP94   CLI   15(R6),X'FF'        OS/VS LINKAGE EDITOR?
         BE    VERP95              NO, BRANCH
         CLC   15(1,R6),RLDCOUNT   RLD/CONTROL COUNTS MATCH?
         BE    VERP95              YES, BRANCH
         TM    FLAGSCC,RECFMU      RECFM=U?
         BNO   VERP95              NO, BRANCH
         SPACE 1
         CLI   11(R6),0            LENGTH=12, DATA?           DRK NOV20
         BE    VERP95              YES, BRANCH                DRK NOV20
         SPACE 1                                              DRK AUG05
         TM    DS1SMSFG,DS1PDSE    PDSE DATASET?              DRK AUG05
         BNO   *+4+2+4             NO, RLD ERROR              DRK AUG05
         SR    R1,R1               YES                        DRK AUG05
         B     VERP94E             SKIP ERROR MESSAGE         DRK AUG05
         SPACE 1                                              DRK AUG05
         LA    R1,L826             ERROR MESSAGE
VERP94E  DS    0H                                             DRK AUG05
         BAS   R2,VERMBR
         SPACE 2
VERP95   TM    FLAGSCC,RECFMU      RECFM=U?
         BO    VERP96              YES, BRANCH
         CLI   #VERSIZE,2          NOSIZE REQUESTED?
         BE    VERP96              YES, BRANCH
         L     R0,#VERCLOG         GET LOGICAL RECORD COUNT
         CVD   R0,DOUBLE           CONVERT TO DECIMAL
         L     R1,ATTRCNT2
         LA    R1,1(,R1)             ADD ONE MORE
         ST    R1,ATTRCNT2
         OI    ATTRTOT2+7,X'0F'      INSURE VALID PACKED SIGN
         AP    ATTRTOT2(8),DOUBLE(8) ADD IN CURRENT VALUE
*        LA    R1,VERSUMM          VERIFY SUMMARY
         L     R1,AVERSUMM         VERIFY SUMMARY
         ST    R1,ADDRSUMR         SET FOR A LATTER CALL
         TM    #VERUPDT,4+2        SINGLE MEMBER CHECK?
         BO    VERP96              YES, BRANCH
         MVC   ATTRTOT2(8),DOUBLE  NO, RESET THE LRECL COUNT
         SPACE 2
VERP96   CLI   #MEMCNT,X'FF'       END OF DATA SET?
         BNE   VERP74              NO, BRANCH
         M$ERRST MSGBLANK
         MVI   MTLEN,44            LENGTH OF INSERT           DRK JUL16
         MVC   INSERT#1(44),DSNAME                            DRK JUL16
         M$MSG L006$1              YES, STATUS MESSAGE        DRK JUL16
         SPACE 2
VERP97   TM    #VERSTAT,2          STATISTICS DESIRED?
         BO    NEWCMD              NO, BRANCH
         TM    DS1SMSFG,DS1PDSE    PDSE DATASET?              DRK AUG05
         BNO   VERP97E             NO, CONTINUE               DRK AUG05
         TM    FLAGSAA,FMEMBER1+FMEMBER2  ALL MEMBERS?        DRK AUG05
         BNZ   VERP97E                    NO, BRANCH          DRK AUG05
         M$ERRST MSGBLANK                 YES, SIMULATE EOD   DRK AUG05
         MVI   MTLEN,44            LENGTH OF INSERT           DRK JUL16
         MVC   INSERT#1(44),DSNAME                            DRK JUL16
         M$MSG L006$1              EOD STATUS MESSAGE         DRK JUL16
VERP97E  DS    0H                                             DRK AUG05
         M$ERRST MSGBLANK
         MVI   MTLEN,10
         TM    FLAGSCC,RECFMU      RECFM=U?
         BO    VERP98              YES, BRANCH
         SPACE 1
         TM    FLAGSCC,RECFMV      RECFM=V?                   DRK DEC06
         BNO   VERP97F             NO, BRANCH                 DRK DEC06
         SPACE 1
         L     R2,#VERCBLK         NUMBER OF PHYSICAL BLOCKS INPUT
         LTR   R2,R2               ANY RECORDS?               DRK FEB09
         BNP   VERP98B             NO, BRANCH                 DRK FEB09
         SR    R0,R0                                          DRK FEB09
         L     R1,#VERCBLC         NUMBER OF CHARACTERS READ  DRK FEB09
         S     R1,=F'4'            SUBSTRACT 1 BDW LENGTH     DRK FEB09
         D     R0,#VERCLOG         AVERAGE CHARACTERS/LREC    DRK FEB09
         CVD   R1,DOUBLE                                      DRK FEB09
         MVC   INSERT#1(10),VERP206B                          DRK FEB09
         ED    INSERT#1(10),DOUBLE+4                          DRK FEB09
         M$MSG L108$1                                         DRK FEB09
         SPACE 1
         L     R0,#VERMAXV         MAXIMUM LRECL VALUE        DRK DEC06
         CVD   R0,DOUBLE                                      DRK DEC06
         MVC   INSERT#1(10),VERP206B                          DRK DEC06
         ED    INSERT#1(10),DOUBLE+4                          DRK DEC06
         M$MSG L109$1                                         DRK DEC06
         SPACE 1
VERP97F  DS    0H                                             DRK DEC06
         L     R0,#VERCLOG         NUMBER OF LOGICAL RECORDS INPUT
         CVD   R0,DOUBLE
         MVC   INSERT#1(10),VERP206B
         ED    INSERT#1(10),DOUBLE+4
         M$MSG L110$1
         SPACE 1
VERP98   L     R2,#VERCBLK         NUMBER OF PHYSICAL BLOCKS INPUT
         CVD   R2,DOUBLE
         MVC   INSERT#1(10),VERP206B
         ED    INSERT#1(10),DOUBLE+4
         M$MSG L111$1
         SPACE 1
         LTR   R2,R2               ANY RECORDS?
         BNP   VERP98B             NO, BRANCH
         L     R1,#VERMAX          MAXIMUM CHARACTERS/BLOCK
         CVD   R1,DOUBLE
         MVC   INSERT#1(10),VERP206B
         ED    INSERT#1(10),DOUBLE+4
         M$MSG L112$1
         SR    R0,R0
         L     R1,#VERCBLC         NUMBER OF CHARACTERS READ
         SRL   R2,1                AMOUNT FOR ROUNDING
         AR    R1,R2
         D     R0,#VERCBLK         AVERAGE CHARACTERS/BLOCK
         CVD   R1,DOUBLE
         MVC   INSERT#1(10),VERP206B
         ED    INSERT#1(10),DOUBLE+4
         M$MSG L113$1
         SPACE 1
VERP98B  TM    #VERUPDT,4+2        SINGLE MEMBER CHECK?
         BO    NEWCMD              YES, DONE
         LH    R0,DS1LSTAR         GET TT OF LAST TRACK USED
         S     R0,#VERCTRK         LESS THE AMOUNT AFTER THE COMPRESS
         BNM   *+6                 ZERO OR MORE, BRANCH
         SR    R0,R0               NEGATIVE, ERROR (USE ZERO)
         CVD   R0,DOUBLE           NUMBER OF WHOLE COMPRESSED TRACKS
         MVC   INSERT#1(10),VERP206B
         ED    INSERT#1(10),DOUBLE+4
         M$MSG L114$1
         B     VERP99B
         SPACE 2
VERP99   TM    #VERSTAT,2          STATISTICS DESIRED?
         BO    NEWCMD              NO, BRANCH
         TM    #VERUPDT,4+2        SINGLE MEMBER CHECK?
         BO    NEWCMD              YES, DONE
         M$ERRST MSGBLANK
         SPACE 1
VERP99B  MVI   MTLEN,10            TEN BYTES OF INSERTED MESSAGE
         L     R1,#MEMLAST         LAST MEMBER POINTER
         L     R2,#MEMPTR          START OF MEMBER LIST
         LA    R2,0(,R2)           CLEAR HIGH-ORDER DIGIT
         SR    R1,R2               (END+1 - START) * 16
         SRL   R1,4                DIVIDE BY 16
         CVD   R1,DOUBLE
         MVC   INSERT#1(10),VERP206B
         ED    INSERT#1(10),DOUBLE+4
         M$MSG L115$1
         M$ERRST MSGBLANK
         B     NEWCMD
         SPACE 2
VERMBR   ST    R2,#SAVEREG         SAVE REGISTER
         ST    R1,MSGTEXT1         SAVE FOR LATER OUTPUT
         LA    R1,MEMNAME          ASSUME DATA SET CHECK
         TM    #VERUPDT,4+2        MEMBER NAME CHECK?
         BNO   *+8                 NO, BRANCH
         LA    R1,DIRNAME          YES, USE DIRECTORY NAME
         CLC   #VERMEM(8),0(R1)    MEMBER NAME DISPLAYED?
         BE    VERMBR20            YES, BRANCH
         MVC   #VERMEM(8),0(R1)    MEMBER NAME DISPLAYED
         SPACE 1
         MVC   MSGTEXT2(136),MSGBL132
         MVC   MSGTEXT2+4(2),VERLSTAR
         MVC   MSGTEXT2+4+3(8),##SUBCOM
         TM    DSORG,DS1DSGPO      PARTITIONED?
         BNO   VERMBR10            NO, BRANCH
         M$ERRST MSGBLANK          ONE BLANK LINE
         MVC   MSGTEXT2+4+3+8+1(8),#VERMEM
         TR    MSGTEXT2+4+3+8+1(8),TRLINE
         MVC   DOUBLE(8),#VERMEM
         LA    R1,7
         LA    R14,DOUBLE+8
         SPACE 1
VERMBR02 BCTR  R14,0               SCAN
         CLI   0(R14),X'40'            BACKWARDS
         BNE   *+8                              FOR FIRST
         BCT   R1,VERMBR02                               NON-BLANK
         SPACE 1
         CLI   #VERMEM,C'0'        NUMERIC FIRST CHARACTER?
         BNL   VERMBR04            YES, NON-STANDARD
         TRT   DOUBLE(*-*),TRTMEM  <<EXECUTED>>
         EX    R1,*-6              VALID CHARACTERS?
         BZ    VERMBR10            YES, BRANCH
         SPACE 1
VERMBR04 MVC   MSGTEXT2+4+3+8+1+8+2(2),=C'X'''
         UNPK  MSGTEXT2+4+3+8+1+8+2+2(9),#VERMEM(5)
         UNPK  MSGTEXT2+4+3+8+1+8+2+2+8(9),#VERMEM+4(5)
         TR    MSGTEXT2+4+3+8+1+8+2+2(8+8),TRTABLE
         MVI   MSGTEXT2+4+3+8+1+8+2+2+8+8,C''''
         TM    #VERSTND,X'02'          STANDARD NAME CHECKING?
         BNZ   VERMBR10                NO, BRANCH
         MVC   MSGTEXT2+48(24),VERLNONS
         M$ERRST MSGTEXT2          MEMBER HEADER MESSAGE
         L     R2,#SAVEREG         RESTORE REGISTER
         BR    R2
         SPACE 1
VERMBR10 M$ERRST MSGTEXT2          MEMBER HEADER MESSAGE
         SPACE 1
VERMBR20 L     R1,MSGTEXT1         MESSAGE IDENTIFIER OR MESSAGE
         LTR   R1,R1               ANY MESSAGE?
         BZ    VERMBR30            NO, BRANCH
         M$MSG (R1)
VERMBR30 MVI   MTLEN,8             RESET THE MESSAGE LENGTH
         L     R2,#SAVEREG         RESTORE REGISTER
         BR    R2
         SPACE 2
VERCOMP  ST    R0,#VERCREG
         SPACE 1
VERCOMP2 L     R0,#VERCREG
         L     R1,#VERCREC
         A     R1,=F'1'            CURRENT COMPRESS RECORD
         ST    R1,#VERCREC
         ICM   R0,B'1000',#VERCREC+3  RECORD NUMBER IN RKDD (R OF RKDD)
         SPACE 1
         PUSH  ACONTROL                                       DRK MAY08
         ACONTROL FLAG(NOPAGE0)                               DRK MAY08
         TRKCALC FUNCTN=TRKBAL,TYPE=BYTEUCB+1,REGSAVE=YES,             X
               BALANCE=#VERCBAL+2,RKDD=(0),MF=(E,PARMLIST)
         POP   ACONTROL                                       DRK MAY08
         SPACE 1
         LTR   R15,R15             FIT ON CURRENT TRACK?
         BZ    VERCOMPX            YES, BRANCH
         SPACE 1
         L     R1,#VERCTRK
         A     R1,=F'1'            START A NEW TRACK IN OUTPUT
         ST    R1,#VERCTRK
         MVI   #VERCREC+3,0        RECORD ZERO
         B     VERCOMP2            TRY AGAIN
         SPACE 1
VERCOMPX ST    R0,#VERCBAL         SAVE THE TRACK BALANCE
         BR    R2
         SPACE 3
VERLSTAR DC    C'**'
VERLNONS DC    CL24'NON-STANDARD MEMBER NAME'
VERP2020 DC    X'4020206B20212040'
VERP206B DC    X'40206B2020206B202120'
VERRENT  DC    C'RENT'
VERREUS  DC    C'REUS'
VERREFR  DC    C'REFR'
VERSCTR  DC    C'SCTR'
AVERS00  DC    A(VERS00)
AVERSUMM DC    A(VERSUMM)
         SPACE 3
VERAD00  CLI   #VERSIZE,2            NOSIZE REQUESTED?
         BER   R14                   YES, BRANCH
         SR    R0,R0
         ICM   R0,B'0111',DIRSTORE   LOAD MODULE
         CVD   R0,DOUBLE             CONVERT TO DECIMAL
*        LA    R1,VERSUMM            VERIFY SUMMARY
         L     R1,AVERSUMM           VERIFY SUMMARY
         ST    R1,ADDRSUMR           SET FOR A LATTER CALL
         TM    DIRATTR3,DIRRMANY     RMODE ANY?
         L     R1,=A(VERAD10)
         BOR   R1                    YES, BRANCH
*        BO    VERAD10               YES, BRANCH
         L     R1,ATTRCNT2
         LA    R1,1(,R1)             ADD ONE MORE
         ST    R1,ATTRCNT2
         OI    ATTRTOT2+7,X'0F'      INSURE VALID PACKED SIGN
         AP    ATTRTOT2(8),DOUBLE(8) ADD IN CURRENT VALUE
         BR    R14
VERAD10  L     R1,ATTRCNTA
         LA    R1,1(,R1)             ADD ONE MORE
         ST    R1,ATTRCNTA
         OI    ATTRTOTA+7,X'0F'      INSURE VALID PACKED SIGN
         AP    ATTRTOTA(8),DOUBLE(8) ADD IN CURRENT VALUE
         BR    R14
         SPACE 2
***      SUMMARIZATION ROUTINE TO WRITE PDS117I MESSAGE
         USING *,R8
VERSUMM  XC    ADDRSUMR,ADDRSUMR   CLEAR THE SUMMARY ADDRESS
         CLI   #VERSIZE,2          NOSIZE REQUESTED?
         BE    VERU90              YES, BRANCH
         L     R1,ATTRCNT2
         A     R1,ATTRCNTA
         C     R1,=F'0'            ONE OR MORE ENTRIES?       DRK APR14
         BNH   VERU90              NO, BRANCH
         MVI   MTLEN,16            GET LENGTH
         MVI   MTLEN+4,16
         M$ERRST MSGBLANK          ONE BLANK LINE
         OI    ATTRTOT2+7,X'0F'    INSURE VALID PACKED SIGN
         L     R1,ATTRCNT2         COUNT OF MODULES
         CVD   R1,DOUBLE           IN PACKED FORM
         BAS   R14,VERU99          GET THE FIRST COUNT
         MVC   INSERT#1(16),0(R1)  MOVE INTO COUNT AREA
         MVC   DOUBLE(8),ATTRTOT2  MOVE INTO COMMON AREA
         TM    FLAGSCC,RECFMU      LOAD MODULES?
         BNO   VERU10              NO, BRANCH
         AP    DOUBLE(8),VERSUFFF  ADD 1023
         DP    DOUBLE(8),VERSU1K   DIVIDE BY 1024
         ZAP   ATTRTOT2(8),DOUBLE(5) MOVE IN QUOTIENT
         MVC   DOUBLE(8),ATTRTOT2  MOVE INTO COMMON AREA
VERU10   BAS   R14,VERU99          CONVERT SIZE
         MVC   INSERT#2(16),0(R1)  MOVE IN SIZE PARAMETER
         TM    FLAGSCC,RECFMU      LOAD MODULES?
         BO    VERU20              YES, BRANCH
         M$MSG L117$2
         B     VERU90
VERU20   ICM   R1,B'1111',ATTRCNT2 ANY COUNT?
         BZ    VERU25              NO, BRANCH
         M$MSG L118$2
VERU25   OI    ATTRTOTA+7,X'0F'    INSURE VALID PACKED SIGN
*
         ICM   R1,B'1111',ATTRCNTA ANY COUNT?
         BZ    VERU90              NO, BRANCH
         L     R1,ATTRCNTA         COUNT OF MODULES
         CVD   R1,DOUBLE           IN PACKED FORM
         BAS   R14,VERU99          GET THE FIRST COUNT
         MVC   INSERT#1(16),0(R1)  MOVE INTO COUNT AREA
         MVC   DOUBLE(8),ATTRTOTA  MOVE INTO COMMON AREA
         AP    DOUBLE(8),VERSUFFF  ADD 1023
         DP    DOUBLE(8),VERSU1K   DIVIDE BY 1024
         ZAP   ATTRTOTA(8),DOUBLE(5) MOVE IN QUOTIENT
         MVC   DOUBLE(8),ATTRTOTA  MOVE INTO COMMON AREA
         BAS   R14,VERU99          CONVERT SIZE
         MVC   INSERT#2(16),0(R1)  MOVE IN SIZE PARAMETER
         M$MSG L119$2
*
VERU90   M$ERRST MSGBLANK                 ONE BLANK LINE
         XC    ATTRCNT2(4+4),ATTRCNT2     COUNTS
         XC    ATTRTOT2(8+8),ATTRTOT2     TOTALS
         MVI   MTLEN,8                    RESET LENGTH
         MVI   MTLEN+4,8                  RESET LENGTH
         BR    R2
VERU99   DS    0H
         MVC   INSERT#2+16(15),VERSUMMP EDIT PICTURE
         MVC   INSERT#2+16+15(16),BLANK128
         ED    INSERT#2+16(15),DOUBLE+2
         LA    R1,INSERT#2+16
         LA    R1,1(,R1)   <---+
         CLI   0(R1),X'40'     |
         BE    *-8         ----+
         BR    R14
         SPACE 2
*                 1 2 3 4 5 6 7 8 9 0 1 2 3 4 5
*                   1 2 , 3 4 5 , 6 7 8 , 9 0 1
VERSUMMP DC    X'4020206B2020206B2020206B202120'
VERSUFFF DC    PL3'1023'
VERSU1K  DC    PL3'1024'
         SPACE 3
VERS00   BASR  R8,0
         USING *,R8
         OI    ##ADRPA#,$Q         ENTER QUIET MODE PROCESSING
         SPACE 1
         MVC   MEMNAME(8),BLANKS   INITIALIZE THE MEMBER NAME
         MVC   DSNMEMQ(8),MEMNAME  MEMBER NAME TO CHECK
         BAS   R2,ENQMTEST         MEMBER IN USE?
         B     *+8                 YES, BRANCH
         B     VERS40              NO, BRANCH
         MVC   INSERT#1(8),INSERT#2  JOBNAME
         M$MSG L851$1
         SPACE 2
VERS40   CLI   #VERREAD,X'02'      NOINPUT REQUESTED?
         BE    VERS99              YES, BRANCH
         SPACE 2
         MVI   #MEMPTR,0           CLEAR THE MEMBER FLAG
         XC    #MEMCNT(4),#MEMCNT  COUNT OF INPUT LOGICAL RECORDS
         MVI   STARTTR+2,X'01'     TTR=000001 (START OF DATA SET)
         SPACE 2
VERS76   L     R15,=V(EXCP)
         BASR  R14,R15
         B     *+4(R15)            PROCESS RETURN CODE
         B     VERS80                00 - SUCCESSFUL READ
         B     VERS90                04 - END OF FILE
         B     VERS92                08 - END OF DATA SET
         B     VERS78                12 - I/O ERROR
         SPACE 2
*  PERMANENT I/O ERROR  -- SIMULATE END OF DATA SET
VERS78   MVI   MTLEN,6             LENGTH OF THE TTR INSERT
*        MVC   INSERT#1(6),INSERT#1        FIRST PARAMETER
         M$MSG L991$1                      OUTPUT AN ERROR MESSAGE
         MVI   MTLEN,8                     RESET TO EIGHT
         B     VERS97
         SPACE 2
VERS80   LR    R3,R0               BUFFER START
         L     R5,LS               BLOCK LENGTH
         C     R5,#VERMAX          GREATER THAN MAXIMUM?
         BNH   *+8                 NO, BRANCH
         ST    R5,#VERMAX          YES, SAVE NEW MAXIMUM
         L     R0,#VERCBLK         NUMBER
         A     R0,=F'1'                  OF PHYSICAL
         ST    R0,#VERCBLK                          BLOCKS
         L     R0,#VERCBLC         NUMBER
         AR    R0,R5                     OF PHYSICAL
         ST    R0,#VERCBLC                          CHARACTERS
         SPACE 1
         CH    R5,#MAXBLKV         EXCEED MAXBLK?
         BNH   VERS81              YES, BRANCH
         OC    #MAXBLKV(2),#MAXBLKV  ANY MAXBLK()?
         BZ    VERS80A               NO, BRANCH
         TM    #MEMPTR,#MEMBLKE    BLKSIZE SIZE ERROR NOTED?
         BO    VERS81              YES, BRANCH
         OI    #MEMPTR,#MEMBLKE    BLKSIZE ERROR NOTED NOW
         CVD   R5,DOUBLE
         MVC   INSERT#1(8),VERS2020
         ED    INSERT#1(7),DOUBLE+5
         M$MSG L806$1              BLKSIZE ERROR
         SPACE 1
VERS80A  CH    R5,BLKSI            BLKSIZE VALID?
         BNH   VERS81              YES, BRANCH
         TM    #MEMPTR,#MEMBLKE    BLKSIZE SIZE ERROR NOTED?
         BO    VERS81              YES, BRANCH
         OI    #MEMPTR,#MEMBLKE    BLKSIZE ERROR NOTED NOW
         CVD   R5,DOUBLE
         MVC   INSERT#1(8),VERS2020
         ED    INSERT#1(7),DOUBLE+5
         M$MSG L812$1
         SPACE 1
VERS81   TM    FLAGSCC,RECFMF      FIXED FORMAT?
         BNO   VERS82              NO, BRANCH
         LH    R14,LRECL           CURRENT LRECL
         TM    #MEMPTR,#MEMDIVE    BLKSIZE/LRECL ERROR NOTED?
         BO    VERS82              YES, BRANCH
         LR    R0,R5               BLOCK LENGTH
         SRDA  R0,32
         DR    R0,R14              BLKSIZE/LRECL
         LTR   R0,R0               ANY REMAINDER?
         BZ    VERS82              YES, BRANCH
         OI    #MEMPTR,#MEMDIVE    BLKSIZE/LRECL ERROR NOTED NOW
         CVD   R5,DOUBLE
         MVC   INSERT#1(8),VERS2020
         ED    INSERT#1(7),DOUBLE+5
         M$MSG L814$1
         LH    R14,LRECL           CURRENT LRECL
         SPACE 1
VERS82   LR    R1,R5
         AR    R5,R3               END OF BUFFER
         BCTR  R5,0                END OF BUFFER -1
         TM    FLAGSCC,RECFMF      RECFM=F?
         LH    R4,LRECL
         BO    VERS88              YES, BRANCH
         TM    FLAGSCC,RECFMU      RECFM=U?
         LR    R4,R1
         BO    VERS88              YES, BRANCH
         TM    FLAGSCC,RECFMV      RECFM=V?
         BNO   VERS88              NO, ASSUME RECFM=U
         AH    R3,=H'4'            SKIP BLOCK LENGTH WORD
         B     VERS86
         SPACE 3
         CNOP  0,8                 ALIGN FOR LOOP
VERS84   BXLE  R3,R4,VERS86        GET THE NEXT LOGICAL RECORD
         B     VERS76              END OF BLOCK, BRANCH
         SPACE 2
VERS86   TM    FLAGSCC,RECFMV      RECFM=V?
         BNO   VERS88              NO, BRANCH
         SR    R4,R4
         ICM   R4,B'0011',0(R3)    RECORD LENGTH +4
         LR    R14,R4              RECORD LENGTH +4
         MVC   FULLWORD(1),2(R3)   SEGMENT CONTROL CODE (SDW) DRK NOV13
         C     R14,#VERMAXV        GREATER THAN MAX?          DRK DEC06
         BNH   *+8                 NO                         DRK DEC06
         ST    R14,#VERMAXV        YES, SAVE NEW MAXIMUM      DRK DEC06
         AH    R3,=H'4'            SKIP RECORD LENGTH WORD
         SH    R4,=H'4'            RECORD LENGTH VALID?
         BP    VERS88              YES, BRANCH
         TM    #MEMPTR,#MEMLRL0    LRECL ERROR NOTED?
         BO    VERS76              YES, BRANCH
         OI    #MEMPTR,#MEMLRL0    LRECL ERROR NOTED
         MVI   MTLEN,1             LENGTH OF MESSAGE
         LA    R14,240(,R14)       RECORD LENGTH IN DISPLAY
         STC   R14,INSERT#1        PLACE IN THE MESSAGE
         M$MSG L811$1
         MVI   MTLEN,8                     RESET TO EIGHT
         B     VERS76              YES, BRANCH
         SPACE 2
VERS88   L     R1,#MEMCNT          ANOTHER
         LA    R1,1(,R1)                  OUTPUT
         ST    R1,#MEMCNT                       RECORD
         SPACE 1                                              DRK NOV13
         TM    RECFM,X'48'         RECFM=VS                   DRK NOV13
         BNO   VERS88A             NO, COUNT                  DRK NOV13
         CLI   FULLWORD,2          0=COMPLETE, 1=FIRST SEGMENT?       "
         BNL   VERS88B             NO, DON'T COUNT AS LREC    DRK NOV13
VERS88A  DS    0H                                             DRK NOV13
         L     R0,#VERCLOG         NUMBER
         A     R0,=F'1'                  OF LOGICAL
         ST    R0,#VERCLOG                         RECORDS
VERS88B  DS    0H                                             DRK NOV13
         CH    R14,LRECL           VALID LRECL VALUE?
         BNH   VERS84              YES, BRANCH
         TM    FLAGSCC,RECFMU      RECFM=U?
         BO    VERS84              YES, BRANCH
         TM    #MEMPTR,#MEMLRLE    LRECL ERROR NOTED?
         BO    VERS84              YES, BRANCH
         OI    #MEMPTR,#MEMLRLE    LRECL ERROR HAS BEEN NOTED
         CVD   R14,DOUBLE
         MVC   INSERT#1(8),VERS2020
         ED    INSERT#1(7),DOUBLE+5
         M$MSG L813$1
         B     VERS84
         SPACE 2
VERS90   M$ERRST MSGBLANK
         M$MSG L005                       END OF FILE
         M$ERRST MSGBLANK
         B     VERS76                     LOOP
         SPACE 2
VERS92   M$ERRST MSGBLANK
         MVI   MTLEN,44            LENGTH OF INSERT           DRK JUL16
         MVC   INSERT#1(44),DSNAME                            DRK JUL16
         M$MSG L006$1              END OF DATA SET            DRK JUL16
         M$ERRST MSGBLANK                                     DRK JUL16
         SPACE 2
VERS97   MVI   MTLEN,10
         TM    FLAGSCC,RECFMU      RECFM=U?
         BO    VERS98              YES, BRANCH
         SPACE 1
         TM    FLAGSCC,RECFMV      RECFM=V?                   DRK DEC06
         BNO   VERS97F             NO, BRANCH                 DRK DEC06
         SPACE 1
         L     R2,#VERCBLK         NUMBER OF PHYSICAL BLOCKS INPUT
         LTR   R2,R2               ANY RECORDS?               DRK FEB09
         BNP   VERS99              NO, BRANCH                 DRK FEB09
         SR    R0,R0                                          DRK FEB09
         L     R1,#VERCBLC         NUMBER OF CHARACTERS READ  DRK FEB09
         S     R1,=F'4'            SUBSTRACT 1 BDW LENGTH     DRK FEB09
         D     R0,#VERCLOG         AVERAGE CHARACTERS/LREC    DRK FEB09
         CVD   R1,DOUBLE                                      DRK FEB09
         MVC   INSERT#1(10),VERS206B                          DRK FEB09
         ED    INSERT#1(10),DOUBLE+4                          DRK FEB09
         M$MSG L108$1                                         DRK FEB09
         SPACE 1
         L     R0,#VERMAXV         MAXIMUM LRECL VALUE        DRK DEC06
         CVD   R0,DOUBLE                                      DRK DEC06
         MVC   INSERT#1(10),VERS206B                          DRK DEC06
         ED    INSERT#1(10),DOUBLE+4                          DRK DEC06
         M$MSG L109$1                                         DRK DEC06
         SPACE 1
VERS97F  DS    0H                                             DRK DEC06
         L     R0,#VERCLOG         NUMBER OF LOGICAL RECORDS INPUT
         CVD   R0,DOUBLE
         MVC   INSERT#1(10),VERS206B
         ED    INSERT#1(10),DOUBLE+4
         M$MSG L110$1              NO
         TM    RECFM,X'48'         RECFM=VS?                  DRK NOV13
         BNO   VERS98              NO                         DRK NOV13
         L     R0,#MEMCNT          NUMBER OF SPANNED SEGMENT RECS     "
         CVD   R0,DOUBLE                                      DRK NOV13
         MVC   INSERT#1(10),VERS206B                          DRK NOV13
         ED    INSERT#1(10),DOUBLE+4                          DRK NOV13
         M$MSG L121$1                                         DRK NOV13
         SPACE 1
VERS98   L     R2,#VERCBLK         NUMBER OF PHYSICAL BLOCKS INPUT
         CVD   R2,DOUBLE
         MVC   INSERT#1(10),VERS206B
         ED    INSERT#1(10),DOUBLE+4
         M$MSG L111$1
         SPACE 1
         LTR   R2,R2               ANY RECORDS?
         BNP   VERS99              NO, BRANCH
         L     R1,#VERMAX          MAXIMUM CHARACTERS/BLOCK
         CVD   R1,DOUBLE
         MVC   INSERT#1(10),VERS206B
         ED    INSERT#1(10),DOUBLE+4
         M$MSG L112$1
         SR    R0,R0
         L     R1,#VERCBLC         NUMBER OF CHARACTERS READ
         SRL   R2,1                AMOUNT FOR ROUNDING
         AR    R1,R2
         D     R0,#VERCBLK         AVERAGE CHARACTERS/BLOCK
         CVD   R1,DOUBLE
         MVC   INSERT#1(10),VERS206B
         ED    INSERT#1(10),DOUBLE+4
         M$MSG L113$1
         B     NEWCMD
         SPACE 2
VERS99   M$ERRST MSGBLANK
         LA    R1,L116
         B     MSGNEW
VERS2020 DC    X'4020206B20212040'
VERS206B DC    X'40206B2020206B202120'
         SPACE 1
VERRM64  DS    0H
         USING *,R15
         ST    R14,R14SAVE                 M$MSG USES R14     DRK APR18
         LA    R1,=C'ANY'                  ASSUME AMODE IS ANY        "
         TM    MODESAVE,DIRAM64+DIRAM31 03 AMODE ANY?         DRK APR18
         BO    VERRM64A                    YES, INVALID       DRK APR18
         LA    R1,=C'31 '                  ASSUME AMODE IS 31         "
         TM    MODESAVE,DIRAM31         02 AMODE 31?          DRK APR18
         BO    VERRM64A                    YES, INVALID       DRK APR18
         TM    MODESAVE,DIRAM64         01 AMODE 64?          DRK APR18
         BO    VERRM64B                    YES, VALID         DRK APR18
         LA    R1,=C'24 '               00 AMODE IS 24        DRK APR18
VERRM64A DS    0H
         MVI   MTLEN,3                     MESSAGE LENGTH
         MVC   INSERT#1(3),0(R1)           MOVE IN THE AMODE TEXT
         LA    R1,L879$1A                  RMODE ANY/AMODE CONFLICT
         BAS   R2,VERMBR64                 OUTPUT AN ERROR MESSAGE
VERRM64B DS    0H
         L     R14,R14SAVE                 RESTORE R14        DRK APR18
         BR    R14                         RETURN             DRK APR18
L879$1A  DC    C'879'                                         DRK APR18
         SPACE 2
VERMBR64 DS    0H                                             DRK APR18
         ST    R2,#SAVEREG         SAVE REGISTER
         ST    R1,MSGTEXT1         SAVE FOR LATER OUTPUT
         LA    R1,MEMNAME          ASSUME DATA SET CHECK
         TM    #VERUPDT,4+2        MEMBER NAME CHECK?
         BNO   *+8                 NO, BRANCH
         LA    R1,DIRNAME          YES, USE DIRECTORY NAME
         CLC   #VERMEM(8),0(R1)    MEMBER NAME DISPLAYED?
         BE    VERRM64$            YES, BRANCH
         MVC   #VERMEM(8),0(R1)    MEMBER NAME DISPLAYED
         MVC   MSGTEXT2(136),MSGBL132
         MVC   MSGTEXT2+4(2),=C'**'
         MVC   MSGTEXT2+4+3(8),##SUBCOM
         MVC   MSGTEXT2+4+3+8+1(8),#VERMEM
         TR    MSGTEXT2+4+3+8+1(8),TRLINE
         M$ERRST MSGBLANK          ONE BLANK LINE
         M$ERRST MSGTEXT2          MEMBER HEADER MESSAGE
VERRM64$ DS    0H
         L     R1,MSGTEXT1         MESSAGE IDENTIFIER OR MESSAGE
         M$MSG (R1)
         MVI   MTLEN,8             RESET THE MESSAGE LENGTH
         L     R2,#SAVEREG         RESTORE REGISTER
         BR    R2
         DROP  R15
