         TITLE 'P D S  --  PDS DELETE, MEMBERS               09/28/88'
***********************************************************************
***      DELETE SUBCOMMAND  MODIFIED BY BRUCE LELAND -- OCT., 1983  ***
***                                                                 ***
***      MEMBERS SUBCOMMAND MODIFIED BY BRUCE LELAND -- JUNE, 1985  ***
***                                                                 ***
***      SUBMIT  SUBCOMMAND                                         ***
***********************************************************************
*
         SPACE 1
DELETE   CSECT
         USING *,R8
         CLC   ##SUBCOM(8),$MEM          MEMBERS SUBCOMMAND?
         BNE   *+8                       NO, BRANCH
         OI    #MEMFLAG,X'80'            YES, SET A FLAG
         CLC   ##SUBCOM(8),$SUB          SUBMIT SUBCOMMAND?
         BNE   DELE001                   NO, BRANCH
         MVC   WORKTBL(120),MSGLINE      SAVE THE OPERAND     DRK SEP02
         CLI   ##SUBCAL,C'%'             IMPLIED CLIST?
         BE    DELE998                   YES, BRANCH
         TM    DSORG,DS1DSGPO            PARTITIONED?
         BNO   DELE998                   NO, BRANCH
         OI    #MEMFLAG,X'40'            YES, SET A SUBMIT FLAG
         SPACE 1
DELE001  LA    R1,L530                   ASSUME PARTITIONED
         TM    DSORG,DS1DSGPO            CORRECT?
         BNO   MSGNEW                    NO, BRANCH
         SPACE 2
DELE002  TM    FLAGSII,FSINGLE+FMEMBERS  IF OR FIND THEN(DEL/MEM/SUB)?
         BZ    DELE010                   NO, BRANCH
         TM    #MEMFLAG,X'80'            MEMBERS?
         BO    NEWCMD                    YES, DONE
         TM    #MEMFLAG,X'40'            SUBMIT?
         BO    DELE008                   YES, BRANCH
         MVC   IODIR1+4+00(8),CURDIR     RESET READ ADDRESS
         MVI   IODIR1+4+08,X'08'         RESET KEY LENGTH
         MVC   IODIR1+4+09(2),=H'256'    RESET DATA LENGTH
         MVI   IODIR1+4+11,0             RESET SECTOR ADDRESS TO ZERO
         MVI   IOECB,X'7F'               LAST READ WAS OK
DELE008  LA    R4,DIRNAME                DUMMY ARRAY ENTRY
         ST    R4,#MEMPTR
         LA    R4,16(,R4)                POSITION FOR ONE ENTRY
         TM    #MEMFLAG,X'40'            SUBMIT?
         BO    DELE998                   YES, BRANCH
         B     DELE100                   PROMPT BY DEFAULT
         SPACE 1
DELE010  MVC   MEMNAME(12),DIRNAME   ENSURE MEMBER NAME IS PRESENT
         MVC   #DELSAVE(1),FLAGSAA   SAVE FLAGSAA
         TM    FLAGSAA,FMEM#MEM      MEMBER GROUP?
         BO    DELE200               YES, BRANCH
         MVI   #DELSAVE,FF-FMEM#MEM  MARK MEMBER AS ADDED
         TM    SPFLAG2,SPFLNCD       LINE COMMAND?
         BO    DELE020               YES, BRANCH
         TM    PMEMMIN,X'80'         MEMBER LIST?
         BO    DELE200               YES, BRANCH
         SPACE 1
DELE020  OI    #DELFLAG,#DELONE      ONLY A SINGLE MEMBER
         TM    #DELFLAG,#DELASS2     DELETE ASSOCIATES TOO?
         BO    DELE200               YES, BRANCH
         SPACE 1
         LA    R4,DIRNAME            DUMMY ARRAY ENTRY
         ST    R4,#MEMPTR
         LA    R4,16(,R4)            POSITION FOR ONE ENTRY
         SPACE 1
         TM    #MEMFLAG,X'80'+X'40'  MEMBER/SUBMIT SUBCOMMAND?
         BZ    DELE090               NO, BRANCH
         TM    FLAGSHH,FMEMSTAR      ASTERISK NOTATION?
         BNO   DELE080               NO, BRANCH
         MVC   INSERT#1(8),MEMBER1
         M$MSG L002$1                WHO IS BEING PROCESSED
DELE080  TM    #MEMFLAG,X'40'        SUBMIT SUBCOMMAND?
         BO    DELE998               YES, BRANCH
         B     NEWCMD                NO, MUST BE MEMBERS, QUIT
         SPACE 2
DELE090  TM    FLAGSHH,FMEMSTAR      ASTERISK NOTATION?
         BO    DELE110               YES, NO PROMPTING
         TM    FLAGSHH,FOPTIONS      ANY MEMBER NAME?
         BO    DELE110               YES, NO PROMPTING
         SPACE 2
DELE100  LA    R1,PDS393A            DELETE SINGLE MEMBER
         BAS   R2,YESNO              PROMPT FOR YES OR NO
         B     NEWCMD                NO -- DONE
         SPACE 1
DELE110  MVC   DSNMEMQ(8),DIRNAME    MEMBER NAME
         BAS   R2,ENQMTEST           MEMBER IN USE?
         B     NEWCMD                YES, IGNORE THE COMMAND
         B     DELE900               NO, DELETE IT
         SPACE 3
DELE200  OI    FLAGSII,FDIRGRP       SIMULATE MEMBER GROUP INPUT
         NI    FLAGSAA,FF-FMEM#MEM   TURN OFF MEMBER GROUPS NOW
         OI    FLAGSBB,FLINESET      LINE NOW IN PROGRESS
         LA    R1,80                 ASSUME AN ACTIVE MODE
         TM    SPFLAG0,SPFDON        ISPMODE ACTIVE?
         BO    DELE202               YES, BRANCH
         TM    CONTOPTN,1            ANY LOG RECORDING?
         BO    DELE202               YES, BRANCH
*        GTSIZE ,                    TERMINAL SIZE             SS MAY89
         GTSIZE
         CH    R1,=H'120'            120 OR LESS BYTES?
         BL    *+8                   YES, BRANCH
         LH    R1,=H'120'            NO, USE 120 BYTES
DELE202  SH    R1,=H'7'              LESS SEVEN FOR PREFIX
         ST    R1,LINESIZE           CHARACTERS/LINE
         MVC   MSGLINE+4(L'MSGDELET),MSGDELET
         LA    R6,MSGLINE+4+L'MSGDELET
         MVC   FULLWORD(3),L162$1    MEMBERS TO BE DELETED
         NI    FLAGSBB,FF-FLINESET   NO LINE IN PROGRESS
         MVI   SUBPOOLT,21           SUBPOOL 21 DATA
         LA    R5,256/2              256 MEMBERS INITIALLY
         TM    #MEMFLAG,X'80'+X'40'  MEMBER/SUBMIT SUBCOMMAND?
         BZ    DELE210               NO, BRANCH
         MVC   MSGLINE+4(L'MSGMEMBS),MSGMEMBS
         LA    R6,MSGLINE+4+L'MSGMEMBS
         MVC   FULLWORD(3),L165$1    MEMBERS ARE:
         SPACE 1
DELE210  SLL   R5,1                  MEMBERS*2 FOR EACH LOOP
         LR    R3,R5
         LR    R0,R5
         SLL   R0,4                  TABLE SIZE=MEMBERS*16
         ICM   R0,B'1000',SUBPOOLT
         GETMAIN R,LV=(0)
         LR    R4,R1                 NEW TABLE ADDRESS
         ICM   R0,B'1111',#MEMPTR    ANY PREVIOUS MEMBERS?
         BNZ   DELE220               YES, BRANCH
         ST    R4,#MEMPTR            INITIALIZE MEMBER BASE
         B     DELE500
         SPACE 1
DELE220  SRL   R3,1                  SECOND HALF OF TABLE (# MEMBERS)
         LR    R14,R4                NEW TABLE START ADDRESS
         LR    R15,R5
         SLL   R15,3                 OLD TABLE SIZE=MEMBERS*8
         LR    R2,R15                START OF NEW PART
         LR    R1,R15
         MVCL  R14,R0                PRESERVE THE OLD TABLE
         L     R1,#MEMPTR
         LR    R0,R2                 LENGTH TO FREE
         ICM   R0,B'1000',SUBPOOLT   SUBPOOL TO FREE
         FREEMAIN R,LV=(0),A=(1)
         ST    R4,#MEMPTR            START OF NEW TABLE
         AR    R4,R2                 WHERE TO ADD MEMBERS
         SPACE 3
DELE300  TM    #DELFLAG,#DELASSP     ASSOCIATES PHASE?
         BO    DELE320               YES, BRANCH
         TM    #DELSAVE,FMEM#MEM     MEMBER GROUP?
         BO    DELE320               YES, BRANCH
         TM    #DELSAVE,FF-FMEM#MEM  MEMBER ALREADY ADDED?
         MVI   #DELSAVE,FF-FMEM#MEM  MARK MEMBER AS ADDED
         BNO   DELE500               NO, BRANCH
         L     R1,PMEMCURR           CURRENT LIST ELEMENT
         TM    0(R1),X'80'           ANOTHER ELEMENT?
         BNO   DELE605               NO, END OF LIST
         MVC   FLAGSAA(1),4(R1)      NEXT DESCRIPTOR FLAGS
         MVC   LMEMBER1+1(19),5(R1)  LEN1, MEMBER1, LEN2, MEMBER2
         L     R1,0(,R1)             NEXT ELEMENT POINTER
         ST    R1,PMEMCURR
         MVC   #DELSAVE(1),FLAGSAA   SAVE THE MEMBER DESCRIPTOR
         MVC   MEMNAME(8),MEMBER1    FIRST MEMBER NAME
         NI    FLAGSAA,FF-FMEM#MEM   TURN OFF MEMBER GROUP
         TM    #DELSAVE,FMEM#MEM     MEMBER GROUP?
         BO    DELE319               YES, BRANCH
         MVI   #DELSAVE,FF-FMEM#MEM  MARK MEMBER AS ADDED
         MVC   DIRNAME,MEMBER1         FIRST PART OF DSNAME2
         SPACE 2
         BLDL  INDCB,BLDLLIST          LOCATE DIRECTORY ENTRY
         B     *+4(R15)                PROCESS RETURN CODE
         B     DELE315                   00 - SUCCESSFUL
         B     DELE310                   04 - MEMBER NOT FOUND
         B     IOERROR                   08 - I/O ERROR
         SPACE 1
DELE310  MVC   INSERT#1(8),DIRNAME
         M$MSG L853$1                MSG - (MEMBER) NOT FOUND
         B     DELE300
         SPACE 1
DELE315  MVC   MEMNAME(12),DIRNAME   FIRST MEMBER NAME
         B     DELE500
         SPACE 1
DELE319  MVI   STARTTR+2,X'01'       REREAD THE DIRECTORY
         SPACE 1
DELE320  BAS   R14,READDIR           GET NEXT DIRECTORY BLOCK
         B     DELE600               END OF MEMBERS -- BRANCH
         SPACE 1
         TM    #DELFLAG,#DELASSP     ASSOCIATES PHASE?
         BO    DELE400               YES, BRANCH
         SPACE 1
         BAS   R2,MFILTER            CHECK MEMBER NAMES
         B     DELE600                 00 - SIMULATE DIRECTORY END
         B     DELE300                 04 - MEMBER FAILED FILTERING
         B     DELE500                 08 - MEMBER PASSED FILTERING
         SPACE 3
DELE400  L     R1,#MEMPTR            START OF MEMBER LIST
         B     DELE410+4             SKIP FIRST ADD
DELE410  LA    R1,16(,R1)            NEXT TABLE ENTRY
         CR    R1,R4                 END OF TABLE?
         BNL   DELE300               YES, IGNORE THIS ENTRY
         CLC   8(3,R1),MEMTTR        MATCHING TTR?
         BNE   DELE410               NO, BRANCH
         SPACE 3
         L     R1,#MEMPTR            START OF MEMBER LIST
         B     DELE430+4             SKIP FIRST ADD
DELE430  LA    R1,16(,R1)            NEXT TABLE ENTRY
         CR    R1,R4                 END OF TABLE?
         BNL   DELE500               YES, ADD THIS ENTRY
         CLC   0(8,R1),MEMNAME       FIND MYSELF?
         BE    DELE300               YES, IGNORE THIS ENTRY
         B     DELE430               NO, CONTINUE SEARCHING
         SPACE 3
DELE500  TM    #MEMFLAG,X'80'+X'40'  MEMBER/SUBMIT SUBCOMMAND?
         BNZ   DELE505               YES, BRANCH
         MVC   DSNMEMQ(8),MEMNAME
         BAS   R2,ENQMTEST           MEMBER IN USE?
         OI    #DELFLAG,#DELENQ      YES, MARK TO EXIT LATER
         SPACE 1
DELE505  L     R1,#MEMCNTM
         LA    R1,1(,R1)
         ST    R1,#MEMCNTM           ONE MORE MEMBER IN THIS GROUP
         LA    R2,MEMNAME+7
         TM    FLAGSBB,FLINESET      LINE IN PROGRESS?
         BO    DELE510               YES, BRANCH
         OI    FLAGSBB,FLINESET      LINE NOW IN PROGRESS
DELE510  CLI   0(R2),X'40'           SCAN
         BNE   *+8                       FOR LAST
         BCT   R2,DELE510                        NON-BLANK
         LA    R0,MEMNAME
         SR    R2,R0
         BNM   *+6
         SR    R2,R2
         LA    R15,2(R6,R2)
         LA    R1,MSGLINE
         SR    R15,R1
         C     R15,LINESIZE          FIT ON THIS OUTPUT LINE?
         BNH   DELE520               YES, BRANCH
         LA    R1,MSGLINE+4
         SR    R6,R1
         STC   R6,MTLEN
         MVC   INSERT#1(127),MSGLINE+4
         LA    R1,FULLWORD
         M$MSG (R1)
         MVI   MTLEN,8
         MVI   FULLWORD+2,C'9'       MESSAGE L169             DRK MAY08
         LA    R6,MSGLINE+3          INDENT FOR BLANKS
         SPACE 1
DELE520  MVI   0(R6),X'40'
         MVC   1(8,R6),MEMNAME
         TR    1(8,R6),TRLINE        MAKE PRINTABLE
         LA    R1,2(R2,R6)
         MVI   0(R1),C','            ADD A COMMA
         LA    R6,3(R2,R6)
         MVC   0(16,R4),MEMNAME      ADD TO THE TABLE
         LA    R4,16(,R4)            NEXT MEMBER POSITION
         S     R3,=F'1'              ANY MORE FIT?
         BNP   DELE210               NO, BRANCH
         TM    #DELFLAG,#DELONE      A SINGLE MEMBER?
         BNO   DELE300               NO, BRANCH
         SPACE 3
DELE600  TM    #DELFLAG,#DELASSP     ASSOCIATED MEMBERS?
         BO    DELE605               YES, BRANCH
         MVI   #DELSAVE,FF-FMEM#MEM  ELEMENT HAS NOW BEEN ADDED
         TM    SPFLAG2,SPFLNCD       LINE COMMAND?
         BO    DELE605               YES, BRANCH
         TM    PMEMMIN,X'80'         MEMBER LIST?
         BO    DELE300               YES, BRANCH
         SPACE 1
DELE605  TM    FLAGSBB,FLINESET      ANY MEMBERS FOUND?
         BNO   DELE610               NO, BRANCH
         LA    R1,MSGLINE+5
         SR    R6,R1
         STC   R6,MTLEN
         MVC   INSERT#1(127),MSGLINE+4
         LA    R1,FULLWORD
         M$MSG (R1)
         MVI   MTLEN,8
         M$ERRST MSGBLANK
         SPACE 1
DELE610  TM    #DELFLAG,#DELASSP     ASSOCIATES PHASE STARTED?
         BO    DELE700               YES, BRANCH
         TM    #DELFLAG,#DELASS2     DELETE ASSOCIATE MEMBERS TOO?
         BNO   DELE700               NO, BRANCH
         OI    #DELFLAG,#DELASSP     YES, ASSOCIATES PHASE STARTED
         NI    #DELFLAG,FF-#DELONE   WANT ALL ASSOCIATED MEMBERS
         NI    FLAGSII,FF-FDIRGRP    NORMAL INPUT NOW
         NI    FLAGSBB,FF-FLINESET   NEW OUTPUT LINE
         MVC   MSGLINE+4(L'MSGDELAS),MSGDELAS
         LA    R6,MSGLINE+4+L'MSGDELAS
         MVC   FULLWORD(3),L163$1    MEMBERS TO BE DELETED
         MVI   STARTTR+2,X'01'       TTR=000001 (START OF DIRECTORY)
         B     DELE300               DO ANY ASSOCIATED MEMBERS
         SPACE 3
DELE700  NI    FLAGSII,FF-FDIRGRP    NORMAL INPUT NOW
         TM    #DELFLAG,#DELENQ      ANY MEMBERS IN USE?
         BO    NEWCMD                YES, BRANCH
         SPACE 1
         TM    #MEMFLAG,X'80'        MEMBERS SUBCOMMAND?
         BO    DELE810               YES, BRANCH
         SPACE 1
         LA    R1,PDS395A            SUBMIT PROMPT MESSAGE
         TM    #MEMFLAG,X'40'        SUBMIT SUBCOMMAND?
         BO    *+8                   YES, BRANCH
         LA    R1,PDS394A            DELETE PROMPT MESSAGE
         BAS   R2,YESNO              PROMPT FOR YES OR NO
         B     NEWCMD                NO, QUIT
         B     DELE900               YES, BRANCH
         SPACE 2
DELE800  MVI   #MEMCNTM+3,1          SET COUNT TO 1
         CLI   #MEMCNTK,2            NOCOUNT KEYWORD?
         BE    NEWCMD                YES, BRANCH
         M$ERRST MSGBLANK
         SPACE 1
DELE810  CLI   #MEMCNTK,2            NOCOUNT KEYWORD?
         BE    NEWCMD                YES, BRANCH
         L     R1,#MEMCNTM           COUNT OF MEMBERS
         CVD   R1,DOUBLE
         MVC   INSERT#2(16),MEMEDIT  EDIT PICTURE
         ED    INSERT#2(9),DOUBLE+4
         LA    R1,INSERT#2
         LA    R1,1(,R1)
         CLI   0(R1),X'40'
         BE    *-8
         MVC   INSERT#1(8),0(R1)     COUNT OF MEMBERS
         M$MSG L193$1                MEMBER COUNT MESSAGE
         B     NEWCMD                NO OPEN -- QUIT
         SPACE 2
DELE900  TM    #MEMFLAG,X'40'        SUBMIT SUBCOMMAND?
         BO    DELE998               YES, BRANCH
         BAS   R2,OPENSTOW           OPEN STOW DCB; ENQUEUE
         B     NEWCMD                NO OPEN -- QUIT
         SPACE 1
         AGO   .TESTORD              ***TEST MODIFY DELETE ORDER
         L     R3,#MEMPTR            START OF MEMBER LIST
         B     DELE910+4             SKIP FIRST ADD
DELE910  LA    R3,16(,R3)
         CR    R3,R4
         BNL   DELE980
.TESTORD ANOP
         LR    R3,R4                 END OF MEMBER LIST
         LA    R6,16                 DECREMENT AMOUNT
DELE910  SR    R3,R6
         CLM   R3,B'0111',#MEMPTR+1
         BL    DELE980
         STOW  STOWDCB,0(R3),D       ISSUE STOW WITH DELETE OPTION
         SPACE 1
         CH    R15,=H'12'              RC > 12                DRK AUG05
         BH    IOERROR                 YES, ERROR CONDITION   DRK AUG05
         B     *+4(R15)              PROCESS RETURN CODE
         B     DELE940                 00 - SUCCESSFUL
         EX    0,*                     04 - SHOULD NOT OCCUR
         B     DELE960                 08 - MEMBER NOT FOUND
         EX    0,*                     12 - SHOULD NOT OCCUR  DRK JUN03
*        B     IOERROR                 16 - I/O ERROR IN DIRECTORY
         SPACE 1
DELE940  MVC   INSERT#1(8),0(R3)
         M$MSG L040$1
         B     DELE910
         SPACE 1
DELE960  MVC   INSERT#1(8),0(R3)
         M$MSG L853$1
         B     DELE910
         SPACE 1
DELE980  BAS   R2,SHUTSTOW            CLOSE FOR: IF * THEN(DELETE)
         B     NEWCMD
         SPACE 1
DELE998  L     R1,=V($PRI)            PRINTOFF
         MVC   ##ADRCMD(12),8(R1)     CONTINUE WITH PRINTOFF
         L     R8,##ADRCMD            SUBCOMMAND ROUTINE START
         LA    R2,NEWCMD              SUBCOMMAND TERMINATION ADDRESS
         BR    R8                     LINK TO THE SUBCOMMAND
         SPACE 2
#DELASSP EQU     X'80'            DELETE: ASSOCIATES PHASE OF DELETE
#DELONE  EQU     X'10'            DELETE: DELETE A SINGLE MEMBER
#DELENQ  EQU     X'08'            DELETE: A DELETE MEMBER IS "IN USE"
#DELASS2 EQU     X'01'            DELETE: DELETE ASSOCIATES TOO
#MEMBLKE EQU     X'08'            BLKSIZE TOO LARGE
#MEMDIVE EQU     X'04'            BLKSIZE/LRECL NOT INTEGRAL
#MEMLRLE EQU     X'02'            LRECL TOO LARGE
#MEMLRL0 EQU     X'01'            LRECL NEGATIVE OR ZERO
MSGDELET DC    C'MEMBERS TO BE DELETED ARE:'
MSGMEMBS DC    C'MEMBERS ARE:'
MSGDELAS DC    C'ASSOCIATED MEMBERS TO BE DELETED ARE:'
MEMEDIT  DC    X'40202020206B20212040404040404040'
