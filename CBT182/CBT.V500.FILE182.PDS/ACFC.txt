./ ADD NAME=ACFC     0143-95291-14100-1316-00088-00042-00000-TRIDJK  38
/* Rexx */
/*                                                              */
/* Modified version of ACFCOMP for the "ACFC" PDS line command  */
/* on a MEMLIST display.  No SAVE required, and END is issued.  */
/*                                                              */
Address ISREDIT
"ISREDIT MACRO (PARM)"
Trace
If Translate(parm) = 'HELP' |,
   parm = '?' Then
  Do
    Address ISPEXEC "DISPLAY PANEL(#ACFCOMP)"
    Exit 0
  End

"ISREDIT (MEM) = MEMBER"
"ISREDIT (DS) = DATASET"

Address TSO
"ACFCOMP '"ds"("mem")' STORE FORCE"

/* Resource rule ? */
"ISREDIT FIND FIRST 'TYPE('"
If rc = 0 Then
  Do
    "ISREDIT (LINE) = LINE .ZCSR"
     name = 'R'||SUBSTR(line,POS('TYPE(',line)+5,3)
     show_header = '-- RESIDENT INFOSTORAGE DIRECTORIES --'
  End
Else
  Do
    name = mem
    show_header = '-- RESIDENT ACCESS RULES --'
  End

Queue 'SHOW RESIDENT'
Queue 'END'
x = OUTTRAP("trap.","*")
Address TSO
"ACF"
x= OUTTRAP("off")

showres = ''
Do line = 1 to trap.0
  If trap.line = show_header Then
    Do rar = line+1 to trap.0
      showres = showres||trap.rar||' '
      If trap.rar = '  ' Then
        Leave line
    End
End

showres = Translate(showres,' ',',')   /* Remove commas for WORDPOS */
If Wordpos(name,showres) > 0 Then
  Do
    If Wordpos('ACCESS',show_header) > 0 Then
      Do
        action = 'RELOAD'
        resident = 'RESIDENT ACCESS RULE'
      End
    Else
      Do
        /* Remove 'R' from name */
        name = SUBSTR(name,2)
        action = 'REBUILD'
        resident = 'RESIDENT DIRECTORY'
      End

    QUEUE "F ACF2,"action"("name")"
    QUEUE 'END'
    ADDRESS TSO
    "ACF"

    zedsmsg = 'ACF2 'action' issued'
    zedlmsg = 'ACF2 'action' issued for 'resident name
    Address ISPEXEC "SETMSG MSG(ISRZ001W)"
  End

"ISREDIT END"

Exit

/* Source:    CBT Mods tape, Michael Botos  042685           */
/* Modified:  John Kalinich, CSC                             */
/*                                                           */
/* Edit macro to compile the ACF2 rule being edited          */
/* Issue "F ACF2,RELOAD(rule)" or "F ACF2,REBUILD(dir)"      */
/* if rule is in the SHOW RESIDENT list                      */
./ ADD NAME=#ACFCOMP 0111-89305-96257-1013-00011-00018-00000-TRIDJK  53
%TUTORIAL ---------------- EDIT - 'ACFCOMP' MACRO --------------------- TUTORIAL
%OPTION  ===>_ZCMD                                                             +

 The%ACFCOMP+macro will invoke ACF2 and automatically compile and store the
 rule you were editing.  ACF2 displays the complilation process.  If the rule
 is an access rule and is a resident access rule, a "F ACF2,RELOAD(rule)"
 operator command is issued.  If the rule is a resource rule and the directory
 is a resident directory, a "F ACF2,REBUILD(directory)" operator command is
 issued.  The user is returned to the EDIT display panel.

)END
