//ASM      EXEC  PGM=ASMA90,REGION=32M,
//             PARM=(NORLD,RENT,TERM,OBJ,
//             'XREF(SHORT),SIZE(MAX,ABOVE)')
//SYSLIB   DD  DSN=SYS1.MODGEN,DISP=SHARE
//         DD  DSN=SYS1.MACLIB,DISP=SHARE
//         DD  DSN=ACF2.R12.CAIMAC,DISP=SHR    <=== ACF2 SOURCE MACLIB
//SYSUT1   DD  UNIT=VIO,SPACE=(4096,(120,120),,,ROUND),DCB=BUFNO=1
//SYSLIN   DD  UNIT=VIO,SPACE=(TRK,(5,5)),DISP=(MOD,PASS),DSN=&&X,
//             DCB=BLKSIZE=3120
//SYSPRINT DD  SYSOUT=*
//SYSTERM  DD  SYSOUT=*
//SYSIN    DD  *
VTSOCMD  CSECT
*
* SAMPLE TSO COMMAND AUTHORIZATION EXIT FOR ACF2 SYSTEMS WRITTEN BY:
*   SAM LEPORE   WELLS FARGO BANK, SAN FRANCISCO, CALIFORNIA
*** UPDATED 6/12/85 PER SAM LEPORE
*
*        TSO COMMAND VALIDATION EXIT
*
*        THIS ROUTINE IS CALLED BY THE TSO PROCESSOR TO VALIDATE
*        THE TSO COMMAND USAGE.
*
*        FUNCTIONS -
*            1) INVOKE ACF2 TO VALIDATE USERS ACCESS TO THE TSO COMMAND
*
*            2) IF ACCESS IS ALLOWED, CHECK THE FIRST CHARACTER OF THE
*               COMMAND NAME FOR THE ACF2 COMMAND LIMITING "BYPASS",
*               AND REMOVE IT IF IT IS PRESENT.
*
*        INPUT -
*            R1 POINTS TO A STANDARD TSO CPPL FOLLOWED BY A POINTER
*            TO AN EIGHT BYTE AREA CONTAINING THE COMMAND NAME.
*
*        OUTPUT -
*            R15 CONTAINS A RETURN CODE WITH THE FOLLOWING MEANING:
*               0 - COMMAND IS AUTHORIZED
*               4 - COMMAND IS NOT AUTHORIZED
*
*        NOTE - USE OF THE ACF2 MACROS ACFSVC AND ACFGACVT REQUIRES
*            INCLUSION OF MODULE ACF$GCVT DURING LINK EDIT.
*
         EJECT
         SAVE  (14,12),,VTSOCMD-&SYSDATE-&SYSTIME SAVE REGISTERS
         USING VTSOCMD,R12        ESTABLISH ADDRESSABILITY
         LR    R12,R15            LOAD BASE REGISTER
         LR    R11,R1             SET UP PARAMETER REGISTER
         GETMAIN RU,LV=WORKSIZE,BNDRY=PAGE  ACQUIRE WORK AREA
         ST    R13,4(,R1)         CHAIN
         ST    R1,8(,R13)           SAVEAREAS
         LR    R13,R1             POINT TO MY SAVE AREA
         USING WORKAREA,R13       ESTABLISH ADDRESSABILITY
         L     R11,16(R11)        POINT TO COMMAND NAME
*
*        PREPARE PARAMETER LIST FOR ACF2 CALL
*        (ACF COMMAND LIMITING USES SAME FORMAT AS BLDL SVC)
*
         MVC   BLDLPARM(BLDLHDRL),BLDLHDR MOVE IN COUNT AND LENGTH
         MVC   BLDLCMD(8),0(R11)  MOVE IN COMMAND NAME
*
*        ISSUE ACF2 SVC FOR VALIDATION
*
         ACFSVC BLDLPARM,TYPE=C,NONE=NOACF2
         LTR   R15,R15            TEST RETURN CODE
         BNZ   NOTAUTH            COMMAND IS NOT AVAILABLE TO USER
*
*        COMMAND IS ALLOWED. IF THE FIRST CHARACTER IS THE COMMAND
*        LIMITING BYPASS CHARACTER, IT MUST BE REMOVED BEFORE ATTACH.
*
         ACFGACVT R2,NONE=NOACF2  GET ADDRESS OF ACF CVT
         USING ACCVT,R2
****                                                                 SL
****===>       THE FOLLOWING THREE LINES HAVE BEEN COMMENTED OUT     SL
****           BECAUSE THE COMMAND LIMITING BYPASS CHARACTER IS      SL
****           NO LONGER IN THE FDR AS OF ACF2 RELEASE 4.0           SL
****                                                                 SL
****                                                                 SL
****                                                                 SL
****===> L     R2,ACCFDR          ADDRESS OF FIELD DEFINITION RECORD SL
****===> USING ACFDR,R2                                              SL
****===> CLC   0(1,R11),FDRTBYPC  IS FIRST CHAR THE BYPASS CHAR ?    SL
         CLC   0(1,R11),ACCTBYPC  IS FIRST CHAR THE BYPASS CHAR ?    SL
         DROP  R2
         BNE   AUTH               NO, DONE
         MVC   0(7,R11),1(R11)    YES, SHIFT LEFT (OVERLAY BYP CHAR)
         MVI   7(R11),C' '        INSERT TRAILING BLANK
         B     AUTH
*
*        IF ACF2 IS NOT ACTIVE, INFORM THE USER, WARN THE OPERATOR
*           THEN ALLOW ACCESS
*
NOACF2   WTO   'WARNING - ACF2 IS NOT ACTIVE',ROUTCDE=(1,9,11),DESC=(6)
         B     AUTH
         SPACE 3
NOTAUTH  DS    0H                 FAIL REQUEST
         LA    R2,4
         B     EXIT               EXIT
AUTH     DS    0H                 ALLOW REQUEST
         LA    R2,0
         B     EXIT               EXIT
         SPACE 2
EXIT     DS    0H
         LR    R1,R13             GET POINTER TO WORK AREA
         L     R13,4(,R13)        UNCHAIN SAVE AREAS
         FREEMAIN R,LV=WORKSIZE,A=(1)  FREE DYNAMIC AREA
         LR    R15,R2             RESTORE RETURN CODE
         RETURN (14,12),RC=(15)
         SPACE 2
         EJECT
*
*        CONSTANTS
*
BLDLHDR  DS    0H
         DC    XL2'0001'               COUNT OF ENTRIES
         DC    XL2'0008'               LENGTH OF DATA
         SPACE 3
BLDLHDRL EQU   *-BLDLHDR
         SPACE 3
         LTORG
****===> REGS        <=== DELETED, USE AXCFREGS INSTEAD.             SL
         SPACE 3
         ENTRY PATCH
PATCH    DC    50S(*)             PATCH AREA
         EJECT
WORKAREA DSECT
SAVEAREA DS    18F                SAVE AREA
BLDLPARM DS    0XL12              BLDL PARAMETER LIST
BLDLCNT  DS    H                  COUNT OF ENTRIES
BLDLLEN  DS    H                  LENGTH OF DATA
BLDLCMD  DS    CL8                COMMAND NAME
         DS    0D                 FILL OUT DSECT
WORKSIZE EQU   *-WORKAREA         LENGTH OF WORK AREA
         SPACE 3
         ACFREGS                  GENERATE ACF2 REGISTER EQUATES     SL
         SPACE 3
         ACCVT                    GENERATE ACF2 COMMUNICATION VECTOR
****===> SPACE 3     <===  2 LINES DELETED, FDR NOT USED FOR R4.0    SL
****===> ACFDR       <===  DELETED         ACF2 FIELD DEFINITION REC SL
         END
//LK1    EXEC  PGM=IEWL,PARM='MAP,RENT,REUS,REFR,LET,NCAL',COND=(5,LT)
//SYSPRINT DD  SYSOUT=*
//SYSLMOD  DD  DISP=SHR,DSN=LINK.LIBRARY    <=== MODIFY
//ACFMOD   DD  DISP=SHR,DSN=ACFMOD.LIBRARY  <=== MODIFY
//IN       DD  DSN=&&X,DISP=(OLD,DELETE)
//SYSUT1   DD  UNIT=SYSDA,SPACE=(TRK,(5,5))
//SYSLIN   DD  *
  REPLACE  VTSOCMD                          <=== ADDED FOR RELINK    SL
  INCLUDE  SYSLMOD(PDS45678)                <=== MODIFY
  INCLUDE  IN
  INCLUDE  ACFMOD(ACF$GCVT)
  ENTRY    PDSMAIN
  NAME     PDS45678(R)                      <=== MODIFY
