         TITLE 'P D S  --  PDS ATTRIB                        09/28/88'
***********************************************************************
***      ATTRIB SUBCOMMAND                                          ***
***********************************************************************
*
         SPACE 1
ATTRIB   CSECT
         USING *,R8,R6
         LA    R6,2048                                        DRK FEB98
         LA    R6,2048(R6,R8)      SECOND BASE                DRK FEB98
         SPACE 1
         TM    FLAGSCC,RECFMU          RECFM=U?               DRK SEP05
         BNO   ATTRCHG1                NO, CHECK FOR MODS     DRK JUN18
*        BNO   ATTRCONT                NO, CONTINUE           DRK SEP05
         TM    DS1SMSFG,DS1PDSE        PDSE DATASET?          DRK SEP05
         BO    ATTRCHG                 YES, CHECK FOR MODS    DRK APR16
         TM    FLAGSHH,FMEMALLM        ALL LOAD MEMBERS?      DRK APR16
         BO    ATTRCHG                 YES, CHECK FOR MODS    DRK APR16
         B     ATTRCONT                NO, CONTINUE           DRK APR16
         SPACE 1                                              DRK SEP05
ATTRCHG  DS    0H
         CLC   ##SUBCOM+#LSUB(8),$ATTL IF OR THEN FORM?       DRK AUG06
         BNE   ATTRCONT                YES, CONTINUE          DRK AUG06
         CLI   #AMODE,0                ANY AMODE CHANGE?      DRK SEP05
         BNE   ATTRCHG2                YES                    DRK SEP05
         CLI   #AUTHOPT,0              CHANGE APF PROCESSING? DRK SEP05
         BNE   ATTRCHG2                YES                    DRK SEP05
         CLC   #MODTXT(8),BLANKS       ANY ENTRY(EXTNAME)?    DRK SEP05
         BNE   ATTRCHG2                YES                    DRK SEP05
         CLI   #PAGEOPT,0              CHANGE PAGE ALIGNMENT? DRK SEP05
         BNE   ATTRCHG2                YES                    DRK SEP05
         CLI   #RLDERR,0               RLDFIX OPERAND?        DRK SEP05
         BNE   ATTRCHG2                YES                    DRK SEP05
         CLI   #RMODE,0                ANY RMODE CHANGE?      DRK SEP05
         BNE   ATTRCHG2                YES                    DRK SEP05
         CLI   #SSIOPT,0               ANY SSI CHANGE?        DRK SEP05
         BNE   ATTRCHG2                YES                    DRK SEP05
         CLI   #UNALIAS,0              TURN OFF ALIAS BIT?    DRK SEP05
         BNE   ATTRCHG2                YES                    DRK SEP05
         CLI   #ATTRNO,X'FF'           DIRFLAGS?              DRK SEP05
         BNE   ATTRCHG2                YES                    DRK SEP05
         CLI   #ATTRNO+1,X'FF'         DIRFLAGS?              DRK SEP05
         BNE   ATTRCHG2                YES                    DRK SEP05
         CLI   #ATTRYES,0              DIRFLAGS?              DRK SEP05
         BNE   ATTRCHG2                YES                    DRK SEP05
         CLI   #ATTRYES+1,0            DIRFLAGS?              DRK SEP05
         BNE   ATTRCHG2                YES                    DRK SEP05
         B     ATTRCONT                                       DRK SEP05
* CHECK SOURCE IF MODS AND ALL MEMBERS                        DRK JUN18
ATTRCHG1 DS    0H                                                     "
         SR    R1,R1                   CLEAR R1               DRK JUN18
         ICM   R1,B'0001',#ATTROPT     SPF OPTIONS            DRK JUN18
         ICM   R1,B'0010',#OPTOPT      OTHER OPTIONS          DRK JUN18
         LTR   R1,R1                   ANY ATTRIBUTES CHANGED?        "
         BZ    ATTRCONT                NO, CONTINUE           DRK JUN18
         B     ATTRCHG3                YES, CHECK ALL MEMBERS DRK JUN18
ATTRCHG2 DS    0H                                             DRK APR16
         TM    DS1SMSFG,DS1PDSE        PDSE DATASET?          DRK APR16
         BNO   ATTRCHG3                NO, THEN ALL MEMBERS   DRK APR16
         MVI   MTLEN,15                LENGTH OF INSERT       DRK SEP05
         MVC   INSERT#1(7),=C'ATTRIB ' COMMAND NAME           DRK SEP05
         MVC   INSERT#1+7(8),=X'9392858481A3A399' LKEDATTR    DRK SEP05
         M$MSG L996$1                  NOT SUPPORTED MSG      DRK SEP05
         M$ERRST MSGBLANK              ONE BLANK LINE         DRK SEP05
         B     NEWCMD                  NOBBLED                DRK SEP05
ATTRCHG3 DS    0H                                             DRK APR16
         TM    FLAGSHH,FMEMALLM        ALL MEMBERS?           DRK APR16
         BNO   ATTRCONT                NO, CONTINUE           DRK APR16
         TM    FLAGSZZ,ATT#MSG         PDS397A ISSUED?        DRK APR16
         BO    ATTRCONT                YES                    DRK APR16
         OI    FLAGSZZ,ATT#MSG         NO, TURN ON FLAG       DRK APR16
         LA    R1,PDS397A              ATTRIB PROMPT MESSAGE  DRK APR16
         BAS   R2,YESNO                PROMPT FOR YES OR NO   DRK APR16
         B     MSGNEWXX                NO, TERMINATE GROUP    DRK JUN18
*        NOP   NEWCMD                  NO, PA1 TO ABORT       DRK APR16
ATTRCONT DS    0H                                             DRK SEP05
         CLI   #EXTEND,2               EXTENDED SPF STATS?    DRK AUG09
         BNE   ATTRCON2                NO                     DRK AUG09
         CLI   #ADDSTAT,2              ADDSTATS?              DRK AUG09
         BE    ATTRCON2                YES, BRANCH            DRK AUG09
         SPACE 1                       NO, EXTEND REQUIRES    DRK AUG09
         M$MSG L522                        ADDSTATS           DRK AUG09
         M$ERRST MSGBLANK              ONE BLANK LINE         DRK AUG09
         B     NEWCMD                  NOBBLED                DRK AUG09
ATTRCON2 DS    0H
         SPACE 1
         LA    R1,L530             ASSUME A NON-PARTITIONED DATA SET
         TM    DSORG,DS1DSGPO      CORRECT?
         BZ    MSGNEW              YES, BRANCH
         SPACE 1
         L     R1,AATTRSUM         ATTRIB SUMMARY
         ST    R1,ADDRSUMR         SET FOR A LATTER CALL
         TM    FLAGSCC,RECFMU      LOAD MODULE LIBRARY?
         BO    ATTR020             YES, BRANCH
         CLI   DIRFLAG,X'0F'       ISPF STATISTICS AND NOT ALIAS?
         BE    ATTRIB01            YES                        DRK AUG09
         CLI   DIRFLAG,X'14'       ISPF XSTATISTICS AND NOT ALIAS?    "
         BE    ATTRIB01            YES                                "
         B     ATTRIB02            NO, BRANCH                         "
ATTRIB01 DS    0H                                                     "
         L     R1,ATTRCNT2         NUMBER OF ENTRIES
         LA    R1,1(,R1)           ONE MORE ENTRY
         ST    R1,ATTRCNT2         NUMBER OF ENTRIES
         SPACE 1
ATTRIB02 DS    0H
         CLI   #LONGOPT,X'01'          LONG COUNTS?           DRK SEP09
         BNE   ATTRIB03                NO                     DRK SEP09
         MVC   INSERT#1(72),SPFSTATL   8-DIGIT SIZE/INIT      DRK SEP09
         B     ATTRIB04                                       DRK SEP09
ATTRIB03 DS    0H
         MVC   INSERT#1(72),SPFSTAT1   5-DIGIT SIZE/INIT/MOD
ATTRIB04 DS    0H
         MVI   MTLEN,72
         TM    ##ADRCM#+#LSUB,FATTRHDR HEADER WRITTEN YET?
         BO    ATTR000                 YES, BRANCH
         CLI   #LONGOPT,X'01'          LONG COUNTS?           DRK SEP09
         BNE   ATTRIB05                NO                     DRK SEP09
         M$MSG L231$1                  HEADER LINE            DRK SEP09
         B     ATTRIB06                                       DRK SEP09
ATTRIB05 DS    0H
         M$MSG L230$1
ATTRIB06 DS    0H
         OI    ##ADRCM#+#LSUB,FATTRHDR MARK FOR NEXT CALL
         SPACE 1
ATTR000  MVC   INSERT#1(72),BLANK128
         CLI   #SSIOPT,0               ANY SSI CHANGE?
         BE    ATTR003                 NO, BRANCH
         CLI   #SSIOPT,1               SSI(HEXDATA)?
         BE    ATTR002                 YES, BRANCH
         TM    DIRFLAG,X'0F'           ISPF STATISTICS?
         BO    ATTR003                 YES, BRANCH
         TM    DIRFLAG,X'14'           ISPF XSTATISTICS?      DRK AUG09
         BO    ATTR003                 YES, BRANCH            DRK AUG09
         NI    DIRFLAG,FF-X'1F'        TURN OFF SSI DATA
         XC    DIRSTART(4),DIRSTART    CLEAR THE SSI FIELD
         B     ATTR003
         SPACE 1
ATTR002  MVC   DIRSTART(4),#SSITEXT    ADD THE CURRENT SSI DATA
         NI    DIRFLAG,FF-X'1F'        TURNS OFF ANY HALFWORDS
         OI    DIRFLAG,X'02'           ADDS IN TWO HALFWORDS
         SPACE 1
ATTR003  CLI   #ADDSTAT,0              ANY STATS CHANGE?
         BE    ATTR006                 NO, BRANCH
         CLI   #ADDSTAT,2              ADDSTATS?
         BE    ATTR004                 YES, BRANCH
*                                      NO, THEN NONE
         NI    DIRFLAG,FF-X'1F'        TURN OFF SSI DATA OR ISPF STATS
         XC    DIRSTART(4),DIRSTART    CLEAR THE SSI FIELD
** ATTRIB XX:YY NONE FOR SOURCE MODULE -- REREAD THE LAST DATA BLOCK
** TO ENSURE THAT WE DON'T MISS ANY DIRECTORY ENTRIES WHEN THESE SHIFT
         MVC   IODIR1+4+00(8),CURDIR   RESET READ ADDRESS
         MVI   IODIR1+4+08,X'08'       RESET KEY LENGTH
         MVC   IODIR1+4+09(2),=H'256'  RESET DATA LENGTH
         MVI   IODIR1+4+11,0           RESET SECTOR ADDRESS TO ZERO
         MVI   IOECB,X'7F'             LAST READ WAS OK
         B     ATTR006
         SPACE 1
ATTR004  DS    0H
         XC    DIRSAVE(30),DIRSAVE     CLEAR DIR SAVE AREA    DRK SEP09
         TM    DIRFLAG,X'0F'           ISPF STATISTICS?
         BO    ATTR0040                YES, CHECK FOR EXTEND  DRK SEP09
         TM    DIRFLAG,X'14'           ISPF XSTATISTICS?      DRK AUG09
         BO    ATTR006                 YES, DON'T ADD         DRK AUG09
         B     ATTR0041                NO
ATTR0040 DS    0H
         CLI   #EXTEND,2               EXTENDED SPF STATS?    DRK SEP09
         BNE   ATTR006                 NO, DON'T ADD          DRK SEP09
         MVC   DIRSAVE(30),DIRSTART    YES, SAVE REG STATS    DRK SEP09
ATTR0041 DS    0H
         XC    DIRSTART(30),DIRSTART   CLEAR THE DIRECTORY AREA
         CLI   #EXTEND,2               EXTENDED SPF STATS?    DRK AUG09
         BNE   ATTR0042                NO                     DRK AUG09
         OI    DIRSPFFL,DIRSPFXS       YES, SET EXTENDED FLAG DRK AUG09
         OI    DIRFLAG,X'14'           ADDS 20 HALFWORDS      DRK AUG09
         B     ATTR0043                                       DRK AUG09
ATTR0042 DS    0H
         OI    DIRFLAG,X'0F'           ADDS 15 HALFWORDS
ATTR0043 DS    0H
         MVI   DIRSPFV,1               VERSION=1 IS THE DEFAULT
         TIME  DEC                     GOT THE TIME?
         STCM  R1,B'1111',DIRSPFCR     CREATION DATE
         STCM  R1,B'1111',DIRSPFCD     CHANGED DATE
         STCM  R0,B'1100',DIRSPFCT     TIME LAST CHANGE (HHMM)
         STCM  R0,B'0010',DIRSPFCS     TIME LAST CHANGE (SS)  DRK OCT09
         SPACE 2
         SR    R2,R2                   LOGICAL RECORDS
         MVC   STARTTR(3),DIRTTR       FIRST TTR
         SPACE 2
ATTR0051 L     R15,=V(EXCP)
         ST    R2,#SAVEREG             SAVE R2 BEFORE PAM     DRK AUG05
         BASR  R14,R15
         L     R2,#SAVEREG             RESTORE R2 AFTER PAM   DRK AUG05
         B     *+4(R15)                PROCESS RETURN CODE
         B     ATTR0052                  00 - SUCCESSFUL READ
         B     ATTR0058                  04 - END OF FILE
         B     ATTR0058                  08 - END OF DATA SET
         B     ATTR0058                  12 - I/O ERROR
         SPACE 2
ATTR0052 LR    R3,R0                   BUFFER START
         L     R5,LS                   BLOCK LENGTH
ATTR0053 AR    R5,R3                   END OF BUFFER
         BCTR  R5,0                    END OF BUFFER -1
         LH    R4,LRECL
         TM    FLAGSCC,RECFMF          RECFM=F?
         BO    ATTR0056                YES, BRANCH
         AH    R3,=H'4'                SKIP BLOCK LENGTH WORD
         SPACE 1
ATTR0055 TM    FLAGSCC,RECFMV          RECFM=V?
         BNO   ATTR0056                NO, BRANCH
         SR    R4,R4
         ICM   R4,B'0011',0(R3)        RECORD LENGTH +4
         AH    R3,=H'4'                SKIP RECORD LENGTH WORD
         SH    R4,=H'4'                RECORD LENGTH VALID?
         BNP   ATTR0051                NO, BRANCH
         SPACE 1
ATTR0056 A     R2,=F'1'                NUMBER OF LOGICAL RECORDS
ATTR0057 BXLE  R3,R4,ATTR0055          GET THE NEXT LOGICAL RECORD
         B     ATTR0051                END OF BLOCK, BRANCH
         SPACE 2
ATTR0058 MVC   DIRSPFID+7(3),BLANKS    BLANK TRAILING ID BYTES
*        L     R1,ADDRPSCB             ADDRESS OF THE PSCB
*        MVC   DIRSPFID(7),PSCBUSER-PSCB(R1)  LOGON USERID
         LA    R1,@4K                       BASE FOR TSOUID   DRK JUL17
         MVC   DIRSPFID(8),TSOUID-@4K(R1)   USERID            DRK JUL17
         CLI   #EXTEND,2               EXTENDED STATISTICS?   DRK AUG09
         BNE   ATTR005B                NO                     DRK AUG09
         CLC   DIRSAVE(30),HEXZERO     REGULAR STATS SAVED?   DRK SEP09
         BE    ATTR0059                NO                     DRK SEP09
         MVC   DIRSTART(30),DIRSAVE    YES, RESTORE           DRK SEP09
         OI    DIRSPFFL,DIRSPFXS       SET EXTENDED FLAG      DRK SEP09
         NI    DIRFLAG,X'F0'           - 15 HALFWORDS         DRK SEP09
         OI    DIRFLAG,X'14'           ADDS 20 HALFWORDS      DRK SEP09
ATTR0059 DS    0H
         ST    R2,DIRSPXSI             EXTENDED SIZE          DRK AUG09
         ST    R2,DIRSPXIN             EXTENDED INITIAL       DRK AUG09
         XC    DIRSPXMD(4),DIRSPXMD    EXTENDED MODIFIED      DRK AUG09
         CLC   DIRSAVE(30),HEXZERO     REGULAR STATS SAVED?   DRK SEP09
         BE    ATTR005B                NO                     DRK SEP09
         XC    DIRSPXIN(2),DIRSPXIN    YES, CLEAR HI ORDER 2  DRK SEP09
         MVC   DIRSPXIN+2(2),DIRSPFIN  RESTORE INIT LINES     DRK SEP09
         MVC   DIRSPXMD+2(2),DIRSPFMD  RESTORE MOD LINES      DRK SEP09
         SR    R1,R1                   CLEAR R1               DRK JAN10
         ICM   R1,B'0011',DIRSPFIN     INITIAL SIZE           DRK JAN10
         C     R1,=F'65535'            INITIAL @ 64K LIMIT?   DRK JAN10
         BNE   ATTR005A                NO                     DRK JAN10
         ST    R2,DIRSPXIN             YES, USE EXTENDED INIT SIZE    "
ATTR005A DS    0H
         L     R1,=F'65535'            SET TO X'FFFF'         DRK MAY17
         STCM  R1,B'0011',DIRSPFSI     CURRENT SIZE = MAX     DRK MAY17
         C     R2,=F'65536'            LESS THAN 64K LIMIT?   DRK MAY17
         BNL   ATTR006                 NO                     DRK MAY17
         STCM  R2,B'0011',DIRSPFSI     CURRENT SIZE (RESIZE)  DRK MAY17
         B     ATTR006                 CONTINUE               DRK MAY17
ATTR005B DS    0H
         C     R2,=F'65536'            LESS THAN 64K LIMIT?   DRK JAN98
         BL    ATTR005C                YES                    DRK JAN98
         L     R2,=F'65535'            NO, SET TO X'FFFF'     DRK JAN98
ATTR005C DS    0H
         STCM  R2,B'0011',DIRSPFSI     CURRENT SIZE           DRK JAN98
         STCM  R2,B'0011',DIRSPFIN     INITIAL SIZE           DRK JAN98
         SPACE 1
ATTR006  MVC   INSERT#1+1(8),DIRNAME
         CLI   #UNALIAS,1              TURN OFF ALIAS BIT?
         BNE   *+8                     NO, BRANCH
         NI    DIRFLAG,FF-X'80'        YES, RESET ANY ALIAS BIT
         TM    DIRFLAG,X'80'           ALIAS MEMBER?
         BNO   *+10                    NO, BRANCH
         MVC   INSERT#1+1+8(2),=C'-A'  YES, ADD A FLAG
         LA    R2,DIRUSER          LOAD START OF USER AREA (FOR SSI)
         TM    DIRFLAG,X'0F'       SPF STATISTICS PRESENT?
         BO    ATTR0060            YES
         TM    DIRFLAG,X'14'       EXT SPF STATISTICS PRESENT?DRK AUG09
         BO    ATTR0060            YES                        DRK AUG09
         B     ATTR010             NO, BRANCH                 DRK AUG09
ATTR0060 DS    0H
         TM    DIRSPFFL,DIRSPFXS     EXTENDED STATS?          DRK SEP17
         BNO   *+4+4+4               NO                       DRK SEP17
         TM    DIRSPFFL,255-DIRSPFXS-DIRSPFSC  MUST BE ZEROS  DRK OCT16
         BNZ   ATTR012                         NO, SKIP       DRK OCT16
         TM    DIRSPFCR,X'FE'      0C OF 0CYYDDDF ZEROS?      Y2K AUG97
         BNZ   ATTR010             NO, BRANCH                 Y2K AUG97
         TM    DIRSPFCD,X'FE'      0C OF OTHER 0CYYDDDF ZERO? Y2K AUG97
         BNZ   ATTR010             NO, BRANCH                 Y2K AUG97
         SPACE 1
         TM    #ATTROPT,X'80'        USERID CHANGE?
         BNO   *+10                  NO, BRANCH
         MVC   DIRSPFID(8),#USERTXT  NEW USERID               DRK JAN17
         SPACE 1
         TM    #ATTROPT,X'40'        VERSION CHANGE?
         BNO   *+10                  NO, BRANCH
         MVC   DIRSPFV(1),#ATTRVER   NEW VERSION NUMBER
         SPACE 1
         TM    #ATTROPT,X'20'        MOD CHANGE?
         BNO   *+10                  NO, BRANCH
         MVC   DIRSPFR(1),#ATTRMOD   NEW MODIFICATION NUMBER
         SPACE 1
         TM    #ATTROPT,X'10'        CREATED CHANGE?          DRK FEB01
         BNO   ATTR0061              NO, BRANCH               DRK FEB01
         MVI   DIRSPFCR,X'00'         ASSUME 19XX             DRK FEB01
         MVC   DIRSPFCR+1(3),#IFCREIF NEW CREATION DATE       DRK FEB01
         CLI   DIRSPFCR+1,X'&WIN19XX' BELOW 19XX WINDOW?      DRK FEB01
         BNL   *+4+4                  NO, THEN 19XX           DRK FEB01
         MVI   DIRSPFCR,X'01'         YES, THEN 20XX          DRK FEB01
         SPACE 1                                              DRK FEB01
ATTR0061 TM    #ATTROPT,X'08'        MODIFIED CHANGE?         DRK FEB01
         BNO   ATTR0062              NO, BRANCH               DRK FEB01
         MVI   DIRSPFCD,X'00'         ASSUME 19XX             DRK FEB01
         MVC   DIRSPFCD+1(3),#IFMODIF NEW MODIFIED DATE       DRK FEB01
         CLI   DIRSPFCD+1,X'&WIN19XX' BELOW 19XX WINDOW?      DRK FEB01
         BNL   *+4+4                  NO, THEN 19XX           DRK FEB01
         MVI   DIRSPFCD,X'01'         YES, THEN 20XX          DRK FEB01
         SPACE 1                                              DRK FEB01
ATTR0062 TM    #ATTROPT,X'04'        TIME CHANGE?             DRK FEB01
         BNO   *+4+6+6               NO, BRANCH               DRK FEB01
         MVC   DIRSPFCT(2),#ATTRTIM   NEW MODIFIED TIME       DRK FEB01
         XC    DIRSPFZ+1(1),DIRSPFZ+1 ZERO SECONDS FIELD      DRK FEB01
         SPACE 1                                              DRK FEB01
ATTR0063 TM    #ATTROPT,X'02'        TIME SECONDS CHANGE?     DRK FEB01
         BNO   ATTR0064              NO, BRANCH               DRK FEB01
         XC    DIRSPFZ+1(1),DIRSPFZ+1 ZERO SECONDS FIELD      DRK FEB01
         CLI   #ATTRSEC,X'60'         SS < 60?                DRK FEB01
         BNL   ATTR0064               NO, IGNORE              DRK FEB01
         MVC   DIRSPFZ+1(1),#ATTRSEC  NEW MODIFIED TIME SECS  DRK FEB01
         SPACE 1                                              DRK FEB01
ATTR0064 DS    0H                                             DRK FEB01
         LA    R2,INSERT#1-7
         SR    R1,R1
         IC    R1,DIRSPFR          REVISION NUMBER FIRST
         CVD   R1,DOUBLE
         MVC   21(4,R2),=X'40212020'
         ED    21(4,R2),DOUBLE+6
         MVI   22(R2),C'.'
         IC    R1,DIRSPFV          VERSION NUMBER
         CVD   R1,DOUBLE
         MVC   18(4,R2),=X'40212020'
         ED    18(4,R2),DOUBLE+6
         LA    R1,DIRSPFCR+1           POINT TO YYDDDF
         LA    R15,FULLWORD+1
         BAS   R14,CONVDATE            CONVERT TO MMDDYY FORMAT
         MVC   FULLWORD(1),FULLWORD+3  CONVERT TO YYMMDD
         MVC   26(L'DATEMASK,R2),DATEMASK
         ED    26(L'DATEMASK,R2),FULLWORD
         OI    27(R2),X'F0'            UNSUPPRESS ZERO        Y2K AUG97
         LA    R1,DIRSPFCD+1           POINT TO YYDDDF
         LA    R15,FULLWORD+1
         BAS   R14,CONVDATE            CONVERT TO MMDDYY FORMAT
         MVC   FULLWORD(1),FULLWORD+3  CONVERT TO YYMMDD
         MVC   36(L'DATEMASK,R2),DATEMASK
         ED    36(L'DATEMASK,R2),FULLWORD
         OI    37(R2),X'F0'            UNSUPPRESS ZERO        Y2K AUG97
         MVC   45(6,R2),=X'4021207A2020'
         ED    45(6,R2),DIRSPFCT       TIME OF LAST CHANGE
         SPACE 1
         CLI   #LONGOPT,X'01'          LONG COUNTS?           DRK SEP09
         BNE   ATTR007                 NO                     DRK SEP09
         TM    DIRSPFFL,DIRSPFXS       EXTENDED STATS?        DRK SEP09
         BNO   ATTR007                 NO                     DRK SEP09
         SGR   R1,R1                   CLEAR ALL 64 BITS      DRK MAY16
         L     R1,DIRSPXSI             YES                    DRK SEP09
         CVDG  R1,DOUBLE-8             31 DIGITS              DRK MAY16
         B     ATTR0071                                       DRK SEP09
ATTR007  DS    0H                                             DRK FEB01
         SR    R1,R1                                          DRK JAN98
         ICM   R1,B'0011',DIRSPFSI     CURRENT SIZE           DRK JAN98
         CVD   R1,DOUBLE               15 DIGITS              DRK MAY16
ATTR0071 DS    0H                                             DRK FEB01
         TM    DIRFLAG,X'80'           ALIAS MEMBER?
         BO    ATTR008                 YES, DO NOT ACCUMULATE
         OI    ATTRTOT2+7,X'0F'        INSURE VALID PACKED SIGN
         AP    ATTRTOT2(8),DOUBLE(8)   ADD IN CURRENT VALUE
         SPACE 1
ATTR008  DS    0H
         CLI   #LONGOPT,X'01'          LONG COUNTS?           DRK SEP09
         BNE   ATTR009                 NO                     DRK SEP09
         MVC   WORKTBL(10),=XL10'40202020202020202120'        DRK SEP09
         ED    WORKTBL(10),DOUBLE+3                           DRK SEP09
         MVC   51(9,R2),WORKTBL+1      CURRENT SIZE           DRK SEP09
         CP    DOUBLE+3(5),=PL5'99999999'                     DRK FEB11
         BNH   *+4+6                                          DRK FEB11
         MVC   51(9,R2),=CL9'     >99M'                       DRK FEB11
         TM    DIRSPFFL,DIRSPFXS       EXTENDED STATS?        DRK SEP09
         BNO   ATTR0081                NO                     DRK SEP09
         SGR   R1,R1                   CLEAR ALL 64 BITS      DRK MAY16
         L     R1,DIRSPXIN             YES, INITIAL SIZE      DRK SEP09
         CVDG  R1,DOUBLE-8             31 DIGITS              DRK MAY16
         B     ATTR0082                                       DRK SEP09
ATTR0081 DS    0H
         SR    R1,R1                                          DRK SEP09
         ICM   R1,B'0011',DIRSPFIN     INITIAL SIZE           DRK SEP09
         CVD   R1,DOUBLE               15 DIGITS              DRK MAY16
ATTR0082 DS    0H
         MVC   WORKTBL(10),=XL10'40202020202020202120'        DRK SEP09
         ED    WORKTBL(10),DOUBLE+3                           DRK SEP09
         MVC   60(9,R2),WORKTBL+1      INITIAL SIZE           DRK SEP09
         CP    DOUBLE+3(5),=PL5'99999999'                     DRK FEB11
         BNH   *+4+6                                          DRK FEB11
         MVC   60(9,R2),=CL9'     >99M'                       DRK FEB11
         B     ATTR0091                                       DRK SEP09
ATTR009  DS    0H
         SR    R1,R1                                          DRK SEP09
         ICM   R1,B'0011',DIRSPFSI     CURRENT SIZE           DRK SEP09
         CVD   R1,DOUBLE
         MVC   51(6,R2),=X'402020202120'
         ED    51(6,R2),DOUBLE+5
         SPACE 1
         SR    R1,R1                                          DRK JAN98
         ICM   R1,B'0011',DIRSPFIN     INITIAL SIZE           DRK JAN98
         CVD   R1,DOUBLE
         MVC   57(6,R2),=X'402020202120'
         ED    57(6,R2),DOUBLE+5
         SPACE 1
         SR    R1,R1                                          DRK JAN98
         ICM   R1,B'0011',DIRSPFMD     MODIFIED LINES         DRK JAN98
         CVD   R1,DOUBLE
         MVC   63(6,R2),=X'402020202120'
         ED    63(6,R2),DOUBLE+5
ATTR0091 DS    0H
         MVC   71(8,R2),DIRSPFID
         B     ATTR012                 NO SSI FOR SPF-SAVED MEMBERS
         SPACE 1
ATTR010  CLC   ZERO,0(R2)              ZERO?
         BE    ATTR012                 YES, NO SSI
         CLC   =F'-1',0(R2)            FFFFFFFF?
         BE    ATTR012                 YES, NO SSI
         SPACE 1
         MVC   INSERT#1+2+8+3(4),=C'SSI:'
         UNPK  INSERT#1+2+8+3+5(9),0(5,R2)
         TR    INSERT#1+2+8+3+5(8),TRTABLE
         MVI   INSERT#1+2+8+3+5+8,X'40'
         SPACE 1
ATTR012  DS    0H
         CLI   #LONGOPT,X'01'          LONG COUNTS?           DRK SEP09
         BNE   ATTR0121                NO                     DRK SEP09
         M$MSG L231$1                  YES, OUTPUT STATISTICS DRK SEP09
         B     ATTR0122                                       DRK SEP09
ATTR0121 DS    0H
         M$MSG L230$1                  OUTPUT STATISTICS
ATTR0122 DS    0H
         MVI   MTLEN,8                 RESET THE INSERT LENGTH
         CLI   #OPTOPT,FF              ANY ILLEGAL CHANGES?
         BNE   ATTR014                 NO, BRANCH
         M$MSG L701                    NOT A LOAD MODULE
ATTR014  CLI   #OPTOPT,X'F0'           ANY SSI CHANGE?
         BE    ATTR240                 YES, BRANCH
         TM    #ATTROPT,X'FF'          ANY ATTRIBUTES CHANGED?
         BNZ   ATTR240                 YES, BRANCH
         B     ATTR600                 CHECK FOR ALIAS
         SPACE 3
ATTR020  CLC   ##SUBCOM+#LSUB(8),$ATTL IF OR THEN FORM?
         BE    *+8                     NO, BRANCH
         MVI   #ALIAOPT,X'03'          YES, SET FOR SHORT FORMAT
         TM    FLAGSHH,FHEAD           BLANK SEPARATOR WRITTEN YET?
*        BNO   ATTR022                 NO, BRANCH
         NOP   ATTR022                 NOP FOR SUBLISTS       DRK JUN05
         CLI   #ALIAOPT,X'03'          SHORT FORMAT DESIRED?
         BE    ATTR022                 YES, BRANCH
         M$ERRST MSGBLANK              NOW SEPARATORS CAN BE WRITTEN
         SPACE 1
ATTR022  OI    FLAGSHH,FHEAD
         LA    R1,L702                 ASSUME SOURCE CHANGES WERE MADE
         TM    #ATTROPT,X'FF'          CORRECT?
         BNZ   MSGNEWXX                YES, BRANCH
         SPACE 2
         MVI   RLDCOUNT,X'FF'          ASSUME NO RLD/CONTROL DATA
         MVI   LKEDDATE,X'FF'          ASSUME NO LKED DATE
         CLI   #RLDERR,X'01'           "RLDFIX" OPERAND?
         BE    ATTR024                 YES, BRANCH
         CLI   #LKEDOPT,X'02'          "NOLKEDDATE"?
         BE    ATTR030                 YES, BRANCH
         CLI   #LKEDOPT,X'01'          "LKEDDATE" DESIRED?
         BE    ATTR024                 YES, BRANCH
         TM    FLAGSGG,FLKEDCON        NO LKEDDATE DEFAULT?
         BO    ATTR030                 YES, BRANCH
ATTR024  L     R15,=A(READIDR)         FIRST RLD COUNT, LKED DATA
         BASR  R14,R15                 IDR DATA AVAILABLE?
         MVI   LKEDDATE,X'FF'          NO, ERROR
         SPACE 1
ATTR030  TM    #OPTOPT,X'F0'           ANY CHANGED ATTRIBUTES?
         BNO   ATTR250                 NO, BRANCH
         SPACE 1
         CLI   #RLDERR,X'01'           "RLDFIX"?
         BNE   ATTR038                 NO, BRANCH
         TM    DIRATTR2,DIRAOSLE       OS/VS LINKAGE-EDITOR?
         BNO   ATTR034                 NO, BRANCH
         CLI   RLDCOUNT,X'FF'          ANY RLD/CONTROL DATA FOUND?
         BE    ATTR034                 NO, BRANCH
         MVC   DIRATTR4(1),RLDCOUNT    MODIFY THE DIRECTORY ENTRY
         B     ATTR038
ATTR034  M$MSG L723                    NO CHANGE IS POSSIBLE
         SPACE 2
ATTR038  CLI   #AUTHOPT,X'01'          CHANGE APF PROCESSING?
         BL    ATTR060                 NO, BRANCH
         BE    ATTR040                 YES, BRANCH
         TM    DIRATTR2,DIRAOSLE+DIRAPFLG  NEED CHANGE APF INFORMATION?
         BNO   ATTR060                     NO, BRANCH
         SPACE 1
ATTR040  TM    DIRATTR2,DIRAOSLE+DIRAPFLG  CAN CHANGE APF INFORMATION?
         BNO   ATTR050                     NO, BRANCH
         LA    R2,DIRAPF                   YES, POINT TO APF DATA
         TM    DIRATTR,ATTRSCTR        SCATTER LOAD?
         BNO   *+8                     NO, BRANCH
         LA    R2,8(,R2)               SCATTER SIZE
         TM    DIRFLAG,X'80'           ALIAS?
         BO    ATTR044                 YES, BRANCH
***PDSMAN CHANGES: ATTRIB AUTH/NOAUTH                         ABL NOV86
         TM    5(R2),X'0F'             PDSMAN DATE?           ABL NOV86
         BO    ATTR046                 YES, BRANCH            ABL NOV86
         TM    6(R2),X'0F'             PDSMAN DATE?           ABL NOV86
         BO    ATTR046                 YES, BRANCH            ABL NOV86
         TM    4+5(R2),X'0F'           PDSMAN DATE AND SSI?   ABL NOV86
         BO    ATTR046                 YES, BRANCH            ABL NOV86
         TM    4+6(R2),X'0F'           PDSMAN DATE AND SSI?   ABL NOV86
         BO    ATTR046                 YES, BRANCH            ABL NOV86
         CLI   8(R2),0                 CONVERTED ALIAS ENTRY?
*        BE    *+8                     NO, BRANCH
         B     *+8                     BRANCH                 DRK JAN98
ATTR044  LA    R2,11(,R2)              ADD ALIAS LENGTH
ATTR046  DS    0H                                             ABL NOV86
         TM    DIRATTR2,DIR2SSI        SSI?
         BNO   *+12                    NO, BRANCH
         LA    R2,5(,R2)               ADD 4 AND ROUND TO HALFWORD
         N     R2,=F'-2'
         CLI   0(R2),1                 LENGTH RIGHT?
         BNE   ATTR050                 NO, BRANCH
         MVI   1(R2),1                 ASSUME TURN AUTHORIZATION ON
         CLI   #AUTHOPT,X'01'          CORRECT?
         BE    ATTR060                 YES, BRANCH
         MVI   1(R2),0                 NO, TURN OFF
         B     ATTR060
         SPACE 3
ATTR050  M$MSG L722                        INVALID APF DATA LENGTH
         SPACE 2
ATTR060  CLI   #PAGEOPT,X'01'              CHANGE PAGE ALIGNMENT?
         BL    ATTR100                     NO, BRANCH
         BE    ATTR070                     YES, BRANCH
         TM    DIRATTR2,DIRAOSLE+DIRAPFLG  NEED CHANGE PAGE INFO?
         BNO   ATTR100                     NO, BRANCH
         SPACE 1
ATTR070  TM    DIRATTR2,DIRAOSLE+DIRAPFLG  CAN CHANGE PAGE INFORMATION?
         BNO   ATTR080                     NO, BRANCH
         OI    DIRATTR2,DIR2PAGA           ASSUME TURN ON PAGE BOUNDARY
         CLI   #PAGEOPT,X'01'              CORRECT?
         BE    ATTR100                     YES, BRANCH
         NI    DIRATTR2,FF-DIR2PAGA        NO, TURN OFF PAGE BOUNDARY
         B     ATTR100
         SPACE 1
ATTR080  M$MSG L726                    PAGE ALIGNMENT CANNOT BE CHANGED
         SPACE 2
ATTR100  CLI   #RMODE,1                    ANY RMODE CHANGE?
         BL    ATTR120                     NO, BRANCH
         B     ATTR110                     YES, BRANCH        DRK OCT02
*        TM    DIRATTR2,DIRAOSLE           CAN CHANGE RMODE?  DRK OCT02
*        BNO   ATTR150                     NO, BRANCH         DRK OCT02
         SPACE 1
ATTR110  LA    R1,L724                     NOT MVS LKED MESSAGE
         TM    DIRATTR2,DIRAOSLE           CAN CHANGE RMODE?
         BNO   ATTR150                     NO, BRANCH
         NI    DIRATTR3,FF-DIRRMANY        SET RMODE=24
         CLI   #RMODE,1                    CORRECT?
         BE    ATTR120                     YES, BRANCH
         OI    DIRATTR3,DIRRMANY           NO, TURN ON RMODE ANY
         SPACE 2
ATTR120  CLI   #AMODE,1                    ANY AMODE CHANGE?
         BL    ATTR160                     NO, BRANCH
         B     ATTR130                     YES, BRANCH        DRK OCT02
*        TM    DIRATTR2,DIRAOSLE           CAN CHANGE AMODE?  DRK OCT02
*        BNO   ATTR160                     NO, BRANCH         DRK OCT02
         SPACE 1
ATTR130  LA    R1,L724                     NOT MVS LKED MESSAGE
         TM    DIRATTR2,DIRAOSLE           CAN CHANGE AMODE?
         BNO   ATTR150                     NO, BRANCH
         TM    DIRFLAG,X'80'               ALIAS ENTRY?
         BO    ATTR140                     YES, BRANCH
         OI    DIRATTR3,DIRAM64+DIRAM31    MAIN ENTRY -- AMODE=64+31
         XI    DIRATTR3,DIRAM31            SET AMODE=64       DRK OCT02
         CLI   #AMODE,4                    CORRECT?           DRK OCT02
         BE    ATTR160                     YES, BRANCH
         OI    DIRATTR3,DIRAM31            NO, SET AMODE=ANY
         CLI   #AMODE,3                    CORRECT?
         BE    ATTR160                     YES, BRANCH
         XI    DIRATTR3,DIRAM64            SET AMODE=31
         CLI   #AMODE,2                    CORRECT?           DRK OCT02
         BE    ATTR160                     YES, BRANCH        DRK OCT02
         NI    DIRATTR3,FF-DIRAM64-DIRAM31 SET AMODE=24       DRK OCT02
         B     ATTR160
         SPACE 1
ATTR140  OI    DIRATTR3,DIRAA64+DIRAA31    ALIAS ENTRY -- AMODE=64+31
         XI    DIRATTR3,DIRAA31            SET AMODE=64       DRK OCT02
         CLI   #AMODE,4                    CORRECT?           DRK OCT02
         BE    ATTR160                     YES, BRANCH
         OI    DIRATTR3,DIRAA31            NO, SET AMODE=ANY
         CLI   #AMODE,3                    CORRECT?
         BE    ATTR160                     YES, BRANCH
         XI    DIRATTR3,DIRAA64            SET AMODE=31
         CLI   #AMODE,2                    CORRECT?           DRK OCT02
         BE    ATTR160                     YES, BRANCH        DRK OCT02
         NI    DIRATTR3,FF-DIRAA64-DIRAA31 SET AMODE=24       DRK OCT02
         B     ATTR160
         SPACE 1
ATTR150  M$MSG (R1)                        AMODE/RMODE MESSAGE
         SPACE 2
ATTR160  OC    DIRATTR,#ATTRYES        ADD POSITIVE ATTRIBUTES
         NC    DIRATTR,#ATTRNO         REMOVE NEGATIVE ATTRIBUTES
         SPACE 1
*
*   RESTRICTION: BOTH RENT AND REUS MUST BE SPECIFIED FOR REENTRANT
         TM    DIRATTR,ATTRRENT        WAS REENTRABLE SPECIFIED?
         BNO   *+8                     NO, BRANCH
         OI    DIRATTR,ATTRREUS        YES, ALSO FORCE REUSABLE
         SPACE 1
ATTR180  CLI   #SSIOPT,0               SSI(HEXDATA) OR NOSSI?
         BE    ATTR216                 NO, BRANCH
         SR    R1,R1                   SSI INDICATOR
         LA    R2,DIRAPF               POINT TO APF INFORMATION
         TM    DIRATTR,ATTRSCTR        SCATTER FORMAT?
         BNO   *+8                     NO, BRANCH
         LA    R2,8(,R2)               YES, ADD SCATTER SIZE BYTES
         SPACE 1
         TM    DIRFLAG,X'80'           ALIAS?
         BO    ATTR182                 YES, BRANCH
***PDSMAN CHANGES: ATTRIB SSI(HEXDATA) OR NOSSI               ABL NOV86
         TM    5(R2),X'0F'             PDSMAN DATE?           ABL NOV86
         BO    ATTR183                 YES, BRANCH            ABL NOV86
         TM    6(R2),X'0F'             PDSMAN DATE?           ABL NOV86
         BO    ATTR183                 YES, BRANCH            ABL NOV86
         TM    4+5(R2),X'0F'           PDSMAN DATE AND SSI?   ABL NOV86
         BO    ATTR183                 YES, BRANCH            ABL NOV86
         TM    4+6(R2),X'0F'           PDSMAN DATE AND SSI?   ABL NOV86
         BO    ATTR183                 YES, BRANCH            ABL NOV86
         CLI   8(R2),0                 CONVERTED ALIAS ENTRY?
*        BE    *+8                     NO, BRANCH
         B     *+8                     BRANCH                 DRK JAN98
ATTR182  LA    R2,11(,R2)              ADD ALIAS LENGTH
ATTR183  DS    0H                                             ABL NOV86
         SPACE 1
         LR    R3,R2                   START OF DATA BEFORE ANY SSI
         LA    R2,1(,R2)               ROUND UP TO HALFWORD
         N     R2,=F'-2'
         TM    DIRATTR2,DIRAOSLE       VS LINKAGE EDITOR?
         BO    ATTR184                 YES, BRANCH
         SPACE 1
         CLC   ZERO,0(R2)              ZERO?
         BE    ATTR188                 YES, NO SSI
         CLC   =F'-1',0(R2)            FFFFFFFF?
         BE    ATTR188                 YES, NO SSI
         B     ATTR186                 NO, POINT TO SSI
         SPACE 1
ATTR184  TM    DIRATTR2,DIR2SSI        SSI PRESENT?
         BNO   ATTR188                 NO, NO SSI
         SPACE 1
ATTR186  LR    R1,R2                   POINT TO SSI
ATTR188  LR    R15,R2                  SSI POSITION
         SR    R15,R3                  OFFSET FROM LAST DATA TO SSI
         LA    R15,2(,R15)             ADJUSTMENT AMOUNT
         CLI   #SSIOPT,1               SSI(HEXDATA)?
         BE    ATTR200                 YES, BRANCH
         SPACE 1
*** NOSSI
         LTR   R1,R1                   ANY SSI?
         BZ    ATTR216                 NO, DONE
         ICM   R0,B'0011',4(R2)        APF BYTES
         XC    0(8,R3),0(R3)           CLEAR FOLLOWING BYTES
         STCM  R0,B'0011',0(R3)        MOVE APF BEFORE THE SSI
         TM    DIRATTR2,DIRAOSLE       VS LINKAGE EDITOR?
         BNO   *+8                     NO, BRANCH
         NI    DIRATTR2,FF-DIR2SSI     NO SSI PRESENT
         LA    R3,2+1(,R3)             2 FOR APF AND 1 FOR ROUNDING
         B     ATTR214
         SPACE 2
ATTR200  LTR   R1,R1                   SSI ALREADY?
         BZ    ATTR210                 NO, BRANCH
*** SSI(HEXTEXT) WITH EXISTING SSI INFORMATION
         MVC   0(4,R2),#SSITEXT        UPDATE THE SSI DATA
         B     ATTR216
         SPACE 2
*** SSI(HEXTEXT) WITH NO PREVIOUS SSI INFORMATION
ATTR210  ICM   R0,B'0011',0(R3)        APF BYTES
         MVI   0(R3),0                 CLEAR THE FIRST APF BYTE
         MVC   0(4,R2),#SSITEXT        MOVE IN ADDED SSI DATA
         STCM  R0,B'0011',4(R2)        RESET THE APF INFORMATION
         TM    DIRATTR2,DIRAOSLE       VS LINKAGE EDITOR?
         BNO   *+8                     NO, BRANCH
         OI    DIRATTR2,DIR2SSI        SSI PRESENT
         LA    R3,4+2(,R2)             4 FOR SSI AND 2 FOR APF
         SPACE 1
ATTR214  LA    R0,DIRSTART             FIRST BYTE AFTER DIRFLAG
         SR    R3,R0                   LENGTH OF ADDED DATA
         SRL   R3,1                    IN HALFWORDS
         NI    DIRFLAG,FF-X'1F'        REMOVE LENGTH BITS
         IC    R0,DIRFLAG              PICK UP ALIAS FLAG, TTR COUNT
         OR    R0,R3                   ADD IN NEW LENGTH IN HALFWORDS
         STC   R0,DIRFLAG              UPDATE DIRECTORY FLAG ENTRY
         SPACE 2
ATTR216  CLI   #UNALIAS,1              UNALIAS?
         BNE   ATTR220                 NO, BRANCH
         TM    DIRFLAG,X'80'           ALIAS?
         BNO   ATTR220                 NO, BRANCH
         XI    DIRFLAG,X'80'           TURN OFF THE ALIAS FLAG
         SR    R1,R1
         LA    R3,DIRAPF               NORMAL APF LOCATION
         TM    DIRATTR,ATTRSCTR        SCATTER LOAD?
         BNO   *+8                     NO, BRANCH
         LA    R3,8(,R3)               YES, ADD 8
         LA    R1,11(,R3)              POINT AFTER THE ALIAS INFO.
         TM    DIRATTR2,DIRAOSLE       VS LKED?
         BNO   ATTR218                 NO, BRANCH
         TM    DIRATTR2,DIR2SSI        SSI PRESENT?
         BNO   ATTR217                 NO, BRANCH
         MVI   0(R3),0                 CLEAR THE FIRST BYTE
         LA    R3,1(,R3)               SET FOR ROUNDING
         N     R3,=F'-2'               ROUND TO HALFWORD
         LA    R1,1(,R1)               POINT TO SSI
         N     R1,=F'-2'               ROUND TO HALFWORD
ATTR217  MVC   0(8,R3),0(R1)           SLIDE DOWN DATA
         LA    R3,2+1(,R3)             ACCOUNT FOR APF
         TM    DIRATTR2,DIR2SSI        SSI PRESENT?
         BNO   ATTR214                 NO, BRANCH
         LA    R3,4(,R3)               ACCOUNT FOR APF
         B     ATTR214                 UPDATE DIRECTORY ENTRY LENGTH
         SPACE 1
ATTR218  LA    R3,1(,R3)
         LA    R14,1(,R1)              ROUND TO
         N     R14,=F'-2'                      NEXT HALFWORD
         CLC   ZERO,0(R14)             ZERO?
         BE    ATTR214                 YES, NO SSI
         CLC   =F'-1',0(R14)           FFFFFFFF?
         BE    ATTR214                 YES, NO SSI
         N     R3,=F'-2'               ROUND TO HALFWORD
         MVC   0(8,R3),0(R14)          SLIDE DOWN DATA
         LA    R3,4(,R3)               ADD FOR SSI
         B     ATTR214                 UPDATE DIRECTORY ENTRY LENGTH
         SPACE 2
ATTR220  CLI   #MODTXT,X'41'              ANY ENTRY(EXTNAME)?
         BL    ATTR240                    NO, BRANCH
         MVC   #USERTXT(8),DIRNAME        SAVE THE CURRENT MEMBER NAME
         MVC   DIRNAME(8),#MODTXT         NAME TO SEARCH FOR IN ESD
         SPACE 1
         L     R15,=A(READESD)            SCAN ESD
         BASR  R14,R15                    EXTERNAL NAME IN ESD ENTRIES?
         NOP   0(,R0)                     NO, IGNORE
         CLC   ENTRYPT(8),#MODTXT         NAME MATCH?
         BE    ATTR222                    YES, BRANCH
         SPACE 1
         MVC   DIRNAME(8),#USERTXT        RESTORE THE MEMBER NAME
         MVC   INSERT#1(8),#MODTXT        EXTERNAL NAME
         M$MSG L705$1
         B     ATTR240
         SPACE 1
ATTR222  MVC   DIRNAME(8),#USERTXT        RESTORE THE MEMBER NAME
         OI    DIRATTR+1,ATTREP0          ASSUME ENTRY POINT ZERO
         LTR   R1,R1                      CORRECT?
         BZ    *+8                        YES, BRANCH
         XI    DIRATTR+1,ATTREP0          NO, INSURE ATTR FLAG OFF
         STCM  R1,B'0111',DIREPA          SAVE ENTRY ADDRESS
         TM    DIRATTR,ATTRSCTR           SCATTER LOADED?
         BNO   *+8                        NO, BRANCH
         STCM  R15,B'0011',DIRSCEP        YES, SAVE ESDID OF ENTRY PT.
         STC   R0,#ALIAESD                SAVE MODE RMODE/AMODE INFO
         TM    DIRATTR,ATTROVLY           OVERLAY ATTRIBUTES
         BO    ATTR240                    YES, BRANCH
         OC    #RMODE(2),#RMODE           RMODE/AMODE CHANGES ALREADY?
         BNZ   ATTR240                    YES, BRANCH
         TM    DIRATTR2,DIRAOSLE          CAN CHANGE AMODE?
         BNO   ATTR240                    NO, BRANCH
         OI    #RMODE,1                   MARK RMODE/AMODE CHANGE
         SPACE 1
         TM    DIRFLAG,X'80'              ALIAS ENTRY?
         BO    ATTR224                    YES, BRANCH
         NI    DIRATTR3,FF-DIRRMANY       SET RMODE=24
         TM    #ALIAESD,RMODEANY          CORRECT?
         BZ    *+8                        YES, BRANCH
         OI    DIRATTR3,DIRRMANY          NO, TURN ON RMODE ANY
         SPACE 2
         NI    DIRATTR3,FF-DIRAM64-DIRAM31  MAIN ENTRY -- AMODE=64+31
         TM    #ALIAESD,AMODE64           AMODE 64?
         BNO   *+8                        NO, BRANCH
         OI    DIRATTR3,DIRAM64           YES, SET THE DIRECTORY
         TM    #ALIAESD,AMODE31           AMODE 31?
         BNO   *+8                        NO, BRANCH
         OI    DIRATTR3,DIRAM31           YES, SET THE DIRECTORY
         B     ATTR240
         SPACE 1
ATTR224  NI    DIRATTR3,FF-DIRAA64-DIRAA31  ALIAS ENTRY -- AMODE=64+31
         TM    #ALIAESD,AMODE64           AMODE 64?
         BNO   *+8                        NO, BRANCH
         OI    DIRATTR3,DIRAA64           YES, SET THE DIRECTORY
         TM    #ALIAESD,AMODE31           AMODE 31?
         BNO   *+8                        NO, BRANCH
         OI    DIRATTR3,DIRAA31           YES, SET THE DIRECTORY
         SPACE 2
ATTR240  DS    0H
         ST    R6,R6SAVE               SAVE 2ND BASE          DRK AUG05
         BAS   R2,OPENSTOW             OPEN STOW DCB; ENQUEUES
         B     NEWCMD                  COULD NOT OPEN -- ERROR
         L     R6,R6SAVE               RESTORE 2ND BASE       DRK AUG05
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR
         STOW  STOWDCB,DIRNAME,R       REPLACE OPTION
         LR    R5,R15                  SAVE RETURN CODE
***      BAS   R2,SHUTSTOW             *** CLOSE STOW AFTER NEWCMD
         SPACE 1
         CH    R15,=H'12'              RC > 12                DRK AUG05
         BH    IOERROR                 YES, ERROR CONDITION   DRK AUG05
         B     *+4(R5)                 PROCESS RETURN CODE
         B     ATTR250                   00 - SUCCESSFUL
         EX    0,*                       04 - SHOULD NOT OCCUR
         EX    0,*                       08 - SHOULD NOT OCCUR
         B     FULLDIR                   12 - DIRECTORY FULL
*        B     IOERROR                   16 - I/O ERROR
         SPACE 3
ATTR250  TM    FLAGSCC,RECFMU          LOAD LIBRARY?
         BNO   ATTR600                 NO, CHECK FOR ALIASES
         TM    DIRFLAG,X'80'           ALIAS?
         BO    ATTR260                 YES, BRANCH
         SR    R0,R0
         ICM   R0,B'0111',DIRSTORE LOAD MODULE
         CVD   R0,DOUBLE           CONVERT TO DECIMAL
         TM    DIRATTR3,DIRRMANY       RMODE ANY?
         BO    ATTR254                 YES, BRANCH
         L     R1,ATTRCNT2
         LA    R1,1(,R1)               ONE MORE ENTRY
         ST    R1,ATTRCNT2             NUMBER OF ENTRIES
         OI    ATTRTOT2+7,X'0F'        INSURE VALID PACKED SIGN
         AP    ATTRTOT2(8),DOUBLE(8)   ADD IN CURRENT VALUE
         B     ATTR260
ATTR254  L     R1,ATTRCNTA
         LA    R1,1(,R1)               ONE MORE ENTRY
         ST    R1,ATTRCNTA             NUMBER OF ENTRIES
         OI    ATTRTOTA+7,X'0F'        INSURE VALID PACKED SIGN
         AP    ATTRTOTA(8),DOUBLE(8)   ADD IN CURRENT VALUE
ATTR260  MVC   INSERT#1(8),DIRNAME     MEMBER NAME
         MVC   INSERT#2(128),BLANK128
         LA    R1,INSERT#2-1           FIRST COMMA ADDRESS (HIDDEN)
         MVC   INSERT#2+1(4),=C'NONE'  IF NO SPECIAL ATTRIBUTES
         LA    R14,TBLATTR-12          ATTRIBUTES TABLE
         LH    R2,DIRATTR              GET MODULE ATTRIBUTES
         SPACE 2
ATTR300  LA    R14,12(R14)
         CLI   0(R14),X'FF'            END OF TABLE?
         BE    ATTR310                 YES, BRANCH
         SPACE 1
         LH    R15,0(R14)              GET ATTRIBUTE FLAGS
         TM    2(R14),X'80'            POSITIVE OR NEGATIVE ATTRIBUTE?
         LA    R3,X'80'                MASK FOR POS (FOR BZ INST)
         BZ    *+8
         LA    R3,X'70'                MASK FOR NEG (FOR BNZ) INST
         NR    R15,R2                  CHECK FOR ATTRIBUTE PRESENT
         NOP   ATTR300                 <<BZ OR BNZ>>
         EX    R3,*-4                  BRANCH IF NO DISPLAY ATTRIBUTE
         SPACE 1
         MVC   0(6,R1),ATTRCOMM        COMMA AND FOLLOWING BLANKS
         IC    R15,2(R14)              LENGTH-1 OF ATTRIBUTE
         LA    R3,X'7F'                MASK FOR 7 BITS
         NR    R15,R3
         MVC   2(*-*,R1),3(R14)        <<EXECUTED>>
         EX    R15,*-6
         LA    R1,3(R15,R1)            JUMP POINTER
         B     ATTR300
         SPACE 2
ATTR310  DS    0H
         L     R1,AATTS310
         CLI   #ALIAOPT,X'03'          SHORT FORMAT DESIRED?
         BER   R1                      YES, BRANCH
         MVI   MTLEN,8                 RESET THE INSERT LENGTH GP JAN08
         MVI   MTLEN+4,127
         M$MSG L020$2                  ATTRIBUTES MSG
         MVI   MTLEN+4,8
         SPACE 2
         LA    R2,DIRAPF               POINT TO APF INFORMATION
         SPACE 1
         TM    DIRATTR,ATTRSCTR        SCATTER FORMAT?
         BNO   *+8                     NO, BRANCH
         LA    R2,8(,R2)               YES, ADD SCATTER SIZE BYTES
         SPACE 1
         TM    DIRFLAG,X'80'           ALIAS?
         BO    ATTR314                 YES, BRANCH
***PDSMAN CHANGES: ATTRIB FOR LISTING SSI AND APF CODE        ABL NOV86
         TM    5(R2),X'0F'             PDSMAN DATE?           ABL NOV86
         BO    ATTR320                 YES, BRANCH            ABL NOV86
         TM    6(R2),X'0F'             PDSMAN DATE?           ABL NOV86
         BO    ATTR320                 YES, BRANCH            ABL NOV86
         TM    4+5(R2),X'0F'           PDSMAN DATE AND SSI?   ABL NOV86
         BO    ATTR320                 YES, BRANCH            ABL NOV86
         TM    4+6(R2),X'0F'           PDSMAN DATE AND SSI?   ABL NOV86
         BO    ATTR320                 YES, BRANCH            ABL NOV86
         CLI   8(R2),0                 CONVERTED ALIAS ENTRY?
*        BE    *+8                     NO, BRANCH
         B     *+8                     BRANCH                 DRK JAN98
ATTR314  LA    R2,11(,R2)              ADD ALIAS LENGTH
ATTR320  DS    0H                                             ABL NOV86
         SPACE 1
         TM    DIRATTR2,DIRAOSLE       VS LINKAGE EDITOR?
         BO    ATTR330                 YES, BRANCH
         M$MSG L022                    NON-VS LINKAGE EDITOR MESSAGE
         B     ATTR340
         SPACE 1
ATTR330  TM    DIRATTR2,DIR2SSI        SSI PRESENT?
         BNO   ATTR360                 NO, SKIP SSI PROCESSING
         LA    R2,1(,R2)               ROUND UP TO HALFWORD
         N     R2,=F'-2'
         B     ATTR341                 SKIP ZERO AND FFFFFFFF TESTS
         SPACE 1
ATTR340  LA    R2,1(,R2)               ROUND UP TO HALFWORD
         N     R2,=F'-2'
         SPACE 1
         CLC   ZERO,0(R2)              ZERO?
         BE    ATTR360                 YES, NO SSI
         CLC   =F'-1',0(R2)            FFFFFFFF?
         BE    ATTR360                 YES, NO SSI
         SPACE 1
ATTR341  UNPK  INSERT#1(9),0(5,R2)
         TR    INSERT#1(8),TRTABLE
         M$MSG L025$1
         LA    R2,4(,R2)               ADD SSI SIZE
         SPACE 2
ATTR360  TM    FLAGSCC,RECFMU          LOAD MODULE LIB?
         BZ    ATTR600                 NO, BRANCH
         SPACE 1
         MVI   MODESAVE,0
         TM    DIRATTR2,DIRAOSLE           VS LINKAGE EDITOR?
         BZ    ATTR532                     NO, NOT AUTH.      ABL NOV86
         SR    R1,R1
         IC    R1,DIRATTR3                 AMODE BITS
         TM    DIRFLAG,X'80'               ALIAS?
         BNO   *+8                         NO, BRANCH
         SRL   R1,2                        YES, SHIFT AMODE BITS
         STC   R1,MODESAVE                 SAVE CURRENT AMODE
* RMODE64
         TM    DIRATTR3,DIRRMANY+DIRRM64   RMODE 64?          DRK APR18
         BNO   ATT$500                     NO, BRANCH                 "
         OI    MODESAVE,DIRRMANY+DIRRM64   YES, SET RMODE AGAIN       "
         SPACE 1
         MVC   INSERT#1(8),BLANKS          RMODE FILLER
         MVC   INSERT#2(8),BLANKS          AMODE FILLER
         MVC   INSERT#1(3),=C'64 '         MOVE IN THE RMODE TEXT     "
         SPACE 1
         TM    MODESAVE,DIRAM64         01 AMODE 64?          DRK APR18
         BNO   *+10                        NO, BRANCH
         MVC   INSERT#2(3),=C'64 '         MOVE IN THE AMODE TEXT OCT02
         TM    MODESAVE,DIRAM31         02 AMODE 31?
         BNO   *+10                        NO, BRANCH
         MVC   INSERT#2(3),=C'31 '         MOVE IN THE AMODE TEXT
         TM    MODESAVE,DIRAM64+DIRAM31 03 AMODE ANY?         DRK OCT02
         BNO   *+10                        NO, BRANCH
         MVC   INSERT#2(3),=C'ANY'         MOVE IN THE AMODE TEXT
         TM    MODESAVE,DIRAM31+DIRAM64 00 AMODE 24?          DRK APR18
         BNZ   *+10                        NO, BRANCH         DRK APR18
         MVC   INSERT#2(3),=C'24 '         MOVE IN THE AMODE TEXT     "
         M$MSG L120$2
         SPACE 1
         TM    MODESAVE,DIRRMANY+DIRRM64   RMODE 64?          DRK APR18
         BNO   ATT$400                     NO, BRANCH
         LA    R1,=C'ANY'                  ASSUME AMODE IS ANY
         TM    MODESAVE,DIRAM64+DIRAM31    AMODE ANY?
         BO    ATT$370                     YES, INVALID
         LA    R1,=C'31 '                  ASSUME AMODE IS 31
         TM    MODESAVE,DIRAM31            AMODE 31?
         BO    ATT$370                     YES, INVALID
         TM    MODESAVE,DIRAM64            AMODE 64?          DRK OCT02
         BO    ATT$400                     YES, BRANCH        DRK OCT02
         LA    R1,=C'24 '                  AMODE IS 24
ATT$370  DS    0H
         MVC   INSERT#1(8),BLANKS
         MVC   INSERT#1(3),0(R1)           MOVE IN THE AMODE TEXT
         M$MSG L879$1                      RMODE 64/AMODE CONFLICT
         SPACE 2
ATT$400  DS    0H
         B     ATTR400                                        DRK APR18
ATT$500  DS    0H
         TM    DIRATTR3,DIRRMANY           RMODE ANY?
         BNO   *+8                         NO, BRANCH
         OI    MODESAVE,DIRRMANY           YES, SET RMODE AGAIN
         SPACE 1
         TM    MODESAVE,DIRRMANY+DIRAM31+DIRAM64 UNUSUAL RMODE/AMODE?
         BZ    ATTR400                     NO, BRANCH
         MVC   INSERT#1(8),BLANKS          RMODE FILLER
         MVC   INSERT#2(8),BLANKS          AMODE FILLER
         MVC   INSERT#1(3),=C'24 '         MOVE IN THE RMODE TEXT
         TM    MODESAVE,DIRRMANY           RMODE ANY?
         BNO   *+10                        NO, BRANCH
         MVC   INSERT#1(3),=C'ANY'         MOVE IN THE RMODE TEXT
         SPACE 1
         TM    MODESAVE,DIRAM64         01 AMODE 64?          DRK APR18
         BNO   *+10                        NO, BRANCH
         MVC   INSERT#2(3),=C'64 '         MOVE IN THE AMODE TEXT OCT02
         TM    MODESAVE,DIRAM31         02 AMODE 31?
         BNO   *+10                        NO, BRANCH
         MVC   INSERT#2(3),=C'31 '         MOVE IN THE AMODE TEXT
         TM    MODESAVE,DIRAM64+DIRAM31 03 AMODE ANY?         DRK OCT02
         BNO   *+10                        NO, BRANCH
         MVC   INSERT#2(3),=C'ANY'         MOVE IN THE AMODE TEXT
         TM    MODESAVE,DIRAM31+DIRAM64 00 AMODE 24?          DRK APR18
         BNZ   *+10                        NO, BRANCH         DRK APR18
         MVC   INSERT#2(3),=C'24 '         MOVE IN THE AMODE TEXT     "
         M$MSG L120$2
         TM    MODESAVE,DIRRMANY           RMODE ANY?
         BZ    ATTR400                     NO, BRANCH
         LA    R1,=C'ANY'                  ASSUME AMODE IS ANY
         TM    MODESAVE,DIRAM64+DIRAM31 03 AMODE ANY?
         BO    ATTR370                     YES, INVALID
         TM    MODESAVE,DIRAM31         02 AMODE 31?
         BO    ATTR400                     YES, BRANCH
         TM    MODESAVE,DIRAM64         01 AMODE 64?          DRK OCT02
         BO    ATTR400                     YES, BRANCH        DRK OCT02
         LA    R1,=C'24 '               00 AMODE IS 24
ATTR370  MVC   INSERT#1(8),BLANKS
         MVC   INSERT#1(3),0(R1)           MOVE IN THE AMODE TEXT
         M$MSG L880$1                      RMODE ANY/AMODE CONFLICT
         SPACE 2
ATTR400  CLI   RLDCOUNT,X'FF'          ANY DATA TO CHECK?
         BE    ATTR500                 NO, BRANCH
         CLC   RLDCOUNT(1),DIRATTR4    RLD/CONTROL AMOUNTS AGREE?
         BE    ATTR500                 YES, BRANCH
         TM    DS1SMSFG,DS1PDSE        PDSE DATASET?          DRK AUG05
         BO    ATTR500                 YES, NO MESSAGE        DRK AUG05
         M$MSG L826
         SPACE 2
ATTR500  TM    DIRATTR2,DIR2PAGA       PAGE ALIGNMENT REQUIRED?
         BNO   ATTR510                 NO, BRANCH
         M$MSG L024                    PAGE ALIGNMENT REQUIRED
         SPACE 2
ATTR510  LA    R1,L720                 ASSUME NO APF DATA
         TM    DIRATTR2,DIRAPFLG       APF DATA PRESENT AND VALID?
         BO    ATTR520                 YES, CHECK LENGTH      DRK AUG05
         TM    DS1SMSFG,DS1PDSE        PDSE DATASET?          DRK AUG05
         BNO   ATTR530                 NO, ISSUE MESSAGE      DRK AUG05
         B     ATTR532                 YES, NO MESSAGE        DRK AUG05
         SPACE 1                                              DRK AUG05
ATTR520  DS    0H                                             DRK AUG05
         LA    R1,L721                 ASSUME APF LENGTH INCORRECT
         CLI   0(R2),1                 APF DATA LENGTH OK?
         BNE   ATTR530                 NO, BRANCH
         SPACE 3
         LA    R1,L021                 ASSUME AUTHORIZED
         CLI   1(R2),1                 AUTHORIZED?
         BE    ATTR530                 YES, BRANCH
         BL    ATTR532                 NO, APF=0 (NO MESSAGE) ABL NOV86
         LA    R1,L023                 APF DATA IS TOO LARGE
         SPACE 1
ATTR530  M$MSG (R1)
         SPACE 2
ATTR532  LA    R2,3(,R2)               SKIP APF CODE          ABL NOV86
         N     R2,=F'-2'               HALFWORD BOUNDARY      ABL NOV86
***PDSMAN CHANGES: ATTRIB FOR LISTING PDSMAN ATTRIBUTES       ABL NOV86
         TM    3(R2),X'0F'             PDSMAN DATE? 0CYYDDDF  ABL NOV86
         BNO   ATTR540                 NO, BRANCH             ABL NOV86
         TM    0(R2),X'FE'             PDSMAN DATE? 0CYYDDDF  DRK JAN98
         BNZ   ATTR540                 NO, BRANCH             DRK JAN98
         TM    8(R2),X'0F'             PDSMAN DATE?   YYDDDF  DRK FEB98
         BNO   ATTR540                 NO, BRANCH             DRK FEB98
         TM    21(R2),X'0F'            PDSMAN DATE?   YYDDDF  DRK FEB98
         BNO   ATTR540                 NO, BRANCH             DRK FEB98
         MVC   INSERT#1(66),BLANK128   BLANK THE INSERT AREA  ABL NOV86
         MVI   MTLEN,51                LENGTH OF INSERT       ABL NOV86
         LA    R1,1(,R2)                                      ABL NOV86
         LA    R15,FULLWORD            RESULT                 ABL NOV86
         BAS   R14,CONVDATE            CONVERT TO MMDDYY      ABL NOV86
         MVC   INSERT#1(L'DATEMASK),DATEMASK                  ABL NOV86
         ED    INSERT#1(L'DATEMASK),FULLWORD                  ABL NOV86
         AIF   ('&ISODT'  NE 'YES').NOISOA1                   DRK NOV01
         SPACE 1
         MVC   DOUBLE(8),INSERT#1+1      MM/DD/YY             DRK NOV01
         ICM   R0,B'1100',DOUBLE+6       YY OF MM/DD/YY       DRK NOV01
         MVC   DOUBLE+6(2),DOUBLE+3      DD TO ../../DD       DRK NOV01
         MVC   DOUBLE+3(2),DOUBLE        MM TO ../MM/DD       DRK NOV01
         STCM  R0,B'1100',DOUBLE         YY TO YY/MM/DD       DRK NOV01
         OI    DOUBLE+3,X'F0'            LEADIN BLANK IN MM   DRK NOV01
         MVC   INSERT#1+1(8),DOUBLE      YY/MM/DD             DRK NOV01
         SPACE 1
.NOISOA1 ANOP                                                 DRK NOV01
         SPACE 1                                              ABL NOV86
         MVC   INSERT#1+09(6),=X'4021207A2020'                ABL NOV86
         ED    INSERT#1+09(6),4(R2)    CONVERT TO HH:MM       ABL NOV86
         SPACE 1                                              ABL NOV86
         SR    R1,R1                                          ABL NOV86
         IC    R1,4+2+3(R2)            MODIFICATION NUMBER    ABL NOV86
         CVD   R1,DOUBLE                                      ABL NOV86
         MVC   DOUBLE(4),=X'40202120'                         ABL NOV86
         ED    DOUBLE(4),DOUBLE+6      CONVERT                ABL NOV86
         MVC   DOUBLE+4(4),BLANKS                             ABL NOV86
         LA    R1,DOUBLE                                      ABL NOV86
         LA    R1,1(,R1)               FIND                   ABL NOV86
         CLI   0(R1),X'40'                 FIRST              ABL NOV86
         BE    *-8                              NON-BLANK     ABL NOV86
         MVC   INSERT#1+20(3),0(R1)    MODIFICATION NUMBER    ABL NOV86
         MVC   INSERT#1+16(4),ATTRLEV  LEV:                   ABL NOV86
         SPACE 1                                              ABL NOV86
         LA    R1,4+2+3+1(R2)          LAST MODIFIER          ABL NOV86
         MVC   INSERT#1+27(8),0(R1)                           ABL NOV86
         MVC   INSERT#1+24(2),ATTRBY   BY                     ABL NOV86
         SPACE 1                                              ABL NOV86
         LA    R1,4+2(,R2)             EXPIRATION DATE        ABL NOV86
         LA    R15,FULLWORD            RESULT                 ABL NOV86
         BAS   R14,CONVDATE            CONVERT TO MMDDYY      ABL NOV86
         MVC   INSERT#1+42(L'DATEMASK),DATEMASK               ABL NOV86
         ED    INSERT#1+42(L'DATEMASK),FULLWORD               ABL NOV86
         AIF   ('&ISODT'  NE 'YES').NOISOA2                   DRK NOV01
         SPACE 1
         MVC   DOUBLE(8),INSERT#1+42+1   MM/DD/YY             DRK NOV01
         ICM   R0,B'1100',DOUBLE+6       YY OF MM/DD/YY       DRK NOV01
         MVC   DOUBLE+6(2),DOUBLE+3      DD TO ../../DD       DRK NOV01
         MVC   DOUBLE+3(2),DOUBLE        MM TO ../MM/DD       DRK NOV01
         STCM  R0,B'1100',DOUBLE         YY TO YY/MM/DD       DRK NOV01
         OI    DOUBLE+3,X'F0'            LEADIN BLANK IN MM   DRK NOV01
         MVC   INSERT#1+42+1(8),DOUBLE   YY/MM/DD             DRK NOV01
         SPACE 1
.NOISOA2 ANOP                                                 DRK NOV01
         MVC   INSERT#1+36(7),ATTREXPI EXPIRE:                ABL NOV86
         SPACE 1                                              ABL NOV86
         LA    R1,4+2+3+1+8+1(R2)      DATE MEMBER LAST READ  ABL NOV86
         CLC   0(3,R1),ATTRNODA        ANY VALID DATE?        ABL NOV86
         BE    ATTR534                 NO, BRANCH             ABL NOV86
         MVI   MTLEN,66                LENGTH OF INSERT       ABL NOV86
         LA    R15,FULLWORD            RESULT                 ABL NOV86
         BAS   R14,CONVDATE            CONVERT TO MMDDYY      ABL NOV86
         MVC   INSERT#1+56(L'DATEMASK),DATEMASK               ABL NOV86
         ED    INSERT#1+56(L'DATEMASK),FULLWORD               ABL NOV86
         AIF   ('&ISODT'  NE 'YES').NOISOA3                   DRK NOV01
         SPACE 1
         MVC   DOUBLE(8),INSERT#1+56+1   MM/DD/YY             DRK NOV01
         ICM   R0,B'1100',DOUBLE+6       YY OF MM/DD/YY       DRK NOV01
         MVC   DOUBLE+6(2),DOUBLE+3      DD TO ../../DD       DRK NOV01
         MVC   DOUBLE+3(2),DOUBLE        MM TO ../MM/DD       DRK NOV01
         STCM  R0,B'1100',DOUBLE         YY TO YY/MM/DD       DRK NOV01
         OI    DOUBLE+3,X'F0'            LEADIN BLANK IN MM   DRK NOV01
         MVC   INSERT#1+56+1(8),DOUBLE   YY/MM/DD             DRK NOV01
         SPACE 1
.NOISOA3 ANOP                                                 DRK NOV01
         MVC   INSERT#1+52(5),ATTRREAD READ:                  ABL NOV86
ATTR534  DS    0H                                             ABL NOV86
         M$MSG L026$1                                         ABL NOV86
         MVI   MTLEN,8                                        ABL NOV86
         SPACE 2                                              ABL NOV86
ATTR540  TM    DIRATTR,ATTROVLY                OVERLAY DEFINED?
         BNO   ATTR550                         NO, BRANCH
         MVC   INSERT#1(8),BLANKS              BLANK FILL
         MVC   INSERT#1(4),REUSTXT+3           OVERLAY AND REENTRANT
         TM    DIRATTR,ATTRRENT                CORRECT?
         BNO   ATTR542                         NO, BRANCH
         M$MSG L884$1                          YES, ERROR
ATTR542  MVC   INSERT#1(4),REUSTXT+3           OVERLAY AND REUSABLE
         TM    DIRATTR,ATTRREUS                CORRECT?
         BNO   ATTR543                         NO, BRANCH
         M$MSG L884$1                          YES, ERROR
ATTR543  MVC   INSERT#1(4),REFRTXT+3           OVERLAY AND REFRESHABLE
         TM    DIRATTR+1,ATTRREFR              CORRECT?
         BNO   ATTR544                         NO, BRANCH
         M$MSG L884$1                          YES, ERROR
ATTR544  MVC   INSERT#1(4),SCTRTXT+3           OVERLAY AND SCATTER
         TM    DIRATTR,ATTRSCTR                CORRECT?
         BNO   ATTR545                         NO, BRANCH
         M$MSG L884$1                          YES, ERROR
ATTR545  TM    DIRATTR2,DIRAOSLE               MVS LINKAGE-EDITOR?
         BNO   ATTR550                         NO, BRANCH
         MVC   INSERT#1(5),=C'RMODE'
         MVC   INSERT#1+6(3),=C'ANY'           RMODE ANY
         TM    MODESAVE,DIRRMANY               CORRECT?
         BNO   ATTR546                         NO, BRANCH
         M$MSG L884$1                          YES, ERROR
ATTR546  MVI   INSERT#1,C'A'                   AMODE ANY
         TM    MODESAVE,DIRAM31+DIRAM64        AMODE ANY?     DRK OCT02
         BO    ATTR548                         YES, ERROR     DRK OCT02
         MVC   INSERT#1+6(3),=C'31 '           AMODE 31       DRK OCT02
         TM    MODESAVE,DIRAM31                AMODE 31?      DRK OCT02
         BO    ATTR548                         YES, ERROR     DRK OCT02
         MVC   INSERT#1+6(3),=C'64 '           AMODE 64       DRK OCT02
         TM    MODESAVE,DIRAM64                AMODE 64?      DRK OCT02
         BO    ATTR548                         YES, ERROR     DRK OCT02
         B     ATTR550                         NO, BRANCH     DRK OCT02
ATTR548  DS    0H                                             DRK OCT02
         M$MSG L884$1                          YES, ERROR
         SPACE 1
ATTR550  TM    DIRATTR+1,ATTRSYMS+ATTRNE       TEST AND NOT EDIT?
         BNO   ATTR560                         NO, BRANCH
         M$MSG L882                            YES, ERROR
         SPACE 1
ATTR560  TM    DIRATTR,ATTRRENT                REENTRANT?
         BNO   ATTR570                         NO, BRANCH
         TM    DIRATTR,ATTRREUS                REUSABLE?
         BO    ATTR570                         YES, BRANCH
         M$MSG L881                            ERROR MESSAGE
         SPACE 1
ATTR570  TM    DIRATTR,ATTRREUS+ATTRSCTR       REUSABLE AND SCATTER?
         BNO   ATTR590                         NO, BRANCH
         M$MSG L883                            YES, ERROR
         SPACE 2
ATTR590  UNPK  INSERT#1(7),DIREPA(4)
         TR    INSERT#1(6),TRTABLE
         MVC   INSERT#1+6(2),BLANKS
         MVI   MTLEN,8                RESET THE INSERT LENGTH  GP JAN08
         M$MSG L102$1                 NO ENTRY POINT MESSAGE
         SPACE 1
         UNPK  INSERT#1(7),DIRSTORE(4)
         TR    INSERT#1(6),TRTABLE
         MVC   INSERT#1+6(2),BLANKS
         ICM   R1,B'0111',DIRSTORE    ACTUAL MODULE LENGTH
         LA    R1,1023(,R1)           NEXT HIGHER
         SRL   R1,10                             1K VALUE
         CVD   R1,DOUBLE
         MVC   INSERT#2(8),=X'402020202120D2404040'
         ED    INSERT#2(6),DOUBLE+5
         M$MSG L104$2
         SPACE 2
         CLI   LKEDDATE,X'FF'          ANY DATA TO OUTPUT?
         BE    ATTR600                 NO, BRANCH
         CLI   #LKEDOPT,X'02'          "NOLKEDDATE" DESIRED?
         BE    ATTR600                 YES, BRANCH
         CLI   #LKEDOPT,X'01'          "LKEDDATE" DESIRED?
         BE    *+12                    YES, BRANCH
         TM    FLAGSGG,FLKEDCON        NO LKEDDATE DEFAULT?
         BO    ATTR600                 YES, BRANCH
         SPACE 1
         MVC   INSERT#1-1(9),DATEMASK
         ED    INSERT#1-1(9),LKEDDATE
         AIF   ('&ISODT'  NE 'YES').NOISOA4                   DRK NOV01
         SPACE 1
         MVC   DOUBLE(8),INSERT#1-1+1    MM/DD/YY             DRK NOV01
         ICM   R0,B'1100',DOUBLE+6       YY OF MM/DD/YY       DRK NOV01
         MVC   DOUBLE+6(2),DOUBLE+3      DD TO ../../DD       DRK NOV01
         MVC   DOUBLE+3(2),DOUBLE        MM TO ../MM/DD       DRK NOV01
         STCM  R0,B'1100',DOUBLE         YY TO YY/MM/DD       DRK NOV01
         OI    DOUBLE+3,X'F0'            LEADIN BLANK IN MM   DRK NOV01
         MVC   INSERT#1-1+1(8),DOUBLE    YY/MM/DD             DRK NOV01
         SPACE 1
.NOISOA4 ANOP                                                 DRK NOV01
         MVI   MTLEN+4,18+14                  LENGTH OF THIS INSERT
         MVC   INSERT#2(10),LKEDNAME
         MVC   INSERT#2+10(2),=CL2' V'
         UNPK  INSERT#2+10+2(3),LKEDVVMM(2)
         MVC   INSERT#2+10+2+2(2),=CL2' M'
         UNPK  INSERT#2+10+2+2+2(3),LKEDVVMM+1(2)
         MVI   INSERT#2+10+2+2+2+2,C' '                       DRK JUN99
         SPACE 1                                              DRK MAY99
         CLC   LKEDTIME(4),=X'0000000F' ANY LINK EDIT TIME?   DRK JUL99
         BE    *+4+6+6+6                NO                    DRK MAY99
         MVC   INSERT#2+10+2+2+2+2(4),=CL4'  AT'              DRK MAY99
         MVC   INSERT#2+10+2+2+2+2+4(10),TIMEMSK2             DRK MAR00
         ED    INSERT#2+10+2+2+2+2+4(10),LKEDTIME             DRK MAY99
         SPACE 1
         M$MSG L064$2                         LINKAGE EDIT MESSAGE
         MVI   MTLEN+4,8                      LENGTH OF STANDARD INSERT
         SPACE 3
ATTR600  TM    DIRFLAG,X'80'           ALIAS?
         BO    ATTR630                 YES, BRANCH
         OC    #RMODE(2),#RMODE        ANY RMODE/AMODE CHANGE?
         BZ    ATTR630                 NO, BRANCH
         TM    FLAGSCC,RECFMU          RECFM=U?
         BNO   ATTR630                 NO, BRANCH
         NI    MODESAVE,DIRRMANY+DIRAM31+DIRAM64              DRK OCT02
         MVC   MSGTEXT2(DIREND-DIRNAME),DIRNAME  SAVE MAIN ENTRY
         MVI   STARTTR+2,X'01'         TTR=000001 (START OF DIRECTORY)
         SPACE 1
ATTR610  MVC   DIRNAME(DIREND-DIRNAME),MSGTEXT2  RESTORE MAIN ENTRY
         BAS   R14,READDIR             GET NEXT DIRECTORY MEMBER
         B     ATTR630                 LAST MEMBER PROCESSED
         SPACE 2
         CLC   DIRTTR,MEMTTR           TTR'S MATCH?
         BNE   ATTR610                 NO, BRANCH
         TM    MEMFLAG,X'80'           ALIAS ENTRY?
         BNO   ATTR610                 NO, CONTINUE SEARCHING
         SPACE 2
         L     R14,DIRPTRS             CURRENT DIRECTORY ENTRY
         MVC   DIRNAME(DIREND-DIRNAME),0(R14)
         SPACE 1
         MVC   DOUBLE(1),DIRATTR3
         NI    DOUBLE,DIRRMANY+DIRAM31+DIRAM64                DRK OCT02
         CLC   MODESAVE(1),DOUBLE      RMODE/AMODE ENTRIES EQUAL?
         BE    ATTR610                 YES, BRANCH
         NI    DIRATTR3,FF-DIRRMANY-DIRAM31-DIRAM64           DRK OCT02
         OC    DIRATTR3(1),MODESAVE    CHANGE THE RMODE/AMODE ENTRY
         SPACE 1
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR  TTR
         STOW  STOWDCB,DIRNAME,R       REPLACE THIS MEMBER ENTRY
         SPACE 1
         LR    R5,R15                  SAVE RETURN CODE
         MVC   INSERT#1(8),DIRNAME     MEMBER NAME
         M$MSG L092$1
         LTR   R15,R5                  SUCCESSFUL?
         BNZ   IOERROR                 NO, I/O ERROR
         B     ATTR610                 CONTINUE FOR ALL ALIASES
         SPACE 2
ATTR630  NI    FLAGSBB,FF-FLINESET
         CLI   #ALIAOPT,X'01'          "ALIASINFO" DESIRED?
         BE    ATTR640                 YES, BRANCH
         BH    NEWCMD                  "NOALIASINFO", DONE
         TM    FLAGSGG,FALINCON        NO ALIAS INFORMATION DEFAULT?
         BO    NEWCMD                  YES, BRANCH
         TM    FLAGSCC,RECFMU          RECFM=U?
         BO    ATTR640                 YES, BRANCH
         TM    DIRFLAG,X'80'           MODULE AN ALIAS?
         BNO   NEWCMD                  NO, DONE
         SPACE 1
ATTR640  MVI   STARTTR+2,X'01'         TTR=000001 (START OF DIRECTORY)
         TM    DIRFLAG,X'80'           MODULE AN ALIAS?
         BNO   ATTR900                 NO, FIND ALL ALIASES
         XC    INSERT#1(8),INSERT#1    NO MAIN(S) FOUND
         SPACE 2
*  LIST ANY CORRESPONDING MAIN MODULE NAMES
         SPACE 1
ATTR800  BAS   R14,READDIR        GET NEXT DIRECTORY MEMBER
         B     ATTR810            LAST MEMBER PROCESSED
         SPACE 1
         TM    MEMFLAG,X'80'      THIS MODULE AN ALIAS?
         BO    ATTR800            YES, IGNORE
         CLC   DIRTTR,MEMTTR      TTR MATCH?
         BNE   ATTR800            NO, BRANCH
         MVC   INSERT#1(8),MEMNAME
         M$MSG L066$1
         B     ATTR800
         SPACE 1
ATTR810  OC    INSERT#1(8),INSERT#1  ANY MAIN(S) FOUND?
         BNZ   NEWCMD                YES, BRANCH
         M$MSG L860
         TM    FLAGSCC,RECFMU     LOAD MODULE?
         BNO   NEWCMD             NO, BRANCH
         MVC   INSERT#1(8),DIRREAL
         SPACE 1
         TM    DIRATTR,ATTRSCTR   SCATTER LOADED?
         BNO   *+10               NO, BRANCH
         MVC   INSERT#1(8),DIRREALS
         LA    R1,L861$1
         B     MSGNEW
         SPACE 2
*  LIST ANY ALIASES FOR THE MODULE
         SPACE 1
ATTR900  BAS   R14,READDIR          GET NEXT DIRECTORY MEMBER
         B     ATTR940              LAST MEMBER PROCESSED
         SPACE 1
         CLC   DIRTTR,MEMTTR        TTR MATCH?
         BNE   ATTR900              NO, BRANCH
         TM    MEMFLAG,X'80'        AN ALIAS?
         BO    ATTR910              YES, BRANCH
         CLC   DIRNAME(8),MEMNAME   DID I FIND MYSELF?
         BE    ATTR900              YES, BRANCH
         MVC   INSERT#1(8),MEMNAME
         M$MSG L864$1               THIS IS AN APPARENT ALIAS MEMBER
         B     ATTR900
         SPACE 1
ATTR910  LA    R2,MEMNAME+7
         TM    FLAGSBB,FLINESET     LINE IN PROGRESS?
         BO    ATTR920              YES, BRANCH
         OI    FLAGSBB,FLINESET     LINE NOW IN PROGRESS
         LA    R1,80                ASSUME AN ACTIVE MODE
         TM    SPFLAG0,SPFDON       ISPMODE ACTIVE?
         BO    ATTR912              YES, BRANCH
         TM    CONTOPTN,1           ANY LOG RECORDING?
         BO    ATTR912              YES, BRANCH
*        GTSIZE ,                   TERMINAL SIZE              SS MAY89
         GTSIZE
         CH    R1,=H'120'           120 OR LESS BYTES?
         BL    *+8                  YES, BRANCH
         LH    R1,=H'120'           NO, USE 120 BYTES
ATTR912  SH    R1,=H'7'             LESS SEVEN FOR PREFIX
         ST    R1,LINESIZE          CHARACTERS/LINE
         MVC   MSGLINE+4(L'MSGALIAS),MSGALIAS
         LA    R5,MSGLINE+4+L'MSGALIAS
         MVC   FULLWORD(3),L160$1   MESSAGE IDENTIFIER
         SPACE 1
ATTR920  CLI   0(R2),X'40'          SCAN FOR
         BNE   *+8                          LAST
         BCT   R2,ATTR920                       NON-BLANK
         SPACE 1
         LA    R0,MEMNAME
         SR    R2,R0
         BNM   *+6
         SR    R2,R2
         LA    R15,2(R5,R2)
         LA    R1,MSGLINE
         SR    R15,R1
         C     R15,LINESIZE
         BNH   ATTR930
         LA    R1,MSGLINE+4
         SR    R5,R1
         STC   R5,MTLEN
         MVC   INSERT#1(127),MSGLINE+4
         LA    R1,FULLWORD
         M$MSG (R1)
         MVI   MTLEN,8
         MVI   FULLWORD+2,C'9'
         LA    R5,MSGLINE+3         INDENT FOR BLANKS
         SPACE 1
ATTR930  MVI   0(R5),X'40'
         MVC   1(8,R5),MEMNAME
         TR    1(8,R5),TRLINE
         LA    R1,2(R2,R5)
         MVI   0(R1),C','
         LA    R5,3(R2,R5)
         B     ATTR900
         SPACE 2
ATTR940  TM    FLAGSBB,FLINESET     LINE IN PROGRESS?
         BZ    NEWCMD               NO, EXIT
         LA    R1,MSGLINE+5
         SR    R5,R1
         STC   R5,MTLEN
         MVC   INSERT#1(127),MSGLINE+4
         LA    R1,FULLWORD
         M$MSG (R1)
         MVI   MTLEN,8
         B     NEWCMD
HEXZERO  DC    XL30'00'
*                                                             DRK JUN18
PDS397A  M$TX  'PDS397A SHOULD ENTIRE DIRECTORY BE MODIFIED (Y/N) ?'  "
TIMEMSK2 DC    X'402120207A20207A2020'     HH:MM:SS           DRK MAR00
ATTRCOMM DC    C',     '         COMMA AND FOLLOWING BLANKS
MSGALIAS DC    C'ALIASES FOR THIS MEMBER ARE:'
TBLATTR  DS    0H                                    PDS PROCESSING
*                                                    ==============
         DC    AL1(ATTROVLY,0,00006),CL9'OVERLAY'       LIST ONLY
         DC    AL1(ATTRTEST,0,00003),CL9'TEST'          LIST ONLY
         AIF   ('&MVSLEV' GE 'MVS6XX').MVSNEW                 DRK AUG05
         DC    AL1(0,ATTRFLEV,128+6),CL9'E-LEVEL'       LIST ONLY
.MVSNEW  ANOP                                                 DRK AUG05
SCTRTXT  DC    AL1(ATTRSCTR,0,00003),CL9'SCTR'          LIST ONLY
RENTTXT  DC    AL1(ATTRRENT,0,00003),CL9'RENT'       CHANGE AND LIST
REUSTXT  DC    AL1(ATTRREUS,0,00003),CL9'REUS'       CHANGE AND LIST
REFRTXT  DC    AL1(0,ATTRREFR,00003),CL9'REFR'       CHANGE AND LIST
         DC    AL1(ATTRLOAD,0,00008),CL9'LOAD ONLY'  CHANGE AND LIST
         DC    AL1(0,ATTRNE+0,00007),CL9'NOT EDIT'   CHANGE AND LIST
         DC    AL1(ATTREXEC,0,128+7),CL9'NOT EXEC'   CHANGE AND LIST
         DC    AL1(0,ATTRNODC,128+1),CL9'DC'         CHANGE AND LIST
         DC    X'FFFF'
ATTREXPI DC    C'EXPIRE:'                                     ABL NOV86
ATTRLEV  DC    C'LEV:'                                        ABL NOV86
ATTRBY   DC    C'BY'                                          ABL NOV86
ATTRREAD DC    C'READ:'                                       ABL NOV86
ATTRNODA DC    X'00000F'               NULL DATE              ABL NOV86
         SPACE 2
AATTS310 DC    A(ATTS310)
AATTRSUM DC    A(ATTRSUMM)
*PFSTAT1 DC    CL72'SPF STATS:  VER.MOD  CREATED   LAST MODIFIED  SIZE
SPFSTAT1 DC    CL72'MEMBER      VER.MOD  CREATED   LAST MODIFIED  SIZE X
                INIT   MOD   ID'    PDS230I
SPFSTATL DC    CL72'MEMBER      VER.MOD  CREATED   LAST MODIFIED     SIX
               ZE     INIT   ID'    PDS231I
         DROP  R6                                             DRK OCT98
         SPACE 3
         USING *,R8
ATTRSUMM XC    ADDRSUMR,ADDRSUMR   CLEAR THE SUMMARY ADDRESS
         L     R1,ATTRCNT2
         A     R1,ATTRCNTA
*        C     R1,=F'1'            MORE THAN ONE ENTRY?
         C     R1,=F'0'            AT LEAST ONE ENTRY?        DRK AUG18
         BNH   ATTRSU90            NO, BRANCH
         MVI   MTLEN,16            GET LENGTH
         MVI   MTLEN+4,16
         CLI   #ALIAOPT,X'03'          SHORT FORMAT DESIRED?
         BE    ATTRSU04                YES, BRANCH
         TM    FLAGSCC,RECFMU          LOAD MODULE LIBRARY?
         BNO   ATTRSU04                NO, BRANCH
         M$ERRST MSGBLANK                 ONE BLANK LINE
ATTRSU04 OI    ATTRTOT2+7,X'0F'    INSURE VALID PACKED SIGN
*
         L     R1,ATTRCNT2         COUNT OF MODULES
         CVD   R1,DOUBLE           IN PACKED FORM
         BAS   R14,ATTRSU99        GET THE FIRST COUNT
         MVC   INSERT#1(16),0(R1)  MOVE INTO COUNT AREA
         MVC   DOUBLE(8),ATTRTOT2  MOVE INTO COMMON AREA
         TM    FLAGSCC,RECFMU      LOAD MODULES?
         BNO   ATTRSU10            NO, BRANCH
         AP    DOUBLE(8),ATTRUFFF  ADD 1023
         DP    DOUBLE(8),ATTRU1K   DIVIDE BY 1024
         ZAP   ATTRTOT2(8),DOUBLE(5) MOVE IN QUOTIENT
         MVC   DOUBLE(8),ATTRTOT2  MOVE INTO COMMON AREA
ATTRSU10 BAS   R14,ATTRSU99        CONVERT SIZE
         MVC   INSERT#2(16),0(R1)  MOVE IN SIZE PARAMETER
         TM    FLAGSCC,RECFMU      LOAD MODULES?
         BO    ATTRSU20            YES, BRANCH
         M$MSG L117$2
         B     ATTRSU90
ATTRSU20 ICM   R1,B'1111',ATTRCNT2 ANY COUNT?
         BZ    ATTRSU25            NO, BRANCH
         M$MSG L118$2
ATTRSU25 OI    ATTRTOTA+7,X'0F'    INSURE VALID PACKED SIGN
*
         ICM   R1,B'1111',ATTRCNTA ANY COUNT?
         BZ    ATTRSU90            NO, BRANCH
         L     R1,ATTRCNTA         COUNT OF MODULES
         CVD   R1,DOUBLE           IN PACKED FORM
         BAS   R14,ATTRSU99        GET THE FIRST COUNT
         MVC   INSERT#1(16),0(R1)  MOVE INTO COUNT AREA
         MVC   DOUBLE(8),ATTRTOTA  MOVE INTO COMMON AREA
         AP    DOUBLE(8),ATTRUFFF  ADD 1023
         DP    DOUBLE(8),ATTRU1K   DIVIDE BY 1024
         ZAP   ATTRTOTA(8),DOUBLE(5) MOVE IN QUOTIENT
         MVC   DOUBLE(8),ATTRTOTA  MOVE INTO COMMON AREA
         BAS   R14,ATTRSU99        CONVERT SIZE
         MVC   INSERT#2(16),0(R1)  MOVE IN SIZE PARAMETER
         M$MSG L119$2
*
ATTRSU90 M$ERRST MSGBLANK                 ONE BLANK LINE
         XC    ATTRCNT2(4+4),ATTRCNT2     COUNTS
         XC    ATTRTOT2(8+8),ATTRTOT2     TOTALS
         MVI   MTLEN,8                    RESET LENGTH
         MVI   MTLEN+4,8                  RESET LENGTH
         BR    R2
ATTRSU99 DS    0H
         MVC   INSERT#2+16(15),ATTRUMMP EDIT PICTURE
         MVC   INSERT#2+16+15(16),BLANK128
         ED    INSERT#2+16(15),DOUBLE+2
         LA    R1,INSERT#2+16
         LA    R1,1(,R1)   <---+
         CLI   0(R1),X'40'     |
         BE    *-8         ----+
         BR    R14
         SPACE 2
*                 1 2 3 4 5 6 7 8 9 0 1 2 3 4 5
*                   1 2 , 3 4 5 , 6 7 8 , 9 0 1
ATTRUMMP DC    X'4020206B2020206B2020206B202120'
ATTRUFFF DC    PL3'1023'
ATTRU1K  DC    PL3'1024'
         SPACE 3
ATTS310  BASR  R8,0                    SHORT FORM ATTRIB SUBCOMMAND
         USING *,R8                    4K OF ADDRESSABILITY
         MVC   MSGTEXT2(86),INSERT#2   SAVE THE VARIABLE PART
         TM    ##ADRCM#+#LSUB,FATTRHDR HEADER WRITTEN YET?
         BO    ATTS312                 YES, BRANCH
         MVC   INSERT#1(72),ATTSTAT    HEADER INFORMATION
         MVI   MTLEN,72
         M$MSG L232$1
         OI    ##ADRCM#+#LSUB,FATTRHDR MARK FOR NEXT CALL
         SPACE 1
ATTS312  MVC   INSERT#1(127),BLANK128
         MVI   MTLEN,112               MAXIMUM LENGTH INSERT
         MVC   INSERT#1(8),DIRNAME
         SPACE 2
         LA    R2,DIRAPF               POINT TO APF INFORMATION
         SPACE 1
         TM    DIRATTR,ATTRSCTR        SCATTER FORMAT?
         BNO   *+8                     NO, BRANCH
         LA    R2,8(,R2)               YES, ADD SCATTER SIZE BYTES
         SPACE 1
         TM    DIRFLAG,X'80'           ALIAS?
***PDSMAN CHANGES: ATTRIB FOR LISTING SSI (SHORT FORM)        ABL NOV86
         BO    ATTS314                 YES, BRANCH
         TM    5(R2),X'0F'             PDSMAN DATE?           ABL NOV86
         BO    ATTS320                 YES, BRANCH            ABL NOV86
         TM    6(R2),X'0F'             PDSMAN DATE?           ABL NOV86
         BO    ATTS320                 YES, BRANCH            ABL NOV86
         TM    4+5(R2),X'0F'           PDSMAN DATE AND SSI?   ABL NOV86
         BO    ATTS320                 YES, BRANCH            ABL NOV86
         TM    4+6(R2),X'0F'           PDSMAN DATE AND SSI?   ABL NOV86
         BO    ATTS320                 YES, BRANCH            ABL NOV86
         CLI   8(R2),0                 CONVERTED ALIAS ENTRY?
*        BE    *+8                     NO, BRANCH
         B     *+8                     BRANCH                 DRK JAN98
ATTS314  LA    R2,11(,R2)              ADD ALIAS LENGTH
ATTS320  DS    0H                                             ABL NOV86
         SPACE 1
         TM    DIRATTR2,DIRAOSLE       VS LINKAGE EDITOR?
         BO    ATTS330                 YES, BRANCH
***      M$MSG L022                    NON-VS LINKAGE EDITOR MESSAGE
         B     ATTS340
         SPACE 1
ATTS330  TM    DIRATTR2,DIR2SSI        SSI PRESENT?
         BNO   ATTS360                 NO, SKIP SSI PROCESSING
         SPACE 1
ATTS340  LA    R2,1(,R2)               ROUND UP TO HALFWORD
         N     R2,=F'-2'
         SPACE 1
         CLC   ZERO,0(R2)              ZERO?
         BE    ATTS360                 YES, NO SSI
         CLC   =F'-1',0(R2)            FFFFFFFF?
         BE    ATTS360                 YES, NO SSI
         SPACE 1
         UNPK  INSERT#1+33(9),0(5,R2)  CONVERT TO HEXADECIMAL
         TR    INSERT#1+33(8),TRTABLE  CONVERT TO PRINTABLE
         MVI   INSERT#1+33+8,X'40'     FIX UNPRINTABLE CHARACTER
***      M$MSG L025$1
         LA    R2,4(,R2)               ADD SSI SIZE
         SPACE 2
ATTS360  DS    0H
         MVI   MODESAVE,0
         LA    R3,INSERT#1+42              WHERE VARIABLE OUTPUT STARTS
         TM    DIRATTR2,DIRAOSLE           VS LINKAGE EDITOR?
         BZ    ATTS540                     NO, CANNOT BE AUTHORIZED
         SR    R1,R1
         IC    R1,DIRATTR3                 AMODE BITS
         TM    DIRFLAG,X'80'               ALIAS?
         BNO   *+8                         NO, BRANCH
         SRL   R1,2                        YES, SHIFT AMODE BITS
         STC   R1,MODESAVE                 SAVE CURRENT AMODE
         TM    DIRATTR3,DIRRMANY           RMODE ANY?
         BNO   *+8                         NO, BRANCH
         OI    MODESAVE,DIRRMANY           YES, SET RMODE AGAIN
         SPACE 1
         TM    MODESAVE,DIRRMANY+DIRAM31+DIRAM64 UNUSUAL RMODE/AMODE?
         BZ    ATTS400                     NO, BRANCH
         MVI   0(R3),C'R'                  R FOR RMODE
         MVC   1(3,R3),ATTS24              MOVE IN THE RMODE TEXT
         SPACE 1
         TM    MODESAVE,DIRRMANY+DIRRM64   RMODE 64?          DRK APR18
         BNO   *+4+6+4                     NO, BRANCH         DRK APR18
         MVC   1(3,R3),ATTS64              MOVE IN THE RMODE TEXT     "
         B     ATTS362                                        DRK APR18
         SPACE 1
         TM    MODESAVE,DIRRMANY           RMODE ANY?
         BZ    ATTS362                     NO, BRANCH
         MVC   1(4,R3),ATTSANY             MOVE IN THE RMODE TEXT
         LA    R3,1(,R3)                   ADD AN EXTRA ONE
ATTS362  LA    R3,5(,R3)                   ALLOW FOR "R24, "
         SPACE 1
         MVI   0(R3),C'A'                  A FOR AMODE
         MVC   1(3,R3),ATTS24              MOVE IN THE AMODE TEXT
         TM    MODESAVE,DIRAM64            AMODE 64?          DRK APR18
         BNO   *+10                        NO, BRANCH         DRK APR18
         MVC   1(3,R3),ATTS64              MOVE IN THE AMODE TEXT     "
         TM    MODESAVE,DIRAM31            AMODE 31?
         BNO   *+10                        NO, BRANCH
         MVC   1(3,R3),ATTS31              MOVE IN THE AMODE TEXT
         TM    MODESAVE,DIRAM64+DIRAM31    AMODE ANY?
         BNO   ATTS364                     NO, BRANCH
         MVC   1(4,R3),ATTSANY             MOVE IN THE AMODE TEXT
         LA    R3,1(,R3)                   ADD AN EXTRA ONE
ATTS364  LA    R3,5(,R3)                   ALLOW FOR "A24, "
         SPACE 1
***      M$MSG L120$2
***      TM    MODESAVE,DIRRMANY           RMODE ANY?
***      BZ    ATTS400                     NO, BRANCH
***      LA    R1,=C'ANY'                  ASSUME AMODE IS ANY
***      TM    MODESAVE,DIRAM24+DIRAM31    AMODE ANY?
***      BO    ATTS370                     YES, INVALID
***      TM    MODESAVE,DIRAM31            AMODE 31?
***      BO    ATTS400                     YES, BRANCH
***      LA    R1,=C'24 '                  AMODE IS 24
***S370  MVC   INSERT#1(8),BLANKS
***      MVC   INSERT#1(3),0(R1)           MOVE IN THE AMODE TEXT
***      M$MSG L880$1                      RMODE ANY/AMODE CONFLICT
         SPACE 2
ATTS400  CLI   RLDCOUNT,X'FF'          ANY DATA TO CHECK?
         BE    ATTS500                 NO, BRANCH
         CLC   RLDCOUNT(1),DIRATTR4    RLD/CONTROL AMOUNTS AGREE?
         BE    ATTS500                 YES, BRANCH
***      M$MSG L826
         SPACE 2
ATTS500  TM    DIRATTR2,DIR2PAGA       PAGE ALIGNMENT REQUIRED?
         BNO   ATTS510                 NO, BRANCH
         MVC   0(5,R3),ATTSPAGE        PAGE, TEXT
         LA    R3,6(,R3)               NEXT PARAMETER
***      M$MSG L024                    PAGE ALIGNMENT REQUIRED
         SPACE 2
ATTS510  TM    DIRATTR2,DIRAPFLG       APF DATA PRESENT AND VALID?
         BNO   ATTS540                 NO, BRANCH
         SPACE 1
         LA    R1,ATTSAPFX             ASSUME APF LENGTH INCORRECT
         CLI   0(R2),1                 APF DATA LENGTH OK?
         BNE   ATTS530                 NO, BRANCH
         SPACE 3
         LA    R1,ATTSAPF1             ASSUME AUTHORIZED
         CLI   1(R2),1                 AUTHORIZED?
         BE    ATTS530                 YES, BRANCH
         BL    ATTS540                 NO, APF=0 (NO MESSAGE NEEDED)
         SPACE 1
***S530  M$MSG (R1)
ATTS530  MVC   0(5,R3),0(R1)           MOVE IN TEXT
         MVI   5(R3),C','              ADD A COMMA
         LA    R3,7(,R3)               NEXT PARAMETER
         SPACE 2
ATTS540  DS    0H
         CLC   MSGTEXT2+1(4),ATTSNONE  NONE PARAMETER?
         BNE   ATTS542                 NO, BRANCH
         LR    R1,R3                   NEXT POSITION FOR TEXT
         SH    R1,=H'2'                POINT TO LAST COMMA IF A LIST
         CLI   0(R1),C','              COMMA?
         BNE   ATTS542                 NO, BRANCH
         MVI   0(R1),X'40'             BLANK THE LAST COMMA
         MVC   MSGTEXT2+1(4),BLANK128  BLANK "NONE"
         SPACE 1
ATTS542  MVC   0(85,R3),MSGTEXT2+1     ADD THE VARIABLE PART
         SPACE 2
ATTS590  CLI   LKEDDATE,X'FF'          ANY DATA TO OUTPUT?
         BE    ATTS600                 NO, BRANCH
         CLI   #LKEDOPT,X'02'          "NOLKEDDATE" DESIRED?
         BE    ATTS600                 YES, BRANCH
         CLI   #LKEDOPT,X'01'          "LKEDDATE" DESIRED?
         BE    *+12                    YES, BRANCH
         TM    FLAGSGG,FLKEDCON        NO LKEDDATE DEFAULT?
         BO    ATTS600                 YES, BRANCH
         SPACE 1
         MVC   MSGTEXT2(9),DATEMASK
         ED    MSGTEXT2(9),LKEDDATE
         MVC   INSERT#1+18(2),MSGTEXT2+7 YY OF MM/DD/YY
         MVI   INSERT#1+20,C'/'          YY/
         MVC   INSERT#1+21(5),MSGTEXT2+1 MM/DD OF MM/DD/YY
         OI    INSERT#1+21,X'F0'         REMOVE ANY LEADIN BLANK IN MM
***      M$MSG L064$2                         LINKAGE EDIT MESSAGE
         SPACE 3
ATTS600  SR    R1,R1                                          ABL NOV86
         ICM   R1,B'0111',DIRSTORE    ACTUAL MODULE LENGTH
         CVD   R1,DOUBLE           CONVERT TO DECIMAL
         C     R1,=F'99999'           BELOW 99,999?           ABL NOV86
         BH    ATTS606                NO, BRANCH              ABL NOV86
         MVC   MSGTEXT2(7),=X'402020202120D2404040'           ABL NOV86
         ED    MSGTEXT2(6),DOUBLE+5                           ABL NOV86
         MVC   INSERT#1+27(5),MSGTEXT2+1                      ABL NOV86
         B     ATTS608                                        ABL NOV86
ATTS606  DS    0H                                             ABL NOV86
         LA    R1,1023(,R1)           NEXT HIGHER
         SRL   R1,10                             1K VALUE
         CVD   R1,DOUBLE
         MVC   MSGTEXT2(7),=X'402020202120D2404040'
         ED    MSGTEXT2(6),DOUBLE+5
         MVC   INSERT#1+26(6),MSGTEXT2+1
ATTS608  DS    0H                                             ABL NOV86
***      M$MSG L104$2
         SPACE 3
         TM    DIRFLAG,X'80'           ALIAS?
         BO    ATTS630                 YES, BRANCH
         OC    #RMODE(2),#RMODE        ANY RMODE/AMODE CHANGE?
         BZ    ATTS630                 NO, BRANCH
         TM    FLAGSCC,RECFMU          RECFM=U?
         BNO   ATTS630                 NO, BRANCH
         NI    MODESAVE,DIRRMANY+DIRAM31+DIRAM64              DRK OCT02
         MVC   MSGTEXT2(DIREND-DIRNAME),DIRNAME  SAVE MAIN ENTRY
         MVI   STARTTR+2,X'01'         TTR=000001 (START OF DIRECTORY)
         SPACE 1
ATTS610  MVC   DIRNAME(DIREND-DIRNAME),MSGTEXT2  RESTORE MAIN ENTRY
         BAS   R14,READDIR             GET NEXT DIRECTORY MEMBER
         B     ATTS630                 LAST MEMBER PROCESSED
         SPACE 2
         CLC   DIRTTR,MEMTTR           TTR'S MATCH?
         BNE   ATTS610                 NO, BRANCH
         TM    MEMFLAG,X'80'           ALIAS ENTRY?
         BNO   ATTS610                 NO, CONTINUE SEARCHING
         SPACE 2
         L     R14,DIRPTRS             CURRENT DIRECTORY ENTRY
         MVC   DIRNAME(DIREND-DIRNAME),0(R14)
         SPACE 1
         MVC   DOUBLE(1),DIRATTR3
         NI    DOUBLE,DIRRMANY+DIRAM31+DIRAM64
         CLC   MODESAVE(1),DOUBLE      RMODE/AMODE ENTRIES EQUAL?
         BE    ATTS610                 YES, BRANCH
         NI    DIRATTR3,FF-DIRRMANY-DIRAM31-DIRAM64           DRK OCT02
         OC    DIRATTR3(1),MODESAVE    CHANGE THE RMODE/AMODE ENTRY
         SPACE 1
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR  TTR
         STOW  STOWDCB,DIRNAME,R       REPLACE THIS MEMBER ENTRY
         SPACE 1
         LR    R5,R15                  SAVE RETURN CODE
***      MVC   INSERT#1(8),DIRNAME     MEMBER NAME
***      M$MSG L092$1
         LTR   R15,R5                  SUCCESSFUL?
         BNZ   IOERROR                 NO, I/O ERROR
         B     ATTS610                 CONTINUE FOR ALL ALIASES
         SPACE 2
ATTS630  NI    FLAGSBB,FF-FLINESET
***      CLI   #ALIAOPT,X'01'          "ALIASINFO" DESIRED?
***      BE    ATTS640                 YES, BRANCH
***      BH    NEWCMD                  "NOALIASINFO", DONE
         TM    FLAGSGG,FALINCON        NO ALIAS INFORMATION DEFAULT?
         BO    ATTS900                 YES, BRANCH
***      TM    FLAGSCC,RECFMU          RECFM=U?
***      BO    ATTS640                 YES, BRANCH
***      TM    DIRFLAG,X'80'           MODULE AN ALIAS?
***      BNO   NEWCMD                  NO, DONE
         SPACE 1
ATTS640  TM    DIRFLAG,X'80'           MODULE AN ALIAS?
         BNO   ATTS900                 NO, FIND ALL ALIASES
         MVC   INSERT#1+9(8),ATTSUNK   NO MAIN FOUND
         MVI   STARTTR+2,X'01'         TTR=000001 (START OF DIRECTORY)
         SPACE 2
*  FIND ANY CORRESPONDING MAIN MODULE NAME
         SPACE 1
ATTS800  BAS   R14,READDIR        GET NEXT DIRECTORY MEMBER
         B     ATTS900            LAST MEMBER PROCESSED
         SPACE 1
         TM    MEMFLAG,X'80'      THIS MODULE AN ALIAS?
         BO    ATTS800            YES, IGNORE
         CLC   DIRTTR,MEMTTR      TTR MATCH?
         BNE   ATTS800            NO, BRANCH
         MVC   INSERT#1+9(8),MEMNAME
***      M$MSG L066$1
***      B     ATTS800
         SPACE 1
ATTS900  DS    0H                      OUTPUT FINAL RESULT
         M$MSG L232$1
         B     NEWCMD
         SPACE 1
L879$1   DC    C'879'                                         DRK APR18
*
         SPACE 2
ATTSTAT  DC    CL72'NAME     ALIASOF   CREATED  SIZE SSI      ATTRIBUTEX
               S'
ATTS24   DC    C'24,'
ATTS31   DC    C'31,'
ATTS64   DC    C'64,'
ATTSANY  DC    C'ANY,'
ATTSPAGE DC    C'PAGE,'
ATTSNONE DC    C'NONE'
ATTSAPFX DC    C'APF=?'
ATTSAPF1 DC    C'APF=1'
ATTSUNK  DC    C'?UNKNOWN'
*PDS232I NAME     ALIASOF   CREATED  SIZE SSI      ATTRIBUTES
*PDS232I 12345678 A2345678 86/01/16 9999K 12345678
*PDS232I                                           AC=1,
*PDS232I                                           A24,
*PDS232I                                           A31,
*PDS232I                                           A64,
*PDS232I                                           AANY,
*PDS232I                                           R24,
*PDS232I                                           RANY,
*PDS232I                                           R64,
*PDS232I                                           NONE **
*PDS232I                                           LOAD,
*PDS232I                                           NOT EDIT,
*PDS232I                                           NOT EXEC,
*PDS232I                                           NOTF,
*PDS232I                                           NOVS,
*PDS232I                                           RENT,
*PDS232I                                           REUS,
*PDS232I                                           REFR,
*PDS232I                                           DC,
*PDS232I                                           TEST,
*PDS232I                                           OVLY,
*PDS232I                                           SCTR,
