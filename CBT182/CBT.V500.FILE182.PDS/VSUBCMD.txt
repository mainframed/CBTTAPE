//ASM      EXEC  PGM=ASMA90,REGION=32M,
//             PARM=(NORLD,RENT,TERM,OBJ,
//             'XREF(SHORT),SIZE(MAX,ABOVE)')
//SYSLIB   DD  DSN=SYS1.MODGEN,DISP=SHARE
//         DD  DSN=SYS1.MACLIB,DISP=SHARE
//SYSUT1   DD  UNIT=VIO,SPACE=(4096,(120,120),,,ROUND),DCB=BUFNO=1
//SYSLIN   DD  UNIT=VIO,SPACE=(TRK,(5,5)),DISP=(MOD,PASS),DSN=&&X,
//             DCB=BLKSIZE=3120
//SYSPRINT DD  SYSOUT=*
//SYSTERM  DD  SYSOUT=*
//SYSIN    DD  *
VSUBCMD  TITLE 'PDS SUBCOMMAND VERIFICATION -- SAMPLE EXIT'
VSUBCMD  CSECT
         SAVE  (14,12),,*
         LR    R6,R1
         LR    R12,R15
         USING VSUBCMD,R12
         LA    R0,VSUBCMDL                  LENGTH OF SAVE AREA
         GETMAIN RU,LV=(0),SP=0
         ST    R1,8(,R13)
         ST    R13,4(,R1)
         LR    R13,R1
         USING VSUBCMDD,R13
         SPACE 1
         LA    R14,REGEND                     START OF AREA TO CLEAR
         LA    R15,VSUBCMDL-(REGEND-VSUBCMDD) LENGTH OF AREA TO CLEAR
         SR    R4,R4                          SOURCE ADDRESS
         SR    R5,R5                          PAD, LENGTH OF SOURCE
         MVCL  R14,R4                         CLEAR THE DYNAMIC AREA
         SPACE 1
***
***  THE ARGUMENT LIST FOR VSUBCMD HAS THE FOLLOWING ENTRIES:
***
***
***  DISPLACEMENT    NAME         DESCRIPTION
***  ------------    ----         -----------
***             0    FULLTEXT     POINTS TO AL2(LENGTH),AL2(OFFSET),
***                                         C'ORIGINAL SUBCOMMAND'
***             4    UPT          UPT DSECT
***             8    PSCB         PSCB DSECT
***            12    ECT          ECT DSECT
***            16    SUBCOMMAND   THE 8 CHARACTER SUBCOMMAND NAME
***            20    DSNAME       THE 44 CHARACTER DATA SET NAME
***
***
***
***   NOTE: CONTROL WILL BE RECEIVED IN THIS EXIT AFTER PARSE COMPLETES
***         FOR ALL PDS SUBCOMMANDS EXCEPT % (IMPLIED CLIST) AND RECALL
***
***   NOTE: CONTROL WILL ALSO BE RECEIVED IN THIS EXIT FOR THE IF AND
***         FIND SUBCOMMAND OPERANDS THEN AND ELSE.
***
         SPACE 3
***
***  ANY USER WITH ACCOUNT AUTHORITY IS AUTHORIZED.
***
         SR    R15,R15                     ASSUME USER IS AUTHORIZED
         L     R1,8(,R6)                   ADDRESS OF THE PSCB
         TM    PSCBATR1-PSCB(R1),PSCBACCT  ACCOUNT AUTHORITY?
         BO    DONE                        YES, AUTHORIZED
         SPACE 3
***
***  A USER WHOSE JOBNAME OR USERID MATCHES ANY NAME IN THE AUTHUSR
***  TABLE FOR THE NUMBER OF CHARACTERS IN THE TABLE IS AUTHORIZED.
***
         SR    R15,R15               ASSUME USER IS AUTHORIZED
         L     R1,X'21C'             CURRENT TCB
         L     R1,X'00C'(,R1)        USERID OR JOBNAME
         LA    R2,AUTHUSR-AUTHUSRL   INITIALIZE FOR USER SEARCH
         SR    R14,R14
         SPACE 1
CHKUSER  LA    R2,AUTHUSRL(,R2)      NEXT USER IN TABLE
         IC    R14,0(,R2)            LENGTH TO COMPARE
         BCTR  R14,0                 MACHINE LENGTH TO COMPARE
         CLI   0(R2),0               END OF AUTHORIZED USERS?
         BE    CONTCHK               YES, BRANCH
         CLC   1(*-*,R2),0(R1)       <<EXECUTED>>
         EX    R14,*-6               SPECIAL USER?
         BNE   CHKUSER               NO, CONTINUE CHECKING
         B     DONE                  YES, BRANCH
         SPACE 3
***
***  A USER IS AUTHORIZED IF THE DATA SET ACCESSED DOES NOT MATCH
***  ANY DATA SET NAME IN THE AUTHDSN TABLE.
***
CONTCHK  SR    R15,R15               ASSUME USER IS AUTHORIZED
         L     R1,20(,R6)            DSNAME PARAMETER
         LA    R2,AUTHDSN-AUTHDSNL   INITIALIZE FOR DSNAME SEARCH
         SR    R14,R14
         SPACE 1
CHKDSN   LA    R2,AUTHDSNL(,R2)      NEXT DSNAME IN THE TABLE
         IC    R14,0(,R2)            LENGTH TO COMPARE
         BCTR  R14,0                 MACHINE LENGTH TO COMPARE
         CLI   0(R2),0               END OF DATA SET NAMES?
         BE    DONE                  YES, BRANCH
         CLC   1(*-*,R2),0(R1)       <<EXECUTED>>
         EX    R14,*-6               PROTECTED DATA SET?
         BNE   CHKDSN                NO, CONTINUE CHECKING
         SPACE 3
***
***  A USER IS NOT AUTHORIZED IF THE SUBCOMMAND NAME MATCHES THE
***  NAME OF A SUBCOMMAND WHICH MODIFIES THE DATA SET.
***
         SR    R15,R15               ASSUME USER IS AUTHORIZED
         L     R1,16(,R6)            SUBCOMMAND PARAMETER
         LA    R2,AUTHSUB-AUTHSUBL   INITIALIZE FOR SUBCOMMAND SEARCH
         SR    R14,R14
         SPACE 1
CHKSUB   LA    R2,AUTHSUBL(,R2)      NEXT SUBCOMMAND IN THE TABLE
         CLI   0(R2),X'40'           END OF SUBCOMMANDS?
         BE    DONE                  YES, BRANCH
         CLC   0(8,R2),0(R1)         MODIFICATION SUBCOMMAND?
         BNE   CHKSUB                NO, CONTINUE CHECKING
         SPACE 3
NOTAUTH  LA    R15,4                 USER IS NOT AUTHORIZED
         SPACE 1
DONE     LR    R2,R13                SAVE AREA
         L     R13,4(R13)            PREVIOUS SAVE AREA
         STM   R15,R1,16(R13)        RESET REGISTERS R15, R0, R1
         LA    R0,VSUBCMDL           LENGTH OF AREA
         FREEMAIN R,LV=(0),A=(2)     FREE THIS AREA
         LM    R14,R12,12(R13)
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*   TABLE OF SPECIAL USERS  (ONE BYTE COMPARE LENGTH, USERID)         *
*                                                                     *
***********************************************************************
AUTHUSR  DC    AL1(4),CL8'HABL    '
AUTHUSRL EQU   *-AUTHUSR
         DC    AL1(4),CL8'HRJM    '
         DC    AL1(4),CL8'HRIS    '
         DC    AL1(0),CL8'        '   ZAP ENTRY
         DC    AL1(0),CL8'        '   ZAP ENTRY
         DC    AL1(0),CL8'        '   ZAP ENTRY
         DC    AL1(0),CL8'        '   ZAP ENTRY
         DC    AL1(0),CL8'        '   ZAP ENTRY
         DC    AL1(0),CL8'        '   ZAP ENTRY
         DC    AL1(0),CL8'        '   ZAP ENTRY
         DC    AL1(0)                 TABLE TERMINATOR
         SPACE 2
***********************************************************************
*                                                                     *
*   TABLE OF PROTECTED DATA SETS  (ONE BYTE COMPARE LENGTH, DSNAME)   *
*                                                                     *
***********************************************************************
AUTHDSN  DC    AL1(05),CL44'SYS1.'      PARTIAL DSNAME TO PROTECT
AUTHDSNL EQU   *-AUTHDSN
         DC    AL1(05),CL44'IPO1.'      PARTIAL DSNAME TO PROTECT
         DC    AL1(00),CL44'    '       ZAP ENTRY
         DC    AL1(00),CL44'    '       ZAP ENTRY
         DC    AL1(00),CL44'    '       ZAP ENTRY
         DC    AL1(00),CL44'    '       ZAP ENTRY
         DC    AL1(00),CL44'    '       ZAP ENTRY
         DC    AL1(00),CL44'    '       ZAP ENTRY
         DC    AL1(00)                  TABLE TERMINATOR
         SPACE 2
***********************************************************************
*                                                                     *
*   TABLE OF SUBCOMMANDS WHICH MODIFY THE TARGET DATA SET             *
*                                                                     *
***********************************************************************
AUTHSUB  DC    CL8'ALIAS   '
AUTHSUBL EQU   *-AUTHSUB
         DC    CL8'ATTRMOD '          IF ATTRIB AND ANY CHANGES
         DC    CL8'COMPRESS'
         DC    CL8'DELETE  '
         DC    CL8'EDIT    '
         DC    CL8'FIXPDS  '
         DC    CL8'FSE     '
         DC    CL8'RENAME  '
         DC    CL8'REPLACE '
         DC    CL8'RESTORE '
         DC    CL8'SPFEDIT '
         DC    CL8'VERMOD  '          IF VERIFY UPDATE
         DC    CL8'        '          ZAP ENTRY
         DC    CL8'        '          ZAP ENTRY
         DC    CL8'        '          ZAP ENTRY
         DC    CL8'        '          ZAP ENTRY
         DC    CL8'        '          ZAP ENTRY
         DC    CL8'        '          ZAP ENTRY
         DC    X'40'                  TERMINATOR
***********************************************************************
*                                                                     *
*   DSECTS AND EQUATES                                                *
*                                                                     *
***********************************************************************
VSUBCMDD DSECT
         DS    18F       SAVE OF SAVE AREA
         SPACE 1
REGEND   DS    0F        END OF REGISTER SAVE AREA -- DO NOT MOVE
         SPACE 3
PGMSTOR  DS    F
* ADD ANY STORAGE DEFINITIONS HERE -- KEEP THIS EXIT REENTRANT
VSUBCMDL EQU   *-VSUBCMDD
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
         IKJUPT
         EJECT
         IKJPSCB
         EJECT
         IKJECT
         END   VSUBCMD
//LK1    EXEC  PGM=IEWL,PARM='MAP,RENT,REUS,REFR,LET,NCAL',COND=(5,LT)
//SYSPRINT DD  SYSOUT=*
//SYSLMOD  DD  DISP=SHR,DSN=LINK.LIBRARY    <=== MODIFY
//IN       DD  DSN=&&X,DISP=(OLD,DELETE)
//SYSUT1   DD  UNIT=SYSDA,SPACE=(TRK,(5,5))
//SYSLIN   DD  *
  INCLUDE  IN
  ENTRY    PDS71                            <=== MODIFY??
  INCLUDE  SYSLMOD(PDS45678)                <=== MODIFY
  NAME     PDS45678(R)                      <=== MODIFY
