         MACRO M$ENTER - INITIAL ENTRY FOR A CSECT
&NAME    M$ENTER &TYPE
         LCLC  &LABEL
         GBLC  &VERS
&LABEL   SETC  'IHB'.'&SYSNDX'
         AIF   ('&TYPE' EQ 'VALCHECK').VALCK
         AIF   ('&TYPE' EQ 'ATTNEXIT').ATTN
         AIF   ('&TYPE' EQ '').MAIN
         MNOTE 8,'INVALID TYPE ''&TYPE'''
         MEXIT
.MAIN    ANOP
&NAME    CSECT
         USING *,R15
         B     PDSSTM
         DROP  R15
         DC    AL1(PDSCEND-*-1)         LENGTH OF EYE-CATCHER
PDSCSECT DC    CL16'&NAME'              CSECT NAME
PDSRELNO DC    CL16'&VERS'              RELEASE/UPDATE NUMBER
         DC    CL10'&SYSCLOCK',CL6' '
         DC    CL5'&SYSTIME',CL11' '
PDSDRS   DC    CL16'DRK/GP'             THE US/AU DOCTORS
PDSDATE  DC    CL16'&SYSDATE'           ASSEMBLY DATE
PDSTIME  DC    CL16'&SYSTIME'           ASSEMBLY TIME
PDSCEND  DS    0X
PDSSTM   STM   R14,R12,12(R13)          SAVE CALLER'S REGS
         LCLA  &R
&R       SETA  0
.EQU     ANOP
R&R      EQU   &R
&R       SETA  &R+1
         AIF   (&R LT 16).EQU
         LR    R9,R15
         USING &NAME,R9,R10,R11
         LA    R11,2048
         LA    R10,2048(R9,R11)
         LA    R11,2048(R10,R11)
         MEXIT
.VALCK   ANOP
&NAME    CSECT
         SAVE  (14,12)
         LR    R8,R15             BASE REGISTER ADDRESS
         USING &NAME,R8           BASE REGISTER NOTIFICATION
         L     R7,4(,R1)          WORK AREA ADDRESS
         LM    R9,R12,BASES       RESTORE BASE REGISTERS
         L     R6,0(R1)           PDE ADDRESS
         LA    R15,VALSAVE
         AGO   .CHAIN
.ATTN    ANOP
&NAME    CSECT
         SAVE  (14,12)
         L     R7,8(,R1)
         LM    R9,R12,BASES
         LA    R15,FIRST4K
         LA    R15,ATTNSAVE-FIRST4K(,R15)
.CHAIN   ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15
         L     R8,=A(&NAME)       BASE REGISTER ADDRESS
         USING &NAME,R8           BASE REGISTER NOTIFICATION
         SPACE 1
         MEND
