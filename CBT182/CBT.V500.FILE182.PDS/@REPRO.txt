         TITLE 'P D S  --  PDS REPRO                         09/28/88'
***********************************************************************
***      REPRO SUBCOMMAND      ADDED BY BRUCE LELAND -- SEPT, 1985  ***
***********************************************************************
*
         SPACE 1
REPRO    CSECT
         USING *,R8
         TM    DS1SMSFG,DS1PDSE         PDSE DATASET?         DRK AUG05
         BNO   REPROC                   NO, CONTINUE          DRK AUG05
         TM    FLAGSCC,RECFMU           RECFM=U?              DRK AUG05
         BNO   REPROC                   NO, CONTINUE          DRK AUG05
         MVI   MTLEN,5                  LENGTH OF INSERT      DRK SEP05
         MVC   INSERT#1(5),=C'REPRO'    INSERT COMMAND NAME   DRK SEP05
         M$MSG L996$1                   NOT SUPPORTED MSG     DRK SEP05
         M$ERRST MSGBLANK               ONE BLANK LINE        DRK SEP05
         B     NEWCMD                   NOBBLED               DRK AUG05
         SPACE 1
REPROC   DS    0H                                             DRK AUG05
         LA    R1,L530               ASSUME A NON-PARTITIONED DATA SET
         TM    DSORG,DS1DSGPO        CORRECT?
         BZ    MSGNEW                YES, BRANCH
         SPACE 2
         MVC   #REPRMAX+2(2),BLKSI   BUFFER SIZE
         CLC   #REPRBLK(4),ZERO      ANY MAXBLK(SIZE)?
         BE    REPR040               NO, BRANCH
         LH    R1,LRECL              MINIMUM RECORD LENGTH
         TM    FLAGSCC,RECFMV        RECFM=V?
         BNO   *+8                   NO, BRANCH
         LA    R1,4(,R1)             YES, ADD 4 FOR COUNT
         C     R1,#REPRBLK           LRECL:MAXBLK
         BNH   REPR020                 LOW OR EQUAL, BRANCH
         ST    R1,#REPRBLK             USE THE LRECL VALUE
         M$MSG L443                    MAXBLK HAS BEEN RAISED TO LRECL
         SPACE 1
REPR020  CLC   #REPRMAX(4),#REPRBLK  BLKSIZE:MAXBLK
         BNL   REPR030                 LOW OR EQUAL, BRANCH
         M$MSG L444                    MAXBLK EXCEEDS BLKSIZE
REPR030  MVC   #REPRMAX(4),#REPRBLK  USE MAXBLK(SIZE)
         SPACE 1
REPR040  MVI   SUBPOOLT,21           SUBPOOL NUMBER
         L     R0,#REPRMAX           BUFFER SIZE
         ICM   R0,B'1000',SUBPOOLT   ADD SUBPOOL NUMBER
         GETMAIN R,LV=(0)            GET TEMPORARY STORAGE
         ST    R1,#REPRBUF           POINT TO BUFFER WORK AREA
         SPACE 1
         OI    FLAGSII,FDIRGRP       SIMULATE MEMBER GROUP INPUT
         LA    R3,#MEMPTR            CURRENT MEMBER POINTER
         MVC   MEMNAME(12),DIRNAME   ENSURE MEMBER NAME IS PRESENT
         MVC   #DELSAVE(1),FLAGSAA   SAVE FLAGSAA
         TM    FLAGSAA,FMEM#MEM      MEMBER GROUP?
         BO    REPR090               YES, BRANCH
         MVI   #DELSAVE,FF-FMEM#MEM  MARK MEMBER AS ADDED
         TM    SPFLAG2,SPFLNCD       LINE COMMAND?
         BO    REPR050               YES, BRANCH
         TM    PMEMMIN,X'80'         MEMBER LIST?
         BO    REPR090               YES, BRANCH
REPR050  OI    #COPALI,#COPONE       ONLY A SINGLE MEMBER
         SPACE 1
REPR090  NI    FLAGSAA,FF-FMEM#MEM   TURN OFF MEMBER GROUPS NOW
         B     REPR300               PROCESS
         SPACE 3
REPR100  TM    #COPALI,#COPASSP      ASSOCIATES PHASE?
         BO    REPR120               YES, BRANCH
         TM    #DELSAVE,FMEM#MEM     MEMBER GROUP?
         BO    REPR120               YES, BRANCH
         TM    #DELSAVE,FF-FMEM#MEM  MEMBER ALREADY ADDED?
         MVI   #DELSAVE,FF-FMEM#MEM  MARK MEMBER AS ADDED
         BNO   REPR300               NO, BRANCH
         L     R1,PMEMCURR           CURRENT LIST ELEMENT
         TM    0(R1),X'80'           ANOTHER ELEMENT?
         BNO   REPR405               NO, END OF LIST
         MVC   FLAGSAA(1),4(R1)      NEXT DESCRIPTOR FLAGS
         MVC   LMEMBER1+1(19),5(R1)  LEN1, MEMBER1, LEN2, MEMBER2
         L     R1,0(,R1)             NEXT ELEMENT POINTER
         ST    R1,PMEMCURR
         MVC   #DELSAVE(1),FLAGSAA   SAVE THE MEMBER DESCRIPTOR
         MVC   MEMNAME(8),MEMBER1    FIRST MEMBER NAME
         NI    FLAGSAA,FF-FMEM#MEM   TURN OFF MEMBER GROUP
         TM    #DELSAVE,FMEM#MEM     MEMBER GROUP?
         BO    REPR119               YES, BRANCH
         MVI   #DELSAVE,FF-FMEM#MEM  MARK MEMBER AS ADDED
         MVC   DIRNAME,MEMBER1         FIRST PART OF DSNAME2
         SPACE 2
         BLDL  INDCB,BLDLLIST          LOCATE DIRECTORY ENTRY
         B     *+4(R15)                PROCESS RETURN CODE
         B     REPR115                   00 - SUCCESSFUL
         B     REPR110                   04 - MEMBER NOT FOUND
         B     IOERROR                   08 - I/O ERROR
         SPACE 1
REPR110  MVC   INSERT#1(8),DIRNAME
         M$MSG L853$1                MSG - (MEMBER) NOT FOUND
         B     REPR100
         SPACE 1
REPR115  MVC   DIRFLAG(DIREND-DIRFLAG),DIRFLAG+2  DELETE ADDED BYTES
         MVC   MEMNAME(12),DIRNAME   FIRST MEMBER NAME
         B     REPR300
         SPACE 1
REPR119  MVI   STARTTR+2,X'01'       REREAD THE DIRECTORY
         SPACE 1
REPR120  BAS   R14,READDIR           GET NEXT DIRECTORY BLOCK
         B     REPR400               END OF MEMBERS -- BRANCH
         SPACE 1
         L     R14,DIRPTRS           START OF DIRECTORY ENTRY
         MVC   DIRNAME(DIREND-DIRNAME),0(R14)
         TM    #COPALI,#COPASSP      ASSOCIATES PHASE?
         BO    REPR200               YES, BRANCH
         SPACE 1
         BAS   R2,MFILTER            CHECK MEMBER NAMES
         B     REPR400                 00 - SIMULATE DIRECTORY END
         B     REPR100                 04 - MEMBER FAILED FILTERING
         B     REPR300                 08 - MEMBER PASSED FILTERING
         SPACE 3
REPR200  LA    R1,#MEMPTR            START OF MEMBER LIST
REPR210  ICM   R1,B'0111',1(R1)      LAST TABLE ENTRY?
         BZ    REPR100               YES, IGNORE THIS ENTRY
         CLC   12(3,R1),DIRTTR       MATCHING TTR?
         BNE   REPR210               NO, BRANCH
         SPACE 3
REPR300  LA    R1,#MEMPTR            START OF MEMBER LIST
REPR310  ICM   R1,B'0111',1(R1)      LAST TABLE ENTRY?
         BZ    REPR340               YES, ADD THIS ENTRY
         CLC   4(8,R1),DIRNAME       FIND MYSELF?
         BE    REPR100               YES, IGNORE THIS ENTRY
         B     REPR310               NO, CONTINUE SEARCHING
         SPACE 3
REPR340  SR    R15,R15
         ICM   R15,B'0001',#COPTO#   ANY RENAME?
         BZ    REPR380               NO, BRANCH
         BCTR  R15,0                 MACHINE LENGTH OF COMPARE
         LA    R1,#MEMPTR            START OF MEMBER LIST
         TM    #COPALI,#COPONE       SINGLE MEMBER?
         BNO   REPR350               NO, BRANCH
         TM    #COPALI,#COPASS2      ALIAS KEYWORD?
         BO    REPR350               YES, BRANCH
         MVI   #COPTO#,8             SET LENGTH TO 8
         LA    R15,7                 SET LENGTH TO 8
         SPACE 1
REPR350  ICM   R1,B'0111',1(R1)      ANY MORE MEMBERS?
         BZ    REPR380               NO, BRANCH
         MVC   DOUBLE(8),DIRNAME     FIRST MEMBER NAME
         XC    DOUBLE(8),4(R1)       VERSUS SECOND MEMBER NAME
         XC    DOUBLE(*-*),DOUBLE    <<EXECUTED>>
         EX    R15,*-6               CLEAR THE FRONT FEW CHARACTERS
         OC    DOUBLE(8),DOUBLE      EXACT MATCH AFTER FIRST PART?
         BNZ   REPR350               NO, LOOP
         SPACE 1
         MVC   INSERT#1(8),4(R1)     MEMBER NAME
         MVC   INSERT#1(*-*),#COPTON <<EXECUTED>>
         EX    R15,*-6               MOVE THE FIRST FEW CHARACTERS
         M$MSG L885$1                ERROR MESSAGE
         OI    #COPALI,#COPDUP       MARK FOR LATER
         SPACE 2
REPR380  MVI   SUBPOOLT,21           TEMPORARY SUBPOOL
         LA    R0,DIREND-DIRLINK+8   PTR, DIR ENTRY, OLD NAME  GP AUG05
         ICM   R0,B'1000',SUBPOOLT   ADD SUBPOOL NUMBER
         GETMAIN R,LV=(0)
         XC    0(DIREND-DIRLINK,R1),0(R1)
         ST    R1,0(,R3)             CHAIN TO PREVIOUS ENTRY
         LR    R3,R1                 SAVE CURRENT POINTER
         MVC   4(DIREND-DIRNAME,R3),DIRNAME
         MVC   4+DIREND-DIRNAME(8,R3),DIRNAME  OLD/FROM NAME   GP AUG05
         MVC   INSERT#1(8),DIRNAME   WHO TO MOVE
         MVC   INSERT#2(8),=CL8'MOVED'
         SR    R1,R1
         ICM   R1,B'0001',#COPTO#         ANY RENAME OPERAND?
         BZ    REPR390                    NO, BRANCH
         BCTR  R1,0                       MACHINE LENGTH
         MVC   INSERT#2(8),=CL8'COPIED'   "COPIED  "
         MVC   INSERT#2+7(3),=C'TO '      "TO "
         MVC   INSERT#2+7+3(8),DIRNAME    MEMBER NAME
         MVC   INSERT#2+7+3(*-*),#COPTON  <<EXECUTED>>
         EX    R1,*-6                     MOVE IN PARTIAL NAME
         TR    INSERT#2+7+3(8),TRLINE     MAKE PRINTABLE
         MVI   MTLEN+4,18
REPR390  M$MSG L050$2
         MVI   MTLEN+4,8
         TM    #COPALI,#COPONE       A SINGLE MEMBER?
         BNO   REPR100               NO, BRANCH
         SPACE 3
REPR400  TM    #COPALI,#COPASSP      ASSOCIATED MEMBERS?
         BO    REPR405               YES, BRANCH
         MVI   #DELSAVE,FF-FMEM#MEM  ELEMENT HAS NOW BEEN ADDED
         TM    SPFLAG2,SPFLNCD       LINE COMMAND?
         BO    REPR405               YES, BRANCH
         TM    PMEMMIN,X'80'         MEMBER LIST?
         BO    REPR100               YES, BRANCH
         SPACE 1
REPR405  TM    #COPALI,#COPASSP      ASSOCIATES PHASE STARTED?
         BO    REPR500               YES, BRANCH
         TM    #COPALI,#COPASS2      ASSOCIATE MEMBERS TOO?
         BNO   REPR500               NO, BRANCH
         OI    #COPALI,#COPASSP      YES, ASSOCIATES PHASE STARTED
         NI    #COPALI,FF-#COPONE    WANT ALL ASSOCIATED MEMBERS
         NI    FLAGSII,FF-FDIRGRP    NORMAL INPUT NOW
         MVI   STARTTR+2,X'01'       TTR=000001 (START OF DIRECTORY)
         B     REPR100               DO ANY ASSOCIATED MEMBERS
         SPACE 3
REPR500  NI    FLAGSII,FF-FDIRGRP    NORMAL INPUT NOW
REPR600  ST    R4,#MEMLAST           SAVE LAST MEMBER POINTER
         TM    #COPALI,#COPDUP       ANY COLLISIONS?
         BO    NEWCMD                YES, BRANCH
         SPACE 4
         LA    R3,#MEMPTR              BASE FOR THE MEMBER LIST
         USING DIRLINK,R3
REPR740  LR    R4,R3                   SAVE LAST MEMBER ENTRY
         ICM   R3,B'0111',DIRLINK+1    LAST TABLE ENTRY?
         BZ    REPR770                 YES, BRANCH
         SR    R1,R1
         ICM   R1,B'0001',#COPTO#      ANY RENAME OPERAND?
         BZ    REPR760                 NO, BRANCH
         BCTR  R1,0                    ACTUAL STRING COUNT
         MVC   DIRNAME(*-*),#COPTON    <<EXECUTED>>
         EX    R1,*-6                  MOVE IN PARTIAL NAME
         SPACE 1
         CLI   #COPREP,1               REPLACE?
         BE    REPR760                 YES, BRANCH
         SPACE 1
         LA    R2,BLDLLIST-WORKAREA(R7)  IN WORKING STORAGE ADDRESS
         MVC   4(8,R2),DIRNAME         MOVE FOR BLDL
         BLDL  INDCB,0(R2)
         B     *+4(R15)                PROCESS RETURN CODE
         B     REPR750                   00 - ALREADY THERE
         B     REPR760                   04 - NOT FOUND
         B     IOERROR                   08 - I/O ERROR
         SPACE 1
REPR750  MVC   INSERT#1(8),DIRNAME     MEMBER NAME
         M$MSG L852$1                  MEMBER IS ALREADY PRESENT
         OI    #MEMPTR,X'80'           MARK FOR LATER
         SPACE 1
REPR760  MVC   DSNMEMQ(8),DIRNAME      MEMBER NAME
         BAS   R2,ENQMTEST             MEMBER IN USE?
         OI    #MEMPTR,X'40'           YES, MARK FOR LATER
         B     REPR740                 REPEAT FOR ALL MEMBERS
         SPACE 2
REPR770  TM    SPFLAG2,SPFLNCD         LINE COMMAND?
         BNO   REPR775                 NO, BRANCH
         MVC   SPFSAVM2,MEMNAME        DEFAULT OUTPUT MEMBER NAME
         CLI   #COPTON,X'40'           ANY COPY TO NAME?
         BNH   REPR776                 NO, BRANCH
         TM    #MEMPTR,X'80'           ANY FOUND ALREADY?
         BO    NEWCMD                  YES, BRANCH
         MVC   SPFSAVM2,#COPTON        ACTUAL OUTPUT MEMBER NAME
         B     REPR776                 NO, BRANCH
REPR775  M$ERRST MSGBLANK              ONE SPACING LINE
         TM    #MEMPTR,X'80'           ANY FOUND ALREADY?
         BO    NEWCMD                  YES, BRANCH
         LA    R1,PDS396A              REPRO PROMPT MESSAGE
         BAS   R2,YESNO                PROMPT FOR YES OR NO
         B     NEWCMD                  NO, QUIT
         SPACE 2
REPR776  DS    0H
         USING DIRLINK,R3
         LA    R3,#MEMPTR
         XC    #FIXTTRO,#FIXTTRO       CLEAR HIGHEST TTR
         SPACE 1
REPR780  ICM   R3,B'0111',DIRLINK+1    NEXT MEMBER ENTRY
         BZ    REPR782                 END OF LIST, BRANCH
         CLC   #FIXTTRO(3),DIRTTR      SAVE:CURRENT
         BH    REPR780                   HIGHER, BRANCH
         MVC   #FIXTTRO(3),DIRTTR      SAVE NEW HIGHEST TTR
         B     REPR780                 LOOP FOR ALL ENTRIES
         SPACE 1
         DROP  R3
REPR782  CLC   #FIXTTRO(3),DS1LSTAR    BEYOND END OF DATA MARKER?
         BL    REPR790                 NO, BRANCH
*** PAST DS1LSTAR -- PERHAPS WE NEED TO REOPEN THE DATA SET
         OI    FLAGSJJ,FNOREAD         CALL EXCP FOR OPEN ONLY
         MVC   STARTTR(3),DS1LSTAR     FIRST DISK ADDRESS TO READ
         L     R15,=V(EXCP)
         BASR  R14,R15
         NI    FLAGSJJ,FF-FNOREAD      EXCP CALL IS COMPLETED
         SPACE 2
REPR790  SR    R1,R1
         ICM   R1,B'0001',#COPTO#      ANY RENAME OPERAND?
         BZ    REPR794                 NO, BRANCH
         TM    SPFLAG2,SPFLNCD         LINE COMMAND?
         BO    REPR794                 YES, BRANCH
         BCTR  R1,0                    MACHINE LENGTH
         USING DIRLINK,R3
         LR    R3,R4                   LAST MEMBER NAME
         SPACE 1
         STCM  R1,B'0011',MEMBERD+1    LENGTH
         MVC   MEMBERD+1+2(8),DIRNAME  MEMBER NAME
         MVI   MEMBERD,FMEMBER1        SINGLE MEMBER
         TM    #COPALI,#COPONE         SINGLE MEMBER?
         BO    REPR792                 YES, BRANCH
         STCM  R1,B'0011',MEMBERD+1+2+8+8
         MVC   MEMBERD+1+2+8(8),DIRNAME  MEMBER NAME
         MVI   MEMBERD,FMEMBER1+FMEMBER2+FMEMRANG+FMEM#MEM
REPR792  NI    PMEMMIN,FF-X'80'        TURN OFF MEMBER LIST
         BAS   R14,DEFGROUP            DEFINE NEW MEMBER GROUP
REPR794  BAS   R6,RESERVE              RESERVE/ENQUEUE AS REQUIRED
         SPACE 1
         MVI   SUBPOOLT,21             MARK TEMPORARY STORAGE OBTAINED
         LA    R0,1024*3               STORAGE FOR THREE NOTELISTS
         ICM   R0,B'1000',SUBPOOLT     SUBPOOL
         GETMAIN R,LV=(0)
         ST    R1,#NOTEPTR             POINTER TO NOTELIST WORK AREA
         BAS   R2,SHUTSTW2             CLOSE AND FREE THE STOW DCB
         SPACE 1
         MVI   OPENLIST,X'80'
         OPEN  (STOWDCB,OUTPUT),MF=(E,OPENLIST)  OPEN IT FOR OUTPUT
         SPACE 1
         TM    DCBOFLGS-IHADCB+STOWDCB,DCBOFOPN  STOW DCB OPEN?
         LA    R1,L831                           NO OPEN ERROR MSG
         BNO   MSGNEW                            NO, QUIT
         LA    R3,#MEMPTR
         SPACE 1
REPR812  ICM   R3,B'0111',DIRLINK+1    NEXT MEMBER ENTRY
         BZ    REPR820                 END OF LIST, BRANCH
         MVI   DIRLINK,X'80'           ASSUME THE MEMBER IS TO BE MOVED
         TM    DIRFLAG,DIRALIAS        ALIAS ENTRY?
         BNO   REPR818                 NO, BRANCH
         LA    R2,#MEMPTR
         SPACE 1
REPR814  ICM   R2,B'0111',1(R2)              MORE IN THE LIST?
         BZ    REPR818                       NO, THIS ITEM WILL MOVE
         CR    R2,R3                         SAME ITEM?
         BE    REPR814                       YES, TRY AGAIN
         CLC   DIRTTR(3),DIRTTR-DIRLINK(R2)  SAME TTR?
         BNE   REPR814                       NO, BRANCH
         TM    DIRLINK-DIRLINK(R2),X'80'     OTHER ITEM TO BE MOVED?
         BO    REPR816                       YES, TRUE ALIAS
         TM    DIRFLAG-DIRLINK(R2),DIRALIAS  OTHER ITEM AN ALIAS?
         BO    REPR814                       YES, BRANCH
         SPACE 1
REPR816  MVI   DIRLINK,0                     DO NOT MOVE THIS ENTRY
REPR818  TM    DIRFLAG,DIR1TTR+DIR2TTR       ANY USER TTR'S?
         BZ    REPR812                       NO, BRANCH
         SPACE 1
         OI    DIRLINK,X'01'                 AT LEAST ONE TTR
         TM    DIRFLAG,DIR2TTR               TWO OR MORE TTR'S?
         BNO   *+8                           NO, BRANCH
         OI    DIRLINK,X'02'                 AT LEAST TWO TTR'S
         TM    DIRFLAG,DIR1TTR+DIR2TTR       THREE TTR'S?
         BNO   *+8                           NO, BRANCH
         OI    DIRLINK,X'04'                 THREE TTR'S
         B     REPR812
         SPACE 3
REPR820  LA    R3,#MEMPTR
REPR822  ICM   R3,B'0111',DIRLINK+1    ANY MORE MEMBERS IN THE LIST?
         BZ    REPR980                 NO, BRANCH
         MVC   INSERT#1(8),DIRNAME     MEMBER NAME
         MVC   INSERT#2(8),=CL8'MOVED'
         CLI   #COPTO#,0                 ANY RENAME OPERAND?
         BE    *+10                      NO, BRANCH
         MVC   INSERT#2(8),=CL8'CREATED' YES, CREATED INSTEAD
         M$MSG L051$2
         TM    DIRLINK,X'80'           MEMBER TO BE MOVED?
         BNO   REPR822                 NO, BRANCH
         LA    R2,DIRSTART             FIRST USER TTR
         L     R4,#NOTEPTR             START OF NOTELIST STORAGE
         LA    R5,X'10'+X'01'          MASK BITS FOR TESTING
         LA    R6,3                    LOOP CONTROL
         SPACE 1
*  INPUT THE OVERLAY NOTELIST TABLE
         TM    DIRLINK,*-*             <<EXECUTED>>
REPR824  EX    R5,*-4                  ANOTHER TTR?
         BZ    REPR830                 NO, BRANCH
         CLI   3(R2),0                 NOTELIST COUNT?
         BZ    REPR826                 NO, BRANCH
         OI    DIRLINK,*-*             <<EXECUTED>>
         EX    R5,*-4                  MARK FOR NOTELIST PROCESSING
         MVC   STARTTR(3),0(R2)        NOTELIST TTR ADDRESS
         L     R15,=V(EXCP)            INPUT IT
         BASR  R14,R15
         LA    R1,L751                 ASSUME AN INPUT PROBLEM
         LTR   R15,R15                 CORRECT?
         BP    MSGNEW                  YES, BRANCH
         LR    R14,R0                  INPUT ADDRESS
         SR    R15,R15
         IC    R15,3(,R2)              NUMBER OF NOTELIST ENTRIES
         SLL   R15,2                   SIZE OF NOTELIST AREA
         LR    R0,R4                   START OF THIS NOTELIST AREA
         LR    R1,R15                  SAVE AREA MOVE LENGTH
         MVCL  R0,R14                  SAVE THE NOTELIST DATA
         SPACE 1
REPR826  LA    R2,4(,R2)               NEXT POTENTIAL USER TTR
         LA    R4,1024(,R4)            NEXT POTENTIAL NOTELIST AREA
         SLL   R5,1                    NEXT MASK BITS
         BCT   R6,REPR824
         SPACE 3
*  READ EACH RECORD OF THE MEMBER
REPR830  MVC   STARTTR(3),DIRTTR       FIRST TTR OF THE MEMBER
         MVC   DIRNAME-WORKAREA(8,R7),DIREND    NAME FOR PAM   GP AUG05
         MVC   #FINDTTR(3),DIRTTR      SAVE THE FIRST TTR FOR LATER
         XC    #REPRLEN(4),#REPRLEN    RESET THE COUNT FIELD
         TM    FLAGSCC,RECFMF          RECFM=F?
         BO    REPR870                 YES, BRANCH
         MVI   #REPRLEN+3,4            SET LENGTH FOR RECFM=V
         TM    FLAGSCC,RECFMV          RECFM=V?
         BO    REPR870                 YES, BRANCH
         SPACE 2
REPR832  L     R15,=V(EXCP)
         BASR  R14,R15
         B     *+4(R15)                PROCESS RETURN CODE
         B     REPR834                   00 - GOOD READ
         B     REPR960                   04 - END OF MEMBER
         B     REPR960                   08 - END OF DATA SET
         B     NEWCMD                    12 - I/O ERROR
         SPACE 1
REPR834  STM   R2,R12,28(R13)          CONVERT CCHHR TO TTR
         LA    R2,CURMBB
         L     R1,INDCB+(DCBDEBAD-IHADCB)
         L     R15,ADDRRLTV
         LR    R3,R13
         BASR  R14,R15
         LR    R13,R3
         LM    R2,R12,28(R13)
         ST    R0,#FIXTTRI             SAVE CURRENT INPUT TTR
         TM    DIRLINK,X'77'           ANY PENDING TTR UPDATES?
         BZ    REPR844                 NO, BRANCH
         LA    R2,DIRSTART             FIRST USER TTR
         L     R4,#NOTEPTR             START OF NOTELIST STORAGE
         LA    R5,X'10'                MASK
         LA    R6,3                    LOOP CONTROL
         SPACE 1
         TM    DIRLINK,*-*             <<EXECUTED>>
REPR836  EX    R5,*-4                  ANY NOTELIST FOR THIS ENTRY?
         BZ    REPR838                 NO, BRANCH
         CLM   R0,B'1110',0(R2)        IS THIS THE NOTELIST ENTRY?
         BE    REPR840                 YES, BRANCH
REPR838  LA    R2,4(,R2)               NEXT USER TTR
         LA    R4,1024(,R4)            NEXT NOTELIST ENTRY
         SLL   R5,1                    NEXT MASK
         BCT   R6,REPR836
         B     REPR844                 NOT THIS RECORD, BRANCH
         SPACE 2
REPR840  SR    R0,R0
         IC    R0,3(R2)                NUMBER OF NOTELIST ENTRIES
         LA    R1,L750                 ERROR MESSAGE FOR A PROBLEM
         XI    DIRLINK,*-*             <<EXECUTED>>
         EX    R5,*-4                  TURN OFF THIS NOTELIST ENTRY
         L     R5,EXCPBUFF             OUTPUT START ADDRESS
REPR842  TM    3(R4),X'80'             NOTELIST TTR PREVIOUSLY UPDATED?
         BNO   MSGNEW                  NO, ERROR
         MVC   0(3,R5),0(R4)           REPLACE THE BUFFER TTR
         LA    R4,4(,R4)               NEXT STORAGE ADDRESS
         LA    R5,4(,R5)               NEXT BUFFER ADDRESS
         BCT   R0,REPR842              MOVE AND CHECK ALL ENTRIES
         SPACE 3
REPR844  L     R2,EXCPBUFF             START OF OUTPUT
         CLI   0(R2),X'80'             IDR RECORD?
         BNE   REPR846                 NO, BRANCH
         TM    2(R2),X'01'             ZAP IDR?
         BNO   REPR846                 NO, BRANCH
         OI    #REPRLEN,X'80'          MARK ZAP IDR INPUT
         CLI   #REPADD,1               ADD AN IDR RECORD?
         BNE   REPR846                 NO, BRANCH
         OI    3(R2),X'40'             MARK ZAP IDR RECORD AS CONTINUED
         SPACE 1
REPR846  TM    #REPRLEN,X'80'          ANY ZAP IDR?
         BNO   REPR850                 NO, BRANCH
         TM    #REPRLEN,X'40'          ZAP IDR ALREADY ADDED?
         BO    REPR850                 YES, BRANCH
         CLI   0(R2),X'80'             IDR RECORD?
         BNE   REPR848                 NO, BRANCH
         TM    2(R2),X'01'             ZAP IDR?
         BO    REPR850                 YES, BRANCH
REPR848  CLI   #REPADD,1               ADD AN IDR RECORD?
         BNE   REPR850                 NO, BRANCH
         OI    #REPRLEN,X'40'          IDR RECORD ADDED
         XC    MSGTEXT1(256),MSGTEXT1  CLEAR THE RECORD TO ADD
         MVI   MSGTEXT1+0,X'80'        IDR RECORD
         MVI   MSGTEXT1+1,X'FA'        IDR RECORD LENGTH
         MVI   MSGTEXT1+2,X'01'        IDR RECORD FOR ZAP INFORMATION
         LA    R0,251                  RECORD LENGTH
         STH   R0,STOWDCB+DCBBLKSI-IHADCB  UPDATE IN THE DCB
         LA    R2,MSGTEXT1             START OF DATA
         WRITE FIXPDSD,SF,STOWDCB,(2),'S',MF=E
         CHECK FIXPDSD
         M$MSG L276                    ZAP IDR ADDED          DRK NOV06
         SPACE 3
*  OUTPUT EACH RECORD AND CHANGE TTR POINTERS AS NECESSARY
REPR850  L     R0,LS                   RECORD LENGTH
         STH   R0,STOWDCB+DCBBLKSI-IHADCB  UPDATE IN THE DCB
         L     R2,EXCPBUFF             START OF DATA
         WRITE FIXPDSD,SF,STOWDCB,(2),'S',MF=E
         CHECK FIXPDSD
         NOTE  STOWDCB                 OUTPUT TTR
         ST    R1,#FIXTTRO
         SPACE 1
         CLC   DIRTTR(3),#FINDTTR      FIRST MEMBER RECORD?
         BNE   *+10                    NO, BRANCH
         MVC   DIRTTR(3),#FIXTTRO      YES, UPDATE THE TTR
         SPACE 1
         TM    DIRLINK,X'77'           ANY PENDING TTR UPDATES?
         BZ    REPR832                 NO, BRANCH
         LA    R2,DIRSTART             FIRST USER TTR
         L     R4,#NOTEPTR             START OF NOTELIST
         LA    R5,X'10'                NOTELIST MASK
         LA    R6,3                    LOOP CONTROL
         LA    R14,X'01'               TTR MASK
         SPACE 1
         TM    DIRLINK,*-*             <<EXECUTED>>
REPR852  EX    R14,*-4                 TTR IN THIS POSITION?
         BNO   REPR854                 NO, BRANCH
         CLC   #FIXTTRI(3),0(R2)       THIS INPUT TTR?
         BNE   REPR854                 NO, BRANCH
         STCM  R1,B'1110',0(R2)        YES, UPDATE THE TTR
         XI    DIRLINK,*-*             <<EXECUTED>>
         EX    R14,*-4                 TURN OFF THE TTR INDICATOR
         SPACE 1
         TM    DIRLINK,*-*             <<EXECUTED>>
REPR854  EX    R5,*-4                  NOTELIST FOR THIS ENTRY?
         BNO   REPR858                 NO, BRANCH
         LR    R15,R4                  START OF NOTELIST
         SR    R0,R0
         IC    R0,3(R2)                NUMBER OF NOTELIST ENTRIES
REPR856  CLC   0(3,R15),#FIXTTRI       THIS TTR?
         BNE   *+12                    NO, BRANCH
         STCM  R1,B'1110',0(R15)       YES, UPDATE THE TTR
         OI    3(R15),X'80'                 AND MARK AS UPDATED
         LA    R15,4(,R15)             NEXT NOTELIST ENTRY
         BCT   R0,REPR856
REPR858  LA    R2,4(,R2)               NEXT USER TTR
         LA    R4,1024(,R4)            NEXT NOTELIST STORAGE
         SLL   R5,1                    NEXT NOTELIST BIT MASK
         SLL   R14,1                   NEXT TTR BIT MASK
         BCT   R6,REPR852
         B     REPR832                MOVE ALL MEMBERS
         SPACE 3
*  REBLOCK FOR REPRO
REPR870  L     R15,=V(EXCP)            NEXT USER TTR
         BASR  R14,R15
         B     *+4(R15)                PROCESS RETURN CODE
         B     REPR872                   00 - GOOD READ
         B     REPR918                   04 - END OF MEMBER
         B     REPR918                   08 - END OF DATA SET
         B     NEWCMD                    12 - I/O ERROR
         SPACE 1
REPR872  L     R5,LS                   CURRENT LENGTH
         LR    R6,R0                   START OF RECORD
         AR    R5,R6                   END OF RECORD
         BCTR  R5,0                    END OF RECORD -1
         LA    R2,REPR890              WHERE-TO-GO FOR FB
         TM    FLAGSCC,RECFMF          RECFM=F?
         LH    R4,LRECL
         BOR   R2                      YES, BRANCH
         AH    R6,=H'4'                SKIP FIRST VB COUNT
         LA    R2,REPR874              WHERE-TO-GO FOR VB
         SPACE 1
REPR874  SR    R4,R4
         ICM   R4,B'0011',0(R6)        LRECL FOR VB
         B     REPR890                 PROCESS THIS RECORD
         SPACE 1
REPR880  BXLE  R6,R4,0(R2)             GET NEXT LRECL
         B     REPR870                 GET NEXT RECORD
         SPACE 1
REPR890  STM   R3,R6,#LFSAVE4
         L     R6,#REPRBUF             START OF BUFFER
         L     R5,#REPRLEN             LENGTH OF BUFFER
         AR    R5,R6                   END OF BUFFER
         SPACE 1
         LR    R1,R5
         AR    R1,R4                   TEST ADDITION
         SPACE 1
         L     R0,#REPRMAX             MAXIMUM BLKSIZE
         A     R0,#REPRBUF             MAXIMUM ADDRESS
         CR    R1,R0                   FIT IN THIS BLOCK?
         BH    REPR920                 NO, BRANCH
         SPACE 2
REPR910  L     R0,#LFSAVE4+12          CURRENT RECORD
         LR    R1,R4                   CURRENT LRECL
         LR    R4,R5                   CURRENT OUTPUT START
         LR    R5,R1                   CURRENT LRECL
         MVCL  R4,R0                   MOVE THE CURRENT RECORD
         LM    R3,R6,#LFSAVE4          RESET REGISTERS
         L     R0,#REPRLEN
         AR    R0,R4                   ADD CURRENT LENGTH
         ST    R0,#REPRLEN             NEW LENGTH
         B     REPR880
         SPACE 2
REPR918  OI    #REPRLEN,X'80'          MARK END OF MEMBER
         LH    R0,#REPRLEN+2           LENGTH
         LTR   R0,R0                   ANY DATA?
         BZ    REPR960                 NO, BRANCH
         CH    R0,=H'4'                FOUR?
         BH    REPR920                 NO, BRANCH
         TM    FLAGSCC,RECFMV          RECFM=V?
         BO    REPR960                 YES, BRANCH
         SPACE 1
REPR920  LH    R0,#REPRLEN+2           LENGTH
         STH   R0,STOWDCB+DCBBLKSI-IHADCB  UPDATE IN THE DCB
         L     R4,#REPRBUF             START OF DATA
         TM    FLAGSCC,RECFMV          RECFM=V?
         BNO   *+8                     NO, BRANCH
         STH   R0,0(,R4)               UPDATE THE LENGTH FIELD
         WRITE FIXPDSD,SF,STOWDCB,(4),'S',MF=E
         CHECK FIXPDSD
         NOTE  STOWDCB                 OUTPUT TTR
         ST    R1,#FIXTTRO
         SPACE 1
         CLC   DIRTTR(3),#FINDTTR      FIRST MEMBER RECORD?
         BNE   *+10                    NO, BRANCH
         MVC   DIRTTR(3),#FIXTTRO      YES, UPDATE THE TTR
         SPACE 1
         LM    R3,R6,#LFSAVE4          RESET REGISTERS
         TM    #REPRLEN,X'80'          END OF MEMBER?
         BO    REPR960                 YES, BRANCH
         MVC   #REPRLEN(4),ZERO        CLEAR THE LENGTH FIELD
         TM    FLAGSCC,RECFMF          RECFM=F?
         BO    REPR890                 YES, BRANCH
         MVI   #REPRLEN+3,4            SET INPUT FOR COUNT FIELD
         B     REPR890
         SPACE 3
*  VERIFY THAT THE MEMBER WAS CORRECTLY PROCESSED
REPR960  LA    R1,L871                 ASSUME A TTR WAS NOT UPDATED
         TM    DIRLINK,X'77'           CORRECT?
         BNZ   MSGNEW                  YES, BRANCH
         SPACE 1
         CLC   DIRTTR(3),#FINDTTR      A NULL MEMBER?
         BNE   REPR962                 NO, BRANCH
         TM    DS1SMSFG-WORKAREA(R7),DS1PDSE PDSE DATASET?     GP AUG05
         BO    REPR962                 YES, LEAVE RELAD ALONE  GP AUG05
         SPACE 1
* UPDATE TTR BY TWO AND STOW AGAIN FOR NULL MEMBERS
         STOW  STOWDCB,DIRNAME,D
         SPACE 1
         ICM   R1,B'0111',#FIXTTRO                 HIGHEST OUTPUT TTR
         LA    R1,2(,R1)
         STCM  R1,B'0111',DCBRELAD-IHADCB+STOWDCB  HIGHEST OUTPUT TTR+2
         STOW  STOWDCB,DIRNAME,A
         LA    R1,L832
         LTR   R15,R15
         BP    EXIT8M
         SPACE 1
         SR    R1,R1                               YES, CHANGE THE TTR
         ICM   R1,B'0111',DCBRELAD-IHADCB+STOWDCB  TTR OF LAST EOF
         S     R1,=F'1'                            TTR OF NULL DATA
         STCM  R1,B'0111',DIRTTR                   TTR OF NULL DATA
         STCM  R1,B'0111',#FIXTTRO                 HIGHEST OUTPUT TTR
         SPACE 2
REPR962  TM    DIRFLAG,DIRALIAS        ALIAS (WITH NO MAIN)?
         BNO   REPR970                 NO, BRANCH
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR  TTR
         XI    DIRFLAG,DIRALIAS        TEMPORARILY TURN OFF ALIAS BIT
         STOW  STOWDCB,DIRNAME,R       REPLACE THIS MEMBER ENTRY
         OI    DIRFLAG,DIRALIAS        RESET THE ALIAS BIT
         SPACE 1
         CH    R15,=H'12'              RC > 12                DRK AUG05
         BH    IOERROR                 YES, ERROR CONDITION   DRK AUG05
         B     *+4(R15)                PROCESS RETURN CODE
         B     REPR970                   00 - REPLACED
         EX    0,*                       04 - SHOULD NOT OCCUR
         B     REPR970                   08 - NOT PRESENT
         B     FULLDIR                   12 - DIRECTORY FULL
*        B     IOERROR                   16 - I/O ERROR
         SPACE 3
*  UPDATE THIS MEMBER AND ALL OF ITS ALIASES
REPR970  LA    R2,#MEMPTR
         LR    R4,R3
         SPACE 1
REPR972  TM    DS1SMSFG-WORKAREA(R7),DS1PDSE PDSE DATASET?     GP AUG05
         BO    REPR973                 YES, LEAVE RELAD ALONE  GP AUG05
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR-DIRLINK(R4)  TTR
REPR973  EQU   *                                               GP AUG05
         STOW  STOWDCB,4(R4),R         REPLACE THIS MEMBER ENTRY
         SPACE 1
         CH    R15,=H'12'              RC > 12                DRK AUG05
         BH    IOERROR                 YES, ERROR CONDITION   DRK AUG05
         B     *+4(R15)                PROCESS RETURN CODE
         B     REPR974                   00 - REPLACED
         EX    0,*                       04 - SHOULD NOT OCCUR
         B     REPR974                   08 - NOT PRESENT
         B     FULLDIR                   12 - DIRECTORY FULL
*        B     IOERROR                   16 - I/O ERROR
         SPACE 1
REPR974  ICM   R2,B'0111',1(R2)               NEXT MEMBER ENTRY
         BZ    REPR822                        END OF LIST, BRANCH
         CLC   #FINDTTR(3),DIRTTR-DIRLINK(R2) THIS TTR TOO?
         BNE   REPR974                        NO, BRANCH
         CR    R2,R3                          SAME ENTRY?
         BE    REPR974                        YES, BRANCH
         LR    R4,R2
         NI    DIRLINK-DIRLINK(R2),FF-X'80'   DO NOT MOVE THIS MEMBER
         MVC   DIRTTR-DIRLINK(3,R2),DIRTTR    NEW PRIMARY TTR
         LA    R14,DIRSTART-DIRLINK(,R2)      FIRST ALIAS USER TTR
         LA    R15,DIRSTART                   FIRST MOVED USER TTR
         LA    R5,X'01'                       MASK BIT
         LA    R6,3                           LOOP CONTROL
         SPACE 1
         TM    DIRLINK-DIRLINK(R2),*-*        <<EXECUTED>>
REPR976  EX    R5,*-4                         A TTR AT THIS ENTRY?
         BNO   REPR972                        NO, BRANCH
         MVC   0(3,R14),0(R15)                YES, UPDATE THE TTR ENTRY
         LA    R14,4(,R14)                    NEXT ALIAS TTR
         LA    R15,4(,R15)                    NEXT MOVED TTR
         SLL   R5,1                           NEXT MASK BIT
         BCT   R6,REPR976
         B     REPR972
         DROP  R3
         SPACE 3
REPR980  BAS   R2,CLOSEIT                        CLOSE INPUT DATA SET
         LA    R0,RESTART0                       ABEND RECOVERY ADDRESS
         ST    R0,RECOVER
         BAS   R2,DEALLDCB             FREE THE DATA SET
         B     RESTART0                REALLOCATE AND START OVER
