         TITLE 'P D S  --  PDS RENAME                        09/28/88'
***********************************************************************
***      RENAME SUBCOMMAND                                          ***
***********************************************************************
*
         SPACE 1
RENAME   CSECT
         USING *,R8
         TM    DS1SMSFG,DS1PDSE        PDSE DATASET?          DRK MAY08
         BNO   RENAME2                 NO, CONTINUE           DRK MAY08
         TM    #RENSWAP,X'01'          SWAP MEMBER NAMES?     DRK MAY08
         BNO   RENAME2                 NO, CONTINUE           DRK MAY08
         M$MSG L707                    YES, DO NOT ALLOW      DRK MAY08
         M$ERRST MSGBLANK              ONE BLANK LINE         DRK MAY08
         B     NEWCMD                                         DRK MAY08
RENAME2  DS    0H                                             DRK MAY08
         CLI   #RENRANG,1              RENAME GROUP?
         BE    RENAME30                YES, BRANCH
         SPACE 1
         TM    SPFLAG2,SPFLNCD            LINE COMMAND?        SS AUG85
         BNO   RENAME3                    NO, BRANCH
         MVC   SPFSAVM2,MEMBER2           GET MEMBER NAME      SS AUG85
         B     RENAME6                    NO, BRANCH
         SPACE 1
RENAME3  MVC   MEMBERD+1(2),LMEMBER2   CHANGE DEFAULT MEMBER LENGTH
         MVC   MEMBERD+1+2(8),MEMBER2  CHANGE DEFAULT MEMBER NAME
         MVI   MEMBERD,FMEMBER1        ONLY ONE MEMBER NAME NOW
         NI    PMEMMIN,FF-X'80'        NO MEMBER LIST NOW
         BAS   R14,DEFGROUP            ADD DEFAULT GROUP
         CLI   #RENREP,0               ANY REPBY NAME?
         BE    RENAME6                 NO, BRANCH
         MVC   MEMBERD+1(2),LMEMBER1   YES, SAVE NEW DEFAULT NAME
         MVC   MEMBERD+1+2(8),MEMBER1  YES, SAVE NEW DEFAULT NAME
         BAS   R14,DEFGROUP            ADD DEFAULT GROUP
         SPACE 1
RENAME6  MVC   DSNMEMQ(8),MEMBER1      MEMBER NAME TO TEST
         BAS   R2,ENQMTEST             MEMBER IN USE?
         B     NEWCMD                  YES, ERROR
         SPACE 1
         BAS   R2,OPENSTOW             OPEN STOW DCB; ENQUEUES
         B     NEWCMD                  COULD NOT OPEN -- ERROR
         LA    R6,NEWCMD               ASSUME NOT A SWAP REQUEST
         SPACE 1
         CLC   MEMBER1(8),MEMBER2      UPDATE ALIASES ONLY?
         BE    RENAME90                YES, BRANCH
         SPACE 2
         TM    #RENSWAP,X'01'          SWAP MEMBER NAMES?
         BNO   RENAME20                NO, BRANCH
         SPACE 1
*** RENAME WITH SWAP OPERAND *** *** *** *** *** *** *** *** *** *** **
         MVC   MSGTEXT1(DIREND-DIRNAME),DIRNAME  SAVE THE FIRST MEMBER
         XC    DIRUSER,DIRUSER         CLEAR THE USER FIELDS
         MVC   DIRNAME,MEMBER2         SECOND MEMBER NAME
         SPACE 2
         BLDL  INDCB,BLDLLIST          LOCATE DIRECTORY ENTRY
         B     *+4(R15)                PROCESS RETURN CODE
         B     RENAME10                  00 - SUCCESSFUL
         B     NOMEMBER                  04 - MEMBER NOT FOUND
         B     IOERROR                   08 - I/O ERROR
         SPACE 2
RENAME10 MVC   DIRFLAG(DIREND-DIRFLAG),DIRFLAG+2 BLDL ADDS THINGS
         SPACE 1
         MVC   DIRNAME,MEMBER2         SECOND MEMBER NAME
         STOW  STOWDCB,DIRNAME,D       ISSUE STOW WITH DELETE OPTION
         LTR   R15,R15                 SUCCESSFUL?
         BNZ   IOERROR                 NO, I/O ERROR
         MVC   DIRNAME,MEMBER1         FIRST MEMBER NAME
         STOW  STOWDCB,DIRNAME,D       ISSUE STOW WITH DELETE OPTION
         LTR   R15,R15                 SUCCESSFUL?
         BNZ   IOERROR                 NO, I/O ERROR
         SPACE 1
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR  TTR
         STOW  STOWDCB,DIRNAME,A       ADD THIS MEMBER ENTRY
         SPACE 1
         LTR   R15,R15                 SUCCESSFUL?
         BNZ   IOERROR                 NO, I/O ERROR
         MVC   MSGTEXT2(DIREND-DIRNAME),DIRNAME  SAVE THE SECOND MEMBER
         SPACE 2
         MVC   DIRNAME(DIREND-DIRNAME),MSGTEXT1  FIRST MEMBER
         MVC   DIRNAME,MEMBER2         SECOND MEMBER NAME
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR  TTR
         STOW  STOWDCB,DIRNAME,A       ADD THIS MEMBER ENTRY
         SPACE 1
         LTR   R15,R15                 SUCCESSFUL?
         BNZ   IOERROR                 NO, I/O ERROR
         BAS   R6,RENAME90             UPDATE ANY ALIAS MEMBERS
         MVC   DIRNAME(DIREND-DIRNAME),MSGTEXT2  SECOND MEMBER
         MVC   MEMBER1(8),MEMBER2      SWAP THE
         MVC   MEMBER2(8),DIRNAME              MEMBER NAMES
         BAS   R6,RENAME90             UPDATE ANY ALIAS MEMBERS
         B     NEWCMD
         SPACE 3
*** RENAME USING THE REPBY PARAMETER *** *** *** *** *** *** *** *** **
RENAME20 CLI   #RENREP,0               ANY REPBY OPERAND?
         BE    RENAME26                NO, BRANCH
         SPACE 1
         MVC   DIRNAME(8),#RENREP      MEMBER NAME FOR AN ERROR MESSAGE
         BLDL  INDCB,BLDLLIST          CHECK FOR MEMBER PRESENCE
         B     *+4(R15)                PROCESS RETURN CODE
         B     RENAME22                  00 - FOUND THE MEMBER
         B     NOMEMBER                  04 - MEMBER WAS NOT FOUND
         B     IOERROR                   08 - I/O ERROR
         SPACE 1
RENAME22 MVC   DIRNAME(8),MEMBER2      MEMBER NAME FOR AN ERROR MESSAGE
         STOW  STOWDCB,MEMBER1,C       ISSUE STOW WITH CHANGE OPTION
         SPACE 1
         CH    R15,=H'12'              RC > 12                DRK AUG05
         BH    IOERROR                 YES, ERROR CONDITION   DRK AUG05
         B     *+4(R15)                PROCESS RETURN CODE
         B     RENAME24                  00 - SUCCESSFUL
         B     MEMEXIST                  04 - THIS NAME ALREADY EXISTS
         B     NOMEMBER                  08 - MEMBER NAME NOT FOUND
         B     FULLDIR                   12 - DIRECTORY FULL  DRK MAY03
*        EX    0,*                       12 - INVALID RETURN CODE
*        B     IOERROR                   16 - DIRECTORY I/O ERROR
         SPACE 1
RENAME24 BAS   R6,RENAME90             UPDATE ANY ALIAS ENTRIES
         MVC   MEMBER2(8),MEMBER1      SWAP MEMBER NAMES
         MVC   MEMBER1(8),#RENREP      SWAP MEMBER NAMES
         LA    R6,NEWCMD               EXIT ADDRESS
         SPACE 3
*** RENAME WITH NO OPERANDS  *** *** *** *** *** *** *** *** *** *** **
RENAME26 MVC   DIRNAME(8),MEMBER2      MEMBER NAME FOR AN ERROR MESSAGE
         STOW  STOWDCB,MEMBER1,C       ISSUE STOW WITH CHANGE OPTION
         SPACE 1
         CH    R15,=H'12'              RC > 12                DRK AUG05
         BH    IOERROR                 YES, ERROR CONDITION   DRK AUG05
         B     *+4(R15)                PROCESS RETURN CODE
         B     RENAME90                  00 - SUCCESSFUL
         B     MEMEXIST                  04 - THIS NAME ALREADY EXISTS
         B     NOMEMBER                  08 - MEMBER NAME NOT FOUND
         B     FULLDIR                   12 - DIRECTORY FULL  DRK MAY03
*        EX    0,*                       12 - INVALID RETURN CODE
*        B     IOERROR                   16 - DIRECTORY I/O ERROR
         SPACE 3
*** RENAME WITH GROUP OPERAND ** *** *** *** *** *** *** *** *** *** **
RENAME30 NI    FLAGSBB,FF-FLINESET     NO OUTPUT IN PROGRESS YET
         MVI   SUBPOOLT,21             TEMPORARY STORAGE IN USE
         MVI   STARTTR+2,X'01'         READ THE DIRECTORY
         LA    R5,256/2                256 MEMBERS INITIALLY
         SPACE 1
RENAME40 SLL   R5,1                    MEMBERS*2 FOR EACH LOOP
         SR    R2,R2
         LR    R3,R5
         LR    R0,R5
         SLL   R0,4                    MEMBERS*16 IS TABLE SIZE
         ICM   R0,B'1000',SUBPOOLT
         GETMAIN R,LV=(0)              ALLOCATE A NEW MEMBER TABLE
         LR    R4,R1                   NEW TABLE ADDRESS
         ICM   R0,B'1111',#MEMPTR      ANY PREVIOUS MEMBERS?
         BZ    RENAME42                NO, BRANCH
         SRL   R3,1                    YES, USE SECOND HALF OF TABLE
         LR    R14,R4                  NEW TABLE START ADDRESS
         LR    R15,R5
         SLL   R15,3                   MEMBERS*8 IS OLD TABLE SIZE
         LR    R2,R15                  DISPLACEMENT TO NEW PART
         LR    R1,R15
         MVCL  R14,R0                  PRESERVE THE PREVIOUS TABLE
         L     R1,#MEMPTR
         LR    R0,R2                   LENGTH TO FREE
         ICM   R0,B'1000',SUBPOOLT     SUBPOOL TO FREE
         FREEMAIN R,LV=(0),A=(1)
         SPACE 1
RENAME42 ST    R4,#MEMPTR              MEMBER TABLE BASE
         AR    R4,R2                   WHERE TO ADD MEMBERS
         SPACE 3
RENAME44 BAS   R14,READDIR             GET THE NEXT MEMBER
         B     RENAME52                END OF MEMBERS, BRANCH
         SPACE 1
         LH    R1,LMEMBER1             MEMBER NAME LENGTH
         CLC   MEMNAME(*-*),MEMBER1    <<EXECUTED>>
         EX    R1,*-6                  MEMBER:GROUP
         BL    RENAME44                  LOW, LOOP
         BH    RENAME52                  HIGH, DONE WITH FIRST PHASE
         MVC   DSNMEMQ(8),MEMNAME        EQUAL, CHECK FOR ENQUEUED
         BAS   R2,ENQMTEST
         NOP   0(,R0)                  OUTPUT AN ERROR MESSAGE ONLY
         SPACE 2
         LA    R2,MEMNAME+7
         TM    FLAGSBB,FLINESET        LINE IN PROGRESS?
         BO    RENAME46                YES, BRANCH
         OI    FLAGSBB,FLINESET        LINE NOW IN PROGRESS
         LA    R1,80                   ASSUME AN ACTIVE MODE
         TM    SPFLAG0,SPFDON          ISPMODE ACTIVE?
         BO    RENAME45                YES, BRANCH
         TM    CONTOPTN,1              ANY LOG RECORDING?
         BO    RENAME45                YES, BRANCH
*        GTSIZE ,                      TERMINAL SIZE           SS MAY89
         GTSIZE
         CH    R1,=H'120'              120 OR LESS BYTES?
         BL    *+8                     YES, BRANCH
         LH    R1,=H'120'              NO, USE 120 BYTES
RENAME45 SH    R1,=H'7'                LESS SEVEN FOR PREFIX
         ST    R1,LINESIZE             CHARACTERS/LINE
         MVC   MSGLINE+4(L'MSGRENAM),MSGRENAM
         LA    R6,MSGLINE+4+L'MSGRENAM
         MVC   FULLWORD(3),L161$1
         SPACE 1
RENAME46 CLI   0(R2),X'40'             SCAN FOR
         BNE   *+8                             LAST
         BCT   R2,RENAME46                         NON-BLANK
         SPACE 1
         LA    R0,MEMNAME
         SR    R2,R0
         BNM   *+6
         SR    R2,R2
         LA    R15,2(R6,R2)
         LA    R1,MSGLINE
         SR    R15,R1
         C     R15,LINESIZE
         BNH   RENAME50
         LA    R1,MSGLINE+4
         SR    R6,R1
         STC   R6,MTLEN
         MVC   INSERT#1(127),MSGLINE+4
         LA    R1,FULLWORD
         M$MSG (R1)
         MVI   MTLEN,8
         MVI   FULLWORD+2,C'9'
         LA    R6,MSGLINE+3            INDENT FOR BLANKS
         SPACE 1
RENAME50 MVI   0(R6),X'40'
         MVC   1(8,R6),MEMNAME
         TR    1(8,R6),TRLINE
         LA    R1,2(R2,R6)
         MVI   0(R1),C','
         LA    R6,3(R2,R6)
         MVC   0(16,R4),MEMNAME        CURRENT MEMBER NAME
         LA    R4,16(,R4)              NEXT MEMBER NAME
         S     R3,=F'1'                ANY HOLD POSITIONS LEFT?
         BP    RENAME44                YES, BRANCH
         B     RENAME40                NO, REALLOCATE
         SPACE 2
RENAME52 LA    R1,L711                 ASSUME NO MEMBERS FOUND
         TM    FLAGSBB,FLINESET        LINE IN PROGRESS?
         BZ    MSGNEW                  NO, EXIT
         LA    R1,MSGLINE+5
         SR    R6,R1
         STC   R6,MTLEN
         MVC   INSERT#1(127),MSGLINE+4
         LA    R1,FULLWORD
         M$MSG (R1)
         MVI   MTLEN,8
         M$ERRST MSGBLANK
         SPACE 1
         LA    R1,PDS391A              VERIFY THAT YOU WISH TO RENAME..
         BAS   R2,YESNO                PROMPT FOR YES OR NO
         B     NEWCMD                  NO, BRANCH
         SPACE 2
         BAS   R2,OPENSTOW             OPEN STOW DCB; ENQUEUE
         B     NEWCMD                  OPEN FAILED -- ERROR
         SPACE 3
         L     R3,#MEMPTR              FIRST MEMBER POINTER
         LA    R6,RENAME60             WHERE TO GO
         MVI   #RENDEFT,FMEMBER1+FMEMBER2+FMEMRANG+FMEM#MEM
         MVC   #RENDEFT+1(2+8+8+2),LMEMBER1  LENGTH1, MEMBER1, M2, L2
         SPACE 1
         MVC   MEMBERD(1+2+8+8+2),#RENDEFT    FLAG, LEN1, MEM1, M2, L2
         MVC   MEMBERD+1(2),#RENDEFT+1+2+8+8  REPEAT LEN2 FOR LEN1
         MVC   MEMBERD+1+2(8),#RENDEFT+1+2+8  REPEAT MEM2 FOR MEM1
         NI    PMEMMIN,FF-X'80'               NO MEMBER LIST NOW
         BAS   R14,DEFGROUP            DEFINE NEW MEMBER GROUP
RENAME60 CR    R3,R4                   DONE?
         BE    NEWCMD                  YES, BRANCH
         MVC   DIRNAME(16),0(R3)       CURRENT MEMBER NAME
         LA    R3,16(,R3)              NEXT MEMBER NAME
         AGO   .RN02                                          ABL DEC86
         MVC   MEMBER1(8),DIRNAME
         MVC   MEMBER2(8),DIRNAME
         LH    R1,LMEMBER2
         MVC   MEMBER2(*-*),#RENDEFT+1+2+8  <<EXECUTED>>
         EX    R1,*-6                       MOVE IN FIRST FEW BYTES
.RN02    ANOP                                                 ABL DEC86
         MVC   MEMBER1(8),DIRNAME
         MVC   MEMBER2(8),BLANKS
         LH    R1,LMEMBER2
         MVC   MEMBER2(*-*),#RENDEFT+1+2+8  <<EXECUTED>>
         EX    R1,*-6                       MOVE IN FIRST FEW BYTES
         LH    R0,LMEMBER1                  LENGTH1
         LA    R15,MEMBER1+1                WHERE TO GET
         AR    R15,R0                                   INPUT
         LA    R14,MEMBER2+1                WHERE TO MOVE
         AR    R14,R1                                    OUTPUT
         CR    R0,R1                        LENGTH1>LENGTH2?
         BH    *+6                          YES, BRANCH
         LR    R0,R1                        NO, USE LENGTH2
         LA    R1,6                         MACHINE LENGTH
         SR    R1,R0                        6-MAX(LENGTH1,LENGTH2) OK?
         BM    RENAME62                     NO, BRANCH
         MVC   0(*-*,R14),0(R15)            <<EXECUTED>>
         EX    R1,*-6                       MOVE IN REST OF NAME
RENAME62 DS    0H
         SPACE 1
         CLC   MEMBER1(8),MEMBER2      UPDATE ALIASES ONLY?
         BE    RENAME90                YES, BRANCH
         SPACE 2
         TM    #RENSWAP,X'01'          SWAP MEMBER NAMES?
         BO    RENAME70                YES, BRANCH
         SPACE 1
         MVC   DIRNAME(8),MEMBER2      MEMBER NAME FOR AN ERROR MESSAGE
         STOW  STOWDCB,MEMBER1,C       ISSUE STOW WITH CHANGE OPTION
         SPACE 1
         CH    R15,=H'12'              RC > 12                DRK AUG05
         BH    IOERROR                 YES, ERROR CONDITION   DRK AUG05
         B     *+4(R15)                PROCESS RETURN CODE
         B     RENAME90                  00 - SUCCESSFUL
         B     MEMEXIST                  04 - THIS NAME ALREADY EXISTS
         EX    0,*                       08 - MEMBER NAME NOT FOUND
         B     FULLDIR                   12 - DIRECTORY FULL  DRK MAY03
*        EX    0,*                       12 - INVALID RETURN CODE
*        B     IOERROR                   16 - DIRECTORY I/O ERROR
         SPACE 3
         SPACE 3
*** RENAME WITH GROUP AND SWAP OPERAND   *** *** *** *** *** *** *** **
RENAME70 XC    DIRUSER,DIRUSER         CLEAR THE USER FIELDS
         MVC   DIRNAME,MEMBER1         FIRST MEMBER NAME
         SPACE 1
         BLDL  INDCB,BLDLLIST          LOCATE DIRECTORY ENTRY
         LTR   R15,R15                 ANY PROBLEM?
         BNZ   IOERROR                 YES, ERROR
         SPACE 1
         MVC   DIRFLAG(DIREND-DIRFLAG),DIRFLAG+2 BLDL ADDS THINGS
         MVC   MSGTEXT1(DIREND-DIRNAME),DIRNAME  SAVE THE FIRST MEMBER
         XC    DIRUSER,DIRUSER         CLEAR THE USER FIELDS
         MVC   DIRNAME,MEMBER2         SECOND MEMBER NAME
         SPACE 2
         BLDL  INDCB,BLDLLIST          LOCATE DIRECTORY ENTRY
         B     *+4(R15)                PROCESS RETURN CODE
         B     RENAME80                  00 - SUCCESSFUL
         B     NOMEMBER                  04 - MEMBER NOT FOUND
         B     IOERROR                   08 - I/O ERROR
         SPACE 3
RENAME80 MVC   DIRFLAG(DIREND-DIRFLAG),DIRFLAG+2 BLDL ADDS THINGS
         SPACE 1
         MVC   DIRNAME,MEMBER2         SECOND MEMBER NAME
         STOW  STOWDCB,DIRNAME,D       ISSUE STOW WITH DELETE OPTION
         LTR   R15,R15                 SUCCESSFUL?
         BNZ   IOERROR                 NO, I/O ERROR
         MVC   DIRNAME,MEMBER1         FIRST MEMBER NAME
         STOW  STOWDCB,DIRNAME,D       ISSUE STOW WITH DELETE OPTION
         LTR   R15,R15                 SUCCESSFUL?
         BNZ   IOERROR                 NO, I/O ERROR
         SPACE 1
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR  TTR
         STOW  STOWDCB,DIRNAME,A       ADD THIS MEMBER ENTRY
         SPACE 1
         LTR   R15,R15                 SUCCESSFUL?
         BNZ   IOERROR                 NO, I/O ERROR
         MVC   MSGTEXT2(DIREND-DIRNAME),DIRNAME  SAVE THE SECOND MEMBER
         SPACE 2
         MVC   DIRNAME(DIREND-DIRNAME),MSGTEXT1  FIRST MEMBER
         MVC   DIRNAME,MEMBER2         SECOND MEMBER NAME
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR  TTR
         STOW  STOWDCB,DIRNAME,A       ADD THIS MEMBER ENTRY
         SPACE 1
         LTR   R15,R15                 SUCCESSFUL?
         BNZ   IOERROR                 NO, I/O ERROR
         BAS   R6,RENAME90             UPDATE ANY ALIAS MEMBERS
         MVC   DIRNAME(DIREND-DIRNAME),MSGTEXT2  SECOND MEMBER
         MVC   MEMBER1(8),MEMBER2      SWAP THE
         MVC   MEMBER2(8),DIRNAME              MEMBER NAMES
         BAS   R6,RENAME90             UPDATE ANY ALIAS MEMBERS
         B     RENAME60                PROCESS OTHER MEMBERS IN GROUP
         SPACE 2
** UPDATE ANY ALIAS ENTRIES FOR LOAD MODULES
RENAME90 MVC   INSERT#1(8),MEMBER1
         MVC   INSERT#2(8),MEMBER2        NEW MEMBER NAME
         TR    INSERT#2+8+3(8),TRLINE     MAKE PRINTABLE
         M$MSG L090$2
         SPACE 1
         TM    FLAGSCC,RECFMU          LOAD MODULE?
         BNOR  R6                      NO, BRANCH
         TM    DIRFLAG,DIRALIAS        AN ALIAS?
         BOR   R6                      YES, BRANCH
         SPACE 1
         MVI   STARTTR+2,X'01'         TTR=000001 (START OF DIRECTORY)
         SPACE 1
RENAME95 BAS   R14,READDIR             GET NEXT DIRECTORY MEMBER
         B     0(R6)                   LAST MEMBER PROCESSED
         SPACE 1
         CLC   DIRTTR,MEMTTR           TTR'S MATCH?
         BNE   RENAME95                NO, BRANCH
         TM    MEMFLAG,X'80'           ALIAS ENTRY?
         BNO   RENAME95                NO, CONTINUE SEARCHING
         SPACE 1
         L     R14,DIRPTRS             CURRENT DIRECTORY ENTRY
         MVC   DIRNAME(DIREND-DIRNAME),0(R14)
         SPACE 1
         LA    R14,DIRREAL             CORRESPONDING MAIN ENTRY NAME
         TM    DIRATTR,ATTRSCTR        SCATTER LOADED?
         BNO   *+8                     NO, BRANCH
         LA    R14,DIRREALS            YES, USE THIS NAME
         CLC   0(8,R14),MEMBER2        ALIAS NAME POINTER=MEMBER NAME?
         BE    RENAME95                YES, BRANCH
         SPACE 1
         MVC   0(8,R14),MEMBER2        CHANGE THE MEMBER POINTER
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR  TTR
         STOW  STOWDCB,DIRNAME,R       REPLACE THIS MEMBER ENTRY
         SPACE 1
         LR    R5,R15                  SAVE RETURN CODE
         MVC   INSERT#1(8),DIRNAME     MEMBER NAME
         M$MSG L065$1
         LTR   R15,R5                  SUCCESSFUL?
         BNZ   IOERROR                 NO, I/O ERROR
         B     RENAME95                CONTINUE FOR ALL ALIASES
MSGRENAM DC    C'MEMBERS TO BE RENAMED ARE:'
