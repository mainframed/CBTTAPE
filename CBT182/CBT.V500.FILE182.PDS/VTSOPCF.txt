//ONE   EXEC  PGM=ASMA90,REGION=2048K,
//             PARM=(NORLD,RENT,TERM,'XREF(SHORT)')
//SYSLIB   DD  DSN=SYS1.MODGEN,DISP=SHR
//         DD  DSN=SYS1.MACLIB,DISP=SHR
//SYSUT1   DD  UNIT=SYSDA,SPACE=(CYL,(10,5))
//SYSUT2   DD  UNIT=SYSDA,SPACE=(CYL,(10,5))
//SYSUT3   DD  UNIT=SYSDA,SPACE=(CYL,(10,5))
//SYSLIN   DD  UNIT=SYSDA,SPACE=(TRK,(5,5)),DISP=(,PASS),DSN=&&X,
//         DCB=BLKSIZE=3120
//SYSPRINT DD  SYSOUT=*
//SYSTERM  DD  SYSOUT=*
//SYSIN    DD  *
VTSOCMD  TITLE 'PDS TSO COMMAND VERIFICATION -- PCF VERSION'
VTSOCMD  CSECT
*
* SAMPLE TSO COMMAND AUTHORIZATION EXIT FOR PCF SYSTEMS WRITTEN BY:
*   J MULTACH    AMERITECH SERVICES, INC.
*
*
*        TSO COMMAND VALIDATION EXIT
*
*        THIS ROUTINE IS CALLED BY THE TSO PROCESSOR TO VALIDATE THE
*        TSO COMMAND USAGE.  CONTROL WILL BE RECEIVED IN THIS EXIT
*        BEFORE EACH TSO COMMAND IS ATTACHED BY THE TSO SUBCOMMAND.
*
*        FUNCTIONS -
*            1) INVOKE IKJSCAN TO VALIDATE USERS ACCESS TO THE COMMAND.
*               IKJSCAN HAS PREVIOUSLY BEEN CALLED BY PDS TO VALIDATE
*               THE COMMAND.  THIS ONLY TRAPS COMMANDS WHICH PCF
*               INDICATES ARE UNAUTHORIZED.  IT DOES NOT INDICATE THAT
*               THE COMMAND SHOULD BE TRIED AS AN IMPLIED CLIST.
*
*               IKJSCAN IS CALLED WITH 'SPF' AS THE PRIMARY COMMAND
*               NAME IN THE ECT.  THIS CAUSES PCF FRONT END TO IKJSCAN
*               TO SET THE IMPLIED CLIST INDICATOR IF A COMMAND IS NOT
*               FOUND OR IS BLOCKED.  PDS HAS BEEN MODIFIED TO ACCEPT
*               A NEW RETURN CODE FROM THIS MODULE AND PDS WILL THEN
*               SET THE IMPLIED EXEC INDICATOR TO BLOCK COMMANDS AS
*               REQUIRED.
*
*            2) FOR UNAUTHORIZED ATTEMPTS, IKJSCAN (PCF) WILL ISSUE
*               APPROPRIATE ERROR MESSAGES.
***
***  THE ARGUMENT LIST FOR VTSOCMD HAS THE FOLLOWING ENTRIES:
***
***
***  DISPLACEMENT    NAME         DESCRIPTION
***  ------------    ----         -----------
***             0    FULLTEXT     POINTS TO AL2(LENGTH),AL2(OFFSET),
***                                         C'ORIGINAL SUBCOMMAND'
***             4    UPT          UPT DSECT
***             8    PSCB         PSCB DSECT
***            12    ECT          ECT DSECT
***            16    COMMAND      THE 8 CHARACTER COMMAND NAME
***
***
***
*
*        INPUT -
*            R1 POINTS TO A STANDARD TSO CPPL FOLLOWED BY A POINTER
*            TO AN EIGHT BYTE AREA CONTAINING THE COMMAND NAME.
*
*        OUTPUT -
*            R15 CONTAINS A RETURN CODE WITH THE FOLLOWING MEANING:
*               0 - COMMAND IS AUTHORIZED
*               4 - COMMAND IS NOT AUTHORIZED
*               8 - COMMAND NOT FOUND, TRY AS AN IMPLIED CLIST
*
***
VTSOCMD  CSECT
         SAVE  (14,12),,*
         LR    R6,R1
         LR    R12,R15
         USING VTSOCMD,R12
         LA    R0,VTSOCMDL                  LENGTH OF SAVE AREA
         GETMAIN RU,LV=(0),SP=0
         ST    R1,8(,R13)
         ST    R13,4(,R1)
         LR    R13,R1
         USING VTSOCMDD,R13
         SPACE 1
         LA    R14,REGEND                     START OF AREA TO CLEAR
         LA    R15,VTSOCMDL-(REGEND-VTSOCMDD) LENGTH OF AREA TO CLEAR
         SR    R4,R4                          SOURCE ADDRESS
         SR    R5,R5                          PAD, LENGTH OF SOURCE
         MVCL  R14,R4                         CLEAR THE DYNAMIC AREA
         EJECT
***
***  BUILD A DUMMY COMMAND BUFFER USING ONLY THE COMMAND AND NOT
***  THE FULL COMMAND LINE
***
         LA    R4,13
         STH   R4,CMDBUFL
         LA    R4,0
         STH   R4,CMDBUFO
         L     R4,16(R6)
         MVC   CMDBUF,0(R4)
         MVI   CMDBUF+8,C' '
         SPACE 3
***
***  SAVE THE CURRENT SECONDARY COMMAND (TSO XXXX) AND REPLACE
***  WITH THE TRUE SECONDARY COMMAND.
***
         L     R4,12(,R6)
         USING ECT,R4
         MVC   SAVEPCMD,ECTPCMD
         MVC   SAVESCMD,ECTSCMD
         MVC   ECTPCMD,=CL8'SPF'
         L     R3,16(R6)
         MVC   ECTSCMD,0(R3)
         SPACE 1
         LA    R3,BUFFER
         ST    R3,CSPLCBUF           BUFFER ADDRESS IN CSPL
         SPACE 1
         MVC   CSPLUPT,4(R6)         UPT ADDRESS
         MVC   CSPLECT,12(R6)        ECT ADDRESS
         SPACE 1
         LA    R3,CSOACNM            OUTPUT AREA ADDRESS
         ST    R3,CSPLOA
         SPACE 1
         LA    R3,TSOECB             ECB ADDRESS
         ST    R3,CSPLECB
         SPACE 1
         LA    R3,FLAGAD             FLAG ADDRESS
         ST    R3,CSPLFLG
         SPACE 1
         LA    R1,PARMLIST           PARAMETER LIST
         LINK  EP=IKJSCAN
         SPACE 1
         MVC   ECTPCMD,SAVEPCMD
         MVC   ECTSCMD,SAVESCMD
         SPACE 1
         LTR   R15,R15
         BZ    CKEXEC
NOTAUTH  LA    R15,4           ***** BAD RETURN CODE OR NOT AUTHORIZED
         B     DONE
         SPACE 1
CKEXEC   TM    CSOAFLG,CSOAEXEC      VALID COMMAND?
         BZ    DONE                  YES, BRANCH
ACLIST   LA    R15,8           ***** BLOCKED COMMAND
         SPACE 2
DONE     LR    R2,R13                SAVE AREA
         L     R13,4(R13)            PREVIOUS SAVE AREA
         STM   R15,R1,16(R13)        RESET REGISTERS R15, R0, R1
         LA    R0,VTSOCMDL           LENGTH OF AREA
         FREEMAIN R,LV=(0),A=(2)     FREE THIS AREA
         LM    R14,R12,12(R13)
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*   DSECTS AND EQUATES                                                *
*                                                                     *
***********************************************************************
VTSOCMDD DSECT
         DS    18F       SAVE OF SAVE AREA
         SPACE 1
REGEND   DS    0F        END OF REGISTER SAVE AREA -- DO NOT MOVE
         SPACE 3
TSOECB   DS    A
FLAGAD   DS    A
SAVEPCMD DS    CL8
SAVESCMD DS    CL8
BUFFER   DS    0F
CMDBUFL  DS    H
CMDBUFO  DS    H
CMDBUF   DS    CL8
         DS    CL1
PARMLIST DS    A
CSPLUPT  DS    A
CSPLECT  DS    A
CSPLECB  DS    A
CSPLFLG  DS    A
CSPLOA   DS    A
CSPLCBUF DS    A
CSOACNM  DS    A
CSOALNM  DS    H
CSOAFLG  DS    X
CSOAVWP  EQU   X'80'         VALID WITH PARAMETERS
CSOAVNP  EQU   X'40'         VALID NO PARAMETERS
CSOAQM   EQU   X'20'         QUESTION MARK
CSOANOC  EQU   X'10'         NO COMMAND
CSOABAD  EQU   X'08'         BAD COMMAND NAME
CSOAEXEC EQU   X'04'         IMPLICIT EXEC COMMAND NAME
         DS    CL1           RESERVED
VTSOCMDL EQU   *-VTSOCMDD
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
         IKJUPT
         EJECT
         IKJPSCB
         EJECT
         IKJECT
         END   VTSOCMD
//LK1    EXEC  PGM=IEWL,PARM='MAP,RENT,REUS,REFR,LET,NCAL',COND=(5,LT)
//SYSPRINT DD  SYSOUT=*
//SYSLMOD  DD  DISP=SHR,DSN=LINK.LIBRARY    <=== MODIFY
//IN       DD  DSN=&&X,DISP=(OLD,DELETE)
//SYSUT1   DD  UNIT=SYSDA,SPACE=(TRK,(5,5))
//SYSLIN   DD  *
  INCLUDE  IN
  ENTRY    PDSMAIN
  INCLUDE  SYSLMOD(PDS45678)                <=== MODIFY
  NAME     PDS45678(R)                      <=== MODIFY
