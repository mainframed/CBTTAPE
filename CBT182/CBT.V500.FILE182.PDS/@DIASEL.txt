         TITLE 'ISPF - ISPEXEC SELECT           '              SS AUG86
* SELECT CMD OR PGM SUPPORT                                    SS AUG86
ISPSLCT  DS    0H                                              SS AUG86
         USING ISPSLCT,R4                                      SS AUG86
         STAX
         ESTAE 0                        CANCEL THE ESTAE ADDR  SS AUG86
ESTAE11  DS    0H                       Z/XDC PROBE           DRK DEC08
         BAS   R2,ISPCERT               CNTL ERROR RETURN      SS AUG86
         LA    R15,ISPSLCTC             SELECT CHAR            SS AUG86
         LA    R0,ISPL250               STANDARD LENGTH        SS AUG86
         LA    R1,SPFZSEL               POINT TO ZSEL          SS AUG86
         STM   R15,R1,PARMLIST          SET PARMLIST           SS AUG86
         OI    PARMLIST+8,X'80'         END FLAG               SS AUG86
         LA    R1,PARMLIST              *                      SS AUG86
         L     R15,ISPLINK              *                      SS AUG86
         BASR  R14,R15                  *                      SS AUG86
         ST    R15,SPFTSV2              SAVE                   SS AUG86
         MVC   STAEPARM(4),STAELIST     RESET THE ESTAE ADDRES SS AUG86
         L     R2,ISPESTAE              ESTAE EXIT ADDRESS     SS AUG86
         ESTAE (R2),CT,PARAM=(R7),MF=(E,STAEPARM)              SS AUG86
ESTAE12  DS    0H                       Z/XDC PROBE           DRK DEC08
         L     R2,SPFSTAXX              GET ORIGINAL EX ADDR   SS AUG86
         STAX  (2),USADDR=(R7),REPLACE=YES,IBUF=0,OBUF=0,              X
               MF=(E,STAXPARM)          *                      SS AUG86
         BAS   R2,ISPCERC               CNTL ERROR CANCEL      SS AUG86
*    SPFZERR ... CHECK FOR ATTN OR ABEND                       SS AUG86
*                 ISPD210=ABEND         *                      SS AUG86
*                 ISPD211=ATTN          *                      SS AUG86
*                 ISPD2..=OTHER ERROR (INVALID)                SS AUG86
         CLC   SPFZERR,ISPSLABD         ISPD210                SS AUG86
         BE    ISPSLCTB                 ABEND                  SS AUG86
         CLC   SPFZERR,ISPSLATT         ISPD211                SS AUG86
         BE    ISPSLCTT                 ATTN                   SS AUG86
         CLC   SPFTSV2,ISPSLR20         IS IT RC 20            SS NOV88
         BNE   ISPSLCT5                 NO, USER RC            SS NOV88
         CLC   SPFZERR(5),ISPSLERR      ISPD2..                SS AUG86
         BE    ISPSLCTE                 ERROR                  SS AUG86
ISPSLCT5 DS    0H                       *                      SS NOV88
         L     R1,SPFTSV2               GET RC                 SS AUG86
         BAS   R2,ISPDECCV              MAKE PRINTABLE         SS AUG86
         MVC   SPFMLIN1(4),ISPSLMGR     RC=                    SS AUG86
         L     R1,SPFTSV2               GET RC                 SS AUG86
         CH    R1,ISPH9999              4 DIGIT NUMBER         SS AUG86
         BH    ISPSLCT7                 NO MAKE IT 7           SS AUG86
         MVC   SPFMLIN1+4(4),SPFDCNV4   RC VALUE               SS AUG86
         B     ISPSLCTR                 *                      SS AUG86
ISPSLCT7 DS    0H                       *                      SS AUG86
         MVC   SPFMLIN1+1(7),SPFDCONV+5 7 DIGIT NUMBER         SS AUG86
         B     ISPSLCTR                 *                      SS AUG86
ISPSLCTB DS    0H                       *                      SS AUG86
         MVC   SPFMSGID,SPFZERR         COPY ID                SS AUG86
         MVC   SPFMLIN1,ISPSLMGA                               SS AUG86
         B     ISPSLCTR                 *                      SS AUG86
ISPSLCTT DS    0H                       *                      SS AUG86
         MVC   SPFMSGID,SPFZERR         *                      SS AUG86
         MVC   SPFMLIN1,ISPSLMGT        *                      SS AUG86
         B     ISPSLCTR                 *                      SS AUG86
ISPSLCTE DS    0H                       *                      SS AUG86
         MVC   SPFMSGID,SPFZERR         *                      SS AUG86
         MVC   SPFMLIN1,ISPSLMGE        *                      SS AUG86
         B     ISPSLCTR                 *                      SS AUG86
ISPSLCTR DS    0H                       *                      SS AUG86
         L     R15,SPFTSV2              GET RC                 SS AUG86
         BR    R3                       *                      SS AUG86
* LITERALS                              *                      SS AUG86
ISPSLR20 DC    F'20'             RETURN CODE 20                SS NOV88
ISPSLCTC DC    CL8'SELECT '      SELECT VERB                   SS AUG86
ISPSLABD DC    CL8'ISPD210'      ABEND MSG ID                  SS AUG86
ISPSLATT DC    CL8'ISPD211'      ATTN  MSG ID                  SS AUG86
ISPSLERR DC    CL5'ISPD2'        ERR   MSG ID RANGE            SS AUG86
ISPSLMGA DC    CL8'*ABEND'       ABEND MSG                     SS AUG86
ISPSLMGT DC    CL8'*ATTN'        ATTN  MSG                     SS AUG86
ISPSLMGE DC    CL8'*INVALID'     ERR   MSG                     SS AUG86
ISPSLMGR DC    CL4'*RC='         RC    MSG                     SS AUG86
         DROP  R4                                              SS AUG86
         TITLE 'ISPF - TRAP SUBROUTINE          '              SS NOV88
* TRAP SUBROUTINE                                              SS NOV88
ISPTRAPX DS    0H                                              SS NOV88
         USING ISPTRAPX,R4                                     SS NOV88
         LA    R1,ISPTRAPV        DEF VAR TABLE                SS NOV88
         BAS   R2,ISPVDEFX                                     SS NOV88
         LTR   R15,R15            FIRST TIME                   SS NOV88
         BNZ   ISPTRAP2           NOT FIRST TIME               SS NOV88
         MVC   SPFTVARN(8),SPFCSUF      NAME                   SS NOV88
         MVI   SPFTVARN+7,C'V'            WITH A V ON END      SS NOV88
         LA    R14,SPFTVARN             *                      SS NOV88
         LA    R1,245                   *                      SS NOV88
         ST    R1,SPFFIXED              SET LENGTH             SS NOV88
         LA    R1,SPFFIXED              *                      SS NOV88
         LA    R15,SPFZSEL+4            USE FOR VAR            SS NOV88
         BAS   R2,ISPVDEF               DEFINE                 SS NOV88
ISPTRAP2 DS    0H                       *                      SS NOV88
         LA    R1,SPFZSEL+249           LAST CHAR              SS NOV88
         LA    R14,249                  COUNT                  SS NOV88
ISPTRAPB DS    0H                       *                      SS NOV88
         CLI   0(R1),C' '               BLANK                  SS NOV88
         BNE   ISPTRAPG                 GOT SOMETHING          SS NOV88
         BCTR  R1,0                     BACK UP ONE            SS NOV88
         BCT   R14,ISPTRAPB               UNTIL START          SS NOV88
         LA    R15,20                   NO GOOD                SS NOV88
         BR    R3                       EXIT                   SS NOV88
ISPTRAPG DS    0H                       *                      SS NOV88
         CLI   0(R1),C')'               SHOULD BE              SS NOV88
         BNE   *+8                      MISSING, OK            SS NOV88
         MVI   0(R1),C' '               BLANK OUT              SS NOV88
         LA    R15,SPFTVARN             GET NAME               SS NOV88
         BAS   R2,ISPVPUTS              COPY TO SHR POOL       SS NOV88
ISPTRAP3 DS    0H                       *                      SS NOV88
         CLI   SPFTLIM,C'0'             IS IT BLANK OR ZEROS   SS NOV88
         BH    *+10                     *                      SS NOV88
         MVC   SPFTLIM,ISPTRAPD         DEFAULT LIMIT          SS NOV88
         CLC   SPFTLIM,ISPBLK8          IS IT BLANK            SS NOV88
         BNE   *+10                     *                      SS NOV88
         MVC   SPFTLIM,ISPTRAPD         DEFAULT LIMIT          SS NOV88
         CLI   SPFTPLOG,C'*'            USE CURRENT LOG        SS NOV88
         BE    ISPTRAPK                 YES, OK                SS NOV88
         MVC   SPFTSVB,SPFTPLOG         LOG NUMBER             SS NOV88
         NI    SPFTSVB,X'0F'            MAKE NUMERIC           SS DEC89
         SR    R0,R0                    *                      SS NOV88
         IC    R0,SPFTSVB               *                      SS NOV88
         LTR   R0,R0                    ANY VALUE              SS NOV88
         BNZ   ISPTRAPL                 YES, SET LOG           SS NOV88
         LA    R0,1                     USE LOG 1              SS NOV88
ISPTRAPL DS    0H                       *                      SS NOV88
         BAS   R2,ISPVIEWI              SET LOG                SS NOV88
ISPTRAPK DS    0H                       *                      SS NOV88
         MVI   SPFTCNT,C'0'             NO OUTPUT YET          SS NOV88
         MVC   SPFMLIN1,ISPBLK8         NO MESSAGE YET         SS NOV88
         MVC   SPFSTART,SPFLAST         DISPLAY FROM HERE      SS NOV88
         BAS   R2,ISPLOGBT              GOT BOTTOM OF LOG      SS NOV88
         CLI   SPFTPCPY,C'N'            COPY CMD TO LOG        SS NOV88
         BE    ISPTRAPJ                 NO, CONTINUE           SS NOV88
         MVC   SPFINPUT(249),SPFZSEL    COPY COMMAND           SS NOV88
         MVI   SPFINPUT+249,C' '                               SS NOV88
         MVC   PUTGETBF(4),ISPLEN2      SET LENGTH             SS NOV88
         LA    R1,PUTGETBF              PARM FOR LOGCD         SS NOV88
         L     R15,SPFSTART             NEED BY LOGCD          SS NOV88
         BAS   R2,ISPLOGCD              *                      SS NOV88
ISPTRAPJ DS    0H                       *                      SS NOV88
         MVI   SPFZSEL,C' '             BLANK                  SS NOV88
         MVC   SPFZSEL+1(249),SPFZSEL     AND RIPPLE           SS NOV88
         MVC   SPFZSEL(L'ISPTRAPS),ISPTRAPS    PDSTRAP         SS NOV88
         MVC   SPFZSEL+12(8),SPFLOGNM   COPY LOG NAME          SS NOV88
         MVC   SPFZSEL+21(8),SPFTVARN   COPY VER NAME          SS NOV88
         MVC   SPFZSEL+30(6),SPFTLIM    LIMIT                  SS NOV88
         BAS   R2,ISPSLCTI              SELECT PDSTRAP         SS NOV88
         ST    R15,SPFTRAPR             SAVE RETURN CODE       SS NOV88
         LA    R15,VSPFTRC              GET SHARED             SS NOV88
         BAS   R2,ISPVGETS              *         VAR          SS NOV88
         LA    R15,VSPFTCNT             GET SHARED             SS NOV88
         BAS   R2,ISPVGETS              *         VAR          SS NOV88
         L     R15,SPFTRAPR             GET  RETURN CODE       SS NOV88
         LTR   R15,R15                  CHECK RETURN CODE      SS NOV88
         BZR   R3                       ZERO RETURN CODE       SS NOV88
         C     R15,ISPL4                NOZERO RETURN CODE     SS NOV88
         BE    ISPTRAP4                 *                      SS NOV88
         C     R15,ISPL8                ABEND                  SS NOV88
         BNER  R3                       ISPEXEC ERROR          SS NOV88
         MVC   SPFMLIN1(3),ISPTRAPE     ABEND                  SS NOV88
         MVC   SPFMLIN1+3(5),SPFTRC     ABEND                  SS NOV88
         BR    R3                       *                      SS NOV88
ISPTRAP4 DS    0H                       *                      SS NOV88
         MVC   SPFMLIN1+4(4),SPFTRC     RETURN CODE            SS NOV88
         CLI   SPFTRC+4,C' '            LESS THAN 5 LONG       SS NOV88
         BE    *+10                     YES                    SS NOV88
         MVC   SPFMLIN1+1(7),SPFTRC     PASS BACK 7 DIGITS     SS NOV88
         BR    R3                       *                      SS NOV88
         DS    0F                       *                      SS NOV88
ISPTRAPV DC    H'1',X'10',X'00'                                SS NOV88
         DC    A(VSPFTRC),AL2(SPFTRC-SPWRKC),H'12'             SS NOV88
         DC    A(VSPFTCNT),AL2(SPFTCNT-SPWRKC),H'06'           SS NOV88
         DC    X'FF'                                           SS NOV88
VSPFTRC  DC    C'PDSTRC  '                                     SS NOV88
VSPFTCNT DC    C'PDSTCNT  '                                    SS NOV88
ISPTRAPD DC    CL6'3000'                DEFAULT LIMIT          SS NOV88
ISPTRAPE DC    C'*A='                   ABEND= MESSAGE         SS NOV88
ISPTRAPS DC    C'CMD(PDSTRAP T2345678 V2345678 L23456) MODE(FSCR)'
         DROP  R4                                              SS NOV88
