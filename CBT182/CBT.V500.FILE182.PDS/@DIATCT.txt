         TITLE 'TSO COMMAND TABLE FUNCTION'                    SS NOV88
*************************************************************  SS NOV88
*                                                              SS NOV88
*      TCT FUNCTION                                            SS NOV88
*                                                              SS NOV88
*************************************************************  SS NOV88
         SPACE 2                                               SS NOV88
ISPTCT   DS    0H                                              SS NOV88
         USING *,R4                                            SS NOV88
         BAS   R2,ISPSETST              SET STATS PREV TABLE   SS NOV88
         CLI   SPFFUNST+6,C'P'          TCT     PENDING PROC   SS NOV88
         BNE   ISPTCT5                  NO,                    SS NOV88
         LH    R1,SPFFUNLP+12     GET LAST PANEL               SS NOV88
         LNR   R1,R1                    *                      SS NOV88
         BAS   R2,ISPSTKI         SWAP PANEL                   SS NOV88
         LA    R15,36             GO TO PENDING ACT PROC       SS NOV88
         BR    R3                                              SS NOV88
ISPTCT5  DS    0H                       *                      SS NOV88
         CLI   SPFFUNST+6,C'I'    INACTIVE                     SS NOV88
         BE    ISPTCT20           YES   OPEN THE TABLE         SS NOV88
         CLI   SPFTBPFI+1,6       ALREADY AT TCT               SS NOV88
         BE    ISPTCT40           THEN DISPLAY                 SS NOV88
ISPTCT1  DS    0H                                              SS NOV88
         LH    R1,SPFFUNLP+12     GET LAST PANEL               SS NOV88
         LNR   R1,R1                    *                      SS NOV88
         BAS   R2,ISPSTKI         SWAP PANEL                   SS NOV88
ISPTCT40 DS    0H                       *                      SS NOV88
         LA    R15,40             DISPLAY IF NO MORE CMDS      SS NOV88
         BR    R3                 TO ROUTER                    SS NOV88
ISPTCT20 DS    0H                       *                      SS NOV88
* OPEN TB                                                      SS NOV88
* DO VDEF FIRST                                                SS NOV88
         LA    R1,ISPTCTAV        DEF VAR TABLE                SS NOV88
         BAS   R2,ISPVDEFX                                     SS NOV88
         LA    R1,23              TCT PANEL                    SS NOV88
         LNR   R1,R1                    *                      SS NOV88
         BAS   R2,ISPSTKI         SWAP PANEL                   SS NOV88
         MVC   SPFTBL,SPFCSUF    GET BASE NAME                 SS NOV88
         MVI   SPFTBL+2,C'T'     SET TO PDT                    SS NOV88
         XC    SPFTFLG1(4),SPFTFLG1                            SS NOV88
         XC    SPFTFF(2),SPFTFF                                SS NOV88
         LA    R15,ISPBLK8       NO KEYS                       SS NOV88
         LA    R0,ISPTCTVN       VER NAMES                     SS NOV88
         BAS   R2,ISPTBOPN                                     SS NOV88
         BAS   R2,ISPSETST         ALSO INIT STATUS FLAG       SS NOV88
* INSERT BLANK LINES                    *                      SS NOV88
         MVC   SPFTOPT,ISPBLK8          *                      SS NOV88
         MVC   SPFTDATA,ISPBLK8         *                      SS NOV88
         MVI   SPFTTYPE,C'-'            *                      SS NOV88
         LA    R1,SPFTCMDD-SPFSEC4K     *                      SS NOV88
         AL    R1,SPFSEC4A              *                      SS NOV88
         MVI   0(R1),C' '               BLANK                  SS NOV88
         MVC   1(144,R1),0(R1)           RIPPLE                SS NOV88
         LA    R1,20                    *                      SS NOV88
ISPTCT30 DS    0H                       *                      SS NOV88
         STH   R1,SPFTSVH               SAVE LOOP COUNT        SS NOV88
         BAS   R2,ISPTBADD              *                      SS NOV88
         LH    R1,SPFTSVH               GET LOOP COUNT         SS NOV88
         BCT   R1,ISPTCT30              *                      SS NOV88
         B     ISPTCT40            DISPLAY TABLE               SS NOV88
         DS    0F                       *                      SS NOV88
ISPTCTAV DC    H'0',X'01',X'00'                                SS NOV88
         DC    A(VSPFTOPT),AL2(SPFTOPT-SPWRKC),H'04'           SS NOV88
         DC    A(VSPFTDAT),AL2(SPFTDATA-SPWRKC),H'08'          SS NOV88
         DC    A(VSPFTTYP),AL2(SPFTTYPE-SPWRKC),H'01'          SS NOV88
         DC    A(VSPFTCMD),AL2(SPFTCMDD-SPWRKC),H'245'         SS NOV88
         DC    X'FF'                                           SS NOV88
VSPFTOPT DC    C'PDSTOPT '                                     SS NOV88
VSPFTDAT DC    C'PDSTDATA '                                    SS NOV88
VSPFTTYP DC    C'PDSTTYPE '                                    SS NOV88
VSPFTCMD DC    C'PDSTCMDD '                                    SS NOV88
ISPTCTVN DC    C'(PDSTOPT PDSTDATA PDSTTYPE PDSTCMDD'          SS NOV88
         DC    C')'  ABOVE TABLE LAST EDIT                     SS NOV88
         DROP  R4                       *                      SS NOV88
         TITLE 'TCT ADD LINES'                                 SS NOV88
*************************************************************  SS NOV88
*                                                              SS NOV88
*      TCT FUNCTION                                            SS NOV88
*                                                              SS NOV88
*************************************************************  SS NOV88
         SPACE 2                                               SS NOV88
ISPTCTAD DS    0H                                              SS NOV88
         USING *,R4                                            SS NOV88
         BAS   R2,ISPSETST              SET STATS PREV TABLE   SS NOV88
*        B     ISPTCTAB                 *TEST* ADD TO BOTTOM   SS NOV88
         L     R14,SPFVTPT              RESTORE POINTER        SS NOV88
         SR    R15,R15                  *                      SS NOV88
         IC    R15,$TLEX(R14)           SUB FUNCTION IX        SS NOV88
         L     R15,ISPTCTAT(R15)        SUB FUNC ADDRESS      DRK AUG06
         BR    R15                      *                      SS NOV88
ISPTCTAT DS    0F                       *                      SS NOV88
         DC    A(ISPTCTAB)      0       ADD TO BOTTOM          SS NOV88
         DC    A(ISPTCTA1)      4       INSERT                 SS NOV88
ISPTCTA1 DS    0F                       *                      SS NOV88
         BAS   R2,ISPQUERY              *                      SS NOV88
         SR    R14,R14                  *                      SS NOV88
         ST    R14,SPFFIXED        ZERO DELETE AMOUNT          SS NOV88
         S     R15,SPFTCRP         DELTA                       SS NOV88
         ST    R15,SPFFIXE2        SET POSITION                SS NOV88
         BAS   R2,ISPDEL           DO IT                       SS NOV88
         BAS   R2,ISPQUERY         RESET STATS                 SS NOV88
         B     ISPTCTA2                 *                      SS NOV88
ISPTCTAB DS    0F                       *                      SS NOV88
         BAS   R2,ISPBOTM               ADD TO BOTTOM          SS NOV88
         B     ISPTCTA2                 *                      SS NOV88
ISPTCTA2 DS    0F                       *                      SS NOV88
* INSERT BLANK LINES                    *                      SS NOV88
         MVC   SPFTOPT,ISPBLK8          *                      SS NOV88
         MVC   SPFTDATA,ISPBLK8         *                      SS NOV88
         MVI   SPFTTYPE,C'-'            *                      SS MAR89
         LA    R1,SPFTCMDD-SPFSEC4K     *                      SS NOV88
         AL    R1,SPFSEC4A              *                      SS NOV88
         MVI   0(R1),C' '               BLANK                  SS NOV88
         MVC   1(144,R1),0(R1)           RIPPLE                SS NOV88
         LA    R1,5                     ADD FIVE               SS NOV88
ISPTCTA5 DS    0H                       *                      SS NOV88
         STH   R1,SPFTSVH               SAVE LOOP COUNT        SS NOV88
         BAS   R2,ISPTBADD              *                      SS NOV88
         LH    R1,SPFTSVH               GET LOOP COUNT         SS NOV88
         BCT   R1,ISPTCTA5              *                      SS NOV88
         B     ISPTCTA4            DISPLAY TABLE               SS NOV88
ISPTCTA4 DS    0H                       *                      SS NOV88
         LA    R15,40             DISPLAY IF NO MORE CMDS      SS NOV88
         BR    R3                 TO ROUTER                    SS NOV88
         DROP  R4                       *                      SS NOV88
