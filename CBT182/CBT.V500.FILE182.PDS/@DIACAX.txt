         TITLE 'ACTIVE CATALOG    FUNCTION'                    SS NOV88
*************************************************************  SS NOV88
*                                                              SS NOV88
*      CAX FUNCTION                                            SS NOV88
*                                                              SS NOV88
*  NOTE:  THIS FUNCTION USES THE CATALOG AUXILIARY WORK AREA   SS NOV88
*         (CAXWA) CONTROL BLOCK CHAIN FOR ALL ITS INFORMATION  SS NOV88
*                                                              SS NOV88
*************************************************************  SS NOV88
         SPACE 2                                               SS NOV88
ISPCAX   DS    0H                                              SS NOV88
         USING *,R4                                            SS NOV88
         BAS   R2,ISPSETST              SET STATS PREV TABLE   SS NOV88
         CLI   SPFFUNST+7,C'P'          LISTC   PENDING PROC   SS NOV88
         BNE   ISPCAX5                  NO                     SS NOV88
         LH    R1,SPFFUNLP+14     GET LAST PANEL               SS NOV88
         LNR   R1,R1                    *                      SS NOV88
         BAS   R2,ISPSTKI         SWAP PANEL                   SS NOV88
         LA    R15,36             GO TO PENDING ACT PROC       SS NOV88
         BR    R3                                              SS NOV88
ISPCAX5  DS    0H                       *                      SS NOV88
         CLI   SPFFUNST+7,C'I'    INACTIVE                     SS NOV88
         BE    ISPCAX7            YES   OPEN THE TABLE         SS NOV88
         CLI   SPFTBPFI+1,7       ALREADY AT CAX               SS NOV88
         BE    ISPCAX6            THEN DISPLAY                 SS NOV88
         LH    R1,SPFFUNLP+14     GET LAST PANEL               SS NOV88
         LNR   R1,R1                    *                      SS NOV88
         BAS   R2,ISPSTKI         SWAP PANEL                   SS NOV88
ISPCAX6  DS    0H                       *                      SS NOV88
         BAS   R2,ISPQUERY        GET                          SS NOV88
         L     R1,SPFTSIZE            COUNT                    SS NOV88
         LTR   R1,R1              ZERO  *                      SS NOV88
         BZ    ISPCAX8            RELOAD TABLE                 SS NOV88
ISPCAX40 DS    0H                       *                      SS NOV88
         TM    SPFLAG5,SPFXA            REALLY XA             DRK MAR07
         BZ    *+4+4+2                  NO, SKIP  BSM         DRK MAR07
         LA    R14,*+4+2                POINT AFTER BSM       DRK MAR07
         BSM   0,R14                    SET MODE 24           DRK MAR07
         SPACE 1                                              DRK MAR07
         LA    R15,40             DISPLAY IF NO MORE CMDS      SS NOV88
         BR    R3                 TO ROUTER                    SS NOV88
ISPCAX7  DS    0H                       *                      SS NOV88
* OPEN TB                                                      SS NOV88
* DO VDEF FIRST                                                SS NOV88
         LA    R1,ISPCAXAV        DEF VAR TABLE                SS NOV88
         BAS   R2,ISPVDEFX                                     SS NOV88
         LA    R1,24              CAX PANEL                    SS NOV88
         LNR   R1,R1                    *                      SS NOV88
         BAS   R2,ISPSTKI         SWAP PANEL                   SS NOV88
         MVC   SPFTBL,SPFCSUF    GET BASE NAME                 SS NOV88
         MVI   SPFTBL+2,C'X'     SET TO PDX                    SS NOV88
         XC    SPFTFLG1(4),SPFTFLG1                            SS NOV88
         XC    SPFTFF(2),SPFTFF                                SS NOV88
*        LA    R15,ISPBLK8       NO KEYS                       SS NOV88
         LA    R15,CAXTKEY       NUMBER IS KEY                DRK AUG98
         LA    R0,ISPCAXVN       VER NAMES                     SS NOV88
         BAS   R2,ISPTBOPN                                     SS NOV88
         BAS   R2,ISPSETST         ALSO INIT STATUS FLAG       SS NOV88
         LH    R0,SPFTBPSR         SORT INIT INDEX             SS NOV88
         BAS   R2,ISPSORTI         REQUEST SORT INIT           SS NOV88
* INSERT LINES                          *                      SS NOV88
ISPCAX8  DS    0H                       *                      SS NOV88
         TM    SPFLAG5,SPFXA            REALLY XA             DRK MAR07
         BZ    *+4+4+4+2                NO, SKIP  BSM         DRK MAR07
         LA    R14,*+4+4+2              POINT AFTER BSM       DRK MAR07
         O     R14,ISPX80               31 BIT MODE           DRK MAR07
         BSM   0,R14                    SET MODE              DRK MAR07
         SPACE 1                                              DRK MAR07
         L     R1,CVTPTR(,R0)   PT. TO CVT                     SS NOV88
         L     R1,CVTCBSP-CVT(R1)      AMCBS PTER              SS NOV88
         L     R1,20(R1)        PT. TO 1ST CAXWA               SS NOV88
         XC    SPFTSVH,SPFTSVH  SET ZERO COUNT                 SS NOV88
ISPCAX10 DS    0H                                              SS NOV88
*        LA    R1,0(R1)         CLEAR HIGH ORDER BYTE         DRK MAR07
         LTR   R1,R1            IS THERE A CAXWA CHAIN?        SS NOV88
         BZ    ISPCAX40         NO CAX  GO DISPLAY             SS NOV88
*  IS IT THE MASTER CATALOG?                                   SS NOV88
         TM    8(R1),X'04'                                     SS NOV88
         BZ    ISPCAX11         NO                             SS NOV88
         MVC   SPFXTYPE,ISPCAXMS                               SS NOV88
         B     ISPCAX20                 *                      SS NOV88
ISPCAX11 DS    0H                                              SS NOV88
         TM    10(R1),X'20'     ICF                            SS NOV88
         BZ    ISPCAX12         NO                             SS NOV88
         MVC   SPFXTYPE,ISPCAXIC                               SS NOV88
         B     ISPCAX20                 *                      SS NOV88
ISPCAX12 DS    0H                                              SS NOV88
         TM    09(R1),X'02'     RECV VSAM                      SS NOV88
         BZ    ISPCAX13         NO                             SS NOV88
         MVC   SPFXTYPE,ISPCAXRV                               SS NOV88
         B     ISPCAX20                 *                      SS NOV88
ISPCAX13 DS    0H                                              SS NOV88
         MVC   SPFXTYPE,ISPCAXVS  VSAM                         SS NOV88
ISPCAX20 DS    0H                                              SS NOV88
         MVC   SPFXSTAT,ISPCAXAC  ASSUME ACTIVE                SS NOV88
*  HAS THE CATALOG BEEN DELETED?                               SS NOV88
         TM    9(R1),X'80'      DELETED                        SS NOV88
         BZ    ISPCAX21         NO                             SS NOV88
         MVC   SPFXSTAT,ISPCAXDE                               SS NOV88
         B     ISPCAX30                 *                      SS NOV88
ISPCAX21 DS    0H                                              SS NOV88
         TM    1(R1),X'08'      DISCONNECTED                   SS NOV88
         BZ    ISPCAX30         NO                             SS NOV88
         MVC   SPFXSTAT,ISPCAXDS                               SS NOV88
ISPCAX30 DS    0H                                              SS NOV88
*  ABLE TO GET CORE?                                           SS NOV88
         TM    9(R1),X'20'      GETMAIN ERROR                  SS NOV88
         BZ    ISPCAX31         NO                             SS NOV88
         MVC   SPFXSTAT+2(4),ISPCAXNM                          SS NOV88
         B     ISPCAX50                 *                      SS NOV88
ISPCAX31 DS    0H                                              SS NOV88
*  CHECK FOR AN I/O ERROR                                      SS NOV88
         TM    9(R1),X'10'                                    DRK AUG98
         BZ    ISPCAX50         NO                             SS NOV88
         MVC   SPFXSTAT+2(4),ISPCAXIO                          SS NOV88
ISPCAX50 DS    0H                                              SS NOV88
*  PICKUP THE VOL SER                                          SS NOV88
         ICM   R2,B'1111',188(R1)       31-BIT UCB ADDR?      DRK SEP98
         BNZ   *+4+4                    YES                   DRK SEP98
         L     R2,28(,R1)               NO, USE 24-BIT ADDR   DRK SEP98
         ST    R2,UCBADDR               SAVE UCB ADDRESS      DRK MAR07
         MVC   SPFXVOL(6),28(R2)        PICKUP VOL SER         SS NOV88
*  FIND CATALOG NAME                                           SS NOV88
NAMES    MVC   SPFXDSN(44),52(R1)  MOVE IN CATALOG NAME        SS NOV88
         MVC   SPFXOPT,ISPBLK8          *                      SS NOV88
         MVC   SPFXDATA,ISPBLK8         *                      SS NOV88
         ST    R1,SPFTSV1               SAVE ADDRESS           SS NOV88
         BAS   R2,ISPHEXCV              *                      SS NOV88
         MVC   SPFXADDR,SPFHEXCV+2      GET 3 BYTE ADDRESS     SS NOV88
         LH    R1,SPFTSVH               GET COUNT              SS NOV88
         LA    R1,1(,R1)                ADD ONE                SS NOV88
         STH   R1,SPFTSVH               SAVE NEW COUNT         SS NOV88
         BAS   R2,ISPDECCV              CONVERT TO DECIMAL     SS NOV88
         MVC   SPFXNUM,SPFDCNV3         3 DIGITS               SS NOV88
         SPACE 1
*  REPLACEMENT FOR STATUS AND ADDR                            DRK MAR07
*        AGO   .SKIPNEW                 UNCOMMENT FOR OLD STYLE
         L     R1,SPFTSV1               GET CAXWA ADDR        DRK MAR07
         USING IGGCAXWA,R1                                    DRK MAR07
         SLR   R15,R15                  CLEAR FOR DIVIDE      DRK MAR07
         CLC   CAXSEAR,=F'0'            FROM SHOWZOS ------.     RS0701
         BE    ISPCAX60                                    "     RS0701
         L     R15,CAXFOUND             NO OF FOUND        "     RS0701
         M     R14,=F'100'              * 100              "     RS0701
         D     R14,CAXSEAR              NO OF SEARCHES     "     RS0701
ISPCAX60 DS    0H
         LR    R1,R15                   R15=HIT%              DRK MAR07
         BAS   R2,ISPDECCV              CONVERT TO DECIMAL    DRK MAR07
         MVC   SPFXADDR,ISPBLK8                               DRK MAR07
         MVC   SPFXADDR+2(3),SPFDCNV3   3 DIGITS              DRK MAR07
         SPACE 1
         L     R1,SPFTSV1              GET CAXWA ADDR         DRK MAR07
         MVC   SPFXSTAT(6),=C'N-----'  DEFAULT             "     RS0301
         ICM   R2,B'1111',CAXDSABP     ALLOCATE TO CAS?    "     RS1199
         BZ    CAXST21                   NO                "     RS1199
         MVI   SPFXSTAT,C'Y'             YES, SET INDICATOR"     RS1199
CAXST21  DS    0H                                          "     RS1199
         TM    CAXFLG3,CAXF3ICF        CHECK SMS           "     RS1199
         BZ    CAXST22                   NO                "     RS1199
         L     R14,UCBADDR                                    DRK MAR07
         LTR   R14,R14                 UCB                 "     DL0200
         BZ    CAXST22                   NO                "     RS1199
         TM    1(R14),X'20'            DATA MANAGEMENT?    "     DL0200
         BZ    CAXST22                   NO                "     RS1199
         MVI   SPFXSTAT+1,C'S'           YES, SET SMS FLAG "     RS1199
CAXST22  DS    0H                                          "     RS1199
         MVI   SPFXSTAT+2,C'D'         MARK AS DELETED     "     RS1199
         TM    CAXFLG2,CAXF2DT         DELETED?            "     RS1199
         BNZ   CAXST210                  YES, LEAVE THE D-FLAG   RS1199
         MVI   SPFXSTAT+2,C'C'         MARK AS CLOSED      "     RS1199
         ICM   R2,B'1111',CAXACB       ACB PRESENT         "     RS1199
         BZ    CAXST23                   NO, CHECK ISC     "     RS1199
         ICM   R2,B'1111',4(R2)        CHECK ACB           "     RS1199
         BZ    CAXST210                  OK, LEAVE THE C-FLAG    RS1199
CAXST23  DS    0H                                          "     RS1199
         MVI   SPFXSTAT+2,C'V'         MARK AS VLF         "     RS1199
         TM    CAXFLG6,CAXCDSCA        VLF?                "     RS1199
         BNZ   CAXST29                   YES, LEAVE THE V-FLAG   RS0402
         MVI   SPFXSTAT+2,C'I'         MARK AS ISC         "     RS1199
         ICM   R2,B'1111',CAXISC       ISC?                "     RS1199
         BNZ   CAXST29                   YES, LEAVE THE I-FLAG   RS0301
         MVI   SPFXSTAT+2,C'-'         MARK AS NEUTRAL     "     RS1199
CAXST29  DS    0H                                          "     RS0301
         TM    CAXFLG4,CAXLOCK         LOCKED?             "     RS0301
         BZ    CAXST210                  NO                "     RS0301
         MVI   SPFXSTAT+5,C'K'           YES, LOCKED       "     RS0301
CAXST210 DS    0H                                          "     RS0301
         TM    CAXFLG3,CAXF3ICF+CAXF3SO4+CAXF3SHD  CROSS SHARED? RS1199
         BNO   CAXST211                  NO                "     RS1199
         MVI   SPFXSTAT+4,C'R'         SET R-FLAG          "     RS1199
         ICM   R2,B'1111',CAXECS       ECS?                "     RS0301
         BZ    CAXST211                  NO                "     RS0301
         MVI   SPFXSTAT+4,C'E'           YES, SET E-FLAG   "     RS1199
CAXST211 DS    0H                                          "     RS1199
         TM    CAXFLG5,CAXLCDB         ATL?                "     RS1199
         BZ    CAXST212                  NO                "     RS1199
         MVI   SPFXSTAT+3,C'A'           YES, SET A-FLAG   "     RS1199
CAXST212 DS    0H
*                                                           * DRK MAR07
*  STATUS                                                   * DRK MAR07
*  ------                                                   * DRK MAR07
*  YSDARK                                                   * DRK MAR07
*  N C E                                                    * DRK MAR07
*    V                                                      * DRK MAR07
*    I                                                      * DRK MAR07
*                                                           * DRK MAR07
*  Y/N-ALLOC, S-SMS, V-VLF, I-ISC, C-CLOSED,                * DRK MAR07
*  R-SHARED, A-ATL, E-ECS, K=LOCKED, D-DELETED              * DRK MAR07
*                                                           * DRK MAR07
.SKIPNEW ANOP                                                 DRK MAR07
         SPACE 1
         BAS   R2,ISPTBADD              ADD TO TABLE           SS NOV88
*  PT TO NEXT CAXWA                                            SS NOV88
         L     R1,SPFTSV1               GET CAXWA ADDR         SS NOV88
         L     R1,4(R1)                                        SS NOV88
         B     ISPCAX10                                        SS NOV88
ISPCAXMS DC    CL4'MSTR'                *                      SS NOV88
ISPCAXIC DC    CL4'ICF '                *                      SS NOV88
ISPCAXVS DC    CL4'VSAM'                *                      SS NOV88
ISPCAXRV DC    CL4'RECV'                *                      SS NOV88
ISPCAXAC DC    CL6'ACTIVE'              *                      SS NOV88
ISPCAXDS DC    CL6'DISCON'              *                      SS NOV88
ISPCAXDE DC    CL6'DELETE'              *                      SS NOV88
ISPCAXHG DC    CL6'HUNG  '              *                      SS NOV88
ISPCAXIO DC    CL4'-IOE'                *                      SS NOV88
ISPCAXNM DC    CL4'-NOM'                *                      SS NOV88
         DS    0F                       *                      SS NOV88
ISPCAXAV DC    H'1',X'80',X'00'                                SS NOV88
         DC    A(VSPFXOPT),AL2(SPFXOPT-SPWRKC),H'04'           SS NOV88
         DC    A(VSPFXDAT),AL2(SPFXDATA-SPWRKC),H'08'          SS NOV88
         DC    A(VSPFXTYP),AL2(SPFXTYPE-SPWRKC),H'04'          SS NOV88
         DC    A(VSPFXNUM),AL2(SPFXNUM-SPWRKC),H'03'           SS NOV88
         DC    A(VSPFXSTA),AL2(SPFXSTAT-SPWRKC),H'06'          SS NOV88
         DC    A(VSPFXADR),AL2(SPFXADDR-SPWRKC),H'06'          SS NOV88
         DC    A(VSPFXVOL),AL2(SPFXVOL-SPWRKC),H'06'           SS NOV88
         DC    A(VSPFXDSN),AL2(SPFXDSN-SPWRKC),H'44'           SS NOV88
         DC    X'FF'                                           SS NOV88
VSPFXOPT DC    C'PDSXOPT '                                     SS NOV88
VSPFXDAT DC    C'PDSXDATA '                                    SS NOV88
VSPFXTYP DC    C'PDSXTYPE '                                    SS NOV88
VSPFXNUM DC    C'PDSXNUM  '                                    SS NOV88
VSPFXSTA DC    C'PDSXSTAT '                                    SS NOV88
VSPFXADR DC    C'PDSXADDR '                                    SS NOV88
VSPFXVOL DC    C'PDSXVOL  '                                    SS NOV88
VSPFXDSN DC    C'PDSXDSN  '                                    SS NOV88
CAXTKEY  DC    C'(PDSXNUM)'                                   DRK AUG98
ISPCAXVN DC    C'(PDSXOPT PDSXDATA PDSXTYPE '                 DRK AUG98
         DC    C'PDSXSTAT PDSXADDR PDSXVOL  PDSXDSN '          SS NOV88
         DC    C')'  ABOVE TABLE LAST EDIT                     SS NOV88
         LTORG ,                                              DRK MAR07
         DROP  R1,R4                    *                     DRK MAR07
