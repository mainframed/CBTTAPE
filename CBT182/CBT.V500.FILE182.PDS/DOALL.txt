/* ---------------------  rexx procedure  ----------------------
 * Name:      DoAll
 *
 * Function:  This rexx exec will process the specified
 *            ispf edit macro against every member of the
 *            specified partitioned dataset.
 *
 *            Only standard system services are used. The
 *            LISTD TSO command with the MEMBERS keyword
 *            is used to extract the member names.
 *
 * Syntax:    %DoAll dsname edit-macro
 *
 * Sample Edit Macro to change SYS1 to SYS2
 *  * rexx exec chsys1t2 (change sys1 to sys2) *
 *  (the / was removed from the above line to avoid syntax
 *   errors in this exec).
 *  Address ISREDIT
 *  "MACRO"
 *  "CHANGE 'DSN=SYS1.' 'DSN=SYS2.' ALL"
 *  "SAVE"
 *  "END"
 *
 * Sample Execution:  %Doall 'sys2.testjcl' chsys1t2
 *
 * Author:    Lionel B. Dyck
 *            Kaiser Permanente Information Technology
 *            25 N. Via Monte Ave.
 *            Walnut Creek, CA 94598
 *            (925) 926-5332
 *            Internet: lbdyck@gmail.com
 *
 * History:   11/30/90 - created
 *
 * ---------------------------------------------------------- */

arg dsn exec

if left(dsn,1) <> "'" then do
   dsn = sysvar(syspref)"."dsn
   end
   else do
        dsn = substr(dsn,2,length(dsn)-2)
        end

x = outtrap("lm.","*")

"LISTD" "'"dsn"'" "MEMBERS"

x = outtrap("off")

do i = 1 to lm.0
   if lm.i = "--MEMBERS--" then signal domem
   end

domem:  do j = i+1 to lm.0
        parse value lm.j with mem extra
        Address ISPEXEC "EDIT DATASET('"dsn"("mem")')" ,
                        "MACRO("exec")"
        end
