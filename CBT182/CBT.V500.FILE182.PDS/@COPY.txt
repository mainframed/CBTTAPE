         TITLE 'P D S  --  PDS COPY                          09/28/88'
***********************************************************************
***      COPY SUBCOMMAND       ADDED BY BRUCE LELAND -- AUG., 1985  ***
***********************************************************************
*
         SPACE 1
COPY     CSECT
         USING *,R8,R6
         LA    R6,2048
         LA    R6,2048(R6,R8)        SECOND BASE
         SPACE 1
         CLI   #CMXFLAG,0            LIST OPTION SET?
         BNE   *+8                   YES, BRANCH
         MVI   #CMXFLAG,&COPYLST     NO, USE THE DEFAULT
         CLI   #COPEXST,4            EXCLUDE?
         BNE   *+8                   NO, BRANCH
         MVI   #COPTO#,0             YES, CLEAR REPLACE LENGTH
         TM    DSORG,DS1DSGPO        PARTITIONED INPUT?
         BO    COP2100               YES, BRANCH
         OI    #COPALI,#COPSEQ       NO, MUST USE IEBGENER
         B     COP2130               YES, BRANCH              DRK FEB01
         SPACE 2
COP2100  MVC   MEMNAME(12),DIRNAME   ENSURE MEMBER NAME IS PRESENT
         MVC   #DELSAVE(1),FLAGSAA   SAVE FLAGSAA
         TM    FLAGSAA,FMEM#MEM      MEMBER GROUP?
         BO    COP2200               YES, BRANCH
         MVI   #DELSAVE,FF-FMEM#MEM  MARK MEMBER AS ADDED
         TM    SPFLAG2,SPFLNCD       LINE COMMAND?
         BO    COP2110               YES, BRANCH
         TM    PMEMMIN,X'80'         MEMBER LIST?
         BO    COP2200               YES, BRANCH
COP2110  TM    #COPALI,#COPASS2      ASSOCIATES TOO?
         BO    COP2200               YES, BRANCH
         SPACE 1
COP2120  DS    0H                                             DRK FEB01
         TM    #COPALI,#COPASS2+2    ALIAS/NOALIAS SPECIFIED? DRK FEB01
         BM    COP2130               YES                      DRK FEB01
         CLC   #COPTON(8),=XL8'00'   TO(PNAME) SPECIFIED?     DRK FEB01
         BNE   COP2130               YES                      DRK FEB01
         TM    FLAGSCC,RECFMU        LOAD LIBRARY?            DRK FEB01
         BNO   COP2130               NO, BRANCH               DRK FEB01
         OI    #COPALI,&COPYALI      USE THE DEFAULT          DRK FEB01
         TM    #COPALI,#COPASS2      ASSOCIATES TOO?          DRK FEB01
         BO    COP2200               YES                      DRK FEB01
COP2130  DS    0H                                             DRK FEB01
         OI    #COPALI,#COPONE       ONLY A SINGLE MEMBER/DATA SET
         LA    R4,DDNAMEH            HOLD FOR THIS SUBCOMMAND
         MVC   0(8,R4),DIRNAME
         ST    R4,#MEMPTR
         LA    R4,16(,R4)            POSITION FOR ONE ENTRY
         B     COP2900
         SPACE 3
COP2200  OI    FLAGSII,FDIRGRP       SIMULATE MEMBER GROUP INPUT
         TM    #COPALI,#COPASS2+2    ALIAS/NOALIAS SPECIFIED? DRK FEB01
         BM    *+8                   YES                      DRK FEB01
         OI    #COPALI,&COPYALI      NO, USE THE DEFAULT      DRK FEB01
         NI    FLAGSAA,FF-FMEM#MEM   TURN OFF MEMBER GROUPS NOW
         MVI   SUBPOOLT,21           SUBPOOL 21 DATA
         LA    R5,256/2              256 MEMBERS INITIALLY
         SPACE 1
COP2210  SLL   R5,1                  MEMBERS*2 FOR EACH LOOP
         LR    R3,R5
         LR    R0,R5
         SLL   R0,4                  TABLE SIZE=MEMBERS*16
         ICM   R0,B'1000',SUBPOOLT
         GETMAIN R,LV=(0)
         LR    R4,R1                 NEW TABLE ADDRESS
         ICM   R0,B'1111',#MEMPTR    ANY PREVIOUS MEMBERS?
         BNZ   COP2220               YES, BRANCH
         ST    R4,#MEMPTR            INITIALIZE MEMBER BASE
         B     COP2500
         SPACE 1
COP2220  SRL   R3,1                  SECOND HALF OF TABLE (# MEMBERS)
         LR    R14,R4                NEW TABLE START ADDRESS
         LR    R15,R5
         SLL   R15,3                 OLD TABLE SIZE=MEMBERS*8
         LR    R2,R15                START OF NEW PART
         LR    R1,R15
         MVCL  R14,R0                PRESERVE THE OLD TABLE
         L     R1,#MEMPTR
         LR    R0,R2                 LENGTH TO FREE
         ICM   R0,B'1000',SUBPOOLT   SUBPOOL TO FREE
         FREEMAIN R,LV=(0),A=(1)
         ST    R4,#MEMPTR            START OF NEW TABLE
         AR    R4,R2                 WHERE TO ADD MEMBERS
         SPACE 3
COP2300  TM    #COPALI,#COPASSP      ASSOCIATES PHASE?
         BO    COP2320               YES, BRANCH
         TM    #DELSAVE,FMEM#MEM     MEMBER GROUP?
         BO    COP2320               YES, BRANCH
         TM    #DELSAVE,FF-FMEM#MEM  MEMBER ALREADY ADDED?
         MVI   #DELSAVE,FF-FMEM#MEM  MARK MEMBER AS ADDED
         BNO   COP2500               NO, BRANCH
         TM    SPFLAG2,SPFLNCD       LINE COMMAND?
         BO    COP2605               YES, BRANCH
         TM    PMEMMIN,X'80'         MEMBER LIST?
         BNO   COP2605               NO, BRANCH
         L     R1,PMEMCURR           CURRENT LIST ELEMENT
         TM    0(R1),X'80'           ANOTHER ELEMENT?
         BNO   COP2605               NO, END OF LIST
         MVC   FLAGSAA(1),4(R1)      NEXT DESCRIPTOR FLAGS
         MVC   LMEMBER1+1(19),5(R1)  LEN1, MEMBER1, LEN2, MEMBER2
         L     R1,0(,R1)             NEXT ELEMENT POINTER
         ST    R1,PMEMCURR
         MVC   #DELSAVE(1),FLAGSAA   SAVE THE MEMBER DESCRIPTOR
         MVC   MEMNAME(8),MEMBER1    FIRST MEMBER NAME
         NI    FLAGSAA,FF-FMEM#MEM   TURN OFF MEMBER GROUP
         TM    #DELSAVE,FMEM#MEM     MEMBER GROUP?
         BO    COP2319               YES, BRANCH
         MVI   #DELSAVE,FF-FMEM#MEM  MARK MEMBER AS ADDED
         MVC   DIRNAME,MEMBER1         FIRST PART OF DSNAME2
         SPACE 2
         BLDL  INDCB,BLDLLIST          LOCATE DIRECTORY ENTRY
         B     *+4(R15)                PROCESS RETURN CODE
         B     COP2315                   00 - SUCCESSFUL
         B     COP2310                   04 - MEMBER NOT FOUND
         B     IOERROR                   08 - I/O ERROR
         SPACE 1
COP2310  MVC   INSERT#1(8),DIRNAME
         M$MSG L853$1                MSG - (MEMBER) NOT FOUND
         B     COP2300
         SPACE 1
COP2315  MVC   MEMNAME(12),DIRNAME   FIRST MEMBER NAME
         B     COP2500
         SPACE 1
COP2319  MVI   STARTTR+2,X'01'       REREAD THE DIRECTORY
         SPACE 1
COP2320  BAS   R14,READDIR           GET NEXT DIRECTORY BLOCK
         B     COP2600               END OF MEMBERS -- BRANCH
         SPACE 1
         TM    #COPALI,#COPASSP      ASSOCIATES PHASE?
         BO    COP2400               YES, BRANCH
         SPACE 1
         BAS   R2,MFILTER            CHECK MEMBER NAMES
         B     COP2600                 00 - SIMULATE DIRECTORY END
         B     COP2300                 04 - MEMBER FAILED FILTERING
         B     COP2500                 08 - MEMBER PASSED FILTERING
         SPACE 3
COP2400  L     R1,#MEMPTR            START OF MEMBER LIST
         B     COP2410+4             SKIP FIRST ADD
COP2410  LA    R1,16(,R1)            NEXT TABLE ENTRY
         CR    R1,R4                 END OF TABLE?
         BNL   COP2300               YES, IGNORE THIS ENTRY
         CLC   8(3,R1),MEMTTR        MATCHING TTR?
         BNE   COP2410               NO, BRANCH
         SPACE 3
COP2500  L     R1,#MEMPTR            START OF MEMBER LIST
         B     COP2510+4             SKIP FIRST ADD
COP2510  LA    R1,16(,R1)            NEXT TABLE ENTRY
         CR    R1,R4                 END OF TABLE?
         BNL   COP2580               YES, ADD THIS ENTRY
         CLC   0(8,R1),MEMNAME       FIND MYSELF?
         BE    COP2300               YES, IGNORE THIS ENTRY
         B     COP2510               NO, CONTINUE SEARCHING
         SPACE 2
COP2580  MVC   0(16,R4),MEMNAME      ADD TO THE TABLE
         LA    R4,16(,R4)            NEXT MEMBER POSITION
         S     R3,=F'1'              ANY MORE FIT?
         BNP   COP2210               NO, BRANCH
         B     COP2300               YES, GET NEXT MEMBER
         SPACE 3
COP2600  TM    #COPALI,#COPASSP      ASSOCIATED MEMBERS?
         BO    COP2605               YES, BRANCH
         MVI   #DELSAVE,FF-FMEM#MEM  ELEMENT HAS NOW BEEN ADDED
         TM    SPFLAG2,SPFLNCD       LINE COMMAND?
         BO    COP2605               YES, BRANCH
         TM    PMEMMIN,X'80'         MEMBER LIST?
         BO    COP2300               YES, BRANCH
         SPACE 1
COP2605  TM    #COPALI,#COPASSP      ASSOCIATES PHASE STARTED?
         BO    COP2700               YES, BRANCH
         TM    #COPALI,#COPASS2      ASSOCIATE MEMBERS TOO?
         BNO   COP2700               NO, BRANCH
         OI    #COPALI,#COPASSP      YES, ASSOCIATES PHASE STARTED
         NI    FLAGSII,FF-FDIRGRP    NORMAL INPUT NOW
         MVI   STARTTR+2,X'01'       TTR=000001 (START OF DIRECTORY)
         B     COP2300               DO ANY ASSOCIATED MEMBERS
         SPACE 3
COP2700  NI    FLAGSII,FF-FDIRGRP    NORMAL INPUT NOW
         SPACE 3
COP2900  DS    0H
         ST    R4,#MEMLAST           SAVE LAST MEMBER POINTER
         LA    R1,FIRST4K
         CLC   MSGDS2(2),=C'* '               PREVIOUS DSNAME DESIRED?
         BNE   COP3100                        NO, BRANCH
         CLI   PMSGDS2-FIRST4K(R1),0          ANY PREVIOUS DSNAME?
         BNE   COP3000                        YES, BRANCH
         M$MSG L973                           NO, ERROR
         B     NEWCMD
COP3000  MVC   MSGDS2(64),PMSGDS2-FIRST4K(R1) GET PREVIOUS DSNAME
         MVI   MTLEN,54
         MVC   INSERT#1(54),MSGDS2            CURRENT DSNAME
         M$MSG L273$1
         MVI   MTLEN,8
COP3100  LA    R1,FIRST4K
         MVC   PMSGDS2-FIRST4K(64,R1),MSGDS2  SAVE CURRENT DSNAME
         SPACE 2
         MVC   #CMXDDT1,DDNAME       SYSUT1 DDNAME
*  ALLOCATE COPY //SYSIN FILE
         XC    M99RBPTR(40),M99RBPTR      CLEAR THE WORK AREA
         LA    R1,M99RBPTR                DYNAMIC ALLOCATION
         USING S99RBP,R1
         SPACE 1
         LA    R4,M99RB
         USING S99RB,R4
         ST    R4,S99RBPTR
         OI    S99RBPTR,S99RBPND          MARK END OF LIST
         SPACE 1
         LA    R5,M99TUPL
         USING S99TUPL,R5
         MVI   S99RBLN,20                 LENGTH 20
         MVI   S99VERB,S99VRBAL           ALLOCATE
         ST    R5,S99TXTPP                POINT TO TEXT POINTERS
         SPACE 1
         MVC   #CMXSYS3(14),COPDDNAM      REQUEST DDNAME FEEDBACK
         LA    R3,#CMXSYS3                FIRST TEXT POINTER
         USING S99TUNIT,R3
         ST    R3,S99TUPTR                POINT TO FIRST TEXT UNITS
         MVC   S99TUPTR+4(40),COPSYSIN    ADD OTHER LIST ELEMENTS
         SPACE 1
         DYNALLOC
         MVC   #CMXDDIN(8),#CMXSYS3+6     MOVE IN THE DDNAME
         SPACE 2
         DROP  R1,R3,R4,R5
         LTR   R15,R15                    SUCCESSFUL?
         BNZ   COPERROR                   NO, BRANCH
         SPACE 3
*  ALLOCATE COPY //SYSPRINT DATA SET
         XC    M99RBPTR(40),M99RBPTR      CLEAR THE WORK AREA
         LA    R1,M99RBPTR                DYNAMIC ALLOCATION
         USING S99RBP,R1
         SPACE 1
         LA    R4,M99RB
         USING S99RB,R4
         ST    R4,S99RBPTR
         OI    S99RBPTR,S99RBPND          MARK END OF LIST
         SPACE 1
         LA    R5,M99TUPL
         USING S99TUPL,R5
         MVI   S99RBLN,20                 LENGTH 20
         MVI   S99VERB,S99VRBAL           ALLOCATE
         ST    R5,S99TXTPP                POINT TO TEXT POINTERS
         SPACE 1
         MVC   #CMXSYS3(14),COPDDNAM      REQUEST DDNAME FEEDBACK
         LA    R3,#CMXSYS3                FIRST TEXT POINTER
         USING S99TUNIT,R3
         ST    R3,S99TUPTR                POINT TO FIRST TEXT UNITS
         MVC   S99TUPTR+4(4),COPDUMMA     ASSUME DUMMY TEXT IS DESIRED
         CLI   #CMXFLAG,3                 NOLIST?
         BNE   *+8                        NO, BRANCH
         LA    R5,4(,R5)                  YES, USE THE DUMMY TEXT
         MVC   S99TUPTR+4(40),COPSYSPR    ADD OTHER LIST ELEMENTS
         MVC   MSGTEXT2(9+9),COPPRTFI     ADD PRIMARY, SECONDARY
         LA    R0,MSGTEXT2                START OF PRIMARY AMOUNT
         ST    R0,S99TUPTR+4              CHANGE ADDRESS POINTER
         LA    R0,MSGTEXT2+9              START OF SECONDARY AMOUNT
         ST    R0,S99TUPTR+4+4            CHANGE ADDRESS POINTER
         L     R15,TOTUSEDX               NUMBER OF USED MEMBER BLOCKS
         MH    R15,=H'21'                 MAXIMUM MEMBERS
         AR    R15,R15                    MAXIMUM MEMBERS*2
         SR    R14,R14
         D     R14,=F'50'                 50 MEMBERS/PAGE
         SRL   R15,4                      DIVIDED BY 16
         LA    R15,2(,R15)                ADD 2
         STCM  R15,B'0111',MSGTEXT2+6     UPDATE IN PRIMARY SPACE
         STCM  R15,B'0111',MSGTEXT2+9+6   UPDATE IN SECONDARY SPACE
         SPACE 1
         DYNALLOC
         SPACE 2
         MVC   #CMXDDPR(8),#CMXSYS3+6     MOVE IN THE DDNAME
         DROP  R1,R3,R4,R5
         LTR   R15,R15                    SUCCESSFUL?
         BNZ   COPERROR                   NO, BRANCH
         SPACE 2
*  ALLOCATE COPY //SYSUT3 DATA SET
         XC    M99RBPTR(40),M99RBPTR      CLEAR THE WORK AREA
         LA    R1,M99RBPTR                DYNAMIC ALLOCATION
         USING S99RBP,R1
         SPACE 1
         LA    R4,M99RB
         USING S99RB,R4
         ST    R4,S99RBPTR
         OI    S99RBPTR,S99RBPND          MARK END OF LIST
         SPACE 1
         LA    R5,M99TUPL
         USING S99TUPL,R5
         MVI   S99RBLN,20                 LENGTH 20
         MVI   S99VERB,S99VRBAL           ALLOCATE
         ST    R5,S99TXTPP                POINT TO TEXT POINTERS
         SPACE 1
         MVC   #CMXSYS3(14),COPDDNAM      REQUEST DDNAME FEEDBACK
         LA    R3,#CMXSYS3                FIRST TEXT POINTER
         USING S99TUNIT,R3
         ST    R3,S99TUPTR                POINT TO FIRST TEXT UNITS
         MVC   S99TUPTR+4(24),COPSYSUT    ADD OTHER LIST ELEMENTS
         MVC   MSGTEXT2(9+9),COPPRTFI     ADD PRIMARY, SECONDARY
         LA    R0,MSGTEXT2                START OF PRIMARY AMOUNT
         ST    R0,S99TUPTR+4              CHANGE ADDRESS POINTER
         LA    R0,MSGTEXT2+9              START OF SECONDARY AMOUNT
         ST    R0,S99TUPTR+4+4            CHANGE ADDRESS POINTER
         L     R15,TOTUSEDX               NUMBER OF USED MEMBER BLOCKS
         MH    R15,=H'21'                 MAXIMUM MEMBERS
         MH    R15,=H'105'
         SR    R14,R14
         D     R14,=F'100'                TOTAL SPACE IN BLOCKS
         SRL   R15,4                      DIVIDED BY 16
         LA    R15,2(,R15)                ADD 2
         STCM  R15,B'0111',MSGTEXT2+6     UPDATE IN PRIMARY SPACE
         STCM  R15,B'0111',MSGTEXT2+9+6   UPDATE IN SECONDARY SPACE
         SPACE 1
         DYNALLOC
         SPACE 2
         MVC   #CMXDDT3(8),#CMXSYS3+6     MOVE IN THE DDNAME
         DROP  R1,R3,R4,R5
         LTR   R15,R15                    SUCCESSFUL?
         BNZ   COPERROR                   NO, BRANCH
         SPACE 2
*  ALLOCATE COPY //SYSUT4 DATA SET
         XC    M99RBPTR(40),M99RBPTR      CLEAR THE WORK AREA
         LA    R1,M99RBPTR                DYNAMIC ALLOCATION
         USING S99RBP,R1
         SPACE 1
         LA    R4,M99RB
         USING S99RB,R4
         ST    R4,S99RBPTR
         OI    S99RBPTR,S99RBPND          MARK END OF LIST
         SPACE 1
         LA    R5,M99TUPL
         USING S99TUPL,R5
         MVI   S99RBLN,20                 LENGTH 20
         MVI   S99VERB,S99VRBAL           ALLOCATE
         ST    R5,S99TXTPP                POINT TO TEXT POINTERS
         SPACE 1
         MVC   #CMXSYS3(14),COPDDNAM      REQUEST DDNAME FEEDBACK
         LA    R3,#CMXSYS3                FIRST TEXT POINTER
         USING S99TUNIT,R3
         ST    R3,S99TUPTR                POINT TO FIRST TEXT UNITS
         MVC   S99TUPTR+4(28),COPSYSU4    ADD OTHER LIST ELEMENTS
         MVC   MSGTEXT2(9+9),COPPRTFI     ADD PRIMARY, SECONDARY
         LA    R0,MSGTEXT2                START OF PRIMARY AMOUNT
         ST    R0,S99TUPTR+4+4            CHANGE ADDRESS POINTER
         LA    R0,MSGTEXT2+9              START OF SECONDARY AMOUNT
         ST    R0,S99TUPTR+4+4+4          CHANGE ADDRESS POINTER
         MVI   MSGTEXT2+9+6+2,30          SECONDARY IS 30 TRACKS
         LA    R0,COPTRK
         ST    R0,S99TUPTR+4+12           CHANGE BLK(8) TO TRACK ALLOC.
         SPACE 1
         DYNALLOC
         SPACE 2
         MVC   #CMXDDT4(8),#CMXSYS3+6     MOVE IN THE DDNAME
         DROP  R1,R3,R4,R5
         LTR   R15,R15                    SUCCESSFUL?
         BNZ   COPERROR                   NO, BRANCH
         SPACE 2
*  ALLOCATE COPY //SYSUT2 DATA SET
         XC    M99RBPTR(40),M99RBPTR    CLEAR THE WORK AREA
         LA    R1,M99RBPTR              DYNAMIC ALLOCATION
         USING S99RBP,R1
         SPACE 1
         LA    R4,M99RB
         USING S99RB,R4
         ST    R4,S99RBPTR
         OI    S99RBPTR,S99RBPND        MARK END OF LIST
         SPACE 1
         LA    R5,M99TUPL
         USING S99TUPL,R5
         MVI   S99RBLN,20               LENGTH 20
         MVI   S99VERB,S99VRBAL         ALLOCATE
         ST    R5,S99TXTPP              POINT TO TEXT POINTERS
         LA    R3,WORKTBL                        START OF TEXT
         OI    S99FLG11,S99NOCNV                 DO NOT CONVERT ALLOC
         SPACE 1
         MVC   0(L'COPDDNAM,R3),COPDDNAM         SPECIFY DDNAME TEXT
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'COPDDNAM(,R3)                START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COPDSORG,R3),COPDSORG         SPECIFY DSORG TEXT
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'COPDSORG(,R3)                START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COPDSNAM,R3),COPDSNAM         SPECIFY DSNAME TEXT
         LA    R2,5(,R3)                         WHERE TO STORE OUTPUT
         MVC   6(44,R3),BLANK128                 CLEAR DSNAME AREA
         LA    R1,MSGDS2
         LA    R0,44
         CLI   0(R1),C''''                       QUOTED?
         BE    COP4510                           YES, BRANCH
         SPACE 1
         SR    R14,R14
         L     R15,ADDRUPT                       START OF THE UPT
* 8-CHAR PREFIX                                               DRK JUL17
         TM    FLAGSZZ,TSO#8                     8-CHAR ID/PREFIX?    "
         BNO   COP7PRE                           NO                   "
COP8PRE  DS    0H                                YES                  "
         ICM   R14,B'0001',UPTPRF8L-UPT(R15)     PREFIX LENGTH ZERO?  "
         BZ    COP4510+4                         YES, BRANCH          "
         MVC   6(8,R3),UPTPREF8-UPT(R15)         DSNAME PREFIX        "
         B     COPCONT                                                "
COP7PRE  DS    0H                                                     "
         ICM   R14,B'0001',UPTPREFL-UPT(R15)     PREFIX LENGTH ZERO?  "
         BZ    COP4510+4                         YES, BRANCH          "
         MVC   6(7,R3),UPTPREFX-UPT(R15)         DSNAME PREFIX        "
COPCONT  DS    0H                                             DRK JUL17
         AR    R2,R14                            POSITION FOR PERIOD
         SR    R0,R14
         MVI   1(R2),C'.'
         BCTR  R0,0
         BCTR  R1,0
         LA    R2,1(,R2)
         SPACE 1
COP4510  LA    R1,1(,R1)                         NEXT INPUT BYTE
         LA    R2,1(,R2)                         NEXT OUTPUT BYTE
         CLI   0(R1),C''''                       END OF DSNAME?
         BE    COP5000                           YES, BRANCH
         CLI   0(R1),C'('                        PARENTHESIS?
         BE    COP4520                           YES, BRANCH
         CLI   0(R1),X'40'                       BLANK?
         BE    COP5000                           YES, BRANCH
         MVC   0(1,R2),0(R1)                     MOVE IN ONE MORE BYTE
         BCT   R0,COP4510
         B     COP5000
         SPACE 1
COP4520  MVC   DOUBLE(8),BLANK128                (MEMBER OR DDNAME)
         LA    R0,8
         LA    R2,DOUBLE-1
         SPACE 1
COP4530  LA    R1,1(,R1)                         NEXT INPUT BYTE
         LA    R2,1(,R2)                         NEXT OUTPUT BYTE
         CLI   0(R1),C''''                       END OF DSNAME?
         BE    COP4540                           YES, BRANCH
         CLI   0(R1),C')'                        PARENTHESIS?
         BE    COP4540                           YES, BRANCH
         CLI   0(R1),X'40'                       BLANK?
         BE    COP4540                           YES, BRANCH
         MVC   0(1,R2),0(R1)                     MOVE IN ONE MORE BYTE
         BCT   R0,COP4530
         SPACE 1
COP4540  CLC   MSGDS2(5),COPLFIL5                FILE(?
         BE    COP4580                           YES, BRANCH
         CLC   MSGDS2(4),COPLFIL4                FIL(?
         BE    COP4580                           YES, BRANCH
         CLC   MSGDS2(3),COPLFIL3                FI(?
         BE    COP4580                           YES, BRANCH
         CLC   MSGDS2(2),COPLFIL2                F(?
         BE    COP4580                           YES, BRANCH
         SPACE 1
         CLI   DOUBLE,X'40'                      MEMBER NAME?
         BE    COP4560                           NO, BRANCH
         SR    R15,R15
         ICM   R15,B'0001',#COPTO#               ANY CURRENT RENAME?
         BNZ   COP4560                           YES, IGNORE THIS
         MVC   #COPTON(8),DOUBLE                 RENAME STRING
         SR    R15,R15
         LA    R1,DOUBLE
         MVI   DOUBLE+8,X'40'                    STOP THE SCAN
         SPACE 1
COP4550  LA    R15,1(,R15)                       LENGTH COUNT
         LA    R1,1(,R1)                         CHARACTER POINTER
         CLI   0(R1),X'40'                       BLANK?
         BNE   COP4550                           NO, BRANCH
         STC   R15,#COPTO#                       SAVE LENGTH COUNT
         SPACE 1
COP4560  TM    #COPALI,#COPSEQ                   SEQUENTIAL INPUT?
         BNO   COP5000                           NO, BRANCH
         OI    #COPALI,#COPSEQ+#COPMEMB          MUST USE IEBGENER
         LA    R1,M99RBPTR                       RESET R1
         MVC   MSGDS2(44),6(R3)                  RESET THE DSNAME
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'COPDSNAM+44(,R3)             START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COPMEMBE,R3),COPMEMBE         SPECIFY MEMBER TEXT
         MVC   6(8,R3),#COPTON                   GET THE MEMBER NAME
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'COPMEMBE(,R3)                START OF NEXT TEXT
         B     COP5005
         SPACE 2
COP4580  MVC   #CMXDDT2(8),DOUBLE                DDNAME ALLOCATION
* GET DSN FROM DDNAME + CAT NUMBER      *                      SS JUL88
         L     R1,PSATOLD-PSA(,R0)       ACT TCB               SS JUL88
         L     R1,TCBJSCB-TCBRBP(R1)     JSCB                  SS JUL88
         L     R1,JSCDSABQ-IEZJSCB(R1)   DSAB QUE              SS JUL88
         L     R15,12(,R1)               FIRST DSAB            SS JUL88
         USING DSAB,R15                                        SS JUL88
COP4582  DS    0H                       *                      SS JUL88
         LTR   R15,R15                  END OF THE LINE        SS JUL88
         BZ    COP5090                  NO DDNAME              SS JUL88
         L     R1,DSABTIOT                                     SS JUL88
         CLC   4(8,R1),#CMXDDT2         THIS DDNAME            SS JUL88
         BE    COP4584                  YES                    SS JUL88
         L     R15,DSABFCHN             NEXT DSAB              SS JUL88
         B     COP4582                  CONTINUE SEARCH        SS JUL88
COP4584  DS    0H                       *                      SS JUL88
         LR    R14,R15                  SAVE DSAB ADDR         SS JUL88
         L     R15,DSABFCHN             NEXT ENTRY             SS JUL88
         LTR   R15,R15                  ANY ENTRY              SS JUL88
         BZ    COP5090                  NO DDNAME              SS JUL88
         L     R1,DSABTIOT                                     SS JUL88
         CLC   4(8,R1),BLANKS           CONCATENATED           SS JUL88
         BNE   COP5090                  NO                     SS JUL88
         LR    R15,R14                  START WITH FIRST       SS JUL88
         SR    R0,R0                    ZERO COUNTER           SS JUL88
COP4590  DS    0H                       *                      SS JUL88
         A     R0,=F'1'                 ADD ONE                SS JUL88
         C     R0,#COPCONC              IS THIS THE NUMBER     SS JUL88
         BNL   COP4594                  YES                   ABL AUG88
         LR    R14,R15                  SAVE PREV              SS JUL88
         L     R15,DSABFCHN             NEXT ENTRY             SS JUL88
         LTR   R15,R15                  ANY ENTRY              SS JUL88
         BZ    COP4592                  USE LAST ENTRY         SS JUL88
         L     R1,DSABTIOT                                     SS JUL88
         CLC   4(8,R1),BLANKS           CONCATENATED           SS JUL88
         BE    COP4590                  YES, CONTINUE SEARCH   SS JUL88
COP4592  DS    0H                       *                      SS JUL88
         LR    R15,R14                  GET LAST ENTRY         SS JUL88
COP4594  DS    0H                       *                      SS JUL88
         L     R1,DSABTIOT                                     SS JUL88
         USING TIOENTRY,R1                                     SS JUL88
         SR    R14,R14                  *                      SS JUL88
         ICM   R14,7,TIOEFSRT           UCB ADDRESS            SS JUL88
         MVC   #COPVOL(6),UCBVOLI(R14)  VOL SER                SS JUL88
         AIF   ('&MVSLEV' GE 'MVS220').MVS22C                  SS JUL88
         ICM   R1,7,TIOEJFCB                                   SS JUL88
         LA    R1,16(,R1)        OVER HEADER                   SS JUL88
         MVC   6(44,R3),0(R1)           MOVE DSN               SS JUL88
         AGO   .MVS22CX                                        SS JUL88
.MVS22C  ANOP                                                  SS JUL88
         L     R1,CVTPTR(,R0)           *                      SS JUL88
         TM    CVTDCB-CVT(R1),CVTMVSE   REALLY XA              SS JUL88
         BZ    *+4+4+4+2                NO, SKIP  BSM          SS JUL88
         LA    R1,*+4+4+2               POINT AFTER BSM        SS JUL88
         O     R1,=X'80000000'          31 BIT MODE            SS JUL88
         BSM   0,R1                     SET MODE               SS JUL88
         L     R1,DSABSIOT              POINT TO SIOT          SS JUL88
         L     R1,SJFCBPTR-SIOT(R1)     JFCB ADDR              SS JUL88
         MVC   6(44,R3),0(R1)           MOVE DSN               SS JUL88
         L     R1,CVTPTR(,R0)           *                      SS JUL88
         TM    CVTDCB-CVT(R1),CVTMVSE   REALLY XA              SS JUL88
         BZ    *+4+4+2                  NO, SKIP  BSM          SS JUL88
         LA    R1,*+4+2                 POINT AFTER BSM        SS JUL88
         BSM   0,R1                     SET MODE 24            SS JUL88
.MVS22CX ANOP                                                  SS JUL88
         DROP  R15                      *                      SS JUL88
         SPACE 2
COP5000  LA    R1,M99RBPTR                       RESET R1
         MVC   MSGDS2(44),6(R3)                  RESET THE DSNAME
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'COPDSNAM+44(,R3)             START OF NEXT TEXT
         SPACE 1
COP5005  LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COPALLOC,R3),COPALLOC         SPECIFY DISP TEXT
         CLI   #CMXSHR,0                         ANY SHR/OLD/NEW?
         BNE   *+8                               YES, BRANCH
         MVI   #CMXSHR,&COPYSHR                  NO, USE DEFAULT
         CLI   #CMXSHR,2                         SHR?
         BNE   *+8                               NO, BRANCH
         MVI   6(R3),COPOSHR                     SPECIFY DISPOSITION
         CLI   #CMXSHR,3                         NEW?
         BNE   *+8                               NO, BRANCH
         MVI   6(R3),COPONEW                     SPECIFY DISPOSITION
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'COPALLOC(,R3)                START OF NEXT TEXT
         SPACE 1
         CLI   #COPVOL,0                         ANY VOLUME SERIAL?
         BE    COP5010                           NO, BRANCH
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COPVOLSR,R3),COPVOLSR         VOLUME TEXT
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         MVC   6(6,R3),#COPVOL                   VOLUME SERIAL
         LA    R3,L'COPVOLSR(,R3)                START OF NEXT TEXT
         SPACE 1
         AIF ('&PUNIT' EQ ' ').NOUNIT1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COPUNIT+8,R3),COPUNITP        ADD UNIT=SYSALLDA
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'COPUNIT+8(,R3)               START OF NEXT TEXT
         SPACE 1
.NOUNIT1 ANOP
COP5010  CLI   #CMXSHR,3                         NEW?
         BL    COP5050                           NO, BRANCH
         SPACE 1
         TM    #COPALI,#COPMEMB                  MEMBER OUTPUT?
         BO    COP5011                           YES, BRANCH
         TM    #COPALI,#COPSEQ                   SEQUENTIAL INPUT?
         BO    COP5014                           YES, BRANCH
COP5011  LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COPDIR,R3),COPDIR             SPECIFY DIRECTORY BLKS
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         ICM   R0,B'1111',#COPDIR#               ANY DIR(NUM) SPECIFIED
         BNZ   COP5012                           YES, BRANCH
         ICM   R0,B'1111',TOTALLOX               NO, ANY CURRENT COUNT?
         BNZ   COP5012                           YES, BRANCH
         LA    R0,20                             NO, JUST USE 20
COP5012  STCM  R0,B'0111',6(R3)                  UPDATE DIR COUNT
         LA    R3,L'COPDIR(,R3)                  START OF NEXT TEXT
         SPACE 1                                              DRK MAR07
         CLC   #COPTYPE(8),=CL8'PDS'             DSNTYPE(PDS)?
         BE    COP5014                           YES
         CLC   #COPTYPE(8),=CL8'LIBRARY'         DSNTYPE(LIBRARY)?
         BE    COP5013                           YES
         TM    DS1SMSFG,DS1PDSE                  PDSE INPUT DATASET?
         BNO   COP5014                           NO
COP5013  DS    0H
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COP2PDSE,R3),COP2PDSE         SPECIFY PDSE TYPE
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'COP2PDSE(,R3)                START OF NEXT TEXT
         SPACE 1
         AIF   ('&MVSLEV' LT 'MVS721').NOMAXG                 DRK SEP18
         ICM   R0,B'1111',#COPMAXG               ANY MAXGENS SPECIFIED?
         BZ    COP5014                           NO           DRK MAY18
         C     R0,=F'2000000000'                 >2G?                 "
         BNH   COP5013A                          NO                   "
         M$MSG L983                              YES, ERROR           "
         B     NEWCMD                                                 "
COP5013A DS    0H
         L     R15,CVTPTR(,R0)        LOAD CVT POINTER        DRK MAY18
         L     R15,CVTDFA-CVT(,R15)   DATA FACILITIES AREA    DRK MAY18
         L     R15,DFAMAXGN-DFA(,R15) PDSE MAX GENERATIONS ALLOWED    "
         CR    R0,R15                 MAXGENS > SMS LIMIT?    DRK MAY18
         BNH   COP5013B               NO                      DRK MAY18
         M$MSG L984                   YES, ERROR              DRK MAY18
         B     NEWCMD                                                 "
COP5013B DS    0H
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COPMAXG,R3),COPMAXG           SPECIFY MAXGENS
         MVC   6(4,R3),#COPMAXG                  MAXGENS      DRK MAY18
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'COPMAXG(,R3)                 START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COPVER2,R3),COPVER2           SPECIFY PDSE V2
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'COPVER2(,R3)                 START OF NEXT TEXT
         SPACE 1
.NOMAXG  ANOP                                                 DRK SEP18
COP5014  LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COPTRK,R3),COPTRK             SPECIFY TRACK ALLOC
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         TM    DS1SCALO,X'C0'                    CYLINDER ALLOCATION?
         BNO   *+8                               NO, BRANCH
         MVI   1(R3),X'08'                       YES, CONVERT TO CYL
         SR    R0,R0
         ICM   R0,B'0011',#COPSPAC               ANY PRIMARY SPACE?
         BZ    COP5018                           NO, BRANCH
         CLI   #COPTRCY,0                        TRK/CYL REQUESTED?
         BH    COP5018                           YES, BRANCH
         MVI   #COPTRCY,1                        NO, DEFAULT TO TRK
COP5018  CLI   #COPTRCY,1                        TRACK?
         BNE   *+8                               NO, BRANCH
         MVI   1(R3),X'07'                       YES, CONVERT TO TRK
         CLI   #COPTRCY,2                        CYLINDER?
         BNE   *+8                               NO, BRANCH
         MVI   1(R3),X'08'                       YES, CONVERT TO CYL
         LA    R3,L'COPTRK(,R3)                  START OF NEXT TEXT
         SPACE 2
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COPFIRST,R3),COPFIRST         SPECIFY PRIMARY
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         SR    R14,R14
         ICM   R14,B'0011',#COPSPAC              ANY PRIMARY SPACE?
         BNZ   COP5020                           YES, BRANCH
         L     R14,DSNTOTAL                      TOTAL TRACKS ALLOC
         LTR   R14,R14                           ZERO?        DRK MAR16
         BNZ   *+4+4                             NO                   "
         L     R14,=F'100'                       YES, NO PDS200I YET  "
         SPACE 1
         TM    DS1SCALO,X'C0'                    CYLINDER ALLOCATION?
         BNO   COP5020                           NO, BRANCH
         LH    R0,DEVTTRKS                       TRACKS PER CYLINDER?
         LR    R15,R0
         BCTR  R15,0
         AR    R15,R14
         SR    R14,R14
         DR    R14,R0
         LR    R14,R15                           CONVERTED TO CEIL(CYL)
COP5020  STCM  R14,B'0111',6(R3)                 UPDATE FIRST QUANTITY
         LA    R3,L'COPFIRST(,R3)                START OF NEXT TEXT
         SPACE 2
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COPSECON,R3),COPSECON         SPECIFY SECONDARY
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         SR    R14,R14
         ICM   R14,B'0011',#COPSPAC              ANY PRIMARY SPACE?
         BZ    COP5026                           NO, BRANCH
         ICM   R14,B'0011',#COPSPAC+2            SECONDARY SPACE?
         B     COP5030
         SPACE 1
COP5026  ICM   R14,B'0111',DS1SCALO+1            ANY SECONDARY?
         BZ    COP5030                           NO, BRANCH
         TM    DS1SCALO,X'80'                    TRK OR CYL ALLOC?
         BO    COP5030                           YES, BRANCH
         LH    R14,BLKSI                         BLOCKSIZE
         ICM   R14,B'1000',=X'01'                R=1, K=0, D=BLKSIZE
         PUSH  ACONTROL                                       DRK MAY08
         ACONTROL FLAG(NOPAGE0)                               DRK MAY08
         TRKCALC FUNCTN=TRKCAP,TYPE=BYTEUCB+1,RKDD=(14),              XX
               REGSAVE=YES,MF=(E,PARMLIST)
         POP   ACONTROL                                       DRK MAY08
         SR    R14,R14
         ICM   R14,B'0111',DS1SCALO+1            SECONDARY SIZE
         LR    R15,R0
         AR    R15,R14
         SR    R14,R14
         DR    R14,R0
         LR    R14,R15                           CONVERTED TO CEIL(TRK)
COP5030  STCM  R14,B'0111',6(R3)                 UPDATE OTHER QUANTITY
         LA    R3,L'COPSECON(,R3)                START OF NEXT TEXT
         SPACE 2
COP5050  LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COPNORM,R3),COPNORM           DISP=(,CATLG)
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         CLI   #CMXSHR,3                         NEW?
         BE    *+8                               YES, BRANCH
         MVI   6(R3),X'08'                       NO, USE DISP=(,KEEP)
         LA    R3,L'COPNORM(,R3)                 START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COPANORM,R3),COPANORM         DISP=(,,DELETE)
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         CLI   #CMXSHR,3                         NEW?
         BE    *+8                               YES, BRANCH
         MVI   6(R3),X'08'                       NO, USE DISP=(,,KEEP)
         SPACE 1
         OI    S99TUPTR,X'80'                    MARK END OF LIST
         DYNALLOC
         SPACE 2
         DROP  R1,R4,R5
         MVC   #CMXDDT2,WORKTBL+6            SAVE DDNAME
         LTR   R15,R15                       SUCCESSFUL?
         BNZ   COPERROR                      NO, BRANCH
         SPACE 2
         CLI   WORKTBL+14+6,DS1DSGPO         PARTITIONED?
         BE    COP5079                       YES, BRANCH
         TM    #COPALI,#COPSEQ               SEQUENTIAL INPUT?
         BO    COP5080                       YES, BRANCH
         TM    #COPALI,#COPONE               SINGLE MEMBER?
         BNO   COP5078                       NO, BRANCH
         OI    #COPALI,#COPSEQ               SEQUENTIAL INPUT NOW
         SPACE 2
*  ALLOCATE COPY //SYSUT1 DATA SET TO A SPECIFIC MEMBER
         XC    M99RBPTR(40),M99RBPTR    CLEAR THE WORK AREA
         LA    R1,M99RBPTR              DYNAMIC ALLOCATION
         USING S99RBP,R1
         SPACE 1
         LA    R4,M99RB
         USING S99RB,R4
         ST    R4,S99RBPTR
         OI    S99RBPTR,S99RBPND        MARK END OF LIST
         SPACE 1
         LA    R5,M99TUPL
         USING S99TUPL,R5
         MVI   S99RBLN,20               LENGTH 20
         MVI   S99VERB,S99VRBAL         ALLOCATE
         ST    R5,S99TXTPP              POINT TO TEXT POINTERS
         LA    R3,WORKTBL                        START OF TEXT
         SPACE 1
         MVC   0(L'COPDDNAM,R3),COPDDNAM         SPECIFY DDNAME TEXT
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'COPDDNAM(,R3)                START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COPDSNAM,R3),COPDSNAM         SPECIFY DSNAME TEXT
         LA    R2,5(,R3)                         WHERE TO STORE OUTPUT
         MVC   6(44,R3),DSNAME                   MOVE IN DSNAME TEXT
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'COPDSNAM+44(,R3)             START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COPMEMBE,R3),COPMEMBE         SPECIFY MEMBER TEXT
         MVC   6(8,R3),MEMNAME                   GET THE MEMBER NAME
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'COPMEMBE(,R3)                START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COPALLOC,R3),COPALLOC         SPECIFY DISP TEXT
         MVI   6(R3),COPOSHR                     SPECIFY DISPOSITION
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'COPALLOC(,R3)                START OF NEXT TEXT
         SPACE 1
         CLI   VOLUME,X'41'                      ANY VOLUME SERIAL?
         BL    COP5060                           NO, BRANCH
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COPVOLSR,R3),COPVOLSR         VOLUME TEXT
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         MVC   6(6,R3),VOLUME                    VOLUME SERIAL
         LA    R3,L'COPVOLSR(,R3)                START OF NEXT TEXT
         SPACE 1
         AIF ('&PUNIT' EQ ' ').NOUNIT2
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COPUNIT+8,R3),COPUNITP        ADD UNIT=SYSALLDA
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'COPUNIT+8(,R3)               START OF NEXT TEXT
         SPACE 1
.NOUNIT2 ANOP
COP5060  LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COPNORM,R3),COPNORM           DISP=(,CATLG)
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         MVI   6(R3),X'08'                       USE DISP=(,KEEP)
         LA    R3,L'COPNORM(,R3)                 START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COPANORM,R3),COPANORM         DISP=(,,DELETE)
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         MVI   6(R3),X'08'                       USE DISP=(,,KEEP)
         SPACE 1
         OI    S99TUPTR,X'80'                    MARK END OF LIST
         DYNALLOC
         SPACE 2
         DROP  R1,R4,R5
         MVC   #CMXDDT1,WORKTBL+6            SAVE DDNAME
         LTR   R15,R15                       SUCCESSFUL?
         BNZ   COPERROR                      NO, BRANCH
         B     COP5080
         SPACE 1
COP5078  M$MSG L532                          NO, MULTIPLE MEMBER UNLOAD
         B     COP5080                       DO NOT OPEN FOR BLDL
         SPACE 2
COP5079  TM    DSORG,DS1DSGPO                PARTITIONED INPUT?
         BO    COP5090                       YES, BRANCH
         TM    #COPALI,#COPMEMB              SINGLE MEMBER OUTPUT?
         BO    COP5080                       YES, BRANCH
         M$MSG L982                          NO, PARTITIONED BUT NO MEM
         B     COP7525
         SPACE 2
*** OPEN THE OUTPUT DATA SET TO GET THE UCB ADDRESS IF DISP=SHR
COP5080  OI    #COPALI,#COPSOME              SOME OUTPUT MEMBERS
         CLI   #CMXSHR,2                     DISP=SHR?
         BNE   COP6000                       NO, BRANCH
         MVC   BAMDCB(LCOPDCB),COPDCBPO      CONSTRUCT A DCB
         MVC   DCBDDNAM-IHADCB+BAMDCB(8),#CMXDDT2  ADD DDNAME
         MVI   DCBDSORG-IHADCB+BAMDCB,DS1DSGPS     DSORG=PS
         MVI   OPENLIST,X'80'
         OPEN  (BAMDCB,(OUTPUT)),MF=(E,OPENLIST)
         TM    DCBOFLGS-IHADCB+BAMDCB,X'10'  OPEN OK?
         BO    COP5084                       YES, BRANCH
         MVC   INSERT#1(8),COPYDCB           NO, ERROR
         M$MSG L780$1
         B     COP7525
         SPACE 2
COP5084  L     R1,DCBDEBAD-IHADCB+BAMDCB     DEB ADDRESS
         L     R1,DEBUCBAD(,R1)              UCB ADDRESS
         ST    R1,#SUBHOLD+48                UPDATE FOR THIS SUBCOMMAND
         MVI   OPENLIST,X'80'
         CLOSE (BAMDCB),MF=(E,OPENLIST)
         B     COP6000
         SPACE 2
*** OPEN THE OUTPUT DATA SET TO CHECK FOR MEMBER PRESENCE
COP5090  MVC   BAMDCB(LCOPDCB),COPDCBPO      CONSTRUCT A DCB
         MVC   DCBDDNAM-IHADCB+BAMDCB(8),#CMXDDT2  ADD DDNAME
         MVI   OPENLIST,X'80'
         OPEN  (BAMDCB,(OUTPUT)),MF=(E,OPENLIST)
         TM    DCBOFLGS-IHADCB+BAMDCB,X'10'  OPEN OK?
         BO    COP5500                       YES, BRANCH
         MVC   INSERT#1(8),COPYDCB           NO, ERROR
         M$MSG L780$1
         B     COP7525
         SPACE 2
COP5500  L     R1,DCBDEBAD-IHADCB+BAMDCB     OUTPUT DEB ADDRESS
         L     R1,DEBUCBAD(,R1)              UCB ADDRESS
         ST    R1,#SUBHOLD+48                UPDATE FOR THIS SUBCOMMAND
         LH    R1,DCBBLKSI-IHADCB+BAMDCB     OUTPUT BLKSIZE
         STH   R1,#REPRBLK                   SAVE FOR LATER
         L     R3,#MEMPTR
         L     R4,#MEMLAST                   LAST VALUE
         SH    R3,=H'16'
         CLI   #COPTO#,0                     ANY REPLACE VALUE?
         BE    COP5510                       NO, BRANCH
         TM    #COPALI,#COPONE               SINGLE MEMBER - NO ALIAS?
         BNO   COP5510                       NO, BRANCH
         MVI   #COPTO#,8                     YES, SET LENGTH TO 8
         SPACE 1
COP5510  LA    R3,16(,R3)                    NEXT ELEMENT
         CR    R3,R4                         DONE?
         BNL   COP5560                       YES, BRANCH
         CLI   #COPEXST,2                    NOBLDL OR EXCLUDE?
         BH    COP5522                       YES, IGNORE BLDL
         CLI   #COPEXST,0                    ANY EXIST/NOEXIST?
         BH    COP5511                       YES, BRANCH
         CLI   #COPREP,1                     REPLACE?
         BNE   COP5511                       NO, BRANCH
         OI    #COPALI,#COPSOME              SOME OUTPUT
         B     COP5510
         SPACE 1
COP5511  MVC   DIRNAME(8),0(R3)
         SR    R1,R1
         ICM   R1,B'0001',#COPTO#            NUMBER OF BYTES
         BZ    COP5512                       NONE, BRANCH
         BCTR  R1,0
         MVC   DIRNAME(*-*),#COPTON          <<EXECUTED>>
         EX    R1,*-6                        UPDATE NAME
         SPACE 1
COP5512  BLDL  BAMDCB,BLDLLIST
         B     *+4(R15)                      PROCESS RETURN CODE
         B     COP5520                        00 - ALREADY THERE
         B     COP5540                        04 - NOT FOUND
         B     IOERROR                        08 - I/O ERROR
         SPACE 1
COP5520  CLI   #COPEXST,1                    ANY EXIST/NOEXIST?
         BL    COP5524                       NONE, ---BRANCH
         BE    COP5522                       EXIST, --BRANCH
         MVI   0(R3),0                       NOEXIST, NULLIFY ENTRY
         B     COP5510
         SPACE 1
COP5522  OI    #COPALI,#COPSOME              SOME OUTPUT
         B     COP5510
         SPACE 1
COP5524  OI    #COPALI,#COPSOME              SOME OUTPUT
         MVC   INSERT#1(8),DIRNAME
         M$MSG L852$1
         B     COP5510
         SPACE 2
COP5540  CLI   #COPEXST,1                    ANY EXIST/NOEXIST?
         BL    COP5544                       NONE, ---BRANCH
         BE    COP5542                       EXIST, --BRANCH
         OI    #COPALI,#COPSOME              NOEXIST, SOME OUTPUT
         B     COP5510
         SPACE 1
COP5542  MVI   0(R3),0                       NULLIFY ENTRY
         B     COP5510
         SPACE 1
COP5544  OI    #COPALI,#COPSOME              SOME OUTPUT
         B     COP5510
         SPACE 3
COP5560  MVI   OPENLIST,X'80'
         CLOSE (BAMDCB),MF=(E,OPENLIST)
         LA    R1,L245                       NO MATCHING MEMBERS FOUND
         TM    #COPALI,#COPSOME              ANY OUTPUT?
         BNO   MSGNEW                        NO, BRANCH
         B     COP6000
         SPACE 2
COPERROR ST    R15,DAIRRC                 SAVE RETURN CODE
         LA    R14,M99RB                  POINTER TO SVC 99 BLOCK
         LA    R15,DAIRRC                 POINTER TO RETURN CODE
         LA    R0,ADDRFF02                POINTER TO A(IKJEFF02)
         LA    R1,=AL2(DFSVC99)           POINTER TO INVOCATION TYPE
         L     R2,ADDRCPPL                POINTER TO THE CPPL
         STM   R14,R2,DFDAPLP             INITIALIZE DFDAPLP, DFDRCP,
*                                           DFJEFF02, DFIDP AND DFCPPLP
         SPACE 1
         LINK  EP=IKJEFF18,MF=(E,DFPARMS)
         LTR   R15,R15                    PROBLEM WITH IKJEFF18?
         BZ    COP7525                    NO, BRANCH
         SPACE 1
         CVD   R15,DOUBLE
         MVI   MTLEN,4
         MVC   INSERT#1(4),=X'40202120'
         ED    INSERT#1(4),DOUBLE+6
         MVI   INSERT#1,C'='
         M$MSG L835$1
         B     COP7525
         SPACE 3
COP6000  DS    0H
* OPEN SYSIN DATA SET
         MVC   BAMDCB(COPDL),COPDCB           GET MODEL DCB
         MVC   DCBDDNAM-IHADCB+BAMDCB(8),#CMXDDIN
         MVI   OPENLIST,X'80'
         OPEN  (BAMDCB,OUTPUT),MF=(E,OPENLIST) OPEN SYSIN
         TM    DCBOFLGS-IHADCB+BAMDCB,X'10'    SUCCESSFUL OPEN?
         BO    COP6010                         YES, BRANCH
         M$MSG L865                            NO STATUS AVAILABLE
         B     COP7525                         EXIT
         SPACE 3
*  OUTPUT SYSIN RECORDS
         SPACE 1
COP6010  TM    #COPALI,#COPSEQ             SEQUENTIAL PROCESSING?
         BO    COP6590                     YES, BRANCH
         SPACE 1
         L     R3,#MEMPTR            FIRST MEMBER POINTER
         L     R4,#MEMLAST           LAST MEMBER POINTER
         B     COP6040+4             SKIP FIRST ADD
COP6040  LA    R3,16(,R3)            NEXT MEMBER POSITION
         CR    R3,R4                 END OF MEMBER TABLE?
         BNL   COP6070               YES, NOT FOUND
         SR    R15,R15
         ICM   R15,B'0001',#COPTO#   ANY RENAME?
         BZ    COP6070               NO, BRANCH
         BCTR  R15,0                 MACHINE LENGTH OF COMPARE
         LR    R1,R3                 NEXT MEMBER POSITION
COP6050  LA    R1,16(,R1)            NEXT MEMBER POSITION
         CR    R1,R4                 END OF MEMBER TABLE?
         BNL   COP6040               YES, NO MATCH
         MVC   DOUBLE(8),0(R3)       FIRST MEMBER NAME
         XC    DOUBLE(8),0(R1)       VERSUS SECOND MEMBER NAME
         XC    DOUBLE(*-*),DOUBLE    <<EXECUTED>>
         EX    R15,*-6               CLEAR THE FRONT FEW CHARACTERS
         OC    DOUBLE(8),DOUBLE      EXACT MATCH AFTER FIRST PART?
         BNZ   COP6050               NO, LOOP
         SPACE 1
         MVC   INSERT#1(8),0(R3)     MEMBER NAME
         MVC   INSERT#1(*-*),#COPTON <<EXECUTED>>
         EX    R15,*-6               SET THE FRONT FEW CHARACTERS
         M$MSG L885$1                ERROR MESSAGE
         OI    #COPALI,#COPDUP       MARK FOR LATER
         B     COP6040               NO, LOOP
         SPACE 1
COP6070  MVC   MSGTEXT1(136),MSGBL132      CLEAR THE MESSAGE AREA
         MVC   MSGTEXT1+4(11),COPDSN1M     " COPY    O="
         MVC   MSGTEXT1+4+11(8),#CMXDDT2
         LA    R1,MSGTEXT1+4+11
         SPACE 1
         LA    R1,1(,R1) NEXT CHARACTER
         CLI   0(R1),X'40'                 BLANK?
         BNE   *-8                         NO, LOOP
         SPACE 1
         CLI   #CMXFLAG,1                  SUMMARY LIST REQUESTED?
         BH    COP6100                     NO, BRANCH
         MVC   0(8,R1),COPLISTN            ADD ",LIST=NO"
         LA    R1,8(,R1)                   EIGHT CHARACTERS
         SPACE 1
COP6100  DS    0H
         AIF ('&COPYMOD' EQ 'NO').NOCOPYM
         TM    FLAGSCC,RECFMU              LOAD LIBRARY?
         BNO   COP6300                     NO, BRANCH
         OC    #REPRBLK(2),#REPRBLK        ANY BLKSIZE?
         BZ    COP6300                     NO, MUST BE SEQUENTIAL
         OC    #REPRBLK+2(2),#REPRBLK+2    ANY MAXBLK PARAMETER?
         BNZ   COP6150                     YES, BRANCH
         CLC   BLKSI(2),#REPRBLK           INPUT BLKSIZE<=OUTPUT?
         BNH   COP6300                     YES, BRANCH
         LH    R15,#REPRBLK                COPY OUTPUT BLKSIZE
         STH   R15,#REPRBLK+2              TO MAXBLK FIELD
         SPACE 1
COP6150  MVC   MSGTEXT1+4(8),COPCOPMO      CHANGE COPY TO COPYMOD
         MVC   0(8,R1),COPMAXBL            ",MAXBLK="
         LH    R15,#REPRBLK+2              OUTPUT MAXBLK OR BLKSIZE
         CVD   R15,DOUBLE
         UNPK  DOUBLE(5),DOUBLE+5(3)       CONVERT TO DISPLAY
         OI    DOUBLE+4,X'F0'              MAKE LAST DIGIT PRINTABLE
         MVC   0+8(5,R1),DOUBLE            MOVE TO MESSAGE
         LA    R1,8+5(,R1)                 UPDATE LENGTH
.NOCOPYM ANOP
         SPACE 1
COP6300  MVC   0(3,R1),COPDSN2M            ",I="
         MVC   3(8,R1),#CMXDDT1
         CLI   #COPREP,1                   REPLACE?
         BNE   COP6310                     NO, BRANCH
         MVC   3(2,R1),COPDSNLP            "(("
         MVC   3+2(8,R1),#CMXDDT1
         SPACE 1
         LA    R1,1(,R1) NEXT CHARACTER
         CLI   0(R1),X'40'                 BLANK?
         BNE   *-8                         NO, LOOP
         SPACE 1
         MVC   0(4,R1),COPDSNRP            ",R))"
COP6310  PUT   BAMDCB,MSGTEXT1+4           OUTPUT COPY STATEMENT
         SPACE 1
         L     R3,#MEMPTR            FIRST MEMBER POINTER
         L     R4,#MEMLAST           LAST MEMBER POINTER
         CLI   #COPNCAL,2            NOCALL?
         BNE   COP6513               NO, BRANCH
         M$ERRST MSGTEXT1            YES, OUTPUT FIRST LINE
         SPACE 1
COP6513  TM    FLAGSHH,FMEMALLM                  ALL MEMBERS?
         BZ    COP6520                           NO, BRANCH
         CLI   #COPEXST,4                        EXCLUDE?
         BE    COP6520                           YES, BRANCH
         CLI   #COPEXST,2                        EXIST?
         BE    COP6520                           YES, BRANCH
         CLI   #COPEXST,1                        NOEXIST?
         BE    COP6520                           YES, BRANCH
         CLI   #COPTO#,0                         ANY REP NAME?
         BNE   COP6520                           YES, BRANCH
***COPY  B     COP6590                           NO INDIVIDUAL COPY
**
COP6520  LA    R1,MSGTEXT1+4+5-1                 START OF MEMBER NAMES
         LA    R0,MSGTEXT1+4+60                  END OF MEMBER NAMES
         MVC   MSGTEXT1(136),MSGBL132
         MVC   MSGTEXT1+4(5),COPMEM1M            " S M="
         CLI   #COPEXST,4                        EXCLUDE?
         BNE   *+8                               NO, BRANCH
         MVI   MSGTEXT1+4+1,C'E'                 YES, CHANGE TO " E M="
         SR    R15,R15
         ICM   R15,B'0001',#COPTO#               ANY REP NAME?
         BZ    COP6530                           NO, BRANCH
         LA    R1,MSGTEXT1+4+5                   START OF MEMBER NAMES
         LA    R0,MSGTEXT1+4+47                  47 INSTEAD OF 60
         SPACE 1
COP6530  CR    R3,R4
         BNL   COP6580
         CLI   0(R3),0                           NULLIFIED ENTRY?
         BNE   COP6532                           NO, BRANCH
         LA    R3,16(,R3)                        YES, GET NEXT ENTRY
         B     COP6530
         SPACE 1
COP6532  SR    R15,R15
         ICM   R15,B'0001',#COPTO#               ANY REP NAME?
         BZ    COP6540                           NO, BRANCH
         SPACE 1
         BCTR  R15,0                             MACHINE LENGTH
         MVI   MSGTEXT1+4+5,C'('                 BEGINNING (
         CLC   0(*-*,R3),#COPTON                 <<EXECUTED>>
         EX    R15,*-6                           SAME PREFIX?
         BE    COP6540                           YES, BRANCH
         SPACE 1
COP6535  MVI   1(R1),C'('                        BEGINNING ((
         MVC   2(8,R1),0(R3)                     INPUT MEMBER NAME
         LA    R14,3+8(,R1)
         SPACE 1
         CLI   0(R14),X'40'                      BLANK?
         BNE   *+8                               NO, BRANCH
         BCT   R14,*-8
         SPACE 1
         MVI   1(R14),C','                       ADD A COMMA
         LA    R1,2(,R14)
         MVC   0(8,R1),0(R3)                     INPUT MEMBER NAME
         MVC   0(*-*,R1),#COPTON                 <<EXECUTED>>
         EX    R15,*-6                           OUTPUT MEMBER NAME
         LA    R14,8(,R1)
         SPACE 1
         CLI   0(R14),X'40'                      BLANK?
         BNE   *+8                               NO, BRANCH
         BCT   R14,*-8
         SPACE 1
         MVI   1(R14),C')'                       FINAL )
         LA    R14,1(,R14)
         B     COP6560
         SPACE 2
COP6540  MVC   1(8,R1),0(R3)                     ADD THE MEMBER NAME
         LA    R14,1+8(,R1)
         SPACE 1
         CLI   0(R14),X'40'                      BLANK?
         BNE   *+8                               NO, BRANCH
         BCT   R14,*-8
         SPACE 1
COP6560  MVI   1(R14),C','                       ADD A COMMA
         LA    R1,1(,R14)
         LA    R3,16(,R3)                        NEXT MEMBER ENTRY
         CR    R1,R0                             END OF CARD?
         BNH   COP6530                           NO, BRANCH
         SPACE 2
         MVI   0(R1),X'40'                       CLEAR THE LAST COMMA
         SR    R15,R15
         ICM   R15,B'0001',#COPTO#               ANY REP NAME?
         BZ    *+8                               NO, BRANCH
         MVI   0(R1),C')'                        YES, FINAL ))
         PUT   BAMDCB,MSGTEXT1+4                 OUTPUT SELECT STMT
         CLI   #COPNCAL,2                        NOCALL?
         BNE   COP6570                           NO, BRANCH
         SPACE 1
         M$ERRST MSGTEXT1                        YES, OUTPUT FIRST LINE
COP6570  MVC   MSGTEXT1(136),MSGBL132
         B     COP6520
         SPACE 2
COP6580  CLI   MSGTEXT1+4+5,X'40'                ANY TEXT TO OUTPUT?
         BE    COP6590                           NO, BRANCH
         MVI   0(R1),X'40'                       CLEAR THE LAST COMMA
         SR    R15,R15
         ICM   R15,B'0001',#COPTO#               ANY REP NAME?
         BZ    *+8                               NO, BRANCH
         MVI   0(R1),C')'                        YES, FINAL ))
         SPACE 1
         PUT   BAMDCB,MSGTEXT1+4                 OUTPUT SELECT STMT
         CLI   #COPNCAL,2                        NOCALL?
         BNE   COP6590                           NO, BRANCH
         M$ERRST MSGTEXT1                        YES, OUTPUT FIRST LINE
         SPACE 2
COP6590  MVI   OPENLIST,X'80'
         CLOSE (BAMDCB),MF=(E,OPENLIST)   CLOSE SYSIN
         LA    R1,BAMDCB
         FREEPOOL (1)
         TM    #COPALI,#COPDUP            ANY COLLISIONS?
         BO    COP7525                    YES, BRANCH
         SPACE 2
         CLI   #CMXSHR,3                NEW?                  DRK APR16
         BE    COP6600                  YES, NO LOCATE        DRK APR16
*        CLC   #CMXDDT2(8),=XL8'00'     DDNAME ALLOCATION?    DRK AUG16
*        BNE   COP6600                  YES, NO LOCATE        DRK AUG16
         CLC   MSGDS2(5),COPLFIL5       FILE(?                DRK DEC16
         BE    COP6600                  YES, NO LOCATE        DRK DEC16
         CLC   MSGDS2(4),COPLFIL4       FIL(?                 DRK DEC16
         BE    COP6600                  YES, NO LOCATE        DRK DEC16
         CLC   MSGDS2(3),COPLFIL3       FI(?                  DRK DEC16
         BE    COP6600                  YES, NO LOCATE        DRK DEC16
         CLC   MSGDS2(2),COPLFIL2       F(?                   DRK DEC16
         BE    COP6600                  YES, NO LOCATE        DRK DEC16
*  LOCATE OUTPUT DSNAME                                       DRK APR16
         MVC   WORKTMP(44),MSGDS2       COPY DSNAME           DRK APR16
         L     R14,LOCATEC                                    DRK APR16
         LA    R15,WORKTMP              ACTUAL DSNAME         DRK APR16
         SR    R0,R0                    NO CVOL VALUE         DRK APR16
         LA    R1,MSGTEXT1              LOCATE WORK AREA      DRK APR16
         STM   R14,R1,PARMLIST          SAVE CAMLIST PARAM    DRK APR16
         LOCATE PARMLIST                                      DRK APR16
         LTR   15,15                    SUCCESSFUL LOCATE?    DRK APR16
         BNZ   COPNODSN                 NO, ERROR             DRK APR16
         CLI   #COPVOL,0                ANY VOLUME?           DRK APR16
         BNE   COP6600                  YES                   DRK APR16
         MVC   #COPVOL(6),MSGTEXT1+6    NO, USE LOCATE VOLSER DRK APR16
         B     COP6600                                        DRK APR16
         SPACE 1                                              DRK APR16
COPNODSN MVI   MTLEN,44                 LENGTH OF INSERT      DRK APR16
         MVC   INSERT#1(44),WORKTMP     DSNAME                DRK APR16
         M$MSG L844$1                                         DRK APR16
         B     NEWCMD                                         DRK APR16
COP6600  DS    0H                                             DRK APR16
         CLC   DSNAME(44),MSGDS2     SAME DSNAME?
         BNE   COP6620               NO, BRANCH
         CLI   #COPVOL,0             ANY VOLUME
         BE    COP6610               NO, BRANCH
         CLC   #COPVOL(6),VOLALLOC   SAME VOLUME?
         BNE   COP6620               NO, BRANCH
COP6610  M$MSG L900                  SAME DATA SET MESSAGE
         B     COP7525
         SPACE 2
COP6620  CLI   #COPNCAL,2                 NOCALL?
         BE    COP7530                    YES, BRANCH
         M$MSG L484                       COPY IN PROGRESS MESSAGE
         OI    ##ADRPA#,$A                DEFER ALL ATTENTIONS
         SPACE 1
         XC    WORKTBL,WORKTBL            CLEAR THE TEMPORARY TABLE
         STAX  COPYNULL,USADDR=(R7),REPLACE=NO,IBUF=0,                 X
               OBUF=(PDS484W,PDS484WL),MF=(E,WORKTBL)
         TM    #COPALI,#COPSEQ             SEQUENTIAL COPY?
         BNO   *+10                        NO, BRANCH
         MVC   ##SUBCAL(8),COPGENER        YES, USE IEBGENER
         LA    R1,=F'0'                   NO FIRST PARAMETER
         ST    R1,#CMXLIST
         LA    R1,#CMXDDNA                SECOND PARAMETER
         ST    R1,#CMXLIST+4
         OI    #CMXLIST+4,X'80'           MARK END OF LIST
         XC    #CMXDDNA(34),#CMXDDNA      CLEAR FIRST PORTION OF LIST
         MVI   #CMXDDNA+1,80              LENGTH OF DDNAME LIST
         XC    #SUBHOLD(28+20),#SUBHOLD
         LA    R14,#SUBHOLD+28            POINT TO FLAGS
         LA    R15,COPCOPY                POINT TO CALLED PROGRAM
         LA    R0,#SUBHOLD+28+4           POINT TO PROGRAM NAME LENGTH
         LA    R1,#SUBHOLD+28+8           POINT TO RETURN CODE
         LA    R2,#SUBHOLD+28+12          POINT TO REASON CODE
         LA    R3,#SUBHOLD+28+16          POINT TO ABEND CODE
         LA    R4,#CMXLIST                POINT TO PARAMETER LIST
         STM   R14,R4,#SUBHOLD            UPDATE IKJEFTSR PARM LIST
         OI    #SUBHOLD+24,X'80'          INDICATE THE END OF THE LIST
         MVI   #SUBHOLD+28+3,X'02'        A PROGRAM IS BEING INVOKED
         MVI   #SUBHOLD+28+4+3,8          PROGRAM NAME LENGTH IS 8
         SPACE 2
         L     R2,RECOVER
         LA    R1,COP7080                  RECOVERY ADDRESS
         ST    R1,RECOVER
         STM   R2,R8,#SUBHOLD+88           SAVE REGISTERS
         SPACE 1
         CLI   #CMXSHR,2                   SHR?
         BNE   COP7020                     NO, BRANCH
         SPACE 1
         CLI   RESERVET,C'R'               ALREADY RESERVED?
         BH    COP7020                     YES, BRANCH
         MVI   RESERVET,C'S'               MARK AS OUTPUT RESERVED
         SPACE 2
         MVC   PARMLIST(4),COPRESV         LIST FORM OF RESERVE
         RESERVE (COPEDIT,MSGDS2,E,44,SYSTEMS),RET=HAVE,              XX
               UCB=#SUBHOLD+48,MF=(E,PARMLIST)
         SPACE 1
         TM    FLAGSCC,RECFMU              LOAD LIBRARY?
         BNO   COP7020                     NO, RETURN
         TM    DS1SMSFG,DS1PDSE            PDSE DATASET?      DRK AUG02
         BO    COP7020                     YES, DON'T RESERVE DRK AUG02
         SPACE 1
         L     R1,#SUBHOLD+48              UCB ADDRESS
         TM    UCBTYP+1(R1),UCBRR          SHARED DEVICE?
         BNO   COP7000                     NO, BRANCH
         MVC   PARMLIST(4),COPRESV         LIST FORM OF RESERVE
         RESERVE (COPIEWLP,MSGDS2,E,44,SYSTEMS),RET=HAVE,             XX
               UCB=#SUBHOLD+48,MF=(E,PARMLIST)
         B     COP7020
         SPACE 1
COP7000  MVC   PARMLIST(4),COPHAVE         LIST FORM OF RESERVE
         ENQ   (COPIEWLP,MSGDS2,E,44,SYSTEM),RET=HAVE,MF=(E,PARMLIST)
COP7020  DS    0H
         SPACE 3
COPYRZAP LA    R0,3239  **TO BE ZAPPED    PASSWORD
         SVC   247      **TO BE ZAPPED    GET AUTHORIZED
         ORG   *-2      -- NOTE --        OVERLAY THE SVC CALL
         NOPR  0        **TO BE ZAPPED    OVERLAY THE SVC CALL
         LA    R3,##SUBCAL                WHO TO ATTACH
         LA    R1,#CMXLIST                PARAMETERS TO PASS
         CLC   ##SUBCAL(8),COPEFTSR       INVOKE IKJEFTSR?
         BNE   COPYNLNK                   NO, BRANCH
         LA    R1,#SUBHOLD                POINT TO THE PARAMETER LIST
         SPACE 2
COPYNLNK BAS   R2,ATTACH                  SPFCOPY/IEBCOPY/$PDSFAST
COPYLINK SR    R0,R0                      UNAUTHORIZATION CODE
         SVC   247      **TO BE ZAPPED    GET UNAUTHORIZED
         ORG   *-2      -- NOTE --        OVERLAY THE SVC CALL
         NOPR  0        **TO BE ZAPPED    OVERLAY THE SVC CALL
         SPACE 3
COP7080  LM    R2,R8,#SUBHOLD+88
         ST    R2,RECOVER                  RESET RECOVERY ADDRESS
         CLI   RESERVET,C'S'               RESERVED?
         BNE   COP7520                     NO, BRANCH
         MVI   RESERVET,0                  NO LONGER RESERVED
         SPACE 2
         MVC   PARMLIST(4),COPSYSS         LIST FORM, DEQ "SYSTEMS"
         DEQ   (COPEDIT,MSGDS2,44,SYSTEMS),RET=HAVE,                   X
               UCB=#SUBHOLD+48,MF=(E,PARMLIST)
         SPACE 2
         TM    FLAGSCC,RECFMU              LOAD LIBRARY?
         BNO   COP7520                     NO, RETURN
         SPACE 1
         L     R1,#SUBHOLD+48              UCB ADDRESS
         TM    UCBTYP+1(R1),UCBRR          SHARED DEVICE?
         BNO   COP7500                     NO, BRANCH
         MVC   PARMLIST(4),COPSYSS         LIST FORM, DEQ "SYSTEMS"
         DEQ   (COPIEWLP,MSGDS2,44,SYSTEMS),RET=HAVE,                  X
               UCB=#SUBHOLD+48,MF=(E,PARMLIST)
         B     COP7520
         SPACE 1
COP7500  MVC   PARMLIST(4),COPSYS          LIST FORM, DEQ "SYSTEM"
         DEQ   (COPIEWLP,MSGDS2,44,SYSTEM),RET=HAVE,MF=(E,PARMLIST)
COP7520  B     COP7530
         SPACE 1
COP7525  MVI   #COPNCAL,2                 NO CALL
COP7530  STAX  ,
         NI    ##ADRPA#,FF-$A             ALLOW ATTENTIONS AGAIN
         MVI   ATTNECB,0                  CLEAR THE ATTENTION ECB
         SPACE 1
         LA    R1,#CMXDDIN                SYSIN DDNAME
         BAS   R2,COP8580                 FREE THIS DATA SET
         SPACE 1
         LA    R1,#CMXDDT3                SYSUT3 DDNAME
         BAS   R2,COP8580                 FREE THIS DATA SET
         SPACE 1
         LA    R1,#CMXDDT4                SYSUT4 DDNAME
         BAS   R2,COP8580                 FREE THIS DATA SET
         SPACE 1
         MVC   DOUBLE(8),#CMXDDT2         COPY DDNAME
         CLC   DOUBLE(3),=CL8'SYS00000'   TEMPORARY TYPE DDNAME?
         BNE   COP7540                    NO, BRANCH
         NC    DOUBLE(8),=CL8'SYS00000'   MASK WITH "SYS00000"
         CLC   DOUBLE(8),=CL8'SYS00000'   TEMPORARY TYPE DDNAME?
         BNE   COP7540                    NO, BRANCH
         LA    R1,#CMXDDT2                SYSUT2 DDNAME
         BAS   R2,COP8580                 FREE THIS DATA SET
         SPACE 1
COP7540  CLC   DDNAME(8),#CMXDDT1         EXPLICIT FREE NEEDED?
         BE    COP7550                    NO, BRANCH
         LA    R1,#CMXDDT1                SYSUT1 DDNAME
         BAS   R2,COP8580                 FREE THIS DATA SET
         SPACE 1
COP7550  CLI   #COPNCAL,2                 NOCALL?
         BE    COP8550                    YES, BRANCH
         CLC   ##SUBCAL(8),COPEFTSR       INVOKE IKJEFTSR?
         BNE   COP7570                    NO, BRANCH
         MVC   SUBSECB+3(1),#SUBHOLD+28+11  COPY FINAL RETURN CODE
         CLI   SUBSECB+3,8                RC:8                DRK FEB13
         BL    COP7570                    LOW, VALID COPY
         BE    *+12                       EQUAL, ATTENTION
         CLI   SUBSECB+3,X'FF'            RETURN CODE = FF?  (-1)
         BNE   COP7570                    NO, BRANCH
         M$MSG L980                       ATTENTION WITH IKJEFTSR
         B     COP8000                    READ SYSPRINT       DRK FEB13
         SPACE 1                            FOR ABEND CODE    DRK FEB13
COP7570  CLI   SUBSECB+3,8                RETURN CODE >  8?   DRK FEB13
         BH    COP7580                    YES, BRANCH
         CLI   #CMXFLAG,3                 NOLIST REQUESTED?
         BNE   COP8000                    NO, BRANCH
COP7580  MVI   MTLEN+4,4                                      DRK FEB13
         SR    R15,R15
         ICM   R15,B'0001',SUBSECB+3
         CVD   R15,DOUBLE
         MVC   INSERT#1(8),##SUBCOM
         MVC   INSERT#2(4),=X'40202120'                       DRK FEB13
         ED    INSERT#2(4),DOUBLE+6                           DRK FEB13
         MVI   INSERT#2,C'='                                  DRK FEB13
         M$MSG L171$2
         B     COP8550
*        NOP   COP8550                      FOR DEBUGGING     DRK FEB13
         SPACE 3
COP8000  MVC   BAMDCB(COPDL),COPDCB         GET MODEL DCB
         MVC   DCBDDNAM-IHADCB+BAMDCB(8),#CMXDDPR
         MVI   OPENLIST,X'80'
         OPEN  (BAMDCB),MF=(E,OPENLIST)     OPEN SYSPRINT
         TM    DCBOFLGS-IHADCB+BAMDCB,X'10' SUCCESSFUL OPEN?
         BO    COP8010                      YES, BRANCH
         M$MSG L865                         NO STATUS AVAILABLE
         B     COP8550                      EXIT
         SPACE 3
*  PROCESS SYSPRINT RECORDS
         SPACE 1
COP8010  GET   BAMDCB                     GET LIST RECORD
         CLI   2(R1),X'40'                HEADER LINE?        DRK FEB01
         BNE   COP8040                    NO, BRANCH
         CLI   22(R1),X'40'               HEADER LINE?
         BE    COP8010                    YES, IGNORE
COP8040  CLI   #CMXFLAG,2                 FULL LIST REQUESTED?
         BE    COP8080                    YES, OUTPUT
         LA    R3,COPEXCL-6               START OF EXCLUSION TABLE -6
COP8060  LA    R3,6(,R3)                  NEXT MESSAGE ID
         CLC   1(6,R1),0(R3)              MESSAGE ID TO BE EXCLUDED?
         BE    COP8010                    YES, IGNORE
         CLI   0(R3),FF                   END OF TABLE?
         BNE   COP8060                    NO, LOOP
         SPACE 3
COP8080  MVC   MSGTEXT1(136),MSGBL132     BLANK THE MESSAGE LINE
*        CLC   1(6,R1),COPIEB14           UNUSED TRACKS MESSAGE?
*        BNE   *+8                        NO, BRANCH
*        MVI   MSGTEXT1+1,64              YES, TRUNCATE THE MESSAGE
         CLC   1(27,R1),IEBGENS           IEBGENER START?     DRK APR16
         BNE   COP8080A                   NO                  DRK APR16
         MVC   MSGTEXT1+4(8),IEBGEN0      YES, ADD IEBG00I    DRK APR16
         MVC   MSGTEXT1+4+8(112),1(R1)                        DRK APR16
         B     COP8090                                        DRK APR16
COP8080A DS    0H
         CLC   1(27,R1),IEBGENE           IEBGENER END?       DRK APR16
         BNE   COP8080B                   NO                  DRK APR16
         MVC   MSGTEXT1+4(8),IEBGEN9      YES, ADD IEBG99I    DRK APR16
         MVC   MSGTEXT1+4+8(112),1(R1)                        DRK APR16
         B     COP8090                                        DRK APR16
COP8080B DS    0H
         MVC   MSGTEXT1+4(120),1(R1)      ADD THE IEBCOPY MESSAGE
         CLC   MSGTEXT1+4+1(10),BLANK128  FIRST 10 BLANKS?
         BNE   *+10                       NO, BRANCH
         MVC   MSGTEXT1+4(110),11(R1)     SHIFT THE IEBCOPY MESSAGE
COP8090  DS    0H
         M$ERRST MSGTEXT1                 OUTPUT THE MESSAGE
         B     COP8010
         SPACE 3
COPEXCL  DC    C'IEB154'           IEB154I MEMBERNAME HAS BEEN SUCCESSF
         DC    C'IEB155'           IEB155I MEMBERNAME HAS BEEN SUCCESSF
         DC    C'IEB167'           IEB167I FOLLOWING MEMBER(S) COPIED F
         DC    C'IEB161'           IEB161I COMPRESS TO BE DONE USING IN
         DC    C'PDSFAS'           PDSFAST   ... HEADER LINE
         DC    C'PDF00 '           PDF00  START EXECUTION
         DC    C'PDF06 '           PDF06   INPUT DATASET:  ...
         DC    C'PDF09 '           PDF09  MEMBER ........ - MOVED ...
         DC    X'FF'
         SPACE 2
*** THE FOLLOWING MESSAGES ARE ALSO CANDIDATES FOR SUPPRESSION:
COPIEB14 DC    C'IEB144'           IEB144I THERE ARE XXXXXXX UNUSED TRA
***      DC    C'IEB147'           IEB147I END OF JOB X WAS HIGHEST RET
***      DC    C'IEB149'           IEB149I THERE ARE XXXXXXX UNUSED DIR
         DC    X'FF'
IEBGENS  DC    CL27'DATA SET UTILITY - GENERATE'
IEBGENE  DC    CL27'PROCESSING ENDED AT EOD'
IEBGEN0  DC    CL8'IEBG00I'
IEBGEN9  DC    CL8'IEBG99I'
         SPACE 2
COP8500  DS    0H
         MVI   OPENLIST,X'80'
         CLOSE (BAMDCB),MF=(E,OPENLIST)   CLOSE THE SYSPRINT FILE.
         LA    R1,BAMDCB
         FREEPOOL (1)
         SPACE 2
COP8550  M$ERRST MSGBLANK
         LA    R1,#CMXDDPR                SYSPRINT DDNAME
         BAS   R2,COP8580                 FREE THIS DATA SET
         B     NEWSTAX
         SPACE 1
COP8580  CLI   0(R1),X'41'                ANY DDNAME ALLOCATED?
         BLR   R2                         NO, RETURN
         MVC   #CMXSYS3+6(8),0(R1)        DDNAME TO FREE
         XC    M99RBPTR(40),M99RBPTR      CLEAR THE WORK AREA
         LA    R1,M99RBPTR                DYNAMIC ALLOCATION
         USING S99RBP,R1
         SPACE 1
         LA    R4,M99RB
         USING S99RB,R4
         ST    R4,S99RBPTR
         OI    S99RBPTR,S99RBPND          MARK END OF LIST
         SPACE 1
         LA    R5,M99TUPL
         USING S99TUPL,R5
         MVI   S99RBLN,20                 LENGTH 20
         MVI   S99VERB,S99VRBUN           UNALLOCATE
         ST    R5,S99TXTPP                POINT TO TEXT POINTER
         SPACE 1
         LA    R3,#CMXSYS3                TEXT POINTER
         USING S99TUNIT,R3
         MVC   S99TUKEY(6),COPFDDNA       ADD TEXT KEYS
         ST    R3,S99TUPTR                POINT TO TEXT UNIT
         LA    R0,COPUNALC                UNALLOC EVEN IF "PERM"
         ST    R0,S99TUPTR+4              ADD THIS ELEMENT TOO
         OI    S99TUPTR+4,X'80'           MARK END OF THE TEXT LIST
         SPACE 1
         DYNALLOC
         BR    R2
         SPACE 1
         DROP  R1,R3,R4,R5
         SPACE 2
         B     RESTART0
         SPACE 2
         USING IHADCB,R1
*** DCB OPEN EXIT
***8980  CLC   DCBDSORG(2),=F'0'             ANY DSORG?
***      BNE   *+8                           YES, BRANCH
***      OI    DCBDSORG,DS1DSGPO             NO, USE DSORG=PO
COP8980  CLI   DCBRECFM,0                    ANY RECFM?
         BNE   *+10                          YES, BRANCH
         MVC   DCBRECFM(1),DCBRECFM-IHADCB+INDCB
         CLC   DCBLRECL(2),=F'0'             ANY LRECL?
         BNE   *+10                          YES, BRANCH
         MVC   DCBLRECL(2),LRECL             NO, USE INPUT DEFAULT
         CLC   DCBBLKSI(2),=F'0'             ANY BLKSIZE?
         BNE   *+10                          YES, BRANCH
         MVC   DCBBLKSI(2),BLKSI             NO, USE INPUT DEFAULT
         BR    R14
         DROP  R1
         SPACE 2
COPYNULL BR    R14
         SPACE 2
*  COPY CONSTANTS
         PRINT NOGEN
COPDCB   DCB   DSORG=PS,DDNAME=COP,MACRF=(GL,PM),EODAD=COP8500
COPDL    EQU   *-COPDCB
         SPACE 2
COPDCBPO DCB   DSORG=PO,DEVD=DA,DDNAME=X,MACRF=(R,W),EXLST=COP8990
LCOPDCB  EQU   *-COPDCBPO
COP8990  DC    0F'0',X'85',AL3(COP8980)      OPEN EXIT ONLY
         PRINT GEN
         SPACE 2
L983     DC    C'983'                                         DRK MAY18
L984     DC    C'984'                                         DRK MAY18
COPLFIL5 DC    C'FILE('
COPLFIL4 DC    C'FIL('
COPLFIL3 DC    C'FI('
COPLFIL2 DC    C'F('
COPYDCB  DC    CL8'COPYDCB'
COPMEMBR DC    X'0003000100084040404040404040'     MEMBER NAME
COPDSNAM DC    X'00020001002C'                     DSNAME
COPALLOC DC    X'00040001000101'                   DISP=(_)
COPNORM  DC    X'00050001000102'                   DISP=(,CATLG)
COPANORM DC    X'00060001000104'                   DISP=(,,DELETE)
COPTRK   DC    X'00070000'                         SPACE=(TRK,
COPFIRST DC    X'000A00010003000001'               SPACE=(,1
COPSECON DC    X'000B00010003000004'               SPACE=(,,4
COPDIR   DC    X'000C00010003000005'               SPACE=(,,,5)
*COP2PDS DC    X'80120001000140'                   DSNTYPE=PDS
COP2PDSE DC    X'80120001000180'                   DSNTYPE=LIBRARY
COPVER2  DC    X'802C0001000102'                   PDSE VERSION 2
COPMAXG  DC    X'802D0001000400000000'             MAXGENS
COPOOLD  EQU   X'01'
COPONEW  EQU   X'04'
COPOSHR  EQU   X'08'
         SPACE 1
COPEFTSR DC    CL8'IKJEFTSR'
COPCOPY  DC    CL8'IEBCOPY'
COPDDNAM DC    X'0055000100084040404040404040'   RETURN DDNAME
COPMEMBE DC    X'0003000100084040404040404040'   MEMBER NAME
COPFDDNA DC    X'000100010008'                   FOR FREE DDNAME
COPDSORG DC    X'0057000100024040'               RETURN DSORG
COPDUMMY DC    X'00240000'                       DUMMY DATA SET
COPNEW   DC    X'00040001000104'                 DISP=(NEW)
COPDEL   DC    X'00050001000104'                 DISP=(,DELETE)
COPCDEL  DC    X'00060001000104'                 DISP=(,,DELETE)
COPPERM  DC    X'00520000'                       PERMANENT ALLOCATION
COPUNALC DC    X'00070000'                       UNALLOCATE PERMANENT
COPVOLSR DC    X'001000010006404040404040'       VOL=SER=
         SPACE 2
COPPRTFI DC    X'000A00010003',AL3(005)          SPACE=(,NNN)
COPPRTSE DC    X'000B00010003',AL3(005)          SPACE=(,(,NNN))
COPPRTIN DC    X'000900010003',AL3(6171)         SPACE=(6171)
COPSINSP DC    X'000900010003',AL3(3120)         SPACE=(3120)
COPUT3IN DC    X'000900010003',AL3(80)           SPACE=(80)
COPUNIT  DC    X'001500010008',CL8'&TUNIT'       UNIT=SYSDA    SS JUL87
COPUNITP DC    X'001500010008',CL8'&PUNIT'       UNIT=SYSALLDA BL MAY88
         SPACE 1
COPPRTRE DC    X'004900010001',XL1'94'           DCB=RECFM=FBA
COPPRTLR DC    X'004200010002',AL2(121)          DCB=LRECL=121
COPPRTBL DC    X'003000010002',AL2(6171)         DCB=BLKSIZE=6171
         SPACE 2
COPSINRE DC    X'004900010001',XL1'90'           DCB=RECFM=FB
COPSINLR DC    X'004200010002',AL2(80)           DCB=LRECL=80
COPSINBL DC    X'003000010002',AL2(3120)         DCB=BLKSIZE=3120
         SPACE 2
COPDUMMA DC    A(COPDUMMY)
COPSYSIN DC    A(COPPRTFI,COPPRTSE,COPSINSP)
         DC    A(COPSINRE,COPSINLR,COPSINBL)
         DC    A(COPNEW,COPDEL,COPCDEL),X'80',AL3(COPPERM)
COPSYSU4 DC    A(COPUNIT)
COPSYSUT DC    A(COPPRTFI,COPPRTSE,COPUT3IN)
         DC    A(COPNEW,COPDEL),X'80',AL3(COPCDEL)
COPSYSPR DC    A(COPPRTFI,COPPRTSE,COPPRTIN)
         DC    A(COPPRTRE,COPPRTLR,COPPRTBL)
         DC    A(COPNEW,COPDEL,COPCDEL),X'80',AL3(COPPERM)
COPGENER DC    CL8'IEBGENER'
COPDSN1M DC    C' COPY    O='
COPCOPMO DC    C' COPYMOD'
COPDSN2M DC    C',I='
COPLISTN DC    C',LIST=NO'
COPMAXBL DC    C',MAXBLK='
COPMEM1M DC    C' S M='
COPDSNLP DC    C'(('
COPDSNRP DC    C',R))'
         SPACE 2
PDS484W  DC    C'PDS484W COPY SHOULD NOT BE INTERRUPTED'
PDS484WL EQU   *-PDS484W
         SPACE 2
COPHAVE  ENQ   (*-*,*-*,E,*-*,SYSTEM),RET=HAVE,MF=L
         ORG   COPHAVE+4
         SPACE 7
COPRESV  RESERVE (*-*,*-*,E,*-*,SYSTEMS),RET=HAVE,UCB=0,MF=L
         ORG   COPRESV+4
         SPACE 3
COPSYSS  DEQ   (*-*,*-*,*-*,SYSTEMS),RET=HAVE,UCB=0,MF=L
         ORG   COPSYSS+4
         SPACE 3
COPEDIT  DC    CL8'SPFEDIT'               FOR SPF MEMBER ENQ TEST
COPIEWLP DC    CL8'SYSIEWLP'              FOR LINKAGE-EDITOR RESERVE
COPSYS   DEQ   (*-*,*-*,*-*,SYSTEM),RET=HAVE,MF=L
         ORG   COPSYS+4
         SPACE 2
LOCATEC  CAMLST NAME,0,,0                                     DRK APR16
         ORG   LOCATEC+4                                      DRK APR16
#COPASSP EQU   X'80'            COPY: ASSOCIATES PHASE
#COPSEQ  EQU   X'40'            COPY: USE IEBGENER FOR THE COPY
#COPMEMB EQU   X'20'            COPY: DSNAME(MEMBER) FORM WAS ENTERED
#COPONE  EQU   X'10'            COPY: A SINGLE MEMBER
#COPSOME EQU   X'08'            COPY: SOME MEMBERS EXIST/NOEXIST
#COPDUP  EQU   X'04'            COPY: A COLLISION WAS FOUND
#COPASS2 EQU   X'01'            COPY: COPY ASSOCIATES TOO
DEBNMEXT EQU   16           OFFSET IN DEB FOR NUMBER OF EXTENTS
DEBUCBAD EQU   32           OFFSET IN DEB FOR UCB ADDRESS
UCBTYP   EQU   16           OFFSET IN UCB FOR UNIT TYPE WORD
UCBRR    EQU   X'20'        UCB IS ON A SHARED DASD DEVICE
UCBVOLI  EQU   28           OFFSET IN UCB FOR DISK VOLUME NAME
         DROP  R6                                             DRK OCT98
