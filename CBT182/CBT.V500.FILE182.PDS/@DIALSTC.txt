         TITLE 'LISTC/LISTFILE FUNCTION'                       SS OCT88
*************************************************************  SS OCT88
*                                                              SS OCT88
*      LISTC/LISTFILE FUNCTION                                 SS OCT88
*                                                              SS OCT88
*************************************************************  SS OCT88
         SPACE 2                                               SS OCT88
ISPLISTC DS    0H                                              SS OCT88
         USING *,R4                                            SS OCT88
         BAS   R2,ISPSETST              SET STATS PREV TABLE   SS OCT88
         CLI   SPFFUNST+5,C'P'          LISTC   PENDING PROC   SS OCT88
         BE    ISPLSC5                  YES, SET TO REPROCESS  SS OCT88
ISPLSCAN DS    0H                       *                      SS OCT88
         L     R15,SPFOPER      FIRST BLANK AFTER CMD          SS OCT88
ISPLSCRT DS    0H                       *                      SS OCT88
         LA    R14,30           MAX LENGTH                     SS OCT88
         LA    R1,ISPLSCPR      PARSE LIST                     SS OCT88
         BAS   R2,ISPPARSI      PARSE IT                       SS OCT88
         C     R15,ISPL8        RETURN CODE                    SS OCT88
         BH    ISPLSCE1         BAD KEYS                       SS OCT88
         TM    SPFPRBI1,X'30'     CONFLICTING REQUESTS         SS OCT88
         BO    ISPLSCE2           YES GIVE MESSAGE             SS OCT88
         CLI   SPFFUNST+5,C'I'    INACTIVE                     SS OCT88
         BE    ISPLSC20           YES   OPEN THE TABLE         SS OCT88
         CLI   SPFTBPFI+1,5       ALREADY AT LISTC             SS OCT88
         BE    ISPLSC05           THEN CHECK FOR OTHER         SS OCT88
ISPLSC1  DS    0H                                              SS OCT88
         LH    R1,SPFFUNLP+10     GET LAST PANEL               SS OCT88
         LNR   R1,R1                    *                      SS OCT88
         BAS   R2,ISPSTKI         SWAP PANEL                   SS OCT88
ISPLSC05 DS    0H                       *                      SS OCT88
         CLI   SPFPRRC,8          ANY OPERANDS?                SS OCT88
         BNE   ISPLSC10           YES, CHECK REQUESTS          SS OCT88
ISPLSC40 DS    0H                       *                      SS OCT88
         LA    R15,40             DISPLAY IF NO MORE CMDS      SS OCT88
         BR    R3                 TO ROUTER                    SS OCT88
ISPLSC10 DS    0H                       *                      SS OCT88
         TM    SPFPRBI1,X'08'     RESET REQUEST                SS OCT88
         BZ    ISPLSC30           NO, CHECK OTHER REQUESTS     SS OCT88
* RESET TB IF OPEN                                             SS OCT88
         BAS   R2,ISPTBEND                                     SS OCT88
ISPLSC20 DS    0H                                              SS OCT88
         BAS   R2,ISPLSCOI        GO CREATE AND OPEN TABLE     SS OCT88
ISPLSC30 DS    0H                                              SS OCT88
         TM    SPFPRBI1,X'20'     VTOC  REQUEST                SS OCT88
         BO    ISPVTOC            YES GO FOR IT                SS OCT88
         TM    SPFPRBI1,X'10'     DSAT  REQUEST                SS OCT88
         BO    ISPDSAT            YES GO FOR IT                SS OCT88
         TM    SPFPRBI1,X'01'     V2    REQUEST                SS OCT88
         BO    ISPVTC2            YES GO FOR IT                SS OCT88
         TM    SPFPRBI1,X'02'     VV    REQUEST                SS OCT88
         BO    ISPVTC3            YES GO FOR IT                SS OCT88
         BAS   R2,ISPQUERY              *                      SS OCT88
         ICM   R1,15,SPFTSIZE     GET SIZE OF TABLE            SS OCT88
         BNZ   ISPLSC40           HAVE TABLE        DISPLAY    SS OCT88
         MVC   SPFMSGID,ISPBLK8   NO MESSAGE                   SS OCT88
         B     ISPLSCE1           PROMPT FOR REQUEST           SS OCT88
ISPLSCE3 DS    0H                       *                      SS OCT88
* LONG MESSAGE MUST ALREADY BE IN SPFMLIN2                     SS OCT88
         MVC   SPFMLIN1,ISPBLK8   NO SHORT MESSAGE             SS OCT88
         MVC   SPFMSGID,ISPPDSM   SET MESSAGE                  SS OCT88
         MVI   SPFMSGID+7,C'A'    ACTION MESSAGE               SS OCT88
         MVI   SPFMSGID+6,C'1'    GENERAL PURPOSE MESSAGE     DRK SEP01
         B     ISPLSCE1           PROMPT FOR DATA              SS OCT88
ISPLSCE2 DS    0H                       *                      SS OCT88
         MVC   SPFMSGID,ISPPDSM   SET MESSAGE                  SS OCT88
         MVI   SPFMSGID+7,C'A'    ACTION MESSAGE               SS OCT88
         MVI   SPFMSGID+6,C'7'    CONFLICT ERROR              DRK SEP01
         B     ISPLSCE1           PROMPT FOR DATA              SS OCT88
ISPLSCE1 DS    0H                                             GLA JUN88
         MVC   SPFVZCMD(250),SPFINPUT COPY BAD COMMAND        GLA JUN88
         LA    R15,ISPLSCPP       LISTC PROMPT PANEL OFFSET    SS OCT88
         LA    R1,SPFMSGID        SET FOR MESSAGE             GLA JUN88
         BAS   R2,ISPSINGI        DISPLAY PANEL               GLA JUN88
         MVI   SPFMSGID,C' '      CLEAR MESSAGE ID            GLA JUN88
         C     R15,ISPL8          END KEY                     GLA JUN88
         BE    ISPLSC40           DISPLAY LAST ACTIVE TABLE    SS OCT88
         MVC   SPFINPUT(250),SPFZSEL    COPY                  GLA JUN88
         LA    R15,SPFINPUT             STARTING ADDRESS      GLA JUN88
         LA    R14,30                   LENGTH                GLA JUN88
         LA    R1,C' '                  BLANK                 GLA JUN88
         BAS   R2,ISPCLI                FIND BLANK AFTER CMD  GLA JUN88
         B     ISPLSCRT                 RETRY                 GLA JUN88
ISPLSC5  DS    0H                       *                     GLA JUN88
         L     R15,SPFOPER              BLANK AFTER CMD        SS FEB89
         LA    R14,30                   SCAN FOR OPERAND       SS FEB89
         BAS   R2,ISPCKBLK                                     SS FEB89
         LTR   R15,R15                  ANY                    SS FEB89
         BNZ   ISPLSC1B                 YES, CANNOT PROCESS    SS FEB89
         LH    R1,SPFFUNLP+10     GET LAST PANEL               SS OCT88
         LNR   R1,R1                    *                     GLA JUN88
         BAS   R2,ISPSTKI         SWAP PANEL                  GLA JUN88
         LA    R15,36             GO TO PENDING ACT PROC      GLA JUN88
         BR    R3                                             GLA JUN88
ISPLSC1B DS    0H                       *                      SS FEB89
         MVC   SPFMSGID,ISPPDSM         GENERIC MESSAGE        SS FEB89
         MVI   SPFMSGID+6,C'1'          PDSM..1B MESSAGE       SS FEB89
         MVI   SPFMSGID+7,C'B'          BUSY MSG               SS FEB89
         LA    R15,100                  TO DISPLAY             SS FEB89
         BR    R3                       RETURN TO ROUTER       SS FEB89
ISPLSCPP DC    CL8'&P.LCLTC'      LISTC PROMPT PANEL           SS OCT88
ISPLSCPR DS    0F                                              SS OCT88
         DC    X'32',X'080000',CL8'RESET'                     DRK JUL98
         DC    X'33',X'200000',CL8'VTOC'                       SS OCT88
         DC    X'21',X'010000',CL8'V2'     *TEST*              SS OCT88
         DC    X'21',X'020000',CL8'VV'     *TEST*              SS OCT88
         DC    X'33',X'100000',CL8'DSAT'                       SS OCT88
         DC    X'FF'                                           SS OCT88
         SPACE 2
ISPVTOC  DS    0H                       *                     GLA JUN88
         SPACE 2                                              GLA JUN88
*  DISPLAY PANEL TO GET VTOC COMMAND OPTIONS                  GLA JUN88
         SPACE 1                                              GLA JUN88
         LA    R1,ISPVTCNM    GET VTOC COMMAND NAME            SS OCT88
         BAS   R2,ISPXLNKI    CHECK IF COMMAND EXISTS          SS OCT88
         LTR   R15,R15        RC WILL TELL                     SS OCT88
         BNZ   ISPLSCE1       GIVE MESSAGE                     SS OCT88
         LA    R0,VX070              START OF WRITE SUBROUTINE SS OCT88
         ST    R0,WORKTBL+12         OPERAND OFFSET            SS OCT88
         LA    R15,ISPVTCPP   VTOC PROMPT PANEL                SS OCT88
         B     ISPLSCPT                 *                      SS OCT88
ISPVTC2  DS    0H                       *                     GLA JUN88
         SPACE 2                                              GLA JUN88
*  DISPLAY PANEL TO GET VTOC COMMAND OPTIONS                  GLA JUN88
         SPACE 1                                              GLA JUN88
         LA    R1,ISPVTCNM    GET VTOC COMMAND NAME            SS OCT88
         BAS   R2,ISPXLNKI    CHECK IF COMMAND EXISTS          SS OCT88
         LTR   R15,R15        RC WILL TELL                     SS OCT88
         BNZ   ISPLSCE1       GIVE MESSAGE                     SS OCT88
         LA    R0,VX070              START OF WRITE SUBROUTINE SS OCT88
         ST    R0,WORKTBL+12         OPERAND OFFSET            SS OCT88
         LA    R15,ISPVTCP2   VTC2 PROMPT PANEL                SS OCT88
         B     ISPLSCPT                 *                      SS OCT88
ISPVTC3  DS    0H                       *                     GLA JUN88
         SPACE 2                                              GLA JUN88
*  DISPLAY PANEL TO GET VTOC COMMAND OPTIONS                  GLA JUN88
         SPACE 1                                              GLA JUN88
         LA    R1,ISPVTCNM    GET VTOC COMMAND NAME            SS OCT88
         BAS   R2,ISPXLNKI    CHECK IF COMMAND EXISTS          SS OCT88
         LTR   R15,R15        RC WILL TELL                     SS OCT88
         BNZ   ISPLSCE1       GIVE MESSAGE                     SS OCT88
         LA    R0,VX070              START OF WRITE SUBROUTINE SS OCT88
         ST    R0,WORKTBL+12         OPERAND OFFSET            SS OCT88
         LA    R15,ISPVTCP3   VTC2 PROMPT PANEL                SS OCT88
         B     ISPLSCPT                 *                      SS OCT88
ISPDSAT  DS    0H                       *                      SS OCT88
         SPACE 2                                               SS OCT88
*  DISPLAY PANEL TO GET DSAT COMMAND OPTIONS                   SS OCT88
         SPACE 1                                               SS OCT88
         LA    R1,ISPDSTNM    GET DSAT COMMAND NAME            SS OCT88
         BAS   R2,ISPXLNKI    CHECK IF COMMAND EXISTS          SS OCT88
         LTR   R15,R15        RC WILL TELL                     SS OCT88
         BNZ   ISPLSCE1       GIVE MESSAGE                     SS OCT88
         LA    R0,DX070              START OF WRITE SUBROUTINE SS OCT88
         ST    R0,WORKTBL+12         OPERAND OFFSET            SS OCT88
         LA    R15,ISPDSTPP   DSAT PROMPT PANEL                SS OCT88
         B     ISPLSCPT                 *                      SS OCT88
*                                                              SS OCT88
* SUBROUTINE TO CHECK COMMAND NAME EXISTS IN LINKLIB OR LPALIB SS OCT88
*                                                              SS OCT88
*  R1 = ADDRESS OF NAME                                        SS OCT88
*  R15 RETURNED   0=THERE  4=NOT THERE                         SS OCT88
*                                                              SS OCT88
ISPXLNKI DS    0H                       *                      SS OCT88
ISPXLINK DS    0H                       *                      SS OCT88
         MVC   DIRNAME,0(R1)                                   SS OCT88
         SPACE 2                                              GLA JUN88
*** IN A LINKLIST LIBRARY?                                    GLA JUN88
         BLDL  0,BLDLLIST ----+            CHECK FOR A LOAD MODULEJUN88
         LA    R1,X'100'      |            TTR=1 (FOR SEQUENTIAL) JUN88
         ST    R1,DIRTTR      |            RESET TTR          GLA JUN88
         LTR   R15,R15    <---+            IN THE LINKLIST?   GLA JUN88
         BZR   R2                          YES, OK             SS OCT88
         SR    R15,R15                     ASSUME IN LPA       SS FEB90
         L     R1,CVTPTR(,R0)              NEED CHECK OF LPA   SS OCT88
         L     R1,CVTLPDIA-CVTMAP(R1)      POINT TO LINK PACK DIRECTORY
         SPACE 2                                              GLA JUN88
*** MODULE NOT IN LINKLIST, TRY THE IN-STORAGE LPALIB DIRECTORYLA JUN88
ISPXLNK1 CLC   8(8,R1),DIRNAME             THIS MODULE NAME?   SS OCT88
         BER   R2                          YES, OK             SS OCT88
         CLI   8(R1),X'FF'                 END OF DIRECTORY?   SS OCT88
         LA    R1,40(,R1)                                     DRK SEP01
         BNE   ISPXLNK1                    NO, BRANCH          SS OCT88
         MVC   SPFMLIN1,ISPBLK8   NO SHORT MESSAGE             SS OCT88
         MVC   SPFMLIN2,ISPXLNKM  MESSAGE MODEL                SS OCT88
         MVC   SPFMLIN2+8(4),DIRNAME  INSERT NAME             DRK SEP01
         MVC   SPFMSGID,ISPPDSM   SET MESSAGE                  SS OCT88
         MVI   SPFMSGID+7,C'A'    ACTION MESSAGE               SS OCT88
         MVI   SPFMSGID+6,C'1'    GENERAL PURPOSE MESSAGE     DRK SEP01
         LA    R15,4                       DID NOT FIND        SS OCT88
         BR    R2                                              SS OCT88
         SPACE 3                                              GLA JUN88
ISPXLNKM DC    CL80'COMMAND 1234 WAS NOT FOUND'               DRK SEP01
ISPLSCPT DS    0H                       *                      SS OCT88
         NI    SPFLAG5,255-SPFVT0C-SPFVCAT CLEAR INIT FLAGS   DRK OCT99
         MVI   SPFVZCMD,C' '                                  GLA JUN88
         MVC   SPFVZCMD+1(249),SPFVZCMD                       GLA JUN88
         LA    R1,ISPBLK8     NO MESSAGE                      GLA JUN88
         BAS   R2,ISPSINGI    PUT UP PANEL                    GLA JUN88
         LTR   R15,R15        ANY END COMMAND                 GLA JUN88
         BNZ   ISPLSC40       IF SO THEN EXIT                  SS OCT88
         MVC   SPFZSEL(4),ISPVTCBF GET BUFFER LEN/OFFSET       SS OCT88
*        CLC   SPFZSEL+235(8),ISPBLK8   OPERANDS TO LONG       SS OCT88
*        CLC   SPFZSEL+235(8),ISPBLK8   OPERANDS TO LONG       SS OCT88
         MVC   SPFZSEL+235(15),ISPVTCPC  PDSCALL OPTION        SS OCT88
         LA    R1,WORKTBL                INTERFACE SUBROUTINE  SS OCT88
         STCM  R1,15,SPFZSEL+245         PUT ADDR IN OPTION    SS OCT88
         LA    R1,SPFZSEL                  NEW COMMAND BUFFER GLA JUN88
         ST    R1,ADDRTEXT                 POINT TO THE COMMAND BUFFER"
*  BUILD PARM BLOCKS TO INVOKE COMMAND                        GLA JUN88
         SPACE 1                                              GLA JUN88
         L     R14,ADDRECT                                    GLA JUN88
         MVC   ECTPCMD-ECT(8,R14),ECTSCMD-ECT(R14)  PRIMARY   CMD NAME8
         MVC   ECTSCMD-ECT(8,R14),ISPBLK8           SECONDARY  SS OCT88
         LA    R1,ADDRTEXT                 CPPL START         GLA JUN88
         SPACE 2                                              GLA JUN88
         LA    R15,WORKTBL           START OF PARAMETER LIST  GLA JUN88
         MVC   0(12,R15),VX060       SAVE RE-ENTRY CODE       GLA JUN88
* WRITER SUBR ALREADY AT WORKTBL+12     *                      SS OCT88
         ST    R7,16(,R15)           SAVE WORK AREA ADDRESS   GLA JUN88
         LA    R14,DIRNAME           COMMAND NAME              SS OCT88
         SPACE 2                                              GLA JUN88
         XC    PARMLIST(8),PARMLIST  INITIALIZE LINK LIST     GLA JUN88
         LINK  EPLOC=((14)),SF=(E,PARMLIST)                   GLA JUN88
         L     R14,ADDRECT                                    GLA JUN88
         MVC   ECTSCMD-ECT(8,R14),ECTPCMD-ECT(R14)  SECONDARY CMD NAME8
         MVC   ECTPCMD-ECT(8,R14),PDSNAME           PRIMARY   CMD NAME8
         B     ISPLSC40             RETURN TO DISPLAY          SS OCT88
         SPACE 3                                              GLA JUN88
ISPVTCPP DC    CL8'&P.LCVTC'  LISTC VTOC PROMPT PANEL          SS OCT88
ISPVTCP2 DC    CL8'&P.LCVT2'  LISTC VTOC PROMPT PANEL *TEST*   SS OCT88
ISPVTCP3 DC    CL8'&P.LCVTV'  LISTC VTOC PROMPT PANEL FROM LV  SS OCT88
ISPVTCNM DC    CL8'&NVTO'     VTOC COMMAND NAME                SS OCT88
ISPDSTPP DC    CL8'&P.LCDST'  LISTC DSAT PROMPT PANEL          SS OCT88
ISPDSTNM DC    CL8'&NDSA'     DSAT COMMAND NAME                SS OCT88
ISPVTCPC DC    C'  PDSCALL(XXXX)'    PDS CALLER IDENTIFIER     SS OCT88
ISPVTCBF DC    H'235',H'4'           BUF LEN AND OFFSET        SS OCT88
         SPACE 1                                              GLA JUN88
VX060    ST    R1,20(,R15)         0 REENTER: SAVE PARAMETER ADDR JUN88
         L     R15,12(,R15)        4 REENTER: GET ROUTINE ADDRESS JUN88
         BAS   R1,0(,R15)          8 REENTER: LINK TO SUBROUTINE  JUN88
***      DS    A                  12 REENTER: A(ROUTINE)      GLA JUN88
***      DS    A                  16 REENTER: A(WORK AREA)    GLA JUN88
***      DS    A                  20 REENTER: A(MESSAGE)      GLA JUN88
         SPACE 3                                              GLA JUN88
*  BAD OPERANDS ON COMMAND                                    GLA JUN88
         SPACE 3                                              GLA JUN88
         SPACE 3                                              GLA JUN88
         DROP  R4                                             DRK OCT98
*#*#*#*#*#*#*#*#                                              GLA JUN88
*   PARSE LINES COMING BACK FROM VTOC                         GLA JUN88
* TO GET  DSN (SPFADSN)  VOL (SPFAVOL)  DSO (SPFATYPE)        GLA JUN88
*           44 BYTES       6 BYTES        4 BYTES             GLA JUN88
*#*#*#*#*#*#*#*#                                              GLA JUN88
VX070    SAVE  (14,12)                                        GLA JUN88
         USING VX070,R4              BASE REGISTER NOTIFY     GLA JUN88
         LR    R4,R15         SET THIS MODULE BASE            GLA JUN88
         L     R8,=A(ISPDSPY) GET REAL MOD BASE               GLA JUN88
         L     R5,=A(ISPDSPY+4096) GET REAL MOD BASE          GLA JUN88
         L     R7,4(,R1)             WORK AREA ADDRESS        GLA JUN88
         L     R1,8(,R1)             OUTPUT MESSAGE START     GLA JUN88
         LM    R9,R12,BASES          RESTORE BASE REGISTERS   GLA JUN88
         L     R6,SPFXWRK     GET SPF DIALOG VAR ADDR         GLA JUN88
         LA    R15,VALSAVE           SAVE AREA FOR THIS ROUTINELA JUN88
         ST    R13,4(,R15)                                    GLA JUN88
         ST    R15,8(,R13)           CHAIN SAVE AREAS         GLA JUN88
         LR    R13,R15                                        GLA JUN88
         CLC   VX095,3(R1)        TITLE LINE                   SS OCT88
         BE    VX089              YES , SKIP                   SS OCT88
         CLC   VX095,4(R1)        TITLE LINE OTHER VT0CPRNT    SS APR89
         BE    VX088              YES , SKIP AND SET FLAG      SS APR89
         CLC   VX096,3(R1)        TOTAL LINE                   SS OCT88
         BE    VX089X             YES , SKIP                   SS OCT88
* FIRST SET KEY AND SEE IF DUP          *                      SS OCT88
         TM    SPFLAG5,SPFVT0C       USING OTHER VTOCPRNT      SS APR89
         BZ    VX072                    *                      SS APR89
         CLI   8(R1),C'0'         ALLOC VALUE                 Y2K AUG97
         BL    VX076              NO, MUST BE ERROR           Y2K AUG97
         MVC   SPFCVOL,57(R1)     VOLUME                       SS APR89
         MVC   SPFCDSN(44),64(R1) DSNAME                       SS APR89
         B     VX074                    *                      SS APR89
VX072    DS    0H                       *                      SS APR89
         CLI   7(R1),C'0'         ALLOC VALUE                 Y2K AUG97
         BL    VX076              NO, MUST BE ERROR           Y2K AUG97
         MVC   SPFCVOL,55(R1)     VOLUME                       SS OCT88
         MVC   SPFCDSN(44),62(R1) DSNAME                       SS OCT88
VX074    DS    0H                       *                      SS APR89
         ST    R1,SPFTSV1               *                      SS OCT88
         BAS   R2,ISPGET          SAVE R1                      SS OCT88
         L     R1,SPFTSV1         RESTORE R1                   SS OCT88
         ST    R15,SPFTSV2        SAVE RC                      SS SEP89
* NEED TO EXTRACT DEVTYPE               *                      SS OCT88
*        XC    SPFCDEV,SPFCDEV    NO TYPE                      SS OCT88
         LA    R15,SPFCVOL        GET VOLUME                   SS AUG89
         BAS   R2,ISPDEVTY        GET DEVTYPE                  SS AUG89
         LTR   R15,R15            FOUND *                      SS AUG89
         BNZ   *+8                NO SKIP                      SS AUG89
         STCM  R1,B'1111',SPFCDEV SET DEVTYPE                  SS AUG89
         L     R1,SPFTSV1            RESTORE R1                SS AUG89
         TM    SPFLAG5,SPFVT0C       USING OTHER VTOCPRNT      SS APR89
         BZ    *+8                      *                      SS APR89
         LA    R1,1(,R1)          ADJUST FOR OFFSET            SS APR89
         MVI   SPFCTRKS,C' '                                  DRK SEP11
         MVI   SPFCTRKS+1,C' '                                DRK SEP11
         MVC   SPFCTRKS+2(5),03(R1)  SIZE                     DRK SEP11
* NEED TO CONVERT TOT AND FREE TO REQUESTED TYPE               SS OCT88
* FOR NOW GIVE IN TRACKS                *                      SS OCT88
         MVI   SPFCTOT,C' '                                    SS OCT88
         MVI   SPFCTOT+1,C' '                                 DRK SEP11
         MVI   SPFCTOT+7,C'T'     UNITS OF TRKS               DRK SEP11
         MVC   SPFCTOT+2(5),3(R1) ALLOC                       DRK SEP11
         TM    SPFLAG5,SPFVT0C       USING OTHER VTOCPRNT      SS APR89
         BZ    *+8                      *                      SS APR89
         LA    R1,1(,R1)          ADJUST FOR OFFSET            SS APR89
         MVI   SPFCFREE,C' '                                   SS OCT88
         MVI   SPFCFREE+1,C' '                                DRK SEP11
         MVI   SPFCFREE+7,C'T'    UNITS OF TRKS               DRK SEP11
         MVC   SPFCFREE+2(5),10(R1) UNUSED                    DRK SEP11
         OI    SPFCFREE+6,X'F0'   ALWAYS HAVE A NUMERIC NUM   DRK SEP11
         SPACE 1                                              DRK MAY14
         STM   R0,R2,SAVELSTC        SAVE                     DRK MAY14
         PACK  DOUBLE(8),SPFCFREE(7) FREE TRKS                DRK MAY14
         CVB   R0,DOUBLE                                      DRK MAY14
         PACK  DOUBLE(8),SPFCTOT(7)  TOTAL TRKS               DRK MAY14
         CVB   R2,DOUBLE                                      DRK MAY14
         LTR   R2,R2                 ZERO?                    DRK MAY14
         BNZ   *+4+4                 NO                       DRK MAY14
         LA    R2,1(R2)              YES, AVOID S0C9          DRK MAY14
         SPACE 1                                              DRK MAY14
         LR    R1,R2               TOTAL TRKS                 DRK MAY14
         SR    R1,R0               TOTAL TRKS - FREE TRKS     DRK MAY14
         MH    R1,=H'100'          * 100 FOR PERCENT          DRK MAY14
         SR    R0,R0               CLEAR EVEN FOR DIVIDE      DRK MAY14
         DR    R0,R2               PERCENT SPACE USED         DRK MAY14
         BAS   R2,ISPDECCV         CONVERT VALUE IN R1        DRK MAY14
         MVC   SPFCUSED,SPFDCNV3   SPACE USED                 DRK MAY14
         MVI   SPFCUSED+3,C'%'                                DRK MAY14
         LM    R0,R2,SAVELSTC      RESTORE                    DRK MAY14
         SPACE 1                                              DRK MAY14
         MVC   SPFCEXT,19(R1)     EXTENTS                      SS OCT88
         MVC   SPFCDSOR,23(R1)    ORGANIZATION                 SS OCT88
         CLI   SPFCDSOR,C' '      BLANK DSORG?                ABL JAN89
         BNE   *+10               NO, BRANCH                  ABL JAN89
         MVC   SPFCDSOR,VX098S    YES, MOVE IN **             ABL JAN89
         MVC   SPFCRF(3),27(R1)   RECFM                        SS OCT88
         MVC   SPFCRF+3(2),ISPBLK8      *                      SS OCT88
         MVC   SPFCLREC,31(R1)    LRECL                        SS OCT88
         MVC   SPFCBLKS,37(R1)    BLKSIZE                      SS OCT88
* NEED TO CONVERT VTOC DATES TO MONTH AND DAY                  SS OCT88
         MVI   SPFCCCEN,C'0'          ASSUME 19XX             Y2K AUG97
         OI    43(R1),X'F0'           TURN ON FOR COMPARE     Y2K AUG97
         CLC   43(2,R1),=C'&WIN19XX'  BELOW 19XX WINDOW?      Y2K AUG97
         BNL   *+8                    NO, THEN 19XX           Y2K AUG97
         MVI   SPFCCCEN,C'1'          YES, THEN 20XX          Y2K AUG97
         MVI   SPFCRCEN,C'0'          ASSUME 19XX             Y2K AUG97
         CLC   49(5,R1),=C' 0000'     REFDATE OF ZEROS?       Y2K AUG97
         BE    VX074A                 YES, THEN NO REF DATE   Y2K AUG97
         OI    49(R1),X'F0'           TURN ON FOR COMPARE     Y2K AUG97
         CLC   49(2,R1),=C'&WIN19XX'  BELOW 19XX WINDOW?      Y2K AUG97
         BNL   *+8                    NO, THEN 19XX           Y2K AUG97
         MVI   SPFCRCEN,C'1'          YES, THEN 20XX          Y2K AUG97
VX074A   DS    0H                                             Y2K AUG97
         PACK  MEMLWK(3),43(5,R1)     YYDDD --> YYDDDF CREATE ABL JAN89
         PACK  MEMLWK+9(3),49(5,R1)   YYDDD --> YYDDDF REFER  ABL JAN89
         LA    R1,MEMLWK              ADDRESS OF INPUT DATE   ABL JAN89
         LA    R15,LKEDDATE           ADDRESS OF OUTPUT       ABL JAN89
         L     R2,VX098R              CONVERT TO MMDDYY       ABL JAN89
         BASR  R14,R2                                         ABL JAN89
         SPACE 2                                              ABL JAN89
         MVC   MEMLWK(9),VX098M      EDIT MASK                ABL JAN89
         MVC   LKEDDATE+3(2),LKEDDATE  MMDDYY TO YYMMDD       ABL JAN89
         ED    MEMLWK(9),LKEDDATE+2  DATE AS " YY/MM/DD"      ABL JAN89
         MVC   SPFCCDAT(8),MEMLWK+1  MOVE INTO STORAGE        ABL JAN89
*
         LA    R1,MEMLWK+9            ADDRESS OF INPUT DATE   ABL JAN89
         LA    R15,LKEDDATE           ADDRESS OF OUTPUT       ABL JAN89
         L     R2,VX098R              CONVERT TO MMDDYY       ABL JAN89
         BASR  R14,R2                                         ABL JAN89
         SPACE 2                                              ABL JAN89
         MVC   MEMLWK(9),VX098M      EDIT MASK                ABL JAN89
         MVC   LKEDDATE+3(2),LKEDDATE  MMDDYY TO YYMMDD       ABL JAN89
         ED    MEMLWK(9),LKEDDATE+2  DATE AS " YY/MM/DD"      ABL JAN89
         MVC   SPFCRDAT(8),MEMLWK+1  MOVE INTO STORAGE        ABL JAN89
         CLC   SPFCRDAT(8),=C'00/01/00' REFDATE OF ZEROS?     Y2K AUG97
         BNE   *+10                     NO, BRANCH            ABL JAN89
         MVC   SPFCRDAT,ISPBLK44        YES, CLEAR REFER DATE DRK SEP11
         MVI   SPFCVTOC,C'Y'            YES IN VTOC            SS OCT88
         SPACE 1
         MVC   SPFCEDAT(2),=C'20'   ASSUME 20XX               DRK SEP11
         CLI   SPFCCCEN,C'1'        20XX?                     DRK SEP11
         BE    *+4+6                YES                       DRK SEP11
         MVC   SPFCEDAT(2),=C'19'   NO, 19XX                  DRK SEP11
         MVC   SPFCEDAT+2(8),SPFCCDAT YY/MM/DD                DRK SEP11
         MVC   SPFCCDAT(10),SPFCEDAT  YYYY/MM/DD              DRK SEP11
         SPACE 1
         CLC   SPFCRDAT(10),ISPBLK44  REF DATE SPACES?        DRK SEP11
         BE    NOREF1                 YES                     DRK SEP11
         MVC   SPFCEDAT(2),=C'20'   ASSUME 20XX               DRK SEP11
         CLI   SPFCRCEN,C'1'        20XX?                     DRK SEP11
         BE    *+4+6                YES                       DRK SEP11
         MVC   SPFCEDAT(2),=C'19'   NO, 19XX                  DRK SEP11
         MVC   SPFCEDAT+2(8),SPFCRDAT YY/MM/DD                DRK SEP11
         MVC   SPFCRDAT(10),SPFCEDAT  YYYY/MM/DD              DRK SEP11
NOREF1   DS    0H                                             DRK SEP11
         SPACE 1
         L     R1,SPFTSV1            RESTORE R1               DRK OCT99
         TM    SPFLAG5,SPFVT0C       USING OTHER VTOCPRNT     DRK OCT99
         BZ    *+4+6+6+4             NO                       DRK OCT99
         MVC   SPFCSEC(5),115(R1)    YES, MOVE SEC QUANTITY   DRK OCT99
         MVC   SPFCUPD(1),122(R1)    MOVE UPD INDICATOR       DRK OCT99
         B     *+4+6+6                                        DRK OCT99
         MVC   SPFCSEC(5),113(R1)    MOVE SEC QUANTITY        DRK OCT99
         MVC   SPFCUPD(1),120(R1)    MOVE UPD INDICATOR       DRK OCT99
         TM    SPFLAG5,SPFVCAT       USING CAT OPTION OF VTOC DRK OCT99
         BZ    VX074B                NO                       DRK OCT99
         TM    SPFLAG5,SPFVT0C       USING OTHER VTOCPRNT     DRK OCT99
         BZ    *+4+6+4               NO                       DRK OCT99
         MVC   SPFCCAT(1),126(R1)    YES, MOVE CTLG INDICATOR DRK OCT99
         B     VX074C                CONTINUE                 DRK OCT99
         MVC   SPFCCAT(1),124(R1)    MOVE CTLG INDICATOR      DRK OCT99
         B     VX074C                CONTINUE                 DRK OCT99
VX074B   DS    0H                                             DRK OCT99
         MVI   SPFCCAT,C'-'          NO CATALOG INDICATOR     DRK OCT99
VX074C   DS    0H                                             DRK OCT99
         CLI   SPFCCAT,C'C'          CATALOGED ON THIS VOLUME DRK OCT99
         BNE   *+4+4                 NO                       DRK OCT99
         MVI   SPFCCAT,C'Y'          CHANGE TO Y(ES)          DRK OCT99
         SPACE 1
         ICM   R0,15,SPFTSV2            TEST RC 0              SS SEP89
         BZ    VX075                    YES, MUST BE MOD       SS OCT88
*  FILL IN REST OF ROW WITH BLANKS                             SS OCT88
         MVC   SPFCOPT,ISPBLK8          *                      SS OCT88
         MVC   SPFCDATA,ISPBLK8         *                      SS OCT88
         MVC   SPFCREQ,ISPBLK8          *                      SS OCT88
         MVC   SPFCDSN2,ISPBLK8         *                      SS OCT88
         MVC   SPFCDSNL,ISPBLK8         *                      SS OCT88
*        MVI   SPFCCAT,C'-'             *                      SS OCT88
         MVC   SPFCKEYL,ISPBLK8         *                      SS OCT88
         MVC   SPFCRKP,ISPBLK8          *                      SS OCT88
         MVC   SPFCEDAT,ISPBLK44        *                      SS OCT88
*        MVC   SPFCSECU,ISPBLK8         *                      SS OCT88
*        MVC   SPFCUPD,ISPBLK8          *                      SS OCT88
         MVC   SPFCATYP,ISPBLK8         *                      SS OCT88
*        MVC   SPFCSEC,ISPBLK8          *                      SS OCT88
         MVC   SPFCDIR,ISPBLK8          *                      SS OCT88
         MVC   SPFCMMCT,ISPBLK8         *                      SS OCT88
*                                                              SS OCT88
**       ADD LINE TO TABLE                                     SS OCT88
*                                                              SS OCT88
         BAS   R2,ISPTBADD                                     SS OCT88
         B     VX089                 SUCCESSFUL OUTPUT         SS OCT88
*                                                              SS OCT88
**       REPLACE LINE IN TABLE                                 SS OCT88
*                                                              SS OCT88
VX075    DS    0H                       *                      SS OCT88
         BAS   R2,ISPTBMOD              YES, REPLACE           SS OCT88
         B     VX089                    RETURN CODE 0          SS OCT88
* PUT ERROR MESSAGE INTO LOG            *                      SS OCT88
VX076    DS    0H                       *                      SS OCT88
         MVI   SPFLINE,C' '             BLANK                  SS OCT88
         MVC   SPFLINE+1(157),SPFLINE      RIPPLE              SS OCT88
         SR    R15,R15                  *                      SS OCT88
         ICM   R15,1,0(R1)           GET LENGTH                SS OCT88
         BNZ   *+8                   GOOD LENGTH               SS OCT88
         LA    R15,80                NO, USE 80                SS OCT88
         CLI   0(R1),150             MAX LENGTH                SS OCT88
         BNH   *+8                      *                      SS OCT88
         LA    R15,150                  *                      SS OCT88
         BCTR  R15,0                    *                      SS OCT88
         EX    R15,VX077                *                      SS OCT88
         BAS   R2,ISPADDCO           PUT MESSAGE IN LOG        SS OCT88
         SR    R15,R15               ZERO RC                   SS OCT88
         B     VX090                    *                      SS OCT88
VX077    MVC   SPFLINE(0),1(R1)      <EXECUTED>                SS OCT88
VX088    OI    SPFLAG5,SPFVT0C       USING OTHER VTOCPRNT      SS APR89
         CLC   125(3,R1),=C'CAT'     USING CAT OPTION OF VTOC DRK OCT99
         BNE   *+4+4                 NO                       DRK OCT99
         OI    SPFLAG5,SPFVCAT       YES, SET FLAG            DRK OCT99
         B     VX089X                                         DRK OCT99
VX089    CLC   123(3,R1),=C'CAT'     USING CAT OPTION OF VTOC DRK OCT99
         BNE   *+4+4                 NO                       DRK OCT99
         OI    SPFLAG5,SPFVCAT       YES, SET FLAG            DRK OCT99
VX089X   SR    R15,R15               ZERO RETURN CODE          SS OCT88
         B     VX090                    *                      SS OCT88
VX080    LA    R15,4                 SET REGISTER R15         GLA JUN88
VX090    L     R13,4(,R13)           UNCHAIN SAVE AREAS       GLA JUN88
         ST    R15,16(R13)           SAVE RETURN CODE         GLA JUN88
         RETURN (14,12),T,RC=(15)    RETURN                   GLA JUN88
VX095    DC    C'ALLOC'              TITLE LINE                SS OCT88
VX096    DC    C'TOTAL'              TOTAL LINE                SS OCT88
VX098R   DC    V(CONVDATE)           CONVERSION ROUTINE       ABL JAN89
VX098S   DC    C'** '                UNKNOWN DSORG            ABL JAN89
VX098M   DC    X'F02120612020612020' CONVERSION MASK          ABL JAN89
         DROP  R4                                             DRK OCT98
*#*#*#*#*#*#*#*#                                              DRK SEP11
*                                                             DRK SEP11
*   PARSE LINES COMING BACK FROM DSAT:                        DRK SEP11
*   Y2K (HOWARD DEAN) AND EAV (ANDREAS FREYBIER)              DRK SEP11
*                                                             DRK SEP11
*#*#*#*#*#*#*#*#                                              DRK SEP11
DX070    SAVE  (14,12)                                         SS OCT88
         USING DX070,R4              BASE REGISTER NOTIFY      SS OCT88
         LR    R4,R15                SET THIS MODULE BASE      SS OCT88
         L     R8,=A(ISPDSPY)        GET REAL MOD BASE         SS OCT88
         L     R5,=A(ISPDSPY+4096)   GET REAL MOD BASE         SS OCT88
         L     R7,4(,R1)             WORK AREA ADDRESS         SS OCT88
         L     R1,8(,R1)             OUTPUT MESSAGE START      SS OCT88
         LM    R9,R12,BASES          RESTORE BASE REGISTERS    SS OCT88
         L     R6,SPFXWRK            GET SPF DIALOG VAR ADDR   SS OCT88
         LA    R15,VALSAVE           SAVE AREA FOR THIS ROUTINE       "
         ST    R13,4(,R15)                                     SS OCT88
         ST    R15,8(,R13)           CHAIN SAVE AREAS          SS OCT88
         LR    R13,R15                                         SS OCT88
         SPACE 1
         USING DSATEAV,R1                                     DRK SEP11
         CLC   DX095,DSSER        TITLE LINE                  DRK SEP11
         BE    DX070A             YES, CHECK VERSION          DRK SEP11
         B     DX070D             NO                          DRK SEP11
DX070A   DS    0H
         CLC   33(3,R1),=C'EXT'   EAV VERSION?                DRK SEP11
         BNE   DX070B             NO                          DRK SEP11
         MVC   SPFCVER(3),=C'EAV' ANDREAS FREYBIER VERSION    DRK SEP11
         B     DX070C                                         DRK SEP11
DX070B   DS    0H
         MVC   SPFCVER(3),=C'Y2K' HOWARD DEAN VERSION         DRK SEP11
DX070C   DS    0H
         B     DX089              SKIP TITLE LINE             DRK SEP11
DX070D   DS    0H
         CLC   SPFCVER(3),=C'EAV' ANDREAS FREYBIER VERSION?   DRK SEP11
         BE    DX070X             YES, THEN NO MOVE
         MVI   WORKTBL+100,C' '   BLANK                       DRK SEP11
         MVC   WORKTBL+101(127),WORKTBL+100  RIPPLE           DRK SEP11
         MVC   WORKTBL+100(1),0(R1)      LENGTH               DRK SEP11
         MVC   WORKTBL+101(6),1(R1)      SERIAL               DRK SEP11
         MVC   WORKTBL+108(8),8(R1)      DEVTYPE              DRK SEP11
         MVC   WORKTBL+119(5),17(R1)     ALLOC                DRK SEP11
         MVC   WORKTBL+127(5),23(R1)     FREE                 DRK SEP11
         MVC   WORKTBL+134(2),29(R1)     EXT                  DRK SEP11
         MVC   WORKTBL+138(90),32(R1)    DSORG .... DSN       DRK SEP11
         MVC   0(128,R1),WORKTBL+100     Y2K IN EAV FORMAT    DRK SEP11
         SPACE 1
DX070X   DS    0H
         CLC   DSSER(8),ISPBLK8   TOTAL OR ERROR LINE?        DRK SEP11
         BNE   DX071              NO, BRANCH                  ABL JAN89
         CLC   92(8,R1),=C'DATA SET' EAV TOTAL LINE?          DRK SEP11
         BE    DX089              YES, SKIP TOTALS            DRK SEP11
         B     DX076              NO, SOME ERROR MESSAGE      ABL JAN89
DX071    CLC   DX096,DSSER        EMPTY DESIGNATION?          DRK SEP11
         BE    DX076              YES, SOME ERROR MESSAGE     ABL JAN89
         SPACE 1                                              DRK JAN99
* FIRST SET KEY AND SEE IF DUP          *                      SS OCT88
         MVC   SPFCVOL,DSSER      VOLUME                      DRK SEP11
         CLI   DSDSN,C' '         MULTI-VOLUME DSNAME?        DRK SEP11
         BNE   DX072              NO                          DRK JUL03
         B     *+4+6              SKIP BLANK DSNAME MOVE      DRK JUL03
DX072    DS    0H                       *                      SS OCT88
         MVC   SPFCDSN(44),DSDSN  DSNAME                      DRK SEP11
         ST    R1,SPFTSV1               *                      SS OCT88
         BAS   R2,ISPGET          SAVE R1                      SS OCT88
         L     R1,SPFTSV1         RESTORE R1                   SS OCT88
         ST    R15,SPFTSV2        SAVE RC                      SS SEP89
         MVI   SPFCCAT,C'Y'       YES IN CATALOG               SS OCT88
         MVI   SPFCVTOC,C'N'      ASSUME NOT ON VTOC           SS OCT88
* NEED TO EXTRACT DEVTYPE               *                      SS OCT88
*        XC    SPFCDEV,SPFCDEV    NO TYPE                      SS AUG89
         MVC   SPFHEXCV(8),DSDEVT GET DEVTYPE                  SS AUG89
         BAS   R2,ISPBINCV        CONVERT TO BINARY            SS SEP89
         STCM  R1,15,SPFCDEV      SET   *                      SS AUG89
         L     R1,SPFTSV1         RESTORE R1                   SS SEP89
         CLI   DSALLOC+6,C'0'     ALLOC VALUE                 DRK SEP11
         BNL   DX073              YES, VOLUME IS MOUNTED       SS OCT88
         ICM   R0,15,SPFTSV2            TEST RC 0              SS SEP89
         BZ    DX075                    YES, MUST BE MOD       SS OCT88
         MVC   SPFCTRKS,ISPBLK8   SIZE                         SS OCT88
         MVC   SPFCTOT,ISPBLK8    TOT                          SS OCT88
         MVC   SPFCFREE,ISPBLK8   FREE                         SS OCT88
         MVC   SPFCUSED,ISPBLK8   USED %                      DRK MAY14
         MVC   SPFCEXT,ISPBLK8    EXTENTS                      SS OCT88
         MVC   SPFCDSOR,ISPBLK8   ORGANIZATION                 SS OCT88
         MVC   SPFCRF,ISPBLK8     RECFM                        SS OCT88
         MVC   SPFCLREC,ISPBLK8   LRECL                        SS OCT88
         MVC   SPFCBLKS,ISPBLK8   BLKSIZE                      SS OCT88
         MVC   SPFCUPD,ISPBLK8          *                      SS OCT88
         MVC   SPFCCDAT,ISPBLK44  CREATION DATE               DRK SEP11
         MVC   SPFCRDAT,ISPBLK44  REFERENCE DATE              DRK SEP11
         MVC   SPFCCCEN,ISPBLK8   CREATION DATE CENTURY       DRK MAR99
         MVC   SPFCRCEN,ISPBLK8   REFERENCE DATE CENTURY      DRK MAR99
         B     DX074                    *                      SS OCT88
DX073    DS    0H                       *                      SS OCT88
         MVC   SPFCTRKS,DSALLOC   SIZE                        DRK SEP11
* NEED TO CONVERT TOT AND FREE TO REQUESTED TYPE               SS OCT88
* FOR NOW GIVE IN TRACKS                *                      SS OCT88
         MVI   SPFCTOT+7,C'T'     UNITS OF TRKS               DRK SEP11
         MVC   SPFCTOT(7),DSALLOC ALLOC                       DRK SEP11
         MVI   SPFCFREE+7,C'T'    UNITS OF TRKS               DRK SEP11
         MVC   SPFCFREE(7),DSFREE UNUSED                      DRK SEP11
         OI    SPFCFREE+6,X'F0'   ALWAYS HAVE A NUMERIC NUM   ABL JAN89
         SPACE 1                                              DRK MAY14
         STM   R0,R2,SAVELSTC        SAVE                     DRK MAY14
         PACK  DOUBLE(8),SPFCFREE(7) FREE TRKS                DRK MAY14
         CVB   R0,DOUBLE                                      DRK MAY14
         PACK  DOUBLE(8),SPFCTOT(7)  TOTAL TRKS               DRK MAY14
         CVB   R2,DOUBLE                                      DRK MAY14
         LTR   R2,R2                 ZERO?                    DRK MAY14
         BNZ   *+4+4                 NO                       DRK MAY14
         LA    R2,1(R2)              YES, AVOID S0C9          DRK MAY14
         SPACE 1                                              DRK MAY14
         LR    R1,R2               TOTAL TRKS                 DRK MAY14
         SR    R1,R0               TOTAL TRKS - FREE TRKS     DRK MAY14
         MH    R1,=H'100'          * 100 FOR PERCENT          DRK MAY14
         SR    R0,R0               CLEAR EVEN FOR DIVIDE      DRK MAY14
         DR    R0,R2               PERCENT SPACE USED         DRK MAY14
         BAS   R2,ISPDECCV         CONVERT VALUE IN R1        DRK MAY14
         MVC   SPFCUSED,SPFDCNV3   SPACE USED                 DRK MAY14
         MVI   SPFCUSED+3,C'%'                                DRK MAY14
         LM    R0,R2,SAVELSTC      RESTORE                    DRK MAY14
         SPACE 1                                              DRK MAY14
         MVC   SPFCEXT,DSEXT      EXTENTS                     DRK SEP11
         MVC   SPFCDSOR,DSDSORG   ORGANIZATION IF ICF         DRK SEP11
         CLI   SPFCDSOR,C'A'      AM?                          SS FEB89
         BNE   *+10               NO, SKIP                     SS FEB89
         MVC   SPFCDSOR(2),DX097  YES, MAKE VS                DRK DEC08
         MVC   SPFCRF(5),DSRECFM  RECFM                        SS OCT88
         MVC   SPFCLREC,DSLRECL   LRECL                        SS OCT88
         MVC   SPFCBLKS,DSBLKSI   BLKSIZE                      SS OCT88
         SPACE 1
         MVC   SPFCCDAT(4),DSCRDT+6   CREATION DATE YYYY      DRK SEP11
         MVI   SPFCCDAT+4,C'/'                                DRK SEP11
         MVC   SPFCCDAT+5(5),DSCRDT MM/DD                     DRK SEP11
         SPACE 1
         CLC   DSRFDT(7),=CL7' 000000'  ZERO REFDATE?         DRK SEP11
         BNE   DX073A                   NO                    DRK SEP11
         MVC   SPFCRDAT(10),ISPBLK44    YES                   DRK SEP11
         B     DX073B                                         DRK SEP11
DX073A   DS    0H
         CLC   DSRFDT,ISPBLK44          BLANK REFDATE?        DRK SEP11
         BE    DX073B                   YES                   DRK SEP11
         MVC   SPFCRDAT(4),DSRFDT+6   REFERENCE DATE YYYY     DRK SEP11
         MVI   SPFCRDAT+4,C'/'                                DRK SEP11
         MVC   SPFCRDAT+5(5),DSRFDT MM/DD                     DRK SEP11
DX073B   DS    0H
         CLC   SPFCCDAT(2),=C'20'   20XX?                     DRK SEP11
         BNE   DX073C               NO                        DRK SEP11
         MVI   SPFCCCEN,C'1'        20TH                      DRK SEP11
         B     DX073D                                         DRK SEP11
DX073C   DS    0H
         MVI   SPFCCCEN,C'0'        19TH                      DRK SEP11
DX073D   DS    0H
         CLC   SPFCRDAT(10),ISPBLK44  REF DATE SPACES?        DRK SEP11
         BE    DX073F                 YES                     DRK SEP11
         CLC   SPFCRDAT(2),=C'20'   20XX?                     DRK SEP11
         BNE   DX073E               NO                        DRK SEP11
         MVI   SPFCRCEN,C'1'        20TH                      DRK SEP11
         B     DX073F                                         DRK SEP11
DX073E   DS    0H
         MVI   SPFCRCEN,C'0'        19TH                      DRK SEP11
DX073F   DS    0H
         CLI   DSRFDT+10,C'*'     DATASET UPDATED?            DRK SEP11
         BNE   *+4+4+4            NO                          DRK OCT99
         MVI   SPFCUPD,C'Y'       UPDATED = YES               DRK OCT99
         B     *+4+4              CONTINUE                    DRK OCT99
         MVI   SPFCUPD,C'N'       UPDATED = NO                DRK OCT99
         MVI   SPFCVTOC,C'Y'      YES IN VTOC                  SS OCT88
DX074    DS    0H                       *                      SS OCT88
         ICM   R0,15,SPFTSV2            TEST RC 0              SS SEP88
         BZ    DX075                    YES, MUST BE MOD       SS OCT88
*  FILL IN REST OF ROW WITH BLANKS                             SS OCT88
         MVC   SPFCOPT,ISPBLK8          *                      SS OCT88
         MVC   SPFCDATA,ISPBLK8         *                      SS OCT88
         MVC   SPFCREQ,ISPBLK8          *                      SS OCT88
         MVC   SPFCDSN2,ISPBLK8         *                      SS OCT88
         MVC   SPFCDSNL,ISPBLK8         *                      SS OCT88
         MVC   SPFCKEYL,ISPBLK8         *                      SS OCT88
         MVC   SPFCRKP,ISPBLK8          *                      SS OCT88
         MVC   SPFCEDAT,ISPBLK44        *                      SS OCT88
*        MVC   SPFCSECU,ISPBLK8         *                      SS OCT88
         MVC   SPFCATYP,ISPBLK8         *                      SS OCT88
         MVC   SPFCSEC,ISPBLK8          *                      SS OCT88
         MVC   SPFCDIR,ISPBLK8          *                      SS OCT88
         MVC   SPFCMMCT,ISPBLK8         *                      SS OCT88
*                                                              SS OCT88
**       ADD LINE TO TABLE                                     SS OCT88
*                                                              SS OCT88
         BAS   R2,ISPTBADD                                     SS OCT88
         B     DX089                 SUCCESSFUL OUTPUT         SS OCT88
*                                                              SS OCT88
**       REPLACE LINE IN TABLE                                 SS OCT88
*                                                              SS OCT88
DX075    DS    0H                       *                      SS OCT88
         BAS   R2,ISPTBMOD              YES, REPLACE           SS OCT88
         B     DX089                    RETURN CODE 0          SS OCT88
* PUT ERROR MESSAGE INTO LOG            *                      SS OCT88
DX076    DS    0H                       *                      SS OCT88
         ST    R1,SPFTSV1               SAVE R1               ABL JAN89
         BAS   R2,ISPLOGBT              GO TO BOTTOM OF LOG   ABL JAN89
         L     R1,SPFTSV1               RELOAD R1             ABL JAN89
         MVI   SPFLINE,C' '             BLANK                  SS OCT88
         MVC   SPFLINE+1(157),SPFLINE      RIPPLE              SS OCT88
         SR    R15,R15                  *                      SS OCT88
         ICM   R15,1,0(R1)           GET LENGTH                SS OCT88
         BNZ   *+8                   GOOD LENGTH               SS OCT88
         LA    R15,80                NO, USE 80                SS OCT88
         CLI   0(R1),150             MAX LENGTH                SS OCT88
         BNH   *+8                      *                      SS OCT88
         LA    R15,150                  *                      SS OCT88
         BCTR  R15,0                    *                      SS OCT88
         EX    R15,DX077                *                      SS OCT88
         BAS   R2,ISPADDCO           PUT MESSAGE IN LOG        SS OCT88
         SR    R15,R15               ZERO RC                   SS OCT88
         B     DX090                    *                      SS OCT88
DX077    MVC   SPFLINE(0),1(R1)         *                      SS OCT88
DX089    SR    R15,R15               ZERO RETURN CODE          SS OCT88
         B     DX090                    *                      SS OCT88
DX080    LA    R15,4                 SET REGISTER R15          SS OCT88
DX090    L     R13,4(,R13)           UNCHAIN SAVE AREAS        SS OCT88
         ST    R15,16(R13)           SAVE RETURN CODE          SS OCT88
         RETURN (14,12),T,RC=(15)    RETURN                    SS OCT88
DX095    DC    C'SERIAL'             TITLE LINE                SS OCT88
DX096    DC    C'EMPTY '             EMPTY GDG HEADER LINE    ABL JAN89
DX097    DC    C'VS'                 VSAM                      SS FEB89
         LTORG ,                                              GLA JUN88
         DROP  R4,R1                    *                      SS OCT88
         TITLE 'LISTC/LISTFILE OPEN SUBROUTINE'                SS OCT88
*************************************************************  SS OCT88
*                                                              SS OCT88
*      LISTC/LISTFILE OPEN SUBROUTINE                          SS OCT88
*                                                              SS OCT88
*************************************************************  SS OCT88
         SPACE 2                                               SS OCT88
ISPLSCOP DS    0H                       *                      SS OCT88
         USING *,R4                                            SS OCT88
         BAS   R2,ISPSETST              SET STATS PREV TABLE   SS OCT88
* OPEN TB                                                      SS OCT88
* DO VDEF FIRST                                                SS OCT88
         LA    R1,ISPLSCAV        DEF VAR TABLE                SS OCT88
         BAS   R2,ISPVDEFX                                     SS OCT88
         CLI   SPFFUNST+5,C'I'    INACTIVE                     SS OCT88
         BNER  R3                 NO, SKIP                     SS OCT88
         CLI   SPFTBPFI+1,5       ALREADY AT LISTC             SS OCT88
         BE    ISPLSCNK           THEN DISPLAY AS IS           SS OCT88
         LA    R1,20              PANEL J                      SS OCT88
         LNR   R1,R1              SET FOR SWAP                 SS OCT88
         BAS   R2,ISPSTKI         STACK IT                     SS OCT88
ISPLSCNK MVC   SPFTBL,SPFCSUF    GET BASE NAME                 SS OCT88
         MVI   SPFTBL+2,C'C'     SET TO PDC                    SS OCT88
         XC    SPFTFLG1(4),SPFTFLG1                            SS OCT88
         XC    SPFTFF(2),SPFTFF                                SS OCT88
         LA    R15,ISPLSCKY      KEYS                          SS OCT88
         LA    R0,ISPLSCVN       VER NAMES                     SS OCT88
         BAS   R2,ISPTBOPN                                     SS OCT88
         BAS   R2,ISPSETST         ALSO INIT STATUS FLAG       SS OCT88
         LH    R0,SPFTBPSR         SORT INIT INDEX             SS OCT88
         BAS   R2,ISPSORTI         REQUEST SORT INIT           SS OCT88
         BR    R3                  RETURN TO CALLER            SS OCT88
         DS    0F                       *                      SS SEP90
ISPLSCAV DC    H'0',X'02',X'00'                                SS OCT88
         DC    A(VSPFCOPT),AL2(SPFCOPT-SPWRKC),H'04'           SS OCT88
         DC    A(VSPFCDAT),AL2(SPFCDATA-SPWRKC),H'08'          SS OCT88
         DC    A(VSPFCCAT),AL2(SPFCCAT-SPWRKC),H'01'           SS OCT88
         DC    A(VSPFCDSN),AL2(SPFCDSN-SPWRKC),H'44'           SS OCT88
         DC    A(VSPFCVOL),AL2(SPFCVOL-SPWRKC),H'06'           SS OCT88
         DC    A(VSPFCDSO),AL2(SPFCDSOR-SPWRKC),H'03'         DRK NOV08
         DC    A(VSPFCRF),AL2(SPFCRF-SPWRKC),H'05'             SS OCT88
         DC    A(VSPFCKEY),AL2(SPFCKEYL-SPWRKC),H'03'          SS OCT88
         DC    A(VSPFCUPD),AL2(SPFCUPD-SPWRKC),H'01'           SS OCT88
         DC    A(VSPFCCDA),AL2(SPFCCDAT-SPWRKC),H'10'         DRK SEP11
         DC    A(VSPFCSEC),AL2(SPFCSECU-SPWRKC),H'04'          SS OCT88
         DC    A(VSPFCEXT),AL2(SPFCEXT-SPWRKC),H'03'           SS OCT88
         DC    A(VSPFCTRK),AL2(SPFCTRKS-SPWRKC),H'07'         DRK SEP11
         DC    A(VSPFCSEX),AL2(SPFCSEC-SPWRKC),H'05'          DRK SEP11
         DC    A(VSPFCTOT),AL2(SPFCTOT-SPWRKC),H'08'          DRK SEP11
* ADDITIONAL WORK VARIABLES             *                      SS OCT88
         DC    A(VSPFCPVO),AL2(SPFCPVOL-SPWRKC),H'06'          SS NOV88
         DC    A(VSPFCUCU),AL2(SPFCUCUR-SPWRKC),H'03'          SS AUG89
         DC    A(VSPFCCEN),AL2(SPFCCCEN-SPWRKC),H'01'         Y2K AUG97
         DC    X'FF'                                           SS OCT88
VSPFCOPT DC    C'(PDSCOPT PDSCREQ PDSCDEV)'                    SS OCT88
VSPFCDAT DC    C'(PDSCDATA PDSCDSN2 PDSCDSNL)'                 SS OCT88
VSPFCCAT DC    C'(PDSCCAT  PDSCVTOC)'                          SS OCT88
VSPFCDSN DC    C'PDSCDSN '                                     SS OCT88
VSPFCVOL DC    C'PDSCVOL '                                     SS OCT88
VSPFCDSO DC    C'PDSCDSOR'                                     SS OCT88
VSPFCRF  DC    C'(PDSCRF PDSCLREC PDSCBLKS)'                   SS OCT88
VSPFCKEY DC    C'(PDSCKEYL PDSCRKP)'                           SS OCT88
VSPFCUPD DC    C'PDSCUPD '                                     SS OCT88
VSPFCCDA DC    C'(PDSCCDAT PDSCRDAT PDSCEDAT)'                 SS OCT88
VSPFCSEC DC    C'(PDSCUSED)' REUSED PDSCSECU VARIABLE         DRK MAY14
VSPFCEXT DC    C'(PDSCEXT PDSCATYP)'                           SS OCT88
VSPFCTRK DC    C'(PDSCTRKS)'                                  DRK SEP11
VSPFCSEX DC    C'(PDSCSEC  PDSCDIR  PDSCMMCT)'                DRK SEP11
VSPFCTOT DC    C'(PDSCTOT PDSCFREE)'                           SS OCT88
VSPFCPVO DC    C'PDSCPVOL'                                     SS OCT88
VSPFCUCU DC    C'(PDSCUCUR PDSCCNEW PDSCCNTL)'                 SS AUG89
VSPFCCEN DC    C'(PDSCCCEN PDSCRCEN)'                         Y2K AUG97
* TABLE DEFINITION                      *                      SS OCT88
ISPLSCKY DC    C'(PDSCDSN PDSCVOL)'  KEY VARIABLES             SS OCT88
ISPLSCVN DC    C'(PDSCOPT PDSCDATA PDSCREQ  PDSCDEV  PDSCDSN2 PDSCDSNL X
               PDSCCAT PDSCVTOC PDSCDSOR PDSCRF   PDSCLREC PDSCBLKS '
         DC    C'PDSCKEYL PDSCRKP  PDSCUPD  PDSCCDAT PDSCRDAT PDSCEDAT X
               PDSCUSED PDSCEXT PDSCATYP PDSCTRKS PDSCSEC  PDSCDIR '
         DC    C'PDSCMMCT PDSCTOT  PDSCFREE '
         DC    C'PDSCCCEN PDSCRCEN '                          Y2K AUG97
         DC    C')'  ABOVE TABLE LAST EDIT                     SS OCT88
         DROP  R4                       *                      SS OCT88
DSATEAV  DSECT ,              OFFSET                          DRK SEP11
         DS    CL1                 0
DSSER    DS    CL6                 1
         DS    CL1
DSDEVT   DS    CL8                 8
         DS    CL1
DSALLOC  DS    CL7                17
         DS    CL1
DSFREE   DS    CL7                25
         DS    CL1
DSEXT    DS    CL3                33
         DS    CL2
DSDSOPRE DS    CL2                38
DSDSORG  DS    CL3                40
         DS    CL1
DSRECFM  DS    CL4                44
         DS    CL1
DSBLKSI  DS    CL5                49
         DS    CL1
DSLRECL  DS    CL5                55
         DS    CL1
DSCRDT   DS    CL10               61
         DS    CL1
DSRFDT   DS    CL10               72
DSUPD    DS    CL1                82
         DS    CL1
DSDSN    DS    CL44               84
DSLEN    EQU   *-DSATEAV         128
         SPACE 2
ISPDSPY  CSECT ,              RESUME CSECT                    DRK SEP11
