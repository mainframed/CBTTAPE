         /************************************************************/
         /* ISPF/PDF EDIT MACRO SETS UP AND CAN EXECUTE FIND AND     */
         /* CHANGE COMMANDS WHICH ARE SAVE FOR REUSE ON OTHER EDITS. */
         /* COMMANDS WHICH ARE SAVE FOR REUSE ON REPEADED EDITS.     */
         /* THIS MACRO CAN BE USED AS AN INITIAL MACRO OR FROM THE   */
         /* EDIT COMMAND LINE.  THIS MACRO WAS ENTENDED FOR USES     */
         /* BY PDS DIALOG BUT CAN BE USE DIRECTLY BY THE USER.       */
         /*                                                          */
         /*   OWNER:                                                 */
         /*     SPAC TECH SUPPORT, SCP  LAST MODIFIED SCPSDS AUG 88  */
         /*                                                          */
         /************************************************************/

            ISREDIT MACRO (PARM1) NOPROCESS
            ISPEXEC CONTROL ERRORS RETURN
         /*  CONTROL LIST CONLIST SYMLIST
            SET P=P86
            SET &FLAG = N
            IF &PARM1 = &Z THEN GOTO VGET
            IF   &STR(&PARM1) = ? THEN GOTO HELP
            ISPEXEC CONTROL NONDISPL
            OPTIONS: -
            ISPEXEC DISPLAY PANEL(&P.EQUA2)
            SET LCC=&LASTCC
            IF &LCC¬=0 THEN EXIT CODE(&LCC)
            SET PARM1 = &SUBSTR(1:1,&STR(&PARM1 ))
            IF   &STR(&PARM1) ¬= H THEN GOTO VGET
            HELP: -
              ISPEXEC SELECT PGM(ISPTUTOR) PARM(&P.EQUH@)
          /*  ISPEXEC BROWSE DATASET('SYS6.DOC.DATA(EQUAL)')
              SET PARM1 = &Z
              GOTO OPTIONS
          /* GET SHARED VARIABLES
          VGET: -
             ISPEXEC VGET (PDSEREQ  PDSERCHN PDSEEXCD PDSEAUTO +
                           PDSEPROM PDSEDISP PDSEERAC PDSESTR1 +
                           PDSESTR2 PDS2IMBD PDSECOL1 PDSECOL2) +
                          SHARED
          /* CHECK FOR PARM EQUAL TO BLANK
            IF &PARM1  = &Z THEN +
              DO
                 /* CHECK FOR INTIAL MACRO
                 ISREDIT UP MAX
                 IF &LASTCC < 12 THEN GOTO PROMPT
                 SET &FLAG = I
                 IF &PDSEPROM=YES THEN GOTO PROMPT
                 GOTO PROCESS
             END
          /* CHECK FOR PARM EQUAL TO NOPROMPT
            IF &PARM1  = N THEN GOTO NOPROMPT
          /* CHECK FOR PARM EQUAL TO PROMPT
            IF &PARM1  = P THEN DO
                      SET PARM1 = &Z
                      GOTO OPTIONS
                      END
            IF &PARM1  = F THEN GOTO FIND
            IF &PARM1  = C THEN GOTO CONTINUE
            IF &PARM1  = R  THEN +
                  DO
                    SET &PDSEPROM  = YES
                    ISPEXEC VPUT (PDSEPROM) SHARED
                    EXIT CODE(0)
                  END
          /* NONE OF THE ABOVE
            SET ZEDSMSG = INVALID PARAMETER
            SET ZEDLMSG = REENTER WITH H OR HELP OPERAND FOR
            SET ZEDLMSG = &ZEDLMSG FOR MORE INFORATION
            ISPEXEC SETMSG MSG(ISRZ001)
            EXIT CODE(12)

         NOPROMPT: -
              IF &PDSEREQ ¬= &Z THEN GOTO PROCESS
         PROMPT: -
              SET &PDSEMSG=&Z
           /* DISPLAY PANEL
              ISPEXEC DISPLAY PANEL(&P.EQUAG)
              SET SCC=&LASTCC
              IF &SCC ¬= 0 THEN +
               DO
                 IF &FLAG=I THEN GOTO CANCEL
                 EXIT CODE(0)
               END
               ISPEXEC VPUT (PDSEREQ  PDSERCHN PDSEEXCD PDSEAUTO +
                             PDSEPROM PDSEDISP PDSEERAC PDSESTR1 +
                             PDSESTR2 PDS2IMBD PDSECOL1 PDSECOL2) +
                          SHARED
         PROCESS: -
             ISREDIT CURSOR =  1  0
              IF &PDSEREQ = FIND THEN GOTO FIND
              IF &PDSEREQ = CHANGE THEN GOTO CHANGE
              GOTO PROMPT  /* REQ NOT SET */
         FIND: -
            IF &PDSEEXCD=YES THEN ISREDIT EXCLUDE ALL
             SET COLS= &PDSECOL1 &PDSECOL2
             ISREDIT FIND &NRSTR(&PDSESTR1) ALL &PDSEIMBD &COLS
             SET SCC = &LASTCC
             IF &FLAG = I && &SCC =4 THEN GOTO CANCEL
             IF &SCC > 11 THEN +
                DO
                ISPEXEC SETMSG MSG(&ZERRMSG)
                END
             ELSE DO
               ISREDIT (STRING LINES) = FIND_COUNTS
               SET STRING = &EVAL(&STRING)
               SET LINES = &EVAL(&LINES)
               SET ZEDSMSG = &STR(&STRING STRINGS ON &LINES LINES)
               SET ZEDLMSG = &STR(&PARMS FOUND &STRING TIMES ON &LINES +
               LINES)
               ISPEXEC SETMSG MSG(ISRZ000)
               IF &PDSERCHN = YES THEN GOTO SETCHN
               END
             EXIT CODE(&SCC)
         CHANGE: -
            IF &PDSEEXCD=YES THEN ISREDIT EXCLUDE ALL
             ISREDIT CHANGE  &NRSTR(&PDSESTR1) &NRSTR(&PDSESTR2) +
             ALL &PDSEIMBD &PDSECOL1 &PDSECOL2
             SET SCC = &LASTCC
             IF &FLAG = I && &SCC =4 && &PDSEDISP =  YES THEN +
                                          GOTO NOMATCH
             IF &FLAG = I && &SCC =4 THEN GOTO CANCEL
             IF &SCC = 4 THEN GOTO NOMATCH
             IF &SCC=8 THEN GOTO CHKTRUN
             IF &SCC > 11 THEN +
                DO
                ISPEXEC SETMSG MSG(&ZERRMSG)
                EXIT CODE(12)
                END
            /* GOOD CHANGE
             IF &PDSEDISP = YES THEN GOTO REPORT
             IF &FLAG = I THEN GOTO SAVE
             GOTO REPORT
         SETCHN: -
             SET ZEDSMSG = &Z
             SET ZEDLMSG = &STR(FOUND ON &LINES LINES, ===> +
                                DO RFIND/RCHANGE OR EQUAL CHANGE)
             ISPEXEC SETMSG MSG(ISRZ000)
             ISREDIT (SVLN) = LINE .ZCSR
             ISREDIT (R C) = CURSOR
             ISREDIT CURSOR =  &R 0
             ISREDIT CHANGE  &NRSTR(&PDSESTR1) &NRSTR(&PDSESTR2) +
             NEXT &PDSEIMBD &PDSECOL1 &PDSECOL2
             SET SCC = &LASTCC
          /* IF &SCC=8 THEN GOTO CHKTRUN
             IF &SCC > 11 THEN +
                DO
                ISPEXEC SETMSG MSG(&ZERRMSG)
                EXIT CODE(12)
                END
             ISREDIT LINE .ZCSR = (SVLN)
             ISREDIT CURSOR =  &R 0
             ISREDIT RFIND
             EXIT CODE(0)
         CONTINUE: -
             IF &STR(&PDSESTR2) = &Z THEN +
             DO
                SET ZEDSMSG = NO CHANGE TO STRING
                SET ZEDLMSG = CHANGE TO STRING NOT PREVIOUS SET, ENTER +
                              STRING TO CONTINUE, END TO STOP
                ISPEXEC SETMSG MSG(ISRZ001)
                ISPEXEC DISPLAY PANEL(&P.EQUAG)
                SET SCC=&LASTCC
                IF &SCC ¬= 0 THEN +
                 DO
                   EXIT CODE(0)
                 END
                 ISPEXEC VPUT (PDSEREQ  PDSERCHN PDSEEXCD PDSEAUTO +
                               PDSEPROM PDSEDISP PDSEERAC PDSESTR1 +
                               PDSESTR2 PDS2IMBD PDSECOL1 PDSECOL2) +
                          SHARED
             END
             ISREDIT CHANGE  &NRSTR(&PDSESTR1) &NRSTR(&PDSESTR2) +
             ALL NX  &PDSEIMBD &PDSECOL1 &PDSECOL2
             SET SCC = &LASTCC
             IF &SCC = 4 THEN GOTO NOMATCH
             IF &SCC=8 THEN GOTO CHKTRUN
             IF &SCC > 11 THEN +
                DO
                ISPEXEC SETMSG MSG(&ZERRMSG)
                EXIT CODE(12)
                END
            /* GOOD CHANGE
             GOTO REPORT
            /* CHECK IF TRUNCATION OCCURRED, IF SO DISPLAY MESSAGE */
          CHKTRUN: -
            ISREDIT (CCOUNT FCOUNT) = CHANGE_COUNTS
            SET TCOUNT=&CCOUNT+&FCOUNT
            IF &FCOUNT =¬ 0 THEN +
              DO
              IF &FLAG=I THEN +
                DO
                   IF &PDSEERAC=CANCEL THEN GOTO CANCEL
                   IF &PDSEINCP=SAVE THEN GOTO SAVE
                END
              SET ZEDSMSG = &FCOUNT LINES NOT CHANGED
              SET ZEDLMSG = &STR(REPLACE STRING LARGER THAN FIND +
                              STRING, NO ROOM FOR UPDATE FOR +
                              &FCOUNT OF &TCOUNT LINES)
              ISPEXEC SETMSG MSG(ISRZ001)
              SET SCC = 12
              GOTO SETDISP
              END
          REPORT: -
             ISREDIT (CCOUNT FCOUNT) = CHANGE_COUNTS
             SET ZEDSMSG = &CCOUNT LINES CHANGED
             SET ZEDLMSG = &STR(EQUAL MACRO CHANGE OPTION COMPLETE +
                           FOR &CCOUNT LINES)
             ISPEXEC SETMSG MSG(ISRZ001)
             SET SCC = 0
             GOTO SETDISP
          /* TERMINATION OPTIONS
          CANCEL: -
             ISREDIT CANCEL
             EXIT CODE(0)
          NOMATCH: -
             SET ZEDSMSG = STRING NOT FOUND
             SET ZEDLMSG = THE FIND STRING WAS NOT FOUND
             ISPEXEC SETMSG MSG(ISRZ001)
             EXIT CODE(12)
          SETDISP: -
             IF &PDSEAUTO = YES THEN ISREDIT AUTOSAVE OFF PROMPT
             EXIT CODE(&SCC)
          SAVE: -
             ISREDIT SAVE
             SET SCC = &LASTCC
             IF &SCC > 4 THEN +
             DO
                ISPEXEC SETMSG MSG(&ZERRMSG)
                EXIT CODE(12)
             END
             ISREDIT END
             EXIT CODE(0)
