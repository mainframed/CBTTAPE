/* --------------------  rexx procedure  -------------------- *
 | Name:      FX                                              |
 |                                                            |
 | Function:  FIND ALL after eXclude ALL                      |
 |            then HIDE eXcluded lines                        |
 |                                                            |
 | Syntax:    FX fxstring col1 col2                           |
 |                                                            |
 | Author:    John Kalinich                                   |
 |                                                            |
 | History:  (most recent on top)                             |
 |            05/14/93 - Creation                             |
 |            02/05/19 - Touch up                             |
 |                                                            |
 * ---------------------------------------------------------- */
"ISREDIT MACRO (FXSTRING COL1 COL2)"

If fxstring = "" Then
  Do
    zedsmsg = "Enter FIND string"
    zedlmsg = "FX requires a string as an argument"
    "ISPEXEC SETMSG MSG(ISRZ001W)"
    Exit 12
  End
If Datatype(fxstring) = "NUM" Then
  fxstring = "'"fxstring"'"
If Substr(fxstring,1,2) = "C'" |,
   Substr(fxstring,1,2) = "c'" |,
   Substr(fxstring,1,2) = "P'" |,
   Substr(fxstring,1,2) = "p'" |,
   Substr(fxstring,1,2) = "T'" |,
   Substr(fxstring,1,2) = "t'" |,
   Substr(fxstring,1,2) = "X'" |,
   Substr(fxstring,1,2) = "x'" |,
   Substr(fxstring,1,1) = '"'  |,
   Substr(fxstring,1,1) = "'"     Then
  quote = ''
Else
  quote = "'"
"ISREDIT (STAT,VALUE) = NUMBER"  /* Save NUMBER setting */
"ISREDIT NUMBER OFF"             /* Search sequence numbers on 'FIND' */
"ISREDIT EXCLUDE ALL"
"ISPEXEC CONTROL ERRORS RETURN"
"ISREDIT SCAN OFF"
"ISREDIT FIND ALL" fxstring col1 col2
 findcc = rc
"ISREDIT (FINDS) = FIND_COUNTS"
"ISREDIT (LEFT,RIGHT) = BOUNDS"
Select
  When col1 = ""  &,
       col2 = ""  Then
    Nop
  Otherwise
    Do
      If col1 ¬= "" Then
        left = col1
      If col2 ¬= "" Then
        right = col2
      If col1 ¬= "" &,
           col2  = "" Then
        right = (left + LENGTH(fxstring)) - 1
    End
End
If findcc > 0
  Then
    Do
      zedsmsg = "No CHARS "quote||fxstring||quote" found"
      zedlmsg = "CHARS "quote||fxstring||quote" - not found "||,
                "on any lines (COLS "left+0" to "right+0")."
      "ISPEXEC SETMSG MSG(ISRZ001W)"
      "ISREDIT RESET EXCLUDED"
    End
  Else
    Do
      zedsmsg = finds+0" CHARS "quote||fxstring||quote
      zedlmsg = "CHARS "quote||fxstring||quote" found "||,
                 finds+0" times within columns "left+0" to "right+0"."
      "ISPEXEC SETMSG MSG(ISRZ000W)"
    End
If stat = "OFF" Then
  Nop
Else
  "ISREDIT NUMBER = "stat value          /* Restore NUMBER setting */
"ISREDIT HIDE  EXCLUDED"
Exit 1
