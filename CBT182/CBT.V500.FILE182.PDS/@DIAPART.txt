         TITLE 'ISPF - PARSE LEVEL 1 SUBROUTINE'               SS NOV85
*                                                              SS MAR85
*   PARSE SUBROUTINE  R3 = RETURN                              SS MAR85
*                     R1 = PARSE LIST                          SS MAR85
*                     R14= LENGTH OF INPUT LINE                SS MAR85
*                     R15= INPUT LINE ADDRESS                  SS MAR85
*         EACH PARSE ENTRY IN LIST  (MUST BE ON WORD BOUNDARY) SS FEB88
*-- X'NN' ENTRY TYPE/LEN                                       SS FEB88
*   X'80' POSITIONAL STRING .. MUST BE FIRST                   SS FEB88
*   X'10' WITH POSITIONAL STR  LENGTH CHECK                    SS OCT88
*   X'01' WITH POSITIONAL STR  DO NOT CUT FROM THE LIST        SS OCT88
*   X'08' WITH POSITIONAL STR  CONDITIONAL ON DELIMITER        SS OCT88
*   X'04' WITH POSITIONAL STR  CONDITIONAL ON LENGTH           SS OCT88
*   X'40' NUMBER.. SELF DEFINING KEY WORK                      SS FEB88
*   X'50' DO RANGE CHECK ON NUMBER                             SS FEB88
*         ZERO, ONE, OR TWO NUMBERS MAY BE USED                SS FEB88
*         THESE MUST BE FIRST AFTER POSITIONAL STRING          SS FEB88
*         IF SPECIFIED.  ANY NUMERIC VALUES ENTERED BY         SS FEB88
*         THE USER WILL BE SAVED IN THE ORDER FOUND            SS FEB88
*   X'20' KEY WORD                                             SS FEB88
*   X'30' KEY WORD MAY BE ABBREVIATED BY ONE CHARACTER         SS FEB88
*   X'07' LENGTH OF KEYWORD-1, VALUES 0-7 (ABBREVIATION LEN)   SS FEB88
*-- X'NNNNNN'   ENTRY ID (BIT PATTERN ORED INTO RETURN FLAGS   SS FEB88
*   X'800000'   IF ON AND USED WITH POSITIONAL STRING, THEN    SS FEB88
*               THE CHARACTERS * / : ARE NOT DELIMITERS        SS FEB88
*-- H'NNNNN',XL6'00'  LENGTH OF STRING -1 (CONDITIONAL OR MAX) SS OCT88
*   CL8'KKK'    KEY WORD NAME                                  SS OCT88
*   F'LLLL',F'HHHH'  LOW AND HI RANGE NUMBERS (MAY BE +/-)     SS FEB88
ISPPELN  EQU   12    LENGTH OF EACH ENTRY                      SS NOV85
ISPPKW   EQU   4     OFFSET TO KEY WORD IN ENTRY               SS NOV85
*                                                              SS NOV85
ISPPARSE DS    0H                                              SS MAR85
         USING *,R4                                            SS MAR85
         MVI   SPFPRRC,0    SET RC ZERO                        SS MAR85
         MVI   SPFPRFG,0    ZERO PROCESS FLAG                  SS NOV85
         XC    SPFPRBI,SPFPRBI ZERO PARM BITS                  SS NOV85
         SR    R0,R0                                           SS NOV85
         ST    R0,SPFPRNU                                      SS NOV85
         ST    R0,SPFPRNU2                                     SS NOV85
         BAS   R2,ISPCKBLK  POSITION AT FIRST NON BLANK        SS MAR85
         LTR   R15,R15      ANY                                SS MAR85
         BZ    ISPPARE8     NO  STRING FOUND                   SS MAR85
         ST    R1,SPFPRAD   SAVE PARSE LIST                    SS MAR85
         TM    0(R1),X'80'  STRING REQUEST                     SS MAR85
         BZ    ISPPAR50     NO TRY OTHER                       SS MAR85
ISPPAR01 DS    0H                       *                      SS OCT88
         CLI   0(R15),X'8B' BRACKET                            SS MAR85
         BE    ISPPAR05     YES USE A DELIMITER                SS MAR85
         CLI   0(R15),X'80' ALPHA (UPPER OR LOWER)             SS MAR85
         BH    ISPPAR10     YES, USE BLANK AS DELIMITER        SS MAR85
         CLI   0(R15),C'$'  NATIONAL CHAR                      SS MAR85
         BE    ISPPAR10     YES, USE BLANK AS DELIMITER        SS MAR85
         CLI   0(R15),C'@'  NATIONAL CHAR                      SS MAR85
         BE    ISPPAR10     YES, USE BLANK AS DELIMITER        SS MAR85
         CLI   0(R15),C'#'  NATIONAL CHAR                      SS MAR85
         BE    ISPPAR10     YES, USE BLANK AS DELIMITER        SS MAR85
         TM    1(R1),X'80'  FIND COMMAND?                     ABL JUN85
         BZ    ISPPAR02     YES, USE CHARACTER AS DELIMITER   ABL JUN85
         CLI   0(R15),C'*'  RECALL GROUP                       SS MAR85
         BE    ISPPAR10     YES, USE BLANK AS DELIMITER        SS MAR85
         CLI   0(R15),C':'  GROUP                              SS MAR85
         BE    ISPPAR10     YES, USE BLANK AS DELIMITER        SS MAR85
         CLI   0(R15),C'/'  PATTERN?                          ABL JUN85
         BE    ISPPAR10     YES, USE BLANK AS DELIMITER?      ABL JUN85
ISPPAR02 DS    0H                                             ABL JUN85
ISPPAR05 DS    0H                                              SS MAR85
         IC    R1,0(R15)    INSERT COMPARE CHAR                SS MAR85
         LA    R15,1(,R15)  START AT NEXT CHAR                 SS MAR85
         OI    SPFPRFG,SPFPRDEL  DELIMITED STRING              SS OCT88
         B     ISPPAR15     CONTINUE                           SS MAR85
ISPPAR10 DS    0H                                              SS MAR85
         NI    SPFPRFG,255-SPFPRDEL NO DELIMITER               SS OCT88
         LA    R1,C' '      USE BLANK                          SS MAR85
ISPPAR15 DS    0H                                              SS MAR85
         ST    R15,SPFPRSV  START OF STRING                    SS MAR85
         BAS   R2,ISPCLI                                       SS MAR85
         LTR   R15,R15                                         SS MAR85
         BZ    ISPPAREL     RC 12 ERROR IN LENGTH OF STRING    SS MAR85
         LR    R0,R15                                          SS MAR85
         SL    R0,SPFPRSV   CALCULATE FIRST LENGTH             SS MAR85
         BCTR  R0,0         LESS ON                            SS MAR85
         L     R1,SPFPRAD   GET ADDRESS OF PARM                SS OCT88
         TM    0(R1),X'08'  CONDITIONAL ON DELIMITER           SS OCT88
         BZ    ISPPAR16     NO, SKIP    *                      SS OCT88
         TM    SPFPRFG,SPFPRDEL  USING A DELIMITER             SS OCT88
         BO    ISPPAR19     YES, GOOD ONE                      SS OCT88
         B     ISPPAR21     NO,  SKIP IT                       SS OCT88
ISPPAR16 DS    0H                       *                      SS OCT88
         TM    0(R1),X'04'  CONDITIONAL ON LENGTH              SS OCT88
         BZ    ISPPAR17     NO, SKIP    *                      SS OCT88
         CH    R0,4(R1)     TO LONG                            SS OCT88
         BNH   ISPPAR19     NO, GOOD ONE                       SS OCT88
         B     ISPPAR21     YES, SKIP IT                       SS OCT88
ISPPAR17 DS    0H                       *                      SS OCT88
         TM    0(R1),X'10'  MAX LENGTH CHECK                   SS OCT88
         BZ    ISPPAR19     NO, SKIP    *                      SS OCT88
         CH    R0,4(R1)     TO LONG                            SS OCT88
         BNH   ISPPAR19     NO, GOOD ONE                       SS OCT88
         B     ISPPAREL     YES, LENGTH ERROR                  SS OCT88
ISPPAR19 STH   R0,SPFPRLN                                      SS OCT88
         MVC   SPFPRSR,SPFPRSV  SAVE STRING ADDRESS            SS OCT88
         L     R1,SPFPRAD   GET ADDRESS OF PARM                SS MAR85
         OC    SPFPRBI,1(R1) OR IN PARM BIT                    SS MAR85
         CLI   0(R15),C' '  WE AT BLANK                        SS MAR85
         BE    ISPPAR20     YES                                SS MAR85
         LA    R15,1(,R15)  SKIP OVER DELIMITER                SS MAR85
ISPPAR20 DS    0H                                              SS MAR85
         BAS   R2,ISPCKBLK  FIND NEXT PARM                     SS MAR85
         LTR   R15,R15      ANY FOUND                          SS MAR85
         BZR   R3           NO DONE                            SS MAR85
         B     ISPPAR22                 *                      SS OCT88
ISPPAR21 DS    0H                       *                      SS OCT88
         L     R15,SPFPRSV  BACKUP TO START OF STRING          SS OCT88
         L     R1,SPFPRAD   GET ADDRESS OF PARM                SS MAR85
ISPPAR22 DS    0H                       *                      SS OCT88
         TM    0(R1),X'01'  ARE WE DONE WITH STRINGS           SS OCT88
         BZ    ISPPAR25     NO, SKIP    *                      SS OCT88
         LA    R1,ISPPELN(,R1) SET TO NEXT PARSE ENTRY         SS MAR85
         B     ISPPAR70     LOOK FOR MORE KEYWORDS             SS OCT88
ISPPAR25 DS    0H                       *                      SS OCT88
         LA    R1,ISPPELN(,R1) SET TO NEXT PARSE ENTRY         SS MAR85
         ST    R1,SPFPRAD                                      SS MAR85
ISPPAR50 DS    0H                                              SS MAR85
         MVI   SPFPRRC,24   INCASE INVALID KEY                 SS MAY85
         ST    R14,SPFPRLL  SAVE IT                            SS MAR85
         L     R1,SPFPRAD   GET ADDRESS                        SS MAR85
         CLI   0(R1),X'FF'  END OF TABLE                       SS MAR85
         BE    ISPPAR90     DONE, SETUP RC                     SS MAR85
         CLI   0(R15),C'-'  IS IT NUMERIC (NEGATIVE)           SS MAR85
         BE    ISPPAR60     YES                                SS MAR85
         CLI   0(R15),C'0'  IS IT NUMERIC                      SS MAR85
         BNL   ISPPAR62     YES                                SS MAR85
         TM    0(R1),X'40'  NUMERIC PARM                       SS MAR85
         BZ    ISPPAR70     NO                                 SS MAR85
         LA    R1,ISPPELN(,R1) NEXT ENTRY                      SS MAR85
         TM    0(R1),X'40'  NUMERIC SECOND PARM                SS NOV85
         BZ    ISPPAR70     NO                                 SS NOV85
         LA    R1,ISPPELN(,R1) NEXT ENTRY                      SS NOV85
         B     ISPPAR70     TRY KEYWORDS                       SS MAR85
ISPPAR60 DS    0H                                              SS MAR85
         OI    SPFPRRC,X'80' SAY NEGATIVE VALUE                SS MAR85
         LA    R15,1(,R15)  SKIP OVER - SIGN                   SS MAR85
         BCT   R14,ISPPAR61                                    SS MAR85
         B     ISPPARES     NO NUMBERS                         SS MAR85
ISPPAR61 CLI   0(R15),C'0'  CHECK NUMERIC FOR SURE             SS MAR85
         BL    ISPPARES     NO NUMBERS                         SS MAR85
ISPPAR62 DS    0H                                              SS MAR85
         TM    0(R1),X'40'  WE LOOKING FOR NUMERIC             SS MAR85
         BZ    ISPPARES     NO NUMBERS ALLOWED                 SS MAR85
         ST    R15,SPFPRSV  SAVE LOCATION                      SS MAR85
ISPPAR63 CLI   0(R15),C'0'  FIND ALL THE NUMBERS               SS MAR85
         BL    ISPPAR65     END OF NUMBERS                     SS MAR85
         LA    R15,1(,R15)  NEXT                               SS MAR85
         BCT   R14,ISPPAR63 CONTINUE                           SS MAR85
ISPPAR65 ST    R14,SPFPRLL  SAVE                               SS MAR85
         L     R14,SPFPRSV  GET ADDRESS NUMBER                 SS MAR85
         LR    R1,R15                                          SS MAR85
         SLR   R1,R14       CALCULATE LENGTH                   SS MAR85
         BCTR  R1,0         ON LESS FOR PACK                   SS MAR85
         EX    R1,ISPPAR95  DO PACK                            SS MAR85
         TM    SPFPRRC,X'80'  WAS IT NEG                       SS MAR85
         BZ    ISPPAR66                                        SS MAR85
         NI    WORKTBL+7,X'FD' MAKE NEG DECIMAL                SS MAR85
ISPPAR66 DS    0H                                              SS MAR85
         CVB   R14,WORKTBL                                     SS MAR85
         TM    SPFPRFG,SPFPRN1  WE HAVE FIRST NUMBER           SS NOV85
         BO    ISPPAR6A                                        SS NOV85
         ST    R14,SPFPRNU  SAVE VALUE                         SS MAR85
         B     ISPPAR6B                                        SS NOV85
ISPPAR6A ST    R14,SPFPRNU2                                    SS NOV85
ISPPAR6B OI    SPFPRFG,SPFPRN1 GOT FIRST NUMBER                SS NOV85
         L     R1,SPFPRAD   GET ADDRESS OF PARM                SS MAR85
         TM    0(R1),X'10'  CHECK RANGE REQUIRED               SS MAY85
         BZ    ISPPAR67     NO                                 SS MAY85
         C     R14,4(,R1)   LOWER RANGE                       ABL MAR86
         BL    ISPPARER     LOWER IS ERROR                     SS MAY85
         C     R14,8(,R1)   UPPER RANGE                       ABL MAR86
         BH    ISPPARER     UPPER IS ERROR                     SS MAY85
ISPPAR67 DS    0H                                              SS MAY85
         MVI   SPFPRRC,0    GOOD SO FAR                        SS MAY85
         OC    SPFPRBI,1(R1) OR IN PARM BIT                    SS MAR85
         LA    R1,ISPPELN(,R1) BUMP TO NEXT PARSE ENTRY        SS NOV85
         ST    R1,SPFPRAD                                      SS NOV85
         B     ISPPAR80     CONTINUE LOOKING FOR OTHER PARMS   SS MAR85
ISPPAR95 PACK  WORKTBL(8),0(0,R14)   <<EXECUTED>>              SS MAR85
ISPPAR70 DS    0H                                              SS MAR85
         CLI   0(R1),X'FF'  END OF TABLE                       SS MAR85
         BE    ISPPAREK     DONE, SETUP RC                     SS MAR85
         TM    0(R1),X'81'  IS IT A NO CUT STRING              SS OCT88
         BO    ISPPAR01     YES, CHECK IT OUT                  SS OCT88
         TM    0(R1),X'20'  IS IT KEYWORD                      SS MAR85
         BZ    ISPPAREP     INVALID PARSE PARM RC=16           SS MAR85
         TM    0(R1),X'10'  SINGLE CHAR CHECK                  SS MAY85
         BZ    ISPPAR72     NO                                 SS MAY85
         CLI   1(R15),C' '  ONLY ONE CHAR                      SS MAY85
         BNE   ISPPAR72     NO                                 SS MAY85
         OC    0(1,R15),ISPBLK8 MAKE UPPER CASE                SS MAR85
         CLC   0(1,R15),ISPPKW(R1) COMP PARM TO KEYWORD       ABL APR86
         BNE   ISPPAR75     TRY NEXT KEYWORD                   SS MAR85
         B     ISPPAR73     GOOD KEY                           SS MAR85
ISPPAR72 DS    0H                                              SS MAY85
         IC    R14,0(R1)    GET COMPARE LENGTH                 SS MAR85
         N     R14,ISPPAR97 AND OUT OTHER BITS                 SS MAR85
         OC    0(8,R15),ISPBLK8 MAKE UPPER CASE                SS MAR85
         EX    R14,ISPPAR98 COMPARE KEYWORD                    SS MAR85
         BNE   ISPPAR75     TRY NEXT KEYWORD                   SS MAR85
ISPPAR73 DS    0H                                              SS MAY85
         MVI   SPFPRRC,0    GOOD KEY                           SS MAY85
         OC    SPFPRBI,1(R1) FOUND THIS KEYWORD                SS MAR85
         B     ISPPAR80     SETUP FOR NEXT PARM                SS MAR85
ISPPAR75 DS    0H                                              SS MAR85
         LA    R1,ISPPELN(,R1) NEXT ENTRY                      SS MAR85
         B     ISPPAR70     CONTINUE                           SS MAR85
         DS    0F                                             ABL APR85
ISPPAR97 DC    X'00000007'  MASK LAST 3 BITS                   SS MAR85
ISPPAR98 CLC   0(0,R15),ISPPKW(R1) COMP PARM TO KEYWORD        SS NOV85
ISPPAR80 DS    0H                                              SS MAR85
         L     R14,SPFPRLL  GET LENGTH                         SS MAR85
         LTR   R14,R14      ANY MORE                           SS MAR85
         BZ    ISPPAR90     SETUP RC                           SS MAR85
         LA    R1,C' '      FIND NEXT BLANK                    SS MAR85
         BAS   R2,ISPCLI                                       SS MAR85
         LTR   R15,R15                                         SS MAY85
         BZ    ISPPAR90     END OF INPUT                       SS MAY85
         BAS   R2,ISPCKBLK  FIND NEXT PARM                     SS MAY85
         LTR   R15,R15                                         SS MAY85
         BZ    ISPPAR90     END OF INPUT                       SS MAY85
         B     ISPPAR50     VERIFY THIS PARM                   SS MAR85
ISPPARE8 MVI   SPFPRRC,8    NO STRING PARM                     SS MAY85
         B     ISPPAR90                                        SS MAY85
ISPPAREL MVI   SPFPRRC,12   STRING PARM TO LONG                SS MAY85
         MVC   SPFMSGID,ISPPDSM SETUP MESSAGE                  SS MAY85
         MVI   SPFMSGID+7,C'A'                                 SS MAY85
         MVI   SPFMSGID+6,C'3'                                 SS MAY85
         B     ISPPAR90                                        SS MAY85
ISPPARES MVI   SPFPRRC,16   SYNTAX ERROR IN NUMERIC            SS MAY85
         MVC   SPFMSGID,ISPPDSM SETUP MESSAGE                  SS MAY85
         MVI   SPFMSGID+7,C'A'                                 SS MAY85
         MVI   SPFMSGID+6,C'4'                                 SS MAY85
         B     ISPPAR90                                        SS MAY85
ISPPARER MVI   SPFPRRC,20   RANGE  ERROR IN NUMERIC            SS MAY85
         MVC   SPFMSGID,ISPPDSM SETUP MESSAGE                  SS MAY85
         MVI   SPFMSGID+7,C'A'                                 SS MAY85
         MVI   SPFMSGID+6,C'5'                                 SS MAY85
         B     ISPPAR90                                        SS MAY85
ISPPAREK MVI   SPFPRRC,24   INVALID KEY                        SS MAY85
         MVC   SPFMSGID,ISPPDSM SETUP MESSAGE                  SS MAY85
         MVI   SPFMSGID+7,C'A'                                 SS MAY85
         MVI   SPFMSGID+6,C'6'                                 SS MAY85
         B     ISPPAR90                                        SS MAY85
ISPPAREP MVI   SPFPRRC,32   INVALID PARSE LIST                 SS MAY85
         DC    H'0'         TEST .. FORCE  0C1                 SS MAY85
         B     ISPPAR90                                        SS MAY85
ISPPAR90 DS    0H                                              SS MAR85
         SR    R15,R15                                         SS MAR85
         IC    R15,SPFPRRC                                     SS MAR85
         BR    R3                                              SS MAR85
         DROP  R4                                              SS MAR85
         TITLE  'ISPF - FIND SUBCMD VERB IN TABLE'             SS AUG86
         SPACE 2                                               SS NOV85
*                                                              SS NOV85
*  FIND SUBCMD VERB IN TABLE                                   SS NOV85
*                                                              SS NOV85
         USING *,R4                                            SS MAR85
ISPVER11 DS    0H                                              SS MAR85
         LR    R2,R3                                           SS NOV85
         CLI   0(R1),C' '     SCAN FIRST NON BLANK             SS NOV84
         BNE   ISPVER2                                         SS SEP84
         LA    R1,1(,R1)      NEXT CHAR                        SS SEP84
         BCT   R15,ISPVER11                                    SS NOV84
ISPVER16 DS    0H            BLANK INPUT                       SS NOV84
         ST    R1,SPFVERB    SAVE ADDRESS                      SS NOV84
         LA    R3,1(,R1)     RESUME SCAN AT NEXT CHAR          SS SEP84
         ST    R3,SPFOPER    SAVE ADDRESS                      SS NOV84
         SR    R3,R3         LENGTH 0                          SS NOV84
         MVI   SPFNAMEH,ISPBLKC BLANK OUT CMD                  SS NOV84
         B     ISPVER5                                         SS NOV84
ISPVER2  LA    R15,8         EXPAND SCAN                       SS SEP84
         ST    R1,SPFVERB    SAVE ADDRESS                      SS NOV84
         LA    R3,1(,R1)     RESUME SCAN AT NEXT CHAR          SS SEP84
ISPVER3  CLI   0(R3),C' '    SCAN NEXT BLANK                   SS SEP84
         BE    ISPVER4                                         SS SEP84
         CLI   0(R3),C'.'    SCAN NEXT PERIOD (PANEL SUPPORT)  SS NOV84
         BE    ISPVER4                                         SS NOV84
         LA    R3,1(,R3)     NEXT CHAR                         SS SEP84
         BCT   R15,ISPVER3                                     SS SEP84
ISPVER20 LA    R15,20        INVALID SYNTAX                    SS SEP84
ISPVER4  DS    0H                                              SS SEP84
         ST    R3,SPFOPER    SAVE ADDRESS                      SS NOV84
         SR    R3,R1         LENGTH                            SS SEP84
         BCTR  R3,0          LESS ONE FOR CLC                  SS SEP84
         MVC   SPFNAMEH,0(R1)   SAVE THE SUBCOMMAND NAME      ABL OCT84
         OC    SPFNAMEH,ISPBLK8 UPPER CASE THE NAME           ABL OCT84
ISPVER5  DS    0H
         TM    $TLF1(R14),#E END OF TABLE                      SS NOV84
         BO    ISPVER12      NOT VALID                         SS NOV84
         LA    R15,$TLCM(R14) POINT TO VALUE IN ENTRY          SS JUL85
ISPVER7  DS    0H                                              SS SEP84
         CLC   0(0,R15),SPFNAMEH  MATCH TABLE AND SUBCOMMAND  ABL OCT84
         EX    R3,*-6        FOR LENGTH OF CMD                 SS SEP84
         BE    ISPVER8       MATCH                             SS SEP84
         LA    R14,ISPTEL(,R14)  NEXT ENTRY                    SS SEP84
         B     ISPVER5                                         SS SEP84
ISPVER8  DS    0H                                              SS SEP84
         ST    R14,SPFVTPT   SAVE TABLE ENTRY ADDR             SS NOV85
         TM    $TLF1(R14),#K FORCE MEMLIST INACTIVE            SS SEP84
         BO    ISPVER04                                        SS SEP84
         SR    R15,R15       GOOD CMD                          SS SEP84
         BR    R2                                              SS SEP84
ISPVER04 LA    R15,04        FORCE MEMLIST INACTIVE            SS SEP84
         BR    R2                                              SS SEP84
ISPVER08 LA    R15,08        NOT USED                          SS AUG86
         BR    R2                                              SS SEP84
ISPVER12 LA    R15,12        NOT FOUND                         SS SEP84
         ST    R14,SPFVTPT   SAVE TABLE ENTRY ADDR             SS NOV84
         BR    R2                                              SS SEP84
         DROP  R4                                              SS MAR85
         TITLE 'COMMAND PROCESSING'                            SS NOV85
ISPCMDCK DS    0H                   CHECK FOR A PDS COMMAND    SS NOV84
         USING *,R4                                            SS NOV85
         LA    R1,SPFINPUT                                     SS NOV85
         SR    R15,R15                                         SS NOV85
         L     R14,SPFVTPT          RELOAD R14                 SS NOV85
         IC    R15,$TLEX(R14)       GET ANY EXTEND CODE        SS NOV85
         TM    $TLF2(R14),#C        COMMAND TABLE IN EXTEND    SS NOV85
         BO    *+8                  YES, USE IT                SS NOV85
         LA    R15,0                NO, USE PRIMARY            SS NOV85
         L     R14,ISPCMDTT(R15)    INDEX TO TABLE             SS NOV85
         BAS   R2,ISPVERG                                      SS NOV84
         C     R15,ISPL8                                       SS NOV84
         BE    ISPCMDIV                                        SS NOV84
         CH    R15,ISPH12           INVALID COMMAND?           SS NOV84
         BNE   ISPCMDC1             NO, BRANCH                ABL DEC84
         CLI   SPFNAMEH,C'?'        QUESTION MARK?            ABL DEC84
         BE    ISPCMDC0             YES, ALLOW IT             ABL DEC84
         CLI   SPFNAMEH,C'.'        . OF .?                   ABL DEC84
         BE    ISPCMDC0             YES, ALLOW IT             ABL DEC84
         CLI   SPFNAMEH,C'%'        IMPLIED CLIST?            ABL DEC84
         BE    ISPCMDC0             YES ALLOW IT               SS NOV86
         TM    $TLF2(R14),#T        INTERNAL PROCESSOR ON #E   SS NOV86
         BO    ISPCMDC3             GO FOR IT                  SS NOV86
         B     ISPCMDIV             NO, MUST BE INCORRECT      SS NOV86
*        BNE   ISPCMDIV             YES, INCORRECT            ABL DEC84
ISPCMDC0 L     R14,ISPEXECA         ADDRESS OF EXEC           ABL DEC84
         ST    R14,SPFVTPT          SAVE THE ADDRESS           SS NOV85
ISPCMDC1 DS    0H                                             ABL DEC84
         TM    $TLF1(R14),#S        SOURCE ONLY?               SS NOV84
         BZ    ISPCMDC2             NO, BRANCH                 SS NOV84
         TM    FLAGSCC,RECFMU       LOAD LIBRARY?              SS NOV84
         BO    ISPCMDIV             YES, BAD                   SS NOV84
         B     ISPCMDC3                                        SS NOV84
ISPCMDC2 DS    0H                                              SS NOV84
         TM    $TLF1(R14),#U        LOAD LIBRARY ONLY?         SS NOV84
         BZ    ISPCMDC3             NO, BRANCH                 SS NOV84
         TM    FLAGSCC,RECFMU       LOAD LIBRARY?              SS NOV84
         BZ    ISPCMDIV             NO, BAD                    SS NOV84
ISPCMDC3 DS    0H                                              SS NOV84
         TM    $TLF2(R14),#T         COMMAND PROCESS PROVIDED  SS MAR85
         BZ    ISPCMDC7              NO, BRANCH                SS MAR85
         SR    R15,R15                                         SS NOV85
         IC    R15,$TLFU(R14)        GET FUNCTION CODE         SS NOV85
         BR    R3                    RETURN TO ROUTER          SS NOV85
ISPCMDIV DS    0H                                              SS NOV85
         LA    R15,28                ROUTE TO INVALID          SS NOV85
         BR    R3                    RETURN TO ROUTER          SS NOV85
ISPCMDC7 DS    0H                                             ABL DEC84
         LA    R15,20                ROUTE TO CONTINUE         SS AUG86
         BR    R3                    RETURN TO ROUTER          SS AUG86
         DROP  R4                                             DRK NOV98
         TITLE 'COMMAND PROCESSING CONTINUED'                  SS AUG86
ISPCMDCC DS    0H                   CHECK FOR A PDS COMMAND    SS NOV84
         USING *,R4                                            SS NOV85
         L     R14,SPFVTPT          RESTORE POINTER           ABL JAN87
         CLI   SPFFUNST+1,C'I'      MEMLIST ACTIVE             SS AUG86
         BE    ISPCMDC4             NO, SKIP                   SS AUG86
         TM    SPFTBPF1,SPFTLIN     THIS MEMLIST PANEL         SS AUG86
         BZ    ISPCMDCO             NO, SKIP                   SS AUG86
         CLC   0(8,R14),$SUL        SUBLIST?                  ABL APR86
         BNE   ISPCMDCO             NO, BRANCH                ABL APR86
         TM    SPFLAG0,SPFVIEW      VIEW MODE?                ABL APR86
         BO    ISPCMDCO             YES, IGNORE = OPERAND     ABL APR86
         L     R15,SPFOPER          GET BLANK AFTER COMMAND   ABL APR86
         LA    R14,20               LOOK THIS FAR             ABL APR86
         BAS   R2,ISPCKBLK          FIND NON-BLANK OPERAND    ABL APR86
         CLI   0(R15),C'='          OPERAND "="?              ABL APR86
         BNE   ISPCMDCO             NO, BRANCH                ABL APR86
         BAS   R2,ISPQUERY          CHECK IF ANY ENTRIES       SS AUG86
         L     R14,SPFVTPT          RESTORE POINTER            SS AUG86
         ICM   R2,15,SPFTSIZE       GET SIZE                   SS AUG86
         SR    R0,R0                NORMAL FUNCTION            SS NOV88
         LA    R15,88               FUNCTION CODE FOR SUBLIST ABL APR86
         BR    R3                   RETURN TO ROUTER          ABL APR86
ISPCMDCO DS    0H                                             ABL APR86
         L     R14,SPFVTPT          RESTORE POINTER           DRK NOV07
         TM    $TLF1(R14),#K        INACTIVATE MEMLIST?        SS MAR85
         BZ    ISPCMDCZ             NO, BRANCH                 SS MAR85
         BAS   R2,ISPUSTKM        UNSTACK, KILL, SET NEXT P ID SS AUG86
         B     ISPCMD                                          SS AUG86
ISPCMDCZ DS    0H                                              SS MAR85
         TM    $TLF2(R14),#M         COULD CHANGE STATS        SS MAR85
         BZ    ISPCMDC4                                        SS MAR85
         MVI   SPFPOST1,136         CMD POST PROCESS NUM 8     SS NOV85
         OI    SPFLAG4,SPFCPOST     SET POST PROC FLAG         SS MAR85
         CLI   SPFEDUP,C'Y'         FULL UPDATE REQUEST        SS JUL87
         BNE   ISPCMDC4                 *                      SS JUL87
         TIME  DEC                      *                      SS JUL87
         ST    R1,SPFDAY                STORE TODAYS DATE      SS JUL87
         ST    R0,SPFTIME               STORE TIME OF DAY      SS JUL87
ISPCMDC4 DS    0H                                              SS NOV84
         NI    SPFLAG3,255-SPFCONV           CLEAR SCAN FLAGS ABL JAN87
         NI    SPFLAG0,255-SPFCKER-SPFMSGER  CLEAR SCAN FLAGS ABL JAN87
* MEMLIST FUNCTION DOES ALL PANEL UNSTACKING AND SWAPPING      SS AUG86
* HIERARCHICAL SUPPORT REMOVED                                 SS OCT88
         CLI   SPFTBPFI+1,0          LOG TABLE                 SS AUG86
         BE    ISPCMD                YES, NO CHANGE            SS AUG86
         TM    $TLF1(R14),#E         NO MATCH                  SS NOV84
         BO    ISPCMDIN              INVALID MSG               SS AUG86
         TM    $TLF1(R14),#V         VIEW MODE                 SS NOV85
         BZ    ISPCMDCY              NO                        SS NOV85
ISPCMDCV DS    0H                       *                      SS AUG86
         LH    R1,SPFFUNLP+0         LOG PANEL ID              SS AUG86
         LNR   R1,R1                 SET FOR SWAP              SS AUG86
         BAS   R2,ISPSTKI            STACK ROUTINE             SS NOV85
         B     ISPCMD                                          SS NOV85
ISPCMDIN DS    0H                                              SS AUG86
         LA    R15,28                ROUTE TO INVALID          SS NOV85
         BR    R3                    RETURN TO ROUTER          SS NOV85
ISPCMDCY DS    0H                                              SS NOV85
         OI    SPFLAG0,SPFCKER       CHECK FOR PDS ERROR MSGS  SS NOV84
         OI    SPFLAG3,SPFCONV       CONVERT ERROR --> VIEW    SS NOV84
ISPCMD   DS    0H                                              SS NOV84
         LH    R15,PUTGETBF       GET LENG                     SS NOV84
         BCTR  R15,0              LESS 1 MVC                   SS NOV84
         L     R14,SPFVTPT        REFRESH R14                  SS NOV85
         LA    R2,ISPCMDNO        IGNORE TABLE                ABL APR86
         LA    R1,9               9 ENTRIES                   ABL MAR86
ISPCMDNL DS    0H                                              SS NOV85
         CLC   0(8,R14),0(R2)     COMPARE TO TABLE             SS NOV85
         BE    ISPCMDM3           SKIP SAVE                    SS NOV85
         LA    R2,8(,R2)          NEXT ENTRY                   SS NOV85
         BCT   R1,ISPCMDNL        TRY ALL 9                    SS NOV85
         CLI   SPFNAMEH,C'.'      . OF .?                     ABL FEB87
         BE    ISPCMDM3           YES, BRANCH                 ABL FEB87
         CLI   SPFNAMEH,C'?'      QUESTION MARK?              ABL FEB87
         BE    ISPCMDM3           YES, BRANCH                 ABL FEB87
ISPCMDM  MVC   SPFZSAV(0),PUTGETBF                             SS NOV84
         EX    R15,ISPCMDM        SAVE CMD FOR RECALL          SS NOV84
ISPCMDM3 DS    0H                                              SS NOV85
         OI    SPFLAG3,SPFLOGM    LOG THE COMMAND              SS DEC84
         LA    R15,112              ROUTE TO ISPCMDC           SS NOV85
         BR    R3                                              SS NOV85
         DROP  R4                                              SS MAR85
ISPCMDTT DS    0F                                              SS NOV85
         DC    A(ISP$TL2)   0   MAIN COMMAND TABLE             SS NOV85
         DC    A(ISP$TL3)   4   DISPLAY ONLY COMMANDS          SS NOV85
         DC    A(ISP$TL3)   8   PAD                            SS NOV85
ISPCMDNO DS    0F                                              SS NOV85
         DC    CL8'&NOR1',CL8'&NOR2',CL8'&NOR3'                SS NOV85
         DC    CL8'&NOR4',CL8'&NOR5',CL8'&NOR6'                SS NOV85
         DC    CL8'&NOR7',CL8'&NOR8',CL8'&NOR9'                SS NOV85
         TITLE 'COMMAND SETUP FOR PDS'                         SS NOV85
*                                                              SS NOV85
*   SETUP COMMAND FOR PDS PROCESSING                           SS NOV85
*                                                              SS NOV85
ISPCMDC  DS    0H                                              SS NOV84
         USING *,R4                                            SS NOV85
         MVC   SPFTTPT,SPFVTPT      GOOD CMD, SAVE             SS NOV85
         MVC   SPFSAVPT,SPFVTPT     SAVE POINTER               SS NOV85
         NI    SPFLAG4,255-SPF38XA  NOT PROMPTING NOW         ABL DEC84
         LA    R1,3                 POINT AT CKP PANEL         SS NOV85
         BAS   R2,ISPSTKI            STACK                     SS NOV85
         BAS   R2,ISPBOTM            GO TO BOTTOM              SS NOV85
         BAS   R2,ISPQUERY                                     SS NOV85
         L     R15,SPFLAST          SAVE INFO                  SS JUL84
         ST    R15,SPFSTART                                    SS JUL84
         LA    R1,PUTGETBF                                    ABL OCT84
         ST    R1,SPFSAV1                                     ABL OCT84
         TM    SPFLAG3,SPFLOGM    TO BE LOGGED?               ABL OCT84
         BNO   ISPNOLOG           NO, BRANCH                  ABL OCT84
         BAS   R2,ISPLOGCD        SPECIAL CODE TO LOG          SS NOV85
ISPNOLOG DS    0H                                             ABL NOV84
         TM    FLAGSFF,FLOGWRT    LOGGING TO A DATA SET?      ABL NOV84
         BNO   ISPNOLDS           NO, BRANCH                  ABL NOV84
         LA    R1,PUTGETBF        POINT TO THE DATA LINE      ABL NOV84
         L     R2,VLOGDATA        GET VCON ADDRESS             SS JUL85
         BASR  R14,R2             LOG TO THE DATA SET          SS JUL85
ISPNOLDS DS    0H                                             ABL NOV84
         L     R14,SPFVTPT        REFRESH R14                 ABL FEB87
         LA    R2,ISPNOLD9        LOG TABLE                   ABL FEB87
         LA    R1,15              15 ENTRIES                  DRK JUN12
         TM    FLAGSZZ,LOG#CMDS   LOG ALL TO ISPLOG?          DRK APR16
         BO    ISPNOLD4           YES                         DRK APR16
         LA    R15,@4K                     BASE FOR TSOUID    DRK JAN18
         CLC   TSOUID-@4K(6,R15),SYSPROG   JANKO?             DRK JAN18
         BE    ISPNOLD4                    YES, LOG ALL       DRK JAN18
         SPACE 1
ISPNOLD2 DS    0H                                             ABL FEB87
         CLC   0(8,R14),0(R2)     COMPARE TO TABLE            ABL FEB87
         BE    ISPNOLD4           LOG COMMAND                 DRK APR06
         LA    R2,8(,R2)          NEXT ENTRY                  ABL FEB87
         BCT   R1,ISPNOLD2        TRY ALL 15                  DRK AUG08
         B     ISPNOLD8           NOT FOUND EXIT              ABL FEB87
ISPNOLD4 CLI   0(R2),X'40'        MATCH ON BLANK?             ABL FEB87
         BE    ISPNOLD8           YES, IGNORE THIS ENTRY      ABL FEB87
         MVI   SPFMLIN2,X'40'     BLANK THE ERROR MESSAGE     ABL FEB87
         MVC   SPFMLIN2+1(78),SPFMLIN2                        ABL FEB87
         LA    R1,PUTGETBF        PDS SUBCOMMAND              ABL FEB87
         LH    R14,0(R1)          LENGTH                      ABL FEB87
         SH    R14,ISPH5          -4 FOR HEADER -1 FOR MVC    ABL FEB87
         CH    R14,ISPH78         MAX LENGTH                  ABL FEB87
         BNH   ISPNOLD6           OK                          ABL FEB87
         LH    R14,ISPH78         TRUNCATE                    ABL FEB87
ISPNOLD6 MVC   SPFMLIN2(0),4(R1)  MOVE TO DIALOG VARIABLE     ABL FEB87
         EX    R14,*-6                                        ABL FEB87
         LA    R14,ISPLOG         LOG FUNCTION                ABL FEB87
         LA    R15,ISPL1C         MESSAGE NUMBER              DRK AUG08
         STM   R14,R15,PARMLIST                               ABL FEB87
         OI    PARMLIST+08,X'80'                              ABL FEB87
         BAS   R2,ISPSXEQ8                                    ABL FEB87
ISPNOLD8 DS    0H                                             ABL FEB87
         LA    R14,0                                           SS JUL84
         ST    R14,SPFCKPT                                     SS JUL84
*            CODE TO DELETE OLD OUTPUT IF OVER TABLE SIZE      SS JUL84
         L     R14,SPFLAST        CURRENT SIZE                 SS JUL84
         C     R14,SPFCSIZE       SIZE LIMIT                   SS OCT88
         BNH   ISPSZOK            NOT TO BIG                   SS JUL84
         S     R14,SPFCSIZE       CALC DELETE AMOUNT           SS OCT88
         ST    R14,SPFFIXED       SAVE DELETE COUNT            SS JUL84
         L     R14,SPFCSIZE       TABLE SIZE                   SS OCT88
         LNR   R14,R14                NEGATIVE                 SS JUL84
         ST    R14,SPFFIXE2       SAVE SKIP COUNT              SS JUL84
         BAS   R2,ISPDEL          GO DELETE                    SS JUL84
         BAS   R2,ISPQUERY       GET NEW SIZE                  SS JUL84
         L     R15,SPFLAST        SAVE INFO                    SS JUL84
         ST    R15,SPFSTART                                    SS JUL84
ISPSZOK  DS    0H                                              SS NOV84
         B     ISPEXIT                                         SS NOV84
ISPNOLD9 DS    0H                                             ABL FEB87
         DC    CL8'&NLO1',CL8'&NLO2',CL8'&NLO3'               ABL FEB87
         DC    CL8'&NLO4',CL8'&NLO5',CL8'&NLO6'               ABL FEB87
         DC    CL8'&NLO7',CL8'&NLO8',CL8'&NLO9'               ABL FEB87
         DC    CL8'&NLOA',CL8'&NLOB',CL8'&NLOC'               DRK AUG08
         DC    CL8'&NLOD',CL8'&NLOE',CL8'&NLOF'               DRK APR16
SYSPROG  DC    CL6'TRIDJK'                                    DRK JAN18
         DROP  R4                                              SS MAR85
         TITLE 'ISPF - COMMAND OPTION FIND ROUTINE  '          SS MAY88
ISPOPTT  DS    0H                                              SS MAY88
         USING ISPOPTT,R4                                      SS MAY88
         L     R14,SPFTBPCD       LOAD COMMAND TABLE ADDR      SS NOV85
         LA    R1,SPFINPUT        POINT TO INPUT               SS NOV84
         BAS   R2,ISPVERG         CHECK IT                     SS NOV84
         C     R15,ISPL8                                       SS NOV84
         BE    ISPOPTE            TOO LONG                     SS MAY88
         CH    R15,ISPH12                                      SS NOV84
         BE    ISPOPT3            SHOULD BE A COMMAND          SS NOV84
         C     R15,ISPL4                                       SS NOV84
         BNE   ISPOPT4                                         SS NOV84
         BAS   R2,ISPUSTKM        UNSTACK AND KILL MEMLIST     SS NOV85
         L     R14,SPFVTPT                                     SS NOV85
ISPOPT4  DS    0H                                              SS NOV84
         L     R15,SPFOPER                                     SS NOV84
         CLI   0(R15),C'.'        DOT                          SS NOV84
         BNE   ISPFUNC                                         SS NOV84
         L     R1,SPFSEC4A        BASE                         SS OCT88
         MVC   SPFLSAVZ-SPFSEC4K(250,R1),SPFINPUT              SS OCT88
*                                 SAVE FOR REPROCESSING        SS OCT88
         OI    SPFLAG4,SPFMCMD         FOR MULTIPLE COMMANDS   SS NOV84
         B     ISPFUNC                                         SS NOV84
ISPOPT3  DS    0H                                              SS NOV84
         CLI   SPFNAMEH,C'0'      NUMERIC?                    ABL DEC84
         BL    ISPFUNC            NO, MUST BE A COMMAND        SS NOV84
         TM    SPFLAG4,SPF38XA    PROMPTING FOR PDS3..A?      ABL NOV87
         BO    ISPFUNC            YES, ALLOW IT               ABL NOV87
ISPOPTE  DS    0H                       *                      SS MAY88
         LA    R15,28             ISPOPTIV                     SS MAY88
         BR    R3                 ROUT TO IT                   SS MAY88
*        B     ISPOPTIV           MUST BE INVALID              SS NOV84
         SPACE 2                                               SS JUL87
ISPFUNC  DS    0H                                              SS NOV84
         L     R14,SPFVTPT                                     SS NOV85
         SR    R15,R15                                         SS NOV84
         IC    R15,$TLFU(R14)                                  SS JUL85
         SR    R0,R0              ZERO SUB FUNCTION ASSUMED    SS MAY88
         BR    R3                       *                      SS MAY88
         DROP  R4                                              SS NOV85
         SPACE 4                                               SS NOV85
         TITLE 'ISPF - INVALID OR OTHER ERROR PROMPT'          SS DEC87
* PROMPT FOR CORRECTED COMMAND                                 SS DEC87
ISPVERR  DS    0H          ****   ERROR ******                 SS DEC87
         USING ISPVERR,R4                                      SS DEC87
         ST    R0,SPFTSV1          SAVE PANEL ADDRESS          SS OCT88
         BAS   R2,ISPSETVI                                     SS DEC87
         MVC   SPFVZCMD(250),SPFINPUT  GET INVALID COMMAND     SS DEC87
         L     R15,SPFTSV1         GET PANEL ADDRESS           SS OCT88
         LTR   R15,R15             DEFAULT PANEL REQUEST       SS OCT88
         BNZ   *+8                 NO, USE PASSED PANEL ADDR   SS OCT88
         LA    R15,ISPVERRP        ADDRESS                     SS DEC87
         LA    R1,SPFMSGID         NO MESSAGE                  SS DEC87
         BAS   R2,ISPSINGI         DISPLAY THIS PANEL          SS DEC87
         LTR   R15,R15             NO END KEY                  SS DEC87
         BNZ   ISPVERRE            END KEY.. NO EXECUTE        SS DEC87
         MVC   SPFINPUT(250),SPFZSEL                           SS DEC87
         MVC   PUTGETBF(4),ISPLEN2                             SS DEC87
         LA    R15,104              ISPOPTT                    SS DEC87
         BR    R3                   RETURN TO ROUTER           SS DEC87
ISPVERRE DS    0H                       *                      SS DEC87
         LA    R15,100              RETURN TO NORMAL PANEL     SS DEC87
         BR    R3                   RETURN TO ROUTER           SS DEC87
ISPVERRP DC    CL8'&P.VERR'             *                      SS DEC87
         DROP  R4                                              SS DEC87
         TITLE 'ISPF - PUTGET ROUTINE           '              SS NOV85
ISPPTGT0 DS    0H                                              SS MAY88
         USING ISPPTGT0,R4                                     SS MAY88
         MVC   SPFVTPT,SPFSAVPT       RESTORE   POINTER        SS NOV85
*        LA    R1,3                 PANEL 3                    SS NOV85
*        CH    R1,SPFTBPI           THE SAME                   SS NOV85
*        BNE   ISPPTGT9             NO                         SS NOV85
         TM    SPFTBPF2,SPFTCKPT    CHECKPOINT TABLE           SS OCT88
         BZ    ISPPTGT9             NO                         SS OCT88
         LA    R1,0                 POP IT                     SS NOV85
         BAS   R2,ISPSTKI           DO IT                      SS NOV85
ISPPTGT9 DS    0H                                              SS NOV85
         NI    MEMFLAG2,255-MEMLTMCK-MEMLTMPR-MEMLTMLK RESET   SS AUG86
         NI    MEMFLAG2,255-MEMLTMED-MEMLALED          RESET   SS NOV88
         CLI   SPFALCK,C'Y'         DO ALIAS IS DEFAULT        SS AUG86
         BNE   ISPPTGTN             NO                         SS AUG86
         TM    MEMFLAG2,MEMLNTBL    NEW TABLE                  SS AUG86
         BZ    ISPPTGTN             NO                         SS AUG86
         OI    SPFLAG4,SPFCPOST     SET UP TO GO TO            SS AUG86
         MVI   SPFPOST1,80              ALIAS CHECK            SS AUG86
         B     ISPPTGTO             CONTINUE                   SS AUG86
         SPACE 2
ISPPTGTN DS    0H                       *                      SS AUG86
         NI    MEMFLAG2,255-MEMLNTBL-MEMLNALL  RESET NEW MEML  SS AUG86
ISPPTGTO DS    0H                       *                      SS AUG86
         LA    R1,PUTGETBF            SETUP BUFFER             SS JUL84
         ST    R1,ADDRCBUF                        ADDRESS      SS JUL84
         L     R1,SPFSAV1         OUTPUT BUFFER                SS AUG84
         NI    SPFLAG2,255-SPFUPDA  CLEANUP                    SS AUG86
         NI    SPFLAG4,255-SPF38XA  CLEAR PROMPTING FLAG      ABL DEC84
         CLI   10(R1),C'A'          POSSIBLE ACTION MESSAGE?  ABL DEC84
         BNE   ISPPTGT1             NO, BRANCH                ABL DEC84
         CLC   4(4,R1),ISP300A      PDS3...?                  ABL DEC84
         BNE   ISPPTGT1             NO, BRANCH                ABL DEC84
         CLI   8(R1),C'8'           PDS38.. OR PDS39..?       ABL DEC84
         BL    ISPPTGT1             NO, BRANCH                ABL DEC84
         TM    9(R1),X'F0'          PDS38X. OR PDS39X.?       ABL DEC84
         BNO   ISPPTGT1             NO, BRANCH                ABL DEC84
         OI    SPFLAG4,SPF38XA      YES, SET A FLAG FOR LATER ABL DEC84
         CLC   4+3(3,R1),ISPDSNPC  DSN PROMPT                  SS NOV88
         BNE   ISPPTGTC             NO, CONTINUE               SS NOV88
         MVC   SPFMLIN2,SPFLIN1     GET PREVIOUS MSG FROM LOG  SS NOV88
         LA    R1,9               PANEL 9                      SS NOV88
         BAS   R2,ISPSTKI         STACK IT                     SS NOV88
         MVC   SPFMSGID,ISPPDSM   SET MESSAGE ID               SS NOV88
         MVI   SPFMSGID+6,C'1'    MODIFY MSG  TO PDSM..1R      SS NOV88
         MVI   SPFMSGID+7,C'A'                                 SS NOV88
         BAS   R2,ISPADDLN        ADD DSN MSG TO LOG           SS NOV88
         B     ISPDSNP            DSN PROMPT PANEL             SS NOV88
ISPPTGTC DS    0H                       *                      SS NOV88
         MVI   SPFMLIN2,C' '        CLEAR OUTPUT LINE         ABL MAR86
         MVC   SPFMLIN2+1(78),SPFMLIN2                        ABL MAR86
         LH    R14,0(R1)         LENGTH                       ABL MAR86
         SH    R14,ISPH5         -4 FOR HEADER -1 FOR MVC     ABL MAR86
         CH    R14,ISPH78        MAX LENGTH                   ABL MAR86
         BNH   ISPPTGTM          OK                           ABL MAR86
         LH    R14,ISPH78        TRUNCATE                     ABL MAR86
ISPPTGTM MVC   SPFMLIN2(0),4(R1) MOVE TO DIALOG VARIABLE      ABL MAR86
         EX    R14,*-6                                         SS JUL84
         LA    R1,9               PANEL 9                      SS NOV85
         BAS   R2,ISPSTKI         STACK IT                     SS NOV85
         MVC   SPFMSGID,ISPPDSM   SET MESSAGE ID              ABL JUN85
         MVI   SPFMSGID+6,C'1'    MODIFY MSG  TO PDSM..1R     ABL JUN85
         MVI   SPFMSGID+7,C'R'                                ABL JUN85
ISPPTGT1 DS    0H                                             ABL DEC84
         TM    SPFLAG3,SPFCHN     HAS CHANGE DSN BEEN DONE     SS SEP84
         BZ    ISPPTGT2           NO, SKIP                     SS AUG84
         NI    SPFLAG3,255-SPFCHN-SPF300A CLEAR                SS SEP84
         BAS   R2,ISPUSTKM        UNSTACK                      SS NOV85
         L     R1,SPFSAV1         OUTPUT BUFFER                SS AUG84
ISPPTGT2 DS    0H                                              SS SEP84
         CLC   4(7,R1),ISP300A    SAME OLD MSG                 SS AUG84
         BE    ISPPTGT3           YES, CHECK                   SS AUG84
         BAS   R2,ISPADDLN        NO, DISPLAY IT               SS AUG84
         B     ISPDISPL                                        SS SEP84
ISPPTGT3 DS    0H                                              SS SEP84
         TM    SPFLAG3,SPF300A    HAS 300A OUTPUT ONCE         SS SEP84
         BO    ISPDISPL           YES, SKIP                    SS SEP84
         OI    SPFLAG3,SPF300A    NOW 300A OUTPUT ONCE         SS SEP84
         BAS   R2,ISPADDLN        DISPLAY IT                   SS AUG84
ISPDISPL DS    0H                                              SS JUL84
         TM    SPFLAG4,SPF38XA    NEED A REPLY                 SS NOV85
         BO    ISPDISP4           YES                          SS NOV85
         TM    SPFTFLG1,SPFTLPOS  POST PROCESSING REQUIRED     SS NOV85
         BZ    ISPNPOST           NO                           SS NOV85
         LH    R15,SPFTBPLF       GET FUNCTION CODE            SS NOV85
         BR    R3                                              SS MAY88
ISPNPOST DS    0H                                              SS NOV85
         TM    SPFLAG3,SPFCONV    CONVERT AN ERROR TO VIEW?   ABL OCT84
         BZ    ISPDISP5           NO, BRANCH                   SS NOV85
         TM    SPFLAG0,SPFMSGER   ANY ERRORS?                  SS NOV85
         BZ    ISPDISP5           YES, DISPLAY NOW             SS MAY88
         LH    R1,SPFFUNLP+0         LOG PANEL ID              SS AUG86
         LNR   R1,R1                 SET FOR SWAP              SS AUG86
         BAS   R2,ISPSTKI         STACK PANEL                  SS NOV85
         NI    SPFLAG4,255-SPFCPOST-SPFMCMD TERMINATE OTHERS   SS NOV85
         NI    SPFLAG5,255-SPFICMD                             SS JUL87
ISPDISP4 DS    0H                                              SS MAY88
         LA    R15,100            ROUTE TO PANEL DISPLAY       SS MAY88
         BR    R3                 ROUTE SUPPORT                SS MAY88
ISPDISP5 DS    0H                       *                      SS MAY88
         LA    R15,40             ROUTE TO ISPDISP6            SS MAY88
         BR    R3                 ROUTE SUPPORT                SS MAY88
* PROMPT FOR DATASET NAME                                      SS JUL87
ISPDSNP  DS    0H                                              SS MAY88
         MVI   SPFVZCMD,C' '                                   SS JUL87
         MVC   SPFVZCMD+1(249),SPFVZCMD                        SS JUL87
         L     R1,SPFSEC4A         GET  BASE                   SS MAY88
         LA    R1,SPFCHSV-SPFSEC4K(,R1)  POINT TO SPFCHSV      SS MAY88
         MVC   SPFVZCMD(128),0(R1)  SET  CHANGE COMMAND        SS MAY88
         BAS   R2,ISPSETVI                                     SS JUL87
         SR    R15,R15                                         SS JUL87
         LA    R15,ISPDSNPP        ADDRESS                     SS JUL87
         LA    R1,SPFMSGID         SET MESG ID                 SS NOV88
         BAS   R2,ISPSINGI         DISPLAY THIS PANEL          SS JUL87
         LTR   R15,R15             NO END KEY                  SS JUL87
         BNZ   ISPDSNPE            END KEY.. NO EXECUTE        SS JUL87
         L     R1,SPFSEC4A         GET  BASE                   SS MAY88
         LA    R1,SPFCHSV-SPFSEC4K(,R1)  POINT TO SPFCHSV      SS MAY88
         CLI   SPFZSEL,C'?'        USER QUESTION               SS NOV88
         BE    ISPDISP4            YES, DISPLAY LOG            SS NOV88
         MVC   0(128,R1),SPFZSEL   NEW DSN USER TRYING         SS MAY88
         MVC   SPFINPUT(250),SPFZSEL                           SS JUL87
         MVC   PUTGETBF(4),ISPLEN2                             SS JUL87
         LA    R15,104              ISPOPTT                    SS JUL87
         BR    R3                   RETURN TO ROUTER           SS JUL87
ISPDSNPE DS    0H                       *                      SS JUL87
         SR    R15,R15              ZERO FUNCTION CODE (END)   SS JUL87
         BR    R3                   RETURN TO ROUTER           SS JUL87
ISPDSNPP DC    CL8'&P.DSNP'             *                      SS JUL87
ISPDSNPC DC    C'381'               PDS381A MSG NUMBER         SS JUL87
         DROP  R4                                              SS JUL87
