         TITLE 'ISPF - INITIALIZATION 1 SUBROUTINE'            SS NOV85
* INITIALIZE THE DIALOG VARIABLES                              SS JUL84
ISPVINIT DS    0H                                              SS MAR85
         USING *,R4                                            SS MAR85
         MVI   SPFINPUT,C' '           BLANK IF NOT USED YET   SS NOV84
         MVC   SPFINPUT+1(48),SPFINPUT                         SS NOV84
         MVC   SPFZSAV(13),ISPENDIT  DEFAULT                   SS NOV84
         MVC   SPFCOMPP(8),ISPCOMPP  DEFAULT                  DRK SEP05
         L     R1,VPDSMAIN           PDSMAIN                  DRK MAY13
         MVC   SPFPGMVV(10),21(R1)   PDS VERSION NUMBER       DRK MAY13
         XC    SPFVFLGS,SPFVFLGS  CLEAR ODD VARIABLE FLAGS     SS NOV85
* CHECK FOR VER 2 OR LATER              *                      SS AUG86
         LA    R14,VCOPY            GET THE ENVIRONMENT        SS MAR85
         ST    R14,PARMLIST                                    SS MAR85
         LA    R14,VZENVIR          GET ISPF ENVIRONMENT       SS MAR85
         L     R15,ISPL32           SET LENGTH                 SS MAR85
         ST    R15,SPFFIXED            TO 32                   SS MAR85
         LA    R15,SPFFIXED               USE AS PARM          SS MAR85
         LA    R0,SPFZENVI                                     SS MAR85
         LA    R1,ISPMOVE                                      SS MAR85
         STM   R14,R1,PARMLIST+4                               SS MAR85
         OI    PARMLIST+16,X'80'                               SS MAR85
         LA    R1,PARMLIST                                     SS MAR85
         L     R15,ISPLINK                                     SS MAR85
         BASR  R14,R15                                         SS MAR85
         CH    R15,ISPH16           SHORT                      SS AUG86
         BE    ISPVINT1             CONTINUE ANYWAY            SS AUG86
         C     R15,ISPL8            RC=8 -- FROM ISPFV1?      ABL APR85
         BL    ISPVINT1             RC 0 FROM ISP V2           SS AUG86
         BH    ISPERR               RC 20 SEVER ERROR          SS AUG86
         B     ISPCLN               CLEANUP AND EXIT           SS AUG86
ISPVINT1 DS    0H                                              SS AUG86
         L     R1,CVTPTR(,R0)           CVT                    SS JUL88
         TM    CVTDCB-CVT(R1),CVTMVSE   XA SYSTEM              SS JUL88
         BZ    *+8                      NO                     SS JUL88
         OI    SPFLAG5,SPFXA            FLAG FOR XA            SS JUL88
         BAS   R2,ISPCTLSM         REFRESH FIRST TIME          SS AUG86
         LA    R14,VDEFINE         START VDEFINES              SS JUL84
         ST    R14,PARMLIST                                    SS JUL84
         LA    R14,VSPFVFIX        ALL FIXED DIALOG VERBS      SS SEP84
         LA    R15,SPFFIXED                                    SS JUL84
         LA    R0,ISPFIX                                       SS JUL84
         LA    R1,ISPL4                                        SS JUL84
         STM   R14,R1,PARMLIST+4                               SS JUL84
         OI    PARMLIST+16,X'80'                               SS JUL84
         LA    R1,PARMLIST                                     SS JUL84
         L     R15,ISPLINK                                     SS JUL84
         BASR  R14,R15                                         SS JUL84
         LTR   R15,R15                                         SS JUL84
         BNZ   ISPERR                                          SS JUL84
         LA    R1,ISPVIV                                       SS NOV85
         BAS   R2,ISPVDEFX                                     SS NOV85
         LA    R1,ISPVIV2                                      SS AUG86
         BAS   R2,ISPVDEFX                                     SS AUG86
         B     ISPVINT2                                        SS NOV85
         DS    0F                                              SS NOV85
ISPVIV   DC    H'0',X'80',X'00'                                SS NOV85
         DC    A(VZCMD),AL2(SPFVZCMD-SPWRKC),H'250'            SS NOV85
         DC    A(VZVERB),AL2(SPFRETRN-SPWRKC),H'8'             SS AUG86
         DC    A(VZSCRLA),AL2(SPFSCRLA-SPWRKC),H'4'            SS OCT88
         DC    A(VSPFMSG1),AL2(SPFMLIN1-SPWRKC),H'8'           SS NOV85
         DC    A(VSPFMSG2),AL2(SPFMLIN2-SPWRKC),H'79'          SS NOV85
         DC    A(VSPFZSEL),AL2(SPFZSEL-SPWRKC),H'250'          SS AUG86
         DC    A(VPDSTNAM),AL2(SPFTBL-SPWRKC),H'8'             SS AUG86
         DC    A(VPDSTCOD),AL2(SPFTBL+2-SPWRKC),H'1'           SS AUG86
         DC    A(VZERRMSG),AL2(SPFZERR-SPWRKC),H'8'            SS AUG86
         DC    A(VPDSUSRX),AL2(SPFUSRXX-SPWRKC),H'8'           SS AUG86
         DC    A(VPDSUSRX+09),AL2(SPFUSRXX-SPWRKC),H'8'        SS AUG86
         DC    A(VPDSUSRX+18),AL2(SPFUSRXX-SPWRKC),H'8'        SS AUG86
         DC    A(VPDSUSRX+27),AL2(SPFUSRXX-SPWRKC),H'8'        SS AUG86
         DC    A(VPDSUSRX+36),AL2(SPFUSRX2-SPWRKC),H'8'        SS OCT88
         DC    A(VPDSUSRX+45),AL2(SPFUSRX2-SPWRKC),H'8'        SS OCT88
         DC    A(VPDSLN1),AL2(SPFLIN1-SPWRKC),H'79'            SS OCT88
         DC    A(VPDSLN2),AL2(SPFLIN2-SPWRKC),H'79'            SS OCT88
         DC    A(VPDSLN1W),AL2(SPFLIN1W-SPWRKC),H'158'        DRK JUL04
         DC    A(VPDSSTX),AL2(SPFFUNST-SPWRKC),H'1'            SS AUG86
         DC    A(VPDFLTX),AL2(SPFMEMP-SPWRKC),H'3'             SS AUG86
         DC    A(VPDSSEL),AL2(SPFSELFU-SPWRKC),H'4'            SS JUL87
         DC    A(VPDSPASS),AL2(SPFPASST-SPWRKC),H'8'           SS JUL87
         DC    A(VPDSBLK),AL2(SPFBLKCM-SPWRKC),H'4'            SS DEC87
         DC    A(VPDSTLIM),AL2(SPFTLIM-SPWRKC),H'6'            SS NOV88
         DC    A(VPDSFIND),AL2(SPFFDSAV-SPWRKC),H'32'         DRK DEC02
         DC    A(VPDSCOMP),AL2(SPFCOMPP-SPWRKC),H'8'          DRK SEP05
         DC    A(VPDSVERS),AL2(SPFPGMVV-SPWRKC),H'10'         DRK MAY13
         DC    X'FF'                                           SS NOV85
VSPFZSEL DC    C'PDSZSEL '                                     SS AUG86
VPDSTNAM DC    C'PDSTNAME '             *                      SS AUG86
VPDSTCOD DC    C'PDSTCODE '             *                      SS AUG86
VZERRMSG DC    C'ZERRMSG '              *                      SS AUG86
VPDSUSRX DC    CL9'USRLEFT'             *                      SS AUG86
         DC    CL9'USRRIGHT'            *                      SS AUG86
         DC    CL9'USRRFIND'            *                      SS AUG86
         DC    CL9'USRRCHG '            *                      SS OCT88
         DC    CL9'USRUP   '            *                      SS OCT88
         DC    CL9'USRDOWN '            *                      SS AUG86
VPDSPASS DC    C'PDSPASS '                                     SS JUL87
VPDSSTX  DC    C'(PDSSTL PDSSTM PDSSTA PDSSTV '                SS OCT88
         DC    C'PDSSTW PDSSTC PDSSTT PDSSTX '                 SS OCT88
         DC    C'PDSSTF PDSSTS PDSSTI PDSSTN '                 SS OCT88
         DC    C')'        END DEFAULT LIST                    SS OCT88
VPDFLTX  DC    C'(PDSMEMP PDSLKDT PDSALCK PDSALUP PDSENDX '    SS OCT88
         DC    C' PDSTDEL PDSTOLD PDSSWAP PDSEDUP PDSENDP '    SS DEC87
         DC    C' PDSSPAC PDSSPWA '                            SS OCT88
         DC    C' PDSLFOLD PDSLFDEL PDSLFAUT PDSLFPRM '        SS NOV88
         DC    C' PDSTPSET PDSTPLOG PDSTPCPY '                 SS NOV88
* SORT DEFAULTS  (CONTINUATION TO ABOVE) MUST BE 9 BYTES EACH  SS OCT88
VPDFSRT  DS    0X   INDEXED BY SORT                            SS OCT88
         DC    C'PDSSRTDM PDSSRTDU PDSSRTDV '                  SS OCT88
         DC    C'PDSSRTDA PDSSRTDF PDSSRTDC '                  SS OCT88
         DC    C'PDSSRTDX '                                    SS OCT88
         DC    C')'        END DEFAULT LIST                    SS OCT88
VPDSSEL  DC    C'(PDSSCSEL PDSLDSEL PDSLASEL PDSLVSEL '        SS NOV88
         DC    C'PDSLCSEL PDSTTSEL PDSCXSEL '                  SS NOV88
         DC    C'PDSLFSEL PDSCSSEL '                           SS NOV88
         DC    C')'        END SELECT  LIST                    SS NOV88
VPDSBLK  DC    C'PDSBLKCM '                                    SS DEC87
VPDSTLIM DC    C'PDSTLIM  '                                    SS NOV88
VPDSFIND DC    C'PDSFIND  '                                   DRK DEC02
VPDSCOMP DC    C'PDSCOMPP '                                   DRK SEP05
VPDSVERS DC    C'PDSPGMVV '                                   DRK MAY13
*     DEF VARS IN #MDSECT               *                      SS AUG86
         DS    0F                       *                      SS AUG86
ISPVIV2  DC    H'0',X'04',X'01'      DEF VARS IN #MDSECT       SS AUG86
         DC    A(VSPFMSG),AL2(SPFDSNMG-WORKAREA),H'79'         SS AUG86
         DC    A(VPDSSETP),AL2(SETPROF-WORKAREA),H'8'          SS AUG86
         DC    A(VPDSSETM),AL2(SETMACRO-WORKAREA),H'8'         SS AUG86
         DC    X'FF'                                           SS AUG86
VPDSSETP DC    C'PDSSETP '              *                      SS AUG86
VPDSSETM DC    C'PDSSETM '              *                      SS AUG86
* SELECT DEFAULTS .. MUST BE IN ORDER   *                      SS MAY88
ISPDEFS1 DC    CL4'&ISCSEL'   SOURCE    *                      SS JUL87
         DC    CL4'&ILDSEL'   LOAD      *                      SS JUL87
         DC    CL4'&ILASEL'   LISTA     *                      SS JUL87
         DC    CL4'&ILVSEL'   LISTV     *                      SS JUL87
         DC    CL4'&ILCSEL'   LISTC/LISTF                      SS OCT88
         DC    CL4'&ITTSEL'   TSO COMMAND TABLE                SS OCT88
         DC    CL4'&ICXSEL'   CAX       *                      SS OCT88
         DC    CL4'&IFLSEL'   FILTER    *                      SS OCT88
         DC    CL4'&ICSSEL'   CSECTS    *                      SS OCT88
         DC    X'FF'          END OF LIST                      SS MAY88
* ENVIORMENTAL AND MEMLIST DEFAULTS MUST BE IN ORDER           SS MAY88
ISPDEFE1 DC    CL3'&LKEDDAT'            *                      SS JUL87
         DC    CL3'&ALICHK'             *                      SS MAY88
         DC    CL3'NO'        ALIAS UPDATE OPTION              SS OCT88
         DC    CL3'YES'       ENDX      *                      SS MAY88
         DC    CL3'&SPFTDEL'            *                      SS MAY88
         DC    CL3'&SPFTOLD'            *                      SS MAY88
         DC    CL3'&SWAP'               *                      SS MAY88
         DC    CL3'&EDUPDT'             *                      SS MAY88
         DC    CL3'NO'        ORDERED PENDING ON END           SS MAY88
         DC    CL3'YES'       LISTV SPACE IS DEFAULT           SS JUL88
         DC    CL3'NO'        WAIT ON VTOC IS NO               SS JUL88
         DC    CL3'NO'        LISTF REMOVE OLD                 SS NOV88
         DC    CL3'NO'        LISTF REMOVE DELETE              SS NOV88
         DC    CL3'NO'        LISTF AUTO UPDATE ON CHANGE      SS NOV88
         DC    CL3'YES'       LISTF DELETE PROMPT              SS NOV88
         DC    CL3'&TRAP'     TRAP ON                          SS NOV88
         DC    CL3'*'         TRAP LOG NUMBER                  SS NOV88
         DC    CL3'YES'       TRAP COPY CMD TO LOG             SS NOV88
* SORT DEFAULTS  * FOLLOWED BY 2 BYTES OF HEX FOR TBL MATCH    SS OCT88
         DC    XL3'5C0000'    MEML SOURCE                      SS OCT88
         DC    XL3'5C0000'    MEML LOAD                        SS OCT88
         DC    XL3'5C0000'    LISTV VOL                        SS OCT88
         DC    XL3'5CA000'    LISTV ADDRESS                    SS OCT88
         DC    XL3'5C0000'    LISTC/LISTFILE                   SS OCT88
         DC    XL3'5C0000'    CSECT                            SS OCT88
         DC    XL3'5C0000'    CAX                              SS OCT88
         DC    X'FF'          END OF LIST                      SS MAY88
VSPFFX2  DC    C'(PDSCSIZE PDSCMAX PDSCCKPT)'                  SS JUL87
ISPVINT2 DS    0H                                              SS NOV85
         MVI   SPFVZCMD,C' '                BLANK ..           SS AUG86
         MVC   SPFVZCMD+1(L'SPFVZCMD-1),SPFVZCMD    RIPPLE     SS AUG86
         MVC   SPFUSRXX,ISPBLK8   BLANK FOR PASS ENTRY         SS AUG86
         MVC   SPFUSRX2,ISPBLK8   BLANK FOR PASS ENTRY         SS OCT88
         MVC   SPFSTAXX(4),STAXPARM  SAVE STAX EXIT RTN ADDR   SS AUG86
*  SETUP DEFAULTS                       *                      SS AUG86
*        BAS   R2,ISPCERT         CONTROL ERRORS RETURN       DRK JUN19
*        BAS   R2,ISPPTON         CONTROL PASSTHRU LRSCROLL   DRK JUN19
*                                                  PASON      DRK JUN19
*        BAS   R2,ISPCERC         CONTROL ERRORS CANCEL       DRK JUN19
         MVC   SPFPASST,ISPPASST        SETUP PASSTHRU         SS JUL87
         LA    R15,VPDFLTX              *                      SS AUG86
         BAS   R2,ISPVGETP              GET FROM PROFILE       SS AUG86
         LA    R15,VPDSSEL              *                      SS JUL87
         BAS   R2,ISPVGETP              GET FROM PROFILE       SS JUL87
         LA    R15,VSPFFX2              *                      SS JUL87
         BAS   R2,ISPVGETP              GET FROM PROFILE       SS JUL87
         LA    R15,VPDSTLIM             *                      SS NOV88
         BAS   R2,ISPVGETP              GET FROM PROFILE       SS NOV88
         LA    R1,SPFLKDT               FIRST TO PROCESS       SS MAY88
         LA    R15,ISPDEFE1             FIRST DEFAULT          SS MAY88
ISPVINT3 DS    0H                       *                      SS MAY88
         CLI   0(R15),X'FF'             END OF LIST            SS MAY88
         BE    ISPVINT4                 YES                    SS MAY88
         CLI   0(R1),C' '               BLANK USER DEFAULT     SS MAY88
         BNE   *+10                     NO, GOOD               SS MAY88
         MVC   0(3,R1),0(R15)           YES, SUPPLY DEFAULT    SS MAY88
         LA    R1,3(,R1)                NEXT                   SS MAY88
         LA    R15,3(,R15)              NEXT                   SS MAY88
         B     ISPVINT3                 WORK THE WHOLE LIST    SS MAY88
ISPVINT4 DS    0H                       *                      SS MAY88
         LA    R1,SPFSCSEL              FIRST TO PROCESS       SS MAY88
         LA    R15,ISPDEFS1             FIRST DEFAULT          SS MAY88
ISPVINT5 DS    0H                       *                      SS MAY88
         CLI   0(R15),X'FF'             END OF LIST            SS MAY88
         BE    ISPVINT6                 YES                    SS MAY88
         CLI   0(R1),C' '               BLANK USER DEFAULT     SS MAY88
         BNE   *+10                     NO, GOOD               SS MAY88
         MVC   0(4,R1),0(R15)           YES, SUPPLY DEFAULT    SS MAY88
         LA    R1,4(,R1)                NEXT                   SS MAY88
         LA    R15,4(,R15)              NEXT                   SS MAY88
         B     ISPVINT5                 WORK THE WHOLE LIST    SS MAY88
ISPVINT6 DS    0H                       *                      SS MAY88
         LA     R15,0                   *                      SS OCT88
         C      R15,SPFCMAX         IS IT SET                  SS OCT88
         BL     *+10                YES                        SS MAR89
         MVC    SPFCMAX,ISPMAX      NO, SET IT                 SS OCT88
         C      R15,SPFCCKPT        IS IT SET                  SS OCT88
         BL     *+10                YES                        SS MAR89
         MVC    SPFCCKPT,ISPCKPT    NO, SET IT                 SS OCT88
         C      R15,SPFCSIZE        IS IT SET                  SS OCT88
         BL     *+10                YES                        SS MAR89
         MVC    SPFCSIZE,ISPSIZE    NO, SET IT                 SS OCT88
         LA     R15,15              MINIMUM CKPT SIZE          SS OCT88
         C      R15,SPFCCKPT        LESS THAN 15               SS OCT88
         BNH    *+8                 NO, OK                     SS OCT88
         ST     R15,SPFCCKPT        YES, SET TO 15             SS OCT88
         LA     R15,100             MINIMUM SIZE               SS OCT88
         C      R15,SPFCMAX         LESS THAN 100              SS MAR89
         BNH    *+8                 NO, OK                     SS MAR89
         ST     R15,SPFCMAX         YES, SET TO 100            SS MAR89
         C      R15,SPFCSIZE        LESS THAN 100              SS OCT88
         BNH    *+8                 NO, OK                     SS OCT88
         ST     R15,SPFCSIZE        YES, SET TO 100            SS OCT88
*  CREATE TABLE NAME FROM TIME OF DAY TO MAKE UNIQUE           SS JUL87
         LA    R14,VCOPY            GET THE TIME               SS JUL84
         ST    R14,PARMLIST                                    SS JUL84
         LA    R14,VZTIME           GET TIME OF DATA           SS JUL84
         L     R15,ISPL8            SET LENGTH                 SS JUL84
         ST    R15,SPFFIXED            TO 8                    SS JUL84
         LA    R15,SPFFIXED               USE AS PARM          SS JUL84
         LA    R0,SPFTBL                                       SS JUL84
         LA    R1,ISPMOVE                                      SS JUL84
         STM   R14,R1,PARMLIST+4                               SS JUL84
         OI    PARMLIST+16,X'80'                               SS JUL84
         LA    R1,PARMLIST                                     SS JUL84
         L     R15,ISPLINK                                     SS JUL84
         BASR  R14,R15                                         SS JUL84
         LTR   R15,R15                                         SS JUL84
         BNZ   ISPERR                                          SS JUL84
         MVC   SPFTBL+5(2),SPFTBL+3 MINUTES PART OF TABLE NAME SS JUL84
         MVC   SPFTBL+3(2),SPFTBL   HOURS ALSO                 SS JUL84
         MVC   SPFTBL(3),ISPPDS     TABLE NAME STARTS PDS      SS JUL84
         MVI   SPFTBL+7,C'1'        FILL OUT TO 8 CHAR        DRK MAR03
*        LA    R14,VCOPY            GET THE ISPF SCREEN #     DRK MAR03
*        ST    R14,PARMLIST                                   DRK MAR03
*        LA    R14,VZSCREEN         GET ISPF LOGICAL SCREEN   DRK MAR03
*        L     R15,ISPL1            SET LENGTH                DRK MAR03
*        ST    R15,SPFFIXED            TO 1                   DRK MAR03
*        LA    R15,SPFFIXED               USE AS PARM         DRK MAR03
*        LA    R0,SPFTBL+7          FILL OUT TO 8 CHAR        DRK MAR03
*        LA    R1,ISPMOVE              PDSHHMM.ZSCREEN        DRK MAR03
*        STM   R14,R1,PARMLIST+4                              DRK MAR03
*        OI    PARMLIST+16,X'80'                              DRK MAR03
*        LA    R1,PARMLIST                                    DRK MAR03
*        L     R15,ISPLINK                                    DRK MAR03
*        BASR  R14,R15                                        DRK MAR03
*        LTR   R15,R15                                        DRK MAR03
*        BNZ   ISPERR                                         DRK MAR03
ISPCREAT DS    0H                                              SS AUG86
         MVI   SPFTBL+2,C'S'      MAKE NAME "PDS"              SS SEP84
         LA    R14,TBCREATE       CREATE TABLE                 SS JUL84
         ST    R14,PARMLIST                                    SS JUL84
         LA    R14,SPFTBL                                      SS JUL84
         LA    R15,0                                           SS JUL84
         LA    R0,VPDSLIN                                      SS AUG86
         ST    R0,SPFTVARA              SAVE FOR LATER         SS AUG86
         LA    R1,ISPNOWRT                                     SS JUL84
         STM   R14,R1,PARMLIST+4                               SS JUL84
         OI    PARMLIST+16,X'80'                               SS JUL84
ISPCREA2 LA    R1,PARMLIST                                     SS JUL84
         L     R15,ISPLINK                                     SS JUL84
         BASR  R14,R15                                         SS JUL84
         LTR   R15,R15                                         SS JUL84
         BZ    ISPIDONE                                        SS JUL84
         C     R15,ISPL8          DUPLICATE NAME               SS JUL84
         BNE   ISPERR             NO, MUST BE SEVERE ERROR     SS JUL84
         CLI   SPFTBL+6,C'9'      9 DIGIT                      SS OCT88
         BE    ISPCREA3           YES, CARRY ONE               SS OCT88
         SR    R15,R15                                         SS OCT88
         IC    R15,SPFTBL+6       GET DIGIT                    SS OCT88
         LA    R15,1(R15)         ADD ONE TO MAKE DIFFERENT    SS OCT88
         STC   R15,SPFTBL+6                                    SS OCT88
         B     ISPCREA2           TRY AGAIN                    SS OCT88
ISPCREA3 DS    0H                       *                      SS OCT88
         MVI   SPFTBL+6,C'0'      ZERO ONES DIGIT              SS OCT88
         SR    R15,R15                                         SS OCT88
         IC    R15,SPFTBL+5       GET DIGIT                    SS OCT88
         LA    R15,1(R15)         ADD ONE TO 10S DIGIT         SS OCT88
         STC   R15,SPFTBL+5                                    SS OCT88
         B     ISPCREA2           TRY AGAIN                    SS OCT88
ISPIDONE OI    SPFLAG0,SPFTABLE  GOOD TABLE CREATED            SS JUL84
         XC    SPFTFLG1(4),SPFTFLG1                            SS NOV85
         XC    SPFTFF(2),SPFTFF                                SS NOV85
         OI    SPFTFLG1,SPFTACT   SET TABLE ACTIVE             SS AUG86
         MVI   SPFFUNST,C'I'      INIT TO INACTIVE             SS AUG86
         MVC   SPFFUNST+1(7),SPFFUNST   REPEL                  SS AUG86
         BAS   R2,ISPSETST        INIT STATUS FOR LOG          SS AUG86
         CLI   SPFLAG1,SPFPTGET   HAVE LINE TO ADD             SS SEP84
         BNE   ISPIDON2                 *                      SS AUG86
         BAS   R2,ISPADDLN                                     SS AUG86
         B     ISPIDON3                 *                      SS AUG86
ISPIDON2 CLI   SPFLAG1,SPFPUTL    LINE WILL BE ADDED SOON      SS AUG86
         BE    *+8                                             SS AUG86
         BAS   R2,ISPADD1        MAKE TABLE NON EMPTY          SS AUG86
ISPIDON3 LA    R14,SPFPSTKT        TOP OF STACK                SS AUG86
         ST    R14,SPFPANTT        SET TOP                     SS NOV85
         ST    R14,SPFPANCT        SET CURRENT                 SS NOV85
         LA    R14,SPFPSTKL        LAST OF STACK               SS NOV85
         ST    R14,SPFPANBT        SET BOTTOM                  SS NOV85
         MVC   SPFLOGNM(8),SPFTBL  MAKE IT THE SAME            SS OCT88
         MVC   SPFCSUF,SPFTBL      SET SUFFIX                  SS NOV85
         L     R1,SPFSEC4A         GET BASE                    SS OCT88
         LA    R1,SPFLOGHM-SPFSEC4K(R1) HOME FOR LOG           SS NOV85
         ST    R1,SPFTHOME         INIT HOME  POINTER          SS NOV85
         ST    R1,SPFLOGHA         INIT LOG HOME ADDR          SS OCT88
         MVC   0(8,R1),SPFTBL      SET NAME                    SS OCT88
         MVI   SPFTBINX+1,255      DUMMY TB CONT INDEX         SS MAY89
         LA    R1,1                PANEL INDEX 1               SS NOV85
         LNR   R1,R1               NEG FOR SWAP                SS NOV85
         BAS   R2,ISPSTKI          CALL STACK ROUTINE          SS NOV85
         BAS   R2,ISPSETST         ALSO INIT STATUS FLAG       SS AUG86
         LA    R14,VREPLACE        VREPLACE ISPF FUNCTION      SS DEC87
         LA    R15,ISPZHTOP        THIS VARIABLE               SS DEC87
         LA    R0,ISPL8            8  CHARS LONG               SS DEC87
         LA    R1,ISPTOPH          FIND IT HEAR                SS DEC87
         STM   R14,R1,PARMLIST     SET PARMLIST                SS DEC87
         OI    PARMLIST+12,X'80'   END PARMLIST FLAG           SS DEC87
         BAS   R2,ISPSXEQ8         CALL ISPLINK                SS DEC87
         LA    R1,ISPEDREC         EDIT RECOVERY               SS JUL87
         LA    R2,ISPQURY          QUERY OPTION                SS JUL87
         STM   R1,R2,PARMLIST      SET PARMLIST 1/2            SS JUL87
         OI    PARMLIST+4,X'80'    END PARMLIST FLAG           SS JUL87
         BAS   R2,ISPSXEQ8         CALL ISPLINK                SS JUL87
         LTR   R15,R15             RC ZERO                     SS JUL87
         BZR   R3                  YES, DONE                   SS JUL87
         LA    R15,ISPIRCVP        EDIT RECOVERY PENDING PANEL SS JUL87
         LA    R1,ISPBLK8          NO MESSAGE                  SS JUL87
         BAS   R2,ISPSINGI         DISPLAY PANEL               SS JUL87
ISPIDON4 DS    0H                       *                      SS DEC87
         LA    R1,ISPEDREC         EDIT RECOVERY               SS JUL87
         LA    R2,ISPDEFER         DEFER HANDLING FOR NOW      SS JUL87
         STM   R1,R2,PARMLIST      SET PARMLIST 1/2            SS JUL87
         OI    PARMLIST+4,X'80'    END PARMLIST FLAG           SS JUL87
         BAS   R2,ISPSXEQ0         CALL ISPLINK                SS JUL87
         LA    R1,ISPEDREC         EDIT RECOVERY               SS DEC87
         LA    R2,ISPQURY          QUERY OPTION                SS DEC87
         STM   R1,R2,PARMLIST      SET PARMLIST 1/2            SS DEC87
         OI    PARMLIST+4,X'80'    END PARMLIST FLAG           SS DEC87
         BAS   R2,ISPSXEQ8         CALL ISPLINK                SS DEC87
         LTR   R15,R15             RC ZERO                     SS DEC87
         BZR   R3                  YES, DONE                   SS DEC87
         B     ISPIDON4            LOOP THROUGH THEN           SS DEC87
ISPSIZE  DC    F'&SPFSIZE'                                     SS JUL84
ISPMAX   DC    F'&SPFMAX'                                      SS JUL84
ISPCKPT  DC    F'&SPFCKPT'                                     SS JUL84
ISPIRCVP DC    CL8'&P.RECO2'            *                      SS JUL87
ISPEDREC DC    CL8'EDREC'               *                      SS JUL87
ISPQURY  DC    CL8'QUERY'               *                      SS JUL87
ISPDEFER DC    CL8'DEFER'               *                      SS JUL87
ISPZHTOP DC    CL8'ZHTOP'          HELP TOP PANEL POINTER      SS DEC87
ISPTOPH  DC    CL8'&P.PN@PO'       PDS TOP PANEL               SS DEC87
ISPCOMPP DC    CL8'&CCAR'          PDS COMPARE PROGRAM        DRK SEP05
VPDSMAIN DC    V(PDSMAIN)                                     DRK MAY13
         DROP  R4                                              SS MAR85
         SPACE 2                                               SS MAR85
         TITLE 'ISPF - SET VARIABLES SUBROUTINE'               SS FEB89
*                                                              SS JUL84
*  SUPPORT ROUTE TO VDEFINE AND COPY DATA SET INFO             SS JUL84
*    RETURN REG IS R3                                          SS JUL84
*                                                              SS JUL84
ISPSETVS DS    0H                                              SS SEP84
         USING *,R4                                            SS MAR85
         TM    SPFVFLGS,X'40'          VDEFINES DONE           SS NOV85
         BO    ISPSETV2                                        SS NOV85
         LA    R1,SPFPSTK                                      SS NOV85
         ST    R1,SPFPANT                                      SS NOV85
         ST    R1,SPFPANC                                      SS NOV85
         LA    R1,SPFPSTKB                                     SS NOV85
         ST    R1,SPFPANB                                      SS NOV85
         LA    R1,ISPSETVZ                                     SS NOV85
         BAS   R2,ISPVDEFX                                     SS NOV85
ISPSETV2 DS    0H                                              SS SEP84
         MVC   SPFDSNAM,DSNAME                                 SS SEP84
         MVC   SPFPWD,PASSWORD                                 SS SEP84
         MVC   SPFVOL,VOLUME       REQ VOL, MAY BE BLANK       SS NOV88
         MVC   SPFVOLAL,VOLALLOC        ALLOCATED VOLUME       SS NOV88
*              SPFMEMB SET BY CALLER                           SS NOV84
*              MEMLNMSV SET BY CALLER                          SS NOV84
         MVC   SPFDSORG,ISPPS          ASSUME DSORG=PS         SS SEP84
         MVI   SPFTYPE,C' '            TYPE BLANK              SS NOV84
         TM    DSORG,DS1DSGPO          IS IT PO                SS SEP84
         BZ    ISPSETV4                NO                      SS NOV84
         MVC   SPFDSORG,ISPPO          YES, SET TO PO          SS SEP84
ISPSETV3 DS    0H                                              SS SEP84
         TM    FLAGSCC,RECFMU              IS IT LOAD          SS SEP84
         BZ    ISPSETV4                    NO                  SS SEP84
         MVI   SPFTYPE,C'U'                YES, SET U          SS SEP84
ISPSETV4 DS    0H                                              SS SEP84
         MVI   SPFZSEL,C' '            CLEAR ZSEL              SS SEP84
         MVC   SPFZSEL+1(249),SPFZSEL        RIPPLE            SS SEP84
         BR    R3                                              SS SEP84
         DROP  R4                                              SS MAR85
         DS    0F                                              SS NOV85
ISPSETVZ DC    H'0',X'40',X'00'                                SS NOV85
         DC    A(VSPFDSN),AL2(SPFDSNAM-SPWRKC),H'44'           SS NOV85
         DC    A(VSPFPWD),AL2(SPFPWD-SPWRKC),H'8'              SS NOV85
         DC    A(VSPFVOL),AL2(SPFVOL-SPWRKC),H'6'              SS NOV85
         DC    A(VSPFMEM),AL2(SPFMEMB-SPWRKC),H'8'             SS NOV85
         DC    A(VSPFDATA),AL2(MEMLNMSV-SPWRKC),H'8'           SS NOV85
         DC    A(VSPFDSG),AL2(SPFDSORG-SPWRKC),H'2'            SS NOV85
         DC    A(VSPFTYPE),AL2(SPFTYPE-SPWRKC),H'1'            SS NOV85
         DC    A(VSPFENTR),AL2(SPFENTER-SPWRKC),H'3'           SS DEC87
         DC    A(VSPFSETA),AL2(SPFSETA-SPWRKC),H'3'            SS DEC87
         DC    A(VSPFRCOP),AL2(SPFRCOPT-SPWRKC),H'46'          SS DEC87
         DC    X'FF'                                           SS NOV85
         SPACE 2                                               SS SEP84
VSPFDSN  DC    C'PDSDSN '                                      SS JUL84
VSPFPWD  DC    C'PDSPWD '                                      SS JUL84
VSPFVOL  DC    C'(PDSVOL PDSVOLAL)'                            SS NOV88
VSPFMEM  DC    C'PDSMEM '                                      SS JUL84
VSPFDATA DC    C'PDSDATA '                                     SS NOV84
VSPFDSG  DC    C'PDSDSORG '                                    SS JUL84
VSPFTYPE DC    C'PDSTYPE '                                     SS JUL84
VSPFENTR DC    C'PDSENTER '             *                      SS DEC87
VSPFSETA DC    C'PDSSETA '              *                      SS DEC87
VSPFRCOP DC    C'PDSRCOPT '             *                      SS DEC87
