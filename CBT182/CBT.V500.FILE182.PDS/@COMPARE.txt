         TITLE 'P D S  --  PDS COMPARE                       09/28/88'
***********************************************************************
***      COMPARE SUBCOMMAND    ADDED BY BRUCE LELAND --  MAY, 1984  ***
***********************************************************************
*
         SPACE 1
COMPARE  CSECT
         USING *,R8
         SPACE 1
*
*  MSGLINE
*      OFFSET  |  FIELD    |  DESCRIPTION
*     -------- | --------- | -------------
*     0-99     | OPERANDS  | ANY OPERANDS AFTER THE NAMES
*MSGDS1  EQU   MSGLINE+100,64
*     +0,56    | DSNAME1   | THE FIRST DATA SET NAME STRING
*     +61,1    | FLAG      | X'80' IF MEMBER ONLY
*     +62,2    | LSTR1     | LENGTH OF STRING
*              |           |
*MSGDS2  EQU   MSGLINE+164,64
*     +0,56    | DSNAME2   | THE SECOND DATA SET NAME STRING
*     +61,1    | FLAG      | X'80' IF MEMBER ONLY
*     +62,2    | LSTR2     | LENGTH OF STRING
*
*
         SPACE 1
* TEST FOR SUPERC COMPARE TYPES
         CLC   ##SUBCAL(8),SUPCPGM     SUPERC COMPARE?        DRK NOV16
         BNE   COMPA01                 NO                     DRK NOV16
         CLC   MSGLINE(4),SUPCLINE     1ST OPERAND = LINE?    DRK DEC16
         BE    COMPTYPL                YES                    DRK DEC16
         CLC   MSGLINE(4),SUPCBYTE     1ST OPERAND = BYTE?    DRK NOV16
         BE    COMPTYPB                YES                    DRK NOV16
         CLC   MSGLINE(4),SUPCFILE     1ST OPERAND = FILE?    DRK NOV16
         BE    COMPTYPF                YES                    DRK NOV16
         CLC   MSGLINE(4),SUPCWORD     1ST OPERAND = WORD?    DRK NOV16
         BE    COMPTYPW                YES                    DRK NOV16
         CLC   MSGLINE(4),YALEPGM      1ST OPERAND = YALE?    DRK JUN18
         BE    COMPTYPY                YES                    DRK JUN18
         B     COMPA01                 NO                     DRK NOV16
COMPTYPL DS    0H                                             DRK DEC16
         MVC   MSGLINE(96),MSGLINE+4   SHIFT LEFT TO CLEAR    DRK DEC16
         B     COMPA01                                        DRK DEC16
COMPTYPB MVC   ##SUBCAL+4(4),SUPCBYTE  CHANGE PGM NAME        DRK NOV16
         MVC   MSGLINE(96),MSGLINE+4   SHIFT LEFT TO CLEAR    DRK NOV16
         B     COMPA01                                        DRK NOV16
COMPTYPF MVC   ##SUBCAL+4(4),SUPCFILE  CHANGE PGM NAME        DRK NOV16
         MVC   MSGLINE(96),MSGLINE+4   SHIFT LEFT TO CLEAR    DRK NOV16
         B     COMPA01                                        DRK NOV16
COMPTYPW MVC   ##SUBCAL+4(4),SUPCWORD  CHANGE PGM NAME        DRK JUN18
         MVC   MSGLINE(96),MSGLINE+4   SHIFT LEFT TO CLEAR    DRK JUN18
         B     COMPA01                                        DRK JUN18
COMPTYPY MVC   ##SUBCAL+4(4),SUPCYALE  CHANGE PGM NAME        DRK NOV16
         MVC   MSGLINE(96),MSGLINE+4   SHIFT LEFT TO CLEAR    DRK NOV16
         B     COMPA01                                        DRK NOV16
SUPCPGM  DC    CL8'COMPAREC'                                  DRK NOV16
SUPCLINE DC    CL4'LINE'                                      DRK NOV16
SUPCBYTE DC    CL4'BYTE'                                      DRK NOV16
SUPCFILE DC    CL4'FILE'                                      DRK NOV16
SUPCWORD DC    CL4'WORD'                                      DRK NOV16
YALEPGM  DC    CL4'YALE'                                      DRK JUN18
SUPCYALE DC    CL4'ARE$'               YALE COMPARE PROGRAM   DRK JUN18
COMPA01  DS    0H                                             DRK NOV16
         XC    DIRUSER,DIRUSER         CLEAR THE USER FIELDS
         MVC   DIRNAME,MSGDS1          FIRST PART OF DSNAME1
         CLI   MSGDS1+63,8             MORE THAN 8 DIGITS?
         BH    COMPA10                 YES, BRANCH
         CLI   MSGDS1,C''''            QUOTED?
         BE    COMPA10                 YES, BRANCH
         TM    DSORG,DS1DSGPO          PARTITIONED?
         BNO   COMPA10                 NO, BRANCH
         LA    R1,DIRNAME-1            START OF SEARCH -1
         LA    R0,8                    CHARACTERS TO SEARCH
         SPACE 1
COMPA02  LA    R1,1(,R1)               NEXT CHARACTER
         CLI   0(R1),C'.'              PERIOD?
         BE    COMPA10                 YES, BRANCH
         CLI   0(R1),C'('              PARENTHESIS?
         BE    COMPA10                 YES, BRANCH
         BCT   R0,COMPA02              CHECK ALL 8 CHARACTERS
         SPACE 1
         OI    MSGDS1+61,X'80'         USE THIS AS A MEMBER NAME
         TM    SPFLAG2,SPFLNCD         LINE COMMAND?           SS AUG85
         BO    COMPA07                 YES, BRANCH
         SPACE 1
         LH    R0,MSGDS1+62            MEMBER NAME LENGTH
         BCTR  R0,0                    MACHINE LENGTH
         STCM  R0,B'0011',MEMBERD+1    CHANGE DEFAULT MEMBER LENGTH
         MVC   MEMBERD+1+2(8),DIRNAME  CHANGE DEFAULT MEMBER NAME
         MVI   MEMBERD,FMEMBER1        ONLY ONE MEMBER NAME NOW
         NI    PMEMMIN,FF-X'80'        NO MEMBER LIST NOW
         BAS   R14,DEFGROUP            ADD DEFAULT GROUP
         SPACE 2
COMPA07  BLDL  INDCB,BLDLLIST          LOCATE DIRECTORY ENTRY
         B     *+4(R15)                PROCESS RETURN CODE
         B     COMPA10                   00 - SUCCESSFUL
         B     NOMEMBER                  04 - MEMBER NOT FOUND
         B     IOERROR                   08 - I/O ERROR
         SPACE 2
COMPA10  MVC   DIRNAME,MSGDS2          FIRST PART OF DSNAME2
         CLI   MSGDS2+63,8             MORE THAN 8 DIGITS?
         BH    COMPA20                 YES, BRANCH
         CLI   MSGDS2,C''''            QUOTED?
         BE    COMPA20                 YES, BRANCH
         TM    DSORG,DS1DSGPO          PARTITIONED?
         BNO   COMPA20                 NO, BRANCH
         LA    R1,DIRNAME-1            START OF SEARCH -1
         LA    R0,8                    CHARACTERS TO SEARCH
         SPACE 1
COMPA12  LA    R1,1(,R1)               NEXT CHARACTER
         CLI   0(R1),C'.'              PERIOD?
         BE    COMPA20                 YES, BRANCH
         CLI   0(R1),C'('              PARENTHESIS?
         BE    COMPA20                 YES, BRANCH
         BCT   R0,COMPA12              CHECK ALL 8 CHARACTERS
         SPACE 1
         OI    MSGDS2+61,X'80'         USE THIS AS A MEMBER NAME
         TM    SPFLAG2,SPFLNCD         LINE COMMAND?           SS AUG85
         BO    COMPA17                 YES, BRANCH
         SPACE 1
         LH    R0,MSGDS2+62            MEMBER NAME LENGTH
         BCTR  R0,0                    MACHINE LENGTH
         STCM  R0,B'0011',MEMBERD+1    CHANGE DEFAULT MEMBER LENGTH
         MVC   MEMBERD+1+2(8),DIRNAME  CHANGE DEFAULT MEMBER NAME
         MVI   MEMBERD,FMEMBER1        ONLY ONE MEMBER NAME NOW
         NI    PMEMMIN,FF-X'80'        NO MEMBER LIST NOW
         BAS   R14,DEFGROUP            ADD DEFAULT GROUP
         SPACE 2
COMPA17  BLDL  INDCB,BLDLLIST          LOCATE DIRECTORY ENTRY
         B     *+4(R15)                PROCESS RETURN CODE
         B     COMPA20                   00 - SUCCESSFUL
         B     NOMEMBER                  04 - MEMBER NOT FOUND
         B     IOERROR                   08 - I/O ERROR
         SPACE 3
COMPA20  MVC   DIRNAME(8),MSGDS1       FIRST MEMBER NAME
         MVC   MSGTEXT1+4(8),##SUBCAL  PROCESSOR TO ATTACH
         MVI   MSGTEXT1+12,X'40'       ADD A BLANK
         SPACE 2
         MVI   MSGTEXT1+13,C''''       ADD A QUOTE
         MVC   MSGTEXT1+14(44),DSNAME  ADD THE DATA SET NAME
         LH    R15,DSNLEN              DSNAME LENGTH
         LA    R3,MSGTEXT1+13(R15)     POINT TO CURRENT BYTE -1
         SPACE 1
         MVI   1(R3),C'('              MEMBER NAME PARENTHESIS
         MVC   2(8,R3),DIRNAME         MEMBER NAME
         LA    R3,2+8(,R3)             END OF MEMBER +1
         LA    R2,8                    MAXIMUM MACHINE LENGTH+1
         SPACE 1
         TM    MSGDS1+61,X'80'         USE THIS AS A MEMBER NAME?
         BO    COMPA22                 YES, BRANCH
         LA    R3,MSGTEXT1+13          POINT TO CURRENT BYTE
         MVC   0(57,R3),MSGDS1         ADD THE FIRST DATA SET NAME
         AH    R3,MSGDS1+62            ADD ITS LENGTH
         MVI   0(R3),X'40'             ADD A BLANK
         LA    R3,1(,R3)
         B     COMPA27
         SPACE 1
COMPA22  BCTR  R3,0                    SCAN
         CLI   0(R3),X'40'                  FOR
         BNE   *+8                              FIRST
         BCT   R2,COMPA22                             NON-BLANK
         SPACE 1
         BCTR  R2,0                    MACHINE LENGTH
         TRT   DIRNAME(*-*),TRTMEM     <<EXECUTED>>
         EX    R2,*-6                  VALID MEMBER NAME?
         BNZ   BADMEMB                 NO, ERROR
         CLI   DIRNAME,C'0'            VALID FIRST CHARACTER?
         BNL   BADMEMB                 NO, BRANCH
         LA    R3,1(,R3)               POINT TO TERMINATOR
         MVI   0(R3),C')'              ADD A CLOSING PARENTHESIS
         SPACE 1
         MVI   1(R3),C''''             ADD A QUOTE
         MVI   2(R3),X'40'             ADD A BLANK
         LA    R3,3(,R3)               ACCOUNT FOR ")' "
         SPACE 2
COMPA27  MVC   DIRNAME(8),MSGDS2       SECOND MEMBER NAME
         TM    MSGDS2+61,X'80'         USE THIS AS A MEMBER NAME?
         BO    COMPA29                 YES, BRANCH
         MVC   0(57,R3),MSGDS2         ADD THE SECOND DATA SET NAME
         AH    R3,MSGDS2+62            ADD ITS LENGTH
         MVI   0(R3),X'40'             ADD A BLANK
         LA    R3,1(,R3)
         B     COMPA37
         SPACE 2
COMPA29  MVI   0(R3),C''''             ADD A QUOTE
         MVC   1(44,R3),DSNAME         ADD THE DATA SET NAME
         LH    R15,DSNLEN              DSNAME LENGTH
         LA    R3,0(R3,R15)            CURRENT BYTE -1
         SPACE 1
         MVI   1(R3),C'('              MEMBER NAME PARENTHESIS
         MVC   2(8,R3),DIRNAME         MEMBER NAME
         LA    R3,2+8(,R3)             END OF MEMBER +1
         LA    R2,8                    MAXIMUM MACHINE LENGTH+1
         SPACE 1
COMPA30  BCTR  R3,0                    SCAN
         CLI   0(R3),X'40'                  FOR
         BNE   *+8                              FIRST
         BCT   R2,COMPA30                             NON-BLANK
         SPACE 1
         BCTR  R2,0                    MACHINE LENGTH
         TRT   DIRNAME(*-*),TRTMEM     <<EXECUTED>>
         EX    R2,*-6                  VALID MEMBER NAME?
         BNZ   BADMEMB                 NO, ERROR
         CLI   DIRNAME,C'0'            VALID FIRST CHARACTER?
         BNL   BADMEMB                 NO, BRANCH
         LA    R3,1(,R3)               POINT TO TERMINATOR
         MVI   0(R3),C')'              ADD A CLOSING PARENTHESIS
         SPACE 1
         MVI   1(R3),C''''             ADD A QUOTE
         MVI   2(R3),X'40'             ADD A BLANK
         LA    R3,3(,R3)               ACCOUNT FOR ")' "
         SPACE 1
COMPA37  MVC   0(100,R3),MSGLINE       ADD ANY OTHER DATA
         AH    R3,#COMMDSZ             ADD LENGTH OF KEYWORD DATA
         SPACE 2
         LA    R2,MSGTEXT1             START OF COMMAND TEXT
         SR    R3,R2                   LENGTH OF COMMAND
         SLL   R3,16                   CLEAR BOTTOM TWO BYTES
         STCM  R3,B'1111',0(R2)        SAVE STRING TOTAL LENGTH
*        M$ERRST (R2)                  DELETE * TO OUTPUT COMMAND
         ST    R2,ADDRTEXT             COMMAND ADDRESS
         ST    R2,ADDRCBUF             COMMAND ADDRESS
         LA    R1,ADDRTEXT             COMMAND ADDRESS
         CLI   ##SUBCAL,C'%'           CLIST CALL?
         BNE   COMPA38                 NO, BRANCH
         L     R8,=A(EXEC)             YES, IMPLIED CLIST
         BR    R8
         SPACE 1
COMPA38  MVI   3(R2),9                 OPERAND OFFSET
         SPACE 1
         LA    R3,##SUBCAL             PROCESSOR TO ATTACH
COMPAZAP B     COMPA39                 LINK TO COMPARE
***  THE FOLLOWING INTERFACE IS FOR COMPARE FROM THE CBT TAPE, FILE 300
         BAS   R2,ATTACH               ATTACH THIS COMMAND PROCESSOR
         B     NEWCMD
         SPACE 1
***  EITHER THE ABOVE OR THE FOLLOWING INTERFACE MAY BE
***  USED BY THE COMPARE COMMAND FROM THE CBT TAPE, FILE 296
COMPA39  LA    R0,NEWSTAX              ABEND RECOVERY
         ST    R0,RECOVER
         L     R1,ADDRTEXT             COMMAND ADDRESS
         ST    R1,ADDRCBUF             COMMAND ADDRESS
         L     R15,=A(RX)              EXTERNAL INTERFACE START
         BASR  R2,R15                  EXTERNAL LINK TO RELEASE
         B     NEWCMD                  REALLOCATE AND START OVER
