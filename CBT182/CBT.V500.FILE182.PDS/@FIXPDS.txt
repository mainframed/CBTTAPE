         TITLE 'P D S  --  PDS FIXPDS                        09/28/88'
***********************************************************************
***      FIXPDS SUBCOMMAND     ADDED BY BRUCE LELAND -- NOV., 1982  ***
***********************************************************************
*
         SPACE 1
FIXPDS   CSECT
         USING *,R8
         CLI   #FIXOPT,6                STOWINIT?             DRK JUN08
         BE    FIXPDSI2                 YES, OK               DRK JUN08
         TM    DS1SMSFG,DS1PDSE         PDSE DATASET?         DRK SEP05
         BNO   FIXPDSA                  NO, CONTINUE          DRK SEP05
         CLI   #FIXOPT,2                RESET?                DRK JAN06
         BE    FIXPDSI                  YES, OK               DRK JAN06
         CLI   #RTYPE,0                 GENERAL DATA TYPE?    DRK JAN06
         BH    FIXPDSX                  YES, EXIT             DRK JAN06
         CLC   #BLKSI(4),=F'0'          BLKSIZE?              DRK JAN06
         BH    FIXPDSI                  YES, OK               DRK JAN06
         CLI   #OPTCD,0                 OPTCODE?              DRK JAN06
         BH    FIXPDSI                  YES, OK               DRK JAN06
FIXPDSX  DS    0H                                             DRK JAN06
         MVI   MTLEN,25                 NO, INSERT LENGTH     DRK JAN06
         MVC   INSERT#1(25),FIXPDSE     SOME FUNCTIONS LIMITED        "
         M$MSG L531$1                   DATA SET IS A PDSE    DRK JAN06
         M$ERRST MSGBLANK               ONE BLANK LINE        DRK SEP05
         B     NEWCMD                   NOBBLED               DRK SEP05
FIXPDSI  DS    0H                                             DRK SEP05
         CLI   #FIXOPT,2               RESET?                 DRK SEP05
         BNE   FIXPDSA                 NO                     DRK SEP05
*                                      YES                    DRK SEP05
*        STOW INITIALIZE FOR PDSE                             DRK SEP05
*        PDSE MUST BE ALLOCATED DISP=OLD VIA CHANGE COMMAND   DRK SEP05
*        OR BY PDS COMMAND INVOCATION WITH THE "OLD" OPERAND  DRK SEP05
*                                                             DRK FEB06
*        APAR OA13224 SUPPORTS DISP=SHR FOR STOW INITIALIZE   DRK FEB06
*                                                             DRK FEB06
         SPACE 1                                              DRK SEP05
         AIF ('&STOWI' EQ 'YES').STOWY APAR OA13224 APPLIED?  DRK FEB06
         LA    R1,L932                 ASSUME DISP=SHR        DRK SEP05
         CLI   DSPALLOC,ALLOOLD        DISP=OLD               DRK SEP05
         BNE   MSGNEW                  NO, ERROR MESSAGE      DRK SEP05
.STOWY   ANOP                                                 DRK FEB06
FIXPDSI2 DS    0H                                             DRK JUN08
         M$ERRST MSGBLANK              ONE SPACING LINE       DRK SEP05
         L     R15,=A(DSNAMES)                                DRK SEP05
         BASR  R2,R15                  DISPLAY ALLOCATION STATUS      "
         LA    R1,PDS392A              CORRECT DATA SET               "
         BAS   R2,YESNO                RESPONSE "YES" OR "NO"?        "
         B     NEWCMD                  "NO", QUIT                     "
         BAS   R2,OPENSTOW             OPEN STOW DCB; ENQUEUES        "
         B     NEWCMD                  COULD NOT OPEN -- ERROR        "
         STOW  STOWDCB,,I              INITIALIZE FUNCTION    DRK SEP05
         CH    R15,=H'12'              RC > 12                DRK SEP05
         BH    IOERROR                 YES, ERROR CONDITION   DRK SEP05
         B     NEWCMD                                         DRK SEP05
FIXPDSA  DS    0H                                             DRK SEP05
         SPACE 1
         ICM   R1,B'1111',#RECFM       NEW DCB=RECFM SPECIFIED?
         BP    *+10                    YES, BRANCH
         MVC   #RECFM+3(1),RECFM       NO, USE PREVIOUS RECFM
         ICM   R2,B'1111',#LRECL       NEW DCB=LRECL SPECIFIED?
         BP    *+10                    YES, BRANCH
         MVC   #LRECL+2(2),LABLRECL    NO, USE THE LRECL FROM THE DCB
         TM    #RECFM+3,X'C0'          RECFM=U..?
         BNO   *+10                    NO, BRANCH
         XC    #LRECL,#LRECL           YES, USE LRECL=0
         ICM   R3,B'1111',#BLKSI       NEW DCB=BLKSIZE SPECIFIED?
         BP    *+10                    YES, BRANCH
         MVC   #BLKSI+2(2),BLKSI       NO, USE PREVIOUS BLOCKSIZE
         C     R3,=F'88888'            BLKSIZE=88888?   SDB   DRK JUN03
         BNE   *+4+6                   NO                     DRK JUN03
         XC    #BLKSI(4),#BLKSI        YES, MAKE IT ZERO      DRK JUN03
FIXPDSB  DS    0H                                             DRK JUN03
         C     R3,=F'99999'            BLKSIZE=99999?   PDB   DRK JUN03
         BNE   FIXPDSBX                NO                     DRK JUN03
         TM    #RECFM+3,DCBRECBR       BLOCKED RECORDS?       DRK JUN03
         BO    *+4+6+4                 YES                    DRK JUN03
         MVC   #BLKSI+2(2),BLKSI       NO, USE PREVIOUS BLKSI DRK JUN03
         B     FIXPDSBX                                       DRK JUN03
         SPACE 1
         LH    R15,BYTEUCB             UCBBYTE                DRK JUN03
         MH    R15,=H'9'               INDEX INTO UNIT TABLE  DRK JUN03
         LA    R15,UNITTBL(R15)        DEVICE UNIT TYPE       DRK JUN03
         MVC   #BLKSI(4),=F'27998'     DEFAULT BLKSIZE        DRK JUN03
         CLC   1(4,R15),=C'3390'       3390?                  DRK JUN03
         BNE   *+4+4                   NO                     DRK JUN03
         B     FIXPDSB2                                       DRK JUN03
         CLC   1(4,R15),=C'3380'       3380?                  DRK JUN03
         BNE   *+4+6+4                 NO                     DRK JUN03
         MVC   #BLKSI(4),=F'23476'     1/2 TRACK              DRK JUN03
         B     FIXPDSB2                                       DRK JUN03
         CLC   1(4,R15),=C'9345'       9345?                  DRK JUN03
         BNE   *+4+6+4                 NO                     DRK JUN03
         MVC   #BLKSI(4),=F'22928'     1/2 TRACK              DRK JUN03
         B     FIXPDSB2                                       DRK JUN03
         CLC   1(4,R15),=C'3350'       3350?                  DRK JUN03
         BNE   *+4+6+4                 NO                     DRK JUN03
         MVC   #BLKSI(4),=F'9442'      1/2 TRACK              DRK JUN03
         B     FIXPDSB2                                       DRK OCT03
         CLC   1(4,R15),=C'3330'       3330?                  DRK OCT03
         BNE   *+4+6                   NO                     DRK OCT03
         MVC   #BLKSI(4),=F'6447'      1/2 TRACK              DRK OCT03
FIXPDSB2 DS    0H                                             DRK JUN03
         TM    #RECFM+3,DCBRECV+DCBRECBR  RECFM=VB?           DRK JUN03
         BNO   *+4+4                   NO, BRANCH             DRK JUN03
         B     FIXPDSBX                                       DRK JUN03
         TM    #RECFM+3,DCBRECF+DCBRECBR  RECFM=FB?           DRK JUN03
         BNO   FIXPDSBX                NO, BRANCH             DRK JUN03
         SPACE 1                                              DRK JUN03
         L     R15,#BLKSI              REQUESTED BLOCKSIZE    DRK JUN03
         SR    R14,R14                                        DRK JUN03
         ICM   R4,B'1111',#LRECL       ANY LRECL?             DRK JUN03
         BZ    FIXPDSBX                NO, AVOID DIVIDE ERROR DRK JUN03
         DR    R14,R4                  LRECL'S PER BLOCK      DRK JUN03
         M     R14,#LRECL              PDS DETERMINED BLKSIZE DRK JUN03
         ST    R15,#BLKSI              STORE PDB              DRK JUN03
         M$MSG L247                    PDB INFO MESSAGE       DRK FEB17
FIXPDSBX DS    0H                                             DRK JUN03
         SPACE 1
         TM    #RECFM+3,DCBRECF        RECFM=F OR RECFM=U?
         BNO   FIXPDS10                NO, BRANCH
         TM    #RECFM+3,DCBRECV        RECFM=U?
         BO    FIXPDS10                YES, BRANCH
         SPACE 1
         L     R15,#BLKSI              REQUESTED BLOCKSIZE
         SR    R14,R14
         ICM   R4,B'1111',#LRECL       ANY LRECL?
         BZ    FIXPDS10                NO, AVOID DIVIDE ERROR
         DR    R14,R4                  CHECK LRECL'S PER BLOCK
         LTR   R14,R14                 INTEGRAL NUMBER?
         BZ    FIXPDS10                YES, BRANCH
         M$MSG L482                    NO, DIVIDE ERROR MESSAGE
         SPACE 2
FIXPDS10 TM    DSORG,DS1DSGPO          PARTITIONED?
         BNO   FIXPDS50                NO, BRANCH
         CLI   #FIXOPT,2               DCB?
         BL    FIXPDS50                YES, BRANCH
         SPACE 1
         L     R14,=X'01080100'        R=1, K=08, DD=256 BYTES
         PUSH  ACONTROL                                       DRK MAY08
         ACONTROL FLAG(NOPAGE0)                               DRK MAY08
         TRKCALC FUNCTN=TRKCAP,TYPE=BYTEUCB+1,RKDD=(14),              XX
               REGSAVE=YES,MF=(E,PARMLIST)
         POP   ACONTROL                                       DRK MAY08
         ST    R0,#DIRBTRK             SAVE DIRECTORY BLOCKS PER TRACK
         SPACE 1
         MVC   ##SUBCOM(8),$USA        TEMPORARY SUBCOMMAND IDENTIFIER
         MVI   STARTTR+2,X'01'         START OF DIRECTORY
         BAS   R14,READDIR             NUMBER OF DIRECTORY BLOCKS
         MVC   ##SUBCOM(8),$FIX        RESET THE SUBCOMMAND NAME
         SPACE 1
         ICM   R3,B'1111',TOTMEMR      REAL (2 BYTES) AND ALIASES (2)
         ICM   R6,B'1111',TOTBLOCK     ANY DIRECTORY BLOCKS?
         BNZ   FIXPDS20                YES, BRANCH
         LA    R1,L752                 ASSUME RESET OR EXPAND & NOCOUNT
         CLC   #NEWDIR,ZERO            CORRECT?
         BE    MSGNEW                  YES, ERROR EXIT
         SPACE 1
FIXPDS20 STM   R2,R12,28(R13)          CONVERT CCHHR TO TTR
         LA    R2,CURMBB
         L     R1,INDCB+(DCBDEBAD-IHADCB)
         L     R15,ADDRRLTV
         LR    R3,R13
         BASR  R14,R15
         LR    R13,R3
         LM    R2,R12,28(R13)
         ST    R0,#EOF1TTR             TTR OF CURRENT DIRECTORY EOF
         SPACE 1
         CLI   #FIXOPT,3               EXPANDDIR OR FREEDIR?
         BH    FIXPDS22                NO, BRANCH
         ICM   R1,B'1111',#NEWDIR      ANY DIRECTORY BLOCKS SPECIFIED?
         BP    *+8                     YES, BRANCH
         ST    R6,#NEWDIR              NO, USE CURRENT NUMBER
         SR    R6,R6                   CLEAR CURRENT DIRECTORY NUMBER
         LTR   R3,R3                   ANY MAIN OR ALIAS MEMBERS?
         BZ    FIXPDS22                NO, A WARNING IS NOT NEEDED
         M$MSG L451                    YES, ALL MEMBERS WILL BE LOST
         SPACE 1
FIXPDS22 CLI   #FIXOPT,5               FREEDIR?
         BNE   FIXPDS28                NO, BRANCH
         L     R4,#NEWDIR              NUMBER TO ADD
         A     R4,TOTUSED              ADD TO CURRENT USED
         C     R4,TOTBLOCK             WITHIN THE CURRENT DIRECTORY?
         BH    FIXPDS26                NO, CONVERT TO EXPANDDIR
         L     R6,TOTUSED              NUMBER OF DIRECTORY BLOCKS USED
         L     R0,#DIRBTRK             BLOCKS PER TRACK
         L     R4,TOTUSED              CURRENT USED BLOCKS
         SRDA  R4,32                   TOTAL BLOCKS
         DR    R4,R0                   TOTAL / ENTRIES PER TRACK
         LH    R0,#EOF1TTR             PREVIOUS DIRECTORY EOF
         N     R0,=XL4'0000FFFF'       MASK OFF SIGN BITS
         STH   R5,#EOF1TTR             POTENTIAL NEW DIRECTORY EOF
         AH    R4,=H'1'                R OF TTR
         STC   R4,#EOF1TTR+2           R OF TTR FOR NEW DIRECTORY EOF
         CLM   R0,3,#EOF1TTR           ACTUAL TT OF EOF ABOVE NEW?
         BH    FIXPDS28                YES, NO MOVE PROBLEM
         MVI   #FIXOPT,4               CONVERT TO EXPANDIR
         B     FIXPDS28
         SPACE 1
FIXPDS26 MVI   #FIXOPT,4               CONVERT TO EXPANDIR
         S     R4,TOTBLOCK             NUMBER OF BLOCKS TO EXPAND
         ST    R4,#NEWDIR              UPDATE BLOCKS TO EXPAND
         SPACE 1
FIXPDS28 L     R4,#NEWDIR              DIRECTORY BLOCKS TO ADD
         AR    R4,R6                   DESIRED + CURRENT DIR. BLOCKS
         ST    R4,#NEWDIR              UPDATE (NOW TOTAL # OF BLOCKS)
         LTR   R3,R3                   ANY MAIN OR ALIAS MEMBERS?
         BNZ   *+6                     YES, BRANCH
         SR    R6,R6                   NO, CLEAR DIRECTORY COUNT
         SPACE 1
         LR    R1,R4
         SR    R1,R6                   NUMBER OF BLOCKS TO ADD
         ST    R1,#NEWDIR              UPDATE (NOW NUMBER TO ADD)
         SPACE 1
         L     R0,#DIRBTRK             BLOCKS PER TRACK
         SRDA  R4,32                   TOTAL BLOCKS
         DR    R4,R0                   TOTAL / ENTRIES PER TRACK
         STH   R5,#EOF2TT              TRACK OF NEW DIRECTORY EOF
         L     R2,DCBDEBAD-IHADCB+INDCB  DEB ADDRESS
         LA    R1,L870                 ASSUME MULTI-EXTENT DIRECTORY
         CLC   32+14(2,R2),ZERO        NULL DATA SET?
         BE    FIXPDS31                YES, ALLOW THE ALLOCATION
         CLC   #EOF2TT(2),32+14(R2)    CORRECT?
         BNL   MSGNEW                  YES, ERROR
         SPACE 1
FIXPDS31 LTR   R4,R4                   PARTIAL TRACK USED?
         BP    FIXPDS32                YES, BRANCH
         OI    #NEWDIR,X'80'           MARK SPECIAL HANDLING
         SPACE 2
FIXPDS32 CLI   #FIXOPT,5               FREEDIR MODE?
         BE    FIXPDS50                YES, NO DIRECTORY READ
         LA    R3,#MEMPTR              BASE FOR THE MEMBER LIST
         USING DIRLINK,R3              NO, BRANCH
         MVI   STARTTR+2,X'01'         TTR=000001 (START OF DIRECTORY)
FIXPDS40 BAS   R14,READDIR             GET THE NEXT MEMBER
         B     FIXPDS50                LAST MEMBER, BRANCH
         SPACE 1
         CLI   #FIXOPT,4               RESET OR INITDIR?
         BL    FIXPDS42                YES, BRANCH
         CLC   #EOF2TT(2),MEMTTR       IN THE NEW DIRECTORY AREA?
         BL    FIXPDS40                NO, BRANCH
         MVI   SUBPOOLT,21             MARK TEMPORARY SUBPOOL IN USE
         LA    R0,DIREND-DIRLINK       GET ANOTHER DIRECTORY ENTRY
         ICM   R0,B'1000',SUBPOOLT
         GETMAIN R,LV=(0)
         XC    0(DIREND-DIRLINK,R1),0(R1)
         ST    R1,DIRLINK              CHAIN TO THE
         LR    R3,R1                               NEXT ENTRY
         L     R14,DIRPTRS             ACTUAL DIRECTORY ENTRY
         MVC   DIRNAME(74),0(R14)      ADD THE DIRECTORY INFORMATION
         MVC   INSERT#1(8),DIRNAME
         MVC   INSERT#2(8),=CL8'MOVED'
         M$MSG L050$2
         DROP  R3
         SPACE 2
FIXPDS42 CLI   #FIXCHK,2               NO MEMBER CHECK?
         BE    FIXPDS40                YES, NO MEMBER CHECK
         TM    FLAGSCC,RECFMU          RECFM=U?
         BO    FIXPDS40                YES, NO MEMBER CHECK
         MVC   DSNMEMQ(8),MEMNAME      MEMBER NAME TO CHECK
         BAS   R2,ENQMTEST             MEMBER IN USE?
         B     FIXPDS40                YES, OUTPUT WARNING MESSAGE ONLY
         B     FIXPDS40                REPEAT FOR ALL MEMBERS
         SPACE 2
FIXPDS50 DS    0H
         CLI   #FIXOPT3,0              ANY ADDTRK OPTION?     DRK NOV18
         BE    FIXPDS52                NO, BRANCH                     "
         ST    R6,R6SAVE                                              "
         L     R6,=A(LSPACEX)          LSPACE                         "
         BASR  R2,R6                   LARGEST FREE AREA ON VOLUME    "
         L     R6,R6SAVE                                              "
         SPACE 1                                                      "
FIXPDS52 DS    0H                                                     "
         M$ERRST MSGBLANK              ONE SPACING LINE
         L     R15,=A(DSNAMES)
         BASR  R2,R15                  DISPLAY ALLOCATION STATUS
         LA    R1,PDS392A              CORRECT DATA SET
         BAS   R2,YESNO                RESPONSE "YES" OR "NO"?
         B     NEWCMD                  "NO", QUIT
         SPACE 1
         TM    DSORG,DS1DSGPO          PARTITIONED?
         BNO   FIXDCB                  NO, BRANCH
*** REOPEN THE DATA SET TO ENSURE DS1LSTAR IS CORRECT
         OI    FLAGSJJ,FNOREAD         CALL EXCP FOR OPEN ONLY
         MVC   STARTTR(3),DS1LSTAR     SET BEYOND END OF DATA SET
         L     R15,=V(EXCP)
         BASR  R14,R15
         NI    FLAGSJJ,FF-FNOREAD      CALL IS COMPLETED
         SPACE 2
         MVI   #DIRFIX,X'F9'           HIGH IN COLLATING SEQUENCE
         MVC   #DIRFIX+1(7),=C'FIXPDS ' TEMPORARY STOW AND DELETE NAME
         BAS   R6,RESERVE              RESERVE/ENQUEUE AS REQUIRED
         L     R4,#NEWDIR              DIRECTORY BLOCKS DESIRED
         N     R4,=X'7FFFFFFF'         CLEAR THE TOP BIT
         CLI   #FIXOPT,2               WHICH PROCESSING MODE?
         BL    FIXDCB                    DCB       -- CHANGE DCB ONLY
         BE    FIXRES                    RESETDIR  -- REWRITE DIRECTORY
         CLI   #FIXOPT,3
         BE    FIXINIT                   INITDIR   -- REWRITE DIRECTORY
***      BH    EXPAND                    EXPANDDIR -- ADD TO DIRECTORY
         SPACE 3
EXPAND   ICM   R1,B'1111',TOTMEMR      ANY MAIN
         A     R1,TOTMEMA                      OR ALIAS MEMBERS?
         BZ    FIXRES                  NO, USE RESETDIR INSTEAD
         SPACE 1
         MVI   SUBPOOLT,21             MARK TEMPORARY STORAGE OBTAINED
         LA    R0,1024*3               STORAGE FOR THREE NOTELISTS
         ICM   R0,B'1000',SUBPOOLT     SUBPOOL
         GETMAIN R,LV=(0)
         ST    R1,#NOTEPTR             POINTER TO NOTELIST WORK AREA
         BAS   R2,SHUTSTW2             CLOSE AND FREE THE STOW DCB
         SPACE 1
         MVI   OPENLIST,X'80'
         OPEN  (STOWDCB,OUTPUT),MF=(E,OPENLIST)  OPEN IT FOR OUTPUT
         SPACE 1
         TM    DCBOFLGS-IHADCB+STOWDCB,DCBOFOPN  STOW DCB OPEN?
         LA    R1,L831                           NO OPEN ERROR MSG
         BNO   MSGNEW                            NO, QUIT
         CLC   #MEMPTR(4),ZERO         ANY MEMBERS TO MOVE?
         BE    EXPAND80                NO, BRANCH
         LA    R0,80                   FIRST RECORD IS 80 BYTES LONG
         SPACE 2
*  ONE OR MORE MEMBERS MUST BE MOVED
*    WRITE GARBAGE RECORDS TO OCCUPY SPACE UP TO THE NEW DIRECTORY EOF
EXPAND02 STH   R0,STOWDCB+DCBBLKSI-IHADCB  UPDATE IN THE DCB
         L     R2,IOBUFF1              START OF DATA
         WRITE FIXPDSD,SF,STOWDCB,(2),'S',MF=E
         CHECK FIXPDSD
         NOTE  STOWDCB                 OUTPUT TTR
         LH    R0,BLKSI                MAXIMUM OUTPUT RECORD LENGTH
         CLM   R1,B'1100',#EOF2TT      PAST THE NEW DIRECTORY EOF?
         BNH   EXPAND02                NO, BRANCH
         ST    R1,#FIXTTRO             HIGHEST OUTPUT TTR
         LA    R3,#MEMPTR
         USING DIRLINK,R3              NO, BRANCH
         SPACE 1
EXPAND12 ICM   R3,B'0111',DIRLINK+1    NEXT MEMBER ENTRY
         BZ    EXPAND20                END OF LIST, BRANCH
         MVI   DIRLINK,X'80'           ASSUME THE MEMBER IS TO BE MOVED
         TM    DIRFLAG,DIRALIAS        ALIAS ENTRY?
         BNO   EXPAND18                NO, BRANCH
         LA    R2,#MEMPTR
         SPACE 1
EXPAND14 ICM   R2,B'0111',1(R2)              MORE IN THE LIST?
         BZ    EXPAND18                      NO, THIS ITEM WILL MOVE
         CR    R2,R3                         SAME ITEM?
         BE    EXPAND14                      YES, TRY AGAIN
         CLC   DIRTTR(3),DIRTTR-DIRLINK(R2)  SAME TTR?
         BNE   EXPAND14                      NO, BRANCH
         TM    DIRLINK-DIRLINK(R2),X'80'     OTHER ITEM TO BE MOVED?
         BO    EXPAND16                      YES, TRUE ALIAS
         TM    DIRFLAG-DIRLINK(R2),DIRALIAS  OTHER ITEM AN ALIAS?
         BO    EXPAND14                      YES, BRANCH
         SPACE 1
EXPAND16 MVI   DIRLINK,0                     DO NOT MOVE THIS ENTRY
EXPAND18 TM    DIRFLAG,DIR1TTR+DIR2TTR       ANY USER TTR'S?
         BZ    EXPAND12                      NO, BRANCH
         SPACE 1
         OI    DIRLINK,X'01'                 AT LEAST ONE TTR
         TM    DIRFLAG,DIR2TTR               TWO OR MORE TTR'S?
         BNO   *+8                           NO, BRANCH
         OI    DIRLINK,X'02'                 AT LEAST TWO TTR'S
         TM    DIRFLAG,DIR1TTR+DIR2TTR       THREE TTR'S?
         BNO   *+8                           NO, BRANCH
         OI    DIRLINK,X'04'                 THREE TTR'S
         B     EXPAND12
         SPACE 3
EXPAND20 LA    R3,#MEMPTR
EXPAND22 ICM   R3,B'0111',DIRLINK+1    ANY MORE MEMBERS IN THE LIST?
         BZ    EXPAND80                NO, BRANCH
         MVC   INSERT#1(8),DIRNAME     MEMBER NAME
         MVC   INSERT#2(8),=CL8'MOVED'
         M$MSG L051$2
         TM    DIRLINK,X'80'           MEMBER TO BE MOVED?
         BNO   EXPAND22                NO, BRANCH
         LA    R2,DIRSTART             FIRST USER TTR
         L     R4,#NOTEPTR             START OF NOTELIST STORAGE
         LA    R5,X'10'+X'01'          MASK BITS FOR TESTING
         LA    R6,3                    LOOP CONTROL
         SPACE 1
*  INPUT THE OVERLAY NOTELIST TABLE
         TM    DIRLINK,*-*             <<EXECUTED>>
EXPAND24 EX    R5,*-4                  ANOTHER TTR?
         BZ    EXPAND30                NO, BRANCH
         CLI   3(R2),0                 NOTELIST COUNT?
         BZ    EXPAND26                NO, BRANCH
         OI    DIRLINK,*-*             <<EXECUTED>>
         EX    R5,*-4                  MARK FOR NOTELIST PROCESSING
         MVC   STARTTR(3),0(R2)        NOTELIST TTR ADDRESS
         L     R15,=V(EXCP)            INPUT IT
         BASR  R14,R15
         LA    R1,L751                 ASSUME AN INPUT PROBLEM
         LTR   R15,R15                 CORRECT?
         BP    MSGNEW                  YES, BRANCH
         LR    R14,R0                  INPUT ADDRESS
         SR    R15,R15
         IC    R15,3(,R2)              NUMBER OF NOTELIST ENTRIES
         SLL   R15,2                   SIZE OF NOTELIST AREA
         LR    R0,R4                   START OF THIS NOTELIST AREA
         LR    R1,R15                  SAVE AREA MOVE LENGTH
         MVCL  R0,R14                  SAVE THE NOTELIST DATA
         SPACE 1
EXPAND26 LA    R2,4(,R2)               NEXT POTENTIAL USER TTR
         LA    R4,1024(,R4)            NEXT POTENTIAL NOTELIST AREA
         SLL   R5,1                    NEXT MASK BITS
         BCT   R6,EXPAND24
         SPACE 3
*  READ EACH RECORD OF THE MEMBER
EXPAND30 MVC   STARTTR(3),DIRTTR       FIRST TTR OF THE MEMBER
         MVC   #FINDTTR(3),DIRTTR      SAVE THE FIRST TTR FOR LATER
         SPACE 2
EXPAND32 L     R15,=V(EXCP)
         BASR  R14,R15
         B     *+4(R15)                PROCESS RETURN CODE
         B     EXPAND34                  00 - GOOD READ
         B     EXPAND60                  04 - END OF MEMBER
         B     EXPAND60                  08 - END OF DATA SET
         B     NEWCMD                    12 - I/O ERROR
         SPACE 1
EXPAND34 STM   R2,R12,28(R13)          CONVERT CCHHR TO TTR
         LA    R2,CURMBB
         L     R1,INDCB+(DCBDEBAD-IHADCB)
         L     R15,ADDRRLTV
         LR    R3,R13
         BASR  R14,R15
         LR    R13,R3
         LM    R2,R12,28(R13)
         ST    R0,#FIXTTRI             SAVE CURRENT INPUT TTR
         TM    DIRLINK,X'77'           ANY PENDING TTR UPDATES?
         BZ    EXPAND50                NO, BRANCH
         LA    R2,DIRSTART             FIRST USER TTR
         L     R4,#NOTEPTR             START OF NOTELIST STORAGE
         LA    R5,X'10'                MASK
         LA    R6,3                    LOOP CONTROL
         SPACE 1
         TM    DIRLINK,*-*             <<EXECUTED>>
EXPAND36 EX    R5,*-4                  ANY NOTELIST FOR THIS ENTRY?
         BZ    EXPAND38                NO, BRANCH
         CLM   R0,B'1110',0(R2)        IS THIS THE NOTELIST ENTRY?
         BE    EXPAND40                YES, BRANCH
EXPAND38 LA    R2,4(,R2)               NEXT USER TTR
         LA    R4,1024(,R4)            NEXT NOTELIST ENTRY
         SLL   R5,1                    NEXT MASK
         BCT   R6,EXPAND36
         B     EXPAND50                NOT THIS RECORD, BRANCH
         SPACE 2
EXPAND40 SR    R0,R0
         IC    R0,3(R2)                NUMBER OF NOTELIST ENTRIES
         LA    R1,L750                 ERROR MESSAGE FOR A PROBLEM
         XI    DIRLINK,*-*             <<EXECUTED>>
         EX    R5,*-4                  TURN OFF THIS NOTELIST ENTRY
         L     R5,EXCPBUFF             OUTPUT START ADDRESS
EXPAND42 TM    3(R4),X'80'             NOTELIST TTR PREVIOUSLY UPDATED?
         BNO   MSGNEW                  NO, ERROR
         MVC   0(3,R5),0(R4)           REPLACE THE BUFFER TTR
         LA    R4,4(,R4)               NEXT STORAGE ADDRESS
         LA    R5,4(,R5)               NEXT BUFFER ADDRESS
         BCT   R0,EXPAND42             MOVE AND CHECK ALL ENTRIES
         SPACE 3
*  OUTPUT EACH RECORD AND CHANGE TTR POINTERS AS NECESSARY
EXPAND50 L     R0,LS                   RECORD LENGTH
         STH   R0,STOWDCB+DCBBLKSI-IHADCB  UPDATE IN THE DCB
         L     R2,EXCPBUFF             START OF DATA
         WRITE FIXPDSD,SF,STOWDCB,(2),'S',MF=E
         CHECK FIXPDSD
         NOTE  STOWDCB                 OUTPUT TTR
         ST    R1,#FIXTTRO
         SPACE 1
         CLC   DIRTTR(3),#FINDTTR      FIRST MEMBER RECORD?
         BNE   *+10                    NO, BRANCH
         MVC   DIRTTR(3),#FIXTTRO      YES, UPDATE THE TTR
         SPACE 1
         TM    DIRLINK,X'77'           ANY PENDING TTR UPDATES?
         BZ    EXPAND32                NO, BRANCH
         LA    R2,DIRSTART             FIRST USER TTR
         L     R4,#NOTEPTR             START OF NOTELIST
         LA    R5,X'10'                NOTELIST MASK
         LA    R6,3                    LOOP CONTROL
         LA    R14,X'01'               TTR MASK
         SPACE 1
         TM    DIRLINK,*-*             <<EXECUTED>>
EXPAND52 EX    R14,*-4                 TTR IN THIS POSITION?
         BNO   EXPAND54                NO, BRANCH
         CLC   #FIXTTRI(3),0(R2)       THIS INPUT TTR?
         BNE   EXPAND54                NO, BRANCH
         STCM  R1,B'1110',0(R2)        YES, UPDATE THE TTR
         XI    DIRLINK,*-*             <<EXECUTED>>
         EX    R14,*-4                 TURN OFF THE TTR INDICATOR
         SPACE 1
         TM    DIRLINK,*-*             <<EXECUTED>>
EXPAND54 EX    R5,*-4                  NOTELIST FOR THIS ENTRY?
         BNO   EXPAND58                NO, BRANCH
         LR    R15,R4                  START OF NOTELIST
         SR    R0,R0
         IC    R0,3(R2)                NUMBER OF NOTELIST ENTRIES
EXPAND56 CLC   0(3,R15),#FIXTTRI       THIS TTR?
         BNE   *+12                    NO, BRANCH
         STCM  R1,B'1110',0(R15)       YES, UPDATE THE TTR
         OI    3(R15),X'80'                 AND MARK AS UPDATED
         LA    R15,4(,R15)             NEXT NOTELIST ENTRY
         BCT   R0,EXPAND56
EXPAND58 LA    R2,4(,R2)               NEXT USER TTR
         LA    R4,1024(,R4)            NEXT NOTELIST STORAGE
         SLL   R5,1                    NEXT NOTELIST BIT MASK
         SLL   R14,1                   NEXT TTR BIT MASK
         BCT   R6,EXPAND52
         B     EXPAND32               MOVE ALL MEMBERS
         SPACE 3
*  VERIFY THAT THE MEMBER WAS CORRECTLY PROCESSED
EXPAND60 LA    R1,L871                 ASSUME A TTR WAS NOT UPDATED
         TM    DIRLINK,X'77'           CORRECT?
         BNZ   MSGNEW                  YES, BRANCH
         SPACE 1
         CLC   DIRTTR(3),#FINDTTR      A NULL MEMBER?
         BNE   EXPAND62                NO, BRANCH
         SPACE 1
* UPDATE TTR BY TWO AND STOW AGAIN FOR NULL MEMBERS
         STOW  STOWDCB,DIRNAME,D
         LA    R1,L833
         LTR   R15,R15
         BP    EXIT8M
         SPACE 1
         ICM   R1,B'0111',#FIXTTRO                 HIGHEST OUTPUT TTR
         LA    R1,2(,R1)
         STCM  R1,B'0111',DCBRELAD-IHADCB+STOWDCB  HIGHEST OUTPUT TTR+2
         STOW  STOWDCB,DIRNAME,A
         LA    R1,L832
         LTR   R15,R15
         BP    EXIT8M
         SPACE 1
         SR    R1,R1                               YES, CHANGE THE TTR
         ICM   R1,B'0111',DCBRELAD-IHADCB+STOWDCB  TTR OF LAST EOF
         S     R1,=F'1'                            TTR OF NULL DATA
         STCM  R1,B'0111',DIRTTR                   TTR OF NULL DATA
         STCM  R1,B'0111',#FIXTTRO                 HIGHEST OUTPUT TTR
         SPACE 2
EXPAND62 TM    DIRFLAG,DIRALIAS        ALIAS (WITH NO MAIN)?
         BNO   EXPAND70                NO, BRANCH
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR  TTR
         XI    DIRFLAG,DIRALIAS        TEMPORARILY TURN OFF ALIAS BIT
         STOW  STOWDCB,DIRNAME,R       REPLACE THIS MEMBER ENTRY
         OI    DIRFLAG,DIRALIAS        RESET THE ALIAS BIT
         SPACE 1
         B     *+4(R15)                PROCESS RETURN CODE
         B     EXPAND70                  00 - REPLACED
         EX    0,*                       04 - SHOULD NOT OCCUR
         EX    0,*                       08 - SHOULD NOT OCCUR
         B     FULLDIR                   12 - DIRECTORY FULL
         B     IOERROR                   16 - I/O ERROR
         SPACE 3
*  UPDATE THIS MEMBER AND ALL OF ITS ALIASES
EXPAND70 LA    R2,#MEMPTR
         LR    R4,R3
         SPACE 1
EXPAND72 MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR-DIRLINK(R4)  TTR
         STOW  STOWDCB,4(R4),R         REPLACE THIS MEMBER ENTRY
         SPACE 1
         B     *+4(R15)                PROCESS RETURN CODE
         B     EXPAND74                  00 - REPLACED
         EX    0,*                       04 - SHOULD NOT OCCUR
         EX    0,*                       08 - SHOULD NOT OCCUR
         B     FULLDIR                   12 - DIRECTORY FULL
         B     IOERROR                   16 - I/O ERROR
         SPACE 1
EXPAND74 ICM   R2,B'0111',1(R2)               NEXT MEMBER ENTRY
         BZ    EXPAND22                       END OF LIST, BRANCH
         CLC   #FINDTTR(3),DIRTTR-DIRLINK(R2) THIS TTR TOO?
         BNE   EXPAND74                       NO, BRANCH
         CR    R2,R3                          SAME ENTRY?
         BE    EXPAND74                       YES, BRANCH
         LR    R4,R2
         NI    DIRLINK-DIRLINK(R2),FF-X'80'   DO NOT MOVE THIS MEMBER
         MVC   DIRTTR-DIRLINK(3,R2),DIRTTR    NEW PRIMARY TTR
         LA    R14,DIRSTART-DIRLINK(,R2)      FIRST ALIAS USER TTR
         LA    R15,DIRSTART                   FIRST MOVED USER TTR
         LA    R5,X'01'                       MASK BIT
         LA    R6,3                           LOOP CONTROL
         SPACE 1
         TM    DIRLINK-DIRLINK(R2),*-*        <<EXECUTED>>
EXPAND76 EX    R5,*-4                         A TTR AT THIS ENTRY?
         BNO   EXPAND72                       NO, BRANCH
         MVC   0(3,R14),0(R15)                YES, UPDATE THE TTR ENTRY
         LA    R14,4(,R14)                    NEXT ALIAS TTR
         LA    R15,4(,R15)                    NEXT MOVED TTR
         SLL   R5,1                           NEXT MASK BIT
         BCT   R6,EXPAND76
         B     EXPAND72
         DROP  R3
         SPACE 3
*  WRITE THE ADDED DIRECTORY RECORDS
EXPAND80 DS    0H                                              SS MAY90
         MVC   STARTTR,DS1LSTAR               FORCE REOPEN     SS MAY90
         L     R15,=V(EXCP)                   EXCP ROUTINE     SS MAY90
         BASR  R14,R15                        EXCP ROUTINE     SS MAY90
         L     R6,#NEWDIR                     DIRECTORY BLOCKS TO ADD
         N     R6,=X'7FFFFFFF'                CLEAR THE TOP BIT
         LA    R0,CCW1                        FIRST CCW
         ST    R0,IOBCCW                               TO USE
         MVI   CCW3,X'1D'                     COMMAND FOR WRITE C,K,D
         LA    R0,MSGTEXT1+3                  STORAGE FOR
         STCM  R0,B'0111',CCW3+1                         WRITE ADDRESS
         NI    CCW3+4,FF-X'40'                LAST CCW IN CHAIN
         MVC   CCW3+6(2),=H'8'                DATA LENGTH IS EIGHT
         XC    MSGTEXT1(20),MSGTEXT1          CLEAR THE WRITE ADDRESS
         MVI   MSGTEXT1+8,8                   KEY IS EIGHT BYTES
         MVI   MSGTEXT1+9,1                   DATA IS 1*256 BYTES
         MVI   OPENLIST,X'80'
         CLOSE (INDCB),MF=(E,OPENLIST)
         OPEN  (INDCB,UPDAT),MF=(E,OPENLIST)
         SPACE 2
EXPAND82 L     R0,#EOF1TTR                    POINT TO LAST RECORD
         STM   R15,R12,12+4(R13)              CONVERT TTR TO MBBCCHHR
         L     R1,INDCB+(DCBDEBAD-IHADCB)     DEB ADDRESS
         LA    R2,MSGTEXT1                    RETURN CCHHR
         L     R15,ADDRCNVT                   TTR TO MBBCCHHR CONVERT
         LR    R3,R13                         SAVE AREA ADDRESS
         BASR  R14,R15
         LR    R13,R3                         SAVE AREA ADDRESS
         LM    R15,R12,12+4(R13)              RESTORE REGISTERS
         MVC   IOBSEEK(8),MSGTEXT1
         LTR   R6,R6                          ANY RECORD TO WRITE?
         BZ    EXPAND84                       NO, BRANCH
         SR    R1,R1
         IC    R1,IOBSEEK+7
         S     R1,=F'1'                       REDUCE INITIAL TTR
         STC   R1,IOBSEEK+7
         MVI   IOECB,0
         EXCP  IOB                            WRITE THIS RECORD
         WAIT  ECB=IOECB                      WAIT FOR I/O COMPLETION
         LA    R1,L836                        I/O ERROR IN DIRECTORY
         CLI   IOECB,X'7F'                    SUCCESSFUL WRITE?
         BNE   EXIT8M                         NO, ERROR
         SR    R1,R1
         IC    R1,#EOF1TTR+2                  INCREMENT
         A     R1,=F'1'                                R OF
         STC   R1,#EOF1TTR+2                               TTR
         C     R1,#DIRBTRK                    FIT ON THIS TRACK?
         BNH   EXPAND84                       YES, BRANCH
         LH    R1,#EOF1TTR                    INCREMENT
         N     R1,=XL4'0000FFFF'
         A     R1,=F'1'                                TT OF
         STH   R1,#EOF1TTR                                  TTR
         MVI   #EOF1TTR+2,1                   RESET R OF TTR
         SPACE 1
EXPAND84 S     R6,=F'1'                       MORE BLOCKS?
         BP    EXPAND82                       YES, BRANCH
         S     R6,=F'1'                       ENSURE R6 IS NEGATIVE
         SPACE 2
*  WRITE THE DATA SET END-OF-FILE MARKER
         OC    MSGTEXT1+8(3),MSGTEXT1+8          END-OF-FILE WRITTEN?
         BZ    FIXDCB                            YES, BRANCH
         XC    MSGTEXT1+8(3),MSGTEXT1+8          SET KEY, DL=0
         TM    #NEWDIR,X'80'                     END OF TRACK?
         BNO   *+10                              NO, BRANCH
         MVC   INDCB+DCBTRBAL-IHADCB(2),ZERO     FORCE TO NEXT TRACK
         B     EXPAND82
         SPACE 3
         AGO   .TESTNON
***TEST  FOR NO DIRECTORY NUMBER CHANGE
FIXRES   BAS   R2,CLOSEIT                        CLOSE INPUT DATA SET
         L     R0,=A(CHANGE2)                    ABEND RECOVERY ADDRESS
         ST    R0,RECOVER
         MVC   BAMDCB(LDIRDCB),DIRDCB            COPY PROTOTYPE DCB
         MVC   DCBDDNAM-IHADCB+BAMDCB,DDNAME     CURRENT DDNAME
         MVI   OPENLIST,X'80'                    END OF LIST MARKER
         OPEN  (BAMDCB,(UPDAT)),MF=(E,OPENLIST)
         TM    DCBOFLGS-IHADCB+BAMDCB,X'10'  DIRECTORY OPEN?
         BNO   EXIT12O                       NO, QUIT
         SPACE 1
         LA    R2,WORKTBL              START OF BUFFER
         READ  FIXPDSD,SF,BAMDCB,(2),'S',MF=E
         CHECK FIXPDSD
         SPACE 1
         LA    R2,WORKTBL              START OF BUFFER
         MVC   0(18,R2),KEYDATA        MOVE IN THE KEY AND DATA
         XC    18(256,R2),18(R2)       FOLLOWED BY ZEROES
         WRITE FIXPDSD,SF,BAMDCB,(2),'S',MF=E
         CHECK FIXPDSD
         CLOSE (BAMDCB),MF=(E,OPENLIST)
         B     FIXDCB
.TESTNON ANOP
         SPACE 1
FIXRES   BAS   R2,CLOSEIT                        CLOSE INPUT DATA SET
         L     R0,=A(CHANGE2)                    ABEND RECOVERY ADDRESS
         ST    R0,RECOVER
         OI    SPFLAG3,SPFCHN                    TELL ISPMODE
         MVC   BAMDCB(LDIRDCB),DIRDCB            COPY PROTOTYPE DCB
         MVC   DCBDDNAM-IHADCB+BAMDCB,DDNAME     CURRENT DDNAME
         MVI   OPENLIST,X'80'                    END OF LIST MARKER
         OPEN  (BAMDCB,(OUTPUT)),MF=(E,OPENLIST)
         TM    DCBOFLGS-IHADCB+BAMDCB,X'10'  DIRECTORY OPEN?
         BNO   EXIT12O                       NO, QUIT
         SPACE 1
         AGO   .TESTTRK
***TEST FOR 1 TRK EXTENT AND LESS THAN 3 DIRECTORY BLOCKS
         L     R1,DCBDEBAD-IHADCB+BAMDCB     GET DEB ADDRESS
         SR    R15,R15
         ICM   R15,3,32+14(R1)               NUMBER OF TRACKS IN EXTENT
         C     R15,=F'2'                     1 TRACK EXTENT?
         BNL   FIXRES05                      NO
         C     R4,=F'3'                      YES, DIRBLKS < 3?
         BNL   FIXRES05                           NO
         LA    R4,3                               YES, INCREASE TO 3
FIXRES05 DS    0H
.TESTTRK ANOP
         SPACE 1
         MVI   SUBPOOLT,21                   TEMPORARY STORAGE IN USE
         LA    R0,50*20                      50 I/O CONCURRENTLY
         SPACE 1
         ICM   R0,B'1000',SUBPOOLT
         GETMAIN R,LV=(0)
         LR    R3,R1                         START OF DECB LIST
         LA    R15,50*20                     LENGTH TO CLEAR
         LR    R14,R3                        START OF AREA
         SR    R1,R1                         NULL SOURCE
         MVCL  R14,R0                        PAD 0 FROM R1(0:7)
         SPACE 1
         MVC   MSGTEXT1(18),KEYDATA          MOVE IN THE KEY AND DATA
         XC    MSGTEXT1+18(256),MSGTEXT1+18  FOLLOWED BY ZEROES
         LA    R2,WORKTBL                    START OF BUFFER
         XC    0(18,R2),0(R2)                ZERO KEY
         XC    18(256,R2),18(R2)             AND MORE ZEROS
FIXRES10 LR    R5,R4                         NUMBER OF BLOCKS
         C     R5,=F'50'                     VALID COUNT?
         BL    *+8                           YES, BRANCH
         L     R5,=F'50'                     NO, USE 50
         LR    R6,R3                         START OF DECB'S
         LA    R2,MSGTEXT1                   FIRST BLOCK TO WRITE
         SPACE 2
FIXRES30 WRITE (R6),SF,BAMDCB,(R2),'S',MF=E
         LA    R2,WORKTBL              START OF BUFFER
         LA    R6,20(,R6)              NEXT DECB
         S     R5,=F'1'                SUFFICIENT BLOCKS WRITTEN?
         BP    FIXRES30                NO, BRANCH
         SPACE 2
         LR    R5,R4                   NUMBER OF BLOCKS
         C     R5,=F'50'               VALID COUNT?
         BL    *+8                     YES, BRANCH
         L     R5,=F'50'               NO, USE 50
         SR    R4,R5                   UPDATE TOTAL REMAINING
         LR    R6,R3                   START OF DECB'S
FIXRES50 CHECK (R6)                    CHECK THIS OUTPUT OPERATION
         LA    R6,20(,R6)              NEXT DECB
         S     R5,=F'1'                SUFFICIENT BLOCKS WRITTEN?
         BP    FIXRES50                NO, BRANCH
         XC    MSGTEXT1(18),MSGTEXT1   CLEAR THE OUTPUT BLOCK
         LTR   R5,R4                   ANY BLOCKS?
         BP    FIXRES10                YES, LOOP
         SPACE 2
FIXRES60 TM    #NEWDIR,X'80'                     END OF TRACK?
         BNO   *+10                              NO, BRANCH
         MVC   BAMDCB+DCBTRBAL-IHADCB(2),ZERO    FORCE TO NEXT TRACK
         CLOSE (BAMDCB),MF=(E,OPENLIST)
         SPACE 3
         MVI   OPENLIST,X'80'          END OF LIST MARKER
         MVC   STOWDCB(LSAMDCB),SAMDCB
         MVC   DCBDDNAM-IHADCB+STOWDCB,DDNAME
         MVI   ##ADRCM#,FIXOPEN        MARK FOR DCB OPEN EXIT USE
         SPACE 2
         OPEN  (STOWDCB,(OUTPUT)),MF=(E,OPENLIST)
         TM    DCBOFLGS-IHADCB+STOWDCB,X'10'  OUT OPEN?
         BNO   EXIT12O                        NO, QUIT
         SPACE 1
         STOW  STOWDCB,#DIRFIX,A       ADD A NULL MEMBER
         LA    R1,L832                 NOT ADDED MESSAGE
         LTR   R15,R15                 ADDED?
         BP    IOERRORE                NO, QUIT WITH MESSAGE
*        BP    EXIT8M                  NO, QUIT
         STOW  STOWDCB,#DIRFIX,D       DELETE THE DUMMY MEMBER
         LA    R1,L833                 NOT DELETED MESSAGE
         LTR   R15,R15                 DELETED?
         BP    IOERRORE                NO, QUIT WITH MESSAGE
*        BP    EXIT8M                  NO, QUIT
         B     FIXDCB
         SPACE 1
*  REWRITE THE DIRECTORY WITHOUT CHANGING DS1LSTAR
FIXINIT  BAS   R2,CLOSEIT                     CLOSE INPUT DATA SET
         L     R0,=A(CHANGE2)                 ABEND RECOVERY ADDRESS
         ST    R0,RECOVER
         OI    SPFLAG3,SPFCHN                 TELL ISPMODE
         L     R6,#NEWDIR                     DIRECTORY BLOCKS TO ADD
         N     R6,=X'7FFFFFFF'                CLEAR THE TOP BIT
         LA    R0,CCW1                        FIRST CCW
         ST    R0,IOBCCW                               TO USE
         MVI   CCW3,X'1D'                     COMMAND FOR WRITE C,K,D
         LA    R0,MSGTEXT1+3                  STORAGE FOR
         STCM  R0,B'0111',CCW3+1                         WRITE ADDRESS
         NI    CCW3+4,FF-X'40'                LAST CCW IN CHAIN
         MVC   CCW3+6(2),=H'264'              DATA LENGTH IS 264
         XC    MSGTEXT1(11),MSGTEXT1          CLEAR THE WRITE ADDRESS
         MVI   MSGTEXT1+8,8                   KEY IS EIGHT BYTES
         MVI   MSGTEXT1+9,1                   DATA IS 1*256 BYTES
         MVC   MSGTEXT1+11(18),KEYDATA        MOVE IN THE KEY AND DATA
         XC    MSGTEXT1+29(256),MSGTEXT1+29   FOLLOWED BY ZEROES
         MVI   OPENLIST,X'80'
         CLOSE (INDCB),MF=(E,OPENLIST)
         OPEN  (INDCB,UPDAT),MF=(E,OPENLIST)
         XC    #EOF1TTR,#EOF1TTR              RESET THE STARTING TTR
         MVI   #EOF1TTR+2,1                   TTR=1
         SPACE 2
FIXINIT2 L     R0,#EOF1TTR                    POINT TO LAST RECORD
         STM   R15,R12,12+4(R13)              CONVERT TTR TO MBBCCHHR
         L     R1,INDCB+(DCBDEBAD-IHADCB)     DEB ADDRESS
         LA    R2,MSGTEXT1                    RETURN CCHHR
         L     R15,ADDRCNVT                   TTR TO MBBCCHHR CONVERT
         LR    R3,R13                         SAVE AREA ADDRESS
         BASR  R14,R15
         LR    R13,R3                         SAVE AREA ADDRESS
         LM    R15,R12,12+4(R13)              RESTORE REGISTERS
         MVC   IOBSEEK(8),MSGTEXT1
         LTR   R6,R6                          ANY RECORD TO WRITE?
         BZ    FIXINIT4                       NO, BRANCH
         SR    R1,R1
         IC    R1,IOBSEEK+7
         S     R1,=F'1'                       REDUCE INITIAL TTR
         STC   R1,IOBSEEK+7
         MVI   IOECB,0
         EXCP  IOB                            WRITE THIS RECORD
         WAIT  ECB=IOECB                      WAIT FOR I/O COMPLETION
         XC    MSGTEXT1+11(18),MSGTEXT1+11    RESET THE KEY AND DATA
         LA    R1,L836                        I/O ERROR IN DIRECTORY
         CLI   IOECB,X'7F'                    SUCCESSFUL WRITE?
         BNE   EXIT8M                         NO, ERROR
         SR    R1,R1
         IC    R1,#EOF1TTR+2                  INCREMENT
         A     R1,=F'1'                                R OF
         STC   R1,#EOF1TTR+2                               TTR
         C     R1,#DIRBTRK                    FIT ON THIS TRACK?
         BNH   FIXINIT4                       YES, BRANCH
         LH    R1,#EOF1TTR                    INCREMENT
         N     R1,=XL4'0000FFFF'
         A     R1,=F'1'                                TT OF
         STH   R1,#EOF1TTR                                  TTR
         MVI   #EOF1TTR+2,1                   RESET R OF TTR
         SPACE 1
FIXINIT4 S     R6,=F'1'                       MORE BLOCKS?
         BP    FIXINIT2                       YES, BRANCH
         S     R6,=F'1'                       ENSURE R6 IS NEGATIVE
         SPACE 2
*  WRITE THE DATA SET END-OF-FILE MARKER
         OC    MSGTEXT1+8(3),MSGTEXT1+8          END-OF-FILE WRITTEN?
         BZ    FIXDCB                            YES, BRANCH
         XC    MSGTEXT1+8(3),MSGTEXT1+8          SET KEY, DL=0
         TM    #NEWDIR,X'80'                     END OF TRACK?
         BNO   *+10                              NO, BRANCH
         MVC   INDCB+DCBTRBAL-IHADCB(2),ZERO     FORCE TO NEXT TRACK
         B     FIXINIT2
         SPACE 3
FIXDCB   MVC   WORKTBL+4(3),DS1LSTAR   LAST USED TTR ADDRESS
         MVC   WORKTBL+8(2),DS1TRBAL   BYTES LEFT ON THIS TRACK
         BAS   R2,CLOSEIT              CLOSE THE INPUT DCB
         SPACE 1
         CLI   #FIXOPT3,0              ANY ADDTRK OPTION?
         BE    FIXDCB8                 NO, BRANCH
         SPACE 2
         L     R0,=A(CHANGE2)                  ABEND RECOVERY ADDRESS
         ST    R0,RECOVER
         MVC   INDCB(LEXCPDCB),EXCPDCB         MOVE PATTERN DCB
         CLC   =C'8',DS1FMTID                  FORMAT 8 DSCB? DRK OCT18
         BNE   FIXNOEAV                        NO                     "
         AIF   ('&MVSLEV' LT 'MVS710').NOEAV1                 DRK JAN13
         MVC   INDCB(LEXCPDCE),EXCPDCBE        YES, MOVE PATTERN DCB  "
         LA    R14,FIRST4K         BASE FOR INDCBE             JH DEC12
         MVC   INDCBE-FIRST4K(C_DCBEL,R14),C_DCBE              JH DEC12
         LA    R1,INDCBE-FIRST4K(,R14)                         JH DEC12
         ST    R1,DCBDCBE-IHADCB+INDCB                         JH DEC12
.NOEAV1  ANOP                                                 DRK JAN13
FIXNOEAV DS    0H                                             DRK OCT18
         LA    R1,WORKTBL+16
         ST    R1,WORKTBL
         OI    WORKTBL,X'87'                   MARK JFCB EXIT ONLY
         LA    R1,WORKTBL
         STCM  R1,B'0111',DCBEXLSA-IHADCB+INDCB  UPDATE EXIT LIST
         MVC   DCBDDNAM-IHADCB+INDCB(8),DDNAME
         MVI   OPENLIST,X'80'                  MARK END OF LIST
         BAS   R6,RESERVE                      RESERVE THE FILE
         SPACE 2
         RDJFCB (INDCB,(OUTPUT)),MF=(E,OPENLIST)
         NI    WORKTBL+16+JFCBCTRI-INFMJFCB,FF-JFCBSPAC
         OI    WORKTBL+16+JFCBCTRI-INFMJFCB,JFCBTRK  ASSUME TRK
         CLI   #FIXOPT3,1                            CORRECT?
         BE    *+8                                   YES, BRANCH
         OI    WORKTBL+16+JFCBCTRI-INFMJFCB,JFCBCYL  NO, USE CYL
         MVC   WORKTBL+16+JFCBSQTY-INFMJFCB(3),#FIXOPT3+1  SIZE
         OI    WORKTBL+16+JFCBMASK-INFMJFCB+4,X'80'  REWRITE THE JFCB
         SPACE 2
         OPEN  (INDCB,(OUTPUT)),TYPE=J,MF=(E,OPENLIST)
         TM    DCBOFLGS-IHADCB+INDCB,X'10'     OUT OPEN?
         BNO   EXIT12O                         NO, QUIT
         EOV   INDCB                           ADD AN EXTENT
         SPACE 2
         ICM   R0,B'0111',WORKTBL+4            PREVIOUS DS1LSTAR
         SLL   R0,8                            TTR0 -- NEW DS1LSTAR
         STM   R14,R12,12(R13)                 SAVE REGISTERS
         L     R1,DCBDEBAD-IHADCB+INDCB        DEB ADDRESS
         LA    R2,DCBFDAD-IHADCB+INDCB         RETURN ADDRESS
         L     R15,ADDRCNVT                    CONVERSION ROUTINE
         LR    R3,R13                          SAVE R13
         BASR  R14,R15
         LR    R13,R3                          RESTORE R13
         LM    R14,R12,12(R13)                 RESTORE REGISTERS
         MVC   DCBTRBAL-IHADCB+INDCB(2),WORKTBL+8  PREVIOUS TRBAL
         CLOSE (INDCB),MF=(E,OPENLIST)
         SPACE 2
FIXDCB8  TM    DSORG,DS1DSGPO          PARTITIONED DATA SET?
         BO    FIXDCB10                YES, BRANCH
         LA    R0,RESTART0             ABEND RECOVERY ADDRESS
         ST    R0,RECOVER
         BAS   R2,DEALLDCB             FREE THE DATA SET
         MVI   DSPALLOC,CONOMOD        ASSUME MOD -- NO CHANGE
         CLI   #FIXOPT,2               RESET PROCESSING?
         BNE   *+8                     NO, BRANCH
         MVI   DSPALLOC,CONOOLD        YES, OPEN FOR RESET PROCESSING
         MVI   ##ADRCM#,EDITOR         NO REPEATED WARNING MESSAGE
         L     R15,=V(ALLOCATE)        ALLOCATION ROUTINE
         BASR  R14,R15                 GOOD ALLOCATION?
         B     RESTART0                NO, ERROR
         SPACE 1
         L     R8,##ADRCMD             REESTABLISH THE BASE REGISTER
         CLI   DSPALLOC,CONOSHR        REQUESTED ALLOCATION?
         BE    RESTART2                NO, OLD ALLOCATION FAILED
         SPACE 1
         L     R0,=A(CHANGE2)          ABEND RECOVERY ADDRESS
         ST    R0,RECOVER
         MVI   OPENLIST,X'80'          END OF LIST MARKER
         MVC   STOWDCB(LSAMDCB),SAMDCB
         MVI   DCBDSORG-IHADCB+STOWDCB,DS1DSGPS  SET FOR SEQUENTIAL
         MVC   DCBDDNAM-IHADCB+STOWDCB,DDNAME
         MVI   ##ADRCM#,FIXOPEN                  MARK FOR DCB OPEN EXIT
         SPACE 1
         OPEN  (STOWDCB,(OUTPUT)),MF=(E,OPENLIST)
         TM    DCBOFLGS-IHADCB+STOWDCB,X'10'  OUT OPEN?
         BNO   EXIT12O                        NO, QUIT
         B     FIXDCB20
         SPACE 1
FIXDCB10 L     R0,=A(CHANGE2)          ABEND RECOVERY ADDRESS
         ST    R0,RECOVER
         MVI   OPENLIST,X'80'          END OF LIST MARKER
         MVC   STOWDCB(LSAMDCB),SAMDCB
         MVC   DCBDDNAM-IHADCB+STOWDCB,DDNAME
         MVI   ##ADRCM#,FIXOPEN                  MARK FOR DCB OPEN EXIT
         SPACE 1
         OPEN  (STOWDCB,(OUTPUT)),MF=(E,OPENLIST)
         BAS   R6,RESERVE
         TM    DCBOFLGS-IHADCB+STOWDCB,X'10'  OUT OPEN?
         BNO   EXIT12O                        NO, QUIT
         SPACE 2
FIXDCB20 CLI   #FIXOPT2,4                      MAXSPACE?
         BNE   FIXDCB30                        NO, BRANCH
         L     R1,DCBDEBAD-IHADCB+STOWDCB      GET DEB ADDRESS
         LH    R14,NUMEXT                      NUMBER OF EXTENTS
         SR    R0,R0
         SR    R15,R15
*
         ICM   R15,3,32+14(R1) <----+   NUMBER OF TRACKS IN EXTENT
         AR    R0,R15               |   ADD NUMBER OF TRACKS
         LA    R1,16(,R1)           |   NEXT EXTENT
         BCT   R14,*-4-2-4  --------+   BRANCH FOR ALL EXTENTS
*
         S     R0,=F'1'                        MAX 00TT OF TTR VALID?
         BM    FIXDCB30                        NO, BRANCH
         SLL   R0,8                            0TTX
         A     R0,=F'1'                        0TT1 (R OF TTR IS 1)
         SLL   R0,8                            TT10 -- NEW DS1LSTAR
         STM   R14,R12,12(R13)                 SAVE REGISTERS
         L     R1,DCBDEBAD-IHADCB+STOWDCB      DEB ADDRESS
         LA    R2,DCBFDAD-IHADCB+STOWDCB       RETURN ADDRESS
         L     R15,ADDRCNVT                    CONVERSION ROUTINE
         LR    R3,R13                          SAVE R13
         BASR  R14,R15
         LR    R13,R3                          RESTORE R13
         LM    R14,R12,12(R13)                 RESTORE REGISTERS
         XC    DCBTRBAL-IHADCB+STOWDCB(2),DCBTRBAL-IHADCB+STOWDCB
         SPACE 2
FIXDCB30 CLOSE (STOWDCB),MF=(E,OPENLIST)
         M$ERRST MSGBLANK              ONE SPACING LINE
         L     R1,=A(DSNAMES)          ALLOCATION STATUS ROUTINE
         ST    R1,##ADRCMD             EXECUTED AFTER OPEN OF DATA SET
         MVI   ##ADRCM#,CONTINUE+EDITOR  CONTINUE AND NO WARNING
         CLI   #FIXOPT2,0              RELEASE/RELSAVE/RELEXT/MAXSPACE?
         BE    FIXDCB90                NO, BRANCH
         CLI   #FIXOPT2,4              MAXSPACE?
         BE    FIXDCB90                YES, BRANCH
         MVC   MSGTEXT1+4(8),##SUBCAL  RELEASE COMMAND NAME
         MVI   MSGTEXT1+12,X'40'       ADD A BLANK
         MVI   MSGTEXT1+13,C''''       ADD A QUOTE
         MVC   MSGTEXT1+14(44),DSNAME  ADD THE DSNAME
         LH    R15,DSNLEN              LENGTH OF DSNAME
         LA    R3,MSGTEXT1+14(R15)     WHERE ADDITIONAL TEXT STARTS
         MVC   0(22,R3),BLANK128       BLANK IT FIRST
         MVI   0(R3),C''''             ADD A FINAL QUOTE
         LA    R4,14+22(,R15)          LENGTH OF COMMAND BUFFER
         SLL   R4,16
         ST    R4,MSGTEXT1             UPDATE LENGTH FIELD
         SPACE 1
         CLI   #FIXOPT2,1              RELEASE ALL?
         BE    FIXDCB70                YES, BRANCH
         MVC   2(3,R3),FIXLEXT         ADD EXT FOR EXTENTS
         CLI   #FIXOPT2,3              RELEASE EXTENTS ONLY?
         BE    FIXDCB70                YES, BRANCH
         MVC   2(4,R3),FIXLSPA         ADD SPA( FOR SPACE(
         L     R1,#FIXRES              TRACKS TO RESERVE
         CVD   R1,DOUBLE
         OI    DOUBLE+7,X'0F'          CONVERT SIGN
         UNPK  2+4(5,R3),DOUBLE+5(3)   CONVERT TO PRINTABLE
         MVI   2+4+5(R3),C')'          ENDING )
         SPACE 1
         AIF ('&CF296' EQ 'YES').YESF296
FIXDCB70 B     FIXDCB80                ***ZAP (NOP ) TO ADD V(SERIAL)
         AGO   .NOTF296
.YESF296 ANOP
FIXDCB70 NOP   FIXDCB80                ***ZAP (B ) TO BYPASS V(SERIAL)
.NOTF296 ANOP
         MVI   2+4+7(R3),C'V'          V OF VOLUME
         MVI   2+4+8(R3),C'('          BEGINNING (
         MVC   2+4+8+1(6,R3),VOLALLOC  VOLUME ALLOCATED
         MVI   2+4+8+1+6(R3),C')'      ENDING )
         SPACE 1
FIXDCB80 LA    R1,MSGTEXT1             START OF COMMAND BUFFER
         ST    R1,ADDRTEXT             COMMAND ADDRESS
         MVI   3(R1),9                 OFFSET TO OPERANDS
         L     R1,ADDRTEXT             COMMAND ADDRESS
         ST    R1,ADDRCBUF             COMMAND ADDRESS
         NOP   FIXDCB89                *** CHANGE TO BRANCH TO ATTACH
         L     R15,=A(RX)              EXTERNAL INTERFACE START
         BASR  R2,R15                  EXTERNAL LINK TO RELEASE
         BAS   R2,DEALLDCB             FREE THE DATA SET
         M$ERRST MSGBLANK              ONE SPACING LINE
         B     RESTART0                REALLOCATE AND START OVER
         SPACE 2
FIXDCB89 DS    0H                      *** ALTERNATE ATTACH METHOD
         LA    R3,##SUBCAL             RELEASE COMMAND PROCESSOR
         BAS   R2,ATTACH               CALL THE COMMAND
FIXDCB90 BAS   R2,DEALLDCB             FREE THE DATA SET
         M$ERRST MSGBLANK              ONE SPACING LINE
         B     RESTART0                REALLOCATE AND START OVER
FIXPDSE  DC    C' - SOME FUNCTIONS LIMITED'
FIXLEXT  DC    C'EXT'
FIXLSPA  DC    C'SPA('

         AIF   ('&MVSLEV' LT 'MVS710').NOEAV2                 DRK JAN13
EXCPDCBE DCB   DDNAME=X,MACRF=E,DEVD=DA,DCBE=*-*               JH DEC12
LEXCPDCE EQU   *-EXCPDCBE
C_DCBE   DCBE  EADSCB=OK                                       JH DEC12
C_DCBEL  EQU   *-C_DCBE                                        JH DEC12
.NOEAV2  ANOP                                                 DRK JAN13
*        AIF   ('&MVSLEV' GE 'MVS710').EAV1                   DRK OCT18
EXCPDCB  DCB   DDNAME=X,MACRF=E,DEVD=DA
LEXCPDCB EQU   *-EXCPDCB
*.EAV1    ANOP                                                DRK OCT18
         SPACE 1
* PDS199I LARGEST FREE AREA ON VOLUME MVSRES IS 1234567 CYLINDERS
LSPACEX  DS    0H                                             DRK NOV18
         USING *,R6                                                   "
         L     R1,DCBDEBAD-IHADCB+INDCB GET DEB ADDRES                "
         L     R0,32(,R1)               UCB ADDRESS                   "
         MVC   PARMLIST(LSPACEL),LSPACEM                              "
         LSPACE UCB=(R0),EXPDATA=WORKTBL,                              X
               MF=(E,PARMLIST)
         LTR   R15,R15                                                "
         BNZR  R2                                                     "
         SPACE 1                                                      "
         MVI   MTLEN,6                   INSERT LENGTH 1              "
         MVI   MTLEN+4,8                 INSERT LENGTH 2              "
         MVC   INSERT#1(6),VOLALLOC      VOLSER                       "
         L     R1,WORKTBL+LSPDLCYL       CYLS IN LARGEST FREE         "
         CVD   R1,DOUBLE                 PACKED                       "
         MVC   INSERT#2(8),LCYLMASK                                   "
         ED    INSERT#2(8),DOUBLE+4      DISPLAY                      "
         M$MSG L199$2                                                 "
         BR    R2                                                     "
         SPACE 1                                                      "
LSPDLCYL EQU   16                                                     "
LCYLMASK DC    XL8'4020202020202120'                                  "
L199$2   DC    C'199'                                                 "
LSPACEM  LSPACE EXPDATA=0,MF=L,PLISTVER=MAX                           "
LSPACEL  EQU   *-LSPACEM                                              "
         DROP  R6                                                     "
