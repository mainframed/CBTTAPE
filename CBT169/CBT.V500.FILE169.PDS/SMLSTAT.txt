 SMLSTAT: PROC OPTIONS(MAIN) REORDER;

   DECLARE

     1 BOWLER(0:#_BOWLERS)   CONTROLLED,
       2 L_NAME              CHAR(72) VAR,
       2 F_NAME              CHAR(72) VAR,
       2 LY_AVG              FIXED BIN(15),
       2 GAME21_AVG          FIXED BIN(15),
       2 PHONE               CHAR(8),
       2 INACTIVE            BIT(1),
       2 WEEK(0:WEEK_#),
         3 PRESENT           BIT(1),
         3 WEEKS             FIXED BIN(15),
         3 GAME(3)           FIXED BIN(15),
         3 PINS              FIXED BIN(15),
         3 GAMES             FIXED BIN(15),
         3 AVG               FIXED BIN(15),
         3 HDCP              FIXED BIN(15),
         3 PTS               FLOAT,
     1 TEMP,
       2 L_NAME              CHAR(72) VAR,
       2 F_NAME              CHAR(72) VAR,
       2 LY_AVG              FIXED BIN(15),
       2 PHONE               CHAR(8),

     1 TEAM(0:#_TEAMS)       CONTROLLED,
       2 NAME                CHAR(72) VAR,
       2 #_MEMBERS           FIXED BIN(15),
       2 MEMBER(6)           FIXED BIN(15),
       2 WEEK(0:WEEK_#),
         3 BOWLER(#_ACTIVE)  FIXED BIN(15),
         3 WON               FLOAT,
         3 LOST              FLOAT,
         3 AVG               FIXED BIN(15),
         3 HDCP              FIXED BIN(15),

     RECAP_STR(0:#_ACTIVE)   CHAR(120) VAR CONTROLLED,
     LANE(WEEK_#,#_TEAMS)    FIXED BIN(15) CONTROLLED,
     DATE(0:WEEK_#+1)        CHAR(9) CONTROLLED,
     NOTE(WEEK_#+1,9)        CHAR(72) VAR CONTROLLED,
     #_NOTES(WEEK_#+1)       FIXED BIN(15) CONTROLLED,
     T_NOTE                  CHAR(72) VAR,

     1 SCORE(0:#_ACTIVE,0:3,2) CONTROLLED,
       2 AVG                 FIXED BIN(15),
       2 HDCP                FIXED BIN(15),
       2 GROSS               FIXED BIN(15),
       2 NET                 FIXED BIN(15),
       2 PTS                 FLOAT,

     1 HI(0:WEEK_#,2)        CONTROLLED,
       2 GA                  FIXED BIN(15),
       2 GH                  FIXED BIN(15),
       2 SA                  FIXED BIN(15),
       2 SH                  FIXED BIN(15),
       2 GAB                 FIXED BIN(15),
       2 GHB                 FIXED BIN(15),
       2 SAB                 FIXED BIN(15),
       2 SHB                 FIXED BIN(15),

     1 POT(WEEK_#+1)         CONTROLLED,
       2 HDCP_LANE           FIXED BIN(15),
       2 FIFTY50_LANE        FIXED BIN(15),
       2 PICKLE_JAR          FIXED BIN(15),
       2 PICKLE_LANE         FIXED BIN(15),
     HDCP_STR                CHAR(72) VAR,
     FIFTY50_STR             CHAR(72) VAR,
     PICKLE_STR              CHAR(72) VAR,
     DIGIT(0:9)              CHAR(1) INIT('0','1','2','3','4',
                                          '5','6','7','8','9'),

     SORT(#_BOWLERS)         FIXED BIN(15) CONTROLLED,
     SUB(0:#_BOWLERS+1)      FIXED BIN(15) CONTROLLED;

   DECLARE

     (T1,T2,B1,B2)           FIXED BIN(15),

     SMLDATA                 FILE,
     SMRECAP                 FILE PRINT,
     SMSTAND                 FILE PRINT,
     SMINDIV                 FILE PRINT,
     SMSUMRY                 FILE PRINT,
     SMTEAMS                 FILE PRINT,
     SMPRIZE                 FILE PRINT,

     YEAR                    CHAR(72) VAR,
     WEEK_#                  FIXED BIN(15),
     PRINT_ALL               BIT(1),
     MID_POINT               FIXED BIN(15),
     #_BOWLERS               FIXED BIN(15),
     #_SUBS                  FIXED BIN(15),
     SUB_#                   FIXED BIN(15),
     TOT_PINS                FIXED BIN(31),

     LEAGUE                  CHAR(72) VAR,
     BIG_LEAGUE              CHAR(72) VAR,
     #_TEAMS                 FIXED BIN(15),
     MAX_MEMBERS             FIXED BIN(15),
     #_ACTIVE                FIXED BIN(15),
     PTS_I_G                 FIXED BIN(15),
     PTS_I_S                 FIXED BIN(15),
     PTS_T_G                 FIXED BIN(15),
     PTS_T_S                 FIXED BIN(15),
     HDCP_BASIS              FIXED BIN(15),
     HDCP_PCT                FIXED BIN(15),
     BLIND                   FIXED BIN(15),
     EST_AVG                 FIXED BIN(15),

     (I0,I1,I2,I3,I4,I5,I6)  FIXED BIN(15),

     (ABS,FLOAT,FLOOR,LENGTH,MAX,MOD,REPEAT,ROUND,SUBSTR)
                             BUILTIN,

     (SYSIN,SYSPRINT)        FILE;

   LEAGUE      = 'SEYMOUR MEN''S LEAGUE';
   BIG_LEAGUE  = 'SEYMOUR  MEN S  LEAGUE';
   #_TEAMS     =  10;
   MAX_MEMBERS =   6;
   #_ACTIVE    =   5;
   PTS_I_G     =   1;
   PTS_I_S     =   0;
   PTS_T_G     =   4;
   PTS_T_S     =   0;
   HDCP_BASIS  = 200;
   HDCP_PCT    =  80;
   BLIND       = 150;
   EST_AVG     =   3;
   MID_POINT   =  17;
   PRINT_ALL   = '0'B;

   OPEN FILE(SMRECAP) OUTPUT PAGESIZE(999);
   OPEN FILE(SMSTAND) OUTPUT PAGESIZE(999);
   OPEN FILE(SMINDIV) OUTPUT PAGESIZE(999);
   OPEN FILE(SMSUMRY) OUTPUT PAGESIZE(999);
   OPEN FILE(SMTEAMS) OUTPUT PAGESIZE(999);
   OPEN FILE(SMPRIZE) OUTPUT PAGESIZE(999);

  /*******************************************************************
   *  Read in league prolog information.  These numbers define the   *
   *  league and the size of the controlled structures needed.       *
   *******************************************************************/
   OPEN FILE(SMLDATA) INPUT;
   GET FILE(SMLDATA)
        EDIT(YEAR,#_BOWLERS,WEEK_#)
            (COL(1),A(7),X(1),F(2,0),X(1),F(3,0));
   IF WEEK_# < 0 THEN DO;
     PRINT_ALL = '1'B;
     WEEK_# = ABS(WEEK_#);
   END;

  /*******************************************************************
   *  Allocate the controlled structures using the nubmers read in   *
   *  above for sizes.                                               *
   *******************************************************************/
   ALLOCATE BOWLER;
   ALLOCATE TEAM;
   ALLOCATE RECAP_STR;
   ALLOCATE LANE;
   ALLOCATE DATE;
   ALLOCATE #_NOTES;
   ALLOCATE NOTE;
   ALLOCATE POT;
   ALLOCATE SCORE;
   ALLOCATE HI;
   ALLOCATE SORT;
   ALLOCATE SUB;

  /*******************************************************************
   *  Initialize zeros and blind scores.                             *
   *******************************************************************/
   DATE(*) = '         ';
   BOWLER.WEEK.GAME(*,*,*) = 0;
   BOWLER.WEEK.WEEKS(*,*) = 0;
   BOWLER.WEEK.GAMES(*,*) = 0;
   BOWLER.WEEK.PINS(*,*) = 0;
   BOWLER.WEEK.AVG(*,*) = 0;
   BOWLER.WEEK.HDCP(*,*) = 0;
   BOWLER.WEEK.PTS(*,*) = 0.0;
   BOWLER.L_NAME(0) = 'BLIND';
   BOWLER.F_NAME(0) = '*';
   BOWLER.LY_AVG(0) = BLIND;
   BOWLER.WEEK.GAME(0,*,*) = BLIND - 10;
   BOWLER.WEEK.AVG(0,*) = BLIND;
   BOWLER.WEEK.HDCP(0,*) = HDCP(BLIND);
   TEAM.WEEK.WON(*,*) = 0.0;
   TEAM.WEEK.LOST(*,*) = 0.0;
   HI.GA(*,*)  = 0;
   HI.GAB(*,*) = 0;
   HI.SA(*,*)  = 0;
   HI.SAB(*,*) = 0;
   HI.GH(*,*)  = 0;
   HI.GHB(*,*) = 0;
   HI.SH(*,*)  = 0;
   HI.SHB(*,*) = 0;

  /*******************************************************************
   *  Read in the name of each bowler in the league.                 *
   *******************************************************************/
   DO I0 = 1 TO #_BOWLERS;
     GET FILE(SMLDATA)
           EDIT(TEMP.L_NAME,TEMP.F_NAME,TEMP.LY_AVG,TEMP.PHONE,I3)
               (SKIP,COL(1),A(20),X(1),A(15),X(1),F(3,0),X(4),A(8),
                X(3),F(3,0));
     IF I3 < 0 THEN DO;
       I3 = ABS(I3);
       BOWLER.INACTIVE(I3) = '1'B;
     END;
     ELSE
       BOWLER.INACTIVE(I3) = '0'B;
     BOWLER.L_NAME(I3)     = TEMP.L_NAME;
     BOWLER.F_NAME(I3)     = TEMP.F_NAME;
     BOWLER.GAME21_AVG(I3) = 0;
     BOWLER.LY_AVG(I3)     = TEMP.LY_AVG;
     BOWLER.PHONE(I3)      = TEMP.PHONE;
   END;

  /*******************************************************************
   *  Read in the names and rosters of each team in the league.      *
   *******************************************************************/
   DO I0 = 1 TO #_TEAMS;
     GET FILE(SMLDATA)
           EDIT(TEAM.NAME(I0),TEAM.#_MEMBERS(I0))
               (SKIP,COL(1),A(15),COL(22),F(1,0));
     GET FILE(SMLDATA)
           EDIT((TEAM.MEMBER(I0,I1) DO I1=1 TO TEAM.#_MEMBERS(I0)))
               (COL(25),(10)(F(2,0),X(1)));
   END;

  /*******************************************************************
   *  Read in the lane assignments for each week.                    *
   *******************************************************************/
   IF WEEK_# > 0 THEN DO;
     DO I0 = 1 TO WEEK_#;
       GET FILE(SMLDATA)
           EDIT(DATE(I0),POT.PICKLE_JAR(I0),POT.PICKLE_LANE(I0),
                HDCP_LANE(I0),FIFTY50_LANE(I0),#_NOTES(I0))
               (SKIP,COL(1),A(9),X(1),F(3,0),X(1),F(2,0),
                X(1),F(2,0),X(1),F(2,0),X(1),F(1,0));
       DO I1 = 1 TO #_NOTES(I0);
         GET FILE(SMLDATA)
             EDIT(NOTE(I0,I1)) (SKIP,COL(1),A(72));
       END;
       GET FILE(SMLDATA) EDIT(#_SUBS) (SKIP,COL(1),F(3,0));
       DO I1 = 1 TO #_SUBS;
         GET FILE(SMLDATA) EDIT(SUB_#) (X(1),F(2,0));
         GET FILE(SMLDATA)
             EDIT(BOWLER.WEEK.GAME(SUB_#,I0,*))
                 ((3)(X(1),F(3,0)));
       END;
       DO I1 = 1 TO #_TEAMS;
         GET FILE(SMLDATA)
             EDIT(LANE(I0,I1)) (SKIP,COL(1),F(3,0));
         DO I2 = 1 TO #_ACTIVE;
           GET FILE(SMLDATA)
                    EDIT(TEAM.WEEK.BOWLER(ABS(LANE(I0,I1)),I0,I2))
                (X(1),F(2,0));
           GET FILE(SMLDATA)
                    EDIT(BOWLER.WEEK.GAME(
                        TEAM.WEEK.BOWLER(ABS(LANE(I0,I1)),I0,I2),I0,*))
                ((3)(X(1),F(3,0)));
         END;
       END;
     END;
   END;
   GET FILE(SMLDATA)
       EDIT(DATE(WEEK_#+1),POT.PICKLE_JAR(WEEK_#+1),
            POT.PICKLE_LANE(WEEK_#+1),POT.HDCP_LANE(WEEK_#+1),
            POT.FIFTY50_LANE(WEEK_#+1),#_NOTES(WEEK_#+1))
           (SKIP,COL(1),A(9),X(1),F(3,0),X(1),F(2,0),X(1),F(2,0),
            X(1),F(2,0),X(1),F(1,0));
   DO I1 = 1 TO #_NOTES(WEEK_#+1);
     GET FILE(SMLDATA)
         EDIT(NOTE(WEEK_#+1,I1)) (SKIP,COL(1),A(72));
   END;

   CLOSE FILE(SMLDATA);

  /*******************************************************************
   *  For each team, loop through the active roster, marking each    *
   *  bowler as a non-sub.                                           *
   *******************************************************************/
   SUB(*) = 1;
   DO I1 = 1 TO #_TEAMS;
     DO I2 = 1 TO TEAM.#_MEMBERS(I1);
       SUB(TEAM.MEMBER(I1,I2)) = 0;
     END;
   END;

  /*******************************************************************
   *  Calculate running totals of games and pins for each bowler.    *
   *  Use these numbers to calculate running average and handicap.   *
   *******************************************************************/
   DO I0 = 1 TO #_BOWLERS;
     DO I1 = 1 TO WEEK_#;
      /***************************************************************
       *  Add up the series and number of games bowled for each      *
       *  bowler for each week.                                      *
       ***************************************************************/
       BOWLER.WEEK.PINS(I0,I1) = 0;
       BOWLER.WEEK.PRESENT(I0,I1) = '0'B;
       DO I2 = 1 TO 3;
         IF BOWLER.WEEK.GAME(I0,I1,I2) ¬= 0 THEN DO;
           BOWLER.WEEK.PRESENT(I0,I1) = '1'B;
           BOWLER.WEEK.PINS(I0,I1) = BOWLER.WEEK.PINS(I0,I1) +
                                            BOWLER.WEEK.GAME(I0,I1,I2);
           BOWLER.WEEK.GAMES(I0,I1) = BOWLER.WEEK.GAMES(I0,I1) + 1;
         END;
       END;
       IF BOWLER.WEEK.PRESENT(I0,I1) THEN
         BOWLER.WEEK.WEEKS(I0,I1) = BOWLER.WEEK.WEEKS(I0,I1) + 1;
      /***************************************************************
       *  Add previous week's pins and games to get a running        *
       *  total for each bowler for each week.                       *
       ***************************************************************/
       IF I1 > 1 THEN DO;
         BOWLER.WEEK.WEEKS(I0,I1) = BOWLER.WEEK.WEEKS(I0,I1) +
                                            BOWLER.WEEK.WEEKS(I0,I1-1);
         BOWLER.WEEK.GAMES(I0,I1) = BOWLER.WEEK.GAMES(I0,I1) +
                                            BOWLER.WEEK.GAMES(I0,I1-1);
         BOWLER.WEEK.PINS(I0,I1) = BOWLER.WEEK.PINS(I0,I1) +
                                             BOWLER.WEEK.PINS(I0,I1-1);
       END;
      /***************************************************************
       *  If they haven't bowled, their average is 0.                *
       ***************************************************************/
       IF BOWLER.WEEK.GAMES(I0,I1) = 0 THEN DO;
         IF BOWLER.LY_AVG(I0) = 0 THEN DO;
           BOWLER.WEEK.AVG(I0,I1) = 0;
           BOWLER.WEEK.HDCP(I0,I1) = 0;
         END;
         ELSE DO;
           BOWLER.WEEK.AVG(I0,I1) = BOWLER.LY_AVG(I0);
           BOWLER.WEEK.HDCP(I0,I1) = HDCP(BOWLER.LY_AVG(I0));
         END;
       END;
      /***************************************************************
       *  Calculate average and handicap.                            *
       ***************************************************************/
       ELSE DO;
         IF BOWLER.WEEK.WEEKS(I0,I1) < EST_AVG &
                                        BOWLER.LY_AVG(I0) ¬= 0 THEN DO;
           BOWLER.WEEK.AVG(I0,I1) = BOWLER.LY_AVG(I0);
           BOWLER.WEEK.HDCP(I0,I1) = HDCP(BOWLER.LY_AVG(I0));
         END;
         ELSE DO;
           BOWLER.WEEK.AVG(I0,I1) = BOWLER.WEEK.PINS(I0,I1) /
                                              BOWLER.WEEK.GAMES(I0,I1);
           BOWLER.WEEK.HDCP(I0,I1) = HDCP(BOWLER.WEEK.AVG(I0,I1));
         END;
         IF BOWLER.WEEK.GAMES(I0,I1) > 20 &
              BOWLER.WEEK.GAMES(I0,I1) < 24 THEN
           BOWLER.GAME21_AVG(I0) = BOWLER.WEEK.AVG(I0,I1);
       END;

      /***************************************************************
       *  Only check for high scores if this is not a sub.           *
       ***************************************************************/
       IF SUB(I0) = 0 THEN DO;
        /*************************************************************
         *  Check for high actual game.                              *
         *************************************************************/
         DO I2 = 1 TO 3;
           IF BOWLER.WEEK.GAME(I0,I1,I2) > HI.GA(I1,1) THEN DO;
             IF HI.GAB(I1,1) ¬= I0 THEN DO;
               HI.GA(I1,2)  = HI.GA(I1,1);
               HI.GAB(I1,2) = HI.GAB(I1,1);
             END;
             HI.GA(I1,1)  = BOWLER.WEEK.GAME(I0,I1,I2);
             HI.GAB(I1,1) = I0;
           END;
           ELSE DO;
             IF BOWLER.WEEK.GAME(I0,I1,I2) > HI.GA(I1,2) &
                                            HI.GAB(I1,1) ¬= I0 THEN DO;
               HI.GA(I1,2)  = BOWLER.WEEK.GAME(I0,I1,I2);
               HI.GAB(I1,2) = I0;
             END;
           END;
        /*************************************************************
         *  Check for high handicap game using the scrores from      *
         *  this week with the handicap from last week.              *
         *************************************************************/
           IF BOWLER.WEEK.GAME(I0,I1,I2) +
                     BOWLER.WEEK.HDCP(I0,I1-1) > HI.GH(I1,1) &
                       BOWLER.WEEK.WEEKS(I0,I1-1) >= EST_AVG THEN DO;
             IF HI.GHB(I1,1) ¬= I0 THEN DO;
               HI.GH(I1,2)  = HI.GH(I1,1);
               HI.GHB(I1,2) = HI.GHB(I1,1);
             END;
             HI.GH(I1,1)  = BOWLER.WEEK.GAME(I0,I1,I2) +
                                             BOWLER.WEEK.HDCP(I0,I1-1);
             HI.GHB(I1,1) = I0;
           END;
           ELSE DO;
             IF BOWLER.WEEK.GAME(I0,I1,I2) +
                BOWLER.WEEK.HDCP(I0,I1-1) > HI.GH(I1,2) &
                          BOWLER.WEEK.WEEKS(I0,I1-1) >= EST_AVG &
                   HI.GHB(I1,1) ¬= I0 THEN DO;
               HI.GH(I1,2)  = BOWLER.WEEK.GAME(I0,I1,I2) +
                                             BOWLER.WEEK.HDCP(I0,I1-1);
               HI.GHB(I1,2) = I0;
             END;
           END;
         END;
        /*************************************************************
         *  Check for high actual series.                            *
         *************************************************************/
         IF BOWLER.WEEK.GAME(I0,I1,1) +
                  BOWLER.WEEK.GAME(I0,I1,2) +
                    BOWLER.WEEK.GAME(I0,I1,3) > HI.SA(I1,1) THEN DO;
           IF HI.SAB(I1,1) ¬= I0 THEN DO;
             HI.SA(I1,2)  = HI.SA(I1,1);
             HI.SAB(I1,2) = HI.SAB(I1,1);
           END;
           HI.SA(I1,1)  = BOWLER.WEEK.GAME(I0,I1,1) +
                                 BOWLER.WEEK.GAME(I0,I1,2) +
                                   BOWLER.WEEK.GAME(I0,I1,3);
           HI.SAB(I1,1) = I0;
         END;
         ELSE DO;
           IF BOWLER.WEEK.GAME(I0,I1,1) +
                     BOWLER.WEEK.GAME(I0,I1,2) +
                       BOWLER.WEEK.GAME(I0,I1,3) > HI.SA(I1,2) &
                         HI.SAB(I1,1) ¬= I0 THEN DO;
             HI.SA(I1,2)  = BOWLER.WEEK.GAME(I0,I1,1) +
                                    BOWLER.WEEK.GAME(I0,I1,2) +
                                      BOWLER.WEEK.GAME(I0,I1,3);
             HI.SAB(I1,2) = I0;
           END;
         END;
        /*************************************************************
         *  Check for high handicap series using the scores from     *
         *  this week with the handicap from last week.              *
         *************************************************************/
         IF BOWLER.WEEK.GAME(I0,I1,1) +
                  BOWLER.WEEK.GAME(I0,I1,2) +
                    BOWLER.WEEK.GAME(I0,I1,3) +
               3*BOWLER.WEEK.HDCP(I0,I1-1) > HI.SH(I1,1) &
                BOWLER.WEEK.WEEKS(I0,I1-1) >= EST_AVG THEN DO;
           IF HI.SHB(I1,1) ¬= I0 THEN DO;
             HI.SH(I1,2)  = HI.SH(I1,1);
             HI.SHB(I1,2) = HI.SHB(I1,1);
           END;
           HI.SH(I1,1)  = BOWLER.WEEK.GAME(I0,I1,1) +
                                 BOWLER.WEEK.GAME(I0,I1,2) +
                                   BOWLER.WEEK.GAME(I0,I1,3) +
                                     3*BOWLER.WEEK.HDCP(I0,I1-1);
           HI.SHB(I1,1) = I0;
         END;
         ELSE DO;
           IF BOWLER.WEEK.GAME(I0,I1,1) +
                     BOWLER.WEEK.GAME(I0,I1,2) +
                       BOWLER.WEEK.GAME(I0,I1,3) +
                         3*BOWLER.WEEK.HDCP(I0,I1-1) > HI.SH(I1,2) &
                           BOWLER.WEEK.WEEKS(I0,I1-1) >= EST_AVG &
                             HI.SHB(I1,1) ¬= I0 THEN DO;
             HI.SH(I1,2)  = BOWLER.WEEK.GAME(I0,I1,1) +
                                    BOWLER.WEEK.GAME(I0,I1,2) +
                                      BOWLER.WEEK.GAME(I0,I1,3) +
                                        3*BOWLER.WEEK.HDCP(I0,I1-1);
             HI.SHB(I1,2) = I0;
           END;
         END;
       END;
     END;
   END;

  /*******************************************************************
   *  Since our bowler/week loops were nested so as to find the      *
   *  high games/series for each week, we have to go back and        *
   *  carry forward the highs overall.                               *
   *******************************************************************/
   DO I0 = 2 TO WEEK_#;
     IF HI.GA(I0,1) > HI.GA(I0-1,1) THEN DO;
       IF HI.GA(I0-1,1) >= HI.GA(I0,2) & HI.GAB(I0-1,1) ¬= HI.GAB(I0,1)
                                                               THEN DO;
         HI.GA(I0,2)  = HI.GA(I0-1,1);
         HI.GAB(I0,2) = HI.GAB(I0-1,1);
       END;
       ELSE DO;
         IF HI.GA(I0-1,2) >= HI.GA(I0,2) &
                                HI.GAB(I0-1,2) ¬= HI.GAB(I0,1) THEN DO;
           HI.GA(I0,2)  = HI.GA(I0-1,2);
           HI.GAB(I0,2) = HI.GAB(I0-1,2);
         END;
       END;
     END;
     ELSE DO;
       IF HI.GA(I0,1) > HI.GA(I0-1,2) &
           HI.GAB(I0,1) ¬= HI.GAB(I0-1,1) THEN DO;
         HI.GA(I0,2)  = HI.GA(I0,1);
         HI.GAB(I0,2) = HI.GAB(I0,1);
       END;
       ELSE DO;
         HI.GA(I0,2)  = HI.GA(I0-1,2);
         HI.GAB(I0,2) = HI.GAB(I0-1,2);
       END;
       HI.GA(I0,1)  = HI.GA(I0-1,1);
       HI.GAB(I0,1) = HI.GAB(I0-1,1);
     END;
     IF HI.SA(I0,1) > HI.SA(I0-1,1) THEN DO;
       IF HI.SA(I0-1,1) >= HI.SA(I0,2) &
                                HI.SAB(I0-1,1) ¬= HI.SAB(I0,1) THEN DO;
         HI.SA(I0,2)  = HI.SA(I0-1,1);
         HI.SAB(I0,2) = HI.SAB(I0-1,1);
       END;
       ELSE DO;
         IF HI.SA(I0-1,2) >= HI.SA(I0,2) &
                                HI.SAB(I0-1,2) ¬= HI.SAB(I0,1) THEN DO;
           HI.SA(I0,2)  = HI.SA(I0-1,2);
           HI.SAB(I0,2) = HI.SAB(I0-1,2);
         END;
       END;
     END;
     ELSE DO;
       IF HI.SA(I0,1) > HI.SA(I0-1,2) &
           HI.SAB(I0,1) ¬= HI.SAB(I0-1,1) THEN DO;
         HI.SA(I0,2)  = HI.SA(I0,1);
         HI.SAB(I0,2) = HI.SAB(I0,1);
       END;
       ELSE DO;
         HI.SA(I0,2)  = HI.SA(I0-1,2);
         HI.SAB(I0,2) = HI.SAB(I0-1,2);
       END;
       HI.SA(I0,1)  = HI.SA(I0-1,1);
       HI.SAB(I0,1) = HI.SAB(I0-1,1);
     END;
     IF HI.GH(I0,1) > HI.GH(I0-1,1) THEN DO;
       IF HI.GH(I0-1,1) >= HI.GH(I0,2) &
                                HI.GHB(I0-1,1) ¬= HI.GHB(I0,1) THEN DO;
         HI.GH(I0,2)  = HI.GH(I0-1,1);
         HI.GHB(I0,2) = HI.GHB(I0-1,1);
       END;
       ELSE DO;
         IF HI.GH(I0-1,2) >= HI.GH(I0,2) &
                                HI.GHB(I0-1,2) ¬= HI.GHB(I0,1) THEN DO;
           HI.GH(I0,2)  = HI.GH(I0-1,2);
           HI.GHB(I0,2) = HI.GHB(I0-1,2);
         END;
       END;
     END;
     ELSE DO;
       IF HI.GH(I0,1) > HI.GH(I0-1,2) &
           HI.GHB(I0,1) ¬= HI.GHB(I0-1,1) THEN DO;
         HI.GH(I0,2)  = HI.GH(I0,1);
         HI.GHB(I0,2) = HI.GHB(I0,1);
       END;
       ELSE DO;
         HI.GH(I0,2)  = HI.GH(I0-1,2);
         HI.GHB(I0,2) = HI.GHB(I0-1,2);
       END;
       HI.GH(I0,1)  = HI.GH(I0-1,1);
       HI.GHB(I0,1) = HI.GHB(I0-1,1);
     END;
     IF HI.SH(I0,1) > HI.SH(I0-1,1) THEN DO;
       IF HI.SH(I0-1,1) >= HI.SH(I0,2) &
                                HI.SHB(I0-1,1) ¬= HI.SHB(I0,1) THEN DO;
         HI.SH(I0,2)  = HI.SH(I0-1,1);
         HI.SHB(I0,2) = HI.SHB(I0-1,1);
       END;
       ELSE DO;
         IF HI.SH(I0-1,2) >= HI.SH(I0,2) &
                                HI.SHB(I0-1,2) ¬= HI.SHB(I0,1) THEN DO;
           HI.SH(I0,2)  = HI.SH(I0-1,2);
           HI.SHB(I0,2) = HI.SHB(I0-1,2);
         END;
       END;
     END;
     ELSE DO;
       IF HI.SH(I0,1) > HI.SH(I0-1,2) &
           HI.SHB(I0,1) ¬= HI.SHB(I0-1,1) THEN DO;
         HI.SH(I0,2)  = HI.SH(I0,1);
         HI.SHB(I0,2) = HI.SHB(I0,1);
       END;
       ELSE DO;
         HI.SH(I0,2)  = HI.SH(I0-1,2);
         HI.SHB(I0,2) = HI.SHB(I0-1,2);
       END;
       HI.SH(I0,1)  = HI.SH(I0-1,1);
       HI.SHB(I0,1) = HI.SHB(I0-1,1);
     END;
   END;

   DO I0 = 1 TO WEEK_#;
     DO I1 = 1 TO #_TEAMS - 1 BY 2;
      /***************************************************************
       *  Zero out the scoring arrays and get the teams that are     *
       *  facing each other.                                         *
       ***************************************************************/
       SCORE.AVG(*,*,*)   = 0;
       SCORE.HDCP(*,*,*)  = 0;
       SCORE.GROSS(*,*,*) = 0;
       SCORE.NET(*,*,*)   = 0;
       SCORE.PTS(*,*,*)   = 0;
       RECAP_STR(*)       = REPEAT(' ',119);
       T1 = LANE(I0,I1);
       T2 = LANE(I0,I1+1);
       DO I2 = 1 TO #_ACTIVE;
        /*************************************************************
         *  Get each pair of bowlers facing each other.              *
         *************************************************************/
         B1 = TEAM.WEEK.BOWLER(ABS(T1),I0,I2);
         B2 = TEAM.WEEK.BOWLER(ABS(T2),I0,I2);
         DO I3 = 1 TO 3;
          /***********************************************************
           *  If bowler 1 has not yet bowled enough games to         *
           *  establish an average and he has an average from last   *
           *  year, use last year's average.  Otherwise, if he has   *
           *  an average from last week, use that.  If neither case  *
           *  is true and he has an average for this week, then use  *
           *  that.  If all else fails, use the blind average.       *
           *  The calculate his handicap.                            *
           ***********************************************************/
           IF BOWLER.WEEK.WEEKS(B1,I0-1) < EST_AVG &
                                            BOWLER.LY_AVG(B1) ¬= 0 THEN
             SCORE.AVG(I2,I3,1) = BOWLER.LY_AVG(B1);
           ELSE DO;
             IF BOWLER.WEEK.AVG(B1,I0-1) ¬= 0 THEN
               SCORE.AVG(I2,I3,1) = BOWLER.WEEK.AVG(B1,I0-1);
             ELSE DO;
               IF BOWLER.WEEK.AVG(B1,I0) ¬= 0 THEN
                 SCORE.AVG(I2,I3,1) = BOWLER.WEEK.AVG(B1,I0);
               ELSE
                 SCORE.AVG(I2,I3,1) = BLIND;
             END;
           END;
           SCORE.HDCP(I2,I3,1) = HDCP(SCORE.AVG(I2,I3,1));

          /***********************************************************
           *  If bowler 2 has not yet bowled enough games to         *
           *  establish an average and he has an average from last   *
           *  year, use last year's average.  Otherwise, if he has   *
           *  an average from last week, use that.  If neither case  *
           *  is true and he has an average for this week, then use  *
           *  that.  If all else fails, use the blind average.       *
           *  The calculate his handicap.                            *
           ***********************************************************/
           IF BOWLER.WEEK.WEEKS(B2,I0-1) < EST_AVG &
                                            BOWLER.LY_AVG(B2) ¬= 0 THEN
             SCORE.AVG(I2,I3,2) = BOWLER.LY_AVG(B2);
           ELSE DO;
             IF BOWLER.WEEK.AVG(B2,I0-1) ¬= 0 THEN
               SCORE.AVG(I2,I3,2) = BOWLER.WEEK.AVG(B2,I0-1);
             ELSE DO;
               IF BOWLER.WEEK.AVG(B2,I0) ¬= 0 THEN
                 SCORE.AVG(I2,I3,2) = BOWLER.WEEK.AVG(B2,I0);
               ELSE
                 SCORE.AVG(I2,I3,2) = BLIND;
             END;
           END;
           SCORE.HDCP(I2,I3,2) = HDCP(SCORE.AVG(I2,I3,2));

          /***********************************************************
           *  If bowler 1 actually bowled a game, use that as his    *
           *  gross score.  If this isn't a blind score, count him   *
           *  as present.  If he didn't bowl and hasn't established  *
           *  an average yet but has an average from last year, use  *
           *  last year's average less 10.  Otherwise, if he has an  *
           *  average from last week, use last week's average less   *
           *  10.  If none of that applies, use a blind score.       *
           ***********************************************************/
           IF BOWLER.WEEK.GAME(B1,I0,I3) ¬= 0 THEN
             SCORE.GROSS(I2,I3,1) = BOWLER.WEEK.GAME(B1,I0,I3);
           ELSE DO;
             IF BOWLER.WEEK.WEEKS(B1,I0-1) < EST_AVG &
                                            BOWLER.LY_AVG(B1) ¬= 0 THEN
               SCORE.GROSS(I2,I3,1) = BOWLER.LY_AVG(B1) - 10;
             ELSE DO;
               IF BOWLER.WEEK.AVG(B1,I0-1) ¬= 0 THEN
                         SCORE.GROSS(I2,I3,1) =
                                         BOWLER.WEEK.AVG(B1,I0-1) - 10;
               ELSE DO;
                         IF BOWLER.WEEK.AVG(B1,I0) ¬= 0 THEN
                   SCORE.GROSS(I2,I3,1) = BOWLER.WEEK.AVG(B1,I0) - 10;
                         ELSE
                   SCORE.GROSS(I2,I3,1) = BLIND - 10;
               END;
             END;
           END;

          /***********************************************************
           *  If bowler 2 actually bowled a game, use that as his    *
           *  gross score.  If this isn't a blind score, count him   *
           *  as present.  If he didn't bowl and hasn't established  *
           *  an average yet but has an average from last year, use  *
           *  last year's average less 10.  Otherwise, if he has an  *
           *  average from last week, use last week's average less   *
           *  10.  If none of that applies, use a blind score.       *
           ***********************************************************/
           IF BOWLER.WEEK.GAME(B2,I0,I3) ¬= 0 THEN
             SCORE.GROSS(I2,I3,2) = BOWLER.WEEK.GAME(B2,I0,I3);
           ELSE DO;
             IF BOWLER.WEEK.WEEKS(B2,I0-1) < EST_AVG &
                                            BOWLER.LY_AVG(B2) ¬= 0 THEN
               SCORE.GROSS(I2,I3,2) = BOWLER.LY_AVG(B2) - 10;
             ELSE DO;
               IF BOWLER.WEEK.AVG(B2,I0-1) ¬= 0 THEN
                 SCORE.GROSS(I2,I3,2) = BOWLER.WEEK.AVG(B2,I0-1) - 10;
               ELSE DO;
                 IF BOWLER.WEEK.AVG(B2,I0) ¬= 0 THEN
                   SCORE.GROSS(I2,I3,2) = BOWLER.WEEK.AVG(B2,I0) - 10;
                 ELSE
                   SCORE.GROSS(I2,I3,2) = BLIND - 10;
               END;
             END;
           END;

          /***********************************************************
           *  The net score for each bowler is the sum of his gross  *
           *  score and handicap (as determined above).              *
           ***********************************************************/
           SCORE.NET(I2,I3,1) = SCORE.GROSS(I2,I3,1) +
                                                   SCORE.HDCP(I2,I3,1);
           SCORE.NET(I2,I3,2) = SCORE.GROSS(I2,I3,2) +
                                                   SCORE.HDCP(I2,I3,2);
          /***********************************************************
           *  Add up the total scores and handicaps for each         *
           *  individual series.                                     *
           ***********************************************************/
           SCORE.AVG(I2,0,1) = SCORE.AVG(I2,0,1) + SCORE.AVG(I2,I3,1);
           SCORE.AVG(I2,0,2) = SCORE.AVG(I2,0,2) + SCORE.AVG(I2,I3,2);
           SCORE.AVG(0,I3,1) = SCORE.AVG(0,I3,1) + SCORE.AVG(I2,I3,1);
           SCORE.AVG(0,I3,2) = SCORE.AVG(0,I3,2) + SCORE.AVG(I2,I3,2);
           SCORE.HDCP(I2,0,1) = SCORE.HDCP(I2,0,1)+SCORE.HDCP(I2,I3,1);
           SCORE.HDCP(I2,0,2) = SCORE.HDCP(I2,0,2)+SCORE.HDCP(I2,I3,2);
           SCORE.HDCP(0,I3,1) = SCORE.HDCP(0,I3,1)+SCORE.HDCP(I2,I3,1);
           SCORE.HDCP(0,I3,2) = SCORE.HDCP(0,I3,2)+SCORE.HDCP(I2,I3,2);
           SCORE.GROSS(I2,0,1) = SCORE.GROSS(I2,0,1) +
                                                  SCORE.GROSS(I2,I3,1);
           SCORE.GROSS(I2,0,2) = SCORE.GROSS(I2,0,2) +
                                                  SCORE.GROSS(I2,I3,2);
           SCORE.GROSS(0,I3,1) = SCORE.GROSS(0,I3,1) +
                                                  SCORE.GROSS(I2,I3,1);
           SCORE.GROSS(0,I3,2) = SCORE.GROSS(0,I3,2) +
                                                  SCORE.GROSS(I2,I3,2);
           SCORE.NET(I2,0,1) = SCORE.NET(I2,0,1) + SCORE.NET(I2,I3,1);
           SCORE.NET(I2,0,2) = SCORE.NET(I2,0,2) + SCORE.NET(I2,I3,2);
           SCORE.NET(0,I3,1) = SCORE.NET(0,I3,1) + SCORE.NET(I2,I3,1);
           SCORE.NET(0,I3,2) = SCORE.NET(0,I3,2) + SCORE.NET(I2,I3,2);
         END;

        /*********************************************************
         *  Add up the total scores and handicaps for the team   *
         *  series.                                              *
         *********************************************************/
         SCORE.AVG(0,0,1) = SCORE.AVG(0,0,1) + SCORE.AVG(I2,0,1);
         SCORE.AVG(0,0,2) = SCORE.AVG(0,0,2) + SCORE.AVG(I2,0,2);
         SCORE.HDCP(0,0,1) = SCORE.HDCP(0,0,1) +
                                                   SCORE.HDCP(I2,0,1);
         SCORE.HDCP(0,0,2) = SCORE.HDCP(0,0,2) +
                                                   SCORE.HDCP(I2,0,2);
         SCORE.GROSS(0,0,1) = SCORE.GROSS(0,0,1) + SCORE.GROSS(I2,0,1);
         SCORE.GROSS(0,0,2) = SCORE.GROSS(0,0,2) + SCORE.GROSS(I2,0,2);
         SCORE.NET(0,0,1) = SCORE.NET(0,0,1) + SCORE.NET(I2,0,1);
         SCORE.NET(0,0,2) = SCORE.NET(0,0,2) + SCORE.NET(I2,0,2);
       END;

       DO I2 = 1 TO #_ACTIVE;
        /*************************************************************
         *  Get each pair of bowlers facing each other.              *
         *************************************************************/
         B1 = TEAM.WEEK.BOWLER(ABS(T1),I0,I2);
         B2 = TEAM.WEEK.BOWLER(ABS(T2),I0,I2);
         DO I3 = 1 TO 3;
          /***********************************************************
           *  If bowler 1's net score is higher than that of bowler  *
           *  2 and at least one of them was present, give team 1    *
           *  points for an individual game.                         *
           ***********************************************************/
           IF SCORE.NET(I2,I3,1) > SCORE.NET(I2,I3,2) &
                             (BOWLER.WEEK.GAME(B1,I0,I3) ¬= 0 |
                              BOWLER.WEEK.GAME(B2,I0,I3) ¬= 0) THEN DO;
             IF PTS_I_G ¬= 0 THEN
               SUBSTR(RECAP_STR(I2),24+5*I3,3) = '___';
             SCORE.PTS(I2,I3,1) = FLOAT(PTS_I_G);
             SCORE.PTS(0,I3,1) = SCORE.PTS(0,I3,1) + FLOAT(PTS_I_G);
             SCORE.PTS(I2,0,1) = SCORE.PTS(I2,0,1) + FLOAT(PTS_I_G);
             SCORE.PTS(0,0,1) = SCORE.PTS(0,0,1) + FLOAT(PTS_I_G);

            /*********************************************************
             *  If bowler 1 was present give him credit for beating  *
             *  his opponent.                                        *
             *********************************************************/
             IF B1 ¬= 0 & BOWLER.WEEK.GAME(B1,I0,I3) ¬= 0 THEN
               BOWLER.WEEK.PTS(B1,I0) =
                     BOWLER.WEEK.PTS(B1,I0) + FLOAT(PTS_I_G);
           END;

          /***********************************************************
           *  If bowler 1 tied bowler 2 and at least one of then was *
           *  present, split the points for the individual game.     *
           ***********************************************************/
           IF SCORE.NET(I2,I3,1) = SCORE.NET(I2,I3,2) &
                             (BOWLER.WEEK.GAME(B1,I0,I3) ¬= 0 |
                              BOWLER.WEEK.GAME(B2,I0,I3) ¬= 0) THEN DO;
             IF PTS_I_G ¬= 0 THEN DO;
               SUBSTR(RECAP_STR(I2),24+5*I3,3) = '  _';
               SUBSTR(RECAP_STR(I2),84+5*I3,3) = '  _';
             END;
             SCORE.PTS(I2,I3,1) = FLOAT(PTS_I_G) / 2.0;
             SCORE.PTS(0,I3,1) = SCORE.PTS(0,I3,1) +
                                                  FLOAT(PTS_I_G) / 2.0;
             SCORE.PTS(I2,0,1) = SCORE.PTS(I2,0,1) +
                                                  FLOAT(PTS_I_G) / 2.0;
             SCORE.PTS(0,0,1) = SCORE.PTS(0,0,1) +
                                                  FLOAT(PTS_I_G) / 2.0;
             SCORE.PTS(I2,I3,2) = FLOAT(PTS_I_G) / 2.0;
             SCORE.PTS(0,I3,2) = SCORE.PTS(0,I3,2) +
                                                  FLOAT(PTS_I_G) / 2.0;
             SCORE.PTS(I2,0,2) = SCORE.PTS(I2,0,2) +
                                                  FLOAT(PTS_I_G) / 2.0;
             SCORE.PTS(0,0,2) = SCORE.PTS(0,0,2) +
                                                  FLOAT(PTS_I_G) / 2.0;

            /*********************************************************
             *  If bowler 1 was present, give him credit for split-  *
             *  ting with his opponent.                              *
             *********************************************************/
             IF B1 ¬= 0 & BOWLER.WEEK.GAME(B1,I0,I3) ¬= 0 THEN
               BOWLER.WEEK.PTS(B1,I0) =
                         BOWLER.WEEK.PTS(B1,I0) + FLOAT(PTS_I_G) / 2.0;

            /*********************************************************
             *  If bowler 2 was present, give him credit for split-  *
             *  ting with his opponent.                              *
             *********************************************************/
             IF B2 ¬= 0 & BOWLER.WEEK.GAME(B2,I0,I3) ¬= 0 THEN
               BOWLER.WEEK.PTS(B2,I0) =
                         BOWLER.WEEK.PTS(B2,I0) + FLOAT(PTS_I_G) / 2.0;
           END;

          /***********************************************************
           *  If bowler 2's net score is higher than that of bowler  *
           *  1 and at least one of them was present, give team 2    *
           *  the points for an individual game.                     *
           ***********************************************************/
           IF SCORE.NET(I2,I3,2) > SCORE.NET(I2,I3,1) &
                             (BOWLER.WEEK.GAME(B1,I0,I3) ¬= 0 |
                              BOWLER.WEEK.GAME(B2,I0,I3) ¬= 0) THEN DO;
             IF PTS_I_G ¬= 0 THEN
               SUBSTR(RECAP_STR(I2),84+5*I3,3) = '___';
             SCORE.PTS(I2,I3,2) = FLOAT(PTS_I_G);
             SCORE.PTS(0,I3,2) = SCORE.PTS(0,I3,2) + FLOAT(PTS_I_G);
             SCORE.PTS(I2,0,2) = SCORE.PTS(I2,0,2) + FLOAT(PTS_I_G);
             SCORE.PTS(0,0,2) = SCORE.PTS(0,0,2) + FLOAT(PTS_I_G);

            /***************************************************
             *  If bowler 2 was present, give him credit for   *
             *  beating his opponent.                          *
             ***************************************************/
             IF B2 ¬= 0 & BOWLER.WEEK.GAME(B2,I0,I3) ¬= 0 THEN
               BOWLER.WEEK.PTS(B2,I0) =
                                      BOWLER.WEEK.PTS(B2,I0) + PTS_I_G;
           END;
          /***********************************************************
           *  If both bowlers are absent, the points go to the team  *
           *  that won the game, or split them if the teams tie.     *
           ***********************************************************/
           IF BOWLER.WEEK.GAME(B1,I0,I3) = 0 &
                                 BOWLER.WEEK.GAME(B2,I0,I3) = 0 THEN DO;
             IF SCORE.NET(0,I3,1) > SCORE.NET(0,I3,2) THEN DO;
               IF PTS_I_G ¬= 0 THEN
                 SUBSTR(RECAP_STR(I2),24+5*I3,3) = '___';
               SCORE.PTS(I2,I3,1) = FLOAT(PTS_I_G);
               SCORE.PTS(I2,0,1) = SCORE.PTS(I2,0,1) + FLOAT(PTS_I_G);
               SCORE.PTS(0,I3,1) = SCORE.PTS(0,I3,1) + FLOAT(PTS_I_G);
               SCORE.PTS(0,0,1) = SCORE.PTS(0,0,1) + FLOAT(PTS_I_G);
             END;
             IF SCORE.NET(0,I3,2) > SCORE.NET(0,I3,1) THEN DO;
               IF PTS_I_G ¬= 0 THEN
                 SUBSTR(RECAP_STR(I2),84+5*I3,3) = '___';
               SCORE.PTS(I2,I3,2) = FLOAT(PTS_I_G);
               SCORE.PTS(I2,0,2) = SCORE.PTS(I2,0,2) + FLOAT(PTS_I_G);
               SCORE.PTS(0,I3,2) = SCORE.PTS(0,I3,2) + FLOAT(PTS_I_G);
               SCORE.PTS(0,0,2) = SCORE.PTS(0,0,2) + FLOAT(PTS_I_G);
             END;
             IF SCORE.NET(0,I3,1) = SCORE.NET(0,I3,2) THEN DO;
               IF PTS_I_G ¬= 0 THEN DO;
                 SUBSTR(RECAP_STR(I2),24+5*I3,3) = '  _';
                 SUBSTR(RECAP_STR(I2),84+5*I3,3) = '  _';
               END;
               SCORE.PTS(I2,I3,1) = FLOAT(PTS_I_G) / 2.0;
               SCORE.PTS(I2,0,1) = SCORE.PTS(I2,0,1) +
                                                  FLOAT(PTS_I_G) / 2.0;
               SCORE.PTS(0,I3,1) = SCORE.PTS(0,I3,1) +
                                                  FLOAT(PTS_I_G) / 2.0;
               SCORE.PTS(0,0,1) = SCORE.PTS(0,0,1) +
                                                  FLOAT(PTS_I_G) / 2.0;
               SCORE.PTS(I2,I3,2) = FLOAT(PTS_I_G) / 2.0;
               SCORE.PTS(I2,0,2) = SCORE.PTS(I2,0,2) +
                                                  FLOAT(PTS_I_G) / 2.0;
               SCORE.PTS(0,I3,2) = SCORE.PTS(0,I3,2) +
                                                  FLOAT(PTS_I_G) / 2.0;
               SCORE.PTS(0,0,2) = SCORE.PTS(0,0,2) +
                                                  FLOAT(PTS_I_G) / 2.0;
             END;
           END;
         END;

        /*********************************************************
         *  If bowler 1 had a higher series than bowler 2, then  *
         *  give team 1 the points for an individual series.     *
         *********************************************************/
         IF SCORE.NET(I2,0,1) > SCORE.NET(I2,0,2) THEN DO;
           IF PTS_I_S ¬= 0 THEN
             SUBSTR(RECAP_STR(I2),45,3) = '___';
           SCORE.PTS(I2,0,1) = SCORE.PTS(I2,0,1) + FLOAT(PTS_I_S);
           SCORE.PTS(0,0,1) = SCORE.PTS(0,0,1) + FLOAT(PTS_I_S);

          /******************************************************
           *  If bowler 1 was present for at least one of the   *
           *  three games, give him the points for winning the  *
           *  series.                                           *
           ******************************************************/
           IF B1 ¬= 0 & (BOWLER.WEEK.GAME(B1,I0,1) ¬= 0 |
                              BOWLER.WEEK.GAME(B1,I0,2) ¬= 0 |
                              BOWLER.WEEK.GAME(B1,I0,3) ¬= 0) THEN
             BOWLER.WEEK.PTS(B1,I0) =
                         BOWLER.WEEK.PTS(B1,I0) + FLOAT(PTS_I_S);
         END;

        /*********************************************************
         *  If bowler 1 had the same series as bowler 2 and at   *
         *  least one of them was present, split the points for  *
         *  individual series between the two teams.             *
         *********************************************************/
         IF SCORE.NET(I2,0,1) = SCORE.NET(I2,0,2) &
                              (BOWLER.WEEK.GAME(B1,I0,1) ¬= 0 |
                               BOWLER.WEEK.GAME(B1,I0,2) ¬= 0 |
                               BOWLER.WEEK.GAME(B1,I0,3) ¬= 0 |
                               BOWLER.WEEK.GAME(B2,I0,1) ¬= 0 |
                               BOWLER.WEEK.GAME(B2,I0,2) ¬= 0 |
                               BOWLER.WEEK.GAME(B2,I0,3) ¬= 0) THEN DO;
           IF PTS_I_S ¬= 0 THEN DO;
             SUBSTR(RECAP_STR(I2),45,3) = '  _';
             SUBSTR(RECAP_STR(I2),105,3) = '  _';
           END;
           SCORE.PTS(I2,0,1) = SCORE.PTS(I2,0,1)+ FLOAT(PTS_I_S) / 2.0;
           SCORE.PTS(0,0,1) = SCORE.PTS(0,0,1) + FLOAT(PTS_I_S) / 2.0;
           SCORE.PTS(I2,0,2) = SCORE.PTS(I2,0,2) +FLOAT(PTS_I_S) / 2.0;
           SCORE.PTS(0,0,2) = SCORE.PTS(0,0,2) + FLOAT(PTS_I_S) / 2.0;

          /******************************************************
           *  If bowler 1 was present for at least one of the   *
           *  three games, give him the points for splitting    *
           *  the series.                                       *
           ******************************************************/
           IF B1 ¬= 0 & BOWLER.WEEK.PRESENT(B1,I0) THEN
             BOWLER.WEEK.PTS(B1,I0) =
                         BOWLER.WEEK.PTS(B1,I0) + FLOAT(PTS_I_S) / 2.0;

          /******************************************************
           *  If bowler 2 was present for at least one of the   *
           *  three games, give him the points for splitting    *
           *  the series.                                       *
           ******************************************************/
           IF B2 ¬= 0 & BOWLER.WEEK.PRESENT(B2,I0) THEN
             BOWLER.WEEK.PTS(B2,I0) =
                         BOWLER.WEEK.PTS(B2,I0) + FLOAT(PTS_I_S) / 2.0;
         END;

        /*********************************************************
         *  If bowler 2 had a higher series than bowler 1, then  *
         *  give team 2 the points for an individual series.     *
         *********************************************************/
         IF SCORE.NET(I2,0,2) > SCORE.NET(I2,0,1) THEN DO;
           IF PTS_I_S ¬= 0 THEN
             SUBSTR(RECAP_STR(I2),105,3) = '___';
           SCORE.PTS(I2,0,2) = SCORE.PTS(I2,0,2) + FLOAT(PTS_I_S);
           SCORE.PTS(0,0,2) = SCORE.PTS(0,0,2) +FLOAT(PTS_I_S);

          /******************************************************
           *  If bowler 2 was present for at least one of the   *
           *  three games, give him the points for winning the  *
           *  series.                                           *
           ******************************************************/
           IF B2 ¬= 0 & (BOWLER.WEEK.GAME(B2,I0,1) ¬= 0 |
                              BOWLER.WEEK.GAME(B2,I0,2) ¬= 0 |
                              BOWLER.WEEK.GAME(B2,I0,3) ¬= 0) THEN
             BOWLER.WEEK.PTS(B2,I0) = BOWLER.WEEK.PTS(B2,I0) + PTS_I_S;
         END;
       END;

       DO I3 = 1 TO 3;
        /*********************************************************
         *  If one of the two teams didn't field enough bowlers  *
         *  then the other team automatically wins the game.     *
         *  If the team number is less than 0, then that team    *
         *  didn't field enough bowlers.                         *
         *********************************************************/
         IF (T1 > 0 & T2 < 0) | (T1 < 0 & T2 > 0) THEN DO;
           IF T1 > 0 THEN DO;
             IF PTS_T_G ¬= 0 THEN
               IF SCORE.NET(0,I3,1) < 1000 THEN
                         SUBSTR(RECAP_STR(0),23+5*I3,4) = ' ___';
               ELSE
                         SUBSTR(RECAP_STR(0),23+5*I3,4) = '____';
             SCORE.PTS(0,I3,1) = SCORE.PTS(0,I3,1) + FLOAT(PTS_T_G);
             SCORE.PTS(0,0,1) = SCORE.PTS(0,0,1) + FLOAT(PTS_T_G);
           END;
           ELSE DO;
             IF PTS_T_G ¬= 0 THEN
               IF SCORE.NET(0,I3,2) < 1000 THEN
                         SUBSTR(RECAP_STR(0),83+5*I3,4) = ' ___';
               ELSE
                         SUBSTR(RECAP_STR(0),83+5*I3,4) = '____';
             SCORE.PTS(0,I3,2) = SCORE.PTS(0,I3,2) + FLOAT(PTS_T_G);
             SCORE.PTS(0,0,2) = SCORE.PTS(0,0,2) + FLOAT(PTS_T_G);
           END;
         END;
         ELSE DO;

          /******************************************************
           *  If team 1 bowled a higher net game than team 2    *
           *  then give them the points.                        *
           ******************************************************/
           IF SCORE.NET(0,I3,1) > SCORE.NET(0,I3,2) THEN DO;
             IF PTS_T_G ¬= 0 THEN
               IF SCORE.NET(0,I3,1) < 1000 THEN
                         SUBSTR(RECAP_STR(0),23+5*I3,4) = ' ___';
               ELSE
                         SUBSTR(RECAP_STR(0),23+5*I3,4) = '____';
             SCORE.PTS(0,I3,1) = SCORE.PTS(0,I3,1) + FLOAT(PTS_T_G);
             SCORE.PTS(0,0,1) = SCORE.PTS(0,0,1) + FLOAT(PTS_T_G);
           END;

        /*********************************************************
         *  If team 1 and team 2 tied, split the points for the  *
         *  team game.                                           *
         *********************************************************/
           IF SCORE.NET(0,I3,1) = SCORE.NET(0,I3,2) THEN DO;
             IF PTS_T_G ¬= 0 THEN DO;
               SUBSTR(RECAP_STR(0),23+5*I3,4) = '   _';
               SUBSTR(RECAP_STR(0),83+5*I3,4) = '   _';
             END;
             SCORE.PTS(0,I3,1) = SCORE.PTS(0,I3,1) +
                                                  FLOAT(PTS_T_G) / 2.0;
             SCORE.PTS(0,0,1) = SCORE.PTS(0,0,1) +
                                                  FLOAT(PTS_T_G) / 2.0;
             SCORE.PTS(0,I3,2) = SCORE.PTS(0,I3,2) +
                                                  FLOAT(PTS_T_G) / 2.0;
             SCORE.PTS(0,0,2) = SCORE.PTS(0,0,2) +
                                                  FLOAT(PTS_T_G) / 2.0;
           END;

          /******************************************************
           *  If team 2 bowled a higher net game than team 1    *
           *  then give them the points.                        *
           ******************************************************/
           IF SCORE.NET(0,I3,2) > SCORE.NET(0,I3,1) THEN DO;
             IF PTS_T_G ¬= 0 THEN
               IF SCORE.NET(0,I3,2) < 1000 THEN
                 SUBSTR(RECAP_STR(0),83+5*I3,4) = ' ___';
               ELSE
                 SUBSTR(RECAP_STR(0),83+5*I3,4) = '____';
             SCORE.PTS(0,I3,2) = SCORE.PTS(0,I3,2) + FLOAT(PTS_T_G);
             SCORE.PTS(0,0,2) = SCORE.PTS(0,0,2) + FLOAT(PTS_T_G);
           END;
         END;
       END;

      /************************************************************
       *  If one of the two teams didn't field enough bowlers     *
       *  then the other team automatically wins the series.  If  *
       *  the team number is less than 0, then that team didn't   *
       *  field enough bowlers.                                   *
       ************************************************************/
       IF (T1 > 0 & T2 < 0) & (T1 < 0 & T2 > 0) THEN DO;
         IF T1 > 0 THEN DO;
           IF PTS_T_S ¬= 0 THEN
             SUBSTR(RECAP_STR(0),44,4) = '____';
           SCORE.PTS(0,0,1) = SCORE.PTS(0,0,1) + FLOAT(PTS_T_S);
         END;
         ELSE DO;
           IF PTS_T_S ¬= 0 THEN
             SUBSTR(RECAP_STR(0),104,4) = '____';
           SCORE.PTS(0,0,2) = SCORE.PTS(0,0,2) + FLOAT(PTS_T_S);
         END;
       END;
       ELSE DO;
        /*********************************************************
         *  If team 1 beat team 2 overall then give them points  *
         *  for winning team series.                             *
         *********************************************************/
         IF SCORE.NET(0,0,1) > SCORE.NET(0,0,2) THEN DO;
           IF PTS_T_S ¬= 0 THEN
             SUBSTR(RECAP_STR(0),44,4) = '____';
           SCORE.PTS(0,0,1) = SCORE.PTS(0,0,1) + FLOAT(PTS_T_S);
         END;

        /*********************************************************
         *  If team 1 tied team 2 overall then split the points  *
         *  for winning team series.                             *
         *********************************************************/
         IF SCORE.NET(0,0,1) = SCORE.NET(0,0,2) THEN DO;
           IF PTS_T_S ¬= 0 THEN DO;
             SUBSTR(RECAP_STR(0),44,4) = '   _';
             SUBSTR(RECAP_STR(0),104,4) = '   _';
           END;
           SCORE.PTS(0,0,1) = SCORE.PTS(0,0,1) + FLOAT(PTS_T_S) / 2.0;
           SCORE.PTS(0,0,2) = SCORE.PTS(0,0,2) + FLOAT(PTS_T_S) / 2.0;
         END;

        /*********************************************************
         *  If team 2 beat team 1 overall then give them points  *
         *  for winning team series.                             *
         *********************************************************/
         IF SCORE.NET(0,0,2) > SCORE.NET(0,0,1) THEN DO;
           IF PTS_T_S ¬= 0 THEN
             SUBSTR(RECAP_STR(0),104,4) = '____';
           SCORE.PTS(0,0,2) = SCORE.PTS(0,0,2) + FLOAT(PTS_T_S);
         END;
       END;

      /************************************************************
       *  Save this week's total points.                          *
       ************************************************************/
       TEAM.WEEK.WON(ABS(T1),I0)  = SCORE.PTS(0,0,1);
       TEAM.WEEK.LOST(ABS(T1),I0) = 3*(#_ACTIVE*PTS_I_G + PTS_T_G) +
                         #_ACTIVE*PTS_I_S + PTS_T_S - SCORE.PTS(0,0,1);
       TEAM.WEEK.WON(ABS(T2),I0)  = SCORE.PTS(0,0,2);
       TEAM.WEEK.LOST(ABS(T2),I0) = 3*(#_ACTIVE*PTS_I_G + PTS_T_G) +
                         #_ACTIVE*PTS_I_S + PTS_T_S - SCORE.PTS(0,0,2);

      /************************************************************
       *  Print the recap sheet for this game.                    *
       ************************************************************/
       IF I0 = WEEK_# | PRINT_ALL THEN DO;
         PUT FILE(SMRECAP) EDIT(' ') (SKIP,COL(1),A);
         PUT FILE(SMRECAP)
                 EDIT(TEAM.NAME(ABS(T1)),DATE(I0),
               TEAM.NAME(ABS(T2)),DATE(I0))
                     (COL(1),A(20),COL(45),A(9),
               COL(61),A(20),COL(105),A(9));
         PUT FILE(SMRECAP)
                 EDIT('HDCP','NAME','AVG','1','2','3','SER','PTS',
               'HDCP','NAME','AVG','1','2','3','SER','PTS')
                     (SKIP,COL(1),A,COL(6),A,COL(24),A,COL(30),A,
               COL(35),A,COL(40),A,COL(45),A,COL(51),A,
               COL(61),A,COL(66),A,COL(84),A,COL(90),A,
               COL(95),A,COL(100),A,COL(105),A,COL(111),A);
         DO I2 = 1 TO #_ACTIVE;
           B1 = TEAM.WEEK.BOWLER(ABS(T1),I0,I2);
           B2 = TEAM.WEEK.BOWLER(ABS(T2),I0,I2);
           PUT FILE(SMRECAP)
                    EDIT(SCORE.HDCP(I2,1,1),BOWLER.F_NAME(B1),
                         BOWLER.L_NAME(B1),SCORE.AVG(I2,1,1),
                         SCORE.GROSS(I2,1,1),SCORE.GROSS(I2,2,1),
                         SCORE.GROSS(I2,3,1),SCORE.GROSS(I2,0,1),
                         SCORE.PTS(I2,0,1),
                         SCORE.HDCP(I2,1,2),BOWLER.F_NAME(B2),
                         BOWLER.L_NAME(B2),SCORE.AVG(I2,1,2),
                         SCORE.GROSS(I2,1,2),SCORE.GROSS(I2,2,2),
                         SCORE.GROSS(I2,3,2),SCORE.GROSS(I2,0,2),
                         SCORE.PTS(I2,0,2))
                (SKIP,COL(1),F(3,0),COL(6),A(1),COL(8),
                         A(15),COL(23),F(4,0),COL(28),F(4,0),COL(33),
                         F(4,0),COL(38),F(4,0),COL(44),F(4,0),
                         COL(51),F(3,1),
                         COL(61),F(3,0),COL(66),A(1),COL(68),
                         A(15),COL(83),F(4,0),COL(88),F(4,0),COL(93),
                         F(4,0),COL(98),F(4,0),COL(104),F(4,0),
                         COL(111),F(3,1));
           PUT FILE(SMRECAP)
                    EDIT(RECAP_STR(I2)) (SKIP(0),COL(1),A);
         END;
         PUT FILE(SMRECAP)
                 EDIT('     SUBTOTAL:',SCORE.AVG(0,1,1),
               SCORE.GROSS(0,1,1),SCORE.GROSS(0,2,1),
               SCORE.GROSS(0,3,1),SCORE.GROSS(0,0,1),
               '     SUBTOTAL:',SCORE.AVG(0,1,2),
               SCORE.GROSS(0,1,2),SCORE.GROSS(0,2,2),
               SCORE.GROSS(0,3,2),SCORE.GROSS(0,0,2))
                     (SKIP,COL(8),A,COL(23),F(4,0),COL(28),F(4,0),
               COL(33),F(4,0),COL(38),F(4,0),COL(44),F(4,0),
               COL(68),A,COL(83),F(4,0),COL(88),F(4,0),
               COL(93),F(4,0),COL(98),F(4,0),COL(104),F(4,0));
         PUT FILE(SMRECAP)
                 EDIT(SCORE.HDCP(0,1,1),'     HANDICAP:',
               SCORE.HDCP(0,1,1),SCORE.HDCP(0,1,1),
               SCORE.HDCP(0,2,1),SCORE.HDCP(0,3,1),
               SCORE.HDCP(0,0,1),
               SCORE.HDCP(0,1,2),'     HANDICAP:',
               SCORE.HDCP(0,1,2),SCORE.HDCP(0,1,2),
               SCORE.HDCP(0,2,2),SCORE.HDCP(0,3,2),
               SCORE.HDCP(0,0,2))
                     (SKIP,COL(1),F(3,0),COL(8),A,COL(23),F(4,0),
               COL(28),F(4,0),COL(33),F(4,0),COL(38),F(4,0),
               COL(44),F(4,0),
               COL(61),F(3,0),COL(68),A,COL(83),F(4,0),
               COL(88),F(4,0),COL(93),F(4,0),COL(98),F(4,0),
               COL(104),F(4,0));
         PUT FILE(SMRECAP)
                 EDIT('        TOTAL:',
               SCORE.AVG(0,1,1)+SCORE.HDCP(0,1,1),
               SCORE.NET(0,1,1),SCORE.NET(0,2,1),
               SCORE.NET(0,3,1),SCORE.NET(0,0,1),
               '        TOTAL:',
               SCORE.AVG(0,1,2)+SCORE.HDCP(0,1,2),
               SCORE.NET(0,1,2),SCORE.NET(0,2,2),
               SCORE.NET(0,3,2),SCORE.NET(0,0,2))
                     (SKIP,COL(8),A,COL(23),F(4,0),COL(28),F(4,0),
               COL(33),F(4,0),COL(38),F(4,0),COL(44),F(4,0),
               COL(68),A,COL(83),F(4,0),COL(88),F(4,0),
               COL(93),F(4,0),COL(98),F(4,0),COL(104),F(4,0));
         PUT FILE(SMRECAP)
                 EDIT(RECAP_STR(0)) (SKIP(0),COL(1),A);
         PUT FILE(SMRECAP)
                 EDIT('       POINTS:',SCORE.PTS(0,1,1),
               SCORE.PTS(0,2,1),SCORE.PTS(0,3,1),
               SCORE.PTS(0,0,1),
               '       POINTS:',SCORE.PTS(0,1,2),
               SCORE.PTS(0,2,2),SCORE.PTS(0,3,2),
               SCORE.PTS(0,0,2))
                     (SKIP,COL(8),A,COL(28),F(4,1),COL(33),F(4,1),
               COL(38),F(4,1),COL(44),F(4,1),
               COL(68),A,COL(88),F(4,1),COL(93),F(4,1),
               COL(98),F(4,1),COL(104),F(4,1));
         PUT FILE(SMRECAP) EDIT(' ') (SKIP,COL(1),A);
       END;
     END;
 /* */
     IF I0 = WEEK_# | PRINT_ALL THEN
       PUT FILE(SMRECAP) PAGE;
   END;

   CLOSE FILE(SMRECAP);

  /*******************************************************************
   *  Put the Xerox 4045 printer into portrait mode.                 *
   *******************************************************************/
   PUT FILE(SMSTAND) EDIT('=UDK=`') (SKIP,COL(1),A);
   PUT FILE(SMSTAND) EDIT('`+0Titan10iso-P') (SKIP,COL(1),A);
   PUT FILE(SMSTAND) EDIT('`+1M13L-P') (SKIP,COL(1),A);
   PUT FILE(SMSTAND) EDIT('`+2M48-P') (SKIP,COL(1),A);
   PUT FILE(SMSTAND) EDIT('`+3VINTAGE10-P') (SKIP,COL(1),A);
   PUT FILE(SMSTAND) EDIT('`+4XCP14-P') (SKIP,COL(1),A);
   PUT FILE(SMSTAND) EDIT('`m660,15,0,30,510') (SKIP,COL(1),A);
   PUT FILE(SMSTAND) EDIT('`0') (SKIP,COL(1),A);

  /*******************************************************************
   *  Add each bowler's points by week for a running total.          *
   *******************************************************************/
   DO I0 = 1 TO #_BOWLERS;
     DO I1 = 2 TO WEEK_#;
       BOWLER.WEEK.PTS(I0,I1) = BOWLER.WEEK.PTS(I0,I1-1) +
                                                BOWLER.WEEK.PTS(I0,I1);
     END;
   END;

   DO I0 = 0 TO WEEK_#;
     DO I1 = 1 TO #_TEAMS;
       IF I0 > 1 & I0 ¬= MID_POINT THEN DO;
         TEAM.WEEK.WON(I1,I0) = TEAM.WEEK.WON(I1,I0) +
                                                TEAM.WEEK.WON(I1,I0-1);
         TEAM.WEEK.LOST(I1,I0) = TEAM.WEEK.LOST(I1,I0) +
                                               TEAM.WEEK.LOST(I1,I0-1);
       END;
       TEAM.WEEK.AVG(I1,I0) = 0;
       TEAM.WEEK.HDCP(I1,I0) = 0;

      /************************************************************
       *  For each team, loop through the active roster and sum   *
       *  the averages.                                           *
       ************************************************************/
       DO I2 = 1 TO TEAM.#_MEMBERS(I1);
         IF BOWLER.WEEK.AVG(TEAM.MEMBER(I1,I2),I0) = 0 THEN DO;
           TEAM.WEEK.AVG(I1,I0) = TEAM.WEEK.AVG(I1,I0) +
                                     BOWLER.LY_AVG(TEAM.MEMBER(I1,I2));
           TEAM.WEEK.HDCP(I1,I0) = TEAM.WEEK.HDCP(I1,I0) +
                     HDCP(BOWLER.LY_AVG(TEAM.MEMBER(I1,I2)));
         END;
         ELSE DO;
           TEAM.WEEK.AVG(I1,I0) = TEAM.WEEK.AVG(I1,I0) +
                                BOWLER.WEEK.AVG(TEAM.MEMBER(I1,I2),I0);
           TEAM.WEEK.HDCP(I1,I0) = TEAM.WEEK.HDCP(I1,I0) +
                          HDCP(BOWLER.WEEK.AVG(TEAM.MEMBER(I1,I2),I0));
         END;
       END;
     END;

    /************************************************************
     *  Tag sort the teams by points won.                       *
     ************************************************************/
     DO I1 = 1 TO #_TEAMS;
       SORT(I1) = I1;
     END;
     IF I0 > 0 THEN DO;
       DO I1 = 1 TO #_TEAMS - 1;
         DO I2 = I1 + 1 TO #_TEAMS;
           IF ROUND(TEAM.WEEK.WON(SORT(I1),I0),1) <
                           ROUND(TEAM.WEEK.WON(SORT(I2),I0),1) THEN DO;
             I3 = SORT(I1);
             SORT(I1) = SORT(I2);
             SORT(I2) = I3;
           END;
         END;
       END;
     END;

    /************************************************************
     *  Print the headings for the weekly stat sheet.           *
     ************************************************************/
     IF I0 = WEEK_# | PRINT_ALL THEN DO;
       PUT FILE(SMSTAND) EDIT('TODAY''S DATE:',DATE(I0+1))
                             (PAGE,COL(54),A,COL(68),A);
       PUT FILE(SMSTAND) EDIT('RESULTS AFTER:',DATE(I0))
                             (SKIP,COL(53),A,COL(68),A);
       PUT FILE(SMSTAND)
              EDIT('W','L','AVG','HDCP')
                  (SKIP(3),COL(24),A,COL(31),A,COL(37),A,COL(42),A);
       PUT FILE(SMSTAND)
              EDIT('_____','_____','___','____')
                  (SKIP(0),COL(22),A,COL(29),A,COL(37),A,COL(42),A);

      /************************************************************
       *  Print the team name, the number of points won, and the  *
       *  number of points lost.                                  *
       ************************************************************/
       DO I1 = 1 TO #_TEAMS;
         PUT FILE(SMSTAND)
                 EDIT(I1,TEAM.NAME(SORT(I1)),
               TEAM.WEEK.WON(SORT(I1),I0),
               TEAM.WEEK.LOST(SORT(I1),I0),
               TEAM.WEEK.AVG(SORT(I1),I0),
               TEAM.WEEK.HDCP(SORT(I1),I0))
                     (SKIP,COL(1),F(2,0),COL(4),A,COL(22),F(5,1),
               COL(29),F(5,1),COL(36),F(4,0),COL(43),F(3,0));
       END;

      /************************************************************
       *  Print the high actual game and series.                  *
       ************************************************************/
       PUT FILE(SMSTAND)
              EDIT('  HIGH ACTUAL GAME  ',' HIGH ACTUAL SERIES ')
                  (SKIP(2),COL(1),A,COL(26),A);
       PUT FILE(SMSTAND)
              EDIT('____________________','____________________')
                  (SKIP(0),COL(1),A,COL(26),A);
       IF I0 > 0 THEN DO;
         PUT FILE(SMSTAND)
                 EDIT(BOWLER.F_NAME(HI.GAB(I0,1)),
               BOWLER.L_NAME(HI.GAB(I0,1)),HI.GA(I0,1),
               BOWLER.F_NAME(HI.SAB(I0,1)),
               BOWLER.L_NAME(HI.SAB(I0,1)),HI.SA(I0,1))
                     (SKIP,COL(1),A(1),COL(3),A(14),COL(18),F(3,0),
               COL(26),A(1),COL(28),A(14),COL(43),F(3,0));
         PUT FILE(SMSTAND)
                 EDIT(BOWLER.F_NAME(HI.GAB(I0,2)),
               BOWLER.L_NAME(HI.GAB(I0,2)),HI.GA(I0,2),
               BOWLER.F_NAME(HI.SAB(I0,2)),
               BOWLER.L_NAME(HI.SAB(I0,2)),HI.SA(I0,2))
                     (SKIP,COL(1),A(1),COL(3),A(14),COL(18),F(3,0),
               COL(26),A(1),COL(28),A(14),COL(43),F(3,0));
       END;

      /************************************************************
       *  If there is a high handicap game or series print the    *
       *  header for either or both.  Then print the bowlers and  *
       *  scores.                                                 *
       ************************************************************/
       PUT FILE(SMSTAND)
              EDIT(' HIGH HANDICAP GAME ','HIGH HANDICAP SERIES')
                  (SKIP(2),COL(1),A,COL(26),A);
       PUT FILE(SMSTAND)
              EDIT('____________________','____________________')
                  (SKIP(0),COL(1),A,COL(26),A);
       PUT FILE(SMSTAND) SKIP;
       IF HI.GH(I0,1) > 0 THEN
         PUT FILE(SMSTAND)
                 EDIT(BOWLER.F_NAME(HI.GHB(I0,1)),
               BOWLER.L_NAME(HI.GHB(I0,1)),HI.GH(I0,1))
                     (COL(1),A(1),COL(3),A(14),COL(18),F(3,0));
       IF HI.SH(I0,1) > 0 THEN
         PUT FILE(SMSTAND)
                 EDIT(BOWLER.F_NAME(HI.SHB(I0,1)),
               BOWLER.L_NAME(HI.SHB(I0,1)),HI.SH(I0,1))
                     (COL(26),A(1),COL(28),A(14),COL(43),F(3,0));
       IF HI.GH(I0,2) > 0 | HI.SH(I0,2) > 0 THEN DO;
         IF HI.GH(I0,2) > 0 THEN
           PUT FILE(SMSTAND)
                    EDIT(BOWLER.F_NAME(HI.GHB(I0,2)),
                         BOWLER.L_NAME(HI.GHB(I0,2)),HI.GH(I0,2))
                (COL(1),A(1),COL(3),A(14),COL(18),F(3,0));
         IF HI.SH(I0,2) > 0 THEN
           PUT FILE(SMSTAND)
                    EDIT(BOWLER.F_NAME(HI.SHB(I0,2)),
                         BOWLER.L_NAME(HI.SHB(I0,2)),HI.SH(I0,2))
                (COL(26),A(1),COL(28),A(14),COL(43),F(3,0));
       END;

       IF POT.HDCP_LANE(I0+1) ¬= 0 THEN DO;
         HDCP_STR = 'See Tom on lane ' ||
                           DIGITS(POT.HDCP_LANE(I0+1)) ||
                              ' for the Handicap Pot.';
         PUT FILE(SMSTAND) EDIT(CENTER(TRIM(HDCP_STR)))
                                   (SKIP(2),COL(3),A);
       END;
       IF POT.FIFTY50_LANE(I0+1) ¬= 0 THEN DO;
         FIFTY50_STR = 'See Stu on lane ' ||
                           DIGITS(POT.FIFTY50_LANE(I0+1)) ||
                              ' for 50/50 tickets.';
         PUT FILE(SMSTAND) EDIT(CENTER(TRIM(FIFTY50_STR)))
                                   (SKIP,COL(3),A);
       END;
       IF POT.PICKLE_LANE(I0+1) ¬= 0 THEN DO;
         PICKLE_STR = 'See Dale on lane ' ||
                             DIGITS(POT.PICKLE_LANE(I0+1)) ||
                                ' for the Pickle Jar';
         IF POT.PICKLE_JAR(I0+1) > 0 THEN
           PICKLE_STR = PICKLE_STR || ' (currently at $' ||
                                DIGITS(POT.PICKLE_JAR(I0+1)) || '.00)';
         PICKLE_STR = PICKLE_STR || '.';
         PUT FILE(SMSTAND) EDIT(CENTER(TRIM(PICKLE_STR)))
                                   (SKIP,COL(3),A);
       END;
       IF #_NOTES(I0+1) > 0 THEN DO;
         PUT FILE(SMSTAND) SKIP;
         DO I1 = 1 TO #_NOTES(I0+1);
           T_NOTE = NOTE(I0+1,I1);
           PUT FILE(SMSTAND) EDIT(CENTER(TRIM(T_NOTE)))
                                 (SKIP,COL(3),A(72));
         END;
       END;

       DO I1 = 1 TO #_TEAMS BY 2;
         PUT FILE(SMSTAND)
                 EDIT('`b',TEAM.NAME(I1),'`p',
               '`b',TEAM.NAME(I1+1),'`p',
               'HDCP  PTS  PINS  G AVG',
               'HDCP  PTS  PINS  G AVG')
                     (SKIP(2),COL(1),A,A(14),A,
               SKIP(0),COL(41),A,A(14),A,
               SKIP(0),COL(15),A,COL(55),A);
         PUT FILE(SMSTAND)
                 EDIT(REPEAT('_',35),REPEAT('_',35))
                     (SKIP(0),COL(1),A,COL(41),A);
         DO I2 = 1 TO MAX(TEAM.#_MEMBERS(I1),TEAM.#_MEMBERS(I1+1));
           PUT FILE(SMSTAND) SKIP;
           IF I2 <= TEAM.#_MEMBERS(I1) THEN DO;
             PUT FILE(SMSTAND)
                       EDIT(BOWLER.F_NAME(TEAM.MEMBER(I1,I2)),
                   BOWLER.L_NAME(TEAM.MEMBER(I1,I2)))
                           (COL(1),A(1),COL(3),A(12));
             IF BOWLER.WEEK.WEEKS(TEAM.MEMBER(I1,I2),I0) < EST_AVG
                          & BOWLER.LY_AVG(TEAM.MEMBER(I1,I2)) ¬= 0 THEN
               PUT FILE(SMSTAND)
                   EDIT(HDCP(BOWLER.LY_AVG(TEAM.MEMBER(I1,I2))))
                       (COL(15),F(3,0));
             ELSE
               PUT FILE(SMSTAND)
                   EDIT(BOWLER.WEEK.HDCP(TEAM.MEMBER(I1,I2),I0))
                       (COL(15),F(3,0));
             PUT FILE(SMSTAND)
                 EDIT(BOWLER.WEEK.PTS(TEAM.MEMBER(I1,I2),I0),
                      BOWLER.WEEK.PINS(TEAM.MEMBER(I1,I2),I0),
                      BOWLER.WEEK.GAMES(TEAM.MEMBER(I1,I2),I0))
                     (COL(20),F(4,1),COL(25),F(5,0),
                      COL(31),F(2,0));
             IF BOWLER.WEEK.WEEKS(TEAM.MEMBER(I1,I2),I0) < EST_AVG
                          & BOWLER.LY_AVG(TEAM.MEMBER(I1,I2)) ¬= 0 THEN
               PUT FILE(SMSTAND)
                   EDIT(BOWLER.LY_AVG(TEAM.MEMBER(I1,I2)))
                       (COL(34),F(3,0));
             ELSE
               PUT FILE(SMSTAND)
                   EDIT(BOWLER.WEEK.AVG(TEAM.MEMBER(I1,I2),I0))
                       (COL(34),F(3,0));
           END;
           IF I2 <= TEAM.#_MEMBERS(I1+1) THEN DO;
             PUT FILE(SMSTAND)
                 EDIT(BOWLER.F_NAME(TEAM.MEMBER(I1+1,I2)),
                      BOWLER.L_NAME(TEAM.MEMBER(I1+1,I2)))
                     (COL(41),A(1),COL(43),A(12));
             IF BOWLER.WEEK.WEEKS(TEAM.MEMBER(I1+1,I2),I0) < EST_AVG
                        & BOWLER.LY_AVG(TEAM.MEMBER(I1+1,I2)) ¬= 0 THEN
               PUT FILE(SMSTAND)
                   EDIT(HDCP(BOWLER.LY_AVG(TEAM.MEMBER(I1+1,I2))))
                       (COL(55),F(3,0));
             ELSE
               PUT FILE(SMSTAND)
                   EDIT(BOWLER.WEEK.HDCP(TEAM.MEMBER(I1+1,I2),I0))
                       (COL(55),F(3,0));
             PUT FILE(SMSTAND)
                 EDIT(BOWLER.WEEK.PTS(TEAM.MEMBER(I1+1,I2),I0),
                      BOWLER.WEEK.PINS(TEAM.MEMBER(I1+1,I2),I0),
                      BOWLER.WEEK.GAMES(TEAM.MEMBER(I1+1,I2),I0))
                     (COL(60),F(4,1),COL(65),F(5,0),COL(71),F(2,0));
             IF BOWLER.WEEK.WEEKS(TEAM.MEMBER(I1+1,I2),I0) < EST_AVG
                & BOWLER.LY_AVG(TEAM.MEMBER(I1+1,I2)) ¬= 0 THEN
               PUT FILE(SMSTAND)
                   EDIT(BOWLER.LY_AVG(TEAM.MEMBER(I1+1,I2)))
                       (COL(74),F(3,0));
             ELSE
               PUT FILE(SMSTAND)
                   EDIT(BOWLER.WEEK.AVG(TEAM.MEMBER(I1+1,I2),I0))
                       (COL(74),F(3,0));
           END;
         END;
       END;
       PUT FILE(SMSTAND) EDIT('`a875,3225') (SKIP,COL(1),A);
       PUT FILE(SMSTAND) EDIT('`3`b''`p') (SKIP,COL(1),A);
       PUT FILE(SMSTAND) EDIT('`a150,3100') (SKIP,COL(1),A);
       PUT FILE(SMSTAND) EDIT('`2',BIG_LEAGUE,' `1`b',YEAR,'`p')
                                (SKIP,COL(1),A,A,A,A,A);

       SORT(*) = 0;
       I4 = 0;
       DO I2 = 1 TO #_BOWLERS;
         IF SUB(I2) ¬= 0 & ¬BOWLER.INACTIVE(I2)
              & BOWLER.WEEK.GAMES(I2,I0) > 0 THEN DO;
           I4 = I4 + 1;
           SORT(I4) = I2;
         END;
       END;
       DO I2 = 1 TO I4 - 1;
         DO I3 = I2 + 1 TO I4;
           IF BOWLER.L_NAME(SORT(I2)) || BOWLER.F_NAME(SORT(I2)) >
              BOWLER.L_NAME(SORT(I3)) || BOWLER.F_NAME(SORT(I3))
                                                               THEN DO;
             I5 = SORT(I2);
             SORT(I2) = SORT(I3);
             SORT(I3) = I5;
           END;
         END;
       END;

       PUT FILE(SMSTAND) EDIT('`4`a150,3035') (SKIP,COL(1),A);
       PUT FILE(SMSTAND)
              EDIT('SUBSTITUTE','PHONE','HDCP','AVG')
                  (SKIP,COL(68),A,COL(85),A,COL(96),A,COL(102),A);
       PUT FILE(SMSTAND)
              EDIT('__________','_____','____','___')
                  (SKIP(0),COL(68),A,COL(85),A,COL(96),A,COL(102),A);
       DO I2 = 1 TO I4;
         PUT FILE(SMSTAND)
             EDIT(BOWLER.F_NAME(SORT(I2)),BOWLER.L_NAME(SORT(I2)),
                  BOWLER.PHONE(SORT(I2)))
                 (COL(68),A(1),X(1),A(15),A(8));
         IF BOWLER.WEEK.WEEKS(SORT(I2),I0) <
                              EST_AVG & BOWLER.LY_AVG(SORT(I2))¬=0 THEN
           PUT FILE(SMSTAND)
               EDIT(HDCP(BOWLER.LY_AVG(SORT(I2))),
                    BOWLER.LY_AVG(SORT(I2)))
                   (COL(96),F(3,0),COL(102),F(3,0));
         ELSE
           PUT FILE(SMSTAND)
               EDIT(BOWLER.WEEK.HDCP(SORT(I2),I0),
                    BOWLER.WEEK.AVG(SORT(I2),I0))
                   (COL(96),F(3,0),COL(102),F(3,0));
       END;

     END;
   END;
   PUT FILE(SMSTAND) EDIT('`+X') (SKIP,COL(1),A);


   CLOSE FILE(SMSTAND);

  /*******************************************************************
   *  Put the Xerox 4045 printer into portrait mode.                 *
   *******************************************************************/
   PUT FILE(SMINDIV) EDIT('=UDK=`') (SKIP,COL(1),A);
   PUT FILE(SMINDIV) EDIT('`+0Titan10iso-P') (SKIP,COL(1),A);
   PUT FILE(SMINDIV) EDIT('`m660,15,0,30,510') (SKIP,COL(1),A);
   PUT FILE(SMINDIV) EDIT('`0') (SKIP,COL(1),A);

  /*******************************************************************
   *  Put the Xerox 4045 printer into portrait mode.                 *
   *******************************************************************/
   PUT FILE(SMSUMRY) EDIT('=UDK=`') (SKIP,COL(1),A);
   PUT FILE(SMSUMRY) EDIT('`+0Titan10iso-P') (SKIP,COL(1),A);
   PUT FILE(SMSUMRY) EDIT('`m660,15,0,30,510') (SKIP,COL(1),A);
   PUT FILE(SMSUMRY) EDIT('`0') (SKIP,COL(1),A);

  /****************************************************************
   *  Tag sort the bowlers by name.                               *
   ****************************************************************/
   DO I1 = 1 TO #_BOWLERS;
     SORT(I1) = I1;
     IF BOWLER.L_NAME(I1) = 'J Schultz' THEN DO;
       BOWLER.F_NAME(I1) = 'D.J.';
       BOWLER.L_NAME(I1) = 'Schultz';
     END;
   END;
   DO I1 = 1 TO #_BOWLERS - 1;
     DO I2 = I1 + 1 TO #_BOWLERS;
       IF BOWLER.L_NAME(SORT(I1)) > BOWLER.L_NAME(SORT(I2)) THEN DO;
         I3 = SORT(I1);
         SORT(I1) = SORT(I2);
         SORT(I2) = I3;
       END;
       IF BOWLER.L_NAME(SORT(I1)) = BOWLER.L_NAME(SORT(I2)) &
         BOWLER.F_NAME(SORT(I1)) > BOWLER.F_NAME(SORT(I2)) THEN DO;
         I3 = SORT(I1);
         SORT(I1) = SORT(I2);
         SORT(I2) = I3;
       END;
     END;
   END;

   DO I1 = 1 TO #_BOWLERS;
     I0 = SORT(I1);
     IF BOWLER.WEEK.GAMES(I0,WEEK_#) ¬= 0 THEN DO;
       PUT FILE(SMINDIV) EDIT(CENTER(LEAGUE)) (PAGE,COL(1),A);
       PUT FILE(SMINDIV) EDIT('Individual Record Sheet')
                                (SKIP(2),COL(26),A);
       PUT FILE(SMINDIV)
              EDIT(TRIM(BOWLER.L_NAME(I0)),', ',BOWLER.F_NAME(I0),
                  'Average Last Season:')
                  (SKIP(3),COL(1),A,A,A,COL(48),A);
       IF BOWLER.LY_AVG(I0) ¬= 0 THEN
            PUT FILE(SMINDIV) EDIT(BOWLER.LY_AVG(I0))
                       (COL(70),F(3,0));
       PUT FILE(SMINDIV)
              EDIT('Games','Grand','High','High')
                  (SKIP(2),COL(22),A,COL(37),A,COL(56),A,COL(63),A);
       PUT FILE(SMINDIV)
              EDIT('Lanes','Date','1','2','3','Tot','Total','Games',
             'Avg','Game','Series','Pts')
                  (COL(1),A,COL(10),A,COL(20),A,COL(24),A,COL(28),A,
             COL(32),A,COL(37),A,COL(44),A,COL(51),A,COL(56),A,
             COL(62),A,COL(70),A);
       PUT FILE(SMINDIV)
              EDIT(REPEAT('_',71)) (SKIP(0),COL(1),A);
       I5 = 0;
       I6 = 0;
       DO I2 = 1 TO WEEK_#;
         IF BOWLER.WEEK.GAME(I0,I2,1) ¬= 0 |
                 BOWLER.WEEK.GAME(I0,I2,2) ¬= 0 |
                  BOWLER.WEEK.GAME(I0,I2,3) ¬= 0 THEN DO;
           T2 = 0;
           DO I3 = 1 TO #_TEAMS;
             T1 = ABS(LANE(I2,I3));
             DO I4 = 1 TO #_ACTIVE;
               IF TEAM.WEEK.BOWLER(T1,I2,I4) = I0 THEN DO;
                         T2 = FLOOR((I3-1)/2) * 2 + 1;
                         PUT FILE(SMINDIV) EDIT(T2,'-',T2+1)
                                              (COL(1),F(2,0),A,F(2,0));
               END;
             END;
           END;
           PUT FILE(SMINDIV) EDIT(DATE(I2)) (COL(8),A);
           IF BOWLER.WEEK.GAME(I0,I2,1) ¬= 0 THEN
             PUT FILE(SMINDIV) EDIT(BOWLER.WEEK.GAME(I0,I2,1))
                                         (COL(19),F(3,0));
           IF BOWLER.WEEK.GAME(I0,I2,2) ¬= 0 THEN
             PUT FILE(SMINDIV) EDIT(BOWLER.WEEK.GAME(I0,I2,2))
                                         (COL(23),F(3,0));
           IF BOWLER.WEEK.GAME(I0,I2,3) ¬= 0 THEN
             PUT FILE(SMINDIV) EDIT(BOWLER.WEEK.GAME(I0,I2,3))
                                         (COL(27),F(3,0));
           IF BOWLER.WEEK.GAME(I0,I2,1) > I5 THEN
             I5 = BOWLER.WEEK.GAME(I0,I2,1);
           IF BOWLER.WEEK.GAME(I0,I2,2) > I5 THEN
             I5 = BOWLER.WEEK.GAME(I0,I2,2);
           IF BOWLER.WEEK.GAME(I0,I2,3) > I5 THEN
             I5 = BOWLER.WEEK.GAME(I0,I2,3);
           IF BOWLER.WEEK.GAME(I0,I2,1) +
                    BOWLER.WEEK.GAME(I0,I2,2) +
                     BOWLER.WEEK.GAME(I0,I2,3) > I6 THEN
             I6 = BOWLER.WEEK.GAME(I0,I2,1) +
                         BOWLER.WEEK.GAME(I0,I2,2) +
                          BOWLER.WEEK.GAME(I0,I2,3);
           PUT FILE(SMINDIV) EDIT(BOWLER.WEEK.GAME(I0,I2,1) +
                                       BOWLER.WEEK.GAME(I0,I2,2) +
                                       BOWLER.WEEK.GAME(I0,I2,3))
                                      (COL(32),F(3,0));
           PUT FILE(SMINDIV)
               EDIT(BOWLER.WEEK.PINS(I0,I2),
                         BOWLER.WEEK.GAMES(I0,I2),
                         BOWLER.WEEK.AVG(I0,I2),I5,I6,
                         BOWLER.WEEK.PTS(I0,I2))
                        (COL(37),F(5,0),COL(45),F(3,0),COL(51),F(3,0),
                         COL(57),F(3,0),COL(64),F(3,0),COL(69),F(4,1));
         END;
       END;

       IF MOD(I1,50) = 1 THEN DO;
         PUT FILE(SMSUMRY) EDIT(CENTER(LEAGUE)) (PAGE,COL(1),A);
         PUT FILE(SMSUMRY)
             EDIT(CENTER('Bowler Summary')) (SKIP(2),COL(1),A);
         PUT FILE(SMSUMRY)
             EDIT('Final','Final','21-Game')
                 (SKIP(3),COL(38),A,COL(46),A,COL(54),A);
         PUT FILE(SMSUMRY) EDIT('Name','Games','Average','Hdcp',
                                'Average','Change')
                               (SKIP,COL(1),A,COL(29),A,COL(37),A,
                                COL(47),A,COL(54),A,COL(64),A);
         PUT FILE(SMSUMRY) EDIT('____','_____','_______','____',
                                '_______','______')
                               (SKIP(0),COL(1),A,COL(29),A,COL(37),A,
                                COL(47),A,COL(54),A,COL(64),A);
       END;
       IF BOWLER.WEEK.GAMES(I0,WEEK_#) > 21 THEN
         PUT FILE(SMSUMRY) EDIT('`b') (SKIP,COL(1),A);
       ELSE
         PUT FILE(SMSUMRY) EDIT('`p') (SKIP,COL(1),A);
       PUT FILE(SMSUMRY)
              EDIT(TRIM(BOWLER.L_NAME(I0)) || ', ' ||
             TRIM(BOWLER.F_NAME(I0)),
             BOWLER.WEEK.GAMES(I0,WEEK_#),
             BOWLER.WEEK.AVG(I0,WEEK_#),
             HDCP(BOWLER.WEEK.AVG(I0,WEEK_#)))
                  (SKIP(0),COL(1),A,COL(30),F(3,0),COL(39),F(3,0),
             COL(47),F(3,0));
       IF BOWLER.GAME21_AVG(I0) > 0 THEN
         PUT FILE(SMSUMRY)
                 EDIT(BOWLER.GAME21_AVG(I0),
               BOWLER.WEEK.AVG(I0,WEEK_#)-BOWLER.GAME21_AVG(I0))
                     (COL(56),F(3,0),COL(64),F(4,0));
     END;
   END;

   PUT FILE(SMINDIV) EDIT('`+X') (SKIP,COL(1),A);
   CLOSE FILE(SMINDIV);

   PUT FILE(SMSUMRY) EDIT('`+X') (SKIP,COL(1),A);
   CLOSE FILE(SMSUMRY);

  /*******************************************************************
   *  Put the Xerox 4045 printer into portrait mode.                *
    ******************************************************************/
   PUT FILE(SMTEAMS) EDIT('=UDK=`') (SKIP,COL(1),A);
   PUT FILE(SMTEAMS) EDIT('`+0Titan10iso-P') (SKIP,COL(1),A);
   PUT FILE(SMTEAMS) EDIT('`m660,15,0,30,510') (SKIP,COL(1),A);
   PUT FILE(SMTEAMS) EDIT('`0') (SKIP,COL(1),A);

   DO I0 = 1 TO #_TEAMS;
     PUT FILE(SMTEAMS) EDIT(CENTER(LEAGUE)) (PAGE,COL(1),A);
     PUT FILE(SMTEAMS) EDIT('Team Record Sheet')
                             (SKIP(2),COL(29),A);
     PUT FILE(SMTEAMS) EDIT(CENTER(TEAM.NAME(I0)))
                             (SKIP(2),COL(1),A);
     PUT FILE(SMTEAMS) EDIT('(---- Actual Pinfall ----)')
                             (SKIP(2),COL(47),A);
     PUT FILE(SMTEAMS)
           EDIT('Points','Total','Games','Total','Grand')
               (COL(25),A,COL(37),A,COL(52),A,COL(61),A,COL(68),A);
     PUT FILE(SMTEAMS)
           EDIT('Lanes','Date','Team','Won','Lost','Won','Lost',
           '1','2','3','Pins','Total')
               (COL(1),A,COL(10),A,COL(18),A,COL(24),A,COL(28),A,
           COL(35),A,COL(41),A,COL(48),A,COL(53),A,COL(58),A,
           COL(62),A,COL(68),A);
     PUT FILE(SMTEAMS)
           EDIT(REPEAT('_',71)) (SKIP(0),COL(1),A);
     TOT_PINS = 0;
     DO I1 = 1 TO WEEK_#;
       T2 = 0;
       DO I2 = 1 TO #_TEAMS;
         T1 = ABS(LANE(I1,I2));
         IF T1 = I0 THEN DO;
           T2 = FLOOR((I2-1)/2) * 2 + 1;
           PUT FILE(SMTEAMS) EDIT(T2,'-',T2+1)
                                      (COL(1),F(2,0),A,F(2,0));
         END;
       END;
       IF T2 ¬= 0 THEN DO;
         PUT FILE(SMTEAMS) EDIT(DATE(I1)) (COL(8),A);
         IF ABS(LANE(I1,T2)) = I0 THEN
           PUT FILE(SMTEAMS) EDIT(ABS(LANE(I1,T2+1)))
                                      (COL(19),F(2,0));
         ELSE
           PUT FILE(SMTEAMS) EDIT(ABS(LANE(I1,T2)))
                                      (COL(19),F(2,0));
         IF I1 ¬= 1 & I1 ¬= 17 THEN
           PUT FILE(SMTEAMS)
                    EDIT(TEAM.WEEK.WON(I0,I1)-TEAM.WEEK.WON(I0,I1-1),
                         TEAM.WEEK.LOST(I0,I1)-TEAM.WEEK.LOST(I0,I1-1))
                (COL(23),F(4,1),COL(28),F(4,1));
         ELSE
           PUT FILE(SMTEAMS)
                    EDIT(TEAM.WEEK.WON(I0,I1),TEAM.WEEK.LOST(I0,I1))
                (COL(23),F(4,1),COL(28),F(4,1));
         IF I1 <= 16 THEN
           PUT FILE(SMTEAMS)
                    EDIT(TEAM.WEEK.WON(I0,I1),TEAM.WEEK.LOST(I0,I1))
                (COL(34),F(5,1),COL(40),F(5,1));
         ELSE
           PUT FILE(SMTEAMS)
                    EDIT(TEAM.WEEK.WON(I0,I1)+TEAM.WEEK.WON(I0,16),
                         TEAM.WEEK.LOST(I0,I1)+TEAM.WEEK.LOST(I0,16))
                (COL(34),F(5,1),COL(40),F(5,1));

         SCORE.GROSS(*,*,1) = 0;
         DO I2 = 1 TO #_ACTIVE;
           B1 = TEAM.WEEK.BOWLER(I0,I1,I2);
           DO I3 = 1 TO 3;
             IF BOWLER.WEEK.GAME(B1,I1,I3) ¬= 0 THEN
               SCORE.GROSS(I2,I3,1) =BOWLER.WEEK.GAME(B1,I1,I3);
             ELSE DO;
               IF BOWLER.WEEK.WEEKS(B1,I1-1) < EST_AVG &
                                            BOWLER.LY_AVG(B1) ¬= 0 THEN
                         SCORE.GROSS(I2,I3,1) = BOWLER.LY_AVG(B1) - 10;
               ELSE DO;
                         IF BOWLER.WEEK.AVG(B1,I1-1) ¬= 0 THEN
                   SCORE.GROSS(I2,I3,1) =BOWLER.WEEK.AVG(B1,I1-1) - 10;
                         ELSE DO;
                   IF BOWLER.WEEK.AVG(B1,I1) ¬= 0 THEN
                     SCORE.GROSS(I2,I3,1) =BOWLER.WEEK.AVG(B1,I1) - 10;
                   ELSE
                     SCORE.GROSS(I2,I3,1) = BLIND - 10;
                         END;
               END;
             END;
             SCORE.GROSS(0,I3,1) = SCORE.GROSS(0,I3,1) +
                                                  SCORE.GROSS(I2,I3,1);
             SCORE.GROSS(0,0,1) = SCORE.GROSS(0,0,1) +
                                                  SCORE.GROSS(I2,I3,1);
           END;
         END;
         TOT_PINS = TOT_PINS + SCORE.GROSS(0,0,1);
         PUT FILE(SMTEAMS)
                 EDIT(SCORE.GROSS(0,1,1),SCORE.GROSS(0,2,1),
               SCORE.GROSS(0,3,1),SCORE.GROSS(0,0,1),TOT_PINS)
                     (COL(46),F(4,0),COL(51),F(4,0),COL(56),F(4,0),
               COL(62),F(4,0),COL(67),F(6,0));
       END;
     END;
     PUT FILE(SMTEAMS)
           EDIT('Team','Name','Team','Name')
               (SKIP(3),COL(13),A,COL(19),A,COL(43),A,COL(49),A);
     PUT FILE(SMTEAMS)
           EDIT('____','____','____','____')
               (SKIP(0),COL(13),A,COL(19),A,COL(43),A,COL(49),A);
     I2 = #_TEAMS / 2;
     DO I1 = 1 TO I2;
       PUT FILE(SMTEAMS)
              EDIT(I1,TEAM.NAME(I1),I1+I2,TEAM.NAME(I1+I2))
                  (COL(14),F(2,0),COL(19),A,COL(44),F(2,0),COL(49),A);
     END;
   END;

   PUT FILE(SMTEAMS) EDIT('`+X') (SKIP,COL(1),A);

   CLOSE FILE(SMTEAMS);

  /*******************************************************************
   *  Put the Xerox 4045 printer into portrait mode.                *
    ******************************************************************/
   PUT FILE(SMPRIZE) EDIT('=UDK=`') (SKIP,COL(1),A);
   PUT FILE(SMPRIZE) EDIT('`+0Titan10iso-P') (SKIP,COL(1),A);
   PUT FILE(SMPRIZE) EDIT('`m660,15,0,30,510') (SKIP,COL(1),A);
   PUT FILE(SMPRIZE) EDIT('`0') (SKIP,COL(1),A);

   PUT FILE(SMPRIZE) EDIT('`+X') (SKIP,COL(1),A);

   CLOSE FILE(SMPRIZE);

 HDCP: PROC(Z0) RETURNS(FIXED BIN(15)) REORDER;

   DECLARE
     Z0                      FIXED BIN(15);

   IF Z0 = 0 | Z0 > HDCP_BASIS THEN
     RETURN(0);
   ELSE
     RETURN((HDCP_BASIS - Z0) * HDCP_PCT / 100);

 END HDCP;

 TRIM: PROC(Z0) RETURNS(CHAR(72) VAR) REORDER;

   DECLARE
     Z0                      CHAR(72) VAR,
     Z1                      FIXED BIN(15);

   Z1 = LENGTH(Z0);
   DO WHILE (SUBSTR(Z0,Z1,1) = ' ' & Z1 >= 0);
     Z1 = Z1 - 1;
   END;
   RETURN(SUBSTR(Z0,1,Z1));

 END TRIM;

 CENTER: PROC(Z0) RETURNS(CHAR(72) VAR) REORDER;

   DECLARE
     Z0                      CHAR(72) VAR,
     Z1                      FIXED BIN(15);

   Z1 = 36 - (LENGTH(TRIM(Z0))/2);
   RETURN(REPEAT(' ',Z1-1) || TRIM(Z0));

 END CENTER;

 DIGITS: PROC(Z0) RETURNS(CHAR(72) VAR) REORDER;

   DECLARE
     Z0                      FIXED BIN(15),
     Z1                      CHAR(72) VAR;

   IF Z0 = 0 THEN
     RETURN('0');
   ELSE DO;
     Z1 = '';
     DO WHILE (Z0 > 0);
       Z1 = DIGIT(MOD(Z0,10)) || Z1;
       Z0 = Z0 / 10;
     END;
     RETURN(Z1);
   END;

 END DIGITS;

 END SMLSTAT;
