         MACRO
&NAME    VPUT  &M,&MF=I,&MLOC=
.*
.**********************************************************************
.*                                                                    *
.*  COPYRIGHT (C) 1979 BY BROIDO COMPUTER CONSULTING.  ALL RIGHTS     *
.*  RESERVED EXCEPT:                                                  *
.*                                                                    *
.*  PARTICULAR LICENSE IS GRANTED TO DISTRIBUTE THIS MACRO FREE       *
.*  OF CHARGE, BUT IT MUST NOT BE SOLD.  A SERVICE CHARGE FOR         *
.*  HANDLING A COLLECTION OF SOFTWARE OF WHICH THIS IS A PART,        *
.*  ON THE OTHER HAND, IS APPROPRIATE.                                *
.*                                                                    *
.*                                  BROIDO COMPUTER CONSULTING        *
.*                                  BROIDOJ@GMAIL.COM                 *
.*                                                                    *
.**********************************************************************
.*
         LCLB  &PAIR
         LCLA  &LEN,&I
&LEN     SETA  K'&M+2
&PAIR    SETB  0
&I       SETA  1
.LOOP    ANOP
&I       SETA  &I+1+&PAIR
         AIF   (&I GE K'&M).DONE
&PAIR    SETB  ('&M'(&I,2) EQ '''''' OR '&M'(&I,2) EQ '&&')
&LEN     SETA  &LEN-&PAIR
         AGO   .LOOP
.DONE    ANOP
         AIF   ('&MF' EQ 'L').VL
&NAME    LA    1,SYSPRINT          LOAD DCB ADDRESS.
         BAL   0,VPUT&SYSNDX       LOAD MSG ADDRESS
         DC    AL2(&LEN),AL2(0)    RCW - LENGTH OF MESSAGE.
         AIF   (&LEN LE 4).NOMSG
&MLOC    DC    C&M
.NOMSG   ANOP
VPUT&SYSNDX PUT (1),(0)
         MEXIT
.VL      ANOP
&NAME    DC    AL2(&LEN),AL2(0)    RCW - LENGTH OF MESSAGE.
&MLOC    DC    C&M
         MEND
