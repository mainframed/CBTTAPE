CKO      TITLE 'A CLIST UTILITY TO DETERMINE USER''S PRIVELEGE.'
*%A AMODGEN PPLINK
***********************************************************************
*                                                                     *
*  COPYRIGHT (C) 1984 BY BROIDO COMPUTER CONSULTING.  ALL RIGHTS      *
*  RESERVED EXCEPT:                                                   *
*                                                                     *
*  PARTICULAR LICENSE IS GRANTED TO DISTRIBUTE THIS PROGRAM FREE      *
*  OF CHARGE, BUT IT MUST NOT BE SOLD.  A SERVICE CHARGE FOR          *
*  HANDLING A COLLECTION OF SOFTWARE OF WHICH THIS IS A PART,         *
*  ON THE OTHER HAND, IS APPROPRIATE.                                 *
*                                                                     *
*                                  BROIDO COMPUTER CONSULTING         *
*                                  BROIDOJ@GMAIL.COM                  *
*                                                                     *
***********************************************************************
         SPACE 1
***********************************************************************
*                                                                     *
*   CKO - A TSO CLIST UTILITY TO RETURN IN THE CONDITION CODE         *
*       (&LASTCC) THE USER'S PRIVILEGES.                              *
*                                                                     *
*      &LASTCC = 0  -  USER IS NEITHER OPER NOR ACCT PRIVILEGED.      *
*      &LASTCC = 4  -  USER IS OPER BUT NOT ACCT PRIVILEGED.          *
*      &LASTCC = 8  -  USER IS BOTH OPER AND ACCT PRIVILEGED.         *
*                                                                     *
*                                  JEFFREY R. BROIDO                  *
*                                  TWIN COUNTY GROCERS, INC           *
*                                  145 TALMADGE ROAD                  *
*                                  EDISON, NJ 08818                   *
*                                  19 SEPTEMBER 1984                  *
*                                                                     *
***********************************************************************
         SPACE 1
CKO      CSECT
         REGS
         USING CKO,R15             (ESTABLISH TEMPORARY BASE).
         B     BEGIN               SKIP OVER HEADER.
HEAD0    DC    AL1(HEADL)          EXCLUSIVE LENGTH OF HEADER.
HEAD     DC    C' CKO - &SYSDATE - &SYSTIME -'
         DC    C'COPYRIGHT (C) BROIDO COMPUTER CONSULTING '
         DC    C'1984 - '
         DC    C'ALL RIGHTS RESERVED'
HEADL    EQU   *-HEAD
         DROP  R15                 (DROP TEMPORARY BASE).
BEGIN    STM   R14,R12,12(R13)     SAVE CALLER'S REGISTERS.
         LR    R12,R15             COPY PRE-LOADED BASE.
         USING CKO,R12             (TELL ASSEMBLER).
         DROP  R15                 DROP TEMPORARY BASE.
         LR    R14,R13             PRESERVE CALLER'S SAVE AREA POINTER.
         LA    R13,SAVE            POINT TO MY SAVE AREA.
         ST    R13,8(,R14)         CHAIN ...
         ST    R14,4(,R13)         ... SAVE AREAS.
         SPACE 2
         L     R2,X'10'            POINT TO CVT.
         L     R2,0(,R2)           POINT TO TCB/ASCB WORDS.
         L     R2,0(,R2)           POINT TO CURRENT TCB.
         L     R2,X'B4'(,R2)       POINT TO JSCB.
         L     R2,X'108'(,R2)      POINT TO PSCB.
         USING PSCB,R2             TELL ASSEMBLER.
         SR    R15,R15             PRESET CC TO 0 (NO PRIVILEGES).
         TM    PSCBATR1,PSCBCTRL+PSCBACCT  TEST FOR OPER, ACCT.
         BZ    EOJ                 B IF NEITHER OPER, ACCT.
         BM    CKOPER              B IF EITHER OPER OR ACCT.
         LA    R15,8               SET CC TO 8 (OPER AND ACCT).
         B     EOJ                 CONTINUE.
CKOPER   TM    PSCBATR1,PSCBCTRL   OPER PRIVILEGED?
         BZ    EOJ                 ACCT BUT NOT OPER - EXIT W/ CC=0.
         LA    R15,4               SET CC TO 4 (OPER ONLY).
         SPACE 2
EOJ      L     R13,SAVE+4          POINT TO CALLER'S SAVE AREA.
         LM    R0,R12,20(R13)      RELOAD CALLER'S REGISTERS.
         L     R14,12(,R13)        RELOAD RETURN ADDRESS.
         BR    R14                 RETURN TO CALLER.
         SPACE 2
SAVE     DS    9D                  SAVE AREA.
         TITLE 'CONTROL BLOCK DSECTS.'
         IKJPSCB
         END
