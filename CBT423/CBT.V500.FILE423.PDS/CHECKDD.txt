CHECKDD  TITLE 'CHECKDD - CLIST UTILITY TO CHECK FOR LIST OF ALLOCATED +
               FILENAMES.'
*%A PPLINK NOTEST
         SPACE 1
***********************************************************************
*                                                                     *
*  COPYRIGHT (C) 1979 BY BROIDO COMPUTER CONSULTING.  ALL RIGHTS      *
*  RESERVED EXCEPT:                                                   *
*                                                                     *
*  PARTICULAR LICENSE IS GRANTED TO DISTRIBUTE THIS PROGRAM FREE      *
*  OF CHARGE, BUT IT MUST NOT BE SOLD.  A SERVICE CHARGE FOR          *
*  HANDLING A COLLECTION OF SOFTWARE OF WHICH THIS IS A PART,         *
*  ON THE OTHER HAND, IS APPROPRIATE.                                 *
*                                                                     *
*                                  BROIDO COMPUTER CONSULTING         *
*                                  broidoj@gmail.com                  *
*                                                                     *
***********************************************************************
         SPACE 1
***********************************************************************
*                                                                     *
*  CLIST UTILITY TO CHECK FOR ALLOCATION OF LIST OF FILE NAMES.       *
*                                                                     *
*    CHECKDD ...ddname list...                                        *
*                                                                     *
*      If all the DDNAMEs in the list are presently allocated, the    *
*      command will return a condition code of 0 (&LASTCC=0).   If    *
*      any of the DDNAMEs are missing,  it will return a condition    *
*      code of 4 (&lastcc=4).                                         *
*                                                                     *
*                                  JEFFREY R. BROIDO                  *
*                                  J & J, MIC                         *
*                                  1979                               *
*                                                                     *
***********************************************************************
         SPACE 1
CHECKDD  CSECT
         REGS
         USING CHECKDD,R15         (ESTABLISH TEMPORARY BASE).
         B     BEGIN               SKIP OVER HEADER.
HEAD0    DC    AL1(HEADL)          EXCLUSIVE LENGTH OF HEADER.
HEAD     DC    C'CHECKDD - &SYSDATE - &SYSTIME -'
         DC    C'COPYRIGHT (C) BROIDO COMPUTER CONSULTING '
         DC    C'1979 - '
         DC    C'ALL RIGHTS RESERVED'
HEADL    EQU   *-HEAD
         DROP  R15                 (DROP TEMPORARY BASE).
BEGIN    STM   R14,R12,12(R13)
         USING CHECKDD,R12
         USING CPPL,R3
         USING DDDSECT,R5
         LR    R12,R15
         LA    R2,SAVE
         ST    R2,8(,R13)
         ST    R13,SAVE+4
         LR    R13,R2
         SPACE 1
         LR    R3,R1               SAVE CPPL PTR.
         L     R1,0(,R1)           POINT TO COMMAND BUFFER.
         ST    R1,PPLCBUF          SAVE IT IN PPL.
         LTR   R1,R1               IS POINTER 0?
         BZ    NOTCMD              B IF YES.  NOT A COMMAND.  ERROR.
         LH    R2,0(,R1)           LOAD LENGTH.
         CH    R2,=H'4'            IS LENGTH IMPOSSIBLE?
         BZ    NOTCMD              B IF YES.  NOT A COMMAND.  ERROR.
         CLI   2(R1),X'00'         PROPER OFFSET?
         BE    ISCMD               B IF YES.  OK.
NOTCMD   VTPUT 'CHECKDD MUST BE EXECUTED AS A COMMAND.'
EXIT8    LA    R15,8               LOAD CC OF 8.
         B     EXIT1               CONTINUE.
EXIT     LR    R9,R15
         IKJRLSA ANSPLACE          FREEMAIN CORE.
         LR    R15,R9
EXIT1    L     R13,SAVE+4
         LM    R0,R12,20(R13)
         L     R14,12(,R13)
         BR    R14
         SPACE 1
ISCMD    MVC   PPLUPT,CPPLUPT      MOVE UPT POINTER.
         MVC   PPLECT,CPPLECT      MOVE ECT POINTER.
         LA    R1,PPL              POINT TO PPL.
         LINK  EP=IKJPARS          PARSE INPUT.
         L     R5,ANSPLACE         POINT TO ANSWER PLACE.
         LA    R2,DDNAMES          POINT TO DDNAMES.
         L     R4,16               POINT TO CVT.
         L     R4,0(,R4)           POINT TO TCB WORDS.
         L     R4,0(,R4)           POINT TO TCB.
         L     R4,12(,R4)          POINT TO TIOT.
         LA    R4,24(,R4)          POINT TO FIRST DD ENTRY.
         SR    R10,R10             CLEAR WORK REG.
DDMAJLUP TM    6(R2),X'80'         PARM PRESENT?
         BZ    MAJBUMP             B IF NOT.
         L     R6,0(,R2)           POINT TO DDNAME TO BE FOUND.
         LH    R1,4(,R2)           LOAD LENGTH OF THIS DDNAME.
         BCTR  R1,*-*              CREATE SS LENGTH.
         MVC   DDAREA,=CL8' '      CLEAR DDNAME AREA.
         EX    R1,DDNMOVE          MOVE DDNAME.
         LR    R7,R4               POINT TO FIRST TIOT DD ENTRY.
DDMINLUP IC    R10,0(,R7)          LOAD ENTRY LENGTH.
         LTR   R10,R10             END OF TIOT?
         BNZ   DDPOS               B IF NOT.
         LA    R15,4               SET CC OF 4.  A DDNAME NOT FOUND.
         B     EXIT                BYE.
DDPOS    CLC   DDAREA,4(R7)        COMPARE DDNAME.
         BE    MAJBUMP             B IF EQUAL.  FOUND THIS ONE.
         LA    R7,0(R10,R7)        POINT TO NEXT DD ENTRY IN TIOT.
         B     DDMINLUP            LOOP THROUGH TIOT.
MAJBUMP  L     R2,8(,R2)           POINT TO NEXT PARSE LIST ENTRY.
         LA    R2,0(,R2)           CLEAR HIGH BYTE.
         LTR   R2,R2               ANY MORE LIST ENTRIES?
         BNZ   DDMAJLUP            B IF YES.
         SR    R15,R15             CLEAR CC.  FOUND ALL DDNAMES.
         B     EXIT                BYE.
         SPACE 3
SAVE     DS    9D
DDNMOVE  MVC   DDAREA(*-*),0(R6)   EXECUTED.
DDAREA   DS    CL8                 DDNAME AREA FOR COMPARES.
PPL      DS    0A                  PARSE PARM LIST.
PPLUPT   DC    A(0)
PPLECT   DC    A(0)
PPLECB   DC    A(CPECB)
PPLPCL   DC    A(DDPCL)
PPLANS   DC    A(ANSPLACE)
PPLCBUF  DC    A(0)
PPLUWA   DC    A(0)
         SPACE 1
CPECB    DC    A(0)                ECB FOR PARSE.
ANSPLACE DC    A(0)                PARSE ANSWER PLACE
         PRINT NOGEN
DDPCL    IKJPARM DSECT=DDDSECT
DDNAMES  IKJIDENT 'DDNAME',LIST,MAXLNTH=8,OTHER=ALPHANUM,              X
               PROMPT='DDNAME LIST +',HELP='LIST OF DDNAMES YOU WISH TOX
                CHECK.'
         IKJENDP
         IKJCPPL
         END
