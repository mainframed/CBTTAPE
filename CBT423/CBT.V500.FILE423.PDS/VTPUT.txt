         MACRO
&NAME    VTPUT &M,&MLAB,&MF=I
.*
.**********************************************************************
.*                                                                    *
.*  COPYRIGHT (C) 1979 BY BROIDO COMPUTER CONSULTING.  ALL RIGHTS     *
.*  RESERVED EXCEPT:                                                  *
.*                                                                    *
.*  PARTICULAR LICENSE IS GRANTED TO DISTRIBUTE THIS MACRO FREE       *
.*  OF CHARGE, BUT IT MUST NOT BE SOLD.  A SERVICE CHARGE FOR         *
.*  HANDLING A COLLECTION OF SOFTWARE OF WHICH THIS IS A PART,        *
.*  ON THE OTHER HAND, IS APPROPRIATE.                                *
.*                                                                    *
.*                                  BROIDO COMPUTER CONSULTING        *
.*                                  BROIDOJ@GMAIL.COM                 *
.*                                                                    *
.**********************************************************************
.*
         LCLA  &LEN,&I
         LCLB  &PAIR
&LEN     SETA  K'&M-2
&PAIR    SETB  0
&I       SETA  1
.QLOOPH  ANOP
&I       SETA  &I+1+&PAIR
         AIF   (&I GE K'&M).QDONEH
&PAIR    SETB  ('&M'(&I,2) EQ '''''' OR '&M'(&I,2) EQ '&&')
&LEN     SETA  &LEN-&PAIR
         AGO   .QLOOPH
.QDONEH  ANOP
         AIF   ('&MF' EQ 'E').E
         AIF   ('&MF' EQ 'L').L
&NAME    BAL   1,V&SYSNDX          POINT TO MESSAGE.
&MLAB    DC    C&M                 MESSAGE.
         DS    0H
V&SYSNDX LA    0,&LEN              LOAD LENGTH.
         LA    1,0(,1)             CLEAR OPTION BYTE.
         SVC   93                  ISSUE TPUT/TGET SVC.
         MEXIT
.E       ANOP
&NAME    SR    0,0                 LOAD ...
         ICM   0,3,&M              ... LENGTH.
         LA    1,&M+2              POINT TO MESSAGE.
         SVC   93                  ISSUE TPUT/TGET SVC.
         MEXIT
.L       AIF   (&LEN EQ 0).LZ
&NAME    DC    AL2(&LEN),C&M       MESSAGE.
         MEXIT
.LZ      ANOP
&NAME    DC    AL2(0)              EMPTY MESSAGE.
         MEND
