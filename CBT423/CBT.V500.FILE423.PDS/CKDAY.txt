CKDAY    TITLE 'CLIST UTILITY TO DETECT THE FIRST LOGON OF THE DAY.'
*%A NOTEST PPLINK
*LALIAS CLEARDAY
*LNAME CKDAY(R)
         SPACE 1
***********************************************************************
*                                                                     *
*  COPYRIGHT (C) 1982 BY BROIDO COMPUTER CONSULTING.  ALL RIGHTS      *
*  RESERVED EXCEPT:                                                   *
*                                                                     *
*  PARTICULAR LICENSE IS GRANTED TO DISTRIBUTE THIS PROGRAM FREE      *
*  OF CHARGE, BUT IT MUST NOT BE SOLD.  A SERVICE CHARGE FOR          *
*  HANDLING A COLLECTION OF SOFTWARE OF WHICH THIS IS A PART,         *
*  ON THE OTHER HAND, IS APPROPRIATE.                                 *
*                                                                     *
*                                  BROIDO COMPUTER CONSULTING         *
*                                  BROIDOJ@GMAIL.COM                  *
*                                                                     *
***********************************************************************
         SPACE 1
***********************************************************************
*                                                                     *
* CKDAY - CLIST UTILITY TO DETECT THE FIRST LOGON OF THE DAY, ETC.    *
*                                                                     *
*  RETURNS CC OF 0 IF THIS COMMAND HAS BEEN EXECUTED ALREADY TODAY.   *
*                4 IF COMMAND HAS BEEN EXECUTED, BUT NOT YET TODAY.   *
*                8 IF COMMAND HAS NEVER BEEN EXECUTED.                *
*               12 IF COMMAND CANNOT SAVE DATE BECAUSE UPTUSER        *
*                  IS IN USE BY SOME OTHER PIECE OF SOFTWARE.         *
*               16 IF COMMAND NOT RUN UNDER TSO.                      *
*                                                                     *
*  ENTRY POINT CLEARDAY SETS THE FIRST 6 BYTES OF UPTUSER TO X'00'    *
*  CLEARDAY MUST BE ASSIGNED AS AN ALIAS.                             *
*                                                                     *
*                                  JEFFREY  R.  BROIDO                *
*                                  AIGDC-E. ORANGE, NJ                *
*                                  3  SEPTEMBER   1982                *
*                                                                     *
***********************************************************************
         SPACE 1
CKDAY    CSECT
         ENTRY CLEARDAY
         REGS
         USING CKDAY,R15
         B     CKDAY0              SKIP HEADER.
HEAD0    DC    AL1(HEADL)          EXCLUSIVE LENGTH OF HEADER.
HEAD     DC    C' CKDAY COPYRIGHT (C) BROIDO COMPUTER CONSULTING '
         DC    C'1982 - '
         DC    C'ALL RIGHTS RESERVED'
HEADL    EQU   *-HEAD
         USING CLEARDAY,R15
CLEARDAY STM   R14,R12,12(R13)
         SR    R11,R11             CLEAR R11 (ENTRY FLAG).
         LR    R12,R15
         SH    R12,=AL2(CLEARDAY-CKDAY) SUBTRACT EP OFFSET.
         B     CKDAY1              CONTINUE
         LTORG
         DROP  R15
CKDAY0   STM   R14,R12,12(R13)
         LA    R11,1               SET R11 TO 1 (ENTRY FLAG).
         LR    R12,R15
         USING CKDAY,R12
CKDAY1   LR    R14,R13
         LA    R13,SAVE
         ST    R13,8(,R14)
         ST    R14,SAVE+4
         SPACE 2
         L     R2,X'10'            POINT TO CVT.
         ED    DATE,X'39'(R2)      UNPACK SYSTEM DATE.
         L     R2,0(,R2)           POINT TO TCB/ASCB WORDS.
         L     R3,X'C'(,R2)        POINT TO ASCB.
         CLC   =A(0),X'3C'(R3)     ANY TSB?
         BNE   ONLINE              B IF NOT.
         LA    R15,16              SET CC OF 16.  NOT UNDER TSO.
         B     EOJ                 BYE.
ONLINE   L     R3,0(,R2)           POINT TO TCB.
         L     R3,X'B4'(,R3)       POINT TO JSCB.
         L     R3,X'108'(,R3)      POINT TO PSCB.
         L     R3,X'34'(,R3)       POINT TO UPT.
         CLC   =XL6'00',2(R3)      FIRST 6 BYTES OF UPTUSER ZERO?
         BNE   NOTZERO             B IF NOT.
         LA    R15,8               SET CC OF 8.  COMMAND NEVER EXEC.
         LTR   R11,R11             CLEARDAY?
         BZ    EOJ                 B IF YES.
         MVC   2(6,R3),DATE+1      MOVE TODAY'S DATE TO UPT.
         B     EOJ                 BYE.
NOTZERO  MVZ   ZONETEST,2(R3)      MOVE ZONES.
         CLC   ZONETEST,=C'00 000' FORMAT CHECK OUT?
         BE    CKPD                B IF YES.
         LA    R15,12              SET CC OF 12.  UPTUSER IN USE.
         B     EOJ                 BYE.
CKPD     CLI   4(R3),C'.'          ...BUT IS 3RD BYTE A PERIOD?
         BE    CKENTRY             B IF YES.
         LA    R15,12              SET CC OF 12.  UPTUSER IN USE.
         B     EOJ                 BYE.
CKENTRY  LTR   R11,R11             CHECK ENTRY POINT FLAG.
         BNZ   CKTODAY             B IF CKDAY ENTRY.
         MVC   2(6,R3),=XL6'00'    CLEAR FIRST 6 BYTES OF UPTUSER.
         B     EOJ0                BYE.
CKTODAY  CLC   2(6,R3),DATE+1      COMPARE IT TO DATE IN UPT.
         BE    EOJ0                B IF THEY ARE THE SAME.
         LA    R15,4               SET CC OF 4.  CMD NOT EXEC TODAY.
         MVC   2(6,R3),DATE+1      MOVE TODAY'S DATE TO UPT.
         B     EOJ                 BYE.
EOJ0     SR    R15,R15             SET CC OF 0.  CMD EXEC TODAY.
EOJ      L     R13,SAVE+4
         LM    R0,R12,20(R13)
         L     R14,12(,R13)
         BR    R14
         SPACE 2
SAVE     DS    18F
ZONETEST DC    CL6' '              ZONE TEST COMPARAND AREA.
DATE     DC    X'4021204B202020'   DATE UNPACK AREA.
         END
LINKEDIT TITLE 'CKDAY - LINKAGE EDITOR CONTROL CARDS.'
         PUNCH ' ALIAS CLEARDAY'
         END
