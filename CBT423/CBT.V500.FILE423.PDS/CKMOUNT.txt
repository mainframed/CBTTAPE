CKMOUNT  TITLE 'A CLIST UTILITY TO DETERMINE USER''S MOUNT PRIVELEGE.'
*%A AMODGEN PPLINK
***********************************************************************
*                                                                     *
*  COPYRIGHT (C) 1984 BY BROIDO COMPUTER CONSULTING.  ALL RIGHTS      *
*  RESERVED EXCEPT:                                                   *
*                                                                     *
*  PARTICULAR LICENSE IS GRANTED TO DISTRIBUTE THIS PROGRAM FREE      *
*  OF CHARGE, BUT IT MUST NOT BE SOLD.  A SERVICE CHARGE FOR          *
*  HANDLING A COLLECTION OF SOFTWARE OF WHICH THIS IS A PART,         *
*  ON THE OTHER HAND, IS APPROPRIATE.                                 *
*                                                                     *
*                                  BROIDO COMPUTER CONSULTING         *
*                                  BROIDOJ@GMAIL.COM                  *
*                                                                     *
***********************************************************************
         SPACE 1
***********************************************************************
*                                                                     *
*   CKMOUNT - A TSO CLIST UTILITY TO RETURN IN THE CONDITION CODE     *
*       (&LASTCC) THE USER'S MOUNT PRIVELEGES.                        *
*                                                                     *
*      &LASTCC = 0  -  USER IS NOT MOUNT PRIVELEGED.                  *
*      &LASTCC = 4  -  USER IS MOUNT PRIVELEGED.                      *
*                                                                     *
*                                  JEFFREY R. BROIDO                  *
*                                  TWIN COUNTY GROCERS, INC           *
*                                  145 TALMADGE ROAD                  *
*                                  EDISON, NJ 08818                   *
*                                  19 SEPTEMBER 1984                  *
*                                                                     *
***********************************************************************
         SPACE 1
CKMOUNT  CSECT
CKMOUNT  AMODE 24
CKMOUNT  RMODE 24
         REGS
         USING CKMOUNT,R15
         B     BEGIN               SKIP OVER HEADER.
         DC    AL1(HEADEND-HEAD)   HEADER LENGTH.
HEAD     DC    C' CKMOUNT - &SYSDATE &SYSTIME - '
         DC    C'COPYRIGHT (C) BROIDO COMPUTER CONSULTING '
         DC    C'1985 - ALL RIGHTS RESERVED'
HEADEND  EQU   *
BEGIN    STM   R14,R12,12(R13)     SAVE CALLER'S REGISTERS.
         LR    R12,R15             COPY PRE-LOADED BASE.
         USING CKMOUNT,R12         (TELL ASSEMBLER).
         DROP  R15                 DROP TEMPORARY BASE.
         LR    R14,R13             PRESERVE CALLER'S SAVE AREA POINTER.
         LA    R13,SAVE            POINT TO MY SAVE AREA.
         ST    R13,8(,R14)         CHAIN ...
         ST    R14,4(,R13)         ... SAVE AREAS.
         SPACE 2
         L     R2,X'10'            POINT TO CVT.
         L     R2,0(,R2)           POINT TO TCB/ASCB WORDS.
         L     R2,0(,R2)           POINT TO CURRENT TCB.
         L     R2,X'B4'(,R2)       POINT TO JSCB.
         L     R2,X'108'(,R2)      POINT TO PSCB.
         USING PSCB,R2             TELL ASSEMBLER.
         SR    R15,R15             PRESET CC TO 0 (NO MOUNT PRIVELEGE).
         TM    PSCBATR1,PSCBVMNT   TEST FOR MOUNT PRIVELEGE.
         BZ    EOJ                 B IF NOT MOUNT PRIVELEGED.
         LA    R15,4               SET CC TO 4 (MOUNT PRIVELEGED).
         SPACE 1
EOJ      L     R13,SAVE+4          POINT TO CALLER'S SAVE AREA.
         LM    R0,R12,20(R13)      RELOAD CALLER'S REGISTERS.
         L     R14,12(,R13)        RELOAD RETURN ADDRESS.
         BR    R14                 RETURN TO CALLER.
         SPACE 2
SAVE     DS    9D                  SAVE AREA.
         TITLE 'CONTROL BLOCK DSECTS.'
         IKJPSCB
         END
