*%A PPLINK NOTEST
CKM      TITLE 'COMMAND TO CHECK PRESENCE OF MEMBER IN PDS.'
         SPACE 1
***********************************************************************
*                                                                     *
*  COPYRIGHT (C) 1978 BY BROIDO COMPUTER CONSULTING.  ALL RIGHTS      *
*  RESERVED EXCEPT:                                                   *
*                                                                     *
*  PARTICULAR LICENSE IS GRANTED TO DISTRIBUTE THIS PROGRAM FREE      *
*  OF CHARGE, BUT IT MUST NOT BE SOLD.  A SERVICE CHARGE FOR          *
*  HANDLING A COLLECTION OF SOFTWARE OF WHICH THIS IS A PART,         *
*  ON THE OTHER HAND, IS APPROPRIATE.                                 *
*                                                                     *
*                                  BROIDO COMPUTER CONSULTING         *
*                                  BROIDOJ@GMAIL.COM                  *
*                                                                     *
***********************************************************************
         SPACE 1
**********************************************************************
*                                                                    *
*      CKM - COMMAND TO CHECK PRESENCE OF MEMBER IN PDS.             *
*                                                                    *
*        USE: 1) ALLOCATE THE PDS, SPECIFING MEMBER.  USE FILENAME   *
*                OF SYSUT9 IF YOU TAKE THE COMMAND DEFAULT.          *
*                                                                    *
*             2) CKM ..DDNAME.. WHERE DDNAME IS OPTIONAL,            *
*                AND DEFAULTS TO SYSUT9 IF NOT SPECIFIED.  IF        *
*                DDNAME IS OVER 8 CHARACTERS, DEFAULT IS TAKEN.      *
*                                                                    *
*             3) CHECK &LASTCC.  IF 0, MEMBER IS PRESENT.            *
*                                   4, MEMBER IS NOT IN DATASET.     *
*                                   8, NO MEMBER SPECIFIED.          *
*                                  16, DATASET IS NOT PARTITIONED.   *
*                                  20, DATASET NOT FOUND ON VOLUME.  *
*                                  24, SYSUT9 (OR YOUR OWN DDNAME)   *
*                                      NOT ALLOCATED.                *
*                                                                    *
*                                     JEFFREY R. BROIDO              *
*                                     27 OCT 78                      *
*                                                                    *
**********************************************************************
         SPACE 3
CKM      CSECT
         REGS
         EJECT
         STM   R14,R12,12(R13)
         LR    R12,R15
         USING CKM,R12
         LA    R2,SAVE
         ST    R2,8(,R13)
         ST    R13,SAVE+4
         LR    R13,R2
         SPACE 2
         L     R2,0(,R1)           POINT TO BUFFER.
         LH    R4,0(,R2)           LOAD LENGTH.
         LTR   R4,R4               POSSIBLE NULL BATCH PARM?
         BZ    CONT                B IF YES.  USE DDNAME OF SYSUT9.
         CLI   2(R2),0             TSO COMMAND BUFFER?
         BNE   CONT                B IF NOT.  USE DDNAME OF SYSUT9.
         LH    R3,2(,R2)           LOAD OFFSET.
         LA    R3,4(,R3)           MAKE OFFSET ABSOLUTE.
         AR    R2,R3               POINT TO PARM.
         CLI   0(R2),C' '          ANY PARM?
         BE    CONT                B IF NOT.
         SR    R4,R3               GENERATE LENGTH ...
         BCTR  R4,*-*              ... OF DDNAME
         CH    R4,=H'7'            GREATER THAN 7?
         BH    CONT                B IF YES. USE SYSUT9.
         MVC   SYSUT9+41(7),=CL8' '     CLEAR DDNAME.
         EX    R4,DDMOVE           MOVE DDNAME TO DCB.
         OC    SYSUT9+40(8),=CL8' '     UPPERCASE DDNAME.
CONT     SR    R3,R3               CLEAR CC.
         RDJFCB (SYSUT9)           GET JFCB.
POSTR    LTR   R15,R15             SYSUT9 EXIST?
         BZ    CONT0               B IF YES.
         LA    R3,24               SET CC OF 24.
         B     EXIT                EXIT.
CONT0    MVC   VOL,JFCB+118        MOVE VOLUME NAME FOR OBTAIN.
         OBTAIN OBCAM              OBTAIN TO CKECK FOR DATA SET.
         LTR   R15,R15             DATA SET EXIST?
         BZ    CONT1               B IF YES.
         LA    R3,20               SET CC OF 20.
         B     EXIT                EXIT.
CONT1    TM    OBWORK+38,X'02'     CHECK OBTAINED DSCB FOR DSORG.
         BO    CONT2               B IF PARTITIONED.
         LA    R3,16               SET CC OF 16.
         B     EXIT                EXIT.
CONT2    TM    JFCB+86,X'01'       MEMBER SPECIFIED?
         BO    CONT3               B IF YES
         LA    R3,8                SET CC OF 8.
         B     EXIT                EXIT
CONT3    MVC   BLDLMEM,JFCB+44     MOVE MEMBER NAME.
         OPEN  (SYSUT9),TYPE=J     OPEN DATASET.
         BLDL  SYSUT9,BL           BLDL MEMBER.
         LR    R4,R15              SAVE CC.
         CLOSE  (SYSUT9)           CLOSE DATASET
         LTR   R4,R4               MEMBER FOUND?
         BZ    EXIT                B IF YES. EVERYTHING OK.
         LA    R3,4                SET CC OF 4.
EXIT     L     R13,SAVE+4
         L     R14,12(,R13)        LOAD RETURN ADDR.
         LR    R15,R3              SET CC.
         LM    R0,R12,20(R13)
         BR    R14
         SPACE 2
SAVE     DS    9D
OBCAM    CAMLST SEARCH,JFCB,VOL,OBWORK
DDMOVE   MVC   SYSUT9+40(*-*),0(R2) EXECUTED.
VOL      DS    CL6
OBWORK   DS    CL140
BL       DC    H'1',H'12'
BLDLMEM  DS    CL8,XL4
RJ       DS    0A
         DC    X'07',AL3(JFCB)
JFCB     DS    CL176
         PRINT NOGEN
SYSUT9   DCB   DDNAME=SYSUT9,EXLST=RJ,DSORG=PS,MACRF=(E)
         END   CKM
