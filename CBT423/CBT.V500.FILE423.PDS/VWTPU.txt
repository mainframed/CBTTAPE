         MACRO
&NAME    VWTPU &MM,&MLAB1,&MF=I,&MODE=
.*
.**********************************************************************
.*                                                                    *
.*  COPYRIGHT (C) 1979 BY BROIDO COMPUTER CONSULTING.  ALL RIGHTS     *
.*  RESERVED EXCEPT:                                                  *
.*                                                                    *
.*  PARTICULAR LICENSE IS GRANTED TO DISTRIBUTE THIS MACRO FREE       *
.*  OF CHARGE, BUT IT MUST NOT BE SOLD.  A SERVICE CHARGE FOR         *
.*  HANDLING A COLLECTION OF SOFTWARE OF WHICH THIS IS A PART,        *
.*  ON THE OTHER HAND, IS APPROPRIATE.                                *
.*                                                                    *
.*                                  BROIDO COMPUTER CONSULTING        *
.*                                  broidoj@gmail.com                 *
.*                                                                    *
.**********************************************************************
.*
         LCLA  &LEN,&I,&J,&LEN1
         LCLB  &PAIR
         LCLC  &M1,&M2,&U,&L,&MLAB,&X,&M
&M       SETC  '&MM'
&MLAB    SETC  '&MLAB1'
&U       SETC  'AEIOUBCDFGHJKLMNPQRSTVWXYZ'
&L       SETC  'aeioubcdfghjklmnpqrstvwxyz'
&LEN     SETA  K'&M-2
         AIF   ('&MF' EQ 'E').QEX
         AIF   ('&MODE' NE 'FOLD' OR '&MLAB' NE '').QCONT8
&MLAB    SETC  'V&SYSNDX.E'
.QCONT8  ANOP
&PAIR    SETB  0
&I       SETA  1
&LEN1    SETA  &LEN+2
.QLOOPH  ANOP
&I       SETA  &I+1+&PAIR
         AIF   (&I GE K'&M).QDONEH
&PAIR    SETB  ('&M'(&I,2) EQ '''''' OR '&M'(&I,2) EQ '&&')
&LEN     SETA  &LEN-&PAIR
         AGO   .QLOOPH
.QDONEH  ANOP
         AIF   ('&MF' EQ 'L').QPARM
&NAME    CNOP  0,4                 FULLWORD ALIGN PARM LIST.
         BAL   1,V&SYSNDX.A        POINT TO MESSAGE.
         AGO   .QCONT
.QPARM   ANOP
&NAME    DS    0F                  FULLWORD ALIGN PARM LIST.
.QCONT   ANOP
         DC    AL2(&LEN+4),X'8000' LENGTH AND FLAGS FOR WTO.
         AIF   ('&MODE' EQ 'FOLD').MFOLD
&MLAB    DC    C&M                 MESSAGE.
         AGO   .QCONT1
.MFOLD   ANOP
&M2      SETC  ''
&J       SETA  1
.LU1     ANOP
         AIF   ('&J' GT '&LEN1').QCONT5
&I       SETA  1
.LU2     ANOP
         AIF   ('&M'(&J,1) NE '&L'(&I,1)).BU2
&M2      SETC  '&M2'.'&U'(&I,1)
         AGO   .BU1
.BU2     ANOP
&I       SETA  &I+1
         AIF   (&I LE 26).LU2
&M2      SETC  '&M2'.'&M'(&J,1)
.BU1     ANOP
&J       SETA  &J+1
         AGO   .LU1
.QCONT5  ANOP
&X       SETC  'V&SYSNDX.D'
&X       DC    C&M2                UPPERCASE MESSAGE.
.QCONT1  DC    X'02000020'         ROUTCDE=11,DESC=7
         AIF   ('&MF' EQ 'L').QEND
         AIF   ('&MODE' NE 'FOLD').QCONT2
&MLAB    DC    C&M                 RAW MESSAGE.
.QCONT2  DS    0H
&X       SETC  'V&SYSNDX.A'
&X       TM    FLAG,$TSO           RUNNING UNDER TSO?
         BO    V&SYSNDX.B          B IF YES.
         SVC   35                  DO WTO (FOR WTP).
         B     V&SYSNDX.C          CONTINUE.
         AIF   ('&MODE' NE 'FOLD').QCONT6
&X       SETC  'V&SYSNDX.B'
&X       LA    1,&MLAB             POINT TO MESSAGE.
         AGO   .QCONT7
.QCONT6  ANOP
&X       SETC  'V&SYSNDX.B'
&X       LA    1,4(,1)             POINT TO MESSAGE.
.QCONT7  LA    0,&LEN              LOAD LENGTH.
         SVC   93                  TPUT SVC.
&X       SETC  'V&SYSNDX.C'
&X       EQU   *
.QEND    MEXIT
.QEX     AIF   ('&M' EQ '').QEX2
         AIF   ('&M' EQ '(1)').QEX2
         AIF   ('&M'(1,1) EQ '(').QEX1
         LA    1,&M                POINT TO PARM.
         AGO   .QEX2
.QEX1    ANOP
&M1      SETC  '&M'
         LR    1,&M1               POINT TO PARM.
.QEX2    TM    FLAG,$TSO           RUNNING UNDER TSO?
         BO    V&SYSNDX.B          B IF YES.
         SVC   35                  DO WTO (FOR WTP).
         B     V&SYSNDX.C          CONTINUE.
&X       SETC  'V&SYSNDX.B'
&X       LH    0,0(,1)             LOAD LENGTH.
         SH    0,=H'4'             CORRECT LENGTH.
         LA    1,4(,1)             POINT TO MESSAGE.
         SVC   93                  TPUT SVC.
&X       SETC  'V&SYSNDX.C'
&X       EQU   *
         MEND
