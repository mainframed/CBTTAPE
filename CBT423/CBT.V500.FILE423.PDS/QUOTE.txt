QUOTE    TITLE 'QUOTE - TSO COMMAND TO READ QUOTE-OF-THE-DAY.'
*%A PPLINK
QUOTE    CSECT
         SPACE 1
***********************************************************************
*                                                                     *
*  COPYRIGHT (C) 1982 BY BROIDO COMPUTER CONSULTING.  ALL RIGHTS      *
*  RESERVED EXCEPT:                                                   *
*                                                                     *
*  PARTICULAR LICENSE IS GRANTED TO DISTRIBUTE THIS PROGRAM FREE      *
*  OF CHARGE, BUT IT MUST NOT BE SOLD.  A SERVICE CHARGE FOR          *
*  HANDLING A COLLECTION OF SOFTWARE OF WHICH THIS IS A PART,         *
*  ON THE OTHER HAND, IS APPROPRIATE.                                 *
*                                                                     *
*                                  BROIDO COMPUTER CONSULTING         *
*                                  broidoj@gmail.com                  *
*                                                                     *
***********************************************************************
         SPACE 1
***********************************************************************
*                                                                     *
*  QUOTE - TSO COMMAND TO READ QUOTE-OF-THE-DAY WITH BACKGROUND       *
*          FROM SYS1.BRODCAST DATASET.  THE BRODCAST PROGRAM LOADS    *
*          SYS1.BRODCAST WITH THE APPROPRIATE QUOTE-OF-THE-DAY,       *
*          PLACING THE QUOTE ITSELF IN THE NOTICES AREA OF THE        *
*          DATASE AND THE BACKGROUND IN THE MAIL AREA.  IT            *
*          PLACES THE ADDRES (TTR) OF THE FIRST MAIL ("@")            *
*          RECORD IN A RESERVED FIELD IN THE BRODCAST HEADER,         *
*          WHICH THIS PROGRAM READS.                                  *
*                                                                     *
*                                  JEFFREY  R.  BROIDO                *
*                                  AIG - E. ORANGE, NJ                *
*                                  16    MARCH    1982                *
*                                                                     *
***********************************************************************
         SPACE 1
         REGS
         STM   R14,R12,12(R13)
         LR    R12,R15
         USING QUOTE,R12
         ST    R12,PPLUWA          SAVE BASE FOR PARSE VLD. CHECKER.
         LR    R14,R13
         LA    R13,SAVE
         ST    R14,4(,R13)
         ST    R13,8(,R14)
         SPACE 1
         LR    R2,R1
         USING CPPL,R2
         MVC   PPLUPT,CPPLUPT      MOVE CONTROL ...
         MVC   PPLECT,CPPLECT      ... POINTERS FROM ...
         MVC   PPLCBUF,CPPLCBUF    ... CPPL TO PPL.
         MVC   PSCB,CPPLPSCB       SAVE ADDR. OF PSCB.
         ST    R2,DFCPPLP          SAVE ADDR OF CPPL FOR DAIRFAIL.
         LA    R1,PPL              POINT TO PARSE PARM LIST.
         L     R15,16              POINT TO CVT.
         L     R15,X'20C'(,R15)    POINT TO IKJPARS.
         BALR  R14,R15             PARSE COMMAND BUFFER.
         CH    R15,=H'4'           PARSE GO OK?
         BNH   PARSEOK             B IF YES.
         VTPUT 'Unable to parse your command.  Please call a Systems Pr+
               ogrammer.'
         B     EOJ                 BYE.
PARSEOK  ON    PARSOKFG            SAY PARSE WENT OK.
         L     R6,ANSWERPL         POINT TO PDES.
         USING IKJPARMD,R6
         L     R2,16               POINT TO CVT.
         L     R2,0(,R2)           POINT TO TCB WORDS.
         L     R2,0(,R2)           POINT TO TCB.
         L     R3,X'B4'(,R2)       POINT TO JSCB.
         L     R3,X'140'(,R3)      POINT TO QDB FOR DSAB QUEUE.
         ICM   R3,7,13(R3)         POINT TO FIRST DSAB.
         BZ    ALLOCATE            B IF NO DSABS... ALLOCATE DATASET.
DSABLOOP L     R1,16(,R3)          POINT TO TIOT ENTRY FOR DDNAME.
         CLC   =C'SYSLBC ',4(R1)   IS THIS THE BRODCAST DS ALLOCATION?
         BE    OPEN                B IF YES.
         ICM   R3,7,5(R3)          POINT TO NEXT DSAB.
         BNZ   DSABLOOP            B IF NOT END OF CHAIN.
ALLOCATE LA    R1,S99RBPTR         POINT TO RB POINTER FOR DYNALLOC.
         DYNALLOC                  ALLOCATE 'SYS1.BRODCAST'
         LTR   R15,R15             DYNALLOC SUCCESSFUL?
         BZ    OPEN                B IF YES.
         ST    R15,D99RET          SAVE RETURN CODE.
         LA    R1,DFPARM           POINT TO DAIRFAIL PARMS.
         LINK  EP=IKJEFF18         ISSUE BONZO MESSAGE.
         LA    R15,16              SET CC OF 16.
         B     EOJ                 BYE.
OPEN     OPEN  (SYSLBC)            OPEN BRODCAST DATASET.
         TM    SYSLBC+48,X'10'     OPEN OK?
         BO    OPEN1
         VTPUT 'Unable to open BRODCAST dataset.  Please call Systems P+
               rogrammer.'
         LA    R15,8
         B     EOJ
OPEN1    BAL   R14,BREAD           READ HEADER.
         MVC   EPTR(9),BDATA+120   MOVE QUOTE MESSAGE POINTERS.
         SR    R3,R3               CLEAR INSERT ...
         LR    R5,R3               ... REGISTERS FOR BXLE PARMS.
         LA    R4,1                LOAD BXLE INCREMENT.
         ICM   R3,7,FQPTR          POINT TO FIRST QUOTE RECORD.
         BNZ   OPEN2               B IF THERE IS A QUOTE.
         VTPUT 'There is no Quote-of-the-Day today.'
         LA    R15,4               SET CC OF 4.
         B     EOJ                 BYE.
OPEN2    ICM   R5,7,LQPTR          POINT TO LAST QUOTE RECORD.
         GTSIZE ,                  GET TERMINAL DEPTH.
         LTR   R0,R0               IS THERE ANY DEPTH (DISPLAY)?
         BZ    OPEN3               B IF NOT.
         ON    DISPFG              TURN ON DISPLAY FLAG.
         CLI   CLEARK+1,X'01'      USER SAY "NOCLEAR?"
         BE    OPEN3               B IF YES.
         STFSMODE ON,INITIAL=YES   SET FULLSCREEN MODE.
         K     ,                   CLEAR THE SCREEN.
OPEN3    LM    R0,R1,BLN           WRITE ...
         TPUT  (1),(0)             ... TWO ...
         LM    R0,R1,BLN           ... BLANK ...
         TPUT  (1),(0)             ... LINES.
QLOOP    STCM  R3,7,BLOCK#         SAVE BLOCK # OF CURRENT QUOTE REC.
         BAL   R14,BREAD           READ CURRENT QUOTE REC.
         CLI   KEY,X'02'           THIS A PROPER NOTICE RECORD?
         BE    QLOOP0              B IT YES.
         VTPUT '"NOTICE" record expected, not found.  Please call Syste+
               ms Programmer.'
         LA    R15,16              SET CC OF 16.
         B     EOJ                 BYE.
QLOOP0   SR    R0,R0               CLEAR LENGTH REGISTER.
         ICM   R0,1,BDATA          INSERT LENGTH OF MESSAGE.
         BNZ   QLOOP1              B IF THERE IS A MESSAGE.
         LM    R0,R1,BLN           POINT TO BLANK LINE (IF 0 LEN MSG).
         B     QLOOP2              OUTPUT BLANK LINE.
QLOOP1   LA    R1,BDATA+1          POINT TO MESSAGE.
QLOOP2   TPUT  (1),(0)             OUTPUT QUOTE.
         BXLE  R3,R4,QLOOP         GET ANOTHER QUOTE RECORD.
         LM    R0,R1,BLN           OUTPUT ...
         TPUT  (1),(0)             ... BLANK LINE.
         CLI   LENGTHK+1,X'01'     USER SAY "SHORT?"
         BNE   EHEAD               B IF NO.  OUTPUT EXTENDED MSGS.
         SR    R15,R15             SET CC OF 0.
         B     EOJ                 BYE.
EHEAD    ICM   R3,7,EPTR           POINT TO FIRST EXTENDED MESSAGE.
         BNZ   ELOOP               B IF THERE ARE ANY.
         SR    R15,R15             SET CC OF 0.
         B     EOJ                 BYE.
ELOOP    STCM  R3,7,BLOCK#         SAVE BLOCK OF CURRENT EXTENDED REC.
         BAL   R14,BREAD           READ CURRENT EXTENDED RECORD.
         CLI   KEY,C'Q'            IS THIS A PROPER EXTENDED RECORD?
         BE    ELOOP0              B IF YES.
         VTPUT '"Q" record expected, not found.  Please call Systems Pr+
               ogrammer.'
         LA    R15,16              SET CC OF 16.
         B     EOJ                 BYE.
ELOOP0   SR    R4,R4               CLEAR LENGTH INSERT REGISTER.
         IC    R4,BDATA            LOAD LENGTH OF MESSAGE.
         SH    R4,=H'1'            SUBTRACT LENGTH OF FLAG CHARACTER.
         BNM   ELOOP1              B IF LENGTH AT LEAST 1.
         VTPUT '"Q" record length is 0.  Please call Systems Programmer+
               .'
         LA    R15,16              SET CC OF 16.
         B     EOJ                 BYE.
ELOOP1   CLI   BDATA+1,C'@'        NORMAL EXTENDED MESSAGE?
         BE    ELOOP2              B IF YES.
         CLI   BDATA+1,C'='        ALMOST NORMAL EXTENDED MSG (FOR OLD
*                                  QUOTE CLIST, WOULD KNOW TO COMPRESS
*                                  PAIRS OF APOSTROPHES).
         BE    ELOOP2              B IF YES.
         CLI   BDATA+1,C'#'        "MORE?" FLAG?
         BE    ELOOPM              B IF YES.
         VTPUT '"Q" record has invalid flag character.  Please call Sys+
               tems Programmer.'
         LA    R15,16              SET CC OF 16.
         B     EOJ                 BYE.
ELOOPM   CLI   PAUSEK+1,X'01'      USER SAY "NOPAUSE?"
         BE    ELOOP2              HE DID, SO DON'T ASK FOR MORE.
         LM    R0,R1,BLN           OUTPUT ...
         TPUT  (1),(0)             ... BLANK LINE.
         LM    R0,R1,MORE          POINT TO "MORE?"
         TPUT  (1),(0),ASIS        PLEASE, SIR, COULD I HAVE S'"MORE?"
         TGET  ANSWER,20           GET ANSWER.
         LTR   R1,R1               NULL LINE?
         BZ    ELOOPM1             B IF YES (USER WANTS MORE).
         TRT   ANSWER,TRTAB        FIND ANSWER.
         BZ    ELOOPM1             B IF NOTHING BUT BLANKS.
         EX    R2,COMP             IS FIRST CHARACTER "N"?
*      ( CLI   =X'02',*-*)
         BNE   ELOOPM1             B IF NOT (USER WANTS MORE).
         SR    R15,R15             SET CC OF 0.
         B     EOJ                 BYE.
ELOOPM1  LM    R0,R1,BLN           OUTPUT ...
         TPUT  (1),(0)             ... BLANK LINE.
ELOOP2   LA    R1,BDATA+2          POINT TO ACTUAL MESSAGE.
         LTR   R0,R4               EMPTY LINE?
         BNZ   ELOOP3              B IF NOT.
         LM    R0,R1,BLN           POINT TO BLANK LINE.
ELOOP3   TPUT  (1),(0)             OUTPUT EXTENDED QUOTE RECORD.
         ICM   R3,7,BDATA+126      LOAD RRN OF NEXT EXTENDED RECORD.
         BNZ   ELOOP               B IF CHAIN FIELD NON-ZERO.
         LM    R0,R1,BLN           OUTPUT BLANK ...
         TPUT  (1),(0)             ... LINE.
         SR    R15,R15             SET CC OF 0.
         B     EOJ                 BYE.
BREAD    ST    R14,SAVE1           SAVE RETURN ADDR.
         READ  BDECB,DI,SYSLBC,BDATA,'S',KEY,BLOCK#   READ A BC REC.
         CHECK BDECB               WAIT FOR IO TO COMPLETE.
         LA    R15,16              PRE-LOAD CC OF 16.
         TF    SYNADFG             IO ERROR?
         BO    EOJ                 B IF YES.
         L     R14,SAVE1           RE-LOAD RETURN ADDR.
         BR    R14                 RETURN TO CALLER.
         SPACE 1
EOJ      LR    R4,R15              SAVE CC.
         TM    SYSLBC+48,X'10'     IS DCB OPEN?
         BZ    EOJ1                B IF NOT.
         CLOSE (SYSLBC)            CLOSE FILE.
         TF    DISPFG              THIS A DISPLAY CONSOLE?
         BZ    EOJ1                B IF NOT.
         STFSMODE OFF              SET MESSAGE STREAM MODE.
EOJ1     TF    PARSOKFG            PARSE GO OK?
         BZ    EOJ2                B IF NOT.
         IKJRLSA ANSWERPL          RELEASE PARSE GETMAINED STORAGE.
EOJ2     LR    R15,R4              RE-LOAD CC.
         L     R13,SAVE+4          RELOAD  ...
         LM    R0,R12,20(R13)      ... CALLER'S ...
         L     R14,12(,R13)        ... REGISTERS.
         BR    R14                 RETURN TO CALLER.
         SPACE 1
SYNAD    SYNADAF ACSMETH=BDAM,PARM1(1)
         STM   R14,R12,12(R13)     SAVE REGS IN SYNADAF PROVIDED S.A.
         LR    R14,R13             CHAIN ...
         LA    R13,SAVE1           ...
         ST    R14,4(,R13)         ...
         ST    R13,8(,R14)         ... SAVE AREAS.
         LH    R0,0(,R1)           LOAD LENGTH OF SYNADAF MESSAGE
         SH    R0,=H'4'            SUBTRACT LENGTH OF PSEUDO RDW.
         LA    R1,4(,R1)           POINT TO MESSAGE TEXT.
         TPUT  (1),(0)             OUTPUT SYNADAF MESSAGE.
         ON    SYNADFG             TURN ON SYNAD-EXIT-ENTERED FLAG.
         L     R13,SAVE1+4
         LM    R14,R12,12(R13)
         SYNADRLS                  FREE SAVE AREA AND MESSAGE.
         BR    R14                 RETURN.
         SPACE 1
DV       STM   R14,R12,12(R13)     SAVE PARSE'S REGISTERS.
         L     R12,4(,R1)          RE-LOAD BASE ADDRESS.
         LR    R14,R13             CHAIN ...
         LA    R13,SAVE1           ... SAVE
         ST    R13,8(,R14)         ...
         ST    R14,4(,R13)         ... AREAS.
         L     R8,0(,R1)           POINT TO PDE.
         LH    R3,4(,R8)           LOAD LENGTH.
         CH    R3,=H'6'            IS IT AT LEAST 6 CHARACTERS?
         BL    DVEOJBAD            B IF NOT.
         L     R4,0(,R8)           POINT TO PARM.
         CLI   1(R4),C'/'          SECOND CHAR A SLASH?
         BNE   DV1                 B IF NOT.
         MVC   MONTH+1(1),0(R4)    MOVE ONE CHAR. MONTH.
         LA    R4,2(,R4)           POINT TO DAY.
         B     DV2
DV1      CLI   2(R4),C'/'          THIRD CHAR. A SLASH?
         BNE   DVEOJBAD            B IF NOT.
         MVC   MONTH,0(R4)         MOVE TWO CHAR. MONTH.
         LA    R4,3(,R4)           POINT TO DAY.
DV2      CLI   1(R4),C'/'          SECOND CHAR. OF DAY A SLASH?
         BNE   DV3                 B IF NOT.
         MVC   DAY+1(1),0(R4)      MOVE ONE CHAR. DAY.
         LA    R4,2(,R4)           POINT TO YEAR.
         B     DV4
DV3      CLI   2(R4),C'/'          THIRD CHAR. OF DAY A SLASH?
         BNE   DVEOJBAD            B IF NOT.
         MVC   DAY,0(R4)           MOVE TWO CHAR. DAY.
         LA    R4,3(,R4)           POINT TO YEAR.
DV4      MVC   YEAR,0(R4)          MOVE YEAR.
         MVZ   DTEST,YEAR          MOVE ZONES OF YEAR-MONTH-DAY.
         CLC   DTEST,=C'000000'    ALL NUMERIC?
         BNE   DVEOJBAD            B IF NOT.
         SR    R15,R15             SET CC OF 0 IF YES.
         B     DVEOJ               EXIT VALIDITY CHECKER.
DVEOJBAD LM    R0,R1,BLN           OUTPUT ...
         TPUT  (1),(0)             ... BLANK LINE.
         VTPUT 'Date must be of the form MM/DD/YY.'
         LA    R15,8               SET CC OF 8.
DVEOJ    L     R13,SAVE1+4
         LM    R0,R12,20(R13)
         L     R14,12(,R13)
         BR    R14
         SPACE 1
SAVE     DS    9D                  SAVE AREA
SAVE1    DS    9D                  ALTERNATE SAVE AREA.
         SPACE 1
DMEM     DC    C'Q'                ALTERNATE MEMBER NAME.
YEAR     DC    C'00'               OUTPUT ...
MONTH    DC    C'00'               ... FROM DATE VALIDITY
DAY      DC    C'00'               ... CHECKING ROUTINE.
DTEST    DC    XL6'00'             MVZ GENERATED COMPARAND.
         SPACE 1
COMP     CLI   =X'02',*-*          ** EXECUTED **
         SPACE 1
SYNADFG  EQU   *,B'10000000'       SYNAD ROUTINE ENTERED FLAG.
DISPFG   EQU   *,B'01000000'       USER AT DISPLAY TERMINAL.
PARSOKFG EQU   *,B'00100000'       PARSE WENT OK.
FLAG     DC    X'00'               VARIOUS FLAGS.
         SPACE 1
EPTR     DS    XL3                 RRN OF FIRST EXTENDED ('Q') REC.
FQPTR    DS    XL3                 RRN OF FIRST NORMAL QUOTE REC.
LQPTR    DS    XL3                 RRN OF LAST NORMAL QUOTE REC.
         SPACE 1
BLN      DC    A(1)                BLANK OUTPUT LINE.
         DC    A(*+4)              (TPUT PARMS AND A BLANK)
         DC    C' '
         SPACE 1
KEY      DS    X                   KEY OF BRODCAST RECORD.
BDATA    DS    CL129               I/O BUFFER.
BLOCK#   DC    X'000000'           RELATIVE BLOCK #.
         SPACE 1
MORE     DC    A(L'MORE1)          LENGTH OF MESSAGE.
         DC    A(MORE1)            PTR TO MESSAGE.
MORE1    DC    C'   More? (Hit ENTER or type "no") '
ANSWER   DS    CL20                ANSWER AREA FOR "MORE?"
         SPACE 1
DFPARM   DC    A(S99RB)            DAIRFAIL PARMS.
DFRCP    DC    A(D99RET)           DYNALLOC RETURN CODE.
         DC    A(DFKRUD)           ADDRESS OF A FULLWORD OF ZEROS.
         DC    A(DFIDP)            ADDRESS OF OPTIONS
DFCPPLP  DC    A(0)                POINTER TO CPPL.
DFKRUD   DC    A(0)                UNUSED.
D99RET   DC    A(0)                RETURN CODE.
DFIDP    DC    X'0032'             SAY PROBLEM IN SVC99.
         SPACE 1
S99RBPTR DS    0A                  DYNALLOC ...
         DC    X'80',AL3(S99RB)    ... RB POINTER.
S99RB    DC    AL1(20,1,0,0)       LENGTH, VERB CODE, FLAGS.
         DC    AL2(0,0)            ERROR, INFO CODES.
S99TU    DC    A(TUPS)             TU POINTERS POINTER.
         DC    A(0)                RESERVED.
         DC    A(0)                UNUSED FLAGS.
         SPACE 1
TUPS     DS    0A                  TU POINTERS.
         DC    A(TU0001)           POINTER TO DDNAME TU.
         DC    A(TU0002)           POINTER TO DSNAME TU.
         DC    X'80',AL3(TU0004)   POINTER TO STATUS TU.
         SPACE 1
TU0001   DC    AL2(1,1,6),C'SYSLBC'  DDNAME.
TU0002   DC    AL2(2,1,L'DSN)
DSN      DC    C'SYS1.BRODCAST'      DSNAME.
TU0004   DC    AL2(4,1,1),X'08'      DISP=SHR.
         SPACE 1
         PRINT NOGEN
SYSLBC   DCB   DDNAME=SYSLBC,DSORG=DA,RECFM=F,BLKSIZE=129,KEYLEN=1,    +
               MACRF=(RIC),OPTCD=R,SYNAD=SYNAD
         PRINT GEN
TRTAB    DC    256X'01'            TRANSLATE-AND-TEST TABLE.
         ORG   TRTAB+C' '
         DC    X'00'               SKIP BLANKS.
         ORG   TRTAB+C'n'
         DC    X'02'               LOOKING FOR SMALL "n" ...
         ORG   TRTAB+C'N'
         DC    X'02'               ... OR CAPITAL "N".
         ORG
         SPACE 1
PSCB     DS    A                   PSCB ADDRESS.
         SPACE 1
PPL      DS    0F                  PARSE PARM LIST.
PPLUPT   DS    A                   POINTER TO UPT.
PPLECT   DS    A                   POINTER TO ECT.
PPLECB   DC    A(ECB)              POINTER TO ECB.
PPLPCL   DC    A(PCL)              POINTER TO PCL.
PPLANS   DC    A(ANSWERPL)         POINTER TO ANSWER PLACE.
PPLCBUF  DS    A                   POINTER TO COMMAND BUFFER.
PPLUWA   DS    A                   USER WORK AREA.
         SPACE 1
ECB      DC    A(0)                ECB FOR PARSE.
ANSWERPL DS    A                   ANSWER PLACE.
         SPACE 1
PCL      IKJPARM
LENGTHK  IKJKEYWD
         IKJNAME SHORT
         IKJNAME LONG
PAUSEK   IKJKEYWD
         IKJNAME NOPAUSE
         IKJNAME PAUSE
DISPK    IKJKEYWD
         IKJNAME TOMORROW,ALIAS='PLUS',SUBFLD=DAYSS
         IKJNAME YESTERDAY,ALIAS='MINUS',SUBFLD=DAYSS
         IKJNAME MEMBER,SUBFLD=MEMBERS
         IKJNAME DATE,SUBFLD=DATES
CLEARK   IKJKEYWD
         IKJNAME NOCLEAR
         IKJNAME CLEAR
DAYSS    IKJSUBF
DAYS     IKJIDENT 'NUMBER OF DAYS',INTEG,LIST
MEMBERS  IKJSUBF
MEMBER   IKJIDENT 'MEMBER NAME',OTHER=ALPHANUM,PROMPT='MEMBER NAME',   +
               LIST
DATES    IKJSUBF
DATE     IKJIDENT 'DATE+',FIRST=ANY,OTHER=ANY,PROMPT='DATE+',          +
               HELP=('DATE YOU WISH TO SEE QUOTATION FOR+',            +
               'DATE OF THE FORM MM/DD/YY.'),MAXLNTH=8,VALIDCK=DV,LIST
         IKJENDP
         IKJCPPL
         END
